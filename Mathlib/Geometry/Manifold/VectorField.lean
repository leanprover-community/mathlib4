/-
Copyright (c) 2024 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Calculus.VectorField
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import Mathlib.Geometry.Manifold.MFDeriv.NormedSpace
import Mathlib.Geometry.Manifold.VectorBundle.MDifferentiable

/-!
# Vector fields in manifolds

We study functions of the form `V : Œ† (x : M) ‚Üí TangentSpace I x` on a manifold, i.e.,
vector fields.

We define the pullback of a vector field under a map, as
`VectorField.mpullback I I' f V x := (mfderiv I I' f x).inverse (V (f x))`
(together with the same notion within a set).

We also define the Lie bracket of two vector fields, denoted with
`VectorField.mlieBracket I V W x`, as the pullback in the manifold of the corresponding notion
in the model space (through `extChartAt I x`).

In addition to comprehensive API on these two notions, the main results are the following:
* `VectorField.mpullback_mlieBracket` states that the pullback of the Lie bracket
  is the Lie bracket of the pullbacks.
* `VectorField.leibniz_identity_mlieBracket` is the Leibniz
  identity `[U, [V, W]] = [[U, V], W] + [V, [U, W]]`.

All these are given in the `VectorField` namespace because pullbacks, Lie brackets, and so on,
are notions that make sense in a variety of contexts. We also prefix the notions with `m` to
distinguish the manifold notions from the vector spaces notions.

For notions that come naturally in other namespaces for dot notation, we specify `vectorField` in
the name to lift ambiguities. For instance, the fact that the Lie bracket of two smooth vector
fields is smooth is `ContMDiffAt.mlieBracket_vectorField`.

Note that a smoothness assumption for a vector field is written by seeing the vector field as
a function from `M` to its tangent bundle through a coercion, as in:
`MDifferentiableWithinAt I I.tangent (fun y ‚Ü¶ (V y : TangentBundle I M)) s x`.
-/

open Set Function Filter
open scoped Topology Manifold ContDiff

noncomputable section

/- We work in the `VectorField` namespace because pullbacks, Lie brackets, and so on, are notions
that make sense in a variety of contexts. We also prefix the notions with `m` to distinguish the
manifold notions from the vector spaces notions. For instance, the Lie bracket of two vector
fields in a manifold is denoted with `VectorField.mlieBracket I V W x`, where `I` is the relevant
model with corners, `V W : Œ† (x : M), TangentSpace I x` are the vector fields, and `x : M` is
the basepoint.
-/

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {H : Type*} [TopologicalSpace H] {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {I : ModelWithCorners ùïú E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {H' : Type*} [TopologicalSpace H'] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E']
  {I' : ModelWithCorners ùïú E' H'}
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {H'' : Type*} [TopologicalSpace H''] {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ùïú E'']
  {I'' : ModelWithCorners ùïú E'' H''}
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  {f : M ‚Üí M'} {s t : Set M} {x x‚ÇÄ : M}

namespace VectorField

section

/-! ### Pullback of vector fields in manifolds -/

open ContinuousLinearMap

variable {V W V‚ÇÅ W‚ÇÅ : Œ† (x : M'), TangentSpace I' x}
variable {c : ùïú} {m n : WithTop ‚Ñï‚àû} {t : Set M'} {y‚ÇÄ : M'}

variable (I I') in
/-- The pullback of a vector field under a map between manifolds, within a set `s`. -/
def mpullbackWithin (f : M ‚Üí M') (V : Œ† (x : M'), TangentSpace I' x) (s : Set M) (x : M) :
    TangentSpace I x :=
  (mfderivWithin I I' f s x).inverse (V (f x))

variable (I I') in
/-- The pullback of a vector field under a map between manifolds. -/
def mpullback (f : M ‚Üí M') (V : Œ† (x : M'), TangentSpace I' x) (x : M) :
    TangentSpace I x :=
  (mfderiv I I' f x).inverse (V (f x))

lemma mpullbackWithin_apply :
    mpullbackWithin I I' f V s x = (mfderivWithin I I' f s x).inverse (V (f x)) := rfl

lemma mpullbackWithin_smul_apply :
    mpullbackWithin I I' f (c ‚Ä¢ V) s x = c ‚Ä¢ mpullbackWithin I I' f V s x := by
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_smul :
    mpullbackWithin I I' f (c ‚Ä¢ V) s = c ‚Ä¢ mpullbackWithin I I' f V s := by
  ext x
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_add_apply :
    mpullbackWithin I I' f (V + V‚ÇÅ) s x =
      mpullbackWithin I I' f V s x + mpullbackWithin I I' f V‚ÇÅ s x := by
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_add :
    mpullbackWithin I I' f (V + V‚ÇÅ) s =
      mpullbackWithin I I' f V s + mpullbackWithin I I' f V‚ÇÅ s := by
  ext x
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_neg_apply :
    mpullbackWithin I I' f (-V) s x = - mpullbackWithin I I' f V s x := by
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_neg :
    mpullbackWithin I I' f (-V) s = - mpullbackWithin I I' f V s := by
  ext x
  simp [mpullbackWithin_apply]

lemma mpullback_apply :
    mpullback I I' f V x = (mfderiv I I' f x).inverse (V (f x)) := rfl

lemma mpullback_add_apply :
    mpullback I I' f (V + V‚ÇÅ) x = mpullback I I' f V x + mpullback I I' f V‚ÇÅ x := by
  simp [mpullback_apply]

lemma mpullback_add :
    mpullback I I' f (V + V‚ÇÅ) = mpullback I I' f V + mpullback I I' f V‚ÇÅ := by
  ext x
  simp [mpullback_apply]

lemma mpullback_neg_apply :
    mpullback I I' f (-V) x = - mpullback I I' f V x := by
  simp [mpullback_apply]

lemma mpullback_neg :
    mpullback I I' f (-V) = - mpullback I I' f V := by
  ext x
  simp [mpullback_apply]

@[simp] lemma mpullbackWithin_univ : mpullbackWithin I I' f V univ = mpullback I I' f V := by
  ext x
  simp [mpullback_apply, mpullbackWithin_apply]

lemma mpullbackWithin_eq_pullbackWithin {f : E ‚Üí E'} {V : E' ‚Üí E'} {s : Set E} :
    mpullbackWithin ùìò(ùïú, E) ùìò(ùïú, E') f V s = pullbackWithin ùïú f V s := by
  ext x
  simp only [mpullbackWithin, mfderivWithin_eq_fderivWithin, pullbackWithin]
  rfl

@[simp] lemma mpullback_id {V : Œ† (x : M), TangentSpace I x} : mpullback I I id V = V := by
  ext x
  simp [mpullback]

/-! ### Smoothness of pullback of vector fields -/
section

variable [IsManifold I 2 M] [IsManifold I' 2 M'] [CompleteSpace E]

/-- The pullback of a differentiable vector field by a `C^n` function with `2 ‚â§ n` is
differentiable. Version within a set at a point. -/
protected lemma _root_.MDifferentiableWithinAt.mpullbackWithin_vectorField_inter
    (hV : MDifferentiableWithinAt I' I'.tangent
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : 2 ‚â§ n) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s ‚à© f ‚Åª¬π' t) x‚ÇÄ := by
  /- We want to apply the general theorem `MDifferentiableWithinAt.clm_apply_of_inCoordinates`,
  stating that applying linear maps to vector fields gives a smooth result when the linear map and
  the vector field are smooth. This theorem is general, we will apply it to
  `b‚ÇÅ = f`, `b‚ÇÇ = id`, `v = V ‚àò f`, `œï = fun x ‚Ü¶ (mfderivWithin I I' f s x).inverse`-/
  let b‚ÇÅ := f
  let b‚ÇÇ : M ‚Üí M := id
  let v : Œ† (x : M), TangentSpace I' (f x) := V ‚àò f
  let œï : Œ† (x : M), TangentSpace I' (f x) ‚ÜíL[ùïú] TangentSpace I x :=
    fun x ‚Ü¶ (mfderivWithin I I' f s x).inverse
  have hv : MDifferentiableWithinAt I I'.tangent
      (fun x ‚Ü¶ (v x : TangentBundle I' M')) (s ‚à© f ‚Åª¬π' t) x‚ÇÄ := by
    apply hV.comp x‚ÇÄ ((hf.mdifferentiableWithinAt (one_le_two.trans hmn)).mono inter_subset_left)
    exact MapsTo.mono_left (mapsTo_preimage _ _) inter_subset_right
  /- The only nontrivial fact, from which the conclusion follows, is
  that `œï` depends smoothly on `x`. -/
  suffices hœï : MDifferentiableWithinAt I ùìò(ùïú, E' ‚ÜíL[ùïú] E)
      (fun (x : M) ‚Ü¶ ContinuousLinearMap.inCoordinates
        E' (TangentSpace I' (M := M')) E (TangentSpace I (M := M))
        (b‚ÇÅ x‚ÇÄ) (b‚ÇÅ x) (b‚ÇÇ x‚ÇÄ) (b‚ÇÇ x) (œï x)) s x‚ÇÄ from
    MDifferentiableWithinAt.clm_apply_of_inCoordinates (hœï.mono inter_subset_left)
      hv mdifferentiableWithinAt_id
  /- To prove that `œï` depends smoothly on `x`, we use that the derivative depends smoothly on `x`
  (this is `ContMDiffWithinAt.mfderivWithin_const`), and that taking the inverse is a smooth
  operation at an invertible map. -/
  -- the derivative in coordinates depends smoothly on the point
  have : MDifferentiableWithinAt I ùìò(ùïú, E ‚ÜíL[ùïú] E')
      (fun (x : M) ‚Ü¶ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        x‚ÇÄ x (f x‚ÇÄ) (f x) (mfderivWithin I I' f s x)) s x‚ÇÄ :=
    ((hf.of_le hmn).mfderivWithin_const le_rfl hx‚ÇÄ hs).mdifferentiableWithinAt le_rfl
  -- therefore, its inverse in coordinates also depends smoothly on the point
  have : MDifferentiableWithinAt I ùìò(ùïú, E' ‚ÜíL[ùïú] E)
      (ContinuousLinearMap.inverse ‚àò (fun (x : M) ‚Ü¶ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        x‚ÇÄ x (f x‚ÇÄ) (f x) (mfderivWithin I I' f s x))) s x‚ÇÄ := by
    apply MDifferentiableAt.comp_mdifferentiableWithinAt _ _ this
    apply ContMDiffAt.mdifferentiableAt _ le_rfl
    apply ContDiffAt.contMDiffAt
    apply IsInvertible.contDiffAt_map_inverse
    rw [inCoordinates_eq (FiberBundle.mem_baseSet_trivializationAt' x‚ÇÄ)
      (FiberBundle.mem_baseSet_trivializationAt' (f x‚ÇÄ))]
    exact isInvertible_equiv.comp (hf'.comp isInvertible_equiv)
  -- the inverse in coordinates coincides with the in-coordinate version of the inverse,
  -- therefore the previous point gives the conclusion
  apply this.congr_of_eventuallyEq_of_mem _ hx‚ÇÄ
  have A : (trivializationAt E (TangentSpace I) x‚ÇÄ).baseSet ‚àà ùìù[s] x‚ÇÄ := by
    apply nhdsWithin_le_nhds
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  have B : f ‚Åª¬π' (trivializationAt E' (TangentSpace I') (f x‚ÇÄ)).baseSet ‚àà ùìù[s] x‚ÇÄ := by
    apply hf.continuousWithinAt.preimage_mem_nhdsWithin
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  filter_upwards [A, B] with x hx h'x
  simp only [Function.comp_apply]
  rw [inCoordinates_eq hx h'x, inCoordinates_eq h'x (by exact hx)]
  simp only [inverse_equiv_comp, inverse_comp_equiv, ContinuousLinearEquiv.symm_symm, œï]
  rfl

lemma _root_.MDifferentiableWithinAt.mpullbackWithin_vectorField_inter_of_eq
    (hV : MDifferentiableWithinAt I' I'.tangent
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : 2 ‚â§ n) (h : y‚ÇÄ = f x‚ÇÄ) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s ‚à© f‚Åª¬π' t) x‚ÇÄ := by
  subst h
  exact hV.mpullbackWithin_vectorField_inter hf hf' hx‚ÇÄ hs hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version on a set. -/
protected lemma _root_.MDifferentiableOn.mpullbackWithin_vectorField_inter
    (hV : MDifferentiableOn I' I'.tangent (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t)
    (hf : ContMDiffOn I I' n f s) (hf' : ‚àÄ x ‚àà s ‚à© f ‚Åª¬π' t, (mfderivWithin I I' f s x).IsInvertible)
    (hs : UniqueMDiffOn I s) (hmn : 2 ‚â§ n) :
    MDifferentiableOn I I.tangent
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s ‚à© f ‚Åª¬π' t) :=
  fun _ hx‚ÇÄ ‚Ü¶ MDifferentiableWithinAt.mpullbackWithin_vectorField_inter
    (hV _ hx‚ÇÄ.2) (hf _ hx‚ÇÄ.1) (hf' _ hx‚ÇÄ) hx‚ÇÄ.1 hs hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 ‚â§ n` is
differentiable. Version within a set at a point, but with full pullback. -/
protected lemma _root_.MDifferentiableWithinAt.mpullback_vectorField_preimage
    (hV : MDifferentiableWithinAt I' I'.tangent
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : 2 ‚â§ n) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) (f ‚Åª¬π' t) x‚ÇÄ := by
  simp only [‚Üê contMDiffWithinAt_univ, ‚Üê mfderivWithin_univ, ‚Üê mpullbackWithin_univ] at hV hf hf' ‚ä¢
  simpa using hV.mpullbackWithin_vectorField_inter hf hf' (mem_univ _) uniqueMDiffOn_univ hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 ‚â§ n` is
differentiable. Version within a set at a point, but with full pullback. -/
protected lemma _root_.MDifferentiableWithinAt.mpullback_vectorField_preimage_of_eq
    (hV : MDifferentiableWithinAt I' I'.tangent (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : 2 ‚â§ n)
    (hy‚ÇÄ : y‚ÇÄ = f x‚ÇÄ) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) (f ‚Åª¬π' t) x‚ÇÄ := by
  subst hy‚ÇÄ
  exact hV.mpullback_vectorField_preimage hf hf' hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 ‚â§ n` is
differentiable. Version on a set, but with full pullback -/
protected lemma _root_.MDifferentiableOn.mpullback_vectorField_preimage
    (hV : MDifferentiableOn I' I'.tangent (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t)
    (hf : ContMDiff I I' n f) (hf' : ‚àÄ x ‚àà f ‚Åª¬π' t, (mfderiv I I' f x).IsInvertible)
    (hmn : 2 ‚â§ n) :
    MDifferentiableOn I I.tangent
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) (f ‚Åª¬π' t) :=
  fun x‚ÇÄ hx‚ÇÄ ‚Ü¶ MDifferentiableWithinAt.mpullback_vectorField_preimage
    (hV _ hx‚ÇÄ) (hf x‚ÇÄ) (hf' _ hx‚ÇÄ) hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 ‚â§ n` is
differentiable. Version at a point. -/
protected lemma _root_.MDifferentiableAt.mpullback_vectorField
    (hV : MDifferentiableAt I' I'.tangent (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) (f x‚ÇÄ))
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : 2 ‚â§ n) :
    MDifferentiableAt I I.tangent
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) x‚ÇÄ := by
  simpa using MDifferentiableWithinAt.mpullback_vectorField_preimage hV hf hf' hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 ‚â§ n` is
differentiable. -/
protected lemma _root_.MDifferentiable.mpullback_vectorField
    (hV : MDifferentiable I' I'.tangent (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')))
    (hf : ContMDiff I I' n f) (hf' : ‚àÄ x, (mfderiv I I' f x).IsInvertible) (hmn : 2 ‚â§ n) :
    MDifferentiable I I.tangent (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) :=
  fun x ‚Ü¶ MDifferentiableAt.mpullback_vectorField (hV (f x)) (hf x) (hf' x) hmn

end

section

variable [IsManifold I n M] [IsManifold I' n M'] [CompleteSpace E]
-- the next assumptions will usually follow from the previous ones, but they are needed for
-- the statements to make sense, so we add them here.
[IsManifold I 1 M] [IsManifold I' 1 M']

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_inter
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s ‚à© f ‚Åª¬π' t) x‚ÇÄ := by
  /- We want to apply the general theorem `ContMDiffWithinAt.clm_apply_of_inCoordinates`, stating
  that applying linear maps to vector fields gives a smooth result when the linear map and the
  vector field are smooth. This theorem is general, we will apply it to
  `b‚ÇÅ = f`, `b‚ÇÇ = id`, `v = V ‚àò f`, `œï = fun x ‚Ü¶ (mfderivWithin I I' f s x).inverse`-/
  let b‚ÇÅ := f
  let b‚ÇÇ : M ‚Üí M := id
  let v : Œ† (x : M), TangentSpace I' (f x) := V ‚àò f
  let œï : Œ† (x : M), TangentSpace I' (f x) ‚ÜíL[ùïú] TangentSpace I x :=
    fun x ‚Ü¶ (mfderivWithin I I' f s x).inverse
  have hv : ContMDiffWithinAt I I'.tangent m
      (fun x ‚Ü¶ (v x : TangentBundle I' M')) (s ‚à© f ‚Åª¬π' t) x‚ÇÄ := by
    apply hV.comp x‚ÇÄ ((hf.of_le (le_trans (le_self_add) hmn)).mono inter_subset_left)
    exact MapsTo.mono_left (mapsTo_preimage _ _) inter_subset_right
  /- The only nontrivial fact, from which the conclusion follows, is
  that `œï` depends smoothly on `x`. -/
  suffices hœï : ContMDiffWithinAt I ùìò(ùïú, E' ‚ÜíL[ùïú] E) m
      (fun (x : M) ‚Ü¶ ContinuousLinearMap.inCoordinates
        E' (TangentSpace I' (M := M')) E (TangentSpace I (M := M))
        (b‚ÇÅ x‚ÇÄ) (b‚ÇÅ x) (b‚ÇÇ x‚ÇÄ) (b‚ÇÇ x) (œï x)) s x‚ÇÄ from
    ContMDiffWithinAt.clm_apply_of_inCoordinates (hœï.mono inter_subset_left) hv contMDiffWithinAt_id
  /- To prove that `œï` depends smoothly on `x`, we use that the derivative depends smoothly on `x`
  (this is `ContMDiffWithinAt.mfderivWithin_const`), and that taking the inverse is a smooth
  operation at an invertible map. -/
  -- the derivative in coordinates depends smoothly on the point
  have : ContMDiffWithinAt I ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (fun (x : M) ‚Ü¶ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        x‚ÇÄ x (f x‚ÇÄ) (f x) (mfderivWithin I I' f s x)) s x‚ÇÄ :=
    hf.mfderivWithin_const hmn hx‚ÇÄ hs
  -- therefore, its inverse in coordinates also depends smoothly on the point
  have : ContMDiffWithinAt I ùìò(ùïú, E' ‚ÜíL[ùïú] E) m
      (ContinuousLinearMap.inverse ‚àò (fun (x : M) ‚Ü¶ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        x‚ÇÄ x (f x‚ÇÄ) (f x) (mfderivWithin I I' f s x))) s x‚ÇÄ := by
    apply ContMDiffAt.comp_contMDiffWithinAt _ _ this
    apply ContDiffAt.contMDiffAt
    apply IsInvertible.contDiffAt_map_inverse
    rw [inCoordinates_eq (FiberBundle.mem_baseSet_trivializationAt' x‚ÇÄ)
      (FiberBundle.mem_baseSet_trivializationAt' (f x‚ÇÄ))]
    exact isInvertible_equiv.comp (hf'.comp isInvertible_equiv)
  -- the inverse in coordinates coincides with the in-coordinate version of the inverse,
  -- therefore the previous point gives the conclusion
  apply this.congr_of_eventuallyEq_of_mem _ hx‚ÇÄ
  have A : (trivializationAt E (TangentSpace I) x‚ÇÄ).baseSet ‚àà ùìù[s] x‚ÇÄ := by
    apply nhdsWithin_le_nhds
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  have B : f ‚Åª¬π' (trivializationAt E' (TangentSpace I') (f x‚ÇÄ)).baseSet ‚àà ùìù[s] x‚ÇÄ := by
    apply hf.continuousWithinAt.preimage_mem_nhdsWithin
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  filter_upwards [A, B] with x hx h'x
  simp only [Function.comp_apply]
  rw [inCoordinates_eq hx h'x, inCoordinates_eq h'x (by exact hx)]
  simp only [inverse_equiv_comp, inverse_comp_equiv, ContinuousLinearEquiv.symm_symm, œï]
  rfl

lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_inter_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) (h : f x‚ÇÄ = y‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s ‚à© f‚Åª¬π' t) x‚ÇÄ := by
  subst h
  exact ContMDiffWithinAt.mpullbackWithin_vectorField_inter hV hf hf' hx‚ÇÄ hs hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) s x‚ÇÄ := by
  apply (ContMDiffWithinAt.mpullbackWithin_vectorField_inter
    hV hf hf' hx‚ÇÄ hs hmn).mono_of_mem_nhdsWithin
  exact Filter.inter_mem self_mem_nhdsWithin hst

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ)
    (hy‚ÇÄ : f x‚ÇÄ = y‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) s x‚ÇÄ := by
  subst hy‚ÇÄ
  exact ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem hV hf hf' hx‚ÇÄ hs hmn hst

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) (hst : MapsTo f s t) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) s x‚ÇÄ :=
  ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem hV hf hf' hx‚ÇÄ hs hmn
    hst.preimage_mem_nhdsWithin

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) (hst : MapsTo f s t) (h : f x‚ÇÄ = y‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) s x‚ÇÄ := by
  subst h
  exact ContMDiffWithinAt.mpullbackWithin_vectorField hV hf hf' hx‚ÇÄ hs hmn hst

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point, with a set used for the pullback possibly larger. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField' {u : Set M}
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffWithinAt I I' n f u x‚ÇÄ) (hf' : (mfderivWithin I I' f u x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n)
    (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) (hu : s ‚äÜ u) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V u y : TangentBundle I M)) s x‚ÇÄ := by
  have hn : (1 : ‚Ñï) ‚â§ n := le_trans le_add_self hmn
  have hh : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible := by
    convert hf' using 1
    exact (hf.mdifferentiableWithinAt hn).mfderivWithin_mono (hs _ hx‚ÇÄ) hu
  apply (hV.mpullbackWithin_vectorField_of_mem (hf.mono hu) hh hx‚ÇÄ hs hmn
    hst).congr_of_eventuallyEq_of_mem _ hx‚ÇÄ
  have Y := (contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin (by simp)).1 (hf.of_le hn)
  simp_rw [insert_eq_of_mem (hu hx‚ÇÄ)] at Y
  filter_upwards [self_mem_nhdsWithin, nhdsWithin_mono _ hu Y] with y hy h'y
  simp only [mpullbackWithin, Bundle.TotalSpace.mk_inj]
  rw [MDifferentiableWithinAt.mfderivWithin_mono (h'y.mdifferentiableWithinAt le_rfl) (hs _ hy) hu]

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point, with a set used for the pullback possibly larger. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_eq' {u : Set M}
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffWithinAt I I' n f u x‚ÇÄ) (hf' : (mfderivWithin I I' f u x‚ÇÄ).IsInvertible)
    (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ)
    (hu : s ‚äÜ u) (hy‚ÇÄ : f x‚ÇÄ = y‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V u y : TangentBundle I M)) s x‚ÇÄ := by
  subst hy‚ÇÄ
  exact ContMDiffWithinAt.mpullbackWithin_vectorField' hV hf hf' hx‚ÇÄ hs hmn hst hu

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version on a set. -/
protected lemma _root_.ContMDiffOn.mpullbackWithin_vectorField_inter
    (hV : ContMDiffOn I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t)
    (hf : ContMDiffOn I I' n f s) (hf' : ‚àÄ x ‚àà s ‚à© f ‚Åª¬π' t, (mfderivWithin I I' f s x).IsInvertible)
    (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) :
    ContMDiffOn I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s ‚à© f ‚Åª¬π' t) :=
  fun _ hx‚ÇÄ ‚Ü¶ ContMDiffWithinAt.mpullbackWithin_vectorField_inter
    (hV _ hx‚ÇÄ.2) (hf _ hx‚ÇÄ.1) (hf' _ hx‚ÇÄ) hx‚ÇÄ.1 hs hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_preimage
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : m + 1 ‚â§ n) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) (f ‚Åª¬π' t) x‚ÇÄ := by
  simp only [‚Üê contMDiffWithinAt_univ, ‚Üê mfderivWithin_univ, ‚Üê mpullbackWithin_univ] at hV hf hf' ‚ä¢
  simpa using hV.mpullbackWithin_vectorField_inter hf hf' (mem_univ _) uniqueMDiffOn_univ hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_preimage_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : m + 1 ‚â§ n)
    (hy‚ÇÄ : y‚ÇÄ = f x‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) (f ‚Åª¬π' t) x‚ÇÄ := by
  subst hy‚ÇÄ
  exact ContMDiffWithinAt.mpullback_vectorField_preimage hV hf hf' hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t (f x‚ÇÄ))
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : m + 1 ‚â§ n)
    (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) s x‚ÇÄ :=
  (ContMDiffWithinAt.mpullback_vectorField_preimage hV hf hf' hmn).mono_of_mem_nhdsWithin hst

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t y‚ÇÄ)
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : m + 1 ‚â§ n)
    (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) (hy‚ÇÄ : y‚ÇÄ = f x‚ÇÄ) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) s x‚ÇÄ := by
  subst hy‚ÇÄ
  exact ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin hV hf hf' hmn hst

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version on a set, but with full pullback -/
protected lemma _root_.ContMDiffOn.mpullback_vectorField_preimage
    (hV : ContMDiffOn I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) t)
    (hf : ContMDiff I I' n f) (hf' : ‚àÄ x ‚àà f ‚Åª¬π' t, (mfderiv I I' f x).IsInvertible)
    (hmn : m + 1 ‚â§ n) :
    ContMDiffOn I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) (f ‚Åª¬π' t) :=
  fun x‚ÇÄ hx‚ÇÄ ‚Ü¶ ContMDiffWithinAt.mpullback_vectorField_preimage (hV _ hx‚ÇÄ) (hf x‚ÇÄ) (hf' _ hx‚ÇÄ) hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`.
Version at a point. -/
protected lemma _root_.ContMDiffAt.mpullback_vectorField_preimage
    (hV : ContMDiffAt I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')) (f x‚ÇÄ))
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hf' : (mfderiv I I' f x‚ÇÄ).IsInvertible) (hmn : m + 1 ‚â§ n) :
    ContMDiffAt I I.tangent m
      (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) x‚ÇÄ := by
  simp only [‚Üê contMDiffWithinAt_univ] at hV hf hf' ‚ä¢
  simpa using ContMDiffWithinAt.mpullback_vectorField_preimage hV hf hf' hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with `m + 1 ‚â§ n` is `C^m`. -/
protected lemma _root_.ContMDiff.mpullback_vectorField
    (hV : ContMDiff I' I'.tangent m (fun (y : M') ‚Ü¶ (V y : TangentBundle I' M')))
    (hf : ContMDiff I I' n f) (hf' : ‚àÄ x, (mfderiv I I' f x).IsInvertible) (hmn : m + 1 ‚â§ n) :
    ContMDiff I I.tangent m (fun (y : M) ‚Ü¶ (mpullback I I' f V y : TangentBundle I M)) :=
  fun x ‚Ü¶ ContMDiffAt.mpullback_vectorField_preimage (hV (f x)) (hf x) (hf' x) hmn

lemma contMDiffWithinAt_mpullbackWithin_extChartAt_symm
    {V : Œ† (x : M), TangentSpace I x}
    (hV : ContMDiffWithinAt I I.tangent m (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffOn I s) (hx : x ‚àà s) (hmn : m + 1 ‚â§ n) :
    ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent m
      (fun y ‚Ü¶ (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm V (range I) y :
        TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x x) :=
  ContMDiffWithinAt.mpullbackWithin_vectorField_of_eq' hV
    (contMDiffWithinAt_extChartAt_symm_range (n := n) _ (mem_extChartAt_target x))
    (isInvertible_mfderivWithin_extChartAt_symm (mem_extChartAt_target x))
    (by simp [hx]) (UniqueMDiffOn.uniqueMDiffOn_target_inter hs x) hmn
    ((mapsTo_preimage _ _).mono_left inter_subset_right).preimage_mem_nhdsWithin
    (Subset.trans inter_subset_left (extChartAt_target_subset_range x)) (extChartAt_to_inv x)

lemma eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm
    {V : Œ† (x : M), TangentSpace I x}
    (hV : ContMDiffWithinAt I I.tangent m (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffOn I s) (hx : x ‚àà s) (hmn : m + 1 ‚â§ n) (hm : m ‚â† ‚àû) :
    ‚àÄ·∂† y in ùìù[s] x, ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent m
    (fun z ‚Ü¶ (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm V (range I) z :
      TangentBundle ùìò(ùïú, E) E))
    ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x y) := by
  have T := nhdsWithin_mono _ (subset_insert _ _)
    ((contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin hm).1
      (contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx hmn))
  have A := (continuousAt_extChartAt (I := I) x).continuousWithinAt.preimage_mem_nhdsWithin'' T rfl
  apply (nhdsWithin_le_iff.2 _) A
  filter_upwards [self_mem_nhdsWithin, nhdsWithin_le_nhds (extChartAt_source_mem_nhds (I := I) x)]
    with y hy h'y
  simp only [mfld_simps] at hy h'y
  simp [hy, h'y]

omit [CompleteSpace E] in
lemma eventuallyEq_mpullback_mpullbackWithin_extChartAt (V : Œ† (x : M), TangentSpace I x) :
    V =·∂†[ùìù[s] x] mpullback I ùìò(ùïú, E) (extChartAt I x)
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm V (range I)) := by
  apply nhdsWithin_le_nhds
  filter_upwards [extChartAt_source_mem_nhds (I := I) x] with y hy
  have A : (extChartAt I x).symm (extChartAt I x y) = y := (extChartAt I x).left_inv hy
  rw [mpullback_apply, mpullbackWithin_apply,
    ‚Üê (isInvertible_mfderiv_extChartAt hy).inverse_comp_apply_of_right,
    mfderivWithin_extChartAt_symm_comp_mfderiv_extChartAt' hy, A]
  simp only [ContinuousLinearMap.inverse_id, ContinuousLinearMap.coe_id', id_eq]

end

lemma mpullbackWithin_comp_of_left
    {g : M' ‚Üí M''} {f : M ‚Üí M'} {V : Œ† (x : M''), TangentSpace I'' x}
    {s : Set M} {t : Set M'} {x‚ÇÄ : M} (hg : MDifferentiableWithinAt I' I'' g t (f x‚ÇÄ))
    (hf : MDifferentiableWithinAt I I' f s x‚ÇÄ) (h : Set.MapsTo f s t)
    (hu : UniqueMDiffWithinAt I s x‚ÇÄ) (hg' : (mfderivWithin I' I'' g t (f x‚ÇÄ)).IsInvertible) :
    mpullbackWithin I I'' (g ‚àò f) V s x‚ÇÄ =
      mpullbackWithin I I' f (mpullbackWithin I' I'' g V t) s x‚ÇÄ := by
  simp only [mpullbackWithin, comp_apply]
  rw [mfderivWithin_comp _ hg hf h hu, IsInvertible.inverse_comp_apply_of_left]
  ¬∑ rfl
  ¬∑ exact hg'

lemma mpullbackWithin_comp_of_right
    {g : M' ‚Üí M''} {f : M ‚Üí M'} {V : Œ† (x : M''), TangentSpace I'' x}
    {s : Set M} {t : Set M'} {x‚ÇÄ : M} (hg : MDifferentiableWithinAt I' I'' g t (f x‚ÇÄ))
    (hf : MDifferentiableWithinAt I I' f s x‚ÇÄ) (h : Set.MapsTo f s t)
    (hu : UniqueMDiffWithinAt I s x‚ÇÄ) (hf' : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible) :
    mpullbackWithin I I'' (g ‚àò f) V s x‚ÇÄ =
      mpullbackWithin I I' f (mpullbackWithin I' I'' g V t) s x‚ÇÄ := by
  simp only [mpullbackWithin, comp_apply]
  rw [mfderivWithin_comp _ hg hf h hu, IsInvertible.inverse_comp_apply_of_right hf']
  rfl

end

/-! ### The Lie bracket of vector fields in manifolds -/

variable {V W V‚ÇÅ W‚ÇÅ : Œ† (x : M), TangentSpace I x}

variable (I I') in
/-- The Lie bracket of two vector fields in a manifold, within a set. -/
def mlieBracketWithin (V W : Œ† (x : M), TangentSpace I x) (s : Set M) (x‚ÇÄ : M) :
    TangentSpace I x‚ÇÄ :=
  mpullback I ùìò(ùïú, E) (extChartAt I x‚ÇÄ)
    (lieBracketWithin ùïú
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm V (range I))
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm W (range I))
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I)) x‚ÇÄ

variable (I I') in
/-- The Lie bracket of two vector fields in a manifold. -/
def mlieBracket (V W : Œ† (x : M), TangentSpace I x) (x‚ÇÄ : M) : TangentSpace I x‚ÇÄ :=
  mlieBracketWithin I V W univ x‚ÇÄ

lemma mlieBracketWithin_def  :
    mlieBracketWithin I V W s = fun x‚ÇÄ ‚Ü¶
    mpullback I ùìò(ùïú, E) (extChartAt I x‚ÇÄ)
    (lieBracketWithin ùïú
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm V (range I))
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm W (range I))
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I)) x‚ÇÄ := rfl

lemma mlieBracketWithin_apply :
    mlieBracketWithin I V W s x‚ÇÄ =
    (mfderiv I ùìò(ùïú, E) (extChartAt I x‚ÇÄ) x‚ÇÄ).inverse
    ((lieBracketWithin ùïú
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm V (range I))
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm W (range I))
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I)) ((extChartAt I x‚ÇÄ x‚ÇÄ))) := rfl

lemma mlieBracketWithin_eq_lieBracketWithin {V W : Œ† (x : E), TangentSpace ùìò(ùïú, E) x} {s : Set E} :
    mlieBracketWithin ùìò(ùïú, E) V W s  = lieBracketWithin ùïú V W s := by
  ext x
  simp [mlieBracketWithin_apply]

/-********************************************************************************
Copy of the `lieBracket` API in manifolds
-/

@[simp] lemma mlieBracketWithin_univ :
    mlieBracketWithin I V W univ = mlieBracket I V W := rfl

lemma mlieBracketWithin_eq_zero_of_eq_zero (hV : V x = 0) (hW : W x = 0) :
    mlieBracketWithin I V W s x = 0 := by
  simp only [mlieBracketWithin, mpullback_apply, comp_apply]
  rw [lieBracketWithin_eq_zero_of_eq_zero]
  ¬∑ simp
  ¬∑ simp only [mpullbackWithin_apply]
    have : (extChartAt I x).symm ((extChartAt I x) x) = x := by simp
    rw [this, hV]
    simp
  ¬∑ simp only [mpullbackWithin_apply]
    have : (extChartAt I x).symm ((extChartAt I x) x) = x := by simp
    rw [this, hW]
    simp

lemma mlieBracketWithin_swap_apply :
    mlieBracketWithin I V W s x = - mlieBracketWithin I W V s x := by
  rw [mlieBracketWithin, lieBracketWithin_swap, mpullback_neg]
  rfl

lemma mlieBracketWithin_swap :
    mlieBracketWithin I V W s = - mlieBracketWithin I W V s := by
  ext x
  exact mlieBracketWithin_swap_apply

lemma mlieBracket_swap_apply : mlieBracket I V W x = - mlieBracket I W V x :=
  mlieBracketWithin_swap_apply

lemma mlieBracket_swap : mlieBracket I V W = - mlieBracket I W V :=
  mlieBracketWithin_swap

@[simp] lemma mlieBracketWithin_self : mlieBracketWithin I V V = 0 := by
  ext x; simp [mlieBracketWithin, mpullback]

@[simp] lemma mlieBracket_self : mlieBracket I V V = 0 := by
  ext x; simp_rw [mlieBracket, mlieBracketWithin_self, Pi.zero_apply]

/-- Variant of `mlieBracketWithin_congr_set` where one requires the sets to coincide only in
the complement of a point. -/
theorem mlieBracketWithin_congr_set' (y : M) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x := by
  simp only [mlieBracketWithin_apply]
  congr 1
  have : T1Space M := I.t1Space M
  suffices A : ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I : Set E)
    =·∂†[ùìù[{(extChartAt I x) x}·∂ú] (extChartAt I x x)]
      ((extChartAt I x).symm ‚Åª¬π' t ‚à© range I : Set E) by
    apply lieBracketWithin_congr_set' _ A
  obtain ‚ü®u, u_mem, hu‚ü© : ‚àÉ u ‚àà ùìù x, u ‚à© {x}·∂ú ‚äÜ {y | (y ‚àà s) = (y ‚àà t)} :=
    mem_nhdsWithin_iff_exists_mem_nhds_inter.1 (nhdsWithin_compl_singleton_le x y h)
  rw [‚Üê extChartAt_to_inv (I := I) x] at u_mem
  have B : (extChartAt I x).target ‚à™ (range I)·∂ú ‚àà ùìù (extChartAt I x x) := by
    rw [‚Üê nhdsWithin_univ, ‚Üê union_compl_self (range I), nhdsWithin_union]
    apply Filter.union_mem_sup (extChartAt_target_mem_nhdsWithin x) self_mem_nhdsWithin
  apply mem_nhdsWithin_iff_exists_mem_nhds_inter.2
    ‚ü®_, Filter.inter_mem ((continuousAt_extChartAt_symm x).preimage_mem_nhds u_mem) B, ?_‚ü©
  rintro z ‚ü®hz, h'z‚ü©
  simp only [eq_iff_iff, mem_setOf_eq]
  change z ‚àà (extChartAt I x).symm ‚Åª¬π' s ‚à© range I ‚Üî z ‚àà (extChartAt I x).symm ‚Åª¬π' t ‚à© range I
  by_cases hIz : z ‚àà range I
  ¬∑ simp [-extChartAt, hIz] at hz ‚ä¢
    rw [‚Üê eq_iff_iff]
    apply hu ‚ü®hz.1, ?_‚ü©
    simp only [mem_compl_iff, mem_singleton_iff, ne_comm, ne_eq] at h'z ‚ä¢
    rw [(extChartAt I x).eq_symm_apply (by simp) hz.2]
    exact Ne.symm h'z
  ¬∑ simp [hIz]

theorem mlieBracketWithin_congr_set (h : s =·∂†[ùìù x] t) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x :=
  mlieBracketWithin_congr_set' x <| h.filter_mono inf_le_left

theorem mlieBracketWithin_inter (ht : t ‚àà ùìù x) :
    mlieBracketWithin I V W (s ‚à© t) x = mlieBracketWithin I V W s x := by
  apply mlieBracketWithin_congr_set
  filter_upwards [ht] with y hy
  change (y ‚àà s ‚à© t) = (y ‚àà s)
  aesop

theorem mlieBracketWithin_of_mem_nhds (h : s ‚àà ùìù x) :
    mlieBracketWithin I V W s x = mlieBracket I V W x := by
  rw [‚Üê mlieBracketWithin_univ, ‚Üê univ_inter s, mlieBracketWithin_inter h]

theorem mlieBracketWithin_of_isOpen (hs : IsOpen s) (hx : x ‚àà s) :
    mlieBracketWithin I V W s x = mlieBracket I V W x :=
  mlieBracketWithin_of_mem_nhds (hs.mem_nhds hx)

/-- Variant of `mlieBracketWithin_eventually_congr_set` where one requires the sets to coincide only
in  the complement of a point. -/
theorem mlieBracketWithin_eventually_congr_set' (y : M) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
    mlieBracketWithin I V W s =·∂†[ùìù x] mlieBracketWithin I V W t :=
  (eventually_nhds_nhdsWithin.2 h).mono fun _ => mlieBracketWithin_congr_set' y

theorem mlieBracketWithin_eventually_congr_set (h : s =·∂†[ùìù x] t) :
    mlieBracketWithin I V W s =·∂†[ùìù x] mlieBracketWithin I V W t :=
  mlieBracketWithin_eventually_congr_set' x <| h.filter_mono inf_le_left

theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_eq
    (hV : V‚ÇÅ =·∂†[ùìù[s] x] V) (hxV : V‚ÇÅ x = V x) (hW : W‚ÇÅ =·∂†[ùìù[s] x] W) (hxW : W‚ÇÅ x = W x) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s x = mlieBracketWithin I V W s x := by
  simp only [mlieBracketWithin_apply]
  congr 1
  let I1 : NormedAddCommGroup (TangentSpace ùìò(ùïú, E) (extChartAt I x x)) :=
    inferInstanceAs (NormedAddCommGroup E)
  let _I2 : NormedSpace ùïú (TangentSpace ùìò(ùïú, E) (extChartAt I x x)) :=
    inferInstanceAs (NormedSpace ùïú E)
  apply Filter.EventuallyEq.lieBracketWithin_vectorField_eq
  ¬∑ apply nhdsWithin_mono _ inter_subset_left
    filter_upwards [(continuousAt_extChartAt_symm x).continuousWithinAt.preimage_mem_nhdsWithin''
      hV (by simp)] with y hy
    simp only [mpullbackWithin_apply]
    congr 1
  ¬∑ simp only [mpullbackWithin_apply]
    congr 1
    convert hxV <;> exact extChartAt_to_inv x
  ¬∑ apply nhdsWithin_mono _ inter_subset_left
    filter_upwards [(continuousAt_extChartAt_symm x).continuousWithinAt.preimage_mem_nhdsWithin''
      hW (by simp)] with y hy
    simp only [mpullbackWithin_apply]
    congr 1
  ¬∑ simp only [mpullbackWithin_apply]
    congr 1
    convert hxW <;> exact extChartAt_to_inv x

theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem
    (hV : V‚ÇÅ =·∂†[ùìù[s] x] V) (hW : W‚ÇÅ =·∂†[ùìù[s] x] W) (hx : x ‚àà s) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s x = mlieBracketWithin I V W s x :=
  hV.mlieBracketWithin_vectorField_eq (mem_of_mem_nhdsWithin hx hV :)
    hW (mem_of_mem_nhdsWithin hx hW :)

/-- If vector fields coincide on a neighborhood of a point within a set, then the Lie brackets
also coincide on a neighborhood of this point within this set. Version where one considers the Lie
bracket within a subset. -/
theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField'
    (hV : V‚ÇÅ =·∂†[ùìù[s] x] V) (hW : W‚ÇÅ =·∂†[ùìù[s] x] W) (ht : t ‚äÜ s) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ t =·∂†[ùìù[s] x] mlieBracketWithin I V W t := by
  filter_upwards [hV, hW, eventually_eventually_nhdsWithin.2 hV,
    eventually_eventually_nhdsWithin.2 hW] with y hVy hWy hVy' hWy'
  apply Filter.EventuallyEq.mlieBracketWithin_vectorField_eq
  ¬∑ apply nhdsWithin_mono _ ht
    exact hVy'
  ¬∑ exact hVy
  ¬∑ apply nhdsWithin_mono _ ht
    exact hWy'
  ¬∑ exact hWy

protected theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField
    (hV : V‚ÇÅ =·∂†[ùìù[s] x] V) (hW : W‚ÇÅ =·∂†[ùìù[s] x] W) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s =·∂†[ùìù[s] x] mlieBracketWithin I V W s :=
  hV.mlieBracketWithin_vectorField' hW Subset.rfl

protected theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_of_insert
    (hV : V‚ÇÅ =·∂†[ùìù[insert x s] x] V) (hW : W‚ÇÅ =·∂†[ùìù[insert x s] x] W) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s x = mlieBracketWithin I V W s x := by
  apply mem_of_mem_nhdsWithin (mem_insert x s)
    (hV.mlieBracketWithin_vectorField' hW (subset_insert x s))

theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_nhds
    (hV : V‚ÇÅ =·∂†[ùìù x] V) (hW : W‚ÇÅ =·∂†[ùìù x] W) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s x = mlieBracketWithin I V W s x :=
  (hV.filter_mono nhdsWithin_le_nhds).mlieBracketWithin_vectorField_eq hV.self_of_nhds
    (hW.filter_mono nhdsWithin_le_nhds) hW.self_of_nhds

theorem mlieBracketWithin_congr
    (hV : EqOn V‚ÇÅ V s) (hVx : V‚ÇÅ x = V x) (hW : EqOn W‚ÇÅ W s) (hWx : W‚ÇÅ x = W x) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s x = mlieBracketWithin I V W s x :=
  (hV.eventuallyEq.filter_mono inf_le_right).mlieBracketWithin_vectorField_eq hVx
    (hW.eventuallyEq.filter_mono inf_le_right) hWx

/-- Version of `mlieBracketWithin_congr` in which one assumes that the point belongs to the
given set. -/
theorem mlieBracketWithin_congr' (hV : EqOn V‚ÇÅ V s) (hW : EqOn W‚ÇÅ W s) (hx : x ‚àà s) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ s x = mlieBracketWithin I V W s x :=
  mlieBracketWithin_congr hV (hV hx) hW (hW hx)

theorem _root_.Filter.EventuallyEq.mlieBracket_vectorField_eq
    (hV : V‚ÇÅ =·∂†[ùìù x] V) (hW : W‚ÇÅ =·∂†[ùìù x] W) :
    mlieBracket I V‚ÇÅ W‚ÇÅ x = mlieBracket I V W x := by
  rw [‚Üê mlieBracketWithin_univ, ‚Üê mlieBracketWithin_univ,
    hV.mlieBracketWithin_vectorField_eq_nhds hW]

protected theorem _root_.Filter.EventuallyEq.mlieBracket_vectorField
    (hV : V‚ÇÅ =·∂†[ùìù x] V) (hW : W‚ÇÅ =·∂†[ùìù x] W) : mlieBracket I V‚ÇÅ W‚ÇÅ =·∂†[ùìù x] mlieBracket I V W := by
  filter_upwards [hV.eventuallyEq_nhds, hW.eventuallyEq_nhds] with y hVy hWy
  exact hVy.mlieBracket_vectorField_eq hWy

section

variable {c : ùïú}
variable [IsManifold I 2 M] [CompleteSpace E]

lemma _root_.MDifferentiableWithinAt.differentiableWithinAt_mpullbackWithin_vectorField
    (hV : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) s x) :
    DifferentiableWithinAt ùïú (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm V (range I))
    ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x) := by
  apply MDifferentiableWithinAt.differentiableWithinAt
  have := MDifferentiableWithinAt.mpullbackWithin_vectorField_inter_of_eq hV
    (contMDiffWithinAt_extChartAt_symm_range x (mem_extChartAt_target x))
    (isInvertible_mfderivWithin_extChartAt_symm (mem_extChartAt_target x)) (mem_range_self _)
    I.uniqueMDiffOn le_rfl (extChartAt_to_inv x).symm
  rw [inter_comm]
  exact ((contMDiff_snd_tangentBundle_modelSpace E ùìò(ùïú, E)).contMDiffAt.mdifferentiableAt
    le_rfl).comp_mdifferentiableWithinAt _ this

lemma mlieBracketWithin_smul_left
    (hV : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I (c ‚Ä¢ V) W s x = c ‚Ä¢ mlieBracketWithin I V W s x := by
  simp only [mlieBracketWithin_apply]
  rw [‚Üê ContinuousLinearMap.map_smul, mpullbackWithin_smul, lieBracketWithin_smul_left]
  ¬∑ exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ exact uniqueMDiffWithinAt_iff_inter_range.1 hs

lemma mlieBracket_smul_left
    (hV : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) x) :
    mlieBracket I (c ‚Ä¢ V) W  x = c ‚Ä¢ mlieBracket I V W x := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê contMDiffWithinAt_univ] at hV ‚ä¢
  exact mlieBracketWithin_smul_left hV (uniqueMDiffWithinAt_univ _)

lemma mlieBracketWithin_smul_right
    (hW : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I V (c ‚Ä¢ W) s x = c ‚Ä¢ mlieBracketWithin I V W s x := by
  simp only [mlieBracketWithin_apply]
  rw [‚Üê ContinuousLinearMap.map_smul, mpullbackWithin_smul, lieBracketWithin_smul_right]
  ¬∑ exact hW.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ exact uniqueMDiffWithinAt_iff_inter_range.1 hs

lemma mlieBracket_smul_right
    (hW : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) x) :
    mlieBracket I V (c ‚Ä¢ W) x = c ‚Ä¢ mlieBracket I V W x := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê contMDiffWithinAt_univ] at hW ‚ä¢
  exact mlieBracketWithin_smul_right hW (uniqueMDiffWithinAt_univ _)

lemma mlieBracketWithin_add_left
    (hV : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hV‚ÇÅ : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V‚ÇÅ x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I (V + V‚ÇÅ) W s x =
      mlieBracketWithin I V W s x + mlieBracketWithin I V‚ÇÅ W s x := by
  simp only [mlieBracketWithin_apply]
  rw [‚Üê ContinuousLinearMap.map_add, mpullbackWithin_add, lieBracketWithin_add_left]
  ¬∑ exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ exact hV‚ÇÅ.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ exact uniqueMDiffWithinAt_iff_inter_range.1 hs

lemma mlieBracket_add_left
    (hV : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) x)
    (hV‚ÇÅ : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (V‚ÇÅ x : TangentBundle I M)) x) :
    mlieBracket I (V + V‚ÇÅ) W  x =
      mlieBracket I V W x + mlieBracket I V‚ÇÅ W x := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê contMDiffWithinAt_univ] at hV hV‚ÇÅ ‚ä¢
  exact mlieBracketWithin_add_left hV hV‚ÇÅ (uniqueMDiffWithinAt_univ _)

lemma mlieBracketWithin_add_right
    (hW : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) s x)
    (hW‚ÇÅ : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (W‚ÇÅ x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I V (W + W‚ÇÅ) s x =
      mlieBracketWithin I V W s x + mlieBracketWithin I V W‚ÇÅ s x := by
  rw [mlieBracketWithin_swap, Pi.neg_apply, mlieBracketWithin_add_left hW hW‚ÇÅ hs,
    mlieBracketWithin_swap (V := V), mlieBracketWithin_swap (V := V), Pi.neg_apply, Pi.neg_apply]
  abel

lemma mlieBracket_add_right
    (hW : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) x)
    (hW‚ÇÅ : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (W‚ÇÅ x : TangentBundle I M)) x) :
    mlieBracket I V (W + W‚ÇÅ) x =
      mlieBracket I V W x + mlieBracket I V W‚ÇÅ x := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê contMDiffWithinAt_univ] at hW hW‚ÇÅ ‚ä¢
  exact mlieBracketWithin_add_right hW hW‚ÇÅ (uniqueMDiffWithinAt_univ _)

theorem mlieBracketWithin_of_mem_nhdsWithin
    (st : t ‚àà ùìù[s] x) (hs : UniqueMDiffWithinAt I s x)
    (hV : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) t x)
    (hW : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) t x) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x := by
  simp only [mlieBracketWithin_apply]
  congr 1
  rw [lieBracketWithin_of_mem_nhdsWithin]
  ¬∑ apply Filter.inter_mem
    ¬∑ apply nhdsWithin_mono _ inter_subset_left
      exact (continuousAt_extChartAt_symm x).continuousWithinAt.preimage_mem_nhdsWithin''
        st (by simp)
    ¬∑ exact nhdsWithin_mono _ inter_subset_right self_mem_nhdsWithin
  ¬∑ exact uniqueMDiffWithinAt_iff_inter_range.1 hs
  ¬∑ exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ exact hW.differentiableWithinAt_mpullbackWithin_vectorField

theorem mlieBracketWithin_subset (st : s ‚äÜ t) (ht : UniqueMDiffWithinAt I s x)
    (hV : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) t x)
    (hW : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) t x) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x :=
  mlieBracketWithin_of_mem_nhdsWithin (nhdsWithin_mono _ st self_mem_nhdsWithin) ht hV hW

theorem mlieBracketWithin_eq_mlieBracket (hs : UniqueMDiffWithinAt I s x)
    (hV : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) x)
    (hW : MDifferentiableAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) x) :
    mlieBracketWithin I V W s x = mlieBracket I V W x := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê mdifferentiableWithinAt_univ] at hV hW ‚ä¢
  exact mlieBracketWithin_subset (subset_univ _) hs hV hW

theorem _root_.DifferentiableWithinAt.mlieBracketWithin_congr_mono
    (hV : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hVs : EqOn V‚ÇÅ V t) (hVx : V‚ÇÅ x = V x)
    (hW : MDifferentiableWithinAt I I.tangent (fun x ‚Ü¶ (W x : TangentBundle I M)) s x)
    (hWs : EqOn W‚ÇÅ W t) (hWx : W‚ÇÅ x = W x)
    (hxt : UniqueMDiffWithinAt I t x) (h‚ÇÅ : t ‚äÜ s) :
    mlieBracketWithin I V‚ÇÅ W‚ÇÅ t x = mlieBracketWithin I V W s x := by
  rw [mlieBracketWithin_congr hVs hVx hWs hWx]
  exact mlieBracketWithin_subset h‚ÇÅ hxt hV hW

end

section

variable [IsManifold I 2 M] [IsManifold I' 2 M'] [CompleteSpace E]

/- The Lie bracket of vector fields on manifolds is well defined, i.e., it is invariant under
diffeomorphisms. Auxiliary version where one assumes that all relevant sets are contained
in chart domains. -/
private lemma mpullbackWithin_mlieBracketWithin_aux [CompleteSpace E']
    {f : M ‚Üí M'} {V W : Œ† (x : M'), TangentSpace I' x} {x‚ÇÄ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (V x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (W x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffOn I I' 2 f s) (hx‚ÇÄ : x‚ÇÄ ‚àà s)
    (ht : t ‚äÜ (extChartAt I' (f x‚ÇÄ)).source) (hst : MapsTo f s t)
    (hsymm : IsSymmSndFDerivWithinAt ùïú ((extChartAt I' (f x‚ÇÄ)) ‚àò f ‚àò (extChartAt I x‚ÇÄ).symm)
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x‚ÇÄ x‚ÇÄ)) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s x‚ÇÄ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s x‚ÇÄ := by
  have A : (extChartAt I x‚ÇÄ).symm (extChartAt I x‚ÇÄ x‚ÇÄ) = x‚ÇÄ := by simp
  have A' : x‚ÇÄ = (extChartAt I x‚ÇÄ).symm (extChartAt I x‚ÇÄ x‚ÇÄ) := by simp
  have h'f : MDifferentiableWithinAt I I' f s x‚ÇÄ := (hf x‚ÇÄ hx‚ÇÄ).mdifferentiableWithinAt one_le_two
  simp only [mlieBracketWithin_apply, mpullbackWithin_apply]
  -- first, rewrite the pullback of the Lie bracket as a pullback in `E` under the map
  -- `F = extChartAt I' (f x‚ÇÄ) ‚àò f ‚àò (extChartAt I x‚ÇÄ).symm` of a Lie bracket computed in `E'`,
  -- of two vector fields `V'` and `W'`.
  rw [‚Üê ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_left
    (isInvertible_mfderiv_extChartAt (mem_extChartAt_source (f x‚ÇÄ)))]
  rw [‚Üê mfderiv_comp_mfderivWithin _ (mdifferentiableAt_extChartAt
    (ChartedSpace.mem_chart_source (f x‚ÇÄ))) h'f (hu x‚ÇÄ hx‚ÇÄ)]
  rw [eq_comm, (isInvertible_mfderiv_extChartAt (mem_extChartAt_source x‚ÇÄ)).inverse_apply_eq]
  have : (mfderivWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm (range I) (extChartAt I x‚ÇÄ x‚ÇÄ)).inverse =
      mfderiv I ùìò(ùïú, E) (extChartAt I x‚ÇÄ) x‚ÇÄ := by
    apply ContinuousLinearMap.inverse_eq
    ¬∑ convert mfderivWithin_extChartAt_symm_comp_mfderiv_extChartAt (I := I) (x := x‚ÇÄ)
        (y := extChartAt I x‚ÇÄ x‚ÇÄ) (by simp)
    ¬∑ convert mfderiv_extChartAt_comp_mfderivWithin_extChartAt_symm (I := I) (x := x‚ÇÄ)
        (y := extChartAt I x‚ÇÄ x‚ÇÄ) (by simp)
  rw [‚Üê this, ‚Üê ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_right]; swap
  ¬∑ exact isInvertible_mfderivWithin_extChartAt_symm (mem_extChartAt_target x‚ÇÄ)
  have : mfderivWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm (range I) (extChartAt I x‚ÇÄ x‚ÇÄ) =
      mfderivWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I)
      (extChartAt I x‚ÇÄ x‚ÇÄ) :=
    (MDifferentiableWithinAt.mfderivWithin_mono
      (mdifferentiableWithinAt_extChartAt_symm (mem_extChartAt_target x‚ÇÄ))
      (UniqueDiffWithinAt.uniqueMDiffWithinAt (hu x‚ÇÄ hx‚ÇÄ)) inter_subset_right).symm
  rw [this]; clear this
  rw [‚Üê mfderivWithin_comp_of_eq]; rotate_left
  ¬∑ apply MDifferentiableAt.comp_mdifferentiableWithinAt (I' := I') _ _ h'f
    exact mdifferentiableAt_extChartAt (ChartedSpace.mem_chart_source (f x‚ÇÄ))
  ¬∑ exact (mdifferentiableWithinAt_extChartAt_symm (mem_extChartAt_target x‚ÇÄ)).mono
      inter_subset_right
  ¬∑ exact inter_subset_left
  ¬∑ exact UniqueDiffWithinAt.uniqueMDiffWithinAt (hu x‚ÇÄ hx‚ÇÄ)
  ¬∑ simp
  set V' := mpullbackWithin ùìò(ùïú, E') I' (extChartAt I' (f x‚ÇÄ)).symm V (range I') with hV'
  set W' := mpullbackWithin ùìò(ùïú, E') I' (extChartAt I' (f x‚ÇÄ)).symm W (range I') with hW'
  set F := ((extChartAt I' (f x‚ÇÄ)) ‚àò f) ‚àò ‚Üë(extChartAt I x‚ÇÄ).symm with hF
  have hFx‚ÇÄ : extChartAt I' (f x‚ÇÄ) (f x‚ÇÄ) = F (extChartAt I x‚ÇÄ x‚ÇÄ) := by simp [F]
  rw [hFx‚ÇÄ, ‚Üê mpullbackWithin_apply]
  -- second rewrite, the Lie bracket of the pullback as the Lie bracket of the pullback of the
  -- vector fields `V'` and `W'` in `E'`.
  have P (Y : (x : M') ‚Üí TangentSpace I' x) :
      (mpullbackWithin ùìò(ùïú, E) I (extChartAt I x‚ÇÄ).symm (mpullbackWithin I I' f Y s)
      (range I)) =·∂†[ùìù[(extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I] (extChartAt I x‚ÇÄ x‚ÇÄ)]
        mpullbackWithin ùìò(ùïú, E) ùìò(ùïú, E') F
          (mpullbackWithin ùìò(ùïú, E') I' ((extChartAt I' (f x‚ÇÄ)).symm) Y (range I'))
          ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I) := by
    have : (extChartAt I x‚ÇÄ).target
        ‚àà ùìù[(extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I] (extChartAt I x‚ÇÄ x‚ÇÄ) :=
      nhdsWithin_mono _ inter_subset_right (extChartAt_target_mem_nhdsWithin x‚ÇÄ)
    filter_upwards [self_mem_nhdsWithin, this] with y hy h'''y
    have h'y : f ((extChartAt I x‚ÇÄ).symm y) ‚àà (extChartAt I' (f x‚ÇÄ)).source := ht (hst hy.1)
    have h''y : f ((extChartAt I x‚ÇÄ).symm y) ‚àà (chartAt H' (f x‚ÇÄ)).source := by simpa using h'y
    have huy : UniqueMDiffWithinAt ùìò(ùïú, E) ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I) y := by
      apply UniqueDiffWithinAt.uniqueMDiffWithinAt
      rw [inter_comm]
      apply hu.uniqueDiffWithinAt_range_inter
      exact ‚ü®h'''y, hy.1‚ü©
    simp only [mpullbackWithin_apply, hF, comp_apply, ‚Üê mfderivWithin_eq_fderivWithin]
    rw [mfderivWithin_comp (I' := I) (u := s)]; rotate_left
    ¬∑ apply (mdifferentiableAt_extChartAt h''y).comp_mdifferentiableWithinAt (I' := I')
      exact (hf _ hy.1).mdifferentiableWithinAt one_le_two
    ¬∑ exact (mdifferentiableWithinAt_extChartAt_symm h'''y).mono inter_subset_right
    ¬∑ exact inter_subset_left
    ¬∑ exact huy
    rw [mfderiv_comp_mfderivWithin (I' := I')]; rotate_left
    ¬∑ exact mdifferentiableAt_extChartAt h''y
    ¬∑ exact (hf _ hy.1).mdifferentiableWithinAt one_le_two
    ¬∑ exact hu _ hy.1
    rw [‚Üê ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_right]; swap
    ¬∑ exact isInvertible_mfderivWithin_extChartAt_symm h'''y
    rw [‚Üê ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_left]; swap
    ¬∑ exact isInvertible_mfderivWithin_extChartAt_symm (PartialEquiv.map_source _ h'y)
    have : f ((extChartAt I x‚ÇÄ).symm y)
        = (extChartAt I' (f x‚ÇÄ)).symm ((extChartAt I' (f x‚ÇÄ)) (f ((extChartAt I x‚ÇÄ).symm y))) :=
      (PartialEquiv.left_inv (extChartAt I' (f x‚ÇÄ)) h'y).symm
    congr 2
    have : (mfderivWithin ùìò(ùïú, E') I' ((extChartAt I' (f x‚ÇÄ)).symm) (range I')
        (extChartAt I' (f x‚ÇÄ) (f ((extChartAt I x‚ÇÄ).symm y)))) ‚àòL
        (mfderiv I' ùìò(ùïú, E') (‚Üë(extChartAt I' (f x‚ÇÄ))) (f ((extChartAt I x‚ÇÄ).symm y))) =
        ContinuousLinearMap.id _ _ := by
      convert mfderivWithin_extChartAt_symm_comp_mfderiv_extChartAt
        ((PartialEquiv.map_source _ h'y))
    simp only [‚Üê ContinuousLinearMap.comp_assoc, this, ContinuousLinearMap.id_comp]
    congr 1
    exact ((mdifferentiableWithinAt_extChartAt_symm h'''y).mfderivWithin_mono huy
      inter_subset_right).symm
  rw [Filter.EventuallyEq.lieBracketWithin_vectorField_eq_of_mem (P V) (P W) (by simp [hx‚ÇÄ]),
    ‚Üê hV', ‚Üê hW']
  simp only [mpullbackWithin_eq_pullbackWithin]
  -- finally, use the fact that for `C^2` maps between vector spaces with symmetric second
  -- derivative, the pullback and the Lie bracket commute.
  rw [pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt_of_eventuallyEq
      (u := (extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© (extChartAt I x‚ÇÄ).target)]
  ¬∑ exact hsymm
  ¬∑ rw [hF, comp_assoc]
    apply ContMDiffWithinAt.contDiffWithinAt
    apply ContMDiffAt.comp_contMDiffWithinAt (I' := I')
    ¬∑ exact contMDiffAt_extChartAt' (by simp)
    apply ContMDiffWithinAt.comp_of_eq (I' := I) (hf _ hx‚ÇÄ) _ _ A
    ¬∑ exact (contMDiffWithinAt_extChartAt_symm_range _ (mem_extChartAt_target x‚ÇÄ)).mono
        inter_subset_right
    ¬∑ exact (mapsTo_preimage _ _).mono_left inter_subset_left
  ¬∑ rw [‚Üê hFx‚ÇÄ]
    exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ rw [‚Üê hFx‚ÇÄ]
    exact hW.differentiableWithinAt_mpullbackWithin_vectorField
  ¬∑ rw [inter_comm]
    exact UniqueMDiffOn.uniqueDiffOn_target_inter hu x‚ÇÄ
  ¬∑ simp [hx‚ÇÄ]
  ¬∑ intro z hz
    simp only [comp_apply, mem_inter_iff, mem_preimage, mem_range, F]
    refine ‚ü®?_, mem_range_self _‚ü©
    convert hst hz.1
    exact PartialEquiv.left_inv (extChartAt I' (f x‚ÇÄ)) (ht (hst hz.1))
  ¬∑ rw [‚Üê nhdsWithin_eq_iff_eventuallyEq]
    apply le_antisymm
    ¬∑ exact nhdsWithin_mono _ (inter_subset_inter_right _ (extChartAt_target_subset_range x‚ÇÄ))
    ¬∑ rw [nhdsWithin_le_iff, nhdsWithin_inter]
      exact Filter.inter_mem_inf self_mem_nhdsWithin (extChartAt_target_mem_nhdsWithin x‚ÇÄ)

/- The Lie bracket of vector fields on manifolds is well defined, i.e., it is invariant under
diffeomorphisms. -/
lemma mpullbackWithin_mlieBracketWithin_of_isSymmSndFDerivWithinAt
    {f : M ‚Üí M'} {V W : Œ† (x : M'), TangentSpace I' x} {x‚ÇÄ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (V x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (W x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffWithinAt I I' 2 f s x‚ÇÄ) (hx‚ÇÄ : x‚ÇÄ ‚àà s)
    (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ)
    (hsymm : IsSymmSndFDerivWithinAt ùïú ((extChartAt I' (f x‚ÇÄ)) ‚àò f ‚àò (extChartAt I x‚ÇÄ).symm)
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x‚ÇÄ x‚ÇÄ)) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s x‚ÇÄ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s x‚ÇÄ := by
  have A : (extChartAt I x‚ÇÄ).symm (extChartAt I x‚ÇÄ x‚ÇÄ) = x‚ÇÄ := by simp
  by_cases hfi : (mfderivWithin I I' f s x‚ÇÄ).IsInvertible; swap
  ¬∑ simp only [mlieBracketWithin_apply, mpullbackWithin_apply,
      ContinuousLinearMap.inverse_of_not_isInvertible hfi, ContinuousLinearMap.zero_apply]
    rw [lieBracketWithin_eq_zero_of_eq_zero]
    ¬∑ simp [-extChartAt]
    ¬∑ simp only [mpullbackWithin_apply]
      rw [A, ContinuousLinearMap.inverse_of_not_isInvertible hfi]
      simp [-extChartAt]
    ¬∑ simp only [mpullbackWithin_apply]
      rw [A, ContinuousLinearMap.inverse_of_not_isInvertible hfi]
      simp [-extChartAt]
  -- Now, interesting case where the derivative of `f` is invertible
  have : CompleteSpace E' := by
    rcases hfi with ‚ü®M, -‚ü©
    let M' : E ‚âÉL[ùïú] E' := M
    exact (completeSpace_congr (e := M'.toEquiv) M'.isUniformEmbedding).1 (by assumption)
  -- choose a small open set `v` around `x‚ÇÄ` where `f` is `C^2`
  obtain ‚ü®u, u_open, x‚ÇÄu, ut, maps_u, u_smooth‚ü© :
      ‚àÉ u, IsOpen u ‚àß x‚ÇÄ ‚àà u ‚àß s ‚à© u ‚äÜ f ‚Åª¬π' t ‚àß
        s ‚à© u ‚äÜ f ‚Åª¬π' (extChartAt I' (f x‚ÇÄ)).source ‚àß ContMDiffOn I I' 2 f (s ‚à© u) := by
    obtain ‚ü®u, u_open, x‚ÇÄu, hu‚ü© :
        ‚àÉ u, IsOpen u ‚àß x‚ÇÄ ‚àà u ‚àß ContMDiffOn I I' 2 f (insert x‚ÇÄ s ‚à© u) :=
      hf.contMDiffOn' le_rfl (by simp)
    have : f ‚Åª¬π' (extChartAt I' (f x‚ÇÄ)).source ‚àà ùìù[s] x‚ÇÄ :=
      hf.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (f x‚ÇÄ))
    rcases mem_nhdsWithin.1 (Filter.inter_mem hst this) with ‚ü®w, w_open, x‚ÇÄw, hw‚ü©
    refine ‚ü®u ‚à© w, u_open.inter w_open, by simp [x‚ÇÄu, x‚ÇÄw], ?_, ?_, ?_‚ü©
    ¬∑ apply Subset.trans _ (hw.trans inter_subset_left)
      exact fun y hy ‚Ü¶ ‚ü®hy.2.2, hy.1‚ü©
    ¬∑ apply Subset.trans _ (hw.trans inter_subset_right)
      exact fun y hy ‚Ü¶ ‚ü®hy.2.2, hy.1‚ü©
    ¬∑ apply hu.mono
      exact fun y hy ‚Ü¶ ‚ü®subset_insert _ _ hy.1, hy.2.1‚ü©
  have u_mem : u ‚àà ùìù x‚ÇÄ := u_open.mem_nhds x‚ÇÄu
  -- apply the auxiliary version to `s ‚à© u`
  set s' := s ‚à© u with hs'
  have s'_eq : s' =·∂†[ùìù x‚ÇÄ] s := by
    filter_upwards [u_mem] with y hy
    change (y ‚àà s ‚à© u) = (y ‚àà s)
    simp [hy]
  set t' := t ‚à© (extChartAt I' (f x‚ÇÄ)).source with ht'
  calc mpullbackWithin I I' f (mlieBracketWithin I' V W t) s x‚ÇÄ
  _ = mpullbackWithin I I' f (mlieBracketWithin I' V W t) s' x‚ÇÄ := by
    simp only [mpullbackWithin, hs', mfderivWithin_inter u_mem]
  _ = mpullbackWithin I I' f (mlieBracketWithin I' V W t') s' x‚ÇÄ := by
    simp only [mpullbackWithin, ht', mlieBracketWithin_inter (extChartAt_source_mem_nhds (f x‚ÇÄ))]
  _ = mlieBracketWithin I (mpullbackWithin I I' f V s') (mpullbackWithin I I' f W s') s' x‚ÇÄ := by
    apply mpullbackWithin_mlieBracketWithin_aux (t := t') (hV.mono inter_subset_left)
      (hW.mono inter_subset_left) (hu.inter u_open) u_smooth ‚ü®hx‚ÇÄ, x‚ÇÄu‚ü© inter_subset_right
      (fun y hy ‚Ü¶ ‚ü®ut hy, maps_u hy‚ü©)
    apply hsymm.congr_set
    have : (extChartAt I x‚ÇÄ).symm ‚Åª¬π' u ‚àà ùìù (extChartAt I x‚ÇÄ x‚ÇÄ) := by
      apply (continuousAt_extChartAt_symm x‚ÇÄ).preimage_mem_nhds
      apply u_open.mem_nhds (by simpa using x‚ÇÄu)
    filter_upwards [this] with y hy
    change (y ‚àà (extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I) =
      (y ‚àà (extChartAt I x‚ÇÄ).symm ‚Åª¬π' (s ‚à© u) ‚à© range I)
    simp [-extChartAt, hy]
  _ = mlieBracketWithin I (mpullbackWithin I I' f V s') (mpullbackWithin I I' f W s') s x‚ÇÄ := by
    simp only [hs', mlieBracketWithin_inter u_mem]
  _ = mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s x‚ÇÄ := by
    apply Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem _ _ hx‚ÇÄ
    ¬∑ apply nhdsWithin_le_nhds
      filter_upwards [mfderivWithin_eventually_congr_set (I := I) (I' := I') (f := f) s'_eq]
        with y hy using by simp [mpullbackWithin, hy]
    ¬∑ apply nhdsWithin_le_nhds
      filter_upwards [mfderivWithin_eventually_congr_set (I := I) (I' := I') (f := f) s'_eq]
        with y hy using by simp [mpullbackWithin, hy]

end

section

/- Only register this instance with `2` and not a general `n` to prevent looping:
if looking for `IsManifold I 3 M`, otherwise it could try to see if
`IsManifold I (minSmoothness ùïú 3) M`, then `IsManifold I (minSmoothness ùïú (minSmoothness ùïú 3)) M`
and so on. -/
instance [IsManifold I (minSmoothness ùïú 2) M] :
    IsManifold I 2 M :=
  IsManifold.of_le (n := minSmoothness ùïú 2) le_minSmoothness

variable [IsManifold I (minSmoothness ùïú 2) M] [IsManifold I' (minSmoothness ùïú 2) M']
  [CompleteSpace E] {n : WithTop ‚Ñï‚àû}

/-- The pullback commutes with the Lie bracket of vector fields on manifolds. Version where one
assumes that the map is smooth on a larget set `u` (so that the
condition `x‚ÇÄ ‚àà closure (interior u)`, needed to guarantee the symmetry of the second derivative,
becomes easier to check.) -/
lemma mpullbackWithin_mlieBracketWithin'
    {f : M ‚Üí M'} {V W : Œ† (x : M'), TangentSpace I' x} {x‚ÇÄ : M} {s u : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (V x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (W x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hs : UniqueMDiffOn I s) (hu : UniqueMDiffOn I u)
    (hf : ContMDiffWithinAt I I' n f u x‚ÇÄ) (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hn : minSmoothness ùïú 2 ‚â§ n)
    (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) (h'x‚ÇÄ : x‚ÇÄ ‚àà closure (interior u)) (hsu : s ‚äÜ u) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s x‚ÇÄ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s x‚ÇÄ := by
  have B : ContDiffWithinAt ùïú n ((extChartAt I' (f x‚ÇÄ)) ‚àò f ‚àò (extChartAt I x‚ÇÄ).symm)
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' u ‚à© (extChartAt I x‚ÇÄ).target) (extChartAt I x‚ÇÄ x‚ÇÄ) := by
    apply (contMDiffWithinAt_iff.1 hf).2.congr_set
    exact EventuallyEq.inter (by rfl) extChartAt_target_eventuallyEq.symm
  apply mpullbackWithin_mlieBracketWithin_of_isSymmSndFDerivWithinAt hV hW hs
    ((hf.mono hsu).of_le (le_minSmoothness.trans hn)) hx‚ÇÄ hst
  have : ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© (extChartAt I x‚ÇÄ).target : Set E)
      =·∂†[ùìù (extChartAt I x‚ÇÄ x‚ÇÄ)] ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' s ‚à© range I : Set E) :=
    EventuallyEq.inter (by rfl) extChartAt_target_eventuallyEq
  apply IsSymmSndFDerivWithinAt.congr_set _ this
  have : IsSymmSndFDerivWithinAt ùïú ((extChartAt I' (f x‚ÇÄ)) ‚àò f ‚àò (extChartAt I x‚ÇÄ).symm)
      ((extChartAt I x‚ÇÄ).symm ‚Åª¬π' u ‚à© (extChartAt I x‚ÇÄ).target) (extChartAt I x‚ÇÄ x‚ÇÄ) := by
    apply ContDiffWithinAt.isSymmSndFDerivWithinAt (n := minSmoothness ùïú 2) _ le_rfl
    ¬∑ rw [inter_comm]
      exact UniqueMDiffOn.uniqueDiffOn_target_inter hu x‚ÇÄ
    ¬∑ apply extChartAt_mem_closure_interior h'x‚ÇÄ (mem_extChartAt_source x‚ÇÄ)
    ¬∑ simp [hsu hx‚ÇÄ]
    ¬∑ exact B.of_le hn
  apply IsSymmSndFDerivWithinAt.mono_of_mem_nhdsWithin this
  ¬∑ apply mem_of_superset self_mem_nhdsWithin (inter_subset_inter_left _ (preimage_mono hsu))
  ¬∑ exact (B.of_le hn).of_le le_minSmoothness
  ¬∑ rw [inter_comm]
    exact UniqueMDiffOn.uniqueDiffOn_target_inter hs x‚ÇÄ
  ¬∑ rw [inter_comm]
    exact UniqueMDiffOn.uniqueDiffOn_target_inter hu x‚ÇÄ
  ¬∑ simp [hx‚ÇÄ]

/-- The pullback commutes with the Lie bracket of vector fields on manifolds. -/
lemma mpullbackWithin_mlieBracketWithin
    {f : M ‚Üí M'} {V W : Œ† (x : M'), TangentSpace I' x} {x‚ÇÄ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (V x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (W x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ) (hx‚ÇÄ : x‚ÇÄ ‚àà s)
    (hn : minSmoothness ùïú 2 ‚â§ n)
    (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) (h'x‚ÇÄ : x‚ÇÄ ‚àà closure (interior s)) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s x‚ÇÄ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s x‚ÇÄ :=
  mpullbackWithin_mlieBracketWithin' hV hW hu hu hf hx‚ÇÄ hn hst h'x‚ÇÄ Subset.rfl

/-- The pullback commutes with the Lie bracket of vector fields on manifolds. -/
lemma mpullback_mlieBracketWithin
    {f : M ‚Üí M'} {V W : Œ† (x : M'), TangentSpace I' x} {x‚ÇÄ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (V x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x ‚Ü¶ (W x : TangentBundle I' M')) t (f x‚ÇÄ))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffAt I I' n f x‚ÇÄ) (hx‚ÇÄ : x‚ÇÄ ‚àà s)
    (hn : minSmoothness ùïú 2 ‚â§ n) (hst : f ‚Åª¬π' t ‚àà ùìù[s] x‚ÇÄ) :
    mpullback I I' f (mlieBracketWithin I' V W t) x‚ÇÄ =
      mlieBracketWithin I (mpullback I I' f V) (mpullback I I' f W) s x‚ÇÄ := by
  have : mpullback I I' f (mlieBracketWithin I' V W t) x‚ÇÄ =
      mpullbackWithin I I' f (mlieBracketWithin I' V W t) s x‚ÇÄ := by
    simp only [mpullback, mpullbackWithin]
    congr
    apply (mfderivWithin_eq_mfderiv (hu _ hx‚ÇÄ) _).symm
    exact hf.mdifferentiableAt (one_le_two.trans (le_minSmoothness.trans hn))
  rw [this, mpullbackWithin_mlieBracketWithin' hV hW hu uniqueMDiffOn_univ hf.contMDiffWithinAt
    hx‚ÇÄ hn hst (by simp) (subset_univ _)]
  apply Filter.EventuallyEq.mlieBracketWithin_vectorField_of_insert
  ¬∑ rw [insert_eq_of_mem hx‚ÇÄ]
    filter_upwards [nhdsWithin_le_nhds ((contMDiffAt_iff_contMDiffAt_nhds (by simp)).1
      (hf.of_le (le_minSmoothness.trans hn))), self_mem_nhdsWithin] with y hy h'y
    simp only [mpullback, mpullbackWithin]
    congr
    apply mfderivWithin_eq_mfderiv (hu _ h'y)
    exact hy.mdifferentiableAt one_le_two
  ¬∑ rw [insert_eq_of_mem hx‚ÇÄ]
    filter_upwards [nhdsWithin_le_nhds ((contMDiffAt_iff_contMDiffAt_nhds (by simp)).1
      (hf.of_le (le_minSmoothness.trans hn))), self_mem_nhdsWithin] with y hy h'y
    simp only [mpullback, mpullbackWithin]
    congr
    apply mfderivWithin_eq_mfderiv (hu _ h'y)
    exact hy.mdifferentiableAt one_le_two

lemma mpullback_mlieBracket
    {f : M ‚Üí M'} {V W : Œ† (x : M'), TangentSpace I' x} {x‚ÇÄ : M}
    (hV : MDifferentiableAt I' I'.tangent (fun x ‚Ü¶ (V x : TangentBundle I' M')) (f x‚ÇÄ))
    (hW : MDifferentiableAt I' I'.tangent (fun x ‚Ü¶ (W x : TangentBundle I' M')) (f x‚ÇÄ))
    (hf : ContMDiffAt I I' n f x‚ÇÄ) (hn : minSmoothness ùïú 2 ‚â§ n) :
    mpullback I I' f (mlieBracket I' V W) x‚ÇÄ =
      mlieBracket I (mpullback I I' f V) (mpullback I I' f W) x‚ÇÄ := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê mdifferentiableWithinAt_univ] at hV hW ‚ä¢
  exact mpullback_mlieBracketWithin hV hW uniqueMDiffOn_univ hf (mem_univ _) hn (by simp)

/-- If two vector fields are `C^n` with `n ‚â• m + 1`, then their Lie bracket is `C^m`. -/
protected lemma _root_.ContMDiffWithinAt.mlieBracketWithin_vectorField
    [IsManifold I (n + 1) M] {m : WithTop ‚Ñï‚àû}
    {U V : Œ† (x : M), TangentSpace I x} {s : Set M} {x : M}
    (hU : ContMDiffWithinAt I I.tangent n (fun x ‚Ü¶ (U x : TangentBundle I M)) s x)
    (hV : ContMDiffWithinAt I I.tangent n (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffOn I s) (hx : x ‚àà s) (hmn : minSmoothness ùïú (m + 1) ‚â§ n) :
    ContMDiffWithinAt I I.tangent m
      (fun x ‚Ü¶ (mlieBracketWithin I U V s x : TangentBundle I M)) s x := by
  /- The statement is not obvious, since at different points the Lie bracket is defined using
  different charts. However, since we know that the Lie bracket is invariant under diffeos, we can
  use a single chart to prove the statement. Let `U'` and `V'` denote the pullbacks of `U` and `V`
  in the chart around `x`. Then the Lie bracket there is smooth (as it coincides with the vector
  space Lie bracket, given by an explicit formula). Pulling back this Lie bracket in `M` gives
  locally a smooth function, which coincides with the initial Lie bracket by invariance
  under diffeos. -/
  have min2 : minSmoothness ùïú 2 ‚â§ n + 1 := by
    simp only [minSmoothness] at hmn ‚ä¢
    split_ifs with h
    ¬∑ simp [h] at hmn
      apply le_trans _ (add_le_add_right hmn 1)
      simp only [add_assoc]
      exact le_add_self
    ¬∑ simpa [h] using hmn
  apply contMDiffWithinAt_iff_le_ne_infty.2 (fun m' hm' h'm' ‚Ü¶ ?_)
  have : IsManifold I n M := IsManifold.of_le (n := n + 1) le_self_add
  have hn : 1 ‚â§ m' + 1 := le_add_self
  have hm'n : m' + 1 ‚â§ n := le_trans (add_le_add_right hm' 1) (le_minSmoothness.trans hmn)
  have : IsManifold I (m' + 1) M := IsManifold.of_le (n := n + 1) (hm'n.trans le_self_add)
  have pre_mem : (extChartAt I x) ‚Åª¬π' ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s)
      ‚àà ùìù[s] x := by
    filter_upwards [self_mem_nhdsWithin,
      nhdsWithin_le_nhds (extChartAt_source_mem_nhds (I := I) x)] with y hy h'y
    exact ‚ü®(extChartAt I x).map_source h'y,
      by simpa only [mem_preimage, (extChartAt I x).left_inv h'y] using hy‚ü©
  let U' := mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm U (range I)
  let V' := mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm V (range I)
  have A : ContDiffWithinAt ùïú m' (lieBracketWithin ùïú U' V'
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x x) :=
    ContDiffWithinAt.lieBracketWithin_vectorField
      (contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.1
        (contMDiffWithinAt_mpullbackWithin_extChartAt_symm hU hs hx le_rfl))
      (contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.1
        (contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx le_rfl))
      (hs.uniqueDiffOn_target_inter x) hm'n (by simp [hx])
  have B : ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent m' (fun y ‚Ü¶ (mlieBracketWithin ùìò(ùïú, E) U' V'
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x x) := by
    rw [‚Üê mlieBracketWithin_eq_lieBracketWithin] at A
    exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.2 A
  have C : ContMDiffWithinAt I I.tangent m' (fun y ‚Ü¶ (mpullback I ùìò(ùïú, E) (extChartAt I x)
      ((mlieBracketWithin ùìò(ùïú, E) U' V'
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s))) y : TangentBundle I M)) s x :=
    ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin_of_eq B (n := m' + 1)
      contMDiffAt_extChartAt
      (isInvertible_mfderiv_extChartAt (mem_extChartAt_source x)) le_rfl pre_mem rfl
  apply C.congr_of_eventuallyEq_of_mem _ hx
  filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem,
    eventually_eventually_nhdsWithin.2 (eventuallyEq_mpullback_mpullbackWithin_extChartAt U),
    eventually_eventually_nhdsWithin.2 (eventuallyEq_mpullback_mpullbackWithin_extChartAt V),
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm (hU.of_le hm'n) hs hx
      (add_le_add_right hm'n 1) (by simp [h'm']),
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm (hV.of_le hm'n) hs hx
      (add_le_add_right hm'n 1) (by simp [h'm']),
    nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin]
    with y hy hyU hyV h'yU h'yV hy_chart hys
  simp only [Bundle.TotalSpace.mk_inj]
  rw [mpullback_mlieBracketWithin (h'yU.mdifferentiableWithinAt hn)
    (h'yV.mdifferentiableWithinAt hn) hs (contMDiffAt_extChartAt' hy_chart) hys min2 hy]
  exact Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem hyU hyV hys

/-- If two vector fields are `C^n` with `n ‚â• m + 1`, then their Lie bracket is `C^m`. -/
lemma _root_.ContMDiffAt.mlieBracket_vectorField {m n : ‚Ñï‚àû}
    [IsManifold I (n + 1) M] {U V : Œ† (x : M), TangentSpace I x} {x : M}
    (hU : ContMDiffAt I I.tangent n (fun x ‚Ü¶ (U x : TangentBundle I M)) x)
    (hV : ContMDiffAt I I.tangent n (fun x ‚Ü¶ (V x : TangentBundle I M)) x)
    (hmn : m + 1 ‚â§ n) :
    ContMDiffAt I I.tangent m (fun x ‚Ü¶ (mlieBracket I U V x : TangentBundle I M)) x := by
  simp only [‚Üê contMDiffWithinAt_univ, ‚Üê mlieBracketWithin_univ] at hU hV ‚ä¢
  exact hU.mlieBracketWithin_vectorField hV uniqueMDiffOn_univ (mem_univ _) hmn

/-- If two vector fields are `C^n` with `n ‚â• m + 1`, then their Lie bracket is `C^m`. -/
lemma _root_.ContMDiffOn.mlieBracketWithin_vectorField {m n : ‚Ñï‚àû}
    [IsManifold I (n + 1) M] {U V : Œ† (x : M), TangentSpace I x}
    (hU : ContMDiffOn I I.tangent n (fun x ‚Ü¶ (U x : TangentBundle I M)) s)
    (hV : ContMDiffOn I I.tangent n (fun x ‚Ü¶ (V x : TangentBundle I M)) s)
    (hs : UniqueMDiffOn I s) (hmn : m + 1 ‚â§ n) :
    ContMDiffOn I I.tangent m (fun x ‚Ü¶ (mlieBracketWithin I U V s x : TangentBundle I M)) s :=
  fun x hx ‚Ü¶ (hU x hx).mlieBracketWithin_vectorField (hV x hx) hs hx hmn

/-- If two vector fields are `C^n` with `n ‚â• m + 1`, then their Lie bracket is `C^m`. -/
lemma _root_.ContDiff.mlieBracket_vectorField {m n : ‚Ñï‚àû}
    [IsManifold I (n + 1) M] {U V : Œ† (x : M), TangentSpace I x}
    (hU : ContMDiff I I.tangent n (fun x ‚Ü¶ (U x : TangentBundle I M)))
    (hV : ContMDiff I I.tangent n (fun x ‚Ü¶ (V x : TangentBundle I M)))
    (hmn : m + 1 ‚â§ n) :
    ContMDiff I I.tangent m (fun x ‚Ü¶ (mlieBracket I U V x : TangentBundle I M)) := by
  simp only [‚Üê contMDiffOn_univ, mlieBracketWithin_univ] at hU hV ‚ä¢
  exact hU.mlieBracketWithin_vectorField hV uniqueMDiffOn_univ hmn

/-- The Lie bracket of vector fields in manifolds satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]`. -/
theorem leibniz_identity_mlieBracketWithin_apply
    {U V W : Œ† (x : M), TangentSpace I x} {s : Set M} {x : M}
    (hs : UniqueMDiffOn I s) (h's : x ‚àà closure (interior s)) (hx : x ‚àà s)
    (hU : ContMDiffWithinAt I I.tangent 2 (fun x ‚Ü¶ (U x : TangentBundle I M)) s x)
    (hV : ContMDiffWithinAt I I.tangent 2 (fun x ‚Ü¶ (V x : TangentBundle I M)) s x)
    (hW : ContMDiffWithinAt I I.tangent 2 (fun x ‚Ü¶ (W x : TangentBundle I M)) s x) :
    mlieBracketWithin I U (mlieBracketWithin I V W s) s x =
      mlieBracketWithin I (mlieBracketWithin I U V s) W s x
      + mlieBracketWithin I V (mlieBracketWithin I U W s) s x := by
  have s_inter_mem : s ‚à© (extChartAt I x).source ‚àà ùìù[s] x :=
    inter_mem self_mem_nhdsWithin (nhdsWithin_le_nhds (extChartAt_source_mem_nhds x))
  have pre_mem : (extChartAt I x) ‚Åª¬π' ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s)
      ‚àà ùìù[s] x := by
    filter_upwards [s_inter_mem] with y hy
    exact ‚ü®(extChartAt I x).map_source hy.2,
      by simpa only [mem_preimage, (extChartAt I x).left_inv hy.2] using hy.1‚ü©
  -- write everything as pullbacks of vector fields in `E` (denoted with primes), for which
  -- the identity can be checked via direct calculation.
  let U' := mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm U (range I)
  let V' := mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm V (range I)
  let W' := mpullbackWithin ùìò(ùïú, E) I (extChartAt I x).symm W (range I)
  -- register basic facts on the pullbacks in the vector space
  have J0U : ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent 2 (fun y ‚Ü¶ (U' y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x x) :=
    contMDiffWithinAt_mpullbackWithin_extChartAt_symm hU hs hx
  have J0V : ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent 2 (fun y ‚Ü¶ (V' y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x x) :=
    contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx
  have J0W : ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent 2 (fun y ‚Ü¶ (W' y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x x) :=
    contMDiffWithinAt_mpullbackWithin_extChartAt_symm hW hs hx
  have J1U : ‚àÄ·∂† y in ùìù[s] x, ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent 2
      (fun y ‚Ü¶ (U' y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x y) :=
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm hU hs hx
  have J1V : ‚àÄ·∂† y in ùìù[s] x, ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent 2
      (fun y ‚Ü¶ (V' y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x y) :=
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx
  have J1W : ‚àÄ·∂† y in ùìù[s] x, ContMDiffWithinAt ùìò(ùïú, E) ùìò(ùïú, E).tangent 2
      (fun y ‚Ü¶ (W' y : TangentBundle ùìò(ùïú, E) E))
      ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s) (extChartAt I x y) :=
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm hW hs hx
  have JU : U =·∂†[ùìù[s] x] mpullback I ùìò(ùïú, E) (extChartAt I x) U' :=
    eventuallyEq_mpullback_mpullbackWithin_extChartAt U
  have JV : V =·∂†[ùìù[s] x] mpullback I ùìò(ùïú, E) (extChartAt I x) V' :=
    eventuallyEq_mpullback_mpullbackWithin_extChartAt V
  have JW : W =·∂†[ùìù[s] x] mpullback I ùìò(ùïú, E) (extChartAt I x) W' :=
    eventuallyEq_mpullback_mpullbackWithin_extChartAt W
  rw [JU.mlieBracketWithin_vectorField_eq_of_mem (JV.mlieBracketWithin_vectorField JW) hx,
    (JU.mlieBracketWithin_vectorField JV).mlieBracketWithin_vectorField_eq_of_mem JW hx,
    JV.mlieBracketWithin_vectorField_eq_of_mem (JU.mlieBracketWithin_vectorField JW) hx]
  /- Rewrite the first term as a pullback-/
  have : ‚àÄ·∂† y in ùìù[s] x, mlieBracketWithin I
        (mpullback I ùìò(ùïú, E) (extChartAt I x) V') (mpullback I ùìò(ùïú, E) (extChartAt I x) W') s y
      = mpullback I ùìò(ùïú, E) (extChartAt I x) (mlieBracketWithin ùìò(ùïú, E) V' W'
        ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s)) y := by
    filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem, J1V, J1W,
      nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin] with y hy hyV hyW h'y ys
    exact (mpullback_mlieBracketWithin (hyV.mdifferentiableWithinAt one_le_two)
      (hyW.mdifferentiableWithinAt one_le_two) hs (contMDiffAt_extChartAt' h'y) ys hy).symm
  rw [Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem EventuallyEq.rfl this hx,
    ‚Üê mpullback_mlieBracketWithin (J0U.mdifferentiableWithinAt one_le_two) _ hs
      contMDiffAt_extChartAt hx pre_mem]; swap
  ¬∑ apply ContMDiffWithinAt.mdifferentiableWithinAt _ le_rfl
    apply J0V.mlieBracketWithin_vectorField J0W (m := 1)
    ¬∑ exact hs.uniqueMDiffOn_target_inter x
    ¬∑ exact ‚ü®mem_extChartAt_target x, by simp [hx]‚ü©
    ¬∑ exact le_rfl
  /- Rewrite the second term as a pullback-/
  have : ‚àÄ·∂† y in ùìù[s] x, mlieBracketWithin I
        (mpullback I ùìò(ùïú, E) (extChartAt I x) U') (mpullback I ùìò(ùïú, E) (extChartAt I x) V') s y
      = mpullback I ùìò(ùïú, E) (extChartAt I x) (mlieBracketWithin ùìò(ùïú, E) U' V'
        ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s)) y := by
    filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem, J1U, J1V,
      nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin] with y hy hyU hyV h'y ys
    exact (mpullback_mlieBracketWithin (hyU.mdifferentiableWithinAt one_le_two)
      (hyV.mdifferentiableWithinAt one_le_two) hs (contMDiffAt_extChartAt' h'y) ys hy).symm
  rw [Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem this EventuallyEq.rfl hx,
    ‚Üê mpullback_mlieBracketWithin _ (J0W.mdifferentiableWithinAt one_le_two) hs
      contMDiffAt_extChartAt hx pre_mem]; swap
  ¬∑ apply ContMDiffWithinAt.mdifferentiableWithinAt _ le_rfl
    apply J0U.mlieBracketWithin_vectorField J0V (m := 1)
    ¬∑ exact hs.uniqueMDiffOn_target_inter x
    ¬∑ exact ‚ü®mem_extChartAt_target x, by simp [hx]‚ü©
    ¬∑ exact le_rfl
  /- Rewrite the third term as a pullback-/
  have : ‚àÄ·∂† y in ùìù[s] x, mlieBracketWithin I
        (mpullback I ùìò(ùïú, E) (extChartAt I x) U') (mpullback I ùìò(ùïú, E) (extChartAt I x) W') s y
      = mpullback I ùìò(ùïú, E) (extChartAt I x) (mlieBracketWithin ùìò(ùïú, E) U' W'
        ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' s)) y := by
    filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem, J1U, J1W,
      nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin] with y hy hyU hyW h'y ys
    exact (mpullback_mlieBracketWithin (hyU.mdifferentiableWithinAt one_le_two)
      (hyW.mdifferentiableWithinAt one_le_two) hs (contMDiffAt_extChartAt' h'y) ys hy).symm
  rw [Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem EventuallyEq.rfl this hx,
    ‚Üê mpullback_mlieBracketWithin (J0V.mdifferentiableWithinAt one_le_two) _ hs
      contMDiffAt_extChartAt hx pre_mem]; swap
  ¬∑ apply ContMDiffWithinAt.mdifferentiableWithinAt _ le_rfl
    apply J0U.mlieBracketWithin_vectorField J0W (m := 1)
    ¬∑ exact hs.uniqueMDiffOn_target_inter x
    ¬∑ exact ‚ü®mem_extChartAt_target x, by simp [hx]‚ü©
    ¬∑ exact le_rfl
  /- Now that everything is in pullback form, use the leibniz identity in the vector space -/
  rw [‚Üê mpullback_add_apply, mpullback_apply, mpullback_apply]
  congr 1
  simp_rw [mlieBracketWithin_eq_lieBracketWithin]
  apply leibniz_identity_lieBracketWithin (E := E)
  ¬∑ exact hs.uniqueDiffOn_target_inter x
  ¬∑ rw [inter_comm]
    exact extChartAt_mem_closure_interior h's (mem_extChartAt_source x)
  ¬∑ exact ‚ü®mem_extChartAt_target x, by simp [hx]‚ü©
  ¬∑ exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.mp J0U
  ¬∑ exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.mp J0V
  ¬∑ exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.mp J0W

/-- The Lie bracket of vector fields in manifolds satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]`. -/
lemma leibniz_identity_mlieBracket_apply
    {U V W : Œ† (x : M), TangentSpace I x} {x : M}
    (hU : ContMDiffAt I I.tangent 2 (fun x ‚Ü¶ (U x : TangentBundle I M)) x)
    (hV : ContMDiffAt I I.tangent 2 (fun x ‚Ü¶ (V x : TangentBundle I M)) x)
    (hW : ContMDiffAt I I.tangent 2 (fun x ‚Ü¶ (W x : TangentBundle I M)) x) :
    mlieBracket I U (mlieBracket I V W) x =
      mlieBracket I (mlieBracket I U V) W x + mlieBracket I V (mlieBracket I U W) x := by
  simp only [‚Üê mlieBracketWithin_univ, ‚Üê contMDiffWithinAt_univ] at hU hV hW ‚ä¢
  exact leibniz_identity_mlieBracketWithin_apply uniqueMDiffOn_univ (by simp) (mem_univ _) hU hV hW

/-- The Lie bracket of vector fields in manifolds satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]`. -/
lemma leibniz_identity_mlieBracket
    {U V W : Œ† (x : M), TangentSpace I x}
    (hU : ContMDiff I I.tangent 2 (fun x ‚Ü¶ (U x : TangentBundle I M)))
    (hV : ContMDiff I I.tangent 2 (fun x ‚Ü¶ (V x : TangentBundle I M)))
    (hW : ContMDiff I I.tangent 2 (fun x ‚Ü¶ (W x : TangentBundle I M))) :
    mlieBracket I U (mlieBracket I V W) =
      mlieBracket I (mlieBracket I U V) W + mlieBracket I V (mlieBracket I U W) := by
  ext x
  exact leibniz_identity_mlieBracket_apply (hU x) (hV x) (hW x)

end VectorField

set_option linter.style.longFile 1700
