/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.VectorField
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import Mathlib.Geometry.Manifold.MFDeriv.NormedSpace
import Mathlib.Geometry.Manifold.VectorBundle.MDifferentiable

/-!
# Vector fields in manifolds

We study functions of the form `V : Î  (x : M) â†’ TangentSpace I x` on a manifold, i.e.,
vector fields.

We define the pullback of a vector field under a map, as
`VectorField.mpullback I I' f V x := (mfderiv I I' f x).inverse (V (f x))`
(together with the same notion within a set). Note that the pullback uses the junk-value pattern:
if the derivative of the map is not invertible, then pullback is given the junk value zero.

We also define the Lie bracket of two vector fields, denoted with
`VectorField.mlieBracket I V W x`, as the pullback in the manifold of the corresponding notion
in the model space (through `extChartAt I x`).

In addition to comprehensive API on these two notions, the main results are the following:
* `VectorField.mpullback_mlieBracket` states that the pullback of the Lie bracket
  is the Lie bracket of the pullbacks.
* `VectorField.leibniz_identity_mlieBracket` is the Leibniz (or Jacobi)
  identity `[U, [V, W]] = [[U, V], W] + [V, [U, W]]`.

All these are given in the `VectorField` namespace because pullbacks, Lie brackets, and so on,
are notions that make sense in a variety of contexts. We also prefix the notions with `m` to
distinguish the manifold notions from the vector space notions.

For notions that come naturally in other namespaces for dot notation, we specify `vectorField` in
the name to lift ambiguities. For instance, the fact that the Lie bracket of two smooth vector
fields is smooth is `ContMDiffAt.mlieBracket_vectorField`.

Note that a smoothness assumption for a vector field is written by seeing the vector field as
a function from `M` to its tangent bundle through a coercion, as in:
`MDifferentiableWithinAt I I.tangent (fun y â†¦ (V y : TangentBundle I M)) s x`.
-/

open Set Function Filter
open scoped Topology Manifold ContDiff

noncomputable section

/- We work in the `VectorField` namespace because pullbacks, Lie brackets, and so on, are notions
that make sense in a variety of contexts. We also prefix the notions with `m` to distinguish the
manifold notions from the vector spaces notions. For instance, the Lie bracket of two vector
fields in a manifold is denoted with `VectorField.mlieBracket I V W x`, where `I` is the relevant
model with corners, `V W : Î  (x : M), TangentSpace I x` are the vector fields, and `x : M` is
the basepoint.
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {H : Type*} [TopologicalSpace H] {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {I : ModelWithCorners ğ•œ E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {H' : Type*} [TopologicalSpace H'] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {I' : ModelWithCorners ğ•œ E' H'}
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {H'' : Type*} [TopologicalSpace H''] {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E'']
  {I'' : ModelWithCorners ğ•œ E'' H''}
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  {f : M â†’ M'} {s t : Set M} {x xâ‚€ : M}

instance {n : â„•} [n.AtLeastTwo] [IsManifold I (minSmoothness ğ•œ (ofNat(n))) M] :
    IsManifold I (ofNat(n)) M :=
  IsManifold.of_le (n := minSmoothness ğ•œ n) le_minSmoothness

instance [IsManifold I (minSmoothness ğ•œ 1) M] :
    IsManifold I 1 M :=
  IsManifold.of_le (n := minSmoothness ğ•œ 1) le_minSmoothness

instance [IsManifold I (minSmoothness ğ•œ 3) M] :
    IsManifold I (minSmoothness ğ•œ 2) M :=
  IsManifold.of_le (n := minSmoothness ğ•œ 3) (minSmoothness_monotone (by norm_cast))

instance [IsManifold I (minSmoothness ğ•œ 2) M] :
    IsManifold I (minSmoothness ğ•œ 1) M :=
  IsManifold.of_le (n := minSmoothness ğ•œ 2) (minSmoothness_monotone (by norm_cast))

namespace VectorField

section Pullback

/-! ### Pullback of vector fields in manifolds -/

open ContinuousLinearMap

variable {V W Vâ‚ Wâ‚ : Î  (x : M'), TangentSpace I' x}
variable {c : ğ•œ} {m n : WithTop â„•âˆ} {t : Set M'} {yâ‚€ : M'}

variable (I I') in
/-- The pullback of a vector field under a map between manifolds, within a set `s`. If the
derivative of the map within `s` is not invertible, then pullback is given the junk value zero. -/
def mpullbackWithin (f : M â†’ M') (V : Î  (x : M'), TangentSpace I' x) (s : Set M) (x : M) :
    TangentSpace I x :=
  (mfderivWithin I I' f s x).inverse (V (f x))

variable (I I') in
/-- The pullback of a vector field under a map between manifolds. If the derivative of the map is
not invertible, then pullback is given the junk value zero. -/
def mpullback (f : M â†’ M') (V : Î  (x : M'), TangentSpace I' x) (x : M) :
    TangentSpace I x :=
  (mfderiv I I' f x).inverse (V (f x))

lemma mpullbackWithin_apply :
    mpullbackWithin I I' f V s x = (mfderivWithin I I' f s x).inverse (V (f x)) := rfl

lemma mpullbackWithin_smul_apply :
    mpullbackWithin I I' f (c â€¢ V) s x = c â€¢ mpullbackWithin I I' f V s x := by
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_smul :
    mpullbackWithin I I' f (c â€¢ V) s = c â€¢ mpullbackWithin I I' f V s := by
  ext x
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_add_apply :
    mpullbackWithin I I' f (V + Vâ‚) s x =
      mpullbackWithin I I' f V s x + mpullbackWithin I I' f Vâ‚ s x := by
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_add :
    mpullbackWithin I I' f (V + Vâ‚) s =
      mpullbackWithin I I' f V s + mpullbackWithin I I' f Vâ‚ s := by
  ext x
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_neg_apply :
    mpullbackWithin I I' f (-V) s x = - mpullbackWithin I I' f V s x := by
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_neg :
    mpullbackWithin I I' f (-V) s = - mpullbackWithin I I' f V s := by
  ext x
  simp [mpullbackWithin_apply]

lemma mpullbackWithin_id {V : Î  (x : M), TangentSpace I x} (h : UniqueMDiffWithinAt I s x) :
    mpullbackWithin I I id V s x = V x := by
  simp [mpullbackWithin_apply, mfderivWithin_id h]

lemma mpullback_apply :
    mpullback I I' f V x = (mfderiv I I' f x).inverse (V (f x)) := rfl

lemma mpullback_smul_apply :
    mpullback I I' f (c â€¢ V) x = c â€¢ mpullback I I' f V x := by
  simp [mpullback]

lemma mpullback_smul :
    mpullback I I' f (c â€¢ V) = c â€¢ mpullback I I' f V := by
  ext x
  simp [mpullback_apply]

lemma mpullback_add_apply :
    mpullback I I' f (V + Vâ‚) x = mpullback I I' f V x + mpullback I I' f Vâ‚ x := by
  simp [mpullback_apply]

lemma mpullback_add :
    mpullback I I' f (V + Vâ‚) = mpullback I I' f V + mpullback I I' f Vâ‚ := by
  ext x
  simp [mpullback_apply]

lemma mpullback_neg_apply :
    mpullback I I' f (-V) x = - mpullback I I' f V x := by
  simp [mpullback_apply]

lemma mpullback_neg :
    mpullback I I' f (-V) = - mpullback I I' f V := by
  ext x
  simp [mpullback_apply]

@[simp] lemma mpullbackWithin_univ : mpullbackWithin I I' f V univ = mpullback I I' f V := by
  ext x
  simp [mpullback_apply, mpullbackWithin_apply]

lemma mpullbackWithin_eq_pullbackWithin {f : E â†’ E'} {V : E' â†’ E'} {s : Set E} :
    mpullbackWithin ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f V s = pullbackWithin ğ•œ f V s := by
  ext x
  simp only [mpullbackWithin, mfderivWithin_eq_fderivWithin, pullbackWithin]
  rfl

lemma mpullback_eq_pullback {f : E â†’ E'} {V : E' â†’ E'} :
    mpullback ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f V = pullback ğ•œ f V := by
  simp only [â† mpullbackWithin_univ, â† pullbackWithin_univ, mpullbackWithin_eq_pullbackWithin]

@[simp] lemma mpullback_id {V : Î  (x : M), TangentSpace I x} : mpullback I I id V = V := by
  ext x
  simp [mpullback]

lemma mpullbackWithin_comp_of_left
    {g : M' â†’ M''} {f : M â†’ M'} {V : Î  (x : M''), TangentSpace I'' x} {s : Set M} {t : Set M'}
    {xâ‚€ : M} (hf : MDifferentiableWithinAt I I' f s xâ‚€) (h : Set.MapsTo f s t)
    (hu : UniqueMDiffWithinAt I s xâ‚€) (hg' : (mfderivWithin I' I'' g t (f xâ‚€)).IsInvertible) :
    mpullbackWithin I I'' (g âˆ˜ f) V s xâ‚€ =
      mpullbackWithin I I' f (mpullbackWithin I' I'' g V t) s xâ‚€ := by
  simp only [mpullbackWithin, comp_apply]
  have hg : MDifferentiableWithinAt I' I'' g t (f xâ‚€) :=
    mdifferentiableWithinAt_of_isInvertible_mfderivWithin hg'
  rw [mfderivWithin_comp _ hg hf h hu, Function.comp_apply,
    IsInvertible.inverse_comp_apply_of_left hg']

lemma mpullbackWithin_comp_of_right
    {g : M' â†’ M''} {f : M â†’ M'} {V : Î  (x : M''), TangentSpace I'' x} {s : Set M} {t : Set M'}
    {xâ‚€ : M} (hg : MDifferentiableWithinAt I' I'' g t (f xâ‚€)) (h : Set.MapsTo f s t)
    (hu : UniqueMDiffWithinAt I s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible) :
    mpullbackWithin I I'' (g âˆ˜ f) V s xâ‚€ =
      mpullbackWithin I I' f (mpullbackWithin I' I'' g V t) s xâ‚€ := by
  simp only [mpullbackWithin, comp_apply]
  have hf : MDifferentiableWithinAt I I' f s xâ‚€ :=
    mdifferentiableWithinAt_of_isInvertible_mfderivWithin hf'
  rw [mfderivWithin_comp _ hg hf h hu, IsInvertible.inverse_comp_apply_of_right hf',
    Function.comp_apply]


/-! ### Regularity of pullback of vector fields

In this paragraph, we assume that the model space is complete, to ensure that the set of invertible
linear maps is open and that inversion is a smooth map there. Otherwise, the pullback of vector
fields could behave wildly, even at points where the derivative of the map is invertible.
-/

section MDifferentiability

variable [IsManifold I 2 M] [IsManifold I' 2 M'] [CompleteSpace E]

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. Version within a set at a point. -/
protected lemma _root_.MDifferentiableWithinAt.mpullbackWithin_vectorField_inter
    (hV : MDifferentiableWithinAt I' I'.tangent
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : 2 â‰¤ n) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s âˆ© f â»Â¹' t) xâ‚€ := by
  /- We want to apply the theorem `MDifferentiableWithinAt.clm_apply_of_inCoordinates`,
  stating that applying linear maps to vector fields gives a smooth result when the linear map and
  the vector field are smooth. This theorem is general, we will apply it to
  `bâ‚ = f`, `bâ‚‚ = id`, `v = V âˆ˜ f`, `Ï• = fun x â†¦ (mfderivWithin I I' f s x).inverse` -/
  let bâ‚ := f
  let bâ‚‚ : M â†’ M := id
  let v : Î  (x : M), TangentSpace I' (f x) := V âˆ˜ f
  let Ï• : Î  (x : M), TangentSpace I' (f x) â†’L[ğ•œ] TangentSpace I x :=
    fun x â†¦ (mfderivWithin I I' f s x).inverse
  have hv : MDifferentiableWithinAt I I'.tangent
      (fun x â†¦ (v x : TangentBundle I' M')) (s âˆ© f â»Â¹' t) xâ‚€ := by
    apply hV.comp xâ‚€ ((hf.mdifferentiableWithinAt (one_le_two.trans hmn)).mono inter_subset_left)
    exact MapsTo.mono_left (mapsTo_preimage _ _) inter_subset_right
  /- The only nontrivial fact, from which the conclusion follows, is
  that `Ï•` depends smoothly on `x`. -/
  suffices hÏ• : MDifferentiableWithinAt I ğ“˜(ğ•œ, E' â†’L[ğ•œ] E)
      (fun (x : M) â†¦ ContinuousLinearMap.inCoordinates
        E' (TangentSpace I' (M := M')) E (TangentSpace I (M := M))
        (bâ‚ xâ‚€) (bâ‚ x) (bâ‚‚ xâ‚€) (bâ‚‚ x) (Ï• x)) s xâ‚€ from
    MDifferentiableWithinAt.clm_apply_of_inCoordinates (hÏ•.mono inter_subset_left)
      hv mdifferentiableWithinAt_id
  /- To prove that `Ï•` depends smoothly on `x`, we use that the derivative depends smoothly on `x`
  (this is `ContMDiffWithinAt.mfderivWithin_const`), and that taking the inverse is a smooth
  operation at an invertible map. -/
  -- the derivative in coordinates depends smoothly on the point
  have : MDifferentiableWithinAt I ğ“˜(ğ•œ, E â†’L[ğ•œ] E')
      (fun (x : M) â†¦ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        xâ‚€ x (f xâ‚€) (f x) (mfderivWithin I I' f s x)) s xâ‚€ :=
    ((hf.of_le hmn).mfderivWithin_const le_rfl hxâ‚€ hs).mdifferentiableWithinAt le_rfl
  -- therefore, its inverse in coordinates also depends smoothly on the point
  have : MDifferentiableWithinAt I ğ“˜(ğ•œ, E' â†’L[ğ•œ] E)
      (ContinuousLinearMap.inverse âˆ˜ (fun (x : M) â†¦ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        xâ‚€ x (f xâ‚€) (f x) (mfderivWithin I I' f s x))) s xâ‚€ := by
    apply MDifferentiableAt.comp_mdifferentiableWithinAt _ _ this
    apply ContMDiffAt.mdifferentiableAt _ le_rfl
    apply ContDiffAt.contMDiffAt
    apply IsInvertible.contDiffAt_map_inverse
    rw [inCoordinates_eq (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)
      (FiberBundle.mem_baseSet_trivializationAt' (f xâ‚€))]
    exact isInvertible_equiv.comp (hf'.comp isInvertible_equiv)
  -- the inverse in coordinates coincides with the in-coordinate version of the inverse,
  -- therefore the previous point gives the conclusion
  apply this.congr_of_eventuallyEq_of_mem _ hxâ‚€
  have A : (trivializationAt E (TangentSpace I) xâ‚€).baseSet âˆˆ ğ“[s] xâ‚€ := by
    apply nhdsWithin_le_nhds
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  have B : f â»Â¹' (trivializationAt E' (TangentSpace I') (f xâ‚€)).baseSet âˆˆ ğ“[s] xâ‚€ := by
    apply hf.continuousWithinAt.preimage_mem_nhdsWithin
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  filter_upwards [A, B] with x hx h'x
  simp only [Function.comp_apply]
  rw [inCoordinates_eq hx h'x, inCoordinates_eq h'x (by exact hx)]
  simp only [inverse_equiv_comp, inverse_comp_equiv, ContinuousLinearEquiv.symm_symm, Ï•]
  rfl

lemma _root_.MDifferentiableWithinAt.mpullbackWithin_vectorField_inter_of_eq
    (hV : MDifferentiableWithinAt I' I'.tangent
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : 2 â‰¤ n) (h : yâ‚€ = f xâ‚€) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s âˆ© fâ»Â¹' t) xâ‚€ := by
  subst h
  exact hV.mpullbackWithin_vectorField_inter hf hf' hxâ‚€ hs hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. Version on a set. -/
protected lemma _root_.MDifferentiableOn.mpullbackWithin_vectorField_inter
    (hV : MDifferentiableOn I' I'.tangent (fun (y : M') â†¦ (V y : TangentBundle I' M')) t)
    (hf : ContMDiffOn I I' n f s) (hf' : âˆ€ x âˆˆ s âˆ© f â»Â¹' t, (mfderivWithin I I' f s x).IsInvertible)
    (hs : UniqueMDiffOn I s) (hmn : 2 â‰¤ n) :
    MDifferentiableOn I I.tangent
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s âˆ© f â»Â¹' t) :=
  fun _ hxâ‚€ â†¦ MDifferentiableWithinAt.mpullbackWithin_vectorField_inter
    (hV _ hxâ‚€.2) (hf _ hxâ‚€.1) (hf' _ hxâ‚€) hxâ‚€.1 hs hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. Version within a set at a point, but with full pullback. -/
protected lemma _root_.MDifferentiableWithinAt.mpullback_vectorField_preimage
    (hV : MDifferentiableWithinAt I' I'.tangent
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : 2 â‰¤ n) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) (f â»Â¹' t) xâ‚€ := by
  simp only [â† contMDiffWithinAt_univ, â† mfderivWithin_univ, â† mpullbackWithin_univ] at hV hf hf' âŠ¢
  simpa using hV.mpullbackWithin_vectorField_inter hf hf' (mem_univ _) uniqueMDiffOn_univ hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. Version within a set at a point, but with full pullback. -/
protected lemma _root_.MDifferentiableWithinAt.mpullback_vectorField_preimage_of_eq
    (hV : MDifferentiableWithinAt I' I'.tangent (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : 2 â‰¤ n)
    (hyâ‚€ : yâ‚€ = f xâ‚€) :
    MDifferentiableWithinAt I I.tangent
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) (f â»Â¹' t) xâ‚€ := by
  subst hyâ‚€
  exact hV.mpullback_vectorField_preimage hf hf' hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. Version on a set, but with full pullback -/
protected lemma _root_.MDifferentiableOn.mpullback_vectorField_preimage
    (hV : MDifferentiableOn I' I'.tangent (fun (y : M') â†¦ (V y : TangentBundle I' M')) t)
    (hf : ContMDiff I I' n f) (hf' : âˆ€ x âˆˆ f â»Â¹' t, (mfderiv I I' f x).IsInvertible)
    (hmn : 2 â‰¤ n) :
    MDifferentiableOn I I.tangent
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) (f â»Â¹' t) :=
  fun xâ‚€ hxâ‚€ â†¦ MDifferentiableWithinAt.mpullback_vectorField_preimage
    (hV _ hxâ‚€) (hf xâ‚€) (hf' _ hxâ‚€) hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. Version at a point. -/
protected lemma _root_.MDifferentiableAt.mpullback_vectorField
    (hV : MDifferentiableAt I' I'.tangent (fun (y : M') â†¦ (V y : TangentBundle I' M')) (f xâ‚€))
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : 2 â‰¤ n) :
    MDifferentiableAt I I.tangent
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) xâ‚€ := by
  simpa using MDifferentiableWithinAt.mpullback_vectorField_preimage hV hf hf' hmn

/-- The pullback of a differentiable vector field by a `C^n` function with `2 â‰¤ n` is
differentiable. -/
protected lemma _root_.MDifferentiable.mpullback_vectorField
    (hV : MDifferentiable I' I'.tangent (fun (y : M') â†¦ (V y : TangentBundle I' M')))
    (hf : ContMDiff I I' n f) (hf' : âˆ€ x, (mfderiv I I' f x).IsInvertible) (hmn : 2 â‰¤ n) :
    MDifferentiable I I.tangent (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) :=
  fun x â†¦ MDifferentiableAt.mpullback_vectorField (hV (f x)) (hf x) (hf' x) hmn

end MDifferentiability


section ContMDiff

variable [IsManifold I n M] [IsManifold I' n M'] [CompleteSpace E]
  -- If `1 < n` then `IsManifold.of_le` shows the following assumptions are redundant.
  -- We include them since they are necessary to make the statement.
  [IsManifold I 1 M] [IsManifold I' 1 M']

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
`m + 1 â‰¤ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_inter
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s âˆ© f â»Â¹' t) xâ‚€ := by
  /- We want to apply the theorem `ContMDiffWithinAt.clm_apply_of_inCoordinates`, stating
  that applying linear maps to vector fields gives a smooth result when the linear map and the
  vector field are smooth. This theorem is general, we will apply it to
  `bâ‚ = f`, `bâ‚‚ = id`, `v = V âˆ˜ f`, `Ï• = fun x â†¦ (mfderivWithin I I' f s x).inverse` -/
  let bâ‚ := f
  let bâ‚‚ : M â†’ M := id
  let v : Î  (x : M), TangentSpace I' (f x) := V âˆ˜ f
  let Ï• : Î  (x : M), TangentSpace I' (f x) â†’L[ğ•œ] TangentSpace I x :=
    fun x â†¦ (mfderivWithin I I' f s x).inverse
  have hv : ContMDiffWithinAt I I'.tangent m
      (fun x â†¦ (v x : TangentBundle I' M')) (s âˆ© f â»Â¹' t) xâ‚€ := by
    apply hV.comp xâ‚€ ((hf.of_le (le_trans (le_self_add) hmn)).mono inter_subset_left)
    exact MapsTo.mono_left (mapsTo_preimage _ _) inter_subset_right
  /- The only nontrivial fact, from which the conclusion follows, is
  that `Ï•` depends smoothly on `x`. -/
  suffices hÏ• : ContMDiffWithinAt I ğ“˜(ğ•œ, E' â†’L[ğ•œ] E) m
      (fun (x : M) â†¦ ContinuousLinearMap.inCoordinates
        E' (TangentSpace I' (M := M')) E (TangentSpace I (M := M))
        (bâ‚ xâ‚€) (bâ‚ x) (bâ‚‚ xâ‚€) (bâ‚‚ x) (Ï• x)) s xâ‚€ from
    ContMDiffWithinAt.clm_apply_of_inCoordinates (hÏ•.mono inter_subset_left) hv contMDiffWithinAt_id
  /- To prove that `Ï•` depends smoothly on `x`, we use that the derivative depends smoothly on `x`
  (this is `ContMDiffWithinAt.mfderivWithin_const`), and that taking the inverse is a smooth
  operation at an invertible map. -/
  -- the derivative in coordinates depends smoothly on the point
  have : ContMDiffWithinAt I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (fun (x : M) â†¦ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        xâ‚€ x (f xâ‚€) (f x) (mfderivWithin I I' f s x)) s xâ‚€ :=
    hf.mfderivWithin_const hmn hxâ‚€ hs
  -- therefore, its inverse in coordinates also depends smoothly on the point
  have : ContMDiffWithinAt I ğ“˜(ğ•œ, E' â†’L[ğ•œ] E) m
      (ContinuousLinearMap.inverse âˆ˜ (fun (x : M) â†¦ ContinuousLinearMap.inCoordinates
        E (TangentSpace I (M := M)) E' (TangentSpace I' (M := M'))
        xâ‚€ x (f xâ‚€) (f x) (mfderivWithin I I' f s x))) s xâ‚€ := by
    apply ContMDiffAt.comp_contMDiffWithinAt _ _ this
    apply ContDiffAt.contMDiffAt
    apply IsInvertible.contDiffAt_map_inverse
    rw [inCoordinates_eq (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)
      (FiberBundle.mem_baseSet_trivializationAt' (f xâ‚€))]
    exact isInvertible_equiv.comp (hf'.comp isInvertible_equiv)
  -- the inverse in coordinates coincides with the in-coordinate version of the inverse,
  -- therefore the previous point gives the conclusion
  apply this.congr_of_eventuallyEq_of_mem _ hxâ‚€
  have A : (trivializationAt E (TangentSpace I) xâ‚€).baseSet âˆˆ ğ“[s] xâ‚€ := by
    apply nhdsWithin_le_nhds
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  have B : f â»Â¹' (trivializationAt E' (TangentSpace I') (f xâ‚€)).baseSet âˆˆ ğ“[s] xâ‚€ := by
    apply hf.continuousWithinAt.preimage_mem_nhdsWithin
    apply (trivializationAt _ _ _).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' _
  filter_upwards [A, B] with x hx h'x
  simp only [Function.comp_apply]
  rw [inCoordinates_eq hx h'x, inCoordinates_eq h'x (by exact hx)]
  simp only [inverse_equiv_comp, inverse_comp_equiv, ContinuousLinearEquiv.symm_symm, Ï•]
  rfl

lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_inter_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) (h : f xâ‚€ = yâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s âˆ© fâ»Â¹' t) xâ‚€ := by
  subst h
  exact ContMDiffWithinAt.mpullbackWithin_vectorField_inter hV hf hf' hxâ‚€ hs hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) s xâ‚€ := by
  apply (ContMDiffWithinAt.mpullbackWithin_vectorField_inter
    hV hf hf' hxâ‚€ hs hmn).mono_of_mem_nhdsWithin
  exact Filter.inter_mem self_mem_nhdsWithin hst

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€)
    (hyâ‚€ : f xâ‚€ = yâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) s xâ‚€ := by
  subst hyâ‚€
  exact ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem hV hf hf' hxâ‚€ hs hmn hst

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) (hst : MapsTo f s t) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) s xâ‚€ :=
  ContMDiffWithinAt.mpullbackWithin_vectorField_of_mem hV hf hf' hxâ‚€ hs hmn
    hst.preimage_mem_nhdsWithin

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hf' : (mfderivWithin I I' f s xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) (hst : MapsTo f s t) (h : f xâ‚€ = yâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) s xâ‚€ := by
  subst h
  exact ContMDiffWithinAt.mpullbackWithin_vectorField hV hf hf' hxâ‚€ hs hmn hst

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point, with a set used for the pullback possibly larger. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField' {u : Set M}
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffWithinAt I I' n f u xâ‚€) (hf' : (mfderivWithin I I' f u xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n)
    (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) (hu : s âŠ† u) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V u y : TangentBundle I M)) s xâ‚€ := by
  have hn : (1 : â„•) â‰¤ n := le_trans le_add_self hmn
  have hh : (mfderivWithin I I' f s xâ‚€).IsInvertible := by
    convert hf' using 1
    exact (hf.mdifferentiableWithinAt hn).mfderivWithin_mono (hs _ hxâ‚€) hu
  apply (hV.mpullbackWithin_vectorField_of_mem (hf.mono hu) hh hxâ‚€ hs hmn
    hst).congr_of_eventuallyEq_of_mem _ hxâ‚€
  have Y := (contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin (by simp)).1 (hf.of_le hn)
  simp_rw [insert_eq_of_mem (hu hxâ‚€)] at Y
  filter_upwards [self_mem_nhdsWithin, nhdsWithin_mono _ hu Y] with y hy h'y
  simp only [mpullbackWithin, Bundle.TotalSpace.mk_inj]
  rw [MDifferentiableWithinAt.mfderivWithin_mono (h'y.mdifferentiableWithinAt le_rfl) (hs _ hy) hu]

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point, with a set used for the pullback possibly larger. -/
protected lemma _root_.ContMDiffWithinAt.mpullbackWithin_vectorField_of_eq' {u : Set M}
    (hV : ContMDiffWithinAt I' I'.tangent m
      (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffWithinAt I I' n f u xâ‚€) (hf' : (mfderivWithin I I' f u xâ‚€).IsInvertible)
    (hxâ‚€ : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€)
    (hu : s âŠ† u) (hyâ‚€ : f xâ‚€ = yâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V u y : TangentBundle I M)) s xâ‚€ := by
  subst hyâ‚€
  exact ContMDiffWithinAt.mpullbackWithin_vectorField' hV hf hf' hxâ‚€ hs hmn hst hu

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version on a set. -/
protected lemma _root_.ContMDiffOn.mpullbackWithin_vectorField_inter
    (hV : ContMDiffOn I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) t)
    (hf : ContMDiffOn I I' n f s) (hf' : âˆ€ x âˆˆ s âˆ© f â»Â¹' t, (mfderivWithin I I' f s x).IsInvertible)
    (hs : UniqueMDiffOn I s) (hmn : m + 1 â‰¤ n) :
    ContMDiffOn I I.tangent m
      (fun (y : M) â†¦ (mpullbackWithin I I' f V s y : TangentBundle I M)) (s âˆ© f â»Â¹' t) :=
  fun _ hxâ‚€ â†¦ ContMDiffWithinAt.mpullbackWithin_vectorField_inter
    (hV _ hxâ‚€.2) (hf _ hxâ‚€.1) (hf' _ hxâ‚€) hxâ‚€.1 hs hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_preimage
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : m + 1 â‰¤ n) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) (f â»Â¹' t) xâ‚€ := by
  simp only [â† contMDiffWithinAt_univ, â† mfderivWithin_univ, â† mpullbackWithin_univ] at hV hf hf' âŠ¢
  simpa using hV.mpullbackWithin_vectorField_inter hf hf' (mem_univ _) uniqueMDiffOn_univ hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_preimage_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : m + 1 â‰¤ n)
    (hyâ‚€ : yâ‚€ = f xâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) (f â»Â¹' t) xâ‚€ := by
  subst hyâ‚€
  exact ContMDiffWithinAt.mpullback_vectorField_preimage hV hf hf' hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) t (f xâ‚€))
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : m + 1 â‰¤ n)
    (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) s xâ‚€ :=
  (ContMDiffWithinAt.mpullback_vectorField_preimage hV hf hf' hmn).mono_of_mem_nhdsWithin hst

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version within a set at a point, but with full pullback. -/
protected lemma _root_.ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin_of_eq
    (hV : ContMDiffWithinAt I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) t yâ‚€)
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : m + 1 â‰¤ n)
    (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) (hyâ‚€ : yâ‚€ = f xâ‚€) :
    ContMDiffWithinAt I I.tangent m
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) s xâ‚€ := by
  subst hyâ‚€
  exact ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin hV hf hf' hmn hst

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version on a set, but with full pullback -/
protected lemma _root_.ContMDiffOn.mpullback_vectorField_preimage
    (hV : ContMDiffOn I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) t)
    (hf : ContMDiff I I' n f) (hf' : âˆ€ x âˆˆ f â»Â¹' t, (mfderiv I I' f x).IsInvertible)
    (hmn : m + 1 â‰¤ n) :
    ContMDiffOn I I.tangent m
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) (f â»Â¹' t) :=
  fun xâ‚€ hxâ‚€ â†¦ ContMDiffWithinAt.mpullback_vectorField_preimage (hV _ hxâ‚€) (hf xâ‚€) (hf' _ hxâ‚€) hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`.
Version at a point. -/
protected lemma _root_.ContMDiffAt.mpullback_vectorField_preimage
    (hV : ContMDiffAt I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')) (f xâ‚€))
    (hf : ContMDiffAt I I' n f xâ‚€) (hf' : (mfderiv I I' f xâ‚€).IsInvertible) (hmn : m + 1 â‰¤ n) :
    ContMDiffAt I I.tangent m
      (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) xâ‚€ := by
  simp only [â† contMDiffWithinAt_univ] at hV hf hf' âŠ¢
  simpa using ContMDiffWithinAt.mpullback_vectorField_preimage hV hf hf' hmn

/-- The pullback of a `C^m` vector field by a `C^n` function with invertible derivative and
with `m + 1 â‰¤ n` is `C^m`. -/
protected lemma _root_.ContMDiff.mpullback_vectorField
    (hV : ContMDiff I' I'.tangent m (fun (y : M') â†¦ (V y : TangentBundle I' M')))
    (hf : ContMDiff I I' n f) (hf' : âˆ€ x, (mfderiv I I' f x).IsInvertible) (hmn : m + 1 â‰¤ n) :
    ContMDiff I I.tangent m (fun (y : M) â†¦ (mpullback I I' f V y : TangentBundle I M)) :=
  fun x â†¦ ContMDiffAt.mpullback_vectorField_preimage (hV (f x)) (hf x) (hf' x) hmn

lemma contMDiffWithinAt_mpullbackWithin_extChartAt_symm
    {V : Î  (x : M), TangentSpace I x}
    (hV : ContMDiffWithinAt I I.tangent m (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffOn I s) (hx : x âˆˆ s) (hmn : m + 1 â‰¤ n) :
    ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent m
      (fun y â†¦ (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm V (range I) y :
        TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x x) :=
  ContMDiffWithinAt.mpullbackWithin_vectorField_of_eq' hV
    (contMDiffWithinAt_extChartAt_symm_range (n := n) _ (mem_extChartAt_target x))
    (isInvertible_mfderivWithin_extChartAt_symm (mem_extChartAt_target x))
    (by simp [hx]) (UniqueMDiffOn.uniqueMDiffOn_target_inter hs x) hmn
    ((mapsTo_preimage _ _).mono_left inter_subset_right).preimage_mem_nhdsWithin
    (Subset.trans inter_subset_left (extChartAt_target_subset_range x)) (extChartAt_to_inv x)

lemma eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm
    {V : Î  (x : M), TangentSpace I x}
    (hV : ContMDiffWithinAt I I.tangent m (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffOn I s) (hx : x âˆˆ s) (hmn : m + 1 â‰¤ n) (hm : m â‰  âˆ) :
    âˆ€á¶  y in ğ“[s] x, ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent m
    (fun z â†¦ (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm V (range I) z :
      TangentBundle ğ“˜(ğ•œ, E) E))
    ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x y) := by
  have T := nhdsWithin_mono _ (subset_insert _ _)
    ((contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin hm).1
      (contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx hmn))
  have A := (continuousAt_extChartAt (I := I) x).continuousWithinAt.preimage_mem_nhdsWithin'' T rfl
  apply (nhdsWithin_le_iff.2 _) A
  filter_upwards [self_mem_nhdsWithin, nhdsWithin_le_nhds (extChartAt_source_mem_nhds (I := I) x)]
    with y hy h'y
  simp only [mfld_simps] at hy h'y
  simp [hy, h'y]

omit [CompleteSpace E] in
lemma eventuallyEq_mpullback_mpullbackWithin_extChartAt (V : Î  (x : M), TangentSpace I x) :
    V =á¶ [ğ“[s] x] mpullback I ğ“˜(ğ•œ, E) (extChartAt I x)
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm V (range I)) := by
  apply nhdsWithin_le_nhds
  filter_upwards [extChartAt_source_mem_nhds (I := I) x] with y hy
  have A : (extChartAt I x).symm (extChartAt I x y) = y := (extChartAt I x).left_inv hy
  rw [mpullback_apply, mpullbackWithin_apply,
    â† (isInvertible_mfderiv_extChartAt hy).inverse_comp_apply_of_right,
    mfderivWithin_extChartAt_symm_comp_mfderiv_extChartAt' hy, A]
  simp only [ContinuousLinearMap.inverse_id, ContinuousLinearMap.coe_id', id_eq]

end ContMDiff

end Pullback

section LieBracket

/-! ### The Lie bracket of vector fields in manifolds -/

variable {V W Vâ‚ Wâ‚ : Î  (x : M), TangentSpace I x}

variable (I I') in
/-- The Lie bracket of two vector fields in a manifold, within a set. -/
def mlieBracketWithin (V W : Î  (x : M), TangentSpace I x) (s : Set M) (xâ‚€ : M) :
    TangentSpace I xâ‚€ :=
  mpullback I ğ“˜(ğ•œ, E) (extChartAt I xâ‚€)
    (lieBracketWithin ğ•œ
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm V (range I))
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm W (range I))
      ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I)) xâ‚€

variable (I I') in
/-- The Lie bracket of two vector fields in a manifold. -/
def mlieBracket (V W : Î  (x : M), TangentSpace I x) (xâ‚€ : M) : TangentSpace I xâ‚€ :=
  mlieBracketWithin I V W univ xâ‚€

lemma mlieBracketWithin_def :
    mlieBracketWithin I V W s = fun xâ‚€ â†¦
    mpullback I ğ“˜(ğ•œ, E) (extChartAt I xâ‚€)
    (lieBracketWithin ğ•œ
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm V (range I))
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm W (range I))
      ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I)) xâ‚€ := rfl

lemma mlieBracketWithin_apply :
    mlieBracketWithin I V W s xâ‚€ =
    (mfderiv I ğ“˜(ğ•œ, E) (extChartAt I xâ‚€) xâ‚€).inverse
    ((lieBracketWithin ğ•œ
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm V (range I))
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm W (range I))
      ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I)) ((extChartAt I xâ‚€ xâ‚€))) := rfl

lemma mlieBracketWithin_eq_lieBracketWithin {V W : Î  (x : E), TangentSpace ğ“˜(ğ•œ, E) x} {s : Set E} :
    mlieBracketWithin ğ“˜(ğ•œ, E) V W s = lieBracketWithin ğ•œ V W s := by
  ext x
  simp [mlieBracketWithin_apply]

/- Copy of the `lieBracket` API to manifolds -/

@[simp] lemma mlieBracketWithin_univ :
    mlieBracketWithin I V W univ = mlieBracket I V W := rfl

lemma mlieBracketWithin_eq_zero_of_eq_zero (hV : V x = 0) (hW : W x = 0) :
    mlieBracketWithin I V W s x = 0 := by
  simp only [mlieBracketWithin, mpullback_apply, comp_apply]
  rw [lieBracketWithin_eq_zero_of_eq_zero]
  Â· simp
  Â· simp only [mpullbackWithin_apply]
    have : (extChartAt I x).symm ((extChartAt I x) x) = x := by simp
    rw [this, hV]
    simp
  Â· simp only [mpullbackWithin_apply]
    have : (extChartAt I x).symm ((extChartAt I x) x) = x := by simp
    rw [this, hW]
    simp

lemma mlieBracketWithin_swap_apply :
    mlieBracketWithin I V W s x = - mlieBracketWithin I W V s x := by
  rw [mlieBracketWithin, lieBracketWithin_swap, mpullback_neg]
  rfl

lemma mlieBracketWithin_swap :
    mlieBracketWithin I V W s = - mlieBracketWithin I W V s := by
  ext x
  exact mlieBracketWithin_swap_apply

lemma mlieBracket_swap_apply : mlieBracket I V W x = - mlieBracket I W V x :=
  mlieBracketWithin_swap_apply

lemma mlieBracket_swap : mlieBracket I V W = - mlieBracket I W V :=
  mlieBracketWithin_swap

@[simp] lemma mlieBracketWithin_self : mlieBracketWithin I V V = 0 := by
  ext x; simp [mlieBracketWithin, mpullback]

@[simp] lemma mlieBracket_self : mlieBracket I V V = 0 := by
  ext x; simp_rw [mlieBracket, mlieBracketWithin_self, Pi.zero_apply]

/-- Variant of `mlieBracketWithin_congr_set` where one requires the sets to coincide only in
the complement of a point. -/
theorem mlieBracketWithin_congr_set' (y : M) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x := by
  simp only [mlieBracketWithin_apply]
  congr 1
  have : T1Space M := I.t1Space M
  suffices A : ((extChartAt I x).symm â»Â¹' s âˆ© range I : Set E)
    =á¶ [ğ“[{(extChartAt I x) x}á¶œ] (extChartAt I x x)]
      ((extChartAt I x).symm â»Â¹' t âˆ© range I : Set E) by
    apply lieBracketWithin_congr_set' _ A
  obtain âŸ¨u, u_mem, huâŸ© : âˆƒ u âˆˆ ğ“ x, u âˆ© {x}á¶œ âŠ† {y | (y âˆˆ s) = (y âˆˆ t)} :=
    mem_nhdsWithin_iff_exists_mem_nhds_inter.1 (nhdsWithin_compl_singleton_le x y h)
  rw [â† extChartAt_to_inv (I := I) x] at u_mem
  have B : (extChartAt I x).target âˆª (range I)á¶œ âˆˆ ğ“ (extChartAt I x x) :=
    extChartAt_target_union_compl_range_mem_nhds_of_mem (mem_extChartAt_target x)
  apply mem_nhdsWithin_iff_exists_mem_nhds_inter.2
    âŸ¨_, Filter.inter_mem ((continuousAt_extChartAt_symm x).preimage_mem_nhds u_mem) B, ?_âŸ©
  rintro z âŸ¨hz, h'zâŸ©
  simp only [eq_iff_iff, mem_setOf_eq]
  change z âˆˆ (extChartAt I x).symm â»Â¹' s âˆ© range I â†” z âˆˆ (extChartAt I x).symm â»Â¹' t âˆ© range I
  by_cases hIz : z âˆˆ range I
  Â· simp [-extChartAt, hIz] at hz âŠ¢
    rw [â† eq_iff_iff]
    apply hu âŸ¨hz.1, ?_âŸ©
    simp only [mem_compl_iff, mem_singleton_iff, ne_comm, ne_eq] at h'z âŠ¢
    rw [(extChartAt I x).eq_symm_apply (by simp) hz.2]
    exact Ne.symm h'z
  Â· simp [hIz]

theorem mlieBracketWithin_congr_set (h : s =á¶ [ğ“ x] t) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x :=
  mlieBracketWithin_congr_set' x <| h.filter_mono inf_le_left

theorem mlieBracketWithin_inter (ht : t âˆˆ ğ“ x) :
    mlieBracketWithin I V W (s âˆ© t) x = mlieBracketWithin I V W s x := by
  apply mlieBracketWithin_congr_set
  filter_upwards [ht] with y hy
  change (y âˆˆ s âˆ© t) = (y âˆˆ s)
  aesop

theorem mlieBracketWithin_of_mem_nhds (h : s âˆˆ ğ“ x) :
    mlieBracketWithin I V W s x = mlieBracket I V W x := by
  rw [â† mlieBracketWithin_univ, â† univ_inter s, mlieBracketWithin_inter h]

theorem mlieBracketWithin_of_isOpen (hs : IsOpen s) (hx : x âˆˆ s) :
    mlieBracketWithin I V W s x = mlieBracket I V W x :=
  mlieBracketWithin_of_mem_nhds (hs.mem_nhds hx)

/-- Variant of `mlieBracketWithin_eventually_congr_set` where one requires the sets to coincide only
in the complement of a point. -/
theorem mlieBracketWithin_eventually_congr_set' (y : M) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    mlieBracketWithin I V W s =á¶ [ğ“ x] mlieBracketWithin I V W t :=
  (eventually_nhds_nhdsWithin.2 h).mono fun _ => mlieBracketWithin_congr_set' y

theorem mlieBracketWithin_eventually_congr_set (h : s =á¶ [ğ“ x] t) :
    mlieBracketWithin I V W s =á¶ [ğ“ x] mlieBracketWithin I V W t :=
  mlieBracketWithin_eventually_congr_set' x <| h.filter_mono inf_le_left

theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_eq
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hxV : Vâ‚ x = V x) (hW : Wâ‚ =á¶ [ğ“[s] x] W) (hxW : Wâ‚ x = W x) :
    mlieBracketWithin I Vâ‚ Wâ‚ s x = mlieBracketWithin I V W s x := by
  simp only [mlieBracketWithin_apply]
  congr 1
  let I1 : NormedAddCommGroup (TangentSpace ğ“˜(ğ•œ, E) (extChartAt I x x)) :=
    inferInstanceAs (NormedAddCommGroup E)
  let _I2 : NormedSpace ğ•œ (TangentSpace ğ“˜(ğ•œ, E) (extChartAt I x x)) :=
    inferInstanceAs (NormedSpace ğ•œ E)
  apply Filter.EventuallyEq.lieBracketWithin_vectorField_eq
  Â· apply nhdsWithin_mono _ inter_subset_left
    filter_upwards [(continuousAt_extChartAt_symm x).continuousWithinAt.preimage_mem_nhdsWithin''
      hV (by simp)] with y hy
    simp only [mpullbackWithin_apply]
    congr 1
  Â· simp only [mpullbackWithin_apply]
    congr 1
    convert hxV <;> exact extChartAt_to_inv x
  Â· apply nhdsWithin_mono _ inter_subset_left
    filter_upwards [(continuousAt_extChartAt_symm x).continuousWithinAt.preimage_mem_nhdsWithin''
      hW (by simp)] with y hy
    simp only [mpullbackWithin_apply]
    congr 1
  Â· simp only [mpullbackWithin_apply]
    congr 1
    convert hxW <;> exact extChartAt_to_inv x

theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hW : Wâ‚ =á¶ [ğ“[s] x] W) (hx : x âˆˆ s) :
    mlieBracketWithin I Vâ‚ Wâ‚ s x = mlieBracketWithin I V W s x :=
  hV.mlieBracketWithin_vectorField_eq (mem_of_mem_nhdsWithin hx hV :)
    hW (mem_of_mem_nhdsWithin hx hW :)

/-- If vector fields coincide on a neighborhood of a point within a set, then the Lie brackets
also coincide on a neighborhood of this point within this set. Version where one considers the Lie
bracket within a subset. -/
theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField'
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hW : Wâ‚ =á¶ [ğ“[s] x] W) (ht : t âŠ† s) :
    mlieBracketWithin I Vâ‚ Wâ‚ t =á¶ [ğ“[s] x] mlieBracketWithin I V W t := by
  filter_upwards [hV, hW, eventually_eventually_nhdsWithin.2 hV,
    eventually_eventually_nhdsWithin.2 hW] with y hVy hWy hVy' hWy'
  apply Filter.EventuallyEq.mlieBracketWithin_vectorField_eq
  Â· apply nhdsWithin_mono _ ht
    exact hVy'
  Â· exact hVy
  Â· apply nhdsWithin_mono _ ht
    exact hWy'
  Â· exact hWy

protected theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hW : Wâ‚ =á¶ [ğ“[s] x] W) :
    mlieBracketWithin I Vâ‚ Wâ‚ s =á¶ [ğ“[s] x] mlieBracketWithin I V W s :=
  hV.mlieBracketWithin_vectorField' hW Subset.rfl

protected theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_of_insert
    (hV : Vâ‚ =á¶ [ğ“[insert x s] x] V) (hW : Wâ‚ =á¶ [ğ“[insert x s] x] W) :
    mlieBracketWithin I Vâ‚ Wâ‚ s x = mlieBracketWithin I V W s x := by
  apply mem_of_mem_nhdsWithin (mem_insert x s)
    (hV.mlieBracketWithin_vectorField' hW (subset_insert x s))

theorem _root_.Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_nhds
    (hV : Vâ‚ =á¶ [ğ“ x] V) (hW : Wâ‚ =á¶ [ğ“ x] W) :
    mlieBracketWithin I Vâ‚ Wâ‚ s x = mlieBracketWithin I V W s x :=
  (hV.filter_mono nhdsWithin_le_nhds).mlieBracketWithin_vectorField_eq hV.self_of_nhds
    (hW.filter_mono nhdsWithin_le_nhds) hW.self_of_nhds

theorem mlieBracketWithin_congr
    (hV : EqOn Vâ‚ V s) (hVx : Vâ‚ x = V x) (hW : EqOn Wâ‚ W s) (hWx : Wâ‚ x = W x) :
    mlieBracketWithin I Vâ‚ Wâ‚ s x = mlieBracketWithin I V W s x :=
  (hV.eventuallyEq.filter_mono inf_le_right).mlieBracketWithin_vectorField_eq hVx
    (hW.eventuallyEq.filter_mono inf_le_right) hWx

/-- Version of `mlieBracketWithin_congr` in which one assumes that the point belongs to the
given set. -/
theorem mlieBracketWithin_congr' (hV : EqOn Vâ‚ V s) (hW : EqOn Wâ‚ W s) (hx : x âˆˆ s) :
    mlieBracketWithin I Vâ‚ Wâ‚ s x = mlieBracketWithin I V W s x :=
  mlieBracketWithin_congr hV (hV hx) hW (hW hx)

theorem _root_.Filter.EventuallyEq.mlieBracket_vectorField_eq
    (hV : Vâ‚ =á¶ [ğ“ x] V) (hW : Wâ‚ =á¶ [ğ“ x] W) :
    mlieBracket I Vâ‚ Wâ‚ x = mlieBracket I V W x := by
  rw [â† mlieBracketWithin_univ, â† mlieBracketWithin_univ,
    hV.mlieBracketWithin_vectorField_eq_nhds hW]

protected theorem _root_.Filter.EventuallyEq.mlieBracket_vectorField
    (hV : Vâ‚ =á¶ [ğ“ x] V) (hW : Wâ‚ =á¶ [ğ“ x] W) : mlieBracket I Vâ‚ Wâ‚ =á¶ [ğ“ x] mlieBracket I V W := by
  filter_upwards [hV.eventuallyEq_nhds, hW.eventuallyEq_nhds] with y hVy hWy
  exact hVy.mlieBracket_vectorField_eq hWy

section

variable {c : ğ•œ}
variable [IsManifold I 2 M] [CompleteSpace E]

lemma _root_.MDifferentiableWithinAt.differentiableWithinAt_mpullbackWithin_vectorField
    (hV : MDifferentiableWithinAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) s x) :
    DifferentiableWithinAt ğ•œ (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm V (range I))
    ((extChartAt I x).symm â»Â¹' s âˆ© range I) (extChartAt I x x) := by
  apply MDifferentiableWithinAt.differentiableWithinAt
  have := MDifferentiableWithinAt.mpullbackWithin_vectorField_inter_of_eq hV
    (contMDiffWithinAt_extChartAt_symm_range x (mem_extChartAt_target x))
    (isInvertible_mfderivWithin_extChartAt_symm (mem_extChartAt_target x)) (mem_range_self _)
    I.uniqueMDiffOn le_rfl (extChartAt_to_inv x).symm
  rw [inter_comm]
  exact ((contMDiff_snd_tangentBundle_modelSpace E ğ“˜(ğ•œ, E)).contMDiffAt.mdifferentiableAt
    le_rfl).comp_mdifferentiableWithinAt _ this

lemma mlieBracketWithin_smul_left
    (hV : MDifferentiableWithinAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I (c â€¢ V) W s x = c â€¢ mlieBracketWithin I V W s x := by
  simp only [mlieBracketWithin_apply]
  rw [â† ContinuousLinearMap.map_smul, mpullbackWithin_smul, lieBracketWithin_smul_left]
  Â· exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  Â· exact uniqueMDiffWithinAt_iff_inter_range.1 hs

lemma mlieBracket_smul_left
    (hV : MDifferentiableAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) x) :
    mlieBracket I (c â€¢ V) W x = c â€¢ mlieBracket I V W x := by
  simp only [â† mlieBracketWithin_univ, â† contMDiffWithinAt_univ] at hV âŠ¢
  exact mlieBracketWithin_smul_left hV (uniqueMDiffWithinAt_univ _)

lemma mlieBracketWithin_smul_right
    (hW : MDifferentiableWithinAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I V (c â€¢ W) s x = c â€¢ mlieBracketWithin I V W s x := by
  simp only [mlieBracketWithin_apply]
  rw [â† ContinuousLinearMap.map_smul, mpullbackWithin_smul, lieBracketWithin_smul_right]
  Â· exact hW.differentiableWithinAt_mpullbackWithin_vectorField
  Â· exact uniqueMDiffWithinAt_iff_inter_range.1 hs

lemma mlieBracket_smul_right
    (hW : MDifferentiableAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) x) :
    mlieBracket I V (c â€¢ W) x = c â€¢ mlieBracket I V W x := by
  simp only [â† mlieBracketWithin_univ, â† contMDiffWithinAt_univ] at hW âŠ¢
  exact mlieBracketWithin_smul_right hW (uniqueMDiffWithinAt_univ _)

lemma mlieBracketWithin_add_left
    (hV : MDifferentiableWithinAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hVâ‚ : MDifferentiableWithinAt I I.tangent (fun x â†¦ (Vâ‚ x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I (V + Vâ‚) W s x =
      mlieBracketWithin I V W s x + mlieBracketWithin I Vâ‚ W s x := by
  simp only [mlieBracketWithin_apply]
  rw [â† ContinuousLinearMap.map_add, mpullbackWithin_add, lieBracketWithin_add_left]
  Â· exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  Â· exact hVâ‚.differentiableWithinAt_mpullbackWithin_vectorField
  Â· exact uniqueMDiffWithinAt_iff_inter_range.1 hs

lemma mlieBracket_add_left
    (hV : MDifferentiableAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) x)
    (hVâ‚ : MDifferentiableAt I I.tangent (fun x â†¦ (Vâ‚ x : TangentBundle I M)) x) :
    mlieBracket I (V + Vâ‚) W x =
      mlieBracket I V W x + mlieBracket I Vâ‚ W x := by
  simp only [â† mlieBracketWithin_univ, â† contMDiffWithinAt_univ] at hV hVâ‚ âŠ¢
  exact mlieBracketWithin_add_left hV hVâ‚ (uniqueMDiffWithinAt_univ _)

lemma mlieBracketWithin_add_right
    (hW : MDifferentiableWithinAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) s x)
    (hWâ‚ : MDifferentiableWithinAt I I.tangent (fun x â†¦ (Wâ‚ x : TangentBundle I M)) s x)
    (hs : UniqueMDiffWithinAt I s x) :
    mlieBracketWithin I V (W + Wâ‚) s x =
      mlieBracketWithin I V W s x + mlieBracketWithin I V Wâ‚ s x := by
  rw [mlieBracketWithin_swap, Pi.neg_apply, mlieBracketWithin_add_left hW hWâ‚ hs,
    mlieBracketWithin_swap (V := V), mlieBracketWithin_swap (V := V), Pi.neg_apply, Pi.neg_apply]
  abel

lemma mlieBracket_add_right
    (hW : MDifferentiableAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) x)
    (hWâ‚ : MDifferentiableAt I I.tangent (fun x â†¦ (Wâ‚ x : TangentBundle I M)) x) :
    mlieBracket I V (W + Wâ‚) x =
      mlieBracket I V W x + mlieBracket I V Wâ‚ x := by
  simp only [â† mlieBracketWithin_univ, â† contMDiffWithinAt_univ] at hW hWâ‚ âŠ¢
  exact mlieBracketWithin_add_right hW hWâ‚ (uniqueMDiffWithinAt_univ _)

theorem mlieBracketWithin_of_mem_nhdsWithin
    (st : t âˆˆ ğ“[s] x) (hs : UniqueMDiffWithinAt I s x)
    (hV : MDifferentiableWithinAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) t x)
    (hW : MDifferentiableWithinAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) t x) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x := by
  simp only [mlieBracketWithin_apply]
  congr 1
  rw [lieBracketWithin_of_mem_nhdsWithin]
  Â· apply Filter.inter_mem
    Â· apply nhdsWithin_mono _ inter_subset_left
      exact (continuousAt_extChartAt_symm x).continuousWithinAt.preimage_mem_nhdsWithin''
        st (by simp)
    Â· exact nhdsWithin_mono _ inter_subset_right self_mem_nhdsWithin
  Â· exact uniqueMDiffWithinAt_iff_inter_range.1 hs
  Â· exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  Â· exact hW.differentiableWithinAt_mpullbackWithin_vectorField

theorem mlieBracketWithin_subset (st : s âŠ† t) (ht : UniqueMDiffWithinAt I s x)
    (hV : MDifferentiableWithinAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) t x)
    (hW : MDifferentiableWithinAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) t x) :
    mlieBracketWithin I V W s x = mlieBracketWithin I V W t x :=
  mlieBracketWithin_of_mem_nhdsWithin (nhdsWithin_mono _ st self_mem_nhdsWithin) ht hV hW

theorem mlieBracketWithin_eq_mlieBracket (hs : UniqueMDiffWithinAt I s x)
    (hV : MDifferentiableAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) x)
    (hW : MDifferentiableAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) x) :
    mlieBracketWithin I V W s x = mlieBracket I V W x := by
  simp only [â† mlieBracketWithin_univ, â† mdifferentiableWithinAt_univ] at hV hW âŠ¢
  exact mlieBracketWithin_subset (subset_univ _) hs hV hW

theorem _root_.DifferentiableWithinAt.mlieBracketWithin_congr_mono
    (hV : MDifferentiableWithinAt I I.tangent (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hVs : EqOn Vâ‚ V t) (hVx : Vâ‚ x = V x)
    (hW : MDifferentiableWithinAt I I.tangent (fun x â†¦ (W x : TangentBundle I M)) s x)
    (hWs : EqOn Wâ‚ W t) (hWx : Wâ‚ x = W x)
    (hxt : UniqueMDiffWithinAt I t x) (hâ‚ : t âŠ† s) :
    mlieBracketWithin I Vâ‚ Wâ‚ t x = mlieBracketWithin I V W s x := by
  rw [mlieBracketWithin_congr hVs hVx hWs hWx]
  exact mlieBracketWithin_subset hâ‚ hxt hV hW

end

section Invariance_IsSymmSndFDerivWithinAt

variable [IsManifold I 2 M] [IsManifold I' 2 M'] [CompleteSpace E]

/- The Lie bracket of vector fields on manifolds is well defined, i.e., it is invariant under
diffeomorphisms. Auxiliary version where one assumes that all relevant sets are contained
in chart domains. -/
private lemma mpullbackWithin_mlieBracketWithin_aux [CompleteSpace E']
    {f : M â†’ M'} {V W : Î  (x : M'), TangentSpace I' x} {xâ‚€ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (V x : TangentBundle I' M')) t (f xâ‚€))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (W x : TangentBundle I' M')) t (f xâ‚€))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffOn I I' 2 f s) (hxâ‚€ : xâ‚€ âˆˆ s)
    (ht : t âŠ† (extChartAt I' (f xâ‚€)).source) (hst : MapsTo f s t)
    (hsymm : IsSymmSndFDerivWithinAt ğ•œ ((extChartAt I' (f xâ‚€)) âˆ˜ f âˆ˜ (extChartAt I xâ‚€).symm)
      ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I) (extChartAt I xâ‚€ xâ‚€)) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s xâ‚€ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s xâ‚€ := by
  have A : (extChartAt I xâ‚€).symm (extChartAt I xâ‚€ xâ‚€) = xâ‚€ := by simp
  have A' : xâ‚€ = (extChartAt I xâ‚€).symm (extChartAt I xâ‚€ xâ‚€) := by simp
  have h'f : MDifferentiableWithinAt I I' f s xâ‚€ := (hf xâ‚€ hxâ‚€).mdifferentiableWithinAt one_le_two
  simp only [mlieBracketWithin_apply, mpullbackWithin_apply]
  -- first, rewrite the pullback of the Lie bracket as a pullback in `E` under the map
  -- `F = extChartAt I' (f xâ‚€) âˆ˜ f âˆ˜ (extChartAt I xâ‚€).symm` of a Lie bracket computed in `E'`,
  -- of two vector fields `V'` and `W'`.
  rw [â† ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_left
    (isInvertible_mfderiv_extChartAt (mem_extChartAt_source (f xâ‚€)))]
  rw [â† mfderiv_comp_mfderivWithin _ (mdifferentiableAt_extChartAt
    (ChartedSpace.mem_chart_source (f xâ‚€))) h'f (hu xâ‚€ hxâ‚€)]
  rw [eq_comm, (isInvertible_mfderiv_extChartAt (mem_extChartAt_source xâ‚€)).inverse_apply_eq]
  have : (mfderivWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm (range I) (extChartAt I xâ‚€ xâ‚€)).inverse =
      mfderiv I ğ“˜(ğ•œ, E) (extChartAt I xâ‚€) xâ‚€ := by
    apply ContinuousLinearMap.inverse_eq
    Â· convert mfderivWithin_extChartAt_symm_comp_mfderiv_extChartAt (I := I) (x := xâ‚€)
        (y := extChartAt I xâ‚€ xâ‚€) (by simp)
    Â· convert mfderiv_extChartAt_comp_mfderivWithin_extChartAt_symm (I := I) (x := xâ‚€)
        (y := extChartAt I xâ‚€ xâ‚€) (by simp)
  rw [â† this, â† ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_right]; swap
  Â· exact isInvertible_mfderivWithin_extChartAt_symm (mem_extChartAt_target xâ‚€)
  have : mfderivWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm (range I) (extChartAt I xâ‚€ xâ‚€) =
      mfderivWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I)
      (extChartAt I xâ‚€ xâ‚€) :=
    (MDifferentiableWithinAt.mfderivWithin_mono
      (mdifferentiableWithinAt_extChartAt_symm (mem_extChartAt_target xâ‚€))
      (UniqueDiffWithinAt.uniqueMDiffWithinAt (hu xâ‚€ hxâ‚€)) inter_subset_right).symm
  rw [this]; clear this
  rw [â† mfderivWithin_comp_of_eq]; rotate_left
  Â· apply MDifferentiableAt.comp_mdifferentiableWithinAt (I' := I') _ _ h'f
    exact mdifferentiableAt_extChartAt (ChartedSpace.mem_chart_source (f xâ‚€))
  Â· exact (mdifferentiableWithinAt_extChartAt_symm (mem_extChartAt_target xâ‚€)).mono
      inter_subset_right
  Â· exact inter_subset_left
  Â· exact UniqueDiffWithinAt.uniqueMDiffWithinAt (hu xâ‚€ hxâ‚€)
  Â· simp
  set V' := mpullbackWithin ğ“˜(ğ•œ, E') I' (extChartAt I' (f xâ‚€)).symm V (range I') with hV'
  set W' := mpullbackWithin ğ“˜(ğ•œ, E') I' (extChartAt I' (f xâ‚€)).symm W (range I') with hW'
  set F := ((extChartAt I' (f xâ‚€)) âˆ˜ f) âˆ˜ â†‘(extChartAt I xâ‚€).symm with hF
  have hFxâ‚€ : extChartAt I' (f xâ‚€) (f xâ‚€) = F (extChartAt I xâ‚€ xâ‚€) := by simp [F]
  rw [hFxâ‚€, â† mpullbackWithin_apply]
  -- second rewrite, the Lie bracket of the pullback as the Lie bracket of the pullback of the
  -- vector fields `V'` and `W'` in `E'`.
  have P (Y : (x : M') â†’ TangentSpace I' x) :
      (mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I xâ‚€).symm (mpullbackWithin I I' f Y s)
      (range I)) =á¶ [ğ“[(extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I] (extChartAt I xâ‚€ xâ‚€)]
        mpullbackWithin ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') F
          (mpullbackWithin ğ“˜(ğ•œ, E') I' ((extChartAt I' (f xâ‚€)).symm) Y (range I'))
          ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I) := by
    have : (extChartAt I xâ‚€).target
        âˆˆ ğ“[(extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I] (extChartAt I xâ‚€ xâ‚€) :=
      nhdsWithin_mono _ inter_subset_right (extChartAt_target_mem_nhdsWithin xâ‚€)
    filter_upwards [self_mem_nhdsWithin, this] with y hy h'''y
    have h'y : f ((extChartAt I xâ‚€).symm y) âˆˆ (extChartAt I' (f xâ‚€)).source := ht (hst hy.1)
    have h''y : f ((extChartAt I xâ‚€).symm y) âˆˆ (chartAt H' (f xâ‚€)).source := by simpa using h'y
    have huy : UniqueMDiffWithinAt ğ“˜(ğ•œ, E) ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I) y := by
      apply UniqueDiffWithinAt.uniqueMDiffWithinAt
      rw [inter_comm]
      apply hu.uniqueDiffWithinAt_range_inter
      exact âŸ¨h'''y, hy.1âŸ©
    simp only [mpullbackWithin_apply, hF, comp_apply, â† mfderivWithin_eq_fderivWithin]
    rw [mfderivWithin_comp (I' := I) (u := s)]; rotate_left
    Â· apply (mdifferentiableAt_extChartAt h''y).comp_mdifferentiableWithinAt (I' := I')
      exact (hf _ hy.1).mdifferentiableWithinAt one_le_two
    Â· exact (mdifferentiableWithinAt_extChartAt_symm h'''y).mono inter_subset_right
    Â· exact inter_subset_left
    Â· exact huy
    rw [mfderiv_comp_mfderivWithin (I' := I')]; rotate_left
    Â· exact mdifferentiableAt_extChartAt h''y
    Â· exact (hf _ hy.1).mdifferentiableWithinAt one_le_two
    Â· exact hu _ hy.1
    rw [â† ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_right]; swap
    Â· exact isInvertible_mfderivWithin_extChartAt_symm h'''y
    rw [â† ContinuousLinearMap.IsInvertible.inverse_comp_apply_of_left]; swap
    Â· exact isInvertible_mfderivWithin_extChartAt_symm (PartialEquiv.map_source _ h'y)
    have : f ((extChartAt I xâ‚€).symm y)
        = (extChartAt I' (f xâ‚€)).symm ((extChartAt I' (f xâ‚€)) (f ((extChartAt I xâ‚€).symm y))) :=
      (PartialEquiv.left_inv (extChartAt I' (f xâ‚€)) h'y).symm
    congr 2
    have : (mfderivWithin ğ“˜(ğ•œ, E') I' ((extChartAt I' (f xâ‚€)).symm) (range I')
        (extChartAt I' (f xâ‚€) (f ((extChartAt I xâ‚€).symm y)))) âˆ˜L
        (mfderiv I' ğ“˜(ğ•œ, E') (â†‘(extChartAt I' (f xâ‚€))) (f ((extChartAt I xâ‚€).symm y))) =
        ContinuousLinearMap.id _ _ := by
      convert mfderivWithin_extChartAt_symm_comp_mfderiv_extChartAt
        ((PartialEquiv.map_source _ h'y))
    simp only [â† ContinuousLinearMap.comp_assoc, this, ContinuousLinearMap.id_comp]
    congr 1
    exact ((mdifferentiableWithinAt_extChartAt_symm h'''y).mfderivWithin_mono huy
      inter_subset_right).symm
  rw [Filter.EventuallyEq.lieBracketWithin_vectorField_eq_of_mem (P V) (P W) (by simp [hxâ‚€]),
    â† hV', â† hW']
  simp only [mpullbackWithin_eq_pullbackWithin]
  -- finally, use the fact that for `C^2` maps between vector spaces with symmetric second
  -- derivative, the pullback and the Lie bracket commute.
  rw [pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt_of_eventuallyEq
      (u := (extChartAt I xâ‚€).symm â»Â¹' s âˆ© (extChartAt I xâ‚€).target)]
  Â· exact hsymm
  Â· rw [hF, comp_assoc]
    apply ContMDiffWithinAt.contDiffWithinAt
    apply ContMDiffAt.comp_contMDiffWithinAt (I' := I')
    Â· exact contMDiffAt_extChartAt' (by simp)
    apply ContMDiffWithinAt.comp_of_eq (I' := I) (hf _ hxâ‚€) _ _ A
    Â· exact (contMDiffWithinAt_extChartAt_symm_range _ (mem_extChartAt_target xâ‚€)).mono
        inter_subset_right
    Â· exact (mapsTo_preimage _ _).mono_left inter_subset_left
  Â· rw [â† hFxâ‚€]
    exact hV.differentiableWithinAt_mpullbackWithin_vectorField
  Â· rw [â† hFxâ‚€]
    exact hW.differentiableWithinAt_mpullbackWithin_vectorField
  Â· rw [inter_comm]
    exact UniqueMDiffOn.uniqueDiffOn_target_inter hu xâ‚€
  Â· simp [hxâ‚€]
  Â· intro z hz
    simp only [comp_apply, mem_inter_iff, mem_preimage, mem_range, F]
    refine âŸ¨?_, mem_range_self _âŸ©
    convert hst hz.1
    exact PartialEquiv.left_inv (extChartAt I' (f xâ‚€)) (ht (hst hz.1))
  Â· rw [â† nhdsWithin_eq_iff_eventuallyEq]
    apply le_antisymm
    Â· exact nhdsWithin_mono _ (inter_subset_inter_right _ (extChartAt_target_subset_range xâ‚€))
    Â· rw [nhdsWithin_le_iff, nhdsWithin_inter]
      exact Filter.inter_mem_inf self_mem_nhdsWithin (extChartAt_target_mem_nhdsWithin xâ‚€)

/- The Lie bracket of vector fields on manifolds is well defined, i.e., it is invariant under
diffeomorphisms. -/
lemma mpullbackWithin_mlieBracketWithin_of_isSymmSndFDerivWithinAt
    {f : M â†’ M'} {V W : Î  (x : M'), TangentSpace I' x} {xâ‚€ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (V x : TangentBundle I' M')) t (f xâ‚€))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (W x : TangentBundle I' M')) t (f xâ‚€))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffWithinAt I I' 2 f s xâ‚€) (hxâ‚€ : xâ‚€ âˆˆ s)
    (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€)
    (hsymm : IsSymmSndFDerivWithinAt ğ•œ ((extChartAt I' (f xâ‚€)) âˆ˜ f âˆ˜ (extChartAt I xâ‚€).symm)
      ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I) (extChartAt I xâ‚€ xâ‚€)) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s xâ‚€ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s xâ‚€ := by
  have A : (extChartAt I xâ‚€).symm (extChartAt I xâ‚€ xâ‚€) = xâ‚€ := by simp
  by_cases hfi : (mfderivWithin I I' f s xâ‚€).IsInvertible; swap
  Â· simp only [mlieBracketWithin_apply, mpullbackWithin_apply,
      ContinuousLinearMap.inverse_of_not_isInvertible hfi, ContinuousLinearMap.zero_apply]
    rw [lieBracketWithin_eq_zero_of_eq_zero]
    Â· simp [-extChartAt]
    Â· simp only [mpullbackWithin_apply]
      rw [A, ContinuousLinearMap.inverse_of_not_isInvertible hfi]
      simp [-extChartAt]
    Â· simp only [mpullbackWithin_apply]
      rw [A, ContinuousLinearMap.inverse_of_not_isInvertible hfi]
      simp [-extChartAt]
  -- Now, interesting case where the derivative of `f` is invertible
  have : CompleteSpace E' := by
    rcases hfi with âŸ¨M, -âŸ©
    let M' : E â‰ƒL[ğ•œ] E' := M
    exact (completeSpace_congr (e := M'.toEquiv) M'.isUniformEmbedding).1 (by assumption)
  -- choose a small open set `v` around `xâ‚€` where `f` is `C^2`
  obtain âŸ¨u, u_open, xâ‚€u, ut, maps_u, u_smoothâŸ© :
      âˆƒ u, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ s âˆ© u âŠ† f â»Â¹' t âˆ§
        s âˆ© u âŠ† f â»Â¹' (extChartAt I' (f xâ‚€)).source âˆ§ ContMDiffOn I I' 2 f (s âˆ© u) := by
    obtain âŸ¨u, u_open, xâ‚€u, huâŸ© :
        âˆƒ u, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ ContMDiffOn I I' 2 f (insert xâ‚€ s âˆ© u) :=
      hf.contMDiffOn' le_rfl (by simp)
    have : f â»Â¹' (extChartAt I' (f xâ‚€)).source âˆˆ ğ“[s] xâ‚€ :=
      hf.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (f xâ‚€))
    rcases mem_nhdsWithin.1 (Filter.inter_mem hst this) with âŸ¨w, w_open, xâ‚€w, hwâŸ©
    refine âŸ¨u âˆ© w, u_open.inter w_open, by simp [xâ‚€u, xâ‚€w], ?_, ?_, ?_âŸ©
    Â· apply Subset.trans _ (hw.trans inter_subset_left)
      exact fun y hy â†¦ âŸ¨hy.2.2, hy.1âŸ©
    Â· apply Subset.trans _ (hw.trans inter_subset_right)
      exact fun y hy â†¦ âŸ¨hy.2.2, hy.1âŸ©
    Â· apply hu.mono
      exact fun y hy â†¦ âŸ¨subset_insert _ _ hy.1, hy.2.1âŸ©
  have u_mem : u âˆˆ ğ“ xâ‚€ := u_open.mem_nhds xâ‚€u
  -- apply the auxiliary version to `s âˆ© u`
  set s' := s âˆ© u with hs'
  have s'_eq : s' =á¶ [ğ“ xâ‚€] s := by
    filter_upwards [u_mem] with y hy
    change (y âˆˆ s âˆ© u) = (y âˆˆ s)
    simp [hy]
  set t' := t âˆ© (extChartAt I' (f xâ‚€)).source with ht'
  calc mpullbackWithin I I' f (mlieBracketWithin I' V W t) s xâ‚€
  _ = mpullbackWithin I I' f (mlieBracketWithin I' V W t) s' xâ‚€ := by
    simp only [mpullbackWithin, hs', mfderivWithin_inter u_mem]
  _ = mpullbackWithin I I' f (mlieBracketWithin I' V W t') s' xâ‚€ := by
    simp only [mpullbackWithin, ht', mlieBracketWithin_inter (extChartAt_source_mem_nhds (f xâ‚€))]
  _ = mlieBracketWithin I (mpullbackWithin I I' f V s') (mpullbackWithin I I' f W s') s' xâ‚€ := by
    apply mpullbackWithin_mlieBracketWithin_aux (t := t') (hV.mono inter_subset_left)
      (hW.mono inter_subset_left) (hu.inter u_open) u_smooth âŸ¨hxâ‚€, xâ‚€uâŸ© inter_subset_right
      (fun y hy â†¦ âŸ¨ut hy, maps_u hyâŸ©)
    apply hsymm.congr_set
    have : (extChartAt I xâ‚€).symm â»Â¹' u âˆˆ ğ“ (extChartAt I xâ‚€ xâ‚€) := by
      apply (continuousAt_extChartAt_symm xâ‚€).preimage_mem_nhds
      apply u_open.mem_nhds (by simpa using xâ‚€u)
    filter_upwards [this] with y hy
    change (y âˆˆ (extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I) =
      (y âˆˆ (extChartAt I xâ‚€).symm â»Â¹' (s âˆ© u) âˆ© range I)
    simp [-extChartAt, hy]
  _ = mlieBracketWithin I (mpullbackWithin I I' f V s') (mpullbackWithin I I' f W s') s xâ‚€ := by
    simp only [hs', mlieBracketWithin_inter u_mem]
  _ = mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s xâ‚€ := by
    apply Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem _ _ hxâ‚€
    Â· apply nhdsWithin_le_nhds
      filter_upwards [mfderivWithin_eventually_congr_set (I := I) (I' := I') (f := f) s'_eq]
        with y hy using by simp [mpullbackWithin, hy]
    Â· apply nhdsWithin_le_nhds
      filter_upwards [mfderivWithin_eventually_congr_set (I := I) (I' := I') (f := f) s'_eq]
        with y hy using by simp [mpullbackWithin, hy]

end Invariance_IsSymmSndFDerivWithinAt

section Invariance

variable [IsManifold I (minSmoothness ğ•œ 2) M] [IsManifold I' (minSmoothness ğ•œ 2) M']
  [CompleteSpace E] {n : WithTop â„•âˆ}

/-- The pullback commutes with the Lie bracket of vector fields on manifolds. Version where one
assumes that the map is smooth on a larget set `u` (so that the
condition `xâ‚€ âˆˆ closure (interior u)`, needed to guarantee the symmetry of the second derivative,
becomes easier to check.) -/
lemma mpullbackWithin_mlieBracketWithin'
    {f : M â†’ M'} {V W : Î  (x : M'), TangentSpace I' x} {xâ‚€ : M} {s u : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (V x : TangentBundle I' M')) t (f xâ‚€))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (W x : TangentBundle I' M')) t (f xâ‚€))
    (hs : UniqueMDiffOn I s) (hu : UniqueMDiffOn I u)
    (hf : ContMDiffWithinAt I I' n f u xâ‚€) (hxâ‚€ : xâ‚€ âˆˆ s) (hn : minSmoothness ğ•œ 2 â‰¤ n)
    (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) (h'xâ‚€ : xâ‚€ âˆˆ closure (interior u)) (hsu : s âŠ† u) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s xâ‚€ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s xâ‚€ := by
  have B : ContDiffWithinAt ğ•œ n ((extChartAt I' (f xâ‚€)) âˆ˜ f âˆ˜ (extChartAt I xâ‚€).symm)
      ((extChartAt I xâ‚€).symm â»Â¹' u âˆ© (extChartAt I xâ‚€).target) (extChartAt I xâ‚€ xâ‚€) := by
    apply (contMDiffWithinAt_iff.1 hf).2.congr_set
    exact EventuallyEq.inter (by rfl) extChartAt_target_eventuallyEq.symm
  apply mpullbackWithin_mlieBracketWithin_of_isSymmSndFDerivWithinAt hV hW hs
    ((hf.mono hsu).of_le (le_minSmoothness.trans hn)) hxâ‚€ hst
  have : ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© (extChartAt I xâ‚€).target : Set E)
      =á¶ [ğ“ (extChartAt I xâ‚€ xâ‚€)] ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© range I : Set E) :=
    EventuallyEq.inter (by rfl) extChartAt_target_eventuallyEq
  apply IsSymmSndFDerivWithinAt.congr_set _ this
  have : IsSymmSndFDerivWithinAt ğ•œ ((extChartAt I' (f xâ‚€)) âˆ˜ f âˆ˜ (extChartAt I xâ‚€).symm)
      ((extChartAt I xâ‚€).symm â»Â¹' u âˆ© (extChartAt I xâ‚€).target) (extChartAt I xâ‚€ xâ‚€) := by
    apply ContDiffWithinAt.isSymmSndFDerivWithinAt (n := minSmoothness ğ•œ 2) _ le_rfl
    Â· rw [inter_comm]
      exact UniqueMDiffOn.uniqueDiffOn_target_inter hu xâ‚€
    Â· apply extChartAt_mem_closure_interior h'xâ‚€ (mem_extChartAt_source xâ‚€)
    Â· simp [hsu hxâ‚€]
    Â· exact B.of_le hn
  apply IsSymmSndFDerivWithinAt.mono_of_mem_nhdsWithin this
  Â· apply mem_of_superset self_mem_nhdsWithin (inter_subset_inter_left _ (preimage_mono hsu))
  Â· exact (B.of_le hn).of_le le_minSmoothness
  Â· rw [inter_comm]
    exact UniqueMDiffOn.uniqueDiffOn_target_inter hs xâ‚€
  Â· rw [inter_comm]
    exact UniqueMDiffOn.uniqueDiffOn_target_inter hu xâ‚€
  Â· simp [hxâ‚€]

/-- The pullback commutes with the Lie bracket of vector fields on manifolds. -/
lemma mpullbackWithin_mlieBracketWithin
    {f : M â†’ M'} {V W : Î  (x : M'), TangentSpace I' x} {xâ‚€ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (V x : TangentBundle I' M')) t (f xâ‚€))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (W x : TangentBundle I' M')) t (f xâ‚€))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffWithinAt I I' n f s xâ‚€) (hxâ‚€ : xâ‚€ âˆˆ s)
    (hn : minSmoothness ğ•œ 2 â‰¤ n)
    (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) (h'xâ‚€ : xâ‚€ âˆˆ closure (interior s)) :
    mpullbackWithin I I' f (mlieBracketWithin I' V W t) s xâ‚€ =
      mlieBracketWithin I (mpullbackWithin I I' f V s) (mpullbackWithin I I' f W s) s xâ‚€ :=
  mpullbackWithin_mlieBracketWithin' hV hW hu hu hf hxâ‚€ hn hst h'xâ‚€ Subset.rfl

/-- The pullback commutes with the Lie bracket of vector fields on manifolds. -/
lemma mpullback_mlieBracketWithin
    {f : M â†’ M'} {V W : Î  (x : M'), TangentSpace I' x} {xâ‚€ : M} {s : Set M} {t : Set M'}
    (hV : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (V x : TangentBundle I' M')) t (f xâ‚€))
    (hW : MDifferentiableWithinAt I' I'.tangent (fun x â†¦ (W x : TangentBundle I' M')) t (f xâ‚€))
    (hu : UniqueMDiffOn I s) (hf : ContMDiffAt I I' n f xâ‚€) (hxâ‚€ : xâ‚€ âˆˆ s)
    (hn : minSmoothness ğ•œ 2 â‰¤ n) (hst : f â»Â¹' t âˆˆ ğ“[s] xâ‚€) :
    mpullback I I' f (mlieBracketWithin I' V W t) xâ‚€ =
      mlieBracketWithin I (mpullback I I' f V) (mpullback I I' f W) s xâ‚€ := by
  have : mpullback I I' f (mlieBracketWithin I' V W t) xâ‚€ =
      mpullbackWithin I I' f (mlieBracketWithin I' V W t) s xâ‚€ := by
    simp only [mpullback, mpullbackWithin]
    congr
    apply (mfderivWithin_eq_mfderiv (hu _ hxâ‚€) _).symm
    exact hf.mdifferentiableAt (one_le_two.trans (le_minSmoothness.trans hn))
  rw [this, mpullbackWithin_mlieBracketWithin' hV hW hu uniqueMDiffOn_univ hf.contMDiffWithinAt
    hxâ‚€ hn hst (by simp) (subset_univ _)]
  apply Filter.EventuallyEq.mlieBracketWithin_vectorField_of_insert
  Â· rw [insert_eq_of_mem hxâ‚€]
    filter_upwards [nhdsWithin_le_nhds ((contMDiffAt_iff_contMDiffAt_nhds (by simp)).1
      (hf.of_le (le_minSmoothness.trans hn))), self_mem_nhdsWithin] with y hy h'y
    simp only [mpullback, mpullbackWithin]
    congr
    apply mfderivWithin_eq_mfderiv (hu _ h'y)
    exact hy.mdifferentiableAt one_le_two
  Â· rw [insert_eq_of_mem hxâ‚€]
    filter_upwards [nhdsWithin_le_nhds ((contMDiffAt_iff_contMDiffAt_nhds (by simp)).1
      (hf.of_le (le_minSmoothness.trans hn))), self_mem_nhdsWithin] with y hy h'y
    simp only [mpullback, mpullbackWithin]
    congr
    apply mfderivWithin_eq_mfderiv (hu _ h'y)
    exact hy.mdifferentiableAt one_le_two

lemma mpullback_mlieBracket
    {f : M â†’ M'} {V W : Î  (x : M'), TangentSpace I' x} {xâ‚€ : M}
    (hV : MDifferentiableAt I' I'.tangent (fun x â†¦ (V x : TangentBundle I' M')) (f xâ‚€))
    (hW : MDifferentiableAt I' I'.tangent (fun x â†¦ (W x : TangentBundle I' M')) (f xâ‚€))
    (hf : ContMDiffAt I I' n f xâ‚€) (hn : minSmoothness ğ•œ 2 â‰¤ n) :
    mpullback I I' f (mlieBracket I' V W) xâ‚€ =
      mlieBracket I (mpullback I I' f V) (mpullback I I' f W) xâ‚€ := by
  simp only [â† mlieBracketWithin_univ, â† mdifferentiableWithinAt_univ] at hV hW âŠ¢
  exact mpullback_mlieBracketWithin hV hW uniqueMDiffOn_univ hf (mem_univ _) hn (by simp)

/-- If two vector fields are `C^n` with `n â‰¥ m + 1`, then their Lie bracket is `C^m`. -/
protected lemma _root_.ContMDiffWithinAt.mlieBracketWithin_vectorField
    [IsManifold I (n + 1) M] {m : WithTop â„•âˆ}
    {U V : Î  (x : M), TangentSpace I x} {s : Set M} {x : M}
    (hU : ContMDiffWithinAt I I.tangent n (fun x â†¦ (U x : TangentBundle I M)) s x)
    (hV : ContMDiffWithinAt I I.tangent n (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hs : UniqueMDiffOn I s) (hx : x âˆˆ s) (hmn : minSmoothness ğ•œ (m + 1) â‰¤ n) :
    ContMDiffWithinAt I I.tangent m
      (fun x â†¦ (mlieBracketWithin I U V s x : TangentBundle I M)) s x := by
  /- The statement is not obvious, since at different points the Lie bracket is defined using
  different charts. However, since we know that the Lie bracket is invariant under diffeos, we can
  use a single chart to prove the statement. Let `U'` and `V'` denote the pullbacks of `U` and `V`
  in the chart around `x`. Then the Lie bracket there is smooth (as it coincides with the vector
  space Lie bracket, given by an explicit formula). Pulling back this Lie bracket in `M` gives
  locally a smooth function, which coincides with the initial Lie bracket by invariance
  under diffeos. -/
  have min2 : minSmoothness ğ•œ 2 â‰¤ n + 1 := by
    apply le_trans _ (add_le_add_right hmn 1)
    rw [â† minSmoothness_add, add_assoc]
    exact minSmoothness_monotone le_add_self
  apply contMDiffWithinAt_iff_le_ne_infty.2 (fun m' hm' h'm' â†¦ ?_)
  have hn : 1 â‰¤ m' + 1 := le_add_self
  have hm'n : m' + 1 â‰¤ n := le_trans (add_le_add_right hm' 1) (le_minSmoothness.trans hmn)
  have : IsManifold I (m' + 1) M := IsManifold.of_le (n := n + 1) (hm'n.trans le_self_add)
  have pre_mem : (extChartAt I x) â»Â¹' ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s)
      âˆˆ ğ“[s] x := by
    filter_upwards [self_mem_nhdsWithin,
      nhdsWithin_le_nhds (extChartAt_source_mem_nhds (I := I) x)] with y hy h'y
    exact âŸ¨(extChartAt I x).map_source h'y,
      by simpa only [mem_preimage, (extChartAt I x).left_inv h'y] using hyâŸ©
  let U' := mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm U (range I)
  let V' := mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm V (range I)
  have A : ContDiffWithinAt ğ•œ m' (lieBracketWithin ğ•œ U' V'
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x x) :=
    ContDiffWithinAt.lieBracketWithin_vectorField
      (contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.1
        (contMDiffWithinAt_mpullbackWithin_extChartAt_symm hU hs hx le_rfl))
      (contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.1
        (contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx le_rfl))
      (hs.uniqueDiffOn_target_inter x) hm'n (by simp [hx])
  have B : ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent m' (fun y â†¦ (mlieBracketWithin ğ“˜(ğ•œ, E) U' V'
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x x) := by
    rw [â† mlieBracketWithin_eq_lieBracketWithin] at A
    exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.2 A
  have C : ContMDiffWithinAt I I.tangent m' (fun y â†¦ (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x)
      ((mlieBracketWithin ğ“˜(ğ•œ, E) U' V'
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s))) y : TangentBundle I M)) s x :=
    ContMDiffWithinAt.mpullback_vectorField_of_mem_nhdsWithin_of_eq B (n := m' + 1)
      contMDiffAt_extChartAt
      (isInvertible_mfderiv_extChartAt (mem_extChartAt_source x)) le_rfl pre_mem rfl
  apply C.congr_of_eventuallyEq_of_mem _ hx
  filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem,
    eventually_eventually_nhdsWithin.2 (eventuallyEq_mpullback_mpullbackWithin_extChartAt U),
    eventually_eventually_nhdsWithin.2 (eventuallyEq_mpullback_mpullbackWithin_extChartAt V),
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm (hU.of_le hm'n) hs hx
      (add_le_add_right hm'n 1) (by simp [h'm']),
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm (hV.of_le hm'n) hs hx
      (add_le_add_right hm'n 1) (by simp [h'm']),
    nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin]
    with y hy hyU hyV h'yU h'yV hy_chart hys
  simp only [Bundle.TotalSpace.mk_inj]
  rw [mpullback_mlieBracketWithin (h'yU.mdifferentiableWithinAt hn)
    (h'yV.mdifferentiableWithinAt hn) hs (contMDiffAt_extChartAt' hy_chart) hys min2 hy]
  exact Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem hyU hyV hys

/-- If two vector fields are `C^n` with `n â‰¥ m + 1`, then their Lie bracket is `C^m`. -/
lemma _root_.ContMDiffAt.mlieBracket_vectorField {m n : â„•âˆ}
    [IsManifold I (n + 1) M] {U V : Î  (x : M), TangentSpace I x} {x : M}
    (hU : ContMDiffAt I I.tangent n (fun x â†¦ (U x : TangentBundle I M)) x)
    (hV : ContMDiffAt I I.tangent n (fun x â†¦ (V x : TangentBundle I M)) x)
    (hmn : minSmoothness ğ•œ (m + 1) â‰¤ n) :
    ContMDiffAt I I.tangent m (fun x â†¦ (mlieBracket I U V x : TangentBundle I M)) x := by
  simp only [â† contMDiffWithinAt_univ, â† mlieBracketWithin_univ] at hU hV âŠ¢
  exact hU.mlieBracketWithin_vectorField hV uniqueMDiffOn_univ (mem_univ _) hmn

/-- If two vector fields are `C^n` with `n â‰¥ m + 1`, then their Lie bracket is `C^m`. -/
lemma _root_.ContMDiffOn.mlieBracketWithin_vectorField {m n : â„•âˆ}
    [IsManifold I (n + 1) M] {U V : Î  (x : M), TangentSpace I x}
    (hU : ContMDiffOn I I.tangent n (fun x â†¦ (U x : TangentBundle I M)) s)
    (hV : ContMDiffOn I I.tangent n (fun x â†¦ (V x : TangentBundle I M)) s)
    (hs : UniqueMDiffOn I s) (hmn : minSmoothness ğ•œ (m + 1) â‰¤ n) :
    ContMDiffOn I I.tangent m (fun x â†¦ (mlieBracketWithin I U V s x : TangentBundle I M)) s :=
  fun x hx â†¦ (hU x hx).mlieBracketWithin_vectorField (hV x hx) hs hx hmn

/-- If two vector fields are `C^n` with `n â‰¥ m + 1`, then their Lie bracket is `C^m`. -/
lemma _root_.ContDiff.mlieBracket_vectorField {m n : â„•âˆ}
    [IsManifold I (n + 1) M] {U V : Î  (x : M), TangentSpace I x}
    (hU : ContMDiff I I.tangent n (fun x â†¦ (U x : TangentBundle I M)))
    (hV : ContMDiff I I.tangent n (fun x â†¦ (V x : TangentBundle I M)))
    (hmn : minSmoothness ğ•œ (m + 1) â‰¤ n) :
    ContMDiff I I.tangent m (fun x â†¦ (mlieBracket I U V x : TangentBundle I M)) := by
  simp only [â† contMDiffOn_univ, mlieBracketWithin_univ] at hU hV âŠ¢
  exact hU.mlieBracketWithin_vectorField hV uniqueMDiffOn_univ hmn

end Invariance

section Leibniz

variable [IsManifold I (minSmoothness ğ•œ 3) M] [CompleteSpace E]

/-- The Lie bracket of vector fields in manifolds satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]` (also called Jacobi identity). -/
theorem leibniz_identity_mlieBracketWithin_apply
    {U V W : Î  (x : M), TangentSpace I x} {s : Set M} {x : M}
    (hs : UniqueMDiffOn I s) (h's : x âˆˆ closure (interior s)) (hx : x âˆˆ s)
    (hU : ContMDiffWithinAt I I.tangent (minSmoothness ğ•œ 2)
      (fun x â†¦ (U x : TangentBundle I M)) s x)
    (hV : ContMDiffWithinAt I I.tangent (minSmoothness ğ•œ 2)
      (fun x â†¦ (V x : TangentBundle I M)) s x)
    (hW : ContMDiffWithinAt I I.tangent (minSmoothness ğ•œ 2)
      (fun x â†¦ (W x : TangentBundle I M)) s x) :
    mlieBracketWithin I U (mlieBracketWithin I V W s) s x =
      mlieBracketWithin I (mlieBracketWithin I U V s) W s x
      + mlieBracketWithin I V (mlieBracketWithin I U W s) s x := by
  have A : minSmoothness ğ•œ 2 + 1 â‰¤ minSmoothness ğ•œ 3 := by
    simp only [â† minSmoothness_add]
    exact le_rfl
  have s_inter_mem : s âˆ© (extChartAt I x).source âˆˆ ğ“[s] x :=
    inter_mem self_mem_nhdsWithin (nhdsWithin_le_nhds (extChartAt_source_mem_nhds x))
  have pre_mem : (extChartAt I x) â»Â¹' ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s)
      âˆˆ ğ“[s] x := by
    filter_upwards [s_inter_mem] with y hy
    exact âŸ¨(extChartAt I x).map_source hy.2,
      by simpa only [mem_preimage, (extChartAt I x).left_inv hy.2] using hy.1âŸ©
  -- write everything as pullbacks of vector fields in `E` (denoted with primes), for which
  -- the identity can be checked via direct calculation.
  let U' := mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm U (range I)
  let V' := mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm V (range I)
  let W' := mpullbackWithin ğ“˜(ğ•œ, E) I (extChartAt I x).symm W (range I)
  -- register basic facts on the pullbacks in the vector space
  have J0U : ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent (minSmoothness ğ•œ 2)
      (fun y â†¦ (U' y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x x) :=
    contMDiffWithinAt_mpullbackWithin_extChartAt_symm hU hs hx A
  have J0V : ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent (minSmoothness ğ•œ 2)
      (fun y â†¦ (V' y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x x) :=
    contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx A
  have J0W : ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent (minSmoothness ğ•œ 2)
      (fun y â†¦ (W' y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x x) :=
    contMDiffWithinAt_mpullbackWithin_extChartAt_symm hW hs hx A
  have J1U : âˆ€á¶  y in ğ“[s] x, ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent (minSmoothness ğ•œ 2)
      (fun y â†¦ (U' y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x y) :=
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm hU hs hx A (by simp)
  have J1V : âˆ€á¶  y in ğ“[s] x, ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent (minSmoothness ğ•œ 2)
      (fun y â†¦ (V' y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x y) :=
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm hV hs hx A (by simp)
  have J1W : âˆ€á¶  y in ğ“[s] x, ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent (minSmoothness ğ•œ 2)
      (fun y â†¦ (W' y : TangentBundle ğ“˜(ğ•œ, E) E))
      ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s) (extChartAt I x y) :=
    eventually_contMDiffWithinAt_mpullbackWithin_extChartAt_symm hW hs hx A (by simp)
  have JU : U =á¶ [ğ“[s] x] mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) U' :=
    eventuallyEq_mpullback_mpullbackWithin_extChartAt U
  have JV : V =á¶ [ğ“[s] x] mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) V' :=
    eventuallyEq_mpullback_mpullbackWithin_extChartAt V
  have JW : W =á¶ [ğ“[s] x] mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) W' :=
    eventuallyEq_mpullback_mpullbackWithin_extChartAt W
  rw [JU.mlieBracketWithin_vectorField_eq_of_mem (JV.mlieBracketWithin_vectorField JW) hx,
    (JU.mlieBracketWithin_vectorField JV).mlieBracketWithin_vectorField_eq_of_mem JW hx,
    JV.mlieBracketWithin_vectorField_eq_of_mem (JU.mlieBracketWithin_vectorField JW) hx]
  /- Rewrite the first term as a pullback-/
  have : âˆ€á¶  y in ğ“[s] x, mlieBracketWithin I
        (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) V') (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) W') s y
      = mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) (mlieBracketWithin ğ“˜(ğ•œ, E) V' W'
        ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s)) y := by
    filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem, J1V, J1W,
      nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin] with y hy hyV hyW h'y ys
    symm
    exact mpullback_mlieBracketWithin (n := minSmoothness ğ•œ 2)
      (hyV.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness))
      (hyW.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness)) hs
      (contMDiffAt_extChartAt' h'y) ys le_rfl hy
  rw [Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem EventuallyEq.rfl this hx,
    â† mpullback_mlieBracketWithin (J0U.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness))
      _ hs contMDiffAt_extChartAt hx le_rfl pre_mem]; swap
  Â· apply ContMDiffWithinAt.mdifferentiableWithinAt _ le_rfl
    apply J0V.mlieBracketWithin_vectorField J0W (m := 1)
    Â· exact hs.uniqueMDiffOn_target_inter x
    Â· exact âŸ¨mem_extChartAt_target x, by simp [hx]âŸ©
    Â· exact le_rfl
  /- Rewrite the second term as a pullback-/
  have : âˆ€á¶  y in ğ“[s] x, mlieBracketWithin I
        (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) U') (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) V') s y
      = mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) (mlieBracketWithin ğ“˜(ğ•œ, E) U' V'
        ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s)) y := by
    filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem, J1U, J1V,
      nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin] with y hy hyU hyV h'y ys
    symm
    exact mpullback_mlieBracketWithin (n := minSmoothness ğ•œ 2)
      (hyU.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness))
      (hyV.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness)) hs
      (contMDiffAt_extChartAt' h'y) ys le_rfl hy
  rw [Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem this EventuallyEq.rfl hx,
    â† mpullback_mlieBracketWithin _ (J0W.mdifferentiableWithinAt
      (one_le_two.trans le_minSmoothness)) hs contMDiffAt_extChartAt hx le_rfl pre_mem]; swap
  Â· apply ContMDiffWithinAt.mdifferentiableWithinAt _ le_rfl
    apply J0U.mlieBracketWithin_vectorField J0V (m := 1)
    Â· exact hs.uniqueMDiffOn_target_inter x
    Â· exact âŸ¨mem_extChartAt_target x, by simp [hx]âŸ©
    Â· exact le_rfl
  /- Rewrite the third term as a pullback-/
  have : âˆ€á¶  y in ğ“[s] x, mlieBracketWithin I
        (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) U') (mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) W') s y
      = mpullback I ğ“˜(ğ•œ, E) (extChartAt I x) (mlieBracketWithin ğ“˜(ğ•œ, E) U' W'
        ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' s)) y := by
    filter_upwards [eventually_eventually_nhdsWithin.2 pre_mem, J1U, J1W,
      nhdsWithin_le_nhds (chart_source_mem_nhds H x), self_mem_nhdsWithin] with y hy hyU hyW h'y ys
    symm
    exact mpullback_mlieBracketWithin (n := minSmoothness ğ•œ 2)
      (hyU.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness))
      (hyW.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness)) hs
      (contMDiffAt_extChartAt' h'y) ys le_rfl hy
  rw [Filter.EventuallyEq.mlieBracketWithin_vectorField_eq_of_mem EventuallyEq.rfl this hx,
    â† mpullback_mlieBracketWithin (J0V.mdifferentiableWithinAt (one_le_two.trans le_minSmoothness))
      _ hs contMDiffAt_extChartAt hx le_rfl pre_mem]; swap
  Â· apply ContMDiffWithinAt.mdifferentiableWithinAt _ le_rfl
    apply J0U.mlieBracketWithin_vectorField J0W (m := 1)
    Â· exact hs.uniqueMDiffOn_target_inter x
    Â· exact âŸ¨mem_extChartAt_target x, by simp [hx]âŸ©
    Â· exact le_rfl
  /- Now that everything is in pullback form, use the leibniz identity in the vector space -/
  rw [â† mpullback_add_apply, mpullback_apply, mpullback_apply]
  congr 1
  simp_rw [mlieBracketWithin_eq_lieBracketWithin]
  apply leibniz_identity_lieBracketWithin (E := E) le_rfl
  Â· exact hs.uniqueDiffOn_target_inter x
  Â· rw [inter_comm]
    exact extChartAt_mem_closure_interior h's (mem_extChartAt_source x)
  Â· exact âŸ¨mem_extChartAt_target x, by simp [hx]âŸ©
  Â· exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.mp J0U
  Â· exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.mp J0V
  Â· exact contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt.mp J0W

/-- The Lie bracket of vector fields in manifolds satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]` (also called Jacobi identity). -/
lemma leibniz_identity_mlieBracket_apply
    {U V W : Î  (x : M), TangentSpace I x} {x : M}
    (hU : ContMDiffAt I I.tangent (minSmoothness ğ•œ 2) (fun x â†¦ (U x : TangentBundle I M)) x)
    (hV : ContMDiffAt I I.tangent (minSmoothness ğ•œ 2) (fun x â†¦ (V x : TangentBundle I M)) x)
    (hW : ContMDiffAt I I.tangent (minSmoothness ğ•œ 2) (fun x â†¦ (W x : TangentBundle I M)) x) :
    mlieBracket I U (mlieBracket I V W) x =
      mlieBracket I (mlieBracket I U V) W x + mlieBracket I V (mlieBracket I U W) x := by
  simp only [â† mlieBracketWithin_univ, â† contMDiffWithinAt_univ] at hU hV hW âŠ¢
  exact leibniz_identity_mlieBracketWithin_apply uniqueMDiffOn_univ (by simp) (mem_univ _) hU hV hW

/-- The Lie bracket of vector fields in manifolds satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]` (also called Jacobi identity). -/
lemma leibniz_identity_mlieBracket
    {U V W : Î  (x : M), TangentSpace I x}
    (hU : ContMDiff I I.tangent (minSmoothness ğ•œ 2) (fun x â†¦ (U x : TangentBundle I M)))
    (hV : ContMDiff I I.tangent (minSmoothness ğ•œ 2) (fun x â†¦ (V x : TangentBundle I M)))
    (hW : ContMDiff I I.tangent (minSmoothness ğ•œ 2) (fun x â†¦ (W x : TangentBundle I M))) :
    mlieBracket I U (mlieBracket I V W) =
      mlieBracket I (mlieBracket I U V) W + mlieBracket I V (mlieBracket I U W) := by
  ext x
  exact leibniz_identity_mlieBracket_apply (hU x) (hV x) (hW x)

end Leibniz

end LieBracket

end VectorField

set_option linter.style.longFile 1700
