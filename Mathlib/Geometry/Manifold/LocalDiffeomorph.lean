/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/

import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Topology.IsLocalHomeomorph

/-!
# Local diffeomorphisms between manifolds

In this file, we define `C^n` local diffeomorphisms between manifolds.

A `C^n` map `f : M ‚Üí N` is a **local diffeomorphism at `x`** iff there are neighbourhoods `s`
and `t` of `x` and `f x`, respectively, such that `f` restricts to a diffeomorphism
between `s` and `t`. `f` is called a **local diffeomorphism on `s`** iff it is a local
diffeomorphism at every `x ‚àà s`, and a **local diffeomorphism** iff it is a local diffeomorphism on
`univ`.

## Main definitions
* `IsLocalDiffeomorphAt I J n f x`: `f` is a `C^n` local diffeomorphism at `x`
* `IsLocalDiffeomorphOn I J n f s`: `f` is a `C^n` local diffeomorphism on `s`
* `IsLocalDiffeomorph I J n f`: `f` is a `C^n` local diffeomorphism

## Main results
* Each of `Diffeomorph`, `IsLocalDiffeomorph`, `IsLocalDiffeomorphOn` and `IsLocalDiffeomorphAt`
  implies the next condition.
* `IsLocalDiffeomorph.isLocalHomeomorph`: a local diffeomorphism is a local homeomorphism,
  and similarly for a local diffeomorphism on `s`.
* `IsLocalDiffeomorph.isOpen_range`: the image of a local diffeomorphism is open
* `IsLocalDiffeomorph.diffeomorph_of_bijective`:
  a bijective local diffeomorphism is a diffeomorphism

* `Diffeomorph.mfderivToContinuousLinearEquiv`: each differential of a `C^n` diffeomorphism
  (`n ‚â• 1`) is a linear equivalence.
* `LocalDiffeomorphAt.mfderivToContinuousLinearEquiv`: if `f` is a local diffeomorphism
  at `x`, the differential `mfderiv I J n f x` is a continuous linear equivalence.
* `LocalDiffeomorph.mfderivToContinuousLinearEquiv`: if `f` is a local diffeomorphism,
  each differential `mfderiv I J n f x` is a continuous linear equivalence.

## TODO
* an injective local diffeomorphism is a diffeomorphism to its image
* if `f` is `C^n` at `x` and `mfderiv I J n f x` is a linear isomorphism,
  `f` is a local diffeomorphism at `x` (using the inverse function theorem).

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of local structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

## Tags
local diffeomorphism, manifold

-/

open Manifold Set TopologicalSpace

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {H : Type*} [TopologicalSpace H]
  {G : Type*} [TopologicalSpace G]
  (I : ModelWithCorners ùïú E H) (J : ModelWithCorners ùïú F G)
  (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] (n : WithTop ‚Ñï‚àû)

section PartialDiffeomorph
/-- A partial diffeomorphism on `s` is a function `f : M ‚Üí N` such that `f` restricts to a
diffeomorphism `s ‚Üí t` between open subsets of `M` and `N`, respectively.
This is an auxiliary definition and should not be used outside of this file. -/
structure PartialDiffeomorph extends PartialEquiv M N where
  open_source : IsOpen source
  open_target : IsOpen target
  contMDiffOn_toFun : ContMDiffOn I J n toFun source
  contMDiffOn_invFun : ContMDiffOn J I n invFun target

/-- Coercion of a `PartialDiffeomorph` to function.
Note that a `PartialDiffeomorph` is not `DFunLike` (like `PartialHomeomorph`),
as `toFun` doesn't determine `invFun` outside of `target`. -/
instance : CoeFun (PartialDiffeomorph I J M N n) fun _ => M ‚Üí N :=
  ‚ü®fun Œ¶ => Œ¶.toFun‚ü©

variable {I J M N n}

/-- A diffeomorphism is a partial diffeomorphism. -/
def Diffeomorph.toPartialDiffeomorph (h : Diffeomorph I J M N n) :
    PartialDiffeomorph I J M N n where
  toPartialEquiv := h.toHomeomorph.toPartialEquiv
  open_source := isOpen_univ
  open_target := isOpen_univ
  contMDiffOn_toFun x _ := h.contMDiff_toFun x
  contMDiffOn_invFun _ _ := h.symm.contMDiffWithinAt

-- Add the very basic API we need.
namespace PartialDiffeomorph
variable (Œ¶ : PartialDiffeomorph I J M N n)

/-- A partial diffeomorphism is also a local homeomorphism. -/
def toPartialHomeomorph : PartialHomeomorph M N where
  toPartialEquiv := Œ¶.toPartialEquiv
  open_source := Œ¶.open_source
  open_target := Œ¶.open_target
  continuousOn_toFun := Œ¶.contMDiffOn_toFun.continuousOn
  continuousOn_invFun := Œ¶.contMDiffOn_invFun.continuousOn

/-- The inverse of a local diffeomorphism. -/
protected def symm : PartialDiffeomorph J I N M n where
  toPartialEquiv := Œ¶.toPartialEquiv.symm
  open_source := Œ¶.open_target
  open_target := Œ¶.open_source
  contMDiffOn_toFun := Œ¶.contMDiffOn_invFun
  contMDiffOn_invFun := Œ¶.contMDiffOn_toFun

protected theorem contMDiffOn : ContMDiffOn I J n Œ¶ Œ¶.source :=
  Œ¶.contMDiffOn_toFun

protected theorem mdifferentiableOn (hn : 1 ‚â§ n) : MDifferentiableOn I J Œ¶ Œ¶.source :=
  (Œ¶.contMDiffOn).mdifferentiableOn hn

protected theorem mdifferentiableAt (hn : 1 ‚â§ n) {x : M} (hx : x ‚àà Œ¶.source) :
    MDifferentiableAt I J Œ¶ x :=
  (Œ¶.mdifferentiableOn hn x hx).mdifferentiableAt (Œ¶.open_source.mem_nhds hx)

/- We could add lots of additional API (following `Diffeomorph` and `PartialHomeomorph`), such as
* further continuity and differentiability lemmas
* refl and trans instances; lemmas between them.
As this declaration is meant for internal use only, we keep it simple. -/
end PartialDiffeomorph
end PartialDiffeomorph

variable {M N}

/-- `f : M ‚Üí N` is called a **`C^n` local diffeomorphism at *x*** iff there exist
open sets `U ‚àã x` and `V ‚àã f x` and a diffeomorphism `Œ¶ : U ‚Üí V` such that `f = Œ¶` on `U`. -/
def IsLocalDiffeomorphAt (f : M ‚Üí N) (x : M) : Prop :=
  ‚àÉ Œ¶ : PartialDiffeomorph I J M N n, x ‚àà Œ¶.source ‚àß EqOn f Œ¶ Œ¶.source

lemma PartialDiffeomorph.isLocalDiffeomorphAt (œÜ : PartialDiffeomorph I J M N n)
    {x : M} (hx : x ‚àà œÜ.source) : IsLocalDiffeomorphAt I J n œÜ x :=
  ‚ü®œÜ, hx, Set.eqOn_refl _ _‚ü©

namespace IsLocalDiffeomorphAt

variable {f : M ‚Üí N} {x : M}

variable {I I' J n}

/-- An arbitrary choice of local inverse of `f` near `x`. -/
noncomputable def localInverse (hf : IsLocalDiffeomorphAt I J n f x) :
    PartialDiffeomorph J I N M n := (Classical.choose hf).symm

lemma localInverse_open_source (hf : IsLocalDiffeomorphAt I J n f x) :
    IsOpen hf.localInverse.source :=
  PartialDiffeomorph.open_source _

lemma localInverse_mem_source (hf : IsLocalDiffeomorphAt I J n f x) :
    f x ‚àà hf.localInverse.source := by
  rw [(hf.choose_spec.2 hf.choose_spec.1)]
  exact (Classical.choose hf).map_source hf.choose_spec.1

lemma localInverse_mem_target (hf : IsLocalDiffeomorphAt I J n f x) :
    x ‚àà hf.localInverse.target :=
  hf.choose_spec.1

lemma contmdiffOn_localInverse (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffOn J I n hf.localInverse hf.localInverse.source :=
  hf.localInverse.contMDiffOn_toFun

lemma localInverse_right_inv (hf : IsLocalDiffeomorphAt I J n f x) {y : N}
    (hy : y ‚àà hf.localInverse.source) : f (hf.localInverse y) = y := by
  have : hf.localInverse y ‚àà hf.choose.source := by
    rw [‚Üê hf.choose.symm_target]
    exact hf.choose.symm.map_source hy
  rw [hf.choose_spec.2 this]
  exact hf.choose.right_inv hy

lemma localInverse_eqOn_right (hf : IsLocalDiffeomorphAt I J n f x) :
    EqOn (f ‚àò hf.localInverse) id hf.localInverse.source :=
  fun _y hy ‚Ü¶ hf.localInverse_right_inv hy

lemma localInverse_eventuallyEq_right (hf : IsLocalDiffeomorphAt I J n f x) :
    f ‚àò hf.localInverse =·∂†[nhds (f x)] id :=
  Filter.eventuallyEq_of_mem
    (hf.localInverse.open_source.mem_nhds hf.localInverse_mem_source)
    hf.localInverse_eqOn_right

lemma localInverse_left_inv (hf : IsLocalDiffeomorphAt I J n f x) {x' : M}
    (hx' : x' ‚àà hf.localInverse.target) : hf.localInverse (f x') = x' := by
  rw [hf.choose_spec.2 (hf.choose.symm_target ‚ñ∏ hx')]
  exact hf.choose.left_inv hx'

lemma localInverse_eqOn_left (hf : IsLocalDiffeomorphAt I J n f x) :
    EqOn (hf.localInverse ‚àò f) id hf.localInverse.target :=
  fun _ hx ‚Ü¶ hf.localInverse_left_inv hx

lemma localInverse_eventuallyEq_left (hf : IsLocalDiffeomorphAt I J n f x) :
    hf.localInverse ‚àò f =·∂†[nhds x] id :=
  Filter.eventuallyEq_of_mem
    (hf.localInverse.open_target.mem_nhds hf.localInverse_mem_target) hf.localInverse_eqOn_left

lemma localInverse_isLocalDiffeomorphAt (hf : IsLocalDiffeomorphAt I J n f x) :
    IsLocalDiffeomorphAt J I n (hf.localInverse) (f x) :=
  hf.localInverse.isLocalDiffeomorphAt _ _ _ hf.localInverse_mem_source

lemma localInverse_contMDiffOn (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffOn J I n hf.localInverse hf.localInverse.source :=
  hf.localInverse.contMDiffOn_toFun

lemma localInverse_contMDiffAt (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffAt J I n hf.localInverse (f x) :=
  hf.localInverse_contMDiffOn.contMDiffAt
    (hf.localInverse.open_source.mem_nhds hf.localInverse_mem_source)

lemma localInverse_mdifferentiableAt (hf : IsLocalDiffeomorphAt I J n f x) (hn : 1 ‚â§ n) :
    MDifferentiableAt J I hf.localInverse (f x) :=
  hf.localInverse_contMDiffAt.mdifferentiableAt hn

end IsLocalDiffeomorphAt

/-- `f : M ‚Üí N` is called a **`C^n` local diffeomorphism on *s*** iff it is a local diffeomorphism
at each `x : s`. -/
def IsLocalDiffeomorphOn (f : M ‚Üí N) (s : Set M) : Prop :=
  ‚àÄ x : s, IsLocalDiffeomorphAt I J n f x

/-- `f : M ‚Üí N` is a **`C^n` local diffeomorphism** iff it is a local diffeomorphism
at each `x ‚àà M`. -/
def IsLocalDiffeomorph (f : M ‚Üí N) : Prop :=
  ‚àÄ x : M, IsLocalDiffeomorphAt I J n f x

variable {I J n} in
lemma isLocalDiffeomorphOn_iff {f : M ‚Üí N} (s : Set M) :
    IsLocalDiffeomorphOn I J n f s ‚Üî ‚àÄ x : s, IsLocalDiffeomorphAt I J n f x := by rfl

variable {I J n} in
lemma isLocalDiffeomorph_iff {f : M ‚Üí N} :
    IsLocalDiffeomorph I J n f ‚Üî ‚àÄ x : M, IsLocalDiffeomorphAt I J n f x := by rfl

variable {I J n} in
theorem isLocalDiffeomorph_iff_isLocalDiffeomorphOn_univ {f : M ‚Üí N} :
    IsLocalDiffeomorph I J n f ‚Üî IsLocalDiffeomorphOn I J n f Set.univ :=
  ‚ü®fun hf x ‚Ü¶ hf x, fun hf x ‚Ü¶ hf ‚ü®x, trivial‚ü©‚ü©

variable {I J n} in
lemma IsLocalDiffeomorph.isLocalDiffeomorphOn
    {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J n f) (s : Set M) : IsLocalDiffeomorphOn I J n f s :=
  fun x ‚Ü¶ hf x

/-! # Basic properties of local diffeomorphisms -/
section Basic
variable {f : M ‚Üí N} {s : Set M} {x : M}
variable {I J n}

/-- A `C^n` local diffeomorphism at `x` is `C^n` differentiable at `x`. -/
lemma IsLocalDiffeomorphAt.contMDiffAt (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffAt I J n f x := by
  choose Œ¶ hx heq using hf
  -- In fact, even `ContMDiffOn I J n f Œ¶.source`.
  exact ((Œ¶.contMDiffOn_toFun).congr heq).contMDiffAt (Œ¶.open_source.mem_nhds hx)

/-- A local diffeomorphism at `x` is differentiable at `x`. -/
lemma IsLocalDiffeomorphAt.mdifferentiableAt (hf : IsLocalDiffeomorphAt I J n f x) (hn : 1 ‚â§ n) :
    MDifferentiableAt I J f x :=
  hf.contMDiffAt.mdifferentiableAt hn

/-- A `C^n` local diffeomorphism on `s` is `C^n` on `s`. -/
lemma IsLocalDiffeomorphOn.contMDiffOn (hf : IsLocalDiffeomorphOn I J n f s) :
    ContMDiffOn I J n f s :=
  fun x hx ‚Ü¶ (hf ‚ü®x, hx‚ü©).contMDiffAt.contMDiffWithinAt

/-- A local diffeomorphism on `s` is differentiable on `s`. -/
lemma IsLocalDiffeomorphOn.mdifferentiableOn (hf : IsLocalDiffeomorphOn I J n f s) (hn : 1 ‚â§ n) :
    MDifferentiableOn I J f s :=
  hf.contMDiffOn.mdifferentiableOn hn

/-- A `C^n` local diffeomorphism is `C^n`. -/
lemma IsLocalDiffeomorph.contMDiff (hf : IsLocalDiffeomorph I J n f) : ContMDiff I J n f :=
  fun x ‚Ü¶ (hf x).contMDiffAt

/-- A `C^n` local diffeomorphism is differentiable. -/
lemma IsLocalDiffeomorph.mdifferentiable (hf : IsLocalDiffeomorph I J n f) (hn : 1 ‚â§ n) :
    MDifferentiable I J f :=
  fun x ‚Ü¶ (hf x).mdifferentiableAt hn

/-- A `C^n` diffeomorphism is a local diffeomorphism. -/
lemma Diffeomorph.isLocalDiffeomorph (Œ¶ : M ‚âÉ‚Çò^n‚üÆI, J‚üØ N) : IsLocalDiffeomorph I J n Œ¶ :=
  fun _x ‚Ü¶ ‚ü®Œ¶.toPartialDiffeomorph, by trivial, eqOn_refl Œ¶ _‚ü©

-- FUTURE: if useful, also add "a `PartialDiffeomorph` is a local diffeomorphism on its source"

/-- A local diffeomorphism on `s` is a local homeomorphism on `s`. -/
theorem IsLocalDiffeomorphOn.isLocalHomeomorphOn {s : Set M} (hf : IsLocalDiffeomorphOn I J n f s) :
    IsLocalHomeomorphOn f s := by
  apply IsLocalHomeomorphOn.mk
  intro x hx
  choose U hyp using hf ‚ü®x, hx‚ü©
  exact ‚ü®U.toPartialHomeomorph, hyp‚ü©

/-- A local diffeomorphism is a local homeomorphism. -/
theorem IsLocalDiffeomorph.isLocalHomeomorph (hf : IsLocalDiffeomorph I J n f) :
    IsLocalHomeomorph f := by
  rw [isLocalHomeomorph_iff_isLocalHomeomorphOn_univ]
  rw [isLocalDiffeomorph_iff_isLocalDiffeomorphOn_univ] at hf
  exact hf.isLocalHomeomorphOn

/-- A local diffeomorphism is an open map. -/
lemma IsLocalDiffeomorph.isOpenMap (hf : IsLocalDiffeomorph I J n f) : IsOpenMap f :=
  (hf.isLocalHomeomorph).isOpenMap

/-- A local diffeomorphism has open range. -/
lemma IsLocalDiffeomorph.isOpen_range (hf : IsLocalDiffeomorph I J n f) : IsOpen (range f) :=
  (hf.isOpenMap).isOpen_range

/-- The image of a local diffeomorphism is open. -/
def IsLocalDiffeomorph.image (hf : IsLocalDiffeomorph I J n f) : Opens N :=
  ‚ü®range f, hf.isOpen_range‚ü©

lemma IsLocalDiffeomorph.image_coe (hf : IsLocalDiffeomorph I J n f) : hf.image.1 = range f :=
  rfl

-- TODO: this result holds more generally for (local) structomorphisms
-- This argument implies a `LocalDiffeomorphOn f s` for `s` open is a `PartialDiffeomorph`

/-- A bijective local diffeomorphism is a diffeomorphism. -/
noncomputable def IsLocalDiffeomorph.diffeomorph_of_bijective
    (hf : IsLocalDiffeomorph I J n f) (hf' : Function.Bijective f) : Diffeomorph I J M N n := by
  -- Choose a right inverse `g` of `f`.
  choose g hgInverse using (Function.bijective_iff_has_inverse).mp hf'
  -- Choose diffeomorphisms œÜ_x which coincide which `f` near `x`.
  choose Œ¶ hyp using (fun x ‚Ü¶ hf x)
  -- Two such diffeomorphisms (and their inverses!) coincide on their sources:
  -- they're both inverses to g. In fact, the latter suffices for our proof.
  -- have (x y) : EqOn (Œ¶ x).symm (Œ¶ y).symm ((Œ¶ x).target ‚à© (Œ¶ y).target) := sorry
  have aux (x) : EqOn g (Œ¶ x).symm (Œ¶ x).target :=
    eqOn_of_leftInvOn_of_rightInvOn (fun x' _ ‚Ü¶ hgInverse.1 x')
      (LeftInvOn.congr_left ((Œ¶ x).toPartialHomeomorph).rightInvOn
        ((Œ¶ x).toPartialHomeomorph).symm_mapsTo (hyp x).2.symm)
      (fun _y hy ‚Ü¶ (Œ¶ x).map_target hy)
  exact {
    toFun := f
    invFun := g
    left_inv := hgInverse.1
    right_inv := hgInverse.2
    contMDiff_toFun := hf.contMDiff
    contMDiff_invFun := by
      intro y
      let x := g y
      obtain ‚ü®hx, hfx‚ü© := hyp x
      apply ((Œ¶ x).symm.contMDiffOn.congr (aux x)).contMDiffAt (((Œ¶ x).open_target).mem_nhds ?_)
      have : y = (Œ¶ x) x := ((hgInverse.2 y).congr (hfx hx)).mp rfl
      exact this ‚ñ∏ (Œ¶ x).map_source hx }

@[deprecated (since := "2025-03-24")] alias
IslocalDiffeomorph.diffeomorph_of_bijective := IsLocalDiffeomorph.diffeomorph_of_bijective

end Basic

section Differential

variable {f : M ‚Üí N} {s : Set M} {x : M}

variable {I I' J n}

/-- If `f` is a `C^n` local diffeomorphism at `x`, for `n ‚â• 1`, the differential `df_x`
is a linear equivalence. -/
noncomputable def IsLocalDiffeomorphAt.mfderivToContinuousLinearEquiv
    (hf : IsLocalDiffeomorphAt I J n f x) (hn : 1 ‚â§ n) :
    TangentSpace I x ‚âÉL[ùïú] TangentSpace J (f x) where
  toFun := mfderiv I J f x
  invFun := mfderiv J I hf.localInverse (f x)
  left_inv := by
    apply ContinuousLinearMap.leftInverse_of_comp
    rw [‚Üê mfderiv_id, ‚Üê hf.localInverse_eventuallyEq_left.mfderiv_eq]
    exact (mfderiv_comp _ (hf.localInverse_mdifferentiableAt hn) (hf.mdifferentiableAt hn)).symm
  right_inv := by
    apply ContinuousLinearMap.rightInverse_of_comp
    rw [‚Üê mfderiv_id, ‚Üê hf.localInverse_eventuallyEq_right.mfderiv_eq]
    -- We need to rewrite the base point hf.localInverse (f x) = x twice,
    -- in the differentiability hypothesis and for applying the chain rule.
    have hf' : MDifferentiableAt I J f (hf.localInverse (f x)) := by
      rw [hf.localInverse_left_inv hf.localInverse_mem_target]
      exact hf.mdifferentiableAt hn
    rw [mfderiv_comp _ hf' (hf.localInverse_mdifferentiableAt hn),
      hf.localInverse_left_inv hf.localInverse_mem_target]
  continuous_toFun := (mfderiv I J f x).cont
  continuous_invFun := (mfderiv J I hf.localInverse (f x)).cont
  map_add' := fun x_1 y ‚Ü¶ ContinuousLinearMap.map_add _ x_1 y
  map_smul' := by intros; simp

@[simp, mfld_simps]
lemma IsLocalDiffeomorphAt.mfderivToContinuousLinearEquiv_coe
    (hf : IsLocalDiffeomorphAt I J n f x) (hn : 1 ‚â§ n) :
    hf.mfderivToContinuousLinearEquiv hn = mfderiv I J f x := rfl

/-- Each differential of a `C^n` diffeomorphism of Banach manifolds (`n ‚â• 1`)
is a linear equivalence. -/
noncomputable def Diffeomorph.mfderivToContinuousLinearEquiv
    (Œ¶ : M ‚âÉ‚Çò^n‚üÆI, J‚üØ N) (hn : 1 ‚â§ n) (x : M) :
    TangentSpace I x ‚âÉL[ùïú] TangentSpace J (Œ¶ x) :=
  (Œ¶.isLocalDiffeomorph x).mfderivToContinuousLinearEquiv hn

lemma Diffeomorph.mfderivToContinuousLinearEquiv_coe (Œ¶ : M ‚âÉ‚Çò^n‚üÆI, J‚üØ N) (hn : 1 ‚â§ n) :
    Œ¶.mfderivToContinuousLinearEquiv hn x = mfderiv I J Œ¶ x := by rfl

/-- If `f` is a `C^n` local diffeomorphism of Banach manifolds (`n ‚â• 1`),
each differential is a linear equivalence. -/
noncomputable def IsLocalDiffeomorph.mfderivToContinuousLinearEquiv
    (hf : IsLocalDiffeomorph I J n f) (hn : 1 ‚â§ n) (x : M) :
    TangentSpace I x ‚âÉL[ùïú] TangentSpace J (f x) :=
  (hf x).mfderivToContinuousLinearEquiv hn

lemma IsLocalDiffeomorph.mfderivToContinuousLinearEquiv_coe
    (hf : IsLocalDiffeomorph I J n f) (hn : 1 ‚â§ n) (x : M) :
    hf.mfderivToContinuousLinearEquiv hn x = mfderiv I J f x :=
  (hf x).mfderivToContinuousLinearEquiv_coe hn

end Differential
