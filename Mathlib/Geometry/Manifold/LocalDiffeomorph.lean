/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/

import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Geometry.Manifold.MFDeriv

/-!
# Local diffeomorphisms between smooth manifolds

In this file, we define `C^n` local diffeomorphisms between manifolds.

A `C^n` map `f : M ‚Üí N` is a **local diffeomorphism at `x`** iff there are neighbourhoods `s`
and `t` of `x` and `f x`, respectively such that `f` restricts to a diffeomorphism from `s` and `t`.
`f` is called a **local diffeomorphism** iff it is a local diffeomorphism at every `x ‚àà M`.

## Main definitions
* `IsLocalDiffeomorphAt I J n f x`: `f` is a `C^n` local diffeomorphism at `x`
* `IsLocalDiffeomorph I J n f`: `f` is a `C^n` local diffeomorphism

## Main results
* Each of `Diffeomorph`, `IsLocalDiffeomorph`, and `IsLocalDiffeomorphAt` implies the next.
* `LocalDiffeomorph.isOpen_range`: the image of a local diffeomorphism is open
* `Diffeomorph.of_bijective_isLocalDiffeomorph`:
  a bijective local diffeomorphism is a diffeomorphism.

## TODO
* a local diffeomorphisms is a local homeomorphism
* an injective local diffeomorphism is a diffeomorphism to its image
* each differential of a `C^n` diffeomorphism (`n ‚â• 1`) is a linear equivalence.
* if `f` is a local diffeomorphism at `x`, the differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* conversely, if `f` is `C^n` at `x` and `mfderiv I J n f x` is a linear isomorphism,
`f` is a local diffeomorphism at `x`.
* if `f` is a local diffeomorphism, each differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* Conversely, if `f` is `C^n` and each differential is a linear isomorphism,
`f` is a local diffeomorphism.

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of local structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

## Tags
local diffeomorphism, manifold

-/

open Manifold Set TopologicalSpace

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {H : Type*} [TopologicalSpace H]
  {G : Type*} [TopologicalSpace G]
  (I : ModelWithCorners ùïú E H) (J : ModelWithCorners ùïú F G)
  (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] (n : ‚Ñï‚àû)

section PartialDiffeomorph
/-- A partial diffeomorphism on `s` is a function `f : M ‚Üí N` such that `f` restricts to a
diffeomorphism `s ‚Üí t` between open subsets of `M` and `N`, respectively.
This is an auxiliary definition and should not be used outside of this file. -/
structure PartialDiffeomorph extends PartialEquiv M N where
  open_source : IsOpen source
  open_target : IsOpen target
  contMDiffOn_toFun : ContMDiffOn I J n toFun source
  contMDiffOn_invFun : ContMDiffOn J I n invFun target

/-- Coercion of a `PartialDiffeomorph` to function.
Note that a `PartialDiffeomorph` is not `FunLike` (like `PartialHomeomorph`),
as `toFun` doesn't determine `invFun` outside of `target`. -/
instance : CoeFun (PartialDiffeomorph I J M N n) fun _ => M ‚Üí N :=
  ‚ü®fun Œ¶ => Œ¶.toFun‚ü©

-- Add the very basic API we need.
namespace PartialDiffeomorph
variable (Œ¶ : PartialDiffeomorph I J M N n) (hn : 1 ‚â§ n)

/-- A partial diffeomorphism is also a local homeomorphism. -/
def toPartialHomeomorph : PartialHomeomorph M N :=
  {
    toPartialEquiv := Œ¶.toPartialEquiv
    open_source := Œ¶.open_source
    open_target := Œ¶.open_target
    continuousOn_toFun := Œ¶.contMDiffOn_toFun.continuousOn
    continuousOn_invFun := Œ¶.contMDiffOn_invFun.continuousOn
  }

/-- The inverse of a local diffeomorphism. -/
protected def symm : PartialDiffeomorph J I N M n :=
  {
    toPartialEquiv := Œ¶.toPartialEquiv.symm
    open_source := Œ¶.open_target
    open_target := Œ¶.open_source
    contMDiffOn_toFun := Œ¶.contMDiffOn_invFun
    contMDiffOn_invFun := Œ¶.contMDiffOn_toFun
  }

protected theorem contMDiffOn : ContMDiffOn I J n Œ¶ Œ¶.source :=
  Œ¶.contMDiffOn_toFun

protected theorem contMDiffOn_symm : ContMDiffOn J I n Œ¶.symm Œ¶.target :=
  Œ¶.contMDiffOn_invFun

protected theorem mdifferentiableOn : MDifferentiableOn I J Œ¶ Œ¶.source :=
  (Œ¶.contMDiffOn).mdifferentiableOn hn

protected theorem mdifferentiableOn_symm : MDifferentiableOn J I Œ¶.symm Œ¶.target :=
  (Œ¶.symm).mdifferentiableOn hn

protected theorem mdifferentiableAt {x : M} (hx : x ‚àà Œ¶.source) : MDifferentiableAt I J Œ¶ x :=
  (Œ¶.mdifferentiableOn hn x hx).mdifferentiableAt (Œ¶.open_source.mem_nhds hx)

protected theorem mdifferentiableAt_symm {x : M} (hx : x ‚àà Œ¶.source) :
    MDifferentiableAt J I Œ¶.symm (Œ¶ x) :=
  (Œ¶.symm).mdifferentiableAt hn (Œ¶.map_source hx)

/- We could add lots of additional API (following `Diffeomorph` and `PartialHomeomorph`), such as
* further continuity and differentiability lemmas
* refl and trans instances; lemmas between them.
As this declaration is meant for internal use only, we keep it simple. -/
end PartialDiffeomorph
end PartialDiffeomorph

variable {M N}

/-- `f : M ‚Üí N` is called a **`C^n` local diffeomorphism at *x*** iff there exist
  open sets `U ‚àã x` and `V ‚àã f x` and a diffeomorphism `Œ¶ : U ‚Üí V` such that `f = Œ¶` on `U`. -/
def IsLocalDiffeomorphAt (f : M ‚Üí N) (x : M) : Prop :=
  ‚àÉ Œ¶ : PartialDiffeomorph I J M N n, x ‚àà Œ¶.source ‚àß EqOn f Œ¶ Œ¶.source

/-- `f : M ‚Üí N` is a **`C^n` local diffeomorphism** iff it is a local diffeomorphism
at each `x ‚àà M`. -/
def IsLocalDiffeomorph (f : M ‚Üí N) : Prop :=
  ‚àÄ x : M, IsLocalDiffeomorphAt I J n f x

lemma isLocalDiffeomorph_iff {f : M ‚Üí N} :
    IsLocalDiffeomorph I J n f ‚Üî ‚àÄ x : M, IsLocalDiffeomorphAt I J n f x := by rfl

variable {n}

/-! # Basic properties of local diffeomorphisms -/
section Basic
variable {f : M ‚Üí N} {x : M}

/-- A `C^n` local diffeomorphism at `x` is `C^n` differentiable at `x`. -/
lemma contMDiffAt_of_isLocalDiffeomorphAt (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffAt I J n f x := by
  choose Œ¶ hx heq using hf
  -- In fact, even `ContMDiffOn I J n f Œ¶.source`.
  exact ((Œ¶.contMDiffOn_toFun).congr heq).contMDiffAt (Œ¶.open_source.mem_nhds hx)

/-- A local diffeomorphism at `x` is differentiable at `x`. -/
lemma mdifferentiableAt_of_isLocalDiffeomorphAt (hn : 1 ‚â§ n) (hf : IsLocalDiffeomorphAt I J n f x) :
    MDifferentiableAt I J f x :=
  (contMDiffAt_of_isLocalDiffeomorphAt I J hf).mdifferentiableAt hn

/-- A `C^n` local diffeomorphism is `C^n`. -/
lemma contMDiff_of_isLocalDiffeomorph (hf : IsLocalDiffeomorph I J n f) : ContMDiff I J n f :=
  fun x ‚Ü¶ contMDiffAt_of_isLocalDiffeomorphAt I J (hf x)

/-- A `C^n` local diffeomorphism is differentiable. -/
lemma mdifferentiable_of_isLocalDiffeomorph (hn : 1 ‚â§ n) (hf : IsLocalDiffeomorph I J n f) :
    MDifferentiable I J f :=
  fun x ‚Ü¶ mdifferentiableAt_of_isLocalDiffeomorphAt I J hn (hf x)

/-- A diffeomorphism is a partial diffeomorphism. -/
def Diffeomorph.toPartialDiffeomorph (h : Diffeomorph I J M N n) : PartialDiffeomorph I J M N n :=
  {
    toPartialEquiv := h.toHomeomorph.toPartialEquiv
    open_source := isOpen_univ
    open_target := isOpen_univ
    contMDiffOn_toFun := fun x _ ‚Ü¶ h.contMDiff_toFun x
    contMDiffOn_invFun := fun _ _ ‚Ü¶ h.symm.contMDiffWithinAt
  }

/-- A `C^n` diffeomorphism is a local diffeomorphism. -/
lemma Diffeomorph.isLocalDiffeomorph (Œ¶ : M ‚âÉ‚Çò^n‚üÆI, J‚üØ N) : IsLocalDiffeomorph I J n Œ¶ :=
  fun _x ‚Ü¶ ‚ü®Œ¶.toPartialDiffeomorph, by trivial, eqOn_refl Œ¶ _‚ü©

-- TODO: golf this using IsLocalHomeomorph.isOpenMap,
-- after showing that local diffeosmorphisms are local homeomorphisms
/-- A local diffeomorphism has open range. -/
lemma LocalDiffeomorph.isOpen_range {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J n f) :
    IsOpen (range f) := by
  apply isOpen_iff_forall_mem_open.mpr
  intro y hy

  -- Given `y = f x ‚àà range f`, we need to find `V ‚äÜ N` open containing `y`.
  rw [mem_range] at hy
  rcases hy with ‚ü®x, hxy‚ü©

  -- As f is a local diffeo at x, on some open set `U' ‚àã x` it agrees with a diffeo `Œ¶ : U' ‚Üí V'`.
  choose Œ¶ hyp using hf x
  rcases hyp with ‚ü®hxU, heq‚ü©
  -- Then `V:=Œ¶.target` has the desired properties.
  refine ‚ü®Œ¶.target, ?_, Œ¶.open_target, ?_‚ü©
  ¬∑ rw [‚Üê PartialEquiv.image_source_eq_target, ‚Üê heq.image_eq]
    exact image_subset_range f Œ¶.source
  ¬∑ rw [‚Üê hxy, heq hxU]
    exact Œ¶.map_source' hxU

/-- The image of a local diffeomorphism is open. -/
def LocalDiffeomorph.image {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J n f) : Opens N :=
  ‚ü®range f, isOpen_range I J hf‚ü©

lemma LocalDiffeomorph.image_coe {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J n f) :
    (LocalDiffeomorph.image I J hf).1 = range f := rfl

/-- A bijective local diffeomorphism is a diffeomorphism. -/
noncomputable def Diffeomorph.of_bijective_isLocalDiffeomorph {f : M ‚Üí N}
    (hf' : Function.Bijective f) (hf : IsLocalDiffeomorph I J n f) : Diffeomorph I J M N n := by
  -- Choose a right inverse `g` of `f`.
  choose g hgInverse using (Function.bijective_iff_has_inverse).mp hf'
   -- Choose diffeomorphisms œÜ_x which coincide which `f` near `x`.
  choose Œ¶ hyp using (fun x ‚Ü¶ hf x)
  -- Two such diffeomorphisms (and their inverses!) coincide on their sources:
  -- they're both inverses to g. In fact, the latter suffices for our proof.
  -- have : ‚àÄ x y, EqOn (Œ¶ x).symm (Œ¶ y).symm ((Œ¶ x).target ‚à© (Œ¶ y).target) := sorry
  have aux : ‚àÄ x, EqOn g (Œ¶ x).symm (Œ¶ x).target :=
    fun x ‚Ü¶ eqOn_of_leftInvOn_of_rightInvOn (fun x' _ ‚Ü¶ hgInverse.1 x')
      (LeftInvOn.congr_left ((Œ¶ x).toPartialHomeomorph).rightInvOn
        ((Œ¶ x).toPartialHomeomorph).symm_mapsTo (hyp x).2.symm)
      (fun _y hy ‚Ü¶(Œ¶ x).map_target hy)
  exact {
    toFun := f
    invFun := g
    left_inv := hgInverse.1
    right_inv := hgInverse.2
    contMDiff_toFun := contMDiff_of_isLocalDiffeomorph I J hf
    contMDiff_invFun := by
      intro y
      let x := g y
      obtain ‚ü®hx, hfx‚ü© := hyp x
      apply ((PartialDiffeomorph.contMDiffOn J I N M n (Œ¶ x).symm).congr (aux x)).contMDiffAt
      apply (((Œ¶ x).open_target).mem_nhds ?_)
      have : y = (Œ¶ x) x := (Eq.congr (hgInverse.2 y) (hfx hx)).mp rfl
      exact this ‚ñ∏ (Œ¶ x).map_source hx
  }

end Basic
