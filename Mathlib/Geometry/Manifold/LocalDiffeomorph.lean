/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/

import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Topology.IsLocalHomeomorph

/-!
# Local diffeomorphisms between manifolds

In this file, we define `C^n` local diffeomorphisms between manifolds.

A `C^n` map `f : M â†’ N` is a **local diffeomorphism at `x`** iff there are neighbourhoods `s`
and `t` of `x` and `f x`, respectively such that `f` restricts to a diffeomorphism
between `s` and `t`. `f` is called a **local diffeomorphism on s** iff it is a local diffeomorphism
at every `x âˆˆ s`, and a **local diffeomorphism** iff it is a local diffeomorphism on `univ`.

## Main definitions
* `IsLocalDiffeomorphAt I J n f x`: `f` is a `C^n` local diffeomorphism at `x`
* `IsLocalDiffeomorphOn I J n f s`: `f` is a `C^n` local diffeomorphism on `s`
* `IsLocalDiffeomorph I J n f`: `f` is a `C^n` local diffeomorphism

## Main results
* Each of `Diffeomorph`, `IsLocalDiffeomorph`, `IsLocalDiffeomorphOn` and `IsLocalDiffeomorphAt`
  implies the next.
* `IsLocalDiffeomorph.isLocalHomeomorph`: a local diffeomorphisms is a local homeomorphism,
  similarly for local diffeomorphism on `s`
* `IsLocalDiffeomorph.isOpen_range`: the image of a local diffeomorphism is open
* `IslocalDiffeomorph.diffeomorph_of_bijective`:
  a bijective local diffeomorphism is a diffeomorphism

## TODO
* an injective local diffeomorphism is a diffeomorphism to its image
* each differential of a `C^n` diffeomorphism (`n â‰¥ 1`) is a linear equivalence.
* if `f` is a local diffeomorphism at `x`, the differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* conversely, if `f` is `C^n` at `x` and `mfderiv I J n f x` is a linear isomorphism,
`f` is a local diffeomorphism at `x`.
* if `f` is a local diffeomorphism, each differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* Conversely, if `f` is `C^n` and each differential is a linear isomorphism,
`f` is a local diffeomorphism.

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of local structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

## Tags
local diffeomorphism, manifold

-/

open Manifold Set TopologicalSpace

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ð•œ F]
  {H : Type*} [TopologicalSpace H]
  {G : Type*} [TopologicalSpace G]
  (I : ModelWithCorners ð•œ E H) (J : ModelWithCorners ð•œ F G)
  (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] (n : â„•âˆž)

section PartialDiffeomorph
/-- A partial diffeomorphism on `s` is a function `f : M â†’ N` such that `f` restricts to a
diffeomorphism `s â†’ t` between open subsets of `M` and `N`, respectively.
This is an auxiliary definition and should not be used outside of this file. -/
structure PartialDiffeomorph extends PartialEquiv M N where
  open_source : IsOpen source
  open_target : IsOpen target
  contMDiffOn_toFun : ContMDiffOn I J n toFun source
  contMDiffOn_invFun : ContMDiffOn J I n invFun target

/-- Coercion of a `PartialDiffeomorph` to function.
Note that a `PartialDiffeomorph` is not `DFunLike` (like `PartialHomeomorph`),
as `toFun` doesn't determine `invFun` outside of `target`. -/
instance : CoeFun (PartialDiffeomorph I J M N n) fun _ => M â†’ N :=
  âŸ¨fun Î¦ => Î¦.toFunâŸ©

variable {I J M N n}

/-- A diffeomorphism is a partial diffeomorphism. -/
def Diffeomorph.toPartialDiffeomorph (h : Diffeomorph I J M N n) :
    PartialDiffeomorph I J M N n where
  toPartialEquiv := h.toHomeomorph.toPartialEquiv
  open_source := isOpen_univ
  open_target := isOpen_univ
  contMDiffOn_toFun x _ := h.contMDiff_toFun x
  contMDiffOn_invFun _ _ := h.symm.contMDiffWithinAt

-- Add the very basic API we need.
namespace PartialDiffeomorph
variable (Î¦ : PartialDiffeomorph I J M N n) (hn : 1 â‰¤ n)

/-- A partial diffeomorphism is also a local homeomorphism. -/
def toPartialHomeomorph : PartialHomeomorph M N where
  toPartialEquiv := Î¦.toPartialEquiv
  open_source := Î¦.open_source
  open_target := Î¦.open_target
  continuousOn_toFun := Î¦.contMDiffOn_toFun.continuousOn
  continuousOn_invFun := Î¦.contMDiffOn_invFun.continuousOn

/-- The inverse of a local diffeomorphism. -/
protected def symm : PartialDiffeomorph J I N M n where
  toPartialEquiv := Î¦.toPartialEquiv.symm
  open_source := Î¦.open_target
  open_target := Î¦.open_source
  contMDiffOn_toFun := Î¦.contMDiffOn_invFun
  contMDiffOn_invFun := Î¦.contMDiffOn_toFun

protected theorem contMDiffOn : ContMDiffOn I J n Î¦ Î¦.source :=
  Î¦.contMDiffOn_toFun

protected theorem mdifferentiableOn : MDifferentiableOn I J Î¦ Î¦.source :=
  (Î¦.contMDiffOn).mdifferentiableOn hn

protected theorem mdifferentiableAt {x : M} (hx : x âˆˆ Î¦.source) : MDifferentiableAt I J Î¦ x :=
  (Î¦.mdifferentiableOn hn x hx).mdifferentiableAt (Î¦.open_source.mem_nhds hx)

/- We could add lots of additional API (following `Diffeomorph` and `PartialHomeomorph`), such as
* further continuity and differentiability lemmas
* refl and trans instances; lemmas between them.
As this declaration is meant for internal use only, we keep it simple. -/
end PartialDiffeomorph
end PartialDiffeomorph

variable {M N}

/-- `f : M â†’ N` is called a **`C^n` local diffeomorphism at *x*** iff there exist
  open sets `U âˆ‹ x` and `V âˆ‹ f x` and a diffeomorphism `Î¦ : U â†’ V` such that `f = Î¦` on `U`. -/
def IsLocalDiffeomorphAt (f : M â†’ N) (x : M) : Prop :=
  âˆƒ Î¦ : PartialDiffeomorph I J M N n, x âˆˆ Î¦.source âˆ§ EqOn f Î¦ Î¦.source

/-- `f : M â†’ N` is called a **`C^n` local diffeomorphism on *s*** iff it is a local diffeomorphism
  at each `x : s`. -/
def IsLocalDiffeomorphOn (f : M â†’ N) (s : Set M) : Prop :=
  âˆ€ x : s, IsLocalDiffeomorphAt I J n f x

/-- `f : M â†’ N` is a **`C^n` local diffeomorphism** iff it is a local diffeomorphism
at each `x âˆˆ M`. -/
def IsLocalDiffeomorph (f : M â†’ N) : Prop :=
  âˆ€ x : M, IsLocalDiffeomorphAt I J n f x

variable {I J n} in
lemma isLocalDiffeomorphOn_iff {f : M â†’ N} (s : Set M) :
    IsLocalDiffeomorphOn I J n f s â†” âˆ€ x : s, IsLocalDiffeomorphAt I J n f x := by rfl

variable {I J n} in
lemma isLocalDiffeomorph_iff {f : M â†’ N} :
    IsLocalDiffeomorph I J n f â†” âˆ€ x : M, IsLocalDiffeomorphAt I J n f x := by rfl

variable {I J n} in
theorem isLocalDiffeomorph_iff_isLocalDiffeomorphOn_univ {f : M â†’ N} :
    IsLocalDiffeomorph I J n f â†” IsLocalDiffeomorphOn I J n f Set.univ :=
  âŸ¨fun hf x â†¦ hf x, fun hf x â†¦ hf âŸ¨x, trivialâŸ©âŸ©

variable {I J n} in
lemma IsLocalDiffeomorph.isLocalDiffeomorphOn
    {f : M â†’ N} (hf : IsLocalDiffeomorph I J n f) (s : Set M) : IsLocalDiffeomorphOn I J n f s :=
  fun x â†¦ hf x

/-! # Basic properties of local diffeomorphisms -/
section Basic
variable {f : M â†’ N} {s : Set M} {x : M}
variable {I J n}

/-- A `C^n` local diffeomorphism at `x` is `C^n` differentiable at `x`. -/
lemma IsLocalDiffeomorphAt.contMDiffAt (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffAt I J n f x := by
  choose Î¦ hx heq using hf
  -- In fact, even `ContMDiffOn I J n f Î¦.source`.
  exact ((Î¦.contMDiffOn_toFun).congr heq).contMDiffAt (Î¦.open_source.mem_nhds hx)

/-- A local diffeomorphism at `x` is differentiable at `x`. -/
lemma IsLocalDiffeomorphAt.mdifferentiableAt (hf : IsLocalDiffeomorphAt I J n f x) (hn : 1 â‰¤ n) :
    MDifferentiableAt I J f x :=
  hf.contMDiffAt.mdifferentiableAt hn

/-- A `C^n` local diffeomorphism on `s` is `C^n` on `s`. -/
lemma IsLocalDiffeomorphOn.contMDiffOn (hf : IsLocalDiffeomorphOn I J n f s) :
    ContMDiffOn I J n f s :=
  fun x hx â†¦ (hf âŸ¨x, hxâŸ©).contMDiffAt.contMDiffWithinAt

/-- A local diffeomorphism on `s` is differentiable on `s`. -/
lemma IsLocalDiffeomorphOn.mdifferentiableOn (hf : IsLocalDiffeomorphOn I J n f s) (hn : 1 â‰¤ n) :
    MDifferentiableOn I J f s :=
  hf.contMDiffOn.mdifferentiableOn hn

/-- A `C^n` local diffeomorphism is `C^n`. -/
lemma IsLocalDiffeomorph.contMDiff (hf : IsLocalDiffeomorph I J n f) : ContMDiff I J n f :=
  fun x â†¦ (hf x).contMDiffAt

/-- A `C^n` local diffeomorphism is differentiable. -/
lemma IsLocalDiffeomorph.mdifferentiable (hf : IsLocalDiffeomorph I J n f) (hn : 1 â‰¤ n) :
    MDifferentiable I J f :=
  fun x â†¦ (hf x).mdifferentiableAt hn

/-- A `C^n` diffeomorphism is a local diffeomorphism. -/
lemma Diffeomorph.isLocalDiffeomorph (Î¦ : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : IsLocalDiffeomorph I J n Î¦ :=
  fun _x â†¦ âŸ¨Î¦.toPartialDiffeomorph, by trivial, eqOn_refl Î¦ _âŸ©

-- FUTURE: if useful, also add "a `PartialDiffeomorph` is a local diffeomorphism on its source"

/-- A local diffeomorphism on `s` is a local homeomorphism on `s`. -/
theorem IsLocalDiffeomorphOn.isLocalHomeomorphOn {s : Set M} (hf : IsLocalDiffeomorphOn I J n f s):
    IsLocalHomeomorphOn f s := by
  apply IsLocalHomeomorphOn.mk
  intro x hx
  choose U hyp using hf âŸ¨x, hxâŸ©
  exact âŸ¨U.toPartialHomeomorph, hypâŸ©

/-- A local diffeomorphism is a local homeomorphism. -/
theorem IsLocalDiffeomorph.isLocalHomeomorph (hf : IsLocalDiffeomorph I J n f) :
    IsLocalHomeomorph f := by
  rw [isLocalHomeomorph_iff_isLocalHomeomorphOn_univ]
  rw [isLocalDiffeomorph_iff_isLocalDiffeomorphOn_univ] at hf
  exact hf.isLocalHomeomorphOn

/-- A local diffeomorphism is an open map. -/
lemma IsLocalDiffeomorph.isOpenMap (hf : IsLocalDiffeomorph I J n f) : IsOpenMap f :=
  (hf.isLocalHomeomorph).isOpenMap

/-- A local diffeomorphism has open range. -/
lemma IsLocalDiffeomorph.isOpen_range (hf : IsLocalDiffeomorph I J n f) : IsOpen (range f) :=
  (hf.isOpenMap).isOpen_range

/-- The image of a local diffeomorphism is open. -/
def IsLocalDiffeomorph.image (hf : IsLocalDiffeomorph I J n f) : Opens N :=
  âŸ¨range f, hf.isOpen_rangeâŸ©

lemma IsLocalDiffeomorph.image_coe (hf : IsLocalDiffeomorph I J n f) : hf.image.1 = range f :=
  rfl

-- TODO: this result holds more generally for (local) structomorphisms
-- This argument implies a `LocalDiffeomorphOn f s` for `s` open is a `PartialDiffeomorph`

/-- A bijective local diffeomorphism is a diffeomorphism. -/
noncomputable def IslocalDiffeomorph.diffeomorph_of_bijective
    (hf : IsLocalDiffeomorph I J n f) (hf' : Function.Bijective f) : Diffeomorph I J M N n := by
  -- Choose a right inverse `g` of `f`.
  choose g hgInverse using (Function.bijective_iff_has_inverse).mp hf'
   -- Choose diffeomorphisms Ï†_x which coincide which `f` near `x`.
  choose Î¦ hyp using (fun x â†¦ hf x)
  -- Two such diffeomorphisms (and their inverses!) coincide on their sources:
  -- they're both inverses to g. In fact, the latter suffices for our proof.
  -- have (x y) : EqOn (Î¦ x).symm (Î¦ y).symm ((Î¦ x).target âˆ© (Î¦ y).target) := sorry
  have aux (x) : EqOn g (Î¦ x).symm (Î¦ x).target :=
    eqOn_of_leftInvOn_of_rightInvOn (fun x' _ â†¦ hgInverse.1 x')
      (LeftInvOn.congr_left ((Î¦ x).toPartialHomeomorph).rightInvOn
        ((Î¦ x).toPartialHomeomorph).symm_mapsTo (hyp x).2.symm)
      (fun _y hy â†¦ (Î¦ x).map_target hy)
  exact {
    toFun := f
    invFun := g
    left_inv := hgInverse.1
    right_inv := hgInverse.2
    contMDiff_toFun := hf.contMDiff
    contMDiff_invFun := by
      intro y
      let x := g y
      obtain âŸ¨hx, hfxâŸ© := hyp x
      apply ((Î¦ x).symm.contMDiffOn.congr (aux x)).contMDiffAt (((Î¦ x).open_target).mem_nhds ?_)
      have : y = (Î¦ x) x := ((hgInverse.2 y).congr (hfx hx)).mp rfl
      exact this â–¸ (Î¦ x).map_source hx }

end Basic
