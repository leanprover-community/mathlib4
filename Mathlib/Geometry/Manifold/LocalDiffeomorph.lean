/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/

import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Topology.IsLocalHomeomorph

/-!
# Local diffeomorphisms between manifolds

In this file, we define `C^n` local diffeomorphisms between manifolds.

A `C^n` map `f : M ‚Üí N` is a **local diffeomorphism at `x`** iff there are neighbourhoods `s`
and `t` of `x` and `f x`, respectively such that `f` restricts to a diffeomorphism
between `s` and `t`. `f` is called a **local diffeomorphism on s** iff it is a local diffeomorphism
at every `x ‚àà s`, and a **local diffeomorphism** iff it is a local diffeomorphism on `univ`.

## Main definitions
* `IsLocalDiffeomorphAt I J n f x`: `f` is a `C^n` local diffeomorphism at `x`
* `IsLocalDiffeomorphOn I J n f s`: `f` is a `C^n` local diffeomorphism on `s`
* `IsLocalDiffeomorph I J n f`: `f` is a `C^n` local diffeomorphism

## Main results
* Each of `Diffeomorph`, `IsLocalDiffeomorph`, `IsLocalDiffeomorphOn` and `IsLocalDiffeomorphAt`
  implies the next.
* `IsLocalDiffeomorph.isLocalHomeomorph`: a local diffeomorphisms is a local homeomorphism,
  similarly for local diffeomorphism on `s`
* `IsLocalDiffeomorph.isOpen_range`: the image of a local diffeomorphism is open
* `IslocalDiffeomorph.diffeomorph_of_bijective`:
  a bijective local diffeomorphism is a diffeomorphism

## TODO
* an injective local diffeomorphism is a diffeomorphism to its image
* each differential of a `C^n` diffeomorphism (`n ‚â• 1`) is a linear equivalence.
* if `f` is a local diffeomorphism at `x`, the differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* conversely, if `f` is `C^n` at `x` and `mfderiv I J n f x` is a linear isomorphism,
`f` is a local diffeomorphism at `x`.
* if `f` is a local diffeomorphism, each differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* Conversely, if `f` is `C^n` and each differential is a linear isomorphism,
`f` is a local diffeomorphism.

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of local structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

## Tags
local diffeomorphism, manifold

-/

open Manifold Set TopologicalSpace

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {H : Type*} [TopologicalSpace H]
  {G : Type*} [TopologicalSpace G]
  (I : ModelWithCorners ùïú E H) (J : ModelWithCorners ùïú F G)
  (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] (n : WithTop ‚Ñï‚àû)

section PartialDiffeomorph
/-- A partial diffeomorphism on `s` is a function `f : M ‚Üí N` such that `f` restricts to a
diffeomorphism `s ‚Üí t` between open subsets of `M` and `N`, respectively.
This is an auxiliary definition and should not be used outside of this file. -/
structure PartialDiffeomorph extends PartialEquiv M N where
  open_source : IsOpen source
  open_target : IsOpen target
  contMDiffOn_toFun : ContMDiffOn I J n toFun source
  contMDiffOn_invFun : ContMDiffOn J I n invFun target

/-- Coercion of a `PartialDiffeomorph` to function.
Note that a `PartialDiffeomorph` is not `DFunLike` (like `PartialHomeomorph`),
as `toFun` doesn't determine `invFun` outside of `target`. -/
instance : CoeFun (PartialDiffeomorph I J M N n) fun _ => M ‚Üí N :=
  ‚ü®fun Œ¶ => Œ¶.toFun‚ü©

variable {I J M N n}

/-- A diffeomorphism is a partial diffeomorphism. -/
def Diffeomorph.toPartialDiffeomorph (h : Diffeomorph I J M N n) :
    PartialDiffeomorph I J M N n where
  toPartialEquiv := h.toHomeomorph.toPartialEquiv
  open_source := isOpen_univ
  open_target := isOpen_univ
  contMDiffOn_toFun x _ := h.contMDiff_toFun x
  contMDiffOn_invFun _ _ := h.symm.contMDiffWithinAt

-- Add the very basic API we need.
namespace PartialDiffeomorph
variable (Œ¶ : PartialDiffeomorph I J M N n)

/-- A partial diffeomorphism is also a local homeomorphism. -/
def toPartialHomeomorph : PartialHomeomorph M N where
  toPartialEquiv := Œ¶.toPartialEquiv
  open_source := Œ¶.open_source
  open_target := Œ¶.open_target
  continuousOn_toFun := Œ¶.contMDiffOn_toFun.continuousOn
  continuousOn_invFun := Œ¶.contMDiffOn_invFun.continuousOn

/-- The inverse of a local diffeomorphism. -/
protected def symm : PartialDiffeomorph J I N M n where
  toPartialEquiv := Œ¶.toPartialEquiv.symm
  open_source := Œ¶.open_target
  open_target := Œ¶.open_source
  contMDiffOn_toFun := Œ¶.contMDiffOn_invFun
  contMDiffOn_invFun := Œ¶.contMDiffOn_toFun

protected theorem contMDiffOn : ContMDiffOn I J n Œ¶ Œ¶.source :=
  Œ¶.contMDiffOn_toFun

protected theorem mdifferentiableOn (hn : 1 ‚â§ n) : MDifferentiableOn I J Œ¶ Œ¶.source :=
  (Œ¶.contMDiffOn).mdifferentiableOn hn

protected theorem mdifferentiableAt (hn : 1 ‚â§ n) {x : M} (hx : x ‚àà Œ¶.source) :
    MDifferentiableAt I J Œ¶ x :=
  (Œ¶.mdifferentiableOn hn x hx).mdifferentiableAt (Œ¶.open_source.mem_nhds hx)

/- We could add lots of additional API (following `Diffeomorph` and `PartialHomeomorph`), such as
* further continuity and differentiability lemmas
* refl and trans instances; lemmas between them.
As this declaration is meant for internal use only, we keep it simple. -/
end PartialDiffeomorph
end PartialDiffeomorph

variable {M N}

/-- `f : M ‚Üí N` is called a **`C^n` local diffeomorphism at *x*** iff there exist
open sets `U ‚àã x` and `V ‚àã f x` and a diffeomorphism `Œ¶ : U ‚Üí V` such that `f = Œ¶` on `U`. -/
def IsLocalDiffeomorphAt (f : M ‚Üí N) (x : M) : Prop :=
  ‚àÉ Œ¶ : PartialDiffeomorph I J M N n, x ‚àà Œ¶.source ‚àß EqOn f Œ¶ Œ¶.source

/-- `f : M ‚Üí N` is called a **`C^n` local diffeomorphism on *s*** iff it is a local diffeomorphism
at each `x : s`. -/
def IsLocalDiffeomorphOn (f : M ‚Üí N) (s : Set M) : Prop :=
  ‚àÄ x : s, IsLocalDiffeomorphAt I J n f x

/-- `f : M ‚Üí N` is a **`C^n` local diffeomorphism** iff it is a local diffeomorphism
at each `x ‚àà M`. -/
def IsLocalDiffeomorph (f : M ‚Üí N) : Prop :=
  ‚àÄ x : M, IsLocalDiffeomorphAt I J n f x

variable {I J n} in
lemma isLocalDiffeomorphOn_iff {f : M ‚Üí N} (s : Set M) :
    IsLocalDiffeomorphOn I J n f s ‚Üî ‚àÄ x : s, IsLocalDiffeomorphAt I J n f x := by rfl

variable {I J n} in
lemma isLocalDiffeomorph_iff {f : M ‚Üí N} :
    IsLocalDiffeomorph I J n f ‚Üî ‚àÄ x : M, IsLocalDiffeomorphAt I J n f x := by rfl

variable {I J n} in
theorem isLocalDiffeomorph_iff_isLocalDiffeomorphOn_univ {f : M ‚Üí N} :
    IsLocalDiffeomorph I J n f ‚Üî IsLocalDiffeomorphOn I J n f Set.univ :=
  ‚ü®fun hf x ‚Ü¶ hf x, fun hf x ‚Ü¶ hf ‚ü®x, trivial‚ü©‚ü©

variable {I J n} in
lemma IsLocalDiffeomorph.isLocalDiffeomorphOn
    {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J n f) (s : Set M) : IsLocalDiffeomorphOn I J n f s :=
  fun x ‚Ü¶ hf x

/-! # Basic properties of local diffeomorphisms -/
section Basic
variable {f : M ‚Üí N} {s : Set M} {x : M}
variable {I J n}

/-- A `C^n` local diffeomorphism at `x` is `C^n` differentiable at `x`. -/
lemma IsLocalDiffeomorphAt.contMDiffAt (hf : IsLocalDiffeomorphAt I J n f x) :
    ContMDiffAt I J n f x := by
  choose Œ¶ hx heq using hf
  -- In fact, even `ContMDiffOn I J n f Œ¶.source`.
  exact ((Œ¶.contMDiffOn_toFun).congr heq).contMDiffAt (Œ¶.open_source.mem_nhds hx)

/-- A local diffeomorphism at `x` is differentiable at `x`. -/
lemma IsLocalDiffeomorphAt.mdifferentiableAt (hf : IsLocalDiffeomorphAt I J n f x) (hn : 1 ‚â§ n) :
    MDifferentiableAt I J f x :=
  hf.contMDiffAt.mdifferentiableAt hn

/-- A `C^n` local diffeomorphism on `s` is `C^n` on `s`. -/
lemma IsLocalDiffeomorphOn.contMDiffOn (hf : IsLocalDiffeomorphOn I J n f s) :
    ContMDiffOn I J n f s :=
  fun x hx ‚Ü¶ (hf ‚ü®x, hx‚ü©).contMDiffAt.contMDiffWithinAt

/-- A local diffeomorphism on `s` is differentiable on `s`. -/
lemma IsLocalDiffeomorphOn.mdifferentiableOn (hf : IsLocalDiffeomorphOn I J n f s) (hn : 1 ‚â§ n) :
    MDifferentiableOn I J f s :=
  hf.contMDiffOn.mdifferentiableOn hn

/-- A `C^n` local diffeomorphism is `C^n`. -/
lemma IsLocalDiffeomorph.contMDiff (hf : IsLocalDiffeomorph I J n f) : ContMDiff I J n f :=
  fun x ‚Ü¶ (hf x).contMDiffAt

/-- A `C^n` local diffeomorphism is differentiable. -/
lemma IsLocalDiffeomorph.mdifferentiable (hf : IsLocalDiffeomorph I J n f) (hn : 1 ‚â§ n) :
    MDifferentiable I J f :=
  fun x ‚Ü¶ (hf x).mdifferentiableAt hn

/-- A `C^n` diffeomorphism is a local diffeomorphism. -/
lemma Diffeomorph.isLocalDiffeomorph (Œ¶ : M ‚âÉ‚Çò^n‚üÆI, J‚üØ N) : IsLocalDiffeomorph I J n Œ¶ :=
  fun _x ‚Ü¶ ‚ü®Œ¶.toPartialDiffeomorph, by trivial, eqOn_refl Œ¶ _‚ü©

-- FUTURE: if useful, also add "a `PartialDiffeomorph` is a local diffeomorphism on its source"

/-- A local diffeomorphism on `s` is a local homeomorphism on `s`. -/
theorem IsLocalDiffeomorphOn.isLocalHomeomorphOn {s : Set M} (hf : IsLocalDiffeomorphOn I J n f s) :
    IsLocalHomeomorphOn f s := by
  apply IsLocalHomeomorphOn.mk
  intro x hx
  choose U hyp using hf ‚ü®x, hx‚ü©
  exact ‚ü®U.toPartialHomeomorph, hyp‚ü©

/-- A local diffeomorphism is a local homeomorphism. -/
theorem IsLocalDiffeomorph.isLocalHomeomorph (hf : IsLocalDiffeomorph I J n f) :
    IsLocalHomeomorph f := by
  rw [isLocalHomeomorph_iff_isLocalHomeomorphOn_univ]
  rw [isLocalDiffeomorph_iff_isLocalDiffeomorphOn_univ] at hf
  exact hf.isLocalHomeomorphOn

/-- A local diffeomorphism is an open map. -/
lemma IsLocalDiffeomorph.isOpenMap (hf : IsLocalDiffeomorph I J n f) : IsOpenMap f :=
  (hf.isLocalHomeomorph).isOpenMap

/-- A local diffeomorphism has open range. -/
lemma IsLocalDiffeomorph.isOpen_range (hf : IsLocalDiffeomorph I J n f) : IsOpen (range f) :=
  (hf.isOpenMap).isOpen_range

/-- The image of a local diffeomorphism is open. -/
def IsLocalDiffeomorph.image (hf : IsLocalDiffeomorph I J n f) : Opens N :=
  ‚ü®range f, hf.isOpen_range‚ü©

lemma IsLocalDiffeomorph.image_coe (hf : IsLocalDiffeomorph I J n f) : hf.image.1 = range f :=
  rfl

-- TODO: this result holds more generally for (local) structomorphisms
-- This argument implies a `LocalDiffeomorphOn f s` for `s` open is a `PartialDiffeomorph`

/-- A bijective local diffeomorphism is a diffeomorphism. -/
noncomputable def IslocalDiffeomorph.diffeomorph_of_bijective
    (hf : IsLocalDiffeomorph I J n f) (hf' : Function.Bijective f) : Diffeomorph I J M N n := by
  -- Choose a right inverse `g` of `f`.
  choose g hgInverse using (Function.bijective_iff_has_inverse).mp hf'
   -- Choose diffeomorphisms œÜ_x which coincide which `f` near `x`.
  choose Œ¶ hyp using (fun x ‚Ü¶ hf x)
  -- Two such diffeomorphisms (and their inverses!) coincide on their sources:
  -- they're both inverses to g. In fact, the latter suffices for our proof.
  -- have (x y) : EqOn (Œ¶ x).symm (Œ¶ y).symm ((Œ¶ x).target ‚à© (Œ¶ y).target) := sorry
  have aux (x) : EqOn g (Œ¶ x).symm (Œ¶ x).target :=
    eqOn_of_leftInvOn_of_rightInvOn (fun x' _ ‚Ü¶ hgInverse.1 x')
      (LeftInvOn.congr_left ((Œ¶ x).toPartialHomeomorph).rightInvOn
        ((Œ¶ x).toPartialHomeomorph).symm_mapsTo (hyp x).2.symm)
      (fun _y hy ‚Ü¶ (Œ¶ x).map_target hy)
  exact {
    toFun := f
    invFun := g
    left_inv := hgInverse.1
    right_inv := hgInverse.2
    contMDiff_toFun := hf.contMDiff
    contMDiff_invFun := by
      intro y
      let x := g y
      obtain ‚ü®hx, hfx‚ü© := hyp x
      apply ((Œ¶ x).symm.contMDiffOn.congr (aux x)).contMDiffAt (((Œ¶ x).open_target).mem_nhds ?_)
      have : y = (Œ¶ x) x := ((hgInverse.2 y).congr (hfx hx)).mp rfl
      exact this ‚ñ∏ (Œ¶ x).map_source hx }

section IFT

-- TODO: prove this, from the inverse function theorem for manifolds
/-- If `f` has bijective differential at `x`, it is a local diffeomorphism at `x`. -/
lemma IsLocalDiffeomorphAt.of_mfderiv_bijective (hdiff: Function.Bijective (mfderiv I J f x)) :
    IsLocalDiffeomorphAt I J n f x := sorry

/-- If `f` has bijective differential everywhere, it is a local diffeomorphism. -/
lemma IsLocalDiffeomorph.of_mfderiv_bijective (hdiff: ‚àÄ x, Function.Bijective (mfderiv I J f x)) :
    IsLocalDiffeomorph I J n f :=
  fun x ‚Ü¶ IsLocalDiffeomorphAt.of_mfderiv_bijective (hdiff x)

end IFT

-- XXX: move to Diffeomorph? split that file?
section Distributivity

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ùïú E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E'] {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ùïú F] {H : Type*} [TopologicalSpace H] {H' : Type*}
  [TopologicalSpace H'] {G : Type*} [TopologicalSpace G] {G' : Type*} [TopologicalSpace G']
  {I : ModelWithCorners ùïú E H} {I' : ModelWithCorners ùïú E' H'} {J : ModelWithCorners ùïú F G}
  {J' : ModelWithCorners ùïú F G'}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {M' : Type*} [TopologicalSpace M']
  [ChartedSpace H' M'] {N : Type*} [TopologicalSpace N] [ChartedSpace G N] {N' : Type*}
  [TopologicalSpace N'] [ChartedSpace G' N'] {n : WithTop ‚Ñï‚àû}

variable {J : ModelWithCorners ùïú E' H}
  {M' M'' N : Type*} [TopologicalSpace M'] [ChartedSpace H M']
  [TopologicalSpace M''] [ChartedSpace H M''] [TopologicalSpace N] [ChartedSpace H N]
  {N' : Type*} [TopologicalSpace N'] [ChartedSpace H N']

section

variable {R R‚ÇÇ M M‚ÇÇ : Type*} [DivisionRing R] [Semiring R‚ÇÇ] [AddCommMonoid M] [AddCommGroup M‚ÇÇ]
  [Module R M] [Module R M‚ÇÇ] [FiniteDimensional R M‚ÇÇ]

namespace _root_.LinearEquiv

/-- An injective linear map between finite-dimensional space of equal rank
is a linear equivalence. -/
noncomputable def of_injective_finrank_eq (f : M ‚Üí‚Çó[R] M‚ÇÇ) (hinj : Function.Injective f)
    (hrank : Module.finrank R M = Module.finrank R M‚ÇÇ) : M ‚âÉ‚Çó[R] M‚ÇÇ :=
  haveI : LinearMap.range f = ‚ä§ := by
    apply Submodule.eq_top_of_finrank_eq (S := LinearMap.range f)
    exact (LinearMap.finrank_range_of_inj hinj).trans hrank
  (LinearEquiv.ofInjective f hinj).trans (LinearEquiv.ofTop (LinearMap.range f) this)

@[simp]
lemma of_injective_finrank_eq_coe (f : M ‚Üí‚Çó[R] M‚ÇÇ) (hinj : Function.Injective f)
    (hrank : Module.finrank R M = Module.finrank R M‚ÇÇ) :
    (of_injective_finrank_eq f hinj hrank).toLinearMap = f := rfl

end _root_.LinearEquiv

end

variable [FiniteDimensional ùïú E] [FiniteDimensional ùïú E']

variable (I J M M' N n) in
noncomputable def prodSumDistrib :
    Diffeomorph (I.prod J) (I.prod J) ((M √ó N) ‚äï (M' √ó N)) ((M ‚äï M') √ó N) n := by
  refine IslocalDiffeomorph.diffeomorph_of_bijective (f := (Equiv.sumProdDistrib M M' N).symm) ?_
    (Equiv.bijective _)
  apply IsLocalDiffeomorph.of_mfderiv_bijective
  intro x
  set f := (Equiv.sumProdDistrib M M' N).symm
  have : ContMDiff (I.prod J) (I.prod J) n f := by
    apply ContMDiff.sumElim
    ¬∑ exact ContMDiff.prod_map ContMDiff.inl contMDiff_id
    ¬∑ exact ContMDiff.prod_map ContMDiff.inr contMDiff_id
  have hinj : Function.Injective (mfderiv (I.prod J) (I.prod J) f x) := by
    -- two cases, depending on whether x is a left or right point
    -- in each, it follows by computing the mfderiv of a product with the identity
    sorry
  -- The domain and co-domain have the same finite dimension, hence they are equivalent.
  have : FiniteDimensional ùïú (TangentSpace (I.prod J) (f x)) := by
    change FiniteDimensional ùïú (E √ó E')
    infer_instance
  -- Both tangent spaces are defeq to E.prod E', hence the proof by rfl...
  have hrank : Module.finrank ùïú (TangentSpace (I.prod J) x) =
    Module.finrank ùïú (TangentSpace (I.prod J) (f x)) := rfl
  let aux := _root_.LinearEquiv.of_injective_finrank_eq
    (mfderiv (I.prod J) (I.prod J) f x).toLinearMap hinj rfl
  exact LinearEquiv.bijective aux

@[simp]
theorem prodSumDistrib_toEquiv :
    (prodSumDistrib I M n J M' N).toEquiv = (Equiv.sumProdDistrib M M' N).symm :=
  sorry -- rfl -- TODO: fix!

end Distributivity

end Basic
