/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Geometry.Manifold.Algebra.Structures
import Mathlib.Geometry.Manifold.BumpFunction
import Mathlib.Topology.MetricSpace.PartitionOfUnity
import Mathlib.Topology.ShrinkingLemma

#align_import geometry.manifold.partition_of_unity from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Smooth partition of unity

In this file we define two structures, `SmoothBumpCovering` and `SmoothPartitionOfUnity`. Both
structures describe coverings of a set by a locally finite family of supports of smooth functions
with some additional properties. The former structure is mostly useful as an intermediate step in
the construction of a smooth partition of unity but some proofs that traditionally deal with a
partition of unity can use a `SmoothBumpCovering` as well.

Given a real manifold `M` and its subset `s`, a `SmoothBumpCovering Î¹ I M s` is a collection of
`SmoothBumpFunction`s `f i` indexed by `i : Î¹` such that

* the center of each `f i` belongs to `s`;
* the family of sets `support (f i)` is locally finite;
* for each `x âˆˆ s`, there exists `i : Î¹` such that `f i =á¶ [ğ“ x] 1`.
In the same settings, a `SmoothPartitionOfUnity Î¹ I M s` is a collection of smooth nonnegative
functions `f i : C^âˆâŸ®I, M; ğ“˜(â„), â„âŸ¯`, `i : Î¹`, such that

* the family of sets `support (f i)` is locally finite;
* for each `x âˆˆ s`, the sum `âˆ‘á¶  i, f i x` equals one;
* for each `x`, the sum `âˆ‘á¶  i, f i x` is less than or equal to one.

We say that `f : SmoothBumpCovering Î¹ I M s` is *subordinate* to a map `U : M â†’ Set M` if for each
index `i`, we have `tsupport (f i) âŠ† U (f i).c`. This notion is a bit more general than
being subordinate to an open covering of `M`, because we make no assumption about the way `U x`
depends on `x`.

We prove that on a smooth finitely dimensional real manifold with `Ïƒ`-compact Hausdorff topology,
for any `U : M â†’ Set M` such that `âˆ€ x âˆˆ s, U x âˆˆ ğ“ x` there exists a `SmoothBumpCovering Î¹ I M s`
subordinate to `U`. Then we use this fact to prove a similar statement about smooth partitions of
unity, see `SmoothPartitionOfUnity.exists_isSubordinate`.

Finally, we use existence of a partition of unity to prove lemma
`exists_smooth_forall_mem_convex_of_local` that allows us to construct a globally defined smooth
function from local functions.

## TODO

* Build a framework for to transfer local definitions to global using partition of unity and use it
  to define, e.g., the integral of a differential form over a manifold. Lemma
  `exists_smooth_forall_mem_convex_of_local` is a first step in this direction.

## Tags

smooth bump function, partition of unity
-/


universe uÎ¹ uE uH uM uF

open Function Filter FiniteDimensional Set

open scoped Topology Manifold Classical Filter

noncomputable section

variable {Î¹ : Type uÎ¹} {E : Type uE} [NormedAddCommGroup E] [NormedSpace â„ E]
  [FiniteDimensional â„ E] {F : Type uF} [NormedAddCommGroup F] [NormedSpace â„ F] {H : Type uH}
  [TopologicalSpace H] (I : ModelWithCorners â„ E H) {M : Type uM} [TopologicalSpace M]
  [ChartedSpace H M] [SmoothManifoldWithCorners I M]

/-!
### Covering by supports of smooth bump functions

In this section we define `SmoothBumpCovering Î¹ I M s` to be a collection of
`SmoothBumpFunction`s such that their supports is a locally finite family of sets and for each
`x âˆˆ s` some function `f i` from the collection is equal to `1` in a neighborhood of `x`. A covering
of this type is useful to construct a smooth partition of unity and can be used instead of a
partition of unity in some proofs.

We prove that on a smooth finite dimensional real manifold with `Ïƒ`-compact Hausdorff topology, for
any `U : M â†’ Set M` such that `âˆ€ x âˆˆ s, U x âˆˆ ğ“ x` there exists a `SmoothBumpCovering Î¹ I M s`
subordinate to `U`. -/

variable (Î¹ M)

/-- We say that a collection of `SmoothBumpFunction`s is a `SmoothBumpCovering` of a set `s` if

* `(f i).c âˆˆ s` for all `i`;
* the family `fun i â†¦ support (f i)` is locally finite;
* for each point `x âˆˆ s` there exists `i` such that `f i =á¶ [ğ“ x] 1`;
  in other words, `x` belongs to the interior of `{y | f i y = 1}`;

If `M` is a finite dimensional real manifold which is a `Ïƒ`-compact Hausdorff topological space,
then for every covering `U : M â†’ Set M`, `âˆ€ x, U x âˆˆ ğ“ x`, there exists a `SmoothBumpCovering`
subordinate to `U`, see `SmoothBumpCovering.exists_isSubordinate`.

This covering can be used, e.g., to construct a partition of unity and to prove the weak
Whitney embedding theorem. -/
-- Porting note(#5171): was @[nolint has_nonempty_instance]
structure SmoothBumpCovering (s : Set M := univ) where
  /-- The center point of each bump in the smooth covering. -/
  c : Î¹ â†’ M
  /-- A smooth bump function around `c i`. -/
  toFun : âˆ€ i, SmoothBumpFunction I (c i)
  /-- All the bump functions in the covering are centered at points in `s`. -/
  c_mem' : âˆ€ i, c i âˆˆ s
  /-- Around each point, there are only finitely many nonzero bump functions in the family. -/
  locallyFinite' : LocallyFinite fun i => support (toFun i)
  /-- Around each point in `s`, one of the bump functions is equal to `1`. -/
  eventuallyEq_one' : âˆ€ x âˆˆ s, âˆƒ i, toFun i =á¶ [ğ“ x] 1
#align smooth_bump_covering SmoothBumpCovering

/-- We say that a collection of functions form a smooth partition of unity on a set `s` if

* all functions are infinitely smooth and nonnegative;
* the family `fun i â†¦ support (f i)` is locally finite;
* for all `x âˆˆ s` the sum `âˆ‘á¶  i, f i x` equals one;
* for all `x`, the sum `âˆ‘á¶  i, f i x` is less than or equal to one. -/
structure SmoothPartitionOfUnity (s : Set M := univ) where
  /-- The family of functions forming the partition of unity. -/
  toFun : Î¹ â†’ C^âˆâŸ®I, M; ğ“˜(â„), â„âŸ¯
  /-- Around each point, there are only finitely many nonzero functions in the family. -/
  locallyFinite' : LocallyFinite fun i => support (toFun i)
  /-- All the functions in the partition of unity are nonnegative. -/
  nonneg' : âˆ€ i x, 0 â‰¤ toFun i x
  /-- The functions in the partition of unity add up to `1` at any point of `s`. -/
  sum_eq_one' : âˆ€ x âˆˆ s, âˆ‘á¶  i, toFun i x = 1
  /-- The functions in the partition of unity add up to at most `1` everywhere. -/
  sum_le_one' : âˆ€ x, âˆ‘á¶  i, toFun i x â‰¤ 1
#align smooth_partition_of_unity SmoothPartitionOfUnity

variable {Î¹ I M}

namespace SmoothPartitionOfUnity

variable {s : Set M} (f : SmoothPartitionOfUnity Î¹ I M s) {n : â„•âˆ}

instance {s : Set M} : FunLike (SmoothPartitionOfUnity Î¹ I M s) Î¹ C^âˆâŸ®I, M; ğ“˜(â„), â„âŸ¯ where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr

protected theorem locallyFinite : LocallyFinite fun i => support (f i) :=
  f.locallyFinite'
#align smooth_partition_of_unity.locally_finite SmoothPartitionOfUnity.locallyFinite

theorem nonneg (i : Î¹) (x : M) : 0 â‰¤ f i x :=
  f.nonneg' i x
#align smooth_partition_of_unity.nonneg SmoothPartitionOfUnity.nonneg

theorem sum_eq_one {x} (hx : x âˆˆ s) : âˆ‘á¶  i, f i x = 1 :=
  f.sum_eq_one' x hx
#align smooth_partition_of_unity.sum_eq_one SmoothPartitionOfUnity.sum_eq_one

theorem exists_pos_of_mem {x} (hx : x âˆˆ s) : âˆƒ i, 0 < f i x := by
  by_contra! h
  have H : âˆ€ i, f i x = 0 := fun i â†¦ le_antisymm (h i) (f.nonneg i x)
  have := f.sum_eq_one hx
  simp_rw [H] at this
  simpa

theorem sum_le_one (x : M) : âˆ‘á¶  i, f i x â‰¤ 1 :=
  f.sum_le_one' x
#align smooth_partition_of_unity.sum_le_one SmoothPartitionOfUnity.sum_le_one

/-- Reinterpret a smooth partition of unity as a continuous partition of unity. -/
@[simps]
def toPartitionOfUnity : PartitionOfUnity Î¹ M s :=
  { f with toFun := fun i => f i }
#align smooth_partition_of_unity.to_partition_of_unity SmoothPartitionOfUnity.toPartitionOfUnity

theorem smooth_sum : Smooth I ğ“˜(â„) fun x => âˆ‘á¶  i, f i x :=
  smooth_finsum (fun i => (f i).smooth) f.locallyFinite
#align smooth_partition_of_unity.smooth_sum SmoothPartitionOfUnity.smooth_sum

theorem le_one (i : Î¹) (x : M) : f i x â‰¤ 1 :=
  f.toPartitionOfUnity.le_one i x
#align smooth_partition_of_unity.le_one SmoothPartitionOfUnity.le_one

theorem sum_nonneg (x : M) : 0 â‰¤ âˆ‘á¶  i, f i x :=
  f.toPartitionOfUnity.sum_nonneg x
#align smooth_partition_of_unity.sum_nonneg SmoothPartitionOfUnity.sum_nonneg

theorem contMDiff_smul {g : M â†’ F} {i} (hg : âˆ€ x âˆˆ tsupport (f i), ContMDiffAt I ğ“˜(â„, F) n g x) :
    ContMDiff I ğ“˜(â„, F) n fun x => f i x â€¢ g x :=
  contMDiff_of_tsupport fun x hx =>
    ((f i).contMDiff.contMDiffAt.of_le le_top).smul <| hg x <| tsupport_smul_subset_left _ _ hx
#align smooth_partition_of_unity.cont_mdiff_smul SmoothPartitionOfUnity.contMDiff_smul

theorem smooth_smul {g : M â†’ F} {i} (hg : âˆ€ x âˆˆ tsupport (f i), SmoothAt I ğ“˜(â„, F) g x) :
    Smooth I ğ“˜(â„, F) fun x => f i x â€¢ g x :=
  f.contMDiff_smul hg
#align smooth_partition_of_unity.smooth_smul SmoothPartitionOfUnity.smooth_smul

/-- If `f` is a smooth partition of unity on a set `s : Set M` and `g : Î¹ â†’ M â†’ F` is a family of
functions such that `g i` is $C^n$ smooth at every point of the topological support of `f i`, then
the sum `fun x â†¦ âˆ‘á¶  i, f i x â€¢ g i x` is smooth on the whole manifold. -/
theorem contMDiff_finsum_smul {g : Î¹ â†’ M â†’ F}
    (hg : âˆ€ (i), âˆ€ x âˆˆ tsupport (f i), ContMDiffAt I ğ“˜(â„, F) n (g i) x) :
    ContMDiff I ğ“˜(â„, F) n fun x => âˆ‘á¶  i, f i x â€¢ g i x :=
  (contMDiff_finsum fun i => f.contMDiff_smul (hg i)) <|
    f.locallyFinite.subset fun _ => support_smul_subset_left _ _
#align smooth_partition_of_unity.cont_mdiff_finsum_smul SmoothPartitionOfUnity.contMDiff_finsum_smul

/-- If `f` is a smooth partition of unity on a set `s : Set M` and `g : Î¹ â†’ M â†’ F` is a family of
functions such that `g i` is smooth at every point of the topological support of `f i`, then the sum
`fun x â†¦ âˆ‘á¶  i, f i x â€¢ g i x` is smooth on the whole manifold. -/
theorem smooth_finsum_smul {g : Î¹ â†’ M â†’ F}
    (hg : âˆ€ (i), âˆ€ x âˆˆ tsupport (f i), SmoothAt I ğ“˜(â„, F) (g i) x) :
    Smooth I ğ“˜(â„, F) fun x => âˆ‘á¶  i, f i x â€¢ g i x :=
  f.contMDiff_finsum_smul hg
#align smooth_partition_of_unity.smooth_finsum_smul SmoothPartitionOfUnity.smooth_finsum_smul

theorem contMDiffAt_finsum {xâ‚€ : M} {g : Î¹ â†’ M â†’ F}
    (hÏ† : âˆ€ i, xâ‚€ âˆˆ tsupport (f i) â†’ ContMDiffAt I ğ“˜(â„, F) n (g i) xâ‚€) :
    ContMDiffAt I ğ“˜(â„, F) n (fun x â†¦ âˆ‘á¶  i, f i x â€¢ g i x) xâ‚€ := by
  refine _root_.contMDiffAt_finsum (f.locallyFinite.smul_left _) fun i â†¦ ?_
  by_cases hx : xâ‚€ âˆˆ tsupport (f i)
  Â· exact ContMDiffAt.smul ((f i).smooth.of_le le_top).contMDiffAt (hÏ† i hx)
  Â· exact contMDiffAt_of_not_mem (compl_subset_compl.mpr
      (tsupport_smul_subset_left (f i) (g i)) hx) n

theorem contDiffAt_finsum {s : Set E} (f : SmoothPartitionOfUnity Î¹ ğ“˜(â„, E) E s) {xâ‚€ : E}
    {g : Î¹ â†’ E â†’ F} (hÏ† : âˆ€ i, xâ‚€ âˆˆ tsupport (f i) â†’ ContDiffAt â„ n (g i) xâ‚€) :
    ContDiffAt â„ n (fun x â†¦ âˆ‘á¶  i, f i x â€¢ g i x) xâ‚€ := by
  simp only [â† contMDiffAt_iff_contDiffAt] at *
  exact f.contMDiffAt_finsum hÏ†

theorem finsum_smul_mem_convex {g : Î¹ â†’ M â†’ F} {t : Set F} {x : M} (hx : x âˆˆ s)
    (hg : âˆ€ i, f i x â‰  0 â†’ g i x âˆˆ t) (ht : Convex â„ t) : âˆ‘á¶  i, f i x â€¢ g i x âˆˆ t :=
  ht.finsum_mem (fun _ => f.nonneg _ _) (f.sum_eq_one hx) hg
#align smooth_partition_of_unity.finsum_smul_mem_convex SmoothPartitionOfUnity.finsum_smul_mem_convex

section finsupport

variable {s : Set M} (Ï : SmoothPartitionOfUnity Î¹ I M s) (xâ‚€ : M)

/-- The support of a smooth partition of unity at a point `xâ‚€` as a `Finset`.
  This is the set of `i : Î¹` such that `xâ‚€ âˆˆ support f i`, i.e. `f i â‰  xâ‚€`. -/
def finsupport : Finset Î¹ := Ï.toPartitionOfUnity.finsupport xâ‚€

@[simp]
theorem mem_finsupport {i : Î¹} : i âˆˆ Ï.finsupport xâ‚€ â†” i âˆˆ support fun i â†¦ Ï i xâ‚€ :=
  Ï.toPartitionOfUnity.mem_finsupport xâ‚€

@[simp]
theorem coe_finsupport : (Ï.finsupport xâ‚€ : Set Î¹) = support fun i â†¦ Ï i xâ‚€ :=
  Ï.toPartitionOfUnity.coe_finsupport xâ‚€

theorem sum_finsupport (hxâ‚€ : xâ‚€ âˆˆ s) : âˆ‘ i âˆˆ Ï.finsupport xâ‚€, Ï i xâ‚€ = 1 :=
  Ï.toPartitionOfUnity.sum_finsupport hxâ‚€

theorem sum_finsupport' (hxâ‚€ : xâ‚€ âˆˆ s) {I : Finset Î¹} (hI : Ï.finsupport xâ‚€ âŠ† I) :
    âˆ‘ i âˆˆ I, Ï i xâ‚€ = 1 :=
  Ï.toPartitionOfUnity.sum_finsupport' hxâ‚€ hI

theorem sum_finsupport_smul_eq_finsum {A : Type*} [AddCommGroup A] [Module â„ A] (Ï† : Î¹ â†’ M â†’ A) :
    âˆ‘ i âˆˆ Ï.finsupport xâ‚€, Ï i xâ‚€ â€¢ Ï† i xâ‚€ = âˆ‘á¶  i, Ï i xâ‚€ â€¢ Ï† i xâ‚€ :=
  Ï.toPartitionOfUnity.sum_finsupport_smul_eq_finsum Ï†

end finsupport

section fintsupport -- smooth partitions of unity have locally finite `tsupport`
variable {s : Set M} (Ï : SmoothPartitionOfUnity Î¹ I M s) (xâ‚€ : M)

/-- The `tsupport`s of a smooth partition of unity are locally finite. -/
theorem finite_tsupport : {i | xâ‚€ âˆˆ tsupport (Ï i)}.Finite :=
  Ï.toPartitionOfUnity.finite_tsupport _

/-- The tsupport of a partition of unity at a point `xâ‚€` as a `Finset`.
  This is the set of `i : Î¹` such that `xâ‚€ âˆˆ tsupport f i`. -/
def fintsupport (x : M) : Finset Î¹ :=
  (Ï.finite_tsupport x).toFinset

theorem mem_fintsupport_iff (i : Î¹) : i âˆˆ Ï.fintsupport xâ‚€ â†” xâ‚€ âˆˆ tsupport (Ï i) :=
  Finite.mem_toFinset _

theorem eventually_fintsupport_subset : âˆ€á¶  y in ğ“ xâ‚€, Ï.fintsupport y âŠ† Ï.fintsupport xâ‚€ :=
  Ï.toPartitionOfUnity.eventually_fintsupport_subset _

theorem finsupport_subset_fintsupport : Ï.finsupport xâ‚€ âŠ† Ï.fintsupport xâ‚€ :=
  Ï.toPartitionOfUnity.finsupport_subset_fintsupport xâ‚€

theorem eventually_finsupport_subset : âˆ€á¶  y in ğ“ xâ‚€, Ï.finsupport y âŠ† Ï.fintsupport xâ‚€ :=
  Ï.toPartitionOfUnity.eventually_finsupport_subset xâ‚€

end fintsupport

section IsSubordinate

/-- A smooth partition of unity `f i` is subordinate to a family of sets `U i` indexed by the same
type if for each `i` the closure of the support of `f i` is a subset of `U i`. -/
def IsSubordinate (f : SmoothPartitionOfUnity Î¹ I M s) (U : Î¹ â†’ Set M) :=
  âˆ€ i, tsupport (f i) âŠ† U i
#align smooth_partition_of_unity.is_subordinate SmoothPartitionOfUnity.IsSubordinate

variable {f} {U : Î¹ â†’ Set M}

@[simp]
theorem isSubordinate_toPartitionOfUnity :
    f.toPartitionOfUnity.IsSubordinate U â†” f.IsSubordinate U :=
  Iff.rfl
#align smooth_partition_of_unity.is_subordinate_to_partition_of_unity SmoothPartitionOfUnity.isSubordinate_toPartitionOfUnity

alias âŸ¨_, IsSubordinate.toPartitionOfUnityâŸ© := isSubordinate_toPartitionOfUnity
#align smooth_partition_of_unity.is_subordinate.to_partition_of_unity SmoothPartitionOfUnity.IsSubordinate.toPartitionOfUnity

/-- If `f` is a smooth partition of unity on a set `s : Set M` subordinate to a family of open sets
`U : Î¹ â†’ Set M` and `g : Î¹ â†’ M â†’ F` is a family of functions such that `g i` is $C^n$ smooth on
`U i`, then the sum `fun x â†¦ âˆ‘á¶  i, f i x â€¢ g i x` is $C^n$ smooth on the whole manifold. -/
theorem IsSubordinate.contMDiff_finsum_smul {g : Î¹ â†’ M â†’ F} (hf : f.IsSubordinate U)
    (ho : âˆ€ i, IsOpen (U i)) (hg : âˆ€ i, ContMDiffOn I ğ“˜(â„, F) n (g i) (U i)) :
    ContMDiff I ğ“˜(â„, F) n fun x => âˆ‘á¶  i, f i x â€¢ g i x :=
  f.contMDiff_finsum_smul fun i _ hx => (hg i).contMDiffAt <| (ho i).mem_nhds (hf i hx)
#align smooth_partition_of_unity.is_subordinate.cont_mdiff_finsum_smul SmoothPartitionOfUnity.IsSubordinate.contMDiff_finsum_smul

/-- If `f` is a smooth partition of unity on a set `s : Set M` subordinate to a family of open sets
`U : Î¹ â†’ Set M` and `g : Î¹ â†’ M â†’ F` is a family of functions such that `g i` is smooth on `U i`,
then the sum `fun x â†¦ âˆ‘á¶  i, f i x â€¢ g i x` is smooth on the whole manifold. -/
theorem IsSubordinate.smooth_finsum_smul {g : Î¹ â†’ M â†’ F} (hf : f.IsSubordinate U)
    (ho : âˆ€ i, IsOpen (U i)) (hg : âˆ€ i, SmoothOn I ğ“˜(â„, F) (g i) (U i)) :
    Smooth I ğ“˜(â„, F) fun x => âˆ‘á¶  i, f i x â€¢ g i x :=
  hf.contMDiff_finsum_smul ho hg
#align smooth_partition_of_unity.is_subordinate.smooth_finsum_smul SmoothPartitionOfUnity.IsSubordinate.smooth_finsum_smul

end IsSubordinate

end SmoothPartitionOfUnity

namespace BumpCovering

-- Repeat variables to drop `[FiniteDimensional â„ E]` and `[SmoothManifoldWithCorners I M]`
theorem smooth_toPartitionOfUnity {E : Type uE} [NormedAddCommGroup E] [NormedSpace â„ E]
    {H : Type uH} [TopologicalSpace H] {I : ModelWithCorners â„ E H} {M : Type uM}
    [TopologicalSpace M] [ChartedSpace H M] {s : Set M} (f : BumpCovering Î¹ M s)
    (hf : âˆ€ i, Smooth I ğ“˜(â„) (f i)) (i : Î¹) : Smooth I ğ“˜(â„) (f.toPartitionOfUnity i) :=
  (hf i).mul <| (smooth_finprod_cond fun j _ => smooth_const.sub (hf j)) <| by
    simp only [Pi.sub_def, mulSupport_one_sub]
    exact f.locallyFinite
#align bump_covering.smooth_to_partition_of_unity BumpCovering.smooth_toPartitionOfUnity

variable {s : Set M}

/-- A `BumpCovering` such that all functions in this covering are smooth generates a smooth
partition of unity.

In our formalization, not every `f : BumpCovering Î¹ M s` with smooth functions `f i` is a
`SmoothBumpCovering`; instead, a `SmoothBumpCovering` is a covering by supports of
`SmoothBumpFunction`s. So, we define `BumpCovering.toSmoothPartitionOfUnity`, then reuse it
in `SmoothBumpCovering.toSmoothPartitionOfUnity`. -/
def toSmoothPartitionOfUnity (f : BumpCovering Î¹ M s) (hf : âˆ€ i, Smooth I ğ“˜(â„) (f i)) :
    SmoothPartitionOfUnity Î¹ I M s :=
  { f.toPartitionOfUnity with
    toFun := fun i => âŸ¨f.toPartitionOfUnity i, f.smooth_toPartitionOfUnity hf iâŸ© }
#align bump_covering.to_smooth_partition_of_unity BumpCovering.toSmoothPartitionOfUnity

@[simp]
theorem toSmoothPartitionOfUnity_toPartitionOfUnity (f : BumpCovering Î¹ M s)
    (hf : âˆ€ i, Smooth I ğ“˜(â„) (f i)) :
    (f.toSmoothPartitionOfUnity hf).toPartitionOfUnity = f.toPartitionOfUnity :=
  rfl
#align bump_covering.to_smooth_partition_of_unity_to_partition_of_unity BumpCovering.toSmoothPartitionOfUnity_toPartitionOfUnity

@[simp]
theorem coe_toSmoothPartitionOfUnity (f : BumpCovering Î¹ M s) (hf : âˆ€ i, Smooth I ğ“˜(â„) (f i))
    (i : Î¹) : â‡‘(f.toSmoothPartitionOfUnity hf i) = f.toPartitionOfUnity i :=
  rfl
#align bump_covering.coe_to_smooth_partition_of_unity BumpCovering.coe_toSmoothPartitionOfUnity

theorem IsSubordinate.toSmoothPartitionOfUnity {f : BumpCovering Î¹ M s} {U : Î¹ â†’ Set M}
    (h : f.IsSubordinate U) (hf : âˆ€ i, Smooth I ğ“˜(â„) (f i)) :
    (f.toSmoothPartitionOfUnity hf).IsSubordinate U :=
  h.toPartitionOfUnity
#align bump_covering.is_subordinate.to_smooth_partition_of_unity BumpCovering.IsSubordinate.toSmoothPartitionOfUnity

end BumpCovering

namespace SmoothBumpCovering

variable {s : Set M} {U : M â†’ Set M} (fs : SmoothBumpCovering Î¹ I M s)

instance : CoeFun (SmoothBumpCovering Î¹ I M s) fun x => âˆ€ i : Î¹, SmoothBumpFunction I (x.c i) :=
  âŸ¨toFunâŸ©

#noalign smooth_bump_covering.coe_mk

/--
We say that `f : SmoothBumpCovering Î¹ I M s` is *subordinate* to a map `U : M â†’ Set M` if for each
index `i`, we have `tsupport (f i) âŠ† U (f i).c`. This notion is a bit more general than
being subordinate to an open covering of `M`, because we make no assumption about the way `U x`
depends on `x`.
-/
def IsSubordinate {s : Set M} (f : SmoothBumpCovering Î¹ I M s) (U : M â†’ Set M) :=
  âˆ€ i, tsupport (f i) âŠ† U (f.c i)
#align smooth_bump_covering.is_subordinate SmoothBumpCovering.IsSubordinate

theorem IsSubordinate.support_subset {fs : SmoothBumpCovering Î¹ I M s} {U : M â†’ Set M}
    (h : fs.IsSubordinate U) (i : Î¹) : support (fs i) âŠ† U (fs.c i) :=
  Subset.trans subset_closure (h i)
#align smooth_bump_covering.is_subordinate.support_subset SmoothBumpCovering.IsSubordinate.support_subset

variable (I)

/-- Let `M` be a smooth manifold with corners modelled on a finite dimensional real vector space.
Suppose also that `M` is a Hausdorff `Ïƒ`-compact topological space. Let `s` be a closed set
in `M` and `U : M â†’ Set M` be a collection of sets such that `U x âˆˆ ğ“ x` for every `x âˆˆ s`.
Then there exists a smooth bump covering of `s` that is subordinate to `U`. -/
theorem exists_isSubordinate [T2Space M] [SigmaCompactSpace M] (hs : IsClosed s)
    (hU : âˆ€ x âˆˆ s, U x âˆˆ ğ“ x) :
    âˆƒ (Î¹ : Type uM) (f : SmoothBumpCovering Î¹ I M s), f.IsSubordinate U := by
  -- First we deduce some missing instances
  haveI : LocallyCompactSpace H := I.locallyCompactSpace
  haveI : LocallyCompactSpace M := ChartedSpace.locallyCompactSpace H M
  -- Next we choose a covering by supports of smooth bump functions
  have hB := fun x hx => SmoothBumpFunction.nhds_basis_support I (hU x hx)
  rcases refinement_of_locallyCompact_sigmaCompact_of_nhds_basis_set hs hB with
    âŸ¨Î¹, c, f, hf, hsub', hfinâŸ©
  choose hcs hfU using hf
  -- Then we use the shrinking lemma to get a covering by smaller open
  rcases exists_subset_iUnion_closed_subset hs (fun i => (f i).isOpen_support)
    (fun x _ => hfin.point_finite x) hsub' with âŸ¨V, hsV, hVc, hVfâŸ©
  choose r hrR hr using fun i => (f i).exists_r_pos_lt_subset_ball (hVc i) (hVf i)
  refine âŸ¨Î¹, âŸ¨c, fun i => (f i).updateRIn (r i) (hrR i), hcs, ?_, fun x hx => ?_âŸ©, fun i => ?_âŸ©
  Â· simpa only [SmoothBumpFunction.support_updateRIn]
  Â· refine (mem_iUnion.1 <| hsV hx).imp fun i hi => ?_
    exact ((f i).updateRIn _ _).eventuallyEq_one_of_dist_lt
      ((f i).support_subset_source <| hVf _ hi) (hr i hi).2
  Â· simpa only [SmoothBumpFunction.support_updateRIn, tsupport] using hfU i
#align smooth_bump_covering.exists_is_subordinate SmoothBumpCovering.exists_isSubordinate

variable {I}

protected theorem locallyFinite : LocallyFinite fun i => support (fs i) :=
  fs.locallyFinite'
#align smooth_bump_covering.locally_finite SmoothBumpCovering.locallyFinite

protected theorem point_finite (x : M) : {i | fs i x â‰  0}.Finite :=
  fs.locallyFinite.point_finite x
#align smooth_bump_covering.point_finite SmoothBumpCovering.point_finite

theorem mem_chartAt_source_of_eq_one {i : Î¹} {x : M} (h : fs i x = 1) :
    x âˆˆ (chartAt H (fs.c i)).source :=
  (fs i).support_subset_source <| by simp [h]
#align smooth_bump_covering.mem_chart_at_source_of_eq_one SmoothBumpCovering.mem_chartAt_source_of_eq_one

theorem mem_extChartAt_source_of_eq_one {i : Î¹} {x : M} (h : fs i x = 1) :
    x âˆˆ (extChartAt I (fs.c i)).source := by
  rw [extChartAt_source]; exact fs.mem_chartAt_source_of_eq_one h
#align smooth_bump_covering.mem_ext_chart_at_source_of_eq_one SmoothBumpCovering.mem_extChartAt_source_of_eq_one

/-- Index of a bump function such that `fs i =á¶ [ğ“ x] 1`. -/
def ind (x : M) (hx : x âˆˆ s) : Î¹ :=
  (fs.eventuallyEq_one' x hx).choose
#align smooth_bump_covering.ind SmoothBumpCovering.ind

theorem eventuallyEq_one (x : M) (hx : x âˆˆ s) : fs (fs.ind x hx) =á¶ [ğ“ x] 1 :=
  (fs.eventuallyEq_one' x hx).choose_spec
#align smooth_bump_covering.eventually_eq_one SmoothBumpCovering.eventuallyEq_one

theorem apply_ind (x : M) (hx : x âˆˆ s) : fs (fs.ind x hx) x = 1 :=
  (fs.eventuallyEq_one x hx).eq_of_nhds
#align smooth_bump_covering.apply_ind SmoothBumpCovering.apply_ind

theorem mem_support_ind (x : M) (hx : x âˆˆ s) : x âˆˆ support (fs <| fs.ind x hx) := by
  simp [fs.apply_ind x hx]
#align smooth_bump_covering.mem_support_ind SmoothBumpCovering.mem_support_ind

theorem mem_chartAt_ind_source (x : M) (hx : x âˆˆ s) : x âˆˆ (chartAt H (fs.c (fs.ind x hx))).source :=
  fs.mem_chartAt_source_of_eq_one (fs.apply_ind x hx)
#align smooth_bump_covering.mem_chart_at_ind_source SmoothBumpCovering.mem_chartAt_ind_source

theorem mem_extChartAt_ind_source (x : M) (hx : x âˆˆ s) :
    x âˆˆ (extChartAt I (fs.c (fs.ind x hx))).source :=
  fs.mem_extChartAt_source_of_eq_one (fs.apply_ind x hx)
#align smooth_bump_covering.mem_ext_chart_at_ind_source SmoothBumpCovering.mem_extChartAt_ind_source

/-- The index type of a `SmoothBumpCovering` of a compact manifold is finite. -/
protected def fintype [CompactSpace M] : Fintype Î¹ :=
  fs.locallyFinite.fintypeOfCompact fun i => (fs i).nonempty_support
#align smooth_bump_covering.fintype SmoothBumpCovering.fintype

variable [T2Space M]

/-- Reinterpret a `SmoothBumpCovering` as a continuous `BumpCovering`. Note that not every
`f : BumpCovering Î¹ M s` with smooth functions `f i` is a `SmoothBumpCovering`. -/
def toBumpCovering : BumpCovering Î¹ M s where
  toFun i := âŸ¨fs i, (fs i).continuousâŸ©
  locallyFinite' := fs.locallyFinite
  nonneg' i _ := (fs i).nonneg
  le_one' i _ := (fs i).le_one
  eventuallyEq_one' := fs.eventuallyEq_one'
#align smooth_bump_covering.to_bump_covering SmoothBumpCovering.toBumpCovering

-- Porting note: `simpNF` says that `simp` can't simplify LHS but it can.
@[simp, nolint simpNF]
theorem isSubordinate_toBumpCovering {f : SmoothBumpCovering Î¹ I M s} {U : M â†’ Set M} :
    (f.toBumpCovering.IsSubordinate fun i => U (f.c i)) â†” f.IsSubordinate U :=
  Iff.rfl
#align smooth_bump_covering.is_subordinate_to_bump_covering SmoothBumpCovering.isSubordinate_toBumpCovering

alias âŸ¨_, IsSubordinate.toBumpCoveringâŸ© := isSubordinate_toBumpCovering
#align smooth_bump_covering.is_subordinate.to_bump_covering SmoothBumpCovering.IsSubordinate.toBumpCovering

/-- Every `SmoothBumpCovering` defines a smooth partition of unity. -/
def toSmoothPartitionOfUnity : SmoothPartitionOfUnity Î¹ I M s :=
  fs.toBumpCovering.toSmoothPartitionOfUnity fun i => (fs i).smooth
#align smooth_bump_covering.to_smooth_partition_of_unity SmoothBumpCovering.toSmoothPartitionOfUnity

theorem toSmoothPartitionOfUnity_apply (i : Î¹) (x : M) :
    fs.toSmoothPartitionOfUnity i x = fs i x * âˆá¶  (j) (_ : WellOrderingRel j i), (1 - fs j x) :=
  rfl
#align smooth_bump_covering.to_smooth_partition_of_unity_apply SmoothBumpCovering.toSmoothPartitionOfUnity_apply

theorem toSmoothPartitionOfUnity_eq_mul_prod (i : Î¹) (x : M) (t : Finset Î¹)
    (ht : âˆ€ j, WellOrderingRel j i â†’ fs j x â‰  0 â†’ j âˆˆ t) :
    fs.toSmoothPartitionOfUnity i x =
      fs i x * âˆ j âˆˆ t.filter fun j => WellOrderingRel j i, (1 - fs j x) :=
  fs.toBumpCovering.toPartitionOfUnity_eq_mul_prod i x t ht
#align smooth_bump_covering.to_smooth_partition_of_unity_eq_mul_prod SmoothBumpCovering.toSmoothPartitionOfUnity_eq_mul_prod

theorem exists_finset_toSmoothPartitionOfUnity_eventuallyEq (i : Î¹) (x : M) :
    âˆƒ t : Finset Î¹,
      fs.toSmoothPartitionOfUnity i =á¶ [ğ“ x]
        fs i * âˆ j âˆˆ t.filter fun j => WellOrderingRel j i, ((1 : M â†’ â„) - fs j) := by
  -- Porting note: was defeq, now the continuous lemma uses bundled homs
  simpa using fs.toBumpCovering.exists_finset_toPartitionOfUnity_eventuallyEq i x
#align smooth_bump_covering.exists_finset_to_smooth_partition_of_unity_eventually_eq SmoothBumpCovering.exists_finset_toSmoothPartitionOfUnity_eventuallyEq

theorem toSmoothPartitionOfUnity_zero_of_zero {i : Î¹} {x : M} (h : fs i x = 0) :
    fs.toSmoothPartitionOfUnity i x = 0 :=
  fs.toBumpCovering.toPartitionOfUnity_zero_of_zero h
#align smooth_bump_covering.to_smooth_partition_of_unity_zero_of_zero SmoothBumpCovering.toSmoothPartitionOfUnity_zero_of_zero

theorem support_toSmoothPartitionOfUnity_subset (i : Î¹) :
    support (fs.toSmoothPartitionOfUnity i) âŠ† support (fs i) :=
  fs.toBumpCovering.support_toPartitionOfUnity_subset i
#align smooth_bump_covering.support_to_smooth_partition_of_unity_subset SmoothBumpCovering.support_toSmoothPartitionOfUnity_subset

theorem IsSubordinate.toSmoothPartitionOfUnity {f : SmoothBumpCovering Î¹ I M s} {U : M â†’ Set M}
    (h : f.IsSubordinate U) : f.toSmoothPartitionOfUnity.IsSubordinate fun i => U (f.c i) :=
  h.toBumpCovering.toPartitionOfUnity
#align smooth_bump_covering.is_subordinate.to_smooth_partition_of_unity SmoothBumpCovering.IsSubordinate.toSmoothPartitionOfUnity

theorem sum_toSmoothPartitionOfUnity_eq (x : M) :
    âˆ‘á¶  i, fs.toSmoothPartitionOfUnity i x = 1 - âˆá¶  i, (1 - fs i x) :=
  fs.toBumpCovering.sum_toPartitionOfUnity_eq x
#align smooth_bump_covering.sum_to_smooth_partition_of_unity_eq SmoothBumpCovering.sum_toSmoothPartitionOfUnity_eq

end SmoothBumpCovering

variable (I)

/-- Given two disjoint closed sets `s, t` in a Hausdorff Ïƒ-compact finite dimensional manifold,
there exists an infinitely smooth function that is equal to `0` on `s` and to `1` on `t`.
See also `exists_msmooth_zero_iff_one_iff_of_isClosed`, which ensures additionally that
`f` is equal to `0` exactly on `s` and to `1` exactly on `t`. -/
theorem exists_smooth_zero_one_of_isClosed [T2Space M] [SigmaCompactSpace M] {s t : Set M}
    (hs : IsClosed s) (ht : IsClosed t) (hd : Disjoint s t) :
    âˆƒ f : C^âˆâŸ®I, M; ğ“˜(â„), â„âŸ¯, EqOn f 0 s âˆ§ EqOn f 1 t âˆ§ âˆ€ x, f x âˆˆ Icc 0 1 := by
  have : âˆ€ x âˆˆ t, sá¶œ âˆˆ ğ“ x := fun x hx => hs.isOpen_compl.mem_nhds (disjoint_right.1 hd hx)
  rcases SmoothBumpCovering.exists_isSubordinate I ht this with âŸ¨Î¹, f, hfâŸ©
  set g := f.toSmoothPartitionOfUnity
  refine
    âŸ¨âŸ¨_, g.smooth_sumâŸ©, fun x hx => ?_, fun x => g.sum_eq_one, fun x =>
      âŸ¨g.sum_nonneg x, g.sum_le_one xâŸ©âŸ©
  suffices âˆ€ i, g i x = 0 by simp only [this, ContMDiffMap.coeFn_mk, finsum_zero, Pi.zero_apply]
  refine fun i => f.toSmoothPartitionOfUnity_zero_of_zero ?_
  exact nmem_support.1 (subset_compl_comm.1 (hf.support_subset i) hx)
#align exists_smooth_zero_one_of_closed exists_smooth_zero_one_of_isClosed

/-- Given two disjoint closed sets `s, t` in a Hausdorff normal Ïƒ-compact finite dimensional
manifold `M`, there exists a smooth function `f : M â†’ [0,1]` that vanishes in a neighbourhood of `s`
and is equal to `1` in a neighbourhood of `t`. -/
theorem exists_smooth_zero_one_nhds_of_isClosed [T2Space M] [NormalSpace M] [SigmaCompactSpace M]
    {s t : Set M} (hs : IsClosed s) (ht : IsClosed t) (hd : Disjoint s t) :
    âˆƒ f : C^âˆâŸ®I, M; ğ“˜(â„), â„âŸ¯, (âˆ€á¶  x in ğ“Ë¢ s, f x = 0) âˆ§ (âˆ€á¶  x in ğ“Ë¢ t, f x = 1) âˆ§
      âˆ€ x, f x âˆˆ Icc 0 1 := by
  obtain âŸ¨u, u_op, hsu, hutâŸ© := normal_exists_closure_subset hs ht.isOpen_compl
    (subset_compl_iff_disjoint_left.mpr hd.symm)
  obtain âŸ¨v, v_op, htv, hvuâŸ© := normal_exists_closure_subset ht isClosed_closure.isOpen_compl
    (subset_compl_comm.mp hut)
  obtain âŸ¨f, hfu, hfv, hfâŸ© := exists_smooth_zero_one_of_isClosed I isClosed_closure isClosed_closure
    (subset_compl_iff_disjoint_left.mp hvu)
  refine âŸ¨f, ?_, ?_, hfâŸ©
  Â· exact eventually_of_mem (mem_of_superset (u_op.mem_nhdsSet.mpr hsu) subset_closure) hfu
  Â· exact eventually_of_mem (mem_of_superset (v_op.mem_nhdsSet.mpr htv) subset_closure) hfv

/-- Given two sets `s, t` in a Hausdorff normal Ïƒ-compact finite-dimensional manifold `M`
with `s` open and `s âŠ† interior t`, there is a smooth function `f : M â†’ [0,1]` which is equal to `s`
in a neighbourhood of `s` and has support contained in `t`. -/
theorem exists_smooth_one_nhds_of_subset_interior [T2Space M] [NormalSpace M] [SigmaCompactSpace M]
    {s t : Set M} (hs : IsClosed s) (hd : s âŠ† interior t) :
    âˆƒ f : C^âˆâŸ®I, M; ğ“˜(â„), â„âŸ¯, (âˆ€á¶  x in ğ“Ë¢ s, f x = 1) âˆ§ (âˆ€ x âˆ‰ t, f x = 0) âˆ§
      âˆ€ x, f x âˆˆ Icc 0 1 := by
  rcases exists_smooth_zero_one_nhds_of_isClosed I isOpen_interior.isClosed_compl hs
    (by rwa [â† subset_compl_iff_disjoint_left, compl_compl]) with âŸ¨f, h0, h1, hfâŸ©
  refine âŸ¨f, h1, fun x hx â†¦ ?_, hfâŸ©
  exact h0.self_of_nhdsSet _ fun hx' â†¦ hx <| interior_subset hx'

namespace SmoothPartitionOfUnity

/-- A `SmoothPartitionOfUnity` that consists of a single function, uniformly equal to one,
defined as an example for `Inhabited` instance. -/
def single (i : Î¹) (s : Set M) : SmoothPartitionOfUnity Î¹ I M s :=
  (BumpCovering.single i s).toSmoothPartitionOfUnity fun j => by
    rcases eq_or_ne j i with (rfl | h)
    Â· simp only [smooth_one, ContinuousMap.coe_one, BumpCovering.coe_single, Pi.single_eq_same]
    Â· simp only [smooth_zero, BumpCovering.coe_single, Pi.single_eq_of_ne h, ContinuousMap.coe_zero]
#align smooth_partition_of_unity.single SmoothPartitionOfUnity.single

instance [Inhabited Î¹] (s : Set M) : Inhabited (SmoothPartitionOfUnity Î¹ I M s) :=
  âŸ¨single I default sâŸ©

variable [T2Space M] [SigmaCompactSpace M]

/-- If `X` is a paracompact normal topological space and `U` is an open covering of a closed set
`s`, then there exists a `SmoothPartitionOfUnity Î¹ M s` that is subordinate to `U`. -/
theorem exists_isSubordinate {s : Set M} (hs : IsClosed s) (U : Î¹ â†’ Set M) (ho : âˆ€ i, IsOpen (U i))
    (hU : s âŠ† â‹ƒ i, U i) : âˆƒ f : SmoothPartitionOfUnity Î¹ I M s, f.IsSubordinate U := by
  haveI : LocallyCompactSpace H := I.locallyCompactSpace
  haveI : LocallyCompactSpace M := ChartedSpace.locallyCompactSpace H M
  -- porting note(https://github.com/leanprover/std4/issues/116):
  -- split `rcases` into `have` + `rcases`
  have := BumpCovering.exists_isSubordinate_of_prop (Smooth I ğ“˜(â„)) ?_ hs U ho hU
  Â· rcases this with âŸ¨f, hf, hfUâŸ©
    exact âŸ¨f.toSmoothPartitionOfUnity hf, hfU.toSmoothPartitionOfUnity hfâŸ©
  Â· intro s t hs ht hd
    rcases exists_smooth_zero_one_of_isClosed I hs ht hd with âŸ¨f, hfâŸ©
    exact âŸ¨f, f.smooth, hfâŸ©
#align smooth_partition_of_unity.exists_is_subordinate SmoothPartitionOfUnity.exists_isSubordinate

theorem exists_isSubordinate_chartAt_source_of_isClosed {s : Set M} (hs : IsClosed s) :
    âˆƒ f : SmoothPartitionOfUnity s I M s,
      f.IsSubordinate (fun x â†¦ (chartAt H (x : M)).source) := by
  apply exists_isSubordinate _ hs _ (fun i â†¦ (chartAt H _).open_source) (fun x hx â†¦ ?_)
  exact mem_iUnion_of_mem âŸ¨x, hxâŸ© (mem_chart_source H x)

variable (M)
theorem exists_isSubordinate_chartAt_source :
    âˆƒ f : SmoothPartitionOfUnity M I M univ, f.IsSubordinate (fun x â†¦ (chartAt H x).source) := by
  apply exists_isSubordinate _ isClosed_univ _ (fun i â†¦ (chartAt H _).open_source) (fun x _ â†¦ ?_)
  exact mem_iUnion_of_mem x (mem_chart_source H x)

end SmoothPartitionOfUnity

variable [SigmaCompactSpace M] [T2Space M] {t : M â†’ Set F} {n : â„•âˆ}

/-- Let `M` be a Ïƒ-compact Hausdorff finite dimensional topological manifold. Let `t : M â†’ Set F`
be a family of convex sets. Suppose that for each point `x : M` there exists a neighborhood
`U âˆˆ ğ“ x` and a function `g : M â†’ F` such that `g` is $C^n$ smooth on `U` and `g y âˆˆ t y` for all
`y âˆˆ U`. Then there exists a $C^n$ smooth function `g : C^âˆâŸ®I, M; ğ“˜(â„, F), FâŸ¯` such that `g x âˆˆ t x`
for all `x`. See also `exists_smooth_forall_mem_convex_of_local` and
`exists_smooth_forall_mem_convex_of_local_const`. -/
theorem exists_contMDiffOn_forall_mem_convex_of_local (ht : âˆ€ x, Convex â„ (t x))
    (Hloc : âˆ€ x : M, âˆƒ U âˆˆ ğ“ x, âˆƒ g : M â†’ F, ContMDiffOn I ğ“˜(â„, F) n g U âˆ§ âˆ€ y âˆˆ U, g y âˆˆ t y) :
    âˆƒ g : C^nâŸ®I, M; ğ“˜(â„, F), FâŸ¯, âˆ€ x, g x âˆˆ t x := by
  choose U hU g hgs hgt using Hloc
  obtain âŸ¨f, hfâŸ© :=
    SmoothPartitionOfUnity.exists_isSubordinate I isClosed_univ (fun x => interior (U x))
      (fun x => isOpen_interior) fun x _ => mem_iUnion.2 âŸ¨x, mem_interior_iff_mem_nhds.2 (hU x)âŸ©
  refine âŸ¨âŸ¨fun x => âˆ‘á¶  i, f i x â€¢ g i x,
      hf.contMDiff_finsum_smul (fun i => isOpen_interior) fun i => (hgs i).mono interior_subsetâŸ©,
    fun x => f.finsum_smul_mem_convex (mem_univ x) (fun i hi => hgt _ _ ?_) (ht _)âŸ©
  exact interior_subset (hf _ <| subset_closure hi)
#align exists_cont_mdiff_forall_mem_convex_of_local exists_contMDiffOn_forall_mem_convex_of_local

/-- Let `M` be a Ïƒ-compact Hausdorff finite dimensional topological manifold. Let `t : M â†’ Set F`
be a family of convex sets. Suppose that for each point `x : M` there exists a neighborhood
`U âˆˆ ğ“ x` and a function `g : M â†’ F` such that `g` is smooth on `U` and `g y âˆˆ t y` for all `y âˆˆ U`.
Then there exists a smooth function `g : C^âˆâŸ®I, M; ğ“˜(â„, F), FâŸ¯` such that `g x âˆˆ t x` for all `x`.
See also `exists_contMDiffOn_forall_mem_convex_of_local` and
`exists_smooth_forall_mem_convex_of_local_const`. -/
theorem exists_smooth_forall_mem_convex_of_local (ht : âˆ€ x, Convex â„ (t x))
    (Hloc : âˆ€ x : M, âˆƒ U âˆˆ ğ“ x, âˆƒ g : M â†’ F, SmoothOn I ğ“˜(â„, F) g U âˆ§ âˆ€ y âˆˆ U, g y âˆˆ t y) :
    âˆƒ g : C^âˆâŸ®I, M; ğ“˜(â„, F), FâŸ¯, âˆ€ x, g x âˆˆ t x :=
  exists_contMDiffOn_forall_mem_convex_of_local I ht Hloc
#align exists_smooth_forall_mem_convex_of_local exists_smooth_forall_mem_convex_of_local

/-- Let `M` be a Ïƒ-compact Hausdorff finite dimensional topological manifold. Let `t : M â†’ Set F` be
a family of convex sets. Suppose that for each point `x : M` there exists a vector `c : F` such that
for all `y` in a neighborhood of `x` we have `c âˆˆ t y`. Then there exists a smooth function
`g : C^âˆâŸ®I, M; ğ“˜(â„, F), FâŸ¯` such that `g x âˆˆ t x` for all `x`.  See also
`exists_contMDiffOn_forall_mem_convex_of_local` and `exists_smooth_forall_mem_convex_of_local`. -/
theorem exists_smooth_forall_mem_convex_of_local_const (ht : âˆ€ x, Convex â„ (t x))
    (Hloc : âˆ€ x : M, âˆƒ c : F, âˆ€á¶  y in ğ“ x, c âˆˆ t y) : âˆƒ g : C^âˆâŸ®I, M; ğ“˜(â„, F), FâŸ¯, âˆ€ x, g x âˆˆ t x :=
  exists_smooth_forall_mem_convex_of_local I ht fun x =>
    let âŸ¨c, hcâŸ© := Hloc x
    âŸ¨_, hc, fun _ => c, smoothOn_const, fun _ => idâŸ©
#align exists_smooth_forall_mem_convex_of_local_const exists_smooth_forall_mem_convex_of_local_const

/-- Let `M` be a smooth Ïƒ-compact manifold with extended distance. Let `K : Î¹ â†’ Set M` be a locally
finite family of closed sets, let `U : Î¹ â†’ Set M` be a family of open sets such that `K i âŠ† U i` for
all `i`. Then there exists a positive smooth function `Î´ : M â†’ â„â‰¥0` such that for any `i` and
`x âˆˆ K i`, we have `EMetric.closedBall x (Î´ x) âŠ† U i`. -/
theorem Emetric.exists_smooth_forall_closedBall_subset {M} [EMetricSpace M] [ChartedSpace H M]
    [SmoothManifoldWithCorners I M] [SigmaCompactSpace M] {K : Î¹ â†’ Set M} {U : Î¹ â†’ Set M}
    (hK : âˆ€ i, IsClosed (K i)) (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i)
    (hfin : LocallyFinite K) :
    âˆƒ Î´ : C^âˆâŸ®I, M; ğ“˜(â„, â„), â„âŸ¯,
      (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ (i), âˆ€ x âˆˆ K i, EMetric.closedBall x (ENNReal.ofReal (Î´ x)) âŠ† U i := by
  simpa only [mem_inter_iff, forall_and, mem_preimage, mem_iInter, @forall_swap Î¹ M]
    using exists_smooth_forall_mem_convex_of_local_const I
      EMetric.exists_forall_closedBall_subset_auxâ‚‚
      (EMetric.exists_forall_closedBall_subset_auxâ‚ hK hU hKU hfin)
#align emetric.exists_smooth_forall_closed_ball_subset Emetric.exists_smooth_forall_closedBall_subset

/-- Let `M` be a smooth Ïƒ-compact manifold with a metric. Let `K : Î¹ â†’ Set M` be a locally finite
family of closed sets, let `U : Î¹ â†’ Set M` be a family of open sets such that `K i âŠ† U i` for all
`i`. Then there exists a positive smooth function `Î´ : M â†’ â„â‰¥0` such that for any `i` and `x âˆˆ K i`,
we have `Metric.closedBall x (Î´ x) âŠ† U i`. -/
theorem Metric.exists_smooth_forall_closedBall_subset {M} [MetricSpace M] [ChartedSpace H M]
    [SmoothManifoldWithCorners I M] [SigmaCompactSpace M] {K : Î¹ â†’ Set M} {U : Î¹ â†’ Set M}
    (hK : âˆ€ i, IsClosed (K i)) (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i)
    (hfin : LocallyFinite K) :
    âˆƒ Î´ : C^âˆâŸ®I, M; ğ“˜(â„, â„), â„âŸ¯,
      (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ (i), âˆ€ x âˆˆ K i, Metric.closedBall x (Î´ x) âŠ† U i := by
  rcases Emetric.exists_smooth_forall_closedBall_subset I hK hU hKU hfin with âŸ¨Î´, hÎ´0, hÎ´âŸ©
  refine âŸ¨Î´, hÎ´0, fun i x hx => ?_âŸ©
  rw [â† Metric.emetric_closedBall (hÎ´0 _).le]
  exact hÎ´ i x hx
#align metric.exists_smooth_forall_closed_ball_subset Metric.exists_smooth_forall_closedBall_subset

lemma IsOpen.exists_msmooth_support_eq_aux {s : Set H} (hs : IsOpen s) :
    âˆƒ f : H â†’ â„, f.support = s âˆ§ Smooth I ğ“˜(â„) f âˆ§ Set.range f âŠ† Set.Icc 0 1 := by
  have h's : IsOpen (I.symm â»Â¹' s) := I.continuous_symm.isOpen_preimage _ hs
  rcases h's.exists_smooth_support_eq with âŸ¨f, f_supp, f_diff, f_rangeâŸ©
  refine âŸ¨f âˆ˜ I, ?_, ?_, ?_âŸ©
  Â· rw [support_comp_eq_preimage, f_supp, â† preimage_comp]
    simp only [ModelWithCorners.symm_comp_self, preimage_id_eq, id_eq]
  Â· exact f_diff.comp_contMDiff contMDiff_model
  Â· exact Subset.trans (range_comp_subset_range _ _) f_range

/-- Given an open set in a finite-dimensional real manifold, there exists a nonnegative smooth
function with support equal to `s`. -/
theorem IsOpen.exists_msmooth_support_eq {s : Set M} (hs : IsOpen s) :
    âˆƒ f : M â†’ â„, f.support = s âˆ§ Smooth I ğ“˜(â„) f âˆ§ âˆ€ x, 0 â‰¤ f x := by
  rcases SmoothPartitionOfUnity.exists_isSubordinate_chartAt_source I M with âŸ¨f, hfâŸ©
  have A : âˆ€ (c : M), âˆƒ g : H â†’ â„,
      g.support = (chartAt H c).target âˆ© (chartAt H c).symm â»Â¹' s âˆ§
      Smooth I ğ“˜(â„) g âˆ§ Set.range g âŠ† Set.Icc 0 1 := by
    intro i
    apply IsOpen.exists_msmooth_support_eq_aux
    exact PartialHomeomorph.isOpen_inter_preimage_symm _ hs
  choose g g_supp g_diff hg using A
  have h'g : âˆ€ c x, 0 â‰¤ g c x := fun c x â†¦ (hg c (mem_range_self (f := g c) x)).1
  have h''g : âˆ€ c x, 0 â‰¤ f c x * g c (chartAt H c x) :=
    fun c x â†¦ mul_nonneg (f.nonneg c x) (h'g c _)
  refine âŸ¨fun x â†¦ âˆ‘á¶  c, f c x * g c (chartAt H c x), ?_, ?_, ?_âŸ©
  Â· refine support_eq_iff.2 âŸ¨fun x hx â†¦ ?_, fun x hx â†¦ ?_âŸ©
    Â· apply ne_of_gt
      have B : âˆƒ c, 0 < f c x * g c (chartAt H c x) := by
        obtain âŸ¨c, hcâŸ© : âˆƒ c, 0 < f c x := f.exists_pos_of_mem (mem_univ x)
        refine âŸ¨c, mul_pos hc ?_âŸ©
        apply lt_of_le_of_ne (h'g _ _) (Ne.symm _)
        rw [â† mem_support, g_supp, â† mem_preimage, preimage_inter]
        have Hx : x âˆˆ tsupport (f c) := subset_tsupport _ (ne_of_gt hc)
        simp [(chartAt H c).left_inv (hf c Hx), hx, (chartAt H c).map_source (hf c Hx)]
      apply finsum_pos' (fun c â†¦ h''g c x) B
      apply (f.locallyFinite.point_finite x).subset
      apply compl_subset_compl.2
      rintro c (hc : f c x = 0)
      simpa only [mul_eq_zero] using Or.inl hc
    Â· apply finsum_eq_zero_of_forall_eq_zero
      intro c
      by_cases Hx : x âˆˆ tsupport (f c)
      Â· suffices g c (chartAt H c x) = 0 by simp only [this, mul_zero]
        rw [â† nmem_support, g_supp, â† mem_preimage, preimage_inter]
        contrapose! hx
        simp only [mem_inter_iff, mem_preimage, (chartAt H c).left_inv (hf c Hx)] at hx
        exact hx.2
      Â· have : x âˆ‰ support (f c) := by contrapose! Hx; exact subset_tsupport _ Hx
        rw [nmem_support] at this
        simp [this]
  Â· apply SmoothPartitionOfUnity.smooth_finsum_smul
    intro c x hx
    apply (g_diff c (chartAt H c x)).comp
    exact contMDiffAt_of_mem_maximalAtlas (SmoothManifoldWithCorners.chart_mem_maximalAtlas I _)
      (hf c hx)
  Â· intro x
    apply finsum_nonneg (fun c â†¦ h''g c x)

/-- Given an open set `s` containing a closed set `t` in a finite-dimensional real manifold, there
exists a smooth function with support equal to `s`, taking values in `[0,1]`, and equal to `1`
exactly on `t`. -/
theorem exists_msmooth_support_eq_eq_one_iff
    {s t : Set M} (hs : IsOpen s) (ht : IsClosed t) (h : t âŠ† s) :
    âˆƒ f : M â†’ â„, Smooth I ğ“˜(â„) f âˆ§ range f âŠ† Icc 0 1 âˆ§ support f = s
      âˆ§ (âˆ€ x, x âˆˆ t â†” f x = 1) := by
  /- Take `f` with support equal to `s`, and `g` with support equal to `tá¶œ`. Then `f / (f + g)`
  satisfies the conclusion of the theorem. -/
  rcases hs.exists_msmooth_support_eq I with âŸ¨f, f_supp, f_diff, f_posâŸ©
  rcases ht.isOpen_compl.exists_msmooth_support_eq I with âŸ¨g, g_supp, g_diff, g_posâŸ©
  have A : âˆ€ x, 0 < f x + g x := by
    intro x
    by_cases xs : x âˆˆ support f
    Â· have : 0 < f x := lt_of_le_of_ne (f_pos x) (Ne.symm xs)
      linarith [g_pos x]
    Â· have : 0 < g x := by
        apply lt_of_le_of_ne (g_pos x) (Ne.symm ?_)
        rw [â† mem_support, g_supp]
        contrapose! xs
        simp? at xs says simp only [mem_compl_iff, Decidable.not_not] at xs
        exact h.trans f_supp.symm.subset xs
      linarith [f_pos x]
  refine âŸ¨fun x â†¦ f x / (f x + g x), ?_, ?_, ?_, ?_âŸ©
  -- show that `f / (f + g)` is smooth
  Â· exact f_diff.divâ‚€ (f_diff.add g_diff) (fun x â†¦ ne_of_gt (A x))
  -- show that the range is included in `[0, 1]`
  Â· refine range_subset_iff.2 (fun x â†¦ âŸ¨div_nonneg (f_pos x) (A x).le, ?_âŸ©)
    apply div_le_one_of_le _ (A x).le
    simpa only [le_add_iff_nonneg_right] using g_pos x
  -- show that the support is `s`
  Â· have B : support (fun x â†¦ f x + g x) = univ := eq_univ_of_forall (fun x â†¦ (A x).ne')
    simp only [support_div, f_supp, B, inter_univ]
  -- show that the function equals one exactly on `t`
  Â· intro x
    simp [div_eq_one_iff_eq (A x).ne', self_eq_add_right, â† nmem_support, g_supp]

/-- Given two disjoint closed sets `s, t` in a Hausdorff Ïƒ-compact finite dimensional manifold,
there exists an infinitely smooth function that is equal to `0` exactly on `s` and to `1`
exactly on `t`. See also `exists_smooth_zero_one_of_isClosed` for a slightly weaker version. -/
theorem exists_msmooth_zero_iff_one_iff_of_isClosed {s t : Set M}
    (hs : IsClosed s) (ht : IsClosed t) (hd : Disjoint s t) :
    âˆƒ f : M â†’ â„, Smooth I ğ“˜(â„) f âˆ§ range f âŠ† Icc 0 1 âˆ§ (âˆ€ x, x âˆˆ s â†” f x = 0)
      âˆ§ (âˆ€ x, x âˆˆ t â†” f x = 1) := by
  rcases exists_msmooth_support_eq_eq_one_iff I hs.isOpen_compl ht hd.subset_compl_left with
    âŸ¨f, f_diff, f_range, fs, ftâŸ©
  refine âŸ¨f, f_diff, f_range, ?_, ftâŸ©
  simp [â† nmem_support, fs]
