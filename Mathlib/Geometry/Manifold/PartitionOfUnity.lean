/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Geometry.Manifold.Algebra.Structures
import Mathlib.Geometry.Manifold.BumpFunction
import Mathlib.Topology.MetricSpace.PartitionOfUnity
import Mathlib.Topology.ShrinkingLemma

/-!
# Smooth partition of unity

In this file we define two structures, `SmoothBumpCovering` and `SmoothPartitionOfUnity`. Both
structures describe coverings of a set by a locally finite family of supports of smooth functions
with some additional properties. The former structure is mostly useful as an intermediate step in
the construction of a smooth partition of unity but some proofs that traditionally deal with a
partition of unity can use a `SmoothBumpCovering` as well.

Given a real manifold `M` and its subset `s`, a `SmoothBumpCovering Œπ I M s` is a collection of
`SmoothBumpFunction`s `f i` indexed by `i : Œπ` such that

* the center of each `f i` belongs to `s`;
* the family of sets `support (f i)` is locally finite;
* for each `x ‚àà s`, there exists `i : Œπ` such that `f i =·∂†[ùìù x] 1`.

In the same settings, a `SmoothPartitionOfUnity Œπ I M s` is a collection of smooth nonnegative
functions `f i : C^‚àû‚üÆI, M; ùìò(‚Ñù), ‚Ñù‚üØ`, `i : Œπ`, such that

* the family of sets `support (f i)` is locally finite;
* for each `x ‚àà s`, the sum `‚àë·∂† i, f i x` equals one;
* for each `x`, the sum `‚àë·∂† i, f i x` is less than or equal to one.

We say that `f : SmoothBumpCovering Œπ I M s` is *subordinate* to a map `U : M ‚Üí Set M` if for each
index `i`, we have `tsupport (f i) ‚äÜ U (f i).c`. This notion is a bit more general than
being subordinate to an open covering of `M`, because we make no assumption about the way `U x`
depends on `x`.

We prove that on a smooth finitely dimensional real manifold with `œÉ`-compact Hausdorff topology,
for any `U : M ‚Üí Set M` such that `‚àÄ x ‚àà s, U x ‚àà ùìù x` there exists a `SmoothBumpCovering Œπ I M s`
subordinate to `U`. Then we use this fact to prove a similar statement about smooth partitions of
unity, see `SmoothPartitionOfUnity.exists_isSubordinate`.

Finally, we use existence of a partition of unity to prove lemma
`exists_smooth_forall_mem_convex_of_local` that allows us to construct a globally defined smooth
function from local functions.

## TODO

* Build a framework for to transfer local definitions to global using partition of unity and use it
  to define, e.g., the integral of a differential form over a manifold. Lemma
  `exists_smooth_forall_mem_convex_of_local` is a first step in this direction.

## Tags

smooth bump function, partition of unity
-/

universe uŒπ uE uH uM uF

open Function Filter Module Set
open scoped Topology Manifold ContDiff

noncomputable section

variable {Œπ : Type uŒπ} {E : Type uE} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {F : Type uF} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] {H : Type uH}
  [TopologicalSpace H] (I : ModelWithCorners ‚Ñù E H) {M : Type uM} [TopologicalSpace M]
  [ChartedSpace H M]

/-!
### Covering by supports of smooth bump functions

In this section we define `SmoothBumpCovering Œπ I M s` to be a collection of
`SmoothBumpFunction`s such that their supports is a locally finite family of sets and for each
`x ‚àà s` some function `f i` from the collection is equal to `1` in a neighborhood of `x`. A covering
of this type is useful to construct a smooth partition of unity and can be used instead of a
partition of unity in some proofs.

We prove that on a smooth finite dimensional real manifold with `œÉ`-compact Hausdorff topology, for
any `U : M ‚Üí Set M` such that `‚àÄ x ‚àà s, U x ‚àà ùìù x` there exists a `SmoothBumpCovering Œπ I M s`
subordinate to `U`. -/

variable (Œπ M)

/-- We say that a collection of `SmoothBumpFunction`s is a `SmoothBumpCovering` of a set `s` if

* `(f i).c ‚àà s` for all `i`;
* the family `fun i ‚Ü¶ support (f i)` is locally finite;
* for each point `x ‚àà s` there exists `i` such that `f i =·∂†[ùìù x] 1`;
  in other words, `x` belongs to the interior of `{y | f i y = 1}`;

If `M` is a finite dimensional real manifold which is a `œÉ`-compact Hausdorff topological space,
then for every covering `U : M ‚Üí Set M`, `‚àÄ x, U x ‚àà ùìù x`, there exists a `SmoothBumpCovering`
subordinate to `U`, see `SmoothBumpCovering.exists_isSubordinate`.

This covering can be used, e.g., to construct a partition of unity and to prove the weak
Whitney embedding theorem. -/
structure SmoothBumpCovering [FiniteDimensional ‚Ñù E] (s : Set M := univ) where
  /-- The center point of each bump in the smooth covering. -/
  c : Œπ ‚Üí M
  /-- A smooth bump function around `c i`. -/
  toFun : ‚àÄ i, SmoothBumpFunction I (c i)
  /-- All the bump functions in the covering are centered at points in `s`. -/
  c_mem' : ‚àÄ i, c i ‚àà s
  /-- Around each point, there are only finitely many nonzero bump functions in the family. -/
  locallyFinite' : LocallyFinite fun i ‚Ü¶ support (toFun i)
  /-- Around each point in `s`, one of the bump functions is equal to `1`. -/
  eventuallyEq_one' : ‚àÄ x ‚àà s, ‚àÉ i, toFun i =·∂†[ùìù x] 1

/-- We say that a collection of functions form a smooth partition of unity on a set `s` if

* all functions are infinitely smooth and nonnegative;
* the family `fun i ‚Ü¶ support (f i)` is locally finite;
* for all `x ‚àà s` the sum `‚àë·∂† i, f i x` equals one;
* for all `x`, the sum `‚àë·∂† i, f i x` is less than or equal to one. -/
structure SmoothPartitionOfUnity (s : Set M := univ) where
  /-- The family of functions forming the partition of unity. -/
  toFun : Œπ ‚Üí C^‚àû‚üÆI, M; ùìò(‚Ñù), ‚Ñù‚üØ
  /-- Around each point, there are only finitely many nonzero functions in the family. -/
  locallyFinite' : LocallyFinite fun i ‚Ü¶ support (toFun i)
  /-- All the functions in the partition of unity are nonnegative. -/
  nonneg' : ‚àÄ i x, 0 ‚â§ toFun i x
  /-- The functions in the partition of unity add up to `1` at any point of `s`. -/
  sum_eq_one' : ‚àÄ x ‚àà s, ‚àë·∂† i, toFun i x = 1
  /-- The functions in the partition of unity add up to at most `1` everywhere. -/
  sum_le_one' : ‚àÄ x, ‚àë·∂† i, toFun i x ‚â§ 1

variable {Œπ I M}

namespace SmoothPartitionOfUnity

variable {s : Set M} (f : SmoothPartitionOfUnity Œπ I M s) {n : ‚Ñï‚àû}

instance {s : Set M} : FunLike (SmoothPartitionOfUnity Œπ I M s) Œπ C^‚àû‚üÆI, M; ùìò(‚Ñù), ‚Ñù‚üØ where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr

protected theorem locallyFinite : LocallyFinite fun i ‚Ü¶ support (f i) :=
  f.locallyFinite'

theorem nonneg (i : Œπ) (x : M) : 0 ‚â§ f i x :=
  f.nonneg' i x

theorem sum_eq_one {x} (hx : x ‚àà s) : ‚àë·∂† i, f i x = 1 :=
  f.sum_eq_one' x hx

theorem exists_pos_of_mem {x} (hx : x ‚àà s) : ‚àÉ i, 0 < f i x := by
  by_contra! h
  have H : ‚àÄ i, f i x = 0 := fun i ‚Ü¶ le_antisymm (h i) (f.nonneg i x)
  have := f.sum_eq_one hx
  simp_rw [H] at this
  simpa

theorem sum_le_one (x : M) : ‚àë·∂† i, f i x ‚â§ 1 :=
  f.sum_le_one' x

/-- Reinterpret a smooth partition of unity as a continuous partition of unity. -/
@[simps]
def toPartitionOfUnity : PartitionOfUnity Œπ M s :=
  { f with toFun := fun i ‚Ü¶ f i }

theorem contMDiff_sum : ContMDiff I ùìò(‚Ñù) ‚àû fun x ‚Ü¶ ‚àë·∂† i, f i x :=
  contMDiff_finsum (fun i ‚Ü¶ (f i).contMDiff) f.locallyFinite

theorem le_one (i : Œπ) (x : M) : f i x ‚â§ 1 :=
  f.toPartitionOfUnity.le_one i x

theorem sum_nonneg (x : M) : 0 ‚â§ ‚àë·∂† i, f i x :=
  f.toPartitionOfUnity.sum_nonneg x

theorem finsum_smul_mem_convex {g : Œπ ‚Üí M ‚Üí F} {t : Set F} {x : M} (hx : x ‚àà s)
    (hg : ‚àÄ i, f i x ‚â† 0 ‚Üí g i x ‚àà t) (ht : Convex ‚Ñù t) : ‚àë·∂† i, f i x ‚Ä¢ g i x ‚àà t :=
  ht.finsum_mem (fun _ ‚Ü¶ f.nonneg _ _) (f.sum_eq_one hx) hg

theorem contMDiff_smul {g : M ‚Üí F} {i} (hg : ‚àÄ x ‚àà tsupport (f i), ContMDiffAt I ùìò(‚Ñù, F) n g x) :
    ContMDiff I ùìò(‚Ñù, F) n fun x ‚Ü¶ f i x ‚Ä¢ g x :=
  contMDiff_of_tsupport fun x hx ‚Ü¶
    ((f i).contMDiff.contMDiffAt.of_le (mod_cast le_top)).smul <| hg x
      <| tsupport_smul_subset_left _ _ hx

/-- If `f` is a smooth partition of unity on a set `s : Set M` and `g : Œπ ‚Üí M ‚Üí F` is a family of
functions such that `g i` is $C^n$ smooth at every point of the topological support of `f i`, then
the sum `fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x` is smooth on the whole manifold. -/
theorem contMDiff_finsum_smul {g : Œπ ‚Üí M ‚Üí F}
    (hg : ‚àÄ (i), ‚àÄ x ‚àà tsupport (f i), ContMDiffAt I ùìò(‚Ñù, F) n (g i) x) :
    ContMDiff I ùìò(‚Ñù, F) n fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x :=
  (contMDiff_finsum fun i ‚Ü¶ f.contMDiff_smul (hg i)) <|
    f.locallyFinite.subset fun _ ‚Ü¶ support_smul_subset_left _ _

theorem contMDiffAt_finsum {x‚ÇÄ : M} {g : Œπ ‚Üí M ‚Üí F}
    (hœÜ : ‚àÄ i, x‚ÇÄ ‚àà tsupport (f i) ‚Üí ContMDiffAt I ùìò(‚Ñù, F) n (g i) x‚ÇÄ) :
    ContMDiffAt I ùìò(‚Ñù, F) n (fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x) x‚ÇÄ := by
  refine _root_.contMDiffAt_finsum (f.locallyFinite.smul_left _) fun i ‚Ü¶ ?_
  by_cases hx : x‚ÇÄ ‚àà tsupport (f i)
  ¬∑ exact ContMDiffAt.smul ((f i).contMDiff.of_le (mod_cast le_top)).contMDiffAt (hœÜ i hx)
  ¬∑ exact contMDiffAt_of_notMem (compl_subset_compl.mpr
      (tsupport_smul_subset_left (f i) (g i)) hx) n

theorem contDiffAt_finsum {s : Set E} (f : SmoothPartitionOfUnity Œπ ùìò(‚Ñù, E) E s) {x‚ÇÄ : E}
    {g : Œπ ‚Üí E ‚Üí F} (hœÜ : ‚àÄ i, x‚ÇÄ ‚àà tsupport (f i) ‚Üí ContDiffAt ‚Ñù n (g i) x‚ÇÄ) :
    ContDiffAt ‚Ñù n (fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x) x‚ÇÄ := by
  simp only [‚Üê contMDiffAt_iff_contDiffAt] at *
  exact f.contMDiffAt_finsum hœÜ

section finsupport

variable {s : Set M} (œÅ : SmoothPartitionOfUnity Œπ I M s) (x‚ÇÄ : M)

/-- The support of a smooth partition of unity at a point `x‚ÇÄ` as a `Finset`.
This is the set of `i : Œπ` such that `x‚ÇÄ ‚àà support f i`, i.e. `f i ‚â† x‚ÇÄ`. -/
def finsupport : Finset Œπ := œÅ.toPartitionOfUnity.finsupport x‚ÇÄ

@[simp]
theorem mem_finsupport {i : Œπ} : i ‚àà œÅ.finsupport x‚ÇÄ ‚Üî i ‚àà support fun i ‚Ü¶ œÅ i x‚ÇÄ :=
  œÅ.toPartitionOfUnity.mem_finsupport x‚ÇÄ

@[simp]
theorem coe_finsupport : (œÅ.finsupport x‚ÇÄ : Set Œπ) = support fun i ‚Ü¶ œÅ i x‚ÇÄ :=
  œÅ.toPartitionOfUnity.coe_finsupport x‚ÇÄ

theorem sum_finsupport (hx‚ÇÄ : x‚ÇÄ ‚àà s) : ‚àë i ‚àà œÅ.finsupport x‚ÇÄ, œÅ i x‚ÇÄ = 1 :=
  œÅ.toPartitionOfUnity.sum_finsupport hx‚ÇÄ

theorem sum_finsupport' (hx‚ÇÄ : x‚ÇÄ ‚àà s) {I : Finset Œπ} (hI : œÅ.finsupport x‚ÇÄ ‚äÜ I) :
    ‚àë i ‚àà I, œÅ i x‚ÇÄ = 1 :=
  œÅ.toPartitionOfUnity.sum_finsupport' hx‚ÇÄ hI

theorem sum_finsupport_smul_eq_finsum {A : Type*} [AddCommGroup A] [Module ‚Ñù A] (œÜ : Œπ ‚Üí M ‚Üí A) :
    ‚àë i ‚àà œÅ.finsupport x‚ÇÄ, œÅ i x‚ÇÄ ‚Ä¢ œÜ i x‚ÇÄ = ‚àë·∂† i, œÅ i x‚ÇÄ ‚Ä¢ œÜ i x‚ÇÄ :=
  œÅ.toPartitionOfUnity.sum_finsupport_smul_eq_finsum œÜ

end finsupport

section fintsupport -- smooth partitions of unity have locally finite `tsupport`
variable {s : Set M} (œÅ : SmoothPartitionOfUnity Œπ I M s) (x‚ÇÄ : M)

/-- The `tsupport`s of a smooth partition of unity are locally finite. -/
theorem finite_tsupport : {i | x‚ÇÄ ‚àà tsupport (œÅ i)}.Finite :=
  œÅ.toPartitionOfUnity.finite_tsupport _

/-- The tsupport of a partition of unity at a point `x‚ÇÄ` as a `Finset`.
This is the set of `i : Œπ` such that `x‚ÇÄ ‚àà tsupport f i`. -/
def fintsupport (x : M) : Finset Œπ :=
  (œÅ.finite_tsupport x).toFinset

theorem mem_fintsupport_iff (i : Œπ) : i ‚àà œÅ.fintsupport x‚ÇÄ ‚Üî x‚ÇÄ ‚àà tsupport (œÅ i) :=
  Finite.mem_toFinset _

theorem eventually_fintsupport_subset : ‚àÄ·∂† y in ùìù x‚ÇÄ, œÅ.fintsupport y ‚äÜ œÅ.fintsupport x‚ÇÄ :=
  œÅ.toPartitionOfUnity.eventually_fintsupport_subset _

theorem finsupport_subset_fintsupport : œÅ.finsupport x‚ÇÄ ‚äÜ œÅ.fintsupport x‚ÇÄ :=
  œÅ.toPartitionOfUnity.finsupport_subset_fintsupport x‚ÇÄ

theorem eventually_finsupport_subset : ‚àÄ·∂† y in ùìù x‚ÇÄ, œÅ.finsupport y ‚äÜ œÅ.fintsupport x‚ÇÄ :=
  œÅ.toPartitionOfUnity.eventually_finsupport_subset x‚ÇÄ

end fintsupport

section IsSubordinate

/-- A smooth partition of unity `f i` is subordinate to a family of sets `U i` indexed by the same
type if for each `i` the closure of the support of `f i` is a subset of `U i`. -/
def IsSubordinate (f : SmoothPartitionOfUnity Œπ I M s) (U : Œπ ‚Üí Set M) :=
  ‚àÄ i, tsupport (f i) ‚äÜ U i

variable {f}
variable {U : Œπ ‚Üí Set M}

@[simp]
theorem isSubordinate_toPartitionOfUnity :
    f.toPartitionOfUnity.IsSubordinate U ‚Üî f.IsSubordinate U :=
  Iff.rfl

alias ‚ü®_, IsSubordinate.toPartitionOfUnity‚ü© := isSubordinate_toPartitionOfUnity

/-- If `f` is a smooth partition of unity on a set `s : Set M` subordinate to a family of open sets
`U : Œπ ‚Üí Set M` and `g : Œπ ‚Üí M ‚Üí F` is a family of functions such that `g i` is $C^n$ smooth on
`U i`, then the sum `fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x` is $C^n$ smooth on the whole manifold. -/
theorem IsSubordinate.contMDiff_finsum_smul {g : Œπ ‚Üí M ‚Üí F} (hf : f.IsSubordinate U)
    (ho : ‚àÄ i, IsOpen (U i)) (hg : ‚àÄ i, ContMDiffOn I ùìò(‚Ñù, F) n (g i) (U i)) :
    ContMDiff I ùìò(‚Ñù, F) n fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x :=
  f.contMDiff_finsum_smul fun i _ hx ‚Ü¶ (hg i).contMDiffAt <| (ho i).mem_nhds (hf i hx)

end IsSubordinate

end SmoothPartitionOfUnity

namespace BumpCovering

-- Repeat variables to drop `[FiniteDimensional ‚Ñù E]` and `[IsManifold I ‚àû M]`
theorem contMDiff_toPartitionOfUnity {E : Type uE} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    {H : Type uH} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù E H} {M : Type uM}
    [TopologicalSpace M] [ChartedSpace H M] {s : Set M} (f : BumpCovering Œπ M s)
    (hf : ‚àÄ i, ContMDiff I ùìò(‚Ñù) ‚àû (f i)) (i : Œπ) : ContMDiff I ùìò(‚Ñù) ‚àû (f.toPartitionOfUnity i) :=
  (hf i).mul <| (contMDiff_finprod_cond fun j _ ‚Ü¶ contMDiff_const.sub (hf j)) <| by
    simp only [mulSupport_one_sub]
    exact f.locallyFinite

variable {s : Set M}

/-- A `BumpCovering` such that all functions in this covering are smooth generates a smooth
partition of unity.

In our formalization, not every `f : BumpCovering Œπ M s` with smooth functions `f i` is a
`SmoothBumpCovering`; instead, a `SmoothBumpCovering` is a covering by supports of
`SmoothBumpFunction`s. So, we define `BumpCovering.toSmoothPartitionOfUnity`, then reuse it
in `SmoothBumpCovering.toSmoothPartitionOfUnity`. -/
def toSmoothPartitionOfUnity (f : BumpCovering Œπ M s) (hf : ‚àÄ i, ContMDiff I ùìò(‚Ñù) ‚àû (f i)) :
    SmoothPartitionOfUnity Œπ I M s :=
  { f.toPartitionOfUnity with
    toFun := fun i ‚Ü¶ ‚ü®f.toPartitionOfUnity i, f.contMDiff_toPartitionOfUnity hf i‚ü© }

@[simp]
theorem toSmoothPartitionOfUnity_toPartitionOfUnity (f : BumpCovering Œπ M s)
    (hf : ‚àÄ i, ContMDiff I ùìò(‚Ñù) ‚àû (f i)) :
    (f.toSmoothPartitionOfUnity hf).toPartitionOfUnity = f.toPartitionOfUnity :=
  rfl

@[simp]
theorem coe_toSmoothPartitionOfUnity (f : BumpCovering Œπ M s) (hf : ‚àÄ i, ContMDiff I ùìò(‚Ñù) ‚àû (f i))
    (i : Œπ) : ‚áë(f.toSmoothPartitionOfUnity hf i) = f.toPartitionOfUnity i :=
  rfl

theorem IsSubordinate.toSmoothPartitionOfUnity {f : BumpCovering Œπ M s} {U : Œπ ‚Üí Set M}
    (h : f.IsSubordinate U) (hf : ‚àÄ i, ContMDiff I ùìò(‚Ñù) ‚àû (f i)) :
    (f.toSmoothPartitionOfUnity hf).IsSubordinate U :=
  h.toPartitionOfUnity

end BumpCovering

namespace SmoothBumpCovering

variable [FiniteDimensional ‚Ñù E]
variable {s : Set M} {U : M ‚Üí Set M} (fs : SmoothBumpCovering Œπ I M s)

instance : CoeFun (SmoothBumpCovering Œπ I M s) fun x ‚Ü¶ ‚àÄ i : Œπ, SmoothBumpFunction I (x.c i) :=
  ‚ü®toFun‚ü©

/--
We say that `f : SmoothBumpCovering Œπ I M s` is *subordinate* to a map `U : M ‚Üí Set M` if for each
index `i`, we have `tsupport (f i) ‚äÜ U (f i).c`. This notion is a bit more general than
being subordinate to an open covering of `M`, because we make no assumption about the way `U x`
depends on `x`.
-/
def IsSubordinate {s : Set M} (f : SmoothBumpCovering Œπ I M s) (U : M ‚Üí Set M) :=
  ‚àÄ i, tsupport (f i) ‚äÜ U (f.c i)

theorem IsSubordinate.support_subset {fs : SmoothBumpCovering Œπ I M s} {U : M ‚Üí Set M}
    (h : fs.IsSubordinate U) (i : Œπ) : support (fs i) ‚äÜ U (fs.c i) :=
  Subset.trans subset_closure (h i)

variable (I) in
/-- Let `M` be a smooth manifold modelled on a finite dimensional real vector space.
Suppose also that `M` is a Hausdorff `œÉ`-compact topological space. Let `s` be a closed set
in `M` and `U : M ‚Üí Set M` be a collection of sets such that `U x ‚àà ùìù x` for every `x ‚àà s`.
Then there exists a smooth bump covering of `s` that is subordinate to `U`. -/
theorem exists_isSubordinate [T2Space M] [SigmaCompactSpace M] (hs : IsClosed s)
    (hU : ‚àÄ x ‚àà s, U x ‚àà ùìù x) :
    ‚àÉ (Œπ : Type uM) (f : SmoothBumpCovering Œπ I M s), f.IsSubordinate U := by
  -- First we deduce some missing instances
  haveI : LocallyCompactSpace H := I.locallyCompactSpace
  haveI : LocallyCompactSpace M := ChartedSpace.locallyCompactSpace H M
  -- Next we choose a covering by supports of smooth bump functions
  have hB := fun x hx ‚Ü¶ SmoothBumpFunction.nhds_basis_support (I := I) (hU x hx)
  rcases refinement_of_locallyCompact_sigmaCompact_of_nhds_basis_set hs hB with
    ‚ü®Œπ, c, f, hf, hsub', hfin‚ü©
  choose hcs hfU using hf
  -- Then we use the shrinking lemma to get a covering by smaller open
  rcases exists_subset_iUnion_closed_subset hs (fun i ‚Ü¶ (f i).isOpen_support)
    (fun x _ ‚Ü¶ hfin.point_finite x) hsub' with ‚ü®V, hsV, hVc, hVf‚ü©
  choose r hrR hr using fun i ‚Ü¶ (f i).exists_r_pos_lt_subset_ball (hVc i) (hVf i)
  refine ‚ü®Œπ, ‚ü®c, fun i ‚Ü¶ (f i).updateRIn (r i) (hrR i), hcs, ?_, fun x hx ‚Ü¶ ?_‚ü©, fun i ‚Ü¶ ?_‚ü©
  ¬∑ simpa only [SmoothBumpFunction.support_updateRIn]
  ¬∑ refine (mem_iUnion.1 <| hsV hx).imp fun i hi ‚Ü¶ ?_
    exact ((f i).updateRIn _ _).eventuallyEq_one_of_dist_lt
      ((f i).support_subset_source <| hVf _ hi) (hr i hi).2
  ¬∑ simpa only [SmoothBumpFunction.support_updateRIn, tsupport] using hfU i

protected theorem locallyFinite : LocallyFinite fun i ‚Ü¶ support (fs i) :=
  fs.locallyFinite'

protected theorem point_finite (x : M) : {i | fs i x ‚â† 0}.Finite :=
  fs.locallyFinite.point_finite x

/-- Index of a bump function such that `fs i =·∂†[ùìù x] 1`. -/
def ind (x : M) (hx : x ‚àà s) : Œπ :=
  (fs.eventuallyEq_one' x hx).choose

theorem eventuallyEq_one (x : M) (hx : x ‚àà s) : fs (fs.ind x hx) =·∂†[ùìù x] 1 :=
  (fs.eventuallyEq_one' x hx).choose_spec

theorem apply_ind (x : M) (hx : x ‚àà s) : fs (fs.ind x hx) x = 1 :=
  (fs.eventuallyEq_one x hx).eq_of_nhds

theorem mem_support_ind (x : M) (hx : x ‚àà s) : x ‚àà support (fs <| fs.ind x hx) := by
  simp [fs.apply_ind x hx]

theorem mem_chartAt_source_of_eq_one {i : Œπ} {x : M} (h : fs i x = 1) :
    x ‚àà (chartAt H (fs.c i)).source :=
  (fs i).support_subset_source <| by simp [h]

theorem mem_extChartAt_source_of_eq_one {i : Œπ} {x : M} (h : fs i x = 1) :
    x ‚àà (extChartAt I (fs.c i)).source := by
  rw [extChartAt_source]; exact fs.mem_chartAt_source_of_eq_one h

theorem mem_chartAt_ind_source (x : M) (hx : x ‚àà s) : x ‚àà (chartAt H (fs.c (fs.ind x hx))).source :=
  fs.mem_chartAt_source_of_eq_one (fs.apply_ind x hx)

theorem mem_extChartAt_ind_source (x : M) (hx : x ‚àà s) :
    x ‚àà (extChartAt I (fs.c (fs.ind x hx))).source :=
  fs.mem_extChartAt_source_of_eq_one (fs.apply_ind x hx)

/-- The index type of a `SmoothBumpCovering` of a compact manifold is finite. -/
protected def fintype [CompactSpace M] : Fintype Œπ :=
  fs.locallyFinite.fintypeOfCompact fun i ‚Ü¶ (fs i).nonempty_support

variable [T2Space M]
variable [IsManifold I ‚àû M]

/-- Reinterpret a `SmoothBumpCovering` as a continuous `BumpCovering`. Note that not every
`f : BumpCovering Œπ M s` with smooth functions `f i` is a `SmoothBumpCovering`. -/
def toBumpCovering : BumpCovering Œπ M s where
  toFun i := ‚ü®fs i, (fs i).continuous‚ü©
  locallyFinite' := fs.locallyFinite
  nonneg' i _ := (fs i).nonneg
  le_one' i _ := (fs i).le_one
  eventuallyEq_one' := fs.eventuallyEq_one'

@[simp]
theorem isSubordinate_toBumpCovering {f : SmoothBumpCovering Œπ I M s} {U : M ‚Üí Set M} :
    (f.toBumpCovering.IsSubordinate fun i ‚Ü¶ U (f.c i)) ‚Üî f.IsSubordinate U :=
  Iff.rfl

alias ‚ü®_, IsSubordinate.toBumpCovering‚ü© := isSubordinate_toBumpCovering

/-- Every `SmoothBumpCovering` defines a smooth partition of unity. -/
def toSmoothPartitionOfUnity : SmoothPartitionOfUnity Œπ I M s :=
  fs.toBumpCovering.toSmoothPartitionOfUnity fun i ‚Ü¶ (fs i).contMDiff

theorem toSmoothPartitionOfUnity_apply (i : Œπ) (x : M) :
    fs.toSmoothPartitionOfUnity i x = fs i x * ‚àè·∂† (j) (_ : WellOrderingRel j i), (1 - fs j x) :=
  rfl

open Classical in
theorem toSmoothPartitionOfUnity_eq_mul_prod (i : Œπ) (x : M) (t : Finset Œπ)
    (ht : ‚àÄ j, WellOrderingRel j i ‚Üí fs j x ‚â† 0 ‚Üí j ‚àà t) :
    fs.toSmoothPartitionOfUnity i x = fs i x * ‚àè j ‚àà t with WellOrderingRel j i, (1 - fs j x) :=
  fs.toBumpCovering.toPartitionOfUnity_eq_mul_prod i x t ht

open Classical in
theorem exists_finset_toSmoothPartitionOfUnity_eventuallyEq (i : Œπ) (x : M) :
    ‚àÉ t : Finset Œπ,
      fs.toSmoothPartitionOfUnity i =·∂†[ùìù x]
        fs i * ‚àè j ‚àà t with WellOrderingRel j i, ((1 : M ‚Üí ‚Ñù) - fs j) := by
  -- Porting note: was defeq, now the continuous lemma uses bundled homs
  simpa using fs.toBumpCovering.exists_finset_toPartitionOfUnity_eventuallyEq i x

theorem toSmoothPartitionOfUnity_zero_of_zero {i : Œπ} {x : M} (h : fs i x = 0) :
    fs.toSmoothPartitionOfUnity i x = 0 :=
  fs.toBumpCovering.toPartitionOfUnity_zero_of_zero h

theorem support_toSmoothPartitionOfUnity_subset (i : Œπ) :
    support (fs.toSmoothPartitionOfUnity i) ‚äÜ support (fs i) :=
  fs.toBumpCovering.support_toPartitionOfUnity_subset i

theorem IsSubordinate.toSmoothPartitionOfUnity {f : SmoothBumpCovering Œπ I M s} {U : M ‚Üí Set M}
    (h : f.IsSubordinate U) : f.toSmoothPartitionOfUnity.IsSubordinate fun i ‚Ü¶ U (f.c i) :=
  h.toBumpCovering.toPartitionOfUnity

theorem sum_toSmoothPartitionOfUnity_eq (x : M) :
    ‚àë·∂† i, fs.toSmoothPartitionOfUnity i x = 1 - ‚àè·∂† i, (1 - fs i x) :=
  fs.toBumpCovering.sum_toPartitionOfUnity_eq x

end SmoothBumpCovering

variable (I)
variable [FiniteDimensional ‚Ñù E]
variable [IsManifold I ‚àû M]

/-- Given two disjoint closed sets `s, t` in a Hausdorff œÉ-compact finite dimensional manifold,
there exists an infinitely smooth function that is equal to `0` on `s` and to `1` on `t`.
See also `exists_msmooth_zero_iff_one_iff_of_isClosed`, which ensures additionally that
`f` is equal to `0` exactly on `s` and to `1` exactly on `t`. -/
theorem exists_smooth_zero_one_of_isClosed [T2Space M] [SigmaCompactSpace M] {s t : Set M}
    (hs : IsClosed s) (ht : IsClosed t) (hd : Disjoint s t) :
    ‚àÉ f : C^‚àû‚üÆI, M; ùìò(‚Ñù), ‚Ñù‚üØ, EqOn f 0 s ‚àß EqOn f 1 t ‚àß ‚àÄ x, f x ‚àà Icc 0 1 := by
  have : ‚àÄ x ‚àà t, s·∂ú ‚àà ùìù x := fun x hx ‚Ü¶ hs.isOpen_compl.mem_nhds (disjoint_right.1 hd hx)
  rcases SmoothBumpCovering.exists_isSubordinate I ht this with ‚ü®Œπ, f, hf‚ü©
  set g := f.toSmoothPartitionOfUnity
  refine
    ‚ü®‚ü®_, g.contMDiff_sum‚ü©, fun x hx ‚Ü¶ ?_, fun x ‚Ü¶ g.sum_eq_one, fun x ‚Ü¶
      ‚ü®g.sum_nonneg x, g.sum_le_one x‚ü©‚ü©
  suffices ‚àÄ i, g i x = 0 by simp only [this, ContMDiffMap.coeFn_mk, finsum_zero, Pi.zero_apply]
  refine fun i ‚Ü¶ f.toSmoothPartitionOfUnity_zero_of_zero ?_
  exact notMem_support.1 (subset_compl_comm.1 (hf.support_subset i) hx)

/-- Given two disjoint closed sets `s, t` in a Hausdorff normal œÉ-compact finite dimensional
manifold `M`, there exists a smooth function `f : M ‚Üí [0,1]` that vanishes in a neighbourhood of `s`
and is equal to `1` in a neighbourhood of `t`. -/
theorem exists_smooth_zero_one_nhds_of_isClosed [T2Space M] [NormalSpace M] [SigmaCompactSpace M]
    {s t : Set M} (hs : IsClosed s) (ht : IsClosed t) (hd : Disjoint s t) :
    ‚àÉ f : C^‚àû‚üÆI, M; ùìò(‚Ñù), ‚Ñù‚üØ, (‚àÄ·∂† x in ùìùÀ¢ s, f x = 0) ‚àß (‚àÄ·∂† x in ùìùÀ¢ t, f x = 1) ‚àß
      ‚àÄ x, f x ‚àà Icc 0 1 := by
  obtain ‚ü®u, u_op, hsu, hut‚ü© := normal_exists_closure_subset hs ht.isOpen_compl
    (subset_compl_iff_disjoint_left.mpr hd.symm)
  obtain ‚ü®v, v_op, htv, hvu‚ü© := normal_exists_closure_subset ht isClosed_closure.isOpen_compl
    (subset_compl_comm.mp hut)
  obtain ‚ü®f, hfu, hfv, hf‚ü© := exists_smooth_zero_one_of_isClosed I isClosed_closure isClosed_closure
    (subset_compl_iff_disjoint_left.mp hvu)
  refine ‚ü®f, ?_, ?_, hf‚ü©
  ¬∑ exact eventually_of_mem (mem_of_superset (u_op.mem_nhdsSet.mpr hsu) subset_closure) hfu
  ¬∑ exact eventually_of_mem (mem_of_superset (v_op.mem_nhdsSet.mpr htv) subset_closure) hfv

/-- Given two sets `s, t` in a Hausdorff normal œÉ-compact finite-dimensional manifold `M`
with `s` open and `s ‚äÜ interior t`, there is a smooth function `f : M ‚Üí [0,1]` which is equal to `s`
in a neighbourhood of `s` and has support contained in `t`. -/
theorem exists_smooth_one_nhds_of_subset_interior [T2Space M] [NormalSpace M] [SigmaCompactSpace M]
    {s t : Set M} (hs : IsClosed s) (hd : s ‚äÜ interior t) :
    ‚àÉ f : C^‚àû‚üÆI, M; ùìò(‚Ñù), ‚Ñù‚üØ, (‚àÄ·∂† x in ùìùÀ¢ s, f x = 1) ‚àß (‚àÄ x ‚àâ t, f x = 0) ‚àß
      ‚àÄ x, f x ‚àà Icc 0 1 := by
  rcases exists_smooth_zero_one_nhds_of_isClosed I isOpen_interior.isClosed_compl hs
    (by rwa [‚Üê subset_compl_iff_disjoint_left, compl_compl]) with ‚ü®f, h0, h1, hf‚ü©
  refine ‚ü®f, h1, fun x hx ‚Ü¶ ?_, hf‚ü©
  exact h0.self_of_nhdsSet _ fun hx' ‚Ü¶ hx <| interior_subset hx'

namespace SmoothPartitionOfUnity

/-- A `SmoothPartitionOfUnity` that consists of a single function, uniformly equal to one,
defined as an example for `Inhabited` instance. -/
def single (i : Œπ) (s : Set M) : SmoothPartitionOfUnity Œπ I M s :=
  (BumpCovering.single i s).toSmoothPartitionOfUnity fun j ‚Ü¶ by
    classical
    rcases eq_or_ne j i with (rfl | h)
    ¬∑ simp only [contMDiff_one, ContinuousMap.coe_one, BumpCovering.coe_single, Pi.single_eq_same]
    ¬∑ simp only [contMDiff_zero, BumpCovering.coe_single, Pi.single_eq_of_ne h,
        ContinuousMap.coe_zero]

instance [Inhabited Œπ] (s : Set M) : Inhabited (SmoothPartitionOfUnity Œπ I M s) :=
  ‚ü®single I default s‚ü©

variable [T2Space M] [SigmaCompactSpace M]

/-- If `X` is a paracompact normal topological space and `U` is an open covering of a closed set
`s`, then there exists a `SmoothPartitionOfUnity Œπ M s` that is subordinate to `U`. -/
theorem exists_isSubordinate {s : Set M} (hs : IsClosed s) (U : Œπ ‚Üí Set M) (ho : ‚àÄ i, IsOpen (U i))
    (hU : s ‚äÜ ‚ãÉ i, U i) : ‚àÉ f : SmoothPartitionOfUnity Œπ I M s, f.IsSubordinate U := by
  haveI : LocallyCompactSpace H := I.locallyCompactSpace
  haveI : LocallyCompactSpace M := ChartedSpace.locallyCompactSpace H M
  -- porting note(https://github.com/leanprover/std4/issues/116):
  -- split `rcases` into `have` + `rcases`
  have := BumpCovering.exists_isSubordinate_of_prop (ContMDiff I ùìò(‚Ñù) ‚àû) ?_ hs U ho hU
  ¬∑ rcases this with ‚ü®f, hf, hfU‚ü©
    exact ‚ü®f.toSmoothPartitionOfUnity hf, hfU.toSmoothPartitionOfUnity hf‚ü©
  ¬∑ intro s t hs ht hd
    rcases exists_smooth_zero_one_of_isClosed I hs ht hd with ‚ü®f, hf‚ü©
    exact ‚ü®f, f.contMDiff, hf‚ü©

theorem exists_isSubordinate_chartAt_source_of_isClosed {s : Set M} (hs : IsClosed s) :
    ‚àÉ f : SmoothPartitionOfUnity s I M s,
      f.IsSubordinate (fun x ‚Ü¶ (chartAt H (x : M)).source) := by
  apply exists_isSubordinate _ hs _ (fun i ‚Ü¶ (chartAt H _).open_source) (fun x hx ‚Ü¶ ?_)
  exact mem_iUnion_of_mem ‚ü®x, hx‚ü© (mem_chart_source H x)

variable (M)
theorem exists_isSubordinate_chartAt_source :
    ‚àÉ f : SmoothPartitionOfUnity M I M univ, f.IsSubordinate (fun x ‚Ü¶ (chartAt H x).source) := by
  apply exists_isSubordinate _ isClosed_univ _ (fun i ‚Ü¶ (chartAt H _).open_source) (fun x _ ‚Ü¶ ?_)
  exact mem_iUnion_of_mem x (mem_chart_source H x)

end SmoothPartitionOfUnity

variable [SigmaCompactSpace M] [T2Space M] {t : M ‚Üí Set F} {n : ‚Ñï‚àû}

/-- Let `M` be a œÉ-compact Hausdorff finite dimensional topological manifold. Let `t : M ‚Üí Set F`
be a family of convex sets. Suppose that for each point `x : M` there exists a neighborhood
`U ‚àà ùìù x` and a function `g : M ‚Üí F` such that `g` is $C^n$ smooth on `U` and `g y ‚àà t y` for all
`y ‚àà U`. Then there exists a $C^n$ smooth function `g : C^‚àû‚üÆI, M; ùìò(‚Ñù, F), F‚üØ` such that `g x ‚àà t x`
for all `x`. See also `exists_smooth_forall_mem_convex_of_local` and
`exists_smooth_forall_mem_convex_of_local_const`. -/
theorem exists_contMDiffOn_forall_mem_convex_of_local (ht : ‚àÄ x, Convex ‚Ñù (t x))
    (Hloc : ‚àÄ x : M, ‚àÉ U ‚àà ùìù x, ‚àÉ g : M ‚Üí F, ContMDiffOn I ùìò(‚Ñù, F) n g U ‚àß ‚àÄ y ‚àà U, g y ‚àà t y) :
    ‚àÉ g : C^n‚üÆI, M; ùìò(‚Ñù, F), F‚üØ, ‚àÄ x, g x ‚àà t x := by
  choose U hU g hgs hgt using Hloc
  obtain ‚ü®f, hf‚ü© :=
    SmoothPartitionOfUnity.exists_isSubordinate I isClosed_univ (fun x ‚Ü¶ interior (U x))
      (fun x ‚Ü¶ isOpen_interior) fun x _ ‚Ü¶ mem_iUnion.2 ‚ü®x, mem_interior_iff_mem_nhds.2 (hU x)‚ü©
  refine ‚ü®‚ü®fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x,
      hf.contMDiff_finsum_smul (fun i ‚Ü¶ isOpen_interior) fun i ‚Ü¶ (hgs i).mono interior_subset‚ü©,
    fun x ‚Ü¶ f.finsum_smul_mem_convex (mem_univ x) (fun i hi ‚Ü¶ hgt _ _ ?_) (ht _)‚ü©
  exact interior_subset (hf _ <| subset_closure hi)

/-- Let `M` be a œÉ-compact Hausdorff finite dimensional topological manifold. Let `t : M ‚Üí Set F`
be a family of convex sets. Suppose that for each point `x : M` there exists a neighborhood
`U ‚àà ùìù x` and a function `g : M ‚Üí F` such that `g` is smooth on `U` and `g y ‚àà t y` for all `y ‚àà U`.
Then there exists a smooth function `g : C^‚àû‚üÆI, M; ùìò(‚Ñù, F), F‚üØ` such that `g x ‚àà t x` for all `x`.
See also `exists_contMDiffOn_forall_mem_convex_of_local` and
`exists_smooth_forall_mem_convex_of_local_const`. -/
theorem exists_smooth_forall_mem_convex_of_local (ht : ‚àÄ x, Convex ‚Ñù (t x))
    (Hloc : ‚àÄ x : M, ‚àÉ U ‚àà ùìù x, ‚àÉ g : M ‚Üí F, ContMDiffOn I ùìò(‚Ñù, F) ‚àû g U ‚àß ‚àÄ y ‚àà U, g y ‚àà t y) :
    ‚àÉ g : C^‚àû‚üÆI, M; ùìò(‚Ñù, F), F‚üØ, ‚àÄ x, g x ‚àà t x :=
  exists_contMDiffOn_forall_mem_convex_of_local I ht Hloc

/-- Let `M` be a œÉ-compact Hausdorff finite dimensional topological manifold. Let `t : M ‚Üí Set F` be
a family of convex sets. Suppose that for each point `x : M` there exists a vector `c : F` such that
for all `y` in a neighborhood of `x` we have `c ‚àà t y`. Then there exists a smooth function
`g : C^‚àû‚üÆI, M; ùìò(‚Ñù, F), F‚üØ` such that `g x ‚àà t x` for all `x`.  See also
`exists_contMDiffOn_forall_mem_convex_of_local` and `exists_smooth_forall_mem_convex_of_local`. -/
theorem exists_smooth_forall_mem_convex_of_local_const (ht : ‚àÄ x, Convex ‚Ñù (t x))
    (Hloc : ‚àÄ x : M, ‚àÉ c : F, ‚àÄ·∂† y in ùìù x, c ‚àà t y) : ‚àÉ g : C^‚àû‚üÆI, M; ùìò(‚Ñù, F), F‚üØ, ‚àÄ x, g x ‚àà t x :=
  exists_smooth_forall_mem_convex_of_local I ht fun x ‚Ü¶
    let ‚ü®c, hc‚ü© := Hloc x
    ‚ü®_, hc, fun _ ‚Ü¶ c, contMDiffOn_const, fun _ ‚Ü¶ id‚ü©

/-- Let `M` be a smooth œÉ-compact manifold with extended distance. Let `K : Œπ ‚Üí Set M` be a locally
finite family of closed sets, let `U : Œπ ‚Üí Set M` be a family of open sets such that `K i ‚äÜ U i` for
all `i`. Then there exists a positive smooth function `Œ¥ : M ‚Üí ‚Ñù‚â•0` such that for any `i` and
`x ‚àà K i`, we have `EMetric.closedBall x (Œ¥ x) ‚äÜ U i`. -/
theorem Emetric.exists_smooth_forall_closedBall_subset {M} [EMetricSpace M] [ChartedSpace H M]
    [IsManifold I ‚àû M] [SigmaCompactSpace M] {K : Œπ ‚Üí Set M} {U : Œπ ‚Üí Set M}
    (hK : ‚àÄ i, IsClosed (K i)) (hU : ‚àÄ i, IsOpen (U i)) (hKU : ‚àÄ i, K i ‚äÜ U i)
    (hfin : LocallyFinite K) :
    ‚àÉ Œ¥ : C^‚àû‚üÆI, M; ùìò(‚Ñù, ‚Ñù), ‚Ñù‚üØ,
      (‚àÄ x, 0 < Œ¥ x) ‚àß ‚àÄ (i), ‚àÄ x ‚àà K i, EMetric.closedBall x (ENNReal.ofReal (Œ¥ x)) ‚äÜ U i := by
  simpa only [mem_inter_iff, forall_and, mem_preimage, mem_iInter, @forall_swap Œπ M]
    using exists_smooth_forall_mem_convex_of_local_const I
      EMetric.exists_forall_closedBall_subset_aux‚ÇÇ
      (EMetric.exists_forall_closedBall_subset_aux‚ÇÅ hK hU hKU hfin)

/-- Let `M` be a smooth œÉ-compact manifold with a metric. Let `K : Œπ ‚Üí Set M` be a locally finite
family of closed sets, let `U : Œπ ‚Üí Set M` be a family of open sets such that `K i ‚äÜ U i` for all
`i`. Then there exists a positive smooth function `Œ¥ : M ‚Üí ‚Ñù‚â•0` such that for any `i` and `x ‚àà K i`,
we have `Metric.closedBall x (Œ¥ x) ‚äÜ U i`. -/
theorem Metric.exists_smooth_forall_closedBall_subset {M} [MetricSpace M] [ChartedSpace H M]
    [IsManifold I ‚àû M] [SigmaCompactSpace M] {K : Œπ ‚Üí Set M} {U : Œπ ‚Üí Set M}
    (hK : ‚àÄ i, IsClosed (K i)) (hU : ‚àÄ i, IsOpen (U i)) (hKU : ‚àÄ i, K i ‚äÜ U i)
    (hfin : LocallyFinite K) :
    ‚àÉ Œ¥ : C^‚àû‚üÆI, M; ùìò(‚Ñù, ‚Ñù), ‚Ñù‚üØ,
      (‚àÄ x, 0 < Œ¥ x) ‚àß ‚àÄ (i), ‚àÄ x ‚àà K i, Metric.closedBall x (Œ¥ x) ‚äÜ U i := by
  rcases Emetric.exists_smooth_forall_closedBall_subset I hK hU hKU hfin with ‚ü®Œ¥, hŒ¥0, hŒ¥‚ü©
  refine ‚ü®Œ¥, hŒ¥0, fun i x hx ‚Ü¶ ?_‚ü©
  rw [‚Üê Metric.emetric_closedBall (hŒ¥0 _).le]
  exact hŒ¥ i x hx

lemma IsOpen.exists_msmooth_support_eq_aux {s : Set H} (hs : IsOpen s) :
    ‚àÉ f : H ‚Üí ‚Ñù, f.support = s ‚àß ContMDiff I ùìò(‚Ñù) ‚àû f ‚àß Set.range f ‚äÜ Set.Icc 0 1 := by
  have h's : IsOpen (I.symm ‚Åª¬π' s) := I.continuous_symm.isOpen_preimage _ hs
  rcases h's.exists_smooth_support_eq with ‚ü®f, f_supp, f_diff, f_range‚ü©
  refine ‚ü®f ‚àò I, ?_, ?_, ?_‚ü©
  ¬∑ rw [support_comp_eq_preimage, f_supp, ‚Üê preimage_comp]
    simp only [ModelWithCorners.symm_comp_self, preimage_id_eq, id_eq]
  ¬∑ exact f_diff.comp_contMDiff contMDiff_model
  ¬∑ exact Subset.trans (range_comp_subset_range _ _) f_range

/-- Given an open set in a finite-dimensional real manifold, there exists a nonnegative smooth
function with support equal to `s`. -/
theorem IsOpen.exists_msmooth_support_eq {s : Set M} (hs : IsOpen s) :
    ‚àÉ f : M ‚Üí ‚Ñù, f.support = s ‚àß ContMDiff I ùìò(‚Ñù) ‚àû f ‚àß ‚àÄ x, 0 ‚â§ f x := by
  rcases SmoothPartitionOfUnity.exists_isSubordinate_chartAt_source I M with ‚ü®f, hf‚ü©
  have A : ‚àÄ (c : M), ‚àÉ g : H ‚Üí ‚Ñù,
      g.support = (chartAt H c).target ‚à© (chartAt H c).symm ‚Åª¬π' s ‚àß
      ContMDiff I ùìò(‚Ñù) ‚àû g ‚àß Set.range g ‚äÜ Set.Icc 0 1 := by
    intro i
    apply IsOpen.exists_msmooth_support_eq_aux
    exact PartialHomeomorph.isOpen_inter_preimage_symm _ hs
  choose g g_supp g_diff hg using A
  have h'g : ‚àÄ c x, 0 ‚â§ g c x := fun c x ‚Ü¶ (hg c (mem_range_self (f := g c) x)).1
  have h''g : ‚àÄ c x, 0 ‚â§ f c x * g c (chartAt H c x) :=
    fun c x ‚Ü¶ mul_nonneg (f.nonneg c x) (h'g c _)
  refine ‚ü®fun x ‚Ü¶ ‚àë·∂† c, f c x * g c (chartAt H c x), ?_, ?_, ?_‚ü©
  ¬∑ refine support_eq_iff.2 ‚ü®fun x hx ‚Ü¶ ?_, fun x hx ‚Ü¶ ?_‚ü©
    ¬∑ apply ne_of_gt
      have B : ‚àÉ c, 0 < f c x * g c (chartAt H c x) := by
        obtain ‚ü®c, hc‚ü© : ‚àÉ c, 0 < f c x := f.exists_pos_of_mem (mem_univ x)
        refine ‚ü®c, mul_pos hc ?_‚ü©
        apply lt_of_le_of_ne (h'g _ _) (Ne.symm _)
        rw [‚Üê mem_support, g_supp, ‚Üê mem_preimage, preimage_inter]
        have Hx : x ‚àà tsupport (f c) := subset_tsupport _ (ne_of_gt hc)
        simp [(chartAt H c).left_inv (hf c Hx), hx, (chartAt H c).map_source (hf c Hx)]
      apply finsum_pos' (fun c ‚Ü¶ h''g c x) B
      apply (f.locallyFinite.point_finite x).subset
      apply compl_subset_compl.2
      rintro c (hc : f c x = 0)
      simpa only [mul_eq_zero] using Or.inl hc
    ¬∑ apply finsum_eq_zero_of_forall_eq_zero
      intro c
      by_cases Hx : x ‚àà tsupport (f c)
      ¬∑ suffices g c (chartAt H c x) = 0 by simp only [this, mul_zero]
        rw [‚Üê notMem_support, g_supp, ‚Üê mem_preimage, preimage_inter]
        contrapose! hx
        simp only [mem_inter_iff, mem_preimage, (chartAt H c).left_inv (hf c Hx)] at hx
        exact hx.2
      ¬∑ have : x ‚àâ support (f c) := by contrapose! Hx; exact subset_tsupport _ Hx
        rw [notMem_support] at this
        simp [this]
  ¬∑ apply SmoothPartitionOfUnity.contMDiff_finsum_smul
    intro c x hx
    apply (g_diff c (chartAt H c x)).comp
    exact contMDiffAt_of_mem_maximalAtlas (IsManifold.chart_mem_maximalAtlas _)
      (hf c hx)
  ¬∑ intro x
    apply finsum_nonneg (fun c ‚Ü¶ h''g c x)

/-- Given an open set `s` containing a closed set `t` in a finite-dimensional real manifold, there
exists a smooth function with support equal to `s`, taking values in `[0,1]`, and equal to `1`
exactly on `t`. -/
theorem exists_msmooth_support_eq_eq_one_iff
    {s t : Set M} (hs : IsOpen s) (ht : IsClosed t) (h : t ‚äÜ s) :
    ‚àÉ f : M ‚Üí ‚Ñù, ContMDiff I ùìò(‚Ñù) ‚àû f ‚àß range f ‚äÜ Icc 0 1 ‚àß support f = s
      ‚àß (‚àÄ x, x ‚àà t ‚Üî f x = 1) := by
  /- Take `f` with support equal to `s`, and `g` with support equal to `t·∂ú`. Then `f / (f + g)`
  satisfies the conclusion of the theorem. -/
  rcases hs.exists_msmooth_support_eq I with ‚ü®f, f_supp, f_diff, f_pos‚ü©
  rcases ht.isOpen_compl.exists_msmooth_support_eq I with ‚ü®g, g_supp, g_diff, g_pos‚ü©
  have A : ‚àÄ x, 0 < f x + g x := by
    intro x
    by_cases xs : x ‚àà support f
    ¬∑ have : 0 < f x := lt_of_le_of_ne (f_pos x) (Ne.symm xs)
      linarith [g_pos x]
    ¬∑ have : 0 < g x := by
        classical
        apply lt_of_le_of_ne (g_pos x) (Ne.symm ?_)
        rw [‚Üê mem_support, g_supp]
        contrapose! xs
        simp? at xs says simp only [mem_compl_iff, Decidable.not_not] at xs
        exact h.trans f_supp.symm.subset xs
      linarith [f_pos x]
  refine ‚ü®fun x ‚Ü¶ f x / (f x + g x), ?_, ?_, ?_, ?_‚ü©
  -- show that `f / (f + g)` is smooth
  ¬∑ exact f_diff.div‚ÇÄ (f_diff.add g_diff) (fun x ‚Ü¶ ne_of_gt (A x))
  -- show that the range is included in `[0, 1]`
  ¬∑ refine range_subset_iff.2 (fun x ‚Ü¶ ‚ü®div_nonneg (f_pos x) (A x).le, ?_‚ü©)
    apply div_le_one_of_le‚ÇÄ _ (A x).le
    simpa only [le_add_iff_nonneg_right] using g_pos x
  -- show that the support is `s`
  ¬∑ have B : support (fun x ‚Ü¶ f x + g x) = univ := eq_univ_of_forall (fun x ‚Ü¶ (A x).ne')
    simp only [support_div, f_supp, B, inter_univ]
  -- show that the function equals one exactly on `t`
  ¬∑ intro x
    simp [div_eq_one_iff_eq (A x).ne', left_eq_add, ‚Üê notMem_support, g_supp]

/-- Given two disjoint closed sets `s, t` in a Hausdorff œÉ-compact finite dimensional manifold,
there exists an infinitely smooth function that is equal to `0` exactly on `s` and to `1`
exactly on `t`. See also `exists_smooth_zero_one_of_isClosed` for a slightly weaker version. -/
theorem exists_msmooth_zero_iff_one_iff_of_isClosed {s t : Set M}
    (hs : IsClosed s) (ht : IsClosed t) (hd : Disjoint s t) :
    ‚àÉ f : M ‚Üí ‚Ñù, ContMDiff I ùìò(‚Ñù) ‚àû f ‚àß range f ‚äÜ Icc 0 1 ‚àß (‚àÄ x, x ‚àà s ‚Üî f x = 0)
      ‚àß (‚àÄ x, x ‚àà t ‚Üî f x = 1) := by
  rcases exists_msmooth_support_eq_eq_one_iff I hs.isOpen_compl ht hd.subset_compl_left with
    ‚ü®f, f_diff, f_range, fs, ft‚ü©
  refine ‚ü®f, f_diff, f_range, ?_, ft‚ü©
  simp [‚Üê notMem_support, fs]
