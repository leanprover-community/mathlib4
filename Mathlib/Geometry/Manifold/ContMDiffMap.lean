/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.ContMDiff.NormedSpace

/-!
# `C^n` bundled maps

In this file we define the type `ContMDiffMap` of `n` times continuously differentiable
bundled maps.
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H : Type*}
  [TopologicalSpace H] {H' : Type*} [TopologicalSpace H'] {I : ModelWithCorners ğ•œ E H}
  {I' : ModelWithCorners ğ•œ E' H'} (M : Type*) [TopologicalSpace M] [ChartedSpace H M] (M' : Type*)
  [TopologicalSpace M'] [ChartedSpace H' M'] {E'' : Type*} [NormedAddCommGroup E'']
  [NormedSpace ğ•œ E''] {H'' : Type*} [TopologicalSpace H''] {I'' : ModelWithCorners ğ•œ E'' H''}
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  -- declare a manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ğ•œ F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N] (n : WithTop â„•âˆ)

variable (I I') in
/-- Bundled `n` times continuously differentiable maps,
denoted as `C^n(I, M; I', M')` and `C^n(I, M; k)` (when the target is a normed space `k` with
the trivial model) in the `Manifold` namespace. -/
def ContMDiffMap :=
  { f : M â†’ M' // ContMDiff I I' n f }

@[inherit_doc]
scoped[Manifold] notation "C^" n "âŸ®" I ", " M "; " I' ", " M' "âŸ¯" => ContMDiffMap I I' M M' n

@[inherit_doc]
scoped[Manifold]
  notation "C^" n "âŸ®" I ", " M "; " k "âŸ¯" => ContMDiffMap I (modelWithCornersSelf k k) M k n

open scoped Manifold ContDiff

namespace ContMDiffMap

variable {M} {M'} {n}

instance instFunLike : FunLike C^nâŸ®I, M; I', M'âŸ¯ M M' where
  coe := Subtype.val
  coe_injective' := Subtype.coe_injective

protected theorem contMDiff (f : C^nâŸ®I, M; I', M'âŸ¯) : ContMDiff I I' n f :=
  f.prop

-- Porting note: use generic instance instead
-- instance : Coe C^nâŸ®I, M; I', M'âŸ¯ C(M, M') :=
--   âŸ¨fun f â†¦ âŸ¨f, f.contMDiff.continuousâŸ©âŸ©

attribute [to_additive_ignore_args 21] ContMDiffMap ContMDiffMap.instFunLike

variable {f g : C^nâŸ®I, M; I', M'âŸ¯}

@[simp]
theorem coeFn_mk (f : M â†’ M') (hf : ContMDiff I I' n f) :
    DFunLike.coe (F := C^nâŸ®I, M; I', M'âŸ¯) âŸ¨f, hfâŸ© = f :=
  rfl

theorem coe_injective â¦ƒf g : C^nâŸ®I, M; I', M'âŸ¯â¦„ (h : (f : M â†’ M') = g) : f = g :=
  DFunLike.ext' h

@[ext]
theorem ext (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext _ _ h

instance : ContinuousMapClass C^nâŸ®I, M; I', M'âŸ¯ M M' where
  map_continuous f := f.contMDiff.continuous

/-- The identity as a `C^n` map. -/
nonrec def id : C^nâŸ®I, M; I, MâŸ¯ :=
  âŸ¨id, contMDiff_idâŸ©

/-- The composition of `C^n` maps, as a `C^n` map. -/
def comp (f : C^nâŸ®I', M'; I'', M''âŸ¯) (g : C^nâŸ®I, M; I', M'âŸ¯) : C^nâŸ®I, M; I'', M''âŸ¯ where
  val a := f (g a)
  property := f.contMDiff.comp g.contMDiff

@[simp]
theorem comp_apply (f : C^nâŸ®I', M'; I'', M''âŸ¯) (g : C^nâŸ®I, M; I', M'âŸ¯) (x : M) :
    f.comp g x = f (g x) :=
  rfl

instance [Inhabited M'] : Inhabited C^nâŸ®I, M; I', M'âŸ¯ :=
  âŸ¨âŸ¨fun _ â†¦ default, contMDiff_constâŸ©âŸ©

/-- Constant map as a `C^n` map -/
def const (y : M') : C^nâŸ®I, M; I', M'âŸ¯ :=
  âŸ¨fun _ â†¦ y, contMDiff_constâŸ©

/-- The first projection of a product, as a `C^n` map. -/
def fst : C^nâŸ®I.prod I', M Ã— M'; I, MâŸ¯ :=
  âŸ¨Prod.fst, contMDiff_fstâŸ©

/-- The second projection of a product, as a `C^n` map. -/
def snd : C^nâŸ®I.prod I', M Ã— M'; I', M'âŸ¯ :=
  âŸ¨Prod.snd, contMDiff_sndâŸ©

/-- Given two `C^n` maps `f` and `g`, this is the `C^n` map `x â†¦ (f x, g x)`. -/
def prodMk (f : C^nâŸ®J, N; I, MâŸ¯) (g : C^nâŸ®J, N; I', M'âŸ¯) : C^nâŸ®J, N; I.prod I', M Ã— M'âŸ¯ :=
  âŸ¨fun x â†¦ (f x, g x), f.2.prodMk g.2âŸ©

end ContMDiffMap

instance ContinuousLinearMap.hasCoeToContMDiffMap :
    Coe (E â†’L[ğ•œ] E') C^nâŸ®ğ“˜(ğ•œ, E), E; ğ“˜(ğ•œ, E'), E'âŸ¯ :=
  âŸ¨fun f â†¦ âŸ¨f, f.contMDiffâŸ©âŸ©
