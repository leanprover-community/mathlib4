/-
Copyright (c) 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri
-/
import Mathlib.Geometry.Manifold.ContMDiff.NormedSpace

/-!
# `C^n` bundled maps

In this file we define the type `ContMDiffMap` of `n` times continuously differentiable
bundled maps.
-/

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ùïú E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H : Type*}
  [TopologicalSpace H] {H' : Type*} [TopologicalSpace H'] {I : ModelWithCorners ùïú E H}
  {I' : ModelWithCorners ùïú E' H'} (M : Type*) [TopologicalSpace M] [ChartedSpace H M] (M' : Type*)
  [TopologicalSpace M'] [ChartedSpace H' M'] {E'' : Type*} [NormedAddCommGroup E'']
  [NormedSpace ùïú E''] {H'' : Type*} [TopologicalSpace H''] {I'' : ModelWithCorners ùïú E'' H''}
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  -- declare a manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ùïú F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ùïú F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N] (n : WithTop ‚Ñï‚àû)

variable (I I') in
/-- Bundled `n` times continuously differentiable maps,
denoted as `C^n(I, M; I', M')` and `C^n(I, M; k)` (when the target is a normed space `k` with
the trivial model) in the `Manifold` namespace. -/
def ContMDiffMap :=
  { f : M ‚Üí M' // ContMDiff I I' n f }

@[inherit_doc]
scoped[Manifold] notation "C^" n "‚üÆ" I ", " M "; " I' ", " M' "‚üØ" => ContMDiffMap I I' M M' n

@[inherit_doc]
scoped[Manifold]
  notation "C^" n "‚üÆ" I ", " M "; " k "‚üØ" => ContMDiffMap I (modelWithCornersSelf k k) M k n

open scoped Manifold ContDiff

namespace ContMDiffMap

variable {M} {M'} {n}

instance instFunLike : FunLike C^n‚üÆI, M; I', M'‚üØ M M' where
  coe := Subtype.val
  coe_injective' := Subtype.coe_injective

protected theorem contMDiff (f : C^n‚üÆI, M; I', M'‚üØ) : ContMDiff I I' n f :=
  f.prop

attribute [to_additive_ignore_args 21] ContMDiffMap ContMDiffMap.instFunLike

variable {f g : C^n‚üÆI, M; I', M'‚üØ}

@[simp]
theorem coeFn_mk (f : M ‚Üí M') (hf : ContMDiff I I' n f) :
    DFunLike.coe (F := C^n‚üÆI, M; I', M'‚üØ) ‚ü®f, hf‚ü© = f :=
  rfl

theorem coe_injective ‚¶Éf g : C^n‚üÆI, M; I', M'‚üØ‚¶Ñ (h : (f : M ‚Üí M') = g) : f = g :=
  DFunLike.ext' h

@[ext]
theorem ext (h : ‚àÄ x, f x = g x) : f = g := DFunLike.ext _ _ h

instance : ContinuousMapClass C^n‚üÆI, M; I', M'‚üØ M M' where
  map_continuous f := f.contMDiff.continuous

/-- The identity as a `C^n` map. -/
nonrec def id : C^n‚üÆI, M; I, M‚üØ :=
  ‚ü®id, contMDiff_id‚ü©

/-- The composition of `C^n` maps, as a `C^n` map. -/
def comp (f : C^n‚üÆI', M'; I'', M''‚üØ) (g : C^n‚üÆI, M; I', M'‚üØ) : C^n‚üÆI, M; I'', M''‚üØ where
  val a := f (g a)
  property := f.contMDiff.comp g.contMDiff

@[simp]
theorem comp_apply (f : C^n‚üÆI', M'; I'', M''‚üØ) (g : C^n‚üÆI, M; I', M'‚üØ) (x : M) :
    f.comp g x = f (g x) :=
  rfl

instance [Inhabited M'] : Inhabited C^n‚üÆI, M; I', M'‚üØ :=
  ‚ü®‚ü®fun _ => default, contMDiff_const‚ü©‚ü©

/-- Constant map as a `C^n` map -/
def const (y : M') : C^n‚üÆI, M; I', M'‚üØ :=
  ‚ü®fun _ => y, contMDiff_const‚ü©

/-- The first projection of a product, as a `C^n` map. -/
def fst : C^n‚üÆI.prod I', M √ó M'; I, M‚üØ :=
  ‚ü®Prod.fst, contMDiff_fst‚ü©

/-- The second projection of a product, as a `C^n` map. -/
def snd : C^n‚üÆI.prod I', M √ó M'; I', M'‚üØ :=
  ‚ü®Prod.snd, contMDiff_snd‚ü©

/-- Given two `C^n` maps `f` and `g`, this is the `C^n` map `x ‚Ü¶ (f x, g x)`. -/
def prodMk (f : C^n‚üÆJ, N; I, M‚üØ) (g : C^n‚üÆJ, N; I', M'‚üØ) : C^n‚üÆJ, N; I.prod I', M √ó M'‚üØ :=
  ‚ü®fun x => (f x, g x), f.2.prodMk g.2‚ü©

end ContMDiffMap

instance ContinuousLinearMap.hasCoeToContMDiffMap :
    Coe (E ‚ÜíL[ùïú] E') C^n‚üÆùìò(ùïú, E), E; ùìò(ùïú, E'), E'‚üØ :=
  ‚ü®fun f => ‚ü®f, f.contMDiff‚ü©‚ü©
