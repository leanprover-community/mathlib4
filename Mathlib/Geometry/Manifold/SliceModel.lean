/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.Instances.Real -- XXX: disentangle these later
/-!
# Embedded submanifolds

We define a new typeclass `SliceModel` for models with corners.

A future PR will use this to define immersed and embedded submanifolds.

TODO: expand this doc-string!

-/

section -- PRed in #26086

/-- The canonical linear homeomorphism between `EuclideanSpace ğ•œ (Î¹ âŠ• Îº)` and
`EuclideanSpace ğ•œ Î¹ Ã— EuclideanSpace ğ•œ Îº`. Note that this is not an isometry because
product spaces are equipped with the supremum norm. -/
def EuclideanSpace.sumEquivProd {ğ•œ : Type*} [RCLike ğ•œ] {Î¹ Îº : Type*} [Fintype Î¹] [Fintype Îº] :
    EuclideanSpace ğ•œ (Î¹ âŠ• Îº) â‰ƒL[ğ•œ] EuclideanSpace ğ•œ Î¹ Ã— EuclideanSpace ğ•œ Îº :=
  (PiLp.sumPiLpEquivProdLpPiLp 2 _).toContinuousLinearEquiv.trans <|
    WithLp.prodContinuousLinearEquiv _ _ _ _

/-- The canonical linear homeomorphism between `EuclideanSpace ğ•œ (Fin (n + m))` and
`EuclideanSpace ğ•œ (Fin n) Ã— EuclideanSpace ğ•œ (Fin m)`. -/
def EuclideanSpace.finAddEquivProd {ğ•œ : Type*} [RCLike ğ•œ] {n m : â„•} :
    EuclideanSpace ğ•œ (Fin (n + m)) â‰ƒL[ğ•œ] EuclideanSpace ğ•œ (Fin n) Ã— EuclideanSpace ğ•œ (Fin m) :=
  (LinearIsometryEquiv.piLpCongrLeft 2 ğ•œ ğ•œ finSumFinEquiv.symm).toContinuousLinearEquiv.trans
    sumEquivProd

end

open scoped Manifold ContDiff
open Topology Function Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E E' E'' E''' : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup E']
    [NormedSpace ğ•œ E'] [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] [NormedAddCommGroup E''']
    [NormedSpace ğ•œ E''']
  {F F' : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup F'] [NormedSpace ğ•œ F']
  {H H' H'' H''' : Type*} [TopologicalSpace H] [TopologicalSpace H']
  [TopologicalSpace H''] [TopologicalSpace H''']
  {I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'} {I'' : ModelWithCorners ğ•œ E'' H''}
  {J : ModelWithCorners ğ•œ E''' H'''}
  {M M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] {n : WithTop â„•âˆ}

variable (I I' F) in
/-- Two models with corners `I` and `I'` form a **slice model** if "I includes into I'".
More precisely, there are an embedding `H â†’ H'` and a continuous linear map `E â†’ E'` so the diagram
  H  -I  â†’ E'
  |        |
  |        |
  H' -I' â†’ E'
commutes. More precisely, we prescribe a linear equivalence `E Ã— F â†’ E`, for some normed space `F`,
which induces the map `E â†’ E'` in the obvious way.
-/
class SliceModel where
  equiv: (E Ã— F) â‰ƒL[ğ•œ] E'
  map: H â†’ H'
  hmap : Topology.IsEmbedding map
  compatible : I' âˆ˜ map = equiv âˆ˜ ((Â·, 0) : E â†’ E Ã— F) âˆ˜ I

namespace SliceModel

/-- A choice of inverse of `map`: its value outside of `range map` is unspecified. -/
noncomputable def inverse [Nonempty H] (h : SliceModel F I I') : H' â†’ H :=
  (Function.extend h.map id (fun _ â†¦ (Classical.arbitrary H)))

-- warm-up: I' âˆ˜ map âŠ† im equiv âˆ˜ I: that's basically obvious, nothing to prove

lemma inverse_left_inv [Nonempty H] (h : SliceModel F I I') (x : H) :
    h.inverse (h.map x) = x :=
  Injective.extend_apply h.hmap.injective ..

lemma inverse_right_inv [Nonempty H] (h : SliceModel F I I') (z : H') (hz : z âˆˆ range h.map) :
    h.map (h.inverse z) = z := by
  choose x hx using hz
  rw [â† hx, h.inverse_left_inv]

end SliceModel

section

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] [Unique G]

namespace ContinuousLinearEquiv

section prodAssoc -- PRed in #26082

variable (R Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*) [Semiring R]
  [AddCommMonoid Mâ‚] [AddCommMonoid Mâ‚‚] [AddCommMonoid Mâ‚ƒ] [Module R Mâ‚] [Module R Mâ‚‚] [Module R Mâ‚ƒ]
  [TopologicalSpace Mâ‚] [TopologicalSpace Mâ‚‚] [TopologicalSpace Mâ‚ƒ]

/-- The product of topological modules is associative up to continuous linear isomorphism.
This is `LinearEquiv.prodAssoc` prodAssoc as a continuous linear equivalence. -/
def prodAssoc : ((Mâ‚ Ã— Mâ‚‚) Ã— Mâ‚ƒ) â‰ƒL[R] Mâ‚ Ã— Mâ‚‚ Ã— Mâ‚ƒ where
  toLinearEquiv := LinearEquiv.prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ
  continuous_toFun := (continuous_fst.comp continuous_fst).prodMk
    ((continuous_snd.comp continuous_fst).prodMk continuous_snd)
  continuous_invFun := (continuous_fst.prodMk (continuous_fst.comp continuous_snd)).prodMk
    (continuous_snd.comp continuous_snd)

@[simp]
lemma prodAssoc_toLinearEquiv :
  (prodAssoc ğ•œ E E' E'').toLinearEquiv = LinearEquiv.prodAssoc ğ•œ E E' E'' := rfl

-- not simp as the combination of existing lemmas. TODO: should this one still be added?
lemma prodAssoc_toEquiv :
  (prodAssoc ğ•œ E E' E'').toEquiv = Equiv.prodAssoc E E' E'' := rfl

-- TODO: move up to Equiv.prodAssoc or so, then this one is implied...
@[simp]
lemma prodAssoc_apply (pâ‚ : E) (pâ‚‚ : E') (pâ‚ƒ : E'') :
  (prodAssoc ğ•œ E E' E'') ((pâ‚, pâ‚‚), pâ‚ƒ) = (pâ‚, (pâ‚‚, pâ‚ƒ)) := rfl

@[simp]
lemma prodAssoc_symm_apply (pâ‚ : E) (pâ‚‚ : E') (pâ‚ƒ : E'') :
  (prodAssoc ğ•œ E E' E'').symm (pâ‚, (pâ‚‚, pâ‚ƒ)) = ((pâ‚, pâ‚‚), pâ‚ƒ) := rfl

end prodAssoc

end ContinuousLinearEquiv

end

section instances

/-- Every model with corners is a slice model over itself. -/
instance : SliceModel (âŠ¥ : Subspace ğ•œ E) I I where
  equiv := ContinuousLinearEquiv.prodUnique ğ•œ E _
  map := id
  hmap := Topology.IsEmbedding.id
  compatible := by ext x; dsimp

/-- If `I` is a slice model of `I'`, then `J.prod I` is a slice model of `J.prod I'`. -/
instance [h : SliceModel F I I'] : SliceModel F (J.prod I) (J.prod I') where
  -- Up to the obvious maps, we just apply h.equiv.
  equiv := (ContinuousLinearEquiv.prodAssoc ğ•œ E''' E F).trans <|
    (ContinuousLinearEquiv.refl ğ•œ E''').prodCongr h.equiv
  map := Prod.map id h.map
  hmap := IsEmbedding.id.prodMap h.hmap
  compatible := by
    dsimp
    ext âŸ¨x, yâŸ© <;> simp
    -- XXX: is there a better tactic for this?
    change (I' âˆ˜ SliceModel.map F I I') y = ((SliceModel.equiv I I') âˆ˜ ((Â·, 0) : E â†’ E Ã— F) âˆ˜ I) y
    rw [h.compatible]

/-- If `I` is a slice model of `I'`, then `I.prod J` is a slice model of `I'.prod J`. -/
-- a bit more cumbersome, as equiv needs some reordering
instance [h : SliceModel F I I'] : SliceModel F (I.prod J) (I'.prod J) where
  equiv := by
    letI pre := (ContinuousLinearEquiv.prodComm ğ•œ E E''').prodCongr (.refl ğ•œ F)
    letI post := ContinuousLinearEquiv.prodComm ğ•œ E' E'''
    letI main : ((E''' Ã— E) Ã— F) â‰ƒL[ğ•œ] E''' Ã— E' :=
      (ContinuousLinearEquiv.prodAssoc ğ•œ E''' E F).trans <|
      (ContinuousLinearEquiv.refl ğ•œ E''').prodCongr h.equiv
    apply pre.trans (main.trans post.symm)
  map := Prod.map h.map id
  hmap := h.hmap.prodMap IsEmbedding.id
  compatible := by
    ext âŸ¨x, yâŸ© <;> simp
    -- XXX: is there a better tactic for this?
    change (I' âˆ˜ SliceModel.map F I I') x = ((SliceModel.equiv I I') âˆ˜ ((Â·, 0) : E â†’ E Ã— F) âˆ˜ I) x
    rw [h.compatible]

/-- If `E' â‰ƒ E Ã— F`, then the trivial models with corners of `E` and `E'` form a slice model. -/
instance (h : (E Ã— F) â‰ƒL[ğ•œ] E') : SliceModel F (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) where
  equiv := h
  map := h âˆ˜ (Â·, (0 : F))
  hmap := by
    apply h.toHomeomorph.isEmbedding.comp
    rw [â† IsEmbedding.of_comp_iff (ContinuousLinearEquiv.prodComm ğ•œ E F).toHomeomorph.isEmbedding]
    exact isEmbedding_prodMkRight (0 : F)
  compatible := by simp

/-- *Any* model with corners on `E` which is an embedding is a slice model with the trivial model
on `E`. (The embedding condition excludes strange cases of submanifolds with boundary).
For boundaryless models, that is always true. -/
def SliceModel.ofEmbedding {I : ModelWithCorners ğ•œ E H} (hI : IsEmbedding I) :
    SliceModel (âŠ¥ : Subspace ğ•œ E) I ğ“˜(ğ•œ, E) where
  equiv := ContinuousLinearEquiv.prodUnique ğ•œ E _
  map := I
  hmap := hI
  compatible := by ext; simp

-- TODO: prove that I is an embedding if I is boundaryless, then add the corresponding instance
-- TODO: think about the boundary case, and which particular version of submanifolds
-- this definition enforces...

open scoped Manifold

/-- Euclidean half-space is a slice model w.r.t. Euclidean space. -/
-- NB. Golfing this using the previous instance is not as obvious because of instance mismatches.
noncomputable instance {n : â„•} [NeZero n] :
    SliceModel (âŠ¥ : Subspace â„ ((Fin n â†’ â„))) (ğ“¡âˆ‚ n) (ğ“¡ n) where
  equiv := ContinuousLinearEquiv.prodUnique â„ (EuclideanSpace â„ (Fin n)) _
  map := Subtype.val
  hmap := Topology.IsEmbedding.subtypeVal
  compatible := by
    ext x'
    simp only [modelWithCornersSelf_coe, comp_apply, id_eq, ContinuousLinearEquiv.prodUnique_apply]
    rfl

/-- The standard model on `â„^n` is a slice model for the standard model for `â„^m`, for `n â‰¤ m`. -/
noncomputable instance {n m : â„•} [NeZero n] :
    SliceModel ((EuclideanSpace â„ (Fin m))) (ğ“¡ n) (ğ“¡ (n + m)) where
  equiv := EuclideanSpace.finAddEquivProd.symm
  map x := EuclideanSpace.finAddEquivProd.symm (x, 0)
  hmap :=
    (EuclideanSpace.finAddEquivProd.symm).toHomeomorph.isEmbedding.comp (isEmbedding_prodMkLeft 0)
  compatible := by ext; simp

noncomputable instance {n : â„•} [NeZero n] :
    SliceModel (âŠ¥ : Subspace â„ ((Fin n â†’ â„))) (modelWithCornersEuclideanQuadrant n) (ğ“¡âˆ‚ n) where
  equiv := ContinuousLinearEquiv.prodUnique â„ (EuclideanSpace â„ (Fin n)) _
  map := fun âŸ¨x, hxâŸ© â†¦ âŸ¨x, hx 0âŸ©
  hmap := by
    have h : IsEmbedding (Subtype.val : (EuclideanHalfSpace n) â†’ (EuclideanSpace â„ (Fin n))) :=
      IsEmbedding.subtypeVal
    have : IsEmbedding (Subtype.val : (EuclideanQuadrant n) â†’ (EuclideanSpace â„ (Fin n))) :=
      IsEmbedding.subtypeVal
    rw [â† IsEmbedding.of_comp_iff h]
    convert this
  compatible := by
    ext x
    simp only [comp_apply, ContinuousLinearEquiv.prodUnique_apply]
    rfl

-- TODO: make an instance/ figure out why Lean complains about synthesisation order!
def instTrans (h : SliceModel F I I') (h' : SliceModel F' I' I'') : SliceModel (F Ã— F') I I'' where
  equiv := (ContinuousLinearEquiv.prodAssoc ğ•œ E F F').symm.trans
    ((h.equiv.prodCongr (ContinuousLinearEquiv.refl ğ•œ F')).trans h'.equiv)
  map := h'.map âˆ˜ h.map
  hmap := h'.hmap.comp h.hmap
  compatible := by -- paste the two commutative diagrams together
    ext x
    have : (ContinuousLinearEquiv.prodAssoc ğ•œ E F F').symm (I x, 0) = ((I x, 0), 0) := rfl
    simp only [comp_apply, ContinuousLinearEquiv.trans_apply, ContinuousLinearEquiv.prodCongr_apply,
      ContinuousLinearEquiv.refl_apply, this]
    -- can this be condensed? feels unnecessarily painful
    -- (grind errors with `unknown constant h.compatible`)
    calc
      _ = (I'' âˆ˜ SliceModel.map F' I' I'') (SliceModel.map F I I' x) := by
        simp [Function.comp_apply]
      _ = (SliceModel.equiv I' I'' âˆ˜ (fun x â†¦ (x, (0 : F'))) âˆ˜ â†‘I') (SliceModel.map F I I' x) := by
        rw [h'.compatible]
      _ = (SliceModel.equiv I' I'' âˆ˜ (fun x â†¦ (x, (0 : F')))) ((I' âˆ˜ SliceModel.map F I I') x) := by
        simp [Function.comp_apply]
      _ = _ := by
        rw [h.compatible]
        congr

end instances
