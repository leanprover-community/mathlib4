/-
Copyright (c) 2025 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Patrick Massot
-/
module

public import Mathlib.Geometry.Manifold.Notation
public import Mathlib.Geometry.Manifold.PartitionOfUnity

/-!
# Approximation of continuous functions by smooth functions

In this file, we deduce from the existence of smooth partitions of unity that any continuous map
from a real œÉ-compact finite dimensional manifold `M` to a real normed space `F` can be
approximated uniformly by smooth functions.

More precisely, we strengthen this result in three ways :
* instead of a single number `Œµ > 0`, one may prescribe the precision of the approximation using
  an arbitrary continuous positive function `Œµ : M ‚Üí ‚Ñù`. This allows, for example, a control
  on the asymptotic behaviour of the approximation (e.g, choosing a precision `Œµ` which vanishes
  at infinity yields that continuous functions vanishing at infinity can be approximated by
  smooth functions vanishing at infinity).
* if the original map `f` already has the desired regularity on some neighborhood of a closed
  set `M`, one can choose an approximation which stays equal to `f` on `S`. This allows
  for some additional control in a setting with iterated approximations.
* finally, one may prescribe the approximation to vanish wherever the original function vanishes.
  For example, this shows that continuous functions supported on some compact set `K` may be
  approximated uniformly by smooth function supported on the **same** compact `K`.
  (Compare with arguments based on convolution where one needs to thicken `K` a bit).

## Main results

* `Continuous.exists_contMDiff_approx_and_eqOn`: approximating a continuous function `f : M ‚Üí F`
  by a `C^n` function `g : M ‚Üí E`, with precision prescribed by a continuous positive `Œµ : M ‚Üí ‚Ñù`,
  while ensuring that `support g ‚äÜ support f` and that `g` coincides with `f` on some closed set `S`
  in the neighborhood of which `f` is already `C^n`.
* `Continuous.exists_contMDiff_approx`: a simpler version of the previous result when one does not
  care about prescribing points with `g x = f x`. One still gets `support g ‚äÜ support f` for free,
  so we put it in the conclusion.
* `Continuous.exists_contDiff_approx_and_eqOn`, `Continuous.exists_contDiff_approx`: specializations
  of the previous results when `M = E` is a normed space.

## Implementation notes

With minor work, we could strengthen the statements in the following ways:
- the precision function `Œµ : M ‚Üí ‚Ñù` may be assumed `LowerSemicontinuous` instead of `Continuous`,
- the condition `support g ‚äÜ support f`, which translates to `‚àÄ x, f x = 0 ‚Üí g x = 0`,
  may be strengthened to `‚àÄ x, f x = h x ‚Üí g x = h x` for some arbitrary smooth `h : M ‚Üí F`.

This file depends on the manifold library, which may be annoying if you only need the normed space
statements. **Please do not let this refrain you from using them** if they apply naturally in your
context: if this is too much of a problem, you should complain on Zulip, so that we get more data
about the need for a non-manifold version of `SmoothPartitionOfUnity`.

## TODO

- More generally, all results should apply to approximating continuous sections of a smooth
  vector bundle by smooth sections.
- If needed, specialize to `M = U` an open subset of a normed space `E`
  (we currently do `M = E` only).

-/

@[expose] public section

open Set Function
open scoped Topology ContDiff Manifold

noncomputable section

section Manifold

variable {E F H M : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  [TopologicalSpace H] (I : ModelWithCorners ‚Ñù E H)
  [TopologicalSpace M] [ChartedSpace H M] [IsManifold I ‚àû M] [SigmaCompactSpace M] [T2Space M]

variable {f : M ‚Üí F} {Œµ : M ‚Üí ‚Ñù}

theorem Continuous.exists_contMDiff_approx_and_eqOn (n : ‚Ñï‚àû)
    (f_cont : Continuous f) (Œµ_cont : Continuous Œµ) (Œµ_pos : ‚àÄ x, 0 < Œµ x)
    {S U : Set M} (hS : IsClosed S) (hU : U ‚àà ùìùÀ¢ S) (hfU : CMDiff[U] n f) :
    ‚àÉ g : C^n‚üÆI, M; ùìò(‚Ñù, F), F‚üØ,
      (‚àÄ x, dist (g x) (f x) < Œµ x) ‚àß EqOn g f S ‚àß support g ‚äÜ support f := by
  have dist_f_f : ‚àÄ x, dist (f x) (f x) < Œµ x := by simpa only [dist_self] using Œµ_pos
  let t : M ‚Üí Set F := fun x ‚Ü¶ {y | dist y (f x) < Œµ x ‚àß (x ‚àà S ‚Üí y = f x) ‚àß (f x = 0 ‚Üí y = 0)}
  suffices ‚àÉ g : C^n‚üÆI, M; ùìò(‚Ñù, F), F‚üØ, ‚àÄ x, g x ‚àà t x by
    rcases this with ‚ü®g, hg‚ü©
    exact ‚ü®g, fun x ‚Ü¶ (hg x).1, fun x ‚Ü¶ (hg x).2.1, fun x ‚Ü¶ mt (hg x).2.2‚ü©
  have t_conv (x) : Convex ‚Ñù (t x) := (convex_ball (f x) (Œµ x)).inter <|
    (convex_singleton _).setOf_const_imp.inter (convex_singleton _).setOf_const_imp
  apply exists_contMDiffOn_forall_mem_convex_of_local I t_conv
  intro x
  by_cases hx : x ‚àà S
  ¬∑ refine ‚ü®U, mem_nhdsSet_iff_forall.mp hU x hx, ?_‚ü©
    exact ‚ü®f, hfU, fun y _ ‚Ü¶ ‚ü®dist_f_f y, fun _ ‚Ü¶ rfl, id‚ü©‚ü©
  ¬∑ have : ‚àÄ·∂† y in ùìù x, y ‚àâ S ‚àß dist (f x) (f y) < Œµ y := (hS.isOpen_compl.eventually_mem hx).and
      ((continuous_const.dist f_cont).continuousAt.eventually_lt Œµ_cont.continuousAt (dist_f_f x))
    have : ‚àÄ·∂† y in ùìù x, (y ‚àâ S ‚àß dist (f x) (f y) < Œµ y) ‚àß (f y = 0 ‚Üí f x = 0) := by
      by_cases hx' : f x = 0
      ¬∑ simpa [hx'] using this
      ¬∑ simpa [hx'] using this.and (f_cont.continuousAt.eventually_ne hx')
    exact ‚ü®_, this, (fun _ ‚Ü¶ f x), contMDiffOn_const, fun y hy ‚Ü¶ ‚ü®hy.1.2, by simp [hy.1.1], hy.2‚ü©‚ü©

theorem Continuous.exists_contMDiff_approx (n : ‚Ñï‚àû)
    (f_cont : Continuous f) (Œµ_cont : Continuous Œµ) (Œµ_pos : ‚àÄ x, 0 < Œµ x) :
    ‚àÉ g : C^n‚üÆI, M; ùìò(‚Ñù, F), F‚üØ, (‚àÄ x, dist (g x) (f x) < Œµ x) ‚àß support g ‚äÜ support f := by
  obtain ‚ü®g, g_approx, -, g_supp‚ü© := f_cont.exists_contMDiff_approx_and_eqOn I n Œµ_cont Œµ_pos
    isClosed_empty mem_nhdsSet_empty contMDiffOn_empty
  exact ‚ü®g, g_approx, g_supp‚ü©

end Manifold

section NormedSpace

variable {E F : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]

variable {f : E ‚Üí F} {Œµ : E ‚Üí ‚Ñù}

theorem Continuous.exists_contDiff_approx_and_eqOn (n : ‚Ñï‚àû)
    (f_cont : Continuous f) (Œµ_cont : Continuous Œµ) (Œµ_pos : ‚àÄ x, 0 < Œµ x)
    {S U : Set E} (hS : IsClosed S) (hU : U ‚àà ùìùÀ¢ S) (hfU : ContDiffOn ‚Ñù n f U) :
    ‚àÉ g : E ‚Üí F, ContDiff ‚Ñù n g ‚àß
      (‚àÄ x, dist (g x) (f x) < Œµ x) ‚àß EqOn g f S ‚àß support g ‚äÜ support f := by
  obtain ‚ü®g, g_approx, g_eqOn, g_supp‚ü© := f_cont.exists_contMDiff_approx_and_eqOn ùìò(‚Ñù, E) n
    Œµ_cont Œµ_pos hS hU hfU.contMDiffOn
  exact ‚ü®g, g.contMDiff.contDiff, g_approx, g_eqOn, g_supp‚ü©

theorem Continuous.exists_contDiff_approx (n : ‚Ñï‚àû)
    (f_cont : Continuous f) (Œµ_cont : Continuous Œµ) (Œµ_pos : ‚àÄ x, 0 < Œµ x) :
    ‚àÉ g : E ‚Üí F, ContDiff ‚Ñù n g ‚àß (‚àÄ x, dist (g x) (f x) < Œµ x) ‚àß support g ‚äÜ support f := by
  obtain ‚ü®g, g_contDiff, g_approx, -, g_supp‚ü© := f_cont.exists_contDiff_approx_and_eqOn n
    Œµ_cont Œµ_pos isClosed_empty mem_nhdsSet_empty contDiffOn_empty
  exact ‚ü®g, g_contDiff, g_approx, g_supp‚ü©

end NormedSpace
