/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.Complex.AbsMax
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Geometry.Manifold.MFDeriv.Basic
import Mathlib.Topology.LocallyConstant.Basic

/-! # Holomorphic functions on complex manifolds

Thanks to the rigidity of complex-differentiability compared to real-differentiability, there are
many results about complex manifolds with no analogue for manifolds over a general normed field. For
now, this file contains just two (closely related) such results:

## Main results

* `MDifferentiable.isLocallyConstant`: A complex-differentiable function on a compact complex
  manifold is locally constant.
* `MDifferentiable.exists_eq_const_of_compactSpace`: A complex-differentiable function on a compact
  preconnected complex manifold is constant.

## TODO

There is a whole theory to develop here.  Maybe a next step would be to develop a theory of
holomorphic vector/line bundles, including:
* the finite-dimensionality of the space of sections of a holomorphic vector bundle
* Siegel's theorem: for any `n + 1` formal ratios `g 0 / h 0`, `g 1 / h 1`, .... `g n / h n` of
  sections of a fixed line bundle `L` over a complex `n`-manifold, there exists a polynomial
  relationship `P (g 0 / h 0, g 1 / h 1, .... g n / h n) = 0`

Another direction would be to develop the relationship with sheaf theory, building the sheaves of
holomorphic and meromorphic functions on a complex manifold and proving algebraic results about the
stalks, such as the Weierstrass preparation theorem.

-/

open scoped Manifold Topology Filter
open Function Set Filter Complex

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F]
variable {H : Type*} [TopologicalSpace H] {I : ModelWithCorners â„‚ E H} [I.Boundaryless]
variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [IsManifold I 1 M]

/-- **Maximum modulus principle**: if `f : M â†’ F` is complex differentiable in a neighborhood of `c`
and the norm `â€–f zâ€–` has a local maximum at `c`, then `â€–f zâ€–` is locally constant in a neighborhood
of `c`. This is a manifold version of `Complex.norm_eventually_eq_of_isLocalMax`. -/
theorem Complex.norm_eventually_eq_of_mdifferentiableAt_of_isLocalMax {f : M â†’ F} {c : M}
    (hd : âˆ€á¶  z in ğ“ c, MDifferentiableAt I ğ“˜(â„‚, F) f z) (hc : IsLocalMax (norm âˆ˜ f) c) :
    âˆ€á¶  y in ğ“ c, â€–f yâ€– = â€–f câ€– := by
  set e := extChartAt I c
  have hI : range I = univ := ModelWithCorners.Boundaryless.range_eq_univ
  have Hâ‚ : ğ“[range I] (e c) = ğ“ (e c) := by rw [hI, nhdsWithin_univ]
  have Hâ‚‚ : map e.symm (ğ“ (e c)) = ğ“ c := by
    rw [â† map_extChartAt_symm_nhdsWithin_range (I := I) c, Hâ‚]
  rw [â† Hâ‚‚, eventually_map]
  replace hd : âˆ€á¶  y in ğ“ (e c), DifferentiableAt â„‚ (f âˆ˜ e.symm) y := by
    have : e.target âˆˆ ğ“ (e c) := Hâ‚ â–¸ extChartAt_target_mem_nhdsWithin c
    filter_upwards [this, Tendsto.eventually Hâ‚‚.le hd] with y hyt hyâ‚‚
    have hys : e.symm y âˆˆ (chartAt H c).source := by
      rw [â† extChartAt_source I c]
      exact (extChartAt I c).map_target hyt
    have hfy : f (e.symm y) âˆˆ (chartAt F (0 : F)).source := mem_univ _
    rw [mdifferentiableAt_iff_of_mem_source hys hfy, hI, differentiableWithinAt_univ,
      e.right_inv hyt] at hyâ‚‚
    exact hyâ‚‚.2
  convert norm_eventually_eq_of_isLocalMax hd _
  Â· exact congr_arg f (extChartAt_to_inv _).symm
  Â· simpa only [e, IsLocalMax, IsMaxFilter, â† Hâ‚‚, (Â· âˆ˜ Â·), extChartAt_to_inv] using hc

/-!
### Functions holomorphic on a set
-/

namespace MDifferentiableOn

/-- **Maximum modulus principle** on a connected set. Let `U` be a (pre)connected open set in a
complex normed space. Let `f : E â†’ F` be a function that is complex differentiable on `U`. Suppose
that `â€–f xâ€–` takes its maximum value on `U` at `c âˆˆ U`. Then `â€–f xâ€– = â€–f câ€–` for all `x âˆˆ U`. -/
theorem norm_eqOn_of_isPreconnected_of_isMaxOn {f : M â†’ F} {U : Set M} {c : M}
    (hd : MDifferentiableOn I ğ“˜(â„‚, F) f U) (hc : IsPreconnected U) (ho : IsOpen U)
    (hcU : c âˆˆ U) (hm : IsMaxOn (norm âˆ˜ f) U c) : EqOn (norm âˆ˜ f) (const M â€–f câ€–) U := by
  set V := {z âˆˆ U | â€–f zâ€– = â€–f câ€–}
  suffices U âŠ† V from fun x hx â†¦ (this hx).2
  have hVo : IsOpen V := by
    refine isOpen_iff_mem_nhds.2 fun x hx â†¦ inter_mem (ho.mem_nhds hx.1) ?_
    replace hm : IsLocalMax (â€–f Â·â€–) x :=
      mem_of_superset (ho.mem_nhds hx.1) fun z hz â†¦ (hm hz).out.trans_eq hx.2.symm
    replace hd : âˆ€á¶  y in ğ“ x, MDifferentiableAt I ğ“˜(â„‚, F) f y :=
      (eventually_mem_nhds_iff.2 (ho.mem_nhds hx.1)).mono fun z â†¦ hd.mdifferentiableAt
    exact (Complex.norm_eventually_eq_of_mdifferentiableAt_of_isLocalMax hd hm).mono fun _ â†¦
      (Eq.trans Â· hx.2)
  have hVne : (U âˆ© V).Nonempty := âŸ¨c, hcU, hcU, rflâŸ©
  set W := U âˆ© {z | â€–f zâ€– = â€–f câ€–}á¶œ
  have hWo : IsOpen W := hd.continuousOn.norm.isOpen_inter_preimage ho isOpen_ne
  have hdVW : Disjoint V W := disjoint_compl_right.mono inf_le_right inf_le_right
  have hUVW : U âŠ† V âˆª W := fun x hx â†¦ (eq_or_ne â€–f xâ€– â€–f câ€–).imp (.intro hx) (.intro hx)
  exact hc.subset_left_of_subset_union hVo hWo hdVW hUVW hVne

/-- **Maximum modulus principle** on a connected set. Let `U` be a (pre)connected open set in a
complex normed space.  Let `f : E â†’ F` be a function that is complex differentiable on `U`. Suppose
that `â€–f xâ€–` takes its maximum value on `U` at `c âˆˆ U`. Then `f x = f c` for all `x âˆˆ U`.

TODO: change assumption from `IsMaxOn` to `IsLocalMax`. -/
theorem eqOn_of_isPreconnected_of_isMaxOn_norm [StrictConvexSpace â„ F] {f : M â†’ F} {U : Set M}
    {c : M} (hd : MDifferentiableOn I ğ“˜(â„‚, F) f U) (hc : IsPreconnected U) (ho : IsOpen U)
    (hcU : c âˆˆ U) (hm : IsMaxOn (norm âˆ˜ f) U c) : EqOn f (const M (f c)) U := fun x hx â†¦
  have Hâ‚ : â€–f xâ€– = â€–f câ€– := hd.norm_eqOn_of_isPreconnected_of_isMaxOn hc ho hcU hm hx
  -- TODO: Add `MDifferentiableOn.add` etc; does it mean importing `Manifold.Algebra.Monoid`?
  have hd' : MDifferentiableOn I ğ“˜(â„‚, F) (f Â· + f c) U := fun x hx â†¦
    âŸ¨(hd x hx).1.add continuousWithinAt_const, (hd x hx).2.add_const _âŸ©
  have Hâ‚‚ : â€–f x + f câ€– = â€–f c + f câ€– :=
    hd'.norm_eqOn_of_isPreconnected_of_isMaxOn hc ho hcU hm.norm_add_self hx
  eq_of_norm_eq_of_norm_add_eq Hâ‚ <| by simp only [Hâ‚‚, SameRay.rfl.norm_add, Hâ‚, Function.const]

/-- If a function `f : M â†’ F` from a complex manifold to a complex normed space is holomorphic on a
(pre)connected compact open set, then it is a constant on this set. -/
theorem apply_eq_of_isPreconnected_isCompact_isOpen {f : M â†’ F} {U : Set M} {a b : M}
    (hd : MDifferentiableOn I ğ“˜(â„‚, F) f U) (hpc : IsPreconnected U) (hc : IsCompact U)
    (ho : IsOpen U) (ha : a âˆˆ U) (hb : b âˆˆ U) : f a = f b := by
  refine ?_
  -- Subtract `f b` to avoid the assumption `[StrictConvexSpace â„ F]`
  wlog hbâ‚€ : f b = 0 generalizing f
  Â· have hd' : MDifferentiableOn I ğ“˜(â„‚, F) (f Â· - f b) U := fun x hx â†¦
      âŸ¨(hd x hx).1.sub continuousWithinAt_const, (hd x hx).2.sub_const _âŸ©
    simpa [sub_eq_zero] using this hd' (sub_self _)
  rcases hc.exists_isMaxOn âŸ¨a, haâŸ© hd.continuousOn.norm with âŸ¨c, hcU, hcâŸ©
  have : âˆ€ x âˆˆ U, â€–f xâ€– = â€–f câ€– :=
    norm_eqOn_of_isPreconnected_of_isMaxOn hd hpc ho hcU hc
  rw [hbâ‚€, â† norm_eq_zero, this a ha, â† this b hb, hbâ‚€, norm_zero]

end MDifferentiableOn

/-!
### Functions holomorphic on the whole manifold

Porting note: lemmas in this section were generalized from `ğ“˜(â„‚, E)` to an unspecified boundaryless
model so that it works, e.g., on a product of two manifolds without a boundary. This can break
`apply MDifferentiable.apply_eq_of_compactSpace`, use
`apply MDifferentiable.apply_eq_of_compactSpace (I := I)` instead or dot notation on an existing
`MDifferentiable` hypothesis.
-/

namespace MDifferentiable

variable [CompactSpace M]

/-- A holomorphic function on a compact complex manifold is locally constant. -/
protected theorem isLocallyConstant {f : M â†’ F} (hf : MDifferentiable I ğ“˜(â„‚, F) f) :
    IsLocallyConstant f :=
  haveI : LocallyConnectedSpace H := I.toHomeomorph.locallyConnectedSpace
  haveI : LocallyConnectedSpace M := ChartedSpace.locallyConnectedSpace H M
  IsLocallyConstant.of_constant_on_preconnected_clopens fun _ hpc hclo _a ha _b hb â†¦
    hf.mdifferentiableOn.apply_eq_of_isPreconnected_isCompact_isOpen hpc
      hclo.isClosed.isCompact hclo.isOpen hb ha

/-- A holomorphic function on a compact connected complex manifold is constant. -/
theorem apply_eq_of_compactSpace [PreconnectedSpace M] {f : M â†’ F}
    (hf : MDifferentiable I ğ“˜(â„‚, F) f) (a b : M) : f a = f b :=
  hf.isLocallyConstant.apply_eq_of_preconnectedSpace _ _

/-- A holomorphic function on a compact connected complex manifold is the constant function `f â‰¡ v`,
for some value `v`. -/
theorem exists_eq_const_of_compactSpace [PreconnectedSpace M] {f : M â†’ F}
    (hf : MDifferentiable I ğ“˜(â„‚, F) f) : âˆƒ v : F, f = Function.const M v :=
  hf.isLocallyConstant.exists_eq_const

end MDifferentiable
