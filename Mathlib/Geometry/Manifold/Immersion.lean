/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
module

public import Mathlib.Geometry.Manifold.ContMDiff.Atlas
public import Mathlib.Geometry.Manifold.ContMDiff.NormedSpace
public import Mathlib.Geometry.Manifold.IsManifold.ExtChartAt
public import Mathlib.Geometry.Manifold.LocalSourceTargetProperty
public import Mathlib.Analysis.Normed.Module.Shrink
public import Mathlib.Topology.Algebra.Module.TransferInstance

public import Mathlib.Geometry.Manifold.Notation

/-! # Smooth immersions

In this file, we define `C^n` immersions between `C^n` manifolds.
The correct definition in the infinite-dimensional setting differs from the standard
finite-dimensional definition (concerning the `mfderiv` being injective): future pull requests will
prove that our definition implies the latter, and that both are equivalent for finite-dimensional
manifolds.

This definition can be conveniently formulated in terms of local properties: `f` is an immersion at
`x` iff there exist suitable charts near `x` and `f x` such that `f` has a nice form w.r.t. these
charts. Most results below can be deduced from more abstract results about such local properties.
This shortens the overall argument, as the definition of submersions has the same general form.

## Main definitions

* `IsImmersionAtOfComplement F I J n f x` means a map `f : M â†’ N` between `C^n` manifolds `M` and
  `N` is an immersion at `x : M`: there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and `f x`,
  respectively, such that in these charts, `f` looks like `u â†¦ (u, 0)`, w.r.t. some equivalence
  `E' â‰ƒL[ğ•œ] E Ã— F`. We do not demand that `f` be differentiable (this follows from this definition).
* `IsImmersionAt I J n f x` means that `f` is a `C^n` immersion at `x : M` for some choice of a
  complement `F` of the model normed space `E` of `M` in the model normed space `E'` of `N`.
  In most cases, prefer this definition over `IsImmersionAtOfComplement`.
* `IsImmersionOfComplement F I J n f` means `f : M â†’ N` is an immersion at every point `x : M`,
  w.r.t. the chosen complement `F`.
* `IsImmersion I J n f` means `f : M â†’ N` is an immersion at every point `x : M`,
  w.r.t. some global choice of complement.

## Main results

* `IsImmersionAt.congr_of_eventuallyEq`: being an immersion is a local property.
  If `f` and `g` agree near `x` and `f` is an immersion at `x`, so is `g`
* `IsImmersionAtOfComplement.congr_F`, `IsImmersionOfComplement.congr_F`:
  being an immersion (at `x`) w.r.t. `F` is stable under
  replacing the complement `F` by an isomorphic copy
* `IsOpen.isImmersionAtOfComplement` and `IsOpen.isImmersionAt`:
  the set of points where `IsImmersionAt(OfComplement)` holds is open.
* `IsImmersionAt.prodMap` and `IsImmersion.prodMap`: the product of two immersions (at a point)
  is an immersion (at a point).
* `IsImmersion.id`: the identity map is an immersion
* `IsImmersion.of_opens`: the inclusion of an open subset `s â†’ M` of a smooth manifold
  is a smooth immersion
* `IsImmersionAt.contMDiffAt`: if f is an immersion at `x`, it is `C^n` at `x`.
* `IsImmersion.contMDiff`: if f is a `C^n` immersion, it is automatically `C^n`
  in the sense of `ContMDiff`..

## Implementation notes

* In most applications, there is no need to control the chosen complement in the definition of
  immersions, so `IsImmersion(At)` is perfectly fine to use. Such control will be helpful, however,
  when considering the local characterisation of submanifolds: locally, a submanifold is described
  either as the image of an immersion, or the preimage of a submersion --- w.r.t. the same
  complement. Having access to a definition version with complements allows stating this equivalence
  cleanly.
* To avoid a free universe variable in `IsImmersion(At)`, we ask for a complement in the same
  universe as the model normed space for `N`. We provide convenience constructors which do not
  have this restriction (recovering usability).
  The underlying observation is that the equivalence in the definition of immersions allows
  shrinking the universe of the complement: this is implemented in
  `IsImmersion(At)OfComplement.small` and `IsImmersion(At)OfComplement.smallEquiv`.

## TODO
* The converse to `IsImmersionAtOfComplement.congr_F` also holds: any two complements are
  isomorphic, as they are isomorphic to the cokernel of the differential `mfderiv I J f x`.
* If `f` is an immersion at `x`, its differential splits, hence is injective.
* If `f : M â†’ N` is a map between Banach manifolds, `mfderiv I J f x` splitting implies `f` is an
  immersion at `x`. (This requires the inverse function theorem.)
* `IsImmersionAt.comp`: if `f : M â†’ N` and `g: N â†’ N'` are maps between Banach manifolds such that
  `f` is an immersion at `x : M` and `g` is an immersion at `f x`, then `g âˆ˜ f` is an immersion
  at `x`.
* `IsImmersion.comp`: the composition of immersions (between Banach manifolds) is an immersion
* If `f : M â†’ N` is a map between finite-dimensional manifolds, `mfderiv I J f x` being injective
  implies `f` is an immersion at `x`.
* `IsLocalDiffeomorphAt.isImmersionAt` and `IsLocalDiffeomorph.isImmersion`:
  a local diffeomorphism (at `x`) is an immersion (at `x`)
* `Diffeomorph.isImmersion`: in particular, a diffeomorphism is an immersion

## References

* [Juan Margalef-Roig and Enrique Outerelo Dominguez, *Differential topology*][roigdomingues1992]

-/

open scoped Topology ContDiff
open Function Set Manifold

@[expose] public section

noncomputable section

namespace Manifold

-- We manually name the universe of `E''` as `IsImmersionAt` will use it.
universe u
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E E' E''' : Type*} {E'' : Type u} {F F' : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] [NormedAddCommGroup E'''] [NormedSpace ğ•œ E''']
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup F'] [NormedSpace ğ•œ F']
  {H : Type*} [TopologicalSpace H] {H' : Type*} [TopologicalSpace H']
  {G : Type*} [TopologicalSpace G] {G' : Type*} [TopologicalSpace G']
  {I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'}
  {J : ModelWithCorners ğ•œ E'' G} {J' : ModelWithCorners ğ•œ E''' G'}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  {n : WithTop â„•âˆ}

variable (F I J M N) in
/-- The local property of being an immersion at a point: `f : M â†’ N` is an immersion at `x` if
there exist charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and `f x`, respectively, such that in these
charts, `f` looks like the inclusion `u â†¦ (u, 0)`.

This definition has a fixed parameter `F`, which is a choice of complement of `E` in the model
normed space `E'` of `N`: being an immersion at `x` includes a choice of linear isomorphism
between `E Ã— F` and `E'`. -/
def ImmersionAtProp : (M â†’ N) â†’ OpenPartialHomeomorph M H â†’ OpenPartialHomeomorph N G â†’ Prop :=
  fun f domChart codChart â†¦ âˆƒ equiv : (E Ã— F) â‰ƒL[ğ•œ] E'',
    EqOn ((codChart.extend J) âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target

omit [ChartedSpace H M] [ChartedSpace G N] in
/-- Being an immersion at `x` is a local property. -/
lemma isLocalSourceTargetProperty_immersionAtProp :
    IsLocalSourceTargetProperty (ImmersionAtProp F I J M N) where
  mono_source {f Ï† Ïˆ s} hs hf := by
    obtain âŸ¨equiv, hfâŸ© := hf
    exact âŸ¨equiv, hf.mono (by simp; grind)âŸ©
  congr {f g Ï† Ïˆ} hfg hf := by
    obtain âŸ¨equiv, hfâŸ© := hf
    refine âŸ¨equiv, EqOn.trans (fun x hx â†¦ ?_) (hf.mono (by simp))âŸ©
    have : ((Ï†.extend I).symm) x âˆˆ Ï†.source := by simp_all
    grind

variable (F I J n) in
/-- `f : M â†’ N` is a `C^n` immersion at `x` if there are charts `Ï†` and `Ïˆ` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u â†¦ (u, 0)`.
Additionally, we demand that `f` map `Ï†.source` into `Ïˆ.source`.

NB. We don't know the particular atlases used for `M` and `N`, so asking for `Ï†` and `Ïˆ` to be
in the `atlas` would be too optimistic: lying in the `maximalAtlas` is sufficient.

This definition has a fixed parameter `F`, which is a choice of complement of `E` in `E'`:
being an immersion at `x` includes a choice of linear isomorphism between `E Ã— F` and `E'`.
While the particular choice of complement is often not important, choosing a complement is useful
in some settings, such as proving that embedded submanifolds are locally given either by an
immersion or a submersion.
Unless you have a particular reason, prefer to use `IsImmersionAt` instead.
-/
irreducible_def IsImmersionAtOfComplement (f : M â†’ N) (x : M) : Prop :=
  LiftSourceTargetPropertyAt I J n f x (ImmersionAtProp F I J M N)

open Lean Elab Meta Qq Manifold.Elab in
/-- `IsImmersionAtOfComplement% F n f x` elaborates to `IsImmersionAtOfComplement F I J n f x`,
trying to determine `I` and `J` from the local context. -/
scoped elab:max "IsImmersionAtOfComplement%" ppSpace F:term:arg ppSpace nt:term:arg ppSpace
    f:term:arg : term => do
  let eF â† Term.elabTerm F none
  let ne â† Term.elabTermEnsuringType nt q(WithTop â„•âˆ)
  let ef â† ensureIsFunction <|â† Term.elabTerm f none
  let (srcI, tgtI) â† findModels ef none
  mkAppM ``IsImmersionAtOfComplement #[eF, srcI, tgtI, ne, ef]

-- Lift the universe from `E''`, to avoid a free universe parameter.
variable (I J n) in
/-- `f : M â†’ N` is a `C^n` immersion at `x` if there are charts `Ï†` and `Ïˆ` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u â†¦ (u, 0)`.
Additionally, we demand that `f` map `Ï†.source` into `Ïˆ.source`.

NB. We don't know the particular atlases used for `M` and `N`, so asking for `Ï†` and `Ïˆ` to be
in the `atlas` would be too optimistic: lying in the `maximalAtlas` is sufficient.

Implicit in this definition is an abstract choice `F` of a complement of `E` in `E'`: being an
immersion at `x` includes a choice of linear isomorphism between `E Ã— F` and `E'`, which is
where the choice of `F` enters.
If you need stronger control over the complement `F`, use `IsImmersionAtOfComplement` instead.
-/
irreducible_def IsImmersionAt (f : M â†’ N) (x : M) : Prop :=
  âˆƒ (F : Type u) (_ : NormedAddCommGroup F) (_ : NormedSpace ğ•œ F),
    IsImmersionAtOfComplement F I J n f x

open Lean Elab Meta Qq Manifold.Elab in
/-- `IsImmersionAt% n f x` elaborates to `IsImmersionAt I J n f x`,
trying to determine `I` and `J` from the local context. -/
scoped elab:max "IsImmersionAt%" ppSpace nt:term:arg ppSpace f:term:arg : term => do
  let ne â† Term.elabTermEnsuringType nt q(WithTop â„•âˆ)
  let ef â† ensureIsFunction <|â† Term.elabTerm f none
  let (srcI, tgtI) â† findModels ef none
  mkAppM ``IsImmersionAt #[srcI, tgtI, ne, ef]

variable {f g : M â†’ N} {x : M}

namespace IsImmersionAtOfComplement

lemma mk_of_charts (equiv : (E Ã— F) â‰ƒL[ğ•œ] E'') (domChart : OpenPartialHomeomorph M H)
    (codChart : OpenPartialHomeomorph N G)
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas J n N)
    (hsource : domChart.source âŠ† f â»Â¹' codChart.source)
    (hwrittenInExtend : EqOn ((codChart.extend J) âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target) : IsImmersionAtOfComplement% F n f x := by
  rw [IsImmersionAtOfComplement_def]
  use domChart, codChart
  use equiv

/-- `f : M â†’ N` is a `C^n` immersion at `x` if there are charts `Ï†` and `Ïˆ` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u â†¦ (u, 0)`.
This version does not assume that `f` maps `Ï†.source` to `Ïˆ.source`,
but that `f` is continuous at `x`. -/
lemma mk_of_continuousAt {f : M â†’ N} {x : M} (hf : ContinuousAt f x) (equiv : (E Ã— F) â‰ƒL[ğ•œ] E'')
    (domChart : OpenPartialHomeomorph M H) (codChart : OpenPartialHomeomorph N G)
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas J n N)
    (hwrittenInExtend : EqOn ((codChart.extend J) âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target) : IsImmersionAtOfComplement% F n f x := by
  rw [IsImmersionAtOfComplement_def]
  exact LiftSourceTargetPropertyAt.mk_of_continuousAt hf isLocalSourceTargetProperty_immersionAtProp
    _ _ hx hfx hdomChart hcodChart âŸ¨equiv, hwrittenInExtendâŸ©

/-- A choice of chart on the domain `M` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.codChart` and `h.equiv`,
`f` will look like an inclusion `u â†¦ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.codChart` and `h.codChart`. -/
def domChart (h : IsImmersionAtOfComplement% F n f x) : OpenPartialHomeomorph M H := by
  rw [IsImmersionAtOfComplement_def] at h
  exact LiftSourceTargetPropertyAt.domChart h

/-- A choice of chart on the co-domain `N` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.domChart` and `h.equiv`,
`f` will look like an inclusion `u â†¦ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.equiv` and `h.domChart`. -/
def codChart (h : IsImmersionAtOfComplement% F n f x) : OpenPartialHomeomorph N G := by
  rw [IsImmersionAtOfComplement_def] at h
  exact LiftSourceTargetPropertyAt.codChart h

lemma mem_domChart_source (h : IsImmersionAtOfComplement% F n f x) : x âˆˆ h.domChart.source := by
  rw [IsImmersionAtOfComplement_def] at h
  exact LiftSourceTargetPropertyAt.mem_domChart_source h

lemma mem_codChart_source (h : IsImmersionAtOfComplement% F n f x) :
    f x âˆˆ h.codChart.source := by
  rw [IsImmersionAtOfComplement_def] at h
  exact LiftSourceTargetPropertyAt.mem_codChart_source h

lemma domChart_mem_maximalAtlas (h : IsImmersionAtOfComplement% F n f x) :
    h.domChart âˆˆ IsManifold.maximalAtlas I n M := by
  rw [IsImmersionAtOfComplement_def] at h
  exact LiftSourceTargetPropertyAt.domChart_mem_maximalAtlas h

lemma codChart_mem_maximalAtlas (h : IsImmersionAtOfComplement% F n f x) :
    h.codChart âˆˆ IsManifold.maximalAtlas J n N := by
  rw [IsImmersionAtOfComplement_def] at h
  exact LiftSourceTargetPropertyAt.codChart_mem_maximalAtlas h

lemma source_subset_preimage_source (h : IsImmersionAtOfComplement% F n f x) :
    h.domChart.source âŠ† f â»Â¹' h.codChart.source := by
  rw [IsImmersionAtOfComplement_def] at h
  exact h.source_subset_preimage_source

lemma mapsto_domChart_source_codChart_source (h : IsImmersionAtOfComplement% F n f x) :
    MapsTo f h.domChart.source h.codChart.source := by
  rw [IsImmersionAtOfComplement_def] at h
  exact h.source_subset_preimage_source

/-- A linear equivalence `E Ã— F â‰ƒL[ğ•œ] E''` which belongs to the data of an immersion `f` at `x`:
the particular equivalence is arbitrary, but this choice matches the witnesses given by
`h.domChart` and `h.codChart`. -/
def equiv (h : IsImmersionAtOfComplement% F n f x) : (E Ã— F) â‰ƒL[ğ•œ] E'' := by
  rw [IsImmersionAtOfComplement_def] at h
  exact Classical.choose <| LiftSourceTargetPropertyAt.property h

lemma writtenInCharts (h : IsImmersionAtOfComplement% F n f x) :
    EqOn ((h.codChart.extend J) âˆ˜ f âˆ˜ (h.domChart.extend I).symm) (h.equiv âˆ˜ (Â·, 0))
      (h.domChart.extend I).target := by
  rw [IsImmersionAtOfComplement_def] at h
  exact Classical.choose_spec <| LiftSourceTargetPropertyAt.property h

lemma property (h : IsImmersionAtOfComplement% F n f x) :
    LiftSourceTargetPropertyAt I J n f x (ImmersionAtProp F I J M N) := by
  rwa [IsImmersionAtOfComplement_def] at h

/--
If `f` is an immersion at `x`, it maps its domain chart's target `(h.domChart.extend I).target`
to its codomain chart's target `(h.domChart.extend J).target`.

Roig and Domingues' [roigdomingues1992] definition of immersions only asks for this inclusion
between the targets of the local charts: using mathlib's formalisation conventions, that condition
is *slightly* weaker than `source_subset_preimage_source`: the latter implies that
`h.codChart.extend J âˆ˜ f` maps `h.domChart.source` to
`(h.codChart.extend J).target = (h.codChart.extend I) '' h.codChart.source`,
but that does *not* imply `f` maps `h.domChart.source` to `h.codChart.source`;
a priori `f` could map some point `f âˆ˜ h.domChart.extend I x âˆ‰ h.codChart.source` into the target.
Note that this difference only occurs because of our design using junk values;
this is not a mathematically meaningful difference.

At the same time, this condition is fairly weak: it is implied, for instance, by `f` being
continuous at `x` (see `mk_of_continuousAt`), which is easy to ascertain in practice.

See `target_subset_preimage_target` for a version stated using preimages instead of images.
-/
lemma map_target_subset_target (h : IsImmersionAtOfComplement% F n f x) :
    (h.equiv âˆ˜ (Â·, 0)) '' (h.domChart.extend I).target âŠ† (h.codChart.extend J).target := by
  rw [â† h.writtenInCharts.image_eq, Set.image_comp, Set.image_comp,
    PartialEquiv.symm_image_target_eq_source, OpenPartialHomeomorph.extend_source,
    â† PartialEquiv.image_source_eq_target]
  have : f '' h.domChart.source âŠ† h.codChart.source := by
    simp [h.source_subset_preimage_source]
  grw [this, OpenPartialHomeomorph.extend_source]

/-- If `f` is an immersion at `x`, its domain chart's target `(h.domChart.extend I).target`
is mapped to its codomain chart's target `(h.domChart.extend J).target`:
see `map_target_subset_target` for a version stated using images. -/
lemma target_subset_preimage_target (h : IsImmersionAtOfComplement% F n f x) :
    (h.domChart.extend I).target âŠ† (h.equiv âˆ˜ (Â·, 0)) â»Â¹' (h.codChart.extend J).target :=
  fun _x hx â†¦ h.map_target_subset_target (mem_image_of_mem _ hx)

/-- If `f` is an immersion at `x` and `g = f` on some neighbourhood of `x`,
then `g` is an immersion at `x`. -/
lemma congr_of_eventuallyEq (hf : IsImmersionAtOfComplement% F n f x) (hfg : f =á¶ [ğ“ x] g) :
    IsImmersionAtOfComplement% F n g x := by
  rw [IsImmersionAtOfComplement_def]
  exact LiftSourceTargetPropertyAt.congr_of_eventuallyEq
    isLocalSourceTargetProperty_immersionAtProp hf.property hfg

/-- If `f = g` on some neighbourhood of `x`,
then `f` is an immersion at `x` if and only if `g` is an immersion at `x`. -/
lemma congr_iff_of_eventuallyEq (hfg : f =á¶ [ğ“ x] g) :
    IsImmersionAtOfComplement% F n f x â†” IsImmersionAtOfComplement% F n g x := by
  simpa only [IsImmersionAtOfComplement_def] using
    LiftSourceTargetPropertyAt.congr_iff_of_eventuallyEq
      isLocalSourceTargetProperty_immersionAtProp hfg

lemma small (hf : IsImmersionAtOfComplement% F n f x) : Small.{u} F :=
  small_of_injective <| hf.equiv.injective.comp (Prod.mk_right_injective 0)

/-- Given an immersion `f` at `x`, this is a choice of complement which lives in the same universe
as the model space for the co-domain of `f`: this is useful to avoid universe restrictions. -/
def smallComplement (hf : IsImmersionAtOfComplement% F n f x) : Type u :=
  haveI := hf.small
  Shrink.{u} F

instance (hf : IsImmersionAtOfComplement% F n f x) : NormedAddCommGroup hf.smallComplement := by
  haveI := hf.small
  unfold smallComplement
  infer_instance

instance (hf : IsImmersionAtOfComplement% F n f x) : NormedSpace ğ•œ hf.smallComplement := by
  haveI := hf.small
  unfold smallComplement
  infer_instance

/-- Given an immersion `f` at `x` w.r.t. a complement `F`, this construction provides
a continuous linear equivalence from `F` to the small complement of `F`:
mathematically, this is just the identity map; however, this is technically useful as it enables
us to always work with `hf.smallComplement`. -/
def smallEquiv (hf : IsImmersionAtOfComplement% F n f x) : F â‰ƒL[ğ•œ] hf.smallComplement :=
  haveI := hf.small
  ((equivShrink F).symm.continuousLinearEquiv ğ•œ).symm

lemma trans_F (h : IsImmersionAtOfComplement% F n f x) (e : F â‰ƒL[ğ•œ] F') :
    IsImmersionAtOfComplement% F' n f x := by
  rewrite [IsImmersionAtOfComplement_def]
  refine âŸ¨h.domChart, h.codChart, h.mem_domChart_source, h.mem_codChart_source,
    h.domChart_mem_maximalAtlas, h.codChart_mem_maximalAtlas, h.source_subset_preimage_source, ?_âŸ©
  use ((ContinuousLinearEquiv.refl ğ•œ E).prodCongr e.symm).trans h.equiv
  apply Set.EqOn.trans h.writtenInCharts
  intro x hx
  simp

/-- Being an immersion at `x` w.r.t. `F` is stable under replacing `F` by an isomorphic copy. -/
lemma congr_F (e : F â‰ƒL[ğ•œ] F') :
    IsImmersionAtOfComplement% F n f x â†” IsImmersionAtOfComplement% F' n f x :=
  âŸ¨fun h â†¦ trans_F (e := e) h, fun h â†¦ trans_F (e := e.symm) hâŸ©

/- The set of points where `IsImmersionAtOfComplement` holds is open. -/
lemma _root_.IsOpen.isImmersionAtOfComplement :
    IsOpen {x | IsImmersionAtOfComplement% F n f x} := by
  simp_rw [IsImmersionAtOfComplement_def]
  exact .liftSourceTargetPropertyAt

/-- If `f: M â†’ N` and `g: M' Ã— N'` are immersions at `x` and `x'`, respectively,
then `f Ã— g: M Ã— N â†’ M' Ã— N'` is an immersion at `(x, x')`. -/
theorem prodMap {f : M â†’ N} {g : M' â†’ N'} {x' : M'}
    [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N] [IsManifold J' n N']
    (hf : IsImmersionAtOfComplement% F n f x) (hg : IsImmersionAtOfComplement% F' n g x') :
    IsImmersionAtOfComplement (F Ã— F') (I.prod I') (J.prod J') n (Prod.map f g) (x, x') := by
  rw [IsImmersionAtOfComplement_def]
  apply LiftSourceTargetPropertyAt.prodMap hf.property hg.property
  rintro f Ï†â‚ Ïˆâ‚ g Ï†â‚‚ Ïˆâ‚‚ âŸ¨equivâ‚, hfpropâŸ© âŸ¨equivâ‚‚, hgpropâŸ©
  use (ContinuousLinearEquiv.prodProdProdComm ğ•œ E E' F F').trans (equivâ‚.prodCongr equivâ‚‚)
  rw [Ï†â‚.extend_prod Ï†â‚‚, Ïˆâ‚.extend_prod, PartialEquiv.prod_target, eqOn_prod_iff]
  exact âŸ¨fun x âŸ¨hx, hx'âŸ© â†¦ by simpa using hfprop hx, fun x âŸ¨hx, hx'âŸ© â†¦ by simpa using hgprop hx'âŸ©

/-- If `f` is an immersion at `x` w.r.t. some complement `F`, it is an immersion at `x`.

Note that the proof contains a small formalisation-related subtlety: `F` can live in any universe,
while being an immersion at `x` requires the existence of a complement in the same universe as
the model normed space of `N`. This is solved by `smallComplement` and `smallEquiv`.
-/
lemma isImmersionAt (h : IsImmersionAtOfComplement% F n f x) : IsImmersionAt% n f x := by
  rw [IsImmersionAt_def]
  use h.smallComplement, by infer_instance, by infer_instance
  exact (IsImmersionAtOfComplement.congr_F h.smallEquiv).mp h

open IsManifold in
/- The inclusion of an open subset `s` of a smooth manifold `M` is an immersion at every point. -/
lemma of_opens [IsManifold I n M] (s : TopologicalSpace.Opens M) (y : s) :
    IsImmersionAtOfComplement PUnit I I n (Subtype.val : s â†’ M) y := by
  apply IsImmersionAtOfComplement.mk_of_continuousAt (by fun_prop) (.prodUnique ğ•œ E _)
    (chartAt H y) (chartAt H y.val) (mem_chart_source H y) (mem_chart_source H y.val)
    (chart_mem_maximalAtlas y) (chart_mem_maximalAtlas y.val)
  intro x hx
  suffices I ((chartAt H â†‘y) ((chartAt H y).symm (I.symm x))) = x by simpa +contextual
  trans I (I.symm x)
  Â· congr 1
    apply OpenPartialHomeomorph.right_inv
    simp_all
  Â· exact I.right_inv (by simp_all)

/-- Prefer using `IsImmersionAtOfComplement.continuousAt` instead -/
theorem continuousOn (h : IsImmersionAtOfComplement% F n f x) :
    ContinuousOn f h.domChart.source := by
  rw [â† h.domChart.continuousOn_writtenInExtend_iff le_rfl
      h.mapsto_domChart_source_codChart_source (I' := J) (I := I),
    â† h.domChart.extend_target_eq_image_source]
  have : ContinuousOn (h.equiv âˆ˜ fun x â†¦ (x, 0)) (h.domChart.extend I).target := by fun_prop
  exact this.congr h.writtenInCharts

/-- A `C^n` immersion at `x` is continuous at `x`. -/
theorem continuousAt (h : IsImmersionAtOfComplement% F n f x) : ContinuousAt f x :=
  h.continuousOn.continuousAt (h.domChart.open_source.mem_nhds (mem_domChart_source h))

variable [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N]

/-- Prefer using `IsImmersionAtOfComplement.contMDiffAt` instead -/
theorem contMDiffOn (h : IsImmersionAtOfComplement% F n f x) : CMDiff[h.domChart.source] n f := by
  rw [â† contMDiffOn_writtenInExtend_iff h.domChart_mem_maximalAtlas
    h.codChart_mem_maximalAtlas le_rfl h.mapsto_domChart_source_codChart_source,
    â† h.domChart.extend_target_eq_image_source]
  have : ContMDiff ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E'') n (h.equiv âˆ˜ fun x â†¦ (x, 0)) := by
    have : ContMDiff (ğ“˜(ğ•œ, E Ã— F)) ğ“˜(ğ•œ, E'') n h.equiv := by
      rw [contMDiff_iff_contDiff]
      exact h.equiv.contDiff
    apply this.comp
    rw [contMDiff_iff_contDiff, contDiff_prod_iff]
    exact âŸ¨contDiff_id, contDiff_const (c := (0 : F))âŸ©
  exact this.contMDiffOn.congr h.writtenInCharts

/-- A `C^n` immersion at `x` is `C^n` at `x`. -/
theorem contMDiffAt (h : IsImmersionAtOfComplement% F n f x) : CMDiffAt n f x :=
  h.contMDiffOn.contMDiffAt (h.domChart.open_source.mem_nhds (mem_domChart_source h))

end IsImmersionAtOfComplement

namespace IsImmersionAt

lemma mk_of_charts (equiv : (E Ã— F) â‰ƒL[ğ•œ] E'')
    (domChart : OpenPartialHomeomorph M H) (codChart : OpenPartialHomeomorph N G)
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas J n N)
    (hsource : domChart.source âŠ† f â»Â¹' codChart.source)
    (hwrittenInExtend : EqOn ((codChart.extend J) âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target) : IsImmersionAt% n f x := by
  rw [IsImmersionAt_def]
  have aux : IsImmersionAtOfComplement% F n f x := by
    apply IsImmersionAtOfComplement.mk_of_charts <;> assumption
  use aux.smallComplement, by infer_instance, by infer_instance
  rwa [â† IsImmersionAtOfComplement.congr_F aux.smallEquiv]

/-- `f : M â†’ N` is a `C^n` immersion at `x` if there are charts `Ï†` and `Ïˆ` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u â†¦ (u, 0)`.
This version does not assume that `f` maps `Ï†.source` to `Ïˆ.source`,
but that `f` is continuous at `x`. -/
lemma mk_of_continuousAt {f : M â†’ N} {x : M} (hf : ContinuousAt f x) (equiv : (E Ã— F) â‰ƒL[ğ•œ] E'')
    (domChart : OpenPartialHomeomorph M H) (codChart : OpenPartialHomeomorph N G)
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas J n N)
    (hwrittenInExtend : EqOn ((codChart.extend J) âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target) : IsImmersionAt% n f x := by
  rw [IsImmersionAt_def]
  have aux : IsImmersionAtOfComplement% F n f x := by
    apply IsImmersionAtOfComplement.mk_of_continuousAt <;> assumption
  use aux.smallComplement, by infer_instance, by infer_instance
  rwa [â† IsImmersionAtOfComplement.congr_F aux.smallEquiv]

/-- A choice of complement of the model normed space `E` of `M` in the model normed space
`E'` of `N` -/
def complement (h : IsImmersionAt% n f x) : Type u := by
  rw [IsImmersionAt_def] at h
  exact Classical.choose h

noncomputable instance (h : IsImmersionAt% n f x) : NormedAddCommGroup h.complement := by
  rw [IsImmersionAt_def] at h
  exact Classical.choose <| Classical.choose_spec h

noncomputable instance (h : IsImmersionAt% n f x) : NormedSpace ğ•œ h.complement := by
  rw [IsImmersionAt_def] at h
  exact Classical.choose <| Classical.choose_spec <| Classical.choose_spec h

lemma isImmersionAtOfComplement_complement (h : IsImmersionAt% n f x) :
    IsImmersionAtOfComplement% h.complement n f x := by
  rw [IsImmersionAt_def] at h
  exact Classical.choose_spec <| Classical.choose_spec <| Classical.choose_spec h

/-- A choice of chart on the domain `M` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.codChart` and `h.equiv`,
`f` will look like an inclusion `u â†¦ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.codChart` and `h.codChart`. -/
def domChart (h : IsImmersionAt% n f x) : OpenPartialHomeomorph M H :=
  h.isImmersionAtOfComplement_complement.domChart

/-- A choice of chart on the co-domain `N` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.domChart` and `h.equiv`,
`f` will look like an inclusion `u â†¦ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.equiv` and `h.domChart`. -/
def codChart (h : IsImmersionAt% n f x) : OpenPartialHomeomorph N G :=
  h.isImmersionAtOfComplement_complement.codChart

lemma mem_domChart_source (h : IsImmersionAt% n f x) : x âˆˆ h.domChart.source :=
  h.isImmersionAtOfComplement_complement.mem_domChart_source

lemma mem_codChart_source (h : IsImmersionAt% n f x) : f x âˆˆ h.codChart.source :=
  h.isImmersionAtOfComplement_complement.mem_codChart_source

lemma domChart_mem_maximalAtlas (h : IsImmersionAt% n f x) :
    h.domChart âˆˆ IsManifold.maximalAtlas I n M :=
  h.isImmersionAtOfComplement_complement.domChart_mem_maximalAtlas

lemma codChart_mem_maximalAtlas (h : IsImmersionAt% n f x) :
    h.codChart âˆˆ IsManifold.maximalAtlas J n N :=
  h.isImmersionAtOfComplement_complement.codChart_mem_maximalAtlas

lemma source_subset_preimage_source (h : IsImmersionAt% n f x) :
    h.domChart.source âŠ† f â»Â¹' h.codChart.source :=
  h.isImmersionAtOfComplement_complement.source_subset_preimage_source

/-- A linear equivalence `E Ã— F â‰ƒL[ğ•œ] E''` which belongs to the data of an immersion `f` at `x`:
the particular equivalence is arbitrary, but this choice matches the witnesses given by
`h.domChart` and `h.codChart`. -/
def equiv (h : IsImmersionAt% n f x) : (E Ã— h.complement) â‰ƒL[ğ•œ] E'' :=
  h.isImmersionAtOfComplement_complement.equiv

lemma writtenInCharts (h : IsImmersionAt% n f x) :
    EqOn ((h.codChart.extend J) âˆ˜ f âˆ˜ (h.domChart.extend I).symm) (h.equiv âˆ˜ (Â·, 0))
      (h.domChart.extend I).target :=
  h.isImmersionAtOfComplement_complement.writtenInCharts

lemma property (h : IsImmersionAt% n f x) :
    LiftSourceTargetPropertyAt I J n f x (ImmersionAtProp h.complement I J M N) :=
  h.isImmersionAtOfComplement_complement.property

/--
If `f` is an immersion at `x`, it maps its domain chart's target to its codomain chart's target:
`(h.domChart.extend I).target` to `(h.domChart.extend J).target`.

Roig and Domingues' [roigdomingues1992] definition of immersions only asks for this inclusion
between the targets of the local charts: using mathlib's formalisation conventions, that condition
is *slightly* weaker than `source_subset_preimage_source`: the latter implies that
`h.codChart.extend J âˆ˜ f` maps `h.domChart.source` to
`(h.codChart.extend J).target = (h.codChart.extend I) '' h.codChart.source`,
but that does *not* imply `f` maps `h.domChart.source` to `h.codChart.source`;
a priori `f` could map some point `f âˆ˜ h.domChart.extend I x âˆ‰ h.codChart.source` into the target.
Note that this difference only occurs because of our design using junk values;
this is not a mathematically meaningful difference.

At the same time, this condition is fairly weak: it is implied, for instance, by `f` being
continuous at `x` (see `mk_of_continuousAt`), which is easy to ascertain in practice.

See `target_subset_preimage_target` for a version stated using preimages instead of images.
-/
lemma map_target_subset_target (h : IsImmersionAt% n f x) :
    (h.equiv âˆ˜ (Â·, 0)) '' (h.domChart.extend I).target âŠ† (h.codChart.extend J).target :=
  h.isImmersionAtOfComplement_complement.map_target_subset_target

/-- If `f` is an immersion at `x`, its domain chart's target `(h.domChart.extend I).target`
is mapped to its codomain chart's target `(h.domChart.extend J).target`:
see `map_target_subset_target` for a version stated using images. -/
lemma target_subset_preimage_target (h : IsImmersionAt% n f x) :
    (h.domChart.extend I).target âŠ† (h.equiv âˆ˜ (Â·, 0)) â»Â¹' (h.codChart.extend J).target :=
  fun _x hx â†¦ h.map_target_subset_target (mem_image_of_mem _ hx)

/-- If `f` is an immersion at `x` and `g = f` on some neighbourhood of `x`,
then `g` is an immersion at `x`. -/
lemma congr_of_eventuallyEq (hf : IsImmersionAt% n f x) (hfg : f =á¶ [ğ“ x] g) :
    IsImmersionAt% n g x := by
  rw [IsImmersionAt_def]
  use hf.complement, by infer_instance, by infer_instance
  exact hf.isImmersionAtOfComplement_complement.congr_of_eventuallyEq hfg

/-- If `f = g` on some neighbourhood of `x`,
then `f` is an immersion at `x` if and only if `g` is an immersion at `x`. -/
lemma congr_iff (hfg : f =á¶ [ğ“ x] g) :
    IsImmersionAt% n f x â†” IsImmersionAt% n g x :=
  âŸ¨fun h â†¦ h.congr_of_eventuallyEq hfg, fun h â†¦ h.congr_of_eventuallyEq hfg.symmâŸ©

/- The set of points where `IsImmersionAt` holds is open. -/
lemma _root_.IsOpen.isImmersionAt :
    IsOpen {x | IsImmersionAt% n f x} := by
  rw [isOpen_iff_forall_mem_open]
  exact fun x hx â†¦ âŸ¨{x | IsImmersionAtOfComplement% hx.complement n f x },
    fun y hy â†¦ hy.isImmersionAt, .isImmersionAtOfComplement,
    by simp [hx.isImmersionAtOfComplement_complement]âŸ©

/-- If `f: M â†’ N` and `g: M' Ã— N'` are immersions at `x` and `x'`, respectively,
then `f Ã— g: M Ã— N â†’ M' Ã— N'` is an immersion at `(x, x')`. -/
theorem prodMap {f : M â†’ N} {g : M' â†’ N'} {x' : M'}
    [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N] [IsManifold J' n N']
    (hf : IsImmersionAt% n f x) (hg : IsImmersionAt I' J' n g x') :
    IsImmersionAt (I.prod I') (J.prod J') n (Prod.map f g) (x, x') :=
  hf.isImmersionAtOfComplement_complement.prodMap hg.isImmersionAtOfComplement_complement
    |>.isImmersionAt

/- The inclusion of an open subset `s` of a smooth manifold `M` is an immersion at every point. -/
lemma of_opens [IsManifold I n M] (s : TopologicalSpace.Opens M) (hx : x âˆˆ s) :
    IsImmersionAt I I n (Subtype.val : s â†’ M) âŸ¨x, hxâŸ© := by
  rw [IsImmersionAt_def]
  use PUnit, by infer_instance, by infer_instance
  apply Manifold.IsImmersionAtOfComplement.of_opens

/-- Prefer using `IsImmersionAt.continuousAt` instead -/
theorem continuousOn (h : IsImmersionAt% n f x) : ContinuousOn f h.domChart.source :=
  h.isImmersionAtOfComplement_complement.continuousOn

/-- A `C^n` immersion at `x` is continuous at `x`. -/
theorem continuousAt (h : IsImmersionAt% n f x) : ContinuousAt f x :=
  h.isImmersionAtOfComplement_complement.continuousAt

variable [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N]

/-- Prefer using `IsImmersionAt.contMDiffAt` instead -/
theorem contMDiffOn (h : IsImmersionAt% n f x) : CMDiff[h.domChart.source] n f :=
  h.isImmersionAtOfComplement_complement.contMDiffOn

/-- A `C^n` immersion at `x` is `C^n` at `x`. -/
theorem contMDiffAt (h : IsImmersionAt% n f x) : CMDiffAt n f x :=
  h.isImmersionAtOfComplement_complement.contMDiffAt

end IsImmersionAt

variable (F I J n) in
/-- `f : M â†’ N` is a `C^n` immersion if around each point `x âˆˆ M`,
there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and `f x`, respectively
such that in these charts, `f` looks like `u â†¦ (u, 0)`.

In other words, `f` is an immersion at each `x âˆˆ M`.

This definition has a fixed parameter `F`, which is a choice of complement of `E` in `E'`:
being an immersion at `x` includes a choice of linear isomorphism between `E Ã— F` and `E'`.
-/
def IsImmersionOfComplement (f : M â†’ N) : Prop := âˆ€ x, IsImmersionAtOfComplement F I J n f x

variable (I J n) in
/-- `f : M â†’ N` is a `C^n` immersion if around each point `x âˆˆ M`,
there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and `f x`, respectively
such that in these charts, `f` looks like `u â†¦ (u, 0)`.

Implicit in this definition is an abstract choice `F` of a complement of `E` in `E'`:
being an immersion includes a choice of linear isomorphism between `E Ã— F` and `E'`, which is where
the choice of `F` enters. If you need stronger control over the complement `F`,
use `IsImmersionOfComplement` instead.

Note that our global choice of complement is a bit stronger than asking `f` to be an immersion at
each `x âˆˆ M` w.r.t. potentially varying complements: see `isImmersionAt` for details.
-/
def IsImmersion (f : M â†’ N) : Prop :=
  âˆƒ (F : Type u) (_ : NormedAddCommGroup F) (_ : NormedSpace ğ•œ F), IsImmersionOfComplement F I J n f

open Lean Elab Meta Qq Manifold.Elab in
/-- `IsImmersion% n f` elaborates to `IsImmersion I J n f`,
trying to determine `I` and `J` from the local context. -/
scoped elab:max "IsImmersion%" ppSpace nt:term:arg ppSpace f:term:arg : term => do
  let ne â† Term.elabTermEnsuringType nt q(WithTop â„•âˆ)
  let ef â† ensureIsFunction <|â† Term.elabTerm f none
  let (srcI, tgtI) â† findModels ef none
  mkAppM ``IsImmersion #[srcI, tgtI, ne, ef]

namespace IsImmersionOfComplement

variable {f g : M â†’ N}

/-- If `f` is an immersion, it is an immersion at each point. -/
-- TODO: using the elaborator in the hypothesis fails; investigate and fix!
lemma isImmersionAt (h : IsImmersionOfComplement F I J n f) (x : M) :
    IsImmersionAtOfComplement% F n f x := h x

/-- If `f = g` and `f` is an immersion, so is `g`. -/
theorem congr (h : IsImmersionOfComplement F I J n f) (heq : f = g) :
    IsImmersionOfComplement F I J n g :=
  heq â–¸ h

lemma trans_F (h : IsImmersionOfComplement F I J n f) (e : F â‰ƒL[ğ•œ] F') :
    IsImmersionOfComplement F' I J n f :=
  fun x â†¦ (h x).trans_F e

/-- Being an immersion w.r.t. `F` is stable under replacing `F` by an isomorphic copy. -/
lemma congr_F (e : F â‰ƒL[ğ•œ] F') :
    IsImmersionOfComplement F I J n f â†” IsImmersionOfComplement F' I J n f :=
  âŸ¨fun h â†¦ trans_F (e := e) h, fun h â†¦ trans_F (e := e.symm) hâŸ©

/-- If `f: M â†’ N` and `g: M' Ã— N'` are immersions at `x` and `x'` (w.r.t. `F` and `F'`),
respectively, then `f Ã— g: M Ã— N â†’ M' Ã— N'` is an immersion at `(x, x')` w.r.t. `F Ã— F'`. -/
theorem prodMap {f : M â†’ N} {g : M' â†’ N'}
    [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N] [IsManifold J' n N']
    (h : IsImmersionOfComplement F I J n f) (h' : IsImmersionOfComplement F' I' J' n g) :
    IsImmersionOfComplement (F Ã— F') (I.prod I') (J.prod J') n (Prod.map f g) :=
  fun âŸ¨x, x'âŸ© â†¦ (h x).prodMap (h' x')

/-- If `f` is an immersion w.r.t. some complement `F`, it is an immersion.

Note that the proof contains a small formalisation-related subtlety: `F` can live in any universe,
while being an immersion requires the existence of a complement in the same universe as
the model normed space of `N`. This is solved by `smallComplement` and `smallEquiv`.
-/
lemma isImmersion (h : IsImmersionOfComplement F I J n f) : IsImmersion I J n f := by
  by_cases! hM : IsEmpty M
  Â· rw [IsImmersion]
    use PUnit, by infer_instance, by infer_instance
    exact fun x â†¦ (IsEmpty.false x).elim
  inhabit M
  let x : M := Inhabited.default
  use (h x).smallComplement, by infer_instance, by infer_instance
  exact (IsImmersionOfComplement.congr_F (h x).smallEquiv).mp h

open IsManifold in
/-- The identity map is an immersion with complement `PUnit`. -/
protected lemma id [IsManifold I n M] : IsImmersionOfComplement PUnit I I n (@id M) := by
  intro x
  apply IsImmersionAtOfComplement.mk_of_continuousAt (continuousAt_id) (.prodUnique ğ•œ E _)
    (chartAt H x) (chartAt H x) (mem_chart_source H x) (mem_chart_source H x)
    (chart_mem_maximalAtlas x) (chart_mem_maximalAtlas x)
  intro y hy
  have : I ((chartAt H x) ((chartAt H x).symm (I.symm y))) = y := by
    rw [(chartAt H x).right_inv (by simp_all), I.right_inv (by simp_all)]
  simpa

/- The inclusion of an open subset `s` of a smooth manifold `M` is an immersion. -/
lemma of_opens [IsManifold I n M] (s : TopologicalSpace.Opens M) :
    IsImmersionOfComplement PUnit I I n (Subtype.val : s â†’ M) :=
  fun y â†¦ IsImmersionAtOfComplement.of_opens s y

/-- A `C^n` immersion is `C^n`. -/
theorem contMDiff [IsManifold I n M] [IsManifold J n N]
    (h : IsImmersionOfComplement F I J n f) : ContMDiff I J n f :=
  fun x â†¦ (h x).contMDiffAt

end IsImmersionOfComplement

namespace IsImmersion

variable {f g : M â†’ N}

/-- A choice of complement of the model normed space `E` of `M` in the model normed space
`E'` of `N` -/
def complement (h : IsImmersion% n f) : Type u := Classical.choose h

noncomputable instance (h : IsImmersion% n f) : NormedAddCommGroup h.complement :=
  Classical.choose <| Classical.choose_spec h

noncomputable instance (h : IsImmersion% n f) : NormedSpace ğ•œ h.complement :=
  Classical.choose <| Classical.choose_spec <| Classical.choose_spec h

lemma isImmersionOfComplement_complement (h : IsImmersion% n f) :
    IsImmersionOfComplement h.complement I J n f := -- TODO: using the elaborator here errors!
  Classical.choose_spec <| Classical.choose_spec <| Classical.choose_spec h

/-- If `f` is an immersion, it is an immersion at each point.

Note that the converse statement is false in general:
if `f` is an immersion at each `x`, but with the choice of complement possibly depending on `x`,
there need not be a global choice of complement for which `f` is an immersion at each point.
The complement of `f` at `x` is isomorphic to the cokernel of `mfderiv I J f x`, but the `mfderiv`
of `f` at (even nearby) points `x` and `x'` are not directly related. They have the same rank
(the dimension of `E`, as will follow from injectivity), but if `E''` is infinite-dimensional this
is not conclusive. If `E''` is infinite-dimensional, this dimension can indeed change between
different connected components of `M`.
-/
lemma isImmersionAt (h : IsImmersion% n f) (x : M) : IsImmersionAt% n f x := by
  rw [IsImmersionAt_def]
  use h.complement, by infer_instance, by infer_instance, h.isImmersionOfComplement_complement x

/-- If `f = g` and `f` is an immersion, so is `g`. -/
theorem congr (h : IsImmersion% n f) (heq : f = g) : IsImmersion% n g :=
  heq â–¸ h

/-- If `f: M â†’ N` and `g: M' Ã— N'` are immersions at `x` and `x'`, respectively,
then `f Ã— g: M Ã— N â†’ M' Ã— N'` is an immersion at `(x, x')`. -/
theorem prodMap {f : M â†’ N} {g : M' â†’ N'}
    [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N] [IsManifold J' n N']
    (hf : IsImmersion% n f) (hg : IsImmersion% n g) :
    IsImmersion (I.prod I') (J.prod J') n (Prod.map f g) :=
  (hf.isImmersionOfComplement_complement.prodMap hg.isImmersionOfComplement_complement).isImmersion

open IsManifold in
/-- The identity map is an immersion. -/
protected lemma id [IsManifold I n M] : IsImmersion% n (@id M) :=
  âŸ¨PUnit, by infer_instance, by infer_instance, IsImmersionOfComplement.idâŸ©

/- The inclusion of an open subset `s` of a smooth manifold `M` is an immersion. -/
lemma of_opens [IsManifold I n M] (s : TopologicalSpace.Opens M) :
    IsImmersion I I n (Subtype.val : s â†’ M) :=
  âŸ¨PUnit, by infer_instance, by infer_instance, IsImmersionOfComplement.of_opens sâŸ©

/-- A `C^n` immersion is `C^n`. -/
theorem contMDiff [IsManifold I n M] [IsManifold J n N] (h : IsImmersion% n f) : CMDiff n f :=
  h.isImmersionOfComplement_complement.contMDiff

end IsImmersion

end Manifold
