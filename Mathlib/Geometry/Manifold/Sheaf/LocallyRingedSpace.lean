/-
Copyright (c) 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Geometry.Manifold.Sheaf.Smooth
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace

/-! # Smooth manifolds as locally ringed spaces

This file equips a smooth manifold with the structure of a locally ringed space.

## Main results

* `smoothSheafCommRing.isUnit_stalk_iff`: The units of the stalk at `x` of the sheaf of smooth
  functions from a smooth manifold `M` to its scalar field `ğ•œ`, considered as a sheaf of commutative
  rings, are the functions whose values at `x` are nonzero.

## Main definitions

* `IsManifold.locallyRingedSpace`: A smooth manifold can be considered as a locally ringed space.

## TODO

Characterize morphisms-of-locally-ringed-spaces (`AlgebraicGeometry.LocallyRingedSpace.Hom`) between
smooth manifolds.

-/

noncomputable section
universe u

open scoped ContDiff

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
  {EM : Type*} [NormedAddCommGroup EM] [NormedSpace ğ•œ EM]
  {HM : Type*} [TopologicalSpace HM] (IM : ModelWithCorners ğ•œ EM HM)
  {M : Type u} [TopologicalSpace M] [ChartedSpace HM M]

open AlgebraicGeometry Manifold TopologicalSpace Topology

/-- The units of the stalk at `x` of the sheaf of smooth functions from `M` to `ğ•œ`, considered as a
sheaf of commutative rings, are the functions whose values at `x` are nonzero. -/
theorem smoothSheafCommRing.isUnit_stalk_iff {x : M}
    (f : (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.stalk x) :
    IsUnit f â†” f âˆ‰ RingHom.ker (smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x) := by
  constructor
  Â· rintro âŸ¨âŸ¨f, g, hf, hgâŸ©, rflâŸ© (h' : smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x f = 0)
    simpa [h'] using congr_arg (smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x) hf
  Â· let S := (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf
    -- Suppose that `f`, in the stalk at `x`, is nonzero at `x`
    rintro (hf : _ â‰  0)
    -- Represent `f` as the germ of some function (also called `f`) on an open neighbourhood `U` of
    -- `x`, which is nonzero at `x`
    obtain âŸ¨U : Opens M, hxU, f : C^âˆâŸ®IM, U; ğ“˜(ğ•œ), ğ•œâŸ¯, rflâŸ© := S.germ_exist x f
    have hf' : f âŸ¨x, hxUâŸ© â‰  0 := by
      convert hf
      exact (smoothSheafCommRing.eval_germ U x hxU f).symm
    -- In fact, by continuity, `f` is nonzero on a neighbourhood `V` of `x`
    have H :  âˆ€á¶  (z : U) in ğ“ âŸ¨x, hxUâŸ©, f z â‰  0 := f.2.continuous.continuousAt.eventually_ne hf'
    rw [eventually_nhds_iff] at H
    obtain âŸ¨Vâ‚€, hVâ‚€f, hVâ‚€, hxVâ‚€âŸ© := H
    let V : Opens M := âŸ¨Subtype.val '' Vâ‚€, U.2.isOpenMap_subtype_val Vâ‚€ hVâ‚€âŸ©
    have hUV : V â‰¤ U := Subtype.coe_image_subset (U : Set M) Vâ‚€
    have hV : Vâ‚€ = Set.range (Set.inclusion hUV) := by
      convert (Set.range_inclusion hUV).symm
      ext y
      show _ â†” y âˆˆ Subtype.val â»Â¹' (Subtype.val '' Vâ‚€)
      rw [Set.preimage_image_eq _ Subtype.coe_injective]
    clear_value V
    subst hV
    have hxV : x âˆˆ (V : Set M) := by
      obtain âŸ¨xâ‚€, hxxâ‚€âŸ© := hxVâ‚€
      convert xâ‚€.2
      exact congr_arg Subtype.val hxxâ‚€.symm
    have hVf : âˆ€ y : V, f (Set.inclusion hUV y) â‰  0 :=
      fun y â†¦ hVâ‚€f (Set.inclusion hUV y) (Set.mem_range_self y)
    -- Let `g` be the pointwise inverse of `f` on `V`, which is smooth since `f` is nonzero there
    let g : C^âˆâŸ®IM, V; ğ“˜(ğ•œ), ğ•œâŸ¯ := âŸ¨(f âˆ˜ Set.inclusion hUV)â»Â¹, ?_âŸ©
    -- The germ of `g` is inverse to the germ of `f`, so `f` is a unit
    Â· refine âŸ¨âŸ¨S.germ _ x (hxV) (ContMDiffMap.restrictRingHom IM ğ“˜(ğ•œ) ğ•œ hUV f), S.germ _ x hxV g,
        ?_, ?_âŸ©, S.germ_res_apply hUV.hom x hxV fâŸ©
      Â· rw [â† map_mul]
        -- Qualified the name to avoid Lean not finding a `OneHomClass` https://github.com/leanprover-community/mathlib4/pull/8386
        convert RingHom.map_one _
        apply Subtype.ext
        ext y
        apply mul_inv_cancelâ‚€
        exact hVf y
      Â· rw [â† map_mul]
        -- Qualified the name to avoid Lean not finding a `OneHomClass` https://github.com/leanprover-community/mathlib4/pull/8386
        convert RingHom.map_one _
        apply Subtype.ext
        ext y
        apply inv_mul_cancelâ‚€
        exact hVf y
    Â· intro y
      #adaptation_note /-- https://github.com/leanprover/lean4/pull/6024
        was `exact`; somehow `convert` bypasess unification issues -/
      convert ((contDiffAt_inv _ (hVf y)).contMDiffAt).comp y
        (f.contMDiff.comp (contMDiff_inclusion hUV)).contMDiffAt

/-- The non-units of the stalk at `x` of the sheaf of smooth functions from `M` to `ğ•œ`, considered
as a sheaf of commutative rings, are the functions whose values at `x` are zero. -/
theorem smoothSheafCommRing.nonunits_stalk (x : M) :
    nonunits ((smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.stalk x)
    = RingHom.ker (smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x) := by
  ext1 f
  rw [mem_nonunits_iff, not_iff_comm, Iff.comm]
  apply smoothSheafCommRing.isUnit_stalk_iff

/-- The stalks of the structure sheaf of a smooth manifold are local rings. -/
instance smoothSheafCommRing.instLocalRing_stalk (x : M) :
    IsLocalRing ((smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.stalk x) := by
  apply IsLocalRing.of_nonunits_add
  rw [smoothSheafCommRing.nonunits_stalk]
  intro f g
  exact Ideal.add_mem _

variable (M)

/-- A smooth manifold can be considered as a locally ringed space. -/
def IsManifold.locallyRingedSpace : LocallyRingedSpace where
  carrier := TopCat.of M
  presheaf := smoothPresheafCommRing IM ğ“˜(ğ•œ) M ğ•œ
  IsSheaf := (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).cond
  isLocalRing x := smoothSheafCommRing.instLocalRing_stalk IM x

@[deprecated (since := "2025-01-09")]
noncomputable alias SmoothManifoldWithCorners.locallyRingedSpace := IsManifold.locallyRingedSpace
