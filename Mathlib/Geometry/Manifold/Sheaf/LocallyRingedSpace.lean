/-
Copyright Â© 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Geometry.Manifold.Sheaf.Smooth
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace

/-! # Smooth manifolds as locally ringed spaces

This file equips a smooth manifold-with-corners with the structure of a locally ringed space.

## Main results

* `smoothSheafCommRing.isUnit_stalk_iff`: The units of the stalk at `x` of the sheaf of smooth
  functions from a smooth manifold `M` to its scalar field `ğ•œ`, considered as a sheaf of commutative
  rings, are the functions whose values at `x` are nonzero.

## Main definitions

* `SmoothManifoldWithCorners.locallyRingedSpace`: A smooth manifold-with-corners can be considered
  as a locally ringed space.

## TODO

Characterize morphisms-of-locally-ringed-spaces (`AlgebraicGeometry.LocallyRingedSpace.Hom`) between
smooth manifolds.

-/

noncomputable section
universe u

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
  {EM : Type*} [NormedAddCommGroup EM] [NormedSpace ğ•œ EM]
  {HM : Type*} [TopologicalSpace HM] (IM : ModelWithCorners ğ•œ EM HM)
  (M : Type u) [TopologicalSpace M] [ChartedSpace HM M]

open AlgebraicGeometry Manifold TopologicalSpace Topology Opposite

/-- The units of the stalk at `x` of the sheaf of smooth functions from `M` to `ğ•œ`, considered as a
sheaf of commutative rings, are the functions whose values at `x` are nonzero. -/
theorem smoothSheafCommRing.isUnit_stalk_iff {x : M}
    (f : (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.stalk x) :
    IsUnit f â†” f âˆ‰ RingHom.ker (smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x) := by
  constructor
  Â· rintro âŸ¨âŸ¨f, g, hf, hgâŸ©, rflâŸ© (h' : smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x f = 0)
    simpa [h'] using congr_arg (smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x) hf
  Â· let S := (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf
    -- Suppose that `f`, in the stalk at `x`, is nonzero at `x`
    rintro (hf : _ â‰  0)
    -- Represent `f` as the germ of some function (also called `f`) on an open neighbourhood `U` of
    -- `x`, which is nonzero at `x`
    obtain âŸ¨U : Opens M, hxU, f : C^âˆâŸ®IM, U; ğ“˜(ğ•œ), ğ•œâŸ¯, rflâŸ© := S.germ_exist x f
    have hf' : f âŸ¨x, hxUâŸ© â‰  0 := by
      convert hf
      exact (smoothSheafCommRing.eval_germ U âŸ¨x, hxUâŸ© f).symm
    -- In fact, by continuity, `f` is nonzero on a neighbourhood `V` of `x`
    have H :  âˆ€á¶  (z : U) in ğ“ âŸ¨x, hxUâŸ©, f z â‰  0 := f.2.continuous.continuousAt.eventually_ne hf'
    rw [eventually_nhds_iff] at H
    obtain âŸ¨Vâ‚€, hVâ‚€f, hVâ‚€, hxVâ‚€âŸ© := H
    let V : Opens M := âŸ¨Subtype.val '' Vâ‚€, U.2.isOpenMap_subtype_val Vâ‚€ hVâ‚€âŸ©
    have hUV : V â‰¤ U := Subtype.coe_image_subset (U : Set M) Vâ‚€
    have hV : Vâ‚€ = Set.range (Set.inclusion hUV) := by
      convert (Set.range_inclusion hUV).symm
      ext y
      show _ â†” y âˆˆ Subtype.val â»Â¹' (Subtype.val '' Vâ‚€)
      rw [Set.preimage_image_eq _ Subtype.coe_injective]
    clear_value V
    subst hV
    have hxV : x âˆˆ (V : Set M) := by
      obtain âŸ¨xâ‚€, hxxâ‚€âŸ© := hxVâ‚€
      convert xâ‚€.2
      exact congr_arg Subtype.val hxxâ‚€.symm
    have hVf : âˆ€ y : V, f (Set.inclusion hUV y) â‰  0 :=
      fun y â†¦ hVâ‚€f (Set.inclusion hUV y) (Set.mem_range_self y)
    -- Let `g` be the pointwise inverse of `f` on `V`, which is smooth since `f` is nonzero there
    let g : C^âˆâŸ®IM, V; ğ“˜(ğ•œ), ğ•œâŸ¯ := âŸ¨(f âˆ˜ Set.inclusion hUV)â»Â¹, ?_âŸ©
    -- The germ of `g` is inverse to the germ of `f`, so `f` is a unit
    Â· refine âŸ¨âŸ¨S.germ âŸ¨x, hxVâŸ© (SmoothMap.restrictRingHom IM ğ“˜(ğ•œ) ğ•œ hUV f), S.germ âŸ¨x, hxVâŸ© g,
        ?_, ?_âŸ©, S.germ_res_apply hUV.hom âŸ¨x, hxVâŸ© fâŸ©
      Â· rw [â† map_mul]
        convert map_one _
        apply Subtype.ext
        ext y
        apply mul_inv_cancel
        exact hVf y
      Â· rw [â† map_mul]
        convert map_one _
        apply Subtype.ext
        ext y
        apply inv_mul_cancel
        exact hVf y
    Â· intro y
      exact ((contDiffAt_inv _ (hVf y)).contMDiffAt).comp y
        (f.smooth.comp (smooth_inclusion hUV)).smoothAt

/-- The non-units of the stalk at `x` of the sheaf of smooth functions from `M` to `ğ•œ`, considered
as a sheaf of commutative rings, are the functions whose values at `x` are zero. -/
theorem smoothSheafCommRing.nonunits_stalk {x : M} :
    nonunits ((smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.stalk x)
    = RingHom.ker (smoothSheafCommRing.eval IM ğ“˜(ğ•œ) M ğ•œ x) := by
  ext1 f
  rw [mem_nonunits_iff, not_iff_comm, Iff.comm]
  apply smoothSheafCommRing.isUnit_stalk_iff

/-- A smooth manifold-with-corners can be considered as a locally ringed space. -/
def SmoothManifoldWithCorners.locallyRingedSpace : LocallyRingedSpace where
  carrier := TopCat.of M
  presheaf := smoothPresheafCommRing IM ğ“˜(ğ•œ) M ğ•œ
  IsSheaf := (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).cond
  localRing := by
    intro (x : M)
    show LocalRing ((smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.stalk x)
    apply LocalRing.of_nonunits_add
    rw [smoothSheafCommRing.nonunits_stalk]
    intro f g
    exact Ideal.add_mem _

variable {IM M} in
/-- Let `W` be an open set in a complex manifold `M`, and let `F` and `G` be holomorphic functions
on `W` with `F * G â‰¡ 0` on `W`. Let `x` be a point in `W`.  Then the germ of either `F` or `G` at
`x` must be zero. -/
theorem germ_zero_or_germ_zero_of_mul_eq_zero {W : Opens M}
    {F G : (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.obj (op W)}
    (H : F * G = 0) (x : W) :
    (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.germ x F = 0
    âˆ¨ (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf.germ x G = 0 :=
  sorry

/-- The stalk at `x` of the sheaf of holomorphic functions from `M` to `â„‚`, considered as a sheaf of
commutative rings, has no zero divisors.  This is the main step in showing that it is an integral
domain. -/
instance (x : M) : NoZeroDivisors ((smoothPresheafCommRing IM ğ“˜(ğ•œ, ğ•œ) M ğ•œ).stalk (x:M)) where
  eq_zero_or_eq_zero_of_mul_eq_zero := by
    let S := (smoothSheafCommRing IM ğ“˜(ğ•œ) M ğ•œ).presheaf
    intro f g hfg
    obtain âŸ¨U : Opens M, hxU, f : C^âˆâŸ®IM, U; ğ“˜(ğ•œ), ğ•œâŸ¯, rflâŸ© := S.germ_exist x f
    obtain âŸ¨V : Opens M, hxV, g : C^âˆâŸ®IM, V; ğ“˜(ğ•œ), ğ•œâŸ¯, rflâŸ© := S.germ_exist x g
    let X : â†‘(U âŠ“ V) := âŸ¨x, âŸ¨hxU, hxVâŸ©âŸ©
    have hU' : U âŠ“ V â‰¤ U := inf_le_left
    let F : C^âˆâŸ®IM, â†‘(U âŠ“ V); ğ“˜(ğ•œ), ğ•œâŸ¯ := S.map hU'.hom.op f
    have hF : S.germ X F = _ := S.germ_res_apply hU'.hom X f
    have hV' : U âŠ“ V â‰¤ V := inf_le_right
    let G : C^âˆâŸ®IM, â†‘(U âŠ“ V); ğ“˜(ğ•œ), ğ•œâŸ¯ := S.map hV'.hom.op g
    have hG : S.germ X G = _ := S.germ_res_apply hV'.hom X g
    rw [â† hF, â† hG] at hfg âŠ¢
    have hFG : S.germ X (F * G) = S.germ X 0 := by rwa [map_mul, map_zero]
    obtain âŸ¨W, hxW, i, _, hFGWâŸ© := S.germ_eq _ _ _ _ _ hFG
    have hFGW' : S.map i.op F * S.map i.op G = 0 := by rwa [map_mul, map_zero] at hFGW
    have hFGW'' := germ_zero_or_germ_zero_of_mul_eq_zero hFGW' âŸ¨x, hxWâŸ©
    rwa [S.germ_res_apply i, S.germ_res_apply i] at hFGW''

instance (x : M) : Nontrivial ((smoothPresheafCommRing IM ğ“˜(ğ•œ, ğ•œ) M ğ•œ).stalk (x:M)) :=
  smoothSheafCommRing.stalk_nontrivial ..

/-- The stalk at `x` of the sheaf of holomorphic functions from `M` to `â„‚`, considered as a sheaf of
commutative rings, is an integral domain. -/
instance (x : M) : IsDomain ((smoothPresheafCommRing IM ğ“˜(ğ•œ, ğ•œ) M ğ•œ).stalk (x:M)) :=
  NoZeroDivisors.to_isDomain ..
