/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.SmoothManifoldWithCorners

/-!
# Topological property of topological or smooth manifolds
In this file, we prove a few basic topological properties of manifolds.
Let $M$ be a topological manifold (not necessarily C^n or smooth).
* `locallyCompact_of_finiteDimensional_of_boundaryless`: If `M` is finite-dimensional, boundaryless
  and the underlying field `ğ•œ` is locally compact (such as â„, â„‚ or the p-adic numbers),
  `M` is locally compact.

**TODO:**
* adapt the argument to include manifolds with boundary; this probably requires a
stronger definition of boundary to show local compactness of the half-spaces
-/

open Set Topology

variable
  {E : Type*} {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ğ•œ E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- Let M be a topological manifold over the field ğ•œ.
  [HasGroupoid M (contDiffGroupoid 0 I)]

/-- Auxiliary lemma for local compactness of `M`. -/
lemma localCompactness_aux [LocallyCompactSpace ğ•œ] [FiniteDimensional ğ•œ E]
    (hI : ModelWithCorners.Boundaryless I) {x : M} {n : Set M} (hn : n âˆˆ ğ“ x) :
    âˆƒ s : Set M, sâˆˆ ğ“ x âˆ§ s âŠ† n âˆ§ IsCompact s  := by
  -- Assume `n` is contained in some chart at x. (Choose the distinguished chart from our atlas.)
  let chart := chartAt H x
  let echart := extChartAt I x
  have hn : n âˆ© echart.source âˆˆ ğ“ x := Filter.inter_mem hn
    (chart.extend_source_mem_nhds _ (mem_chart_source H x))

  -- Apply the chart to obtain a neighbourhood `n'` of $echart x âˆˆ E$.
  let x' := echart x
  let n' := echart '' (n âˆ© echart.source)
  have hn' : n' âˆˆ ğ“ x' := by
    let r := chart.map_extend_nhds I (mem_chart_source H x)
    rw [I.range_eq_univ, nhdsWithin_univ, â† extChartAt] at r
    exact r â–¸ Filter.image_mem_map hn
  -- Since ğ•œ is locally compact, so is E. In particular, x' has a compact neighbourhood s' âŠ† n'.
  have : ProperSpace E := FiniteDimensional.proper ğ•œ E
  have : LocallyCompactSpace E := by infer_instance
  rcases this.local_compact_nhds x' n' hn' with âŸ¨s', hs', hsn', hscompactâŸ©
  -- Transport back: s := echart â»Â¹ (s') is a compact neighbourhood of x.
  let s := echart.symm '' s'
  have hstarget : s' âŠ† echart.target := calc s'
    _ âŠ† n' := hsn'
    _ âŠ† echart '' (echart.source) := image_subset _ (inter_subset_right _ _)
    _ âŠ† echart.target := LocalEquiv.map_source'' echart
  refine âŸ¨s, ?_, ?_, ?_âŸ©
  Â· -- FIXME: (how to) avoid the additional rewrites?
    let r := chart.extend_image_mem_nhds_symm I hs' hstarget
    have : LocalHomeomorph.extend chart I = echart := rfl
    rw [this, â† image_eta, (extChartAt_to_inv I x)] at r
    apply r
  Â· calc s
      _ âŠ† echart.symm '' n' := image_subset echart.symm hsn'
      _ = (echart.symm âˆ˜ echart) '' (n âˆ© echart.source) := by rw [image_comp]
      _ = n âˆ© echart.source := by
        rw [extChartAt_source]
        apply chart.extend_left_inv' _ (inter_subset_right _ _)
      _ âŠ† n := inter_subset_left _ _
  Â· apply hscompact.image_of_continuousOn ((chart.continuousOn_extend_symm I).mono hstarget)

/-- A finite-dimensional manifold without boundary modelled on a locally compact field
  (such as â„, â„‚ or the p-adic numbers) is locally compact. -/
-- FIXME: make this an instance!
-- TODO: also allow manifolds with boundary.
lemma Manifold.locallyCompact_of_finiteDimensional_of_boundaryless
    [LocallyCompactSpace ğ•œ] [FiniteDimensional ğ•œ E] (hI : ModelWithCorners.Boundaryless I) :
    LocallyCompactSpace M := by
  exact { local_compact_nhds := fun x n hn â†¦ localCompactness_aux I hI hn }
