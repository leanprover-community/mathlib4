/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.ContMDiff.Basic

/-!
## Smoothness of charts and local structomorphisms

We show that the model with corners, charts, extended charts and their inverses are `C^n`,
and that local structomorphisms are `C^n` with `C^n` inverses.
-/

open Set ChartedSpace IsManifold
open scoped Manifold ContDiff

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  -- declare a `C^n` manifold `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ğ•œ E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {n : WithTop â„•âˆ}
  [IsManifold I n M]
  -- declare a topological space `M'`.
  {M' : Type*} [TopologicalSpace M']
  -- declare functions, sets, points and smoothness indices
  {e : PartialHomeomorph M H} {x : M}

/-! ### Atlas members are `C^n` -/

section Atlas

theorem contMDiff_model : ContMDiff I ğ“˜(ğ•œ, E) n I := by
  intro x
  refine contMDiffAt_iff.mpr âŸ¨I.continuousAt, ?_âŸ©
  simp only [mfld_simps]
  refine contDiffWithinAt_id.congr_of_eventuallyEq ?_ ?_
  Â· exact Filter.eventuallyEq_of_mem self_mem_nhdsWithin fun xâ‚‚ => I.right_inv
  simp_rw [Function.comp_apply, I.left_inv, Function.id_def]

theorem contMDiffOn_model_symm : ContMDiffOn ğ“˜(ğ•œ, E) I n I.symm (range I) := by
  rw [contMDiffOn_iff]
  refine âŸ¨I.continuousOn_symm, fun x y => ?_âŸ©
  simp only [mfld_simps]
  exact contDiffOn_id.congr fun x' => I.right_inv

/-- An atlas member is `C^n` for any `n`. -/
theorem contMDiffOn_of_mem_maximalAtlas (h : e âˆˆ maximalAtlas I n M) :
    ContMDiffOn I I n e e.source :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_of_mem_maximalAtlas
    contDiffWithinAtProp_id h

/-- The inverse of an atlas member is `C^n` for any `n`. -/
theorem contMDiffOn_symm_of_mem_maximalAtlas (h : e âˆˆ maximalAtlas I n M) :
    ContMDiffOn I I n e.symm e.target :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_symm_of_mem_maximalAtlas
      contDiffWithinAtProp_id h

theorem contMDiffAt_of_mem_maximalAtlas (h : e âˆˆ maximalAtlas I n M) (hx : x âˆˆ e.source) :
    ContMDiffAt I I n e x :=
  (contMDiffOn_of_mem_maximalAtlas h).contMDiffAt <| e.open_source.mem_nhds hx

theorem contMDiffAt_symm_of_mem_maximalAtlas {x : H} (h : e âˆˆ maximalAtlas I n M)
    (hx : x âˆˆ e.target) : ContMDiffAt I I n e.symm x :=
  (contMDiffOn_symm_of_mem_maximalAtlas h).contMDiffAt <| e.open_target.mem_nhds hx

theorem contMDiffOn_chart : ContMDiffOn I I n (chartAt H x) (chartAt H x).source :=
  contMDiffOn_of_mem_maximalAtlas <| chart_mem_maximalAtlas x

theorem contMDiffOn_chart_symm : ContMDiffOn I I n (chartAt H x).symm (chartAt H x).target :=
  contMDiffOn_symm_of_mem_maximalAtlas <| chart_mem_maximalAtlas x

theorem contMDiffAt_extend {x : M} (he : e âˆˆ maximalAtlas I n M) (hx : x âˆˆ e.source) :
    ContMDiffAt I ğ“˜(ğ•œ, E) n (e.extend I) x :=
  (contMDiff_model _).comp x <| contMDiffAt_of_mem_maximalAtlas he hx

theorem contMDiffAt_extChartAt' {x' : M} (h : x' âˆˆ (chartAt H x).source) :
    ContMDiffAt I ğ“˜(ğ•œ, E) n (extChartAt I x) x' :=
  contMDiffAt_extend (chart_mem_maximalAtlas x) h

omit [IsManifold I n M] in
theorem contMDiffAt_extChartAt : ContMDiffAt I ğ“˜(ğ•œ, E) n (extChartAt I x) x := by
  rw [contMDiffAt_iff_source]
  apply contMDiffWithinAt_id.congr_of_eventuallyEq_of_mem _ (by simp)
  filter_upwards [extChartAt_target_mem_nhdsWithin x] with y hy
  exact PartialEquiv.right_inv (extChartAt I x) hy

theorem contMDiffOn_extChartAt : ContMDiffOn I ğ“˜(ğ•œ, E) n (extChartAt I x) (chartAt H x).source :=
  fun _x' hx' => (contMDiffAt_extChartAt' hx').contMDiffWithinAt

theorem contMDiffOn_extend_symm (he : e âˆˆ maximalAtlas I n M) :
    ContMDiffOn ğ“˜(ğ•œ, E) I n (e.extend I).symm (I '' e.target) := by
  refine (contMDiffOn_symm_of_mem_maximalAtlas he).comp
    (contMDiffOn_model_symm.mono <| image_subset_range _ _) ?_
  simp_rw [image_subset_iff, PartialEquiv.restr_coe_symm, I.toPartialEquiv_coe_symm,
    preimage_preimage, I.left_inv, preimage_id']; rfl

theorem contMDiffOn_extChartAt_symm (x : M) :
    ContMDiffOn ğ“˜(ğ•œ, E) I n (extChartAt I x).symm (extChartAt I x).target := by
  convert contMDiffOn_extend_symm (chart_mem_maximalAtlas (I := I) x)
  Â· rw [extChartAt_target, I.image_eq]
  Â· infer_instance
  Â· infer_instance

theorem contMDiffWithinAt_extChartAt_symm_target
    (x : M) {y : E} (hy : y âˆˆ (extChartAt I x).target) :
    ContMDiffWithinAt ğ“˜(ğ•œ, E) I n (extChartAt I x).symm (extChartAt I x).target y :=
  contMDiffOn_extChartAt_symm x y hy

theorem contMDiffWithinAt_extChartAt_symm_range
    (x : M) {y : E} (hy : y âˆˆ (extChartAt I x).target) :
    ContMDiffWithinAt ğ“˜(ğ•œ, E) I n (extChartAt I x).symm (range I) y :=
  (contMDiffWithinAt_extChartAt_symm_target x hy).mono_of_mem_nhdsWithin
    (extChartAt_target_mem_nhdsWithin_of_mem hy)

omit [IsManifold I n M] in
theorem contMDiffWithinAt_extChartAt_symm_target_self (x : M) :
    ContMDiffWithinAt ğ“˜(ğ•œ, E) I n (extChartAt I x).symm (extChartAt I x).target
      (extChartAt I x x) := by
  rw [contMDiffWithinAt_iff_target]
  constructor
  Â· apply ContinuousAt.continuousWithinAt
    apply ContinuousAt.comp _ I.continuousAt_symm
    exact (chartAt H x).symm.continuousAt (by simp)
  Â· apply contMDiffWithinAt_id.congr_of_mem (fun y hy â†¦ ?_) (by simp)
    convert PartialEquiv.right_inv (extChartAt I x) hy
    simp

omit [IsManifold I n M] in
theorem contMDiffWithinAt_extChartAt_symm_range_self (x : M) :
    ContMDiffWithinAt ğ“˜(ğ•œ, E) I n (extChartAt I x).symm (range I) (extChartAt I x x) :=
  (contMDiffWithinAt_extChartAt_symm_target_self x).mono_of_mem_nhdsWithin
    (extChartAt_target_mem_nhdsWithin x)

/-- An element of `contDiffGroupoid n I` is `C^n`. -/
theorem contMDiffOn_of_mem_contDiffGroupoid {e' : PartialHomeomorph H H}
    (h : e' âˆˆ contDiffGroupoid n I) : ContMDiffOn I I n e' e'.source :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_of_mem_groupoid contDiffWithinAtProp_id h

end Atlas

/-! ### (local) structomorphisms are `C^n` -/

section IsLocalStructomorph

variable [ChartedSpace H M'] [IsM' : IsManifold I n M']

theorem isLocalStructomorphOn_contDiffGroupoid_iff_aux {f : PartialHomeomorph M M'}
    (hf : LiftPropOn (contDiffGroupoid n I).IsLocalStructomorphWithinAt f f.source) :
    ContMDiffOn I I n f f.source := by
  -- It suffices to show regularity near each `x`
  apply contMDiffOn_of_locally_contMDiffOn
  intro x hx
  let c := chartAt H x
  let c' := chartAt H (f x)
  obtain âŸ¨-, hxfâŸ© := hf x hx
  -- Since `f` is a local structomorph, it is locally equal to some transferred element `e` of
  -- the `contDiffGroupoid`.
  obtain
    âŸ¨e, he, he' : EqOn (c' âˆ˜ f âˆ˜ c.symm) e (c.symm â»Â¹' f.source âˆ© e.source), hex :
      c x âˆˆ e.sourceâŸ© :=
    hxf (by simp only [hx, mfld_simps])
  -- We choose a convenient set `s` in `M`.
  let s : Set M := (f.trans c').source âˆ© ((c.trans e).trans c'.symm).source
  refine âŸ¨s, (f.trans c').open_source.inter ((c.trans e).trans c'.symm).open_source, ?_, ?_âŸ©
  Â· simp only [s, mfld_simps]
    rw [â† he'] <;> simp only [c, c', hx, hex, mfld_simps]
  -- We need to show `f` is `ContMDiffOn` the domain `s âˆ© f.source`.  We show this in two
  -- steps: `f` is equal to `c'.symm âˆ˜ e âˆ˜ c` on that domain and that function is
  -- `ContMDiffOn` it.
  have Hâ‚ : ContMDiffOn I I n (c'.symm âˆ˜ e âˆ˜ c) s := by
    have hc' : ContMDiffOn I I n c'.symm _ := contMDiffOn_chart_symm
    have he'' : ContMDiffOn I I n e _ := contMDiffOn_of_mem_contDiffGroupoid he
    have hc : ContMDiffOn I I n c _ := contMDiffOn_chart
    refine (hc'.comp' (he''.comp' hc)).mono ?_
    dsimp [s, c, c']
    mfld_set_tac
  have Hâ‚‚ : EqOn f (c'.symm âˆ˜ e âˆ˜ c) s := by
    intro y hy
    simp only [s, mfld_simps] at hy
    have hyâ‚ : f y âˆˆ c'.source := by simp only [hy, mfld_simps]
    have hyâ‚‚ : y âˆˆ c.source := by simp only [hy, mfld_simps]
    have hyâ‚ƒ : c y âˆˆ c.symm â»Â¹' f.source âˆ© e.source := by simp only [hy, mfld_simps]
    calc
      f y = c'.symm (c' (f y)) := by rw [c'.left_inv hyâ‚]
      _ = c'.symm (c' (f (c.symm (c y)))) := by rw [c.left_inv hyâ‚‚]
      _ = c'.symm (e (c y)) := by rw [â† he' hyâ‚ƒ]; rfl
  refine (Hâ‚.congr Hâ‚‚).mono ?_
  mfld_set_tac

/-- Let `M` and `M'` be manifolds with the same model-with-corners, `I`.  Then `f : M â†’ M'`
is a local structomorphism for `I`, if and only if it is manifold-`C^n` on the domain of definition
in both directions. -/
theorem isLocalStructomorphOn_contDiffGroupoid_iff (f : PartialHomeomorph M M') :
    LiftPropOn (contDiffGroupoid n I).IsLocalStructomorphWithinAt f f.source â†”
      ContMDiffOn I I n f f.source âˆ§ ContMDiffOn I I n f.symm f.target := by
  constructor
  Â· intro h
    refine âŸ¨isLocalStructomorphOn_contDiffGroupoid_iff_aux h,
      isLocalStructomorphOn_contDiffGroupoid_iff_aux ?_âŸ©
    -- todo: we can generalize this part of the proof to a lemma
    intro X hX
    let x := f.symm X
    have hx : x âˆˆ f.source := f.symm.mapsTo hX
    let c := chartAt H x
    let c' := chartAt H X
    obtain âŸ¨-, hxfâŸ© := h x hx
    refine âŸ¨(f.symm.continuousAt hX).continuousWithinAt, fun h2x => ?_âŸ©
    obtain âŸ¨e, he, h2e, hef, hexâŸ© :
      âˆƒ e : PartialHomeomorph H H,
        e âˆˆ contDiffGroupoid n I âˆ§
          e.source âŠ† (c.symm â‰«â‚• f â‰«â‚• c').source âˆ§
            EqOn (c' âˆ˜ f âˆ˜ c.symm) e e.source âˆ§ c x âˆˆ e.source := by
      have h1 : c' = chartAt H (f x) := by simp only [x, c', f.right_inv hX]
      have h2 : c' âˆ˜ f âˆ˜ c.symm = â‡‘(c.symm â‰«â‚• f â‰«â‚• c') := rfl
      have hcx : c x âˆˆ c.symm â»Â¹' f.source := by simp only [c, hx, mfld_simps]
      rw [h2]
      rw [â† h1, h2, PartialHomeomorph.isLocalStructomorphWithinAt_iff'] at hxf
      Â· exact hxf hcx
      Â· dsimp [x, c]; mfld_set_tac
      Â· apply Or.inl
        simp only [c, hx, h1, mfld_simps]
    have h2X : c' X = e (c (f.symm X)) := by
      rw [â† hef hex]
      dsimp only [Function.comp_def]
      have hfX : f.symm X âˆˆ c.source := by simp only [c, x, mfld_simps]
      rw [c.left_inv hfX, f.right_inv hX]
    have h3e : EqOn (c âˆ˜ f.symm âˆ˜ c'.symm) e.symm (c'.symm â»Â¹' f.target âˆ© e.target) := by
      have h1 : EqOn (c.symm â‰«â‚• f â‰«â‚• c').symm e.symm (e.target âˆ© e.target) := by
        apply EqOn.symm
        refine e.isImage_source_target.symm_eqOn_of_inter_eq_of_eqOn ?_ ?_
        Â· rw [inter_self, inter_eq_right.mpr h2e]
        Â· rw [inter_self]; exact hef.symm
      have h2 : e.target âŠ† (c.symm â‰«â‚• f â‰«â‚• c').target := by
        intro x hx; rw [â† e.right_inv hx, â† hef (e.symm.mapsTo hx)]
        exact PartialHomeomorph.mapsTo _ (h2e <| e.symm.mapsTo hx)
      rw [inter_self] at h1
      rwa [inter_eq_right.mpr]
      refine h2.trans ?_
      mfld_set_tac
    refine âŸ¨e.symm, StructureGroupoid.symm _ he, h3e, ?_âŸ©
    rw [h2X]; exact e.mapsTo hex
  Â· -- We now show the converse: a partial homeomorphism `f : M â†’ M'` which is `C^n` in both
    -- directions is a local structomorphism.  We do this by proposing
    -- `((chart_at H x).symm.trans f).trans (chart_at H (f x))` as a candidate for a structomorphism
    -- of `H`.
    rintro âŸ¨hâ‚, hâ‚‚âŸ© x hx
    refine âŸ¨(hâ‚ x hx).continuousWithinAt, ?_âŸ©
    let c := chartAt H x
    let c' := chartAt H (f x)
    rintro (hx' : c x âˆˆ c.symm â»Â¹' f.source)
    -- propose `(c.symm.trans f).trans c'` as a candidate for a local structomorphism of `H`
    refine âŸ¨(c.symm.trans f).trans c', âŸ¨?_, ?_âŸ©, (?_ : EqOn (c' âˆ˜ f âˆ˜ c.symm) _ _), ?_âŸ©
    Â· -- regularity of the candidate local structomorphism in the forward direction
      intro y hy
      simp only [mfld_simps] at hy
      have H : ContMDiffWithinAt I I n f (f â‰«â‚• c').source ((extChartAt I x).symm y) := by
        refine (hâ‚ ((extChartAt I x).symm y) ?_).mono ?_
        Â· simp only [c, hy, mfld_simps]
        Â· mfld_set_tac
      have hy' : (extChartAt I x).symm y âˆˆ c.source := by simp only [c, hy, mfld_simps]
      have hy'' : f ((extChartAt I x).symm y) âˆˆ c'.source := by
        simp only [c, hy, mfld_simps]
      rw [contMDiffWithinAt_iff_of_mem_source hy' hy''] at H
      convert H.2.mono _
      Â· simp only [c, hy, mfld_simps]
      Â· dsimp [c, c']; mfld_set_tac
    Â· -- regularity of the candidate local structomorphism in the reverse direction
      intro y hy
      simp only [mfld_simps] at hy
      have H : ContMDiffWithinAt I I n f.symm (f.symm â‰«â‚• c).source
          ((extChartAt I (f x)).symm y) := by
        refine (hâ‚‚ ((extChartAt I (f x)).symm y) ?_).mono ?_
        Â· simp only [c', hy, mfld_simps]
        Â· mfld_set_tac
      have hy' : (extChartAt I (f x)).symm y âˆˆ c'.source := by simp only [c', hy, mfld_simps]
      have hy'' : f.symm ((extChartAt I (f x)).symm y) âˆˆ c.source := by
        simp only [c', hy, mfld_simps]
      rw [contMDiffWithinAt_iff_of_mem_source hy' hy''] at H
      convert H.2.mono _
      Â· simp only [c', hy, mfld_simps]
      Â· dsimp [c, c']; mfld_set_tac
    -- now check the candidate local structomorphism agrees with `f` where it is supposed to
    Â· simp only [mfld_simps]; apply eqOn_refl
    Â· simp only [c, c', hx', mfld_simps]

end IsLocalStructomorph
