/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn, Michael Rothgang
-/
import Mathlib.Geometry.Manifold.ContMDiff.Basic

/-!
## Smoothness of standard maps associated to the product of manifolds

This file contains results about smoothness of standard maps associated to products and sums
(disjoint unions) of smooth manifolds:
- if `f` and `g` are `C^n`, so is their point-wise product.
- the component projections from a product of manifolds are smooth.
- functions into a product (*pi type*) are `C^n` iff their components are
- if `M` and `N` are manifolds modelled over the same space, `Sum.inl` and `Sum.inr` are
`C^n`, as are `Sum.elim`, `Sum.map` and `Sum.swap`.

-/

open Set Function Filter ChartedSpace

open scoped Topology Manifold

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  -- declare a charted space `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ğ•œ E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- declare a charted space `M'` over the pair `(E', H')`.
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- declare a charted space `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ğ•œ F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  -- declare a charted space `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ğ•œ F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ğ•œ F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  -- declare a few vector spaces
  {Fâ‚ : Type*} [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  {Fâ‚‚ : Type*} [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  -- declare functions, sets, points and smoothness indices
  {f : M â†’ M'} {s : Set M} {x : M} {n : WithTop â„•âˆ}

section ProdMk

theorem ContMDiffWithinAt.prodMk {f : M â†’ M'} {g : M â†’ N'} (hf : ContMDiffWithinAt I I' n f s x)
    (hg : ContMDiffWithinAt I J' n g s x) :
    ContMDiffWithinAt I (I'.prod J') n (fun x â†¦ (f x, g x)) s x := by
  rw [contMDiffWithinAt_iff] at *
  exact âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

@[deprecated (since := "2025-03-08")]
alias ContMDiffWithinAt.prod_mk := ContMDiffWithinAt.prodMk

theorem ContMDiffWithinAt.prodMk_space {f : M â†’ E'} {g : M â†’ F'}
    (hf : ContMDiffWithinAt I ğ“˜(ğ•œ, E') n f s x) (hg : ContMDiffWithinAt I ğ“˜(ğ•œ, F') n g s x) :
    ContMDiffWithinAt I ğ“˜(ğ•œ, E' Ã— F') n (fun x â†¦ (f x, g x)) s x := by
  rw [contMDiffWithinAt_iff] at *
  exact âŸ¨hf.1.prodMk hg.1, hf.2.prodMk hg.2âŸ©

@[deprecated (since := "2025-03-08")]
alias ContMDiffWithinAt.prod_mk_space := ContMDiffWithinAt.prodMk_space

nonrec theorem ContMDiffAt.prodMk {f : M â†’ M'} {g : M â†’ N'} (hf : ContMDiffAt I I' n f x)
    (hg : ContMDiffAt I J' n g x) : ContMDiffAt I (I'.prod J') n (fun x â†¦ (f x, g x)) x :=
  hf.prodMk hg

@[deprecated (since := "2025-03-08")]
alias ContMDiffAt.prod_mk := ContMDiffAt.prodMk

nonrec theorem ContMDiffAt.prodMk_space {f : M â†’ E'} {g : M â†’ F'}
    (hf : ContMDiffAt I ğ“˜(ğ•œ, E') n f x) (hg : ContMDiffAt I ğ“˜(ğ•œ, F') n g x) :
    ContMDiffAt I ğ“˜(ğ•œ, E' Ã— F') n (fun x â†¦ (f x, g x)) x :=
  hf.prodMk_space hg

@[deprecated (since := "2025-03-08")]
alias ContMDiffAt.prod_mk_space := ContMDiffAt.prodMk_space

theorem ContMDiffOn.prodMk {f : M â†’ M'} {g : M â†’ N'} (hf : ContMDiffOn I I' n f s)
    (hg : ContMDiffOn I J' n g s) : ContMDiffOn I (I'.prod J') n (fun x â†¦ (f x, g x)) s :=
  fun x hx â†¦ (hf x hx).prodMk (hg x hx)

@[deprecated (since := "2025-03-08")]
alias ContMDiffOn.prod_mk := ContMDiffOn.prodMk

theorem ContMDiffOn.prodMk_space {f : M â†’ E'} {g : M â†’ F'} (hf : ContMDiffOn I ğ“˜(ğ•œ, E') n f s)
    (hg : ContMDiffOn I ğ“˜(ğ•œ, F') n g s) : ContMDiffOn I ğ“˜(ğ•œ, E' Ã— F') n (fun x â†¦ (f x, g x)) s :=
  fun x hx â†¦ (hf x hx).prodMk_space (hg x hx)

@[deprecated (since := "2025-03-08")]
alias ContMDiffOn.prod_mk_space := ContMDiffOn.prodMk_space

nonrec theorem ContMDiff.prodMk {f : M â†’ M'} {g : M â†’ N'} (hf : ContMDiff I I' n f)
    (hg : ContMDiff I J' n g) : ContMDiff I (I'.prod J') n fun x â†¦ (f x, g x) := fun x â†¦
  (hf x).prodMk (hg x)

@[deprecated (since := "2025-03-08")]
alias ContMDiff.prod_mk := ContMDiff.prodMk

theorem ContMDiff.prodMk_space {f : M â†’ E'} {g : M â†’ F'} (hf : ContMDiff I ğ“˜(ğ•œ, E') n f)
    (hg : ContMDiff I ğ“˜(ğ•œ, F') n g) : ContMDiff I ğ“˜(ğ•œ, E' Ã— F') n fun x â†¦ (f x, g x) := fun x â†¦
  (hf x).prodMk_space (hg x)

@[deprecated (since := "2025-03-08")]
alias ContMDiff.prod_mk_space := ContMDiff.prodMk_space

end ProdMk

section Projections

theorem contMDiffWithinAt_fst {s : Set (M Ã— N)} {p : M Ã— N} :
    ContMDiffWithinAt (I.prod J) I n Prod.fst s p := by
  /- porting note: `simp` fails to apply lemmas to `ModelProd`. Was
  rw [contMDiffWithinAt_iff']
  refine' âŸ¨continuousWithinAt_fst, _âŸ©
  refine' contDiffWithinAt_fst.congr (fun y hy â†¦ _) _
  Â· simp only [mfld_simps] at hy
    simp only [hy, mfld_simps]
  Â· simp only [mfld_simps]
  -/
  rw [contMDiffWithinAt_iff']
  refine âŸ¨continuousWithinAt_fst, contDiffWithinAt_fst.congr (fun y hy â†¦ ?_) ?_âŸ©
  Â· exact (extChartAt I p.1).right_inv âŸ¨hy.1.1.1, hy.1.2.1âŸ©
  Â· exact (extChartAt I p.1).right_inv <| (extChartAt I p.1).map_source (mem_extChartAt_source _)

theorem ContMDiffWithinAt.fst {f : N â†’ M Ã— M'} {s : Set N} {x : N}
    (hf : ContMDiffWithinAt J (I.prod I') n f s x) :
    ContMDiffWithinAt J I n (fun x â†¦ (f x).1) s x :=
  contMDiffWithinAt_fst.comp x hf (mapsTo_image f s)

theorem contMDiffAt_fst {p : M Ã— N} : ContMDiffAt (I.prod J) I n Prod.fst p :=
  contMDiffWithinAt_fst

theorem contMDiffOn_fst {s : Set (M Ã— N)} : ContMDiffOn (I.prod J) I n Prod.fst s := fun _ _ â†¦
  contMDiffWithinAt_fst

theorem contMDiff_fst : ContMDiff (I.prod J) I n (@Prod.fst M N) := fun _ â†¦ contMDiffAt_fst

theorem ContMDiffAt.fst {f : N â†’ M Ã— M'} {x : N} (hf : ContMDiffAt J (I.prod I') n f x) :
    ContMDiffAt J I n (fun x â†¦ (f x).1) x :=
  contMDiffAt_fst.comp x hf

theorem ContMDiff.fst {f : N â†’ M Ã— M'} (hf : ContMDiff J (I.prod I') n f) :
    ContMDiff J I n fun x â†¦ (f x).1 :=
  contMDiff_fst.comp hf

theorem contMDiffWithinAt_snd {s : Set (M Ã— N)} {p : M Ã— N} :
    ContMDiffWithinAt (I.prod J) J n Prod.snd s p := by
  /- porting note: `simp` fails to apply lemmas to `ModelProd`. Was
  rw [contMDiffWithinAt_iff']
  refine' âŸ¨continuousWithinAt_snd, _âŸ©
  refine' contDiffWithinAt_snd.congr (fun y hy â†¦ _) _
  Â· simp only [mfld_simps] at hy
    simp only [hy, mfld_simps]
  Â· simp only [mfld_simps]
  -/
  rw [contMDiffWithinAt_iff']
  refine âŸ¨continuousWithinAt_snd, contDiffWithinAt_snd.congr (fun y hy â†¦ ?_) ?_âŸ©
  Â· exact (extChartAt J p.2).right_inv âŸ¨hy.1.1.2, hy.1.2.2âŸ©
  Â· exact (extChartAt J p.2).right_inv <| (extChartAt J p.2).map_source (mem_extChartAt_source _)

theorem ContMDiffWithinAt.snd {f : N â†’ M Ã— M'} {s : Set N} {x : N}
    (hf : ContMDiffWithinAt J (I.prod I') n f s x) :
    ContMDiffWithinAt J I' n (fun x â†¦ (f x).2) s x :=
  contMDiffWithinAt_snd.comp x hf (mapsTo_image f s)

theorem contMDiffAt_snd {p : M Ã— N} : ContMDiffAt (I.prod J) J n Prod.snd p :=
  contMDiffWithinAt_snd

theorem contMDiffOn_snd {s : Set (M Ã— N)} : ContMDiffOn (I.prod J) J n Prod.snd s := fun _ _ â†¦
  contMDiffWithinAt_snd

theorem contMDiff_snd : ContMDiff (I.prod J) J n (@Prod.snd M N) := fun _ â†¦ contMDiffAt_snd

theorem ContMDiffAt.snd {f : N â†’ M Ã— M'} {x : N} (hf : ContMDiffAt J (I.prod I') n f x) :
    ContMDiffAt J I' n (fun x â†¦ (f x).2) x :=
  contMDiffAt_snd.comp x hf

theorem ContMDiff.snd {f : N â†’ M Ã— M'} (hf : ContMDiff J (I.prod I') n f) :
    ContMDiff J I' n fun x â†¦ (f x).2 :=
  contMDiff_snd.comp hf

end Projections

theorem contMDiffWithinAt_prod_iff (f : M â†’ M' Ã— N') :
    ContMDiffWithinAt I (I'.prod J') n f s x â†”
      ContMDiffWithinAt I I' n (Prod.fst âˆ˜ f) s x âˆ§ ContMDiffWithinAt I J' n (Prod.snd âˆ˜ f) s x :=
  âŸ¨fun h â†¦ âŸ¨h.fst, h.sndâŸ©, fun h â†¦ h.1.prodMk h.2âŸ©

theorem contMDiffWithinAt_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    ContMDiffWithinAt I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) n f s x â†”
      ContMDiffWithinAt I ğ“˜(ğ•œ, Fâ‚) n (Prod.fst âˆ˜ f) s x âˆ§
      ContMDiffWithinAt I ğ“˜(ğ•œ, Fâ‚‚) n (Prod.snd âˆ˜ f) s x := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact contMDiffWithinAt_prod_iff f

theorem contMDiffAt_prod_iff (f : M â†’ M' Ã— N') :
    ContMDiffAt I (I'.prod J') n f x â†”
      ContMDiffAt I I' n (Prod.fst âˆ˜ f) x âˆ§ ContMDiffAt I J' n (Prod.snd âˆ˜ f) x := by
  simp_rw [â† contMDiffWithinAt_univ]; exact contMDiffWithinAt_prod_iff f

theorem contMDiffAt_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    ContMDiffAt I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) n f x â†”
      ContMDiffAt I ğ“˜(ğ•œ, Fâ‚) n (Prod.fst âˆ˜ f) x âˆ§ ContMDiffAt I ğ“˜(ğ•œ, Fâ‚‚) n (Prod.snd âˆ˜ f) x := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact contMDiffAt_prod_iff f

theorem contMDiffOn_prod_iff (f : M â†’ M' Ã— N') :
    ContMDiffOn I (I'.prod J') n f s â†”
      ContMDiffOn I I' n (Prod.fst âˆ˜ f) s âˆ§ ContMDiffOn I J' n (Prod.snd âˆ˜ f) s :=
  âŸ¨fun h â†¦ âŸ¨fun x hx â†¦ ((contMDiffWithinAt_prod_iff f).1 (h x hx)).1,
      fun x hx â†¦ ((contMDiffWithinAt_prod_iff f).1 (h x hx)).2âŸ© ,
    fun h x hx â†¦ (contMDiffWithinAt_prod_iff f).2 âŸ¨h.1 x hx, h.2 x hxâŸ©âŸ©

theorem contMDiffOn_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    ContMDiffOn I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) n f s â†”
      ContMDiffOn I ğ“˜(ğ•œ, Fâ‚) n (Prod.fst âˆ˜ f) s âˆ§ ContMDiffOn I ğ“˜(ğ•œ, Fâ‚‚) n (Prod.snd âˆ˜ f) s := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact contMDiffOn_prod_iff f

theorem contMDiff_prod_iff (f : M â†’ M' Ã— N') :
    ContMDiff I (I'.prod J') n f â†”
      ContMDiff I I' n (Prod.fst âˆ˜ f) âˆ§ ContMDiff I J' n (Prod.snd âˆ˜ f) :=
  âŸ¨fun h â†¦ âŸ¨h.fst, h.sndâŸ©, fun h â†¦ by convert h.1.prodMk h.2âŸ©

theorem contMDiff_prod_module_iff (f : M â†’ Fâ‚ Ã— Fâ‚‚) :
    ContMDiff I ğ“˜(ğ•œ, Fâ‚ Ã— Fâ‚‚) n f â†”
      ContMDiff I ğ“˜(ğ•œ, Fâ‚) n (Prod.fst âˆ˜ f) âˆ§ ContMDiff I ğ“˜(ğ•œ, Fâ‚‚) n (Prod.snd âˆ˜ f) := by
  rw [modelWithCornersSelf_prod, â† chartedSpaceSelf_prod]
  exact contMDiff_prod_iff f

theorem contMDiff_prod_assoc :
    ContMDiff ((I.prod I').prod J) (I.prod (I'.prod J)) n
      fun x : (M Ã— M') Ã— N â†¦ (x.1.1, x.1.2, x.2) :=
  contMDiff_fst.fst.prodMk <| contMDiff_fst.snd.prodMk contMDiff_snd

section prodMap

variable {g : N â†’ N'} {r : Set N} {y : N}

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
theorem ContMDiffWithinAt.prodMap' {p : M Ã— N} (hf : ContMDiffWithinAt I I' n f s p.1)
    (hg : ContMDiffWithinAt J J' n g r p.2) :
    ContMDiffWithinAt (I.prod J) (I'.prod J') n (Prod.map f g) (s Ã—Ë¢ r) p :=
  (hf.comp p contMDiffWithinAt_fst mapsTo_fst_prod).prodMk <|
    hg.comp p contMDiffWithinAt_snd mapsTo_snd_prod

@[deprecated (since := "2025-03-08")]
alias ContMDiffWithinAt.prod_map' := ContMDiffWithinAt.prodMap'

theorem ContMDiffWithinAt.prodMap (hf : ContMDiffWithinAt I I' n f s x)
    (hg : ContMDiffWithinAt J J' n g r y) :
    ContMDiffWithinAt (I.prod J) (I'.prod J') n (Prod.map f g) (s Ã—Ë¢ r) (x, y) :=
  ContMDiffWithinAt.prodMap' hf hg

@[deprecated (since := "2025-03-08")]
alias ContMDiffWithinAt.prod_map := ContMDiffWithinAt.prodMap

theorem ContMDiffAt.prodMap (hf : ContMDiffAt I I' n f x) (hg : ContMDiffAt J J' n g y) :
    ContMDiffAt (I.prod J) (I'.prod J') n (Prod.map f g) (x, y) := by
  simp only [â† contMDiffWithinAt_univ, â† univ_prod_univ] at *
  exact hf.prodMap hg

@[deprecated (since := "2025-03-08")]
alias ContMDiffAt.prod_map := ContMDiffAt.prodMap

theorem ContMDiffAt.prodMap' {p : M Ã— N} (hf : ContMDiffAt I I' n f p.1)
    (hg : ContMDiffAt J J' n g p.2) : ContMDiffAt (I.prod J) (I'.prod J') n (Prod.map f g) p :=
  hf.prodMap hg

@[deprecated (since := "2025-03-08")]
alias ContMDiffAt.prod_map' := ContMDiffAt.prodMap'

theorem ContMDiffOn.prodMap (hf : ContMDiffOn I I' n f s) (hg : ContMDiffOn J J' n g r) :
    ContMDiffOn (I.prod J) (I'.prod J') n (Prod.map f g) (s Ã—Ë¢ r) :=
  (hf.comp contMDiffOn_fst mapsTo_fst_prod).prodMk <| hg.comp contMDiffOn_snd mapsTo_snd_prod

@[deprecated (since := "2025-03-08")]
alias ContMDiffOn.prod_map := ContMDiffOn.prodMap

theorem ContMDiff.prodMap (hf : ContMDiff I I' n f) (hg : ContMDiff J J' n g) :
    ContMDiff (I.prod J) (I'.prod J') n (Prod.map f g) := by
  intro p
  exact (hf p.1).prodMap' (hg p.2)

@[deprecated (since := "2025-03-08")]
alias ContMDiff.prod_map := ContMDiff.prodMap

end prodMap

section PiSpace

/-!
### Regularity of functions with codomain `Î  i, F i`

We have no `ModelWithCorners.pi` yet, so we prove lemmas about functions `f : M â†’ Î  i, F i` and
use `ğ“˜(ğ•œ, Î  i, F i)` as the model space.
-/


variable {Î¹ : Type*} [Fintype Î¹] {Fi : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (Fi i)]
  [âˆ€ i, NormedSpace ğ•œ (Fi i)] {Ï† : M â†’ âˆ€ i, Fi i}

theorem contMDiffWithinAt_pi_space :
    ContMDiffWithinAt I ğ“˜(ğ•œ, âˆ€ i, Fi i) n Ï† s x â†”
      âˆ€ i, ContMDiffWithinAt I ğ“˜(ğ•œ, Fi i) n (fun x â†¦ Ï† x i) s x := by
  simp only [contMDiffWithinAt_iff, continuousWithinAt_pi, contDiffWithinAt_pi, forall_and,
    extChartAt_model_space_eq_id, Function.comp_def, PartialEquiv.refl_coe, id]

theorem contMDiffOn_pi_space :
    ContMDiffOn I ğ“˜(ğ•œ, âˆ€ i, Fi i) n Ï† s â†” âˆ€ i, ContMDiffOn I ğ“˜(ğ•œ, Fi i) n (fun x â†¦ Ï† x i) s :=
  âŸ¨fun h i x hx â†¦ contMDiffWithinAt_pi_space.1 (h x hx) i, fun h x hx â†¦
    contMDiffWithinAt_pi_space.2 fun i â†¦ h i x hxâŸ©

theorem contMDiffAt_pi_space :
    ContMDiffAt I ğ“˜(ğ•œ, âˆ€ i, Fi i) n Ï† x â†” âˆ€ i, ContMDiffAt I ğ“˜(ğ•œ, Fi i) n (fun x â†¦ Ï† x i) x :=
  contMDiffWithinAt_pi_space

theorem contMDiff_pi_space :
    ContMDiff I ğ“˜(ğ•œ, âˆ€ i, Fi i) n Ï† â†” âˆ€ i, ContMDiff I ğ“˜(ğ•œ, Fi i) n fun x â†¦ Ï† x i :=
  âŸ¨fun h i x â†¦ contMDiffAt_pi_space.1 (h x) i, fun h x â†¦ contMDiffAt_pi_space.2 fun i â†¦ h i xâŸ©

end PiSpace

section disjointUnion

variable {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M'] {n : WithTop â„•âˆ}
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*} [TopologicalSpace H']
  {J : Type*} {J : ModelWithCorners ğ•œ E' H'}
  {N N' : Type*} [TopologicalSpace N] [TopologicalSpace N'] [ChartedSpace H' N] [ChartedSpace H' N']

open Topology

lemma ContMDiff.inl : ContMDiff I I n (@Sum.inl M M') := by
  intro x
  rw [contMDiffAt_iff]
  refine âŸ¨continuous_inl.continuousAt, ?_âŸ©
  -- In extended charts, .inl equals the identity (on the chart sources).
  apply contDiffWithinAt_id.congr_of_eventuallyEq; swap
  Â· simp [ChartedSpace.sum_chartAt_inl]
    congr
    apply Sum.inl_injective.extend_apply (chartAt _ x)
  set C := chartAt H x with hC
  have : I.symm â»Â¹' C.target âˆ© range I âˆˆ ğ“[range I] (extChartAt I x) x := by
    rw [â† I.image_eq (chartAt H x).target]
    exact (chartAt H x).extend_image_target_mem_nhds (mem_chart_source _ x)
  filter_upwards [this] with y hy
  simp [extChartAt, sum_chartAt_inl, â† hC, Sum.inl_injective.extend_apply C, C.right_inv hy.1,
    I.right_inv hy.2]

lemma ContMDiff.inr : ContMDiff I I n (@Sum.inr M M') := by
  intro x
  rw [contMDiffAt_iff]
  refine âŸ¨continuous_inr.continuousAt, ?_âŸ©
  -- In extended charts, .inl equals the identity (on the chart sources).
  apply contDiffWithinAt_id.congr_of_eventuallyEq; swap
  Â· simp only [mfld_simps, sum_chartAt_inr]
    congr
    apply Sum.inr_injective.extend_apply (chartAt _ x)
  set C := chartAt H x with hC
  have : I.symm â»Â¹' (chartAt H x).target âˆ© range I âˆˆ ğ“[range I] (extChartAt I x) x := by
    rw [â† I.image_eq (chartAt H x).target]
    exact (chartAt H x).extend_image_target_mem_nhds (mem_chart_source _ x)
  filter_upwards [this] with y hy
  simp [extChartAt, sum_chartAt_inr, â† hC, Sum.inr_injective.extend_apply C, C.right_inv hy.1,
    I.right_inv hy.2]

lemma extChartAt_inl_apply {x y : M} :
    (extChartAt I (.inl x : M âŠ• M')) (Sum.inl y) = (extChartAt I x) y := by simp

lemma extChartAt_inr_apply {x y : M'} :
    (extChartAt I (.inr x : M âŠ• M')) (Sum.inr y) = (extChartAt I x) y := by simp

lemma ContMDiff.sumElim {f : M â†’ N} {g : M' â†’ N}
    (hf : ContMDiff I J n f) (hg : ContMDiff I J n g) : ContMDiff I J n (Sum.elim f g) := by
  intro p
  rw [contMDiffAt_iff]
  refine âŸ¨(Continuous.sumElim hf.continuous hg.continuous).continuousAt, ?_âŸ©
  cases p with
  | inl x =>
    -- In charts around x : M, the map f âŠ” g looks like f.
    -- This is how they both look like in extended charts.
    have : ContDiffWithinAt ğ•œ n ((extChartAt J (f x)) âˆ˜ f âˆ˜ (extChartAt I x).symm)
        (range I) ((extChartAt I (.inl x : M âŠ• M')) (Sum.inl x)) := by
      let hf' := hf x
      rw [contMDiffAt_iff] at hf'
      simpa using hf'.2
    apply this.congr_of_eventuallyEq
    Â· simp only [extChartAt, Sum.elim_inl, ChartedSpace.sum_chartAt_inl]
      filter_upwards with a
      congr
    Â· -- They agree at the image of x.
      simp only [extChartAt, ChartedSpace.sum_chartAt_inl, Sum.elim_inl]
      congr
  | inr x =>
    -- In charts around x : M, the map f âŠ” g looks like g.
    -- This is how they both look like in extended charts.
    have : ContDiffWithinAt ğ•œ n ((extChartAt J (g x)) âˆ˜ g âˆ˜ (extChartAt I x).symm)
        (range I) ((extChartAt I (.inr x : M âŠ• M')) (Sum.inr x)) := by
      let hg' := hg x
      rw [contMDiffAt_iff] at hg'
      simpa using hg'.2
    apply this.congr_of_eventuallyEq
    Â· simp only [extChartAt, Sum.elim_inr, ChartedSpace.sum_chartAt_inr]
      filter_upwards with a
      congr
    Â· -- They agree at the image of x.
      simp only [extChartAt, ChartedSpace.sum_chartAt_inr, Sum.elim_inr]
      congr

@[deprecated (since := "2025-02-20")] alias ContMDiff.sum_elim := ContMDiff.sumElim

lemma ContMDiff.sumMap {f : M â†’ N} {g : M' â†’ N'}
    (hf : ContMDiff I J n f) (hg : ContMDiff I J n g) : ContMDiff I J n (Sum.map f g) :=
  ContMDiff.sumElim (ContMDiff.inl.comp hf) (ContMDiff.inr.comp hg)

@[deprecated (since := "2025-02-20")] alias ContMDiff.sum_map := ContMDiff.sumMap

lemma contMDiff_of_contMDiff_inl {f : M â†’ N}
    (h : ContMDiff I J n ((@Sum.inl N N') âˆ˜ f)) : ContMDiff I J n f := by
  nontriviality N
  inhabit N
  let aux : N âŠ• N' â†’ N := Sum.elim (@id N) (fun _ â†¦ inhabited_h.default)
  have : aux âˆ˜ (@Sum.inl N N') âˆ˜ f = f := by ext; simp [aux]
  rw [â† this]
  rw [â† contMDiffOn_univ] at h âŠ¢
  apply (contMDiff_id.sumElim contMDiff_const).contMDiffOn (s := @Sum.inl N N' '' univ).comp h
  intro x _hx
  rw [mem_preimage, Function.comp_apply]
  use f x, trivial

lemma contMDiff_of_contMDiff_inr {g : M' â†’ N'}
    (h : ContMDiff I J n ((@Sum.inr N N') âˆ˜ g)) : ContMDiff I J n g := by
  nontriviality N'
  inhabit N'
  let aux : N âŠ• N' â†’ N' := Sum.elim (fun _ â†¦ inhabited_h.default) (@id N')
  have : aux âˆ˜ (@Sum.inr N N') âˆ˜ g = g := by ext; simp [aux]
  rw [â† this]
  rw [â† contMDiffOn_univ] at h âŠ¢
  apply ((contMDiff_const.sumElim contMDiff_id).contMDiffOn (s := Sum.inr '' univ)).comp h
  intro x _hx
  rw [mem_preimage, Function.comp_apply]
  use g x, trivial

lemma contMDiff_sum_map {f : M â†’ N} {g : M' â†’ N'} :
    ContMDiff I J n (Sum.map f g) â†” ContMDiff I J n f âˆ§ ContMDiff I J n g :=
  âŸ¨fun h â†¦ âŸ¨contMDiff_of_contMDiff_inl (h.comp ContMDiff.inl),
    contMDiff_of_contMDiff_inr (h.comp ContMDiff.inr)âŸ©,
   fun h â†¦ ContMDiff.sumMap h.1 h.2âŸ©

lemma ContMDiff.swap : ContMDiff I I n (@Sum.swap M M') := ContMDiff.sumElim inr inl

end disjointUnion
