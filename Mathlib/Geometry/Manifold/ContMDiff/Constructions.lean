/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn, Michael Rothgang
-/
module

public import Mathlib.Geometry.Manifold.ContMDiff.Basic
import Mathlib.Geometry.Manifold.Notation

/-!
## Smoothness of standard maps associated to the product of manifolds

This file contains results about smoothness of standard maps associated to products and sums
(disjoint unions) of smooth manifolds:
- if `f` and `g` are `C^n`, so is their point-wise product.
- the component projections from a product of manifolds are smooth.
- functions into a product (*pi type*) are `C^n` iff their components are
- if `M` and `N` are manifolds modelled over the same space, `Sum.inl` and `Sum.inr` are
`C^n`, as are `Sum.elim`, `Sum.map` and `Sum.swap`.

-/

public section

open Set Function Filter ChartedSpace

open scoped Topology Manifold

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  -- declare a charted space `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ùïú E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- declare a charted space `M'` over the pair `(E', H')`.
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- declare a charted space `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ùïú F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ùïú F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  -- declare a charted space `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ùïú F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ùïú F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  -- declare a few vector spaces
  {F‚ÇÅ : Type*} [NormedAddCommGroup F‚ÇÅ] [NormedSpace ùïú F‚ÇÅ]
  {F‚ÇÇ : Type*} [NormedAddCommGroup F‚ÇÇ] [NormedSpace ùïú F‚ÇÇ]
  -- declare functions, sets, points and smoothness indices
  {f : M ‚Üí M'} {s : Set M} {x : M} {n : WithTop ‚Ñï‚àû}

section ProdMk

theorem ContMDiffWithinAt.prodMk {f : M ‚Üí M'} {g : M ‚Üí N'} (hf : CMDiffAt[s] n f x)
    (hg : CMDiffAt[s] n g x) : CMDiffAt[s] n (fun x => (f x, g x)) x := by
  rw [contMDiffWithinAt_iff] at *
  exact ‚ü®hf.1.prodMk hg.1, hf.2.prodMk hg.2‚ü©

theorem ContMDiffWithinAt.prodMk_space {f : M ‚Üí E'} {g : M ‚Üí F'}
    (hf : CMDiffAt[s] n f x) (hg : CMDiffAt[s] n g x) :
    ContMDiffWithinAt I ùìò(ùïú, E' √ó F') n (fun x => (f x, g x)) s x := by
  rw [contMDiffWithinAt_iff] at *
  exact ‚ü®hf.1.prodMk hg.1, hf.2.prodMk hg.2‚ü©

nonrec theorem ContMDiffAt.prodMk {f : M ‚Üí M'} {g : M ‚Üí N'} (hf : CMDiffAt n f x)
    (hg : CMDiffAt n g x) : CMDiffAt n (fun x => (f x, g x)) x :=
  hf.prodMk hg

nonrec theorem ContMDiffAt.prodMk_space {f : M ‚Üí E'} {g : M ‚Üí F'}
    (hf : CMDiffAt n f x) (hg : CMDiffAt n g x) :
    ContMDiffAt I ùìò(ùïú, E' √ó F') n (fun x => (f x, g x)) x :=
  hf.prodMk_space hg

theorem ContMDiffOn.prodMk {f : M ‚Üí M'} {g : M ‚Üí N'} (hf : CMDiff[s] n f)
    (hg : CMDiff[s] n g) : CMDiff[s] n (fun x => (f x, g x)) :=
  fun x hx => (hf x hx).prodMk (hg x hx)

theorem ContMDiffOn.prodMk_space {f : M ‚Üí E'} {g : M ‚Üí F'} (hf : CMDiff[s] n f)
    (hg : CMDiff[s] n g) : ContMDiffOn I ùìò(ùïú, E' √ó F') n (fun x => (f x, g x)) s :=
  fun x hx => (hf x hx).prodMk_space (hg x hx)

nonrec theorem ContMDiff.prodMk {f : M ‚Üí M'} {g : M ‚Üí N'} (hf : CMDiff n f)
    (hg : CMDiff n g) : CMDiff n fun x => (f x, g x) := fun x =>
  (hf x).prodMk (hg x)

theorem ContMDiff.prodMk_space {f : M ‚Üí E'} {g : M ‚Üí F'} (hf : CMDiff n f) (hg : CMDiff n g) :
    ContMDiff I ùìò(ùïú, E' √ó F') n fun x => (f x, g x) := fun x =>
  (hf x).prodMk_space (hg x)

end ProdMk

section Projections

theorem contMDiffWithinAt_fst {s : Set (M √ó N)} {p : M √ó N} : CMDiffAt[s] n (@Prod.fst M N) p := by
  /- porting note: `simp` fails to apply lemmas to `ModelProd`. Was
  rw [contMDiffWithinAt_iff']
  refine' ‚ü®continuousWithinAt_fst, _‚ü©
  refine' contDiffWithinAt_fst.congr (fun y hy => _) _
  ¬∑ simp only [mfld_simps] at hy
    simp only [hy, mfld_simps]
  ¬∑ simp only [mfld_simps]
  -/
  rw [contMDiffWithinAt_iff']
  refine ‚ü®continuousWithinAt_fst, contDiffWithinAt_fst.congr (fun y hy => ?_) ?_‚ü©
  ¬∑ exact (extChartAt I p.1).right_inv ‚ü®hy.1.1.1, hy.1.2.1‚ü©
  ¬∑ exact (extChartAt I p.1).right_inv <| (extChartAt I p.1).map_source (mem_extChartAt_source _)

theorem ContMDiffWithinAt.fst {f : N ‚Üí M √ó M'} {s : Set N} {x : N}
    (hf : CMDiffAt[s] n f x) : CMDiffAt[s] n (fun x => (f x).1) x :=
  contMDiffWithinAt_fst.comp x hf (mapsTo_image f s)

theorem contMDiffAt_fst {p : M √ó N} : CMDiffAt n (@Prod.fst M N) p :=
  contMDiffWithinAt_fst

theorem contMDiffOn_fst {s : Set (M √ó N)} : CMDiff[s] n (@Prod.fst M N) := fun _ _ =>
  contMDiffWithinAt_fst

theorem contMDiff_fst : CMDiff n (@Prod.fst M N) := fun _ => contMDiffAt_fst

theorem ContMDiffAt.fst {f : N ‚Üí M √ó M'} {x : N} (hf : CMDiffAt n f x) :
    CMDiffAt n (fun x => (f x).1) x :=
  contMDiffAt_fst.comp x hf

theorem ContMDiff.fst {f : N ‚Üí M √ó M'} (hf : CMDiff n f) : CMDiff n fun x => (f x).1 :=
  contMDiff_fst.comp hf

theorem contMDiffWithinAt_snd {s : Set (M √ó N)} {p : M √ó N} :
    CMDiffAt[s] n (@Prod.snd M N) p := by
  /- porting note: `simp` fails to apply lemmas to `ModelProd`. Was
  rw [contMDiffWithinAt_iff']
  refine' ‚ü®continuousWithinAt_snd, _‚ü©
  refine' contDiffWithinAt_snd.congr (fun y hy => _) _
  ¬∑ simp only [mfld_simps] at hy
    simp only [hy, mfld_simps]
  ¬∑ simp only [mfld_simps]
  -/
  rw [contMDiffWithinAt_iff']
  refine ‚ü®continuousWithinAt_snd, contDiffWithinAt_snd.congr (fun y hy => ?_) ?_‚ü©
  ¬∑ exact (extChartAt J p.2).right_inv ‚ü®hy.1.1.2, hy.1.2.2‚ü©
  ¬∑ exact (extChartAt J p.2).right_inv <| (extChartAt J p.2).map_source (mem_extChartAt_source _)

theorem ContMDiffWithinAt.snd {f : N ‚Üí M √ó M'} {s : Set N} {x : N}
    (hf : CMDiffAt[s] n f x) : CMDiffAt[s] n (fun x => (f x).2) x :=
  contMDiffWithinAt_snd.comp x hf (mapsTo_image f s)

theorem contMDiffAt_snd {p : M √ó N} : CMDiffAt n (@Prod.snd M N) p :=
  contMDiffWithinAt_snd

theorem contMDiffOn_snd {s : Set (M √ó N)} : CMDiff[s] n (@Prod.snd M N) := fun _ _ =>
  contMDiffWithinAt_snd

theorem contMDiff_snd : CMDiff n (@Prod.snd M N) := fun _ => contMDiffAt_snd

theorem ContMDiffAt.snd {f : N ‚Üí M √ó M'} {x : N} (hf : CMDiffAt n f x) :
    CMDiffAt n (fun x => (f x).2) x :=
  contMDiffAt_snd.comp x hf

theorem ContMDiff.snd {f : N ‚Üí M √ó M'} (hf : CMDiff n f) : CMDiff n fun x => (f x).2 :=
  contMDiff_snd.comp hf

end Projections

theorem contMDiffWithinAt_prod_iff (f : M ‚Üí M' √ó N') :
    CMDiffAt[s] n f x ‚Üî
      CMDiffAt[s] n (Prod.fst ‚àò f) x ‚àß CMDiffAt[s] n (Prod.snd ‚àò f) x :=
  ‚ü®fun h => ‚ü®h.fst, h.snd‚ü©, fun h => h.1.prodMk h.2‚ü©

theorem contMDiffWithinAt_prod_module_iff (f : M ‚Üí F‚ÇÅ √ó F‚ÇÇ) :
    ContMDiffWithinAt I ùìò(ùïú, F‚ÇÅ √ó F‚ÇÇ) n f s x ‚Üî
      CMDiffAt[s] n (Prod.fst ‚àò f) x ‚àß CMDiffAt[s] n (Prod.snd ‚àò f) x := by
  rw [modelWithCornersSelf_prod, ‚Üê chartedSpaceSelf_prod]
  exact contMDiffWithinAt_prod_iff f

theorem contMDiffAt_prod_iff (f : M ‚Üí M' √ó N') :
    CMDiffAt n f x ‚Üî
      CMDiffAt n (Prod.fst ‚àò f) x ‚àß CMDiffAt n (Prod.snd ‚àò f) x := by
  simp_rw [‚Üê contMDiffWithinAt_univ]; exact contMDiffWithinAt_prod_iff f

theorem contMDiffAt_prod_module_iff (f : M ‚Üí F‚ÇÅ √ó F‚ÇÇ) :
    ContMDiffAt I ùìò(ùïú, F‚ÇÅ √ó F‚ÇÇ) n f x ‚Üî
      CMDiffAt n (Prod.fst ‚àò f) x ‚àß CMDiffAt n (Prod.snd ‚àò f) x := by
  rw [modelWithCornersSelf_prod, ‚Üê chartedSpaceSelf_prod]
  exact contMDiffAt_prod_iff f

theorem contMDiffOn_prod_iff (f : M ‚Üí M' √ó N') :
    CMDiff[s] n f ‚Üî
      CMDiff[s] n (Prod.fst ‚àò f) ‚àß CMDiff[s] n (Prod.snd ‚àò f) :=
  ‚ü®fun h ‚Ü¶ ‚ü®fun x hx ‚Ü¶ ((contMDiffWithinAt_prod_iff f).1 (h x hx)).1,
      fun x hx ‚Ü¶ ((contMDiffWithinAt_prod_iff f).1 (h x hx)).2‚ü©,
    fun h x hx ‚Ü¶ (contMDiffWithinAt_prod_iff f).2 ‚ü®h.1 x hx, h.2 x hx‚ü©‚ü©

theorem contMDiffOn_prod_module_iff (f : M ‚Üí F‚ÇÅ √ó F‚ÇÇ) :
    ContMDiffOn I ùìò(ùïú, F‚ÇÅ √ó F‚ÇÇ) n f s ‚Üî
      CMDiff[s] n (Prod.fst ‚àò f) ‚àß CMDiff[s] n (Prod.snd ‚àò f) := by
  rw [modelWithCornersSelf_prod, ‚Üê chartedSpaceSelf_prod]
  exact contMDiffOn_prod_iff f

theorem contMDiff_prod_iff (f : M ‚Üí M' √ó N') :
    CMDiff n f ‚Üî CMDiff n (Prod.fst ‚àò f) ‚àß CMDiff n (Prod.snd ‚àò f) :=
  ‚ü®fun h => ‚ü®h.fst, h.snd‚ü©, fun h => by convert h.1.prodMk h.2‚ü©

theorem contMDiff_prod_module_iff (f : M ‚Üí F‚ÇÅ √ó F‚ÇÇ) :
    ContMDiff I ùìò(ùïú, F‚ÇÅ √ó F‚ÇÇ) n f ‚Üî
      CMDiff n (Prod.fst ‚àò f) ‚àß CMDiff n (Prod.snd ‚àò f) := by
  rw [modelWithCornersSelf_prod, ‚Üê chartedSpaceSelf_prod]
  exact contMDiff_prod_iff f

theorem contMDiff_prod_assoc :
    ContMDiff ((I.prod I').prod J) (I.prod (I'.prod J)) n
      fun x : (M √ó M') √ó N => (x.1.1, x.1.2, x.2) :=
  contMDiff_fst.fst.prodMk <| contMDiff_fst.snd.prodMk contMDiff_snd

/-- `ContMDiffWithinAt.comp` for a function of two arguments. -/
theorem ContMDiffWithinAt.comp‚ÇÇ {h : M' √ó N' ‚Üí N} {f : M ‚Üí M'} {g : M ‚Üí N'} {x : M}
    {t : Set (M' √ó N')} (ha : CMDiffAt[t] n h (f x, g x))
    (fa : CMDiffAt[s] n f x) (ga : CMDiffAt[s] n g x)
    (st : MapsTo (fun x ‚Ü¶ (f x, g x)) s t) :
    CMDiffAt[s] n (fun x ‚Ü¶ h (f x, g x)) x :=
  ha.comp (f := fun x ‚Ü¶ (f x, g x)) _ (fa.prodMk ga) st

/-- `ContMDiffWithinAt.comp‚ÇÇ`, with a separate argument for point equality. -/
theorem ContMDiffWithinAt.comp‚ÇÇ_of_eq {h : M' √ó N' ‚Üí N} {f : M ‚Üí M'} {g : M ‚Üí N'} {x : M}
    {y : M' √ó N'} {t : Set (M' √ó N')} (ha : CMDiffAt[t] n h y)
    (fa : CMDiffAt[s] n f x) (ga : CMDiffAt[s] n g x)
    (e : (f x, g x) = y) (st : MapsTo (fun x ‚Ü¶ (f x, g x)) s t) :
    CMDiffAt[s] n (fun x ‚Ü¶ h (f x, g x)) x := by
  rw [‚Üê e] at ha
  exact ha.comp‚ÇÇ fa ga st

/-- `ContMDiffAt.comp` for a function of two arguments. -/
theorem ContMDiffAt.comp‚ÇÇ {h : M' √ó N' ‚Üí N} {f : M ‚Üí M'} {g : M ‚Üí N'} {x : M}
    (ha : CMDiffAt n h (f x, g x)) (fa : CMDiffAt n f x)
    (ga : CMDiffAt n g x) : CMDiffAt n (fun x ‚Ü¶ h (f x, g x)) x :=
  ha.comp (f := fun x ‚Ü¶ (f x, g x)) _ (fa.prodMk ga)

/-- `ContMDiffAt.comp‚ÇÇ`, with a separate argument for point equality. -/
theorem ContMDiffAt.comp‚ÇÇ_of_eq {h : M' √ó N' ‚Üí N} {f : M ‚Üí M'} {g : M ‚Üí N'} {x : M} {y : M' √ó N'}
    (ha : CMDiffAt n h y) (fa : CMDiffAt n f x) (ga : CMDiffAt n g x) (e : (f x, g x) = y) :
    CMDiffAt n (fun x ‚Ü¶ h (f x, g x)) x := by
  rw [‚Üê e] at ha
  exact ha.comp‚ÇÇ fa ga

/-- Curried `C^n` functions are `C^n` in the first coordinate. -/
theorem ContMDiffWithinAt.curry_left {f : M ‚Üí M' ‚Üí N} {x : M} {y : M'} {s : Set (M √ó M')}
    (fa : CMDiffAt[s] n (uncurry f) (x, y)) :
    CMDiffAt[{x | (x, y) ‚àà s}] n (fun x ‚Ü¶ f x y) x :=
  fa.comp‚ÇÇ contMDiffWithinAt_id contMDiffWithinAt_const (fun _ h ‚Ü¶ h)
alias ContMDiffWithinAt.along_fst := ContMDiffWithinAt.curry_left

/-- Curried `C^n` functions are `C^n` in the second coordinate. -/
theorem ContMDiffWithinAt.curry_right {f : M ‚Üí M' ‚Üí N} {x : M} {y : M'} {s : Set (M √ó M')}
    (fa : CMDiffAt[s] n (uncurry f) (x, y)) :
    CMDiffAt[{y | (x, y) ‚àà s}] n (fun y ‚Ü¶ f x y) y :=
  fa.comp‚ÇÇ contMDiffWithinAt_const contMDiffWithinAt_id (fun _ h ‚Ü¶ h)
alias ContMDiffWithinAt.along_snd := ContMDiffWithinAt.curry_right

/-- Curried `C^n` functions are `C^n` in the first coordinate. -/
theorem ContMDiffAt.curry_left {f : M ‚Üí M' ‚Üí N} {x : M} {y : M'}
    (fa : CMDiffAt n (uncurry f) (x, y)) : CMDiffAt n (fun x ‚Ü¶ f x y) x :=
  fa.comp‚ÇÇ contMDiffAt_id contMDiffAt_const
alias ContMDiffAt.along_fst := ContMDiffAt.curry_left

/-- Curried `C^n` functions are `C^n` in the second coordinate. -/
theorem ContMDiffAt.curry_right {f : M ‚Üí M' ‚Üí N} {x : M} {y : M'}
    (fa : CMDiffAt n (uncurry f) (x, y)) : CMDiffAt n (fun y ‚Ü¶ f x y) y :=
  fa.comp‚ÇÇ contMDiffAt_const contMDiffAt_id
alias ContMDiffAt.along_snd := ContMDiffAt.curry_right

/-- Curried `C^n` functions are `C^n` in the first coordinate. -/
theorem ContMDiffOn.curry_left {f : M ‚Üí M' ‚Üí N} {s : Set (M √ó M')}
    (fa : CMDiff[s] n (uncurry f)) {y : M'} :
    CMDiff[{x | (x, y) ‚àà s}] n (fun x ‚Ü¶ f x y) :=
  fun x m ‚Ü¶ (fa (x, y) m).along_fst
alias ContMDiffOn.along_fst := ContMDiffOn.curry_left

/-- Curried `C^n` functions are `C^n` in the second coordinate. -/
theorem ContMDiffOn.curry_right {f : M ‚Üí M' ‚Üí N} {x : M} {s : Set (M √ó M')}
    (fa : CMDiff[s] n (uncurry f)) : CMDiff[{y | (x, y) ‚àà s}] n (fun y ‚Ü¶ f x y) :=
  fun y m ‚Ü¶ (fa (x, y) m).along_snd
alias ContMDiffOn.along_snd := ContMDiffOn.curry_right

/-- Curried `C^n` functions are `C^n` in the first coordinate. -/
theorem ContMDiff.curry_left {f : M ‚Üí M' ‚Üí N} (fa : CMDiff n (uncurry f)) {y : M'} :
    CMDiff n (fun x ‚Ü¶ f x y) :=
  fun _ ‚Ü¶ (fa _).along_fst
alias ContMDiff.along_fst := ContMDiff.curry_left

/-- Curried `C^n` functions are `C^n` in the second coordinate. -/
theorem ContMDiff.curry_right {f : M ‚Üí M' ‚Üí N} {x : M} (fa : CMDiff n (uncurry f)) :
    CMDiff n (fun y ‚Ü¶ f x y) :=
  fun _ ‚Ü¶ (fa _).along_snd
alias ContMDiff.along_snd := ContMDiff.curry_right

section prodMap

variable {g : N ‚Üí N'} {r : Set N} {y : N}

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
theorem ContMDiffWithinAt.prodMap' {p : M √ó N}
    (hf : CMDiffAt[s] n f p.1) (hg : CMDiffAt[r] n g p.2) :
    CMDiffAt[s √óÀ¢ r] n (Prod.map f g) p :=
  (hf.comp p contMDiffWithinAt_fst mapsTo_fst_prod).prodMk <|
    hg.comp p contMDiffWithinAt_snd mapsTo_snd_prod

theorem ContMDiffWithinAt.prodMap (hf : CMDiffAt[s] n f x) (hg : CMDiffAt[r] n g y) :
    CMDiffAt[s √óÀ¢ r] n (Prod.map f g) (x, y) :=
  ContMDiffWithinAt.prodMap' hf hg

theorem ContMDiffAt.prodMap (hf : CMDiffAt n f x) (hg : CMDiffAt n g y) :
    CMDiffAt n (Prod.map f g) (x, y) := by
  simp only [‚Üê contMDiffWithinAt_univ, ‚Üê univ_prod_univ] at *
  exact hf.prodMap hg

theorem ContMDiffAt.prodMap' {p : M √ó N} (hf : CMDiffAt n f p.1)
    (hg : CMDiffAt n g p.2) : CMDiffAt n (Prod.map f g) p :=
  hf.prodMap hg

theorem ContMDiffOn.prodMap (hf : CMDiff[s] n f) (hg : CMDiff[r] n g) :
    CMDiff[s √óÀ¢ r] n (Prod.map f g) :=
  (hf.comp contMDiffOn_fst mapsTo_fst_prod).prodMk <| hg.comp contMDiffOn_snd mapsTo_snd_prod

theorem ContMDiff.prodMap (hf : CMDiff n f) (hg : CMDiff n g) : CMDiff n (Prod.map f g) := by
  intro p
  exact (hf p.1).prodMap' (hg p.2)

end prodMap

section PiSpace

/-!
### Regularity of functions with codomain `Œ† i, F i`

We have no `ModelWithCorners.pi` yet, so we prove lemmas about functions `f : M ‚Üí Œ† i, F i` and
use `ùìò(ùïú, Œ† i, F i)` as the model space.
-/


variable {Œπ : Type*} [Fintype Œπ] {Fi : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (Fi i)]
  [‚àÄ i, NormedSpace ùïú (Fi i)] {œÜ : M ‚Üí ‚àÄ i, Fi i}

theorem contMDiffWithinAt_pi_space :
    ContMDiffWithinAt I ùìò(ùïú, ‚àÄ i, Fi i) n œÜ s x ‚Üî
      ‚àÄ i, ContMDiffWithinAt I ùìò(ùïú, Fi i) n (fun x => œÜ x i) s x := by
  simp only [contMDiffWithinAt_iff, continuousWithinAt_pi, contDiffWithinAt_pi, forall_and,
    extChartAt_model_space_eq_id, Function.comp_def, PartialEquiv.refl_coe, id]

theorem contMDiffOn_pi_space :
    ContMDiffOn I ùìò(ùïú, ‚àÄ i, Fi i) n œÜ s ‚Üî ‚àÄ i, ContMDiffOn I ùìò(ùïú, Fi i) n (fun x => œÜ x i) s :=
  ‚ü®fun h i x hx => contMDiffWithinAt_pi_space.1 (h x hx) i, fun h x hx =>
    contMDiffWithinAt_pi_space.2 fun i => h i x hx‚ü©

theorem contMDiffAt_pi_space :
    ContMDiffAt I ùìò(ùïú, ‚àÄ i, Fi i) n œÜ x ‚Üî ‚àÄ i, ContMDiffAt I ùìò(ùïú, Fi i) n (fun x => œÜ x i) x :=
  contMDiffWithinAt_pi_space

theorem contMDiff_pi_space :
    ContMDiff I ùìò(ùïú, ‚àÄ i, Fi i) n œÜ ‚Üî ‚àÄ i, ContMDiff I ùìò(ùïú, Fi i) n fun x => œÜ x i :=
  ‚ü®fun h i x => contMDiffAt_pi_space.1 (h x) i, fun h x => contMDiffAt_pi_space.2 fun i => h i x‚ü©

end PiSpace

section disjointUnion

variable {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M'] {n : WithTop ‚Ñï‚àû}
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {J : Type*} {J : ModelWithCorners ùïú E' H'}
  {N N' : Type*} [TopologicalSpace N] [TopologicalSpace N'] [ChartedSpace H' N] [ChartedSpace H' N']

open Topology

-- The non-terminal simp has a large simp set
set_option linter.flexible false in
lemma ContMDiff.inl : ContMDiff I I n (@Sum.inl M M') := by
  intro x
  rw [contMDiffAt_iff]
  refine ‚ü®continuous_inl.continuousAt, ?_‚ü©
  -- In extended charts, .inl equals the identity (on the chart sources).
  apply contDiffWithinAt_id.congr_of_eventuallyEq; swap
  ¬∑ simp [ChartedSpace.sum_chartAt_inl]
    congr
    apply Sum.inl_injective.extend_apply (chartAt _ x)
  set C := chartAt H x with hC
  have : I.symm ‚Åª¬π' C.target ‚à© range I ‚àà ùìù[range I] (extChartAt I x) x := by
    rw [‚Üê I.image_eq (chartAt H x).target]
    exact (chartAt H x).extend_image_target_mem_nhds (mem_chart_source _ x)
  filter_upwards [this] with y hy
  simp [extChartAt, sum_chartAt_inl, ‚Üê hC, Sum.inl_injective.extend_apply C, C.right_inv hy.1,
    I.right_inv hy.2]

lemma ContMDiff.inr : ContMDiff I I n (@Sum.inr M M') := by
  intro x
  rw [contMDiffAt_iff]
  refine ‚ü®continuous_inr.continuousAt, ?_‚ü©
  -- In extended charts, .inl equals the identity (on the chart sources).
  apply contDiffWithinAt_id.congr_of_eventuallyEq; swap
  ¬∑ simp only [mfld_simps, sum_chartAt_inr]
    congr
    apply Sum.inr_injective.extend_apply (chartAt _ x)
  set C := chartAt H x with hC
  have : I.symm ‚Åª¬π' (chartAt H x).target ‚à© range I ‚àà ùìù[range I] (extChartAt I x) x := by
    rw [‚Üê I.image_eq (chartAt H x).target]
    exact (chartAt H x).extend_image_target_mem_nhds (mem_chart_source _ x)
  filter_upwards [this] with y hy
  simp [extChartAt, sum_chartAt_inr, ‚Üê hC, Sum.inr_injective.extend_apply C, C.right_inv hy.1,
    I.right_inv hy.2]

lemma extChartAt_inl_apply {x y : M} :
    (extChartAt I (.inl x : M ‚äï M')) (Sum.inl y) = (extChartAt I x) y := by simp

lemma extChartAt_inr_apply {x y : M'} :
    (extChartAt I (.inr x : M ‚äï M')) (Sum.inr y) = (extChartAt I x) y := by simp

lemma ContMDiff.sumElim {f : M ‚Üí N} {g : M' ‚Üí N}
    (hf : CMDiff n f) (hg : CMDiff n g) : ContMDiff I J n (Sum.elim f g) := by
  intro p
  rw [contMDiffAt_iff]
  refine ‚ü®(Continuous.sumElim hf.continuous hg.continuous).continuousAt, ?_‚ü©
  cases p with
  | inl x =>
    -- In charts around x : M, the map f ‚äî g looks like f.
    -- This is how they both look like in extended charts.
    have : ContDiffWithinAt ùïú n ((extChartAt J (f x)) ‚àò f ‚àò (extChartAt I x).symm)
        (range I) ((extChartAt I (.inl x : M ‚äï M')) (Sum.inl x)) := by
      let hf' := hf x
      rw [contMDiffAt_iff] at hf'
      simpa using hf'.2
    apply this.congr_of_eventuallyEq
    ¬∑ simp only [extChartAt, Sum.elim_inl, ChartedSpace.sum_chartAt_inl]
      filter_upwards with a
      congr
    ¬∑ -- They agree at the image of x.
      simp only [extChartAt, ChartedSpace.sum_chartAt_inl, Sum.elim_inl]
      congr
  | inr x =>
    -- In charts around x : M, the map f ‚äî g looks like g.
    -- This is how they both look like in extended charts.
    have : ContDiffWithinAt ùïú n ((extChartAt J (g x)) ‚àò g ‚àò (extChartAt I x).symm)
        (range I) ((extChartAt I (.inr x : M ‚äï M')) (Sum.inr x)) := by
      let hg' := hg x
      rw [contMDiffAt_iff] at hg'
      simpa using hg'.2
    apply this.congr_of_eventuallyEq
    ¬∑ simp only [extChartAt, Sum.elim_inr, ChartedSpace.sum_chartAt_inr]
      filter_upwards with a
      congr
    ¬∑ -- They agree at the image of x.
      simp only [extChartAt, ChartedSpace.sum_chartAt_inr, Sum.elim_inr]
      congr

lemma ContMDiff.sumMap {f : M ‚Üí N} {g : M' ‚Üí N'}
    (hf : CMDiff n f) (hg : CMDiff n g) : ContMDiff I J n (Sum.map f g) :=
  ContMDiff.sumElim (ContMDiff.inl.comp hf) (ContMDiff.inr.comp hg)

lemma contMDiff_of_contMDiff_inl {f : M ‚Üí N}
    (h : ContMDiff I J n ((@Sum.inl N N') ‚àò f)) : CMDiff n f := by
  nontriviality N
  inhabit N
  let aux : N ‚äï N' ‚Üí N := Sum.elim (@id N) (fun _ ‚Ü¶ inhabited_h.default)
  have : aux ‚àò (@Sum.inl N N') ‚àò f = f := by ext; simp [aux]
  rw [‚Üê this]
  rw [‚Üê contMDiffOn_univ] at h ‚ä¢
  apply (contMDiff_id.sumElim contMDiff_const).contMDiffOn (s := @Sum.inl N N' '' univ).comp h
  intro x _hx
  rw [mem_preimage, Function.comp_apply]
  use f x, trivial

lemma contMDiff_of_contMDiff_inr {g : M' ‚Üí N'}
    (h : ContMDiff I J n ((@Sum.inr N N') ‚àò g)) : CMDiff n g := by
  nontriviality N'
  inhabit N'
  let aux : N ‚äï N' ‚Üí N' := Sum.elim (fun _ ‚Ü¶ inhabited_h.default) (@id N')
  have : aux ‚àò (@Sum.inr N N') ‚àò g = g := by ext; simp [aux]
  rw [‚Üê this]
  rw [‚Üê contMDiffOn_univ] at h ‚ä¢
  apply ((contMDiff_const.sumElim contMDiff_id).contMDiffOn (s := Sum.inr '' univ)).comp h
  intro x _hx
  rw [mem_preimage, Function.comp_apply]
  use g x, trivial

lemma contMDiff_sum_map {f : M ‚Üí N} {g : M' ‚Üí N'} :
    ContMDiff I J n (Sum.map f g) ‚Üî CMDiff n f ‚àß CMDiff n g :=
  ‚ü®fun h ‚Ü¶ ‚ü®contMDiff_of_contMDiff_inl (h.comp ContMDiff.inl),
    contMDiff_of_contMDiff_inr (h.comp ContMDiff.inr)‚ü©,
   fun h ‚Ü¶ ContMDiff.sumMap h.1 h.2‚ü©

lemma ContMDiff.swap : ContMDiff I I n (@Sum.swap M M') := ContMDiff.sumElim inr inl

end disjointUnion
