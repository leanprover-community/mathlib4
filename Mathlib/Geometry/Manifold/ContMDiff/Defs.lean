/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.IsManifold.ExtChartAt
import Mathlib.Geometry.Manifold.LocalInvariantProperties

/-!
# `C^n` functions between manifolds

We define `C‚Åø` functions between manifolds, as functions which are `C‚Åø` in charts, and prove
basic properties of these notions. Here, `n` can be finite, or `‚àû`, or `œâ`.

## Main definitions and statements

Let `M` and `M'` be two manifolds, with respect to models with corners `I` and `I'`. Let
`f : M ‚Üí M'`.

* `ContMDiffWithinAt I I' n f s x` states that the function `f` is `C‚Åø` within the set `s`
  around the point `x`.
* `ContMDiffAt I I' n f x` states that the function `f` is `C‚Åø` around `x`.
* `ContMDiffOn I I' n f s` states that the function `f` is `C‚Åø` on the set `s`
* `ContMDiff I I' n f` states that the function `f` is `C‚Åø`.

We also give some basic properties of `C‚Åø` functions between manifolds, following the API of
`C^n` functions between vector spaces.
See `Basic.lean` for further basic properties of `C‚Åø` functions between manifolds,
`NormedSpace.lean` for the equivalence of manifold-smoothness to usual smoothness,
`Product.lean` for smoothness results related to the product of manifolds and
`Atlas.lean` for smoothness of atlas members and local structomorphisms.

## Implementation details

Many properties follow for free from the corresponding properties of functions in vector spaces,
as being `C‚Åø` is a local property invariant under the `C‚Åø` groupoid. We take advantage of the
general machinery developed in `LocalInvariantProperties.lean` to get these properties
automatically. For instance, the fact that being `C‚Åø` does not depend on the chart one considers
is given by `liftPropWithinAt_indep_chart`.

For this to work, the definition of `ContMDiffWithinAt` and friends has to
follow definitionally the setup of local invariant properties. Still, we recast the definition
in terms of extended charts in `contMDiffOn_iff` and `contMDiff_iff`.
-/


open Set Function Filter ChartedSpace IsManifold

open scoped Topology Manifold ContDiff

/-! ### Definition of `C‚Åø` functions between manifolds -/


variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  -- Prerequisite typeclasses to say that `M` is a manifold over the pair `(E, H)`
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ùïú E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- Prerequisite typeclasses to say that `M'` is a manifold over the pair `(E', H')`
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- Prerequisite typeclasses to say that `M''` is a manifold over the pair `(E'', H'')`
  {E'' : Type*}
  [NormedAddCommGroup E''] [NormedSpace ùïú E''] {H'' : Type*} [TopologicalSpace H'']
  {I'' : ModelWithCorners ùïú E'' H''} {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  -- declare functions, sets, points and smoothness indices
  {e : OpenPartialHomeomorph M H}
  {e' : OpenPartialHomeomorph M' H'} {f f‚ÇÅ : M ‚Üí M'} {s s‚ÇÅ t : Set M} {x : M} {m n : WithTop ‚Ñï‚àû}

variable (I I') in
/-- Property in the model space of a model with corners of being `C^n` within at set at a point,
when read in the model vector space. This property will be lifted to manifolds to define `C^n`
functions between manifolds. -/
def ContDiffWithinAtProp (n : WithTop ‚Ñï‚àû) (f : H ‚Üí H') (s : Set H) (x : H) : Prop :=
  ContDiffWithinAt ùïú n (I' ‚àò f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I) (I x)

theorem contDiffWithinAtProp_self_source {f : E ‚Üí H'} {s : Set E} {x : E} :
    ContDiffWithinAtProp ùìò(ùïú, E) I' n f s x ‚Üî ContDiffWithinAt ùïú n (I' ‚àò f) s x := by
  simp_rw [ContDiffWithinAtProp, modelWithCornersSelf_coe, range_id, inter_univ,
    modelWithCornersSelf_coe_symm, CompTriple.comp_eq, preimage_id_eq, id_eq]

theorem contDiffWithinAtProp_self {f : E ‚Üí E'} {s : Set E} {x : E} :
    ContDiffWithinAtProp ùìò(ùïú, E) ùìò(ùïú, E') n f s x ‚Üî ContDiffWithinAt ùïú n f s x :=
  contDiffWithinAtProp_self_source

theorem contDiffWithinAtProp_self_target {f : H ‚Üí E'} {s : Set H} {x : H} :
    ContDiffWithinAtProp I ùìò(ùïú, E') n f s x ‚Üî
      ContDiffWithinAt ùïú n (f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I) (I x) :=
  Iff.rfl

/-- Being `C‚Åø` in the model space is a local property, invariant under `C‚Åø` maps. Therefore,
it lifts nicely to manifolds. -/
theorem contDiffWithinAt_localInvariantProp_of_le (n m : WithTop ‚Ñï‚àû) (hmn : m ‚â§ n) :
    (contDiffGroupoid n I).LocalInvariantProp (contDiffGroupoid n I')
      (ContDiffWithinAtProp I I' m) where
  is_local {s x u f} u_open xu := by
    have : I.symm ‚Åª¬π' (s ‚à© u) ‚à© range I = I.symm ‚Åª¬π' s ‚à© range I ‚à© I.symm ‚Åª¬π' u := by
      simp only [inter_right_comm, preimage_inter]
    rw [ContDiffWithinAtProp, ContDiffWithinAtProp, this]
    symm
    apply contDiffWithinAt_inter
    have : u ‚àà ùìù (I.symm (I x)) := by
      rw [ModelWithCorners.left_inv]
      exact u_open.mem_nhds xu
    apply ContinuousAt.preimage_mem_nhds I.continuous_symm.continuousAt this
  right_invariance' {s x f e} he hx h := by
    rw [ContDiffWithinAtProp] at h ‚ä¢
    have : I x = (I ‚àò e.symm ‚àò I.symm) (I (e x)) := by simp only [hx, mfld_simps]
    rw [this] at h
    have : I (e x) ‚àà I.symm ‚Åª¬π' e.target ‚à© range I := by simp only [hx, mfld_simps]
    have := (mem_groupoid_of_pregroupoid.2 he).2.contDiffWithinAt this
    convert (h.comp_inter _ (this.of_le hmn)).mono_of_mem_nhdsWithin _
      using 1
    ¬∑ ext y; simp only [mfld_simps]
    refine mem_nhdsWithin.mpr
      ‚ü®I.symm ‚Åª¬π' e.target, e.open_target.preimage I.continuous_symm, by
        simp_rw [mem_preimage, I.left_inv, e.mapsTo hx], ?_‚ü©
    mfld_set_tac
  congr_of_forall {s x f g} h hx hf := by
    apply hf.congr
    ¬∑ intro y hy
      simp only [mfld_simps] at hy
      simp only [h, hy, mfld_simps]
    ¬∑ simp only [hx, mfld_simps]
  left_invariance' {s x f e'} he' hs hx h := by
    rw [ContDiffWithinAtProp] at h ‚ä¢
    have A : (I' ‚àò f ‚àò I.symm) (I x) ‚àà I'.symm ‚Åª¬π' e'.source ‚à© range I' := by
      simp only [hx, mfld_simps]
    have := (mem_groupoid_of_pregroupoid.2 he').1.contDiffWithinAt A
    convert (this.of_le hmn).comp _ h _
    ¬∑ ext y; simp only [mfld_simps]
    ¬∑ intro y hy; simp only [mfld_simps] at hy; simpa only [hy, mfld_simps] using hs hy.1

/-- Being `C‚Åø` in the model space is a local property, invariant under `C^n` maps. Therefore,
it lifts nicely to manifolds. -/
theorem contDiffWithinAt_localInvariantProp (n : WithTop ‚Ñï‚àû) :
    (contDiffGroupoid n I).LocalInvariantProp (contDiffGroupoid n I')
      (ContDiffWithinAtProp I I' n) :=
  contDiffWithinAt_localInvariantProp_of_le n n le_rfl

theorem contDiffWithinAtProp_mono_of_mem_nhdsWithin
    (n : WithTop ‚Ñï‚àû) ‚¶És x t‚¶Ñ ‚¶Éf : H ‚Üí H'‚¶Ñ (hts : s ‚àà ùìù[t] x)
    (h : ContDiffWithinAtProp I I' n f s x) : ContDiffWithinAtProp I I' n f t x := by
  refine h.mono_of_mem_nhdsWithin ?_
  refine inter_mem ?_ (mem_of_superset self_mem_nhdsWithin inter_subset_right)
  rwa [‚Üê Filter.mem_map, ‚Üê I.image_eq, I.symm_map_nhdsWithin_image]

theorem contDiffWithinAtProp_id (x : H) : ContDiffWithinAtProp I I n id univ x := by
  simp only [ContDiffWithinAtProp, id_comp, preimage_univ, univ_inter]
  have : ContDiffWithinAt ùïú n id (range I) (I x) := contDiff_id.contDiffAt.contDiffWithinAt
  refine this.congr (fun y hy => ?_) ?_
  ¬∑ simp only [ModelWithCorners.right_inv I hy, mfld_simps]
  ¬∑ simp only [mfld_simps]

variable (I I') in
/-- A function is `n` times continuously differentiable within a set at a point in a manifold if
it is continuous and it is `n` times continuously differentiable in this set around this point, when
read in the preferred chart at this point. -/
def ContMDiffWithinAt (n : WithTop ‚Ñï‚àû) (f : M ‚Üí M') (s : Set M) (x : M) :=
  LiftPropWithinAt (ContDiffWithinAtProp I I' n) f s x

variable (I I') in
/-- A function is `n` times continuously differentiable at a point in a manifold if
it is continuous and it is `n` times continuously differentiable around this point, when
read in the preferred chart at this point. -/
def ContMDiffAt (n : WithTop ‚Ñï‚àû) (f : M ‚Üí M') (x : M) :=
  ContMDiffWithinAt I I' n f univ x

theorem contMDiffAt_iff {n : WithTop ‚Ñï‚àû} {f : M ‚Üí M'} {x : M} :
    ContMDiffAt I I' n f x ‚Üî
      ContinuousAt f x ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm) (range I)
          (extChartAt I x x) :=
  liftPropAt_iff.trans <| by rw [ContDiffWithinAtProp, preimage_univ, univ_inter]; rfl

variable (I I') in
/-- A function is `n` times continuously differentiable in a set of a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable on this set in the charts
around these points. -/
def ContMDiffOn (n : WithTop ‚Ñï‚àû) (f : M ‚Üí M') (s : Set M) :=
  ‚àÄ x ‚àà s, ContMDiffWithinAt I I' n f s x

variable (I I') in
/-- A function is `n` times continuously differentiable in a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable in the charts
around these points. -/
def ContMDiff (n : WithTop ‚Ñï‚àû) (f : M ‚Üí M') :=
  ‚àÄ x, ContMDiffAt I I' n f x

/-! ### Deducing smoothness from higher smoothness -/

theorem ContMDiffWithinAt.of_le (hf : ContMDiffWithinAt I I' n f s x) (le : m ‚â§ n) :
    ContMDiffWithinAt I I' m f s x := by
  simp only [ContMDiffWithinAt] at hf ‚ä¢
  exact ‚ü®hf.1, hf.2.of_le (mod_cast le)‚ü©

theorem ContMDiffAt.of_le (hf : ContMDiffAt I I' n f x) (le : m ‚â§ n) : ContMDiffAt I I' m f x :=
  ContMDiffWithinAt.of_le hf le

theorem ContMDiffOn.of_le (hf : ContMDiffOn I I' n f s) (le : m ‚â§ n) : ContMDiffOn I I' m f s :=
  fun x hx => (hf x hx).of_le le

theorem ContMDiff.of_le (hf : ContMDiff I I' n f) (le : m ‚â§ n) : ContMDiff I I' m f := fun x =>
  (hf x).of_le le

/-! ### Basic properties of `C^n` functions between manifolds -/

theorem ContMDiff.contMDiffAt (h : ContMDiff I I' n f) : ContMDiffAt I I' n f x :=
  h x

theorem contMDiffWithinAt_univ : ContMDiffWithinAt I I' n f univ x ‚Üî ContMDiffAt I I' n f x :=
  Iff.rfl

theorem contMDiffOn_univ : ContMDiffOn I I' n f univ ‚Üî ContMDiff I I' n f := by
  simp only [ContMDiffOn, ContMDiff, contMDiffWithinAt_univ, forall_prop_of_true, mem_univ]

/-- One can reformulate being `C^n` within a set at a point as continuity within this set at this
point, and being `C^n` in the corresponding extended chart. -/
theorem contMDiffWithinAt_iff :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x) := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff']; rfl

/-- One can reformulate being `C‚Åø` within a set at a point as continuity within this set at this
point, and being `C‚Åø` in the corresponding extended chart. This form states regularity of `f`
written in such a way that the set is restricted to lie within the domain/codomain of the
corresponding charts.
Even though this expression is more complicated than the one in `contMDiffWithinAt_iff`, it is
a smaller set, but their germs at `extChartAt I x x` are equal. It is sometimes useful to rewrite
using this in the goal.
-/
theorem contMDiffWithinAt_iff' :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).target ‚à©
            (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' (f x)).source))
          (extChartAt I x x) := by
  simp only [ContMDiffWithinAt, liftPropWithinAt_iff']
  exact and_congr_right fun hc => contDiffWithinAt_congr_set <|
    hc.extChartAt_symm_preimage_inter_range_eventuallyEq

/-- One can reformulate being `C‚Åø` within a set at a point as continuity within this set at this
point, and being `C‚Åø` in the corresponding extended chart in the target. -/
theorem contMDiffWithinAt_iff_target :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß ContMDiffWithinAt I ùìò(ùïú, E') n (extChartAt I' (f x) ‚àò f) s x := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff', ‚Üê and_assoc]
  have cont :
    ContinuousWithinAt f s x ‚àß ContinuousWithinAt (extChartAt I' (f x) ‚àò f) s x ‚Üî
        ContinuousWithinAt f s x :=
      and_iff_left_of_imp <| (continuousAt_extChartAt _).comp_continuousWithinAt
  simp_rw [cont, ContDiffWithinAtProp, extChartAt, OpenPartialHomeomorph.extend,
    PartialEquiv.coe_trans, ModelWithCorners.toPartialEquiv_coe, OpenPartialHomeomorph.coe_coe,
    modelWithCornersSelf_coe, chartAt_self_eq, OpenPartialHomeomorph.refl_apply, id_comp]
  rfl

theorem contMDiffAt_iff_target {x : M} :
    ContMDiffAt I I' n f x ‚Üî
      ContinuousAt f x ‚àß ContMDiffAt I ùìò(ùïú, E') n (extChartAt I' (f x) ‚àò f) x := by
  rw [ContMDiffAt, ContMDiffAt, contMDiffWithinAt_iff_target, continuousWithinAt_univ]

/-- One can reformulate being `C‚Åø` within a set at a point as being `C‚Åø` in the source space when
composing with the extended chart. -/
theorem contMDiffWithinAt_iff_source :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (extChartAt I x).symm)
        ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x) := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff']
  have : ContinuousWithinAt f s x
      ‚Üî ContinuousWithinAt (f ‚àò ‚Üë(extChartAt I x).symm) (‚Üë(extChartAt I x).symm ‚Åª¬π' s ‚à© range ‚ÜëI)
        (extChartAt I x x) := by
    refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
    ¬∑ apply h.comp_of_eq
      ¬∑ exact (continuousAt_extChartAt_symm x).continuousWithinAt
      ¬∑ exact (mapsTo_preimage _ _).mono_left inter_subset_left
      ¬∑ exact extChartAt_to_inv x
    ¬∑ rw [‚Üê continuousWithinAt_inter (extChartAt_source_mem_nhds (I := I) x)]
      have : ContinuousWithinAt ((f ‚àò ‚Üë(extChartAt I x).symm) ‚àò ‚Üë(extChartAt I x))
          (s ‚à© (extChartAt I x).source) x := by
        apply h.comp (continuousAt_extChartAt x).continuousWithinAt
        intro y hy
        have : (chartAt H x).symm ((chartAt H x) y) = y :=
          OpenPartialHomeomorph.left_inv _ (by simpa using hy.2)
        simpa [this] using hy.1
      apply this.congr
      ¬∑ intro y hy
        have : (chartAt H x).symm ((chartAt H x) y) = y :=
          OpenPartialHomeomorph.left_inv _ (by simpa using hy.2)
        simp [this]
      ¬∑ simp
  rw [‚Üê this]
  simp only [ContDiffWithinAtProp, mfld_simps, preimage_comp, comp_assoc]

/-- One can reformulate being `C‚Åø` at a point as being `C‚Åø` in the source space when
composing with the extended chart. -/
theorem contMDiffAt_iff_source :
    ContMDiffAt I I' n f x ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (extChartAt I x).symm) (range I) (extChartAt I x x) := by
  rw [‚Üê contMDiffWithinAt_univ, contMDiffWithinAt_iff_source]
  simp

section IsManifold

theorem contMDiffWithinAt_iff_source_of_mem_maximalAtlas
    [IsManifold I n M] (he : e ‚àà maximalAtlas I n M) (hx : x ‚àà e.source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (e.extend I).symm) ((e.extend I).symm ‚Åª¬π' s ‚à© range I)
        (e.extend I x) := by
  have h2x := hx; rw [‚Üê e.extend_source (I := I)] at h2x
  simp_rw [ContMDiffWithinAt,
    (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_indep_chart_source he hx,
    StructureGroupoid.liftPropWithinAt_self_source,
    e.extend_symm_continuousWithinAt_comp_right_iff, contDiffWithinAtProp_self_source,
    ContDiffWithinAtProp, Function.comp, e.left_inv hx, (e.extend I).left_inv h2x]
  rfl

theorem contMDiffWithinAt_iff_source_of_mem_source
    [IsManifold I n M] {x' : M} (hx' : x' ‚àà (chartAt H x).source) :
    ContMDiffWithinAt I I' n f s x' ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (extChartAt I x).symm)
        ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x') :=
  contMDiffWithinAt_iff_source_of_mem_maximalAtlas (chart_mem_maximalAtlas x) hx'

theorem contMDiffAt_iff_source_of_mem_source
    [IsManifold I n M] {x' : M} (hx' : x' ‚àà (chartAt H x).source) :
    ContMDiffAt I I' n f x' ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (extChartAt I x).symm) (range I) (extChartAt I x x') := by
  simp_rw [ContMDiffAt, contMDiffWithinAt_iff_source_of_mem_source hx', preimage_univ, univ_inter]

theorem contMDiffWithinAt_iff_target_of_mem_source
    [IsManifold I' n M'] {x : M} {y : M'} (hy : f x ‚àà (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß ContMDiffWithinAt I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) s x := by
  simp_rw [ContMDiffWithinAt]
  rw [(contDiffWithinAt_localInvariantProp n).liftPropWithinAt_indep_chart_target
      (chart_mem_maximalAtlas y) hy,
    and_congr_right]
  intro hf
  simp_rw [StructureGroupoid.liftPropWithinAt_self_target]
  simp_rw [((chartAt H' y).continuousAt hy).comp_continuousWithinAt hf]
  rw [‚Üê extChartAt_source (I := I')] at hy
  simp_rw [(continuousAt_extChartAt' hy).comp_continuousWithinAt hf]
  rfl

theorem contMDiffAt_iff_target_of_mem_source
    [IsManifold I' n M'] {x : M} {y : M'} (hy : f x ‚àà (chartAt H' y).source) :
    ContMDiffAt I I' n f x ‚Üî
      ContinuousAt f x ‚àß ContMDiffAt I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) x := by
  rw [ContMDiffAt, contMDiffWithinAt_iff_target_of_mem_source hy, continuousWithinAt_univ,
    ContMDiffAt]

variable [IsManifold I n M] [IsManifold I' n M']

theorem contMDiffWithinAt_iff_of_mem_maximalAtlas {x : M} (he : e ‚àà maximalAtlas I n M)
    (he' : e' ‚àà maximalAtlas I' n M') (hx : x ‚àà e.source) (hy : f x ‚àà e'.source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm)
          ((e.extend I).symm ‚Åª¬π' s ‚à© range I) (e.extend I x) :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_indep_chart he hx he' hy

/-- An alternative formulation of `contMDiffWithinAt_iff_of_mem_maximalAtlas`
if the set if `s` lies in `e.source`. -/
theorem contMDiffWithinAt_iff_image {x : M} (he : e ‚àà maximalAtlas I n M)
    (he' : e' ‚àà maximalAtlas I' n M')
    (hs : s ‚äÜ e.source) (hx : x ‚àà e.source) (hy : f x ‚àà e'.source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm) (e.extend I '' s)
          (e.extend I x) := by
  rw [contMDiffWithinAt_iff_of_mem_maximalAtlas he he' hx hy, and_congr_right_iff]
  refine fun _ => contDiffWithinAt_congr_set ?_
  simp_rw [e.extend_symm_preimage_inter_range_eventuallyEq hs hx]

/-- One can reformulate being `C^n` within a set at a point as continuity within this set at this
point, and being `C^n` in any chart containing that point. -/
theorem contMDiffWithinAt_iff_of_mem_source {x' : M} {y : M'} (hx : x' ‚àà (chartAt H x).source)
    (hy : f x' ‚àà (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x' ‚Üî
      ContinuousWithinAt f s x' ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x') :=
  contMDiffWithinAt_iff_of_mem_maximalAtlas (chart_mem_maximalAtlas x)
    (chart_mem_maximalAtlas y) hx hy

theorem contMDiffWithinAt_iff_of_mem_source' {x' : M} {y : M'} (hx : x' ‚àà (chartAt H x).source)
    (hy : f x' ‚àà (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x' ‚Üî
      ContinuousWithinAt f s x' ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' y).source))
          (extChartAt I x x') := by
  refine (contMDiffWithinAt_iff_of_mem_source hx hy).trans ?_
  rw [‚Üê extChartAt_source I] at hx
  rw [‚Üê extChartAt_source I'] at hy
  rw [and_congr_right_iff]
  set e := extChartAt I x; set e' := extChartAt I' (f x)
  refine fun hc => contDiffWithinAt_congr_set ?_
  rw [‚Üê nhdsWithin_eq_iff_eventuallyEq, ‚Üê e.image_source_inter_eq',
    ‚Üê map_extChartAt_nhdsWithin_eq_image' hx,
    ‚Üê map_extChartAt_nhdsWithin' hx, inter_comm, nhdsWithin_inter_of_mem]
  exact hc (extChartAt_source_mem_nhds' hy)

theorem contMDiffAt_iff_of_mem_source {x' : M} {y : M'} (hx : x' ‚àà (chartAt H x).source)
    (hy : f x' ‚àà (chartAt H' y).source) :
    ContMDiffAt I I' n f x' ‚Üî
      ContinuousAt f x' ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm) (range I)
          (extChartAt I x x') :=
  (contMDiffWithinAt_iff_of_mem_source hx hy).trans <| by
    rw [continuousWithinAt_univ, preimage_univ, univ_inter]

theorem contMDiffOn_iff_of_mem_maximalAtlas (he : e ‚àà maximalAtlas I n M)
    (he' : e' ‚àà maximalAtlas I' n M') (hs : s ‚äÜ e.source) (h2s : MapsTo f s e'.source) :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ContDiffOn ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm) (e.extend I '' s) := by
  simp_rw [ContinuousOn, ContDiffOn, Set.forall_mem_image, ‚Üê forall_and, ContMDiffOn]
  exact forall‚ÇÇ_congr fun x hx => contMDiffWithinAt_iff_image he he' hs (hs hx) (h2s hx)

theorem contMDiffOn_iff_of_mem_maximalAtlas' (he : e ‚àà maximalAtlas I n M)
    (he' : e' ‚àà maximalAtlas I' n M') (hs : s ‚äÜ e.source) (h2s : MapsTo f s e'.source) :
    ContMDiffOn I I' n f s ‚Üî
      ContDiffOn ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm) (e.extend I '' s) :=
  (contMDiffOn_iff_of_mem_maximalAtlas he he' hs h2s).trans <| and_iff_right_of_imp fun h ‚Ü¶
    (e.continuousOn_writtenInExtend_iff hs h2s).1 h.continuousOn

/-- If the set where you want `f` to be `C^n` lies entirely in a single chart, and `f` maps it
into a single chart, the fact that `f` is `C^n` on that set can be expressed by purely looking in
these charts.
Note: this lemma uses `extChartAt I x '' s` instead of `(extChartAt I x).symm ‚Åª¬π' s` to ensure
that this set lies in `(extChartAt I x).target`. -/
theorem contMDiffOn_iff_of_subset_source {x : M} {y : M'} (hs : s ‚äÜ (chartAt H x).source)
    (h2s : MapsTo f s (chartAt H' y).source) :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm) (extChartAt I x '' s) :=
  contMDiffOn_iff_of_mem_maximalAtlas (chart_mem_maximalAtlas x) (chart_mem_maximalAtlas y) hs
    h2s

/-- If the set where you want `f` to be `C^n` lies entirely in a single chart, and `f` maps it
into a single chart, the fact that `f` is `C^n` on that set can be expressed by purely looking in
these charts.
Note: this lemma uses `extChartAt I x '' s` instead of `(extChartAt I x).symm ‚Åª¬π' s` to ensure
that this set lies in `(extChartAt I x).target`. -/
theorem contMDiffOn_iff_of_subset_source' {x : M} {y : M'} (hs : s ‚äÜ (extChartAt I x).source)
    (h2s : MapsTo f s (extChartAt I' y).source) :
    ContMDiffOn I I' n f s ‚Üî
        ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm) (extChartAt I x '' s) := by
  rw [extChartAt_source] at hs h2s
  exact contMDiffOn_iff_of_mem_maximalAtlas' (chart_mem_maximalAtlas x)
    (chart_mem_maximalAtlas y) hs h2s

/-- One can reformulate being `C^n` on a set as continuity on this set, and being `C^n` in any
extended chart. -/
theorem contMDiffOn_iff :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ‚àÄ (x : M) (y : M'),
          ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
            ((extChartAt I x).target ‚à©
              (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' y).source)) := by
  constructor
  ¬∑ intro h
    refine ‚ü®fun x hx => (h x hx).1, fun x y z hz => ?_‚ü©
    simp only [mfld_simps] at hz
    let w := (extChartAt I x).symm z
    have : w ‚àà s := by simp only [w, hz, mfld_simps]
    specialize h w this
    have w1 : w ‚àà (chartAt H x).source := by simp only [w, hz, mfld_simps]
    have w2 : f w ‚àà (chartAt H' y).source := by simp only [w, hz, mfld_simps]
    convert ((contMDiffWithinAt_iff_of_mem_source w1 w2).mp h).2.mono _
    ¬∑ simp only [w, hz, mfld_simps]
    ¬∑ mfld_set_tac
  ¬∑ rintro ‚ü®hcont, hdiff‚ü© x hx
    refine (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_iff.mpr ?_
    refine ‚ü®hcont x hx, ?_‚ü©
    dsimp [ContDiffWithinAtProp]
    convert hdiff x (f x) (extChartAt I x x) (by simp only [hx, mfld_simps]) using 1
    mfld_set_tac

/-- zero-smoothness on a set is equivalent to continuity on this set. -/
theorem contMDiffOn_zero_iff :
    ContMDiffOn I I' 0 f s ‚Üî ContinuousOn f s := by
  rw [contMDiffOn_iff]
  refine ‚ü®fun h ‚Ü¶ h.1, fun h ‚Ü¶ ‚ü®h, ?_‚ü©‚ü©
  intro x y
  rw [contDiffOn_zero]
  apply (continuousOn_extChartAt _).comp
  ¬∑ apply h.comp ((continuousOn_extChartAt_symm _).mono inter_subset_left) (fun z hz ‚Ü¶ ?_)
    simp only [preimage_inter, mem_inter_iff, mem_preimage] at hz
    exact hz.2.1
  ¬∑ intro z hz
    simp only [preimage_inter, mem_inter_iff, mem_preimage] at hz
    exact hz.2.2

/-- One can reformulate being `C^n` on a set as continuity on this set, and being `C^n` in any
extended chart in the target. -/
theorem contMDiffOn_iff_target :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ‚àÄ y : M',
          ContMDiffOn I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) (s ‚à© f ‚Åª¬π' (extChartAt I' y).source) := by
  simp only [contMDiffOn_iff, ModelWithCorners.source_eq, chartAt_self_eq,
    OpenPartialHomeomorph.refl_partialEquiv, PartialEquiv.refl_trans, extChartAt,
    OpenPartialHomeomorph.extend, Set.preimage_univ, Set.inter_univ, and_congr_right_iff]
  intro h
  constructor
  ¬∑ refine fun h' y => ‚ü®?_, fun x _ => h' x y‚ü©
    have h'' : ContinuousOn _ univ := (ModelWithCorners.continuous I').continuousOn
    convert (h''.comp_inter (chartAt H' y).continuousOn_toFun).comp_inter h
    simp
  ¬∑ exact fun h' x y => (h' y).2 x 0


/-- One can reformulate being `C^n` as continuity and being `C^n` in any extended chart. -/
theorem contMDiff_iff :
    ContMDiff I I' n f ‚Üî
      Continuous f ‚àß
        ‚àÄ (x : M) (y : M'),
          ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
            ((extChartAt I x).target ‚à©
              (extChartAt I x).symm ‚Åª¬π' (f ‚Åª¬π' (extChartAt I' y).source)) := by
  simp [‚Üê contMDiffOn_univ, contMDiffOn_iff, continuousOn_univ]

/-- One can reformulate being `C^n` as continuity and being `C^n` in any extended chart in the
target. -/
theorem contMDiff_iff_target :
    ContMDiff I I' n f ‚Üî
      Continuous f ‚àß ‚àÄ y : M',
        ContMDiffOn I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) (f ‚Åª¬π' (extChartAt I' y).source) := by
  rw [‚Üê contMDiffOn_univ, contMDiffOn_iff_target]
  simp [continuousOn_univ]

/-- zero-smoothness is equivalent to continuity. -/
theorem contMDiff_zero_iff :
    ContMDiff I I' 0 f ‚Üî Continuous f := by
  rw [‚Üê contMDiffOn_univ, ‚Üê continuousOn_univ, contMDiffOn_zero_iff]

end IsManifold


/-! ### `C^(n+1)` functions are `C^n` -/

theorem ContMDiffWithinAt.of_succ (h : ContMDiffWithinAt I I' (n + 1) f s x) :
    ContMDiffWithinAt I I' n f s x :=
  h.of_le le_self_add

theorem ContMDiffAt.of_succ (h : ContMDiffAt I I' (n + 1) f x) : ContMDiffAt I I' n f x :=
  ContMDiffWithinAt.of_succ h

theorem ContMDiffOn.of_succ (h : ContMDiffOn I I' (n + 1) f s) : ContMDiffOn I I' n f s :=
  fun x hx => (h x hx).of_succ

theorem ContMDiff.of_succ (h : ContMDiff I I' (n + 1) f) : ContMDiff I I' n f := fun x =>
  (h x).of_succ


/-! ### `C^n` functions are continuous -/

theorem ContMDiffWithinAt.continuousWithinAt (hf : ContMDiffWithinAt I I' n f s x) :
    ContinuousWithinAt f s x :=
  hf.1

theorem ContMDiffAt.continuousAt (hf : ContMDiffAt I I' n f x) : ContinuousAt f x :=
  (continuousWithinAt_univ _ _).1 <| ContMDiffWithinAt.continuousWithinAt hf

theorem ContMDiffOn.continuousOn (hf : ContMDiffOn I I' n f s) : ContinuousOn f s := fun x hx =>
  (hf x hx).continuousWithinAt

theorem ContMDiff.continuous (hf : ContMDiff I I' n f) : Continuous f :=
  continuous_iff_continuousAt.2 fun x => (hf x).continuousAt

/-! ### `C^‚àû` functions -/

theorem contMDiffWithinAt_infty :
    ContMDiffWithinAt I I' ‚àû f s x ‚Üî ‚àÄ n : ‚Ñï, ContMDiffWithinAt I I' n f s x :=
  ‚ü®fun h n => ‚ü®h.1, contDiffWithinAt_infty.1 h.2 n‚ü©, fun H =>
    ‚ü®(H 0).1, contDiffWithinAt_infty.2 fun n => (H n).2‚ü©‚ü©

theorem contMDiffAt_infty : ContMDiffAt I I' ‚àû f x ‚Üî ‚àÄ n : ‚Ñï, ContMDiffAt I I' n f x :=
  contMDiffWithinAt_infty

theorem contMDiffOn_infty : ContMDiffOn I I' ‚àû f s ‚Üî ‚àÄ n : ‚Ñï, ContMDiffOn I I' n f s :=
  ‚ü®fun h _ => h.of_le (mod_cast le_top),
    fun h x hx => contMDiffWithinAt_infty.2 fun n => h n x hx‚ü©

theorem contMDiff_infty : ContMDiff I I' ‚àû f ‚Üî ‚àÄ n : ‚Ñï, ContMDiff I I' n f :=
  ‚ü®fun h _ => h.of_le (mod_cast le_top), fun h x => contMDiffWithinAt_infty.2 fun n => h n x‚ü©

theorem contMDiffWithinAt_iff_nat {n : ‚Ñï‚àû} :
    ContMDiffWithinAt I I' n f s x ‚Üî ‚àÄ m : ‚Ñï, (m : ‚Ñï‚àû) ‚â§ n ‚Üí ContMDiffWithinAt I I' m f s x := by
  refine ‚ü®fun h m hm => h.of_le (mod_cast hm), fun h => ?_‚ü©
  obtain - | n := n
  ¬∑ exact contMDiffWithinAt_infty.2 fun n => h n le_top
  ¬∑ exact h n le_rfl

theorem contMDiffAt_iff_nat {n : ‚Ñï‚àû} :
    ContMDiffAt I I' n f x ‚Üî ‚àÄ m : ‚Ñï, (m : ‚Ñï‚àû) ‚â§ n ‚Üí ContMDiffAt I I' m f x := by
  simp [‚Üê contMDiffWithinAt_univ, contMDiffWithinAt_iff_nat]

/-- A function is `C^n` within a set at a point iff it is `C^m` within this set at this point, for
any `m ‚â§ n` which is different from `‚àû`. This result is useful because, when `m ‚â† ‚àû`, being
`C^m` extends locally to a neighborhood, giving flexibility for local proofs. -/
theorem contMDiffWithinAt_iff_le_ne_infty :
    ContMDiffWithinAt I I' n f s x ‚Üî ‚àÄ m, m ‚â§ n ‚Üí m ‚â† ‚àû ‚Üí ContMDiffWithinAt I I' m f s x := by
  refine ‚ü®fun h m hm h'm ‚Ü¶ h.of_le hm, fun h ‚Ü¶ ?_‚ü©
  cases n with
  | top =>
    exact h _ le_rfl (by simp)
  | coe n =>
    exact contMDiffWithinAt_iff_nat.2 (fun m hm ‚Ü¶ h _ (mod_cast hm) (by simp))

/-- A function is `C^n`at a point iff it is `C^m`at this point, for
any `m ‚â§ n` which is different from `‚àû`. This result is useful because, when `m ‚â† ‚àû`, being
`C^m` extends locally to a neighborhood, giving flexibility for local proofs. -/
theorem contMDiffAt_iff_le_ne_infty :
    ContMDiffAt I I' n f x ‚Üî ‚àÄ m, m ‚â§ n ‚Üí m ‚â† ‚àû ‚Üí ContMDiffAt I I' m f x := by
  simp only [‚Üê contMDiffWithinAt_univ]
  rw [contMDiffWithinAt_iff_le_ne_infty]

/-! ### Restriction to a smaller set -/

theorem ContMDiffWithinAt.mono_of_mem_nhdsWithin
    (hf : ContMDiffWithinAt I I' n f s x) (hts : s ‚àà ùìù[t] x) :
    ContMDiffWithinAt I I' n f t x :=
  StructureGroupoid.LocalInvariantProp.liftPropWithinAt_mono_of_mem_nhdsWithin
    (contDiffWithinAtProp_mono_of_mem_nhdsWithin n) hf hts

theorem ContMDiffWithinAt.mono (hf : ContMDiffWithinAt I I' n f s x) (hts : t ‚äÜ s) :
    ContMDiffWithinAt I I' n f t x :=
  hf.mono_of_mem_nhdsWithin <| mem_of_superset self_mem_nhdsWithin hts

theorem contMDiffWithinAt_congr_set (h : s =·∂†[ùìù x] t) :
    ContMDiffWithinAt I I' n f s x ‚Üî ContMDiffWithinAt I I' n f t x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_set h

theorem ContMDiffWithinAt.congr_set (h : ContMDiffWithinAt I I' n f s x) (hst : s =·∂†[ùìù x] t) :
    ContMDiffWithinAt I I' n f t x :=
  (contMDiffWithinAt_congr_set hst).1 h

theorem contMDiffWithinAt_insert_self :
    ContMDiffWithinAt I I' n f (insert x s) x ‚Üî ContMDiffWithinAt I I' n f s x := by
  simp only [contMDiffWithinAt_iff, continuousWithinAt_insert_self]
  refine Iff.rfl.and <| (contDiffWithinAt_congr_set ?_).trans contDiffWithinAt_insert_self
  simp only [‚Üê map_extChartAt_nhdsWithin, nhdsWithin_insert, Filter.map_sup, Filter.map_pure,
    ‚Üê nhdsWithin_eq_iff_eventuallyEq]

alias ‚ü®ContMDiffWithinAt.of_insert, _‚ü© := contMDiffWithinAt_insert_self

-- TODO: use `alias` again once it can make protected theorems
protected theorem ContMDiffWithinAt.insert (h : ContMDiffWithinAt I I' n f s x) :
    ContMDiffWithinAt I I' n f (insert x s) x :=
  contMDiffWithinAt_insert_self.2 h

/-- Being `C^n` in a set only depends on the germ of the set. Version where one only requires
the two sets to coincide locally in the complement of a point `y`. -/
theorem contMDiffWithinAt_congr_set' (y : M) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
    ContMDiffWithinAt I I' n f s x ‚Üî ContMDiffWithinAt I I' n f t x := by
  have : T1Space M := I.t1Space M
  rw [‚Üê contMDiffWithinAt_insert_self (s := s), ‚Üê contMDiffWithinAt_insert_self (s := t)]
  exact contMDiffWithinAt_congr_set (eventuallyEq_insert h)

protected theorem ContMDiffAt.contMDiffWithinAt (hf : ContMDiffAt I I' n f x) :
    ContMDiffWithinAt I I' n f s x :=
  ContMDiffWithinAt.mono hf (subset_univ _)

theorem ContMDiffOn.mono (hf : ContMDiffOn I I' n f s) (hts : t ‚äÜ s) : ContMDiffOn I I' n f t :=
  fun x hx => (hf x (hts hx)).mono hts

protected theorem ContMDiff.contMDiffOn (hf : ContMDiff I I' n f) : ContMDiffOn I I' n f s :=
  fun x _ => (hf x).contMDiffWithinAt

theorem contMDiffWithinAt_inter' (ht : t ‚àà ùìù[s] x) :
    ContMDiffWithinAt I I' n f (s ‚à© t) x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_inter' ht

theorem contMDiffWithinAt_inter (ht : t ‚àà ùìù x) :
    ContMDiffWithinAt I I' n f (s ‚à© t) x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_inter ht

protected theorem ContMDiffWithinAt.contMDiffAt
    (h : ContMDiffWithinAt I I' n f s x) (ht : s ‚àà ùìù x) :
    ContMDiffAt I I' n f x :=
  (contDiffWithinAt_localInvariantProp n).liftPropAt_of_liftPropWithinAt h ht

protected theorem ContMDiffOn.contMDiffAt (h : ContMDiffOn I I' n f s) (hx : s ‚àà ùìù x) :
    ContMDiffAt I I' n f x :=
  (h x (mem_of_mem_nhds hx)).contMDiffAt hx

theorem contMDiffOn_iff_source_of_mem_maximalAtlas [IsManifold I n M]
    (he : e ‚àà maximalAtlas I n M) (hs : s ‚äÜ e.source) :
    ContMDiffOn I I' n f s ‚Üî
      ContMDiffOn ùìò(ùïú, E) I' n (f ‚àò (e.extend I).symm) (e.extend I '' s) := by
  simp_rw [ContMDiffOn, Set.forall_mem_image]
  refine forall‚ÇÇ_congr fun x hx => ?_
  rw [contMDiffWithinAt_iff_source_of_mem_maximalAtlas he (hs hx)]
  apply contMDiffWithinAt_congr_set
  simp_rw [e.extend_symm_preimage_inter_range_eventuallyEq hs (hs hx)]

/-- A function is `C^n` within a set at a point, for `n : ‚Ñï` or `n = œâ`,
if and only if it is `C^n` on a neighborhood of this point. -/
theorem contMDiffWithinAt_iff_contMDiffOn_nhds
    [IsManifold I n M] [IsManifold I' n M'] (hn : n ‚â† ‚àû) :
    ContMDiffWithinAt I I' n f s x ‚Üî ‚àÉ u ‚àà ùìù[insert x s] x, ContMDiffOn I I' n f u := by
  -- WLOG, `x ‚àà s`, otherwise we add `x` to `s`
  wlog hxs : x ‚àà s generalizing s
  ¬∑ rw [‚Üê contMDiffWithinAt_insert_self, this (mem_insert _ _), insert_idem]
  rw [insert_eq_of_mem hxs]
  -- The `‚Üê` implication is trivial
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®u, hmem, hu‚ü© ‚Ü¶
    (hu _ (mem_of_mem_nhdsWithin hxs hmem)).mono_of_mem_nhdsWithin hmem‚ü©
  -- The property is true in charts. Let `v` be a good neighborhood in the chart where the function
  -- is `C‚Åø`.
  rcases (contMDiffWithinAt_iff'.1 h).2.contDiffOn le_rfl (by simp [hn]) with ‚ü®v, hmem, hsub, hv‚ü©
  have hxs' : extChartAt I x x ‚àà (extChartAt I x).target ‚à©
      (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' (f x)).source) :=
    ‚ü®(extChartAt I x).map_source (mem_extChartAt_source _), by rwa [extChartAt_to_inv], by
      rw [extChartAt_to_inv]; apply mem_extChartAt_source‚ü©
  rw [insert_eq_of_mem hxs'] at hmem hsub
  -- Then `(extChartAt I x).symm '' v` is the neighborhood we are looking for.
  refine ‚ü®(extChartAt I x).symm '' v, ?_, ?_‚ü©
  ¬∑ rw [‚Üê map_extChartAt_symm_nhdsWithin (I := I),
      h.1.nhdsWithin_extChartAt_symm_preimage_inter_range (I := I) (I' := I')]
    exact image_mem_map hmem
  ¬∑ have hv‚ÇÅ : (extChartAt I x).symm '' v ‚äÜ (extChartAt I x).source :=
      image_subset_iff.2 fun y hy ‚Ü¶ (extChartAt I x).map_target (hsub hy).1
    have hv‚ÇÇ : MapsTo f ((extChartAt I x).symm '' v) (extChartAt I' (f x)).source := by
      rintro _ ‚ü®y, hy, rfl‚ü©
      exact (hsub hy).2.2
    rwa [contMDiffOn_iff_of_subset_source' hv‚ÇÅ hv‚ÇÇ, PartialEquiv.image_symm_image_of_subset_target]
    exact hsub.trans inter_subset_left

/-- If a function is `C^m` within a set at a point, for some finite `m`, then it is `C^m` within
this set on an open set around the basepoint. -/
theorem ContMDiffWithinAt.contMDiffOn'
    [IsManifold I n M] [IsManifold I' n M']
    (hm : m ‚â§ n) (h' : m = ‚àû ‚Üí n = œâ)
    (h : ContMDiffWithinAt I I' n f s x) :
    ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ContMDiffOn I I' m f (insert x s ‚à© u) := by
  have : IsManifold I m M := .of_le hm
  have : IsManifold I' m M' := .of_le hm
  match m with
  | (m : ‚Ñï) | œâ =>
    rcases (contMDiffWithinAt_iff_contMDiffOn_nhds (by simp)).1 (h.of_le hm) with ‚ü®t, ht, h't‚ü©
    rcases mem_nhdsWithin.1 ht with ‚ü®u, u_open, xu, hu‚ü©
    rw [inter_comm] at hu
    exact ‚ü®u, u_open, xu, h't.mono hu‚ü©
  | ‚àû =>
    rcases (contMDiffWithinAt_iff_contMDiffOn_nhds (by simp [h'])).1 h with ‚ü®t, ht, h't‚ü©
    rcases mem_nhdsWithin.1 ht with ‚ü®u, u_open, xu, hu‚ü©
    rw [inter_comm] at hu
    exact ‚ü®u, u_open, xu, (h't.mono hu).of_le hm‚ü©

/-- If a function is `C^m` within a set at a point, for some finite `m`, then it is `C^m` within
this set on a neighborhood of the basepoint. -/
theorem ContMDiffWithinAt.contMDiffOn
    [IsManifold I n M] [IsManifold I' n M']
    (hm : m ‚â§ n) (h' : m = ‚àû ‚Üí n = œâ)
    (h : ContMDiffWithinAt I I' n f s x) :
    ‚àÉ u ‚àà ùìù[insert x s] x, u ‚äÜ insert x s ‚àß ContMDiffOn I I' m f u := by
  let ‚ü®_u, uo, xu, h‚ü© := h.contMDiffOn' hm h'
  exact ‚ü®_, inter_mem_nhdsWithin _ (uo.mem_nhds xu), inter_subset_left, h‚ü©

/-- A function is `C^n` at a point, for `n : ‚Ñï`, if and only if it is `C^n` on
a neighborhood of this point. -/
theorem contMDiffAt_iff_contMDiffOn_nhds
    [IsManifold I n M] [IsManifold I' n M'] (hn : n ‚â† ‚àû) :
    ContMDiffAt I I' n f x ‚Üî ‚àÉ u ‚àà ùìù x, ContMDiffOn I I' n f u := by
  simp [‚Üê contMDiffWithinAt_univ, contMDiffWithinAt_iff_contMDiffOn_nhds hn, nhdsWithin_univ]

/-- Note: This does not hold for `n = ‚àû`. `f` being `C^‚àû` at `x` means that for every `n`, `f` is
`C^n` on some neighborhood of `x`, but this neighborhood can depend on `n`. -/
theorem contMDiffAt_iff_contMDiffAt_nhds
    [IsManifold I n M] [IsManifold I' n M'] (hn : n ‚â† ‚àû) :
    ContMDiffAt I I' n f x ‚Üî ‚àÄ·∂† x' in ùìù x, ContMDiffAt I I' n f x' := by
  refine ‚ü®?_, fun h => h.self_of_nhds‚ü©
  rw [contMDiffAt_iff_contMDiffOn_nhds hn]
  rintro ‚ü®u, hu, h‚ü©
  refine (eventually_mem_nhds_iff.mpr hu).mono fun x' hx' => ?_
  exact (h x' <| mem_of_mem_nhds hx').contMDiffAt hx'

/-- Note: This does not hold for `n = ‚àû`. `f` being `C^‚àû` at `x` means that for every `n`, `f` is
`C^n` on some neighborhood of `x`, but this neighborhood can depend on `n`. -/
theorem contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin
    [IsManifold I n M] [IsManifold I' n M'] (hn : n ‚â† ‚àû) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ‚àÄ·∂† x' in ùìù[insert x s] x, ContMDiffWithinAt I I' n f s x' := by
  refine ‚ü®?_, fun h ‚Ü¶ mem_of_mem_nhdsWithin (mem_insert x s) h‚ü©
  rw [contMDiffWithinAt_iff_contMDiffOn_nhds hn]
  rintro ‚ü®u, hu, h‚ü©
  filter_upwards [hu, eventually_mem_nhdsWithin_iff.mpr hu] with x' h'x' hx'
  apply (h x' h'x').mono_of_mem_nhdsWithin
  exact nhdsWithin_mono _ (subset_insert x s) hx'

/-! ### Congruence lemmas -/

theorem ContMDiffWithinAt.congr (h : ContMDiffWithinAt I I' n f s x) (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y)
    (hx : f‚ÇÅ x = f x) : ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr h h‚ÇÅ hx

/-- Version of `ContMDiffWithinAt.congr` where `x` need not be contained in `s`,
but `f` and `f‚ÇÅ` are equal on a set containing both. -/
theorem ContMDiffWithinAt.congr' (h : ContMDiffWithinAt I I' n f s x) (h‚ÇÅ : ‚àÄ y ‚àà t, f‚ÇÅ y = f y)
    (hst : s ‚äÜ t) (hxt : x ‚àà t) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  h.congr (fun _y hy ‚Ü¶ h‚ÇÅ _ (hst hy)) (h‚ÇÅ x hxt)

theorem contMDiffWithinAt_congr (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) (hx : f‚ÇÅ x = f x) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_iff h‚ÇÅ hx

theorem ContMDiffWithinAt.congr_of_mem
    (h : ContMDiffWithinAt I I' n f s x) (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) (hx : x ‚àà s) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_of_mem h h‚ÇÅ hx

theorem contMDiffWithinAt_congr_of_mem (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) (hx : x ‚àà s) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_iff_of_mem h‚ÇÅ hx

theorem ContMDiffWithinAt.congr_of_eventuallyEq (h : ContMDiffWithinAt I I' n f s x)
    (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : f‚ÇÅ x = f x) : ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_of_eventuallyEq h h‚ÇÅ hx

theorem ContMDiffWithinAt.congr_of_eventuallyEq_of_mem (h : ContMDiffWithinAt I I' n f s x)
    (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : x ‚àà s) : ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_of_eventuallyEq_of_mem h h‚ÇÅ hx

theorem Filter.EventuallyEq.contMDiffWithinAt_iff (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : f‚ÇÅ x = f x) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_iff_of_eventuallyEq h‚ÇÅ hx

theorem ContMDiffAt.congr_of_eventuallyEq (h : ContMDiffAt I I' n f x) (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù x] f) :
    ContMDiffAt I I' n f‚ÇÅ x :=
  (contDiffWithinAt_localInvariantProp n).liftPropAt_congr_of_eventuallyEq h h‚ÇÅ

theorem Filter.EventuallyEq.contMDiffAt_iff (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù x] f) :
    ContMDiffAt I I' n f‚ÇÅ x ‚Üî ContMDiffAt I I' n f x :=
  (contDiffWithinAt_localInvariantProp n).liftPropAt_congr_iff_of_eventuallyEq h‚ÇÅ

theorem ContMDiffOn.congr (h : ContMDiffOn I I' n f s) (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) :
    ContMDiffOn I I' n f‚ÇÅ s :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_congr h h‚ÇÅ

theorem contMDiffOn_congr (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) :
    ContMDiffOn I I' n f‚ÇÅ s ‚Üî ContMDiffOn I I' n f s :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_congr_iff h‚ÇÅ

theorem ContMDiffOn.congr_mono (hf : ContMDiffOn I I' n f s) (h‚ÇÅ : ‚àÄ y ‚àà s‚ÇÅ, f‚ÇÅ y = f y)
    (hs : s‚ÇÅ ‚äÜ s) : ContMDiffOn I I' n f‚ÇÅ s‚ÇÅ :=
  (hf.mono hs).congr h‚ÇÅ

theorem ContMDiff.congr (h : ContMDiff I I' n f) (h‚ÇÅ : ‚àÄ y, f‚ÇÅ y = f y) :
    ContMDiff I I' n f‚ÇÅ := by
  rw [‚Üê contMDiffOn_univ] at h ‚ä¢
  exact (contMDiffOn_congr fun y _ ‚Ü¶ h‚ÇÅ y).mpr h

theorem contMDiff_congr (h‚ÇÅ : ‚àÄ y, f‚ÇÅ y = f y) :
    ContMDiff I I' n f‚ÇÅ ‚Üî ContMDiff I I' n f := by
  simp_rw [‚Üê contMDiffOn_univ]
  exact contMDiffOn_congr fun y _ ‚Ü¶ h‚ÇÅ y

/-! ### Locality -/


/-- Being `C^n` is a local property. -/
theorem contMDiffOn_of_locally_contMDiffOn
    (h : ‚àÄ x ‚àà s, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ContMDiffOn I I' n f (s ‚à© u)) : ContMDiffOn I I' n f s :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_of_locally_liftPropOn h

theorem contMDiff_of_locally_contMDiffOn (h : ‚àÄ x, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ContMDiffOn I I' n f u) :
    ContMDiff I I' n f :=
  (contDiffWithinAt_localInvariantProp n).liftProp_of_locally_liftPropOn h
