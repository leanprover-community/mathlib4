/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.SmoothManifoldWithCorners
import Mathlib.Geometry.Manifold.LocalInvariantProperties

#align_import geometry.manifold.cont_mdiff from "leanprover-community/mathlib"@"e5ab837fc252451f3eb9124ae6e7b6f57455e7b9"

/-!
# Smooth functions between smooth manifolds

We define `C‚Åø` functions between smooth manifolds, as functions which are `C‚Åø` in charts, and prove
basic properties of these notions.

## Main definitions and statements

Let `M` and `M'` be two smooth manifolds, with respect to model with corners `I` and `I'`. Let
`f : M ‚Üí M'`.

* `ContMDiffWithinAt I I' n f s x` states that the function `f` is `C‚Åø` within the set `s`
  around the point `x`.
* `ContMDiffAt I I' n f x` states that the function `f` is `C‚Åø` around `x`.
* `ContMDiffOn I I' n f s` states that the function `f` is `C‚Åø` on the set `s`
* `ContMDiff I I' n f` states that the function `f` is `C‚Åø`.

We also give some basic properties of smooth functions between manifolds, following the API of
smooth functions between vector spaces.
See `Basic.lean` for further basic properties of smooth functions between smooth manifolds,
`NormedSpace.lean` for the equivalence of manifold-smoothness to usual smoothness,
`Product.lean` for smoothness results related to the product of manifolds and
`Atlas.lean` for smoothness of atlas members and local structomorphisms.

## Implementation details

Many properties follow for free from the corresponding properties of functions in vector spaces,
as being `C‚Åø` is a local property invariant under the smooth groupoid. We take advantage of the
general machinery developed in `LocalInvariantProperties.lean` to get these properties
automatically. For instance, the fact that being `C‚Åø` does not depend on the chart one considers
is given by `liftPropWithinAt_indep_chart`.

For this to work, the definition of `ContMDiffWithinAt` and friends has to
follow definitionally the setup of local invariant properties. Still, we recast the definition
in terms of extended charts in `contMDiffOn_iff` and `contMDiff_iff`.
-/


open Set Function Filter ChartedSpace SmoothManifoldWithCorners

open scoped Topology Manifold

/-! ### Definition of smooth functions between manifolds -/


variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ùïú E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [SmoothManifoldWithCorners I M]
  -- declare a smooth manifold `M'` over the pair `(E', H')`.
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  (I' : ModelWithCorners ùïú E' H') {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  [SmoothManifoldWithCorners I' M']
  -- declare a manifold `M''` over the pair `(E'', H'')`.
  {E'' : Type*}
  [NormedAddCommGroup E''] [NormedSpace ùïú E''] {H'' : Type*} [TopologicalSpace H'']
  {I'' : ModelWithCorners ùïú E'' H''} {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ùïú F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ùïú F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [SmoothManifoldWithCorners J N]
  -- declare a smooth manifold `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ùïú F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ùïú F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  [SmoothManifoldWithCorners J' N']
  -- F‚ÇÅ, F‚ÇÇ, F‚ÇÉ, F‚ÇÑ are normed spaces
  {F‚ÇÅ : Type*}
  [NormedAddCommGroup F‚ÇÅ] [NormedSpace ùïú F‚ÇÅ] {F‚ÇÇ : Type*} [NormedAddCommGroup F‚ÇÇ]
  [NormedSpace ùïú F‚ÇÇ] {F‚ÇÉ : Type*} [NormedAddCommGroup F‚ÇÉ] [NormedSpace ùïú F‚ÇÉ] {F‚ÇÑ : Type*}
  [NormedAddCommGroup F‚ÇÑ] [NormedSpace ùïú F‚ÇÑ]
  -- declare functions, sets, points and smoothness indices
  {e : PartialHomeomorph M H}
  {e' : PartialHomeomorph M' H'} {f f‚ÇÅ : M ‚Üí M'} {s s‚ÇÅ t : Set M} {x : M} {m n : ‚Ñï‚àû}

/-- Property in the model space of a model with corners of being `C^n` within at set at a point,
when read in the model vector space. This property will be lifted to manifolds to define smooth
functions between manifolds. -/
def ContDiffWithinAtProp (n : ‚Ñï‚àû) (f : H ‚Üí H') (s : Set H) (x : H) : Prop :=
  ContDiffWithinAt ùïú n (I' ‚àò f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I) (I x)
#align cont_diff_within_at_prop ContDiffWithinAtProp

theorem contDiffWithinAtProp_self_source {f : E ‚Üí H'} {s : Set E} {x : E} :
    ContDiffWithinAtProp ùìò(ùïú, E) I' n f s x ‚Üî ContDiffWithinAt ùïú n (I' ‚àò f) s x := by
  simp_rw [ContDiffWithinAtProp, modelWithCornersSelf_coe, range_id, inter_univ,
    modelWithCornersSelf_coe_symm, CompTriple.comp_eq, preimage_id_eq, id_eq]
#align cont_diff_within_at_prop_self_source contDiffWithinAtProp_self_source

theorem contDiffWithinAtProp_self {f : E ‚Üí E'} {s : Set E} {x : E} :
    ContDiffWithinAtProp ùìò(ùïú, E) ùìò(ùïú, E') n f s x ‚Üî ContDiffWithinAt ùïú n f s x :=
  contDiffWithinAtProp_self_source ùìò(ùïú, E')
#align cont_diff_within_at_prop_self contDiffWithinAtProp_self

theorem contDiffWithinAtProp_self_target {f : H ‚Üí E'} {s : Set H} {x : H} :
    ContDiffWithinAtProp I ùìò(ùïú, E') n f s x ‚Üî
      ContDiffWithinAt ùïú n (f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I) (I x) :=
  Iff.rfl
#align cont_diff_within_at_prop_self_target contDiffWithinAtProp_self_target

/-- Being `C‚Åø` in the model space is a local property, invariant under smooth maps. Therefore,
it will lift nicely to manifolds. -/
theorem contDiffWithinAt_localInvariantProp (n : ‚Ñï‚àû) :
    (contDiffGroupoid ‚àû I).LocalInvariantProp (contDiffGroupoid ‚àû I')
      (ContDiffWithinAtProp I I' n) where
  is_local {s x u f} u_open xu := by
    have : I.symm ‚Åª¬π' (s ‚à© u) ‚à© range I = I.symm ‚Åª¬π' s ‚à© range I ‚à© I.symm ‚Åª¬π' u := by
      simp only [inter_right_comm, preimage_inter]
    rw [ContDiffWithinAtProp, ContDiffWithinAtProp, this]
    symm
    apply contDiffWithinAt_inter
    have : u ‚àà ùìù (I.symm (I x)) := by
      rw [ModelWithCorners.left_inv]
      exact u_open.mem_nhds xu
    apply ContinuousAt.preimage_mem_nhds I.continuous_symm.continuousAt this
  right_invariance' {s x f e} he hx h := by
    rw [ContDiffWithinAtProp] at h ‚ä¢
    have : I x = (I ‚àò e.symm ‚àò I.symm) (I (e x)) := by simp only [hx, mfld_simps]
    rw [this] at h
    have : I (e x) ‚àà I.symm ‚Åª¬π' e.target ‚à© range I := by simp only [hx, mfld_simps]
    have := (mem_groupoid_of_pregroupoid.2 he).2.contDiffWithinAt this
    convert (h.comp' _ (this.of_le le_top)).mono_of_mem _ using 1
    ¬∑ ext y; simp only [mfld_simps]
    refine mem_nhdsWithin.mpr
      ‚ü®I.symm ‚Åª¬π' e.target, e.open_target.preimage I.continuous_symm, by
        simp_rw [mem_preimage, I.left_inv, e.mapsTo hx], ?_‚ü©
    mfld_set_tac
  congr_of_forall {s x f g} h hx hf := by
    apply hf.congr
    ¬∑ intro y hy
      simp only [mfld_simps] at hy
      simp only [h, hy, mfld_simps]
    ¬∑ simp only [hx, mfld_simps]
  left_invariance' {s x f e'} he' hs hx h := by
    rw [ContDiffWithinAtProp] at h ‚ä¢
    have A : (I' ‚àò f ‚àò I.symm) (I x) ‚àà I'.symm ‚Åª¬π' e'.source ‚à© range I' := by
      simp only [hx, mfld_simps]
    have := (mem_groupoid_of_pregroupoid.2 he').1.contDiffWithinAt A
    convert (this.of_le le_top).comp _ h _
    ¬∑ ext y; simp only [mfld_simps]
    ¬∑ intro y hy; simp only [mfld_simps] at hy; simpa only [hy, mfld_simps] using hs hy.1
#align cont_diff_within_at_local_invariant_prop contDiffWithinAt_localInvariantProp

theorem contDiffWithinAtProp_mono_of_mem (n : ‚Ñï‚àû) ‚¶És x t‚¶Ñ ‚¶Éf : H ‚Üí H'‚¶Ñ (hts : s ‚àà ùìù[t] x)
    (h : ContDiffWithinAtProp I I' n f s x) : ContDiffWithinAtProp I I' n f t x := by
  refine h.mono_of_mem ?_
  refine inter_mem ?_ (mem_of_superset self_mem_nhdsWithin inter_subset_right)
  rwa [‚Üê Filter.mem_map, ‚Üê I.image_eq, I.symm_map_nhdsWithin_image]
#align cont_diff_within_at_prop_mono_of_mem contDiffWithinAtProp_mono_of_mem

theorem contDiffWithinAtProp_id (x : H) : ContDiffWithinAtProp I I n id univ x := by
  simp only [ContDiffWithinAtProp, id_comp, preimage_univ, univ_inter]
  have : ContDiffWithinAt ùïú n id (range I) (I x) := contDiff_id.contDiffAt.contDiffWithinAt
  refine this.congr (fun y hy => ?_) ?_
  ¬∑ simp only [ModelWithCorners.right_inv I hy, mfld_simps]
  ¬∑ simp only [mfld_simps]
#align cont_diff_within_at_prop_id contDiffWithinAtProp_id

/-- A function is `n` times continuously differentiable within a set at a point in a manifold if
it is continuous and it is `n` times continuously differentiable in this set around this point, when
read in the preferred chart at this point. -/
def ContMDiffWithinAt (n : ‚Ñï‚àû) (f : M ‚Üí M') (s : Set M) (x : M) :=
  LiftPropWithinAt (ContDiffWithinAtProp I I' n) f s x
#align cont_mdiff_within_at ContMDiffWithinAt

/-- Abbreviation for `ContMDiffWithinAt I I' ‚ä§ f s x`. See also documentation for `Smooth`.
-/
abbrev SmoothWithinAt (f : M ‚Üí M') (s : Set M) (x : M) :=
  ContMDiffWithinAt I I' ‚ä§ f s x
#align smooth_within_at SmoothWithinAt

/-- A function is `n` times continuously differentiable at a point in a manifold if
it is continuous and it is `n` times continuously differentiable around this point, when
read in the preferred chart at this point. -/
def ContMDiffAt (n : ‚Ñï‚àû) (f : M ‚Üí M') (x : M) :=
  ContMDiffWithinAt I I' n f univ x
#align cont_mdiff_at ContMDiffAt

theorem contMDiffAt_iff {n : ‚Ñï‚àû} {f : M ‚Üí M'} {x : M} :
    ContMDiffAt I I' n f x ‚Üî
      ContinuousAt f x ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm) (range I)
          (extChartAt I x x) :=
  liftPropAt_iff.trans <| by rw [ContDiffWithinAtProp, preimage_univ, univ_inter]; rfl
#align cont_mdiff_at_iff contMDiffAt_iff

/-- Abbreviation for `ContMDiffAt I I' ‚ä§ f x`. See also documentation for `Smooth`. -/
abbrev SmoothAt (f : M ‚Üí M') (x : M) :=
  ContMDiffAt I I' ‚ä§ f x
#align smooth_at SmoothAt

/-- A function is `n` times continuously differentiable in a set of a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable on this set in the charts
around these points. -/
def ContMDiffOn (n : ‚Ñï‚àû) (f : M ‚Üí M') (s : Set M) :=
  ‚àÄ x ‚àà s, ContMDiffWithinAt I I' n f s x
#align cont_mdiff_on ContMDiffOn

/-- Abbreviation for `ContMDiffOn I I' ‚ä§ f s`. See also documentation for `Smooth`. -/
abbrev SmoothOn (f : M ‚Üí M') (s : Set M) :=
  ContMDiffOn I I' ‚ä§ f s
#align smooth_on SmoothOn

/-- A function is `n` times continuously differentiable in a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable in the charts
around these points. -/
def ContMDiff (n : ‚Ñï‚àû) (f : M ‚Üí M') :=
  ‚àÄ x, ContMDiffAt I I' n f x
#align cont_mdiff ContMDiff

/-- Abbreviation for `ContMDiff I I' ‚ä§ f`.
Short note to work with these abbreviations: a lemma of the form `ContMDiffFoo.bar` will
apply fine to an assumption `SmoothFoo` using dot notation or normal notation.
If the consequence `bar` of the lemma involves `ContDiff`, it is still better to restate
the lemma replacing `ContDiff` with `Smooth` both in the assumption and in the conclusion,
to make it possible to use `Smooth` consistently.
This also applies to `SmoothAt`, `SmoothOn` and `SmoothWithinAt`. -/
abbrev Smooth (f : M ‚Üí M') :=
  ContMDiff I I' ‚ä§ f
#align smooth Smooth

variable {I I'}

/-! ### Deducing smoothness from higher smoothness -/

theorem ContMDiffWithinAt.of_le (hf : ContMDiffWithinAt I I' n f s x) (le : m ‚â§ n) :
    ContMDiffWithinAt I I' m f s x := by
  simp only [ContMDiffWithinAt, LiftPropWithinAt] at hf ‚ä¢
  exact ‚ü®hf.1, hf.2.of_le le‚ü©
#align cont_mdiff_within_at.of_le ContMDiffWithinAt.of_le

theorem ContMDiffAt.of_le (hf : ContMDiffAt I I' n f x) (le : m ‚â§ n) : ContMDiffAt I I' m f x :=
  ContMDiffWithinAt.of_le hf le
#align cont_mdiff_at.of_le ContMDiffAt.of_le

theorem ContMDiffOn.of_le (hf : ContMDiffOn I I' n f s) (le : m ‚â§ n) : ContMDiffOn I I' m f s :=
  fun x hx => (hf x hx).of_le le
#align cont_mdiff_on.of_le ContMDiffOn.of_le

theorem ContMDiff.of_le (hf : ContMDiff I I' n f) (le : m ‚â§ n) : ContMDiff I I' m f := fun x =>
  (hf x).of_le le
#align cont_mdiff.of_le ContMDiff.of_le

/-! ### Basic properties of smooth functions between manifolds -/

theorem ContMDiff.smooth (h : ContMDiff I I' ‚ä§ f) : Smooth I I' f :=
  h
#align cont_mdiff.smooth ContMDiff.smooth

theorem Smooth.contMDiff (h : Smooth I I' f) : ContMDiff I I' n f :=
  h.of_le le_top
#align smooth.cont_mdiff Smooth.contMDiff

theorem ContMDiffOn.smoothOn (h : ContMDiffOn I I' ‚ä§ f s) : SmoothOn I I' f s :=
  h
#align cont_mdiff_on.smooth_on ContMDiffOn.smoothOn

theorem SmoothOn.contMDiffOn (h : SmoothOn I I' f s) : ContMDiffOn I I' n f s :=
  h.of_le le_top
#align smooth_on.cont_mdiff_on SmoothOn.contMDiffOn

theorem ContMDiffAt.smoothAt (h : ContMDiffAt I I' ‚ä§ f x) : SmoothAt I I' f x :=
  h
#align cont_mdiff_at.smooth_at ContMDiffAt.smoothAt

theorem SmoothAt.contMDiffAt (h : SmoothAt I I' f x) : ContMDiffAt I I' n f x :=
  h.of_le le_top
#align smooth_at.cont_mdiff_at SmoothAt.contMDiffAt

theorem ContMDiffWithinAt.smoothWithinAt (h : ContMDiffWithinAt I I' ‚ä§ f s x) :
    SmoothWithinAt I I' f s x :=
  h
#align cont_mdiff_within_at.smooth_within_at ContMDiffWithinAt.smoothWithinAt

theorem SmoothWithinAt.contMDiffWithinAt (h : SmoothWithinAt I I' f s x) :
    ContMDiffWithinAt I I' n f s x :=
  h.of_le le_top
#align smooth_within_at.cont_mdiff_within_at SmoothWithinAt.contMDiffWithinAt

theorem ContMDiff.contMDiffAt (h : ContMDiff I I' n f) : ContMDiffAt I I' n f x :=
  h x
#align cont_mdiff.cont_mdiff_at ContMDiff.contMDiffAt

theorem Smooth.smoothAt (h : Smooth I I' f) : SmoothAt I I' f x :=
  ContMDiff.contMDiffAt h
#align smooth.smooth_at Smooth.smoothAt

theorem contMDiffWithinAt_univ : ContMDiffWithinAt I I' n f univ x ‚Üî ContMDiffAt I I' n f x :=
  Iff.rfl
#align cont_mdiff_within_at_univ contMDiffWithinAt_univ

theorem smoothWithinAt_univ : SmoothWithinAt I I' f univ x ‚Üî SmoothAt I I' f x :=
  contMDiffWithinAt_univ
#align smooth_within_at_univ smoothWithinAt_univ

theorem contMDiffOn_univ : ContMDiffOn I I' n f univ ‚Üî ContMDiff I I' n f := by
  simp only [ContMDiffOn, ContMDiff, contMDiffWithinAt_univ, forall_prop_of_true, mem_univ]
#align cont_mdiff_on_univ contMDiffOn_univ

theorem smoothOn_univ : SmoothOn I I' f univ ‚Üî Smooth I I' f :=
  contMDiffOn_univ
#align smooth_on_univ smoothOn_univ

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart. -/
theorem contMDiffWithinAt_iff :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x) := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff']; rfl
#align cont_mdiff_within_at_iff contMDiffWithinAt_iff

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart. This form states smoothness of `f`
written in such a way that the set is restricted to lie within the domain/codomain of the
corresponding charts.
Even though this expression is more complicated than the one in `contMDiffWithinAt_iff`, it is
a smaller set, but their germs at `extChartAt I x x` are equal. It is sometimes useful to rewrite
using this in the goal.
-/
theorem contMDiffWithinAt_iff' :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).target ‚à©
            (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' (f x)).source))
          (extChartAt I x x) := by
  simp only [ContMDiffWithinAt, liftPropWithinAt_iff']
  exact and_congr_right fun hc => contDiffWithinAt_congr_nhds <|
    hc.nhdsWithin_extChartAt_symm_preimage_inter_range I I'
#align cont_mdiff_within_at_iff' contMDiffWithinAt_iff'

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart in the target. -/
theorem contMDiffWithinAt_iff_target :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß ContMDiffWithinAt I ùìò(ùïú, E') n (extChartAt I' (f x) ‚àò f) s x := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff', ‚Üê and_assoc]
  have cont :
    ContinuousWithinAt f s x ‚àß ContinuousWithinAt (extChartAt I' (f x) ‚àò f) s x ‚Üî
        ContinuousWithinAt f s x :=
      and_iff_left_of_imp <| (continuousAt_extChartAt _ _).comp_continuousWithinAt
  simp_rw [cont, ContDiffWithinAtProp, extChartAt, PartialHomeomorph.extend, PartialEquiv.coe_trans,
    ModelWithCorners.toPartialEquiv_coe, PartialHomeomorph.coe_coe, modelWithCornersSelf_coe,
    chartAt_self_eq, PartialHomeomorph.refl_apply, id_comp]
  rfl
#align cont_mdiff_within_at_iff_target contMDiffWithinAt_iff_target

theorem smoothWithinAt_iff :
    SmoothWithinAt I I' f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú ‚àû (extChartAt I' (f x) ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x) :=
  contMDiffWithinAt_iff
#align smooth_within_at_iff smoothWithinAt_iff

theorem smoothWithinAt_iff_target :
    SmoothWithinAt I I' f s x ‚Üî
      ContinuousWithinAt f s x ‚àß SmoothWithinAt I ùìò(ùïú, E') (extChartAt I' (f x) ‚àò f) s x :=
  contMDiffWithinAt_iff_target
#align smooth_within_at_iff_target smoothWithinAt_iff_target

theorem contMDiffAt_iff_target {x : M} :
    ContMDiffAt I I' n f x ‚Üî
      ContinuousAt f x ‚àß ContMDiffAt I ùìò(ùïú, E') n (extChartAt I' (f x) ‚àò f) x := by
  rw [ContMDiffAt, ContMDiffAt, contMDiffWithinAt_iff_target, continuousWithinAt_univ]
#align cont_mdiff_at_iff_target contMDiffAt_iff_target

theorem smoothAt_iff_target {x : M} :
    SmoothAt I I' f x ‚Üî ContinuousAt f x ‚àß SmoothAt I ùìò(ùïú, E') (extChartAt I' (f x) ‚àò f) x :=
  contMDiffAt_iff_target
#align smooth_at_iff_target smoothAt_iff_target

theorem contMDiffWithinAt_iff_of_mem_maximalAtlas {x : M} (he : e ‚àà maximalAtlas I M)
    (he' : e' ‚àà maximalAtlas I' M') (hx : x ‚àà e.source) (hy : f x ‚àà e'.source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm)
          ((e.extend I).symm ‚Åª¬π' s ‚à© range I) (e.extend I x) :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_indep_chart he hx he' hy
#align cont_mdiff_within_at_iff_of_mem_maximal_atlas contMDiffWithinAt_iff_of_mem_maximalAtlas

/-- An alternative formulation of `contMDiffWithinAt_iff_of_mem_maximalAtlas`
  if the set if `s` lies in `e.source`. -/
theorem contMDiffWithinAt_iff_image {x : M} (he : e ‚àà maximalAtlas I M)
    (he' : e' ‚àà maximalAtlas I' M') (hs : s ‚äÜ e.source) (hx : x ‚àà e.source) (hy : f x ‚àà e'.source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß
        ContDiffWithinAt ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm) (e.extend I '' s)
          (e.extend I x) := by
  rw [contMDiffWithinAt_iff_of_mem_maximalAtlas he he' hx hy, and_congr_right_iff]
  refine fun _ => contDiffWithinAt_congr_nhds ?_
  simp_rw [nhdsWithin_eq_iff_eventuallyEq, e.extend_symm_preimage_inter_range_eventuallyEq I hs hx]
#align cont_mdiff_within_at_iff_image contMDiffWithinAt_iff_image

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in any chart containing that point. -/
theorem contMDiffWithinAt_iff_of_mem_source {x' : M} {y : M'} (hx : x' ‚àà (chartAt H x).source)
    (hy : f x' ‚àà (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x' ‚Üî
      ContinuousWithinAt f s x' ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x') :=
  contMDiffWithinAt_iff_of_mem_maximalAtlas (chart_mem_maximalAtlas _ x)
    (chart_mem_maximalAtlas _ y) hx hy
#align cont_mdiff_within_at_iff_of_mem_source contMDiffWithinAt_iff_of_mem_source

theorem contMDiffWithinAt_iff_of_mem_source' {x' : M} {y : M'} (hx : x' ‚àà (chartAt H x).source)
    (hy : f x' ‚àà (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x' ‚Üî
      ContinuousWithinAt f s x' ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
          ((extChartAt I x).target ‚à© (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' y).source))
          (extChartAt I x x') := by
  refine (contMDiffWithinAt_iff_of_mem_source hx hy).trans ?_
  rw [‚Üê extChartAt_source I] at hx
  rw [‚Üê extChartAt_source I'] at hy
  rw [and_congr_right_iff]
  set e := extChartAt I x; set e' := extChartAt I' (f x)
  refine fun hc => contDiffWithinAt_congr_nhds ?_
  rw [‚Üê e.image_source_inter_eq', ‚Üê map_extChartAt_nhdsWithin_eq_image' I hx, ‚Üê
    map_extChartAt_nhdsWithin' I hx, inter_comm, nhdsWithin_inter_of_mem]
  exact hc (extChartAt_source_mem_nhds' _ hy)
#align cont_mdiff_within_at_iff_of_mem_source' contMDiffWithinAt_iff_of_mem_source'

theorem contMDiffAt_iff_of_mem_source {x' : M} {y : M'} (hx : x' ‚àà (chartAt H x).source)
    (hy : f x' ‚àà (chartAt H' y).source) :
    ContMDiffAt I I' n f x' ‚Üî
      ContinuousAt f x' ‚àß
        ContDiffWithinAt ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm) (range I)
          (extChartAt I x x') :=
  (contMDiffWithinAt_iff_of_mem_source hx hy).trans <| by
    rw [continuousWithinAt_univ, preimage_univ, univ_inter]
#align cont_mdiff_at_iff_of_mem_source contMDiffAt_iff_of_mem_source

theorem contMDiffWithinAt_iff_target_of_mem_source {x : M} {y : M'}
    (hy : f x ‚àà (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContinuousWithinAt f s x ‚àß ContMDiffWithinAt I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) s x := by
  simp_rw [ContMDiffWithinAt]
  rw [(contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_indep_chart_target
      (chart_mem_maximalAtlas I' y) hy,
    and_congr_right]
  intro hf
  simp_rw [StructureGroupoid.liftPropWithinAt_self_target]
  simp_rw [((chartAt H' y).continuousAt hy).comp_continuousWithinAt hf]
  rw [‚Üê extChartAt_source I'] at hy
  simp_rw [(continuousAt_extChartAt' I' hy).comp_continuousWithinAt hf]
  rfl
#align cont_mdiff_within_at_iff_target_of_mem_source contMDiffWithinAt_iff_target_of_mem_source

theorem contMDiffAt_iff_target_of_mem_source {x : M} {y : M'} (hy : f x ‚àà (chartAt H' y).source) :
    ContMDiffAt I I' n f x ‚Üî
      ContinuousAt f x ‚àß ContMDiffAt I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) x := by
  rw [ContMDiffAt, contMDiffWithinAt_iff_target_of_mem_source hy, continuousWithinAt_univ,
    ContMDiffAt]
#align cont_mdiff_at_iff_target_of_mem_source contMDiffAt_iff_target_of_mem_source

theorem contMDiffWithinAt_iff_source_of_mem_maximalAtlas (he : e ‚àà maximalAtlas I M)
    (hx : x ‚àà e.source) :
    ContMDiffWithinAt I I' n f s x ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (e.extend I).symm) ((e.extend I).symm ‚Åª¬π' s ‚à© range I)
        (e.extend I x) := by
  have h2x := hx; rw [‚Üê e.extend_source I] at h2x
  simp_rw [ContMDiffWithinAt,
    (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_indep_chart_source he hx,
    StructureGroupoid.liftPropWithinAt_self_source,
    e.extend_symm_continuousWithinAt_comp_right_iff, contDiffWithinAtProp_self_source,
    ContDiffWithinAtProp, Function.comp, e.left_inv hx, (e.extend I).left_inv h2x]
  rfl
#align cont_mdiff_within_at_iff_source_of_mem_maximal_atlas contMDiffWithinAt_iff_source_of_mem_maximalAtlas

theorem contMDiffWithinAt_iff_source_of_mem_source {x' : M} (hx' : x' ‚àà (chartAt H x).source) :
    ContMDiffWithinAt I I' n f s x' ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (extChartAt I x).symm)
        ((extChartAt I x).symm ‚Åª¬π' s ‚à© range I) (extChartAt I x x') :=
  contMDiffWithinAt_iff_source_of_mem_maximalAtlas (chart_mem_maximalAtlas I x) hx'
#align cont_mdiff_within_at_iff_source_of_mem_source contMDiffWithinAt_iff_source_of_mem_source

theorem contMDiffAt_iff_source_of_mem_source {x' : M} (hx' : x' ‚àà (chartAt H x).source) :
    ContMDiffAt I I' n f x' ‚Üî
      ContMDiffWithinAt ùìò(ùïú, E) I' n (f ‚àò (extChartAt I x).symm) (range I) (extChartAt I x x') := by
  simp_rw [ContMDiffAt, contMDiffWithinAt_iff_source_of_mem_source hx', preimage_univ, univ_inter]
#align cont_mdiff_at_iff_source_of_mem_source contMDiffAt_iff_source_of_mem_source

theorem contMDiffOn_iff_of_mem_maximalAtlas (he : e ‚àà maximalAtlas I M)
    (he' : e' ‚àà maximalAtlas I' M') (hs : s ‚äÜ e.source) (h2s : MapsTo f s e'.source) :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ContDiffOn ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm) (e.extend I '' s) := by
  simp_rw [ContinuousOn, ContDiffOn, Set.forall_mem_image, ‚Üê forall_and, ContMDiffOn]
  exact forall‚ÇÇ_congr fun x hx => contMDiffWithinAt_iff_image he he' hs (hs hx) (h2s hx)
#align cont_mdiff_on_iff_of_mem_maximal_atlas contMDiffOn_iff_of_mem_maximalAtlas

theorem contMDiffOn_iff_of_mem_maximalAtlas' (he : e ‚àà maximalAtlas I M)
    (he' : e' ‚àà maximalAtlas I' M') (hs : s ‚äÜ e.source) (h2s : MapsTo f s e'.source) :
    ContMDiffOn I I' n f s ‚Üî
      ContDiffOn ùïú n (e'.extend I' ‚àò f ‚àò (e.extend I).symm) (e.extend I '' s) :=
  (contMDiffOn_iff_of_mem_maximalAtlas he he' hs h2s).trans <| and_iff_right_of_imp fun h ‚Ü¶
    (e.continuousOn_writtenInExtend_iff _ _ hs h2s).1 h.continuousOn

/-- If the set where you want `f` to be smooth lies entirely in a single chart, and `f` maps it
  into a single chart, the smoothness of `f` on that set can be expressed by purely looking in
  these charts.
  Note: this lemma uses `extChartAt I x '' s` instead of `(extChartAt I x).symm ‚Åª¬π' s` to ensure
  that this set lies in `(extChartAt I x).target`. -/
theorem contMDiffOn_iff_of_subset_source {x : M} {y : M'} (hs : s ‚äÜ (chartAt H x).source)
    (h2s : MapsTo f s (chartAt H' y).source) :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm) (extChartAt I x '' s) :=
  contMDiffOn_iff_of_mem_maximalAtlas (chart_mem_maximalAtlas I x) (chart_mem_maximalAtlas I' y) hs
    h2s
#align cont_mdiff_on_iff_of_subset_source contMDiffOn_iff_of_subset_source

/-- If the set where you want `f` to be smooth lies entirely in a single chart, and `f` maps it
  into a single chart, the smoothness of `f` on that set can be expressed by purely looking in
  these charts.
  Note: this lemma uses `extChartAt I x '' s` instead of `(extChartAt I x).symm ‚Åª¬π' s` to ensure
  that this set lies in `(extChartAt I x).target`. -/
theorem contMDiffOn_iff_of_subset_source' {x : M} {y : M'} (hs : s ‚äÜ (extChartAt I x).source)
    (h2s : MapsTo f s (extChartAt I' y).source) :
    ContMDiffOn I I' n f s ‚Üî
        ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm) (extChartAt I x '' s) := by
  rw [extChartAt_source] at hs h2s
  exact contMDiffOn_iff_of_mem_maximalAtlas' (chart_mem_maximalAtlas I x)
    (chart_mem_maximalAtlas I' y) hs h2s

/-- One can reformulate smoothness on a set as continuity on this set, and smoothness in any
extended chart. -/
theorem contMDiffOn_iff :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ‚àÄ (x : M) (y : M'),
          ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
            ((extChartAt I x).target ‚à©
              (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' y).source)) := by
  constructor
  ¬∑ intro h
    refine ‚ü®fun x hx => (h x hx).1, fun x y z hz => ?_‚ü©
    simp only [mfld_simps] at hz
    let w := (extChartAt I x).symm z
    have : w ‚àà s := by simp only [w, hz, mfld_simps]
    specialize h w this
    have w1 : w ‚àà (chartAt H x).source := by simp only [w, hz, mfld_simps]
    have w2 : f w ‚àà (chartAt H' y).source := by simp only [w, hz, mfld_simps]
    convert ((contMDiffWithinAt_iff_of_mem_source w1 w2).mp h).2.mono _
    ¬∑ simp only [w, hz, mfld_simps]
    ¬∑ mfld_set_tac
  ¬∑ rintro ‚ü®hcont, hdiff‚ü© x hx
    refine (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_iff.mpr ?_
    refine ‚ü®hcont x hx, ?_‚ü©
    dsimp [ContDiffWithinAtProp]
    convert hdiff x (f x) (extChartAt I x x) (by simp only [hx, mfld_simps]) using 1
    mfld_set_tac
#align cont_mdiff_on_iff contMDiffOn_iff

/-- One can reformulate smoothness on a set as continuity on this set, and smoothness in any
extended chart in the target. -/
theorem contMDiffOn_iff_target :
    ContMDiffOn I I' n f s ‚Üî
      ContinuousOn f s ‚àß
        ‚àÄ y : M',
          ContMDiffOn I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) (s ‚à© f ‚Åª¬π' (extChartAt I' y).source) := by
  simp only [contMDiffOn_iff, ModelWithCorners.source_eq, chartAt_self_eq,
    PartialHomeomorph.refl_partialEquiv, PartialEquiv.refl_trans, extChartAt,
    PartialHomeomorph.extend, Set.preimage_univ, Set.inter_univ, and_congr_right_iff]
  intro h
  constructor
  ¬∑ refine fun h' y => ‚ü®?_, fun x _ => h' x y‚ü©
    have h'' : ContinuousOn _ univ := (ModelWithCorners.continuous I').continuousOn
    convert (h''.comp' (chartAt H' y).continuousOn_toFun).comp' h
    simp
  ¬∑ exact fun h' x y => (h' y).2 x 0
#align cont_mdiff_on_iff_target contMDiffOn_iff_target

theorem smoothOn_iff :
    SmoothOn I I' f s ‚Üî
      ContinuousOn f s ‚àß
        ‚àÄ (x : M) (y : M'),
          ContDiffOn ùïú ‚ä§ (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
            ((extChartAt I x).target ‚à©
              (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' y).source)) :=
  contMDiffOn_iff
#align smooth_on_iff smoothOn_iff

theorem smoothOn_iff_target :
    SmoothOn I I' f s ‚Üî
      ContinuousOn f s ‚àß
        ‚àÄ y : M', SmoothOn I ùìò(ùïú, E') (extChartAt I' y ‚àò f) (s ‚à© f ‚Åª¬π' (extChartAt I' y).source) :=
  contMDiffOn_iff_target
#align smooth_on_iff_target smoothOn_iff_target

/-- One can reformulate smoothness as continuity and smoothness in any extended chart. -/
theorem contMDiff_iff :
    ContMDiff I I' n f ‚Üî
      Continuous f ‚àß
        ‚àÄ (x : M) (y : M'),
          ContDiffOn ùïú n (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
            ((extChartAt I x).target ‚à©
              (extChartAt I x).symm ‚Åª¬π' (f ‚Åª¬π' (extChartAt I' y).source)) := by
  simp [‚Üê contMDiffOn_univ, contMDiffOn_iff, continuous_iff_continuousOn_univ]
#align cont_mdiff_iff contMDiff_iff

/-- One can reformulate smoothness as continuity and smoothness in any extended chart in the
target. -/
theorem contMDiff_iff_target :
    ContMDiff I I' n f ‚Üî
      Continuous f ‚àß ‚àÄ y : M',
        ContMDiffOn I ùìò(ùïú, E') n (extChartAt I' y ‚àò f) (f ‚Åª¬π' (extChartAt I' y).source) := by
  rw [‚Üê contMDiffOn_univ, contMDiffOn_iff_target]
  simp [continuous_iff_continuousOn_univ]
#align cont_mdiff_iff_target contMDiff_iff_target

theorem smooth_iff :
    Smooth I I' f ‚Üî
      Continuous f ‚àß
        ‚àÄ (x : M) (y : M'),
          ContDiffOn ùïú ‚ä§ (extChartAt I' y ‚àò f ‚àò (extChartAt I x).symm)
            ((extChartAt I x).target ‚à©
              (extChartAt I x).symm ‚Åª¬π' (f ‚Åª¬π' (extChartAt I' y).source)) :=
  contMDiff_iff
#align smooth_iff smooth_iff

theorem smooth_iff_target :
    Smooth I I' f ‚Üî
      Continuous f ‚àß
        ‚àÄ y : M', SmoothOn I ùìò(ùïú, E') (extChartAt I' y ‚àò f) (f ‚Åª¬π' (extChartAt I' y).source) :=
  contMDiff_iff_target
#align smooth_iff_target smooth_iff_target

/-! ### Deducing smoothness from smoothness one step beyond -/


theorem ContMDiffWithinAt.of_succ {n : ‚Ñï} (h : ContMDiffWithinAt I I' n.succ f s x) :
    ContMDiffWithinAt I I' n f s x :=
  h.of_le (WithTop.coe_le_coe.2 (Nat.le_succ n))
#align cont_mdiff_within_at.of_succ ContMDiffWithinAt.of_succ

theorem ContMDiffAt.of_succ {n : ‚Ñï} (h : ContMDiffAt I I' n.succ f x) : ContMDiffAt I I' n f x :=
  ContMDiffWithinAt.of_succ h
#align cont_mdiff_at.of_succ ContMDiffAt.of_succ

theorem ContMDiffOn.of_succ {n : ‚Ñï} (h : ContMDiffOn I I' n.succ f s) : ContMDiffOn I I' n f s :=
  fun x hx => (h x hx).of_succ
#align cont_mdiff_on.of_succ ContMDiffOn.of_succ

theorem ContMDiff.of_succ {n : ‚Ñï} (h : ContMDiff I I' n.succ f) : ContMDiff I I' n f := fun x =>
  (h x).of_succ
#align cont_mdiff.of_succ ContMDiff.of_succ

/-! ### Deducing continuity from smoothness -/


theorem ContMDiffWithinAt.continuousWithinAt (hf : ContMDiffWithinAt I I' n f s x) :
    ContinuousWithinAt f s x :=
  hf.1
#align cont_mdiff_within_at.continuous_within_at ContMDiffWithinAt.continuousWithinAt

theorem ContMDiffAt.continuousAt (hf : ContMDiffAt I I' n f x) : ContinuousAt f x :=
  (continuousWithinAt_univ _ _).1 <| ContMDiffWithinAt.continuousWithinAt hf
#align cont_mdiff_at.continuous_at ContMDiffAt.continuousAt

theorem ContMDiffOn.continuousOn (hf : ContMDiffOn I I' n f s) : ContinuousOn f s := fun x hx =>
  (hf x hx).continuousWithinAt
#align cont_mdiff_on.continuous_on ContMDiffOn.continuousOn

theorem ContMDiff.continuous (hf : ContMDiff I I' n f) : Continuous f :=
  continuous_iff_continuousAt.2 fun x => (hf x).continuousAt
#align cont_mdiff.continuous ContMDiff.continuous

/-! ### `C^‚àû` smoothness -/

theorem contMDiffWithinAt_top :
    SmoothWithinAt I I' f s x ‚Üî ‚àÄ n : ‚Ñï, ContMDiffWithinAt I I' n f s x :=
  ‚ü®fun h n => ‚ü®h.1, contDiffWithinAt_top.1 h.2 n‚ü©, fun H =>
    ‚ü®(H 0).1, contDiffWithinAt_top.2 fun n => (H n).2‚ü©‚ü©
#align cont_mdiff_within_at_top contMDiffWithinAt_top

theorem contMDiffAt_top : SmoothAt I I' f x ‚Üî ‚àÄ n : ‚Ñï, ContMDiffAt I I' n f x :=
  contMDiffWithinAt_top
#align cont_mdiff_at_top contMDiffAt_top

theorem contMDiffOn_top : SmoothOn I I' f s ‚Üî ‚àÄ n : ‚Ñï, ContMDiffOn I I' n f s :=
  ‚ü®fun h _ => h.of_le le_top, fun h x hx => contMDiffWithinAt_top.2 fun n => h n x hx‚ü©
#align cont_mdiff_on_top contMDiffOn_top

theorem contMDiff_top : Smooth I I' f ‚Üî ‚àÄ n : ‚Ñï, ContMDiff I I' n f :=
  ‚ü®fun h _ => h.of_le le_top, fun h x => contMDiffWithinAt_top.2 fun n => h n x‚ü©
#align cont_mdiff_top contMDiff_top

theorem contMDiffWithinAt_iff_nat :
    ContMDiffWithinAt I I' n f s x ‚Üî ‚àÄ m : ‚Ñï, (m : ‚Ñï‚àû) ‚â§ n ‚Üí ContMDiffWithinAt I I' m f s x := by
  refine ‚ü®fun h m hm => h.of_le hm, fun h => ?_‚ü©
  cases' n with n
  ¬∑ exact contMDiffWithinAt_top.2 fun n => h n le_top
  ¬∑ exact h n le_rfl
#align cont_mdiff_within_at_iff_nat contMDiffWithinAt_iff_nat

/-! ### Restriction to a smaller set -/

theorem ContMDiffWithinAt.mono_of_mem (hf : ContMDiffWithinAt I I' n f s x) (hts : s ‚àà ùìù[t] x) :
    ContMDiffWithinAt I I' n f t x :=
  StructureGroupoid.LocalInvariantProp.liftPropWithinAt_mono_of_mem
    (contDiffWithinAtProp_mono_of_mem I I' n) hf hts
#align cont_mdiff_within_at.mono_of_mem ContMDiffWithinAt.mono_of_mem

theorem ContMDiffWithinAt.mono (hf : ContMDiffWithinAt I I' n f s x) (hts : t ‚äÜ s) :
    ContMDiffWithinAt I I' n f t x :=
  hf.mono_of_mem <| mem_of_superset self_mem_nhdsWithin hts
#align cont_mdiff_within_at.mono ContMDiffWithinAt.mono

theorem contMDiffWithinAt_congr_nhds (hst : ùìù[s] x = ùìù[t] x) :
    ContMDiffWithinAt I I' n f s x ‚Üî ContMDiffWithinAt I I' n f t x :=
  ‚ü®fun h => h.mono_of_mem <| hst ‚ñ∏ self_mem_nhdsWithin, fun h =>
    h.mono_of_mem <| hst.symm ‚ñ∏ self_mem_nhdsWithin‚ü©
#align cont_mdiff_within_at_congr_nhds contMDiffWithinAt_congr_nhds

theorem contMDiffWithinAt_insert_self :
    ContMDiffWithinAt I I' n f (insert x s) x ‚Üî ContMDiffWithinAt I I' n f s x := by
  simp only [contMDiffWithinAt_iff, continuousWithinAt_insert_self]
  refine Iff.rfl.and <| (contDiffWithinAt_congr_nhds ?_).trans contDiffWithinAt_insert_self
  simp only [‚Üê map_extChartAt_nhdsWithin I, nhdsWithin_insert, Filter.map_sup, Filter.map_pure]

alias ‚ü®ContMDiffWithinAt.of_insert, _‚ü© := contMDiffWithinAt_insert_self

-- TODO: use `alias` again once it can make protected theorems
theorem ContMDiffWithinAt.insert (h : ContMDiffWithinAt I I' n f s x) :
    ContMDiffWithinAt I I' n f (insert x s) x :=
  contMDiffWithinAt_insert_self.2 h

theorem ContMDiffAt.contMDiffWithinAt (hf : ContMDiffAt I I' n f x) :
    ContMDiffWithinAt I I' n f s x :=
  ContMDiffWithinAt.mono hf (subset_univ _)
#align cont_mdiff_at.cont_mdiff_within_at ContMDiffAt.contMDiffWithinAt

theorem SmoothAt.smoothWithinAt (hf : SmoothAt I I' f x) : SmoothWithinAt I I' f s x :=
  ContMDiffAt.contMDiffWithinAt hf
#align smooth_at.smooth_within_at SmoothAt.smoothWithinAt

theorem ContMDiffOn.mono (hf : ContMDiffOn I I' n f s) (hts : t ‚äÜ s) : ContMDiffOn I I' n f t :=
  fun x hx => (hf x (hts hx)).mono hts
#align cont_mdiff_on.mono ContMDiffOn.mono

theorem ContMDiff.contMDiffOn (hf : ContMDiff I I' n f) : ContMDiffOn I I' n f s := fun x _ =>
  (hf x).contMDiffWithinAt
#align cont_mdiff.cont_mdiff_on ContMDiff.contMDiffOn

theorem Smooth.smoothOn (hf : Smooth I I' f) : SmoothOn I I' f s :=
  ContMDiff.contMDiffOn hf
#align smooth.smooth_on Smooth.smoothOn

theorem contMDiffWithinAt_inter' (ht : t ‚àà ùìù[s] x) :
    ContMDiffWithinAt I I' n f (s ‚à© t) x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_inter' ht
#align cont_mdiff_within_at_inter' contMDiffWithinAt_inter'

theorem contMDiffWithinAt_inter (ht : t ‚àà ùìù x) :
    ContMDiffWithinAt I I' n f (s ‚à© t) x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_inter ht
#align cont_mdiff_within_at_inter contMDiffWithinAt_inter

theorem ContMDiffWithinAt.contMDiffAt (h : ContMDiffWithinAt I I' n f s x) (ht : s ‚àà ùìù x) :
    ContMDiffAt I I' n f x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropAt_of_liftPropWithinAt h ht
#align cont_mdiff_within_at.cont_mdiff_at ContMDiffWithinAt.contMDiffAt

theorem SmoothWithinAt.smoothAt (h : SmoothWithinAt I I' f s x) (ht : s ‚àà ùìù x) :
    SmoothAt I I' f x :=
  ContMDiffWithinAt.contMDiffAt h ht
#align smooth_within_at.smooth_at SmoothWithinAt.smoothAt

theorem ContMDiffOn.contMDiffAt (h : ContMDiffOn I I' n f s) (hx : s ‚àà ùìù x) :
    ContMDiffAt I I' n f x :=
  (h x (mem_of_mem_nhds hx)).contMDiffAt hx
#align cont_mdiff_on.cont_mdiff_at ContMDiffOn.contMDiffAt

theorem SmoothOn.smoothAt (h : SmoothOn I I' f s) (hx : s ‚àà ùìù x) : SmoothAt I I' f x :=
  h.contMDiffAt hx
#align smooth_on.smooth_at SmoothOn.smoothAt

theorem contMDiffOn_iff_source_of_mem_maximalAtlas (he : e ‚àà maximalAtlas I M) (hs : s ‚äÜ e.source) :
    ContMDiffOn I I' n f s ‚Üî
      ContMDiffOn ùìò(ùïú, E) I' n (f ‚àò (e.extend I).symm) (e.extend I '' s) := by
  simp_rw [ContMDiffOn, Set.forall_mem_image]
  refine forall‚ÇÇ_congr fun x hx => ?_
  rw [contMDiffWithinAt_iff_source_of_mem_maximalAtlas he (hs hx)]
  apply contMDiffWithinAt_congr_nhds
  simp_rw [nhdsWithin_eq_iff_eventuallyEq,
    e.extend_symm_preimage_inter_range_eventuallyEq I hs (hs hx)]
#align cont_mdiff_on_iff_source_of_mem_maximal_atlas contMDiffOn_iff_source_of_mem_maximalAtlas

-- Porting note: didn't compile; fixed by golfing the proof and moving parts to lemmas
/-- A function is `C^n` within a set at a point, for `n : ‚Ñï`, if and only if it is `C^n` on
a neighborhood of this point. -/
theorem contMDiffWithinAt_iff_contMDiffOn_nhds {n : ‚Ñï} :
    ContMDiffWithinAt I I' n f s x ‚Üî ‚àÉ u ‚àà ùìù[insert x s] x, ContMDiffOn I I' n f u := by
  -- WLOG, `x ‚àà s`, otherwise we add `x` to `s`
  wlog hxs : x ‚àà s generalizing s
  ¬∑ rw [‚Üê contMDiffWithinAt_insert_self, this (mem_insert _ _), insert_idem]
  rw [insert_eq_of_mem hxs]
  -- The `‚Üê` implication is trivial
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®u, hmem, hu‚ü© ‚Ü¶ (hu _ (mem_of_mem_nhdsWithin hxs hmem)).mono_of_mem hmem‚ü©
  -- The property is true in charts. Let `v` be a good neighborhood in the chart where the function
  -- is smooth.
  rcases (contMDiffWithinAt_iff'.1 h).2.contDiffOn le_rfl with ‚ü®v, hmem, hsub, hv‚ü©
  have hxs' : extChartAt I x x ‚àà (extChartAt I x).target ‚à©
      (extChartAt I x).symm ‚Åª¬π' (s ‚à© f ‚Åª¬π' (extChartAt I' (f x)).source) :=
    ‚ü®(extChartAt I x).map_source (mem_extChartAt_source _ _), by rwa [extChartAt_to_inv], by
      rw [extChartAt_to_inv]; apply mem_extChartAt_source‚ü©
  rw [insert_eq_of_mem hxs'] at hmem hsub
  -- Then `(extChartAt I x).symm '' v` is the neighborhood we are looking for.
  refine ‚ü®(extChartAt I x).symm '' v, ?_, ?_‚ü©
  ¬∑ rw [‚Üê map_extChartAt_symm_nhdsWithin I,
      h.1.nhdsWithin_extChartAt_symm_preimage_inter_range I I']
    exact image_mem_map hmem
  ¬∑ have hv‚ÇÅ : (extChartAt I x).symm '' v ‚äÜ (extChartAt I x).source :=
      image_subset_iff.2 fun y hy ‚Ü¶ (extChartAt I x).map_target (hsub hy).1
    have hv‚ÇÇ : MapsTo f ((extChartAt I x).symm '' v) (extChartAt I' (f x)).source := by
      rintro _ ‚ü®y, hy, rfl‚ü©
      exact (hsub hy).2.2
    rwa [contMDiffOn_iff_of_subset_source' hv‚ÇÅ hv‚ÇÇ, PartialEquiv.image_symm_image_of_subset_target]
    exact hsub.trans inter_subset_left
#align cont_mdiff_within_at_iff_cont_mdiff_on_nhds contMDiffWithinAt_iff_contMDiffOn_nhds

/-- A function is `C^n` at a point, for `n : ‚Ñï`, if and only if it is `C^n` on
a neighborhood of this point. -/
theorem contMDiffAt_iff_contMDiffOn_nhds {n : ‚Ñï} :
    ContMDiffAt I I' n f x ‚Üî ‚àÉ u ‚àà ùìù x, ContMDiffOn I I' n f u := by
  simp [‚Üê contMDiffWithinAt_univ, contMDiffWithinAt_iff_contMDiffOn_nhds, nhdsWithin_univ]
#align cont_mdiff_at_iff_cont_mdiff_on_nhds contMDiffAt_iff_contMDiffOn_nhds

/-- Note: This does not hold for `n = ‚àû`. `f` being `C^‚àû` at `x` means that for every `n`, `f` is
`C^n` on some neighborhood of `x`, but this neighborhood can depend on `n`. -/
theorem contMDiffAt_iff_contMDiffAt_nhds {n : ‚Ñï} :
    ContMDiffAt I I' n f x ‚Üî ‚àÄ·∂† x' in ùìù x, ContMDiffAt I I' n f x' := by
  refine ‚ü®?_, fun h => h.self_of_nhds‚ü©
  rw [contMDiffAt_iff_contMDiffOn_nhds]
  rintro ‚ü®u, hu, h‚ü©
  refine (eventually_mem_nhds.mpr hu).mono fun x' hx' => ?_
  exact (h x' <| mem_of_mem_nhds hx').contMDiffAt hx'
#align cont_mdiff_at_iff_cont_mdiff_at_nhds contMDiffAt_iff_contMDiffAt_nhds

/-! ### Congruence lemmas -/

theorem ContMDiffWithinAt.congr (h : ContMDiffWithinAt I I' n f s x) (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y)
    (hx : f‚ÇÅ x = f x) : ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_congr h h‚ÇÅ hx
#align cont_mdiff_within_at.congr ContMDiffWithinAt.congr

theorem contMDiffWithinAt_congr (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) (hx : f‚ÇÅ x = f x) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_congr_iff h‚ÇÅ hx
#align cont_mdiff_within_at_congr contMDiffWithinAt_congr

theorem ContMDiffWithinAt.congr_of_eventuallyEq (h : ContMDiffWithinAt I I' n f s x)
    (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : f‚ÇÅ x = f x) : ContMDiffWithinAt I I' n f‚ÇÅ s x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_congr_of_eventuallyEq h h‚ÇÅ hx
#align cont_mdiff_within_at.congr_of_eventually_eq ContMDiffWithinAt.congr_of_eventuallyEq

theorem Filter.EventuallyEq.contMDiffWithinAt_iff (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : f‚ÇÅ x = f x) :
    ContMDiffWithinAt I I' n f‚ÇÅ s x ‚Üî ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropWithinAt_congr_iff_of_eventuallyEq h‚ÇÅ hx
#align filter.eventually_eq.cont_mdiff_within_at_iff Filter.EventuallyEq.contMDiffWithinAt_iff

theorem ContMDiffAt.congr_of_eventuallyEq (h : ContMDiffAt I I' n f x) (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù x] f) :
    ContMDiffAt I I' n f‚ÇÅ x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropAt_congr_of_eventuallyEq h h‚ÇÅ
#align cont_mdiff_at.congr_of_eventually_eq ContMDiffAt.congr_of_eventuallyEq

theorem Filter.EventuallyEq.contMDiffAt_iff (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù x] f) :
    ContMDiffAt I I' n f‚ÇÅ x ‚Üî ContMDiffAt I I' n f x :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropAt_congr_iff_of_eventuallyEq h‚ÇÅ
#align filter.eventually_eq.cont_mdiff_at_iff Filter.EventuallyEq.contMDiffAt_iff

theorem ContMDiffOn.congr (h : ContMDiffOn I I' n f s) (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) :
    ContMDiffOn I I' n f‚ÇÅ s :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropOn_congr h h‚ÇÅ
#align cont_mdiff_on.congr ContMDiffOn.congr

theorem contMDiffOn_congr (h‚ÇÅ : ‚àÄ y ‚àà s, f‚ÇÅ y = f y) :
    ContMDiffOn I I' n f‚ÇÅ s ‚Üî ContMDiffOn I I' n f s :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropOn_congr_iff h‚ÇÅ
#align cont_mdiff_on_congr contMDiffOn_congr

theorem ContMDiffOn.congr_mono (hf : ContMDiffOn I I' n f s) (h‚ÇÅ : ‚àÄ y ‚àà s‚ÇÅ, f‚ÇÅ y = f y)
    (hs : s‚ÇÅ ‚äÜ s) : ContMDiffOn I I' n f‚ÇÅ s‚ÇÅ :=
  (hf.mono hs).congr h‚ÇÅ

/-! ### Locality -/


/-- Being `C^n` is a local property. -/
theorem contMDiffOn_of_locally_contMDiffOn
    (h : ‚àÄ x ‚àà s, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ContMDiffOn I I' n f (s ‚à© u)) : ContMDiffOn I I' n f s :=
  (contDiffWithinAt_localInvariantProp I I' n).liftPropOn_of_locally_liftPropOn h
#align cont_mdiff_on_of_locally_cont_mdiff_on contMDiffOn_of_locally_contMDiffOn

theorem contMDiff_of_locally_contMDiffOn (h : ‚àÄ x, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ContMDiffOn I I' n f u) :
    ContMDiff I I' n f :=
  (contDiffWithinAt_localInvariantProp I I' n).liftProp_of_locally_liftPropOn h
#align cont_mdiff_of_locally_cont_mdiff_on contMDiff_of_locally_contMDiffOn
