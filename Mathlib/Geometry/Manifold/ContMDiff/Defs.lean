/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.IsManifold.ExtChartAt
import Mathlib.Geometry.Manifold.LocalInvariantProperties

/-!
# `C^n` functions between manifolds

We define `Câ¿` functions between manifolds, as functions which are `Câ¿` in charts, and prove
basic properties of these notions. Here, `n` can be finite, or `âˆ`, or `Ï‰`.

## Main definitions and statements

Let `M` and `M'` be two manifolds, with respect to models with corners `I` and `I'`. Let
`f : M â†’ M'`.

* `ContMDiffWithinAt I I' n f s x` states that the function `f` is `Câ¿` within the set `s`
  around the point `x`.
* `ContMDiffAt I I' n f x` states that the function `f` is `Câ¿` around `x`.
* `ContMDiffOn I I' n f s` states that the function `f` is `Câ¿` on the set `s`
* `ContMDiff I I' n f` states that the function `f` is `Câ¿`.

We also give some basic properties of `Câ¿` functions between manifolds, following the API of
`C^n` functions between vector spaces.
See `Basic.lean` for further basic properties of `Câ¿` functions between manifolds,
`NormedSpace.lean` for the equivalence of manifold-smoothness to usual smoothness,
`Product.lean` for smoothness results related to the product of manifolds and
`Atlas.lean` for smoothness of atlas members and local structomorphisms.

## Implementation details

Many properties follow for free from the corresponding properties of functions in vector spaces,
as being `Câ¿` is a local property invariant under the `Câ¿` groupoid. We take advantage of the
general machinery developed in `LocalInvariantProperties.lean` to get these properties
automatically. For instance, the fact that being `Câ¿` does not depend on the chart one considers
is given by `liftPropWithinAt_indep_chart`.

For this to work, the definition of `ContMDiffWithinAt` and friends has to
follow definitionally the setup of local invariant properties. Still, we recast the definition
in terms of extended charts in `contMDiffOn_iff` and `contMDiff_iff`.
-/


open Set Function Filter ChartedSpace IsManifold

open scoped Topology Manifold ContDiff

/-! ### Definition of `Câ¿` functions between manifolds -/


variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  -- Prerequisite typeclasses to say that `M` is a manifold over the pair `(E, H)`
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ğ•œ E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- Prerequisite typeclasses to say that `M'` is a manifold over the pair `(E', H')`
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- Prerequisite typeclasses to say that `M''` is a manifold over the pair `(E'', H'')`
  {E'' : Type*}
  [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] {H'' : Type*} [TopologicalSpace H'']
  {I'' : ModelWithCorners ğ•œ E'' H''} {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M'']
  -- declare functions, sets, points and smoothness indices
  {e : PartialHomeomorph M H}
  {e' : PartialHomeomorph M' H'} {f fâ‚ : M â†’ M'} {s sâ‚ t : Set M} {x : M} {m n : WithTop â„•âˆ}

variable (I I') in
/-- Property in the model space of a model with corners of being `C^n` within at set at a point,
when read in the model vector space. This property will be lifted to manifolds to define `C^n`
functions between manifolds. -/
def ContDiffWithinAtProp (n : WithTop â„•âˆ) (f : H â†’ H') (s : Set H) (x : H) : Prop :=
  ContDiffWithinAt ğ•œ n (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) (I x)

theorem contDiffWithinAtProp_self_source {f : E â†’ H'} {s : Set E} {x : E} :
    ContDiffWithinAtProp ğ“˜(ğ•œ, E) I' n f s x â†” ContDiffWithinAt ğ•œ n (I' âˆ˜ f) s x := by
  simp_rw [ContDiffWithinAtProp, modelWithCornersSelf_coe, range_id, inter_univ,
    modelWithCornersSelf_coe_symm, CompTriple.comp_eq, preimage_id_eq, id_eq]

theorem contDiffWithinAtProp_self {f : E â†’ E'} {s : Set E} {x : E} :
    ContDiffWithinAtProp ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') n f s x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAtProp_self_source

theorem contDiffWithinAtProp_self_target {f : H â†’ E'} {s : Set H} {x : H} :
    ContDiffWithinAtProp I ğ“˜(ğ•œ, E') n f s x â†”
      ContDiffWithinAt ğ•œ n (f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) (I x) :=
  Iff.rfl

/-- Being `Câ¿` in the model space is a local property, invariant under `Câ¿` maps. Therefore,
it lifts nicely to manifolds. -/
theorem contDiffWithinAt_localInvariantProp_of_le (n m : WithTop â„•âˆ) (hmn : m â‰¤ n) :
    (contDiffGroupoid n I).LocalInvariantProp (contDiffGroupoid n I')
      (ContDiffWithinAtProp I I' m) where
  is_local {s x u f} u_open xu := by
    have : I.symm â»Â¹' (s âˆ© u) âˆ© range I = I.symm â»Â¹' s âˆ© range I âˆ© I.symm â»Â¹' u := by
      simp only [inter_right_comm, preimage_inter]
    rw [ContDiffWithinAtProp, ContDiffWithinAtProp, this]
    symm
    apply contDiffWithinAt_inter
    have : u âˆˆ ğ“ (I.symm (I x)) := by
      rw [ModelWithCorners.left_inv]
      exact u_open.mem_nhds xu
    apply ContinuousAt.preimage_mem_nhds I.continuous_symm.continuousAt this
  right_invariance' {s x f e} he hx h := by
    rw [ContDiffWithinAtProp] at h âŠ¢
    have : I x = (I âˆ˜ e.symm âˆ˜ I.symm) (I (e x)) := by simp only [hx, mfld_simps]
    rw [this] at h
    have : I (e x) âˆˆ I.symm â»Â¹' e.target âˆ© range I := by simp only [hx, mfld_simps]
    have := (mem_groupoid_of_pregroupoid.2 he).2.contDiffWithinAt this
    convert (h.comp_inter _ (this.of_le hmn)).mono_of_mem_nhdsWithin _
      using 1
    Â· ext y; simp only [mfld_simps]
    refine mem_nhdsWithin.mpr
      âŸ¨I.symm â»Â¹' e.target, e.open_target.preimage I.continuous_symm, by
        simp_rw [mem_preimage, I.left_inv, e.mapsTo hx], ?_âŸ©
    mfld_set_tac
  congr_of_forall {s x f g} h hx hf := by
    apply hf.congr
    Â· intro y hy
      simp only [mfld_simps] at hy
      simp only [h, hy, mfld_simps]
    Â· simp only [hx, mfld_simps]
  left_invariance' {s x f e'} he' hs hx h := by
    rw [ContDiffWithinAtProp] at h âŠ¢
    have A : (I' âˆ˜ f âˆ˜ I.symm) (I x) âˆˆ I'.symm â»Â¹' e'.source âˆ© range I' := by
      simp only [hx, mfld_simps]
    have := (mem_groupoid_of_pregroupoid.2 he').1.contDiffWithinAt A
    convert (this.of_le hmn).comp _ h _
    Â· ext y; simp only [mfld_simps]
    Â· intro y hy; simp only [mfld_simps] at hy; simpa only [hy, mfld_simps] using hs hy.1

/-- Being `Câ¿` in the model space is a local property, invariant under `C^n` maps. Therefore,
it lifts nicely to manifolds. -/
theorem contDiffWithinAt_localInvariantProp (n : WithTop â„•âˆ) :
    (contDiffGroupoid n I).LocalInvariantProp (contDiffGroupoid n I')
      (ContDiffWithinAtProp I I' n) :=
  contDiffWithinAt_localInvariantProp_of_le n n le_rfl

theorem contDiffWithinAtProp_mono_of_mem_nhdsWithin
    (n : WithTop â„•âˆ) â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„ (hts : s âˆˆ ğ“[t] x)
    (h : ContDiffWithinAtProp I I' n f s x) : ContDiffWithinAtProp I I' n f t x := by
  refine h.mono_of_mem_nhdsWithin ?_
  refine inter_mem ?_ (mem_of_superset self_mem_nhdsWithin inter_subset_right)
  rwa [â† Filter.mem_map, â† I.image_eq, I.symm_map_nhdsWithin_image]

theorem contDiffWithinAtProp_id (x : H) : ContDiffWithinAtProp I I n id univ x := by
  simp only [ContDiffWithinAtProp, id_comp, preimage_univ, univ_inter]
  have : ContDiffWithinAt ğ•œ n id (range I) (I x) := contDiff_id.contDiffAt.contDiffWithinAt
  refine this.congr (fun y hy â†¦ ?_) ?_
  Â· simp only [ModelWithCorners.right_inv I hy, mfld_simps]
  Â· simp only [mfld_simps]

variable (I I') in
/-- A function is `n` times continuously differentiable within a set at a point in a manifold if
it is continuous and it is `n` times continuously differentiable in this set around this point, when
read in the preferred chart at this point. -/
def ContMDiffWithinAt (n : WithTop â„•âˆ) (f : M â†’ M') (s : Set M) (x : M) :=
  LiftPropWithinAt (ContDiffWithinAtProp I I' n) f s x

variable (I I') in
/-- A function is `n` times continuously differentiable at a point in a manifold if
it is continuous and it is `n` times continuously differentiable around this point, when
read in the preferred chart at this point. -/
def ContMDiffAt (n : WithTop â„•âˆ) (f : M â†’ M') (x : M) :=
  ContMDiffWithinAt I I' n f univ x

theorem contMDiffAt_iff {n : WithTop â„•âˆ} {f : M â†’ M'} {x : M} :
    ContMDiffAt I I' n f x â†”
      ContinuousAt f x âˆ§
        ContDiffWithinAt ğ•œ n (extChartAt I' (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm) (range I)
          (extChartAt I x x) :=
  liftPropAt_iff.trans <| by rw [ContDiffWithinAtProp, preimage_univ, univ_inter]; rfl

variable (I I') in
/-- A function is `n` times continuously differentiable in a set of a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable on this set in the charts
around these points. -/
def ContMDiffOn (n : WithTop â„•âˆ) (f : M â†’ M') (s : Set M) :=
  âˆ€ x âˆˆ s, ContMDiffWithinAt I I' n f s x

variable (I I') in
/-- A function is `n` times continuously differentiable in a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable in the charts
around these points. -/
def ContMDiff (n : WithTop â„•âˆ) (f : M â†’ M') :=
  âˆ€ x, ContMDiffAt I I' n f x

/-! ### Deducing smoothness from higher smoothness -/

theorem ContMDiffWithinAt.of_le (hf : ContMDiffWithinAt I I' n f s x) (le : m â‰¤ n) :
    ContMDiffWithinAt I I' m f s x := by
  simp only [ContMDiffWithinAt] at hf âŠ¢
  exact âŸ¨hf.1, hf.2.of_le (mod_cast le)âŸ©

theorem ContMDiffAt.of_le (hf : ContMDiffAt I I' n f x) (le : m â‰¤ n) : ContMDiffAt I I' m f x :=
  ContMDiffWithinAt.of_le hf le

theorem ContMDiffOn.of_le (hf : ContMDiffOn I I' n f s) (le : m â‰¤ n) : ContMDiffOn I I' m f s :=
  fun x hx â†¦ (hf x hx).of_le le

theorem ContMDiff.of_le (hf : ContMDiff I I' n f) (le : m â‰¤ n) : ContMDiff I I' m f := fun x â†¦
  (hf x).of_le le

/-! ### Basic properties of `C^n` functions between manifolds -/

theorem ContMDiff.contMDiffAt (h : ContMDiff I I' n f) : ContMDiffAt I I' n f x :=
  h x

theorem contMDiffWithinAt_univ : ContMDiffWithinAt I I' n f univ x â†” ContMDiffAt I I' n f x :=
  Iff.rfl

theorem contMDiffOn_univ : ContMDiffOn I I' n f univ â†” ContMDiff I I' n f := by
  simp only [ContMDiffOn, ContMDiff, contMDiffWithinAt_univ, forall_prop_of_true, mem_univ]

/-- One can reformulate being `C^n` within a set at a point as continuity within this set at this
point, and being `C^n` in the corresponding extended chart. -/
theorem contMDiffWithinAt_iff :
    ContMDiffWithinAt I I' n f s x â†”
      ContinuousWithinAt f s x âˆ§
        ContDiffWithinAt ğ•œ n (extChartAt I' (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm)
          ((extChartAt I x).symm â»Â¹' s âˆ© range I) (extChartAt I x x) := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff']; rfl

/-- One can reformulate being `Câ¿` within a set at a point as continuity within this set at this
point, and being `Câ¿` in the corresponding extended chart. This form states regularity of `f`
written in such a way that the set is restricted to lie within the domain/codomain of the
corresponding charts.
Even though this expression is more complicated than the one in `contMDiffWithinAt_iff`, it is
a smaller set, but their germs at `extChartAt I x x` are equal. It is sometimes useful to rewrite
using this in the goal.
-/
theorem contMDiffWithinAt_iff' :
    ContMDiffWithinAt I I' n f s x â†”
      ContinuousWithinAt f s x âˆ§
        ContDiffWithinAt ğ•œ n (extChartAt I' (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm)
          ((extChartAt I x).target âˆ©
            (extChartAt I x).symm â»Â¹' (s âˆ© f â»Â¹' (extChartAt I' (f x)).source))
          (extChartAt I x x) := by
  simp only [ContMDiffWithinAt, liftPropWithinAt_iff']
  exact and_congr_right fun hc â†¦ contDiffWithinAt_congr_set <|
    hc.extChartAt_symm_preimage_inter_range_eventuallyEq

/-- One can reformulate being `Câ¿` within a set at a point as continuity within this set at this
point, and being `Câ¿` in the corresponding extended chart in the target. -/
theorem contMDiffWithinAt_iff_target :
    ContMDiffWithinAt I I' n f s x â†”
      ContinuousWithinAt f s x âˆ§ ContMDiffWithinAt I ğ“˜(ğ•œ, E') n (extChartAt I' (f x) âˆ˜ f) s x := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff', â† and_assoc]
  have cont :
    ContinuousWithinAt f s x âˆ§ ContinuousWithinAt (extChartAt I' (f x) âˆ˜ f) s x â†”
        ContinuousWithinAt f s x :=
      and_iff_left_of_imp <| (continuousAt_extChartAt _).comp_continuousWithinAt
  simp_rw [cont, ContDiffWithinAtProp, extChartAt, PartialHomeomorph.extend, PartialEquiv.coe_trans,
    ModelWithCorners.toPartialEquiv_coe, PartialHomeomorph.coe_coe, modelWithCornersSelf_coe,
    chartAt_self_eq, PartialHomeomorph.refl_apply, id_comp]
  rfl

theorem contMDiffAt_iff_target {x : M} :
    ContMDiffAt I I' n f x â†”
      ContinuousAt f x âˆ§ ContMDiffAt I ğ“˜(ğ•œ, E') n (extChartAt I' (f x) âˆ˜ f) x := by
  rw [ContMDiffAt, ContMDiffAt, contMDiffWithinAt_iff_target, continuousWithinAt_univ]

/-- One can reformulate being `Câ¿` within a set at a point as being `Câ¿` in the source space when
composing with the extended chart. -/
theorem contMDiffWithinAt_iff_source :
    ContMDiffWithinAt I I' n f s x â†”
      ContMDiffWithinAt ğ“˜(ğ•œ, E) I' n (f âˆ˜ (extChartAt I x).symm)
        ((extChartAt I x).symm â»Â¹' s âˆ© range I) (extChartAt I x x) := by
  simp_rw [ContMDiffWithinAt, liftPropWithinAt_iff']
  have : ContinuousWithinAt f s x
      â†” ContinuousWithinAt (f âˆ˜ â†‘(extChartAt I x).symm) (â†‘(extChartAt I x).symm â»Â¹' s âˆ© range â†‘I)
        (extChartAt I x x) := by
    refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
    Â· apply h.comp_of_eq
      Â· exact (continuousAt_extChartAt_symm x).continuousWithinAt
      Â· exact (mapsTo_preimage _ _).mono_left inter_subset_left
      Â· exact extChartAt_to_inv x
    Â· rw [â† continuousWithinAt_inter (extChartAt_source_mem_nhds (I := I) x)]
      have : ContinuousWithinAt ((f âˆ˜ â†‘(extChartAt I x).symm) âˆ˜ â†‘(extChartAt I x))
          (s âˆ© (extChartAt I x).source) x := by
        apply h.comp (continuousAt_extChartAt x).continuousWithinAt
        intro y hy
        have : (chartAt H x).symm ((chartAt H x) y) = y :=
          PartialHomeomorph.left_inv _ (by simpa using hy.2)
        simpa [this] using hy.1
      apply this.congr
      Â· intro y hy
        have : (chartAt H x).symm ((chartAt H x) y) = y :=
          PartialHomeomorph.left_inv _ (by simpa using hy.2)
        simp [this]
      Â· simp
  rw [â† this]
  simp only [ContDiffWithinAtProp, mfld_simps, preimage_comp, comp_assoc]

/-- One can reformulate being `Câ¿` at a point as being `Câ¿` in the source space when
composing with the extended chart. -/
theorem contMDiffAt_iff_source :
    ContMDiffAt I I' n f x â†”
      ContMDiffWithinAt ğ“˜(ğ•œ, E) I' n (f âˆ˜ (extChartAt I x).symm) (range I) (extChartAt I x x) := by
  rw [â† contMDiffWithinAt_univ, contMDiffWithinAt_iff_source]
  simp

section IsManifold

theorem contMDiffWithinAt_iff_source_of_mem_maximalAtlas
    [IsManifold I n M] (he : e âˆˆ maximalAtlas I n M) (hx : x âˆˆ e.source) :
    ContMDiffWithinAt I I' n f s x â†”
      ContMDiffWithinAt ğ“˜(ğ•œ, E) I' n (f âˆ˜ (e.extend I).symm) ((e.extend I).symm â»Â¹' s âˆ© range I)
        (e.extend I x) := by
  have h2x := hx; rw [â† e.extend_source (I := I)] at h2x
  simp_rw [ContMDiffWithinAt,
    (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_indep_chart_source he hx,
    StructureGroupoid.liftPropWithinAt_self_source,
    e.extend_symm_continuousWithinAt_comp_right_iff, contDiffWithinAtProp_self_source,
    ContDiffWithinAtProp, Function.comp, e.left_inv hx, (e.extend I).left_inv h2x]
  rfl

theorem contMDiffWithinAt_iff_source_of_mem_source
    [IsManifold I n M] {x' : M} (hx' : x' âˆˆ (chartAt H x).source) :
    ContMDiffWithinAt I I' n f s x' â†”
      ContMDiffWithinAt ğ“˜(ğ•œ, E) I' n (f âˆ˜ (extChartAt I x).symm)
        ((extChartAt I x).symm â»Â¹' s âˆ© range I) (extChartAt I x x') :=
  contMDiffWithinAt_iff_source_of_mem_maximalAtlas (chart_mem_maximalAtlas x) hx'

theorem contMDiffAt_iff_source_of_mem_source
    [IsManifold I n M] {x' : M} (hx' : x' âˆˆ (chartAt H x).source) :
    ContMDiffAt I I' n f x' â†”
      ContMDiffWithinAt ğ“˜(ğ•œ, E) I' n (f âˆ˜ (extChartAt I x).symm) (range I) (extChartAt I x x') := by
  simp_rw [ContMDiffAt, contMDiffWithinAt_iff_source_of_mem_source hx', preimage_univ, univ_inter]

theorem contMDiffWithinAt_iff_target_of_mem_source
    [IsManifold I' n M'] {x : M} {y : M'} (hy : f x âˆˆ (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x â†”
      ContinuousWithinAt f s x âˆ§ ContMDiffWithinAt I ğ“˜(ğ•œ, E') n (extChartAt I' y âˆ˜ f) s x := by
  simp_rw [ContMDiffWithinAt]
  rw [(contDiffWithinAt_localInvariantProp n).liftPropWithinAt_indep_chart_target
      (chart_mem_maximalAtlas y) hy,
    and_congr_right]
  intro hf
  simp_rw [StructureGroupoid.liftPropWithinAt_self_target]
  simp_rw [((chartAt H' y).continuousAt hy).comp_continuousWithinAt hf]
  rw [â† extChartAt_source (I := I')] at hy
  simp_rw [(continuousAt_extChartAt' hy).comp_continuousWithinAt hf]
  rfl

theorem contMDiffAt_iff_target_of_mem_source
    [IsManifold I' n M'] {x : M} {y : M'} (hy : f x âˆˆ (chartAt H' y).source) :
    ContMDiffAt I I' n f x â†”
      ContinuousAt f x âˆ§ ContMDiffAt I ğ“˜(ğ•œ, E') n (extChartAt I' y âˆ˜ f) x := by
  rw [ContMDiffAt, contMDiffWithinAt_iff_target_of_mem_source hy, continuousWithinAt_univ,
    ContMDiffAt]

variable [IsManifold I n M] [IsManifold I' n M']

theorem contMDiffWithinAt_iff_of_mem_maximalAtlas {x : M} (he : e âˆˆ maximalAtlas I n M)
    (he' : e' âˆˆ maximalAtlas I' n M') (hx : x âˆˆ e.source) (hy : f x âˆˆ e'.source) :
    ContMDiffWithinAt I I' n f s x â†”
      ContinuousWithinAt f s x âˆ§
        ContDiffWithinAt ğ•œ n (e'.extend I' âˆ˜ f âˆ˜ (e.extend I).symm)
          ((e.extend I).symm â»Â¹' s âˆ© range I) (e.extend I x) :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_indep_chart he hx he' hy

/-- An alternative formulation of `contMDiffWithinAt_iff_of_mem_maximalAtlas`
if the set if `s` lies in `e.source`. -/
theorem contMDiffWithinAt_iff_image {x : M} (he : e âˆˆ maximalAtlas I n M)
    (he' : e' âˆˆ maximalAtlas I' n M')
    (hs : s âŠ† e.source) (hx : x âˆˆ e.source) (hy : f x âˆˆ e'.source) :
    ContMDiffWithinAt I I' n f s x â†”
      ContinuousWithinAt f s x âˆ§
        ContDiffWithinAt ğ•œ n (e'.extend I' âˆ˜ f âˆ˜ (e.extend I).symm) (e.extend I '' s)
          (e.extend I x) := by
  rw [contMDiffWithinAt_iff_of_mem_maximalAtlas he he' hx hy, and_congr_right_iff]
  refine fun _ â†¦ contDiffWithinAt_congr_set ?_
  simp_rw [e.extend_symm_preimage_inter_range_eventuallyEq hs hx]

/-- One can reformulate being `C^n` within a set at a point as continuity within this set at this
point, and being `C^n` in any chart containing that point. -/
theorem contMDiffWithinAt_iff_of_mem_source {x' : M} {y : M'} (hx : x' âˆˆ (chartAt H x).source)
    (hy : f x' âˆˆ (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x' â†”
      ContinuousWithinAt f s x' âˆ§
        ContDiffWithinAt ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm)
          ((extChartAt I x).symm â»Â¹' s âˆ© range I) (extChartAt I x x') :=
  contMDiffWithinAt_iff_of_mem_maximalAtlas (chart_mem_maximalAtlas x)
    (chart_mem_maximalAtlas y) hx hy

theorem contMDiffWithinAt_iff_of_mem_source' {x' : M} {y : M'} (hx : x' âˆˆ (chartAt H x).source)
    (hy : f x' âˆˆ (chartAt H' y).source) :
    ContMDiffWithinAt I I' n f s x' â†”
      ContinuousWithinAt f s x' âˆ§
        ContDiffWithinAt ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm)
          ((extChartAt I x).target âˆ© (extChartAt I x).symm â»Â¹' (s âˆ© f â»Â¹' (extChartAt I' y).source))
          (extChartAt I x x') := by
  refine (contMDiffWithinAt_iff_of_mem_source hx hy).trans ?_
  rw [â† extChartAt_source I] at hx
  rw [â† extChartAt_source I'] at hy
  rw [and_congr_right_iff]
  set e := extChartAt I x; set e' := extChartAt I' (f x)
  refine fun hc â†¦ contDiffWithinAt_congr_set ?_
  rw [â† nhdsWithin_eq_iff_eventuallyEq, â† e.image_source_inter_eq',
    â† map_extChartAt_nhdsWithin_eq_image' hx,
    â† map_extChartAt_nhdsWithin' hx, inter_comm, nhdsWithin_inter_of_mem]
  exact hc (extChartAt_source_mem_nhds' hy)

theorem contMDiffAt_iff_of_mem_source {x' : M} {y : M'} (hx : x' âˆˆ (chartAt H x).source)
    (hy : f x' âˆˆ (chartAt H' y).source) :
    ContMDiffAt I I' n f x' â†”
      ContinuousAt f x' âˆ§
        ContDiffWithinAt ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm) (range I)
          (extChartAt I x x') :=
  (contMDiffWithinAt_iff_of_mem_source hx hy).trans <| by
    rw [continuousWithinAt_univ, preimage_univ, univ_inter]

theorem contMDiffOn_iff_of_mem_maximalAtlas (he : e âˆˆ maximalAtlas I n M)
    (he' : e' âˆˆ maximalAtlas I' n M') (hs : s âŠ† e.source) (h2s : MapsTo f s e'.source) :
    ContMDiffOn I I' n f s â†”
      ContinuousOn f s âˆ§
        ContDiffOn ğ•œ n (e'.extend I' âˆ˜ f âˆ˜ (e.extend I).symm) (e.extend I '' s) := by
  simp_rw [ContinuousOn, ContDiffOn, Set.forall_mem_image, â† forall_and, ContMDiffOn]
  exact forallâ‚‚_congr fun x hx â†¦ contMDiffWithinAt_iff_image he he' hs (hs hx) (h2s hx)

theorem contMDiffOn_iff_of_mem_maximalAtlas' (he : e âˆˆ maximalAtlas I n M)
    (he' : e' âˆˆ maximalAtlas I' n M') (hs : s âŠ† e.source) (h2s : MapsTo f s e'.source) :
    ContMDiffOn I I' n f s â†”
      ContDiffOn ğ•œ n (e'.extend I' âˆ˜ f âˆ˜ (e.extend I).symm) (e.extend I '' s) :=
  (contMDiffOn_iff_of_mem_maximalAtlas he he' hs h2s).trans <| and_iff_right_of_imp fun h â†¦
    (e.continuousOn_writtenInExtend_iff hs h2s).1 h.continuousOn

/-- If the set where you want `f` to be `C^n` lies entirely in a single chart, and `f` maps it
into a single chart, the fact that `f` is `C^n` on that set can be expressed by purely looking in
these charts.
Note: this lemma uses `extChartAt I x '' s` instead of `(extChartAt I x).symm â»Â¹' s` to ensure
that this set lies in `(extChartAt I x).target`. -/
theorem contMDiffOn_iff_of_subset_source {x : M} {y : M'} (hs : s âŠ† (chartAt H x).source)
    (h2s : MapsTo f s (chartAt H' y).source) :
    ContMDiffOn I I' n f s â†”
      ContinuousOn f s âˆ§
        ContDiffOn ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm) (extChartAt I x '' s) :=
  contMDiffOn_iff_of_mem_maximalAtlas (chart_mem_maximalAtlas x) (chart_mem_maximalAtlas y) hs
    h2s

/-- If the set where you want `f` to be `C^n` lies entirely in a single chart, and `f` maps it
into a single chart, the fact that `f` is `C^n` on that set can be expressed by purely looking in
these charts.
Note: this lemma uses `extChartAt I x '' s` instead of `(extChartAt I x).symm â»Â¹' s` to ensure
that this set lies in `(extChartAt I x).target`. -/
theorem contMDiffOn_iff_of_subset_source' {x : M} {y : M'} (hs : s âŠ† (extChartAt I x).source)
    (h2s : MapsTo f s (extChartAt I' y).source) :
    ContMDiffOn I I' n f s â†”
        ContDiffOn ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm) (extChartAt I x '' s) := by
  rw [extChartAt_source] at hs h2s
  exact contMDiffOn_iff_of_mem_maximalAtlas' (chart_mem_maximalAtlas x)
    (chart_mem_maximalAtlas y) hs h2s

/-- One can reformulate being `C^n` on a set as continuity on this set, and being `C^n` in any
extended chart. -/
theorem contMDiffOn_iff :
    ContMDiffOn I I' n f s â†”
      ContinuousOn f s âˆ§
        âˆ€ (x : M) (y : M'),
          ContDiffOn ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm)
            ((extChartAt I x).target âˆ©
              (extChartAt I x).symm â»Â¹' (s âˆ© f â»Â¹' (extChartAt I' y).source)) := by
  constructor
  Â· intro h
    refine âŸ¨fun x hx â†¦ (h x hx).1, fun x y z hz â†¦ ?_âŸ©
    simp only [mfld_simps] at hz
    let w := (extChartAt I x).symm z
    have : w âˆˆ s := by simp only [w, hz, mfld_simps]
    specialize h w this
    have w1 : w âˆˆ (chartAt H x).source := by simp only [w, hz, mfld_simps]
    have w2 : f w âˆˆ (chartAt H' y).source := by simp only [w, hz, mfld_simps]
    convert ((contMDiffWithinAt_iff_of_mem_source w1 w2).mp h).2.mono _
    Â· simp only [w, hz, mfld_simps]
    Â· mfld_set_tac
  Â· rintro âŸ¨hcont, hdiffâŸ© x hx
    refine (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_iff.mpr ?_
    refine âŸ¨hcont x hx, ?_âŸ©
    dsimp [ContDiffWithinAtProp]
    convert hdiff x (f x) (extChartAt I x x) (by simp only [hx, mfld_simps]) using 1
    mfld_set_tac

/-- zero-smoothness on a set is equivalent to continuity on this set. -/
theorem contMDiffOn_zero_iff :
    ContMDiffOn I I' 0 f s â†” ContinuousOn f s := by
  rw [contMDiffOn_iff]
  refine âŸ¨fun h â†¦ h.1, fun h â†¦ âŸ¨h, ?_âŸ©âŸ©
  intro x y
  rw [contDiffOn_zero]
  apply (continuousOn_extChartAt _).comp
  Â· apply h.comp ((continuousOn_extChartAt_symm _).mono inter_subset_left) (fun z hz â†¦ ?_)
    simp only [preimage_inter, mem_inter_iff, mem_preimage] at hz
    exact hz.2.1
  Â· intro z hz
    simp only [preimage_inter, mem_inter_iff, mem_preimage] at hz
    exact hz.2.2

/-- One can reformulate being `C^n` on a set as continuity on this set, and being `C^n` in any
extended chart in the target. -/
theorem contMDiffOn_iff_target :
    ContMDiffOn I I' n f s â†”
      ContinuousOn f s âˆ§
        âˆ€ y : M',
          ContMDiffOn I ğ“˜(ğ•œ, E') n (extChartAt I' y âˆ˜ f) (s âˆ© f â»Â¹' (extChartAt I' y).source) := by
  simp only [contMDiffOn_iff, ModelWithCorners.source_eq, chartAt_self_eq,
    PartialHomeomorph.refl_partialEquiv, PartialEquiv.refl_trans, extChartAt,
    PartialHomeomorph.extend, Set.preimage_univ, Set.inter_univ, and_congr_right_iff]
  intro h
  constructor
  Â· refine fun h' y â†¦ âŸ¨?_, fun x _ â†¦ h' x yâŸ©
    have h'' : ContinuousOn _ univ := (ModelWithCorners.continuous I').continuousOn
    convert (h''.comp_inter (chartAt H' y).continuousOn_toFun).comp_inter h
    simp
  Â· exact fun h' x y â†¦ (h' y).2 x 0


/-- One can reformulate being `C^n` as continuity and being `C^n` in any extended chart. -/
theorem contMDiff_iff :
    ContMDiff I I' n f â†”
      Continuous f âˆ§
        âˆ€ (x : M) (y : M'),
          ContDiffOn ğ•œ n (extChartAt I' y âˆ˜ f âˆ˜ (extChartAt I x).symm)
            ((extChartAt I x).target âˆ©
              (extChartAt I x).symm â»Â¹' (f â»Â¹' (extChartAt I' y).source)) := by
  simp [â† contMDiffOn_univ, contMDiffOn_iff, continuousOn_univ]

/-- One can reformulate being `C^n` as continuity and being `C^n` in any extended chart in the
target. -/
theorem contMDiff_iff_target :
    ContMDiff I I' n f â†”
      Continuous f âˆ§ âˆ€ y : M',
        ContMDiffOn I ğ“˜(ğ•œ, E') n (extChartAt I' y âˆ˜ f) (f â»Â¹' (extChartAt I' y).source) := by
  rw [â† contMDiffOn_univ, contMDiffOn_iff_target]
  simp [continuousOn_univ]

/-- zero-smoothness is equivalent to continuity. -/
theorem contMDiff_zero_iff :
    ContMDiff I I' 0 f â†” Continuous f := by
  rw [â† contMDiffOn_univ, â† continuousOn_univ, contMDiffOn_zero_iff]

end IsManifold


/-! ### `C^(n+1)` functions are `C^n` -/

theorem ContMDiffWithinAt.of_succ (h : ContMDiffWithinAt I I' (n + 1) f s x) :
    ContMDiffWithinAt I I' n f s x :=
  h.of_le le_self_add

theorem ContMDiffAt.of_succ (h : ContMDiffAt I I' (n + 1) f x) : ContMDiffAt I I' n f x :=
  ContMDiffWithinAt.of_succ h

theorem ContMDiffOn.of_succ (h : ContMDiffOn I I' (n + 1) f s) : ContMDiffOn I I' n f s :=
  fun x hx â†¦ (h x hx).of_succ

theorem ContMDiff.of_succ (h : ContMDiff I I' (n + 1) f) : ContMDiff I I' n f := fun x â†¦
  (h x).of_succ


/-! ### `C^n` functions are continuous -/

theorem ContMDiffWithinAt.continuousWithinAt (hf : ContMDiffWithinAt I I' n f s x) :
    ContinuousWithinAt f s x :=
  hf.1

theorem ContMDiffAt.continuousAt (hf : ContMDiffAt I I' n f x) : ContinuousAt f x :=
  (continuousWithinAt_univ _ _).1 <| ContMDiffWithinAt.continuousWithinAt hf

theorem ContMDiffOn.continuousOn (hf : ContMDiffOn I I' n f s) : ContinuousOn f s := fun x hx â†¦
  (hf x hx).continuousWithinAt

theorem ContMDiff.continuous (hf : ContMDiff I I' n f) : Continuous f :=
  continuous_iff_continuousAt.2 fun x â†¦ (hf x).continuousAt

/-! ### `C^âˆ` functions -/

theorem contMDiffWithinAt_infty :
    ContMDiffWithinAt I I' âˆ f s x â†” âˆ€ n : â„•, ContMDiffWithinAt I I' n f s x :=
  âŸ¨fun h n â†¦ âŸ¨h.1, contDiffWithinAt_infty.1 h.2 nâŸ©, fun H â†¦
    âŸ¨(H 0).1, contDiffWithinAt_infty.2 fun n â†¦ (H n).2âŸ©âŸ©

theorem contMDiffAt_infty : ContMDiffAt I I' âˆ f x â†” âˆ€ n : â„•, ContMDiffAt I I' n f x :=
  contMDiffWithinAt_infty

theorem contMDiffOn_infty : ContMDiffOn I I' âˆ f s â†” âˆ€ n : â„•, ContMDiffOn I I' n f s :=
  âŸ¨fun h _ â†¦ h.of_le (mod_cast le_top),
    fun h x hx â†¦ contMDiffWithinAt_infty.2 fun n â†¦ h n x hxâŸ©

theorem contMDiff_infty : ContMDiff I I' âˆ f â†” âˆ€ n : â„•, ContMDiff I I' n f :=
  âŸ¨fun h _ â†¦ h.of_le (mod_cast le_top), fun h x â†¦ contMDiffWithinAt_infty.2 fun n â†¦ h n xâŸ©

theorem contMDiffWithinAt_iff_nat {n : â„•âˆ} :
    ContMDiffWithinAt I I' n f s x â†” âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContMDiffWithinAt I I' m f s x := by
  refine âŸ¨fun h m hm â†¦ h.of_le (mod_cast hm), fun h â†¦ ?_âŸ©
  obtain - | n := n
  Â· exact contMDiffWithinAt_infty.2 fun n â†¦ h n le_top
  Â· exact h n le_rfl

theorem contMDiffAt_iff_nat {n : â„•âˆ} :
    ContMDiffAt I I' n f x â†” âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContMDiffAt I I' m f x := by
  simp [â† contMDiffWithinAt_univ, contMDiffWithinAt_iff_nat]

/-- A function is `C^n` within a set at a point iff it is `C^m` within this set at this point, for
any `m â‰¤ n` which is different from `âˆ`. This result is useful because, when `m â‰  âˆ`, being
`C^m` extends locally to a neighborhood, giving flexibility for local proofs. -/
theorem contMDiffWithinAt_iff_le_ne_infty :
    ContMDiffWithinAt I I' n f s x â†” âˆ€ m, m â‰¤ n â†’ m â‰  âˆ â†’ ContMDiffWithinAt I I' m f s x := by
  refine âŸ¨fun h m hm h'm â†¦ h.of_le hm, fun h â†¦ ?_âŸ©
  cases n with
  | top =>
    exact h _ le_rfl (by simp)
  | coe n =>
    exact contMDiffWithinAt_iff_nat.2 (fun m hm â†¦ h _ (mod_cast hm) (by simp))

/-- A function is `C^n`at a point iff it is `C^m`at this point, for
any `m â‰¤ n` which is different from `âˆ`. This result is useful because, when `m â‰  âˆ`, being
`C^m` extends locally to a neighborhood, giving flexibility for local proofs. -/
theorem contMDiffAt_iff_le_ne_infty :
    ContMDiffAt I I' n f x â†” âˆ€ m, m â‰¤ n â†’ m â‰  âˆ â†’ ContMDiffAt I I' m f x := by
  simp only [â† contMDiffWithinAt_univ]
  rw [contMDiffWithinAt_iff_le_ne_infty]

/-! ### Restriction to a smaller set -/

theorem ContMDiffWithinAt.mono_of_mem_nhdsWithin
    (hf : ContMDiffWithinAt I I' n f s x) (hts : s âˆˆ ğ“[t] x) :
    ContMDiffWithinAt I I' n f t x :=
  StructureGroupoid.LocalInvariantProp.liftPropWithinAt_mono_of_mem_nhdsWithin
    (contDiffWithinAtProp_mono_of_mem_nhdsWithin n) hf hts

theorem ContMDiffWithinAt.mono (hf : ContMDiffWithinAt I I' n f s x) (hts : t âŠ† s) :
    ContMDiffWithinAt I I' n f t x :=
  hf.mono_of_mem_nhdsWithin <| mem_of_superset self_mem_nhdsWithin hts

theorem contMDiffWithinAt_congr_set (h : s =á¶ [ğ“ x] t) :
    ContMDiffWithinAt I I' n f s x â†” ContMDiffWithinAt I I' n f t x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_set h

theorem ContMDiffWithinAt.congr_set (h : ContMDiffWithinAt I I' n f s x) (hst : s =á¶ [ğ“ x] t) :
    ContMDiffWithinAt I I' n f t x :=
  (contMDiffWithinAt_congr_set hst).1 h

theorem contMDiffWithinAt_insert_self :
    ContMDiffWithinAt I I' n f (insert x s) x â†” ContMDiffWithinAt I I' n f s x := by
  simp only [contMDiffWithinAt_iff, continuousWithinAt_insert_self]
  refine Iff.rfl.and <| (contDiffWithinAt_congr_set ?_).trans contDiffWithinAt_insert_self
  simp only [â† map_extChartAt_nhdsWithin, nhdsWithin_insert, Filter.map_sup, Filter.map_pure,
    â† nhdsWithin_eq_iff_eventuallyEq]

alias âŸ¨ContMDiffWithinAt.of_insert, _âŸ© := contMDiffWithinAt_insert_self

-- TODO: use `alias` again once it can make protected theorems
protected theorem ContMDiffWithinAt.insert (h : ContMDiffWithinAt I I' n f s x) :
    ContMDiffWithinAt I I' n f (insert x s) x :=
  contMDiffWithinAt_insert_self.2 h

/-- Being `C^n` in a set only depends on the germ of the set. Version where one only requires
the two sets to coincide locally in the complement of a point `y`. -/
theorem contMDiffWithinAt_congr_set' (y : M) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    ContMDiffWithinAt I I' n f s x â†” ContMDiffWithinAt I I' n f t x := by
  have : T1Space M := I.t1Space M
  rw [â† contMDiffWithinAt_insert_self (s := s), â† contMDiffWithinAt_insert_self (s := t)]
  exact contMDiffWithinAt_congr_set (eventuallyEq_insert h)

protected theorem ContMDiffAt.contMDiffWithinAt (hf : ContMDiffAt I I' n f x) :
    ContMDiffWithinAt I I' n f s x :=
  ContMDiffWithinAt.mono hf (subset_univ _)

theorem ContMDiffOn.mono (hf : ContMDiffOn I I' n f s) (hts : t âŠ† s) : ContMDiffOn I I' n f t :=
  fun x hx â†¦ (hf x (hts hx)).mono hts

protected theorem ContMDiff.contMDiffOn (hf : ContMDiff I I' n f) : ContMDiffOn I I' n f s :=
  fun x _ â†¦ (hf x).contMDiffWithinAt

theorem contMDiffWithinAt_inter' (ht : t âˆˆ ğ“[s] x) :
    ContMDiffWithinAt I I' n f (s âˆ© t) x â†” ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_inter' ht

theorem contMDiffWithinAt_inter (ht : t âˆˆ ğ“ x) :
    ContMDiffWithinAt I I' n f (s âˆ© t) x â†” ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_inter ht

protected theorem ContMDiffWithinAt.contMDiffAt
    (h : ContMDiffWithinAt I I' n f s x) (ht : s âˆˆ ğ“ x) :
    ContMDiffAt I I' n f x :=
  (contDiffWithinAt_localInvariantProp n).liftPropAt_of_liftPropWithinAt h ht

protected theorem ContMDiffOn.contMDiffAt (h : ContMDiffOn I I' n f s) (hx : s âˆˆ ğ“ x) :
    ContMDiffAt I I' n f x :=
  (h x (mem_of_mem_nhds hx)).contMDiffAt hx

theorem contMDiffOn_iff_source_of_mem_maximalAtlas [IsManifold I n M]
    (he : e âˆˆ maximalAtlas I n M) (hs : s âŠ† e.source) :
    ContMDiffOn I I' n f s â†”
      ContMDiffOn ğ“˜(ğ•œ, E) I' n (f âˆ˜ (e.extend I).symm) (e.extend I '' s) := by
  simp_rw [ContMDiffOn, Set.forall_mem_image]
  refine forallâ‚‚_congr fun x hx â†¦ ?_
  rw [contMDiffWithinAt_iff_source_of_mem_maximalAtlas he (hs hx)]
  apply contMDiffWithinAt_congr_set
  simp_rw [e.extend_symm_preimage_inter_range_eventuallyEq hs (hs hx)]

-- Porting note: didn't compile; fixed by golfing the proof and moving parts to lemmas
/-- A function is `C^n` within a set at a point, for `n : â„•` or `n = Ï‰`,
if and only if it is `C^n` on a neighborhood of this point. -/
theorem contMDiffWithinAt_iff_contMDiffOn_nhds
    [IsManifold I n M] [IsManifold I' n M'] (hn : n â‰  âˆ) :
    ContMDiffWithinAt I I' n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x, ContMDiffOn I I' n f u := by
  -- WLOG, `x âˆˆ s`, otherwise we add `x` to `s`
  wlog hxs : x âˆˆ s generalizing s
  Â· rw [â† contMDiffWithinAt_insert_self, this (mem_insert _ _), insert_idem]
  rw [insert_eq_of_mem hxs]
  -- The `â†` implication is trivial
  refine âŸ¨fun h â†¦ ?_, fun âŸ¨u, hmem, huâŸ© â†¦
    (hu _ (mem_of_mem_nhdsWithin hxs hmem)).mono_of_mem_nhdsWithin hmemâŸ©
  -- The property is true in charts. Let `v` be a good neighborhood in the chart where the function
  -- is `Câ¿`.
  rcases (contMDiffWithinAt_iff'.1 h).2.contDiffOn le_rfl (by simp [hn]) with âŸ¨v, hmem, hsub, hvâŸ©
  have hxs' : extChartAt I x x âˆˆ (extChartAt I x).target âˆ©
      (extChartAt I x).symm â»Â¹' (s âˆ© f â»Â¹' (extChartAt I' (f x)).source) :=
    âŸ¨(extChartAt I x).map_source (mem_extChartAt_source _), by rwa [extChartAt_to_inv], by
      rw [extChartAt_to_inv]; apply mem_extChartAt_sourceâŸ©
  rw [insert_eq_of_mem hxs'] at hmem hsub
  -- Then `(extChartAt I x).symm '' v` is the neighborhood we are looking for.
  refine âŸ¨(extChartAt I x).symm '' v, ?_, ?_âŸ©
  Â· rw [â† map_extChartAt_symm_nhdsWithin (I := I),
      h.1.nhdsWithin_extChartAt_symm_preimage_inter_range (I := I) (I' := I')]
    exact image_mem_map hmem
  Â· have hvâ‚ : (extChartAt I x).symm '' v âŠ† (extChartAt I x).source :=
      image_subset_iff.2 fun y hy â†¦ (extChartAt I x).map_target (hsub hy).1
    have hvâ‚‚ : MapsTo f ((extChartAt I x).symm '' v) (extChartAt I' (f x)).source := by
      rintro _ âŸ¨y, hy, rflâŸ©
      exact (hsub hy).2.2
    rwa [contMDiffOn_iff_of_subset_source' hvâ‚ hvâ‚‚, PartialEquiv.image_symm_image_of_subset_target]
    exact hsub.trans inter_subset_left

/-- If a function is `C^m` within a set at a point, for some finite `m`, then it is `C^m` within
this set on an open set around the basepoint. -/
theorem ContMDiffWithinAt.contMDiffOn'
    [IsManifold I n M] [IsManifold I' n M']
    (hm : m â‰¤ n) (h' : m = âˆ â†’ n = Ï‰)
    (h : ContMDiffWithinAt I I' n f s x) :
    âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContMDiffOn I I' m f (insert x s âˆ© u) := by
  have : IsManifold I m M := .of_le hm
  have : IsManifold I' m M' := .of_le hm
  match m with
  | (m : â„•) | Ï‰ =>
    rcases (contMDiffWithinAt_iff_contMDiffOn_nhds (by simp)).1 (h.of_le hm) with âŸ¨t, ht, h'tâŸ©
    rcases mem_nhdsWithin.1 ht with âŸ¨u, u_open, xu, huâŸ©
    rw [inter_comm] at hu
    exact âŸ¨u, u_open, xu, h't.mono huâŸ©
  | âˆ =>
    rcases (contMDiffWithinAt_iff_contMDiffOn_nhds (by simp [h'])).1 h with âŸ¨t, ht, h'tâŸ©
    rcases mem_nhdsWithin.1 ht with âŸ¨u, u_open, xu, huâŸ©
    rw [inter_comm] at hu
    exact âŸ¨u, u_open, xu, (h't.mono hu).of_le hmâŸ©

/-- If a function is `C^m` within a set at a point, for some finite `m`, then it is `C^m` within
this set on a neighborhood of the basepoint. -/
theorem ContMDiffWithinAt.contMDiffOn
    [IsManifold I n M] [IsManifold I' n M']
    (hm : m â‰¤ n) (h' : m = âˆ â†’ n = Ï‰)
    (h : ContMDiffWithinAt I I' n f s x) :
    âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ ContMDiffOn I I' m f u := by
  let âŸ¨_u, uo, xu, hâŸ© := h.contMDiffOn' hm h'
  exact âŸ¨_, inter_mem_nhdsWithin _ (uo.mem_nhds xu), inter_subset_left, hâŸ©

/-- A function is `C^n` at a point, for `n : â„•`, if and only if it is `C^n` on
a neighborhood of this point. -/
theorem contMDiffAt_iff_contMDiffOn_nhds
    [IsManifold I n M] [IsManifold I' n M'] (hn : n â‰  âˆ) :
    ContMDiffAt I I' n f x â†” âˆƒ u âˆˆ ğ“ x, ContMDiffOn I I' n f u := by
  simp [â† contMDiffWithinAt_univ, contMDiffWithinAt_iff_contMDiffOn_nhds hn, nhdsWithin_univ]

/-- Note: This does not hold for `n = âˆ`. `f` being `C^âˆ` at `x` means that for every `n`, `f` is
`C^n` on some neighborhood of `x`, but this neighborhood can depend on `n`. -/
theorem contMDiffAt_iff_contMDiffAt_nhds
    [IsManifold I n M] [IsManifold I' n M'] (hn : n â‰  âˆ) :
    ContMDiffAt I I' n f x â†” âˆ€á¶  x' in ğ“ x, ContMDiffAt I I' n f x' := by
  refine âŸ¨?_, fun h â†¦ h.self_of_nhdsâŸ©
  rw [contMDiffAt_iff_contMDiffOn_nhds hn]
  rintro âŸ¨u, hu, hâŸ©
  refine (eventually_mem_nhds_iff.mpr hu).mono fun x' hx' â†¦ ?_
  exact (h x' <| mem_of_mem_nhds hx').contMDiffAt hx'

/-- Note: This does not hold for `n = âˆ`. `f` being `C^âˆ` at `x` means that for every `n`, `f` is
`C^n` on some neighborhood of `x`, but this neighborhood can depend on `n`. -/
theorem contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin
    [IsManifold I n M] [IsManifold I' n M'] (hn : n â‰  âˆ) :
    ContMDiffWithinAt I I' n f s x â†”
      âˆ€á¶  x' in ğ“[insert x s] x, ContMDiffWithinAt I I' n f s x' := by
  refine âŸ¨?_, fun h â†¦ mem_of_mem_nhdsWithin (mem_insert x s) hâŸ©
  rw [contMDiffWithinAt_iff_contMDiffOn_nhds hn]
  rintro âŸ¨u, hu, hâŸ©
  filter_upwards [hu, eventually_mem_nhdsWithin_iff.mpr hu] with x' h'x' hx'
  apply (h x' h'x').mono_of_mem_nhdsWithin
  exact nhdsWithin_mono _ (subset_insert x s) hx'

/-! ### Congruence lemmas -/

theorem ContMDiffWithinAt.congr (h : ContMDiffWithinAt I I' n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : fâ‚ x = f x) : ContMDiffWithinAt I I' n fâ‚ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr h hâ‚ hx

/-- Version of `ContMDiffWithinAt.congr` where `x` need not be contained in `s`,
but `f` and `fâ‚` are equal on a set containing both. -/
theorem ContMDiffWithinAt.congr' (h : ContMDiffWithinAt I I' n f s x) (hâ‚ : âˆ€ y âˆˆ t, fâ‚ y = f y)
    (hst : s âŠ† t) (hxt : x âˆˆ t) :
    ContMDiffWithinAt I I' n fâ‚ s x :=
  h.congr (fun _y hy â†¦ hâ‚ _ (hst hy)) (hâ‚ x hxt)

theorem contMDiffWithinAt_congr (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : fâ‚ x = f x) :
    ContMDiffWithinAt I I' n fâ‚ s x â†” ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_iff hâ‚ hx

theorem ContMDiffWithinAt.congr_of_mem
    (h : ContMDiffWithinAt I I' n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : x âˆˆ s) :
    ContMDiffWithinAt I I' n fâ‚ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_of_mem h hâ‚ hx

theorem contMDiffWithinAt_congr_of_mem (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : x âˆˆ s) :
    ContMDiffWithinAt I I' n fâ‚ s x â†” ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_iff_of_mem hâ‚ hx

theorem ContMDiffWithinAt.congr_of_eventuallyEq (h : ContMDiffWithinAt I I' n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : ContMDiffWithinAt I I' n fâ‚ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_of_eventuallyEq h hâ‚ hx

theorem ContMDiffWithinAt.congr_of_eventuallyEq_of_mem (h : ContMDiffWithinAt I I' n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : ContMDiffWithinAt I I' n fâ‚ s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_of_eventuallyEq_of_mem h hâ‚ hx

theorem Filter.EventuallyEq.contMDiffWithinAt_iff (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    ContMDiffWithinAt I I' n fâ‚ s x â†” ContMDiffWithinAt I I' n f s x :=
  (contDiffWithinAt_localInvariantProp n).liftPropWithinAt_congr_iff_of_eventuallyEq hâ‚ hx

theorem ContMDiffAt.congr_of_eventuallyEq (h : ContMDiffAt I I' n f x) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    ContMDiffAt I I' n fâ‚ x :=
  (contDiffWithinAt_localInvariantProp n).liftPropAt_congr_of_eventuallyEq h hâ‚

theorem Filter.EventuallyEq.contMDiffAt_iff (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    ContMDiffAt I I' n fâ‚ x â†” ContMDiffAt I I' n f x :=
  (contDiffWithinAt_localInvariantProp n).liftPropAt_congr_iff_of_eventuallyEq hâ‚

theorem ContMDiffOn.congr (h : ContMDiffOn I I' n f s) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) :
    ContMDiffOn I I' n fâ‚ s :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_congr h hâ‚

theorem contMDiffOn_congr (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) :
    ContMDiffOn I I' n fâ‚ s â†” ContMDiffOn I I' n f s :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_congr_iff hâ‚

theorem ContMDiffOn.congr_mono (hf : ContMDiffOn I I' n f s) (hâ‚ : âˆ€ y âˆˆ sâ‚, fâ‚ y = f y)
    (hs : sâ‚ âŠ† s) : ContMDiffOn I I' n fâ‚ sâ‚ :=
  (hf.mono hs).congr hâ‚

/-! ### Locality -/


/-- Being `C^n` is a local property. -/
theorem contMDiffOn_of_locally_contMDiffOn
    (h : âˆ€ x âˆˆ s, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContMDiffOn I I' n f (s âˆ© u)) : ContMDiffOn I I' n f s :=
  (contDiffWithinAt_localInvariantProp n).liftPropOn_of_locally_liftPropOn h

theorem contMDiff_of_locally_contMDiffOn (h : âˆ€ x, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContMDiffOn I I' n f u) :
    ContMDiff I I' n f :=
  (contDiffWithinAt_localInvariantProp n).liftProp_of_locally_liftPropOn h
