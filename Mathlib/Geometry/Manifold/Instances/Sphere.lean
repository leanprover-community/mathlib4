/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.NormedSpace.BallAction
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Geometry.Manifold.Algebra.LieGroup
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.MFDeriv

#align_import geometry.manifold.instances.sphere from "leanprover-community/mathlib"@"0dc4079202c28226b2841a51eb6d3cc2135bb80f"

/-!
# Manifold structure on the sphere

This file defines stereographic projection from the sphere in an inner product space `E`, and uses
it to put a smooth manifold structure on the sphere.

## Main results

For a unit vector `v` in `E`, the definition `stereographic` gives the stereographic projection
centred at `v`, a local homeomorphism from the sphere to `(â„ âˆ™ v)á—®` (the orthogonal complement of
`v`).

For finite-dimensional `E`, we then construct a smooth manifold instance on the sphere; the charts
here are obtained by composing the local homeomorphisms `stereographic` with arbitrary isometries
from `(â„ âˆ™ v)á—®` to Euclidean space.

We prove two lemmas about smooth maps:
* `contMDiff_coe_sphere` states that the coercion map from the sphere into `E` is smooth;
  this is a useful tool for constructing smooth maps *from* the sphere.
* `contMDiff.codRestrict_sphere` states that a map from a manifold into the sphere is
  smooth if its lift to a map to `E` is smooth; this is a useful tool for constructing smooth maps
  *to* the sphere.

As an application we prove `contMdiffNegSphere`, that the antipodal map is smooth.

Finally, we equip the `circle` (defined in `Analysis.Complex.Circle` to be the sphere in `â„‚`
centred at `0` of radius `1`) with the following structure:
* a charted space with model space `EuclideanSpace â„ (Fin 1)` (inherited from `Metric.Sphere`)
* a Lie group with model with corners `ğ“¡ 1`

We furthermore show that `expMapCircle` (defined in `Analysis.Complex.Circle` to be the natural
map `fun t â†¦ exp (t * I)` from `â„` to `circle`) is smooth.


## Implementation notes

The model space for the charted space instance is `EuclideanSpace â„ (Fin n)`, where `n` is a
natural number satisfying the typeclass assumption `[Fact (finrank â„ E = n + 1)]`.  This may seem a
little awkward, but it is designed to circumvent the problem that the literal expression for the
dimension of the model space (up to definitional equality) determines the type.  If one used the
naive expression `EuclideanSpace â„ (Fin (finrank â„ E - 1))` for the model space, then the sphere in
`â„‚` would be a manifold with model space `EuclideanSpace â„ (Fin (2 - 1))` but not with model space
`EuclideanSpace â„ (Fin 1)`.

## TODO

Relate the stereographic projection to the inversion of the space.
-/


variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]

noncomputable section

open Metric FiniteDimensional Function

open scoped Manifold

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

section StereographicProjection

variable (v : E)

/-! ### Construction of the stereographic projection -/


/-- Stereographic projection, forward direction. This is a map from an inner product space `E` to
the orthogonal complement of an element `v` of `E`. It is smooth away from the affine hyperplane
through `v` parallel to the orthogonal complement.  It restricts on the sphere to the stereographic
projection. -/
def stereoToFun (x : E) : (â„ âˆ™ v)á—® :=
  (2 / ((1 : â„) - innerSL â„ v x)) â€¢ orthogonalProjection (â„ âˆ™ v)á—® x
#align stereo_to_fun stereoToFun

variable {v}

@[simp]
theorem stereoToFun_apply (x : E) :
    stereoToFun v x = (2 / ((1 : â„) - innerSL â„ v x)) â€¢ orthogonalProjection (â„ âˆ™ v)á—® x :=
  rfl
#align stereo_to_fun_apply stereoToFun_apply

theorem contDiffOn_stereoToFun :
    ContDiffOn â„ âŠ¤ (stereoToFun v) {x : E | innerSL _ v x â‰  (1 : â„)} := by
  refine' ContDiffOn.smul _ (orthogonalProjection (â„ âˆ™ v)á—®).contDiff.contDiffOn
  refine' contDiff_const.contDiffOn.div _ _
  Â· exact (contDiff_const.sub (innerSL â„ v).contDiff).contDiffOn
  Â· intro x h h'
    exact h (sub_eq_zero.mp h').symm
#align cont_diff_on_stereo_to_fun contDiffOn_stereoToFun

theorem continuousOn_stereoToFun :
    ContinuousOn (stereoToFun v) {x : E | innerSL _ v x â‰  (1 : â„)} :=
  contDiffOn_stereoToFun.continuousOn
#align continuous_on_stereo_to_fun continuousOn_stereoToFun

variable (v)

/-- Auxiliary function for the construction of the reverse direction of the stereographic
projection.  This is a map from the orthogonal complement of a unit vector `v` in an inner product
space `E` to `E`; we will later prove that it takes values in the unit sphere.

For most purposes, use `stereoInvFun`, not `stereoInvFunAux`. -/
def stereoInvFunAux (w : E) : E :=
  (â€–wâ€– ^ 2 + 4)â»Â¹ â€¢ ((4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v)
#align stereo_inv_fun_aux stereoInvFunAux

variable {v}

@[simp]
theorem stereoInvFunAux_apply (w : E) :
    stereoInvFunAux v w = (â€–wâ€– ^ 2 + 4)â»Â¹ â€¢ ((4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v) :=
  rfl
#align stereo_inv_fun_aux_apply stereoInvFunAux_apply

theorem stereoInvFunAux_mem (hv : â€–vâ€– = 1) {w : E} (hw : w âˆˆ (â„ âˆ™ v)á—®) :
    stereoInvFunAux v w âˆˆ sphere (0 : E) 1 := by
  have hâ‚ : (0 : â„) < â€–wâ€– ^ 2 + 4 := by positivity
  suffices : â€–(4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ vâ€– = â€–wâ€– ^ 2 + 4
  Â· simp only [mem_sphere_zero_iff_norm, norm_smul, Real.norm_eq_abs, abs_inv, this,
      abs_of_pos hâ‚, stereoInvFunAux_apply, inv_mul_cancel hâ‚.ne']
  suffices : â€–(4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ vâ€– ^ 2 = (â€–wâ€– ^ 2 + 4) ^ 2
  Â· simpa [sq_eq_sq_iff_abs_eq_abs, abs_of_pos hâ‚] using this
  rw [Submodule.mem_orthogonal_singleton_iff_inner_left] at hw
  simp [norm_add_sq_real, norm_smul, inner_smul_left, inner_smul_right, hw, mul_pow,
    Real.norm_eq_abs, hv]
  ring
#align stereo_inv_fun_aux_mem stereoInvFunAux_mem

theorem hasFDerivAt_stereoInvFunAux (v : E) :
    HasFDerivAt (stereoInvFunAux v) (ContinuousLinearMap.id â„ E) 0 := by
  have hâ‚€ : HasFDerivAt (fun w : E => â€–wâ€– ^ 2) (0 : E â†’L[â„] â„) 0 := by
    convert (hasStrictFDerivAt_norm_sq (0 : E)).hasFDerivAt
    simp
  have hâ‚ : HasFDerivAt (fun w : E => (â€–wâ€– ^ 2 + 4)â»Â¹) (0 : E â†’L[â„] â„) 0 := by
    convert (hasFDerivAt_inv _).comp _ (hâ‚€.add (hasFDerivAt_const 4 0)) <;> simp
  have hâ‚‚ :
    HasFDerivAt (fun w => (4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v) ((4 : â„) â€¢ ContinuousLinearMap.id â„ E) 0
  Â· convert ((hasFDerivAt_const (4 : â„) 0).smul (hasFDerivAt_id 0)).add
      ((hâ‚€.sub (hasFDerivAt_const (4 : â„) 0)).smul (hasFDerivAt_const v 0)) using 1
    ext w
    simp
  convert hâ‚.smul hâ‚‚ using 1
  ext w
  simp
#align has_fderiv_at_stereo_inv_fun_aux hasFDerivAt_stereoInvFunAux

theorem hasFDerivAt_stereoInvFunAux_comp_coe (v : E) :
    HasFDerivAt (stereoInvFunAux v âˆ˜ ((â†‘) : (â„ âˆ™ v)á—® â†’ E)) (â„ âˆ™ v)á—®.subtypeL 0 := by
  have : HasFDerivAt (stereoInvFunAux v) (ContinuousLinearMap.id â„ E) ((â„ âˆ™ v)á—®.subtypeL 0) :=
    hasFDerivAt_stereoInvFunAux v
  convert this.comp (0 : (â„ âˆ™ v)á—®) (by apply ContinuousLinearMap.hasFDerivAt)
#align has_fderiv_at_stereo_inv_fun_aux_comp_coe hasFDerivAt_stereoInvFunAux_comp_coe

theorem contDiff_stereoInvFunAux : ContDiff â„ âŠ¤ (stereoInvFunAux v) := by
  have hâ‚€ : ContDiff â„ âŠ¤ fun w : E => â€–wâ€– ^ 2 := contDiff_norm_sq â„
  have hâ‚ : ContDiff â„ âŠ¤ fun w : E => (â€–wâ€– ^ 2 + 4)â»Â¹ := by
    refine' (hâ‚€.add contDiff_const).inv _
    intro x
    nlinarith
  have hâ‚‚ : ContDiff â„ âŠ¤ fun w => (4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v := by
    refine' (contDiff_const.smul contDiff_id).add _
    refine' (hâ‚€.sub contDiff_const).smul contDiff_const
  exact hâ‚.smul hâ‚‚
#align cont_diff_stereo_inv_fun_aux contDiff_stereoInvFunAux

/-- Stereographic projection, reverse direction.  This is a map from the orthogonal complement of a
unit vector `v` in an inner product space `E` to the unit sphere in `E`. -/
def stereoInvFun (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) : sphere (0 : E) 1 :=
  âŸ¨stereoInvFunAux v (w : E), stereoInvFunAux_mem hv w.2âŸ©
#align stereo_inv_fun stereoInvFun

@[simp]
theorem stereoInvFun_apply (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) :
    (stereoInvFun hv w : E) = (â€–wâ€– ^ 2 + 4)â»Â¹ â€¢ ((4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v) :=
  rfl
#align stereo_inv_fun_apply stereoInvFun_apply

theorem stereoInvFun_ne_north_pole (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) :
    stereoInvFun hv w â‰  (âŸ¨v, by simp [hv]âŸ© : sphere (0 : E) 1) := by
  refine' Subtype.ne_of_val_ne _
  rw [â† inner_lt_one_iff_real_of_norm_one _ hv]
  Â· have hw : âŸªv, wâŸ«_â„ = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp w.2
    have hw' : (â€–(w : E)â€– ^ 2 + 4)â»Â¹ * (â€–(w : E)â€– ^ 2 - 4) < 1 := by
      refine' (inv_mul_lt_iff' _).mpr _
      Â· nlinarith
      linarith
    simpa [real_inner_comm, inner_add_right, inner_smul_right, real_inner_self_eq_norm_mul_norm, hw,
      hv] using hw'
  Â· simpa using stereoInvFunAux_mem hv w.2
#align stereo_inv_fun_ne_north_pole stereoInvFun_ne_north_pole

theorem continuous_stereoInvFun (hv : â€–vâ€– = 1) : Continuous (stereoInvFun hv) :=
  continuous_induced_rng.2 (contDiff_stereoInvFunAux.continuous.comp continuous_subtype_val)
#align continuous_stereo_inv_fun continuous_stereoInvFun

theorem stereo_left_inv (hv : â€–vâ€– = 1) {x : sphere (0 : E) 1} (hx : (x : E) â‰  v) :
    stereoInvFun hv (stereoToFun v x) = x := by
  ext
  simp only [stereoToFun_apply, stereoInvFun_apply, smul_add]
  -- name two frequently-occuring quantities and write down their basic properties
  set a : â„ := innerSL _ v x
  set y := orthogonalProjection (â„ âˆ™ v)á—® x
  have split : â†‘x = a â€¢ v + â†‘y := by
    convert (orthogonalProjection_add_orthogonalProjection_orthogonal (â„ âˆ™ v) x).symm
    exact (orthogonalProjection_unit_singleton â„ hv x).symm
  have hvy : âŸªv, yâŸ«_â„ = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp y.2
  have pythag : 1 = a ^ 2 + â€–yâ€– ^ 2 := by
    have hvy' : âŸªa â€¢ v, yâŸ«_â„ = 0 := by simp only [inner_smul_left, hvy, mul_zero]
    convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _ hvy' using 2
    -- Porting note: was simp [â† split] but wasn't finding `norm_eq_of_mem_sphere`
    Â· simp only [norm_eq_of_mem_sphere, Nat.cast_one, mul_one, â† split]
    Â· simp [norm_smul, hv, â† sq, sq_abs]
    Â· exact sq _
  -- Porting note : added to work around cancel_denoms and nlinarith failures
  have duh : â€–y.valâ€– ^ 2 = 1 - a ^ 2 := by
    rw [â†Submodule.coe_norm, pythag]; ring
  -- two facts which will be helpful for clearing denominators in the main calculation
  have ha : 1 - a â‰  0 := by
    have : a < 1 := (inner_lt_one_iff_real_of_norm_one hv (by simp)).mpr hx.symm
    linarith
  have : 2 ^ 2 * â€–yâ€– ^ 2 + 4 * (1 - a) ^ 2 â‰  0 := by
    refine' ne_of_gt _
    have : (0 : â„) < (1 - a) ^ 2 := sq_pos_of_ne_zero (1 - a) ha
    -- Porting note: nlinarith needed a little help
    change 0 < 4 * _ + 4 * _
    nlinarith
  -- the core of the problem is these two algebraic identities:
  have hâ‚ : (2 ^ 2 / (1 - a) ^ 2 * â€–yâ€– ^ 2 + 4)â»Â¹ * 4 * (2 / (1 - a)) = 1 := by
    -- Porting note: used to be `field_simp; simp only [Submodule.coe_norm] at *; nlinarith`
    -- but cancel_denoms does not seem to be working and
    -- nlinarith cannot close the goal even if it did
    -- clear_value because field_simp does zeta-reduction (by design?) and is annoying
    clear_value a y
    field_simp
    rw [div_eq_iff, duh]
    Â· ring
    Â· apply mul_ne_zero_iff.mpr âŸ¨?_,haâŸ©
      convert this using 2; rw [Submodule.coe_norm]; ring
  have hâ‚‚ : (2 ^ 2 / (1 - a) ^ 2 * â€–yâ€– ^ 2 + 4)â»Â¹ * (2 ^ 2 / (1 - a) ^ 2 * â€–yâ€– ^ 2 - 4) = a := by
    -- Porting note: field_simp is not behaving as in ml3
    -- see porting note above; previous proof used trans and was comparably complicated
    clear_value a y
    field_simp
    rw [div_eq_iff, duh]
    ring_nf
    -- Porting note: shouldn't repeat myself but getting the coercion right is annoying
    apply mul_ne_zero_iff.mpr âŸ¨?_,?_âŸ©
    Â· convert this using 2; rw [Submodule.coe_norm]; ring
    Â· apply pow_ne_zero _ ha
  convert
    congr_argâ‚‚ Add.add (congr_arg (fun t => t â€¢ (y : E)) hâ‚) (congr_arg (fun t => t â€¢ v) hâ‚‚) using 1
  Â· simp [inner_add_right, inner_smul_right, hvy, real_inner_self_eq_norm_mul_norm, hv, mul_smul,
      mul_pow, Real.norm_eq_abs, sq_abs, norm_smul]
    -- Porting note: used to be simp only [split, add_comm] but get maxRec errors
    Â· rw [split, add_comm]
      ac_rfl
  -- Porting note: this branch did not exit in ml3
  Â· rw [split, add_comm]
    congr!
    dsimp
    rw [one_smul]
#align stereo_left_inv stereo_left_inv

theorem stereo_right_inv (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) : stereoToFun v (stereoInvFun hv w) = w := by
  have : 2 / (1 - (â€–(w : E)â€– ^ 2 + 4)â»Â¹ * (â€–(w : E)â€– ^ 2 - 4)) * (â€–(w : E)â€– ^ 2 + 4)â»Â¹ * 4 = 1 := by
    have : â€–(w : E)â€– ^ 2 + 4 â‰  0 := by nlinarith
    have : (4 : â„) + 4 â‰  0 := by nlinarith
    field_simp
    ring
  convert congr_arg (Â· â€¢ w) this
  Â· have hâ‚ : orthogonalProjection (â„ âˆ™ v)á—® v = 0 :=
      orthogonalProjection_orthogonalComplement_singleton_eq_zero v
    -- Porting note: was innerSL _ and now just inner
    have hâ‚ƒ : inner v w = (0 : â„) := Submodule.mem_orthogonal_singleton_iff_inner_right.mp w.2
    -- Porting note: was innerSL _ and now just inner
    have hâ‚„ : inner v v = (1 : â„) := by simp [real_inner_self_eq_norm_mul_norm, hv]
    simp [hâ‚, hâ‚ƒ, hâ‚„, ContinuousLinearMap.map_add, ContinuousLinearMap.map_smul, mul_smul]
  Â· simp
#align stereo_right_inv stereo_right_inv

/-- Stereographic projection from the unit sphere in `E`, centred at a unit vector `v` in `E`; this
is the version as a local homeomorphism. -/
def stereographic (hv : â€–vâ€– = 1) : LocalHomeomorph (sphere (0 : E) 1) (â„ âˆ™ v)á—® where
  toFun := stereoToFun v âˆ˜ (â†‘)
  invFun := stereoInvFun hv
  source := {âŸ¨v, by simp [hv]âŸ©}á¶œ
  target := Set.univ
  map_source' := by simp
  map_target' {w} _ := fun h => (stereoInvFun_ne_north_pole hv w) (Set.eq_of_mem_singleton h)
  left_inv' x hx := stereo_left_inv hv fun h => hx (by
    rw [â†h] at hv
    apply Subtype.ext
    dsimp
    exact h)
  right_inv' w _ := stereo_right_inv hv w
  open_source := isOpen_compl_singleton
  open_target := isOpen_univ
  continuous_toFun :=
    continuousOn_stereoToFun.comp continuous_subtype_val.continuousOn fun w h => by
      dsimp
      exact
        h âˆ˜ Subtype.ext âˆ˜ Eq.symm âˆ˜ (inner_eq_one_iff_of_norm_one hv (by simp)).mp
  continuous_invFun := (continuous_stereoInvFun hv).continuousOn
#align stereographic stereographic

theorem stereographic_apply (hv : â€–vâ€– = 1) (x : sphere (0 : E) 1) :
    stereographic hv x = (2 / ((1 : â„) - inner v x)) â€¢ orthogonalProjection (â„ âˆ™ v)á—® x :=
  rfl
#align stereographic_apply stereographic_apply

@[simp]
theorem stereographic_source (hv : â€–vâ€– = 1) : (stereographic hv).source = {âŸ¨v, by simp [hv]âŸ©}á¶œ :=
  rfl
#align stereographic_source stereographic_source

@[simp]
theorem stereographic_target (hv : â€–vâ€– = 1) : (stereographic hv).target = Set.univ :=
  rfl
#align stereographic_target stereographic_target

@[simp]
theorem stereographic_apply_neg (v : sphere (0 : E) 1) :
    stereographic (norm_eq_of_mem_sphere v) (-v) = 0 := by
  simp [stereographic_apply, orthogonalProjection_orthogonalComplement_singleton_eq_zero]
#align stereographic_apply_neg stereographic_apply_neg

@[simp]
theorem stereographic_neg_apply (v : sphere (0 : E) 1) :
    stereographic (norm_eq_of_mem_sphere (-v)) v = 0 := by
  convert stereographic_apply_neg (-v)
  ext1
  simp
#align stereographic_neg_apply stereographic_neg_apply

end StereographicProjection

section ChartedSpace

/-!
### Charted space structure on the sphere

In this section we construct a charted space structure on the unit sphere in a finite-dimensional
real inner product space `E`; that is, we show that it is locally homeomorphic to the Euclidean
space of dimension one less than `E`.

The restriction to finite dimension is for convenience.  The most natural `charted_space`
structure for the sphere uses the stereographic projection from the antipodes of a point as the
canonical chart at this point.  However, the codomain of the stereographic projection constructed
in the previous section is `(â„ âˆ™ v)á—®`, the orthogonal complement of the vector `v` in `E` which is
the "north pole" of the projection, so a priori these charts all have different codomains.

So it is necessary to prove that these codomains are all continuously linearly equivalent to a
fixed normed space.  This could be proved in general by a simple case of Gram-Schmidt
orthogonalization, but in the finite-dimensional case it follows more easily by dimension-counting.
-/

-- Porting note: unnecessary in Lean 3
private theorem findim (n : â„•) [Fact (finrank â„ E = n + 1)] : FiniteDimensional â„ E :=
  fact_finiteDimensional_of_finrank_eq_succ n

/-- Variant of the stereographic projection, for the sphere in an `n + 1`-dimensional inner product
space `E`.  This version has codomain the Euclidean space of dimension `n`, and is obtained by
composing the original sterographic projection (`stereographic`) with an arbitrary linear isometry
from `(â„ âˆ™ v)á—®` to the Euclidean space. -/
def stereographic' (n : â„•) [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    LocalHomeomorph (sphere (0 : E) 1) (EuclideanSpace â„ (Fin n)) :=
  stereographic (norm_eq_of_mem_sphere v) â‰«â‚•
    (OrthonormalBasis.fromOrthogonalSpanSingleton n
            (ne_zero_of_mem_unit_sphere v)).repr.toHomeomorph.toLocalHomeomorph
#align stereographic' stereographic'

@[simp]
theorem stereographic'_source {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    (stereographic' n v).source = {v}á¶œ := by simp [stereographic']
#align stereographic'_source stereographic'_source

@[simp]
theorem stereographic'_target {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    (stereographic' n v).target = Set.univ := by simp [stereographic']
#align stereographic'_target stereographic'_target

/-- The unit sphere in an `n + 1`-dimensional inner product space `E` is a charted space
modelled on the Euclidean space of dimension `n`. -/
instance chartedSpace {n : â„•} [Fact (finrank â„ E = n + 1)] :
    ChartedSpace (EuclideanSpace â„ (Fin n)) (sphere (0 : E) 1) where
  atlas := {f | âˆƒ v : sphere (0 : E) 1, f = stereographic' n v}
  chartAt v := stereographic' n (-v)
  mem_chart_source v := by simpa using ne_neg_of_mem_unit_sphere â„ v
  chart_mem_atlas v := âŸ¨-v, rflâŸ©

end ChartedSpace

section SmoothManifold

theorem sphere_ext_iff (u v : sphere (0 : E) 1) : u = v â†” âŸª(u : E), vâŸ«_â„ = 1 := by
  simp [Subtype.ext_iff, inner_eq_one_iff_of_norm_one]
#align sphere_ext_iff sphere_ext_iff

theorem stereographic'_symm_apply {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1)
    (x : EuclideanSpace â„ (Fin n)) :
    ((stereographic' n v).symm x : E) =
      let U : (â„ âˆ™ (v : E))á—® â‰ƒâ‚—áµ¢[â„] EuclideanSpace â„ (Fin n) :=
        (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere v)).repr
      (â€–(U.symm x : E)â€– ^ 2 + 4)â»Â¹ â€¢ (4 : â„) â€¢ (U.symm x : E) +
        (â€–(U.symm x : E)â€– ^ 2 + 4)â»Â¹ â€¢ (â€–(U.symm x : E)â€– ^ 2 - 4) â€¢ v.val :=
  by simp [real_inner_comm, stereographic, stereographic', â† Submodule.coe_norm]
#align stereographic'_symm_apply stereographic'_symm_apply

/-! ### Smooth manifold structure on the sphere -/

/-- The unit sphere in an `n + 1`-dimensional inner product space `E` is a smooth manifold,
modelled on the Euclidean space of dimension `n`. -/
instance smoothMfldWithCorners {n : â„•} [Fact (finrank â„ E = n + 1)] :
    SmoothManifoldWithCorners (ğ“¡ n) (sphere (0 : E) 1) :=
  smoothManifoldWithCorners_of_contDiffOn (ğ“¡ n) (sphere (0 : E) 1)
    (by
      rintro _ _ âŸ¨v, rflâŸ© âŸ¨v', rflâŸ©
      let U :=
        (-- Removed type ascription, and this helped for some reason with timeout issues?
            OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„)
            n (ne_zero_of_mem_unit_sphere v)).repr
      let U' :=
        (-- Removed type ascription, and this helped for some reason with timeout issues?
            OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„)
            n (ne_zero_of_mem_unit_sphere v')).repr
      -- Porting note: trouble synth instances
      have := findim (E := E) n
      have Hâ‚ := U'.contDiff.comp_contDiffOn contDiffOn_stereoToFun
      -- Porting note: need to help with implicit variables again
      have Hâ‚‚ := (contDiff_stereoInvFunAux (v := v.val)|>.comp
        (â„ âˆ™ (v : E))á—®.subtypeL.contDiff).comp U.symm.contDiff
      convert Hâ‚.comp' (Hâ‚‚.contDiffOn : ContDiffOn â„ âŠ¤ _ Set.univ) using 1
      -- -- squeezed from `ext, simp [sphere_ext_iff, stereographic'_symm_apply, real_inner_comm]`
      simp only [LocalHomeomorph.trans_toLocalEquiv, LocalHomeomorph.symm_toLocalEquiv,
        LocalEquiv.trans_source, LocalEquiv.symm_source, stereographic'_target,
        stereographic'_source]
      simp only [modelWithCornersSelf_coe, modelWithCornersSelf_coe_symm, Set.preimage_id,
        Set.range_id, Set.inter_univ, Set.univ_inter, Set.compl_singleton_eq, Set.preimage_setOf_eq]
      simp only [id.def, comp_apply, Submodule.subtypeL_apply, LocalHomeomorph.coe_coe_symm,
        innerSL_apply, Ne.def, sphere_ext_iff, real_inner_comm (v' : E)]
      rfl)

/-- The inclusion map (i.e., `coe`) from the sphere in `E` to `E` is smooth.  -/
theorem contMDiff_coe_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] :
    ContMDiff (ğ“¡ n) ğ“˜(â„, E) âˆ ((â†‘) : sphere (0 : E) 1 â†’ E) := by
  -- Porting note: trouble with filling these implicit variables in the instance
  have := smoothMfldWithCorners (E := E) (n := n)
  rw [contMDiff_iff]
  constructor
  Â· exact continuous_subtype_val
  Â· intro v _
    let U : _ â‰ƒâ‚—áµ¢[â„] _ :=
      (-- Again, partially removing type ascription...
          OrthonormalBasis.fromOrthogonalSpanSingleton
          n (ne_zero_of_mem_unit_sphere (-v))).repr
    exact
      ((contDiff_stereoInvFunAux.comp (â„ âˆ™ (-v : E))á—®.subtypeL.contDiff).comp
          U.symm.contDiff).contDiffOn
#align cont_mdiff_coe_sphere contMDiff_coe_sphere

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

variable {H : Type*} [TopologicalSpace H] {I : ModelWithCorners â„ F H}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M]

/-- If a `cont_mdiff` function `f : M â†’ E`, where `M` is some manifold, takes values in the
sphere, then it restricts to a `cont_mdiff` function from `M` to the sphere. -/
theorem ContMDiff.codRestrict_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] {m : â„•âˆ} {f : M â†’ E}
    (hf : ContMDiff I ğ“˜(â„, E) m f) (hf' : âˆ€ x, f x âˆˆ sphere (0 : E) 1) :
    ContMDiff I (ğ“¡ n) m (Set.codRestrict _ _ hf' : M â†’ sphere (0 : E) 1) := by
  rw [contMDiff_iff_target]
  refine' âŸ¨continuous_induced_rng.2 hf.continuous, _âŸ©
  intro v
  let U : _ â‰ƒâ‚—áµ¢[â„] _ :=
    (-- Again, partially removing type ascription... Weird that this helps!
        OrthonormalBasis.fromOrthogonalSpanSingleton
        n (ne_zero_of_mem_unit_sphere (-v))).repr
  have h : ContDiffOn â„ âŠ¤ _ Set.univ := U.contDiff.contDiffOn
  have Hâ‚ := (h.comp' contDiffOn_stereoToFun).contMDiffOn
  have Hâ‚‚ : ContMDiffOn _ _ _ _ Set.univ := hf.contMDiffOn
  convert (Hâ‚.of_le le_top).comp' Hâ‚‚ using 1
  ext x
  have hfxv : f x = -â†‘v â†” âŸªf x, -â†‘vâŸ«_â„ = 1 := by
    have hfx : â€–f xâ€– = 1 := by simpa using hf' x
    rw [inner_eq_one_iff_of_norm_one hfx]
    exact norm_eq_of_mem_sphere (-v)
  -- Porting note: unfold more
  dsimp [chartAt, Set.codRestrict, ChartedSpace.chartAt]
  simp [not_iff_not, Subtype.ext_iff, hfxv, real_inner_comm]
#align cont_mdiff.cod_restrict_sphere ContMDiff.codRestrict_sphere

/-- The antipodal map is smooth. -/
theorem contMDiff_neg_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] :
    ContMDiff (ğ“¡ n) (ğ“¡ n) âˆ fun x : sphere (0 : E) 1 => -x := by
  -- this doesn't elaborate well in term mode
  apply ContMDiff.codRestrict_sphere
  apply contDiff_neg.contMDiff.comp _
  exact contMDiff_coe_sphere
#align cont_mdiff_neg_sphere contMDiff_neg_sphere

/-- Consider the differential of the inclusion of the sphere in `E` at the point `v` as a continuous
linear map from `TangentSpace (ğ“¡ n) v` to `E`.  The range of this map is the orthogonal complement
of `v` in `E`.

Note that there is an abuse here of the defeq between `E` and the tangent space to `E` at `(v:E`).
In general this defeq is not canonical, but in this case (the tangent space of a vector space) it is
canonical. -/
theorem range_mfderiv_coe_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    LinearMap.range (mfderiv (ğ“¡ n) ğ“˜(â„, E) ((â†‘) : sphere (0 : E) 1 â†’ E) v :
    TangentSpace (ğ“¡ n) v â†’L[â„] E) = (â„ âˆ™ (v : E))á—® := by
  rw [((contMDiff_coe_sphere v).mdifferentiableAt le_top).mfderiv]
  dsimp [chartAt]
  -- rw [LinearIsometryEquiv.toHomeomorph_symm]
  -- rw [â†LinearIsometryEquiv.coe_toHomeomorph]
  simp only [chartAt, stereographic_neg_apply, fderivWithin_univ,
    LinearIsometryEquiv.toHomeomorph_symm, LinearIsometryEquiv.coe_toHomeomorph,
    LinearIsometryEquiv.map_zero, mfld_simps]
  let U := (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) n
    (ne_zero_of_mem_unit_sphere (-v))).repr
  -- Porting note: this `suffices` was a `change`
  suffices :
    LinearMap.range (fderiv â„ ((stereoInvFunAux (-v : E) âˆ˜ (â†‘)) âˆ˜ U.symm) 0) = (â„ âˆ™ (v : E))á—®
  Â· convert this using 3
    show stereographic' n (-v) v = 0
    dsimp [stereographic']
    simp only [AddEquivClass.map_eq_zero_iff]
    apply stereographic_neg_apply
  have :
    HasFDerivAt (stereoInvFunAux (-v : E) âˆ˜ (Subtype.val : (â„ âˆ™ (â†‘(-v) : E))á—® â†’ E))
      (â„ âˆ™ (â†‘(-v) : E))á—®.subtypeL (U.symm 0) := by
    convert hasFDerivAt_stereoInvFunAux_comp_coe (-v : E)
    simp
  convert congrArg LinearMap.range (this.comp 0 U.symm.toContinuousLinearEquiv.hasFDerivAt).fderiv
  symm
  convert
    (U.symm : EuclideanSpace â„ (Fin n) â‰ƒâ‚—áµ¢[â„] (â„ âˆ™ (â†‘(-v) : E))á—®).range_comp
      (â„ âˆ™ (â†‘(-v) : E))á—®.subtype using 1
  simp only [Submodule.range_subtype, coe_neg_sphere]
  congr 1
  -- we must show `submodule.span â„ {v} = submodule.span â„ {-v}`
  apply Submodule.span_eq_span
  Â· simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    rw [â† Submodule.neg_mem_iff]
    exact Submodule.mem_span_singleton_self (-v : E)
  Â· simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    rw [Submodule.neg_mem_iff]
    exact Submodule.mem_span_singleton_self (v:E)
#align range_mfderiv_coe_sphere range_mfderiv_coe_sphere

/-- Consider the differential of the inclusion of the sphere in `E` at the point `v` as a continuous
linear map from `TangentSpace (ğ“¡ n) v` to `E`.  This map is injective. -/
theorem mfderiv_coe_sphere_injective {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    Injective (mfderiv (ğ“¡ n) ğ“˜(â„, E) ((â†‘) : sphere (0 : E) 1 â†’ E) v) := by
  rw [((contMDiff_coe_sphere v).mdifferentiableAt le_top).mfderiv]
  simp only [chartAt, stereographic', stereographic_neg_apply, fderivWithin_univ,
    LinearIsometryEquiv.toHomeomorph_symm, LinearIsometryEquiv.coe_toHomeomorph,
    LinearIsometryEquiv.map_zero, mfld_simps]
  let U := (OrthonormalBasis.fromOrthogonalSpanSingleton
      (ğ•œ := â„) n (ne_zero_of_mem_unit_sphere (-v))).repr
  suffices : Injective (fderiv â„ ((stereoInvFunAux (-v : E) âˆ˜ (â†‘)) âˆ˜ U.symm) 0)
  Â· convert this using 3
    show stereographic' n (-v) v = 0
    dsimp [stereographic']
    simp only [AddEquivClass.map_eq_zero_iff]
    apply stereographic_neg_apply
  have :
    HasFDerivAt (stereoInvFunAux (-v : E) âˆ˜ (Subtype.val : (â„ âˆ™ (â†‘(-v) : E))á—® â†’ E))
      (â„ âˆ™ (â†‘(-v) : E))á—®.subtypeL (U.symm 0) := by
    convert hasFDerivAt_stereoInvFunAux_comp_coe (-v : E)
    simp
  have := congr_arg FunLike.coe <| (this.comp 0 U.symm.toContinuousLinearEquiv.hasFDerivAt).fderiv
  refine Eq.subst this.symm ?_
  rw [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe]
  simpa using Subtype.coe_injective
#align mfderiv_coe_sphere_injective mfderiv_coe_sphere_injective

end SmoothManifold

section circle

open Complex

-- Porting note: 1+1 = 2 except when synthing instances
theorem finrank_real_complex_fact' : Fact (finrank â„ â„‚ = 1 + 1) :=
  finrank_real_complex_fact

attribute [local instance] finrank_real_complex_fact'

/-- The unit circle in `â„‚` is a charted space modelled on `EuclideanSpace â„ (Fin 1)`.  This
follows by definition from the corresponding result for `Metric.Sphere`. -/
instance : ChartedSpace (EuclideanSpace â„ (Fin 1)) circle :=
  chartedSpace

instance : SmoothManifoldWithCorners (ğ“¡ 1) circle :=
  smoothMfldWithCorners (E := â„‚)

/-- The unit circle in `â„‚` is a Lie group. -/
instance : LieGroup (ğ“¡ 1) circle where
  smooth_mul := by
    apply ContMDiff.codRestrict_sphere
    let c : circle â†’ â„‚ := (â†‘)
    have hâ‚‚ : ContMDiff (ğ“˜(â„, â„‚).prod ğ“˜(â„, â„‚)) ğ“˜(â„, â„‚) âˆ fun z : â„‚ Ã— â„‚ => z.fst * z.snd := by
      rw [contMDiff_iff]
      exact âŸ¨continuous_mul, fun x y => contDiff_mul.contDiffOnâŸ©
    -- Porting note: needed to fill in first 3 arguments or could not figure out typeclasses
    suffices hâ‚ : ContMDiff ((ğ“¡ 1).prod (ğ“¡ 1)) (ğ“˜(â„, â„‚).prod ğ“˜(â„, â„‚)) âŠ¤ (Prod.map c c)
    Â· apply hâ‚‚.comp hâ‚
    Â· apply ContMDiff.prod_map <;>
      exact contMDiff_coe_sphere
  smooth_inv := by
    apply ContMDiff.codRestrict_sphere
    simp only [â† coe_inv_circle, coe_inv_circle_eq_conj]
    exact Complex.conjCle.contDiff.contMDiff.comp contMDiff_coe_sphere

/-- The map `fun t â†¦ exp (t * I)` from `â„` to the unit circle in `â„‚` is smooth. -/
theorem contMDiff_expMapCircle : ContMDiff ğ“˜(â„, â„) (ğ“¡ 1) âˆ expMapCircle :=
  (contDiff_exp.comp (contDiff_id.smul contDiff_const)).contMDiff.codRestrict_sphere _
#align cont_mdiff_exp_map_circle contMDiff_expMapCircle

end circle
