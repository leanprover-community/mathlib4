/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.Normed.Module.Ball.Action
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Geometry.Manifold.Algebra.LieGroup
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.MFDeriv.Basic
import Mathlib.LinearAlgebra.Complex.FiniteDimensional
import Mathlib.Tactic.Module

/-!
# Manifold structure on the sphere

This file defines stereographic projection from the sphere in an inner product space `E`, and uses
it to put an analytic manifold structure on the sphere.

## Main results

For a unit vector `v` in `E`, the definition `stereographic` gives the stereographic projection
centred at `v`, a partial homeomorphism from the sphere to `(‚Ñù ‚àô v)·óÆ` (the orthogonal complement of
`v`).

For finite-dimensional `E`, we then construct an analytic manifold instance on the sphere; the
charts here are obtained by composing the partial homeomorphisms `stereographic` with arbitrary
isometries from `(‚Ñù ‚àô v)·óÆ` to Euclidean space.

We prove two lemmas about `C^n` maps:
* `contMDiff_coe_sphere` states that the coercion map from the sphere into `E` is analytic;
  this is a useful tool for constructing smooth maps *from* the sphere.
* `contMDiff.codRestrict_sphere` states that a map from a manifold into the sphere is
  `C^m` if its lift to a map to `E` is `C^m`; this is a useful tool for constructing `C^m` maps
  *to* the sphere.

As an application we prove `contMDiffNegSphere`, that the antipodal map is analytic.

Finally, we equip the `Circle` (defined in `Analysis.Complex.Circle` to be the sphere in `‚ÑÇ`
centred at `0` of radius `1`) with the following structure:
* a charted space with model space `EuclideanSpace ‚Ñù (Fin 1)` (inherited from `Metric.Sphere`)
* an analytic Lie group with model with corners `ùì° 1`

We furthermore show that `Circle.exp` (defined in `Analysis.Complex.Circle` to be the natural
map `fun t ‚Ü¶ exp (t * I)` from `‚Ñù` to `Circle`) is analytic.


## Implementation notes

The model space for the charted space instance is `EuclideanSpace ‚Ñù (Fin n)`, where `n` is a
natural number satisfying the typeclass assumption `[Fact (finrank ‚Ñù E = n + 1)]`.  This may seem a
little awkward, but it is designed to circumvent the problem that the literal expression for the
dimension of the model space (up to definitional equality) determines the type.  If one used the
naive expression `EuclideanSpace ‚Ñù (Fin (finrank ‚Ñù E - 1))` for the model space, then the sphere in
`‚ÑÇ` would be a manifold with model space `EuclideanSpace ‚Ñù (Fin (2 - 1))` but not with model space
`EuclideanSpace ‚Ñù (Fin 1)`.

## TODO

Relate the stereographic projection to the inversion of the space.
-/


variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E]

noncomputable section

open Metric Module Function

open scoped Manifold ContDiff RealInnerProductSpace

section StereographicProjection

variable (v : E)

/-! ### Construction of the stereographic projection -/


/-- Stereographic projection, forward direction. This is a map from an inner product space `E` to
the orthogonal complement of an element `v` of `E`. It is smooth away from the affine hyperplane
through `v` parallel to the orthogonal complement.  It restricts on the sphere to the stereographic
projection. -/
def stereoToFun (x : E) : (‚Ñù ‚àô v)·óÆ :=
  (2 / ((1 : ‚Ñù) - innerSL ‚Ñù v x)) ‚Ä¢ (‚Ñù ‚àô v)·óÆ.orthogonalProjection x

variable {v}

@[simp]
theorem stereoToFun_apply (x : E) :
    stereoToFun v x = (2 / ((1 : ‚Ñù) - innerSL ‚Ñù v x)) ‚Ä¢ (‚Ñù ‚àô v)·óÆ.orthogonalProjection x :=
  rfl

theorem contDiffOn_stereoToFun {n : WithTop ‚Ñï‚àû} :
    ContDiffOn ‚Ñù n (stereoToFun v) {x : E | innerSL _ v x ‚â† (1 : ‚Ñù)} := by
  refine ContDiffOn.smul ?_ (‚Ñù ‚àô v)·óÆ.orthogonalProjection.contDiff.contDiffOn
  refine contDiff_const.contDiffOn.div ?_ ?_
  ¬∑ exact (contDiff_const.sub (innerSL ‚Ñù v).contDiff).contDiffOn
  ¬∑ intro x h h'
    exact h (sub_eq_zero.mp h').symm

theorem continuousOn_stereoToFun :
    ContinuousOn (stereoToFun v) {x : E | innerSL _ v x ‚â† (1 : ‚Ñù)} :=
  (contDiffOn_stereoToFun (n := 0)).continuousOn

variable (v) in
/-- Auxiliary function for the construction of the reverse direction of the stereographic
projection.  This is a map from the orthogonal complement of a unit vector `v` in an inner product
space `E` to `E`; we will later prove that it takes values in the unit sphere.

For most purposes, use `stereoInvFun`, not `stereoInvFunAux`. -/
def stereoInvFunAux (w : E) : E :=
  (‚Äñw‚Äñ ^ 2 + 4)‚Åª¬π ‚Ä¢ ((4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v)

@[simp]
theorem stereoInvFunAux_apply (w : E) :
    stereoInvFunAux v w = (‚Äñw‚Äñ ^ 2 + 4)‚Åª¬π ‚Ä¢ ((4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v) :=
  rfl

theorem stereoInvFunAux_mem (hv : ‚Äñv‚Äñ = 1) {w : E} (hw : w ‚àà (‚Ñù ‚àô v)·óÆ) :
    stereoInvFunAux v w ‚àà sphere (0 : E) 1 := by
  have h‚ÇÅ : (0 : ‚Ñù) < ‚Äñw‚Äñ ^ 2 + 4 := by positivity
  suffices ‚Äñ(4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v‚Äñ = ‚Äñw‚Äñ ^ 2 + 4 by
    simp only [mem_sphere_zero_iff_norm, norm_smul, Real.norm_eq_abs, abs_inv, this,
      abs_of_pos h‚ÇÅ, stereoInvFunAux_apply, inv_mul_cancel‚ÇÄ h‚ÇÅ.ne']
  suffices ‚Äñ(4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v‚Äñ ^ 2 = (‚Äñw‚Äñ ^ 2 + 4) ^ 2 by
    simpa only [sq_eq_sq_iff_abs_eq_abs, abs_norm, abs_of_pos h‚ÇÅ] using this
  rw [Submodule.mem_orthogonal_singleton_iff_inner_left] at hw
  simp [norm_add_sq_real, norm_smul, inner_smul_left, inner_smul_right, hw, mul_pow,
    Real.norm_eq_abs, hv]
  ring

theorem hasFDerivAt_stereoInvFunAux (v : E) :
    HasFDerivAt (stereoInvFunAux v) (ContinuousLinearMap.id ‚Ñù E) 0 := by
  have h‚ÇÄ : HasFDerivAt (fun w : E => ‚Äñw‚Äñ ^ 2) (0 : StrongDual ‚Ñù E) 0 := by
    convert (hasStrictFDerivAt_norm_sq (0 : E)).hasFDerivAt
    simp only [map_zero, smul_zero]
  have h‚ÇÅ : HasFDerivAt (fun w : E => (‚Äñw‚Äñ ^ 2 + 4)‚Åª¬π) (0 : StrongDual ‚Ñù E) 0 := by
    convert (hasFDerivAt_inv _).comp _ (h‚ÇÄ.add (hasFDerivAt_const 4 0)) <;> simp
  have h‚ÇÇ : HasFDerivAt (fun w => (4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v)
      ((4 : ‚Ñù) ‚Ä¢ ContinuousLinearMap.id ‚Ñù E) 0 := by
    convert ((hasFDerivAt_const (4 : ‚Ñù) 0).smul (hasFDerivAt_id 0)).add
      ((h‚ÇÄ.sub (hasFDerivAt_const (4 : ‚Ñù) 0)).smul (hasFDerivAt_const v 0)) using 1
    ext w
    simp
  convert h‚ÇÅ.smul h‚ÇÇ using 1
  ext w
  simp

theorem hasFDerivAt_stereoInvFunAux_comp_coe (v : E) :
    HasFDerivAt (stereoInvFunAux v ‚àò ((‚Üë) : (‚Ñù ‚àô v)·óÆ ‚Üí E)) (‚Ñù ‚àô v)·óÆ.subtypeL 0 := by
  have : HasFDerivAt (stereoInvFunAux v) (ContinuousLinearMap.id ‚Ñù E) ((‚Ñù ‚àô v)·óÆ.subtypeL 0) :=
    hasFDerivAt_stereoInvFunAux v
  refine this.comp (0 : (‚Ñù ‚àô v)·óÆ) (by apply ContinuousLinearMap.hasFDerivAt)

theorem contDiff_stereoInvFunAux {m : WithTop ‚Ñï‚àû} : ContDiff ‚Ñù m (stereoInvFunAux v) := by
  have h‚ÇÄ : ContDiff ‚Ñù œâ fun w : E => ‚Äñw‚Äñ ^ 2 := contDiff_norm_sq ‚Ñù
  have h‚ÇÅ : ContDiff ‚Ñù œâ fun w : E => (‚Äñw‚Äñ ^ 2 + 4)‚Åª¬π := by
    refine (h‚ÇÄ.add contDiff_const).inv ?_
    intro x
    nlinarith
  have h‚ÇÇ : ContDiff ‚Ñù œâ fun w => (4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v := by
    refine (contDiff_const.smul contDiff_id).add ?_
    exact (h‚ÇÄ.sub contDiff_const).smul contDiff_const
  exact (h‚ÇÅ.smul h‚ÇÇ).of_le le_top

/-- Stereographic projection, reverse direction.  This is a map from the orthogonal complement of a
unit vector `v` in an inner product space `E` to the unit sphere in `E`. -/
def stereoInvFun (hv : ‚Äñv‚Äñ = 1) (w : (‚Ñù ‚àô v)·óÆ) : sphere (0 : E) 1 :=
  ‚ü®stereoInvFunAux v (w : E), stereoInvFunAux_mem hv w.2‚ü©

@[simp]
theorem stereoInvFun_apply (hv : ‚Äñv‚Äñ = 1) (w : (‚Ñù ‚àô v)·óÆ) :
    (stereoInvFun hv w : E) = (‚Äñw‚Äñ ^ 2 + 4)‚Åª¬π ‚Ä¢ ((4 : ‚Ñù) ‚Ä¢ w + (‚Äñw‚Äñ ^ 2 - 4) ‚Ä¢ v) :=
  rfl

open scoped InnerProductSpace in
theorem stereoInvFun_ne_north_pole (hv : ‚Äñv‚Äñ = 1) (w : (‚Ñù ‚àô v)·óÆ) :
    stereoInvFun hv w ‚â† (‚ü®v, by simp [hv]‚ü© : sphere (0 : E) 1) := by
  refine Subtype.coe_ne_coe.1 ?_
  rw [‚Üê inner_lt_one_iff_real_of_norm_one _ hv]
  ¬∑ have hw : ‚ü™v, w‚ü´_‚Ñù = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp w.2
    have hw' : (‚Äñ(w : E)‚Äñ ^ 2 + 4)‚Åª¬π * (‚Äñ(w : E)‚Äñ ^ 2 - 4) < 1 := by
      rw [inv_mul_lt_iff‚ÇÄ']
      ¬∑ linarith
      positivity
    simpa [real_inner_comm, inner_add_right, inner_smul_right, real_inner_self_eq_norm_mul_norm, hw,
      hv] using hw'
  ¬∑ simpa using stereoInvFunAux_mem hv w.2

theorem continuous_stereoInvFun (hv : ‚Äñv‚Äñ = 1) : Continuous (stereoInvFun hv) :=
  continuous_induced_rng.2
    ((contDiff_stereoInvFunAux (m := 0)).continuous.comp continuous_subtype_val)

open scoped InnerProductSpace in
attribute [-simp] AddSubgroupClass.coe_norm Submodule.coe_norm in
theorem stereo_left_inv (hv : ‚Äñv‚Äñ = 1) {x : sphere (0 : E) 1} (hx : (x : E) ‚â† v) :
    stereoInvFun hv (stereoToFun v x) = x := by
  ext
  simp only [stereoToFun_apply, stereoInvFun_apply, smul_add]
  -- name two frequently-occurring quantities and write down their basic properties
  set a : ‚Ñù := innerSL _ v x
  set y := (‚Ñù ‚àô v)·óÆ.orthogonalProjection x
  have split : ‚Üëx = a ‚Ä¢ v + ‚Üëy := by
    rw [‚Üê ((‚Ñù ‚àô v).starProjection_add_starProjection_orthogonal x),
      Submodule.starProjection_unit_singleton ‚Ñù hv x]
    rfl
  have hvy : ‚ü™v, y‚ü´_‚Ñù = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp y.2
  have pythag : 1 = a ^ 2 + ‚Äñy‚Äñ ^ 2 := by
    have hvy' : ‚ü™a ‚Ä¢ v, y‚ü´_‚Ñù = 0 := by simp only [inner_smul_left, hvy, mul_zero]
    convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _ hvy' using 2
    ¬∑ simp [‚Üê split]
    ¬∑ simp [norm_smul, hv, ‚Üê sq, sq_abs]
    ¬∑ exact sq _
  -- a fact which will be helpful for clearing denominators in the main calculation
  have ha : 0 < 1 - a := by
    have : a < 1 := (inner_lt_one_iff_real_of_norm_one hv (by simp)).mpr hx.symm
    linarith
  rw [split, Submodule.coe_smul_of_tower]
  simp only [norm_smul, norm_div, RCLike.norm_ofNat, Real.norm_eq_abs, abs_of_nonneg ha.le]
  match_scalars
  ¬∑ field_simp
    linear_combination 4 * pythag
  ¬∑ field_simp
    linear_combination 4 * (a - 1) * pythag

theorem stereo_right_inv (hv : ‚Äñv‚Äñ = 1) (w : (‚Ñù ‚àô v)·óÆ) : stereoToFun v (stereoInvFun hv w) = w := by
  simp only [stereoToFun, stereoInvFun, stereoInvFunAux, smul_add, map_add, map_smul, innerSL_apply,
    Submodule.orthogonalProjection_mem_subspace_eq_self]
  have h‚ÇÅ : (‚Ñù ‚àô v)·óÆ.orthogonalProjection v = 0 :=
    Submodule.orthogonalProjection_orthogonalComplement_singleton_eq_zero v
  have h‚ÇÇ : ‚ü™v, w‚ü´ = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp w.2
  have h‚ÇÉ : ‚ü™v, v‚ü´ = 1 := by simp [real_inner_self_eq_norm_mul_norm, hv]
  rw [h‚ÇÅ, h‚ÇÇ, h‚ÇÉ]
  match_scalars
  simp [field]
  ring

/-- Stereographic projection from the unit sphere in `E`, centred at a unit vector `v` in `E`;
this is the version as a partial homeomorphism. -/
def stereographic (hv : ‚Äñv‚Äñ = 1) : PartialHomeomorph (sphere (0 : E) 1) (‚Ñù ‚àô v)·óÆ where
  toFun := stereoToFun v ‚àò (‚Üë)
  invFun := stereoInvFun hv
  source := {‚ü®v, by simp [hv]‚ü©}·∂ú
  target := Set.univ
  map_source' := by simp
  map_target' {w} _ := fun h => (stereoInvFun_ne_north_pole hv w) (Set.eq_of_mem_singleton h)
  left_inv' x hx := stereo_left_inv hv fun h => hx (by
    rw [‚Üê h] at hv
    apply Subtype.ext
    dsimp
    exact h)
  right_inv' w _ := stereo_right_inv hv w
  open_source := isOpen_compl_singleton
  open_target := isOpen_univ
  continuousOn_toFun :=
    continuousOn_stereoToFun.comp continuous_subtype_val.continuousOn fun w h => by
      dsimp
      exact
        h ‚àò Subtype.ext ‚àò Eq.symm ‚àò (inner_eq_one_iff_of_norm_one hv (by simp)).mp
  continuousOn_invFun := (continuous_stereoInvFun hv).continuousOn

theorem stereographic_apply (hv : ‚Äñv‚Äñ = 1) (x : sphere (0 : E) 1) :
    stereographic hv x = (2 / ((1 : ‚Ñù) - ‚ü™v, x‚ü´)) ‚Ä¢ (‚Ñù ‚àô v)·óÆ.orthogonalProjection x :=
  rfl

@[simp]
theorem stereographic_source (hv : ‚Äñv‚Äñ = 1) : (stereographic hv).source = {‚ü®v, by simp [hv]‚ü©}·∂ú :=
  rfl

@[simp]
theorem stereographic_target (hv : ‚Äñv‚Äñ = 1) : (stereographic hv).target = Set.univ :=
  rfl

@[simp]
theorem stereographic_apply_neg (v : sphere (0 : E) 1) :
    stereographic (norm_eq_of_mem_sphere v) (-v) = 0 := by
  simp [stereographic_apply, Submodule.orthogonalProjection_orthogonalComplement_singleton_eq_zero]

@[simp]
theorem stereographic_neg_apply (v : sphere (0 : E) 1) :
    stereographic (norm_eq_of_mem_sphere (-v)) v = 0 := by
  convert stereographic_apply_neg (-v)
  ext1
  simp

theorem surjective_stereographic (hv : ‚Äñv‚Äñ = 1) :
    Surjective (stereographic hv) :=
  (stereographic hv).surjective_of_target_eq_univ rfl

@[simp]
theorem range_stereographic_symm (hv : ‚Äñv‚Äñ = 1) (hv' : v ‚àà sphere 0 1 := by simpa) :
    Set.range (stereographic hv).symm = {‚ü®v, hv'‚ü©}·∂ú := by
  refine le_antisymm ?_ (stereographic hv).symm.target_subset_range
  rintro x ‚ü®y, rfl‚ü©
  suffices y ‚àà (stereographic hv).target from (fun _ ‚Ü¶ (stereographic hv).map_target) y this
  simp

lemma isOpenEmbedding_stereographic_symm (hv : ‚Äñv‚Äñ = 1) :
    Topology.IsOpenEmbedding (stereographic hv).symm :=
  (stereographic hv).symm.to_isOpenEmbedding (by simp)

end StereographicProjection

section ChartedSpace

/-!
### Charted space structure on the sphere

In this section we construct a charted space structure on the unit sphere in a finite-dimensional
real inner product space `E`; that is, we show that it is locally homeomorphic to the Euclidean
space of dimension one less than `E`.

The restriction to finite dimension is for convenience.  The most natural `ChartedSpace`
structure for the sphere uses the stereographic projection from the antipodes of a point as the
canonical chart at this point.  However, the codomain of the stereographic projection constructed
in the previous section is `(‚Ñù ‚àô v)·óÆ`, the orthogonal complement of the vector `v` in `E` which is
the "north pole" of the projection, so a priori these charts all have different codomains.

So it is necessary to prove that these codomains are all continuously linearly equivalent to a
fixed normed space.  This could be proved in general by a simple case of Gram-Schmidt
orthogonalization, but in the finite-dimensional case it follows more easily by dimension-counting.
-/

/-- Variant of the stereographic projection, for the sphere in an `n + 1`-dimensional inner product
space `E`.  This version has codomain the Euclidean space of dimension `n`, and is obtained by
composing the original stereographic projection (`stereographic`) with an arbitrary linear isometry
from `(‚Ñù ‚àô v)·óÆ` to the Euclidean space. -/
def stereographic' (n : ‚Ñï) [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1) :
    PartialHomeomorph (sphere (0 : E) 1) (EuclideanSpace ‚Ñù (Fin n)) :=
  stereographic (norm_eq_of_mem_sphere v) ‚â´‚Çï
    (OrthonormalBasis.fromOrthogonalSpanSingleton n
            (ne_zero_of_mem_unit_sphere v)).repr.toHomeomorph.toPartialHomeomorph

@[simp]
theorem stereographic'_source {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1) :
    (stereographic' n v).source = {v}·∂ú := by simp [stereographic']

@[simp]
theorem stereographic'_target {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1) :
    (stereographic' n v).target = Set.univ := by simp [stereographic']

/-- The unit sphere in an `n + 1`-dimensional inner product space `E` is a charted space
modelled on the Euclidean space of dimension `n`. -/
instance EuclideanSpace.instChartedSpaceSphere {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] :
    ChartedSpace (EuclideanSpace ‚Ñù (Fin n)) (sphere (0 : E) 1) where
  atlas := {f | ‚àÉ v : sphere (0 : E) 1, f = stereographic' n v}
  chartAt v := stereographic' n (-v)
  mem_chart_source v := by simpa using ne_neg_of_mem_unit_sphere ‚Ñù v
  chart_mem_atlas v := ‚ü®-v, rfl‚ü©

instance (n : ‚Ñï) :
    ChartedSpace (EuclideanSpace ‚Ñù (Fin n)) (sphere (0 : EuclideanSpace ‚Ñù (Fin (n + 1))) 1) :=
  have := Fact.mk (@finrank_euclideanSpace_fin ‚Ñù _ (n + 1))
  EuclideanSpace.instChartedSpaceSphere

end ChartedSpace

section ContMDiffManifold

open scoped InnerProductSpace

theorem sphere_ext_iff (u v : sphere (0 : E) 1) : u = v ‚Üî ‚ü™(u : E), v‚ü´_‚Ñù = 1 := by
  simp [Subtype.ext_iff, inner_eq_one_iff_of_norm_one]

theorem stereographic'_symm_apply {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1)
    (x : EuclideanSpace ‚Ñù (Fin n)) :
    ((stereographic' n v).symm x : E) =
      let U : (‚Ñù ‚àô (v : E))·óÆ ‚âÉ‚Çó·µ¢[‚Ñù] EuclideanSpace ‚Ñù (Fin n) :=
        (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere v)).repr
      (‚Äñ(U.symm x : E)‚Äñ ^ 2 + 4)‚Åª¬π ‚Ä¢ (4 : ‚Ñù) ‚Ä¢ (U.symm x : E) +
        (‚Äñ(U.symm x : E)‚Äñ ^ 2 + 4)‚Åª¬π ‚Ä¢ (‚Äñ(U.symm x : E)‚Äñ ^ 2 - 4) ‚Ä¢ v.val := by
  simp [stereographic, stereographic', ‚Üê Submodule.coe_norm]

/-! ### Analytic manifold structure on the sphere -/

/-- The unit sphere in an `n + 1`-dimensional inner product space `E` is an analytic manifold,
modelled on the Euclidean space of dimension `n`. -/
instance EuclideanSpace.instIsManifoldSphere
    {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] :
    IsManifold (ùì° n) œâ (sphere (0 : E) 1) :=
  isManifold_of_contDiffOn (ùì° n) œâ (sphere (0 : E) 1)
    (by
      rintro _ _ ‚ü®v, rfl‚ü© ‚ü®v', rfl‚ü©
      let U :=
        (-- Removed type ascription, and this helped for some reason with timeout issues?
            OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù)
            n (ne_zero_of_mem_unit_sphere v)).repr
      let U' :=
        (-- Removed type ascription, and this helped for some reason with timeout issues?
            OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù)
            n (ne_zero_of_mem_unit_sphere v')).repr
      have H‚ÇÅ := U'.contDiff.comp_contDiffOn (contDiffOn_stereoToFun (n := œâ))
      -- Porting note: need to help with implicit variables again
      have H‚ÇÇ := (contDiff_stereoInvFunAux (m := œâ) (v := v.val)|>.comp
        (‚Ñù ‚àô (v : E))·óÆ.subtypeL.contDiff).comp U.symm.contDiff
      convert H‚ÇÅ.comp_inter (H‚ÇÇ.contDiffOn : ContDiffOn ‚Ñù œâ _ Set.univ) using 1
      -- -- squeezed from `ext, simp [sphere_ext_iff, stereographic'_symm_apply, real_inner_comm]`
      simp only [PartialHomeomorph.trans_toPartialEquiv, PartialHomeomorph.symm_toPartialEquiv,
        PartialEquiv.trans_source, PartialEquiv.symm_source, stereographic'_target,
        stereographic'_source]
      simp only [modelWithCornersSelf_coe, modelWithCornersSelf_coe_symm,
        Set.range_id, Set.inter_univ, Set.univ_inter, Set.compl_singleton_eq, Set.preimage_setOf_eq]
      simp only [id, comp_apply, Submodule.subtypeL_apply, PartialHomeomorph.coe_coe_symm,
        innerSL_apply, Ne, sphere_ext_iff, real_inner_comm (v' : E)]
      rfl)

instance (n : ‚Ñï) : IsManifold (ùì° n) œâ (sphere (0 :  EuclideanSpace ‚Ñù (Fin (n + 1))) 1) :=
  haveI := Fact.mk (@finrank_euclideanSpace_fin ‚Ñù _ (n + 1))
  EuclideanSpace.instIsManifoldSphere

/-- The inclusion map (i.e., `coe`) from the sphere in `E` to `E` is analytic. -/
theorem contMDiff_coe_sphere {m : WithTop ‚Ñï‚àû} {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] :
    ContMDiff (ùì° n) ùìò(‚Ñù, E) m ((‚Üë) : sphere (0 : E) 1 ‚Üí E) := by
  rw [contMDiff_iff]
  constructor
  ¬∑ exact continuous_subtype_val
  ¬∑ intro v _
    let U : _ ‚âÉ‚Çó·µ¢[‚Ñù] _ :=
      (-- Again, partially removing type ascription...
          OrthonormalBasis.fromOrthogonalSpanSingleton
          n (ne_zero_of_mem_unit_sphere (-v))).repr
    exact
      ((contDiff_stereoInvFunAux.comp (‚Ñù ‚àô (-v : E))·óÆ.subtypeL.contDiff).comp
          U.symm.contDiff).contDiffOn

variable {m : WithTop ‚Ñï‚àû} {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
variable {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù F H}
variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I m M]

/-- If a `C^m` function `f : M ‚Üí E`, where `M` is some manifold, takes values in the
sphere, then it restricts to a `C^m` function from `M` to the sphere. -/
theorem ContMDiff.codRestrict_sphere {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] {f : M ‚Üí E}
    (hf : ContMDiff I ùìò(‚Ñù, E) m f) (hf' : ‚àÄ x, f x ‚àà sphere (0 : E) 1) :
    ContMDiff I (ùì° n) m (Set.codRestrict _ _ hf' : M ‚Üí sphere (0 : E) 1) := by
  rw [contMDiff_iff_target]
  refine ‚ü®continuous_induced_rng.2 hf.continuous, ?_‚ü©
  intro v
  let U : _ ‚âÉ‚Çó·µ¢[‚Ñù] _ :=
    (-- Again, partially removing type ascription... Weird that this helps!
        OrthonormalBasis.fromOrthogonalSpanSingleton
        n (ne_zero_of_mem_unit_sphere (-v))).repr
  have h : ContDiffOn ‚Ñù œâ _ Set.univ := U.contDiff.contDiffOn
  have H‚ÇÅ := (h.comp_inter contDiffOn_stereoToFun).contMDiffOn
  have H‚ÇÇ : ContMDiffOn _ _ _ _ Set.univ := hf.contMDiffOn
  convert (H‚ÇÅ.of_le le_top).comp' H‚ÇÇ using 1
  ext x
  have hfxv : f x = -‚Üëv ‚Üî ‚ü™f x, -‚Üëv‚ü´_‚Ñù = 1 := by
    have hfx : ‚Äñf x‚Äñ = 1 := by simpa using hf' x
    rw [inner_eq_one_iff_of_norm_one hfx]
    exact norm_eq_of_mem_sphere (-v)
  simp [chartAt, ChartedSpace.chartAt, Subtype.ext_iff, hfxv, real_inner_comm]

/-- The antipodal map is analytic. -/
theorem contMDiff_neg_sphere {m : WithTop ‚Ñï‚àû} {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] :
    ContMDiff (ùì° n) (ùì° n) m fun x : sphere (0 : E) 1 => -x := by
  -- this doesn't elaborate well in term mode
  apply ContMDiff.codRestrict_sphere
  apply contDiff_neg.contMDiff.comp _
  exact contMDiff_coe_sphere

private lemma stereographic'_neg {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1) :
    stereographic' n (-v) v = 0 := by
  dsimp [stereographic']
  simp only [EmbeddingLike.map_eq_zero_iff]
  apply stereographic_neg_apply

/-- Consider the differential of the inclusion of the sphere in `E` at the point `v` as a continuous
linear map from `TangentSpace (ùì° n) v` to `E`.  The range of this map is the orthogonal complement
of `v` in `E`.

Note that there is an abuse here of the defeq between `E` and the tangent space to `E` at `(v:E`).
In general this defeq is not canonical, but in this case (the tangent space of a vector space) it is
canonical. -/
theorem range_mfderiv_coe_sphere {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1) :
    LinearMap.range (mfderiv (ùì° n) ùìò(‚Ñù, E) ((‚Üë) : sphere (0 : E) 1 ‚Üí E) v :
    TangentSpace (ùì° n) v ‚ÜíL[‚Ñù] E) = (‚Ñù ‚àô (v : E))·óÆ := by
  rw [((contMDiff_coe_sphere v).mdifferentiableAt le_top).mfderiv]
  dsimp [chartAt]
  simp only [fderivWithin_univ, mfld_simps]
  let U := (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) n
    (ne_zero_of_mem_unit_sphere (-v))).repr
  -- Porting note: this `suffices` was a `change`
  suffices
      LinearMap.range (fderiv ‚Ñù ((stereoInvFunAux (-v : E) ‚àò (‚Üë)) ‚àò U.symm) 0) = (‚Ñù ‚àô (v : E))·óÆ by
    convert this using 3
    apply stereographic'_neg
  have :
    HasFDerivAt (stereoInvFunAux (-v : E) ‚àò (Subtype.val : (‚Ñù ‚àô (‚Üë(-v) : E))·óÆ ‚Üí E))
      (‚Ñù ‚àô (‚Üë(-v) : E))·óÆ.subtypeL (U.symm 0) := by
    convert hasFDerivAt_stereoInvFunAux_comp_coe (-v : E)
    simp
  convert congrArg LinearMap.range (this.comp 0 U.symm.toContinuousLinearEquiv.hasFDerivAt).fderiv
  symm
  convert
    (U.symm : EuclideanSpace ‚Ñù (Fin n) ‚âÉ‚Çó·µ¢[‚Ñù] (‚Ñù ‚àô (‚Üë(-v) : E))·óÆ).range_comp
      (‚Ñù ‚àô (‚Üë(-v) : E))·óÆ.subtype using 1
  simp only [Submodule.range_subtype, coe_neg_sphere]
  congr 1
  -- we must show `Submodule.span ‚Ñù {v} = Submodule.span ‚Ñù {-v}`
  apply Submodule.span_eq_span
  ¬∑ simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    rw [‚Üê Submodule.neg_mem_iff]
    exact Submodule.mem_span_singleton_self (-v : E)
  ¬∑ simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    rw [Submodule.neg_mem_iff]
    exact Submodule.mem_span_singleton_self (v : E)

/-- Consider the differential of the inclusion of the sphere in `E` at the point `v` as a continuous
linear map from `TangentSpace (ùì° n) v` to `E`.  This map is injective. -/
theorem mfderiv_coe_sphere_injective {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)] (v : sphere (0 : E) 1) :
    Injective (mfderiv (ùì° n) ùìò(‚Ñù, E) ((‚Üë) : sphere (0 : E) 1 ‚Üí E) v) := by
  rw [((contMDiff_coe_sphere v).mdifferentiableAt le_top).mfderiv]
  simp only [chartAt, fderivWithin_univ, mfld_simps]
  let U := (OrthonormalBasis.fromOrthogonalSpanSingleton
      (ùïú := ‚Ñù) n (ne_zero_of_mem_unit_sphere (-v))).repr
  suffices Injective (fderiv ‚Ñù ((stereoInvFunAux (-v : E) ‚àò (‚Üë)) ‚àò U.symm) 0) by
    convert this using 3
    apply stereographic'_neg
  have : HasFDerivAt (stereoInvFunAux (-v : E) ‚àò (Subtype.val : (‚Ñù ‚àô (‚Üë(-v) : E))·óÆ ‚Üí E))
      (‚Ñù ‚àô (‚Üë(-v) : E))·óÆ.subtypeL (U.symm 0) := by
    convert hasFDerivAt_stereoInvFunAux_comp_coe (-v : E)
    simp
  have := congr_arg DFunLike.coe <| (this.comp 0 U.symm.toContinuousLinearEquiv.hasFDerivAt).fderiv
  refine Eq.subst this.symm ?_
  rw [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe]
  simpa [- Subtype.val_injective] using Subtype.val_injective

end ContMDiffManifold

section Circle

open Complex

/-- Local instance to help the typeclass system to figure out `1 + 1 = 2`. -/
theorem finrank_real_complex_fact' : Fact (finrank ‚Ñù ‚ÑÇ = 1 + 1) :=
  finrank_real_complex_fact

attribute [local instance] finrank_real_complex_fact'

/-- The unit circle in `‚ÑÇ` is a charted space modelled on `EuclideanSpace ‚Ñù (Fin 1)`.  This
follows by definition from the corresponding result for `Metric.Sphere`. -/
instance : ChartedSpace (EuclideanSpace ‚Ñù (Fin 1)) Circle :=
  EuclideanSpace.instChartedSpaceSphere

instance : IsManifold (ùì° 1) œâ Circle :=
  EuclideanSpace.instIsManifoldSphere (E := ‚ÑÇ)

/-- The unit circle in `‚ÑÇ` is an analytic Lie group. -/
instance : LieGroup (ùì° 1) œâ Circle where
  contMDiff_mul := by
    apply ContMDiff.codRestrict_sphere
    let c : Circle ‚Üí ‚ÑÇ := (‚Üë)
    have h‚ÇÇ : ContMDiff (ùìò(‚Ñù, ‚ÑÇ).prod ùìò(‚Ñù, ‚ÑÇ)) ùìò(‚Ñù, ‚ÑÇ) œâ fun z : ‚ÑÇ √ó ‚ÑÇ => z.fst * z.snd := by
      rw [contMDiff_iff]
      exact ‚ü®continuous_mul, fun x y => contDiff_mul.contDiffOn‚ü©
    suffices h‚ÇÅ : ContMDiff _ _ _ (Prod.map c c) from
      h‚ÇÇ.comp h‚ÇÅ
    apply ContMDiff.prodMap <;> exact contMDiff_coe_sphere
  contMDiff_inv := by
    apply ContMDiff.codRestrict_sphere
    simp only [‚Üê Circle.coe_inv, Circle.coe_inv_eq_conj]
    exact Complex.conjCLE.contDiff.contMDiff.comp contMDiff_coe_sphere

/-- The map `fun t ‚Ü¶ exp (t * I)` from `‚Ñù` to the unit circle in `‚ÑÇ` is analytic. -/
theorem contMDiff_circleExp {m : WithTop ‚Ñï‚àû} : ContMDiff ùìò(‚Ñù, ‚Ñù) (ùì° 1) m Circle.exp :=
  (contDiff_exp.comp (contDiff_id.smul contDiff_const)).contMDiff.codRestrict_sphere _

end Circle
