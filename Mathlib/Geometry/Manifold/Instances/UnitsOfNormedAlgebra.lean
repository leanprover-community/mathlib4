/-
Copyright Â© 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Heather Macbeth, Winston Yin
-/
import Mathlib.Geometry.Manifold.Algebra.LieGroup
import Mathlib.Analysis.NormedSpace.Units

#align_import geometry.manifold.instances.units_of_normed_algebra from "leanprover-community/mathlib"@"ef901ea68d3bb1dd08f8bc3034ab6b32b2e6ecdf"

/-!
# Units of a normed algebra

We construct the Lie group structure on the group of units of a complete normed `ğ•œ`-algebra `R`. The
group of units `RË£` has a natural smooth manifold structure modelled on `R` given by its embedding
into `R`. Together with the smoothness of the multiplication and inverse of its elements, `RË£` forms
a Lie group.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ğ•œ`, the `ğ•œ`-linear endomorphisms of `V` are a normed `ğ•œ`-algebra (see
`ContinuousLinearMap.toNormedAlgebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ğ•œ`, `V`), as demonstrated by:
```
example {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] [CompleteSpace V] [Nontrivial V] :
  LieGroup ğ“˜(ğ•œ, V â†’L[ğ•œ] V) (V â†’L[ğ•œ] V)Ë£ := by infer_instance
```

The ingredients needed for the construction are
* smoothness of multiplication and inversion in the charts, i.e. as functions on the normed
  `ğ•œ`-space `R`:  see `contDiffAt_ring_inverse` for the inversion result, and
  `contDiff_mul` (needs to be generalized from field to algebra) for the multiplication
  result
* for an open embedding `f`, whose domain is equipped with the induced manifold structure
  `f.singleton_smoothManifoldWithCorners`, characterization of smoothness of functions to/from
  this manifold in terms of smoothness in the target space.  See the pair of lemmas
  `ContMDiff_coe_sphere` and `ContMDiff.codRestrict_sphere` for a model.
None of this should be particularly difficult.

-/


noncomputable section

open scoped Manifold

namespace Units

variable {R : Type*} [NormedRing R] [CompleteSpace R]

instance : ChartedSpace R RË£ :=
  openEmbedding_val.singletonChartedSpace

theorem chartAt_apply {a : RË£} {b : RË£} : chartAt R a b = b :=
  rfl
#align units.chart_at_apply Units.chartAt_apply

theorem chartAt_source {a : RË£} : (chartAt R a).source = Set.univ :=
  rfl
#align units.chart_at_source Units.chartAt_source

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedAlgebra ğ•œ R]

instance : SmoothManifoldWithCorners ğ“˜(ğ•œ, R) RË£ :=
  openEmbedding_val.singleton_smoothManifoldWithCorners ğ“˜(ğ•œ, R)

/-- The embedding of the units `RË£` of a complete normed ring `R` into `R` is a smooth manifold
map. -/
lemma contMDiff_val {m : WithTop â„•} : ContMDiff ğ“˜(ğ•œ, R) ğ“˜(ğ•œ, R) m (val : RË£ â†’ R) :=
contMDiff_openEmbedding ğ“˜(ğ•œ, R) Units.openEmbedding_val

/-- For any map `f` from a manifold `M` to the units `RË£` of a complete normed ring `R`, the
smoothness of `val âˆ˜ f`, where `val : RË£ â†’ R` is the embedding, implies the smoothness of `f`.

This can be used to show that ring multiplication `RË£ Ã— RË£ â†’ RË£` and inverse `RË£ â†’ RË£` are
smooth. -/
lemma ContMDiff.of_comp_units_val
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H}
    {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
    {n : WithTop â„•} {f : M â†’ RË£} (hf : ContMDiff I ğ“˜(ğ•œ, R) n ((val : RË£ â†’ R) âˆ˜ f)) :
  ContMDiff I ğ“˜(ğ•œ, R) n f :=
ContMDiff.of_comp_openEmbedding Units.openEmbedding_val hf

/-- The units of a complete normed ring form a Lie group. -/
instance : LieGroup ğ“˜(ğ•œ, R) RË£ where
smooth_mul := by
  apply ContMDiff.of_comp_units_val
  have : (val : RË£ â†’ R) âˆ˜ (fun x : RË£ Ã— RË£ => x.1 * x.2) =
    (fun x : R Ã— R => x.1 * x.2) âˆ˜ (fun x : RË£ Ã— RË£ => (x.1, x.2))
  Â· ext; simp
  rw [this]
  have : ContMDiff (ğ“˜(ğ•œ, R).prod ğ“˜(ğ•œ, R)) ğ“˜(ğ•œ, R Ã— R) âˆ
    (fun x : RË£ Ã— RË£ => ((x.1 : R), (x.2 : R))) :=
    (ContMDiff.comp contMDiff_val contMDiff_fst).prod_mk_space
      (ContMDiff.comp contMDiff_val contMDiff_snd)
  refine' ContMDiff.comp _ this
  rw [contMDiff_iff_contDiff]
  exact contDiff_mul
smooth_inv := by
  apply ContMDiff.of_comp_units_val
  have : (val : RË£ â†’ R) âˆ˜ (fun x : RË£ => xâ»Â¹) = Ring.inverse âˆ˜ val
  Â· ext; simp
  rw [this, ContMDiff]
  intro x
  refine' ContMDiffAt.comp x _ (contMDiff_val x)
  rw [contMDiffAt_iff_contDiffAt]
  exact contDiffAt_ring_inverse _ _

end Units
