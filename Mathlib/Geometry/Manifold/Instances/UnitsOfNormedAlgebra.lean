/-
Copyright (c) 2021 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri, Heather Macbeth, Winston Yin
-/
import Mathlib.Geometry.Manifold.Algebra.LieGroup

/-!
# Units of a normed algebra

We construct the Lie group structure on the group of units of a complete normed `ùïú`-algebra `R`. The
group of units `RÀ£` has a natural smooth manifold structure modelled on `R` given by its embedding
into `R`. Together with the smoothness of the multiplication and inverse of its elements, `RÀ£` forms
a Lie group.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ùïú`, the `ùïú`-linear endomorphisms of `V` are a normed `ùïú`-algebra (see
`ContinuousLinearMap.toNormedAlgebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ùïú`, `V`), as demonstrated by:
```
example {V : Type*} [NormedAddCommGroup V] [NormedSpace ùïú V] [CompleteSpace V] :
    LieGroup ùìò(ùïú, V ‚ÜíL[ùïú] V) (V ‚ÜíL[ùïú] V)À£ := inferInstance
```
-/


noncomputable section

open scoped Manifold
/- Next line is necessary while the manifold smoothness class is not extended to `œâ`.
Later, replace with `open scoped ContDiff`. -/
local notation "‚àû" => (‚ä§ : ‚Ñï‚àû)

namespace Units

variable {R : Type*} [NormedRing R] [CompleteSpace R]

instance : ChartedSpace R RÀ£ :=
  isOpenEmbedding_val.singletonChartedSpace

theorem chartAt_apply {a : RÀ£} {b : RÀ£} : chartAt R a b = b :=
  rfl

theorem chartAt_source {a : RÀ£} : (chartAt R a).source = Set.univ :=
  rfl

variable {ùïú : Type*} [NontriviallyNormedField ùïú] [NormedAlgebra ùïú R]

instance : SmoothManifoldWithCorners ùìò(ùïú, R) RÀ£ :=
  isOpenEmbedding_val.singleton_smoothManifoldWithCorners

/-- For a complete normed ring `R`, the embedding of the units `RÀ£` into `R` is a smooth map between
manifolds. -/
lemma contMDiff_val {m : ‚Ñï‚àû} : ContMDiff ùìò(ùïú, R) ùìò(ùïú, R) m (val : RÀ£ ‚Üí R) :=
  contMDiff_isOpenEmbedding Units.isOpenEmbedding_val

/-- The units of a complete normed ring form a Lie group. -/
instance : LieGroup ùìò(ùïú, R) RÀ£ where
  smooth_mul := by
    apply ContMDiff.of_comp_isOpenEmbedding Units.isOpenEmbedding_val
    have : (val : RÀ£ ‚Üí R) ‚àò (fun x : RÀ£ √ó RÀ£ => x.1 * x.2) =
      (fun x : R √ó R => x.1 * x.2) ‚àò (fun x : RÀ£ √ó RÀ£ => (x.1, x.2)) := by ext; simp
    rw [this]
    have : ContMDiff (ùìò(ùïú, R).prod ùìò(ùïú, R)) ùìò(ùïú, R √ó R) ‚àû
      (fun x : RÀ£ √ó RÀ£ => ((x.1 : R), (x.2 : R))) :=
      (contMDiff_val.comp contMDiff_fst).prod_mk_space (contMDiff_val.comp contMDiff_snd)
    refine ContMDiff.comp ?_ this
    rw [contMDiff_iff_contDiff]
    exact contDiff_mul
  smooth_inv := by
    apply ContMDiff.of_comp_isOpenEmbedding Units.isOpenEmbedding_val
    have : (val : RÀ£ ‚Üí R) ‚àò (fun x : RÀ£ => x‚Åª¬π) = Ring.inverse ‚àò val := by ext; simp
    rw [this, ContMDiff]
    refine fun x ‚Ü¶ ContMDiffAt.comp x ?_ (contMDiff_val x)
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_ring_inverse _ _

end Units
