/-
Copyright (c) 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Heather Macbeth, Winston Yin
-/
import Mathlib.Geometry.Manifold.Algebra.LieGroup

/-!
# Units of a normed algebra

We construct the Lie group structure on the group of units of a complete normed `ð•œ`-algebra `R`. The
group of units `RË£` has a natural `C^n` manifold structure modelled on `R` given by its embedding
into `R`. Together with the smoothness of the multiplication and inverse of its elements, `RË£` forms
a Lie group.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ð•œ`, the `ð•œ`-linear endomorphisms of `V` are a normed `ð•œ`-algebra (see
`ContinuousLinearMap.toNormedAlgebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ð•œ`, `V`), as demonstrated by:
```
example {V : Type*} [NormedAddCommGroup V] [NormedSpace ð•œ V] [CompleteSpace V] :
    LieGroup ð“˜(ð•œ, V â†’L[ð•œ] V) (V â†’L[ð•œ] V)Ë£ := inferInstance
```
-/


noncomputable section

open scoped Manifold ContDiff

namespace Units

variable {R : Type*} [NormedRing R] [CompleteSpace R] {n : WithTop â„•âˆž}

instance : ChartedSpace R RË£ :=
  isOpenEmbedding_val.singletonChartedSpace

theorem chartAt_apply {a : RË£} {b : RË£} : chartAt R a b = b :=
  rfl

theorem chartAt_source {a : RË£} : (chartAt R a).source = Set.univ :=
  rfl

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] [NormedAlgebra ð•œ R]

instance : IsManifold ð“˜(ð•œ, R) n RË£ :=
  isOpenEmbedding_val.isManifold_singleton

/-- For a complete normed ring `R`, the embedding of the units `RË£` into `R` is a `C^n` map between
manifolds. -/
lemma contMDiff_val : ContMDiff ð“˜(ð•œ, R) ð“˜(ð•œ, R) n (val : RË£ â†’ R) :=
  contMDiff_isOpenEmbedding Units.isOpenEmbedding_val

/-- The units of a complete normed ring form a Lie group. -/
instance : LieGroup ð“˜(ð•œ, R) n RË£ where
  contMDiff_mul := by
    apply ContMDiff.of_comp_isOpenEmbedding Units.isOpenEmbedding_val
    have : (val : RË£ â†’ R) âˆ˜ (fun x : RË£ Ã— RË£ => x.1 * x.2) =
      (fun x : R Ã— R => x.1 * x.2) âˆ˜ (fun x : RË£ Ã— RË£ => (x.1, x.2)) := by ext; simp
    rw [this]
    have : ContMDiff (ð“˜(ð•œ, R).prod ð“˜(ð•œ, R)) ð“˜(ð•œ, R Ã— R) n
      (fun x : RË£ Ã— RË£ => ((x.1 : R), (x.2 : R))) :=
      (contMDiff_val.comp contMDiff_fst).prodMk_space (contMDiff_val.comp contMDiff_snd)
    refine ContMDiff.comp ?_ this
    rw [contMDiff_iff_contDiff]
    exact contDiff_mul
  contMDiff_inv := by
    apply ContMDiff.of_comp_isOpenEmbedding Units.isOpenEmbedding_val
    have : (val : RË£ â†’ R) âˆ˜ (fun x : RË£ => xâ»Â¹) = Ring.inverse âˆ˜ val := by ext; simp
    rw [this, ContMDiff]
    refine fun x => ContMDiffAt.comp x ?_ (contMDiff_val x)
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_ringInverse _ _

end Units
