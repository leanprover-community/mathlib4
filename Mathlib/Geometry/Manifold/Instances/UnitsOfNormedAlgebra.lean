/-
Copyright (c) 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Heather Macbeth, Winston Yin
-/
import Mathlib.Geometry.Manifold.Algebra.LieGroup

#align_import geometry.manifold.instances.units_of_normed_algebra from "leanprover-community/mathlib"@"ef901ea68d3bb1dd08f8bc3034ab6b32b2e6ecdf"

/-!
# Units of a normed algebra

We construct the Lie group structure on the group of units of a complete normed `ð•œ`-algebra `R`. The
group of units `RË£` has a natural smooth manifold structure modelled on `R` given by its embedding
into `R`. Together with the smoothness of the multiplication and inverse of its elements, `RË£` forms
a Lie group.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ð•œ`, the `ð•œ`-linear endomorphisms of `V` are a normed `ð•œ`-algebra (see
`ContinuousLinearMap.toNormedAlgebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ð•œ`, `V`), as demonstrated by:
```
example {V : Type*} [NormedAddCommGroup V] [NormedSpace ð•œ V] [CompleteSpace V] [Nontrivial V] :
  LieGroup ð“˜(ð•œ, V â†’L[ð•œ] V) (V â†’L[ð•œ] V)Ë£ := by infer_instance
```
-/


noncomputable section

open scoped Manifold

namespace Units

variable {R : Type*} [NormedRing R] [CompleteSpace R]

instance : ChartedSpace R RË£ :=
  openEmbedding_val.singletonChartedSpace

theorem chartAt_apply {a : RË£} {b : RË£} : chartAt R a b = b :=
  rfl
#align units.chart_at_apply Units.chartAt_apply

theorem chartAt_source {a : RË£} : (chartAt R a).source = Set.univ :=
  rfl
#align units.chart_at_source Units.chartAt_source

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] [NormedAlgebra ð•œ R]

instance : SmoothManifoldWithCorners ð“˜(ð•œ, R) RË£ :=
  openEmbedding_val.singleton_smoothManifoldWithCorners ð“˜(ð•œ, R)

/-- For a complete normed ring `R`, the embedding of the units `RË£` into `R` is a smooth map between
manifolds. -/
lemma contMDiff_val {m : â„•âˆž} : ContMDiff ð“˜(ð•œ, R) ð“˜(ð•œ, R) m (val : RË£ â†’ R) :=
  contMDiff_openEmbedding ð“˜(ð•œ, R) Units.openEmbedding_val

/-- The units of a complete normed ring form a Lie group. -/
instance : LieGroup ð“˜(ð•œ, R) RË£ where
  smooth_mul := by
    apply ContMDiff.of_comp_openEmbedding Units.openEmbedding_val
    have : (val : RË£ â†’ R) âˆ˜ (fun x : RË£ Ã— RË£ => x.1 * x.2) =
      (fun x : R Ã— R => x.1 * x.2) âˆ˜ (fun x : RË£ Ã— RË£ => (x.1, x.2)) := by ext; simp
    rw [this]
    have : ContMDiff (ð“˜(ð•œ, R).prod ð“˜(ð•œ, R)) ð“˜(ð•œ, R Ã— R) âˆž
      (fun x : RË£ Ã— RË£ => ((x.1 : R), (x.2 : R))) :=
      (contMDiff_val.comp contMDiff_fst).prod_mk_space (contMDiff_val.comp contMDiff_snd)
    refine ContMDiff.comp ?_ this
    rw [contMDiff_iff_contDiff]
    exact contDiff_mul
  smooth_inv := by
    apply ContMDiff.of_comp_openEmbedding Units.openEmbedding_val
    have : (val : RË£ â†’ R) âˆ˜ (fun x : RË£ => xâ»Â¹) = Ring.inverse âˆ˜ val := by ext; simp
    rw [this, ContMDiff]
    refine fun x => ContMDiffAt.comp x ?_ (contMDiff_val x)
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_ring_inverse _ _

end Units
