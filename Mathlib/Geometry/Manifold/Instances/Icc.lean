/-
Copyright (c) 2025 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Geometry.Manifold.ContMDiff.Basic
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.MFDeriv.FDeriv

/-! # Manifold structure on real intervals

The manifold structure on real intervals is defined in `Mathlib.Geometry.Manifold.Instances.Real`.
We relate it to the manifold structure on the real line, by showing that the inclusion
(`contMDiff_subtype_coe_Icc`) and projection (`contMDiffOn_projIcc`) are smooth, and showing that
a function defined on the interval is smooth iff its composition with the projection is smooth on
the interval in `‚Ñù` (see `contMDiffOn_comp_projIcc_iff` and friends).

We also define `1 : TangentSpace (ùì°‚àÇ 1) z`, and relate it to `1` in the real line.

## TODO

This file can be thoroughly rewritten once mathlib has a good theory of smooth immersions and
embeddings. Once this is done,
- the inclusion `Icc x y ‚Üí ‚Ñù` is a smooth embedding, and in particular smooth
- deduce the dual result: a function `f : M ‚Üí Icc x y` is smooth iff
  its composition with the inclusion into `‚Ñù` is smooth
- prove the projection `‚Ñù ‚Üí Icc x y` is a smooth submersion, hence smooth
- use this to simplify the proof that `f : Icc x y ‚Üí M` is smooth iff the composition `‚Ñù ‚Üí M`
  with the projection `‚Ñù ‚Üí Icc x y` is
-/

open Set
open scoped Manifold Topology

noncomputable section

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù E H} {n : WithTop ‚Ñï‚àû}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

instance (x : ‚Ñù) : One (TangentSpace ùìò(‚Ñù) x) where
  one := (1 : ‚Ñù)

/-- Unit vector in the tangent space to a segment, as the image of the unit vector in the real line
under the canonical projection. It is also mapped to the unit vector in the real line through
the canonical injection, see `mfderiv_subtype_coe_Icc_one`.

Note that one cannot abuse defeqs for this definition: this is *not* the same as the vector
`fun _ ‚Ü¶ 1` in `EuclideanSpace ‚Ñù (Fin 1)` through defeqs, as one of the charts of `Icc x y` is
orientation-reversing. -/
irreducible_def oneTangentSpaceIcc {x y : ‚Ñù} [h : Fact (x < y)] (z : Icc x y) :
    TangentSpace (ùì°‚àÇ 1) z :=
  mfderivWithin ùìò(‚Ñù) (ùì°‚àÇ 1) (Set.projIcc x y h.out.le) (Icc x y) z 1

instance {x y : ‚Ñù} [h : Fact (x < y)] (z : Icc x y) : One (TangentSpace (ùì°‚àÇ 1) z) where
  one := oneTangentSpaceIcc z

variable {x y : ‚Ñù} [h : Fact (x < y)] {n : WithTop ‚Ñï‚àû}

/-- The inclusion map from of a closed segment to `‚Ñù` is smooth in the manifold sense. -/
lemma contMDiff_subtype_coe_Icc :
    ContMDiff (ùì°‚àÇ 1) ùìò(‚Ñù) n (fun (z : Icc x y) ‚Ü¶ (z : ‚Ñù)) := by
  intro z
  rw [contMDiffAt_iff]
  refine ‚ü®by fun_prop, ?_‚ü©
  -- We come back to the definition: we should check that, in each chart, the map is smooth.
  -- There are two charts, and we check things separately in each of them using the
  -- explicit formulas.
  suffices ContDiffWithinAt ‚Ñù n _ (range ‚Üë(ùì°‚àÇ 1)) _ by simpa
  split_ifs with hz
  ¬∑ simp? [IccLeftChart, Function.comp_def, modelWithCornersEuclideanHalfSpace] says
      simp only [IccLeftChart, Fin.isValue, OpenPartialHomeomorph.mk_coe_symm,
        PartialEquiv.coe_symm_mk, modelWithCornersEuclideanHalfSpace, ModelWithCorners.mk_symm,
        Function.comp_def, Function.update_self, ModelWithCorners.mk_coe,
        OpenPartialHomeomorph.mk_coe]
    rw [Subtype.range_val_subtype]
    have : ContDiff ‚Ñù n (fun (z : EuclideanSpace ‚Ñù (Fin 1)) ‚Ü¶ z 0 + x) := by fun_prop
    apply this.contDiffWithinAt.congr_of_eventuallyEq_of_mem; swap
    ¬∑ simpa using z.2.1
    have : {w : EuclideanSpace ‚Ñù (Fin 1) | w 0 < y - x} ‚àà ùìù (fun i ‚Ü¶ z - x) := by
      apply (isOpen_lt (continuous_apply 0) continuous_const).mem_nhds
      simpa using hz
    filter_upwards [self_mem_nhdsWithin, nhdsWithin_le_nhds this] with w hw h'w
    rw [max_eq_left hw, min_eq_left]
    linarith
  ¬∑ simp only [not_lt] at hz
    simp? [IccRightChart, Function.comp_def, modelWithCornersEuclideanHalfSpace] says
      simp only [IccRightChart, Fin.isValue, OpenPartialHomeomorph.mk_coe_symm,
        PartialEquiv.coe_symm_mk, modelWithCornersEuclideanHalfSpace, ModelWithCorners.mk_symm,
        Function.comp_def, Function.update_self, ModelWithCorners.mk_coe,
        OpenPartialHomeomorph.mk_coe]
    rw [Subtype.range_val_subtype]
    have : ContDiff ‚Ñù n (fun (z : EuclideanSpace ‚Ñù (Fin 1)) ‚Ü¶ y - z 0) := by fun_prop
    apply this.contDiffWithinAt.congr_of_eventuallyEq_of_mem; swap
    ¬∑ simpa using z.2.2
    have : {w : EuclideanSpace ‚Ñù (Fin 1) | w 0 < y - x} ‚àà ùìù (fun i ‚Ü¶ y - z) := by
      apply (isOpen_lt (continuous_apply 0) continuous_const).mem_nhds
      simpa using h.out.trans_le hz
    filter_upwards [self_mem_nhdsWithin, nhdsWithin_le_nhds this] with w hw h'w
    rw [max_eq_left hw, max_eq_left]
    linarith

/-- The projection from `‚Ñù` to a closed segment is smooth on the segment, in the manifold sense. -/
lemma contMDiffOn_projIcc :
    ContMDiffOn ùìò(‚Ñù) (ùì°‚àÇ 1) n (Set.projIcc x y h.out.le) (Icc x y) := by
  intro z hz
  rw [contMDiffWithinAt_iff]
  refine ‚ü®by apply ContinuousAt.continuousWithinAt; fun_prop, ?_‚ü©
  -- We come back to the definition: we should check that, in each chart, the map is smooth
  -- There are two charts, and we check things separately in each of them using the
  -- explicit formulas.
  suffices ContDiffWithinAt ‚Ñù n _ (Icc x y) z by simpa
  split_ifs with h'z
  ¬∑ have : ContDiff ‚Ñù n (fun (w : ‚Ñù) ‚Ü¶
        (show EuclideanSpace ‚Ñù (Fin 1) from fun (_ : Fin 1) ‚Ü¶ w - x)) := by
      dsimp
      apply contDiff_euclidean.2 (fun i ‚Ü¶ by fun_prop)
    apply this.contDiffWithinAt.congr_of_eventuallyEq_of_mem _ hz
    filter_upwards [self_mem_nhdsWithin] with w hw
    ext i
    suffices max x (min y w) - x = w - x by
      simpa [modelWithCornersEuclideanHalfSpace, IccLeftChart]
    rw [max_eq_right, min_eq_right hw.2]
    simp [hw.1, h.out.le]
  ¬∑ have : ContDiff ‚Ñù n (fun (w : ‚Ñù) ‚Ü¶
        (show EuclideanSpace ‚Ñù (Fin 1) from fun (_ : Fin 1) ‚Ü¶ y - w)) := by
      dsimp
      apply contDiff_euclidean.2 (fun i ‚Ü¶ by fun_prop)
    apply this.contDiffWithinAt.congr_of_eventuallyEq_of_mem _ hz
    filter_upwards [self_mem_nhdsWithin] with w hw
    ext i
    suffices y - max x (min y w) = y - w by
      simpa [modelWithCornersEuclideanHalfSpace, IccRightChart]
    rw [max_eq_right, min_eq_right hw.2]
    simp [hw.1, h.out.le]

lemma contMDiffOn_comp_projIcc_iff {f : Icc x y ‚Üí M} :
    ContMDiffOn ùìò(‚Ñù) I n (f ‚àò (Set.projIcc x y h.out.le)) (Icc x y) ‚Üî ContMDiff (ùì°‚àÇ 1) I n f := by
  refine ‚ü®fun hf ‚Ü¶ ?_, fun hf ‚Ü¶ hf.comp_contMDiffOn contMDiffOn_projIcc‚ü©
  convert hf.comp_contMDiff (contMDiff_subtype_coe_Icc (x := x) (y := y)) (fun z ‚Ü¶ z.2)
  ext z
  simp

lemma contMDiffWithinAt_comp_projIcc_iff {f : Icc x y ‚Üí M} {w : Icc x y} :
    ContMDiffWithinAt ùìò(‚Ñù) I n (f ‚àò (Set.projIcc x y h.out.le)) (Icc x y) w ‚Üî
      ContMDiffAt (ùì°‚àÇ 1) I n f w := by
  refine ‚ü®fun hf ‚Ü¶ ?_,
    fun hf ‚Ü¶ hf.comp_contMDiffWithinAt_of_eq (contMDiffOn_projIcc w w.2) (by simp)‚ü©
  have A := contMDiff_subtype_coe_Icc (x := x) (y := y) (n := n) w
  rw [‚Üê contMDiffWithinAt_univ] at A ‚ä¢
  convert hf.comp _ A (fun z hz ‚Ü¶ z.2)
  ext z
  simp

lemma mdifferentiableWithinAt_comp_projIcc_iff {f : Icc x y ‚Üí M} {w : Icc x y} :
    MDifferentiableWithinAt ùìò(‚Ñù) I (f ‚àò (Set.projIcc x y h.out.le)) (Icc x y) w ‚Üî
      MDifferentiableAt (ùì°‚àÇ 1) I f w := by
  refine ‚ü®fun hf ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ have A := (contMDiff_subtype_coe_Icc (x := x) (y := y) (n := 1) w).mdifferentiableAt le_rfl
    rw [‚Üê mdifferentiableWithinAt_univ] at A ‚ä¢
    convert hf.comp _ A (fun z hz ‚Ü¶ z.2)
    ext z
    simp
  ¬∑ have := (contMDiffOn_projIcc (x := x) (y := y) (n := 1) w w.2).mdifferentiableWithinAt le_rfl
    exact MDifferentiableAt.comp_mdifferentiableWithinAt_of_eq (w : ‚Ñù) hf this (by simp)

lemma mfderivWithin_projIcc_one {z : ‚Ñù} (hz : z ‚àà Icc x y) :
    mfderivWithin ùìò(‚Ñù) (ùì°‚àÇ 1) (Set.projIcc x y h.out.le) (Icc x y) z 1 = 1 := by
  change _ = oneTangentSpaceIcc (Set.projIcc x y h.out.le z)
  simp only [oneTangentSpaceIcc]
  congr
  simp [projIcc_of_mem h.out.le hz]

lemma mfderivWithin_comp_projIcc_one {f : Icc x y ‚Üí M} {w : Icc x y} :
    mfderivWithin ùìò(‚Ñù) I (f ‚àò (projIcc x y h.out.le)) (Icc x y) w 1 = mfderiv (ùì°‚àÇ 1) I f w 1 := by
  by_cases hw : MDifferentiableAt (ùì°‚àÇ 1) I f w; swap
  ¬∑ rw [mfderiv_zero_of_not_mdifferentiableAt hw, mfderivWithin_zero_of_not_mdifferentiableWithinAt]
    ¬∑ rfl
    ¬∑ rwa [mdifferentiableWithinAt_comp_projIcc_iff]
  rw [mfderiv_comp_mfderivWithin (I' := ùì°‚àÇ 1)]; rotate_left
  ¬∑ simp [hw]
  ¬∑ exact (contMDiffOn_projIcc _ w.2).mdifferentiableWithinAt le_rfl
  ¬∑ exact (uniqueDiffOn_Icc h.out _ w.2).uniqueMDiffWithinAt
  simp only [Function.comp_apply, ContinuousLinearMap.coe_comp']
  have : w = projIcc x y h.out.le (w : ‚Ñù) := by rw [projIcc_of_mem]
  rw [projIcc_of_mem _ w.2]
  congr 1
  convert mfderivWithin_projIcc_one w.2

lemma mfderiv_subtype_coe_Icc_one (z : Icc x y) :
    mfderiv (ùì°‚àÇ 1) ùìò(‚Ñù) (Subtype.val : Icc x y ‚Üí ‚Ñù) z 1 = 1 := by
  have A : mfderivWithin ùìò(‚Ñù) ùìò(‚Ñù) (Subtype.val ‚àò (projIcc x y h.out.le)) (Icc x y) z 1
      = mfderivWithin ùìò(‚Ñù) ùìò(‚Ñù) id (Icc x y) z 1 := by
    congr 1
    apply mfderivWithin_congr_of_mem _ z.2
    intro z hz
    simp [projIcc_of_mem h.out.le hz]
  rw [‚Üê mfderivWithin_comp_projIcc_one, A]
  simp only [id_eq, mfderivWithin_eq_fderivWithin]
  rw [fderivWithin_id (uniqueDiffOn_Icc h.out _ z.2)]
  rfl
