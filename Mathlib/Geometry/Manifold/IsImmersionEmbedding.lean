/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.LocalSourceTargetProperty

/-! # Smooth immersions and embeddings

In this file, we define `C^k` immersions and embeddings between `C^k` manifolds.
The correct definition in the infinite-dimensional setting differs from the standard
finite-dimensional definition (concerning the `mfderiv` being injective): future pull requests will
prove that our definition implies the latter, and that both are equivalent for finite-dimensional
manifolds.

This definition can be conveniently formulated in terms of local properties: `f` is an immersion at
`x` iff there exist suitable charts near `x` and `f x` such that `f` has a nice form w.r.t. these
charts. Most results below can be deduced from more abstract results about such local properties.
This shortens the overall argument, as the definition of submersions has the same general form.

## Main definitions
* `IsImmersionAt F I I' n f x` means a map `f : M â†’ M'` between `C^n` manifolds `M` and `M'`
  is an immersion at `x : M`: there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and `f x`,
  respectively, such that in these charts, `f` looks like `u â†¦ (u, 0)`, w.r.t. some equivalence
  `E' â‰ƒL[ğ•œ] E Ã— F`. We do not demand that `f` be differentiable (this follows from this definition).
* `IsImmersion F I I' n f` means `f: M â†’ M'` is an immersion at every point `x : M`.

## Main results
* `IsImmersionAt.congr_of_eventuallyEq`: being an immersion is a local property.
  If `f` and `g` agree near `x` and `f` is an immersion at `x`, so is `g`

## TODO
* `IsImmersionAt.contMDiffAt`: if f is an immersion at `x`, it is `C^n` at `x`.
* `IsImmersion.contMDiff`: if f is an immersion, it is `C^n`.
* `IsImmersionAt.prodMap`: the product of two immersions is an immersion
* If `f` is an immersion at `x`, its differential splits, hence is injective.
* If `f: M â†’ M'` is a map between Banach manifolds, `mfderiv I I' f x` splitting implies `f` is an
  immersion at `x`. (This requires the inverse function theorem.)
* `IsImmersionAt.comp`: if `f: M â†’ M'` and `g: M' â†’ N` are maps between Banach manifolds such that
  `f` is an immersion at `x : M` and `g` is an immersion at `f x`, then `g âˆ˜ f` is an immersion
  at `x`.
* `IsImmersion.comp`: the composition of immersions (between Banach manifolds) is an immersion
* If `f: M â†’ M'` is a map between finite-dimensional manifolds, `mfderiv I I' f x` being injective
  implies `f` is an immersion at `x`.
* define smooth embeddings, and deduce analogous results for these

## References

* [Juan Margalef-Roig and Enrique Outerelo Dominguez, *Differential topology*][roigdomingues2012]

-/

open scoped Manifold Topology ContDiff

open Function Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {F F' : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup F'] [NormedSpace ğ•œ F']
  {H : Type*} [TopologicalSpace H] {H' : Type*} [TopologicalSpace H']
  {G : Type*} [TopologicalSpace G] {G' : Type*} [TopologicalSpace G']
  {I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'}
  {J : ModelWithCorners ğ•œ F G} {J' : ModelWithCorners ğ•œ F G'}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  {n : WithTop â„•âˆ}

-- XXX: should the next three definitions be a class instead?
-- Are these slice charts canonical enough that we want the typeclass system to kick in?

variable (F I I' M M') in
/-- The local property of being an immersion at `x` -/
def ImmersionAtProp :
    ((M â†’ M') â†’ PartialHomeomorph M H â†’ PartialHomeomorph M' H' â†’ Prop) :=
  fun f domChart codChart â†¦ âˆƒ equiv : (E Ã— F) â‰ƒL[ğ•œ] E',
    EqOn ((codChart.extend I') âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target

omit [ChartedSpace H M] [ChartedSpace H' M'] in
/-- Being an immersion at `x` is a "nice" local property. -/
lemma ImmersionAtPropIsNice : IsLocalSourceTargetProperty (ImmersionAtProp F I I' M M') where
  mono_source {f Ï† Ïˆ s} hs hf := by
    have {a b c : Set E} : a âˆ© (b âˆ© c) âŠ† b := by intro; aesop
    obtain âŸ¨equiv, hfâŸ© := hf
    use equiv
    exact hf.mono (by simpa using this)
  congr {f g Ï† Ïˆ s} hfg hs hÏ† hf := by
    obtain âŸ¨equiv, hfâŸ© := hf
    use equiv
    apply EqOn.trans ?_ (hf.mono (by simp))
    intro x hx
    set Î¦ := Ï†.extend I
    have aux : Î¦.source âŠ† s := by simpa [Î¦]
    have : (f âˆ˜ Î¦.symm) x = (g âˆ˜ Î¦.symm) x := hfg <| aux (PartialEquiv.map_target _ hx)
    rw [Function.comp_apply, â† this]
    simp [Î¦]

variable (F I I' n) in
/-- `f : M â†’ N` is a `C^k` immersion at `x` if there are charts `Ï†` and `Ïˆ` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u â†¦ (u, 0)`.
Additionally, we demand that `f` map `Ï†.source` into `Ïˆ.source`.

NB. We don't know the particular atlasses used for `M` and `N`, so asking for `Ï†` and `Ïˆ` to be
in the `atlas` would be too optimistic: lying in the `maximalAtlas` is sufficient.
-/
def IsImmersionAt (f : M â†’ M') (x : M) : Prop :=
  LiftSourceTargetPropertyAt I I' n f x (ImmersionAtProp F I I' M M')

namespace IsImmersionAt

variable {f g : M â†’ M'} {x : M}

lemma mk_of_charts (equiv : (E Ã— F) â‰ƒL[ğ•œ] E') (domChart : PartialHomeomorph M H)
    (codChart : PartialHomeomorph M' H')
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas I' n M')
    (hsource : f '' domChart.source âŠ† codChart.source)
    (hwrittenInExtend : EqOn ((codChart.extend I') âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target) : IsImmersionAt F I I' n f x := by
  use domChart, codChart
  exact âŸ¨hx, hfx, hdomChart, hcodChart, hsource, equiv, hwrittenInExtendâŸ©

/-- `f : M â†’ N` is a `C^k` immersion at `x` if there are charts `Ï†` and `Ïˆ` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u â†¦ (u, 0)`.
This version does not assume that `f` maps `Ï†.source` to `Ïˆ.source`,
but that `f` is continuous at `x`. -/
lemma mk_of_continuousAt {f : M â†’ M'} {x : M} (hf : ContinuousAt f x) (equiv : (E Ã— F) â‰ƒL[ğ•œ] E')
    (domChart : PartialHomeomorph M H) (codChart : PartialHomeomorph M' H')
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas I' n M')
    (hwrittenInExtend : EqOn ((codChart.extend I') âˆ˜ f âˆ˜ (domChart.extend I).symm) (equiv âˆ˜ (Â·, 0))
      (domChart.extend I).target) : IsImmersionAt F I I' n f x :=
  LiftSourceTargetPropertyAt.mk_of_continuousAt hf ImmersionAtPropIsNice _ _ hx hfx hdomChart
    hcodChart âŸ¨equiv, hwrittenInExtendâŸ©

/-- A choice of chart on the domain `M` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.codChart` and `h.equiv`,
`f` will look like an inclusion `u â†¦ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.codChart` and `h.codChart`. -/
noncomputable def domChart (h : IsImmersionAt F I I' n f x) : PartialHomeomorph M H :=
  Classical.choose h

/-- A choice of chart on the co-domain `N` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.domChart` and `h.equiv`,
`f` will look like an inclusion `u â†¦ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.equiv` and `h.domChart`. -/
noncomputable def codChart (h : IsImmersionAt F I I' n f x) : PartialHomeomorph M' H' :=
  Classical.choose (Classical.choose_spec h)

lemma mem_domChart_source (h : IsImmersionAt F I I' n f x) : x âˆˆ h.domChart.source :=
  (Classical.choose_spec (Classical.choose_spec h)).1

lemma mem_codChart_source (h : IsImmersionAt F I I' n f x) : f x âˆˆ h.codChart.source :=
  (Classical.choose_spec (Classical.choose_spec h)).2.1

lemma domChart_mem_maximalAtlas (h : IsImmersionAt F I I' n f x) :
    h.domChart âˆˆ IsManifold.maximalAtlas I n M :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.1

lemma codChart_mem_maximalAtlas (h : IsImmersionAt F I I' n f x) :
    h.codChart âˆˆ IsManifold.maximalAtlas I' n M' :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.2.1

lemma map_source_subset_source (h : IsImmersionAt F I I' n f x) :
    f '' h.domChart.source âŠ† h.codChart.source :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.2.2.1

/-- A linear equivalence `E Ã— F â‰ƒL[ğ•œ] E'` which belongs to the data of an immersion `f` at `x`:
the particular equivalence is arbitrary, but this choice matches the witnesses given by
`h.domChart` and `h.codChart`. -/
noncomputable def equiv (h : IsImmersionAt F I I' n f x) : (E Ã— F) â‰ƒL[ğ•œ] E' :=
  Classical.choose (Classical.choose_spec (Classical.choose_spec h)).2.2.2.2.2

lemma writtenInCharts (h : IsImmersionAt F I I' n f x) :
    EqOn ((h.codChart.extend I') âˆ˜ f âˆ˜ (h.domChart.extend I).symm) (h.equiv âˆ˜ (Â·, 0))
      (h.domChart.extend I).target :=
  Classical.choose_spec (Classical.choose_spec (Classical.choose_spec h)).2.2.2.2.2

lemma property (h : IsImmersionAt F I I' n f x) :
    LiftSourceTargetPropertyAt I I' n f x (ImmersionAtProp F I I' M M') :=
  âŸ¨h.domChart, h.codChart, h.mem_domChart_source, h.mem_codChart_source,
    h.domChart_mem_maximalAtlas, h.codChart_mem_maximalAtlas, h.map_source_subset_source,
    (Classical.choose_spec (Classical.choose_spec h)).2.2.2.2.2âŸ©

/-- Roig and Domingues [roigdomingues1992] only require this condition on the local charts:
in our setting, this is *slightly* weaker than `map_source_subset_source`: the latter implies
that `h.codChart.extend I' âˆ˜ f` maps `h.domChart.source` to
`(h.codChart.extend I').target = (h.codChart.extend I) '' h.codChart.source`,
but that does *not* imply `f` maps `h.domChart.source` to `h.codChartSource`;
a priori `f` could map some point `f âˆ˜ h.domChart.extend I x âˆ‰ h.codChart.source` into the target.
Note that this difference only occurs because of our design using junk values;
this is not a mathematically meaningful difference.`

At the same time, this condition is fairly weak: it is implied, for instance, by `f` being
continuous at `x` (see `mk_of_continuousAt`), which is easy to acertain in practice.
-/
-- TODO: can this proof be golfed further?
lemma map_target_subset_target (h : IsImmersionAt F I I' n f x) :
    (h.equiv âˆ˜ (Â·, 0)) '' (h.domChart.extend I).target âŠ† (h.codChart.extend I').target := by
  have : (h.domChart.extend I).target = (h.domChart.extend I) '' (h.domChart.extend I).source := by
    rw [PartialEquiv.image_source_eq_target]
  rw [this, PartialHomeomorph.extend_source]
  set Î¨ := h.codChart.extend I'
  set Î¦ := h.domChart.extend I
  suffices (Î¨ âˆ˜ f âˆ˜ Î¦.symm) '' (Î¦ '' h.domChart.source) âŠ† Î¨.target by
    have aux : h.domChart.source = Î¦.source := h.domChart.extend_source.symm
    rw [aux, PartialEquiv.image_source_eq_target] at this âŠ¢
    rwa [h.writtenInCharts.image_eq] at this
  calc
   _ = (Î¨ âˆ˜ f âˆ˜ â†‘Î¦.symm âˆ˜ Î¦) '' h.domChart.source := by simp [â† image_comp]
   _ = (Î¨ âˆ˜ f) '' ((Î¦.symm âˆ˜ Î¦) '' h.domChart.source) := by simp [â† image_comp]
   _ = (Î¨ âˆ˜ f) '' h.domChart.source := by rw [h.domChart.extend_left_inv' fun â¦ƒaâ¦„ a â†¦ a]
   _ = Î¨ '' (f '' h.domChart.source) := by rw [image_comp]
   _ âŠ† Î¨ '' h.codChart.source := by gcongr; exact h.map_source_subset_source
   _ = Î¨ '' Î¨.source := by rw [PartialHomeomorph.extend_source]
   _ âŠ† _ := Î¨.map_source''

/-- If `f` is an immersion at `x` and `g = f` on some neighbourhood of `x`,
then `g` is an immersion at `x`. -/
lemma congr_of_eventuallyEq {x : M} (h : IsImmersionAt F I I' n f x) (h' : f =á¶ [nhds x] g) :
    IsImmersionAt F I I' n g x :=
  LiftSourceTargetPropertyAt.congr_of_eventuallyEq ImmersionAtPropIsNice h.property h'

end IsImmersionAt

variable (F I I' n) in
/-- `f : M â†’ N` is a `C^k` immersion if around each point `x âˆˆ M`,
there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and `f x`, respectively
such that in these charts, `f` looks like `u â†¦ (u, 0)`.

In other words, `f` is an immersion at each `x âˆˆ M`.
-/
def IsImmersion (f : M â†’ M') : Prop := âˆ€ x, IsImmersionAt F I I' n f x

namespace IsImmersion

variable {f g : M â†’ M'}

/-- If `f` is an immersion, it is an immersion at each point. -/
lemma isImmersionAt (h : IsImmersion F I I' n f) (x : M) : IsImmersionAt F I I' n f x := h x

/-- If `f = g` and `f` is an immersion, so is `g`. -/
theorem congr (h : IsImmersion F I I' n f) (heq : f = g) : IsImmersion F I I' n g :=
  heq â–¸ h

end IsImmersion
