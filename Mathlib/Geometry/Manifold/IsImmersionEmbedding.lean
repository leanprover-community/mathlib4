/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.IsManifold.ExtChartAt
import Mathlib.Geometry.Manifold.LocalSourceTargetProperty

/-! # Smooth immersions and embeddings

In this file, we define `C^n` immersions and embeddings between `C^n` manifolds.
The correct definition in the infinite-dimensional setting differs from the standard
finite-dimensional definition (concerning the `mfderiv` being injective): future pull requests will
prove that our definition implies the latter, and that both are equivalent for finite-dimensional
manifolds.

This definition can be conveniently formulated in terms of local properties: `f` is an immersion at
`x` iff there exist suitable charts near `x` and `f x` such that `f` has a nice form w.r.t. these
charts. Most results below can be deduced from more abstract results about such local properties.
This shortens the overall argument, as the definition of submersions has the same general form.

## Main definitions
* `IsImmersionAt F I J n f x` means a map `f : M ‚Üí N` between `C^n` manifolds `M` and `N`
  is an immersion at `x : M`: there are charts `œÜ` and `œà` of `M` and `N` around `x` and `f x`,
  respectively, such that in these charts, `f` looks like `u ‚Ü¶ (u, 0)`, w.r.t. some equivalence
  `E' ‚âÉL[ùïú] E √ó F`. We do not demand that `f` be differentiable (this follows from this definition).
* `IsImmersion F I J n f` means `f : M ‚Üí N` is an immersion at every point `x : M`.

## Main results
* `IsImmersionAt.congr_of_eventuallyEq`: being an immersion is a local property.
  If `f` and `g` agree near `x` and `f` is an immersion at `x`, so is `g`

## TODO
* Show `IsImmersion(At)` is stable under replacing `F` by an isomorphic copy.
* The set where `LiftSourceTargetPropertyAt` holds is open.
* `IsImmersionAt.contMDiffAt`: if f is an immersion at `x`, it is `C^n` at `x`.
* `IsImmersion.contMDiff`: if f is an immersion, it is `C^n`.
* `IsImmersionAt.prodMap`: the product of two immersions is an immersion
* If `f` is an immersion at `x`, its differential splits, hence is injective.
* If `f : M ‚Üí N` is a map between Banach manifolds, `mfderiv I J f x` splitting implies `f` is an
  immersion at `x`. (This requires the inverse function theorem.)
* `IsImmersionAt.comp`: if `f : M ‚Üí N` and `g: N ‚Üí N'` are maps between Banach manifolds such that
  `f` is an immersion at `x : M` and `g` is an immersion at `f x`, then `g ‚àò f` is an immersion
  at `x`.
* `IsImmersion.comp`: the composition of immersions (between Banach manifolds) is an immersion
* If `f : M ‚Üí N` is a map between finite-dimensional manifolds, `mfderiv I J f x` being injective
  implies `f` is an immersion at `x`.
* define smooth embeddings, and deduce analogous results for these

## References

* [Juan Margalef-Roig and Enrique Outerelo Dominguez, *Differential topology*][roigdomingues2012]

-/

open scoped Topology ContDiff
open Function Set Manifold

namespace Manifold

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E E' E'' E''' F : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
    [NormedAddCommGroup E'] [NormedSpace ùïú E'] [NormedAddCommGroup F] [NormedSpace ùïú F]
    [NormedAddCommGroup E''] [NormedSpace ùïú E''] [NormedAddCommGroup E'''] [NormedSpace ùïú E''']
  {H : Type*} [TopologicalSpace H] {H' : Type*} [TopologicalSpace H']
  {G : Type*} [TopologicalSpace G] {G' : Type*} [TopologicalSpace G']
  {I : ModelWithCorners ùïú E H} {I' : ModelWithCorners ùïú E' H'}
  {J : ModelWithCorners ùïú E'' G} {J' : ModelWithCorners ùïú E''' G'}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  {n : WithTop ‚Ñï‚àû}

variable (F I J M N) in
/-- The local property of being an immersion at a point: `f : M ‚Üí N` is an immersion at `x` if
there exist charts `œÜ` and `œà` of `M` and `N` around `x` and `f x`, respectively, such that in these
charts, `f` looks like the inclusion `u ‚Ü¶ (u, 0)`.

This definition has a fixed parameter `F`, which is a choice of complement of `E` in `E'`:
being an immersion at `x` includes a choice of linear isomorphism between `E √ó F` and `E'`. -/
def ImmersionAtProp : (M ‚Üí N) ‚Üí OpenPartialHomeomorph M H ‚Üí OpenPartialHomeomorph N G ‚Üí Prop :=
  fun f domChart codChart ‚Ü¶ ‚àÉ equiv : (E √ó F) ‚âÉL[ùïú] E'',
    EqOn ((codChart.extend J) ‚àò f ‚àò (domChart.extend I).symm) (equiv ‚àò (¬∑, 0))
      (domChart.extend I).target

omit [ChartedSpace H M] [ChartedSpace G N] in
/-- Being an immersion at `x` is a local property. -/
lemma isLocalSourceTargetProperty_immersionAtProp :
    IsLocalSourceTargetProperty (ImmersionAtProp F I J M N) where
  mono_source {f œÜ œà s} hs hf := by
    obtain ‚ü®equiv, hf‚ü© := hf
    exact ‚ü®equiv, hf.mono (by simp; grind)‚ü©
  congr {f g œÜ œà} hfg hf := by
    obtain ‚ü®equiv, hf‚ü© := hf
    refine ‚ü®equiv, EqOn.trans (fun x hx ‚Ü¶ ?_) (hf.mono (by simp))‚ü©
    have : ((œÜ.extend I).symm) x ‚àà œÜ.source := by simp_all
    grind

variable (F I J n) in
/-- `f : M ‚Üí N` is a `C^n` immersion at `x` if there are charts `œÜ` and `œà` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u ‚Ü¶ (u, 0)`.
Additionally, we demand that `f` map `œÜ.source` into `œà.source`.

NB. We don't know the particular atlasses used for `M` and `N`, so asking for `œÜ` and `œà` to be
in the `atlas` would be too optimistic: lying in the `maximalAtlas` is sufficient.

This definition has a fixed parameter `F`, which is a choice of complement of `E` in `E'`:
being an immersion at `x` includes a choice of linear isomorphism between `E √ó F` and `E'`.
-/
irreducible_def IsImmersionAt (f : M ‚Üí N) (x : M) : Prop :=
  LiftSourceTargetPropertyAt I J n f x (ImmersionAtProp F I J M N)

variable {f g : M ‚Üí N} {x : M}

namespace IsImmersionAt

lemma mk_of_charts (equiv : (E √ó F) ‚âÉL[ùïú] E'') (domChart : OpenPartialHomeomorph M H)
    (codChart : OpenPartialHomeomorph N G)
    (hx : x ‚àà domChart.source) (hfx : f x ‚àà codChart.source)
    (hdomChart : domChart ‚àà IsManifold.maximalAtlas I n M)
    (hcodChart : codChart ‚àà IsManifold.maximalAtlas J n N)
    (hsource : domChart.source ‚äÜ f ‚Åª¬π' codChart.source)
    (hwrittenInExtend : EqOn ((codChart.extend J) ‚àò f ‚àò (domChart.extend I).symm) (equiv ‚àò (¬∑, 0))
      (domChart.extend I).target) : IsImmersionAt F I J n f x := by
  rw [IsImmersionAt_def]
  use domChart, codChart
  use equiv

/-- `f : M ‚Üí N` is a `C^n` immersion at `x` if there are charts `œÜ` and `œà` of `M` and `N`
around `x` and `f x`, respectively such that in these charts, `f` looks like `u ‚Ü¶ (u, 0)`.
This version does not assume that `f` maps `œÜ.source` to `œà.source`,
but that `f` is continuous at `x`. -/
lemma mk_of_continuousAt {f : M ‚Üí N} {x : M} (hf : ContinuousAt f x) (equiv : (E √ó F) ‚âÉL[ùïú] E'')
    (domChart : OpenPartialHomeomorph M H) (codChart : OpenPartialHomeomorph N G)
    (hx : x ‚àà domChart.source) (hfx : f x ‚àà codChart.source)
    (hdomChart : domChart ‚àà IsManifold.maximalAtlas I n M)
    (hcodChart : codChart ‚àà IsManifold.maximalAtlas J n N)
    (hwrittenInExtend : EqOn ((codChart.extend J) ‚àò f ‚àò (domChart.extend I).symm) (equiv ‚àò (¬∑, 0))
      (domChart.extend I).target) : IsImmersionAt F I J n f x := by
  rw [IsImmersionAt_def]
  exact LiftSourceTargetPropertyAt.mk_of_continuousAt hf isLocalSourceTargetProperty_immersionAtProp
    _ _ hx hfx hdomChart hcodChart ‚ü®equiv, hwrittenInExtend‚ü©

/-- A choice of chart on the domain `M` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.codChart` and `h.equiv`,
`f` will look like an inclusion `u ‚Ü¶ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.codChart` and `h.codChart`. -/
noncomputable def domChart (h : IsImmersionAt F I J n f x) : OpenPartialHomeomorph M H := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.domChart h

/-- A choice of chart on the co-domain `N` of an immersion `f` at `x`:
w.r.t. this chart and the data `h.domChart` and `h.equiv`,
`f` will look like an inclusion `u ‚Ü¶ (u, 0)` in these extended charts.
The particular chart is arbitrary, but this choice matches the witnesses given by
`h.equiv` and `h.domChart`. -/
noncomputable def codChart (h : IsImmersionAt F I J n f x) : OpenPartialHomeomorph N G := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.codChart h

lemma mem_domChart_source (h : IsImmersionAt F I J n f x) : x ‚àà h.domChart.source := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.mem_domChart_source h

lemma mem_codChart_source (h : IsImmersionAt F I J n f x) : f x ‚àà h.codChart.source := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.mem_codChart_source h

lemma domChart_mem_maximalAtlas (h : IsImmersionAt F I J n f x) :
    h.domChart ‚àà IsManifold.maximalAtlas I n M := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.domChart_mem_maximalAtlas h

lemma codChart_mem_maximalAtlas (h : IsImmersionAt F I J n f x) :
    h.codChart ‚àà IsManifold.maximalAtlas J n N := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.codChart_mem_maximalAtlas h

lemma source_subset_preimage_source (h : IsImmersionAt F I J n f x) :
    h.domChart.source ‚äÜ f ‚Åª¬π' h.codChart.source := by
  rw [IsImmersionAt_def] at h
  exact LiftSourceTargetPropertyAt.source_subset_preimage_source h

/-- A linear equivalence `E √ó F ‚âÉL[ùïú] E''` which belongs to the data of an immersion `f` at `x`:
the particular equivalence is arbitrary, but this choice matches the witnesses given by
`h.domChart` and `h.codChart`. -/
noncomputable def equiv (h : IsImmersionAt F I J n f x) : (E √ó F) ‚âÉL[ùïú] E'' := by
  rw [IsImmersionAt_def] at h
  exact Classical.choose <| LiftSourceTargetPropertyAt.property h

lemma writtenInCharts (h : IsImmersionAt F I J n f x) :
    EqOn ((h.codChart.extend J) ‚àò f ‚àò (h.domChart.extend I).symm) (h.equiv ‚àò (¬∑, 0))
      (h.domChart.extend I).target := by
  rw [IsImmersionAt_def] at h
  exact Classical.choose_spec <| LiftSourceTargetPropertyAt.property h

lemma property (h : IsImmersionAt F I J n f x) :
    LiftSourceTargetPropertyAt I J n f x (ImmersionAtProp F I J M N) := by
  rwa [IsImmersionAt_def] at h

/--
If `f` is an immersion at `x`, it maps its domain chart's target to its codomain chart's target:
`(h.domChart.extend I).target` to `(h.domChart.extend J).target`.

Roig and Domingues' [roigdomingues1992] definition of immersions only asks for this inclusion
between the targets of the local charts: using mathlib's formalisation conventions, that condition
is *slightly* weaker than `source_subset_preimage_source`: the latter implies that
`h.codChart.extend J ‚àò f` maps `h.domChart.source` to
`(h.codChart.extend J).target = (h.codChart.extend I) '' h.codChart.source`,
but that does *not* imply `f` maps `h.domChart.source` to `h.codChartSource`;
a priori `f` could map some point `f ‚àò h.domChart.extend I x ‚àâ h.codChart.source` into the target.
Note that this difference only occurs because of our design using junk values;
this is not a mathematically meaningful difference.`

At the same time, this condition is fairly weak: it is implied, for instance, by `f` being
continuous at `x` (see `mk_of_continuousAt`), which is easy to ascertain in practice.
-/
lemma map_target_subset_target (h : IsImmersionAt F I J n f x) :
    (h.equiv ‚àò (¬∑, 0)) '' (h.domChart.extend I).target ‚äÜ (h.codChart.extend J).target := by
  rw [‚Üê h.writtenInCharts.image_eq, Set.image_comp, Set.image_comp,
    PartialEquiv.symm_image_target_eq_source, OpenPartialHomeomorph.extend_source,
    ‚Üê PartialEquiv.image_source_eq_target]
  have : f '' h.domChart.source ‚äÜ h.codChart.source := by
    simp [h.source_subset_preimage_source]
  grw [this, OpenPartialHomeomorph.extend_source]

/-- If `f` is an immersion at `x` and `g = f` on some neighbourhood of `x`,
then `g` is an immersion at `x`. -/
lemma congr_of_eventuallyEq {x : M} (hf : IsImmersionAt F I J n f x) (hfg : f =·∂†[ùìù x] g) :
    IsImmersionAt F I J n g x := by
  rw [IsImmersionAt_def]
  exact LiftSourceTargetPropertyAt.congr_of_eventuallyEq
    isLocalSourceTargetProperty_immersionAtProp hf.property hfg

/-- If `f = g` on some neighbourhood of `x`,
then `f` is an immersion at `x` if and only if `g` is an immersion at `x`. -/
lemma congr_iff {x : M} (hfg : f =·∂†[ùìù x] g) :
    IsImmersionAt F I J n f x ‚Üî IsImmersionAt F I J n g x :=
  ‚ü®fun h ‚Ü¶ h.congr_of_eventuallyEq hfg, fun h ‚Ü¶ h.congr_of_eventuallyEq hfg.symm‚ü©

end IsImmersionAt

variable (F I J n) in
/-- `f : M ‚Üí N` is a `C^n` immersion if around each point `x ‚àà M`,
there are charts `œÜ` and `œà` of `M` and `N` around `x` and `f x`, respectively
such that in these charts, `f` looks like `u ‚Ü¶ (u, 0)`.

In other words, `f` is an immersion at each `x ‚àà M`.

This definition has a fixed parameter `F`, which is a choice of complement of `E` in `E'`:
being an immersion at `x` includes a choice of linear isomorphism between `E √ó F` and `E'`.
-/
def IsImmersion (f : M ‚Üí N) : Prop := ‚àÄ x, IsImmersionAt F I J n f x

namespace IsImmersion

variable {f g : M ‚Üí N}

/-- If `f` is an immersion, it is an immersion at each point. -/
lemma isImmersionAt (h : IsImmersion F I J n f) (x : M) : IsImmersionAt F I J n f x := h x

/-- If `f = g` and `f` is an immersion, so is `g`. -/
theorem congr (h : IsImmersion F I J n f) (heq : f = g) : IsImmersion F I J n g :=
  heq ‚ñ∏ h

end IsImmersion

end Manifold
