/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Normed.Module.FiniteDimension
import Mathlib.Geometry.Manifold.IsManifold.Basic

/-!
# Extended charts in smooth manifolds

In a `C^n` manifold with corners with the model `I` on `(E, H)`, the charts take values in the
model space `H`. However, we also need to use extended charts taking values in the model vector
space `E`. These extended charts are not `PartialHomeomorph` as the target is not open in `E`
in general, but we can still register them as `PartialEquiv`.

## Main definitions

* `PartialHomeomorph.extend`: compose a partial homeomorphism into `H` with the model `I`,
  to obtain a `PartialEquiv` into `E`. Extended charts are an example of this.
* `extChartAt I x`: the extended chart at `x`, obtained by composing the `chartAt H x` with `I`.
  Since the target is in general not open, this is not a partial homeomorphism in general, but
  we register them as `PartialEquiv`s.

## Main results

* `contDiffOn_extend_coord_change`: if `f` and `f'` lie in the maximal atlas on `M`,
  `f.extend I âˆ˜ (f'.extend I).symm` is continuous on its source

* `contDiffOn_ext_coord_change`: for `x x : M`, the coordinate change
  `(extChartAt I x').symm â‰« extChartAt I x` is continuous on its source

* `Manifold.locallyCompact_of_finiteDimensional`: a finite-dimensional manifold
  modelled on a locally compact field (such as â„, â„‚ or the `p`-adic numbers) is locally compact
* `LocallyCompactSpace.of_locallyCompact_manifold`: a locally compact manifold must be modelled
  on a locally compact space.
* `FiniteDimensional.of_locallyCompact_manifold`: a locally compact manifolds must be modelled
  on a finite-dimensional space

-/

noncomputable section

open Set Filter Function
open scoped Manifold Topology

variable {ğ•œ E M H E' M' H' : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [TopologicalSpace H] [TopologicalSpace M] {n : WithTop â„•âˆ}
  (f f' : PartialHomeomorph M H)
  {I : ModelWithCorners ğ•œ E H} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] [TopologicalSpace H']
  [TopologicalSpace M'] {I' : ModelWithCorners ğ•œ E' H'} {s t : Set M}

section ExtendedCharts

namespace PartialHomeomorph

variable (I) in
/-- Given a chart `f` on a manifold with corners, `f.extend I` is the extended chart to the model
vector space. -/
@[simp, mfld_simps]
def extend : PartialEquiv M E :=
  f.toPartialEquiv â‰« I.toPartialEquiv

theorem extend_coe : â‡‘(f.extend I) = I âˆ˜ f :=
  rfl

theorem extend_coe_symm : â‡‘(f.extend I).symm = f.symm âˆ˜ I.symm :=
  rfl

theorem extend_source : (f.extend I).source = f.source := by
  rw [extend, PartialEquiv.trans_source, I.source_eq, preimage_univ, inter_univ]

theorem isOpen_extend_source : IsOpen (f.extend I).source := by
  rw [extend_source]
  exact f.open_source

theorem extend_target : (f.extend I).target = I.symm â»Â¹' f.target âˆ© range I := by
  simp_rw [extend, PartialEquiv.trans_target, I.target_eq, I.toPartialEquiv_coe_symm, inter_comm]

theorem extend_target' : (f.extend I).target = I '' f.target := by
  rw [extend, PartialEquiv.trans_target'', I.source_eq, univ_inter, I.toPartialEquiv_coe]

lemma isOpen_extend_target [I.Boundaryless] : IsOpen (f.extend I).target := by
  rw [extend_target, I.range_eq_univ, inter_univ]
  exact I.continuous_symm.isOpen_preimage _ f.open_target

theorem mapsTo_extend (hs : s âŠ† f.source) :
    MapsTo (f.extend I) s ((f.extend I).symm â»Â¹' s âˆ© range I) := by
  rw [mapsTo_iff_image_subset, extend_coe, extend_coe_symm, preimage_comp, â† I.image_eq, image_comp,
    f.image_eq_target_inter_inv_preimage hs]
  exact image_mono inter_subset_right

theorem extend_left_inv {x : M} (hxf : x âˆˆ f.source) : (f.extend I).symm (f.extend I x) = x :=
  (f.extend I).left_inv <| by rwa [f.extend_source]

/-- Variant of `f.extend_left_inv I`, stated in terms of images. -/
lemma extend_left_inv' (ht : t âŠ† f.source) : ((f.extend I).symm âˆ˜ (f.extend I)) '' t = t :=
  EqOn.image_eq_self (fun _ hx â†¦ f.extend_left_inv (ht hx))

theorem extend_source_mem_nhds {x : M} (h : x âˆˆ f.source) : (f.extend I).source âˆˆ ğ“ x :=
  (isOpen_extend_source f).mem_nhds <| by rwa [f.extend_source]

theorem extend_source_mem_nhdsWithin {x : M} (h : x âˆˆ f.source) : (f.extend I).source âˆˆ ğ“[s] x :=
  mem_nhdsWithin_of_mem_nhds <| extend_source_mem_nhds f h

theorem continuousOn_extend : ContinuousOn (f.extend I) (f.extend I).source := by
  refine I.continuous.comp_continuousOn ?_
  rw [extend_source]
  exact f.continuousOn

theorem continuousAt_extend {x : M} (h : x âˆˆ f.source) : ContinuousAt (f.extend I) x :=
  (continuousOn_extend f).continuousAt <| extend_source_mem_nhds f h

theorem map_extend_nhds {x : M} (hy : x âˆˆ f.source) :
    map (f.extend I) (ğ“ x) = ğ“[range I] f.extend I x := by
  rwa [extend_coe, comp_apply, â† I.map_nhds_eq, â† f.map_nhds_eq, map_map]

theorem map_extend_nhds_of_mem_interior_range {x : M} (hx : x âˆˆ f.source)
    (h'x : f.extend I x âˆˆ interior (range I)) :
    map (f.extend I) (ğ“ x) = ğ“ (f.extend I x) := by
  rw [f.map_extend_nhds hx, nhdsWithin_eq_nhds]
  exact mem_of_superset (isOpen_interior.mem_nhds h'x) interior_subset

theorem map_extend_nhds_of_boundaryless [I.Boundaryless] {x : M} (hx : x âˆˆ f.source) :
    map (f.extend I) (ğ“ x) = ğ“ (f.extend I x) := by
  rw [f.map_extend_nhds hx, I.range_eq_univ, nhdsWithin_univ]

theorem extend_target_mem_nhdsWithin {y : M} (hy : y âˆˆ f.source) :
    (f.extend I).target âˆˆ ğ“[range I] f.extend I y := by
  rw [â† PartialEquiv.image_source_eq_target, â† map_extend_nhds f hy]
  exact image_mem_map (extend_source_mem_nhds _ hy)

lemma extend_image_target_mem_nhds {x : M} (hx : x âˆˆ f.source) :
    I '' f.target âˆˆ ğ“[range I] (f.extend I) x := by
  rw [â† f.map_extend_nhds hx, Filter.mem_map,
    f.extend_coe, Set.preimage_comp, I.preimage_image f.target]
  exact (f.continuousAt hx).preimage_mem_nhds (f.open_target.mem_nhds (f.map_source hx))

theorem extend_image_nhds_mem_nhds_of_boundaryless [I.Boundaryless] {x} (hx : x âˆˆ f.source)
    {s : Set M} (h : s âˆˆ ğ“ x) : (f.extend I) '' s âˆˆ ğ“ ((f.extend I) x) := by
  rw [â† f.map_extend_nhds_of_boundaryless hx, Filter.mem_map]
  filter_upwards [h] using subset_preimage_image (f.extend I) s

@[deprecated (since := "2025-05-22")]
alias extend_image_nhd_mem_nhds_of_boundaryless := extend_image_nhds_mem_nhds_of_boundaryless

theorem extend_image_nhds_mem_nhds_of_mem_interior_range {x} (hx : x âˆˆ f.source)
    (h'x : f.extend I x âˆˆ interior (range I)) {s : Set M} (h : s âˆˆ ğ“ x) :
    (f.extend I) '' s âˆˆ ğ“ ((f.extend I) x) := by
  rw [â† f.map_extend_nhds_of_mem_interior_range hx h'x, Filter.mem_map]
  filter_upwards [h] using subset_preimage_image (f.extend I) s

@[deprecated (since := "2025-05-22")]
alias extend_image_nhd_mem_nhds_of_mem_interior_range :=
  extend_image_nhds_mem_nhds_of_mem_interior_range

theorem extend_target_subset_range : (f.extend I).target âŠ† range I := by simp only [mfld_simps]

lemma interior_extend_target_subset_interior_range :
    interior (f.extend I).target âŠ† interior (range I) := by
  rw [f.extend_target, interior_inter, (f.open_target.preimage I.continuous_symm).interior_eq]
  exact inter_subset_right

/-- If `y âˆˆ f.target` and `I y âˆˆ interior (range I)`,
then `I y` is an interior point of `(I âˆ˜ f).target`. -/
lemma mem_interior_extend_target {y : H} (hy : y âˆˆ f.target)
    (hy' : I y âˆˆ interior (range I)) : I y âˆˆ interior (f.extend I).target := by
  rw [f.extend_target, interior_inter, (f.open_target.preimage I.continuous_symm).interior_eq,
    mem_inter_iff, mem_preimage]
  exact âŸ¨mem_of_eq_of_mem (I.left_inv (y)) hy, hy'âŸ©

theorem nhdsWithin_extend_target_eq {y : M} (hy : y âˆˆ f.source) :
    ğ“[(f.extend I).target] f.extend I y = ğ“[range I] f.extend I y :=
  (nhdsWithin_mono _ (extend_target_subset_range _)).antisymm <|
    nhdsWithin_le_of_mem (extend_target_mem_nhdsWithin _ hy)

theorem extend_target_eventuallyEq {y : M} (hy : y âˆˆ f.source) :
    (f.extend I).target =á¶ [ğ“ (f.extend I y)] range I :=
  nhdsWithin_eq_iff_eventuallyEq.1 (nhdsWithin_extend_target_eq _ hy)

theorem continuousAt_extend_symm' {x : E} (h : x âˆˆ (f.extend I).target) :
    ContinuousAt (f.extend I).symm x :=
  (f.continuousAt_symm h.2).comp I.continuous_symm.continuousAt

theorem continuousAt_extend_symm {x : M} (h : x âˆˆ f.source) :
    ContinuousAt (f.extend I).symm (f.extend I x) :=
  continuousAt_extend_symm' f <| (f.extend I).map_source <| by rwa [f.extend_source]

theorem continuousOn_extend_symm : ContinuousOn (f.extend I).symm (f.extend I).target := fun _ h =>
  (continuousAt_extend_symm' _ h).continuousWithinAt

theorem extend_symm_continuousWithinAt_comp_right_iff {X} [TopologicalSpace X] {g : M â†’ X}
    {s : Set M} {x : M} :
    ContinuousWithinAt (g âˆ˜ (f.extend I).symm) ((f.extend I).symm â»Â¹' s âˆ© range I) (f.extend I x) â†”
      ContinuousWithinAt (g âˆ˜ f.symm) (f.symm â»Â¹' s) (f x) := by
  rw [â† I.symm_continuousWithinAt_comp_right_iff]; rfl

theorem isOpen_extend_preimage' {s : Set E} (hs : IsOpen s) :
    IsOpen ((f.extend I).source âˆ© f.extend I â»Â¹' s) :=
  (continuousOn_extend f).isOpen_inter_preimage (isOpen_extend_source _) hs

theorem isOpen_extend_preimage {s : Set E} (hs : IsOpen s) :
    IsOpen (f.source âˆ© f.extend I â»Â¹' s) := by
  rw [â† extend_source f (I := I)]; exact isOpen_extend_preimage' f hs

theorem map_extend_nhdsWithin_eq_image {y : M} (hy : y âˆˆ f.source) :
    map (f.extend I) (ğ“[s] y) = ğ“[f.extend I '' ((f.extend I).source âˆ© s)] f.extend I y := by
  set e := f.extend I
  calc
    map e (ğ“[s] y) = map e (ğ“[e.source âˆ© s] y) :=
      congr_arg (map e) (nhdsWithin_inter_of_mem (extend_source_mem_nhdsWithin f hy)).symm
    _ = ğ“[e '' (e.source âˆ© s)] e y :=
      ((f.extend I).leftInvOn.mono inter_subset_left).map_nhdsWithin_eq
        ((f.extend I).left_inv <| by rwa [f.extend_source])
        (continuousAt_extend_symm f hy).continuousWithinAt
        (continuousAt_extend f hy).continuousWithinAt

theorem map_extend_nhdsWithin_eq_image_of_subset {y : M} (hy : y âˆˆ f.source) (hs : s âŠ† f.source) :
    map (f.extend I) (ğ“[s] y) = ğ“[f.extend I '' s] f.extend I y := by
  rw [map_extend_nhdsWithin_eq_image _ hy, inter_eq_self_of_subset_right]
  rwa [extend_source]

theorem map_extend_nhdsWithin {y : M} (hy : y âˆˆ f.source) :
    map (f.extend I) (ğ“[s] y) = ğ“[(f.extend I).symm â»Â¹' s âˆ© range I] f.extend I y := by
  rw [map_extend_nhdsWithin_eq_image f hy, nhdsWithin_inter, â†
    nhdsWithin_extend_target_eq _ hy, â† nhdsWithin_inter, (f.extend I).image_source_inter_eq',
    inter_comm]

theorem map_extend_symm_nhdsWithin {y : M} (hy : y âˆˆ f.source) :
    map (f.extend I).symm (ğ“[(f.extend I).symm â»Â¹' s âˆ© range I] f.extend I y) = ğ“[s] y := by
  rw [â† map_extend_nhdsWithin f hy, map_map, Filter.map_congr, map_id]
  exact (f.extend I).leftInvOn.eqOn.eventuallyEq_of_mem (extend_source_mem_nhdsWithin _ hy)

theorem map_extend_symm_nhdsWithin_range {y : M} (hy : y âˆˆ f.source) :
    map (f.extend I).symm (ğ“[range I] f.extend I y) = ğ“ y := by
  rw [â† nhdsWithin_univ, â† map_extend_symm_nhdsWithin f (I := I) hy, preimage_univ, univ_inter]

theorem tendsto_extend_comp_iff {Î± : Type*} {l : Filter Î±} {g : Î± â†’ M}
    (hg : âˆ€á¶  z in l, g z âˆˆ f.source) {y : M} (hy : y âˆˆ f.source) :
    Tendsto (f.extend I âˆ˜ g) l (ğ“ (f.extend I y)) â†” Tendsto g l (ğ“ y) := by
  refine âŸ¨fun h u hu â†¦ mem_map.2 ?_, (continuousAt_extend _ hy).tendsto.compâŸ©
  have := (f.continuousAt_extend_symm hy).tendsto.comp h
  rw [extend_left_inv _ hy] at this
  filter_upwards [hg, mem_map.1 (this hu)] with z hz hzu
  simpa only [(Â· âˆ˜ Â·), extend_left_inv _ hz, mem_preimage] using hzu

-- there is no definition `writtenInExtend` but we already use some made-up names in this file
theorem continuousWithinAt_writtenInExtend_iff {f' : PartialHomeomorph M' H'} {g : M â†’ M'} {y : M}
    (hy : y âˆˆ f.source) (hgy : g y âˆˆ f'.source) (hmaps : MapsTo g s f'.source) :
    ContinuousWithinAt (f'.extend I' âˆ˜ g âˆ˜ (f.extend I).symm)
      ((f.extend I).symm â»Â¹' s âˆ© range I) (f.extend I y) â†” ContinuousWithinAt g s y := by
  unfold ContinuousWithinAt
  simp only [comp_apply]
  rw [extend_left_inv _ hy, f'.tendsto_extend_comp_iff _ hgy,
    â† f.map_extend_symm_nhdsWithin (I := I) hy, tendsto_map'_iff]
  rw [â† f.map_extend_nhdsWithin (I := I) hy, eventually_map]
  filter_upwards [inter_mem_nhdsWithin _ (f.open_source.mem_nhds hy)] with z hz
  rw [comp_apply, extend_left_inv _ hz.2]
  exact hmaps hz.1

-- there is no definition `writtenInExtend` but we already use some made-up names in this file

/-- If `s âŠ† f.source` and `g x âˆˆ f'.source` whenever `x âˆˆ s`, then `g` is continuous on `s` if and
only if `g` written in charts `f.extend I` and `f'.extend I'` is continuous on `f.extend I '' s`. -/
theorem continuousOn_writtenInExtend_iff {f' : PartialHomeomorph M' H'} {g : M â†’ M'}
    (hs : s âŠ† f.source) (hmaps : MapsTo g s f'.source) :
    ContinuousOn (f'.extend I' âˆ˜ g âˆ˜ (f.extend I).symm) (f.extend I '' s) â†” ContinuousOn g s := by
  refine forall_mem_image.trans <| forallâ‚‚_congr fun x hx â†¦ ?_
  refine (continuousWithinAt_congr_set ?_).trans
    (continuousWithinAt_writtenInExtend_iff _ (hs hx) (hmaps hx) hmaps)
  rw [â† nhdsWithin_eq_iff_eventuallyEq, â† map_extend_nhdsWithin_eq_image_of_subset,
    â† map_extend_nhdsWithin]
  exacts [hs hx, hs hx, hs]

theorem extend_preimage_mem_nhds_of_mem_nhdsWithin {s : Set E} {x : M} (hx : x âˆˆ f.source)
    (hs : s âˆˆ ğ“[range I] (f.extend I x)) :
    (f.extend I) â»Â¹' s âˆˆ ğ“ x := by
  rwa [â† map_extend_nhds (I := I) f hx] at hs

/-- Technical lemma ensuring that the preimage under an extended chart of a neighborhood of a point
in the source is a neighborhood of the preimage, within a set. -/
theorem extend_preimage_mem_nhdsWithin {x : M} (h : x âˆˆ f.source) (ht : t âˆˆ ğ“[s] x) :
    (f.extend I).symm â»Â¹' t âˆˆ ğ“[(f.extend I).symm â»Â¹' s âˆ© range I] f.extend I x := by
  rwa [â† map_extend_symm_nhdsWithin f (I := I) h, mem_map] at ht

theorem extend_preimage_mem_nhds {x : M} (h : x âˆˆ f.source) (ht : t âˆˆ ğ“ x) :
    (f.extend I).symm â»Â¹' t âˆˆ ğ“ (f.extend I x) := by
  apply (continuousAt_extend_symm f h).preimage_mem_nhds
  rwa [(f.extend I).left_inv]
  rwa [f.extend_source]

/-- Technical lemma to rewrite suitably the preimage of an intersection under an extended chart, to
bring it into a convenient form to apply derivative lemmas. -/
theorem extend_preimage_inter_eq :
    (f.extend I).symm â»Â¹' (s âˆ© t) âˆ© range I =
      (f.extend I).symm â»Â¹' s âˆ© range I âˆ© (f.extend I).symm â»Â¹' t := by
  mfld_set_tac

@[deprecated "Removed without replacement" (since := "2025-08-27")]
theorem extend_symm_preimage_inter_range_eventuallyEq_aux {s : Set M} {x : M} (hx : x âˆˆ f.source) :
    ((f.extend I).symm â»Â¹' s âˆ© range I : Set _) =á¶ [ğ“ (f.extend I x)]
      ((f.extend I).target âˆ© (f.extend I).symm â»Â¹' s : Set _) := by
  rw [f.extend_target, inter_assoc, inter_comm (range I)]
  conv =>
    congr
    Â· skip
    rw [â† univ_inter (_ âˆ© range I)]
  refine (eventuallyEq_univ.mpr ?_).symm.inter EventuallyEq.rfl
  refine I.continuousAt_symm.preimage_mem_nhds (f.open_target.mem_nhds ?_)
  simp_rw [f.extend_coe, Function.comp_apply, I.left_inv, f.mapsTo hx]

theorem extend_symm_preimage_inter_range_eventuallyEq {s : Set M} {x : M} (hs : s âŠ† f.source)
    (hx : x âˆˆ f.source) :
    ((f.extend I).symm â»Â¹' s âˆ© range I : Set _) =á¶ [ğ“ (f.extend I x)] f.extend I '' s := by
  rw [â† nhdsWithin_eq_iff_eventuallyEq, â† map_extend_nhdsWithin _ hx,
    map_extend_nhdsWithin_eq_image_of_subset _ hx hs]

/-! We use the name `extend_coord_change` for `(f'.extend I).symm â‰« f.extend I`. -/

theorem extend_coord_change_source :
    ((f.extend I).symm â‰« f'.extend I).source = I '' (f.symm â‰«â‚• f').source := by
  simp_rw [PartialEquiv.trans_source, I.image_eq, extend_source, PartialEquiv.symm_source,
    extend_target, inter_right_comm _ (range I)]
  rfl

theorem extend_image_source_inter :
    f.extend I '' (f.source âˆ© f'.source) = ((f.extend I).symm â‰« f'.extend I).source := by
  simp_rw [f.extend_coord_change_source, f.extend_coe, image_comp I f, trans_source'', symm_symm,
    symm_target]

theorem extend_coord_change_source_mem_nhdsWithin {x : E}
    (hx : x âˆˆ ((f.extend I).symm â‰« f'.extend I).source) :
    ((f.extend I).symm â‰« f'.extend I).source âˆˆ ğ“[range I] x := by
  rw [f.extend_coord_change_source] at hx âŠ¢
  obtain âŸ¨x, hx, rflâŸ© := hx
  refine I.image_mem_nhdsWithin ?_
  exact (PartialHomeomorph.open_source _).mem_nhds hx

theorem extend_coord_change_source_mem_nhdsWithin' {x : M} (hxf : x âˆˆ f.source)
    (hxf' : x âˆˆ f'.source) :
    ((f.extend I).symm â‰« f'.extend I).source âˆˆ ğ“[range I] f.extend I x := by
  apply extend_coord_change_source_mem_nhdsWithin
  rw [â† extend_image_source_inter]
  exact mem_image_of_mem _ âŸ¨hxf, hxf'âŸ©

variable {f f'}

open IsManifold

theorem contDiffOn_extend_coord_change [ChartedSpace H M] (hf : f âˆˆ maximalAtlas I n M)
    (hf' : f' âˆˆ maximalAtlas I n M) :
    ContDiffOn ğ•œ n (f.extend I âˆ˜ (f'.extend I).symm) ((f'.extend I).symm â‰« f.extend I).source := by
  rw [extend_coord_change_source, I.image_eq]
  exact (StructureGroupoid.compatible_of_mem_maximalAtlas hf' hf).1

theorem contDiffWithinAt_extend_coord_change [ChartedSpace H M] (hf : f âˆˆ maximalAtlas I n M)
    (hf' : f' âˆˆ maximalAtlas I n M) {x : E} (hx : x âˆˆ ((f'.extend I).symm â‰« f.extend I).source) :
    ContDiffWithinAt ğ•œ n (f.extend I âˆ˜ (f'.extend I).symm) (range I) x := by
  apply (contDiffOn_extend_coord_change hf hf' x hx).mono_of_mem_nhdsWithin
  rw [extend_coord_change_source] at hx âŠ¢
  obtain âŸ¨z, hz, rflâŸ© := hx
  exact I.image_mem_nhdsWithin ((PartialHomeomorph.open_source _).mem_nhds hz)

theorem contDiffWithinAt_extend_coord_change' [ChartedSpace H M] (hf : f âˆˆ maximalAtlas I n M)
    (hf' : f' âˆˆ maximalAtlas I n M) {x : M} (hxf : x âˆˆ f.source) (hxf' : x âˆˆ f'.source) :
    ContDiffWithinAt ğ•œ n (f.extend I âˆ˜ (f'.extend I).symm) (range I) (f'.extend I x) := by
  refine contDiffWithinAt_extend_coord_change hf hf' ?_
  rw [â† extend_image_source_inter]
  exact mem_image_of_mem _ âŸ¨hxf', hxfâŸ©

end PartialHomeomorph

open PartialHomeomorph

variable [ChartedSpace H M] [ChartedSpace H' M']

variable (I) in
/-- The preferred extended chart on a manifold with corners around a point `x`, from a neighborhood
of `x` to the model vector space. -/
@[simp, mfld_simps]
def extChartAt (x : M) : PartialEquiv M E :=
  (chartAt H x).extend I

theorem extChartAt_coe (x : M) : â‡‘(extChartAt I x) = I âˆ˜ chartAt H x :=
  rfl

theorem extChartAt_coe_symm (x : M) : â‡‘(extChartAt I x).symm = (chartAt H x).symm âˆ˜ I.symm :=
  rfl

variable (I) in
theorem extChartAt_source (x : M) : (extChartAt I x).source = (chartAt H x).source :=
  extend_source _

theorem isOpen_extChartAt_source (x : M) : IsOpen (extChartAt I x).source :=
  isOpen_extend_source _

theorem mem_extChartAt_source (x : M) : x âˆˆ (extChartAt I x).source := by
  simp only [extChartAt_source, mem_chart_source]

theorem mem_extChartAt_target (x : M) : extChartAt I x x âˆˆ (extChartAt I x).target :=
  (extChartAt I x).map_source <| mem_extChartAt_source _

variable (I) in
theorem extChartAt_target (x : M) :
    (extChartAt I x).target = I.symm â»Â¹' (chartAt H x).target âˆ© range I :=
  extend_target _

theorem uniqueDiffOn_extChartAt_target (x : M) : UniqueDiffOn ğ•œ (extChartAt I x).target := by
  rw [extChartAt_target]
  exact I.uniqueDiffOn_preimage (chartAt H x).open_target

theorem uniqueDiffWithinAt_extChartAt_target (x : M) :
    UniqueDiffWithinAt ğ•œ (extChartAt I x).target (extChartAt I x x) :=
  uniqueDiffOn_extChartAt_target x _ <| mem_extChartAt_target x

theorem extChartAt_to_inv (x : M) : (extChartAt I x).symm ((extChartAt I x) x) = x :=
  (extChartAt I x).left_inv (mem_extChartAt_source x)

theorem mapsTo_extChartAt {x : M} (hs : s âŠ† (chartAt H x).source) :
    MapsTo (extChartAt I x) s ((extChartAt I x).symm â»Â¹' s âˆ© range I) :=
  mapsTo_extend _ hs

theorem extChartAt_source_mem_nhds' {x x' : M} (h : x' âˆˆ (extChartAt I x).source) :
    (extChartAt I x).source âˆˆ ğ“ x' :=
  extend_source_mem_nhds _ <| by rwa [â† extChartAt_source I]

theorem extChartAt_source_mem_nhds (x : M) : (extChartAt I x).source âˆˆ ğ“ x :=
  extChartAt_source_mem_nhds' (mem_extChartAt_source x)

theorem extChartAt_source_mem_nhdsWithin' {x x' : M} (h : x' âˆˆ (extChartAt I x).source) :
    (extChartAt I x).source âˆˆ ğ“[s] x' :=
  mem_nhdsWithin_of_mem_nhds (extChartAt_source_mem_nhds' h)

theorem extChartAt_source_mem_nhdsWithin (x : M) : (extChartAt I x).source âˆˆ ğ“[s] x :=
  mem_nhdsWithin_of_mem_nhds (extChartAt_source_mem_nhds x)

theorem continuousOn_extChartAt (x : M) : ContinuousOn (extChartAt I x) (extChartAt I x).source :=
  continuousOn_extend _

theorem continuousAt_extChartAt' {x x' : M} (h : x' âˆˆ (extChartAt I x).source) :
    ContinuousAt (extChartAt I x) x' :=
  continuousAt_extend _ <| by rwa [â† extChartAt_source I]

theorem continuousAt_extChartAt (x : M) : ContinuousAt (extChartAt I x) x :=
  continuousAt_extChartAt' (mem_extChartAt_source x)

theorem map_extChartAt_nhds' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    map (extChartAt I x) (ğ“ y) = ğ“[range I] extChartAt I x y :=
  map_extend_nhds _ <| by rwa [â† extChartAt_source I]

theorem map_extChartAt_nhds (x : M) : map (extChartAt I x) (ğ“ x) = ğ“[range I] extChartAt I x x :=
  map_extChartAt_nhds' <| mem_extChartAt_source x

theorem map_extChartAt_nhds_of_boundaryless [I.Boundaryless] (x : M) :
    map (extChartAt I x) (ğ“ x) = ğ“ (extChartAt I x x) := by
  rw [extChartAt]
  exact map_extend_nhds_of_boundaryless (chartAt H x) (mem_chart_source H x)

theorem extChartAt_image_nhds_mem_nhds_of_mem_interior_range {x y}
    (hx : y âˆˆ (extChartAt I x).source)
    (h'x : extChartAt I x y âˆˆ interior (range I)) {s : Set M} (h : s âˆˆ ğ“ y) :
    (extChartAt I x) '' s âˆˆ ğ“ (extChartAt I x y) := by
  rw [extChartAt]
  exact extend_image_nhds_mem_nhds_of_mem_interior_range _ (by simpa using hx) h'x h

@[deprecated (since := "2025-05-22")]
alias extChartAt_image_nhd_mem_nhds_of_mem_interior_range :=
  extChartAt_image_nhds_mem_nhds_of_mem_interior_range

variable {x} in
theorem extChartAt_image_nhds_mem_nhds_of_boundaryless [I.Boundaryless]
    {x : M} (hx : s âˆˆ ğ“ x) : extChartAt I x '' s âˆˆ ğ“ (extChartAt I x x) := by
  rw [extChartAt]
  exact extend_image_nhds_mem_nhds_of_boundaryless _ (mem_chart_source H x) hx

@[deprecated (since := "2025-05-22")]
alias extChartAt_image_nhd_mem_nhds_of_boundaryless :=
  extChartAt_image_nhds_mem_nhds_of_boundaryless

theorem extChartAt_target_mem_nhdsWithin' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    (extChartAt I x).target âˆˆ ğ“[range I] extChartAt I x y :=
  extend_target_mem_nhdsWithin _ <| by rwa [â† extChartAt_source I]

theorem extChartAt_target_mem_nhdsWithin (x : M) :
    (extChartAt I x).target âˆˆ ğ“[range I] extChartAt I x x :=
  extChartAt_target_mem_nhdsWithin' (mem_extChartAt_source x)

theorem extChartAt_target_mem_nhdsWithin_of_mem {x : M} {y : E} (hy : y âˆˆ (extChartAt I x).target) :
    (extChartAt I x).target âˆˆ ğ“[range I] y := by
  rw [â† (extChartAt I x).right_inv hy]
  apply extChartAt_target_mem_nhdsWithin'
  exact (extChartAt I x).map_target hy

theorem extChartAt_target_union_compl_range_mem_nhds_of_mem {y : E} {x : M}
    (hy : y âˆˆ (extChartAt I x).target) : (extChartAt I x).target âˆª (range I)á¶œ âˆˆ ğ“ y := by
  rw [â† nhdsWithin_univ, â† union_compl_self (range I), nhdsWithin_union]
  exact Filter.union_mem_sup (extChartAt_target_mem_nhdsWithin_of_mem hy) self_mem_nhdsWithin

/-- If we're boundaryless, `extChartAt` has open target -/
theorem isOpen_extChartAt_target [I.Boundaryless] (x : M) : IsOpen (extChartAt I x).target := by
  simp_rw [extChartAt_target, I.range_eq_univ, inter_univ]
  exact (PartialHomeomorph.open_target _).preimage I.continuous_symm

/-- If we're boundaryless, `(extChartAt I x).target` is a neighborhood of the key point -/
theorem extChartAt_target_mem_nhds [I.Boundaryless] (x : M) :
    (extChartAt I x).target âˆˆ ğ“ (extChartAt I x x) := by
  convert extChartAt_target_mem_nhdsWithin x
  simp only [I.range_eq_univ, nhdsWithin_univ]

/-- If we're boundaryless, `(extChartAt I x).target` is a neighborhood of any of its points -/
theorem extChartAt_target_mem_nhds' [I.Boundaryless] {x : M} {y : E}
    (m : y âˆˆ (extChartAt I x).target) : (extChartAt I x).target âˆˆ ğ“ y :=
  (isOpen_extChartAt_target x).mem_nhds m

theorem extChartAt_target_subset_range (x : M) : (extChartAt I x).target âŠ† range I := by
  simp only [mfld_simps]

/-- Around the image of a point in the source, the neighborhoods are the same
within `(extChartAt I x).target` and within `range I`. -/
theorem nhdsWithin_extChartAt_target_eq' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    ğ“[(extChartAt I x).target] extChartAt I x y = ğ“[range I] extChartAt I x y :=
  nhdsWithin_extend_target_eq _ <| by rwa [â† extChartAt_source I]

/-- Around a point in the target, the neighborhoods are the same within `(extChartAt I x).target`
and within `range I`. -/
theorem nhdsWithin_extChartAt_target_eq_of_mem {x : M} {z : E} (hz : z âˆˆ (extChartAt I x).target) :
    ğ“[(extChartAt I x).target] z = ğ“[range I] z := by
  rw [â† PartialEquiv.right_inv (extChartAt I x) hz]
  exact nhdsWithin_extChartAt_target_eq' ((extChartAt I x).map_target hz)

/-- Around the image of the base point, the neighborhoods are the same
within `(extChartAt I x).target` and within `range I`. -/
theorem nhdsWithin_extChartAt_target_eq (x : M) :
    ğ“[(extChartAt I x).target] (extChartAt I x) x = ğ“[range I] (extChartAt I x) x :=
  nhdsWithin_extChartAt_target_eq' (mem_extChartAt_source x)

/-- Around the image of a point in the source, `(extChartAt I x).target` and `range I`
coincide locally. -/
theorem extChartAt_target_eventuallyEq' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    (extChartAt I x).target =á¶ [ğ“ (extChartAt I x y)] range I :=
  nhdsWithin_eq_iff_eventuallyEq.1 (nhdsWithin_extChartAt_target_eq' hy)

/-- Around a point in the target, `(extChartAt I x).target` and `range I` coincide locally. -/
theorem extChartAt_target_eventuallyEq_of_mem {x : M} {z : E} (hz : z âˆˆ (extChartAt I x).target) :
    (extChartAt I x).target =á¶ [ğ“ z] range I :=
  nhdsWithin_eq_iff_eventuallyEq.1 (nhdsWithin_extChartAt_target_eq_of_mem hz)

/-- Around the image of the base point, `(extChartAt I x).target` and `range I` coincide locally. -/
theorem extChartAt_target_eventuallyEq {x : M} :
    (extChartAt I x).target =á¶ [ğ“ (extChartAt I x x)] range I :=
  nhdsWithin_eq_iff_eventuallyEq.1 (nhdsWithin_extChartAt_target_eq x)

theorem continuousAt_extChartAt_symm'' {x : M} {y : E} (h : y âˆˆ (extChartAt I x).target) :
    ContinuousAt (extChartAt I x).symm y :=
  continuousAt_extend_symm' _ h

theorem continuousAt_extChartAt_symm' {x x' : M} (h : x' âˆˆ (extChartAt I x).source) :
    ContinuousAt (extChartAt I x).symm (extChartAt I x x') :=
  continuousAt_extChartAt_symm'' <| (extChartAt I x).map_source h

theorem continuousAt_extChartAt_symm (x : M) :
    ContinuousAt (extChartAt I x).symm ((extChartAt I x) x) :=
  continuousAt_extChartAt_symm' (mem_extChartAt_source x)

theorem continuousOn_extChartAt_symm (x : M) :
    ContinuousOn (extChartAt I x).symm (extChartAt I x).target :=
  fun _y hy => (continuousAt_extChartAt_symm'' hy).continuousWithinAt

lemma extChartAt_target_subset_closure_interior {x : M} :
    (extChartAt I x).target âŠ† closure (interior (extChartAt I x).target) := by
  intro y hy
  rw [mem_closure_iff_nhds]
  intro t ht
  have A : t âˆ© ((extChartAt I x).target âˆª (range I)á¶œ) âˆˆ ğ“ y :=
    inter_mem ht (extChartAt_target_union_compl_range_mem_nhds_of_mem hy)
  have B : y âˆˆ closure (interior (range I)) := by
    apply I.range_subset_closure_interior (extChartAt_target_subset_range x hy)
  obtain âŸ¨z, âŸ¨tz, h'zâŸ©, hzâŸ© :
      (t âˆ© ((extChartAt I x).target âˆª (range â†‘I)á¶œ) âˆ© interior (range I)).Nonempty :=
    mem_closure_iff_nhds.1 B _ A
  refine âŸ¨z, âŸ¨tz, ?_âŸ©âŸ©
  have h''z : z âˆˆ (extChartAt I x).target := by simpa [interior_subset hz] using h'z
  exact (extChartAt_target_eventuallyEq_of_mem h''z).symm.mem_interior hz

variable (I) in
theorem interior_extChartAt_target_nonempty (x : M) :
    (interior (extChartAt I x).target).Nonempty := by
  by_contra! H
  have := extChartAt_target_subset_closure_interior (mem_extChartAt_target (I := I) x)
  simp only [H, closure_empty, mem_empty_iff_false] at this

lemma extChartAt_mem_closure_interior {xâ‚€ x : M}
    (hx : x âˆˆ closure (interior s)) (h'x : x âˆˆ (extChartAt I xâ‚€).source) :
    extChartAt I xâ‚€ x âˆˆ
      closure (interior ((extChartAt I xâ‚€).symm â»Â¹' s âˆ© (extChartAt I xâ‚€).target)) := by
  simp_rw [mem_closure_iff, interior_inter, â† inter_assoc]
  intro o o_open ho
  obtain âŸ¨y, âŸ¨yo, hyâŸ©, ysâŸ© :
      ((extChartAt I xâ‚€) â»Â¹' o âˆ© (extChartAt I xâ‚€).source âˆ© interior s).Nonempty := by
    have : (extChartAt I xâ‚€) â»Â¹' o âˆˆ ğ“ x := by
      apply (continuousAt_extChartAt' h'x).preimage_mem_nhds (o_open.mem_nhds ho)
    refine (mem_closure_iff_nhds.1 hx) _ (inter_mem this ?_)
    apply (isOpen_extChartAt_source xâ‚€).mem_nhds h'x
  have A : interior (â†‘(extChartAt I xâ‚€).symm â»Â¹' s) âˆˆ ğ“ (extChartAt I xâ‚€ y) := by
    simp only [interior_mem_nhds]
    apply (continuousAt_extChartAt_symm' hy).preimage_mem_nhds
    simp only [hy, PartialEquiv.left_inv]
    exact mem_interior_iff_mem_nhds.mp ys
  have B : (extChartAt I xâ‚€) y âˆˆ closure (interior (extChartAt I xâ‚€).target) := by
    apply extChartAt_target_subset_closure_interior (x := xâ‚€)
    exact (extChartAt I xâ‚€).map_source hy
  exact mem_closure_iff_nhds.1 B _ (inter_mem (o_open.mem_nhds yo) A)

theorem isOpen_extChartAt_preimage' (x : M) {s : Set E} (hs : IsOpen s) :
    IsOpen ((extChartAt I x).source âˆ© extChartAt I x â»Â¹' s) :=
  isOpen_extend_preimage' _ hs

theorem isOpen_extChartAt_preimage (x : M) {s : Set E} (hs : IsOpen s) :
    IsOpen ((chartAt H x).source âˆ© extChartAt I x â»Â¹' s) := by
  rw [â† extChartAt_source I]
  exact isOpen_extChartAt_preimage' x hs

theorem map_extChartAt_nhdsWithin_eq_image' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    map (extChartAt I x) (ğ“[s] y) =
      ğ“[extChartAt I x '' ((extChartAt I x).source âˆ© s)] extChartAt I x y :=
  map_extend_nhdsWithin_eq_image _ <| by rwa [â† extChartAt_source I]

theorem map_extChartAt_nhdsWithin_eq_image (x : M) :
    map (extChartAt I x) (ğ“[s] x) =
      ğ“[extChartAt I x '' ((extChartAt I x).source âˆ© s)] extChartAt I x x :=
  map_extChartAt_nhdsWithin_eq_image' (mem_extChartAt_source x)

theorem map_extChartAt_nhdsWithin' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    map (extChartAt I x) (ğ“[s] y) = ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] extChartAt I x y :=
  map_extend_nhdsWithin _ <| by rwa [â† extChartAt_source I]

theorem map_extChartAt_nhdsWithin (x : M) :
    map (extChartAt I x) (ğ“[s] x) = ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] extChartAt I x x :=
  map_extChartAt_nhdsWithin' (mem_extChartAt_source x)

theorem map_extChartAt_symm_nhdsWithin' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    map (extChartAt I x).symm (ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] extChartAt I x y) =
      ğ“[s] y :=
  map_extend_symm_nhdsWithin _ <| by rwa [â† extChartAt_source I]

theorem map_extChartAt_symm_nhdsWithin_range' {x y : M} (hy : y âˆˆ (extChartAt I x).source) :
    map (extChartAt I x).symm (ğ“[range I] extChartAt I x y) = ğ“ y :=
  map_extend_symm_nhdsWithin_range _ <| by rwa [â† extChartAt_source I]

theorem map_extChartAt_symm_nhdsWithin (x : M) :
    map (extChartAt I x).symm (ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] extChartAt I x x) =
      ğ“[s] x :=
  map_extChartAt_symm_nhdsWithin' (mem_extChartAt_source x)

theorem map_extChartAt_symm_nhdsWithin_range (x : M) :
    map (extChartAt I x).symm (ğ“[range I] extChartAt I x x) = ğ“ x :=
  map_extChartAt_symm_nhdsWithin_range' (mem_extChartAt_source x)

theorem extChartAt_preimage_mem_nhds_of_mem_nhdsWithin {s : Set E} {x x' : M}
    (hx : x' âˆˆ (extChartAt I x).source)
    (hs : s âˆˆ ğ“[range I] (extChartAt I x x')) :
    (extChartAt I x) â»Â¹' s âˆˆ ğ“ x' :=
  extend_preimage_mem_nhds_of_mem_nhdsWithin _ (by simpa using hx) hs

/-- Technical lemma ensuring that the preimage under an extended chart of a neighborhood of a point
in the source is a neighborhood of the preimage, within a set. -/
theorem extChartAt_preimage_mem_nhdsWithin' {x x' : M} (h : x' âˆˆ (extChartAt I x).source)
    (ht : t âˆˆ ğ“[s] x') :
    (extChartAt I x).symm â»Â¹' t âˆˆ ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] (extChartAt I x) x' := by
  rwa [â† map_extChartAt_symm_nhdsWithin' h, mem_map] at ht

/-- Technical lemma ensuring that the preimage under an extended chart of a neighborhood of the
base point is a neighborhood of the preimage, within a set. -/
theorem extChartAt_preimage_mem_nhdsWithin {x : M} (ht : t âˆˆ ğ“[s] x) :
    (extChartAt I x).symm â»Â¹' t âˆˆ ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] (extChartAt I x) x :=
  extChartAt_preimage_mem_nhdsWithin' (mem_extChartAt_source x) ht

theorem extChartAt_preimage_mem_nhds' {x x' : M} (h : x' âˆˆ (extChartAt I x).source)
    (ht : t âˆˆ ğ“ x') : (extChartAt I x).symm â»Â¹' t âˆˆ ğ“ (extChartAt I x x') :=
  extend_preimage_mem_nhds _ (by rwa [â† extChartAt_source I]) ht

/-- Technical lemma ensuring that the preimage under an extended chart of a neighborhood of a point
is a neighborhood of the preimage. -/
theorem extChartAt_preimage_mem_nhds {x : M} (ht : t âˆˆ ğ“ x) :
    (extChartAt I x).symm â»Â¹' t âˆˆ ğ“ ((extChartAt I x) x) := by
  apply (continuousAt_extChartAt_symm x).preimage_mem_nhds
  rwa [(extChartAt I x).left_inv (mem_extChartAt_source _)]

/-- Technical lemma to rewrite suitably the preimage of an intersection under an extended chart, to
bring it into a convenient form to apply derivative lemmas. -/
theorem extChartAt_preimage_inter_eq (x : M) :
    (extChartAt I x).symm â»Â¹' (s âˆ© t) âˆ© range I =
      (extChartAt I x).symm â»Â¹' s âˆ© range I âˆ© (extChartAt I x).symm â»Â¹' t := by
  mfld_set_tac

theorem ContinuousWithinAt.nhdsWithin_extChartAt_symm_preimage_inter_range
    {f : M â†’ M'} {x : M} (hc : ContinuousWithinAt f s x) :
    ğ“[(extChartAt I x).symm â»Â¹' s âˆ© range I] (extChartAt I x x) =
      ğ“[(extChartAt I x).target âˆ©
        (extChartAt I x).symm â»Â¹' (s âˆ© f â»Â¹' (extChartAt I' (f x)).source)] (extChartAt I x x) := by
  rw [â† (extChartAt I x).image_source_inter_eq', â† map_extChartAt_nhdsWithin_eq_image,
    â† map_extChartAt_nhdsWithin, nhdsWithin_inter_of_mem']
  exact hc (extChartAt_source_mem_nhds _)

theorem ContinuousWithinAt.extChartAt_symm_preimage_inter_range_eventuallyEq
    {f : M â†’ M'} {x : M} (hc : ContinuousWithinAt f s x) :
    ((extChartAt I x).symm â»Â¹' s âˆ© range I : Set E) =á¶ [ğ“ (extChartAt I x x)]
      ((extChartAt I x).target âˆ©
        (extChartAt I x).symm â»Â¹' (s âˆ© f â»Â¹' (extChartAt I' (f x)).source) : Set E) := by
  rw [â† nhdsWithin_eq_iff_eventuallyEq]
  exact hc.nhdsWithin_extChartAt_symm_preimage_inter_range

/-! We use the name `ext_coord_change` for `(extChartAt I x').symm â‰« extChartAt I x`. -/

theorem ext_coord_change_source (x x' : M) :
    ((extChartAt I x').symm â‰« extChartAt I x).source =
      I '' ((chartAt H x').symm â‰«â‚• chartAt H x).source :=
  extend_coord_change_source _ _

open IsManifold

theorem contDiffOn_ext_coord_change [IsManifold I n M] (x x' : M) :
    ContDiffOn ğ•œ n (extChartAt I x âˆ˜ (extChartAt I x').symm)
      ((extChartAt I x').symm â‰« extChartAt I x).source :=
  contDiffOn_extend_coord_change (chart_mem_maximalAtlas x) (chart_mem_maximalAtlas x')

theorem contDiffWithinAt_ext_coord_change [IsManifold I n M] (x x' : M) {y : E}
    (hy : y âˆˆ ((extChartAt I x').symm â‰« extChartAt I x).source) :
    ContDiffWithinAt ğ•œ n (extChartAt I x âˆ˜ (extChartAt I x').symm) (range I) y :=
  contDiffWithinAt_extend_coord_change (chart_mem_maximalAtlas x) (chart_mem_maximalAtlas x') hy

variable (I I') in
/-- Conjugating a function to write it in the preferred charts around `x`.
The manifold derivative of `f` will just be the derivative of this conjugated function. -/
@[simp, mfld_simps]
def writtenInExtChartAt (x : M) (f : M â†’ M') : E â†’ E' :=
  extChartAt I' (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm

theorem writtenInExtChartAt_chartAt {x : M} {y : E} (h : y âˆˆ (extChartAt I x).target) :
    writtenInExtChartAt I I x (chartAt H x) y = y := by simp_all only [mfld_simps]

theorem writtenInExtChartAt_chartAt_symm {x : M} {y : E} (h : y âˆˆ (extChartAt I x).target) :
    writtenInExtChartAt I I (chartAt H x x) (chartAt H x).symm y = y := by
  simp_all only [mfld_simps]

theorem writtenInExtChartAt_extChartAt {x : M} {y : E} (h : y âˆˆ (extChartAt I x).target) :
    writtenInExtChartAt I ğ“˜(ğ•œ, E) x (extChartAt I x) y = y := by
  simp_all only [mfld_simps]

theorem writtenInExtChartAt_extChartAt_symm {x : M} {y : E} (h : y âˆˆ (extChartAt I x).target) :
    writtenInExtChartAt ğ“˜(ğ•œ, E) I (extChartAt I x x) (extChartAt I x).symm y = y := by
  simp_all only [mfld_simps]

variable (ğ•œ)

theorem extChartAt_self_eq {x : H} : â‡‘(extChartAt I x) = I :=
  rfl

theorem extChartAt_self_apply {x y : H} : extChartAt I x y = I y :=
  rfl

/-- In the case of the manifold structure on a vector space, the extended charts are just the
identity. -/
theorem extChartAt_model_space_eq_id (x : E) : extChartAt ğ“˜(ğ•œ, E) x = PartialEquiv.refl E := by
  simp only [mfld_simps]

theorem ext_chart_model_space_apply {x y : E} : extChartAt ğ“˜(ğ•œ, E) x y = y :=
  rfl

variable {ğ•œ}

theorem extChartAt_prod (x : M Ã— M') :
    extChartAt (I.prod I') x = (extChartAt I x.1).prod (extChartAt I' x.2) := by
  simp only [mfld_simps]
  rw [PartialEquiv.prod_trans]

theorem extChartAt_comp [ChartedSpace H H'] (x : M') :
    (letI := ChartedSpace.comp H H' M'; extChartAt I x) =
      (chartAt H' x).toPartialEquiv â‰« extChartAt I (chartAt H' x x) :=
  PartialEquiv.trans_assoc ..

theorem writtenInExtChartAt_chartAt_comp [ChartedSpace H H'] (x : M') {y}
    (hy : y âˆˆ letI := ChartedSpace.comp H H' M'; (extChartAt I x).target) :
    (letI := ChartedSpace.comp H H' M'; writtenInExtChartAt I I x (chartAt H' x) y) = y := by
  letI := ChartedSpace.comp H H' M'
  simp_all only [mfld_simps, chartAt_comp]

theorem writtenInExtChartAt_chartAt_symm_comp [ChartedSpace H H'] (x : M') {y}
    (hy : y âˆˆ letI := ChartedSpace.comp H H' M'; (extChartAt I x).target) :
    ( letI := ChartedSpace.comp H H' M'
      writtenInExtChartAt I I (chartAt H' x x) (chartAt H' x).symm y) = y := by
  letI := ChartedSpace.comp H H' M'
  simp_all only [mfld_simps, chartAt_comp]

end ExtendedCharts

section Topology

-- Let `M` be a topological manifold over the field ğ•œ.
variable
  {E : Type*} {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

/-- A finite-dimensional manifold modelled on a locally compact field
(such as â„, â„‚ or the `p`-adic numbers) is locally compact. -/
lemma Manifold.locallyCompact_of_finiteDimensional
    (I : ModelWithCorners ğ•œ E H) [LocallyCompactSpace ğ•œ] [FiniteDimensional ğ•œ E] :
    LocallyCompactSpace M := by
  have : ProperSpace E := FiniteDimensional.proper ğ•œ E
  have : LocallyCompactSpace H := I.locallyCompactSpace
  exact ChartedSpace.locallyCompactSpace H M

variable (M)

/-- A locally compact manifold must be modelled on a locally compact space. -/
lemma LocallyCompactSpace.of_locallyCompact_manifold (I : ModelWithCorners ğ•œ E H)
    [h : Nonempty M] [LocallyCompactSpace M] :
    LocallyCompactSpace E := by
  rcases h with âŸ¨xâŸ©
  obtain âŸ¨y, hyâŸ© := interior_extChartAt_target_nonempty I x
  have h'y : y âˆˆ (extChartAt I x).target := interior_subset hy
  obtain âŸ¨s, hmem, hss, hcomâŸ© :=
    LocallyCompactSpace.local_compact_nhds ((extChartAt I x).symm y) (extChartAt I x).source
      ((isOpen_extChartAt_source x).mem_nhds ((extChartAt I x).map_target h'y))
  have : IsCompact <| (extChartAt I x) '' s :=
    hcom.image_of_continuousOn <| (continuousOn_extChartAt x).mono hss
  apply this.locallyCompactSpace_of_mem_nhds_of_addGroup (x := y)
  rw [â† (extChartAt I x).right_inv h'y]
  apply extChartAt_image_nhds_mem_nhds_of_mem_interior_range
    (PartialEquiv.map_target (extChartAt I x) h'y) _ hmem
  simp only [(extChartAt I x).right_inv h'y]
  exact interior_mono (extChartAt_target_subset_range x) hy

/-- Riesz's theorem applied to manifolds: a locally compact manifolds must be modelled on a
finite-dimensional space. This is the converse to `Manifold.locallyCompact_of_finiteDimensional`. -/
theorem FiniteDimensional.of_locallyCompact_manifold
    [CompleteSpace ğ•œ] (I : ModelWithCorners ğ•œ E H) [Nonempty M] [LocallyCompactSpace M] :
    FiniteDimensional ğ•œ E := by
  have := LocallyCompactSpace.of_locallyCompact_manifold M I
  exact FiniteDimensional.of_locallyCompactSpace ğ•œ

end Topology
