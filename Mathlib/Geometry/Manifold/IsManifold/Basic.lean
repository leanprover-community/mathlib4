/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.Analysis.RCLike.TangentCone
import Mathlib.Data.Bundle
import Mathlib.Geometry.Manifold.ChartedSpace

/-!
# `C^n` manifolds (possibly with boundary or corners)

A `C^n` manifold is a manifold modelled on a normed vector space, or a subset like a
half-space (to get manifolds with boundaries) for which the changes of coordinates are `C^n` maps.
We define a model with corners as a map `I : H â†’ E` embedding nicely the topological space `H` in
the vector space `E` (or more precisely as a structure containing all the relevant properties).
Given such a model with corners `I` on `(E, H)`, we define the groupoid of local
homeomorphisms of `H` which are `C^n` when read in `E` (for any regularity `n : WithTop â„•âˆ`).
With this groupoid at hand and the general machinery of charted spaces, we thus get the notion
of `C^n` manifold with respect to any model with corners `I` on `(E, H)`.

Some texts assume manifolds to be Hausdorff and second countable. We (in mathlib) assume neither,
but add these assumptions later as needed. (Quite a few results still do not require them.)

## Main definitions

* `ModelWithCorners ğ•œ E H` :
  a structure containing information on the way a space `H` embeds in a
  model vector space E over the field `ğ•œ`. This is all that is needed to
  define a `C^n` manifold with model space `H`, and model vector space `E`.
* `modelWithCornersSelf ğ•œ E` :
  trivial model with corners structure on the space `E` embedded in itself by the identity.
* `contDiffGroupoid n I` :
  when `I` is a model with corners on `(ğ•œ, E, H)`, this is the groupoid of partial homeos of `H`
  which are of class `C^n` over the normed field `ğ•œ`, when read in `E`.
* `IsManifold I n M` :
  a type class saying that the charted space `M`, modelled on the space `H`, has `C^n` changes of
  coordinates with respect to the model with corners `I` on `(ğ•œ, E, H)`. This type class is just
  a shortcut for `HasGroupoid M (contDiffGroupoid n I)`.

We define a few constructions of smooth manifolds:
* every empty type is a smooth manifold
* `IsManifold.of_discreteTopology`: a discrete space is a smooth manifold
  (over the trivial model with corners on the trivial space)
* the product of two smooth manifolds
* the disjoint union of two manifolds (over the same charted space)

As specific examples of models with corners, we define (in `Geometry.Manifold.Instances.Real`)
* `modelWithCornersEuclideanHalfSpace n :
  ModelWithCorners â„ (EuclideanSpace â„ (Fin n)) (EuclideanHalfSpace n)` for the model space used to
  define `n`-dimensional real manifolds without boundary
  (with notation `ğ“¡ n` in the scope `Manifold`)
* `modelWithCornersEuclideanHalfSpace n :
  ModelWithCorners â„ (EuclideanSpace â„ (Fin n)) (EuclideanHalfSpace n)` for the model space
  used to define `n`-dimensional real manifolds with boundary (with notation `ğ“¡âˆ‚ n` in the locale
  `Manifold`)
* `modelWithCornersEuclideanQuadrant n :
  ModelWithCorners â„ (EuclideanSpace â„ (Fin n)) (EuclideanQuadrant n)` for the model space used
  to define `n`-dimensional real manifolds with corners

With these definitions at hand, to invoke an `n`-dimensional `C^âˆ` real manifold without boundary,
one could use

  `variable {n : â„•} {M : Type*} [TopologicalSpace M] [ChartedSpace (EuclideanSpace â„ (Fin n)) M]
   [IsManifold (ğ“¡ n) âˆ M]`.

However, this is not the recommended way: a theorem proved using this assumption would not apply
for instance to the tangent space of such a manifold, which is modelled on
`(EuclideanSpace â„ (Fin n)) Ã— (EuclideanSpace â„ (Fin n))`
and not on `EuclideanSpace â„ (Fin (2 * n))`!
In the same way, it would not apply to product manifolds, modelled on
`(EuclideanSpace â„ (Fin n)) Ã— (EuclideanSpace â„ (Fin m))`.
The right invocation does not focus on one specific construction, but on all constructions sharing
the right properties, like

  `variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  {I : ModelWithCorners â„ E E} [I.Boundaryless]
  {M : Type*} [TopologicalSpace M] [ChartedSpace E M] [IsManifold I âˆ M]`

Here, `I.Boundaryless` is a typeclass property ensuring that there is no boundary (this is for
instance the case for `modelWithCornersSelf`, or products of these). Note that one could consider
as a natural assumption to only use the trivial model with corners `modelWithCornersSelf â„ E`,
but again in product manifolds the natural model with corners will not be this one but the product
one (and they are not defeq as `(fun p : E Ã— F â†¦ (p.1, p.2))` is not defeq to the identity).
So, it is important to use the above incantation to maximize the applicability of theorems.

Even better, if the result should apply in a parallel way to smooth manifolds and to analytic
manifolds, the last typeclass should be replaced with `[IsManifold I n M]`
for `n : WithTop â„•âˆ`.

We also define `TangentSpace I (x : M)` as a type synonym of `E`, and `TangentBundle I M` as a
type synonym for `Î  (x : M), TangentSpace I x` (in the form of an
abbrev of `Bundle.TotalSpace E (TangentSpace I : M â†’ Type _)`). Apart from basic typeclasses on
`TangentSpace I x`, nothing is proved about them in this file, but it is useful to have them
available as definitions early on to get a clean import structure below. The smooth bundle structure
is defined in `VectorBundle.Tangent`, while the definition is used to talk about manifold
derivatives in `MFDeriv.Basic`, and neither file needs import the other.

## Implementation notes

We want to talk about manifolds modelled on a vector space, but also on manifolds with
boundary, modelled on a half space (or even manifolds with corners). For the latter examples,
we still want to define smooth functions, tangent bundles, and so on. As smooth functions are
well defined on vector spaces or subsets of these, one could take for model space a subtype of a
vector space. With the drawback that the whole vector space itself (which is the most basic
example) is not directly a subtype of itself: the inclusion of `univ : Set E` in `Set E` would
show up in the definition, instead of `id`.

A good abstraction covering both cases it to have a vector
space `E` (with basic example the Euclidean space), a model space `H` (with basic example the upper
half space), and an embedding of `H` into `E` (which can be the identity for `H = E`, or
`Subtype.val` for manifolds with corners). We say that the pair `(E, H)` with their embedding is a
model with corners, and we encompass all the relevant properties (in particular the fact that the
image of `H` in `E` should have unique differentials) in the definition of `ModelWithCorners`.

I have considered using the model with corners `I` as a typeclass argument, possibly `outParam`, to
get lighter notations later on, but it did not turn out right, as on `E Ã— F` there are two natural
model with corners, the trivial (identity) one, and the product one, and they are not defeq and one
needs to indicate to Lean which one we want to use.
This means that when talking on objects on manifolds one will most often need to specify the model
with corners one is using. For instance, the tangent bundle will be `TangentBundle I M` and the
derivative will be `mfderiv I I' f`, instead of the more natural notations `TangentBundle ğ•œ M` and
`mfderiv ğ•œ f` (the field has to be explicit anyway, as some manifolds could be considered both as
real and complex manifolds).
-/

open Topology

noncomputable section

universe u v w u' v' w'

namespace PartialEquiv

/- This lemma is here in this file, because in `PartialEquiv.basic` it would
have required to import some topology, and it did not look right. -/
@[fun_prop]
lemma Continuous.invFun {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
    (e : PartialEquiv Î± Î²) (he : Continuous e.symm) : Continuous e.invFun := he

end PartialEquiv

open Set Filter Function PartialEquiv

open scoped Manifold Topology ContDiff

/-! ### Models with corners. -/

open scoped Classical in
/-- A structure containing information on the way a space `H` embeds in a
model vector space `E` over the field `ğ•œ`. This is all what is needed to
define a `C^n` manifold with model space `H`, and model vector space `E`.

We require that, when the field is `â„` or `â„‚`, the range is `â„`-convex, as this is what is needed
to do calculus and covers the standard examples of manifolds with boundary. Over other fields,
we require that the range is `univ`, as there is no relevant notion of manifold with boundary there.
-/
@[ext]
structure ModelWithCorners (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*)
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] (H : Type*) [TopologicalSpace H] extends
    PartialEquiv H E where
  source_eq : source = univ
  /-- To check this condition when the space already has a real normed space structure,
  use `Convex.convex_isRCLikeNormedField` which eliminates the `letI`s below, or the constructor
  `ModelWithCorners.of_convex_range` -/
  convex_range' :
    if h : IsRCLikeNormedField ğ•œ then
      letI := h.rclike ğ•œ
      letI : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ E
      Convex â„ (range toPartialEquiv)
    else range toPartialEquiv = univ
  nonempty_interior' : (interior (range toPartialEquiv)).Nonempty
  continuous_toFun : Continuous toFun := by fun_prop
  continuous_invFun : Continuous invFun := by fun_prop

lemma ModelWithCorners.range_eq_target {ğ•œ E H : Type*} [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) :
    range I.toPartialEquiv = I.target := by
  rw [â† I.image_source_eq_target, I.source_eq, image_univ.symm]

/-- If a model with corners has full range, the `convex_range'` condition is satisfied. -/
def ModelWithCorners.of_target_univ (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (Ï† : PartialEquiv H E) (hsource : Ï†.source = univ) (htarget : Ï†.target = univ)
    (hcont : Continuous Ï†) (hcont_inv : Continuous Ï†.symm) : ModelWithCorners ğ•œ E H where
  toPartialEquiv := Ï†
  source_eq := hsource
  convex_range' := by
    have : range Ï† = Ï†.target := by rw [â† Ï†.image_source_eq_target, hsource, image_univ.symm]
    simp only [this, htarget, dite_else_true]
    intro h
    letI := h.rclike ğ•œ
    letI := NormedSpace.restrictScalars â„ ğ•œ E
    exact convex_univ
  nonempty_interior' := by
    have : range Ï† = Ï†.target := by rw [â† Ï†.image_source_eq_target, hsource, image_univ.symm]
    simp [this, htarget]

attribute [simp, mfld_simps] ModelWithCorners.source_eq

/-- A vector space is a model with corners, denoted as `ğ“˜(ğ•œ, E)` within the `Manifold` namespace. -/
def modelWithCornersSelf (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*)
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] : ModelWithCorners ğ•œ E E :=
  ModelWithCorners.of_target_univ ğ•œ (PartialEquiv.refl E) rfl rfl continuous_id continuous_id

@[inherit_doc] scoped[Manifold] notation "ğ“˜(" ğ•œ ", " E ")" => modelWithCornersSelf ğ•œ E

/-- A normed field is a model with corners. -/
scoped[Manifold] notation "ğ“˜(" ğ•œ ")" => modelWithCornersSelf ğ•œ ğ•œ

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)

namespace ModelWithCorners

/-- Coercion of a model with corners to a function. We don't use `e.toFun` because it is actually
`e.toPartialEquiv.toFun`, so `simp` will apply lemmas about `toPartialEquiv`. While we may want to
switch to this behavior later, doing it mid-port will break a lot of proofs. -/
@[coe] def toFun' (e : ModelWithCorners ğ•œ E H) : H â†’ E := e.toFun

instance : CoeFun (ModelWithCorners ğ•œ E H) fun _ => H â†’ E := âŸ¨toFun'âŸ©

/-- The inverse to a model with corners, only registered as a `PartialEquiv`. -/
protected def symm : PartialEquiv E H :=
  I.toPartialEquiv.symm

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
because it is a composition of multiple projections. -/
def Simps.apply (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*) [NormedAddCommGroup E]
    [NormedSpace ğ•œ E] (H : Type*) [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) : H â†’ E :=
  I

/-- See Note [custom simps projection] -/
def Simps.symm_apply (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*) [NormedAddCommGroup E]
    [NormedSpace ğ•œ E] (H : Type*) [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) : E â†’ H :=
  I.symm

initialize_simps_projections ModelWithCorners (toFun â†’ apply, invFun â†’ symm_apply)

-- Register a few lemmas to make sure that `simp` puts expressions in normal form
@[simp, mfld_simps]
theorem toPartialEquiv_coe : (I.toPartialEquiv : H â†’ E) = I :=
  rfl

@[simp, mfld_simps]
theorem mk_coe (e : PartialEquiv H E) (a b c d d') :
    ((ModelWithCorners.mk e a b c d d' : ModelWithCorners ğ•œ E H) : H â†’ E) = (e : H â†’ E) :=
  rfl

@[simp, mfld_simps]
theorem toPartialEquiv_coe_symm : (I.toPartialEquiv.symm : E â†’ H) = I.symm :=
  rfl

@[simp, mfld_simps]
theorem mk_symm (e : PartialEquiv H E) (a b c d d') :
    (ModelWithCorners.mk e a b c d d' : ModelWithCorners ğ•œ E H).symm = e.symm :=
  rfl

@[fun_prop]
protected theorem continuous : Continuous I :=
  I.continuous_toFun

protected theorem continuousAt {x} : ContinuousAt I x :=
  I.continuous.continuousAt

protected theorem continuousWithinAt {s x} : ContinuousWithinAt I s x :=
  I.continuousAt.continuousWithinAt

@[fun_prop]
theorem continuous_symm : Continuous I.symm :=
  I.continuous_invFun

theorem continuousAt_symm {x} : ContinuousAt I.symm x :=
  I.continuous_symm.continuousAt

theorem continuousWithinAt_symm {s x} : ContinuousWithinAt I.symm s x :=
  I.continuous_symm.continuousWithinAt

theorem continuousOn_symm {s} : ContinuousOn I.symm s :=
  I.continuous_symm.continuousOn

@[simp, mfld_simps]
theorem target_eq : I.target = range (I : H â†’ E) := by
  rw [â† image_univ, â† I.source_eq]
  exact I.image_source_eq_target.symm

theorem nonempty_interior : (interior (range I)).Nonempty :=
  I.nonempty_interior'

theorem range_eq_univ_of_not_isRCLikeNormedField (h : Â¬ IsRCLikeNormedField ğ•œ) :
    range I = univ := by
  simpa [h] using I.convex_range'

/-- If a set is `â„`-convex for some normed space structure, then it is `â„`-convex for the
normed space structure coming from an `IsRCLikeNormedField ğ•œ`. Useful when constructing model
spaces to avoid diamond issues when populating the field `convex_range'`. -/
lemma _root_.Convex.convex_isRCLikeNormedField [NormedSpace â„ E] [h : IsRCLikeNormedField ğ•œ]
    {s : Set E} (hs : Convex â„ s) :
    letI := h.rclike
    letI := NormedSpace.restrictScalars â„ ğ•œ E
    Convex â„ s := by
  letI := h.rclike
  letI := NormedSpace.restrictScalars â„ ğ•œ E
  simp only [Convex, StarConvex] at hs âŠ¢
  intro u hu v hv a b ha hb hab
  convert hs hu hv ha hb hab using 2
  Â· rw [â† @algebraMap_smul (R := â„) (A := ğ•œ), â† @algebraMap_smul (R := â„) (A := ğ•œ)]
  Â· rw [â† @algebraMap_smul (R := â„) (A := ğ•œ), â† @algebraMap_smul (R := â„) (A := ğ•œ)]

/-- Construct a model with corners over `â„` from a continuous partial equiv with convex range. -/
def of_convex_range
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {H : Type*} [TopologicalSpace H]
    (Ï† : PartialEquiv H E) (hsource : Ï†.source = univ) (htarget : Convex â„ Ï†.target)
    (hcont : Continuous Ï†) (hcont_inv : Continuous Ï†.symm) (hint : (interior Ï†.target).Nonempty) :
    ModelWithCorners â„ E H where
  toPartialEquiv := Ï†
  source_eq := hsource
  convex_range' := by
    have : range Ï† = Ï†.target := by rw [â† Ï†.image_source_eq_target, hsource, image_univ.symm]
    simp only [instIsRCLikeNormedField, â†“reduceDIte, this]
    exact htarget.convex_isRCLikeNormedField
  nonempty_interior' := by
    have : range Ï† = Ï†.target := by rw [â† Ï†.image_source_eq_target, hsource, image_univ.symm]
    simp [this, hint]

theorem convex_range [NormedSpace â„ E] : Convex â„ (range I) := by
  by_cases h : IsRCLikeNormedField ğ•œ
  Â· letI : RCLike ğ•œ := h.rclike
    have W := I.convex_range'
    simp only [h, â†“reduceDIte, toPartialEquiv_coe] at W
    simp only [Convex, StarConvex] at W âŠ¢
    intro u hu v hv a b ha hb hab
    convert W hu hv ha hb hab using 2
    Â· rw [â† @algebraMap_smul (R := â„) (A := ğ•œ)]
      rfl
    Â· rw [â† @algebraMap_smul (R := â„) (A := ğ•œ)]
      rfl
  Â· simp [range_eq_univ_of_not_isRCLikeNormedField I h, convex_univ]

protected theorem uniqueDiffOn : UniqueDiffOn ğ•œ (range I) := by
  by_cases h : IsRCLikeNormedField ğ•œ
  Â· letI := h.rclike ğ•œ
    letI := NormedSpace.restrictScalars â„ ğ•œ E
    apply uniqueDiffOn_convex_of_isRCLikeNormedField _ I.nonempty_interior
    simpa [h] using I.convex_range
  Â· simp [range_eq_univ_of_not_isRCLikeNormedField I h, uniqueDiffOn_univ]

theorem range_subset_closure_interior : range I âŠ† closure (interior (range I)) := by
  by_cases h : IsRCLikeNormedField ğ•œ
  Â· letI := h.rclike ğ•œ
    letI := NormedSpace.restrictScalars â„ ğ•œ E
    rw [Convex.closure_interior_eq_closure_of_nonempty_interior (ğ•œ := â„)]
    Â· apply subset_closure
    Â· apply I.convex_range
    Â· apply I.nonempty_interior
  Â· simp [range_eq_univ_of_not_isRCLikeNormedField I h]

@[simp, mfld_simps]
protected theorem left_inv (x : H) : I.symm (I x) = x := by refine I.left_inv' ?_; simp

protected theorem leftInverse : LeftInverse I.symm I :=
  I.left_inv

theorem injective : Injective I :=
  I.leftInverse.injective

@[simp, mfld_simps]
theorem symm_comp_self : I.symm âˆ˜ I = id :=
  I.leftInverse.comp_eq_id

protected theorem rightInvOn : RightInvOn I.symm I (range I) :=
  I.leftInverse.rightInvOn_range

@[simp, mfld_simps]
protected theorem right_inv {x : E} (hx : x âˆˆ range I) : I (I.symm x) = x :=
  I.rightInvOn hx

theorem preimage_image (s : Set H) : I â»Â¹' (I '' s) = s :=
  I.injective.preimage_image s

protected theorem image_eq (s : Set H) : I '' s = I.symm â»Â¹' s âˆ© range I := by
  refine (I.toPartialEquiv.image_eq_target_inter_inv_preimage ?_).trans ?_
  Â· rw [I.source_eq]; exact subset_univ _
  Â· rw [inter_comm, I.target_eq, I.toPartialEquiv_coe_symm]

theorem isClosedEmbedding : IsClosedEmbedding I :=
  I.leftInverse.isClosedEmbedding I.continuous_symm I.continuous

theorem isClosed_range : IsClosed (range I) :=
  I.isClosedEmbedding.isClosed_range


theorem range_eq_closure_interior : range I = closure (interior (range I)) :=
  Subset.antisymm I.range_subset_closure_interior I.isClosed_range.closure_interior_subset

theorem map_nhds_eq (x : H) : map I (ğ“ x) = ğ“[range I] I x :=
  I.isClosedEmbedding.isEmbedding.map_nhds_eq x

theorem map_nhdsWithin_eq (s : Set H) (x : H) : map I (ğ“[s] x) = ğ“[I '' s] I x :=
  I.isClosedEmbedding.isEmbedding.map_nhdsWithin_eq s x

theorem image_mem_nhdsWithin {x : H} {s : Set H} (hs : s âˆˆ ğ“ x) : I '' s âˆˆ ğ“[range I] I x :=
  I.map_nhds_eq x â–¸ image_mem_map hs

theorem symm_map_nhdsWithin_image {x : H} {s : Set H} : map I.symm (ğ“[I '' s] I x) = ğ“[s] x := by
  rw [â† I.map_nhdsWithin_eq, map_map, I.symm_comp_self, map_id]

theorem symm_map_nhdsWithin_range (x : H) : map I.symm (ğ“[range I] I x) = ğ“ x := by
  rw [â† I.map_nhds_eq, map_map, I.symm_comp_self, map_id]

theorem uniqueDiffOn_preimage {s : Set H} (hs : IsOpen s) :
    UniqueDiffOn ğ•œ (I.symm â»Â¹' s âˆ© range I) := by
  rw [inter_comm]
  exact I.uniqueDiffOn.inter (hs.preimage I.continuous_invFun)

theorem uniqueDiffOn_preimage_source {Î² : Type*} [TopologicalSpace Î²]
    {e : OpenPartialHomeomorph H Î²} : UniqueDiffOn ğ•œ (I.symm â»Â¹' e.source âˆ© range I) :=
  I.uniqueDiffOn_preimage e.open_source

theorem uniqueDiffWithinAt_image {x : H} : UniqueDiffWithinAt ğ•œ (range I) (I x) :=
  I.uniqueDiffOn _ (mem_range_self _)

theorem symm_continuousWithinAt_comp_right_iff {X} [TopologicalSpace X] {f : H â†’ X} {s : Set H}
    {x : H} :
    ContinuousWithinAt (f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) (I x) â†” ContinuousWithinAt f s x := by
  refine âŸ¨fun h => ?_, fun h => ?_âŸ©
  Â· have := h.comp I.continuousWithinAt (mapsTo_preimage _ _)
    simp_rw [preimage_inter, preimage_preimage, I.left_inv, preimage_id', preimage_range,
      inter_univ] at this
    rwa [Function.comp_assoc, I.symm_comp_self] at this
  Â· rw [â† I.left_inv x] at h; exact h.comp I.continuousWithinAt_symm inter_subset_left

protected theorem locallyCompactSpace [LocallyCompactSpace E] (I : ModelWithCorners ğ•œ E H) :
    LocallyCompactSpace H := by
  have : âˆ€ x : H, (ğ“ x).HasBasis (fun s => s âˆˆ ğ“ (I x) âˆ§ IsCompact s)
      fun s => I.symm '' (s âˆ© range I) := fun x â†¦ by
    rw [â† I.symm_map_nhdsWithin_range]
    exact ((compact_basis_nhds (I x)).inf_principal _).map _
  refine .of_hasBasis this ?_
  rintro x s âŸ¨-, hscâŸ©
  exact (hsc.inter_right I.isClosed_range).image I.continuous_symm

open TopologicalSpace

protected theorem secondCountableTopology [SecondCountableTopology E] (I : ModelWithCorners ğ•œ E H) :
    SecondCountableTopology H :=
  I.isClosedEmbedding.isEmbedding.secondCountableTopology

include I in
/-- Every manifold is a FrÃ©chet space (T1 space) -- regardless of whether it is
Hausdorff. -/
protected theorem t1Space (M : Type*) [TopologicalSpace M] [ChartedSpace H M] : T1Space M := by
  have : T2Space H := I.isClosedEmbedding.toIsEmbedding.t2Space
  exact ChartedSpace.t1Space H M

end ModelWithCorners

section

variable (ğ•œ E)

/-- In the trivial model with corners, the associated `PartialEquiv` is the identity. -/
@[simp, mfld_simps]
theorem modelWithCornersSelf_partialEquiv : ğ“˜(ğ•œ, E).toPartialEquiv = PartialEquiv.refl E :=
  rfl

@[simp, mfld_simps]
theorem modelWithCornersSelf_coe : (ğ“˜(ğ•œ, E) : E â†’ E) = id :=
  rfl

@[simp, mfld_simps]
theorem modelWithCornersSelf_coe_symm : (ğ“˜(ğ•œ, E).symm : E â†’ E) = id :=
  rfl

end

end

section ModelWithCornersProd

/-- Given two model_with_corners `I` on `(E, H)` and `I'` on `(E', H')`, we define the model with
corners `I.prod I'` on `(E Ã— E', ModelProd H H')`. This appears in particular for the manifold
structure on the tangent bundle to a manifold modelled on `(E, H)`: it will be modelled on
`(E Ã— E, H Ã— E)`. See note [Manifold type tags] for explanation about `ModelProd H H'`
vs `H Ã— H'`. -/
@[simps -isSimp]
def ModelWithCorners.prod {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type v}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type w} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) {E' : Type v'} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
    {H' : Type w'} [TopologicalSpace H'] (I' : ModelWithCorners ğ•œ E' H') :
    ModelWithCorners ğ•œ (E Ã— E') (ModelProd H H') :=
  { I.toPartialEquiv.prod I'.toPartialEquiv with
    toFun := fun x => (I x.1, I' x.2)
    invFun := fun x => (I.symm x.1, I'.symm x.2)
    source := { x | x.1 âˆˆ I.source âˆ§ x.2 âˆˆ I'.source }
    source_eq := by simp only [setOf_true, mfld_simps]
    convex_range' := by
      have : range (fun (x : ModelProd H H') â†¦ (I x.1, I' x.2)) = range (Prod.map I I') := rfl
      rw [this, Set.range_prodMap]
      split_ifs with h
      Â· letI := h.rclike
        letI := NormedSpace.restrictScalars â„ ğ•œ E; letI := NormedSpace.restrictScalars â„ ğ•œ E'
        exact I.convex_range.prod I'.convex_range
      Â· simp [range_eq_univ_of_not_isRCLikeNormedField, h]
    nonempty_interior' := by
      have : range (fun (x : ModelProd H H') â†¦ (I x.1, I' x.2)) = range (Prod.map I I') := rfl
      simp [this, interior_prod_eq, nonempty_interior]
    continuous_toFun := I.continuous_toFun.prodMap I'.continuous_toFun
    continuous_invFun := I.continuous_invFun.prodMap I'.continuous_invFun }

/-- Given a finite family of `ModelWithCorners` `I i` on `(E i, H i)`, we define the model with
corners `pi I` on `(Î  i, E i, ModelPi H)`. See note [Manifold type tags] for explanation about
`ModelPi H`. -/
def ModelWithCorners.pi {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {Î¹ : Type v} [Fintype Î¹]
    {E : Î¹ â†’ Type w} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)] {H : Î¹ â†’ Type u'}
    [âˆ€ i, TopologicalSpace (H i)] (I : âˆ€ i, ModelWithCorners ğ•œ (E i) (H i)) :
    ModelWithCorners ğ•œ (âˆ€ i, E i) (ModelPi H) where
  toPartialEquiv := PartialEquiv.pi fun i => (I i).toPartialEquiv
  source_eq := by simp only [pi_univ, mfld_simps]
  convex_range' := by
    rw [PartialEquiv.pi_apply, Set.range_piMap]
    split_ifs with h
    Â· letI := h.rclike
      letI := fun i â†¦ NormedSpace.restrictScalars â„ ğ•œ (E i)
      exact convex_pi fun i _hi â†¦ (I i).convex_range
    Â· simp [range_eq_univ_of_not_isRCLikeNormedField, h]
  nonempty_interior' := by
    rw [PartialEquiv.pi_apply, Set.range_piMap]
    simp [interior_pi_set finite_univ, univ_pi_nonempty_iff, nonempty_interior]
  continuous_toFun := continuous_pi fun i => (I i).continuous.comp (continuous_apply i)
  continuous_invFun := continuous_pi fun i => (I i).continuous_symm.comp (continuous_apply i)

/-- Special case of product model with corners, which is trivial on the second factor. This shows up
as the model to tangent bundles. -/
abbrev ModelWithCorners.tangent {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type v}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type w} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) : ModelWithCorners ğ•œ (E Ã— E) (ModelProd H E) :=
  I.prod ğ“˜(ğ•œ, E)

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {H : Type*} [TopologicalSpace H] {H' : Type*} [TopologicalSpace H'] {G : Type*}
  [TopologicalSpace G] {I : ModelWithCorners ğ•œ E H}
  {J : ModelWithCorners ğ•œ F G}

@[simp, mfld_simps]
theorem modelWithCorners_prod_toPartialEquiv :
    (I.prod J).toPartialEquiv = I.toPartialEquiv.prod J.toPartialEquiv :=
  rfl

@[simp, mfld_simps]
theorem modelWithCorners_prod_coe (I : ModelWithCorners ğ•œ E H) (I' : ModelWithCorners ğ•œ E' H') :
    (I.prod I' : _ Ã— _ â†’ _ Ã— _) = Prod.map I I' :=
  rfl

@[simp, mfld_simps]
theorem modelWithCorners_prod_coe_symm (I : ModelWithCorners ğ•œ E H)
    (I' : ModelWithCorners ğ•œ E' H') :
    ((I.prod I').symm : _ Ã— _ â†’ _ Ã— _) = Prod.map I.symm I'.symm :=
  rfl

/-- This lemma should be erased, or at least burn in hell, as it uses bad defeq: the left model
with corners is for `E times F`, the right one for `ModelProd E F`, and there's a good reason
we are distinguishing them. -/
theorem modelWithCornersSelf_prod : ğ“˜(ğ•œ, E Ã— F) = ğ“˜(ğ•œ, E).prod ğ“˜(ğ•œ, F) := by ext1 <;> simp

theorem ModelWithCorners.range_prod : range (I.prod J) = range I Ã—Ë¢ range J := by
  simp_rw [â† ModelWithCorners.target_eq]; rfl

end ModelWithCornersProd

section Boundaryless

/-- Property ensuring that the model with corners `I` defines manifolds without boundary. This
differs from the more general `BoundarylessManifold`, which requires every point on the manifold
to be an interior point. -/
class ModelWithCorners.Boundaryless {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) : Prop where
  range_eq_univ : range I = univ

theorem ModelWithCorners.range_eq_univ {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) [I.Boundaryless] :
    range I = univ := ModelWithCorners.Boundaryless.range_eq_univ

/-- If `I` is a `ModelWithCorners.Boundaryless` model, then it is a homeomorphism. -/
@[simps +simpRhs]
def ModelWithCorners.toHomeomorph {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) [I.Boundaryless] : H â‰ƒâ‚œ E where
  __ := I
  left_inv := I.left_inv
  right_inv _ := I.right_inv <| I.range_eq_univ.symm â–¸ mem_univ _

/-- The trivial model with corners has no boundary -/
instance modelWithCornersSelf_boundaryless (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*)
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] : (modelWithCornersSelf ğ•œ E).Boundaryless :=
  âŸ¨by simpâŸ©

/-- If two model with corners are boundaryless, their product also is -/
instance ModelWithCorners.range_eq_univ_prod {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type v}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type w} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) [I.Boundaryless] {E' : Type v'} [NormedAddCommGroup E']
    [NormedSpace ğ•œ E'] {H' : Type w'} [TopologicalSpace H'] (I' : ModelWithCorners ğ•œ E' H')
    [I'.Boundaryless] : (I.prod I').Boundaryless := by
  constructor
  dsimp [ModelWithCorners.prod, ModelProd]
  rw [â† prod_range_range_eq, ModelWithCorners.Boundaryless.range_eq_univ,
    ModelWithCorners.Boundaryless.range_eq_univ, univ_prod_univ]

end Boundaryless

section contDiffGroupoid

/-! ### `C^n` functions on models with corners -/


variable {m n : WithTop â„•âˆ} {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ğ•œ E H} {M : Type*} [TopologicalSpace M]

variable (n I) in
/-- Given a model with corners `(E, H)`, we define the pregroupoid of `C^n` transformations of `H`
as the maps that are `C^n` when read in `E` through `I`. -/
def contDiffPregroupoid : Pregroupoid H where
  property f s := ContDiffOn ğ•œ n (I âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I)
  comp {f g u v} hf hg _ _ _ := by
    have : I âˆ˜ (g âˆ˜ f) âˆ˜ I.symm = (I âˆ˜ g âˆ˜ I.symm) âˆ˜ I âˆ˜ f âˆ˜ I.symm := by ext x; simp
    simp only [this]
    refine hg.comp (hf.mono fun x âŸ¨hx1, hx2âŸ© â†¦ âŸ¨hx1.1, hx2âŸ©) ?_
    rintro x âŸ¨hx1, _âŸ©
    simp only [mfld_simps] at hx1 âŠ¢
    exact hx1.2
  id_mem := by
    apply ContDiffOn.congr contDiff_id.contDiffOn
    rintro x âŸ¨_, hx2âŸ©
    rcases mem_range.1 hx2 with âŸ¨y, hyâŸ©
    rw [â† hy]
    simp only [mfld_simps]
  locality {f u} _ H := by
    apply contDiffOn_of_locally_contDiffOn
    rintro y âŸ¨hy1, hy2âŸ©
    rcases mem_range.1 hy2 with âŸ¨x, hxâŸ©
    rw [â† hx] at hy1 âŠ¢
    simp only [mfld_simps] at hy1 âŠ¢
    rcases H x hy1 with âŸ¨v, v_open, xv, hvâŸ©
    have : I.symm â»Â¹' (u âˆ© v) âˆ© range I = I.symm â»Â¹' u âˆ© range I âˆ© I.symm â»Â¹' v := by
      rw [preimage_inter, inter_assoc, inter_assoc]
      congr 1
      rw [inter_comm]
    rw [this] at hv
    exact âŸ¨I.symm â»Â¹' v, v_open.preimage I.continuous_symm, by simpa, hvâŸ©
  congr {f g u} _ fg hf := by
    apply hf.congr
    rintro y âŸ¨hy1, hy2âŸ©
    rcases mem_range.1 hy2 with âŸ¨x, hxâŸ©
    rw [â† hx] at hy1 âŠ¢
    simp only [mfld_simps] at hy1 âŠ¢
    rw [fg _ hy1]

variable (n I) in
/-- Given a model with corners `(E, H)`, we define the groupoid of invertible `C^n` transformations
of `H` as the invertible maps that are `C^n` when read in `E` through `I`. -/
def contDiffGroupoid : StructureGroupoid H :=
  Pregroupoid.groupoid (contDiffPregroupoid n I)

/-- Inclusion of the groupoid of `C^n` local diffeos in the groupoid of `C^m` local diffeos when
`m â‰¤ n` -/
theorem contDiffGroupoid_le (h : m â‰¤ n) : contDiffGroupoid n I â‰¤ contDiffGroupoid m I := by
  rw [contDiffGroupoid, contDiffGroupoid]
  apply groupoid_of_pregroupoid_le
  intro f s hfs
  exact ContDiffOn.of_le hfs h

/-- The groupoid of `0`-times continuously differentiable maps is just the groupoid of all
open partial homeomorphisms -/
theorem contDiffGroupoid_zero_eq : contDiffGroupoid 0 I = continuousGroupoid H := by
  apply le_antisymm le_top
  intro u _
  -- we have to check that every open partial homeomorphism belongs to `contDiffGroupoid 0 I`,
  -- by unfolding its definition
  change u âˆˆ contDiffGroupoid 0 I
  rw [contDiffGroupoid, mem_groupoid_of_pregroupoid, contDiffPregroupoid]
  simp only [contDiffOn_zero]
  constructor
  Â· refine I.continuous.comp_continuousOn (u.continuousOn.comp I.continuousOn_symm ?_)
    exact (mapsTo_preimage _ _).mono_left inter_subset_left
  Â· refine I.continuous.comp_continuousOn (u.symm.continuousOn.comp I.continuousOn_symm ?_)
    exact (mapsTo_preimage _ _).mono_left inter_subset_left

-- FIXME: does this generalise to other groupoids? The argument is not specific
-- to C^n functions, but uses something about the groupoid's property that is not easy to abstract.
/-- Any change of coordinates with empty source belongs to `contDiffGroupoid`. -/
lemma ContDiffGroupoid.mem_of_source_eq_empty (f : OpenPartialHomeomorph H H)
    (hf : f.source = âˆ…) : f âˆˆ contDiffGroupoid n I := by
  constructor
  Â· intro x âŸ¨hx, _âŸ©
    rw [mem_preimage] at hx
    simp_all only [mem_empty_iff_false]
  Â· intro x âŸ¨hx, _âŸ©
    have : f.target = âˆ… := by simp [â† f.image_source_eq_target, hf]
    simp_all

include I in
/-- Any change of coordinates with empty source belongs to `continuousGroupoid`. -/
lemma ContinuousGroupoid.mem_of_source_eq_empty (f : OpenPartialHomeomorph H H)
    (hf : f.source = âˆ…) : f âˆˆ continuousGroupoid H := by
  rw [â† contDiffGroupoid_zero_eq (I := I)]
  exact ContDiffGroupoid.mem_of_source_eq_empty f hf

/-- An identity open partial homeomorphism belongs to the `C^n` groupoid. -/
theorem ofSet_mem_contDiffGroupoid {s : Set H} (hs : IsOpen s) :
    OpenPartialHomeomorph.ofSet s hs âˆˆ contDiffGroupoid n I := by
  rw [contDiffGroupoid, mem_groupoid_of_pregroupoid]
  suffices h : ContDiffOn ğ•œ n (I âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) by
    simp [h, contDiffPregroupoid]
  have : ContDiffOn ğ•œ n id (univ : Set E) := contDiff_id.contDiffOn
  exact this.congr_mono (fun x hx => I.right_inv hx.2) (subset_univ _)

/-- The composition of an open partial homeomorphism from `H` to `M` and its inverse belongs to
the `C^n` groupoid. -/
theorem symm_trans_mem_contDiffGroupoid (e : OpenPartialHomeomorph M H) :
    e.symm.trans e âˆˆ contDiffGroupoid n I :=
  haveI : e.symm.trans e â‰ˆ OpenPartialHomeomorph.ofSet e.target e.open_target :=
    OpenPartialHomeomorph.symm_trans_self _
  StructureGroupoid.mem_of_eqOnSource _ (ofSet_mem_contDiffGroupoid e.open_target) this

variable {E' H' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] [TopologicalSpace H']

/-- The product of two `C^n` open partial homeomorphisms is `C^n`. -/
theorem contDiffGroupoid_prod {I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'}
    {e : OpenPartialHomeomorph H H} {e' : OpenPartialHomeomorph H' H'}
    (he : e âˆˆ contDiffGroupoid n I) (he' : e' âˆˆ contDiffGroupoid n I') :
    e.prod e' âˆˆ contDiffGroupoid n (I.prod I') := by
  obtain âŸ¨he, he_symmâŸ© := he
  obtain âŸ¨he', he'_symmâŸ© := he'
  constructor <;> simp only [PartialEquiv.prod_source, OpenPartialHomeomorph.prod_toPartialEquiv,
    contDiffPregroupoid]
  Â· have h3 := ContDiffOn.prodMap he he'
    rw [â† I.image_eq, â† I'.image_eq, prod_image_image_eq] at h3
    rw [â† (I.prod I').image_eq]
    exact h3
  Â· have h3 := ContDiffOn.prodMap he_symm he'_symm
    rw [â† I.image_eq, â† I'.image_eq, prod_image_image_eq] at h3
    rw [â† (I.prod I').image_eq]
    exact h3

/-- The `C^n` groupoid is closed under restriction. -/
instance : ClosedUnderRestriction (contDiffGroupoid n I) :=
  (closedUnderRestriction_iff_id_le _).mpr
    (by
      rw [StructureGroupoid.le_iff]
      rintro e âŸ¨s, hs, hesâŸ©
      apply (contDiffGroupoid n I).mem_of_eqOnSource' _ _ _ hes
      exact ofSet_mem_contDiffGroupoid hs)

end contDiffGroupoid

section IsManifold

/-! ### `C^n` manifolds (possibly with boundary or corners) -/

/-- Typeclass defining manifolds with respect to a model with corners, over a
field `ğ•œ`. This definition includes the model with corners `I` (which might allow boundary, corners,
or not, so this class covers both manifolds with boundary and manifolds without boundary), and
a smoothness parameter `n : WithTop â„•âˆ` (where `n = 0` means topological manifold, `n = âˆ` means
smooth manifold and `n = Ï‰` means analytic manifold). -/
class IsManifold {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) (n : WithTop â„•âˆ) (M : Type*)
    [TopologicalSpace M] [ChartedSpace H M] : Prop
    extends HasGroupoid M (contDiffGroupoid n I)

/-- Building a `C^n` manifold from a `HasGroupoid` assumption. -/
theorem IsManifold.mk' {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) (n : WithTop â„•âˆ)
    (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
    [gr : HasGroupoid M (contDiffGroupoid n I)] : IsManifold I n M :=
  { gr with }

theorem isManifold_of_contDiffOn {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ğ•œ E H) (n : WithTop â„•âˆ) (M : Type*)
    [TopologicalSpace M] [ChartedSpace H M]
    (h : âˆ€ e e' : OpenPartialHomeomorph M H, e âˆˆ atlas H M â†’ e' âˆˆ atlas H M â†’
      ContDiffOn ğ•œ n (I âˆ˜ e.symm â‰«â‚• e' âˆ˜ I.symm) (I.symm â»Â¹' (e.symm â‰«â‚• e').source âˆ© range I)) :
    IsManifold I n M where
  compatible := by
    haveI : HasGroupoid M (contDiffGroupoid n I) := hasGroupoid_of_pregroupoid _ (h _ _)
    apply StructureGroupoid.compatible

/-- For any model with corners, the model space is a `C^n` manifold -/
instance instIsManifoldModelSpace {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
    {I : ModelWithCorners ğ•œ E H} {n : WithTop â„•âˆ} : IsManifold I n H :=
  { hasGroupoid_model_space _ _ with }

@[deprecated (since := "2025-04-22")]
alias intIsManifoldModelSpace := instIsManifoldModelSpace

end IsManifold

namespace IsManifold

/- We restate in the namespace `IsManifold` some lemmas that hold for general
charted space with a structure groupoid, avoiding the need to specify the groupoid
`contDiffGroupoid n I` explicitly. -/
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H}
  {n : WithTop â„•âˆ} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

protected theorem of_le {m n : WithTop â„•âˆ} (hmn : m â‰¤ n)
    [IsManifold I n M] : IsManifold I m M := by
  have : HasGroupoid M (contDiffGroupoid m I) :=
    hasGroupoid_of_le (Gâ‚ := contDiffGroupoid n I) (by infer_instance)
      (contDiffGroupoid_le hmn)
  exact mk' I m M

/-- A typeclass registering that a smoothness exponent is smaller than `âˆ`. Used to deduce that
some manifolds are `C^n` when they are `C^âˆ`. -/
class _root_.ENat.LEInfty (m : WithTop â„•âˆ) where
  out : m â‰¤ âˆ

open ENat

instance (n : â„•) : LEInfty (n : WithTop â„•âˆ) := âŸ¨mod_cast le_topâŸ©
instance (n : â„•) [n.AtLeastTwo] : LEInfty (no_index (OfNat.ofNat n) : WithTop â„•âˆ) :=
  inferInstanceAs (LEInfty (n : WithTop â„•âˆ))
instance : LEInfty (1 : WithTop â„•âˆ) := inferInstanceAs (LEInfty ((1 : â„•) : WithTop â„•âˆ))
instance : LEInfty (0 : WithTop â„•âˆ) := inferInstanceAs (LEInfty ((0 : â„•) : WithTop â„•âˆ))

instance {a : WithTop â„•âˆ} [IsManifold I âˆ M] [h : LEInfty a] :
    IsManifold I a M :=
  IsManifold.of_le h.out

instance {a : WithTop â„•âˆ} [IsManifold I Ï‰ M] :
    IsManifold I a M :=
  IsManifold.of_le le_top

instance : IsManifold I 0 M := by
  suffices HasGroupoid M (contDiffGroupoid 0 I) from mk' I 0 M
  constructor
  intro e e' he he'
  rw [contDiffGroupoid_zero_eq]
  trivial

instance [IsManifold I 2 M] :
    IsManifold I 1 M :=
  IsManifold.of_le one_le_two

instance [IsManifold I 3 M] : IsManifold I 2 M := IsManifold.of_le (n := 3) (by norm_cast)

variable (I n M) in
/-- The maximal atlas of `M` for the `C^n` manifold with corners structure corresponding to the
model with corners `I`. -/
def maximalAtlas :=
  (contDiffGroupoid n I).maximalAtlas M

theorem subset_maximalAtlas [IsManifold I n M] : atlas H M âŠ† maximalAtlas I n M :=
  StructureGroupoid.subset_maximalAtlas _

theorem chart_mem_maximalAtlas [IsManifold I n M] (x : M) :
    chartAt H x âˆˆ maximalAtlas I n M :=
  StructureGroupoid.chart_mem_maximalAtlas _ x

theorem compatible_of_mem_maximalAtlas {e e' : OpenPartialHomeomorph M H}
    (he : e âˆˆ maximalAtlas I n M) (he' : e' âˆˆ maximalAtlas I n M) :
    e.symm.trans e' âˆˆ contDiffGroupoid n I :=
  StructureGroupoid.compatible_of_mem_maximalAtlas he he'

lemma maximalAtlas_subset_of_le {m n : WithTop â„•âˆ} (h : m â‰¤ n) :
    maximalAtlas I n M âŠ† maximalAtlas I m M :=
  StructureGroupoid.maximalAtlas_mono (contDiffGroupoid_le h)

variable (n) in
/-- The empty set is a `C^n` manifold w.r.t. any charted space and model. -/
instance empty [IsEmpty M] : IsManifold I n M := by
  apply isManifold_of_contDiffOn
  intro e e' _ _ x hx
  set t := I.symm â»Â¹' (e.symm â‰«â‚• e').source âˆ© range I
  -- Since `M` is empty, the condition about compatibility of transition maps is vacuous.
  have : (e.symm â‰«â‚• e').source = âˆ… := calc (e.symm â‰«â‚• e').source
    _ = (e.symm.source) âˆ© e.symm â»Â¹' e'.source := by rw [â† OpenPartialHomeomorph.trans_source]
    _ = (e.symm.source) âˆ© e.symm â»Â¹' âˆ… := by rw [eq_empty_of_isEmpty (e'.source)]
    _ = (e.symm.source) âˆ© âˆ… := by rw [preimage_empty]
    _ = âˆ… := inter_empty e.symm.source
  have : t = âˆ… := calc t
    _ = I.symm â»Â¹' (e.symm â‰«â‚• e').source âˆ© range I := by
      rw [â† Subtype.preimage_val_eq_preimage_val_iff]
    _ = âˆ… âˆ© range I := by rw [this, preimage_empty]
    _ = âˆ… := empty_inter (range I)
  apply (this â–¸ hx).elim

attribute [local instance] ChartedSpace.of_discreteTopology in
variable (n) in
/-- A discrete space `M` is a smooth manifold over the trivial model on a trivial normed space. -/
theorem of_discreteTopology [DiscreteTopology M] [Unique E] :
    IsManifold (modelWithCornersSelf ğ•œ E) n M := by
  apply isManifold_of_contDiffOn _ _ _ (fun _ _ _ _ â†¦ contDiff_of_subsingleton.contDiffOn)

attribute [local instance] ChartedSpace.of_discreteTopology in
example [Unique E] : IsManifold (ğ“˜(ğ•œ, E)) n (Fin 2) := of_discreteTopology _

/-- The product of two `C^n` manifolds is naturally a `C^n` manifold. -/
instance prod {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H : Type*}
    [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {H' : Type*} [TopologicalSpace H']
    {I' : ModelWithCorners ğ•œ E' H'} (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
    [IsManifold I n M] (M' : Type*) [TopologicalSpace M'] [ChartedSpace H' M']
    [IsManifold I' n M'] :
    IsManifold (I.prod I') n (M Ã— M') where
  compatible := by
    rintro f g âŸ¨f1, hf1, f2, hf2, rflâŸ© âŸ¨g1, hg1, g2, hg2, rflâŸ©
    rw [OpenPartialHomeomorph.prod_symm, OpenPartialHomeomorph.prod_trans]
    have h1 := (contDiffGroupoid n I).compatible hf1 hg1
    have h2 := (contDiffGroupoid n I').compatible hf2 hg2
    exact contDiffGroupoid_prod h1 h2

section DisjointUnion

variable {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M']
  [hM : IsManifold I n M] [hM' : IsManifold I n M']

/-- The disjoint union of two `C^n` manifolds modelled on `(E, H)`
is a `C^n` manifold modeled on `(E, H)`. -/
instance disjointUnion : IsManifold I n (M âŠ• M') where
  compatible {e} e' he he' := by
    obtain (h | h) := isEmpty_or_nonempty H
    Â· exact ContDiffGroupoid.mem_of_source_eq_empty _ (eq_empty_of_isEmpty _)
    obtain (âŸ¨f, hf, hefâŸ© | âŸ¨f, hf, hefâŸ©) := ChartedSpace.mem_atlas_sum he
    Â· obtain (âŸ¨f', hf', he'f'âŸ© | âŸ¨f', hf', he'f'âŸ©) := ChartedSpace.mem_atlas_sum he'
      Â· rw [hef, he'f', f.lift_openEmbedding_trans f' IsOpenEmbedding.inl]
        exact hM.compatible hf hf'
      Â· rw [hef, he'f']
        apply ContDiffGroupoid.mem_of_source_eq_empty
        ext x
        exact âŸ¨fun âŸ¨hxâ‚, hxâ‚‚âŸ© â†¦ by simp_all, fun hx â†¦ hx.elimâŸ©
    Â· -- Analogous argument to the first case: is there a way to deduplicate?
      obtain (âŸ¨f', hf', he'f'âŸ© | âŸ¨f', hf', he'f'âŸ©) := ChartedSpace.mem_atlas_sum he'
      Â· rw [hef, he'f']
        apply ContDiffGroupoid.mem_of_source_eq_empty
        ext x
        exact âŸ¨fun âŸ¨hxâ‚, hxâ‚‚âŸ© â†¦ by simp_all, fun hx â†¦ hx.elimâŸ©
      Â· rw [hef, he'f', f.lift_openEmbedding_trans f' IsOpenEmbedding.inr]
        exact hM'.compatible hf hf'

end DisjointUnion

end IsManifold

theorem OpenPartialHomeomorph.isManifold_singleton
    {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {n : WithTop â„•âˆ}
    {M : Type*} [TopologicalSpace M] (e : OpenPartialHomeomorph M H) (h : e.source = Set.univ) :
    @IsManifold ğ•œ _ E _ _ H _ I n M _ (e.singletonChartedSpace h) :=
  @IsManifold.mk' _ _ _ _ _ _ _ _ _ _ _ (id _) <|
    e.singleton_hasGroupoid h (contDiffGroupoid n I)

theorem Topology.IsOpenEmbedding.isManifold_singleton {ğ•œ E H : Type*}
    [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [TopologicalSpace H]
    {I : ModelWithCorners ğ•œ E H} {n : WithTop â„•âˆ}
    {M : Type*} [TopologicalSpace M] [Nonempty M] {f : M â†’ H} (h : IsOpenEmbedding f) :
    @IsManifold ğ•œ _ E _ _ H _ I n M _ h.singletonChartedSpace :=
  (h.toOpenPartialHomeomorph f).isManifold_singleton (by simp)

namespace TopologicalSpace.Opens

open TopologicalSpace

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {n : WithTop â„•âˆ}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I n M]
  (s : Opens M)

instance : IsManifold I n s :=
  { s.instHasGroupoid (contDiffGroupoid n I) with }

end TopologicalSpace.Opens

section TangentSpace

/- We define the tangent space to `M` modelled on `I : ModelWithCorners ğ•œ E H` as a type synonym
for `E`. This is enough to define linear maps between tangent spaces, for instance derivatives,
but the interesting part is to define a manifold structure on the whole tangent bundle, which
requires that `M` is a `C^n` manifold. The definition is put here to avoid importing
all the smooth bundle structure when defining manifold derivatives. -/

set_option linter.unusedVariables false in
/-- The tangent space at a point of the manifold `M`. It is just `E`. We could use instead
`(tangentBundleCore I M).toFiberBundleCore.fiber x`, but we use `E` to help the kernel.

The definition of `TangentSpace` is not reducible so that type class inference
does not pick wrong instances.
-/
@[nolint unusedArguments]
def TangentSpace {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
    {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
    {M : Type*} [TopologicalSpace M] [ChartedSpace H M] (_x : M) : Type u := E
deriving
  TopologicalSpace, AddCommGroup, IsTopologicalAddGroup, Module ğ•œ,
  ContinuousSMul ğ•œ,
  -- the following instance derives from the previous one, but through an instance with priority 100
  -- which takes a long time to be found. We register a shortcut instance instead
  ContinuousConstSMul ğ•œ

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {x : M}

instance : Inhabited (TangentSpace I x) := âŸ¨0âŸ©

variable (M) in
-- is empty if the base manifold is empty
/-- The tangent bundle to a manifold, as a Sigma type. Defined in terms of
`Bundle.TotalSpace` to be able to put a suitable topology on it. -/
abbrev TangentBundle := Bundle.TotalSpace E (TangentSpace I : M â†’ Type _)

end TangentSpace

section Real

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners â„ E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {x : M}

deriving instance PathConnectedSpace for TangentSpace I x

end Real
