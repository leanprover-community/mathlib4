/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Michael Rothgang
-/
import Mathlib.Geometry.Manifold.Algebra.Monoid
import Mathlib.Geometry.Manifold.Notation
import Mathlib.Geometry.Manifold.VectorBundle.MDifferentiable
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection


/-!
# Local frames in a vector bundle

Let `V ‚Üí M` be a finite rank smooth vector bundle with standard fiber `F`.
Given a basis `b` for `F` and a local trivialisation `e` for `V`,
we construct a **smooth local frame** on `V` w.r.t. `e` and `b`,
i.e. a collection of sections `s·µ¢` of `V` which is smooth on `e.baseSet` such that `{s·µ¢ x}` is a
basis of `V x` for each `x ‚àà e.baseSet`. Any section `s` of `e` can be uniquely written as
`s = ‚àë i, f^i s·µ¢` near `x`, and `s` is smooth at `x` iff the functions `f^i` are.

The latter statement holds in many cases, but not for every vector bundle. In this file, we prove
it for local frames induced by a trivialisation, for finite rank bundles over a complete field.
In `OrthonormalFrame.lean`, we prove the same for real vector bundles of any rank which admit
a `C^n` bundle metric. This includes bundles of finite rank, modelled on a Hilbert space or
on a Banach space which has smooth partitions of unity.

We use this to construct local extensions of a vector to a section which is smooth on the
trivialisation domain.

## Main definitions and results

* `IsLocalFrameOn`: a family of sections `s i` of `V ‚Üí M` is called a **C^k local frame** on a set
  `U ‚äÜ M` iff each section `s i` is `C^k` on `U`, and the section values `s i x` form a basis for
  each `x ‚àà U`

Suppose `{s·µ¢}` is a local frame on `U`, and `hs : IsLocalFrameOn s U`.
* `IsLocalFrameOn.toBasisAt hs`: for each `x ‚àà U`, the vectors `s·µ¢ x` form a basis of `F`
* `IsLocalFrameOn.repr hs` describes the coefficient of sections of `V` w.r.t. `{s·µ¢}`.
  `hs.repr i` is a linear map from sections of `V` to functions `M ‚Üí ùïú`.
* `IsLocalFrameOn.repr_spec hs`: for a local frame `{s·µ¢}` near `x`, for each section `t` we have
  `t = ‚àë i, (hs.repr i t) ‚Ä¢ s·µ¢`.
* `IsLocalFrameOn.repr_sum_eq hs t hx` proves that `t x = ‚àë i, (hs.repr i t) x ‚Ä¢ s·µ¢ x`, provided
  that `hx : x ‚àà U`.
* `IsLocalFrameOn.repr_congr hs`: the coefficient `hs.repr i` of `t` in the local frame `{s·µ¢}`
  only depends on `t` at `x`.
* `IsLocalFrameOn.eq_iff_repr hs`: two sections `t` and `t'` are equal at `x` if and only if their
  coefficients at `x` w.r.t. `{s·µ¢}` agree.
* `IsLocalFrameOn.contMDiffOn_repr hs`: if `t` is a `C^k` section, each coefficient
  `hs.repr i t` is `C^k` on `U`
* `IsLocalFrameOn.contMDiffAt_iff_repr hs`: a section `t` is `C^k` at `x ‚àà U`
  iff all of its frame coefficients are
* `IsLocalFrameOn.contMDiffOn_iff_repr hs`: a section `t` is `C^k` on an open set `t ‚äÜ U`
  iff all of its frame coefficients are

* `Basis.localFrame e b`: the local frame on `V` w.r.t. a local trivialisation `e` of `V` and a
  basis `b` of `F`. Use `b.localFrame e i` to access the i-th section in that frame.
* `b.contMDiffOn_localFrame_baseSet`: each section `b.localFrame e i` is smooth on `e.baseSet`
* `b.localFrame_toBasis_at e`: for each `x ‚àà e.baseSet`, the vectors `b.localFrame e i x` form
  a basis of `F`
* `Basis.localFrame_repr e b i` describes the coefficient of sections of `V` w.r.t.`b.localFrame e`:
  `b.localFrame e i` is a linear map from sections of `V` to functions `M ‚Üí ùïú`.
* `b.localFrame_repr_spec e`: near `x`, we have
  `s = ‚àë i, (b.localFrame_repr e i s) ‚Ä¢ b.localFrame e i`
* `b.localFrame_repr_congr e`: the coefficient `b.localFrame_repr e b i` of `s` in the local frame
  induced by `e` and `b` at `x` only depends on `s` at `x`.
* `b.contMDiffOn_localFrame_repr`: if `s` is a `C^k` section, each coefficient
  `b.localFrame_repr e i s` is `C^k` on `e.baseSet`
* `b.contMDiffAt_iff_localFrame_repr e`: a section `s` is `C^k` at `x ‚àà e.baseSet`
  iff all of its frame coefficients are
* `b.contMDiffOn_iff_localFrame_repr e`: a section `s` is `C^k` on an open set `t ‚äÜ e.baseSet`
  iff all of its frame coefficients are

## Implementation notes
* local frames use the junk value pattern: they are defined on all of `M`, but their value is
  only meaningful inside `e.baseSet`
* something about local extensions (and different fields)

## Tags
vector bundle, local frame, smoothness

-/
open Bundle Filter Function Topology Module

open scoped Bundle Manifold ContDiff

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ùïú E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I 0 M]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  -- `F` model fiber
  (n : WithTop ‚Ñï‚àû)
  {V : M ‚Üí Type*} [TopologicalSpace (TotalSpace F V)]
  [‚àÄ x, AddCommGroup (V x)] [‚àÄ x, Module ùïú (V x)]
  [‚àÄ x : M, TopologicalSpace (V x)]
  -- not needed in this file
  -- [‚àÄ x, IsTopologicalAddGroup (V x)] [‚àÄ x, ContinuousSMul ùïú (V x)]
  [FiberBundle F V] [VectorBundle ùïú F V] [ContMDiffVectorBundle n F V I]
  -- `V` vector bundle

noncomputable section

section IsLocalFrame

omit [IsManifold I 0 M] [VectorBundle ùïú F V]

variable {Œπ : Type*} {s s' : Œπ ‚Üí (x : M) ‚Üí V x} {u u' : Set M} {x : M} {n : WithTop ‚Ñï‚àû}

variable (I F n) in
/-
A family of sections `s i` of `V ‚Üí M` is called a **C^k local frame** on a set `U ‚äÜ M` iff
- the section values `s i x` form a basis for each `x ‚àà U`,
- each section `s i` is `C^k` on `U`.
-/
structure IsLocalFrameOn (s : Œπ ‚Üí (x : M) ‚Üí V x) (u : Set M) where
  linearIndependent {x : M} (hx : x ‚àà u) : LinearIndependent ùïú (s ¬∑ x)
  generating {x : M} (hx : x ‚àà u) : ‚ä§ ‚â§ Submodule.span ùïú (Set.range (s ¬∑ x))
  contMDiffOn (i : Œπ) : CMDiff[u] n (T% (s i))

namespace IsLocalFrameOn

/-- If `s = s'` on `u` and `s i` is a local frame on `u`, then so is `s'`. -/
lemma congr (hs : IsLocalFrameOn I F n s u) (hs' : ‚àÄ i, ‚àÄ x, x ‚àà u ‚Üí s i x = s' i x) :
    IsLocalFrameOn I F n s' u where
  linearIndependent := by
    intro x hx
    have := hs.linearIndependent hx
    simp_all
  generating := by
    intro x hx
    have := hs.generating hx
    simp_all
  contMDiffOn i := (hs.contMDiffOn i).congr fun y hy ‚Ü¶ by simp [hs' i y hy]

lemma mono (hs : IsLocalFrameOn I F n s u) (hu'u : u' ‚äÜ u) : IsLocalFrameOn I F n s u' where
  linearIndependent := by
    intro x hx
    exact hs.linearIndependent (hu'u hx)
  generating := by
    intro x hx
    exact hs.generating (hu'u hx)
  contMDiffOn i := (hs.contMDiffOn i).mono hu'u

-- TODO think: is this bracketing behaviour desired!
-- in any case, add a test and a nicer error message!
lemma contMDiffAt (hs : IsLocalFrameOn I F n s u) (hu : IsOpen u) (hx : x ‚àà u) (i : Œπ) :
    CMDiffAt n (T% (s i)) x :=
  (hs.contMDiffOn i).contMDiffAt <| hu.mem_nhds hx

/-- Given a local frame `{s i}` on `U ‚àã x`, returns the basis `{s i}` of `V x` -/
def toBasisAt (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) : Basis Œπ ùïú (V x) :=
  Basis.mk (hs.linearIndependent hx) (hs.generating hx)

@[simp]
lemma toBasisAt_coe (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) (i : Œπ) :
    (toBasisAt hs hx) i = s i x := by
  simpa only [toBasisAt] using Basis.mk_apply (hs.linearIndependent hx) (hs.generating hx) i

open scoped Classical in
/-- Coefficients of a section `s` of `V` w.r.t. a local frame `{s i}` on `u`.
Outside of `u`, this returns the junk value 0. -/
-- NB. We don't use simps here, as we prefer to have dedicated `_apply` lemmas for the separate
-- cases.
def repr (hs : IsLocalFrameOn I F n s u) (i : Œπ) : (Œ† x : M, V x) ‚Üí‚Çó[ùïú] M ‚Üí ùïú where
  toFun s x := if hx : x ‚àà u then (hs.toBasisAt hx).repr (s x) i else 0
  map_add' s s' := by
    ext x
    by_cases hx : x ‚àà u <;> simp [hx]
  map_smul' c s := by
    ext x
    by_cases hx : x ‚àà u <;> simp [hx]

variable {x : M}

@[simp]
lemma repr_apply_of_notMem (hs : IsLocalFrameOn I F n s u) (hx : x ‚àâ u) (t : Œ† x : M, V x) (i : Œπ) :
    hs.repr i t x = 0 := by
  simp [repr, hx]

@[simp]
lemma repr_apply_of_mem (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) (t : Œ† x : M, V x) (i : Œπ) :
    hs.repr i t x = (hs.toBasisAt hx).repr (t x) i := by
  simp [repr, hx]

-- TODO: add uniqueness of the decomposition; follows from the IsBasis property in the definition

lemma repr_sum_eq [Fintype Œπ] (hs : IsLocalFrameOn I F n s u) (t : Œ† x : M,  V x) (hx : x ‚àà u) :
    t x = (‚àë i, (hs.repr i t x) ‚Ä¢ (s i x)) := by
  simpa [repr, hx] using (Basis.sum_repr (hs.toBasisAt hx) (t x)).symm

/-- A local frame locally spans the space of sections for `V`: for each local frame `s i` on an open
set `u` around `x`, we have `t = ‚àë i, (hs.repr i t) ‚Ä¢ (s i x)` near `x`. -/
lemma repr_spec [Fintype Œπ] (hs : IsLocalFrameOn I F n s u) (t : Œ† x : M,  V x) (hu'' : u ‚àà ùìù x) :
    ‚àÄ·∂† x' in ùìù x, t x' = ‚àë i, (hs.repr i t x') ‚Ä¢ (s i x') :=
  eventually_of_mem hu'' fun _ hx ‚Ü¶ hs.repr_sum_eq _ hx

/-- The representation of `s` in a local frame at `x` only depends on `s` at `x`. -/
lemma repr_congr (hs : IsLocalFrameOn I F n s u) {t t' : Œ† x : M, V x}
    (htt' : t x = t' x) (i : Œπ) :
    hs.repr i t x = hs.repr i t' x := by
  by_cases hxe : x ‚àà u
  ¬∑ simp [repr, hxe]
    congr
  ¬∑ simp [repr, hxe]

/-- If `s` and `s'` are local frames which are equal at `x`,
a section `t` has equal frame coefficients in them. -/
lemma repr_eq_of_eq (hs : IsLocalFrameOn I F n s u) (hs' : IsLocalFrameOn I F n s' u) {x}
    (hss' : ‚àÄ i, s i x = s' i x) {t : Œ† x : M,  V x} (i : Œπ) :
    hs.repr i t x = hs'.repr i t x := by
  by_cases hxe : x ‚àà u
  ¬∑ simp [repr, hxe]
    simp_all [toBasisAt]
  ¬∑ simp [repr, hxe]

/-- Two sections `s` and `t` are equal at `x` if and only if their coefficients w.r.t. some local
frame at `x` agree. -/
lemma eq_iff_repr [Fintype Œπ] (hs : IsLocalFrameOn I F n s u) {t t' : Œ† x : M, V x} (hx : x ‚àà u) :
    t x = t' x ‚Üî ‚àÄ i, hs.repr i t x = hs.repr i t' x :=
  ‚ü®fun h i ‚Ü¶ hs.repr_congr h i, fun h ‚Ü¶ by
    simp +contextual [h, hs.repr_sum_eq t hx, hs.repr_sum_eq t' hx]‚ü©

lemma repr_apply_zero_at (hs : IsLocalFrameOn I F n s u) {t : Œ† x : M, V x} (ht : t x = 0) (i : Œπ) :
    hs.repr i t x = 0 := by
  simp [hs.repr_congr (t' := 0) ht]

variable (hs : IsLocalFrameOn I F n s u) {t : Œ† x : M, V x} [VectorBundle ùïú F V]

/-- Given a local frame `s i ` on `u`, if a section `t` has `C^k` coefficients on `u` w.r.t. `s i`,
then `t` is `C^n` on `u`. -/
lemma contMDiffOn_of_repr [Fintype Œπ] (h : ‚àÄ i, CMDiff[u] n (hs.repr i t)) :
    CMDiff[u] n (T% t) := by
  have this (i) : CMDiff[u] n (T% (hs.repr i t ‚Ä¢ s i)) :=
    (h i).smul_section (hs.contMDiffOn i)
  have almost : CMDiff[u] n (T% (fun x ‚Ü¶ ‚àë i, (hs.repr i t) x ‚Ä¢ s i x)) :=
    .sum_section fun i _ ‚Ü¶ this i
  apply almost.congr
  intro y hy
  simp [hs.repr_sum_eq t hy]

/-- Given a local frame `s i` on a neighbourhood `u` of `x`,
if a section `t` has `C^k` coefficients at `x` w.r.t. `s i`, then `t` is `C^n` at `x`. -/
lemma contMDiffAt_of_repr [Fintype Œπ]
    (h : ‚àÄ i, CMDiffAt n (hs.repr i t) x) (hu : u ‚àà ùìù x) : CMDiffAt n (T% t) x := by
  have this (i) : CMDiffAt n (T% (hs.repr i t ‚Ä¢ s i)) x :=
    (h i).smul_section <| (hs.contMDiffOn i).contMDiffAt hu
  have almost : CMDiffAt n
    (T% (fun x ‚Ü¶ ‚àë i, (hs.repr i t) x ‚Ä¢ s i x)) x := .sum_section (fun i _ ‚Ü¶ this i)
  exact almost.congr_of_eventuallyEq <| (hs.repr_spec t hu).mono fun x h ‚Ü¶ by simp [h]

/-- Given a local frame `s i` on an open set `u` containing `x`, if a section `t` has `C^k`
coefficients at `x ‚àà u` w.r.t. `s i`, then `t` is `C^n` at `x`. -/
lemma contMDiffAt_of_repr_aux [Fintype Œπ]
    (h : ‚àÄ i, CMDiffAt n (hs.repr i t) x) (hu : IsOpen u) (hx : x ‚àà u) : CMDiffAt n (T% t) x :=
  hs.contMDiffAt_of_repr h (hu.mem_nhds hx)

section

variable (hs : IsLocalFrameOn I F 1 s u)

/-- Given a local frame `s i ` on `u`, if a section `t` has differentiable coefficients on `u`
w.r.t. `s i`, then `t` is differentiable on `u`. -/
lemma mdifferentiableOn_of_repr [Fintype Œπ] (h : ‚àÄ i, MDiff[u] (hs.repr i t)) :
    MDiff[u] (T% t) := by
  have this (i) : MDiff[u] (T% (hs.repr i t ‚Ä¢ s i)) :=
    (h i).smul_section ((hs.contMDiffOn i).mdifferentiableOn le_rfl)
  have almost : MDiff[u] (T% (fun x ‚Ü¶ ‚àë i, (hs.repr i t) x ‚Ä¢ s i x)) :=
    .sum_section (fun i _ hx ‚Ü¶ this i _ hx)
  apply almost.congr
  intro y hy
  simp [hs.repr_sum_eq t hy]

/-- Given a local frame `s i` on a neighbourhood `u` of `x`, if a section `t` has differentiable
coefficients at `x` w.r.t. `s i`, then `t` is differentiable at `x`. -/
lemma mdifferentiableAt_of_repr [Fintype Œπ]
    (h : ‚àÄ i, MDiffAt (hs.repr i t) x) (hu : u ‚àà ùìù x) : MDiffAt (T% t) x := by
  have this (i) : MDiffAt (T% (hs.repr i t ‚Ä¢ s i)) x :=
    (h i).smul_section <| ((hs.contMDiffOn i).mdifferentiableOn le_rfl).mdifferentiableAt hu
  have almost : MDiffAt
    (T% (fun x ‚Ü¶ ‚àë i, (hs.repr i t) x ‚Ä¢ s i x)) x := .sum_section (fun i ‚Ü¶ this i)
  exact almost.congr_of_eventuallyEq <| (hs.repr_spec t hu).mono fun x h ‚Ü¶ by simp [h]

/-- Given a local frame `s i` on open set `u` containing `x`, if a section `t`
has differentiable coefficients at `x ‚àà u` w.r.t. `s i`, then `t` is differentiable at `x`. -/
lemma mdifferentiableAt_of_repr_aux [Fintype Œπ]
    (h : ‚àÄ i, MDiffAt (hs.repr i t) x) (hu : IsOpen u) (hx : x ‚àà u) : MDiffAt (T% t) x :=
  hs.mdifferentiableAt_of_repr h (hu.mem_nhds hx)

end

end IsLocalFrameOn

end IsLocalFrame

namespace Module.Basis

variable {Œπ : Type*}

noncomputable def localFrame_toBasis_at
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e]
    (b : Basis Œπ ùïú F) {x : M} (hx : x ‚àà e.baseSet) : Basis Œπ ùïú (V x) :=
  b.map (e.linearEquivAt (R := ùïú) x hx).symm

open scoped Classical in
-- If x is outside of `e.baseSet`, this returns the junk value 0.
noncomputable def localFrame
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e]
    (b : Basis Œπ ùïú F) : Œπ ‚Üí (x : M) ‚Üí V x := fun i x ‚Ü¶
  -- idea: take the vector b i and apply the trivialisation e to it.
  if hx : x ‚àà e.baseSet then b.localFrame_toBasis_at e hx i else 0

-- TODO: understand why this isn‚Äôt already a simp lemma
attribute [simp] Trivialization.apply_mk_symm

omit [IsManifold I 0 M] in
/-- Each local frame `s^i ‚àà Œì(E)` of a `C^k` vector bundle, defined by a local trivialisation `e`,
is `C^k` on `e.baseSet`. -/
lemma contMDiffOn_localFrame_baseSet
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e] (b : Basis Œπ ùïú F) (i : Œπ) :
    CMDiff[e.baseSet] n (T% (b.localFrame e i)) := by
  rw [e.contMDiffOn_section_baseSet_iff]
  apply (contMDiffOn_const (c := b i)).congr
  intro y hy
  simp [localFrame, hy, localFrame_toBasis_at]

omit [IsManifold I 0 M] in
variable (I) in
/-- `b.localFrame e i` is indeed a local frame on `e.baseSet` -/
lemma localFrame_isLocalFrameOn_baseSet
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e] (b : Basis Œπ ùïú F) : IsLocalFrameOn I F n (b.localFrame e) e.baseSet
    where
  contMDiffOn i := b.contMDiffOn_localFrame_baseSet _ e i
  linearIndependent := by
    intro x hx
    convert (b.localFrame_toBasis_at e hx).linearIndependent
    simp [localFrame, hx, localFrame_toBasis_at]
  generating := by
    intro x hx
    convert (b.localFrame_toBasis_at e hx).span_eq.ge
    simp [localFrame, hx, localFrame_toBasis_at]

omit [IsManifold I 0 M] in
lemma _root_.contMDiffAt_localFrame_of_mem
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e] (b : Basis Œπ ùïú F) (i : Œπ) {x : M} (hx : x ‚àà e.baseSet) :
    CMDiffAt n (T% (b.localFrame e i)) x :=
  (b.localFrame_isLocalFrameOn_baseSet I n e).contMDiffAt e.open_baseSet hx _

@[simp]
lemma localFrame_apply_of_mem_baseSet
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e] (b : Basis Œπ ùïú F) {i : Œπ} {x : M} (hx : x ‚àà e.baseSet) :
    b.localFrame e i x = b.localFrame_toBasis_at e hx i := by
  simp [localFrame, hx]

@[simp]
lemma localFrame_apply_of_notMem
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e] (b : Basis Œπ ùïú F) {i : Œπ} {x : M} (hx : x ‚àâ e.baseSet) :
    b.localFrame e i x = 0 := by
  simp [localFrame, hx]

lemma localFrame_toBasis_at_coe
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e]
    (b : Basis Œπ ùïú F) {x : M} (i : Œπ) (hx : x ‚àà e.baseSet) :
    b.localFrame_toBasis_at e hx i = b.localFrame e i x := by simp [hx]

variable [ContMDiffVectorBundle 1 F V I]

open scoped Classical in
variable (I) in
/-- Coefficients of a section `s` of `V` w.r.t. the local frame `b.localFrame e i` -/
-- If x is outside of `e.baseSet`, this returns the junk value 0.
-- NB. We don't use simps here, as we prefer to have dedicated `_apply` lemmas for the separate
-- cases.
noncomputable def localFrame_repr
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M))
    [MemTrivializationAtlas e]
    (b : Basis Œπ ùïú F) (i : Œπ) : (Œ† x : M, V x) ‚Üí‚Çó[ùïú] M ‚Üí ùïú :=
  (b.localFrame_isLocalFrameOn_baseSet I 1 e).repr i

variable {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M)}
  [MemTrivializationAtlas e] {b : Basis Œπ ùïú F} {x : M}

omit [IsManifold I 0 M] in
variable (e b) in
@[simp]
lemma localFrame_repr_apply_of_notMem_baseSet (hx : x ‚àâ e.baseSet) (s : Œ† x : M, V x) (i : Œπ) :
    b.localFrame_repr I e i s x = 0 := by
  simpa [localFrame_repr] using
    (localFrame_isLocalFrameOn_baseSet I 1 e b).repr_apply_of_notMem hx s i

omit [IsManifold I 0 M] in
variable (e b) in
@[simp]
lemma localFrame_repr_apply_of_mem_baseSet (hx : x ‚àà e.baseSet) (s : Œ† x : M, V x) (i : Œπ) :
    b.localFrame_repr I e i s x = (b.localFrame_toBasis_at e hx).repr (s x) i := by
  have ilf := b.localFrame_isLocalFrameOn_baseSet I 1 e
  rw [show localFrame_toBasis_at e b hx = ilf.toBasisAt hx by ext j; simp [localFrame, hx]]
  exact ilf.repr_apply_of_mem hx s i

-- TODO: better name?
omit [IsManifold I 0 M] in
lemma localFrame_repr_sum_eq [Fintype Œπ] (s : Œ† x : M,  V x) {x'} (hx : x' ‚àà e.baseSet) :
    s x' = (‚àë i, (b.localFrame_repr I e i s x') ‚Ä¢ b.localFrame e i x') := by
  simp only [localFrame_repr]
  exact (localFrame_isLocalFrameOn_baseSet I 1 e b).repr_sum_eq s hx

variable (b) in
omit [IsManifold I 0 M] in
/-- A local frame locally spans the space of sections for `V`: for each local trivialisation `e`
  of `V` around `x`, we have `s = ‚àë i, (b.localFrame_repr e i s) ‚Ä¢ b.localFrame e i` -/
lemma localFrame_repr_spec [Fintype Œπ] {x : M} (hxe : x ‚àà e.baseSet) (s : Œ† x : M,  V x) :
    ‚àÄ·∂† x' in ùìù x, s x' = ‚àë i, (b.localFrame_repr I e i s x') ‚Ä¢ b.localFrame e i x' :=
  eventually_nhds_iff.mpr ‚ü®e.baseSet, fun _ h ‚Ü¶ localFrame_repr_sum_eq s h, e.open_baseSet, hxe‚ü©

omit [IsManifold I 0 M] in
/-- The representation of `s` in a local frame at `x` only depends on `s` at `x`. -/
lemma localFrame_repr_congr (b : Basis Œπ ùïú F)
    {s s' : Œ† x : M,  V x} {i : Œπ} (hss' : s x = s' x) :
    b.localFrame_repr I e i s x = b.localFrame_repr I e i s' x := by
  by_cases hxe : x ‚àà e.baseSet
  ¬∑ simp [localFrame_repr, hxe]
    congr
  ¬∑ simp [localFrame_repr, hxe]

omit [IsManifold I 0 M] in
lemma localFrame_repr_apply_zero_at
    (b : Basis Œπ ùïú F) {s : Œ† x : M, V x} (hs : s x = 0) (i : Œπ) :
    b.localFrame_repr I e i s x = 0 := by
  simp only [localFrame_repr]
  exact (localFrame_isLocalFrameOn_baseSet I 1 e b).repr_apply_zero_at hs i

variable {n}

omit [IsManifold I 0 M] in
/-- Suppose `e` is a compatible trivialisation around `x ‚àà M`, and `s` a bundle section.
Then the coefficient of `s` w.r.t. the local frame induced by `b` and `e`
equals the cofficient of "`s x` read in the trivialisation `e`" for `b i`. -/
lemma localFrame_repr_eq_repr (hxe : x ‚àà e.baseSet) (b : Basis Œπ ùïú F) {i : Œπ} {s : Œ† x : M, V x} :
    b.localFrame_repr I e i s x = b.repr (e (s x)).2 i := by
  --simp only [localFrame_repr]
  simp [b.localFrame_repr_apply_of_mem_baseSet e hxe, Basis.localFrame_toBasis_at]

end Module.Basis

/-! # Determining smoothness of a section via its local frame coefficients
We show that for finite rank bundles over a complete field, a section is smooth iff its coefficients
in a local frame induced by a local trivialisation are. In many contexts, this statement holds for
*any* local frame (e.g., for all real bundles which admit a continuous bundle metric, as is
proven in `OrthonormalFrame.lean`).
-/

variable {Œπ : Type*} {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V ‚Üí M)}
    [MemTrivializationAtlas e] {b : Basis Œπ ùïú F} {x : M}
    [ContMDiffVectorBundle 1 F V I]

omit [IsManifold I 0 M] in
/-- If `s` is `C^k` at `x`, so is its coefficient `b.localFrame_repr e i` in the local frame
near `x` induced by `e` and `b` -/
lemma contMDiffAt_localFrame_repr [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (hxe : x ‚àà e.baseSet) (b : Basis Œπ ùïú F)
    {s : Œ† x : M,  V x} {k : WithTop ‚Ñï‚àû} [ContMDiffVectorBundle k F V I]
    (hs : CMDiffAt k (T% s) x) (i : Œπ) :
    CMDiffAt k (b.localFrame_repr I e i s) x := by
  -- This boils down to computing the frame coefficients in a local trivialisation.
  classical
  -- step 1: on e.baseSet, can compute the coefficient very well
  let aux := fun x ‚Ü¶ b.repr (e (s x)).2 i
  -- Since e.baseSet is open, this is sufficient.
  suffices CMDiffAt k aux x by
    apply this.congr_of_eventuallyEq ?_
    apply eventuallyEq_of_mem (s := e.baseSet) (by simp [e.open_baseSet.mem_nhds hxe])
    intro y hy
    simp [aux, Basis.localFrame_repr_eq_repr hy]
  simp only [aux]

  -- step 2: `s` read in trivialization `e` is `C^k`
  have h‚ÇÅ : CMDiffAt k (fun x ‚Ü¶ (e (s x)).2) x := e.contMDiffAt_section_iff hxe |>.1 hs
  -- step 3: `b.repr` is a linear map, so the composition is smooth
  let bas := fun v ‚Ü¶ b.repr v i
  let basl : F ‚Üí‚Çó[ùïú] ùïú := {
    toFun := bas
    map_add' m m' := by simp [bas]
    map_smul' m x := by simp [bas]
  }
  let basL : F ‚ÜíL[ùïú] ùïú := {
    toLinearMap := basl
    cont := basl.continuous_of_finiteDimensional
  }
  have hbas : ContMDiffAt ùìò(ùïú, F) ùìò(ùïú) k basL (e (s x)).2 :=
    contMDiffAt_iff_contDiffAt.mpr <| (basL.contDiff (n := k)).contDiffAt
  exact hbas.comp x h‚ÇÅ

omit [IsManifold I 0 M] in
/-- If `s` is `C^k` on `t ‚äÜ e.baseSet`, so is its coefficient `b.localFrame_repr e i`
in the local frame induced by `e` -/
lemma contMDiffOn_localFrame_repr [FiniteDimensional ùïú F] [CompleteSpace ùïú] (b : Basis Œπ ùïú F)
    {s : Œ† x : M,  V x} {k : WithTop ‚Ñï‚àû} {t : Set M} [ContMDiffVectorBundle k F V I]
    (ht : IsOpen t) (ht' : t ‚äÜ e.baseSet)
    (hs : CMDiff[t] k (T% s)) (i : Œπ) : CMDiff[t] k (b.localFrame_repr I e i s) :=
  fun _ hx ‚Ü¶ (contMDiffAt_localFrame_repr (ht' hx) b
    (hs.contMDiffAt (ht.mem_nhds hx)) i).contMDiffWithinAt

omit [IsManifold I 0 M] in -- [ContMDiffVectorBundle n F V I] in
/-- If `s` is `C^k` on `e.baseSet`, so is its coefficient `b.localFrame_repr e i` in the local frame
induced by `e` -/
lemma contMDiffOn_baseSet_localFrame_repr [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x} {k : WithTop ‚Ñï‚àû} [ContMDiffVectorBundle k F V I]
    (hs : CMDiff[e.baseSet] k (T% s)) (i : Œπ) : CMDiff[e.baseSet] k (b.localFrame_repr I e i s) :=
  contMDiffOn_localFrame_repr b e.open_baseSet (subset_refl _) hs _

omit [IsManifold I 0 M] in
/-- A section `s` of `V` is `C^k` at `x ‚àà e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma contMDiffAt_iff_localFrame_repr [Fintype Œπ] [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x} {k : WithTop ‚Ñï‚àû} [ContMDiffVectorBundle k F V I]
    {x' : M} (hx : x' ‚àà e.baseSet) :
    CMDiffAt k (T% s) x' ‚Üî ‚àÄ i, CMDiffAt k (b.localFrame_repr I e i s) x' :=
  ‚ü®fun h i ‚Ü¶ contMDiffAt_localFrame_repr hx b h i,
    fun hi ‚Ü¶ (b.localFrame_isLocalFrameOn_baseSet I k e).contMDiffAt_of_repr hi
    (e.open_baseSet.mem_nhds hx)‚ü©

omit [IsManifold I 0 M] in
/-- A section `s` of `V` is `C^k` on `t ‚äÜ e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma contMDiffOn_iff_localFrame_repr [Fintype Œπ] [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x} {k : WithTop ‚Ñï‚àû} [ContMDiffVectorBundle k F V I]
    {t : Set M} (ht : IsOpen t) (ht' : t ‚äÜ e.baseSet) :
    CMDiff[t] k (T% s) ‚Üî ‚àÄ i, CMDiff[t] k (b.localFrame_repr I e i s) := by
  refine ‚ü®fun h i ‚Ü¶ contMDiffOn_localFrame_repr b ht ht' h i, fun hi ‚Ü¶ ?_‚ü©
  -- TODO: golf this using the lemmas above
  -- intro x hx
  -- let aux := (b.localFrame_isLocalFrameOn_baseSet I k e).contMDiffAt_of_repr (t := s) (x := x)
  have this (i) : CMDiff[t] k (T% ((b.localFrame_repr I e i) s ‚Ä¢ b.localFrame e i)) :=
    (hi i).smul_section ((b.contMDiffOn_localFrame_baseSet k e i).mono ht')
  let rhs := fun x' ‚Ü¶ ‚àë i, (b.localFrame_repr I e i) s x' ‚Ä¢ b.localFrame e i x'
  have almost : CMDiff[t] k (T% rhs) := .sum_section fun i _ ‚Ü¶ this i
  apply almost.congr
  intro y hy
  simpa using b.localFrame_repr_sum_eq s (ht' hy)

omit [IsManifold I 0 M] in
/-- A section `s` of `V` is `C^k` on a trivialisation domain `e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma contMDiffOn_baseSet_iff_localFrame_repr [Fintype Œπ] [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x} {k : WithTop ‚Ñï‚àû} [ContMDiffVectorBundle k F V I] :
    CMDiff[e.baseSet] k (T% s) ‚Üî ‚àÄ i, CMDiff[e.baseSet] k (b.localFrame_repr I e i s) := by
  rw [contMDiffOn_iff_localFrame_repr b e.open_baseSet (subset_refl _)]

-- Differentiability of a section can be checked in terms of its local frame coefficients
section MDifferentiable

omit [IsManifold I 0 M] in
/-- If `s` is diffentiable at `x`, so is its coefficient `b.localFrame_repr e i` in the local frame
near `x` induced by `e` and `b` -/
lemma mdifferentiableAt_localFrame_repr [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (hxe : x ‚àà e.baseSet) (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x} (hs : MDiffAt (T% s) x) (i : Œπ) :
    MDiffAt (b.localFrame_repr I e i s) x := by
  -- This boils down to computing the frame coefficients in a local trivialisation.
  classical
  -- step 1: on e.baseSet, can compute the coefficient very well
  let aux := fun x ‚Ü¶ b.repr (e (s x)).2 i
  -- Since e.baseSet is open, this is sufficient.
  suffices MDiffAt aux x by
    apply this.congr_of_eventuallyEq
    apply eventuallyEq_of_mem (s := e.baseSet) (by simp [e.open_baseSet.mem_nhds hxe])
    intro y hy
    simp [aux, Basis.localFrame_repr_eq_repr hy]
  simp only [aux]

  -- step 2: `s` read in trivialization `e` is differentiable
  have h‚ÇÅ : MDiffAt (fun x ‚Ü¶ (e (s x)).2) x := e.mdifferentiableAt_section_iff I s hxe |>.1 hs
  -- step 3: `b.repr` is a linear map, so the composition is smooth
  let bas := fun v ‚Ü¶ b.repr v i
  let basl : F ‚Üí‚Çó[ùïú] ùïú := {
    toFun := bas
    map_add' m m' := by simp [bas]
    map_smul' m x := by simp [bas]
  }
  let basL : F ‚ÜíL[ùïú] ùïú := {
    toLinearMap := basl
    cont := basl.continuous_of_finiteDimensional
  }
  have hbas : MDifferentiableAt ùìò(ùïú, F) ùìò(ùïú) basL (e (s x)).2 :=
    mdifferentiableAt_iff_differentiableAt.mpr (basL.differentiable _)
  exact hbas.comp x h‚ÇÅ

omit [IsManifold I 0 M] in
/-- If `s` is differentiable on `t ‚äÜ e.baseSet`, so is its coefficient `b.localFrame_repr e i`
in the local frame induced by `e` -/
lemma mdifferentiableOn_localFrame_repr [FiniteDimensional ùïú F] [CompleteSpace ùïú] (b : Basis Œπ ùïú F)
    {s : Œ† x : M,  V x} {t : Set M}
    (ht : IsOpen t) (ht' : t ‚äÜ e.baseSet) (hs : MDiff[t] (T% s)) (i : Œπ) :
    MDiff[t] (b.localFrame_repr I e i s) :=
  fun _ hx ‚Ü¶ (mdifferentiableAt_localFrame_repr (ht' hx) b
    (hs.mdifferentiableAt (ht.mem_nhds hx)) i).mdifferentiableWithinAt

omit [IsManifold I 0 M] in
/-- If `s` is differentiable on `e.baseSet`, so is its coefficient `b.localFrame_repr e i` in the
local frame induced by `e` -/
lemma mdifferentiableOn_baseSet_localFrame_repr [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x}
    (hs : MDiff[e.baseSet] (T% s)) (i : Œπ) :
    MDiff[e.baseSet] (b.localFrame_repr I e i s) :=
  mdifferentiableOn_localFrame_repr b e.open_baseSet (subset_refl _) hs _

omit [IsManifold I 0 M] in
/-- A section `s` of `V` is differentiable at `x ‚àà e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma mdifferentiableAt_iff_localFrame_repr [Fintype Œπ] [FiniteDimensional ùïú F] [CompleteSpace ùïú]
    (b : Basis Œπ ùïú F) {s : Œ† x : M,  V x} {x' : M} (hx : x' ‚àà e.baseSet) :
    MDiffAt (T% s) x' ‚Üî ‚àÄ i, MDiffAt (b.localFrame_repr I e i s) x' :=
  ‚ü®fun h i ‚Ü¶ mdifferentiableAt_localFrame_repr hx b h i, fun hi ‚Ü¶
    (b.localFrame_isLocalFrameOn_baseSet I 1 e).mdifferentiableAt_of_repr_aux hi e.open_baseSet hx‚ü©

end MDifferentiable

end
