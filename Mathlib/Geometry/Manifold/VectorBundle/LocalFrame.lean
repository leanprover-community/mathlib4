/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Michael Rothgang
-/
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.Algebra.Monoid

/-!
# Local frames in a vector bundle

Let `V â†’ M` be a finite rank smooth vector bundle with standard fiber `F`.
Given a basis `b` for `F` and a local trivialisation `e` for `V`,
we construct a **smooth local frame** on `V` w.r.t. `e` and `b`,
i.e. a collection of sections `s_i` of `V` which is smooth on `e.baseSet` such that `{s_i x}` is a
basis of `V x` for each `x âˆˆ e.baseSet`. Any section `s` of `e` can be uniquely written as
`s = âˆ‘ i, f^i s_i` near `x`, and `s` is smooth at `x` iff the functions `f^i` are.

We use this to construct local extensions of a vector to a section which is smooth on the
trivialisation domain.

## Main definitions and results
* `Basis.localFrame e b`: the local frame on `V` w.r.t. a local trivialisation `e` of `V` and a
  basis `b` of `F`. Use `b.localFrame e i` to access the i-th section in that frame.
* `b.contMDiffOn_localFrame_baseSet`: each section `b.localFrame e i` is smooth on `e.baseSet`
* `b.localFrame_toBasis_at e`: for each `x âˆˆ e.baseSet`, the vectors `b.localFrame e i x` form
  a basis of `F`
* `Basis.localFrame_repr e b i` describes the coefficient of sections of `V` w.r.t.`b.localFrame e`:
  `b.localFrame e i` is a linear map from sections of `V` to functions `M â†’ ğ•œ`.
* `b.localFrame_repr_spec e`: near `x`, we have
  `s = âˆ‘ i, (b.localFrame_repr e i s) â€¢ b.localFrame e i`
* `b.localFrame_repr_congr e`: the coefficient `b.localFrame_repr e b i` of `s` in the local frame
  induced by `e` and `b` at `x` only depends on `s` at `x`.
* `b.contMDiffOn_localFrame_repr`: if `s` is a `C^k` section, each coefficient
  `b.localFrame_repr e i s` is `C^k` on `e.baseSet`
* `b.contMDiffAt_iff_localFrame_repr e`: a section `s` is `C^k` at `x âˆˆ e.baseSet`
  iff all of its frame coefficients are
* `b.contMDiffOn_iff_localFrame_repr e`: a section `s` is `C^k` on an open set `t âŠ† e.baseSet`
  iff all of its frame coefficients are

* TODO: mention all the localExtensionOn definitions and results

TODO add a more complete doc-string!

## Implementation notes
* local frames use the junk value pattern: they are defined on all of `M`, but their value is
  only meaningful inside `e.baseSet`
* something about local extensions (and different fields)

## Tags
vector bundle, local frame, smoothness

-/
open Bundle Filter Function Topology

open scoped Bundle Manifold ContDiff

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I 0 M]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  -- `F` model fiber
  (n : WithTop â„•âˆ)
  {V : M â†’ Type*} [TopologicalSpace (TotalSpace F V)]
  [âˆ€ x, AddCommGroup (V x)] [âˆ€ x, Module ğ•œ (V x)]
  [âˆ€ x : M, TopologicalSpace (V x)]
  -- not needed in this file
  -- [âˆ€ x, IsTopologicalAddGroup (V x)] [âˆ€ x, ContinuousSMul ğ•œ (V x)]
  [FiberBundle F V] [VectorBundle ğ•œ F V] [ContMDiffVectorBundle n F V I]
  -- `V` vector bundle

namespace Basis

variable {Î¹ : Type*}

noncomputable def localFrame_toBasis_at
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e]
    (b : Basis Î¹ ğ•œ F) {x : M} (hx : x âˆˆ e.baseSet) : Basis Î¹ ğ•œ (V x) :=
  b.map (e.linearEquivAt (R := ğ•œ) x hx).symm


open scoped Classical in
-- If x is outside of `e.baseSet`, this returns the junk value 0.
noncomputable def localFrame
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e]
    (b : Basis Î¹ ğ•œ F) : Î¹ â†’ (x : M) â†’ V x := fun i x â†¦
  -- idea: take the vector b i and apply the trivialisation e to it.
  if hx : x âˆˆ e.baseSet then b.localFrame_toBasis_at e hx i else 0

-- TODO: understand why this isnâ€™t already a simp lemma
attribute [simp] Trivialization.apply_mk_symm

omit [IsManifold I 0 M] [ContMDiffVectorBundle n F V I] in
/-- Each local frame `s^i âˆˆ Î“(E)` of a `C^k` vector bundle, defined by a local trivialisation `e`,
is `C^k` on `e.baseSet`. -/
lemma contMDiffOn_localFrame_baseSet
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e] (b : Basis Î¹ ğ•œ F) (i : Î¹) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n
      (fun x â†¦ TotalSpace.mk' F x (b.localFrame e i x)) e.baseSet := by
  rw [contMDiffOn_section_of_mem_baseSetâ‚€]
  apply (contMDiffOn_const (c := b i)).congr
  intro y hy
  simp [localFrame, hy, localFrame_toBasis_at]

@[simp]
lemma localFrame_apply_of_mem_baseSet
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e] (b : Basis Î¹ ğ•œ F) {i : Î¹} {x : M} (hx : x âˆˆ e.baseSet) :
    b.localFrame e i x = b.localFrame_toBasis_at e hx i := by
  simp [localFrame, hx]

@[simp]
lemma localFrame_apply_of_notMem
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e] (b : Basis Î¹ ğ•œ F) {i : Î¹} {x : M} (hx : x âˆ‰ e.baseSet) :
    b.localFrame e i x = 0 := by
  simp [localFrame, hx]


lemma localFrame_toBasis_at_coe
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e]
    (b : Basis Î¹ ğ•œ F) {x : M} (i : Î¹) (hx : x âˆˆ e.baseSet) :
    b.localFrame_toBasis_at e hx i = b.localFrame e i x := by simp [hx]

-- XXX: is this result actually needed now? perhaps not, because of the toBasis definition?
/-- At each point `x âˆˆ M`, the sections `{sâ±(x)}` of a local frame form a basis for `V x`. -/
def isBasis_localFrame
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e]
    (b : Basis Î¹ ğ•œ F) : sorry := by
  -- the b i form a basis of F,
  -- and the trivialisation e is a linear equivalence (thus preserves bases)
  sorry

open scoped Classical in
/-- Coefficients of a section `s` of `V` w.r.t. the local frame `b.localFrame e i` -/
-- If x is outside of `e.baseSet`, this returns the junk value 0.
-- NB. We don't use simps here, as we prefer to have dedicated `_apply` lemmas for the separate
-- cases.
noncomputable def localFrame_repr
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M))
    [MemTrivializationAtlas e]
    (b : Basis Î¹ ğ•œ F) (i : Î¹) : (Î  x : M, V x) â†’â‚—[ğ•œ] M â†’ ğ•œ where
  toFun s x := if hx : x âˆˆ e.baseSet then (b.localFrame_toBasis_at e hx).repr (s x) i else 0
  map_add' s s' := by
    ext x
    by_cases hx : x âˆˆ e.baseSet <;> simp [hx]
  map_smul' c s := by
    ext x
    by_cases hx : x âˆˆ e.baseSet <;> simp [hx]

variable {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M)}
    [MemTrivializationAtlas e] {b : Basis Î¹ ğ•œ F}

variable (e b) in
@[simp]
lemma localFrame_repr_apply_of_notMem_baseSet {x : M}
    (hx : x âˆ‰ e.baseSet) (s : Î  x : M, V x) (i : Î¹) : b.localFrame_repr e i s x = 0 := by
  simpa [localFrame_repr] using fun hx' â†¦ (hx hx').elim

variable (e b) in
@[simp]
lemma localFrame_repr_apply_of_mem_baseSet {x : M}
    (hx : x âˆˆ e.baseSet) (s : Î  x : M, V x) (i : Î¹) :
    b.localFrame_repr e i s x = (b.localFrame_toBasis_at e hx).repr (s x) i := by
  simp [localFrame_repr, hx]

-- uniqueness of the decomposition: follows from the IsBasis property above

-- TODO: better name?
lemma localFrame_repr_sum_eq [Fintype Î¹] (s : Î  x : M,  V x) {x'} (hx : x' âˆˆ e.baseSet) :
    s x' = (âˆ‘ i, (b.localFrame_repr e i s x') â€¢ b.localFrame e i x') := by
  simp [Basis.localFrame_repr, hx]
  exact (sum_repr (localFrame_toBasis_at e b hx) (s x')).symm

variable (b) in
/-- A local frame locally spans the space of sections for `V`: for each local trivialisation `e`
  of `V` around `x`, we have `s = âˆ‘ i, (b.localFrame_repr e i s) â€¢ b.localFrame e i` -/
lemma localFrame_repr_spec [Fintype Î¹] {x : M} (hxe : x âˆˆ e.baseSet) (s : Î  x : M,  V x) :
    âˆ€á¶  x' in ğ“ x, s x' = âˆ‘ i, (b.localFrame_repr e i s x') â€¢ b.localFrame e i x' :=
  eventually_nhds_iff.mpr âŸ¨e.baseSet, fun _ h â†¦ localFrame_repr_sum_eq s h, e.open_baseSet, hxeâŸ©

variable {Î¹ : Type*} {x : M}
  {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M)}
  [MemTrivializationAtlas e]

/-- The representation of `s` in a local frame at `x` only depends on `s` at `x`. -/
lemma localFrame_repr_congr (b : Basis Î¹ ğ•œ F)
    {s s' : Î  x : M,  V x} {i : Î¹} (hss' : s x = s' x) :
    b.localFrame_repr e i s x = b.localFrame_repr e i s' x := by
  by_cases hxe : x âˆˆ e.baseSet
  Â· simp [localFrame_repr, hxe, localFrame_toBasis_at]
    congr
  Â· simp [localFrame_repr, hxe]

lemma localFrame_repr_apply_zero_at
    (b : Basis Î¹ ğ•œ F) {s : Î  x : M, V x} (hs : s x = 0) (i : Î¹) :
    b.localFrame_repr e i s x = 0 := by
  rw [b.localFrame_repr_congr (s' := 0) (by simp [hs])]
  simp
  -- This proof may indicate a missing simp lemma.
  -- by_cases hxe : x âˆˆ e.baseSet; swap
  -- Â· simp [localFrame_repr, hxe]
  -- simp [localFrame_repr, localFrame_toBasis_at, hxe, hs]
  -- have : e.symm x = 0 := sorry
  -- have : (e { proj := x, snd := 0 }).2 = 0 := by
  --   trans (e { proj := x, snd := e.symm x 0 }).2
  --   Â· simp [this]
  --   Â· simp [e.apply_mk_symm hxe]
  -- simp [this]

variable {n}

/-- Suppose `e` is a compatible trivialisation around `x âˆˆ M`, and `s` a bundle section.
Then the coefficient of `s` w.r.t. the local frame induced by `b` and `e`
equals the cofficient of "`s x` read in the trivialisation `e`" for `b i`. -/
lemma localFrame_repr_eq_repr (hxe : x âˆˆ e.baseSet) (b : Basis Î¹ ğ•œ F) {i : Î¹} {s : Î  x : M, V x} :
    b.localFrame_repr e i s x = b.repr (e (s x)).2 i := by
  simp [b.localFrame_repr_apply_of_mem_baseSet e hxe, Basis.localFrame_toBasis_at]

omit [IsManifold I 0 M] [ContMDiffVectorBundle n F V I] in
/-- If `s` is `C^k` at `x`, so is its coefficient `b.localFrame_repr e i` in the local frame
near `x` induced by `e` and `b` -/
lemma contMDiffAt_localFrame_repr [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ]
    (hxe : x âˆˆ e.baseSet) (b : Basis Î¹ ğ•œ F)
    {s : Î  x : M,  V x} {k : WithTop â„•âˆ}
    (hs : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦ TotalSpace.mk' F x (s x)) x)
    (i : Î¹) : ContMDiffAt I ğ“˜(ğ•œ) k (b.localFrame_repr e i s) x := by
  -- This boils down to computing the frame coefficients in a local trivialisation.
  classical
  -- step 1: on e.baseSet, can compute the coefficient very well
  let aux := fun x â†¦ b.repr (e (s x)).2 i
  -- Since e.baseSet is open, this is sufficient.
  suffices ContMDiffAt I ğ“˜(ğ•œ) k aux x by
    apply this.congr_of_eventuallyEq_of_mem ?_ trivial
    apply eventuallyEq_of_mem (s := e.baseSet) (by simp [e.open_baseSet.mem_nhds hxe])
    intro y hy
    simp [aux, hy, Basis.localFrame_repr_eq_repr hy]
  simp only [aux]

  -- step 2: `s` read in trivialization `e` is `C^k`
  have hâ‚ : ContMDiffAt I ğ“˜(ğ•œ, F) k (fun x â†¦ (e (s x)).2) x := by
    rw [contMDiffAt_section_of_mem_baseSet hxe] at hs
    exact hs
  -- step 3: `b.repr` is a linear map, so the composition is smooth
  let bas := fun v â†¦ b.repr v i
  let basl : F â†’â‚—[ğ•œ] ğ•œ := {
    toFun := bas
    map_add' m m' := by simp [bas]
    map_smul' m x := by simp [bas]
  }
  let basL : F â†’L[ğ•œ] ğ•œ := {
    toLinearMap := basl
    cont := basl.continuous_of_finiteDimensional
  }
  have hbas : ContMDiffAt ğ“˜(ğ•œ, F) ğ“˜(ğ•œ) k basL (e (s x)).2 :=
    contMDiffAt_iff_contDiffAt.mpr <| (basL.contDiff (n := k)).contDiffAt
  exact hbas.comp x hâ‚

omit [IsManifold I 0 M] [ContMDiffVectorBundle n F V I] in
/-- If `s` is `C^k` on `t âŠ† e.baseSet`, so is its coefficient `b.localFrame_repr e i`
in the local frame induced by `e` -/
lemma contMDiffOn_localFrame_repr [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ] (b : Basis Î¹ ğ•œ F)
    {s : Î  x : M,  V x} {k : WithTop â„•âˆ} {t : Set M}
    (ht : IsOpen t) (ht' : t âŠ† e.baseSet)
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦ TotalSpace.mk' F x (s x)) t) (i : Î¹) :
    ContMDiffOn I ğ“˜(ğ•œ) k (b.localFrame_repr e i s) t :=
  fun _ hx â†¦ (b.contMDiffAt_localFrame_repr (ht' hx)
    (hs.contMDiffAt (ht.mem_nhds hx)) i).contMDiffWithinAt

omit [IsManifold I 0 M] [ContMDiffVectorBundle n F V I] in
/-- If `s` is `C^k` on `e.baseSet`, so is its coefficient `b.localFrame_repr e i` in the local frame
induced by `e` -/
lemma contMDiffOn_baseSet_localFrame_repr [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ]
    (b : Basis Î¹ ğ•œ F) {s : Î  x : M,  V x} {k : WithTop â„•âˆ}
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦ TotalSpace.mk' F x (s x)) e.baseSet) (i : Î¹) :
    ContMDiffOn I ğ“˜(ğ•œ) k (b.localFrame_repr e i s) e.baseSet :=
  contMDiffOn_localFrame_repr b e.open_baseSet (subset_refl _) hs _

/-- A section `s` of `V` is `C^k` at `x âˆˆ e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma contMDiffAt_iff_localFrame_repr [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ] (b : Basis Î¹ ğ•œ F)
    {s : Î  x : M,  V x} {k : WithTop â„•âˆ} {x' : M} (hx : x' âˆˆ e.baseSet) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦ TotalSpace.mk' F x (s x)) x' â†”
    âˆ€ i, ContMDiffAt I ğ“˜(ğ•œ) k (b.localFrame_repr e i s) x' := by
  refine âŸ¨fun h i â†¦ b.contMDiffAt_localFrame_repr hx h i, fun i â†¦ ?_âŸ©
  -- needs two missing API lemmas, see below
  sorry

/-- A section `s` of `V` is `C^k` on `t âŠ† e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma contMDiffOn_iff_localFrame_repr [Fintype Î¹] [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ]
    (b : Basis Î¹ ğ•œ F) {s : Î  x : M,  V x} {k : WithTop â„•âˆ} {t : Set M}
    (ht : IsOpen t) (ht' : t âŠ† e.baseSet) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦ TotalSpace.mk' F x (s x)) t â†”
    âˆ€ i, ContMDiffOn I ğ“˜(ğ•œ) k (b.localFrame_repr e i s) t := by
  refine âŸ¨fun h i â†¦ contMDiffOn_localFrame_repr b ht ht' h i, fun i â†¦ ?_âŸ©

  have inner (i) : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦
      TotalSpace.mk' F x ((localFrame_repr e b i) s x â€¢ localFrame e b i x)) t := by
    -- lemma localFrame_repr is smooth, localFrame is smooth => scalar product is
    -- does this already exist? if not, missing API!
    sorry
  let rhsâ‚€ (i) := fun x' â†¦ (localFrame_repr e b i) s x' â€¢ localFrame e b i x'
  let rhs := fun x' â†¦ âˆ‘ i, rhsâ‚€ i x'
  have almost : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) k
      (fun x â†¦ TotalSpace.mk' F x (rhs x)) t := by
    unfold rhs
    -- TODO: add a dependent function version of contMDiffOn_finsum, for sections of a vector bundle
    -- have aux := contMDiffOn_finsum (I' := I) (I := I.prod ğ“˜(ğ•œ, F)) (f := fun i x â†¦ rhsâ‚€ i x)
    sorry
  apply almost.congr
  intro y hy
  congr
  exact localFrame_repr_sum_eq s (ht' hy)

/-- A section `s` of `V` is `C^k` on a trivialisation domain `e.baseSet` iff each of its
coefficients `b.localFrame_repr e i s` in a local frame near `x` is -/
lemma contMDiffOn_baseSet_iff_localFrame_repr [Fintype Î¹] [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ]
    (b : Basis Î¹ ğ•œ F) {s : Î  x : M,  V x} {k : WithTop â„•âˆ} :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) k (fun x â†¦ TotalSpace.mk' F x (s x)) e.baseSet â†”
    âˆ€ i, ContMDiffOn I ğ“˜(ğ•œ) k (b.localFrame_repr e i s) e.baseSet := by
  rw [b.contMDiffOn_iff_localFrame_repr e.open_baseSet (subset_refl _)]

end Basis

-- local extension of a vector field in a trivialisation's base set
section extendLocally

variable {Î¹ : Type*} [Fintype Î¹] {b : Basis Î¹ ğ•œ F}
  {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M)}
  [MemTrivializationAtlas e] {x : M}

open scoped Classical in
-- TODO: add longer docs!
-- a starting point (not fully updated any more) is this:
/- Extend a vector `v âˆˆ V x` to a section of the bundle `V`, whose value at `x` is `v`.
The details of the extension are mostly unspecified: for covariant derivatives, the value of
`s` at points other than `x` will not matter (except for shorter proofs).
Thus, we choose `s` to be somewhat nice: our chosen construction is linear in `v`.
-/

-- comment: need not be smooth (outside of e.baseSet), but this is a useful building block for
-- global smooth extensions of vector fields
-- the latter caps this with a smooth bump function, which need not exist if k=C
-- In contrast, this definition makes sense over any field
-- (for example, *locally* holomorphic sections always exist),

/--
Extend a vector `v âˆˆ V x` to a local section of `V`, w.r.t. a chosen local trivialisation.
This construction uses a choice of local frame near `x`, w.r.t. to a basis `b` of `F` and a
compatible local trivialisation `e` of `V` near `x`: the resulting extension has constant
coefficients on `e.baseSet` w.r.t. this trivialisation (and is zero otherwise).

In particular, our construction is smooth on `e.baseSet`, and linear in the input vector `v`.
-/
noncomputable def localExtensionOn (b : Basis Î¹ ğ•œ F)
    (e : Trivialization F (TotalSpace.proj : TotalSpace F V â†’ M)) [MemTrivializationAtlas e]
    (x : M) (v : V x) : (x' : M) â†’ V x' :=
  fun x' â†¦ if hx : x âˆˆ e.baseSet then
    letI bV := b.localFrame_toBasis_at e hx; âˆ‘ i, bV.repr v i â€¢ b.localFrame e i x'
    else 0

variable (b e) in
@[simp]
lemma localExtensionOn_apply_self (hx : x âˆˆ e.baseSet) (v : V x) :
    ((localExtensionOn b e x v) x) = v := by
  simp [localExtensionOn, hx]
  nth_rw 2 [â† (b.localFrame_toBasis_at e hx).sum_repr v]

/-- A local extension has constant frame coefficients within its defining trivialisation. -/
lemma localExtensionOn_localFrame_repr (b : Basis Î¹ ğ•œ F)
    {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F V â†’ M)}
    [MemTrivializationAtlas e] {x : M} (hx : x âˆˆ e.baseSet) (v : V x) (i : Î¹)
    {x' : M} (hx' : x' âˆˆ e.baseSet):
    b.localFrame_repr e i (localExtensionOn b e x v) x' =
      b.localFrame_repr e i (localExtensionOn b e x v) x := by
  simp [localExtensionOn, hx, hx']

-- By construction, localExtensionOn is a linear map.

variable (b e) in
lemma localExtensionOn_add (v v' : V x) :
    localExtensionOn b e x (v + v') = localExtensionOn b e x v + localExtensionOn b e x v' := by
  ext x'
  by_cases hx: x âˆˆ e.baseSet; swap
  Â· simp [hx, localExtensionOn]
  Â· simp [hx, localExtensionOn, add_smul, Finset.sum_add_distrib]

variable (b e) in
lemma localExtensionOn_smul (a : ğ•œ) (v : V x) :
    localExtensionOn b e x (a â€¢ v) = a â€¢ localExtensionOn b e x v := by
  ext x'
  by_cases hx: x âˆˆ e.baseSet; swap
  Â· simp [hx, localExtensionOn]
  Â· simp [hx, localExtensionOn, Finset.smul_sum]
    set B := Basis.localFrame_toBasis_at e b hx
    congr
    ext i
    rw [mul_smul a ((B.repr v) i)]

variable (F) in
omit [IsManifold I 0 M] in
lemma contMDiffOn_localExtensionOn [FiniteDimensional ğ•œ F] [CompleteSpace ğ•œ]
    {x : M} (hx : x âˆˆ e.baseSet) (v : V x) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) 1
    (fun x' â†¦ TotalSpace.mk' F x' (localExtensionOn b e x v x')) e.baseSet := by
  -- The local frame coefficients of `localExtensionOn` w.r.t. the frame induced by `e` are
  -- constant, hence smoothness follows.
  rw [b.contMDiffOn_baseSet_iff_localFrame_repr]
  intro i
  apply (contMDiffOn_const (c := (b.localFrame_repr e i) (localExtensionOn b e x v) x)).congr
  intro y hy
  rw [localExtensionOn_localFrame_repr b hx v i hy]

end extendLocally
