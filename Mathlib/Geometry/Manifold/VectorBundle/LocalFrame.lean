/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Michael Rothgang
-/
import Mathlib.Geometry.Manifold.Algebra.Monoid
import Mathlib.Geometry.Manifold.Notation
import Mathlib.Geometry.Manifold.VectorBundle.MDifferentiable
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection


/-!
# Local frames in a vector bundle

Let `V ‚Üí M` be a finite rank smooth vector bundle with standard fiber `F`.
A family of sections `s i` of `V ‚Üí M` is called a **C^k local frame** on a set `U ‚äÜ M` iff each
section `s i` is `C^k` on `U`, and the section values `s i x` form a basis for each `x ‚àà U`.
We define a predicate `IsLocalFrame` for a collection of sections to be a local frame on a set,
and define basic notions (such as the coefficients of a section w.r.t. a local frame, and
checking the smoothness of `t` via its coefficients in a local frame).

In future work, given a basis `b` for `F` and a local trivialisation `e` for `V`,
we will construct a **smooth local frame** on `V` w.r.t. `e` and `b`,
i.e. a collection of sections `s·µ¢` of `V` which is smooth on `e.baseSet` such that `{s·µ¢ x}` is a
basis of `V x` for each `x ‚àà e.baseSet`. Any section `s` of `e` can be uniquely written as
`s = ‚àë i, f^i s·µ¢` near `x`, and `s` is smooth at `x` iff the functions `f^i` are.

In this file, we prove the latter statement for finite-rank bundles (with coefficients in a
complete field). In `OrthonormalFrame.lean`, we will prove the same for real vector bundles of any
rank which admit a `C^n` bundle metric. This includes bundles of finite rank, modelled on a Hilbert
space or on a Banach space which has smooth partitions of unity.

We will use this to construct local extensions of a vector to a section which is smooth on the
trivialisation domain.

## Main definitions and results

* `IsLocalFrameOn`: a family of sections `s i` of `V ‚Üí M` is called a **C^k local frame** on a set
  `U ‚äÜ M` iff each section `s i` is `C^k` on `U`, and the section values `s i x` form a basis for
  each `x ‚àà U`

Suppose `{s·µ¢}` is a local frame on `U`, and `hs : IsLocalFrameOn s U`.
* `IsLocalFrameOn.toBasisAt hs`: for each `x ‚àà U`, the vectors `s·µ¢ x` form a basis of `F`
* `IsLocalFrameOn.coeff hs` describes the coefficient of sections of `V` w.r.t. `{s·µ¢}`.
  `hs.coeff i` is a linear map from sections of `V` to functions `M ‚Üí ùïú`.
* `IsLocalFrameOn.eventually_eq_sum_coeff_smul hs`: for a local frame `{s·µ¢}` near `x`,
  for each section `t` we have `t = ‚àë i, (hs.coeff i t) ‚Ä¢ s·µ¢`.
* `IsLocalFrameOn.coeff_sum_eq hs t hx` proves that `t x = ‚àë i, (hs.coeff i t) x ‚Ä¢ s·µ¢ x`, provided
  that `hx : x ‚àà U`.
* `IsLocalFrameOn.coeff_congr hs`: the coefficient `hs.coeff i` of `t` in the local frame `{s·µ¢}`
  only depends on `t` at `x`.
* `IsLocalFrameOn.eq_iff_coeff hs`: two sections `t` and `t'` are equal at `x` if and only if their
  coefficients at `x` w.r.t. `{s·µ¢}` agree.
* `IsLocalFrameOn.contMDiffOn_of_coeff hs`: a section `t` is `C^k` on `U` if each coefficient
  `hs.coeff i t` is `C^k` on `U`
* `IsLocalFrameOn.contMDiffAt_of_coeff hs`: a section `t` is `C^k` at `x ‚àà U`
  if all of its frame coefficients are
* `IsLocalFrameOn.contMDiffOn_off_coeff hs`: a section `t` is `C^k` on an open set `t ‚äÜ U`
  ff all of its frame coefficients are
* `MDifferentiable` versions of the above three statements

# TODO
* `IsLocalFrameOn.contMDiffOn_coeff hs`: if `t` is a `C^k` section, each coefficient
  `hs.coeff i t` is `C^k` on `U`
* `IsLocalFrameOn.contMDiffAt_iff_coeff hs`: a section `t` is `C^k` at `x ‚àà U`
  iff all of its frame coefficients are
* `IsLocalFrameOn.contMDiffOn_iff_coeff hs`: a section `t` is `C^k` on an open set `t ‚äÜ U`
  iff all of its frame coefficients are
* a `MDifferentiable` version of each of these

## Implementation notes

Local frames use the junk value pattern: they are defined on all of `M`, but their value is
only meaningful on the set on which they are a local frame.

## Tags
vector bundle, local frame, smoothness

-/
open Bundle Filter Function Topology Module

open scoped Bundle Manifold ContDiff

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ùïú E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I 0 M]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  -- `F` model fiber
  (n : WithTop ‚Ñï‚àû)
  {V : M ‚Üí Type*} [TopologicalSpace (TotalSpace F V)]
  [‚àÄ x, AddCommGroup (V x)] [‚àÄ x, Module ùïú (V x)]
  [‚àÄ x : M, TopologicalSpace (V x)]
  [FiberBundle F V] [VectorBundle ùïú F V] [ContMDiffVectorBundle n F V I]
  -- `V` vector bundle

noncomputable section

section IsLocalFrame

omit [IsManifold I 0 M] [VectorBundle ùïú F V]

variable {Œπ : Type*} {s s' : Œπ ‚Üí (x : M) ‚Üí V x} {u u' : Set M} {x : M} {n : WithTop ‚Ñï‚àû}

variable (I F n) in
/--
A family of sections `s i` of `V ‚Üí M` is called a **C^k local frame** on a set `U ‚äÜ M` iff
- the section values `s i x` form a basis for each `x ‚àà U`,
- each section `s i` is `C^k` on `U`.
-/
structure IsLocalFrameOn (s : Œπ ‚Üí (x : M) ‚Üí V x) (u : Set M) where
  linearIndependent {x : M} (hx : x ‚àà u) : LinearIndependent ùïú (s ¬∑ x)
  generating {x : M} (hx : x ‚àà u) : ‚ä§ ‚â§ Submodule.span ùïú (Set.range (s ¬∑ x))
  contMDiffOn (i : Œπ) : CMDiff[u] n (T% (s i))

namespace IsLocalFrameOn

/-- If `s = s'` on `u` and `s i` is a local frame on `u`, then so is `s'`. -/
lemma congr (hs : IsLocalFrameOn I F n s u) (hs' : ‚àÄ i, ‚àÄ x, x ‚àà u ‚Üí s i x = s' i x) :
    IsLocalFrameOn I F n s' u where
  linearIndependent := by
    intro x hx
    have := hs.linearIndependent hx
    simp_all
  generating := by
    intro x hx
    have := hs.generating hx
    simp_all
  contMDiffOn i := (hs.contMDiffOn i).congr (by simp +contextual [hs'])

lemma mono (hs : IsLocalFrameOn I F n s u) (hu'u : u' ‚äÜ u) : IsLocalFrameOn I F n s u' where
  linearIndependent := by
    intro x hx
    exact hs.linearIndependent (hu'u hx)
  generating := by
    intro x hx
    exact hs.generating (hu'u hx)
  contMDiffOn i := (hs.contMDiffOn i).mono hu'u

lemma contMDiffAt (hs : IsLocalFrameOn I F n s u) (hu : IsOpen u) (hx : x ‚àà u) (i : Œπ) :
    CMDiffAt n (T% (s i)) x :=
  (hs.contMDiffOn i).contMDiffAt <| hu.mem_nhds hx

/-- Given a local frame `{s i}` on `U ‚àã x`, returns the basis `{s i}` of `V x` -/
def toBasisAt (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) : Basis Œπ ùïú (V x) :=
  Basis.mk (hs.linearIndependent hx) (hs.generating hx)

@[simp]
lemma toBasisAt_coe (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) (i : Œπ) :
    toBasisAt hs hx i = s i x := by
  simpa only [toBasisAt] using Basis.mk_apply (hs.linearIndependent hx) (hs.generating hx) i

open scoped Classical in
/-- Coefficients of a section `s` of `V` w.r.t. a local frame `{s i}` on `u`.
Outside of `u`, this returns the junk value 0. -/
def coeff (hs : IsLocalFrameOn I F n s u) (i : Œπ) : (Œ† x : M, V x) ‚Üí‚Çó[ùïú] M ‚Üí ùïú where
  toFun s x := if hx : x ‚àà u then (hs.toBasisAt hx).repr (s x) i else 0
  map_add' s s' := by
    ext x
    by_cases hx : x ‚àà u <;> simp [hx]
  map_smul' c s := by
    ext x
    by_cases hx : x ‚àà u <;> simp [hx]

variable {x : M}

@[simp]
lemma coeff_apply_of_notMem (hs : IsLocalFrameOn I F n s u) (hx : x ‚àâ u) (t : Œ† x : M, V x)
    (i : Œπ) : hs.coeff i t x = 0 := by
  simp [coeff, hx]

@[simp]
lemma coeff_apply_of_mem (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) (t : Œ† x : M, V x) (i : Œπ) :
    hs.coeff i t x = (hs.toBasisAt hx).repr (t x) i := by
  simp [coeff, hx]

-- TODO: add uniqueness of the decomposition; follows from the IsBasis property in the definition

lemma coeff_sum_eq [Fintype Œπ] (hs : IsLocalFrameOn I F n s u) (t : Œ† x : M,  V x) (hx : x ‚àà u) :
    t x = ‚àë i, (hs.coeff i t x) ‚Ä¢ (s i x) := by
  simpa [coeff, hx] using (Basis.sum_repr (hs.toBasisAt hx) (t x)).symm

/-- A local frame locally spans the space of sections for `V`: for each local frame `s i` on an open
set `u` around `x`, we have `t = ‚àë i, (hs.coeff i t) ‚Ä¢ (s i x)` near `x`. -/
lemma eventually_eq_sum_coeff_smul [Fintype Œπ]
    (hs : IsLocalFrameOn I F n s u) (t : Œ† x : M,  V x) (hu'' : u ‚àà ùìù x) :
    ‚àÄ·∂† x' in ùìù x, t x' = ‚àë i, (hs.coeff i t x') ‚Ä¢ (s i x') :=
  eventually_of_mem hu'' fun _ hx ‚Ü¶ hs.coeff_sum_eq _ hx

variable {t t' : Œ† x : M, V x}

/-- The coefficients of `t` in a local frame at `x` only depend on `t` at `x`. -/
lemma coeff_congr (hs : IsLocalFrameOn I F n s u) (htt' : t x = t' x) (i : Œπ) :
    hs.coeff i t x = hs.coeff i t' x := by
  by_cases hxe : x ‚àà u
  ¬∑ simp [coeff, hxe]
    congr
  ¬∑ simp [coeff, hxe]

/-- If `s` and `s'` are local frames which are equal at `x`,
a section `t` has equal frame coefficients in them. -/
lemma coeff_eq_of_eq (hs : IsLocalFrameOn I F n s u) (hs' : IsLocalFrameOn I F n s' u)
    (hss' : ‚àÄ i, s i x = s' i x) {t : Œ† x : M,  V x} (i : Œπ) :
    hs.coeff i t x = hs'.coeff i t x := by
  by_cases hxe : x ‚àà u
  ¬∑ simp [coeff, hxe]
    simp_all [toBasisAt]
  ¬∑ simp [coeff, hxe]

/-- Two sections `s` and `t` are equal at `x` if and only if their coefficients w.r.t. some local
frame at `x` agree. -/
lemma eq_iff_coeff [Fintype Œπ] (hs : IsLocalFrameOn I F n s u) (hx : x ‚àà u) :
    t x = t' x ‚Üî ‚àÄ i, hs.coeff i t x = hs.coeff i t' x :=
  ‚ü®fun h i ‚Ü¶ hs.coeff_congr h i, fun h ‚Ü¶ by
    simp +contextual [h, hs.coeff_sum_eq t hx, hs.coeff_sum_eq t' hx]‚ü©

lemma coeff_apply_zero_at (hs : IsLocalFrameOn I F n s u) (ht : t x = 0) (i : Œπ) :
    hs.coeff i t x = 0 := by
  simp [hs.coeff_congr (t' := 0) ht]

variable (hs : IsLocalFrameOn I F n s u) [VectorBundle ùïú F V]

/-- Given a local frame `s i ` on `u`, if a section `t` has `C^k` coefficients on `u` w.r.t. `s i`,
then `t` is `C^n` on `u`. -/
lemma contMDiffOn_of_coeff [Fintype Œπ] (h : ‚àÄ i, CMDiff[u] n (hs.coeff i t)) :
    CMDiff[u] n (T% t) := by
  have this (i) : CMDiff[u] n (T% (hs.coeff i t ‚Ä¢ s i)) :=
    (h i).smul_section (hs.contMDiffOn i)
  have almost : CMDiff[u] n (T% (fun x ‚Ü¶ ‚àë i, (hs.coeff i t) x ‚Ä¢ s i x)) :=
    .sum_section fun i _ ‚Ü¶ this i
  apply almost.congr
  intro y hy
  simp [hs.coeff_sum_eq t hy]

/-- Given a local frame `s i` on a neighbourhood `u` of `x`,
if a section `t` has `C^k` coefficients at `x` w.r.t. `s i`, then `t` is `C^n` at `x`. -/
lemma contMDiffAt_of_coeff [Fintype Œπ]
    (h : ‚àÄ i, CMDiffAt n (hs.coeff i t) x) (hu : u ‚àà ùìù x) : CMDiffAt n (T% t) x := by
  have almost : CMDiffAt n (T% (fun x ‚Ü¶ ‚àë i, (hs.coeff i t) x ‚Ä¢ s i x)) x :=
    .sum_section (fun i _ ‚Ü¶ (h i).smul_section <| (hs.contMDiffOn i).contMDiffAt hu)
  exact almost.congr_of_eventuallyEq <| (hs.eventually_eq_sum_coeff_smul t hu).mono (by simp)

/-- Given a local frame `s i` on an open set `u` containing `x`, if a section `t` has `C^k`
coefficients at `x ‚àà u` w.r.t. `s i`, then `t` is `C^n` at `x`. -/
lemma contMDiffAt_of_coeff_aux [Fintype Œπ] (h : ‚àÄ i, CMDiffAt n (hs.coeff i t) x)
    (hu : IsOpen u) (hx : x ‚àà u) : CMDiffAt n (T% t) x :=
  hs.contMDiffAt_of_coeff h (hu.mem_nhds hx)

section

variable (hs : IsLocalFrameOn I F 1 s u)

/-- Given a local frame `s i ` on `u`, if a section `t` has differentiable coefficients on `u`
w.r.t. `s i`, then `t` is differentiable on `u`. -/
lemma mdifferentiableOn_of_coeff [Fintype Œπ] (h : ‚àÄ i, MDiff[u] (hs.coeff i t)) :
    MDiff[u] (T% t) := by
  have this (i) : MDiff[u] (T% (hs.coeff i t ‚Ä¢ s i)) :=
    (h i).smul_section ((hs.contMDiffOn i).mdifferentiableOn le_rfl)
  have almost : MDiff[u] (T% (fun x ‚Ü¶ ‚àë i, (hs.coeff i t) x ‚Ä¢ s i x)) :=
    .sum_section (fun i _ hx ‚Ü¶ this i _ hx)
  apply almost.congr
  intro y hy
  simp [hs.coeff_sum_eq t hy]

/-- Given a local frame `s i` on a neighbourhood `u` of `x`, if a section `t` has differentiable
coefficients at `x` w.r.t. `s i`, then `t` is differentiable at `x`. -/
lemma mdifferentiableAt_of_coeff [Fintype Œπ]
    (h : ‚àÄ i, MDiffAt (hs.coeff i t) x) (hu : u ‚àà ùìù x) : MDiffAt (T% t) x := by
  have almost : MDiffAt (T% (fun x ‚Ü¶ ‚àë i, (hs.coeff i t) x ‚Ä¢ s i x)) x :=
    .sum_section (fun i ‚Ü¶ (h i).smul_section <|
      ((hs.contMDiffOn i).mdifferentiableOn le_rfl).mdifferentiableAt hu)
  exact almost.congr_of_eventuallyEq <| (hs.eventually_eq_sum_coeff_smul t hu).mono (by simp)

/-- Given a local frame `s i` on open set `u` containing `x`, if a section `t`
has differentiable coefficients at `x ‚àà u` w.r.t. `s i`, then `t` is differentiable at `x`. -/
lemma mdifferentiableAt_of_coeff_aux [Fintype Œπ] (h : ‚àÄ i, MDiffAt (hs.coeff i t) x)
    (hu : IsOpen u) (hx : x ‚àà u) : MDiffAt (T% t) x :=
  hs.mdifferentiableAt_of_coeff h (hu.mem_nhds hx)

end

end IsLocalFrameOn

end IsLocalFrame
