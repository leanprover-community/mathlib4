/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Geometry.Manifold.VectorBundle.Basic

/-! # Tangent bundles

This file defines the tangent bundle as a `C^n` vector bundle.

Let `M` be a manifold with model `I` on `(E, H)`. The tangent space `TangentSpace I (x : M)` has
already been defined as a type synonym for `E`, and the tangent bundle `TangentBundle I M` as an
abbrev of `Bundle.TotalSpace E (TangentSpace I : M â†’ Type _)`.

In this file, when `M` is `C^1`, we construct a vector bundle structure
on `TangentBundle I M` using the `VectorBundleCore` construction indexed by the charts of `M`
with fibers `E`. Given two charts `i, j : OpenPartialHomeomorph M H`, the coordinate change
between `i` and `j` at a point `x : M` is the derivative of the composite
```
  I.symm   i.symm    j     I
E -----> H -----> M --> H --> E
```
within the set `range I âŠ† E` at `I (i x) : E`.
This defines a vector bundle `TangentBundle` with fibers `TangentSpace`.

## Main definitions and results

* `tangentBundleCore I M` is the vector bundle core for the tangent bundle over `M`.

* When `M` is a `C^{n+1}` manifold, `TangentBundle I M` has a `C^n` vector bundle
  structure over `M`. In particular, it is a topological space, a vector bundle, a fiber bundle,
  and a `C^n` manifold.
-/


open Bundle Set IsManifold OpenPartialHomeomorph ContinuousLinearMap

open scoped Manifold Topology Bundle ContDiff

noncomputable section

section General

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ} {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H : Type*}
  [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- Auxiliary lemma for tangent spaces: the derivative of a coordinate change between two charts is
  `C^n` on its source. -/
theorem contDiffOn_fderiv_coord_change [IsManifold I (n + 1) M]
    (i j : atlas H M) :
    ContDiffOn ğ•œ n (fderivWithin ğ•œ (j.1.extend I âˆ˜ (i.1.extend I).symm) (range I))
      ((i.1.extend I).symm â‰« j.1.extend I).source := by
  have h : ((i.1.extend I).symm â‰« j.1.extend I).source âŠ† range I := by
    rw [i.1.extend_coord_change_source]; apply image_subset_range
  intro x hx
  refine (ContDiffWithinAt.fderivWithin_right ?_ I.uniqueDiffOn le_rfl
    <| h hx).mono h
  refine (OpenPartialHomeomorph.contDiffOn_extend_coord_change (subset_maximalAtlas j.2)
    (subset_maximalAtlas i.2) x hx).mono_of_mem_nhdsWithin ?_
  exact i.1.extend_coord_change_source_mem_nhdsWithin j.1 hx

open IsManifold

variable [IsManifold I 1 M] [IsManifold I' 1 M']

variable (I M) in
/-- Let `M` be a `C^1` manifold with model `I` on `(E, H)`.
Then `tangentBundleCore I M` is the vector bundle core for the tangent bundle over `M`.
It is indexed by the atlas of `M`, with fiber `E` and its change of coordinates from the chart `i`
to the chart `j` at point `x : M` is the derivative of the composite
```
  I.symm   i.symm    j     I
E -----> H -----> M --> H --> E
```
within the set `range I âŠ† E` at `I (i x) : E`. -/
@[simps indexAt coordChange]
def tangentBundleCore : VectorBundleCore ğ•œ M E (atlas H M) where
  baseSet i := i.1.source
  isOpen_baseSet i := i.1.open_source
  indexAt := achart H
  mem_baseSet_at := mem_chart_source H
  coordChange i j x :=
    fderivWithin ğ•œ (j.1.extend I âˆ˜ (i.1.extend I).symm) (range I) (i.1.extend I x)
  coordChange_self i x hx v := by
    rw [Filter.EventuallyEq.fderivWithin_eq, fderivWithin_id', ContinuousLinearMap.id_apply]
    Â· exact I.uniqueDiffWithinAt_image
    Â· filter_upwards [i.1.extend_target_mem_nhdsWithin hx] with y hy
      exact (i.1.extend I).right_inv hy
    Â· simp_rw [Function.comp_apply, i.1.extend_left_inv hx]
  continuousOn_coordChange i j := by
    have : IsManifold I (0 + 1) M := by simpa
    refine (contDiffOn_fderiv_coord_change (n := 0) i j).continuousOn.comp
      (i.1.continuousOn_extend.mono ?_) ?_
    Â· rw [i.1.extend_source]; exact inter_subset_left
    simp_rw [â† i.1.extend_image_source_inter, mapsTo_image]
  coordChange_comp := by
    have : IsManifold I (0 + 1) M := by simpa
    rintro i j k x âŸ¨âŸ¨hxi, hxjâŸ©, hxkâŸ© v
    rw [fderivWithin_fderivWithin, Filter.EventuallyEq.fderivWithin_eq]
    Â· have := i.1.extend_preimage_mem_nhds (I := I) hxi (j.1.extend_source_mem_nhds (I := I) hxj)
      filter_upwards [nhdsWithin_le_nhds this] with y hy
      simp_rw [Function.comp_apply, (j.1.extend I).left_inv hy]
    Â· simp_rw [Function.comp_apply, i.1.extend_left_inv hxi, j.1.extend_left_inv hxj]
    Â· exact (contDiffWithinAt_extend_coord_change' (subset_maximalAtlas k.2)
        (subset_maximalAtlas j.2) hxk hxj).differentiableWithinAt le_rfl
    Â· exact (contDiffWithinAt_extend_coord_change' (subset_maximalAtlas j.2)
        (subset_maximalAtlas i.2) hxj hxi).differentiableWithinAt le_rfl
    Â· intro x _; exact mem_range_self _
    Â· exact I.uniqueDiffWithinAt_image
    Â· rw [Function.comp_apply, i.1.extend_left_inv hxi]

/-- `simp`-normal form is `tangentBundleCore_localTriv_baseSet`. -/
theorem tangentBundleCore_baseSet (i) : (tangentBundleCore I M).baseSet i = i.1.source := rfl

@[simp]
theorem tangentBundleCore_localTriv_baseSet (i) :
    ((tangentBundleCore I M).localTriv i).baseSet = i.1.source := rfl

theorem tangentBundleCore_coordChange_achart (x x' z : M) :
    (tangentBundleCore I M).coordChange (achart H x) (achart H x') z =
      fderivWithin ğ•œ (extChartAt I x' âˆ˜ (extChartAt I x).symm) (range I) (extChartAt I x z) :=
  rfl

section tangentCoordChange

variable (I) in
/-- In a manifold `M`, given two preferred charts indexed by `x y : M`, `tangentCoordChange I x y`
is the family of derivatives of the corresponding change-of-coordinates map. It takes junk values
outside the intersection of the sources of the two charts.

Note that this definition takes advantage of the fact that `tangentBundleCore` has the same base
sets as the preferred charts of the base manifold. -/
abbrev tangentCoordChange (x y : M) : M â†’ E â†’L[ğ•œ] E :=
  (tangentBundleCore I M).coordChange (achart H x) (achart H y)

lemma tangentCoordChange_def {x y z : M} : tangentCoordChange I x y z =
    fderivWithin ğ•œ (extChartAt I y âˆ˜ (extChartAt I x).symm) (range I) (extChartAt I x z) := rfl

lemma tangentCoordChange_self {x z : M} {v : E} (h : z âˆˆ (extChartAt I x).source) :
    tangentCoordChange I x x z v = v := by
  apply (tangentBundleCore I M).coordChange_self
  rw [tangentBundleCore_baseSet, coe_achart, â† extChartAt_source I]
  exact h

lemma tangentCoordChange_comp {w x y z : M} {v : E}
    (h : z âˆˆ (extChartAt I w).source âˆ© (extChartAt I x).source âˆ© (extChartAt I y).source) :
    tangentCoordChange I x y z (tangentCoordChange I w x z v) = tangentCoordChange I w y z v := by
  apply (tangentBundleCore I M).coordChange_comp
  simp only [tangentBundleCore_baseSet, coe_achart, â† extChartAt_source I]
  exact h

lemma hasFDerivWithinAt_tangentCoordChange {x y z : M}
    (h : z âˆˆ (extChartAt I x).source âˆ© (extChartAt I y).source) :
    HasFDerivWithinAt ((extChartAt I y) âˆ˜ (extChartAt I x).symm) (tangentCoordChange I x y z)
      (range I) (extChartAt I x z) :=
  have h' : extChartAt I x z âˆˆ ((extChartAt I x).symm â‰« (extChartAt I y)).source := by
    rw [PartialEquiv.trans_source'', PartialEquiv.symm_symm, PartialEquiv.symm_target]
    exact mem_image_of_mem _ h
  ((contDiffWithinAt_ext_coord_change y x h').differentiableWithinAt le_rfl).hasFDerivWithinAt

lemma continuousOn_tangentCoordChange (x y : M) : ContinuousOn (tangentCoordChange I x y)
    ((extChartAt I x).source âˆ© (extChartAt I y).source) := by
  convert (tangentBundleCore I M).continuousOn_coordChange (achart H x) (achart H y) <;>
  simp only [tangentBundleCore_baseSet, coe_achart, â† extChartAt_source I]

end tangentCoordChange

local notation "TM" => TangentBundle I M

section TangentBundleInstances

instance : TopologicalSpace TM :=
  (tangentBundleCore I M).toTopologicalSpace

instance TangentSpace.fiberBundle : FiberBundle E (TangentSpace I : M â†’ Type _) :=
  (tangentBundleCore I M).fiberBundle

instance TangentSpace.vectorBundle : VectorBundle ğ•œ E (TangentSpace I : M â†’ Type _) :=
  (tangentBundleCore I M).vectorBundle

namespace TangentBundle

protected theorem chartAt (p : TM) :
    chartAt (ModelProd H E) p =
      ((tangentBundleCore I M).toFiberBundleCore.localTriv
        (achart H p.1)).toOpenPartialHomeomorph â‰«â‚•
        (chartAt H p.1).prod (OpenPartialHomeomorph.refl E) :=
  rfl

theorem chartAt_toPartialEquiv (p : TM) :
    (chartAt (ModelProd H E) p).toPartialEquiv =
      (tangentBundleCore I M).toFiberBundleCore.localTrivAsPartialEquiv (achart H p.1) â‰«
        (chartAt H p.1).toPartialEquiv.prod (PartialEquiv.refl E) :=
  rfl

theorem trivializationAt_eq_localTriv (x : M) :
    trivializationAt E (TangentSpace I) x =
      (tangentBundleCore I M).toFiberBundleCore.localTriv (achart H x) :=
  rfl

@[simp, mfld_simps]
theorem trivializationAt_source (x : M) :
    (trivializationAt E (TangentSpace I) x).source =
      Ï€ E (TangentSpace I) â»Â¹' (chartAt H x).source :=
  rfl

@[simp, mfld_simps]
theorem trivializationAt_target (x : M) :
    (trivializationAt E (TangentSpace I) x).target = (chartAt H x).source Ã—Ë¢ univ :=
  rfl

@[simp, mfld_simps]
theorem trivializationAt_baseSet (x : M) :
    (trivializationAt E (TangentSpace I) x).baseSet = (chartAt H x).source :=
  rfl

theorem trivializationAt_apply (x : M) (z : TM) :
    trivializationAt E (TangentSpace I) x z =
      (z.1, fderivWithin ğ•œ ((chartAt H x).extend I âˆ˜ ((chartAt H z.1).extend I).symm) (range I)
        ((chartAt H z.1).extend I z.1) z.2) :=
  rfl

@[simp, mfld_simps]
theorem trivializationAt_fst (x : M) (z : TM) : (trivializationAt E (TangentSpace I) x z).1 = z.1 :=
  rfl

@[simp, mfld_simps]
theorem mem_chart_source_iff (p q : TM) :
    p âˆˆ (chartAt (ModelProd H E) q).source â†” p.1 âˆˆ (chartAt H q.1).source := by
  simp only [FiberBundle.chartedSpace_chartAt, mfld_simps]

@[simp, mfld_simps]
theorem mem_chart_target_iff (p : H Ã— E) (q : TM) :
    p âˆˆ (chartAt (ModelProd H E) q).target â†” p.1 âˆˆ (chartAt H q.1).target := by
  /- porting note: was
  simp +contextual only [FiberBundle.chartedSpace_chartAt,
    and_iff_left_iff_imp, mfld_simps]
  -/
  simp only [FiberBundle.chartedSpace_chartAt, mfld_simps]
  rw [PartialEquiv.prod_symm]
  simp +contextual only [and_iff_left_iff_imp, mfld_simps]

@[simp, mfld_simps]
theorem coe_chartAt_fst (p q : TM) : ((chartAt (ModelProd H E) q) p).1 = chartAt H q.1 p.1 :=
  rfl

@[simp, mfld_simps]
theorem coe_chartAt_symm_fst (p : H Ã— E) (q : TM) :
    ((chartAt (ModelProd H E) q).symm p).1 = ((chartAt H q.1).symm : H â†’ M) p.1 :=
  rfl

/-- The trivialization of the tangent space can be expressed in terms of the tangent bundle core.
To write it as the manifold derivative of `extChartAt`, see
`TangentBundle.continuousLinearMapAt_trivializationAt`.

Use with care as it abuses the defeq `TangentSpace I b = E`. -/
theorem continuousLinearMapAt_trivializationAt_eq_core {bâ‚€ b : M} (hb : b âˆˆ (chartAt H bâ‚€).source) :
    (trivializationAt E (TangentSpace I) bâ‚€).continuousLinearMapAt ğ•œ b =
      (tangentBundleCore I M).coordChange (achart H b) (achart H bâ‚€) b := by
  simp [hb]

@[deprecated (since := "2025-07-03")]
alias trivializationAt_continuousLinearMapAt := continuousLinearMapAt_trivializationAt_eq_core

/-- The inverse trivialization of the tangent space can be expressed in terms of the tangent bundle
core. To write it as the manifold derivative of `(extChartAt I bâ‚€).symm`, see
`TangentBundle.symmL_trivializationAt`.

Use with care as it abuses the defeq `TangentSpace I b = E`. -/
theorem symmL_trivializationAt_eq_core {bâ‚€ b : M} (hb : b âˆˆ (chartAt H bâ‚€).source) :
    (trivializationAt E (TangentSpace I) bâ‚€).symmL ğ•œ b =
      (tangentBundleCore I M).coordChange (achart H bâ‚€) (achart H b) b := by
  simp [hb]

@[deprecated (since := "2025-07-03")]
alias trivializationAt_symmL := symmL_trivializationAt_eq_core

/-! The lemmas below have high priority because `simp` simplifies the LHS to `.id _ _`;
we prefer `1` as the simp-normal form. -/
@[simp high, mfld_simps]
theorem coordChange_model_space (b b' x : F) :
    (tangentBundleCore ğ“˜(ğ•œ, F) F).coordChange (achart F b) (achart F b') x = 1 := by
  simpa only [tangentBundleCore_coordChange, mfld_simps] using
    fderivWithin_id uniqueDiffWithinAt_univ

@[simp high, mfld_simps]
theorem symmL_model_space (b b' : F) :
    (trivializationAt F (TangentSpace ğ“˜(ğ•œ, F)) b).symmL ğ•œ b' = (1 : F â†’L[ğ•œ] F) := by
  rw [TangentBundle.symmL_trivializationAt_eq_core, coordChange_model_space]
  apply mem_univ

@[simp high, mfld_simps]
theorem continuousLinearMapAt_model_space (b b' : F) :
    (trivializationAt F (TangentSpace ğ“˜(ğ•œ, F)) b).continuousLinearMapAt ğ•œ b' = (1 : F â†’L[ğ•œ] F) := by
  rw [TangentBundle.continuousLinearMapAt_trivializationAt_eq_core, coordChange_model_space]
  apply mem_univ

end TangentBundle

omit [IsManifold I 1 M] in
lemma tangentBundleCore.isContMDiff [h : IsManifold I (n + 1) M] :
    haveI : IsManifold I 1 M := .of_le (n := n + 1) le_add_self
    (tangentBundleCore I M).IsContMDiff I n := by
  have : IsManifold I n M := .of_le (n := n + 1) (le_self_add)
  refine âŸ¨fun i j => ?_âŸ©
  rw [contMDiffOn_iff_source_of_mem_maximalAtlas (subset_maximalAtlas i.2),
    contMDiffOn_iff_contDiffOn]
  Â· refine ((contDiffOn_fderiv_coord_change (I := I) i j).congr fun x hx => ?_).mono ?_
    Â· rw [PartialEquiv.trans_source'] at hx
      simp_rw [Function.comp_apply, tangentBundleCore_coordChange, (i.1.extend I).right_inv hx.1]
    Â· exact (i.1.extend_image_source_inter j.1).subset
  Â· apply inter_subset_left

omit [IsManifold I 1 M] in
lemma TangentBundle.contMDiffVectorBundle [h : IsManifold I (n + 1) M] :
    haveI : IsManifold I 1 M := .of_le (n := n + 1) le_add_self
    ContMDiffVectorBundle n E (TangentSpace I : M â†’ Type _) I := by
  have : IsManifold I 1 M := .of_le (n := n + 1) le_add_self
  have : (tangentBundleCore I M).IsContMDiff I n := tangentBundleCore.isContMDiff
  exact (tangentBundleCore I M).instContMDiffVectorBundle

omit [IsManifold I 1 M] in
instance [h : IsManifold I âˆ M] :
    ContMDiffVectorBundle âˆ E (TangentSpace I : M â†’ Type _) I := by
  have : IsManifold I (âˆ + 1) M := h
  exact TangentBundle.contMDiffVectorBundle

omit [IsManifold I 1 M] in
instance [IsManifold I Ï‰ M] :
    ContMDiffVectorBundle Ï‰ E (TangentSpace I : M â†’ Type _) I :=
  TangentBundle.contMDiffVectorBundle

omit [IsManifold I 1 M] in
instance [h : IsManifold I 2 M] :
    ContMDiffVectorBundle 1 E (TangentSpace I : M â†’ Type _) I := by
  have : IsManifold I (1 + 1) M := h
  exact TangentBundle.contMDiffVectorBundle

end TangentBundleInstances

/-! ## The tangent bundle to the model space -/

@[simp, mfld_simps]
theorem trivializationAt_model_space_apply (p : TangentBundle I H) (x : H) :
    trivializationAt E (TangentSpace I) x p = (p.1, p.2) := by
  simp only [TangentBundle.trivializationAt_apply]
  have : fderivWithin ğ•œ (â†‘I âˆ˜ â†‘I.symm) (range I) (I p.proj) =
      fderivWithin ğ•œ id (range I) (I p.proj) :=
    fderivWithin_congr' (fun y hy â†¦ by simp [hy]) (mem_range_self p.proj)
  simp [this, fderivWithin_id (ModelWithCorners.uniqueDiffWithinAt_image I)]

/-- In the tangent bundle to the model space, the charts are just the canonical identification
between a product type and a sigma type, a.k.a. `TotalSpace.toProd`. -/
@[simp, mfld_simps]
theorem tangentBundle_model_space_chartAt (p : TangentBundle I H) :
    (chartAt (ModelProd H E) p).toPartialEquiv = (TotalSpace.toProd H E).toPartialEquiv := by
  ext x : 1
  Â· ext; Â· rfl
    exact (tangentBundleCore I H).coordChange_self (achart _ x.1) x.1 (mem_achart_source H x.1) x.2
  Â· ext; Â· rfl
    apply heq_of_eq
    exact (tangentBundleCore I H).coordChange_self (achart _ x.1) x.1 (mem_achart_source H x.1) x.2
  simp_rw [TangentBundle.chartAt, FiberBundleCore.localTriv,
    FiberBundleCore.localTrivAsPartialEquiv, VectorBundleCore.toFiberBundleCore_baseSet,
    tangentBundleCore_baseSet]
  simp only [mfld_simps]

@[simp, mfld_simps]
theorem tangentBundle_model_space_coe_chartAt (p : TangentBundle I H) :
    â‡‘(chartAt (ModelProd H E) p) = TotalSpace.toProd H E := by
  rw [â† OpenPartialHomeomorph.coe_coe, tangentBundle_model_space_chartAt]; rfl

@[simp, mfld_simps]
theorem tangentBundle_model_space_coe_chartAt_symm (p : TangentBundle I H) :
    ((chartAt (ModelProd H E) p).symm : ModelProd H E â†’ TangentBundle I H) =
      (TotalSpace.toProd H E).symm := by
  rw [â† OpenPartialHomeomorph.coe_coe, OpenPartialHomeomorph.symm_toPartialEquiv,
    tangentBundle_model_space_chartAt]; rfl

theorem tangentBundleCore_coordChange_model_space (x x' z : H) :
    (tangentBundleCore I H).coordChange (achart H x) (achart H x') z =
    ContinuousLinearMap.id ğ•œ E := by
  ext v; exact (tangentBundleCore I H).coordChange_self (achart _ z) z (mem_univ _) v

variable (I) in
/-- The canonical identification between the tangent bundle to the model space and the product,
as a homeomorphism. For the diffeomorphism version, see `tangentBundleModelSpaceDiffeomorph`. -/
def tangentBundleModelSpaceHomeomorph : TangentBundle I H â‰ƒâ‚œ ModelProd H E :=
  { TotalSpace.toProd H E with
    continuous_toFun := by
      let p : TangentBundle I H := âŸ¨I.symm (0 : E), (0 : E)âŸ©
      have : Continuous (chartAt (ModelProd H E) p) := by
        rw [â† continuousOn_univ]
        convert (chartAt (ModelProd H E) p).continuousOn
        simp only [TangentSpace.fiberBundle, mfld_simps]
      simpa only [mfld_simps] using this
    continuous_invFun := by
      let p : TangentBundle I H := âŸ¨I.symm (0 : E), (0 : E)âŸ©
      have : Continuous (chartAt (ModelProd H E) p).symm := by
        rw [â† continuousOn_univ]
        convert (chartAt (ModelProd H E) p).symm.continuousOn
        simp only [mfld_simps]
      simpa only [mfld_simps] using this }

@[simp, mfld_simps]
theorem tangentBundleModelSpaceHomeomorph_coe :
    (tangentBundleModelSpaceHomeomorph I : TangentBundle I H â†’ ModelProd H E) =
      TotalSpace.toProd H E :=
  rfl

@[simp, mfld_simps]
theorem tangentBundleModelSpaceHomeomorph_coe_symm :
    ((tangentBundleModelSpaceHomeomorph I).symm : ModelProd H E â†’ TangentBundle I H) =
      (TotalSpace.toProd H E).symm :=
  rfl

theorem contMDiff_tangentBundleModelSpaceHomeomorph :
    ContMDiff I.tangent (I.prod ğ“˜(ğ•œ, E)) n
    (tangentBundleModelSpaceHomeomorph I : TangentBundle I H â†’ ModelProd H E) := by
  apply contMDiff_iff.2 âŸ¨Homeomorph.continuous _, fun x y â†¦ ?_âŸ©
  apply contDiffOn_id.congr
  simp only [mfld_simps, mem_range, TotalSpace.toProd, Equiv.coe_fn_symm_mk, forall_exists_index,
    Prod.forall, Prod.mk.injEq]
  rintro a b x rfl
  simp [PartialEquiv.prod]

theorem contMDiff_tangentBundleModelSpaceHomeomorph_symm :
    ContMDiff (I.prod ğ“˜(ğ•œ, E)) I.tangent n
    ((tangentBundleModelSpaceHomeomorph I).symm : ModelProd H E â†’ TangentBundle I H) := by
  apply contMDiff_iff.2 âŸ¨Homeomorph.continuous _, fun x y â†¦ ?_âŸ©
  apply contDiffOn_id.congr
  simp only [mfld_simps, mem_range, TotalSpace.toProd, Equiv.coe_fn_symm_mk, forall_exists_index,
    Prod.forall, Prod.mk.injEq]
  rintro a b x rfl
  simpa [PartialEquiv.prod] using âŸ¨rfl, rflâŸ©

variable (H I) in
/-- In the tangent bundle to the model space, the second projection is `C^n`. -/
lemma contMDiff_snd_tangentBundle_modelSpace :
    ContMDiff I.tangent ğ“˜(ğ•œ, E) n (fun (p : TangentBundle I H) â†¦ p.2) := by
  change ContMDiff I.tangent ğ“˜(ğ•œ, E) n
    ((id Prod.snd : ModelProd H E â†’ E) âˆ˜ (tangentBundleModelSpaceHomeomorph I))
  apply ContMDiff.comp (I' := I.prod ğ“˜(ğ•œ, E))
  Â· convert contMDiff_snd
    rw [chartedSpaceSelf_prod]
    rfl
  Â· exact contMDiff_tangentBundleModelSpaceHomeomorph

/-- A vector field on a vector space is `C^n` in the manifold sense iff it is `C^n` in the vector
space sense. -/
lemma contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt
    {V : Î  (x : E), TangentSpace ğ“˜(ğ•œ, E) x} {s : Set E} {x : E} :
    ContMDiffWithinAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent n (fun x â†¦ (V x : TangentBundle ğ“˜(ğ•œ, E) E)) s x â†”
      ContDiffWithinAt ğ•œ n V s x := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· exact ContMDiffWithinAt.contDiffWithinAt <|
      (contMDiff_snd_tangentBundle_modelSpace E ğ“˜(ğ•œ, E)).contMDiffAt.comp_contMDiffWithinAt _ h
  Â· apply Bundle.contMDiffWithinAt_totalSpace.2
    refine âŸ¨contMDiffWithinAt_id, ?_âŸ©
    convert h.contMDiffWithinAt with y
    simp

/-- A vector field on a vector space is `C^n` in the manifold sense iff it is `C^n` in the vector
space sense. -/
lemma contMDiffAt_vectorSpace_iff_contDiffAt
    {V : Î  (x : E), TangentSpace ğ“˜(ğ•œ, E) x} {x : E} :
    ContMDiffAt ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent n (fun x â†¦ (V x : TangentBundle ğ“˜(ğ•œ, E) E)) x â†”
      ContDiffAt ğ•œ n V x := by
  simp only [â† contMDiffWithinAt_univ, â† contDiffWithinAt_univ,
    contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt]

/-- A vector field on a vector space is `C^n` in the manifold sense iff it is `C^n` in the vector
space sense. -/
lemma contMDiffOn_vectorSpace_iff_contDiffOn
    {V : Î  (x : E), TangentSpace ğ“˜(ğ•œ, E) x} {s : Set E} :
    ContMDiffOn ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent n (fun x â†¦ (V x : TangentBundle ğ“˜(ğ•œ, E) E)) s â†”
      ContDiffOn ğ•œ n V s := by
  simp only [ContMDiffOn, ContDiffOn, contMDiffWithinAt_vectorSpace_iff_contDiffWithinAt ]

/-- A vector field on a vector space is `C^n` in the manifold sense iff it is `C^n` in the vector
space sense. -/
lemma contMDiff_vectorSpace_iff_contDiff
    {V : Î  (x : E), TangentSpace ğ“˜(ğ•œ, E) x} :
    ContMDiff ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E).tangent n (fun x â†¦ (V x : TangentBundle ğ“˜(ğ•œ, E) E)) â†”
      ContDiff ğ•œ n V := by
  simp only [â† contMDiffOn_univ, â† contDiffOn_univ, contMDiffOn_vectorSpace_iff_contDiffOn]

section inTangentCoordinates

variable {N : Type*}

/-- The map `inCoordinates` for the tangent bundle is trivial on the model spaces -/
theorem inCoordinates_tangent_bundle_core_model_space (xâ‚€ x : H) (yâ‚€ y : H') (Ï• : E â†’L[ğ•œ] E') :
    inCoordinates E (TangentSpace I) E' (TangentSpace I') xâ‚€ x yâ‚€ y Ï• = Ï• := by
  erw [VectorBundleCore.inCoordinates_eq] <;> try trivial
  simp_rw [tangentBundleCore_indexAt, tangentBundleCore_coordChange_model_space,
    ContinuousLinearMap.id_comp, ContinuousLinearMap.comp_id]

variable (I I') in
/-- When `Ï• x` is a continuous linear map that changes vectors in charts around `f x` to vectors
in charts around `g x`, `inTangentCoordinates I I' f g Ï• xâ‚€ x` is a coordinate change of
this continuous linear map that makes sense from charts around `f xâ‚€` to charts around `g xâ‚€`
by composing it with appropriate coordinate changes.
Note that the type of `Ï•` is more accurately
`Î  x : N, TangentSpace I (f x) â†’L[ğ•œ] TangentSpace I' (g x)`.
We are unfolding `TangentSpace` in this type so that Lean recognizes that the type of `Ï•` doesn't
actually depend on `f` or `g`.

This is the underlying function of the trivializations of the hom of (pullbacks of) tangent spaces.
-/
def inTangentCoordinates (f : N â†’ M) (g : N â†’ M') (Ï• : N â†’ E â†’L[ğ•œ] E') : N â†’ N â†’ E â†’L[ğ•œ] E' :=
  fun xâ‚€ x => inCoordinates E (TangentSpace I) E' (TangentSpace I') (f xâ‚€) (f x) (g xâ‚€) (g x) (Ï• x)

theorem inTangentCoordinates_model_space (f : N â†’ H) (g : N â†’ H') (Ï• : N â†’ E â†’L[ğ•œ] E') (xâ‚€ : N) :
    inTangentCoordinates I I' f g Ï• xâ‚€ = Ï• := by
  simp +unfoldPartialApp only [inTangentCoordinates,
    inCoordinates_tangent_bundle_core_model_space]

/-- To write a linear map between tangent spaces in coordinates amounts to precomposing and
postcomposing it with suitable coordinate changes. For a concrete version expressing the
change of coordinates as derivatives of extended charts,
see `inTangentCoordinates_eq_mfderiv_comp`. -/
theorem inTangentCoordinates_eq (f : N â†’ M) (g : N â†’ M') (Ï• : N â†’ E â†’L[ğ•œ] E') {xâ‚€ x : N}
    (hx : f x âˆˆ (chartAt H (f xâ‚€)).source) (hy : g x âˆˆ (chartAt H' (g xâ‚€)).source) :
    inTangentCoordinates I I' f g Ï• xâ‚€ x =
      (tangentBundleCore I' M').coordChange (achart H' (g x)) (achart H' (g xâ‚€)) (g x) âˆ˜L
        Ï• x âˆ˜L (tangentBundleCore I M).coordChange (achart H (f xâ‚€)) (achart H (f x)) (f x) :=
  (tangentBundleCore I M).inCoordinates_eq (tangentBundleCore I' M') (Ï• x) hx hy

end inTangentCoordinates

end General
