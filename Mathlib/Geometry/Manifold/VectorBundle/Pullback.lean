/-
Copyright (c) 2023 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.VectorBundle.Basic

/-! # Pullbacks of `C^n` vector bundles

This file defines pullbacks of `C^n` vector bundles over a manifold.

## Main definitions

* `ContMDiffVectorBundle.pullback`: For a `C^n` vector bundle `E` over a manifold `B` and a `C^n`
  map `f : B' â†’ B`, the pullback vector bundle `f *áµ– E` is a `C^n` vector bundle.

-/

open Bundle Set
open scoped Manifold Topology

variable {ğ•œ B B' : Type*} (F : Type*) (E : B â†’ Type*) {n : WithTop â„•âˆ}
variable [NontriviallyNormedField ğ•œ] [âˆ€ x, AddCommMonoid (E x)]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [TopologicalSpace (TotalSpace F E)]
  [âˆ€ x, TopologicalSpace (E x)] {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ğ•œ EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners ğ•œ EB HB} [TopologicalSpace B]
  [ChartedSpace HB B] {EB' : Type*} [NormedAddCommGroup EB']
  [NormedSpace ğ•œ EB'] {HB' : Type*} [TopologicalSpace HB'] {IB' : ModelWithCorners ğ•œ EB' HB'}
  [TopologicalSpace B'] [ChartedSpace HB' B'] [FiberBundle F E]
  {M EM HM : Type*} [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  (f : ContMDiffMap IB' IB B' B n)

/-- For a fiber bundle `E` over a manifold `B` and a regular map `f : B' â†’ B`, the natural
"lift" map from the total space of `f *áµ– E` to the total space of `E` is regular. -/
theorem Bundle.Pullback.contMDiff_lift :
    ContMDiff (IB'.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) n
      (Pullback.lift f : TotalSpace F (f *áµ– E) â†’ TotalSpace F E) := by
  intro x
  rw [contMDiffAt_totalSpace]
  refine âŸ¨f.contMDiff.contMDiffAt.comp _ (contMDiffAt_proj (f *áµ– E)), ?_âŸ©
  simp only [contMDiffAt_of_totalSpace, trivializationAt, lift_proj, FiberBundle.trivializationAt']
  apply ContMDiffAt.congr_of_eventuallyEq contMDiffAt_snd
  have : (trivializationAt F (f *áµ– E) x.proj).target âˆˆ
      ğ“ ((trivializationAt F (f *áµ– E) x.proj) x) := by
    apply IsOpen.mem_nhds
    Â· exact (trivializationAt F (f *áµ– E) x.proj).open_target
    Â· simpa [Trivialization.mem_target] using FiberBundle.mem_baseSet_trivializationAt' x.proj
  filter_upwards [this]
  rintro âŸ¨y, vâŸ© hy
  have A : f y âˆˆ (FiberBundle.trivializationAt' (E := E) (F := F) (f x.proj)).baseSet := by
    simpa [trivializationAt, FiberBundle.trivializationAt', Trivialization.pullback] using hy
  simp only [Function.comp_apply, A, lift_pullback_symm_apply]
  rw [Trivialization.apply_symm_apply]
  exact (Trivialization.mk_mem_target (FiberBundle.trivializationAt' (f x.proj))).mpr A

/-- Given a fiber bundle `E` over a manifold `B` and a regular map `f : B' â†’ B`, if `Ï†` is
a map into the total space of the pullback `f *áµ– E`, then its regularity can be checked by checking
the regularity of (1) the map `TotalSpace.proj âˆ˜ Ï†` into `B'`, and (2) the map
`Pullback.lift f âˆ˜ Ï†` into the total space of `E`. -/
theorem Bundle.Pullback.contMDiff_of_contMDiff_proj_comp_of_contMDiff_lift_comp
    {Ï† : M â†’ TotalSpace F (f *áµ– E)} (h1 : ContMDiff IM IB' n (TotalSpace.proj âˆ˜ Ï†))
    (h2 : ContMDiff IM (IB.prod ğ“˜(ğ•œ, F)) n (Pullback.lift f âˆ˜ Ï†)) :
    ContMDiff IM (IB'.prod ğ“˜(ğ•œ, F)) n Ï† := by
  intro x
  have h1_cont : Continuous (TotalSpace.proj âˆ˜ Ï†) := h1.continuous
  have h2_cont : Continuous (Pullback.lift f âˆ˜ Ï†) := h2.continuous
  specialize h1 x
  specialize h2 x
  rw [contMDiffAt_iff_target] at h1 h2 âŠ¢
  constructor
  Â· exact Pullback.continuous_of_continuous_proj_comp_of_continuous_lift_comp f h1_cont h2_cont
      |>.continuousAt
  apply ContMDiffAt.prodMk_space h1.2
  have (x : EB Ã— F) : ContMDiffAt ğ“˜(ğ•œ, EB Ã— F) ğ“˜(ğ•œ, F) n Prod.snd x := by
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_snd
  exact (this _).comp _ h2.2

/-- Given a fiber bundle `E` over a manifold `B` and a regular map `f : B' â†’ B`, a map `Ï†`
into the total space of the pullback `f *áµ– E` is regular if and only if the following two maps are
regular: (1) the map `TotalSpace.proj âˆ˜ Ï†` into `B'`, and (2) the map `Pullback.lift f âˆ˜ Ï†` into the
total space of `E`. -/
theorem Bundle.Pullback.contMDiff_iff_contMDiff_proj_comp_and_contMDiff_lift_comp
    (Ï† : M â†’ TotalSpace F (f *áµ– E)) :
    ContMDiff IM (IB'.prod ğ“˜(ğ•œ, F)) n Ï† â†” (ContMDiff IM IB' n (TotalSpace.proj âˆ˜ Ï†)
      âˆ§ ContMDiff IM (IB.prod ğ“˜(ğ•œ, F)) n (Pullback.lift f âˆ˜ Ï†)) := by
  refine âŸ¨fun h â†¦ âŸ¨?_, ?_âŸ©, fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ ?_âŸ©
  Â· exact (Bundle.contMDiff_proj (f *áµ– E)).comp h
  Â· exact (Bundle.Pullback.contMDiff_lift F E f).comp h
  Â· exact Bundle.Pullback.contMDiff_of_contMDiff_proj_comp_of_contMDiff_lift_comp F E f hâ‚ hâ‚‚

variable [âˆ€ x, Module ğ•œ (E x)] [VectorBundle ğ•œ F E] [ContMDiffVectorBundle n F E IB]

/-- For a `C^n` vector bundle `E` over a manifold `B` and a `C^n` map `f : B' â†’ B`, the pullback
vector bundle `f *áµ– E` is a `C^n` vector bundle. -/
instance ContMDiffVectorBundle.pullback : ContMDiffVectorBundle n F (f *áµ– E) IB' where
  contMDiffOn_coordChangeL := by
    rintro _ _ âŸ¨e, he, rflâŸ© âŸ¨e', he', rflâŸ©
    refine ((contMDiffOn_coordChangeL e e').comp f.contMDiff.contMDiffOn fun b hb => hb).congr ?_
    rintro b (hb : f b âˆˆ e.baseSet âˆ© e'.baseSet); ext v
    show ((e.pullback f).coordChangeL ğ•œ (e'.pullback f) b) v = (e.coordChangeL ğ•œ e' (f b)) v
    rw [e.coordChangeL_apply e' hb, (e.pullback f).coordChangeL_apply' _]
    exacts [rfl, hb]
