/-
Copyright (c) 2023 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.VectorBundle.Basic

/-! # Pullbacks of smooth vector bundles

This file defines pullbacks of smooth vector bundles over a smooth manifold.

## Main definitions

* `SmoothVectorBundle.pullback`: For a smooth vector bundle `E` over a manifold `B` and a smooth
  map `f : B' â†’ B`, the pullback vector bundle `f *áµ– E` is a smooth vector bundle.

-/

open Bundle Set
open scoped Manifold

variable {ğ•œ B B' : Type*} (F : Type*) (E : B â†’ Type*)
variable [NontriviallyNormedField ğ•œ] [âˆ€ x, AddCommMonoid (E x)] [âˆ€ x, Module ğ•œ (E x)]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [TopologicalSpace (TotalSpace F E)]
  [âˆ€ x, TopologicalSpace (E x)] {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ğ•œ EB]
  {HB : Type*} [TopologicalSpace HB] (IB : ModelWithCorners ğ•œ EB HB) [TopologicalSpace B]
  [ChartedSpace HB B] [SmoothManifoldWithCorners IB B] {EB' : Type*} [NormedAddCommGroup EB']
  [NormedSpace ğ•œ EB'] {HB' : Type*} [TopologicalSpace HB'] (IB' : ModelWithCorners ğ•œ EB' HB')
  [TopologicalSpace B'] [ChartedSpace HB' B'] [SmoothManifoldWithCorners IB' B'] [FiberBundle F E]
  [VectorBundle ğ•œ F E] [SmoothVectorBundle F E IB] (f : SmoothMap IB' IB B' B)

/-- For a smooth vector bundle `E` over a manifold `B` and a smooth map `f : B' â†’ B`, the pullback
vector bundle `f *áµ– E` is a smooth vector bundle. -/
instance SmoothVectorBundle.pullback : SmoothVectorBundle F (f *áµ– E) IB' where
  smoothOn_coordChangeL := by
    rintro _ _ âŸ¨e, he, rflâŸ© âŸ¨e', he', rflâŸ©
    refine ((smoothOn_coordChangeL _ e e').comp f.smooth.smoothOn fun b hb => hb).congr ?_
    rintro b (hb : f b âˆˆ e.baseSet âˆ© e'.baseSet); ext v
    show ((e.pullback f).coordChangeL ğ•œ (e'.pullback f) b) v = (e.coordChangeL ğ•œ e' (f b)) v
    rw [e.coordChangeL_apply e' hb, (e.pullback f).coordChangeL_apply' _]
    exacts [rfl, hb]

omit [SmoothManifoldWithCorners IB B] [SmoothManifoldWithCorners IB' B']

/-- For a smooth vector bundle `E` over a manifold `B` and a smooth map `f : B' â†’ B`, the natural
"lift" map from the total space of `f *áµ– E` to the total space of `E` is smooth. -/
theorem Bundle.Pullback.smooth_lift :
    Smooth (IB'.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) (Pullback.lift f : TotalSpace F (f *áµ– E) â†’ _) := by
  intro x
  rw [contMDiffAt_totalSpace]
  refine âŸ¨f.smooth.smoothAt.comp _ (smoothAt_proj (f *áµ– E)), ?_âŸ©
  refine (contMDiffAt_snd (M := B')).comp _ <|
    (smoothOn_trivializationAt IB' x).contMDiffAt ?_
  apply (trivializationAt F (f *áµ– E) x.proj).open_source.mem_nhds
  simp

variable {M EM HM : Type*} [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]

omit [(x : B) â†’ Module ğ•œ (E x)] in
/-- Given a smooth fibre bundle `E` over a manifold `B` and a smooth map `f : B' â†’ B`, if `Ï†` is
a map into the total space of the pullback `f *áµ– E`, then its smoothness can be checked by checking
the smoothness of (1) the map `TotalSpace.proj âˆ˜ Ï†` into `B'`, and (ii) the map
`Pullback.lift f âˆ˜ Ï†` into the total space of `E`. -/
theorem Bundle.Pullback.smooth_of_smooth_proj_comp_of_smooth_lift_comp
    {Ï† : M â†’ TotalSpace F (f *áµ– E)} (h1 : Smooth IM IB' (TotalSpace.proj âˆ˜ Ï†))
    (h2 : Smooth IM (IB.prod ğ“˜(ğ•œ, F)) (Pullback.lift f âˆ˜ Ï†)) : Smooth IM (IB'.prod ğ“˜(ğ•œ, F)) Ï† := by
  intro x
  have h1_cont : Continuous (TotalSpace.proj âˆ˜ Ï†) := h1.continuous
  have h2_cont : Continuous (Pullback.lift f âˆ˜ Ï†) := h2.continuous
  specialize h1 x
  specialize h2 x
  rw [contMDiffAt_iff_target] at h1 h2 âŠ¢
  constructor
  Â· have := Bundle.Pullback.continuous_of_continuous_proj_comp_of_smooth_lift_comp f h1_cont h2_cont
    exact this.continuousAt
  apply ContMDiffAt.prod_mk_space h1.2
  have (x : EB Ã— F) : ContMDiffAt ğ“˜(ğ•œ, EB Ã— F) ğ“˜(ğ•œ, F) âŠ¤ Prod.snd x := by
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_snd
  exact (this _).comp _ h2.2
