/-
Copyright (c) 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Floris van Doorn, Michael Rothgang
-/
import Mathlib.Geometry.Manifold.Algebra.LieGroup
import Mathlib.Geometry.Manifold.MFDeriv.Basic
import Mathlib.Topology.ContinuousMap.Basic
import Mathlib.Geometry.Manifold.VectorBundle.Basic

/-!
# `C^n` sections

In this file we define the type `ContMDiffSection` of `n` times continuously differentiable
sections of a vector bundle over a manifold `M` and prove that it's a module over the base field.

In passing, we prove that binary and finite sums, differences and scalar products of `C^n`
sections are `C^n`.

-/


open Bundle Filter Function

open scoped Bundle Manifold ContDiff

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

variable (F : Type*) [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  -- `F` model fiber
  (n : WithTop â„•âˆ)
  (V : M â†’ Type*) [TopologicalSpace (TotalSpace F V)]
  -- `V` vector bundle
  [âˆ€ x : M, TopologicalSpace (V x)] [FiberBundle F V]

-- Binary and finite sums, negative, differences and scalar products of smooth sections are smooth
section operations

-- Let V be a vector bundle
variable [âˆ€ x, AddCommGroup (V x)] [âˆ€ x, Module ğ•œ (V x)] [VectorBundle ğ•œ F V]

variable {I F n V}

variable {f : M â†’ ğ•œ} {a : ğ•œ} {s t : Î  x : M, V x} {u : Set M} {xâ‚€ : M}

lemma ContMDiffWithinAt.add_section
    (hs : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€)
    (ht : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) u xâ‚€ := by
  rw [contMDiffWithinAt_section] at hs ht âŠ¢
  set e := trivializationAt F V xâ‚€
  refine (hs.add ht).congr_of_eventuallyEq ?_ ?_
  Â· apply eventually_of_mem (U := e.baseSet)
    Â· exact mem_nhdsWithin_of_mem_nhds <|
        (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F V xâ‚€)
    Â· intro x hx
      apply (e.linear ğ•œ hx).1
  Â· apply (e.linear ğ•œ (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)).1

lemma ContMDiffAt.add_section
    (hs : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€)
    (ht : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) xâ‚€ := by
  rw [â† contMDiffWithinAt_univ] at hs âŠ¢
  exact hs.add_section ht

lemma ContMDiffOn.add_section
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u)
    (ht : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) u :=
  fun xâ‚€ hxâ‚€ â†¦ (hs xâ‚€ hxâ‚€).add_section (ht xâ‚€ hxâ‚€)

lemma ContMDiff.add_section
    (hs : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)))
    (ht : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) :=
  fun xâ‚€ â†¦ (hs xâ‚€).add_section (ht xâ‚€)

lemma ContMDiffWithinAt.neg_section
    (hs : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (-s x)) u xâ‚€ := by
  rw [contMDiffWithinAt_section] at hs âŠ¢
  set e := trivializationAt F V xâ‚€
  refine hs.neg.congr_of_eventuallyEq ?_ ?_
  Â· apply eventually_of_mem (U := e.baseSet)
    Â· exact mem_nhdsWithin_of_mem_nhds <|
        (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F V xâ‚€)
    Â· intro x hx
      apply (e.linear ğ•œ hx).map_neg
  Â· apply (e.linear ğ•œ (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)).map_neg

lemma ContMDiffAt.neg_section
    (hs : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (-s x)) xâ‚€ := by
  rw [â† contMDiffWithinAt_univ] at hs âŠ¢
  exact hs.neg_section

lemma ContMDiffOn.neg_section
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (-s x)) u :=
  fun xâ‚€ hxâ‚€ â†¦ (hs xâ‚€ hxâ‚€).neg_section

lemma ContMDiff.neg_section
    (hs : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (-s x)) :=
  fun xâ‚€ â†¦ (hs xâ‚€).neg_section

lemma ContMDiffWithinAt.sub_section
    (hs : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€)
    (ht : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s - t) x)) u xâ‚€ := by
  rw [sub_eq_add_neg]
  exact hs.add_section ht.neg_section

lemma ContMDiffAt.sub_section
    (hs : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€)
    (ht : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s - t) x)) xâ‚€ := by
  rw [sub_eq_add_neg]
  apply hs.add_section ht.neg_section

lemma ContMDiffOn.sub_section
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u)
    (ht : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s - t) x)) u :=
  fun xâ‚€ hxâ‚€ â†¦ (hs xâ‚€ hxâ‚€).sub_section (ht xâ‚€ hxâ‚€)

lemma ContMDiff.sub_section
    (hs : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)))
    (ht : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x ((s - t) x)) :=
  fun xâ‚€ â†¦ (hs xâ‚€).sub_section (ht xâ‚€)

lemma ContMDiffWithinAt.smul_section (hf : ContMDiffWithinAt I ğ“˜(ğ•œ) n f u xâ‚€)
    (hs : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) u xâ‚€ := by
  rw [contMDiffWithinAt_section] at hs âŠ¢
  set e := trivializationAt F V xâ‚€
  refine (hf.smul hs).congr_of_eventuallyEq ?_ ?_
  Â· apply eventually_of_mem (U := e.baseSet)
    Â· exact mem_nhdsWithin_of_mem_nhds <|
        (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F V xâ‚€)
    Â· intro x hx
      apply (e.linear ğ•œ hx).2
  Â· apply (e.linear ğ•œ (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)).2

lemma ContMDiffAt.smul_section (hf : ContMDiffAt I ğ“˜(ğ•œ) n f xâ‚€)
    (hs : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) xâ‚€ := by
  rw [â† contMDiffWithinAt_univ] at hs âŠ¢
  exact .smul_section hf hs

lemma ContMDiffOn.smul_section (hf : ContMDiffOn I ğ“˜(ğ•œ) n f u)
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) u :=
  fun xâ‚€ hxâ‚€ â†¦ (hf xâ‚€ hxâ‚€).smul_section (hs xâ‚€ hxâ‚€)

lemma ContMDiff.smul_section (hf : ContMDiff I ğ“˜(ğ•œ) n f)
    (hs : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) :=
  fun xâ‚€ â†¦ (hf xâ‚€).smul_section (hs xâ‚€)

lemma ContMDiffWithinAt.const_smul_section
    (hs : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) u xâ‚€ :=
  contMDiffWithinAt_const.smul_section hs

lemma ContMDiffAt.const_smul_section
    (hs : ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) xâ‚€ :=
  contMDiffAt_const.smul_section hs

lemma ContMDiffOn.const_smul_section
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) u :=
  contMDiffOn_const.smul_section hs

lemma ContMDiff.const_smul_section
    (hs : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) :=
  fun xâ‚€ â†¦ (hs xâ‚€).const_smul_section

variable {Î¹ : Type*} {t : Î¹ â†’ (x : M) â†’ V x}

lemma ContMDiffWithinAt.sum_section {s : Finset Î¹}
    (hs : âˆ€ i âˆˆ s,
      ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n
      (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) u xâ‚€ := by
  classical
  induction s using Finset.induction_on with
  | empty =>
    simpa only [Finset.sum_empty] using contMDiffWithinAt_zeroSection ..
  | insert i s hi h =>
    simp only [Finset.sum_insert hi]
    apply (hs _ (s.mem_insert_self i)).add_section
    exact h fun i a â†¦ hs _ (s.mem_insert_of_mem a)

lemma ContMDiffAt.sum_section {s : Finset Î¹} {xâ‚€ : M}
    (hs : âˆ€ i âˆˆ s, ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) xâ‚€ := by
  simp_rw [â† contMDiffWithinAt_univ] at hs âŠ¢
  exact .sum_section hs

lemma ContMDiffOn.sum_section {s : Finset Î¹}
    (hs : âˆ€ i âˆˆ s, ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) u :=
  fun xâ‚€ hxâ‚€ â†¦ .sum_section fun i hi â†¦ hs i hi xâ‚€ hxâ‚€

lemma ContMDiff.sum_section {s : Finset Î¹}
    (hs : âˆ€ i âˆˆ s, ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) :=
  fun xâ‚€ â†¦ .sum_section fun i hi â†¦ (hs i hi) xâ‚€

/-- The scalar product `Ïˆ â€¢ s` of a `C^k` function `Ïˆ : M â†’ ğ•œ` and a section `s` of a vector
bundle `V â†’ M` is `C^k` once `s` is `C^k` on an open set containing `tsupport Ïˆ`.

This is a vector bundle analogue of `contMDiff_of_tsupport`. -/
lemma ContMDiffOn.smul_section_of_tsupport {s : Î  (x : M), V x} {Ïˆ : M â†’ ğ•œ}
    (hÏˆ : ContMDiffOn I ğ“˜(ğ•œ) n Ïˆ u) (ht : IsOpen u) (ht' : tsupport Ïˆ âŠ† u)
    (hs : ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (s x)) u) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (Ïˆ x â€¢ s x)) := by
  apply contMDiff_of_contMDiffOn_union_of_isOpen (hÏˆ.smul_section hs) ?_ ?_ ht
      (isOpen_compl_iff.mpr <| isClosed_tsupport Ïˆ)
  Â· apply ((contMDiff_zeroSection _ _).contMDiffOn (s := (tsupport Ïˆ)á¶œ)).congr
    intro y hy
    simp [image_eq_zero_of_notMem_tsupport hy, zeroSection]
  Â· exact Set.compl_subset_iff_union.mp <| Set.compl_subset_compl.mpr ht'

/-- The sum of a locally finite collection of sections is `C^k` iff each section is.
Version at a point within a set. -/
lemma ContMDiffWithinAt.sum_section_of_locallyFinite
    (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘' i, (t i x))) u xâ‚€ := by
  obtain âŸ¨u', hu', hfinâŸ© := ht xâ‚€
  -- All sections `t i` but a finite set `s` vanish near `xâ‚€`: choose a neighbourhood `u` of `xâ‚€`
  -- and a finite set `s` of sections which don't vanish.
  let s := {i | ((fun i â†¦ {x | t i x â‰  0}) i âˆ© u').Nonempty}
  have := hfin.fintype
  have : ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n
      (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) (u âˆ© u') xâ‚€ :=
    ContMDiffWithinAt.sum_section fun i hi â†¦ ((ht' i).mono Set.inter_subset_left)
  apply (contMDiffWithinAt_inter hu').mp
  apply this.congr fun y hy â†¦ ?_
  Â· rw [TotalSpace.mk_inj, tsum_eq_sum']
    refine support_subset_iff'.mpr fun i hi â†¦ ?_
    by_contra! h
    have : i âˆˆ s.toFinset := by
      refine Set.mem_toFinset.mpr ?_
      simp only [s, ne_eq, Set.mem_setOf_eq]
      use xâ‚€
      simpa using âŸ¨h, mem_of_mem_nhds hu'âŸ©
    exact hi this
  rw [TotalSpace.mk_inj, tsum_eq_sum']
  refine support_subset_iff'.mpr fun i hi â†¦ ?_
  by_contra! h
  have : i âˆˆ s.toFinset := by
    refine Set.mem_toFinset.mpr ?_
    simp only [s, ne_eq, Set.mem_setOf_eq]
    use y
    simpa using âŸ¨h, Set.mem_of_mem_inter_right hyâŸ©
  exact hi this

/-- The sum of a locally finite collection of sections is `C^k` at `x` iff each section is. -/
lemma ContMDiffAt.sum_section_of_locallyFinite (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘' i, (t i x))) xâ‚€ := by
  simp_rw [â† contMDiffWithinAt_univ] at ht' âŠ¢
  exact .sum_section_of_locallyFinite ht ht'

/-- The sum of a locally finite collection of sections is `C^k` on a set `u` iff each section is. -/
lemma ContMDiffOn.sum_section_of_locallyFinite (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘' i, (t i x))) u :=
  fun x hx â†¦ .sum_section_of_locallyFinite ht (ht' Â· x hx)

/-- The sum of a locally finite collection of sections is `C^k` iff each section is. -/
lemma ContMDiff.sum_section_of_locallyFinite (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘' i, (t i x))) :=
  fun x â†¦ .sum_section_of_locallyFinite ht fun i â†¦ ht' i x

-- Future: the next four lemmas can presumably be generalised, but some hypotheses on the supports
-- of the sections `t i` are necessary.
lemma ContMDiffWithinAt.finsum_section_of_locallyFinite
    (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) u xâ‚€) :
    ContMDiffWithinAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘á¶  i, t i x)) u xâ‚€ := by
  apply (ContMDiffWithinAt.sum_section_of_locallyFinite ht ht').congr' (t := Set.univ)
      (fun y hy â†¦ ?_) (by grind) trivial
  rw [â† tsum_eq_finsum (L := SummationFilter.unconditional Î¹)]
  choose U hu hfin using ht y
  have : {x | t x y â‰  0} âŠ† {i | ((fun i â†¦ {x | t i x â‰  0}) i âˆ© U).Nonempty} := by
    intro x hx
    rw [Set.mem_setOf] at hx âŠ¢
    use y
    simpa using âŸ¨hx, mem_of_mem_nhds huâŸ©
  exact Set.Finite.subset hfin this

lemma ContMDiffAt.finsum_section_of_locallyFinite
    (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) xâ‚€) :
    ContMDiffAt I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘á¶  i, t i x)) xâ‚€ := by
  simp_rw [â† contMDiffWithinAt_univ] at ht' âŠ¢
  exact ContMDiffWithinAt.finsum_section_of_locallyFinite ht ht'

lemma ContMDiffOn.finsum_section_of_locallyFinite
    (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x)) u) :
    ContMDiffOn I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘á¶  i, t i x)) u :=
  fun x hx â†¦ ContMDiffWithinAt.finsum_section_of_locallyFinite ht fun i â†¦ ht' i x hx

lemma ContMDiff.finsum_section_of_locallyFinite (ht : LocallyFinite fun i â†¦ {x : M | t i x â‰  0})
    (ht' : âˆ€ i, ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (t i x))) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n (fun x â†¦ TotalSpace.mk' F x (âˆ‘á¶  i, t i x)) :=
  fun x â†¦ ContMDiffAt.finsum_section_of_locallyFinite ht fun i â†¦ ht' i x

end operations

/-- Bundled `n` times continuously differentiable sections of a vector bundle.
Denoted as `Câ‚›^nâŸ®I; F, VâŸ¯` within the `Manifold` namespace. -/
structure ContMDiffSection where
  /-- the underlying function of this section -/
  protected toFun : âˆ€ x, V x
  /-- proof that this section is `C^n` -/
  protected contMDiff_toFun : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n fun x â†¦
    TotalSpace.mk' F x (toFun x)

@[inherit_doc] scoped[Manifold] notation "Câ‚›^" n "âŸ®" I "; " F ", " V "âŸ¯" => ContMDiffSection I F n V

namespace ContMDiffSection

variable {I} {n} {F} {V}

instance : DFunLike Câ‚›^nâŸ®I; F, VâŸ¯ M V where
  coe := ContMDiffSection.toFun
  coe_injective' := by rintro âŸ¨âŸ© âŸ¨âŸ© h; congr

variable {s t : Câ‚›^nâŸ®I; F, VâŸ¯}

@[simp]
theorem coeFn_mk (s : âˆ€ x, V x)
    (hs : ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n fun x => TotalSpace.mk x (s x)) :
    (mk s hs : âˆ€ x, V x) = s :=
  rfl

protected theorem contMDiff (s : Câ‚›^nâŸ®I; F, VâŸ¯) :
    ContMDiff I (I.prod ğ“˜(ğ•œ, F)) n fun x => TotalSpace.mk' F x (s x : V x) :=
  s.contMDiff_toFun

theorem coe_inj â¦ƒs t : Câ‚›^nâŸ®I; F, VâŸ¯â¦„ (h : (s : âˆ€ x, V x) = t) : s = t :=
  DFunLike.ext' h

theorem coe_injective : Injective ((â†‘) : Câ‚›^nâŸ®I; F, VâŸ¯ â†’ âˆ€ x, V x) :=
  coe_inj

@[ext]
theorem ext (h : âˆ€ x, s x = t x) : s = t := DFunLike.ext _ _ h

section
variable [âˆ€ x, AddCommGroup (V x)] [âˆ€ x, Module ğ•œ (V x)] [VectorBundle ğ•œ F V]

instance instAdd : Add Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨fun s t â†¦ âŸ¨s + t, s.contMDiff.add_section t.contMDiffâŸ©âŸ©

@[simp]
theorem coe_add (s t : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(s + t) = â‡‘s + t :=
  rfl

instance instSub : Sub Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨fun s t â†¦ âŸ¨s - t, s.contMDiff.sub_section t.contMDiffâŸ©âŸ©

@[simp]
theorem coe_sub (s t : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(s - t) = s - t :=
  rfl

instance instZero : Zero Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨âŸ¨fun _ => 0, (contMDiff_zeroSection ğ•œ V).of_le le_topâŸ©âŸ©

instance inhabited : Inhabited Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨0âŸ©

@[simp]
theorem coe_zero : â‡‘(0 : Câ‚›^nâŸ®I; F, VâŸ¯) = 0 :=
  rfl

instance instNeg : Neg Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨fun s â†¦ âŸ¨-s, s.contMDiff.neg_sectionâŸ©âŸ©

@[simp]
theorem coe_neg (s : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(-s : Câ‚›^nâŸ®I; F, VâŸ¯) = -s :=
  rfl

instance instNSMul : SMul â„• Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨nsmulRecâŸ©

@[simp]
theorem coe_nsmul (s : Câ‚›^nâŸ®I; F, VâŸ¯) (k : â„•) : â‡‘(k â€¢ s : Câ‚›^nâŸ®I; F, VâŸ¯) = k â€¢ â‡‘s := by
  induction k with
  | zero => simp_rw [zero_smul]; rfl
  | succ k ih => simp_rw [succ_nsmul, â† ih]; rfl

instance instZSMul : SMul â„¤ Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨zsmulRecâŸ©

@[simp]
theorem coe_zsmul (s : Câ‚›^nâŸ®I; F, VâŸ¯) (z : â„¤) : â‡‘(z â€¢ s : Câ‚›^nâŸ®I; F, VâŸ¯) = z â€¢ â‡‘s := by
  rcases z with n | n
  Â· refine (coe_nsmul s n).trans ?_
    simp only [Int.ofNat_eq_natCast, natCast_zsmul]
  Â· refine (congr_arg Neg.neg (coe_nsmul s (n + 1))).trans ?_
    simp only [negSucc_zsmul]

instance instAddCommGroup : AddCommGroup Câ‚›^nâŸ®I; F, VâŸ¯ :=
  coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub coe_nsmul coe_zsmul

instance instSMul : SMul ğ•œ Câ‚›^nâŸ®I; F, VâŸ¯ :=
  âŸ¨fun c s â†¦ âŸ¨c â€¢ â‡‘s, s.contMDiff.const_smul_sectionâŸ©âŸ©

@[simp]
theorem coe_smul (r : ğ•œ) (s : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(r â€¢ s : Câ‚›^nâŸ®I; F, VâŸ¯) = r â€¢ â‡‘s :=
  rfl

variable (I F V n) in
/-- The additive morphism from `C^n` sections to dependent maps. -/
def coeAddHom : Câ‚›^nâŸ®I; F, VâŸ¯ â†’+ âˆ€ x, V x where
  toFun := (â†‘)
  map_zero' := coe_zero
  map_add' := coe_add

@[simp]
theorem coeAddHom_apply (s : Câ‚›^nâŸ®I; F, VâŸ¯) : coeAddHom I F n V s = s := rfl

instance instModule : Module ğ•œ Câ‚›^nâŸ®I; F, VâŸ¯ :=
  coe_injective.module ğ•œ (coeAddHom I F n V) coe_smul

end

protected theorem mdifferentiable' (s : Câ‚›^nâŸ®I; F, VâŸ¯) (hn : 1 â‰¤ n) :
    MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) fun x => TotalSpace.mk' F x (s x : V x) :=
  s.contMDiff.mdifferentiable hn

protected theorem mdifferentiable (s : Câ‚›^âˆâŸ®I; F, VâŸ¯) :
    MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) fun x => TotalSpace.mk' F x (s x : V x) :=
  s.contMDiff.mdifferentiable (mod_cast le_top)

protected theorem mdifferentiableAt (s : Câ‚›^âˆâŸ®I; F, VâŸ¯) {x} :
    MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x => TotalSpace.mk' F x (s x : V x)) x :=
  s.mdifferentiable x

end ContMDiffSection
