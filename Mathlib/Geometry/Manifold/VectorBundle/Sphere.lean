/-
Copyright (c) 2025 Miyahara KÅ. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Miyahara KÅ
-/

import Mathlib.Geometry.Manifold.VectorBundle.Tangent
import Mathlib.Geometry.Manifold.Instances.Sphere

/-!
# Constructing a smooth map to `Tğ•Šâ¿` from its components

This file provides a more convenient way to construct a smooth map `M â†’ Tğ•Šâ¿`
from its components: smooth functions `M â†’ ğ•Šâ¿` and `M â†’ â„â¿âºÂ¹` which are orthogonal in `â„â¿`.

## Main definitions

* `sphereTangentMap f g` : convert map `f : M â†’ ğ•Šâ¿` and `g : M â†’ â„â¿âºÂ¹`
  which satisfy `âˆ€ x, âŸªf x, g xâŸ«_â„ = 0` to a map `M â†’ Tğ•Šâ¿`.

## Main statements

* `mfderiv_coe_sphere_sphereTangentMap_snd` : Let `Î¹` be an inclusion map from `Tğ•Šâ¿` to `Tâ„â¿âºÂ¹`,
  then `Î¹ (sphereTangentMap n f g hf x).snd = g x`.

* `contMDiff_sphereTangentMap` : if `f` and `g` are smooth, then so is `sphereTangentMap n f g hf'`

-/

open Metric Module Function

open scoped Manifold ContDiff InnerProductSpace

open private stereographic'_neg from Mathlib.Geometry.Manifold.Instances.Sphere

noncomputable section

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E] (v : E)
variable {m : WithTop â„•âˆ} {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
variable {H : Type*} [TopologicalSpace H] {I : ModelWithCorners â„ F H}
variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I m M]

/-- Convert `f : M â†’ ğ•Šâ¿` and `g : M â†’ â„â¿âºÂ¹` which satisfy `âˆ€ x, âŸªf x, g xâŸ«_â„ = 0` to `M â†’ Tğ•Šâ¿`. -/
def sphereTangentMap (n : â„•) [Fact (finrank â„ E = n + 1)]
    (f : M â†’ sphere (0 : E) 1) (g : M â†’ E)
    (hf : âˆ€ x, âŸª(â†‘(f x) : E), g xâŸ«_â„ = 0) (x : M) : TangentBundle (ğ“¡ n) (sphere (0 : E) 1) :=
  âŸ¨f x,
    (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere (-f x))).repr
      (âŸ¨g x, Submodule.mem_orthogonal_singleton_iff_inner_right.mpr (by simp [hf x])âŸ© :
        (â„ âˆ™ (-â†‘(f x) : E))á—®)âŸ©

omit [TopologicalSpace M] in
@[simp]
theorem sphereTangentMap_proj (n : â„•) [Fact (finrank â„ E = n + 1)]
    (f : M â†’ sphere (0 : E) 1) (g : M â†’ E)
    (hf : âˆ€ x, âŸª(â†‘(f x) : E), g xâŸ«_â„ = 0) (x : M) : (sphereTangentMap n f g hf x).proj = f x :=
  rfl

omit [TopologicalSpace M] in
/-- Let `Î¹` be an inclusion map from `Tğ•Šâ¿` to `Tâ„â¿âºÂ¹`, then
`Î¹ (sphereTangentMap n f g hf x).snd = g x`. -/
@[simp]
theorem mfderiv_coe_sphere_sphereTangentMap_snd (n : â„•) [Fact (finrank â„ E = n + 1)]
    (f : M â†’ sphere (0 : E) 1) (g : M â†’ E)
    (hf : âˆ€ x, âŸª(â†‘(f x) : E), g xâŸ«_â„ = 0) (x : M) :
    mfderiv (ğ“¡ n) ğ“˜(â„, E) ((â†‘) : sphere (0 : E) 1 â†’ E) (f x) (sphereTangentMap n f g hf x).snd =
      g x := by
  rw [((contMDiff_coe_sphere (f x)).mdifferentiableAt le_top).mfderiv]
  simp only [mfld_simps, chartAt, ChartedSpace.chartAt, stereographic'_neg]
  simp only [
    PartialHomeomorph.coe_trans_symm, PartialHomeomorph.mk_coe_symm, PartialEquiv.coe_symm_mk,
    stereographic', coe_neg_sphere, stereographic,
    LinearIsometryEquiv.coe_toHomeomorph, â† comp_assoc, coe_sphere_comp_stereoInvFun]
  simp only [comp_assoc]
  conv =>
    enter [1, 1]
    tactic =>
      rw [fderivWithin_univ, fderiv_comp]
      Â· exact contDiff_stereoInvFunAux.contDiffAt.differentiableAt (right_eq_inf.mp rfl)
      Â· exact
          (((â„ âˆ™ (-â†‘(f x) : E))á—®).subtypeL.comp
            (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) (E := E) n
              (ne_zero_of_mem_unit_sphere (-f x))
            ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).differentiableAt
  conv =>
    enter [1, 1, 2]
    exact
      (((â„ âˆ™ (-â†‘(f x) : E))á—®).subtypeL.comp
        (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) (E := E) n
            (ne_zero_of_mem_unit_sphere (-f x))
          ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).fderiv
  simp [sphereTangentMap, TangentSpace, (hasFDerivAt_stereoInvFunAux (-(â†‘(f x) : E))).fderiv]

omit [IsManifold I m M] in
private theorem contMDiff_sphereTangentMap_aux {n : â„•} [Fact (finrank â„ E = n + 1)]
    {f : M â†’ â†¥(sphere (0 : E) 1)} {g : M â†’ E}
    (hf : ContMDiff I (ğ“¡ n) m f) (hf' : âˆ€ x, âŸª(â†‘(f x) : E), g xâŸ«_â„ = 0)
    (hg : ContMDiff I ğ“˜(â„, E) m g) (p : â†¥(sphere (0 : E) 1)) :
    ContMDiffOn I ğ“˜(â„, EuclideanSpace â„ (Fin n)) m
      (fun x =>
        (fderiv â„
            (â‡‘(chartAt (EuclideanSpace â„ (Fin n)) p) âˆ˜
              â‡‘(chartAt (EuclideanSpace â„ (Fin n)) (f x)).symm) 0)
          (sphereTangentMap n f g hf' x).snd)
      (f â»Â¹' (chartAt (EuclideanSpace â„ (Fin n)) p).source) := by
  conv =>
    enter [4, x, 1, 2]
    change â‡‘(stereographic' n (-p)) âˆ˜ â‡‘(stereographic' n (-f x)).symm
    simp only [mfld_simps, stereographic', coe_neg_sphere, stereographic,
      LinearIsometryEquiv.coe_toHomeomorph, LinearIsometryEquiv.toHomeomorph_symm,
      comp_assoc]
    simp only [â† comp_assoc Subtype.val, coe_sphere_comp_stereoInvFun, â† comp_assoc]
    simp only [comp_assoc _ Subtype.val, comp_assoc _ (stereoToFun (-(â†‘p : E)))]
  conv =>
    tactic =>
      apply propext âˆ˜ contMDiffOn_congr
      intro x hx
      rw [fderiv_comp]
      Â· refine
          (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) (E := E) n
            (ne_zero_of_mem_unit_sphere (-p))
          ).repr.toContinuousLinearEquiv.toContinuousLinearMap.differentiableAt.comp _
            (DifferentiableAt.comp _ ?_
              (contDiff_stereoInvFunAux.contDiffAt.differentiableAt (right_eq_inf.mp rfl)))
        simp only [coe_neg_sphere, comp_apply, map_zero, ZeroMemClass.coe_zero,
          stereoInvFunAux_apply, norm_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true,
          zero_pow, zero_add, smul_zero, zero_sub, smul_neg, neg_smul, neg_neg, inv_smul_smulâ‚€]
        refine
          ((contDiffOn_stereoToFun (E := E) (v := -(â†‘p : E))).contDiffAt (IsOpen.mem_nhds ?_ ?_)
            ).differentiableAt (right_eq_inf.mp rfl)
        Â· exact
            (innerSL â„ (-(â†‘p : E))).continuous.isOpen_preimage {1}á¶œ isOpen_compl_singleton
        Â· simp only [innerSL_apply, ne_eq, Set.mem_setOf_eq, â† sphere_ext_iff, â† coe_neg_sphere]
          simpa [chartAt, ChartedSpace.chartAt, eq_comm (a := f x)] using hx
      Â· exact
          (((â„ âˆ™ (-â†‘(f x) : E))á—®).subtypeL.comp
            (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) (E := E) n
              (ne_zero_of_mem_unit_sphere (-f x))
            ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).differentiableAt
  conv =>
    enter [4, x, 1, 2]
    exact
      (((â„ âˆ™ (-â†‘(f x) : E))á—®).subtypeL.comp
        (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) (E := E) n
            (ne_zero_of_mem_unit_sphere (-f x))
          ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).fderiv
  simp only [comp_apply, map_zero, ZeroMemClass.coe_zero, coe_neg_sphere, sphereTangentMap,
    ContinuousLinearMap.coe_comp', Submodule.coe_subtypeL', Submodule.coe_subtype,
    ContinuousLinearEquiv.coe_coe, LinearIsometryEquiv.coe_toContinuousLinearEquiv,
    LinearIsometryEquiv.symm_apply_apply]
  refine ContMDiffOn.clm_apply (fun x hx => ContMDiffAt.contMDiffWithinAt ?_) hg.contMDiffOn
  conv =>
    arg 4
    change
      (fun x => fderiv â„ ((OrthonormalBasis.fromOrthogonalSpanSingleton n
        (ne_zero_of_mem_unit_sphere (-p))).repr âˆ˜ stereoToFun (-(â†‘p : E)) âˆ˜
          stereoInvFunAux (-x)) 0) âˆ˜ ((â†‘) : â†¥(sphere (0 : E) 1) â†’ E) âˆ˜ f
  refine
    ContMDiffAt.comp _ (ContMDiffAt.of_le ?_ le_top)
      (((contMDiff_coe_sphere (E := E) (n := n)).contMDiffAt.of_le le_top).comp _
        hf.contMDiffAt)
  rw [contMDiffAt_iff_contDiffAt]
  refine ContDiffAt.fderiv (n := Ï‰) ?_ contDiffAt_const (right_eq_inf.mp rfl)
  conv =>
    arg 3
    change
      (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere (-p))).repr âˆ˜
        (stereoToFun (-(â†‘p : E)) âˆ˜ uncurry stereoInvFunAux) âˆ˜ Prod.map Neg.neg id
  refine
    (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) (E := E) n
        (ne_zero_of_mem_unit_sphere (-p))
      ).repr.toContinuousLinearEquiv.toContinuousLinearMap.contDiff.contDiffAt.comp _ ?_
  refine ContDiffAt.comp _ ?_ (ContDiffAt.prod_map contDiff_neg.contDiffAt contDiffAt_id)
  refine ContDiffAt.comp _ ?_ contDiff_uncurry_stereoInvFunAux.contDiffAt
  simp only [coe_neg_sphere, comp_apply, Prod.map_apply, id_eq, uncurry_apply_pair,
    stereoInvFunAux_apply, norm_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow,
    zero_add, smul_zero, zero_sub, smul_neg, neg_smul, neg_neg, inv_smul_smulâ‚€]
  refine
    (contDiffOn_stereoToFun (E := E) (v := -(â†‘p : E))).contDiffAt (IsOpen.mem_nhds ?_ ?_)
  Â· exact
      (innerSL â„ (-(â†‘p : E))).continuous.isOpen_preimage {1}á¶œ isOpen_compl_singleton
  Â· simp only [map_neg, ContinuousLinearMap.neg_apply, innerSL_apply, ne_eq,
      Set.mem_setOf_eq]
    have hp := inner_eq_norm_mul_iff_real (x := (â†‘p : E)) (y := -(â†‘(f x) : E))
    simp only [inner_neg_right, norm_eq_of_mem_sphere, norm_neg, mul_one, one_smul,
      smul_neg, chartAt, ChartedSpace.chartAt, stereographic'_source, Set.mem_compl_iff,
      Set.mem_singleton_iff] at hp hx
    rw [hp, â† coe_neg_sphere, Subtype.val_inj, â† neg_eq_iff_eq_neg, â† ne_eq]
    symm; exact hx

/-- If `f` & `g` are smooth, then `sphereTangentMap n f g hf'` too. -/
theorem contMDiff_sphereTangentMap {n : â„•} [Fact (finrank â„ E = n + 1)]
    {f : M â†’ â†¥(sphere (0 : E) 1)} {g : M â†’ E}
    (hf : ContMDiff I (ğ“¡ n) m f) (hf' : âˆ€ x, âŸª(â†‘(f x) : E), g xâŸ«_â„ = 0)
    (hg : ContMDiff I ğ“˜(â„, E) m g) : ContMDiff I (ğ“¡ n).tangent m (sphereTangentMap n f g hf') := by
  rw [contMDiff_iff_target]
  constructor
  Â· rw [continuous_generateFrom_iff]
    simp only [mfld_simps, Set.iUnion_coe_set, Set.mem_iUnion,
      Set.mem_singleton_iff, exists_prop, forall_exists_index, and_imp]
    rintro _ _ âŸ¨p, rflâŸ© s hs rfl; rcases neg_surjective p with âŸ¨p, rflâŸ©
    rw [Set.preimage_inter]
    conv =>
      enter [1, 1]
      change
        sphereTangentMap n f g hf' â»Â¹'
          (Bundle.TotalSpace.proj â»Â¹' (chartAt (EuclideanSpace â„ (Fin n)) p).source)
    simp only [mfld_simps, stereographic'_source, Set.preimage_compl, Set.preimage_preimage,
      sphereTangentMap_proj, FiberBundleCore.localTriv_apply,
      tangentBundleCore_indexAt, tangentBundleCore_coordChange, Set.range_id, fderivWithin_univ]
    refine
      ContinuousOn.isOpen_inter_preimage (ContinuousOn.prod (contMDiff_iff.mp hf).1.continuousOn ?_)
        ((contMDiff_iff.mp hf).1.isOpen_preimage _
          (chartAt (EuclideanSpace â„ (Fin n)) p).open_source) hs
    simp only [chartAt, ChartedSpace.chartAt, stereographic'_neg]
    exact (contMDiffOn_iff.mp (contMDiff_sphereTangentMap_aux hf hf' hg p)).1
  Â· rintro âŸ¨p, vâŸ©
    conv =>
      arg 4
      simp only [mfld_simps, TangentBundle.chartAt, â† TangentBundle.trivializationAt_eq_localTriv,
        comp_def, TangentBundle.trivializationAt_apply,
        fderivWithin_univ, chartAt, ChartedSpace.chartAt, stereographic'_neg]
    conv =>
      arg 5
      tactic =>
        ext x
        simp only [mfld_simps, sphereTangentMap_proj]
        rw [â† Set.mem_preimage]
    apply ContMDiffOn.prod_mk_space
    Â· rw [contMDiff_iff_target] at hf
      simpa using hf.2 p
    Â· exact contMDiff_sphereTangentMap_aux hf hf' hg p
