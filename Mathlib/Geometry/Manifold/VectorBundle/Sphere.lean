/-
Copyright (c) 2025 Miyahara K≈ç. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Miyahara K≈ç
-/

import Mathlib.Geometry.Manifold.VectorBundle.Tangent
import Mathlib.Geometry.Manifold.Instances.Sphere

/-!
# Convert orthogonal smooth `M ‚Üí ùïä‚Åø` & `M ‚Üí ‚Ñù‚Åø‚Å∫¬π` to smooth `M ‚Üí Tùïä‚Åø`

## Main definitions

* `sphereTangentMap` : Convert `f : M ‚Üí ùïä‚Åø` & `g : M ‚Üí ‚Ñù‚Åø‚Å∫¬π` which satisfy `‚àÄ x, ‚ü™f x, g x‚ü´_‚Ñù = 0`
  to `M ‚Üí Tùïä‚Åø`.

## Main statements

* `mfderiv_coe_sphere_sphereTangentMap_snd` : Let `Œπ` be an inclusion map from `Tùïä‚Åø` to `T‚Ñù‚Åø‚Å∫¬π`,
  then `Œπ (sphereTangentMap n f g hf x).snd = g x`.

* `contMDiff_sphereTangentMap` : If `f` & `g` are smooth, then `sphereTangentMap n f g hf'` too.

-/

open Metric Module Function

open scoped Manifold ContDiff InnerProductSpace

open private stereographic'_neg from Mathlib.Geometry.Manifold.Instances.Sphere

noncomputable section

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] (v : E)
variable {m : WithTop ‚Ñï‚àû} {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
variable {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù F H}
variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I m M]

/-- Convert `f : M ‚Üí ùïä‚Åø` & `g : M ‚Üí ‚Ñù‚Åø‚Å∫¬π` which satisfy `‚àÄ x, ‚ü™f x, g x‚ü´_‚Ñù = 0` to `M ‚Üí Tùïä‚Åø`. -/
def sphereTangentMap (n : ‚Ñï) [Fact (finrank ‚Ñù E = n + 1)]
    (f : M ‚Üí sphere (0 : E) 1) (g : M ‚Üí E)
    (hf : ‚àÄ x, ‚ü™(‚Üë(f x) : E), g x‚ü´_‚Ñù = 0) (x : M) : TangentBundle (ùì° n) (sphere (0 : E) 1) :=
  ‚ü®f x,
    (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere (-f x))).repr
      (‚ü®g x, Submodule.mem_orthogonal_singleton_iff_inner_right.mpr (by simp [hf x])‚ü© :
        (‚Ñù ‚àô (-‚Üë(f x) : E))·óÆ)‚ü©

omit [TopologicalSpace M] in
@[simp]
theorem sphereTangentMap_proj (n : ‚Ñï) [Fact (finrank ‚Ñù E = n + 1)]
    (f : M ‚Üí sphere (0 : E) 1) (g : M ‚Üí E)
    (hf : ‚àÄ x, ‚ü™(‚Üë(f x) : E), g x‚ü´_‚Ñù = 0) (x : M) : (sphereTangentMap n f g hf x).proj = f x :=
  rfl

omit [TopologicalSpace M] in
/-- Let `Œπ` be an inclusion map from `Tùïä‚Åø` to `T‚Ñù‚Åø‚Å∫¬π`, then
`Œπ (sphereTangentMap n f g hf x).snd = g x`. -/
@[simp]
theorem mfderiv_coe_sphere_sphereTangentMap_snd (n : ‚Ñï) [Fact (finrank ‚Ñù E = n + 1)]
    (f : M ‚Üí sphere (0 : E) 1) (g : M ‚Üí E)
    (hf : ‚àÄ x, ‚ü™(‚Üë(f x) : E), g x‚ü´_‚Ñù = 0) (x : M) :
    mfderiv (ùì° n) ùìò(‚Ñù, E) ((‚Üë) : sphere (0 : E) 1 ‚Üí E) (f x) (sphereTangentMap n f g hf x).snd =
      g x := by
  rw [((contMDiff_coe_sphere (f x)).mdifferentiableAt le_top).mfderiv]
  simp only [writtenInExtChartAt, extChartAt, PartialHomeomorph.extend,
    PartialHomeomorph.refl_partialEquiv, PartialEquiv.refl_source,
    PartialHomeomorph.singletonChartedSpace_chartAt_eq, modelWithCornersSelf_partialEquiv,
    PartialEquiv.trans_refl, PartialEquiv.refl_coe, PartialHomeomorph.coe_coe_symm,
    CompTriple.comp_eq, modelWithCornersSelf_coe, Set.range_id, PartialHomeomorph.toFun_eq_coe,
    chartAt, ChartedSpace.chartAt, stereographic'_neg, fderivWithin_univ]
  simp only [chartAt, ChartedSpace.chartAt, stereographic', coe_neg_sphere, stereographic,
    PartialHomeomorph.coe_trans_symm, PartialHomeomorph.mk_coe_symm, PartialEquiv.coe_symm_mk,
    Homeomorph.toPartialHomeomorph_symm_apply, LinearIsometryEquiv.toHomeomorph_symm,
    LinearIsometryEquiv.coe_toHomeomorph, ‚Üê comp_assoc, coe_sphere_comp_stereoInvFun]
  simp only [comp_assoc]
  conv =>
    enter [1, 1]
    tactic =>
      rw [fderiv_comp]
      ¬∑ exact contDiff_stereoInvFunAux.contDiffAt.differentiableAt (right_eq_inf.mp rfl)
      ¬∑ exact
          (((‚Ñù ‚àô (-‚Üë(f x) : E))·óÆ).subtypeL.comp
            (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) (E := E) n
              (ne_zero_of_mem_unit_sphere (-f x))
            ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).differentiableAt
  conv =>
    enter [1, 1, 2]
    exact
      (((‚Ñù ‚àô (-‚Üë(f x) : E))·óÆ).subtypeL.comp
        (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) (E := E) n
            (ne_zero_of_mem_unit_sphere (-f x))
          ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).fderiv
  simp [sphereTangentMap, TangentSpace, (hasFDerivAt_stereoInvFunAux (-(‚Üë(f x) : E))).fderiv]

omit [IsManifold I m M] in
private theorem contMDiff_sphereTangentMap_aux {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)]
    {f : M ‚Üí ‚Ü•(sphere (0 : E) 1)} {g : M ‚Üí E}
    (hf : ContMDiff I (ùì° n) m f) (hf' : ‚àÄ x, ‚ü™(‚Üë(f x) : E), g x‚ü´_‚Ñù = 0)
    (hg : ContMDiff I ùìò(‚Ñù, E) m g) (p : ‚Ü•(sphere (0 : E) 1)) :
    ContMDiffOn I ùìò(‚Ñù, EuclideanSpace ‚Ñù (Fin n)) m
      (fun x =>
        (fderiv ‚Ñù
            (‚áë(chartAt (EuclideanSpace ‚Ñù (Fin n)) p) ‚àò
              ‚áë(chartAt (EuclideanSpace ‚Ñù (Fin n)) (f x)).symm) 0)
          (sphereTangentMap n f g hf' x).snd)
      (f ‚Åª¬π' (chartAt (EuclideanSpace ‚Ñù (Fin n)) p).source) := by
  conv =>
    enter [4, x, 1, 2]
    change ‚áë(stereographic' n (-p)) ‚àò ‚áë(stereographic' n (-f x)).symm
    simp only [stereographic', coe_neg_sphere, stereographic,
      PartialHomeomorph.coe_trans, Homeomorph.toPartialHomeomorph_apply,
      LinearIsometryEquiv.coe_toHomeomorph, PartialHomeomorph.mk_coe,
      PartialHomeomorph.coe_trans_symm, PartialHomeomorph.mk_coe_symm, PartialEquiv.coe_symm_mk,
      Homeomorph.toPartialHomeomorph_symm_apply, LinearIsometryEquiv.toHomeomorph_symm,
      comp_assoc]
    simp only [‚Üê comp_assoc Subtype.val, coe_sphere_comp_stereoInvFun]
    simp only [‚Üê comp_assoc]
    simp only [comp_assoc _ Subtype.val, comp_assoc _ (stereoToFun (-(‚Üëp : E)))]
  conv =>
    tactic =>
      apply propext ‚àò contMDiffOn_congr
      intro x hx
      rw [fderiv_comp]
      ¬∑ refine
          (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) (E := E) n
            (ne_zero_of_mem_unit_sphere (-p))
          ).repr.toContinuousLinearEquiv.toContinuousLinearMap.differentiableAt.comp _
            (DifferentiableAt.comp _ ?_
              (contDiff_stereoInvFunAux.contDiffAt.differentiableAt (right_eq_inf.mp rfl)))
        simp only [coe_neg_sphere, comp_apply, map_zero, ZeroMemClass.coe_zero,
          stereoInvFunAux_apply, norm_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true,
          zero_pow, zero_add, smul_zero, zero_sub, smul_neg, neg_smul, neg_neg, inv_smul_smul‚ÇÄ]
        refine
          ((contDiffOn_stereoToFun (E := E) (v := -(‚Üëp : E))).contDiffAt (IsOpen.mem_nhds ?_ ?_)
            ).differentiableAt (right_eq_inf.mp rfl)
        ¬∑ exact
            (innerSL ‚Ñù (-(‚Üëp : E))).continuous.isOpen_preimage {1}·∂ú isOpen_compl_singleton
        ¬∑ simp only [innerSL_apply, ne_eq, Set.mem_setOf_eq, ‚Üê sphere_ext_iff, ‚Üê coe_neg_sphere]
          simpa [chartAt, ChartedSpace.chartAt, eq_comm (a := f x)] using hx
      ¬∑ exact
          (((‚Ñù ‚àô (-‚Üë(f x) : E))·óÆ).subtypeL.comp
            (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) (E := E) n
              (ne_zero_of_mem_unit_sphere (-f x))
            ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).differentiableAt
  conv =>
    enter [4, x, 1, 2]
    exact
      (((‚Ñù ‚àô (-‚Üë(f x) : E))·óÆ).subtypeL.comp
        (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) (E := E) n
            (ne_zero_of_mem_unit_sphere (-f x))
          ).repr.symm.toContinuousLinearEquiv.toContinuousLinearMap).fderiv
  simp only [comp_apply, map_zero, ZeroMemClass.coe_zero, coe_neg_sphere, sphereTangentMap,
    ContinuousLinearMap.coe_comp', Submodule.coe_subtypeL', Submodule.coe_subtype,
    ContinuousLinearEquiv.coe_coe, LinearIsometryEquiv.coe_toContinuousLinearEquiv,
    LinearIsometryEquiv.symm_apply_apply]
  refine ContMDiffOn.clm_apply (fun x hx => ContMDiffAt.contMDiffWithinAt ?_) hg.contMDiffOn
  conv =>
    arg 4
    change
      (fun x => fderiv ‚Ñù ((OrthonormalBasis.fromOrthogonalSpanSingleton n
        (ne_zero_of_mem_unit_sphere (-p))).repr ‚àò stereoToFun (-(‚Üëp : E)) ‚àò
          stereoInvFunAux (-x)) 0) ‚àò ((‚Üë) : ‚Ü•(sphere (0 : E) 1) ‚Üí E) ‚àò f
  refine
    ContMDiffAt.comp _ (ContMDiffAt.of_le ?_ le_top)
      (((contMDiff_coe_sphere (E := E) (n := n)).contMDiffAt.of_le le_top).comp _
        hf.contMDiffAt)
  rw [contMDiffAt_iff_contDiffAt]
  refine ContDiffAt.fderiv (n := œâ) ?_ contDiffAt_const (right_eq_inf.mp rfl)
  conv =>
    arg 3
    change
      (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere (-p))).repr ‚àò
        (stereoToFun (-(‚Üëp : E)) ‚àò uncurry stereoInvFunAux) ‚àò Prod.map Neg.neg id
  refine
    (OrthonormalBasis.fromOrthogonalSpanSingleton (ùïú := ‚Ñù) (E := E) n
        (ne_zero_of_mem_unit_sphere (-p))
      ).repr.toContinuousLinearEquiv.toContinuousLinearMap.contDiff.contDiffAt.comp _ ?_
  refine ContDiffAt.comp _ ?_ (ContDiffAt.prod_map contDiff_neg.contDiffAt contDiffAt_id)
  refine ContDiffAt.comp _ ?_ contDiff_uncurry_stereoInvFunAux.contDiffAt
  simp only [coe_neg_sphere, comp_apply, Prod.map_apply, id_eq, uncurry_apply_pair,
    stereoInvFunAux_apply, norm_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow,
    zero_add, smul_zero, zero_sub, smul_neg, neg_smul, neg_neg, inv_smul_smul‚ÇÄ]
  refine
    (contDiffOn_stereoToFun (E := E) (v := -(‚Üëp : E))).contDiffAt (IsOpen.mem_nhds ?_ ?_)
  ¬∑ exact
      (innerSL ‚Ñù (-(‚Üëp : E))).continuous.isOpen_preimage {1}·∂ú isOpen_compl_singleton
  ¬∑ simp only [map_neg, ContinuousLinearMap.neg_apply, innerSL_apply, ne_eq,
      Set.mem_setOf_eq]
    have hp := inner_eq_norm_mul_iff_real (x := (‚Üëp : E)) (y := -(‚Üë(f x) : E))
    simp only [inner_neg_right, norm_eq_of_mem_sphere, norm_neg, mul_one, one_smul,
      smul_neg, chartAt, ChartedSpace.chartAt, stereographic'_source, Set.mem_compl_iff,
      Set.mem_singleton_iff] at hp hx
    rw [hp, ‚Üê coe_neg_sphere, Subtype.val_inj, ‚Üê neg_eq_iff_eq_neg, ‚Üê ne_eq]
    symm; exact hx

/-- If `f` & `g` are smooth, then `sphereTangentMap n f g hf'` too. -/
theorem contMDiff_sphereTangentMap {n : ‚Ñï} [Fact (finrank ‚Ñù E = n + 1)]
    {f : M ‚Üí ‚Ü•(sphere (0 : E) 1)} {g : M ‚Üí E}
    (hf : ContMDiff I (ùì° n) m f) (hf' : ‚àÄ x, ‚ü™(‚Üë(f x) : E), g x‚ü´_‚Ñù = 0)
    (hg : ContMDiff I ùìò(‚Ñù, E) m g) : ContMDiff I (ùì° n).tangent m (sphereTangentMap n f g hf') := by
  rw [contMDiff_iff_target]
  constructor
  ¬∑ rw [continuous_generateFrom_iff]
    simp only [FiberBundleCore.localTrivAsPartialEquiv_source, FiberBundleCore.proj,
      FiberBundleCore.localTrivAsPartialEquiv_coe, Set.iUnion_coe_set, Set.mem_iUnion,
      Set.mem_singleton_iff, exists_prop, forall_exists_index, and_imp]
    rintro _ _ ‚ü®p, rfl‚ü© s hs rfl; rcases neg_surjective p with ‚ü®p, rfl‚ü©
    rw [Set.preimage_inter]
    conv =>
      enter [1, 1]
      change
        sphereTangentMap n f g hf' ‚Åª¬π'
          (Bundle.TotalSpace.proj ‚Åª¬π' (chartAt (EuclideanSpace ‚Ñù (Fin n)) p).source)
    simp only [stereographic'_source, Set.preimage_compl, Set.preimage_preimage,
      sphereTangentMap_proj, FiberBundleCore.localTriv_apply,
      VectorBundleCore.toFiberBundleCore_indexAt, tangentBundleCore_indexAt,
      VectorBundleCore.coe_coordChange, tangentBundleCore_coordChange, PartialHomeomorph.extend,
      modelWithCornersSelf_partialEquiv, PartialEquiv.trans_refl, PartialHomeomorph.toFun_eq_coe,
      coe_achart, PartialHomeomorph.coe_coe_symm, modelWithCornersSelf_coe, Set.range_id,
      fderivWithin_univ]
    refine
      ContinuousOn.isOpen_inter_preimage (ContinuousOn.prod (contMDiff_iff.mp hf).1.continuousOn ?_)
        ((contMDiff_iff.mp hf).1.isOpen_preimage _
          (chartAt (EuclideanSpace ‚Ñù (Fin n)) p).open_source) hs
    simp only [chartAt, ChartedSpace.chartAt, stereographic'_neg]
    exact (contMDiffOn_iff.mp (contMDiff_sphereTangentMap_aux hf hf' hg p)).1
  ¬∑ rintro ‚ü®p, v‚ü©
    conv =>
      arg 4
      simp only [extChartAt, PartialHomeomorph.extend, TangentBundle.chartAt,
        FiberBundleCore.proj, ‚Üê TangentBundle.trivializationAt_eq_localTriv,
        PartialHomeomorph.trans_toPartialEquiv, PartialHomeomorph.prod_toPartialEquiv,
        PartialHomeomorph.refl_partialEquiv, modelWithCorners_prod_toPartialEquiv,
        modelWithCornersSelf_partialEquiv, PartialEquiv.refl_prod_refl, PartialEquiv.coe_trans,
        PartialEquiv.refl_coe, PartialEquiv.prod_coe, PartialHomeomorph.toFun_eq_coe, id_eq,
        Trivialization.coe_coe, comp_def, TangentBundle.trivializationAt_apply,
        PartialEquiv.trans_refl, PartialHomeomorph.coe_coe_symm, modelWithCornersSelf_coe,
        Set.range_id, fderivWithin_univ, CompTriple.comp_eq, sphereTangentMap_proj,
        chartAt, ChartedSpace.chartAt, stereographic'_neg]
    conv =>
      arg 5
      tactic =>
        ext x
        simp only [extChartAt, PartialHomeomorph.extend, modelWithCorners_prod_toPartialEquiv,
          modelWithCornersSelf_partialEquiv, PartialEquiv.refl_prod_refl, PartialEquiv.trans_source,
          PartialHomeomorph.toFun_eq_coe, PartialEquiv.refl_source, Set.preimage_univ,
          Set.inter_univ, Set.mem_preimage, TangentBundle.mem_chart_source_iff,
          sphereTangentMap_proj]
        rw [‚Üê Set.mem_preimage]
    apply ContMDiffOn.prod_mk_space
    ¬∑ rw [contMDiff_iff_target] at hf
      simpa using hf.2 p
    ¬∑ exact contMDiff_sphereTangentMap_aux hf hf' hg p
