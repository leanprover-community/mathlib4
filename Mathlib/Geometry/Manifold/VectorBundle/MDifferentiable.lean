/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.MFDeriv.NormedSpace
import Mathlib.Geometry.Manifold.MFDeriv.SpecificFunctions


/-!
# Differentiability of functions in vector bundles

-/

open Bundle Set PartialHomeomorph ContinuousLinearMap Pretrivialization Filter

open scoped Manifold Bundle Topology

section


variable {ğ•œ B B' F M : Type*} {E : B â†’ Type*}

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [TopologicalSpace (TotalSpace F E)] [âˆ€ x, TopologicalSpace (E x)] {EB : Type*}
  [NormedAddCommGroup EB] [NormedSpace ğ•œ EB] {HB : Type*} [TopologicalSpace HB]
  (IB : ModelWithCorners ğ•œ EB HB) (E' : B â†’ Type*) [âˆ€ x, Zero (E' x)] {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  {n : â„•âˆ}

variable [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]


/-- Characterization of differentiable functions into a vector bundle.
Version at a point within a set -/
theorem mdifferentiableWithinAt_totalSpace (f : M â†’ TotalSpace F E) {s : Set M} {xâ‚€ : M} :
    MDifferentiableWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) f s xâ‚€ â†”
      MDifferentiableWithinAt IM IB (fun x => (f x).proj) s xâ‚€ âˆ§
      MDifferentiableWithinAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (trivializationAt F E (f xâ‚€).proj (f x)).2) s xâ‚€ := by
  simp +singlePass only [mdifferentiableWithinAt_iff_target]
  rw [and_and_and_comm, â† FiberBundle.continuousWithinAt_totalSpace, and_congr_right_iff]
  intro hf
  simp_rw [modelWithCornersSelf_prod, FiberBundle.extChartAt, Function.comp_def,
    PartialEquiv.trans_apply, PartialEquiv.prod_coe, PartialEquiv.refl_coe,
    extChartAt_self_apply, modelWithCornersSelf_coe, Function.id_def, â† chartedSpaceSelf_prod]
  refine (mdifferentiableWithinAt_prod_iff _).trans (and_congr ?_ Iff.rfl)
  have h1 : (fun x => (f x).proj) â»Â¹' (trivializationAt F E (f xâ‚€).proj).baseSet âˆˆ ğ“[s] xâ‚€ :=
    ((FiberBundle.continuous_proj F E).continuousWithinAt.comp hf (mapsTo_image f s))
      ((Trivialization.open_baseSet _).mem_nhds (mem_baseSet_trivializationAt F E _))
  refine EventuallyEq.mdifferentiableWithinAt_iff (eventually_of_mem h1 fun x hx => ?_) ?_
  Â· simp_rw [Function.comp, PartialHomeomorph.coe_coe, Trivialization.coe_coe]
    rw [Trivialization.coe_fst']
    exact hx
  Â· simp only [mfld_simps]

/-- Characterization of differentiable functions into a vector bundle.
Version at a point -/
theorem mdifferentiableAt_totalSpace (f : M â†’ TotalSpace F E) {xâ‚€ : M} :
    MDifferentiableAt IM (IB.prod ğ“˜(ğ•œ, F)) f xâ‚€ â†”
      MDifferentiableAt IM IB (fun x => (f x).proj) xâ‚€ âˆ§
      MDifferentiableAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (trivializationAt F E (f xâ‚€).proj (f x)).2) xâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using mdifferentiableWithinAt_totalSpace _ f

/-- Characterization of differentiable sections of a vector bundle at a point within a set
in terms of the preferred trivialization at that point. -/
theorem mdifferentiableWithinAt_section (s : Î  b, E b) {u : Set B} {bâ‚€ : B} :
    MDifferentiableWithinAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) u bâ‚€ â†”
      MDifferentiableWithinAt IB ğ“˜(ğ•œ, F) (fun b â†¦ (trivializationAt F E bâ‚€ (s b)).2) u bâ‚€ := by
  rw [mdifferentiableWithinAt_totalSpace]
  change MDifferentiableWithinAt _ _ id _ _ âˆ§ _ â†” _
  simp [mdifferentiableWithinAt_id]

/-- Characterization of differentiable sections of a vector bundle at a point within a set
in terms of the preferred trivialization at that point. -/
theorem mdifferentiableAt_section (s : Î  b, E b) {bâ‚€ : B} :
    MDifferentiableAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) bâ‚€ â†”
      MDifferentiableAt IB ğ“˜(ğ•œ, F) (fun b â†¦ (trivializationAt F E bâ‚€ (s b)).2) bâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using mdifferentiableWithinAt_section _ _

namespace Bundle

variable (E) {IB}

theorem mdifferentiable_proj : MDifferentiable (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) := fun x â†¦ by
  have : MDifferentiableAt (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) id x := mdifferentiableAt_id
  rw [mdifferentiableAt_totalSpace] at this
  exact this.1

theorem mdifferentiableOn_proj {s : Set (TotalSpace F E)} :
    MDifferentiableOn (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) s :=
  (mdifferentiable_proj E).mdifferentiableOn

theorem mdifferentiableAt_proj {p : TotalSpace F E} :
    MDifferentiableAt (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) p :=
  (mdifferentiable_proj E).mdifferentiableAt

theorem mdifferentiableWithinAt_proj {s : Set (TotalSpace F E)} {p : TotalSpace F E} :
    MDifferentiableWithinAt (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) s p :=
  (mdifferentiableAt_proj E).mdifferentiableWithinAt

variable (ğ•œ) [âˆ€ x, AddCommMonoid (E x)]
variable [âˆ€ x, Module ğ•œ (E x)] [VectorBundle ğ•œ F E]

theorem mdifferentiable_zeroSection : MDifferentiable IB (IB.prod ğ“˜(ğ•œ, F)) (zeroSection F E) := by
  intro x
  unfold zeroSection
  rw [mdifferentiableAt_section]
  apply (mdifferentiableAt_const (c := 0)).congr_of_eventuallyEq
  filter_upwards [(trivializationAt F E x).open_baseSet.mem_nhds
    (mem_baseSet_trivializationAt F E x)] with y hy
    using congr_arg Prod.snd <| (trivializationAt F E x).zeroSection ğ•œ hy

theorem mdifferentiableOn_zeroSection {t : Set B} :
    MDifferentiableOn IB (IB.prod ğ“˜(ğ•œ, F)) (zeroSection F E) t :=
  (mdifferentiable_zeroSection _ _).mdifferentiableOn

theorem mdifferentiableAt_zeroSection {x : B} :
    MDifferentiableAt IB (IB.prod ğ“˜(ğ•œ, F)) (zeroSection F E) x :=
  (mdifferentiable_zeroSection _ _).mdifferentiableAt

theorem mdifferentiableWithinAt_zeroSection {t : Set B} {x : B} :
    MDifferentiableWithinAt IB (IB.prod ğ“˜(ğ•œ, F)) (zeroSection F E) t x :=
  (mdifferentiable_zeroSection _ _ x).mdifferentiableWithinAt

end Bundle

section coordChange

variable [(x : B) â†’ AddCommMonoid (E x)] [(x : B) â†’ Module ğ•œ (E x)]
variable (e e' : Trivialization F (Ï€ F E)) [MemTrivializationAtlas e] [MemTrivializationAtlas e']
  [VectorBundle ğ•œ F E] [ContMDiffVectorBundle 1 F E IB]
variable {IB}

theorem mdifferentiableOn_coordChangeL :
    MDifferentiableOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B â†¦ (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F))
      (e.baseSet âˆ© e'.baseSet) :=
  (contMDiffOn_coordChangeL e e').mdifferentiableOn le_rfl

theorem mdifferentiableOn_symm_coordChangeL :
    MDifferentiableOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B â†¦ ((e.coordChangeL ğ•œ e' b).symm : F â†’L[ğ•œ] F))
      (e.baseSet âˆ© e'.baseSet) :=
  (contMDiffOn_symm_coordChangeL e e').mdifferentiableOn le_rfl

variable {e e'}

theorem mdifferentiableAt_coordChangeL {x : B}
    (h : x âˆˆ e.baseSet) (h' : x âˆˆ e'.baseSet) :
    MDifferentiableAt IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B â†¦ (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F)) x :=
  (contMDiffAt_coordChangeL h h').mdifferentiableAt le_rfl

variable {s : Set M} {f : M â†’ B} {g : M â†’ F} {x : M}

protected theorem MDifferentiableWithinAt.coordChangeL (hf : MDifferentiableWithinAt IM IB f s x)
    (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F)
      (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) s x :=
  (mdifferentiableAt_coordChangeL he he').comp_mdifferentiableWithinAt _ hf

protected theorem MDifferentiableAt.coordChangeL
    (hf : MDifferentiableAt IM IB f x) (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    MDifferentiableAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) x :=
  MDifferentiableWithinAt.coordChangeL hf he he'

protected theorem MDifferentiableOn.coordChangeL
    (hf : MDifferentiableOn IM IB f s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    MDifferentiableOn IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) s :=
  fun x hx â†¦ (hf x hx).coordChangeL (he hx) (he' hx)

protected theorem MDifferentiable.coordChangeL
    (hf : MDifferentiable IM IB f) (he : âˆ€ x, f x âˆˆ e.baseSet) (he' : âˆ€ x, f x âˆˆ e'.baseSet) :
    MDifferentiable IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) := fun x â†¦
  (hf x).coordChangeL (he x) (he' x)

protected theorem MDifferentiableWithinAt.coordChange
    (hf : MDifferentiableWithinAt IM IB f s x) (hg : MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) g s x)
    (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) s x := by
  refine ((hf.coordChangeL he he').clm_apply hg).congr_of_eventuallyEq ?_ ?_
  Â· have : e.baseSet âˆ© e'.baseSet âˆˆ ğ“ (f x) :=
     (e.open_baseSet.inter e'.open_baseSet).mem_nhds âŸ¨he, he'âŸ©
    filter_upwards [hf.continuousWithinAt this] with y hy
    exact (Trivialization.coordChangeL_apply' e e' hy (g y)).symm
  Â· exact (Trivialization.coordChangeL_apply' e e' âŸ¨he, he'âŸ© (g x)).symm

protected theorem MDifferentiableAt.coordChange
    (hf : MDifferentiableAt IM IB f x) (hg : MDifferentiableAt IM ğ“˜(ğ•œ, F) g x)
    (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    MDifferentiableAt IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) x :=
  MDifferentiableWithinAt.coordChange hf hg he he'

protected theorem MDifferentiableOn.coordChange
    (hf : MDifferentiableOn IM IB f s) (hg : MDifferentiableOn IM ğ“˜(ğ•œ, F) g s)
    (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    MDifferentiableOn IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) s := fun x hx â†¦
  (hf x hx).coordChange (hg x hx) (he hx) (he' hx)

protected theorem MDifferentiable.coordChange
    (hf : MDifferentiable IM IB f) (hg : MDifferentiable IM ğ“˜(ğ•œ, F) g)
    (he : âˆ€ x, f x âˆˆ e.baseSet) (he' : âˆ€ x, f x âˆˆ e'.baseSet) :
    MDifferentiable IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) := fun x â†¦
  (hf x).coordChange (hg x) (he x) (he' x)

end coordChange

variable [(x : B) â†’ AddCommMonoid (E x)] [(x : B) â†’ Module ğ•œ (E x)]
  [VectorBundle ğ•œ F E] [ContMDiffVectorBundle 1 F E IB]

lemma MDifferentiableWithinAt.change_section_trivialization
    {e : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e]
    {e' : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e']
    {f : M â†’ TotalSpace F E} {s : Set M} {xâ‚€ : M}
    (hf : MDifferentiableWithinAt IM IB (Ï€ F E âˆ˜ f) s xâ‚€)
    (he'f : MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) s xâ‚€)
    (he : f xâ‚€ âˆˆ e.source) (he' : f xâ‚€ âˆˆ e'.source) :
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e' (f x)).2) s xâ‚€ := by
  rw [Trivialization.mem_source] at he he'
  refine (hf.coordChange he'f he he').congr_of_eventuallyEq ?_ ?_
  Â· filter_upwards [hf.continuousWithinAt (e.open_baseSet.mem_nhds he)] with y hy
    rw [Function.comp_apply, e.coordChange_apply_snd e' hy]
  Â· rw [Function.comp_apply, e.coordChange_apply_snd _ he]

theorem Trivialization.mdifferentiableWithinAt_snd_comp_iffâ‚‚
    {e e' : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e] [MemTrivializationAtlas e']
    {f : M â†’ TotalSpace F E} {s : Set M} {xâ‚€ : M}
    (hexâ‚€ : f xâ‚€ âˆˆ e.source) (he'xâ‚€ : f xâ‚€ âˆˆ e'.source)
    (hf : MDifferentiableWithinAt IM IB (Ï€ F E âˆ˜ f) s xâ‚€) :
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) s xâ‚€ â†”
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e' (f x)).2) s xâ‚€ :=
  âŸ¨(hf.change_section_trivialization IB Â· hexâ‚€ he'xâ‚€),
   (hf.change_section_trivialization IB Â· he'xâ‚€ hexâ‚€)âŸ©

variable (e e')

theorem Trivialization.mdifferentiableAt_snd_comp_iffâ‚‚
    {e e' : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e] [MemTrivializationAtlas e']
    {f : M â†’ TotalSpace F E} {xâ‚€ : M}
    (he : f xâ‚€ âˆˆ e.source) (he' : f xâ‚€ âˆˆ e'.source)
    (hf : MDifferentiableAt IM IB (fun x â†¦ (f x).proj) xâ‚€) :
    MDifferentiableAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) xâ‚€ â†”
    MDifferentiableAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e' (f x)).2) xâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using
    e.mdifferentiableWithinAt_snd_comp_iffâ‚‚ IB he he' hf

/-- Characterization of differentiable functions into a vector bundle in terms
of any trivialization. Version at a point within at set. -/
theorem Trivialization.mdifferentiableWithinAt_totalSpace_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (f : M â†’ TotalSpace F E) {s : Set M} {xâ‚€ : M}
    (he : f xâ‚€ âˆˆ e.source) :
    MDifferentiableWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) f s xâ‚€ â†”
      MDifferentiableWithinAt IM IB (fun x => (f x).proj) s xâ‚€ âˆ§
      MDifferentiableWithinAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (e (f x)).2) s xâ‚€ := by
  rw [mdifferentiableWithinAt_totalSpace]
  apply and_congr_right
  intro hf
  rw [Trivialization.mdifferentiableWithinAt_snd_comp_iffâ‚‚ IB
    (FiberBundle.mem_trivializationAt_proj_source) he hf]

/-- Characterization of differentiable functions into a vector bundle in terms
of any trivialization. Version at a point. -/
theorem Trivialization.mdifferentiableAt_totalSpace_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (f : M â†’ TotalSpace F E) {xâ‚€ : M}
    (he : f xâ‚€ âˆˆ e.source) :
    MDifferentiableAt IM (IB.prod ğ“˜(ğ•œ, F)) f xâ‚€ â†”
      MDifferentiableAt IM IB (fun x => (f x).proj) xâ‚€ âˆ§
      MDifferentiableAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (e (f x)).2) xâ‚€ := by
  rw [mdifferentiableAt_totalSpace]
  apply and_congr_right
  intro hf
  rw [Trivialization.mdifferentiableAt_snd_comp_iffâ‚‚ IB
    (FiberBundle.mem_trivializationAt_proj_source) he hf]

/-- Characterization of differentiable sections a vector bundle in terms
of any trivialization. Version at a point within at set. -/
theorem Trivialization.mdifferentiableWithinAt_section_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (s : Î  b : B, E b) {u : Set B} {bâ‚€ : B}
    (hexâ‚€ : bâ‚€ âˆˆ e.baseSet) :
    MDifferentiableWithinAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) u bâ‚€ â†”
      MDifferentiableWithinAt IB ğ“˜(ğ•œ, F) (fun x â†¦ (e (s x)).2) u bâ‚€ := by
  rw [e.mdifferentiableWithinAt_totalSpace_iff IB]
  Â· change MDifferentiableWithinAt IB IB id u bâ‚€ âˆ§ _ â†” _
    simp [mdifferentiableWithinAt_id]
  exact (coe_mem_source e).mpr hexâ‚€

/-- Characterization of differentiable functions into a vector bundle in terms
of any trivialization. Version at a point. -/
theorem Trivialization.mdifferentiableAt_section_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (s : Î  b : B, E b) {bâ‚€ : B}
    (hexâ‚€ : bâ‚€ âˆˆ e.baseSet) :
    MDifferentiableAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) bâ‚€ â†”
      MDifferentiableAt IB ğ“˜(ğ•œ, F) (fun x â†¦ (e (s x)).2) bâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using e.mdifferentiableWithinAt_section_iff IB s hexâ‚€

variable {IB} in
/-- Differentiability of a section on `s` can be determined
using any trivialisation whose `baseSet` contains `s`. -/
theorem Trivialization.mdifferentiableOn_section_iff {s : âˆ€ x, E x} {a : Set B}
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F E â†’ B))
    [MemTrivializationAtlas e] (ha : IsOpen a) (ha' : a âŠ† e.baseSet) :
    MDifferentiableOn IB (IB.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) a â†”
      MDifferentiableOn IB ğ“˜(ğ•œ, F) (fun x â†¦ (e âŸ¨x, s xâŸ©).2) a := by
  refine âŸ¨fun h x hx â†¦ ?_, fun h x hx â†¦ ?_âŸ© <;>
  have := (h x hx).mdifferentiableAt <| ha.mem_nhds hx
  Â· exact ((e.mdifferentiableAt_section_iff _ _ (ha' hx)).mp this).mdifferentiableWithinAt
  Â· exact ((e.mdifferentiableAt_section_iff _ _ (ha' hx)).mpr this).mdifferentiableWithinAt

variable {IB} in
/-- For any trivialization `e`, the differentiability of a section on `e.baseSet`
can be determined using `e`. -/
theorem Trivialization.mdifferentiableOn_section_baseSet_iff {s : âˆ€ x, E x}
    (e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F E â†’ B))
    [MemTrivializationAtlas e] :
    MDifferentiableOn IB (IB.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) e.baseSet â†”
      MDifferentiableOn IB ğ“˜(ğ•œ, F) (fun x â†¦ (e âŸ¨x, s xâŸ©).2) e.baseSet :=
  e.mdifferentiableOn_section_iff e.open_baseSet subset_rfl

end

section

/- Declare two manifolds `Bâ‚` and `Bâ‚‚` (with models `IBâ‚ : HBâ‚ â†’ EBâ‚` and `IBâ‚‚ : HBâ‚‚ â†’ EBâ‚‚`),
and two vector bundles `Eâ‚` and `Eâ‚‚` respectively over `Bâ‚` and `Bâ‚‚` (with model fibers
`Fâ‚` and `Fâ‚‚`).

Also a third manifold `M`, which will be the source of all our maps.
-/
variable {ğ•œ Fâ‚ Fâ‚‚ Bâ‚ Bâ‚‚ M : Type*} {Eâ‚ : Bâ‚ â†’ Type*} {Eâ‚‚ : Bâ‚‚ â†’ Type*} [NontriviallyNormedField ğ•œ]
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, TopologicalSpace (Eâ‚ x)] [âˆ€ x, AddCommGroup (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, TopologicalSpace (Eâ‚‚ x)]
  {EBâ‚ : Type*}
  [NormedAddCommGroup EBâ‚] [NormedSpace ğ•œ EBâ‚] {HBâ‚ : Type*} [TopologicalSpace HBâ‚]
  {IBâ‚ : ModelWithCorners ğ•œ EBâ‚ HBâ‚} [TopologicalSpace Bâ‚] [ChartedSpace HBâ‚ Bâ‚]
  {EBâ‚‚ : Type*}
  [NormedAddCommGroup EBâ‚‚] [NormedSpace ğ•œ EBâ‚‚] {HBâ‚‚ : Type*} [TopologicalSpace HBâ‚‚]
  {IBâ‚‚ : ModelWithCorners ğ•œ EBâ‚‚ HBâ‚‚} [TopologicalSpace Bâ‚‚] [ChartedSpace HBâ‚‚ Bâ‚‚]
  {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  {n : â„•âˆ} [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  {bâ‚ : M â†’ Bâ‚} {bâ‚‚ : M â†’ Bâ‚‚} {mâ‚€ : M}
  {Ï• : Î  (m : M), Eâ‚ (bâ‚ m) â†’L[ğ•œ] Eâ‚‚ (bâ‚‚ m)} {v : Î  (m : M), Eâ‚ (bâ‚ m)} {s : Set M}

/-- Consider a differentiable map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending
differentiably on `m`, one can apply `Ï• m` to `g m`, and the resulting map is differentiable.

Note that the differentiability of `Ï•` cannot be always be stated as differentiability of a map
into a manifold, as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚`
and `bâ‚‚` are globally smooth, but we want to apply this lemma with only local information.
Therefore, we formulate it using differentiability of `Ï•` read in coordinates.

Version for `MDifferentiableWithinAt`. We also give a version for `MDifferentiableAt`, but no
version for `MDifferentiableOn` or `MDifferentiable` as our assumption, written in coordinates,
only makes sense around a point.
-/
lemma MDifferentiableWithinAt.clm_apply_of_inCoordinates
    (hÏ• : MDifferentiableWithinAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) s mâ‚€)
    (hv : MDifferentiableWithinAt IM (IBâ‚.prod ğ“˜(ğ•œ, Fâ‚)) (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) s mâ‚€)
    (hbâ‚‚ : MDifferentiableWithinAt IM IBâ‚‚ bâ‚‚ s mâ‚€) :
    MDifferentiableWithinAt IM (IBâ‚‚.prod ğ“˜(ğ•œ, Fâ‚‚))
      (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) s mâ‚€ := by
  rw [mdifferentiableWithinAt_totalSpace] at hv âŠ¢
  refine âŸ¨hbâ‚‚, ?_âŸ©
  apply (MDifferentiableWithinAt.clm_apply hÏ• hv.2).congr_of_eventuallyEq_insert
  have A : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚ m âˆˆ (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).baseSet := by
    apply hv.1.insert.continuousWithinAt
    apply (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚ mâ‚€)
  have A' : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚‚ m âˆˆ (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).baseSet := by
    apply hbâ‚‚.insert.continuousWithinAt
    apply (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚‚ mâ‚€)
  filter_upwards [A, A'] with m hm h'm
  rw [inCoordinates_eq hm h'm]
  simp only [coe_comp', ContinuousLinearEquiv.coe_coe, Trivialization.continuousLinearEquivAt_apply,
    Trivialization.continuousLinearEquivAt_symm_apply, Function.comp_apply]
  congr
  rw [Trivialization.symm_apply_apply_mk (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)) hm (v m)]

/-- Consider a differentiable map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending
differentiably on `m`, one can apply `Ï• m` to `g m`, and the resulting map is differentiable.

Note that the differentiability of `Ï•` cannot be always be stated as differentiability of a map
into a manifold, as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚`
and `bâ‚‚` are globally smooth, but we want to apply this lemma with only local information.
Therefore, we formulate it using differentiability of `Ï•` read in coordinates.

Version for `MDifferentiableAt`. We also give a version for `MDifferentiableWithinAt`,
but no version for `MDifferentiableOn` or `MDifferentiable` as our assumption, written
in coordinates, only makes sense around a point.
-/
lemma MDifferentiableAt.clm_apply_of_inCoordinates
    (hÏ• : MDifferentiableAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) mâ‚€)
    (hv : MDifferentiableAt IM (IBâ‚.prod ğ“˜(ğ•œ, Fâ‚)) (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) mâ‚€)
    (hbâ‚‚ : MDifferentiableAt IM IBâ‚‚ bâ‚‚ mâ‚€) :
    MDifferentiableAt IM (IBâ‚‚.prod ğ“˜(ğ•œ, Fâ‚‚)) (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) mâ‚€ := by
  rw [â† mdifferentiableWithinAt_univ] at hÏ• hv hbâ‚‚ âŠ¢
  exact MDifferentiableWithinAt.clm_apply_of_inCoordinates hÏ• hv hbâ‚‚

end
