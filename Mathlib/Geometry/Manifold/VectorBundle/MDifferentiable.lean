/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Geometry.Manifold.Algebra.Monoid
import Mathlib.Geometry.Manifold.MFDeriv.NormedSpace
import Mathlib.Geometry.Manifold.MFDeriv.SpecificFunctions


/-!
# Differentiability of functions in vector bundles

-/

open Bundle Set PartialHomeomorph ContinuousLinearMap Pretrivialization Filter

open scoped Manifold Bundle Topology

section


variable {ğ•œ B B' F M : Type*} {E : B â†’ Type*}

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [TopologicalSpace (TotalSpace F E)] [âˆ€ x, TopologicalSpace (E x)] {EB : Type*}
  [NormedAddCommGroup EB] [NormedSpace ğ•œ EB] {HB : Type*} [TopologicalSpace HB]
  (IB : ModelWithCorners ğ•œ EB HB) (E' : B â†’ Type*) [âˆ€ x, Zero (E' x)] {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  {n : â„•âˆ}

variable [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]


/-- Characterization of differentiable functions into a vector bundle. -/
theorem mdifferentiableWithinAt_totalSpace (f : M â†’ TotalSpace F E) {s : Set M} {xâ‚€ : M} :
    MDifferentiableWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) f s xâ‚€ â†”
      MDifferentiableWithinAt IM IB (fun x => (f x).proj) s xâ‚€ âˆ§
      MDifferentiableWithinAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (trivializationAt F E (f xâ‚€).proj (f x)).2) s xâ‚€ := by
  simp +singlePass only [mdifferentiableWithinAt_iff_target]
  rw [and_and_and_comm, â† FiberBundle.continuousWithinAt_totalSpace, and_congr_right_iff]
  intro hf
  simp_rw [modelWithCornersSelf_prod, FiberBundle.extChartAt, Function.comp_def,
    PartialEquiv.trans_apply, PartialEquiv.prod_coe, PartialEquiv.refl_coe,
    extChartAt_self_apply, modelWithCornersSelf_coe, Function.id_def, â† chartedSpaceSelf_prod]
  refine (mdifferentiableWithinAt_prod_iff _).trans (and_congr ?_ Iff.rfl)
  have h1 : (fun x => (f x).proj) â»Â¹' (trivializationAt F E (f xâ‚€).proj).baseSet âˆˆ ğ“[s] xâ‚€ :=
    ((FiberBundle.continuous_proj F E).continuousWithinAt.comp hf (mapsTo_image f s))
      ((Trivialization.open_baseSet _).mem_nhds (mem_baseSet_trivializationAt F E _))
  refine EventuallyEq.mdifferentiableWithinAt_iff (eventually_of_mem h1 fun x hx => ?_) ?_
  Â· simp_rw [Function.comp, PartialHomeomorph.coe_coe, Trivialization.coe_coe]
    rw [Trivialization.coe_fst']
    exact hx
  Â· simp only [mfld_simps]

theorem mdifferentiableAt_totalSpace (f : M â†’ TotalSpace F E) {xâ‚€ : M} :
    MDifferentiableAt IM (IB.prod ğ“˜(ğ•œ, F)) f xâ‚€ â†”
      MDifferentiableAt IM IB (fun x => (f x).proj) xâ‚€ âˆ§
      MDifferentiableAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (trivializationAt F E (f xâ‚€).proj (f x)).2) xâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using mdifferentiableWithinAt_totalSpace _ f

/-- Characterization of differentiable sections of a vector bundle at a point within a set
in term of the preferred trivialization at that point. -/
theorem mdifferentiableWithinAt_section (s : Î  b, E b) {u : Set B} {bâ‚€ : B} :
    MDifferentiableWithinAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) u bâ‚€ â†”
      MDifferentiableWithinAt IB ğ“˜(ğ•œ, F) (fun b â†¦ (trivializationAt F E bâ‚€ (s b)).2) u bâ‚€ := by
  rw [mdifferentiableWithinAt_totalSpace]
  change MDifferentiableWithinAt _ _ id _ _ âˆ§ _ â†” _
  simp [mdifferentiableWithinAt_id]

/-- Characterization of differentiable sections of a vector bundle at a point within a set
in term of the preferred trivialization at that point. -/
theorem mdifferentiableAt_section (s : Î  b, E b) {bâ‚€ : B} :
    MDifferentiableAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) bâ‚€ â†”
      MDifferentiableAt IB ğ“˜(ğ•œ, F) (fun b â†¦ (trivializationAt F E bâ‚€ (s b)).2) bâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using mdifferentiableWithinAt_section _ _

variable [(x : B) â†’ AddCommMonoid (E x)] [(x : B) â†’ Module ğ•œ (E x)]
         [VectorBundle ğ•œ F E] [ContMDiffVectorBundle 1 F E IB]

lemma MDifferentiableWithinAt.coordChange
    {e : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e]
    (e' : Trivialization F TotalSpace.proj)  [MemTrivializationAtlas e']
    {f : M â†’ TotalSpace F E} {s : Set M} {xâ‚€ : M}
    (hexâ‚€ : (f xâ‚€).proj âˆˆ e.baseSet) (he'xâ‚€ : (f xâ‚€).proj âˆˆ e'.baseSet)
    (hf : MDifferentiableWithinAt IM IB (fun x â†¦ (f x).proj) s xâ‚€)
    (he'f : MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e' (f x)).2) s xâ‚€) :
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) s xâ‚€ := by
  have : âˆ€á¶  x in ğ“[s] xâ‚€, (e (f x)).2 = e'.coordChangeL ğ•œ e (f x).proj (e' (f x)).2 := by
    have mem : âˆ€á¶  x in ğ“[s] xâ‚€, (f x).proj âˆˆ e'.baseSet âˆ© e.baseSet := by
      exact  hf.continuousWithinAt <|
        (e'.open_baseSet.eventually_mem he'xâ‚€).and (e.open_baseSet.eventually_mem hexâ‚€)
    filter_upwards [mem] with x hx
    rw [e'.coordChangeL_apply e hx, e'.symm_proj_apply (f x) hx.1]
  apply Filter.EventuallyEq.mdifferentiableWithinAt_iff this ?_ |>.1
  Â· let c := Trivialization.coordChangeL ğ•œ e' e
    have bar : MDifferentiableWithinAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F)
        (fun x : M â†¦ (c (f x).proj : F â†’L[ğ•œ] F)) s xâ‚€ := by
      exact contMDiffAt_coordChangeL he'xâ‚€ hexâ‚€ |>.mdifferentiableAt le_rfl
        |>.comp_mdifferentiableWithinAt xâ‚€ hf
    exact bar.clm_apply he'f
  rw [e'.coordChangeL_apply e âŸ¨he'xâ‚€, hexâ‚€âŸ©, e'.symm_proj_apply (f xâ‚€) he'xâ‚€]

theorem mdifferentiableWithinAt_coordChange
    {e e' : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e] [MemTrivializationAtlas e']
    {f : M â†’ TotalSpace F E} {s : Set M} {xâ‚€ : M}
    (hexâ‚€ : (f xâ‚€).proj âˆˆ e.baseSet) (he'xâ‚€ : (f xâ‚€).proj âˆˆ e'.baseSet)
    (hf : MDifferentiableWithinAt IM IB (fun x â†¦ (f x).proj) s xâ‚€) :
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) s xâ‚€ â†”
    MDifferentiableWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e' (f x)).2) s xâ‚€ :=
  âŸ¨hf.coordChange IB e he'xâ‚€ hexâ‚€, hf.coordChange IB e' hexâ‚€ he'xâ‚€âŸ©

theorem mdifferentiableAt_change_triv
    {e e' : Trivialization F TotalSpace.proj} [MemTrivializationAtlas e] [MemTrivializationAtlas e']
    {f : M â†’ TotalSpace F E} {xâ‚€ : M}
    (hexâ‚€ : (f xâ‚€).proj âˆˆ e.baseSet) (he'xâ‚€ : (f xâ‚€).proj âˆˆ e'.baseSet)
    (hf : MDifferentiableAt IM IB (fun x â†¦ (f x).proj) xâ‚€) :
    MDifferentiableAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) xâ‚€ â†”
    MDifferentiableAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e' (f x)).2) xâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using mdifferentiableWithinAt_coordChange IB hexâ‚€ he'xâ‚€ hf

/-- Characterization of differentiable functions into a vector bundle in terms
of any trivialization. Version at a point within at set. -/
theorem Trivialization.mdifferentiableWithinAt_totalSpace_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (f : M â†’ TotalSpace F E) {s : Set M} {xâ‚€ : M}
    (hexâ‚€ : (f xâ‚€).proj âˆˆ e.baseSet) :
    MDifferentiableWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) f s xâ‚€ â†”
      MDifferentiableWithinAt IM IB (fun x => (f x).proj) s xâ‚€ âˆ§
      MDifferentiableWithinAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (e (f x)).2) s xâ‚€ := by
  rw [mdifferentiableWithinAt_totalSpace]
  apply and_congr_right
  intro hf
  rw [mdifferentiableWithinAt_coordChange IB hexâ‚€ (FiberBundle.mem_baseSet_trivializationAt' _) hf]

/-- Characterization of differentiable functions into a vector bundle in terms
of any trivialization. Version at a point. -/
theorem Trivialization.mdifferentiableAt_totalSpace_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (f : M â†’ TotalSpace F E) {xâ‚€ : M}
    (hexâ‚€ : (f xâ‚€).proj âˆˆ e.baseSet) :
    MDifferentiableAt IM (IB.prod ğ“˜(ğ•œ, F)) f xâ‚€ â†”
      MDifferentiableAt IM IB (fun x => (f x).proj) xâ‚€ âˆ§
      MDifferentiableAt IM ğ“˜(ğ•œ, F)
        (fun x â†¦ (e (f x)).2) xâ‚€ := by
  rw [mdifferentiableAt_totalSpace]
  apply and_congr_right
  intro hf
  rw [mdifferentiableAt_change_triv IB hexâ‚€ (FiberBundle.mem_baseSet_trivializationAt' _) hf]

/-- Characterization of differentiable sections a vector bundle in terms
of any trivialization. Version at a point within at set. -/
theorem Trivialization.mdifferentiableWithinAt_section_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (s : Î  b : B, E b) {u : Set B} {bâ‚€ : B}
    (hexâ‚€ : bâ‚€ âˆˆ e.baseSet) :
    MDifferentiableWithinAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) u bâ‚€ â†”
      MDifferentiableWithinAt IB ğ“˜(ğ•œ, F) (fun x â†¦ (e (s x)).2) u bâ‚€ := by
  rw [e.mdifferentiableWithinAt_totalSpace_iff IB]
  Â· change MDifferentiableWithinAt IB IB id u bâ‚€ âˆ§ _ â†” _
    simp [mdifferentiableWithinAt_id]
  simp [hexâ‚€]

/-- Characterization of differentiable functions into a vector bundle in terms
of any trivialization. Version at a point. -/
theorem Trivialization.mdifferentiableAt_section_iff
    (e : Trivialization F (TotalSpace.proj : TotalSpace F E â†’ B)) [MemTrivializationAtlas e]
    (s : Î  b : B, E b) {bâ‚€ : B}
    (hexâ‚€ : bâ‚€ âˆˆ e.baseSet) :
    MDifferentiableAt IB (IB.prod ğ“˜(ğ•œ, F)) (fun b â†¦ TotalSpace.mk' F b (s b)) bâ‚€ â†”
      MDifferentiableAt IB ğ“˜(ğ•œ, F) (fun x â†¦ (e (s x)).2) bâ‚€ := by
  simpa [â† mdifferentiableWithinAt_univ] using e.mdifferentiableWithinAt_section_iff IB s hexâ‚€
end

section contMDiff_addsmulfinsum_section

variable {ğ•œ B B' F M : Type*} {E : B â†’ Type*}

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [TopologicalSpace (TotalSpace F E)] [âˆ€ x, TopologicalSpace (E x)] {EB : Type*}
  [NormedAddCommGroup EB] [NormedSpace ğ•œ EB] {HB : Type*} [TopologicalSpace HB]
  (I : ModelWithCorners ğ•œ EB HB) -- (E' : B â†’ Type*) [âˆ€ x, Zero (E' x)] {EM : Type*}
  -- [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  -- {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  -- {n : â„•âˆ}

variable [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]

variable [(x : B) â†’ AddCommMonoid (E x)] [(x : B) â†’ Module ğ•œ (E x)]
         [VectorBundle ğ•œ F E]

-- Proofs taken from SmoothSection: TODO golf those with these lemmas!
-- XXX: also add sub, neg, nsmul, zsmul lemmas?

variable {I V}

variable {f : B â†’ ğ•œ} {a : ğ•œ} {s t : Î  x : B, E x} {u : Set B} {xâ‚€ : B}

omit [ContMDiffVectorBundle 1 F E I] in
lemma mdifferentiableWithinAt_add_section
    (hs : MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€)
    (ht : MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t x)) u xâ‚€) :
    MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) u xâ‚€ := by
  rw [mdifferentiableWithinAt_section] at hs ht âŠ¢
  set e := trivializationAt F E xâ‚€

  refine (hs.add ht).congr_of_eventuallyEq ?_ ?_
  Â· apply eventually_of_mem (U := e.baseSet)
    Â· exact mem_nhdsWithin_of_mem_nhds <|
        (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F E xâ‚€)
    Â· intro x hx
      apply (e.linear ğ•œ hx).1
  Â· apply (e.linear ğ•œ (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)).1

omit [ContMDiffVectorBundle 1 F E I] in
lemma mdifferentiableAt_add_section
    (hs : MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€)
    (ht : MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t x)) xâ‚€) :
    MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) xâ‚€ := by
  rw [mdifferentiableAt_section] at hs ht âŠ¢
  set e := trivializationAt F E xâ‚€
  refine (hs.add ht).congr_of_eventuallyEq ?_
  refine eventually_of_mem (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F E xâ‚€) ?_
  intro x hx
  apply (e.linear ğ•œ hx).1

lemma mdifferentiableOn_add_section
    (hs : MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) u)
    (ht : MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t x)) u) :
    MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) u :=
  fun xâ‚€ hxâ‚€ â†¦ mdifferentiableWithinAt_add_section (hs xâ‚€ hxâ‚€) (ht xâ‚€ hxâ‚€)

lemma mdifferentiable_add_section
    (hs : MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)))
    (ht : MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t x))) :
    MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x ((s + t) x)) :=
  fun xâ‚€ â†¦ mdifferentiableAt_add_section (hs xâ‚€) (ht xâ‚€)

lemma mdifferentiableWithinAt_smul_section
    (hs : MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€)
    (hf : MDifferentiableWithinAt I ğ“˜(ğ•œ) f u xâ‚€) :
    MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) u xâ‚€ := by
  rw [mdifferentiableWithinAt_section] at hs âŠ¢
  set e := trivializationAt F E xâ‚€
  refine (hf.smul hs).congr_of_eventuallyEq ?_ ?_
  Â· apply eventually_of_mem (U := e.baseSet)
    Â· exact mem_nhdsWithin_of_mem_nhds <|
        (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F E xâ‚€)
    Â· intro x hx
      apply (e.linear ğ•œ hx).2
  Â· apply (e.linear ğ•œ (FiberBundle.mem_baseSet_trivializationAt' xâ‚€)).2

lemma mdifferentiableAt_smul_section
    (hs : MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€)
    (hf : MDifferentiableAt I ğ“˜(ğ•œ) f xâ‚€) :
    MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) xâ‚€ := by
  rw [mdifferentiableAt_section] at hs âŠ¢
  set e := trivializationAt F E xâ‚€
  refine (hf.smul hs).congr_of_eventuallyEq ?_
  refine eventually_of_mem (e.open_baseSet.mem_nhds <| mem_baseSet_trivializationAt F E xâ‚€) ?_
  intro x hx
  apply (e.linear ğ•œ hx).2

lemma mdifferentiableOn_smul_section
    (hs : MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) u)
    (hf : MDifferentiableOn I ğ“˜(ğ•œ) f u) :
    MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) u :=
  fun xâ‚€ hxâ‚€ â†¦ mdifferentiableWithinAt_smul_section (hs xâ‚€ hxâ‚€) (hf xâ‚€ hxâ‚€)

lemma mdifferentiable_smul_section
    (hs : MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)))
    (hf : MDifferentiable I ğ“˜(ğ•œ) f) :
    MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (f x â€¢ s x)) :=
  fun xâ‚€ â†¦ mdifferentiableAt_smul_section (hs xâ‚€) (hf xâ‚€)

lemma mdifferentiableWithinAt_smul_const_section
    (hs : MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) u xâ‚€) :
    MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) u xâ‚€ :=
  mdifferentiableWithinAt_smul_section hs mdifferentiableWithinAt_const

lemma mdifferentiableAt_smul_const_section
    (hs : MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) xâ‚€) :
    MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) xâ‚€ :=
  mdifferentiableAt_smul_section hs mdifferentiableAt_const

lemma mdifferentiableOn_smul_const_section
    (hs : MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x)) u) :
    MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) u :=
  mdifferentiableOn_smul_section hs mdifferentiableOn_const

lemma mdifferentiable_smul_const_section
    (hs : MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (s x))) :
    MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (a â€¢ s x)) :=
  fun xâ‚€ â†¦ mdifferentiableAt_smul_const_section (hs xâ‚€)

lemma mdifferentiableWithinAt_finsum_section {Î¹ : Type*} {s : Finset Î¹} {t : Î¹ â†’ (x : B) â†’ E x}
    (hs : âˆ€ i, MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F))
                 (fun x â†¦ TotalSpace.mk' F x (t i x)) u xâ‚€) :
    MDifferentiableWithinAt I (I.prod ğ“˜(ğ•œ, F))
      (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) u xâ‚€ := by
  classical
  induction s using Finset.induction_on with
  | empty =>
    simp only [Finset.sum_empty]
    -- TODO: xâ‚€ âˆˆ u should not be required -> add mdifferentiableWithinAt_zeroSection!
    apply MDifferentiable.mdifferentiableOn
    Â· apply  ContMDiff.mdifferentiable _ le_rfl
      apply contMDiff_zeroSection
    Â· sorry -- xâ‚€ âˆˆ u...
  | insert i s hi h =>
    simpa [Finset.sum_insert hi] using mdifferentiableWithinAt_add_section (hs i) h

lemma mdifferentiableAt_finsum_section {Î¹ : Type*} {s : Finset Î¹} {t : Î¹ â†’ (x : B) â†’ E x} {xâ‚€ : B}
    (hs : âˆ€ i, MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t i x)) xâ‚€) :
    MDifferentiableAt I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) xâ‚€ := by
  classical
  induction s using Finset.induction_on with
  | empty =>
     apply ContMDiff.mdifferentiable _ le_rfl
     apply contMDiff_zeroSection
  | insert i s hi h =>
    simpa [Finset.sum_insert hi] using mdifferentiableWithinAt_add_section (hs i) h

lemma mdifferentiableOn_finsum_section {Î¹ : Type*} {s : Finset Î¹} {t : Î¹ â†’ (x : B) â†’ E x}
    (hs : âˆ€ i, MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t i x)) u) :
    MDifferentiableOn I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) u :=
  fun xâ‚€ hxâ‚€ â†¦ mdifferentiableWithinAt_finsum_section fun i â†¦ hs i xâ‚€ hxâ‚€

lemma mdifferentiable_finsum_section {Î¹ : Type*} {s : Finset Î¹} {t : Î¹ â†’ (x : B) â†’ E x}
    (hs : âˆ€ i, MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (t i x))) :
    MDifferentiable I (I.prod ğ“˜(ğ•œ, F)) (fun x â†¦ TotalSpace.mk' F x (âˆ‘ i âˆˆ s, (t i x))) :=
  fun xâ‚€ â†¦ mdifferentiableAt_finsum_section fun i â†¦ (hs i) xâ‚€

end contMDiff_addsmulfinsum_section

section

/- Declare two manifolds `Bâ‚` and `Bâ‚‚` (with models `IBâ‚ : HBâ‚ â†’ EBâ‚` and `IBâ‚‚ : HBâ‚‚ â†’ EBâ‚‚`),
and two vector bundles `Eâ‚` and `Eâ‚‚` respectively over `Bâ‚` and `Bâ‚‚` (with model fibers
`Fâ‚` and `Fâ‚‚`).

Also a third manifold `M`, which will be the source of all our maps.
-/
variable {ğ•œ Fâ‚ Fâ‚‚ Bâ‚ Bâ‚‚ M : Type*} {Eâ‚ : Bâ‚ â†’ Type*} {Eâ‚‚ : Bâ‚‚ â†’ Type*} [NontriviallyNormedField ğ•œ]
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, TopologicalSpace (Eâ‚ x)] [âˆ€ x, AddCommGroup (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, TopologicalSpace (Eâ‚‚ x)]
  {EBâ‚ : Type*}
  [NormedAddCommGroup EBâ‚] [NormedSpace ğ•œ EBâ‚] {HBâ‚ : Type*} [TopologicalSpace HBâ‚]
  {IBâ‚ : ModelWithCorners ğ•œ EBâ‚ HBâ‚} [TopologicalSpace Bâ‚] [ChartedSpace HBâ‚ Bâ‚]
  {EBâ‚‚ : Type*}
  [NormedAddCommGroup EBâ‚‚] [NormedSpace ğ•œ EBâ‚‚] {HBâ‚‚ : Type*} [TopologicalSpace HBâ‚‚]
  {IBâ‚‚ : ModelWithCorners ğ•œ EBâ‚‚ HBâ‚‚} [TopologicalSpace Bâ‚‚] [ChartedSpace HBâ‚‚ Bâ‚‚]
  {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  {n : â„•âˆ} [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  {bâ‚ : M â†’ Bâ‚} {bâ‚‚ : M â†’ Bâ‚‚} {mâ‚€ : M}
  {Ï• : Î  (m : M), Eâ‚ (bâ‚ m) â†’L[ğ•œ] Eâ‚‚ (bâ‚‚ m)} {v : Î  (m : M), Eâ‚ (bâ‚ m)} {s : Set M}

/-- Consider a differentiable map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending
differentiably on `m`, one can apply `Ï• m` to `g m`, and the resulting map is differentiable.

Note that the differentiability of `Ï•` can not be always be stated as differentiability of a map
into a manifold, as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚`
and `bâ‚‚` are globally smooth, but we want to apply this lemma with only local information.
Therefore, we formulate it using differentiability of `Ï•` read in coordinates.

Version for `MDifferentiableWithinAt`. We also give a version for `MDifferentiableAt`, but no
version for `MDifferentiableOn` or `MDifferentiable` as our assumption, written in coordinates,
only makes sense around a point.
-/
lemma MDifferentiableWithinAt.clm_apply_of_inCoordinates
    (hÏ• : MDifferentiableWithinAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) s mâ‚€)
    (hv : MDifferentiableWithinAt IM (IBâ‚.prod ğ“˜(ğ•œ, Fâ‚)) (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) s mâ‚€)
    (hbâ‚‚ : MDifferentiableWithinAt IM IBâ‚‚ bâ‚‚ s mâ‚€) :
    MDifferentiableWithinAt IM (IBâ‚‚.prod ğ“˜(ğ•œ, Fâ‚‚))
      (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) s mâ‚€ := by
  rw [mdifferentiableWithinAt_totalSpace] at hv âŠ¢
  refine âŸ¨hbâ‚‚, ?_âŸ©
  apply (MDifferentiableWithinAt.clm_apply hÏ• hv.2).congr_of_eventuallyEq_insert
  have A : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚ m âˆˆ (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).baseSet := by
    apply hv.1.insert.continuousWithinAt
    apply (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚ mâ‚€)
  have A' : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚‚ m âˆˆ (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).baseSet := by
    apply hbâ‚‚.insert.continuousWithinAt
    apply (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚‚ mâ‚€)
  filter_upwards [A, A'] with m hm h'm
  rw [inCoordinates_eq hm h'm]
  simp only [coe_comp', ContinuousLinearEquiv.coe_coe, Trivialization.continuousLinearEquivAt_apply,
    Trivialization.continuousLinearEquivAt_symm_apply, Function.comp_apply]
  congr
  rw [Trivialization.symm_apply_apply_mk (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)) hm (v m)]

/-- Consider a differentiable map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending
differentiably on `m`, one can apply `Ï• m` to `g m`, and the resulting map is differentiable.

Note that the differentiability of `Ï•` can not be always be stated as differentiability of a map
into a manifold, as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚`
and `bâ‚‚` are globally smooth, but we want to apply this lemma with only local information.
Therefore, we formulate it using differentiability of `Ï•` read in coordinates.

Version for `MDifferentiableAt`. We also give a version for `MDifferentiableWithinAt`,
but no version for `MDifferentiableOn` or `MDifferentiable` as our assumption, written
in coordinates, only makes sense around a point.
-/
lemma MDifferentiableAt.clm_apply_of_inCoordinates
    (hÏ• : MDifferentiableAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) mâ‚€)
    (hv : MDifferentiableAt IM (IBâ‚.prod ğ“˜(ğ•œ, Fâ‚)) (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) mâ‚€)
    (hbâ‚‚ : MDifferentiableAt IM IBâ‚‚ bâ‚‚ mâ‚€) :
    MDifferentiableAt IM (IBâ‚‚.prod ğ“˜(ğ•œ, Fâ‚‚)) (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) mâ‚€ := by
  rw [â† mdifferentiableWithinAt_univ] at hÏ• hv hbâ‚‚ âŠ¢
  exact MDifferentiableWithinAt.clm_apply_of_inCoordinates hÏ• hv hbâ‚‚

end
