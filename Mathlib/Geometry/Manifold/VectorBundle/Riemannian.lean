/-
Copyright (c) 2025 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Geometry.Manifold.VectorBundle.Hom
import Mathlib.Geometry.Manifold.VectorBundle.MDifferentiable
import Mathlib.Topology.VectorBundle.Riemannian

/-! # Riemannian vector bundles

Given a vector bundle over a manifold whose fibers are all endowed with a scalar product, we
say that this bundle is Riemannian if the scalar product depends smoothly on the base point.

We introduce a typeclass `[IsContMDiffRiemannianBundle IB n F E]` registering this property.
Under this assumption, we show that the scalar product of two smooth maps into the same fibers of
the bundle is a smooth function.

If the fibers of a bundle `E` have a preexisting topology (like the tangent bundle), one can not
assume additionally `[âˆ€ b, InnerProductSpace â„ (E b)]` as this would create diamonds. Instead,
use `[RiemannianBundle E]`, which endows the fibers with a scalar product while ensuring that
there is no diamond (for this, the `Bundle` scope should be open). We provide a
constructor for `[RiemannianBundle E]` from a smooth family of metrics, which registers
automatically `[IsContMDiffRiemannianBundle IB n F E]`.

The following code block is the standard way to say "Let `E` be a smooth vector bundle equipped with
a `C^n` Riemannian structure over a `C^n` manifold `B`":
```
variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace â„ EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners â„ EB HB} {n : WithTop â„•âˆ}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  {E : B â†’ Type*} [TopologicalSpace (TotalSpace F E)] [âˆ€ x, NormedAddCommGroup (E x)]
  [âˆ€ x, InnerProductSpace â„ (E x)] [FiberBundle F E] [VectorBundle â„ F E]
  [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
  [IsContMDiffRiemannianBundle IB n F E]
```
-/

open Manifold Bundle ContinuousLinearMap ENat Bornology
open scoped ContDiff Topology

section

variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace â„ EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners â„ EB HB} {n n' : WithTop â„•âˆ}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  {E : B â†’ Type*} [TopologicalSpace (TotalSpace F E)] [âˆ€ x, NormedAddCommGroup (E x)]
  [âˆ€ x, InnerProductSpace â„ (E x)]
  [FiberBundle F E] [VectorBundle â„ F E]

local notation "âŸª" x ", " y "âŸ«" => inner â„ x y

variable (IB n F E) in
/-- Consider a real vector bundle in which each fiber is endowed with a scalar product.
We that the bundle is Riemannian if the scalar product depends smoothly on the base point.
This assumption is spelled `IsContMDiffRiemannianBundle IB n F E` where `IB` is the model space of
the base, `n` is the smoothness, `F` is the model fiber, and `E : B â†’ Type*` is the bundle. -/
class IsContMDiffRiemannianBundle : Prop where
  exists_contMDiff : âˆƒ g : Î  (x : B), E x â†’L[â„] E x â†’L[â„] â„,
    ContMDiff IB (IB.prod ğ“˜(â„, F â†’L[â„] F â†’L[â„] â„)) n
      (fun b â†¦ TotalSpace.mk' (F â†’L[â„] F â†’L[â„] â„) b (g b))
    âˆ§ âˆ€ (x : B) (v w : E x), âŸªv, wâŸ« = g x v w

lemma IsContMDiffRiemannianBundle.of_le [h : IsContMDiffRiemannianBundle IB n F E] (h' : n' â‰¤ n) :
    IsContMDiffRiemannianBundle IB n' F E := by
  rcases h.exists_contMDiff with âŸ¨g, g_smooth, hgâŸ©
  exact âŸ¨g, g_smooth.of_le h', hgâŸ©

instance {a : WithTop â„•âˆ} [IsContMDiffRiemannianBundle IB âˆ F E] [h : LEInfty a] :
    IsContMDiffRiemannianBundle IB a F E :=
  IsContMDiffRiemannianBundle.of_le h.out

instance {a : WithTop â„•âˆ} [IsContMDiffRiemannianBundle IB Ï‰ F E] :
    IsContMDiffRiemannianBundle IB a F E :=
  IsContMDiffRiemannianBundle.of_le le_top

instance [IsContMDiffRiemannianBundle IB 1 F E] : IsContMDiffRiemannianBundle IB 0 F E :=
  IsContMDiffRiemannianBundle.of_le zero_le_one

instance [IsContMDiffRiemannianBundle IB 2 F E] : IsContMDiffRiemannianBundle IB 1 F E :=
  IsContMDiffRiemannianBundle.of_le one_le_two

instance [IsContMDiffRiemannianBundle IB 3 F E] : IsContMDiffRiemannianBundle IB 2 F E :=
  IsContMDiffRiemannianBundle.of_le (n := 3) (by norm_cast)

section Trivial

variable {Fâ‚ : Type*} [NormedAddCommGroup Fâ‚] [InnerProductSpace â„ Fâ‚]

/-- A trivial vector bundle, in which the model fiber has a scalar product,
is a Riemannian bundle. -/
instance : IsContMDiffRiemannianBundle IB n Fâ‚ (Bundle.Trivial B Fâ‚) := by
  refine âŸ¨fun x â†¦ innerSL â„, fun x â†¦ ?_, fun x v w â†¦ rflâŸ©
  simp only [contMDiffAt_section]
  convert contMDiffAt_const (c := innerSL â„)
  ext v w
  simp [hom_trivializationAt_apply, inCoordinates, Trivialization.linearMapAt_apply]

end Trivial

section ContMDiff

variable
  {EM : Type*} [NormedAddCommGroup EM] [NormedSpace â„ EM]
  {HM : Type*} [TopologicalSpace HM] {IM : ModelWithCorners â„ EM HM}
  {M : Type*} [TopologicalSpace M] [ChartedSpace HM M]
  [h : IsContMDiffRiemannianBundle IB n F E]
  {b : M â†’ B} {v w : âˆ€ x, E (b x)} {s : Set M} {x : M}

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiffWithinAt.inner_bundle
    (hv : ContMDiffWithinAt IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (v m : TotalSpace F E)) s x)
    (hw : ContMDiffWithinAt IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (w m : TotalSpace F E)) s x) :
    ContMDiffWithinAt IM ğ“˜(â„) n (fun m â†¦ âŸªv m, w mâŸ«) s x := by
  rcases h.exists_contMDiff with âŸ¨g, g_smooth, hgâŸ©
  have hb : ContMDiffWithinAt IM IB n b s x := by
    simp only [contMDiffWithinAt_totalSpace] at hv
    exact hv.1
  simp only [hg]
  have : ContMDiffWithinAt IM (IB.prod ğ“˜(â„)) n
      (fun m â†¦ TotalSpace.mk' â„ (E := Bundle.Trivial B â„) (b m) (g (b m) (v m) (w m))) s x := by
    apply ContMDiffWithinAt.clm_bundle_applyâ‚‚ (Fâ‚ := F) (Fâ‚‚ := F)
    Â· exact ContMDiffAt.comp_contMDiffWithinAt x g_smooth.contMDiffAt hb
    Â· exact hv
    Â· exact hw
  simp only [contMDiffWithinAt_totalSpace] at this
  exact this.2

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiffAt.inner_bundle
    (hv : ContMDiffAt IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (v m : TotalSpace F E)) x)
    (hw : ContMDiffAt IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (w m : TotalSpace F E)) x) :
    ContMDiffAt IM ğ“˜(â„) n (fun b â†¦ âŸªv b, w bâŸ«) x :=
  ContMDiffWithinAt.inner_bundle hv hw

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiffOn.inner_bundle
    (hv : ContMDiffOn IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (v m : TotalSpace F E)) s)
    (hw : ContMDiffOn IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (w m : TotalSpace F E)) s) :
    ContMDiffOn IM ğ“˜(â„) n (fun b â†¦ âŸªv b, w bâŸ«) s :=
  fun x hx â†¦ (hv x hx).inner_bundle (hw x hx)

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiff.inner_bundle
    (hv : ContMDiff IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (v m : TotalSpace F E)))
    (hw : ContMDiff IM (IB.prod ğ“˜(â„, F)) n (fun m â†¦ (w m : TotalSpace F E))) :
    ContMDiff IM ğ“˜(â„) n (fun b â†¦ âŸªv b, w bâŸ«) :=
  fun x â†¦ (hv x).inner_bundle (hw x)

end ContMDiff

section MDifferentiable

variable
  {EM : Type*} [NormedAddCommGroup EM] [NormedSpace â„ EM]
  {HM : Type*} [TopologicalSpace HM] {IM : ModelWithCorners â„ EM HM}
  {M : Type*} [TopologicalSpace M] [ChartedSpace HM M]
  [h : IsContMDiffRiemannianBundle IB 1 F E]
  {b : M â†’ B} {v w : âˆ€ x, E (b x)} {s : Set M} {x : M}

/-- Given two differentiable maps into the same fibers of a Riemannian bundle,
their scalar product is differentiable. -/
lemma MDifferentiableWithinAt.inner_bundle
    (hv : MDifferentiableWithinAt IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (v m : TotalSpace F E)) s x)
    (hw : MDifferentiableWithinAt IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (w m : TotalSpace F E)) s x) :
    MDifferentiableWithinAt IM ğ“˜(â„) (fun m â†¦ âŸªv m, w mâŸ«) s x := by
  rcases h.exists_contMDiff with âŸ¨g, g_smooth, hgâŸ©
  have hb : MDifferentiableWithinAt IM IB b s x := by
    simp only [mdifferentiableWithinAt_totalSpace] at hv
    exact hv.1
  simp only [hg]
  have : MDifferentiableWithinAt IM (IB.prod ğ“˜(â„))
      (fun m â†¦ TotalSpace.mk' â„ (E := Bundle.Trivial B â„) (b m) (g (b m) (v m) (w m))) s x := by
    apply MDifferentiableWithinAt.clm_bundle_applyâ‚‚ (Fâ‚ := F) (Fâ‚‚ := F)
    Â· exact MDifferentiableAt.comp_mdifferentiableWithinAt x (g_smooth.mdifferentiableAt le_rfl) hb
    Â· exact hv
    Â· exact hw
  simp only [mdifferentiableWithinAt_totalSpace] at this
  exact this.2

/-- Given two differentiable maps into the same fibers of a Riemannian bundle,
their scalar product is differentiable. -/
lemma MDifferentiableAt.inner_bundle
    (hv : MDifferentiableAt IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (v m : TotalSpace F E)) x)
    (hw : MDifferentiableAt IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (w m : TotalSpace F E)) x) :
    MDifferentiableAt IM ğ“˜(â„) (fun b â†¦ âŸªv b, w bâŸ«) x :=
  MDifferentiableWithinAt.inner_bundle hv hw

/-- Given two differentiable maps into the same fibers of a Riemannian bundle,
their scalar product is differentiable. -/
lemma MDifferentiableOn.inner_bundle
    (hv : MDifferentiableOn IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (v m : TotalSpace F E)) s)
    (hw : MDifferentiableOn IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (w m : TotalSpace F E)) s) :
    MDifferentiableOn IM ğ“˜(â„) (fun b â†¦ âŸªv b, w bâŸ«) s :=
  fun x hx â†¦ (hv x hx).inner_bundle (hw x hx)

/-- Given two differentiable maps into the same fibers of a Riemannian bundle,
their scalar product is differentiable. -/
lemma MDifferentiable.inner_bundle
    (hv : MDifferentiable IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (v m : TotalSpace F E)))
    (hw : MDifferentiable IM (IB.prod ğ“˜(â„, F)) (fun m â†¦ (w m : TotalSpace F E))) :
    MDifferentiable IM ğ“˜(â„) (fun b â†¦ âŸªv b, w bâŸ«) :=
  fun x â†¦ (hv x).inner_bundle (hw x)

end MDifferentiable

end

namespace Bundle

section Construction

variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace â„ EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners â„ EB HB} {n n' : WithTop â„•âˆ}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  {E : B â†’ Type*} [TopologicalSpace (TotalSpace F E)]
  [âˆ€ b, TopologicalSpace (E b)] [âˆ€ b, AddCommGroup (E b)] [âˆ€ b, Module â„ (E b)]
  [âˆ€ b, IsTopologicalAddGroup (E b)] [âˆ€ b, ContinuousConstSMul â„ (E b)]
  [FiberBundle F E] [VectorBundle â„ F E]

variable (IB n F E) in
/-- A family of inner product space structures on the fibers of a fiber bundle, defining the same
topology as the already existing one, and varying continuously with the base point. See also
`ContinuousRiemannianMetric` for a continuous version.

This structure is used through `RiemannianBundle` for typeclass inference, to register the inner
product space structure on the fibers without creating diamonds. -/
structure ContMDiffRiemannianMetric where
  /-- The scalar product along the fibers of the bundle. -/
  inner (b : B) : E b â†’L[â„] E b â†’L[â„] â„
  symm (b : B) (v w : E b) : inner b v w = inner b w v
  pos (b : B) (v : E b) (hv : v â‰  0) : 0 < inner b v v
  isVonNBounded (b : B) : IsVonNBounded â„ {v : E b | inner b v v < 1}
  contMDiff : ContMDiff IB (IB.prod ğ“˜(â„, F â†’L[â„] F â†’L[â„] â„)) n
    (fun b â†¦ TotalSpace.mk' (F â†’L[â„] F â†’L[â„] â„) b (inner b))

/-- A smooth Riemannian metric defines in particular a continuous Riemannian metric. -/
def ContMDiffRiemannianMetric.toContinuousRiemannianMetric
    (g : ContMDiffRiemannianMetric IB n F E) : ContinuousRiemannianMetric F E :=
  { g with continuous := g.contMDiff.continuous }

/-- A smooth Riemannian metric defines in particular a Riemannian metric. -/
def ContMDiffRiemannianMetric.toRiemannianMetric
    (g : ContMDiffRiemannianMetric IB n F E) : RiemannianMetric E :=
  g.toContinuousRiemannianMetric.toRiemannianMetric

instance (g : ContMDiffRiemannianMetric IB n F E) :
    letI : RiemannianBundle E := âŸ¨g.toRiemannianMetricâŸ©
    IsContMDiffRiemannianBundle IB n F E :=
  letI : RiemannianBundle E := âŸ¨g.toRiemannianMetricâŸ©
  âŸ¨g.inner, g.contMDiff, fun _ _ _ â†¦ rflâŸ©

end Construction

end Bundle
