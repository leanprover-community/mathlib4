/-
Copyright (c) 2025 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Geometry.Manifold.VectorBundle.Hom
import Mathlib.Topology.VectorBundle.Riemannian

/-! # Riemannian vector bundles

Given a vector bundle over a manifold whose fibers are all endowed with a scalar product, we
say that this bundle is Riemannian if the scalar product depends smoothly on the base point.

We introduce a typeclass `[IsContMDiffRiemannianBundle IB n F E]` registering this property.
Under this assumption, we show that the scalar product of two smooth maps into the same fibers of
the bundle is a smooth function.

If the fibers of a bundle `E` have a preexisting topology (like the tangent bundle), one can not
assume additionally `[‚àÄ b, InnerProductSpace ‚Ñù (E b)]` as this would create diamonds. Instead,
use `[RiemannianBundle E]`, which endows the fibers with a scalar product while ensuring that
there is no diamond. We provide a constructor for `[RiemannianBundle E]` from a smooth family
of metrics, which registers automatically `[IsContMDiffRiemannianBundle IB n F E]`.

The following code block is the standard way to say "Let `E` be a smooth vector bundle equipped with
a `C^n` Riemannian structure over a `C^n` manifold `B`":
```
variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ‚Ñù EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners ‚Ñù EB HB} {n : WithTop ‚Ñï‚àû}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  {E : B ‚Üí Type*} [TopologicalSpace (TotalSpace F E)] [‚àÄ x, NormedAddCommGroup (E x)]
  [‚àÄ x, InnerProductSpace ‚Ñù (E x)] [FiberBundle F E] [VectorBundle ‚Ñù F E]
  [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
  [IsContMDiffRiemannianBundle IB n F E]
```
-/

open Manifold Bundle ContinuousLinearMap ENat Bornology
open scoped ContDiff Topology

section

variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ‚Ñù EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners ‚Ñù EB HB} {n n' : WithTop ‚Ñï‚àû}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  {E : B ‚Üí Type*} [TopologicalSpace (TotalSpace F E)] [‚àÄ x, NormedAddCommGroup (E x)]
  [‚àÄ x, InnerProductSpace ‚Ñù (E x)]
  [FiberBundle F E] [VectorBundle ‚Ñù F E]

local notation "‚ü™" x ", " y "‚ü´" => inner ‚Ñù x y

variable (IB n F E) in
/-- Consider a real vector bundle in which each fiber is endowed with a scalar product.
We that the bundle is Riemannian if the scalar product depends smoothly on the base point.
This assumption is spelled `IsContMDiffRiemannianBundle IB n F E` where `IB` is the model space of
the base, `n` is the smoothness, `F` is the model fiber, and `E : B ‚Üí Type*` is the bundle. -/
class IsContMDiffRiemannianBundle : Prop where
  exists_contMDiff : ‚àÉ g : Œ† (x : B), E x ‚ÜíL[‚Ñù] E x ‚ÜíL[‚Ñù] ‚Ñù,
    ContMDiff IB (IB.prod ùìò(‚Ñù, F ‚ÜíL[‚Ñù] F ‚ÜíL[‚Ñù] ‚Ñù)) n
      (fun b ‚Ü¶ TotalSpace.mk' (F ‚ÜíL[‚Ñù] F ‚ÜíL[‚Ñù] ‚Ñù) b (g b))
    ‚àß ‚àÄ (x : B) (v w : E x), ‚ü™v, w‚ü´ = g x v w

lemma IsContMDiffRiemannianBundle.of_le [h : IsContMDiffRiemannianBundle IB n F E] (h' : n' ‚â§ n) :
    IsContMDiffRiemannianBundle IB n' F E := by
  rcases h.exists_contMDiff with ‚ü®g, g_smooth, hg‚ü©
  exact ‚ü®g, g_smooth.of_le h', hg‚ü©

instance {a : WithTop ‚Ñï‚àû} [IsContMDiffRiemannianBundle IB ‚àû F E] [h : LEInfty a] :
    IsContMDiffRiemannianBundle IB a F E :=
  IsContMDiffRiemannianBundle.of_le h.out

instance {a : WithTop ‚Ñï‚àû} [IsContMDiffRiemannianBundle IB œâ F E] :
    IsContMDiffRiemannianBundle IB a F E :=
  IsContMDiffRiemannianBundle.of_le le_top

instance [IsContMDiffRiemannianBundle IB 1 F E] : IsContMDiffRiemannianBundle IB 0 F E :=
  IsContMDiffRiemannianBundle.of_le zero_le_one

instance [IsContMDiffRiemannianBundle IB 2 F E] : IsContMDiffRiemannianBundle IB 1 F E :=
  IsContMDiffRiemannianBundle.of_le one_le_two

instance [IsContMDiffRiemannianBundle IB 3 F E] : IsContMDiffRiemannianBundle IB 2 F E :=
  IsContMDiffRiemannianBundle.of_le (n := 3) (by norm_cast)

section Trivial

variable {F‚ÇÅ : Type*} [NormedAddCommGroup F‚ÇÅ] [InnerProductSpace ‚Ñù F‚ÇÅ]

/-- A trivial vector bundle, in which the model fiber has a scalar product,
is a Riemannian bundle. -/
instance : IsContMDiffRiemannianBundle IB n F‚ÇÅ (Bundle.Trivial B F‚ÇÅ) := by
  refine ‚ü®fun x ‚Ü¶ innerSL ‚Ñù, fun x ‚Ü¶ ?_, fun x v w ‚Ü¶ rfl‚ü©
  simp only [contMDiffAt_section]
  convert contMDiffAt_const (c := innerSL ‚Ñù)
  ext v w
  simp [hom_trivializationAt_apply, inCoordinates, Trivialization.linearMapAt_apply]

end Trivial

section ContMDiff

variable
  {EM : Type*} [NormedAddCommGroup EM] [NormedSpace ‚Ñù EM]
  {HM : Type*} [TopologicalSpace HM] {IM : ModelWithCorners ‚Ñù EM HM}
  {M : Type*} [TopologicalSpace M] [ChartedSpace HM M]
  [h : IsContMDiffRiemannianBundle IB n F E]
  {b : M ‚Üí B} {v w : ‚àÄ x, E (b x)} {s : Set M} {x : M}

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiffWithinAt.inner_bundle
    (hv : ContMDiffWithinAt IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (v m : TotalSpace F E)) s x)
    (hw : ContMDiffWithinAt IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (w m : TotalSpace F E)) s x) :
    ContMDiffWithinAt IM ùìò(‚Ñù) n (fun m ‚Ü¶ ‚ü™v m, w m‚ü´) s x := by
  rcases h.exists_contMDiff with ‚ü®g, g_smooth, hg‚ü©
  have hb : ContMDiffWithinAt IM IB n b s x := by
    simp only [contMDiffWithinAt_totalSpace] at hv
    exact hv.1
  simp only [hg]
  have : ContMDiffWithinAt IM (IB.prod ùìò(‚Ñù)) n
      (fun m ‚Ü¶ TotalSpace.mk' ‚Ñù (E := Bundle.Trivial B ‚Ñù) (b m) (g (b m) (v m) (w m))) s x := by
    apply ContMDiffWithinAt.clm_bundle_apply‚ÇÇ (F‚ÇÅ := F) (F‚ÇÇ := F)
    ¬∑ exact ContMDiffAt.comp_contMDiffWithinAt x g_smooth.contMDiffAt hb
    ¬∑ exact hv
    ¬∑ exact hw
  simp only [contMDiffWithinAt_totalSpace] at this
  exact this.2

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiffAt.inner_bundle
    (hv : ContMDiffAt IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (v m : TotalSpace F E)) x)
    (hw : ContMDiffAt IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (w m : TotalSpace F E)) x) :
    ContMDiffAt IM ùìò(‚Ñù) n (fun b ‚Ü¶ ‚ü™v b, w b‚ü´) x :=
  ContMDiffWithinAt.inner_bundle hv hw

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiffOn.inner_bundle
    (hv : ContMDiffOn IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (v m : TotalSpace F E)) s)
    (hw : ContMDiffOn IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (w m : TotalSpace F E)) s) :
    ContMDiffOn IM ùìò(‚Ñù) n (fun b ‚Ü¶ ‚ü™v b, w b‚ü´) s :=
  fun x hx ‚Ü¶ (hv x hx).inner_bundle (hw x hx)

/-- Given two smooth maps into the same fibers of a Riemannian bundle,
their scalar product is smooth. -/
lemma ContMDiff.inner_bundle
    (hv : ContMDiff IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (v m : TotalSpace F E)))
    (hw : ContMDiff IM (IB.prod ùìò(‚Ñù, F)) n (fun m ‚Ü¶ (w m : TotalSpace F E))) :
    ContMDiff IM ùìò(‚Ñù) n (fun b ‚Ü¶ ‚ü™v b, w b‚ü´) :=
  fun x ‚Ü¶ (hv x).inner_bundle (hw x)

end ContMDiff

end

namespace Bundle

section Construction

variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ‚Ñù EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners ‚Ñù EB HB} {n n' : WithTop ‚Ñï‚àû}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  {E : B ‚Üí Type*} [TopologicalSpace (TotalSpace F E)]
  [‚àÄ b, TopologicalSpace (E b)] [‚àÄ b, AddCommGroup (E b)] [‚àÄ b, Module ‚Ñù (E b)]
  [‚àÄ b, IsTopologicalAddGroup (E b)] [‚àÄ b, ContinuousConstSMul ‚Ñù (E b)]
  [FiberBundle F E] [VectorBundle ‚Ñù F E]

variable (IB n F E) in
/-- A family of inner product space structures on the fibers of a fiber bundle, defining the same
topology as the already existing one, and varying continuously with the base point. See also
`ContinuousRiemannianMetric` for a continuous version.

This structure is used through `RiemannianBundle` for typeclass inference, to register the inner
product space structure on the fibers without creating diamonds. -/
structure ContMDiffRiemannianMetric where
  /-- The scalar product along the fibers of the bundle. -/
  inner (b : B) : E b ‚ÜíL[‚Ñù] E b ‚ÜíL[‚Ñù] ‚Ñù
  symm (b : B) (v w : E b) : inner b v w = inner b w v
  pos (b : B) (v : E b) (hv : v ‚â† 0) : 0 < inner b v v
  isVonNBounded (b : B) : IsVonNBounded ‚Ñù {v : E b | inner b v v < 1}
  contMDiff : ContMDiff IB (IB.prod ùìò(‚Ñù, F ‚ÜíL[‚Ñù] F ‚ÜíL[‚Ñù] ‚Ñù)) n
    (fun b ‚Ü¶ TotalSpace.mk' (F ‚ÜíL[‚Ñù] F ‚ÜíL[‚Ñù] ‚Ñù) b (inner b))

/-- A smooth Riemannian metric defines in particular a continuous Riemannian metric. -/
def ContMDiffRiemannianMetric.toContinuousRiemannianMetric
    (g : ContMDiffRiemannianMetric IB n F E) : ContinuousRiemannianMetric F E :=
  { g with continuous := g.contMDiff.continuous }

/-- A smooth Riemannian metric defines in particular a Riemannian metric. -/
def ContMDiffRiemannianMetric.toRiemannianMetric
    (g : ContMDiffRiemannianMetric IB n F E) : RiemannianMetric E :=
  g.toContinuousRiemannianMetric.toRiemannianMetric

instance (g : ContMDiffRiemannianMetric IB n F E) :
    letI : RiemannianBundle E := ‚ü®g.toRiemannianMetric‚ü©
    IsContMDiffRiemannianBundle IB n F E :=
  letI : RiemannianBundle E := ‚ü®g.toRiemannianMetric‚ü©
  ‚ü®g.inner, g.contMDiff, fun _ _ _ ‚Ü¶ rfl‚ü©

end Construction

end Bundle
