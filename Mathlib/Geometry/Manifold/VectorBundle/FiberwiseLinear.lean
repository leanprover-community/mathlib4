/-
Copyright (c) 2022 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Geometry.Manifold.ContMDiff.NormedSpace

/-! # The groupoid of `C^n`, fiberwise-linear maps

This file contains preliminaries for the definition of a `C^n` vector bundle: an associated
`StructureGroupoid`, the groupoid of `contMDiffFiberwiseLinear` functions.
-/

noncomputable section

open Set TopologicalSpace

open scoped Manifold Topology

/-! ### The groupoid of `C^n`, fiberwise-linear maps -/


variable {ğ•œ B F : Type*} [TopologicalSpace B]
variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace FiberwiseLinear

variable {Ï† Ï†' : B â†’ F â‰ƒL[ğ•œ] F} {U U' : Set B}

/-- For `B` a topological space and `F` a `ğ•œ`-normed space, a map from `U : Set B` to `F â‰ƒL[ğ•œ] F`
determines a partial homeomorphism from `B Ã— F` to itself by its action fiberwise. -/
def partialHomeomorph (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (hU : IsOpen U)
    (hÏ† : ContinuousOn (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
    (h2Ï† : ContinuousOn (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U) :
    PartialHomeomorph (B Ã— F) (B Ã— F) where
  toFun x := (x.1, Ï† x.1 x.2)
  invFun x := (x.1, (Ï† x.1).symm x.2)
  source := U Ã—Ë¢ univ
  target := U Ã—Ë¢ univ
  map_source' _x hx := mk_mem_prod hx.1 (mem_univ _)
  map_target' _x hx := mk_mem_prod hx.1 (mem_univ _)
  left_inv' _ _ := Prod.ext rfl (ContinuousLinearEquiv.symm_apply_apply _ _)
  right_inv' _ _ := Prod.ext rfl (ContinuousLinearEquiv.apply_symm_apply _ _)
  open_source := hU.prod isOpen_univ
  open_target := hU.prod isOpen_univ
  continuousOn_toFun :=
    have : ContinuousOn (fun p : B Ã— F => ((Ï† p.1 : F â†’L[ğ•œ] F), p.2)) (U Ã—Ë¢ univ) :=
      hÏ†.prodMap continuousOn_id
    continuousOn_fst.prodMk (isBoundedBilinearMap_apply.continuous.comp_continuousOn this)
  continuousOn_invFun :=
    have : ContinuousOn (fun p : B Ã— F => (((Ï† p.1).symm : F â†’L[ğ•œ] F), p.2)) (U Ã—Ë¢ univ) :=
      h2Ï†.prodMap continuousOn_id
    continuousOn_fst.prodMk (isBoundedBilinearMap_apply.continuous.comp_continuousOn this)

/-- Compute the composition of two partial homeomorphisms induced by fiberwise linear
equivalences. -/
theorem trans_partialHomeomorph_apply (hU : IsOpen U)
    (hÏ† : ContinuousOn (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
    (h2Ï† : ContinuousOn (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U) (hU' : IsOpen U')
    (hÏ†' : ContinuousOn (fun x => Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
    (h2Ï†' : ContinuousOn (fun x => (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U') (b : B) (v : F) :
    (FiberwiseLinear.partialHomeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
      FiberwiseLinear.partialHomeomorph Ï†' hU' hÏ†' h2Ï†')
        âŸ¨b, vâŸ© =
      âŸ¨b, Ï†' b (Ï† b v)âŸ© :=
  rfl

/-- Compute the source of the composition of two partial homeomorphisms induced by fiberwise linear
equivalences. -/
theorem source_trans_partialHomeomorph (hU : IsOpen U)
    (hÏ† : ContinuousOn (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
    (h2Ï† : ContinuousOn (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U) (hU' : IsOpen U')
    (hÏ†' : ContinuousOn (fun x => Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
    (h2Ï†' : ContinuousOn (fun x => (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U') :
    (FiberwiseLinear.partialHomeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
          FiberwiseLinear.partialHomeomorph Ï†' hU' hÏ†' h2Ï†').source =
      (U âˆ© U') Ã—Ë¢ univ := by
  dsimp only [FiberwiseLinear.partialHomeomorph]; mfld_set_tac

/-- Compute the target of the composition of two partial homeomorphisms induced by fiberwise linear
equivalences. -/
theorem target_trans_partialHomeomorph (hU : IsOpen U)
    (hÏ† : ContinuousOn (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
    (h2Ï† : ContinuousOn (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U) (hU' : IsOpen U')
    (hÏ†' : ContinuousOn (fun x => Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
    (h2Ï†' : ContinuousOn (fun x => (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U') :
    (FiberwiseLinear.partialHomeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
          FiberwiseLinear.partialHomeomorph Ï†' hU' hÏ†' h2Ï†').target =
      (U âˆ© U') Ã—Ë¢ univ := by
  dsimp only [FiberwiseLinear.partialHomeomorph]; mfld_set_tac

end FiberwiseLinear

variable {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ğ•œ EB] {HB : Type*}
  [TopologicalSpace HB] [ChartedSpace HB B] {IB : ModelWithCorners ğ•œ EB HB}

/-- Let `e` be a partial homeomorphism of `B Ã— F`.  Suppose that at every point `p` in the source of
`e`, there is some neighbourhood `s` of `p` on which `e` is equal to a bi-`C^n` fiberwise linear
partial homeomorphism.
Then the source of `e` is of the form `U Ã—Ë¢ univ`, for some set `U` in `B`, and, at any point `x` in
`U`, admits a neighbourhood `u` of `x` such that `e` is equal on `u Ã—Ë¢ univ` to some bi-`C^n`
fiberwise linear partial homeomorphism. -/
theorem ContMDiffFiberwiseLinear.locality_auxâ‚
    (n : WithTop â„•âˆ) (e : PartialHomeomorph (B Ã— F) (B Ã— F))
    (h : âˆ€ p âˆˆ e.source, âˆƒ s : Set (B Ã— F), IsOpen s âˆ§ p âˆˆ s âˆ§
      âˆƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (u : Set B) (hu : IsOpen u)
        (hÏ† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x : F â†’L[ğ•œ] F)) u)
        (h2Ï† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
          (e.restr s).EqOnSource
            (FiberwiseLinear.partialHomeomorph Ï† hu hÏ†.continuousOn h2Ï†.continuousOn)) :
    âˆƒ U : Set B, e.source = U Ã—Ë¢ univ âˆ§ âˆ€ x âˆˆ U,
        âˆƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (u : Set B) (hu : IsOpen u) (_huU : u âŠ† U) (_hux : x âˆˆ u),
          âˆƒ (hÏ† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x : F â†’L[ğ•œ] F)) u)
            (h2Ï† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
            (e.restr (u Ã—Ë¢ univ)).EqOnSource
              (FiberwiseLinear.partialHomeomorph Ï† hu hÏ†.continuousOn h2Ï†.continuousOn) := by
  rw [SetCoe.forall'] at h
  choose s hs hsp Ï† u hu hÏ† h2Ï† heÏ† using h
  have hesu : âˆ€ p : e.source, e.source âˆ© s p = u p Ã—Ë¢ univ := by
    intro p
    rw [â† e.restr_source' (s _) (hs _)]
    exact (heÏ† p).1
  have hu' : âˆ€ p : e.source, (p : B Ã— F).fst âˆˆ u p := by
    intro p
    have : (p : B Ã— F) âˆˆ e.source âˆ© s p := âŸ¨p.prop, hsp pâŸ©
    simpa only [hesu, mem_prod, mem_univ, and_true] using this
  have heu : âˆ€ p : e.source, âˆ€ q : B Ã— F, q.fst âˆˆ u p â†’ q âˆˆ e.source := by
    intro p q hq
    have : q âˆˆ u p Ã—Ë¢ (univ : Set F) := âŸ¨hq, trivialâŸ©
    rw [â† hesu p] at this
    exact this.1
  have he : e.source = (Prod.fst '' e.source) Ã—Ë¢ (univ : Set F) := by
    apply HasSubset.Subset.antisymm
    Â· intro p hp
      exact âŸ¨âŸ¨p, hp, rflâŸ©, trivialâŸ©
    Â· rintro âŸ¨x, vâŸ© âŸ¨âŸ¨p, hp, rfl : p.fst = xâŸ©, -âŸ©
      exact heu âŸ¨p, hpâŸ© (p.fst, v) (hu' âŸ¨p, hpâŸ©)
  refine âŸ¨Prod.fst '' e.source, he, ?_âŸ©
  rintro x âŸ¨p, hp, rflâŸ©
  refine âŸ¨Ï† âŸ¨p, hpâŸ©, u âŸ¨p, hpâŸ©, hu âŸ¨p, hpâŸ©, ?_, hu' _, hÏ† âŸ¨p, hpâŸ©, h2Ï† âŸ¨p, hpâŸ©, ?_âŸ©
  Â· intro y hy; exact âŸ¨(y, 0), heu âŸ¨p, hpâŸ© âŸ¨_, _âŸ© hy, rflâŸ©
  Â· rw [â† hesu, e.restr_source_inter]; exact heÏ† âŸ¨p, hpâŸ©

/-- Let `e` be a partial homeomorphism of `B Ã— F` whose source is `U Ã—Ë¢ univ`, for some set `U` in
`B`, and which, at any point `x` in `U`, admits a neighbourhood `u` of `x` such that `e` is equal
on `u Ã—Ë¢ univ` to some bi-`C^n` fiberwise linear partial homeomorphism.  Then `e` itself
is equal to some bi-`C^n` fiberwise linear partial homeomorphism.

This is the key mathematical point of the `locality` condition in the construction of the
`StructureGroupoid` of bi-`C^n` fiberwise linear partial homeomorphisms.  The proof is by gluing
together the various bi-`C^n` fiberwise linear partial homeomorphism which exist locally.

The `U` in the conclusion is the same `U` as in the hypothesis. We state it like this, because this
is exactly what we need for `contMDiffFiberwiseLinear`. -/
theorem ContMDiffFiberwiseLinear.locality_auxâ‚‚
    (n : WithTop â„•âˆ) (e : PartialHomeomorph (B Ã— F) (B Ã— F)) (U : Set B) (hU : e.source = U Ã—Ë¢ univ)
    (h : âˆ€ x âˆˆ U,
      âˆƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (u : Set B) (hu : IsOpen u) (_hUu : u âŠ† U) (_hux : x âˆˆ u)
        (hÏ† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x : F â†’L[ğ•œ] F)) u)
        (h2Ï† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
          (e.restr (u Ã—Ë¢ univ)).EqOnSource
            (FiberwiseLinear.partialHomeomorph Ï† hu hÏ†.continuousOn h2Ï†.continuousOn)) :
    âˆƒ (Î¦ : B â†’ F â‰ƒL[ğ•œ] F) (U : Set B) (hUâ‚€ : IsOpen U) (hÎ¦ :
      ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Î¦ x : F â†’L[ğ•œ] F)) U) (h2Î¦ :
      ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => ((Î¦ x).symm : F â†’L[ğ•œ] F)) U),
      e.EqOnSource (FiberwiseLinear.partialHomeomorph Î¦ hUâ‚€ hÎ¦.continuousOn h2Î¦.continuousOn) := by
  classical
  rw [SetCoe.forall'] at h
  choose! Ï† u hu hUu hux hÏ† h2Ï† heÏ† using h
  have heuÏ† : âˆ€ x : U, EqOn e (fun q => (q.1, Ï† x q.1 q.2)) (u x Ã—Ë¢ univ) := fun x p hp â†¦ by
    refine (heÏ† x).2 ?_
    rw [(heÏ† x).1]
    exact hp
  have huÏ† : âˆ€ (x x' : U) (y : B), y âˆˆ u x â†’ y âˆˆ u x' â†’ Ï† x y = Ï† x' y := fun p p' y hyp hyp' â†¦ by
    ext v
    have h1 : e (y, v) = (y, Ï† p y v) := heuÏ† _ âŸ¨(id hyp : (y, v).fst âˆˆ u p), trivialâŸ©
    have h2 : e (y, v) = (y, Ï† p' y v) := heuÏ† _ âŸ¨(id hyp' : (y, v).fst âˆˆ u p'), trivialâŸ©
    exact congr_arg Prod.snd (h1.symm.trans h2)
  have hUu' : U = â‹ƒ i, u i := by
    ext x
    rw [mem_iUnion]
    refine âŸ¨fun h => âŸ¨âŸ¨x, hâŸ©, hux _âŸ©, ?_âŸ©
    rintro âŸ¨x, hxâŸ©
    exact hUu x hx
  have hU' : IsOpen U := by
    rw [hUu']
    apply isOpen_iUnion hu
  let Î¦â‚€ : U â†’ F â‰ƒL[ğ•œ] F := iUnionLift u (fun x => Ï† x âˆ˜ (â†‘)) huÏ† U hUu'.le
  let Î¦ : B â†’ F â‰ƒL[ğ•œ] F := fun y =>
    if hy : y âˆˆ U then Î¦â‚€ âŸ¨y, hyâŸ© else ContinuousLinearEquiv.refl ğ•œ F
  have hÎ¦ : âˆ€ (y) (hy : y âˆˆ U), Î¦ y = Î¦â‚€ âŸ¨y, hyâŸ© := fun y hy => dif_pos hy
  have hÎ¦Ï† : âˆ€ x : U, âˆ€ y âˆˆ u x, Î¦ y = Ï† x y := by
    intro x y hyu
    refine (hÎ¦ y (hUu x hyu)).trans ?_
    exact iUnionLift_mk âŸ¨y, hyuâŸ© _
  have hÎ¦ : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun y => (Î¦ y : F â†’L[ğ•œ] F)) U := by
    apply contMDiffOn_of_locally_contMDiffOn
    intro x hx
    refine âŸ¨u âŸ¨x, hxâŸ©, hu âŸ¨x, hxâŸ©, hux _, ?_âŸ©
    refine (ContMDiffOn.congr (hÏ† âŸ¨x, hxâŸ©) ?_).mono inter_subset_right
    intro y hy
    rw [hÎ¦Ï† âŸ¨x, hxâŸ© y hy]
  have h2Î¦ : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun y => ((Î¦ y).symm : F â†’L[ğ•œ] F)) U := by
    apply contMDiffOn_of_locally_contMDiffOn
    intro x hx
    refine âŸ¨u âŸ¨x, hxâŸ©, hu âŸ¨x, hxâŸ©, hux _, ?_âŸ©
    refine (ContMDiffOn.congr (h2Ï† âŸ¨x, hxâŸ©) ?_).mono inter_subset_right
    intro y hy
    rw [hÎ¦Ï† âŸ¨x, hxâŸ© y hy]
  refine âŸ¨Î¦, U, hU', hÎ¦, h2Î¦, hU, fun p hp => ?_âŸ©
  rw [hU] at hp
  rw [heuÏ† âŸ¨p.fst, hp.1âŸ© âŸ¨hux _, hp.2âŸ©]
  congrm (_, ?_)
  rw [hÎ¦Ï†]
  apply hux

-- Having this private lemma speeds up `simp` calls below a lot.
-- TODO: understand why and fix the underlying issue (relatedly, the `simp` calls
-- in `contMDiffFiberwiseLinear` are quite slow, even with this change)
private theorem mem_aux {e : PartialHomeomorph (B Ã— F) (B Ã— F)} {n : WithTop â„•âˆ} :
    (e âˆˆ â‹ƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : Set B) (hU : IsOpen U)
      (hÏ† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
      (h2Ï† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
        {e | e.EqOnSource (FiberwiseLinear.partialHomeomorph Ï† hU hÏ†.continuousOn
          h2Ï†.continuousOn)}) â†”
      âˆƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : Set B) (hU : IsOpen U)
        (hÏ† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
        (h2Ï† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
          e.EqOnSource
            (FiberwiseLinear.partialHomeomorph Ï† hU hÏ†.continuousOn h2Ï†.continuousOn) := by
  simp only [mem_iUnion, mem_setOf_eq]

variable (F B IB)

/-- For `B` a manifold and `F` a normed space, the groupoid on `B Ã— F` consisting of local
homeomorphisms which are bi-`C^n` and fiberwise linear, and induce the identity on `B`.
When a (topological) vector bundle is `C^n`, then the composition of charts associated
to the vector bundle belong to this groupoid. -/
def contMDiffFiberwiseLinear (n : WithTop â„•âˆ) : StructureGroupoid (B Ã— F) where
  members :=
    â‹ƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : Set B) (hU : IsOpen U)
      (hÏ† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U)
      (h2Ï† : ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
        {e | e.EqOnSource (FiberwiseLinear.partialHomeomorph Ï† hU hÏ†.continuousOn h2Ï†.continuousOn)}
  trans' := by
    simp only [mem_aux]
    rintro e e' âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© âŸ¨Ï†', U', hU', hÏ†', h2Ï†', heÏ†'âŸ©
    refine âŸ¨fun b => (Ï† b).trans (Ï†' b), _, hU.inter hU', ?_, ?_,
      Setoid.trans (PartialHomeomorph.EqOnSource.trans' heÏ† heÏ†') âŸ¨?_, ?_âŸ©âŸ©
    Â· change
        ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n
          (fun x : B => (Ï†' x).toContinuousLinearMap âˆ˜L (Ï† x).toContinuousLinearMap) (U âˆ© U')
      exact (hÏ†'.mono inter_subset_right).clm_comp (hÏ†.mono inter_subset_left)
    Â· change
        ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n
          (fun x : B => (Ï† x).symm.toContinuousLinearMap âˆ˜L (Ï†' x).symm.toContinuousLinearMap)
          (U âˆ© U')
      exact (h2Ï†.mono inter_subset_left).clm_comp (h2Ï†'.mono inter_subset_right)
    Â· apply FiberwiseLinear.source_trans_partialHomeomorph
    Â· rintro âŸ¨b, vâŸ© -; apply FiberwiseLinear.trans_partialHomeomorph_apply
  -- Porting note: without introducing `e` first, the first `simp only` fails
  symm' := fun e â†¦ by
    simp only [mem_aux]
    rintro âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ©
    refine âŸ¨fun b => (Ï† b).symm, U, hU, h2Ï†, ?_, PartialHomeomorph.EqOnSource.symm' heÏ†âŸ©
    simp_rw [ContinuousLinearEquiv.symm_symm]
    exact hÏ†
  id_mem' := by
    simp_rw [mem_aux]
    refine âŸ¨fun _ â†¦ ContinuousLinearEquiv.refl ğ•œ F, univ, isOpen_univ, contMDiffOn_const,
      contMDiffOn_const, âŸ¨?_, fun b _hb â†¦ rflâŸ©âŸ©
    simp only [FiberwiseLinear.partialHomeomorph, PartialHomeomorph.refl_partialEquiv,
      PartialEquiv.refl_source, univ_prod_univ]
  locality' := by
    -- the hard work has been extracted to `locality_auxâ‚` and `locality_auxâ‚‚`
    simp only [mem_aux]
    intro e he
    obtain âŸ¨U, hU, hâŸ© := ContMDiffFiberwiseLinear.locality_auxâ‚ n e he
    exact ContMDiffFiberwiseLinear.locality_auxâ‚‚ n e U hU h
  mem_of_eqOnSource' := by
    simp only [mem_aux]
    rintro e e' âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© hee'
    exact âŸ¨Ï†, U, hU, hÏ†, h2Ï†, Setoid.trans hee' heÏ†âŸ©

@[simp]
theorem mem_contMDiffFiberwiseLinear_iff {n : WithTop â„•âˆ} (e : PartialHomeomorph (B Ã— F) (B Ã— F)) :
    e âˆˆ contMDiffFiberwiseLinear B F IB n â†”
      âˆƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : Set B) (hU : IsOpen U) (hÏ† :
        ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => Ï† x : B â†’ F â†’L[ğ•œ] F) U) (h2Ï† :
        ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun x => (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
        e.EqOnSource (FiberwiseLinear.partialHomeomorph Ï† hU hÏ†.continuousOn h2Ï†.continuousOn) :=
  mem_aux

