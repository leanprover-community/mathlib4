/-
Copyright (c) 2022 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Geometry.Manifold.ContMDiff.Atlas
import Mathlib.Geometry.Manifold.VectorBundle.FiberwiseLinear
import Mathlib.Topology.VectorBundle.Constructions

#align_import geometry.manifold.vector_bundle.basic from "leanprover-community/mathlib"@"e473c3198bb41f68560cab68a0529c854b618833"

/-! # Smooth vector bundles

This file defines smooth vector bundles over a smooth manifold.

Let `E` be a topological vector bundle, with model fiber `F` and base space `B`.  We consider `E` as
carrying a charted space structure given by its trivializations -- these are charts to `B Ã— F`.
Then, by "composition", if `B` is itself a charted space over `H` (e.g. a smooth manifold), then `E`
is also a charted space over `H Ã— F`.

Now, we define `SmoothVectorBundle` as the `Prop` of having smooth transition functions.
Recall the structure groupoid `smoothFiberwiseLinear` on `B Ã— F` consisting of smooth, fiberwise
linear partial homeomorphisms.  We show that our definition of "smooth vector bundle" implies
`HasGroupoid` for this groupoid, and show (by a "composition" of `HasGroupoid` instances) that
this means that a smooth vector bundle is a smooth manifold.

Since `SmoothVectorBundle` is a mixin, it should be easy to make variants and for many such
variants to coexist -- vector bundles can be smooth vector bundles over several different base
fields, they can also be C^k vector bundles, etc.

## Main definitions and constructions

* `FiberBundle.chartedSpace`: A fiber bundle `E` over a base `B` with model fiber `F` is naturally
  a charted space modelled on `B Ã— F`.

* `FiberBundle.chartedSpace'`: Let `B` be a charted space modelled on `HB`.  Then a fiber bundle
  `E` over a base `B` with model fiber `F` is naturally a charted space modelled on `HB.prod F`.

* `SmoothVectorBundle`: Mixin class stating that a (topological) `VectorBundle` is smooth, in the
  sense of having smooth transition functions.

* `SmoothFiberwiseLinear.hasGroupoid`: For a smooth vector bundle `E` over `B` with fiber
  modelled on `F`, the change-of-co-ordinates between two trivializations `e`, `e'` for `E`,
  considered as charts to `B Ã— F`, is smooth and fiberwise linear, in the sense of belonging to the
  structure groupoid `smoothFiberwiseLinear`.

* `Bundle.TotalSpace.smoothManifoldWithCorners`: A smooth vector bundle is naturally a smooth
  manifold.

* `VectorBundleCore.smoothVectorBundle`: If a (topological) `VectorBundleCore` is smooth,
  in the sense of having smooth transition functions (cf. `VectorBundleCore.IsSmooth`),
  then the vector bundle constructed from it is a smooth vector bundle.

* `VectorPrebundle.smoothVectorBundle`: If a `VectorPrebundle` is smooth,
  in the sense of having smooth transition functions (cf. `VectorPrebundle.IsSmooth`),
  then the vector bundle constructed from it is a smooth vector bundle.

* `Bundle.Prod.smoothVectorBundle`: The direct sum of two smooth vector bundles is a smooth vector
  bundle.
-/

assert_not_exists mfderiv

open Bundle Set PartialHomeomorph

open Function (id_def)

open Filter

open scoped Manifold Bundle Topology

variable {ğ•œ B B' F M : Type*} {E : B â†’ Type*}

/-! ### Charted space structure on a fiber bundle -/


section

variable [TopologicalSpace F] [TopologicalSpace (TotalSpace F E)] [âˆ€ x, TopologicalSpace (E x)]
  {HB : Type*} [TopologicalSpace HB] [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]

/-- A fiber bundle `E` over a base `B` with model fiber `F` is naturally a charted space modelled on
`B Ã— F`. -/
instance FiberBundle.chartedSpace' : ChartedSpace (B Ã— F) (TotalSpace F E) where
  atlas := (fun e : Trivialization F (Ï€ F E) => e.toPartialHomeomorph) '' trivializationAtlas F E
  chartAt x := (trivializationAt F E x.proj).toPartialHomeomorph
  mem_chart_source x :=
    (trivializationAt F E x.proj).mem_source.mpr (mem_baseSet_trivializationAt F E x.proj)
  chart_mem_atlas _ := mem_image_of_mem _ (trivialization_mem_atlas F E _)
#align fiber_bundle.charted_space FiberBundle.chartedSpace'

theorem FiberBundle.chartedSpace'_chartAt (x : TotalSpace F E) :
    chartAt (B Ã— F) x = (trivializationAt F E x.proj).toPartialHomeomorph :=
  rfl

/- Porting note: In Lean 3, the next instance was inside a section with locally reducible
`ModelProd` and it used `ModelProd B F` as the intermediate space. Using `B Ã— F` in the middle
gives the same instance.
-/
--attribute [local reducible] ModelProd

/-- Let `B` be a charted space modelled on `HB`.  Then a fiber bundle `E` over a base `B` with model
fiber `F` is naturally a charted space modelled on `HB.prod F`. -/
instance FiberBundle.chartedSpace : ChartedSpace (ModelProd HB F) (TotalSpace F E) :=
  ChartedSpace.comp _ (B Ã— F) _
#align fiber_bundle.charted_space' FiberBundle.chartedSpace

theorem FiberBundle.chartedSpace_chartAt (x : TotalSpace F E) :
    chartAt (ModelProd HB F) x =
      (trivializationAt F E x.proj).toPartialHomeomorph â‰«â‚•
        (chartAt HB x.proj).prod (PartialHomeomorph.refl F) := by
  dsimp only [chartAt_comp, prodChartedSpace_chartAt, FiberBundle.chartedSpace'_chartAt,
    chartAt_self_eq]
  rw [Trivialization.coe_coe, Trivialization.coe_fst' _ (mem_baseSet_trivializationAt F E x.proj)]
#align fiber_bundle.charted_space_chart_at FiberBundle.chartedSpace_chartAt

theorem FiberBundle.chartedSpace_chartAt_symm_fst (x : TotalSpace F E) (y : ModelProd HB F)
    (hy : y âˆˆ (chartAt (ModelProd HB F) x).target) :
    ((chartAt (ModelProd HB F) x).symm y).proj = (chartAt HB x.proj).symm y.1 := by
  simp only [FiberBundle.chartedSpace_chartAt, mfld_simps] at hy âŠ¢
  exact (trivializationAt F E x.proj).proj_symm_apply hy.2
#align fiber_bundle.charted_space_chart_at_symm_fst FiberBundle.chartedSpace_chartAt_symm_fst

end

section

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [TopologicalSpace (TotalSpace F E)] [âˆ€ x, TopologicalSpace (E x)] {EB : Type*}
  [NormedAddCommGroup EB] [NormedSpace ğ•œ EB] {HB : Type*} [TopologicalSpace HB]
  (IB : ModelWithCorners ğ•œ EB HB) (E' : B â†’ Type*) [âˆ€ x, Zero (E' x)] {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  [Is : SmoothManifoldWithCorners IM M] {n : â„•âˆ}

variable [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]

protected theorem FiberBundle.extChartAt (x : TotalSpace F E) :
    extChartAt (IB.prod ğ“˜(ğ•œ, F)) x =
      (trivializationAt F E x.proj).toPartialEquiv â‰«
        (extChartAt IB x.proj).prod (PartialEquiv.refl F) := by
  simp_rw [extChartAt, FiberBundle.chartedSpace_chartAt, extend]
  simp only [PartialEquiv.trans_assoc, mfld_simps]
  -- Porting note: should not be needed
  rw [PartialEquiv.prod_trans, PartialEquiv.refl_trans]
#align fiber_bundle.ext_chart_at FiberBundle.extChartAt

protected theorem FiberBundle.extChartAt_target (x : TotalSpace F E) :
    (extChartAt (IB.prod ğ“˜(ğ•œ, F)) x).target =
      ((extChartAt IB x.proj).target âˆ©
        (extChartAt IB x.proj).symm â»Â¹' (trivializationAt F E x.proj).baseSet) Ã—Ë¢ univ := by
  rw [FiberBundle.extChartAt, PartialEquiv.trans_target, Trivialization.target_eq, inter_prod]
  rfl

theorem FiberBundle.writtenInExtChartAt_trivializationAt {x : TotalSpace F E} {y}
    (hy : y âˆˆ (extChartAt (IB.prod ğ“˜(ğ•œ, F)) x).target) :
    writtenInExtChartAt (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) x
      (trivializationAt F E x.proj) y = y :=
  writtenInExtChartAt_chartAt_comp _ _ hy

theorem FiberBundle.writtenInExtChartAt_trivializationAt_symm {x : TotalSpace F E} {y}
    (hy : y âˆˆ (extChartAt (IB.prod ğ“˜(ğ•œ, F)) x).target) :
    writtenInExtChartAt (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) (trivializationAt F E x.proj x)
      (trivializationAt F E x.proj).toPartialHomeomorph.symm y = y :=
  writtenInExtChartAt_chartAt_symm_comp _ _ hy

/-! ### Smoothness of maps in/out fiber bundles

Note: For these results we don't need that the bundle is a smooth vector bundle, or even a vector
bundle at all, just that it is a fiber bundle over a charted base space.
-/

namespace Bundle

variable {IB}

/-- Characterization of C^n functions into a smooth vector bundle. -/
theorem contMDiffWithinAt_totalSpace (f : M â†’ TotalSpace F E) {s : Set M} {xâ‚€ : M} :
    ContMDiffWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) n f s xâ‚€ â†”
      ContMDiffWithinAt IM IB n (fun x => (f x).proj) s xâ‚€ âˆ§
      ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun x â†¦ (trivializationAt F E (f xâ‚€).proj (f x)).2) s xâ‚€ := by
  simp (config := { singlePass := true }) only [contMDiffWithinAt_iff_target]
  rw [and_and_and_comm, â† FiberBundle.continuousWithinAt_totalSpace, and_congr_right_iff]
  intro hf
  simp_rw [modelWithCornersSelf_prod, FiberBundle.extChartAt, Function.comp,
    PartialEquiv.trans_apply, PartialEquiv.prod_coe, PartialEquiv.refl_coe,
    extChartAt_self_apply, modelWithCornersSelf_coe, Function.id_def, â† chartedSpaceSelf_prod]
  refine (contMDiffWithinAt_prod_iff _).trans (and_congr ?_ Iff.rfl)
  have h1 : (fun x => (f x).proj) â»Â¹' (trivializationAt F E (f xâ‚€).proj).baseSet âˆˆ ğ“[s] xâ‚€ :=
    ((FiberBundle.continuous_proj F E).continuousWithinAt.comp hf (mapsTo_image f s))
      ((Trivialization.open_baseSet _).mem_nhds (mem_baseSet_trivializationAt F E _))
  refine EventuallyEq.contMDiffWithinAt_iff (eventually_of_mem h1 fun x hx => ?_) ?_
  Â· simp_rw [Function.comp, PartialHomeomorph.coe_coe, Trivialization.coe_coe]
    rw [Trivialization.coe_fst']
    exact hx
  Â· simp only [mfld_simps]
#align bundle.cont_mdiff_within_at_total_space Bundle.contMDiffWithinAt_totalSpace

/-- Characterization of C^n functions into a smooth vector bundle. -/
theorem contMDiffAt_totalSpace (f : M â†’ TotalSpace F E) (xâ‚€ : M) :
    ContMDiffAt IM (IB.prod ğ“˜(ğ•œ, F)) n f xâ‚€ â†”
      ContMDiffAt IM IB n (fun x => (f x).proj) xâ‚€ âˆ§
        ContMDiffAt IM ğ“˜(ğ•œ, F) n (fun x => (trivializationAt F E (f xâ‚€).proj (f x)).2) xâ‚€ := by
  simp_rw [â† contMDiffWithinAt_univ]; exact contMDiffWithinAt_totalSpace f
#align bundle.cont_mdiff_at_total_space Bundle.contMDiffAt_totalSpace

/-- Characterization of C^n sections of a smooth vector bundle. -/
theorem contMDiffAt_section (s : âˆ€ x, E x) (xâ‚€ : B) :
    ContMDiffAt IB (IB.prod ğ“˜(ğ•œ, F)) n (fun x => TotalSpace.mk' F x (s x)) xâ‚€ â†”
      ContMDiffAt IB ğ“˜(ğ•œ, F) n (fun x â†¦ (trivializationAt F E xâ‚€ âŸ¨x, s xâŸ©).2) xâ‚€ := by
  simp_rw [contMDiffAt_totalSpace, and_iff_right_iff_imp]; intro; exact contMDiffAt_id
#align bundle.cont_mdiff_at_section Bundle.contMDiffAt_section

variable (E)

theorem contMDiff_proj : ContMDiff (IB.prod ğ“˜(ğ•œ, F)) IB n (Ï€ F E) := fun x â†¦ by
  have : ContMDiffAt (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) n id x := contMDiffAt_id
  rw [contMDiffAt_totalSpace] at this
  exact this.1
#align bundle.cont_mdiff_proj Bundle.contMDiff_proj

theorem smooth_proj : Smooth (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) :=
  contMDiff_proj E
#align bundle.smooth_proj Bundle.smooth_proj

theorem contMDiffOn_proj {s : Set (TotalSpace F E)} :
    ContMDiffOn (IB.prod ğ“˜(ğ•œ, F)) IB n (Ï€ F E) s :=
  (Bundle.contMDiff_proj E).contMDiffOn
#align bundle.cont_mdiff_on_proj Bundle.contMDiffOn_proj

theorem smoothOn_proj {s : Set (TotalSpace F E)} : SmoothOn (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) s :=
  contMDiffOn_proj E
#align bundle.smooth_on_proj Bundle.smoothOn_proj

theorem contMDiffAt_proj {p : TotalSpace F E} : ContMDiffAt (IB.prod ğ“˜(ğ•œ, F)) IB n (Ï€ F E) p :=
  (Bundle.contMDiff_proj E).contMDiffAt
#align bundle.cont_mdiff_at_proj Bundle.contMDiffAt_proj

theorem smoothAt_proj {p : TotalSpace F E} : SmoothAt (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) p :=
  Bundle.contMDiffAt_proj E
#align bundle.smooth_at_proj Bundle.smoothAt_proj

theorem contMDiffWithinAt_proj {s : Set (TotalSpace F E)} {p : TotalSpace F E} :
    ContMDiffWithinAt (IB.prod ğ“˜(ğ•œ, F)) IB n (Ï€ F E) s p :=
  (Bundle.contMDiffAt_proj E).contMDiffWithinAt
#align bundle.cont_mdiff_within_at_proj Bundle.contMDiffWithinAt_proj

theorem smoothWithinAt_proj {s : Set (TotalSpace F E)} {p : TotalSpace F E} :
    SmoothWithinAt (IB.prod ğ“˜(ğ•œ, F)) IB (Ï€ F E) s p :=
  Bundle.contMDiffWithinAt_proj E
#align bundle.smooth_within_at_proj Bundle.smoothWithinAt_proj

variable (ğ•œ) [âˆ€ x, AddCommMonoid (E x)]
variable [âˆ€ x, Module ğ•œ (E x)] [VectorBundle ğ•œ F E]

theorem smooth_zeroSection : Smooth IB (IB.prod ğ“˜(ğ•œ, F)) (zeroSection F E) := fun x â†¦ by
  unfold zeroSection
  rw [Bundle.contMDiffAt_section]
  apply (contMDiffAt_const (c := 0)).congr_of_eventuallyEq
  filter_upwards [(trivializationAt F E x).open_baseSet.mem_nhds
    (mem_baseSet_trivializationAt F E x)] with y hy
    using congr_arg Prod.snd <| (trivializationAt F E x).zeroSection ğ•œ hy
#align bundle.smooth_zero_section Bundle.smooth_zeroSection

end Bundle

end

/-! ### Smooth vector bundles -/


variable [NontriviallyNormedField ğ•œ] {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ğ•œ EB]
  {HB : Type*} [TopologicalSpace HB] (IB : ModelWithCorners ğ•œ EB HB) [TopologicalSpace B]
  [ChartedSpace HB B] [SmoothManifoldWithCorners IB B] {EM : Type*} [NormedAddCommGroup EM]
  [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM] {IM : ModelWithCorners ğ•œ EM HM}
  [TopologicalSpace M] [ChartedSpace HM M] [Is : SmoothManifoldWithCorners IM M] {n : â„•âˆ}
  [âˆ€ x, AddCommMonoid (E x)] [âˆ€ x, Module ğ•œ (E x)] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

section WithTopology

variable [TopologicalSpace (TotalSpace F E)] [âˆ€ x, TopologicalSpace (E x)] (F E)
variable [FiberBundle F E] [VectorBundle ğ•œ F E]

/-- When `B` is a smooth manifold with corners with respect to a model `IB` and `E` is a
topological vector bundle over `B` with fibers isomorphic to `F`, then `SmoothVectorBundle F E IB`
registers that the bundle is smooth, in the sense of having smooth transition functions.
This is a mixin, not carrying any new data. -/
class SmoothVectorBundle : Prop where
  protected smoothOn_coordChangeL :
    âˆ€ (e e' : Trivialization F (Ï€ F E)) [MemTrivializationAtlas e] [MemTrivializationAtlas e'],
      SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B => (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F))
        (e.baseSet âˆ© e'.baseSet)
#align smooth_vector_bundle SmoothVectorBundle
#align smooth_vector_bundle.smooth_on_coord_change SmoothVectorBundle.smoothOn_coordChangeL

variable [SmoothVectorBundle F E IB]

section SmoothCoordChange

variable {F E}
variable (e e' : Trivialization F (Ï€ F E)) [MemTrivializationAtlas e] [MemTrivializationAtlas e']

theorem smoothOn_coordChangeL :
    SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B => (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F))
      (e.baseSet âˆ© e'.baseSet) :=
  SmoothVectorBundle.smoothOn_coordChangeL e e'

theorem smoothOn_symm_coordChangeL :
    SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B => ((e.coordChangeL ğ•œ e' b).symm : F â†’L[ğ•œ] F))
      (e.baseSet âˆ© e'.baseSet) := by
  rw [inter_comm]
  refine (SmoothVectorBundle.smoothOn_coordChangeL e' e).congr fun b hb â†¦ ?_
  rw [e.symm_coordChangeL e' hb]

theorem contMDiffOn_coordChangeL :
    ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun b : B => (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F))
      (e.baseSet âˆ© e'.baseSet) :=
  (smoothOn_coordChangeL IB e e').of_le le_top

theorem contMDiffOn_symm_coordChangeL :
    ContMDiffOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun b : B => ((e.coordChangeL ğ•œ e' b).symm : F â†’L[ğ•œ] F))
      (e.baseSet âˆ© e'.baseSet) :=
  (smoothOn_symm_coordChangeL IB e e').of_le le_top

variable {e e'}

theorem contMDiffAt_coordChangeL {x : B} (h : x âˆˆ e.baseSet) (h' : x âˆˆ e'.baseSet) :
    ContMDiffAt IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun b : B => (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F)) x :=
  (contMDiffOn_coordChangeL IB e e').contMDiffAt <|
    (e.open_baseSet.inter e'.open_baseSet).mem_nhds âŸ¨h, h'âŸ©

theorem smoothAt_coordChangeL {x : B} (h : x âˆˆ e.baseSet) (h' : x âˆˆ e'.baseSet) :
    SmoothAt IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun b : B => (e.coordChangeL ğ•œ e' b : F â†’L[ğ•œ] F)) x :=
  contMDiffAt_coordChangeL IB h h'

variable {IB}
variable {s : Set M} {f : M â†’ B} {g : M â†’ F} {x : M}

protected theorem ContMDiffWithinAt.coordChangeL
    (hf : ContMDiffWithinAt IM IB n f s x) (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    ContMDiffWithinAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) s x :=
  (contMDiffAt_coordChangeL IB he he').comp_contMDiffWithinAt _ hf

protected nonrec theorem ContMDiffAt.coordChangeL
    (hf : ContMDiffAt IM IB n f x) (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    ContMDiffAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) x :=
  hf.coordChangeL he he'

protected theorem ContMDiffOn.coordChangeL
    (hf : ContMDiffOn IM IB n f s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    ContMDiffOn IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) s :=
  fun x hx â†¦ (hf x hx).coordChangeL (he hx) (he' hx)

protected theorem ContMDiff.coordChangeL
    (hf : ContMDiff IM IB n f) (he : âˆ€ x, f x âˆˆ e.baseSet) (he' : âˆ€ x, f x âˆˆ e'.baseSet) :
    ContMDiff IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) n (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) := fun x â†¦
  (hf x).coordChangeL (he x) (he' x)

protected nonrec theorem SmoothWithinAt.coordChangeL
    (hf : SmoothWithinAt IM IB f s x) (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    SmoothWithinAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) s x :=
  hf.coordChangeL he he'

protected nonrec theorem SmoothAt.coordChangeL
    (hf : SmoothAt IM IB f x) (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    SmoothAt IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) x :=
  hf.coordChangeL he he'

protected nonrec theorem SmoothOn.coordChangeL
    (hf : SmoothOn IM IB f s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    SmoothOn IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) s :=
  hf.coordChangeL he he'

protected nonrec theorem Smooth.coordChangeL
    (hf : Smooth IM IB f) (he : âˆ€ x, f x âˆˆ e.baseSet) (he' : âˆ€ x, f x âˆˆ e'.baseSet) :
    Smooth IM ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (fun y â†¦ (e.coordChangeL ğ•œ e' (f y) : F â†’L[ğ•œ] F)) :=
  hf.coordChangeL he he'

protected theorem ContMDiffWithinAt.coordChange
    (hf : ContMDiffWithinAt IM IB n f s x) (hg : ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n g s x)
    (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun y â†¦ e.coordChange e' (f y) (g y)) s x := by
  refine ((hf.coordChangeL he he').clm_apply hg).congr_of_eventuallyEq ?_ ?_
  Â· have : e.baseSet âˆ© e'.baseSet âˆˆ ğ“ (f x) :=
     (e.open_baseSet.inter e'.open_baseSet).mem_nhds âŸ¨he, he'âŸ©
    filter_upwards [hf.continuousWithinAt this] with y hy
    exact (Trivialization.coordChangeL_apply' e e' hy (g y)).symm
  Â· exact (Trivialization.coordChangeL_apply' e e' âŸ¨he, he'âŸ© (g x)).symm

protected nonrec theorem ContMDiffAt.coordChange
    (hf : ContMDiffAt IM IB n f x) (hg : ContMDiffAt IM ğ“˜(ğ•œ, F) n g x) (he : f x âˆˆ e.baseSet)
    (he' : f x âˆˆ e'.baseSet) :
    ContMDiffAt IM ğ“˜(ğ•œ, F) n (fun y â†¦ e.coordChange e' (f y) (g y)) x :=
  hf.coordChange hg he he'

protected theorem ContMDiffOn.coordChange (hf : ContMDiffOn IM IB n f s)
    (hg : ContMDiffOn IM ğ“˜(ğ•œ, F) n g s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    ContMDiffOn IM ğ“˜(ğ•œ, F) n (fun y â†¦ e.coordChange e' (f y) (g y)) s := fun x hx â†¦
  (hf x hx).coordChange (hg x hx) (he hx) (he' hx)

protected theorem ContMDiff.coordChange (hf : ContMDiff IM IB n f)
    (hg : ContMDiff IM ğ“˜(ğ•œ, F) n g) (he : âˆ€ x, f x âˆˆ e.baseSet) (he' : âˆ€ x, f x âˆˆ e'.baseSet) :
    ContMDiff IM ğ“˜(ğ•œ, F) n (fun y â†¦ e.coordChange e' (f y) (g y)) := fun x â†¦
  (hf x).coordChange (hg x) (he x) (he' x)

protected nonrec theorem SmoothWithinAt.coordChange
    (hf : SmoothWithinAt IM IB f s x) (hg : SmoothWithinAt IM ğ“˜(ğ•œ, F) g s x)
    (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    SmoothWithinAt IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) s x :=
  hf.coordChange hg he he'

protected nonrec theorem SmoothAt.coordChange (hf : SmoothAt IM IB f x)
    (hg : SmoothAt IM ğ“˜(ğ•œ, F) g x) (he : f x âˆˆ e.baseSet) (he' : f x âˆˆ e'.baseSet) :
    SmoothAt IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) x :=
  hf.coordChange hg he he'

protected nonrec theorem SmoothOn.coordChange (hf : SmoothOn IM IB f s)
    (hg : SmoothOn IM ğ“˜(ğ•œ, F) g s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    SmoothOn IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) s :=
  hf.coordChange hg he he'

protected theorem Smooth.coordChange (hf : Smooth IM IB f)
    (hg : Smooth IM ğ“˜(ğ•œ, F) g) (he : âˆ€ x, f x âˆˆ e.baseSet) (he' : âˆ€ x, f x âˆˆ e'.baseSet) :
    Smooth IM ğ“˜(ğ•œ, F) (fun y â†¦ e.coordChange e' (f y) (g y)) := fun x â†¦
  (hf x).coordChange (hg x) (he x) (he' x)

variable (IB e e')

theorem Trivialization.contMDiffOn_symm_trans :
    ContMDiffOn (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) n
      (e.toPartialHomeomorph.symm â‰«â‚• e'.toPartialHomeomorph) (e.target âˆ© e'.target) := by
  have Hmaps : MapsTo Prod.fst (e.target âˆ© e'.target) (e.baseSet âˆ© e'.baseSet) := fun x hx â†¦
    âŸ¨e.mem_target.1 hx.1, e'.mem_target.1 hx.2âŸ©
  rw [mapsTo_inter] at Hmaps
  -- TODO: drop `congr` #5473
  refine (contMDiffOn_fst.prod_mk
    (contMDiffOn_fst.coordChange contMDiffOn_snd Hmaps.1 Hmaps.2)).congr ?_
  rintro âŸ¨b, xâŸ© hb
  refine Prod.ext ?_ rfl
  have : (e.toPartialHomeomorph.symm (b, x)).1 âˆˆ e'.baseSet := by
    simp_all only [Trivialization.mem_target, mfld_simps]
  exact (e'.coe_fst' this).trans (e.proj_symm_apply hb.1)

variable {IB e e'}

theorem ContMDiffWithinAt.change_section_trivialization {f : M â†’ TotalSpace F E}
    (hp : ContMDiffWithinAt IM IB n (Ï€ F E âˆ˜ f) s x)
    (hf : ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun y â†¦ (e (f y)).2) s x)
    (he : f x âˆˆ e.source) (he' : f x âˆˆ e'.source) :
    ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun y â†¦ (e' (f y)).2) s x := by
  rw [Trivialization.mem_source] at he he'
  refine (hp.coordChange hf he he').congr_of_eventuallyEq ?_ ?_
  Â· filter_upwards [hp.continuousWithinAt (e.open_baseSet.mem_nhds he)] with y hy
    rw [Function.comp_apply, e.coordChange_apply_snd _ hy]
  Â· rw [Function.comp_apply, e.coordChange_apply_snd _ he]

theorem Trivialization.contMDiffWithinAt_snd_comp_iffâ‚‚ {f : M â†’ TotalSpace F E}
    (hp : ContMDiffWithinAt IM IB n (Ï€ F E âˆ˜ f) s x)
    (he : f x âˆˆ e.source) (he' : f x âˆˆ e'.source) :
    ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun y â†¦ (e (f y)).2) s x â†”
      ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun y â†¦ (e' (f y)).2) s x :=
  âŸ¨(hp.change_section_trivialization Â· he he'), (hp.change_section_trivialization Â· he' he)âŸ©

end SmoothCoordChange

/-- For a smooth vector bundle `E` over `B` with fiber modelled on `F`, the change-of-co-ordinates
between two trivializations `e`, `e'` for `E`, considered as charts to `B Ã— F`, is smooth and
fiberwise linear. -/
instance SmoothFiberwiseLinear.hasGroupoid :
    HasGroupoid (TotalSpace F E) (smoothFiberwiseLinear B F IB) where
  compatible := by
    rintro _ _ âŸ¨e, he, rflâŸ© âŸ¨e', he', rflâŸ©
    haveI : MemTrivializationAtlas e := âŸ¨heâŸ©
    haveI : MemTrivializationAtlas e' := âŸ¨he'âŸ©
    rw [mem_smoothFiberwiseLinear_iff]
    refine âŸ¨_, _, e.open_baseSet.inter e'.open_baseSet, smoothOn_coordChangeL IB e e',
      smoothOn_symm_coordChangeL IB e e', ?_âŸ©
    refine PartialHomeomorph.eqOnSourceSetoid.symm âŸ¨?_, ?_âŸ©
    Â· simp only [e.symm_trans_source_eq e', FiberwiseLinear.partialHomeomorph, trans_toPartialEquiv,
        symm_toPartialEquiv]
    Â· rintro âŸ¨b, vâŸ© hb
      exact (e.apply_symm_apply_eq_coordChangeL e' hb.1 v).symm
#align smooth_fiberwise_linear.has_groupoid SmoothFiberwiseLinear.hasGroupoid

/-- A smooth vector bundle `E` is naturally a smooth manifold. -/
instance Bundle.TotalSpace.smoothManifoldWithCorners :
    SmoothManifoldWithCorners (IB.prod ğ“˜(ğ•œ, F)) (TotalSpace F E) := by
  refine { StructureGroupoid.HasGroupoid.comp (smoothFiberwiseLinear B F IB) ?_ with }
  intro e he
  rw [mem_smoothFiberwiseLinear_iff] at he
  obtain âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© := he
  rw [isLocalStructomorphOn_contDiffGroupoid_iff]
  refine âŸ¨ContMDiffOn.congr ?_ (EqOnSource.eqOn heÏ†),
      ContMDiffOn.congr ?_ (EqOnSource.eqOn (EqOnSource.symm' heÏ†))âŸ©
  Â· rw [EqOnSource.source_eq heÏ†]
    apply smoothOn_fst.prod_mk
    exact (hÏ†.comp contMDiffOn_fst <| prod_subset_preimage_fst _ _).clm_apply contMDiffOn_snd
  Â· rw [EqOnSource.target_eq heÏ†]
    apply smoothOn_fst.prod_mk
    exact (h2Ï†.comp contMDiffOn_fst <| prod_subset_preimage_fst _ _).clm_apply contMDiffOn_snd
#align bundle.total_space.smooth_manifold_with_corners Bundle.TotalSpace.smoothManifoldWithCorners

section

variable {F E}
variable {e e' : Trivialization F (Ï€ F E)} [MemTrivializationAtlas e] [MemTrivializationAtlas e']

theorem Trivialization.contMDiffWithinAt_iff {f : M â†’ TotalSpace F E} {s : Set M} {xâ‚€ : M}
    (he : f xâ‚€ âˆˆ e.source) :
    ContMDiffWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) n f s xâ‚€ â†”
      ContMDiffWithinAt IM IB n (fun x => (f x).proj) s xâ‚€ âˆ§
      ContMDiffWithinAt IM ğ“˜(ğ•œ, F) n (fun x â†¦ (e (f x)).2) s xâ‚€ :=
  (contMDiffWithinAt_totalSpace _).trans <| and_congr_right fun h â†¦
    Trivialization.contMDiffWithinAt_snd_comp_iffâ‚‚ h FiberBundle.mem_trivializationAt_proj_source he

theorem Trivialization.contMDiffAt_iff {f : M â†’ TotalSpace F E} {xâ‚€ : M} (he : f xâ‚€ âˆˆ e.source) :
    ContMDiffAt IM (IB.prod ğ“˜(ğ•œ, F)) n f xâ‚€ â†”
      ContMDiffAt IM IB n (fun x => (f x).proj) xâ‚€ âˆ§
      ContMDiffAt IM ğ“˜(ğ•œ, F) n (fun x â†¦ (e (f x)).2) xâ‚€ :=
  e.contMDiffWithinAt_iff _ he

theorem Trivialization.contMDiffOn_iff {f : M â†’ TotalSpace F E} {s : Set M}
    (he : MapsTo f s e.source) :
    ContMDiffOn IM (IB.prod ğ“˜(ğ•œ, F)) n f s â†”
      ContMDiffOn IM IB n (fun x => (f x).proj) s âˆ§
      ContMDiffOn IM ğ“˜(ğ•œ, F) n (fun x â†¦ (e (f x)).2) s := by
  simp only [ContMDiffOn, â† forall_and]
  exact forallâ‚‚_congr fun x hx â†¦ e.contMDiffWithinAt_iff IB (he hx)

theorem Trivialization.contMDiff_iff {f : M â†’ TotalSpace F E} (he : âˆ€ x, f x âˆˆ e.source) :
    ContMDiff IM (IB.prod ğ“˜(ğ•œ, F)) n f â†”
      ContMDiff IM IB n (fun x => (f x).proj) âˆ§
      ContMDiff IM ğ“˜(ğ•œ, F) n (fun x â†¦ (e (f x)).2) :=
  (forall_congr' fun x â†¦ e.contMDiffAt_iff IB (he x)).trans forall_and

theorem Trivialization.smoothWithinAt_iff {f : M â†’ TotalSpace F E} {s : Set M} {xâ‚€ : M}
    (he : f xâ‚€ âˆˆ e.source) :
    SmoothWithinAt IM (IB.prod ğ“˜(ğ•œ, F)) f s xâ‚€ â†”
      SmoothWithinAt IM IB (fun x => (f x).proj) s xâ‚€ âˆ§
      SmoothWithinAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) s xâ‚€ :=
  e.contMDiffWithinAt_iff IB he

theorem Trivialization.smoothAt_iff {f : M â†’ TotalSpace F E} {xâ‚€ : M} (he : f xâ‚€ âˆˆ e.source) :
    SmoothAt IM (IB.prod ğ“˜(ğ•œ, F)) f xâ‚€ â†”
      SmoothAt IM IB (fun x => (f x).proj) xâ‚€ âˆ§ SmoothAt IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) xâ‚€ :=
  e.contMDiffAt_iff IB he

theorem Trivialization.smoothOn_iff {f : M â†’ TotalSpace F E} {s : Set M}
    (he : MapsTo f s e.source) :
    SmoothOn IM (IB.prod ğ“˜(ğ•œ, F)) f s â†”
      SmoothOn IM IB (fun x => (f x).proj) s âˆ§ SmoothOn IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) s :=
  e.contMDiffOn_iff IB he

theorem Trivialization.smooth_iff {f : M â†’ TotalSpace F E} (he : âˆ€ x, f x âˆˆ e.source) :
    Smooth IM (IB.prod ğ“˜(ğ•œ, F)) f â†”
      Smooth IM IB (fun x => (f x).proj) âˆ§ Smooth IM ğ“˜(ğ•œ, F) (fun x â†¦ (e (f x)).2) :=
  e.contMDiff_iff IB he

theorem Trivialization.smoothOn (e : Trivialization F (Ï€ F E)) [MemTrivializationAtlas e] :
    SmoothOn (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) e e.source := by
  have : SmoothOn (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) id e.source := smoothOn_id
  rw [e.smoothOn_iff IB (mapsTo_id _)] at this
  exact (this.1.prod_mk this.2).congr fun x hx â†¦ (e.mk_proj_snd hx).symm

theorem Trivialization.smoothOn_symm (e : Trivialization F (Ï€ F E)) [MemTrivializationAtlas e] :
    SmoothOn (IB.prod ğ“˜(ğ•œ, F)) (IB.prod ğ“˜(ğ•œ, F)) e.toPartialHomeomorph.symm e.target := by
  rw [e.smoothOn_iff IB e.toPartialHomeomorph.symm_mapsTo]
  refine âŸ¨smoothOn_fst.congr fun x hx â†¦ e.proj_symm_apply hx, smoothOn_snd.congr fun x hx â†¦ ?_âŸ©
  rw [e.apply_symm_apply hx]

end

/-! ### Core construction for smooth vector bundles -/

namespace VectorBundleCore

variable {Î¹ : Type*} {F}
variable (Z : VectorBundleCore ğ•œ B F Î¹)

/-- Mixin for a `VectorBundleCore` stating smoothness (of transition functions). -/
class IsSmooth (IB : ModelWithCorners ğ•œ EB HB) : Prop where
  smoothOn_coordChange :
    âˆ€ i j, SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Z.coordChange i j) (Z.baseSet i âˆ© Z.baseSet j)
#align vector_bundle_core.is_smooth VectorBundleCore.IsSmooth

theorem smoothOn_coordChange (IB : ModelWithCorners ğ•œ EB HB) [h : Z.IsSmooth IB] (i j : Î¹) :
    SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Z.coordChange i j) (Z.baseSet i âˆ© Z.baseSet j) :=
  h.1 i j

variable [Z.IsSmooth IB]

/-- If a `VectorBundleCore` has the `IsSmooth` mixin, then the vector bundle constructed from it
is a smooth vector bundle. -/
instance smoothVectorBundle : SmoothVectorBundle F Z.Fiber IB where
  smoothOn_coordChangeL := by
    rintro - - âŸ¨i, rflâŸ© âŸ¨i', rflâŸ©
    refine (Z.smoothOn_coordChange IB i i').congr fun b hb â†¦ ?_
    ext v
    exact Z.localTriv_coordChange_eq i i' hb v
#align vector_bundle_core.smooth_vector_bundle VectorBundleCore.smoothVectorBundle

end VectorBundleCore

/-! ### The trivial smooth vector bundle -/

/-- A trivial vector bundle over a smooth manifold is a smooth vector bundle. -/
instance Bundle.Trivial.smoothVectorBundle : SmoothVectorBundle F (Bundle.Trivial B F) IB where
  smoothOn_coordChangeL := by
    intro e e' he he'
    obtain rfl := Bundle.Trivial.eq_trivialization B F e
    obtain rfl := Bundle.Trivial.eq_trivialization B F e'
    simp_rw [Bundle.Trivial.trivialization.coordChangeL]
    exact smooth_const.smoothOn
#align bundle.trivial.smooth_vector_bundle Bundle.Trivial.smoothVectorBundle

/-! ### Direct sums of smooth vector bundles -/


section Prod

variable (Fâ‚ : Type*) [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚] (Eâ‚ : B â†’ Type*)
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, AddCommMonoid (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)]

variable (Fâ‚‚ : Type*) [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚] (Eâ‚‚ : B â†’ Type*)
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, AddCommMonoid (Eâ‚‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚‚ x)]

variable [âˆ€ x : B, TopologicalSpace (Eâ‚ x)] [âˆ€ x : B, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚] [SmoothVectorBundle Fâ‚ Eâ‚ IB]
  [SmoothVectorBundle Fâ‚‚ Eâ‚‚ IB]

/-- The direct sum of two smooth vector bundles over the same base is a smooth vector bundle. -/
instance Bundle.Prod.smoothVectorBundle : SmoothVectorBundle (Fâ‚ Ã— Fâ‚‚) (Eâ‚ Ã—áµ‡ Eâ‚‚) IB where
  smoothOn_coordChangeL := by
    rintro _ _ âŸ¨eâ‚, eâ‚‚, iâ‚, iâ‚‚, rflâŸ© âŸ¨eâ‚', eâ‚‚', iâ‚', iâ‚‚', rflâŸ©
    rw [SmoothOn]
    refine ContMDiffOn.congr ?_ (eâ‚.coordChangeL_prod ğ•œ eâ‚' eâ‚‚ eâ‚‚')
    refine ContMDiffOn.clm_prodMap ?_ ?_
    Â· refine (smoothOn_coordChangeL IB eâ‚ eâ‚').mono ?_
      simp only [Trivialization.baseSet_prod, mfld_simps]
      mfld_set_tac
    Â· refine (smoothOn_coordChangeL IB eâ‚‚ eâ‚‚').mono ?_
      simp only [Trivialization.baseSet_prod, mfld_simps]
      mfld_set_tac
#align bundle.prod.smooth_vector_bundle Bundle.Prod.smoothVectorBundle

end Prod

end WithTopology

/-! ### Prebundle construction for smooth vector bundles -/

namespace VectorPrebundle

variable [âˆ€ x, TopologicalSpace (E x)]

/-- Mixin for a `VectorPrebundle` stating smoothness of coordinate changes. -/
class IsSmooth (a : VectorPrebundle ğ•œ F E) : Prop where
  exists_smoothCoordChange :
    âˆ€áµ‰ (e âˆˆ a.pretrivializationAtlas) (e' âˆˆ a.pretrivializationAtlas),
      âˆƒ f : B â†’ F â†’L[ğ•œ] F,
        SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) f (e.baseSet âˆ© e'.baseSet) âˆ§
          âˆ€ (b : B) (_ : b âˆˆ e.baseSet âˆ© e'.baseSet) (v : F),
            f b v = (e' âŸ¨b, e.symm b vâŸ©).2
#align vector_prebundle.is_smooth VectorPrebundle.IsSmooth

variable (a : VectorPrebundle ğ•œ F E) [ha : a.IsSmooth IB] {e e' : Pretrivialization F (Ï€ F E)}

/-- A randomly chosen coordinate change on a `SmoothVectorPrebundle`, given by
  the field `exists_coordChange`. Note that `a.smoothCoordChange` need not be the same as
  `a.coordChange`. -/
noncomputable def smoothCoordChange (he : e âˆˆ a.pretrivializationAtlas)
    (he' : e' âˆˆ a.pretrivializationAtlas) (b : B) : F â†’L[ğ•œ] F :=
  Classical.choose (ha.exists_smoothCoordChange e he e' he') b
#align vector_prebundle.smooth_coord_change VectorPrebundle.smoothCoordChange

variable {IB}

theorem smoothOn_smoothCoordChange (he : e âˆˆ a.pretrivializationAtlas)
    (he' : e' âˆˆ a.pretrivializationAtlas) :
    SmoothOn IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (a.smoothCoordChange IB he he') (e.baseSet âˆ© e'.baseSet) :=
  (Classical.choose_spec (ha.exists_smoothCoordChange e he e' he')).1
#align vector_prebundle.smooth_on_smooth_coord_change VectorPrebundle.smoothOn_smoothCoordChange

theorem smoothCoordChange_apply (he : e âˆˆ a.pretrivializationAtlas)
    (he' : e' âˆˆ a.pretrivializationAtlas) {b : B} (hb : b âˆˆ e.baseSet âˆ© e'.baseSet) (v : F) :
    a.smoothCoordChange IB he he' b v = (e' âŸ¨b, e.symm b vâŸ©).2 :=
  (Classical.choose_spec (ha.exists_smoothCoordChange e he e' he')).2 b hb v
#align vector_prebundle.smooth_coord_change_apply VectorPrebundle.smoothCoordChange_apply

theorem mk_smoothCoordChange (he : e âˆˆ a.pretrivializationAtlas)
    (he' : e' âˆˆ a.pretrivializationAtlas) {b : B} (hb : b âˆˆ e.baseSet âˆ© e'.baseSet) (v : F) :
    (b, a.smoothCoordChange IB he he' b v) = e' âŸ¨b, e.symm b vâŸ© := by
  ext
  Â· rw [e.mk_symm hb.1 v, e'.coe_fst', e.proj_symm_apply' hb.1]
    rw [e.proj_symm_apply' hb.1]; exact hb.2
  Â· exact a.smoothCoordChange_apply he he' hb v
#align vector_prebundle.mk_smooth_coord_change VectorPrebundle.mk_smoothCoordChange

variable (IB)
/-- Make a `SmoothVectorBundle` from a `SmoothVectorPrebundle`. -/
theorem smoothVectorBundle : @SmoothVectorBundle
    _ _ F E _ _ _ _ _ _ IB _ _ _ _ _ _ a.totalSpaceTopology _ a.toFiberBundle a.toVectorBundle :=
  letI := a.totalSpaceTopology; letI := a.toFiberBundle; letI := a.toVectorBundle
  { smoothOn_coordChangeL := by
      rintro _ _ âŸ¨e, he, rflâŸ© âŸ¨e', he', rflâŸ©
      refine (a.smoothOn_smoothCoordChange he he').congr ?_
      intro b hb
      ext v
      rw [a.smoothCoordChange_apply he he' hb v, ContinuousLinearEquiv.coe_coe,
        Trivialization.coordChangeL_apply]
      exacts [rfl, hb] }
#align vector_prebundle.smooth_vector_bundle VectorPrebundle.smoothVectorBundle

end VectorPrebundle
