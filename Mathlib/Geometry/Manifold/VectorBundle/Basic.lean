/-
Copyright (c) 2022 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Geometry.Manifold.ContMDiff.Atlas
import Mathlib.Geometry.Manifold.VectorBundle.FiberwiseLinear
import Mathlib.Topology.VectorBundle.Constructions

#align_import geometry.manifold.vector_bundle.basic from "leanprover-community/mathlib"@"e473c3198bb41f68560cab68a0529c854b618833"

/-! # Smooth vector bundles

This file defines smooth vector bundles over a smooth manifold.

Let `E` be a topological vector bundle, with model fiber `F` and base space `B`.  We consider `E` as
carrying a charted space structure given by its trivializations -- these are charts to `B √ó F`.
Then, by "composition", if `B` is itself a charted space over `H` (e.g. a smooth manifold), then `E`
is also a charted space over `H √ó F`.

Now, we define `SmoothVectorBundle` as the `Prop` of having smooth transition functions.
Recall the structure groupoid `smoothFiberwiseLinear` on `B √ó F` consisting of smooth, fiberwise
linear local homeomorphisms.  We show that our definition of "smooth vector bundle" implies
`HasGroupoid` for this groupoid, and show (by a "composition" of `HasGroupoid` instances) that
this means that a smooth vector bundle is a smooth manifold.

Since `SmoothVectorBundle` is a mixin, it should be easy to make variants and for many such
variants to coexist -- vector bundles can be smooth vector bundles over several different base
fields, they can also be C^k vector bundles, etc.

## Main definitions and constructions

* `FiberBundle.chartedSpace`: A fiber bundle `E` over a base `B` with model fiber `F` is naturally
  a charted space modelled on `B √ó F`.

* `FiberBundle.chartedSpace'`: Let `B` be a charted space modelled on `HB`.  Then a fiber bundle
  `E` over a base `B` with model fiber `F` is naturally a charted space modelled on `HB.prod F`.

* `SmoothVectorBundle`: Mixin class stating that a (topological) `VectorBundle` is smooth, in the
  sense of having smooth transition functions.

* `SmoothFiberwiseLinear.hasGroupoid`: For a smooth vector bundle `E` over `B` with fiber
  modelled on `F`, the change-of-co-ordinates between two trivializations `e`, `e'` for `E`,
  considered as charts to `B √ó F`, is smooth and fiberwise linear, in the sense of belonging to the
  structure groupoid `smoothFiberwiseLinear`.

* `Bundle.TotalSpace.smoothManifoldWithCorners`: A smooth vector bundle is naturally a smooth
  manifold.

* `VectorBundleCore.smoothVectorBundle`: If a (topological) `VectorBundleCore` is smooth,
  in the sense of having smooth transition functions (cf. `VectorBundleCore.IsSmooth`),
  then the vector bundle constructed from it is a smooth vector bundle.

* `VectorPrebundle.smoothVectorBundle`: If a `VectorPrebundle` is smooth,
  in the sense of having smooth transition functions (cf. `VectorPrebundle.IsSmooth`),
  then the vector bundle constructed from it is a smooth vector bundle.

* `Bundle.Prod.smoothVectorBundle`: The direct sum of two smooth vector bundles is a smooth vector
  bundle.
-/

set_option autoImplicit true


assert_not_exists mfderiv

open Bundle Set LocalHomeomorph

open Function (id_def)

open Filter

open scoped Manifold Bundle Topology

variable {ùïú B B' F M : Type*} {E : B ‚Üí Type*}

/-! ### Charted space structure on a fiber bundle -/


section

variable [TopologicalSpace F] [TopologicalSpace (TotalSpace F E)] [‚àÄ x, TopologicalSpace (E x)]
  {HB : Type*} [TopologicalSpace HB] [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]

/-- A fiber bundle `E` over a base `B` with model fiber `F` is naturally a charted space modelled on
`B √ó F`. -/
instance FiberBundle.chartedSpace' : ChartedSpace (B √ó F) (TotalSpace F E) where
  atlas := (fun e : Trivialization F (œÄ F E) => e.toLocalHomeomorph) '' trivializationAtlas F E
  chartAt x := (trivializationAt F E x.proj).toLocalHomeomorph
  mem_chart_source x :=
    (trivializationAt F E x.proj).mem_source.mpr (mem_baseSet_trivializationAt F E x.proj)
  chart_mem_atlas _ := mem_image_of_mem _ (trivialization_mem_atlas F E _)
#align fiber_bundle.charted_space FiberBundle.chartedSpace'

theorem FiberBundle.chartedSpace'_chartAt (x : TotalSpace F E) :
    chartAt (B √ó F) x = (trivializationAt F E x.proj).toLocalHomeomorph :=
  rfl

/- Porting note: In Lean 3, the next instance was inside a section with locally reducible
`ModelProd` and it used `ModelProd B F` as the intermediate space. Using `B √ó F` in the middle
gives the same instance.
-/
--attribute [local reducible] ModelProd

/-- Let `B` be a charted space modelled on `HB`.  Then a fiber bundle `E` over a base `B` with model
fiber `F` is naturally a charted space modelled on `HB.prod F`. -/
instance FiberBundle.chartedSpace : ChartedSpace (ModelProd HB F) (TotalSpace F E) :=
  ChartedSpace.comp _ (B √ó F) _
#align fiber_bundle.charted_space' FiberBundle.chartedSpace

theorem FiberBundle.chartedSpace_chartAt (x : TotalSpace F E) :
    chartAt (ModelProd HB F) x =
      (trivializationAt F E x.proj).toLocalHomeomorph ‚â´‚Çï
        (chartAt HB x.proj).prod (LocalHomeomorph.refl F) := by
  dsimp only [chartAt_comp, prodChartedSpace_chartAt, FiberBundle.chartedSpace'_chartAt,
    chartAt_self_eq]
  rw [Trivialization.coe_coe, Trivialization.coe_fst' _ (mem_baseSet_trivializationAt F E x.proj)]
#align fiber_bundle.charted_space_chart_at FiberBundle.chartedSpace_chartAt

theorem FiberBundle.chartedSpace_chartAt_symm_fst (x : TotalSpace F E) (y : ModelProd HB F)
    (hy : y ‚àà (chartAt (ModelProd HB F) x).target) :
    ((chartAt (ModelProd HB F) x).symm y).proj = (chartAt HB x.proj).symm y.1 := by
  simp only [FiberBundle.chartedSpace_chartAt, mfld_simps] at hy ‚ä¢
  exact (trivializationAt F E x.proj).proj_symm_apply hy.2
#align fiber_bundle.charted_space_chart_at_symm_fst FiberBundle.chartedSpace_chartAt_symm_fst

end

section

variable [NontriviallyNormedField ùïú] [NormedAddCommGroup F] [NormedSpace ùïú F]
  [TopologicalSpace (TotalSpace F E)] [‚àÄ x, TopologicalSpace (E x)] {EB : Type*}
  [NormedAddCommGroup EB] [NormedSpace ùïú EB] {HB : Type*} [TopologicalSpace HB]
  (IB : ModelWithCorners ùïú EB HB) (E' : B ‚Üí Type*) [‚àÄ x, Zero (E' x)] {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ùïú EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ùïú EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  [Is : SmoothManifoldWithCorners IM M] {n : ‚Ñï‚àû}

variable [TopologicalSpace B] [ChartedSpace HB B] [FiberBundle F E]

protected theorem FiberBundle.extChartAt (x : TotalSpace F E) :
    extChartAt (IB.prod ùìò(ùïú, F)) x =
      (trivializationAt F E x.proj).toLocalEquiv ‚â´
        (extChartAt IB x.proj).prod (LocalEquiv.refl F) := by
  simp_rw [extChartAt, FiberBundle.chartedSpace_chartAt, extend]
  simp only [LocalEquiv.trans_assoc, mfld_simps]
  -- porting note: should not be needed
  rw [LocalEquiv.prod_trans, LocalEquiv.refl_trans]
#align fiber_bundle.ext_chart_at FiberBundle.extChartAt

protected theorem FiberBundle.extChartAt_target (x : TotalSpace F E) :
    (extChartAt (IB.prod ùìò(ùïú, F)) x).target =
      ((extChartAt IB x.proj).target ‚à©
        (extChartAt IB x.proj).symm ‚Åª¬π' (trivializationAt F E x.proj).baseSet) √óÀ¢ univ := by
  rw [FiberBundle.extChartAt, LocalEquiv.trans_target, Trivialization.target_eq, inter_prod]
  rfl

theorem FiberBundle.writtenInExtChartAt_trivializationAt {x : TotalSpace F E} {y}
    (hy : y ‚àà (extChartAt (IB.prod ùìò(ùïú, F)) x).target) :
    writtenInExtChartAt (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) x
      (trivializationAt F E x.proj) y = y :=
  writtenInExtChartAt_chartAt_comp _ _ hy

theorem FiberBundle.writtenInExtChartAt_trivializationAt_symm {x : TotalSpace F E} {y}
    (hy : y ‚àà (extChartAt (IB.prod ùìò(ùïú, F)) x).target) :
    writtenInExtChartAt (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) (trivializationAt F E x.proj x)
      (trivializationAt F E x.proj).toLocalHomeomorph.symm y = y :=
  writtenInExtChartAt_chartAt_symm_comp _ _ hy

/-! ### Smoothness of maps in/out fiber bundles

Note: For these results we don't need that the bundle is a smooth vector bundle, or even a vector
bundle at all, just that it is a fiber bundle over a charted base space.
-/

namespace Bundle

variable {IB}

/-- Characterization of C^n functions into a smooth vector bundle. -/
theorem contMDiffWithinAt_totalSpace (f : M ‚Üí TotalSpace F E) {s : Set M} {x‚ÇÄ : M} :
    ContMDiffWithinAt IM (IB.prod ùìò(ùïú, F)) n f s x‚ÇÄ ‚Üî
      ContMDiffWithinAt IM IB n (fun x => (f x).proj) s x‚ÇÄ ‚àß
      ContMDiffWithinAt IM ùìò(ùïú, F) n (fun x ‚Ü¶ (trivializationAt F E (f x‚ÇÄ).proj (f x)).2) s x‚ÇÄ := by
  simp (config := { singlePass := true }) only [contMDiffWithinAt_iff_target]
  rw [and_and_and_comm, ‚Üê FiberBundle.continuousWithinAt_totalSpace, and_congr_right_iff]
  intro hf
  simp_rw [modelWithCornersSelf_prod, FiberBundle.extChartAt, Function.comp, LocalEquiv.trans_apply,
    LocalEquiv.prod_coe, LocalEquiv.refl_coe, extChartAt_self_apply, modelWithCornersSelf_coe,
    id_def]
  refine (contMDiffWithinAt_prod_iff _).trans (and_congr ?_ Iff.rfl)
  have h1 : (fun x => (f x).proj) ‚Åª¬π' (trivializationAt F E (f x‚ÇÄ).proj).baseSet ‚àà ùìù[s] x‚ÇÄ :=
    ((FiberBundle.continuous_proj F E).continuousWithinAt.comp hf (mapsTo_image f s))
      ((Trivialization.open_baseSet _).mem_nhds (mem_baseSet_trivializationAt F E _))
  refine EventuallyEq.contMDiffWithinAt_iff (eventually_of_mem h1 fun x hx => ?_) ?_
  ¬∑ simp_rw [Function.comp, LocalHomeomorph.coe_coe, Trivialization.coe_coe]
    rw [Trivialization.coe_fst']
    exact hx
  ¬∑ simp only [mfld_simps]
#align bundle.cont_mdiff_within_at_total_space Bundle.contMDiffWithinAt_totalSpace

/-- Characterization of C^n functions into a smooth vector bundle. -/
theorem contMDiffAt_totalSpace (f : M ‚Üí TotalSpace F E) (x‚ÇÄ : M) :
    ContMDiffAt IM (IB.prod ùìò(ùïú, F)) n f x‚ÇÄ ‚Üî
      ContMDiffAt IM IB n (fun x => (f x).proj) x‚ÇÄ ‚àß
        ContMDiffAt IM ùìò(ùïú, F) n (fun x => (trivializationAt F E (f x‚ÇÄ).proj (f x)).2) x‚ÇÄ := by
  simp_rw [‚Üê contMDiffWithinAt_univ]; exact contMDiffWithinAt_totalSpace f
#align bundle.cont_mdiff_at_total_space Bundle.contMDiffAt_totalSpace

/-- Characterization of C^n sections of a smooth vector bundle. -/
theorem contMDiffAt_section (s : ‚àÄ x, E x) (x‚ÇÄ : B) :
    ContMDiffAt IB (IB.prod ùìò(ùïú, F)) n (fun x => TotalSpace.mk' F x (s x)) x‚ÇÄ ‚Üî
      ContMDiffAt IB ùìò(ùïú, F) n (fun x ‚Ü¶ (trivializationAt F E x‚ÇÄ ‚ü®x, s x‚ü©).2) x‚ÇÄ := by
  simp_rw [contMDiffAt_totalSpace, and_iff_right_iff_imp]; intro; exact contMDiffAt_id
#align bundle.cont_mdiff_at_section Bundle.contMDiffAt_section

variable (E)

theorem contMDiff_proj : ContMDiff (IB.prod ùìò(ùïú, F)) IB n (œÄ F E) := fun x ‚Ü¶ by
  have : ContMDiffAt (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) n id x := contMDiffAt_id
  rw [contMDiffAt_totalSpace] at this
  exact this.1
#align bundle.cont_mdiff_proj Bundle.contMDiff_proj

theorem smooth_proj : Smooth (IB.prod ùìò(ùïú, F)) IB (œÄ F E) :=
  contMDiff_proj E
#align bundle.smooth_proj Bundle.smooth_proj

theorem contMDiffOn_proj {s : Set (TotalSpace F E)} :
    ContMDiffOn (IB.prod ùìò(ùïú, F)) IB n (œÄ F E) s :=
  (Bundle.contMDiff_proj E).contMDiffOn
#align bundle.cont_mdiff_on_proj Bundle.contMDiffOn_proj

theorem smoothOn_proj {s : Set (TotalSpace F E)} : SmoothOn (IB.prod ùìò(ùïú, F)) IB (œÄ F E) s :=
  contMDiffOn_proj E
#align bundle.smooth_on_proj Bundle.smoothOn_proj

theorem contMDiffAt_proj {p : TotalSpace F E} : ContMDiffAt (IB.prod ùìò(ùïú, F)) IB n (œÄ F E) p :=
  (Bundle.contMDiff_proj E).contMDiffAt
#align bundle.cont_mdiff_at_proj Bundle.contMDiffAt_proj

theorem smoothAt_proj {p : TotalSpace F E} : SmoothAt (IB.prod ùìò(ùïú, F)) IB (œÄ F E) p :=
  Bundle.contMDiffAt_proj E
#align bundle.smooth_at_proj Bundle.smoothAt_proj

theorem contMDiffWithinAt_proj {s : Set (TotalSpace F E)} {p : TotalSpace F E} :
    ContMDiffWithinAt (IB.prod ùìò(ùïú, F)) IB n (œÄ F E) s p :=
  (Bundle.contMDiffAt_proj E).contMDiffWithinAt
#align bundle.cont_mdiff_within_at_proj Bundle.contMDiffWithinAt_proj

theorem smoothWithinAt_proj {s : Set (TotalSpace F E)} {p : TotalSpace F E} :
    SmoothWithinAt (IB.prod ùìò(ùïú, F)) IB (œÄ F E) s p :=
  Bundle.contMDiffWithinAt_proj E
#align bundle.smooth_within_at_proj Bundle.smoothWithinAt_proj

variable (ùïú) [‚àÄ x, AddCommMonoid (E x)]
variable [‚àÄ x, Module ùïú (E x)] [VectorBundle ùïú F E]

theorem smooth_zeroSection : Smooth IB (IB.prod ùìò(ùïú, F)) (zeroSection F E) := fun x ‚Ü¶ by
  unfold zeroSection
  rw [Bundle.contMDiffAt_section]
  apply (contMDiffAt_const (c := 0)).congr_of_eventuallyEq
  filter_upwards [(trivializationAt F E x).open_baseSet.mem_nhds
    (mem_baseSet_trivializationAt F E x)] with y hy
    using congr_arg Prod.snd <| (trivializationAt F E x).zeroSection ùïú hy
#align bundle.smooth_zero_section Bundle.smooth_zeroSection

end Bundle

end

/-! ### Smooth vector bundles -/


variable [NontriviallyNormedField ùïú] {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ùïú EB]
  {HB : Type*} [TopologicalSpace HB] (IB : ModelWithCorners ùïú EB HB) [TopologicalSpace B]
  [ChartedSpace HB B] [SmoothManifoldWithCorners IB B] {EM : Type*} [NormedAddCommGroup EM]
  [NormedSpace ùïú EM] {HM : Type*} [TopologicalSpace HM] {IM : ModelWithCorners ùïú EM HM}
  [TopologicalSpace M] [ChartedSpace HM M] [Is : SmoothManifoldWithCorners IM M] {n : ‚Ñï‚àû}
  [‚àÄ x, AddCommMonoid (E x)] [‚àÄ x, Module ùïú (E x)] [NormedAddCommGroup F] [NormedSpace ùïú F]

section WithTopology

variable [TopologicalSpace (TotalSpace F E)] [‚àÄ x, TopologicalSpace (E x)] (F E)

variable [FiberBundle F E] [VectorBundle ùïú F E]

/-- When `B` is a smooth manifold with corners with respect to a model `IB` and `E` is a
topological vector bundle over `B` with fibers isomorphic to `F`, then `SmoothVectorBundle F E IB`
registers that the bundle is smooth, in the sense of having smooth transition functions.
This is a mixin, not carrying any new data. -/
class SmoothVectorBundle : Prop where
  protected smoothOn_coordChangeL :
    ‚àÄ (e e' : Trivialization F (œÄ F E)) [MemTrivializationAtlas e] [MemTrivializationAtlas e'],
      SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun b : B => (e.coordChangeL ùïú e' b : F ‚ÜíL[ùïú] F))
        (e.baseSet ‚à© e'.baseSet)
#align smooth_vector_bundle SmoothVectorBundle
#align smooth_vector_bundle.smooth_on_coord_change SmoothVectorBundle.smoothOn_coordChangeL

variable [SmoothVectorBundle F E IB]

section SmoothCoordChange

variable {F E}
variable (e e' : Trivialization F (œÄ F E)) [MemTrivializationAtlas e] [MemTrivializationAtlas e']

theorem smoothOn_coordChangeL :
    SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun b : B => (e.coordChangeL ùïú e' b : F ‚ÜíL[ùïú] F))
      (e.baseSet ‚à© e'.baseSet) :=
  SmoothVectorBundle.smoothOn_coordChangeL e e'

theorem smoothOn_symm_coordChangeL :
    SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun b : B => ((e.coordChangeL ùïú e' b).symm : F ‚ÜíL[ùïú] F))
      (e.baseSet ‚à© e'.baseSet) := by
  rw [inter_comm]
  refine (SmoothVectorBundle.smoothOn_coordChangeL e' e).congr fun b hb ‚Ü¶ ?_
  rw [e.symm_coordChangeL e' hb]

theorem contMDiffOn_coordChangeL :
    ContMDiffOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun b : B => (e.coordChangeL ùïú e' b : F ‚ÜíL[ùïú] F))
      (e.baseSet ‚à© e'.baseSet) :=
  (smoothOn_coordChangeL IB e e').of_le le_top

theorem contMDiffOn_symm_coordChangeL :
    ContMDiffOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun b : B => ((e.coordChangeL ùïú e' b).symm : F ‚ÜíL[ùïú] F))
      (e.baseSet ‚à© e'.baseSet) :=
  (smoothOn_symm_coordChangeL IB e e').of_le le_top

variable {e e'}

theorem contMDiffAt_coordChangeL (h : x ‚àà e.baseSet) (h' : x ‚àà e'.baseSet) :
    ContMDiffAt IB ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun b : B => (e.coordChangeL ùïú e' b : F ‚ÜíL[ùïú] F)) x :=
  (contMDiffOn_coordChangeL IB e e').contMDiffAt <|
    (e.open_baseSet.inter e'.open_baseSet).mem_nhds ‚ü®h, h'‚ü©

theorem smoothAt_coordChangeL (h : x ‚àà e.baseSet) (h' : x ‚àà e'.baseSet) :
    SmoothAt IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun b : B => (e.coordChangeL ùïú e' b : F ‚ÜíL[ùïú] F)) x :=
  contMDiffAt_coordChangeL IB h h'

variable {IB}

protected theorem ContMDiffWithinAt.coordChangeL {f : M ‚Üí B}
    (hf : ContMDiffWithinAt IM IB n f s x) (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    ContMDiffWithinAt IM ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) s x :=
  (contMDiffAt_coordChangeL IB he he').comp_contMDiffWithinAt _ hf

protected nonrec theorem ContMDiffAt.coordChangeL {f : M ‚Üí B}
    (hf : ContMDiffAt IM IB n f x) (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    ContMDiffAt IM ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) x :=
  hf.coordChangeL he he'

protected theorem ContMDiffOn.coordChangeL {f : M ‚Üí B}
    (hf : ContMDiffOn IM IB n f s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    ContMDiffOn IM ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) s :=
  fun x hx ‚Ü¶ (hf x hx).coordChangeL (he hx) (he' hx)

protected theorem ContMDiff.coordChangeL {f : M ‚Üí B}
    (hf : ContMDiff IM IB n f) (he : ‚àÄ x, f x ‚àà e.baseSet) (he' : ‚àÄ x, f x ‚àà e'.baseSet) :
    ContMDiff IM ùìò(ùïú, F ‚ÜíL[ùïú] F) n (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) := fun x ‚Ü¶
  (hf x).coordChangeL (he x) (he' x)

protected nonrec theorem SmoothWithinAt.coordChangeL {f : M ‚Üí B}
    (hf : SmoothWithinAt IM IB f s x) (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    SmoothWithinAt IM ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) s x :=
  hf.coordChangeL he he'

protected nonrec theorem SmoothAt.coordChangeL {f : M ‚Üí B}
    (hf : SmoothAt IM IB f x) (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    SmoothAt IM ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) x :=
  hf.coordChangeL he he'

protected nonrec theorem SmoothOn.coordChangeL {f : M ‚Üí B}
    (hf : SmoothOn IM IB f s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    SmoothOn IM ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) s :=
  hf.coordChangeL he he'

protected nonrec theorem Smooth.coordChangeL {f : M ‚Üí B}
    (hf : Smooth IM IB f) (he : ‚àÄ x, f x ‚àà e.baseSet) (he' : ‚àÄ x, f x ‚àà e'.baseSet) :
    Smooth IM ùìò(ùïú, F ‚ÜíL[ùïú] F) (fun y ‚Ü¶ (e.coordChangeL ùïú e' (f y) : F ‚ÜíL[ùïú] F)) :=
  hf.coordChangeL he he'

protected theorem ContMDiffWithinAt.coordChange {f : M ‚Üí B} {g : M ‚Üí F}
    (hf : ContMDiffWithinAt IM IB n f s x) (hg : ContMDiffWithinAt IM ùìò(ùïú, F) n g s x)
    (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    ContMDiffWithinAt IM ùìò(ùïú, F) n (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) s x := by
  refine ((hf.coordChangeL he he').clm_apply hg).congr_of_eventuallyEq ?_ ?_
  ¬∑ have : e.baseSet ‚à© e'.baseSet ‚àà ùìù (f x) :=
     (e.open_baseSet.inter e'.open_baseSet).mem_nhds ‚ü®he, he'‚ü©
    filter_upwards [hf.continuousWithinAt this] with y hy
    exact (Trivialization.coordChangeL_apply' e e' hy (g y)).symm
  ¬∑ exact (Trivialization.coordChangeL_apply' e e' ‚ü®he, he'‚ü© (g x)).symm

protected nonrec theorem ContMDiffAt.coordChange {f : M ‚Üí B} {g : M ‚Üí F}
    (hf : ContMDiffAt IM IB n f x) (hg : ContMDiffAt IM ùìò(ùïú, F) n g x) (he : f x ‚àà e.baseSet)
    (he' : f x ‚àà e'.baseSet) :
    ContMDiffAt IM ùìò(ùïú, F) n (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) x :=
  hf.coordChange hg he he'

protected theorem ContMDiffOn.coordChange {f : M ‚Üí B} {g : M ‚Üí F} (hf : ContMDiffOn IM IB n f s)
    (hg : ContMDiffOn IM ùìò(ùïú, F) n g s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    ContMDiffOn IM ùìò(ùïú, F) n (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) s := fun x hx ‚Ü¶
  (hf x hx).coordChange (hg x hx) (he hx) (he' hx)

protected theorem ContMDiff.coordChange {f : M ‚Üí B} {g : M ‚Üí F} (hf : ContMDiff IM IB n f)
    (hg : ContMDiff IM ùìò(ùïú, F) n g) (he : ‚àÄ x, f x ‚àà e.baseSet) (he' : ‚àÄ x, f x ‚àà e'.baseSet) :
    ContMDiff IM ùìò(ùïú, F) n (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) := fun x ‚Ü¶
  (hf x).coordChange (hg x) (he x) (he' x)

protected nonrec theorem SmoothWithinAt.coordChange {f : M ‚Üí B} {g : M ‚Üí F}
    (hf : SmoothWithinAt IM IB f s x) (hg : SmoothWithinAt IM ùìò(ùïú, F) g s x)
    (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    SmoothWithinAt IM ùìò(ùïú, F) (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) s x :=
  hf.coordChange hg he he'

protected nonrec theorem SmoothAt.coordChange {f : M ‚Üí B} {g : M ‚Üí F} (hf : SmoothAt IM IB f x)
    (hg : SmoothAt IM ùìò(ùïú, F) g x) (he : f x ‚àà e.baseSet) (he' : f x ‚àà e'.baseSet) :
    SmoothAt IM ùìò(ùïú, F) (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) x :=
  hf.coordChange hg he he'

protected nonrec theorem SmoothOn.coordChange {f : M ‚Üí B} {g : M ‚Üí F} (hf : SmoothOn IM IB f s)
    (hg : SmoothOn IM ùìò(ùïú, F) g s) (he : MapsTo f s e.baseSet) (he' : MapsTo f s e'.baseSet) :
    SmoothOn IM ùìò(ùïú, F) (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) s :=
  hf.coordChange hg he he'

protected theorem Smooth.coordChange {f : M ‚Üí B} {g : M ‚Üí F} (hf : Smooth IM IB f)
    (hg : Smooth IM ùìò(ùïú, F) g) (he : ‚àÄ x, f x ‚àà e.baseSet) (he' : ‚àÄ x, f x ‚àà e'.baseSet) :
    Smooth IM ùìò(ùïú, F) (fun y ‚Ü¶ e.coordChange e' (f y) (g y)) := fun x ‚Ü¶
  (hf x).coordChange (hg x) (he x) (he' x)

variable (IB e e')

theorem Trivialization.contMDiffOn_symm_trans :
    ContMDiffOn (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) n
      (e.toLocalHomeomorph.symm ‚â´‚Çï e'.toLocalHomeomorph) (e.target ‚à© e'.target) := by
  have Hmaps : MapsTo Prod.fst (e.target ‚à© e'.target) (e.baseSet ‚à© e'.baseSet) := fun x hx ‚Ü¶
    ‚ü®e.mem_target.1 hx.1, e'.mem_target.1 hx.2‚ü©
  rw [mapsTo_inter] at Hmaps
  -- TODO: drop `congr` #5473
  refine (contMDiffOn_fst.prod_mk
    (contMDiffOn_fst.coordChange contMDiffOn_snd Hmaps.1 Hmaps.2)).congr ?_
  rintro ‚ü®b, x‚ü© hb
  refine Prod.ext ?_ rfl
  ¬∑ have : (e.toLocalHomeomorph.symm (b, x)).1 ‚àà e'.baseSet
    ¬∑ simp_all only [Trivialization.mem_target, mfld_simps]
    exact (e'.coe_fst' this).trans (e.proj_symm_apply hb.1)

variable {IB e e'}

theorem ContMDiffWithinAt.change_section_trivialization {f : M ‚Üí TotalSpace F E}
    (hp : ContMDiffWithinAt IM IB n (œÄ F E ‚àò f) s x)
    (hf : ContMDiffWithinAt IM ùìò(ùïú, F) n (fun y ‚Ü¶ (e (f y)).2) s x)
    (he : f x ‚àà e.source) (he' : f x ‚àà e'.source) :
    ContMDiffWithinAt IM ùìò(ùïú, F) n (fun y ‚Ü¶ (e' (f y)).2) s x := by
  rw [Trivialization.mem_source] at he he'
  refine (hp.coordChange hf he he').congr_of_eventuallyEq ?_ ?_
  ¬∑ filter_upwards [hp.continuousWithinAt (e.open_baseSet.mem_nhds he)] with y hy
    rw [Function.comp_apply, e.coordChange_apply_snd _ hy]
  ¬∑ rw [Function.comp_apply, e.coordChange_apply_snd _ he]

theorem Trivialization.contMDiffWithinAt_snd_comp_iff‚ÇÇ {f : M ‚Üí TotalSpace F E}
    (hp : ContMDiffWithinAt IM IB n (œÄ F E ‚àò f) s x)
    (he : f x ‚àà e.source) (he' : f x ‚àà e'.source) :
    ContMDiffWithinAt IM ùìò(ùïú, F) n (fun y ‚Ü¶ (e (f y)).2) s x ‚Üî
      ContMDiffWithinAt IM ùìò(ùïú, F) n (fun y ‚Ü¶ (e' (f y)).2) s x :=
  ‚ü®(hp.change_section_trivialization ¬∑ he he'), (hp.change_section_trivialization ¬∑ he' he)‚ü©

end SmoothCoordChange

/-- For a smooth vector bundle `E` over `B` with fiber modelled on `F`, the change-of-co-ordinates
between two trivializations `e`, `e'` for `E`, considered as charts to `B √ó F`, is smooth and
fiberwise linear. -/
instance SmoothFiberwiseLinear.hasGroupoid :
    HasGroupoid (TotalSpace F E) (smoothFiberwiseLinear B F IB) where
  compatible := by
    rintro _ _ ‚ü®e, he, rfl‚ü© ‚ü®e', he', rfl‚ü©
    haveI : MemTrivializationAtlas e := ‚ü®he‚ü©
    haveI : MemTrivializationAtlas e' := ‚ü®he'‚ü©
    rw [mem_smoothFiberwiseLinear_iff]
    refine' ‚ü®_, _, e.open_baseSet.inter e'.open_baseSet, smoothOn_coordChangeL IB e e',
      smoothOn_symm_coordChangeL IB e e', _‚ü©
    refine LocalHomeomorph.eqOnSourceSetoid.symm ‚ü®?_, ?_‚ü©
    ¬∑ simp only [e.symm_trans_source_eq e', FiberwiseLinear.localHomeomorph, trans_toLocalEquiv,
        symm_toLocalEquiv]
    ¬∑ rintro ‚ü®b, v‚ü© hb
      exact (e.apply_symm_apply_eq_coordChangeL e' hb.1 v).symm
#align smooth_fiberwise_linear.has_groupoid SmoothFiberwiseLinear.hasGroupoid

/-- A smooth vector bundle `E` is naturally a smooth manifold. -/
instance Bundle.TotalSpace.smoothManifoldWithCorners :
    SmoothManifoldWithCorners (IB.prod ùìò(ùïú, F)) (TotalSpace F E) := by
  refine' { StructureGroupoid.HasGroupoid.comp (smoothFiberwiseLinear B F IB) _ with }
  intro e he
  rw [mem_smoothFiberwiseLinear_iff] at he
  obtain ‚ü®œÜ, U, hU, hœÜ, h2œÜ, heœÜ‚ü© := he
  rw [isLocalStructomorphOn_contDiffGroupoid_iff]
  refine' ‚ü®ContMDiffOn.congr _ (EqOnSource.eqOn heœÜ),
      ContMDiffOn.congr _ (EqOnSource.eqOn (EqOnSource.symm' heœÜ))‚ü©
  ¬∑ rw [EqOnSource.source_eq heœÜ]
    apply smoothOn_fst.prod_mk
    exact (hœÜ.comp contMDiffOn_fst <| prod_subset_preimage_fst _ _).clm_apply contMDiffOn_snd
  ¬∑ rw [EqOnSource.target_eq heœÜ]
    apply smoothOn_fst.prod_mk
    exact (h2œÜ.comp contMDiffOn_fst <| prod_subset_preimage_fst _ _).clm_apply contMDiffOn_snd
#align bundle.total_space.smooth_manifold_with_corners Bundle.TotalSpace.smoothManifoldWithCorners

section

variable {F E}
variable {e e' : Trivialization F (œÄ F E)} [MemTrivializationAtlas e] [MemTrivializationAtlas e']

theorem Trivialization.contMDiffWithinAt_iff {f : M ‚Üí TotalSpace F E} {s : Set M} {x‚ÇÄ : M}
    (he : f x‚ÇÄ ‚àà e.source) :
    ContMDiffWithinAt IM (IB.prod ùìò(ùïú, F)) n f s x‚ÇÄ ‚Üî
      ContMDiffWithinAt IM IB n (fun x => (f x).proj) s x‚ÇÄ ‚àß
      ContMDiffWithinAt IM ùìò(ùïú, F) n (fun x ‚Ü¶ (e (f x)).2) s x‚ÇÄ :=
  (contMDiffWithinAt_totalSpace _).trans <| and_congr_right fun h ‚Ü¶
    Trivialization.contMDiffWithinAt_snd_comp_iff‚ÇÇ h FiberBundle.mem_trivializationAt_proj_source he

theorem Trivialization.contMDiffAt_iff {f : M ‚Üí TotalSpace F E} {x‚ÇÄ : M} (he : f x‚ÇÄ ‚àà e.source) :
    ContMDiffAt IM (IB.prod ùìò(ùïú, F)) n f x‚ÇÄ ‚Üî
      ContMDiffAt IM IB n (fun x => (f x).proj) x‚ÇÄ ‚àß
      ContMDiffAt IM ùìò(ùïú, F) n (fun x ‚Ü¶ (e (f x)).2) x‚ÇÄ :=
  e.contMDiffWithinAt_iff _ he

theorem Trivialization.contMDiffOn_iff {f : M ‚Üí TotalSpace F E} {s : Set M}
    (he : MapsTo f s e.source) :
    ContMDiffOn IM (IB.prod ùìò(ùïú, F)) n f s ‚Üî
      ContMDiffOn IM IB n (fun x => (f x).proj) s ‚àß
      ContMDiffOn IM ùìò(ùïú, F) n (fun x ‚Ü¶ (e (f x)).2) s := by
  simp only [ContMDiffOn, ‚Üê forall_and]
  exact forall‚ÇÇ_congr fun x hx ‚Ü¶ e.contMDiffWithinAt_iff IB (he hx)

theorem Trivialization.contMDiff_iff {f : M ‚Üí TotalSpace F E} (he : ‚àÄ x, f x ‚àà e.source) :
    ContMDiff IM (IB.prod ùìò(ùïú, F)) n f ‚Üî
      ContMDiff IM IB n (fun x => (f x).proj) ‚àß
      ContMDiff IM ùìò(ùïú, F) n (fun x ‚Ü¶ (e (f x)).2) :=
  (forall_congr' fun x ‚Ü¶ e.contMDiffAt_iff IB (he x)).trans forall_and

theorem Trivialization.smoothWithinAt_iff {f : M ‚Üí TotalSpace F E} {s : Set M} {x‚ÇÄ : M}
    (he : f x‚ÇÄ ‚àà e.source) :
    SmoothWithinAt IM (IB.prod ùìò(ùïú, F)) f s x‚ÇÄ ‚Üî
      SmoothWithinAt IM IB (fun x => (f x).proj) s x‚ÇÄ ‚àß
      SmoothWithinAt IM ùìò(ùïú, F) (fun x ‚Ü¶ (e (f x)).2) s x‚ÇÄ :=
  e.contMDiffWithinAt_iff IB he

theorem Trivialization.smoothAt_iff {f : M ‚Üí TotalSpace F E} {x‚ÇÄ : M} (he : f x‚ÇÄ ‚àà e.source) :
    SmoothAt IM (IB.prod ùìò(ùïú, F)) f x‚ÇÄ ‚Üî
      SmoothAt IM IB (fun x => (f x).proj) x‚ÇÄ ‚àß SmoothAt IM ùìò(ùïú, F) (fun x ‚Ü¶ (e (f x)).2) x‚ÇÄ :=
  e.contMDiffAt_iff IB he

theorem Trivialization.smoothOn_iff {f : M ‚Üí TotalSpace F E} {s : Set M}
    (he : MapsTo f s e.source) :
    SmoothOn IM (IB.prod ùìò(ùïú, F)) f s ‚Üî
      SmoothOn IM IB (fun x => (f x).proj) s ‚àß SmoothOn IM ùìò(ùïú, F) (fun x ‚Ü¶ (e (f x)).2) s :=
  e.contMDiffOn_iff IB he

theorem Trivialization.smooth_iff {f : M ‚Üí TotalSpace F E} (he : ‚àÄ x, f x ‚àà e.source) :
    Smooth IM (IB.prod ùìò(ùïú, F)) f ‚Üî
      Smooth IM IB (fun x => (f x).proj) ‚àß Smooth IM ùìò(ùïú, F) (fun x ‚Ü¶ (e (f x)).2) :=
  e.contMDiff_iff IB he

theorem Trivialization.smoothOn (e : Trivialization F (œÄ F E)) [MemTrivializationAtlas e] :
    SmoothOn (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) e e.source := by
  have : SmoothOn (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) id e.source := smoothOn_id
  rw [e.smoothOn_iff IB (mapsTo_id _)] at this
  exact (this.1.prod_mk this.2).congr fun x hx ‚Ü¶ (e.mk_proj_snd hx).symm

theorem Trivialization.smoothOn_symm (e : Trivialization F (œÄ F E)) [MemTrivializationAtlas e] :
    SmoothOn (IB.prod ùìò(ùïú, F)) (IB.prod ùìò(ùïú, F)) e.toLocalHomeomorph.symm e.target := by
  rw [e.smoothOn_iff IB e.toLocalHomeomorph.symm_mapsTo]
  refine ‚ü®smoothOn_fst.congr fun x hx ‚Ü¶ e.proj_symm_apply hx, smoothOn_snd.congr fun x hx ‚Ü¶ ?_‚ü©
  rw [e.apply_symm_apply hx]

end

/-! ### Core construction for smooth vector bundles -/

namespace VectorBundleCore

variable {Œπ : Type*} {F}
variable (Z : VectorBundleCore ùïú B F Œπ)

/-- Mixin for a `VectorBundleCore` stating smoothness (of transition functions). -/
class IsSmooth (IB : ModelWithCorners ùïú EB HB) : Prop where
  smoothOn_coordChange :
    ‚àÄ i j, SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (Z.coordChange i j) (Z.baseSet i ‚à© Z.baseSet j)
#align vector_bundle_core.is_smooth VectorBundleCore.IsSmooth

theorem smoothOn_coordChange (IB : ModelWithCorners ùïú EB HB) [h : Z.IsSmooth IB] (i j : Œπ) :
    SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (Z.coordChange i j) (Z.baseSet i ‚à© Z.baseSet j) :=
  h.1 i j

variable [Z.IsSmooth IB]

/-- If a `VectorBundleCore` has the `IsSmooth` mixin, then the vector bundle constructed from it
is a smooth vector bundle. -/
instance smoothVectorBundle : SmoothVectorBundle F Z.Fiber IB where
  smoothOn_coordChangeL := by
    rintro - - ‚ü®i, rfl‚ü© ‚ü®i', rfl‚ü©
    -- Porting note: Originally `Z.smoothOn_coordChange IB i i'`
    refine'
      (VectorBundleCore.IsSmooth.smoothOn_coordChange (Z := Z) (IB := IB) i i').congr fun b hb => _
    ext v
    exact Z.localTriv_coordChange_eq i i' hb v
#align vector_bundle_core.smooth_vector_bundle VectorBundleCore.smoothVectorBundle

end VectorBundleCore

/-! ### The trivial smooth vector bundle -/

/-- A trivial vector bundle over a smooth manifold is a smooth vector bundle. -/
instance Bundle.Trivial.smoothVectorBundle : SmoothVectorBundle F (Bundle.Trivial B F) IB where
  smoothOn_coordChangeL := by
    intro e e' he he'
    obtain rfl := Bundle.Trivial.eq_trivialization B F e
    obtain rfl := Bundle.Trivial.eq_trivialization B F e'
    simp_rw [Bundle.Trivial.trivialization.coordChangeL]
    exact smooth_const.smoothOn
#align bundle.trivial.smooth_vector_bundle Bundle.Trivial.smoothVectorBundle

/-! ### Direct sums of smooth vector bundles -/


section Prod

variable (F‚ÇÅ : Type*) [NormedAddCommGroup F‚ÇÅ] [NormedSpace ùïú F‚ÇÅ] (E‚ÇÅ : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F‚ÇÅ E‚ÇÅ)] [‚àÄ x, AddCommMonoid (E‚ÇÅ x)] [‚àÄ x, Module ùïú (E‚ÇÅ x)]

variable (F‚ÇÇ : Type*) [NormedAddCommGroup F‚ÇÇ] [NormedSpace ùïú F‚ÇÇ] (E‚ÇÇ : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F‚ÇÇ E‚ÇÇ)] [‚àÄ x, AddCommMonoid (E‚ÇÇ x)] [‚àÄ x, Module ùïú (E‚ÇÇ x)]

variable [‚àÄ x : B, TopologicalSpace (E‚ÇÅ x)] [‚àÄ x : B, TopologicalSpace (E‚ÇÇ x)] [FiberBundle F‚ÇÅ E‚ÇÅ]
  [FiberBundle F‚ÇÇ E‚ÇÇ] [VectorBundle ùïú F‚ÇÅ E‚ÇÅ] [VectorBundle ùïú F‚ÇÇ E‚ÇÇ] [SmoothVectorBundle F‚ÇÅ E‚ÇÅ IB]
  [SmoothVectorBundle F‚ÇÇ E‚ÇÇ IB]

/-- The direct sum of two smooth vector bundles over the same base is a smooth vector bundle. -/
instance Bundle.Prod.smoothVectorBundle : SmoothVectorBundle (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) IB where
  smoothOn_coordChangeL := by
    rintro _ _ ‚ü®e‚ÇÅ, e‚ÇÇ, i‚ÇÅ, i‚ÇÇ, rfl‚ü© ‚ü®e‚ÇÅ', e‚ÇÇ', i‚ÇÅ', i‚ÇÇ', rfl‚ü©
    rw [SmoothOn]
    refine' ContMDiffOn.congr _ (e‚ÇÅ.coordChangeL_prod ùïú e‚ÇÅ' e‚ÇÇ e‚ÇÇ')
    refine' ContMDiffOn.clm_prodMap _ _
    ¬∑ refine' (smoothOn_coordChangeL IB e‚ÇÅ e‚ÇÅ').mono _
      simp only [Trivialization.baseSet_prod, mfld_simps]
      mfld_set_tac
    ¬∑ refine' (smoothOn_coordChangeL IB e‚ÇÇ e‚ÇÇ').mono _
      simp only [Trivialization.baseSet_prod, mfld_simps]
      mfld_set_tac
#align bundle.prod.smooth_vector_bundle Bundle.Prod.smoothVectorBundle

end Prod

end WithTopology

/-! ### Prebundle construction for smooth vector bundles -/

namespace VectorPrebundle

variable [‚àÄ x, TopologicalSpace (E x)]

/-- Mixin for a `VectorPrebundle` stating smoothness of coordinate changes. -/
class IsSmooth (a : VectorPrebundle ùïú F E) : Prop where
  exists_smoothCoordChange :
    ‚àÄ·µâ (e ‚àà a.pretrivializationAtlas) (e' ‚àà a.pretrivializationAtlas),
      ‚àÉ f : B ‚Üí F ‚ÜíL[ùïú] F,
        SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) f (e.baseSet ‚à© e'.baseSet) ‚àß
          ‚àÄ (b : B) (_ : b ‚àà e.baseSet ‚à© e'.baseSet) (v : F),
            f b v = (e' ‚ü®b, e.symm b v‚ü©).2
#align vector_prebundle.is_smooth VectorPrebundle.IsSmooth

variable (a : VectorPrebundle ùïú F E) [ha : a.IsSmooth IB] {e e' : Pretrivialization F (œÄ F E)}

/-- A randomly chosen coordinate change on a `SmoothVectorPrebundle`, given by
  the field `exists_coordChange`. Note that `a.smoothCoordChange` need not be the same as
  `a.coordChange`. -/
noncomputable def smoothCoordChange (he : e ‚àà a.pretrivializationAtlas)
    (he' : e' ‚àà a.pretrivializationAtlas) (b : B) : F ‚ÜíL[ùïú] F :=
  Classical.choose (ha.exists_smoothCoordChange e he e' he') b
#align vector_prebundle.smooth_coord_change VectorPrebundle.smoothCoordChange

variable {IB}

theorem smoothOn_smoothCoordChange (he : e ‚àà a.pretrivializationAtlas)
    (he' : e' ‚àà a.pretrivializationAtlas) :
    SmoothOn IB ùìò(ùïú, F ‚ÜíL[ùïú] F) (a.smoothCoordChange IB he he') (e.baseSet ‚à© e'.baseSet) :=
  (Classical.choose_spec (ha.exists_smoothCoordChange e he e' he')).1
#align vector_prebundle.smooth_on_smooth_coord_change VectorPrebundle.smoothOn_smoothCoordChange

theorem smoothCoordChange_apply (he : e ‚àà a.pretrivializationAtlas)
    (he' : e' ‚àà a.pretrivializationAtlas) {b : B} (hb : b ‚àà e.baseSet ‚à© e'.baseSet) (v : F) :
    a.smoothCoordChange IB he he' b v = (e' ‚ü®b, e.symm b v‚ü©).2 :=
  (Classical.choose_spec (ha.exists_smoothCoordChange e he e' he')).2 b hb v
#align vector_prebundle.smooth_coord_change_apply VectorPrebundle.smoothCoordChange_apply

theorem mk_smoothCoordChange (he : e ‚àà a.pretrivializationAtlas)
    (he' : e' ‚àà a.pretrivializationAtlas) {b : B} (hb : b ‚àà e.baseSet ‚à© e'.baseSet) (v : F) :
    (b, a.smoothCoordChange IB he he' b v) = e' ‚ü®b, e.symm b v‚ü© := by
  ext
  ¬∑ rw [e.mk_symm hb.1 v, e'.coe_fst', e.proj_symm_apply' hb.1]
    rw [e.proj_symm_apply' hb.1]; exact hb.2
  ¬∑ exact a.smoothCoordChange_apply he he' hb v
#align vector_prebundle.mk_smooth_coord_change VectorPrebundle.mk_smoothCoordChange

variable (IB)
/-- Make a `SmoothVectorBundle` from a `SmoothVectorPrebundle`. -/
theorem smoothVectorBundle : @SmoothVectorBundle
    _ _ F E _ _ _ _ _ _ IB _ _ _ _ _ _ a.totalSpaceTopology _ a.toFiberBundle a.toVectorBundle :=
  letI := a.totalSpaceTopology; letI := a.toFiberBundle; letI := a.toVectorBundle
  { smoothOn_coordChangeL := by
      rintro _ _ ‚ü®e, he, rfl‚ü© ‚ü®e', he', rfl‚ü©
      refine' (a.smoothOn_smoothCoordChange he he').congr _
      intro b hb
      ext v
      rw [a.smoothCoordChange_apply he he' hb v, ContinuousLinearEquiv.coe_coe,
        Trivialization.coordChangeL_apply]
      exacts [rfl, hb] }
#align vector_prebundle.smooth_vector_bundle VectorPrebundle.smoothVectorBundle

end VectorPrebundle
