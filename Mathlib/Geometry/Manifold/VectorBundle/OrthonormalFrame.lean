/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Michael Rothgang
-/
import Mathlib.Geometry.Manifold.VectorBundle.GramSchmidtOrtho
import Mathlib.Geometry.Manifold.VectorBundle.LocalFrame

/-!
# Existence of orthonormal frames on Riemannian vector bundles

In this file, we prove that a Riemannian vector bundle has orthonormal frames near each point.
These are constructed by taking any local frame, and applying Gram-Schmidt orthonormalisation
to it (point-wise). If the bundle metric is `C^k`, the resulting orthonormal frame also is.

TODO: add main results, etc

## Implementation note

Like local frames, orthonormal frames use the junk value pattern: their value is meaningless
outside of the `baseSet` of the trivialisation used to define them.

## Tags
vector bundle, local frame, smoothness

-/

open Manifold Bundle ContinuousLinearMap ENat Bornology
open scoped ContDiff Topology

-- Let `V` be a smooth vector bundle with a `C^n` Riemannian structure over a `C^k` manifold `B`.
variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ‚Ñù EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners ‚Ñù EB HB}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  {E : B ‚Üí Type*} [TopologicalSpace (TotalSpace F E)] [‚àÄ x, NormedAddCommGroup (E x)]
  [‚àÄ x, InnerProductSpace ‚Ñù (E x)] [FiberBundle F E] [VectorBundle ‚Ñù F E] {n : WithTop ‚Ñï‚àû}
  [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
  [IsContMDiffRiemannianBundle IB n F E]

local notation "‚ü™" x ", " y "‚ü´" => inner ‚Ñù x y

variable {Œπ : Type*} [LinearOrder Œπ] [LocallyFiniteOrderBot Œπ] [WellFoundedLT Œπ]

variable {s : Œπ ‚Üí (x : B) ‚Üí E x} {u u' : Set B}

variable (IB F n) in
structure IsOrthonormalFrameOn (s : Œπ ‚Üí (x : B) ‚Üí E x) (u : Set B)
    extends IsLocalFrameOn IB F n s u where
  orthonormal {x} : x ‚àà u ‚Üí Orthonormal ‚Ñù (s ¬∑ x)
  -- /-- Any two distinct sections are point-wise orthogonal on `u`. -/
  -- orthogonal {i j : Œπ} {x : B} : i ‚â† j ‚Üí x ‚àà u ‚Üí ‚ü™s i x, s j x‚ü´ = 0
  -- normalised {i : Œπ} {x : B} : x ‚àà u ‚Üí ‚Äñs i x‚Äñ = 1

omit [VectorBundle ‚Ñù F E] [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
  [IsContMDiffRiemannianBundle IB n F E]
  [LinearOrder Œπ] [LocallyFiniteOrderBot Œπ] [WellFoundedLT Œπ] in
lemma IsOrthonormalFrameOn.mono (hs : IsOrthonormalFrameOn IB F n s u) (huu' : u' ‚äÜ u) :
    IsOrthonormalFrameOn IB F n s u' where
  toIsLocalFrameOn := hs.toIsLocalFrameOn.mono huu'
  orthonormal hx := hs.orthonormal (huu' hx)

/-- Applying the Gram-Schmidt procedure to a local frame yields an orthonormal local frame. -/
def IsLocalFrameOn.gramSchmidtNormed (hs : IsLocalFrameOn IB F n s u) :
    IsOrthonormalFrameOn IB F n (VectorBundle.gramSchmidtNormed s) u where
  linearIndependent := by
    intro x hx
    exact VectorBundle.gramSchmidtNormed_linearIndependent <| hs.linearIndependent hx
  generating := by
    intro x hx
    simpa only [VectorBundle.span_gramSchmidtNormed_range, VectorBundle.gramSchmidt_apply,
      InnerProductSpace.span_gramSchmidt] using hs.generating hx
  contMDiffOn i := gramSchmidtNormed_contMDiffOn (fun i ‚Ü¶ hs.contMDiffOn i) <|
      fun x hx ‚Ü¶ (hs.linearIndependent hx).comp _ Subtype.val_injective
  orthonormal hx := (VectorBundle.gramSchmidtNormed_orthonormal (hs.linearIndependent hx))

-- XXX: is this one necessary?
/-- Applying the normalised Gram-Schmidt procedure to an orthonormal local frame yields
another orthonormal local frame. -/
def IsOrthonormalFrameOn.gramSchmidtNormed (hs : IsOrthonormalFrameOn IB F n s u) :
    IsOrthonormalFrameOn IB F n (VectorBundle.gramSchmidtNormed s) u :=
  hs.toIsLocalFrameOn.gramSchmidtNormed

/-! # Determining smoothness of a section via its local frame coefficients

We show that for any orthogonal local frame `{s i}` on `u ‚äÜ B`, a section `t` is smooth on `u`
if and only if its coefficients in `{s i}` are. This argument crucially depends on the existence of
a smooth bundle metric on `V` (which always holds in finite dimensions, and in certain important
infinite-dimensional cases).

See `LocalFrame.lean` for a similar statement, about local frames induced by a local trivialisation
on finite rank bundles over any complete field.

The orthogonality requirement can be removed, with additional technical effort: see the comment
below for details. It simplifies the proofs as the local frame coefficients take a particularly
simple form in orthgonal frames.
-/

section smoothness

namespace IsOrthonormalFrameOn

omit [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
variable [Fintype Œπ]

variable (hs : IsOrthonormalFrameOn IB F n s u) {t : (x : B) ‚Üí E x} {x : B}

omit [VectorBundle ‚Ñù F E] [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
  [IsContMDiffRiemannianBundle IB n F E] in
variable (t) in
lemma coeff_eq_inner' (hs : IsOrthonormalFrameOn IB F n s u) (hx : x ‚àà u) (i : Œπ) :
    hs.coeff i t x = ‚ü™s i x, t x‚ü´ := by
  let b := VectorBundle.gramSchmidtOrthonormalBasis (hs.linearIndependent hx) (hs.generating hx)
  have beq (i : Œπ) : b i = s i x := by
    simp [b, VectorBundle.gramSchmidtNormed_apply_of_orthonormal (hs.orthonormal hx) i]
  have heq' : b.toBasis = hs.toBasisAt hx := by
    ext i
    simp [b, VectorBundle.gramSchmidtNormed_apply_of_orthonormal (hs.orthonormal hx) i]
  have aux := b.repr_apply_apply (t x) i
  rw [beq] at aux
  simp [‚Üê aux, IsLocalFrameOn.coeff, hx, ‚Üê heq']

-- This lemma would hold more generally for an *orthogonal frame*.
-- variable (t) in
-- lemma coeff_eq_inner (hs : IsOrthonormalFrameOn IB F n s u) (hx : x ‚àà u) (i : Œπ) :
--     hs.coeff i t x = ‚ü™s i x, t x‚ü´ / (‚Äñs i x‚Äñ ^ 2) := by
--   sorry -- need a version of b.repr_apply_apply for *orthogonal* bases

/-- If `t` is `C^k` at `x`, so is its coefficient `hs.coeff i t` in a local frame s near `x` -/
lemma contMDiffWithinAt_coeff (ht : CMDiffAt[u] n (T% t) x) (hx : x ‚àà u) (i : Œπ) :
    CMDiffAt[u] n (hs.coeff i t) x :=
  ((hs.contMDiffOn i x hx).inner_bundle ht).congr_of_mem (fun _ hy ‚Ü¶ hs.coeff_eq_inner' _ hy _) hx

omit [IsManifold IB n B] [ContMDiffVectorBundle n F E IB] in
/-- If `t` is `C^k` at `x`, so is its coefficient `hs.coeff i t` in a local frame s near `x` -/
lemma contMDiffAt_coeff (hu : u ‚àà ùìù x) (ht : CMDiffAt n (T% t) x) (i : Œπ) :
    CMDiffAt n (hs.coeff i t) x :=
  (((hs.contMDiffOn i).contMDiffAt hu).inner_bundle ht).congr_of_eventuallyEq <|
    Filter.eventually_of_mem hu fun _ hx ‚Ü¶ hs.coeff_eq_inner' _ hx _

-- Future: prove the same result for all local frames
-- if `{s i}` is a local frame on `u`, and `{s' i}` are the corresponding orthogonalised frame,
-- for each `x ‚àà u` the vectors `{s i x}` and `{s' i x}` are bases of `E x`,
-- and the coefficients w.r.t. `s i` and `s' i x` are related by the base change matrix.
-- This matrix depends smoothly on `x`, hence the frame coefficients w.r.t. `{s i}` are smooth iff
-- those w.r.t. `{s' i}` are.

/-- If `{s i}` is an orthogonal local frame on a neighbourhood `u` of `x` and `t` is `C^k` on `u`,
so is its coefficient in the frame `{s i}`. -/
lemma contMDiffOn_coeff (ht : CMDiff[u] n (T% t)) (i : Œπ) : CMDiff[u] n (hs.coeff i t) :=
  fun x' hx ‚Ü¶ hs.contMDiffWithinAt_coeff (ht x' hx) hx _

/-- A section `s` of `V` is `C^k` at `x` iff each of its coefficients in an orthogonal
local frame near `x` is. -/
lemma contMDiffAt_iff_coeff (hu : u ‚àà ùìù x) :
    CMDiffAt n (T% t) x ‚Üî ‚àÄ i, CMDiffAt n (hs.coeff i t) x :=
  ‚ü®fun h i ‚Ü¶ hs.contMDiffAt_coeff hu h i, fun h ‚Ü¶ hs.contMDiffAt_of_coeff h hu‚ü©

/-- If `{s i}` is an orthogonal local frame on `s`, a section `s` of `V` is `C^k` on `u` iff
each of its coefficients `hs.coeff i s` w.r.t. the local frame `{s i}` is. -/
lemma contMDiffOn_iff_coeff : CMDiff[u] n (T% t) ‚Üî ‚àÄ i, CMDiff[u] n (hs.coeff i t) :=
  ‚ü®fun h i ‚Ü¶ hs.contMDiffOn_coeff h i, fun hi ‚Ü¶ hs.contMDiffOn_of_coeff hi‚ü©

-- unused, just stating for convenience/nice API
include hs in
lemma contMDiffAt_iff_coeff' (hu : u ‚àà ùìù x) :
    CMDiffAt n (T% t) x ‚Üî ‚àÄ i, CMDiffAt n (fun x ‚Ü¶ ‚ü™s i x, t x‚ü´) x := by
  rw [hs.contMDiffAt_iff_coeff hu]
  have (i : Œπ) := Filter.eventually_of_mem hu fun x hx ‚Ü¶ (hs.coeff_eq_inner' t hx i)
  exact ‚ü®fun h i ‚Ü¶ (h i).congr_of_eventuallyEq <| Filter.EventuallyEq.symm (this i),
    fun h i ‚Ü¶ (h i).congr_of_eventuallyEq (this i)‚ü©

-- unused, just stating for convenience/nice API
lemma contMDiffOn_iff_coeff' :
    CMDiff[u] n (T% t) ‚Üî ‚àÄ i, CMDiff[u] n (fun x ‚Ü¶ ‚ü™s i x, t x‚ü´) :=
  sorry -- similar to the above lemma

end IsOrthonormalFrameOn

end smoothness

namespace Module.Basis

variable {b : Basis Œπ ‚Ñù F}
    {e : Trivialization F (Bundle.TotalSpace.proj : Bundle.TotalSpace F E ‚Üí B)}
    [MemTrivializationAtlas e] {x : B} -- (hx : x ‚àà e.baseSet)

variable (b e) in
/-- The orthonormal frame associated to the basis `b` and the trivialisation `e`:
this is obtained by applying the Gram-Schmidt orthonormalisation procedure to `b.localFrame e`.
In particular, if x is outside of `e.baseSet`, this returns the junk value 0. -/
noncomputable def orthonormalFrame : Œπ ‚Üí (x : B) ‚Üí E x :=
  VectorBundle.gramSchmidtNormed (b.localFrame e)

omit [IsManifold IB n B] in
variable (b e) in
/-- An orthonormal frame w.r.t. a local trivialisation is an orthonormal local frame. -/
lemma orthonormalFrame_isOrthonormalFrameOn :
    IsOrthonormalFrameOn IB F n (b.orthonormalFrame e) e.baseSet :=
  (b.localFrame_isLocalFrameOn_baseSet IB n e).gramSchmidtNormed

omit [IsManifold IB n B] in
variable (b e) in
/-- Each orthonormal frame `s^i ‚àà Œì(E)` of a `C^k` vector bundle, defined by a local
trivialisation `e`, is `C^k` on `e.baseSet`. -/
lemma contMDiffOn_orthonormalFrame_baseSet (i : Œπ) :
    CMDiff[e.baseSet] n (T% (b.orthonormalFrame e i)) := by
  apply IsLocalFrameOn.contMDiffOn
  exact (b.orthonormalFrame_isOrthonormalFrameOn e).toIsLocalFrameOn

omit [IsManifold IB n B] in
variable (b e) in
lemma _root_.contMDiffAt_orthonormalFrame_of_mem (i : Œπ) {x : B} (hx : x ‚àà e.baseSet) :
    CMDiffAt n (T% (b.orthonormalFrame e i)) x :=
  -- bug: if I change this to a by apply, and put #check after the `by`, it works, but #check' fails
  -- #check' contMDiffOn_orthonormalFrame_baseSet
  (contMDiffOn_orthonormalFrame_baseSet b e i).contMDiffAt <| e.open_baseSet.mem_nhds hx

-- TODO: add more variants with mdifferentiable!
omit [ContMDiffVectorBundle n F E IB] [IsContMDiffRiemannianBundle IB n F E] in
variable [ContMDiffVectorBundle 1 F E IB] [IsContMDiffRiemannianBundle IB 1 F E] in
variable (b e) in
lemma _root_.mdifferentiableAt_orthonormalFrame_of_mem (i : Œπ) {x : B} (hx : x ‚àà e.baseSet) :
    MDiffAt (T% (b.orthonormalFrame e i)) x := by
  apply ContMDiffAt.mdifferentiableAt _ le_rfl
  exact (contMDiffOn_orthonormalFrame_baseSet b e i).contMDiffAt <| e.open_baseSet.mem_nhds hx

@[simp]
lemma orthonormalFrame_apply_of_notMem {i : Œπ} (hx : x ‚àâ e.baseSet) :
    b.orthonormalFrame e i x = 0 := by
  simp only [orthonormalFrame, VectorBundle.gramSchmidtNormed, InnerProductSpace.gramSchmidtNormed]
  simp
  convert InnerProductSpace.gramSchmidt_zero ‚Ñù i
  apply localFrame_apply_of_notMem e b hx

end Module.Basis
