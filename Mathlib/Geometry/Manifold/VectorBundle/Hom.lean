/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Geometry.Manifold.VectorBundle.Basic
import Mathlib.Topology.VectorBundle.Hom

/-! # Homs of `C^n` vector bundles over the same base space

Here we show that the bundle of continuous linear maps is a `C^n` vector bundle.

Note that we only do this for bundles of linear maps, not for bundles of arbitrary semilinear maps.
To do it for semilinear maps, we would need to generalize `ContinuousLinearMap.contMDiff`
(and `ContinuousLinearMap.contDiff`) to semilinear maps.
-/


noncomputable section

open Bundle Set PartialHomeomorph ContinuousLinearMap Pretrivialization

open scoped Manifold Bundle Topology

section

variable {ğ•œ B Fâ‚ Fâ‚‚ M : Type*} {n : WithTop â„•âˆ}
  {Eâ‚ : B â†’ Type*} {Eâ‚‚ : B â†’ Type*} [NontriviallyNormedField ğ•œ]
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, TopologicalSpace (Eâ‚ x)] [âˆ€ x, AddCommGroup (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, TopologicalSpace (Eâ‚‚ x)]
  {EB : Type*}
  [NormedAddCommGroup EB] [NormedSpace ğ•œ EB] {HB : Type*} [TopologicalSpace HB]
  {IB : ModelWithCorners ğ•œ EB HB} [TopologicalSpace B] [ChartedSpace HB B] {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚] {eâ‚ eâ‚' : Trivialization Fâ‚ (Ï€ Fâ‚ Eâ‚)}
  {eâ‚‚ eâ‚‚' : Trivialization Fâ‚‚ (Ï€ Fâ‚‚ Eâ‚‚)}

local notation "LEâ‚Eâ‚‚" => TotalSpace (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (fun (b : B) â†¦ Eâ‚ b â†’L[ğ•œ] Eâ‚‚ b)

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11083): moved slow parts to separate lemmas
theorem contMDiffOn_continuousLinearMapCoordChange
    [ContMDiffVectorBundle n Fâ‚ Eâ‚ IB] [ContMDiffVectorBundle n Fâ‚‚ Eâ‚‚ IB]
    [MemTrivializationAtlas eâ‚] [MemTrivializationAtlas eâ‚']
    [MemTrivializationAtlas eâ‚‚] [MemTrivializationAtlas eâ‚‚'] :
    ContMDiffOn IB ğ“˜(ğ•œ, (Fâ‚ â†’L[ğ•œ] Fâ‚‚) â†’L[ğ•œ] Fâ‚ â†’L[ğ•œ] Fâ‚‚) n
      (continuousLinearMapCoordChange (RingHom.id ğ•œ) eâ‚ eâ‚' eâ‚‚ eâ‚‚')
      (eâ‚.baseSet âˆ© eâ‚‚.baseSet âˆ© (eâ‚'.baseSet âˆ© eâ‚‚'.baseSet)) := by
  have hâ‚ := contMDiffOn_coordChangeL (IB := IB) eâ‚' eâ‚ (n := n)
  have hâ‚‚ := contMDiffOn_coordChangeL (IB := IB) eâ‚‚ eâ‚‚' (n := n)
  refine (hâ‚.mono ?_).cle_arrowCongr (hâ‚‚.mono ?_) <;> mfld_set_tac

@[deprecated (since := "2024-11-21")]
alias smoothOn_continuousLinearMapCoordChange := contMDiffOn_continuousLinearMapCoordChange

variable [âˆ€ x, IsTopologicalAddGroup (Eâ‚‚ x)] [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚‚ x)]

theorem hom_chart (yâ‚€ y : LEâ‚Eâ‚‚) :
    chartAt (ModelProd HB (Fâ‚ â†’L[ğ•œ] Fâ‚‚)) yâ‚€ y =
      (chartAt HB yâ‚€.1 y.1, inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ yâ‚€.1 y.1 yâ‚€.1 y.1 y.2) := by
  rw [FiberBundle.chartedSpace_chartAt, trans_apply, PartialHomeomorph.prod_apply,
    Trivialization.coe_coe, PartialHomeomorph.refl_apply, Function.id_def,
    hom_trivializationAt_apply]

theorem contMDiffAt_hom_bundle (f : M â†’ LEâ‚Eâ‚‚) {xâ‚€ : M} {n : â„•âˆ} :
    ContMDiffAt IM (IB.prod ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)) n f xâ‚€ â†”
      ContMDiffAt IM IB n (fun x => (f x).1) xâ‚€ âˆ§
        ContMDiffAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚) n
          (fun x => inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (f xâ‚€).1 (f x).1 (f xâ‚€).1 (f x).1 (f x).2) xâ‚€ :=
  contMDiffAt_totalSpace ..

@[deprecated (since := "2024-11-21")] alias smoothAt_hom_bundle := contMDiffAt_hom_bundle


variable [ContMDiffVectorBundle n Fâ‚ Eâ‚ IB] [ContMDiffVectorBundle n Fâ‚‚ Eâ‚‚ IB]

instance Bundle.ContinuousLinearMap.vectorPrebundle.isContMDiff :
    (Bundle.ContinuousLinearMap.vectorPrebundle (RingHom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).IsContMDiff IB n where
  exists_contMDiffCoordChange := by
    rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© _ âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©
    exact âŸ¨continuousLinearMapCoordChange (RingHom.id ğ•œ) eâ‚ eâ‚' eâ‚‚ eâ‚‚',
      contMDiffOn_continuousLinearMapCoordChange,
      continuousLinearMapCoordChange_apply (RingHom.id ğ•œ) eâ‚ eâ‚' eâ‚‚ eâ‚‚'âŸ©

@[deprecated (since := "2025-01-09")]
alias Bundle.ContinuousLinearMap.vectorPrebundle.isSmooth :=
  Bundle.ContinuousLinearMap.vectorPrebundle.isContMDiff

instance ContMDiffVectorBundle.continuousLinearMap :
    ContMDiffVectorBundle n (Fâ‚ â†’L[ğ•œ] Fâ‚‚) ((fun (b : B) â†¦ Eâ‚ b â†’L[ğ•œ] Eâ‚‚ b)) IB :=
  (Bundle.ContinuousLinearMap.vectorPrebundle (RingHom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).contMDiffVectorBundle IB

end

section

/- Declare two manifolds `Bâ‚` and `Bâ‚‚` (with models `IBâ‚ : HBâ‚ â†’ EBâ‚` and `IBâ‚‚ : HBâ‚‚ â†’ EBâ‚‚`),
and two vector bundles `Eâ‚` and `Eâ‚‚` respectively over `Bâ‚` and `Bâ‚‚` (with model fibers
`Fâ‚` and `Fâ‚‚`).

Also a third manifold `M`, which will be the source of all our maps.
-/
variable {ğ•œ Fâ‚ Fâ‚‚ Bâ‚ Bâ‚‚ M : Type*} {Eâ‚ : Bâ‚ â†’ Type*} {Eâ‚‚ : Bâ‚‚ â†’ Type*} [NontriviallyNormedField ğ•œ]
  [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)] [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, TopologicalSpace (Eâ‚ x)] [âˆ€ x, AddCommGroup (Eâ‚‚ x)]
  [âˆ€ x, Module ğ•œ (Eâ‚‚ x)] [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, TopologicalSpace (Eâ‚‚ x)]
  {EBâ‚ : Type*}
  [NormedAddCommGroup EBâ‚] [NormedSpace ğ•œ EBâ‚] {HBâ‚ : Type*} [TopologicalSpace HBâ‚]
  {IBâ‚ : ModelWithCorners ğ•œ EBâ‚ HBâ‚} [TopologicalSpace Bâ‚] [ChartedSpace HBâ‚ Bâ‚]
  {EBâ‚‚ : Type*}
  [NormedAddCommGroup EBâ‚‚] [NormedSpace ğ•œ EBâ‚‚] {HBâ‚‚ : Type*} [TopologicalSpace HBâ‚‚]
  {IBâ‚‚ : ModelWithCorners ğ•œ EBâ‚‚ HBâ‚‚} [TopologicalSpace Bâ‚‚] [ChartedSpace HBâ‚‚ Bâ‚‚]
  {EM : Type*}
  [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] {HM : Type*} [TopologicalSpace HM]
  {IM : ModelWithCorners ğ•œ EM HM} [TopologicalSpace M] [ChartedSpace HM M]
  {n : WithTop â„•âˆ} [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  {bâ‚ : M â†’ Bâ‚} {bâ‚‚ : M â†’ Bâ‚‚} {mâ‚€ : M}
  {Ï• : Î  (m : M), Eâ‚ (bâ‚ m) â†’L[ğ•œ] Eâ‚‚ (bâ‚‚ m)} {v : Î  (m : M), Eâ‚ (bâ‚ m)} {s : Set M}

/-- Consider a `C^n` map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending smoothly
on `m`, one can apply `Ï• m` to `g m`, and the resulting map is `C^n`.

Note that the smoothness of `Ï•` can not be always be stated as smoothness of a map into a manifold,
as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚` and `bâ‚‚` are globally
smooth, but we want to apply this lemma with only local information. Therefore, we formulate it
using smoothness of `Ï•` read in coordinates.

Version for `ContMDiffWithinAt`. We also give a version for `ContMDiffAt`, but no version for
`ContMDiffOn` or `ContMDiff` as our assumption, written in coordinates, only makes sense around
a point.
-/
lemma ContMDiffWithinAt.clm_apply_of_inCoordinates
    (hÏ• : ContMDiffWithinAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚) n
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) s mâ‚€)
    (hv : ContMDiffWithinAt IM (IBâ‚.prod ğ“˜(ğ•œ, Fâ‚)) n (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) s mâ‚€)
    (hbâ‚‚ : ContMDiffWithinAt IM IBâ‚‚ n bâ‚‚ s mâ‚€) :
    ContMDiffWithinAt IM (IBâ‚‚.prod ğ“˜(ğ•œ, Fâ‚‚)) n (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) s mâ‚€ := by
  rw [â† contMDiffWithinAt_insert_self] at hÏ• hv hbâ‚‚ âŠ¢
  rw [contMDiffWithinAt_totalSpace] at hv âŠ¢
  refine âŸ¨hbâ‚‚, ?_âŸ©
  apply (ContMDiffWithinAt.clm_apply hÏ• hv.2).congr_of_eventuallyEq_of_mem ?_ (mem_insert mâ‚€ s)
  have A : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚ m âˆˆ (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).baseSet := by
    apply hv.1.continuousWithinAt
    apply (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚ mâ‚€)
  have A' : âˆ€á¶  m in ğ“[insert mâ‚€ s] mâ‚€, bâ‚‚ m âˆˆ (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).baseSet := by
    apply hbâ‚‚.continuousWithinAt
    apply (trivializationAt Fâ‚‚ Eâ‚‚ (bâ‚‚ mâ‚€)).open_baseSet.mem_nhds
    exact FiberBundle.mem_baseSet_trivializationAt' (bâ‚‚ mâ‚€)
  filter_upwards [A, A'] with m hm h'm
  rw [inCoordinates_eq hm h'm]
  simp only [coe_comp', ContinuousLinearEquiv.coe_coe, Trivialization.continuousLinearEquivAt_apply,
    Trivialization.continuousLinearEquivAt_symm_apply, Function.comp_apply]
  congr
  rw [Trivialization.symm_apply_apply_mk (trivializationAt Fâ‚ Eâ‚ (bâ‚ mâ‚€)) hm (v m)]

/-- Consider a `C^n` map `v : M â†’ Eâ‚` to a vector bundle, over a basemap `bâ‚ : M â†’ Bâ‚`, and
another basemap `bâ‚‚ : M â†’ Bâ‚‚`. Given linear maps `Ï• m : Eâ‚ (bâ‚ m) â†’ Eâ‚‚ (bâ‚‚ m)` depending smoothly
on `m`, one can apply `Ï• m` to `g m`, and the resulting map is `C^n`.

Note that the smoothness of `Ï•` can not be always be stated as smoothness of a map into a manifold,
as the pullback bundles `bâ‚ *áµ– Eâ‚` and `bâ‚‚ *áµ– Eâ‚‚` only make sense when `bâ‚` and `bâ‚‚` are globally
smooth, but we want to apply this lemma with only local information. Therefore, we formulate it
using smoothness of `Ï•` read in coordinates.

Version for `ContMDiffAt`. We also give a version for `ContMDiffWithinAt`, but no version for
`ContMDiffOn` or `ContMDiff` as our assumption, written in coordinates, only makes sense around
a point.
-/
lemma ContMDiffAt.clm_apply_of_inCoordinates
    (hÏ• : ContMDiffAt IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚) n
      (fun m â†¦ inCoordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (bâ‚ mâ‚€) (bâ‚ m) (bâ‚‚ mâ‚€) (bâ‚‚ m) (Ï• m)) mâ‚€)
    (hv : ContMDiffAt IM (IBâ‚.prod ğ“˜(ğ•œ, Fâ‚)) n (fun m â†¦ (v m : TotalSpace Fâ‚ Eâ‚)) mâ‚€)
    (hbâ‚‚ : ContMDiffAt IM IBâ‚‚ n bâ‚‚ mâ‚€) :
    ContMDiffAt IM (IBâ‚‚.prod ğ“˜(ğ•œ, Fâ‚‚)) n (fun m â†¦ (Ï• m (v m) : TotalSpace Fâ‚‚ Eâ‚‚)) mâ‚€ := by
  rw [â† contMDiffWithinAt_univ] at hÏ• hv hbâ‚‚ âŠ¢
  exact ContMDiffWithinAt.clm_apply_of_inCoordinates hÏ• hv hbâ‚‚

end
