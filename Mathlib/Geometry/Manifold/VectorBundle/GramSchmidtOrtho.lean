/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Michael Rothgang
-/
import Mathlib.Analysis.InnerProductSpace.GramSchmidtOrtho
import Mathlib.Geometry.Manifold.VectorBundle.Riemannian
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Elaborators

/-!
# Gram-Schmidt orthonormalisation on sections of Riemannian vector bundles

In this file, we provide a version of the Gram-Schmidt orthonormalisation procedure
for sections of Riemannian vector bundles: this produces a system of sections which orthogonal
with respect to the bundle metric. If the initial sections were linearly independent resp.
formed a basis at the point, so do the normalised sections.

If the bundle metric is `C^k`, then the procedure preserves regularity of sections:
if all sections are `C^k`, so are their normalised versions.

This is used in `OrthonormalFrame.lean` to convert a local frame to a local orthonormal frame.

## Implementation note


## Tags
vector bundle, bundle metric, orthonormal frame, Gram-Schmidt

-/

open Manifold Bundle ContinuousLinearMap ENat Bornology
open scoped ContDiff Topology

-- Let `V` be a smooth vector bundle with a `C^n` Riemannian structure over a `C^k` manifold `B`.
variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace ‚Ñù EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners ‚Ñù EB HB} {n : WithTop ‚Ñï‚àû}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  {E : B ‚Üí Type*} [TopologicalSpace (TotalSpace F E)] [‚àÄ x, NormedAddCommGroup (E x)]
  [‚àÄ x, InnerProductSpace ‚Ñù (E x)] [FiberBundle F E] [VectorBundle ‚Ñù F E]
  [IsManifold IB n B] [ContMDiffVectorBundle n F E IB]
  [IsContMDiffRiemannianBundle IB n F E]

variable {Œπ : Type*} [LinearOrder Œπ] [LocallyFiniteOrderBot Œπ] [WellFoundedLT Œπ]

attribute [local instance] IsWellOrder.toHasWellFounded

local notation "‚ü™" x ", " y "‚ü´" => inner ‚Ñù x y

open Finset

namespace VectorBundle

open Submodule

/-- The Gram-Schmidt process takes a set of sections as input
and outputs a set of sections which are point-wise orthogonal with the same span.
Basically, we apply the Gram-Schmidt algorithm point-wise. -/
noncomputable def gramSchmidt [WellFoundedLT Œπ]
    (s : Œπ ‚Üí (x : B) ‚Üí E x) (n : Œπ) : (x : B) ‚Üí E x := fun x ‚Ü¶
  InnerProductSpace.gramSchmidt ‚Ñù (s ¬∑ x) n

-- Let `s i` be a collection of sections in `E`, indexed by `Œπ`.
variable {s : Œπ ‚Üí (x : B) ‚Üí E x}

omit [TopologicalSpace B]

variable (s) in
/-- This lemma uses `‚àë i in` instead of `‚àë i :`. -/
theorem gramSchmidt_def (n : Œπ) (x) :
    gramSchmidt s n x =
      s n x - ‚àë i ‚àà Iio n, (‚Ñù ‚àô gramSchmidt s i x).orthogonalProjection (s n x) := by
  simp only [gramSchmidt, InnerProductSpace.gramSchmidt_def]

variable (s) in
theorem gramSchmidt_def' (n : Œπ) (x) :
    s n x = gramSchmidt s n x +
      ‚àë i ‚àà Iio n, (‚Ñù ‚àô gramSchmidt s i x).orthogonalProjection (s n x) := by
  rw [gramSchmidt_def, sub_add_cancel]

variable (s) in
theorem gramSchmidt_def'' (n : Œπ) (x) :
    s n x = gramSchmidt s n x + ‚àë i ‚àà Iio n,
      (‚ü™gramSchmidt s i x, s n x‚ü´ / (‚ÄñgramSchmidt s i x‚Äñ) ^ 2) ‚Ä¢ gramSchmidt s i x := by
  convert gramSchmidt_def' s n x
  rw [orthogonalProjection_singleton, RCLike.ofReal_pow]
  rfl

variable (s) in
@[simp]
lemma gramSchmidt_apply (n : Œπ) (x) :
    gramSchmidt s n x = InnerProductSpace.gramSchmidt ‚Ñù (s ¬∑ x) n := rfl

variable (s) in
@[simp]
theorem gramSchmidt_bot {Œπ : Type*} [LinearOrder Œπ] [LocallyFiniteOrder Œπ] [OrderBot Œπ]
    [WellFoundedLT Œπ] (s : Œπ ‚Üí (x : B) ‚Üí E x) : gramSchmidt s ‚ä• = s ‚ä• := by
  ext x
  apply InnerProductSpace.gramSchmidt_bot

@[simp]
theorem gramSchmidt_zero (n : Œπ) : gramSchmidt (0 : Œπ ‚Üí (x : B) ‚Üí E x) n = 0 := by
  ext x
  simpa using InnerProductSpace.gramSchmidt_zero ..

variable (s) in
/-- **Gram-Schmidt Orthogonalisation**: `gramSchmidt` produces a point-wise orthogonal system
of sections. -/
theorem gramSchmidt_orthogonal {a b : Œπ} (h‚ÇÄ : a ‚â† b) (x) :
    ‚ü™gramSchmidt s a x, gramSchmidt s b x‚ü´ = 0 :=
  InnerProductSpace.gramSchmidt_orthogonal _ _ h‚ÇÄ

variable (s) in
/-- This is another version of `gramSchmidt_orthogonal` using `Pairwise` instead. -/
theorem gramSchmidt_pairwise_orthogonal (x) :
    Pairwise fun a b ‚Ü¶ ‚ü™gramSchmidt s a x, gramSchmidt s b x‚ü´ = 0 :=
  fun _ _ h ‚Ü¶ gramSchmidt_orthogonal s h _

variable (s) in
theorem gramSchmidt_inv_triangular {i j : Œπ} (hij : i < j) (x) :
    ‚ü™gramSchmidt s j x, s i x‚ü´ = 0 :=
  InnerProductSpace.gramSchmidt_inv_triangular _ _ hij

open Submodule Set Order

variable (s) in
theorem mem_span_gramSchmidt {i j : Œπ} (hij : i ‚â§ j) (x) :
    s i x ‚àà span ‚Ñù ((gramSchmidt s ¬∑ x) '' Set.Iic j) :=
  InnerProductSpace.mem_span_gramSchmidt _ _ hij

variable (s) in
theorem gramSchmidt_mem_span (x) :
    ‚àÄ {j i}, i ‚â§ j ‚Üí gramSchmidt s i x ‚àà span ‚Ñù ((s ¬∑ x) '' Set.Iic j) :=
  InnerProductSpace.gramSchmidt_mem_span _ _

variable (s) in
theorem span_gramSchmidt_Iic (c : Œπ) (x) :
    span ‚Ñù ((gramSchmidt s ¬∑ x) '' Set.Iic c) = span ‚Ñù ((s ¬∑ x) '' Set.Iic c) :=
  InnerProductSpace.span_gramSchmidt_Iic ..

variable (s) in
theorem span_gramSchmidt_Iio (c : Œπ) (x) :
    span ‚Ñù ((gramSchmidt s ¬∑ x) '' Set.Iio c) = span ‚Ñù ((s ¬∑ x) '' Set.Iio c) :=
  InnerProductSpace.span_gramSchmidt_Iio _ _ _

-- variable (s) in
-- /-- `gramSchmidt` preserves the point-wise span of sections. -/
-- theorem span_gramSchmidt (x) : span ‚Ñù (range (gramSchmidt ‚Ñù (s ¬∑ x))) = span ‚Ñù (range (s ¬∑ x)) :=
--   span_eq_span (range_subset_iff.2 fun _ ‚Ü¶
--     span_mono (image_subset_range _ _) <| gramSchmidt_mem_span _ _ le_rfl) <|
--       range_subset_iff.2 fun _ ‚Ü¶
--         span_mono (image_subset_range _ _) <| mem_span_gramSchmidt _ _ le_rfl

theorem gramSchmidt_of_orthogonal {x} (hs : Pairwise fun i j ‚Ü¶ ‚ü™s i x, s j x‚ü´ = 0) :
    ‚àÄ i‚ÇÄ, gramSchmidt s i‚ÇÄ x = s i‚ÇÄ x:= by
  intro i
  rw [gramSchmidt_def]
  trans s i x - 0
  ¬∑ congr
    apply Finset.sum_eq_zero
    intro j hj
    rw [Submodule.coe_eq_zero]
    suffices span ‚Ñù ((s ¬∑ x) '' Set.Iic j) ‚üÇ ‚Ñù ‚àô s i x by
      apply orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero
      rw [mem_orthogonal_singleton_iff_inner_left, ‚Üê mem_orthogonal_singleton_iff_inner_right]
      exact this <| gramSchmidt_mem_span _ _ le_rfl
    rw [isOrtho_span]
    rintro u ‚ü®k, hk, rfl‚ü© v (rfl : v = s i x)
    apply hs
    exact (lt_of_le_of_lt hk (Finset.mem_Iio.mp hj)).ne
  ¬∑ simp

theorem gramSchmidt_ne_zero_coe (n : Œπ) (x)
    (h‚ÇÄ : LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic n ‚Üí Œπ))) : gramSchmidt s n x ‚â† 0 :=
  InnerProductSpace.gramSchmidt_ne_zero_coe _ h‚ÇÄ

variable (s) in
/-- If the input sections of `gramSchmidt` are point-wise linearly independent,
the resulting sections are non-zero. -/
theorem gramSchmidt_ne_zero (n : Œπ) {x} (h‚ÇÄ : LinearIndependent ‚Ñù (s ¬∑ x)) :
    gramSchmidt s n x ‚â† 0 :=
  InnerProductSpace.gramSchmidt_ne_zero _ h‚ÇÄ

-- not needed at the moment: I want a point-wise version, along the lines
-- "if s i x is a basis, then gramSchmidt s i x is a triangular matrix"
/-
/-- At each point, when given a basis, `gramSchmidt` produces a triangular matrix of section
values. -/
theorem gramSchmidt_triangular {x} {i j : Œπ} (hij : i < j) (b : Basis Œπ ‚Ñù (E x)) :
    b.repr (gramSchmidt b i x) j = 0 := sorry
     b.repr (gramSchmidt b i) j = 0 := by
   have : gramSchmidt ‚Ñù b i ‚àà span ‚Ñù (gramSchmidt ‚Ñù b '' Set.Iio j) :=
     subset_span ((Set.mem_image _ _ _).2 ‚ü®i, hij, rfl‚ü©)
   have : gramSchmidt ‚Ñù b i ‚àà span ‚Ñù (b '' Set.Iio j) := by rwa [‚Üê span_gramSchmidt_Iio ‚Ñù b j]
   have : ‚Üë(b.repr (gramSchmidt ‚Ñù b i)).support ‚äÜ Set.Iio j :=
     Basis.repr_support_subset_of_mem_span b (Set.Iio j) this
   exact (Finsupp.mem_supported' _ _).1 ((Finsupp.mem_supported ‚Ñù _).2 this) j Set.notMem_Iio_self-/

/-- `gramSchmidt` produces point-wise linearly independent sections when given linearly
independent sections. -/
theorem gramSchmidt_linearIndependent {x} (h‚ÇÄ : LinearIndependent ‚Ñù (s ¬∑ x)) :
    LinearIndependent ‚Ñù (gramSchmidt s ¬∑ x) :=
  InnerProductSpace.gramSchmidt_linearIndependent h‚ÇÄ

noncomputable def gramSchmidtNormed [WellFoundedLT Œπ]
    (s : Œπ ‚Üí (x : B) ‚Üí E x) (n : Œπ) : (x : B) ‚Üí E x := fun x ‚Ü¶
  InnerProductSpace.gramSchmidtNormed ‚Ñù (s ¬∑ x) n

lemma gramSchmidtNormed_coe {n : Œπ} {x} :
    gramSchmidtNormed s n x = ‚ÄñgramSchmidt s n x‚Äñ‚Åª¬π ‚Ä¢ gramSchmidt s n x := by
  simp [gramSchmidtNormed, InnerProductSpace.gramSchmidtNormed]

variable {x}

theorem gramSchmidtNormed_unit_length_coe (n : Œπ)
    (h‚ÇÄ : LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic n ‚Üí Œπ))) :
    ‚ÄñgramSchmidtNormed s n x‚Äñ = 1 :=
  InnerProductSpace.gramSchmidtNormed_unit_length_coe n h‚ÇÄ

theorem gramSchmidtNormed_unit_length (n : Œπ) (h‚ÇÄ : LinearIndependent ‚Ñù (s ¬∑ x)) :
    ‚ÄñgramSchmidtNormed s n x‚Äñ = 1 :=
  InnerProductSpace.gramSchmidtNormed_unit_length n h‚ÇÄ

theorem gramSchmidtNormed_unit_length' {n : Œπ} (hn : gramSchmidtNormed s n x ‚â† 0) :
    ‚ÄñgramSchmidtNormed s n x‚Äñ = 1 :=
  InnerProductSpace.gramSchmidtNormed_unit_length' hn

/-- **Gram-Schmidt Orthonormalization**: `gramSchmidtNormed` applied to a point-wise linearly
independent set of sections produces a point-wise orthornormal system of sections. -/
theorem gramSchmidtNormed_orthonormal (h‚ÇÄ : LinearIndependent ‚Ñù (s ¬∑ x)) :
    Orthonormal ‚Ñù (gramSchmidtNormed s ¬∑ x) :=
  InnerProductSpace.gramSchmidtNormed_orthonormal h‚ÇÄ

variable (s) in
/-- **Gram-Schmidt Orthonormalization**: `gramSchmidtNormed` produces a point-wise orthornormal
system of sections after removing the sections which become zero in the process. -/
theorem gramSchmidtNormed_orthonormal' (x) :
    Orthonormal ‚Ñù fun i : { i | gramSchmidtNormed s i x ‚â† 0 } => gramSchmidtNormed s i x :=
  InnerProductSpace.gramSchmidtNormed_orthonormal' _

open Submodule Set Order

-- Statement needs to be changed a bit to make it type-check.
-- variable (s) in
-- theorem span_gramSchmidtNormed (t : Set Œπ) :
--     span ‚Ñù (gramSchmidtNormed s '' t) = span ‚Ñù (gramSchmidt s '' t) := sorry

-- theorem span_gramSchmidtNormed_range (f : Œπ ‚Üí E) :
--     span ùïú (range (gramSchmidtNormed ùïú f)) = span ùïú (range (gramSchmidt ùïú f)) := by
--   simpa only [image_univ.symm] using span_gramSchmidtNormed f univ

/-- `gramSchmidtNormed` applied to linearly independent sections at a point `x` produces
sections which are linearly independent at `x`. -/
theorem gramSchmidtNormed_linearIndependent (h‚ÇÄ : LinearIndependent ‚Ñù (s ¬∑ x)) :
    LinearIndependent ‚Ñù (gramSchmidtNormed s ¬∑ x) := by
  simp [gramSchmidtNormed, InnerProductSpace.gramSchmidtNormed_linearIndependent h‚ÇÄ]

end VectorBundle

/-! The Gram-Schmidt process preserves smoothness of sections -/

variable {n : WithTop ‚Ñï‚àû}

-- TODO: fix pretty-printing of my new elaborators!
set_option linter.style.commandStart false

variable [IsContMDiffRiemannianBundle IB n F E]

section helper

variable {s t : (x : B) ‚Üí E x} {u : Set B} {x : B}

-- TODO: give a much better name!
lemma contMDiffWithinAt_aux
    (hs : CMDiffAt[u] n (T% s) x) (ht : CMDiffAt[u] n (T% t) x) (hs' : s x ‚â† 0) :
    CMDiffAt[u] n (fun x ‚Ü¶ ‚ü™s x, t x‚ü´ / (‚Äñs x‚Äñ ^ 2)) x := by
  have := (hs.inner_bundle ht).smul ((hs.inner_bundle hs).inv‚ÇÄ (inner_self_ne_zero.mpr hs'))
  apply this.congr
  ¬∑ intro y hy
    congr
    simp [inner_self_eq_norm_sq_to_K]
  ¬∑ congr
    rw [‚Üê real_inner_self_eq_norm_sq]

lemma contMDiffAt_aux (hs : CMDiffAt n (T% s) x) (ht : CMDiffAt n (T% t) x) (hs' : s x ‚â† 0) :
    CMDiffAt n (fun x ‚Ü¶ ‚ü™s x, t x‚ü´ / (‚Äñs x‚Äñ ^ 2)) x := by
  rw [‚Üê contMDiffWithinAt_univ] at hs ht ‚ä¢
  exact contMDiffWithinAt_aux hs ht hs'

def ContMDiffWithinAt.orthogonalProjection
    (hs : CMDiffAt[u] n (T% s) x) (ht : CMDiffAt[u] n (T% t) x) (hs' : s x ‚â† 0) :
    -- TODO: leaving out the type ascription yields a horrible error message, add test and fix!
    letI S : (x : B) ‚Üí E x := fun x ‚Ü¶ (Submodule.span ‚Ñù {s x}).orthogonalProjection (t x);
    CMDiffAt[u] n (T% S) x := by
  simp_rw [Submodule.orthogonalProjection_singleton]
  exact (contMDiffWithinAt_aux hs ht hs').smul_section hs

lemma contMDiffWithinAt_inner (hs : CMDiffAt[u] n (T% s) x) (hs' : s x ‚â† 0) :
    CMDiffAt[u] n (‚Äñs ¬∑‚Äñ) x := by
  let F (x) := ‚ü™s x, s x‚ü´
  have aux : CMDiffAt[u] n (Real.sqrt ‚àò F) x := by
    have h1 : CMDiffAt[(F '' u)] n (Real.sqrt) (F x) := by
      apply ContMDiffAt.contMDiffWithinAt
      rw [contMDiffAt_iff_contDiffAt]
      exact Real.contDiffAt_sqrt (by simp [F, hs'])
    exact h1.comp x (hs.inner_bundle hs) (Set.mapsTo_image _ u)
  convert aux
  simp [F, ‚Üê norm_eq_sqrt_real_inner]

end helper

variable {s : Œπ ‚Üí (x : B) ‚Üí E x} {u : Set B} {x : B} {i : Œπ}

lemma gramSchmidt_contMDiffWithinAt (hs : ‚àÄ i, CMDiffAt[u] n (T% (s i)) x)
    {i : Œπ} (hs' : LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiffAt[u] n (T% (VectorBundle.gramSchmidt s i)) x := by
  simp_rw [VectorBundle.gramSchmidt_def]
  apply (hs i).sub_section
  apply ContMDiffWithinAt.sum_section
  intro i' hi'
  let aux : { x // x ‚àà Set.Iic i' } ‚Üí { x // x ‚àà Set.Iic i } :=
    fun ‚ü®x, hx‚ü© ‚Ü¶ ‚ü®x, hx.trans (Finset.mem_Iio.mp hi').le‚ü©
  have : LinearIndependent ‚Ñù ((fun x_1 ‚Ü¶ s x_1 x) ‚àò @Subtype.val Œπ fun x ‚Ü¶ x ‚àà Set.Iic i') := by
    apply hs'.comp aux
    intro ‚ü®x, hx‚ü© ‚ü®x', hx'‚ü© h
    simp_all only [Subtype.mk.injEq, aux]
  apply ContMDiffWithinAt.orthogonalProjection (gramSchmidt_contMDiffWithinAt hs this) (hs i)
  apply VectorBundle.gramSchmidt_ne_zero_coe _ _ this
termination_by i
decreasing_by exact (LocallyFiniteOrderBot.finset_mem_Iio i i').mp hi'

lemma gramSchmidt_contMDiffAt (hs : ‚àÄ i, CMDiffAt n (T% (s i)) x)
    (hs' : LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiffAt n (T% (VectorBundle.gramSchmidt s i)) x :=
  contMDiffWithinAt_univ.mpr <| gramSchmidt_contMDiffWithinAt (fun i ‚Ü¶ hs i) hs'

lemma gramSchmidt_contMDiffOn (hs : ‚àÄ i, CMDiff[u] n (T% (s i)))
    (hs' : ‚àÄ x ‚àà u, LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiff[u] n (T% (VectorBundle.gramSchmidt s i)) :=
  fun x hx ‚Ü¶ gramSchmidt_contMDiffWithinAt (fun i ‚Ü¶ hs i x hx) (hs' _ hx)

lemma gramSchmidt_contMDiff (hs : ‚àÄ i, CMDiff n (T% (s i)))
    (hs' : ‚àÄ x, LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiff n (T% (VectorBundle.gramSchmidt s i)) :=
  fun x ‚Ü¶ gramSchmidt_contMDiffAt (fun i ‚Ü¶ hs i x) (hs' x)

lemma gramSchmidtNormed_contMDiffWithinAt (hs : ‚àÄ i, CMDiffAt[u] n (T% (s i)) x)
    (hs' : LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiffAt[u] n (T% (VectorBundle.gramSchmidtNormed s i)) x := by
  have : CMDiffAt[u] n (T%
      (fun x ‚Ü¶ ‚ÄñVectorBundle.gramSchmidt s i x‚Äñ‚Åª¬π ‚Ä¢ VectorBundle.gramSchmidt s i x)) x := by
    refine ContMDiffWithinAt.smul_section ?_ (gramSchmidt_contMDiffWithinAt hs hs')
    refine ContMDiffWithinAt.inv‚ÇÄ ?_ ?_
    ¬∑ refine contMDiffWithinAt_inner (gramSchmidt_contMDiffWithinAt hs hs') ?_
      simpa using InnerProductSpace.gramSchmidt_ne_zero_coe i hs'
    ¬∑ simpa using InnerProductSpace.gramSchmidt_ne_zero_coe i hs'
  exact this.congr (fun y hy ‚Ü¶ by congr) (by congr)

lemma gramSchmidtNormed_contMDiffAt (hs : ‚àÄ i, CMDiffAt n (T% (s i)) x)
    (hs' : LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ)))
    : CMDiffAt n (T% (VectorBundle.gramSchmidtNormed s i)) x :=
  contMDiffWithinAt_univ.mpr <| gramSchmidtNormed_contMDiffWithinAt (fun i ‚Ü¶ hs i) hs'

lemma gramSchmidtNormed_contMDiffOn (hs : ‚àÄ i, CMDiff[u] n (T% (s i)))
    (hs' : ‚àÄ x ‚àà u, LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiff[u] n (T% (VectorBundle.gramSchmidtNormed s i)) :=
  fun x hx ‚Ü¶ gramSchmidtNormed_contMDiffWithinAt (fun i ‚Ü¶ hs i x hx) (hs' _ hx)

lemma gramSchmidtNormed_contMDiff (hs : ‚àÄ i, CMDiff n (T% (s i)))
    (hs' : ‚àÄ x, LinearIndependent ‚Ñù ((s ¬∑ x) ‚àò ((‚Üë) : Set.Iic i ‚Üí Œπ))) :
    CMDiff n (T% (VectorBundle.gramSchmidtNormed s i)) :=
  fun x ‚Ü¶ gramSchmidtNormed_contMDiffAt (fun i ‚Ü¶ hs i x) (hs' x)
