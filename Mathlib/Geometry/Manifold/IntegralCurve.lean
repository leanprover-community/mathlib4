/-
Copyright (c) 2023 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import Mathlib.Analysis.ODE.Gronwall
import Mathlib.Analysis.ODE.PicardLindelof
import Mathlib.Geometry.Manifold.InteriorBoundary
import Mathlib.Geometry.Manifold.MFDeriv.Tangent

/-!
# Integral curves of vector fields on a manifold

Let `M` be a manifold and `v : (x : M) ‚Üí TangentSpace I x` be a vector field on `M`. An integral
curve of `v` is a function `Œ≥ : ‚Ñù ‚Üí M` such that the derivative of `Œ≥` at `t` equals `v (Œ≥ t)`. The
integral curve may only be defined for all `t` within some subset of `‚Ñù`.

## Main definitions

Let `v : M ‚Üí TM` be a vector field on `M`, and let `Œ≥ : ‚Ñù ‚Üí M`.
* `IsIntegralCurve Œ≥ v`: `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t : ‚Ñù`. That is, `Œ≥` is a global
integral curve of `v`.
* `IsIntegralCurveOn Œ≥ v s`: `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t ‚àà s`, where `s : Set ‚Ñù`.
* `IsIntegralCurveAt Œ≥ v t‚ÇÄ`: `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t` in some open interval
around `t‚ÇÄ`. That is, `Œ≥` is a local integral curve of `v`.

For `IsIntegralCurveOn Œ≥ v s` and `IsIntegralCurveAt Œ≥ v t‚ÇÄ`, even though `Œ≥` is defined for all
time, its value outside of the set `s` or a small interval around `t‚ÇÄ` is irrelevant and considered
junk.

## Main results

* `exists_isIntegralCurveAt_of_contMDiffAt_boundaryless`: Existence of local integral curves for a
$C^1$ vector field. This follows from the existence theorem for solutions to ODEs
(`exists_forall_hasDerivAt_Ioo_eq_of_contDiffAt`).
* `isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless`: Uniqueness of local integral curves for a
$C^1$ vector field. This follows from the uniqueness theorem for solutions to ODEs
(`ODE_solution_unique_of_mem_set_Ioo`). This requires the manifold to be Hausdorff (`T2Space`).

## Implementation notes

For the existence and uniqueness theorems, we assume that the image of the integral curve lies in
the interior of the manifold. The case where the integral curve may lie on the boundary of the
manifold requires special treatment, and we leave it as a TODO.

We state simpler versions of the theorem for boundaryless manifolds as corollaries.

## TODO

* The case where the integral curve may venture to the boundary of the manifold. See Theorem 9.34,
Lee. May require submanifolds.

## Reference
* Lee, J. M. (2012). _Introduction to Smooth Manifolds_. Springer New York.

## Tags

integral curve, vector field, local existence, uniqueness
-/

open scoped Manifold Topology

open Function Set Classical

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

/-- If `Œ≥ : ‚Ñù ‚Üí M` is $C^1$ on `s : Set ‚Ñù` and `v` is a vector field on `M`,
`IsIntegralCurveOn Œ≥ v s` means `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t ‚àà s`. The value of `Œ≥`
outside of `s` is irrelevant and considered junk. -/
def IsIntegralCurveOn (Œ≥ : ‚Ñù ‚Üí M) (v : (x : M) ‚Üí TangentSpace I x) (s : Set ‚Ñù) : Prop :=
  ‚àÄ t ‚àà s, HasMFDerivAt ùìò(‚Ñù, ‚Ñù) I Œ≥ t ((1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù).smulRight <| v (Œ≥ t))

/-- If `v` is a vector field on `M` and `t‚ÇÄ : ‚Ñù`, `IsIntegralCurveAt Œ≥ v t‚ÇÄ` means `Œ≥ : ‚Ñù ‚Üí M` is a
local integral curve of `v` in a neighbourhood containing `t‚ÇÄ`. The value of `Œ≥` outside of this
interval is irrelevant and considered junk. -/
def IsIntegralCurveAt (Œ≥ : ‚Ñù ‚Üí M) (v : (x : M) ‚Üí TangentSpace I x) (t‚ÇÄ : ‚Ñù) : Prop :=
  ‚àÄ·∂† t in ùìù t‚ÇÄ, HasMFDerivAt ùìò(‚Ñù, ‚Ñù) I Œ≥ t ((1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù).smulRight <| v (Œ≥ t))

/-- If `v : M ‚Üí TM` is a vector field on `M`, `IsIntegralCurve Œ≥ v` means `Œ≥ : ‚Ñù ‚Üí M` is a global
integral curve of `v`. That is, `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t : ‚Ñù`. -/
def IsIntegralCurve (Œ≥ : ‚Ñù ‚Üí M) (v : (x : M) ‚Üí TangentSpace I x) : Prop :=
  ‚àÄ t : ‚Ñù, HasMFDerivAt ùìò(‚Ñù, ‚Ñù) I Œ≥ t ((1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù).smulRight (v (Œ≥ t)))

variable {Œ≥ Œ≥' : ‚Ñù ‚Üí M} {v : (x : M) ‚Üí TangentSpace I x} {s s' : Set ‚Ñù} {t‚ÇÄ : ‚Ñù}

lemma IsIntegralCurve.isIntegralCurveOn (h : IsIntegralCurve Œ≥ v) (s : Set ‚Ñù) :
    IsIntegralCurveOn Œ≥ v s := fun t _ ‚Ü¶ h t

lemma isIntegralCurve_iff_isIntegralCurveOn : IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurveOn Œ≥ v univ :=
  ‚ü®fun h ‚Ü¶ h.isIntegralCurveOn _, fun h t ‚Ü¶ h t (mem_univ _)‚ü©

lemma isIntegralCurveAt_iff :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî ‚àÉ s ‚àà ùìù t‚ÇÄ, IsIntegralCurveOn Œ≥ v s := by
  simp_rw [IsIntegralCurveOn, ‚Üê Filter.eventually_iff_exists_mem, IsIntegralCurveAt]

/-- `Œ≥` is an integral curve for `v` at `t‚ÇÄ` iff `Œ≥` is an integral curve on some interval
containing `t‚ÇÄ`. -/
lemma isIntegralCurveAt_iff' :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî ‚àÉ Œµ > 0, IsIntegralCurveOn Œ≥ v (Metric.ball t‚ÇÄ Œµ) := by
  simp_rw [IsIntegralCurveOn, ‚Üê Metric.eventually_nhds_iff_ball, IsIntegralCurveAt]

lemma IsIntegralCurve.isIntegralCurveAt (h : IsIntegralCurve Œ≥ v) (t : ‚Ñù) :
    IsIntegralCurveAt Œ≥ v t := isIntegralCurveAt_iff.mpr ‚ü®univ, Filter.univ_mem, fun t _ ‚Ü¶ h t‚ü©

lemma isIntegralCurve_iff_isIntegralCurveAt :
    IsIntegralCurve Œ≥ v ‚Üî ‚àÄ t : ‚Ñù, IsIntegralCurveAt Œ≥ v t :=
  ‚ü®fun h ‚Ü¶ h.isIntegralCurveAt, fun h t ‚Ü¶ by
    obtain ‚ü®s, hs, h‚ü© := isIntegralCurveAt_iff.mp (h t)
    exact h t (mem_of_mem_nhds hs)‚ü©

lemma IsIntegralCurveOn.mono (h : IsIntegralCurveOn Œ≥ v s) (hs : s' ‚äÜ s) :
    IsIntegralCurveOn Œ≥ v s' := fun t ht ‚Ü¶ h t (mem_of_mem_of_subset ht hs)

lemma IsIntegralCurveOn.of_union (h : IsIntegralCurveOn Œ≥ v s) (h' : IsIntegralCurveOn Œ≥ v s') :
    IsIntegralCurveOn Œ≥ v (s ‚à™ s') := fun _ ‚Ü¶ fun | .inl ht => h _ ht | .inr ht => h' _ ht

lemma IsIntegralCurveAt.hasMFDerivAt (h : IsIntegralCurveAt Œ≥ v t‚ÇÄ) :
    HasMFDerivAt ùìò(‚Ñù, ‚Ñù) I Œ≥ t‚ÇÄ ((1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù).smulRight (v (Œ≥ t‚ÇÄ))) :=
  have ‚ü®_, hs, h‚ü© := isIntegralCurveAt_iff.mp h
  h t‚ÇÄ (mem_of_mem_nhds hs)

lemma IsIntegralCurveOn.isIntegralCurveAt (h : IsIntegralCurveOn Œ≥ v s) (hs : s ‚àà ùìù t‚ÇÄ) :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ := isIntegralCurveAt_iff.mpr ‚ü®s, hs, h‚ü©

/-- If `Œ≥` is an integral curve at each `t ‚àà s`, it is an integral curve on `s`. -/
lemma IsIntegralCurveAt.isIntegralCurveOn (h : ‚àÄ t ‚àà s, IsIntegralCurveAt Œ≥ v t) :
    IsIntegralCurveOn Œ≥ v s := by
  intros t ht
  obtain ‚ü®s, hs, h‚ü© := isIntegralCurveAt_iff.mp (h t ht)
  exact h t (mem_of_mem_nhds hs)

lemma IsIntegralCurveOn.congr_of_eqOn (hs : IsOpen s) (h : IsIntegralCurveOn Œ≥ v s)
    (hŒ≥ : EqOn Œ≥ Œ≥' s) : IsIntegralCurveOn Œ≥' v s := by
  intros t ht
  rw [‚Üê hŒ≥ ht]
  apply (h t ht).congr_of_eventuallyEq
  exact Filter.eventuallyEq_of_mem (hs.mem_nhds ht) hŒ≥.symm

lemma isIntegralCurveOn_congr_of_eqOn (hs : IsOpen s) (hŒ≥ : EqOn Œ≥ Œ≥' s) :
    IsIntegralCurveOn Œ≥ v s ‚Üî IsIntegralCurveOn Œ≥' v s :=
  ‚ü®fun h ‚Ü¶ h.congr_of_eqOn hs hŒ≥, fun h ‚Ü¶ h.congr_of_eqOn hs hŒ≥.symm‚ü©

lemma IsIntegralCurveAt.congr_of_eventuallyEq (h : IsIntegralCurveAt Œ≥ v t‚ÇÄ)
    (hŒ≥ : Œ≥ =·∂†[nhds t‚ÇÄ] Œ≥') : IsIntegralCurveAt Œ≥' v t‚ÇÄ := by
  obtain ‚ü®s, hdrv, hs1, hs2‚ü© := eventually_nhds_iff.mp (h.and hŒ≥)
  refine eventually_nhds_iff.mpr ‚ü®s, fun t ht ‚Ü¶ ?_, hs1, hs2‚ü©
  apply ((hdrv t ht).2 ‚ñ∏ (hdrv t ht).1).congr_of_eventuallyEq
  rw [Filter.eventuallyEq_iff_exists_mem]
  exact ‚ü®s, hs1.mem_nhds ht, fun t' ht' ‚Ü¶ (hdrv t' ht').2.symm‚ü©

lemma isIntegralCurveAt_congr_of_eventuallyEq (hŒ≥ : Œ≥ =·∂†[nhds t‚ÇÄ] Œ≥') :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî IsIntegralCurveAt Œ≥' v t‚ÇÄ :=
  ‚ü®fun h ‚Ü¶ h.congr_of_eventuallyEq hŒ≥, fun h ‚Ü¶ h.congr_of_eventuallyEq hŒ≥.symm‚ü©

lemma isIntegralCurveOn_iff_isIntegralCurveAt (hs : IsOpen s) :
    IsIntegralCurveOn Œ≥ v s ‚Üî ‚àÄ t ‚àà s, IsIntegralCurveAt Œ≥ v t :=
  ‚ü®fun h _ ht ‚Ü¶ h.isIntegralCurveAt (hs.mem_nhds ht), IsIntegralCurveAt.isIntegralCurveOn‚ü©

lemma IsIntegralCurveOn.continuousAt (hŒ≥ : IsIntegralCurveOn Œ≥ v s) (ht : t‚ÇÄ ‚àà s) :
    ContinuousAt Œ≥ t‚ÇÄ := (hŒ≥ t‚ÇÄ ht).1

lemma IsIntegralCurveOn.continuousOn (hŒ≥ : IsIntegralCurveOn Œ≥ v s) :
    ContinuousOn Œ≥ s := fun t ht ‚Ü¶ (hŒ≥ t ht).1.continuousWithinAt

lemma IsIntegralCurveAt.continuousAt (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) :
    ContinuousAt Œ≥ t‚ÇÄ :=
  have ‚ü®_, hs, hŒ≥‚ü© := isIntegralCurveAt_iff.mp hŒ≥
  hŒ≥.continuousAt <| mem_of_mem_nhds hs

lemma IsIntegralCurve.continuous (hŒ≥ : IsIntegralCurve Œ≥ v) : Continuous Œ≥ :=
  continuous_iff_continuousAt.mpr fun _ ‚Ü¶ (hŒ≥.isIntegralCurveOn univ).continuousAt (mem_univ _)

variable [SmoothManifoldWithCorners I M] in
/-- If `Œ≥` is an integral curve of a vector field `v`, then `Œ≥ t` is tangent to `v (Œ≥ t)` when
  expressed in the local chart around the initial point `Œ≥ t‚ÇÄ`. -/
lemma IsIntegralCurveOn.hasDerivAt (hŒ≥ : IsIntegralCurveOn Œ≥ v s) {t : ‚Ñù} (ht : t ‚àà s)
    (hsrc : Œ≥ t ‚àà (extChartAt I (Œ≥ t‚ÇÄ)).source) :
    HasDerivAt ((extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥)
      (tangentCoordChange I (Œ≥ t) (Œ≥ t‚ÇÄ) (Œ≥ t) (v (Œ≥ t))) t := by
  -- turn `HasDerivAt` into comp of `HasMFDerivAt`
  have hsrc := extChartAt_source I (Œ≥ t‚ÇÄ) ‚ñ∏ hsrc
  rw [hasDerivAt_iff_hasFDerivAt, ‚Üê hasMFDerivAt_iff_hasFDerivAt]
  apply (HasMFDerivAt.comp t
    (hasMFDerivAt_extChartAt (I := I) hsrc) (hŒ≥ _ ht)).congr_mfderiv
  rw [ContinuousLinearMap.ext_iff]
  intro a
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.smulRight_apply, map_smul,
    ‚Üê ContinuousLinearMap.one_apply (R‚ÇÅ := ‚Ñù) a, ‚Üê ContinuousLinearMap.smulRight_apply,
    mfderiv_chartAt_eq_tangentCoordChange hsrc]
  rfl

variable [SmoothManifoldWithCorners I M] in
lemma IsIntegralCurveAt.eventually_hasDerivAt (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) :
    ‚àÄ·∂† t in ùìù t‚ÇÄ, HasDerivAt ((extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥)
      (tangentCoordChange I (Œ≥ t) (Œ≥ t‚ÇÄ) (Œ≥ t) (v (Œ≥ t))) t := by
  apply eventually_mem_nhds_iff.mpr
    (hŒ≥.continuousAt.preimage_mem_nhds (extChartAt_source_mem_nhds (I := I) _)) |>.and hŒ≥ |>.mono
  rintro t ‚ü®ht1, ht2‚ü©
  have hsrc := mem_of_mem_nhds ht1
  rw [mem_preimage, extChartAt_source I (Œ≥ t‚ÇÄ)] at hsrc
  rw [hasDerivAt_iff_hasFDerivAt, ‚Üê hasMFDerivAt_iff_hasFDerivAt]
  apply (HasMFDerivAt.comp t (hasMFDerivAt_extChartAt (I := I) hsrc) ht2).congr_mfderiv
  rw [ContinuousLinearMap.ext_iff]
  intro a
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.smulRight_apply, map_smul,
    ‚Üê ContinuousLinearMap.one_apply (R‚ÇÅ := ‚Ñù) a, ‚Üê ContinuousLinearMap.smulRight_apply,
    mfderiv_chartAt_eq_tangentCoordChange hsrc]
  rfl

/-! ### Translation lemmas -/

section Translation

lemma IsIntegralCurveOn.comp_add (hŒ≥ : IsIntegralCurveOn Œ≥ v s) (dt : ‚Ñù) :
    IsIntegralCurveOn (Œ≥ ‚àò (¬∑ + dt)) v { t | t + dt ‚àà s } := by
  intros t ht
  rw [comp_apply, ‚Üê ContinuousLinearMap.comp_id (ContinuousLinearMap.smulRight 1 (v (Œ≥ (t + dt))))]
  apply HasMFDerivAt.comp t (hŒ≥ (t + dt) ht)
  refine ‚ü®(continuous_add_right _).continuousAt, ?_‚ü©
  simp only [mfld_simps, hasFDerivWithinAt_univ]
  exact HasFDerivAt.add_const (hasFDerivAt_id _) _

lemma isIntegralCurveOn_comp_add (dt : ‚Ñù) :
    IsIntegralCurveOn Œ≥ v s ‚Üî IsIntegralCurveOn (Œ≥ ‚àò (¬∑ + dt)) v { t | t + dt ‚àà s } := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_add (-dt) <;> ext <;> simp

lemma isIntegralCurveOn_comp_sub (dt : ‚Ñù) :
    IsIntegralCurveOn Œ≥ v s ‚Üî IsIntegralCurveOn (Œ≥ ‚àò (¬∑ - dt)) v { t | t - dt ‚àà s } := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_add dt <;> ext <;> simp

-- TODO: missing lemma(s) for `Ioo`
lemma isIntegralCurveOn_Ioo_comp_add {a b : ‚Ñù} (dt : ‚Ñù) :
    IsIntegralCurveOn Œ≥ v (Ioo a b) ‚Üî
      IsIntegralCurveOn (Œ≥ ‚àò (¬∑ + dt)) v (Ioo (a - dt) (b - dt)) := by
  have : {t | t + dt ‚àà Ioo a b} = Ioo (a - dt) (b - dt) := by
    ext t
    rw [mem_setOf, mem_Ioo, mem_Ioo, sub_lt_iff_lt_add (c := dt), lt_sub_iff_add_lt (b := dt)]
  rw [isIntegralCurveOn_comp_add dt, this]

lemma isIntegralCurveOn_Ioo_comp_sub {a b : ‚Ñù} (dt : ‚Ñù) :
    IsIntegralCurveOn Œ≥ v (Ioo a b) ‚Üî
      IsIntegralCurveOn (Œ≥ ‚àò (¬∑ - dt)) v (Ioo (a + dt) (b + dt)) := by
  have : {t | t - dt ‚àà Ioo a b} = Ioo (a + dt) (b + dt) := by
    ext t
    rw [mem_setOf, mem_Ioo, mem_Ioo, sub_lt_iff_lt_add (c := dt), lt_sub_iff_add_lt (b := dt)]
  rw [isIntegralCurveOn_comp_sub dt, this]

lemma IsIntegralCurveAt.comp_add (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) (dt : ‚Ñù) :
    IsIntegralCurveAt (Œ≥ ‚àò (¬∑ + dt)) v (t‚ÇÄ - dt) := by
  rw [isIntegralCurveAt_iff'] at *
  obtain ‚ü®Œµ, hŒµ, h‚ü© := hŒ≥
  refine ‚ü®Œµ, hŒµ, ?_‚ü©
  convert h.comp_add dt
  ext t
  rw [mem_setOf_eq, Metric.mem_ball, Metric.mem_ball, dist_sub_eq_dist_add_right]

lemma isIntegralCurveAt_comp_add (dt : ‚Ñù) :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî IsIntegralCurveAt (Œ≥ ‚àò (¬∑ + dt)) v (t‚ÇÄ - dt) := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_add (-dt)
  ¬∑ ext t
    simp only [Function.comp_apply, neg_add_cancel_right]
  ¬∑ simp only [sub_neg_eq_add, sub_add_cancel]

lemma isIntegralCurveAt_comp_sub (dt : ‚Ñù) :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî IsIntegralCurveAt (Œ≥ ‚àò (¬∑ - dt)) v (t‚ÇÄ + dt) := by
  simpa using isIntegralCurveAt_comp_add (-dt)

lemma IsIntegralCurve.comp_add (hŒ≥ : IsIntegralCurve Œ≥ v) (dt : ‚Ñù) :
    IsIntegralCurve (Œ≥ ‚àò (¬∑ + dt)) v := by
  rw [isIntegralCurve_iff_isIntegralCurveOn] at *
  exact hŒ≥.comp_add _

lemma isIntegralCurve_comp_add (dt : ‚Ñù) :
    IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurve (Œ≥ ‚àò (¬∑ + dt)) v := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_add (-dt)
  ext t
  simp only [Function.comp_apply, neg_add_cancel_right]

lemma isIntegralCurve_comp_sub (dt : ‚Ñù) :
    IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurve (Œ≥ ‚àò (¬∑ - dt)) v := by
  simpa using isIntegralCurve_comp_add (-dt)

end Translation

/-! ### Scaling lemmas -/

section Scaling

lemma IsIntegralCurveOn.comp_mul (hŒ≥ : IsIntegralCurveOn Œ≥ v s) (a : ‚Ñù) :
    IsIntegralCurveOn (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) { t | t * a ‚àà s } := by
  intros t ht
  rw [comp_apply, Pi.smul_apply, ‚Üê ContinuousLinearMap.smulRight_comp]
  refine HasMFDerivAt.comp t (hŒ≥ (t * a) ht) ‚ü®(continuous_mul_right _).continuousAt, ?_‚ü©
  simp only [mfld_simps, hasFDerivWithinAt_univ]
  exact HasFDerivAt.mul_const' (hasFDerivAt_id _) _

lemma isIntegralCurveOn_comp_mul_ne_zero {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurveOn Œ≥ v s ‚Üî IsIntegralCurveOn (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) { t | t * a ‚àà s } := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_mul a, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_mul a‚Åª¬π
  ¬∑ ext t
    simp only [Function.comp_apply, mul_assoc, inv_mul_eq_div, div_self ha, mul_one]
  ¬∑ simp only [smul_smul, inv_mul_eq_div, div_self ha, one_smul]
  ¬∑ simp only [mem_setOf_eq, mul_assoc, inv_mul_eq_div, div_self ha, mul_one, setOf_mem_eq]

-- TODO: missing lemma(s) about Ioo
lemma isIntegralCurveOn_Ioo_comp_mul_pos {Œµ a : ‚Ñù} (ha : 0 < a) :
    IsIntegralCurveOn Œ≥ v (Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)) ‚Üî
      IsIntegralCurveOn (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) (Ioo ((t‚ÇÄ - Œµ) / a) ((t‚ÇÄ + Œµ) / a)) := by
  have : {t | t * a ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)} = Ioo ((t‚ÇÄ - Œµ) / a) ((t‚ÇÄ + Œµ) / a) := by
    ext t
    rw [mem_setOf, mem_Ioo, mem_Ioo, div_lt_iff‚ÇÄ ha, lt_div_iff‚ÇÄ ha]
  rw [isIntegralCurveOn_comp_mul_ne_zero (ne_of_gt ha), this]

lemma isIntegralCurveOn_Ioo_comp_mul_neg {Œµ a : ‚Ñù} (ha : a < 0) :
    IsIntegralCurveOn Œ≥ v (Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)) ‚Üî
      IsIntegralCurveOn (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) (Ioo ((t‚ÇÄ + Œµ) / a) ((t‚ÇÄ - Œµ) / a)) := by
  have : {t | t * a ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)} = Ioo ((t‚ÇÄ + Œµ) / a) ((t‚ÇÄ - Œµ) / a) := by
    ext t
    rw [mem_setOf, mem_Ioo, mem_Ioo, div_lt_iff_of_neg ha, lt_div_iff_of_neg ha, and_comm]
  rw [isIntegralCurveOn_comp_mul_ne_zero (ne_of_lt ha), this]

lemma IsIntegralCurveAt.comp_mul_ne_zero (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurveAt (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) (t‚ÇÄ / a) := by
  rw [isIntegralCurveAt_iff'] at *
  obtain ‚ü®Œµ, hŒµ, h‚ü© := hŒ≥
  refine ‚ü®Œµ / |a|, by positivity, ?_‚ü©
  convert h.comp_mul a
  ext t
  rw [mem_setOf_eq, Metric.mem_ball, Metric.mem_ball, Real.dist_eq, Real.dist_eq,
    lt_div_iff‚ÇÄ (abs_pos.mpr ha), ‚Üê abs_mul, sub_mul, div_mul_cancel‚ÇÄ _ ha]

lemma isIntegralCurveAt_comp_mul_ne_zero {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî IsIntegralCurveAt (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) (t‚ÇÄ / a) := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_mul_ne_zero ha, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_mul_ne_zero (inv_ne_zero ha)
  ¬∑ ext t
    simp only [Function.comp_apply, mul_assoc, inv_mul_eq_div, div_self ha, mul_one]
  ¬∑ simp only [smul_smul, inv_mul_eq_div, div_self ha, one_smul]
  ¬∑ simp only [div_inv_eq_mul, div_mul_cancel‚ÇÄ _ ha]

lemma IsIntegralCurve.comp_mul (hŒ≥ : IsIntegralCurve Œ≥ v) (a : ‚Ñù) :
    IsIntegralCurve (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) := by
  rw [isIntegralCurve_iff_isIntegralCurveOn] at *
  exact hŒ≥.comp_mul _

lemma isIntegralCurve_comp_mul_ne_zero {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurve (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_mul _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_mul a‚Åª¬π
  ¬∑ ext t
    simp only [Function.comp_apply, mul_assoc, inv_mul_eq_div, div_self ha, mul_one]
  ¬∑ simp only [smul_smul, inv_mul_eq_div, div_self ha, one_smul]

/-- If the vector field `v` vanishes at `x‚ÇÄ`, then the constant curve at `x‚ÇÄ`
is a global integral curve of `v`. -/
lemma isIntegralCurve_const {x : M} (h : v x = 0) : IsIntegralCurve (fun _ ‚Ü¶ x) v := by
  intro t
  rw [h, ‚Üê ContinuousLinearMap.zero_apply (R‚ÇÅ := ‚Ñù) (R‚ÇÇ := ‚Ñù) (1 : ‚Ñù),
    ContinuousLinearMap.smulRight_one_one]
  exact hasMFDerivAt_const ..

end Scaling

/-! ### Existence and uniqueness -/

section ExistUnique

variable [SmoothManifoldWithCorners I M] (t‚ÇÄ) {x‚ÇÄ : M}

/-- Existence of local integral curves for a $C^1$ vector field at interior points of a smooth
manifold. -/
theorem exists_isIntegralCurveAt_of_contMDiffAt [CompleteSpace E]
    (hv : ContMDiffAt I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)) x‚ÇÄ)
    (hx : I.IsInteriorPoint x‚ÇÄ) :
    ‚àÉ Œ≥ : ‚Ñù ‚Üí M, Œ≥ t‚ÇÄ = x‚ÇÄ ‚àß IsIntegralCurveAt Œ≥ v t‚ÇÄ := by
  -- express the differentiability of the vector field `v` in the local chart
  rw [contMDiffAt_iff] at hv
  obtain ‚ü®_, hv‚ü© := hv
  -- use Picard-Lindel√∂f theorem to extract a solution to the ODE in the local chart
  obtain ‚ü®f, hf1, hf2‚ü© := exists_forall_hasDerivAt_Ioo_eq_of_contDiffAt t‚ÇÄ
    (hv.contDiffAt (range_mem_nhds_isInteriorPoint hx)).snd
  simp_rw [‚Üê Real.ball_eq_Ioo, ‚Üê Metric.eventually_nhds_iff_ball] at hf2
  -- use continuity of `f` so that `f t` remains inside `interior (extChartAt I x‚ÇÄ).target`
  have ‚ü®a, ha, hf2'‚ü© := Metric.eventually_nhds_iff_ball.mp hf2
  have hcont := (hf2' t‚ÇÄ (Metric.mem_ball_self ha)).continuousAt
  rw [continuousAt_def, hf1] at hcont
  have hnhds : f ‚Åª¬π' (interior (extChartAt I x‚ÇÄ).target) ‚àà ùìù t‚ÇÄ :=
    hcont _ (isOpen_interior.mem_nhds ((I.isInteriorPoint_iff).mp hx))
  rw [‚Üê eventually_mem_nhds_iff] at hnhds
  -- obtain a neighbourhood `s` so that the above conditions both hold in `s`
  obtain ‚ü®s, hs, haux‚ü© := (hf2.and hnhds).exists_mem
  -- prove that `Œ≥ := (extChartAt I x‚ÇÄ).symm ‚àò f` is a desired integral curve
  refine ‚ü®(extChartAt I x‚ÇÄ).symm ‚àò f,
    Eq.symm (by rw [Function.comp_apply, hf1, PartialEquiv.left_inv _ (mem_extChartAt_source ..)]),
    isIntegralCurveAt_iff.mpr ‚ü®s, hs, ?_‚ü©‚ü©
  intros t ht
  -- collect useful terms in convenient forms
  let x‚Çú : M := (extChartAt I x‚ÇÄ).symm (f t) -- `x‚Çú := Œ≥ t`
  have h : HasDerivAt f (x := t) <| fderivWithin ‚Ñù (extChartAt I x‚ÇÄ ‚àò (extChartAt I x‚Çú).symm)
    (range I) (extChartAt I x‚Çú x‚Çú) (v x‚Çú) := (haux t ht).1
  rw [‚Üê tangentCoordChange_def] at h
  have hf3 := mem_preimage.mp <| mem_of_mem_nhds (haux t ht).2
  have hf3' := mem_of_mem_of_subset hf3 interior_subset
  have hft1 := mem_preimage.mp <|
    mem_of_mem_of_subset hf3' (extChartAt I x‚ÇÄ).target_subset_preimage_source
  have hft2 := mem_extChartAt_source (I := I) x‚Çú
  -- express the derivative of the integral curve in the local chart
  refine ‚ü®(continuousAt_extChartAt_symm'' hf3').comp h.continuousAt,
    HasDerivWithinAt.hasFDerivWithinAt ?_‚ü©
  simp only [mfld_simps, hasDerivWithinAt_univ]
  show HasDerivAt ((extChartAt I x‚Çú ‚àò (extChartAt I x‚ÇÄ).symm) ‚àò f) (v x‚Çú) t
  -- express `v (Œ≥ t)` as `D‚Åª¬π D (v (Œ≥ t))`, where `D` is a change of coordinates, so we can use
  -- `HasFDerivAt.comp_hasDerivAt` on `h`
  rw [‚Üê tangentCoordChange_self (I := I) (x := x‚Çú) (z := x‚Çú) (v := v x‚Çú) hft2,
    ‚Üê tangentCoordChange_comp (x := x‚ÇÄ) ‚ü®‚ü®hft2, hft1‚ü©, hft2‚ü©]
  apply HasFDerivAt.comp_hasDerivAt _ _ h
  apply HasFDerivWithinAt.hasFDerivAt (s := range I) _ <|
    mem_nhds_iff.mpr ‚ü®interior (extChartAt I x‚ÇÄ).target,
      subset_trans interior_subset (extChartAt_target_subset_range ..),
      isOpen_interior, hf3‚ü©
  rw [‚Üê (extChartAt I x‚ÇÄ).right_inv hf3']
  exact hasFDerivWithinAt_tangentCoordChange ‚ü®hft1, hft2‚ü©

/-- Existence of local integral curves for a $C^1$ vector field on a smooth manifold without
boundary. -/
lemma exists_isIntegralCurveAt_of_contMDiffAt_boundaryless
    [CompleteSpace E] [BoundarylessManifold I M]
    (hv : ContMDiffAt I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)) x‚ÇÄ) :
    ‚àÉ Œ≥ : ‚Ñù ‚Üí M, Œ≥ t‚ÇÄ = x‚ÇÄ ‚àß IsIntegralCurveAt Œ≥ v t‚ÇÄ :=
  exists_isIntegralCurveAt_of_contMDiffAt t‚ÇÄ hv BoundarylessManifold.isInteriorPoint

variable {t‚ÇÄ}

/-- Local integral curves are unique.

If a $C^1$ vector field `v` admits two local integral curves `Œ≥ Œ≥' : ‚Ñù ‚Üí M` at `t‚ÇÄ` with
`Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ`, then `Œ≥` and `Œ≥'` agree on some open interval containing `t‚ÇÄ`. -/
theorem isIntegralCurveAt_eventuallyEq_of_contMDiffAt (hŒ≥t‚ÇÄ : I.IsInteriorPoint (Œ≥ t‚ÇÄ))
    (hv : ContMDiffAt I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)) (Œ≥ t‚ÇÄ))
    (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) (hŒ≥' : IsIntegralCurveAt Œ≥' v t‚ÇÄ) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    Œ≥ =·∂†[ùìù t‚ÇÄ] Œ≥' := by
  -- first define `v'` as the vector field expressed in the local chart around `Œ≥ t‚ÇÄ`
  -- this is basically what the function looks like when `hv` is unfolded
  set v' : E ‚Üí E := fun x ‚Ü¶
    tangentCoordChange I ((extChartAt I (Œ≥ t‚ÇÄ)).symm x) (Œ≥ t‚ÇÄ) ((extChartAt I (Œ≥ t‚ÇÄ)).symm x)
      (v ((extChartAt I (Œ≥ t‚ÇÄ)).symm x)) with hv'
  -- extract a set `s` on which `v'` is Lipschitz
  rw [contMDiffAt_iff] at hv
  obtain ‚ü®_, hv‚ü© := hv
  obtain ‚ü®K, s, hs, hlip‚ü© : ‚àÉ K, ‚àÉ s ‚àà ùìù _, LipschitzOnWith K v' s :=
    (hv.contDiffAt (range_mem_nhds_isInteriorPoint hŒ≥t‚ÇÄ)).snd.exists_lipschitzOnWith
  have hlip (t : ‚Ñù) : LipschitzOnWith K ((fun _ ‚Ü¶ v') t) ((fun _ ‚Ü¶ s) t) := hlip
  -- internal lemmas to reduce code duplication
  have hsrc {g} (hg : IsIntegralCurveAt g v t‚ÇÄ) :
    ‚àÄ·∂† t in ùìù t‚ÇÄ, g ‚Åª¬π' (extChartAt I (g t‚ÇÄ)).source ‚àà ùìù t := eventually_mem_nhds_iff.mpr <|
      continuousAt_def.mp hg.continuousAt _ <| extChartAt_source_mem_nhds (g t‚ÇÄ)
  have hmem {g : ‚Ñù ‚Üí M} {t} (ht : g ‚Åª¬π' (extChartAt I (g t‚ÇÄ)).source ‚àà ùìù t) :
    g t ‚àà (extChartAt I (g t‚ÇÄ)).source := mem_preimage.mp <| mem_of_mem_nhds ht
  have hdrv {g} (hg : IsIntegralCurveAt g v t‚ÇÄ) (h' : Œ≥ t‚ÇÄ = g t‚ÇÄ) : ‚àÄ·∂† t in ùìù t‚ÇÄ,
      HasDerivAt ((extChartAt I (g t‚ÇÄ)) ‚àò g) ((fun _ ‚Ü¶ v') t (((extChartAt I (g t‚ÇÄ)) ‚àò g) t)) t ‚àß
      ((extChartAt I (g t‚ÇÄ)) ‚àò g) t ‚àà (fun _ ‚Ü¶ s) t := by
    apply Filter.Eventually.and
    ¬∑ apply (hsrc hg |>.and hg.eventually_hasDerivAt).mono
      rintro t ‚ü®ht1, ht2‚ü©
      rw [hv', h']
      apply ht2.congr_deriv
      congr <;>
      rw [Function.comp_apply, PartialEquiv.left_inv _ (hmem ht1)]
    ¬∑ apply ((continuousAt_extChartAt (g t‚ÇÄ)).comp hg.continuousAt).preimage_mem_nhds
      rw [Function.comp_apply, ‚Üê h']
      exact hs
  have heq {g} (hg : IsIntegralCurveAt g v t‚ÇÄ) :
    g =·∂†[ùìù t‚ÇÄ] (extChartAt I (g t‚ÇÄ)).symm ‚àò ‚Üë(extChartAt I (g t‚ÇÄ)) ‚àò g := by
    apply (hsrc hg).mono
    intros t ht
    rw [Function.comp_apply, Function.comp_apply, PartialEquiv.left_inv _ (hmem ht)]
  -- main proof
  suffices (extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥ =·∂†[ùìù t‚ÇÄ] (extChartAt I (Œ≥' t‚ÇÄ)) ‚àò Œ≥' from
    (heq hŒ≥).trans <| (this.fun_comp (extChartAt I (Œ≥ t‚ÇÄ)).symm).trans (h ‚ñ∏ (heq hŒ≥').symm)
  exact ODE_solution_unique_of_eventually hlip
    (hdrv hŒ≥ rfl) (hdrv hŒ≥' h) (by rw [Function.comp_apply, Function.comp_apply, h])

theorem isIntegralCurveAt_eventuallyEq_of_contMDiffAt_boundaryless [BoundarylessManifold I M]
    (hv : ContMDiffAt I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)) (Œ≥ t‚ÇÄ))
    (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) (hŒ≥' : IsIntegralCurveAt Œ≥' v t‚ÇÄ) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    Œ≥ =·∂†[ùìù t‚ÇÄ] Œ≥' :=
  isIntegralCurveAt_eventuallyEq_of_contMDiffAt BoundarylessManifold.isInteriorPoint hv hŒ≥ hŒ≥' h

variable [T2Space M] {a b : ‚Ñù}

/-- Integral curves are unique on open intervals.

If a $C^1$ vector field `v` admits two integral curves `Œ≥ Œ≥' : ‚Ñù ‚Üí M` on some open interval
`Ioo a b`, and `Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ` for some `t ‚àà Ioo a b`, then `Œ≥` and `Œ≥'` agree on `Ioo a b`. -/
theorem isIntegralCurveOn_Ioo_eqOn_of_contMDiff (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b)
    (hŒ≥t : ‚àÄ t ‚àà Ioo a b, I.IsInteriorPoint (Œ≥ t))
    (hv : ContMDiff I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b)) (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a b))
    (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : EqOn Œ≥ Œ≥' (Ioo a b) := by
  set s := {t | Œ≥ t = Œ≥' t} ‚à© Ioo a b with hs
  -- since `Ioo a b` is connected, we get `s = Ioo a b` by showing that `s` is clopen in `Ioo a b`
  -- in the subtype topology (`s` is also non-empty by assumption)
  -- here we use a slightly weaker alternative theorem
  suffices hsub : Ioo a b ‚äÜ s from fun t ht ‚Ü¶ mem_setOf.mp ((subset_def ‚ñ∏ hsub) t ht).1
  apply isPreconnected_Ioo.subset_of_closure_inter_subset (s := Ioo a b) (u := s) _
    ‚ü®t‚ÇÄ, ‚ü®ht‚ÇÄ, ‚ü®h, ht‚ÇÄ‚ü©‚ü©‚ü©
  ¬∑ -- is this really the most convenient way to pass to subtype topology?
    -- TODO: shorten this when better API around subtype topology exists
    rw [hs, inter_comm, ‚Üê Subtype.image_preimage_val, inter_comm, ‚Üê Subtype.image_preimage_val,
      image_subset_image_iff Subtype.val_injective, preimage_setOf_eq]
    intros t ht
    rw [mem_preimage, ‚Üê closure_subtype] at ht
    revert ht t
    apply IsClosed.closure_subset (isClosed_eq _ _)
    ¬∑ rw [continuous_iff_continuousAt]
      rintro ‚ü®_, ht‚ü©
      apply ContinuousAt.comp _ continuousAt_subtype_val
      rw [Subtype.coe_mk]
      exact hŒ≥.continuousAt ht
    ¬∑ rw [continuous_iff_continuousAt]
      rintro ‚ü®_, ht‚ü©
      apply ContinuousAt.comp _ continuousAt_subtype_val
      rw [Subtype.coe_mk]
      exact hŒ≥'.continuousAt ht
  ¬∑ rw [isOpen_iff_mem_nhds]
    intro t‚ÇÅ ht‚ÇÅ
    have hmem := Ioo_mem_nhds ht‚ÇÅ.2.1 ht‚ÇÅ.2.2
    have heq : Œ≥ =·∂†[ùìù t‚ÇÅ] Œ≥' := isIntegralCurveAt_eventuallyEq_of_contMDiffAt
      (hŒ≥t _ ht‚ÇÅ.2) hv.contMDiffAt (hŒ≥.isIntegralCurveAt hmem) (hŒ≥'.isIntegralCurveAt hmem) ht‚ÇÅ.1
    apply (heq.and hmem).mono
    exact fun _ ht ‚Ü¶ ht

theorem isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless [BoundarylessManifold I M]
    (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b)
    (hv : ContMDiff I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b)) (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a b))
    (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : EqOn Œ≥ Œ≥' (Ioo a b) :=
  isIntegralCurveOn_Ioo_eqOn_of_contMDiff
    ht‚ÇÄ (fun _ _ ‚Ü¶ BoundarylessManifold.isInteriorPoint) hv hŒ≥ hŒ≥' h

/-- Global integral curves are unique.

If a continuously differentiable vector field `v` admits two global integral curves
`Œ≥ Œ≥' : ‚Ñù ‚Üí M`, and `Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ` for some `t‚ÇÄ`, then `Œ≥` and `Œ≥'` are equal. -/
theorem isIntegralCurve_eq_of_contMDiff (hŒ≥t : ‚àÄ t, I.IsInteriorPoint (Œ≥ t))
    (hv : ContMDiff I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurve Œ≥ v) (hŒ≥' : IsIntegralCurve Œ≥' v) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : Œ≥ = Œ≥' := by
  ext t
  obtain ‚ü®T, ht‚ÇÄ, ht‚ü© : ‚àÉ T, t ‚àà Ioo (-T) T ‚àß t‚ÇÄ ‚àà Ioo (-T) T := by
    obtain ‚ü®T, hT‚ÇÅ, hT‚ÇÇ‚ü© := exists_abs_lt t
    obtain ‚ü®hT‚ÇÇ, hT‚ÇÉ‚ü© := abs_lt.mp hT‚ÇÇ
    obtain ‚ü®S, hS‚ÇÅ, hS‚ÇÇ‚ü© := exists_abs_lt t‚ÇÄ
    obtain ‚ü®hS‚ÇÇ, hS‚ÇÉ‚ü© := abs_lt.mp hS‚ÇÇ
    exact ‚ü®T + S, by constructor <;> constructor <;> linarith‚ü©
  exact isIntegralCurveOn_Ioo_eqOn_of_contMDiff ht (fun t _ ‚Ü¶ hŒ≥t t) hv
    ((hŒ≥.isIntegralCurveOn _).mono (subset_univ _))
    ((hŒ≥'.isIntegralCurveOn _).mono (subset_univ _)) h ht‚ÇÄ

theorem isIntegralCurve_Ioo_eq_of_contMDiff_boundaryless [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurve Œ≥ v) (hŒ≥' : IsIntegralCurve Œ≥' v) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : Œ≥ = Œ≥' :=
  isIntegralCurve_eq_of_contMDiff (fun _ ‚Ü¶ BoundarylessManifold.isInteriorPoint) hv hŒ≥ hŒ≥' h

/-- For a global integral curve `Œ≥`, if it crosses itself at `a b : ‚Ñù`, then it is periodic with
period `a - b`. -/
lemma IsIntegralCurve.periodic_of_eq [BoundarylessManifold I M]
    (hŒ≥ : IsIntegralCurve Œ≥ v)
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    (heq : Œ≥ a = Œ≥ b) : Periodic Œ≥ (a - b) := by
  intro t
  apply congrFun <|
    isIntegralCurve_Ioo_eq_of_contMDiff_boundaryless (t‚ÇÄ := b) hv (hŒ≥.comp_add _) hŒ≥ _
  rw [comp_apply, add_sub_cancel, heq]

/-- A global integral curve is injective xor periodic with positive period. -/
lemma IsIntegralCurve.periodic_xor_injective [BoundarylessManifold I M]
    (hŒ≥ : IsIntegralCurve Œ≥ v)
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) :
    Xor' (‚àÉ T > 0, Periodic Œ≥ T) (Injective Œ≥) := by
  rw [xor_iff_iff_not]
  refine ‚ü®fun ‚ü®T, hT, hf‚ü© ‚Ü¶ hf.not_injective (ne_of_gt hT), ?_‚ü©
  intro h
  rw [Injective] at h
  push_neg at h
  obtain ‚ü®a, b, heq, hne‚ü© := h
  refine ‚ü®|a - b|, ?_, ?_‚ü©
  ¬∑ rw [gt_iff_lt, abs_pos, sub_ne_zero]
    exact hne
  ¬∑ by_cases hab : a - b < 0
    ¬∑ rw [abs_of_neg hab, neg_sub]
      exact hŒ≥.periodic_of_eq hv heq.symm
    ¬∑ rw [not_lt] at hab
      rw [abs_of_nonneg hab]
      exact hŒ≥.periodic_of_eq hv heq

section UniformTime

/-- This is the uniqueness theorem of integral curves applied to a real-indexed family of integral
  curves with the same starting point. -/
lemma eqOn_of_isIntegralCurveOn_Ioo [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) {x : M}
    (Œ≥ : ‚Ñù ‚Üí ‚Ñù ‚Üí M) (hŒ≥x : ‚àÄ a, Œ≥ a 0 = x) (hŒ≥ : ‚àÄ a, IsIntegralCurveOn (Œ≥ a) v (Ioo (-a) a))
    {a a' : ‚Ñù} (hpos : 0 < a') (hle : a' ‚â§ a) :
    EqOn (Œ≥ a') (Œ≥ a) (Ioo (-a') a') := by
  apply isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless _ hv (hŒ≥ a') ((hŒ≥ a).mono _)
    (by rw [hŒ≥x a, hŒ≥x a'])
  ¬∑ rw [mem_Ioo]
    exact ‚ü®neg_lt_zero.mpr hpos, by positivity‚ü©
  ¬∑ apply Ioo_subset_Ioo <;> linarith

/-- Suppose for every `a : ‚Ñù`, there exists an integral curve `Œ≥ a` on `Ioo (-a) a`, all with the
  same starting point `Œ≥ 0 = x`. Then, the global curve `Œ≥_ext := fun t ‚Ü¶ Œ≥ (|t| + 1) t` agrees with
  each `Œ≥ a` on `Ioo (-a) a`. This will help us show that `Œ≥_ext` is a global integral curve. -/
lemma exists_integralCurve_of_exists_isIntegralCurveOn_Ioo_eqOn_aux [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) {x : M}
    (h : ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)) {a : ‚Ñù} :
    EqOn (fun t' ‚Ü¶ choose (h (|t'| + 1)) t') (choose (h a)) (Ioo (-a) a) := by
  let Œ≥ := fun a' ‚Ü¶ choose (h a')
  have hŒ≥x := fun a' ‚Ü¶ (choose_spec (h a')).1
  have hŒ≥ := fun a' ‚Ü¶ (choose_spec (h a')).2
  intros t' ht'
  by_cases hlt : |t'| + 1 < a
  ¬∑ exact eqOn_of_isIntegralCurveOn_Ioo hv Œ≥ hŒ≥x hŒ≥
      (by positivity) (le_of_lt hlt) (abs_lt.mp <| lt_add_one _)
  ¬∑ exact eqOn_of_isIntegralCurveOn_Ioo hv Œ≥ hŒ≥x hŒ≥
      (neg_lt_self_iff.mp <| lt_trans ht'.1 ht'.2) (not_lt.mp hlt) ht' |>.symm

/-- If for every `a : ‚Ñù`, there exists an integral curve defined on `Ioo (-a) a`, all with the
  same starting point `x`, then there exists a global integral curve starting at `x`. -/
lemma exists_integralCurve_of_exists_isIntegralCurveOn_Ioo [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) {x : M}
    (h : ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)) :
    ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurve Œ≥ v := by
  refine ‚ü®fun t' ‚Ü¶ choose (h (|t'| + 1)) t', (choose_spec (h (|0| + 1))).1, ?_‚ü©
  intro t
  apply HasMFDerivAt.congr_of_eventuallyEq (f := choose (h (|t| + 1)))
  ¬∑ apply (choose_spec (h (|t| + 1))).2 t
    rw [mem_Ioo, ‚Üê abs_lt]
    exact lt_add_one _
  ¬∑ rw [Filter.eventuallyEq_iff_exists_mem]
    refine ‚ü®Ioo (-(|t| + 1)) (|t| + 1), ?_,
      exists_integralCurve_of_exists_isIntegralCurveOn_Ioo_eqOn_aux hv h‚ü©
    ¬∑ have := lt_add_of_pos_right |t| zero_lt_one
      rw [abs_lt] at this
      exact Ioo_mem_nhds this.1 this.2

lemma exists_isIntegralCurve_iff_exists_isIntegralCurveOn_Ioo [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) (x : M) :
    (‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurve Œ≥ v) ‚Üî
      ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a) :=
  ‚ü®fun ‚ü®Œ≥, h1, h2‚ü© _ ‚Ü¶ ‚ü®Œ≥, h1, h2.isIntegralCurveOn _‚ü©,
   exists_integralCurve_of_exists_isIntegralCurveOn_Ioo hv‚ü©

lemma piecewise_eqOn_symm [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    {a b a' b' : ‚Ñù} (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b))
    (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a' b'))
    (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b ‚à© Ioo a' b') (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    EqOn (piecewise (Ioo a b) Œ≥ Œ≥') Œ≥' (Ioo a' b') := by
  intros t ht
  suffices H : EqOn Œ≥ Œ≥' (Ioo (max a a') (min b b')) by
    by_cases hmem : t ‚àà Ioo a b
    ¬∑ rw [piecewise, if_pos hmem]
      apply H
      simp [ht.1, ht.2, hmem.1, hmem.2]
    ¬∑ rw [piecewise, if_neg hmem]
  apply isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless _ hv
    (hŒ≥.mono (Ioo_subset_Ioo (le_max_left ..) (min_le_left ..)))
    (hŒ≥'.mono (Ioo_subset_Ioo (le_max_right ..) (min_le_right ..))) h
  exact ‚ü®max_lt ht‚ÇÄ.1.1 ht‚ÇÄ.2.1, lt_min ht‚ÇÄ.1.2 ht‚ÇÄ.2.2‚ü©

/-- The extension of an integral curve by another integral curve is an integral curve.

  If two integral curves are defined on overlapping open intervals, and they agree at a point in
  their common domain, then they can be patched together to form a longer integral curve.

  This is stated for manifolds without boundary for simplicity. We actually only need to assume that
  the images of `Œ≥` and `Œ≥'` lie in the interior of the manifold. -/
lemma isIntegralCurveOn_piecewise [BoundarylessManifold I M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    {a b a' b' : ‚Ñù} (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b))
    (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a' b')) {t‚ÇÄ : ‚Ñù}
    (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b ‚à© Ioo a' b') (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    IsIntegralCurveOn (piecewise (Ioo a b) Œ≥ Œ≥') v (Ioo a b ‚à™ Ioo a' b') := by
  intros t ht
  by_cases hmem : t ‚àà Ioo a b
  ¬∑ rw [piecewise, if_pos hmem]
    apply (hŒ≥ t hmem).congr_of_eventuallyEq
    rw [Filter.eventuallyEq_iff_exists_mem]
    refine ‚ü®Ioo a b, isOpen_Ioo.mem_nhds hmem, ?_‚ü©
    intros t' ht'
    rw [piecewise, if_pos ht']
  ¬∑ rw [mem_union, or_iff_not_imp_left] at ht
    have hmem' := ht hmem
    rw [piecewise, if_neg hmem]
    apply (hŒ≥' t hmem').congr_of_eventuallyEq
    rw [Filter.eventuallyEq_iff_exists_mem]
    refine ‚ü®Ioo a' b', isOpen_Ioo.mem_nhds hmem', piecewise_eqOn_symm hv hŒ≥ hŒ≥' ht‚ÇÄ h‚ü©

/-- If there exists `Œµ > 0` such that the local integral curve at each point `x : M` is defined at
  least on an open interval `Ioo (-Œµ) Œµ`, then every point on `M` has a global integral
  curve passing through it.

  See Lemma 9.15, Lee -/
lemma exists_isIntegralCurve_of_isIntegralCurveOn [BoundarylessManifold I M]
    {v : (x : M) ‚Üí TangentSpace I x}
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (h : ‚àÄ x : M, ‚àÉ Œ≥ : ‚Ñù ‚Üí M, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-Œµ) Œµ))
    (x : M) : ‚àÉ Œ≥ : ‚Ñù ‚Üí M, Œ≥ 0 = x ‚àß IsIntegralCurve Œ≥ v := by
  let s := {a | ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)}
  suffices hbdd : ¬¨BddAbove s by
    rw [not_bddAbove_iff] at hbdd
    rw [exists_isIntegralCurve_iff_exists_isIntegralCurveOn_Ioo hv]
    intro a
    obtain ‚ü®‚ü®Œ≥, hŒ≥1, hŒ≥2‚ü©, hlt‚ü© := choose_spec (hbdd a)
    exact ‚ü®Œ≥, hŒ≥1, hŒ≥2.mono <| Ioo_subset_Ioo (neg_le_neg <| le_of_lt hlt) (le_of_lt hlt)‚ü©
  intro hbdd
  set asup := sSup s with hasup
  -- we will obtain two integral curves, one centred at some `t‚ÇÄ > 0` with
  -- `0 ‚â§ asup - Œµ < t‚ÇÄ < asup`; let `t‚ÇÄ = asup - Œµ / 2`
  -- another centred at 0 with domain up to `a ‚àà S` with `t‚ÇÄ < a < asup`
  obtain ‚ü®a, ha, hlt‚ü© := Real.add_neg_lt_sSup (‚ü®Œµ, h x‚ü© : Set.Nonempty s) (Œµ := - (Œµ / 2))
    (by rw [neg_lt, neg_zero]; exact half_pos hŒµ)
  rw [mem_setOf] at ha
  rw [‚Üê hasup, ‚Üê sub_eq_add_neg] at hlt

  -- integral curve defined on `Ioo (-a) a`
  obtain ‚ü®Œ≥, h0, hŒ≥‚ü© := ha
  -- integral curve starting at `-(asup - Œµ / 2)` with radius `Œµ`
  obtain ‚ü®Œ≥1_aux, h1_aux, hŒ≥1‚ü© := h (Œ≥ (-(asup - Œµ / 2)))
  rw [isIntegralCurveOn_Ioo_comp_add (asup - Œµ / 2)] at hŒ≥1
  set Œ≥1 := Œ≥1_aux ‚àò (¬∑ + (asup - Œµ / 2)) with Œ≥1_def
  have heq1 : Œ≥1 (-(asup - Œµ / 2)) = Œ≥ (-(asup - Œµ / 2)) := by simp [Œ≥1_def, h1_aux]
  -- integral curve starting at `asup - Œµ / 2` with radius `Œµ`
  obtain ‚ü®Œ≥2_aux, h2_aux, hŒ≥2‚ü© := h (Œ≥ (asup - Œµ / 2))
  rw [isIntegralCurveOn_Ioo_comp_sub (asup - Œµ / 2)] at hŒ≥2
  set Œ≥2 := Œ≥2_aux ‚àò (¬∑ - (asup - Œµ / 2)) with Œ≥2_def
  have heq2 : Œ≥2 (asup - Œµ / 2) = Œ≥ (asup - Œµ / 2) := by simp [Œ≥2_def, h2_aux]

  -- to help `linarith`
  have hŒµle : Œµ ‚â§ asup := le_csSup hbdd (h x)

  -- extend `Œ≥` on the left by `Œ≥1` and on the right by `Œ≥2`
  set Œ≥_ext : ‚Ñù ‚Üí M := piecewise (Ioo (-(asup + Œµ / 2)) a)
    (piecewise (Ioo (-a) a) Œ≥ Œ≥1) Œ≥2 with Œ≥_ext_def
  have heq_ext : Œ≥_ext 0 = x := by
    rw [Œ≥_ext_def, piecewise, if_pos ‚ü®by linarith, by linarith‚ü©, piecewise,
      if_pos ‚ü®by linarith, by linarith‚ü©, h0]
  -- `asup + Œµ / 2` is an element of `s` greater than `asup`, a contradiction
  suffices hext : IsIntegralCurveOn Œ≥_ext v (Ioo (-(asup + Œµ / 2)) (asup + Œµ / 2)) from
    (not_lt.mpr <| le_csSup hbdd ‚ü®Œ≥_ext, heq_ext, hext‚ü©) <| lt_add_of_pos_right asup (half_pos hŒµ)
  apply (isIntegralCurveOn_piecewise (t‚ÇÄ := asup - Œµ / 2) hv _ hŒ≥2
      ‚ü®‚ü®by linarith, hlt‚ü©, ‚ü®by linarith, by linarith‚ü©‚ü©
      (by rw [piecewise, if_pos ‚ü®by linarith, hlt‚ü©, ‚Üê heq2])).mono
    (Ioo_subset_Ioo_union_Ioo le_rfl (by linarith) (by linarith))
  apply (isIntegralCurveOn_piecewise (t‚ÇÄ := -(asup - Œµ / 2)) hv hŒ≥ hŒ≥1
      ‚ü®‚ü®neg_lt_neg hlt, by linarith‚ü©, ‚ü®by linarith, by linarith‚ü©‚ü© heq1.symm).mono
    (union_comm _ _ ‚ñ∏ Ioo_subset_Ioo_union_Ioo (by linarith) (by linarith) le_rfl)

end UniformTime

end ExistUnique
