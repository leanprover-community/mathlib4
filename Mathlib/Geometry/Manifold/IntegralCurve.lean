/-
Copyright (c) 2023 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import Mathlib.Analysis.ODE.Gronwall
import Mathlib.Analysis.ODE.PicardLindelof
import Mathlib.Geometry.Manifold.InteriorBoundary
import Mathlib.Geometry.Manifold.MFDeriv

/-!
# Integral curves of vector fields on a manifold

Let `M` be a manifold and `v : (x : M) ‚Üí TangentSpace I x` be a vector field on `M`. An integral
curve of `v` is a function `Œ≥ : ‚Ñù ‚Üí M` such that the derivative of `Œ≥` at `t` equals `v (Œ≥ t)`. The
integral curve may only be defined for all `t` within some subset of `‚Ñù`.

Assume `v` is continuously differentiable. The existence theorem for solutions to ODEs implies that
a unique local integral curve exists for any continuously differentiable vector field `v`. The
uniqueness theorem for solutions to ODEs implies that integral curves of `v` are unique. These are
the main results of this file.

## Main definition

Let `v : M ‚Üí TM` be a vector field on `M`, and let `Œ≥ : ‚Ñù ‚Üí M`.
- **`IsIntegralCurve Œ≥ v`**: `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t : ‚Ñù`. That is, `Œ≥` is a global
integral curve of `v`.
- **`IsIntegralCurveOn Œ≥ v s`**: `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t ‚àà s`, where `s : Set ‚Ñù`.
- **`IsIntegralCurveAt Œ≥ v t‚ÇÄ`**: `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t` in some open interval
around `t‚ÇÄ`. That is, `Œ≥` is a local integral curve of `v`.

For `IsIntegralCurveOn Œ≥ v s` and `IsIntegralCurveAt Œ≥ v t‚ÇÄ`, even though `Œ≥` is defined for all
time, its value outside of the set `s` or a small interval around `t‚ÇÄ` is irrelevant and considered
junk.

## Implementation notes

For the existence and uniqueness theorems, we assume that the image of the integral curve lies in
the interior of the manifold. The case where the integral curve may lie on the boundary of the
manifold requires special treatment, and we leave it as a to-do.

The uniqueness theorem requires the manifold to be Hausdorff (T2), so that the set on which two
continuous functions agree is closed.

We state simpler versions of the theorem for manifolds without boundary as corollaries.

## To-do

- The case where the integral curve may venture to the boundary of the manifold. See Theorem 9.34,
  J. M. Lee. May require submanifolds.

## Tags

integral curve, vector field, local existence, uniqueness
-/

open scoped Manifold

open Set Classical

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ‚Ñù E']
  {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners ‚Ñù E' H'}
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [SmoothManifoldWithCorners I' M']

/-- If `Œ≥ : ‚Ñù ‚Üí M`, `v : M ‚Üí TM` is a vector field on `M`, and `s ‚àà Set ‚Ñù`,
  `IsIntegralCurveOn Œ≥ v s` means `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t ‚àà s`. The value of `Œ≥`
  outside of `s` is irrelevant and considered junk.  -/
def IsIntegralCurveOn (Œ≥ : ‚Ñù ‚Üí M) (v : (x : M) ‚Üí TangentSpace I x) (s : Set ‚Ñù) :=
  ‚àÄ (t : ‚Ñù), t ‚àà s ‚Üí HasMFDerivAt ùìò(‚Ñù, ‚Ñù) I Œ≥ t ((1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù).smulRight (v (Œ≥ t)))

/-- If `v : M ‚Üí TM` is a vector field on `M`, and `t‚ÇÄ : ‚Ñù`, `IsIntegralCurveAt Œ≥ v t‚ÇÄ` means
  `Œ≥ : ‚Ñù ‚Üí M` is a local integral curve of `v` in an open interval of `t‚ÇÄ`. That is, there exists
  `Œµ > 0` such that `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)`. The value of
  `Œ≥` outside of this interval is irrelevant and considered junk. -/
def IsIntegralCurveAt (Œ≥ : ‚Ñù ‚Üí M) (v : (x : M) ‚Üí TangentSpace I x) (t : ‚Ñù) :=
  ‚àÉ s ‚àà nhds t, IsIntegralCurveOn Œ≥ v s

/-- If `v : M ‚Üí TM` is a vector field on `M`, `IsIntegralCurve Œ≥ v` means `Œ≥ : ‚Ñù ‚Üí M` is a global
  integral curve of `v`. That is, `Œ≥ t` is tangent to `v (Œ≥ t)` for all `t : ‚Ñù`. -/
def IsIntegralCurve (Œ≥ : ‚Ñù ‚Üí M) (v : (x : M) ‚Üí TangentSpace I x) :=
  ‚àÄ t : ‚Ñù, HasMFDerivAt ùìò(‚Ñù, ‚Ñù) I Œ≥ t ((1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù).smulRight (v (Œ≥ t)))

variable {Œ≥ Œ≥' : ‚Ñù ‚Üí M} {v : (x : M) ‚Üí TangentSpace I x} {s s' : Set ‚Ñù} {t‚ÇÄ : ‚Ñù}

lemma IsIntegralCurve.isIntegralCurveOn (h : IsIntegralCurve Œ≥ v) (s : Set ‚Ñù) :
    IsIntegralCurveOn Œ≥ v s := fun t _ ‚Ü¶ h t

lemma isIntegralCurve_iff_isIntegralCurveOn : IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurveOn Œ≥ v univ :=
  ‚ü®fun h ‚Ü¶ h.isIntegralCurveOn _, fun h t ‚Ü¶ h t (mem_univ _)‚ü©

lemma IsIntegralCurve.isIntegralCurveAt (h : IsIntegralCurve Œ≥ v) (t : ‚Ñù) :
    IsIntegralCurveAt Œ≥ v t := ‚ü®univ, Filter.univ_mem, fun t _ ‚Ü¶ h t‚ü©

lemma isIntegralCurve_iff_isIntegralCurveAt :
    IsIntegralCurve Œ≥ v ‚Üî ‚àÄ t : ‚Ñù, IsIntegralCurveAt Œ≥ v t :=
  ‚ü®fun h ‚Ü¶ h.isIntegralCurveAt, fun h t ‚Ü¶ by
    obtain ‚ü®s, hs, h‚ü© := h t
    exact h t (mem_of_mem_nhds hs)‚ü©

lemma IsIntegralCurveOn.mono (h : IsIntegralCurveOn Œ≥ v s) (hs : s' ‚äÜ s) :
    IsIntegralCurveOn Œ≥ v s' := fun t ht ‚Ü¶ h t (mem_of_mem_of_subset ht hs)

lemma IsIntegralCurveOn.of_union (h : IsIntegralCurveOn Œ≥ v s) (h' : IsIntegralCurveOn Œ≥ v s') :
    IsIntegralCurveOn Œ≥ v (s ‚à™ s') := by
  intros t ht
  rcases ht with ht | ht
  ¬∑ exact h _ ht
  ¬∑ exact h' _ ht

lemma isIntegralCurveAt_iff :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî ‚àÉ Œµ > 0, IsIntegralCurveOn Œ≥ v (Metric.ball t‚ÇÄ Œµ) := by
  refine ‚ü®?_,  fun ‚ü®Œµ, hŒµ, h‚ü© ‚Ü¶ ‚ü®Metric.ball t‚ÇÄ Œµ, Metric.ball_mem_nhds _ hŒµ, h‚ü©‚ü©
  rintro ‚ü®s, hs, h‚ü©
  obtain ‚ü®Œµ, hŒµ, hsub‚ü© := Metric.mem_nhds_iff.mp hs
  exact ‚ü®Œµ, hŒµ, h.mono hsub‚ü©

lemma IsIntegralCurveOn.isIntegralCurveAt (h : IsIntegralCurveOn Œ≥ v s) (hs : s ‚àà nhds t‚ÇÄ) :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ := ‚ü®s, hs, h‚ü©

lemma IsIntegralCurveAt.isIntegralCurveOn (h : ‚àÄ t ‚àà s, IsIntegralCurveAt Œ≥ v t) :
    IsIntegralCurveOn Œ≥ v s := by
  intros t ht
  obtain ‚ü®s, hs, h‚ü© := h t ht
  exact h t (mem_of_mem_nhds hs)

lemma IsIntegralCurveOn.congr_of_eqOn (hs : IsOpen s) (h : IsIntegralCurveOn Œ≥ v s)
    (hŒ≥ : EqOn Œ≥ Œ≥' s) : IsIntegralCurveOn Œ≥' v s := by
  intros t ht
  rw [‚Üê hŒ≥ ht]
  apply (h t ht).congr_of_eventuallyEq
  exact Filter.eventuallyEq_of_mem (hs.mem_nhds ht) hŒ≥.symm

lemma IsIntegralCurveAt.congr_of_eventuallyEq (h : IsIntegralCurveAt Œ≥ v t‚ÇÄ)
    (hŒ≥ : Œ≥ =·∂†[nhds t‚ÇÄ] Œ≥') : IsIntegralCurveAt Œ≥' v t‚ÇÄ := by
  simp_rw [IsIntegralCurveAt, IsIntegralCurveOn, ‚Üê Filter.eventually_iff_exists_mem] at h --lemma?
  obtain ‚ü®s, haux, hs1, hs2‚ü© := eventually_nhds_iff.mp (h.and hŒ≥)
  refine ‚ü®s, hs1.mem_nhds hs2, ?_‚ü©
  intros t ht
  rw [‚Üê (haux t ht).2]
  apply (haux t ht).1.congr_of_eventuallyEq
  rw [Filter.eventuallyEq_iff_exists_mem]
  refine ‚ü®s, hs1.mem_nhds ht, ?_‚ü©
  exact fun t' ht' => (haux t' ht').2.symm

lemma IsIntegralCurve.congr (h : IsIntegralCurve Œ≥ v) (hŒ≥ : Œ≥ = Œ≥') :
    IsIntegralCurve Œ≥' v := by
  rw [isIntegralCurve_iff_isIntegralCurveOn] at *
  apply h.congr_of_eqOn isOpen_univ <| (eqOn_univ Œ≥ Œ≥').mpr hŒ≥

/-! ### Translation lemmas -/

section Translation

lemma IsIntegralCurveOn.comp_add (hŒ≥ : IsIntegralCurveOn Œ≥ v s) (dt : ‚Ñù) :
    IsIntegralCurveOn (Œ≥ ‚àò (¬∑ + dt)) v { t | t + dt ‚àà s } := by
  intros t ht
  rw [Function.comp_apply,
    ‚Üê ContinuousLinearMap.comp_id (ContinuousLinearMap.smulRight 1 (v (Œ≥ (t + dt))))]
  apply HasMFDerivAt.comp t (hŒ≥ (t + dt) ht)
  refine ‚ü®(continuous_add_right _).continuousAt, ?_‚ü©
  simp only [mfld_simps, hasFDerivWithinAt_univ]
  exact HasFDerivAt.add_const (hasFDerivAt_id _) _

lemma isIntegralCurveOn_comp_add {dt : ‚Ñù} :
    IsIntegralCurveOn Œ≥ v s ‚Üî IsIntegralCurveOn (Œ≥ ‚àò (¬∑ + dt)) v { t | t + dt ‚àà s } := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  have := hŒ≥.comp_add (-dt)
  simp only [mem_setOf_eq, neg_add_cancel_right, setOf_mem_eq] at this
  convert this
  ext
  simp only [Function.comp_apply, neg_add_cancel_right]

lemma IsIntegralCurveAt.comp_add (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) (dt : ‚Ñù) :
    IsIntegralCurveAt (Œ≥ ‚àò (¬∑ + dt)) v (t‚ÇÄ - dt) := by
  rw [isIntegralCurveAt_iff] at hŒ≥
  obtain ‚ü®Œµ, hŒµ, h‚ü© := hŒ≥
  refine ‚ü®Metric.ball (t‚ÇÄ - dt) Œµ, Metric.isOpen_ball.mem_nhds (Metric.mem_ball_self hŒµ), ?_‚ü©
  convert h.comp_add dt
  ext t
  rw [mem_setOf_eq, Metric.mem_ball, Metric.mem_ball, dist_sub_eq_dist_add_right]

lemma isIntegralCurveAt_comp_add {dt : ‚Ñù} :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî IsIntegralCurveAt (Œ≥ ‚àò (¬∑ + dt)) v (t‚ÇÄ - dt) := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  have := hŒ≥.comp_add (-dt)
  rw [sub_neg_eq_add, sub_add_cancel] at this
  convert this
  ext
  simp only [Function.comp_apply, neg_add_cancel_right]

lemma IsIntegralCurve.comp_add (hŒ≥ : IsIntegralCurve Œ≥ v) (dt : ‚Ñù) :
    IsIntegralCurve (Œ≥ ‚àò (¬∑ + dt)) v := by
  rw [isIntegralCurve_iff_isIntegralCurveOn] at *
  exact hŒ≥.comp_add _

lemma isIntegralCurve_comp_add {dt : ‚Ñù} :
    IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurve (Œ≥ ‚àò (¬∑ + dt)) v := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_add _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  convert hŒ≥.comp_add (-dt)
  ext
  simp only [Function.comp_apply, neg_add_cancel_right]

end Translation

/-! ### Scaling lemmas -/

section Scaling

lemma IsIntegralCurveOn.comp_mul (hŒ≥ : IsIntegralCurveOn Œ≥ v s) (a : ‚Ñù) :
    IsIntegralCurveOn (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) { t | t * a ‚àà s } := by
  intros t ht
  rw [Function.comp_apply, Pi.smul_apply, ‚Üê ContinuousLinearMap.smulRight_comp]
  refine HasMFDerivAt.comp t (hŒ≥ (t * a) ht) ‚ü®(continuous_mul_right _).continuousAt, ?_‚ü©
  simp only [mfld_simps, hasFDerivWithinAt_univ]
  exact HasFDerivAt.mul_const' (hasFDerivAt_id _) _

lemma isIntegralCurvOn_comp_mul_ne_zero {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurveOn Œ≥ v s ‚Üî IsIntegralCurveOn (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) { t | t * a ‚àà s } := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_mul a, fun hŒ≥ ‚Ü¶ ?_‚ü©
  have := hŒ≥.comp_mul a‚Åª¬π
  simp_rw [smul_smul, inv_mul_eq_div, div_self ha, one_smul, mem_setOf_eq, mul_assoc,
    inv_mul_eq_div, div_self ha, mul_one, setOf_mem_eq] at this
  convert this
  ext t
  rw [Function.comp_apply, Function.comp_apply, mul_assoc, inv_mul_eq_div, div_self ha, mul_one]

lemma IsIntegralCurveAt.comp_mul_ne_zero (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurveAt (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) (t‚ÇÄ / a) := by
  obtain ‚ü®Œµ, hŒµ, h‚ü© := isIntegralCurveAt_iff.mp hŒ≥
  rw [isIntegralCurveAt_iff]
  refine ‚ü®Œµ / |a|, div_pos hŒµ (abs_pos.mpr ha), ?_‚ü©
  convert h.comp_mul a
  ext t
  rw [mem_setOf_eq, Metric.mem_ball, Metric.mem_ball, Real.dist_eq, Real.dist_eq,
    lt_div_iff (abs_pos.mpr ha), ‚Üê abs_mul, sub_mul, div_mul_cancel _ ha]

lemma isIntegralCurveAt_comp_mul_ne_zero {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurveAt Œ≥ v t‚ÇÄ ‚Üî IsIntegralCurveAt (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) (t‚ÇÄ / a) := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_mul_ne_zero ha, fun hŒ≥ ‚Ü¶ ?_‚ü©
  have := hŒ≥.comp_mul_ne_zero (inv_ne_zero ha)
  rw [smul_smul, inv_mul_eq_div, div_self ha, one_smul, ‚Üê div_mul_eq_div_div_swap,
    inv_mul_eq_div, div_self ha, div_one, Function.comp.assoc] at this
  convert this
  ext t
  simp [inv_mul_eq_div, div_self ha]

lemma IsIntegralCurve.comp_mul (hŒ≥ : IsIntegralCurve Œ≥ v) (a : ‚Ñù) :
    IsIntegralCurve (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) := by
  rw [isIntegralCurve_iff_isIntegralCurveOn] at *
  exact hŒ≥.comp_mul _

lemma isIntegralCurve_comp_mul_ne_zero {a : ‚Ñù} (ha : a ‚â† 0) :
    IsIntegralCurve Œ≥ v ‚Üî IsIntegralCurve (Œ≥ ‚àò (¬∑ * a)) (a ‚Ä¢ v) := by
  refine ‚ü®fun hŒ≥ ‚Ü¶ hŒ≥.comp_mul _, fun hŒ≥ ‚Ü¶ ?_‚ü©
  have := hŒ≥.comp_mul a‚Åª¬π
  rw [smul_smul, inv_mul_eq_div, div_self ha, one_smul] at this
  convert this
  ext t
  rw [Function.comp_apply, Function.comp_apply, mul_assoc, inv_mul_eq_div, div_self ha, mul_one]

/-- If the vector field `v` vanishes at `x‚ÇÄ`, then the constant curve at `x‚ÇÄ`
  is a global integral curve of `v`. -/
lemma isIntegralCurve_const {x : M} (h : v x = 0) : IsIntegralCurve (fun _ ‚Ü¶ x) v := by
  intro t
  rw [h, ‚Üê ContinuousLinearMap.zero_apply (R‚ÇÅ := ‚Ñù) (R‚ÇÇ := ‚Ñù) (1 : ‚Ñù),
    ContinuousLinearMap.smulRight_one_one]
  exact hasMFDerivAt_const ..

lemma IsIntegralCurveOn.continuousAt (hŒ≥ : IsIntegralCurveOn Œ≥ v s) (ht : t‚ÇÄ ‚àà s) :
    ContinuousAt Œ≥ t‚ÇÄ := (hŒ≥ t‚ÇÄ ht).1

lemma IsIntegralCurveOn.continuousOn (hŒ≥ : IsIntegralCurveOn Œ≥ v s) :
    ContinuousOn Œ≥ s := fun t ht => (hŒ≥ t ht).1.continuousWithinAt

lemma IsIntegralCurveAt.continuousAt (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) :
    ContinuousAt Œ≥ t‚ÇÄ := by
  obtain ‚ü®Œµ, hŒµ, hŒ≥‚ü© := hŒ≥
  apply hŒ≥.continuousAt
  exact mem_of_mem_nhds hŒµ

lemma IsIntegralCurve.continuous (hŒ≥ : IsIntegralCurve Œ≥ v) :
    Continuous Œ≥ := continuous_iff_continuousAt.mpr
      fun _ => (hŒ≥.isIntegralCurveOn univ).continuousAt (mem_univ _)

end Scaling

variable (t‚ÇÄ) {x‚ÇÄ : M}

/-- For any continuously differentiable vector field and any chosen non-boundary point `x‚ÇÄ` on the
  manifold, there exists an integral curve `Œ≥ : ‚Ñù ‚Üí M` such that `Œ≥ t‚ÇÄ = x‚ÇÄ` and the tangent vector
  of `Œ≥` at `t` coincides with the vector field at `Œ≥ t` for all `t` within an open interval around
  `t‚ÇÄ`.-/
theorem exists_isIntegralCurveAt_of_contMDiffAt
    (hv : ContMDiffAt I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)) x‚ÇÄ)
    (hx : I.IsInteriorPoint x‚ÇÄ) :
    ‚àÉ (Œ≥ : ‚Ñù ‚Üí M), Œ≥ t‚ÇÄ = x‚ÇÄ ‚àß IsIntegralCurveAt Œ≥ v t‚ÇÄ := by
  -- express the differentiability of the vector field `v` in the local chart
  rw [contMDiffAt_iff] at hv
  obtain ‚ü®_, hv‚ü© := hv
  -- use Picard-Lindel√∂f theorem to extract a solution to the ODE in the local chart
  obtain ‚ü®f, hf1, hf2‚ü© := exists_forall_hasDerivAt_Ioo_eq_of_contDiffAt t‚ÇÄ
    (hv.contDiffAt (range_mem_nhds_isInteriorPoint hx)).snd
  simp_rw [‚Üê Real.ball_eq_Ioo, ‚Üê Metric.eventually_nhds_iff_ball] at hf2
  -- use continuity of `f` so that `f t` remains inside `interior (extChartAt I x‚ÇÄ).target`
  have ‚ü®a, ha, hf2'‚ü© := Metric.eventually_nhds_iff_ball.mp hf2
  have hcont := (hf2' t‚ÇÄ (Metric.mem_ball_self ha)).continuousAt
  rw [continuousAt_def, hf1] at hcont
  have hnhds : f ‚Åª¬π' (interior (extChartAt I x‚ÇÄ).target) ‚àà nhds t‚ÇÄ :=
    hcont _ (isOpen_interior.mem_nhds ((I.isInteriorPoint_iff).mp hx))
  rw [‚Üê eventually_mem_nhds] at hnhds
  -- obtain a neighbourhood `s` so that the above conditions both hold in `s`
  obtain ‚ü®s, hs, haux‚ü© := (hf2.and hnhds).exists_mem
  -- prove that `Œ≥ := (extChartAt I x‚ÇÄ).symm ‚àò f` is a desired integral curve
  refine ‚ü®(extChartAt I x‚ÇÄ).symm ‚àò f,
    Eq.symm (by rw [Function.comp_apply, hf1, PartialEquiv.left_inv _ (mem_extChartAt_source ..)]),
    s, hs, ?_‚ü©
  intros t ht
  -- collect useful terms in convenient forms
  have h : HasDerivAt f
    ((fderivWithin ‚Ñù ((extChartAt I x‚ÇÄ) ‚àò (extChartAt I ((extChartAt I x‚ÇÄ).symm (f t))).symm)
        (range I) (extChartAt I ((extChartAt I x‚ÇÄ).symm (f t)) ((extChartAt I x‚ÇÄ).symm (f t))))
      (v ((extChartAt I x‚ÇÄ).symm (f t))))
    t := (haux t ht).1
  rw [‚Üê tangentCoordChange_def] at h
  have hf3 := mem_preimage.mp <| mem_of_mem_nhds (haux t ht).2
  have hf3' := mem_of_mem_of_subset hf3 interior_subset
  have hft1 := mem_preimage.mp <|
    mem_of_mem_of_subset hf3' (extChartAt I x‚ÇÄ).target_subset_preimage_source
  have hft2 := mem_extChartAt_source I ((extChartAt I x‚ÇÄ).symm (f t))
  -- express the derivative of the integral curve in the local chart
  refine ‚ü®(continuousAt_extChartAt_symm'' _ _ hf3').comp h.continuousAt,
    HasDerivWithinAt.hasFDerivWithinAt ?_‚ü©
  simp only [mfld_simps, hasDerivWithinAt_univ]
  show HasDerivAt (((extChartAt I ((extChartAt I x‚ÇÄ).symm (f t))) ‚àò (extChartAt I x‚ÇÄ).symm) ‚àò f)
    (v ((extChartAt I x‚ÇÄ).symm (f t))) t
  -- express `v (Œ≥ t)` as `D‚Åª¬π D (v (Œ≥ t))`, where `D` is a change of coordinates, so we can use
  -- `HasFDerivAt.comp_hasDerivAt` on `h`
  rw [‚Üê tangentCoordChange_self (I := I) (x := (extChartAt I x‚ÇÄ).symm (f t))
      (z := (extChartAt I x‚ÇÄ).symm (f t)) (v := v ((extChartAt I x‚ÇÄ).symm (f t))) hft2,
    ‚Üê tangentCoordChange_comp (x := x‚ÇÄ) ‚ü®‚ü®hft2, hft1‚ü©, hft2‚ü©]
  apply HasFDerivAt.comp_hasDerivAt _ _ h
  apply HasFDerivWithinAt.hasFDerivAt (s := range I) _ <|
    mem_nhds_iff.mpr ‚ü®interior (extChartAt I x‚ÇÄ).target,
      subset_trans interior_subset (extChartAt_target_subset_range ..),
      isOpen_interior, hf3‚ü©
  nth_rw 4 [‚Üê (extChartAt I x‚ÇÄ).right_inv hf3']
  exact hasFDerivWithinAt_tangentCoordChange ‚ü®hft1, hft2‚ü©

variable {t‚ÇÄ}

/-- For any continuously differentiable vector field defined on a manifold without boundary and any
  chosen starting point `x‚ÇÄ : M`, an integral curve `Œ≥ : ‚Ñù ‚Üí M` exists such that `Œ≥ t‚ÇÄ = x‚ÇÄ` and the
  tangent vector of `Œ≥` at `t` coincides with the vector field at `Œ≥ t` for all `t` within an open
  interval around `t‚ÇÄ`. -/
lemma exists_isIntegralCurveAt_of_contMDiffAt_boundaryless [I.Boundaryless]
    (hv : ContMDiffAt I I.tangent 1 (fun x ‚Ü¶ (‚ü®x, v x‚ü© : TangentBundle I M)) x‚ÇÄ) :
    ‚àÉ (Œ≥ : ‚Ñù ‚Üí M), Œ≥ t‚ÇÄ = x‚ÇÄ ‚àß IsIntegralCurveAt Œ≥ v t‚ÇÄ :=
  exists_isIntegralCurveAt_of_contMDiffAt t‚ÇÄ hv I.isInteriorPoint

/-- If `Œ≥` is an integral curve of a vector field `v`, then `Œ≥ t` is tangent to `v (Œ≥ t)` when
  expressed in the local chart around the initial point `Œ≥ t‚ÇÄ`. -/
lemma IsIntegralCurveOn.hasDerivAt (hŒ≥ : IsIntegralCurveOn Œ≥ v s) {t : ‚Ñù} (ht : t ‚àà s)
    (hsrc : Œ≥ t ‚àà (extChartAt I (Œ≥ t‚ÇÄ)).source) :
    HasDerivAt ((extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥)
      ((tangentCoordChange I (Œ≥ t) (Œ≥ t‚ÇÄ) (Œ≥ t)) (v (Œ≥ t))) t := by
  -- turn `HasDerivAt` into comp of `HasMFDerivAt`
  have hsrc := extChartAt_source I (Œ≥ t‚ÇÄ) ‚ñ∏ hsrc
  rw [hasDerivAt_iff_hasFDerivAt, ‚Üê hasMFDerivAt_iff_hasFDerivAt]
  apply (HasMFDerivAt.comp t
    (hasMFDerivAt_extChartAt I hsrc) (hŒ≥ _ ht)).congr_mfderiv
  rw [ContinuousLinearMap.ext_iff]
  intro a
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.smulRight_apply, map_smul,
    ‚Üê ContinuousLinearMap.one_apply (R‚ÇÅ := ‚Ñù) a, ‚Üê ContinuousLinearMap.smulRight_apply,
    mfderiv_chartAt_eq_tangentCoordChange I hsrc]
  rfl

/-- Local integral curves are unique.

  If a continuously differentiable vector field `v` admits two local integral curves `Œ≥ Œ≥' : ‚Ñù ‚Üí M`
  at `t‚ÇÄ` with `Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ`, then `Œ≥` and `Œ≥'` agree on some open interval around `t‚ÇÄ` -/
theorem isIntegralCurveAt_eqOn_of_contMDiffAt (hŒ≥t‚ÇÄ : I.IsInteriorPoint (Œ≥ t‚ÇÄ))
    (hv : ContMDiffAt I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)) (Œ≥ t‚ÇÄ))
    (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) (hŒ≥' : IsIntegralCurveAt Œ≥' v t‚ÇÄ) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    ‚àÉ Œµ > 0, EqOn Œ≥ Œ≥' (Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)) := by
  -- first define `v'` as the vector field expressed in the local chart around `Œ≥ t‚ÇÄ`
  -- this is basically what the function looks like when `hv` is unfolded
  set v' : E ‚Üí E := fun x =>
    tangentCoordChange I ((extChartAt I (Œ≥ t‚ÇÄ)).symm x) (Œ≥ t‚ÇÄ) ((extChartAt I (Œ≥ t‚ÇÄ)).symm x)
      (v ((extChartAt I (Œ≥ t‚ÇÄ)).symm x)) with hv'

  -- extract set `s` on which `v'` is Lipschitz
  rw [contMDiffAt_iff] at hv
  obtain ‚ü®_, hv‚ü© := hv
  obtain ‚ü®K, s, hs, hlip‚ü© : ‚àÉ K, ‚àÉ s ‚àà nhds _, LipschitzOnWith K v' s :=
    ContDiffAt.exists_lipschitzOnWith (hv.contDiffAt (range_mem_nhds_isInteriorPoint hŒ≥t‚ÇÄ)).snd
  have hlip : ‚àÄ t : ‚Ñù, LipschitzOnWith K ((fun _ => v') t) ((fun _ => s) t) := fun _ => hlip

  -- `Œ≥ t` when expressed in the local chart should remain inside `s`
  have hcont : ContinuousAt ((extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥) t‚ÇÄ :=
    (continuousAt_extChartAt ..).comp hŒ≥.continuousAt
  rw [continuousAt_def] at hcont
  have hnhds := hcont _ hs
  rw [‚Üê eventually_mem_nhds] at hnhds

  -- `Œ≥ t` should remain inside the domain of the local chart around `Œ≥ t‚ÇÄ`
  have hsrc := continuousAt_def.mp hŒ≥.continuousAt _ <| extChartAt_source_mem_nhds I (Œ≥ t‚ÇÄ)
  rw [‚Üê eventually_mem_nhds] at hsrc

  -- `Œ≥` is tangent to `v` in some neighbourhood of `t‚ÇÄ`
  simp_rw [IsIntegralCurveAt, IsIntegralCurveOn, ‚Üê Filter.eventually_iff_exists_mem] at hŒ≥

  -- same as above but for `Œ≥'`
  have hcont' : ContinuousAt ((extChartAt I (Œ≥' t‚ÇÄ)) ‚àò Œ≥') t‚ÇÄ :=
    ContinuousAt.comp (continuousAt_extChartAt ..) hŒ≥'.continuousAt
  rw [continuousAt_def] at hcont'
  have hnhds' := hcont' _ (h ‚ñ∏ hs)
  rw [‚Üê eventually_mem_nhds] at hnhds'

  have hsrc' := continuousAt_def.mp hŒ≥'.continuousAt _ <| extChartAt_source_mem_nhds I (Œ≥' t‚ÇÄ)
  rw [‚Üê eventually_mem_nhds] at hsrc'

  simp_rw [IsIntegralCurveAt, IsIntegralCurveOn, ‚Üê Filter.eventually_iff_exists_mem] at hŒ≥'

  -- there exists a neighbourhood around `t‚ÇÄ` in which all of the above hold
  have haux := hnhds.and <| hsrc.and <| hŒ≥.and <| hnhds'.and <| hsrc'.and hŒ≥'
  rw [Metric.eventually_nhds_iff_ball] at haux

  obtain ‚ü®Œµ, hŒµ, haux‚ü© := haux
  refine ‚ü®Œµ, hŒµ, ?_‚ü©

  -- break out all the conditions again
  have hmem := fun t ht => mem_preimage.mp <| mem_of_mem_nhds (haux t ht).1
  have hsrc := fun t ht => mem_preimage.mp <| mem_of_mem_nhds (haux t ht).2.1
  have hmfd : IsIntegralCurveOn _ _ _ := fun t ht => (haux t ht).2.2.1
  have hmem' := fun t ht => mem_preimage.mp <| mem_of_mem_nhds (haux t ht).2.2.2.1
  have hsrc' := fun t ht => mem_preimage.mp <| mem_of_mem_nhds (haux t ht).2.2.2.2.1
  have hmfd' : IsIntegralCurveOn _ _ _ := fun t ht => (haux t ht).2.2.2.2.2

  -- `Œ≥` and `Œ≥'` when expressed in the local chart are continuous on this neighbourhood
  have hcont := (continuousOn_extChartAt I (Œ≥ t‚ÇÄ)).comp
    (IsIntegralCurveOn.continuousOn hmfd) hsrc
  have hcont' := (continuousOn_extChartAt I (Œ≥' t‚ÇÄ)).comp
    (IsIntegralCurveOn.continuousOn hmfd') hsrc'

  simp_rw [Real.ball_eq_Ioo] at hmem hsrc hmfd hcont hmem' hsrc' hmfd' hcont'

  -- `Œ≥` and `Œ≥'` are
  have heqon : EqOn ((extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥) ((extChartAt I (Œ≥' t‚ÇÄ)) ‚àò Œ≥')
    (Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)) := by
    -- uniqueness of ODE solutions in an open interval
    apply ODE_solution_unique_of_mem_set_Ioo hlip (t‚ÇÄ := t‚ÇÄ)
      (Real.ball_eq_Ioo _ _ ‚ñ∏ (Metric.mem_ball_self hŒµ)) hcont _ hmem hcont' _ hmem' (by simp [h])
    ¬∑ intros t ht
      rw [hv']
      have := hmfd.hasDerivAt ht (hsrc t ht)
      apply this.congr_deriv
      have : Œ≥ t = (extChartAt I (Œ≥ t‚ÇÄ)).symm (((extChartAt I (Œ≥ t‚ÇÄ)) ‚àò Œ≥) t) := by
        rw [Function.comp_apply, PartialEquiv.left_inv]
        exact hsrc t ht
      rw [this]
    ¬∑ intros t ht
      rw [hv', h]
      have := hmfd'.hasDerivAt ht (hsrc' t ht)
      apply this.congr_deriv
      have : Œ≥' t = (extChartAt I (Œ≥' t‚ÇÄ)).symm (((extChartAt I (Œ≥' t‚ÇÄ)) ‚àò Œ≥') t) := by
        rw [Function.comp_apply, PartialEquiv.left_inv]
        exact hsrc' t ht
      rw [this]

  -- finally show `EqOn Œ≥ Œ≥' _` by composing with the inverse of the local chart around `Œ≥ t‚ÇÄ`
  refine EqOn.trans ?_ (EqOn.trans (heqon.comp_left (g := (extChartAt I (Œ≥ t‚ÇÄ)).symm)) ?_)
  ¬∑ intros t ht
    rw [Function.comp_apply, Function.comp_apply, PartialEquiv.left_inv _ (hsrc _ ht)]
  ¬∑ intros t ht
    rw [Function.comp_apply, Function.comp_apply, h, PartialEquiv.left_inv _ (hsrc' _ ht)]

theorem isIntegralCurveAt_eqOn_of_contMDiffAt_boundaryless [I.Boundaryless]
    (hv : ContMDiffAt I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)) (Œ≥ t‚ÇÄ))
    (hŒ≥ : IsIntegralCurveAt Œ≥ v t‚ÇÄ) (hŒ≥' : IsIntegralCurveAt Œ≥' v t‚ÇÄ) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    ‚àÉ Œµ > 0, EqOn Œ≥ Œ≥' (Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)) :=
  isIntegralCurveAt_eqOn_of_contMDiffAt I.isInteriorPoint hv hŒ≥ hŒ≥' h

variable [T2Space M] {a b a' b' : ‚Ñù}

/-- Integral curves are unique on open intervals.

  If a continuously differentiable vector field `v` admits two integral curves `Œ≥ Œ≥' : ‚Ñù ‚Üí M`
  on some open interval `Ioo a b`, and `Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ` for some `t ‚àà Ioo a b`, then `Œ≥` and `Œ≥'`
  agree on `Ioo a b`. -/
theorem isIntegralCurveOn_Ioo_eqOn_of_contMDiff (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b)
    (hŒ≥t : ‚àÄ t ‚àà Ioo a b, I.IsInteriorPoint (Œ≥ t))
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b)) (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a b))
    (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : EqOn Œ≥ Œ≥' (Ioo a b) := by
  /-
  strategy:
  * Lee P.213, just need to translate "S is closed in J" to type theory language
  -/
  set s := {t | Œ≥ t = Œ≥' t} ‚à© Ioo a b with hs
  have hsub : Ioo a b ‚äÜ s := by
    apply isPreconnected_Ioo.subset_of_closure_inter_subset (s := Ioo a b) (u := s) _
      ‚ü®t‚ÇÄ, ‚ü®ht‚ÇÄ, ‚ü®h, ht‚ÇÄ‚ü©‚ü©‚ü©
    ¬∑ -- is this really the most convenient way to pass to subtype topology?
      rw [hs, ‚Üê Subtype.image_preimage_val, ‚Üê Subtype.image_preimage_val,
        image_subset_image_iff Subtype.val_injective, preimage_setOf_eq]
      intros t ht
      rw [mem_preimage, ‚Üê closure_subtype] at ht
      revert ht t
      apply IsClosed.closure_subset
      apply isClosed_eq
      ¬∑ rw [continuous_iff_continuousAt]
        rintro ‚ü®_, ht‚ü©
        apply ContinuousAt.comp _ continuousAt_subtype_val
        rw [Subtype.coe_mk]
        exact hŒ≥.continuousAt ht
      ¬∑ rw [continuous_iff_continuousAt]
        rintro ‚ü®_, ht‚ü©
        apply ContinuousAt.comp _ continuousAt_subtype_val
        rw [Subtype.coe_mk]
        exact hŒ≥'.continuousAt ht
    ¬∑ rw [isOpen_iff_mem_nhds]
      intro t‚ÇÅ ht‚ÇÅ
      rw [mem_nhds_iff]
      obtain ‚ü®Œµ, hŒµ, heqon‚ü© : ‚àÉ Œµ > 0, EqOn Œ≥ Œ≥' (Ioo (t‚ÇÅ - Œµ) (t‚ÇÅ + Œµ)) :=
        isIntegralCurveAt_eqOn_of_contMDiffAt (hŒ≥t _ ht‚ÇÅ.2) hv.contMDiffAt
          (hŒ≥.isIntegralCurveAt <| Ioo_mem_nhds ht‚ÇÅ.2.1 ht‚ÇÅ.2.2)
          (hŒ≥'.isIntegralCurveAt <| Ioo_mem_nhds ht‚ÇÅ.2.1 ht‚ÇÅ.2.2)
          ht‚ÇÅ.1
      refine ‚ü®Ioo (max a (t‚ÇÅ - Œµ)) (min b (t‚ÇÅ + Œµ)),
        subset_inter
          (fun t ht => @heqon t <| mem_of_mem_of_subset ht <| Ioo_subset_Ioo (by simp) (by simp))
          (Ioo_subset_Ioo (by simp) (by simp)),
        isOpen_Ioo, ?_‚ü©
      rw [mem_Ioo]
      exact ‚ü®max_lt ht‚ÇÅ.2.1 (by simp [hŒµ]), lt_min ht‚ÇÅ.2.2 (by simp [hŒµ])‚ü©
  intros t ht
  exact mem_setOf.mp ((subset_def ‚ñ∏ hsub) t ht).1

theorem isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless [I.Boundaryless]
    (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b)
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b)) (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a b))
    (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : EqOn Œ≥ Œ≥' (Ioo a b) :=
  isIntegralCurveOn_Ioo_eqOn_of_contMDiff ht‚ÇÄ (fun _ _ => I.isInteriorPoint) hv hŒ≥ hŒ≥' h

/-- Global integral curves are unique.

  If a continuously differentiable vector field `v` admits two global integral curves
  `Œ≥ Œ≥' : ‚Ñù ‚Üí M`, and `Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ` for some `t‚ÇÄ`, then `Œ≥` and `Œ≥'` are equal. -/
theorem isIntegralCurve_eq_of_contMDiff (hŒ≥t : ‚àÄ t, I.IsInteriorPoint (Œ≥ t))
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurve Œ≥ v) (hŒ≥' : IsIntegralCurve Œ≥' v) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : Œ≥ = Œ≥' := by
  ext t
  obtain ‚ü®T, hT, ht‚ü© : ‚àÉ T > 0, t ‚àà Ioo (t‚ÇÄ - T) (t‚ÇÄ + T) := by
    refine ‚ü®2 * |t - t‚ÇÄ| + 1, add_pos_of_nonneg_of_pos (by simp) zero_lt_one, ?_‚ü©
    rw [mem_Ioo]
    by_cases ht : t - t‚ÇÄ < 0
    ¬∑ rw [abs_of_neg ht]
      constructor <;> linarith
    ¬∑ rw [abs_of_nonneg (not_lt.mp ht)]
      constructor <;> linarith
  exact isIntegralCurveOn_Ioo_eqOn_of_contMDiff
    (Real.ball_eq_Ioo t‚ÇÄ T ‚ñ∏ Metric.mem_ball_self hT) (fun t _ => hŒ≥t t) hv
    (IsIntegralCurveOn.mono (hŒ≥.isIntegralCurveOn _) (subset_univ _))
    (IsIntegralCurveOn.mono (hŒ≥'.isIntegralCurveOn _) (subset_univ _)) h ht

theorem isIntegralCurve_Ioo_eq_of_contMDiff_boundaryless [I.Boundaryless]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    (hŒ≥ : IsIntegralCurve Œ≥ v) (hŒ≥' : IsIntegralCurve Œ≥' v) (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) : Œ≥ = Œ≥' :=
  isIntegralCurve_eq_of_contMDiff (fun _ => I.isInteriorPoint) hv hŒ≥ hŒ≥' h

lemma exists_isIntegralCurveOn_Ioo_eqOn [I.Boundaryless]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) {x : M}
    (h : ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)) {a a' : ‚Ñù} (hpos : 0 < a')
    (hle : a' ‚â§ a) : EqOn (choose (h a')) (choose (h a)) (Ioo (-a') a') := by
  have ‚ü®h0', hŒ≥'‚ü© := choose_spec (h a')
  have ‚ü®h0, hŒ≥‚ü© := choose_spec (h a)
  apply isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless (t‚ÇÄ := 0) _ hv hŒ≥' (hŒ≥.mono _)
    (by rw [h0', h0])
  ¬∑ rw [mem_Ioo, ‚Üê abs_lt, abs_zero]
    exact hpos
  ¬∑ apply Ioo_subset_Ioo <;> linarith

/-- Auxiliary lemma. Suppose for every `a : ‚Ñù`, there exists an integral curve defined on
  `Ioo (-a) a`. We choose one for every `a` and call it `Œ≥ a`. We define a global curve
  `Œ≥_ext := fun t ‚Ü¶ Œ≥ (|t| + 1) t`. For every `a : ‚Ñù`, `Œ≥` agrees with `Œ≥ a` on `Ioo (-a) a`. This
  will help us show that `Œ≥` is a global integral curve. -/
lemma integralCurve_of_exists_isIntegralCurveOn_Ioo_eqOn [I.Boundaryless]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) {x : M}
    (h : ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)) {a : ‚Ñù} :
    EqOn (fun t' ‚Ü¶ choose (h (|t'| + 1)) t') (choose (h a)) (Ioo (-a) a) := by
  intros t' ht'
  by_cases hlt : |t'| + 1 < a
  ¬∑ apply exists_isIntegralCurveOn_Ioo_eqOn hv h
    ¬∑ exact add_pos_of_nonneg_of_pos (abs_nonneg _) zero_lt_one
    ¬∑ exact le_of_lt hlt
    ¬∑ rw [mem_Ioo, ‚Üê abs_lt]
      exact lt_add_one _
  ¬∑ apply (exists_isIntegralCurveOn_Ioo_eqOn hv h _ _ _).symm
    ¬∑ have := lt_trans ht'.1 ht'.2
      rw [neg_lt_self_iff] at this
      exact this
    ¬∑ exact not_lt.mp hlt
    ¬∑ exact ht'

/-- If for every `a : ‚Ñù`, there exists an integral curve defined on `Ioo (-a) a`, then there exists
  a global integral curve. -/
lemma exists_integralCurve_of_exists_isIntegralCurveOn_Ioo [I.Boundaryless] [T2Space M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) {x : M}
    (h : ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)) :
    ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurve Œ≥ v := by
  refine ‚ü®fun t' ‚Ü¶ choose (h (|t'| + 1)) t', (choose_spec (h (|0| + 1))).1, ?_‚ü©
  intro t
  apply HasMFDerivAt.congr_of_eventuallyEq (f := choose (h (|t| + 1)))
  ¬∑ apply (choose_spec (h (|t| + 1))).2 t
    rw [mem_Ioo, ‚Üê abs_lt]
    exact lt_add_one _
  ¬∑ rw [Filter.eventuallyEq_iff_exists_mem]
    refine ‚ü®Ioo (-(|t| + 1)) (|t| + 1), ?_, ?_‚ü©
    ¬∑ have := lt_add_of_pos_right |t| zero_lt_one
      rw [abs_lt] at this
      exact Ioo_mem_nhds this.1 this.2
    ¬∑ apply integralCurve_of_exists_isIntegralCurveOn_Ioo_eqOn hv

lemma exists_isIntegralCurve_iff_exists_isIntegralCurveOn_Ioo [I.Boundaryless] [T2Space M]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M))) (x : M) :
    (‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurve Œ≥ v) ‚Üî
      ‚àÄ a, ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a) := by
  constructor
  ¬∑ rintro ‚ü®Œ≥, h1, h2‚ü©
    intro a
    exact ‚ü®Œ≥, h1, h2.isIntegralCurveOn _‚ü©
  ¬∑ apply exists_integralCurve_of_exists_isIntegralCurveOn_Ioo hv

lemma piecewise_eqOn_symm [I.Boundaryless]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    {a b a' b' : ‚Ñù} (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b))
    (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a' b'))
    (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b ‚à© Ioo a' b') (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    EqOn (piecewise (Ioo a b) Œ≥ Œ≥') Œ≥' (Ioo a' b') := by
  intros t ht
  by_cases hle : t ‚â§ a
  ¬∑ rw [piecewise, if_neg]
    rw [mem_Ioo, not_and_or, not_lt]
    exact Or.inl hle
  ¬∑ rw [not_le] at hle
    by_cases hle' : b ‚â§ t
    ¬∑ rw [piecewise, if_neg]
      rw [mem_Ioo, not_and_or, not_lt (b := b)]
      exact Or.inr hle'
    ¬∑ rw [not_le] at hle'
      rw [piecewise, if_pos ‚ü®hle, hle'‚ü©]
      apply isIntegralCurveOn_Ioo_eqOn_of_contMDiff_boundaryless _ hv
        (hŒ≥.mono (Ioo_subset_Ioo (le_max_left ..) (min_le_left ..)))
        (hŒ≥'.mono (Ioo_subset_Ioo (le_max_right ..) (min_le_right ..))) h
        ‚ü®max_lt hle ht.1, lt_min hle' ht.2‚ü©
      rw [‚Üê sup_eq_max, ‚Üê inf_eq_min, ‚Üê Ioo_inter_Ioo]
      exact ht‚ÇÄ

/-- The extension of an integral curve by another integral curve is an integral curve.

  If two integral curves are defined on overlapping open intervals, and they agree at a point in
  their common domain, then they can be patched together to form a longer integral curve.

  This is stated for manifolds without boundary for simplicity. We actually only need to assume that
  the images of `Œ≥` and `Œ≥'` lie in the interior of the manifold. -/
lemma isIntegralCurveOn_piecewise [I.Boundaryless]
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    {a b a' b' : ‚Ñù} (hŒ≥ : IsIntegralCurveOn Œ≥ v (Ioo a b))
    (hŒ≥' : IsIntegralCurveOn Œ≥' v (Ioo a' b')) {t‚ÇÄ : ‚Ñù}
    (ht‚ÇÄ : t‚ÇÄ ‚àà Ioo a b ‚à© Ioo a' b') (h : Œ≥ t‚ÇÄ = Œ≥' t‚ÇÄ) :
    IsIntegralCurveOn (piecewise (Ioo a b) Œ≥ Œ≥') v (Ioo a b ‚à™ Ioo a' b') := by
  intros t ht
  -- five cases:
  -- * `a < t < b`: agrees with `Œ≥` by definition
  -- * `a' < t < a`: agrees with `Œ≥'` by definition
  -- * `b < t < b'`: agrees with `Œ≥'` by definition
  -- * `t = a`: agrees with `Œ≥'` propositionally by uniqueness
  -- * `t = b`: agrees with `Œ≥'` propositionally by uniqueness
  by_cases hmem : t ‚àà Ioo a b
  ¬∑ -- for `a < t < b` the piecewise function is equal to `Œ≥`
    rw [piecewise, if_pos hmem]
    apply (hŒ≥ t hmem).congr_of_eventuallyEq
    rw [Filter.eventuallyEq_iff_exists_mem]
    refine ‚ü®Ioo a b, isOpen_Ioo.mem_nhds hmem, ?_‚ü©
    intros t' ht'
    rw [piecewise, if_pos ht']
  ¬∑ rw [mem_union, or_iff_not_imp_left] at ht
    have hmem' := ht hmem
    rw [piecewise, if_neg hmem]
    apply (hŒ≥' t hmem').congr_of_eventuallyEq
    rw [Filter.eventuallyEq_iff_exists_mem]
    refine ‚ü®Ioo a' b', isOpen_Ioo.mem_nhds hmem', piecewise_eqOn_symm hv hŒ≥ hŒ≥' ht‚ÇÄ h‚ü©

/-- If there exists `Œµ > 0` such that the local integral curve at each point `x : M` is defined at
  least on an open interval `Ioo (-Œµ) Œµ`, then every point on `M` has a global integral
  curve passing through it.

  See Lemma 9.15, Lee -/
lemma exists_isIntegralCurve_of_isIntegralCurveOn' [I.Boundaryless] {M : Type*} [TopologicalSpace M]
    [ChartedSpace H M] [SmoothManifoldWithCorners I M] [T2Space M] {v : (x : M) ‚Üí TangentSpace I x}
    (hv : ContMDiff I I.tangent 1 (fun x => (‚ü®x, v x‚ü© : TangentBundle I M)))
    {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (h : ‚àÄ x : M, ‚àÉ Œ≥ : ‚Ñù ‚Üí M, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-Œµ) Œµ))
    (x : M) : ‚àÉ Œ≥ : ‚Ñù ‚Üí M, Œ≥ 0 = x ‚àß IsIntegralCurve Œ≥ v := by
  have hnon : Set.Nonempty {a | ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)} := ‚ü®Œµ, h x‚ü©
  by_cases hbdd : BddAbove {a | ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)}
  ¬∑ set asup := sSup {a | ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)} with hasup
    -- we will obtain two integral curves, one centred at some `t‚ÇÄ > 0` with
    -- `0 ‚â§ asup - Œµ < t‚ÇÄ < asup`; let `t‚ÇÄ = asup - Œµ / 2`
    -- another centred at 0 with domain up to `a ‚àà S` with `t‚ÇÄ < a < asup`
    obtain ‚ü®a, ha, hlt‚ü© := Real.add_neg_lt_sSup hnon (Œµ := - (Œµ / 2))
      (by rw [neg_lt, neg_zero]; exact half_pos hŒµ)
    have hale : a ‚â§ asup := le_csSup hbdd ha
    rw [mem_setOf] at ha
    rw [‚Üê hasup, ‚Üê sub_eq_add_neg] at hlt

    have hŒµle : Œµ ‚â§ asup := le_csSup hbdd (h x)

    -- integral curve defined on `Ioo (-a) a`
    obtain ‚ü®Œ≥, h0, hŒ≥‚ü© := ha
    -- integral curve starting at `-(asup - Œµ / 2)` with radius `Œµ`
    obtain ‚ü®Œ≥1_aux, h1_aux, hŒ≥1_aux‚ü© := h (Œ≥ (-(asup - Œµ / 2)))
    let Œ≥1 := Œ≥1_aux ‚àò (¬∑ + (asup - Œµ / 2))
    have heq1 : Œ≥1 (-(asup - Œµ / 2)) = Œ≥ (-(asup - Œµ / 2)) := by simp [h1_aux]
    have hŒ≥1 : IsIntegralCurveOn Œ≥1 v (Ioo (-(asup + Œµ / 2)) (-(asup - Œµ))) := by
      apply (hŒ≥1_aux.comp_add (asup - Œµ / 2)).mono
      intro t
      rw [mem_Ioo, mem_setOf, mem_Ioo]
      rintro ‚ü®_, _‚ü©
      constructor <;> linarith
    -- integral curve starting at `asup - Œµ / 2` with radius `Œµ`
    obtain ‚ü®Œ≥2_aux, h2_aux, hŒ≥2_aux‚ü© := h (Œ≥ (asup - Œµ / 2))
    let Œ≥2 := Œ≥2_aux ‚àò (¬∑ + -(asup - Œµ / 2))
    have heq2 : Œ≥2 (asup - Œµ / 2) = Œ≥ (asup - Œµ / 2) := by simp [h2_aux]
    have hŒ≥2 : IsIntegralCurveOn Œ≥2 v (Ioo (asup - Œµ) (asup + Œµ / 2)) := by
      apply (hŒ≥2_aux.comp_add (-(asup - Œµ / 2))).mono
      intro t
      rw [mem_Ioo, mem_setOf, mem_Ioo]
      rintro ‚ü®_, _‚ü©
      constructor <;> linarith

    -- extend `Œ≥` on the left by `Œ≥1` and on the right by `Œ≥2`
    set Œ≥_ext : ‚Ñù ‚Üí M := piecewise (Ioo (-(asup + Œµ / 2)) a)
      (piecewise (Ioo (-a) a) Œ≥ Œ≥1) Œ≥2 with Œ≥_ext_def
    have hext : IsIntegralCurveOn Œ≥_ext v (Ioo (-(asup + Œµ / 2)) (asup + Œµ / 2)) := by
      apply (isIntegralCurveOn_piecewise (t‚ÇÄ := asup - Œµ / 2) hv _ hŒ≥2
        (by refine ‚ü®‚ü®?_, ?_‚ü©, ‚ü®?_, ?_‚ü©‚ü© <;> linarith) _).mono
      ¬∑ rintro t ‚ü®ht1, ht2‚ü©
        by_cases hlt : t < a
        ¬∑ exact mem_union_left _ ‚ü®ht1, hlt‚ü©
        ¬∑ apply mem_union_right
          exact ‚ü®by linarith, by linarith‚ü©
      ¬∑ apply (isIntegralCurveOn_piecewise (t‚ÇÄ := -(asup - Œµ / 2)) hv hŒ≥ hŒ≥1
          (by refine ‚ü®‚ü®?_, ?_‚ü©, ‚ü®?_, ?_‚ü©‚ü© <;> linarith) heq1.symm).mono
        ¬∑ rintro t ‚ü®ht1, ht2‚ü©
          by_cases hlt : -a < t
          ¬∑ exact mem_union_left _ ‚ü®hlt, ht2‚ü©
          ¬∑ apply mem_union_right
            exact ‚ü®by linarith, by linarith‚ü©
      ¬∑ rw [piecewise, if_pos ‚ü®by linarith, by linarith‚ü©, heq2]
    have hmem : asup + Œµ / 2 ‚àà {a | ‚àÉ Œ≥, Œ≥ 0 = x ‚àß IsIntegralCurveOn Œ≥ v (Ioo (-a) a)} := by
      refine ‚ü®Œ≥_ext, ?_, hext‚ü©
      rw [Œ≥_ext_def, piecewise, if_pos ‚ü®by linarith, by linarith‚ü©, piecewise,
        if_pos ‚ü®by linarith, by linarith‚ü©]
      exact h0
    absurd lt_add_of_pos_right asup (half_pos hŒµ)
    rw [not_lt]
    exact le_csSup hbdd hmem
  ¬∑ rw [not_bddAbove_iff] at hbdd
    simp_rw [mem_setOf] at hbdd
    rw [exists_isIntegralCurve_iff_exists_isIntegralCurveOn_Ioo hv]
    intro a
    obtain ‚ü®‚ü®Œ≥, hŒ≥1, hŒ≥2‚ü©, hlt‚ü© := choose_spec (hbdd a)
    refine ‚ü®Œ≥, hŒ≥1, hŒ≥2.mono ?_‚ü©
    apply Ioo_subset_Ioo <;>
    simp [le_of_lt hlt]
