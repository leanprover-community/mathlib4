/-
Copyright (c) 2023 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import Mathlib.Analysis.ODE.PicardLindelof
import Mathlib.Geometry.Manifold.ContMDiff
import Mathlib.Geometry.Manifold.MFDeriv

/-!
# Integral curves of vector fields on a manifold

For any continuously differentiable vector field on a manifold `M` and any chosen non-boundary point
`xâ‚€ : M`, an integral curve `Î³ : â„ â†’ M` exists such that `Î³ tâ‚€ = xâ‚€` and the tangent vector of `Î³`
at `t` coincides with the vector field at `Î³ t` for all `t` within an open interval around `tâ‚€`.

As a corollary, such an integral curve exists for any starting point `xâ‚€` if `M` is a manifold
without boundary.

## Tags

integral curve, vector field
-/

open scoped Manifold
scoped[Manifold] notation "ğ“”(" I ", " x ")" => extChartAt I x
scoped[Manifold] notation "ğ“”â»Â¹(" I ", " x ")" => LocalEquiv.symm (ğ“”(I, x))

section

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners ğ•œ E' H'}
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [SmoothManifoldWithCorners I' M']

/-- An interior point of a manifold is a point whose image in the model vector space is in the
interior of the chart's target. -/
def ModelWithCorners.IsInteriorPoint (x : M) := extChartAt I x x âˆˆ interior (extChartAt I x).target

lemma ModelWithCorners.Boundaryless.isOpen_target
    [I.Boundaryless] {x : M} : IsOpen (extChartAt I x).target := by
rw [extChartAt_target, ModelWithCorners.Boundaryless.range_eq_univ, Set.inter_univ]
exact (ModelWithCorners.continuous_symm _).isOpen_preimage _ (LocalHomeomorph.open_target _)

lemma ModelWithCorners.Boundaryless.isInteriorPoint
    [I.Boundaryless] {x : M} : I.IsInteriorPoint x := by
rw [ModelWithCorners.IsInteriorPoint,
  IsOpen.interior_eq ModelWithCorners.Boundaryless.isOpen_target]
exact LocalEquiv.map_source _ (mem_extChartAt_source _ _)

end

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners â„ E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']
  {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners â„ E' H'}
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [SmoothManifoldWithCorners I' M']
  {v : (x : M) â†’ TangentSpace I x} {xâ‚€ : M}
  (hv : ContMDiffAt I I.tangent 1 (fun x => (âŸ¨x, v xâŸ© : TangentBundle I M)) xâ‚€) (tâ‚€ : â„)

/-- For any continuously differentiable vector field and any chosen non-boundary point `xâ‚€` on the
  manifold, an integral curve `Î³ : â„ â†’ M` exists such that `Î³ tâ‚€ = xâ‚€` and the tangent vector of `Î³`
  at `t` coincides with the vector field at `Î³ t` for all `t` within an open interval around `tâ‚€`.-/
theorem exists_integralCurve_of_contMDiff_tangent_section (hx : I.IsInteriorPoint xâ‚€) :
    âˆƒ Îµ > (0 : â„), âˆƒ (Î³ : â„ â†’ M), Î³ tâ‚€ = xâ‚€ âˆ§ âˆ€ (t : â„), t âˆˆ Set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ) â†’
    HasMFDerivAt ğ“˜(â„, â„) I Î³ t ((1 : â„ â†’L[â„] â„).smulRight (v (Î³ t))) := by
rw [contMDiffAt_iff] at hv
obtain âŸ¨_, hvâŸ© := hv
have hI : Set.range I âˆˆ nhds (extChartAt I xâ‚€ xâ‚€)
Â· rw [mem_nhds_iff]
  refine âŸ¨interior (extChartAt I xâ‚€).target,
    subset_trans interior_subset (extChartAt_target_subset_range ..),
    isOpen_interior, hxâŸ©
obtain âŸ¨Îµ1, hÎµ1, f, hf1, hf2âŸ© :=
  exists_forall_hasDerivAt_Ioo_eq_of_contDiffAt tâ‚€ (ContDiffAt.snd (hv.contDiffAt hI))
rw [â†Real.ball_eq_Ioo] at hf2
-- use continuity of f to extract Îµ2 so that for t âˆˆ Real.ball tâ‚€ Îµ2,
-- f t âˆˆ interior (extChartAt I xâ‚€).target
have hcont := (hf2 tâ‚€ (Real.ball_eq_Ioo .. â–¸ Metric.mem_ball_self hÎµ1)).continuousAt
rw [continuousAt_def, hf1] at hcont
have hnhds : f â»Â¹' (interior (extChartAt I xâ‚€).target) âˆˆ nhds tâ‚€
Â· apply hcont
  exact IsOpen.mem_nhds isOpen_interior hx
rw [Metric.mem_nhds_iff] at hnhds
obtain âŸ¨Îµ2, hÎµ2, hf3âŸ© := hnhds
simp_rw [Set.subset_def, Set.mem_preimage] at hf3
-- prove the theorem
refine' âŸ¨min Îµ1 Îµ2, lt_min hÎµ1 hÎµ2, (extChartAt I xâ‚€).symm âˆ˜ f, _, _âŸ©
Â· apply Eq.symm
  rw [Function.comp_apply, hf1, LocalEquiv.left_inv _ (mem_extChartAt_source ..)]
intros t ht
rw [â†Real.ball_eq_Ioo] at ht
have ht1 := Set.mem_of_mem_of_subset ht (Metric.ball_subset_ball (min_le_left ..))
have ht2 := Set.mem_of_mem_of_subset ht (Metric.ball_subset_ball (min_le_right ..))
have h : HasDerivAt f
  ((fderivWithin â„
        ((extChartAt I xâ‚€) âˆ˜ (extChartAt I ((extChartAt I xâ‚€).symm (f t))).symm)
        (Set.range I)
        (extChartAt I ((extChartAt I xâ‚€).symm (f t)) ((extChartAt I xâ‚€).symm (f t))))
    (v ((extChartAt I xâ‚€).symm (f t))))
  t := hf2 t ht1
have hf3' := Set.mem_of_mem_of_subset (hf3 t ht2) interior_subset
rw [HasMFDerivAt]
use ContinuousAt.comp
  (continuousAt_extChartAt_symm'' _ _ hf3') ((hf2 t ht1).continuousAt)
apply HasDerivWithinAt.hasFDerivWithinAt
rw [modelWithCornersSelf_coe, Set.range_id, hasDerivWithinAt_univ, ext_chart_model_space_apply,
  writtenInExtChartAt, Function.comp_apply, Function.comp.assoc, extChartAt_model_space_eq_id,
  LocalEquiv.refl_symm, LocalEquiv.refl_coe, Function.comp.right_id, â†Function.comp.assoc]
-- v -> identity v
rw [â†tangentBundleCore_coordChange_achart] at h
have hvsub : v ((extChartAt I xâ‚€).symm (f t)) = (tangentBundleCore I M).coordChange
  (achart H xâ‚€) (achart H ((extChartAt I xâ‚€).symm (f t))) ((extChartAt I xâ‚€).symm (f t))
  ((tangentBundleCore I M).coordChange (achart H ((extChartAt I xâ‚€).symm (f t))) (achart H xâ‚€)
    ((extChartAt I xâ‚€).symm (f t)) (v ((extChartAt I xâ‚€).symm (f t))))
Â· rw [(tangentBundleCore I M).coordChange_comp, (tangentBundleCore I M).coordChange_self]
  Â· rw [tangentBundleCore_baseSet, coe_achart, â†extChartAt_source I, â†LocalEquiv.symm_target]
    exact mem_extChartAt_source ..
  Â· rw [tangentBundleCore_baseSet, tangentBundleCore_baseSet, coe_achart, coe_achart,
      â†extChartAt_source I, â†extChartAt_source I, Set.inter_comm, â†Set.inter_assoc, Set.inter_self]
    constructor
    Â· exact mem_extChartAt_source ..
    Â· rw [â†Set.mem_preimage]
      apply Set.mem_of_mem_of_subset _ (LocalEquiv.source_subset_preimage_target _)
      rw [LocalEquiv.symm_source]
      exact hf3'
rw [hvsub]
apply HasFDerivAt.comp_hasDerivAt _ _ h
rw [tangentBundleCore_coordChange_achart, LocalEquiv.right_inv _ hf3', fderivWithin_of_mem_nhds]
Â· apply DifferentiableAt.hasFDerivAt
  apply MDifferentiableAt.differentiableAt
  apply MDifferentiableAt.comp (I' := I)
  Â· exact (contMDiffAt_extChartAt (n := 1)).mdifferentiableAt (le_refl _)
  Â· apply MDifferentiableOn.mdifferentiableAt
      ((contMDiffOn_extChartAt_symm (n := 1) _).mdifferentiableOn (le_refl _))
    rw [mem_nhds_iff]
    exact âŸ¨interior (extChartAt I xâ‚€).target, interior_subset, isOpen_interior, hf3 _ ht2âŸ©
Â· rw [mem_nhds_iff]
  refine âŸ¨interior (extChartAt I xâ‚€).target,
    subset_trans interior_subset (extChartAt_target_subset_range ..), isOpen_interior, hf3 _ ht2âŸ©

/-- For any continuously differentiable vector field defined on a manifold without boundary and any
  chosen starting point `xâ‚€ : M`, an integral curve `Î³ : â„ â†’ M` exists such that `Î³ tâ‚€ = xâ‚€` and the
  tangent vector of `Î³` at `t` coincides with the vector field at `Î³ t` for all `t` within an open
  interval around `tâ‚€`. -/
lemma exists_integralCurve_of_contMDiff_tangent_section_boundaryless [I.Boundaryless] :
    âˆƒ Îµ > (0 : â„), âˆƒ (Î³ : â„ â†’ M), Î³ tâ‚€ = xâ‚€ âˆ§ âˆ€ (t : â„), t âˆˆ Set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ) â†’
      HasMFDerivAt ğ“˜(â„, â„) I Î³ t ((1 : â„ â†’L[â„] â„).smulRight (v (Î³ t))) :=
  exists_integralCurve_of_contMDiff_tangent_section hv _
    ModelWithCorners.Boundaryless.isInteriorPoint
