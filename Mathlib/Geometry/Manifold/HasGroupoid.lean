/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
module

public import Mathlib.Geometry.Manifold.ChartedSpace

/-!
# Charted spaces with a given structure groupoid
-/

@[expose] public section

noncomputable section

open TopologicalSpace Topology

universe u

variable {H : Type u} {H' : Type*} {M : Type*} {M' : Type*} {M'' : Type*}

open Set OpenPartialHomeomorph Manifold

section HasGroupoid

variable [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M]

/-- A charted space has an atlas in a groupoid `G` if the change of coordinates belong to the
groupoid. -/
class HasGroupoid {H : Type*} [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] (G : StructureGroupoid H) : Prop where
  compatible :
    ‚àÄ {e e' : OpenPartialHomeomorph M H}, e ‚àà atlas H M ‚Üí e' ‚àà atlas H M ‚Üí e.symm ‚â´‚Çï e' ‚àà G

/-- Reformulate in the `StructureGroupoid` namespace the compatibility condition of charts in a
charted space admitting a structure groupoid, to make it more easily accessible with dot
notation. -/
theorem StructureGroupoid.compatible {H : Type*} [TopologicalSpace H] (G : StructureGroupoid H)
    {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [HasGroupoid M G]
    {e e' : OpenPartialHomeomorph M H} (he : e ‚àà atlas H M) (he' : e' ‚àà atlas H M) :
    e.symm ‚â´‚Çï e' ‚àà G :=
  HasGroupoid.compatible he he'

theorem hasGroupoid_of_le {G‚ÇÅ G‚ÇÇ : StructureGroupoid H} (h : HasGroupoid M G‚ÇÅ) (hle : G‚ÇÅ ‚â§ G‚ÇÇ) :
    HasGroupoid M G‚ÇÇ :=
  ‚ü®fun he he' ‚Ü¶ hle (h.compatible he he')‚ü©

theorem hasGroupoid_inf_iff {G‚ÇÅ G‚ÇÇ : StructureGroupoid H} : HasGroupoid M (G‚ÇÅ ‚äì G‚ÇÇ) ‚Üî
    HasGroupoid M G‚ÇÅ ‚àß HasGroupoid M G‚ÇÇ :=
  ‚ü®(fun h ‚Ü¶ ‚ü®hasGroupoid_of_le h inf_le_left, hasGroupoid_of_le h inf_le_right‚ü©),
  fun ‚ü®h1, h2‚ü© ‚Ü¶ { compatible := fun he he' ‚Ü¶ ‚ü®h1.compatible he he', h2.compatible he he'‚ü© }‚ü©

theorem hasGroupoid_of_pregroupoid (PG : Pregroupoid H) (h : ‚àÄ {e e' : OpenPartialHomeomorph M H},
    e ‚àà atlas H M ‚Üí e' ‚àà atlas H M ‚Üí PG.property (e.symm ‚â´‚Çï e') (e.symm ‚â´‚Çï e').source) :
    HasGroupoid M PG.groupoid :=
  ‚ü®fun he he' ‚Ü¶ mem_groupoid_of_pregroupoid.mpr ‚ü®h he he', h he' he‚ü©‚ü©

/-- The trivial charted space structure on the model space is compatible with any groupoid. -/
instance hasGroupoid_model_space (H : Type*) [TopologicalSpace H] (G : StructureGroupoid H) :
    HasGroupoid H G where
  compatible {e e'} he he' := by
    rw [chartedSpaceSelf_atlas] at he he'
    simp [he, he', StructureGroupoid.id_mem]

/-- Any charted space structure is compatible with the groupoid of all open partial
homeomorphisms. -/
instance hasGroupoid_continuousGroupoid : HasGroupoid M (continuousGroupoid H) := by
  refine ‚ü®fun _ _ ‚Ü¶ ?_‚ü©
  rw [continuousGroupoid, mem_groupoid_of_pregroupoid]
  simp only [and_self_iff]

/-- If `G` is closed under restriction, the transition function between the restriction of two
charts `e` and `e'` lies in `G`. -/
theorem StructureGroupoid.trans_restricted {e e' : OpenPartialHomeomorph M H}
    {G : StructureGroupoid H} (he : e ‚àà atlas H M) (he' : e' ‚àà atlas H M)
    [HasGroupoid M G] [ClosedUnderRestriction G] {s : Opens M} (hs : Nonempty s) :
    (e.subtypeRestr hs).symm ‚â´‚Çï e'.subtypeRestr hs ‚àà G :=
  G.mem_of_eqOnSource (closedUnderRestriction' (G.compatible he he')
    (e.isOpen_inter_preimage_symm s.2)) (e.subtypeRestr_symm_trans_subtypeRestr hs e')

section MaximalAtlas

variable (G : StructureGroupoid H)

variable (M) in
/-- Given a charted space admitting a structure groupoid, the maximal atlas associated to this
structure groupoid is the set of all charts that are compatible with the atlas, i.e., such
that changing coordinates with an atlas member gives an element of the groupoid. -/
def StructureGroupoid.maximalAtlas : Set (OpenPartialHomeomorph M H) :=
  { e | ‚àÄ e' ‚àà atlas H M, e.symm ‚â´‚Çï e' ‚àà G ‚àß e'.symm ‚â´‚Çï e ‚àà G }

/-- The elements of the atlas belong to the maximal atlas for any structure groupoid. -/
theorem StructureGroupoid.subset_maximalAtlas [HasGroupoid M G] : atlas H M ‚äÜ G.maximalAtlas M :=
  fun _ he _ he' ‚Ü¶ ‚ü®G.compatible he he', G.compatible he' he‚ü©

theorem StructureGroupoid.chart_mem_maximalAtlas [HasGroupoid M G] (x : M) :
    chartAt H x ‚àà G.maximalAtlas M :=
  G.subset_maximalAtlas (chart_mem_atlas H x)

variable {G}

theorem mem_maximalAtlas_iff {e : OpenPartialHomeomorph M H} :
    e ‚àà G.maximalAtlas M ‚Üî ‚àÄ e' ‚àà atlas H M, e.symm ‚â´‚Çï e' ‚àà G ‚àß e'.symm ‚â´‚Çï e ‚àà G :=
  Iff.rfl

/-- Changing coordinates between two elements of the maximal atlas gives rise to an element
of the structure groupoid. -/
theorem StructureGroupoid.compatible_of_mem_maximalAtlas {e e' : OpenPartialHomeomorph M H}
    (he : e ‚àà G.maximalAtlas M) (he' : e' ‚àà G.maximalAtlas M) : e.symm ‚â´‚Çï e' ‚àà G := by
  refine G.locality fun x hx ‚Ü¶ ?_
  set f := chartAt (H := H) (e.symm x)
  let s := e.target ‚à© e.symm ‚Åª¬π' f.source
  have hs : IsOpen s := by
    apply e.symm.continuousOn_toFun.isOpen_inter_preimage <;> apply open_source
  have xs : x ‚àà s := by
    simp only [s, f, mem_inter_iff, mem_preimage, mem_chart_source, and_true]
    exact ((mem_inter_iff _ _ _).1 hx).1
  refine ‚ü®s, hs, xs, ?_‚ü©
  have A : e.symm ‚â´‚Çï f ‚àà G := (mem_maximalAtlas_iff.1 he f (chart_mem_atlas _ _)).1
  have B : f.symm ‚â´‚Çï e' ‚àà G := (mem_maximalAtlas_iff.1 he' f (chart_mem_atlas _ _)).2
  have C : (e.symm ‚â´‚Çï f) ‚â´‚Çï f.symm ‚â´‚Çï e' ‚àà G := G.trans A B
  have D : (e.symm ‚â´‚Çï f) ‚â´‚Çï f.symm ‚â´‚Çï e' ‚âà (e.symm ‚â´‚Çï e').restr s := calc
    (e.symm ‚â´‚Çï f) ‚â´‚Çï f.symm ‚â´‚Çï e' = e.symm ‚â´‚Çï (f ‚â´‚Çï f.symm) ‚â´‚Çï e' := by simp only [trans_assoc]
    _ ‚âà e.symm ‚â´‚Çï ofSet f.source f.open_source ‚â´‚Çï e' :=
      EqOnSource.trans' (refl _) (EqOnSource.trans' (self_trans_symm _) (refl _))
    _ ‚âà (e.symm ‚â´‚Çï ofSet f.source f.open_source) ‚â´‚Çï e' := by rw [trans_assoc]
    _ ‚âà e.symm.restr s ‚â´‚Çï e' := by rw [trans_of_set']; apply refl
    _ ‚âà (e.symm ‚â´‚Çï e').restr s := by rw [restr_trans]
  exact G.mem_of_eqOnSource C (Setoid.symm D)

open OpenPartialHomeomorph in
/-- The maximal atlas of a structure groupoid is stable under equivalence. -/
lemma StructureGroupoid.mem_maximalAtlas_of_eqOnSource {e e' : OpenPartialHomeomorph M H}
    (h : e' ‚âà e) (he : e ‚àà G.maximalAtlas M) : e' ‚àà G.maximalAtlas M := by
  intro e'' he''
  obtain ‚ü®l, r‚ü© := mem_maximalAtlas_iff.mp he e'' he''
  exact ‚ü®G.mem_of_eqOnSource l (EqOnSource.trans' (EqOnSource.symm' h) (e''.eqOnSource_refl)),
         G.mem_of_eqOnSource r (EqOnSource.trans' (e''.symm).eqOnSource_refl h)‚ü©

variable (G)

/-- In the model space, the identity is in any maximal atlas. -/
theorem StructureGroupoid.id_mem_maximalAtlas : OpenPartialHomeomorph.refl H ‚àà G.maximalAtlas H :=
  G.subset_maximalAtlas <| by simp

/-- In the model space, any element of the groupoid is in the maximal atlas. -/
theorem StructureGroupoid.mem_maximalAtlas_of_mem_groupoid {f : OpenPartialHomeomorph H H}
    (hf : f ‚àà G) : f ‚àà G.maximalAtlas H := by
  rintro e (rfl : e = OpenPartialHomeomorph.refl H)
  exact ‚ü®G.trans (G.symm hf) G.id_mem, G.trans (G.symm G.id_mem) hf‚ü©

theorem StructureGroupoid.maximalAtlas_mono {G G' : StructureGroupoid H} (h : G ‚â§ G') :
    G.maximalAtlas M ‚äÜ G'.maximalAtlas M :=
  fun _ he e' he' ‚Ü¶ ‚ü®h (he e' he').1, h (he e' he').2‚ü©

private theorem restr_mem_maximalAtlas_aux1 [ClosedUnderRestriction G]
    {e e' : OpenPartialHomeomorph M H} (he : e ‚àà G.maximalAtlas M) (he' : e' ‚àà atlas H M)
    {s : Set M} (hs : IsOpen s) :
    (e.restr s).symm ‚â´‚Çï e' ‚àà G := by
  have hs'' : IsOpen (e '' (e.source ‚à© s)) := by
    rw [isOpen_image_iff_of_subset_source _ inter_subset_left]
    exact e.open_source.inter hs
  have : (e.restr (e.source ‚à© s)).symm ‚â´‚Çï e' ‚àà G := by
    apply G.mem_of_eqOnSource (closedUnderRestriction' (he e' he').1 hs'')
    exact e.restr_symm_trans (e.open_source.inter hs) hs'' inter_subset_left
  refine G.mem_of_eqOnSource this ?_
  exact EqOnSource.trans' (Setoid.symm e.restr_inter_source).symm' (eqOnSource_refl e')

private theorem restr_mem_maximalAtlas_aux2 [ClosedUnderRestriction G]
    {e e' : OpenPartialHomeomorph M H} (he : e ‚àà G.maximalAtlas M) (he' : e' ‚àà atlas H M)
    {s : Set M} (hs : IsOpen s) :
    e'.symm ‚â´‚Çï e.restr s ‚àà G := by
  have hs'' : IsOpen (e' '' (e'.source ‚à© s)) := by
    rw [isOpen_image_iff_of_subset_source e' inter_subset_left]
    exact e'.open_source.inter hs
  have ht : IsOpen (e'.target ‚à© e'.symm ‚Åª¬π' s) := by
    rw [‚Üê image_source_inter_eq']
    exact isOpen_image_source_inter e' hs
  exact G.mem_of_eqOnSource (closedUnderRestriction' (he e' he').2 ht) (e.symm_trans_restr e' hs)

/-- If a structure groupoid `G` is closed under restriction, for any chart `e` in the maximal atlas,
the restriction `e.restr s` to an open set `s` is also in the maximal atlas. -/
theorem restr_mem_maximalAtlas [ClosedUnderRestriction G]
    {e : OpenPartialHomeomorph M H} (he : e ‚àà G.maximalAtlas M) {s : Set M} (hs : IsOpen s) :
    e.restr s ‚àà G.maximalAtlas M :=
  fun _e' he' ‚Ü¶ ‚ü®restr_mem_maximalAtlas_aux1 G he he' hs, restr_mem_maximalAtlas_aux2 G he he' hs‚ü©

end MaximalAtlas

section Singleton

variable {Œ± : Type*} [TopologicalSpace Œ±]

namespace OpenPartialHomeomorph

variable (e : OpenPartialHomeomorph Œ± H)

/-- If a single open partial homeomorphism `e` from a space `Œ±` into `H` has source covering the
whole space `Œ±`, then that open partial homeomorphism induces an `H`-charted space structure on `Œ±`.
(This condition is equivalent to `e` being an open embedding of `Œ±` into `H`; see
`IsOpenEmbedding.singletonChartedSpace`.) -/
def singletonChartedSpace (h : e.source = Set.univ) : ChartedSpace H Œ± where
  atlas := {e}
  chartAt _ := e
  mem_chart_source _ := by rw [h]; apply mem_univ
  chart_mem_atlas _ := by tauto

@[simp, mfld_simps]
theorem singletonChartedSpace_chartAt_eq (h : e.source = Set.univ) {x : Œ±} :
    @chartAt H _ Œ± _ (e.singletonChartedSpace h) x = e :=
  rfl

theorem singletonChartedSpace_chartAt_source (h : e.source = Set.univ) {x : Œ±} :
    (@chartAt H _ Œ± _ (e.singletonChartedSpace h) x).source = Set.univ :=
  h

theorem singletonChartedSpace_mem_atlas_eq (h : e.source = Set.univ)
    (e' : OpenPartialHomeomorph Œ± H) (h' : e' ‚àà (e.singletonChartedSpace h).atlas) : e' = e :=
  h'

/-- Given an open partial homeomorphism `e` from a space `Œ±` into `H`, if its source covers the
whole space `Œ±`, then the induced charted space structure on `Œ±` is `HasGroupoid G` for any
structure groupoid `G` which is closed under restrictions. -/
theorem singleton_hasGroupoid (h : e.source = Set.univ) (G : StructureGroupoid H)
    [ClosedUnderRestriction G] : @HasGroupoid _ _ _ _ (e.singletonChartedSpace h) G :=
  { __ := e.singletonChartedSpace h
    compatible := by
      intro e' e'' he' he''
      rw [e.singletonChartedSpace_mem_atlas_eq h e' he',
        e.singletonChartedSpace_mem_atlas_eq h e'' he'']
      refine G.mem_of_eqOnSource ?_ e.symm_trans_self
      have hle : idRestrGroupoid ‚â§ G := (closedUnderRestriction_iff_id_le G).mp (by assumption)
      exact StructureGroupoid.le_iff.mp hle _ (idRestrGroupoid_mem _) }

end OpenPartialHomeomorph

namespace Topology.IsOpenEmbedding

variable [Nonempty Œ±]

/-- An open embedding of `Œ±` into `H` induces an `H`-charted space structure on `Œ±`.
See `OpenPartialHomeomorph.singletonChartedSpace`. -/
def singletonChartedSpace {f : Œ± ‚Üí H} (h : IsOpenEmbedding f) : ChartedSpace H Œ± :=
  (h.toOpenPartialHomeomorph f).singletonChartedSpace (toOpenPartialHomeomorph_source _ _)

theorem singletonChartedSpace_chartAt_eq {f : Œ± ‚Üí H} (h : IsOpenEmbedding f) {x : Œ±} :
    ‚áë(@chartAt H _ Œ± _ h.singletonChartedSpace x) = f :=
  rfl

theorem singleton_hasGroupoid {f : Œ± ‚Üí H} (h : IsOpenEmbedding f) (G : StructureGroupoid H)
    [ClosedUnderRestriction G] : @HasGroupoid _ _ _ _ h.singletonChartedSpace G :=
  (h.toOpenPartialHomeomorph f).singleton_hasGroupoid (toOpenPartialHomeomorph_source _ _) G

end Topology.IsOpenEmbedding

end Singleton

namespace TopologicalSpace.Opens

open TopologicalSpace

variable (G : StructureGroupoid H) [HasGroupoid M G]
variable (s : Opens M)

/-- An open subset of a charted space is naturally a charted space. -/
protected instance instChartedSpace : ChartedSpace H s where
  atlas := ‚ãÉ x : s, {(chartAt H x.1).subtypeRestr ‚ü®x‚ü©}
  chartAt x := (chartAt H x.1).subtypeRestr ‚ü®x‚ü©
  mem_chart_source x := ‚ü®trivial, mem_chart_source H x.1‚ü©
  chart_mem_atlas x := by
    simp only [mem_iUnion, mem_singleton_iff]
    use x

lemma chartAt_eq {s : Opens M} {x : s} : chartAt H x = (chartAt H x.1).subtypeRestr ‚ü®x‚ü© := rfl

/-- If `s` is a non-empty open subset of `M`, every chart of `s` is the restriction
of some chart on `M`. -/
lemma chart_eq {s : Opens M} (hs : Nonempty s) {e : OpenPartialHomeomorph s H}
    (he : e ‚àà atlas H s) : ‚àÉ x : s, e = (chartAt H (x : M)).subtypeRestr hs := by
  rcases he with ‚ü®xset, ‚ü®x, hx‚ü©, he‚ü©
  exact ‚ü®x, mem_singleton_iff.mp (by convert he)‚ü©

/-- If `t` is a non-empty open subset of `H`,
every chart of `t` is the restriction of some chart on `H`. -/
-- XXX: can I unify this with `chart_eq`?
lemma chart_eq' {t : Opens H} (ht : Nonempty t) {e' : OpenPartialHomeomorph t H}
    (he' : e' ‚àà atlas H t) : ‚àÉ x : t, e' = (chartAt H ‚Üëx).subtypeRestr ht :=
  chart_eq ht he'

/-- If a groupoid `G` is `ClosedUnderRestriction`, then an open subset of a space which is
`HasGroupoid G` is naturally `HasGroupoid G`. -/
protected instance instHasGroupoid [ClosedUnderRestriction G] : HasGroupoid s G where
  compatible := by
    rintro e e' ‚ü®_, ‚ü®x, hc‚ü©, he‚ü© ‚ü®_, ‚ü®x', hc'‚ü©, he'‚ü©
    rw [hc.symm, mem_singleton_iff] at he
    rw [hc'.symm, mem_singleton_iff] at he'
    rw [he, he']
    refine G.mem_of_eqOnSource ?_
      (subtypeRestr_symm_trans_subtypeRestr (s := s) _ (chartAt H x) (chartAt H x'))
    apply closedUnderRestriction'
    ¬∑ exact G.compatible (chart_mem_atlas _ _) (chart_mem_atlas _ _)
    ¬∑ exact isOpen_inter_preimage_symm (chartAt _ _) s.2

theorem chartAt_subtype_val_symm_eventuallyEq (U : Opens M) {x : U} :
    (chartAt H x.val).symm =·∂†[ùìù (chartAt H x.val x.val)] Subtype.val ‚àò (chartAt H x).symm := by
  set e := chartAt H x.val
  have heUx_nhds : (e.subtypeRestr ‚ü®x‚ü©).target ‚àà ùìù (e x) := by
    apply (e.subtypeRestr ‚ü®x‚ü©).open_target.mem_nhds
    exact e.map_subtype_source ‚ü®x‚ü© (mem_chart_source _ _)
  exact Filter.eventuallyEq_of_mem heUx_nhds (e.subtypeRestr_symm_eqOn ‚ü®x‚ü©)

theorem chartAt_inclusion_symm_eventuallyEq {U V : Opens M} (hUV : U ‚â§ V) {x : U} :
    (chartAt H (Opens.inclusion hUV x)).symm
    =·∂†[ùìù (chartAt H (Opens.inclusion hUV x) (Set.inclusion hUV x))]
    Opens.inclusion hUV ‚àò (chartAt H x).symm := by
  set e := chartAt H (x : M)
  have heUx_nhds : (e.subtypeRestr ‚ü®x‚ü©).target ‚àà ùìù (e x) := by
    apply (e.subtypeRestr ‚ü®x‚ü©).open_target.mem_nhds
    exact e.map_subtype_source ‚ü®x‚ü© (mem_chart_source _ _)
  exact Filter.eventuallyEq_of_mem heUx_nhds <| e.subtypeRestr_symm_eqOn_of_le ‚ü®x‚ü©
    ‚ü®Opens.inclusion hUV x‚ü© hUV
end TopologicalSpace.Opens

/-- Restricting a chart of `M` to an open subset `s` yields a chart in the maximal atlas of `s`.

NB. We cannot deduce membership in `atlas H s` in general: by definition, this atlas contains
precisely the restriction of each preferred chart at `x ‚àà s` --- whereas `atlas H M`
can contain more charts than these. -/
lemma StructureGroupoid.subtypeRestr_mem_maximalAtlas {e : OpenPartialHomeomorph M H}
    (he : e ‚àà atlas H M) {s : Opens M} (hs : Nonempty s) {G : StructureGroupoid H} [HasGroupoid M G]
    [ClosedUnderRestriction G] : e.subtypeRestr hs ‚àà G.maximalAtlas s := by
  intro e' he'
  -- `e'` is the restriction of some chart of `M` at `x`,
  obtain ‚ü®x, this‚ü© := Opens.chart_eq hs he'
  rw [this]
  -- The transition functions between the unrestricted charts lie in the groupoid,
  -- the transition functions of the restriction are the restriction of the transition function.
  exact ‚ü®G.trans_restricted he (chart_mem_atlas H (x : M)) hs,
         G.trans_restricted (chart_mem_atlas H (x : M)) he hs‚ü©

@[deprecated (since := "2025-08-17")] alias StructureGroupoid.restriction_in_maximalAtlas :=
  StructureGroupoid.subtypeRestr_mem_maximalAtlas

/-! ### Structomorphisms -/

/-- A `G`-diffeomorphism between two charted spaces is a homeomorphism which, when read in the
charts, belongs to `G`. We avoid the word diffeomorph as it is too related to the smooth category,
and use structomorph instead. -/
structure Structomorph (G : StructureGroupoid H) (M : Type*) (M' : Type*) [TopologicalSpace M]
  [TopologicalSpace M'] [ChartedSpace H M] [ChartedSpace H M'] extends Homeomorph M M' where
  mem_groupoid : ‚àÄ c : OpenPartialHomeomorph M H, ‚àÄ c' : OpenPartialHomeomorph M' H, c ‚àà atlas H M ‚Üí
    c' ‚àà atlas H M' ‚Üí c.symm ‚â´‚Çï toHomeomorph.toOpenPartialHomeomorph ‚â´‚Çï c' ‚àà G

variable [TopologicalSpace M'] [TopologicalSpace M''] {G : StructureGroupoid H} [ChartedSpace H M']
  [ChartedSpace H M'']

/-- The identity is a diffeomorphism of any charted space, for any groupoid. -/
def Structomorph.refl (M : Type*) [TopologicalSpace M] [ChartedSpace H M] [HasGroupoid M G] :
    Structomorph G M M :=
  { Homeomorph.refl M with
    mem_groupoid := fun c c' hc hc' ‚Ü¶ by
      change OpenPartialHomeomorph.symm c ‚â´‚Çï OpenPartialHomeomorph.refl M ‚â´‚Çï c' ‚àà G
      rw [OpenPartialHomeomorph.refl_trans]
      exact G.compatible hc hc' }

/-- The inverse of a structomorphism is a structomorphism. -/
def Structomorph.symm (e : Structomorph G M M') : Structomorph G M' M :=
  { e.toHomeomorph.symm with
    mem_groupoid := by
      intro c c' hc hc'
      have : (c'.symm ‚â´‚Çï e.toHomeomorph.toOpenPartialHomeomorph ‚â´‚Çï c).symm ‚àà G :=
        G.symm (e.mem_groupoid c' c hc' hc)
      rwa [trans_symm_eq_symm_trans_symm, trans_symm_eq_symm_trans_symm, symm_symm, trans_assoc]
        at this }

/-- The composition of structomorphisms is a structomorphism. -/
def Structomorph.trans (e : Structomorph G M M') (e' : Structomorph G M' M'') :
    Structomorph G M M'' :=
  { Homeomorph.trans e.toHomeomorph e'.toHomeomorph with
    mem_groupoid := by
      /- Let c and c' be two charts in M and M''. We want to show that e' ‚àò e is smooth in these
      charts, around any point x. For this, let y = e (c‚Åª¬π x), and consider a chart g around y.
      Then g ‚àò e ‚àò c‚Åª¬π and c' ‚àò e' ‚àò g‚Åª¬π are both smooth as e and e' are structomorphisms, so
      their composition is smooth, and it coincides with c' ‚àò e' ‚àò e ‚àò c‚Åª¬π around x. -/
      intro c c' hc hc'
      refine G.locality fun x hx ‚Ü¶ ?_
      let f‚ÇÅ := e.toHomeomorph.toOpenPartialHomeomorph
      let f‚ÇÇ := e'.toHomeomorph.toOpenPartialHomeomorph
      let f := (e.toHomeomorph.trans e'.toHomeomorph).toOpenPartialHomeomorph
      have feq : f = f‚ÇÅ ‚â´‚Çï f‚ÇÇ := Homeomorph.trans_toOpenPartialHomeomorph _ _
      -- define the atlas g around y
      let y := (c.symm ‚â´‚Çï f‚ÇÅ) x
      let g := chartAt (H := H) y
      have hg‚ÇÅ := chart_mem_atlas (H := H) y
      have hg‚ÇÇ := mem_chart_source (H := H) y
      let s := (c.symm ‚â´‚Çï f‚ÇÅ).source ‚à© c.symm ‚â´‚Çï f‚ÇÅ ‚Åª¬π' g.source
      have open_s : IsOpen s := by
        apply (c.symm ‚â´‚Çï f‚ÇÅ).continuousOn_toFun.isOpen_inter_preimage <;> apply open_source
      have : x ‚àà s := by
        constructor
        ¬∑ simp only [f‚ÇÅ, trans_source, preimage_univ, inter_univ,
            Homeomorph.toOpenPartialHomeomorph_source]
          rw [trans_source] at hx
          exact hx.1
        ¬∑ exact hg‚ÇÇ
      refine ‚ü®s, open_s, this, ?_‚ü©
      let F‚ÇÅ := (c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï g) ‚â´‚Çï g.symm ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c'
      have A : F‚ÇÅ ‚àà G := G.trans (e.mem_groupoid c g hc hg‚ÇÅ) (e'.mem_groupoid g c' hg‚ÇÅ hc')
      let F‚ÇÇ := (c.symm ‚â´‚Çï f ‚â´‚Çï c').restr s
      have : F‚ÇÅ ‚âà F‚ÇÇ := calc
        F‚ÇÅ ‚âà c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï (g ‚â´‚Çï g.symm) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' := by
            simp only [F‚ÇÅ, trans_assoc, _root_.refl]
        _ ‚âà c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï ofSet g.source g.open_source ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' :=
          EqOnSource.trans' (_root_.refl _) (EqOnSource.trans' (_root_.refl _)
            (EqOnSource.trans' (self_trans_symm g) (_root_.refl _)))
        _ ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ) ‚â´‚Çï ofSet g.source g.open_source) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' := by
          simp only [trans_assoc, _root_.refl]
        _ ‚âà (c.symm ‚â´‚Çï f‚ÇÅ).restr s ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' := by rw [trans_of_set']
        _ ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c').restr s := by rw [restr_trans]
        _ ‚âà (c.symm ‚â´‚Çï (f‚ÇÅ ‚â´‚Çï f‚ÇÇ) ‚â´‚Çï c').restr s := by
          simp only [trans_assoc, _root_.refl]
        _ ‚âà F‚ÇÇ := by simp only [F‚ÇÇ, feq, _root_.refl]
      have : F‚ÇÇ ‚àà G := G.mem_of_eqOnSource A (Setoid.symm this)
      exact this }

/-- Restricting a chart to its source `s ‚äÜ M` yields a chart in the maximal atlas of `s`. -/
theorem StructureGroupoid.restriction_mem_maximalAtlas_subtype
    {e : OpenPartialHomeomorph M H} (he : e ‚àà atlas H M)
    (hs : Nonempty e.source) [HasGroupoid M G] [ClosedUnderRestriction G] :
    let s := { carrier := e.source, is_open' := e.open_source : Opens M }
    let t := { carrier := e.target, is_open' := e.open_target : Opens H }
    ‚àÄ c' ‚àà atlas H t,
      e.toHomeomorphSourceTarget.toOpenPartialHomeomorph ‚â´‚Çï c' ‚àà G.maximalAtlas s := by
  intro s t c' hc'
  have : Nonempty t := nonempty_coe_sort.mpr (e.mapsTo.nonempty (nonempty_coe_sort.mp hs))
  obtain ‚ü®x, hc'‚ü© := Opens.chart_eq this hc'
  -- As H has only one chart, `chartAt H x` is the identity: i.e., `c'` is the inclusion.
  rw [hc', (chartAt_self_eq)]
  -- Our expression equals this chart, at least on its source.
  rw [OpenPartialHomeomorph.subtypeRestr_def, OpenPartialHomeomorph.trans_refl]
  let goal :=
    e.toHomeomorphSourceTarget.toOpenPartialHomeomorph ‚â´‚Çï (t.openPartialHomeomorphSubtypeCoe this)
  have : goal ‚âà e.subtypeRestr (s := s) hs :=
    (goal.eqOnSource_iff (e.subtypeRestr (s := s) hs)).mpr
      ‚ü®by
        simp only [trans_toPartialEquiv, PartialEquiv.trans_source,
          Homeomorph.toOpenPartialHomeomorph_source, toFun_eq_coe,
          Homeomorph.toOpenPartialHomeomorph_apply, Opens.openPartialHomeomorphSubtypeCoe_source,
          preimage_univ, inter_self, subtypeRestr_source, goal, s]
        exact Subtype.coe_preimage_self _ |>.symm, by intro _ _; rfl‚ü©
  exact G.mem_maximalAtlas_of_eqOnSource (M := s) this (G.subtypeRestr_mem_maximalAtlas he hs)

/-- Each chart of a charted space is a structomorphism between its source and target. -/
def OpenPartialHomeomorph.toStructomorph {e : OpenPartialHomeomorph M H} (he : e ‚àà atlas H M)
    [HasGroupoid M G] [ClosedUnderRestriction G] :
    let s : Opens M := { carrier := e.source, is_open' := e.open_source }
    let t : Opens H := { carrier := e.target, is_open' := e.open_target }
    Structomorph G s t := by
  intro s t
  by_cases! h : Nonempty e.source
  ¬∑ exact { e.toHomeomorphSourceTarget with
      mem_groupoid :=
        -- The atlas of H on itself has only one chart, hence c' is the inclusion.
        -- Then, compatibility of `G` *almost* yields our claim --- except that `e` is a chart
        -- on `M` and `c` is one on `s`: we need to show that restricting `e` to `s` and composing
        -- with `c'` yields a chart in the maximal atlas of `s`.
        fun c c' hc hc' ‚Ü¶ G.compatible_of_mem_maximalAtlas (G.subset_maximalAtlas hc)
          (G.restriction_mem_maximalAtlas_subtype he h c' hc') }
  ¬∑ have : IsEmpty t := isEmpty_coe_sort.mpr
      (by convert e.image_source_eq_target ‚ñ∏ image_eq_empty.mpr (isEmpty_coe_sort.mp h))
    exact { Homeomorph.empty with
      -- `c'` cannot exist: it would be the restriction of `chartAt H x` at some `x ‚àà t`.
      mem_groupoid := fun _ c' _ ‚ü®_, ‚ü®x, _‚ü©, _‚ü© ‚Ü¶ (this.false x).elim }

end HasGroupoid
