/-
Copyright (c) 2024 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.ContMDiff.Defs
import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Geometry.Manifold.HasSmoothBoundary

/-!
## (Unoriented) bordism theory

This file defines the beginnings of (unoriented) bordism theory. For the full definition of
smooth oriented bordism groups, a number of prerequisites are missing from mathlib. However,
a significant amount of this work is already possible.

Currently, this file only contains the definition of *singular *n*-manifolds*:
bordism classes are the equivalence classes of singular n-manifolds w.r.t. the (co)bordism relation
and will be added in a future PR, as well as the definition of the (unoriented) bordism groups.

## Main definitions

- **SingularNManifold**: a singular `n`-manifold on a topological space `X`, for `n âˆˆ â„•`, is a pair
  `(M, f)` of a closed `n`-dimensional smooth manifold `M` together with a continuous map `M â†’ X`.
  We don't assume `M` to be modelled on `â„^n`, but add the model topological space `H`,
  the vector space `E` and the model with corners `I` as type parameters.
- `SingularNManifold.map`: a map `X â†’ Y` of topological spaces induces a map between the spaces
  of singular n-manifolds
- `SingularNManifold.comap`: if `(N,f)` is a singular n-manifold on `X`
  and `Ï†: M â†’ N` is continuous, the `comap` of `(N,f)` and `Ï†`
  is the induced singular n-manifold `(M, f âˆ˜ Ï†)` on `X`.
- `SingularNManifold.empty`: the empty set `M`, viewed as an `n`-manifold,
  as a singular `n`-manifold over any space `X`.
- `SingularNManifold.toPUnit`: an `n`-dimensional manifold induces a singular `n`-manifold
  on the one-point space.
- `SingularNManifold.prod`: the product of a singular `n`-manifold and a singular `m`-manifold
  on the one-point space, is a singular `n+m`-manifold on the one-point space.
- `SingularNManifold.sum`: the disjoint union of two singular `n`-manifolds
  is a singular `n`-manifold.

## Implementation notes

To be written! Document the design decisions and why they were made.

## TODO
- define cobordisms and the cobordism relation
- prove that the cobordisms relation is an equivalence relation
- define unoriented bordisms groups (as a set of equivalence classes),
prove they are a group
- define relative bordism groups (generalising the previous three points)
- prove that relative unoriented bordism groups define an extraordinary homology theory

## Tags

singular n-manifold, cobordism
-/

open scoped Manifold
open Module Set

noncomputable section

/-- A **singular `n`-manifold** on a topological space `X`, for `n âˆˆ â„•`, is a pair `(M, f)`
of a closed `n`-dimensional `C^k` manifold `M` together with a continuous map `M â†’ X`.
We assume that `M` is a manifold over the pair `(E, H)` with model `I`.

In practice, one commonly wants to take `k=âˆ` (as then e.g. the intersection form is a powerful tool
to compute bordism groups; for the definition, this makes no difference.) -/
structure SingularNManifold.{u} (X : Type*) [TopologicalSpace X] (k : WithTop â„•âˆ)
  {E H : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  [TopologicalSpace H] (I : ModelWithCorners â„ E H) where
  /-- The manifold `M` of a singular `n`-manifold `(M, f)` -/
  M : Type u
  /-- The manifold `M` is a topological space. -/
  [topSpaceM : TopologicalSpace M]
  /-- The manifold `M` is a charted space over `H`. -/
  [chartedSpace : ChartedSpace H M]
  /-- `M` is a `C^k` manifold. -/
  [isManifold : IsManifold I k M]
  [compactSpace : CompactSpace M]
  [boundaryless : BoundarylessManifold I M]
  /-- The underlying map `M â†’ X` of a singular `n`-manifold `(M, f)` on `X` -/
  f : M â†’ X
  hf : Continuous f

namespace SingularNManifold

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  {k : WithTop â„•âˆ}
  {E H M : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  [TopologicalSpace H] {I : ModelWithCorners â„ E H} [TopologicalSpace M] [ChartedSpace H M]
  [IsManifold I k M] [CompactSpace M] [BoundarylessManifold I M]

instance {s : SingularNManifold X k I} : TopologicalSpace s.M := s.topSpaceM

instance {s : SingularNManifold X k I} : ChartedSpace H s.M := s.chartedSpace

instance {s : SingularNManifold X k I} : IsManifold I k s.M := s.isManifold

instance {s : SingularNManifold X k I} : CompactSpace s.M := s.compactSpace

instance {s : SingularNManifold X k I} : BoundarylessManifold I s.M := s.boundaryless

/-- A map of topological spaces induces a corresponding map of singular n-manifolds. -/
-- This is part of proving functoriality of the bordism groups.
noncomputable def map (s : SingularNManifold X k I)
    {Ï† : X â†’ Y} (hÏ† : Continuous Ï†) : SingularNManifold Y k I where
  f := Ï† âˆ˜ s.f
  hf := hÏ†.comp s.hf

@[simp]
lemma map_f (s : SingularNManifold X k I) {Ï† : X â†’ Y} (hÏ† : Continuous Ï†) :
    (s.map hÏ†).f = Ï† âˆ˜ s.f :=
  rfl

lemma map_comp (s : SingularNManifold X k I)
    {Ï† : X â†’ Y} {Ïˆ : Y â†’ Z} (hÏ† : Continuous Ï†) (hÏˆ : Continuous Ïˆ) :
    ((s.map hÏ†).map hÏˆ).f = (Ïˆ âˆ˜ Ï†) âˆ˜ s.f := by
  simp [Function.comp_def]
  rfl

-- Let M' and W be real C^k manifolds.
variable {E' E'' E''' H' H'' H''' : Type*}
  [NormedAddCommGroup E'] [NormedSpace â„ E'] [NormedAddCommGroup E'']  [NormedSpace â„ E'']
  [NormedAddCommGroup E'''] [NormedSpace â„ E''']
  [TopologicalSpace H'] [TopologicalSpace H''] [TopologicalSpace H''']

variable {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  {I' : ModelWithCorners â„ E' H'} [IsManifold I' k M']
  [BoundarylessManifold I' M'] [CompactSpace M'] [FiniteDimensional â„ E']

variable (M I) in
/-- If `M` is `n`-dimensional and closed, it is a singular `n`-manifold over itself.-/
noncomputable def refl : SingularNManifold M k I where
  f := id
  hf := continuous_id

/-- If `(N, f)` is a singular `n`-manifold on `X` and `M` another `n`-dimensional manifold,
a continuous map `Ï† : M â†’ N` induces a singular `n`-manifold structure `(M, f âˆ˜ Ï†)` on `X`. -/
noncomputable def comap (s : SingularNManifold X k I)
    {Ï† : M â†’ s.M} (hÏ† : Continuous Ï†) : SingularNManifold X k I where
  f := s.f âˆ˜ Ï†
  hf := s.hf.comp hÏ†

@[simp, mfld_simps]
lemma comap_M (s : SingularNManifold X k I) {Ï† : M â†’ s.M} (hÏ† : Continuous Ï†) :
    (s.comap hÏ†).M = M := by
  rfl

@[simp, mfld_simps]
lemma comap_f (s : SingularNManifold X k I) {Ï† : M â†’ s.M} (hÏ† : Continuous Ï†) :
    (s.comap hÏ†).f = s.f âˆ˜ Ï† :=
  rfl

variable (X) in
/-- The canonical singular `n`-manifold associated to the empty set (seen as an `n`-dimensional
manifold, i.e. modelled on an `n`-dimensional space). -/
def empty (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
    (I : ModelWithCorners â„ E H) [IsManifold I k M] [IsEmpty M] : SingularNManifold X k I where
  M := M
  f x := (IsEmpty.false x).elim
  hf := by
    rw [continuous_iff_continuousAt]
    exact fun x â†¦ (IsEmpty.false x).elim

instance [IsEmpty M] : IsEmpty (SingularNManifold.empty X M I (k := k)).M := by
  unfold SingularNManifold.empty
  infer_instance

variable (M I) in
/-- An `n`-dimensional manifold induces a singular `n`-manifold on the one-point space. -/
def toPUnit : SingularNManifold PUnit k I where
  M := M
  f := fun _ â†¦ PUnit.unit
  hf := continuous_const

/-- The product of a singular `n`- and a singular `m`-manifold into a one-point space
is a singular `n+m`-manifold. -/
-- FUTURE: prove that this observation induces a commutative ring structure
-- on the unoriented bordism group `Î©_n^O = Î©_n^O(pt)`.
def prod (s : SingularNManifold PUnit k I) (t : SingularNManifold PUnit k I') :
    SingularNManifold PUnit k (I.prod I') where
  M := s.M Ã— t.M
  f := fun _ â†¦ PUnit.unit
  hf := continuous_const

variable (s t : SingularNManifold X k I)

/-- The disjoint union of two singular `n`-manifolds on `X` is a singular `n`-manifold on `X`. -/
-- We need to choose a model space for the disjoint union (as a priori `s` and `t` could be
-- modelled on very different spaces: for simplicity, we choose `â„^n`; all real work is contained
-- in the two instances above.
def sum (s t : SingularNManifold X k I) : SingularNManifold X k I where
  M := s.M âŠ• t.M
  f := Sum.elim s.f t.f
  hf := s.hf.sumElim t.hf

@[simp, mfld_simps]
lemma sum_M (s t : SingularNManifold X k I) : (s.sum t).M = (s.M âŠ• t.M) := rfl

@[simp, mfld_simps]
lemma sum_f (s t : SingularNManifold X k I) : (s.sum t).f = Sum.elim s.f t.f := rfl

end SingularNManifold

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

-- Let M and M' be smooth manifolds.
variable {E E' E'' E''' H H' H'' H''' : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup E'] [NormedSpace â„ E'] [NormedAddCommGroup E'']  [NormedSpace â„ E'']
  [NormedAddCommGroup E'''] [NormedSpace â„ E''']
  [TopologicalSpace H] [TopologicalSpace H'] [TopologicalSpace H''] [TopologicalSpace H''']

variable {k : WithTop â„•âˆ}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {I : ModelWithCorners â„ E H} [IsManifold I k M]
  -- {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M']
  -- /-{I' : ModelWithCorners â„ E H}-/ [IsManifold I k M']
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H M'']
  {I'' : ModelWithCorners â„ E H} [IsManifold I k M'']
  [CompactSpace M] [BoundarylessManifold I M]
  --[CompactSpace M'] [BoundarylessManifold I M']
  [CompactSpace M''] [BoundarylessManifold I M'']
  [CompactSpace M] [FiniteDimensional â„ E]
  --[CompactSpace M'] [FiniteDimensional â„ E'] [CompactSpace M''] [FiniteDimensional â„ E'']

variable (k) in
/-- An **unoriented cobordism** between two singular `n`-manifolds `(M,f)` and `(N,g)` on `X`
is a compact smooth `n`-manifold `W` with a continuous map `F: W â†’ X`
whose boundary is diffeomorphic to the disjoint union `M âŠ” N` such that `F` restricts to `f`
resp. `g` in the obvious way.

We prescribe the model with corners of the underlying manifold `W` as part of this type,
as glueing arguments require matching models to work. -/
structure UnorientedCobordism.{v} (s : SingularNManifold X k I) (t : SingularNManifold X k I)
    (J : ModelWithCorners â„ E' H') where
  /-- The underlying compact manifold of this unoriented cobordism -/
  W : Type v
  /-- The manifold `W` is a topological space. -/
  [topologicalSpace: TopologicalSpace W]
  [compactSpace : CompactSpace W]
  /-- The manifold `W` is a charted space over `H'`. -/
  [chartedSpace: ChartedSpace H' W]
  [isManifold: IsManifold J k W]
  /-- The presentation of the boundary `W` as a smooth manifold -/
  -- Future: we could allow bd.Mâ‚€ to be modelled on some other model, not necessarily I:
  -- we only care that this is fixed in the type.
  bd: BoundaryManifoldData W J k I
  /-- A continuous map `W â†’ X` of the cobordism into the topological space we work on -/
  F : W â†’ X
  hF : Continuous F
  /-- The boundary of `W` is diffeomorphic to the disjoint union `M âŠ” M'`. -/
  Ï† : Diffeomorph I I (s.M âŠ• t.M) bd.Mâ‚€ k
  /-- `F` restricted to `M â†ª âˆ‚W` equals `f`: this is formalised more nicely as
  `f = F âˆ˜ Î¹ âˆ˜ Ï†â»Â¹ : M â†’ X`, where `Î¹ : âˆ‚W â†’ W` is the inclusion. -/
  hFf : F âˆ˜ bd.f âˆ˜ Ï† âˆ˜ Sum.inl = s.f
  /-- `F` restricted to `N â†ª âˆ‚W` equals `g` -/
  hFg : F âˆ˜ bd.f âˆ˜ Ï† âˆ˜ Sum.inr = t.f

-- TODO: the checkUnivs linter complains that M and bd.Mâ‚€ only occur together

namespace UnorientedCobordism

variable {s s' t t' u : SingularNManifold X k I} {J : ModelWithCorners â„ E' H'}

instance (Ï† : UnorientedCobordism k s t J) : TopologicalSpace Ï†.W := Ï†.topologicalSpace

instance (Ï† : UnorientedCobordism k s t J) : CompactSpace Ï†.W := Ï†.compactSpace

instance (Ï† : UnorientedCobordism k s t J) : ChartedSpace H' Ï†.W := Ï†.chartedSpace

instance (Ï† : UnorientedCobordism k s t J) : IsManifold J k Ï†.W := Ï†.isManifold

def empty [IsEmpty M] : UnorientedCobordism k (SingularNManifold.empty X M I)
    (SingularNManifold.empty X M I) I where
  W := M
  -- XXX: generalise to any model J, by post-composing the boundary data
  bd := BoundaryManifoldData.of_boundaryless M I
  F x := (IsEmpty.false x).elim
  hF := by
    rw [continuous_iff_continuousAt]
    exact fun x â†¦ (IsEmpty.false x).elim
  Ï† := Diffeomorph.empty
  hFf := by ext x; exact (IsEmpty.false x).elim
  hFg := by ext x; exact (IsEmpty.false x).elim

/-- The disjoint union of two unoriented cobordisms (over the same model `J`). -/
noncomputable def sum (Ï† : UnorientedCobordism k s t J) (Ïˆ : UnorientedCobordism k s' t' J) :
    UnorientedCobordism k (s.sum s') (t.sum t') J where
  W := Ï†.W âŠ• Ïˆ.W
  bd := Ï†.bd.sum Ïˆ.bd
  F := Sum.elim Ï†.F Ïˆ.F
  hF := Ï†.hF.sumElim Ïˆ.hF
  Ï† := Diffeomorph.trans (Diffeomorph.sumSumSumComm I s.M k t.M s'.M t'.M).symm
      (Diffeomorph.sumCongr Ï†.Ï† Ïˆ.Ï†)
  hFf := by
    ext x
    cases x with
    | inl x =>
      set Î¦ := (Diffeomorph.sumSumSumComm I s.M k t.M s'.M t'.M).symm
      dsimp
      have : Î¦ (Sum.inl (Sum.inl x)) = Sum.inl (Sum.inl x) := sorry
      rw [this]
      simp --[Ï†.hFf]
      change (Ï†.F âˆ˜ Ï†.bd.f âˆ˜ Ï†.Ï† âˆ˜ Sum.inl) x = s.f x
      rw [Ï†.hFf]
    | inr x =>
      set Î¦ := (Diffeomorph.sumSumSumComm I s.M k t.M s'.M t'.M).symm
      dsimp
      have : Î¦ (Sum.inl (Sum.inr x)) = Sum.inr (Sum.inl x) := sorry
      rw [this]
      simp --[Ï†.hFf]
      change (Ïˆ.F âˆ˜ Ïˆ.bd.f âˆ˜ Ïˆ.Ï† âˆ˜ Sum.inl) x = s'.f x
      rw [Ïˆ.hFf]
  hFg := sorry -- analogous

/-- Suppose `W` is a cobordism between `M` and `N`.
Then a diffeomorphism `f : M'' â†’ M` induces a cobordism between `M''` and `N`. -/
def comap_fst (Ï† : UnorientedCobordism k s t J) (f : Diffeomorph I I M'' s.M k) :
    UnorientedCobordism k (s.comap f.continuous) t J where
  bd := Ï†.bd
  F := Ï†.F
  hF := Ï†.hF
  Ï† := Diffeomorph.trans (f.sumCongr (Diffeomorph.refl _ _ _)) Ï†.Ï†
  hFf := by dsimp; rw [â† Ï†.hFf]; congr
  hFg := by dsimp; rw [â† Ï†.hFg]; congr

variable (s) in
def refl : UnorientedCobordism k s s (I.prod (ğ“¡âˆ‚ 1)) where
  W := s.M Ã— (Set.Icc (0 : â„) 1)
  -- TODO: I want boundary data modelled on I, not I Ã— (âˆ‚[0,1])
  bd := sorry -- BoundaryManifoldData.prod_of_boundaryless_left s.M I (BoundaryManifoldData.Icc k)
  F := s.f âˆ˜ (fun p â†¦ p.1)
  hF := s.hf.comp continuous_fst
  Ï† := sorry
  hFf := sorry
  hFg := sorry

/-- Being cobordant is symmetric. -/
def symm (Ï† : UnorientedCobordism k s t J) : UnorientedCobordism k t s J where
  bd := Ï†.bd
  F := Ï†.F
  hF := Ï†.hF
  Ï† := (Diffeomorph.sumComm I t.M k s.M).trans Ï†.Ï†
  hFf := by rw [â† Ï†.hFg]; congr
  hFg := by rw [â† Ï†.hFf]; congr

section collarNeighbourhood

variable {Iâ‚€ : ModelWithCorners â„ E'' H''} [FiniteDimensional â„ E] [FiniteDimensional â„ E'']

open Fact.Manifold

namespace _root_

/-- A `C^k` collar neighbourhood of a smooth finite-dimensional manifold `M` with smooth boundary
of co-dimension one. -/
structure CollarNeighbourhood (bd : BoundaryManifoldData M I k Iâ‚€) where
  Îµ : â„
  hÎµ : 0 < Îµ
  -- XXX: I may want Ico instead; add if I need it
  Ï† : Set.Icc 0 Îµ Ã— bd.Mâ‚€ â†’ M
  contMDiff : haveI := Fact.mk hÎµ; ContMDiff (((ğ“¡âˆ‚ 1)).prod Iâ‚€) I k Ï†
  isEmbedding: Topology.IsEmbedding Ï†
  isImmersion: haveI := Fact.mk hÎµ; âˆ€ x, Function.Injective (mfderiv ((ğ“¡âˆ‚ 1).prod Iâ‚€) I Ï† x)

/- The collar neighbourhood theorem: if `M` is a compact finite-dimensional manifold
with smooth boundary of co-dimension one,
there exist some `Îµ > 0` and a smooth embedding `[0, Îµ) Ã— âˆ‚M â†’ M`, which maps `{0}Ã—âˆ‚M` to `âˆ‚M`.

Proof outline.
(1) construct a normal vector field `X` in a neighbourhood of `âˆ‚M`, pointing inwards
(In a chart on Euclidean half-space, we can just take the unit vector in the first component.
 These can be combined using e.g. a partition of unity.)
(1') It might simplify the next steps to `X` to a smooth global vector field on `M`, say be zero.
(2) Since `âˆ‚M` is compact, there is an `Îµ` such that the flow of `X` is defined for time `Îµ`.
  (This is not *exactly* the same as ongoing work, but should follow from the same ideas.)
(3) Thus, the flow of `X` defines a map `[0, Îµ) Ã— âˆ‚M â†’ M`
(4) Shrinking `Îµ` if needed, we can assume `Ï†` is a (topological) embedding.
  Since `âˆ‚M` is compact and `M` is Hausdorff, it suffices to show injectivity (and continuity).
  Each `xâˆˆâˆ‚M` has a neighbourhood `U_x` where the vector field looks like a flow box
  (by construction), hence the flow is injective on `U_x` for some time `Îµ_x`.
  Cover `âˆ‚M` with finitely many such neighbourhoods, then `Îµ := min Îµ_i` is positive, and
  each flow line does not self-intersect until time `Îµ`.
  Suppose the map `Ï†` is not injective, then `Ï†(x, t)=Ï†(x', t')`. Say `x âˆˆ U_i` and `x' âˆˆ U_j`,
  then `x, x' âˆ‰ U_i âˆ© U_j` by hypothesis, and `x, x'` lie inside separated closed sets:
  these are some positive distance apart. Now continuity and compactness yields a lower bound
  `Îµ_ij` for each pair, on which there is no intersection. (a bit sketchy, but mostly works)
(5) `Ï†` is smooth, since solutions of smooth ODEs depend smoothly on their initial conditions
(6) `Ï†` is an immersion... that should be obvious

Steps (4) and (5) definitely use ongoing work of Winston Yin; I don't know if the flow of a vector
field is already defined.
-/
def collar_neighbourhood_theorem (h : finrank â„ E = finrank â„ E'' + 1)
    (bd : BoundaryManifoldData M I k Iâ‚€) : CollarNeighbourhood bd := sorry

end _root_

end collarNeighbourhood

section trans

variable {n : â„•} [FiniteDimensional â„ E] [FiniteDimensional â„ E']

/-- Being cobordant is transitive: two `n+1`-dimensional cobordisms with `n`-dimensional boundary
can be glued along their common boundary (thanks to the collar neighbourhood theorem). -/
-- The proof depends on the collar neighbourhood theorem.
-- TODO: do I need a stronger definition of cobordisms, including a *choice* of collars?
-- At least, I need to argue that one *can* choose matching collars...
def trans (Ï† : UnorientedCobordism k s t J) (Ïˆ : UnorientedCobordism k t u J)
    (h : finrank â„ E' = finrank â„ E + 1) : UnorientedCobordism k t u J :=
  /- Outline of the proof:
    - using the collar neighbourhood theorem, choose matching collars for t in Ï† and Ïˆ
      invert the first collar, to get a map (-Îµ, 0] Ã— t.M â†’ Ï†.W
    - let W be the attaching space, of Ï†.W and Ïˆ.W along their common collar
      (i.e., we quotient the disjoint union Ï†.W âŠ• Ïˆ.W along the identification by the collars)
    - the union of the collars defines an open neighbourhood of `t.M`:
      this is where the hypothesis `h` is used
    - the quotient is a smooth manifold: away from the boundary, the charts come from W and W';
      on the image of t.M, we define charts using the common map by the collars
      (smoothness is the tricky part: this requires the collars to *match*!)
    - prove: the inclusions of `Ï†.W` and `Ïˆ.W` into this gluing are smooth
    - then, boundary data etc. are all easy to construct

  We could state a few more sorries, and provide more of an outline: we will not prove this in
  detail, this will be a larger project in itself. -/
  sorry

end trans

end UnorientedCobordism
