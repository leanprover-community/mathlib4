/-
Copyright (c) 2025 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Geometry.Manifold.Riemannian.PathELength
/-! # Riemannian manifolds

A Riemannian manifold `M` is a real manifold such that its tangent spaces are endowed with a
scalar product, depending smoothly on the point, and such that `M` has an emetric space
structure for which the distance is the infimum of lengths of paths. -/

open Bundle Bornology Set MeasureTheory Manifold
open scoped ENNReal ContDiff Topology

local notation "‚ü™" x ", " y "‚ü´" => inner ‚Ñù x y

noncomputable section

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ‚Ñù E H} {n : WithTop ‚Ñï‚àû}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

section

variable [EMetricSpace M] [ChartedSpace H M] [RiemannianBundle (fun (x : M) ‚Ü¶ TangentSpace I x)]

variable (I M) in
/-- Consider a manifold in which the tangent spaces are already endowed with a scalar product, and
already endowed with an extended distance. We say that this is a Riemannian manifold if the distance
is given by the infimum of the lengths of `C^1` paths, measured using the norm in the tangent
spaces.

This is a `Prop` valued typeclass, on top of existing data. -/
class IsRiemannianManifold : Prop where
  out (x y : M) : edist x y = riemannianEDist I x y

/- TODO: show that a vector space with an inner product is a Riemannian manifold. -/

end

section

open Bundle

variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace ‚Ñù F]

variable (F) in
/-- The standard riemannian metric on a vector space with an inner product, given by this inner
product on each tangent space. -/
noncomputable def riemannianMetricVectorSpace :
    ContMDiffRiemannianMetric ùìò(‚Ñù, F) œâ F (fun (x : F) ‚Ü¶ TangentSpace ùìò(‚Ñù, F) x) where
  inner x := (innerSL ‚Ñù (E := F) : F ‚ÜíL[‚Ñù] F ‚ÜíL[‚Ñù] ‚Ñù)
  symm x v w := real_inner_comm  _ _
  pos x v hv := real_inner_self_pos.2 hv
  isVonNBounded x := by
    change IsVonNBounded ‚Ñù {v : F | ‚ü™v, v‚ü´ < 1}
    have : Metric.ball (0 : F) 1 = {v : F | ‚ü™v, v‚ü´ < 1} := by
      ext v
      simp only [Metric.mem_ball, dist_zero_right, norm_eq_sqrt_re_inner (ùïú := ‚Ñù),
        RCLike.re_to_real, Set.mem_setOf_eq]
      conv_lhs => rw [show (1 : ‚Ñù) = ‚àö 1 by simp]
      rw [Real.sqrt_lt_sqrt_iff]
      exact real_inner_self_nonneg
    rw [‚Üê this]
    exact NormedSpace.isVonNBounded_ball ‚Ñù F 1
  contMDiff := by
    intro x
    rw [contMDiffAt_section]
    convert contMDiffAt_const (c := innerSL ‚Ñù)
    ext v w
    simp? [hom_trivializationAt_apply, ContinuousLinearMap.inCoordinates,
        Trivialization.linearMapAt_apply] says
      simp only [hom_trivializationAt_apply, ContinuousLinearMap.inCoordinates,
        TangentBundle.symmL_model_space, ContinuousLinearMap.coe_comp',
        Trivialization.continuousLinearMapAt_apply, Function.comp_apply,
        Trivialization.linearMapAt_apply, hom_trivializationAt_baseSet,
        TangentBundle.trivializationAt_baseSet, PartialHomeomorph.refl_partialEquiv,
        PartialEquiv.refl_source, PartialHomeomorph.singletonChartedSpace_chartAt_eq,
        Trivial.fiberBundle_trivializationAt', Trivial.trivialization_baseSet, inter_self, mem_univ,
        ‚ÜìreduceIte, Trivial.trivialization_apply]
    rfl

noncomputable instance : RiemannianBundle (fun (x : F) ‚Ü¶ TangentSpace ùìò(‚Ñù, F) x) :=
  ‚ü®(riemannianMetricVectorSpace F).toRiemannianMetric‚ü©

set_option synthInstance.maxHeartbeats 30000 in
-- otherwise, the instance is not found!
lemma norm_tangentSpace_vectorSpace {x : F} {v : TangentSpace ùìò(‚Ñù, F) x} :
    ‚Äñv‚Äñ = ‚Äñshow F from v‚Äñ := by
  rw [norm_eq_sqrt_real_inner, norm_eq_sqrt_real_inner]

set_option synthInstance.maxHeartbeats 30000 in
-- otherwise, the instance is not found!
lemma nnnorm_tangentSpace_vectorSpace {x : F} {v : TangentSpace ùìò(‚Ñù, F) x} :
    ‚Äñv‚Äñ‚Çä = ‚Äñshow F from v‚Äñ‚Çä := by
  simp [nnnorm, norm_tangentSpace_vectorSpace]

lemma enorm_tangentSpace_vectorSpace {x : F} {v : TangentSpace ùìò(‚Ñù, F) x} :
    ‚Äñv‚Äñ‚Çë = ‚Äñshow F from v‚Äñ‚Çë := by
  simp [enorm, nnnorm_tangentSpace_vectorSpace]

open MeasureTheory Measure

/-- An inner product vector space is a Riemannian manifold, i.e., the distance between two points
is the infimum of the lengths of paths between these points. -/
instance : IsRiemannianManifold ùìò(‚Ñù, F) F := by
  refine ‚ü®fun x y ‚Ü¶ le_antisymm ?_ ?_‚ü©
  ¬∑ simp only [riemannianEDist, le_iInf_iff]
    intro Œ≥ hŒ≥
    let e : ‚Ñù ‚Üí F := Œ≥ ‚àò (projIcc 0 1 zero_le_one)
    have D : ContDiffOn ‚Ñù 1 e (Icc 0 1) :=
      contMDiffOn_iff_contDiffOn.mp (hŒ≥.comp_contMDiffOn contMDiffOn_projIcc)
    rw [lintegral_norm_mfderiv_Icc_eq_pathELength_projIcc,
      pathELength_eq_lintegral_mfderivWithin_Icc]
    simp only [mfderivWithin_eq_fderivWithin, enorm_tangentSpace_vectorSpace]
    conv_lhs =>
      rw [edist_comm, edist_eq_enorm_sub, show x = e 0 by simp [e], show y = e 1 by simp [e]]
    exact (enorm_sub_le_lintegral_derivWithin_Icc_of_contDiffOn_Icc D zero_le_one).trans_eq rfl
  ¬∑ let Œ≥ := ContinuousAffineMap.lineMap (R := ‚Ñù) x y
    have : riemannianEDist ùìò(‚Ñù, F) x y ‚â§ pathELength ùìò(‚Ñù, F) Œ≥ 0 1 := by
      apply riemannianEDist_le_pathELength ?_ (by simp [Œ≥]) (by simp [Œ≥]) zero_le_one
      rw [contMDiffOn_iff_contDiffOn]
      exact Œ≥.contDiff.contDiffOn
    apply this.trans_eq
    rw [pathELength_eq_lintegral_mfderiv_Ioo]
    simp only [mfderiv_eq_fderiv, enorm_tangentSpace_vectorSpace]
    have : edist x y = ‚à´‚Åª (x_1 : ‚Ñù) in Ioo 0 1, ‚Äñy - x‚Äñ‚Çë := by
      simp [edist_comm x y, edist_eq_enorm_sub]
    rw [this]
    apply lintegral_congr (fun z ‚Ü¶ ?_)
    rw [show y - x = fderiv ‚Ñù (ContinuousAffineMap.lineMap (R := ‚Ñù) x y) z 1 by simp]
    rfl

end
