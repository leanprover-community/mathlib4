/-
Copyright (c) 2025 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Geometry.Manifold.MFDeriv.Atlas
import Mathlib.Geometry.Manifold.Riemannian.PathELength
import Mathlib.Geometry.Manifold.VectorBundle.Riemannian
import Mathlib.Geometry.Manifold.VectorBundle.Tangent
import Mathlib.MeasureTheory.Integral.IntervalIntegral.ContDiff

/-! # Riemannian manifolds

A Riemannian manifold `M` is a real manifold such that its tangent spaces are endowed with an
inner product, depending smoothly on the point, and such that `M` has an emetric space
structure for which the distance is the infimum of lengths of paths. -/

open Bundle Bornology Set MeasureTheory Manifold
open scoped ENNReal ContDiff Topology

local notation "âŸª" x ", " y "âŸ«" => inner â„ x y

noncomputable section

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners â„ E H} {n : WithTop â„•âˆ}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

section

variable [EMetricSpace M] [ChartedSpace H M] [RiemannianBundle (fun (x : M) â†¦ TangentSpace I x)]

variable (I M) in
/-- Consider a manifold in which the tangent spaces are already endowed with an inner product, and
the space is already endowed with an extended distance. We say that this is a Riemannian manifold
if the distance is given by the infimum of the lengths of `C^1` paths, measured using the norm in
the tangent spaces.

This is a `Prop` valued typeclass, on top of existing data. -/
class IsRiemannianManifold : Prop where
  out (x y : M) : edist x y = riemannianEDist I x y

end

section

variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]

variable (F) in
/-- The standard riemannian metric on a vector space with an inner product, given by this inner
product on each tangent space. -/
noncomputable def riemannianMetricVectorSpace :
    ContMDiffRiemannianMetric ğ“˜(â„, F) Ï‰ F (fun (x : F) â†¦ TangentSpace ğ“˜(â„, F) x) where
  inner x := (innerSL â„ (E := F) : F â†’L[â„] F â†’L[â„] â„)
  symm x v w := real_inner_comm  _ _
  pos x v hv := real_inner_self_pos.2 hv
  isVonNBounded x := by
    change IsVonNBounded â„ {v : F | âŸªv, vâŸ« < 1}
    have : Metric.ball (0 : F) 1 = {v : F | âŸªv, vâŸ« < 1} := by
      ext v
      simp only [Metric.mem_ball, dist_zero_right, norm_eq_sqrt_re_inner (ğ•œ := â„),
        RCLike.re_to_real, Set.mem_setOf_eq]
      conv_lhs => rw [show (1 : â„) = âˆš 1 by simp]
      rw [Real.sqrt_lt_sqrt_iff]
      exact real_inner_self_nonneg
    rw [â† this]
    exact NormedSpace.isVonNBounded_ball â„ F 1
  contMDiff := by
    intro x
    rw [contMDiffAt_section]
    convert contMDiffAt_const (c := innerSL â„)
    ext v w
    simp? [hom_trivializationAt_apply, ContinuousLinearMap.inCoordinates,
        Trivialization.linearMapAt_apply] says
      simp only [hom_trivializationAt_apply, ContinuousLinearMap.inCoordinates,
        TangentBundle.symmL_model_space, ContinuousLinearMap.coe_comp',
        Trivialization.continuousLinearMapAt_apply, Function.comp_apply,
        Trivialization.linearMapAt_apply, hom_trivializationAt_baseSet,
        TangentBundle.trivializationAt_baseSet, PartialHomeomorph.refl_partialEquiv,
        PartialEquiv.refl_source, PartialHomeomorph.singletonChartedSpace_chartAt_eq,
        Trivial.fiberBundle_trivializationAt', Trivial.trivialization_baseSet, inter_self, mem_univ,
        â†“reduceIte, Trivial.trivialization_apply]
    rfl

noncomputable instance : RiemannianBundle (fun (x : F) â†¦ TangentSpace ğ“˜(â„, F) x) :=
  âŸ¨(riemannianMetricVectorSpace F).toRiemannianMetricâŸ©

lemma norm_tangentSpace_vectorSpace {x : F} {v : TangentSpace ğ“˜(â„, F) x} :
    â€–vâ€– = â€–show F from vâ€– := by
  rw [norm_eq_sqrt_real_inner, norm_eq_sqrt_real_inner]

lemma nnnorm_tangentSpace_vectorSpace {x : F} {v : TangentSpace ğ“˜(â„, F) x} :
    â€–vâ€–â‚Š = â€–show F from vâ€–â‚Š := by
  simp [nnnorm, norm_tangentSpace_vectorSpace]

lemma enorm_tangentSpace_vectorSpace {x : F} {v : TangentSpace ğ“˜(â„, F) x} :
    â€–vâ€–â‚‘ = â€–show F from vâ€–â‚‘ := by
  simp [enorm, nnnorm_tangentSpace_vectorSpace]

open MeasureTheory Measure

lemma lintegral_fderiv_lineMap_eq_edist {x y : E} :
    âˆ«â» t in Icc 0 1, â€–fderivWithin â„ (ContinuousAffineMap.lineMap (R := â„) x y) (Icc 0 1) t 1â€–â‚‘
      = edist x y := by
  have : edist x y = âˆ«â» t in Icc (0 : â„) 1, â€–y - xâ€–â‚‘ := by
    simp [edist_comm x y, edist_eq_enorm_sub]
  rw [this]
  apply setLIntegral_congr_fun measurableSet_Icc (fun z hz â†¦ ?_)
  rw [show y - x = fderiv â„ (ContinuousAffineMap.lineMap (R := â„) x y) z 1 by simp]
  congr
  exact fderivWithin_eq_fderiv (uniqueDiffOn_Icc zero_lt_one _ hz)
    (ContinuousAffineMap.differentiableAt _)

/-- An inner product vector space is a Riemannian manifold, i.e., the distance between two points
is the infimum of the lengths of paths between these points. -/
instance : IsRiemannianManifold ğ“˜(â„, F) F := by
  refine âŸ¨fun x y â†¦ le_antisymm ?_ ?_âŸ©
  Â· simp only [riemannianEDist, le_iInf_iff]
    intro Î³ hÎ³
    let e : â„ â†’ F := Î³ âˆ˜ (projIcc 0 1 zero_le_one)
    have D : ContDiffOn â„ 1 e (Icc 0 1) :=
      contMDiffOn_iff_contDiffOn.mp (hÎ³.comp_contMDiffOn contMDiffOn_projIcc)
    rw [lintegral_norm_mfderiv_Icc_eq_pathELength_projIcc,
      pathELength_eq_lintegral_mfderivWithin_Icc]
    simp only [mfderivWithin_eq_fderivWithin, enorm_tangentSpace_vectorSpace]
    conv_lhs =>
      rw [edist_comm, edist_eq_enorm_sub, show x = e 0 by simp [e], show y = e 1 by simp [e]]
    exact (enorm_sub_le_lintegral_derivWithin_Icc_of_contDiffOn_Icc D zero_le_one).trans_eq rfl
  Â· let Î³ := ContinuousAffineMap.lineMap (R := â„) x y
    have : riemannianEDist ğ“˜(â„, F) x y â‰¤ pathELength ğ“˜(â„, F) Î³ 0 1 := by
      apply riemannianEDist_le_pathELength ?_ (by simp [Î³, ContinuousAffineMap.coe_lineMap_eq])
        (by simp [Î³, ContinuousAffineMap.coe_lineMap_eq]) zero_le_one
      rw [contMDiffOn_iff_contDiffOn]
      exact Î³.contDiff.contDiffOn
    apply this.trans_eq
    rw [pathELength_eq_lintegral_mfderivWithin_Icc]
    simp only [mfderivWithin_eq_fderivWithin, enorm_tangentSpace_vectorSpace]
    exact lintegral_fderiv_lineMap_eq_edist

end

open Manifold Metric
open scoped NNReal

variable [RiemannianBundle (fun (x : M) â†¦ TangentSpace I x)]
[IsManifold I 1 M]
[IsContinuousRiemannianBundle E (fun (x : M) â†¦ TangentSpace I x)]

section

/-- Register on the tangent space to a normed vector space the same `NormedAddCommGroup` structure
as in the vector space.

Should not be a global instance, as it does not coincide definitionally with the Riemannian
structure for inner product spaces, but can be activated locally. -/
def normedAddCommGroupTangentSpaceVectorSpace (x : E) :
    NormedAddCommGroup (TangentSpace ğ“˜(â„, E) x) :=
  inferInstanceAs (NormedAddCommGroup E)

attribute [local instance] normedAddCommGroupTangentSpaceVectorSpace

/-- Register on the tangent space to a normed vector space the same `NormedSpace` structure
as in the vector space.

Should not be a global instance, as it does not coincide definitionally with the Riemannian
structure for inner product spaces, but can be activated locally. -/
def normedSpaceTangentSpaceVectorSpace (x : E) : NormedSpace â„ (TangentSpace ğ“˜(â„, E) x) :=
  inferInstanceAs (NormedSpace â„ E)

attribute [local instance] normedSpaceTangentSpaceVectorSpace

variable (I)

lemma eventually_norm_mfderiv_extChartAt_lt (x : M) :
    âˆƒ C > 0, âˆ€á¶  y in ğ“ x, â€–mfderiv I ğ“˜(â„, E) (extChartAt I x) yâ€– < C := by
  rcases eventually_norm_trivializationAt_lt E (fun (x : M) â†¦ TangentSpace I x) x
    with âŸ¨C, C_pos, hCâŸ©
  refine âŸ¨C, C_pos, ?_âŸ©
  have hx : (chartAt H x).source âˆˆ ğ“ x := chart_source_mem_nhds H x
  filter_upwards [hC, hx] with y hy h'y
  rwa [â† TangentBundle.continuousLinearMapAt_trivializationAt h'y]

lemma eventually_norm_mfderivWithin_symm_extChartAt_comp_lt (x : M) :
    âˆƒ C > 0, âˆ€á¶  y in ğ“ x,
    â€–mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) (extChartAt I x y)â€– < C := by
  rcases eventually_norm_symmL_trivializationAt_lt E (fun (x : M) â†¦ TangentSpace I x) x
    with âŸ¨C, C_pos, hCâŸ©
  refine âŸ¨C, C_pos, ?_âŸ©
  have hx : (chartAt H x).source âˆˆ ğ“ x := chart_source_mem_nhds H x
  filter_upwards [hC, hx] with y hy h'y
  rw [TangentBundle.symmL_trivializationAt h'y] at hy
  have A : (extChartAt I x).symm (extChartAt I x y) = y :=
    (extChartAt I x).left_inv (by simpa using h'y)
  convert hy using 3 <;> congr

lemma eventually_norm_mfderivWithin_symm_extChartAt_lt (x : M) :
    âˆƒ C > 0, âˆ€á¶  y in ğ“[range I] (extChartAt I x x),
    â€–mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) yâ€– < C := by
  rcases eventually_norm_mfderivWithin_symm_extChartAt_comp_lt I x with âŸ¨C, C_pos, hCâŸ©
  refine âŸ¨C, C_pos, ?_âŸ©
  have : ğ“ x = ğ“ ((extChartAt I x).symm (extChartAt I x x)) := by simp
  rw [this] at hC
  have : ContinuousAt (extChartAt I x).symm (extChartAt I x x) := continuousAt_extChartAt_symm _
  filter_upwards [nhdsWithin_le_nhds (this.preimage_mem_nhds hC),
    extChartAt_target_mem_nhdsWithin x] with y hy h'y
  have : y = (extChartAt I x) ((extChartAt I x).symm y) := by simp [-extChartAt, h'y]
  simp [-extChartAt] at hy
  convert hy

lemma eventually_enorm_mfderivWithin_symm_extChartAt_lt (x : M) :
    âˆƒ C > (0 : â„â‰¥0), âˆ€á¶  y in ğ“[range I] (extChartAt I x x),
    â€–mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) yâ€–â‚‘ < C := by
  rcases eventually_norm_mfderivWithin_symm_extChartAt_lt I x with âŸ¨C, C_pos, hCâŸ©
  lift C to â„â‰¥0 using C_pos.le
  simp only [gt_iff_lt, NNReal.coe_pos] at C_pos
  refine âŸ¨C, C_pos, ?_âŸ©
  filter_upwards [hC] with y hy
  simp only [enorm, nnnorm]
  exact_mod_cast hy

/-- Around any point `x`, the Riemannian distance between two points is controlled by the distance
in the extended chart. In other words, the extended chart is locally Lipschitz. -/
lemma eventually_riemannianEDist_le_edist_extChartAt (x : M) :
    âˆƒ C > (0 : â„â‰¥0), âˆ€á¶  y in ğ“ x,
    riemannianEDist I x y â‰¤ C * edist (extChartAt I x x) (extChartAt I x y) := by
  /- To construct a path with controlled distance from `x` to `y`, we consider the segment from
  `extChartAt x x` to `extChartAt x y` in the chart, and we push it by `(extChartAt x).symm`. As
  the derivative of the latter is locally bounded, this only multiplies the length by a bounded
  amount. -/
  -- first start from a bound on the derivative
  rcases eventually_enorm_mfderivWithin_symm_extChartAt_lt I x with âŸ¨C, C_pos, hCâŸ©
  refine âŸ¨C, C_pos, ?_âŸ©
  -- consider a small convex set around `extChartAt x x` where everything is controlled.
  obtain âŸ¨r, r_pos, hrâŸ© : âˆƒ r > 0,
      ball (extChartAt I x x) r âˆ© range I âŠ† (extChartAt I x).target âˆ©
        {y | â€–mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) yâ€–â‚‘ < C} :=
    mem_nhdsWithin_iff.1 (Filter.inter_mem (extChartAt_target_mem_nhdsWithin x) hC)
  -- pull this set inside `M`: this is the set where we will get the estimate.
  have A : (extChartAt I x) â»Â¹' (ball (extChartAt I x x) r âˆ© range I) âˆˆ ğ“ x := by
    apply extChartAt_preimage_mem_nhds_of_mem_nhdsWithin (by simp)
    rw [inter_comm]
    exact inter_mem_nhdsWithin _ (ball_mem_nhds _ r_pos)
  -- consider `y` in this good set. Let `Î·` be the segment in the extended chart, and
  -- `Î³` its composition with `(extChartAt x).symm`.
  filter_upwards [A, chart_source_mem_nhds H x] with y hy h'y
  let Î· := ContinuousAffineMap.lineMap (R := â„) (extChartAt I x x) (extChartAt I x y)
  set Î³ := (extChartAt I x).symm âˆ˜ Î·
  -- by convexity, the whole segment between `extChartAt x x` and `extChartAt x y` is in the
  -- controlled set.
  have hÎ· : Icc 0 1 âŠ† â‡‘Î· â»Â¹' ((extChartAt I x).target âˆ©
        {y | â€–mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) yâ€–â‚‘ < C}) := by
    simp only [â† preimage_inter, â† image_subset_iff, ContinuousAffineMap.coe_lineMap_eq,
      â† segment_eq_image_lineMap, Î·]
    apply Subset.trans _ hr
    exact ((convex_ball _ _).inter I.convex_range).segment_subset (by simp [r_pos]) hy
  simp only [preimage_inter, subset_inter_iff] at hÎ·
  have Î·_smooth : ContMDiffOn ğ“˜(â„, â„) ğ“˜(â„, E) 1 Î· (Icc 0 1) := by
    apply ContMDiff.contMDiffOn
    rw [contMDiff_iff_contDiff]
    exact ContinuousAffineMap.contDiff _
  -- we can bound the Riemannian distance using the specific path `Î³`.
  have : riemannianEDist I x y â‰¤ pathELength I Î³ 0 1 := by
    apply riemannianEDist_le_pathELength _ _ _ zero_le_one
    Â· exact (contMDiffOn_extChartAt_symm x).comp Î·_smooth hÎ·.1
    Â· simp [Î³, Î·, ContinuousAffineMap.coe_lineMap_eq]
    Â· simp [Î³, Î·, ContinuousAffineMap.coe_lineMap_eq, h'y]
  apply this.trans
  -- Finally, we control the length of `Î³` thanks to the boundedness of the derivative of
  -- `(extChartAt x).symm` on the whole controlled set.
  rw [â† lintegral_fderiv_lineMap_eq_edist, pathELength_eq_lintegral_mfderivWithin_Icc,
    â† lintegral_const_mul' _ _ ENNReal.coe_ne_top]
  apply setLIntegral_mono' measurableSet_Icc (fun t ht â†¦ ?_)
  have : mfderivWithin ğ“˜(â„) I Î³ (Icc 0 1) t =
      (mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) (Î· t)) âˆ˜L
      (mfderivWithin ğ“˜(â„) ğ“˜(â„, E) Î· (Icc 0 1) t) := by
    apply mfderivWithin_comp
    Â· exact mdifferentiableWithinAt_extChartAt_symm (hÎ·.1 ht)
    Â· exact Î·_smooth.mdifferentiableOn le_rfl t ht
    Â· exact hÎ·.1.trans (preimage_mono (extChartAt_target_subset_range x))
    Â· rw [uniqueMDiffWithinAt_iff_uniqueDiffWithinAt]
      exact uniqueDiffOn_Icc zero_lt_one t ht
  have : mfderivWithin ğ“˜(â„) I Î³ (Icc 0 1) t 1 =
      (mfderivWithin ğ“˜(â„, E) I (extChartAt I x).symm (range I) (Î· t))
      (mfderivWithin ğ“˜(â„) ğ“˜(â„, E) Î· (Icc 0 1) t 1) := by
    rw [this]
    rfl
  rw [this]
  apply (ContinuousLinearMap.le_opNorm_enorm _ _).trans
  gcongr
  Â· exact (hÎ·.2 ht).le
  Â· simp only [mfderivWithin_eq_fderivWithin]
    exact le_of_eq rfl

/-- If points are close for the topology, then their Riemannian distance is small. -/
lemma eventually_riemmanianEDist_lt (x : M) {c : â„â‰¥0âˆ} (hc : 0 < c) :
    âˆ€á¶  y in ğ“ x, riemannianEDist I x y < c := by
  rcases eventually_riemannianEDist_le_edist_extChartAt I x with âŸ¨C, C_pos, hCâŸ©
  have : (extChartAt I x) â»Â¹' (EMetric.ball (extChartAt I x x) (c / C)) âˆˆ ğ“ x := by
    apply (continuousAt_extChartAt x).preimage_mem_nhds
    exact EMetric.ball_mem_nhds _ (ENNReal.div_pos hc.ne' (by simp))
  filter_upwards [this, hC] with y hy h'y
  apply h'y.trans_lt
  have : edist (extChartAt I x x) (extChartAt I x y) < c / C := by
    simpa only [mem_preimage, EMetric.mem_ball'] using hy
  rwa [ENNReal.lt_div_iff_mul_lt, mul_comm] at this
  Â· exact Or.inl (mod_cast C_pos.ne')
  Â· simp

end
