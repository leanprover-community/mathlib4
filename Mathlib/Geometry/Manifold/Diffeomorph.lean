/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Yury Kudryashov
-/
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.MFDeriv.UniqueDifferential

#align_import geometry.manifold.diffeomorph from "leanprover-community/mathlib"@"e354e865255654389cc46e6032160238df2e0f40"

/-!
# Diffeomorphisms
This file implements diffeomorphisms.

## Definitions

* `Diffeomorph I I' M M' n`:  `n`-times continuously differentiable diffeomorphism between
  `M` and `M'` with respect to I and I'; we do not introduce a separate definition for the case
  `n = âˆž`; we use notation instead.
* `Diffeomorph.toHomeomorph`: reinterpret a diffeomorphism as a homeomorphism.
* `ContinuousLinearEquiv.toDiffeomorph`: reinterpret a continuous equivalence as
  a diffeomorphism.
* `ModelWithCorners.transDiffeomorph`: compose a given `ModelWithCorners` with a diffeomorphism
  between the old and the new target spaces. Useful, e.g, to turn any finite dimensional manifold
  into a manifold modelled on a Euclidean space.
* `Diffeomorph.toTransDiffeomorph`: the identity diffeomorphism between `M` with model `I` and `M`
  with model `I.trans_diffeomorph e`.

## Notations

* `M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'`  := `Diffeomorph I J M N n`
* `M â‰ƒâ‚˜âŸ®I, I'âŸ¯ M'`    := `Diffeomorph I J M N âŠ¤`
* `E â‰ƒâ‚˜^n[ð•œ] E'`      := `E â‰ƒâ‚˜^nâŸ®ð“˜(ð•œ, E), ð“˜(ð•œ, E')âŸ¯ E'`
* `E â‰ƒâ‚˜[ð•œ] E'`        := `E â‰ƒâ‚˜âŸ®ð“˜(ð•œ, E), ð“˜(ð•œ, E')âŸ¯ E'`

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

## Keywords

diffeomorphism, manifold
-/


open scoped Manifold Topology

open Function Set

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ð•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ð•œ E'] {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ð•œ F] {H : Type*} [TopologicalSpace H] {H' : Type*}
  [TopologicalSpace H'] {G : Type*} [TopologicalSpace G] {G' : Type*} [TopologicalSpace G']
  {I : ModelWithCorners ð•œ E H} {I' : ModelWithCorners ð•œ E' H'} {J : ModelWithCorners ð•œ F G}
  {J' : ModelWithCorners ð•œ F G'}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {M' : Type*} [TopologicalSpace M']
  [ChartedSpace H' M'] {N : Type*} [TopologicalSpace N] [ChartedSpace G N] {N' : Type*}
  [TopologicalSpace N'] [ChartedSpace G' N'] {n : â„•âˆž}

section Defs

variable (I I' M M' n)

/-- `n`-times continuously differentiable diffeomorphism between `M` and `M'` with respect to `I`
and `I'`. -/
-- Porting note(#5171): was @[nolint has_nonempty_instance]
structure Diffeomorph extends M â‰ƒ M' where
  protected contMDiff_toFun : ContMDiff I I' n toEquiv
  protected contMDiff_invFun : ContMDiff I' I n toEquiv.symm
#align diffeomorph Diffeomorph

end Defs

@[inherit_doc]
scoped[Manifold] notation M " â‰ƒâ‚˜^" n:1000 "âŸ®" I ", " J "âŸ¯ " N => Diffeomorph I J M N n

/-- Infinitely differentiable diffeomorphism between `M` and `M'` with respect to `I` and `I'`. -/
scoped[Manifold] notation M " â‰ƒâ‚˜âŸ®" I ", " J "âŸ¯ " N => Diffeomorph I J M N âŠ¤

/-- `n`-times continuously differentiable diffeomorphism between `E` and `E'`. -/
scoped[Manifold]
  notation E " â‰ƒâ‚˜^" n:1000 "[" ð•œ "] " E' =>
    Diffeomorph (modelWithCornersSelf ð•œ E) (modelWithCornersSelf ð•œ E') E E' n

/-- Infinitely differentiable diffeomorphism between `E` and `E'`. -/
scoped[Manifold]
  notation E " â‰ƒâ‚˜[" ð•œ "] " E' =>
    Diffeomorph (modelWithCornersSelf ð•œ E) (modelWithCornersSelf ð•œ E') E E' âŠ¤

namespace Diffeomorph

theorem toEquiv_injective : Injective (Diffeomorph.toEquiv : (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') â†’ M â‰ƒ M')
  | âŸ¨_, _, _âŸ©, âŸ¨_, _, _âŸ©, rfl => rfl
#align diffeomorph.to_equiv_injective Diffeomorph.toEquiv_injective

instance : EquivLike (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') M M' where
  coe Î¦ := Î¦.toEquiv
  inv Î¦ := Î¦.toEquiv.symm
  left_inv Î¦ := Î¦.left_inv
  right_inv Î¦ := Î¦.right_inv
  coe_injective' _ _ h _ := toEquiv_injective <| DFunLike.ext' h

/-- Interpret a diffeomorphism as a `ContMDiffMap`. -/
@[coe]
def toContMDiffMap (Î¦ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : C^nâŸ®I, M; I', M'âŸ¯ :=
  âŸ¨Î¦, Î¦.contMDiff_toFunâŸ©

instance : Coe (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') C^nâŸ®I, M; I', M'âŸ¯ :=
  âŸ¨toContMDiffMapâŸ©

@[continuity]
protected theorem continuous (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : Continuous h :=
  h.contMDiff_toFun.continuous
#align diffeomorph.continuous Diffeomorph.continuous

protected theorem contMDiff (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : ContMDiff I I' n h :=
  h.contMDiff_toFun
#align diffeomorph.cont_mdiff Diffeomorph.contMDiff

protected theorem contMDiffAt (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') {x} : ContMDiffAt I I' n h x :=
  h.contMDiff.contMDiffAt
#align diffeomorph.cont_mdiff_at Diffeomorph.contMDiffAt

protected theorem contMDiffWithinAt (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') {s x} : ContMDiffWithinAt I I' n h s x :=
  h.contMDiffAt.contMDiffWithinAt
#align diffeomorph.cont_mdiff_within_at Diffeomorph.contMDiffWithinAt

-- Porting note (#11215): TODO: should use `E â‰ƒâ‚˜^n[ð•œ] F` notation
protected theorem contDiff (h : E â‰ƒâ‚˜^nâŸ®ð“˜(ð•œ, E), ð“˜(ð•œ, E')âŸ¯ E') : ContDiff ð•œ n h :=
  h.contMDiff.contDiff
#align diffeomorph.cont_diff Diffeomorph.contDiff

protected theorem smooth (h : M â‰ƒâ‚˜âŸ®I, I'âŸ¯ M') : Smooth I I' h := h.contMDiff
#align diffeomorph.smooth Diffeomorph.smooth

protected theorem mdifferentiable (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hn : 1 â‰¤ n) : MDifferentiable I I' h :=
  h.contMDiff.mdifferentiable hn
#align diffeomorph.mdifferentiable Diffeomorph.mdifferentiable

protected theorem mdifferentiableOn (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (s : Set M) (hn : 1 â‰¤ n) :
    MDifferentiableOn I I' h s :=
  (h.mdifferentiable hn).mdifferentiableOn
#align diffeomorph.mdifferentiable_on Diffeomorph.mdifferentiableOn

@[simp]
theorem coe_toEquiv (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : â‡‘h.toEquiv = h :=
  rfl
#align diffeomorph.coe_to_equiv Diffeomorph.coe_toEquiv

@[simp, norm_cast]
theorem coe_coe (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : â‡‘(h : C^nâŸ®I, M; I', M'âŸ¯) = h :=
  rfl
#align diffeomorph.coe_coe Diffeomorph.coe_coe

@[simp]
theorem toEquiv_inj {h h' : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'} : h.toEquiv = h'.toEquiv â†” h = h' :=
  toEquiv_injective.eq_iff
#align diffeomorph.to_equiv_inj Diffeomorph.toEquiv_inj

/-- Coercion to function `fun h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M' â†¦ (h : M â†’ M')` is injective. -/
theorem coeFn_injective : Injective ((â†‘) : (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') â†’ (M â†’ M')) :=
  DFunLike.coe_injective
#align diffeomorph.coe_fn_injective Diffeomorph.coeFn_injective

@[ext]
theorem ext {h h' : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'} (Heq : âˆ€ x, h x = h' x) : h = h' :=
  coeFn_injective <| funext Heq
#align diffeomorph.ext Diffeomorph.ext

instance : ContinuousMapClass (M â‰ƒâ‚˜âŸ®I, JâŸ¯ N) M N where
  map_continuous f := f.continuous

section

variable (M I n)

/-- Identity map as a diffeomorphism. -/
protected def refl : M â‰ƒâ‚˜^nâŸ®I, IâŸ¯ M where
  contMDiff_toFun := contMDiff_id
  contMDiff_invFun := contMDiff_id
  toEquiv := Equiv.refl M
#align diffeomorph.refl Diffeomorph.refl

@[simp]
theorem refl_toEquiv : (Diffeomorph.refl I M n).toEquiv = Equiv.refl _ :=
  rfl
#align diffeomorph.refl_to_equiv Diffeomorph.refl_toEquiv

@[simp]
theorem coe_refl : â‡‘(Diffeomorph.refl I M n) = id :=
  rfl
#align diffeomorph.coe_refl Diffeomorph.coe_refl

end

/-- Composition of two diffeomorphisms. -/
@[trans]
protected def trans (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N where
  contMDiff_toFun := hâ‚‚.contMDiff.comp hâ‚.contMDiff
  contMDiff_invFun := hâ‚.contMDiff_invFun.comp hâ‚‚.contMDiff_invFun
  toEquiv := hâ‚.toEquiv.trans hâ‚‚.toEquiv
#align diffeomorph.trans Diffeomorph.trans

@[simp]
theorem trans_refl (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : h.trans (Diffeomorph.refl I' M' n) = h :=
  ext fun _ => rfl
#align diffeomorph.trans_refl Diffeomorph.trans_refl

@[simp]
theorem refl_trans (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : (Diffeomorph.refl I M n).trans h = h :=
  ext fun _ => rfl
#align diffeomorph.refl_trans Diffeomorph.refl_trans

@[simp]
theorem coe_trans (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) : â‡‘(hâ‚.trans hâ‚‚) = hâ‚‚ âˆ˜ hâ‚ :=
  rfl
#align diffeomorph.coe_trans Diffeomorph.coe_trans

/-- Inverse of a diffeomorphism. -/
@[symm]
protected def symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : N â‰ƒâ‚˜^nâŸ®J, IâŸ¯ M where
  contMDiff_toFun := h.contMDiff_invFun
  contMDiff_invFun := h.contMDiff_toFun
  toEquiv := h.toEquiv.symm
#align diffeomorph.symm Diffeomorph.symm

@[simp]
theorem apply_symm_apply (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (x : N) : h (h.symm x) = x :=
  h.toEquiv.apply_symm_apply x
#align diffeomorph.apply_symm_apply Diffeomorph.apply_symm_apply

@[simp]
theorem symm_apply_apply (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (x : M) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x
#align diffeomorph.symm_apply_apply Diffeomorph.symm_apply_apply

@[simp]
theorem symm_refl : (Diffeomorph.refl I M n).symm = Diffeomorph.refl I M n :=
  ext fun _ => rfl
#align diffeomorph.symm_refl Diffeomorph.symm_refl

@[simp]
theorem self_trans_symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.trans h.symm = Diffeomorph.refl I M n :=
  ext h.symm_apply_apply
#align diffeomorph.self_trans_symm Diffeomorph.self_trans_symm

@[simp]
theorem symm_trans_self (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.symm.trans h = Diffeomorph.refl J N n :=
  ext h.apply_symm_apply
#align diffeomorph.symm_trans_self Diffeomorph.symm_trans_self

@[simp]
theorem symm_trans' (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) :
    (hâ‚.trans hâ‚‚).symm = hâ‚‚.symm.trans hâ‚.symm :=
  rfl
#align diffeomorph.symm_trans' Diffeomorph.symm_trans'

@[simp]
theorem symm_toEquiv (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.symm.toEquiv = h.toEquiv.symm :=
  rfl
#align diffeomorph.symm_to_equiv Diffeomorph.symm_toEquiv

@[simp, mfld_simps]
theorem toEquiv_coe_symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : â‡‘h.toEquiv.symm = h.symm :=
  rfl
#align diffeomorph.to_equiv_coe_symm Diffeomorph.toEquiv_coe_symm

theorem image_eq_preimage (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : Set M) : h '' s = h.symm â»Â¹' s :=
  h.toEquiv.image_eq_preimage s
#align diffeomorph.image_eq_preimage Diffeomorph.image_eq_preimage

theorem symm_image_eq_preimage (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : Set N) : h.symm '' s = h â»Â¹' s :=
  h.symm.image_eq_preimage s
#align diffeomorph.symm_image_eq_preimage Diffeomorph.symm_image_eq_preimage

@[simp, mfld_simps]
nonrec theorem range_comp {Î±} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (f : Î± â†’ M) :
    range (h âˆ˜ f) = h.symm â»Â¹' range f := by
  rw [range_comp, image_eq_preimage]
#align diffeomorph.range_comp Diffeomorph.range_comp

@[simp]
theorem image_symm_image (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : Set N) : h '' (h.symm '' s) = s :=
  h.toEquiv.image_symm_image s
#align diffeomorph.image_symm_image Diffeomorph.image_symm_image

@[simp]
theorem symm_image_image (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : Set M) : h.symm '' (h '' s) = s :=
  h.toEquiv.symm_image_image s
#align diffeomorph.symm_image_image Diffeomorph.symm_image_image

/-- A diffeomorphism is a homeomorphism. -/
def toHomeomorph (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : M â‰ƒâ‚œ N :=
  âŸ¨h.toEquiv, h.continuous, h.symm.continuousâŸ©
#align diffeomorph.to_homeomorph Diffeomorph.toHomeomorph

@[simp]
theorem toHomeomorph_toEquiv (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.toHomeomorph.toEquiv = h.toEquiv :=
  rfl
#align diffeomorph.to_homeomorph_to_equiv Diffeomorph.toHomeomorph_toEquiv

@[simp]
theorem symm_toHomeomorph (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.symm.toHomeomorph = h.toHomeomorph.symm :=
  rfl
#align diffeomorph.symm_to_homeomorph Diffeomorph.symm_toHomeomorph

@[simp]
theorem coe_toHomeomorph (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : â‡‘h.toHomeomorph = h :=
  rfl
#align diffeomorph.coe_to_homeomorph Diffeomorph.coe_toHomeomorph

@[simp]
theorem coe_toHomeomorph_symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : â‡‘h.toHomeomorph.symm = h.symm :=
  rfl
#align diffeomorph.coe_to_homeomorph_symm Diffeomorph.coe_toHomeomorph_symm

@[simp]
theorem contMDiffWithinAt_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'} {s x}
    (hm : m â‰¤ n) :
    ContMDiffWithinAt I I' m (f âˆ˜ h) s x â†” ContMDiffWithinAt J I' m f (h.symm â»Â¹' s) (h x) := by
  constructor
  Â· intro Hfh
    rw [â† h.symm_apply_apply x] at Hfh
    simpa only [(Â· âˆ˜ Â·), h.apply_symm_apply] using
      Hfh.comp (h x) (h.symm.contMDiffWithinAt.of_le hm) (mapsTo_preimage _ _)
  Â· rw [â† h.image_eq_preimage]
    exact fun hf => hf.comp x (h.contMDiffWithinAt.of_le hm) (mapsTo_image _ _)
#align diffeomorph.cont_mdiff_within_at_comp_diffeomorph_iff Diffeomorph.contMDiffWithinAt_comp_diffeomorph_iff

@[simp]
theorem contMDiffOn_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'} {s} (hm : m â‰¤ n) :
    ContMDiffOn I I' m (f âˆ˜ h) s â†” ContMDiffOn J I' m f (h.symm â»Â¹' s) :=
  h.toEquiv.forall_congr fun {_} => by
    simp only [hm, coe_toEquiv, h.symm_apply_apply, contMDiffWithinAt_comp_diffeomorph_iff,
      mem_preimage]
#align diffeomorph.cont_mdiff_on_comp_diffeomorph_iff Diffeomorph.contMDiffOn_comp_diffeomorph_iff

@[simp]
theorem contMDiffAt_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'} {x} (hm : m â‰¤ n) :
    ContMDiffAt I I' m (f âˆ˜ h) x â†” ContMDiffAt J I' m f (h x) :=
  h.contMDiffWithinAt_comp_diffeomorph_iff hm
#align diffeomorph.cont_mdiff_at_comp_diffeomorph_iff Diffeomorph.contMDiffAt_comp_diffeomorph_iff

@[simp]
theorem contMDiff_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'} (hm : m â‰¤ n) :
    ContMDiff I I' m (f âˆ˜ h) â†” ContMDiff J I' m f :=
  h.toEquiv.forall_congr <| h.contMDiffAt_comp_diffeomorph_iff hm
#align diffeomorph.cont_mdiff_comp_diffeomorph_iff Diffeomorph.contMDiff_comp_diffeomorph_iff

@[simp]
theorem contMDiffWithinAt_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M} (hm : m â‰¤ n)
    {s x} : ContMDiffWithinAt I' J m (h âˆ˜ f) s x â†” ContMDiffWithinAt I' I m f s x :=
  âŸ¨fun Hhf => by
    simpa only [(Â· âˆ˜ Â·), h.symm_apply_apply] using
      (h.symm.contMDiffAt.of_le hm).comp_contMDiffWithinAt _ Hhf,
    fun Hf => (h.contMDiffAt.of_le hm).comp_contMDiffWithinAt _ HfâŸ©
#align diffeomorph.cont_mdiff_within_at_diffeomorph_comp_iff Diffeomorph.contMDiffWithinAt_diffeomorph_comp_iff

@[simp]
theorem contMDiffAt_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M} (hm : m â‰¤ n) {x} :
    ContMDiffAt I' J m (h âˆ˜ f) x â†” ContMDiffAt I' I m f x :=
  h.contMDiffWithinAt_diffeomorph_comp_iff hm
#align diffeomorph.cont_mdiff_at_diffeomorph_comp_iff Diffeomorph.contMDiffAt_diffeomorph_comp_iff

@[simp]
theorem contMDiffOn_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M} (hm : m â‰¤ n) {s} :
    ContMDiffOn I' J m (h âˆ˜ f) s â†” ContMDiffOn I' I m f s :=
  forallâ‚‚_congr fun _ _ => h.contMDiffWithinAt_diffeomorph_comp_iff hm
#align diffeomorph.cont_mdiff_on_diffeomorph_comp_iff Diffeomorph.contMDiffOn_diffeomorph_comp_iff

@[simp]
theorem contMDiff_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M} (hm : m â‰¤ n) :
    ContMDiff I' J m (h âˆ˜ f) â†” ContMDiff I' I m f :=
  forall_congr' fun _ => h.contMDiffWithinAt_diffeomorph_comp_iff hm
#align diffeomorph.cont_mdiff_diffeomorph_comp_iff Diffeomorph.contMDiff_diffeomorph_comp_iff

theorem toPartialHomeomorph_mdifferentiable (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) :
    h.toHomeomorph.toPartialHomeomorph.MDifferentiable I J :=
  âŸ¨h.mdifferentiableOn _ hn, h.symm.mdifferentiableOn _ hnâŸ©
#align diffeomorph.to_local_homeomorph_mdifferentiable Diffeomorph.toPartialHomeomorph_mdifferentiable

section Constructions

/-- Product of two diffeomorphisms. -/
def prodCongr (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : N â‰ƒâ‚˜^nâŸ®J, J'âŸ¯ N') :
    (M Ã— N) â‰ƒâ‚˜^nâŸ®I.prod J, I'.prod J'âŸ¯ M' Ã— N' where
  contMDiff_toFun := (hâ‚.contMDiff.comp contMDiff_fst).prod_mk (hâ‚‚.contMDiff.comp contMDiff_snd)
  contMDiff_invFun :=
    (hâ‚.symm.contMDiff.comp contMDiff_fst).prod_mk (hâ‚‚.symm.contMDiff.comp contMDiff_snd)
  toEquiv := hâ‚.toEquiv.prodCongr hâ‚‚.toEquiv
#align diffeomorph.prod_congr Diffeomorph.prodCongr

@[simp]
theorem prodCongr_symm (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : N â‰ƒâ‚˜^nâŸ®J, J'âŸ¯ N') :
    (hâ‚.prodCongr hâ‚‚).symm = hâ‚.symm.prodCongr hâ‚‚.symm :=
  rfl
#align diffeomorph.prod_congr_symm Diffeomorph.prodCongr_symm

@[simp]
theorem coe_prodCongr (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : N â‰ƒâ‚˜^nâŸ®J, J'âŸ¯ N') :
    â‡‘(hâ‚.prodCongr hâ‚‚) = Prod.map hâ‚ hâ‚‚ :=
  rfl
#align diffeomorph.coe_prod_congr Diffeomorph.coe_prodCongr

section

variable (I J J' M N N' n)

/-- `M Ã— N` is diffeomorphic to `N Ã— M`. -/
def prodComm : (M Ã— N) â‰ƒâ‚˜^nâŸ®I.prod J, J.prod IâŸ¯ N Ã— M where
  contMDiff_toFun := contMDiff_snd.prod_mk contMDiff_fst
  contMDiff_invFun := contMDiff_snd.prod_mk contMDiff_fst
  toEquiv := Equiv.prodComm M N
#align diffeomorph.prod_comm Diffeomorph.prodComm

@[simp]
theorem prodComm_symm : (prodComm I J M N n).symm = prodComm J I N M n :=
  rfl
#align diffeomorph.prod_comm_symm Diffeomorph.prodComm_symm

@[simp]
theorem coe_prodComm : â‡‘(prodComm I J M N n) = Prod.swap :=
  rfl
#align diffeomorph.coe_prod_comm Diffeomorph.coe_prodComm

/-- `(M Ã— N) Ã— N'` is diffeomorphic to `M Ã— (N Ã— N')`. -/
def prodAssoc : ((M Ã— N) Ã— N') â‰ƒâ‚˜^nâŸ®(I.prod J).prod J', I.prod (J.prod J')âŸ¯ M Ã— N Ã— N' where
  contMDiff_toFun :=
    (contMDiff_fst.comp contMDiff_fst).prod_mk
      ((contMDiff_snd.comp contMDiff_fst).prod_mk contMDiff_snd)
  contMDiff_invFun :=
    (contMDiff_fst.prod_mk (contMDiff_fst.comp contMDiff_snd)).prod_mk
      (contMDiff_snd.comp contMDiff_snd)
  toEquiv := Equiv.prodAssoc M N N'
#align diffeomorph.prod_assoc Diffeomorph.prodAssoc

end

end Constructions

variable [SmoothManifoldWithCorners I M] [SmoothManifoldWithCorners J N]

theorem uniqueMDiffOn_image_aux (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) {s : Set M}
    (hs : UniqueMDiffOn I s) : UniqueMDiffOn J (h '' s) := by
  convert hs.uniqueMDiffOn_preimage (h.toPartialHomeomorph_mdifferentiable hn)
  simp [h.image_eq_preimage]
#align diffeomorph.unique_mdiff_on_image_aux Diffeomorph.uniqueMDiffOn_image_aux

@[simp]
theorem uniqueMDiffOn_image (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) {s : Set M} :
    UniqueMDiffOn J (h '' s) â†” UniqueMDiffOn I s :=
  âŸ¨fun hs => h.symm_image_image s â–¸ h.symm.uniqueMDiffOn_image_aux hn hs,
    h.uniqueMDiffOn_image_aux hnâŸ©
#align diffeomorph.unique_mdiff_on_image Diffeomorph.uniqueMDiffOn_image

@[simp]
theorem uniqueMDiffOn_preimage (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) {s : Set N} :
    UniqueMDiffOn I (h â»Â¹' s) â†” UniqueMDiffOn J s :=
  h.symm_image_eq_preimage s â–¸ h.symm.uniqueMDiffOn_image hn
#align diffeomorph.unique_mdiff_on_preimage Diffeomorph.uniqueMDiffOn_preimage

-- Porting note (#11215): TODO: should use `E â‰ƒâ‚˜^n[ð•œ] F` notation
@[simp]
theorem uniqueDiffOn_image (h : E â‰ƒâ‚˜^nâŸ®ð“˜(ð•œ, E), ð“˜(ð•œ, F)âŸ¯ F) (hn : 1 â‰¤ n) {s : Set E} :
    UniqueDiffOn ð•œ (h '' s) â†” UniqueDiffOn ð•œ s := by
  simp only [â† uniqueMDiffOn_iff_uniqueDiffOn, uniqueMDiffOn_image, hn]
#align diffeomorph.unique_diff_on_image Diffeomorph.uniqueDiffOn_image

@[simp]
-- Porting note (#11215): TODO: should use `E â‰ƒâ‚˜^n[ð•œ] F` notation
theorem uniqueDiffOn_preimage (h : E â‰ƒâ‚˜^nâŸ®ð“˜(ð•œ, E), ð“˜(ð•œ, F)âŸ¯ F) (hn : 1 â‰¤ n) {s : Set F} :
    UniqueDiffOn ð•œ (h â»Â¹' s) â†” UniqueDiffOn ð•œ s :=
  h.symm_image_eq_preimage s â–¸ h.symm.uniqueDiffOn_image hn
#align diffeomorph.unique_diff_on_preimage Diffeomorph.uniqueDiffOn_preimage

end Diffeomorph

namespace ContinuousLinearEquiv

variable (e : E â‰ƒL[ð•œ] E')

/-- A continuous linear equivalence between normed spaces is a diffeomorphism. -/
def toDiffeomorph : E â‰ƒâ‚˜[ð•œ] E' where
  contMDiff_toFun := e.contDiff.contMDiff
  contMDiff_invFun := e.symm.contDiff.contMDiff
  toEquiv := e.toLinearEquiv.toEquiv
#align continuous_linear_equiv.to_diffeomorph ContinuousLinearEquiv.toDiffeomorph

@[simp]
theorem coe_toDiffeomorph : â‡‘e.toDiffeomorph = e :=
  rfl
#align continuous_linear_equiv.coe_to_diffeomorph ContinuousLinearEquiv.coe_toDiffeomorph

@[simp]
theorem symm_toDiffeomorph : e.symm.toDiffeomorph = e.toDiffeomorph.symm :=
  rfl
#align continuous_linear_equiv.symm_to_diffeomorph ContinuousLinearEquiv.symm_toDiffeomorph

@[simp]
theorem coe_toDiffeomorph_symm : â‡‘e.toDiffeomorph.symm = e.symm :=
  rfl
#align continuous_linear_equiv.coe_to_diffeomorph_symm ContinuousLinearEquiv.coe_toDiffeomorph_symm

end ContinuousLinearEquiv

namespace ModelWithCorners

variable (I) (e : E â‰ƒâ‚˜[ð•œ] E')

/-- Apply a diffeomorphism (e.g., a continuous linear equivalence) to the model vector space. -/
def transDiffeomorph (I : ModelWithCorners ð•œ E H) (e : E â‰ƒâ‚˜[ð•œ] E') : ModelWithCorners ð•œ E' H where
  toPartialEquiv := I.toPartialEquiv.trans e.toEquiv.toPartialEquiv
  source_eq := by simp
  unique_diff' := by simp [range_comp e, I.unique_diff]
  continuous_toFun := e.continuous.comp I.continuous
  continuous_invFun := I.continuous_symm.comp e.symm.continuous
#align model_with_corners.trans_diffeomorph ModelWithCorners.transDiffeomorph

@[simp, mfld_simps]
theorem coe_transDiffeomorph : â‡‘(I.transDiffeomorph e) = e âˆ˜ I :=
  rfl
#align model_with_corners.coe_trans_diffeomorph ModelWithCorners.coe_transDiffeomorph

@[simp, mfld_simps]
theorem coe_transDiffeomorph_symm : â‡‘(I.transDiffeomorph e).symm = I.symm âˆ˜ e.symm :=
  rfl
#align model_with_corners.coe_trans_diffeomorph_symm ModelWithCorners.coe_transDiffeomorph_symm

theorem transDiffeomorph_range : range (I.transDiffeomorph e) = e '' range I :=
  range_comp e I
#align model_with_corners.trans_diffeomorph_range ModelWithCorners.transDiffeomorph_range

theorem coe_extChartAt_transDiffeomorph (x : M) :
    â‡‘(extChartAt (I.transDiffeomorph e) x) = e âˆ˜ extChartAt I x :=
  rfl
#align model_with_corners.coe_ext_chart_at_trans_diffeomorph ModelWithCorners.coe_extChartAt_transDiffeomorph

theorem coe_extChartAt_transDiffeomorph_symm (x : M) :
    â‡‘(extChartAt (I.transDiffeomorph e) x).symm = (extChartAt I x).symm âˆ˜ e.symm :=
  rfl
#align model_with_corners.coe_ext_chart_at_trans_diffeomorph_symm ModelWithCorners.coe_extChartAt_transDiffeomorph_symm

theorem extChartAt_transDiffeomorph_target (x : M) :
    (extChartAt (I.transDiffeomorph e) x).target = e.symm â»Â¹' (extChartAt I x).target := by
  simp only [e.range_comp, preimage_preimage, mfld_simps]; rfl
#align model_with_corners.ext_chart_at_trans_diffeomorph_target ModelWithCorners.extChartAt_transDiffeomorph_target

end ModelWithCorners

namespace Diffeomorph

variable (e : E â‰ƒâ‚˜[ð•œ] F)

instance smoothManifoldWithCorners_transDiffeomorph [SmoothManifoldWithCorners I M] :
    SmoothManifoldWithCorners (I.transDiffeomorph e) M := by
  refine smoothManifoldWithCorners_of_contDiffOn (I.transDiffeomorph e) M fun eâ‚ eâ‚‚ hâ‚ hâ‚‚ => ?_
  refine e.contDiff.comp_contDiffOn
      (((contDiffGroupoid âŠ¤ I).compatible hâ‚ hâ‚‚).1.comp e.symm.contDiff.contDiffOn ?_)
  mfld_set_tac
#align diffeomorph.smooth_manifold_with_corners_trans_diffeomorph Diffeomorph.smoothManifoldWithCorners_transDiffeomorph

variable (I M)

/-- The identity diffeomorphism between a manifold with model `I` and the same manifold
with model `I.trans_diffeomorph e`. -/
def toTransDiffeomorph (e : E â‰ƒâ‚˜[ð•œ] F) : M â‰ƒâ‚˜âŸ®I, I.transDiffeomorph eâŸ¯ M where
  toEquiv := Equiv.refl M
  contMDiff_toFun x := by
    refine contMDiffWithinAt_iff'.2 âŸ¨continuousWithinAt_id, ?_âŸ©
    refine e.contDiff.contDiffWithinAt.congr' (fun y hy â†¦ ?_) ?_
    Â· simp only [Equiv.coe_refl, id, (Â· âˆ˜ Â·), I.coe_extChartAt_transDiffeomorph,
        (extChartAt I x).right_inv hy.1]
    Â· exact
      âŸ¨(extChartAt I x).map_source (mem_extChartAt_source I x), trivial, by simp only [mfld_simps]âŸ©
  contMDiff_invFun x := by
    refine contMDiffWithinAt_iff'.2 âŸ¨continuousWithinAt_id, ?_âŸ©
    refine e.symm.contDiff.contDiffWithinAt.congr' (fun y hy => ?_) ?_
    Â· simp only [mem_inter_iff, I.extChartAt_transDiffeomorph_target] at hy
      simp only [Equiv.coe_refl, Equiv.refl_symm, id, (Â· âˆ˜ Â·),
        I.coe_extChartAt_transDiffeomorph_symm, (extChartAt I x).right_inv hy.1]
    exact âŸ¨(extChartAt _ x).map_source (mem_extChartAt_source _ x), trivial, by
      simp only [e.symm_apply_apply, Equiv.refl_symm, Equiv.coe_refl, mfld_simps]âŸ©
#align diffeomorph.to_trans_diffeomorph Diffeomorph.toTransDiffeomorph

variable {I M}

@[simp]
theorem contMDiffWithinAt_transDiffeomorph_right {f : M' â†’ M} {x s} :
    ContMDiffWithinAt I' (I.transDiffeomorph e) n f s x â†” ContMDiffWithinAt I' I n f s x :=
  (toTransDiffeomorph I M e).contMDiffWithinAt_diffeomorph_comp_iff le_top
#align diffeomorph.cont_mdiff_within_at_trans_diffeomorph_right Diffeomorph.contMDiffWithinAt_transDiffeomorph_right

@[simp]
theorem contMDiffAt_transDiffeomorph_right {f : M' â†’ M} {x} :
    ContMDiffAt I' (I.transDiffeomorph e) n f x â†” ContMDiffAt I' I n f x :=
  (toTransDiffeomorph I M e).contMDiffAt_diffeomorph_comp_iff le_top
#align diffeomorph.cont_mdiff_at_trans_diffeomorph_right Diffeomorph.contMDiffAt_transDiffeomorph_right

@[simp]
theorem contMDiffOn_transDiffeomorph_right {f : M' â†’ M} {s} :
    ContMDiffOn I' (I.transDiffeomorph e) n f s â†” ContMDiffOn I' I n f s :=
  (toTransDiffeomorph I M e).contMDiffOn_diffeomorph_comp_iff le_top
#align diffeomorph.cont_mdiff_on_trans_diffeomorph_right Diffeomorph.contMDiffOn_transDiffeomorph_right

@[simp]
theorem contMDiff_transDiffeomorph_right {f : M' â†’ M} :
    ContMDiff I' (I.transDiffeomorph e) n f â†” ContMDiff I' I n f :=
  (toTransDiffeomorph I M e).contMDiff_diffeomorph_comp_iff le_top
#align diffeomorph.cont_mdiff_trans_diffeomorph_right Diffeomorph.contMDiff_transDiffeomorph_right

-- Porting note (#10618): was `@[simp]` but now `simp` can prove it
theorem smooth_transDiffeomorph_right {f : M' â†’ M} :
    Smooth I' (I.transDiffeomorph e) f â†” Smooth I' I f :=
  contMDiff_transDiffeomorph_right e
#align diffeomorph.smooth_trans_diffeomorph_right Diffeomorph.smooth_transDiffeomorph_right

@[simp]
theorem contMDiffWithinAt_transDiffeomorph_left {f : M â†’ M'} {x s} :
    ContMDiffWithinAt (I.transDiffeomorph e) I' n f s x â†” ContMDiffWithinAt I I' n f s x :=
  ((toTransDiffeomorph I M e).contMDiffWithinAt_comp_diffeomorph_iff le_top).symm
#align diffeomorph.cont_mdiff_within_at_trans_diffeomorph_left Diffeomorph.contMDiffWithinAt_transDiffeomorph_left

@[simp]
theorem contMDiffAt_transDiffeomorph_left {f : M â†’ M'} {x} :
    ContMDiffAt (I.transDiffeomorph e) I' n f x â†” ContMDiffAt I I' n f x :=
  ((toTransDiffeomorph I M e).contMDiffAt_comp_diffeomorph_iff le_top).symm
#align diffeomorph.cont_mdiff_at_trans_diffeomorph_left Diffeomorph.contMDiffAt_transDiffeomorph_left

@[simp]
theorem contMDiffOn_transDiffeomorph_left {f : M â†’ M'} {s} :
    ContMDiffOn (I.transDiffeomorph e) I' n f s â†” ContMDiffOn I I' n f s :=
  ((toTransDiffeomorph I M e).contMDiffOn_comp_diffeomorph_iff le_top).symm
#align diffeomorph.cont_mdiff_on_trans_diffeomorph_left Diffeomorph.contMDiffOn_transDiffeomorph_left

@[simp]
theorem contMDiff_transDiffeomorph_left {f : M â†’ M'} :
    ContMDiff (I.transDiffeomorph e) I' n f â†” ContMDiff I I' n f :=
  ((toTransDiffeomorph I M e).contMDiff_comp_diffeomorph_iff le_top).symm
#align diffeomorph.cont_mdiff_trans_diffeomorph_left Diffeomorph.contMDiff_transDiffeomorph_left

-- Porting note (#10618): was `@[simp]` but now `simp` can prove it
theorem smooth_transDiffeomorph_left {f : M â†’ M'} :
    Smooth (I.transDiffeomorph e) I' f â†” Smooth I I' f :=
  e.contMDiff_transDiffeomorph_left
#align diffeomorph.smooth_trans_diffeomorph_left Diffeomorph.smooth_transDiffeomorph_left

end Diffeomorph
