/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.IsManifold.Basic

/-! # Local properties of smooth functions which depend on both the source and target

In this file, we consider local properties of functions between manifolds, which depend on both the
source and the target: more precisely, properties `P` of functions `f : M ‚Üí N` such that
`f` has property `P` if and only if there is a suitable pair of charts on `M` and `N`, respectively,
such that `f` read in these charts has a particular form.
The motivating example of this general description are immersions and submersions:
`f : M ‚Üí N` is an immersion at `x` iff there are charts `œÜ` and `œà` of `M` and `N` around `x` and
`f x`, respectively, such that in these charts, `f` looks like `u ‚Ü¶ (u, 0)`. Similarly, `f` is a
submersion at `x` iff it looks like a projection `(u, v) ‚Ü¶ u` in suitable charts near `x` and `f x`.

Studying such local properties allows proving several lemmas about immersions and submersions
only once. In `IsImmersionEmbedding.lean`, we prove that being an immersion at `x` is indeed a
local property of this form.

## Main definitions and results

* `Manifold.LocalSourceTargetPropertyAt` captures a local property of the above form:
  for each `f : M ‚Üí N`, and pair of charts `œÜ` of `M` and `œà` of `N`, the local property is either
  safisfied or not.
  We ask that the property be stable under congruence and under restriction of `œÜ`.
* `Manifold.LiftSourceTargetPropertyAt f x P`, where `P` is a `LocalSourceTargetPropertyAt`,
  defines a local property of functions of the above shape:
  `f` has this property at `x` if there exist charts `œÜ` and `œà` such that `P f œÜ œà` holds.
* `Manifold.LiftSourceTargetPropertyAt.congr_of_eventuallyEq`: if `f` has property `P` at `x`
  and `g` equals `f` near `x`, then `g` also has property `P` at `x`.

-/

open scoped Manifold Topology ContDiff

open Function Set

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {H : Type*} [TopologicalSpace H] {G : Type*} [TopologicalSpace G]
  {I : ModelWithCorners ùïú E H} {I' : ModelWithCorners ùïú F G}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N] {n : WithTop ‚Ñï‚àû}

namespace Manifold

/-- Structure recording good behaviour of a property of functions `M ‚Üí N` w.r.t. to compatible
choices of both a chart on `M` and `N`. Currently, we ask for the property being stable under
restriction of the domain chart, and local in the target.

Motivating examples are immersions and submersions of smooth manifolds. -/
structure IsLocalSourceTargetProperty
    (P : (M ‚Üí N) ‚Üí OpenPartialHomeomorph M H ‚Üí OpenPartialHomeomorph N G ‚Üí Prop) : Prop where
  mono_source : ‚àÄ {f : M ‚Üí N}, ‚àÄ {œÜ : OpenPartialHomeomorph M H}, ‚àÄ {œà : OpenPartialHomeomorph N G},
    ‚àÄ {s : Set M}, IsOpen s ‚Üí P f œÜ œà ‚Üí P f (œÜ.restr s) œà
  -- Note: the analogous `mono_target` statement is true for both immersions and submersions.
  -- If and when a future lemma requires it, add this here.
  congr : ‚àÄ {f g : M ‚Üí N}, ‚àÄ {œÜ : OpenPartialHomeomorph M H}, ‚àÄ {œà : OpenPartialHomeomorph N G},
    EqOn f g œÜ.source ‚Üí P f œÜ œà ‚Üí P g œÜ œà

variable (I I' n) in
/-- Data witnessing the fact that `f` has local property `P` at `x` -/
structure LocalPresentationAt (f : M ‚Üí N) (x : M)
    (P : (M ‚Üí N) ‚Üí OpenPartialHomeomorph M H ‚Üí OpenPartialHomeomorph N G ‚Üí Prop) where
  /-- A choice of chart on the domain `M` of the local property `P` of `f` at `x`:
  w.r.t. this chart and `codChart`, `f` has the local property `P` at `x`. -/
  domChart : OpenPartialHomeomorph M H
  /-- A choice of chart on the target `N` of the local property `P` of `f` at `x`:
  w.r.t. this chart and `domChart`, `f` has the local property `P` at `x`. -/
  codChart : OpenPartialHomeomorph N G
  mem_domChart_source : x ‚àà domChart.source
  mem_codChart_source : f x ‚àà codChart.source
  domChart_mem_maximalAtlas : domChart ‚àà IsManifold.maximalAtlas I n M
  codChart_mem_maximalAtlas : codChart ‚àà IsManifold.maximalAtlas I' n N
  source_subset_preimage_source : domChart.source ‚äÜ f ‚Åª¬π' codChart.source
  property : P f domChart codChart

variable (I I' n) in
/-- A property of smooth functions `M ‚Üí N` which is local at both the source and target:
a property `P` is local at `x` iff there exist charts `œÜ` and `œà` of `M` and `N` around
`x` and `f x`, respectively, such that `f` satisfies the property w.r.t. `œÜ` and `œà`.

The motivating example are smooth immersions and submersions: the corresponding condition is that
`f` look like the inclusion `u ‚Ü¶ (u, 0)` (resp. a projection `(u, v) ‚Ü¶ u`)
in the charts `œÜ` and `œà`.
-/
def LiftSourceTargetPropertyAt (f : M ‚Üí N) (x : M)
    (P : (M ‚Üí N) ‚Üí OpenPartialHomeomorph M H ‚Üí OpenPartialHomeomorph N G ‚Üí Prop) : Prop :=
  Nonempty (LocalPresentationAt I I' n f x P)

namespace LiftSourceTargetPropertyAt

variable {f g : M ‚Üí N} {x : M}
  {P : (M ‚Üí N) ‚Üí OpenPartialHomeomorph M H ‚Üí OpenPartialHomeomorph N G ‚Üí Prop}

/-- A choice of charts witnessing the local property `P` of `f` at `x`. -/
noncomputable def localPresentationAt (h : LiftSourceTargetPropertyAt I I' n f x P) :
    LocalPresentationAt I I' n f x P :=
  Classical.choice h

/-- A choice of chart on the domain `M` of a local property of `f` at `x`:
w.r.t. this chart and `h.codChart`, `f` has the local property `P` at `x`.
The particular chart is arbitrary, but this choice matches the witness given by `h.codChart`. -/
noncomputable def domChart (h : LiftSourceTargetPropertyAt I I' n f x P) :
    OpenPartialHomeomorph M H :=
  h.localPresentationAt.domChart

/-- A choice of chart on the co-domain `N` of a local property of `f` at `x`:
w.r.t. this chart and `h.domChart`, `f` has the local property `P` at `x`
The particular chart is arbitrary, but this choice matches the witness given by `h.domChart`. -/
noncomputable def codChart (h : LiftSourceTargetPropertyAt I I' n f x P) :
    OpenPartialHomeomorph N G :=
  h.localPresentationAt.codChart

lemma mem_domChart_source (h : LiftSourceTargetPropertyAt I I' n f x P) :
    x ‚àà h.domChart.source :=
  h.localPresentationAt.mem_domChart_source

lemma mem_codChart_source (h : LiftSourceTargetPropertyAt I I' n f x P) :
    f x ‚àà h.codChart.source :=
  h.localPresentationAt.mem_codChart_source

lemma domChart_mem_maximalAtlas (h : LiftSourceTargetPropertyAt I I' n f x P) :
    h.domChart ‚àà IsManifold.maximalAtlas I n M :=
  h.localPresentationAt.domChart_mem_maximalAtlas

lemma codChart_mem_maximalAtlas (h : LiftSourceTargetPropertyAt I I' n f x P) :
    h.codChart ‚àà IsManifold.maximalAtlas I' n N :=
  h.localPresentationAt.codChart_mem_maximalAtlas

lemma source_subset_preimage_source
 (h : LiftSourceTargetPropertyAt I I' n f x P) :
    h.domChart.source ‚äÜ f ‚Åª¬π' h.codChart.source :=
  h.localPresentationAt.source_subset_preimage_source

lemma property (h : LiftSourceTargetPropertyAt I I' n f x P) : P f h.domChart h.codChart :=
  h.localPresentationAt.property

omit [ChartedSpace H M] [ChartedSpace G N] in
lemma congr_iff (hP : IsLocalSourceTargetProperty P) {f g : M ‚Üí N}
    {œÜ : OpenPartialHomeomorph M H} {œà : OpenPartialHomeomorph N G} (hfg : EqOn f g œÜ.source) :
    P f œÜ œà ‚Üî P g œÜ œà :=
  ‚ü®hP.congr hfg, hP.congr hfg.symm‚ü©

-- XXX: should `OpenPartialHomeomorph.restr_source'` be tagged with grind?
/-- If `P` is a local property, by monotonicity w.r.t. restricting `domChart`,
if `f` is continuous at `x`, to prove `LiftSourceTargetPropertyAt I I' n f x P`
we need not check the condition `f '' domChart.source ‚äÜ codChart.source`. -/
lemma mk_of_continuousAt (hf : ContinuousAt f x)
    (hP : IsLocalSourceTargetProperty P)
    (domChart : OpenPartialHomeomorph M H) (codChart : OpenPartialHomeomorph N G)
    (hx : x ‚àà domChart.source) (hfx : f x ‚àà codChart.source)
    (hdomChart : domChart ‚àà IsManifold.maximalAtlas I n M)
    (hcodChart : codChart ‚àà IsManifold.maximalAtlas I' n N)
    (hfP : P f domChart codChart) : LiftSourceTargetPropertyAt I I' n f x P := by
  obtain ‚ü®s, hs, hsopen, hxs‚ü© := mem_nhds_iff.mp <|
    hf.preimage_mem_nhds (codChart.open_source.mem_nhds hfx)
  exact ‚ü®domChart.restr s, codChart, by grind, hfx,
    restr_mem_maximalAtlas (contDiffGroupoid n I) hdomChart hsopen, hcodChart, by grind,
    hP.mono_source hsopen hfP‚ü©

/-- If `P` is monotone w.r.t. restricting `domChart` and closed under congruence,
if `f` has property `P` at `x` and `f` and `g` are eventually equal near `x`,
then `g` has property `P` at `x`. -/
lemma congr_of_eventuallyEq (hP : IsLocalSourceTargetProperty P)
    (hf : LiftSourceTargetPropertyAt I I' n f x P)
    (h' : f =·∂†[nhds x] g) : LiftSourceTargetPropertyAt I I' n g x P := by
  obtain ‚ü®s', hxs', hfg‚ü© := h'.exists_mem
  obtain ‚ü®s, hss', hs, hxs‚ü© := mem_nhds_iff.mp hxs'
  refine ‚ü®hf.domChart.restr s, hf.codChart, ?_, ?_, ?_, hf.codChart_mem_maximalAtlas, ?_, ?_‚ü©
  ¬∑ simpa using ‚ü®mem_domChart_source hf, by rwa [interior_eq_iff_isOpen.mpr hs]‚ü©
  ¬∑ exact hfg (mem_of_mem_nhds hxs') ‚ñ∏ mem_codChart_source hf
  ¬∑ exact restr_mem_maximalAtlas _ hf.domChart_mem_maximalAtlas hs
  ¬∑ trans s' ‚à© f ‚Åª¬π' hf.codChart.source
    ¬∑ apply subset_inter
      ¬∑ exact Subset.trans (by simp [interior_eq_iff_isOpen.mpr hs]) hss'
      ¬∑ exact Subset.trans (by simp) hf.source_subset_preimage_source
    ¬∑ rw [hfg.inter_preimage_eq]; exact inter_subset_right
  ¬∑ exact hP.congr (hfg.mono hss' |>.mono (by grind)) <| hP.mono_source hs hf.property

/-- If `P` is monotone w.r.t. restricting `domChart` and closed under congruence,
and `f` and `g` are eventually equal near `x`,
then `f` has property `P` at `x` if and only if `g` has property `P` at `x`. -/
lemma congr_iff_eventuallyEq (hP : IsLocalSourceTargetProperty P) (h' : f =·∂†[nhds x] g) :
    LiftSourceTargetPropertyAt I I' n f x P ‚Üî LiftSourceTargetPropertyAt I I' n g x P :=
  ‚ü®fun hf ‚Ü¶ hf.congr_of_eventuallyEq hP h', fun hg ‚Ü¶ hg.congr_of_eventuallyEq hP h'.symm‚ü©

end LiftSourceTargetPropertyAt

end Manifold
