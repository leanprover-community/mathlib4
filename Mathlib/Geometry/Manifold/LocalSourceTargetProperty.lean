/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.IsManifold.Basic

/-! # Local properties of smooth functions which depend on both the source and target

In this file, we consider local properties of functions between manifolds, which depend on both the
source and the target: more precisely, properties `P` of functions `f : M â†’ N` such that
`f` has property `P` if and only if there is a suitable pair of charts on `M` and `N`, respectively,
such that `f` read in these charts has a particular form.
The motivating example of this general description are immersions and submersions:
`f : M â†’ N` is an immersion at `x` iff there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and
`f x`, respectively, such that in these charts, `f` looks like `u â†¦ (u, 0)`. Similarly, `f` is a
submersion at `x` iff it looks like a projection `(u, v) â†¦ u` in suitable charts near `x` and `f x`.

Studying such local properties allows proving several lemmas about immersions and submersions
only once. In `IsImmersionEmbedding.lean`, we prove that being an immersion at `x` is indeed a
local property of this form.

## Main definitions and results

* `Manifold.LocalSourceTargetPropertyAt` captures a local property of the above form:
  for each `f : M â†’ N`, and pair of charts `Ï†` of `M` and `Ïˆ` of `N`, the local property is either
  safisfied or not.
  We ask that the property be stable under congruence and under restriction of `Ï†`.
* `Manifold.LiftSourceTargetPropertyAt f x P`, where `P` is a `LocalSourceTargetPropertyAt`,
  defines a local property of functions of the above shape:
  `f` has this property at `x` if there exist charts `Ï†` and `Ïˆ` such that `P f Ï† Ïˆ` holds.
* `Manifold.LiftSourceTargetPropertyAt.congr_of_eventuallyEq`: if `f` has property `P` at `x`
  and `g` equals `f` near `x`, then `g` also has property `P` at `x`.

-/

open scoped Manifold Topology ContDiff

open Function Set

variable {ğ•œ E E' F F' H H' G G' : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup F'] [NormedSpace ğ•œ F']
  [TopologicalSpace H] [TopologicalSpace H'] [TopologicalSpace G] [TopologicalSpace G']
  {I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'}
  {J : ModelWithCorners ğ•œ F G} {J' : ModelWithCorners ğ•œ F' G'}

variable {M M' N N' : Type*}
  [TopologicalSpace M] [ChartedSpace H M] [TopologicalSpace M'] [ChartedSpace H' M']
  [TopologicalSpace N] [ChartedSpace G N] [TopologicalSpace N'] [ChartedSpace G' N']
  {n : WithTop â„•âˆ}

namespace Manifold

/-- Structure recording good behaviour of a property of functions `M â†’ N` w.r.t. to compatible
choices of both a chart on `M` and `N`. Currently, we ask for the property being stable under
restriction of the domain chart, and local in the target.

Motivating examples are immersions and submersions of smooth manifolds. -/
structure IsLocalSourceTargetProperty
    (P : (M â†’ N) â†’ OpenPartialHomeomorph M H â†’ OpenPartialHomeomorph N G â†’ Prop) : Prop where
  mono_source : âˆ€ {f : M â†’ N}, âˆ€ {Ï† : OpenPartialHomeomorph M H}, âˆ€ {Ïˆ : OpenPartialHomeomorph N G},
    âˆ€ {s : Set M}, IsOpen s â†’ P f Ï† Ïˆ â†’ P f (Ï†.restr s) Ïˆ
  -- Note: the analogous `mono_target` statement is true for both immersions and submersions.
  -- If and when a future lemma requires it, add this here.
  congr : âˆ€ {f g : M â†’ N}, âˆ€ {Ï† : OpenPartialHomeomorph M H}, âˆ€ {Ïˆ : OpenPartialHomeomorph N G},
    âˆ€ {s : Set M}, EqOn f g s â†’ IsOpen s â†’ Ï†.source âŠ† s â†’ P f Ï† Ïˆ â†’ P g Ï† Ïˆ

variable (I J n) in
/-- Data witnessing the fact that `f` has local property `P` at `x` -/
structure LocalPresentationAt (f : M â†’ N) (x : M)
    (P : (M â†’ N) â†’ OpenPartialHomeomorph M H â†’ OpenPartialHomeomorph N G â†’ Prop) where
  /-- A choice of chart on the domain `M` of the local property `P` of `f` at `x`:
  w.r.t. this chart and `codChart`, `f` has the local property `P` at `x`. -/
  domChart : OpenPartialHomeomorph M H
  /-- A choice of chart on the target `N` of the local property `P` of `f` at `x`:
  w.r.t. this chart and `domChart`, `f` has the local property `P` at `x`. -/
  codChart : OpenPartialHomeomorph N G
  mem_domChart_source : x âˆˆ domChart.source
  mem_codChart_source : f x âˆˆ codChart.source
  domChart_mem_maximalAtlas : domChart âˆˆ IsManifold.maximalAtlas I n M
  codChart_mem_maximalAtlas : codChart âˆˆ IsManifold.maximalAtlas J n N
  source_subset_preimage_source : domChart.source âŠ† f â»Â¹' codChart.source
  property : P f domChart codChart

variable (I J n) in
/-- A property of smooth functions `M â†’ N` which is local at both the source and target:
a property `P` is local at `x` iff there exist charts `Ï†` and `Ïˆ` of `M` and `N` around
`x` and `f x`, respectively, such that `f` satisfies the property w.r.t. `Ï†` and `Ïˆ`.

The motivating example are smooth immersions and submersions: the corresponding condition is that
`f` look like the inclusion `u â†¦ (u, 0)` (resp. a projection `(u, v) â†¦ u`)
in the charts `Ï†` and `Ïˆ`.
-/
def LiftSourceTargetPropertyAt (f : M â†’ N) (x : M)
    (P : (M â†’ N) â†’ OpenPartialHomeomorph M H â†’ OpenPartialHomeomorph N G â†’ Prop) : Prop :=
  Nonempty (LocalPresentationAt I J n f x P)

namespace LiftSourceTargetPropertyAt

variable {f g : M â†’ N} {x : M}
  {P : (M â†’ N) â†’ OpenPartialHomeomorph M H â†’ OpenPartialHomeomorph N G â†’ Prop}

/-- A choice of charts witnessing the local property `P` of `f` at `x`. -/
noncomputable def localPresentationAt (h : LiftSourceTargetPropertyAt I J n f x P) :
    LocalPresentationAt I J n f x P :=
  Classical.choice h

/-- A choice of chart on the domain `M` of a local property of `f` at `x`:
w.r.t. this chart and `h.codChart`, `f` has the local property `P` at `x`.
The particular chart is arbitrary, but this choice matches the witness given by `h.codChart`. -/
noncomputable def domChart (h : LiftSourceTargetPropertyAt I J n f x P) :
    OpenPartialHomeomorph M H :=
  h.localPresentationAt.domChart

/-- A choice of chart on the co-domain `N` of a local property of `f` at `x`:
w.r.t. this chart and `h.domChart`, `f` has the local property `P` at `x`
The particular chart is arbitrary, but this choice matches the witness given by `h.domChart`. -/
noncomputable def codChart (h : LiftSourceTargetPropertyAt I J n f x P) :
    OpenPartialHomeomorph N G :=
  h.localPresentationAt.codChart

lemma mem_domChart_source (h : LiftSourceTargetPropertyAt I J n f x P) :
    x âˆˆ h.domChart.source :=
  h.localPresentationAt.mem_domChart_source

lemma mem_codChart_source (h : LiftSourceTargetPropertyAt I J n f x P) :
    f x âˆˆ h.codChart.source :=
  h.localPresentationAt.mem_codChart_source

lemma domChart_mem_maximalAtlas (h : LiftSourceTargetPropertyAt I J n f x P) :
    h.domChart âˆˆ IsManifold.maximalAtlas I n M :=
  h.localPresentationAt.domChart_mem_maximalAtlas

lemma codChart_mem_maximalAtlas (h : LiftSourceTargetPropertyAt I J n f x P) :
    h.codChart âˆˆ IsManifold.maximalAtlas J n N :=
  h.localPresentationAt.codChart_mem_maximalAtlas

lemma source_subset_preimage_source
 (h : LiftSourceTargetPropertyAt I J n f x P) :
    h.domChart.source âŠ† f â»Â¹' h.codChart.source :=
  h.localPresentationAt.source_subset_preimage_source

lemma property (h : LiftSourceTargetPropertyAt I J n f x P) : P f h.domChart h.codChart :=
  h.localPresentationAt.property

omit [ChartedSpace H M] [ChartedSpace G N] in
lemma congr_iff (hP : IsLocalSourceTargetProperty P)
    {f g : M â†’ N} {Ï† : OpenPartialHomeomorph M H} {Ïˆ : OpenPartialHomeomorph N G} {s : Set M}
    (hs : IsOpen s) (hÏ† : Ï†.source âŠ† s) (hfg : EqOn f g s) :
    P f Ï† Ïˆ â†” P g Ï† Ïˆ :=
  âŸ¨hP.congr hfg hs hÏ†, hP.congr hfg.symm hs hÏ†âŸ©

-- XXX: should `OpenPartialHomeomorph.restr_source'` be tagged with grind?
/-- If `P` is a local property, by monotonicity w.r.t. restricting `domChart`,
if `f` is continuous at `x`, to prove `LiftSourceTargetPropertyAt I I' n f x P`
we need not check the condition `f '' domChart.source âŠ† codChart.source`. -/
lemma mk_of_continuousAt (hf : ContinuousAt f x)
    (hP : IsLocalSourceTargetProperty P)
    (domChart : OpenPartialHomeomorph M H) (codChart : OpenPartialHomeomorph N G)
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas J n N)
    (hfP : P f domChart codChart) : LiftSourceTargetPropertyAt I J n f x P := by
  obtain âŸ¨s, hs, hsopen, hxsâŸ© := mem_nhds_iff.mp <|
    hf.preimage_mem_nhds (codChart.open_source.mem_nhds hfx)
  exact âŸ¨domChart.restr s, codChart, by grind [OpenPartialHomeomorph.restr_source'], hfx,
    restr_mem_maximalAtlas (contDiffGroupoid n I) hdomChart hsopen, hcodChart,
    by grind [OpenPartialHomeomorph.restr_source'], hP.mono_source hsopen hfPâŸ©

/-- If `P` is monotone w.r.t. restricting `domChart` and closed under congruence,
if `f` has property `P` at `x` and `f` and `g` are eventually equal near `x`,
then `g` has property `P` at `x`. -/
lemma congr_of_eventuallyEq (hP : IsLocalSourceTargetProperty P)
    (hf : LiftSourceTargetPropertyAt I J n f x P)
    (h' : f =á¶ [ğ“ x] g) : LiftSourceTargetPropertyAt I J n g x P := by
  obtain âŸ¨s', hxs', hfgâŸ© := h'.exists_mem
  obtain âŸ¨s, hss', hs, hxsâŸ© := mem_nhds_iff.mp hxs'
  refine âŸ¨hf.domChart.restr s, hf.codChart, ?_, ?_, ?_, hf.codChart_mem_maximalAtlas, ?_, ?_âŸ©
  Â· simpa using âŸ¨mem_domChart_source hf, by rwa [interior_eq_iff_isOpen.mpr hs]âŸ©
  Â· exact hfg (mem_of_mem_nhds hxs') â–¸ mem_codChart_source hf
  Â· exact restr_mem_maximalAtlas _ hf.domChart_mem_maximalAtlas hs
  Â· trans s' âˆ© f â»Â¹' hf.codChart.source
    Â· apply subset_inter
      Â· exact Subset.trans (by simp [interior_eq_iff_isOpen.mpr hs]) hss'
      Â· exact Subset.trans (by simp) hf.source_subset_preimage_source
    Â· rw [hfg.inter_preimage_eq]; exact inter_subset_right
  Â· apply hP.congr (hfg.mono hss') hs
    Â· grind [OpenPartialHomeomorph.restr_source']
    exact hP.mono_source hs hf.property

/-- If `P` is monotone w.r.t. restricting `domChart` and closed under congruence,
and `f` and `g` are eventually equal near `x`,
then `f` has property `P` at `x` if and only if `g` has property `P` at `x`. -/
lemma congr_iff_eventuallyEq (hP : IsLocalSourceTargetProperty P) (h' : f =á¶ [nhds x] g) :
    LiftSourceTargetPropertyAt I J n f x P â†” LiftSourceTargetPropertyAt I J n g x P :=
  âŸ¨fun hf â†¦ hf.congr_of_eventuallyEq hP h', fun hg â†¦ hg.congr_of_eventuallyEq hP h'.symmâŸ©

lemma prodMap [IsManifold I n M] [IsManifold I' n M'] [IsManifold J n N] [IsManifold J' n N']
    {Q : (M' â†’ N') â†’ OpenPartialHomeomorph M' H' â†’ OpenPartialHomeomorph N' G' â†’ Prop}
    {R : ((M Ã— M') â†’ (N Ã— N')) â†’ OpenPartialHomeomorph (M Ã— M') (H Ã— H') â†’
      OpenPartialHomeomorph (N Ã— N') (G Ã— G') â†’ Prop}
    -- TODO: reorder so hf is the first explicit argument
    (h : âˆ€ {f : M â†’ N}, âˆ€ {Ï†â‚ : OpenPartialHomeomorph M H}, âˆ€ {Ïˆâ‚ : OpenPartialHomeomorph N G},
      âˆ€ {g : M' â†’ N'}, âˆ€ {Ï†â‚‚ : OpenPartialHomeomorph M' H'}, âˆ€ {Ïˆâ‚‚ : OpenPartialHomeomorph N' G'},
      P f Ï†â‚ Ïˆâ‚ â†’ Q g Ï†â‚‚ Ïˆâ‚‚ â†’ R (Prod.map f g) (Ï†â‚.prod Ï†â‚‚) (Ïˆâ‚.prod Ïˆâ‚‚))
    (hf : LiftSourceTargetPropertyAt I J n f x P) {g : M' â†’ N'} {x' : M'}
    (hg : LiftSourceTargetPropertyAt I' J' n g x' Q) :
    LiftSourceTargetPropertyAt (I.prod I') (J.prod J') n (Prod.map f g) (x, x') R := by
  use hf.domChart.prod hg.domChart, hf.codChart.prod hg.codChart
  Â· simp [hf.mem_domChart_source, hg.mem_domChart_source]
  Â· simp [mem_codChart_source hf, mem_codChart_source hg]
  Â· exact IsManifold.mem_maximalAtlas_prod
      (domChart_mem_maximalAtlas hf) (domChart_mem_maximalAtlas hg)
  Â· apply IsManifold.mem_maximalAtlas_prod
      (codChart_mem_maximalAtlas hf) (codChart_mem_maximalAtlas hg)
  Â· simp only [OpenPartialHomeomorph.prod_toPartialEquiv, PartialEquiv.prod_source,
      preimage_prod_map_prod]
    exact prod_mono hf.source_subset_preimage_source hg.source_subset_preimage_source
  Â· exact h hf.property hg.property

end LiftSourceTargetPropertyAt

end Manifold
