/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.IsManifold.ExtChartAt

/-! # Local properties of smooth functions which depend on both the source and target

In this file, we consider local properties of functions between manifolds, which depend on both the
source and the target: more precisely, properties `P` of functions `f : M â†’ N` such that
`f` has property `P` if and only if there is a suitable pair of charts on `M` and `N`, respectively,
such that `f` read in these charts has a particular form.
The motivating example of this general description are immersions and submersions:
`f : M â†’ N` is an immersion at `x` iff there are charts `Ï†` and `Ïˆ` of `M` and `N` around `x` and
`f x`, respectively, such that in these charts, `f` looks like `u â†¦ (u, 0)`. Similarly, `f` is a
submersion at `x` iff it looks like a projection `(u, v) â†¦ u` in suitable charts near `x` and `f x`.

Studying such local properties allows proving several lemmas about immersions and submersions
only once. In `IsImmersionEmbedding.lean`, we prove that being an immersion at `x` is indeed a
local property of this form.

## Main definitions and results

* `LocalSourceTargetPropertyAt` captures a local property of the above form: for each `f: M â†’ N`,
  and pair of charts `Ï†` of `M` and `Ïˆ` of `N`, the local property is either safisfied or not.
  We ask that the property be stable under congruence and under restriction of `Ï†`.
* `LiftSourceTargetPropertyAt f x P`, where `P` is a `LocalSourceTargetPropertyAt`,
  defines a local property of functions of the above shape:
  `f` has this property at `x` if there exist charts `Ï†` and `Ïˆ` such that `P f Ï† Ïˆ` holds.
* `LiftSourceTargetPropertyAt.congr_of_eventuallyEq`: if `f` has property `P` at `x`
  and `g` equals `f` near `x`, then `g` also has property `P` at `x`.

-/

open scoped Manifold Topology ContDiff

open Function Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {H : Type*} [TopologicalSpace H] {H' : Type*} [TopologicalSpace H']
  {I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'}

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] {n : WithTop â„•âˆ}

/-- Structure recording good behaviour of a property of functions `M â†’ N` w.r.t. to compatible
choices of both a chart on `M` and `N`. Currently, we ask for the property being stable under
restriction of the domain chart, and local in the target.

Motivating examples are immersions and submersions of smooth manifolds. -/
structure IsLocalSourceTargetProperty
    (P : (M â†’ M') â†’ PartialHomeomorph M H â†’ PartialHomeomorph M' H' â†’ Prop) : Prop where
  mono_source : âˆ€ {f : M â†’ M'}, âˆ€ {Ï† : PartialHomeomorph M H}, âˆ€ {Ïˆ : PartialHomeomorph M' H'},
    âˆ€ {s : Set M}, IsOpen s â†’ P f Ï† Ïˆ â†’ P f (Ï†.restr s) Ïˆ
  -- Note: the analogous `mono_target` statement is true for both immersions and submersions.
  -- If and when a future lemma requires it, add this here.
  congr : âˆ€ {f g : M â†’ M'}, âˆ€ {Ï† : PartialHomeomorph M H}, âˆ€ {Ïˆ : PartialHomeomorph M' H'},
    âˆ€ {s : Set M}, EqOn f g s â†’ IsOpen s â†’ Ï†.source âŠ† s â†’ P f Ï† Ïˆ â†’ P g Ï† Ïˆ

variable (I I' n) in
/-- A property of smooth functions `M â†’ N` which is local at both the source and target:
a property `P` is local at `x` iff there exist charts `Ï†` and `Ïˆ` of `M` and `N` around
`x` and `f x`, respectively, such that `f` satisfies the property w.r.t. `Ï†` and `Ïˆ`.

The motivating example are smooth immersions and submersions: the corresponding condition is that
`f` look like the inclusion `u â†¦ (u, 0)` (resp. a projection `(u, v) â†¦ u`)
in the charts `Ï†` and `Ïˆ`.
-/
def LiftSourceTargetPropertyAt (f : M â†’ M') (x : M)
    (P : (M â†’ M') â†’ PartialHomeomorph M H â†’ PartialHomeomorph M' H' â†’ Prop) : Prop :=
  âˆƒ domChart : PartialHomeomorph M H, âˆƒ codChart : PartialHomeomorph M' H',
    x âˆˆ domChart.source âˆ§ f x âˆˆ codChart.source âˆ§
    domChart âˆˆ IsManifold.maximalAtlas I n M âˆ§
    codChart âˆˆ IsManifold.maximalAtlas I' n M' âˆ§
    f '' domChart.source âŠ† codChart.source âˆ§
    P f domChart codChart

namespace LiftSourceTargetPropertyAt

variable {f g : M â†’ M'} {x : M}
  {P : (M â†’ M') â†’ PartialHomeomorph M H â†’ PartialHomeomorph M' H' â†’ Prop}

/-- A choice of chart on the domain `M` of a local property of `f` at `x`:
w.r.t. this chart and `h.codChart`, `f` has the local property `P` at `x`.
The particular chart is arbitrary, but this choice matches the witness given by `h.codChart`. -/
noncomputable def domChart (h : LiftSourceTargetPropertyAt I I' n f x P) :
    PartialHomeomorph M H :=
  Classical.choose h

/-- A choice of chart on the co-domain `N` of a local property of `f` at `x`:
w.r.t. this chart and `h.domChart`, `f` has the local property `P` at `x`
The particular chart is arbitrary, but this choice matches the witness given by `h.domChart`. -/
noncomputable def codChart (h : LiftSourceTargetPropertyAt I I' n f x P) :
    PartialHomeomorph M' H' :=
  Classical.choose (Classical.choose_spec h)

lemma mem_domChart_source (h : LiftSourceTargetPropertyAt I I' n f x P) :
    x âˆˆ h.domChart.source :=
  (Classical.choose_spec (Classical.choose_spec h)).1

lemma mem_codChart_source (h : LiftSourceTargetPropertyAt I I' n f x P) :
    f x âˆˆ h.codChart.source :=
  (Classical.choose_spec (Classical.choose_spec h)).2.1

lemma domChart_mem_maximalAtlas (h : LiftSourceTargetPropertyAt I I' n f x P) :
    h.domChart âˆˆ IsManifold.maximalAtlas I n M :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.1

lemma codChart_mem_maximalAtlas (h : LiftSourceTargetPropertyAt I I' n f x P) :
    h.codChart âˆˆ IsManifold.maximalAtlas I' n M' :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.2.1

lemma map_source_subset_source (h : LiftSourceTargetPropertyAt I I' n f x P) :
    f '' h.domChart.source âŠ† h.codChart.source :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.2.2.1

lemma property (h : LiftSourceTargetPropertyAt I I' n f x P) : P f h.domChart h.codChart :=
  (Classical.choose_spec (Classical.choose_spec h)).2.2.2.2.2

omit [ChartedSpace H M] [ChartedSpace H' M'] in
lemma congr_iff (hP : IsLocalSourceTargetProperty P)
    {f g : M â†’ M'} {Ï† : PartialHomeomorph M H} {Ïˆ : PartialHomeomorph M' H'} {s : Set M}
    (hs : IsOpen s) (hÏ† : Ï†.source âŠ† s) (hfg : EqOn f g s) :
    P f Ï† Ïˆ â†” P g Ï† Ïˆ :=
  âŸ¨hP.congr hfg hs hÏ†, hP.congr hfg.symm hs hÏ†âŸ©

/-- If `P` is a nice local property, by monotonicity w.r.t. restricting `domChart`,
if `f` is continuous at `x`, to prove `LiftSourceTargetPropertyAt I I' n f x P`
we need not check the condition `f '' domChart.source âŠ† codChart.source`. -/
lemma mk_of_continuousAt (hf : ContinuousAt f x)
    (hP : IsLocalSourceTargetProperty P)
    (domChart : PartialHomeomorph M H) (codChart : PartialHomeomorph M' H')
    (hx : x âˆˆ domChart.source) (hfx : f x âˆˆ codChart.source)
    (hdomChart : domChart âˆˆ IsManifold.maximalAtlas I n M)
    (hcodChart : codChart âˆˆ IsManifold.maximalAtlas I' n M')
    (hfP : P f domChart codChart) : LiftSourceTargetPropertyAt I I' n f x P := by
  obtain âŸ¨s, hs, hsopen, hxsâŸ© := mem_nhds_iff.mp <|
    hf.preimage_mem_nhds (codChart.open_source.mem_nhds hfx)
  have : f '' (domChart.restr s).source âŠ† codChart.source := by
    refine Subset.trans ?_ (image_subset_iff.mpr hs)
    gcongr
    rw [domChart.restr_source' _ hsopen]
    exact inter_subset_right
  have hmono : ((domChart.restr s).extend I).target âŠ† (domChart.extend I).target := by
    have {a b c : Set E} : a âˆ© (b âˆ© c) âŠ† b := by intro; aesop
    simpa using this
  exact âŸ¨domChart.restr s, codChart,
    by rw [domChart.restr_source, interior_eq_iff_isOpen.mpr hsopen]; exact mem_inter hx hxs, hfx,
    restr_mem_maximalAtlas (G := contDiffGroupoid n I) hdomChart hsopen, hcodChart, this,
    hP.mono_source hsopen hfPâŸ©

/-- If `P` is monotone w.r.t. restricting `domChart` and closed under congruence,
if `f` has property `P` at `x` and `f` and `g` are eventually equal near `x`,
then `g` has property `P` at `x`. -/
lemma congr_of_eventuallyEq (hP : IsLocalSourceTargetProperty P)
    (hf : LiftSourceTargetPropertyAt I I' n f x P)
    (h' : f =á¶ [nhds x] g) : LiftSourceTargetPropertyAt I I' n g x P := by
  obtain âŸ¨s', hxs', hfgâŸ© := h'.exists_mem
  obtain âŸ¨s, hss', hs, hxsâŸ© := mem_nhds_iff.mp hxs'
  refine âŸ¨hf.domChart.restr s, hf.codChart, ?_, ?_, ?_, hf.codChart_mem_maximalAtlas, ?_, ?_âŸ©
  Â· simpa using âŸ¨mem_domChart_source hf, by rwa [interior_eq_iff_isOpen.mpr hs]âŸ©
  Â· exact hfg (mem_of_mem_nhds hxs') â–¸ mem_codChart_source hf
  Â· exact restr_mem_maximalAtlas _ hf.domChart_mem_maximalAtlas hs
  Â· trans f '' (hf.domChart.restr s).source
    Â· have : (hf.domChart.restr s).source âŠ† s' :=
        Subset.trans (by simp [interior_eq_iff_isOpen.mpr hs]) hss'
      exact (hfg.mono this).image_eq.symm.le
    Â· exact Subset.trans (image_mono (by simp)) hf.map_source_subset_source
  Â· apply hP.congr (hfg.mono hss') hs
    Â· grind [PartialHomeomorph.restr_toPartialEquiv, PartialEquiv.restr_source, interior_subset]
    exact hP.mono_source hs hf.property

end LiftSourceTargetPropertyAt
