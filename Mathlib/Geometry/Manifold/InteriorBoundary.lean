/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/

import Mathlib.Geometry.Manifold.SmoothManifoldWithCorners

/-!
# Interior and boundary of a manifold
Define the interior and boundary of a manifold.

## Main definitions
- **IsInteriorPoint x**: `p âˆˆ M` is an interior point if, for `Ï†` being the preferred chart at `x`,
 `Ï† x` is an interior point of `Ï†.target`.
- **IsBoundaryPoint x**: `p âˆˆ M` is a boundary point if, `(extChartAt I x) x âˆˆ frontier (range I)`.
- **interior I M** is the **interior** of `M`, the set of its interior points.
- **boundary I M** is the **boundary** of `M`, the set of its boundary points.

## Main results
- `ModelWithCorners.univ_eq_interior_union_boundary`: `M` is the union of its interior and boundary
- `ModelWithCorners.interior_boundary_disjoint`: interior and boundary of `M` are disjoint
- `BoundarylessManifold.isInteriorPoint`: if `M` is boundaryless, every point is an interior point
- `ModelWithCorners.Boundaryless.boundary_eq_empty` and `of_boundary_eq_empty`:
`M` is boundaryless if and only if its boundary is empty
- `ModelWithCorners.interior_prod`: the interior of `M Ã— N` is the product of the interiors
of `M` and `N`.
- `ModelWithCorners.boundary_prod`: the boundary of `M Ã— N` is `âˆ‚M Ã— N âˆª (M Ã— âˆ‚N)`.
- `ModelWithCorners.BoundarylessManifold.prod`: if `M` and `N` are boundaryless, so is `M Ã— N`

## Tags
manifold, interior, boundary

## TODO
- `x` is an interior point iff *any* chart around `x` maps it to `interior (range I)`;
similarly for the boundary.
- the interior of `M` is open, hence the boundary is closed (and nowhere dense)
  In finite dimensions, this requires e.g. the homology of spheres.
- the interior of `M` is a smooth manifold without boundary
- `boundary M` is a smooth submanifold (possibly with boundary and corners):
follows from the corresponding statement for the model with corners `I`;
this requires a definition of submanifolds
- if `M` is finite-dimensional, its boundary has measure zero

-/

open Set
open scoped Topology

-- Let `M` be a manifold with corners over the pair `(E, H)`.
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

namespace ModelWithCorners
/-- `p âˆˆ M` is an interior point of a manifold `M` iff its image in the extended chart
lies in the interior of the model space. -/
def IsInteriorPoint (x : M) := extChartAt I x x âˆˆ interior (range I)

/-- `p âˆˆ M` is a boundary point of a manifold `M` iff its image in the extended chart
lies on the boundary of the model space. -/
def IsBoundaryPoint (x : M) := extChartAt I x x âˆˆ frontier (range I)

variable (M) in
/-- The **interior** of a manifold `M` is the set of its interior points. -/
protected def interior : Set M := { x : M | I.IsInteriorPoint x }

lemma isInteriorPoint_iff {x : M} :
    I.IsInteriorPoint x â†” extChartAt I x x âˆˆ interior (extChartAt I x).target :=
  âŸ¨fun h â†¦ (chartAt H x).mem_interior_extend_target _ (mem_chart_target H x) h,
    fun h â†¦ PartialHomeomorph.interior_extend_target_subset_interior_range _ _ hâŸ©

variable (M) in
/-- The **boundary** of a manifold `M` is the set of its boundary points. -/
protected def boundary : Set M := { x : M | I.IsBoundaryPoint x }

lemma isBoundaryPoint_iff {x : M} : I.IsBoundaryPoint x â†” extChartAt I x x âˆˆ frontier (range I) :=
  Iff.rfl

/-- Every point is either an interior or a boundary point. -/
lemma isInteriorPoint_or_isBoundaryPoint (x : M) : I.IsInteriorPoint x âˆ¨ I.IsBoundaryPoint x := by
  rw [IsInteriorPoint, or_iff_not_imp_left, I.isBoundaryPoint_iff, â† closure_diff_interior,
    I.isClosed_range.closure_eq, mem_diff]
  exact fun h => âŸ¨mem_range_self _, hâŸ©

/-- A manifold decomposes into interior and boundary. -/
lemma interior_union_boundary_eq_univ : (I.interior M) âˆª (I.boundary M) = (univ : Set M) :=
  eq_univ_of_forall fun x => (mem_union _ _ _).mpr (I.isInteriorPoint_or_isBoundaryPoint x)

/-- The interior and boundary of a manifold `M` are disjoint. -/
lemma disjoint_interior_boundary : Disjoint (I.interior M) (I.boundary M) := by
  by_contra h
  -- Choose some x in the intersection of interior and boundary.
  obtain âŸ¨x, h1, h2âŸ© := not_disjoint_iff.mp h
  rw [â† mem_empty_iff_false (extChartAt I x x),
    â† disjoint_iff_inter_eq_empty.mp (disjoint_interior_frontier (s := range I)), mem_inter_iff]
  exact âŸ¨h1, h2âŸ©

/-- The boundary is the complement of the interior. -/
lemma compl_interior : (I.interior M)á¶œ = I.boundary M:= by
  apply compl_unique ?_ I.interior_union_boundary_eq_univ
  exact disjoint_iff_inter_eq_empty.mp (I.disjoint_interior_boundary)

/-- The interior is the complement of the boundary. -/
lemma compl_boundary : (I.boundary M)á¶œ = I.interior M:= by
  rw [â† compl_interior, compl_compl]

variable {I} in
lemma _root_.range_mem_nhds_isInteriorPoint {x : M} (h : I.IsInteriorPoint x) :
    range I âˆˆ ğ“ (extChartAt I x x) := by
  rw [mem_nhds_iff]
  exact âŸ¨interior (range I), interior_subset, isOpen_interior, hâŸ©

/-- Type class for manifold without boundary. This differs from `ModelWithCorners.Boundaryless`,
  which states that the `ModelWithCorners` maps to the whole model vector space. -/
class _root_.BoundarylessManifold {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
    (M : Type*) [TopologicalSpace M] [ChartedSpace H M] : Prop where
  isInteriorPoint' : âˆ€ x : M, IsInteriorPoint I x

section Boundaryless
variable [I.Boundaryless]

/-- Boundaryless `ModelWithCorners` implies boundaryless manifold. -/
instance : BoundarylessManifold I M where
  isInteriorPoint' x := by
    let r := ((chartAt H x).isOpen_extend_target I).interior_eq
    have : extChartAt I x = (chartAt H x).extend I := rfl
    rw [â† this] at r
    rw [isInteriorPoint_iff, r]
    exact PartialEquiv.map_source _ (mem_extChartAt_source _ _)

end Boundaryless

section BoundarylessManifold

/-- The empty manifold is boundaryless. -/
instance BoundarylessManifold.of_empty [IsEmpty M] : BoundarylessManifold I M where
  isInteriorPoint' x := (IsEmpty.false x).elim

lemma _root_.BoundarylessManifold.isInteriorPoint {x : M} [BoundarylessManifold I M] :
    IsInteriorPoint I x := BoundarylessManifold.isInteriorPoint' x

/-- If `I` is boundaryless, `M` has full interior. -/
lemma interior_eq_univ [BoundarylessManifold I M] : I.interior M = univ :=
  eq_univ_of_forall fun _ => BoundarylessManifold.isInteriorPoint I

/-- Boundaryless manifolds have empty boundary. -/
lemma Boundaryless.boundary_eq_empty [BoundarylessManifold I M] : I.boundary M = âˆ… := by
  rw [â† I.compl_interior, I.interior_eq_univ, compl_empty_iff]

instance [BoundarylessManifold I M] : IsEmpty (I.boundary M) :=
  isEmpty_coe_sort.mpr (Boundaryless.boundary_eq_empty I)

/-- `M` is boundaryless iff its boundary is empty. -/
lemma Boundaryless.iff_boundary_eq_empty : I.boundary M = âˆ… â†” BoundarylessManifold I M := by
  refine âŸ¨fun h â†¦ { isInteriorPoint' := ?_ }, fun a â†¦ boundary_eq_empty IâŸ©
  intro x
  show x âˆˆ I.interior M
  rw [â† compl_interior, compl_empty_iff] at h
  rw [h]
  trivial

/-- Manifolds with empty boundary are boundaryless. -/
lemma Boundaryless.of_boundary_eq_empty (h : I.boundary M = âˆ…) : BoundarylessManifold I M :=
  (Boundaryless.iff_boundary_eq_empty (I := I)).mp h

end BoundarylessManifold

/-! Interior and boundary of the product of two manifolds. -/
section prod

variable {I}
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {H' : Type*} [TopologicalSpace H']
  {N : Type*} [TopologicalSpace N] [ChartedSpace H' N]
  (J : ModelWithCorners ğ•œ E' H') {x : M} {y : N}

/-- The interior of `M Ã— N` is the product of the interiors of `M` and `N`. -/
lemma interior_prod :
    (I.prod J).interior (M Ã— N) = (I.interior M) Ã—Ë¢ (J.interior N) := by
  ext p
  have aux : (interior (range â†‘I)) Ã—Ë¢ (interior (range J)) = interior (range (I.prod J)) := by
    rw [â† interior_prod_eq, â† Set.range_prod_map, modelWithCorners_prod_coe]
  constructor <;> intro hp
  Â· replace hp : (I.prod J).IsInteriorPoint p := hp
    rw [IsInteriorPoint, â† aux] at hp
    exact hp
  Â· show (I.prod J).IsInteriorPoint p
    rw [IsInteriorPoint, â† aux, mem_prod]
    obtain h := Set.mem_prod.mp hp
    rw [ModelWithCorners.interior] at h
    exact h

/-- The boundary of `M Ã— N` is `âˆ‚M Ã— N âˆª (M Ã— âˆ‚N)`. -/
lemma boundary_prod :
    (I.prod J).boundary (M Ã— N) = Set.prod univ (J.boundary N) âˆª Set.prod (I.boundary M) univ := by
  let h := calc (I.prod J).boundary (M Ã— N)
    _ = ((I.prod J).interior (M Ã— N))á¶œ := (I.prod J).compl_interior.symm
    _ = ((I.interior M) Ã—Ë¢ (J.interior N))á¶œ := by rw [interior_prod]
    _ = (I.interior M)á¶œ Ã—Ë¢ univ âˆª univ Ã—Ë¢ (J.interior N)á¶œ := by rw [compl_prod_eq_union]
  rw [h, I.compl_interior, J.compl_interior, union_comm]
  rfl

/-- If `M` is boundaryless, `âˆ‚(MÃ—N) = M Ã— âˆ‚N`. -/
lemma boundary_of_boundaryless_left [BoundarylessManifold I M] :
    (I.prod J).boundary (M Ã— N) = Set.prod (univ : Set M) (J.boundary N) := by
  rw [boundary_prod, Boundaryless.boundary_eq_empty I]
  have : Set.prod (âˆ… : Set M) (univ : Set N) = âˆ… := Set.empty_prod
  rw [this, union_empty]

/-- If `N` is boundaryless, `âˆ‚(MÃ—N) = âˆ‚M Ã— N`. -/
lemma boundary_of_boundaryless_right [BoundarylessManifold J N] :
    (I.prod J).boundary (M Ã— N) = Set.prod (I.boundary M) (univ : Set N) := by
  rw [boundary_prod, Boundaryless.boundary_eq_empty J]
  have : Set.prod (univ : Set M) (âˆ… : Set N) = âˆ… := Set.prod_empty
  rw [this, empty_union]

/-- The product of two boundaryless manifolds is boundaryless. -/
instance BoundarylessManifold.prod [BoundarylessManifold I M] [BoundarylessManifold J N] :
    BoundarylessManifold (I.prod J) (M Ã— N) := by
  apply Boundaryless.of_boundary_eq_empty
  simp only [boundary_prod, Boundaryless.boundary_eq_empty, union_empty_iff]
  -- These are simp lemmas, but `simp` does not apply them on its own:
  -- presumably because of the distinction between `Prod` and `ModelProd`
  exact âŸ¨Set.prod_empty, Set.empty_prodâŸ©

end prod

end ModelWithCorners
