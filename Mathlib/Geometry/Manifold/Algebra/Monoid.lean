/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.MFDeriv.Basic

/-!
# `C^n` monoid
A `C^n` monoid is a monoid that is also a `C^n` manifold, in which multiplication is a `C^n` map
of the product manifold `G` Ã— `G` into `G`.

In this file we define the basic structures to talk about `C^n` monoids: `ContMDiffMul` and its
additive counterpart `ContMDiffAdd`. These structures are general enough to also talk about `C^n`
semigroups.
-/

open scoped Manifold ContDiff

library_note "Design choices about smooth algebraic structures"/--
1. All `C^n` algebraic structures on `G` are `Prop`-valued classes that extend
`IsManifold I n G`. This way we save users from adding both
`[IsManifold I n G]` and `[ContMDiffMul I n G]` to the assumptions. While many API
lemmas hold true without the `IsManifold I n G` assumption, we're not aware of a
mathematically interesting monoid on a topological manifold such that (a) the space is not a
`IsManifold`; (b) the multiplication is `C^n` at `(a, b)` in the charts
`extChartAt I a`, `extChartAt I b`, `extChartAt I (a * b)`.

2. Because of `ModelProd` we can't assume, e.g., that a `LieGroup` is modelled on `ð“˜(ð•œ, E)`. So,
we formulate the definitions and lemmas for any model.

3. While smoothness of an operation implies its continuity, lemmas like
`continuousMul_of_contMDiffMul` can't be instances because otherwise Lean would have to search for
`ContMDiffMul I n G` with unknown `ð•œ`, `E`, `H`, and `I : ModelWithCorners ð•œ E H`. If users needs
`[ContinuousMul G]` in a proof about a `C^n` monoid, then they need to either add
`[ContinuousMul G]` as an assumption (worse) or use `haveI` in the proof (better). -/

-- See note [Design choices about smooth algebraic structures]
/-- Basic hypothesis to talk about a `C^n` (Lie) additive monoid or a `C^n` additive
semigroup. A `C^n` additive monoid over `G`, for example, is obtained by requiring both the
instances `AddMonoid G` and `ContMDiffAdd I n G`. -/
class ContMDiffAdd {ð•œ : Type*} [NontriviallyNormedField ð•œ] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E]
    (I : ModelWithCorners ð•œ E H) (n : WithTop â„•âˆž)
    (G : Type*) [Add G] [TopologicalSpace G] [ChartedSpace H G] : Prop
    extends IsManifold I n G where
  contMDiff_add : ContMDiff (I.prod I) I n fun p : G Ã— G â†¦ p.1 + p.2

-- See note [Design choices about smooth algebraic structures]
/-- Basic hypothesis to talk about a `C^n` (Lie) monoid or a `C^n` semigroup.
A `C^n` monoid over `G`, for example, is obtained by requiring both the instances `Monoid G`
and `ContMDiffMul I n G`. -/
@[to_additive]
class ContMDiffMul {ð•œ : Type*} [NontriviallyNormedField ð•œ] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E]
    (I : ModelWithCorners ð•œ E H) (n : WithTop â„•âˆž)
    (G : Type*) [Mul G] [TopologicalSpace G] [ChartedSpace H G] : Prop
    extends IsManifold I n G where
  contMDiff_mul : ContMDiff (I.prod I) I n fun p : G Ã— G â†¦ p.1 * p.2

section ContMDiffMul

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ð•œ E] {I : ModelWithCorners ð•œ E H} {n : WithTop â„•âˆž}
  {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H G] {E' : Type*} [NormedAddCommGroup E']
  [NormedSpace ð•œ E'] {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners ð•œ E' H'}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]

@[to_additive]
protected theorem ContMDiffMul.of_le {m n : WithTop â„•âˆž} (hmn : m â‰¤ n)
    [h : ContMDiffMul I n G] : ContMDiffMul I m G := by
  have : IsManifold I m G := IsManifold.of_le hmn
  exact âŸ¨h.contMDiff_mul.of_le hmnâŸ©

@[to_additive]
instance {a : WithTop â„•âˆž} [ContMDiffMul I âˆž G] [h : ENat.LEInfty a] : ContMDiffMul I a G :=
  ContMDiffMul.of_le h.out

@[to_additive]
instance {a : WithTop â„•âˆž} [ContMDiffMul I Ï‰ G] : ContMDiffMul I a G :=
  ContMDiffMul.of_le le_top

@[to_additive]
instance [ContinuousMul G] : ContMDiffMul I 0 G := by
  constructor
  rw [contMDiff_zero_iff]
  exact continuous_mul

@[to_additive]
instance [ContMDiffMul I 2 G] : ContMDiffMul I 1 G :=
  ContMDiffMul.of_le one_le_two

section

variable (I n)

@[to_additive]
theorem contMDiff_mul [ContMDiffMul I n G] : ContMDiff (I.prod I) I n fun p : G Ã— G â†¦ p.1 * p.2 :=
  ContMDiffMul.contMDiff_mul

include I n in
/-- If the multiplication is `C^n`, then it is continuous. This is not an instance for technical
reasons, see note [Design choices about smooth algebraic structures]. -/
@[to_additive /-- If the addition is `C^n`, then it is continuous. This is not an instance for
technical reasons, see note [Design choices about smooth algebraic structures]. -/]
theorem continuousMul_of_contMDiffMul [ContMDiffMul I n G] : ContinuousMul G :=
  âŸ¨(contMDiff_mul I n).continuousâŸ©

end

section

variable [ContMDiffMul I n G] {f g : M â†’ G} {s : Set M} {x : M}

@[to_additive]
theorem ContMDiffWithinAt.mul (hf : ContMDiffWithinAt I' I n f s x)
    (hg : ContMDiffWithinAt I' I n g s x) : ContMDiffWithinAt I' I n (f * g) s x :=
  (contMDiff_mul I n).contMDiffAt.comp_contMDiffWithinAt x (hf.prodMk hg)

@[to_additive]
nonrec theorem ContMDiffAt.mul (hf : ContMDiffAt I' I n f x) (hg : ContMDiffAt I' I n g x) :
    ContMDiffAt I' I n (f * g) x :=
  hf.mul hg

@[to_additive]
theorem ContMDiffOn.mul (hf : ContMDiffOn I' I n f s) (hg : ContMDiffOn I' I n g s) :
    ContMDiffOn I' I n (f * g) s := fun x hx â†¦ (hf x hx).mul (hg x hx)

@[to_additive]
theorem ContMDiff.mul (hf : ContMDiff I' I n f) (hg : ContMDiff I' I n g) :
    ContMDiff I' I n (f * g) := fun x â†¦ (hf x).mul (hg x)

@[to_additive]
theorem contMDiff_mul_left {a : G} : ContMDiff I I n (a * Â·) :=
  contMDiff_const.mul contMDiff_id

@[to_additive]
theorem contMDiffAt_mul_left {a b : G} : ContMDiffAt I I n (a * Â·) b :=
  contMDiff_mul_left.contMDiffAt

@[to_additive]
theorem contMDiff_mul_right {a : G} : ContMDiff I I n (Â· * a) :=
  contMDiff_id.mul contMDiff_const

@[to_additive]
theorem contMDiffAt_mul_right {a b : G} : ContMDiffAt I I n (Â· * a) b :=
  contMDiff_mul_right.contMDiffAt

end

section

variable [ContMDiffMul I 1 G]

@[to_additive]
theorem mdifferentiable_mul_left {a : G} : MDifferentiable I I (a * Â·) :=
  contMDiff_mul_left.mdifferentiable le_rfl

@[to_additive]
theorem mdifferentiableAt_mul_left {a b : G} :
    MDifferentiableAt I I (a * Â·) b :=
  contMDiffAt_mul_left.mdifferentiableAt le_rfl

@[to_additive]
theorem mdifferentiable_mul_right {a : G} : MDifferentiable I I (Â· * a) :=
  contMDiff_mul_right.mdifferentiable le_rfl

@[to_additive]
theorem mdifferentiableAt_mul_right {a b : G} :
    MDifferentiableAt I I (Â· * a) b :=
  contMDiffAt_mul_right.mdifferentiableAt le_rfl

end

variable (I) (g h : G)
variable [ContMDiffMul I âˆž G]

/-- Left multiplication by `g`. It is meant to mimic the usual notation in Lie groups.
Used mostly through the notation `ð‘³`.
Lemmas involving `smoothLeftMul` with the notation `ð‘³` usually use `L` instead of `ð‘³` in the
names. -/
def smoothLeftMul : C^âˆžâŸ®I, G; I, GâŸ¯ :=
  âŸ¨(g * Â·), contMDiff_mul_leftâŸ©

/-- Right multiplication by `g`. It is meant to mimic the usual notation in Lie groups.
Used mostly through the notation `ð‘¹`.
Lemmas involving `smoothRightMul` with the notation `ð‘¹` usually use `R` instead of `ð‘¹` in the
names. -/
def smoothRightMul : C^âˆžâŸ®I, G; I, GâŸ¯ :=
  âŸ¨(Â· * g), contMDiff_mul_rightâŸ©

-- Left multiplication. The abbreviation is `MIL`.
@[inherit_doc] scoped[LieGroup] notation "ð‘³" => smoothLeftMul

-- Right multiplication. The abbreviation is `MIR`.
@[inherit_doc] scoped[LieGroup] notation "ð‘¹" => smoothRightMul

open scoped LieGroup

@[simp]
theorem L_apply : (ð‘³ I g) h = g * h :=
  rfl

@[simp]
theorem R_apply : (ð‘¹ I g) h = h * g :=
  rfl

@[simp]
theorem L_mul {G : Type*} [Semigroup G] [TopologicalSpace G] [ChartedSpace H G] [ContMDiffMul I âˆž G]
    (g h : G) : ð‘³ I (g * h) = (ð‘³ I g).comp (ð‘³ I h) := by
  ext
  simp only [ContMDiffMap.comp_apply, L_apply, mul_assoc]

@[simp]
theorem R_mul {G : Type*} [Semigroup G] [TopologicalSpace G] [ChartedSpace H G] [ContMDiffMul I âˆž G]
    (g h : G) : ð‘¹ I (g * h) = (ð‘¹ I h).comp (ð‘¹ I g) := by
  ext
  simp only [ContMDiffMap.comp_apply, R_apply, mul_assoc]

section

variable {G' : Type*} [Monoid G'] [TopologicalSpace G'] [ChartedSpace H G'] [ContMDiffMul I âˆž G']
  (g' : G')

theorem smoothLeftMul_one : (ð‘³ I g') 1 = g' :=
  mul_one g'

theorem smoothRightMul_one : (ð‘¹ I g') 1 = g' :=
  one_mul g'

end

-- Instance of product
@[to_additive prod]
instance ContMDiffMul.prod {ð•œ : Type*} [NontriviallyNormedField ð•œ] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ð•œ E] {H : Type*} [TopologicalSpace H]
    (I : ModelWithCorners ð•œ E H) (G : Type*) [TopologicalSpace G] [ChartedSpace H G] [Mul G]
    [ContMDiffMul I n G] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ð•œ E'] {H' : Type*}
    [TopologicalSpace H'] (I' : ModelWithCorners ð•œ E' H') (G' : Type*) [TopologicalSpace G']
    [ChartedSpace H' G'] [Mul G'] [ContMDiffMul I' n G'] : ContMDiffMul (I.prod I') n (G Ã— G') :=
  { IsManifold.prod G G' with
    contMDiff_mul :=
      ((contMDiff_fst.comp contMDiff_fst).mul (contMDiff_fst.comp contMDiff_snd)).prodMk
        ((contMDiff_snd.comp contMDiff_fst).mul (contMDiff_snd.comp contMDiff_snd)) }

end ContMDiffMul

section Monoid

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] {n : WithTop â„•âˆž}
  {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ð•œ E] {I : ModelWithCorners ð•œ E H} {G : Type*} [Monoid G]
  [TopologicalSpace G] [ChartedSpace H G] [ContMDiffMul I n G] {H' : Type*} [TopologicalSpace H']
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ð•œ E'] {I' : ModelWithCorners ð•œ E' H'}
  {G' : Type*} [Monoid G'] [TopologicalSpace G'] [ChartedSpace H' G'] [ContMDiffMul I' n G']

@[to_additive]
theorem contMDiff_pow : âˆ€ i : â„•, ContMDiff I I n fun a : G â†¦ a ^ i
  | 0 => by simp only [pow_zero, contMDiff_const]
  | k + 1 => by simpa [pow_succ] using (contMDiff_pow _).mul contMDiff_id

/-- Morphism of additive `C^n` monoids. -/
structure ContMDiffAddMonoidMorphism (I : ModelWithCorners ð•œ E H) (I' : ModelWithCorners ð•œ E' H')
    (n : WithTop â„•âˆž) (G : Type*) [TopologicalSpace G] [ChartedSpace H G] [AddMonoid G]
    (G' : Type*) [TopologicalSpace G'] [ChartedSpace H' G'] [AddMonoid G']
    extends G â†’+ G' where
  contMDiff_toFun : ContMDiff I I' n toFun

/-- Morphism of `C^n` monoids. -/
@[to_additive]
structure ContMDiffMonoidMorphism (I : ModelWithCorners ð•œ E H) (I' : ModelWithCorners ð•œ E' H')
    (n : WithTop â„•âˆž) (G : Type*) [TopologicalSpace G] [ChartedSpace H G] [Monoid G] (G' : Type*)
    [TopologicalSpace G'] [ChartedSpace H' G'] [Monoid G'] extends
    G â†’* G' where
  contMDiff_toFun : ContMDiff I I' n toFun

@[to_additive]
instance : One (ContMDiffMonoidMorphism I I' n G G') :=
  âŸ¨{  contMDiff_toFun := contMDiff_const
      toMonoidHom := 1 }âŸ©

@[to_additive]
instance : Inhabited (ContMDiffMonoidMorphism I I' n G G') :=
  âŸ¨1âŸ©

@[to_additive]
instance : FunLike (ContMDiffMonoidMorphism I I' n G G') G G' where
  coe a := a.toFun
  coe_injective' f g h := by cases f; cases g; congr; exact DFunLike.ext' h

@[to_additive]
instance : MonoidHomClass (ContMDiffMonoidMorphism I I' n G G') G G' where
  map_one f := f.map_one
  map_mul f := f.map_mul

@[to_additive]
instance : ContinuousMapClass (ContMDiffMonoidMorphism I I' n G G') G G' where
  map_continuous f := f.contMDiff_toFun.continuous

end Monoid

/-! ### Differentiability of finite point-wise sums and products

  Finite point-wise products (resp. sums) of `C^n` functions `M â†’ G` (at `x`/on `s`)
  into a commutative monoid `G` are `C^n` at `x`/on `s`. -/
section CommMonoid

open Function

variable {Î¹ ð•œ : Type*} [NontriviallyNormedField ð•œ] {n : WithTop â„•âˆž} {H : Type*} [TopologicalSpace H]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E] {I : ModelWithCorners ð•œ E H}
  {G : Type*} [CommMonoid G] [TopologicalSpace G] [ChartedSpace H G] [ContMDiffMul I n G]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ð•œ E']
  {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners ð•œ E' H'}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]
  {s : Set M} {x xâ‚€ : M} {t : Finset Î¹} {f : Î¹ â†’ M â†’ G} {p : Î¹ â†’ Prop}

@[to_additive]
theorem ContMDiffWithinAt.prod (h : âˆ€ i âˆˆ t, ContMDiffWithinAt I' I n (f i) s xâ‚€) :
    ContMDiffWithinAt I' I n (fun x â†¦ âˆ i âˆˆ t, f i x) s xâ‚€ := by
  classical
  induction t using Finset.induction_on with
  | empty => simp [contMDiffWithinAt_const]
  | insert i K iK IH =>
    simp only [iK, Finset.prod_insert, not_false_iff]
    exact (h _ (Finset.mem_insert_self i K)).mul (IH fun j hj â†¦ h _ <| Finset.mem_insert_of_mem hj)

@[to_additive]
theorem contMDiffWithinAt_finprod (lf : LocallyFinite fun i â†¦ mulSupport <| f i) {xâ‚€ : M}
    (h : âˆ€ i, ContMDiffWithinAt I' I n (f i) s xâ‚€) :
    ContMDiffWithinAt I' I n (fun x â†¦ âˆá¶  i, f i x) s xâ‚€ :=
  let âŸ¨_I, hIâŸ© := finprod_eventually_eq_prod lf xâ‚€
  (ContMDiffWithinAt.prod fun i _hi â†¦ h i).congr_of_eventuallyEq
    (eventually_nhdsWithin_of_eventually_nhds hI) hI.self_of_nhds

@[to_additive]
theorem contMDiffWithinAt_finset_prod' (h : âˆ€ i âˆˆ t, ContMDiffWithinAt I' I n (f i) s x) :
    ContMDiffWithinAt I' I n (âˆ i âˆˆ t, f i) s x :=
  Finset.prod_induction f (fun f â†¦ ContMDiffWithinAt I' I n f s x) (fun _ _ hf hg â†¦ hf.mul hg)
    (contMDiffWithinAt_const (c := 1)) h

@[to_additive]
theorem contMDiffWithinAt_finset_prod (h : âˆ€ i âˆˆ t, ContMDiffWithinAt I' I n (f i) s x) :
    ContMDiffWithinAt I' I n (fun x â†¦ âˆ i âˆˆ t, f i x) s x := by
  simp only [â† Finset.prod_apply]
  exact contMDiffWithinAt_finset_prod' h

@[to_additive]
theorem ContMDiffAt.prod (h : âˆ€ i âˆˆ t, ContMDiffAt I' I n (f i) xâ‚€) :
    ContMDiffAt I' I n (fun x â†¦ âˆ i âˆˆ t, f i x) xâ‚€ := by
  simp only [â† contMDiffWithinAt_univ] at *
  exact ContMDiffWithinAt.prod h

@[to_additive]
theorem contMDiffAt_finprod
    (lf : LocallyFinite fun i â†¦ mulSupport <| f i) (h : âˆ€ i, ContMDiffAt I' I n (f i) xâ‚€) :
    ContMDiffAt I' I n (fun x â†¦ âˆá¶  i, f i x) xâ‚€ :=
  contMDiffWithinAt_finprod lf h

@[to_additive]
theorem contMDiffAt_finset_prod' (h : âˆ€ i âˆˆ t, ContMDiffAt I' I n (f i) x) :
    ContMDiffAt I' I n (âˆ i âˆˆ t, f i) x :=
  contMDiffWithinAt_finset_prod' h

@[to_additive]
theorem contMDiffAt_finset_prod (h : âˆ€ i âˆˆ t, ContMDiffAt I' I n (f i) x) :
    ContMDiffAt I' I n (fun x â†¦ âˆ i âˆˆ t, f i x) x :=
  contMDiffWithinAt_finset_prod h

@[to_additive]
theorem contMDiffOn_finprod
    (lf : LocallyFinite fun i â†¦ Function.mulSupport <| f i) (h : âˆ€ i, ContMDiffOn I' I n (f i) s) :
    ContMDiffOn I' I n (fun x â†¦ âˆá¶  i, f i x) s := fun x hx â†¦
  contMDiffWithinAt_finprod lf fun i â†¦ h i x hx

@[to_additive]
theorem contMDiffOn_finset_prod' (h : âˆ€ i âˆˆ t, ContMDiffOn I' I n (f i) s) :
    ContMDiffOn I' I n (âˆ i âˆˆ t, f i) s := fun x hx â†¦
  contMDiffWithinAt_finset_prod' fun i hi â†¦ h i hi x hx

@[to_additive]
theorem contMDiffOn_finset_prod (h : âˆ€ i âˆˆ t, ContMDiffOn I' I n (f i) s) :
    ContMDiffOn I' I n (fun x â†¦ âˆ i âˆˆ t, f i x) s := fun x hx â†¦
  contMDiffWithinAt_finset_prod fun i hi â†¦ h i hi x hx

@[to_additive]
theorem ContMDiff.prod (h : âˆ€ i âˆˆ t, ContMDiff I' I n (f i)) :
    ContMDiff I' I n fun x â†¦ âˆ i âˆˆ t, f i x :=
  fun x â†¦ ContMDiffAt.prod fun j hj â†¦ h j hj x

@[to_additive]
theorem contMDiff_finset_prod' (h : âˆ€ i âˆˆ t, ContMDiff I' I n (f i)) :
    ContMDiff I' I n (âˆ i âˆˆ t, f i) := fun x â†¦ contMDiffAt_finset_prod' fun i hi â†¦ h i hi x

@[to_additive]
theorem contMDiff_finset_prod (h : âˆ€ i âˆˆ t, ContMDiff I' I n (f i)) :
    ContMDiff I' I n fun x â†¦ âˆ i âˆˆ t, f i x := fun x â†¦
  contMDiffAt_finset_prod fun i hi â†¦ h i hi x

@[to_additive]
theorem contMDiff_finprod (h : âˆ€ i, ContMDiff I' I n (f i))
    (hfin : LocallyFinite fun i â†¦ mulSupport (f i)) : ContMDiff I' I n fun x â†¦ âˆá¶  i, f i x :=
  fun x â†¦ contMDiffAt_finprod hfin fun i â†¦ h i x

@[to_additive]
theorem contMDiff_finprod_cond (hc : âˆ€ i, p i â†’ ContMDiff I' I n (f i))
    (hf : LocallyFinite fun i â†¦ mulSupport (f i)) :
    ContMDiff I' I n fun x â†¦ âˆá¶  (i) (_ : p i), f i x := by
  simp only [â† finprod_subtype_eq_finprod_cond]
  exact contMDiff_finprod (fun i â†¦ hc i i.2) (hf.comp_injective Subtype.coe_injective)

end CommMonoid

section

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ð•œ E] {n : WithTop â„•âˆž}

instance instContMDiffAddSelf : ContMDiffAdd ð“˜(ð•œ, E) n E := by
  constructor
  rw [â† modelWithCornersSelf_prod, chartedSpaceSelf_prod]
  exact contDiff_add.contMDiff

end

section DivConst

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ] {n : WithTop â„•âˆž}
  {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ð•œ E] {I : ModelWithCorners ð•œ E H}
  {G : Type*} [DivInvMonoid G] [TopologicalSpace G] [ChartedSpace H G] [ContMDiffMul I n G]
  {E' : Type*} [NormedAddCommGroup E']
  [NormedSpace ð•œ E'] {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners ð•œ E' H'}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]

variable {f : M â†’ G} {s : Set M} {x : M} (c : G)

@[to_additive]
theorem ContMDiffWithinAt.div_const (hf : ContMDiffWithinAt I' I n f s x) :
    ContMDiffWithinAt I' I n (fun x â†¦ f x / c) s x := by
  simpa only [div_eq_mul_inv] using hf.mul contMDiffWithinAt_const

@[to_additive]
nonrec theorem ContMDiffAt.div_const (hf : ContMDiffAt I' I n f x) :
    ContMDiffAt I' I n (fun x â†¦ f x / c) x :=
  hf.div_const c

@[to_additive]
theorem ContMDiffOn.div_const (hf : ContMDiffOn I' I n f s) :
    ContMDiffOn I' I n (fun x â†¦ f x / c) s := fun x hx â†¦ (hf x hx).div_const c

@[to_additive]
theorem ContMDiff.div_const (hf : ContMDiff I' I n f) :
    ContMDiff I' I n (fun x â†¦ f x / c) := fun x â†¦ (hf x).div_const c

end DivConst
