/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.RingTheory.Derivation.Lie
import Mathlib.Geometry.Manifold.DerivationBundle

/-!

# Left invariant derivations

In this file we define the concept of left invariant derivation for a Lie group. The concept is
analogous to the more classical concept of left invariant vector fields, and it holds that the
derivation associated to a vector field is left invariant iff the field is.

Moreover we prove that `LeftInvariantDerivation I G` has the structure of a Lie algebra, hence
implementing one of the possible definitions of the Lie algebra attached to a Lie group.

Note that one can also define a Lie algebra on the space of left-invariant vector fields
(see `instLieAlgebraGroupLieAlgebra`). For finite-dimensional `C^âˆ` real manifolds, the space of
derivations can be canonically identified with the tangent space, and we recover the same Lie
algebra structure (TODO: prove this). In other smoothness classes or on other
fields, this identification is not always true, though, so the derivations point of view does not
work in these settings. The left-invariant vector fields should
therefore be favored to construct a theory of Lie groups in suitable generality.
-/


noncomputable section

open scoped LieGroup Manifold Derivation ContDiff

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ} {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) (G : Type*)
  [TopologicalSpace G] [ChartedSpace H G] [Monoid G] [ContMDiffMul I âˆ G] (g h : G)

-- Generate trivial has_sizeof instance. It prevents weird type class inference timeout problems
-- Porting note (https://github.com/leanprover-community/mathlib4/issues/12096): removed @[nolint instance_priority], linter not ported yet
-- @[local nolint instance_priority, local instance 10000]
-- private def disable_has_sizeof {Î±} : SizeOf Î± :=
--   âŸ¨fun _ => 0âŸ©

/-- Left-invariant global derivations.

A global derivation is left-invariant if it is equal to its pullback along left multiplication by
an arbitrary element of `G`.
-/
structure LeftInvariantDerivation extends Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯ where
  left_invariant'' :
    âˆ€ g, ğ’…â‚• (smoothLeftMul_one I g) (Derivation.evalAt 1 toDerivation) =
      Derivation.evalAt g toDerivation

variable {I G}

namespace LeftInvariantDerivation

instance : Coe (LeftInvariantDerivation I G) (Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
  âŸ¨toDerivationâŸ©

attribute [coe] toDerivation

theorem toDerivation_injective :
    Function.Injective (toDerivation : LeftInvariantDerivation I G â†’ _) :=
  fun X Y h => by cases X; cases Y; congr

instance : FunLike (LeftInvariantDerivation I G) C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯ where
  coe f := f.toDerivation
  coe_injective' _ _ h := toDerivation_injective <| DFunLike.ext' h

instance : LinearMapClass (LeftInvariantDerivation I G) ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯ where
  map_add f := map_add f.1
  map_smulâ‚›â‚— f := map_smul f.1.1

variable {r : ğ•œ} {X Y : LeftInvariantDerivation I G} {f f' : C^âˆâŸ®I, G; ğ•œâŸ¯}

theorem toFun_eq_coe : X.toFun = â‡‘X :=
  rfl

-- Porting note: now LHS is the same as RHS

theorem coe_injective :
    @Function.Injective (LeftInvariantDerivation I G) (_ â†’ C^âˆâŸ®I, G; ğ•œâŸ¯) DFunLike.coe :=
  DFunLike.coe_injective

@[ext]
theorem ext (h : âˆ€ f, X f = Y f) : X = Y := DFunLike.ext _ _ h

variable (X Y f)

theorem coe_derivation :
    â‡‘(X : Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = (X : C^âˆâŸ®I, G; ğ•œâŸ¯ â†’ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
  rfl

/-- Premature version of the lemma. Prefer using `left_invariant` instead. -/
theorem left_invariant' :
    ğ’…â‚• (smoothLeftMul_one I g) (Derivation.evalAt (1 : G) â†‘X) = Derivation.evalAt g â†‘X :=
  left_invariant'' X g

protected theorem map_add : X (f + f') = X f + X f' := by simp

protected theorem map_zero : X 0 = 0 := by simp

protected theorem map_neg : X (-f) = -X f := by simp

protected theorem map_sub : X (f - f') = X f - X f' := by simp

protected theorem map_smul : X (r â€¢ f) = r â€¢ X f := by simp

@[simp]
theorem leibniz : X (f * f') = f â€¢ X f' + f' â€¢ X f :=
  X.leibniz' _ _

instance : Zero (LeftInvariantDerivation I G) :=
  âŸ¨âŸ¨0, fun g => by simp only [map_zero]âŸ©âŸ©

instance : Inhabited (LeftInvariantDerivation I G) :=
  âŸ¨0âŸ©

instance : Add (LeftInvariantDerivation I G) where
  add X Y :=
    âŸ¨X + Y, fun g => by
      simp only [map_add, Derivation.coe_add, left_invariant', Pi.add_apply]âŸ©

instance : Neg (LeftInvariantDerivation I G) where
  neg X := âŸ¨-X, fun g => by simp [left_invariant']âŸ©

instance : Sub (LeftInvariantDerivation I G) where
  sub X Y := âŸ¨X - Y, fun g => by simp [left_invariant']âŸ©

@[simp]
theorem coe_add : â‡‘(X + Y) = X + Y :=
  rfl

@[simp]
theorem coe_zero : â‡‘(0 : LeftInvariantDerivation I G) = 0 :=
  rfl

@[simp]
theorem coe_neg : â‡‘(-X) = -X :=
  rfl

@[simp]
theorem coe_sub : â‡‘(X - Y) = X - Y :=
  rfl

@[simp, norm_cast]
theorem lift_add : (â†‘(X + Y) : Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = X + Y :=
  rfl

@[simp, norm_cast]
theorem lift_zero :
    (â†‘(0 : LeftInvariantDerivation I G) : Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = 0 :=
  rfl

instance hasNatScalar : SMul â„• (LeftInvariantDerivation I G) where
  smul r X := âŸ¨r â€¢ X.1, fun g => by simp_rw [LinearMap.map_smul_of_tower _ r, left_invariant']âŸ©

instance hasIntScalar : SMul â„¤ (LeftInvariantDerivation I G) where
  smul r X := âŸ¨r â€¢ X.1, fun g => by simp_rw [LinearMap.map_smul_of_tower _ r, left_invariant']âŸ©

instance : AddCommGroup (LeftInvariantDerivation I G) :=
  coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ => rfl

instance : SMul ğ•œ (LeftInvariantDerivation I G) where
  smul r X := âŸ¨r â€¢ X.1, fun g => by simp_rw [LinearMap.map_smul, left_invariant']âŸ©

variable (r)

@[simp]
theorem coe_smul : â‡‘(r â€¢ X) = r â€¢ â‡‘X :=
  rfl

@[simp]
theorem lift_smul (k : ğ•œ) : (k â€¢ X).1 = k â€¢ X.1 :=
  rfl

variable (I G)

/-- The coercion to function is a monoid homomorphism. -/
@[simps]
def coeFnAddMonoidHom : LeftInvariantDerivation I G â†’+ C^âˆâŸ®I, G; ğ•œâŸ¯ â†’ C^âˆâŸ®I, G; ğ•œâŸ¯ :=
  âŸ¨âŸ¨DFunLike.coe, coe_zeroâŸ©, coe_addâŸ©

variable {I G}

instance : Module ğ•œ (LeftInvariantDerivation I G) :=
  coe_injective.module _ (coeFnAddMonoidHom I G) coe_smul

/-- Evaluation at a point for left invariant derivation. Same thing as for generic global
derivations (`Derivation.evalAt`). -/
def evalAt : LeftInvariantDerivation I G â†’â‚—[ğ•œ] PointDerivation I g where
  toFun X := Derivation.evalAt g X.1
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

theorem evalAt_apply : evalAt g X f = (X f) g :=
  rfl

@[simp]
theorem evalAt_coe : Derivation.evalAt g â†‘X = evalAt g X :=
  rfl

theorem left_invariant : ğ’…â‚• (smoothLeftMul_one I g) (evalAt (1 : G) X) = evalAt g X :=
  X.left_invariant'' g

theorem evalAt_mul : evalAt (g * h) X = ğ’…â‚• (L_apply I g h) (evalAt h X) := by
  ext f
  rw [â† left_invariant, hfdifferential_apply, hfdifferential_apply, L_mul, fdifferential_comp,
    fdifferential_apply]
  -- Porting note: more aggressive here
  erw [LinearMap.comp_apply]
  -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
  erw [fdifferential_apply, â† hfdifferential_apply, left_invariant]

theorem comp_L : (X f).comp (ğ‘³ I g) = X (f.comp (ğ‘³ I g)) := by
  ext h
  rw [ContMDiffMap.comp_apply, L_apply, â† evalAt_apply, evalAt_mul, hfdifferential_apply,
    fdifferential_apply, evalAt_apply]

instance : Bracket (LeftInvariantDerivation I G) (LeftInvariantDerivation I G) where
  bracket X Y :=
    âŸ¨â…(X : Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯), Yâ†, fun g => by
      ext f
      have hX := Derivation.congr_fun (left_invariant' g X) (Y f)
      have hY := Derivation.congr_fun (left_invariant' g Y) (X f)
      rw [hfdifferential_apply, fdifferential_apply, Derivation.evalAt_apply] at hX hY âŠ¢
      rw [comp_L] at hX hY
      rw [Derivation.commutator_apply, ContMDiffMap.coe_sub, Pi.sub_apply, coe_derivation]
      rw [coe_derivation] at hX hY âŠ¢
      rw [hX, hY]
      rflâŸ©

@[simp]
theorem commutator_coe_derivation :
    â‡‘â…X, Yâ† =
      (â…(X : Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯), Yâ† :
        Derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
  rfl

theorem commutator_apply : â…X, Yâ† f = X (Y f) - Y (X f) :=
  rfl

instance : LieRing (LeftInvariantDerivation I G) where
  add_lie X Y Z := by
    ext1
    simp only [commutator_apply, coe_add, Pi.add_apply, map_add]
    ring
  lie_add X Y Z := by
    ext1
    simp only [commutator_apply, coe_add, Pi.add_apply, map_add]
    ring
  lie_self X := by ext1; simp only [commutator_apply, sub_self]; rfl
  leibniz_lie X Y Z := by
    ext1
    simp only [commutator_apply, coe_add, coe_sub, map_sub, Pi.add_apply]
    ring

instance : LieAlgebra ğ•œ (LeftInvariantDerivation I G) where
  lie_smul r Y Z := by
    ext1
    simp only [commutator_apply, map_smul, smul_sub, coe_smul, Pi.smul_apply]

end LeftInvariantDerivation
