/-
Copyright (c) 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.Monoid

/-!
# Lie groups

A Lie group is a group that is also a `C^n` manifold, in which the group operations of
multiplication and inversion are `C^n` maps. Regularity of the group multiplication means that
multiplication is a `C^n` mapping of the product manifold `G` √ó `G` into `G`.

Note that, since a manifold here is not second-countable and Hausdorff a Lie group here is not
guaranteed to be second-countable (even though it can be proved it is Hausdorff). Note also that Lie
groups here are not necessarily finite dimensional.

## Main definitions

* `LieAddGroup I G` : a Lie additive group where `G` is a manifold on the model with corners `I`.
* `LieGroup I G` : a Lie multiplicative group where `G` is a manifold on the model with corners `I`.
* `ContMDiffInv‚ÇÄ`: typeclass for `C^n` manifolds with `0` and `Inv` such that inversion is `C^n`
  map at each non-zero point. This includes complete normed fields and (multiplicative) Lie groups.


## Main results
* `ContMDiff.inv`, `ContMDiff.div` and variants: point-wise inversion and division of maps `M ‚Üí G`
  is `C^n`.
* `ContMDiff.inv‚ÇÄ` and variants: if `ContMDiffInv‚ÇÄ I n N`, point-wise inversion of `C^n`
  maps `f : M ‚Üí N` is `C^n` at all points at which `f` doesn't vanish.
* `ContMDiff.div‚ÇÄ` and variants: if also `ContMDiffMul I n N` (i.e., `N` is a Lie group except
  possibly for smoothness of inversion at `0`), similar results hold for point-wise division.
* `instNormedSpaceLieAddGroup` : a normed vector space over a nontrivially normed field
  is an additive Lie group.
* `Instances/UnitsOfNormedAlgebra` shows that the group of units of a complete normed `ùïú`-algebra
  is a multiplicative Lie group.

## Implementation notes

A priori, a Lie group here is a manifold with corners.

The definition of Lie group cannot require `I : ModelWithCorners ùïú E E` with the same space as the
model space and as the model vector space, as one might hope, because in the product situation,
the model space is `ModelProd E E'` and the model vector space is `E √ó E'`, which are not the same,
so the definition does not apply. Hence the definition should be more general, allowing
`I : ModelWithCorners ùïú E H`.
-/

noncomputable section

open scoped Manifold ContDiff

-- See note [Design choices about smooth algebraic structures]
/-- An additive Lie group is a group and a `C^n` manifold at the same time in which
the addition and negation operations are `C^n`. -/
class LieAddGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H)
    (n : WithTop ‚Ñï‚àû) (G : Type*)
    [AddGroup G] [TopologicalSpace G] [ChartedSpace H G] : Prop extends ContMDiffAdd I n G where
  /-- Negation is smooth in an additive Lie group. -/
  contMDiff_neg : ContMDiff I I n fun a : G => -a

-- See note [Design choices about smooth algebraic structures]
/-- A (multiplicative) Lie group is a group and a `C^n` manifold at the same time in which
the multiplication and inverse operations are `C^n`. -/
@[to_additive]
class LieGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H)
    (n : WithTop ‚Ñï‚àû) (G : Type*)
    [Group G] [TopologicalSpace G] [ChartedSpace H G] : Prop extends ContMDiffMul I n G where
  /-- Inversion is smooth in a Lie group. -/
  contMDiff_inv : ContMDiff I I n fun a : G => a‚Åª¬π

/-!
  ### Smoothness of inversion, negation, division and subtraction

  Let `f : M ‚Üí G` be a `C^n` function into a Lie group, then `f` is point-wise
  invertible with smooth inverse `f`. If `f` and `g` are two such functions, the quotient
  `f / g` (i.e., the point-wise product of `f` and the point-wise inverse of `g`) is also `C^n`. -/
section PointwiseDivision

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {n : WithTop ‚Ñï‚àû} {G : Type*}
  [TopologicalSpace G] [ChartedSpace H G] [Group G] {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]

@[to_additive]
protected theorem LieGroup.of_le {m n : WithTop ‚Ñï‚àû} (hmn : m ‚â§ n)
    [h : LieGroup I n G] : LieGroup I m G := by
  have : ContMDiffMul I m G := ContMDiffMul.of_le hmn
  exact ‚ü®h.contMDiff_inv.of_le hmn‚ü©

@[to_additive]
instance {a : WithTop ‚Ñï‚àû} [LieGroup I ‚àû G] [h : ENat.LEInfty a] : LieGroup I a G :=
  LieGroup.of_le h.out

@[to_additive]
instance {a : WithTop ‚Ñï‚àû} [LieGroup I œâ G] : LieGroup I a G :=
  LieGroup.of_le le_top

@[to_additive]
instance [IsTopologicalGroup G] : LieGroup I 0 G := by
  constructor
  rw [contMDiff_zero_iff]
  exact continuous_inv

@[to_additive]
instance [LieGroup I 2 G] : LieGroup I 1 G :=
  LieGroup.of_le one_le_two

variable [LieGroup I n G]

section

variable (I n)

/-- In a Lie group, inversion is `C^n`. -/
@[to_additive /-- In an additive Lie group, inversion is a smooth map. -/]
theorem contMDiff_inv : ContMDiff I I n fun x : G => x‚Åª¬π :=
  LieGroup.contMDiff_inv

include I n in
/-- A Lie group is a topological group. This is not an instance for technical reasons,
see note [Design choices about smooth algebraic structures]. -/
@[to_additive /-- An additive Lie group is an additive topological group. This is not an instance
for technical reasons, see note [Design choices about smooth algebraic structures]. -/]
theorem topologicalGroup_of_lieGroup : IsTopologicalGroup G :=
  { continuousMul_of_contMDiffMul I n with continuous_inv := (contMDiff_inv I n).continuous }

end

@[to_additive]
theorem ContMDiffWithinAt.inv {f : M ‚Üí G} {s : Set M} {x‚ÇÄ : M}
    (hf : ContMDiffWithinAt I' I n f s x‚ÇÄ) : ContMDiffWithinAt I' I n (fun x => (f x)‚Åª¬π) s x‚ÇÄ :=
  (contMDiff_inv I n).contMDiffAt.contMDiffWithinAt.comp x‚ÇÄ hf <| Set.mapsTo_univ _ _

@[to_additive]
theorem ContMDiffAt.inv {f : M ‚Üí G} {x‚ÇÄ : M} (hf : ContMDiffAt I' I n f x‚ÇÄ) :
    ContMDiffAt I' I n (fun x => (f x)‚Åª¬π) x‚ÇÄ :=
  (contMDiff_inv I n).contMDiffAt.comp x‚ÇÄ hf

@[to_additive]
theorem ContMDiffOn.inv {f : M ‚Üí G} {s : Set M} (hf : ContMDiffOn I' I n f s) :
    ContMDiffOn I' I n (fun x => (f x)‚Åª¬π) s := fun x hx => (hf x hx).inv

@[to_additive]
theorem ContMDiff.inv {f : M ‚Üí G} (hf : ContMDiff I' I n f) : ContMDiff I' I n fun x => (f x)‚Åª¬π :=
  fun x => (hf x).inv

@[to_additive]
theorem ContMDiffWithinAt.div {f g : M ‚Üí G} {s : Set M} {x‚ÇÄ : M}
    (hf : ContMDiffWithinAt I' I n f s x‚ÇÄ) (hg : ContMDiffWithinAt I' I n g s x‚ÇÄ) :
    ContMDiffWithinAt I' I n (fun x => f x / g x) s x‚ÇÄ := by
  simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[to_additive]
theorem ContMDiffAt.div {f g : M ‚Üí G} {x‚ÇÄ : M} (hf : ContMDiffAt I' I n f x‚ÇÄ)
    (hg : ContMDiffAt I' I n g x‚ÇÄ) : ContMDiffAt I' I n (fun x => f x / g x) x‚ÇÄ := by
  simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[to_additive]
theorem ContMDiffOn.div {f g : M ‚Üí G} {s : Set M} (hf : ContMDiffOn I' I n f s)
    (hg : ContMDiffOn I' I n g s) : ContMDiffOn I' I n (fun x => f x / g x) s := by
  simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[to_additive]
theorem ContMDiff.div {f g : M ‚Üí G} (hf : ContMDiff I' I n f) (hg : ContMDiff I' I n g) :
    ContMDiff I' I n fun x => f x / g x := by simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

end PointwiseDivision

/-! Binary product of Lie groups -/
section Product

-- Instance of product group
@[to_additive]
instance Prod.instLieGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {n : WithTop ‚Ñï‚àû}
    {H : Type*} [TopologicalSpace H] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {G : Type*}
    [TopologicalSpace G] [ChartedSpace H G] [Group G] [LieGroup I n G] {E' : Type*}
    [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
    {I' : ModelWithCorners ùïú E' H'} {G' : Type*} [TopologicalSpace G'] [ChartedSpace H' G']
    [Group G'] [LieGroup I' n G'] : LieGroup (I.prod I') n (G √ó G') :=
  { ContMDiffMul.prod _ _ _ _ with contMDiff_inv := contMDiff_fst.inv.prodMk contMDiff_snd.inv }

end Product

/-! ### Normed spaces are Lie groups -/

instance instNormedSpaceLieAddGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {n : WithTop ‚Ñï‚àû}
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] : LieAddGroup ùìò(ùïú, E) n E where
  contMDiff_neg := contDiff_neg.contMDiff

/-! ## `C^n` manifolds with `C^n` inversion away from zero

Typeclass for `C^n` manifolds with `0` and `Inv` such that inversion is `C^n` at all non-zero
points. (This includes multiplicative Lie groups, but also complete normed semifields.)
Point-wise inversion is `C^n` when the function/denominator is non-zero. -/
section ContMDiffInv‚ÇÄ

-- See note [Design choices about smooth algebraic structures]
/-- A `C^n` manifold with `0` and `Inv` such that `fun x ‚Ü¶ x‚Åª¬π` is `C^n` at all nonzero points.
Any complete normed (semi)field has this property. -/
class ContMDiffInv‚ÇÄ {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H)
    (n : WithTop ‚Ñï‚àû) (G : Type*)
    [Inv G] [Zero G] [TopologicalSpace G] [ChartedSpace H G] : Prop where
  /-- Inversion is `C^n` away from `0`. -/
  contMDiffAt_inv‚ÇÄ : ‚àÄ ‚¶Éx : G‚¶Ñ, x ‚â† 0 ‚Üí ContMDiffAt I I n (fun y ‚Ü¶ y‚Åª¬π) x

instance {ùïú : Type*} [NontriviallyNormedField ùïú] {n : WithTop ‚Ñï‚àû} : ContMDiffInv‚ÇÄ ùìò(ùïú) n ùïú where
  contMDiffAt_inv‚ÇÄ x hx := by
    change ContMDiffAt ùìò(ùïú) ùìò(ùïú) n Inv.inv x
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_inv ùïú hx

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {n : WithTop ‚Ñï‚àû}
  {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {G : Type*}
  [TopologicalSpace G] [ChartedSpace H G] [Inv G] [Zero G] {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]
  {f : M ‚Üí G}

protected theorem ContMDiffInv‚ÇÄ.of_le {m n : WithTop ‚Ñï‚àû} (hmn : m ‚â§ n)
    [h : ContMDiffInv‚ÇÄ I n G] : ContMDiffInv‚ÇÄ I m G := by
  exact ‚ü®fun x hx ‚Ü¶ (h.contMDiffAt_inv‚ÇÄ hx).of_le hmn‚ü©

instance {a : WithTop ‚Ñï‚àû} [ContMDiffInv‚ÇÄ I ‚àû G] [h : ENat.LEInfty a] : ContMDiffInv‚ÇÄ I a G :=
  ContMDiffInv‚ÇÄ.of_le h.out

instance {a : WithTop ‚Ñï‚àû} [ContMDiffInv‚ÇÄ I œâ G] : ContMDiffInv‚ÇÄ I a G :=
  ContMDiffInv‚ÇÄ.of_le le_top

instance [HasContinuousInv‚ÇÄ G] : ContMDiffInv‚ÇÄ I 0 G := by
  have : T1Space G := I.t1Space G
  constructor
  have A : ContMDiffOn I I 0 (fun (x : G) ‚Ü¶ x‚Åª¬π) {0}·∂ú := by
    rw [contMDiffOn_zero_iff]
    exact continuousOn_inv‚ÇÄ
  intro x hx
  have : ContMDiffWithinAt I I 0 (fun (x : G) ‚Ü¶ x‚Åª¬π) {0}·∂ú x := A x hx
  apply ContMDiffWithinAt.contMDiffAt this
  exact IsOpen.mem_nhds isOpen_compl_singleton hx

instance [ContMDiffInv‚ÇÄ I 2 G] : ContMDiffInv‚ÇÄ I 1 G :=
  ContMDiffInv‚ÇÄ.of_le one_le_two

variable [ContMDiffInv‚ÇÄ I n G]

theorem contMDiffAt_inv‚ÇÄ {x : G} (hx : x ‚â† 0) : ContMDiffAt I I n (fun y ‚Ü¶ y‚Åª¬π) x :=
  ContMDiffInv‚ÇÄ.contMDiffAt_inv‚ÇÄ hx

include I n in
/-- In a manifold with `C^n` inverse away from `0`, the inverse is continuous away from `0`.
This is not an instance for technical reasons, see
note [Design choices about smooth algebraic structures]. -/
theorem hasContinuousInv‚ÇÄ_of_hasContMDiffInv‚ÇÄ : HasContinuousInv‚ÇÄ G :=
  { continuousAt_inv‚ÇÄ := fun _ hx ‚Ü¶ (contMDiffAt_inv‚ÇÄ (I := I) (n := n) hx).continuousAt }

theorem contMDiffOn_inv‚ÇÄ : ContMDiffOn I I n (Inv.inv : G ‚Üí G) {0}·∂ú := fun _x hx =>
  (contMDiffAt_inv‚ÇÄ hx).contMDiffWithinAt

variable {s : Set M} {a : M}

theorem ContMDiffWithinAt.inv‚ÇÄ (hf : ContMDiffWithinAt I' I n f s a) (ha : f a ‚â† 0) :
    ContMDiffWithinAt I' I n (fun x => (f x)‚Åª¬π) s a :=
  (contMDiffAt_inv‚ÇÄ ha).comp_contMDiffWithinAt a hf

theorem ContMDiffAt.inv‚ÇÄ (hf : ContMDiffAt I' I n f a) (ha : f a ‚â† 0) :
    ContMDiffAt I' I n (fun x ‚Ü¶ (f x)‚Åª¬π) a :=
  (contMDiffAt_inv‚ÇÄ ha).comp a hf

theorem ContMDiff.inv‚ÇÄ (hf : ContMDiff I' I n f) (h0 : ‚àÄ x, f x ‚â† 0) :
    ContMDiff I' I n (fun x ‚Ü¶ (f x)‚Åª¬π) :=
  fun x ‚Ü¶ ContMDiffAt.inv‚ÇÄ (hf x) (h0 x)

theorem ContMDiffOn.inv‚ÇÄ (hf : ContMDiffOn I' I n f s) (h0 : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContMDiffOn I' I n (fun x => (f x)‚Åª¬π) s :=
  fun x hx ‚Ü¶ ContMDiffWithinAt.inv‚ÇÄ (hf x hx) (h0 x hx)

end ContMDiffInv‚ÇÄ

/-! ### Point-wise division of `C^n` functions

If `[ContMDiffMul I n N]` and `[ContMDiffInv‚ÇÄ I n N]`, point-wise division of `C^n`
functions `f : M ‚Üí N` is `C^n` whenever the denominator is non-zero.
(This includes `N` being a completely normed field.)
-/
section Div

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {n : WithTop ‚Ñï‚àû}
{H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {G : Type*}
  [TopologicalSpace G] [ChartedSpace H G] [GroupWithZero G] [ContMDiffInv‚ÇÄ I n G]
  [ContMDiffMul I n G]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]
  {f g : M ‚Üí G} {s : Set M} {a : M}

theorem ContMDiffWithinAt.div‚ÇÄ
    (hf : ContMDiffWithinAt I' I n f s a) (hg : ContMDiffWithinAt I' I n g s a) (h‚ÇÄ : g a ‚â† 0) :
    ContMDiffWithinAt I' I n (f / g) s a := by
  simpa [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem ContMDiffOn.div‚ÇÄ (hf : ContMDiffOn I' I n f s) (hg : ContMDiffOn I' I n g s)
    (h‚ÇÄ : ‚àÄ x ‚àà s, g x ‚â† 0) : ContMDiffOn I' I n (f / g) s := by
  simpa [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem ContMDiffAt.div‚ÇÄ (hf : ContMDiffAt I' I n f a) (hg : ContMDiffAt I' I n g a)
    (h‚ÇÄ : g a ‚â† 0) : ContMDiffAt I' I n (f / g) a := by
  simpa [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem ContMDiff.div‚ÇÄ (hf : ContMDiff I' I n f) (hg : ContMDiff I' I n g) (h‚ÇÄ : ‚àÄ x, g x ‚â† 0) :
    ContMDiff I' I n (f / g) := by simpa only [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

end Div
