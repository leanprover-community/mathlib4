/-
Copyright (c) 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.Monoid

/-!
# Lie groups

A Lie group is a group that is also a smooth manifold, in which the group operations of
multiplication and inversion are smooth maps. Smoothness of the group multiplication means that
multiplication is a smooth mapping of the product manifold `G` √ó `G` into `G`.

Note that, since a manifold here is not second-countable and Hausdorff a Lie group here is not
guaranteed to be second-countable (even though it can be proved it is Hausdorff). Note also that Lie
groups here are not necessarily finite dimensional.

## Main definitions

* `LieAddGroup I G` : a Lie additive group where `G` is a manifold on the model with corners `I`.
* `LieGroup I G` : a Lie multiplicative group where `G` is a manifold on the model with corners `I`.
* `SmoothInv‚ÇÄ`: typeclass for smooth manifolds with `0` and `Inv` such that inversion is a smooth
  map at each non-zero point. This includes complete normed fields and (multiplicative) Lie groups.


## Main results
* `ContMDiff.inv`, `ContMDiff.div` and variants: point-wise inversion and division of maps `M ‚Üí G`
  is smooth
* `ContMDiff.inv‚ÇÄ` and variants: if `SmoothInv‚ÇÄ N`, point-wise inversion of smooth maps `f : M ‚Üí N`
  is smooth at all points at which `f` doesn't vanish.
* `ContMDiff.div‚ÇÄ` and variants: if also `SmoothMul N` (i.e., `N` is a Lie group except possibly
  for smoothness of inversion at `0`), similar results hold for point-wise division.
* `normedSpaceLieAddGroup` : a normed vector space over a nontrivially normed field
  is an additive Lie group.
* `Instances/UnitsOfNormedAlgebra` shows that the group of units of a complete normed `ùïú`-algebra
  is a multiplicative Lie group.

## Implementation notes

A priori, a Lie group here is a manifold with corners.

The definition of Lie group cannot require `I : ModelWithCorners ùïú E E` with the same space as the
model space and as the model vector space, as one might hope, because in the product situation,
the model space is `ModelProd E E'` and the model vector space is `E √ó E'`, which are not the same,
so the definition does not apply. Hence the definition should be more general, allowing
`I : ModelWithCorners ùïú E H`.
-/

noncomputable section

open scoped Manifold

-- See note [Design choices about smooth algebraic structures]
/-- An additive Lie group is a group and a smooth manifold at the same time in which
the addition and negation operations are smooth. -/
class LieAddGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H) (G : Type*)
    [AddGroup G] [TopologicalSpace G] [ChartedSpace H G] extends SmoothAdd I G : Prop where
  /-- Negation is smooth in an additive Lie group. -/
  smooth_neg : ContMDiff I I ‚ä§ fun a : G => -a

-- See note [Design choices about smooth algebraic structures]
/-- A (multiplicative) Lie group is a group and a smooth manifold at the same time in which
the multiplication and inverse operations are smooth. -/
@[to_additive]
class LieGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H) (G : Type*)
    [Group G] [TopologicalSpace G] [ChartedSpace H G] extends SmoothMul I G : Prop where
  /-- Inversion is smooth in a Lie group. -/
  smooth_inv : ContMDiff I I ‚ä§ fun a : G => a‚Åª¬π

/-!
  ### Smoothness of inversion, negation, division and subtraction

  Let `f : M ‚Üí G` be a `C^n` or smooth functions into a Lie group, then `f` is point-wise
  invertible with smooth inverse `f`. If `f` and `g` are two such functions, the quotient
  `f / g` (i.e., the point-wise product of `f` and the point-wise inverse of `g`) is also smooth. -/
section PointwiseDivision

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {G : Type*}
  [TopologicalSpace G] [ChartedSpace H G] [Group G] [LieGroup I G] {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]
  {n : ‚Ñï‚àû}

section

variable (I)

/-- In a Lie group, inversion is a smooth map. -/
@[to_additive "In an additive Lie group, inversion is a smooth map."]
theorem contMDiff_inv : ContMDiff I I ‚ä§ fun x : G => x‚Åª¬π :=
  LieGroup.smooth_inv

@[deprecated (since := "2024-11-21")] alias smooth_inv := contMDiff_inv
@[deprecated (since := "2024-11-21")] alias smooth_neg := contMDiff_neg

include I in
/-- A Lie group is a topological group. This is not an instance for technical reasons,
see note [Design choices about smooth algebraic structures]. -/
@[to_additive "An additive Lie group is an additive topological group. This is not an instance for
technical reasons, see note [Design choices about smooth algebraic structures]."]
theorem topologicalGroup_of_lieGroup : TopologicalGroup G :=
  { continuousMul_of_smooth I with continuous_inv := (contMDiff_inv I).continuous }

end

@[to_additive]
theorem ContMDiffWithinAt.inv {f : M ‚Üí G} {s : Set M} {x‚ÇÄ : M}
    (hf : ContMDiffWithinAt I' I n f s x‚ÇÄ) : ContMDiffWithinAt I' I n (fun x => (f x)‚Åª¬π) s x‚ÇÄ :=
  ((contMDiff_inv I).of_le le_top).contMDiffAt.contMDiffWithinAt.comp x‚ÇÄ hf <| Set.mapsTo_univ _ _

@[to_additive]
theorem ContMDiffAt.inv {f : M ‚Üí G} {x‚ÇÄ : M} (hf : ContMDiffAt I' I n f x‚ÇÄ) :
    ContMDiffAt I' I n (fun x => (f x)‚Åª¬π) x‚ÇÄ :=
  ((contMDiff_inv I).of_le le_top).contMDiffAt.comp x‚ÇÄ hf

@[to_additive]
theorem ContMDiffOn.inv {f : M ‚Üí G} {s : Set M} (hf : ContMDiffOn I' I n f s) :
    ContMDiffOn I' I n (fun x => (f x)‚Åª¬π) s := fun x hx => (hf x hx).inv

@[to_additive]
theorem ContMDiff.inv {f : M ‚Üí G} (hf : ContMDiff I' I n f) : ContMDiff I' I n fun x => (f x)‚Åª¬π :=
  fun x => (hf x).inv

@[deprecated (since := "2024-11-21")] alias SmoothWithinAt.inv := ContMDiffWithinAt.inv
@[deprecated (since := "2024-11-21")] alias SmoothAt.inv := ContMDiffAt.inv
@[deprecated (since := "2024-11-21")] alias SmoothOn.inv := ContMDiffOn.inv
@[deprecated (since := "2024-11-21")] alias Smooth.inv := ContMDiff.inv

@[deprecated (since := "2024-11-21")] alias SmoothWithinAt.neg := ContMDiffWithinAt.neg
@[deprecated (since := "2024-11-21")] alias SmoothAt.neg := ContMDiffAt.neg
@[deprecated (since := "2024-11-21")] alias SmoothOn.neg := ContMDiffOn.neg
@[deprecated (since := "2024-11-21")] alias Smooth.neg := ContMDiff.neg

@[to_additive]
theorem ContMDiffWithinAt.div {f g : M ‚Üí G} {s : Set M} {x‚ÇÄ : M}
    (hf : ContMDiffWithinAt I' I n f s x‚ÇÄ) (hg : ContMDiffWithinAt I' I n g s x‚ÇÄ) :
    ContMDiffWithinAt I' I n (fun x => f x / g x) s x‚ÇÄ := by
  simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[to_additive]
theorem ContMDiffAt.div {f g : M ‚Üí G} {x‚ÇÄ : M} (hf : ContMDiffAt I' I n f x‚ÇÄ)
    (hg : ContMDiffAt I' I n g x‚ÇÄ) : ContMDiffAt I' I n (fun x => f x / g x) x‚ÇÄ := by
  simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[to_additive]
theorem ContMDiffOn.div {f g : M ‚Üí G} {s : Set M} (hf : ContMDiffOn I' I n f s)
    (hg : ContMDiffOn I' I n g s) : ContMDiffOn I' I n (fun x => f x / g x) s := by
  simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[to_additive]
theorem ContMDiff.div {f g : M ‚Üí G} (hf : ContMDiff I' I n f) (hg : ContMDiff I' I n g) :
    ContMDiff I' I n fun x => f x / g x := by simp_rw [div_eq_mul_inv]; exact hf.mul hg.inv

@[deprecated (since := "2024-11-21")] alias SmoothWithinAt.div := ContMDiffWithinAt.div
@[deprecated (since := "2024-11-21")] alias SmoothAt.div := ContMDiffAt.div
@[deprecated (since := "2024-11-21")] alias SmoothOn.div := ContMDiffOn.div
@[deprecated (since := "2024-11-21")] alias Smooth.div := ContMDiff.div

@[deprecated (since := "2024-11-21")] alias SmoothWithinAt.sub := ContMDiffWithinAt.sub
@[deprecated (since := "2024-11-21")] alias SmoothAt.sub := ContMDiffAt.sub
@[deprecated (since := "2024-11-21")] alias SmoothOn.sub := ContMDiffOn.sub
@[deprecated (since := "2024-11-21")] alias Smooth.sub := ContMDiff.sub

end PointwiseDivision

/-! Binary product of Lie groups -/
section Product

-- Instance of product group
@[to_additive]
instance {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {G : Type*}
    [TopologicalSpace G] [ChartedSpace H G] [Group G] [LieGroup I G] {E' : Type*}
    [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
    {I' : ModelWithCorners ùïú E' H'} {G' : Type*} [TopologicalSpace G'] [ChartedSpace H' G']
    [Group G'] [LieGroup I' G'] : LieGroup (I.prod I') (G √ó G') :=
  { SmoothMul.prod _ _ _ _ with smooth_inv := contMDiff_fst.inv.prod_mk contMDiff_snd.inv }

end Product

/-! ### Normed spaces are Lie groups -/

instance normedSpaceLieAddGroup {ùïú : Type*} [NontriviallyNormedField ùïú] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ùïú E] : LieAddGroup ùìò(ùïú, E) E where
  smooth_neg := contDiff_neg.contMDiff

/-! ## Smooth manifolds with smooth inversion away from zero

Typeclass for smooth manifolds with `0` and `Inv` such that inversion is smooth at all non-zero
points. (This includes multiplicative Lie groups, but also complete normed semifields.)
Point-wise inversion is smooth when the function/denominator is non-zero. -/
section SmoothInv‚ÇÄ

-- See note [Design choices about smooth algebraic structures]
/-- A smooth manifold with `0` and `Inv` such that `fun x ‚Ü¶ x‚Åª¬π` is smooth at all nonzero points.
Any complete normed (semi)field has this property. -/
class SmoothInv‚ÇÄ {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H) (G : Type*)
    [Inv G] [Zero G] [TopologicalSpace G] [ChartedSpace H G] : Prop where
  /-- Inversion is smooth away from `0`. -/
  smoothAt_inv‚ÇÄ : ‚àÄ ‚¶Éx : G‚¶Ñ, x ‚â† 0 ‚Üí ContMDiffAt I I ‚ä§ (fun y ‚Ü¶ y‚Åª¬π) x

instance {ùïú : Type*} [NontriviallyNormedField ùïú] : SmoothInv‚ÇÄ ùìò(ùïú) ùïú where
  smoothAt_inv‚ÇÄ x hx := by
    change ContMDiffAt ùìò(ùïú) ùìò(ùïú) ‚ä§ Inv.inv x
    rw [contMDiffAt_iff_contDiffAt]
    exact contDiffAt_inv ùïú hx

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] (I : ModelWithCorners ùïú E H) {G : Type*}
  [TopologicalSpace G] [ChartedSpace H G] [Inv G] [Zero G] [SmoothInv‚ÇÄ I G] {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]
  {n : ‚Ñï‚àû} {f : M ‚Üí G}

theorem contMDiffAt_inv‚ÇÄ {x : G} (hx : x ‚â† 0) : ContMDiffAt I I ‚ä§ (fun y ‚Ü¶ y‚Åª¬π) x :=
  SmoothInv‚ÇÄ.smoothAt_inv‚ÇÄ hx

@[deprecated (since := "2024-11-21")] alias smoothAt_inv‚ÇÄ := contMDiffAt_inv‚ÇÄ

include I in
/-- In a manifold with smooth inverse away from `0`, the inverse is continuous away from `0`.
This is not an instance for technical reasons, see
note [Design choices about smooth algebraic structures]. -/
theorem hasContinuousInv‚ÇÄ_of_hasSmoothInv‚ÇÄ : HasContinuousInv‚ÇÄ G :=
  { continuousAt_inv‚ÇÄ := fun _ hx ‚Ü¶ (contMDiffAt_inv‚ÇÄ I hx).continuousAt }

theorem contMDiffOn_inv‚ÇÄ : ContMDiffOn I I ‚ä§ (Inv.inv : G ‚Üí G) {0}·∂ú := fun _x hx =>
  (contMDiffAt_inv‚ÇÄ I hx).contMDiffWithinAt

@[deprecated (since := "2024-11-21")] alias smoothOn_inv‚ÇÄ := contMDiffOn_inv‚ÇÄ
@[deprecated (since := "2024-11-21")] alias SmoothOn_inv‚ÇÄ := contMDiffOn_inv‚ÇÄ

variable {I} {s : Set M} {a : M}

theorem ContMDiffWithinAt.inv‚ÇÄ (hf : ContMDiffWithinAt I' I n f s a) (ha : f a ‚â† 0) :
    ContMDiffWithinAt I' I n (fun x => (f x)‚Åª¬π) s a :=
  ((contMDiffAt_inv‚ÇÄ I ha).of_le le_top).comp_contMDiffWithinAt a hf

theorem ContMDiffAt.inv‚ÇÄ (hf : ContMDiffAt I' I n f a) (ha : f a ‚â† 0) :
    ContMDiffAt I' I n (fun x ‚Ü¶ (f x)‚Åª¬π) a :=
  ((contMDiffAt_inv‚ÇÄ I ha).of_le le_top).comp a hf

theorem ContMDiff.inv‚ÇÄ (hf : ContMDiff I' I n f) (h0 : ‚àÄ x, f x ‚â† 0) :
    ContMDiff I' I n (fun x ‚Ü¶ (f x)‚Åª¬π) :=
  fun x ‚Ü¶ ContMDiffAt.inv‚ÇÄ (hf x) (h0 x)

theorem ContMDiffOn.inv‚ÇÄ (hf : ContMDiffOn I' I n f s) (h0 : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContMDiffOn I' I n (fun x => (f x)‚Åª¬π) s :=
  fun x hx ‚Ü¶ ContMDiffWithinAt.inv‚ÇÄ (hf x hx) (h0 x hx)

@[deprecated (since := "2024-11-21")] alias SmoothWithinAt.inv‚ÇÄ := ContMDiffWithinAt.inv‚ÇÄ
@[deprecated (since := "2024-11-21")] alias SmoothAt.inv‚ÇÄ := ContMDiffAt.inv‚ÇÄ
@[deprecated (since := "2024-11-21")] alias SmoothOn.inv‚ÇÄ := ContMDiffOn.inv‚ÇÄ
@[deprecated (since := "2024-11-21")] alias Smooth.inv‚ÇÄ := ContMDiff.inv‚ÇÄ

end SmoothInv‚ÇÄ

/-! ### Point-wise division of smooth functions

If `[SmoothMul I N]` and `[SmoothInv‚ÇÄ I N]`, point-wise division of smooth functions `f : M ‚Üí N`
is smooth whenever the denominator is non-zero. (This includes `N` being a completely normed field.)
-/
section Div

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {I : ModelWithCorners ùïú E H} {G : Type*}
  [TopologicalSpace G] [ChartedSpace H G] [GroupWithZero G] [SmoothInv‚ÇÄ I G] [SmoothMul I G]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M : Type*} [TopologicalSpace M] [ChartedSpace H' M]
  {f g : M ‚Üí G} {s : Set M} {a : M} {n : ‚Ñï‚àû}

theorem ContMDiffWithinAt.div‚ÇÄ
    (hf : ContMDiffWithinAt I' I n f s a) (hg : ContMDiffWithinAt I' I n g s a) (h‚ÇÄ : g a ‚â† 0) :
    ContMDiffWithinAt I' I n (f / g) s a := by
  simpa [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem ContMDiffOn.div‚ÇÄ (hf : ContMDiffOn I' I n f s) (hg : ContMDiffOn I' I n g s)
    (h‚ÇÄ : ‚àÄ x ‚àà s, g x ‚â† 0) : ContMDiffOn I' I n (f / g) s := by
  simpa [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem ContMDiffAt.div‚ÇÄ (hf : ContMDiffAt I' I n f a) (hg : ContMDiffAt I' I n g a)
    (h‚ÇÄ : g a ‚â† 0) : ContMDiffAt I' I n (f / g) a := by
  simpa [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem ContMDiff.div‚ÇÄ (hf : ContMDiff I' I n f) (hg : ContMDiff I' I n g) (h‚ÇÄ : ‚àÄ x, g x ‚â† 0) :
    ContMDiff I' I n (f / g) := by simpa only [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

@[deprecated (since := "2024-11-21")] alias SmoothWithinAt.div‚ÇÄ := ContMDiffWithinAt.div‚ÇÄ
@[deprecated (since := "2024-11-21")] alias SmoothAt.div‚ÇÄ := ContMDiffAt.div‚ÇÄ
@[deprecated (since := "2024-11-21")] alias SmoothOn.div‚ÇÄ := ContMDiffOn.div‚ÇÄ
@[deprecated (since := "2024-11-21")] alias Smooth.div‚ÇÄ := ContMDiff.div‚ÇÄ

end Div
