/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.LieGroup

/-!
# `C^n` structures

In this file we define `C^n` structures that build on Lie groups. We prefer using the
term `ContMDiffRing` instead of Lie mainly because Lie ring has currently another use
in mathematics.
-/

open scoped Manifold ContDiff

section ContMDiffRing

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {H : Type*} [TopologicalSpace H] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {n : WithTop â„•âˆ}

-- See note [Design choices about smooth algebraic structures]
/-- A `C^n` (semi)ring is a (semi)ring `R` where addition and multiplication are `C^n`.
If `R` is a ring, then negation is automatically `C^n`, as it is multiplication with `-1`. -/
class ContMDiffRing (I : ModelWithCorners ğ•œ E H) (n : WithTop â„•âˆ)
    (R : Type*) [Semiring R] [TopologicalSpace R] [ChartedSpace H R] : Prop
    extends ContMDiffAdd I n R where
  contMDiff_mul : ContMDiff (I.prod I) I n fun p : R Ã— R => p.1 * p.2

@[deprecated (since := "2025-01-09")] alias SmoothRing := ContMDiffRing

-- see Note [lower instance priority]
instance (priority := 100) ContMDiffRing.toContMDiffMul (I : ModelWithCorners ğ•œ E H) (R : Type*)
    [Semiring R] [TopologicalSpace R] [ChartedSpace H R] [h : ContMDiffRing I n R] :
    ContMDiffMul I n R :=
  { h with }

-- see Note [lower instance priority]
instance (priority := 100) ContMDiffRing.toLieAddGroup (I : ModelWithCorners ğ•œ E H) (R : Type*)
    [Ring R] [TopologicalSpace R] [ChartedSpace H R] [ContMDiffRing I n R] : LieAddGroup I n R where
  compatible := StructureGroupoid.compatible (contDiffGroupoid n I)
  contMDiff_add := contMDiff_add I n
  contMDiff_neg := by simpa only [neg_one_mul] using contMDiff_mul_left (G := R) (a := -1)

end ContMDiffRing

-- see Note [lower instance priority]
instance (priority := 100) instFieldContMDiffRing
    {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ} :
    ContMDiffRing ğ“˜(ğ•œ) n ğ•œ :=
  { instNormedSpaceLieAddGroup with
    contMDiff_mul := by
      rw [contMDiff_iff]
      refine âŸ¨continuous_mul, fun x y => ?_âŸ©
      simp only [mfld_simps]
      rw [contDiffOn_univ]
      exact contDiff_mul }

variable {ğ•œ R E H : Type*} [TopologicalSpace R] [TopologicalSpace H] [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [ChartedSpace H R] (I : ModelWithCorners ğ•œ E H)
  (n : WithTop â„•âˆ)

/-- A `C^n` (semi)ring is a topological (semi)ring. This is not an instance for technical reasons,
see note [Design choices about smooth algebraic structures]. -/
theorem topologicalSemiring_of_contMDiffRing [Semiring R] [ContMDiffRing I n R] :
    IsTopologicalSemiring R :=
  { continuousMul_of_contMDiffMul I n, continuousAdd_of_contMDiffAdd I n with }

@[deprecated (since := "2025-01-09")]
alias topologicalSemiring_of_smooth := topologicalSemiring_of_contMDiffRing
