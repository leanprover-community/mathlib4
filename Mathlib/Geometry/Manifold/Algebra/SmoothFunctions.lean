/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.Structures

#align_import geometry.manifold.algebra.smooth_functions from "leanprover-community/mathlib"@"e5ab837fc252451f3eb9124ae6e7b6f57455e7b9"

/-!
# Algebraic structures over smooth functions

In this file, we define instances of algebraic structures over smooth functions.
-/


noncomputable section

open scoped Manifold

open TopologicalSpace

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H : Type*}
  [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {N : Type*} [TopologicalSpace N] [ChartedSpace H N]
  {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] {H'' : Type*} [TopologicalSpace H'']
  {I'' : ModelWithCorners ğ•œ E'' H''} {N' : Type*} [TopologicalSpace N'] [ChartedSpace H'' N']

namespace SmoothMap

@[to_additive]
protected instance instMul {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothMul I' G] : Mul C^âˆâŸ®I, N; I', GâŸ¯ :=
  âŸ¨fun f g => âŸ¨f * g, f.smooth.mul g.smoothâŸ©âŸ©
#align smooth_map.has_mul SmoothMap.instMul
#align smooth_map.has_add SmoothMap.instAdd

@[to_additive (attr := simp)]
theorem coe_mul {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H' G] [SmoothMul I' G]
    (f g : C^âˆâŸ®I, N; I', GâŸ¯) : â‡‘(f * g) = f * g :=
  rfl
#align smooth_map.coe_mul SmoothMap.coe_mul
#align smooth_map.coe_add SmoothMap.coe_add

@[to_additive (attr := simp)]
theorem mul_comp {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H' G] [SmoothMul I' G]
    (f g : C^âˆâŸ®I'', N'; I', GâŸ¯) (h : C^âˆâŸ®I, N; I'', N'âŸ¯) : (f * g).comp h = f.comp h * g.comp h :=
  rfl
#align smooth_map.mul_comp SmoothMap.mul_comp
#align smooth_map.add_comp SmoothMap.add_comp

@[to_additive]
protected instance instOne {G : Type*} [One G] [TopologicalSpace G] [ChartedSpace H' G] :
    One C^âˆâŸ®I, N; I', GâŸ¯ :=
  âŸ¨ContMDiffMap.const (1 : G)âŸ©
#align smooth_map.has_one SmoothMap.instOne
#align smooth_map.has_zero SmoothMap.instZero

@[to_additive (attr := simp)]
theorem coe_one {G : Type*} [One G] [TopologicalSpace G] [ChartedSpace H' G] :
    â‡‘(1 : C^âˆâŸ®I, N; I', GâŸ¯) = 1 :=
  rfl
#align smooth_map.coe_one SmoothMap.coe_one
#align smooth_map.coe_zero SmoothMap.coe_zero

instance instNSMul {G : Type*} [AddMonoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothAdd I' G] : SMul â„• C^âˆâŸ®I, N; I', GâŸ¯ where
  smul n f := âŸ¨n â€¢ (f : N â†’ G), (smooth_nsmul n).comp f.smoothâŸ©

@[to_additive existing]
instance instPow {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G] [SmoothMul I' G] :
    Pow C^âˆâŸ®I, N; I', GâŸ¯ â„• where
  pow f n := âŸ¨(f : N â†’ G) ^ n, (smooth_pow n).comp f.smoothâŸ©

@[to_additive (attr := simp)]
theorem coe_pow {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G] [SmoothMul I' G]
    (f : C^âˆâŸ®I, N; I', GâŸ¯) (n : â„•) :
    â‡‘(f ^ n) = (f : N â†’ G) ^ n :=
  rfl

section GroupStructure

/-!
### Group structure

In this section we show that smooth functions valued in a Lie group inherit a group structure
under pointwise multiplication.
-/

@[to_additive]
instance semigroup {G : Type*} [Semigroup G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothMul I' G] : Semigroup C^âˆâŸ®I, N; I', GâŸ¯ :=
  DFunLike.coe_injective.semigroup _ coe_mul
#align smooth_map.semigroup SmoothMap.semigroup
#align smooth_map.add_semigroup SmoothMap.addSemigroup

@[to_additive]
instance monoid {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothMul I' G] : Monoid C^âˆâŸ®I, N; I', GâŸ¯ :=
  DFunLike.coe_injective.monoid _ coe_one coe_mul coe_pow
#align smooth_map.monoid SmoothMap.monoid
#align smooth_map.add_monoid SmoothMap.addMonoid

/-- Coercion to a function as a `MonoidHom`. Similar to `MonoidHom.coeFn`. -/
@[to_additive (attr := simps) "Coercion to a function as an `AddMonoidHom`.
  Similar to `AddMonoidHom.coeFn`."]
def coeFnMonoidHom {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothMul I' G] : C^âˆâŸ®I, N; I', GâŸ¯ â†’* N â†’ G where
  toFun := DFunLike.coe
  map_one' := coe_one
  map_mul' := coe_mul
#align smooth_map.coe_fn_monoid_hom SmoothMap.coeFnMonoidHom
#align smooth_map.coe_fn_add_monoid_hom SmoothMap.coeFnAddMonoidHom

variable (I N)

/-- For a manifold `N` and a smooth homomorphism `Ï†` between Lie groups `G'`, `G''`, the
'left-composition-by-`Ï†`' group homomorphism from `C^âˆâŸ®I, N; I', G'âŸ¯` to `C^âˆâŸ®I, N; I'', G''âŸ¯`. -/
@[to_additive "For a manifold `N` and a smooth homomorphism `Ï†` between additive Lie groups `G'`,
`G''`, the 'left-composition-by-`Ï†`' group homomorphism from `C^âˆâŸ®I, N; I', G'âŸ¯` to
`C^âˆâŸ®I, N; I'', G''âŸ¯`."]
def compLeftMonoidHom {G' : Type*} [Monoid G'] [TopologicalSpace G'] [ChartedSpace H' G']
    [SmoothMul I' G'] {G'' : Type*} [Monoid G''] [TopologicalSpace G''] [ChartedSpace H'' G'']
    [SmoothMul I'' G''] (Ï† : G' â†’* G'') (hÏ† : Smooth I' I'' Ï†) :
    C^âˆâŸ®I, N; I', G'âŸ¯ â†’* C^âˆâŸ®I, N; I'', G''âŸ¯ where
  toFun f := âŸ¨Ï† âˆ˜ f, fun x => (hÏ†.smooth _).comp x (f.contMDiff x)âŸ©
  map_one' := by ext; show Ï† 1 = 1; simp
  map_mul' f g := by ext x; show Ï† (f x * g x) = Ï† (f x) * Ï† (g x); simp
#align smooth_map.comp_left_monoid_hom SmoothMap.compLeftMonoidHom
#align smooth_map.comp_left_add_monoid_hom SmoothMap.compLeftAddMonoidHom

variable (I') {N}

-- Porting note (#11215): TODO: generalize to any smooth map instead of `Set.inclusion`
/-- For a Lie group `G` and open sets `U âŠ† V` in `N`, the 'restriction' group homomorphism from
`C^âˆâŸ®I, V; I', GâŸ¯` to `C^âˆâŸ®I, U; I', GâŸ¯`. -/
@[to_additive "For an additive Lie group `G` and open sets `U âŠ† V` in `N`, the 'restriction' group
homomorphism from `C^âˆâŸ®I, V; I', GâŸ¯` to `C^âˆâŸ®I, U; I', GâŸ¯`."]
def restrictMonoidHom (G : Type*) [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothMul I' G] {U V : Opens N} (h : U â‰¤ V) : C^âˆâŸ®I, V; I', GâŸ¯ â†’* C^âˆâŸ®I, U; I', GâŸ¯ where
  toFun f := âŸ¨f âˆ˜ Set.inclusion h, f.smooth.comp (smooth_inclusion h)âŸ©
  map_one' := rfl
  map_mul' _ _ := rfl
#align smooth_map.restrict_monoid_hom SmoothMap.restrictMonoidHom
#align smooth_map.restrict_add_monoid_hom SmoothMap.restrictAddMonoidHom

variable {I I'}

@[to_additive]
instance commMonoid {G : Type*} [CommMonoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [SmoothMul I' G] : CommMonoid C^âˆâŸ®I, N; I', GâŸ¯ :=
  DFunLike.coe_injective.commMonoid _ coe_one coe_mul coe_pow
#align smooth_map.comm_monoid SmoothMap.commMonoid
#align smooth_map.add_comm_monoid SmoothMap.addCommMonoid

@[to_additive]
instance group {G : Type*} [Group G] [TopologicalSpace G] [ChartedSpace H' G] [LieGroup I' G] :
    Group C^âˆâŸ®I, N; I', GâŸ¯ :=
  { SmoothMap.monoid with
    inv := fun f => âŸ¨fun x => (f x)â»Â¹, f.smooth.invâŸ©
    mul_left_inv := fun a => by ext; exact mul_left_inv _
    div := fun f g => âŸ¨f / g, f.smooth.div g.smoothâŸ©
    div_eq_mul_inv := fun f g => by ext; exact div_eq_mul_inv _ _ }
#align smooth_map.group SmoothMap.group
#align smooth_map.add_group SmoothMap.addGroup

@[to_additive (attr := simp)]
theorem coe_inv {G : Type*} [Group G] [TopologicalSpace G] [ChartedSpace H' G] [LieGroup I' G]
    (f : C^âˆâŸ®I, N; I', GâŸ¯) : â‡‘fâ»Â¹ = (â‡‘f)â»Â¹ :=
  rfl
#align smooth_map.coe_inv SmoothMap.coe_inv
#align smooth_map.coe_neg SmoothMap.coe_neg

@[to_additive (attr := simp)]
theorem coe_div {G : Type*} [Group G] [TopologicalSpace G] [ChartedSpace H' G] [LieGroup I' G]
    (f g : C^âˆâŸ®I, N; I', GâŸ¯) : â‡‘(f / g) = f / g :=
  rfl
#align smooth_map.coe_div SmoothMap.coe_div
#align smooth_map.coe_sub SmoothMap.coe_sub

@[to_additive]
instance commGroup {G : Type*} [CommGroup G] [TopologicalSpace G] [ChartedSpace H' G]
    [LieGroup I' G] : CommGroup C^âˆâŸ®I, N; I', GâŸ¯ :=
  { SmoothMap.group, SmoothMap.commMonoid with }
#align smooth_map.comm_group SmoothMap.commGroup
#align smooth_map.add_comm_group SmoothMap.addCommGroup

end GroupStructure

section RingStructure

/-!
### Ring stucture

In this section we show that smooth functions valued in a smooth ring `R` inherit a ring structure
under pointwise multiplication.
-/


instance semiring {R : Type*} [Semiring R] [TopologicalSpace R] [ChartedSpace H' R]
    [SmoothRing I' R] : Semiring C^âˆâŸ®I, N; I', RâŸ¯ :=
  { SmoothMap.addCommMonoid,
    SmoothMap.monoid with
    left_distrib := fun a b c => by ext; exact left_distrib _ _ _
    right_distrib := fun a b c => by ext; exact right_distrib _ _ _
    zero_mul := fun a => by ext; exact zero_mul _
    mul_zero := fun a => by ext; exact mul_zero _ }
#align smooth_map.semiring SmoothMap.semiring

instance ring {R : Type*} [Ring R] [TopologicalSpace R] [ChartedSpace H' R] [SmoothRing I' R] :
    Ring C^âˆâŸ®I, N; I', RâŸ¯ :=
  { SmoothMap.semiring, SmoothMap.addCommGroup with }
#align smooth_map.ring SmoothMap.ring

instance commRing {R : Type*} [CommRing R] [TopologicalSpace R] [ChartedSpace H' R]
    [SmoothRing I' R] : CommRing C^âˆâŸ®I, N; I', RâŸ¯ :=
  { SmoothMap.semiring, SmoothMap.addCommGroup, SmoothMap.commMonoid with }
#align smooth_map.comm_ring SmoothMap.commRing

variable (I N)

/-- For a manifold `N` and a smooth homomorphism `Ï†` between smooth rings `R'`, `R''`, the
'left-composition-by-`Ï†`' ring homomorphism from `C^âˆâŸ®I, N; I', R'âŸ¯` to `C^âˆâŸ®I, N; I'', R''âŸ¯`. -/
def compLeftRingHom {R' : Type*} [Ring R'] [TopologicalSpace R'] [ChartedSpace H' R']
    [SmoothRing I' R'] {R'' : Type*} [Ring R''] [TopologicalSpace R''] [ChartedSpace H'' R'']
    [SmoothRing I'' R''] (Ï† : R' â†’+* R'') (hÏ† : Smooth I' I'' Ï†) :
    C^âˆâŸ®I, N; I', R'âŸ¯ â†’+* C^âˆâŸ®I, N; I'', R''âŸ¯ :=
  { SmoothMap.compLeftMonoidHom I N Ï†.toMonoidHom hÏ†,
    SmoothMap.compLeftAddMonoidHom I N Ï†.toAddMonoidHom hÏ† with
    toFun := fun f => âŸ¨Ï† âˆ˜ f, fun x => (hÏ†.smooth _).comp x (f.contMDiff x)âŸ© }
#align smooth_map.comp_left_ring_hom SmoothMap.compLeftRingHom

variable (I') {N}

/-- For a "smooth ring" `R` and open sets `U âŠ† V` in `N`, the "restriction" ring homomorphism from
`C^âˆâŸ®I, V; I', RâŸ¯` to `C^âˆâŸ®I, U; I', RâŸ¯`. -/
def restrictRingHom (R : Type*) [Ring R] [TopologicalSpace R] [ChartedSpace H' R] [SmoothRing I' R]
    {U V : Opens N} (h : U â‰¤ V) : C^âˆâŸ®I, V; I', RâŸ¯ â†’+* C^âˆâŸ®I, U; I', RâŸ¯ :=
  { SmoothMap.restrictMonoidHom I I' R h, SmoothMap.restrictAddMonoidHom I I' R h with
    toFun := fun f => âŸ¨f âˆ˜ Set.inclusion h, f.smooth.comp (smooth_inclusion h)âŸ© }
#align smooth_map.restrict_ring_hom SmoothMap.restrictRingHom

variable {I I'}

/-- Coercion to a function as a `RingHom`. -/
@[simps]
def coeFnRingHom {R : Type*} [CommRing R] [TopologicalSpace R] [ChartedSpace H' R]
    [SmoothRing I' R] : C^âˆâŸ®I, N; I', RâŸ¯ â†’+* N â†’ R :=
  { (coeFnMonoidHom : C^âˆâŸ®I, N; I', RâŸ¯ â†’* _), (coeFnAddMonoidHom : C^âˆâŸ®I, N; I', RâŸ¯ â†’+ _) with
    toFun := (â†‘) }
#align smooth_map.coe_fn_ring_hom SmoothMap.coeFnRingHom

/-- `Function.eval` as a `RingHom` on the ring of smooth functions. -/
def evalRingHom {R : Type*} [CommRing R] [TopologicalSpace R] [ChartedSpace H' R] [SmoothRing I' R]
    (n : N) : C^âˆâŸ®I, N; I', RâŸ¯ â†’+* R :=
  (Pi.evalRingHom _ n : (N â†’ R) â†’+* R).comp SmoothMap.coeFnRingHom
#align smooth_map.eval_ring_hom SmoothMap.evalRingHom

end RingStructure

section ModuleStructure

/-!
### Semimodule stucture

In this section we show that smooth functions valued in a vector space `M` over a normed
field `ğ•œ` inherit a vector space structure.
-/


instance instSMul {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    SMul ğ•œ C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ :=
  âŸ¨fun r f => âŸ¨r â€¢ â‡‘f, smooth_const.smul f.smoothâŸ©âŸ©
#align smooth_map.has_smul SmoothMap.instSMul

@[simp]
theorem coe_smul {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] (r : ğ•œ)
    (f : C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯) : â‡‘(r â€¢ f) = r â€¢ â‡‘f :=
  rfl
#align smooth_map.coe_smul SmoothMap.coe_smul

@[simp]
theorem smul_comp {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] (r : ğ•œ)
    (g : C^âˆâŸ®I'', N'; ğ“˜(ğ•œ, V), VâŸ¯) (h : C^âˆâŸ®I, N; I'', N'âŸ¯) : (r â€¢ g).comp h = r â€¢ g.comp h :=
  rfl
#align smooth_map.smul_comp SmoothMap.smul_comp

instance module {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    Module ğ•œ C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ :=
  Function.Injective.module ğ•œ coeFnAddMonoidHom ContMDiffMap.coe_injective coe_smul
#align smooth_map.module SmoothMap.module

/-- Coercion to a function as a `LinearMap`. -/
@[simps]
def coeFnLinearMap {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ â†’â‚—[ğ•œ] N â†’ V :=
  { (coeFnAddMonoidHom : C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ â†’+ _) with
    toFun := (â†‘)
    map_smul' := coe_smul }
#align smooth_map.coe_fn_linear_map SmoothMap.coeFnLinearMap

end ModuleStructure

section AlgebraStructure

/-!
### Algebra structure

In this section we show that smooth functions valued in a normed algebra `A` over a normed field `ğ•œ`
inherit an algebra structure.
-/


variable {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A] [SmoothRing ğ“˜(ğ•œ, A) A]

/-- Smooth constant functions as a `RingHom`. -/
def C : ğ•œ â†’+* C^âˆâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ where
  toFun := fun c : ğ•œ => âŸ¨fun _ => (algebraMap ğ•œ A) c, smooth_constâŸ©
  map_one' := by ext; exact (algebraMap ğ•œ A).map_one
  map_mul' câ‚ câ‚‚ := by ext; exact (algebraMap ğ•œ A).map_mul _ _
  map_zero' := by ext; exact (algebraMap ğ•œ A).map_zero
  map_add' câ‚ câ‚‚ := by ext; exact (algebraMap ğ•œ A).map_add _ _
set_option linter.uppercaseLean3 false in
#align smooth_map.C SmoothMap.C

instance algebra : Algebra ğ•œ C^âˆâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ :=
  { --SmoothMap.semiring with -- Porting note: Commented this out.
    smul := fun r f => âŸ¨r â€¢ f, smooth_const.smul f.smoothâŸ©
    toRingHom := SmoothMap.C
    commutes' := fun c f => by ext x; exact Algebra.commutes' _ _
    smul_def' := fun c f => by ext x; exact Algebra.smul_def' _ _ }
#align smooth_map.algebra SmoothMap.algebra

/-- Coercion to a function as an `AlgHom`. -/
@[simps]
def coeFnAlgHom : C^âˆâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ â†’â‚[ğ•œ] N â†’ A where
  toFun := (â†‘)
  commutes' _ := rfl
  -- `(SmoothMap.coeFnRingHom : C^âˆâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ â†’+* _) with` times out for some reason
  map_zero' := SmoothMap.coe_zero
  map_one' := SmoothMap.coe_one
  map_add' := SmoothMap.coe_add
  map_mul' := SmoothMap.coe_mul
#align smooth_map.coe_fn_alg_hom SmoothMap.coeFnAlgHom

end AlgebraStructure

section ModuleOverContinuousFunctions

/-!
### Structure as module over scalar functions

If `V` is a module over `ğ•œ`, then we show that the space of smooth functions from `N` to `V`
is naturally a vector space over the ring of smooth functions from `N` to `ğ•œ`. -/


instance instSMul' {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    SMul C^âˆâŸ®I, N; ğ•œâŸ¯ C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ :=
  âŸ¨fun f g => âŸ¨fun x => f x â€¢ g x, Smooth.smul f.2 g.2âŸ©âŸ©
#align smooth_map.has_smul' SmoothMap.instSMul'

@[simp]
theorem smul_comp' {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] (f : C^âˆâŸ®I'', N'; ğ•œâŸ¯)
    (g : C^âˆâŸ®I'', N'; ğ“˜(ğ•œ, V), VâŸ¯) (h : C^âˆâŸ®I, N; I'', N'âŸ¯) :
    (f â€¢ g).comp h = f.comp h â€¢ g.comp h :=
  rfl
#align smooth_map.smul_comp' SmoothMap.smul_comp'

instance module' {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    Module C^âˆâŸ®I, N; ğ“˜(ğ•œ), ğ•œâŸ¯ C^âˆâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ where
  smul := (Â· â€¢ Â·)
  smul_add c f g := by ext x; exact smul_add (c x) (f x) (g x)
  add_smul câ‚ câ‚‚ f := by ext x; exact add_smul (câ‚ x) (câ‚‚ x) (f x)
  mul_smul câ‚ câ‚‚ f := by ext x; exact mul_smul (câ‚ x) (câ‚‚ x) (f x)
  one_smul f := by ext x; exact one_smul ğ•œ (f x)
  zero_smul f := by ext x; exact zero_smul _ _
  smul_zero r := by ext x; exact smul_zero _
#align smooth_map.module' SmoothMap.module'

end ModuleOverContinuousFunctions

end SmoothMap
