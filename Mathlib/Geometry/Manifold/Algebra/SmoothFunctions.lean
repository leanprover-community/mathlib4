/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Geometry.Manifold.Algebra.Structures

/-!
# Algebraic structures over `C^n` functions

In this file, we define instances of algebraic structures over `C^n` functions.
-/


noncomputable section

open scoped Manifold ContDiff

open TopologicalSpace

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H : Type*}
  [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {N : Type*} [TopologicalSpace N] [ChartedSpace H N]
  {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] {H'' : Type*} [TopologicalSpace H'']
  {I'' : ModelWithCorners ğ•œ E'' H''} {N' : Type*} [TopologicalSpace N'] [ChartedSpace H'' N']
  {n : WithTop â„•âˆ}

namespace ContMDiffMap

@[to_additive]
protected instance instMul {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] : Mul C^nâŸ®I, N; I', GâŸ¯ :=
  âŸ¨fun f g => âŸ¨f * g, f.contMDiff.mul g.contMDiffâŸ©âŸ©

@[to_additive (attr := simp)]
theorem coe_mul {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H' G] [ContMDiffMul I' n G]
    (f g : C^nâŸ®I, N; I', GâŸ¯) : â‡‘(f * g) = f * g :=
  rfl

@[to_additive (attr := simp)]
theorem mul_comp {G : Type*} [Mul G] [TopologicalSpace G] [ChartedSpace H' G] [ContMDiffMul I' n G]
    (f g : C^nâŸ®I'', N'; I', GâŸ¯) (h : C^nâŸ®I, N; I'', N'âŸ¯) : (f * g).comp h = f.comp h * g.comp h :=
  rfl

@[to_additive]
protected instance instOne {G : Type*} [One G] [TopologicalSpace G] [ChartedSpace H' G] :
    One C^nâŸ®I, N; I', GâŸ¯ :=
  âŸ¨ContMDiffMap.const (1 : G)âŸ©

@[to_additive (attr := simp)]
theorem coe_one {G : Type*} [One G] [TopologicalSpace G] [ChartedSpace H' G] :
    â‡‘(1 : C^nâŸ®I, N; I', GâŸ¯) = 1 :=
  rfl

instance instNSMul {G : Type*} [AddMonoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffAdd I' n G] : SMul â„• C^nâŸ®I, N; I', GâŸ¯ where
  smul n f := âŸ¨n â€¢ (f : N â†’ G), (contMDiff_nsmul n).comp f.contMDiffâŸ©

@[to_additive existing]
instance instPow {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] :
    Pow C^nâŸ®I, N; I', GâŸ¯ â„• where
  pow f n := âŸ¨(f : N â†’ G) ^ n, (contMDiff_pow n).comp f.contMDiffâŸ©

@[to_additive (attr := simp)]
theorem coe_pow {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] (f : C^nâŸ®I, N; I', GâŸ¯) (n : â„•) :
    â‡‘(f ^ n) = (f : N â†’ G) ^ n :=
  rfl

section GroupStructure

/-!
### Group structure

In this section we show that `C^n` functions valued in a Lie group inherit a group structure
under pointwise multiplication.
-/

@[to_additive]
instance semigroup {G : Type*} [Semigroup G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] : Semigroup C^nâŸ®I, N; I', GâŸ¯ :=
  DFunLike.coe_injective.semigroup _ coe_mul

@[to_additive]
instance monoid {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] : Monoid C^nâŸ®I, N; I', GâŸ¯ :=
  DFunLike.coe_injective.monoid _ coe_one coe_mul coe_pow

/-- Coercion to a function as a `MonoidHom`. Similar to `MonoidHom.coeFn`. -/
@[to_additive (attr := simps) /-- Coercion to a function as an `AddMonoidHom`.
  Similar to `AddMonoidHom.coeFn`. -/]
def coeFnMonoidHom {G : Type*} [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] : C^nâŸ®I, N; I', GâŸ¯ â†’* N â†’ G where
  toFun := DFunLike.coe
  map_one' := coe_one
  map_mul' := coe_mul

variable (I N)

/-- For a manifold `N` and a `C^n` homomorphism `Ï†` between Lie groups `G'`, `G''`, the
'left-composition-by-`Ï†`' group homomorphism from `C^nâŸ®I, N; I', G'âŸ¯` to `C^nâŸ®I, N; I'', G''âŸ¯`. -/
@[to_additive /-- For a manifold `N` and a `C^n` homomorphism `Ï†` between additive Lie groups `G'`,
`G''`, the 'left-composition-by-`Ï†`' group homomorphism from `C^nâŸ®I, N; I', G'âŸ¯` to
`C^nâŸ®I, N; I'', G''âŸ¯`. -/]
def compLeftMonoidHom {G' : Type*} [Monoid G'] [TopologicalSpace G'] [ChartedSpace H' G']
    [ContMDiffMul I' n G'] {G'' : Type*} [Monoid G''] [TopologicalSpace G''] [ChartedSpace H'' G'']
    [ContMDiffMul I'' n G''] (Ï† : G' â†’* G'') (hÏ† : ContMDiff I' I'' n Ï†) :
    C^nâŸ®I, N; I', G'âŸ¯ â†’* C^nâŸ®I, N; I'', G''âŸ¯ where
  toFun f := âŸ¨Ï† âˆ˜ f, hÏ†.comp f.contMDiffâŸ©
  map_one' := by ext; change Ï† 1 = 1; simp
  map_mul' f g := by ext x; change Ï† (f x * g x) = Ï† (f x) * Ï† (g x); simp

variable (I') {N}

-- TODO: generalize to any `C^n` map instead of `Set.inclusion`
/-- For a Lie group `G` and open sets `U âŠ† V` in `N`, the 'restriction' group homomorphism from
`C^nâŸ®I, V; I', GâŸ¯` to `C^nâŸ®I, U; I', GâŸ¯`. -/
@[to_additive /-- For an additive Lie group `G` and open sets `U âŠ† V` in `N`, the 'restriction'
group homomorphism from `C^nâŸ®I, V; I', GâŸ¯` to `C^nâŸ®I, U; I', GâŸ¯`. -/]
def restrictMonoidHom (G : Type*) [Monoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] {U V : Opens N} (h : U â‰¤ V) : C^nâŸ®I, V; I', GâŸ¯ â†’* C^nâŸ®I, U; I', GâŸ¯ where
  toFun f := âŸ¨f âˆ˜ Set.inclusion h, f.contMDiff.comp (contMDiff_inclusion h)âŸ©
  map_one' := rfl
  map_mul' _ _ := rfl

variable {I I'}

@[to_additive]
instance commMonoid {G : Type*} [CommMonoid G] [TopologicalSpace G] [ChartedSpace H' G]
    [ContMDiffMul I' n G] : CommMonoid C^nâŸ®I, N; I', GâŸ¯ :=
  DFunLike.coe_injective.commMonoid _ coe_one coe_mul coe_pow

@[to_additive]
instance group {G : Type*} [Group G] [TopologicalSpace G] [ChartedSpace H' G] [LieGroup I' n G] :
    Group C^nâŸ®I, N; I', GâŸ¯ :=
  { ContMDiffMap.monoid with
    inv := fun f => âŸ¨fun x => (f x)â»Â¹, f.contMDiff.invâŸ©
    inv_mul_cancel := fun a => by ext; exact inv_mul_cancel _
    div := fun f g => âŸ¨f / g, f.contMDiff.div g.contMDiffâŸ©
    div_eq_mul_inv := fun f g => by ext; exact div_eq_mul_inv _ _ }

@[to_additive (attr := simp)]
theorem coe_inv {G : Type*} [Group G] [TopologicalSpace G] [ChartedSpace H' G] [LieGroup I' n G]
    (f : C^nâŸ®I, N; I', GâŸ¯) : â‡‘fâ»Â¹ = (â‡‘f)â»Â¹ :=
  rfl

@[to_additive (attr := simp)]
theorem coe_div {G : Type*} [Group G] [TopologicalSpace G] [ChartedSpace H' G] [LieGroup I' n G]
    (f g : C^nâŸ®I, N; I', GâŸ¯) : â‡‘(f / g) = f / g :=
  rfl

@[to_additive]
instance commGroup {G : Type*} [CommGroup G] [TopologicalSpace G] [ChartedSpace H' G]
    [LieGroup I' n G] : CommGroup C^nâŸ®I, N; I', GâŸ¯ :=
  { ContMDiffMap.group, ContMDiffMap.commMonoid with }

end GroupStructure

section RingStructure

/-!
### Ring structure

In this section we show that `C^n` functions valued in a `C^n` ring `R` inherit a ring structure
under pointwise multiplication.
-/


instance semiring {R : Type*} [Semiring R] [TopologicalSpace R] [ChartedSpace H' R]
    [ContMDiffRing I' n R] : Semiring C^nâŸ®I, N; I', RâŸ¯ :=
  { ContMDiffMap.addCommMonoid,
    ContMDiffMap.monoid with
    left_distrib := fun a b c => by ext; exact left_distrib _ _ _
    right_distrib := fun a b c => by ext; exact right_distrib _ _ _
    zero_mul := fun a => by ext; exact zero_mul _
    mul_zero := fun a => by ext; exact mul_zero _ }

instance ring {R : Type*} [Ring R] [TopologicalSpace R] [ChartedSpace H' R] [ContMDiffRing I' n R] :
    Ring C^nâŸ®I, N; I', RâŸ¯ :=
  { ContMDiffMap.semiring, ContMDiffMap.addCommGroup with }

instance commRing {R : Type*} [CommRing R] [TopologicalSpace R] [ChartedSpace H' R]
    [ContMDiffRing I' n R] : CommRing C^nâŸ®I, N; I', RâŸ¯ :=
  { ContMDiffMap.semiring, ContMDiffMap.addCommGroup, ContMDiffMap.commMonoid with }

variable (I N)

/-- For a manifold `N` and a `C^n` homomorphism `Ï†` between `C^n` rings `R'`, `R''`, the
'left-composition-by-`Ï†`' ring homomorphism from `C^nâŸ®I, N; I', R'âŸ¯` to `C^nâŸ®I, N; I'', R''âŸ¯`. -/
def compLeftRingHom {R' : Type*} [Ring R'] [TopologicalSpace R'] [ChartedSpace H' R']
    [ContMDiffRing I' n R'] {R'' : Type*} [Ring R''] [TopologicalSpace R''] [ChartedSpace H'' R'']
    [ContMDiffRing I'' n R''] (Ï† : R' â†’+* R'') (hÏ† : ContMDiff I' I'' n Ï†) :
    C^nâŸ®I, N; I', R'âŸ¯ â†’+* C^nâŸ®I, N; I'', R''âŸ¯ :=
  { ContMDiffMap.compLeftMonoidHom I N Ï†.toMonoidHom hÏ†,
    ContMDiffMap.compLeftAddMonoidHom I N Ï†.toAddMonoidHom hÏ† with
    toFun := fun f => âŸ¨Ï† âˆ˜ f, hÏ†.comp f.contMDiffâŸ© }

variable (I') {N}

/-- For a "`C^n` ring" `R` and open sets `U âŠ† V` in `N`, the "restriction" ring homomorphism from
`C^nâŸ®I, V; I', RâŸ¯` to `C^nâŸ®I, U; I', RâŸ¯`. -/
def restrictRingHom (R : Type*) [Ring R] [TopologicalSpace R] [ChartedSpace H' R]
    [ContMDiffRing I' n R] {U V : Opens N} (h : U â‰¤ V) :
    C^nâŸ®I, V; I', RâŸ¯ â†’+* C^nâŸ®I, U; I', RâŸ¯ :=
  { ContMDiffMap.restrictMonoidHom I I' R h, ContMDiffMap.restrictAddMonoidHom I I' R h with
    toFun := fun f => âŸ¨f âˆ˜ Set.inclusion h, f.contMDiff.comp (contMDiff_inclusion h)âŸ© }

variable {I I'}

/-- Coercion to a function as a `RingHom`. -/
@[simps]
def coeFnRingHom {R : Type*} [CommRing R] [TopologicalSpace R] [ChartedSpace H' R]
    [ContMDiffRing I' n R] : C^nâŸ®I, N; I', RâŸ¯ â†’+* N â†’ R :=
  { (coeFnMonoidHom : C^nâŸ®I, N; I', RâŸ¯ â†’* _), (coeFnAddMonoidHom : C^nâŸ®I, N; I', RâŸ¯ â†’+ _) with
    toFun := (â†‘) }

/-- `Function.eval` as a `RingHom` on the ring of `C^n` functions. -/
def evalRingHom {R : Type*} [CommRing R] [TopologicalSpace R] [ChartedSpace H' R]
    [ContMDiffRing I' n R] (m : N) : C^nâŸ®I, N; I', RâŸ¯ â†’+* R :=
  (Pi.evalRingHom _ m : (N â†’ R) â†’+* R).comp ContMDiffMap.coeFnRingHom

end RingStructure

section ModuleStructure

/-!
### Semimodule structure

In this section we show that `C^n` functions valued in a vector space `M` over a normed
field `ğ•œ` inherit a vector space structure.
-/


instance instSMul {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    SMul ğ•œ C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ :=
  âŸ¨fun r f => âŸ¨r â€¢ â‡‘f, contMDiff_const.smul f.contMDiffâŸ©âŸ©

@[simp]
theorem coe_smul {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] (r : ğ•œ)
    (f : C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯) : â‡‘(r â€¢ f) = r â€¢ â‡‘f :=
  rfl

@[simp]
theorem smul_comp {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] (r : ğ•œ)
    (g : C^nâŸ®I'', N'; ğ“˜(ğ•œ, V), VâŸ¯) (h : C^nâŸ®I, N; I'', N'âŸ¯) : (r â€¢ g).comp h = r â€¢ g.comp h :=
  rfl

instance module {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    Module ğ•œ C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ :=
  Function.Injective.module ğ•œ coeFnAddMonoidHom ContMDiffMap.coe_injective coe_smul

/-- Coercion to a function as a `LinearMap`. -/
@[simps]
def coeFnLinearMap {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ â†’â‚—[ğ•œ] N â†’ V :=
  { (coeFnAddMonoidHom : C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ â†’+ _) with
    toFun := (â†‘)
    map_smul' := coe_smul }

end ModuleStructure

section AlgebraStructure

/-!
### Algebra structure

In this section we show that `C^n` functions valued in a normed algebra `A` over a normed field `ğ•œ`
inherit an algebra structure.
-/


variable {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A] [ContMDiffRing ğ“˜(ğ•œ, A) n A]

/-- `C^n` constant functions as a `RingHom`. -/
def C : ğ•œ â†’+* C^nâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ where
  toFun := fun c : ğ•œ => âŸ¨fun _ => (algebraMap ğ•œ A) c, contMDiff_constâŸ©
  map_one' := by ext; exact (algebraMap ğ•œ A).map_one
  map_mul' câ‚ câ‚‚ := by ext; exact (algebraMap ğ•œ A).map_mul _ _
  map_zero' := by ext; exact (algebraMap ğ•œ A).map_zero
  map_add' câ‚ câ‚‚ := by ext; exact (algebraMap ğ•œ A).map_add _ _

instance algebra : Algebra ğ•œ C^nâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ where
  smul := fun r f => âŸ¨r â€¢ f, contMDiff_const.smul f.contMDiffâŸ©
  algebraMap := ContMDiffMap.C
  commutes' := fun c f => by ext x; exact Algebra.commutes' _ _
  smul_def' := fun c f => by ext x; exact Algebra.smul_def' _ _

/-- Coercion to a function as an `AlgHom`. -/
@[simps]
def coeFnAlgHom : C^nâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ â†’â‚[ğ•œ] N â†’ A where
  toFun := (â†‘)
  commutes' _ := rfl
  -- `(ContMDiffMap.coeFnRingHom : C^nâŸ®I, N; ğ“˜(ğ•œ, A), AâŸ¯ â†’+* _) with` times out for some reason
  map_zero' := ContMDiffMap.coe_zero
  map_one' := ContMDiffMap.coe_one
  map_add' := ContMDiffMap.coe_add
  map_mul' := ContMDiffMap.coe_mul

end AlgebraStructure

section ModuleOverContinuousFunctions

/-!
### Structure as module over scalar functions

If `V` is a module over `ğ•œ`, then we show that the space of `C^n` functions from `N` to `V`
is naturally a vector space over the ring of `C^n` functions from `N` to `ğ•œ`. -/

/-- `C^n` scalar-valued functions act by left-multiplication on `C^n` functions. -/
instance instSMul' {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    SMul C^nâŸ®I, N; ğ•œâŸ¯ C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ :=
  âŸ¨fun f g => âŸ¨fun x => f x â€¢ g x, ContMDiff.smul f.2 g.2âŸ©âŸ©

/-- The left multiplication with a `C^n` scalar function commutes with composition. -/
@[simp]
theorem smul_comp' {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] (f : C^nâŸ®I'', N'; ğ•œâŸ¯)
    (g : C^nâŸ®I'', N'; ğ“˜(ğ•œ, V), VâŸ¯) (h : C^nâŸ®I, N; I'', N'âŸ¯) :
    (f â€¢ g).comp h = f.comp h â€¢ g.comp h :=
  rfl

/-- The space of `C^n` functions with values in a space `V` is a module over the space of `C^n`
functions with values in `ğ•œ`. -/
instance module' {V : Type*} [NormedAddCommGroup V] [NormedSpace ğ•œ V] :
    Module C^nâŸ®I, N; ğ“˜(ğ•œ), ğ•œâŸ¯ C^nâŸ®I, N; ğ“˜(ğ•œ, V), VâŸ¯ where
  smul_add c f g := by ext x; exact smul_add (c x) (f x) (g x)
  add_smul câ‚ câ‚‚ f := by ext x; exact add_smul (câ‚ x) (câ‚‚ x) (f x)
  mul_smul câ‚ câ‚‚ f := by ext x; exact mul_smul (câ‚ x) (câ‚‚ x) (f x)
  one_smul f := by ext x; exact one_smul ğ•œ (f x)
  zero_smul f := by ext x; exact zero_smul _ _
  smul_zero r := by ext x; exact smul_zero _

end ModuleOverContinuousFunctions

end ContMDiffMap
