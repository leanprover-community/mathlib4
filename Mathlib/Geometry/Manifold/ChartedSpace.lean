/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Init.Align
import Mathlib.Topology.PartialHomeomorph

#align_import geometry.manifold.charted_space from "leanprover-community/mathlib"@"431589bce478b2229eba14b14a283250428217db"

/-!
# Charted spaces

A smooth manifold is a topological space `M` locally modelled on a euclidean space (or a euclidean
half-space for manifolds with boundaries, or an infinite dimensional vector space for more general
notions of manifolds), i.e., the manifold is covered by open subsets on which there are local
homeomorphisms (the charts) going to a model space `H`, and the changes of charts should be smooth
maps.

In this file, we introduce a general framework describing these notions, where the model space is an
arbitrary topological space. We avoid the word *manifold*, which should be reserved for the
situation where the model space is a (subset of a) vector space, and use the terminology
*charted space* instead.

If the changes of charts satisfy some additional property (for instance if they are smooth), then
`M` inherits additional structure (it makes sense to talk about smooth manifolds). There are
therefore two different ingredients in a charted space:
* the set of charts, which is data
* the fact that changes of charts belong to some group (in fact groupoid), which is additional Prop.

We separate these two parts in the definition: the charted space structure is just the set of
charts, and then the different smoothness requirements (smooth manifold, orientable manifold,
contact manifold, and so on) are additional properties of these charts. These properties are
formalized through the notion of structure groupoid, i.e., a set of partial homeomorphisms stable
under composition and inverse, to which the change of coordinates should belong.

## Main definitions

* `StructureGroupoid H` : a subset of partial homeomorphisms of `H` stable under composition,
  inverse and restriction (ex: partial diffeomorphisms).
* `continuousGroupoid H` : the groupoid of all partial homeomorphisms of `H`.
* `ChartedSpace H M` : charted space structure on `M` modelled on `H`, given by an atlas of
  partial homeomorphisms from `M` to `H` whose sources cover `M`. This is a type class.
* `HasGroupoid M G` : when `G` is a structure groupoid on `H` and `M` is a charted space
  modelled on `H`, require that all coordinate changes belong to `G`. This is a type class.
* `atlas H M` : when `M` is a charted space modelled on `H`, the atlas of this charted
  space structure, i.e., the set of charts.
* `G.maximalAtlas M` : when `M` is a charted space modelled on `H` and admitting `G` as a
  structure groupoid, one can consider all the partial homeomorphisms from `M` to `H` such that
  changing coordinate from any chart to them belongs to `G`. This is a larger atlas, called the
  maximal atlas (for the groupoid `G`).
* `Structomorph G M M'` : the type of diffeomorphisms between the charted spaces `M` and `M'` for
  the groupoid `G`. We avoid the word diffeomorphism, keeping it for the smooth category.

As a basic example, we give the instance
`instance chartedSpaceSelf (H : Type*) [TopologicalSpace H] : ChartedSpace H H`
saying that a topological space is a charted space over itself, with the identity as unique chart.
This charted space structure is compatible with any groupoid.

Additional useful definitions:

* `Pregroupoid H` : a subset of partial maps of `H` stable under composition and
  restriction, but not inverse (ex: smooth maps)
* `Pregroupoid.groupoid` : construct a groupoid from a pregroupoid, by requiring that a map and
  its inverse both belong to the pregroupoid (ex: construct diffeos from smooth maps)
* `chartAt H x` is a preferred chart at `x : M` when `M` has a charted space structure modelled on
  `H`.
* `G.compatible he he'` states that, for any two charts `e` and `e'` in the atlas, the composition
  of `e.symm` and `e'` belongs to the groupoid `G` when `M` admits `G` as a structure groupoid.
* `G.compatible_of_mem_maximalAtlas he he'` states that, for any two charts `e` and `e'` in the
  maximal atlas associated to the groupoid `G`, the composition of `e.symm` and `e'` belongs to the
  `G` if `M` admits `G` as a structure groupoid.
* `ChartedSpaceCore.toChartedSpace`: consider a space without a topology, but endowed with a set
  of charts (which are partial equivs) for which the change of coordinates are partial homeos.
  Then one can construct a topology on the space for which the charts become partial homeos,
  defining a genuine charted space structure.

## Implementation notes

The atlas in a charted space is *not* a maximal atlas in general: the notion of maximality depends
on the groupoid one considers, and changing groupoids changes the maximal atlas. With the current
formalization, it makes sense first to choose the atlas, and then to ask whether this precise atlas
defines a smooth manifold, an orientable manifold, and so on. A consequence is that structomorphisms
between `M` and `M'` do *not* induce a bijection between the atlases of `M` and `M'`: the
definition is only that, read in charts, the structomorphism locally belongs to the groupoid under
consideration. (This is equivalent to inducing a bijection between elements of the maximal atlas).
A consequence is that the invariance under structomorphisms of properties defined in terms of the
atlas is not obvious in general, and could require some work in theory (amounting to the fact
that these properties only depend on the maximal atlas, for instance). In practice, this does not
create any real difficulty.

We use the letter `H` for the model space thinking of the case of manifolds with boundary, where the
model space is a half space.

Manifolds are sometimes defined as topological spaces with an atlas of local diffeomorphisms, and
sometimes as spaces with an atlas from which a topology is deduced. We use the former approach:
otherwise, there would be an instance from manifolds to topological spaces, which means that any
instance search for topological spaces would try to find manifold structures involving a yet
unknown model space, leading to problems. However, we also introduce the latter approach,
through a structure `ChartedSpaceCore` making it possible to construct a topology out of a set of
partial equivs with compatibility conditions (but we do not register it as an instance).

In the definition of a charted space, the model space is written as an explicit parameter as there
can be several model spaces for a given topological space. For instance, a complex manifold
(modelled over `â„‚^n`) will also be seen sometimes as a real manifold modelled over `â„^(2n)`.

## Notations

In the locale `Manifold`, we denote the composition of partial homeomorphisms with `â‰«â‚•`, and the
composition of partial equivs with `â‰«`.
-/

noncomputable section

open TopologicalSpace Topology

universe u

variable {H : Type u} {H' : Type*} {M : Type*} {M' : Type*} {M'' : Type*}

/- Notational shortcut for the composition of partial homeomorphisms and partial equivs, i.e.,
`PartialHomeomorph.trans` and `PartialEquiv.trans`.
Note that, as is usual for equivs, the composition is from left to right, hence the direction of
the arrow. -/
scoped[Manifold] infixr:100 " â‰«â‚• " => PartialHomeomorph.trans

scoped[Manifold] infixr:100 " â‰« " => PartialEquiv.trans

open Set PartialHomeomorph Manifold  -- Porting note: Added `Manifold`

/-! ### Structure groupoids -/


section Groupoid

/-! One could add to the definition of a structure groupoid the fact that the restriction of an
element of the groupoid to any open set still belongs to the groupoid.
(This is in Kobayashi-Nomizu.)
I am not sure I want this, for instance on `H Ã— E` where `E` is a vector space, and the groupoid is
made of functions respecting the fibers and linear in the fibers (so that a charted space over this
groupoid is naturally a vector bundle) I prefer that the members of the groupoid are always
defined on sets of the form `s Ã— E`. There is a typeclass `ClosedUnderRestriction` for groupoids
which have the restriction property.

The only nontrivial requirement is locality: if a partial homeomorphism belongs to the groupoid
around each point in its domain of definition, then it belongs to the groupoid. Without this
requirement, the composition of structomorphisms does not have to be a structomorphism. Note that
this implies that a partial homeomorphism with empty source belongs to any structure groupoid, as
it trivially satisfies this condition.

There is also a technical point, related to the fact that a partial homeomorphism is by definition a
global map which is a homeomorphism when restricted to its source subset (and its values outside
of the source are not relevant). Therefore, we also require that being a member of the groupoid only
depends on the values on the source.

We use primes in the structure names as we will reformulate them below (without primes) using a
`Membership` instance, writing `e âˆˆ G` instead of `e âˆˆ G.members`.
-/


/-- A structure groupoid is a set of partial homeomorphisms of a topological space stable under
composition and inverse. They appear in the definition of the smoothness class of a manifold. -/
structure StructureGroupoid (H : Type u) [TopologicalSpace H] where
  /-- Members of the structure groupoid are partial homeomorphisms. -/
  members : Set (PartialHomeomorph H H)
  /-- Structure groupoids are stable under composition. -/
  trans' : âˆ€ e e' : PartialHomeomorph H H, e âˆˆ members â†’ e' âˆˆ members â†’ e â‰«â‚• e' âˆˆ members
  /-- Structure groupoids are stable under inverse. -/
  symm' : âˆ€ e : PartialHomeomorph H H, e âˆˆ members â†’ e.symm âˆˆ members
  /-- The identity morphism lies in the structure groupoid. -/
  id_mem' : PartialHomeomorph.refl H âˆˆ members
  /-- Let `e` be a partial homeomorphism. If for every `x âˆˆ e.source`, the restriction of e to some
  open set around `x` lies in the groupoid, then `e` lies in the groupoid. -/
  locality' : âˆ€ e : PartialHomeomorph H H,
    (âˆ€ x âˆˆ e.source, âˆƒ s, IsOpen s âˆ§ x âˆˆ s âˆ§ e.restr s âˆˆ members) â†’ e âˆˆ members
  /-- Membership in a structure groupoid respects the equivalence of partial homeomorphisms. -/
  mem_of_eqOnSource' : âˆ€ e e' : PartialHomeomorph H H, e âˆˆ members â†’ e' â‰ˆ e â†’ e' âˆˆ members
#align structure_groupoid StructureGroupoid

variable [TopologicalSpace H]

instance : Membership (PartialHomeomorph H H) (StructureGroupoid H) :=
  âŸ¨fun (e : PartialHomeomorph H H) (G : StructureGroupoid H) â†¦ e âˆˆ G.membersâŸ©

instance (H : Type u) [TopologicalSpace H] :
    SetLike (StructureGroupoid H) (PartialHomeomorph H H) where
  coe s := s.members
  coe_injective' N O h := by cases N; cases O; congr

instance : Inf (StructureGroupoid H) :=
  âŸ¨fun G G' => StructureGroupoid.mk
    (members := G.members âˆ© G'.members)
    (trans' := fun e e' he he' =>
      âŸ¨G.trans' e e' he.left he'.left, G'.trans' e e' he.right he'.rightâŸ©)
    (symm' := fun e he => âŸ¨G.symm' e he.left, G'.symm' e he.rightâŸ©)
    (id_mem' := âŸ¨G.id_mem', G'.id_mem'âŸ©)
    (locality' := by
      intro e hx
      apply (mem_inter_iff e G.members G'.members).mpr
      refine And.intro (G.locality' e ?_) (G'.locality' e ?_)
      all_goals
        intro x hex
        rcases hx x hex with âŸ¨s, hsâŸ©
        use s
        refine And.intro hs.left (And.intro hs.right.left ?_)
      Â· exact hs.right.right.left
      Â· exact hs.right.right.right)
    (mem_of_eqOnSource' := fun e e' he hee' =>
      âŸ¨G.mem_of_eqOnSource' e e' he.left hee', G'.mem_of_eqOnSource' e e' he.right hee'âŸ©)âŸ©

instance : InfSet (StructureGroupoid H) :=
  âŸ¨fun S => StructureGroupoid.mk
    (members := â‹‚ s âˆˆ S, s.members)
    (trans' := by
      simp only [mem_iInter]
      intro e e' he he' i hi
      exact i.trans' e e' (he i hi) (he' i hi))
    (symm' := by
      simp only [mem_iInter]
      intro e he i hi
      exact i.symm' e (he i hi))
    (id_mem' := by
      simp only [mem_iInter]
      intro i _
      exact i.id_mem')
    (locality' := by
      simp only [mem_iInter]
      intro e he i hi
      refine i.locality' e ?_
      intro x hex
      rcases he x hex with âŸ¨s, hsâŸ©
      exact âŸ¨s, âŸ¨hs.left, âŸ¨hs.right.left, hs.right.right i hiâŸ©âŸ©âŸ©)
    (mem_of_eqOnSource' := by
      simp only [mem_iInter]
      intro e e' he he'e
      exact fun i hi => i.mem_of_eqOnSource' e e' (he i hi) he'e)âŸ©

theorem StructureGroupoid.trans (G : StructureGroupoid H) {e e' : PartialHomeomorph H H}
    (he : e âˆˆ G) (he' : e' âˆˆ G) : e â‰«â‚• e' âˆˆ G :=
  G.trans' e e' he he'
#align structure_groupoid.trans StructureGroupoid.trans

theorem StructureGroupoid.symm (G : StructureGroupoid H) {e : PartialHomeomorph H H} (he : e âˆˆ G) :
    e.symm âˆˆ G :=
  G.symm' e he
#align structure_groupoid.symm StructureGroupoid.symm

theorem StructureGroupoid.id_mem (G : StructureGroupoid H) : PartialHomeomorph.refl H âˆˆ G :=
  G.id_mem'
#align structure_groupoid.id_mem StructureGroupoid.id_mem

theorem StructureGroupoid.locality (G : StructureGroupoid H) {e : PartialHomeomorph H H}
    (h : âˆ€ x âˆˆ e.source, âˆƒ s, IsOpen s âˆ§ x âˆˆ s âˆ§ e.restr s âˆˆ G) : e âˆˆ G :=
  G.locality' e h
#align structure_groupoid.locality StructureGroupoid.locality

theorem StructureGroupoid.mem_of_eqOnSource (G : StructureGroupoid H) {e e' : PartialHomeomorph H H}
    (he : e âˆˆ G) (h : e' â‰ˆ e) : e' âˆˆ G :=
  G.mem_of_eqOnSource' e e' he h
#align structure_groupoid.eq_on_source StructureGroupoid.mem_of_eqOnSource

theorem StructureGroupoid.mem_iff_of_eqOnSource {G : StructureGroupoid H}
    {e e' : PartialHomeomorph H H} (h : e â‰ˆ e') : e âˆˆ G â†” e' âˆˆ G :=
  âŸ¨fun he â†¦ G.mem_of_eqOnSource he (Setoid.symm h), fun he' â†¦ G.mem_of_eqOnSource he' hâŸ©

/-- Partial order on the set of groupoids, given by inclusion of the members of the groupoid. -/
instance StructureGroupoid.partialOrder : PartialOrder (StructureGroupoid H) :=
  PartialOrder.lift StructureGroupoid.members fun a b h â†¦ by
    cases a
    cases b
    dsimp at h
    induction h
    rfl
#align structure_groupoid.partial_order StructureGroupoid.partialOrder

theorem StructureGroupoid.le_iff {Gâ‚ Gâ‚‚ : StructureGroupoid H} : Gâ‚ â‰¤ Gâ‚‚ â†” âˆ€ e, e âˆˆ Gâ‚ â†’ e âˆˆ Gâ‚‚ :=
  Iff.rfl
#align structure_groupoid.le_iff StructureGroupoid.le_iff

/-- The trivial groupoid, containing only the identity (and maps with empty source, as this is
necessary from the definition). -/
def idGroupoid (H : Type u) [TopologicalSpace H] : StructureGroupoid H where
  members := {PartialHomeomorph.refl H} âˆª { e : PartialHomeomorph H H | e.source = âˆ… }
  trans' e e' he he' := by
    cases' he with he he
    Â· simpa only [mem_singleton_iff.1 he, refl_trans]
    Â· have : (e â‰«â‚• e').source âŠ† e.source := sep_subset _ _
      rw [he] at this
      have : e â‰«â‚• e' âˆˆ { e : PartialHomeomorph H H | e.source = âˆ… } := eq_bot_iff.2 this
      exact (mem_union _ _ _).2 (Or.inr this)
  symm' e he := by
    cases' (mem_union _ _ _).1 he with E E
    Â· simp [mem_singleton_iff.mp E]
    Â· right
      simpa only [e.toPartialEquiv.image_source_eq_target.symm, mfld_simps] using E
  id_mem' := mem_union_left _ rfl
  locality' e he := by
    rcases e.source.eq_empty_or_nonempty with h | h
    Â· right
      exact h
    Â· left
      rcases h with âŸ¨x, hxâŸ©
      rcases he x hx with âŸ¨s, open_s, xs, hsâŸ©
      have x's : x âˆˆ (e.restr s).source := by
        rw [restr_source, open_s.interior_eq]
        exact âŸ¨hx, xsâŸ©
      cases' hs with hs hs
      Â· replace hs : PartialHomeomorph.restr e s = PartialHomeomorph.refl H := by
          simpa only using hs
        have : (e.restr s).source = univ := by
          rw [hs]
          simp
        have : e.toPartialEquiv.source âˆ© interior s = univ := this
        have : univ âŠ† interior s := by
          rw [â† this]
          exact inter_subset_right _ _
        have : s = univ := by rwa [open_s.interior_eq, univ_subset_iff] at this
        simpa only [this, restr_univ] using hs
      Â· exfalso
        rw [mem_setOf_eq] at hs
        rwa [hs] at x's
  mem_of_eqOnSource' e e' he he'e := by
    cases' he with he he
    Â· left
      have : e = e' := by
        refine eq_of_eqOnSource_univ (Setoid.symm he'e) ?_ ?_ <;>
          rw [Set.mem_singleton_iff.1 he] <;> rfl
      rwa [â† this]
    Â· right
      have he : e.toPartialEquiv.source = âˆ… := he
      rwa [Set.mem_setOf_eq, EqOnSource.source_eq he'e]
#align id_groupoid idGroupoid

/-- Every structure groupoid contains the identity groupoid. -/
instance instStructureGroupoidOrderBot : OrderBot (StructureGroupoid H) where
  bot := idGroupoid H
  bot_le := by
    intro u f hf
    have hf : f âˆˆ {PartialHomeomorph.refl H} âˆª { e : PartialHomeomorph H H | e.source = âˆ… } := hf
    simp only [singleton_union, mem_setOf_eq, mem_insert_iff] at hf
    cases' hf with hf hf
    Â· rw [hf]
      apply u.id_mem
    Â· apply u.locality
      intro x hx
      rw [hf, mem_empty_iff_false] at hx
      exact hx.elim

instance : Inhabited (StructureGroupoid H) := âŸ¨idGroupoid HâŸ©

/-- To construct a groupoid, one may consider classes of partial homeomorphisms such that
both the function and its inverse have some property. If this property is stable under composition,
one gets a groupoid. `Pregroupoid` bundles the properties needed for this construction, with the
groupoid of smooth functions with smooth inverses as an application. -/
structure Pregroupoid (H : Type*) [TopologicalSpace H] where
  /-- Property describing membership in this groupoid: the pregroupoid "contains"
    all functions `H â†’ H` having the pregroupoid property on some `s : Set H` -/
  property : (H â†’ H) â†’ Set H â†’ Prop
  /-- The pregroupoid property is stable under composition -/
  comp : âˆ€ {f g u v}, property f u â†’ property g v â†’
    IsOpen u â†’ IsOpen v â†’ IsOpen (u âˆ© f â»Â¹' v) â†’ property (g âˆ˜ f) (u âˆ© f â»Â¹' v)
  /-- Pregroupoids contain the identity map (on `univ`) -/
  id_mem : property id univ
  /-- The pregroupoid property is "local", in the sense that `f` has the pregroupoid property on `u`
  iff its restriction to each open subset of `u` has it -/
  locality :
    âˆ€ {f u}, IsOpen u â†’ (âˆ€ x âˆˆ u, âˆƒ v, IsOpen v âˆ§ x âˆˆ v âˆ§ property f (u âˆ© v)) â†’ property f u
  /-- If `f = g` on `u` and `property f u`, then `property g u` -/
  congr : âˆ€ {f g : H â†’ H} {u}, IsOpen u â†’ (âˆ€ x âˆˆ u, g x = f x) â†’ property f u â†’ property g u
#align pregroupoid Pregroupoid

/-- Construct a groupoid of partial homeos for which the map and its inverse have some property,
from a pregroupoid asserting that this property is stable under composition. -/
def Pregroupoid.groupoid (PG : Pregroupoid H) : StructureGroupoid H where
  members := { e : PartialHomeomorph H H | PG.property e e.source âˆ§ PG.property e.symm e.target }
  trans' e e' he he' := by
    constructor
    Â· apply PG.comp he.1 he'.1 e.open_source e'.open_source
      apply e.continuousOn_toFun.isOpen_inter_preimage e.open_source e'.open_source
    Â· apply PG.comp he'.2 he.2 e'.open_target e.open_target
      apply e'.continuousOn_invFun.isOpen_inter_preimage e'.open_target e.open_target
  symm' e he := âŸ¨he.2, he.1âŸ©
  id_mem' := âŸ¨PG.id_mem, PG.id_memâŸ©
  locality' e he := by
    constructor
    Â· refine PG.locality e.open_source fun x xu â†¦ ?_
      rcases he x xu with âŸ¨s, s_open, xs, hsâŸ©
      refine âŸ¨s, s_open, xs, ?_âŸ©
      convert hs.1 using 1
      dsimp [PartialHomeomorph.restr]
      rw [s_open.interior_eq]
    Â· refine PG.locality e.open_target fun x xu â†¦ ?_
      rcases he (e.symm x) (e.map_target xu) with âŸ¨s, s_open, xs, hsâŸ©
      refine âŸ¨e.target âˆ© e.symm â»Â¹' s, ?_, âŸ¨xu, xsâŸ©, ?_âŸ©
      Â· exact ContinuousOn.isOpen_inter_preimage e.continuousOn_invFun e.open_target s_open
      Â· rw [â† inter_assoc, inter_self]
        convert hs.2 using 1
        dsimp [PartialHomeomorph.restr]
        rw [s_open.interior_eq]
  mem_of_eqOnSource' e e' he ee' := by
    constructor
    Â· apply PG.congr e'.open_source ee'.2
      simp only [ee'.1, he.1]
    Â· have A := EqOnSource.symm' ee'
      apply PG.congr e'.symm.open_source A.2
      -- Porting note: was
      -- convert he.2
      -- rw [A.1]
      -- rfl
      rw [A.1, symm_toPartialEquiv, PartialEquiv.symm_source]
      exact he.2
#align pregroupoid.groupoid Pregroupoid.groupoid

theorem mem_groupoid_of_pregroupoid {PG : Pregroupoid H} {e : PartialHomeomorph H H} :
    e âˆˆ PG.groupoid â†” PG.property e e.source âˆ§ PG.property e.symm e.target :=
  Iff.rfl
#align mem_groupoid_of_pregroupoid mem_groupoid_of_pregroupoid

theorem groupoid_of_pregroupoid_le (PGâ‚ PGâ‚‚ : Pregroupoid H)
    (h : âˆ€ f s, PGâ‚.property f s â†’ PGâ‚‚.property f s) : PGâ‚.groupoid â‰¤ PGâ‚‚.groupoid := by
  refine StructureGroupoid.le_iff.2 fun e he â†¦ ?_
  rw [mem_groupoid_of_pregroupoid] at he âŠ¢
  exact âŸ¨h _ _ he.1, h _ _ he.2âŸ©
#align groupoid_of_pregroupoid_le groupoid_of_pregroupoid_le

theorem mem_pregroupoid_of_eqOnSource (PG : Pregroupoid H) {e e' : PartialHomeomorph H H}
    (he' : e â‰ˆ e') (he : PG.property e e.source) : PG.property e' e'.source := by
  rw [â† he'.1]
  exact PG.congr e.open_source he'.eqOn.symm he
#align mem_pregroupoid_of_eq_on_source mem_pregroupoid_of_eqOnSource

/-- The pregroupoid of all partial maps on a topological space `H`. -/
abbrev continuousPregroupoid (H : Type*) [TopologicalSpace H] : Pregroupoid H where
  property _ _ := True
  comp _ _ _ _ _ := trivial
  id_mem := trivial
  locality _ _ := trivial
  congr _ _ _ := trivial
#align continuous_pregroupoid continuousPregroupoid

instance (H : Type*) [TopologicalSpace H] : Inhabited (Pregroupoid H) :=
  âŸ¨continuousPregroupoid HâŸ©

/-- The groupoid of all partial homeomorphisms on a topological space `H`. -/
def continuousGroupoid (H : Type*) [TopologicalSpace H] : StructureGroupoid H :=
  Pregroupoid.groupoid (continuousPregroupoid H)
#align continuous_groupoid continuousGroupoid

/-- Every structure groupoid is contained in the groupoid of all partial homeomorphisms. -/
instance instStructureGroupoidOrderTop : OrderTop (StructureGroupoid H) where
  top := continuousGroupoid H
  le_top _ _ _ := âŸ¨trivial, trivialâŸ©

instance : CompleteLattice (StructureGroupoid H) :=
  { SetLike.instPartialOrder,
    completeLatticeOfInf _ (by
      exact fun s =>
      âŸ¨fun S Ss F hF => mem_iInterâ‚‚.mp hF S Ss,
      fun T Tl F fT => mem_iInterâ‚‚.mpr (fun i his => Tl his fT)âŸ©) with
    le := (Â· â‰¤ Â·)
    lt := (Â· < Â·)
    bot := instStructureGroupoidOrderBot.bot
    bot_le := instStructureGroupoidOrderBot.bot_le
    top := instStructureGroupoidOrderTop.top
    le_top := instStructureGroupoidOrderTop.le_top
    inf := (Â· âŠ“ Â·)
    le_inf := fun Nâ‚ Nâ‚‚ Nâ‚ƒ hâ‚â‚‚ hâ‚â‚ƒ m hm â†¦ âŸ¨hâ‚â‚‚ hm, hâ‚â‚ƒ hmâŸ©
    inf_le_left := fun _ _ _ â†¦ And.left
    inf_le_right := fun _ _ _ â†¦ And.right }

/-- A groupoid is closed under restriction if it contains all restrictions of its element local
homeomorphisms to open subsets of the source. -/
class ClosedUnderRestriction (G : StructureGroupoid H) : Prop where
  closedUnderRestriction :
    âˆ€ {e : PartialHomeomorph H H}, e âˆˆ G â†’ âˆ€ s : Set H, IsOpen s â†’ e.restr s âˆˆ G
#align closed_under_restriction ClosedUnderRestriction

theorem closedUnderRestriction' {G : StructureGroupoid H} [ClosedUnderRestriction G]
    {e : PartialHomeomorph H H} (he : e âˆˆ G) {s : Set H} (hs : IsOpen s) : e.restr s âˆˆ G :=
  ClosedUnderRestriction.closedUnderRestriction he s hs
#align closed_under_restriction' closedUnderRestriction'

/-- The trivial restriction-closed groupoid, containing only partial homeomorphisms equivalent
to the restriction of the identity to the various open subsets. -/
def idRestrGroupoid : StructureGroupoid H where
  members := { e | âˆƒ (s : Set H) (h : IsOpen s), e â‰ˆ PartialHomeomorph.ofSet s h }
  trans' := by
    rintro e e' âŸ¨s, hs, hseâŸ© âŸ¨s', hs', hse'âŸ©
    refine âŸ¨s âˆ© s', hs.inter hs', ?_âŸ©
    have := PartialHomeomorph.EqOnSource.trans' hse hse'
    rwa [PartialHomeomorph.ofSet_trans_ofSet] at this
  symm' := by
    rintro e âŸ¨s, hs, hseâŸ©
    refine âŸ¨s, hs, ?_âŸ©
    rw [â† ofSet_symm]
    exact PartialHomeomorph.EqOnSource.symm' hse
  id_mem' := âŸ¨univ, isOpen_univ, by simp only [mfld_simps, refl]âŸ©
  locality' := by
    intro e h
    refine âŸ¨e.source, e.open_source, by simp only [mfld_simps], ?_âŸ©
    intro x hx
    rcases h x hx with âŸ¨s, hs, hxs, s', hs', hes'âŸ©
    have hes : x âˆˆ (e.restr s).source := by
      rw [e.restr_source]
      refine âŸ¨hx, ?_âŸ©
      rw [hs.interior_eq]
      exact hxs
    simpa only [mfld_simps] using PartialHomeomorph.EqOnSource.eqOn hes' hes
  mem_of_eqOnSource' := by
    rintro e e' âŸ¨s, hs, hseâŸ© hee'
    exact âŸ¨s, hs, Setoid.trans hee' hseâŸ©
#align id_restr_groupoid idRestrGroupoid

theorem idRestrGroupoid_mem {s : Set H} (hs : IsOpen s) : ofSet s hs âˆˆ @idRestrGroupoid H _ :=
  âŸ¨s, hs, refl _âŸ©
#align id_restr_groupoid_mem idRestrGroupoid_mem

/-- The trivial restriction-closed groupoid is indeed `ClosedUnderRestriction`. -/
instance closedUnderRestriction_idRestrGroupoid : ClosedUnderRestriction (@idRestrGroupoid H _) :=
  âŸ¨by
    rintro e âŸ¨s', hs', heâŸ© s hs
    use s' âˆ© s, hs'.inter hs
    refine Setoid.trans (PartialHomeomorph.EqOnSource.restr he s) ?_
    exact âŸ¨by simp only [hs.interior_eq, mfld_simps], by simp only [mfld_simps, eqOn_refl]âŸ©âŸ©
#align closed_under_restriction_id_restr_groupoid closedUnderRestriction_idRestrGroupoid

/-- A groupoid is closed under restriction if and only if it contains the trivial restriction-closed
groupoid. -/
theorem closedUnderRestriction_iff_id_le (G : StructureGroupoid H) :
    ClosedUnderRestriction G â†” idRestrGroupoid â‰¤ G := by
  constructor
  Â· intro _i
    rw [StructureGroupoid.le_iff]
    rintro e âŸ¨s, hs, hesâŸ©
    refine G.mem_of_eqOnSource ?_ hes
    convert closedUnderRestriction' G.id_mem hs
    -- Porting note: was
    -- change s = _ âˆ© _
    -- rw [hs.interior_eq]
    -- simp only [mfld_simps]
    ext
    Â· rw [PartialHomeomorph.restr_apply, PartialHomeomorph.refl_apply, id, ofSet_apply, id_eq]
    Â· simp [hs]
    Â· simp [hs.interior_eq]
  Â· intro h
    constructor
    intro e he s hs
    rw [â† ofSet_trans (e : PartialHomeomorph H H) hs]
    refine G.trans ?_ he
    apply StructureGroupoid.le_iff.mp h
    exact idRestrGroupoid_mem hs
#align closed_under_restriction_iff_id_le closedUnderRestriction_iff_id_le

/-- The groupoid of all partial homeomorphisms on a topological space `H`
is closed under restriction. -/
instance : ClosedUnderRestriction (continuousGroupoid H) :=
  (closedUnderRestriction_iff_id_le _).mpr le_top

end Groupoid

/-! ### Charted spaces -/


/-- A charted space is a topological space endowed with an atlas, i.e., a set of local
homeomorphisms taking value in a model space `H`, called charts, such that the domains of the charts
cover the whole space. We express the covering property by choosing for each `x` a member
`chartAt x` of the atlas containing `x` in its source: in the smooth case, this is convenient to
construct the tangent bundle in an efficient way.
The model space is written as an explicit parameter as there can be several model spaces for a
given topological space. For instance, a complex manifold (modelled over `â„‚^n`) will also be seen
sometimes as a real manifold over `â„^(2n)`.
-/
@[ext]
class ChartedSpace (H : Type*) [TopologicalSpace H] (M : Type*) [TopologicalSpace M] where
  /-- The atlas of charts in the `ChartedSpace`. -/
  protected atlas : Set (PartialHomeomorph M H)
  /-- The preferred chart at each point in the charted space. -/
  protected chartAt : M â†’ PartialHomeomorph M H
  protected mem_chart_source : âˆ€ x, x âˆˆ (chartAt x).source
  protected chart_mem_atlas : âˆ€ x, chartAt x âˆˆ atlas
#align charted_space ChartedSpace

/-- The atlas of charts in a `ChartedSpace`. -/
abbrev atlas (H : Type*) [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] : Set (PartialHomeomorph M H) :=
  ChartedSpace.atlas

/-- The preferred chart at a point `x` in a charted space `M`. -/
abbrev chartAt (H : Type*) [TopologicalSpace H] {M : Type*} [TopologicalSpace M]
    [ChartedSpace H M] (x : M) : PartialHomeomorph M H :=
  ChartedSpace.chartAt x

@[simp, mfld_simps]
lemma mem_chart_source (H : Type*) {M : Type*} [TopologicalSpace H] [TopologicalSpace M]
    [ChartedSpace H M] (x : M) : x âˆˆ (chartAt H x).source :=
  ChartedSpace.mem_chart_source x

@[simp, mfld_simps]
lemma chart_mem_atlas (H : Type*) {M : Type*} [TopologicalSpace H] [TopologicalSpace M]
    [ChartedSpace H M] (x : M) : chartAt H x âˆˆ atlas H M :=
  ChartedSpace.chart_mem_atlas x

section ChartedSpace

/-- Any space is a `ChartedSpace` modelled over itself, by just using the identity chart. -/
instance chartedSpaceSelf (H : Type*) [TopologicalSpace H] : ChartedSpace H H where
  atlas := {PartialHomeomorph.refl H}
  chartAt _ := PartialHomeomorph.refl H
  mem_chart_source x := mem_univ x
  chart_mem_atlas _ := mem_singleton _
#align charted_space_self chartedSpaceSelf

/-- In the trivial `ChartedSpace` structure of a space modelled over itself through the identity,
the atlas members are just the identity. -/
@[simp, mfld_simps]
theorem chartedSpaceSelf_atlas {H : Type*} [TopologicalSpace H] {e : PartialHomeomorph H H} :
    e âˆˆ atlas H H â†” e = PartialHomeomorph.refl H :=
  Iff.rfl
#align charted_space_self_atlas chartedSpaceSelf_atlas

/-- In the model space, `chartAt` is always the identity. -/
theorem chartAt_self_eq {H : Type*} [TopologicalSpace H] {x : H} :
    chartAt H x = PartialHomeomorph.refl H := rfl
#align chart_at_self_eq chartAt_self_eq

section

variable (H) [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M]

-- Porting note: Added `(H := H)` to avoid typeclass instance problem.
theorem mem_chart_target (x : M) : chartAt H x x âˆˆ (chartAt H x).target :=
  (chartAt H x).map_source (mem_chart_source _ _)
#align mem_chart_target mem_chart_target

theorem chart_source_mem_nhds (x : M) : (chartAt H x).source âˆˆ ğ“ x :=
  (chartAt H x).open_source.mem_nhds <| mem_chart_source H x
#align chart_source_mem_nhds chart_source_mem_nhds

theorem chart_target_mem_nhds (x : M) : (chartAt H x).target âˆˆ ğ“ (chartAt H x x) :=
  (chartAt H x).open_target.mem_nhds <| mem_chart_target H x
#align chart_target_mem_nhds chart_target_mem_nhds

variable (M) in
@[simp]
theorem iUnion_source_chartAt : (â‹ƒ x : M, (chartAt H x).source) = (univ : Set M) :=
  eq_univ_iff_forall.mpr fun x â†¦ mem_iUnion.mpr âŸ¨x, mem_chart_source H xâŸ©

theorem ChartedSpace.isOpen_iff (s : Set M) :
    IsOpen s â†” âˆ€ x : M, IsOpen <| chartAt H x '' ((chartAt H x).source âˆ© s) := by
  rw [isOpen_iff_of_cover (fun i â†¦ (chartAt H i).open_source) (iUnion_source_chartAt H M)]
  simp only [(chartAt H _).isOpen_image_iff_of_subset_source (inter_subset_left _ _)]

/-- `achart H x` is the chart at `x`, considered as an element of the atlas.
Especially useful for working with `BasicSmoothVectorBundleCore`. -/
def achart (x : M) : atlas H M :=
  âŸ¨chartAt H x, chart_mem_atlas H xâŸ©
#align achart achart

theorem achart_def (x : M) : achart H x = âŸ¨chartAt H x, chart_mem_atlas H xâŸ© :=
  rfl
#align achart_def achart_def

@[simp, mfld_simps]
theorem coe_achart (x : M) : (achart H x : PartialHomeomorph M H) = chartAt H x :=
  rfl
#align coe_achart coe_achart

@[simp, mfld_simps]
theorem achart_val (x : M) : (achart H x).1 = chartAt H x :=
  rfl
#align achart_val achart_val

theorem mem_achart_source (x : M) : x âˆˆ (achart H x).1.source :=
  mem_chart_source H x
#align mem_achart_source mem_achart_source

open TopologicalSpace

theorem ChartedSpace.secondCountable_of_countable_cover [SecondCountableTopology H] {s : Set M}
    (hs : â‹ƒ (x) (_ : x âˆˆ s), (chartAt H x).source = univ) (hsc : s.Countable) :
    SecondCountableTopology M := by
  haveI : âˆ€ x : M, SecondCountableTopology (chartAt H x).source :=
    fun x â†¦ (chartAt (H := H) x).secondCountableTopology_source
  haveI := hsc.toEncodable
  rw [biUnion_eq_iUnion] at hs
  exact secondCountableTopology_of_countable_cover (fun x : s â†¦ (chartAt H (x : M)).open_source) hs
#align charted_space.second_countable_of_countable_cover ChartedSpace.secondCountable_of_countable_cover

variable (M)

theorem ChartedSpace.secondCountable_of_sigma_compact [SecondCountableTopology H]
    [SigmaCompactSpace M] : SecondCountableTopology M := by
  obtain âŸ¨s, hsc, hsUâŸ© : âˆƒ s, Set.Countable s âˆ§ â‹ƒ (x) (_ : x âˆˆ s), (chartAt H x).source = univ :=
    countable_cover_nhds_of_sigma_compact fun x : M â†¦ chart_source_mem_nhds H x
  exact ChartedSpace.secondCountable_of_countable_cover H hsU hsc
#align charted_space.second_countable_of_sigma_compact ChartedSpace.secondCountable_of_sigma_compact

/-- If a topological space admits an atlas with locally compact charts, then the space itself
is locally compact. -/
theorem ChartedSpace.locallyCompactSpace [LocallyCompactSpace H] : LocallyCompactSpace M := by
  have : âˆ€ x : M, (ğ“ x).HasBasis
      (fun s â†¦ s âˆˆ ğ“ (chartAt H x x) âˆ§ IsCompact s âˆ§ s âŠ† (chartAt H x).target)
      fun s â†¦ (chartAt H x).symm '' s := fun x â†¦ by
    rw [â† (chartAt H x).symm_map_nhds_eq (mem_chart_source H x)]
    exact ((compact_basis_nhds (chartAt H x x)).hasBasis_self_subset
      (chart_target_mem_nhds H x)).map _
  refine .of_hasBasis this ?_
  rintro x s âŸ¨_, hâ‚‚, hâ‚ƒâŸ©
  exact hâ‚‚.image_of_continuousOn ((chartAt H x).continuousOn_symm.mono hâ‚ƒ)
#align charted_space.locally_compact ChartedSpace.locallyCompactSpace

/-- If a topological space admits an atlas with locally connected charts, then the space itself is
locally connected. -/
theorem ChartedSpace.locallyConnectedSpace [LocallyConnectedSpace H] : LocallyConnectedSpace M := by
  let e : M â†’ PartialHomeomorph M H := chartAt H
  refine locallyConnectedSpace_of_connected_bases (fun x s â†¦ (e x).symm '' s)
      (fun x s â†¦ (IsOpen s âˆ§ e x x âˆˆ s âˆ§ IsConnected s) âˆ§ s âŠ† (e x).target) ?_ ?_
  Â· intro x
    simpa only [e, PartialHomeomorph.symm_map_nhds_eq, mem_chart_source] using
      ((LocallyConnectedSpace.open_connected_basis (e x x)).restrict_subset
        ((e x).open_target.mem_nhds (mem_chart_target H x))).map (e x).symm
  Â· rintro x s âŸ¨âŸ¨-, -, hsconnâŸ©, hssubsetâŸ©
    exact hsconn.isPreconnected.image _ ((e x).continuousOn_symm.mono hssubset)
#align charted_space.locally_connected_space ChartedSpace.locallyConnectedSpace

/-- If `M` is modelled on `H'` and `H'` is itself modelled on `H`, then we can consider `M` as being
modelled on `H`. -/
def ChartedSpace.comp (H : Type*) [TopologicalSpace H] (H' : Type*) [TopologicalSpace H']
    (M : Type*) [TopologicalSpace M] [ChartedSpace H H'] [ChartedSpace H' M] :
    ChartedSpace H M where
  atlas := image2 PartialHomeomorph.trans (atlas H' M) (atlas H H')
  chartAt p := (chartAt H' p).trans (chartAt H (chartAt H' p p))
  mem_chart_source p := by simp only [mfld_simps]
  chart_mem_atlas p := âŸ¨chartAt _ p, chart_mem_atlas _ p, chartAt _ _, chart_mem_atlas _ _, rflâŸ©
#align charted_space.comp ChartedSpace.comp

theorem chartAt_comp (H : Type*) [TopologicalSpace H] (H' : Type*) [TopologicalSpace H']
    {M : Type*} [TopologicalSpace M] [ChartedSpace H H'] [ChartedSpace H' M] (x : M) :
    (letI := ChartedSpace.comp H H' M; chartAt H x) = chartAt H' x â‰«â‚• chartAt H (chartAt H' x x) :=
  rfl

end

library_note "Manifold type tags" /-- For technical reasons we introduce two type tags:

* `ModelProd H H'` is the same as `H Ã— H'`;
* `ModelPi H` is the same as `âˆ€ i, H i`, where `H : Î¹ â†’ Type*` and `Î¹` is a finite type.

In both cases the reason is the same, so we explain it only in the case of the product. A charted
space `M` with model `H` is a set of charts from `M` to `H` covering the space. Every space is
registered as a charted space over itself, using the only chart `id`, in `chartedSpaceSelf`. You
can also define a product of charted space `M` and `M'` (with model space `H Ã— H'`) by taking the
products of the charts. Now, on `H Ã— H'`, there are two charted space structures with model space
`H Ã— H'` itself, the one coming from `chartedSpaceSelf`, and the one coming from the product of
the two `chartedSpaceSelf` on each component. They are equal, but not defeq (because the product
of `id` and `id` is not defeq to `id`), which is bad as we know. This expedient of renaming `H Ã— H'`
solves this problem. -/


/-- Same thing as `H Ã— H'`. We introduce it for technical reasons,
see note [Manifold type tags]. -/
def ModelProd (H : Type*) (H' : Type*) :=
  H Ã— H'
#align model_prod ModelProd

/-- Same thing as `âˆ€ i, H i`. We introduce it for technical reasons,
see note [Manifold type tags]. -/
def ModelPi {Î¹ : Type*} (H : Î¹ â†’ Type*) :=
  âˆ€ i, H i
#align model_pi ModelPi

section

-- attribute [local reducible] ModelProd -- Porting note: not available in Lean4

instance modelProdInhabited [Inhabited H] [Inhabited H'] : Inhabited (ModelProd H H') :=
  instInhabitedProd
#align model_prod_inhabited modelProdInhabited

instance (H : Type*) [TopologicalSpace H] (H' : Type*) [TopologicalSpace H'] :
    TopologicalSpace (ModelProd H H') :=
  instTopologicalSpaceProd

-- Porting note: simpNF false positive
-- Next lemma shows up often when dealing with derivatives, register it as simp.
@[simp, mfld_simps, nolint simpNF]
theorem modelProd_range_prod_id {H : Type*} {H' : Type*} {Î± : Type*} (f : H â†’ Î±) :
    (range fun p : ModelProd H H' â†¦ (f p.1, p.2)) = range f Ã—Ë¢ (univ : Set H') := by
  rw [prod_range_univ_eq]
  rfl
#align model_prod_range_prod_id modelProd_range_prod_id

end

section

variable {Î¹ : Type*} {Hi : Î¹ â†’ Type*}

-- Porting note: Old proof was `Pi.inhabited _`.
instance modelPiInhabited [âˆ€ i, Inhabited (Hi i)] : Inhabited (ModelPi Hi) :=
  âŸ¨fun _ â†¦ defaultâŸ©
#align model_pi_inhabited modelPiInhabited

instance [âˆ€ i, TopologicalSpace (Hi i)] : TopologicalSpace (ModelPi Hi) :=
  Pi.topologicalSpace

end

/-- The product of two charted spaces is naturally a charted space, with the canonical
construction of the atlas of product maps. -/
instance prodChartedSpace (H : Type*) [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] (H' : Type*) [TopologicalSpace H'] (M' : Type*) [TopologicalSpace M']
    [ChartedSpace H' M'] : ChartedSpace (ModelProd H H') (M Ã— M') where
  atlas := image2 PartialHomeomorph.prod (atlas H M) (atlas H' M')
  chartAt x := (chartAt H x.1).prod (chartAt H' x.2)
  mem_chart_source x := âŸ¨mem_chart_source H x.1, mem_chart_source H' x.2âŸ©
  chart_mem_atlas x := mem_image2_of_mem (chart_mem_atlas H x.1) (chart_mem_atlas H' x.2)
#align prod_charted_space prodChartedSpace

section prodChartedSpace

@[ext]
theorem ModelProd.ext {x y : ModelProd H H'} (hâ‚ : x.1 = y.1) (hâ‚‚ : x.2 = y.2) : x = y :=
  Prod.ext hâ‚ hâ‚‚

variable [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M] [TopologicalSpace H']
  [TopologicalSpace M'] [ChartedSpace H' M'] {x : M Ã— M'}

@[simp, mfld_simps]
theorem prodChartedSpace_chartAt :
    chartAt (ModelProd H H') x = (chartAt H x.fst).prod (chartAt H' x.snd) :=
  rfl
#align prod_charted_space_chart_at prodChartedSpace_chartAt

theorem chartedSpaceSelf_prod : prodChartedSpace H H H' H' = chartedSpaceSelf (H Ã— H') := by
  ext1
  Â· simp [prodChartedSpace, atlas, ChartedSpace.atlas]
  Â· ext1
    simp only [prodChartedSpace_chartAt, chartAt_self_eq, refl_prod_refl]
    rfl
#align charted_space_self_prod chartedSpaceSelf_prod

end prodChartedSpace

/-- The product of a finite family of charted spaces is naturally a charted space, with the
canonical construction of the atlas of finite product maps. -/
instance piChartedSpace {Î¹ : Type*} [Finite Î¹] (H : Î¹ â†’ Type*) [âˆ€ i, TopologicalSpace (H i)]
    (M : Î¹ â†’ Type*) [âˆ€ i, TopologicalSpace (M i)] [âˆ€ i, ChartedSpace (H i) (M i)] :
    ChartedSpace (ModelPi H) (âˆ€ i, M i) where
  atlas := PartialHomeomorph.pi '' Set.pi univ fun _ â†¦ atlas (H _) (M _)
  chartAt f := PartialHomeomorph.pi fun i â†¦ chartAt (H i) (f i)
  mem_chart_source f i _ := mem_chart_source (H i) (f i)
  chart_mem_atlas f := mem_image_of_mem _ fun i _ â†¦ chart_mem_atlas (H i) (f i)
#align pi_charted_space piChartedSpace

@[simp, mfld_simps]
theorem piChartedSpace_chartAt {Î¹ : Type*} [Finite Î¹] (H : Î¹ â†’ Type*)
    [âˆ€ i, TopologicalSpace (H i)] (M : Î¹ â†’ Type*) [âˆ€ i, TopologicalSpace (M i)]
    [âˆ€ i, ChartedSpace (H i) (M i)] (f : âˆ€ i, M i) :
    chartAt (H := ModelPi H) f = PartialHomeomorph.pi fun i â†¦ chartAt (H i) (f i) :=
  rfl
#align pi_charted_space_chart_at piChartedSpace_chartAt

end ChartedSpace

/-! ### Constructing a topology from an atlas -/


/-- Sometimes, one may want to construct a charted space structure on a space which does not yet
have a topological structure, where the topology would come from the charts. For this, one needs
charts that are only partial equivalences, and continuity properties for their composition.
This is formalised in `ChartedSpaceCore`. -/
-- Porting note(#5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
structure ChartedSpaceCore (H : Type*) [TopologicalSpace H] (M : Type*) where
  /-- An atlas of charts, which are only `PartialEquiv`s -/
  atlas : Set (PartialEquiv M H)
  /-- The preferred chart at each point -/
  chartAt : M â†’ PartialEquiv M H
  mem_chart_source : âˆ€ x, x âˆˆ (chartAt x).source
  chart_mem_atlas : âˆ€ x, chartAt x âˆˆ atlas
  open_source : âˆ€ e e' : PartialEquiv M H, e âˆˆ atlas â†’ e' âˆˆ atlas â†’ IsOpen (e.symm.trans e').source
  continuousOn_toFun : âˆ€ e e' : PartialEquiv M H, e âˆˆ atlas â†’ e' âˆˆ atlas â†’
    ContinuousOn (e.symm.trans e') (e.symm.trans e').source
#align charted_space_core ChartedSpaceCore

namespace ChartedSpaceCore

variable [TopologicalSpace H] (c : ChartedSpaceCore H M) {e : PartialEquiv M H}

/-- Topology generated by a set of charts on a Type. -/
protected def toTopologicalSpace : TopologicalSpace M :=
  TopologicalSpace.generateFrom <|
    â‹ƒ (e : PartialEquiv M H) (_ : e âˆˆ c.atlas) (s : Set H) (_ : IsOpen s),
      {e â»Â¹' s âˆ© e.source}
#align charted_space_core.to_topological_space ChartedSpaceCore.toTopologicalSpace

theorem open_source' (he : e âˆˆ c.atlas) : IsOpen[c.toTopologicalSpace] e.source := by
  apply TopologicalSpace.GenerateOpen.basic
  simp only [exists_prop, mem_iUnion, mem_singleton_iff]
  refine âŸ¨e, he, univ, isOpen_univ, ?_âŸ©
  simp only [Set.univ_inter, Set.preimage_univ]
#align charted_space_core.open_source' ChartedSpaceCore.open_source'

theorem open_target (he : e âˆˆ c.atlas) : IsOpen e.target := by
  have E : e.target âˆ© e.symm â»Â¹' e.source = e.target :=
    Subset.antisymm (inter_subset_left _ _) fun x hx â†¦
      âŸ¨hx, PartialEquiv.target_subset_preimage_source _ hxâŸ©
  simpa [PartialEquiv.trans_source, E] using c.open_source e e he he
#align charted_space_core.open_target ChartedSpaceCore.open_target

/-- An element of the atlas in a charted space without topology becomes a partial homeomorphism
for the topology constructed from this atlas. The `PartialHomeomorph` version is given in this
definition. -/
protected def partialHomeomorph (e : PartialEquiv M H) (he : e âˆˆ c.atlas) :
    @PartialHomeomorph M H c.toTopologicalSpace _ :=
  { __ := c.toTopologicalSpace
    __ := e
    open_source := by convert c.open_source' he
    open_target := by convert c.open_target he
    continuousOn_toFun := by
      letI : TopologicalSpace M := c.toTopologicalSpace
      rw [continuousOn_open_iff (c.open_source' he)]
      intro s s_open
      rw [inter_comm]
      apply TopologicalSpace.GenerateOpen.basic
      simp only [exists_prop, mem_iUnion, mem_singleton_iff]
      exact âŸ¨e, he, âŸ¨s, s_open, rflâŸ©âŸ©
    continuousOn_invFun := by
      letI : TopologicalSpace M := c.toTopologicalSpace
      apply continuousOn_isOpen_of_generateFrom
      intro t ht
      simp only [exists_prop, mem_iUnion, mem_singleton_iff] at ht
      rcases ht with âŸ¨e', e'_atlas, s, s_open, tsâŸ©
      rw [ts]
      let f := e.symm.trans e'
      have : IsOpen (f â»Â¹' s âˆ© f.source) := by
        simpa [f, inter_comm] using (continuousOn_open_iff (c.open_source e e' he e'_atlas)).1
          (c.continuousOn_toFun e e' he e'_atlas) s s_open
      have A : e' âˆ˜ e.symm â»Â¹' s âˆ© (e.target âˆ© e.symm â»Â¹' e'.source) =
          e.target âˆ© (e' âˆ˜ e.symm â»Â¹' s âˆ© e.symm â»Â¹' e'.source) := by
        rw [â† inter_assoc, â† inter_assoc]
        congr 1
        exact inter_comm _ _
      simpa [f, PartialEquiv.trans_source, preimage_inter, preimage_comp.symm, A] using this }
#align charted_space_core.local_homeomorph ChartedSpaceCore.partialHomeomorph

/-- Given a charted space without topology, endow it with a genuine charted space structure with
respect to the topology constructed from the atlas. -/
def toChartedSpace : @ChartedSpace H _ M c.toTopologicalSpace :=
  { __ := c.toTopologicalSpace
    atlas := â‹ƒ (e : PartialEquiv M H) (he : e âˆˆ c.atlas), {c.partialHomeomorph e he}
    chartAt := fun x â†¦ c.partialHomeomorph (c.chartAt x) (c.chart_mem_atlas x)
    mem_chart_source := fun x â†¦ c.mem_chart_source x
    chart_mem_atlas := fun x â†¦ by
      simp only [mem_iUnion, mem_singleton_iff]
      exact âŸ¨c.chartAt x, c.chart_mem_atlas x, rflâŸ©}
#align charted_space_core.to_charted_space ChartedSpaceCore.toChartedSpace

end ChartedSpaceCore

/-! ### Charted space with a given structure groupoid -/


section HasGroupoid

variable [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M]

/-- A charted space has an atlas in a groupoid `G` if the change of coordinates belong to the
groupoid. -/
class HasGroupoid {H : Type*} [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] (G : StructureGroupoid H) : Prop where
  compatible : âˆ€ {e e' : PartialHomeomorph M H}, e âˆˆ atlas H M â†’ e' âˆˆ atlas H M â†’ e.symm â‰«â‚• e' âˆˆ G
#align has_groupoid HasGroupoid

/-- Reformulate in the `StructureGroupoid` namespace the compatibility condition of charts in a
charted space admitting a structure groupoid, to make it more easily accessible with dot
notation. -/
theorem StructureGroupoid.compatible {H : Type*} [TopologicalSpace H] (G : StructureGroupoid H)
    {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [HasGroupoid M G]
    {e e' : PartialHomeomorph M H} (he : e âˆˆ atlas H M) (he' : e' âˆˆ atlas H M) : e.symm â‰«â‚• e' âˆˆ G :=
  HasGroupoid.compatible he he'
#align structure_groupoid.compatible StructureGroupoid.compatible

theorem hasGroupoid_of_le {Gâ‚ Gâ‚‚ : StructureGroupoid H} (h : HasGroupoid M Gâ‚) (hle : Gâ‚ â‰¤ Gâ‚‚) :
    HasGroupoid M Gâ‚‚ :=
  âŸ¨fun he he' â†¦ hle (h.compatible he he')âŸ©
#align has_groupoid_of_le hasGroupoid_of_le

theorem hasGroupoid_inf_iff {Gâ‚ Gâ‚‚ : StructureGroupoid H} : HasGroupoid M (Gâ‚ âŠ“ Gâ‚‚) â†”
    HasGroupoid M Gâ‚ âˆ§ HasGroupoid M Gâ‚‚ :=
  âŸ¨(fun h â†¦ âŸ¨hasGroupoid_of_le h inf_le_left, hasGroupoid_of_le h inf_le_rightâŸ©),
  fun âŸ¨h1, h2âŸ© â†¦ { compatible := fun he he' â†¦ âŸ¨h1.compatible he he', h2.compatible he he'âŸ© }âŸ©

theorem hasGroupoid_of_pregroupoid (PG : Pregroupoid H) (h : âˆ€ {e e' : PartialHomeomorph M H},
    e âˆˆ atlas H M â†’ e' âˆˆ atlas H M â†’ PG.property (e.symm â‰«â‚• e') (e.symm â‰«â‚• e').source) :
    HasGroupoid M PG.groupoid :=
  âŸ¨fun he he' â†¦ mem_groupoid_of_pregroupoid.mpr âŸ¨h he he', h he' heâŸ©âŸ©
#align has_groupoid_of_pregroupoid hasGroupoid_of_pregroupoid

/-- The trivial charted space structure on the model space is compatible with any groupoid. -/
instance hasGroupoid_model_space (H : Type*) [TopologicalSpace H] (G : StructureGroupoid H) :
    HasGroupoid H G where
  compatible {e e'} he he' := by
    rw [chartedSpaceSelf_atlas] at he he'
    simp [he, he', StructureGroupoid.id_mem]
#align has_groupoid_model_space hasGroupoid_model_space

/-- Any charted space structure is compatible with the groupoid of all partial homeomorphisms. -/
instance hasGroupoid_continuousGroupoid : HasGroupoid M (continuousGroupoid H) := by
  refine âŸ¨fun _ _ â†¦ ?_âŸ©
  rw [continuousGroupoid, mem_groupoid_of_pregroupoid]
  simp only [and_self_iff]
#align has_groupoid_continuous_groupoid hasGroupoid_continuousGroupoid

/-- If `G` is closed under restriction, the transition function between
  the restriction of two charts `e` and `e'` lies in `G`. -/
theorem StructureGroupoid.trans_restricted {e e' : PartialHomeomorph M H} {G : StructureGroupoid H}
    (he : e âˆˆ atlas H M) (he' : e' âˆˆ atlas H M)
    [HasGroupoid M G] [ClosedUnderRestriction G] {s : Opens M} (hs : Nonempty s) :
    (e.subtypeRestr hs).symm â‰«â‚• e'.subtypeRestr hs âˆˆ G :=
  G.mem_of_eqOnSource (closedUnderRestriction' (G.compatible he he')
    (e.isOpen_inter_preimage_symm s.2)) (e.subtypeRestr_symm_trans_subtypeRestr hs e')

section MaximalAtlas

variable (M) (G : StructureGroupoid H)

/-- Given a charted space admitting a structure groupoid, the maximal atlas associated to this
structure groupoid is the set of all charts that are compatible with the atlas, i.e., such
that changing coordinates with an atlas member gives an element of the groupoid. -/
def StructureGroupoid.maximalAtlas : Set (PartialHomeomorph M H) :=
  { e | âˆ€ e' âˆˆ atlas H M, e.symm â‰«â‚• e' âˆˆ G âˆ§ e'.symm â‰«â‚• e âˆˆ G }
#align structure_groupoid.maximal_atlas StructureGroupoid.maximalAtlas

variable {M}

/-- The elements of the atlas belong to the maximal atlas for any structure groupoid. -/
theorem StructureGroupoid.subset_maximalAtlas [HasGroupoid M G] : atlas H M âŠ† G.maximalAtlas M :=
  fun _ he _ he' â†¦ âŸ¨G.compatible he he', G.compatible he' heâŸ©
#align structure_groupoid.subset_maximal_atlas StructureGroupoid.subset_maximalAtlas

theorem StructureGroupoid.chart_mem_maximalAtlas [HasGroupoid M G] (x : M) :
    chartAt H x âˆˆ G.maximalAtlas M :=
  G.subset_maximalAtlas (chart_mem_atlas H x)
#align structure_groupoid.chart_mem_maximal_atlas StructureGroupoid.chart_mem_maximalAtlas

variable {G}

theorem mem_maximalAtlas_iff {e : PartialHomeomorph M H} :
    e âˆˆ G.maximalAtlas M â†” âˆ€ e' âˆˆ atlas H M, e.symm â‰«â‚• e' âˆˆ G âˆ§ e'.symm â‰«â‚• e âˆˆ G :=
  Iff.rfl
#align mem_maximal_atlas_iff mem_maximalAtlas_iff

/-- Changing coordinates between two elements of the maximal atlas gives rise to an element
of the structure groupoid. -/
theorem StructureGroupoid.compatible_of_mem_maximalAtlas {e e' : PartialHomeomorph M H}
    (he : e âˆˆ G.maximalAtlas M) (he' : e' âˆˆ G.maximalAtlas M) : e.symm â‰«â‚• e' âˆˆ G := by
  refine G.locality fun x hx â†¦ ?_
  set f := chartAt (H := H) (e.symm x)
  let s := e.target âˆ© e.symm â»Â¹' f.source
  have hs : IsOpen s := by
    apply e.symm.continuousOn_toFun.isOpen_inter_preimage <;> apply open_source
  have xs : x âˆˆ s := by
    simp only [s, f, mem_inter_iff, mem_preimage, mem_chart_source, and_true]
    exact ((mem_inter_iff _ _ _).1 hx).1
  refine âŸ¨s, hs, xs, ?_âŸ©
  have A : e.symm â‰«â‚• f âˆˆ G := (mem_maximalAtlas_iff.1 he f (chart_mem_atlas _ _)).1
  have B : f.symm â‰«â‚• e' âˆˆ G := (mem_maximalAtlas_iff.1 he' f (chart_mem_atlas _ _)).2
  have C : (e.symm â‰«â‚• f) â‰«â‚• f.symm â‰«â‚• e' âˆˆ G := G.trans A B
  have D : (e.symm â‰«â‚• f) â‰«â‚• f.symm â‰«â‚• e' â‰ˆ (e.symm â‰«â‚• e').restr s := calc
    (e.symm â‰«â‚• f) â‰«â‚• f.symm â‰«â‚• e' = e.symm â‰«â‚• (f â‰«â‚• f.symm) â‰«â‚• e' := by simp only [trans_assoc]
    _ â‰ˆ e.symm â‰«â‚• ofSet f.source f.open_source â‰«â‚• e' :=
      EqOnSource.trans' (refl _) (EqOnSource.trans' (self_trans_symm _) (refl _))
    _ â‰ˆ (e.symm â‰«â‚• ofSet f.source f.open_source) â‰«â‚• e' := by rw [trans_assoc]
    _ â‰ˆ e.symm.restr s â‰«â‚• e' := by rw [trans_of_set']; apply refl
    _ â‰ˆ (e.symm â‰«â‚• e').restr s := by rw [restr_trans]
  exact G.mem_of_eqOnSource C (Setoid.symm D)
#align structure_groupoid.compatible_of_mem_maximal_atlas StructureGroupoid.compatible_of_mem_maximalAtlas

open PartialHomeomorph in
/-- The maximal atlas of a structure groupoid is stable under equivalence. -/
lemma StructureGroupoid.mem_maximalAtlas_of_eqOnSource {e e' : PartialHomeomorph M H} (h : e' â‰ˆ e)
    (he : e âˆˆ G.maximalAtlas M) : e' âˆˆ G.maximalAtlas M := by
  intro e'' he''
  obtain âŸ¨l, râŸ© := mem_maximalAtlas_iff.mp he e'' he''
  exact âŸ¨G.mem_of_eqOnSource l (EqOnSource.trans' (EqOnSource.symm' h) (e''.eqOnSource_refl)),
         G.mem_of_eqOnSource r (EqOnSource.trans' (e''.symm).eqOnSource_refl h)âŸ©

variable (G)

/-- In the model space, the identity is in any maximal atlas. -/
theorem StructureGroupoid.id_mem_maximalAtlas : PartialHomeomorph.refl H âˆˆ G.maximalAtlas H :=
  G.subset_maximalAtlas <| by simp
#align structure_groupoid.id_mem_maximal_atlas StructureGroupoid.id_mem_maximalAtlas

/-- In the model space, any element of the groupoid is in the maximal atlas. -/
theorem StructureGroupoid.mem_maximalAtlas_of_mem_groupoid {f : PartialHomeomorph H H}
    (hf : f âˆˆ G) : f âˆˆ G.maximalAtlas H := by
  rintro e (rfl : e = PartialHomeomorph.refl H)
  exact âŸ¨G.trans (G.symm hf) G.id_mem, G.trans (G.symm G.id_mem) hfâŸ©
#align structure_groupoid.mem_maximal_atlas_of_mem_groupoid StructureGroupoid.mem_maximalAtlas_of_mem_groupoid

end MaximalAtlas

section Singleton

variable {Î± : Type*} [TopologicalSpace Î±]

namespace PartialHomeomorph

variable (e : PartialHomeomorph Î± H)

/-- If a single partial homeomorphism `e` from a space `Î±` into `H` has source covering the whole
space `Î±`, then that partial homeomorphism induces an `H`-charted space structure on `Î±`.
(This condition is equivalent to `e` being an open embedding of `Î±` into `H`; see
`OpenEmbedding.singletonChartedSpace`.) -/
def singletonChartedSpace (h : e.source = Set.univ) : ChartedSpace H Î± where
  atlas := {e}
  chartAt _ := e
  mem_chart_source _ := by rw [h]; apply mem_univ
  chart_mem_atlas _ := by tauto
#align local_homeomorph.singleton_charted_space PartialHomeomorph.singletonChartedSpace

@[simp, mfld_simps]
theorem singletonChartedSpace_chartAt_eq (h : e.source = Set.univ) {x : Î±} :
    @chartAt H _ Î± _ (e.singletonChartedSpace h) x = e :=
  rfl
#align local_homeomorph.singleton_charted_space_chart_at_eq PartialHomeomorph.singletonChartedSpace_chartAt_eq

theorem singletonChartedSpace_chartAt_source (h : e.source = Set.univ) {x : Î±} :
    (@chartAt H _ Î± _ (e.singletonChartedSpace h) x).source = Set.univ :=
  h
#align local_homeomorph.singleton_charted_space_chart_at_source PartialHomeomorph.singletonChartedSpace_chartAt_source

theorem singletonChartedSpace_mem_atlas_eq (h : e.source = Set.univ) (e' : PartialHomeomorph Î± H)
    (h' : e' âˆˆ (e.singletonChartedSpace h).atlas) : e' = e :=
  h'
#align local_homeomorph.singleton_charted_space_mem_atlas_eq PartialHomeomorph.singletonChartedSpace_mem_atlas_eq

/-- Given a partial homeomorphism `e` from a space `Î±` into `H`, if its source covers the whole
space `Î±`, then the induced charted space structure on `Î±` is `HasGroupoid G` for any structure
groupoid `G` which is closed under restrictions. -/
theorem singleton_hasGroupoid (h : e.source = Set.univ) (G : StructureGroupoid H)
    [ClosedUnderRestriction G] : @HasGroupoid _ _ _ _ (e.singletonChartedSpace h) G :=
  { __ := e.singletonChartedSpace h
    compatible := by
      intro e' e'' he' he''
      rw [e.singletonChartedSpace_mem_atlas_eq h e' he',
        e.singletonChartedSpace_mem_atlas_eq h e'' he'']
      refine G.mem_of_eqOnSource ?_ e.symm_trans_self
      have hle : idRestrGroupoid â‰¤ G := (closedUnderRestriction_iff_id_le G).mp (by assumption)
      exact StructureGroupoid.le_iff.mp hle _ (idRestrGroupoid_mem _) }
#align local_homeomorph.singleton_has_groupoid PartialHomeomorph.singleton_hasGroupoid

end PartialHomeomorph

namespace OpenEmbedding

variable [Nonempty Î±]

/-- An open embedding of `Î±` into `H` induces an `H`-charted space structure on `Î±`.
See `PartialHomeomorph.singletonChartedSpace`. -/
def singletonChartedSpace {f : Î± â†’ H} (h : OpenEmbedding f) : ChartedSpace H Î± :=
  (h.toPartialHomeomorph f).singletonChartedSpace (toPartialHomeomorph_source _ _)
#align open_embedding.singleton_charted_space OpenEmbedding.singletonChartedSpace

theorem singletonChartedSpace_chartAt_eq {f : Î± â†’ H} (h : OpenEmbedding f) {x : Î±} :
    â‡‘(@chartAt H _ Î± _ h.singletonChartedSpace x) = f :=
  rfl
#align open_embedding.singleton_charted_space_chart_at_eq OpenEmbedding.singletonChartedSpace_chartAt_eq

theorem singleton_hasGroupoid {f : Î± â†’ H} (h : OpenEmbedding f) (G : StructureGroupoid H)
    [ClosedUnderRestriction G] : @HasGroupoid _ _ _ _ h.singletonChartedSpace G :=
  (h.toPartialHomeomorph f).singleton_hasGroupoid (toPartialHomeomorph_source _ _) G
#align open_embedding.singleton_has_groupoid OpenEmbedding.singleton_hasGroupoid

end OpenEmbedding

end Singleton

namespace TopologicalSpace.Opens

open TopologicalSpace

variable (G : StructureGroupoid H) [HasGroupoid M G]
variable (s : Opens M)

/-- An open subset of a charted space is naturally a charted space. -/
protected instance instChartedSpace : ChartedSpace H s where
  atlas := â‹ƒ x : s, {(chartAt H x.1).subtypeRestr âŸ¨xâŸ©}
  chartAt x := (chartAt H x.1).subtypeRestr âŸ¨xâŸ©
  mem_chart_source x := âŸ¨trivial, mem_chart_source H x.1âŸ©
  chart_mem_atlas x := by
    simp only [mem_iUnion, mem_singleton_iff]
    use x
#align topological_space.opens.charted_space TopologicalSpace.Opens.instChartedSpace

/-- If `s` is a non-empty open subset of `M`, every chart of `s` is the restriction
 of some chart on `M`. -/
lemma chart_eq {s : Opens M} (hs : Nonempty s) {e : PartialHomeomorph s H} (he : e âˆˆ atlas H s) :
    âˆƒ x : s, e = (chartAt H (x : M)).subtypeRestr hs := by
  rcases he with âŸ¨xset, âŸ¨x, hxâŸ©, heâŸ©
  exact âŸ¨x, mem_singleton_iff.mp (by convert he)âŸ©

/-- If `t` is a non-empty open subset of `H`,
  every chart of `t` is the restriction of some chart on `H`. -/
-- XXX: can I unify this with `chart_eq`?
lemma chart_eq' {t : Opens H} (ht : Nonempty t) {e' : PartialHomeomorph t H}
    (he' : e' âˆˆ atlas H t) : âˆƒ x : t, e' = (chartAt H â†‘x).subtypeRestr ht := by
  rcases he' with âŸ¨xset, âŸ¨x, hxâŸ©, he'âŸ©
  exact âŸ¨x, mem_singleton_iff.mp (by convert he')âŸ©

/-- If a groupoid `G` is `ClosedUnderRestriction`, then an open subset of a space which is
`HasGroupoid G` is naturally `HasGroupoid G`. -/
protected instance instHasGroupoid [ClosedUnderRestriction G] : HasGroupoid s G where
  compatible := by
    rintro e e' âŸ¨_, âŸ¨x, hcâŸ©, heâŸ© âŸ¨_, âŸ¨x', hc'âŸ©, he'âŸ©
    rw [hc.symm, mem_singleton_iff] at he
    rw [hc'.symm, mem_singleton_iff] at he'
    rw [he, he']
    refine G.mem_of_eqOnSource ?_
      (subtypeRestr_symm_trans_subtypeRestr (s := s) _ (chartAt H x) (chartAt H x'))
    apply closedUnderRestriction'
    Â· exact G.compatible (chart_mem_atlas _ _) (chart_mem_atlas _ _)
    Â· exact isOpen_inter_preimage_symm (chartAt _ _) s.2
#align topological_space.opens.has_groupoid TopologicalSpace.Opens.instHasGroupoid

theorem chartAt_subtype_val_symm_eventuallyEq (U : Opens M) {x : U} :
    (chartAt H x.val).symm =á¶ [ğ“ (chartAt H x.val x.val)] Subtype.val âˆ˜ (chartAt H x).symm := by
  set e := chartAt H x.val
  have heUx_nhds : (e.subtypeRestr âŸ¨xâŸ©).target âˆˆ ğ“ (e x) := by
    apply (e.subtypeRestr âŸ¨xâŸ©).open_target.mem_nhds
    exact e.map_subtype_source âŸ¨xâŸ© (mem_chart_source _ _)
  exact Filter.eventuallyEq_of_mem heUx_nhds (e.subtypeRestr_symm_eqOn âŸ¨xâŸ©)

theorem chartAt_inclusion_symm_eventuallyEq {U V : Opens M} (hUV : U â‰¤ V) {x : U} :
    (chartAt H (Set.inclusion hUV x)).symm
    =á¶ [ğ“ (chartAt H (Set.inclusion hUV x) (Set.inclusion hUV x))]
    Set.inclusion hUV âˆ˜ (chartAt H x).symm := by
  set e := chartAt H (x : M)
  have heUx_nhds : (e.subtypeRestr âŸ¨xâŸ©).target âˆˆ ğ“ (e x) := by
    apply (e.subtypeRestr âŸ¨xâŸ©).open_target.mem_nhds
    exact e.map_subtype_source âŸ¨xâŸ© (mem_chart_source _ _)
  exact Filter.eventuallyEq_of_mem heUx_nhds <| e.subtypeRestr_symm_eqOn_of_le âŸ¨xâŸ©
    âŸ¨Set.inclusion hUV xâŸ© hUV
#align topological_space.opens.chart_at_inclusion_symm_eventually_eq TopologicalSpace.Opens.chartAt_inclusion_symm_eventuallyEq
end TopologicalSpace.Opens

/-- Restricting a chart of `M` to an open subset `s` yields a chart in the maximal atlas of `s`.

NB. We cannot deduce membership in `atlas H s` in general: by definition, this atlas contains
precisely the restriction of each preferred chart at `x âˆˆ s` --- whereas `atlas H M`
can contain more charts than these. -/
lemma StructureGroupoid.restriction_in_maximalAtlas {e : PartialHomeomorph M H}
    (he : e âˆˆ atlas H M) {s : Opens M} (hs : Nonempty s) {G : StructureGroupoid H} [HasGroupoid M G]
    [ClosedUnderRestriction G] : e.subtypeRestr hs âˆˆ G.maximalAtlas s := by
  intro e' he'
  -- `e'` is the restriction of some chart of `M` at `x`,
  obtain âŸ¨x, thisâŸ© := Opens.chart_eq hs he'
  rw [this]
  -- The transition functions between the unrestricted charts lie in the groupoid,
  -- the transition functions of the restriction are the restriction of the transition function.
  exact âŸ¨G.trans_restricted he (chart_mem_atlas H (x : M)) hs,
         G.trans_restricted (chart_mem_atlas H (x : M)) he hsâŸ©

/-! ### Structomorphisms -/

/-- A `G`-diffeomorphism between two charted spaces is a homeomorphism which, when read in the
charts, belongs to `G`. We avoid the word diffeomorph as it is too related to the smooth category,
and use structomorph instead. -/
-- Porting note(#5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
structure Structomorph (G : StructureGroupoid H) (M : Type*) (M' : Type*) [TopologicalSpace M]
  [TopologicalSpace M'] [ChartedSpace H M] [ChartedSpace H M'] extends Homeomorph M M' where
  mem_groupoid : âˆ€ c : PartialHomeomorph M H, âˆ€ c' : PartialHomeomorph M' H, c âˆˆ atlas H M â†’
    c' âˆˆ atlas H M' â†’ c.symm â‰«â‚• toHomeomorph.toPartialHomeomorph â‰«â‚• c' âˆˆ G
#align structomorph Structomorph

variable [TopologicalSpace M'] [TopologicalSpace M''] {G : StructureGroupoid H} [ChartedSpace H M']
  [ChartedSpace H M'']

/-- The identity is a diffeomorphism of any charted space, for any groupoid. -/
def Structomorph.refl (M : Type*) [TopologicalSpace M] [ChartedSpace H M] [HasGroupoid M G] :
    Structomorph G M M :=
  { Homeomorph.refl M with
    mem_groupoid := fun c c' hc hc' â†¦ by
      change PartialHomeomorph.symm c â‰«â‚• PartialHomeomorph.refl M â‰«â‚• c' âˆˆ G
      rw [PartialHomeomorph.refl_trans]
      exact G.compatible hc hc' }
#align structomorph.refl Structomorph.refl

/-- The inverse of a structomorphism is a structomorphism. -/
def Structomorph.symm (e : Structomorph G M M') : Structomorph G M' M :=
  { e.toHomeomorph.symm with
    mem_groupoid := by
      intro c c' hc hc'
      have : (c'.symm â‰«â‚• e.toHomeomorph.toPartialHomeomorph â‰«â‚• c).symm âˆˆ G :=
        G.symm (e.mem_groupoid c' c hc' hc)
      rwa [trans_symm_eq_symm_trans_symm, trans_symm_eq_symm_trans_symm, symm_symm, trans_assoc]
        at this }
#align structomorph.symm Structomorph.symm

/-- The composition of structomorphisms is a structomorphism. -/
def Structomorph.trans (e : Structomorph G M M') (e' : Structomorph G M' M'') :
    Structomorph G M M'' :=
  { Homeomorph.trans e.toHomeomorph e'.toHomeomorph with
    mem_groupoid := by
      /- Let c and c' be two charts in M and M''. We want to show that e' âˆ˜ e is smooth in these
      charts, around any point x. For this, let y = e (câ»Â¹ x), and consider a chart g around y.
      Then g âˆ˜ e âˆ˜ câ»Â¹ and c' âˆ˜ e' âˆ˜ gâ»Â¹ are both smooth as e and e' are structomorphisms, so
      their composition is smooth, and it coincides with c' âˆ˜ e' âˆ˜ e âˆ˜ câ»Â¹ around x. -/
      intro c c' hc hc'
      refine G.locality fun x hx â†¦ ?_
      let fâ‚ := e.toHomeomorph.toPartialHomeomorph
      let fâ‚‚ := e'.toHomeomorph.toPartialHomeomorph
      let f := (e.toHomeomorph.trans e'.toHomeomorph).toPartialHomeomorph
      have feq : f = fâ‚ â‰«â‚• fâ‚‚ := Homeomorph.trans_toPartialHomeomorph _ _
      -- define the atlas g around y
      let y := (c.symm â‰«â‚• fâ‚) x
      let g := chartAt (H := H) y
      have hgâ‚ := chart_mem_atlas (H := H) y
      have hgâ‚‚ := mem_chart_source (H := H) y
      let s := (c.symm â‰«â‚• fâ‚).source âˆ© c.symm â‰«â‚• fâ‚ â»Â¹' g.source
      have open_s : IsOpen s := by
        apply (c.symm â‰«â‚• fâ‚).continuousOn_toFun.isOpen_inter_preimage <;> apply open_source
      have : x âˆˆ s := by
        constructor
        Â· simp only [fâ‚, trans_source, preimage_univ, inter_univ,
            Homeomorph.toPartialHomeomorph_source]
          rw [trans_source] at hx
          exact hx.1
        Â· exact hgâ‚‚
      refine âŸ¨s, open_s, this, ?_âŸ©
      let Fâ‚ := (c.symm â‰«â‚• fâ‚ â‰«â‚• g) â‰«â‚• g.symm â‰«â‚• fâ‚‚ â‰«â‚• c'
      have A : Fâ‚ âˆˆ G := G.trans (e.mem_groupoid c g hc hgâ‚) (e'.mem_groupoid g c' hgâ‚ hc')
      let Fâ‚‚ := (c.symm â‰«â‚• f â‰«â‚• c').restr s
      have : Fâ‚ â‰ˆ Fâ‚‚ := calc
        Fâ‚ â‰ˆ c.symm â‰«â‚• fâ‚ â‰«â‚• (g â‰«â‚• g.symm) â‰«â‚• fâ‚‚ â‰«â‚• c' := by
            simp only [Fâ‚, trans_assoc, _root_.refl]
        _ â‰ˆ c.symm â‰«â‚• fâ‚ â‰«â‚• ofSet g.source g.open_source â‰«â‚• fâ‚‚ â‰«â‚• c' :=
          EqOnSource.trans' (_root_.refl _) (EqOnSource.trans' (_root_.refl _)
            (EqOnSource.trans' (self_trans_symm g) (_root_.refl _)))
        _ â‰ˆ ((c.symm â‰«â‚• fâ‚) â‰«â‚• ofSet g.source g.open_source) â‰«â‚• fâ‚‚ â‰«â‚• c' := by
          simp only [trans_assoc, _root_.refl]
        _ â‰ˆ (c.symm â‰«â‚• fâ‚).restr s â‰«â‚• fâ‚‚ â‰«â‚• c' := by rw [trans_of_set']
        _ â‰ˆ ((c.symm â‰«â‚• fâ‚) â‰«â‚• fâ‚‚ â‰«â‚• c').restr s := by rw [restr_trans]
        _ â‰ˆ (c.symm â‰«â‚• (fâ‚ â‰«â‚• fâ‚‚) â‰«â‚• c').restr s := by
          simp only [EqOnSource.restr, trans_assoc, _root_.refl]
        _ â‰ˆ Fâ‚‚ := by simp only [Fâ‚‚, feq, _root_.refl]
      have : Fâ‚‚ âˆˆ G := G.mem_of_eqOnSource A (Setoid.symm this)
      exact this }
#align structomorph.trans Structomorph.trans

/-- Restricting a chart to its source `s âŠ† M` yields a chart in the maximal atlas of `s`. -/
theorem StructureGroupoid.restriction_mem_maximalAtlas_subtype
    {e : PartialHomeomorph M H} (he : e âˆˆ atlas H M)
    (hs : Nonempty e.source) [HasGroupoid M G] [ClosedUnderRestriction G] :
    let s := { carrier := e.source, is_open' := e.open_source : Opens M };
    let t := { carrier := e.target, is_open' := e.open_target : Opens H };
    âˆ€ c' âˆˆ atlas H t, e.toHomeomorphSourceTarget.toPartialHomeomorph â‰«â‚• c' âˆˆ G.maximalAtlas s := by
  intro s t c' hc'
  have : Nonempty t := nonempty_coe_sort.mpr (e.mapsTo.nonempty (nonempty_coe_sort.mp hs))
  obtain âŸ¨x, hc'âŸ© := Opens.chart_eq this hc'
  -- As H has only one chart, `chartAt H x` is the identity: i.e., `c'` is the inclusion.
  rw [hc', (chartAt_self_eq)]
  -- Our expression equals this chart, at least on its source.
  rw [PartialHomeomorph.subtypeRestr_def, PartialHomeomorph.trans_refl]
  let goal := e.toHomeomorphSourceTarget.toPartialHomeomorph â‰«â‚• (t.partialHomeomorphSubtypeCoe this)
  have : goal â‰ˆ e.subtypeRestr (s := s) hs :=
    (goal.eqOnSource_iff (e.subtypeRestr (s := s) hs)).mpr âŸ¨by simp [s, goal], by intro _ _; rflâŸ©
  exact G.mem_maximalAtlas_of_eqOnSource (M := s) this (G.restriction_in_maximalAtlas he hs)

/-- Each chart of a charted space is a structomorphism between its source and target. -/
def PartialHomeomorph.toStructomorph {e : PartialHomeomorph M H} (he : e âˆˆ atlas H M)
    [HasGroupoid M G] [ClosedUnderRestriction G] :
    let s : Opens M := { carrier := e.source, is_open' := e.open_source }
    let t : Opens H := { carrier := e.target, is_open' := e.open_target }
    Structomorph G s t := by
  intro s t
  by_cases h : Nonempty e.source
  Â· exact { e.toHomeomorphSourceTarget with
      mem_groupoid :=
        -- The atlas of H on itself has only one chart, hence c' is the inclusion.
        -- Then, compatibility of `G` *almost* yields our claim --- except that `e` is a chart
        -- on `M` and `c` is one on `s`: we need to show that restricting `e` to `s` and composing
        -- with `c'` yields a chart in the maximal atlas of `s`.
        fun c c' hc hc' â†¦ G.compatible_of_mem_maximalAtlas (G.subset_maximalAtlas hc)
          (G.restriction_mem_maximalAtlas_subtype he h c' hc') }
  Â· have : IsEmpty s := not_nonempty_iff.mp h
    have : IsEmpty t := isEmpty_coe_sort.mpr
      (by convert e.image_source_eq_target â–¸ image_eq_empty.mpr (isEmpty_coe_sort.mp this))
    exact { Homeomorph.empty with
      -- `c'` cannot exist: it would be the restriction of `chartAt H x` at some `x âˆˆ t`.
      mem_groupoid := fun _ c' _ âŸ¨_, âŸ¨x, _âŸ©, _âŸ© â†¦ (this.false x).elim }

end HasGroupoid
