/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Init.Align
import Mathlib.Topology.LocalHomeomorph

#align_import geometry.manifold.charted_space from "leanprover-community/mathlib"@"431589bce478b2229eba14b14a283250428217db"

/-!
# Charted spaces

A smooth manifold is a topological space `M` locally modelled on a euclidean space (or a euclidean
half-space for manifolds with boundaries, or an infinite dimensional vector space for more general
notions of manifolds), i.e., the manifold is covered by open subsets on which there are local
homeomorphisms (the charts) going to a model space `H`, and the changes of charts should be smooth
maps.

In this file, we introduce a general framework describing these notions, where the model space is an
arbitrary topological space. We avoid the word *manifold*, which should be reserved for the
situation where the model space is a (subset of a) vector space, and use the terminology
*charted space* instead.

If the changes of charts satisfy some additional property (for instance if they are smooth), then
`M` inherits additional structure (it makes sense to talk about smooth manifolds). There are
therefore two different ingredients in a charted space:
* the set of charts, which is data
* the fact that changes of charts belong to some group (in fact groupoid), which is additional Prop.

We separate these two parts in the definition: the charted space structure is just the set of
charts, and then the different smoothness requirements (smooth manifold, orientable manifold,
contact manifold, and so on) are additional properties of these charts. These properties are
formalized through the notion of structure groupoid, i.e., a set of local homeomorphisms stable
under composition and inverse, to which the change of coordinates should belong.

## Main definitions

* `StructureGroupoid H` : a subset of local homeomorphisms of `H` stable under composition,
  inverse and restriction (ex: local diffeos).
* `continuousGroupoid H` : the groupoid of all local homeomorphisms of `H`.
* `ChartedSpace H M` : charted space structure on `M` modelled on `H`, given by an atlas of
  local homeomorphisms from `M` to `H` whose sources cover `M`. This is a type class.
* `HasGroupoid M G` : when `G` is a structure groupoid on `H` and `M` is a charted space
  modelled on `H`, require that all coordinate changes belong to `G`. This is a type class.
* `atlas H M` : when `M` is a charted space modelled on `H`, the atlas of this charted
  space structure, i.e., the set of charts.
* `G.maximalAtlas M` : when `M` is a charted space modelled on `H` and admitting `G` as a
  structure groupoid, one can consider all the local homeomorphisms from `M` to `H` such that
  changing coordinate from any chart to them belongs to `G`. This is a larger atlas, called the
  maximal atlas (for the groupoid `G`).
* `Structomorph G M M'` : the type of diffeomorphisms between the charted spaces `M` and `M'` for
  the groupoid `G`. We avoid the word diffeomorphism, keeping it for the smooth category.

As a basic example, we give the instance
`instance chartedSpaceSelf (H : Type*) [TopologicalSpace H] : ChartedSpace H H`
saying that a topological space is a charted space over itself, with the identity as unique chart.
This charted space structure is compatible with any groupoid.

Additional useful definitions:

* `Pregroupoid H` : a subset of local maps of `H` stable under composition and
  restriction, but not inverse (ex: smooth maps)
* `Pregroupoid.groupoid` : construct a groupoid from a pregroupoid, by requiring that a map and
  its inverse both belong to the pregroupoid (ex: construct diffeos from smooth maps)
* `chartAt H x` is a preferred chart at `x : M` when `M` has a charted space structure modelled on
  `H`.
* `G.compatible he he'` states that, for any two charts `e` and `e'` in the atlas, the composition
  of `e.symm` and `e'` belongs to the groupoid `G` when `M` admits `G` as a structure groupoid.
* `G.compatible_of_mem_maximalAtlas he he'` states that, for any two charts `e` and `e'` in the
  maximal atlas associated to the groupoid `G`, the composition of `e.symm` and `e'` belongs to the
  `G` if `M` admits `G` as a structure groupoid.
* `ChartedSpaceCore.toChartedSpace`: consider a space without a topology, but endowed with a set
  of charts (which are local equivs) for which the change of coordinates are local homeos. Then
  one can construct a topology on the space for which the charts become local homeos, defining
  a genuine charted space structure.

## Implementation notes

The atlas in a charted space is *not* a maximal atlas in general: the notion of maximality depends
on the groupoid one considers, and changing groupoids changes the maximal atlas. With the current
formalization, it makes sense first to choose the atlas, and then to ask whether this precise atlas
defines a smooth manifold, an orientable manifold, and so on. A consequence is that structomorphisms
between `M` and `M'` do *not* induce a bijection between the atlases of `M` and `M'`: the
definition is only that, read in charts, the structomorphism locally belongs to the groupoid under
consideration. (This is equivalent to inducing a bijection between elements of the maximal atlas).
A consequence is that the invariance under structomorphisms of properties defined in terms of the
atlas is not obvious in general, and could require some work in theory (amounting to the fact
that these properties only depend on the maximal atlas, for instance). In practice, this does not
create any real difficulty.

We use the letter `H` for the model space thinking of the case of manifolds with boundary, where the
model space is a half space.

Manifolds are sometimes defined as topological spaces with an atlas of local diffeomorphisms, and
sometimes as spaces with an atlas from which a topology is deduced. We use the former approach:
otherwise, there would be an instance from manifolds to topological spaces, which means that any
instance search for topological spaces would try to find manifold structures involving a yet
unknown model space, leading to problems. However, we also introduce the latter approach,
through a structure `ChartedSpaceCore` making it possible to construct a topology out of a set of
local equivs with compatibility conditions (but we do not register it as an instance).

In the definition of a charted space, the model space is written as an explicit parameter as there
can be several model spaces for a given topological space. For instance, a complex manifold
(modelled over `‚ÑÇ^n`) will also be seen sometimes as a real manifold modelled over `‚Ñù^(2n)`.

## Notations

In the locale `Manifold`, we denote the composition of local homeomorphisms with `‚â´‚Çï`, and the
composition of local equivs with `‚â´`.
-/

set_option autoImplicit true


noncomputable section

open Classical Topology Filter

universe u

variable {H : Type u} {H' : Type*} {M : Type*} {M' : Type*} {M'' : Type*}

/- Notational shortcut for the composition of local homeomorphisms and local equivs, i.e.,
`LocalHomeomorph.trans` and `LocalEquiv.trans`.
Note that, as is usual for equivs, the composition is from left to right, hence the direction of
the arrow. -/
scoped[Manifold] infixr:100 " ‚â´‚Çï " => LocalHomeomorph.trans

scoped[Manifold] infixr:100 " ‚â´ " => LocalEquiv.trans

open Set LocalHomeomorph Manifold  -- Porting note: Added `Manifold`

/-! ### Structure groupoids -/


section Groupoid

/-! One could add to the definition of a structure groupoid the fact that the restriction of an
element of the groupoid to any open set still belongs to the groupoid.
(This is in Kobayashi-Nomizu.)
I am not sure I want this, for instance on `H √ó E` where `E` is a vector space, and the groupoid is
made of functions respecting the fibers and linear in the fibers (so that a charted space over this
groupoid is naturally a vector bundle) I prefer that the members of the groupoid are always
defined on sets of the form `s √ó E`. There is a typeclass `ClosedUnderRestriction` for groupoids
which have the restriction property.

The only nontrivial requirement is locality: if a local homeomorphism belongs to the groupoid
around each point in its domain of definition, then it belongs to the groupoid. Without this
requirement, the composition of structomorphisms does not have to be a structomorphism. Note that
this implies that a local homeomorphism with empty source belongs to any structure groupoid, as
it trivially satisfies this condition.

There is also a technical point, related to the fact that a local homeomorphism is by definition a
global map which is a homeomorphism when restricted to its source subset (and its values outside
of the source are not relevant). Therefore, we also require that being a member of the groupoid only
depends on the values on the source.

We use primes in the structure names as we will reformulate them below (without primes) using a
`Membership` instance, writing `e ‚àà G` instead of `e ‚àà G.members`.
-/


/-- A structure groupoid is a set of local homeomorphisms of a topological space stable under
composition and inverse. They appear in the definition of the smoothness class of a manifold. -/
structure StructureGroupoid (H : Type u) [TopologicalSpace H] where
  members : Set (LocalHomeomorph H H)
  trans' : ‚àÄ e e' : LocalHomeomorph H H, e ‚àà members ‚Üí e' ‚àà members ‚Üí e ‚â´‚Çï e' ‚àà members
  symm' : ‚àÄ e : LocalHomeomorph H H, e ‚àà members ‚Üí e.symm ‚àà members
  id_mem' : LocalHomeomorph.refl H ‚àà members
  locality' : ‚àÄ e : LocalHomeomorph H H,
    (‚àÄ x ‚àà e.source, ‚àÉ s, IsOpen s ‚àß x ‚àà s ‚àß e.restr s ‚àà members) ‚Üí e ‚àà members
  eq_on_source' : ‚àÄ e e' : LocalHomeomorph H H, e ‚àà members ‚Üí e' ‚âà e ‚Üí e' ‚àà members
#align structure_groupoid StructureGroupoid

variable [TopologicalSpace H]

instance : Membership (LocalHomeomorph H H) (StructureGroupoid H) :=
  ‚ü®fun (e : LocalHomeomorph H H) (G : StructureGroupoid H) ‚Ü¶ e ‚àà G.members‚ü©

theorem StructureGroupoid.trans (G : StructureGroupoid H) {e e' : LocalHomeomorph H H} (he : e ‚àà G)
    (he' : e' ‚àà G) : e ‚â´‚Çï e' ‚àà G :=
  G.trans' e e' he he'
#align structure_groupoid.trans StructureGroupoid.trans

theorem StructureGroupoid.symm (G : StructureGroupoid H) {e : LocalHomeomorph H H} (he : e ‚àà G) :
    e.symm ‚àà G :=
  G.symm' e he
#align structure_groupoid.symm StructureGroupoid.symm

theorem StructureGroupoid.id_mem (G : StructureGroupoid H) : LocalHomeomorph.refl H ‚àà G :=
  G.id_mem'
#align structure_groupoid.id_mem StructureGroupoid.id_mem

theorem StructureGroupoid.locality (G : StructureGroupoid H) {e : LocalHomeomorph H H}
    (h : ‚àÄ x ‚àà e.source, ‚àÉ s, IsOpen s ‚àß x ‚àà s ‚àß e.restr s ‚àà G) : e ‚àà G :=
  G.locality' e h
#align structure_groupoid.locality StructureGroupoid.locality

theorem StructureGroupoid.eq_on_source (G : StructureGroupoid H) {e e' : LocalHomeomorph H H}
    (he : e ‚àà G) (h : e' ‚âà e) : e' ‚àà G :=
  G.eq_on_source' e e' he h
#align structure_groupoid.eq_on_source StructureGroupoid.eq_on_source

/-- Partial order on the set of groupoids, given by inclusion of the members of the groupoid. -/
instance StructureGroupoid.partialOrder : PartialOrder (StructureGroupoid H) :=
  PartialOrder.lift StructureGroupoid.members fun a b h ‚Ü¶ by
    cases a
    cases b
    dsimp at h
    induction h
    rfl
#align structure_groupoid.partial_order StructureGroupoid.partialOrder

theorem StructureGroupoid.le_iff {G‚ÇÅ G‚ÇÇ : StructureGroupoid H} : G‚ÇÅ ‚â§ G‚ÇÇ ‚Üî ‚àÄ e, e ‚àà G‚ÇÅ ‚Üí e ‚àà G‚ÇÇ :=
  Iff.rfl
#align structure_groupoid.le_iff StructureGroupoid.le_iff

/-- The trivial groupoid, containing only the identity (and maps with empty source, as this is
necessary from the definition). -/
def idGroupoid (H : Type u) [TopologicalSpace H] : StructureGroupoid H where
  members := {LocalHomeomorph.refl H} ‚à™ { e : LocalHomeomorph H H | e.source = ‚àÖ }
  trans' e e' he he' := by
    cases' he with he he
    ¬∑ simpa only [mem_singleton_iff.1 he, refl_trans]
    ¬∑ have : (e ‚â´‚Çï e').source ‚äÜ e.source := sep_subset _ _
      rw [he] at this
      have : e ‚â´‚Çï e' ‚àà { e : LocalHomeomorph H H | e.source = ‚àÖ } := eq_bot_iff.2 this
      exact (mem_union _ _ _).2 (Or.inr this)
  symm' e he := by
    cases' (mem_union _ _ _).1 he with E E
    ¬∑ simp [mem_singleton_iff.mp E]
    ¬∑ right
      simpa only [e.toLocalEquiv.image_source_eq_target.symm, mfld_simps] using E
  id_mem' := mem_union_left _ rfl
  locality' e he := by
    cases' e.source.eq_empty_or_nonempty with h h
    ¬∑ right
      exact h
    ¬∑ left
      rcases h with ‚ü®x, hx‚ü©
      rcases he x hx with ‚ü®s, open_s, xs, hs‚ü©
      have x's : x ‚àà (e.restr s).source := by
        rw [restr_source, open_s.interior_eq]
        exact ‚ü®hx, xs‚ü©
      cases' hs with hs hs
      ¬∑ replace hs : LocalHomeomorph.restr e s = LocalHomeomorph.refl H
        ¬∑ simpa only using hs
        have : (e.restr s).source = univ := by
          rw [hs]
          simp
        have : e.toLocalEquiv.source ‚à© interior s = univ := this
        have : univ ‚äÜ interior s := by
          rw [‚Üê this]
          exact inter_subset_right _ _
        have : s = univ := by rwa [open_s.interior_eq, univ_subset_iff] at this
        simpa only [this, restr_univ] using hs
      ¬∑ exfalso
        rw [mem_setOf_eq] at hs
        rwa [hs] at x's
  eq_on_source' e e' he he'e := by
    cases' he with he he
    ¬∑ left
      have : e = e' := by
        refine' eq_of_eq_on_source_univ (Setoid.symm he'e) _ _ <;>
          rw [Set.mem_singleton_iff.1 he] <;> rfl
      rwa [‚Üê this]
    ¬∑ right
      have he : e.toLocalEquiv.source = ‚àÖ := he
      rwa [Set.mem_setOf_eq, EqOnSource.source_eq he'e]
#align id_groupoid idGroupoid

/-- Every structure groupoid contains the identity groupoid. -/
instance : OrderBot (StructureGroupoid H) where
  bot := idGroupoid H
  bot_le := by
    intro u f hf
    have hf : f ‚àà {LocalHomeomorph.refl H} ‚à™ { e : LocalHomeomorph H H | e.source = ‚àÖ } := hf
    simp only [singleton_union, mem_setOf_eq, mem_insert_iff] at hf
    cases' hf with hf hf
    ¬∑ rw [hf]
      apply u.id_mem
    ¬∑ apply u.locality
      intro x hx
      rw [hf, mem_empty_iff_false] at hx
      exact hx.elim

instance (H : Type u) [TopologicalSpace H] : Inhabited (StructureGroupoid H) :=
  ‚ü®idGroupoid H‚ü©

/-- To construct a groupoid, one may consider classes of local homeos such that both the function
and its inverse have some property. If this property is stable under composition,
one gets a groupoid. `Pregroupoid` bundles the properties needed for this construction, with the
groupoid of smooth functions with smooth inverses as an application. -/
structure Pregroupoid (H : Type*) [TopologicalSpace H] where
  property : (H ‚Üí H) ‚Üí Set H ‚Üí Prop
  comp : ‚àÄ {f g u v}, property f u ‚Üí property g v ‚Üí
    IsOpen u ‚Üí IsOpen v ‚Üí IsOpen (u ‚à© f ‚Åª¬π' v) ‚Üí property (g ‚àò f) (u ‚à© f ‚Åª¬π' v)
  id_mem : property id univ
  locality :
    ‚àÄ {f u}, IsOpen u ‚Üí (‚àÄ x ‚àà u, ‚àÉ v, IsOpen v ‚àß x ‚àà v ‚àß property f (u ‚à© v)) ‚Üí property f u
  congr : ‚àÄ {f g : H ‚Üí H} {u}, IsOpen u ‚Üí (‚àÄ x ‚àà u, g x = f x) ‚Üí property f u ‚Üí property g u
#align pregroupoid Pregroupoid

/-- Construct a groupoid of local homeos for which the map and its inverse have some property,
from a pregroupoid asserting that this property is stable under composition. -/
def Pregroupoid.groupoid (PG : Pregroupoid H) : StructureGroupoid H where
  members := { e : LocalHomeomorph H H | PG.property e e.source ‚àß PG.property e.symm e.target }
  trans' e e' he he' := by
    constructor
    ¬∑ apply PG.comp he.1 he'.1 e.open_source e'.open_source
      apply e.continuous_toFun.preimage_open_of_open e.open_source e'.open_source
    ¬∑ apply PG.comp he'.2 he.2 e'.open_target e.open_target
      apply e'.continuous_invFun.preimage_open_of_open e'.open_target e.open_target
  symm' e he := ‚ü®he.2, he.1‚ü©
  id_mem' := ‚ü®PG.id_mem, PG.id_mem‚ü©
  locality' e he := by
    constructor
    ¬∑ refine' PG.locality e.open_source fun x xu ‚Ü¶ _
      rcases he x xu with ‚ü®s, s_open, xs, hs‚ü©
      refine' ‚ü®s, s_open, xs, _‚ü©
      convert hs.1 using 1
      dsimp [LocalHomeomorph.restr]
      rw [s_open.interior_eq]
    ¬∑ refine' PG.locality e.open_target fun x xu ‚Ü¶ _
      rcases he (e.symm x) (e.map_target xu) with ‚ü®s, s_open, xs, hs‚ü©
      refine' ‚ü®e.target ‚à© e.symm ‚Åª¬π' s, _, ‚ü®xu, xs‚ü©, _‚ü©
      ¬∑ exact ContinuousOn.preimage_open_of_open e.continuous_invFun e.open_target s_open
      ¬∑ rw [‚Üê inter_assoc, inter_self]
        convert hs.2 using 1
        dsimp [LocalHomeomorph.restr]
        rw [s_open.interior_eq]
  eq_on_source' e e' he ee' := by
    constructor
    ¬∑ apply PG.congr e'.open_source ee'.2
      simp only [ee'.1, he.1]
    ¬∑ have A := EqOnSource.symm' ee'
      apply PG.congr e'.symm.open_source A.2
      -- Porting note: was
      -- convert he.2
      -- rw [A.1]
      -- rfl
      rw [A.1, symm_toLocalEquiv, LocalEquiv.symm_source]
      exact he.2
#align pregroupoid.groupoid Pregroupoid.groupoid

theorem mem_groupoid_of_pregroupoid {PG : Pregroupoid H} {e : LocalHomeomorph H H} :
    e ‚àà PG.groupoid ‚Üî PG.property e e.source ‚àß PG.property e.symm e.target :=
  Iff.rfl
#align mem_groupoid_of_pregroupoid mem_groupoid_of_pregroupoid

theorem groupoid_of_pregroupoid_le (PG‚ÇÅ PG‚ÇÇ : Pregroupoid H)
    (h : ‚àÄ f s, PG‚ÇÅ.property f s ‚Üí PG‚ÇÇ.property f s) : PG‚ÇÅ.groupoid ‚â§ PG‚ÇÇ.groupoid := by
  refine' StructureGroupoid.le_iff.2 fun e he ‚Ü¶ _
  rw [mem_groupoid_of_pregroupoid] at he ‚ä¢
  exact ‚ü®h _ _ he.1, h _ _ he.2‚ü©
#align groupoid_of_pregroupoid_le groupoid_of_pregroupoid_le

theorem mem_pregroupoid_of_eq_on_source (PG : Pregroupoid H) {e e' : LocalHomeomorph H H}
    (he' : e ‚âà e') (he : PG.property e e.source) : PG.property e' e'.source := by
  rw [‚Üê he'.1]
  exact PG.congr e.open_source he'.eqOn.symm he
#align mem_pregroupoid_of_eq_on_source mem_pregroupoid_of_eq_on_source

/-- The pregroupoid of all local maps on a topological space `H`. -/
@[reducible]
def continuousPregroupoid (H : Type*) [TopologicalSpace H] : Pregroupoid H where
  property _ _ := True
  comp _ _ _ _ _ := trivial
  id_mem := trivial
  locality _ _ := trivial
  congr _ _ _ := trivial
#align continuous_pregroupoid continuousPregroupoid

instance (H : Type*) [TopologicalSpace H] : Inhabited (Pregroupoid H) :=
  ‚ü®continuousPregroupoid H‚ü©

/-- The groupoid of all local homeomorphisms on a topological space `H`. -/
def continuousGroupoid (H : Type*) [TopologicalSpace H] : StructureGroupoid H :=
  Pregroupoid.groupoid (continuousPregroupoid H)
#align continuous_groupoid continuousGroupoid

/-- Every structure groupoid is contained in the groupoid of all local homeomorphisms. -/
instance : OrderTop (StructureGroupoid H) where
  top := continuousGroupoid H
  le_top _ _ _ := ‚ü®trivial, trivial‚ü©

/-- A groupoid is closed under restriction if it contains all restrictions of its element local
homeomorphisms to open subsets of the source. -/
class ClosedUnderRestriction (G : StructureGroupoid H) : Prop where
  closedUnderRestriction :
    ‚àÄ {e : LocalHomeomorph H H}, e ‚àà G ‚Üí ‚àÄ s : Set H, IsOpen s ‚Üí e.restr s ‚àà G
#align closed_under_restriction ClosedUnderRestriction

theorem closedUnderRestriction' {G : StructureGroupoid H} [ClosedUnderRestriction G]
    {e : LocalHomeomorph H H} (he : e ‚àà G) {s : Set H} (hs : IsOpen s) : e.restr s ‚àà G :=
  ClosedUnderRestriction.closedUnderRestriction he s hs
#align closed_under_restriction' closedUnderRestriction'

/-- The trivial restriction-closed groupoid, containing only local homeomorphisms equivalent to the
restriction of the identity to the various open subsets. -/
def idRestrGroupoid : StructureGroupoid H where
  members := { e | ‚àÉ (s : Set H) (h : IsOpen s), e ‚âà LocalHomeomorph.ofSet s h }
  trans' := by
    rintro e e' ‚ü®s, hs, hse‚ü© ‚ü®s', hs', hse'‚ü©
    refine' ‚ü®s ‚à© s', IsOpen.inter hs hs', _‚ü©
    have := LocalHomeomorph.EqOnSource.trans' hse hse'
    rwa [LocalHomeomorph.ofSet_trans_ofSet] at this
  symm' := by
    rintro e ‚ü®s, hs, hse‚ü©
    refine' ‚ü®s, hs, _‚ü©
    rw [‚Üê ofSet_symm]
    exact LocalHomeomorph.EqOnSource.symm' hse
  id_mem' := ‚ü®univ, isOpen_univ, by simp only [mfld_simps, refl]‚ü©
  locality' := by
    intro e h
    refine' ‚ü®e.source, e.open_source, by simp only [mfld_simps], _‚ü©
    intro x hx
    rcases h x hx with ‚ü®s, hs, hxs, s', hs', hes'‚ü©
    have hes : x ‚àà (e.restr s).source := by
      rw [e.restr_source]
      refine' ‚ü®hx, _‚ü©
      rw [hs.interior_eq]
      exact hxs
    simpa only [mfld_simps] using LocalHomeomorph.EqOnSource.eqOn hes' hes
  eq_on_source' := by
    rintro e e' ‚ü®s, hs, hse‚ü© hee'
    exact ‚ü®s, hs, Setoid.trans hee' hse‚ü©
#align id_restr_groupoid idRestrGroupoid

theorem idRestrGroupoid_mem {s : Set H} (hs : IsOpen s) : ofSet s hs ‚àà @idRestrGroupoid H _ :=
  ‚ü®s, hs, refl _‚ü©
#align id_restr_groupoid_mem idRestrGroupoid_mem

/-- The trivial restriction-closed groupoid is indeed `ClosedUnderRestriction`. -/
instance closedUnderRestriction_idRestrGroupoid : ClosedUnderRestriction (@idRestrGroupoid H _) :=
  ‚ü®by
    rintro e ‚ü®s', hs', he‚ü© s hs
    use s' ‚à© s, IsOpen.inter hs' hs
    refine' Setoid.trans (LocalHomeomorph.EqOnSource.restr he s) _
    exact ‚ü®by simp only [hs.interior_eq, mfld_simps], by simp only [mfld_simps, eqOn_refl]‚ü©‚ü©
#align closed_under_restriction_id_restr_groupoid closedUnderRestriction_idRestrGroupoid

/-- A groupoid is closed under restriction if and only if it contains the trivial restriction-closed
groupoid. -/
theorem closedUnderRestriction_iff_id_le (G : StructureGroupoid H) :
    ClosedUnderRestriction G ‚Üî idRestrGroupoid ‚â§ G := by
  constructor
  ¬∑ intro _i
    apply StructureGroupoid.le_iff.mpr
    rintro e ‚ü®s, hs, hes‚ü©
    refine' G.eq_on_source _ hes
    convert closedUnderRestriction' G.id_mem hs
    -- Porting note: was
    -- change s = _ ‚à© _
    -- rw [hs.interior_eq]
    -- simp only [mfld_simps]
    ext
    ¬∑ rw [LocalHomeomorph.restr_apply, LocalHomeomorph.refl_apply, id, ofSet_apply, id_eq]
    ¬∑ simp [hs]
    ¬∑ simp [hs.interior_eq]
  ¬∑ intro h
    constructor
    intro e he s hs
    rw [‚Üê ofSet_trans (e : LocalHomeomorph H H) hs]
    refine' G.trans _ he
    apply StructureGroupoid.le_iff.mp h
    exact idRestrGroupoid_mem hs
#align closed_under_restriction_iff_id_le closedUnderRestriction_iff_id_le

/-- The groupoid of all local homeomorphisms on a topological space `H` is closed under restriction.
-/
instance : ClosedUnderRestriction (continuousGroupoid H) :=
  (closedUnderRestriction_iff_id_le _).mpr le_top

end Groupoid

/-! ### Charted spaces -/


/-- A charted space is a topological space endowed with an atlas, i.e., a set of local
homeomorphisms taking value in a model space `H`, called charts, such that the domains of the charts
cover the whole space. We express the covering property by choosing for each `x` a member
`chartAt x` of the atlas containing `x` in its source: in the smooth case, this is convenient to
construct the tangent bundle in an efficient way.
The model space is written as an explicit parameter as there can be several model spaces for a
given topological space. For instance, a complex manifold (modelled over `‚ÑÇ^n`) will also be seen
sometimes as a real manifold over `‚Ñù^(2n)`.
-/
@[ext]
class ChartedSpace (H : Type*) [TopologicalSpace H] (M : Type*) [TopologicalSpace M] where
  protected atlas : Set (LocalHomeomorph M H)
  protected chartAt : M ‚Üí LocalHomeomorph M H
  protected mem_chart_source : ‚àÄ x, x ‚àà (chartAt x).source
  protected chart_mem_atlas : ‚àÄ x, chartAt x ‚àà atlas
#align charted_space ChartedSpace

abbrev atlas (H : Type*) [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] : Set (LocalHomeomorph M H) :=
  ChartedSpace.atlas

abbrev chartAt (H : Type*) [TopologicalSpace H] {M : Type*} [TopologicalSpace M]
    [ChartedSpace H M] (x : M) : LocalHomeomorph M H :=
  ChartedSpace.chartAt x

@[simp, mfld_simps]
lemma mem_chart_source (H : Type*) {M : Type*} [TopologicalSpace H] [TopologicalSpace M]
    [ChartedSpace H M] (x : M) : x ‚àà (chartAt H x).source :=
  ChartedSpace.mem_chart_source x

@[simp, mfld_simps]
lemma chart_mem_atlas (H : Type*) {M : Type*} [TopologicalSpace H] [TopologicalSpace M]
    [ChartedSpace H M] (x : M) : chartAt H x ‚àà atlas H M :=
  ChartedSpace.chart_mem_atlas x

section ChartedSpace

/-- Any space is a `ChartedSpace` modelled over itself, by just using the identity chart. -/
instance chartedSpaceSelf (H : Type*) [TopologicalSpace H] : ChartedSpace H H where
  atlas := {LocalHomeomorph.refl H}
  chartAt _ := LocalHomeomorph.refl H
  mem_chart_source x := mem_univ x
  chart_mem_atlas _ := mem_singleton _
#align charted_space_self chartedSpaceSelf

/-- In the trivial `ChartedSpace` structure of a space modelled over itself through the identity,
the atlas members are just the identity. -/
@[simp, mfld_simps]
theorem chartedSpaceSelf_atlas {H : Type*} [TopologicalSpace H] {e : LocalHomeomorph H H} :
    e ‚àà atlas H H ‚Üî e = LocalHomeomorph.refl H :=
  Iff.rfl
#align charted_space_self_atlas chartedSpaceSelf_atlas

/-- In the model space, `chartAt` is always the identity. -/
theorem chartAt_self_eq {H : Type*} [TopologicalSpace H] {x : H} :
    chartAt H x = LocalHomeomorph.refl H := rfl
#align chart_at_self_eq chartAt_self_eq

section

variable (H) [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M]

-- Porting note: Added `(H := H)` to avoid typeclass instance problem.
theorem mem_chart_target (x : M) : chartAt H x x ‚àà (chartAt H x).target :=
  (chartAt H x).map_source (mem_chart_source _ _)
#align mem_chart_target mem_chart_target

theorem chart_source_mem_nhds (x : M) : (chartAt H x).source ‚àà ùìù x :=
  (chartAt H x).open_source.mem_nhds <| mem_chart_source H x
#align chart_source_mem_nhds chart_source_mem_nhds

theorem chart_target_mem_nhds (x : M) : (chartAt H x).target ‚àà ùìù (chartAt H x x) :=
  (chartAt H x).open_target.mem_nhds <| mem_chart_target H x
#align chart_target_mem_nhds chart_target_mem_nhds

/-- `achart H x` is the chart at `x`, considered as an element of the atlas.
Especially useful for working with `BasicSmoothVectorBundleCore`. -/
def achart (x : M) : atlas H M :=
  ‚ü®chartAt H x, chart_mem_atlas H x‚ü©
#align achart achart

theorem achart_def (x : M) : achart H x = ‚ü®chartAt H x, chart_mem_atlas H x‚ü© :=
  rfl
#align achart_def achart_def

@[simp, mfld_simps]
theorem coe_achart (x : M) : (achart H x : LocalHomeomorph M H) = chartAt H x :=
  rfl
#align coe_achart coe_achart

@[simp, mfld_simps]
theorem achart_val (x : M) : (achart H x).1 = chartAt H x :=
  rfl
#align achart_val achart_val

theorem mem_achart_source (x : M) : x ‚àà (achart H x).1.source :=
  mem_chart_source H x
#align mem_achart_source mem_achart_source

open TopologicalSpace

theorem ChartedSpace.secondCountable_of_countable_cover [SecondCountableTopology H] {s : Set M}
    (hs : ‚ãÉ (x) (_ : x ‚àà s), (chartAt H x).source = univ) (hsc : s.Countable) :
    SecondCountableTopology M := by
  haveI : ‚àÄ x : M, SecondCountableTopology (chartAt H x).source :=
    fun x ‚Ü¶ (chartAt (H := H) x).secondCountableTopology_source
  haveI := hsc.toEncodable
  rw [biUnion_eq_iUnion] at hs
  exact secondCountableTopology_of_countable_cover (fun x : s ‚Ü¶ (chartAt H (x : M)).open_source) hs
#align charted_space.second_countable_of_countable_cover ChartedSpace.secondCountable_of_countable_cover

variable (M)

theorem ChartedSpace.secondCountable_of_sigma_compact [SecondCountableTopology H]
    [SigmaCompactSpace M] : SecondCountableTopology M := by
  obtain ‚ü®s, hsc, hsU‚ü© : ‚àÉ s, Set.Countable s ‚àß ‚ãÉ (x) (_ : x ‚àà s), (chartAt H x).source = univ :=
    countable_cover_nhds_of_sigma_compact fun x : M ‚Ü¶ chart_source_mem_nhds H x
  exact ChartedSpace.secondCountable_of_countable_cover H hsU hsc
#align charted_space.second_countable_of_sigma_compact ChartedSpace.secondCountable_of_sigma_compact

/-- If a topological space admits an atlas with locally compact charts, then the space itself
is locally compact. -/
theorem ChartedSpace.locallyCompactSpace [LocallyCompactSpace H] : LocallyCompactSpace M := by
  have : ‚àÄ x : M, (ùìù x).HasBasis
      (fun s ‚Ü¶ s ‚àà ùìù (chartAt H x x) ‚àß IsCompact s ‚àß s ‚äÜ (chartAt H x).target)
      fun s ‚Ü¶ (chartAt H x).symm '' s := fun x ‚Ü¶ by
    rw [‚Üê (chartAt H x).symm_map_nhds_eq (mem_chart_source H x)]
    exact ((compact_basis_nhds (chartAt H x x)).hasBasis_self_subset
      (chart_target_mem_nhds H x)).map _
  refine locallyCompactSpace_of_hasBasis this ?_
  rintro x s ‚ü®_, h‚ÇÇ, h‚ÇÉ‚ü©
  exact h‚ÇÇ.image_of_continuousOn ((chartAt H x).continuousOn_symm.mono h‚ÇÉ)
#align charted_space.locally_compact ChartedSpace.locallyCompactSpace

/-- If a topological space admits an atlas with locally connected charts, then the space itself is
locally connected. -/
theorem ChartedSpace.locallyConnectedSpace [LocallyConnectedSpace H] : LocallyConnectedSpace M := by
  let e : M ‚Üí LocalHomeomorph M H := chartAt H
  refine' locallyConnectedSpace_of_connected_bases (fun x s ‚Ü¶ (e x).symm '' s)
      (fun x s ‚Ü¶ (IsOpen s ‚àß e x x ‚àà s ‚àß IsConnected s) ‚àß s ‚äÜ (e x).target) _ _
  ¬∑ intro x
    simpa only [LocalHomeomorph.symm_map_nhds_eq, mem_chart_source] using
      ((LocallyConnectedSpace.open_connected_basis (e x x)).restrict_subset
        ((e x).open_target.mem_nhds (mem_chart_target H x))).map (e x).symm
  ¬∑ rintro x s ‚ü®‚ü®-, -, hsconn‚ü©, hssubset‚ü©
    exact hsconn.isPreconnected.image _ ((e x).continuousOn_symm.mono hssubset)
#align charted_space.locally_connected_space ChartedSpace.locallyConnectedSpace

/-- If `M` is modelled on `H'` and `H'` is itself modelled on `H`, then we can consider `M` as being
modelled on `H`. -/
def ChartedSpace.comp (H : Type*) [TopologicalSpace H] (H' : Type*) [TopologicalSpace H']
    (M : Type*) [TopologicalSpace M] [ChartedSpace H H'] [ChartedSpace H' M] :
    ChartedSpace H M where
  atlas := image2 LocalHomeomorph.trans (atlas H' M) (atlas H H')
  chartAt p := (chartAt H' p).trans (chartAt H (chartAt H' p p))
  mem_chart_source p := by simp only [mfld_simps]
  chart_mem_atlas p := ‚ü®chartAt _ p, chartAt _ _, chart_mem_atlas _ p, chart_mem_atlas _ _, rfl‚ü©
#align charted_space.comp ChartedSpace.comp

theorem chartAt_comp (H : Type*) [TopologicalSpace H] (H' : Type*) [TopologicalSpace H']
    {M : Type*} [TopologicalSpace M] [ChartedSpace H H'] [ChartedSpace H' M] (x : M) :
    (letI := ChartedSpace.comp H H' M; chartAt H x) = chartAt H' x ‚â´‚Çï chartAt H (chartAt H' x x) :=
  rfl

end

library_note "Manifold type tags" /-- For technical reasons we introduce two type tags:

* `ModelProd H H'` is the same as `H √ó H'`;
* `ModelPi H` is the same as `‚àÄ i, H i`, where `H : Œπ ‚Üí Type*` and `Œπ` is a finite type.

In both cases the reason is the same, so we explain it only in the case of the product. A charted
space `M` with model `H` is a set of local charts from `M` to `H` covering the space. Every space is
registered as a charted space over itself, using the only chart `id`, in `chartedSpaceSelf`. You
can also define a product of charted space `M` and `M'` (with model space `H √ó H'`) by taking the
products of the charts. Now, on `H √ó H'`, there are two charted space structures with model space
`H √ó H'` itself, the one coming from `chartedSpaceSelf`, and the one coming from the product of
the two `chartedSpaceSelf` on each component. They are equal, but not defeq (because the product
of `id` and `id` is not defeq to `id`), which is bad as we know. This expedient of renaming `H √ó H'`
solves this problem. -/


/-- Same thing as `H √ó H'`. We introduce it for technical reasons,
see note [Manifold type tags]. -/
def ModelProd (H : Type*) (H' : Type*) :=
  H √ó H'
#align model_prod ModelProd

/-- Same thing as `‚àÄ i, H i`. We introduce it for technical reasons,
see note [Manifold type tags]. -/
def ModelPi {Œπ : Type*} (H : Œπ ‚Üí Type*) :=
  ‚àÄ i, H i
#align model_pi ModelPi

section

-- attribute [local reducible] ModelProd -- Porting note: not available in Lean4

instance modelProdInhabited [Inhabited H] [Inhabited H'] : Inhabited (ModelProd H H') :=
  instInhabitedProd
#align model_prod_inhabited modelProdInhabited

instance (H : Type*) [TopologicalSpace H] (H' : Type*) [TopologicalSpace H'] :
    TopologicalSpace (ModelProd H H') :=
  instTopologicalSpaceProd

-- Porting note: simpNF false positive
-- Next lemma shows up often when dealing with derivatives, register it as simp.
@[simp, mfld_simps, nolint simpNF]
theorem modelProd_range_prod_id {H : Type*} {H' : Type*} {Œ± : Type*} (f : H ‚Üí Œ±) :
    (range fun p : ModelProd H H' ‚Ü¶ (f p.1, p.2)) = range f √óÀ¢ (univ : Set H') := by
  rw [prod_range_univ_eq]
  rfl
#align model_prod_range_prod_id modelProd_range_prod_id

end

section

variable {Œπ : Type*} {Hi : Œπ ‚Üí Type*}

-- Porting note: Old proof was `Pi.inhabited _`.
instance modelPiInhabited [‚àÄ i, Inhabited (Hi i)] : Inhabited (ModelPi Hi) :=
  ‚ü®fun _ ‚Ü¶ default‚ü©
#align model_pi_inhabited modelPiInhabited

instance [‚àÄ i, TopologicalSpace (Hi i)] : TopologicalSpace (ModelPi Hi) :=
  Pi.topologicalSpace

end

/-- The product of two charted spaces is naturally a charted space, with the canonical
construction of the atlas of product maps. -/
instance prodChartedSpace (H : Type*) [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] (H' : Type*) [TopologicalSpace H'] (M' : Type*) [TopologicalSpace M']
    [ChartedSpace H' M'] : ChartedSpace (ModelProd H H') (M √ó M') where
  atlas := image2 LocalHomeomorph.prod (atlas H M) (atlas H' M')
  chartAt x := (chartAt H x.1).prod (chartAt H' x.2)
  mem_chart_source x := ‚ü®mem_chart_source H x.1, mem_chart_source H' x.2‚ü©
  chart_mem_atlas x := mem_image2_of_mem (chart_mem_atlas H x.1) (chart_mem_atlas H' x.2)
#align prod_charted_space prodChartedSpace

section prodChartedSpace

@[ext]
theorem ModelProd.ext {x y : ModelProd Œ± Œ≤} (h‚ÇÅ : x.1 = y.1) (h‚ÇÇ : x.2 = y.2) : x = y :=
  Prod.ext h‚ÇÅ h‚ÇÇ

variable [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M] [TopologicalSpace H']
  [TopologicalSpace M'] [ChartedSpace H' M'] {x : M √ó M'}

@[simp, mfld_simps]
theorem prodChartedSpace_chartAt :
    chartAt (ModelProd H H') x = (chartAt H x.fst).prod (chartAt H' x.snd) :=
  rfl
#align prod_charted_space_chart_at prodChartedSpace_chartAt

theorem chartedSpaceSelf_prod : prodChartedSpace H H H' H' = chartedSpaceSelf (H √ó H') := by
  ext1
  ¬∑ simp [prodChartedSpace, atlas, ChartedSpace.atlas]
  ¬∑ ext1
    simp only [prodChartedSpace_chartAt, chartAt_self_eq, refl_prod_refl]
    rfl
#align charted_space_self_prod chartedSpaceSelf_prod

end prodChartedSpace

/-- The product of a finite family of charted spaces is naturally a charted space, with the
canonical construction of the atlas of finite product maps. -/
instance piChartedSpace {Œπ : Type*} [Fintype Œπ] (H : Œπ ‚Üí Type*) [‚àÄ i, TopologicalSpace (H i)]
    (M : Œπ ‚Üí Type*) [‚àÄ i, TopologicalSpace (M i)] [‚àÄ i, ChartedSpace (H i) (M i)] :
    ChartedSpace (ModelPi H) (‚àÄ i, M i) where
  atlas := LocalHomeomorph.pi '' Set.pi univ fun _ ‚Ü¶ atlas (H _) (M _)
  chartAt f := LocalHomeomorph.pi fun i ‚Ü¶ chartAt (H i) (f i)
  mem_chart_source f i _ := mem_chart_source (H i) (f i)
  chart_mem_atlas f := mem_image_of_mem _ fun i _ ‚Ü¶ chart_mem_atlas (H i) (f i)
#align pi_charted_space piChartedSpace

@[simp, mfld_simps]
theorem piChartedSpace_chartAt {Œπ : Type*} [Fintype Œπ] (H : Œπ ‚Üí Type*)
    [‚àÄ i, TopologicalSpace (H i)] (M : Œπ ‚Üí Type*) [‚àÄ i, TopologicalSpace (M i)]
    [‚àÄ i, ChartedSpace (H i) (M i)] (f : ‚àÄ i, M i) :
    chartAt (H := ModelPi H) f = LocalHomeomorph.pi fun i ‚Ü¶ chartAt (H i) (f i) :=
  rfl
#align pi_charted_space_chart_at piChartedSpace_chartAt

end ChartedSpace

/-! ### Constructing a topology from an atlas -/


/-- Sometimes, one may want to construct a charted space structure on a space which does not yet
have a topological structure, where the topology would come from the charts. For this, one needs
charts that are only local equivs, and continuity properties for their composition.
This is formalised in `ChartedSpaceCore`. -/
-- @[nolint has_nonempty_instance]  -- Porting note: commented out
structure ChartedSpaceCore (H : Type*) [TopologicalSpace H] (M : Type*) where
  atlas : Set (LocalEquiv M H)
  chartAt : M ‚Üí LocalEquiv M H
  mem_chart_source : ‚àÄ x, x ‚àà (chartAt x).source
  chart_mem_atlas : ‚àÄ x, chartAt x ‚àà atlas
  open_source : ‚àÄ e e' : LocalEquiv M H, e ‚àà atlas ‚Üí e' ‚àà atlas ‚Üí IsOpen (e.symm.trans e').source
  continuous_toFun : ‚àÄ e e' : LocalEquiv M H, e ‚àà atlas ‚Üí e' ‚àà atlas ‚Üí
    ContinuousOn (e.symm.trans e') (e.symm.trans e').source
#align charted_space_core ChartedSpaceCore

namespace ChartedSpaceCore

variable [TopologicalSpace H] (c : ChartedSpaceCore H M) {e : LocalEquiv M H}

/-- Topology generated by a set of charts on a Type. -/
protected def toTopologicalSpace : TopologicalSpace M :=
  TopologicalSpace.generateFrom <|
    ‚ãÉ (e : LocalEquiv M H) (_ : e ‚àà c.atlas) (s : Set H) (_ : IsOpen s),
      {e ‚Åª¬π' s ‚à© e.source}
#align charted_space_core.to_topological_space ChartedSpaceCore.toTopologicalSpace

theorem open_source' (he : e ‚àà c.atlas) : IsOpen[c.toTopologicalSpace] e.source := by
  apply TopologicalSpace.GenerateOpen.basic
  simp only [exists_prop, mem_iUnion, mem_singleton_iff]
  refine' ‚ü®e, he, univ, isOpen_univ, _‚ü©
  simp only [Set.univ_inter, Set.preimage_univ]
#align charted_space_core.open_source' ChartedSpaceCore.open_source'

theorem open_target (he : e ‚àà c.atlas) : IsOpen e.target := by
  have E : e.target ‚à© e.symm ‚Åª¬π' e.source = e.target :=
    Subset.antisymm (inter_subset_left _ _) fun x hx ‚Ü¶
      ‚ü®hx, LocalEquiv.target_subset_preimage_source _ hx‚ü©
  simpa [LocalEquiv.trans_source, E] using c.open_source e e he he
#align charted_space_core.open_target ChartedSpaceCore.open_target

/-- An element of the atlas in a charted space without topology becomes a local homeomorphism
for the topology constructed from this atlas. The `localHomeomorph` version is given in this
definition. -/
protected def localHomeomorph (e : LocalEquiv M H) (he : e ‚àà c.atlas) :
    @LocalHomeomorph M H c.toTopologicalSpace _ :=
  { c.toTopologicalSpace, e with
    open_source := by convert c.open_source' he
    open_target := by convert c.open_target he
    continuous_toFun := by
      letI : TopologicalSpace M := c.toTopologicalSpace
      rw [continuousOn_open_iff (c.open_source' he)]
      intro s s_open
      rw [inter_comm]
      apply TopologicalSpace.GenerateOpen.basic
      simp only [exists_prop, mem_iUnion, mem_singleton_iff]
      exact ‚ü®e, he, ‚ü®s, s_open, rfl‚ü©‚ü©
    continuous_invFun := by
      letI : TopologicalSpace M := c.toTopologicalSpace
      apply continuousOn_open_of_generateFrom
      intro t ht
      simp only [exists_prop, mem_iUnion, mem_singleton_iff] at ht
      rcases ht with ‚ü®e', e'_atlas, s, s_open, ts‚ü©
      rw [ts]
      let f := e.symm.trans e'
      have : IsOpen (f ‚Åª¬π' s ‚à© f.source) := by
        simpa [inter_comm] using (continuousOn_open_iff (c.open_source e e' he e'_atlas)).1
          (c.continuous_toFun e e' he e'_atlas) s s_open
      have A : e' ‚àò e.symm ‚Åª¬π' s ‚à© (e.target ‚à© e.symm ‚Åª¬π' e'.source) =
          e.target ‚à© (e' ‚àò e.symm ‚Åª¬π' s ‚à© e.symm ‚Åª¬π' e'.source) := by
        rw [‚Üê inter_assoc, ‚Üê inter_assoc]
        congr 1
        exact inter_comm _ _
      simpa [LocalEquiv.trans_source, preimage_inter, preimage_comp.symm, A] using this }
#align charted_space_core.local_homeomorph ChartedSpaceCore.localHomeomorph

/-- Given a charted space without topology, endow it with a genuine charted space structure with
respect to the topology constructed from the atlas. -/
def toChartedSpace : @ChartedSpace H _ M c.toTopologicalSpace :=
  { c.toTopologicalSpace with
    atlas := ‚ãÉ (e : LocalEquiv M H) (he : e ‚àà c.atlas), {c.localHomeomorph e he}
    chartAt := fun x ‚Ü¶ c.localHomeomorph (c.chartAt x) (c.chart_mem_atlas x)
    mem_chart_source := fun x ‚Ü¶ c.mem_chart_source x
    chart_mem_atlas := fun x ‚Ü¶ by
      simp only [mem_iUnion, mem_singleton_iff]
      exact ‚ü®c.chartAt x, c.chart_mem_atlas x, rfl‚ü©}
#align charted_space_core.to_charted_space ChartedSpaceCore.toChartedSpace

end ChartedSpaceCore

/-! ### Charted space with a given structure groupoid -/


section HasGroupoid

variable [TopologicalSpace H] [TopologicalSpace M] [ChartedSpace H M]

/-- A charted space has an atlas in a groupoid `G` if the change of coordinates belong to the
groupoid. -/
class HasGroupoid {H : Type*} [TopologicalSpace H] (M : Type*) [TopologicalSpace M]
    [ChartedSpace H M] (G : StructureGroupoid H) : Prop where
  compatible : ‚àÄ {e e' : LocalHomeomorph M H}, e ‚àà atlas H M ‚Üí e' ‚àà atlas H M ‚Üí e.symm ‚â´‚Çï e' ‚àà G
#align has_groupoid HasGroupoid

/-- Reformulate in the `StructureGroupoid` namespace the compatibility condition of charts in a
charted space admitting a structure groupoid, to make it more easily accessible with dot
notation. -/
theorem StructureGroupoid.compatible {H : Type*} [TopologicalSpace H] (G : StructureGroupoid H)
    {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [HasGroupoid M G]
    {e e' : LocalHomeomorph M H} (he : e ‚àà atlas H M) (he' : e' ‚àà atlas H M) : e.symm ‚â´‚Çï e' ‚àà G :=
  HasGroupoid.compatible he he'
#align structure_groupoid.compatible StructureGroupoid.compatible

theorem hasGroupoid_of_le {G‚ÇÅ G‚ÇÇ : StructureGroupoid H} (h : HasGroupoid M G‚ÇÅ) (hle : G‚ÇÅ ‚â§ G‚ÇÇ) :
    HasGroupoid M G‚ÇÇ :=
  ‚ü®fun he he' ‚Ü¶ hle (h.compatible he he')‚ü©
#align has_groupoid_of_le hasGroupoid_of_le

theorem hasGroupoid_of_pregroupoid (PG : Pregroupoid H) (h : ‚àÄ {e e' : LocalHomeomorph M H},
      e ‚àà atlas H M ‚Üí e' ‚àà atlas H M ‚Üí PG.property (e.symm ‚â´‚Çï e') (e.symm ‚â´‚Çï e').source) :
    HasGroupoid M PG.groupoid :=
  ‚ü®fun he he' ‚Ü¶ mem_groupoid_of_pregroupoid.mpr ‚ü®h he he', h he' he‚ü©‚ü©
#align has_groupoid_of_pregroupoid hasGroupoid_of_pregroupoid

/-- The trivial charted space structure on the model space is compatible with any groupoid. -/
instance hasGroupoid_model_space (H : Type*) [TopologicalSpace H] (G : StructureGroupoid H) :
    HasGroupoid H G where
  compatible {e e'} he he' := by
    rw [chartedSpaceSelf_atlas] at he he'
    simp [he, he', StructureGroupoid.id_mem]
#align has_groupoid_model_space hasGroupoid_model_space

/-- Any charted space structure is compatible with the groupoid of all local homeomorphisms. -/
instance hasGroupoid_continuousGroupoid : HasGroupoid M (continuousGroupoid H) := by
  refine' ‚ü®fun _ _ ‚Ü¶ _‚ü©
  rw [continuousGroupoid, mem_groupoid_of_pregroupoid]
  simp only [and_self_iff]
#align has_groupoid_continuous_groupoid hasGroupoid_continuousGroupoid

section MaximalAtlas

variable (M) (G : StructureGroupoid H)

/-- Given a charted space admitting a structure groupoid, the maximal atlas associated to this
structure groupoid is the set of all local charts that are compatible with the atlas, i.e., such
that changing coordinates with an atlas member gives an element of the groupoid. -/
def StructureGroupoid.maximalAtlas : Set (LocalHomeomorph M H) :=
  { e | ‚àÄ e' ‚àà atlas H M, e.symm ‚â´‚Çï e' ‚àà G ‚àß e'.symm ‚â´‚Çï e ‚àà G }
#align structure_groupoid.maximal_atlas StructureGroupoid.maximalAtlas

variable {M}

/-- The elements of the atlas belong to the maximal atlas for any structure groupoid. -/
theorem StructureGroupoid.subset_maximalAtlas [HasGroupoid M G] : atlas H M ‚äÜ G.maximalAtlas M :=
  fun _ he _ he' ‚Ü¶ ‚ü®G.compatible he he', G.compatible he' he‚ü©
#align structure_groupoid.subset_maximal_atlas StructureGroupoid.subset_maximalAtlas

theorem StructureGroupoid.chart_mem_maximalAtlas [HasGroupoid M G] (x : M) :
    chartAt H x ‚àà G.maximalAtlas M :=
  G.subset_maximalAtlas (chart_mem_atlas H x)
#align structure_groupoid.chart_mem_maximal_atlas StructureGroupoid.chart_mem_maximalAtlas

variable {G}

theorem mem_maximalAtlas_iff {e : LocalHomeomorph M H} :
    e ‚àà G.maximalAtlas M ‚Üî ‚àÄ e' ‚àà atlas H M, e.symm ‚â´‚Çï e' ‚àà G ‚àß e'.symm ‚â´‚Çï e ‚àà G :=
  Iff.rfl
#align mem_maximal_atlas_iff mem_maximalAtlas_iff

/-- Changing coordinates between two elements of the maximal atlas gives rise to an element
of the structure groupoid. -/
theorem StructureGroupoid.compatible_of_mem_maximalAtlas {e e' : LocalHomeomorph M H}
    (he : e ‚àà G.maximalAtlas M) (he' : e' ‚àà G.maximalAtlas M) : e.symm ‚â´‚Çï e' ‚àà G := by
  refine' G.locality fun x hx ‚Ü¶ _
  set f := chartAt (H := H) (e.symm x)
  let s := e.target ‚à© e.symm ‚Åª¬π' f.source
  have hs : IsOpen s := by
    apply e.symm.continuous_toFun.preimage_open_of_open <;> apply open_source
  have xs : x ‚àà s := by
    simp only [mem_inter_iff, mem_preimage, mem_chart_source, and_true]
    exact ((mem_inter_iff _ _ _).1 hx).1
  refine' ‚ü®s, hs, xs, _‚ü©
  have A : e.symm ‚â´‚Çï f ‚àà G := (mem_maximalAtlas_iff.1 he f (chart_mem_atlas _ _)).1
  have B : f.symm ‚â´‚Çï e' ‚àà G := (mem_maximalAtlas_iff.1 he' f (chart_mem_atlas _ _)).2
  have C : (e.symm ‚â´‚Çï f) ‚â´‚Çï f.symm ‚â´‚Çï e' ‚àà G := G.trans A B
  have D : (e.symm ‚â´‚Çï f) ‚â´‚Çï f.symm ‚â´‚Çï e' ‚âà (e.symm ‚â´‚Çï e').restr s := calc
    (e.symm ‚â´‚Çï f) ‚â´‚Çï f.symm ‚â´‚Çï e' = e.symm ‚â´‚Çï (f ‚â´‚Çï f.symm) ‚â´‚Çï e' := by simp only [trans_assoc]
    _ ‚âà e.symm ‚â´‚Çï ofSet f.source f.open_source ‚â´‚Çï e' :=
      EqOnSource.trans' (refl _) (EqOnSource.trans' (trans_self_symm _) (refl _))
    _ ‚âà (e.symm ‚â´‚Çï ofSet f.source f.open_source) ‚â´‚Çï e' := by rw [trans_assoc]
    _ ‚âà e.symm.restr s ‚â´‚Çï e' := by rw [trans_of_set']; apply refl
    _ ‚âà (e.symm ‚â´‚Çï e').restr s := by rw [restr_trans]
  exact G.eq_on_source C (Setoid.symm D)
#align structure_groupoid.compatible_of_mem_maximal_atlas StructureGroupoid.compatible_of_mem_maximalAtlas

variable (G)

/-- In the model space, the identity is in any maximal atlas. -/
theorem StructureGroupoid.id_mem_maximalAtlas : LocalHomeomorph.refl H ‚àà G.maximalAtlas H :=
  G.subset_maximalAtlas <| by simp
#align structure_groupoid.id_mem_maximal_atlas StructureGroupoid.id_mem_maximalAtlas

/-- In the model space, any element of the groupoid is in the maximal atlas. -/
theorem StructureGroupoid.mem_maximalAtlas_of_mem_groupoid {f : LocalHomeomorph H H} (hf : f ‚àà G) :
    f ‚àà G.maximalAtlas H := by
  rintro e (rfl : e = LocalHomeomorph.refl H)
  exact ‚ü®G.trans (G.symm hf) G.id_mem, G.trans (G.symm G.id_mem) hf‚ü©
#align structure_groupoid.mem_maximal_atlas_of_mem_groupoid StructureGroupoid.mem_maximalAtlas_of_mem_groupoid

end MaximalAtlas

section Singleton

variable {Œ± : Type*} [TopologicalSpace Œ±]

namespace LocalHomeomorph

variable (e : LocalHomeomorph Œ± H)

/-- If a single local homeomorphism `e` from a space `Œ±` into `H` has source covering the whole
space `Œ±`, then that local homeomorphism induces an `H`-charted space structure on `Œ±`.
(This condition is equivalent to `e` being an open embedding of `Œ±` into `H`; see
`OpenEmbedding.singletonChartedSpace`.) -/
def singletonChartedSpace (h : e.source = Set.univ) : ChartedSpace H Œ± where
  atlas := {e}
  chartAt _ := e
  mem_chart_source _ := by rw [h]; apply mem_univ
  chart_mem_atlas _ := by tauto
#align local_homeomorph.singleton_charted_space LocalHomeomorph.singletonChartedSpace

@[simp, mfld_simps]
theorem singletonChartedSpace_chartAt_eq (h : e.source = Set.univ) {x : Œ±} :
    @chartAt H _ Œ± _ (e.singletonChartedSpace h) x = e :=
  rfl
#align local_homeomorph.singleton_charted_space_chart_at_eq LocalHomeomorph.singletonChartedSpace_chartAt_eq

theorem singletonChartedSpace_chartAt_source (h : e.source = Set.univ) {x : Œ±} :
    (@chartAt H _ Œ± _ (e.singletonChartedSpace h) x).source = Set.univ :=
  h
#align local_homeomorph.singleton_charted_space_chart_at_source LocalHomeomorph.singletonChartedSpace_chartAt_source

theorem singletonChartedSpace_mem_atlas_eq (h : e.source = Set.univ) (e' : LocalHomeomorph Œ± H)
    (h' : e' ‚àà (e.singletonChartedSpace h).atlas) : e' = e :=
  h'
#align local_homeomorph.singleton_charted_space_mem_atlas_eq LocalHomeomorph.singletonChartedSpace_mem_atlas_eq

/-- Given a local homeomorphism `e` from a space `Œ±` into `H`, if its source covers the whole
space `Œ±`, then the induced charted space structure on `Œ±` is `HasGroupoid G` for any structure
groupoid `G` which is closed under restrictions. -/
theorem singleton_hasGroupoid (h : e.source = Set.univ) (G : StructureGroupoid H)
    [ClosedUnderRestriction G] : @HasGroupoid _ _ _ _ (e.singletonChartedSpace h) G :=
  { e.singletonChartedSpace h with
    compatible := by
      intro e' e'' he' he''
      rw [e.singletonChartedSpace_mem_atlas_eq h e' he',
        e.singletonChartedSpace_mem_atlas_eq h e'' he'']
      refine' G.eq_on_source _ e.trans_symm_self
      have hle : idRestrGroupoid ‚â§ G := (closedUnderRestriction_iff_id_le G).mp (by assumption)
      exact StructureGroupoid.le_iff.mp hle _ (idRestrGroupoid_mem _) }
#align local_homeomorph.singleton_has_groupoid LocalHomeomorph.singleton_hasGroupoid

end LocalHomeomorph

namespace OpenEmbedding

variable [Nonempty Œ±]

/-- An open embedding of `Œ±` into `H` induces an `H`-charted space structure on `Œ±`.
See `LocalHomeomorph.singletonChartedSpace`. -/
def singletonChartedSpace {f : Œ± ‚Üí H} (h : OpenEmbedding f) : ChartedSpace H Œ± :=
  (h.toLocalHomeomorph f).singletonChartedSpace (toLocalHomeomorph_source _ _)
#align open_embedding.singleton_charted_space OpenEmbedding.singletonChartedSpace

theorem singletonChartedSpace_chartAt_eq {f : Œ± ‚Üí H} (h : OpenEmbedding f) {x : Œ±} :
    ‚áë(@chartAt H _ Œ± _ h.singletonChartedSpace x) = f :=
  rfl
#align open_embedding.singleton_charted_space_chart_at_eq OpenEmbedding.singletonChartedSpace_chartAt_eq

theorem singleton_hasGroupoid {f : Œ± ‚Üí H} (h : OpenEmbedding f) (G : StructureGroupoid H)
    [ClosedUnderRestriction G] : @HasGroupoid _ _ _ _ h.singletonChartedSpace G :=
  (h.toLocalHomeomorph f).singleton_hasGroupoid (toLocalHomeomorph_source _ _) G
#align open_embedding.singleton_has_groupoid OpenEmbedding.singleton_hasGroupoid

end OpenEmbedding

end Singleton

namespace TopologicalSpace.Opens

open TopologicalSpace

variable (G : StructureGroupoid H) [HasGroupoid M G]

variable (s : Opens M)

/-- An open subset of a charted space is naturally a charted space. -/
protected instance instChartedSpace : ChartedSpace H s where
  atlas := ‚ãÉ x : s, {@LocalHomeomorph.subtypeRestr _ _ _ _ (chartAt H x.1) s ‚ü®x‚ü©}
  chartAt x := @LocalHomeomorph.subtypeRestr _ _ _ _ (chartAt H x.1) s ‚ü®x‚ü©
  mem_chart_source x := ‚ü®trivial, mem_chart_source H x.1‚ü©
  chart_mem_atlas x := by
    simp only [mem_iUnion, mem_singleton_iff]
    use x
#align topological_space.opens.charted_space TopologicalSpace.Opens.instChartedSpace

/-- If a groupoid `G` is `ClosedUnderRestriction`, then an open subset of a space which is
`HasGroupoid G` is naturally `HasGroupoid G`. -/
protected instance instHasGroupoid [ClosedUnderRestriction G] : HasGroupoid s G where
  compatible := by
    rintro e e' ‚ü®_, ‚ü®x, hc‚ü©, he‚ü© ‚ü®_, ‚ü®x', hc'‚ü©, he'‚ü©
    haveI : Nonempty s := ‚ü®x‚ü©
    have asdf := he
    rw [hc.symm, mem_singleton_iff] at he
    rw [hc'.symm, mem_singleton_iff] at he'
    rw [he, he']
    refine' G.eq_on_source _ (subtypeRestr_symm_trans_subtypeRestr s (chartAt H x) (chartAt H x'))
    apply closedUnderRestriction'
    ¬∑ exact G.compatible (chart_mem_atlas _ _) (chart_mem_atlas _ _)
    ¬∑ exact preimage_open_of_open_symm (chartAt _ _) s.2
#align topological_space.opens.has_groupoid TopologicalSpace.Opens.instHasGroupoid

theorem chartAt_inclusion_symm_eventuallyEq {U V : Opens M} (hUV : U ‚â§ V) {x : U} :
    (chartAt H (Set.inclusion hUV x)).symm
    =·∂†[ùìù (chartAt H (Set.inclusion hUV x) (Set.inclusion hUV x))]
    Set.inclusion hUV ‚àò (chartAt H x).symm := by
  set i := Set.inclusion hUV
  set e := chartAt H (x : M)
  haveI : Nonempty U := ‚ü®x‚ü©
  haveI : Nonempty V := ‚ü®i x‚ü©
  have heUx_nhds : (e.subtypeRestr U).target ‚àà ùìù (e x) := by
    apply (e.subtypeRestr U).open_target.mem_nhds
    exact e.map_subtype_source (mem_chart_source _ _)
  exact Filter.eventuallyEq_of_mem heUx_nhds (e.subtypeRestr_symm_eqOn_of_le hUV)
#align topological_space.opens.chart_at_inclusion_symm_eventually_eq TopologicalSpace.Opens.chartAt_inclusion_symm_eventuallyEq

end TopologicalSpace.Opens

/-! ### Structomorphisms -/

/-- A `G`-diffeomorphism between two charted spaces is a homeomorphism which, when read in the
charts, belongs to `G`. We avoid the word diffeomorph as it is too related to the smooth category,
and use structomorph instead. -/
-- @[nolint has_nonempty_instance]  -- Porting note: commented out
structure Structomorph (G : StructureGroupoid H) (M : Type*) (M' : Type*) [TopologicalSpace M]
  [TopologicalSpace M'] [ChartedSpace H M] [ChartedSpace H M'] extends Homeomorph M M' where
  mem_groupoid : ‚àÄ c : LocalHomeomorph M H, ‚àÄ c' : LocalHomeomorph M' H, c ‚àà atlas H M ‚Üí
    c' ‚àà atlas H M' ‚Üí c.symm ‚â´‚Çï toHomeomorph.toLocalHomeomorph ‚â´‚Çï c' ‚àà G
#align structomorph Structomorph

variable [TopologicalSpace M'] [TopologicalSpace M''] {G : StructureGroupoid H} [ChartedSpace H M']
  [ChartedSpace H M'']

/-- The identity is a diffeomorphism of any charted space, for any groupoid. -/
def Structomorph.refl (M : Type*) [TopologicalSpace M] [ChartedSpace H M] [HasGroupoid M G] :
    Structomorph G M M :=
  { Homeomorph.refl M with
    mem_groupoid := fun c c' hc hc' ‚Ü¶ by
      change LocalHomeomorph.symm c ‚â´‚Çï LocalHomeomorph.refl M ‚â´‚Çï c' ‚àà G
      rw [LocalHomeomorph.refl_trans]
      exact G.compatible hc hc' }
#align structomorph.refl Structomorph.refl

/-- The inverse of a structomorphism is a structomorphism. -/
def Structomorph.symm (e : Structomorph G M M') : Structomorph G M' M :=
  { e.toHomeomorph.symm with
    mem_groupoid := by
      intro c c' hc hc'
      have : (c'.symm ‚â´‚Çï e.toHomeomorph.toLocalHomeomorph ‚â´‚Çï c).symm ‚àà G :=
        G.symm (e.mem_groupoid c' c hc' hc)
      rwa [trans_symm_eq_symm_trans_symm, trans_symm_eq_symm_trans_symm, symm_symm, trans_assoc]
        at this }
#align structomorph.symm Structomorph.symm

/-- The composition of structomorphisms is a structomorphism. -/
def Structomorph.trans (e : Structomorph G M M') (e' : Structomorph G M' M'') :
    Structomorph G M M'' :=
  { Homeomorph.trans e.toHomeomorph e'.toHomeomorph with
    mem_groupoid := by
      /- Let c and c' be two charts in M and M''. We want to show that e' ‚àò e is smooth in these
      charts, around any point x. For this, let y = e (c‚Åª¬π x), and consider a chart g around y.
      Then g ‚àò e ‚àò c‚Åª¬π and c' ‚àò e' ‚àò g‚Åª¬π are both smooth as e and e' are structomorphisms, so
      their composition is smooth, and it coincides with c' ‚àò e' ‚àò e ‚àò c‚Åª¬π around x. -/
      intro c c' hc hc'
      refine' G.locality fun x hx ‚Ü¶ _
      let f‚ÇÅ := e.toHomeomorph.toLocalHomeomorph
      let f‚ÇÇ := e'.toHomeomorph.toLocalHomeomorph
      let f := (e.toHomeomorph.trans e'.toHomeomorph).toLocalHomeomorph
      have feq : f = f‚ÇÅ ‚â´‚Çï f‚ÇÇ := Homeomorph.trans_toLocalHomeomorph _ _
      -- define the atlas g around y
      let y := (c.symm ‚â´‚Çï f‚ÇÅ) x
      let g := chartAt (H := H) y
      have hg‚ÇÅ := chart_mem_atlas (H := H) y
      have hg‚ÇÇ := mem_chart_source (H := H) y
      let s := (c.symm ‚â´‚Çï f‚ÇÅ).source ‚à© c.symm ‚â´‚Çï f‚ÇÅ ‚Åª¬π' g.source
      have open_s : IsOpen s := by
        apply (c.symm ‚â´‚Çï f‚ÇÅ).continuous_toFun.preimage_open_of_open <;> apply open_source
      have : x ‚àà s := by
        constructor
        ¬∑ simp only [trans_source, preimage_univ, inter_univ, Homeomorph.toLocalHomeomorph_source]
          rw [trans_source] at hx
          exact hx.1
        ¬∑ exact hg‚ÇÇ
      refine' ‚ü®s, open_s, this, _‚ü©
      let F‚ÇÅ := (c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï g) ‚â´‚Çï g.symm ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c'
      have A : F‚ÇÅ ‚àà G := G.trans (e.mem_groupoid c g hc hg‚ÇÅ) (e'.mem_groupoid g c' hg‚ÇÅ hc')
      let F‚ÇÇ := (c.symm ‚â´‚Çï f ‚â´‚Çï c').restr s
      have : F‚ÇÅ ‚âà F‚ÇÇ := calc
        F‚ÇÅ ‚âà c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï (g ‚â´‚Çï g.symm) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' := by simp only [trans_assoc, _root_.refl]
        _ ‚âà c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï ofSet g.source g.open_source ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' :=
          EqOnSource.trans' (_root_.refl _) (EqOnSource.trans' (_root_.refl _)
            (EqOnSource.trans' (trans_self_symm g) (_root_.refl _)))
        _ ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ) ‚â´‚Çï ofSet g.source g.open_source) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' :=
          by simp only [trans_assoc, _root_.refl]
        _ ‚âà (c.symm ‚â´‚Çï f‚ÇÅ).restr s ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' := by rw [trans_of_set']
        _ ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c').restr s := by rw [restr_trans]
        _ ‚âà (c.symm ‚â´‚Çï (f‚ÇÅ ‚â´‚Çï f‚ÇÇ) ‚â´‚Çï c').restr s :=
          by simp only [EqOnSource.restr, trans_assoc, _root_.refl]
        _ ‚âà F‚ÇÇ := by simp only [feq, _root_.refl]
      have : F‚ÇÇ ‚àà G := G.eq_on_source A (Setoid.symm this)
      exact this }
#align structomorph.trans Structomorph.trans

end HasGroupoid
