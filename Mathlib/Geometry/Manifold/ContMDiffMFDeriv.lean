/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.MFDeriv.Tangent
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.VectorBundle.Hom

/-!
### Interactions between differentiability, smoothness and manifold derivatives

We give the relation between `MDifferentiable`, `ContMDiff`, `mfderiv`, `tangentMap`
and related notions.

## Main statements

* `ContMDiffOn.contMDiffOn_tangentMapWithin` states that the bundled derivative
  of a `Câ¿` function in a domain is `Cáµ` when `m + 1 â‰¤ n`.
* `ContMDiff.contMDiff_tangentMap` states that the bundled derivative
  of a `Câ¿` function is `Cáµ` when `m + 1 â‰¤ n`.
-/

open Set Function Filter ChartedSpace SmoothManifoldWithCorners Bundle

open scoped Topology Manifold Bundle

/-! ### Definition of smooth functions between manifolds -/


variable {ð•œ : Type*} [NontriviallyNormedField ð•œ]
  -- declare a charted space `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ð•œ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ð•œ E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- declare a charted space `M'` over the pair `(E', H')`.
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ð•œ E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ð•œ E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ð•œ F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ð•œ F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [Js : SmoothManifoldWithCorners J N]
  -- declare a charted space `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ð•œ F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ð•œ F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  -- declare functions, sets, points and smoothness indices
  {f : M â†’ M'}
  {s : Set M} {m n : â„•âˆž}

-- Porting note: section about deducing differentiability from smoothness moved to
-- `Geometry.Manifold.MFDeriv.Basic`

/-! ### The derivative of a smooth function is smooth -/

section mfderiv
variable [Is : SmoothManifoldWithCorners I M] [I's : SmoothManifoldWithCorners I' M']

/-- The function that sends `x` to the `y`-derivative of `f (x, y)` at `g (x)` is `C^m` at `xâ‚€`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^n` at `(xâ‚€, g(xâ‚€))` for `n â‰¥ m + 1` and `g` is `C^m` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
Version within a set.
-/
protected theorem ContMDiffWithinAt.mfderivWithin {xâ‚€ : N} {f : N â†’ M â†’ M'} {g : N â†’ M}
    {t : Set N} {u : Set M}
    (hf : ContMDiffWithinAt (J.prod I) I' n (Function.uncurry f) (t Ã—Ë¢ u) (xâ‚€, g xâ‚€))
    (hg : ContMDiffWithinAt J I m g t xâ‚€) (hxâ‚€ : xâ‚€ âˆˆ t)
    (hu : MapsTo g t u) (hmn : m + 1 â‰¤ n) (h'u : UniqueMDiffOn I u) :
    ContMDiffWithinAt J ð“˜(ð•œ, E â†’L[ð•œ] E') m
      (inTangentCoordinates I I' g (fun x => f x (g x))
        (fun x => mfderivWithin I I' (f x) u (g x)) xâ‚€) t xâ‚€ := by
  -- first localize the result to a smaller set, to make sure everything happens in chart domains
  let t' := t âˆ© g â»Â¹' ((extChartAt I (g xâ‚€)).source)
  have ht't : t' âŠ† t := inter_subset_left
  suffices ContMDiffWithinAt J ð“˜(ð•œ, E â†’L[ð•œ] E') m
      (inTangentCoordinates I I' g (fun x â†¦ f x (g x))
        (fun x â†¦ mfderivWithin I I' (f x) u (g x)) xâ‚€) t' xâ‚€ by
    apply ContMDiffWithinAt.mono_of_mem_nhdsWithin this
    apply inter_mem self_mem_nhdsWithin
    exact hg.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (g xâ‚€))
  -- register a few basic facts that maps send suitable neighborhoods to suitable neighborhoods,
  -- by continuity
  have hxâ‚€gxâ‚€ : (xâ‚€, g xâ‚€) âˆˆ t Ã—Ë¢ u := by simp [hxâ‚€, hu hxâ‚€]
  have h4f : ContinuousWithinAt (fun x => f x (g x)) t xâ‚€ := by
    change ContinuousWithinAt ((Function.uncurry f) âˆ˜ (fun x â†¦ (x, g x))) t xâ‚€
    refine ContinuousWithinAt.comp hf.continuousWithinAt ?_ (fun y hy â†¦ by simp [hy, hu hy])
    exact (continuousWithinAt_id.prod hg.continuousWithinAt)
  have h4f := h4f.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (I := I') (f xâ‚€ (g xâ‚€)))
  have h3f := contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin.mp
    (hf.of_le <| (self_le_add_left 1 m).trans hmn)
  simp only [Nat.cast_one, hxâ‚€gxâ‚€, insert_eq_of_mem] at h3f
  have h2f : âˆ€á¶  xâ‚‚ in ð“[t] xâ‚€, ContMDiffWithinAt I I' 1 (f xâ‚‚) u (g xâ‚‚) := by
    have : MapsTo (fun x â†¦ (x, g x)) t (t Ã—Ë¢ u) := fun y hy â†¦ by simp [hy, hu hy]
    filter_upwards [((continuousWithinAt_id.prod hg.continuousWithinAt)
      |>.tendsto_nhdsWithin this).eventually h3f, self_mem_nhdsWithin] with x hx h'x
    apply hx.comp (g x) (contMDiffWithinAt_const.prod_mk contMDiffWithinAt_id)
    exact fun y hy â†¦ by simp [h'x, hy]
  have h2g : g â»Â¹' (extChartAt I (g xâ‚€)).source âˆˆ ð“[t] xâ‚€ :=
    hg.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (g xâ‚€))
  -- key point: the derivative of `f` composed with extended charts, at the point `g x` read in the
  -- chart, is smooth in the vector space sense. This follows from `ContDiffWithinAt.fderivWithin`,
  -- which is the vector space analogue of the result we are proving.
  have : ContDiffWithinAt ð•œ m (fun x â†¦ fderivWithin ð•œ
        (extChartAt I' (f xâ‚€ (g xâ‚€)) âˆ˜ f ((extChartAt J xâ‚€).symm x) âˆ˜ (extChartAt I (g xâ‚€)).symm)
        ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
        (extChartAt I (g xâ‚€) (g ((extChartAt J xâ‚€).symm x))))
      ((extChartAt J xâ‚€).symm â»Â¹' t' âˆ© range J) (extChartAt J xâ‚€ xâ‚€) := by
    have hf' := hf.mono (prod_mono_left ht't)
    have hg' := hg.mono (show t' âŠ† t from inter_subset_left)
    rw [contMDiffWithinAt_iff] at hf' hg'
    simp_rw [Function.comp_def, uncurry, extChartAt_prod, PartialEquiv.prod_coe_symm,
      ModelWithCorners.range_prod] at hf' âŠ¢
    apply ContDiffWithinAt.fderivWithin _ _ _ (show (m : WithTop â„•âˆž) + 1 â‰¤ n from mod_cast hmn )
    Â· simp [hxâ‚€, t']
    Â· apply inter_subset_left.trans
      rw [preimage_subset_iff]
      intro a ha
      refine âŸ¨PartialEquiv.map_source _ (inter_subset_right ha : _), ?_âŸ©
      rw [mem_preimage, PartialEquiv.left_inv (extChartAt I (g xâ‚€))]
      Â· exact hu (inter_subset_left ha)
      Â· exact (inter_subset_right ha :)
    Â· have : ((fun p â†¦ ((extChartAt J xâ‚€).symm p.1, (extChartAt I (g xâ‚€)).symm p.2)) â»Â¹' t' Ã—Ë¢ u
            âˆ© range J Ã—Ë¢ (extChartAt I (g xâ‚€)).target)
          âŠ† ((fun p â†¦ ((extChartAt J xâ‚€).symm p.1, (extChartAt I (g xâ‚€)).symm p.2)) â»Â¹' t' Ã—Ë¢ u
            âˆ© range J Ã—Ë¢ range I) := by
        apply inter_subset_inter_right
        exact Set.prod_mono_right (extChartAt_target_subset_range (g xâ‚€))
      convert hf'.2.mono this
      Â· ext y; simp; tauto
      Â· simp
    Â· exact hg'.2
    Â· exact UniqueMDiffOn.uniqueDiffOn_target_inter h'u (g xâ‚€)
  -- reformulate the previous point as smoothness in the manifold sense (but still for a map between
  -- vector spaces)
  have :
    ContMDiffWithinAt J ð“˜(ð•œ, E â†’L[ð•œ] E') m
      (fun x =>
        fderivWithin ð•œ (extChartAt I' (f xâ‚€ (g xâ‚€)) âˆ˜ f x âˆ˜ (extChartAt I (g xâ‚€)).symm)
        ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
          (extChartAt I (g xâ‚€) (g x))) t' xâ‚€ := by
    simp_rw [contMDiffWithinAt_iff_source_of_mem_source (mem_chart_source G xâ‚€),
      contMDiffWithinAt_iff_contDiffWithinAt, Function.comp_def] at this âŠ¢
    exact this
  -- finally, argue that the map we control in the previous point coincides locally with the map we
  -- want to prove the smoothness of, so smoothness of the latter follows from smoothness of the
  -- former.
  apply this.congr_of_eventuallyEq_of_mem _ (by simp [t', hxâ‚€])
  apply nhdsWithin_mono _ ht't
  filter_upwards [h2f, h4f, h2g, self_mem_nhdsWithin] with x hx h'x h2 hxt
  have h1 : g x âˆˆ u := hu hxt
  have h3 : UniqueMDiffWithinAt ð“˜(ð•œ, E)
      ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
      ((extChartAt I (g xâ‚€)) (g x)) := by
    apply UniqueDiffWithinAt.uniqueMDiffWithinAt
    apply UniqueMDiffOn.uniqueDiffOn_target_inter h'u
    refine âŸ¨PartialEquiv.map_source _ h2, ?_âŸ©
    rwa [mem_preimage, PartialEquiv.left_inv _ h2]
  have A : mfderivWithin ð“˜(ð•œ, E) I ((extChartAt I (g xâ‚€)).symm)
        (range I) ((extChartAt I (g xâ‚€)) (g x))
      = mfderivWithin ð“˜(ð•œ, E) I ((extChartAt I (g xâ‚€)).symm)
        ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
        ((extChartAt I (g xâ‚€)) (g x)) := by
    apply (MDifferentiableWithinAt.mfderivWithin_mono _ h3 _).symm
    Â· apply mdifferentiableWithinAt_extChartAt_symm
      exact PartialEquiv.map_source (extChartAt I (g xâ‚€)) h2
    Â· exact inter_subset_left.trans (extChartAt_target_subset_range (g xâ‚€))
  rw [inTangentCoordinates_eq_mfderiv_comp, A,
    â† mfderivWithin_comp_of_eq, â† mfderiv_comp_mfderivWithin_of_eq]
  Â· exact mfderivWithin_eq_fderivWithin
  Â· exact mdifferentiableAt_extChartAt (by simpa using h'x)
  Â· apply MDifferentiableWithinAt.comp (I' := I) (u := u) _ _ _ inter_subset_right
    Â· convert hx.mdifferentiableWithinAt le_rfl
      exact PartialEquiv.left_inv (extChartAt I (g xâ‚€)) h2
    Â· apply (mdifferentiableWithinAt_extChartAt_symm _).mono
      Â· exact inter_subset_left.trans (extChartAt_target_subset_range (g xâ‚€))
      Â· exact PartialEquiv.map_source (extChartAt I (g xâ‚€)) h2
  Â· exact h3
  Â· simp only [Function.comp_def, PartialEquiv.left_inv (extChartAt I (g xâ‚€)) h2]
  Â· exact hx.mdifferentiableWithinAt le_rfl
  Â· apply (mdifferentiableWithinAt_extChartAt_symm _).mono
    Â· exact inter_subset_left.trans (extChartAt_target_subset_range (g xâ‚€))
    Â· exact PartialEquiv.map_source (extChartAt I (g xâ‚€)) h2
  Â· exact inter_subset_right
  Â· exact h3
  Â· exact PartialEquiv.left_inv (extChartAt I (g xâ‚€)) h2
  Â· simpa using h2
  Â· simpa using h'x

/-- The derivative `D_yf(y)` is `C^m` at `xâ‚€`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^n` at `xâ‚€` for some `n â‰¥ m + 1`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This is a special case of `ContMDiffWithinAt.mfderivWithin` where `f` does not contain any
parameters and `g = id`.
-/
theorem ContMDiffWithinAt.mfderivWithin_const {xâ‚€ : M} {f : M â†’ M'}
    (hf : ContMDiffWithinAt I I' n f s xâ‚€)
    (hmn : m + 1 â‰¤ n) (hx : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) :
    ContMDiffWithinAt I ð“˜(ð•œ, E â†’L[ð•œ] E') m
      (inTangentCoordinates I I' id f (mfderivWithin I I' f s) xâ‚€) s xâ‚€ := by
  have : ContMDiffWithinAt (I.prod I) I' n (fun x : M Ã— M => f x.2) (s Ã—Ë¢ s) (xâ‚€, xâ‚€) :=
    ContMDiffWithinAt.comp (xâ‚€, xâ‚€) hf contMDiffWithinAt_snd mapsTo_snd_prod
  exact this.mfderivWithin contMDiffWithinAt_id hx (mapsTo_id _) hmn hs

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `gâ‚‚(x)` is
`C^n` at `xâ‚€`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(xâ‚€, g(xâ‚€))` and `g` is `C^n` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `gâ‚(x)` to make the derivative sensible.

This is similar to `ContMDiffWithinAt.mfderivWithin`, but where the continuous linear map is
applied to a (variable) vector.
-/
theorem ContMDiffWithinAt.mfderivWithin_apply {xâ‚€ : N'}
    {f : N â†’ M â†’ M'} {g : N â†’ M} {gâ‚ : N' â†’ N} {gâ‚‚ : N' â†’ E} {t : Set N} {u : Set M} {v : Set N'}
    (hf : ContMDiffWithinAt (J.prod I) I' n (Function.uncurry f) (t Ã—Ë¢ u) (gâ‚ xâ‚€, g (gâ‚ xâ‚€)))
    (hg : ContMDiffWithinAt J I m g t (gâ‚ xâ‚€)) (hgâ‚ : ContMDiffWithinAt J' J m gâ‚ v xâ‚€)
    (hgâ‚‚ : ContMDiffWithinAt J' ð“˜(ð•œ, E) m gâ‚‚ v xâ‚€) (hmn : m + 1 â‰¤ n) (h'gâ‚ : MapsTo gâ‚ v t)
    (hgâ‚xâ‚€ : gâ‚ xâ‚€ âˆˆ t) (h'g : MapsTo g t u) (hu : UniqueMDiffOn I u) :
    ContMDiffWithinAt J' ð“˜(ð•œ, E') m
      (fun x => (inTangentCoordinates I I' g (fun x => f x (g x))
        (fun x => mfderivWithin I I' (f x) u (g x)) (gâ‚ xâ‚€) (gâ‚ x)) (gâ‚‚ x)) v xâ‚€ :=
  ((hf.mfderivWithin hg hgâ‚xâ‚€ h'g hmn hu).comp_of_eq hgâ‚ h'gâ‚ rfl).clm_apply hgâ‚‚

/-- The function that sends `x` to the `y`-derivative of `f (x, y)` at `g (x)` is `C^m` at `xâ‚€`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^n` at `(xâ‚€, g(xâ‚€))` for `n â‰¥ m + 1` and `g` is `C^m` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This result is used to show that maps into the 1-jet bundle and cotangent bundle are smooth.
`ContMDiffAt.mfderiv_const` is a special case of this.
-/
protected theorem ContMDiffAt.mfderiv {xâ‚€ : N} (f : N â†’ M â†’ M') (g : N â†’ M)
    (hf : ContMDiffAt (J.prod I) I' n (Function.uncurry f) (xâ‚€, g xâ‚€)) (hg : ContMDiffAt J I m g xâ‚€)
    (hmn : m + 1 â‰¤ n) :
    ContMDiffAt J ð“˜(ð•œ, E â†’L[ð•œ] E') m
      (inTangentCoordinates I I' g (fun x â†¦ f x (g x)) (fun x â†¦ mfderiv I I' (f x) (g x)) xâ‚€)
      xâ‚€ := by
  rw [â† contMDiffWithinAt_univ] at hf hg âŠ¢
  rw [â† univ_prod_univ] at hf
  simp_rw [â† mfderivWithin_univ]
  exact ContMDiffWithinAt.mfderivWithin hf hg (mem_univ _) (mapsTo_univ _ _) hmn
    uniqueMDiffOn_univ

/-- The derivative `D_yf(y)` is `C^m` at `xâ‚€`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^n` at `xâ‚€` for some `n â‰¥ m + 1`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This is a special case of `ContMDiffAt.mfderiv` where `f` does not contain any parameters and
`g = id`.
-/
theorem ContMDiffAt.mfderiv_const {xâ‚€ : M} {f : M â†’ M'} (hf : ContMDiffAt I I' n f xâ‚€)
    (hmn : m + 1 â‰¤ n) :
    ContMDiffAt I ð“˜(ð•œ, E â†’L[ð•œ] E') m (inTangentCoordinates I I' id f (mfderiv I I' f) xâ‚€) xâ‚€ :=
  haveI : ContMDiffAt (I.prod I) I' n (fun x : M Ã— M => f x.2) (xâ‚€, xâ‚€) :=
    ContMDiffAt.comp (xâ‚€, xâ‚€) hf contMDiffAt_snd
  this.mfderiv (fun _ => f) id contMDiffAt_id hmn

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `gâ‚‚(x)` is
`C^n` at `xâ‚€`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(xâ‚€, g(xâ‚€))` and `g` is `C^n` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `gâ‚(x)` to make the derivative sensible.

This is similar to `ContMDiffAt.mfderiv`, but where the continuous linear map is applied to a
(variable) vector.
-/
theorem ContMDiffAt.mfderiv_apply {xâ‚€ : N'} (f : N â†’ M â†’ M') (g : N â†’ M) (gâ‚ : N' â†’ N) (gâ‚‚ : N' â†’ E)
    (hf : ContMDiffAt (J.prod I) I' n (Function.uncurry f) (gâ‚ xâ‚€, g (gâ‚ xâ‚€)))
    (hg : ContMDiffAt J I m g (gâ‚ xâ‚€)) (hgâ‚ : ContMDiffAt J' J m gâ‚ xâ‚€)
    (hgâ‚‚ : ContMDiffAt J' ð“˜(ð•œ, E) m gâ‚‚ xâ‚€) (hmn : m + 1 â‰¤ n) :
    ContMDiffAt J' ð“˜(ð•œ, E') m
      (fun x => inTangentCoordinates I I' g (fun x => f x (g x))
        (fun x => mfderiv I I' (f x) (g x)) (gâ‚ xâ‚€) (gâ‚ x) (gâ‚‚ x)) xâ‚€ :=
  ((hf.mfderiv f g hg hmn).comp_of_eq hgâ‚ rfl).clm_apply hgâ‚‚

end mfderiv

/-! ### The tangent map of a smooth function is smooth -/

section tangentMap

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative
is `C^m` when `m+1 â‰¤ n`. -/
theorem ContMDiffOn.contMDiffOn_tangentMapWithin
    [Is : SmoothManifoldWithCorners I M] [I's : SmoothManifoldWithCorners I' M']
    (hf : ContMDiffOn I I' n f s) (hmn : m + 1 â‰¤ n)
    (hs : UniqueMDiffOn I s) :
    ContMDiffOn I.tangent I'.tangent m (tangentMapWithin I I' f s)
      (Ï€ E (TangentSpace I) â»Â¹' s) := by
  intro xâ‚€ hxâ‚€
  let s' : Set (TangentBundle I M) := (Ï€ E (TangentSpace I) â»Â¹' s)
  let bâ‚ : TangentBundle I M â†’ M := fun p â†¦ p.1
  let v : Î  (y : TangentBundle I M), TangentSpace I (bâ‚ y) := fun y â†¦ y.2
  have hv : ContMDiffWithinAt I.tangent I.tangent m (fun y â†¦ (v y : TangentBundle I M)) s' xâ‚€ :=
    contMDiffWithinAt_id
  let bâ‚‚ : TangentBundle I M â†’ M' := f âˆ˜ bâ‚
  have hbâ‚‚ : ContMDiffWithinAt I.tangent I' m bâ‚‚ s' xâ‚€ :=
    ((hf (bâ‚ xâ‚€) hxâ‚€).of_le (le_self_add.trans hmn)).comp _
      (contMDiffWithinAt_proj (TangentSpace I)) (fun x h â†¦ h)
  let Ï• : Î  (y : TangentBundle I M), TangentSpace I (bâ‚ y) â†’L[ð•œ] TangentSpace I' (bâ‚‚ y) :=
    fun y â†¦ mfderivWithin I I' f s (bâ‚ y)
  have hÏ• : ContMDiffWithinAt I.tangent ð“˜(ð•œ, E â†’L[ð•œ] E') m
      (fun y â†¦ ContinuousLinearMap.inCoordinates E (TangentSpace I (M := M)) E'
        (TangentSpace I' (M := M')) (bâ‚ xâ‚€) (bâ‚ y) (bâ‚‚ xâ‚€) (bâ‚‚ y) (Ï• y))
      s' xâ‚€ := by
    have A : ContMDiffWithinAt I ð“˜(ð•œ, E â†’L[ð•œ] E') m
        (fun y â†¦ ContinuousLinearMap.inCoordinates E (TangentSpace I (M := M)) E'
          (TangentSpace I' (M := M')) (bâ‚ xâ‚€) y (bâ‚‚ xâ‚€) (f y) (mfderivWithin I I' f s y))
        s (bâ‚ xâ‚€) :=
      ContMDiffWithinAt.mfderivWithin_const (hf _ hxâ‚€) hmn hxâ‚€ hs
    exact A.comp _ (contMDiffWithinAt_proj (TangentSpace I)) (fun x h â†¦ h)
  exact ContMDiffWithinAt.clm_apply_of_inCoordinates hÏ• hv hbâ‚‚

@[deprecated (since := "2024-10-07")]
alias ContMDiffOn.contMDiffOn_tangentMapWithin_aux := ContMDiffOn.contMDiffOn_tangentMapWithin

@[deprecated (since := "2024-10-07")]
alias ContMDiffOn.continuousOn_tangentMapWithin_aux := ContMDiffOn.contMDiffOn_tangentMapWithin

variable [Is : SmoothManifoldWithCorners I M] [I's : SmoothManifoldWithCorners I' M']

/-- If a function is `C^n` on a domain with unique derivatives, with `1 â‰¤ n`, then its bundled
derivative is continuous there. -/
theorem ContMDiffOn.continuousOn_tangentMapWithin (hf : ContMDiffOn I I' n f s) (hmn : 1 â‰¤ n)
    (hs : UniqueMDiffOn I s) :
    ContinuousOn (tangentMapWithin I I' f s) (Ï€ E (TangentSpace I) â»Â¹' s) :=
  haveI :
    ContMDiffOn I.tangent I'.tangent 0 (tangentMapWithin I I' f s) (Ï€ E (TangentSpace I) â»Â¹' s) :=
    hf.contMDiffOn_tangentMapWithin hmn hs
  this.continuousOn

/-- If a function is `C^n`, then its bundled derivative is `C^m` when `m+1 â‰¤ n`. -/
theorem ContMDiff.contMDiff_tangentMap (hf : ContMDiff I I' n f) (hmn : m + 1 â‰¤ n) :
    ContMDiff I.tangent I'.tangent m (tangentMap I I' f) := by
  rw [â† contMDiffOn_univ] at hf âŠ¢
  convert hf.contMDiffOn_tangentMapWithin hmn uniqueMDiffOn_univ
  rw [tangentMapWithin_univ]

/-- If a function is `C^n`, with `1 â‰¤ n`, then its bundled derivative is continuous. -/
theorem ContMDiff.continuous_tangentMap (hf : ContMDiff I I' n f) (hmn : 1 â‰¤ n) :
    Continuous (tangentMap I I' f) := by
  rw [â† contMDiffOn_univ] at hf
  rw [continuous_iff_continuousOn_univ]
  convert hf.continuousOn_tangentMapWithin hmn uniqueMDiffOn_univ
  rw [tangentMapWithin_univ]

@[deprecated (since := "2024-11-21")] alias Smooth.tangentMap := ContMDiff.contMDiff_tangentMap

end tangentMap

namespace TangentBundle

open Bundle

/-- The derivative of the zero section of the tangent bundle maps `âŸ¨x, vâŸ©` to `âŸ¨âŸ¨x, 0âŸ©, âŸ¨v, 0âŸ©âŸ©`.

Note that, as currently framed, this is a statement in coordinates, thus reliant on the choice
of the coordinate system we use on the tangent bundle.

However, the result itself is coordinate-dependent only to the extent that the coordinates
determine a splitting of the tangent bundle.  Moreover, there is a canonical splitting at each
point of the zero section (since there is a canonical horizontal space there, the tangent space
to the zero section, in addition to the canonical vertical space which is the kernel of the
derivative of the projection), and this canonical splitting is also the one that comes from the
coordinates on the tangent bundle in our definitions. So this statement is not as crazy as it
may seem.

TODO define splittings of vector bundles; state this result invariantly. -/
theorem tangentMap_tangentBundle_pure [Is : SmoothManifoldWithCorners I M] (p : TangentBundle I M) :
    tangentMap I I.tangent (zeroSection E (TangentSpace I)) p = âŸ¨âŸ¨p.proj, 0âŸ©, âŸ¨p.2, 0âŸ©âŸ© := by
  rcases p with âŸ¨x, vâŸ©
  have N : I.symm â»Â¹' (chartAt H x).target âˆˆ ð“ (I ((chartAt H x) x)) := by
    apply IsOpen.mem_nhds
    Â· apply (PartialHomeomorph.open_target _).preimage I.continuous_invFun
    Â· simp only [mfld_simps]
  have A : MDifferentiableAt I I.tangent (fun x => @TotalSpace.mk M E (TangentSpace I) x 0) x :=
    haveI : ContMDiff I (I.prod ð“˜(ð•œ, E)) âŠ¤ (zeroSection E (TangentSpace I : M â†’ Type _)) :=
      Bundle.contMDiff_zeroSection ð•œ (TangentSpace I : M â†’ Type _)
    this.mdifferentiableAt le_top
  have B : fderivWithin ð•œ (fun x' : E â†¦ (x', (0 : E))) (Set.range I) (I ((chartAt H x) x)) v
      = (v, 0) := by
    rw [fderivWithin_eq_fderiv, DifferentiableAt.fderiv_prod]
    Â· simp
    Â· exact differentiableAt_id'
    Â· exact differentiableAt_const _
    Â· exact ModelWithCorners.uniqueDiffWithinAt_image I
    Â· exact differentiableAt_id'.prod (differentiableAt_const _)
  simp (config := { unfoldPartialApp := true }) only [Bundle.zeroSection, tangentMap, mfderiv, A,
    if_pos, chartAt, FiberBundle.chartedSpace_chartAt, TangentBundle.trivializationAt_apply,
    tangentBundleCore, Function.comp_def, ContinuousLinearMap.map_zero, mfld_simps]
  rw [â† fderivWithin_inter N] at B
  rw [â† fderivWithin_inter N, â† B]
  congr 1
  refine fderivWithin_congr (fun y hy => ?_) ?_
  Â· simp only [mfld_simps] at hy
    simp only [hy, Prod.mk.inj_iff, mfld_simps]
  Â· simp only [Prod.mk.inj_iff, mfld_simps]

end TangentBundle

namespace ContMDiffMap

-- These helpers for dot notation have been moved here from
-- `Mathlib/Geometry/Manifold/ContMDiffMap.lean` to avoid needing to import this file there.
-- (However as a consequence we import `Mathlib/Geometry/Manifold/ContMDiffMap.lean` here now.)
-- They could be moved to another file (perhaps a new file) if desired.
open scoped Manifold
/- Next line is necessary while the manifold smoothness class is not extended to `Ï‰`.
Later, replace with `open scoped ContDiff`. -/
local notation "âˆž" => (âŠ¤ : â„•âˆž)

protected theorem mdifferentiable' (f : C^nâŸ®I, M; I', M'âŸ¯) (hn : 1 â‰¤ n) : MDifferentiable I I' f :=
  f.contMDiff.mdifferentiable hn

protected theorem mdifferentiable (f : C^âˆžâŸ®I, M; I', M'âŸ¯) : MDifferentiable I I' f :=
  f.contMDiff.mdifferentiable le_top

protected theorem mdifferentiableAt (f : C^âˆžâŸ®I, M; I', M'âŸ¯) {x} : MDifferentiableAt I I' f x :=
  f.mdifferentiable x

end ContMDiffMap
