/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.MFDeriv.Tangent
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.VectorBundle.Hom

/-!
### Interactions between differentiability, smoothness and manifold derivatives

We give the relation between `MDifferentiable`, `ContMDiff`, `mfderiv`, `tangentMap`
and related notions.

## Main statements

* `ContMDiffOn.contMDiffOn_tangentMapWithin` states that the bundled derivative
  of a `C‚Åø` function in a domain is `C·µê` when `m + 1 ‚â§ n`.
* `ContMDiff.contMDiff_tangentMap` states that the bundled derivative
  of a `C‚Åø` function is `C·µê` when `m + 1 ‚â§ n`.
-/

open Set Function Filter ChartedSpace IsManifold Bundle

open scoped Topology Manifold Bundle

/-! ### Definition of `C^n` functions between manifolds -/


variable {ùïú : Type*} [NontriviallyNormedField ùïú] {m n : WithTop ‚Ñï‚àû}
  -- declare a charted space `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ùïú E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- declare a charted space `M'` over the pair `(E', H')`.
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ùïú E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- declare a `C^n` manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ùïú F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ùïú F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [Js : IsManifold J 1 N]
  -- declare a charted space `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ùïú F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ùïú F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  -- declare functions, sets
  {f : M ‚Üí M'} {s : Set M}

/-! ### The derivative of a `C^(n+1)` function is `C^n` -/

section mfderiv
variable [Is : IsManifold I 1 M] [I's : IsManifold I' 1 M']

/-- The function that sends `x` to the `y`-derivative of `f (x, y)` at `g (x)` is `C^m` at `x‚ÇÄ`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^n` at `(x‚ÇÄ, g(x‚ÇÄ))` for `n ‚â• m + 1` and `g` is `C^m` at `x‚ÇÄ`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
Version within a set.
-/
protected theorem ContMDiffWithinAt.mfderivWithin {x‚ÇÄ : N} {f : N ‚Üí M ‚Üí M'} {g : N ‚Üí M}
    {t : Set N} {u : Set M}
    (hf : ContMDiffWithinAt (J.prod I) I' n (Function.uncurry f) (t √óÀ¢ u) (x‚ÇÄ, g x‚ÇÄ))
    (hg : ContMDiffWithinAt J I m g t x‚ÇÄ) (hx‚ÇÄ : x‚ÇÄ ‚àà t)
    (hu : MapsTo g t u) (hmn : m + 1 ‚â§ n) (h'u : UniqueMDiffOn I u) :
    ContMDiffWithinAt J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (inTangentCoordinates I I' g (fun x => f x (g x))
        (fun x => mfderivWithin I I' (f x) u (g x)) x‚ÇÄ) t x‚ÇÄ := by
  -- first localize the result to a smaller set, to make sure everything happens in chart domains
  let t' := t ‚à© g ‚Åª¬π' ((extChartAt I (g x‚ÇÄ)).source)
  have ht't : t' ‚äÜ t := inter_subset_left
  suffices ContMDiffWithinAt J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (inTangentCoordinates I I' g (fun x ‚Ü¶ f x (g x))
        (fun x ‚Ü¶ mfderivWithin I I' (f x) u (g x)) x‚ÇÄ) t' x‚ÇÄ by
    apply ContMDiffWithinAt.mono_of_mem_nhdsWithin this
    apply inter_mem self_mem_nhdsWithin
    exact hg.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (g x‚ÇÄ))
  -- register a few basic facts that maps send suitable neighborhoods to suitable neighborhoods,
  -- by continuity
  have hx‚ÇÄgx‚ÇÄ : (x‚ÇÄ, g x‚ÇÄ) ‚àà t √óÀ¢ u := by simp [hx‚ÇÄ, hu hx‚ÇÄ]
  have h4f : ContinuousWithinAt (fun x => f x (g x)) t x‚ÇÄ := by
    change ContinuousWithinAt ((Function.uncurry f) ‚àò (fun x ‚Ü¶ (x, g x))) t x‚ÇÄ
    refine ContinuousWithinAt.comp hf.continuousWithinAt ?_ (fun y hy ‚Ü¶ by simp [hy, hu hy])
    exact (continuousWithinAt_id.prodMk hg.continuousWithinAt)
  have h4f := h4f.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (I := I') (f x‚ÇÄ (g x‚ÇÄ)))
  have h3f := (contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin (by simp)).mp
    (hf.of_le <| (self_le_add_left 1 m).trans hmn)
  simp only [hx‚ÇÄgx‚ÇÄ, insert_eq_of_mem] at h3f
  have h2f : ‚àÄ·∂† x‚ÇÇ in ùìù[t] x‚ÇÄ, ContMDiffWithinAt I I' 1 (f x‚ÇÇ) u (g x‚ÇÇ) := by
    have : MapsTo (fun x ‚Ü¶ (x, g x)) t (t √óÀ¢ u) := fun y hy ‚Ü¶ by simp [hy, hu hy]
    filter_upwards [((continuousWithinAt_id.prodMk hg.continuousWithinAt)
      |>.tendsto_nhdsWithin this).eventually h3f, self_mem_nhdsWithin] with x hx h'x
    apply hx.comp (g x) (contMDiffWithinAt_const.prodMk contMDiffWithinAt_id)
    exact fun y hy ‚Ü¶ by simp [h'x, hy]
  have h2g : g ‚Åª¬π' (extChartAt I (g x‚ÇÄ)).source ‚àà ùìù[t] x‚ÇÄ :=
    hg.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (g x‚ÇÄ))
  -- key point: the derivative of `f` composed with extended charts, at the point `g x` read in the
  -- chart, is `C^n` in the vector space sense. This follows from `ContDiffWithinAt.fderivWithin`,
  -- which is the vector space analogue of the result we are proving.
  have : ContDiffWithinAt ùïú m (fun x ‚Ü¶ fderivWithin ùïú
        (extChartAt I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò f ((extChartAt J x‚ÇÄ).symm x) ‚àò (extChartAt I (g x‚ÇÄ)).symm)
        ((extChartAt I (g x‚ÇÄ)).target ‚à© (extChartAt I (g x‚ÇÄ)).symm ‚Åª¬π' u)
        (extChartAt I (g x‚ÇÄ) (g ((extChartAt J x‚ÇÄ).symm x))))
      ((extChartAt J x‚ÇÄ).symm ‚Åª¬π' t' ‚à© range J) (extChartAt J x‚ÇÄ x‚ÇÄ) := by
    have hf' := hf.mono (prod_mono_left ht't)
    have hg' := hg.mono (show t' ‚äÜ t from inter_subset_left)
    rw [contMDiffWithinAt_iff] at hf' hg'
    simp_rw [Function.comp_def, uncurry, extChartAt_prod, PartialEquiv.prod_coe_symm,
      ModelWithCorners.range_prod] at hf' ‚ä¢
    apply ContDiffWithinAt.fderivWithin _ _ _ (show (m : WithTop ‚Ñï‚àû) + 1 ‚â§ n from mod_cast hmn )
    ¬∑ simp [hx‚ÇÄ, t']
    ¬∑ apply inter_subset_left.trans
      rw [preimage_subset_iff]
      intro a ha
      refine ‚ü®PartialEquiv.map_source _ (inter_subset_right ha :), ?_‚ü©
      rw [mem_preimage, PartialEquiv.left_inv (extChartAt I (g x‚ÇÄ))]
      ¬∑ exact hu (inter_subset_left ha)
      ¬∑ exact (inter_subset_right ha :)
    ¬∑ have : ((fun p ‚Ü¶ ((extChartAt J x‚ÇÄ).symm p.1, (extChartAt I (g x‚ÇÄ)).symm p.2)) ‚Åª¬π' t' √óÀ¢ u
            ‚à© range J √óÀ¢ (extChartAt I (g x‚ÇÄ)).target)
          ‚äÜ ((fun p ‚Ü¶ ((extChartAt J x‚ÇÄ).symm p.1, (extChartAt I (g x‚ÇÄ)).symm p.2)) ‚Åª¬π' t' √óÀ¢ u
            ‚à© range J √óÀ¢ range I) := by
        apply inter_subset_inter_right
        exact Set.prod_mono_right (extChartAt_target_subset_range (g x‚ÇÄ))
      convert hf'.2.mono this
      ¬∑ ext y; simp; tauto
      ¬∑ simp
    ¬∑ exact hg'.2
    ¬∑ exact UniqueMDiffOn.uniqueDiffOn_target_inter h'u (g x‚ÇÄ)
  -- reformulate the previous point as `C^n` in the manifold sense (but still for a map between
  -- vector spaces)
  have :
    ContMDiffWithinAt J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (fun x =>
        fderivWithin ùïú (extChartAt I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò f x ‚àò (extChartAt I (g x‚ÇÄ)).symm)
        ((extChartAt I (g x‚ÇÄ)).target ‚à© (extChartAt I (g x‚ÇÄ)).symm ‚Åª¬π' u)
          (extChartAt I (g x‚ÇÄ) (g x))) t' x‚ÇÄ := by
    simp_rw [contMDiffWithinAt_iff_source (x := x‚ÇÄ),
      contMDiffWithinAt_iff_contDiffWithinAt, Function.comp_def]
    exact this
  -- finally, argue that the map we control in the previous point coincides locally with the map we
  -- want to prove the regularity of, so regularity of the latter follows from regularity of the
  -- former.
  apply this.congr_of_eventuallyEq_of_mem _ (by simp [t', hx‚ÇÄ])
  apply nhdsWithin_mono _ ht't
  filter_upwards [h2f, h4f, h2g, self_mem_nhdsWithin] with x hx h'x h2 hxt
  have h1 : g x ‚àà u := hu hxt
  have h3 : UniqueMDiffWithinAt ùìò(ùïú, E)
      ((extChartAt I (g x‚ÇÄ)).target ‚à© (extChartAt I (g x‚ÇÄ)).symm ‚Åª¬π' u)
      ((extChartAt I (g x‚ÇÄ)) (g x)) := by
    apply UniqueDiffWithinAt.uniqueMDiffWithinAt
    apply UniqueMDiffOn.uniqueDiffOn_target_inter h'u
    refine ‚ü®PartialEquiv.map_source _ h2, ?_‚ü©
    rwa [mem_preimage, PartialEquiv.left_inv _ h2]
  have A : mfderivWithin ùìò(ùïú, E) I ((extChartAt I (g x‚ÇÄ)).symm)
        (range I) ((extChartAt I (g x‚ÇÄ)) (g x))
      = mfderivWithin ùìò(ùïú, E) I ((extChartAt I (g x‚ÇÄ)).symm)
        ((extChartAt I (g x‚ÇÄ)).target ‚à© (extChartAt I (g x‚ÇÄ)).symm ‚Åª¬π' u)
        ((extChartAt I (g x‚ÇÄ)) (g x)) := by
    apply (MDifferentiableWithinAt.mfderivWithin_mono _ h3 _).symm
    ¬∑ apply mdifferentiableWithinAt_extChartAt_symm
      exact PartialEquiv.map_source (extChartAt I (g x‚ÇÄ)) h2
    ¬∑ exact inter_subset_left.trans (extChartAt_target_subset_range (g x‚ÇÄ))
  rw [inTangentCoordinates_eq_mfderiv_comp, A,
    ‚Üê mfderivWithin_comp_of_eq, ‚Üê mfderiv_comp_mfderivWithin_of_eq]
  ¬∑ exact mfderivWithin_eq_fderivWithin
  ¬∑ exact mdifferentiableAt_extChartAt (by simpa using h'x)
  ¬∑ apply MDifferentiableWithinAt.comp (I' := I) (u := u) _ _ _ inter_subset_right
    ¬∑ convert hx.mdifferentiableWithinAt le_rfl
      exact PartialEquiv.left_inv (extChartAt I (g x‚ÇÄ)) h2
    ¬∑ apply (mdifferentiableWithinAt_extChartAt_symm _).mono
      ¬∑ exact inter_subset_left.trans (extChartAt_target_subset_range (g x‚ÇÄ))
      ¬∑ exact PartialEquiv.map_source (extChartAt I (g x‚ÇÄ)) h2
  ¬∑ exact h3
  ¬∑ simp only [Function.comp_def, PartialEquiv.left_inv (extChartAt I (g x‚ÇÄ)) h2]
  ¬∑ exact hx.mdifferentiableWithinAt le_rfl
  ¬∑ apply (mdifferentiableWithinAt_extChartAt_symm _).mono
    ¬∑ exact inter_subset_left.trans (extChartAt_target_subset_range (g x‚ÇÄ))
    ¬∑ exact PartialEquiv.map_source (extChartAt I (g x‚ÇÄ)) h2
  ¬∑ exact inter_subset_right
  ¬∑ exact h3
  ¬∑ exact PartialEquiv.left_inv (extChartAt I (g x‚ÇÄ)) h2
  ¬∑ simpa using h2
  ¬∑ simpa using h'x

/-- The derivative `D_yf(y)` is `C^m` at `x‚ÇÄ`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^n` at `x‚ÇÄ` for some `n ‚â• m + 1`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
This is a special case of `ContMDiffWithinAt.mfderivWithin` where `f` does not contain any
parameters and `g = id`.
-/
theorem ContMDiffWithinAt.mfderivWithin_const {x‚ÇÄ : M} {f : M ‚Üí M'}
    (hf : ContMDiffWithinAt I I' n f s x‚ÇÄ)
    (hmn : m + 1 ‚â§ n) (hx : x‚ÇÄ ‚àà s) (hs : UniqueMDiffOn I s) :
    ContMDiffWithinAt I ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (inTangentCoordinates I I' id f (mfderivWithin I I' f s) x‚ÇÄ) s x‚ÇÄ := by
  have : ContMDiffWithinAt (I.prod I) I' n (fun x : M √ó M => f x.2) (s √óÀ¢ s) (x‚ÇÄ, x‚ÇÄ) :=
    ContMDiffWithinAt.comp (x‚ÇÄ, x‚ÇÄ) hf contMDiffWithinAt_snd mapsTo_snd_prod
  exact this.mfderivWithin contMDiffWithinAt_id hx (mapsTo_id _) hmn hs

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `g‚ÇÇ(x)` is
`C^n` at `x‚ÇÄ`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(x‚ÇÄ, g(x‚ÇÄ))` and `g` is `C^n` at `x‚ÇÄ`.
We have to insert a coordinate change from `x‚ÇÄ` to `g‚ÇÅ(x)` to make the derivative sensible.

This is similar to `ContMDiffWithinAt.mfderivWithin`, but where the continuous linear map is
applied to a (variable) vector.
-/
theorem ContMDiffWithinAt.mfderivWithin_apply {x‚ÇÄ : N'}
    {f : N ‚Üí M ‚Üí M'} {g : N ‚Üí M} {g‚ÇÅ : N' ‚Üí N} {g‚ÇÇ : N' ‚Üí E} {t : Set N} {u : Set M} {v : Set N'}
    (hf : ContMDiffWithinAt (J.prod I) I' n (Function.uncurry f) (t √óÀ¢ u) (g‚ÇÅ x‚ÇÄ, g (g‚ÇÅ x‚ÇÄ)))
    (hg : ContMDiffWithinAt J I m g t (g‚ÇÅ x‚ÇÄ)) (hg‚ÇÅ : ContMDiffWithinAt J' J m g‚ÇÅ v x‚ÇÄ)
    (hg‚ÇÇ : ContMDiffWithinAt J' ùìò(ùïú, E) m g‚ÇÇ v x‚ÇÄ) (hmn : m + 1 ‚â§ n) (h'g‚ÇÅ : MapsTo g‚ÇÅ v t)
    (hg‚ÇÅx‚ÇÄ : g‚ÇÅ x‚ÇÄ ‚àà t) (h'g : MapsTo g t u) (hu : UniqueMDiffOn I u) :
    ContMDiffWithinAt J' ùìò(ùïú, E') m
      (fun x => (inTangentCoordinates I I' g (fun x => f x (g x))
        (fun x => mfderivWithin I I' (f x) u (g x)) (g‚ÇÅ x‚ÇÄ) (g‚ÇÅ x)) (g‚ÇÇ x)) v x‚ÇÄ :=
  ((hf.mfderivWithin hg hg‚ÇÅx‚ÇÄ h'g hmn hu).comp_of_eq hg‚ÇÅ h'g‚ÇÅ rfl).clm_apply hg‚ÇÇ

/-- The function that sends `x` to the `y`-derivative of `f (x, y)` at `g (x)` is `C^m` at `x‚ÇÄ`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^n` at `(x‚ÇÄ, g(x‚ÇÄ))` for `n ‚â• m + 1` and `g` is `C^m` at `x‚ÇÄ`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
This result is used to show that maps into the 1-jet bundle and cotangent bundle are `C^n`.
`ContMDiffAt.mfderiv_const` is a special case of this.
-/
protected theorem ContMDiffAt.mfderiv {x‚ÇÄ : N} (f : N ‚Üí M ‚Üí M') (g : N ‚Üí M)
    (hf : ContMDiffAt (J.prod I) I' n (Function.uncurry f) (x‚ÇÄ, g x‚ÇÄ)) (hg : ContMDiffAt J I m g x‚ÇÄ)
    (hmn : m + 1 ‚â§ n) :
    ContMDiffAt J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (inTangentCoordinates I I' g (fun x ‚Ü¶ f x (g x)) (fun x ‚Ü¶ mfderiv I I' (f x) (g x)) x‚ÇÄ)
      x‚ÇÄ := by
  rw [‚Üê contMDiffWithinAt_univ] at hf hg ‚ä¢
  rw [‚Üê univ_prod_univ] at hf
  simp_rw [‚Üê mfderivWithin_univ]
  exact ContMDiffWithinAt.mfderivWithin hf hg (mem_univ _) (mapsTo_univ _ _) hmn
    uniqueMDiffOn_univ

/-- The derivative `D_yf(y)` is `C^m` at `x‚ÇÄ`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^n` at `x‚ÇÄ` for some `n ‚â• m + 1`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
This is a special case of `ContMDiffAt.mfderiv` where `f` does not contain any parameters and
`g = id`.
-/
theorem ContMDiffAt.mfderiv_const {x‚ÇÄ : M} {f : M ‚Üí M'} (hf : ContMDiffAt I I' n f x‚ÇÄ)
    (hmn : m + 1 ‚â§ n) :
    ContMDiffAt I ùìò(ùïú, E ‚ÜíL[ùïú] E') m (inTangentCoordinates I I' id f (mfderiv I I' f) x‚ÇÄ) x‚ÇÄ :=
  haveI : ContMDiffAt (I.prod I) I' n (fun x : M √ó M => f x.2) (x‚ÇÄ, x‚ÇÄ) :=
    ContMDiffAt.comp (x‚ÇÄ, x‚ÇÄ) hf contMDiffAt_snd
  this.mfderiv (fun _ => f) id contMDiffAt_id hmn

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `g‚ÇÇ(x)` is
`C^n` at `x‚ÇÄ`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(x‚ÇÄ, g(x‚ÇÄ))` and `g` is `C^n` at `x‚ÇÄ`.
We have to insert a coordinate change from `x‚ÇÄ` to `g‚ÇÅ(x)` to make the derivative sensible.

This is similar to `ContMDiffAt.mfderiv`, but where the continuous linear map is applied to a
(variable) vector.
-/
theorem ContMDiffAt.mfderiv_apply {x‚ÇÄ : N'} (f : N ‚Üí M ‚Üí M') (g : N ‚Üí M) (g‚ÇÅ : N' ‚Üí N) (g‚ÇÇ : N' ‚Üí E)
    (hf : ContMDiffAt (J.prod I) I' n (Function.uncurry f) (g‚ÇÅ x‚ÇÄ, g (g‚ÇÅ x‚ÇÄ)))
    (hg : ContMDiffAt J I m g (g‚ÇÅ x‚ÇÄ)) (hg‚ÇÅ : ContMDiffAt J' J m g‚ÇÅ x‚ÇÄ)
    (hg‚ÇÇ : ContMDiffAt J' ùìò(ùïú, E) m g‚ÇÇ x‚ÇÄ) (hmn : m + 1 ‚â§ n) :
    ContMDiffAt J' ùìò(ùïú, E') m
      (fun x => inTangentCoordinates I I' g (fun x => f x (g x))
        (fun x => mfderiv I I' (f x) (g x)) (g‚ÇÅ x‚ÇÄ) (g‚ÇÅ x) (g‚ÇÇ x)) x‚ÇÄ :=
  ((hf.mfderiv f g hg hmn).comp_of_eq hg‚ÇÅ rfl).clm_apply hg‚ÇÇ

end mfderiv

/-! ### The tangent map of a `C^(n+1)` function is `C^n` -/

section tangentMap

variable [Is : IsManifold I 1 M] [I's : IsManifold I' 1 M']

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative
is `C^m` when `m+1 ‚â§ n`. -/
theorem ContMDiffOn.contMDiffOn_tangentMapWithin
    (hf : ContMDiffOn I I' n f s) (hmn : m + 1 ‚â§ n)
    (hs : UniqueMDiffOn I s) :
    ContMDiffOn I.tangent I'.tangent m (tangentMapWithin I I' f s)
      (œÄ E (TangentSpace I) ‚Åª¬π' s) := by
  intro x‚ÇÄ hx‚ÇÄ
  let s' : Set (TangentBundle I M) := (œÄ E (TangentSpace I) ‚Åª¬π' s)
  let b‚ÇÅ : TangentBundle I M ‚Üí M := fun p ‚Ü¶ p.1
  let v : Œ† (y : TangentBundle I M), TangentSpace I (b‚ÇÅ y) := fun y ‚Ü¶ y.2
  have hv : ContMDiffWithinAt I.tangent I.tangent m (fun y ‚Ü¶ (v y : TangentBundle I M)) s' x‚ÇÄ :=
    contMDiffWithinAt_id
  let b‚ÇÇ : TangentBundle I M ‚Üí M' := f ‚àò b‚ÇÅ
  have hb‚ÇÇ : ContMDiffWithinAt I.tangent I' m b‚ÇÇ s' x‚ÇÄ :=
    ((hf (b‚ÇÅ x‚ÇÄ) hx‚ÇÄ).of_le (le_self_add.trans hmn)).comp _
      (contMDiffWithinAt_proj (TangentSpace I)) (fun x h ‚Ü¶ h)
  let œï : Œ† (y : TangentBundle I M), TangentSpace I (b‚ÇÅ y) ‚ÜíL[ùïú] TangentSpace I' (b‚ÇÇ y) :=
    fun y ‚Ü¶ mfderivWithin I I' f s (b‚ÇÅ y)
  have hœï : ContMDiffWithinAt I.tangent ùìò(ùïú, E ‚ÜíL[ùïú] E') m
      (fun y ‚Ü¶ ContinuousLinearMap.inCoordinates E (TangentSpace I (M := M)) E'
        (TangentSpace I' (M := M')) (b‚ÇÅ x‚ÇÄ) (b‚ÇÅ y) (b‚ÇÇ x‚ÇÄ) (b‚ÇÇ y) (œï y))
      s' x‚ÇÄ := by
    have A : ContMDiffWithinAt I ùìò(ùïú, E ‚ÜíL[ùïú] E') m
        (fun y ‚Ü¶ ContinuousLinearMap.inCoordinates E (TangentSpace I (M := M)) E'
          (TangentSpace I' (M := M')) (b‚ÇÅ x‚ÇÄ) y (b‚ÇÇ x‚ÇÄ) (f y) (mfderivWithin I I' f s y))
        s (b‚ÇÅ x‚ÇÄ) :=
      ContMDiffWithinAt.mfderivWithin_const (hf _ hx‚ÇÄ) hmn hx‚ÇÄ hs
    exact A.comp _ (contMDiffWithinAt_proj (TangentSpace I)) (fun x h ‚Ü¶ h)
  exact ContMDiffWithinAt.clm_apply_of_inCoordinates hœï hv hb‚ÇÇ

/-- If a function is `C^n` on a domain with unique derivatives, with `1 ‚â§ n`, then its bundled
derivative is continuous there. -/
theorem ContMDiffOn.continuousOn_tangentMapWithin (hf : ContMDiffOn I I' n f s) (hmn : 1 ‚â§ n)
    (hs : UniqueMDiffOn I s) :
    ContinuousOn (tangentMapWithin I I' f s) (œÄ E (TangentSpace I) ‚Åª¬π' s) := by
  have :
    ContMDiffOn I.tangent I'.tangent 0 (tangentMapWithin I I' f s) (œÄ E (TangentSpace I) ‚Åª¬π' s) :=
    hf.contMDiffOn_tangentMapWithin hmn hs
  exact this.continuousOn

/-- If a function is `C^n`, then its bundled derivative is `C^m` when `m+1 ‚â§ n`. -/
theorem ContMDiff.contMDiff_tangentMap (hf : ContMDiff I I' n f) (hmn : m + 1 ‚â§ n) :
    ContMDiff I.tangent I'.tangent m (tangentMap I I' f) := by
  rw [‚Üê contMDiffOn_univ] at hf ‚ä¢
  convert hf.contMDiffOn_tangentMapWithin hmn uniqueMDiffOn_univ
  rw [tangentMapWithin_univ]

/-- If a function is `C^n`, with `1 ‚â§ n`, then its bundled derivative is continuous. -/
theorem ContMDiff.continuous_tangentMap (hf : ContMDiff I I' n f) (hmn : 1 ‚â§ n) :
    Continuous (tangentMap I I' f) := by
  rw [‚Üê contMDiffOn_univ] at hf
  rw [‚Üê continuousOn_univ]
  convert hf.continuousOn_tangentMapWithin hmn uniqueMDiffOn_univ
  rw [tangentMapWithin_univ]

end tangentMap

namespace TangentBundle

open Bundle

/-- The derivative of the zero section of the tangent bundle maps `‚ü®x, v‚ü©` to `‚ü®‚ü®x, 0‚ü©, ‚ü®v, 0‚ü©‚ü©`.

Note that, as currently framed, this is a statement in coordinates, thus reliant on the choice
of the coordinate system we use on the tangent bundle.

However, the result itself is coordinate-dependent only to the extent that the coordinates
determine a splitting of the tangent bundle.  Moreover, there is a canonical splitting at each
point of the zero section (since there is a canonical horizontal space there, the tangent space
to the zero section, in addition to the canonical vertical space which is the kernel of the
derivative of the projection), and this canonical splitting is also the one that comes from the
coordinates on the tangent bundle in our definitions. So this statement is not as crazy as it
may seem.

TODO define splittings of vector bundles; state this result invariantly. -/
theorem tangentMap_tangentBundle_pure [Is : IsManifold I 1 M]
    (p : TangentBundle I M) :
    tangentMap I I.tangent (zeroSection E (TangentSpace I)) p = ‚ü®‚ü®p.proj, 0‚ü©, ‚ü®p.2, 0‚ü©‚ü© := by
  rcases p with ‚ü®x, v‚ü©
  have N : I.symm ‚Åª¬π' (chartAt H x).target ‚àà ùìù (I ((chartAt H x) x)) := by
    apply IsOpen.mem_nhds
    ¬∑ apply (PartialHomeomorph.open_target _).preimage I.continuous_invFun
    ¬∑ simp only [mfld_simps]
  have A : MDifferentiableAt I I.tangent (fun x => @TotalSpace.mk M E (TangentSpace I) x 0) x :=
    haveI : ContMDiff I (I.prod ùìò(ùïú, E)) ‚ä§ (zeroSection E (TangentSpace I : M ‚Üí Type _)) :=
      Bundle.contMDiff_zeroSection ùïú (TangentSpace I : M ‚Üí Type _)
    this.mdifferentiableAt le_top
  have B : fderivWithin ùïú (fun x' : E ‚Ü¶ (x', (0 : E))) (Set.range I) (I ((chartAt H x) x)) v
      = (v, 0) := by
    rw [fderivWithin_eq_fderiv, DifferentiableAt.fderiv_prodMk]
    ¬∑ simp
    ¬∑ exact differentiableAt_fun_id
    ¬∑ exact differentiableAt_const _
    ¬∑ exact ModelWithCorners.uniqueDiffWithinAt_image I
    ¬∑ exact differentiableAt_id.prodMk (differentiableAt_const _)
  simp +unfoldPartialApp only [Bundle.zeroSection, tangentMap, mfderiv, A,
    if_pos, chartAt, FiberBundle.chartedSpace_chartAt, TangentBundle.trivializationAt_apply,
    Function.comp_def, ContinuousLinearMap.map_zero, mfld_simps]
  rw [‚Üê fderivWithin_inter N] at B
  rw [‚Üê fderivWithin_inter N, ‚Üê B]
  congr 1
  refine fderivWithin_congr (fun y hy => ?_) ?_
  ¬∑ simp only [mfld_simps] at hy
    simp only [hy, mfld_simps]
  ¬∑ simp only [mfld_simps]

end TangentBundle

namespace ContMDiffMap

-- These helpers for dot notation have been moved here from
-- `Mathlib/Geometry/Manifold/ContMDiffMap.lean` to avoid needing to import this file there.
-- (However as a consequence we import `Mathlib/Geometry/Manifold/ContMDiffMap.lean` here now.)
-- They could be moved to another file (perhaps a new file) if desired.
open scoped Manifold ContDiff

protected theorem mdifferentiable' (f : C^n‚üÆI, M; I', M'‚üØ) (hn : 1 ‚â§ n) : MDifferentiable I I' f :=
  f.contMDiff.mdifferentiable hn

protected theorem mdifferentiable (f : C^‚àû‚üÆI, M; I', M'‚üØ) : MDifferentiable I I' f :=
  f.contMDiff.mdifferentiable (mod_cast le_top)

protected theorem mdifferentiableAt (f : C^‚àû‚üÆI, M; I', M'‚üØ) {x} : MDifferentiableAt I I' f x :=
  f.mdifferentiable x

end ContMDiffMap

section EquivTangentBundleProd

variable (I I' M M') in
/-- The tangent bundle of a product is canonically isomorphic to the product of the tangent
bundles. -/
@[simps] def equivTangentBundleProd :
    TangentBundle (I.prod I') (M √ó M') ‚âÉ (TangentBundle I M) √ó (TangentBundle I' M') where
  toFun p := (‚ü®p.1.1, p.2.1‚ü©, ‚ü®p.1.2, p.2.2‚ü©)
  invFun p := ‚ü®(p.1.1, p.2.1), (p.1.2, p.2.2)‚ü©

lemma equivTangentBundleProd_eq_tangentMap_prod_tangentMap :
    equivTangentBundleProd I M I' M' = fun (p : TangentBundle (I.prod I') (M √ó M')) ‚Ü¶
      (tangentMap (I.prod I') I Prod.fst p, tangentMap (I.prod I') I' Prod.snd p) := by
  simp only [tangentMap_prodFst, tangentMap_prodSnd]; rfl

variable [IsManifold I 1 M] [IsManifold I' 1 M']

/-- The canonical equivalence between the tangent bundle of a product and the product of
tangent bundles is smooth. -/
lemma contMDiff_equivTangentBundleProd :
    ContMDiff (I.prod I').tangent (I.tangent.prod I'.tangent) n
      (equivTangentBundleProd I M I' M') := by
  rw [equivTangentBundleProd_eq_tangentMap_prod_tangentMap]
  exact (contMDiff_fst.contMDiff_tangentMap le_rfl).prodMk
    (contMDiff_snd.contMDiff_tangentMap le_rfl)

/-- The canonical equivalence between the product of tangent bundles and the tangent bundle of a
product is smooth. -/
lemma contMDiff_equivTangentBundleProd_symm :
    ContMDiff (I.tangent.prod I'.tangent) (I.prod I').tangent n
      (equivTangentBundleProd I M I' M').symm := by
  /- Contrary to what one might expect, this proof is nontrivial. It is not a formalization issue:
  even on paper, I don't have a simple proof of the statement. The reason is that there is no nice
  functorial expression for the map from `TM √ó T'M` to `T (M √ó M')`, so I need to come back to
  the definition and break things into pieces.
  The argument goes as follows. Since we're looking at a map into a vector bundle whose basis map
  is smooth, it suffices to check the smoothness of the second component, in a chart. It lands in
  a product vector space `E √ó E'`, so it suffices to check that the composition with each projection
  to `E` and `E'` is smooth.
  We notice that the composition of this map with the first projection coincides with the projection
  `TM √ó TM' ‚Üí TM` read in the target chart, which is smooth, so we're done.
  The issue is with checking differentiability everywhere (to justify that the derivative of a
  product is the product of the derivatives), and writing down things.
  -/
  rintro ‚ü®a, b‚ü©
  have U w w' : UniqueDiffWithinAt ùïú (Set.range (Prod.map I I')) (I w, I' w') := by
    simp only [range_prodMap]
    apply UniqueDiffWithinAt.prod
    ¬∑ exact ModelWithCorners.uniqueDiffWithinAt_image I
    ¬∑ exact ModelWithCorners.uniqueDiffWithinAt_image I'
  rw [contMDiffAt_totalSpace]
  simp only [equivTangentBundleProd, TangentBundle.trivializationAt_apply, mfld_simps,
    Equiv.coe_fn_symm_mk]
  refine ‚ü®?_, (contMDiffAt_prod_module_iff _).2 ‚ü®?_, ?_‚ü©‚ü©
  ¬∑ exact ContMDiffAt.prodMap (contMDiffAt_proj (TangentSpace I))
      (contMDiffAt_proj (TangentSpace I'))
  ¬∑ /- check that the composition with the first projection in the target chart is smooth.
    For this, we check that it coincides locally with the projection `pM : TM √ó TM' ‚Üí TM` read in
    the target chart, which is obviously smooth. -/
    have smooth_pM : ContMDiffAt (I.tangent.prod I'.tangent) I.tangent n Prod.fst (a, b) :=
      contMDiffAt_fst
    apply (contMDiffAt_totalSpace.1 smooth_pM).2.congr_of_eventuallyEq
    filter_upwards [chart_source_mem_nhds (ModelProd (ModelProd H E) (ModelProd H' E')) (a, b)]
      with p hp
    -- now we have to check that the original map coincides locally with `pM` read in target chart.
    simp only [prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod_source, Set.mem_prod, TangentBundle.mem_chart_source_iff] at hp
    let œÜ (x : E) := I ((chartAt H a.proj) ((chartAt H p.1.proj).symm (I.symm x)))
    have D0 : DifferentiableWithinAt ùïú œÜ (Set.range I) (I ((chartAt H p.1.proj) p.1.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt (n := 1) _ le_rfl
      apply contDiffWithinAt_ext_coord_change
      simp [hp.1]
    have D (w : TangentBundle I' M') :
        DifferentiableWithinAt ùïú (œÜ ‚àò (Prod.fst : E √ó E' ‚Üí E)) (Set.range (Prod.map ‚ÜëI ‚ÜëI'))
        (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' w.proj) w.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0)
        differentiableWithinAt_fst (by simp [mapsTo_fst_prod])
    simp only [comp_def, comp_apply]
    rw [DifferentiableWithinAt.fderivWithin_prodMk (by exact D _) ?_ (U _ _)]; swap
    ¬∑ let œÜ' (x : E') := I' ((chartAt H' b.proj) ((chartAt H' p.2.proj).symm (I'.symm x)))
      have D0' : DifferentiableWithinAt ùïú œÜ' (Set.range I')
          (I' ((chartAt H' p.2.proj) p.2.proj)) := by
        apply ContDiffWithinAt.differentiableWithinAt (n := 1) _ le_rfl
        apply contDiffWithinAt_ext_coord_change
        simp [hp.2]
      have D' : DifferentiableWithinAt ùïú (œÜ' ‚àò Prod.snd) (Set.range (Prod.map I I'))
          (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
        DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0')
          differentiableWithinAt_snd (by simp [mapsTo_snd_prod])
      exact D'
    simp only [TangentBundle.trivializationAt_apply, mfld_simps]
    change fderivWithin ùïú (œÜ ‚àò Prod.fst) _ _ _ = fderivWithin ùïú œÜ _ _ _
    rw [range_prodMap] at U
    rw [fderivWithin_comp _ (by exact D0) differentiableWithinAt_fst mapsTo_fst_prod (U _ _)]
    simp [fderivWithin_fst, U]
  ¬∑ /- check that the composition with the second projection in the target chart is smooth.
    For this, we check that it coincides locally with the projection `pM' : TM √ó TM' ‚Üí TM'` read in
    the target chart, which is obviously smooth. -/
    have smooth_pM' : ContMDiffAt (I.tangent.prod I'.tangent) I'.tangent n Prod.snd (a, b) :=
      contMDiffAt_snd
    apply (contMDiffAt_totalSpace.1 smooth_pM').2.congr_of_eventuallyEq
    filter_upwards [chart_source_mem_nhds (ModelProd (ModelProd H E) (ModelProd H' E')) (a, b)]
      with p hp
    -- now we have to check that the original map coincides locally with `pM'` read in target chart.
    simp only [prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod_source, Set.mem_prod, TangentBundle.mem_chart_source_iff] at hp
    let œÜ (x : E') := I' ((chartAt H' b.proj) ((chartAt H' p.2.proj).symm (I'.symm x)))
    have D0 : DifferentiableWithinAt ùïú œÜ (Set.range I') (I' ((chartAt H' p.2.proj) p.2.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt _ le_rfl
      apply contDiffWithinAt_ext_coord_change
      simp [hp.2]
    have D (w : TangentBundle I M) :
        DifferentiableWithinAt ùïú (œÜ ‚àò (Prod.snd : E √ó E' ‚Üí E')) (Set.range (Prod.map ‚ÜëI ‚ÜëI'))
        (I ((chartAt H w.proj) w.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0)
        differentiableWithinAt_snd (by simp [mapsTo_snd_prod])
    simp only [comp_def, comp_apply]
    rw [DifferentiableWithinAt.fderivWithin_prodMk ?_ (by exact D _) (U _ _)]; swap
    ¬∑ let œÜ' (x : E) := I ((chartAt H a.proj) ((chartAt H p.1.proj).symm (I.symm x)))
      have D0' : DifferentiableWithinAt ùïú œÜ' (Set.range I)
          (I ((chartAt H p.1.proj) p.1.proj)) := by
        apply ContDiffWithinAt.differentiableWithinAt _ le_rfl
        apply contDiffWithinAt_ext_coord_change
        simp [hp.1]
      have D' : DifferentiableWithinAt ùïú (œÜ' ‚àò Prod.fst) (Set.range (Prod.map I I'))
          (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
        DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0')
          differentiableWithinAt_fst (by simp [mapsTo_fst_prod])
      exact D'
    simp only [TangentBundle.trivializationAt_apply, mfld_simps]
    change fderivWithin ùïú (œÜ ‚àò Prod.snd) _ _ _ = fderivWithin ùïú œÜ _ _ _
    rw [range_prodMap] at U
    rw [fderivWithin_comp _ (by exact D0) differentiableWithinAt_snd mapsTo_snd_prod (U _ _)]
    simp [fderivWithin_snd, U]

end EquivTangentBundleProd
