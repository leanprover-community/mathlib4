/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Geometry.Manifold.MFDeriv.Tangent
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.VectorBundle.Hom

/-!
### Interactions between differentiability, smoothness and manifold derivatives

We give the relation between `MDifferentiable`, `ContMDiff`, `mfderiv`, `tangentMap`
and related notions.

## Main statements

* `ContMDiffOn.contMDiffOn_tangentMapWithin` states that the bundled derivative
  of a `Câ¿` function in a domain is `Cáµ` when `m + 1 â‰¤ n`.
* `ContMDiff.contMDiff_tangentMap` states that the bundled derivative
  of a `Câ¿` function is `Cáµ` when `m + 1 â‰¤ n`.
-/

open Set Function Filter ChartedSpace IsManifold Bundle

open scoped Topology Manifold Bundle

/-! ### Definition of `C^n` functions between manifolds -/


variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {m n : WithTop â„•âˆ}
  -- declare a charted space `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  {I : ModelWithCorners ğ•œ E H} {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  -- declare a charted space `M'` over the pair `(E', H')`.
  {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type*} [TopologicalSpace H']
  {I' : ModelWithCorners ğ•œ E' H'} {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  -- declare a `C^n` manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ğ•œ F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [Js : IsManifold J 1 N]
  -- declare a charted space `N'` over the pair `(F', G')`.
  {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace ğ•œ F'] {G' : Type*} [TopologicalSpace G']
  {J' : ModelWithCorners ğ•œ F' G'} {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N']
  -- declare functions, sets
  {f : M â†’ M'} {s : Set M}

-- Porting note: section about deducing differentiability for `C^n` functions moved to
-- `Geometry.Manifold.MFDeriv.Basic`

/-! ### The derivative of a `C^(n+1)` function is `C^n` -/

section mfderiv
variable [Is : IsManifold I 1 M] [I's : IsManifold I' 1 M']

/-- The function that sends `x` to the `y`-derivative of `f (x, y)` at `g (x)` is `C^m` at `xâ‚€`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^n` at `(xâ‚€, g(xâ‚€))` for `n â‰¥ m + 1` and `g` is `C^m` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
Version within a set.
-/
protected theorem ContMDiffWithinAt.mfderivWithin {xâ‚€ : N} {f : N â†’ M â†’ M'} {g : N â†’ M}
    {t : Set N} {u : Set M}
    (hf : ContMDiffWithinAt (J.prod I) I' n (Function.uncurry f) (t Ã—Ë¢ u) (xâ‚€, g xâ‚€))
    (hg : ContMDiffWithinAt J I m g t xâ‚€) (hxâ‚€ : xâ‚€ âˆˆ t)
    (hu : MapsTo g t u) (hmn : m + 1 â‰¤ n) (h'u : UniqueMDiffOn I u) :
    ContMDiffWithinAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (inTangentCoordinates I I' g (fun x â†¦ f x (g x))
        (fun x â†¦ mfderivWithin I I' (f x) u (g x)) xâ‚€) t xâ‚€ := by
  -- first localize the result to a smaller set, to make sure everything happens in chart domains
  let t' := t âˆ© g â»Â¹' ((extChartAt I (g xâ‚€)).source)
  have ht't : t' âŠ† t := inter_subset_left
  suffices ContMDiffWithinAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (inTangentCoordinates I I' g (fun x â†¦ f x (g x))
        (fun x â†¦ mfderivWithin I I' (f x) u (g x)) xâ‚€) t' xâ‚€ by
    apply ContMDiffWithinAt.mono_of_mem_nhdsWithin this
    apply inter_mem self_mem_nhdsWithin
    exact hg.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (g xâ‚€))
  -- register a few basic facts that maps send suitable neighborhoods to suitable neighborhoods,
  -- by continuity
  have hxâ‚€gxâ‚€ : (xâ‚€, g xâ‚€) âˆˆ t Ã—Ë¢ u := by simp [hxâ‚€, hu hxâ‚€]
  have h4f : ContinuousWithinAt (fun x â†¦ f x (g x)) t xâ‚€ := by
    change ContinuousWithinAt ((Function.uncurry f) âˆ˜ (fun x â†¦ (x, g x))) t xâ‚€
    refine ContinuousWithinAt.comp hf.continuousWithinAt ?_ (fun y hy â†¦ by simp [hy, hu hy])
    exact (continuousWithinAt_id.prodMk hg.continuousWithinAt)
  have h4f := h4f.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (I := I') (f xâ‚€ (g xâ‚€)))
  have h3f := (contMDiffWithinAt_iff_contMDiffWithinAt_nhdsWithin (by simp)).mp
    (hf.of_le <| (self_le_add_left 1 m).trans hmn)
  simp only [hxâ‚€gxâ‚€, insert_eq_of_mem] at h3f
  have h2f : âˆ€á¶  xâ‚‚ in ğ“[t] xâ‚€, ContMDiffWithinAt I I' 1 (f xâ‚‚) u (g xâ‚‚) := by
    have : MapsTo (fun x â†¦ (x, g x)) t (t Ã—Ë¢ u) := fun y hy â†¦ by simp [hy, hu hy]
    filter_upwards [((continuousWithinAt_id.prodMk hg.continuousWithinAt)
      |>.tendsto_nhdsWithin this).eventually h3f, self_mem_nhdsWithin] with x hx h'x
    apply hx.comp (g x) (contMDiffWithinAt_const.prodMk contMDiffWithinAt_id)
    exact fun y hy â†¦ by simp [h'x, hy]
  have h2g : g â»Â¹' (extChartAt I (g xâ‚€)).source âˆˆ ğ“[t] xâ‚€ :=
    hg.continuousWithinAt.preimage_mem_nhdsWithin (extChartAt_source_mem_nhds (g xâ‚€))
  -- key point: the derivative of `f` composed with extended charts, at the point `g x` read in the
  -- chart, is `C^n` in the vector space sense. This follows from `ContDiffWithinAt.fderivWithin`,
  -- which is the vector space analogue of the result we are proving.
  have : ContDiffWithinAt ğ•œ m (fun x â†¦ fderivWithin ğ•œ
        (extChartAt I' (f xâ‚€ (g xâ‚€)) âˆ˜ f ((extChartAt J xâ‚€).symm x) âˆ˜ (extChartAt I (g xâ‚€)).symm)
        ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
        (extChartAt I (g xâ‚€) (g ((extChartAt J xâ‚€).symm x))))
      ((extChartAt J xâ‚€).symm â»Â¹' t' âˆ© range J) (extChartAt J xâ‚€ xâ‚€) := by
    have hf' := hf.mono (prod_mono_left ht't)
    have hg' := hg.mono (show t' âŠ† t from inter_subset_left)
    rw [contMDiffWithinAt_iff] at hf' hg'
    simp_rw [Function.comp_def, uncurry, extChartAt_prod, PartialEquiv.prod_coe_symm,
      ModelWithCorners.range_prod] at hf' âŠ¢
    apply ContDiffWithinAt.fderivWithin _ _ _ (show (m : WithTop â„•âˆ) + 1 â‰¤ n from mod_cast hmn )
    Â· simp [hxâ‚€, t']
    Â· apply inter_subset_left.trans
      rw [preimage_subset_iff]
      intro a ha
      refine âŸ¨PartialEquiv.map_source _ (inter_subset_right ha :), ?_âŸ©
      rw [mem_preimage, PartialEquiv.left_inv (extChartAt I (g xâ‚€))]
      Â· exact hu (inter_subset_left ha)
      Â· exact (inter_subset_right ha :)
    Â· have : ((fun p â†¦ ((extChartAt J xâ‚€).symm p.1, (extChartAt I (g xâ‚€)).symm p.2)) â»Â¹' t' Ã—Ë¢ u
            âˆ© range J Ã—Ë¢ (extChartAt I (g xâ‚€)).target)
          âŠ† ((fun p â†¦ ((extChartAt J xâ‚€).symm p.1, (extChartAt I (g xâ‚€)).symm p.2)) â»Â¹' t' Ã—Ë¢ u
            âˆ© range J Ã—Ë¢ range I) := by
        apply inter_subset_inter_right
        exact Set.prod_mono_right (extChartAt_target_subset_range (g xâ‚€))
      convert hf'.2.mono this
      Â· ext y; simp; tauto
      Â· simp
    Â· exact hg'.2
    Â· exact UniqueMDiffOn.uniqueDiffOn_target_inter h'u (g xâ‚€)
  -- reformulate the previous point as `C^n` in the manifold sense (but still for a map between
  -- vector spaces)
  have :
    ContMDiffWithinAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (fun x â†¦
        fderivWithin ğ•œ (extChartAt I' (f xâ‚€ (g xâ‚€)) âˆ˜ f x âˆ˜ (extChartAt I (g xâ‚€)).symm)
        ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
          (extChartAt I (g xâ‚€) (g x))) t' xâ‚€ := by
    simp_rw [contMDiffWithinAt_iff_source (x := xâ‚€),
      contMDiffWithinAt_iff_contDiffWithinAt, Function.comp_def]
    exact this
  -- finally, argue that the map we control in the previous point coincides locally with the map we
  -- want to prove the regularity of, so regularity of the latter follows from regularity of the
  -- former.
  apply this.congr_of_eventuallyEq_of_mem _ (by simp [t', hxâ‚€])
  apply nhdsWithin_mono _ ht't
  filter_upwards [h2f, h4f, h2g, self_mem_nhdsWithin] with x hx h'x h2 hxt
  have h1 : g x âˆˆ u := hu hxt
  have h3 : UniqueMDiffWithinAt ğ“˜(ğ•œ, E)
      ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
      ((extChartAt I (g xâ‚€)) (g x)) := by
    apply UniqueDiffWithinAt.uniqueMDiffWithinAt
    apply UniqueMDiffOn.uniqueDiffOn_target_inter h'u
    refine âŸ¨PartialEquiv.map_source _ h2, ?_âŸ©
    rwa [mem_preimage, PartialEquiv.left_inv _ h2]
  have A : mfderivWithin ğ“˜(ğ•œ, E) I ((extChartAt I (g xâ‚€)).symm)
        (range I) ((extChartAt I (g xâ‚€)) (g x))
      = mfderivWithin ğ“˜(ğ•œ, E) I ((extChartAt I (g xâ‚€)).symm)
        ((extChartAt I (g xâ‚€)).target âˆ© (extChartAt I (g xâ‚€)).symm â»Â¹' u)
        ((extChartAt I (g xâ‚€)) (g x)) := by
    apply (MDifferentiableWithinAt.mfderivWithin_mono _ h3 _).symm
    Â· apply mdifferentiableWithinAt_extChartAt_symm
      exact PartialEquiv.map_source (extChartAt I (g xâ‚€)) h2
    Â· exact inter_subset_left.trans (extChartAt_target_subset_range (g xâ‚€))
  rw [inTangentCoordinates_eq_mfderiv_comp, A,
    â† mfderivWithin_comp_of_eq, â† mfderiv_comp_mfderivWithin_of_eq]
  Â· exact mfderivWithin_eq_fderivWithin
  Â· exact mdifferentiableAt_extChartAt (by simpa using h'x)
  Â· apply MDifferentiableWithinAt.comp (I' := I) (u := u) _ _ _ inter_subset_right
    Â· convert hx.mdifferentiableWithinAt le_rfl
      exact PartialEquiv.left_inv (extChartAt I (g xâ‚€)) h2
    Â· apply (mdifferentiableWithinAt_extChartAt_symm _).mono
      Â· exact inter_subset_left.trans (extChartAt_target_subset_range (g xâ‚€))
      Â· exact PartialEquiv.map_source (extChartAt I (g xâ‚€)) h2
  Â· exact h3
  Â· simp only [Function.comp_def, PartialEquiv.left_inv (extChartAt I (g xâ‚€)) h2]
  Â· exact hx.mdifferentiableWithinAt le_rfl
  Â· apply (mdifferentiableWithinAt_extChartAt_symm _).mono
    Â· exact inter_subset_left.trans (extChartAt_target_subset_range (g xâ‚€))
    Â· exact PartialEquiv.map_source (extChartAt I (g xâ‚€)) h2
  Â· exact inter_subset_right
  Â· exact h3
  Â· exact PartialEquiv.left_inv (extChartAt I (g xâ‚€)) h2
  Â· simpa using h2
  Â· simpa using h'x

/-- The derivative `D_yf(y)` is `C^m` at `xâ‚€`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^n` at `xâ‚€` for some `n â‰¥ m + 1`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This is a special case of `ContMDiffWithinAt.mfderivWithin` where `f` does not contain any
parameters and `g = id`.
-/
theorem ContMDiffWithinAt.mfderivWithin_const {xâ‚€ : M} {f : M â†’ M'}
    (hf : ContMDiffWithinAt I I' n f s xâ‚€)
    (hmn : m + 1 â‰¤ n) (hx : xâ‚€ âˆˆ s) (hs : UniqueMDiffOn I s) :
    ContMDiffWithinAt I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (inTangentCoordinates I I' id f (mfderivWithin I I' f s) xâ‚€) s xâ‚€ := by
  have : ContMDiffWithinAt (I.prod I) I' n (fun x : M Ã— M â†¦ f x.2) (s Ã—Ë¢ s) (xâ‚€, xâ‚€) :=
    ContMDiffWithinAt.comp (xâ‚€, xâ‚€) hf contMDiffWithinAt_snd mapsTo_snd_prod
  exact this.mfderivWithin contMDiffWithinAt_id hx (mapsTo_id _) hmn hs

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `gâ‚‚(x)` is
`C^n` at `xâ‚€`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(xâ‚€, g(xâ‚€))` and `g` is `C^n` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `gâ‚(x)` to make the derivative sensible.

This is similar to `ContMDiffWithinAt.mfderivWithin`, but where the continuous linear map is
applied to a (variable) vector.
-/
theorem ContMDiffWithinAt.mfderivWithin_apply {xâ‚€ : N'}
    {f : N â†’ M â†’ M'} {g : N â†’ M} {gâ‚ : N' â†’ N} {gâ‚‚ : N' â†’ E} {t : Set N} {u : Set M} {v : Set N'}
    (hf : ContMDiffWithinAt (J.prod I) I' n (Function.uncurry f) (t Ã—Ë¢ u) (gâ‚ xâ‚€, g (gâ‚ xâ‚€)))
    (hg : ContMDiffWithinAt J I m g t (gâ‚ xâ‚€)) (hgâ‚ : ContMDiffWithinAt J' J m gâ‚ v xâ‚€)
    (hgâ‚‚ : ContMDiffWithinAt J' ğ“˜(ğ•œ, E) m gâ‚‚ v xâ‚€) (hmn : m + 1 â‰¤ n) (h'gâ‚ : MapsTo gâ‚ v t)
    (hgâ‚xâ‚€ : gâ‚ xâ‚€ âˆˆ t) (h'g : MapsTo g t u) (hu : UniqueMDiffOn I u) :
    ContMDiffWithinAt J' ğ“˜(ğ•œ, E') m
      (fun x â†¦ (inTangentCoordinates I I' g (fun x â†¦ f x (g x))
        (fun x â†¦ mfderivWithin I I' (f x) u (g x)) (gâ‚ xâ‚€) (gâ‚ x)) (gâ‚‚ x)) v xâ‚€ :=
  ((hf.mfderivWithin hg hgâ‚xâ‚€ h'g hmn hu).comp_of_eq hgâ‚ h'gâ‚ rfl).clm_apply hgâ‚‚

/-- The function that sends `x` to the `y`-derivative of `f (x, y)` at `g (x)` is `C^m` at `xâ‚€`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^n` at `(xâ‚€, g(xâ‚€))` for `n â‰¥ m + 1` and `g` is `C^m` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This result is used to show that maps into the 1-jet bundle and cotangent bundle are `C^n`.
`ContMDiffAt.mfderiv_const` is a special case of this.
-/
protected theorem ContMDiffAt.mfderiv {xâ‚€ : N} (f : N â†’ M â†’ M') (g : N â†’ M)
    (hf : ContMDiffAt (J.prod I) I' n (Function.uncurry f) (xâ‚€, g xâ‚€)) (hg : ContMDiffAt J I m g xâ‚€)
    (hmn : m + 1 â‰¤ n) :
    ContMDiffAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (inTangentCoordinates I I' g (fun x â†¦ f x (g x)) (fun x â†¦ mfderiv I I' (f x) (g x)) xâ‚€)
      xâ‚€ := by
  rw [â† contMDiffWithinAt_univ] at hf hg âŠ¢
  rw [â† univ_prod_univ] at hf
  simp_rw [â† mfderivWithin_univ]
  exact ContMDiffWithinAt.mfderivWithin hf hg (mem_univ _) (mapsTo_univ _ _) hmn
    uniqueMDiffOn_univ

/-- The derivative `D_yf(y)` is `C^m` at `xâ‚€`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^n` at `xâ‚€` for some `n â‰¥ m + 1`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This is a special case of `ContMDiffAt.mfderiv` where `f` does not contain any parameters and
`g = id`.
-/
theorem ContMDiffAt.mfderiv_const {xâ‚€ : M} {f : M â†’ M'} (hf : ContMDiffAt I I' n f xâ‚€)
    (hmn : m + 1 â‰¤ n) :
    ContMDiffAt I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m (inTangentCoordinates I I' id f (mfderiv I I' f) xâ‚€) xâ‚€ :=
  haveI : ContMDiffAt (I.prod I) I' n (fun x : M Ã— M â†¦ f x.2) (xâ‚€, xâ‚€) :=
    ContMDiffAt.comp (xâ‚€, xâ‚€) hf contMDiffAt_snd
  this.mfderiv (fun _ â†¦ f) id contMDiffAt_id hmn

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `gâ‚‚(x)` is
`C^n` at `xâ‚€`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(xâ‚€, g(xâ‚€))` and `g` is `C^n` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `gâ‚(x)` to make the derivative sensible.

This is similar to `ContMDiffAt.mfderiv`, but where the continuous linear map is applied to a
(variable) vector.
-/
theorem ContMDiffAt.mfderiv_apply {xâ‚€ : N'} (f : N â†’ M â†’ M') (g : N â†’ M) (gâ‚ : N' â†’ N) (gâ‚‚ : N' â†’ E)
    (hf : ContMDiffAt (J.prod I) I' n (Function.uncurry f) (gâ‚ xâ‚€, g (gâ‚ xâ‚€)))
    (hg : ContMDiffAt J I m g (gâ‚ xâ‚€)) (hgâ‚ : ContMDiffAt J' J m gâ‚ xâ‚€)
    (hgâ‚‚ : ContMDiffAt J' ğ“˜(ğ•œ, E) m gâ‚‚ xâ‚€) (hmn : m + 1 â‰¤ n) :
    ContMDiffAt J' ğ“˜(ğ•œ, E') m
      (fun x â†¦ inTangentCoordinates I I' g (fun x â†¦ f x (g x))
        (fun x â†¦ mfderiv I I' (f x) (g x)) (gâ‚ xâ‚€) (gâ‚ x) (gâ‚‚ x)) xâ‚€ :=
  ((hf.mfderiv f g hg hmn).comp_of_eq hgâ‚ rfl).clm_apply hgâ‚‚

end mfderiv

/-! ### The tangent map of a `C^(n+1)` function is `C^n` -/

section tangentMap

variable [Is : IsManifold I 1 M] [I's : IsManifold I' 1 M']

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative
is `C^m` when `m+1 â‰¤ n`. -/
theorem ContMDiffOn.contMDiffOn_tangentMapWithin
    (hf : ContMDiffOn I I' n f s) (hmn : m + 1 â‰¤ n)
    (hs : UniqueMDiffOn I s) :
    ContMDiffOn I.tangent I'.tangent m (tangentMapWithin I I' f s)
      (Ï€ E (TangentSpace I) â»Â¹' s) := by
  intro xâ‚€ hxâ‚€
  let s' : Set (TangentBundle I M) := (Ï€ E (TangentSpace I) â»Â¹' s)
  let bâ‚ : TangentBundle I M â†’ M := fun p â†¦ p.1
  let v : Î  (y : TangentBundle I M), TangentSpace I (bâ‚ y) := fun y â†¦ y.2
  have hv : ContMDiffWithinAt I.tangent I.tangent m (fun y â†¦ (v y : TangentBundle I M)) s' xâ‚€ :=
    contMDiffWithinAt_id
  let bâ‚‚ : TangentBundle I M â†’ M' := f âˆ˜ bâ‚
  have hbâ‚‚ : ContMDiffWithinAt I.tangent I' m bâ‚‚ s' xâ‚€ :=
    ((hf (bâ‚ xâ‚€) hxâ‚€).of_le (le_self_add.trans hmn)).comp _
      (contMDiffWithinAt_proj (TangentSpace I)) (fun x h â†¦ h)
  let Ï• : Î  (y : TangentBundle I M), TangentSpace I (bâ‚ y) â†’L[ğ•œ] TangentSpace I' (bâ‚‚ y) :=
    fun y â†¦ mfderivWithin I I' f s (bâ‚ y)
  have hÏ• : ContMDiffWithinAt I.tangent ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
      (fun y â†¦ ContinuousLinearMap.inCoordinates E (TangentSpace I (M := M)) E'
        (TangentSpace I' (M := M')) (bâ‚ xâ‚€) (bâ‚ y) (bâ‚‚ xâ‚€) (bâ‚‚ y) (Ï• y))
      s' xâ‚€ := by
    have A : ContMDiffWithinAt I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
        (fun y â†¦ ContinuousLinearMap.inCoordinates E (TangentSpace I (M := M)) E'
          (TangentSpace I' (M := M')) (bâ‚ xâ‚€) y (bâ‚‚ xâ‚€) (f y) (mfderivWithin I I' f s y))
        s (bâ‚ xâ‚€) :=
      ContMDiffWithinAt.mfderivWithin_const (hf _ hxâ‚€) hmn hxâ‚€ hs
    exact A.comp _ (contMDiffWithinAt_proj (TangentSpace I)) (fun x h â†¦ h)
  exact ContMDiffWithinAt.clm_apply_of_inCoordinates hÏ• hv hbâ‚‚

/-- If a function is `C^n` on a domain with unique derivatives, with `1 â‰¤ n`, then its bundled
derivative is continuous there. -/
theorem ContMDiffOn.continuousOn_tangentMapWithin (hf : ContMDiffOn I I' n f s) (hmn : 1 â‰¤ n)
    (hs : UniqueMDiffOn I s) :
    ContinuousOn (tangentMapWithin I I' f s) (Ï€ E (TangentSpace I) â»Â¹' s) := by
  have :
    ContMDiffOn I.tangent I'.tangent 0 (tangentMapWithin I I' f s) (Ï€ E (TangentSpace I) â»Â¹' s) :=
    hf.contMDiffOn_tangentMapWithin hmn hs
  exact this.continuousOn

/-- If a function is `C^n`, then its bundled derivative is `C^m` when `m+1 â‰¤ n`. -/
theorem ContMDiff.contMDiff_tangentMap (hf : ContMDiff I I' n f) (hmn : m + 1 â‰¤ n) :
    ContMDiff I.tangent I'.tangent m (tangentMap I I' f) := by
  rw [â† contMDiffOn_univ] at hf âŠ¢
  convert hf.contMDiffOn_tangentMapWithin hmn uniqueMDiffOn_univ
  rw [tangentMapWithin_univ]

/-- If a function is `C^n`, with `1 â‰¤ n`, then its bundled derivative is continuous. -/
theorem ContMDiff.continuous_tangentMap (hf : ContMDiff I I' n f) (hmn : 1 â‰¤ n) :
    Continuous (tangentMap I I' f) := by
  rw [â† contMDiffOn_univ] at hf
  rw [â† continuousOn_univ]
  convert hf.continuousOn_tangentMapWithin hmn uniqueMDiffOn_univ
  rw [tangentMapWithin_univ]

end tangentMap

namespace TangentBundle

open Bundle

/-- The derivative of the zero section of the tangent bundle maps `âŸ¨x, vâŸ©` to `âŸ¨âŸ¨x, 0âŸ©, âŸ¨v, 0âŸ©âŸ©`.

Note that, as currently framed, this is a statement in coordinates, thus reliant on the choice
of the coordinate system we use on the tangent bundle.

However, the result itself is coordinate-dependent only to the extent that the coordinates
determine a splitting of the tangent bundle.  Moreover, there is a canonical splitting at each
point of the zero section (since there is a canonical horizontal space there, the tangent space
to the zero section, in addition to the canonical vertical space which is the kernel of the
derivative of the projection), and this canonical splitting is also the one that comes from the
coordinates on the tangent bundle in our definitions. So this statement is not as crazy as it
may seem.

TODO define splittings of vector bundles; state this result invariantly. -/
theorem tangentMap_tangentBundle_pure [Is : IsManifold I 1 M]
    (p : TangentBundle I M) :
    tangentMap I I.tangent (zeroSection E (TangentSpace I)) p = âŸ¨âŸ¨p.proj, 0âŸ©, âŸ¨p.2, 0âŸ©âŸ© := by
  rcases p with âŸ¨x, vâŸ©
  have N : I.symm â»Â¹' (chartAt H x).target âˆˆ ğ“ (I ((chartAt H x) x)) := by
    apply IsOpen.mem_nhds
    Â· apply (PartialHomeomorph.open_target _).preimage I.continuous_invFun
    Â· simp only [mfld_simps]
  have A : MDifferentiableAt I I.tangent (fun x â†¦ @TotalSpace.mk M E (TangentSpace I) x 0) x :=
    haveI : ContMDiff I (I.prod ğ“˜(ğ•œ, E)) âŠ¤ (zeroSection E (TangentSpace I : M â†’ Type _)) :=
      Bundle.contMDiff_zeroSection ğ•œ (TangentSpace I : M â†’ Type _)
    this.mdifferentiableAt le_top
  have B : fderivWithin ğ•œ (fun x' : E â†¦ (x', (0 : E))) (Set.range I) (I ((chartAt H x) x)) v
      = (v, 0) := by
    rw [fderivWithin_eq_fderiv, DifferentiableAt.fderiv_prodMk]
    Â· simp
    Â· exact differentiableAt_fun_id
    Â· exact differentiableAt_const _
    Â· exact ModelWithCorners.uniqueDiffWithinAt_image I
    Â· exact differentiableAt_id.prodMk (differentiableAt_const _)
  simp +unfoldPartialApp only [Bundle.zeroSection, tangentMap, mfderiv, A,
    if_pos, chartAt, FiberBundle.chartedSpace_chartAt, TangentBundle.trivializationAt_apply,
    Function.comp_def, ContinuousLinearMap.map_zero, mfld_simps]
  rw [â† fderivWithin_inter N] at B
  rw [â† fderivWithin_inter N, â† B]
  congr 1
  refine fderivWithin_congr (fun y hy â†¦ ?_) ?_
  Â· simp only [mfld_simps] at hy
    simp only [hy, mfld_simps]
  Â· simp only [mfld_simps]

end TangentBundle

namespace ContMDiffMap

-- These helpers for dot notation have been moved here from
-- `Mathlib/Geometry/Manifold/ContMDiffMap.lean` to avoid needing to import this file there.
-- (However as a consequence we import `Mathlib/Geometry/Manifold/ContMDiffMap.lean` here now.)
-- They could be moved to another file (perhaps a new file) if desired.
open scoped Manifold ContDiff

protected theorem mdifferentiable' (f : C^nâŸ®I, M; I', M'âŸ¯) (hn : 1 â‰¤ n) : MDifferentiable I I' f :=
  f.contMDiff.mdifferentiable hn

protected theorem mdifferentiable (f : C^âˆâŸ®I, M; I', M'âŸ¯) : MDifferentiable I I' f :=
  f.contMDiff.mdifferentiable (mod_cast le_top)

protected theorem mdifferentiableAt (f : C^âˆâŸ®I, M; I', M'âŸ¯) {x} : MDifferentiableAt I I' f x :=
  f.mdifferentiable x

end ContMDiffMap

section EquivTangentBundleProd

variable (I I' M M') in
/-- The tangent bundle of a product is canonically isomorphic to the product of the tangent
bundles. -/
@[simps] def equivTangentBundleProd :
    TangentBundle (I.prod I') (M Ã— M') â‰ƒ (TangentBundle I M) Ã— (TangentBundle I' M') where
  toFun p := (âŸ¨p.1.1, p.2.1âŸ©, âŸ¨p.1.2, p.2.2âŸ©)
  invFun p := âŸ¨(p.1.1, p.2.1), (p.1.2, p.2.2)âŸ©

lemma equivTangentBundleProd_eq_tangentMap_prod_tangentMap :
    equivTangentBundleProd I M I' M' = fun (p : TangentBundle (I.prod I') (M Ã— M')) â†¦
      (tangentMap (I.prod I') I Prod.fst p, tangentMap (I.prod I') I' Prod.snd p) := by
  simp only [tangentMap_prodFst, tangentMap_prodSnd]; rfl

variable [IsManifold I 1 M] [IsManifold I' 1 M']

/-- The canonical equivalence between the tangent bundle of a product and the product of
tangent bundles is smooth. -/
lemma contMDiff_equivTangentBundleProd :
    ContMDiff (I.prod I').tangent (I.tangent.prod I'.tangent) n
      (equivTangentBundleProd I M I' M') := by
  rw [equivTangentBundleProd_eq_tangentMap_prod_tangentMap]
  exact (contMDiff_fst.contMDiff_tangentMap le_rfl).prodMk
    (contMDiff_snd.contMDiff_tangentMap le_rfl)

/-- The canonical equivalence between the product of tangent bundles and the tangent bundle of a
product is smooth. -/
lemma contMDiff_equivTangentBundleProd_symm :
    ContMDiff (I.tangent.prod I'.tangent) (I.prod I').tangent n
      (equivTangentBundleProd I M I' M').symm := by
  /- Contrary to what one might expect, this proof is nontrivial. It is not a formalization issue:
  even on paper, I don't have a simple proof of the statement. The reason is that there is no nice
  functorial expression for the map from `TM Ã— T'M` to `T (M Ã— M')`, so I need to come back to
  the definition and break things into pieces.
  The argument goes as follows. Since we're looking at a map into a vector bundle whose basis map
  is smooth, it suffices to check the smoothness of the second component, in a chart. It lands in
  a product vector space `E Ã— E'`, so it suffices to check that the composition with each projection
  to `E` and `E'` is smooth.
  We notice that the composition of this map with the first projection coincides with the projection
  `TM Ã— TM' â†’ TM` read in the target chart, which is smooth, so we're done.
  The issue is with checking differentiability everywhere (to justify that the derivative of a
  product is the product of the derivatives), and writing down things.
  -/
  rintro âŸ¨a, bâŸ©
  have U w w' : UniqueDiffWithinAt ğ•œ (Set.range (Prod.map I I')) (I w, I' w') := by
    simp only [range_prodMap]
    apply UniqueDiffWithinAt.prod
    Â· exact ModelWithCorners.uniqueDiffWithinAt_image I
    Â· exact ModelWithCorners.uniqueDiffWithinAt_image I'
  rw [contMDiffAt_totalSpace]
  simp only [equivTangentBundleProd, TangentBundle.trivializationAt_apply, mfld_simps,
    Equiv.coe_fn_symm_mk]
  refine âŸ¨?_, (contMDiffAt_prod_module_iff _).2 âŸ¨?_, ?_âŸ©âŸ©
  Â· exact ContMDiffAt.prodMap (contMDiffAt_proj (TangentSpace I))
      (contMDiffAt_proj (TangentSpace I'))
  Â· /- check that the composition with the first projection in the target chart is smooth.
    For this, we check that it coincides locally with the projection `pM : TM Ã— TM' â†’ TM` read in
    the target chart, which is obviously smooth. -/
    have smooth_pM : ContMDiffAt (I.tangent.prod I'.tangent) I.tangent n Prod.fst (a, b) :=
      contMDiffAt_fst
    apply (contMDiffAt_totalSpace.1 smooth_pM).2.congr_of_eventuallyEq
    filter_upwards [chart_source_mem_nhds (ModelProd (ModelProd H E) (ModelProd H' E')) (a, b)]
      with p hp
    -- now we have to check that the original map coincides locally with `pM` read in target chart.
    simp only [prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod_source, Set.mem_prod, TangentBundle.mem_chart_source_iff] at hp
    let Ï† (x : E) := I ((chartAt H a.proj) ((chartAt H p.1.proj).symm (I.symm x)))
    have D0 : DifferentiableWithinAt ğ•œ Ï† (Set.range I) (I ((chartAt H p.1.proj) p.1.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt (n := 1) _ le_rfl
      apply contDiffWithinAt_ext_coord_change
      simp [hp.1]
    have D (w : TangentBundle I' M') :
        DifferentiableWithinAt ğ•œ (Ï† âˆ˜ (Prod.fst : E Ã— E' â†’ E)) (Set.range (Prod.map â†‘I â†‘I'))
        (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' w.proj) w.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0)
        differentiableWithinAt_fst (by simp [mapsTo_fst_prod])
    simp only [comp_def, comp_apply]
    rw [DifferentiableWithinAt.fderivWithin_prodMk (by exact D _) ?_ (U _ _)]; swap
    Â· let Ï†' (x : E') := I' ((chartAt H' b.proj) ((chartAt H' p.2.proj).symm (I'.symm x)))
      have D0' : DifferentiableWithinAt ğ•œ Ï†' (Set.range I')
          (I' ((chartAt H' p.2.proj) p.2.proj)) := by
        apply ContDiffWithinAt.differentiableWithinAt (n := 1) _ le_rfl
        apply contDiffWithinAt_ext_coord_change
        simp [hp.2]
      have D' : DifferentiableWithinAt ğ•œ (Ï†' âˆ˜ Prod.snd) (Set.range (Prod.map I I'))
          (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
        DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0')
          differentiableWithinAt_snd (by simp [mapsTo_snd_prod])
      exact D'
    simp only [TangentBundle.trivializationAt_apply, mfld_simps]
    change fderivWithin ğ•œ (Ï† âˆ˜ Prod.fst) _ _ _ = fderivWithin ğ•œ Ï† _ _ _
    rw [range_prodMap] at U
    rw [fderivWithin_comp _ (by exact D0) differentiableWithinAt_fst mapsTo_fst_prod (U _ _)]
    simp [fderivWithin_fst, U]
  Â· /- check that the composition with the second projection in the target chart is smooth.
    For this, we check that it coincides locally with the projection `pM' : TM Ã— TM' â†’ TM'` read in
    the target chart, which is obviously smooth. -/
    have smooth_pM' : ContMDiffAt (I.tangent.prod I'.tangent) I'.tangent n Prod.snd (a, b) :=
      contMDiffAt_snd
    apply (contMDiffAt_totalSpace.1 smooth_pM').2.congr_of_eventuallyEq
    filter_upwards [chart_source_mem_nhds (ModelProd (ModelProd H E) (ModelProd H' E')) (a, b)]
      with p hp
    -- now we have to check that the original map coincides locally with `pM'` read in target chart.
    simp only [prodChartedSpace_chartAt, PartialHomeomorph.prod_toPartialEquiv,
      PartialEquiv.prod_source, Set.mem_prod, TangentBundle.mem_chart_source_iff] at hp
    let Ï† (x : E') := I' ((chartAt H' b.proj) ((chartAt H' p.2.proj).symm (I'.symm x)))
    have D0 : DifferentiableWithinAt ğ•œ Ï† (Set.range I') (I' ((chartAt H' p.2.proj) p.2.proj)) := by
      apply ContDiffWithinAt.differentiableWithinAt _ le_rfl
      apply contDiffWithinAt_ext_coord_change
      simp [hp.2]
    have D (w : TangentBundle I M) :
        DifferentiableWithinAt ğ•œ (Ï† âˆ˜ (Prod.snd : E Ã— E' â†’ E')) (Set.range (Prod.map â†‘I â†‘I'))
        (I ((chartAt H w.proj) w.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
      DifferentiableWithinAt.comp (t := Set.range I') _ (by exact D0)
        differentiableWithinAt_snd (by simp [mapsTo_snd_prod])
    simp only [comp_def, comp_apply]
    rw [DifferentiableWithinAt.fderivWithin_prodMk ?_ (by exact D _) (U _ _)]; swap
    Â· let Ï†' (x : E) := I ((chartAt H a.proj) ((chartAt H p.1.proj).symm (I.symm x)))
      have D0' : DifferentiableWithinAt ğ•œ Ï†' (Set.range I)
          (I ((chartAt H p.1.proj) p.1.proj)) := by
        apply ContDiffWithinAt.differentiableWithinAt _ le_rfl
        apply contDiffWithinAt_ext_coord_change
        simp [hp.1]
      have D' : DifferentiableWithinAt ğ•œ (Ï†' âˆ˜ Prod.fst) (Set.range (Prod.map I I'))
          (I ((chartAt H p.1.proj) p.1.proj), I' ((chartAt H' p.2.proj) p.2.proj)) :=
        DifferentiableWithinAt.comp (t := Set.range I) _ (by exact D0')
          differentiableWithinAt_fst (by simp [mapsTo_fst_prod])
      exact D'
    simp only [TangentBundle.trivializationAt_apply, mfld_simps]
    change fderivWithin ğ•œ (Ï† âˆ˜ Prod.snd) _ _ _ = fderivWithin ğ•œ Ï† _ _ _
    rw [range_prodMap] at U
    rw [fderivWithin_comp _ (by exact D0) differentiableWithinAt_snd mapsTo_snd_prod (U _ _)]
    simp [fderivWithin_snd, U]

end EquivTangentBundleProd
