/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, FrÃ©dÃ©ric Dupuis
-/
module

public import Mathlib.Analysis.Convex.Hull

/-!
# Convex cones

In an `R`-module `M`, we define a convex cone as a set `s` such that `a â€¢ x + b â€¢ y âˆˆ s` whenever
`x, y âˆˆ s` and `a, b > 0`. We prove that convex cones form a `CompleteLattice`, and define their
images (`ConvexCone.map`) and preimages (`ConvexCone.comap`) under linear maps.

We define pointed, blunt, flat and salient cones, and prove the correspondence between
convex cones and ordered modules.

We define `Convex.toCone` to be the minimal cone that includes a given convex set.

## Main statements

In `Mathlib/Analysis/Convex/Cone/Extension.lean` we prove
the M. Riesz extension theorem and a form of the Hahn-Banach theorem.

In `Mathlib/Analysis/Convex/Cone/Dual.lean` we prove
a variant of the hyperplane separation theorem.

## Implementation notes

While `Convex R` is a predicate on sets, `ConvexCone R M` is a bundled convex cone.

## References

* https://en.wikipedia.org/wiki/Convex_cone
* [Stephen P. Boyd and Lieven Vandenberghe, *Convex Optimization*][boydVandenberghe2004]
* [Emo Welzl and Bernd GÃ¤rtner, *Cone Programming*][welzl_garter]
-/

@[expose] public section

assert_not_exists TopologicalSpace Real Cardinal

open Set LinearMap Pointwise

variable {ğ•œ R G M N O : Type*}

/-! ### Definition of `ConvexCone` and basic properties -/

section Definitions

variable [Semiring R] [PartialOrder R]

variable (R M) in
/-- A convex cone is a subset `s` of an `R`-module such that `a â€¢ x + b â€¢ y âˆˆ s` whenever `a, b > 0`
and `x, y âˆˆ s`. -/
structure ConvexCone [AddCommMonoid M] [SMul R M] where
  /-- The **carrier set** underlying this cone: the set of points contained in it -/
  carrier : Set M
  smul_mem' : âˆ€ â¦ƒc : Râ¦„, 0 < c â†’ âˆ€ â¦ƒx : Mâ¦„, x âˆˆ carrier â†’ c â€¢ x âˆˆ carrier
  add_mem' : âˆ€ â¦ƒxâ¦„ (_ : x âˆˆ carrier) â¦ƒyâ¦„ (_ : y âˆˆ carrier), x + y âˆˆ carrier

end Definitions

namespace ConvexCone

section OrderedSemiring

variable [Semiring R] [PartialOrder R] [AddCommMonoid M]

section SMul

variable [SMul R M] {C Câ‚ Câ‚‚ : ConvexCone R M} {s : Set M} {c : R} {x : M}

instance : SetLike (ConvexCone R M) M where
  coe := carrier
  coe_injective' Câ‚ Câ‚‚ h := by cases Câ‚; congr!

@[simp, norm_cast] lemma coe_mk (s : Set M) (hâ‚ hâ‚‚) : â†‘(mk (R := R) s hâ‚ hâ‚‚) = s := rfl

@[simp] lemma mem_mk {hâ‚ hâ‚‚} : x âˆˆ mk (R := R) s hâ‚ hâ‚‚ â†” x âˆˆ s := .rfl

/-- Two `ConvexCone`s are equal if they have the same elements. -/
@[ext]
theorem ext (h : âˆ€ x, x âˆˆ Câ‚ â†” x âˆˆ Câ‚‚) : Câ‚ = Câ‚‚ := SetLike.ext h

variable (C) in
@[aesop 90% (rule_sets := [SetLike])]
protected lemma smul_mem (hc : 0 < c) (hx : x âˆˆ C) : c â€¢ x âˆˆ C := C.smul_mem' hc hx

variable (C) in
protected lemma add_mem â¦ƒxâ¦„ (hx : x âˆˆ C) â¦ƒyâ¦„ (hy : y âˆˆ C) : x + y âˆˆ C := C.add_mem' hx hy

instance : AddMemClass (ConvexCone R M) M where add_mem ha hb := add_mem' _ ha hb

/-- Copy of a convex cone with a new `carrier` equal to the old one. Useful to fix definitional
equalities. -/
@[simps] protected def copy (C : ConvexCone R M) (s : Set M) (hs : s = C) : ConvexCone R M where
  carrier := s
  add_mem' := hs.symm â–¸ C.add_mem'
  smul_mem' := by simpa [hs] using C.smul_mem'

lemma copy_eq (C : ConvexCone R M) (s : Set M) (hs) : C.copy s hs = C := SetLike.coe_injective hs

instance : InfSet (ConvexCone R M) where
  sInf S :=
    âŸ¨â‹‚ C âˆˆ S, C, fun _r hr _x hx â†¦ mem_biInter fun C hC â†¦ C.smul_mem hr <| mem_iInterâ‚‚.1 hx C hC,
      fun _ hx _ hy â†¦
      mem_biInter fun C hC â†¦ add_mem (mem_iInterâ‚‚.1 hx C hC) (mem_iInterâ‚‚.1 hy C hC)âŸ©

@[simp, norm_cast]
lemma coe_sInf (S : Set (ConvexCone R M)) : â†‘(sInf S) = â‹‚ C âˆˆ S, (C : Set M) := rfl

@[simp] lemma mem_sInf {S : Set (ConvexCone R M)} : x âˆˆ sInf S â†” âˆ€ C âˆˆ S, x âˆˆ C := mem_iInterâ‚‚

@[simp, norm_cast]
theorem coe_iInf {Î¹ : Sort*} (f : Î¹ â†’ ConvexCone R M) : â†‘(iInf f) = â‹‚ i, (f i : Set M) := by
  simp [iInf]

@[simp]
lemma mem_iInf {Î¹ : Sort*} {f : Î¹ â†’ ConvexCone R M} : x âˆˆ iInf f â†” âˆ€ i, x âˆˆ f i :=
  mem_iInterâ‚‚.trans <| by simp

instance : CompleteSemilatticeInf (ConvexCone R M) where
  sInf_le C C hC := by rw [â† SetLike.coe_subset_coe, coe_sInf]; exact biInter_subset_of_mem hC
  le_sInf C C hC := by rw [â† SetLike.coe_subset_coe, coe_sInf]; exact subset_iInterâ‚‚ hC

variable (R s) in
/-- The cone hull of a set. The smallest convex cone containing that set. -/
def hull : ConvexCone R M := sInf {C : ConvexCone R M | s âŠ† C}

lemma subset_hull : s âŠ† hull R s := by simp [hull]

lemma hull_min (hsC : s âŠ† C) : hull R s â‰¤ C := sInf_le hsC

lemma hull_le_iff : hull R s â‰¤ C â†” s âŠ† C := âŸ¨subset_hull.trans, hull_minâŸ©

lemma gc_hull_coe : GaloisConnection (hull R : Set M â†’ ConvexCone R M) (â†‘) :=
  fun _C _s â†¦ hull_le_iff

/-- Galois insertion between `ConvexCone` and `SetLike.coe`. -/
protected def gi : GaloisInsertion (hull R : Set M â†’ ConvexCone R M) (â†‘) where
  gc := gc_hull_coe
  le_l_u _ := subset_hull
  choice s hs := (hull R s).copy s <| subset_hull.antisymm hs
  choice_eq _ _ := copy_eq _ _ _

instance : Bot (ConvexCone R M) :=
  âŸ¨âŸ¨âˆ…, fun _ _ _ => False.elim, fun _ => False.elimâŸ©âŸ©

@[simp] lemma notMem_bot : x âˆ‰ (âŠ¥ : ConvexCone R M) := id

@[simp, norm_cast] lemma coe_bot : â†‘(âŠ¥ : ConvexCone R M) = (âˆ… : Set M) := rfl

@[simp, norm_cast]
lemma coe_eq_empty : (C : Set M) = âˆ… â†” C = âŠ¥ := by rw [â† coe_bot (R := R)]; norm_cast

instance : CompleteLattice (ConvexCone R M) where
  bot := âŠ¥
  bot_le _ := empty_subset _
  __ := instCompleteSemilatticeInf
  __ := ConvexCone.gi.liftCompleteLattice

variable (Câ‚ Câ‚‚) in
@[simp, norm_cast] lemma coe_inf : (Câ‚ âŠ“ Câ‚‚) = (Câ‚ âˆ© Câ‚‚ : Set M) := rfl

@[simp] lemma mem_inf : x âˆˆ Câ‚ âŠ“ Câ‚‚ â†” x âˆˆ Câ‚ âˆ§ x âˆˆ Câ‚‚ := .rfl

@[simp] lemma mem_top : x âˆˆ (âŠ¤ : ConvexCone R M) := mem_univ x

@[simp, norm_cast] lemma coe_top : â†‘(âŠ¤ : ConvexCone R M) = (univ : Set M) := rfl

@[simp, norm_cast] lemma disjoint_coe : Disjoint (Câ‚ : Set M) Câ‚‚ â†” Disjoint Câ‚ Câ‚‚ := by
  simp [disjoint_iff, â† coe_inf]

instance : Inhabited (ConvexCone R M) := âŸ¨âŠ¥âŸ©

end SMul

section Module

variable [Module R M] (C : ConvexCone R M)

protected theorem convex : Convex R (C : Set M) :=
  convex_iff_forall_pos.2 fun _ hx _ hy _ _ ha hb _ â†¦ add_mem (C.smul_mem ha hx) (C.smul_mem hb hy)

end Module

section Maps

variable [AddCommMonoid N] [AddCommMonoid O]
variable [Module R M] [Module R N] [Module R O]

/-- The image of a convex cone under an `R`-linear map is a convex cone. -/
def map (f : M â†’â‚—[R] N) (C : ConvexCone R M) : ConvexCone R N where
  carrier := f '' C
  smul_mem' := fun c hc _ âŸ¨x, hx, hyâŸ© => hy â–¸ f.map_smul c x â–¸ mem_image_of_mem f (C.smul_mem hc hx)
  add_mem' := fun _ âŸ¨xâ‚, hxâ‚, hyâ‚âŸ© _ âŸ¨xâ‚‚, hxâ‚‚, hyâ‚‚âŸ© =>
    hyâ‚ â–¸ hyâ‚‚ â–¸ f.map_add xâ‚ xâ‚‚ â–¸ mem_image_of_mem f (add_mem hxâ‚ hxâ‚‚)

@[simp, norm_cast]
theorem coe_map (C : ConvexCone R M) (f : M â†’â‚—[R] N) : (C.map f : Set N) = f '' C :=
  rfl

@[simp]
theorem mem_map {f : M â†’â‚—[R] N} {C : ConvexCone R M} {y : N} : y âˆˆ C.map f â†” âˆƒ x âˆˆ C, f x = y :=
  Set.mem_image f C y

theorem map_map (g : N â†’â‚—[R] O) (f : M â†’â‚—[R] N) (C : ConvexCone R M) :
    (C.map f).map g = C.map (g.comp f) :=
  SetLike.coe_injective <| image_image g f C

@[simp]
theorem map_id (C : ConvexCone R M) : C.map LinearMap.id = C :=
  SetLike.coe_injective <| image_id _

/-- The preimage of a convex cone under an `R`-linear map is a convex cone. -/
def comap (f : M â†’â‚—[R] N) (C : ConvexCone R N) : ConvexCone R M where
  carrier := f â»Â¹' C
  smul_mem' c hc x hx := by
    rw [mem_preimage, f.map_smul c]
    exact C.smul_mem hc hx
  add_mem' x hx y hy := by
    rw [mem_preimage, f.map_add]
    exact add_mem hx hy

@[simp]
theorem coe_comap (f : M â†’â‚—[R] N) (C : ConvexCone R N) : (C.comap f : Set M) = f â»Â¹' C :=
  rfl

@[simp]
theorem comap_id (C : ConvexCone R M) : C.comap LinearMap.id = C :=
  rfl

theorem comap_comap (g : N â†’â‚—[R] O) (f : M â†’â‚—[R] N) (C : ConvexCone R O) :
    (C.comap g).comap f = C.comap (g.comp f) :=
  rfl

@[simp]
theorem mem_comap {f : M â†’â‚—[R] N} {C : ConvexCone R N} {x : M} : x âˆˆ C.comap f â†” f x âˆˆ C :=
  Iff.rfl

end Maps

end OrderedSemiring

section LinearOrderedField

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

section MulAction

variable [AddCommMonoid M]
variable [MulAction ğ•œ M] (C : ConvexCone ğ•œ M)

theorem smul_mem_iff {c : ğ•œ} (hc : 0 < c) {x : M} : c â€¢ x âˆˆ C â†” x âˆˆ C :=
  âŸ¨fun h => inv_smul_smulâ‚€ hc.ne' x â–¸ C.smul_mem (inv_pos.2 hc) h, C.smul_mem hcâŸ©

end MulAction
end LinearOrderedField

/-! ### Convex cones with extra properties -/


section OrderedSemiring

variable [Semiring R] [PartialOrder R]

section AddCommMonoid

variable [AddCommMonoid M] [SMul R M] {C Câ‚ Câ‚‚ : ConvexCone R M}

/-- A convex cone is pointed if it includes `0`. -/
def Pointed (C : ConvexCone R M) : Prop := (0 : M) âˆˆ C

/-- A convex cone is blunt if it doesn't include `0`. -/
def Blunt (C : ConvexCone R M) : Prop := (0 : M) âˆ‰ C

lemma blunt_iff_not_pointed : C.Blunt â†” Â¬ C.Pointed := .rfl
lemma pointed_iff_not_blunt : C.Pointed â†” Â¬ C.Blunt := by simp [Blunt, Pointed]

theorem Pointed.mono (h : Câ‚ â‰¤ Câ‚‚) : Câ‚.Pointed â†’ Câ‚‚.Pointed := @h _
theorem Blunt.anti (h : Câ‚‚ â‰¤ Câ‚) : Câ‚.Blunt â†’ Câ‚‚.Blunt := (Â· âˆ˜ @h 0)

end AddCommMonoid

section AddCommGroup

variable [AddCommGroup G] [SMul R G] {C Câ‚ Câ‚‚ : ConvexCone R G}

/-- A convex cone is flat if it contains some nonzero vector `x` and its opposite `-x`. -/
def Flat (C : ConvexCone R G) : Prop := âˆƒ x âˆˆ C, x â‰  (0 : G) âˆ§ -x âˆˆ C

/-- A convex cone is salient if it doesn't include `x` and `-x` for any nonzero `x`. -/
def Salient (C : ConvexCone R G) : Prop := âˆ€ x âˆˆ C, x â‰  (0 : G) â†’ -x âˆ‰ C

theorem salient_iff_not_flat : C.Salient â†” Â¬ C.Flat := by simp [Salient, Flat]

theorem Flat.mono (h : Câ‚ â‰¤ Câ‚‚) : Câ‚.Flat â†’ Câ‚‚.Flat
  | âŸ¨x, hxS, hx, hnxSâŸ© => âŸ¨x, h hxS, hx, h hnxSâŸ©

theorem Salient.anti (h : Câ‚‚ â‰¤ Câ‚) : Câ‚.Salient â†’ Câ‚‚.Salient :=
  fun hS x hxT hx hnT => hS x (h hxT) hx (h hnT)

/-- A flat cone is always pointed (contains `0`). -/
theorem Flat.pointed (hC : C.Flat) : C.Pointed := by
  obtain âŸ¨x, hx, _, hxnegâŸ© := hC
  rw [Pointed, â† add_neg_cancel x]
  exact add_mem hx hxneg

/-- A blunt cone (one not containing `0`) is always salient. -/
theorem Blunt.salient : C.Blunt â†’ C.Salient := by
  rw [salient_iff_not_flat, blunt_iff_not_pointed]
  exact mt Flat.pointed

/-- A pointed convex cone defines a preorder. -/
def toPreorder (C : ConvexCone R G) (hâ‚ : C.Pointed) : Preorder G where
  le x y := y - x âˆˆ C
  le_refl x := by rw [sub_self x]; exact hâ‚
  le_trans x y z xy zy := by simpa using add_mem zy xy

/-- A pointed and salient cone defines a partial order. -/
def toPartialOrder (C : ConvexCone R G) (hâ‚ : C.Pointed) (hâ‚‚ : C.Salient) : PartialOrder G :=
  { toPreorder C hâ‚ with
    le_antisymm := by
      intro a b ab ba
      by_contra h
      have h' : b - a â‰  0 := fun h'' => h (eq_of_sub_eq_zero h'').symm
      have H := hâ‚‚ (b - a) ab h'
      rw [neg_sub b a] at H
      exact H ba }

/-- A pointed and salient cone defines an `IsOrderedAddMonoid`. -/
lemma to_isOrderedAddMonoid (C : ConvexCone R G) (hâ‚ : C.Pointed) (hâ‚‚ : C.Salient) :
    let _ := toPartialOrder C hâ‚ hâ‚‚
    IsOrderedAddMonoid G where
  __ := toPartialOrder C hâ‚ hâ‚‚
  add_le_add_left a b hab c := show b + c - (a + c) âˆˆ C by rwa [add_sub_add_right_eq_sub]

end AddCommGroup

section Module

variable [AddCommMonoid M] [Module R M] {Câ‚ Câ‚‚ : ConvexCone R M} {x : M}

instance : Zero (ConvexCone R M) :=
  âŸ¨âŸ¨0, fun _ _ => by simp, fun _ => by simpâŸ©âŸ©

@[simp] lemma mem_zero : x âˆˆ (0 : ConvexCone R M) â†” x = 0 := .rfl

@[simp, norm_cast] lemma coe_zero : ((0 : ConvexCone R M) : Set M) = 0 := rfl

theorem pointed_zero : (0 : ConvexCone R M).Pointed := by rw [Pointed, mem_zero]

instance instAdd : Add (ConvexCone R M) where
  add Câ‚ Câ‚‚ := {
    carrier := Câ‚ + Câ‚‚
    smul_mem' := by
      rintro c hc _ âŸ¨x, hx, y, hy, rflâŸ©
      rw [smul_add]
      use c â€¢ x, Câ‚.smul_mem hc hx, c â€¢ y, Câ‚‚.smul_mem hc hy
    add_mem' := by
      rintro _ âŸ¨xâ‚, hxâ‚, xâ‚‚, hxâ‚‚, rflâŸ© y âŸ¨yâ‚, hyâ‚, yâ‚‚, hyâ‚‚, rflâŸ©
      exact âŸ¨xâ‚ + yâ‚, add_mem hxâ‚ hyâ‚, xâ‚‚ + yâ‚‚, add_mem hxâ‚‚ hyâ‚‚, add_add_add_comm ..âŸ©
  }

@[simp, norm_cast] lemma coe_add (Câ‚ Câ‚‚ : ConvexCone R M) : â†‘(Câ‚ + Câ‚‚) = (Câ‚ + Câ‚‚ : Set M) := rfl
@[simp] lemma mem_add : x âˆˆ Câ‚ + Câ‚‚ â†” âˆƒ y âˆˆ Câ‚, âˆƒ z âˆˆ Câ‚‚, y + z = x := .rfl

instance instAddZeroClass : AddZeroClass (ConvexCone R M) where
  zero_add _ := by ext; simp
  add_zero _ := by ext; simp

instance instAddCommSemigroup : AddCommSemigroup (ConvexCone R M) where
  add_assoc _ _ _ := SetLike.coe_injective <| add_assoc _ _ _
  add_comm _ _ := SetLike.coe_injective <| add_comm _ _

section Generating

/-- A convex cone `C` is generating if its linear span is the entire `R`-module `M`.

`IsGenerating` is equivalent to `IsReproducing` modulo some conditions.
See `IsReproducing.isGenerating` and `IsGenerating.isReproducing` for details. -/
@[simp] def IsGenerating (C : ConvexCone R M) : Prop :=
  Submodule.span R (C : Set M) = âŠ¤

/-- A sufficient criteria for a convex cone `C` to be generating is that top is less than or equal
to the linear span of `C`. -/
theorem IsGenerating.of_top_le_span {C : ConvexCone R M} (h : âŠ¤ â‰¤ Submodule.span R (C : Set M)) :
    C.IsGenerating :=
  eq_top_iff.mpr h

/-- The linear span of a generating convex cone equals top. -/
lemma IsGenerating.span_eq_top {C : ConvexCone R M} (hC : C.IsGenerating) :
    Submodule.span R (C : Set M) = âŠ¤ :=
  hC

/-- Top is less than or equal to the linear span of a generating convex cone. -/
lemma IsGenerating.top_le_span {C : ConvexCone R M} (hC : C.IsGenerating) :
    âŠ¤ â‰¤ Submodule.span R (C : Set M) :=
  hC.span_eq_top.ge

/-- The whole `R`-module `M` (viewed as the top convex cone) is generating. -/
theorem isGenerating_top : (âŠ¤ : ConvexCone R M).IsGenerating := by
  simp

/-- The empty convex cone is generating iff the module is a subsingleton. -/
theorem isGenerating_bot_iff : (âŠ¥ : ConvexCone R M).IsGenerating â†” Subsingleton M := by
  simpa only [IsGenerating, coe_bot, Submodule.span_empty, â† Submodule.subsingleton_iff R] using
    subsingleton_iff_bot_eq_top

/-- In a subsingleton module, the empty convex cone is generating. -/
theorem isGenerating_bot [Subsingleton M] : (âŠ¥ : ConvexCone R M).IsGenerating :=
  isGenerating_bot_iff.mpr inferInstance

/-- A convex cone containing a generating cone is also a generating cone. -/
@[gcongr]
theorem IsGenerating.mono (h : Câ‚ â‰¤ Câ‚‚) (hgen : Câ‚.IsGenerating) : Câ‚‚.IsGenerating := by
  rw [IsGenerating, â† top_le_iff] at hgen âŠ¢
  exact hgen.trans (Submodule.span_mono h)

/-- A convex cone is reproducing if its set of element differences equals the entire module,
i.e., every element of `M` can be written as a difference of two elements of `C`.

See also (`IsGenerating`). -/
def IsReproducing [AddCommGroup M] (C : ConvexCone R M) : Prop :=
  (C : Set M) - (C : Set M) = Set.univ

/-- A sufficient criterion for a convex cone `C` to be reproducing is that `Set.univ` is a subset
of `C - C`. -/
theorem IsReproducing.of_univ_subset [AddCommGroup M] {C : ConvexCone R M}
    (h : Set.univ âŠ† (C : Set M) - (C : Set M)) : C.IsReproducing :=
  Set.eq_univ_iff_forall.mpr fun _ â†¦ h (Set.mem_univ _)

/-- The set difference of a reproducing cone with itself equals `Set.univ`. -/
lemma IsReproducing.sub_eq_univ [AddCommGroup M] {C : ConvexCone R M} (hC : C.IsReproducing) :
    (C : Set M) - (C : Set M) = Set.univ :=
  hC

/-- A reproducing cone is generating. -/
theorem IsReproducing.isGenerating {R : Type*} {M : Type*} [Ring R] [PartialOrder R]
    [AddCommGroup M] [Module R M] {C : ConvexCone R M} (h : C.IsReproducing) :
    C.IsGenerating := by
  rw [IsGenerating, eq_top_iff]
  rintro x -
  rw [IsReproducing, Set.eq_univ_iff_forall] at h
  obtain âŸ¨y, hy, z, hz, rflâŸ© := Set.mem_sub.mp (h x)
  exact sub_mem (Submodule.subset_span hy) (Submodule.subset_span hz)

/-- A generating cone is reproducing. -/
theorem IsGenerating.isReproducing {R : Type*} {M : Type*} [Ring R] [LinearOrder R]
    [AddLeftStrictMono R] [AddCommGroup M] [Nontrivial M] [Module R M] {C : ConvexCone R M}
    (h : C.IsGenerating) :
    C.IsReproducing := by
  rw [IsReproducing, Set.eq_univ_iff_forall]
  intro x
  -- A generating cone in a nontrivial module must be nonempty
  have hne : (C : Set M).Nonempty := Set.nonempty_iff_ne_empty.2 fun h' =>
      not_subsingleton M <| isGenerating_bot_iff.1 <| coe_eq_empty.1 h' â–¸ h
  -- Build the submodule S = C - C and show span C âŠ† S
  let S : Submodule R M := {
    carrier := (C : Set M) - (C : Set M)
    add_mem' := by
      rintro _ _ âŸ¨yâ‚, hyâ‚, zâ‚, hzâ‚, rflâŸ© âŸ¨yâ‚‚, hyâ‚‚, zâ‚‚, hzâ‚‚, rflâŸ©
      exact âŸ¨yâ‚ + yâ‚‚, C.add_mem hyâ‚ hyâ‚‚, zâ‚ + zâ‚‚, C.add_mem hzâ‚ hzâ‚‚, add_sub_add_comm ..âŸ©
    zero_mem' := by
      obtain âŸ¨c, hcâŸ© := hne
      exact âŸ¨c, hc, c, hc, sub_self câŸ©
    smul_mem' := by
      rintro r _ âŸ¨y, hy, z, hz, rflâŸ©
      simp only [Set.mem_sub, SetLike.mem_coe]
      rcases lt_trichotomy r 0 with hr | rfl | hr
      Â· -- r < 0: use (-r) â€¢ z - (-r) â€¢ y = r â€¢ (y - z)
        refine âŸ¨(-r) â€¢ z, C.smul_mem (neg_pos.mpr hr) hz,
               (-r) â€¢ y, C.smul_mem (neg_pos.mpr hr) hy, ?_âŸ©
        rw [neg_smul, neg_smul, neg_sub_neg, smul_sub]
      Â· -- r = 0
        simp only [zero_smul]
        obtain âŸ¨c, hcâŸ© := hne
        exact âŸ¨c, hc, c, hc, sub_self câŸ©
      Â· -- r > 0: use r â€¢ y - r â€¢ z
        exact âŸ¨r â€¢ y, C.smul_mem hr hy, r â€¢ z, C.smul_mem hr hz, (smul_sub r y z).symmâŸ©}
  have hCS : (C : Set M) âŠ† S := fun x hx â†¦
    let âŸ¨c, hcâŸ© := hne; âŸ¨x + c, C.add_mem hx hc, c, hc, add_sub_cancel_right x câŸ©
  exact (h â–¸ Submodule.span_le.mpr hCS) trivial

/-- A convex cone is generating iff every element is a difference of cone elements. -/
theorem isGenerating_iff_isReproducing {R : Type*} {M : Type*} [Ring R] [LinearOrder R]
    [AddLeftStrictMono R] [AddCommGroup M] [Nontrivial M] [Module R M] {C : ConvexCone R M} :
    C.IsGenerating â†” C.IsReproducing :=
  âŸ¨IsGenerating.isReproducing, IsReproducing.isGeneratingâŸ©

end Generating

end Module

end OrderedSemiring

section Field
variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup M] [Module ğ•œ M]
  {C : ConvexCone ğ•œ M} {s : Set M} {x : M}

/-- The cone hull of a convex set is simply the union of the open halflines through that set. -/
lemma mem_hull_of_convex (hs : Convex ğ•œ s) : x âˆˆ hull ğ•œ s â†” âˆƒ r : ğ•œ, 0 < r âˆ§ x âˆˆ r â€¢ s where
  mp hx := hull_min (C := {
              carrier := {y | âˆƒ r : ğ•œ, 0 < r âˆ§ y âˆˆ r â€¢ s}
              smul_mem' := by
                intro râ‚ hrâ‚ y âŸ¨râ‚‚, hrâ‚‚, hyâŸ©
                refine âŸ¨râ‚ * râ‚‚, mul_pos hrâ‚ hrâ‚‚, ?_âŸ©
                rw [mul_smul]
                exact smul_mem_smul_set hy
              add_mem' := by
                rintro yâ‚ âŸ¨râ‚, hrâ‚, hyâ‚âŸ© yâ‚‚ âŸ¨râ‚‚, hrâ‚‚, hyâ‚‚âŸ©
                refine âŸ¨râ‚ + râ‚‚, add_pos hrâ‚ hrâ‚‚, ?_âŸ©
                rw [hs.add_smul hrâ‚.le hrâ‚‚.le]
                exact add_mem_add hyâ‚ hyâ‚‚
            }) (fun y hy â†¦ âŸ¨1, by simpaâŸ©) hx
  mpr := by rintro âŸ¨r, hr, y, hy, rflâŸ©; exact (hull ğ•œ s).smul_mem hr <| subset_hull hy

/-- The cone hull of a convex set is simply the union of the open halflines through that set. -/
lemma coe_hull_of_convex (hs : Convex ğ•œ s) : hull ğ•œ s = {x | âˆƒ r : ğ•œ, 0 < r âˆ§ x âˆˆ r â€¢ s} := by
  ext; exact mem_hull_of_convex hs

lemma disjoint_hull_left_of_convex (hs : Convex ğ•œ s) : Disjoint (hull ğ•œ s) C â†” Disjoint s C where
  mp := by rw [â† disjoint_coe]; exact .mono_left subset_hull
  mpr := by
    simp_rw [â† disjoint_coe, disjoint_left, SetLike.mem_coe, mem_hull_of_convex hs]
    rintro hsC _ âŸ¨r, hr, y, hy, rflâŸ©
    exact (C.smul_mem_iff hr).not.mpr (hsC hy)

lemma disjoint_hull_right_of_convex (hs : Convex ğ•œ s) : Disjoint C (hull ğ•œ s) â†” Disjoint â†‘C s := by
  rw [disjoint_comm, disjoint_hull_left_of_convex hs, disjoint_comm]

end Field
end ConvexCone

namespace Submodule

/-! ### Submodules are cones -/


section OrderedSemiring

variable [Semiring R] [PartialOrder R]

section AddCommMonoid

variable [AddCommMonoid M] [Module R M] {C Câ‚ Câ‚‚ : Submodule R M} {x : M}

/-- Every submodule is trivially a convex cone. -/
def toConvexCone (C : Submodule R M) : ConvexCone R M where
  carrier := C
  smul_mem' c _ _ hx := C.smul_mem c hx
  add_mem' _ hx _ hy := C.add_mem hx hy

@[simp] lemma coe_toConvexCone (C : Submodule R M) : C.toConvexCone = (C : Set M) := rfl

@[simp] lemma mem_toConvexCone : x âˆˆ C.toConvexCone â†” x âˆˆ C := .rfl

@[simp]
lemma toConvexCone_le_toConvexCone : Câ‚.toConvexCone â‰¤ Câ‚‚.toConvexCone â†” Câ‚ â‰¤ Câ‚‚ := .rfl

@[simp] lemma toConvexCone_bot : (âŠ¥ : Submodule R M).toConvexCone = 0 := rfl
@[simp] lemma toConvexCone_top : (âŠ¤ : Submodule R M).toConvexCone = âŠ¤ := rfl

@[simp]
lemma toConvexCone_inf (Câ‚ Câ‚‚ : Submodule R M) :
    (Câ‚ âŠ“ Câ‚‚).toConvexCone = Câ‚.toConvexCone âŠ“ Câ‚‚.toConvexCone := rfl

@[simp]
lemma pointed_toConvexCone (C : Submodule R M) : C.toConvexCone.Pointed := C.zero_mem

end AddCommMonoid

end OrderedSemiring

end Submodule

/-! ### Positive cone of an ordered module -/

namespace ConvexCone

section PositiveCone
variable [Semiring R] [PartialOrder R] [AddCommMonoid M] [PartialOrder M] [IsOrderedAddMonoid M]
  [Module R M] [PosSMulMono R M] {x : M}

variable (R M) in
/-- The positive cone is the convex cone formed by the set of nonnegative elements in an ordered
module. -/
def positive : ConvexCone R M where
  carrier := Set.Ici 0
  smul_mem' _ hc _ (hx : _ â‰¤ _) := smul_nonneg hc.le hx
  add_mem' _ (hx : _ â‰¤ _) _ (hy : _ â‰¤ _) := add_nonneg hx hy

@[simp] lemma mem_positive : x âˆˆ positive R M â†” 0 â‰¤ x := .rfl

variable (R M) in
@[simp]
theorem coe_positive : â†‘(positive R M) = Set.Ici (0 : M) :=
  rfl

/-- The positive cone of an ordered module is always salient. -/
lemma salient_positive {G : Type*} [AddCommGroup G] [PartialOrder G] [IsOrderedAddMonoid G]
    [Module R G] [PosSMulMono R G] : Salient (positive R G) :=
  fun x hx_nonneg hx_ne_zero hx_nonpos â†¦ lt_irrefl (0 : G) <| by
    simpa using add_pos_of_nonneg_of_pos hx_nonpos <| hx_nonneg.lt_of_ne' hx_ne_zero

/-- The positive cone of an ordered module is always pointed. -/
theorem pointed_positive : Pointed (positive R M) :=
  le_refl 0

end PositiveCone

section StrictlyPositiveCone
variable [Semiring R] [PartialOrder R] [AddCommGroup M] [PartialOrder M] [IsOrderedAddMonoid M]
  [Module R M] [PosSMulStrictMono R M] {x : M}

variable (R M) in
/-- The cone of strictly positive elements.

Note that this naming diverges from the mathlib convention of `pos` and `nonneg` due to "positive
cone" (`ConvexCone.positive`) being established terminology for the non-negative elements. -/
def strictlyPositive : ConvexCone R M where
  carrier := Set.Ioi 0
  smul_mem' _ hc _ (hx : _ < _) := smul_pos hc hx
  add_mem' _ hx _ hy := add_pos hx hy

@[simp]
lemma mem_strictlyPositive : x âˆˆ strictlyPositive R M â†” 0 < x := .rfl

variable (R M) in
@[simp]
theorem coe_strictlyPositive : â†‘(strictlyPositive R M) = Set.Ioi (0 : M) :=
  rfl

lemma strictlyPositive_le_positive : strictlyPositive R M â‰¤ positive R M := fun _ => le_of_lt

/-- The strictly positive cone of an ordered module is always salient. -/
theorem salient_strictlyPositive : Salient (strictlyPositive R M) :=
  salient_positive.anti strictlyPositive_le_positive

/-- The strictly positive cone of an ordered module is always blunt. -/
theorem blunt_strictlyPositive : Blunt (strictlyPositive R M) :=
  lt_irrefl 0

end StrictlyPositiveCone

end ConvexCone

/-! ### Cone over a convex set -/


section ConeFromConvex

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup M] [Module ğ•œ M]

namespace Convex

/-- The set of vectors proportional to those in a convex set forms a convex cone. -/
def toCone (s : Set M) (hs : Convex ğ•œ s) : ConvexCone ğ•œ M := by
  apply ConvexCone.mk (â‹ƒ (c : ğ•œ) (_ : 0 < c), c â€¢ s) <;> simp only [mem_iUnion, mem_smul_set]
  Â· rintro c c_pos _ âŸ¨c', c'_pos, x, hx, rflâŸ©
    exact âŸ¨c * c', mul_pos c_pos c'_pos, x, hx, (smul_smul _ _ _).symmâŸ©
  Â· rintro _ âŸ¨cx, cx_pos, x, hx, rflâŸ© _ âŸ¨cy, cy_pos, y, hy, rflâŸ©
    have : 0 < cx + cy := add_pos cx_pos cy_pos
    refine âŸ¨_, this, _, convex_iff_div.1 hs hx hy cx_pos.le cy_pos.le this, ?_âŸ©
    simp only [smul_add, smul_smul, mul_div_assoc', mul_div_cancel_leftâ‚€ _ this.ne']

variable {s : Set M} (hs : Convex ğ•œ s) {x : M}

theorem mem_toCone : x âˆˆ hs.toCone s â†” âˆƒ c : ğ•œ, 0 < c âˆ§ âˆƒ y âˆˆ s, c â€¢ y = x := by
  simp only [toCone, ConvexCone.mem_mk, mem_iUnion, mem_smul_set, eq_comm, exists_prop]

theorem mem_toCone' : x âˆˆ hs.toCone s â†” âˆƒ c : ğ•œ, 0 < c âˆ§ c â€¢ x âˆˆ s := by
  refine hs.mem_toCone.trans âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨c, hc, y, hy, rflâŸ©
    exact âŸ¨câ»Â¹, inv_pos.2 hc, by rwa [smul_smul, inv_mul_cancelâ‚€ hc.ne', one_smul]âŸ©
  Â· rintro âŸ¨c, hc, hcxâŸ©
    exact âŸ¨câ»Â¹, inv_pos.2 hc, _, hcx, by rw [smul_smul, inv_mul_cancelâ‚€ hc.ne', one_smul]âŸ©

theorem subset_toCone : s âŠ† hs.toCone s := fun x hx =>
  hs.mem_toCone'.2 âŸ¨1, zero_lt_one, by rwa [one_smul]âŸ©

/-- `hs.toCone s` is the least cone that includes `s`. -/
theorem toCone_isLeast : IsLeast { t : ConvexCone ğ•œ M | s âŠ† t } (hs.toCone s) := by
  refine âŸ¨hs.subset_toCone, fun t ht x hx => ?_âŸ©
  rcases hs.mem_toCone.1 hx with âŸ¨c, hc, y, hy, rflâŸ©
  exact t.smul_mem hc (ht hy)

theorem toCone_eq_sInf : hs.toCone s = sInf { t : ConvexCone ğ•œ M | s âŠ† t } :=
  hs.toCone_isLeast.isGLB.sInf_eq.symm

end Convex

theorem convexHull_toCone_isLeast (s : Set M) :
    IsLeast { t : ConvexCone ğ•œ M | s âŠ† t } ((convex_convexHull ğ•œ s).toCone _) := by
  convert (convex_convexHull ğ•œ s).toCone_isLeast using 1
  ext t
  exact âŸ¨fun h => convexHull_min h t.convex, (subset_convexHull ğ•œ s).transâŸ©

theorem convexHull_toCone_eq_sInf (s : Set M) :
    (convex_convexHull ğ•œ s).toCone _ = sInf { t : ConvexCone ğ•œ M | s âŠ† t } :=
  Eq.symm <| IsGLB.sInf_eq <| IsLeast.isGLB <| convexHull_toCone_isLeast s

end ConeFromConvex
