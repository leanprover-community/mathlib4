/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, Manuel Candales
-/
import Mathlib.Geometry.Euclidean.Angle.Oriented.Affine
import Mathlib.Geometry.Euclidean.Angle.Unoriented.Affine
import Mathlib.Tactic.IntervalCases

#align_import geometry.euclidean.triangle from "leanprover-community/mathlib"@"46b633fd842bef9469441c0209906f6dddd2b4f5"

/-!
# Triangles

This file proves basic geometrical results about distances and angles
in (possibly degenerate) triangles in real inner product spaces and
Euclidean affine spaces. More specialized results, and results
developed for simplices in general rather than just for triangles, are
in separate files. Definitions and results that make sense in more
general affine spaces rather than just in the Euclidean case go under
`LinearAlgebra.AffineSpace`.

## Implementation notes

Results in this file are generally given in a form with only those
non-degeneracy conditions needed for the particular result, rather
than requiring affine independence of the points of a triangle
unnecessarily.

## References

* https://en.wikipedia.org/wiki/Law_of_cosines
* https://en.wikipedia.org/wiki/Pons_asinorum
* https://en.wikipedia.org/wiki/Sum_of_angles_of_a_triangle

-/


noncomputable section

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

open scoped BigOperators

open scoped Classical

open scoped Real

open scoped RealInnerProductSpace

namespace InnerProductGeometry

/-!
### Geometrical results on triangles in real inner product spaces

This section develops some results on (possibly degenerate) triangles
in real inner product spaces, where those definitions and results can
most conveniently be developed in terms of vectors and then used to
deduce corresponding results for Euclidean affine spaces.
-/


variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V]

/-- Law of cosines (cosine rule), vector angle form. -/
theorem norm_sub_sq_eq_norm_sq_add_norm_sq_sub_two_mul_norm_mul_norm_mul_cos_angle (x y : V) :
    â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - 2 * â€–xâ€– * â€–yâ€– * Real.cos (angle x y) := by
  rw [show 2 * â€–xâ€– * â€–yâ€– * Real.cos (angle x y) = 2 * (Real.cos (angle x y) * (â€–xâ€– * â€–yâ€–)) by ring,
    cos_angle_mul_norm_mul_norm, â† real_inner_self_eq_norm_mul_norm, â†
    real_inner_self_eq_norm_mul_norm, â† real_inner_self_eq_norm_mul_norm, real_inner_sub_sub_self,
    sub_add_eq_add_sub]
#align inner_product_geometry.norm_sub_sq_eq_norm_sq_add_norm_sq_sub_two_mul_norm_mul_norm_mul_cos_angle InnerProductGeometry.norm_sub_sq_eq_norm_sq_add_norm_sq_sub_two_mul_norm_mul_norm_mul_cos_angle

/-- Pons asinorum, vector angle form. -/
theorem angle_sub_eq_angle_sub_rev_of_norm_eq {x y : V} (h : â€–xâ€– = â€–yâ€–) :
    angle x (x - y) = angle y (y - x) := by
  refine' Real.injOn_cos âŸ¨angle_nonneg _ _, angle_le_pi _ _âŸ© âŸ¨angle_nonneg _ _, angle_le_pi _ _âŸ© _
  -- âŠ¢ Real.cos (angle x (x - y)) = Real.cos (angle y (y - x))
  rw [cos_angle, cos_angle, h, â† neg_sub, norm_neg, neg_sub, inner_sub_right, inner_sub_right,
    real_inner_self_eq_norm_mul_norm, real_inner_self_eq_norm_mul_norm, h, real_inner_comm x y]
#align inner_product_geometry.angle_sub_eq_angle_sub_rev_of_norm_eq InnerProductGeometry.angle_sub_eq_angle_sub_rev_of_norm_eq

/-- Converse of pons asinorum, vector angle form. -/
theorem norm_eq_of_angle_sub_eq_angle_sub_rev_of_angle_ne_pi {x y : V}
    (h : angle x (x - y) = angle y (y - x)) (hpi : angle x y â‰  Ï€) : â€–xâ€– = â€–yâ€– := by
  replace h := Real.arccos_injOn (abs_le.mp (abs_real_inner_div_norm_mul_norm_le_one x (x - y)))
    (abs_le.mp (abs_real_inner_div_norm_mul_norm_le_one y (y - x))) h
  by_cases hxy : x = y
  -- âŠ¢ â€–xâ€– = â€–yâ€–
  Â· rw [hxy]
    -- ğŸ‰ no goals
  Â· rw [â† norm_neg (y - x), neg_sub, mul_comm, mul_comm â€–yâ€–, div_eq_mul_inv, div_eq_mul_inv,
      mul_inv_rev, mul_inv_rev, â† mul_assoc, â† mul_assoc] at h
    replace h :=
      mul_right_cancelâ‚€ (inv_ne_zero fun hz => hxy (eq_of_sub_eq_zero (norm_eq_zero.1 hz))) h
    rw [inner_sub_right, inner_sub_right, real_inner_comm x y, real_inner_self_eq_norm_mul_norm,
      real_inner_self_eq_norm_mul_norm, mul_sub_right_distrib, mul_sub_right_distrib,
      mul_self_mul_inv, mul_self_mul_inv, sub_eq_sub_iff_sub_eq_sub, â† mul_sub_left_distrib] at h
    by_cases hx0 : x = 0
    -- âŠ¢ â€–xâ€– = â€–yâ€–
    Â· rw [hx0, norm_zero, inner_zero_left, zero_mul, zero_sub, neg_eq_zero] at h
      -- âŠ¢ â€–xâ€– = â€–yâ€–
      rw [hx0, norm_zero, h]
      -- ğŸ‰ no goals
    Â· by_cases hy0 : y = 0
      -- âŠ¢ â€–xâ€– = â€–yâ€–
      Â· rw [hy0, norm_zero, inner_zero_right, zero_mul, sub_zero] at h
        -- âŠ¢ â€–xâ€– = â€–yâ€–
        rw [hy0, norm_zero, h]
        -- ğŸ‰ no goals
      Â· rw [inv_sub_inv (fun hz => hx0 (norm_eq_zero.1 hz)) fun hz => hy0 (norm_eq_zero.1 hz), â†
          neg_sub, â† mul_div_assoc, mul_comm, mul_div_assoc, â† mul_neg_one] at h
        symm
        -- âŠ¢ â€–yâ€– = â€–xâ€–
        by_contra hyx
        -- âŠ¢ False
        replace h := (mul_left_cancelâ‚€ (sub_ne_zero_of_ne hyx) h).symm
        -- âŠ¢ False
        rw [real_inner_div_norm_mul_norm_eq_neg_one_iff, â† angle_eq_pi_iff] at h
        -- âŠ¢ False
        exact hpi h
        -- ğŸ‰ no goals
#align inner_product_geometry.norm_eq_of_angle_sub_eq_angle_sub_rev_of_angle_ne_pi InnerProductGeometry.norm_eq_of_angle_sub_eq_angle_sub_rev_of_angle_ne_pi

/-- The cosine of the sum of two angles in a possibly degenerate
triangle (where two given sides are nonzero), vector angle form. -/
theorem cos_angle_sub_add_angle_sub_rev_eq_neg_cos_angle {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    Real.cos (angle x (x - y) + angle y (y - x)) = -Real.cos (angle x y) := by
  by_cases hxy : x = y
  -- âŠ¢ Real.cos (angle x (x - y) + angle y (y - x)) = -Real.cos (angle x y)
  Â· rw [hxy, angle_self hy]
    -- âŠ¢ Real.cos (angle y (y - y) + angle y (y - y)) = -Real.cos 0
    simp
    -- ğŸ‰ no goals
  Â· rw [Real.cos_add, cos_angle, cos_angle, cos_angle]
    -- âŠ¢ inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - Re â€¦
    have hxn : â€–xâ€– â‰  0 := fun h => hx (norm_eq_zero.1 h)
    -- âŠ¢ inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - Re â€¦
    have hyn : â€–yâ€– â‰  0 := fun h => hy (norm_eq_zero.1 h)
    -- âŠ¢ inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - Re â€¦
    have hxyn : â€–x - yâ€– â‰  0 := fun h => hxy (eq_of_sub_eq_zero (norm_eq_zero.1 h))
    -- âŠ¢ inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - Re â€¦
    apply mul_right_cancelâ‚€ hxn
    -- âŠ¢ (inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - R â€¦
    apply mul_right_cancelâ‚€ hyn
    -- âŠ¢ (inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - R â€¦
    apply mul_right_cancelâ‚€ hxyn
    -- âŠ¢ (inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - R â€¦
    apply mul_right_cancelâ‚€ hxyn
    -- âŠ¢ (inner x (x - y) / (â€–xâ€– * â€–x - yâ€–) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) - R â€¦
    have H1 :
      Real.sin (angle x (x - y)) * Real.sin (angle y (y - x)) * â€–xâ€– * â€–yâ€– * â€–x - yâ€– * â€–x - yâ€– =
        Real.sin (angle x (x - y)) * (â€–xâ€– * â€–x - yâ€–) *
          (Real.sin (angle y (y - x)) * (â€–yâ€– * â€–x - yâ€–)) := by
      ring
    have H2 :
      âŸªx, xâŸ« * (âŸªx, xâŸ« - âŸªx, yâŸ« - (âŸªx, yâŸ« - âŸªy, yâŸ«)) - (âŸªx, xâŸ« - âŸªx, yâŸ«) * (âŸªx, xâŸ« - âŸªx, yâŸ«) =
        âŸªx, xâŸ« * âŸªy, yâŸ« - âŸªx, yâŸ« * âŸªx, yâŸ« := by
      ring
    have H3 :
      âŸªy, yâŸ« * (âŸªy, yâŸ« - âŸªx, yâŸ« - (âŸªx, yâŸ« - âŸªx, xâŸ«)) - (âŸªy, yâŸ« - âŸªx, yâŸ«) * (âŸªy, yâŸ« - âŸªx, yâŸ«) =
        âŸªx, xâŸ« * âŸªy, yâŸ« - âŸªx, yâŸ« * âŸªx, yâŸ« := by
      ring
    rw [mul_sub_right_distrib, mul_sub_right_distrib, mul_sub_right_distrib, mul_sub_right_distrib,
      H1, sin_angle_mul_norm_mul_norm, norm_sub_rev x y, sin_angle_mul_norm_mul_norm,
      norm_sub_rev y x, inner_sub_left, inner_sub_left, inner_sub_right, inner_sub_right,
      inner_sub_right, inner_sub_right, real_inner_comm x y, H2, H3,
      Real.mul_self_sqrt (sub_nonneg_of_le (real_inner_mul_inner_self_le x y)),
      real_inner_self_eq_norm_mul_norm, real_inner_self_eq_norm_mul_norm,
      real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two]
    field_simp [hxn, hyn, hxyn]
    -- âŠ¢ ((â€–xâ€– * â€–xâ€– * 2 - (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - â€–x - yâ€– * â€–x - yâ€–)) * (â€–yâ€– * â€–yâ€–  â€¦
    ring
    -- ğŸ‰ no goals
#align inner_product_geometry.cos_angle_sub_add_angle_sub_rev_eq_neg_cos_angle InnerProductGeometry.cos_angle_sub_add_angle_sub_rev_eq_neg_cos_angle

/-- The sine of the sum of two angles in a possibly degenerate
triangle (where two given sides are nonzero), vector angle form. -/
theorem sin_angle_sub_add_angle_sub_rev_eq_sin_angle {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    Real.sin (angle x (x - y) + angle y (y - x)) = Real.sin (angle x y) := by
  by_cases hxy : x = y
  -- âŠ¢ Real.sin (angle x (x - y) + angle y (y - x)) = Real.sin (angle x y)
  Â· rw [hxy, angle_self hy]
    -- âŠ¢ Real.sin (angle y (y - y) + angle y (y - y)) = Real.sin 0
    simp
    -- ğŸ‰ no goals
  Â· rw [Real.sin_add, cos_angle, cos_angle]
    -- âŠ¢ Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x ( â€¦
    have hxn : â€–xâ€– â‰  0 := fun h => hx (norm_eq_zero.1 h)
    -- âŠ¢ Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x ( â€¦
    have hyn : â€–yâ€– â‰  0 := fun h => hy (norm_eq_zero.1 h)
    -- âŠ¢ Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x ( â€¦
    have hxyn : â€–x - yâ€– â‰  0 := fun h => hxy (eq_of_sub_eq_zero (norm_eq_zero.1 h))
    -- âŠ¢ Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x ( â€¦
    apply mul_right_cancelâ‚€ hxn
    -- âŠ¢ (Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x  â€¦
    apply mul_right_cancelâ‚€ hyn
    -- âŠ¢ (Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x  â€¦
    apply mul_right_cancelâ‚€ hxyn
    -- âŠ¢ (Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x  â€¦
    apply mul_right_cancelâ‚€ hxyn
    -- âŠ¢ (Real.sin (angle x (x - y)) * (inner y (y - x) / (â€–yâ€– * â€–y - xâ€–)) + inner x  â€¦
    have H1 :
      Real.sin (angle x (x - y)) * (âŸªy, y - xâŸ« / (â€–yâ€– * â€–y - xâ€–)) * â€–xâ€– * â€–yâ€– * â€–x - yâ€– =
        Real.sin (angle x (x - y)) * (â€–xâ€– * â€–x - yâ€–) * (âŸªy, y - xâŸ« / (â€–yâ€– * â€–y - xâ€–)) * â€–yâ€– := by
      ring
    have H2 :
      âŸªx, x - yâŸ« / (â€–xâ€– * â€–y - xâ€–) * Real.sin (angle y (y - x)) * â€–xâ€– * â€–yâ€– * â€–y - xâ€– =
        âŸªx, x - yâŸ« / (â€–xâ€– * â€–y - xâ€–) * (Real.sin (angle y (y - x)) * (â€–yâ€– * â€–y - xâ€–)) * â€–xâ€– := by
      ring
    have H3 :
      âŸªx, xâŸ« * (âŸªx, xâŸ« - âŸªx, yâŸ« - (âŸªx, yâŸ« - âŸªy, yâŸ«)) - (âŸªx, xâŸ« - âŸªx, yâŸ«) * (âŸªx, xâŸ« - âŸªx, yâŸ«) =
        âŸªx, xâŸ« * âŸªy, yâŸ« - âŸªx, yâŸ« * âŸªx, yâŸ« := by
      ring
    have H4 :
      âŸªy, yâŸ« * (âŸªy, yâŸ« - âŸªx, yâŸ« - (âŸªx, yâŸ« - âŸªx, xâŸ«)) - (âŸªy, yâŸ« - âŸªx, yâŸ«) * (âŸªy, yâŸ« - âŸªx, yâŸ«) =
        âŸªx, xâŸ« * âŸªy, yâŸ« - âŸªx, yâŸ« * âŸªx, yâŸ« := by
      ring
    rw [right_distrib, right_distrib, right_distrib, right_distrib, H1, sin_angle_mul_norm_mul_norm,
      norm_sub_rev x y, H2, sin_angle_mul_norm_mul_norm, norm_sub_rev y x,
      mul_assoc (Real.sin (angle x y)), sin_angle_mul_norm_mul_norm, inner_sub_left, inner_sub_left,
      inner_sub_right, inner_sub_right, inner_sub_right, inner_sub_right, real_inner_comm x y, H3,
      H4, real_inner_self_eq_norm_mul_norm, real_inner_self_eq_norm_mul_norm,
      real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two]
    field_simp [hxn, hyn, hxyn]
    -- âŠ¢ (Real.sqrt (â€–xâ€– * â€–xâ€– * (â€–yâ€– * â€–yâ€–) * (2 * 2) - (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - â€–x  â€¦
    ring
    -- ğŸ‰ no goals
#align inner_product_geometry.sin_angle_sub_add_angle_sub_rev_eq_sin_angle InnerProductGeometry.sin_angle_sub_add_angle_sub_rev_eq_sin_angle

/-- The cosine of the sum of the angles of a possibly degenerate
triangle (where two given sides are nonzero), vector angle form. -/
theorem cos_angle_add_angle_sub_add_angle_sub_eq_neg_one {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    Real.cos (angle x y + angle x (x - y) + angle y (y - x)) = -1 := by
  rw [add_assoc, Real.cos_add, cos_angle_sub_add_angle_sub_rev_eq_neg_cos_angle hx hy,
    sin_angle_sub_add_angle_sub_rev_eq_sin_angle hx hy, mul_neg, â† neg_add', add_comm, â† sq, â† sq,
    Real.sin_sq_add_cos_sq]
#align inner_product_geometry.cos_angle_add_angle_sub_add_angle_sub_eq_neg_one InnerProductGeometry.cos_angle_add_angle_sub_add_angle_sub_eq_neg_one

/-- The sine of the sum of the angles of a possibly degenerate
triangle (where two given sides are nonzero), vector angle form. -/
theorem sin_angle_add_angle_sub_add_angle_sub_eq_zero {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    Real.sin (angle x y + angle x (x - y) + angle y (y - x)) = 0 := by
  rw [add_assoc, Real.sin_add, cos_angle_sub_add_angle_sub_rev_eq_neg_cos_angle hx hy,
    sin_angle_sub_add_angle_sub_rev_eq_sin_angle hx hy]
  ring
  -- ğŸ‰ no goals
#align inner_product_geometry.sin_angle_add_angle_sub_add_angle_sub_eq_zero InnerProductGeometry.sin_angle_add_angle_sub_add_angle_sub_eq_zero

/-- The sum of the angles of a possibly degenerate triangle (where the
two given sides are nonzero), vector angle form. -/
theorem angle_add_angle_sub_add_angle_sub_eq_pi {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    angle x y + angle x (x - y) + angle y (y - x) = Ï€ := by
  have hcos := cos_angle_add_angle_sub_add_angle_sub_eq_neg_one hx hy
  -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
  have hsin := sin_angle_add_angle_sub_add_angle_sub_eq_zero hx hy
  -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
  rw [Real.sin_eq_zero_iff] at hsin
  -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
  cases' hsin with n hn
  -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
  symm at hn
  -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
  have h0 : 0 â‰¤ angle x y + angle x (x - y) + angle y (y - x) :=
    add_nonneg (add_nonneg (angle_nonneg _ _) (angle_nonneg _ _)) (angle_nonneg _ _)
  have h3lt : angle x y + angle x (x - y) + angle y (y - x) < Ï€ + Ï€ + Ï€ := by
    by_contra hnlt
    have hxy : angle x y = Ï€ := by
      by_contra hxy
      exact hnlt (add_lt_add_of_lt_of_le (add_lt_add_of_lt_of_le (lt_of_le_of_ne
        (angle_le_pi _ _) hxy) (angle_le_pi _ _)) (angle_le_pi _ _))
    rw [hxy] at hnlt
    rw [angle_eq_pi_iff] at hxy
    rcases hxy with âŸ¨hx, âŸ¨r, âŸ¨hr, hxrâŸ©âŸ©âŸ©
    rw [hxr, â† one_smul â„ x, â† mul_smul, mul_one, â† sub_smul, one_smul, sub_eq_add_neg,
      angle_smul_right_of_pos _ _ (add_pos zero_lt_one (neg_pos_of_neg hr)), angle_self hx,
      add_zero] at hnlt
    apply hnlt
    rw [add_assoc]
    exact add_lt_add_left (lt_of_le_of_lt (angle_le_pi _ _) (lt_add_of_pos_right Ï€ Real.pi_pos)) _
  have hn0 : 0 â‰¤ n := by
    rw [hn, mul_nonneg_iff_left_nonneg_of_pos Real.pi_pos] at h0
    norm_cast at h0
  have hn3 : n < 3 := by
    rw [hn, show Ï€ + Ï€ + Ï€ = 3 * Ï€ by ring] at h3lt
    replace h3lt := lt_of_mul_lt_mul_right h3lt (le_of_lt Real.pi_pos)
    norm_cast at h3lt
  interval_cases n
  Â· rw [hn] at hcos
    -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
    simp at hcos
    -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
    norm_num at hcos
    -- ğŸ‰ no goals
  Â· rw [hn]
    -- âŠ¢ â†‘1 * Ï€ = Ï€
    norm_num
    -- ğŸ‰ no goals
  Â· rw [hn] at hcos
    -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
    simp at hcos
    -- âŠ¢ angle x y + angle x (x - y) + angle y (y - x) = Ï€
    norm_num at hcos
    -- ğŸ‰ no goals
#align inner_product_geometry.angle_add_angle_sub_add_angle_sub_eq_pi InnerProductGeometry.angle_add_angle_sub_add_angle_sub_eq_pi

end InnerProductGeometry

namespace EuclideanGeometry

/-!
### Geometrical results on triangles in Euclidean affine spaces

This section develops some geometrical definitions and results on
(possibly degenerate) triangles in Euclidean affine spaces.
-/


open InnerProductGeometry

open scoped EuclideanGeometry

variable {V : Type*} {P : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [MetricSpace P]
  [NormedAddTorsor V P]

/-- **Law of cosines** (cosine rule), angle-at-point form. -/
theorem dist_sq_eq_dist_sq_add_dist_sq_sub_two_mul_dist_mul_dist_mul_cos_angle (p1 p2 p3 : P) :
    dist p1 p3 * dist p1 p3 = dist p1 p2 * dist p1 p2 + dist p3 p2 * dist p3 p2 -
      2 * dist p1 p2 * dist p3 p2 * Real.cos (âˆ  p1 p2 p3) := by
  rw [dist_eq_norm_vsub V p1 p3, dist_eq_norm_vsub V p1 p2, dist_eq_norm_vsub V p3 p2]
  -- âŠ¢ â€–p1 -áµ¥ p3â€– * â€–p1 -áµ¥ p3â€– = â€–p1 -áµ¥ p2â€– * â€–p1 -áµ¥ p2â€– + â€–p3 -áµ¥ p2â€– * â€–p3 -áµ¥ p2â€–  â€¦
  unfold angle
  -- âŠ¢ â€–p1 -áµ¥ p3â€– * â€–p1 -áµ¥ p3â€– = â€–p1 -áµ¥ p2â€– * â€–p1 -áµ¥ p2â€– + â€–p3 -áµ¥ p2â€– * â€–p3 -áµ¥ p2â€–  â€¦
  convert norm_sub_sq_eq_norm_sq_add_norm_sq_sub_two_mul_norm_mul_norm_mul_cos_angle
    (p1 -áµ¥ p2 : V) (p3 -áµ¥ p2 : V)
  Â· exact (vsub_sub_vsub_cancel_right p1 p3 p2).symm
    -- ğŸ‰ no goals
  Â· exact (vsub_sub_vsub_cancel_right p1 p3 p2).symm
    -- ğŸ‰ no goals
#align euclidean_geometry.dist_sq_eq_dist_sq_add_dist_sq_sub_two_mul_dist_mul_dist_mul_cos_angle EuclideanGeometry.dist_sq_eq_dist_sq_add_dist_sq_sub_two_mul_dist_mul_dist_mul_cos_angle

alias law_cos := dist_sq_eq_dist_sq_add_dist_sq_sub_two_mul_dist_mul_dist_mul_cos_angle
#align euclidean_geometry.law_cos EuclideanGeometry.law_cos

/-- **Isosceles Triangle Theorem**: Pons asinorum, angle-at-point form. -/
theorem angle_eq_angle_of_dist_eq {p1 p2 p3 : P} (h : dist p1 p2 = dist p1 p3) :
    âˆ  p1 p2 p3 = âˆ  p1 p3 p2 := by
  rw [dist_eq_norm_vsub V p1 p2, dist_eq_norm_vsub V p1 p3] at h
  -- âŠ¢ âˆ  p1 p2 p3 = âˆ  p1 p3 p2
  unfold angle
  -- âŠ¢ InnerProductGeometry.angle (p1 -áµ¥ p2) (p3 -áµ¥ p2) = InnerProductGeometry.angl â€¦
  convert angle_sub_eq_angle_sub_rev_of_norm_eq h
  -- âŠ¢ p3 -áµ¥ p2 = p1 -áµ¥ p2 - (p1 -áµ¥ p3)
  Â· exact (vsub_sub_vsub_cancel_left p3 p2 p1).symm
    -- ğŸ‰ no goals
  Â· exact (vsub_sub_vsub_cancel_left p2 p3 p1).symm
    -- ğŸ‰ no goals
#align euclidean_geometry.angle_eq_angle_of_dist_eq EuclideanGeometry.angle_eq_angle_of_dist_eq

/-- Converse of pons asinorum, angle-at-point form. -/
theorem dist_eq_of_angle_eq_angle_of_angle_ne_pi {p1 p2 p3 : P} (h : âˆ  p1 p2 p3 = âˆ  p1 p3 p2)
    (hpi : âˆ  p2 p1 p3 â‰  Ï€) : dist p1 p2 = dist p1 p3 := by
  unfold angle at h hpi
  -- âŠ¢ dist p1 p2 = dist p1 p3
  rw [dist_eq_norm_vsub V p1 p2, dist_eq_norm_vsub V p1 p3]
  -- âŠ¢ â€–p1 -áµ¥ p2â€– = â€–p1 -áµ¥ p3â€–
  rw [â† angle_neg_neg, neg_vsub_eq_vsub_rev, neg_vsub_eq_vsub_rev] at hpi
  -- âŠ¢ â€–p1 -áµ¥ p2â€– = â€–p1 -áµ¥ p3â€–
  rw [â† vsub_sub_vsub_cancel_left p3 p2 p1, â† vsub_sub_vsub_cancel_left p2 p3 p1] at h
  -- âŠ¢ â€–p1 -áµ¥ p2â€– = â€–p1 -áµ¥ p3â€–
  exact norm_eq_of_angle_sub_eq_angle_sub_rev_of_angle_ne_pi h hpi
  -- ğŸ‰ no goals
#align euclidean_geometry.dist_eq_of_angle_eq_angle_of_angle_ne_pi EuclideanGeometry.dist_eq_of_angle_eq_angle_of_angle_ne_pi

/-- The **sum of the angles of a triangle** (possibly degenerate, where the
given vertex is distinct from the others), angle-at-point. -/
theorem angle_add_angle_add_angle_eq_pi {p1 p2 p3 : P} (h2 : p2 â‰  p1) (h3 : p3 â‰  p1) :
    âˆ  p1 p2 p3 + âˆ  p2 p3 p1 + âˆ  p3 p1 p2 = Ï€ := by
  rw [add_assoc, add_comm, add_comm (âˆ  p2 p3 p1), angle_comm p2 p3 p1]
  -- âŠ¢ âˆ  p3 p1 p2 + âˆ  p1 p3 p2 + âˆ  p1 p2 p3 = Ï€
  unfold angle
  -- âŠ¢ InnerProductGeometry.angle (p3 -áµ¥ p1) (p2 -áµ¥ p1) + InnerProductGeometry.angl â€¦
  rw [â† angle_neg_neg (p1 -áµ¥ p3), â† angle_neg_neg (p1 -áµ¥ p2), neg_vsub_eq_vsub_rev,
    neg_vsub_eq_vsub_rev, neg_vsub_eq_vsub_rev, neg_vsub_eq_vsub_rev, â†
    vsub_sub_vsub_cancel_right p3 p2 p1, â† vsub_sub_vsub_cancel_right p2 p3 p1]
  exact angle_add_angle_sub_add_angle_sub_eq_pi (fun he => h3 (vsub_eq_zero_iff_eq.1 he)) fun he =>
    h2 (vsub_eq_zero_iff_eq.1 he)
#align euclidean_geometry.angle_add_angle_add_angle_eq_pi EuclideanGeometry.angle_add_angle_add_angle_eq_pi

/-- The **sum of the angles of a triangle** (possibly degenerate, where the triangle is a line),
oriented angles at point. -/
theorem oangle_add_oangle_add_oangle_eq_pi [Module.Oriented â„ V (Fin 2)]
    [Fact (FiniteDimensional.finrank â„ V = 2)] {p1 p2 p3 : P} (h21 : p2 â‰  p1) (h32 : p3 â‰  p2)
    (h13 : p1 â‰  p3) : âˆ¡ p1 p2 p3 + âˆ¡ p2 p3 p1 + âˆ¡ p3 p1 p2 = Ï€ := by
  simpa only [neg_vsub_eq_vsub_rev] using
    positiveOrientation.oangle_add_cyc3_neg_left (vsub_ne_zero.mpr h21) (vsub_ne_zero.mpr h32)
      (vsub_ne_zero.mpr h13)
#align euclidean_geometry.oangle_add_oangle_add_oangle_eq_pi EuclideanGeometry.oangle_add_oangle_add_oangle_eq_pi

/-- **Stewart's Theorem**. -/
theorem dist_sq_mul_dist_add_dist_sq_mul_dist (a b c p : P) (h : âˆ  b p c = Ï€) :
    dist a b ^ 2 * dist c p + dist a c ^ 2 * dist b p =
    dist b c * (dist a p ^ 2 + dist b p * dist c p) := by
  rw [pow_two, pow_two, law_cos a p b, law_cos a p c,
    eq_sub_of_add_eq (angle_add_angle_eq_pi_of_angle_eq_pi a h), Real.cos_pi_sub,
    dist_eq_add_dist_of_angle_eq_pi h]
  ring
  -- ğŸ‰ no goals
#align euclidean_geometry.dist_sq_mul_dist_add_dist_sq_mul_dist EuclideanGeometry.dist_sq_mul_dist_add_dist_sq_mul_dist

/-- **Apollonius's Theorem**. -/
theorem dist_sq_add_dist_sq_eq_two_mul_dist_midpoint_sq_add_half_dist_sq (a b c : P) :
    dist a b ^ 2 + dist a c ^ 2 = 2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c / 2) ^ 2) := by
  by_cases hbc : b = c
  -- âŠ¢ dist a b ^ 2 + dist a c ^ 2 = â†‘2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c  â€¦
  Â· simp [hbc, midpoint_self, dist_self, two_mul]
    -- ğŸ‰ no goals
  Â· let m := midpoint â„ b c
    -- âŠ¢ dist a b ^ 2 + dist a c ^ 2 = â†‘2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c  â€¦
    have : dist b c â‰  0 := (dist_pos.mpr hbc).ne'
    -- âŠ¢ dist a b ^ 2 + dist a c ^ 2 = â†‘2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c  â€¦
    have hm := dist_sq_mul_dist_add_dist_sq_mul_dist a b c m (angle_midpoint_eq_pi b c hbc)
    -- âŠ¢ dist a b ^ 2 + dist a c ^ 2 = â†‘2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c  â€¦
    simp only [dist_left_midpoint, dist_right_midpoint, Real.norm_two] at hm
    -- âŠ¢ dist a b ^ 2 + dist a c ^ 2 = â†‘2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c  â€¦
    calc
      dist a b ^ 2 + dist a c ^ 2 = 2 / dist b c * (dist a b ^ 2 *
        ((2:â„)â»Â¹ * dist b c) + dist a c ^ 2 * (2â»Â¹ * dist b c)) := by field_simp; ring
      _ = 2 * (dist a (midpoint â„ b c) ^ 2 + (dist b c / 2) ^ 2) := by rw [hm]; field_simp; ring
#align euclidean_geometry.dist_sq_add_dist_sq_eq_two_mul_dist_midpoint_sq_add_half_dist_sq EuclideanGeometry.dist_sq_add_dist_sq_eq_two_mul_dist_midpoint_sq_add_half_dist_sq

theorem dist_mul_of_eq_angle_of_dist_mul (a b c a' b' c' : P) (r : â„) (h : âˆ  a' b' c' = âˆ  a b c)
    (hab : dist a' b' = r * dist a b) (hcb : dist c' b' = r * dist c b) :
    dist a' c' = r * dist a c := by
  have h' : dist a' c' ^ 2 = (r * dist a c) ^ 2
  -- âŠ¢ dist a' c' ^ 2 = (r * dist a c) ^ 2
  calc
    dist a' c' ^ 2 =
        dist a' b' ^ 2 + dist c' b' ^ 2 - 2 * dist a' b' * dist c' b' * Real.cos (âˆ  a' b' c') := by
      simp [pow_two, law_cos a' b' c']
    _ = r ^ 2 * (dist a b ^ 2 + dist c b ^ 2 - 2 * dist a b * dist c b * Real.cos (âˆ  a b c)) := by
      rw [h, hab, hcb]; ring
    _ = (r * dist a c) ^ 2 := by simp [pow_two, â† law_cos a b c, mul_pow]; ring
  by_cases habâ‚ : a = b
  -- âŠ¢ dist a' c' = r * dist a c
  Â· have hab'â‚ : a' = b' := by
      rw [â† dist_eq_zero, hab, dist_eq_zero.mpr habâ‚, mul_zero r]
    rw [habâ‚, hab'â‚, dist_comm b' c', dist_comm b c, hcb]
    -- ğŸ‰ no goals
  Â· have h1 : 0 â‰¤ r * dist a b := by rw [â† hab]; exact dist_nonneg
    -- âŠ¢ dist a' c' = r * dist a c
    have h2 : 0 â‰¤ r := nonneg_of_mul_nonneg_left h1 (dist_pos.mpr habâ‚)
    -- âŠ¢ dist a' c' = r * dist a c
    exact (sq_eq_sq dist_nonneg (mul_nonneg h2 dist_nonneg)).mp h'
    -- ğŸ‰ no goals
#align euclidean_geometry.dist_mul_of_eq_angle_of_dist_mul EuclideanGeometry.dist_mul_of_eq_angle_of_dist_mul

end EuclideanGeometry
