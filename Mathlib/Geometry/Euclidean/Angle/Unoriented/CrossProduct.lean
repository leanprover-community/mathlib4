/-
Copyright (c) 2020 Vedant Gupta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vedant Gupta, Thomas Browning, Eric Wieser
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Geometry.Euclidean.Angle.Unoriented.Basic
import Mathlib.LinearAlgebra.CrossProduct

/-!
# Norm of cross-products

This file proves `InnerProductGeometry.norm_withLpEquiv_crossProduct`, relating the norm of the
cross-product of two real vectors with their individual norms.
-/

open Real
open Matrix

namespace InnerProductGeometry

open scoped RealInnerProductSpace

/-- The L2 norm of the cross product of two real vectors (of type `EuclideanSpace â„ (Fin 3)`)
equals the product of their individual norms times the sine of the angle between them. -/
theorem norm_withLpEquiv_crossProduct (a b : EuclideanSpace â„ (Fin 3)) :
    â€–(WithLp.equiv 2 (Fin 3 â†’ â„)).symm (WithLp.equiv _ _ a Ã—â‚ƒ WithLp.equiv _ _ b)â€– =
    â€–aâ€– * â€–bâ€– * sin (angle a b) := by
  have := sin_angle_nonneg a b
  refine sq_eq_sqâ‚€ (by positivity) (by positivity) |>.mp ?_
  trans â€–aâ€–^2 * â€–bâ€–^2 - âŸªa, bâŸ« ^ 2
  Â· simp_rw [norm_sq_eq_re_inner (ğ•œ := â„), EuclideanSpace.inner_eq_star_dotProduct, star_trivial,
      RCLike.re_to_real, Equiv.apply_symm_apply, cross_dot_cross,
      dotProduct_comm (WithLp.equiv _ _ b) (WithLp.equiv _ _ a), sq]
  Â· linear_combination (â€–aâ€– * â€–bâ€–) ^ 2 * (sin_sq_add_cos_sq (angle a b)).symm +
      congrArg (Â· ^ 2) (cos_angle_mul_norm_mul_norm a b)

/-- The L2 norm of the cross product of two real vectors (of type `Fin 3 â†’ R`) equals the product
of their individual L2 norms times the sine of the angle between them. -/
theorem norm_withLpEquiv_symm_crossProduct (a b : Fin 3 â†’ â„) :
    â€–(WithLp.equiv 2 (Fin 3 â†’ â„)).symm (a Ã—â‚ƒ b)â€– =
    â€–(WithLp.equiv 2 (Fin 3 â†’ â„)).symm aâ€– * â€–(WithLp.equiv 2 (Fin 3 â†’ â„)).symm bâ€– *
      sin (angle ((WithLp.equiv 2 (Fin 3 â†’ â„)).symm a) ((WithLp.equiv 2 (Fin 3 â†’ â„)).symm b)) := by
  rw [â† norm_withLpEquiv_crossProduct ((WithLp.equiv _ _).symm a) ((WithLp.equiv _ _).symm b)]
  simp

end InnerProductGeometry
