/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, Manuel Candales
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Analysis.InnerProductSpace.Subspace
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Inverse
import Mathlib.LinearAlgebra.CrossProduct

/-!
# Angles between vectors

This file defines unoriented angles in real inner product spaces.

## Main definitions

* `InnerProductGeometry.angle` is the undirected angle between two vectors.

## TODO

Prove the triangle inequality for the angle.
-/


assert_not_exists HasFDerivAt ConformalAt

noncomputable section

open Real Set

open Real

open RealInnerProductSpace

namespace InnerProductGeometry

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ‚Ñù V] {x y : V}

/-- The undirected angle between two vectors. If either vector is 0,
this is œÄ/2. See `Orientation.oangle` for the corresponding oriented angle
definition. -/
def angle (x y : V) : ‚Ñù :=
  Real.arccos (‚ü™x, y‚ü´ / (‚Äñx‚Äñ * ‚Äñy‚Äñ))

theorem continuousAt_angle {x : V √ó V} (hx1 : x.1 ‚â† 0) (hx2 : x.2 ‚â† 0) :
    ContinuousAt (fun y : V √ó V => angle y.1 y.2) x :=
  Real.continuous_arccos.continuousAt.comp <|
    continuous_inner.continuousAt.div
      ((continuous_norm.comp continuous_fst).mul (continuous_norm.comp continuous_snd)).continuousAt
      (by simp [hx1, hx2])

theorem angle_smul_smul {c : ‚Ñù} (hc : c ‚â† 0) (x y : V) : angle (c ‚Ä¢ x) (c ‚Ä¢ y) = angle x y := by
  have : c * c ‚â† 0 := mul_ne_zero hc hc
  rw [angle, angle, real_inner_smul_left, inner_smul_right, norm_smul, norm_smul, Real.norm_eq_abs,
    mul_mul_mul_comm _ ‚Äñx‚Äñ, abs_mul_abs_self, ‚Üê mul_assoc c c, mul_div_mul_left _ _ this]

@[simp]
theorem _root_.LinearIsometry.angle_map {E F : Type*} [NormedAddCommGroup E] [NormedAddCommGroup F]
    [InnerProductSpace ‚Ñù E] [InnerProductSpace ‚Ñù F] (f : E ‚Üí‚Çó·µ¢[‚Ñù] F) (u v : E) :
    angle (f u) (f v) = angle u v := by
  rw [angle, angle, f.inner_map_map, f.norm_map, f.norm_map]

@[simp, norm_cast]
theorem _root_.Submodule.angle_coe {s : Submodule ‚Ñù V} (x y : s) :
    angle (x : V) (y : V) = angle x y :=
  s.subtype‚Çó·µ¢.angle_map x y

/-- The cosine of the angle between two vectors. -/
theorem cos_angle (x y : V) : Real.cos (angle x y) = ‚ü™x, y‚ü´ / (‚Äñx‚Äñ * ‚Äñy‚Äñ) :=
  Real.cos_arccos (abs_le.mp (abs_real_inner_div_norm_mul_norm_le_one x y)).1
    (abs_le.mp (abs_real_inner_div_norm_mul_norm_le_one x y)).2

/-- The angle between two vectors does not depend on their order. -/
theorem angle_comm (x y : V) : angle x y = angle y x := by
  unfold angle
  rw [real_inner_comm, mul_comm]

/-- The angle between the negation of two vectors. -/
@[simp]
theorem angle_neg_neg (x y : V) : angle (-x) (-y) = angle x y := by
  unfold angle
  rw [inner_neg_neg, norm_neg, norm_neg]

/-- The angle between two vectors is nonnegative. -/
theorem angle_nonneg (x y : V) : 0 ‚â§ angle x y :=
  Real.arccos_nonneg _

/-- The angle between two vectors is at most œÄ. -/
theorem angle_le_pi (x y : V) : angle x y ‚â§ œÄ :=
  Real.arccos_le_pi _

/-- The sine of the angle between two vectors is nonnegative. -/
theorem sin_angle_nonneg : 0 ‚â§ sin (angle x y) :=
  sin_nonneg_of_nonneg_of_le_pi (angle_nonneg x y) (angle_le_pi x y)

/-- The angle between a vector and the negation of another vector. -/
theorem angle_neg_right (x y : V) : angle x (-y) = œÄ - angle x y := by
  unfold angle
  rw [‚Üê Real.arccos_neg, norm_neg, inner_neg_right, neg_div]

/-- The angle between the negation of a vector and another vector. -/
theorem angle_neg_left (x y : V) : angle (-x) y = œÄ - angle x y := by
  rw [‚Üê angle_neg_neg, neg_neg, angle_neg_right]

proof_wanted angle_triangle (x y z : V) : angle x z ‚â§ angle x y + angle y z

/-- The angle between the zero vector and a vector. -/
@[simp]
theorem angle_zero_left (x : V) : angle 0 x = œÄ / 2 := by
  unfold angle
  rw [inner_zero_left, zero_div, Real.arccos_zero]

/-- The angle between a vector and the zero vector. -/
@[simp]
theorem angle_zero_right (x : V) : angle x 0 = œÄ / 2 := by
  unfold angle
  rw [inner_zero_right, zero_div, Real.arccos_zero]

/-- The angle between a nonzero vector and itself. -/
@[simp]
theorem angle_self {x : V} (hx : x ‚â† 0) : angle x x = 0 := by
  unfold angle
  rw [‚Üê real_inner_self_eq_norm_mul_norm, div_self (inner_self_ne_zero.2 hx : ‚ü™x, x‚ü´ ‚â† 0),
    Real.arccos_one]

/-- The angle between a nonzero vector and its negation. -/
@[simp]
theorem angle_self_neg_of_nonzero {x : V} (hx : x ‚â† 0) : angle x (-x) = œÄ := by
  rw [angle_neg_right, angle_self hx, sub_zero]

/-- The angle between the negation of a nonzero vector and that
vector. -/
@[simp]
theorem angle_neg_self_of_nonzero {x : V} (hx : x ‚â† 0) : angle (-x) x = œÄ := by
  rw [angle_comm, angle_self_neg_of_nonzero hx]

/-- The angle between a vector and a positive multiple of a vector. -/
@[simp]
theorem angle_smul_right_of_pos (x y : V) {r : ‚Ñù} (hr : 0 < r) : angle x (r ‚Ä¢ y) = angle x y := by
  unfold angle
  rw [inner_smul_right, norm_smul, Real.norm_eq_abs, abs_of_nonneg (le_of_lt hr), ‚Üê mul_assoc,
    mul_comm _ r, mul_assoc, mul_div_mul_left _ _ (ne_of_gt hr)]

/-- The angle between a positive multiple of a vector and a vector. -/
@[simp]
theorem angle_smul_left_of_pos (x y : V) {r : ‚Ñù} (hr : 0 < r) : angle (r ‚Ä¢ x) y = angle x y := by
  rw [angle_comm, angle_smul_right_of_pos y x hr, angle_comm]

/-- The angle between a vector and a negative multiple of a vector. -/
@[simp]
theorem angle_smul_right_of_neg (x y : V) {r : ‚Ñù} (hr : r < 0) :
    angle x (r ‚Ä¢ y) = angle x (-y) := by
  rw [‚Üê neg_neg r, neg_smul, angle_neg_right, angle_smul_right_of_pos x y (neg_pos_of_neg hr),
    angle_neg_right]

/-- The angle between a negative multiple of a vector and a vector. -/
@[simp]
theorem angle_smul_left_of_neg (x y : V) {r : ‚Ñù} (hr : r < 0) : angle (r ‚Ä¢ x) y = angle (-x) y := by
  rw [angle_comm, angle_smul_right_of_neg y x hr, angle_comm]

/-- The cosine of the angle between two vectors, multiplied by the
product of their norms. -/
theorem cos_angle_mul_norm_mul_norm (x y : V) : Real.cos (angle x y) * (‚Äñx‚Äñ * ‚Äñy‚Äñ) = ‚ü™x, y‚ü´ := by
  rw [cos_angle, div_mul_cancel_of_imp]
  simp +contextual [or_imp]

/-- The sine of the angle between two vectors, multiplied by the
product of their norms. -/
theorem sin_angle_mul_norm_mul_norm (x y : V) :
    Real.sin (angle x y) * (‚Äñx‚Äñ * ‚Äñy‚Äñ) = ‚àö(‚ü™x, x‚ü´ * ‚ü™y, y‚ü´ - ‚ü™x, y‚ü´ * ‚ü™x, y‚ü´) := by
  unfold angle
  rw [Real.sin_arccos, ‚Üê Real.sqrt_mul_self (mul_nonneg (norm_nonneg x) (norm_nonneg y)),
    ‚Üê Real.sqrt_mul' _ (mul_self_nonneg _), sq,
    Real.sqrt_mul_self (mul_nonneg (norm_nonneg x) (norm_nonneg y)),
    real_inner_self_eq_norm_mul_norm, real_inner_self_eq_norm_mul_norm]
  by_cases h : ‚Äñx‚Äñ * ‚Äñy‚Äñ = 0
  ¬∑ rw [show ‚Äñx‚Äñ * ‚Äñx‚Äñ * (‚Äñy‚Äñ * ‚Äñy‚Äñ) = ‚Äñx‚Äñ * ‚Äñy‚Äñ * (‚Äñx‚Äñ * ‚Äñy‚Äñ) by ring, h, mul_zero,
      mul_zero, zero_sub]
    cases' eq_zero_or_eq_zero_of_mul_eq_zero h with hx hy
    ¬∑ rw [norm_eq_zero] at hx
      rw [hx, inner_zero_left, zero_mul, neg_zero]
    ¬∑ rw [norm_eq_zero] at hy
      rw [hy, inner_zero_right, zero_mul, neg_zero]
  ¬∑ -- takes 600ms; squeezing the "equivalent" simp call yields an invalid result
    field_simp [h]
    ring_nf

/-- The angle between two vectors is zero if and only if they are
nonzero and one is a positive multiple of the other. -/
theorem angle_eq_zero_iff {x y : V} : angle x y = 0 ‚Üî x ‚â† 0 ‚àß ‚àÉ r : ‚Ñù, 0 < r ‚àß y = r ‚Ä¢ x := by
  rw [angle, ‚Üê real_inner_div_norm_mul_norm_eq_one_iff, Real.arccos_eq_zero, LE.le.le_iff_eq,
    eq_comm]
  exact (abs_le.mp (abs_real_inner_div_norm_mul_norm_le_one x y)).2

/-- The angle between two vectors is œÄ if and only if they are nonzero
and one is a negative multiple of the other. -/
theorem angle_eq_pi_iff {x y : V} : angle x y = œÄ ‚Üî x ‚â† 0 ‚àß ‚àÉ r : ‚Ñù, r < 0 ‚àß y = r ‚Ä¢ x := by
  rw [angle, ‚Üê real_inner_div_norm_mul_norm_eq_neg_one_iff, Real.arccos_eq_pi, LE.le.le_iff_eq]
  exact (abs_le.mp (abs_real_inner_div_norm_mul_norm_le_one x y)).1

/-- If the angle between two vectors is œÄ, the angles between those
vectors and a third vector add to œÄ. -/
theorem angle_add_angle_eq_pi_of_angle_eq_pi {x y : V} (z : V) (h : angle x y = œÄ) :
    angle x z + angle y z = œÄ := by
  rcases angle_eq_pi_iff.1 h with ‚ü®_, ‚ü®r, ‚ü®hr, rfl‚ü©‚ü©‚ü©
  rw [angle_smul_left_of_neg x z hr, angle_neg_left, add_sub_cancel]

/-- Two vectors have inner product 0 if and only if the angle between
them is œÄ/2. -/
theorem inner_eq_zero_iff_angle_eq_pi_div_two (x y : V) : ‚ü™x, y‚ü´ = 0 ‚Üî angle x y = œÄ / 2 :=
  Iff.symm <| by simp +contextual [angle, or_imp]

/-- If the angle between two vectors is œÄ, the inner product equals the negative product
of the norms. -/
theorem inner_eq_neg_mul_norm_of_angle_eq_pi {x y : V} (h : angle x y = œÄ) :
    ‚ü™x, y‚ü´ = -(‚Äñx‚Äñ * ‚Äñy‚Äñ) := by
  simp [‚Üê cos_angle_mul_norm_mul_norm, h]

/-- If the angle between two vectors is 0, the inner product equals the product of the norms. -/
theorem inner_eq_mul_norm_of_angle_eq_zero {x y : V} (h : angle x y = 0) : ‚ü™x, y‚ü´ = ‚Äñx‚Äñ * ‚Äñy‚Äñ := by
  simp [‚Üê cos_angle_mul_norm_mul_norm, h]

/-- The inner product of two non-zero vectors equals the negative product of their norms
if and only if the angle between the two vectors is œÄ. -/
theorem inner_eq_neg_mul_norm_iff_angle_eq_pi {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚ü™x, y‚ü´ = -(‚Äñx‚Äñ * ‚Äñy‚Äñ) ‚Üî angle x y = œÄ := by
  refine ‚ü®fun h => ?_, inner_eq_neg_mul_norm_of_angle_eq_pi‚ü©
  have h‚ÇÅ : ‚Äñx‚Äñ * ‚Äñy‚Äñ ‚â† 0 := (mul_pos (norm_pos_iff.mpr hx) (norm_pos_iff.mpr hy)).ne'
  rw [angle, h, neg_div, div_self h‚ÇÅ, Real.arccos_neg_one]

/-- The inner product of two non-zero vectors equals the product of their norms
if and only if the angle between the two vectors is 0. -/
theorem inner_eq_mul_norm_iff_angle_eq_zero {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚ü™x, y‚ü´ = ‚Äñx‚Äñ * ‚Äñy‚Äñ ‚Üî angle x y = 0 := by
  refine ‚ü®fun h => ?_, inner_eq_mul_norm_of_angle_eq_zero‚ü©
  have h‚ÇÅ : ‚Äñx‚Äñ * ‚Äñy‚Äñ ‚â† 0 := (mul_pos (norm_pos_iff.mpr hx) (norm_pos_iff.mpr hy)).ne'
  rw [angle, h, div_self h‚ÇÅ, Real.arccos_one]

/-- If the angle between two vectors is œÄ, the norm of their difference equals
the sum of their norms. -/
theorem norm_sub_eq_add_norm_of_angle_eq_pi {x y : V} (h : angle x y = œÄ) :
    ‚Äñx - y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ := by
  rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg (x - y)) (add_nonneg (norm_nonneg x) (norm_nonneg y)),
    norm_sub_pow_two_real, inner_eq_neg_mul_norm_of_angle_eq_pi h]
  ring

/-- If the angle between two vectors is 0, the norm of their sum equals
the sum of their norms. -/
theorem norm_add_eq_add_norm_of_angle_eq_zero {x y : V} (h : angle x y = 0) :
    ‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ := by
  rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg (x + y)) (add_nonneg (norm_nonneg x) (norm_nonneg y)),
    norm_add_pow_two_real, inner_eq_mul_norm_of_angle_eq_zero h]
  ring

/-- If the angle between two vectors is 0, the norm of their difference equals
the absolute value of the difference of their norms. -/
theorem norm_sub_eq_abs_sub_norm_of_angle_eq_zero {x y : V} (h : angle x y = 0) :
    ‚Äñx - y‚Äñ = |‚Äñx‚Äñ - ‚Äñy‚Äñ| := by
  rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg (x - y)) (abs_nonneg (‚Äñx‚Äñ - ‚Äñy‚Äñ)), norm_sub_pow_two_real,
    inner_eq_mul_norm_of_angle_eq_zero h, sq_abs (‚Äñx‚Äñ - ‚Äñy‚Äñ)]
  ring

/-- The norm of the difference of two non-zero vectors equals the sum of their norms
if and only the angle between the two vectors is œÄ. -/
theorem norm_sub_eq_add_norm_iff_angle_eq_pi {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚Äñx - y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ ‚Üî angle x y = œÄ := by
  refine ‚ü®fun h => ?_, norm_sub_eq_add_norm_of_angle_eq_pi‚ü©
  rw [‚Üê inner_eq_neg_mul_norm_iff_angle_eq_pi hx hy]
  obtain ‚ü®hxy‚ÇÅ, hxy‚ÇÇ‚ü© := norm_nonneg (x - y), add_nonneg (norm_nonneg x) (norm_nonneg y)
  rw [‚Üê sq_eq_sq‚ÇÄ hxy‚ÇÅ hxy‚ÇÇ, norm_sub_pow_two_real] at h
  calc
    ‚ü™x, y‚ü´ = (‚Äñx‚Äñ ^ 2 + ‚Äñy‚Äñ ^ 2 - (‚Äñx‚Äñ + ‚Äñy‚Äñ) ^ 2) / 2 := by linarith
    _ = -(‚Äñx‚Äñ * ‚Äñy‚Äñ) := by ring

/-- The norm of the sum of two non-zero vectors equals the sum of their norms
if and only the angle between the two vectors is 0. -/
theorem norm_add_eq_add_norm_iff_angle_eq_zero {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ ‚Üî angle x y = 0 := by
  refine ‚ü®fun h => ?_, norm_add_eq_add_norm_of_angle_eq_zero‚ü©
  rw [‚Üê inner_eq_mul_norm_iff_angle_eq_zero hx hy]
  obtain ‚ü®hxy‚ÇÅ, hxy‚ÇÇ‚ü© := norm_nonneg (x + y), add_nonneg (norm_nonneg x) (norm_nonneg y)
  rw [‚Üê sq_eq_sq‚ÇÄ hxy‚ÇÅ hxy‚ÇÇ, norm_add_pow_two_real] at h
  calc
    ‚ü™x, y‚ü´ = ((‚Äñx‚Äñ + ‚Äñy‚Äñ) ^ 2 - ‚Äñx‚Äñ ^ 2 - ‚Äñy‚Äñ ^ 2) / 2 := by linarith
    _ = ‚Äñx‚Äñ * ‚Äñy‚Äñ := by ring

/-- The norm of the difference of two non-zero vectors equals the absolute value
of the difference of their norms if and only the angle between the two vectors is 0. -/
theorem norm_sub_eq_abs_sub_norm_iff_angle_eq_zero {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚Äñx - y‚Äñ = |‚Äñx‚Äñ - ‚Äñy‚Äñ| ‚Üî angle x y = 0 := by
  refine ‚ü®fun h => ?_, norm_sub_eq_abs_sub_norm_of_angle_eq_zero‚ü©
  rw [‚Üê inner_eq_mul_norm_iff_angle_eq_zero hx hy]
  have h1 : ‚Äñx - y‚Äñ ^ 2 = (‚Äñx‚Äñ - ‚Äñy‚Äñ) ^ 2 := by
    rw [h]
    exact sq_abs (‚Äñx‚Äñ - ‚Äñy‚Äñ)
  rw [norm_sub_pow_two_real] at h1
  calc
    ‚ü™x, y‚ü´ = ((‚Äñx‚Äñ + ‚Äñy‚Äñ) ^ 2 - ‚Äñx‚Äñ ^ 2 - ‚Äñy‚Äñ ^ 2) / 2 := by linarith
    _ = ‚Äñx‚Äñ * ‚Äñy‚Äñ := by ring

/-- The norm of the sum of two vectors equals the norm of their difference if and only if
the angle between them is œÄ/2. -/
theorem norm_add_eq_norm_sub_iff_angle_eq_pi_div_two (x y : V) :
    ‚Äñx + y‚Äñ = ‚Äñx - y‚Äñ ‚Üî angle x y = œÄ / 2 := by
  rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg (x + y)) (norm_nonneg (x - y)),
    ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two x y, norm_add_pow_two_real, norm_sub_pow_two_real]
  constructor <;> intro h <;> linarith

/-- The cosine of the angle between two vectors is 1 if and only if the angle is 0. -/
theorem cos_eq_one_iff_angle_eq_zero : cos (angle x y) = 1 ‚Üî angle x y = 0 := by
  rw [‚Üê cos_zero]
  exact injOn_cos.eq_iff ‚ü®angle_nonneg x y, angle_le_pi x y‚ü© (left_mem_Icc.2 pi_pos.le)

/-- The cosine of the angle between two vectors is 0 if and only if the angle is œÄ / 2. -/
theorem cos_eq_zero_iff_angle_eq_pi_div_two : cos (angle x y) = 0 ‚Üî angle x y = œÄ / 2 := by
  rw [‚Üê cos_pi_div_two]
  apply injOn_cos.eq_iff ‚ü®angle_nonneg x y, angle_le_pi x y‚ü©
  constructor <;> linarith [pi_pos]

/-- The cosine of the angle between two vectors is -1 if and only if the angle is œÄ. -/
theorem cos_eq_neg_one_iff_angle_eq_pi : cos (angle x y) = -1 ‚Üî angle x y = œÄ := by
  rw [‚Üê cos_pi]
  exact injOn_cos.eq_iff ‚ü®angle_nonneg x y, angle_le_pi x y‚ü© (right_mem_Icc.2 pi_pos.le)

/-- The sine of the angle between two vectors is 0 if and only if the angle is 0 or œÄ. -/
theorem sin_eq_zero_iff_angle_eq_zero_or_angle_eq_pi :
    sin (angle x y) = 0 ‚Üî angle x y = 0 ‚à® angle x y = œÄ := by
  rw [sin_eq_zero_iff_cos_eq, cos_eq_one_iff_angle_eq_zero, cos_eq_neg_one_iff_angle_eq_pi]

/-- The sine of the angle between two vectors is 1 if and only if the angle is œÄ / 2. -/
theorem sin_eq_one_iff_angle_eq_pi_div_two : sin (angle x y) = 1 ‚Üî angle x y = œÄ / 2 := by
  refine ‚ü®fun h => ?_, fun h => by rw [h, sin_pi_div_two]‚ü©
  rw [‚Üê cos_eq_zero_iff_angle_eq_pi_div_two, ‚Üê abs_eq_zero, abs_cos_eq_sqrt_one_sub_sin_sq, h]
  simp

open Matrix

/-- Returns the l2 norm of a vector. This function is necessary to make sure the l2 norm is taken
for members of `Fin 3 ‚Üí ‚Ñù` instead of the sup norm-/
noncomputable def l2Norm (v : EuclideanSpace ‚Ñù (Fin 3)) : ‚Ñù :=
  ‚Äñv‚Äñ

/- The norm of the cross product of two real vectors equals the product of their individual norms
  times the sine of the angle between them. -/
theorem crossProduct_norm_eq_norm_mul_norm_mul_sin (a b : EuclideanSpace ‚Ñù (Fin 3)) :
    l2Norm (a √ó‚ÇÉ b) = ‚Äña‚Äñ * ‚Äñb‚Äñ * sin (angle a b) := by
  have h_lhs_nonneg : 0 ‚â§ l2Norm (a √ó‚ÇÉ b) := norm_nonneg _
  have h_rhs_nonneg : 0 ‚â§ ‚Äña‚Äñ * ‚Äñb‚Äñ * Real.sin (angle a b) :=
    mul_nonneg (mul_nonneg (norm_nonneg _) (norm_nonneg _)) (sin_angle_nonneg)
  have h_norm_sq_eq_inner (v : EuclideanSpace ‚Ñù (Fin 3)) : (‚Äñv‚Äñ ^ 2 = v ‚¨ù·µ• v) :=
    norm_sq_eq_inner (ùïú := ‚Ñù) v
  have dotProduct_eq_inner (v w : EuclideanSpace ‚Ñù (Fin 3)) : v ‚¨ù·µ• w = inner v w := rfl
  simp_rw [‚Üê sq_eq_sq‚ÇÄ h_lhs_nonneg h_rhs_nonneg, l2Norm, h_norm_sq_eq_inner, cross_dot_cross,
      ‚Üê h_norm_sq_eq_inner, dotProduct_comm b a, dotProduct_eq_inner]
  linear_combination (‚Äña‚Äñ * ‚Äñb‚Äñ) ^ 2 * (sin_sq_add_cos_sq (angle a b)).symm +
    congrArg (¬∑ ^ 2) (cos_angle_mul_norm_mul_norm a b)

end InnerProductGeometry