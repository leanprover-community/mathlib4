/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Arctan
import Mathlib.Geometry.Euclidean.Angle.Unoriented.Affine

#align_import geometry.euclidean.angle.unoriented.right_angle from "leanprover-community/mathlib"@"46b633fd842bef9469441c0209906f6dddd2b4f5"

/-!
# Right-angled triangles

This file proves basic geometrical results about distances and angles in (possibly degenerate)
right-angled triangles in real inner product spaces and Euclidean affine spaces.

## Implementation notes

Results in this file are generally given in a form with only those non-degeneracy conditions
needed for the particular result, rather than requiring affine independence of the points of a
triangle unnecessarily.

## References

* https://en.wikipedia.org/wiki/Pythagorean_theorem

-/


noncomputable section

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

open scoped BigOperators

open scoped EuclideanGeometry

open scoped Real

open scoped RealInnerProductSpace

namespace InnerProductGeometry

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ‚Ñù V]

/-- Pythagorean theorem, if-and-only-if vector angle form. -/
theorem norm_add_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two (x y : V) :
    ‚Äñx + y‚Äñ * ‚Äñx + y‚Äñ = ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ ‚Üî angle x y = œÄ / 2 := by
  rw [norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero]
  -- ‚ä¢ inner x y = 0 ‚Üî angle x y = œÄ / 2
  exact inner_eq_zero_iff_angle_eq_pi_div_two x y
  -- üéâ no goals
#align inner_product_geometry.norm_add_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two InnerProductGeometry.norm_add_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two

/-- Pythagorean theorem, vector angle form. -/
theorem norm_add_sq_eq_norm_sq_add_norm_sq' (x y : V) (h : angle x y = œÄ / 2) :
    ‚Äñx + y‚Äñ * ‚Äñx + y‚Äñ = ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ :=
  (norm_add_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two x y).2 h
#align inner_product_geometry.norm_add_sq_eq_norm_sq_add_norm_sq' InnerProductGeometry.norm_add_sq_eq_norm_sq_add_norm_sq'

/-- Pythagorean theorem, subtracting vectors, if-and-only-if vector angle form. -/
theorem norm_sub_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two (x y : V) :
    ‚Äñx - y‚Äñ * ‚Äñx - y‚Äñ = ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ ‚Üî angle x y = œÄ / 2 := by
  rw [norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero]
  -- ‚ä¢ inner x y = 0 ‚Üî angle x y = œÄ / 2
  exact inner_eq_zero_iff_angle_eq_pi_div_two x y
  -- üéâ no goals
#align inner_product_geometry.norm_sub_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two InnerProductGeometry.norm_sub_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two

/-- Pythagorean theorem, subtracting vectors, vector angle form. -/
theorem norm_sub_sq_eq_norm_sq_add_norm_sq' (x y : V) (h : angle x y = œÄ / 2) :
    ‚Äñx - y‚Äñ * ‚Äñx - y‚Äñ = ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ :=
  (norm_sub_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two x y).2 h
#align inner_product_geometry.norm_sub_sq_eq_norm_sq_add_norm_sq' InnerProductGeometry.norm_sub_sq_eq_norm_sq_add_norm_sq'

/-- An angle in a right-angled triangle expressed using `arccos`. -/
theorem angle_add_eq_arccos_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    angle x (x + y) = Real.arccos (‚Äñx‚Äñ / ‚Äñx + y‚Äñ) := by
  rw [angle, inner_add_right, h, add_zero, real_inner_self_eq_norm_mul_norm]
  -- ‚ä¢ Real.arccos (‚Äñx‚Äñ * ‚Äñx‚Äñ / (‚Äñx‚Äñ * ‚Äñx + y‚Äñ)) = Real.arccos (‚Äñx‚Äñ / ‚Äñx + y‚Äñ)
  by_cases hx : ‚Äñx‚Äñ = 0; ¬∑ simp [hx]
  -- ‚ä¢ Real.arccos (‚Äñx‚Äñ * ‚Äñx‚Äñ / (‚Äñx‚Äñ * ‚Äñx + y‚Äñ)) = Real.arccos (‚Äñx‚Äñ / ‚Äñx + y‚Äñ)
                           -- üéâ no goals
  rw [div_mul_eq_div_div, mul_self_div_self]
  -- üéâ no goals
#align inner_product_geometry.angle_add_eq_arccos_of_inner_eq_zero InnerProductGeometry.angle_add_eq_arccos_of_inner_eq_zero

/-- An angle in a right-angled triangle expressed using `arcsin`. -/
theorem angle_add_eq_arcsin_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y ‚â† 0) :
    angle x (x + y) = Real.arcsin (‚Äñy‚Äñ / ‚Äñx + y‚Äñ) := by
  have hxy : ‚Äñx + y‚Äñ ^ 2 ‚â† 0 := by
    rw [pow_two, norm_add_sq_eq_norm_sq_add_norm_sq_real h, ne_comm]
    refine' ne_of_lt _
    rcases h0 with (h0 | h0)
    ¬∑ exact
        Left.add_pos_of_pos_of_nonneg (mul_self_pos.2 (norm_ne_zero_iff.2 h0)) (mul_self_nonneg _)
    ¬∑ exact
        Left.add_pos_of_nonneg_of_pos (mul_self_nonneg _) (mul_self_pos.2 (norm_ne_zero_iff.2 h0))
  rw [angle_add_eq_arccos_of_inner_eq_zero h,
    Real.arccos_eq_arcsin (div_nonneg (norm_nonneg _) (norm_nonneg _)), div_pow, one_sub_div hxy]
  nth_rw 1 [pow_two]
  -- ‚ä¢ Real.arcsin (Real.sqrt ((‚Äñx + y‚Äñ * ‚Äñx + y‚Äñ - ‚Äñx‚Äñ ^ 2) / ‚Äñx + y‚Äñ ^ 2)) = Real ‚Ä¶
  rw [norm_add_sq_eq_norm_sq_add_norm_sq_real h, pow_two, add_sub_cancel', ‚Üê pow_two, ‚Üê div_pow,
    Real.sqrt_sq (div_nonneg (norm_nonneg _) (norm_nonneg _))]
#align inner_product_geometry.angle_add_eq_arcsin_of_inner_eq_zero InnerProductGeometry.angle_add_eq_arcsin_of_inner_eq_zero

/-- An angle in a right-angled triangle expressed using `arctan`. -/
theorem angle_add_eq_arctan_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0) :
    angle x (x + y) = Real.arctan (‚Äñy‚Äñ / ‚Äñx‚Äñ) := by
  rw [angle_add_eq_arcsin_of_inner_eq_zero h (Or.inl h0), Real.arctan_eq_arcsin, ‚Üê
    div_mul_eq_div_div, norm_add_eq_sqrt_iff_real_inner_eq_zero.2 h]
  nth_rw 3 [‚Üê Real.sqrt_sq (norm_nonneg x)]
  -- ‚ä¢ Real.arcsin (‚Äñy‚Äñ / Real.sqrt (‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ)) = Real.arcsin (‚Äñy‚Äñ / ( ‚Ä¶
  rw_mod_cast [‚Üê Real.sqrt_mul (sq_nonneg _), div_pow, pow_two, pow_two, mul_add, mul_one, mul_div,
    mul_comm (‚Äñx‚Äñ * ‚Äñx‚Äñ), ‚Üê mul_div, div_self (mul_self_pos.2 (norm_ne_zero_iff.2 h0)).ne', mul_one]
#align inner_product_geometry.angle_add_eq_arctan_of_inner_eq_zero InnerProductGeometry.angle_add_eq_arctan_of_inner_eq_zero

/-- An angle in a non-degenerate right-angled triangle is positive. -/
theorem angle_add_pos_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x = 0 ‚à® y ‚â† 0) :
    0 < angle x (x + y) := by
  rw [angle_add_eq_arccos_of_inner_eq_zero h, Real.arccos_pos,
    norm_add_eq_sqrt_iff_real_inner_eq_zero.2 h]
  by_cases hx : x = 0; ¬∑ simp [hx]
  -- ‚ä¢ ‚Äñx‚Äñ / Real.sqrt (‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ) < 1
                         -- üéâ no goals
  rw [div_lt_one (Real.sqrt_pos.2 (Left.add_pos_of_pos_of_nonneg (mul_self_pos.2
    (norm_ne_zero_iff.2 hx)) (mul_self_nonneg _))), Real.lt_sqrt (norm_nonneg _), pow_two]
  simpa [hx] using h0
  -- üéâ no goals
#align inner_product_geometry.angle_add_pos_of_inner_eq_zero InnerProductGeometry.angle_add_pos_of_inner_eq_zero

/-- An angle in a right-angled triangle is at most `œÄ / 2`. -/
theorem angle_add_le_pi_div_two_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    angle x (x + y) ‚â§ œÄ / 2 := by
  rw [angle_add_eq_arccos_of_inner_eq_zero h, Real.arccos_le_pi_div_two]
  -- ‚ä¢ 0 ‚â§ ‚Äñx‚Äñ / ‚Äñx + y‚Äñ
  exact div_nonneg (norm_nonneg _) (norm_nonneg _)
  -- üéâ no goals
#align inner_product_geometry.angle_add_le_pi_div_two_of_inner_eq_zero InnerProductGeometry.angle_add_le_pi_div_two_of_inner_eq_zero

/-- An angle in a non-degenerate right-angled triangle is less than `œÄ / 2`. -/
theorem angle_add_lt_pi_div_two_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0) :
    angle x (x + y) < œÄ / 2 := by
  rw [angle_add_eq_arccos_of_inner_eq_zero h, Real.arccos_lt_pi_div_two,
    norm_add_eq_sqrt_iff_real_inner_eq_zero.2 h]
  exact div_pos (norm_pos_iff.2 h0) (Real.sqrt_pos.2 (Left.add_pos_of_pos_of_nonneg
    (mul_self_pos.2 (norm_ne_zero_iff.2 h0)) (mul_self_nonneg _)))
#align inner_product_geometry.angle_add_lt_pi_div_two_of_inner_eq_zero InnerProductGeometry.angle_add_lt_pi_div_two_of_inner_eq_zero

/-- The cosine of an angle in a right-angled triangle as a ratio of sides. -/
theorem cos_angle_add_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.cos (angle x (x + y)) = ‚Äñx‚Äñ / ‚Äñx + y‚Äñ := by
  rw [angle_add_eq_arccos_of_inner_eq_zero h,
    Real.cos_arccos (le_trans (by norm_num) (div_nonneg (norm_nonneg _) (norm_nonneg _)))
      (div_le_one_of_le _ (norm_nonneg _))]
  rw [mul_self_le_mul_self_iff (norm_nonneg _) (norm_nonneg _),
    norm_add_sq_eq_norm_sq_add_norm_sq_real h]
  exact le_add_of_nonneg_right (mul_self_nonneg _)
  -- üéâ no goals
#align inner_product_geometry.cos_angle_add_of_inner_eq_zero InnerProductGeometry.cos_angle_add_of_inner_eq_zero

/-- The sine of an angle in a right-angled triangle as a ratio of sides. -/
theorem sin_angle_add_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y ‚â† 0) :
    Real.sin (angle x (x + y)) = ‚Äñy‚Äñ / ‚Äñx + y‚Äñ := by
  rw [angle_add_eq_arcsin_of_inner_eq_zero h h0,
    Real.sin_arcsin (le_trans (by norm_num) (div_nonneg (norm_nonneg _) (norm_nonneg _)))
      (div_le_one_of_le _ (norm_nonneg _))]
  rw [mul_self_le_mul_self_iff (norm_nonneg _) (norm_nonneg _),
    norm_add_sq_eq_norm_sq_add_norm_sq_real h]
  exact le_add_of_nonneg_left (mul_self_nonneg _)
  -- üéâ no goals
#align inner_product_geometry.sin_angle_add_of_inner_eq_zero InnerProductGeometry.sin_angle_add_of_inner_eq_zero

/-- The tangent of an angle in a right-angled triangle as a ratio of sides. -/
theorem tan_angle_add_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.tan (angle x (x + y)) = ‚Äñy‚Äñ / ‚Äñx‚Äñ := by
  by_cases h0 : x = 0; ¬∑ simp [h0]
  -- ‚ä¢ Real.tan (angle x (x + y)) = ‚Äñy‚Äñ / ‚Äñx‚Äñ
                         -- üéâ no goals
  rw [angle_add_eq_arctan_of_inner_eq_zero h h0, Real.tan_arctan]
  -- üéâ no goals
#align inner_product_geometry.tan_angle_add_of_inner_eq_zero InnerProductGeometry.tan_angle_add_of_inner_eq_zero

/-- The cosine of an angle in a right-angled triangle multiplied by the hypotenuse equals the
adjacent side. -/
theorem cos_angle_add_mul_norm_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.cos (angle x (x + y)) * ‚Äñx + y‚Äñ = ‚Äñx‚Äñ := by
  rw [cos_angle_add_of_inner_eq_zero h]
  -- ‚ä¢ ‚Äñx‚Äñ / ‚Äñx + y‚Äñ * ‚Äñx + y‚Äñ = ‚Äñx‚Äñ
  by_cases hxy : ‚Äñx + y‚Äñ = 0
  -- ‚ä¢ ‚Äñx‚Äñ / ‚Äñx + y‚Äñ * ‚Äñx + y‚Äñ = ‚Äñx‚Äñ
  ¬∑ have h' := norm_add_sq_eq_norm_sq_add_norm_sq_real h
    -- ‚ä¢ ‚Äñx‚Äñ / ‚Äñx + y‚Äñ * ‚Äñx + y‚Äñ = ‚Äñx‚Äñ
    rw [hxy, zero_mul, eq_comm,
      add_eq_zero_iff' (mul_self_nonneg ‚Äñx‚Äñ) (mul_self_nonneg ‚Äñy‚Äñ), mul_self_eq_zero] at h'
    simp [h'.1]
    -- üéâ no goals
  ¬∑ exact div_mul_cancel _ hxy
    -- üéâ no goals
#align inner_product_geometry.cos_angle_add_mul_norm_of_inner_eq_zero InnerProductGeometry.cos_angle_add_mul_norm_of_inner_eq_zero

/-- The sine of an angle in a right-angled triangle multiplied by the hypotenuse equals the
opposite side. -/
theorem sin_angle_add_mul_norm_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.sin (angle x (x + y)) * ‚Äñx + y‚Äñ = ‚Äñy‚Äñ := by
  by_cases h0 : x = 0 ‚àß y = 0; ¬∑ simp [h0]
  -- ‚ä¢ Real.sin (angle x (x + y)) * ‚Äñx + y‚Äñ = ‚Äñy‚Äñ
                                 -- üéâ no goals
  rw [not_and_or] at h0
  -- ‚ä¢ Real.sin (angle x (x + y)) * ‚Äñx + y‚Äñ = ‚Äñy‚Äñ
  rw [sin_angle_add_of_inner_eq_zero h h0, div_mul_cancel]
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â† 0
  rw [‚Üê mul_self_ne_zero, norm_add_sq_eq_norm_sq_add_norm_sq_real h]
  -- ‚ä¢ ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ ‚â† 0
  refine' (ne_of_lt _).symm
  -- ‚ä¢ 0 < ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ
  rcases h0 with (h0 | h0)
  -- ‚ä¢ 0 < ‚Äñx‚Äñ * ‚Äñx‚Äñ + ‚Äñy‚Äñ * ‚Äñy‚Äñ
  ¬∑ exact Left.add_pos_of_pos_of_nonneg (mul_self_pos.2 (norm_ne_zero_iff.2 h0)) (mul_self_nonneg _)
    -- üéâ no goals
  ¬∑ exact Left.add_pos_of_nonneg_of_pos (mul_self_nonneg _) (mul_self_pos.2 (norm_ne_zero_iff.2 h0))
    -- üéâ no goals
#align inner_product_geometry.sin_angle_add_mul_norm_of_inner_eq_zero InnerProductGeometry.sin_angle_add_mul_norm_of_inner_eq_zero

/-- The tangent of an angle in a right-angled triangle multiplied by the adjacent side equals
the opposite side. -/
theorem tan_angle_add_mul_norm_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y = 0) :
    Real.tan (angle x (x + y)) * ‚Äñx‚Äñ = ‚Äñy‚Äñ := by
  rw [tan_angle_add_of_inner_eq_zero h]
  -- ‚ä¢ ‚Äñy‚Äñ / ‚Äñx‚Äñ * ‚Äñx‚Äñ = ‚Äñy‚Äñ
  rcases h0 with (h0 | h0) <;> simp [h0]
  -- ‚ä¢ ‚Äñy‚Äñ / ‚Äñx‚Äñ * ‚Äñx‚Äñ = ‚Äñy‚Äñ
                               -- üéâ no goals
                               -- üéâ no goals
#align inner_product_geometry.tan_angle_add_mul_norm_of_inner_eq_zero InnerProductGeometry.tan_angle_add_mul_norm_of_inner_eq_zero

/-- A side of a right-angled triangle divided by the cosine of the adjacent angle equals the
hypotenuse. -/
theorem norm_div_cos_angle_add_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y = 0) :
    ‚Äñx‚Äñ / Real.cos (angle x (x + y)) = ‚Äñx + y‚Äñ := by
  rw [cos_angle_add_of_inner_eq_zero h]
  -- ‚ä¢ ‚Äñx‚Äñ / (‚Äñx‚Äñ / ‚Äñx + y‚Äñ) = ‚Äñx + y‚Äñ
  rcases h0 with (h0 | h0)
  -- ‚ä¢ ‚Äñx‚Äñ / (‚Äñx‚Äñ / ‚Äñx + y‚Äñ) = ‚Äñx + y‚Äñ
  ¬∑ rw [div_div_eq_mul_div, mul_comm, div_eq_mul_inv, mul_inv_cancel_right‚ÇÄ (norm_ne_zero_iff.2 h0)]
    -- üéâ no goals
  ¬∑ simp [h0]
    -- üéâ no goals
#align inner_product_geometry.norm_div_cos_angle_add_of_inner_eq_zero InnerProductGeometry.norm_div_cos_angle_add_of_inner_eq_zero

/-- A side of a right-angled triangle divided by the sine of the opposite angle equals the
hypotenuse. -/
theorem norm_div_sin_angle_add_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x = 0 ‚à® y ‚â† 0) :
    ‚Äñy‚Äñ / Real.sin (angle x (x + y)) = ‚Äñx + y‚Äñ := by
  rcases h0 with (h0 | h0); ¬∑ simp [h0]
  -- ‚ä¢ ‚Äñy‚Äñ / Real.sin (angle x (x + y)) = ‚Äñx + y‚Äñ
                              -- üéâ no goals
  rw [sin_angle_add_of_inner_eq_zero h (Or.inr h0), div_div_eq_mul_div, mul_comm, div_eq_mul_inv,
    mul_inv_cancel_right‚ÇÄ (norm_ne_zero_iff.2 h0)]
#align inner_product_geometry.norm_div_sin_angle_add_of_inner_eq_zero InnerProductGeometry.norm_div_sin_angle_add_of_inner_eq_zero

/-- A side of a right-angled triangle divided by the tangent of the opposite angle equals the
adjacent side. -/
theorem norm_div_tan_angle_add_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x = 0 ‚à® y ‚â† 0) :
    ‚Äñy‚Äñ / Real.tan (angle x (x + y)) = ‚Äñx‚Äñ := by
  rw [tan_angle_add_of_inner_eq_zero h]
  -- ‚ä¢ ‚Äñy‚Äñ / (‚Äñy‚Äñ / ‚Äñx‚Äñ) = ‚Äñx‚Äñ
  rcases h0 with (h0 | h0)
  -- ‚ä¢ ‚Äñy‚Äñ / (‚Äñy‚Äñ / ‚Äñx‚Äñ) = ‚Äñx‚Äñ
  ¬∑ simp [h0]
    -- üéâ no goals
  ¬∑ rw [div_div_eq_mul_div, mul_comm, div_eq_mul_inv, mul_inv_cancel_right‚ÇÄ (norm_ne_zero_iff.2 h0)]
    -- üéâ no goals
#align inner_product_geometry.norm_div_tan_angle_add_of_inner_eq_zero InnerProductGeometry.norm_div_tan_angle_add_of_inner_eq_zero

/-- An angle in a right-angled triangle expressed using `arccos`, version subtracting vectors. -/
theorem angle_sub_eq_arccos_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    angle x (x - y) = Real.arccos (‚Äñx‚Äñ / ‚Äñx - y‚Äñ) := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ angle x (x - y) = Real.arccos (‚Äñx‚Äñ / ‚Äñx - y‚Äñ)
  rw [sub_eq_add_neg, angle_add_eq_arccos_of_inner_eq_zero h]
  -- üéâ no goals
#align inner_product_geometry.angle_sub_eq_arccos_of_inner_eq_zero InnerProductGeometry.angle_sub_eq_arccos_of_inner_eq_zero

/-- An angle in a right-angled triangle expressed using `arcsin`, version subtracting vectors. -/
theorem angle_sub_eq_arcsin_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y ‚â† 0) :
    angle x (x - y) = Real.arcsin (‚Äñy‚Äñ / ‚Äñx - y‚Äñ) := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ angle x (x - y) = Real.arcsin (‚Äñy‚Äñ / ‚Äñx - y‚Äñ)
  rw [or_comm, ‚Üê neg_ne_zero, or_comm] at h0
  -- ‚ä¢ angle x (x - y) = Real.arcsin (‚Äñy‚Äñ / ‚Äñx - y‚Äñ)
  rw [sub_eq_add_neg, angle_add_eq_arcsin_of_inner_eq_zero h h0, norm_neg]
  -- üéâ no goals
#align inner_product_geometry.angle_sub_eq_arcsin_of_inner_eq_zero InnerProductGeometry.angle_sub_eq_arcsin_of_inner_eq_zero

/-- An angle in a right-angled triangle expressed using `arctan`, version subtracting vectors. -/
theorem angle_sub_eq_arctan_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0) :
    angle x (x - y) = Real.arctan (‚Äñy‚Äñ / ‚Äñx‚Äñ) := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ angle x (x - y) = Real.arctan (‚Äñy‚Äñ / ‚Äñx‚Äñ)
  rw [sub_eq_add_neg, angle_add_eq_arctan_of_inner_eq_zero h h0, norm_neg]
  -- üéâ no goals
#align inner_product_geometry.angle_sub_eq_arctan_of_inner_eq_zero InnerProductGeometry.angle_sub_eq_arctan_of_inner_eq_zero

/-- An angle in a non-degenerate right-angled triangle is positive, version subtracting
vectors. -/
theorem angle_sub_pos_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x = 0 ‚à® y ‚â† 0) :
    0 < angle x (x - y) := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ 0 < angle x (x - y)
  rw [‚Üê neg_ne_zero] at h0
  -- ‚ä¢ 0 < angle x (x - y)
  rw [sub_eq_add_neg]
  -- ‚ä¢ 0 < angle x (x + -y)
  exact angle_add_pos_of_inner_eq_zero h h0
  -- üéâ no goals
#align inner_product_geometry.angle_sub_pos_of_inner_eq_zero InnerProductGeometry.angle_sub_pos_of_inner_eq_zero

/-- An angle in a right-angled triangle is at most `œÄ / 2`, version subtracting vectors. -/
theorem angle_sub_le_pi_div_two_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    angle x (x - y) ‚â§ œÄ / 2 := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ angle x (x - y) ‚â§ œÄ / 2
  rw [sub_eq_add_neg]
  -- ‚ä¢ angle x (x + -y) ‚â§ œÄ / 2
  exact angle_add_le_pi_div_two_of_inner_eq_zero h
  -- üéâ no goals
#align inner_product_geometry.angle_sub_le_pi_div_two_of_inner_eq_zero InnerProductGeometry.angle_sub_le_pi_div_two_of_inner_eq_zero

/-- An angle in a non-degenerate right-angled triangle is less than `œÄ / 2`, version subtracting
vectors. -/
theorem angle_sub_lt_pi_div_two_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0) :
    angle x (x - y) < œÄ / 2 := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ angle x (x - y) < œÄ / 2
  rw [sub_eq_add_neg]
  -- ‚ä¢ angle x (x + -y) < œÄ / 2
  exact angle_add_lt_pi_div_two_of_inner_eq_zero h h0
  -- üéâ no goals
#align inner_product_geometry.angle_sub_lt_pi_div_two_of_inner_eq_zero InnerProductGeometry.angle_sub_lt_pi_div_two_of_inner_eq_zero

/-- The cosine of an angle in a right-angled triangle as a ratio of sides, version subtracting
vectors. -/
theorem cos_angle_sub_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.cos (angle x (x - y)) = ‚Äñx‚Äñ / ‚Äñx - y‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ Real.cos (angle x (x - y)) = ‚Äñx‚Äñ / ‚Äñx - y‚Äñ
  rw [sub_eq_add_neg, cos_angle_add_of_inner_eq_zero h]
  -- üéâ no goals
#align inner_product_geometry.cos_angle_sub_of_inner_eq_zero InnerProductGeometry.cos_angle_sub_of_inner_eq_zero

/-- The sine of an angle in a right-angled triangle as a ratio of sides, version subtracting
vectors. -/
theorem sin_angle_sub_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y ‚â† 0) :
    Real.sin (angle x (x - y)) = ‚Äñy‚Äñ / ‚Äñx - y‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ Real.sin (angle x (x - y)) = ‚Äñy‚Äñ / ‚Äñx - y‚Äñ
  rw [or_comm, ‚Üê neg_ne_zero, or_comm] at h0
  -- ‚ä¢ Real.sin (angle x (x - y)) = ‚Äñy‚Äñ / ‚Äñx - y‚Äñ
  rw [sub_eq_add_neg, sin_angle_add_of_inner_eq_zero h h0, norm_neg]
  -- üéâ no goals
#align inner_product_geometry.sin_angle_sub_of_inner_eq_zero InnerProductGeometry.sin_angle_sub_of_inner_eq_zero

/-- The tangent of an angle in a right-angled triangle as a ratio of sides, version subtracting
vectors. -/
theorem tan_angle_sub_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.tan (angle x (x - y)) = ‚Äñy‚Äñ / ‚Äñx‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ Real.tan (angle x (x - y)) = ‚Äñy‚Äñ / ‚Äñx‚Äñ
  rw [sub_eq_add_neg, tan_angle_add_of_inner_eq_zero h, norm_neg]
  -- üéâ no goals
#align inner_product_geometry.tan_angle_sub_of_inner_eq_zero InnerProductGeometry.tan_angle_sub_of_inner_eq_zero

/-- The cosine of an angle in a right-angled triangle multiplied by the hypotenuse equals the
adjacent side, version subtracting vectors. -/
theorem cos_angle_sub_mul_norm_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.cos (angle x (x - y)) * ‚Äñx - y‚Äñ = ‚Äñx‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ Real.cos (angle x (x - y)) * ‚Äñx - y‚Äñ = ‚Äñx‚Äñ
  rw [sub_eq_add_neg, cos_angle_add_mul_norm_of_inner_eq_zero h]
  -- üéâ no goals
#align inner_product_geometry.cos_angle_sub_mul_norm_of_inner_eq_zero InnerProductGeometry.cos_angle_sub_mul_norm_of_inner_eq_zero

/-- The sine of an angle in a right-angled triangle multiplied by the hypotenuse equals the
opposite side, version subtracting vectors. -/
theorem sin_angle_sub_mul_norm_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) :
    Real.sin (angle x (x - y)) * ‚Äñx - y‚Äñ = ‚Äñy‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ Real.sin (angle x (x - y)) * ‚Äñx - y‚Äñ = ‚Äñy‚Äñ
  rw [sub_eq_add_neg, sin_angle_add_mul_norm_of_inner_eq_zero h, norm_neg]
  -- üéâ no goals
#align inner_product_geometry.sin_angle_sub_mul_norm_of_inner_eq_zero InnerProductGeometry.sin_angle_sub_mul_norm_of_inner_eq_zero

/-- The tangent of an angle in a right-angled triangle multiplied by the adjacent side equals
the opposite side, version subtracting vectors. -/
theorem tan_angle_sub_mul_norm_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y = 0) :
    Real.tan (angle x (x - y)) * ‚Äñx‚Äñ = ‚Äñy‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ Real.tan (angle x (x - y)) * ‚Äñx‚Äñ = ‚Äñy‚Äñ
  rw [‚Üê neg_eq_zero] at h0
  -- ‚ä¢ Real.tan (angle x (x - y)) * ‚Äñx‚Äñ = ‚Äñy‚Äñ
  rw [sub_eq_add_neg, tan_angle_add_mul_norm_of_inner_eq_zero h h0, norm_neg]
  -- üéâ no goals
#align inner_product_geometry.tan_angle_sub_mul_norm_of_inner_eq_zero InnerProductGeometry.tan_angle_sub_mul_norm_of_inner_eq_zero

/-- A side of a right-angled triangle divided by the cosine of the adjacent angle equals the
hypotenuse, version subtracting vectors. -/
theorem norm_div_cos_angle_sub_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x ‚â† 0 ‚à® y = 0) :
    ‚Äñx‚Äñ / Real.cos (angle x (x - y)) = ‚Äñx - y‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ ‚Äñx‚Äñ / Real.cos (angle x (x - y)) = ‚Äñx - y‚Äñ
  rw [‚Üê neg_eq_zero] at h0
  -- ‚ä¢ ‚Äñx‚Äñ / Real.cos (angle x (x - y)) = ‚Äñx - y‚Äñ
  rw [sub_eq_add_neg, norm_div_cos_angle_add_of_inner_eq_zero h h0]
  -- üéâ no goals
#align inner_product_geometry.norm_div_cos_angle_sub_of_inner_eq_zero InnerProductGeometry.norm_div_cos_angle_sub_of_inner_eq_zero

/-- A side of a right-angled triangle divided by the sine of the opposite angle equals the
hypotenuse, version subtracting vectors. -/
theorem norm_div_sin_angle_sub_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x = 0 ‚à® y ‚â† 0) :
    ‚Äñy‚Äñ / Real.sin (angle x (x - y)) = ‚Äñx - y‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ ‚Äñy‚Äñ / Real.sin (angle x (x - y)) = ‚Äñx - y‚Äñ
  rw [‚Üê neg_ne_zero] at h0
  -- ‚ä¢ ‚Äñy‚Äñ / Real.sin (angle x (x - y)) = ‚Äñx - y‚Äñ
  rw [sub_eq_add_neg, ‚Üê norm_neg, norm_div_sin_angle_add_of_inner_eq_zero h h0]
  -- üéâ no goals
#align inner_product_geometry.norm_div_sin_angle_sub_of_inner_eq_zero InnerProductGeometry.norm_div_sin_angle_sub_of_inner_eq_zero

/-- A side of a right-angled triangle divided by the tangent of the opposite angle equals the
adjacent side, version subtracting vectors. -/
theorem norm_div_tan_angle_sub_of_inner_eq_zero {x y : V} (h : ‚ü™x, y‚ü´ = 0) (h0 : x = 0 ‚à® y ‚â† 0) :
    ‚Äñy‚Äñ / Real.tan (angle x (x - y)) = ‚Äñx‚Äñ := by
  rw [‚Üê neg_eq_zero, ‚Üê inner_neg_right] at h
  -- ‚ä¢ ‚Äñy‚Äñ / Real.tan (angle x (x - y)) = ‚Äñx‚Äñ
  rw [‚Üê neg_ne_zero] at h0
  -- ‚ä¢ ‚Äñy‚Äñ / Real.tan (angle x (x - y)) = ‚Äñx‚Äñ
  rw [sub_eq_add_neg, ‚Üê norm_neg, norm_div_tan_angle_add_of_inner_eq_zero h h0]
  -- üéâ no goals
#align inner_product_geometry.norm_div_tan_angle_sub_of_inner_eq_zero InnerProductGeometry.norm_div_tan_angle_sub_of_inner_eq_zero

end InnerProductGeometry

namespace EuclideanGeometry

open InnerProductGeometry

variable {V : Type*} {P : Type*} [NormedAddCommGroup V] [InnerProductSpace ‚Ñù V] [MetricSpace P]
  [NormedAddTorsor V P]

/-- **Pythagorean theorem**, if-and-only-if angle-at-point form. -/
theorem dist_sq_eq_dist_sq_add_dist_sq_iff_angle_eq_pi_div_two (p1 p2 p3 : P) :
    dist p1 p3 * dist p1 p3 = dist p1 p2 * dist p1 p2 + dist p3 p2 * dist p3 p2 ‚Üî
      ‚à† p1 p2 p3 = œÄ / 2 := by
  erw [dist_comm p3 p2, dist_eq_norm_vsub V p1 p3, dist_eq_norm_vsub V p1 p2,
    dist_eq_norm_vsub V p2 p3, ‚Üê norm_sub_sq_eq_norm_sq_add_norm_sq_iff_angle_eq_pi_div_two,
    vsub_sub_vsub_cancel_right p1, ‚Üê neg_vsub_eq_vsub_rev p2 p3, norm_neg]
#align euclidean_geometry.dist_sq_eq_dist_sq_add_dist_sq_iff_angle_eq_pi_div_two EuclideanGeometry.dist_sq_eq_dist_sq_add_dist_sq_iff_angle_eq_pi_div_two

/-- An angle in a right-angled triangle expressed using `arccos`. -/
theorem angle_eq_arccos_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2) :
    ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ = Real.arccos (dist p‚ÇÉ p‚ÇÇ / dist p‚ÇÅ p‚ÇÉ) := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [angle, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, angle_add_eq_arccos_of_inner_eq_zero h]
#align euclidean_geometry.angle_eq_arccos_of_angle_eq_pi_div_two EuclideanGeometry.angle_eq_arccos_of_angle_eq_pi_div_two

/-- An angle in a right-angled triangle expressed using `arcsin`. -/
theorem angle_eq_arcsin_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ ‚â† p‚ÇÇ ‚à® p‚ÇÉ ‚â† p‚ÇÇ) : ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ = Real.arcsin (dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÅ p‚ÇÉ) := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [‚Üê @vsub_ne_zero V, @ne_comm _ p‚ÇÉ, ‚Üê @vsub_ne_zero V _ _ _ p‚ÇÇ, or_comm] at h0
  -- ‚ä¢ ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ = Real.arcsin (dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÅ p‚ÇÉ)
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, angle_add_eq_arcsin_of_inner_eq_zero h h0]
#align euclidean_geometry.angle_eq_arcsin_of_angle_eq_pi_div_two EuclideanGeometry.angle_eq_arcsin_of_angle_eq_pi_div_two

/-- An angle in a right-angled triangle expressed using `arctan`. -/
theorem angle_eq_arctan_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÉ ‚â† p‚ÇÇ) : ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ = Real.arctan (dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÉ p‚ÇÇ) := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [ne_comm, ‚Üê @vsub_ne_zero V] at h0
  -- ‚ä¢ ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ = Real.arctan (dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÉ p‚ÇÇ)
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, angle_add_eq_arctan_of_inner_eq_zero h h0]
#align euclidean_geometry.angle_eq_arctan_of_angle_eq_pi_div_two EuclideanGeometry.angle_eq_arctan_of_angle_eq_pi_div_two

/-- An angle in a non-degenerate right-angled triangle is positive. -/
theorem angle_pos_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ ‚â† p‚ÇÇ ‚à® p‚ÇÉ = p‚ÇÇ) : 0 < ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [‚Üê @vsub_ne_zero V, eq_comm, ‚Üê @vsub_eq_zero_iff_eq V, or_comm] at h0
  -- ‚ä¢ 0 < ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ
  rw [angle, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ, add_comm]
  -- ‚ä¢ 0 < InnerProductGeometry.angle (p‚ÇÇ -·µ• p‚ÇÉ) (p‚ÇÇ -·µ• p‚ÇÉ + (p‚ÇÅ -·µ• p‚ÇÇ))
  exact angle_add_pos_of_inner_eq_zero h h0
  -- üéâ no goals
#align euclidean_geometry.angle_pos_of_angle_eq_pi_div_two EuclideanGeometry.angle_pos_of_angle_eq_pi_div_two

/-- An angle in a right-angled triangle is at most `œÄ / 2`. -/
theorem angle_le_pi_div_two_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2) :
    ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ ‚â§ œÄ / 2 := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [angle, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ, add_comm]
  -- ‚ä¢ InnerProductGeometry.angle (p‚ÇÇ -·µ• p‚ÇÉ) (p‚ÇÇ -·µ• p‚ÇÉ + (p‚ÇÅ -·µ• p‚ÇÇ)) ‚â§ œÄ / 2
  exact angle_add_le_pi_div_two_of_inner_eq_zero h
  -- üéâ no goals
#align euclidean_geometry.angle_le_pi_div_two_of_angle_eq_pi_div_two EuclideanGeometry.angle_le_pi_div_two_of_angle_eq_pi_div_two

/-- An angle in a non-degenerate right-angled triangle is less than `œÄ / 2`. -/
theorem angle_lt_pi_div_two_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÉ ‚â† p‚ÇÇ) : ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ < œÄ / 2 := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [ne_comm, ‚Üê @vsub_ne_zero V] at h0
  -- ‚ä¢ ‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ < œÄ / 2
  rw [angle, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ, add_comm]
  -- ‚ä¢ InnerProductGeometry.angle (p‚ÇÇ -·µ• p‚ÇÉ) (p‚ÇÇ -·µ• p‚ÇÉ + (p‚ÇÅ -·µ• p‚ÇÇ)) < œÄ / 2
  exact angle_add_lt_pi_div_two_of_inner_eq_zero h h0
  -- üéâ no goals
#align euclidean_geometry.angle_lt_pi_div_two_of_angle_eq_pi_div_two EuclideanGeometry.angle_lt_pi_div_two_of_angle_eq_pi_div_two

/-- The cosine of an angle in a right-angled triangle as a ratio of sides. -/
theorem cos_angle_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2) :
    Real.cos (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÉ p‚ÇÇ / dist p‚ÇÅ p‚ÇÉ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [angle, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, cos_angle_add_of_inner_eq_zero h]
#align euclidean_geometry.cos_angle_of_angle_eq_pi_div_two EuclideanGeometry.cos_angle_of_angle_eq_pi_div_two

/-- The sine of an angle in a right-angled triangle as a ratio of sides. -/
theorem sin_angle_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ ‚â† p‚ÇÇ ‚à® p‚ÇÉ ‚â† p‚ÇÇ) : Real.sin (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÅ p‚ÇÉ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [‚Üê @vsub_ne_zero V, @ne_comm _ p‚ÇÉ, ‚Üê @vsub_ne_zero V _ _ _ p‚ÇÇ, or_comm] at h0
  -- ‚ä¢ Real.sin (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÅ p‚ÇÉ
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, sin_angle_add_of_inner_eq_zero h h0]
#align euclidean_geometry.sin_angle_of_angle_eq_pi_div_two EuclideanGeometry.sin_angle_of_angle_eq_pi_div_two

/-- The tangent of an angle in a right-angled triangle as a ratio of sides. -/
theorem tan_angle_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2) :
    Real.tan (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÇ / dist p‚ÇÉ p‚ÇÇ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, tan_angle_add_of_inner_eq_zero h]
#align euclidean_geometry.tan_angle_of_angle_eq_pi_div_two EuclideanGeometry.tan_angle_of_angle_eq_pi_div_two

/-- The cosine of an angle in a right-angled triangle multiplied by the hypotenuse equals the
adjacent side. -/
theorem cos_angle_mul_dist_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2) :
    Real.cos (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) * dist p‚ÇÅ p‚ÇÉ = dist p‚ÇÉ p‚ÇÇ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [angle, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, cos_angle_add_mul_norm_of_inner_eq_zero h]
#align euclidean_geometry.cos_angle_mul_dist_of_angle_eq_pi_div_two EuclideanGeometry.cos_angle_mul_dist_of_angle_eq_pi_div_two

/-- The sine of an angle in a right-angled triangle multiplied by the hypotenuse equals the
opposite side. -/
theorem sin_angle_mul_dist_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2) :
    Real.sin (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) * dist p‚ÇÅ p‚ÇÉ = dist p‚ÇÅ p‚ÇÇ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, sin_angle_add_mul_norm_of_inner_eq_zero h]
#align euclidean_geometry.sin_angle_mul_dist_of_angle_eq_pi_div_two EuclideanGeometry.sin_angle_mul_dist_of_angle_eq_pi_div_two

/-- The tangent of an angle in a right-angled triangle multiplied by the adjacent side equals
the opposite side. -/
theorem tan_angle_mul_dist_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ = p‚ÇÇ ‚à® p‚ÇÉ ‚â† p‚ÇÇ) : Real.tan (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) * dist p‚ÇÉ p‚ÇÇ = dist p‚ÇÅ p‚ÇÇ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [ne_comm, ‚Üê @vsub_ne_zero V, ‚Üê @vsub_eq_zero_iff_eq V, or_comm] at h0
  -- ‚ä¢ Real.tan (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) * dist p‚ÇÉ p‚ÇÇ = dist p‚ÇÅ p‚ÇÇ
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, tan_angle_add_mul_norm_of_inner_eq_zero h h0]
#align euclidean_geometry.tan_angle_mul_dist_of_angle_eq_pi_div_two EuclideanGeometry.tan_angle_mul_dist_of_angle_eq_pi_div_two

/-- A side of a right-angled triangle divided by the cosine of the adjacent angle equals the
hypotenuse. -/
theorem dist_div_cos_angle_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ = p‚ÇÇ ‚à® p‚ÇÉ ‚â† p‚ÇÇ) : dist p‚ÇÉ p‚ÇÇ / Real.cos (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÉ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [ne_comm, ‚Üê @vsub_ne_zero V, ‚Üê @vsub_eq_zero_iff_eq V, or_comm] at h0
  -- ‚ä¢ dist p‚ÇÉ p‚ÇÇ / Real.cos (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÉ
  rw [angle, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, norm_div_cos_angle_add_of_inner_eq_zero h h0]
#align euclidean_geometry.dist_div_cos_angle_of_angle_eq_pi_div_two EuclideanGeometry.dist_div_cos_angle_of_angle_eq_pi_div_two

/-- A side of a right-angled triangle divided by the sine of the opposite angle equals the
hypotenuse. -/
theorem dist_div_sin_angle_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ ‚â† p‚ÇÇ ‚à® p‚ÇÉ = p‚ÇÇ) : dist p‚ÇÅ p‚ÇÇ / Real.sin (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÉ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [eq_comm, ‚Üê @vsub_ne_zero V, ‚Üê @vsub_eq_zero_iff_eq V, or_comm] at h0
  -- ‚ä¢ dist p‚ÇÅ p‚ÇÇ / Real.sin (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÅ p‚ÇÉ
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÉ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, norm_div_sin_angle_add_of_inner_eq_zero h h0]
#align euclidean_geometry.dist_div_sin_angle_of_angle_eq_pi_div_two EuclideanGeometry.dist_div_sin_angle_of_angle_eq_pi_div_two

/-- A side of a right-angled triangle divided by the tangent of the opposite angle equals the
adjacent side. -/
theorem dist_div_tan_angle_of_angle_eq_pi_div_two {p‚ÇÅ p‚ÇÇ p‚ÇÉ : P} (h : ‚à† p‚ÇÅ p‚ÇÇ p‚ÇÉ = œÄ / 2)
    (h0 : p‚ÇÅ ‚â† p‚ÇÇ ‚à® p‚ÇÉ = p‚ÇÇ) : dist p‚ÇÅ p‚ÇÇ / Real.tan (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÉ p‚ÇÇ := by
  rw [angle, ‚Üê inner_eq_zero_iff_angle_eq_pi_div_two, real_inner_comm, ‚Üê neg_eq_zero, ‚Üê
    inner_neg_left, neg_vsub_eq_vsub_rev] at h
  rw [eq_comm, ‚Üê @vsub_ne_zero V, ‚Üê @vsub_eq_zero_iff_eq V, or_comm] at h0
  -- ‚ä¢ dist p‚ÇÅ p‚ÇÇ / Real.tan (‚à† p‚ÇÇ p‚ÇÉ p‚ÇÅ) = dist p‚ÇÉ p‚ÇÇ
  rw [angle, dist_eq_norm_vsub V p‚ÇÅ p‚ÇÇ, dist_eq_norm_vsub' V p‚ÇÉ p‚ÇÇ, ‚Üê vsub_add_vsub_cancel p‚ÇÅ p‚ÇÇ p‚ÇÉ,
    add_comm, norm_div_tan_angle_add_of_inner_eq_zero h h0]
#align euclidean_geometry.dist_div_tan_angle_of_angle_eq_pi_div_two EuclideanGeometry.dist_div_tan_angle_of_angle_eq_pi_div_two

end EuclideanGeometry
