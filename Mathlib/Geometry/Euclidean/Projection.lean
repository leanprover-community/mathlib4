/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, Manuel Candales
-/
module

public import Mathlib.Analysis.InnerProductSpace.Projection.Reflection
public import Mathlib.Analysis.InnerProductSpace.Projection.Submodule
public import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional

/-!
# Orthogonal projection in affine spaces

This file defines orthogonal projection onto an affine subspace,
and reflection of a point in an affine subspace.

## Main definitions

* `EuclideanGeometry.orthogonalProjection` is the orthogonal
  projection of a point onto an affine subspace.

* `EuclideanGeometry.reflection` is the reflection of a point in an
  affine subspace.

-/

@[expose] public section

noncomputable section

namespace EuclideanGeometry

variable {ùïú : Type*} {V : Type*} {P : Type*} [RCLike ùïú]
variable [NormedAddCommGroup V] [InnerProductSpace ùïú V]
variable {V‚ÇÇ P‚ÇÇ : Type*} [NormedAddCommGroup V‚ÇÇ] [InnerProductSpace ùïú V‚ÇÇ]

open AffineSubspace

section PseudoMetricSpace

variable [PseudoMetricSpace P] [NormedAddTorsor V P]

/-- The orthogonal projection of a point onto a nonempty affine subspace. -/
def orthogonalProjection (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] : P ‚Üí·¥¨[ùïú] s :=
  letI x := Classical.arbitrary s
  AffineIsometryEquiv.vaddConst ùïú x
    |>.toContinuousAffineEquiv.toContinuousAffineMap.comp
      s.direction.orthogonalProjection.toContinuousAffineMap
    |>.comp <| AffineIsometryEquiv.vaddConst ùïú (x : P) |>.symm

theorem orthogonalProjection_apply (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p} :
    orthogonalProjection s p = s.direction.orthogonalProjection (p -·µ• Classical.arbitrary s)
      +·µ• Classical.arbitrary s :=
  rfl

theorem orthogonalProjection_apply' (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p} :
    (orthogonalProjection s p : P) =
      (s.direction.orthogonalProjection (p -·µ• Classical.arbitrary s) : V) +·µ•
      (Classical.arbitrary s : P) :=
  rfl

theorem orthogonalProjection_apply_mem (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p x} (hx : x ‚àà s) :
    orthogonalProjection s p = (s.direction.orthogonalProjection (p -·µ• x) : V) +·µ• x := by
  rw [orthogonalProjection_apply, coe_vadd, vadd_eq_vadd_iff_sub_eq_vsub, ‚Üê Submodule.coe_sub,
    ‚Üê map_sub, vsub_sub_vsub_cancel_left, Submodule.coe_orthogonalProjection_apply,
    Submodule.starProjection_eq_self_iff]
  exact s.vsub_mem_direction (SetLike.coe_mem _) hx

/-- Since both instance arguments are propositions, allow `simp` to rewrite them
alongside the `s` argument.

Note that without the coercion to `P`, the LHS and RHS would have different types. -/
@[congr]
theorem orthogonalProjection_congr {s‚ÇÅ s‚ÇÇ : AffineSubspace ùïú P} {p‚ÇÅ p‚ÇÇ : P}
    [Nonempty s‚ÇÅ] [s‚ÇÅ.direction.HasOrthogonalProjection]
    (h : s‚ÇÅ = s‚ÇÇ) (hp : p‚ÇÅ = p‚ÇÇ) :
    letI : Nonempty s‚ÇÇ := h ‚ñ∏ ‚Äπ_‚Ä∫
    letI : s‚ÇÇ.direction.HasOrthogonalProjection := h ‚ñ∏ ‚Äπ_‚Ä∫
    (orthogonalProjection s‚ÇÅ p‚ÇÅ : P) = (orthogonalProjection s‚ÇÇ p‚ÇÇ : P) := by
  subst h hp
  rfl

/-- The linear map corresponding to `orthogonalProjection`. -/
@[simp]
theorem orthogonalProjection_linear {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] :
    (orthogonalProjection s).linear = s.direction.orthogonalProjection :=
  rfl

/-- The continuous linear map corresponding to `orthogonalProjection`. -/
@[simp]
theorem orthogonalProjection_contLinear {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] :
    (orthogonalProjection s).contLinear = s.direction.orthogonalProjection :=
  rfl

/-- The `orthogonalProjection` lies in the given subspace. -/
theorem orthogonalProjection_mem {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : ‚Üë(orthogonalProjection s p) ‚àà s :=
  (orthogonalProjection s p).2

/-- The `orthogonalProjection` lies in the orthogonal subspace. -/
theorem orthogonalProjection_mem_orthogonal (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    ‚Üë(orthogonalProjection s p) ‚àà mk' p s.direction·óÆ := by
  rw [mem_mk', orthogonalProjection_apply, coe_vadd, vadd_vsub_eq_sub_vsub,
    ‚Üê Submodule.neg_mem_iff, neg_sub]
  apply Submodule.sub_starProjection_mem_orthogonal

/-- The intersection of the subspace and the orthogonal subspace
through the given point is the `orthogonalProjection` of that point
onto the subspace. -/
theorem inter_eq_singleton_orthogonalProjection {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    (s : Set P) ‚à© mk' p s.direction·óÆ = {‚Üë(orthogonalProjection s p)} := by
  obtain ‚ü®q, hq‚ü© := inter_eq_singleton_of_nonempty_of_isCompl (nonempty_subtype.mp ‚Äπ_‚Ä∫)
    (mk'_nonempty p s.direction·óÆ)
    (by
      rw [direction_mk' p s.direction·óÆ]
      exact Submodule.isCompl_orthogonal_of_hasOrthogonalProjection)
  rwa [Set.eq_singleton_iff_nonempty_unique_mem.1 hq |>.2 _
    ‚ü®orthogonalProjection_mem _, orthogonalProjection_mem_orthogonal _ _‚ü©]

/-- Subtracting a point in the given subspace from the
`orthogonalProjection` produces a result in the direction of the
given subspace. -/
theorem orthogonalProjection_vsub_mem_direction {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p‚ÇÅ : P} (p‚ÇÇ : P) (hp‚ÇÅ : p‚ÇÅ ‚àà s) :
    ‚Üë(orthogonalProjection s p‚ÇÇ -·µ• ‚ü®p‚ÇÅ, hp‚ÇÅ‚ü© : s.direction) ‚àà s.direction :=
  (orthogonalProjection s p‚ÇÇ -·µ• ‚ü®p‚ÇÅ, hp‚ÇÅ‚ü© : s.direction).2

/-- Subtracting the `orthogonalProjection` from a point in the given
subspace produces a result in the direction of the given subspace. -/
theorem vsub_orthogonalProjection_mem_direction {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p‚ÇÅ : P} (p‚ÇÇ : P) (hp‚ÇÅ : p‚ÇÅ ‚àà s) :
    ‚Üë((‚ü®p‚ÇÅ, hp‚ÇÅ‚ü© : s) -·µ• orthogonalProjection s p‚ÇÇ : s.direction) ‚àà s.direction :=
  ((‚ü®p‚ÇÅ, hp‚ÇÅ‚ü© : s) -·µ• orthogonalProjection s p‚ÇÇ : s.direction).2

/-- A point equals its orthogonal projection if and only if it lies in
the subspace. -/
theorem orthogonalProjection_eq_self_iff {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} : ‚Üë(orthogonalProjection s p) = p ‚Üî p ‚àà s := by
  constructor
  ¬∑ exact fun h => h ‚ñ∏ orthogonalProjection_mem p
  ¬∑ intro h
    have hp : p ‚àà (s : Set P) ‚à© mk' p s.direction·óÆ := ‚ü®h, self_mem_mk' p _‚ü©
    rw [inter_eq_singleton_orthogonalProjection p] at hp
    symm
    exact hp

@[simp]
theorem orthogonalProjection_mem_subspace_eq_self {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : s) : orthogonalProjection s p = p := by
  ext
  rw [orthogonalProjection_eq_self_iff]
  exact p.2

/-- Orthogonal projection is idempotent. -/
theorem orthogonalProjection_orthogonalProjection (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    orthogonalProjection s (orthogonalProjection s p) = orthogonalProjection s p :=
  orthogonalProjection_mem_subspace_eq_self ((orthogonalProjection s) p)

theorem eq_orthogonalProjection_of_eq_subspace {s s' : AffineSubspace ùïú P} [Nonempty s]
    [Nonempty s'] [s.direction.HasOrthogonalProjection] [s'.direction.HasOrthogonalProjection]
    (h : s = s') (p : P) : (orthogonalProjection s p : P) = (orthogonalProjection s' p : P) := by
  subst h
  rfl

@[simp] lemma orthogonalProjection_affineSpan_singleton (p‚ÇÅ p‚ÇÇ : P) :
    orthogonalProjection (affineSpan ùïú {p‚ÇÅ}) p‚ÇÇ = p‚ÇÅ := by
  have h := SetLike.coe_mem (orthogonalProjection (affineSpan ùïú {p‚ÇÅ}) p‚ÇÇ)
  rwa [mem_affineSpan_singleton] at h

/-- Subtracting `p` from its `orthogonalProjection` produces a result
in the orthogonal direction. -/
theorem orthogonalProjection_vsub_mem_direction_orthogonal (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    (orthogonalProjection s p : P) -·µ• p ‚àà s.direction·óÆ := by
  rw [‚Üê mem_mk']
  apply orthogonalProjection_mem_orthogonal

/-- Subtracting the `orthogonalProjection` from `p` produces a result
in the orthogonal direction. -/
theorem vsub_orthogonalProjection_mem_direction_orthogonal (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : p -·µ• orthogonalProjection s p ‚àà s.direction·óÆ :=
  direction_mk' p s.direction·óÆ ‚ñ∏
    vsub_mem_direction (self_mem_mk' _ _) (orthogonalProjection_mem_orthogonal s p)

/-- Subtracting the `orthogonalProjection` from `p` produces a result in the kernel of the linear
part of the orthogonal projection. -/
theorem orthogonalProjection_vsub_orthogonalProjection (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    s.direction.orthogonalProjection (p -·µ• orthogonalProjection s p) = 0 := by
  simpa using vsub_orthogonalProjection_mem_direction_orthogonal _ _

/-- The characteristic property of the orthogonal projection, for a point given in the underlying
space. This form is typically more convenient to use than
`inter_eq_singleton_orthogonalProjection`. -/
lemma coe_orthogonalProjection_eq_iff_mem {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p q : P} :
    orthogonalProjection s p = q ‚Üî q ‚àà s ‚àß p -·µ• q ‚àà s.direction·óÆ := by
  constructor
  ¬∑ rintro rfl
    exact ‚ü®orthogonalProjection_mem _, vsub_orthogonalProjection_mem_direction_orthogonal _ _‚ü©
  ¬∑ rintro ‚ü®hqs, hpq‚ü©
    have hq : q ‚àà mk' p s.direction·óÆ := by
      rwa [mem_mk', ‚Üê neg_mem_iff, neg_vsub_eq_vsub_rev]
    suffices q ‚àà ({(orthogonalProjection s p : P)} : Set P) by
      simpa [eq_comm] using this
    rw [‚Üê inter_eq_singleton_orthogonalProjection]
    simp only [Set.mem_inter_iff, SetLike.mem_coe]
    exact ‚ü®hqs, hq‚ü©

/-- The characteristic property of the orthogonal projection, for a point given in the relevant
subspace. This form is typically more convenient to use than
`inter_eq_singleton_orthogonalProjection`. -/
lemma orthogonalProjection_eq_iff_mem {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} {q : s} :
    orthogonalProjection s p = q ‚Üî p -·µ• q ‚àà s.direction·óÆ := by
  simpa using coe_orthogonalProjection_eq_iff_mem (s := s) (p := p) (q := (q : P))

/-- A condition for two points to have the same orthogonal projection onto a given subspace. -/
lemma orthogonalProjection_eq_orthogonalProjection_iff_vsub_mem {s : AffineSubspace ùïú P}
    [Nonempty s] [s.direction.HasOrthogonalProjection] {p q : P} :
    orthogonalProjection s p = orthogonalProjection s q ‚Üî p -·µ• q ‚àà s.direction·óÆ := by
  rw [orthogonalProjection_eq_iff_mem, ‚Üê s.direction·óÆ.add_mem_iff_left (x := p -·µ• q)
    (vsub_orthogonalProjection_mem_direction_orthogonal s q)]
  simp

/-- If the orthogonal projections of a point onto two subspaces are equal, so is the projection
onto their supremum. -/
lemma orthogonalProjection_sup_of_orthogonalProjection_eq {s‚ÇÅ s‚ÇÇ : AffineSubspace ùïú P} [Nonempty s‚ÇÅ]
    [Nonempty s‚ÇÇ] [s‚ÇÅ.direction.HasOrthogonalProjection] [s‚ÇÇ.direction.HasOrthogonalProjection]
    {p : P} (h : (orthogonalProjection s‚ÇÅ p : P) = orthogonalProjection s‚ÇÇ p)
    [(s‚ÇÅ ‚äî s‚ÇÇ).direction.HasOrthogonalProjection] :
    (orthogonalProjection (s‚ÇÅ ‚äî s‚ÇÇ) p : P) = orthogonalProjection s‚ÇÅ p := by
  rw [coe_orthogonalProjection_eq_iff_mem]
  refine ‚ü®SetLike.le_def.1 le_sup_left (orthogonalProjection_mem _), ?_‚ü©
  rw [direction_sup_eq_sup_direction (orthogonalProjection_mem p) (h ‚ñ∏ orthogonalProjection_mem p),
    ‚Üê Submodule.inf_orthogonal]
  exact ‚ü®vsub_orthogonalProjection_mem_direction_orthogonal _ _,
    h ‚ñ∏ vsub_orthogonalProjection_mem_direction_orthogonal _ _‚ü©

/-- Adding a vector to a point in the given subspace, then taking the
orthogonal projection, produces the original point if the vector was
in the orthogonal direction. -/
theorem orthogonalProjection_vadd_eq_self {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} (hp : p ‚àà s) {v : V} (hv : v ‚àà s.direction·óÆ) :
    orthogonalProjection s (v +·µ• p) = ‚ü®p, hp‚ü© := by
  have h := vsub_orthogonalProjection_mem_direction_orthogonal s (v +·µ• p)
  rw [vadd_vsub_assoc, Submodule.add_mem_iff_right _ hv] at h
  refine (eq_of_vsub_eq_zero ?_).symm
  ext
  refine Submodule.disjoint_def.1 s.direction.orthogonal_disjoint _ ?_ h
  exact (_ : s.direction).2

/-- Adding a vector to a point in the given subspace, then taking the
orthogonal projection, produces the original point if the vector is a
multiple of the result of subtracting a point's orthogonal projection
from that point. -/
theorem orthogonalProjection_vadd_smul_vsub_orthogonalProjection {s : AffineSubspace ùïú P}
    [Nonempty s] [s.direction.HasOrthogonalProjection] {p‚ÇÅ : P} (p‚ÇÇ : P) (r : ùïú) (hp : p‚ÇÅ ‚àà s) :
    orthogonalProjection s (r ‚Ä¢ (p‚ÇÇ -·µ• orthogonalProjection s p‚ÇÇ : V) +·µ• p‚ÇÅ) = ‚ü®p‚ÇÅ, hp‚ü© :=
  orthogonalProjection_vadd_eq_self hp
    (Submodule.smul_mem _ _ (vsub_orthogonalProjection_mem_direction_orthogonal s _))

lemma orthogonalProjection_orthogonalProjection_of_le {s‚ÇÅ s‚ÇÇ : AffineSubspace ùïú P} [Nonempty s‚ÇÅ]
    [Nonempty s‚ÇÇ] [s‚ÇÅ.direction.HasOrthogonalProjection] [s‚ÇÇ.direction.HasOrthogonalProjection]
    (h : s‚ÇÅ ‚â§ s‚ÇÇ) (p : P) :
    orthogonalProjection s‚ÇÅ (orthogonalProjection s‚ÇÇ p) = orthogonalProjection s‚ÇÅ p := by
  rw [orthogonalProjection_eq_orthogonalProjection_iff_vsub_mem]
  exact SetLike.le_def.1 (Submodule.orthogonal_le (direction_le h))
    (orthogonalProjection_vsub_mem_direction_orthogonal _ _)

/-- The square of the distance from a point in `s` to `p‚ÇÇ` equals the
sum of the squares of the distances of the two points to the
`orthogonalProjection`. -/
theorem dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq
    {s : AffineSubspace ùïú P} [Nonempty s] [s.direction.HasOrthogonalProjection] {p‚ÇÅ : P} (p‚ÇÇ : P)
    (hp‚ÇÅ : p‚ÇÅ ‚àà s) :
    dist p‚ÇÅ p‚ÇÇ * dist p‚ÇÅ p‚ÇÇ =
      dist p‚ÇÅ (orthogonalProjection s p‚ÇÇ) * dist p‚ÇÅ (orthogonalProjection s p‚ÇÇ) +
        dist p‚ÇÇ (orthogonalProjection s p‚ÇÇ) * dist p‚ÇÇ (orthogonalProjection s p‚ÇÇ) := by
  rw [dist_comm p‚ÇÇ _, dist_eq_norm_vsub V p‚ÇÅ _, dist_eq_norm_vsub V p‚ÇÅ _, dist_eq_norm_vsub V _ p‚ÇÇ,
    ‚Üê vsub_add_vsub_cancel p‚ÇÅ (orthogonalProjection s p‚ÇÇ) p‚ÇÇ,
    norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (ùïú := ùïú)]
  exact Submodule.inner_right_of_mem_orthogonal (vsub_orthogonalProjection_mem_direction p‚ÇÇ hp‚ÇÅ)
    (orthogonalProjection_vsub_mem_direction_orthogonal s p‚ÇÇ)

/-- The distance between a point and its orthogonal projection to a subspace equals the distance
to that subspace as given by `Metric.infDist`. This is not a `simp` lemma since the simplest form
depends on the context (if any calculations are to be done with the distance, the version with
the orthogonal projection gives access to more lemmas about orthogonal projections that may be
useful). -/
lemma dist_orthogonalProjection_eq_infDist (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    dist p (orthogonalProjection s p) = Metric.infDist p s := by
  refine le_antisymm ?_ (Metric.infDist_le_dist_of_mem (orthogonalProjection_mem _))
  rw [Metric.infDist_eq_iInf]
  refine le_ciInf fun x ‚Ü¶ le_of_sq_le_sq ?_ dist_nonneg
  rw [dist_comm _ (x : P)]
  simp_rw [pow_two,
    dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq p x.property]
  simp [mul_self_nonneg]

/-- The nonnegative distance between a point and its orthogonal projection to a subspace equals
the distance to that subspace as given by `Metric.infNndist`. This is not a `simp` lemma since
the simplest form depends on the context (if any calculations are to be done with the distance,
the version with the orthogonal projection gives access to more lemmas about orthogonal
projections that may be useful). -/
lemma dist_orthogonalProjection_eq_infNndist (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    nndist p (orthogonalProjection s p) = Metric.infNndist p s := by
  rw [‚Üê NNReal.coe_inj]
  simp [dist_orthogonalProjection_eq_infDist]

/-- The square of the distance between two points constructed by
adding multiples of the same orthogonal vector to points in the same
subspace. -/
theorem dist_sq_smul_orthogonal_vadd_smul_orthogonal_vadd {s : AffineSubspace ùïú P} {p‚ÇÅ p‚ÇÇ : P}
    (hp‚ÇÅ : p‚ÇÅ ‚àà s) (hp‚ÇÇ : p‚ÇÇ ‚àà s) (r‚ÇÅ r‚ÇÇ : ùïú) {v : V} (hv : v ‚àà s.direction·óÆ) :
    dist (r‚ÇÅ ‚Ä¢ v +·µ• p‚ÇÅ) (r‚ÇÇ ‚Ä¢ v +·µ• p‚ÇÇ) * dist (r‚ÇÅ ‚Ä¢ v +·µ• p‚ÇÅ) (r‚ÇÇ ‚Ä¢ v +·µ• p‚ÇÇ) =
      dist p‚ÇÅ p‚ÇÇ * dist p‚ÇÅ p‚ÇÇ + ‚Äñr‚ÇÅ - r‚ÇÇ‚Äñ * ‚Äñr‚ÇÅ - r‚ÇÇ‚Äñ * (‚Äñv‚Äñ * ‚Äñv‚Äñ) :=
  calc
    dist (r‚ÇÅ ‚Ä¢ v +·µ• p‚ÇÅ) (r‚ÇÇ ‚Ä¢ v +·µ• p‚ÇÇ) * dist (r‚ÇÅ ‚Ä¢ v +·µ• p‚ÇÅ) (r‚ÇÇ ‚Ä¢ v +·µ• p‚ÇÇ) =
        ‚Äñp‚ÇÅ -·µ• p‚ÇÇ + (r‚ÇÅ - r‚ÇÇ) ‚Ä¢ v‚Äñ * ‚Äñp‚ÇÅ -·µ• p‚ÇÇ + (r‚ÇÅ - r‚ÇÇ) ‚Ä¢ v‚Äñ := by
      rw [dist_eq_norm_vsub V (r‚ÇÅ ‚Ä¢ v +·µ• p‚ÇÅ), vsub_vadd_eq_vsub_sub, vadd_vsub_assoc, sub_smul,
        add_comm, add_sub_assoc]
    _ = ‚Äñp‚ÇÅ -·µ• p‚ÇÇ‚Äñ * ‚Äñp‚ÇÅ -·µ• p‚ÇÇ‚Äñ + ‚Äñ(r‚ÇÅ - r‚ÇÇ) ‚Ä¢ v‚Äñ * ‚Äñ(r‚ÇÅ - r‚ÇÇ) ‚Ä¢ v‚Äñ :=
      norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _
        (Submodule.inner_right_of_mem_orthogonal (vsub_mem_direction hp‚ÇÅ hp‚ÇÇ)
          (Submodule.smul_mem _ _ hv))
    _ = dist p‚ÇÅ p‚ÇÇ * dist p‚ÇÅ p‚ÇÇ + ‚Äñr‚ÇÅ - r‚ÇÇ‚Äñ * ‚Äñr‚ÇÅ - r‚ÇÇ‚Äñ * (‚Äñv‚Äñ * ‚Äñv‚Äñ) := by
      rw [norm_smul, dist_eq_norm_vsub V p‚ÇÅ]
      ring

/-- `p` is equidistant from two points in `s` if and only if its
`orthogonalProjection` is. -/
theorem dist_eq_iff_dist_orthogonalProjection_eq {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p‚ÇÅ p‚ÇÇ : P} (p‚ÇÉ : P) (hp‚ÇÅ : p‚ÇÅ ‚àà s) (hp‚ÇÇ : p‚ÇÇ ‚àà s) :
    dist p‚ÇÅ p‚ÇÉ = dist p‚ÇÇ p‚ÇÉ ‚Üî
      dist p‚ÇÅ (orthogonalProjection s p‚ÇÉ) = dist p‚ÇÇ (orthogonalProjection s p‚ÇÉ) := by
  rw [‚Üê mul_self_inj_of_nonneg dist_nonneg dist_nonneg, ‚Üê
    mul_self_inj_of_nonneg dist_nonneg dist_nonneg,
    dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq p‚ÇÉ hp‚ÇÅ,
    dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq p‚ÇÉ hp‚ÇÇ]
  simp

/-- `p` is equidistant from a set of points in `s` if and only if its
`orthogonalProjection` is. -/
theorem dist_set_eq_iff_dist_orthogonalProjection_eq {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {ps : Set P} (hps : ps ‚äÜ s) (p : P) :
    (Set.Pairwise ps fun p‚ÇÅ p‚ÇÇ => dist p‚ÇÅ p = dist p‚ÇÇ p) ‚Üî
      Set.Pairwise ps fun p‚ÇÅ p‚ÇÇ =>
        dist p‚ÇÅ (orthogonalProjection s p) = dist p‚ÇÇ (orthogonalProjection s p) :=
  ‚ü®fun h _ hp‚ÇÅ _ hp‚ÇÇ hne =>
    (dist_eq_iff_dist_orthogonalProjection_eq p (hps hp‚ÇÅ) (hps hp‚ÇÇ)).1 (h hp‚ÇÅ hp‚ÇÇ hne),
    fun h _ hp‚ÇÅ _ hp‚ÇÇ hne =>
    (dist_eq_iff_dist_orthogonalProjection_eq p (hps hp‚ÇÅ) (hps hp‚ÇÇ)).2 (h hp‚ÇÅ hp‚ÇÇ hne)‚ü©

/-- There exists `r` such that `p` has distance `r` from all the
points of a set of points in `s` if and only if there exists (possibly
different) `r` such that its `orthogonalProjection` has that distance
from all the points in that set. -/
theorem exists_dist_eq_iff_exists_dist_orthogonalProjection_eq {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {ps : Set P} (hps : ps ‚äÜ s) (p : P) :
    (‚àÉ r, ‚àÄ p‚ÇÅ ‚àà ps, dist p‚ÇÅ p = r) ‚Üî ‚àÉ r, ‚àÄ p‚ÇÅ ‚àà ps, dist p‚ÇÅ ‚Üë(orthogonalProjection s p) = r := by
  have h := dist_set_eq_iff_dist_orthogonalProjection_eq hps p
  simp_rw [Set.pairwise_eq_iff_exists_eq] at h
  exact h

/-- Reflection in an affine subspace, which is expected to be nonempty
and complete. The word "reflection" is sometimes understood to mean
specifically reflection in a codimension-one subspace, and sometimes
more generally to cover operations such as reflection in a point. The
definition here, of reflection in an affine subspace, is a more
general sense of the word that includes both those common cases. -/
def reflection (s : AffineSubspace ùïú P) [Nonempty s] [s.direction.HasOrthogonalProjection] :
    P ‚âÉ·µÉ‚Å±[ùïú] P :=
  letI x : P := Classical.arbitrary s
  AffineIsometryEquiv.vaddConst ùïú x
    |>.symm.trans s.direction.reflection.toAffineIsometryEquiv
    |>.trans <| AffineIsometryEquiv.vaddConst ùïú x

theorem reflection_apply (s : AffineSubspace ùïú P) [Nonempty s] [s.direction.HasOrthogonalProjection]
    (p : P) :
    reflection s p = s.direction.reflection (p -·µ• Classical.arbitrary s)
      +·µ• (Classical.arbitrary s : P) :=
  rfl

theorem reflection_apply_of_mem (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) {x} (hx : x ‚àà s) :
    reflection s p = s.direction.reflection (p -·µ• x) +·µ• x := by
  rw [reflection_apply, vadd_eq_vadd_iff_sub_eq_vsub, ‚Üê map_sub,
    vsub_sub_vsub_cancel_left, s.direction.reflection_eq_self_iff]
  exact s.vsub_mem_direction (SetLike.coe_mem _) hx

theorem reflection_apply' (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    reflection s p = (‚Üë(orthogonalProjection s p) -·µ• p) +·µ• (orthogonalProjection s p : P) := by
  rw [reflection_apply, orthogonalProjection_apply', Submodule.coe_orthogonalProjection_apply]
  set x : P := ‚Üë(Classical.arbitrary s)
  set v : V := s.direction.starProjection (p -·µ• x)
  rw [Submodule.reflection_apply, two_smul, sub_eq_add_neg, neg_vsub_eq_vsub_rev, add_assoc,
    add_comm v, add_vadd, vadd_vsub_assoc]

theorem eq_reflection_of_eq_subspace {s s' : AffineSubspace ùïú P} [Nonempty s] [Nonempty s']
    [s.direction.HasOrthogonalProjection] [s'.direction.HasOrthogonalProjection] (h : s = s')
    (p : P) : (reflection s p : P) = (reflection s' p : P) := by
  subst h
  rfl

/-- Reflecting twice in the same subspace. -/
@[simp]
theorem reflection_reflection (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : reflection s (reflection s p) = p := by
  simp [reflection, -AffineIsometryEquiv.map_vadd]

/-- Reflection is its own inverse. -/
@[simp]
theorem reflection_symm (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] : (reflection s).symm = reflection s := by
  ext
  rw [‚Üê (reflection s).injective.eq_iff]
  simp

/-- Reflection is involutive. -/
theorem reflection_involutive (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] : Function.Involutive (reflection s) :=
  reflection_reflection s

/-- A point is its own reflection if and only if it is in the subspace. -/
theorem reflection_eq_self_iff {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : reflection s p = p ‚Üî p ‚àà s := by
  rw [reflection_apply, Eq.comm, eq_vadd_iff_vsub_eq, Eq.comm, s.direction.reflection_eq_self_iff,
    s.mem_direction_iff_eq_vsub_right (SetLike.coe_mem (Classical.arbitrary s))]
  simp

/-- Reflecting a point in two subspaces produces the same result if
and only if the point has the same orthogonal projection in each of
those subspaces. -/
theorem reflection_eq_iff_orthogonalProjection_eq (s‚ÇÅ s‚ÇÇ : AffineSubspace ùïú P) [Nonempty s‚ÇÅ]
    [Nonempty s‚ÇÇ] [s‚ÇÅ.direction.HasOrthogonalProjection] [s‚ÇÇ.direction.HasOrthogonalProjection]
    (p : P) :
    reflection s‚ÇÅ p = reflection s‚ÇÇ p ‚Üî
      (orthogonalProjection s‚ÇÅ p : P) = orthogonalProjection s‚ÇÇ p := by
  rw [reflection_apply', reflection_apply']
  constructor
  ¬∑ intro h
    rw [‚Üê @vsub_eq_zero_iff_eq V, vsub_vadd_eq_vsub_sub, vadd_vsub_assoc, add_comm, add_sub_assoc,
      vsub_sub_vsub_cancel_right, ‚Üê
      two_smul ùïú ((orthogonalProjection s‚ÇÅ p : P) -·µ• orthogonalProjection s‚ÇÇ p), smul_eq_zero] at h
    norm_num at h
    exact h
  ¬∑ intro h
    rw [h]

/-- The distance between `p‚ÇÅ` and the reflection of `p‚ÇÇ` equals that
between the reflection of `p‚ÇÅ` and `p‚ÇÇ`. -/
theorem dist_reflection (s : AffineSubspace ùïú P) [Nonempty s] [s.direction.HasOrthogonalProjection]
    (p‚ÇÅ p‚ÇÇ : P) : dist p‚ÇÅ (reflection s p‚ÇÇ) = dist (reflection s p‚ÇÅ) p‚ÇÇ := by
  conv_lhs => rw [‚Üê reflection_reflection s p‚ÇÅ]
  exact (reflection s).dist_map _ _

/-- A point in the subspace is equidistant from another point and its
reflection. -/
theorem dist_reflection_eq_of_mem (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p‚ÇÅ : P} (hp‚ÇÅ : p‚ÇÅ ‚àà s) (p‚ÇÇ : P) :
    dist p‚ÇÅ (reflection s p‚ÇÇ) = dist p‚ÇÅ p‚ÇÇ := by
  rw [‚Üê reflection_eq_self_iff p‚ÇÅ] at hp‚ÇÅ
  convert (reflection s).dist_map p‚ÇÅ p‚ÇÇ
  rw [hp‚ÇÅ]

/-- The reflection of a point in a subspace is contained in any larger
subspace containing both the point and the subspace reflected in. -/
theorem reflection_mem_of_le_of_mem {s‚ÇÅ s‚ÇÇ : AffineSubspace ùïú P} [Nonempty s‚ÇÅ]
    [s‚ÇÅ.direction.HasOrthogonalProjection] (hle : s‚ÇÅ ‚â§ s‚ÇÇ) {p : P} (hp : p ‚àà s‚ÇÇ) :
    reflection s‚ÇÅ p ‚àà s‚ÇÇ := by
  rw [reflection_apply']
  have ho : ‚Üë(orthogonalProjection s‚ÇÅ p) ‚àà s‚ÇÇ := hle (orthogonalProjection_mem p)
  exact vadd_mem_of_mem_direction (vsub_mem_direction ho hp) ho

/-- Reflecting an orthogonal vector plus a point in the subspace
produces the negation of that vector plus the point. -/
theorem reflection_orthogonal_vadd {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} (hp : p ‚àà s) {v : V} (hv : v ‚àà s.direction·óÆ) :
    reflection s (v +·µ• p) = -v +·µ• p := by
  rw [reflection_apply', orthogonalProjection_vadd_eq_self hp hv, vsub_vadd_eq_vsub_sub]
  simp

/-- Reflecting a vector plus a point in the subspace produces the
negation of that vector plus the point if the vector is a multiple of
the result of subtracting a point's orthogonal projection from that
point. -/
theorem reflection_vadd_smul_vsub_orthogonalProjection {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p‚ÇÅ : P} (p‚ÇÇ : P) (r : ùïú) (hp‚ÇÅ : p‚ÇÅ ‚àà s) :
    reflection s (r ‚Ä¢ (p‚ÇÇ -·µ• orthogonalProjection s p‚ÇÇ) +·µ• p‚ÇÅ) =
      -(r ‚Ä¢ (p‚ÇÇ -·µ• orthogonalProjection s p‚ÇÇ)) +·µ• p‚ÇÅ :=
  reflection_orthogonal_vadd hp‚ÇÅ
    (Submodule.smul_mem _ _ (vsub_orthogonalProjection_mem_direction_orthogonal s _))

end PseudoMetricSpace

section MetricSpace

variable [MetricSpace P] [NormedAddTorsor V P]
variable [MetricSpace P‚ÇÇ] [NormedAddTorsor V‚ÇÇ P‚ÇÇ]

@[simp] lemma orthogonalProjection_map (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (f : P ‚Üí·µÉ‚Å±[ùïú] P‚ÇÇ)
    [(s.map f.toAffineMap).direction.HasOrthogonalProjection] (p : P) :
    orthogonalProjection (s.map f.toAffineMap) (f p) = f (orthogonalProjection s p) := by
  rw [coe_orthogonalProjection_eq_iff_mem]
  simp only [mem_map, AffineIsometry.coe_toAffineMap, AffineIsometry.map_eq_iff, exists_eq_right,
    SetLike.coe_mem, map_direction, AffineIsometry.linear_eq_linearIsometry, true_and]
  rw [‚Üê AffineIsometry.coe_toAffineMap, ‚Üê AffineMap.linearMap_vsub, Submodule.mem_orthogonal]
  intro u hu
  rw [Submodule.mem_map] at hu
  obtain ‚ü®v, hv, rfl‚ü© := hu
  rw [AffineIsometry.linear_eq_linearIsometry, LinearIsometry.coe_toLinearMap,
    LinearIsometry.inner_map_map, Submodule.inner_right_of_mem_orthogonal hv
      (vsub_orthogonalProjection_mem_direction_orthogonal _ _)]

lemma orthogonalProjection_subtype (s : AffineSubspace ùïú P) [Nonempty s] (s' : AffineSubspace ùïú s)
    [Nonempty s'] [s'.direction.HasOrthogonalProjection]
    [(s'.map s.subtype).direction.HasOrthogonalProjection] (p : s) :
    (orthogonalProjection s' p : P) = orthogonalProjection (s'.map s.subtype) p := by
  rw [eq_comm]
  have : (s'.map s.subtype‚Çê·µ¢.toAffineMap).direction.HasOrthogonalProjection := by
    rw [subtype‚Çê·µ¢_toAffineMap]
    infer_instance
  convert orthogonalProjection_map s' s.subtype‚Çê·µ¢ p

/-- The distance to a point's orthogonal projection is 0 iff it lies in the subspace. -/
theorem dist_orthogonalProjection_eq_zero_iff {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} :
    dist p (orthogonalProjection s p) = 0 ‚Üî p ‚àà s := by
  rw [dist_comm, dist_eq_zero, orthogonalProjection_eq_self_iff]

/-- The distance between a point and its orthogonal projection is
nonzero if it does not lie in the subspace. -/
theorem dist_orthogonalProjection_ne_zero_of_notMem {s : AffineSubspace ùïú P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} (hp : p ‚àâ s) :
    dist p (orthogonalProjection s p) ‚â† 0 :=
  mt dist_orthogonalProjection_eq_zero_iff.mp hp

@[deprecated (since := "2025-05-23")]
alias dist_orthogonalProjection_ne_zero_of_not_mem := dist_orthogonalProjection_ne_zero_of_notMem

@[simp] lemma reflection_map (s : AffineSubspace ùïú P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (f : P ‚Üí·µÉ‚Å±[ùïú] P‚ÇÇ)
    [(s.map f.toAffineMap).direction.HasOrthogonalProjection] (p : P) :
    reflection (s.map f.toAffineMap) (f p) = f (reflection s p) := by
  simp [reflection_apply']

lemma reflection_subtype (s : AffineSubspace ùïú P) [Nonempty s] (s' : AffineSubspace ùïú s)
    [Nonempty s'] [s'.direction.HasOrthogonalProjection]
    [(s'.map s.subtype).direction.HasOrthogonalProjection] (p : s) :
    (reflection s' p : P) = reflection (s'.map s.subtype) p := by
  simp [reflection_apply', orthogonalProjection_subtype]

end MetricSpace

end EuclideanGeometry

namespace Affine

namespace Simplex

open EuclideanGeometry

variable {ùïú : Type*} {V : Type*} {P : Type*} [RCLike ùïú]
variable [NormedAddCommGroup V] [InnerProductSpace ùïú V]
variable {V‚ÇÇ P‚ÇÇ : Type*} [NormedAddCommGroup V‚ÇÇ] [InnerProductSpace ùïú V‚ÇÇ]

section PseudoMetricSpace

variable [PseudoMetricSpace P] [NormedAddTorsor V P]

/-- The orthogonal projection of a point `p` onto the hyperplane spanned by the simplex's points. -/
def orthogonalProjectionSpan {n : ‚Ñï} (s : Simplex ùïú P n) :
    P ‚Üí·¥¨[ùïú] affineSpan ùïú (Set.range s.points) :=
  orthogonalProjection (affineSpan ùïú (Set.range s.points))

lemma orthogonalProjectionSpan_congr {m n : ‚Ñï} {s‚ÇÅ : Simplex ùïú P m} {s‚ÇÇ : Simplex ùïú P n}
    {p‚ÇÅ p‚ÇÇ : P} (h : Set.range s‚ÇÅ.points = Set.range s‚ÇÇ.points) (hp : p‚ÇÅ = p‚ÇÇ) :
    (s‚ÇÅ.orthogonalProjectionSpan p‚ÇÅ : P) = s‚ÇÇ.orthogonalProjectionSpan p‚ÇÇ :=
  orthogonalProjection_congr (by rw [h]) hp

@[simp] lemma orthogonalProjectionSpan_reindex {m n : ‚Ñï} (s : Simplex ùïú P m)
    (e : Fin (m + 1) ‚âÉ Fin (n + 1)) (p : P) :
    ((s.reindex e).orthogonalProjectionSpan p : P) = s.orthogonalProjectionSpan p :=
  orthogonalProjectionSpan_congr (s.reindex_range_points e) rfl

/-- Adding a vector to a point in the given subspace, then taking the
orthogonal projection, produces the original point if the vector is a
multiple of the result of subtracting a point's orthogonal projection
from that point. -/
theorem orthogonalProjection_vadd_smul_vsub_orthogonalProjection {n : ‚Ñï} (s : Simplex ùïú P n)
    {p‚ÇÅ : P} (p‚ÇÇ : P) (r : ùïú) (hp : p‚ÇÅ ‚àà affineSpan ùïú (Set.range s.points)) :
    s.orthogonalProjectionSpan (r ‚Ä¢ (p‚ÇÇ -·µ• s.orthogonalProjectionSpan p‚ÇÇ : V) +·µ• p‚ÇÅ) = ‚ü®p‚ÇÅ, hp‚ü© :=
  EuclideanGeometry.orthogonalProjection_vadd_smul_vsub_orthogonalProjection _ _ _

theorem coe_orthogonalProjection_vadd_smul_vsub_orthogonalProjection {n : ‚Ñï} {r‚ÇÅ : ùïú}
    (s : Simplex ùïú P n) {p p‚ÇÅo : P} (hp‚ÇÅo : p‚ÇÅo ‚àà affineSpan ùïú (Set.range s.points)) :
    ‚Üë(s.orthogonalProjectionSpan (r‚ÇÅ ‚Ä¢ (p -·µ• ‚Üë(s.orthogonalProjectionSpan p)) +·µ• p‚ÇÅo)) = p‚ÇÅo :=
  congrArg ((‚Üë) : _ ‚Üí P) (orthogonalProjection_vadd_smul_vsub_orthogonalProjection _ _ _ hp‚ÇÅo)

theorem dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq {n : ‚Ñï}
    (s : Simplex ùïú P n) {p‚ÇÅ : P} (p‚ÇÇ : P) (hp‚ÇÅ : p‚ÇÅ ‚àà affineSpan ùïú (Set.range s.points)) :
    dist p‚ÇÅ p‚ÇÇ * dist p‚ÇÅ p‚ÇÇ =
      dist p‚ÇÅ (s.orthogonalProjectionSpan p‚ÇÇ) * dist p‚ÇÅ (s.orthogonalProjectionSpan p‚ÇÇ) +
        dist p‚ÇÇ (s.orthogonalProjectionSpan p‚ÇÇ) * dist p‚ÇÇ (s.orthogonalProjectionSpan p‚ÇÇ) :=
  EuclideanGeometry.dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq _ hp‚ÇÅ

@[simp]
lemma orthogonalProjectionSpan_eq_point (s : Simplex ùïú P 0) (p : P) :
    s.orthogonalProjectionSpan p = s.points 0 := by
  rw [orthogonalProjectionSpan]
  convert orthogonalProjection_affineSpan_singleton _ _
  simp [Fin.fin_one_eq_zero]

lemma orthogonalProjectionSpan_faceOpposite_eq_point_rev (s : Simplex ùïú P 1) (i : Fin 2)
    (p : P) : (s.faceOpposite i).orthogonalProjectionSpan p = s.points i.rev := by
  simp [faceOpposite_point_eq_point_rev]

end PseudoMetricSpace

section MetricSpace

variable [MetricSpace P] [NormedAddTorsor V P]
variable [MetricSpace P‚ÇÇ] [NormedAddTorsor V‚ÇÇ P‚ÇÇ]

lemma orthogonalProjectionSpan_map {n : ‚Ñï} (s : Simplex ùïú P n) (f : P ‚Üí·µÉ‚Å±[ùïú] P‚ÇÇ) (p : P) :
    (s.map f.toAffineMap f.injective).orthogonalProjectionSpan (f p) =
      f (s.orthogonalProjectionSpan p) := by
  simp_rw [orthogonalProjectionSpan]
  convert orthogonalProjection_map (affineSpan ùïú (Set.range s.points)) f p
  simp [AffineSubspace.map_span, Set.range_comp]

@[simp] lemma orthogonalProjectionSpan_restrict {n : ‚Ñï} (s : Simplex ùïú P n)
    (S : AffineSubspace ùïú P) (hS : affineSpan ùïú (Set.range s.points) ‚â§ S) (p : S) :
    haveI := Nonempty.map (AffineSubspace.inclusion hS) inferInstance
    ((s.restrict S hS).orthogonalProjectionSpan p : P) = s.orthogonalProjectionSpan p := by
  rw [eq_comm]
  convert (s.restrict S hS).orthogonalProjectionSpan_map S.subtype‚Çê·µ¢ p

end MetricSpace

end Simplex

end Affine
