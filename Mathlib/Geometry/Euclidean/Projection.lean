/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, Manuel Candales
-/
module

public import Mathlib.Analysis.InnerProductSpace.Projection.Reflection
public import Mathlib.Analysis.InnerProductSpace.Projection.Submodule
public import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional

/-!
# Orthogonal projection in affine spaces

This file defines orthogonal projection onto an affine subspace,
and reflection of a point in an affine subspace.

## Main definitions

* `EuclideanGeometry.orthogonalProjection` is the orthogonal
  projection of a point onto an affine subspace.

* `EuclideanGeometry.reflection` is the reflection of a point in an
  affine subspace.

-/

@[expose] public section

noncomputable section

namespace EuclideanGeometry

variable {ğ•œ : Type*} {V : Type*} {P : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
variable {Vâ‚‚ Pâ‚‚ : Type*} [NormedAddCommGroup Vâ‚‚] [InnerProductSpace ğ•œ Vâ‚‚]

open AffineSubspace

variable [MetricSpace P] [NormedAddTorsor V P]

/-- The orthogonal projection of a point onto a nonempty affine subspace. -/
def orthogonalProjection (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] : P â†’á´¬[ğ•œ] s :=
  letI x := Classical.arbitrary s
  AffineIsometryEquiv.vaddConst ğ•œ x
    |>.toContinuousAffineEquiv.toContinuousAffineMap.comp
      s.direction.orthogonalProjection.toContinuousAffineMap
    |>.comp <| AffineIsometryEquiv.vaddConst ğ•œ (x : P) |>.symm

theorem orthogonalProjection_apply (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p} :
    orthogonalProjection s p = s.direction.orthogonalProjection (p -áµ¥ Classical.arbitrary s)
      +áµ¥ Classical.arbitrary s :=
  rfl

theorem orthogonalProjection_apply' (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p} :
    (orthogonalProjection s p : P) =
      (s.direction.orthogonalProjection (p -áµ¥ Classical.arbitrary s) : V) +áµ¥
      (Classical.arbitrary s : P) :=
  rfl

theorem orthogonalProjection_apply_mem (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p x} (hx : x âˆˆ s) :
    orthogonalProjection s p = (s.direction.orthogonalProjection (p -áµ¥ x) : V) +áµ¥ x := by
  rw [orthogonalProjection_apply, coe_vadd, vadd_eq_vadd_iff_sub_eq_vsub, â† Submodule.coe_sub,
    â† map_sub, vsub_sub_vsub_cancel_left, Submodule.coe_orthogonalProjection_apply,
    Submodule.starProjection_eq_self_iff]
  exact s.vsub_mem_direction (SetLike.coe_mem _) hx

/-- Since both instance arguments are propositions, allow `simp` to rewrite them
alongside the `s` argument.

Note that without the coercion to `P`, the LHS and RHS would have different types. -/
@[congr]
theorem orthogonalProjection_congr {sâ‚ sâ‚‚ : AffineSubspace ğ•œ P} {pâ‚ pâ‚‚ : P}
    [Nonempty sâ‚] [sâ‚.direction.HasOrthogonalProjection]
    (h : sâ‚ = sâ‚‚) (hp : pâ‚ = pâ‚‚) :
    letI : Nonempty sâ‚‚ := h â–¸ â€¹_â€º
    letI : sâ‚‚.direction.HasOrthogonalProjection := h â–¸ â€¹_â€º
    (orthogonalProjection sâ‚ pâ‚ : P) = (orthogonalProjection sâ‚‚ pâ‚‚ : P) := by
  subst h hp
  rfl

/-- The linear map corresponding to `orthogonalProjection`. -/
@[simp]
theorem orthogonalProjection_linear {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] :
    (orthogonalProjection s).linear = s.direction.orthogonalProjection :=
  rfl

/-- The continuous linear map corresponding to `orthogonalProjection`. -/
@[simp]
theorem orthogonalProjection_contLinear {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] :
    (orthogonalProjection s).contLinear = s.direction.orthogonalProjection :=
  rfl

/-- The `orthogonalProjection` lies in the given subspace. -/
theorem orthogonalProjection_mem {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : â†‘(orthogonalProjection s p) âˆˆ s :=
  (orthogonalProjection s p).2

/-- The `orthogonalProjection` lies in the orthogonal subspace. -/
theorem orthogonalProjection_mem_orthogonal (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    â†‘(orthogonalProjection s p) âˆˆ mk' p s.directioná—® := by
  rw [mem_mk', orthogonalProjection_apply, coe_vadd, vadd_vsub_eq_sub_vsub,
    â† Submodule.neg_mem_iff, neg_sub]
  apply Submodule.sub_starProjection_mem_orthogonal

/-- The intersection of the subspace and the orthogonal subspace
through the given point is the `orthogonalProjection` of that point
onto the subspace. -/
theorem inter_eq_singleton_orthogonalProjection {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    (s : Set P) âˆ© mk' p s.directioná—® = {â†‘(orthogonalProjection s p)} := by
  obtain âŸ¨q, hqâŸ© := inter_eq_singleton_of_nonempty_of_isCompl (nonempty_subtype.mp â€¹_â€º)
    (mk'_nonempty p s.directioná—®)
    (by
      rw [direction_mk' p s.directioná—®]
      exact Submodule.isCompl_orthogonal_of_hasOrthogonalProjection)
  rwa [Set.eq_singleton_iff_nonempty_unique_mem.1 hq |>.2 _
    âŸ¨orthogonalProjection_mem _, orthogonalProjection_mem_orthogonal _ _âŸ©]

/-- Subtracting a point in the given subspace from the
`orthogonalProjection` produces a result in the direction of the
given subspace. -/
theorem orthogonalProjection_vsub_mem_direction {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {pâ‚ : P} (pâ‚‚ : P) (hpâ‚ : pâ‚ âˆˆ s) :
    â†‘(orthogonalProjection s pâ‚‚ -áµ¥ âŸ¨pâ‚, hpâ‚âŸ© : s.direction) âˆˆ s.direction :=
  (orthogonalProjection s pâ‚‚ -áµ¥ âŸ¨pâ‚, hpâ‚âŸ© : s.direction).2

/-- Subtracting the `orthogonalProjection` from a point in the given
subspace produces a result in the direction of the given subspace. -/
theorem vsub_orthogonalProjection_mem_direction {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {pâ‚ : P} (pâ‚‚ : P) (hpâ‚ : pâ‚ âˆˆ s) :
    â†‘((âŸ¨pâ‚, hpâ‚âŸ© : s) -áµ¥ orthogonalProjection s pâ‚‚ : s.direction) âˆˆ s.direction :=
  ((âŸ¨pâ‚, hpâ‚âŸ© : s) -áµ¥ orthogonalProjection s pâ‚‚ : s.direction).2

/-- A point equals its orthogonal projection if and only if it lies in
the subspace. -/
theorem orthogonalProjection_eq_self_iff {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} : â†‘(orthogonalProjection s p) = p â†” p âˆˆ s := by
  constructor
  Â· exact fun h => h â–¸ orthogonalProjection_mem p
  Â· intro h
    have hp : p âˆˆ (s : Set P) âˆ© mk' p s.directioná—® := âŸ¨h, self_mem_mk' p _âŸ©
    rw [inter_eq_singleton_orthogonalProjection p] at hp
    symm
    exact hp

@[simp]
theorem orthogonalProjection_mem_subspace_eq_self {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : s) : orthogonalProjection s p = p := by
  ext
  rw [orthogonalProjection_eq_self_iff]
  exact p.2

/-- Orthogonal projection is idempotent. -/
theorem orthogonalProjection_orthogonalProjection (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    orthogonalProjection s (orthogonalProjection s p) = orthogonalProjection s p :=
  orthogonalProjection_mem_subspace_eq_self ((orthogonalProjection s) p)

theorem eq_orthogonalProjection_of_eq_subspace {s s' : AffineSubspace ğ•œ P} [Nonempty s]
    [Nonempty s'] [s.direction.HasOrthogonalProjection] [s'.direction.HasOrthogonalProjection]
    (h : s = s') (p : P) : (orthogonalProjection s p : P) = (orthogonalProjection s' p : P) := by
  subst h
  rfl

@[simp] lemma orthogonalProjection_affineSpan_singleton (pâ‚ pâ‚‚ : P) :
    orthogonalProjection (affineSpan ğ•œ {pâ‚}) pâ‚‚ = pâ‚ := by
  have h := SetLike.coe_mem (orthogonalProjection (affineSpan ğ•œ {pâ‚}) pâ‚‚)
  rwa [mem_affineSpan_singleton] at h

/-- The distance to a point's orthogonal projection is 0 iff it lies in the subspace. -/
theorem dist_orthogonalProjection_eq_zero_iff {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} :
    dist p (orthogonalProjection s p) = 0 â†” p âˆˆ s := by
  rw [dist_comm, dist_eq_zero, orthogonalProjection_eq_self_iff]

/-- The distance between a point and its orthogonal projection is
nonzero if it does not lie in the subspace. -/
theorem dist_orthogonalProjection_ne_zero_of_notMem {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} (hp : p âˆ‰ s) :
    dist p (orthogonalProjection s p) â‰  0 :=
  mt dist_orthogonalProjection_eq_zero_iff.mp hp

/-- Subtracting `p` from its `orthogonalProjection` produces a result
in the orthogonal direction. -/
theorem orthogonalProjection_vsub_mem_direction_orthogonal (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    (orthogonalProjection s p : P) -áµ¥ p âˆˆ s.directioná—® := by
  rw [â† mem_mk']
  apply orthogonalProjection_mem_orthogonal

/-- Subtracting the `orthogonalProjection` from `p` produces a result
in the orthogonal direction. -/
theorem vsub_orthogonalProjection_mem_direction_orthogonal (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : p -áµ¥ orthogonalProjection s p âˆˆ s.directioná—® :=
  direction_mk' p s.directioná—® â–¸
    vsub_mem_direction (self_mem_mk' _ _) (orthogonalProjection_mem_orthogonal s p)

/-- Subtracting the `orthogonalProjection` from `p` produces a result in the kernel of the linear
part of the orthogonal projection. -/
theorem orthogonalProjection_vsub_orthogonalProjection (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    s.direction.orthogonalProjection (p -áµ¥ orthogonalProjection s p) = 0 := by
  simpa using vsub_orthogonalProjection_mem_direction_orthogonal _ _

/-- The characteristic property of the orthogonal projection, for a point given in the underlying
space. This form is typically more convenient to use than
`inter_eq_singleton_orthogonalProjection`. -/
lemma coe_orthogonalProjection_eq_iff_mem {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p q : P} :
    orthogonalProjection s p = q â†” q âˆˆ s âˆ§ p -áµ¥ q âˆˆ s.directioná—® := by
  constructor
  Â· rintro rfl
    exact âŸ¨orthogonalProjection_mem _, vsub_orthogonalProjection_mem_direction_orthogonal _ _âŸ©
  Â· rintro âŸ¨hqs, hpqâŸ©
    have hq : q âˆˆ mk' p s.directioná—® := by
      rwa [mem_mk', â† neg_mem_iff, neg_vsub_eq_vsub_rev]
    suffices q âˆˆ ({(orthogonalProjection s p : P)} : Set P) by
      simpa [eq_comm] using this
    rw [â† inter_eq_singleton_orthogonalProjection]
    simp only [Set.mem_inter_iff, SetLike.mem_coe]
    exact âŸ¨hqs, hqâŸ©

/-- The characteristic property of the orthogonal projection, for a point given in the relevant
subspace. This form is typically more convenient to use than
`inter_eq_singleton_orthogonalProjection`. -/
lemma orthogonalProjection_eq_iff_mem {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} {q : s} :
    orthogonalProjection s p = q â†” p -áµ¥ q âˆˆ s.directioná—® := by
  simpa using coe_orthogonalProjection_eq_iff_mem (s := s) (p := p) (q := (q : P))

/-- A condition for two points to have the same orthogonal projection onto a given subspace. -/
lemma orthogonalProjection_eq_orthogonalProjection_iff_vsub_mem {s : AffineSubspace ğ•œ P}
    [Nonempty s] [s.direction.HasOrthogonalProjection] {p q : P} :
    orthogonalProjection s p = orthogonalProjection s q â†” p -áµ¥ q âˆˆ s.directioná—® := by
  rw [orthogonalProjection_eq_iff_mem, â† s.directioná—®.add_mem_iff_left (x := p -áµ¥ q)
    (vsub_orthogonalProjection_mem_direction_orthogonal s q)]
  simp

/-- If the orthogonal projections of a point onto two subspaces are equal, so is the projection
onto their supremum. -/
lemma orthogonalProjection_sup_of_orthogonalProjection_eq {sâ‚ sâ‚‚ : AffineSubspace ğ•œ P} [Nonempty sâ‚]
    [Nonempty sâ‚‚] [sâ‚.direction.HasOrthogonalProjection] [sâ‚‚.direction.HasOrthogonalProjection]
    {p : P} (h : (orthogonalProjection sâ‚ p : P) = orthogonalProjection sâ‚‚ p)
    [(sâ‚ âŠ” sâ‚‚).direction.HasOrthogonalProjection] :
    (orthogonalProjection (sâ‚ âŠ” sâ‚‚) p : P) = orthogonalProjection sâ‚ p := by
  rw [coe_orthogonalProjection_eq_iff_mem]
  refine âŸ¨SetLike.le_def.1 le_sup_left (orthogonalProjection_mem _), ?_âŸ©
  rw [direction_sup_eq_sup_direction (orthogonalProjection_mem p) (h â–¸ orthogonalProjection_mem p),
    â† Submodule.inf_orthogonal]
  exact âŸ¨vsub_orthogonalProjection_mem_direction_orthogonal _ _,
    h â–¸ vsub_orthogonalProjection_mem_direction_orthogonal _ _âŸ©

/-- Adding a vector to a point in the given subspace, then taking the
orthogonal projection, produces the original point if the vector was
in the orthogonal direction. -/
theorem orthogonalProjection_vadd_eq_self {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} (hp : p âˆˆ s) {v : V} (hv : v âˆˆ s.directioná—®) :
    orthogonalProjection s (v +áµ¥ p) = âŸ¨p, hpâŸ© := by
  have h := vsub_orthogonalProjection_mem_direction_orthogonal s (v +áµ¥ p)
  rw [vadd_vsub_assoc, Submodule.add_mem_iff_right _ hv] at h
  refine (eq_of_vsub_eq_zero ?_).symm
  ext
  refine Submodule.disjoint_def.1 s.direction.orthogonal_disjoint _ ?_ h
  exact (_ : s.direction).2

/-- Adding a vector to a point in the given subspace, then taking the
orthogonal projection, produces the original point if the vector is a
multiple of the result of subtracting a point's orthogonal projection
from that point. -/
theorem orthogonalProjection_vadd_smul_vsub_orthogonalProjection {s : AffineSubspace ğ•œ P}
    [Nonempty s] [s.direction.HasOrthogonalProjection] {pâ‚ : P} (pâ‚‚ : P) (r : ğ•œ) (hp : pâ‚ âˆˆ s) :
    orthogonalProjection s (r â€¢ (pâ‚‚ -áµ¥ orthogonalProjection s pâ‚‚ : V) +áµ¥ pâ‚) = âŸ¨pâ‚, hpâŸ© :=
  orthogonalProjection_vadd_eq_self hp
    (Submodule.smul_mem _ _ (vsub_orthogonalProjection_mem_direction_orthogonal s _))

lemma orthogonalProjection_orthogonalProjection_of_le {sâ‚ sâ‚‚ : AffineSubspace ğ•œ P} [Nonempty sâ‚]
    [Nonempty sâ‚‚] [sâ‚.direction.HasOrthogonalProjection] [sâ‚‚.direction.HasOrthogonalProjection]
    (h : sâ‚ â‰¤ sâ‚‚) (p : P) :
    orthogonalProjection sâ‚ (orthogonalProjection sâ‚‚ p) = orthogonalProjection sâ‚ p := by
  rw [orthogonalProjection_eq_orthogonalProjection_iff_vsub_mem]
  exact SetLike.le_def.1 (Submodule.orthogonal_le (direction_le h))
    (orthogonalProjection_vsub_mem_direction_orthogonal _ _)

/-- The square of the distance from a point in `s` to `pâ‚‚` equals the
sum of the squares of the distances of the two points to the
`orthogonalProjection`. -/
theorem dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq
    {s : AffineSubspace ğ•œ P} [Nonempty s] [s.direction.HasOrthogonalProjection] {pâ‚ : P} (pâ‚‚ : P)
    (hpâ‚ : pâ‚ âˆˆ s) :
    dist pâ‚ pâ‚‚ * dist pâ‚ pâ‚‚ =
      dist pâ‚ (orthogonalProjection s pâ‚‚) * dist pâ‚ (orthogonalProjection s pâ‚‚) +
        dist pâ‚‚ (orthogonalProjection s pâ‚‚) * dist pâ‚‚ (orthogonalProjection s pâ‚‚) := by
  rw [dist_comm pâ‚‚ _, dist_eq_norm_vsub V pâ‚ _, dist_eq_norm_vsub V pâ‚ _, dist_eq_norm_vsub V _ pâ‚‚,
    â† vsub_add_vsub_cancel pâ‚ (orthogonalProjection s pâ‚‚) pâ‚‚,
    norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (ğ•œ := ğ•œ)]
  exact Submodule.inner_right_of_mem_orthogonal (vsub_orthogonalProjection_mem_direction pâ‚‚ hpâ‚)
    (orthogonalProjection_vsub_mem_direction_orthogonal s pâ‚‚)

/-- The distance between a point and its orthogonal projection to a subspace equals the distance
to that subspace as given by `Metric.infDist`. This is not a `simp` lemma since the simplest form
depends on the context (if any calculations are to be done with the distance, the version with
the orthogonal projection gives access to more lemmas about orthogonal projections that may be
useful). -/
lemma dist_orthogonalProjection_eq_infDist (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    dist p (orthogonalProjection s p) = Metric.infDist p s := by
  refine le_antisymm ?_ (Metric.infDist_le_dist_of_mem (orthogonalProjection_mem _))
  rw [Metric.infDist_eq_iInf]
  refine le_ciInf fun x â†¦ le_of_sq_le_sq ?_ dist_nonneg
  rw [dist_comm _ (x : P)]
  simp_rw [pow_two,
    dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq p x.property]
  simp [mul_self_nonneg]

/-- The nonnegative distance between a point and its orthogonal projection to a subspace equals
the distance to that subspace as given by `Metric.infNndist`. This is not a `simp` lemma since
the simplest form depends on the context (if any calculations are to be done with the distance,
the version with the orthogonal projection gives access to more lemmas about orthogonal
projections that may be useful). -/
lemma dist_orthogonalProjection_eq_infNndist (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    nndist p (orthogonalProjection s p) = Metric.infNndist p s := by
  rw [â† NNReal.coe_inj]
  simp [dist_orthogonalProjection_eq_infDist]

/-- The square of the distance between two points constructed by
adding multiples of the same orthogonal vector to points in the same
subspace. -/
theorem dist_sq_smul_orthogonal_vadd_smul_orthogonal_vadd {s : AffineSubspace ğ•œ P} {pâ‚ pâ‚‚ : P}
    (hpâ‚ : pâ‚ âˆˆ s) (hpâ‚‚ : pâ‚‚ âˆˆ s) (râ‚ râ‚‚ : ğ•œ) {v : V} (hv : v âˆˆ s.directioná—®) :
    dist (râ‚ â€¢ v +áµ¥ pâ‚) (râ‚‚ â€¢ v +áµ¥ pâ‚‚) * dist (râ‚ â€¢ v +áµ¥ pâ‚) (râ‚‚ â€¢ v +áµ¥ pâ‚‚) =
      dist pâ‚ pâ‚‚ * dist pâ‚ pâ‚‚ + â€–râ‚ - râ‚‚â€– * â€–râ‚ - râ‚‚â€– * (â€–vâ€– * â€–vâ€–) :=
  calc
    dist (râ‚ â€¢ v +áµ¥ pâ‚) (râ‚‚ â€¢ v +áµ¥ pâ‚‚) * dist (râ‚ â€¢ v +áµ¥ pâ‚) (râ‚‚ â€¢ v +áµ¥ pâ‚‚) =
        â€–pâ‚ -áµ¥ pâ‚‚ + (râ‚ - râ‚‚) â€¢ vâ€– * â€–pâ‚ -áµ¥ pâ‚‚ + (râ‚ - râ‚‚) â€¢ vâ€– := by
      rw [dist_eq_norm_vsub V (râ‚ â€¢ v +áµ¥ pâ‚), vsub_vadd_eq_vsub_sub, vadd_vsub_assoc, sub_smul,
        add_comm, add_sub_assoc]
    _ = â€–pâ‚ -áµ¥ pâ‚‚â€– * â€–pâ‚ -áµ¥ pâ‚‚â€– + â€–(râ‚ - râ‚‚) â€¢ vâ€– * â€–(râ‚ - râ‚‚) â€¢ vâ€– :=
      norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _
        (Submodule.inner_right_of_mem_orthogonal (vsub_mem_direction hpâ‚ hpâ‚‚)
          (Submodule.smul_mem _ _ hv))
    _ = dist pâ‚ pâ‚‚ * dist pâ‚ pâ‚‚ + â€–râ‚ - râ‚‚â€– * â€–râ‚ - râ‚‚â€– * (â€–vâ€– * â€–vâ€–) := by
      rw [norm_smul, dist_eq_norm_vsub V pâ‚]
      ring

/-- `p` is equidistant from two points in `s` if and only if its
`orthogonalProjection` is. -/
theorem dist_eq_iff_dist_orthogonalProjection_eq {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {pâ‚ pâ‚‚ : P} (pâ‚ƒ : P) (hpâ‚ : pâ‚ âˆˆ s) (hpâ‚‚ : pâ‚‚ âˆˆ s) :
    dist pâ‚ pâ‚ƒ = dist pâ‚‚ pâ‚ƒ â†”
      dist pâ‚ (orthogonalProjection s pâ‚ƒ) = dist pâ‚‚ (orthogonalProjection s pâ‚ƒ) := by
  rw [â† mul_self_inj_of_nonneg dist_nonneg dist_nonneg, â†
    mul_self_inj_of_nonneg dist_nonneg dist_nonneg,
    dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq pâ‚ƒ hpâ‚,
    dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq pâ‚ƒ hpâ‚‚]
  simp

/-- `p` is equidistant from a set of points in `s` if and only if its
`orthogonalProjection` is. -/
theorem dist_set_eq_iff_dist_orthogonalProjection_eq {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {ps : Set P} (hps : ps âŠ† s) (p : P) :
    (Set.Pairwise ps fun pâ‚ pâ‚‚ => dist pâ‚ p = dist pâ‚‚ p) â†”
      Set.Pairwise ps fun pâ‚ pâ‚‚ =>
        dist pâ‚ (orthogonalProjection s p) = dist pâ‚‚ (orthogonalProjection s p) :=
  âŸ¨fun h _ hpâ‚ _ hpâ‚‚ hne =>
    (dist_eq_iff_dist_orthogonalProjection_eq p (hps hpâ‚) (hps hpâ‚‚)).1 (h hpâ‚ hpâ‚‚ hne),
    fun h _ hpâ‚ _ hpâ‚‚ hne =>
    (dist_eq_iff_dist_orthogonalProjection_eq p (hps hpâ‚) (hps hpâ‚‚)).2 (h hpâ‚ hpâ‚‚ hne)âŸ©

/-- There exists `r` such that `p` has distance `r` from all the
points of a set of points in `s` if and only if there exists (possibly
different) `r` such that its `orthogonalProjection` has that distance
from all the points in that set. -/
theorem exists_dist_eq_iff_exists_dist_orthogonalProjection_eq {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {ps : Set P} (hps : ps âŠ† s) (p : P) :
    (âˆƒ r, âˆ€ pâ‚ âˆˆ ps, dist pâ‚ p = r) â†” âˆƒ r, âˆ€ pâ‚ âˆˆ ps, dist pâ‚ â†‘(orthogonalProjection s p) = r := by
  have h := dist_set_eq_iff_dist_orthogonalProjection_eq hps p
  simp_rw [Set.pairwise_eq_iff_exists_eq] at h
  exact h

/-- Reflection in an affine subspace, which is expected to be nonempty
and complete. The word "reflection" is sometimes understood to mean
specifically reflection in a codimension-one subspace, and sometimes
more generally to cover operations such as reflection in a point. The
definition here, of reflection in an affine subspace, is a more
general sense of the word that includes both those common cases. -/
def reflection (s : AffineSubspace ğ•œ P) [Nonempty s] [s.direction.HasOrthogonalProjection] :
    P â‰ƒáµƒâ±[ğ•œ] P :=
  letI x : P := Classical.arbitrary s
  AffineIsometryEquiv.vaddConst ğ•œ x
    |>.symm.trans s.direction.reflection.toAffineIsometryEquiv
    |>.trans <| AffineIsometryEquiv.vaddConst ğ•œ x

theorem reflection_apply (s : AffineSubspace ğ•œ P) [Nonempty s] [s.direction.HasOrthogonalProjection]
    (p : P) :
    reflection s p = s.direction.reflection (p -áµ¥ Classical.arbitrary s)
      +áµ¥ (Classical.arbitrary s : P) :=
  rfl

theorem reflection_apply_of_mem (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) {x} (hx : x âˆˆ s) :
    reflection s p = s.direction.reflection (p -áµ¥ x) +áµ¥ x := by
  rw [reflection_apply, vadd_eq_vadd_iff_sub_eq_vsub, â† map_sub,
    vsub_sub_vsub_cancel_left, s.direction.reflection_eq_self_iff]
  exact s.vsub_mem_direction (SetLike.coe_mem _) hx

theorem reflection_apply' (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) :
    reflection s p = (â†‘(orthogonalProjection s p) -áµ¥ p) +áµ¥ (orthogonalProjection s p : P) := by
  rw [reflection_apply, orthogonalProjection_apply', Submodule.coe_orthogonalProjection_apply]
  set x : P := â†‘(Classical.arbitrary s)
  set v : V := s.direction.starProjection (p -áµ¥ x)
  rw [Submodule.reflection_apply, two_smul, sub_eq_add_neg, neg_vsub_eq_vsub_rev, add_assoc,
    add_comm v, add_vadd, vadd_vsub_assoc]

theorem eq_reflection_of_eq_subspace {s s' : AffineSubspace ğ•œ P} [Nonempty s] [Nonempty s']
    [s.direction.HasOrthogonalProjection] [s'.direction.HasOrthogonalProjection] (h : s = s')
    (p : P) : (reflection s p : P) = (reflection s' p : P) := by
  subst h
  rfl

/-- Reflecting twice in the same subspace. -/
@[simp]
theorem reflection_reflection (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : reflection s (reflection s p) = p := by
  simp [reflection, -AffineIsometryEquiv.map_vadd]

/-- Reflection is its own inverse. -/
@[simp]
theorem reflection_symm (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] : (reflection s).symm = reflection s := by
  ext
  rw [â† (reflection s).injective.eq_iff]
  simp

/-- Reflection is involutive. -/
theorem reflection_involutive (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] : Function.Involutive (reflection s) :=
  reflection_reflection s

/-- A point is its own reflection if and only if it is in the subspace. -/
theorem reflection_eq_self_iff {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] (p : P) : reflection s p = p â†” p âˆˆ s := by
  rw [reflection_apply, Eq.comm, eq_vadd_iff_vsub_eq, Eq.comm, s.direction.reflection_eq_self_iff,
    s.mem_direction_iff_eq_vsub_right (SetLike.coe_mem (Classical.arbitrary s))]
  simp

/-- Reflecting a point in two subspaces produces the same result if
and only if the point has the same orthogonal projection in each of
those subspaces. -/
theorem reflection_eq_iff_orthogonalProjection_eq (sâ‚ sâ‚‚ : AffineSubspace ğ•œ P) [Nonempty sâ‚]
    [Nonempty sâ‚‚] [sâ‚.direction.HasOrthogonalProjection] [sâ‚‚.direction.HasOrthogonalProjection]
    (p : P) :
    reflection sâ‚ p = reflection sâ‚‚ p â†”
      (orthogonalProjection sâ‚ p : P) = orthogonalProjection sâ‚‚ p := by
  rw [reflection_apply', reflection_apply']
  constructor
  Â· intro h
    rw [â† @vsub_eq_zero_iff_eq V, vsub_vadd_eq_vsub_sub, vadd_vsub_assoc, add_comm, add_sub_assoc,
      vsub_sub_vsub_cancel_right, â†
      two_smul ğ•œ ((orthogonalProjection sâ‚ p : P) -áµ¥ orthogonalProjection sâ‚‚ p), smul_eq_zero] at h
    norm_num at h
    exact h
  Â· intro h
    rw [h]

/-- The distance between `pâ‚` and the reflection of `pâ‚‚` equals that
between the reflection of `pâ‚` and `pâ‚‚`. -/
theorem dist_reflection (s : AffineSubspace ğ•œ P) [Nonempty s] [s.direction.HasOrthogonalProjection]
    (pâ‚ pâ‚‚ : P) : dist pâ‚ (reflection s pâ‚‚) = dist (reflection s pâ‚) pâ‚‚ := by
  conv_lhs => rw [â† reflection_reflection s pâ‚]
  exact (reflection s).dist_map _ _

/-- A point in the subspace is equidistant from another point and its
reflection. -/
theorem dist_reflection_eq_of_mem (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] {pâ‚ : P} (hpâ‚ : pâ‚ âˆˆ s) (pâ‚‚ : P) :
    dist pâ‚ (reflection s pâ‚‚) = dist pâ‚ pâ‚‚ := by
  rw [â† reflection_eq_self_iff pâ‚] at hpâ‚
  convert (reflection s).dist_map pâ‚ pâ‚‚
  rw [hpâ‚]

/-- The reflection of a point in a subspace is contained in any larger
subspace containing both the point and the subspace reflected in. -/
theorem reflection_mem_of_le_of_mem {sâ‚ sâ‚‚ : AffineSubspace ğ•œ P} [Nonempty sâ‚]
    [sâ‚.direction.HasOrthogonalProjection] (hle : sâ‚ â‰¤ sâ‚‚) {p : P} (hp : p âˆˆ sâ‚‚) :
    reflection sâ‚ p âˆˆ sâ‚‚ := by
  rw [reflection_apply']
  have ho : â†‘(orthogonalProjection sâ‚ p) âˆˆ sâ‚‚ := hle (orthogonalProjection_mem p)
  exact vadd_mem_of_mem_direction (vsub_mem_direction ho hp) ho

/-- Reflecting an orthogonal vector plus a point in the subspace
produces the negation of that vector plus the point. -/
theorem reflection_orthogonal_vadd {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {p : P} (hp : p âˆˆ s) {v : V} (hv : v âˆˆ s.directioná—®) :
    reflection s (v +áµ¥ p) = -v +áµ¥ p := by
  rw [reflection_apply', orthogonalProjection_vadd_eq_self hp hv, vsub_vadd_eq_vsub_sub]
  simp

/-- Reflecting a vector plus a point in the subspace produces the
negation of that vector plus the point if the vector is a multiple of
the result of subtracting a point's orthogonal projection from that
point. -/
theorem reflection_vadd_smul_vsub_orthogonalProjection {s : AffineSubspace ğ•œ P} [Nonempty s]
    [s.direction.HasOrthogonalProjection] {pâ‚ : P} (pâ‚‚ : P) (r : ğ•œ) (hpâ‚ : pâ‚ âˆˆ s) :
    reflection s (r â€¢ (pâ‚‚ -áµ¥ orthogonalProjection s pâ‚‚) +áµ¥ pâ‚) =
      -(r â€¢ (pâ‚‚ -áµ¥ orthogonalProjection s pâ‚‚)) +áµ¥ pâ‚ :=
  reflection_orthogonal_vadd hpâ‚
    (Submodule.smul_mem _ _ (vsub_orthogonalProjection_mem_direction_orthogonal s _))

variable [MetricSpace Pâ‚‚] [NormedAddTorsor Vâ‚‚ Pâ‚‚]

@[simp] lemma orthogonalProjection_map (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚)
    [(s.map f.toAffineMap).direction.HasOrthogonalProjection] (p : P) :
    orthogonalProjection (s.map f.toAffineMap) (f p) = f (orthogonalProjection s p) := by
  rw [coe_orthogonalProjection_eq_iff_mem]
  simp only [mem_map, AffineIsometry.coe_toAffineMap, AffineIsometry.map_eq_iff, exists_eq_right,
    SetLike.coe_mem, map_direction, AffineIsometry.linear_eq_linearIsometry, true_and]
  rw [â† AffineIsometry.coe_toAffineMap, â† AffineMap.linearMap_vsub, Submodule.mem_orthogonal]
  intro u hu
  rw [Submodule.mem_map] at hu
  obtain âŸ¨v, hv, rflâŸ© := hu
  rw [AffineIsometry.linear_eq_linearIsometry, LinearIsometry.coe_toLinearMap,
    LinearIsometry.inner_map_map, Submodule.inner_right_of_mem_orthogonal hv
      (vsub_orthogonalProjection_mem_direction_orthogonal _ _)]

lemma orthogonalProjection_subtype (s : AffineSubspace ğ•œ P) [Nonempty s] (s' : AffineSubspace ğ•œ s)
    [Nonempty s'] [s'.direction.HasOrthogonalProjection]
    [(s'.map s.subtype).direction.HasOrthogonalProjection] (p : s) :
    (orthogonalProjection s' p : P) = orthogonalProjection (s'.map s.subtype) p := by
  rw [eq_comm]
  have : (s'.map s.subtypeâ‚áµ¢.toAffineMap).direction.HasOrthogonalProjection := by
    rw [subtypeâ‚áµ¢_toAffineMap]
    infer_instance
  convert orthogonalProjection_map s' s.subtypeâ‚áµ¢ p

@[simp] lemma reflection_map (s : AffineSubspace ğ•œ P) [Nonempty s]
    [s.direction.HasOrthogonalProjection] (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚)
    [(s.map f.toAffineMap).direction.HasOrthogonalProjection] (p : P) :
    reflection (s.map f.toAffineMap) (f p) = f (reflection s p) := by
  simp [reflection_apply']

lemma reflection_subtype (s : AffineSubspace ğ•œ P) [Nonempty s] (s' : AffineSubspace ğ•œ s)
    [Nonempty s'] [s'.direction.HasOrthogonalProjection]
    [(s'.map s.subtype).direction.HasOrthogonalProjection] (p : s) :
    (reflection s' p : P) = reflection (s'.map s.subtype) p := by
  simp [reflection_apply', orthogonalProjection_subtype]

end EuclideanGeometry

namespace Affine

namespace Simplex

open EuclideanGeometry

variable {ğ•œ : Type*} {V : Type*} {P : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
variable {Vâ‚‚ Pâ‚‚ : Type*} [NormedAddCommGroup Vâ‚‚] [InnerProductSpace ğ•œ Vâ‚‚]

variable [MetricSpace P] [NormedAddTorsor V P]

/-- The orthogonal projection of a point `p` onto the hyperplane spanned by the simplex's points. -/
def orthogonalProjectionSpan {n : â„•} (s : Simplex ğ•œ P n) :
    P â†’á´¬[ğ•œ] affineSpan ğ•œ (Set.range s.points) :=
  orthogonalProjection (affineSpan ğ•œ (Set.range s.points))

lemma orthogonalProjectionSpan_congr {m n : â„•} {sâ‚ : Simplex ğ•œ P m} {sâ‚‚ : Simplex ğ•œ P n}
    {pâ‚ pâ‚‚ : P} (h : Set.range sâ‚.points = Set.range sâ‚‚.points) (hp : pâ‚ = pâ‚‚) :
    (sâ‚.orthogonalProjectionSpan pâ‚ : P) = sâ‚‚.orthogonalProjectionSpan pâ‚‚ :=
  orthogonalProjection_congr (by rw [h]) hp

@[simp] lemma orthogonalProjectionSpan_reindex {m n : â„•} (s : Simplex ğ•œ P m)
    (e : Fin (m + 1) â‰ƒ Fin (n + 1)) (p : P) :
    ((s.reindex e).orthogonalProjectionSpan p : P) = s.orthogonalProjectionSpan p :=
  orthogonalProjectionSpan_congr (s.reindex_range_points e) rfl

/-- Adding a vector to a point in the given subspace, then taking the
orthogonal projection, produces the original point if the vector is a
multiple of the result of subtracting a point's orthogonal projection
from that point. -/
theorem orthogonalProjection_vadd_smul_vsub_orthogonalProjection {n : â„•} (s : Simplex ğ•œ P n)
    {pâ‚ : P} (pâ‚‚ : P) (r : ğ•œ) (hp : pâ‚ âˆˆ affineSpan ğ•œ (Set.range s.points)) :
    s.orthogonalProjectionSpan (r â€¢ (pâ‚‚ -áµ¥ s.orthogonalProjectionSpan pâ‚‚ : V) +áµ¥ pâ‚) = âŸ¨pâ‚, hpâŸ© :=
  EuclideanGeometry.orthogonalProjection_vadd_smul_vsub_orthogonalProjection _ _ _

theorem coe_orthogonalProjection_vadd_smul_vsub_orthogonalProjection {n : â„•} {râ‚ : ğ•œ}
    (s : Simplex ğ•œ P n) {p pâ‚o : P} (hpâ‚o : pâ‚o âˆˆ affineSpan ğ•œ (Set.range s.points)) :
    â†‘(s.orthogonalProjectionSpan (râ‚ â€¢ (p -áµ¥ â†‘(s.orthogonalProjectionSpan p)) +áµ¥ pâ‚o)) = pâ‚o :=
  congrArg ((â†‘) : _ â†’ P) (orthogonalProjection_vadd_smul_vsub_orthogonalProjection _ _ _ hpâ‚o)

theorem dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq {n : â„•}
    (s : Simplex ğ•œ P n) {pâ‚ : P} (pâ‚‚ : P) (hpâ‚ : pâ‚ âˆˆ affineSpan ğ•œ (Set.range s.points)) :
    dist pâ‚ pâ‚‚ * dist pâ‚ pâ‚‚ =
      dist pâ‚ (s.orthogonalProjectionSpan pâ‚‚) * dist pâ‚ (s.orthogonalProjectionSpan pâ‚‚) +
        dist pâ‚‚ (s.orthogonalProjectionSpan pâ‚‚) * dist pâ‚‚ (s.orthogonalProjectionSpan pâ‚‚) :=
  EuclideanGeometry.dist_sq_eq_dist_orthogonalProjection_sq_add_dist_orthogonalProjection_sq _ hpâ‚

@[simp]
lemma orthogonalProjectionSpan_eq_point (s : Simplex ğ•œ P 0) (p : P) :
    s.orthogonalProjectionSpan p = s.points 0 := by
  rw [orthogonalProjectionSpan]
  convert orthogonalProjection_affineSpan_singleton _ _
  simp [Fin.fin_one_eq_zero]

lemma orthogonalProjectionSpan_faceOpposite_eq_point_rev (s : Simplex ğ•œ P 1) (i : Fin 2)
    (p : P) : (s.faceOpposite i).orthogonalProjectionSpan p = s.points i.rev := by
  simp [faceOpposite_point_eq_point_rev]

variable [MetricSpace Pâ‚‚] [NormedAddTorsor Vâ‚‚ Pâ‚‚]

lemma orthogonalProjectionSpan_map {n : â„•} (s : Simplex ğ•œ P n) (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚) (p : P) :
    (s.map f.toAffineMap f.injective).orthogonalProjectionSpan (f p) =
      f (s.orthogonalProjectionSpan p) := by
  simp_rw [orthogonalProjectionSpan]
  convert orthogonalProjection_map (affineSpan ğ•œ (Set.range s.points)) f p
  simp [AffineSubspace.map_span, Set.range_comp]

@[simp] lemma orthogonalProjectionSpan_restrict {n : â„•} (s : Simplex ğ•œ P n)
    (S : AffineSubspace ğ•œ P) (hS : affineSpan ğ•œ (Set.range s.points) â‰¤ S) (p : S) :
    haveI := Nonempty.map (AffineSubspace.inclusion hS) inferInstance
    ((s.restrict S hS).orthogonalProjectionSpan p : P) = s.orthogonalProjectionSpan p := by
  rw [eq_comm]
  convert (s.restrict S hS).orthogonalProjectionSpan_map S.subtypeâ‚áµ¢ p

end Simplex

end Affine
