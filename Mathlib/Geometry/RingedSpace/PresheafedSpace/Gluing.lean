/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Topology.Gluing
import Mathlib.Geometry.RingedSpace.OpenImmersion
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace.HasColimits

#align_import algebraic_geometry.presheafed_space.gluing from "leanprover-community/mathlib"@"533f62f4dd62a5aad24a04326e6e787c8f7e98b1"

/-!
# Gluing Structured spaces

Given a family of gluing data of structured spaces (presheafed spaces, sheafed spaces, or locally
ringed spaces), we may glue them together.

The construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `AlgebraicGeometry.PresheafedSpace.GlueData`: A structure containing the family of gluing data.
* `CategoryTheory.GlueData.glued`: The glued presheafed space.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `CategoryTheory.GlueData.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : J`.

## Main results

* `AlgebraicGeometry.PresheafedSpace.GlueData.Î¹IsOpenImmersion`: The map `Î¹ i : U i âŸ¶ glued`
  is an open immersion for each `i : J`.
* `AlgebraicGeometry.PresheafedSpace.GlueData.Î¹_jointly_surjective` : The underlying maps of
  `Î¹ i : U i âŸ¶ glued` are jointly surjective.
* `AlgebraicGeometry.PresheafedSpace.GlueData.vPullbackConeIsLimit` : `V i j` is the pullback
  (intersection) of `U i` and `U j` over the glued space.

Analogous results are also provided for `SheafedSpace` and `LocallyRingedSpace`.

## Implementation details

Almost the whole file is dedicated to showing tht `Î¹ i` is an open immersion. The fact that
this is an open embedding of topological spaces follows from `Mathlib/Topology/Gluing.lean`, and it
remains to construct `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_X, Î¹ i '' U)` for each `U âŠ† U i`.
Since `Î“(ğ’ª_X, Î¹ i '' U)` is the limit of `diagram_over_open`, the components of the structure
sheafs of the spaces in the gluing diagram, we need to construct a map
`Î¹InvApp_Ï€_app : Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_V, U_V)` for each `V` in the gluing diagram.

We will refer to ![this diagram](https://i.imgur.com/P0phrwr.png) in the following doc strings.
The `X` is the glued space, and the dotted arrow is a partial inverse guaranteed by the fact
that it is an open immersion. The map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_{U_j}, _)` is given by the composition
of the red arrows, and the map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_{V_{jk}}, _)` is given by the composition of the
blue arrows. To lift this into a map from `Î“(ğ’ª_X, Î¹ i '' U)`, we also need to show that these
commute with the maps in the diagram (the green arrows), which is just a lengthy diagram-chasing.

-/

set_option linter.uppercaseLean3 false

noncomputable section

open TopologicalSpace CategoryTheory Opposite

open CategoryTheory.Limits AlgebraicGeometry.PresheafedSpace

open AlgebraicGeometry.PresheafedSpace.IsOpenImmersion

open CategoryTheory.GlueData

namespace AlgebraicGeometry

universe v u

variable (C : Type u) [Category.{v} C]

namespace PresheafedSpace

/-- A family of gluing data consists of
1. An index type `J`
2. A presheafed space `U i` for each `i : J`.
3. A presheafed space `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ PresheafedSpace C` rather than `J â†’ J â†’ PresheafedSpace C` to
  connect to the limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.
-/
-- Porting note(#5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
structure GlueData extends GlueData (PresheafedSpace.{u, v, v} C) where
  f_open : âˆ€ i j, IsOpenImmersion (f i j)
#align algebraic_geometry.PresheafedSpace.glue_data AlgebraicGeometry.PresheafedSpace.GlueData

attribute [instance] GlueData.f_open

namespace GlueData

variable {C}
variable (D : GlueData.{v, u} C)

local notation "ğ–£" => D.toGlueData

local notation "Ï€â‚ " i ", " j ", " k => @pullback.fst _ _ _ _ _ (D.f i j) (D.f i k) _

local notation "Ï€â‚‚ " i ", " j ", " k => @pullback.snd _ _ _ _ _ (D.f i j) (D.f i k) _

set_option quotPrecheck false
local notation "Ï€â‚â»Â¹ " i ", " j ", " k =>
  (PresheafedSpace.IsOpenImmersion.pullbackFstOfRight (D.f i j) (D.f i k)).invApp

set_option quotPrecheck false
local notation "Ï€â‚‚â»Â¹ " i ", " j ", " k =>
  (PresheafedSpace.IsOpenImmersion.pullbackSndOfLeft (D.f i j) (D.f i k)).invApp

/-- The glue data of topological spaces associated to a family of glue data of PresheafedSpaces. -/
abbrev toTopGlueData : TopCat.GlueData :=
  { f_open := fun i j => (D.f_open i j).base_open
    toGlueData := ğ–£.mapGlueData (forget C) }
#align algebraic_geometry.PresheafedSpace.glue_data.to_Top_glue_data AlgebraicGeometry.PresheafedSpace.GlueData.toTopGlueData

theorem Î¹_openEmbedding [HasLimits C] (i : D.J) : OpenEmbedding (ğ–£.Î¹ i).base := by
  rw [â† show _ = (ğ–£.Î¹ i).base from ğ–£.Î¹_gluedIso_inv (PresheafedSpace.forget _) _]
  -- Porting note: added this erewrite
  erw [coe_comp]
  refine
    OpenEmbedding.comp
      (TopCat.homeoOfIso (ğ–£.gluedIso (PresheafedSpace.forget _)).symm).openEmbedding
      (D.toTopGlueData.Î¹_openEmbedding i)
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_open_embedding AlgebraicGeometry.PresheafedSpace.GlueData.Î¹_openEmbedding

theorem pullback_base (i j k : D.J) (S : Set (D.V (i, j)).carrier) :
    (Ï€â‚‚ i, j, k) '' ((Ï€â‚ i, j, k) â»Â¹' S) = D.f i k â»Â¹' (D.f i j '' S) := by
  have eqâ‚ : _ = (Ï€â‚ i, j, k).base := PreservesPullback.iso_hom_fst (forget C) _ _
  have eqâ‚‚ : _ = (Ï€â‚‚ i, j, k).base := PreservesPullback.iso_hom_snd (forget C) _ _
  rw [â† eqâ‚, â† eqâ‚‚]
  -- Porting note: `rw` to `erw` on `coe_comp`
  erw [coe_comp]
  rw [Set.image_comp]
  -- Porting note: `rw` to `erw` on `coe_comp`
  erw [coe_comp]
  erw [Set.preimage_comp, Set.image_preimage_eq, TopCat.pullback_snd_image_fst_preimage]
   -- now `erw` after #13170
  Â· rfl
  erw [â† TopCat.epi_iff_surjective] -- now `erw` after #13170
  infer_instance
#align algebraic_geometry.PresheafedSpace.glue_data.pullback_base AlgebraicGeometry.PresheafedSpace.GlueData.pullback_base

/-- The red and the blue arrows in ![this diagram](https://i.imgur.com/0GiBUh6.png) commute. -/
@[simp, reassoc]
theorem f_invApp_f_app (i j k : D.J) (U : Opens (D.V (i, j)).carrier) :
    (D.f_open i j).invApp U â‰« (D.f i k).c.app _ =
      (Ï€â‚ i, j, k).c.app (op U) â‰«
        (Ï€â‚‚â»Â¹ i, j, k) (unop _) â‰«
          (D.V _).presheaf.map
            (eqToHom
              (by
                delta IsOpenImmersion.openFunctor
                dsimp only [Functor.op, IsOpenMap.functor, Opens.map, unop_op]
                congr
                apply pullback_base)) := by
  have := PresheafedSpace.congr_app (@pullback.condition _ _ _ _ _ (D.f i j) (D.f i k) _)
  dsimp only [comp_c_app] at this
  rw [â† cancel_epi (inv ((D.f_open i j).invApp U)), IsIso.inv_hom_id_assoc,
    IsOpenImmersion.inv_invApp]
  simp_rw [Category.assoc]
  erw [(Ï€â‚ i, j, k).c.naturality_assoc, reassoc_of% this, â† Functor.map_comp_assoc,
    IsOpenImmersion.inv_naturality_assoc, IsOpenImmersion.app_invApp_assoc, â†
    (D.V (i, k)).presheaf.map_comp, â† (D.V (i, k)).presheaf.map_comp]
  -- Porting note: need to provide an explicit argument, otherwise Lean does not know which
  -- category we are talking about
  convert (Category.comp_id ((f D.toGlueData i k).c.app _)).symm
  erw [(D.V (i, k)).presheaf.map_id]
  rfl
#align algebraic_geometry.PresheafedSpace.glue_data.f_inv_app_f_app AlgebraicGeometry.PresheafedSpace.GlueData.f_invApp_f_app

set_option backward.isDefEq.lazyWhnfCore false in -- See https://github.com/leanprover-community/mathlib4/issues/12534
/-- We can prove the `eq` along with the lemma. Thus this is bundled together here, and the
lemma itself is separated below.
-/
theorem snd_invApp_t_app' (i j k : D.J) (U : Opens (pullback (D.f i j) (D.f i k)).carrier) :
    âˆƒ eq,
      (Ï€â‚‚â»Â¹ i, j, k) U â‰« (D.t k i).c.app _ â‰« (D.V (k, i)).presheaf.map (eqToHom eq) =
        (D.t' k i j).c.app _ â‰« (Ï€â‚â»Â¹ k, j, i) (unop _) := by
  fconstructor
  -- Porting note: I don't know what the magic was in Lean3 proof, it just skipped the proof of `eq`
  Â· delta IsOpenImmersion.openFunctor
    dsimp only [Functor.op, Opens.map, IsOpenMap.functor, unop_op, Opens.coe_mk]
    congr
    have := (ğ–£.t_fac k i j).symm
    rw [â† IsIso.inv_comp_eq] at this
    replace this := (congr_arg ((PresheafedSpace.Hom.base Â·)) this).symm
    replace this := congr_arg (ContinuousMap.toFun Â·) this
    dsimp at this
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [coe_comp, coe_comp] at this
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [this, Set.image_comp, Set.image_comp, Set.preimage_image_eq]
    swap
    Â· refine Function.HasLeftInverse.injective âŸ¨(D.t i k).base, fun x => ?_âŸ©
      erw [â† comp_apply, â† comp_base, D.t_inv, id_base, id_apply] -- now `erw` after #13170
    refine congr_arg (_ '' Â·) ?_
    refine congr_fun ?_ _
    refine Set.image_eq_preimage_of_inverse ?_ ?_
    Â· intro x
      erw [â† comp_apply, â† comp_base, IsIso.inv_hom_id, id_base, id_apply] -- now `erw` after #13170
    Â· intro x
      erw [â† comp_apply, â† comp_base, IsIso.hom_inv_id, id_base, id_apply] -- now `erw` after #13170
  Â· rw [â† IsIso.eq_inv_comp, IsOpenImmersion.inv_invApp, Category.assoc,
      (D.t' k i j).c.naturality_assoc]
    simp_rw [â† Category.assoc]
    erw [â† comp_c_app]
    rw [congr_app (D.t_fac k i j), comp_c_app]
    simp_rw [Category.assoc]
    erw [IsOpenImmersion.inv_naturality, IsOpenImmersion.inv_naturality_assoc,
      IsOpenImmersion.app_inv_app'_assoc]
    Â· simp_rw [â† (ğ–£.V (k, i)).presheaf.map_comp]; rfl
    rintro x âŸ¨y, -, eqâŸ©
    replace eq := ConcreteCategory.congr_arg (ğ–£.t i k).base eq
    change ((Ï€â‚‚ i, j, k) â‰« D.t i k).base y = (D.t k i â‰« D.t i k).base x at eq
    rw [ğ–£.t_inv, id_base, TopCat.id_app] at eq
    subst eq
    use (inv (D.t' k i j)).base y
    change (inv (D.t' k i j) â‰« Ï€â‚ k, i, j).base y = _
    congr 2
    rw [IsIso.inv_comp_eq, ğ–£.t_fac_assoc, ğ–£.t_inv, Category.comp_id]
#align algebraic_geometry.PresheafedSpace.glue_data.snd_inv_app_t_app' AlgebraicGeometry.PresheafedSpace.GlueData.snd_invApp_t_app'

set_option backward.isDefEq.lazyWhnfCore false in -- See https://github.com/leanprover-community/mathlib4/issues/12534
/-- The red and the blue arrows in ![this diagram](https://i.imgur.com/q6X1GJ9.png) commute. -/
@[simp, reassoc]
theorem snd_invApp_t_app (i j k : D.J) (U : Opens (pullback (D.f i j) (D.f i k)).carrier) :
    (Ï€â‚‚â»Â¹ i, j, k) U â‰« (D.t k i).c.app _ =
      (D.t' k i j).c.app _ â‰«
        (Ï€â‚â»Â¹ k, j, i) (unop _) â‰«
          (D.V (k, i)).presheaf.map (eqToHom (D.snd_invApp_t_app' i j k U).choose.symm) := by
  have e := (D.snd_invApp_t_app' i j k U).choose_spec
  replace e := reassoc_of% e
  rw [â† e]
  simp [eqToHom_map]
#align algebraic_geometry.PresheafedSpace.glue_data.snd_inv_app_t_app AlgebraicGeometry.PresheafedSpace.GlueData.snd_invApp_t_app

variable [HasLimits C]

theorem Î¹_image_preimage_eq (i j : D.J) (U : Opens (D.U i).carrier) :
    (Opens.map (ğ–£.Î¹ j).base).obj ((D.Î¹_openEmbedding i).isOpenMap.functor.obj U) =
      (openFunctor (D.f j i)).obj
        ((Opens.map (ğ–£.t j i).base).obj ((Opens.map (ğ–£.f i j).base).obj U)) := by
  ext1
  dsimp only [Opens.map_coe, IsOpenMap.functor_obj_coe]
  rw [â† show _ = (ğ–£.Î¹ i).base from ğ–£.Î¹_gluedIso_inv (PresheafedSpace.forget _) i, â†
    show _ = (ğ–£.Î¹ j).base from ğ–£.Î¹_gluedIso_inv (PresheafedSpace.forget _) j]
  -- Porting note (#11224): change `rw` to `erw` on `coe_comp`
  erw [coe_comp, coe_comp, coe_comp]
  rw [Set.image_comp, Set.preimage_comp]
  erw [Set.preimage_image_eq]
  Â· refine Eq.trans (D.toTopGlueData.preimage_image_eq_image' _ _ _) ?_
    dsimp
    rw [Set.image_comp]
    refine congr_arg (_ '' Â·) ?_
    rw [Set.eq_preimage_iff_image_eq, â† Set.image_comp]
    swap
    Â· exact CategoryTheory.ConcreteCategory.bijective_of_isIso (C := TopCat) _
    change (D.t i j â‰« D.t j i).base '' _ = _
    rw [ğ–£.t_inv]
    simp
  Â· erw [â† coe_comp, â† TopCat.mono_iff_injective] -- now `erw` after #13170
    infer_instance
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_image_preimage_eq AlgebraicGeometry.PresheafedSpace.GlueData.Î¹_image_preimage_eq

/-- (Implementation). The map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_{U_j}, ğ–£.Î¹ j â»Â¹' (ğ–£.Î¹ i '' U))` -/
def opensImagePreimageMap (i j : D.J) (U : Opens (D.U i).carrier) :
    (D.U i).presheaf.obj (op U) âŸ¶
    (D.U j).presheaf.obj (op <|
      (Opens.map (ğ–£.Î¹ j).base).obj ((D.Î¹_openEmbedding i).isOpenMap.functor.obj U)) :=
  (D.f i j).c.app (op U) â‰«
    (D.t j i).c.app _ â‰«
      (D.f_open j i).invApp (unop _) â‰«
        (ğ–£.U j).presheaf.map (eqToHom (D.Î¹_image_preimage_eq i j U)).op
#align algebraic_geometry.PresheafedSpace.glue_data.opens_image_preimage_map AlgebraicGeometry.PresheafedSpace.GlueData.opensImagePreimageMap

theorem opensImagePreimageMap_app' (i j k : D.J) (U : Opens (D.U i).carrier) :
    âˆƒ eq,
      D.opensImagePreimageMap i j U â‰« (D.f j k).c.app _ =
        ((Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j).c.app (op U) â‰«
          (Ï€â‚‚â»Â¹ j, i, k) (unop _) â‰« (D.V (j, k)).presheaf.map (eqToHom eq) := by
  constructor
  Â· delta opensImagePreimageMap
    simp_rw [Category.assoc]
    rw [(D.f j k).c.naturality, f_invApp_f_app_assoc]
    Â· erw [â† (D.V (j, k)).presheaf.map_comp]
      Â· simp_rw [â† Category.assoc]
        erw [â† comp_c_app, â† comp_c_app]
        Â· simp_rw [Category.assoc]
          dsimp only [Functor.op, unop_op, Quiver.Hom.unop_op]
          rw [eqToHom_map (Opens.map _), eqToHom_op, eqToHom_trans]
          congr
#align algebraic_geometry.PresheafedSpace.glue_data.opens_image_preimage_map_app' AlgebraicGeometry.PresheafedSpace.GlueData.opensImagePreimageMap_app'

/-- The red and the blue arrows in ![this diagram](https://i.imgur.com/mBzV1Rx.png) commute. -/
theorem opensImagePreimageMap_app (i j k : D.J) (U : Opens (D.U i).carrier) :
    D.opensImagePreimageMap i j U â‰« (D.f j k).c.app _ =
      ((Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j).c.app (op U) â‰«
        (Ï€â‚‚â»Â¹ j, i, k) (unop _) â‰«
          (D.V (j, k)).presheaf.map (eqToHom (opensImagePreimageMap_app' D i j k U).choose) :=
  (opensImagePreimageMap_app' D i j k U).choose_spec
#align algebraic_geometry.PresheafedSpace.glue_data.opens_image_preimage_map_app AlgebraicGeometry.PresheafedSpace.GlueData.opensImagePreimageMap_app

-- This is proved separately since `reassoc` somehow timeouts.
theorem opensImagePreimageMap_app_assoc (i j k : D.J) (U : Opens (D.U i).carrier) {X' : C}
    (f' : _ âŸ¶ X') :
    D.opensImagePreimageMap i j U â‰« (D.f j k).c.app _ â‰« f' =
      ((Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j).c.app (op U) â‰«
        (Ï€â‚‚â»Â¹ j, i, k) (unop _) â‰«
          (D.V (j, k)).presheaf.map
            (eqToHom (opensImagePreimageMap_app' D i j k U).choose) â‰« f' := by
  simpa only [Category.assoc] using congr_arg (Â· â‰« f') (opensImagePreimageMap_app D i j k U)
#align algebraic_geometry.PresheafedSpace.glue_data.opens_image_preimage_map_app_assoc AlgebraicGeometry.PresheafedSpace.GlueData.opensImagePreimageMap_app_assoc

/-- (Implementation) Given an open subset of one of the spaces `U âŠ† Uáµ¢`, the sheaf component of
the image `Î¹ '' U` in the glued space is the limit of this diagram. -/
abbrev diagramOverOpen {i : D.J} (U : Opens (D.U i).carrier) :
    -- Porting note : â†“ these need to be explicit
    (WalkingMultispan D.diagram.fstFrom D.diagram.sndFrom)áµ’áµ– â¥¤ C :=
  componentwiseDiagram ğ–£.diagram.multispan ((D.Î¹_openEmbedding i).isOpenMap.functor.obj U)
#align algebraic_geometry.PresheafedSpace.glue_data.diagram_over_open AlgebraicGeometry.PresheafedSpace.GlueData.diagramOverOpen

/-- (Implementation)
The projection from the limit of `diagram_over_open` to a component of `D.U j`. -/
abbrev diagramOverOpenÏ€ {i : D.J} (U : Opens (D.U i).carrier) (j : D.J) :=
  limit.Ï€ (D.diagramOverOpen U) (op (WalkingMultispan.right j))
#align algebraic_geometry.PresheafedSpace.glue_data.diagram_over_open_Ï€ AlgebraicGeometry.PresheafedSpace.GlueData.diagramOverOpenÏ€

/-- (Implementation) We construct the map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_V, U_V)` for each `V` in the gluing
diagram. We will lift these maps into `Î¹InvApp`. -/
def Î¹InvAppÏ€App {i : D.J} (U : Opens (D.U i).carrier) (j) :
    (ğ–£.U i).presheaf.obj (op U) âŸ¶ (D.diagramOverOpen U).obj (op j) := by
  rcases j with (âŸ¨j, kâŸ© | j)
  Â· refine
      D.opensImagePreimageMap i j U â‰« (D.f j k).c.app _ â‰« (D.V (j, k)).presheaf.map (eqToHom ?_)
    rw [Functor.op_obj]
    congr 1; ext1
    dsimp only [Functor.op_obj, Opens.map_coe, unop_op, IsOpenMap.functor_obj_coe]
    rw [Set.preimage_preimage]
    change (D.f j k â‰« ğ–£.Î¹ j).base â»Â¹' _ = _
    -- Porting note: used to be `congr 3`
    refine congr_arg (Â· â»Â¹' _) ?_
    convert congr_arg (ContinuousMap.toFun (Î± := D.V âŸ¨j, kâŸ©) (Î² := D.glued) Â·) ?_
    refine congr_arg (PresheafedSpace.Hom.base (C := C) Â·) ?_
    exact colimit.w ğ–£.diagram.multispan (WalkingMultispan.Hom.fst (j, k))
  Â· exact D.opensImagePreimageMap i j U
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_inv_app_Ï€_app AlgebraicGeometry.PresheafedSpace.GlueData.Î¹InvAppÏ€App

-- Porting note: time out started in `erw [... congr_app (pullbackSymmetry_hom_comp_snd _ _)]` and
-- the last congr has a very difficult `rfl : eqToHom _ â‰« eqToHom _ â‰« ... = eqToHom ... `
set_option maxHeartbeats 600000 in
/-- (Implementation) The natural map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_X, ğ–£.Î¹ i '' U)`.
This forms the inverse of `(ğ–£.Î¹ i).c.app (op U)`. -/
def Î¹InvApp {i : D.J} (U : Opens (D.U i).carrier) :
    (D.U i).presheaf.obj (op U) âŸ¶ limit (D.diagramOverOpen U) :=
  limit.lift (D.diagramOverOpen U)
    { pt := (D.U i).presheaf.obj (op U)
      Ï€ :=
        { app := fun j => D.Î¹InvAppÏ€App U (unop j)
          naturality := fun {X Y} f' => by
            induction X using Opposite.rec' with | h X => ?_
            induction Y using Opposite.rec' with | h Y => ?_
            let f : Y âŸ¶ X := f'.unop; have : f' = f.op := rfl; clear_value f; subst this
            rcases f with (_ | âŸ¨j, kâŸ© | âŸ¨j, kâŸ©)
            Â· erw [Category.id_comp, CategoryTheory.Functor.map_id]
              rw [Category.comp_id]
            Â· erw [Category.id_comp]; congr 1
            erw [Category.id_comp]
            -- It remains to show that the blue is equal to red + green in the original diagram.
            -- The proof strategy is illustrated in ![this diagram](https://i.imgur.com/mBzV1Rx.png)
            -- where we prove red = pink = light-blue = green = blue.
            change
              D.opensImagePreimageMap i j U â‰«
                  (D.f j k).c.app _ â‰« (D.V (j, k)).presheaf.map (eqToHom _) =
                D.opensImagePreimageMap _ _ _ â‰«
                  ((D.f k j).c.app _ â‰« (D.t j k).c.app _) â‰« (D.V (j, k)).presheaf.map (eqToHom _)
            erw [opensImagePreimageMap_app_assoc]
            simp_rw [Category.assoc]
            erw [opensImagePreimageMap_app_assoc, (D.t j k).c.naturality_assoc]
            rw [snd_invApp_t_app_assoc]
            erw [â† PresheafedSpace.comp_c_app_assoc]
            -- light-blue = green is relatively easy since the part that differs does not involve
            -- partial inverses.
            have :
              D.t' j k i â‰« (Ï€â‚ k, i, j) â‰« D.t k i â‰« ğ–£.f i k =
                (pullbackSymmetry _ _).hom â‰« (Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j := by
              rw [â† ğ–£.t_fac_assoc, ğ–£.t'_comp_eq_pullbackSymmetry_assoc,
                pullbackSymmetry_hom_comp_snd_assoc, pullback.condition, ğ–£.t_fac_assoc]
            rw [congr_app this]
            erw [PresheafedSpace.comp_c_app_assoc (pullbackSymmetry _ _).hom]
            simp_rw [Category.assoc]
            congr 1
            rw [â† IsIso.eq_inv_comp]
            erw [IsOpenImmersion.inv_invApp]
            simp_rw [Category.assoc]
            erw [NatTrans.naturality_assoc, â† PresheafedSpace.comp_c_app_assoc,
              congr_app (pullbackSymmetry_hom_comp_snd _ _)]
            simp_rw [Category.assoc]
            erw [IsOpenImmersion.inv_naturality_assoc, IsOpenImmersion.inv_naturality_assoc,
              IsOpenImmersion.inv_naturality_assoc, IsOpenImmersion.app_invApp_assoc]
            repeat' erw [â† (D.V (j, k)).presheaf.map_comp]
            -- Porting note: was just `congr`
            exact congr_arg ((D.V (j, k)).presheaf.map Â·) rfl } }
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_inv_app AlgebraicGeometry.PresheafedSpace.GlueData.Î¹InvApp

/-- `Î¹InvApp` is the left inverse of `D.Î¹ i` on `U`. -/
theorem Î¹InvApp_Ï€ {i : D.J} (U : Opens (D.U i).carrier) :
    âˆƒ eq, D.Î¹InvApp U â‰« D.diagramOverOpenÏ€ U i = (D.U i).presheaf.map (eqToHom eq) := by
  fconstructor
  -- Porting note: I don't know what the magic was in Lean3 proof, it just skipped the proof of `eq`
  Â· congr; ext1; change _ = _ â»Â¹' (_ '' _); ext1 x
    simp only [SetLike.mem_coe, diagram_l, diagram_r, unop_op, Set.mem_preimage, Set.mem_image]
    refine âŸ¨fun h => âŸ¨_, h, rflâŸ©, ?_âŸ©
    rintro âŸ¨y, h1, h2âŸ©
    convert h1 using 1
    delta Î¹ Multicoequalizer.Ï€ at h2
    apply_fun (D.Î¹ _).base
    Â· exact h2.symm
    Â· have := D.Î¹_gluedIso_inv (PresheafedSpace.forget _) i
      dsimp at this
      erw [â† this, coe_comp] -- now `erw` after #13170
      refine Function.Injective.comp ?_ (TopCat.GlueData.Î¹_injective D.toTopGlueData i)
      erw [â† TopCat.mono_iff_injective] -- now `erw` after #13170
      infer_instance
  delta Î¹InvApp
  rw [limit.lift_Ï€]
  change D.opensImagePreimageMap i i U = _
  dsimp [opensImagePreimageMap]
  rw [congr_app (D.t_id _), id_c_app, â† Functor.map_comp]
  erw [IsOpenImmersion.inv_naturality_assoc, IsOpenImmersion.app_inv_app'_assoc]
  Â· simp only [eqToHom_op, eqToHom_trans, eqToHom_map (Functor.op _), â† Functor.map_comp]
    rfl
  Â· rw [Set.range_iff_surjective.mpr _]
    Â· simp
    Â· rw [â† TopCat.epi_iff_surjective]
      infer_instance
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_inv_app_Ï€ AlgebraicGeometry.PresheafedSpace.GlueData.Î¹InvApp_Ï€

/-- The `eqToHom` given by `Î¹InvApp_Ï€`. -/
abbrev Î¹InvAppÏ€EqMap {i : D.J} (U : Opens (D.U i).carrier) :=
  (D.U i).presheaf.map (eqToIso (D.Î¹InvApp_Ï€ U).choose).inv
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_inv_app_Ï€_eq_map AlgebraicGeometry.PresheafedSpace.GlueData.Î¹InvAppÏ€EqMap

/-- `Î¹InvApp` is the right inverse of `D.Î¹ i` on `U`. -/
theorem Ï€_Î¹InvApp_Ï€ (i j : D.J) (U : Opens (D.U i).carrier) :
    D.diagramOverOpenÏ€ U i â‰« D.Î¹InvAppÏ€EqMap U â‰« D.Î¹InvApp U â‰« D.diagramOverOpenÏ€ U j =
      D.diagramOverOpenÏ€ U j := by
  -- Porting note: originally, the proof of monotonicity was left a blank and proved in the end
  -- but Lean 4 doesn't like this any more, so the proof is restructured
  rw [â† @cancel_mono (f := (componentwiseDiagram ğ–£.diagram.multispan _).map
    (Quiver.Hom.op (WalkingMultispan.Hom.snd (i, j))) â‰« ğŸ™ _) _ _ (by
    rw [Category.comp_id]
    apply (config := { allowSynthFailures := true }) mono_comp
    change Mono ((_ â‰« D.f j i).c.app _)
    rw [comp_c_app]
    apply (config := { allowSynthFailures := true }) mono_comp
    Â· erw [D.Î¹_image_preimage_eq i j U]
      infer_instance
    Â· have : IsIso (D.t i j).c := by apply c_isIso_of_iso
      infer_instance)]
  simp_rw [Category.assoc]
  rw [limit.w_assoc]
  erw [limit.lift_Ï€_assoc]
  rw [Category.comp_id, Category.comp_id]
  change _ â‰« _ â‰« (_ â‰« _) â‰« _ = _
  rw [congr_app (D.t_id _), id_c_app]
  simp_rw [Category.assoc]
  rw [â† Functor.map_comp_assoc]
  -- Porting note (#11224): change `rw` to `erw`
  erw [IsOpenImmersion.inv_naturality_assoc]
  erw [IsOpenImmersion.app_invApp_assoc]
  iterate 3 rw [â† Functor.map_comp_assoc]
  rw [NatTrans.naturality_assoc]
  erw [â† (D.V (i, j)).presheaf.map_comp]
  convert
    limit.w (componentwiseDiagram ğ–£.diagram.multispan _)
      (Quiver.Hom.op (WalkingMultispan.Hom.fst (i, j)))
#align algebraic_geometry.PresheafedSpace.glue_data.Ï€_Î¹_inv_app_Ï€ AlgebraicGeometry.PresheafedSpace.GlueData.Ï€_Î¹InvApp_Ï€

/-- `Î¹InvApp` is the inverse of `D.Î¹ i` on `U`. -/
theorem Ï€_Î¹InvApp_eq_id (i : D.J) (U : Opens (D.U i).carrier) :
    D.diagramOverOpenÏ€ U i â‰« D.Î¹InvAppÏ€EqMap U â‰« D.Î¹InvApp U = ğŸ™ _ := by
  ext j
  induction j using Opposite.rec' with | h j => ?_
  rcases j with (âŸ¨j, kâŸ© | âŸ¨jâŸ©)
  Â· rw [â† limit.w (componentwiseDiagram ğ–£.diagram.multispan _)
        (Quiver.Hom.op (WalkingMultispan.Hom.fst (j, k))),
      â† Category.assoc, Category.id_comp]
    congr 1
    simp_rw [Category.assoc]
    apply Ï€_Î¹InvApp_Ï€
  Â· simp_rw [Category.assoc]
    rw [Category.id_comp]
    apply Ï€_Î¹InvApp_Ï€
#align algebraic_geometry.PresheafedSpace.glue_data.Ï€_Î¹_inv_app_eq_id AlgebraicGeometry.PresheafedSpace.GlueData.Ï€_Î¹InvApp_eq_id

instance componentwise_diagram_Ï€_isIso (i : D.J) (U : Opens (D.U i).carrier) :
    IsIso (D.diagramOverOpenÏ€ U i) := by
  use D.Î¹InvAppÏ€EqMap U â‰« D.Î¹InvApp U
  constructor
  Â· apply Ï€_Î¹InvApp_eq_id
  Â· rw [Category.assoc, (D.Î¹InvApp_Ï€ _).choose_spec]
    exact Iso.inv_hom_id ((D.U i).presheaf.mapIso (eqToIso _))
#align algebraic_geometry.PresheafedSpace.glue_data.componentwise_diagram_Ï€_IsIso AlgebraicGeometry.PresheafedSpace.GlueData.componentwise_diagram_Ï€_isIso

instance Î¹IsOpenImmersion (i : D.J) : IsOpenImmersion (ğ–£.Î¹ i) where
  base_open := D.Î¹_openEmbedding i
  c_iso U := by erw [â† colimitPresheafObjIsoComponentwiseLimit_hom_Ï€]; infer_instance
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_IsOpenImmersion AlgebraicGeometry.PresheafedSpace.GlueData.Î¹IsOpenImmersion

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def vPullbackConeIsLimit (i j : D.J) : IsLimit (ğ–£.vPullbackCone i j) :=
  PullbackCone.isLimitAux' _ fun s => by
    refine âŸ¨?_, ?_, ?_, ?_âŸ©
    Â· refine PresheafedSpace.IsOpenImmersion.lift (D.f i j) s.fst ?_
      erw [â† D.toTopGlueData.preimage_range j i]
      have :
        s.fst.base â‰« D.toTopGlueData.Î¹ i =
          s.snd.base â‰« D.toTopGlueData.Î¹ j := by
        rw [â† ğ–£.Î¹_gluedIso_hom (PresheafedSpace.forget _) _, â†
          ğ–£.Î¹_gluedIso_hom (PresheafedSpace.forget _) _]
        have := congr_arg PresheafedSpace.Hom.base s.condition
        rw [comp_base, comp_base] at this
        replace this := reassoc_of% this
        exact this _
      rw [â† Set.image_subset_iff, â† Set.image_univ, â† Set.image_comp, Set.image_univ]
      -- Porting note (#11224): change `rw` to `erw`
      erw [â† coe_comp]
      rw [this, coe_comp, â† Set.image_univ, Set.image_comp]
      exact Set.image_subset_range _ _
    Â· apply IsOpenImmersion.lift_fac
    Â· rw [â† cancel_mono (ğ–£.Î¹ j), Category.assoc, â† (ğ–£.vPullbackCone i j).condition]
      conv_rhs => rw [â† s.condition]
      erw [IsOpenImmersion.lift_fac_assoc]
    Â· intro m eâ‚ _; rw [â† cancel_mono (D.f i j)]; erw [eâ‚]; rw [IsOpenImmersion.lift_fac]
#align algebraic_geometry.PresheafedSpace.glue_data.V_pullback_cone_is_limit AlgebraicGeometry.PresheafedSpace.GlueData.vPullbackConeIsLimit

theorem Î¹_jointly_surjective (x : ğ–£.glued) : âˆƒ (i : D.J) (y : D.U i), (ğ–£.Î¹ i).base y = x :=
  ğ–£.Î¹_jointly_surjective (PresheafedSpace.forget _ â‹™ CategoryTheory.forget TopCat) x
#align algebraic_geometry.PresheafedSpace.glue_data.Î¹_jointly_surjective AlgebraicGeometry.PresheafedSpace.GlueData.Î¹_jointly_surjective

end GlueData

end PresheafedSpace

namespace SheafedSpace

variable [HasProducts.{v} C]

/-- A family of gluing data consists of
1. An index type `J`
2. A sheafed space `U i` for each `i : J`.
3. A sheafed space `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ SheafedSpace C` rather than `J â†’ J â†’ SheafedSpace C` to
  connect to the limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.
-/
-- Porting note(#5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
structure GlueData extends CategoryTheory.GlueData (SheafedSpace.{u, v, v} C) where
  f_open : âˆ€ i j, SheafedSpace.IsOpenImmersion (f i j)
#align algebraic_geometry.SheafedSpace.glue_data AlgebraicGeometry.SheafedSpaceâ‚“.GlueData

attribute [instance] GlueData.f_open

namespace GlueData

variable {C}
variable (D : GlueData C)

local notation "ğ–£" => D.toGlueData

/-- The glue data of presheafed spaces associated to a family of glue data of sheafed spaces. -/
abbrev toPresheafedSpaceGlueData : PresheafedSpace.GlueData C :=
  { f_open := D.f_open
    toGlueData := ğ–£.mapGlueData forgetToPresheafedSpace }
#align algebraic_geometry.SheafedSpace.glue_data.to_PresheafedSpace_glue_data AlgebraicGeometry.SheafedSpaceâ‚“.GlueData.toPresheafedSpaceGlueData

variable [HasLimits C]

/-- The gluing as sheafed spaces is isomorphic to the gluing as presheafed spaces. -/
abbrev isoPresheafedSpace :
    ğ–£.glued.toPresheafedSpace â‰… D.toPresheafedSpaceGlueData.toGlueData.glued :=
  ğ–£.gluedIso forgetToPresheafedSpace
#align algebraic_geometry.SheafedSpace.glue_data.iso_PresheafedSpace AlgebraicGeometry.SheafedSpaceâ‚“.GlueData.isoPresheafedSpace

theorem Î¹_isoPresheafedSpace_inv (i : D.J) :
    D.toPresheafedSpaceGlueData.toGlueData.Î¹ i â‰« D.isoPresheafedSpace.inv = ğ–£.Î¹ i :=
  ğ–£.Î¹_gluedIso_inv _ _
#align algebraic_geometry.SheafedSpace.glue_data.Î¹_iso_PresheafedSpace_inv AlgebraicGeometry.SheafedSpaceâ‚“.GlueData.Î¹_isoPresheafedSpace_inv

instance Î¹IsOpenImmersion (i : D.J) : IsOpenImmersion (ğ–£.Î¹ i) := by
  rw [â† D.Î¹_isoPresheafedSpace_inv]
  have := D.toPresheafedSpaceGlueData.Î¹IsOpenImmersion i
  infer_instance
#align algebraic_geometry.SheafedSpace.glue_data.Î¹_IsOpenImmersion AlgebraicGeometry.SheafedSpaceâ‚“.GlueData.Î¹IsOpenImmersion

theorem Î¹_jointly_surjective (x : ğ–£.glued) : âˆƒ (i : D.J) (y : D.U i), (ğ–£.Î¹ i).base y = x :=
  ğ–£.Î¹_jointly_surjective (SheafedSpace.forget _ â‹™ CategoryTheory.forget TopCat) x
#align algebraic_geometry.SheafedSpace.glue_data.Î¹_jointly_surjective AlgebraicGeometry.SheafedSpaceâ‚“.GlueData.Î¹_jointly_surjective

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def vPullbackConeIsLimit (i j : D.J) : IsLimit (ğ–£.vPullbackCone i j) :=
  ğ–£.vPullbackConeIsLimitOfMap forgetToPresheafedSpace i j
    (D.toPresheafedSpaceGlueData.vPullbackConeIsLimit _ _)
#align algebraic_geometry.SheafedSpace.glue_data.V_pullback_cone_is_limit AlgebraicGeometry.SheafedSpaceâ‚“.GlueData.vPullbackConeIsLimit

end GlueData

end SheafedSpace

namespace LocallyRingedSpace

/-- A family of gluing data consists of
1. An index type `J`
2. A locally ringed space `U i` for each `i : J`.
3. A locally ringed space `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ LocallyRingedSpace` rather than `J â†’ J â†’ LocallyRingedSpace` to
  connect to the limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.
-/
-- Porting note(#5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
structure GlueData extends CategoryTheory.GlueData LocallyRingedSpace where
  f_open : âˆ€ i j, LocallyRingedSpace.IsOpenImmersion (f i j)
#align algebraic_geometry.LocallyRingedSpace.glue_data AlgebraicGeometry.LocallyRingedSpace.GlueData

attribute [instance] GlueData.f_open

namespace GlueData

variable (D : GlueData.{u})

local notation "ğ–£" => D.toGlueData

/-- The glue data of ringed spaces associated to a family of glue data of locally ringed spaces. -/
abbrev toSheafedSpaceGlueData : SheafedSpace.GlueData CommRingCat :=
  { f_open := D.f_open
    toGlueData := ğ–£.mapGlueData forgetToSheafedSpace }
#align algebraic_geometry.LocallyRingedSpace.glue_data.to_SheafedSpace_glue_data AlgebraicGeometry.LocallyRingedSpace.GlueData.toSheafedSpaceGlueData

/-- The gluing as locally ringed spaces is isomorphic to the gluing as ringed spaces. -/
abbrev isoSheafedSpace : ğ–£.glued.toSheafedSpace â‰… D.toSheafedSpaceGlueData.toGlueData.glued :=
  ğ–£.gluedIso forgetToSheafedSpace
#align algebraic_geometry.LocallyRingedSpace.glue_data.iso_SheafedSpace AlgebraicGeometry.LocallyRingedSpace.GlueData.isoSheafedSpace

theorem Î¹_isoSheafedSpace_inv (i : D.J) :
    D.toSheafedSpaceGlueData.toGlueData.Î¹ i â‰« D.isoSheafedSpace.inv = (ğ–£.Î¹ i).1 :=
  ğ–£.Î¹_gluedIso_inv forgetToSheafedSpace i
#align algebraic_geometry.LocallyRingedSpace.glue_data.Î¹_iso_SheafedSpace_inv AlgebraicGeometry.LocallyRingedSpace.GlueData.Î¹_isoSheafedSpace_inv

instance Î¹_isOpenImmersion (i : D.J) : IsOpenImmersion (ğ–£.Î¹ i) := by
  delta IsOpenImmersion; rw [â† D.Î¹_isoSheafedSpace_inv]
  apply (config := { allowSynthFailures := true }) PresheafedSpace.IsOpenImmersion.comp
  -- Porting note: this was automatic
  exact (D.toSheafedSpaceGlueData).Î¹IsOpenImmersion i
#align algebraic_geometry.LocallyRingedSpace.glue_data.Î¹_IsOpenImmersion AlgebraicGeometry.LocallyRingedSpace.GlueData.Î¹_isOpenImmersion

instance (i j k : D.J) : PreservesLimit (cospan (ğ–£.f i j) (ğ–£.f i k)) forgetToSheafedSpace :=
  inferInstance

theorem Î¹_jointly_surjective (x : ğ–£.glued) : âˆƒ (i : D.J) (y : D.U i), (ğ–£.Î¹ i).1.base y = x :=
  ğ–£.Î¹_jointly_surjective
    ((LocallyRingedSpace.forgetToSheafedSpace.{u} â‹™ SheafedSpace.forget CommRingCatMax.{u, u}) â‹™
      forget TopCat.{u}) x
#align algebraic_geometry.LocallyRingedSpace.glue_data.Î¹_jointly_surjective AlgebraicGeometry.LocallyRingedSpace.GlueData.Î¹_jointly_surjective

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def vPullbackConeIsLimit (i j : D.J) : IsLimit (ğ–£.vPullbackCone i j) :=
  ğ–£.vPullbackConeIsLimitOfMap forgetToSheafedSpace i j
    (D.toSheafedSpaceGlueData.vPullbackConeIsLimit _ _)
#align algebraic_geometry.LocallyRingedSpace.glue_data.V_pullback_cone_is_limit AlgebraicGeometry.LocallyRingedSpace.GlueData.vPullbackConeIsLimit

end GlueData

end LocallyRingedSpace

end AlgebraicGeometry
