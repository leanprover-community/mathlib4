/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Geometry.RingedSpace.PresheafedSpace
import Mathlib.Topology.Category.TopCat.Limits.Basic
import Mathlib.Topology.Sheaves.Limits
import Mathlib.CategoryTheory.ConcreteCategory.Elementwise

/-!
# `PresheafedSpace C` has colimits.

If `C` has limits, then the category `PresheafedSpace C` has colimits,
and the forgetful functor to `TopCat` preserves these colimits.

When restricted to a diagram where the underlying continuous maps are open embeddings,
this says that we can glue presheaved spaces.

Given a diagram `F : J ‚•§ PresheafedSpace C`,
we first build the colimit of the underlying topological spaces,
as `colimit (F ‚ãô PresheafedSpace.forget C)`. Call that colimit space `X`.

Our strategy is to push each of the presheaves `F.obj j`
forward along the continuous map `colimit.Œπ (F ‚ãô PresheafedSpace.forget C) j` to `X`.
Since pushforward is functorial, we obtain a diagram `J ‚•§ (presheaf C X)·µí·µñ`
of presheaves on a single space `X`.
(Note that the arrows now point the other direction,
because this is the way `PresheafedSpace C` is set up.)

The limit of this diagram then constitutes the colimit presheaf.
-/


noncomputable section

universe v' u' v u

open CategoryTheory Opposite CategoryTheory.Category CategoryTheory.Functor CategoryTheory.Limits
  TopCat TopCat.Presheaf TopologicalSpace

variable {J : Type u'} [Category.{v'} J] {C : Type u} [Category.{v} C]

namespace AlgebraicGeometry

namespace PresheafedSpace

attribute [local simp] eqToHom_map

-- Porting note: we used to have:
-- local attribute [tidy] tactic.auto_cases_opens
-- We would replace this by:
-- attribute [local aesop safe cases (rule_sets := [CategoryTheory])] Opens
-- although it doesn't appear to help in this file, in any case.

@[simp]
theorem map_id_c_app (F : J ‚•§ PresheafedSpace.{_, _, v} C) (j) (U) :
    (F.map (ùüô j)).c.app U =
      (Pushforward.id (F.obj j).presheaf).inv.app U ‚â´
        (pushforwardEq (by simp) (F.obj j).presheaf).hom.app U := by
  simp [PresheafedSpace.congr_app (F.map_id j)]

@[simp]
theorem map_comp_c_app (F : J ‚•§ PresheafedSpace.{_, _, v} C) {j‚ÇÅ j‚ÇÇ j‚ÇÉ}
    (f : j‚ÇÅ ‚ü∂ j‚ÇÇ) (g : j‚ÇÇ ‚ü∂ j‚ÇÉ) (U) :
    (F.map (f ‚â´ g)).c.app U =
      (F.map g).c.app U ‚â´
        ((pushforward C (F.map g).base).map (F.map f).c).app U ‚â´
          (pushforwardEq (congr_arg Hom.base (F.map_comp f g).symm) _).hom.app U := by
  simp [PresheafedSpace.congr_app (F.map_comp f g)]

/-- Given a diagram of `PresheafedSpace C`s, its colimit is computed by pushing the sheaves onto
the colimit of the underlying spaces, and taking componentwise limit.
This is the componentwise diagram for an open set `U` of the colimit of the underlying spaces.
-/
@[simps]
def componentwiseDiagram (F : J ‚•§ PresheafedSpace.{_, _, v} C) [HasColimit F]
    (U : Opens (Limits.colimit F).carrier) : J·µí·µñ ‚•§ C where
  obj j := (F.obj (unop j)).presheaf.obj (op ((Opens.map (colimit.Œπ F (unop j)).base).obj U))
  map {j k} f := (F.map f.unop).c.app _ ‚â´
    (F.obj (unop k)).presheaf.map (eqToHom (by rw [‚Üê colimit.w F f.unop, comp_base]; rfl))
  map_comp {i j k} f g := by
    simp only [assoc, CategoryTheory.NatTrans.naturality_assoc]
    simp

variable [HasColimitsOfShape J TopCat.{v}]

/-- Given a diagram of presheafed spaces,
we can push all the presheaves forward to the colimit `X` of the underlying topological spaces,
obtaining a diagram in `(Presheaf C X)·µí·µñ`.
-/
@[simps]
def pushforwardDiagramToColimit (F : J ‚•§ PresheafedSpace.{_, _, v} C) :
    J ‚•§ (Presheaf C (colimit (F ‚ãô PresheafedSpace.forget C)))·µí·µñ where
  obj j := op (colimit.Œπ (F ‚ãô PresheafedSpace.forget C) j _* (F.obj j).presheaf)
  map {j j'} f :=
    ((pushforward C (colimit.Œπ (F ‚ãô PresheafedSpace.forget C) j')).map (F.map f).c ‚â´
      (Pushforward.comp ((F ‚ãô PresheafedSpace.forget C).map f)
        (colimit.Œπ (F ‚ãô PresheafedSpace.forget C) j') (F.obj j).presheaf).inv ‚â´
      (pushforwardEq (colimit.w (F ‚ãô PresheafedSpace.forget C) f) (F.obj j).presheaf).hom).op
  map_id j := by
    apply (opEquiv _ _).injective
    refine NatTrans.ext (funext fun U ‚Ü¶ ?_)
    induction U with
    | op U =>
      simp [opEquiv]
      rfl
  map_comp {j‚ÇÅ j‚ÇÇ j‚ÇÉ} f g := by
    apply (opEquiv _ _).injective
    refine NatTrans.ext (funext fun U ‚Ü¶ ?_)
    dsimp [opEquiv]
    have :
      op ((Opens.map (F.map g).base).obj
          ((Opens.map (colimit.Œπ (F ‚ãô forget C) j‚ÇÉ)).obj U.unop)) =
        op ((Opens.map (colimit.Œπ (F ‚ãô PresheafedSpace.forget C) j‚ÇÇ)).obj (unop U)) := by
      apply unop_injective
      rw [‚Üê Opens.map_comp_obj]
      congr
      exact colimit.w (F ‚ãô PresheafedSpace.forget C) g
    simp only [map_comp_c_app, pushforward_obj_obj, pushforward_map_app, comp_base,
      pushforwardEq_hom_app, op_obj, Opens.map_comp_obj, id_comp, assoc, eqToHom_map_comp,
      NatTrans.naturality_assoc, pushforward_obj_map, eqToHom_unop]
    simp [NatTrans.congr (Œ± := (F.map f).c) this]

variable [‚àÄ X : TopCat.{v}, HasLimitsOfShape J·µí·µñ (X.Presheaf C)]

/-- Auxiliary definition for `AlgebraicGeometry.PresheafedSpace.instHasColimits`.
-/
def colimit (F : J ‚•§ PresheafedSpace.{_, _, v} C) : PresheafedSpace C where
  carrier := Limits.colimit (F ‚ãô PresheafedSpace.forget C)
  presheaf := limit (pushforwardDiagramToColimit F).leftOp

@[simp]
theorem colimit_carrier (F : J ‚•§ PresheafedSpace.{_, _, v} C) :
    (colimit F).carrier = Limits.colimit (F ‚ãô PresheafedSpace.forget C) :=
  rfl

@[simp]
theorem colimit_presheaf (F : J ‚•§ PresheafedSpace.{_, _, v} C) :
    (colimit F).presheaf = limit (pushforwardDiagramToColimit F).leftOp :=
  rfl

/-- Auxiliary definition for `AlgebraicGeometry.PresheafedSpace.instHasColimits`.
-/
@[simps]
def colimitCocone (F : J ‚•§ PresheafedSpace.{_, _, v} C) : Cocone F where
  pt := colimit F
  Œπ :=
    { app := fun j ‚Ü¶
        { base := colimit.Œπ (F ‚ãô PresheafedSpace.forget C) j
          c := limit.œÄ _ (op j) }
      naturality := fun {j j'} f ‚Ü¶ by
        ext1
        ¬∑ ext x
          exact colimit.w_apply (F ‚ãô PresheafedSpace.forget C) f x
        ¬∑ ext ‚ü®U, hU‚ü©
          dsimp [-Presheaf.comp_app]
          rw [PresheafedSpace.id_c_app, map_id]
          erw [id_comp]
          rw [NatTrans.comp_app, PresheafedSpace.comp_c_app, whiskerRight_app, eqToHom_app,
            ‚Üê congr_arg NatTrans.app (limit.w (pushforwardDiagramToColimit F).leftOp f.op),
            NatTrans.comp_app, Functor.leftOp_map, pushforwardDiagramToColimit_map]
          simp }

variable [HasLimitsOfShape J·µí·µñ C]

namespace ColimitCoconeIsColimit

/-- Auxiliary definition for `AlgebraicGeometry.PresheafedSpace.colimitCoconeIsColimit`.
-/
def descCApp (F : J ‚•§ PresheafedSpace.{_, _, v} C) (s : Cocone F) (U : (Opens s.pt.carrier)·µí·µñ) :
    s.pt.presheaf.obj U ‚ü∂
      (colimit.desc (F ‚ãô PresheafedSpace.forget C) ((PresheafedSpace.forget C).mapCocone s) _*
            limit (pushforwardDiagramToColimit F).leftOp).obj
        U := by
  refine
    limit.lift _
        { pt := s.pt.presheaf.obj U
          œÄ :=
            { app := fun j ‚Ü¶ ?_
              naturality := fun j j' f ‚Ü¶ ?_ } } ‚â´
      (limitObjIsoLimitCompEvaluation _ _).inv
  -- We still need to construct the `app` and `naturality'` fields omitted above.
  ¬∑ refine (s.Œπ.app (unop j)).c.app U ‚â´ (F.obj (unop j)).presheaf.map (eqToHom ?_)
    dsimp
    rw [‚Üê Opens.map_comp_obj]
    simp
  ¬∑ dsimp
    rw [PresheafedSpace.congr_app (s.w f.unop).symm U]
    have w :=
      Functor.congr_obj
        (congr_arg Opens.map (colimit.Œπ_desc ((PresheafedSpace.forget C).mapCocone s) (unop j)))
        (unop U)
    simp only [Opens.map_comp_obj_unop] at w
    replace w := congr_arg op w
    have w' := NatTrans.congr (F.map f.unop).c w
    rw [w']
    simp

theorem desc_c_naturality (F : J ‚•§ PresheafedSpace.{_, _, v} C) (s : Cocone F)
    {U V : (Opens s.pt.carrier)·µí·µñ} (i : U ‚ü∂ V) :
    s.pt.presheaf.map i ‚â´ descCApp F s V =
      descCApp F s U ‚â´
        (colimit.desc (F ‚ãô forget C) ((forget C).mapCocone s) _* (colimitCocone F).pt.presheaf).map
          i := by
  dsimp [descCApp]
  refine limit_obj_ext (fun j ‚Ü¶ ?_)
  have w := Functor.congr_hom (congr_arg Opens.map
    (colimit.Œπ_desc ((PresheafedSpace.forget C).mapCocone s) (unop j))) i.unop
  simp only [Opens.map_comp_map] at w
  simp [congr_arg Quiver.Hom.op w]

/-- Auxiliary definition for `AlgebraicGeometry.PresheafedSpace.colimitCoconeIsColimit`.
-/
def desc (F : J ‚•§ PresheafedSpace.{_, _, v} C) (s : Cocone F) : colimit F ‚ü∂ s.pt where
  base := colimit.desc (F ‚ãô PresheafedSpace.forget C) ((PresheafedSpace.forget C).mapCocone s)
  c :=
    { app := fun U ‚Ü¶ descCApp F s U
      naturality := fun _ _ i ‚Ü¶ desc_c_naturality F s i }

theorem desc_fac (F : J ‚•§ PresheafedSpace.{_, _, v} C) (s : Cocone F) (j : J) :
    (colimitCocone F).Œπ.app j ‚â´ desc F s = s.Œπ.app j := by
  ext U
  ¬∑ simp [desc]
  ¬∑ -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): the original proof is just `ext; dsimp [desc, descCApp]; simpa`,
    -- but this has to be expanded a bit
    rw [NatTrans.comp_app, PresheafedSpace.comp_c_app, whiskerRight_app]
    dsimp [desc, descCApp]
    simp only [eqToHom_app, op_obj, Opens.map_comp_obj, eqToHom_map, Functor.leftOp, assoc]
    rw [limitObjIsoLimitCompEvaluation_inv_œÄ_app_assoc]
    simp

end ColimitCoconeIsColimit

open ColimitCoconeIsColimit

/-- Auxiliary definition for `AlgebraicGeometry.PresheafedSpace.instHasColimits`.
-/
def colimitCoconeIsColimit (F : J ‚•§ PresheafedSpace.{_, _, v} C) :
    IsColimit (colimitCocone F) where
  desc s := desc F s
  fac s := desc_fac F s
  uniq s m w := by
    -- We need to use the identity on the continuous maps twice, so we prepare that first:
    have t :
      m.base =
        colimit.desc (F ‚ãô PresheafedSpace.forget C) ((PresheafedSpace.forget C).mapCocone s) := by
      dsimp
      -- `colimit.hom_ext` used to be automatically applied by `ext` before https://github.com/leanprover-community/mathlib4/pull/21302
      apply colimit.hom_ext fun j ‚Ü¶ ?_
      ext
      rw [colimit.Œπ_desc, mapCocone_Œπ_app, ‚Üê w j]
      simp
    ext : 1
    ¬∑ exact t
    ¬∑ refine NatTrans.ext (funext fun U ‚Ü¶ limit_obj_ext fun j ‚Ü¶ ?_)
      simp [desc, descCApp,
        PresheafedSpace.congr_app (w (unop j)).symm U,
        NatTrans.congr (limit.œÄ (pushforwardDiagramToColimit F).leftOp j)
        (congr_arg op (Functor.congr_obj (congr_arg Opens.map t) (unop U)))]

instance : HasColimitsOfShape J (PresheafedSpace.{_, _, v} C) where
  has_colimit F := ‚ü®colimitCocone F, colimitCoconeIsColimit F‚ü©

instance : PreservesColimitsOfShape J (PresheafedSpace.forget.{u, v, v} C) :=
  ‚ü®fun {F} ‚Ü¶ preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit F) <| by
    apply IsColimit.ofIsoColimit (colimit.isColimit _)
    fapply Cocones.ext
    ¬∑ rfl
    ¬∑ intro j
      simp‚ü©

/-- When `C` has limits, the category of presheaved spaces with values in `C` itself has colimits.
-/
instance instHasColimits [HasLimits C] : HasColimits (PresheafedSpace.{_, _, v} C) :=
  ‚ü®fun {_ _} ‚Ü¶ ‚ü®fun {F} ‚Ü¶ ‚ü®colimitCocone F, colimitCoconeIsColimit F‚ü©‚ü©‚ü©

/-- The underlying topological space of a colimit of presheaved spaces is
the colimit of the underlying topological spaces.
-/
instance forget_preservesColimits [HasLimits C] :
    PreservesColimits (PresheafedSpace.forget.{_, _, v} C) where
  preservesColimitsOfShape {J ùí•} :=
    { preservesColimit := fun {F} ‚Ü¶ preservesColimit_of_preserves_colimit_cocone
          (colimitCoconeIsColimit F)
          (IsColimit.ofIsoColimit (colimit.isColimit _) (Cocones.ext (Iso.refl _))) }

/-- The components of the colimit of a diagram of `PresheafedSpace C` is obtained
via taking componentwise limits.
-/
def colimitPresheafObjIsoComponentwiseLimit (F : J ‚•§ PresheafedSpace.{_, _, v} C) [HasColimit F]
    (U : Opens (Limits.colimit F).carrier) :
    (Limits.colimit F).presheaf.obj (op U) ‚âÖ limit (componentwiseDiagram F U) := by
  refine
    ((sheafIsoOfIso (colimit.isoColimitCocone ‚ü®_, colimitCoconeIsColimit F‚ü©).symm).app
          (op U)).trans
      ?_
  refine (limitObjIsoLimitCompEvaluation _ _).trans (Limits.lim.mapIso ?_)
  fapply NatIso.ofComponents
  ¬∑ intro X
    refine (F.obj (unop X)).presheaf.mapIso (eqToIso ?_)
    simp only [Functor.op_obj, op_inj_iff, Opens.map_coe, SetLike.ext'_iff,
      Set.preimage_preimage]
    refine congr_arg (Set.preimage ¬∑ U.1) (funext fun x ‚Ü¶ ?_)
    erw [‚Üê TopCat.comp_app]
    congr
    exact Œπ_preservesColimitIso_inv (forget C) F (unop X)
  ¬∑ intro X Y f
    change ((F.map f.unop).c.app _ ‚â´ _ ‚â´ _) ‚â´ (F.obj (unop Y)).presheaf.map _ = _ ‚â´ _
    rw [TopCat.Presheaf.Pushforward.comp_inv_app]
    erw [Category.id_comp]
    rw [Category.assoc]
    erw [‚Üê (F.obj (unop Y)).presheaf.map_comp, (F.map f.unop).c.naturality_assoc,
      ‚Üê (F.obj (unop Y)).presheaf.map_comp]
    rfl

@[simp]
theorem colimitPresheafObjIsoComponentwiseLimit_inv_Œπ_app (F : J ‚•§ PresheafedSpace.{_, _, v} C)
    (U : Opens (Limits.colimit F).carrier) (j : J) :
    (colimitPresheafObjIsoComponentwiseLimit F U).inv ‚â´ (colimit.Œπ F j).c.app (op U) =
      limit.œÄ _ (op j) := by
  delta colimitPresheafObjIsoComponentwiseLimit
  rw [Iso.trans_inv, Iso.trans_inv, Iso.app_inv, sheafIsoOfIso_inv, pushforwardToOfIso_app,
    congr_app (Iso.symm_inv _)]
  dsimp
  rw [map_id, comp_id, assoc, assoc, assoc, NatTrans.naturality,
      ‚Üê comp_c_app_assoc,
      congr_app (colimit.isoColimitCocone_Œπ_hom _ _), assoc]
  erw [limitObjIsoLimitCompEvaluation_inv_œÄ_app_assoc, limMap_œÄ_assoc]
  -- Porting note: `convert` doesn't work due to meta variable, so change to a `suffices` block
  set f := _
  change _ ‚â´ f = _
  suffices f_eq : f = ùüô _ by rw [f_eq, comp_id]
  erw [‚Üê (F.obj j).presheaf.map_id]
  change (F.obj j).presheaf.map _ ‚â´ _ = _
  simp

@[simp]
theorem colimitPresheafObjIsoComponentwiseLimit_hom_œÄ (F : J ‚•§ PresheafedSpace.{_, _, v} C)
    (U : Opens (Limits.colimit F).carrier) (j : J) :
    (colimitPresheafObjIsoComponentwiseLimit F U).hom ‚â´ limit.œÄ _ (op j) =
      (colimit.Œπ F j).c.app (op U) := by
  rw [‚Üê Iso.eq_inv_comp, colimitPresheafObjIsoComponentwiseLimit_inv_Œπ_app]

end PresheafedSpace

end AlgebraicGeometry
