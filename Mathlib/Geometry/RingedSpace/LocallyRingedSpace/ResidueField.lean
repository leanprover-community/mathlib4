/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace
import Mathlib.RingTheory.LocalRing.ResidueField.Basic

/-!

# Residue fields of points

Any point `x` of a locally ringed space `X` comes with a natural residue field, namely the residue
field of the stalk at `x`. Moreover, for every open subset of `X` containing `x`, we have a
canonical evaluation map from `Î“(X, U)` to the residue field of `X` at `x`.

## Main definitions

The following are in the `AlgebraicGeometry.LocallyRingedSpace` namespace:

- `residueField`: the residue field of the stalk at `x`.
- `evaluation`: for open subsets `U` of `X` containing `x`, the evaluation map from sections over
  `U` to the residue field at `x`.
- `evaluationMap`: a morphism of locally ringed spaces induces a morphism, i.e. extension, of
  residue fields.

-/

universe u

open CategoryTheory TopologicalSpace Opposite

noncomputable section

namespace AlgebraicGeometry.LocallyRingedSpace

variable (X : LocallyRingedSpace.{u}) {U : Opens X}

/-- The residue field of `X` at a point `x` is the residue field of the stalk of `X`
at `x`. -/
def residueField (x : X) : CommRingCat :=
  CommRingCat.of <| IsLocalRing.ResidueField (X.presheaf.stalk x)

instance (x : X) : Field (X.residueField x) :=
  inferInstanceAs <| Field (IsLocalRing.ResidueField (X.presheaf.stalk x))

/--
If `U` is an open of `X` containing `x`, we have a canonical ring map from the sections
over `U` to the residue field of `x`.

If we interpret sections over `U` as functions of `X` defined on `U`, then this ring map
corresponds to evaluation at `x`.
-/
def evaluation (x : U) : X.presheaf.obj (op U) âŸ¶ X.residueField x :=
  -- TODO: make a new definition wrapping
  -- `CommRingCat.ofHom (IsLocalRing.residue (X.presheaf.stalk _))`?
  X.presheaf.germ U x.1 x.2 â‰« CommRingCat.ofHom (IsLocalRing.residue (X.presheaf.stalk _))

/-- The global evaluation map from `Î“(X, âŠ¤)` to the residue field at `x`. -/
def Î“evaluation (x : X) : X.presheaf.obj (op âŠ¤) âŸ¶ X.residueField x :=
  X.evaluation âŸ¨x, show x âˆˆ âŠ¤ from trivialâŸ©

@[simp]
lemma evaluation_eq_zero_iff_notMem_basicOpen (x : U) (f : X.presheaf.obj (op U)) :
    X.evaluation x f = 0 â†” x.val âˆ‰ X.toRingedSpace.basicOpen f := by
  rw [X.toRingedSpace.mem_basicOpen f x.1 x.2, â† not_iff_not, not_not]
  exact (IsLocalRing.residue_ne_zero_iff_isUnit _)

@[deprecated (since := "2025-05-23")]
alias evaluation_eq_zero_iff_not_mem_basicOpen := evaluation_eq_zero_iff_notMem_basicOpen

lemma evaluation_ne_zero_iff_mem_basicOpen (x : U) (f : X.presheaf.obj (op U)) :
    X.evaluation x f â‰  0 â†” x.val âˆˆ X.toRingedSpace.basicOpen f := by
  simp

lemma basicOpen_eq_bot_iff_forall_evaluation_eq_zero (f : X.presheaf.obj (op U)) :
    X.toRingedSpace.basicOpen f = âŠ¥ â†” âˆ€ (x : U), X.evaluation x f = 0 := by
  simp only [evaluation_eq_zero_iff_notMem_basicOpen, Subtype.forall]
  exact âŸ¨fun h â†¦ h â–¸ fun a _ hc â†¦ hc,
    fun h â†¦ eq_bot_iff.mpr <| fun a ha â†¦ h a (X.toRingedSpace.basicOpen_le f ha) haâŸ©

@[simp]
lemma Î“evaluation_eq_zero_iff_notMem_basicOpen (x : X) (f : X.presheaf.obj (op âŠ¤)) :
    X.Î“evaluation x f = 0 â†” x âˆ‰ X.toRingedSpace.basicOpen f :=
  evaluation_eq_zero_iff_notMem_basicOpen X âŸ¨x, show x âˆˆ âŠ¤ by trivialâŸ© f

@[deprecated (since := "2025-05-23")]
alias Î“evaluation_eq_zero_iff_not_mem_basicOpen := Î“evaluation_eq_zero_iff_notMem_basicOpen

lemma Î“evaluation_ne_zero_iff_mem_basicOpen (x : X) (f : X.presheaf.obj (op âŠ¤)) :
    X.Î“evaluation x f â‰  0 â†” x âˆˆ X.toRingedSpace.basicOpen f :=
  evaluation_ne_zero_iff_mem_basicOpen X âŸ¨x, show x âˆˆ âŠ¤ by trivialâŸ© f

variable {X Y : LocallyRingedSpace.{u}} (f : X âŸ¶ Y) (x : X)

/-- If `X âŸ¶ Y` is a morphism of locally ringed spaces and `x` a point of `X`, we obtain
a morphism of residue fields in the other direction. -/
def residueFieldMap (x : X) : Y.residueField (f.base x) âŸ¶ X.residueField x :=
  CommRingCat.ofHom (IsLocalRing.ResidueField.map (f.stalkMap x).hom)

lemma residue_comp_residueFieldMap_eq_stalkMap_comp_residue (x : X) :
    CommRingCat.ofHom (IsLocalRing.residue (Y.presheaf.stalk (f.base x))) â‰«
      residueFieldMap f x = f.stalkMap x â‰«
      CommRingCat.ofHom (IsLocalRing.residue (X.presheaf.stalk x)) := by
  simp [residueFieldMap]
  rfl

@[simp]
lemma residueFieldMap_id (x : X) :
    residueFieldMap (ðŸ™ X) x = ðŸ™ (X.residueField x) := by
  ext : 1
  simp only [id_toShHom', SheafedSpace.id_base, TopCat.coe_id, id_eq, residueFieldMap, stalkMap_id]
  apply IsLocalRing.ResidueField.map_id

@[simp]
lemma residueFieldMap_comp {Z : LocallyRingedSpace.{u}} (g : Y âŸ¶ Z) (x : X) :
    residueFieldMap (f â‰« g) x = residueFieldMap g (f.base x) â‰« residueFieldMap f x := by
  ext : 1
  simp only [comp_toShHom, SheafedSpace.comp_base, Function.comp_apply, residueFieldMap,
    CommRingCat.hom_comp, TopCat.comp_app]
  simp_rw [stalkMap_comp]
  apply IsLocalRing.ResidueField.map_comp (Hom.stalkMap g (f.base x)).hom (Hom.stalkMap f x).hom

@[reassoc]
lemma evaluation_naturality {V : Opens Y} (x : (Opens.map f.base).obj V) :
    Y.evaluation âŸ¨f.base x, x.propertyâŸ© â‰« residueFieldMap f x.val =
      f.c.app (op V) â‰« X.evaluation x := by
  dsimp only [LocallyRingedSpace.evaluation,
    LocallyRingedSpace.residueFieldMap]
  rw [Category.assoc]
  ext a
  simp only [CommRingCat.comp_apply]
  erw [IsLocalRing.ResidueField.map_residue]
  rw [LocallyRingedSpace.stalkMap_germ_apply]
  rfl

lemma evaluation_naturality_apply {V : Opens Y} (x : (Opens.map f.base).obj V)
    (a : Y.presheaf.obj (op V)) :
    residueFieldMap f x.val (Y.evaluation âŸ¨f.base x, x.propertyâŸ© a) =
      X.evaluation x (f.c.app (op V) a) := by
  simpa using congrFun (congrArg (DFunLike.coe âˆ˜ CommRingCat.Hom.hom) <|
    evaluation_naturality f x) a

@[reassoc]
lemma Î“evaluation_naturality (x : X) :
    Y.Î“evaluation (f.base x) â‰« residueFieldMap f x =
      f.c.app (op âŠ¤) â‰« X.Î“evaluation x :=
  evaluation_naturality f âŸ¨x, by simp only [Opens.map_top]; trivialâŸ©

lemma Î“evaluation_naturality_apply (x : X) (a : Y.presheaf.obj (op âŠ¤)) :
    residueFieldMap f x (Y.Î“evaluation (f.base x) a) =
      X.Î“evaluation x (f.c.app (op âŠ¤) a) :=
  evaluation_naturality_apply f âŸ¨x, by simp only [Opens.map_top]; trivialâŸ© a

end LocallyRingedSpace

end AlgebraicGeometry
