/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.Geometry.RingedSpace.LocallyRingedSpace

/-!

# Residue fields of points

Any point `x` of a locally ringed space `X` comes with a natural residue field, namely the residue
field of the stalk at `x`. Moreover, for every open subset of `X` containing `x`, we have a
canonical evaluation map from `Î“(X, U)` to the residue field of `X` at `x`.

## Main definitions

The following are in the `AlgebraicGeometry.LocallyRingedSpace` namespace:

- `residueField`: the residue field of the stalk at `x`.
- `evaluation`: for open subsets `U` of `X` containing `x`, the evaluation map from sections over
  `U` to the residue field at `x`.
- `evaluationMap`: a morphism of locally ringed spaces induces a morphism, i.e. extension, of
  residue fields.

-/

universe u

open CategoryTheory TopologicalSpace Opposite

noncomputable section

namespace AlgebraicGeometry.LocallyRingedSpace

variable (X : LocallyRingedSpace.{u}) {U : Opens X}

/-- The residue field of `X` at a point `x` is the residue field of the stalk of `X`
at `x`. -/
def residueField (x : X) : CommRingCat :=
  CommRingCat.of <| LocalRing.ResidueField (X.stalk x)

instance (x : X) : Field (X.residueField x) :=
  inferInstanceAs <| Field (LocalRing.ResidueField (X.stalk x))

/--
If `U` is an open of `X` containing `x`, we have a canonical ring map from the sections
over `U` to the residue field of `x`.

If we interpret sections over `U` as functions of `X` defined on `U`, then this ring map
corresponds to evaluation at `x`.
-/
def evaluation (x : U) : X.presheaf.obj (op U) âŸ¶ X.residueField x :=
  X.presheaf.germ x â‰« LocalRing.residue _

/-- The global evaluation map from `Î“(X, âŠ¤)` to the residue field at `x`. -/
def Î“evaluation (x : X) : X.presheaf.obj (op âŠ¤) âŸ¶ X.residueField x :=
  X.evaluation âŸ¨x, show x âˆˆ âŠ¤ from trivialâŸ©

@[simp]
lemma evaluation_eq_zero_iff_not_mem_basicOpen (x : U) (f : X.presheaf.obj (op U)) :
    X.evaluation x f = 0 â†” x.val âˆ‰ X.toRingedSpace.basicOpen f := by
  rw [X.toRingedSpace.mem_basicOpen f x, â† not_iff_not, not_not]
  exact (LocalRing.residue_ne_zero_iff_isUnit _)

lemma evaluation_ne_zero_iff_mem_basicOpen (x : U) (f : X.presheaf.obj (op U)) :
    X.evaluation x f â‰  0 â†” x.val âˆˆ X.toRingedSpace.basicOpen f := by
  simp

@[simp]
lemma Î“evaluation_eq_zero_iff_not_mem_basicOpen (x : X) (f : X.presheaf.obj (op âŠ¤)) :
    X.Î“evaluation x f = 0 â†” x âˆ‰ X.toRingedSpace.basicOpen f :=
  evaluation_eq_zero_iff_not_mem_basicOpen X âŸ¨x, show x âˆˆ âŠ¤ by trivialâŸ© f

lemma Î“evaluation_ne_zero_iff_mem_basicOpen (x : X) (f : X.presheaf.obj (op âŠ¤)) :
    X.Î“evaluation x f â‰  0 â†” x âˆˆ X.toRingedSpace.basicOpen f :=
  evaluation_ne_zero_iff_mem_basicOpen X âŸ¨x, show x âˆˆ âŠ¤ by trivialâŸ© f

variable {X Y : LocallyRingedSpace.{u}} (f : X âŸ¶ Y)

/-- If `X âŸ¶ Y` is a morphism of locally ringed spaces and `x` a point of `X`, we obtain
a morphism of residue fields in the other direction. -/
def residueFieldMap (x : X) : Y.residueField (f.val.base x) âŸ¶ X.residueField x :=
  LocalRing.ResidueField.map (LocallyRingedSpace.stalkMap f x)

lemma residue_comp_residueFieldMap_eq_stalkMap_comp_residue (x : X) :
    LocalRing.residue _ â‰« residueFieldMap f x = stalkMap f x â‰« LocalRing.residue _ := by
  simp [residueFieldMap]
  rfl

@[simp]
lemma residueFieldMap_id (x : X) :
    residueFieldMap (ðŸ™ X) x = ðŸ™ (X.residueField x) := by
  simp only [id_val', SheafedSpace.id_base, TopCat.coe_id, id_eq, residueFieldMap, stalkMap]
  have : PresheafedSpace.stalkMap (ðŸ™ X.toSheafedSpace) x = ðŸ™ (X.stalk x) :=
    PresheafedSpace.stalkMap.id _ _
  simp_rw [this]
  apply LocalRing.ResidueField.map_id

@[simp]
lemma residueFieldMap_comp {Z : LocallyRingedSpace.{u}} (g : Y âŸ¶ Z) (x : X) :
    residueFieldMap (f â‰« g) x = residueFieldMap g (f.val.base x) â‰« residueFieldMap f x := by
  simp only [comp_val, SheafedSpace.comp_base, Function.comp_apply, residueFieldMap, stalkMap]
  convert_to LocalRing.ResidueField.map
      (PresheafedSpace.stalkMap g.val (f.val.base x) â‰« PresheafedSpace.stalkMap f.val x) = _
  Â· congr
    apply PresheafedSpace.stalkMap.comp
  Â· apply LocalRing.ResidueField.map_comp

@[reassoc]
lemma evaluation_naturality {V : Opens Y} (x : (Opens.map f.1.base).obj V) :
    Y.evaluation âŸ¨f.val.base x, x.propertyâŸ© â‰« residueFieldMap f x.val =
      f.val.c.app (op V) â‰« X.evaluation x := by
  dsimp only [LocallyRingedSpace.evaluation,
    LocallyRingedSpace.residueFieldMap]
  rw [Category.assoc]
  ext a
  simp only [comp_apply]
  erw [LocalRing.ResidueField.map_residue, PresheafedSpace.stalkMap_germ'_apply]
  rfl

lemma evaluation_naturality_apply {V : Opens Y} (x : (Opens.map f.1.base).obj V)
    (a : Y.presheaf.obj (op V)) :
    residueFieldMap f x.val (Y.evaluation âŸ¨f.val.base x, x.propertyâŸ© a) =
      X.evaluation x (f.val.c.app (op V) a) := by
  simpa using congrFun (congrArg DFunLike.coe <| evaluation_naturality f x) a

@[reassoc]
lemma Î“evaluation_naturality (x : X) :
    Y.Î“evaluation (f.val.base x) â‰« residueFieldMap f x =
      f.val.c.app (op âŠ¤) â‰« X.Î“evaluation x :=
  evaluation_naturality f âŸ¨x, by simp only [Opens.map_top]; trivialâŸ©

lemma Î“evaluation_naturality_apply (x : X) (a : Y.presheaf.obj (op âŠ¤)) :
    residueFieldMap f x (Y.Î“evaluation (f.val.base x) a) =
      X.Î“evaluation x (f.val.c.app (op âŠ¤) a) :=
  evaluation_naturality_apply f âŸ¨x, by simp only [Opens.map_top]; trivialâŸ© a

end LocallyRingedSpace

end AlgebraicGeometry
