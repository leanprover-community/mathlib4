/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Geometry.RingedSpace.Basic
import Mathlib.Geometry.RingedSpace.Stalks

/-!
# The category of locally ringed spaces

We define (bundled) locally ringed spaces (as `SheafedSpace CommRing` along with the fact that the
stalks are local rings), and morphisms between these (morphisms in `SheafedSpace` with
`IsLocalRingHom` on the stalk maps).
-/

-- Explicit universe annotations were used in this file to improve performance #12737

universe u

open CategoryTheory

open TopCat

open TopologicalSpace

open Opposite

open CategoryTheory.Category CategoryTheory.Functor

namespace AlgebraicGeometry

/-- A `LocallyRingedSpace` is a topological space equipped with a sheaf of commutative rings
such that all the stalks are local rings.

A morphism of locally ringed spaces is a morphism of ringed spaces
such that the morphisms induced on stalks are local ring homomorphisms. -/
structure LocallyRingedSpace extends SheafedSpace CommRingCat.{u} where
  /-- Stalks of a locally ringed space are local rings. -/
  localRing : ‚àÄ x, LocalRing (presheaf.stalk x)

attribute [instance] LocallyRingedSpace.localRing

namespace LocallyRingedSpace

variable (X : LocallyRingedSpace.{u})

/-- An alias for `toSheafedSpace`, where the result type is a `RingedSpace`.
This allows us to use dot-notation for the `RingedSpace` namespace.
 -/
def toRingedSpace : RingedSpace :=
  X.toSheafedSpace

/-- The underlying topological space of a locally ringed space. -/
def toTopCat : TopCat :=
  X.1.carrier

instance : CoeSort LocallyRingedSpace (Type u) :=
  ‚ü®fun X : LocallyRingedSpace => (X.toTopCat : Type _)‚ü©

instance (x : X) : LocalRing (X.presheaf.stalk x) :=
  X.localRing x

-- PROJECT: how about a typeclass "HasStructureSheaf" to mediate the ùí™ notation, rather
-- than defining it over and over for `PresheafedSpace`, `LocallyRingedSpace`, `Scheme`, etc.
/-- The structure sheaf of a locally ringed space. -/
def ùí™ : Sheaf CommRingCat X.toTopCat :=
  X.sheaf

/-- A morphism of locally ringed spaces is a morphism of ringed spaces
 such that the morphisms induced on stalks are local ring homomorphisms. -/
@[ext]
structure Hom (X Y : LocallyRingedSpace.{u}) : Type _ where
  /-- the underlying morphism between ringed spaces -/
  val : X.toSheafedSpace ‚ü∂ Y.toSheafedSpace
  /-- the underlying morphism induces a local ring homomorphism on stalks -/
  prop : ‚àÄ x, IsLocalRingHom (val.stalkMap x)

instance : Quiver LocallyRingedSpace :=
  ‚ü®Hom‚ü©

@[ext] lemma Hom.ext' (X Y : LocallyRingedSpace.{u}) {f g : X ‚ü∂ Y} (h : f.val = g.val) : f = g :=
  Hom.ext h

/-- A morphism of locally ringed spaces `f : X ‚ü∂ Y` induces
a local ring homomorphism from `Y.stalk (f x)` to `X.stalk x` for any `x : X`.
-/
noncomputable def Hom.stalkMap {X Y : LocallyRingedSpace.{u}} (f : Hom X Y) (x : X) :
    Y.presheaf.stalk (f.1.1 x) ‚ü∂ X.presheaf.stalk x :=
  f.val.stalkMap x

instance {X Y : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (x : X) : IsLocalRingHom (f.stalkMap x) :=
  f.2 x

instance {X Y : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (x : X) :
    IsLocalRingHom (f.val.stalkMap x) :=
  f.2 x

/-- The identity morphism on a locally ringed space. -/
@[simps]
def id (X : LocallyRingedSpace.{u}) : Hom X X :=
  ‚ü®ùüô _, fun x => by erw [PresheafedSpace.stalkMap.id]; apply isLocalRingHom_id‚ü©

instance (X : LocallyRingedSpace.{u}) : Inhabited (Hom X X) :=
  ‚ü®id X‚ü©

/-- Composition of morphisms of locally ringed spaces. -/
def comp {X Y Z : LocallyRingedSpace.{u}} (f : Hom X Y) (g : Hom Y Z) : Hom X Z :=
  ‚ü®f.val ‚â´ g.val, fun x => by
    erw [PresheafedSpace.stalkMap.comp]
    exact @isLocalRingHom_comp _ _ _ _ _ _ _ _ (f.2 _) (g.2 _)‚ü©

/-- The category of locally ringed spaces. -/
instance : Category LocallyRingedSpace.{u} where
  Hom := Hom
  id := id
  comp {X Y Z} f g := comp f g
  comp_id {X Y} f := Hom.ext <| by simp [comp]
  id_comp {X Y} f := Hom.ext <| by simp [comp]
  assoc {_ _ _ _} f g h := Hom.ext <| by simp [comp]

/-- The forgetful functor from `LocallyRingedSpace` to `SheafedSpace CommRing`. -/
@[simps]
def forgetToSheafedSpace : LocallyRingedSpace.{u} ‚•§ SheafedSpace CommRingCat.{u} where
  obj X := X.toSheafedSpace
  map {X Y} f := f.1

instance : forgetToSheafedSpace.Faithful where
  map_injective {_ _} _ _ h := Hom.ext h

/-- The forgetful functor from `LocallyRingedSpace` to `Top`. -/
@[simps!]
def forgetToTop : LocallyRingedSpace.{u} ‚•§ TopCat.{u} :=
  forgetToSheafedSpace ‚ãô SheafedSpace.forget _

@[simp]
theorem comp_val {X Y Z : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).val = f.val ‚â´ g.val :=
  rfl

@[simp] theorem id_val' (X : LocallyRingedSpace.{u}) : Hom.val (ùüô X) = ùüô X.toSheafedSpace :=
  rfl

-- Porting note: complains that `(f ‚â´ g).val.c` can be further simplified
-- so changed to its simp normal form `(f.val ‚â´ g.val).c`
@[simp]
theorem comp_val_c {X Y Z : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f.1 ‚â´ g.1).c = g.val.c ‚â´ (Presheaf.pushforward _ g.val.base).map f.val.c :=
  rfl

theorem comp_val_c_app {X Y Z : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U : (Opens Z)·µí·µñ) :
    (f ‚â´ g).val.c.app U = g.val.c.app U ‚â´ f.val.c.app (op <| (Opens.map g.val.base).obj U.unop) :=
  rfl

/-- Given two locally ringed spaces `X` and `Y`, an isomorphism between `X` and `Y` as _sheafed_
spaces can be lifted to a morphism `X ‚ü∂ Y` as locally ringed spaces.

See also `isoOfSheafedSpaceIso`.
-/
@[simps]
def homOfSheafedSpaceHomOfIsIso {X Y : LocallyRingedSpace.{u}}
    (f : X.toSheafedSpace ‚ü∂ Y.toSheafedSpace) [IsIso f] : X ‚ü∂ Y :=
  Hom.mk f fun x =>
    -- Here we need to see that the stalk maps are really local ring homomorphisms.
    -- This can be solved by type class inference, because stalk maps of isomorphisms
    -- are isomorphisms and isomorphisms are local ring homomorphisms.
    show IsLocalRingHom ((SheafedSpace.forgetToPresheafedSpace.map f).stalkMap x) by
      infer_instance

/-- Given two locally ringed spaces `X` and `Y`, an isomorphism between `X` and `Y` as _sheafed_
spaces can be lifted to an isomorphism `X ‚ü∂ Y` as locally ringed spaces.

This is related to the property that the functor `forgetToSheafedSpace` reflects isomorphisms.
In fact, it is slightly stronger as we do not require `f` to come from a morphism between
_locally_ ringed spaces.
-/
def isoOfSheafedSpaceIso {X Y : LocallyRingedSpace.{u}} (f : X.toSheafedSpace ‚âÖ Y.toSheafedSpace) :
    X ‚âÖ Y where
  hom := homOfSheafedSpaceHomOfIsIso f.hom
  inv := homOfSheafedSpaceHomOfIsIso f.inv
  hom_inv_id := Hom.ext f.hom_inv_id
  inv_hom_id := Hom.ext f.inv_hom_id

instance : forgetToSheafedSpace.ReflectsIsomorphisms where reflects {_ _} f i :=
  { out :=
      ‚ü®homOfSheafedSpaceHomOfIsIso (CategoryTheory.inv (forgetToSheafedSpace.map f)),
        Hom.ext (IsIso.hom_inv_id (I := i)), Hom.ext (IsIso.inv_hom_id (I := i))‚ü© }

instance is_sheafedSpace_iso {X Y : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) [IsIso f] : IsIso f.1 :=
  LocallyRingedSpace.forgetToSheafedSpace.map_isIso f

/-- The restriction of a locally ringed space along an open embedding.
-/
@[simps!]
def restrict {U : TopCat} (X : LocallyRingedSpace.{u}) {f : U ‚ü∂ X.toTopCat} (h : OpenEmbedding f) :
    LocallyRingedSpace where
  localRing := by
    intro x
    -- We show that the stalk of the restriction is isomorphic to the original stalk,
    apply @RingEquiv.localRing _ _ _ (X.localRing (f x))
    exact (X.restrictStalkIso h x).symm.commRingCatIsoToRingEquiv
  toSheafedSpace := X.toSheafedSpace.restrict h

/-- The canonical map from the restriction to the subspace. -/
def ofRestrict {U : TopCat} (X : LocallyRingedSpace.{u})
    {f : U ‚ü∂ X.toTopCat} (h : OpenEmbedding f) : X.restrict h ‚ü∂ X :=
  ‚ü®X.toPresheafedSpace.ofRestrict h, fun _ => inferInstance‚ü©

/-- The restriction of a locally ringed space `X` to the top subspace is isomorphic to `X` itself.
-/
def restrictTopIso (X : LocallyRingedSpace.{u}) :
    X.restrict (Opens.openEmbedding ‚ä§) ‚âÖ X :=
  isoOfSheafedSpaceIso X.toSheafedSpace.restrictTopIso

/-- The global sections, notated Gamma.
-/
def Œì : LocallyRingedSpace.{u}·µí·µñ ‚•§ CommRingCat.{u} :=
  forgetToSheafedSpace.op ‚ãô SheafedSpace.Œì

theorem Œì_def : Œì = forgetToSheafedSpace.op ‚ãô SheafedSpace.Œì :=
  rfl

@[simp]
theorem Œì_obj (X : LocallyRingedSpace.{u}·µí·µñ) : Œì.obj X = X.unop.presheaf.obj (op ‚ä§) :=
  rfl

theorem Œì_obj_op (X : LocallyRingedSpace.{u}) : Œì.obj (op X) = X.presheaf.obj (op ‚ä§) :=
  rfl

@[simp]
theorem Œì_map {X Y : LocallyRingedSpace.{u}·µí·µñ} (f : X ‚ü∂ Y) : Œì.map f = f.unop.1.c.app (op ‚ä§) :=
  rfl

theorem Œì_map_op {X Y : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) : Œì.map f.op = f.1.c.app (op ‚ä§) :=
  rfl

/-- The empty locally ringed space. -/
def empty : LocallyRingedSpace.{u} where
  carrier := TopCat.of PEmpty
  presheaf := (CategoryTheory.Functor.const _).obj (CommRingCat.of PUnit)
  IsSheaf := Presheaf.isSheaf_of_isTerminal _ CommRingCat.punitIsTerminal
  localRing x := PEmpty.elim x

instance : EmptyCollection LocallyRingedSpace.{u} := ‚ü®LocallyRingedSpace.empty‚ü©

/-- The canonical map from the empty locally ringed space. -/
def emptyTo (X : LocallyRingedSpace) : ‚àÖ ‚ü∂ X :=
  ‚ü®‚ü®‚ü®fun x => PEmpty.elim x, by fun_prop‚ü©,
    { app := fun U => by refine ‚ü®‚ü®‚ü®0, ?_‚ü©, ?_‚ü©, ?_, ?_‚ü© <;> intros <;> rfl }‚ü©,
    fun x => PEmpty.elim x‚ü©

noncomputable
instance {X : LocallyRingedSpace} : Unique (‚àÖ ‚ü∂ X) where
  default := LocallyRingedSpace.emptyTo X
  uniq f := by ext ‚ü®‚ü© x; aesop_cat

/-- The empty space is initial in `LocallyRingedSpace`. -/
noncomputable
def emptyIsInitial : Limits.IsInitial (‚àÖ : LocallyRingedSpace.{u}) := Limits.IsInitial.ofUnique _

theorem preimage_basicOpen {X Y : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) {U : Opens Y}
    (s : Y.presheaf.obj (op U)) :
    (Opens.map f.1.base).obj (Y.toRingedSpace.basicOpen s) =
      @RingedSpace.basicOpen X.toRingedSpace ((Opens.map f.1.base).obj U) (f.1.c.app _ s) := by
  ext x
  constructor
  ¬∑ rintro ‚ü®‚ü®y, hyU‚ü©, hy : IsUnit _, rfl : y = _‚ü©
    erw [RingedSpace.mem_basicOpen _ _ ‚ü®x, show x ‚àà (Opens.map f.1.base).obj U from hyU‚ü©,
      ‚Üê PresheafedSpace.stalkMap_germ_apply]
    exact (f.val.stalkMap _).isUnit_map hy
  ¬∑ rintro ‚ü®y, hy : IsUnit _, rfl‚ü©
    erw [RingedSpace.mem_basicOpen _ _ ‚ü®f.1.base y.1, y.2‚ü©]
    erw [‚Üê PresheafedSpace.stalkMap_germ_apply] at hy
    exact (isUnit_map_iff (f.val.stalkMap _) _).mp hy

-- This actually holds for all ringed spaces with nontrivial stalks.
theorem basicOpen_zero (X : LocallyRingedSpace.{u}) (U : Opens X.carrier) :
    X.toRingedSpace.basicOpen (0 : X.presheaf.obj <| op U) = ‚ä• := by
  ext x
  simp only [RingedSpace.basicOpen, Opens.coe_mk, Set.mem_image, Set.mem_setOf_eq, Subtype.exists,
    exists_and_right, exists_eq_right, Opens.coe_bot, Set.mem_empty_iff_false,
    iff_false, not_exists]
  intros hx
  rw [map_zero, isUnit_zero_iff]
  change (0 : X.presheaf.stalk x) ‚â† (1 : X.presheaf.stalk x)
  exact zero_ne_one

@[simp]
lemma basicOpen_eq_bot_of_isNilpotent (X : LocallyRingedSpace.{u}) (U : Opens X.carrier)
    (f : (X.presheaf.obj <| op U)) (hf : IsNilpotent f) :
    X.toRingedSpace.basicOpen f = ‚ä• := by
  obtain ‚ü®n, hn‚ü© := hf
  cases n.eq_zero_or_pos with
  | inr h =>
    rw [‚Üê  X.toRingedSpace.basicOpen_pow f n h, hn]
    simp [basicOpen_zero]
  | inl h =>
    rw [h, pow_zero] at hn
    simp [eq_zero_of_zero_eq_one hn.symm f, basicOpen_zero]

instance component_nontrivial (X : LocallyRingedSpace.{u}) (U : Opens X.carrier) [hU : Nonempty U] :
    Nontrivial (X.presheaf.obj <| op U) :=
  (X.presheaf.germ hU.some).domain_nontrivial

@[simp]
lemma iso_hom_val_base_inv_val_base {X Y : LocallyRingedSpace.{u}} (e : X ‚âÖ Y) :
    e.hom.val.base ‚â´ e.inv.val.base = ùüô _ := by
  rw [‚Üê SheafedSpace.comp_base, ‚Üê LocallyRingedSpace.comp_val]
  simp

@[simp]
lemma iso_hom_val_base_inv_val_base_apply {X Y : LocallyRingedSpace.{u}} (e : X ‚âÖ Y) (x : X) :
    (e.inv.val.base (e.hom.val.base x)) = x := by
  show (e.hom.val.base ‚â´ e.inv.val.base) x = ùüô X.toPresheafedSpace x
  simp

@[simp]
lemma iso_inv_val_base_hom_val_base {X Y : LocallyRingedSpace.{u}} (e : X ‚âÖ Y) :
    e.inv.val.base ‚â´ e.hom.val.base = ùüô _ := by
  rw [‚Üê SheafedSpace.comp_base, ‚Üê LocallyRingedSpace.comp_val]
  simp

@[simp]
lemma iso_inv_val_base_hom_val_base_apply {X Y : LocallyRingedSpace.{u}} (e : X ‚âÖ Y) (y : Y) :
    (e.hom.val.base (e.inv.val.base y)) = y := by
  show (e.inv.val.base ‚â´ e.hom.val.base) y = ùüô Y.toPresheafedSpace y
  simp

section Stalks

variable {X Y Z : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

@[simp]
lemma stalkMap_id (X : LocallyRingedSpace.{u}) (x : X) :
    (ùüô X : X ‚ü∂ X).stalkMap x = ùüô (X.presheaf.stalk x) :=
  PresheafedSpace.stalkMap.id _ x

lemma stalkMap_comp (x : X) :
    (f ‚â´ g : X ‚ü∂ Z).stalkMap x = g.stalkMap (f.val.base x) ‚â´ f.stalkMap x :=
  PresheafedSpace.stalkMap.comp f.val g.val x

@[reassoc]
lemma stalkSpecializes_stalkMap (x x' : X) (h : x ‚§≥ x') :
    Y.presheaf.stalkSpecializes (f.val.base.map_specializes h) ‚â´ f.stalkMap x =
      f.stalkMap x' ‚â´ X.presheaf.stalkSpecializes h :=
  PresheafedSpace.stalkMap.stalkSpecializes_stalkMap f.val h

lemma stalkSpecializes_stalkMap_apply (x x' : X) (h : x ‚§≥ x') (y) :
    f.stalkMap x (Y.presheaf.stalkSpecializes (f.val.base.map_specializes h) y) =
      (X.presheaf.stalkSpecializes h (f.stalkMap x' y)) :=
  DFunLike.congr_fun (stalkSpecializes_stalkMap f x x' h) y

@[reassoc]
lemma stalkMap_congr (f g : X ‚ü∂ Y) (hfg : f = g) (x x' : X) (hxx' : x = x') :
    f.stalkMap x ‚â´ X.presheaf.stalkSpecializes (specializes_of_eq hxx'.symm) =
      Y.presheaf.stalkSpecializes (specializes_of_eq <| hfg ‚ñ∏ hxx' ‚ñ∏ rfl) ‚â´ g.stalkMap x' := by
  subst hfg
  subst hxx'
  simp

@[reassoc]
lemma stalkMap_congr_hom (f g : X ‚ü∂ Y) (hfg : f = g) (x : X) :
    f.stalkMap x = Y.presheaf.stalkSpecializes (specializes_of_eq <| hfg ‚ñ∏ rfl) ‚â´
      g.stalkMap x := by
  subst hfg
  simp

@[reassoc]
lemma stalkMap_congr_point {X Y : LocallyRingedSpace.{u}} (f : X ‚ü∂ Y) (x x' : X) (hxx' : x = x') :
    f.stalkMap x ‚â´ X.presheaf.stalkSpecializes (specializes_of_eq hxx'.symm) =
      Y.presheaf.stalkSpecializes (specializes_of_eq <| hxx' ‚ñ∏ rfl) ‚â´ f.stalkMap x' := by
  subst hxx'
  simp

@[reassoc (attr := simp)]
lemma stalkMap_hom_inv (e : X ‚âÖ Y) (y : Y) :
    e.hom.stalkMap (e.inv.val.base y) ‚â´ e.inv.stalkMap y =
      Y.presheaf.stalkSpecializes (specializes_of_eq <| by simp) := by
  rw [‚Üê stalkMap_comp, LocallyRingedSpace.stalkMap_congr_hom (e.inv ‚â´ e.hom) (ùüô _) (by simp)]
  simp

@[simp]
lemma stalkMap_hom_inv_apply (e : X ‚âÖ Y) (y : Y) (z) :
    e.inv.stalkMap y (e.hom.stalkMap (e.inv.val.base y) z) =
      Y.presheaf.stalkSpecializes (specializes_of_eq <| by simp) z :=
  DFunLike.congr_fun (stalkMap_hom_inv e y) z

@[reassoc (attr := simp)]
lemma stalkMap_inv_hom (e : X ‚âÖ Y) (x : X) :
    e.inv.stalkMap (e.hom.val.base x) ‚â´ e.hom.stalkMap x =
      X.presheaf.stalkSpecializes (specializes_of_eq <| by simp) := by
  rw [‚Üê stalkMap_comp, LocallyRingedSpace.stalkMap_congr_hom (e.hom ‚â´ e.inv) (ùüô _) (by simp)]
  simp

@[simp]
lemma stalkMap_inv_hom_apply (e : X ‚âÖ Y) (x : X) (y) :
    e.hom.stalkMap x (e.inv.stalkMap (e.hom.val.base x) y) =
      X.presheaf.stalkSpecializes (specializes_of_eq <| by simp) y :=
  DFunLike.congr_fun (stalkMap_inv_hom e x) y

@[reassoc]
lemma stalkMap_germ (U : Opens Y)
    (x : (Opens.map f.val.base).obj U) :
    Y.presheaf.germ ‚ü®f.val.base x.val, x.property‚ü© ‚â´ f.stalkMap x.val =
      f.val.c.app (op U) ‚â´ X.presheaf.germ x :=
  PresheafedSpace.stalkMap_germ f.val U x

lemma stalkMap_germ_apply (U : Opens Y) (x : (Opens.map f.val.base).obj U) (y) :
    f.stalkMap x.val (Y.presheaf.germ ‚ü®f.val.base x.val, x.property‚ü© y) =
      X.presheaf.germ x (f.val.c.app (op U) y) :=
  PresheafedSpace.stalkMap_germ_apply f.val U x y

@[reassoc (attr := simp)]
lemma stalkMap_germ' (U : Opens Y) (x : X) (hx : f.val.base x ‚àà U) :
    Y.presheaf.germ ‚ü®f.val.base x, hx‚ü© ‚â´ f.stalkMap x =
      f.val.c.app (op U) ‚â´ X.presheaf.germ (U := (Opens.map f.val.base).obj U) ‚ü®x, hx‚ü© :=
  PresheafedSpace.stalkMap_germ' f.val U x hx

@[simp]
lemma stalkMap_germ'_apply
    (U : Opens Y) (x : X) (hx : f.val.base x ‚àà U) (y : Y.presheaf.obj (op U)) :
    f.stalkMap x (Y.presheaf.germ (U := U) ‚ü®f.val.base x, hx‚ü© y) =
      X.presheaf.germ (U := (Opens.map f.val.base).obj U) ‚ü®x, hx‚ü© (f.val.c.app (op U) y) :=
  PresheafedSpace.stalkMap_germ_apply f.val U ‚ü®x, hx‚ü© y

variable {U : TopCat} (X : LocallyRingedSpace.{u}) {f : U ‚ü∂ X.toTopCat} (h : OpenEmbedding f)
  (V : Opens U) (x : U) (hx : x ‚àà V)

/-- For an open embedding `f : U ‚ü∂ X` and a point `x : U`, we get an isomorphism between the stalk
of `X` at `f x` and the stalk of the restriction of `X` along `f` at t `x`. -/
noncomputable
def restrictStalkIso : (X.restrict h).presheaf.stalk x ‚âÖ X.presheaf.stalk (f x) :=
  X.toPresheafedSpace.restrictStalkIso h x

@[reassoc (attr := simp)]
lemma restrictStalkIso_hom_eq_germ :
    (X.restrict h).presheaf.germ ‚ü®x, hx‚ü© ‚â´ (X.restrictStalkIso h x).hom =
      X.presheaf.germ ‚ü®f x, show f x ‚àà h.isOpenMap.functor.obj V from ‚ü®x, hx, rfl‚ü©‚ü© :=
  PresheafedSpace.restrictStalkIso_hom_eq_germ X.toPresheafedSpace h V x hx

lemma restrictStalkIso_hom_eq_germ_apply (y) :
    (X.restrictStalkIso h x).hom ((X.restrict h).presheaf.germ ‚ü®x, hx‚ü© y) =
      X.presheaf.germ ‚ü®f x, show f x ‚àà h.isOpenMap.functor.obj V from ‚ü®x, hx, rfl‚ü©‚ü© y :=
  PresheafedSpace.restrictStalkIso_hom_eq_germ_apply X.toPresheafedSpace h V x hx y

@[reassoc (attr := simp)]
lemma restrictStalkIso_inv_eq_germ :
    X.presheaf.germ ‚ü®f x, show f x ‚àà h.isOpenMap.functor.obj V from ‚ü®x, hx, rfl‚ü©‚ü© ‚â´
      (X.restrictStalkIso h x).inv = (X.restrict h).presheaf.germ ‚ü®x, hx‚ü© :=
  PresheafedSpace.restrictStalkIso_inv_eq_germ X.toPresheafedSpace h V x hx

lemma restrictStalkIso_inv_eq_germ_apply (y) :
    (X.restrictStalkIso h x).inv
      (X.presheaf.germ ‚ü®f x, show f x ‚àà h.isOpenMap.functor.obj V from ‚ü®x, hx, rfl‚ü©‚ü© y) =
        (X.restrict h).presheaf.germ ‚ü®x, hx‚ü© y :=
  PresheafedSpace.restrictStalkIso_inv_eq_germ_apply X.toPresheafedSpace h V x hx y

lemma restrictStalkIso_inv_eq_ofRestrict :
    (X.restrictStalkIso h x).inv = (X.ofRestrict h).stalkMap x :=
  PresheafedSpace.restrictStalkIso_inv_eq_ofRestrict X.toPresheafedSpace h x

instance ofRestrict_stalkMap_isIso : IsIso ((X.ofRestrict h).stalkMap x) :=
  PresheafedSpace.ofRestrict_stalkMap_isIso X.toPresheafedSpace h x

end Stalks

end LocallyRingedSpace

end AlgebraicGeometry
