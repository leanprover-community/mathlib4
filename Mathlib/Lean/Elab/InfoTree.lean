/-
Copyright (c) 2025 Marc Huisinga. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Marc Huisinga
-/
import Mathlib.Init
import Mathlib.Lean.Environment
import Lean.Server.InfoUtils
import Lean.Meta.TryThis

/-!
# Additions to `Lean.Elab.InfoTree.Main`
-/

namespace Lean.Elab

open Lean.Meta
open Lean.Meta.Tactic.TryThis

/--
Collects all suggestions from all `TryThisInfo`s in `trees`.
`trees` is visited in order and suggestions in each tree are collected in post-order.
-/
def collectTryThisSuggestions (trees : PersistentArray InfoTree) :
    Array Suggestion :=
  go.run #[] |>.2
where
  /-- Visits all trees. -/
  go : StateM (Array Suggestion) Unit := do
    for tree in trees do
      tree.visitM' (postNode := visitNode)
  /-- Visits a node in a tree. -/
  @[nolint unusedArguments]
  visitNode (_ctx : ContextInfo) (i : Info) (_children : PersistentArray InfoTree) :
      StateM (Array Suggestion) Unit := do
    let .ofCustomInfo ci := i
      | return
    let some tti := ci.value.get? TryThisInfo
      | return
    modify (Â·.push tti.suggestion)

namespace InfoTree

/--
Get the `parentDecl`s of every elaborated body.

This includes `let rec`/`where` definitions, but excludes decls without "bodies" (such as
`alias`es, `structure`s, declarations generated by atttributes like `@[ext]`, and so on) as we
might find by considering every `parentDeclCtx` throughout the infotree.

Assumes that every body elaboration proceeds through `Lean.Elab.Term.BodyInfo`.
-/
def getDeclsByBody (t : InfoTree) : List Name :=
  t.collectNodesBottomUp fun ctx i _ decls =>
    match i with
    | .ofCustomInfo i =>
      if i.value.typeName == ``Lean.Elab.Term.BodyInfo then
        if let some decl := ctx.parentDecl? then
          decl :: decls
        else decls
      else decls
    | _ => decls

/--
Get the declarations elaborated in the infotree `t` which are theorems according to the
environment. This includes e.g. `instance`s of `Prop` classes in addition to declarations declared
using the keyword `theorem` directly.
-/
def getTheorems (t : InfoTree) (env : Environment) : List ConstantVal :=
  t.getDeclsByBody.filterMap env.findTheoremConstVal?

end Lean.Elab.InfoTree
