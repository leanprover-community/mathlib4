/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Basic
import Mathlib.CategoryTheory.Adjunction.Reflective
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Opposites
import Mathlib.Util.Superscript

/-!
# Simplicial objects in a category.

A simplicial object in a category `C` is a `C`-valued presheaf on `SimplexCategory`.
(Similarly, a cosimplicial object is a functor `SimplexCategory â¥¤ C`.)

## Notation

The following notations can be enabled via `open Simplicial`.

- `X _â¦‹nâ¦Œ` denotes the `n`-th term of a simplicial object `X`, where `n : â„•`.
- `X ^â¦‹nâ¦Œ` denotes the `n`-th term of a cosimplicial object `X`, where `n : â„•`.

The following notations can be enabled via
`open CategoryTheory.SimplicialObject.Truncated`.

- `X _â¦‹mâ¦Œâ‚™` denotes the `m`-th term of an `n`-truncated simplicial object `X`.
- `X ^â¦‹mâ¦Œâ‚™` denotes the `m`-th term of an `n`-truncated cosimplicial object `X`.
-/

open Opposite

open CategoryTheory

open CategoryTheory.Limits CategoryTheory.Functor

universe v u v' u'

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- The category of simplicial objects valued in a category `C`.
This is the category of contravariant functors from `SimplexCategory` to `C`. -/
def SimplicialObject :=
  SimplexCategoryáµ’áµ– â¥¤ C

@[simps!]
instance : Category (SimplicialObject C) := by
  dsimp only [SimplicialObject]
  infer_instance

namespace SimplicialObject

set_option quotPrecheck false in
/-- `X _â¦‹nâ¦Œ` denotes the `n`th-term of the simplicial object X -/
scoped[Simplicial]
  notation3:1000 X " _â¦‹" n "â¦Œ" =>
      (X : CategoryTheory.SimplicialObject _).obj (Opposite.op (SimplexCategory.mk n))

open Simplicial

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  infer_instance

instance [HasLimits C] : HasLimits (SimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  infer_instance

instance [HasColimits C] : HasColimits (SimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

variable {C}

@[ext]
lemma hom_ext {X Y : SimplicialObject C} (f g : X âŸ¶ Y)
    (h : âˆ€ (n : SimplexCategoryáµ’áµ–), f.app n = g.app n) : f = g :=
  NatTrans.ext (by ext; apply h)

variable (X : SimplicialObject C)

/-- Face maps for a simplicial object. -/
def Î´ {n} (i : Fin (n + 2)) : X _â¦‹n + 1â¦Œ âŸ¶ X _â¦‹nâ¦Œ :=
  X.map (SimplexCategory.Î´ i).op

/-- Degeneracy maps for a simplicial object. -/
def Ïƒ {n} (i : Fin (n + 1)) : X _â¦‹nâ¦Œ âŸ¶ X _â¦‹n + 1â¦Œ :=
  X.map (SimplexCategory.Ïƒ i).op

/-- The diagonal of a simplex is the long edge of the simplex. -/
def diagonal {n : â„•} : X _â¦‹nâ¦Œ âŸ¶ X _â¦‹1â¦Œ := X.map ((SimplexCategory.diag n).op)

/-- Isomorphisms from identities in â„•. -/
def eqToIso {n m : â„•} (h : n = m) : X _â¦‹nâ¦Œ â‰… X _â¦‹mâ¦Œ :=
  X.mapIso (CategoryTheory.eqToIso (by congr))

@[simp]
theorem eqToIso_refl {n : â„•} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  simp [eqToIso]

/-- The generic case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    X.Î´ j.succ â‰« X.Î´ i = X.Î´ (Fin.castSucc i) â‰« X.Î´ j := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´ H]

@[reassoc]
theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Î´ j â‰« X.Î´ i =
      X.Î´ (Fin.castSucc i) â‰«
        X.Î´ (j.pred fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´' H]
@[reassoc]
theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ j.succ â‰« X.Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) =
      X.Î´ i â‰« X.Î´ j := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´'' H]

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} :
    X.Î´ (Fin.castSucc i) â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´_self]

@[reassoc]
theorem Î´_comp_Î´_self' {n} {j : Fin (n + 3)} {i : Fin (n + 2)} (H : j = Fin.castSucc i) :
    X.Î´ j â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i := by
  subst H
  rw [Î´_comp_Î´_self]

/-- The second simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    X.Ïƒ j.succ â‰« X.Î´ (Fin.castSucc i) = X.Î´ i â‰« X.Ïƒ j := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_le H]

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} : X.Ïƒ i â‰« X.Î´ (Fin.castSucc i) = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_self, op_id, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Ïƒ i â‰« X.Î´ j = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_self]

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : X.Ïƒ i â‰« X.Î´ i.succ = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_succ, op_id, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Ïƒ i â‰« X.Î´ j = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_succ]

/-- The fourth simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Ïƒ (Fin.castSucc j) â‰« X.Î´ i.succ = X.Î´ i â‰« X.Ïƒ j := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt H]

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Ïƒ j â‰« X.Î´ i =
      X.Î´ (i.pred fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) â‰«
        X.Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt' H]

/-- The fifth simplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    X.Ïƒ j â‰« X.Ïƒ (Fin.castSucc i) = X.Ïƒ i â‰« X.Ïƒ j.succ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Ïƒ_comp_Ïƒ H]

open Simplicial

@[reassoc (attr := simp)]
theorem Î´_naturality {X' X : SimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 2)) :
    X.Î´ i â‰« f.app (op â¦‹nâ¦Œ) = f.app (op â¦‹n + 1â¦Œ) â‰« X'.Î´ i :=
  f.naturality _

@[reassoc (attr := simp)]
theorem Ïƒ_naturality {X' X : SimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« f.app (op â¦‹n + 1â¦Œ) = f.app (op â¦‹nâ¦Œ) â‰« X'.Ïƒ i :=
  f.naturality _

variable (C)

/-- Functor composition induces a functor on simplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C â¥¤ D) â¥¤ SimplicialObject C â¥¤ SimplicialObject D :=
  whiskeringRight _ _ _

/-- Truncated simplicial objects. -/
def Truncated (n : â„•) :=
  (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C

instance {n : â„•} : Category (Truncated C n) := by
  dsimp [Truncated]
  infer_instance

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasLimits C] : HasLimits (SimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasColimits C] : HasColimits (SimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

variable (C) in
/-- Functor composition induces a functor on truncated simplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C â¥¤ D) â¥¤ Truncated C n â¥¤ Truncated D n :=
  whiskeringRight _ _ _

open Mathlib.Tactic (subscriptTerm) in
/-- For `X : Truncated C n` and `m â‰¤ n`, `X _â¦‹mâ¦Œâ‚™` is the `m`-th term of X. The
proof `p : m â‰¤ n` can also be provided using the syntax `X _â¦‹m, pâ¦Œâ‚™`. -/
scoped syntax:max (name := mkNotation)
  term " _â¦‹" term ("," term)? "â¦Œ" noWs subscriptTerm : term

open scoped SimplexCategory.Truncated in
scoped macro_rules
  | `($X:term _â¦‹$m:termâ¦Œ$n:subscript) =>
    `(($X : CategoryTheory.SimplicialObject.Truncated _ $n).obj
      (Opposite.op âŸ¨SimplexCategory.mk $m, by first | get_elem_tactic |
      fail "Failed to prove truncation property. Try writing `X _â¦‹m, by ...â¦Œâ‚™`."âŸ©))
  | `($X:term _â¦‹$m:term, $p:termâ¦Œ$n:subscript) =>
    `(($X : CategoryTheory.SimplicialObject.Truncated _ $n).obj
      (Opposite.op âŸ¨SimplexCategory.mk $m, $pâŸ©))

variable (C) in
/-- Further truncation of truncated simplicial objects. -/
@[simps!]
def trunc (n m : â„•) (h : m â‰¤ n := by omega) : Truncated C n â¥¤ Truncated C m :=
  (whiskeringLeft _ _ _).obj (SimplexCategory.Truncated.incl m n).op

end Truncated

section Truncation

/-- The truncation functor from simplicial objects to truncated simplicial objects. -/
def truncation (n : â„•) : SimplicialObject C â¥¤ SimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj (SimplexCategory.Truncated.inclusion n).op

/-- For all `m â‰¤ n`, `truncation m` factors through `Truncated n`. -/
def truncationCompTrunc {n m : â„•} (h : m â‰¤ n) :
    truncation n â‹™ Truncated.trunc C n m â‰… truncation m :=
  Iso.refl _

end Truncation


noncomputable section

/-- The n-skeleton as a functor `SimplicialObject.Truncated C n â¥¤ SimplicialObject C`. -/
protected abbrev Truncated.sk (n : â„•) [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasLeftKanExtension F] :
    SimplicialObject.Truncated C n â¥¤ SimplicialObject C :=
  lan (SimplexCategory.Truncated.inclusion n).op

/-- The n-coskeleton as a functor `SimplicialObject.Truncated C n â¥¤ SimplicialObject C`. -/
protected abbrev Truncated.cosk (n : â„•) [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasRightKanExtension F] :
    SimplicialObject.Truncated C n â¥¤ SimplicialObject C :=
  ran (SimplexCategory.Truncated.inclusion n).op

/-- The n-skeleton as an endofunctor on `SimplicialObject C`. -/
abbrev sk (n : â„•) [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasLeftKanExtension F] :
    SimplicialObject C â¥¤ SimplicialObject C := truncation n â‹™ Truncated.sk n

/-- The n-coskeleton as an endofunctor on `SimplicialObject C`. -/
abbrev cosk (n : â„•) [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasRightKanExtension F] :
    SimplicialObject C â¥¤ SimplicialObject C := truncation n â‹™ Truncated.cosk n

end

section adjunctions
/- When the left and right Kan extensions exist, `Truncated.sk n` and `Truncated.cosk n`
respectively define left and right adjoints to `truncation n`. -/


variable (n : â„•)
variable [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasRightKanExtension F]
variable [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasLeftKanExtension F]

/-- The adjunction between the n-skeleton and n-truncation. -/
noncomputable def skAdj : Truncated.sk (C := C) n âŠ£ truncation n :=
  lanAdjunction _ _

/-- The adjunction between n-truncation and the n-coskeleton. -/
noncomputable def coskAdj : truncation (C := C) n âŠ£ Truncated.cosk n :=
  ranAdjunction _ _

instance : ((sk n).obj X).IsLeftKanExtension ((skAdj n).unit.app _) := by
  dsimp [sk, skAdj]
  rw [lanAdjunction_unit]
  infer_instance

instance : ((cosk n).obj X).IsRightKanExtension ((coskAdj n).counit.app _) := by
  dsimp [cosk, coskAdj]
  rw [ranAdjunction_counit]
  infer_instance

namespace Truncated
/- When the left and right Kan extensions exist and are pointwise Kan extensions,
`skAdj n` and `coskAdj n` are respectively coreflective and reflective. -/

variable [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasPointwiseRightKanExtension F]
variable [âˆ€ (F : (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C),
    (SimplexCategory.Truncated.inclusion n).op.HasPointwiseLeftKanExtension F]

instance cosk_reflective : IsIso (coskAdj (C := C) n).counit :=
  reflective' (SimplexCategory.Truncated.inclusion n).op

instance sk_coreflective : IsIso (skAdj (C := C) n).unit :=
  coreflective' (SimplexCategory.Truncated.inclusion n).op

/-- Since `Truncated.inclusion` is fully faithful, so is right Kan extension along it. -/
noncomputable def cosk.fullyFaithful :
    (Truncated.cosk (C := C) n).FullyFaithful := by
  apply Adjunction.fullyFaithfulROfIsIsoCounit (coskAdj n)

instance cosk.full : (Truncated.cosk (C := C) n).Full := FullyFaithful.full (cosk.fullyFaithful _)

instance cosk.faithful : (Truncated.cosk (C := C) n).Faithful :=
  FullyFaithful.faithful (cosk.fullyFaithful _)

noncomputable instance coskAdj.reflective : Reflective (Truncated.cosk (C := C) n) :=
  Reflective.mk (truncation _) (coskAdj _)

/-- Since `Truncated.inclusion` is fully faithful, so is left Kan extension along it. -/
noncomputable def sk.fullyFaithful : (Truncated.sk (C := C) n).FullyFaithful :=
  Adjunction.fullyFaithfulLOfIsIsoUnit (skAdj n)

instance sk.full : (Truncated.sk (C := C) n).Full := FullyFaithful.full (sk.fullyFaithful _)

instance sk.faithful : (Truncated.sk (C := C) n).Faithful :=
  FullyFaithful.faithful (sk.fullyFaithful _)

noncomputable instance skAdj.coreflective : Coreflective (Truncated.sk (C := C) n) :=
  Coreflective.mk (truncation _) (skAdj _)

end Truncated

end adjunctions

variable (C)

/-- The constant simplicial object is the constant functor. -/
abbrev const : C â¥¤ SimplicialObject C :=
  CategoryTheory.Functor.const _

/-- The category of augmented simplicial objects, defined as a comma category. -/
def Augmented :=
  Comma (ğŸ­ (SimplicialObject C)) (const C)

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  infer_instance

variable {C}

namespace Augmented

@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ hâ‚ hâ‚‚

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C â¥¤ SimplicialObject C :=
  Comma.fst _ _

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C â¥¤ C :=
  Comma.snd _ _

/-- The functor from augmented objects to arrows. -/
@[simps]
def toArrow : Augmented C â¥¤ Arrow C where
  obj X :=
    { left := drop.obj X _â¦‹0â¦Œ
      right := point.obj X
      hom := X.hom.app _ }
  map Î· :=
    { left := (drop.map Î·).app _
      right := point.map Î·
      w := by
        dsimp
        rw [â† NatTrans.comp_app]
        erw [Î·.w]
        rfl }

/-- The compatibility of a morphism with the augmentation, on 0-simplices -/
@[reassoc]
theorem wâ‚€ {X Y : Augmented C} (f : X âŸ¶ Y) :
    (Augmented.drop.map f).app (op â¦‹0â¦Œ) â‰« Y.hom.app (op â¦‹0â¦Œ) =
      X.hom.app (op â¦‹0â¦Œ) â‰« Augmented.point.map f := by
  convert congr_app f.w (op â¦‹0â¦Œ)

variable (C)

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C â¥¤ D) : Augmented C â¥¤ Augmented D where
  obj X :=
    { left := ((whiskering _ _).obj F).obj (drop.obj X)
      right := F.obj (point.obj X)
      hom := whiskerRight X.hom F â‰« (Functor.constComp _ _ _).hom }
  map Î· :=
    { left := whiskerRight Î·.left _
      right := F.map Î·.right
      w := by
        ext
        dsimp [whiskerRight]
        simp only [Category.comp_id, â† F.map_comp, â† NatTrans.comp_app]
        erw [Î·.w]
        rfl }

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C â¥¤ D) â¥¤ Augmented C â¥¤ Augmented D where
  obj := whiskeringObj _ _
  map Î· :=
    { app := fun A =>
        { left := whiskerLeft _ Î·
          right := Î·.app _
          w := by
            ext n
            dsimp
            rw [Category.comp_id, Category.comp_id, Î·.naturality] } }
  map_comp := fun _ _ => by ext <;> rfl

variable {C}

/-- The constant augmented simplicial object functor. -/
@[simps]
def const : C â¥¤ Augmented C where
  obj X :=
    { left := (SimplicialObject.const C).obj X
      right := X
      hom := ğŸ™ _ }
  map f :=
    { left := (SimplicialObject.const C).map f
      right := f }

end Augmented

/-- Augment a simplicial object with an object. -/
@[simps]
def augment (X : SimplicialObject C) (Xâ‚€ : C) (f : X _â¦‹0â¦Œ âŸ¶ Xâ‚€)
    (w : âˆ€ (i : SimplexCategory) (gâ‚ gâ‚‚ : â¦‹0â¦Œ âŸ¶ i),
      X.map gâ‚.op â‰« f = X.map gâ‚‚.op â‰« f) :
    SimplicialObject.Augmented C where
  left := X
  right := Xâ‚€
  hom :=
    { app := fun _ => X.map (SimplexCategory.const _ _ 0).op â‰« f
      naturality := by
        intro i j g
        dsimp
        rw [â† g.op_unop]
        simpa only [â† X.map_comp, â† Category.assoc, Category.comp_id, â† op_comp] using w _ _ _ }

-- Not `@[simp]` since `simp` can prove this.
theorem augment_hom_zero (X : SimplicialObject C) (Xâ‚€ : C) (f : X _â¦‹0â¦Œ âŸ¶ Xâ‚€) (w) :
    (X.augment Xâ‚€ f w).hom.app (op â¦‹0â¦Œ) = f := by simp

end SimplicialObject

/-- Cosimplicial objects. -/
def CosimplicialObject :=
  SimplexCategory â¥¤ C

namespace CosimplicialObject

@[simps!]
instance : Category (CosimplicialObject C) := by
  dsimp only [CosimplicialObject]
  infer_instance

/-- `X ^â¦‹nâ¦Œ` denotes the `n`th-term of the cosimplicial object X -/
scoped[Simplicial]
  notation3:1000 X " ^â¦‹" n "â¦Œ" =>
    (X : CategoryTheory.CosimplicialObject _).obj (SimplexCategory.mk n)

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  infer_instance

instance [HasLimits C] : HasLimits (CosimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  infer_instance

instance [HasColimits C] : HasColimits (CosimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

variable {C}

@[ext]
lemma hom_ext {X Y : CosimplicialObject C} (f g : X âŸ¶ Y)
    (h : âˆ€ (n : SimplexCategory), f.app n = g.app n) : f = g :=
  NatTrans.ext (by ext; apply h)

variable (X : CosimplicialObject C)

open Simplicial

/-- Coface maps for a cosimplicial object. -/
def Î´ {n} (i : Fin (n + 2)) : X ^â¦‹nâ¦Œ âŸ¶ X ^â¦‹n + 1â¦Œ :=
  X.map (SimplexCategory.Î´ i)

/-- Codegeneracy maps for a cosimplicial object. -/
def Ïƒ {n} (i : Fin (n + 1)) : X ^â¦‹n + 1â¦Œ âŸ¶ X ^â¦‹nâ¦Œ :=
  X.map (SimplexCategory.Ïƒ i)

/-- Isomorphisms from identities in â„•. -/
def eqToIso {n m : â„•} (h : n = m) : X ^â¦‹nâ¦Œ â‰… X ^â¦‹mâ¦Œ :=
  X.mapIso (CategoryTheory.eqToIso (by rw [h]))

@[simp]
theorem eqToIso_refl {n : â„•} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  simp [eqToIso]

/-- The generic case of the first cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    X.Î´ i â‰« X.Î´ j.succ = X.Î´ j â‰« X.Î´ (Fin.castSucc i) := by
  dsimp [Î´]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´ H]

@[reassoc]
theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Î´ i â‰« X.Î´ j =
      X.Î´ (j.pred fun (hj : j = 0) => by simp only [hj, Fin.not_lt_zero] at H) â‰«
        X.Î´ (Fin.castSucc i) := by
  dsimp [Î´]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´' H]

@[reassoc]
theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) â‰« X.Î´ j.succ =
      X.Î´ j â‰« X.Î´ i := by
  dsimp [Î´]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´'' H]

/-- The special case of the first cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} :
    X.Î´ i â‰« X.Î´ (Fin.castSucc i) = X.Î´ i â‰« X.Î´ i.succ := by
  dsimp [Î´]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´_self]

@[reassoc]
theorem Î´_comp_Î´_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = Fin.castSucc i) :
    X.Î´ i â‰« X.Î´ j = X.Î´ i â‰« X.Î´ i.succ := by
  subst H
  rw [Î´_comp_Î´_self]

/-- The second cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ (Fin.castSucc i) â‰« X.Ïƒ j.succ = X.Ïƒ j â‰« X.Î´ i := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_le H]

/-- The first part of the third cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} : X.Î´ (Fin.castSucc i) â‰« X.Ïƒ i = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_self, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Î´ j â‰« X.Ïƒ i = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_self]

/-- The second part of the third cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : X.Î´ i.succ â‰« X.Ïƒ i = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_succ, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Î´ j â‰« X.Ïƒ i = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_succ]

/-- The fourth cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Î´ i.succ â‰« X.Ïƒ (Fin.castSucc j) = X.Ïƒ j â‰« X.Î´ i := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt H]

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Î´ i â‰« X.Ïƒ j =
      X.Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) â‰«
        X.Î´ (i.pred <|
          fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt' H]

/-- The fifth cosimplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    X.Ïƒ (Fin.castSucc i) â‰« X.Ïƒ j = X.Ïƒ j.succ â‰« X.Ïƒ i := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Ïƒ_comp_Ïƒ H]

@[reassoc (attr := simp)]
theorem Î´_naturality {X' X : CosimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 2)) :
    X.Î´ i â‰« f.app â¦‹n + 1â¦Œ = f.app â¦‹nâ¦Œ â‰« X'.Î´ i :=
  f.naturality _

@[reassoc (attr := simp)]
theorem Ïƒ_naturality {X' X : CosimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« f.app â¦‹nâ¦Œ = f.app â¦‹n + 1â¦Œ â‰« X'.Ïƒ i :=
  f.naturality _

variable (C)

/-- Functor composition induces a functor on cosimplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C â¥¤ D) â¥¤ CosimplicialObject C â¥¤ CosimplicialObject D :=
  whiskeringRight _ _ _

/-- Truncated cosimplicial objects. -/
def Truncated (n : â„•) :=
  SimplexCategory.Truncated n â¥¤ C

instance {n : â„•} : Category (Truncated C n) := by
  dsimp [Truncated]
  infer_instance

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasLimits C] : HasLimits (CosimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasColimits C] : HasColimits (CosimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

variable (C) in
/-- Functor composition induces a functor on truncated cosimplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C â¥¤ D) â¥¤ Truncated C n â¥¤ Truncated D n :=
  whiskeringRight _ _ _

open Mathlib.Tactic (subscriptTerm) in
/-- For `X : Truncated C n` and `m â‰¤ n`, `X ^â¦‹mâ¦Œâ‚™` is the `m`-th term of X. The
proof `p : m â‰¤ n` can also be provided using the syntax `X ^â¦‹m, pâ¦Œâ‚™`. -/
scoped syntax:max (name := mkNotation)
  term " ^â¦‹" term ("," term)? "â¦Œ" noWs subscriptTerm : term

open scoped SimplexCategory.Truncated in
scoped macro_rules
  | `($X:term ^â¦‹$m:termâ¦Œ$n:subscript) =>
    `(($X : CategoryTheory.CosimplicialObject.Truncated _ $n).obj
      âŸ¨SimplexCategory.mk $m, by first | get_elem_tactic |
      fail "Failed to prove truncation property. Try writing `X ^â¦‹m, by ...â¦Œâ‚™`."âŸ©)
  | `($X:term ^â¦‹$m:term, $p:termâ¦Œ$n:subscript) =>
    `(($X : CategoryTheory.CosimplicialObject.Truncated _ $n).obj
      âŸ¨SimplexCategory.mk $m, $pâŸ©)

variable (C) in
/-- Further truncation of truncated cosimplicial objects. -/
def trunc (n m : â„•) (h : m â‰¤ n := by omega) : Truncated C n â¥¤ Truncated C m :=
  (whiskeringLeft _ _ _).obj <| SimplexCategory.Truncated.incl m n

end Truncated

section Truncation

/-- The truncation functor from cosimplicial objects to truncated cosimplicial objects. -/
def truncation (n : â„•) : CosimplicialObject C â¥¤ CosimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj (SimplexCategory.Truncated.inclusion n)

/-- For all `m â‰¤ n`, `truncation m` factors through `Truncated n`. -/
def truncationCompTrunc {n m : â„•} (h : m â‰¤ n) :
    truncation n â‹™ Truncated.trunc C n m â‰… truncation m :=
  Iso.refl _

end Truncation

variable (C)

/-- The constant cosimplicial object. -/
abbrev const : C â¥¤ CosimplicialObject C :=
  CategoryTheory.Functor.const _

/-- Augmented cosimplicial objects. -/
def Augmented :=
  Comma (const C) (ğŸ­ (CosimplicialObject C))

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  infer_instance

variable {C}

namespace Augmented

@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ hâ‚ hâ‚‚

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C â¥¤ CosimplicialObject C :=
  Comma.snd _ _

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C â¥¤ C :=
  Comma.fst _ _

/-- The functor from augmented objects to arrows. -/
@[simps!]
def toArrow : Augmented C â¥¤ Arrow C where
  obj X :=
    { left := point.obj X
      right := (drop.obj X) ^â¦‹0â¦Œ
      hom := X.hom.app _ }
  map Î· :=
    { left := point.map Î·
      right := (drop.map Î·).app _
      w := by
        dsimp
        rw [â† NatTrans.comp_app]
        erw [â† Î·.w]
        rfl }

variable (C)

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C â¥¤ D) : Augmented C â¥¤ Augmented D where
  obj X :=
    { left := F.obj (point.obj X)
      right := ((whiskering _ _).obj F).obj (drop.obj X)
      hom := (Functor.constComp _ _ _).inv â‰« whiskerRight X.hom F }
  map Î· :=
    { left := F.map Î·.left
      right := whiskerRight Î·.right _
      w := by
        ext
        dsimp
        rw [Category.id_comp, Category.id_comp, â† F.map_comp, â† F.map_comp, â† NatTrans.comp_app]
        erw [â† Î·.w]
        rfl }

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C â¥¤ D) â¥¤ Augmented C â¥¤ Augmented D where
  obj := whiskeringObj _ _
  map Î· :=
    { app := fun A =>
        { left := Î·.app _
          right := whiskerLeft _ Î·
          w := by
            ext n
            dsimp
            rw [Category.id_comp, Category.id_comp, Î·.naturality] }
      naturality := fun _ _ f => by ext <;> simp }

variable {C}

/-- The constant augmented cosimplicial object functor. -/
@[simps]
def const : C â¥¤ Augmented C where
  obj X :=
    { left := X
      right := (CosimplicialObject.const C).obj X
      hom := ğŸ™ _ }
  map f :=
    { left := f
      right := (CosimplicialObject.const C).map f }

end Augmented

open Simplicial

/-- Augment a cosimplicial object with an object. -/
@[simps]
def augment (X : CosimplicialObject C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj â¦‹0â¦Œ)
    (w : âˆ€ (i : SimplexCategory) (gâ‚ gâ‚‚ : â¦‹0â¦Œ âŸ¶ i),
      f â‰« X.map gâ‚ = f â‰« X.map gâ‚‚) : CosimplicialObject.Augmented C where
  left := Xâ‚€
  right := X
  hom :=
    { app := fun _ => f â‰« X.map (SimplexCategory.const _ _ 0)
      naturality := by
        intro i j g
        dsimp
        rw [Category.id_comp, Category.assoc, â† X.map_comp, w] }

-- Not `@[simp]` since `simp` can prove this.
theorem augment_hom_zero (X : CosimplicialObject C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj â¦‹0â¦Œ) (w) :
    (X.augment Xâ‚€ f w).hom.app â¦‹0â¦Œ = f := by simp

end CosimplicialObject

/-- The anti-equivalence between simplicial objects and cosimplicial objects. -/
@[simps!]
def simplicialCosimplicialEquiv : (SimplicialObject C)áµ’áµ– â‰Œ CosimplicialObject Cáµ’áµ– :=
  Functor.leftOpRightOpEquiv _ _

/-- The anti-equivalence between cosimplicial objects and simplicial objects. -/
@[simps!]
def cosimplicialSimplicialEquiv : (CosimplicialObject C)áµ’áµ– â‰Œ SimplicialObject Cáµ’áµ– :=
  Functor.opUnopEquiv _ _

variable {C}

/-- Construct an augmented cosimplicial object in the opposite
category from an augmented simplicial object. -/
@[simps!]
def SimplicialObject.Augmented.rightOp (X : SimplicialObject.Augmented C) :
    CosimplicialObject.Augmented Cáµ’áµ– where
  left := Opposite.op X.right
  right := X.left.rightOp
  hom := NatTrans.rightOp X.hom

/-- Construct an augmented simplicial object from an augmented cosimplicial
object in the opposite category. -/
@[simps!]
def CosimplicialObject.Augmented.leftOp (X : CosimplicialObject.Augmented Cáµ’áµ–) :
    SimplicialObject.Augmented C where
  left := X.right.leftOp
  right := X.left.unop
  hom := NatTrans.leftOp X.hom

/-- Converting an augmented simplicial object to an augmented cosimplicial
object and back is isomorphic to the given object. -/
@[simps!]
def SimplicialObject.Augmented.rightOpLeftOpIso (X : SimplicialObject.Augmented C) :
    X.rightOp.leftOp â‰… X :=
  Comma.isoMk X.left.rightOpLeftOpIso (CategoryTheory.eqToIso <| by simp)

/-- Converting an augmented cosimplicial object to an augmented simplicial
object and back is isomorphic to the given object. -/
@[simps!]
def CosimplicialObject.Augmented.leftOpRightOpIso (X : CosimplicialObject.Augmented Cáµ’áµ–) :
    X.leftOp.rightOp â‰… X :=
  Comma.isoMk (CategoryTheory.eqToIso <| by simp) X.right.leftOpRightOpIso

variable (C)

/-- A functorial version of `SimplicialObject.Augmented.rightOp`. -/
@[simps]
def simplicialToCosimplicialAugmented :
    (SimplicialObject.Augmented C)áµ’áµ– â¥¤ CosimplicialObject.Augmented Cáµ’áµ– where
  obj X := X.unop.rightOp
  map f :=
    { left := f.unop.right.op
      right := NatTrans.rightOp f.unop.left
      w := by
        ext x
        dsimp
        simp_rw [â† op_comp]
        congr 1
        exact (congr_app f.unop.w (op x)).symm }

/-- A functorial version of `Cosimplicial_object.Augmented.leftOp`. -/
@[simps]
def cosimplicialToSimplicialAugmented :
    CosimplicialObject.Augmented Cáµ’áµ– â¥¤ (SimplicialObject.Augmented C)áµ’áµ– where
  obj X := Opposite.op X.leftOp
  map f :=
    Quiver.Hom.op <|
      { left := NatTrans.leftOp f.right
        right := f.left.unop
        w := by
          ext x
          dsimp
          simp_rw [â† unop_comp]
          congr 1
          exact (congr_app f.w (unop x)).symm }

/-- The contravariant categorical equivalence between augmented simplicial
objects and augmented cosimplicial objects in the opposite category. -/
@[simps! functor inverse]
def simplicialCosimplicialAugmentedEquiv :
    (SimplicialObject.Augmented C)áµ’áµ– â‰Œ CosimplicialObject.Augmented Cáµ’áµ– where
  functor := simplicialToCosimplicialAugmented _
  inverse := cosimplicialToSimplicialAugmented _
  unitIso := NatIso.ofComponents (fun X => X.unop.rightOpLeftOpIso.op) fun f => by
      dsimp
      rw [â† f.op_unop]
      simp_rw [â† op_comp]
      congr 1
      cat_disch
  counitIso := NatIso.ofComponents fun X => X.leftOpRightOpIso

end CategoryTheory
