/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Basic
import Mathlib.CategoryTheory.Adjunction.Reflective
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Opposites
import Mathlib.Util.Superscript

/-!
# Simplicial objects in a category.

A simplicial object in a category `C` is a `C`-valued presheaf on `SimplexCategory`.
(Similarly, a cosimplicial object is a functor `SimplexCategory ‚•§ C`.)

## Notation

The following notations can be enabled via `open Simplicial`.

- `X _‚¶ãn‚¶å` denotes the `n`-th term of a simplicial object `X`, where `n : ‚Ñï`.
- `X ^‚¶ãn‚¶å` denotes the `n`-th term of a cosimplicial object `X`, where `n : ‚Ñï`.

The following notations can be enabled via the respective `Truncated` namespace.

- `X _‚¶ãm‚¶å‚Çô` denotes the `m`-th term of an `n`-truncated simplicial object `X`.
  Access this notation with `open CategoryTheory.SimplicialObject.Truncated`.
- `X ^‚¶ãm‚¶å‚Çô` denotes the `m`-th term of an `n`-truncated cosimplicial object `X`.
  Access this notation with `open CategoryTheory.CosimplicialObject.Truncated`.
-/

open Opposite

open CategoryTheory

open CategoryTheory.Limits CategoryTheory.Functor

universe v u v' u'

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- The category of simplicial objects valued in a category `C`.
This is the category of contravariant functors from `SimplexCategory` to `C`. -/
def SimplicialObject :=
  SimplexCategory·µí·µñ ‚•§ C

@[simps!]
instance : Category (SimplicialObject C) := by
  dsimp only [SimplicialObject]
  infer_instance

namespace SimplicialObject

set_option quotPrecheck false in
/-- `X _‚¶ãn‚¶å` denotes the `n`th-term of the simplicial object X -/
scoped[Simplicial]
  notation3:1000 X " _‚¶ã" n "‚¶å" =>
      (X : CategoryTheory.SimplicialObject _).obj (Opposite.op (SimplexCategory.mk n))

open Simplicial

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  infer_instance

instance [HasLimits C] : HasLimits (SimplicialObject C) :=
  ‚ü®inferInstance‚ü©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  infer_instance

instance [HasColimits C] : HasColimits (SimplicialObject C) :=
  ‚ü®inferInstance‚ü©

variable {C}

@[ext]
lemma hom_ext {X Y : SimplicialObject C} (f g : X ‚ü∂ Y)
    (h : ‚àÄ (n : SimplexCategory·µí·µñ), f.app n = g.app n) : f = g :=
  NatTrans.ext (by ext; apply h)

variable (X : SimplicialObject C)

/-- Face maps for a simplicial object. -/
def Œ¥ {n} (i : Fin (n + 2)) : X _‚¶ãn + 1‚¶å ‚ü∂ X _‚¶ãn‚¶å :=
  X.map (SimplexCategory.Œ¥ i).op

/-- Degeneracy maps for a simplicial object. -/
def œÉ {n} (i : Fin (n + 1)) : X _‚¶ãn‚¶å ‚ü∂ X _‚¶ãn + 1‚¶å :=
  X.map (SimplexCategory.œÉ i).op

/-- The diagonal of a simplex is the long edge of the simplex. -/
def diagonal {n : ‚Ñï} : X _‚¶ãn‚¶å ‚ü∂ X _‚¶ã1‚¶å := X.map ((SimplexCategory.diag n).op)

/-- Isomorphisms from identities in ‚Ñï. -/
def eqToIso {n m : ‚Ñï} (h : n = m) : X _‚¶ãn‚¶å ‚âÖ X _‚¶ãm‚¶å :=
  X.mapIso (CategoryTheory.eqToIso (by congr))

@[simp]
theorem eqToIso_refl {n : ‚Ñï} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  ext
  simp [eqToIso]

/-- The generic case of the first simplicial identity -/
@[reassoc]
theorem Œ¥_comp_Œ¥ {n} {i j : Fin (n + 2)} (H : i ‚â§ j) :
    X.Œ¥ j.succ ‚â´ X.Œ¥ i = X.Œ¥ (Fin.castSucc i) ‚â´ X.Œ¥ j := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_Œ¥ H]

@[reassoc]
theorem Œ¥_comp_Œ¥' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Œ¥ j ‚â´ X.Œ¥ i =
      X.Œ¥ (Fin.castSucc i) ‚â´
        X.Œ¥ (j.pred fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_Œ¥' H]
@[reassoc]
theorem Œ¥_comp_Œ¥'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i ‚â§ Fin.castSucc j) :
    X.Œ¥ j.succ ‚â´ X.Œ¥ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) =
      X.Œ¥ i ‚â´ X.Œ¥ j := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_Œ¥'' H]

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Œ¥_comp_Œ¥_self {n} {i : Fin (n + 2)} :
    X.Œ¥ (Fin.castSucc i) ‚â´ X.Œ¥ i = X.Œ¥ i.succ ‚â´ X.Œ¥ i := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_Œ¥_self]

@[reassoc]
theorem Œ¥_comp_Œ¥_self' {n} {j : Fin (n + 3)} {i : Fin (n + 2)} (H : j = Fin.castSucc i) :
    X.Œ¥ j ‚â´ X.Œ¥ i = X.Œ¥ i.succ ‚â´ X.Œ¥ i := by
  subst H
  rw [Œ¥_comp_Œ¥_self]

/-- The second simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i ‚â§ Fin.castSucc j) :
    X.œÉ j.succ ‚â´ X.Œ¥ (Fin.castSucc i) = X.Œ¥ i ‚â´ X.œÉ j := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_œÉ_of_le H]

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_self {n} {i : Fin (n + 1)} : X.œÉ i ‚â´ X.Œ¥ (Fin.castSucc i) = ùüô _ := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_œÉ_self, op_id, X.map_id]

@[reassoc]
theorem Œ¥_comp_œÉ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.œÉ i ‚â´ X.Œ¥ j = ùüô _ := by
  subst H
  rw [Œ¥_comp_œÉ_self]

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_succ {n} {i : Fin (n + 1)} : X.œÉ i ‚â´ X.Œ¥ i.succ = ùüô _ := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_œÉ_succ, op_id, X.map_id]

@[reassoc]
theorem Œ¥_comp_œÉ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.œÉ i ‚â´ X.Œ¥ j = ùüô _ := by
  subst H
  rw [Œ¥_comp_œÉ_succ]

/-- The fourth simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.œÉ (Fin.castSucc j) ‚â´ X.Œ¥ i.succ = X.Œ¥ i ‚â´ X.œÉ j := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_œÉ_of_gt H]

@[reassoc]
theorem Œ¥_comp_œÉ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.œÉ j ‚â´ X.Œ¥ i =
      X.Œ¥ (i.pred fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) ‚â´
        X.œÉ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_œÉ_of_gt' H]

/-- The fifth simplicial identity -/
@[reassoc]
theorem œÉ_comp_œÉ {n} {i j : Fin (n + 1)} (H : i ‚â§ j) :
    X.œÉ j ‚â´ X.œÉ (Fin.castSucc i) = X.œÉ i ‚â´ X.œÉ j.succ := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.œÉ_comp_œÉ H]

open Simplicial

@[reassoc (attr := simp)]
theorem Œ¥_naturality {X' X : SimplicialObject C} (f : X ‚ü∂ X') {n : ‚Ñï} (i : Fin (n + 2)) :
    X.Œ¥ i ‚â´ f.app (op ‚¶ãn‚¶å) = f.app (op ‚¶ãn + 1‚¶å) ‚â´ X'.Œ¥ i :=
  f.naturality _

@[reassoc (attr := simp)]
theorem œÉ_naturality {X' X : SimplicialObject C} (f : X ‚ü∂ X') {n : ‚Ñï} (i : Fin (n + 1)) :
    X.œÉ i ‚â´ f.app (op ‚¶ãn + 1‚¶å) = f.app (op ‚¶ãn‚¶å) ‚â´ X'.œÉ i :=
  f.naturality _

variable (C)

/-- Functor composition induces a functor on simplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C ‚•§ D) ‚•§ SimplicialObject C ‚•§ SimplicialObject D :=
  whiskeringRight _ _ _

/-- Truncated simplicial objects. -/
def Truncated (n : ‚Ñï) :=
  (SimplexCategory.Truncated n)·µí·µñ ‚•§ C

instance {n : ‚Ñï} : Category (Truncated C n) := by
  dsimp [Truncated]
  infer_instance

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasLimits C] : HasLimits (SimplicialObject.Truncated C n) :=
  ‚ü®inferInstance‚ü©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasColimits C] : HasColimits (SimplicialObject.Truncated C n) :=
  ‚ü®inferInstance‚ü©

variable (C) in
/-- Functor composition induces a functor on truncated simplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C ‚•§ D) ‚•§ Truncated C n ‚•§ Truncated D n :=
  whiskeringRight _ _ _

section Meta
/-! We provide a macro and a delaborator for the truncated simplicial object
notation `X _‚¶ãm‚¶å‚Çô`. An analogous macro/delaborator pair for the truncated
cosimplicial object notation `X ^‚¶ãm‚¶å‚Çô` is defined later in this file. -/

open SimplexCategory.Truncated Lean PrettyPrinter.Delaborator SubExpr
open Mathlib.Tactic (subscriptTerm delabSubscript)

/-- For `X : Truncated C n` and `m ‚â§ n`, `X _‚¶ãm‚¶å‚Çô` is the `m`-th term of X. The
proof `p : m ‚â§ n` can also be provided using the syntax `X _‚¶ãm, p‚¶å‚Çô`. Access
this notation with `open CategoryTheory.SimplicialObject.Truncated`. -/
scoped syntax:max (name := mkNotation)
  term " _‚¶ã" term ("," term)? "‚¶å" noWs subscriptTerm : term
scoped macro_rules
  | `($X:term _‚¶ã$m:term‚¶å$n:subscript) =>
    `(($X : CategoryTheory.SimplicialObject.Truncated _ $n).obj
      (Opposite.op ‚ü®SimplexCategory.mk $m, by first | trunc |
      fail "Failed to prove truncation property. Try writing `X _‚¶ãm, by ...‚¶å‚Çô`."‚ü©))
  | `($X:term _‚¶ã$m:term, $p:term‚¶å$n:subscript) =>
    `(($X : CategoryTheory.SimplicialObject.Truncated _ $n).obj
      (Opposite.op ‚ü®SimplexCategory.mk $m, $p‚ü©))

/-- Delaborator for the notation `X _‚¶ãm‚¶å‚Çô`. -/
@[app_delab Prefunctor.obj]
def delabMkNotation : Delab :=
  whenNotPPOption getPPExplicit <| whenPPOption getPPNotation do
    let_expr Prefunctor.obj src _ _ _ f x := ‚Üê getExpr | failure
    -- check that f is a contravariant functor out of the truncated simplex category
    guard <| f.isAppOfArity ``Functor.toPrefunctor 5
    let_expr Opposite.op _ x := x | failure
    let_expr FullSubcategory.mk _ _ simplex _ := x | failure
    guard <| simplex.isAppOfArity ``SimplexCategory.mk 1
    let_expr Opposite src := src | failure
    guard <| src.isAppOfArity ``SimplexCategory.Truncated 1
    -- if `pp.proofs` is set to `true`, include the proof `p : m ‚â§ n`
    let n ‚Üê withNaryArg 0 <| withAppArg <| withAppArg <| delabSubscript
    let m ‚Üê withAppArg <| withAppArg <| withNaryArg 2 <| withAppArg delab
    let f ‚Üê withNaryArg 4 <| withAppArg delab
    if (‚Üê getPPOption getPPProofs) then
      let p ‚Üê withAppArg <| withAppArg <| withAppArg delab
      `($f _‚¶ã$m, $p‚¶å$n)
    else `($f _‚¶ã$m‚¶å$n)

end Meta

variable (C) in
/-- Further truncation of truncated simplicial objects. -/
@[simps!]
def trunc (n m : ‚Ñï) (h : m ‚â§ n := by omega) : Truncated C n ‚•§ Truncated C m :=
  (whiskeringLeft _ _ _).obj (SimplexCategory.Truncated.incl m n).op

end Truncated

section Truncation

/-- The truncation functor from simplicial objects to truncated simplicial objects. -/
def truncation (n : ‚Ñï) : SimplicialObject C ‚•§ SimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj (SimplexCategory.Truncated.inclusion n).op

/-- For all `m ‚â§ n`, `truncation m` factors through `Truncated n`. -/
def truncationCompTrunc {n m : ‚Ñï} (h : m ‚â§ n) :
    truncation n ‚ãô Truncated.trunc C n m ‚âÖ truncation m :=
  Iso.refl _

end Truncation


noncomputable section

/-- The n-skeleton as a functor `SimplicialObject.Truncated C n ‚•§ SimplicialObject C`. -/
protected abbrev Truncated.sk (n : ‚Ñï) [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasLeftKanExtension F] :
    SimplicialObject.Truncated C n ‚•§ SimplicialObject C :=
  lan (SimplexCategory.Truncated.inclusion n).op

/-- The n-coskeleton as a functor `SimplicialObject.Truncated C n ‚•§ SimplicialObject C`. -/
protected abbrev Truncated.cosk (n : ‚Ñï) [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasRightKanExtension F] :
    SimplicialObject.Truncated C n ‚•§ SimplicialObject C :=
  ran (SimplexCategory.Truncated.inclusion n).op

/-- The n-skeleton as an endofunctor on `SimplicialObject C`. -/
abbrev sk (n : ‚Ñï) [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasLeftKanExtension F] :
    SimplicialObject C ‚•§ SimplicialObject C := truncation n ‚ãô Truncated.sk n

/-- The n-coskeleton as an endofunctor on `SimplicialObject C`. -/
abbrev cosk (n : ‚Ñï) [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasRightKanExtension F] :
    SimplicialObject C ‚•§ SimplicialObject C := truncation n ‚ãô Truncated.cosk n

end

section adjunctions
/- When the left and right Kan extensions exist, `Truncated.sk n` and `Truncated.cosk n`
respectively define left and right adjoints to `truncation n`. -/


variable (n : ‚Ñï)
variable [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasRightKanExtension F]
variable [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasLeftKanExtension F]

/-- The adjunction between the n-skeleton and n-truncation. -/
noncomputable def skAdj : Truncated.sk (C := C) n ‚ä£ truncation n :=
  lanAdjunction _ _

/-- The adjunction between n-truncation and the n-coskeleton. -/
noncomputable def coskAdj : truncation (C := C) n ‚ä£ Truncated.cosk n :=
  ranAdjunction _ _

instance : ((sk n).obj X).IsLeftKanExtension ((skAdj n).unit.app _) := by
  dsimp [sk, skAdj]
  rw [lanAdjunction_unit]
  infer_instance

instance : ((cosk n).obj X).IsRightKanExtension ((coskAdj n).counit.app _) := by
  dsimp [cosk, coskAdj]
  rw [ranAdjunction_counit]
  infer_instance

namespace Truncated
/- When the left and right Kan extensions exist and are pointwise Kan extensions,
`skAdj n` and `coskAdj n` are respectively coreflective and reflective. -/

variable [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasPointwiseRightKanExtension F]
variable [‚àÄ (F : (SimplexCategory.Truncated n)·µí·µñ ‚•§ C),
    (SimplexCategory.Truncated.inclusion n).op.HasPointwiseLeftKanExtension F]

instance cosk_reflective : IsIso (coskAdj (C := C) n).counit :=
  reflective' (SimplexCategory.Truncated.inclusion n).op

instance sk_coreflective : IsIso (skAdj (C := C) n).unit :=
  coreflective' (SimplexCategory.Truncated.inclusion n).op

/-- Since `Truncated.inclusion` is fully faithful, so is right Kan extension along it. -/
noncomputable def cosk.fullyFaithful :
    (Truncated.cosk (C := C) n).FullyFaithful := by
  apply Adjunction.fullyFaithfulROfIsIsoCounit (coskAdj n)

instance cosk.full : (Truncated.cosk (C := C) n).Full := FullyFaithful.full (cosk.fullyFaithful _)

instance cosk.faithful : (Truncated.cosk (C := C) n).Faithful :=
  FullyFaithful.faithful (cosk.fullyFaithful _)

noncomputable instance coskAdj.reflective : Reflective (Truncated.cosk (C := C) n) :=
  Reflective.mk (truncation _) (coskAdj _)

/-- Since `Truncated.inclusion` is fully faithful, so is left Kan extension along it. -/
noncomputable def sk.fullyFaithful : (Truncated.sk (C := C) n).FullyFaithful :=
  Adjunction.fullyFaithfulLOfIsIsoUnit (skAdj n)

instance sk.full : (Truncated.sk (C := C) n).Full := FullyFaithful.full (sk.fullyFaithful _)

instance sk.faithful : (Truncated.sk (C := C) n).Faithful :=
  FullyFaithful.faithful (sk.fullyFaithful _)

noncomputable instance skAdj.coreflective : Coreflective (Truncated.sk (C := C) n) :=
  Coreflective.mk (truncation _) (skAdj _)

end Truncated

end adjunctions

variable (C)

/-- The constant simplicial object is the constant functor. -/
abbrev const : C ‚•§ SimplicialObject C :=
  CategoryTheory.Functor.const _

/-- The category of augmented simplicial objects, defined as a comma category. -/
def Augmented :=
  Comma (ùü≠ (SimplicialObject C)) (const C)

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  infer_instance

variable {C}

namespace Augmented

@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X ‚ü∂ Y) (h‚ÇÅ : f.left = g.left) (h‚ÇÇ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ h‚ÇÅ h‚ÇÇ

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C ‚•§ SimplicialObject C :=
  Comma.fst _ _

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C ‚•§ C :=
  Comma.snd _ _

/-- The functor from augmented objects to arrows. -/
@[simps]
def toArrow : Augmented C ‚•§ Arrow C where
  obj X :=
    { left := drop.obj X _‚¶ã0‚¶å
      right := point.obj X
      hom := X.hom.app _ }
  map Œ∑ :=
    { left := (drop.map Œ∑).app _
      right := point.map Œ∑
      w := by
        dsimp
        rw [‚Üê NatTrans.comp_app]
        erw [Œ∑.w]
        rfl }

/-- The compatibility of a morphism with the augmentation, on 0-simplices -/
@[reassoc]
theorem w‚ÇÄ {X Y : Augmented C} (f : X ‚ü∂ Y) :
    (Augmented.drop.map f).app (op (SimplexCategory.mk 0)) ‚â´ Y.hom.app (op (SimplexCategory.mk 0)) =
      X.hom.app (op (SimplexCategory.mk 0)) ‚â´ Augmented.point.map f := by
  convert congr_app f.w (op (SimplexCategory.mk 0))

variable (C)

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C ‚•§ D) : Augmented C ‚•§ Augmented D where
  obj X :=
    { left := ((whiskering _ _).obj F).obj (drop.obj X)
      right := F.obj (point.obj X)
      hom := whiskerRight X.hom F ‚â´ (Functor.constComp _ _ _).hom }
  map Œ∑ :=
    { left := whiskerRight Œ∑.left _
      right := F.map Œ∑.right
      w := by
        ext
        dsimp [whiskerRight]
        simp only [Category.comp_id, ‚Üê F.map_comp, ‚Üê NatTrans.comp_app]
        erw [Œ∑.w]
        rfl }

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C ‚•§ D) ‚•§ Augmented C ‚•§ Augmented D where
  obj := whiskeringObj _ _
  map Œ∑ :=
    { app := fun A =>
        { left := whiskerLeft _ Œ∑
          right := Œ∑.app _
          w := by
            ext n
            dsimp
            rw [Category.comp_id, Category.comp_id, Œ∑.naturality] } }
  map_comp := fun _ _ => by ext <;> rfl

variable {C}

/-- The constant augmented simplicial object functor. -/
@[simps]
def const : C ‚•§ Augmented C where
  obj X :=
    { left := (SimplicialObject.const C).obj X
      right := X
      hom := ùüô _ }
  map f :=
    { left := (SimplicialObject.const C).map f
      right := f }

end Augmented

/-- Augment a simplicial object with an object. -/
@[simps]
def augment (X : SimplicialObject C) (X‚ÇÄ : C) (f : X _‚¶ã0‚¶å ‚ü∂ X‚ÇÄ)
    (w : ‚àÄ (i : SimplexCategory) (g‚ÇÅ g‚ÇÇ : ‚¶ã0‚¶å ‚ü∂ i),
      X.map g‚ÇÅ.op ‚â´ f = X.map g‚ÇÇ.op ‚â´ f) :
    SimplicialObject.Augmented C where
  left := X
  right := X‚ÇÄ
  hom :=
    { app := fun _ => X.map (SimplexCategory.const _ _ 0).op ‚â´ f
      naturality := by
        intro i j g
        dsimp
        rw [‚Üê g.op_unop]
        simpa only [‚Üê X.map_comp, ‚Üê Category.assoc, Category.comp_id, ‚Üê op_comp] using w _ _ _ }

-- Not `@[simp]` since `simp` can prove this.
theorem augment_hom_zero (X : SimplicialObject C) (X‚ÇÄ : C) (f : X _‚¶ã0‚¶å ‚ü∂ X‚ÇÄ) (w) :
    (X.augment X‚ÇÄ f w).hom.app (op ‚¶ã0‚¶å) = f := by simp

end SimplicialObject

/-- Cosimplicial objects. -/
def CosimplicialObject :=
  SimplexCategory ‚•§ C

@[simps!]
instance : Category (CosimplicialObject C) := by
  dsimp only [CosimplicialObject]
  infer_instance

namespace CosimplicialObject

/-- `X ^‚¶ãn‚¶å` denotes the `n`th-term of the cosimplicial object X -/
scoped[Simplicial]
  notation3:1000 X " ^‚¶ã" n "‚¶å" =>
    (X : CategoryTheory.CosimplicialObject _).obj (SimplexCategory.mk n)

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  infer_instance

instance [HasLimits C] : HasLimits (CosimplicialObject C) :=
  ‚ü®inferInstance‚ü©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  infer_instance

instance [HasColimits C] : HasColimits (CosimplicialObject C) :=
  ‚ü®inferInstance‚ü©

variable {C}

@[ext]
lemma hom_ext {X Y : CosimplicialObject C} (f g : X ‚ü∂ Y)
    (h : ‚àÄ (n : SimplexCategory), f.app n = g.app n) : f = g :=
  NatTrans.ext (by ext; apply h)

variable (X : CosimplicialObject C)

open Simplicial

/-- Coface maps for a cosimplicial object. -/
def Œ¥ {n} (i : Fin (n + 2)) : X ^‚¶ãn‚¶å ‚ü∂ X ^‚¶ãn + 1‚¶å :=
  X.map (SimplexCategory.Œ¥ i)

/-- Codegeneracy maps for a cosimplicial object. -/
def œÉ {n} (i : Fin (n + 1)) : X ^‚¶ãn + 1‚¶å ‚ü∂ X ^‚¶ãn‚¶å :=
  X.map (SimplexCategory.œÉ i)

/-- Isomorphisms from identities in ‚Ñï. -/
def eqToIso {n m : ‚Ñï} (h : n = m) : X ^‚¶ãn‚¶å ‚âÖ X ^‚¶ãm‚¶å :=
  X.mapIso (CategoryTheory.eqToIso (by rw [h]))

@[simp]
theorem eqToIso_refl {n : ‚Ñï} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  ext
  simp [eqToIso]

/-- The generic case of the first cosimplicial identity -/
@[reassoc]
theorem Œ¥_comp_Œ¥ {n} {i j : Fin (n + 2)} (H : i ‚â§ j) :
    X.Œ¥ i ‚â´ X.Œ¥ j.succ = X.Œ¥ j ‚â´ X.Œ¥ (Fin.castSucc i) := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, SimplexCategory.Œ¥_comp_Œ¥ H]

@[reassoc]
theorem Œ¥_comp_Œ¥' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Œ¥ i ‚â´ X.Œ¥ j =
      X.Œ¥ (j.pred fun (hj : j = 0) => by simp only [hj, Fin.not_lt_zero] at H) ‚â´
        X.Œ¥ (Fin.castSucc i) := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_Œ¥' H]

@[reassoc]
theorem Œ¥_comp_Œ¥'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i ‚â§ Fin.castSucc j) :
    X.Œ¥ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) ‚â´ X.Œ¥ j.succ =
      X.Œ¥ j ‚â´ X.Œ¥ i := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_Œ¥'' H]

/-- The special case of the first cosimplicial identity -/
@[reassoc]
theorem Œ¥_comp_Œ¥_self {n} {i : Fin (n + 2)} :
    X.Œ¥ i ‚â´ X.Œ¥ (Fin.castSucc i) = X.Œ¥ i ‚â´ X.Œ¥ i.succ := by
  dsimp [Œ¥]
  simp only [‚Üê X.map_comp, SimplexCategory.Œ¥_comp_Œ¥_self]

@[reassoc]
theorem Œ¥_comp_Œ¥_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = Fin.castSucc i) :
    X.Œ¥ i ‚â´ X.Œ¥ j = X.Œ¥ i ‚â´ X.Œ¥ i.succ := by
  subst H
  rw [Œ¥_comp_Œ¥_self]

/-- The second cosimplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i ‚â§ Fin.castSucc j) :
    X.Œ¥ (Fin.castSucc i) ‚â´ X.œÉ j.succ = X.œÉ j ‚â´ X.Œ¥ i := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, SimplexCategory.Œ¥_comp_œÉ_of_le H]

/-- The first part of the third cosimplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_self {n} {i : Fin (n + 1)} : X.Œ¥ (Fin.castSucc i) ‚â´ X.œÉ i = ùüô _ := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, SimplexCategory.Œ¥_comp_œÉ_self, X.map_id]

@[reassoc]
theorem Œ¥_comp_œÉ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Œ¥ j ‚â´ X.œÉ i = ùüô _ := by
  subst H
  rw [Œ¥_comp_œÉ_self]

/-- The second part of the third cosimplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_succ {n} {i : Fin (n + 1)} : X.Œ¥ i.succ ‚â´ X.œÉ i = ùüô _ := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, SimplexCategory.Œ¥_comp_œÉ_succ, X.map_id]

@[reassoc]
theorem Œ¥_comp_œÉ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Œ¥ j ‚â´ X.œÉ i = ùüô _ := by
  subst H
  rw [Œ¥_comp_œÉ_succ]

/-- The fourth cosimplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Œ¥ i.succ ‚â´ X.œÉ (Fin.castSucc j) = X.œÉ j ‚â´ X.Œ¥ i := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, SimplexCategory.Œ¥_comp_œÉ_of_gt H]

@[reassoc]
theorem Œ¥_comp_œÉ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Œ¥ i ‚â´ X.œÉ j =
      X.œÉ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) ‚â´
        X.Œ¥ (i.pred <|
          fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, ‚Üê op_comp, SimplexCategory.Œ¥_comp_œÉ_of_gt' H]

/-- The fifth cosimplicial identity -/
@[reassoc]
theorem œÉ_comp_œÉ {n} {i j : Fin (n + 1)} (H : i ‚â§ j) :
    X.œÉ (Fin.castSucc i) ‚â´ X.œÉ j = X.œÉ j.succ ‚â´ X.œÉ i := by
  dsimp [Œ¥, œÉ]
  simp only [‚Üê X.map_comp, SimplexCategory.œÉ_comp_œÉ H]

@[reassoc (attr := simp)]
theorem Œ¥_naturality {X' X : CosimplicialObject C} (f : X ‚ü∂ X') {n : ‚Ñï} (i : Fin (n + 2)) :
    X.Œ¥ i ‚â´ f.app (SimplexCategory.mk (n + 1)) = f.app (SimplexCategory.mk n) ‚â´ X'.Œ¥ i :=
  f.naturality _

@[reassoc (attr := simp)]
theorem œÉ_naturality {X' X : CosimplicialObject C} (f : X ‚ü∂ X') {n : ‚Ñï} (i : Fin (n + 1)) :
    X.œÉ i ‚â´ f.app (SimplexCategory.mk n) = f.app (SimplexCategory.mk (n + 1)) ‚â´ X'.œÉ i :=
  f.naturality _

variable (C)

/-- Functor composition induces a functor on cosimplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C ‚•§ D) ‚•§ CosimplicialObject C ‚•§ CosimplicialObject D :=
  whiskeringRight _ _ _

/-- Truncated cosimplicial objects. -/
def Truncated (n : ‚Ñï) :=
  SimplexCategory.Truncated n ‚•§ C

instance {n : ‚Ñï} : Category (Truncated C n) := by
  dsimp [Truncated]
  infer_instance

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasLimits C] : HasLimits (CosimplicialObject.Truncated C n) :=
  ‚ü®inferInstance‚ü©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasColimits C] : HasColimits (CosimplicialObject.Truncated C n) :=
  ‚ü®inferInstance‚ü©

variable (C) in
/-- Functor composition induces a functor on truncated cosimplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C ‚•§ D) ‚•§ Truncated C n ‚•§ Truncated D n :=
  whiskeringRight _ _ _

section Meta
/-! We provide a macro and a delaborator for the truncated cosimplicial object
notation `X ^‚¶ãm‚¶å‚Çô`. An analogous macro/delaborator pair for the truncated
simplicial object notation `X _‚¶ãm‚¶å‚Çô` is defined earlier in this file. -/

open SimplexCategory.Truncated Lean PrettyPrinter.Delaborator SubExpr
open Mathlib.Tactic (subscriptTerm delabSubscript)

/-- For `X : Truncated C n` and `m ‚â§ n`, `X ^‚¶ãm‚¶å‚Çô` is the `m`-th term of X. The
proof `p : m ‚â§ n` can also be provided using the syntax `X ^‚¶ãm, p‚¶å‚Çô`. Access
this notation with `open CategoryTheory.CosimplicialObject.Truncated`. -/
scoped syntax:max (name := mkNotation)
  term " ^‚¶ã" term ("," term)? "‚¶å" noWs subscriptTerm : term
scoped macro_rules
  | `($X:term ^‚¶ã$m:term‚¶å$n:subscript) =>
    `(($X : CategoryTheory.CosimplicialObject.Truncated _ $n).obj
      ‚ü®SimplexCategory.mk $m, by first | trunc |
      fail "Failed to prove truncation property. Try writing `X ^‚¶ãm, by ...‚¶å‚Çô`."‚ü©)
  | `($X:term ^‚¶ã$m:term, $p:term‚¶å$n:subscript) =>
    `(($X : CategoryTheory.CosimplicialObject.Truncated _ $n).obj
      ‚ü®SimplexCategory.mk $m, $p‚ü©)

/-- Delaborator for the notation `X ^‚¶ãm‚¶å‚Çô`. -/
@[app_delab Prefunctor.obj]
def delabMkNotation : Delab :=
  whenNotPPOption getPPExplicit <| whenPPOption getPPNotation do
    let_expr Prefunctor.obj src _ _ _ f x := ‚Üê getExpr | failure
    -- check that f is a functor out of the truncated simplex category
    guard <| f.isAppOfArity ``Functor.toPrefunctor 5
    guard <| src.isAppOfArity ``SimplexCategory.Truncated 1
    let_expr FullSubcategory.mk _ _ simplex _ := x | failure
    guard <| simplex.isAppOfArity ``SimplexCategory.mk 1
    -- if `pp.proofs` is set to `true`, include the proof `p : m ‚â§ n`
    let n ‚Üê withNaryArg 0 <| withAppArg <| delabSubscript
    let m ‚Üê withAppArg <| withNaryArg 2 <| withAppArg delab
    let f ‚Üê withNaryArg 4 <| withAppArg delab
    if (‚Üê getPPOption getPPProofs) then
      let p ‚Üê withAppArg <| withAppArg delab
      `($f ^‚¶ã$m, $p‚¶å$n)
    else `($f ^‚¶ã$m‚¶å$n)

end Meta

variable (C) in
/-- Further truncation of truncated cosimplicial objects. -/
def trunc (n m : ‚Ñï) (h : m ‚â§ n := by omega) : Truncated C n ‚•§ Truncated C m :=
  (whiskeringLeft _ _ _).obj <| SimplexCategory.Truncated.incl m n

end Truncated

section Truncation

/-- The truncation functor from cosimplicial objects to truncated cosimplicial objects. -/
def truncation (n : ‚Ñï) : CosimplicialObject C ‚•§ CosimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj (SimplexCategory.Truncated.inclusion n)

/-- For all `m ‚â§ n`, `truncation m` factors through `Truncated n`. -/
def truncationCompTrunc {n m : ‚Ñï} (h : m ‚â§ n) :
    truncation n ‚ãô Truncated.trunc C n m ‚âÖ truncation m :=
  Iso.refl _

end Truncation

variable (C)

/-- The constant cosimplicial object. -/
abbrev const : C ‚•§ CosimplicialObject C :=
  CategoryTheory.Functor.const _

/-- Augmented cosimplicial objects. -/
def Augmented :=
  Comma (const C) (ùü≠ (CosimplicialObject C))

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  infer_instance

variable {C}

namespace Augmented

@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X ‚ü∂ Y) (h‚ÇÅ : f.left = g.left) (h‚ÇÇ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ h‚ÇÅ h‚ÇÇ

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C ‚•§ CosimplicialObject C :=
  Comma.snd _ _

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C ‚•§ C :=
  Comma.fst _ _

/-- The functor from augmented objects to arrows. -/
@[simps!]
def toArrow : Augmented C ‚•§ Arrow C where
  obj X :=
    { left := point.obj X
      right := (drop.obj X) ^‚¶ã0‚¶å
      hom := X.hom.app _ }
  map Œ∑ :=
    { left := point.map Œ∑
      right := (drop.map Œ∑).app _
      w := by
        dsimp
        rw [‚Üê NatTrans.comp_app]
        erw [‚Üê Œ∑.w]
        rfl }

variable (C)

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C ‚•§ D) : Augmented C ‚•§ Augmented D where
  obj X :=
    { left := F.obj (point.obj X)
      right := ((whiskering _ _).obj F).obj (drop.obj X)
      hom := (Functor.constComp _ _ _).inv ‚â´ whiskerRight X.hom F }
  map Œ∑ :=
    { left := F.map Œ∑.left
      right := whiskerRight Œ∑.right _
      w := by
        ext
        dsimp
        rw [Category.id_comp, Category.id_comp, ‚Üê F.map_comp, ‚Üê F.map_comp, ‚Üê NatTrans.comp_app]
        erw [‚Üê Œ∑.w]
        rfl }

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C ‚•§ D) ‚•§ Augmented C ‚•§ Augmented D where
  obj := whiskeringObj _ _
  map Œ∑ :=
    { app := fun A =>
        { left := Œ∑.app _
          right := whiskerLeft _ Œ∑
          w := by
            ext n
            dsimp
            rw [Category.id_comp, Category.id_comp, Œ∑.naturality] }
      naturality := fun _ _ f => by ext <;> dsimp <;> simp }

variable {C}

/-- The constant augmented cosimplicial object functor. -/
@[simps]
def const : C ‚•§ Augmented C where
  obj X :=
    { left := X
      right := (CosimplicialObject.const C).obj X
      hom := ùüô _ }
  map f :=
    { left := f
      right := (CosimplicialObject.const C).map f }

end Augmented

open Simplicial

/-- Augment a cosimplicial object with an object. -/
@[simps]
def augment (X : CosimplicialObject C) (X‚ÇÄ : C) (f : X‚ÇÄ ‚ü∂ X.obj ‚¶ã0‚¶å)
    (w : ‚àÄ (i : SimplexCategory) (g‚ÇÅ g‚ÇÇ : ‚¶ã0‚¶å ‚ü∂ i),
      f ‚â´ X.map g‚ÇÅ = f ‚â´ X.map g‚ÇÇ) : CosimplicialObject.Augmented C where
  left := X‚ÇÄ
  right := X
  hom :=
    { app := fun _ => f ‚â´ X.map (SimplexCategory.const _ _ 0)
      naturality := by
        intro i j g
        dsimp
        rw [Category.id_comp, Category.assoc, ‚Üê X.map_comp, w] }

-- Not `@[simp]` since `simp` can prove this.
theorem augment_hom_zero (X : CosimplicialObject C) (X‚ÇÄ : C) (f : X‚ÇÄ ‚ü∂ X.obj ‚¶ã0‚¶å) (w) :
    (X.augment X‚ÇÄ f w).hom.app ‚¶ã0‚¶å = f := by simp

end CosimplicialObject

/-- The anti-equivalence between simplicial objects and cosimplicial objects. -/
@[simps!]
def simplicialCosimplicialEquiv : (SimplicialObject C)·µí·µñ ‚âå CosimplicialObject C·µí·µñ :=
  Functor.leftOpRightOpEquiv _ _

/-- The anti-equivalence between cosimplicial objects and simplicial objects. -/
@[simps!]
def cosimplicialSimplicialEquiv : (CosimplicialObject C)·µí·µñ ‚âå SimplicialObject C·µí·µñ :=
  Functor.opUnopEquiv _ _

variable {C}

/-- Construct an augmented cosimplicial object in the opposite
category from an augmented simplicial object. -/
@[simps!]
def SimplicialObject.Augmented.rightOp (X : SimplicialObject.Augmented C) :
    CosimplicialObject.Augmented C·µí·µñ where
  left := Opposite.op X.right
  right := X.left.rightOp
  hom := NatTrans.rightOp X.hom

/-- Construct an augmented simplicial object from an augmented cosimplicial
object in the opposite category. -/
@[simps!]
def CosimplicialObject.Augmented.leftOp (X : CosimplicialObject.Augmented C·µí·µñ) :
    SimplicialObject.Augmented C where
  left := X.right.leftOp
  right := X.left.unop
  hom := NatTrans.leftOp X.hom

/-- Converting an augmented simplicial object to an augmented cosimplicial
object and back is isomorphic to the given object. -/
@[simps!]
def SimplicialObject.Augmented.rightOpLeftOpIso (X : SimplicialObject.Augmented C) :
    X.rightOp.leftOp ‚âÖ X :=
  Comma.isoMk X.left.rightOpLeftOpIso (CategoryTheory.eqToIso <| by simp)

/-- Converting an augmented cosimplicial object to an augmented simplicial
object and back is isomorphic to the given object. -/
@[simps!]
def CosimplicialObject.Augmented.leftOpRightOpIso (X : CosimplicialObject.Augmented C·µí·µñ) :
    X.leftOp.rightOp ‚âÖ X :=
  Comma.isoMk (CategoryTheory.eqToIso <| by simp) X.right.leftOpRightOpIso

variable (C)

/-- A functorial version of `SimplicialObject.Augmented.rightOp`. -/
@[simps]
def simplicialToCosimplicialAugmented :
    (SimplicialObject.Augmented C)·µí·µñ ‚•§ CosimplicialObject.Augmented C·µí·µñ where
  obj X := X.unop.rightOp
  map f :=
    { left := f.unop.right.op
      right := NatTrans.rightOp f.unop.left
      w := by
        ext x
        dsimp
        simp_rw [‚Üê op_comp]
        congr 1
        exact (congr_app f.unop.w (op x)).symm }

/-- A functorial version of `Cosimplicial_object.Augmented.leftOp`. -/
@[simps]
def cosimplicialToSimplicialAugmented :
    CosimplicialObject.Augmented C·µí·µñ ‚•§ (SimplicialObject.Augmented C)·µí·µñ where
  obj X := Opposite.op X.leftOp
  map f :=
    Quiver.Hom.op <|
      { left := NatTrans.leftOp f.right
        right := f.left.unop
        w := by
          ext x
          dsimp
          simp_rw [‚Üê unop_comp]
          congr 1
          exact (congr_app f.w (unop x)).symm }

/-- The contravariant categorical equivalence between augmented simplicial
objects and augmented cosimplicial objects in the opposite category. -/
@[simps! functor inverse]
def simplicialCosimplicialAugmentedEquiv :
    (SimplicialObject.Augmented C)·µí·µñ ‚âå CosimplicialObject.Augmented C·µí·µñ where
  functor := simplicialToCosimplicialAugmented _
  inverse := cosimplicialToSimplicialAugmented _
  unitIso := NatIso.ofComponents (fun X => X.unop.rightOpLeftOpIso.op) fun f => by
      dsimp
      rw [‚Üê f.op_unop]
      simp_rw [‚Üê op_comp]
      congr 1
      aesop_cat
  counitIso := NatIso.ofComponents fun X => X.leftOpRightOpIso

end CategoryTheory
