/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.GeneratorsRelations.Equivalence
import Mathlib.AlgebraicTopology.SimplicialObject.Basic
/-! # Simplicial objects by generators and relations

This file provides the basic API for leveraging the equivalence obtained in
`Mathlib.AlgebraicTopology.SimplexCategory.GeneratorsRelations.Equivalence`. This is
the intended way of using this equivalence.

## Main results
  - `CosimplicialObject.ofGeneratorsAndRelations` constructs a cosimplicial objects
  out of the data of objects `n â†¦ X n` as well as codegeneracies `Ïƒ i : X (n + 1) â†’ X n`
  and cofaces `Î´ i : X n âŸ¶ X (n + 1)`, as long as they satisfy the simplicial identities.
  - The value at `mk n` of `CosimplicialObject.ofGeneratorsAndRelations` has a canonical
  isomorphism `CosimplicialObject.ofGeneratorsAndRelationsObjMkIso` with the provided object `X n`.
  - Up to conjugation by this isomorphism, the cofaces and codegeneracies maps are ones we started
  with.
  - `CosimplicialObject.mkNatTrans` (resp. `CosimplicialObject.mkNatIso`) builds a natural
  transformation (resp isomorphism) of cosimplicial objects `X Y` out of the data of maps
  `X _[n] âŸ¶ Y _[n]` that commutes with cofaces and codegeneracies.
  - Dual results for simplicial objects are provided.

-/

namespace CategoryTheory

open CategoryTheory SimplexCategory

namespace CosimplicialObject

variable {C : Type*} [Category C]

section MkCosimplicialObject

variable (X : â„• â†’ C)
  (Ïƒ : âˆ€ {n : â„•} (_ : Fin (n + 1)), X (n + 1) âŸ¶ X n)
  (Î´ : âˆ€ {n : â„•} (_ : Fin (n + 2)), X n âŸ¶ X (n + 1))
  (s1: âˆ€ {n : â„•} (i j : Fin (n + 2)) (_ : i â‰¤ j), Î´ i â‰« Î´ j.succ = Î´ j â‰« Î´ i.castSucc)
  (s2: âˆ€ {n : â„•} (i : Fin (n + 2)) (j : Fin (n + 1)) (_ : i â‰¤ j.castSucc),
    Î´ i.castSucc â‰« Ïƒ j.succ = Ïƒ j â‰« Î´ i)
  (s3â‚: âˆ€ {n : â„•} (i : Fin (n + 1)), Î´ i.castSucc â‰« Ïƒ i = ğŸ™ (X n))
  (s3â‚‚: âˆ€ {n : â„•} (i : Fin (n + 1)), Î´ i.succ â‰« Ïƒ i = ğŸ™ (X n))
  (s4: âˆ€ {n : â„•} (i : Fin (n + 2)) (j : Fin (n + 1)) (_ : j.castSucc < i),
      Î´ i.succ â‰« Ïƒ j.castSucc = Ïƒ j â‰« Î´ i)
  (s5: âˆ€ {n : â„•} (i j : Fin (n + 1)) (_ : i â‰¤ j),
      Ïƒ i.castSucc â‰« Ïƒ j = Ïƒ j.succ â‰« Ïƒ i)

/-- Making a cosimplicial object out of generators and relations. -/
noncomputable def ofGeneratorsAndRelations: CosimplicialObject C :=
  SimplexCategoryGenRel.equivSimplexCategory.inverse â‹™ (
    CategoryTheory.Quotient.lift FreeSimplexQuiver.homRel
      (Paths.lift {
        obj x := X x.len
        map {x y} f := match f with
          | .Ïƒ i => Ïƒ i
          | .Î´ i => Î´ i})
      (by
        intro x y f g hfg
        cases hfg with
        | Î´_comp_Î´ H =>
          simp only [Paths.of_obj, Paths.of_map, Functor.map_comp,
            Paths.lift_toPath]
          exact s1 _ _ H
        | Î´_comp_Ïƒ_of_le H =>
          simp only [Paths.of_obj, Paths.of_map, Functor.map_comp,
            Paths.lift_toPath]
          exact s2 _ _ H
        | Î´_comp_Ïƒ_self =>
          simp only [Paths.of_obj, Paths.of_map, Functor.map_comp,
            Paths.lift_toPath]
          exact s3â‚ _
        | Î´_comp_Ïƒ_succ =>
          simp only [Paths.of_obj, Paths.of_map, Functor.map_comp,
            Paths.lift_toPath]
          exact s3â‚‚ _
        | Î´_comp_Ïƒ_of_gt H =>
          simp only [Paths.of_obj, Paths.of_map, Functor.map_comp,
            Paths.lift_toPath]
          exact s4 _ _ H
        | Ïƒ_comp_Ïƒ H =>
          simp only [Paths.of_obj, Paths.of_map, Functor.map_comp,
            Paths.lift_toPath]
          exact s5 _ _ H ))

open SimplexCategoryGenRel in
/-- The cosimplicial object defined by generators and relations has the expected value on objects
up to isomorphism. -/
noncomputable def ofGeneratorsAndRelationsObjMkIso (n : â„•) :
    (ofGeneratorsAndRelations X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5).obj (.mk n) â‰… X n := by
  dsimp only [ofGeneratorsAndRelations, Functor.comp_obj]
  exact ((_ : SimplexCategoryGenRel â¥¤ C).mapIso (equivSimplexCategory_inverse_objIsoMk n)).trans
    (Iso.refl _)

/-- Up to the same isomorphisms, a cosimplicial object defined by generators and relations
has the provided functoriality maps for `Ïƒ`. -/
@[reassoc (attr := simp)]
lemma ofGeneratorsAndRelations_map_Ïƒ {n : â„•} (i : Fin (n + 1)) :
    (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 (n + 1)).inv â‰«
      (ofGeneratorsAndRelations X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5).Ïƒ i â‰«
        (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 n).hom
        = Ïƒ i := by
  simp [CosimplicialObject.Ïƒ, ofGeneratorsAndRelations, ofGeneratorsAndRelationsObjMkIso,
    â† Functor.map_comp, Quotient.lift_map_functor_map]

/-- Up to the same isomorphisms, a cosimplicial object defined by generators and relations
has the provided functoriality maps for `Î´`. -/
@[reassoc (attr := simp)]
lemma ofGeneratorsAndRelations_map_Î´ {n : â„•} (i : Fin (n + 2)) :
    (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 n).inv â‰«
      (ofGeneratorsAndRelations X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5).Î´ i â‰«
        (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 (n + 1)).hom
        = Î´ i := by
  simp [CosimplicialObject.Î´, ofGeneratorsAndRelations, ofGeneratorsAndRelationsObjMkIso,
    â† Functor.map_comp, Quotient.lift_map_functor_map]

end MkCosimplicialObject

section MkNatTrans

variable (X Y : CosimplicialObject C)
  (app : âˆ€ (Î” : SimplexCategory), X.obj Î” âŸ¶ Y.obj Î”)
  (hÎ´ : âˆ€ {n : â„•} (i : Fin (n + 2)), X.Î´ i â‰« app (.mk (n + 1)) = app (.mk n) â‰« Y.Î´ i)
  (hÏƒ : âˆ€ {n : â„•} (i : Fin (n + 1)), X.Ïƒ i â‰« app (.mk n) = app (.mk (n + 1)) â‰« Y.Ïƒ i)

open SimplexCategoryGenRel in
/-- Construct a natural transformation between cosimplicial objects out of an application that
commutes with cofaces and codegeneracies. -/
noncomputable def mkNatTrans: X âŸ¶ Y :=
    letI : equivSimplexCategory.functor â‹™ X âŸ¶ equivSimplexCategory.functor â‹™ Y :=
      Quotient.natTransLift _ {
        app n := app (.mk n)
        naturality {x y} f := by
          induction f using Paths.induction with
          | id => simp
          | @comp z t a f g h_rec =>
            simp only [Paths.of_obj, Functor.comp_obj, Paths.of_map, Functor.comp_map,
              Functor.map_comp, Category.assoc] at h_rec âŠ¢
            cases g with
            | @Î´ n i =>
              slice_rhs 1 2 => rw [â† h_rec]
              rw [Category.assoc]
              congr 1
              exact hÎ´ i
            | @Ïƒ n j =>
              slice_rhs 1 2 => rw [â† h_rec]
              rw [Category.assoc]
              congr 1
              exact hÏƒ j }
    (Functor.rightUnitor _).inv â‰« (whiskerRight equivSimplexCategory.counitInv X) â‰«
      (Functor.associator _ _ _).hom â‰« (whiskerLeft equivSimplexCategory.inverse this) â‰«
        (Functor.associator _ _ _).inv â‰« (whiskerRight equivSimplexCategory.counit Y) â‰«
          (Functor.rightUnitor _).hom

variable {hÎ´} {hÏƒ}

open SimplexCategoryGenRel in
/-- The natural transformation built using `mkNatTrans` have the expected application. -/
@[simp]
lemma mkNatTrans_app (Î” : SimplexCategory) : (mkNatTrans X Y app hÎ´ hÏƒ).app Î” = app Î” := by
  dsimp [mkNatTrans]
  simp only [Category.comp_id, Category.id_comp]
  generalize_proofs p
  letI : equivSimplexCategory.functor â‹™ X âŸ¶ equivSimplexCategory.functor â‹™ Y :=
    Quotient.natTransLift _
      { app n := app (.mk n)
        naturality := p}
  have h_nat := this.naturality (equivSimplexCategory_inverse_objIsoMk Î”.len).hom
  simp only [Functor.comp_obj, equivSimplexCategory_functor_obj_mk,
    equivSimplexCategory_inverse_objIsoMk, Functor.id_obj, Iso.symm_hom, Iso.app_inv,
    Functor.comp_map] at h_nat
  have h :
      equivSimplexCategory.counit.app (.mk Î”.len) =
        equivSimplexCategory.functor.map (equivSimplexCategory.unitIso.inv.app (mk Î”.len)) := by
    rw [â† equivSimplexCategory.counit_app_functor]
    rfl
  rw [â†SimplexCategory.mk_len Î”, h, â† h_nat, â† Functor.map_comp_assoc]
  rw [â† equivSimplexCategory.counit_app_functor]
  simp only [equivSimplexCategory_functor_obj_mk, Iso.inv_hom_id_app, Functor.id_obj,
    CategoryTheory.Functor.map_id, Category.id_comp, this]
  rfl

lemma mkNatTrans_comp X Y (Z : CosimplicialObject C) app
    (app' : âˆ€ (Î” : SimplexCategory), Y.obj Î” âŸ¶ Z.obj Î”)
    {hÎ´} {hÏƒ}
    {hÎ´' : âˆ€ {n : â„•} (i : Fin (n + 2)), Y.Î´ i â‰« app' (.mk (n + 1)) = app' (.mk n) â‰« Z.Î´ i}
    {hÏƒ' : âˆ€ {n : â„•} (i : Fin (n + 1)), Y.Ïƒ i â‰« app' (.mk n) = app' (.mk (n + 1)) â‰« Z.Ïƒ i}
    : mkNatTrans X Z (fun n â†¦ app n â‰« app' n)
        (by intro n i; rw [reassoc_of%(hÎ´ i), hÎ´' i]; simp)
        (by intro n i; rw [reassoc_of%(hÏƒ i), hÏƒ' i]; simp)
      = (mkNatTrans X Y app hÎ´ hÏƒ) â‰« (mkNatTrans Y Z app' hÎ´' hÏƒ') := by
  ext Î”
  simp only [mkNatTrans_app, instCategoryCosimplicialObject_comp_app]
  rw [mkNatTrans_app] <;> assumption

@[simp]
lemma mkNatTrans_id :
  mkNatTrans X X (fun x â†¦ ğŸ™ _) (by simp) (by simp) = ğŸ™ X := by
    ext x
    simp

end MkNatTrans

section MkNatIso

variable (X Y : CosimplicialObject C)
  (app : âˆ€ (Î” : SimplexCategory), X.obj Î” â‰… Y.obj Î”)
  (hÎ´ : âˆ€ {n : â„•} (i : Fin (n + 2)), X.Î´ i â‰« (app (.mk (n + 1))).hom = (app (.mk n)).hom â‰« Y.Î´ i)
  (hÏƒ : âˆ€ {n : â„•} (i : Fin (n + 1)), X.Ïƒ i â‰« (app (.mk n)).hom = (app (.mk (n + 1))).hom â‰« Y.Ïƒ i)

/-- Construct a natural isomorphism between cosimplicial objects out of an isomorphism that commutes
with cofaces and codegeneracies. -/
noncomputable def mkNatIso: X â‰… Y where
  hom := mkNatTrans X Y (fun x â†¦ (app x).hom) hÎ´ hÏƒ
  inv := mkNatTrans Y X (fun x â†¦ (app x).inv)
    (by intro n i
        rw [Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp]
        exact (hÎ´ i).symm)
    (by intro n i
        rw [Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp]
        exact (hÏƒ i).symm)

variable {hÎ´} {hÏƒ}
@[simp]
lemma mkNatIso_hom_app (Î” : SimplexCategory) :
    (mkNatIso X Y app hÎ´ hÏƒ).hom.app Î” = (app Î”).hom := by simp [mkNatIso]

@[simp]
lemma mkNatIso_inv_app (Î” : SimplexCategory) :
    (mkNatIso X Y app hÎ´ hÏƒ).inv.app Î” = (app Î”).inv := by
  simp only [mkNatIso]
  generalize_proofs p p'
  rw [mkNatTrans_app] <;> assumption

end MkNatIso

end CosimplicialObject

namespace SimplicialObject

variable {C : Type*} [Category C]

section MkSimplicialObject

variable (X : â„• â†’ C)
  (Ïƒ : âˆ€ {n : â„•} (_ : Fin (n + 1)), X n âŸ¶ X (n + 1))
  (Î´ : âˆ€ {n : â„•} (_ : Fin (n + 2)), X (n + 1) âŸ¶ X n)
  (s1: âˆ€ {n : â„•} (i j : Fin (n + 2)) (_ : i â‰¤ j), Î´ j.succ â‰« Î´ i = Î´ i.castSucc â‰« Î´ j)
  (s2: âˆ€ {n : â„•} (i : Fin (n + 2)) (j : Fin (n + 1)) (_ : i â‰¤ j.castSucc),
    Ïƒ j.succ â‰« Î´ i.castSucc  = Î´ i â‰« Ïƒ j)
  (s3â‚: âˆ€ {n : â„•} (i : Fin (n + 1)), Ïƒ i â‰« Î´ i.castSucc = ğŸ™ (X n))
  (s3â‚‚: âˆ€ {n : â„•} (i : Fin (n + 1)), Ïƒ i â‰« Î´ i.succ  = ğŸ™ (X n))
  (s4: âˆ€ {n : â„•} (i : Fin (n + 2)) (j : Fin (n + 1)) (_ : j.castSucc < i),
    Ïƒ j.castSucc â‰« Î´ i.succ  = Î´ i â‰« Ïƒ j )
  (s5: âˆ€ {n : â„•} (i j : Fin (n + 1)) (_ : i â‰¤ j),
      Ïƒ j â‰« Ïƒ i.castSucc = Ïƒ i â‰« Ïƒ j.succ )

/-- Making a simplicial object out of generators and relations. -/
noncomputable def ofGeneratorsAndRelations: SimplicialObject C :=
  Functor.leftOp <|
    CosimplicialObject.ofGeneratorsAndRelations
      (fun n â†¦ Opposite.op (X n))
      (fun j â†¦ (Ïƒ j).op)
      (fun j â†¦ (Î´ j).op)
      (by intros; simp only [â† op_comp]; congr 1; apply s1; assumption)
      (by intros; simp only [â† op_comp]; congr 1; apply s2; assumption)
      (by intros; simp only [â† op_comp]; congr 1; apply s3â‚)
      (by intros; simp only [â† op_comp]; congr 1; apply s3â‚‚)
      (by intros; simp only [â† op_comp]; congr 1; apply s4; assumption)
      (by intros; simp only [â† op_comp]; congr 1; apply s5; assumption)

open SimplexCategoryGenRel in
/-- The simplicial object defined by generators and relations has the expected value on objects
up to isomorphism. -/
noncomputable def ofGeneratorsAndRelationsObjMkIso (n : â„•) :
    X n â‰… (ofGeneratorsAndRelations X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5).obj (Opposite.op (.mk n)) := by
  dsimp only [ofGeneratorsAndRelations, Functor.leftOp_obj]
  apply (isoOpEquiv _ _).toFun
  generalize_proofs pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ pâ‚… pâ‚†
  exact CosimplicialObject.ofGeneratorsAndRelationsObjMkIso
    (fun n â†¦ Opposite.op (X n)) (fun j â†¦ (Ïƒ j).op) (fun j â†¦ (Î´ j).op) pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ pâ‚… pâ‚† n

open SimplexCategoryGenRel in
/-- Up to the same isomorphisms, a simplicial object defined by generators and relations
has the provided functoriality maps for `Ïƒ`. -/
@[reassoc (attr := simp)]
lemma ofGeneratorsAndRelations_map_Ïƒ {n : â„•} (i : Fin (n + 1)) :
    (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 n).hom â‰«
      (ofGeneratorsAndRelations X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5).Ïƒ i â‰«
        (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 (n + 1)).inv
        = Ïƒ i := by
  simp only [ofGeneratorsAndRelations, Functor.leftOp_obj, ofGeneratorsAndRelationsObjMkIso,
    isoOpEquiv, Equiv.toFun_as_coe, Equiv.coe_fn_mk, id_eq, Iso.unop_hom, SimplicialObject.Ïƒ,
    Functor.leftOp_map, Quiver.Hom.unop_op, Iso.unop_inv]
  rw [â† unop_comp_assoc, â† unop_comp]
  generalize_proofs pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ pâ‚… pâ‚†
  conv_lhs =>
    congr
    rw [â† CosimplicialObject.Ïƒ, CosimplicialObject.ofGeneratorsAndRelations_map_Ïƒ
      (fun n â†¦ Opposite.op (X n)) (fun j â†¦ (Ïƒ j).op) (fun j â†¦ (Î´ j).op) pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ pâ‚… pâ‚† i]
  simp

/-- Up to the same isomorphisms, a simplicial object defined by generators and relations
has the provided functoriality maps for `Î´`. -/
@[reassoc (attr := simp)]
lemma ofGeneratorsAndRelations_map_Î´ {n : â„•} (i : Fin (n + 2)) :
    (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 (n + 1)).hom â‰«
      (ofGeneratorsAndRelations X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5).Î´ i â‰«
        (ofGeneratorsAndRelationsObjMkIso X Ïƒ Î´ s1 s2 s3â‚ s3â‚‚ s4 s5 n).inv
        = Î´ i := by
  simp only [ofGeneratorsAndRelations, Functor.leftOp_obj, ofGeneratorsAndRelationsObjMkIso,
    isoOpEquiv, Equiv.toFun_as_coe, Equiv.coe_fn_mk, id_eq, Iso.unop_hom, SimplicialObject.Î´,
    Functor.leftOp_map, Quiver.Hom.unop_op, Iso.unop_inv]
  rw [â† unop_comp_assoc, â† unop_comp]
  generalize_proofs pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ pâ‚… pâ‚†
  conv_lhs =>
    congr
    rw [â† CosimplicialObject.Î´, CosimplicialObject.ofGeneratorsAndRelations_map_Î´
      (fun n â†¦ Opposite.op (X n)) (fun j â†¦ (Ïƒ j).op) (fun j â†¦ (Î´ j).op) pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ pâ‚… pâ‚† i]
  simp

end MkSimplicialObject

section MkNatTrans

variable (X Y : SimplicialObject C)
  (app : âˆ€ (Î” : SimplexCategoryáµ’áµ–), X.obj Î” âŸ¶ Y.obj Î”)
  (hÎ´ : âˆ€ {n : â„•} (i : Fin (n + 2)), X.Î´ i â‰« app (.op (.mk n)) = app (.op (.mk (n + 1))) â‰« Y.Î´ i)
  (hÏƒ : âˆ€ {n : â„•} (i : Fin (n + 1)), X.Ïƒ i â‰« app (.op (.mk (n + 1))) = app (.op (.mk n)) â‰« Y.Ïƒ i)

open SimplexCategoryGenRel in
/-- Construct a natural transformation between simplicial objects out of an application that
commutes with faces and degeneracies. -/
noncomputable def mkNatTrans: X âŸ¶ Y :=
  NatTrans.removeRightOp <|
    CosimplicialObject.mkNatTrans Y.rightOp X.rightOp
      (fun Î” â†¦ (app (.op Î”)).op)
      (fun j â†¦ by
        simp only [Functor.rightOp_obj, CosimplicialObject.Î´, Functor.rightOp_map]
        rw [â† op_comp, â† op_comp]
        congr 1
        exact (hÎ´ j).symm)
      (fun j â†¦ by
        simp only [Functor.rightOp_obj, CosimplicialObject.Ïƒ, Functor.rightOp_map]
        rw [â† op_comp, â† op_comp]
        congr 1
        exact (hÏƒ j).symm)

variable {hÎ´} {hÏƒ}
/-- The natural transformation built using mkNatTrans have the expected application. -/
@[simp]
lemma mkNatTrans_app (Î” : SimplexCategoryáµ’áµ–) : (mkNatTrans X Y app hÎ´ hÏƒ).app Î” = app Î” := by
  dsimp [mkNatTrans]
  generalize_proofs p p'
  rw [CosimplicialObject.mkNatTrans_app Y.rightOp X.rightOp (fun Î” â†¦ (app (.op Î”)).op)]
  Â· simp
  Â· assumption
  Â· assumption

lemma mkNatTrans_comp X Y (Z : SimplicialObject C) app
    (app' : âˆ€ (Î” : SimplexCategoryáµ’áµ–), Y.obj Î” âŸ¶ Z.obj Î”)
    {hÎ´} {hÏƒ}
    {hÎ´' : âˆ€ {n : â„•} (i : Fin (n + 2)),
      Y.Î´ i â‰« app' (.op (.mk n)) = app' (.op (.mk (n + 1))) â‰« Z.Î´ i}
    {hÏƒ' : âˆ€ {n : â„•} (i : Fin (n + 1)),
      Y.Ïƒ i â‰« app' (.op (.mk (n + 1))) = app' (.op (.mk n)) â‰« Z.Ïƒ i}
    : mkNatTrans X Z (fun n â†¦ app n â‰« app' n)
        (by intro n i; rw [reassoc_of%(hÎ´ i), hÎ´' i]; simp)
        (by intro n i; rw [reassoc_of%(hÏƒ i), hÏƒ' i]; simp)
      = (mkNatTrans X Y app hÎ´ hÏƒ) â‰« (mkNatTrans Y Z app' hÎ´' hÏƒ') := by
  ext Î”
  simp only [mkNatTrans_app, instCategorySimplicialObject_comp_app]
  rw [mkNatTrans_app] <;> assumption

@[simp]
lemma mkNatTrans_id :
  mkNatTrans X X (fun x â†¦ ğŸ™ _) (by simp) (by simp) = ğŸ™ X := by
    ext x
    simp

end MkNatTrans

section MkNatIso

variable (X Y: SimplicialObject C)
  (app : âˆ€ (Î” : SimplexCategoryáµ’áµ–), X.obj Î” â‰… Y.obj Î”)
  (hÎ´ : âˆ€ {n : â„•} (i : Fin (n + 2)),
    X.Î´ i â‰« (app (.op (.mk n))).hom = (app (.op (.mk (n + 1)))).hom â‰« Y.Î´ i)
  (hÏƒ : âˆ€ {n : â„•} (i : Fin (n + 1)),
    X.Ïƒ i â‰« (app (.op (.mk (n + 1)))).hom = (app (.op (.mk n))).hom â‰« Y.Ïƒ i)

/-- Construct a natural isomorphism between simplicial objects out of an isomorphism that commutes
with faces and degeneracies. -/
noncomputable def mkNatIso: X â‰… Y where
  hom := mkNatTrans X Y (fun x â†¦ (app x).hom) hÎ´ hÏƒ
  inv := mkNatTrans Y X (fun x â†¦ (app x).inv)
    (by intro n i
        rw [Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp]
        exact (hÎ´ i).symm)
    (by intro n i
        rw [Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp]
        exact (hÏƒ i).symm)

variable {hÎ´} {hÏƒ}

@[simp]
lemma mkNatIso_hom_app (Î” : SimplexCategoryáµ’áµ–) :
    (mkNatIso X Y app hÎ´ hÏƒ).hom.app Î” = (app Î”).hom := by simp [mkNatIso]

@[simp]
lemma mkNatIso_inv_app (Î” : SimplexCategoryáµ’áµ–) :
    (mkNatIso X Y app hÎ´ hÏƒ).inv.app Î” = (app Î”).inv := by
  simp only [mkNatIso]
  generalize_proofs p p'
  rw [mkNatTrans_app] <;> assumption

end MkNatIso

end SimplicialObject

end CategoryTheory
