/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialObject.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.Data.Fintype.Sigma

/-!

# Split simplicial objects

In this file, we introduce the notion of split simplicial object.
If `C` is a category that has finite coproducts, a splitting
`s : Splitting X` of a simplicial object `X` in `C` consists
of the datum of a sequence of objects `s.N : â„• â†’ C` (which
we shall refer to as "nondegenerate simplices") and a
sequence of morphisms `s.Î¹ n : s.N n â†’ X _â¦‹nâ¦Œ` that have
the property that a certain canonical map identifies `X _â¦‹nâ¦Œ`
with the coproduct of objects `s.N i` indexed by all possible
epimorphisms `â¦‹nâ¦Œ âŸ¶ â¦‹iâ¦Œ` in `SimplexCategory`. (We do not
assume that the morphisms `s.Î¹ n` are monomorphisms: in the
most common categories, this would be a consequence of the
axioms.)

Simplicial objects equipped with a splitting form a category
`SimplicialObject.Split C`.

## References
* [Stacks: Splitting simplicial objects] https://stacks.math.columbia.edu/tag/017O

-/


noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits Opposite SimplexCategory

open Simplicial

universe u

variable {C : Type*} [Category C]

namespace SimplicialObject

namespace Splitting

/-- The index set which appears in the definition of split simplicial objects. -/
def IndexSet (Î” : SimplexCategoryáµ’áµ–) :=
  Î£ Î”' : SimplexCategoryáµ’áµ–, { Î± : Î”.unop âŸ¶ Î”'.unop // Epi Î± }

namespace IndexSet

/-- The element in `Splitting.IndexSet Î”` attached to an epimorphism `f : Î” âŸ¶ Î”'`. -/
@[simps]
def mk {Î” Î”' : SimplexCategory} (f : Î” âŸ¶ Î”') [Epi f] : IndexSet (op Î”) :=
  âŸ¨op Î”', f, inferInstanceâŸ©

variable {Î” : SimplexCategoryáµ’áµ–} (A : IndexSet Î”)

/-- The epimorphism in `SimplexCategory` associated to `A : Splitting.IndexSet Î”` -/
def e :=
  A.2.1

instance : Epi A.e :=
  A.2.2

theorem ext' : A = âŸ¨A.1, âŸ¨A.e, A.2.2âŸ©âŸ© := rfl

theorem ext (Aâ‚ Aâ‚‚ : IndexSet Î”) (hâ‚ : Aâ‚.1 = Aâ‚‚.1) (hâ‚‚ : Aâ‚.e â‰« eqToHom (by rw [hâ‚]) = Aâ‚‚.e) :
    Aâ‚ = Aâ‚‚ := by
  rcases Aâ‚ with âŸ¨Î”â‚, âŸ¨Î±â‚, hÎ±â‚âŸ©âŸ©
  rcases Aâ‚‚ with âŸ¨Î”â‚‚, âŸ¨Î±â‚‚, hÎ±â‚‚âŸ©âŸ©
  simp only at hâ‚
  subst hâ‚
  simp only [eqToHom_refl, comp_id, IndexSet.e] at hâ‚‚
  simp only [hâ‚‚]

instance : Fintype (IndexSet Î”) :=
  Fintype.ofInjective
    (fun A â†¦
      âŸ¨âŸ¨A.1.unop.len, Nat.lt_succ_iff.mpr (len_le_of_epi A.e)âŸ©,
        A.e.toOrderHomâŸ© :
      IndexSet Î” â†’ Sigma fun k : Fin (Î”.unop.len + 1) â†¦ Fin (Î”.unop.len + 1) â†’ Fin (k + 1))
    (by
      rintro âŸ¨Î”â‚, Î±â‚âŸ© âŸ¨Î”â‚‚, Î±â‚‚âŸ© hâ‚
      induction' Î”â‚ using Opposite.rec with Î”â‚
      induction' Î”â‚‚ using Opposite.rec with Î”â‚‚
      simp only [unop_op, Sigma.mk.inj_iff, Fin.mk.injEq] at hâ‚
      have hâ‚‚ : Î”â‚ = Î”â‚‚ := by
        ext1
        simpa only [Fin.mk_eq_mk] using hâ‚.1
      subst hâ‚‚
      refine ext _ _ rfl ?_
      ext : 2
      exact eq_of_heq hâ‚.2)

variable (Î”)

/-- The distinguished element in `Splitting.IndexSet Î”` which corresponds to the
identity of `Î”`. -/
@[simps]
def id : IndexSet Î” :=
  âŸ¨Î”, âŸ¨ğŸ™ _, by infer_instanceâŸ©âŸ©

instance : Inhabited (IndexSet Î”) :=
  âŸ¨id Î”âŸ©

variable {Î”}

/-- The condition that an element `Splitting.IndexSet Î”` is the distinguished
element `Splitting.IndexSet.Id Î”`. -/
@[simp]
def EqId : Prop :=
  A = id _

theorem eqId_iff_eq : A.EqId â†” A.1 = Î” := by
  constructor
  Â· intro h
    dsimp at h
    rw [h]
    rfl
  Â· intro h
    rcases A with âŸ¨_, âŸ¨f, hfâŸ©âŸ©
    simp only at h
    subst h
    refine ext _ _ rfl ?_
    simp only [eqToHom_refl, comp_id]
    exact eq_id_of_epi f

theorem eqId_iff_len_eq : A.EqId â†” A.1.unop.len = Î”.unop.len := by
  rw [eqId_iff_eq]
  constructor
  Â· intro h
    rw [h]
  Â· intro h
    rw [â† unop_inj_iff]
    ext
    exact h

theorem eqId_iff_len_le : A.EqId â†” Î”.unop.len â‰¤ A.1.unop.len := by
  rw [eqId_iff_len_eq]
  constructor
  Â· intro h
    rw [h]
  Â· exact le_antisymm (len_le_of_epi A.e)

theorem eqId_iff_mono : A.EqId â†” Mono A.e := by
  constructor
  Â· intro h
    dsimp at h
    subst h
    dsimp only [id, e]
    infer_instance
  Â· intro
    rw [eqId_iff_len_le]
    exact len_le_of_mono A.e

/-- Given `A : IndexSet Î”â‚`, if `p.unop : unop Î”â‚‚ âŸ¶ unop Î”â‚` is an epi, this
is the obvious element in `A : IndexSet Î”â‚‚` associated to the composition
of epimorphisms `p.unop â‰« A.e`. -/
@[simps]
def epiComp {Î”â‚ Î”â‚‚ : SimplexCategoryáµ’áµ–} (A : IndexSet Î”â‚) (p : Î”â‚ âŸ¶ Î”â‚‚) [Epi p.unop] :
    IndexSet Î”â‚‚ :=
  âŸ¨A.1, âŸ¨p.unop â‰« A.e, epi_comp _ _âŸ©âŸ©


variable {Î”' : SimplexCategoryáµ’áµ–} (Î¸ : Î” âŸ¶ Î”')

/-- When `A : IndexSet Î”` and `Î¸ : Î” â†’ Î”'` is a morphism in `SimplexCategoryáµ’áµ–`,
an element in `IndexSet Î”'` can be defined by using the epi-mono factorisation
of `Î¸.unop â‰« A.e`. -/
def pull : IndexSet Î”' :=
  mk (factorThruImage (Î¸.unop â‰« A.e))

@[reassoc]
theorem fac_pull : (A.pull Î¸).e â‰« image.Î¹ (Î¸.unop â‰« A.e) = Î¸.unop â‰« A.e :=
  image.fac _

end IndexSet

variable (N : â„• â†’ C) (Î” : SimplexCategoryáµ’áµ–) (X : SimplicialObject C) (Ï† : âˆ€ n, N n âŸ¶ X _â¦‹nâ¦Œ)

/-- Given a sequences of objects `N : â„• â†’ C` in a category `C`, this is
a family of objects indexed by the elements `A : Splitting.IndexSet Î”`.
The `Î”`-simplices of a split simplicial objects shall identify to the
coproduct of objects in such a family. -/
@[simp, nolint unusedArguments]
def summand (A : IndexSet Î”) : C :=
  N A.1.unop.len

/-- The cofan for `summand N Î”` induced by morphisms `N n âŸ¶ X _â¦‹nâ¦Œ` for all `n : â„•`. -/
def cofan' (Î” : SimplexCategoryáµ’áµ–) : Cofan (summand N Î”) :=
  Cofan.mk (X.obj Î”) (fun A â†¦ Ï† A.1.unop.len â‰« X.map A.e.op)

end Splitting

/-- A splitting of a simplicial object `X` consists of the datum of a sequence
of objects `N`, a sequence of morphisms `Î¹ : N n âŸ¶ X _â¦‹nâ¦Œ` such that
for all `Î” : SimplexCategoryáµ’áµ–`, the canonical map `Splitting.map X Î¹ Î”`
is an isomorphism. -/
structure Splitting (X : SimplicialObject C) where
  /-- The "nondegenerate simplices" `N n` for all `n : â„•`. -/
  N : â„• â†’ C
  /-- The "inclusion" `N n âŸ¶ X _â¦‹nâ¦Œ` for all `n : â„•`. -/
  Î¹ : âˆ€ n, N n âŸ¶ X _â¦‹nâ¦Œ
  /-- For each `Î”`, `X.obj Î”` identifies to the coproduct of the objects `N A.1.unop.len`
  for all `A : IndexSet Î”`. -/
  isColimit' : âˆ€ Î” : SimplexCategoryáµ’áµ–, IsColimit (Splitting.cofan' N X Î¹ Î”)

namespace Splitting

variable {X Y : SimplicialObject C} (s : Splitting X)

/-- The cofan for `summand s.N Î”` induced by a splitting of a simplicial object. -/
def cofan (Î” : SimplexCategoryáµ’áµ–) : Cofan (summand s.N Î”) :=
  Cofan.mk (X.obj Î”) (fun A â†¦ s.Î¹ A.1.unop.len â‰« X.map A.e.op)

/-- The cofan `s.cofan Î”` is colimit. -/
def isColimit (Î” : SimplexCategoryáµ’áµ–) : IsColimit (s.cofan Î”) := s.isColimit' Î”

@[reassoc]
theorem cofan_inj_eq {Î” : SimplexCategoryáµ’áµ–} (A : IndexSet Î”) :
    (s.cofan Î”).inj  A = s.Î¹ A.1.unop.len â‰« X.map A.e.op := rfl

theorem cofan_inj_id (n : â„•) : (s.cofan _).inj (IndexSet.id (op â¦‹nâ¦Œ)) = s.Î¹ n := by
  simp [IndexSet.id, IndexSet.e, cofan_inj_eq]

/-- As it is stated in `Splitting.hom_ext`, a morphism `f : X âŸ¶ Y` from a split
simplicial object to any simplicial object is determined by its restrictions
`s.Ï† f n : s.N n âŸ¶ Y _â¦‹nâ¦Œ` to the distinguished summands in each degree `n`. -/
@[simp]
def Ï† (f : X âŸ¶ Y) (n : â„•) : s.N n âŸ¶ Y _â¦‹nâ¦Œ :=
  s.Î¹ n â‰« f.app (op â¦‹nâ¦Œ)

@[reassoc (attr := simp)]
theorem cofan_inj_comp_app (f : X âŸ¶ Y) {Î” : SimplexCategoryáµ’áµ–} (A : IndexSet Î”) :
    (s.cofan Î”).inj A â‰« f.app Î” = s.Ï† f A.1.unop.len â‰« Y.map A.e.op := by
  simp only [cofan_inj_eq_assoc, Ï†, assoc]
  rw [NatTrans.naturality]

theorem hom_ext' {Z : C} {Î” : SimplexCategoryáµ’áµ–} (f g : X.obj Î” âŸ¶ Z)
    (h : âˆ€ A : IndexSet Î”, (s.cofan Î”).inj A â‰« f = (s.cofan Î”).inj A â‰« g) : f = g :=
  Cofan.IsColimit.hom_ext (s.isColimit Î”) _ _ h

theorem hom_ext (f g : X âŸ¶ Y) (h : âˆ€ n : â„•, s.Ï† f n = s.Ï† g n) : f = g := by
  ext Î”
  apply s.hom_ext'
  intro A
  induction' Î” using Opposite.rec with Î”
  induction' Î” using SimplexCategory.rec with n
  dsimp
  simp only [s.cofan_inj_comp_app, h]

/-- The map `X.obj Î” âŸ¶ Z` obtained by providing a family of morphisms on all the
terms of decomposition given by a splitting `s : Splitting X` -/
def desc {Z : C} (Î” : SimplexCategoryáµ’áµ–) (F : âˆ€ A : IndexSet Î”, s.N A.1.unop.len âŸ¶ Z) :
    X.obj Î” âŸ¶ Z :=
  Cofan.IsColimit.desc (s.isColimit Î”) F

@[reassoc (attr := simp)]
theorem Î¹_desc {Z : C} (Î” : SimplexCategoryáµ’áµ–) (F : âˆ€ A : IndexSet Î”, s.N A.1.unop.len âŸ¶ Z)
    (A : IndexSet Î”) : (s.cofan Î”).inj A â‰« s.desc Î” F = F A := by
  apply Cofan.IsColimit.fac

/-- A simplicial object that is isomorphic to a split simplicial object is split. -/
@[simps]
def ofIso (e : X â‰… Y) : Splitting Y where
  N := s.N
  Î¹ n := s.Î¹ n â‰« e.hom.app (op â¦‹nâ¦Œ)
  isColimit' Î” := IsColimit.ofIsoColimit (s.isColimit Î” ) (Cofan.ext (e.app Î”)
    (fun A â†¦ by simp [cofan, cofan']))

@[reassoc]
theorem cofan_inj_epi_naturality {Î”â‚ Î”â‚‚ : SimplexCategoryáµ’áµ–} (A : IndexSet Î”â‚) (p : Î”â‚ âŸ¶ Î”â‚‚)
    [Epi p.unop] : (s.cofan Î”â‚).inj A â‰« X.map p = (s.cofan Î”â‚‚).inj (A.epiComp p) := by
  dsimp [cofan]
  rw [assoc, â† X.map_comp]
  rfl

end Splitting

variable (C)

/-- The category `SimplicialObject.Split C` is the category of simplicial objects
in `C` equipped with a splitting, and morphisms are morphisms of simplicial objects
which are compatible with the splittings. -/
@[ext]
structure Split where
  /-- the underlying simplicial object -/
  X : SimplicialObject C
  /-- a splitting of the simplicial object -/
  s : Splitting X

namespace Split

variable {C}

/-- The object in `SimplicialObject.Split C` attached to a splitting `s : Splitting X`
of a simplicial object `X`. -/
@[simps]
def mk' {X : SimplicialObject C} (s : Splitting X) : Split C :=
  âŸ¨X, sâŸ©

/-- Morphisms in `SimplicialObject.Split C` are morphisms of simplicial objects that
are compatible with the splittings. -/
structure Hom (Sâ‚ Sâ‚‚ : Split C) where
  /-- the morphism between the underlying simplicial objects -/
  F : Sâ‚.X âŸ¶ Sâ‚‚.X
  /-- the morphism between the "nondegenerate" `n`-simplices for all `n : â„•` -/
  f : âˆ€ n : â„•, Sâ‚.s.N n âŸ¶ Sâ‚‚.s.N n
  comm : âˆ€ n : â„•, Sâ‚.s.Î¹ n â‰« F.app (op â¦‹nâ¦Œ) = f n â‰« Sâ‚‚.s.Î¹ n := by cat_disch

@[ext]
theorem Hom.ext {Sâ‚ Sâ‚‚ : Split C} (Î¦â‚ Î¦â‚‚ : Hom Sâ‚ Sâ‚‚) (h : âˆ€ n : â„•, Î¦â‚.f n = Î¦â‚‚.f n) : Î¦â‚ = Î¦â‚‚ := by
  rcases Î¦â‚ with âŸ¨Fâ‚, fâ‚, câ‚âŸ©
  rcases Î¦â‚‚ with âŸ¨Fâ‚‚, fâ‚‚, câ‚‚âŸ©
  have h' : fâ‚ = fâ‚‚ := by
    ext
    apply h
  subst h'
  simp only [mk.injEq, and_true]
  apply Sâ‚.s.hom_ext
  intro n
  dsimp
  rw [câ‚, câ‚‚]

attribute [simp, reassoc] Hom.comm

end Split

instance : Category (Split C) where
  Hom := Split.Hom
  id S :=
    { F := ğŸ™ _
      f := fun _ â†¦ ğŸ™ _ }
  comp Î¦â‚â‚‚ Î¦â‚‚â‚ƒ :=
    { F := Î¦â‚â‚‚.F â‰« Î¦â‚‚â‚ƒ.F
      f := fun n â†¦ Î¦â‚â‚‚.f n â‰« Î¦â‚‚â‚ƒ.f n
      comm := fun n â†¦ by
        dsimp
        simp only [assoc, Split.Hom.comm_assoc, Split.Hom.comm] }

variable {C}

namespace Split

@[ext]
theorem hom_ext {Sâ‚ Sâ‚‚ : Split C} (Î¦â‚ Î¦â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (h : âˆ€ n : â„•, Î¦â‚.f n = Î¦â‚‚.f n) : Î¦â‚ = Î¦â‚‚ :=
  Hom.ext _ _ h

theorem congr_F {Sâ‚ Sâ‚‚ : Split C} {Î¦â‚ Î¦â‚‚ : Sâ‚ âŸ¶ Sâ‚‚} (h : Î¦â‚ = Î¦â‚‚) : Î¦â‚.f = Î¦â‚‚.f := by rw [h]

theorem congr_f {Sâ‚ Sâ‚‚ : Split C} {Î¦â‚ Î¦â‚‚ : Sâ‚ âŸ¶ Sâ‚‚} (h : Î¦â‚ = Î¦â‚‚) (n : â„•) : Î¦â‚.f n = Î¦â‚‚.f n := by
  rw [h]

@[simp]
theorem id_F (S : Split C) : (ğŸ™ S : S âŸ¶ S).F = ğŸ™ S.X :=
  rfl

@[simp]
theorem id_f (S : Split C) (n : â„•) : (ğŸ™ S : S âŸ¶ S).f n = ğŸ™ (S.s.N n) :=
  rfl

@[simp]
theorem comp_F {Sâ‚ Sâ‚‚ Sâ‚ƒ : Split C} (Î¦â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Î¦â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) :
    (Î¦â‚â‚‚ â‰« Î¦â‚‚â‚ƒ).F = Î¦â‚â‚‚.F â‰« Î¦â‚‚â‚ƒ.F :=
  rfl

@[simp]
theorem comp_f {Sâ‚ Sâ‚‚ Sâ‚ƒ : Split C} (Î¦â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Î¦â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) (n : â„•) :
    (Î¦â‚â‚‚ â‰« Î¦â‚‚â‚ƒ).f n = Î¦â‚â‚‚.f n â‰« Î¦â‚‚â‚ƒ.f n :=
  rfl

-- This is not a `@[simp]` lemma as it can later be proved by `simp`.
@[reassoc]
theorem cofan_inj_naturality_symm {Sâ‚ Sâ‚‚ : Split C} (Î¦ : Sâ‚ âŸ¶ Sâ‚‚) {Î” : SimplexCategoryáµ’áµ–}
    (A : Splitting.IndexSet Î”) :
    (Sâ‚.s.cofan Î”).inj A â‰« Î¦.F.app Î” = Î¦.f A.1.unop.len â‰« (Sâ‚‚.s.cofan Î”).inj A := by
  rw [Sâ‚.s.cofan_inj_eq, Sâ‚‚.s.cofan_inj_eq, assoc, Î¦.F.naturality, â† Î¦.comm_assoc]

variable (C)

/-- The functor `SimplicialObject.Split C â¥¤ SimplicialObject C` which forgets
the splitting. -/
@[simps]
def forget : Split C â¥¤ SimplicialObject C where
  obj S := S.X
  map Î¦ := Î¦.F

/-- The functor `SimplicialObject.Split C â¥¤ C` which sends a simplicial object equipped
with a splitting to its nondegenerate `n`-simplices. -/
@[simps]
def evalN (n : â„•) : Split C â¥¤ C where
  obj S := S.s.N n
  map Î¦ := Î¦.f n

/-- The inclusion of each summand in the coproduct decomposition of simplices
in split simplicial objects is a natural transformation of functors
`SimplicialObject.Split C â¥¤ C` -/
@[simps]
def natTransCofanInj {Î” : SimplexCategoryáµ’áµ–} (A : Splitting.IndexSet Î”) :
    evalN C A.1.unop.len âŸ¶ forget C â‹™ (evaluation SimplexCategoryáµ’áµ– C).obj Î” where
  app S := (S.s.cofan Î”).inj A
  naturality _ _ Î¦ := (cofan_inj_naturality_symm Î¦ A).symm

end Split

end SimplicialObject
