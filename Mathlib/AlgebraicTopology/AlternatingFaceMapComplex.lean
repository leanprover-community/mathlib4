/-
Copyright (c) 2021 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Adam Topaz, Johan Commelin
-/
import Mathlib.Algebra.Homology.Additive
import Mathlib.AlgebraicTopology.MooreComplex
import Mathlib.Algebra.BigOperators.Fin
import Mathlib.CategoryTheory.Preadditive.Opposite
import Mathlib.CategoryTheory.Idempotents.FunctorCategories

#align_import algebraic_topology.alternating_face_map_complex from "leanprover-community/mathlib"@"88bca0ce5d22ebfd9e73e682e51d60ea13b48347"

/-!

# The alternating face map complex of a simplicial object in a preadditive category

We construct the alternating face map complex, as a
functor `alternatingFaceMapComplex : SimplicialObject C â¥¤ ChainComplex C â„•`
for any preadditive category `C`. For any simplicial object `X` in `C`,
this is the homological complex `... â†’ X_2 â†’ X_1 â†’ X_0`
where the differentials are alternating sums of faces.

The dual version `alternatingCofaceMapComplex : CosimplicialObject C â¥¤ CochainComplex C â„•`
is also constructed.

We also construct the natural transformation
`inclusionOfMooreComplex : normalizedMooreComplex A âŸ¶ alternatingFaceMapComplex A`
when `A` is an abelian category.

## References
* https://stacks.math.columbia.edu/tag/0194
* https://ncatlab.org/nlab/show/Moore+complex

-/


open CategoryTheory CategoryTheory.Limits CategoryTheory.Subobject

open CategoryTheory.Preadditive CategoryTheory.Category CategoryTheory.Idempotents

open Opposite

open BigOperators

open Simplicial

noncomputable section

namespace AlgebraicTopology

namespace AlternatingFaceMapComplex

/-!
## Construction of the alternating face map complex
-/


variable {C : Type*} [Category C] [Preadditive C]

variable (X : SimplicialObject C)

variable (Y : SimplicialObject C)

/-- The differential on the alternating face map complex is the alternate
sum of the face maps -/
@[simp]
def objD (n : â„•) : X _[n + 1] âŸ¶ X _[n] :=
  âˆ‘ i : Fin (n + 2), (-1 : â„¤) ^ (i : â„•) â€¢ X.Î´ i
#align algebraic_topology.alternating_face_map_complex.obj_d AlgebraicTopology.AlternatingFaceMapComplex.objD

/-- ## The chain complex relation `d â‰« d`
-/
theorem d_squared (n : â„•) : objD X (n + 1) â‰« objD X n = 0 := by
  -- we start by expanding d â‰« d as a double sum
  dsimp
  -- âŠ¢ (âˆ‘ i : Fin (n + 1 + 2), (-1) ^ â†‘i â€¢ SimplicialObject.Î´ X i) â‰« âˆ‘ i : Fin (n + â€¦
  simp only [comp_sum, sum_comp, â† Finset.sum_product']
  -- âŠ¢ âˆ‘ x in Finset.univ Ã—Ë¢ Finset.univ, ((-1) ^ â†‘x.snd â€¢ SimplicialObject.Î´ X x.s â€¦
  -- then, we decompose the index set P into a subset S and its complement Sá¶œ
  let P := Fin (n + 2) Ã— Fin (n + 3)
  -- âŠ¢ âˆ‘ x in Finset.univ Ã—Ë¢ Finset.univ, ((-1) ^ â†‘x.snd â€¢ SimplicialObject.Î´ X x.s â€¦
  let S := Finset.univ.filter fun ij : P => (ij.2 : â„•) â‰¤ (ij.1 : â„•)
  -- âŠ¢ âˆ‘ x in Finset.univ Ã—Ë¢ Finset.univ, ((-1) ^ â†‘x.snd â€¢ SimplicialObject.Î´ X x.s â€¦
  erw [â† Finset.sum_add_sum_compl S, â† eq_neg_iff_add_eq_zero, â† Finset.sum_neg_distrib]
  -- âŠ¢ âˆ‘ i in S, ((-1) ^ â†‘i.snd â€¢ SimplicialObject.Î´ X i.snd) â‰« ((-1) ^ â†‘i.fst â€¢ Si â€¦
  /- we are reduced to showing that two sums are equal, and this is obtained
    by constructing a bijection Ï† : S -> Sá¶œ, which maps (i,j) to (j,i+1),
    and by comparing the terms -/
  let Ï† : âˆ€ ij : P, ij âˆˆ S â†’ P := fun ij hij =>
    (Fin.castLT ij.2 (lt_of_le_of_lt (Finset.mem_filter.mp hij).right (Fin.is_lt ij.1)), ij.1.succ)
  apply Finset.sum_bij Ï†
  Â· -- Ï†(S) is contained in Sá¶œ
    intro ij hij
    -- âŠ¢ Ï† ij hij âˆˆ Sá¶œ
    simp only [Finset.mem_univ, Finset.compl_filter, Finset.mem_filter, true_and_iff, Fin.val_succ,
      Fin.coe_castLT] at hij âŠ¢
    linarith
    -- ğŸ‰ no goals
  Â· -- identification of corresponding terms in both sums
    rintro âŸ¨i, jâŸ© hij
    -- âŠ¢ ((-1) ^ â†‘(i, j).snd â€¢ SimplicialObject.Î´ X (i, j).snd) â‰« ((-1) ^ â†‘(i, j).fst â€¦
    dsimp
    -- âŠ¢ ((-1) ^ â†‘j â€¢ SimplicialObject.Î´ X j) â‰« ((-1) ^ â†‘i â€¢ SimplicialObject.Î´ X i)  â€¦
    simp only [zsmul_comp, comp_zsmul, smul_smul, â† neg_smul]
    -- âŠ¢ ((-1) ^ â†‘i * (-1) ^ â†‘j) â€¢ SimplicialObject.Î´ X j â‰« SimplicialObject.Î´ X i =  â€¦
    congr 1
    -- âŠ¢ (-1) ^ â†‘i * (-1) ^ â†‘j = -((-1) ^ â†‘j * (-1) ^ â†‘(Fin.succ i))
    Â· simp only [Fin.val_succ, pow_add, pow_one, mul_neg, neg_neg, mul_one]
      -- âŠ¢ (-1) ^ â†‘i * (-1) ^ â†‘j = (-1) ^ â†‘j * (-1) ^ â†‘i
      apply mul_comm
      -- ğŸ‰ no goals
    Â· rw [CategoryTheory.SimplicialObject.Î´_comp_Î´'']
      -- âŠ¢ j â‰¤ Fin.castSucc i
      simpa using hij
      -- ğŸ‰ no goals
  Â· -- Ï† : S â†’ Sá¶œ is injective
    rintro âŸ¨i, jâŸ© âŸ¨i', j'âŸ© hij hij' h
    -- âŠ¢ (i, j) = (i', j')
    rw [Prod.mk.inj_iff]
    -- âŠ¢ i = i' âˆ§ j = j'
    exact âŸ¨by simpa using congr_arg Prod.snd h,
      by simpa [Fin.castSucc_castLT] using congr_arg Fin.castSucc (congr_arg Prod.fst h)âŸ©
  Â· -- Ï† : S â†’ Sá¶œ is surjective
    rintro âŸ¨i', j'âŸ© hij'
    -- âŠ¢ âˆƒ a ha, (i', j') = Ï† a ha
    simp only [Finset.mem_univ, forall_true_left, Prod.forall, ge_iff_le, Finset.compl_filter,
      not_le, Finset.mem_filter, true_and] at hij'
    refine' âŸ¨(j'.pred <| _, Fin.castSucc i'), _, _âŸ©
    Â· rintro rfl
      -- âŠ¢ False
      simp only [Fin.val_zero, not_lt_zero'] at hij'
      -- ğŸ‰ no goals
    Â· simpa only [Finset.mem_univ, forall_true_left, Prod.forall, ge_iff_le, Finset.mem_filter,
        Fin.coe_castSucc, Fin.coe_pred, true_and] using Nat.le_pred_of_lt hij'
    Â· simp only [Fin.castLT_castSucc, Fin.succ_pred]
      -- ğŸ‰ no goals
#align algebraic_topology.alternating_face_map_complex.d_squared AlgebraicTopology.AlternatingFaceMapComplex.d_squared

/-!
## Construction of the alternating face map complex functor
-/


/-- The alternating face map complex, on objects -/
def obj : ChainComplex C â„• :=
  ChainComplex.of (fun n => X _[n]) (objD X) (d_squared X)
#align algebraic_topology.alternating_face_map_complex.obj AlgebraicTopology.AlternatingFaceMapComplex.obj

@[simp]
theorem obj_X (X : SimplicialObject C) (n : â„•) : (AlternatingFaceMapComplex.obj X).X n = X _[n] :=
  rfl
set_option linter.uppercaseLean3 false in
#align algebraic_topology.alternating_face_map_complex.obj_X AlgebraicTopology.AlternatingFaceMapComplex.obj_X

@[simp]
theorem obj_d_eq (X : SimplicialObject C) (n : â„•) :
    (AlternatingFaceMapComplex.obj X).d (n + 1) n = âˆ‘ i : Fin (n + 2), (-1 : â„¤) ^ (i : â„•) â€¢ X.Î´ i :=
  by apply ChainComplex.of_d
     -- ğŸ‰ no goals
#align algebraic_topology.alternating_face_map_complex.obj_d_eq AlgebraicTopology.AlternatingFaceMapComplex.obj_d_eq

variable {X} {Y}

/-- The alternating face map complex, on morphisms -/
def map (f : X âŸ¶ Y) : obj X âŸ¶ obj Y :=
  ChainComplex.ofHom _ _ _ _ _ _ (fun n => f.app (op [n])) fun n => by
    dsimp
    -- âŠ¢ NatTrans.app f (op [n + 1]) â‰« âˆ‘ i : Fin (n + 2), (-1) ^ â†‘i â€¢ SimplicialObjec â€¦
    rw [comp_sum, sum_comp]
    -- âŠ¢ âˆ‘ j : Fin (n + 2), NatTrans.app f (op [n + 1]) â‰« ((-1) ^ â†‘j â€¢ SimplicialObje â€¦
    refine' Finset.sum_congr rfl fun _ _ => _
    -- âŠ¢ NatTrans.app f (op [n + 1]) â‰« ((-1) ^ â†‘xâœÂ¹ â€¢ SimplicialObject.Î´ Y xâœÂ¹) = ((- â€¦
    rw [comp_zsmul, zsmul_comp]
    -- âŠ¢ (-1) ^ â†‘xâœÂ¹ â€¢ NatTrans.app f (op [n + 1]) â‰« SimplicialObject.Î´ Y xâœÂ¹ = (-1)  â€¦
    congr 1
    -- âŠ¢ NatTrans.app f (op [n + 1]) â‰« SimplicialObject.Î´ Y xâœÂ¹ = SimplicialObject.Î´  â€¦
    symm
    -- âŠ¢ SimplicialObject.Î´ X xâœÂ¹ â‰« NatTrans.app f (op [n]) = NatTrans.app f (op [n + â€¦
    apply f.naturality
    -- ğŸ‰ no goals
#align algebraic_topology.alternating_face_map_complex.map AlgebraicTopology.AlternatingFaceMapComplex.map

@[simp]
theorem map_f (f : X âŸ¶ Y) (n : â„•) : (map f).f n = f.app (op [n]) :=
  rfl
#align algebraic_topology.alternating_face_map_complex.map_f AlgebraicTopology.AlternatingFaceMapComplex.map_f

end AlternatingFaceMapComplex

variable (C : Type*) [Category C] [Preadditive C]

/-- The alternating face map complex, as a functor -/
def alternatingFaceMapComplex : SimplicialObject C â¥¤ ChainComplex C â„• where
  obj := AlternatingFaceMapComplex.obj
  map f := AlternatingFaceMapComplex.map f
#align algebraic_topology.alternating_face_map_complex AlgebraicTopology.alternatingFaceMapComplex

variable {C}

@[simp]
theorem alternatingFaceMapComplex_obj_X (X : SimplicialObject C) (n : â„•) :
    ((alternatingFaceMapComplex C).obj X).X n = X _[n] :=
  rfl
set_option linter.uppercaseLean3 false in
#align algebraic_topology.alternating_face_map_complex_obj_X AlgebraicTopology.alternatingFaceMapComplex_obj_X

@[simp]
theorem alternatingFaceMapComplex_obj_d (X : SimplicialObject C) (n : â„•) :
    ((alternatingFaceMapComplex C).obj X).d (n + 1) n = AlternatingFaceMapComplex.objD X n := by
 dsimp only [alternatingFaceMapComplex, AlternatingFaceMapComplex.obj]
 -- âŠ¢ HomologicalComplex.d (ChainComplex.of (fun n => X.obj (op [n])) (Alternating â€¦
 apply ChainComplex.of_d
 -- ğŸ‰ no goals
#align algebraic_topology.alternating_face_map_complex_obj_d AlgebraicTopology.alternatingFaceMapComplex_obj_d

@[simp]
theorem alternatingFaceMapComplex_map_f {X Y : SimplicialObject C} (f : X âŸ¶ Y) (n : â„•) :
    ((alternatingFaceMapComplex C).map f).f n = f.app (op [n]) :=
  rfl
#align algebraic_topology.alternating_face_map_complex_map_f AlgebraicTopology.alternatingFaceMapComplex_map_f

theorem map_alternatingFaceMapComplex {D : Type*} [Category D] [Preadditive D] (F : C â¥¤ D)
    [F.Additive] :
    alternatingFaceMapComplex C â‹™ F.mapHomologicalComplex _ =
      (SimplicialObject.whiskering C D).obj F â‹™ alternatingFaceMapComplex D := by
  apply CategoryTheory.Functor.ext
  -- âŠ¢ autoParam (âˆ€ (X Y : SimplicialObject C) (f : X âŸ¶ Y), (alternatingFaceMapComp â€¦
  Â· intro X Y f
    -- âŠ¢ (alternatingFaceMapComplex C â‹™ Functor.mapHomologicalComplex F (ComplexShape â€¦
    ext n
    -- âŠ¢ HomologicalComplex.Hom.f ((alternatingFaceMapComplex C â‹™ Functor.mapHomologi â€¦
    simp only [Functor.comp_map, HomologicalComplex.comp_f, alternatingFaceMapComplex_map_f,
      Functor.mapHomologicalComplex_map_f, HomologicalComplex.eqToHom_f, eqToHom_refl, comp_id,
      id_comp, SimplicialObject.whiskering_obj_map_app]
  Â· intro X
    -- âŠ¢ (alternatingFaceMapComplex C â‹™ Functor.mapHomologicalComplex F (ComplexShape â€¦
    apply HomologicalComplex.ext
    -- âŠ¢ âˆ€ (i j : â„•), ComplexShape.Rel (ComplexShape.down â„•) i j â†’ HomologicalComplex â€¦
    Â· rintro i j (rfl : j + 1 = i)
      -- âŠ¢ HomologicalComplex.d ((alternatingFaceMapComplex C â‹™ Functor.mapHomologicalC â€¦
      dsimp only [Functor.comp_obj]
      -- âŠ¢ HomologicalComplex.d ((Functor.mapHomologicalComplex F (ComplexShape.down â„•) â€¦
      simp only [Functor.mapHomologicalComplex_obj_d, alternatingFaceMapComplex_obj_d,
        eqToHom_refl, id_comp, comp_id, AlternatingFaceMapComplex.objD, Functor.map_sum,
        Functor.map_zsmul]
      rfl
      -- ğŸ‰ no goals
    Â· ext n
      -- âŠ¢ HomologicalComplex.X ((alternatingFaceMapComplex C â‹™ Functor.mapHomologicalC â€¦
      rfl
      -- ğŸ‰ no goals
#align algebraic_topology.map_alternating_face_map_complex AlgebraicTopology.map_alternatingFaceMapComplex

theorem karoubi_alternatingFaceMapComplex_d (P : Karoubi (SimplicialObject C)) (n : â„•) :
    ((AlternatingFaceMapComplex.obj (KaroubiFunctorCategoryEmbedding.obj P)).d (n + 1) n).f =
      P.p.app (op [n + 1]) â‰« (AlternatingFaceMapComplex.obj P.X).d (n + 1) n := by
  dsimp
  -- âŠ¢ (HomologicalComplex.d (AlternatingFaceMapComplex.obj (KaroubiFunctorCategory â€¦
  simp only [AlternatingFaceMapComplex.obj_d_eq, Karoubi.sum_hom, Preadditive.comp_sum,
    Karoubi.zsmul_hom, Preadditive.comp_zsmul]
  rfl
  -- ğŸ‰ no goals
#align algebraic_topology.karoubi_alternating_face_map_complex_d AlgebraicTopology.karoubi_alternatingFaceMapComplex_d

namespace AlternatingFaceMapComplex

/-- The natural transformation which gives the augmentation of the alternating face map
complex attached to an augmented simplicial object. -/
--@[simps]
def Îµ [Limits.HasZeroObject C] :
    SimplicialObject.Augmented.drop â‹™ AlgebraicTopology.alternatingFaceMapComplex C âŸ¶
      SimplicialObject.Augmented.point â‹™ ChainComplex.singleâ‚€ C where
  app X := by
    refine' (ChainComplex.toSingleâ‚€Equiv _ _).symm _
    -- âŠ¢ { f // HomologicalComplex.d ((SimplicialObject.Augmented.drop â‹™ alternatingF â€¦
    refine' âŸ¨X.hom.app (op [0]), _âŸ©
    -- âŠ¢ HomologicalComplex.d ((SimplicialObject.Augmented.drop â‹™ alternatingFaceMapC â€¦
    dsimp
    -- âŠ¢ HomologicalComplex.d ((alternatingFaceMapComplex C).obj X.left) 1 0 â‰« NatTra â€¦
    rw [alternatingFaceMapComplex_obj_d, objD, Fin.sum_univ_two, Fin.val_zero,
      pow_zero, one_smul, Fin.val_one, pow_one, neg_smul, one_smul, add_comp,
      neg_comp, SimplicialObject.Î´_naturality, SimplicialObject.Î´_naturality]
    apply add_right_neg
    -- ğŸ‰ no goals
  naturality _ _ f := ChainComplex.to_singleâ‚€_ext _ _ (by exact congr_app f.w _)
                                                          -- ğŸ‰ no goals
#align algebraic_topology.alternating_face_map_complex.Îµ AlgebraicTopology.AlternatingFaceMapComplex.Îµ

end AlternatingFaceMapComplex

/-!
## Construction of the natural inclusion of the normalized Moore complex
-/

variable {A : Type*} [Category A] [Abelian A]

/-- The inclusion map of the Moore complex in the alternating face map complex -/
def inclusionOfMooreComplexMap (X : SimplicialObject A) :
    (normalizedMooreComplex A).obj X âŸ¶ (alternatingFaceMapComplex A).obj X := by
  dsimp only [normalizedMooreComplex, NormalizedMooreComplex.obj,
    alternatingFaceMapComplex, AlternatingFaceMapComplex.obj]
  apply ChainComplex.ofHom _ _ _ _ _ _ (fun n => (NormalizedMooreComplex.objX X n).arrow)
  -- âŠ¢ âˆ€ (i : â„•), arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« AlternatingFaceMa â€¦
  /- we have to show the compatibility of the differentials on the alternating
           face map complex with those defined on the normalized Moore complex:
           we first get rid of the terms of the alternating sum that are obviously
           zero on the normalized_Moore_complex -/
  intro i
  -- âŠ¢ arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« AlternatingFaceMapComplex.ob â€¦
  simp only [AlternatingFaceMapComplex.objD, comp_sum]
  -- âŠ¢ âˆ‘ j : Fin (i + 2), arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« ((-1) ^ â†‘ â€¦
  rw [Fin.sum_univ_succ, Fintype.sum_eq_zero]
  -- âŠ¢ arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« ((-1) ^ â†‘0 â€¢ SimplicialObjec â€¦
  swap
  -- âŠ¢ âˆ€ (a : Fin (i + 1)), arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« ((-1) ^ â€¦
  Â· intro j
    -- âŠ¢ arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« ((-1) ^ â†‘(Fin.succ j) â€¢ Simp â€¦
    rw [NormalizedMooreComplex.objX, comp_zsmul,
      â† factorThru_arrow _ _ (finset_inf_arrow_factors Finset.univ _ _ (Finset.mem_univ j)),
      Category.assoc, kernelSubobject_arrow_comp, comp_zero, smul_zero]
  -- finally, we study the remaining term which is induced by X.Î´ 0
  rw [add_zero, Fin.val_zero, pow_zero, one_zsmul]
  -- âŠ¢ arrow (NormalizedMooreComplex.objX X (i + 1)) â‰« SimplicialObject.Î´ X 0 = Nor â€¦
  dsimp [NormalizedMooreComplex.objD, NormalizedMooreComplex.objX]
  -- âŠ¢ arrow (Finset.inf Finset.univ fun k => kernelSubobject (SimplicialObject.Î´ X â€¦
  cases i <;> simp
              -- ğŸ‰ no goals
              -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.inclusion_of_Moore_complex_map AlgebraicTopology.inclusionOfMooreComplexMap

@[simp]
theorem inclusionOfMooreComplexMap_f (X : SimplicialObject A) (n : â„•) :
    (inclusionOfMooreComplexMap X).f n = (NormalizedMooreComplex.objX X n).arrow := by
  dsimp only [inclusionOfMooreComplexMap]
  -- âŠ¢ HomologicalComplex.Hom.f (id (ChainComplex.ofHom (fun n => underlying.obj (N â€¦
  exact ChainComplex.ofHom_f _ _ _ _ _ _ _ _ n
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.inclusion_of_Moore_complex_map_f AlgebraicTopology.inclusionOfMooreComplexMap_f

variable (A)

/-- The inclusion map of the Moore complex in the alternating face map complex,
as a natural transformation -/
@[simps]
def inclusionOfMooreComplex : normalizedMooreComplex A âŸ¶ alternatingFaceMapComplex A
    where app := inclusionOfMooreComplexMap
set_option linter.uppercaseLean3 false in
#align algebraic_topology.inclusion_of_Moore_complex AlgebraicTopology.inclusionOfMooreComplex

namespace AlternatingCofaceMapComplex

variable (X Y : CosimplicialObject C)

/-- The differential on the alternating coface map complex is the alternate
sum of the coface maps -/
@[simp]
def objD (n : â„•) : X.obj [n] âŸ¶ X.obj [n + 1] :=
  âˆ‘ i : Fin (n + 2), (-1 : â„¤) ^ (i : â„•) â€¢ X.Î´ i
#align algebraic_topology.alternating_coface_map_complex.obj_d AlgebraicTopology.AlternatingCofaceMapComplex.objD

theorem d_eq_unop_d (n : â„•) :
    objD X n =
      (AlternatingFaceMapComplex.objD ((cosimplicialSimplicialEquiv C).functor.obj (op X))
          n).unop := by
  simp only [objD, AlternatingFaceMapComplex.objD, unop_sum, unop_zsmul]
  -- âŠ¢ âˆ‘ i : Fin (n + 2), (-1) ^ â†‘i â€¢ CosimplicialObject.Î´ X i = âˆ‘ x : Fin (n + 2), â€¦
  rfl
  -- ğŸ‰ no goals
#align algebraic_topology.alternating_coface_map_complex.d_eq_unop_d AlgebraicTopology.AlternatingCofaceMapComplex.d_eq_unop_d

theorem d_squared (n : â„•) : objD X n â‰« objD X (n + 1) = 0 := by
  simp only [d_eq_unop_d, â† unop_comp, AlternatingFaceMapComplex.d_squared, unop_zero]
  -- ğŸ‰ no goals
#align algebraic_topology.alternating_coface_map_complex.d_squared AlgebraicTopology.AlternatingCofaceMapComplex.d_squared

/-- The alternating coface map complex, on objects -/
def obj : CochainComplex C â„• :=
  CochainComplex.of (fun n => X.obj [n]) (objD X) (d_squared X)
#align algebraic_topology.alternating_coface_map_complex.obj AlgebraicTopology.AlternatingCofaceMapComplex.obj

variable {X} {Y}

/-- The alternating face map complex, on morphisms -/
@[simp]
def map (f : X âŸ¶ Y) : obj X âŸ¶ obj Y :=
  CochainComplex.ofHom _ _ _ _ _ _ (fun n => f.app [n]) fun n => by
    dsimp
    -- âŠ¢ NatTrans.app f [n] â‰« âˆ‘ i : Fin (n + 2), (-1) ^ â†‘i â€¢ CosimplicialObject.Î´ Y i â€¦
    rw [comp_sum, sum_comp]
    -- âŠ¢ âˆ‘ j : Fin (n + 2), NatTrans.app f [n] â‰« ((-1) ^ â†‘j â€¢ CosimplicialObject.Î´ Y  â€¦
    refine' Finset.sum_congr rfl fun x _ => _
    -- âŠ¢ NatTrans.app f [n] â‰« ((-1) ^ â†‘x â€¢ CosimplicialObject.Î´ Y x) = ((-1) ^ â†‘x â€¢ C â€¦
    rw [comp_zsmul, zsmul_comp]
    -- âŠ¢ (-1) ^ â†‘x â€¢ NatTrans.app f [n] â‰« CosimplicialObject.Î´ Y x = (-1) ^ â†‘x â€¢ Cosi â€¦
    congr 1
    -- âŠ¢ NatTrans.app f [n] â‰« CosimplicialObject.Î´ Y x = CosimplicialObject.Î´ X x â‰« N â€¦
    symm
    -- âŠ¢ CosimplicialObject.Î´ X x â‰« NatTrans.app f [n + 1] = NatTrans.app f [n] â‰« Cos â€¦
    apply f.naturality
    -- ğŸ‰ no goals
#align algebraic_topology.alternating_coface_map_complex.map AlgebraicTopology.AlternatingCofaceMapComplex.map

end AlternatingCofaceMapComplex

variable (C)

/-- The alternating coface map complex, as a functor -/
@[simps]
def alternatingCofaceMapComplex : CosimplicialObject C â¥¤ CochainComplex C â„• where
  obj := AlternatingCofaceMapComplex.obj
  map f := AlternatingCofaceMapComplex.map f
#align algebraic_topology.alternating_coface_map_complex AlgebraicTopology.alternatingCofaceMapComplex

end AlgebraicTopology
