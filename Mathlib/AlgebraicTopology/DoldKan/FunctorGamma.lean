/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialObject.Split
import Mathlib.AlgebraicTopology.DoldKan.PInfty

/-!

# Construction of the inverse functor of the Dold-Kan equivalence


In this file, we construct the functor `Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C`
which shall be the inverse functor of the Dold-Kan equivalence in the case of abelian categories,
and more generally pseudoabelian categories.

By definition, when `K` is a chain_complex, `Î“â‚€.obj K` is a simplicial object which
sends `Î” : SimplexCategoryáµ’áµ–` to a certain coproduct indexed by the set
`Splitting.IndexSet Î”` whose elements consists of epimorphisms `e : Î”.unop âŸ¶ Î”'.unop`
(with `Î”' : SimplexCategoryáµ’áµ–`); the summand attached to such an `e` is `K.X Î”'.unop.len`.
By construction, `Î“â‚€.obj K` is a split simplicial object whose splitting is `Î“â‚€.splitting K`.

We also construct `Î“â‚‚ : Karoubi (ChainComplex C â„•) â¥¤ Karoubi (SimplicialObject C)`
which shall be an equivalence for any additive category `C`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits SimplexCategory
  SimplicialObject Opposite CategoryTheory.Idempotents Simplicial DoldKan

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C] (K K' : ChainComplex C â„•) (f : K âŸ¶ K')
  {Î” Î”' Î”'' : SimplexCategory}

/-- `IsÎ´â‚€ i` is a simple condition used to check whether a monomorphism `i` in
`SimplexCategory` identifies to the coface map `Î´ 0`. -/
@[nolint unusedArguments]
def IsÎ´â‚€ {Î” Î”' : SimplexCategory} (i : Î”' âŸ¶ Î”) [Mono i] : Prop :=
  Î”.len = Î”'.len + 1 âˆ§ i.toOrderHom 0 â‰  0

namespace IsÎ´â‚€

theorem iff {j : â„•} {i : Fin (j + 2)} : IsÎ´â‚€ (SimplexCategory.Î´ i) â†” i = 0 := by
  constructor
  Â· rintro âŸ¨_, hâ‚‚âŸ©
    by_contra h
    exact hâ‚‚ (Fin.succAbove_ne_zero_zero h)
  Â· rintro rfl
    exact âŸ¨rfl, by dsimp; exact Fin.succ_ne_zero (0 : Fin (j + 1))âŸ©

theorem eq_Î´â‚€ {n : â„•} {i : â¦‹nâ¦Œ âŸ¶ â¦‹n + 1â¦Œ} [Mono i] (hi : IsÎ´â‚€ i) :
    i = SimplexCategory.Î´ 0 := by
  obtain âŸ¨j, rflâŸ© := SimplexCategory.eq_Î´_of_mono i
  rw [iff] at hi
  rw [hi]

end IsÎ´â‚€

namespace Î“â‚€

namespace Obj

/-- In the definition of `(Î“â‚€.obj K).obj Î”` as a direct sum indexed by `A : Splitting.IndexSet Î”`,
the summand `summand K Î” A` is `K.X A.1.len`. -/
def summand (Î” : SimplexCategoryáµ’áµ–) (A : Splitting.IndexSet Î”) : C :=
  K.X A.1.unop.len

/-- The functor `Î“â‚€` sends a chain complex `K` to the simplicial object which
sends `Î”` to the direct sum of the objects `summand K Î” A` for all `A : Splitting.IndexSet Î”` -/
def objâ‚‚ (K : ChainComplex C â„•) (Î” : SimplexCategoryáµ’áµ–) [HasFiniteCoproducts C] : C :=
  âˆ fun A : Splitting.IndexSet Î” => summand K Î” A

namespace Termwise

/-- A monomorphism `i : Î”' âŸ¶ Î”` induces a morphism `K.X Î”.len âŸ¶ K.X Î”'.len` which
is the identity if `Î” = Î”'`, the differential on the complex `K` if `i = Î´ 0`, and
zero otherwise. -/
def mapMono (K : ChainComplex C â„•) {Î”' Î” : SimplexCategory} (i : Î”' âŸ¶ Î”) [Mono i] :
    K.X Î”.len âŸ¶ K.X Î”'.len := by
  by_cases Î” = Î”'
  Â· exact eqToHom (by congr)
  Â· by_cases IsÎ´â‚€ i
    Â· exact K.d Î”.len Î”'.len
    Â· exact 0

variable (Î”) in
theorem mapMono_id : mapMono K (ðŸ™ Î”) = ðŸ™ _ := by
  unfold mapMono
  simp only [eqToHom_refl, dite_eq_ite, if_true]

theorem mapMono_Î´â‚€' (i : Î”' âŸ¶ Î”) [Mono i] (hi : IsÎ´â‚€ i) : mapMono K i = K.d Î”.len Î”'.len := by
  unfold mapMono
  suffices Î” â‰  Î”' by
    simp only [dif_neg this, dif_pos hi]
  rintro rfl
  simpa only [left_eq_add, Nat.one_ne_zero] using hi.1

@[simp]
theorem mapMono_Î´â‚€ {n : â„•} : mapMono K (Î´ (0 : Fin (n + 2))) = K.d (n + 1) n :=
  mapMono_Î´â‚€' K _ (by rw [IsÎ´â‚€.iff])

theorem mapMono_eq_zero (i : Î”' âŸ¶ Î”) [Mono i] (hâ‚ : Î” â‰  Î”') (hâ‚‚ : Â¬IsÎ´â‚€ i) : mapMono K i = 0 := by
  unfold mapMono
  rw [Ne] at hâ‚
  split_ifs
  rfl

variable {K K'}

@[reassoc (attr := simp)]
theorem mapMono_naturality (i : Î” âŸ¶ Î”') [Mono i] :
    mapMono K i â‰« f.f Î”.len = f.f Î”'.len â‰« mapMono K' i := by
  unfold mapMono
  split_ifs with h
  Â· subst h
    simp only [id_comp, eqToHom_refl, comp_id]
  Â· rw [HomologicalComplex.Hom.comm]
  Â· rw [zero_comp, comp_zero]

variable (K)

@[reassoc (attr := simp)]
theorem mapMono_comp (i' : Î”'' âŸ¶ Î”') (i : Î”' âŸ¶ Î”) [Mono i'] [Mono i] :
    mapMono K i â‰« mapMono K i' = mapMono K (i' â‰« i) := by
  -- case where i : Î”' âŸ¶ Î” is the identity
  by_cases hâ‚ : Î” = Î”'
  Â· subst hâ‚
    simp only [SimplexCategory.eq_id_of_mono i, comp_id, id_comp, mapMono_id K]
  -- case where i' : Î”'' âŸ¶ Î”' is the identity
  by_cases hâ‚‚ : Î”' = Î”''
  Â· subst hâ‚‚
    simp only [SimplexCategory.eq_id_of_mono i', comp_id, id_comp, mapMono_id K]
  -- then the RHS is always zero
  obtain âŸ¨k, hkâŸ© := Nat.exists_eq_add_of_lt (len_lt_of_mono i hâ‚)
  obtain âŸ¨k', hk'âŸ© := Nat.exists_eq_add_of_lt (len_lt_of_mono i' hâ‚‚)
  have eq : Î”.len = Î”''.len + (k + k' + 2) := by omega
  rw [mapMono_eq_zero K (i' â‰« i) _ _]; rotate_left
  Â· by_contra h
    simp only [left_eq_add, h, add_eq_zero, and_false, reduceCtorEq] at eq
  Â· by_contra h
    simp only [h.1, add_right_inj] at eq
    omega
  -- in all cases, the LHS is also zero, either by definition, or because d â‰« d = 0
  by_cases hâ‚ƒ : IsÎ´â‚€ i
  Â· by_cases hâ‚„ : IsÎ´â‚€ i'
    Â· rw [mapMono_Î´â‚€' K i hâ‚ƒ, mapMono_Î´â‚€' K i' hâ‚„, HomologicalComplex.d_comp_d]
    Â· simp only [mapMono_eq_zero K i' hâ‚‚ hâ‚„, comp_zero]
  Â· simp only [mapMono_eq_zero K i hâ‚ hâ‚ƒ, zero_comp]

end Termwise

variable [HasFiniteCoproducts C]

/-- The simplicial morphism on the simplicial object `Î“â‚€.obj K` induced by
a morphism `Î”' â†’ Î”` in `SimplexCategory` is defined on each summand
associated to an `A : Splitting.IndexSet Î”` in terms of the epi-mono factorisation
of `Î¸ â‰« A.e`. -/
def map (K : ChainComplex C â„•) {Î”' Î” : SimplexCategoryáµ’áµ–} (Î¸ : Î” âŸ¶ Î”') : objâ‚‚ K Î” âŸ¶ objâ‚‚ K Î”' :=
  Sigma.desc fun A =>
    Termwise.mapMono K (image.Î¹ (Î¸.unop â‰« A.e)) â‰« Sigma.Î¹ (summand K Î”') (A.pull Î¸)

@[reassoc]
theorem map_on_summandâ‚€ {Î” Î”' : SimplexCategoryáµ’áµ–} (A : Splitting.IndexSet Î”) {Î¸ : Î” âŸ¶ Î”'}
    {Î”'' : SimplexCategory} {e : Î”'.unop âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1.unop} [Epi e] [Mono i]
    (fac : e â‰« i = Î¸.unop â‰« A.e) :
    Sigma.Î¹ (summand K Î”) A â‰« map K Î¸ =
      Termwise.mapMono K i â‰« Sigma.Î¹ (summand K Î”') (Splitting.IndexSet.mk e) := by
  simp only [map, colimit.Î¹_desc, Cofan.mk_Î¹_app]
  have h := SimplexCategory.image_eq fac
  subst h
  congr
  Â· exact SimplexCategory.image_Î¹_eq fac
  Â· dsimp only [SimplicialObject.Splitting.IndexSet.pull]
    congr
    exact SimplexCategory.factorThruImage_eq fac

@[reassoc]
theorem map_on_summandâ‚€' {Î” Î”' : SimplexCategoryáµ’áµ–} (A : Splitting.IndexSet Î”) (Î¸ : Î” âŸ¶ Î”') :
    Sigma.Î¹ (summand K Î”) A â‰« map K Î¸ =
      Termwise.mapMono K (image.Î¹ (Î¸.unop â‰« A.e)) â‰« Sigma.Î¹ (summand K _) (A.pull Î¸) :=
  map_on_summandâ‚€ K A (A.fac_pull Î¸)

end Obj

variable [HasFiniteCoproducts C]

/-- The functor `Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C`, on objects. -/
@[simps]
def obj (K : ChainComplex C â„•) : SimplicialObject C where
  obj Î” := Obj.objâ‚‚ K Î”
  map Î¸ := Obj.map K Î¸
  map_id Î” := colimit.hom_ext (fun âŸ¨AâŸ© => by
    dsimp
    have fac : A.e â‰« ðŸ™ A.1.unop = (ðŸ™ Î”).unop â‰« A.e := by rw [unop_id, comp_id, id_comp]
    rw [Obj.map_on_summandâ‚€ K A fac, Obj.Termwise.mapMono_id, id_comp]
    dsimp only [Obj.objâ‚‚]
    rw [comp_id]
    rfl)
  map_comp {Î”'' Î”' Î”} Î¸' Î¸ := colimit.hom_ext (fun âŸ¨AâŸ© => by
    have fac : Î¸.unop â‰« Î¸'.unop â‰« A.e = (Î¸' â‰« Î¸).unop â‰« A.e := by rw [unop_comp, assoc]
    rw [â† image.fac (Î¸'.unop â‰« A.e), â† assoc, â†
      image.fac (Î¸.unop â‰« factorThruImage (Î¸'.unop â‰« A.e)), assoc] at fac
    simp only [Obj.map_on_summandâ‚€'_assoc K A Î¸', Obj.map_on_summandâ‚€' K _ Î¸,
      Obj.Termwise.mapMono_comp_assoc, Obj.map_on_summandâ‚€ K A fac]
    rfl)

/-- By construction, the simplicial `Î“â‚€.obj K` is equipped with a splitting. -/
def splitting (K : ChainComplex C â„•) : SimplicialObject.Splitting (Î“â‚€.obj K) where
  N n := K.X n
  Î¹ n := Sigma.Î¹ (Î“â‚€.Obj.summand K (op â¦‹nâ¦Œ)) (Splitting.IndexSet.id (op â¦‹nâ¦Œ))
  isColimit' Î” := IsColimit.ofIsoColimit (colimit.isColimit _) (Cofan.ext (Iso.refl _) (by
      intro A
      dsimp [Splitting.cofan']
      rw [comp_id, Î“â‚€.Obj.map_on_summandâ‚€ K (SimplicialObject.Splitting.IndexSet.id A.1)
        (show A.e â‰« ðŸ™ _ = A.e.op.unop â‰« ðŸ™ _ by rfl), Î“â‚€.Obj.Termwise.mapMono_id]
      dsimp
      rw [id_comp]
      rfl))

@[reassoc]
theorem Obj.map_on_summand {Î” Î”' : SimplexCategoryáµ’áµ–} (A : Splitting.IndexSet Î”) (Î¸ : Î” âŸ¶ Î”')
    {Î”'' : SimplexCategory} {e : Î”'.unop âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1.unop} [Epi e] [Mono i]
    (fac : e â‰« i = Î¸.unop â‰« A.e) :
    ((Î“â‚€.splitting K).cofan Î”).inj A â‰« (Î“â‚€.obj K).map Î¸ =
      Î“â‚€.Obj.Termwise.mapMono K i â‰« ((Î“â‚€.splitting K).cofan Î”').inj (Splitting.IndexSet.mk e) := by
  dsimp [Splitting.cofan]
  change (_ â‰« (Î“â‚€.obj K).map A.e.op) â‰« (Î“â‚€.obj K).map Î¸ = _
  rw [assoc, â† Functor.map_comp]
  dsimp [splitting]
  rw [Î“â‚€.Obj.map_on_summandâ‚€ K (Splitting.IndexSet.id A.1)
    (show e â‰« i = ((Splitting.IndexSet.e A).op â‰« Î¸).unop â‰« ðŸ™ _ by rw [comp_id, fac]; rfl)]
  dsimp only [Splitting.IndexSet.id_fst, Splitting.IndexSet.mk, op_unop, Splitting.IndexSet.e]
  rw [Î“â‚€.Obj.map_on_summandâ‚€ K (Splitting.IndexSet.id (op Î”''))
      (show e â‰« ðŸ™ Î”'' = e.op.unop â‰« ðŸ™ _ by simp), Termwise.mapMono_id]
  dsimp only [Splitting.IndexSet.id_fst]
  rw [id_comp]
  rfl

@[reassoc]
theorem Obj.map_on_summand' {Î” Î”' : SimplexCategoryáµ’áµ–} (A : Splitting.IndexSet Î”) (Î¸ : Î” âŸ¶ Î”') :
    ((splitting K).cofan Î”).inj A â‰« (obj K).map Î¸ =
      Obj.Termwise.mapMono K (image.Î¹ (Î¸.unop â‰« A.e)) â‰«
        ((splitting K).cofan Î”').inj (A.pull Î¸) := by
  apply Obj.map_on_summand
  apply image.fac

@[reassoc]
theorem Obj.mapMono_on_summand_id {Î” Î”' : SimplexCategory} (i : Î”' âŸ¶ Î”) [Mono i] :
    ((splitting K).cofan _).inj (Splitting.IndexSet.id (op Î”)) â‰« (obj K).map i.op =
      Obj.Termwise.mapMono K i â‰« ((splitting K).cofan _).inj (Splitting.IndexSet.id (op Î”')) :=
  Obj.map_on_summand K (Splitting.IndexSet.id (op Î”)) i.op (rfl : ðŸ™ _ â‰« i = i â‰« ðŸ™ _)

@[reassoc]
theorem Obj.map_epi_on_summand_id {Î” Î”' : SimplexCategory} (e : Î”' âŸ¶ Î”) [Epi e] :
    ((Î“â‚€.splitting K).cofan _).inj (Splitting.IndexSet.id (op Î”)) â‰« (Î“â‚€.obj K).map e.op =
      ((Î“â‚€.splitting K).cofan _).inj (Splitting.IndexSet.mk e) := by
  simpa only [Î“â‚€.Obj.map_on_summand K (Splitting.IndexSet.id (op Î”)) e.op
      (rfl : e â‰« ðŸ™ Î” = e â‰« ðŸ™ Î”),
    Î“â‚€.Obj.Termwise.mapMono_id] using id_comp _

/-- The functor `Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C`, on morphisms. -/
@[simps]
def map {K K' : ChainComplex C â„•} (f : K âŸ¶ K') : obj K âŸ¶ obj K' where
  app Î” := (Î“â‚€.splitting K).desc Î” fun A => f.f A.1.unop.len â‰«
    ((Î“â‚€.splitting K').cofan _).inj A
  naturality {Î”' Î”} Î¸ := by
    apply (Î“â‚€.splitting K).hom_ext'
    intro A
    simp only [(splitting K).Î¹_desc_assoc, Obj.map_on_summand'_assoc K _ Î¸, (splitting K).Î¹_desc,
      assoc, Obj.map_on_summand' K' _ Î¸]
    apply Obj.Termwise.mapMono_naturality_assoc

end Î“â‚€

variable [HasFiniteCoproducts C]

/-- The functor `Î“â‚€' : ChainComplex C â„• â¥¤ SimplicialObject.Split C`
that induces `Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C`, which
shall be the inverse functor of the Dold-Kan equivalence for
abelian or pseudo-abelian categories. -/
@[simps]
def Î“â‚€' : ChainComplex C â„• â¥¤ SimplicialObject.Split C where
  obj K := SimplicialObject.Split.mk' (Î“â‚€.splitting K)
  map {K K'} f :=
    { F := Î“â‚€.map f
      f := f.f
      comm := fun n => by
        dsimp
        simp only [â† Splitting.cofan_inj_id, (Î“â‚€.splitting K).Î¹_desc]
        rfl }

/-- The functor `Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C`, which is
the inverse functor of the Dold-Kan equivalence when `C` is an abelian
category, or more generally a pseudoabelian category. -/
@[simps!]
def Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C :=
  Î“â‚€' â‹™ Split.forget _

/-- The extension of `Î“â‚€ : ChainComplex C â„• â¥¤ SimplicialObject C`
on the idempotent completions. It shall be an equivalence of categories
for any additive category `C`. -/
@[simps!]
def Î“â‚‚ : Karoubi (ChainComplex C â„•) â¥¤ Karoubi (SimplicialObject C) :=
  (CategoryTheory.Idempotents.functorExtensionâ‚‚ _ _).obj Î“â‚€

theorem HigherFacesVanish.on_Î“â‚€_summand_id (K : ChainComplex C â„•) (n : â„•) :
    @HigherFacesVanish C _ _ (Î“â‚€.obj K) _ n (n + 1)
      (((Î“â‚€.splitting K).cofan _).inj (Splitting.IndexSet.id (op â¦‹n + 1â¦Œ))) := by
  intro j _
  have eq := Î“â‚€.Obj.mapMono_on_summand_id K (SimplexCategory.Î´ j.succ)
  rw [Î“â‚€.Obj.Termwise.mapMono_eq_zero K, zero_comp] at eq; rotate_left
  Â· intro h
    exact (Nat.succ_ne_self n) (congr_arg SimplexCategory.len h)
  Â· exact fun h => Fin.succ_ne_zero j (by simpa only [IsÎ´â‚€.iff] using h)
  exact eq

@[reassoc (attr := simp)]
theorem PInfty_on_Î“â‚€_splitting_summand_eq_self (K : ChainComplex C â„•) {n : â„•} :
    ((Î“â‚€.splitting K).cofan _).inj (Splitting.IndexSet.id (op â¦‹nâ¦Œ)) â‰«
      (PInfty : K[Î“â‚€.obj K] âŸ¶ _).f n =
      ((Î“â‚€.splitting K).cofan _).inj (Splitting.IndexSet.id (op â¦‹nâ¦Œ)) := by
  rw [PInfty_f]
  rcases n with _ | n
  Â· simpa only [P_f_0_eq] using comp_id _
  Â· exact (HigherFacesVanish.on_Î“â‚€_summand_id K n).comp_P_eq_self

end DoldKan

end AlgebraicTopology
