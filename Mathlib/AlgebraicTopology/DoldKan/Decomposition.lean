/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.PInfty

#align_import algebraic_topology.dold_kan.decomposition from "leanprover-community/mathlib"@"32a7e535287f9c73f2e4d2aef306a39190f0b504"

/-!

# Decomposition of the Q endomorphisms

In this file, we obtain a lemma `decomposition_Q` which expresses
explicitly the projection `(Q q).f (n+1) : X _[n+1] âŸ¶ X _[n+1]`
(`X : SimplicialObject C` with `C` a preadditive category) as
a sum of terms which are postcompositions with degeneracies.

(TODO @joelriou: when `C` is abelian, define the degenerate
subcomplex of the alternating face map complex of `X` and show
that it is a complement to the normalized Moore complex.)

Then, we introduce an ad hoc structure `MorphComponents X n Z` which
can be used in order to define morphisms `X _[n+1] âŸ¶ Z` using the
decomposition provided by `decomposition_Q`. This shall play a critical
role in the proof that the functor
`Nâ‚ : SimplicialObject C â¥¤ Karoubi (ChainComplex C â„•))`
reflects isomorphisms.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category CategoryTheory.Preadditive
  Opposite Simplicial

noncomputable section

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C] {X X' : SimplicialObject C}

/-- In each positive degree, this lemma decomposes the idempotent endomorphism
`Q q` as a sum of morphisms which are postcompositions with suitable degeneracies.
As `Q q` is the complement projection to `P q`, this implies that in the case of
simplicial abelian groups, any $(n+1)$-simplex $x$ can be decomposed as
$x = x' + \sum (i=0}^{q-1} Ïƒ_{n-i}(y_i)$ where $x'$ is in the image of `P q` and
the $y_i$ are in degree $n$. -/
theorem decomposition_Q (n q : â„•) :
    ((Q q).f (n + 1) : X _[n + 1] âŸ¶ X _[n + 1]) =
      âˆ‘ i âˆˆ Finset.filter (fun i : Fin (n + 1) => (i : â„•) < q) Finset.univ,
        (P i).f (n + 1) â‰« X.Î´ i.rev.succ â‰« X.Ïƒ (Fin.rev i) := by
  induction' q with q hq
  Â· simp only [Nat.zero_eq, Q_zero, HomologicalComplex.zero_f_apply, Nat.not_lt_zero,
      Finset.filter_False, Finset.sum_empty]
  Â· by_cases hqn : q + 1 â‰¤ n + 1
    swap
    Â· rw [Q_is_eventually_constant (show n + 1 â‰¤ q by omega), hq]
      congr 1
      ext âŸ¨x, hxâŸ©
      simp only [Nat.succ_eq_add_one, Finset.mem_filter, Finset.mem_univ, true_and]
      omega
    Â· cases' Nat.le.dest (Nat.succ_le_succ_iff.mp hqn) with a ha
      rw [Q_succ, HomologicalComplex.sub_f_apply, HomologicalComplex.comp_f, hq]
      symm
      conv_rhs => rw [sub_eq_add_neg, add_comm]
      let q' : Fin (n + 1) := âŸ¨q, Nat.succ_le_iff.mp hqnâŸ©
      rw [â† @Finset.add_sum_erase _ _ _ _ _ _ q' (by simp)]
      congr
      Â· have hnaq' : n = a + q := by omega
        simp only [Fin.val_mk, (HigherFacesVanish.of_P q n).comp_HÏƒ_eq hnaq',
          q'.rev_eq hnaq', neg_neg]
        rfl
      Â· ext âŸ¨i, hiâŸ©
        simp only [q', Nat.succ_eq_add_one, Nat.lt_succ_iff_lt_or_eq, Finset.mem_univ,
          forall_true_left, Finset.mem_filter, lt_self_iff_false, or_true, and_self, not_true,
          Finset.mem_erase, ne_eq, Fin.mk.injEq, true_and]
        aesop
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.decomposition_Q AlgebraicTopology.DoldKan.decomposition_Q

variable (X)

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- The structure `MorphComponents` is an ad hoc structure that is used in
the proof that `Nâ‚ : SimplicialObject C â¥¤ Karoubi (ChainComplex C â„•))`
reflects isomorphisms. The fields are the data that are needed in order to
construct a morphism `X _[n+1] âŸ¶ Z` (see `Ï†`) using the decomposition of the
identity given by `decomposition_Q n (n+1)`. -/
@[ext]
structure MorphComponents (n : â„•) (Z : C) where
  a : X _[n + 1] âŸ¶ Z
  b : Fin (n + 1) â†’ (X _[n] âŸ¶ Z)
#align algebraic_topology.dold_kan.morph_components AlgebraicTopology.DoldKan.MorphComponents

namespace MorphComponents

variable {X} {n : â„•} {Z Z' : C} (f : MorphComponents X n Z) (g : X' âŸ¶ X) (h : Z âŸ¶ Z')

/-- The morphism `X _[n+1] âŸ¶ Z` associated to `f : MorphComponents X n Z`. -/
def Ï† {Z : C} (f : MorphComponents X n Z) : X _[n + 1] âŸ¶ Z :=
  PInfty.f (n + 1) â‰« f.a + âˆ‘ i : Fin (n + 1), (P i).f (n + 1) â‰« X.Î´ i.rev.succ â‰«
    f.b (Fin.rev i)
#align algebraic_topology.dold_kan.morph_components.Ï† AlgebraicTopology.DoldKan.MorphComponents.Ï†

variable (X n)

/-- the canonical `MorphComponents` whose associated morphism is the identity
(see `F_id`) thanks to `decomposition_Q n (n+1)` -/
@[simps]
def id : MorphComponents X n (X _[n + 1]) where
  a := PInfty.f (n + 1)
  b i := X.Ïƒ i
#align algebraic_topology.dold_kan.morph_components.id AlgebraicTopology.DoldKan.MorphComponents.id

@[simp]
theorem id_Ï† : (id X n).Ï† = ğŸ™ _ := by
  simp only [â† P_add_Q_f (n + 1) (n + 1), Ï†]
  congr 1
  Â· simp only [id, PInfty_f, P_f_idem]
  Â· exact Eq.trans (by congr; simp) (decomposition_Q n (n + 1)).symm
#align algebraic_topology.dold_kan.morph_components.id_Ï† AlgebraicTopology.DoldKan.MorphComponents.id_Ï†

variable {X n}

/-- A `MorphComponents` can be postcomposed with a morphism. -/
@[simps]
def postComp : MorphComponents X n Z' where
  a := f.a â‰« h
  b i := f.b i â‰« h
#align algebraic_topology.dold_kan.morph_components.post_comp AlgebraicTopology.DoldKan.MorphComponents.postComp

@[simp]
theorem postComp_Ï† : (f.postComp h).Ï† = f.Ï† â‰« h := by
  unfold Ï† postComp
  simp only [add_comp, sum_comp, assoc]
#align algebraic_topology.dold_kan.morph_components.post_comp_Ï† AlgebraicTopology.DoldKan.MorphComponents.postComp_Ï†

/-- A `MorphComponents` can be precomposed with a morphism of simplicial objects. -/
@[simps]
def preComp : MorphComponents X' n Z where
  a := g.app (op [n + 1]) â‰« f.a
  b i := g.app (op [n]) â‰« f.b i
#align algebraic_topology.dold_kan.morph_components.pre_comp AlgebraicTopology.DoldKan.MorphComponents.preComp

@[simp]
theorem preComp_Ï† : (f.preComp g).Ï† = g.app (op [n + 1]) â‰« f.Ï† := by
  unfold Ï† preComp
  simp only [PInfty_f, comp_add]
  congr 1
  Â· simp only [P_f_naturality_assoc]
  Â· simp only [comp_sum, P_f_naturality_assoc, SimplicialObject.Î´_naturality_assoc]
#align algebraic_topology.dold_kan.morph_components.pre_comp_Ï† AlgebraicTopology.DoldKan.MorphComponents.preComp_Ï†

end MorphComponents

end DoldKan

end AlgebraicTopology
