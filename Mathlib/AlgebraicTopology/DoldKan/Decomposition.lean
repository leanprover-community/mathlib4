/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.PInfty

/-!

# Decomposition of the Q endomorphisms

In this file, we obtain a lemma `decomposition_Q` which expresses
explicitly the projection `(Q q).f (n+1) : X _‚¶ãn+1‚¶å ‚ü∂ X _‚¶ãn+1‚¶å`
(`X : SimplicialObject C` with `C` a preadditive category) as
a sum of terms which are postcompositions with degeneracies.

(TODO @joelriou: when `C` is abelian, define the degenerate
subcomplex of the alternating face map complex of `X` and show
that it is a complement to the normalized Moore complex.)

Then, we introduce an ad hoc structure `MorphComponents X n Z` which
can be used in order to define morphisms `X _‚¶ãn+1‚¶å ‚ü∂ Z` using the
decomposition provided by `decomposition_Q`. This shall play a critical
role in the proof that the functor
`N‚ÇÅ : SimplicialObject C ‚•§ Karoubi (ChainComplex C ‚Ñï))`
reflects isomorphisms.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category CategoryTheory.Preadditive
  Opposite Simplicial

noncomputable section

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C] {X X' : SimplicialObject C}

/-- In each positive degree, this lemma decomposes the idempotent endomorphism
`Q q` as a sum of morphisms which are postcompositions with suitable degeneracies.
As `Q q` is the complement projection to `P q`, this implies that in the case of
simplicial abelian groups, any $(n+1)$-simplex $x$ can be decomposed as
$x = x' + \sum (i=0}^{q-1} œÉ_{n-i}(y_i)$ where $x'$ is in the image of `P q` and
the $y_i$ are in degree $n$. -/
theorem decomposition_Q (n q : ‚Ñï) :
    ((Q q).f (n + 1) : X _‚¶ãn + 1‚¶å ‚ü∂ X _‚¶ãn + 1‚¶å) =
      ‚àë i : Fin (n + 1) with i.val < q, (P i).f (n + 1) ‚â´ X.Œ¥ i.rev.succ ‚â´ X.œÉ (Fin.rev i) := by
  induction q with
  | zero =>
    simp only [Q_zero, HomologicalComplex.zero_f_apply, Nat.not_lt_zero,
      Finset.filter_false, Finset.sum_empty]
  | succ q hq =>
    by_cases hqn : q + 1 ‚â§ n + 1
    swap
    ¬∑ rw [Q_is_eventually_constant (show n + 1 ‚â§ q by cutsat), hq]
      congr 1
      ext ‚ü®x, hx‚ü©
      simp_rw [Finset.mem_filter_univ]
      cutsat
    ¬∑ obtain ‚ü®a, ha‚ü© := Nat.le.dest (Nat.succ_le_succ_iff.mp hqn)
      rw [Q_succ, HomologicalComplex.sub_f_apply, HomologicalComplex.comp_f, hq]
      symm
      conv_rhs => rw [sub_eq_add_neg, add_comm]
      let q' : Fin (n + 1) := ‚ü®q, Nat.succ_le_iff.mp hqn‚ü©
      rw [‚Üê @Finset.add_sum_erase _ _ _ _ _ _ q' (by simp [q'])]
      congr
      ¬∑ have hnaq' : n = a + q := by omega
        simp only [(HigherFacesVanish.of_P q n).comp_HœÉ_eq hnaq', q'.rev_eq hnaq', neg_neg]
        rfl
      ¬∑ ext ‚ü®i, hi‚ü©
        simp_rw [Finset.mem_erase, Finset.mem_filter_univ, q', ne_eq, Fin.mk.injEq]
        cutsat

variable (X)

/-- The structure `MorphComponents` is an ad hoc structure that is used in
the proof that `N‚ÇÅ : SimplicialObject C ‚•§ Karoubi (ChainComplex C ‚Ñï))`
reflects isomorphisms. The fields are the data that are needed in order to
construct a morphism `X _‚¶ãn+1‚¶å ‚ü∂ Z` (see `œÜ`) using the decomposition of the
identity given by `decomposition_Q n (n+1)`. -/
@[ext]
structure MorphComponents (n : ‚Ñï) (Z : C) where
  a : X _‚¶ãn + 1‚¶å ‚ü∂ Z
  b : Fin (n + 1) ‚Üí (X _‚¶ãn‚¶å ‚ü∂ Z)

namespace MorphComponents

variable {X} {n : ‚Ñï} {Z Z' : C} (f : MorphComponents X n Z) (g : X' ‚ü∂ X) (h : Z ‚ü∂ Z')

/-- The morphism `X _‚¶ãn+1‚¶å ‚ü∂ Z` associated to `f : MorphComponents X n Z`. -/
def œÜ {Z : C} (f : MorphComponents X n Z) : X _‚¶ãn + 1‚¶å ‚ü∂ Z :=
  PInfty.f (n + 1) ‚â´ f.a + ‚àë i : Fin (n + 1), (P i).f (n + 1) ‚â´ X.Œ¥ i.rev.succ ‚â´
    f.b (Fin.rev i)

variable (X n)

/-- the canonical `MorphComponents` whose associated morphism is the identity
(see `F_id`) thanks to `decomposition_Q n (n+1)` -/
@[simps]
def id : MorphComponents X n (X _‚¶ãn + 1‚¶å) where
  a := PInfty.f (n + 1)
  b i := X.œÉ i

@[simp]
theorem id_œÜ : (id X n).œÜ = ùüô _ := by
  simp only [‚Üê P_add_Q_f (n + 1) (n + 1), œÜ]
  congr 1
  ¬∑ simp only [id, PInfty_f, P_f_idem]
  ¬∑ exact Eq.trans (by simp) (decomposition_Q n (n + 1)).symm

variable {X n}

/-- A `MorphComponents` can be postcomposed with a morphism. -/
@[simps]
def postComp : MorphComponents X n Z' where
  a := f.a ‚â´ h
  b i := f.b i ‚â´ h

@[simp]
theorem postComp_œÜ : (f.postComp h).œÜ = f.œÜ ‚â´ h := by
  unfold œÜ postComp
  simp only [add_comp, sum_comp, assoc]

/-- A `MorphComponents` can be precomposed with a morphism of simplicial objects. -/
@[simps]
def preComp : MorphComponents X' n Z where
  a := g.app (op ‚¶ãn + 1‚¶å) ‚â´ f.a
  b i := g.app (op ‚¶ãn‚¶å) ‚â´ f.b i

@[simp]
theorem preComp_œÜ : (f.preComp g).œÜ = g.app (op ‚¶ãn + 1‚¶å) ‚â´ f.œÜ := by
  unfold œÜ preComp
  simp only [PInfty_f, comp_add]
  congr 1
  ¬∑ simp only [P_f_naturality_assoc]
  ¬∑ simp only [comp_sum, P_f_naturality_assoc, SimplicialObject.Œ¥_naturality_assoc]

end MorphComponents

end DoldKan

end AlgebraicTopology
