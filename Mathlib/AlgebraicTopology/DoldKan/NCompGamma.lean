/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.GammaCompN
import Mathlib.AlgebraicTopology.DoldKan.NReflectsIso

/-! The unit isomorphism of the Dold-Kan equivalence

In order to construct the unit isomorphism of the Dold-Kan equivalence,
we first construct natural transformations
`Î“â‚‚Nâ‚.natTrans : Nâ‚ â‹™ Î“â‚‚ âŸ¶ toKaroubi (SimplicialObject C)` and
`Î“â‚‚Nâ‚‚.natTrans : Nâ‚‚ â‹™ Î“â‚‚ âŸ¶ ğŸ­ (SimplicialObject C)`.
It is then shown that `Î“â‚‚Nâ‚‚.natTrans` is an isomorphism by using
that it becomes an isomorphism after the application of the functor
`Nâ‚‚ : Karoubi (SimplicialObject C) â¥¤ Karoubi (ChainComplex C â„•)`
which reflects isomorphisms.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits CategoryTheory.Idempotents
  SimplexCategory Opposite SimplicialObject Simplicial DoldKan

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C]

theorem PInfty_comp_map_mono_eq_zero (X : SimplicialObject C) {n : â„•} {Î”' : SimplexCategory}
    (i : Î”' âŸ¶ â¦‹nâ¦Œ) [hi : Mono i] (hâ‚ : Î”'.len â‰  n) (hâ‚‚ : Â¬IsÎ´â‚€ i) :
    PInfty.f n â‰« X.map i.op = 0 := by
  induction' Î”' using SimplexCategory.rec with m
  obtain âŸ¨k, hkâŸ© := Nat.exists_eq_add_of_lt (len_lt_of_mono i fun h => by
        rw [â† h] at hâ‚
        exact hâ‚ rfl)
  simp only [len_mk] at hk
  rcases k with _|k
  Â· change n = m + 1 at hk
    subst hk
    obtain âŸ¨j, rflâŸ© := eq_Î´_of_mono i
    rw [IsÎ´â‚€.iff] at hâ‚‚
    have hâ‚ƒ : 1 â‰¤ (j : â„•) := by
      by_contra h
      exact hâ‚‚ (by simpa only [Fin.ext_iff, not_le, Nat.lt_one_iff] using h)
    exact (HigherFacesVanish.of_P (m + 1) m).comp_Î´_eq_zero j hâ‚‚ (by omega)
  Â· simp only [Nat.succ_eq_add_one, â† add_assoc] at hk
    clear hâ‚‚ hi
    subst hk
    obtain âŸ¨jâ‚ : Fin (_ + 1), i, rflâŸ© :=
      eq_comp_Î´_of_not_surjective i fun h => by
        have h' := len_le_of_epi (SimplexCategory.epi_iff_surjective.2 h)
        dsimp at h'
        omega
    obtain âŸ¨jâ‚‚, i, rflâŸ© :=
      eq_comp_Î´_of_not_surjective i fun h => by
        have h' := len_le_of_epi (SimplexCategory.epi_iff_surjective.2 h)
        dsimp at h'
        omega
    by_cases hjâ‚ : jâ‚ = 0
    Â· subst hjâ‚
      rw [assoc, â† SimplexCategory.Î´_comp_Î´'' (Fin.zero_le _)]
      simp only [op_comp, X.map_comp, assoc, PInfty_f]
      erw [(HigherFacesVanish.of_P _ _).comp_Î´_eq_zero_assoc _ jâ‚‚.succ_ne_zero, zero_comp]
      simp only [Nat.succ_eq_add_one, Nat.add, Fin.succ]
      omega
    Â· simp only [op_comp, X.map_comp, assoc, PInfty_f]
      erw [(HigherFacesVanish.of_P _ _).comp_Î´_eq_zero_assoc _ hjâ‚, zero_comp]
      by_contra
      exact hjâ‚ (by simp only [Fin.ext_iff, Fin.val_zero]; omega)

@[reassoc]
theorem Î“â‚€_obj_termwise_mapMono_comp_PInfty (X : SimplicialObject C) {Î” Î”' : SimplexCategory}
    (i : Î” âŸ¶ Î”') [Mono i] :
    Î“â‚€.Obj.Termwise.mapMono (AlternatingFaceMapComplex.obj X) i â‰« PInfty.f Î”.len =
      PInfty.f Î”'.len â‰« X.map i.op := by
  induction' Î” using SimplexCategory.rec with n
  induction' Î”' using SimplexCategory.rec with n'
  dsimp
  -- We start with the case `i` is an identity
  by_cases h : n = n'
  Â· subst h
    simp only [SimplexCategory.eq_id_of_mono i, Î“â‚€.Obj.Termwise.mapMono_id, op_id, X.map_id]
    dsimp
    simp only [id_comp, comp_id]
  by_cases hi : IsÎ´â‚€ i
  -- The case `i = Î´ 0`
  Â· have h' : n' = n + 1 := hi.left
    subst h'
    simp only [Î“â‚€.Obj.Termwise.mapMono_Î´â‚€' _ i hi]
    dsimp
    rw [â† PInfty.comm _ n, AlternatingFaceMapComplex.obj_d_eq]
    simp only [eq_self_iff_true, id_comp, if_true, Preadditive.comp_sum]
    rw [Finset.sum_eq_single (0 : Fin (n + 2))]
    rotate_left
    Â· intro b _ hb
      rw [Preadditive.comp_zsmul, SimplicialObject.Î´,
        PInfty_comp_map_mono_eq_zero X (SimplexCategory.Î´ b) h
          (by
            rw [IsÎ´â‚€.iff]
            exact hb),
        zsmul_zero]
    Â· simp only [Finset.mem_univ, not_true, IsEmpty.forall_iff]
    Â· simp only [hi.eq_Î´â‚€, Fin.val_zero, pow_zero, one_zsmul]
      rfl
  -- The case `i â‰  Î´ 0`
  Â· rw [Î“â‚€.Obj.Termwise.mapMono_eq_zero _ i _ hi, zero_comp]
    swap
    Â· by_contra h'
      exact h (congr_arg SimplexCategory.len h'.symm)
    rw [PInfty_comp_map_mono_eq_zero]
    Â· exact h
    Â· by_contra h'
      exact hi h'

variable [HasFiniteCoproducts C]

namespace Î“â‚‚Nâ‚

/-- The natural transformation `Nâ‚ â‹™ Î“â‚‚ âŸ¶ toKaroubi (SimplicialObject C)`. -/
@[simps]
def natTrans : (Nâ‚ : SimplicialObject C â¥¤ _) â‹™ Î“â‚‚ âŸ¶ toKaroubi _ where
  app X :=
    { f :=
        { app := fun Î” => (Î“â‚€.splitting K[X]).desc Î” fun A => PInfty.f A.1.unop.len â‰« X.map A.e.op
          naturality := fun Î” Î”' Î¸ => by
            apply (Î“â‚€.splitting K[X]).hom_ext'
            intro A
            change _ â‰« (Î“â‚€.obj K[X]).map Î¸ â‰« _ = _
            simp only [Splitting.Î¹_desc_assoc, assoc, Î“â‚€.Obj.map_on_summand'_assoc,
              Splitting.Î¹_desc]
            erw [Î“â‚€_obj_termwise_mapMono_comp_PInfty_assoc X (image.Î¹ (Î¸.unop â‰« A.e))]
            dsimp only [toKaroubi]
            simp only [â† X.map_comp]
            congr 2
            simp only [eqToHom_refl, id_comp, comp_id, â† op_comp]
            exact Quiver.Hom.unop_inj (A.fac_pull Î¸) }
      comm := by
        apply (Î“â‚€.splitting K[X]).hom_ext
        intro n
        dsimp [Nâ‚]
        simp only [â† Splitting.cofan_inj_id, Splitting.Î¹_desc, comp_id, Splitting.Î¹_desc_assoc,
          assoc, PInfty_f_idem_assoc] }
  naturality {X Y} f := by
    ext1
    apply (Î“â‚€.splitting K[X]).hom_ext
    intro n
    dsimp [Nâ‚, toKaroubi]
    simp only [â† Splitting.cofan_inj_id, Splitting.Î¹_desc, Splitting.Î¹_desc_assoc, assoc,
      PInfty_f_idem_assoc, Karoubi.comp_f, NatTrans.comp_app, Î“â‚‚_map_f_app,
      HomologicalComplex.comp_f, AlternatingFaceMapComplex.map_f, PInfty_f_naturality_assoc,
      NatTrans.naturality, Splitting.IndexSet.id_fst, unop_op, len_mk]

end Î“â‚‚Nâ‚

/-- The compatibility isomorphism relating `Nâ‚‚ â‹™ Î“â‚‚` and `Nâ‚ â‹™ Î“â‚‚`. -/
@[simps! hom_app inv_app]
def Î“â‚‚Nâ‚‚ToKaroubiIso : toKaroubi (SimplicialObject C) â‹™ Nâ‚‚ â‹™ Î“â‚‚ â‰… Nâ‚ â‹™ Î“â‚‚ :=
  (Functor.associator _ _ _).symm â‰ªâ‰« isoWhiskerRight toKaroubiCompNâ‚‚IsoNâ‚ Î“â‚‚

namespace Î“â‚‚Nâ‚‚

/-- The natural transformation `Nâ‚‚ â‹™ Î“â‚‚ âŸ¶ ğŸ­ (SimplicialObject C)`. -/
def natTrans : (Nâ‚‚ : Karoubi (SimplicialObject C) â¥¤ _) â‹™ Î“â‚‚ âŸ¶ ğŸ­ _ :=
  ((whiskeringLeft _ _ _).obj (toKaroubi (SimplicialObject C))).preimage
    (Î“â‚‚Nâ‚‚ToKaroubiIso.hom â‰« Î“â‚‚Nâ‚.natTrans)

theorem natTrans_app_f_app (P : Karoubi (SimplicialObject C)) :
    Î“â‚‚Nâ‚‚.natTrans.app P =
      (Nâ‚‚ â‹™ Î“â‚‚).map P.decompId_i â‰«
        (Î“â‚‚Nâ‚‚ToKaroubiIso.hom â‰« Î“â‚‚Nâ‚.natTrans).app P.X â‰« P.decompId_p := by
  dsimp only [natTrans]
  simp only [whiskeringLeft_obj_preimage_app, Functor.id_map, assoc]

end Î“â‚‚Nâ‚‚

theorem compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚_natTrans (X : SimplicialObject C) :
    Î“â‚‚Nâ‚.natTrans.app X =
      (Î“â‚‚Nâ‚‚ToKaroubiIso.app X).inv â‰«
        Î“â‚‚Nâ‚‚.natTrans.app ((toKaroubi (SimplicialObject C)).obj X) := by
  rw [Î“â‚‚Nâ‚‚.natTrans_app_f_app]
  dsimp only [Karoubi.decompId_i_toKaroubi, Karoubi.decompId_p_toKaroubi, Functor.comp_map,
    NatTrans.comp_app]
  rw [Nâ‚‚.map_id, Î“â‚‚.map_id, Iso.app_inv]
  dsimp only [toKaroubi]
  erw [id_comp]
  rw [comp_id, Iso.inv_hom_id_app_assoc]

theorem identity_Nâ‚‚_objectwise (P : Karoubi (SimplicialObject C)) :
    (Nâ‚‚Î“â‚‚.inv.app (Nâ‚‚.obj P) : Nâ‚‚.obj P âŸ¶ Nâ‚‚.obj (Î“â‚‚.obj (Nâ‚‚.obj P))) â‰«
    Nâ‚‚.map (Î“â‚‚Nâ‚‚.natTrans.app P) = ğŸ™ (Nâ‚‚.obj P) := by
  ext n
  have eqâ‚ : (Nâ‚‚Î“â‚‚.inv.app (Nâ‚‚.obj P)).f.f n = PInfty.f n â‰« P.p.app (op â¦‹nâ¦Œ) â‰«
      ((Î“â‚€.splitting (Nâ‚‚.obj P).X).cofan _).inj (Splitting.IndexSet.id (op â¦‹nâ¦Œ)) := by
    simp only [Nâ‚‚Î“â‚‚_inv_app_f_f, Nâ‚‚_obj_p_f, assoc]
  have eqâ‚‚ : ((Î“â‚€.splitting (Nâ‚‚.obj P).X).cofan _).inj (Splitting.IndexSet.id (op â¦‹nâ¦Œ)) â‰«
      (Nâ‚‚.map (Î“â‚‚Nâ‚‚.natTrans.app P)).f.f n = PInfty.f n â‰« P.p.app (op â¦‹nâ¦Œ) := by
    dsimp
    rw [PInfty_on_Î“â‚€_splitting_summand_eq_self_assoc, Î“â‚‚Nâ‚‚.natTrans_app_f_app]
    dsimp
    rw [Î“â‚‚Nâ‚‚ToKaroubiIso_hom_app, assoc, Splitting.Î¹_desc_assoc, assoc, assoc]
    dsimp [toKaroubi]
    rw [Splitting.Î¹_desc_assoc]
    simp [Splitting.IndexSet.e]
  simp only [Karoubi.comp_f, HomologicalComplex.comp_f, Karoubi.id_f, Nâ‚‚_obj_p_f, assoc,
    eqâ‚, eqâ‚‚, PInfty_f_naturality_assoc, app_idem, PInfty_f_idem_assoc]

theorem identity_Nâ‚‚ :
    (ğŸ™ (Nâ‚‚ : Karoubi (SimplicialObject C) â¥¤ _) â—« Nâ‚‚Î“â‚‚.inv) â‰«
    (Functor.associator _ _ _).inv â‰« Î“â‚‚Nâ‚‚.natTrans â—« ğŸ™ (@Nâ‚‚ C _ _) = ğŸ™ Nâ‚‚ := by
  ext P : 2
  dsimp only [NatTrans.comp_app, NatTrans.hcomp_app, Functor.comp_map, Functor.associator,
    NatTrans.id_app, Functor.comp_obj]
  rw [Î“â‚‚.map_id, Nâ‚‚.map_id, comp_id, id_comp, id_comp, identity_Nâ‚‚_objectwise P]

instance : IsIso (Î“â‚‚Nâ‚‚.natTrans : (Nâ‚‚ : Karoubi (SimplicialObject C) â¥¤ _) â‹™ _ âŸ¶ _) := by
  have : âˆ€ P : Karoubi (SimplicialObject C), IsIso (Î“â‚‚Nâ‚‚.natTrans.app P) := by
    intro P
    have : IsIso (Nâ‚‚.map (Î“â‚‚Nâ‚‚.natTrans.app P)) := by
      have h := identity_Nâ‚‚_objectwise P
      dsimp only [Functor.id_obj, Functor.comp_obj] at h
      rw [hom_comp_eq_id] at h
      rw [h]
      infer_instance
    exact isIso_of_reflects_iso _ Nâ‚‚
  apply NatIso.isIso_of_isIso_app

instance : IsIso (Î“â‚‚Nâ‚.natTrans : (Nâ‚ : SimplicialObject C â¥¤ _) â‹™ _ âŸ¶ _) := by
  have : âˆ€ X : SimplicialObject C, IsIso (Î“â‚‚Nâ‚.natTrans.app X) := by
    intro X
    rw [compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚_natTrans]
    infer_instance
  apply NatIso.isIso_of_isIso_app

/-- The unit isomorphism of the Dold-Kan equivalence. -/
@[simps! inv]
def Î“â‚‚Nâ‚‚ : ğŸ­ _ â‰… (Nâ‚‚ : Karoubi (SimplicialObject C) â¥¤ _) â‹™ Î“â‚‚ :=
  (asIso Î“â‚‚Nâ‚‚.natTrans).symm

/-- The natural isomorphism `toKaroubi (SimplicialObject C) â‰… Nâ‚ â‹™ Î“â‚‚`. -/
@[simps! inv]
def Î“â‚‚Nâ‚ : toKaroubi _ â‰… (Nâ‚ : SimplicialObject C â¥¤ _) â‹™ Î“â‚‚ :=
  (asIso Î“â‚‚Nâ‚.natTrans).symm

end DoldKan

end AlgebraicTopology
