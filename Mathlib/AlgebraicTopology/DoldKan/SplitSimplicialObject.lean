/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SplitSimplicialObject
import Mathlib.AlgebraicTopology.DoldKan.Degeneracies
import Mathlib.AlgebraicTopology.DoldKan.FunctorN

#align_import algebraic_topology.dold_kan.split_simplicial_object from "leanprover-community/mathlib"@"32a7e535287f9c73f2e4d2aef306a39190f0b504"

/-!

# Split simplicial objects in preadditive categories

In this file we define a functor `nondegComplex : SimplicialObject.Split C ‚•§ ChainComplex C ‚Ñï`
when `C` is a preadditive category with finite coproducts, and get an isomorphism
`toKaroubiNondegComplexFunctorIsoN‚ÇÅ : nondegComplex ‚ãô toKaroubi _ ‚âÖ forget C ‚ãô DoldKan.N‚ÇÅ`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Limits CategoryTheory.Category CategoryTheory.Preadditive
  CategoryTheory.Idempotents Opposite AlgebraicTopology AlgebraicTopology.DoldKan
  Simplicial DoldKan

namespace SimplicialObject

namespace Splitting

variable {C : Type*} [Category C] {X : SimplicialObject C}
  (s : Splitting X)

/-- The projection on a summand of the coproduct decomposition given
by a splitting of a simplicial object. -/
noncomputable def œÄSummand [HasZeroMorphisms C] {Œî : SimplexCategory·µí·µñ} (A : IndexSet Œî) :
    X.obj Œî ‚ü∂ s.N A.1.unop.len :=
  s.desc Œî (fun B => by
    by_cases h : B = A
    ¬∑ exact eqToHom (by subst h; rfl)
    ¬∑ exact 0)
#align simplicial_object.splitting.œÄ_summand SimplicialObject.Splitting.œÄSummand

@[reassoc (attr := simp)]
theorem cofan_inj_œÄSummand_eq_id [HasZeroMorphisms C] {Œî : SimplexCategory·µí·µñ} (A : IndexSet Œî) :
    (s.cofan Œî).inj A ‚â´ s.œÄSummand A = ùüô _ := by
  simp [œÄSummand]
#align simplicial_object.splitting.Œπ_œÄ_summand_eq_id SimplicialObject.Splitting.cofan_inj_œÄSummand_eq_id

@[reassoc (attr := simp)]
theorem cofan_inj_œÄSummand_eq_zero [HasZeroMorphisms C] {Œî : SimplexCategory·µí·µñ} (A B : IndexSet Œî)
    (h : B ‚â† A) : (s.cofan Œî).inj A ‚â´ s.œÄSummand B = 0 := by
  dsimp [œÄSummand]
  rw [Œπ_desc, dif_neg h.symm]
#align simplicial_object.splitting.Œπ_œÄ_summand_eq_zero SimplicialObject.Splitting.cofan_inj_œÄSummand_eq_zero

variable [Preadditive C]

theorem decomposition_id (Œî : SimplexCategory·µí·µñ) :
    ùüô (X.obj Œî) = ‚àë A : IndexSet Œî, s.œÄSummand A ‚â´ (s.cofan Œî).inj A := by
  apply s.hom_ext'
  intro A
  dsimp
  erw [comp_id, comp_sum, Finset.sum_eq_single A, cofan_inj_œÄSummand_eq_id_assoc]
  ¬∑ intro B _ h‚ÇÇ
    rw [s.cofan_inj_œÄSummand_eq_zero_assoc _ _ h‚ÇÇ, zero_comp]
  ¬∑ simp
#align simplicial_object.splitting.decomposition_id SimplicialObject.Splitting.decomposition_id

@[reassoc (attr := simp)]
theorem œÉ_comp_œÄSummand_id_eq_zero {n : ‚Ñï} (i : Fin (n + 1)) :
    X.œÉ i ‚â´ s.œÄSummand (IndexSet.id (op [n + 1])) = 0 := by
  apply s.hom_ext'
  intro A
  dsimp only [SimplicialObject.œÉ]
  rw [comp_zero, s.cofan_inj_epi_naturality_assoc A (SimplexCategory.œÉ i).op,
    cofan_inj_œÄSummand_eq_zero]
  rw [ne_comm]
  change ¬¨(A.epiComp (SimplexCategory.œÉ i).op).EqId
  rw [IndexSet.eqId_iff_len_eq]
  have h := SimplexCategory.len_le_of_epi (inferInstance : Epi A.e)
  dsimp at h ‚ä¢
  omega
#align simplicial_object.splitting.œÉ_comp_œÄ_summand_id_eq_zero SimplicialObject.Splitting.œÉ_comp_œÄSummand_id_eq_zero

/-- If a simplicial object `X` in an additive category is split,
then `PInfty` vanishes on all the summands of `X _[n]` which do
not correspond to the identity of `[n]`. -/
theorem cofan_inj_comp_PInfty_eq_zero {X : SimplicialObject C} (s : SimplicialObject.Splitting X)
    {n : ‚Ñï} (A : SimplicialObject.Splitting.IndexSet (op [n])) (hA : ¬¨A.EqId) :
    (s.cofan _).inj A ‚â´ PInfty.f n = 0 := by
  rw [SimplicialObject.Splitting.IndexSet.eqId_iff_mono] at hA
  rw [SimplicialObject.Splitting.cofan_inj_eq, assoc, degeneracy_comp_PInfty X n A.e hA, comp_zero]
set_option linter.uppercaseLean3 false in
#align simplicial_object.splitting.Œπ_summand_comp_P_infty_eq_zero SimplicialObject.Splitting.cofan_inj_comp_PInfty_eq_zero

theorem comp_PInfty_eq_zero_iff {Z : C} {n : ‚Ñï} (f : Z ‚ü∂ X _[n]) :
    f ‚â´ PInfty.f n = 0 ‚Üî f ‚â´ s.œÄSummand (IndexSet.id (op [n])) = 0 := by
  constructor
  ¬∑ intro h
    rcases n with _|n
    ¬∑ dsimp at h
      rw [comp_id] at h
      rw [h, zero_comp]
    ¬∑ have h' := f ‚â´= PInfty_f_add_QInfty_f (n + 1)
      dsimp at h'
      rw [comp_id, comp_add, h, zero_add] at h'
      rw [‚Üê h', assoc, QInfty_f, decomposition_Q, Preadditive.sum_comp, Preadditive.comp_sum,
        Finset.sum_eq_zero]
      intro i _
      simp only [assoc, œÉ_comp_œÄSummand_id_eq_zero, comp_zero]
  ¬∑ intro h
    rw [‚Üê comp_id f, assoc, s.decomposition_id, Preadditive.sum_comp, Preadditive.comp_sum,
      Fintype.sum_eq_zero]
    intro A
    by_cases hA : A.EqId
    ¬∑ dsimp at hA
      subst hA
      rw [assoc, reassoc_of% h, zero_comp]
    ¬∑ simp only [assoc, s.cofan_inj_comp_PInfty_eq_zero A hA, comp_zero]
set_option linter.uppercaseLean3 false in
#align simplicial_object.splitting.comp_P_infty_eq_zero_iff SimplicialObject.Splitting.comp_PInfty_eq_zero_iff

@[reassoc (attr := simp)]
theorem PInfty_comp_œÄSummand_id (n : ‚Ñï) :
    PInfty.f n ‚â´ s.œÄSummand (IndexSet.id (op [n])) = s.œÄSummand (IndexSet.id (op [n])) := by
  conv_rhs => rw [‚Üê id_comp (s.œÄSummand _)]
  symm
  rw [‚Üê sub_eq_zero, ‚Üê sub_comp, ‚Üê comp_PInfty_eq_zero_iff, sub_comp, id_comp, PInfty_f_idem,
    sub_self]
set_option linter.uppercaseLean3 false in
#align simplicial_object.splitting.P_infty_comp_œÄ_summand_id SimplicialObject.Splitting.PInfty_comp_œÄSummand_id

@[reassoc (attr := simp)]
theorem œÄSummand_comp_cofan_inj_id_comp_PInfty_eq_PInfty (n : ‚Ñï) :
    s.œÄSummand (IndexSet.id (op [n])) ‚â´ (s.cofan _).inj (IndexSet.id (op [n])) ‚â´ PInfty.f n =
      PInfty.f n := by
  conv_rhs => rw [‚Üê id_comp (PInfty.f n)]
  erw [s.decomposition_id, Preadditive.sum_comp]
  rw [Fintype.sum_eq_single (IndexSet.id (op [n])), assoc]
  rintro A (hA : ¬¨A.EqId)
  rw [assoc, s.cofan_inj_comp_PInfty_eq_zero A hA, comp_zero]
set_option linter.uppercaseLean3 false in
#align simplicial_object.splitting.œÄ_summand_comp_Œπ_summand_comp_P_infty_eq_P_infty SimplicialObject.Splitting.œÄSummand_comp_cofan_inj_id_comp_PInfty_eq_PInfty

/-- The differentials `s.d i j : s.N i ‚ü∂ s.N j` on nondegenerate simplices of a split
simplicial object are induced by the differentials on the alternating face map complex. -/
@[simp]
noncomputable def d (i j : ‚Ñï) : s.N i ‚ü∂ s.N j :=
  (s.cofan _).inj (IndexSet.id (op [i])) ‚â´ K[X].d i j ‚â´ s.œÄSummand (IndexSet.id (op [j]))
#align simplicial_object.splitting.d SimplicialObject.Splitting.d

theorem ŒπSummand_comp_d_comp_œÄSummand_eq_zero (j k : ‚Ñï) (A : IndexSet (op [j])) (hA : ¬¨A.EqId) :
    (s.cofan _).inj A ‚â´ K[X].d j k ‚â´ s.œÄSummand (IndexSet.id (op [k])) = 0 := by
  rw [A.eqId_iff_mono] at hA
  rw [‚Üê assoc, ‚Üê s.comp_PInfty_eq_zero_iff, assoc, ‚Üê PInfty.comm j k, s.cofan_inj_eq, assoc,
    degeneracy_comp_PInfty_assoc X j A.e hA, zero_comp, comp_zero]
#align simplicial_object.splitting.Œπ_summand_comp_d_comp_œÄ_summand_eq_zero SimplicialObject.Splitting.ŒπSummand_comp_d_comp_œÄSummand_eq_zero

/-- If `s` is a splitting of a simplicial object `X` in a preadditive category,
`s.nondegComplex` is a chain complex which is given in degree `n` by
the nondegenerate `n`-simplices of `X`. -/
@[simps]
noncomputable def nondegComplex : ChainComplex C ‚Ñï where
  X := s.N
  d := s.d
  shape i j hij := by simp only [d, K[X].shape i j hij, zero_comp, comp_zero]
  d_comp_d' i j k _ _ := by
    simp only [d, assoc]
    have eq : K[X].d i j ‚â´ ùüô (X.obj (op [j])) ‚â´ K[X].d j k ‚â´
        s.œÄSummand (IndexSet.id (op [k])) = 0 := by
      erw [id_comp, HomologicalComplex.d_comp_d_assoc, zero_comp]
    rw [s.decomposition_id] at eq
    classical
    rw [Fintype.sum_eq_add_sum_compl (IndexSet.id (op [j])), add_comp, comp_add, assoc,
      Preadditive.sum_comp, Preadditive.comp_sum, Finset.sum_eq_zero, add_zero] at eq
    swap
    ¬∑ intro A hA
      simp only [Finset.mem_compl, Finset.mem_singleton] at hA
      simp only [assoc, ŒπSummand_comp_d_comp_œÄSummand_eq_zero _ _ _ _ hA, comp_zero]
    rw [eq, comp_zero]
#align simplicial_object.splitting.nondeg_complex SimplicialObject.Splitting.nondegComplex

/-- The chain complex `s.nondegComplex` attached to a splitting of a simplicial object `X`
becomes isomorphic to the normalized Moore complex `N‚ÇÅ.obj X` defined as a formal direct
factor in the category `Karoubi (ChainComplex C ‚Ñï)`. -/
@[simps]
noncomputable def toKaroubiNondegComplexIsoN‚ÇÅ :
    (toKaroubi _).obj s.nondegComplex ‚âÖ N‚ÇÅ.obj X where
  hom :=
    { f :=
        { f := fun n => (s.cofan _).inj (IndexSet.id (op [n])) ‚â´ PInfty.f n
          comm' := fun i j _ => by
            dsimp
            rw [assoc, assoc, assoc, œÄSummand_comp_cofan_inj_id_comp_PInfty_eq_PInfty,
              HomologicalComplex.Hom.comm] }
      comm := by
        ext n
        dsimp
        rw [id_comp, assoc, PInfty_f_idem] }
  inv :=
    { f :=
        { f := fun n => s.œÄSummand (IndexSet.id (op [n]))
          comm' := fun i j _ => by
            dsimp
            slice_rhs 1 1 => rw [‚Üê id_comp (K[X].d i j)]
            erw [s.decomposition_id]
            rw [sum_comp, sum_comp, Finset.sum_eq_single (IndexSet.id (op [i])), assoc, assoc]
            ¬∑ intro A _ hA
              simp only [assoc, s.ŒπSummand_comp_d_comp_œÄSummand_eq_zero _ _ _ hA, comp_zero]
            ¬∑ simp only [Finset.mem_univ, not_true, IsEmpty.forall_iff] }
      comm := by
        ext n
        dsimp
        simp only [comp_id, PInfty_comp_œÄSummand_id] }
  hom_inv_id := by
    ext n
    simp only [assoc, PInfty_comp_œÄSummand_id, Karoubi.comp_f, HomologicalComplex.comp_f,
      cofan_inj_œÄSummand_eq_id]
    rfl
  inv_hom_id := by
    ext n
    simp only [œÄSummand_comp_cofan_inj_id_comp_PInfty_eq_PInfty, Karoubi.comp_f,
      HomologicalComplex.comp_f, N‚ÇÅ_obj_p, Karoubi.id_eq]
set_option linter.uppercaseLean3 false in
#align simplicial_object.splitting.to_karoubi_nondeg_complex_iso_N‚ÇÅ SimplicialObject.Splitting.toKaroubiNondegComplexIsoN‚ÇÅ

end Splitting

namespace Split

variable {C : Type*} [Category C] [Preadditive C] [HasFiniteCoproducts C]

/-- The functor which sends a split simplicial object in a preadditive category to
the chain complex which consists of nondegenerate simplices. -/
@[simps]
noncomputable def nondegComplexFunctor : Split C ‚•§ ChainComplex C ‚Ñï where
  obj S := S.s.nondegComplex
  map {S‚ÇÅ S‚ÇÇ} Œ¶ :=
    { f := Œ¶.f
      comm' := fun i j _ => by
        dsimp
        erw [‚Üê cofan_inj_naturality_symm_assoc Œ¶ (Splitting.IndexSet.id (op [i])),
          ((alternatingFaceMapComplex C).map Œ¶.F).comm_assoc i j]
        simp only [assoc]
        congr 2
        apply S‚ÇÅ.s.hom_ext'
        intro A
        dsimp [alternatingFaceMapComplex]
        erw [cofan_inj_naturality_symm_assoc Œ¶ A]
        by_cases h : A.EqId
        ¬∑ dsimp at h
          subst h
          rw [Splitting.cofan_inj_œÄSummand_eq_id]
          dsimp
          rw [comp_id, Splitting.cofan_inj_œÄSummand_eq_id_assoc]
        ¬∑ rw [S‚ÇÅ.s.cofan_inj_œÄSummand_eq_zero_assoc _ _ (Ne.symm h),
            S‚ÇÇ.s.cofan_inj_œÄSummand_eq_zero _ _ (Ne.symm h), zero_comp, comp_zero] }
#align simplicial_object.split.nondeg_complex_functor SimplicialObject.Split.nondegComplexFunctor

/-- The natural isomorphism (in `Karoubi (ChainComplex C ‚Ñï)`) between the chain complex
of nondegenerate simplices of a split simplicial object and the normalized Moore complex
defined as a formal direct factor of the alternating face map complex. -/
@[simps!]
noncomputable def toKaroubiNondegComplexFunctorIsoN‚ÇÅ :
    nondegComplexFunctor ‚ãô toKaroubi (ChainComplex C ‚Ñï) ‚âÖ forget C ‚ãô DoldKan.N‚ÇÅ :=
  NatIso.ofComponents (fun S => S.s.toKaroubiNondegComplexIsoN‚ÇÅ) fun Œ¶ => by
    ext n
    dsimp
    simp only [Karoubi.comp_f, toKaroubi_map_f, HomologicalComplex.comp_f,
      nondegComplexFunctor_map_f, Splitting.toKaroubiNondegComplexIsoN‚ÇÅ_hom_f_f, N‚ÇÅ_map_f,
      AlternatingFaceMapComplex.map_f, assoc, PInfty_f_idem_assoc]
    erw [‚Üê Split.cofan_inj_naturality_symm_assoc Œ¶ (Splitting.IndexSet.id (op [n]))]
    rw [PInfty_f_naturality]
set_option linter.uppercaseLean3 false in
#align simplicial_object.split.to_karoubi_nondeg_complex_functor_iso_N‚ÇÅ SimplicialObject.Split.toKaroubiNondegComplexFunctorIsoN‚ÇÅ

end Split

end SimplicialObject
