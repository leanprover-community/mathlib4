/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.Homotopies
import Mathlib.Tactic.Ring

/-!

# Study of face maps for the Dold-Kan correspondence

In this file, we obtain the technical lemmas that are used in the file
`Projections.lean` in order to get basic properties of the endomorphisms
`P q : K[X] âŸ¶ K[X]` with respect to face maps (see `Homotopies.lean` for the
role of these endomorphisms in the overall strategy of proof).

The main lemma in this file is `HigherFacesVanish.induction`. It is based
on two technical lemmas `HigherFacesVanish.comp_HÏƒ_eq` and
`HigherFacesVanish.comp_HÏƒ_eq_zero`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Limits CategoryTheory.Category
  CategoryTheory.Preadditive CategoryTheory.SimplicialObject Simplicial

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C]
variable {X : SimplicialObject C}

/-- A morphism `Ï† : Y âŸ¶ X _â¦‹n+1â¦Œ` satisfies `HigherFacesVanish q Ï†`
when the compositions `Ï† â‰« X.Î´ j` are `0` for `j â‰¥ max 1 (n+2-q)`. When `q â‰¤ n+1`,
it basically means that the composition `Ï† â‰« X.Î´ j` are `0` for the `q` highest
possible values of a nonzero `j`. Otherwise, when `q â‰¥ n+2`, all the compositions
`Ï† â‰« X.Î´ j` for nonzero `j` vanish. See also the lemma `comp_P_eq_self_iff` in
`Projections.lean` which states that `HigherFacesVanish q Ï†` is equivalent to
the identity `Ï† â‰« (P q).f (n+1) = Ï†`. -/
def HigherFacesVanish {Y : C} {n : â„•} (q : â„•) (Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ) : Prop :=
  âˆ€ j : Fin (n + 1), n + 1 â‰¤ (j : â„•) + q â†’ Ï† â‰« X.Î´ j.succ = 0

namespace HigherFacesVanish

@[reassoc]
theorem comp_Î´_eq_zero {Y : C} {n : â„•} {q : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ} (v : HigherFacesVanish q Ï†)
    (j : Fin (n + 2)) (hjâ‚ : j â‰  0) (hjâ‚‚ : n + 2 â‰¤ (j : â„•) + q) : Ï† â‰« X.Î´ j = 0 := by
  obtain âŸ¨i, rflâŸ© := Fin.eq_succ_of_ne_zero hjâ‚
  apply v i
  simp only [Fin.val_succ] at hjâ‚‚
  omega

theorem of_succ {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ} (v : HigherFacesVanish (q + 1) Ï†) :
    HigherFacesVanish q Ï† := fun j hj => v j (by simpa only [â† add_assoc] using le_add_right hj)

theorem of_comp {Y Z : C} {q n : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ} (v : HigherFacesVanish q Ï†) (f : Z âŸ¶ Y) :
    HigherFacesVanish q (f â‰« Ï†) := fun j hj => by rw [assoc, v j hj, comp_zero]

theorem comp_HÏƒ_eq {Y : C} {n a q : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ} (v : HigherFacesVanish q Ï†)
    (hnaq : n = a + q) :
    Ï† â‰« (HÏƒ q).f (n + 1) = -Ï† â‰« X.Î´ âŸ¨a + 1, by omegaâŸ© â‰« X.Ïƒ âŸ¨a, by omegaâŸ© := by
  have hnaq_shift (d : â„•) : n + d = a + d + q := by omega
  rw [HÏƒ, Homotopy.nullHomotopicMap'_f (c_mk (n + 2) (n + 1) rfl) (c_mk (n + 1) n rfl),
    hÏƒ'_eq hnaq (c_mk (n + 1) n rfl), hÏƒ'_eq (hnaq_shift 1) (c_mk (n + 2) (n + 1) rfl)]
  simp only [AlternatingFaceMapComplex.obj_d_eq, eqToHom_refl, comp_id, comp_sum, sum_comp,
    comp_add]
  simp only [comp_zsmul, zsmul_comp, â† assoc, â† mul_zsmul]
  -- cleaning up the first sum
  rw [â† Fin.sum_congr' _ (hnaq_shift 2).symm, Fin.sum_trunc]
  swap
  Â· rintro âŸ¨k, hkâŸ©
    suffices Ï† â‰« X.Î´ (âŸ¨a + 2 + k, by omegaâŸ© : Fin (n + 2)) = 0 by
      simp only [this, Fin.natAdd_mk, Fin.cast_mk, zero_comp, smul_zero]
    convert v âŸ¨a + k + 1, by omegaâŸ© (by rw [Fin.val_mk]; omega)
    dsimp
    omega
  -- cleaning up the second sum
  rw [â† Fin.sum_congr' _ (hnaq_shift 3).symm, @Fin.sum_trunc _ _ (a + 3)]
  swap
  Â· rintro âŸ¨k, hkâŸ©
    rw [assoc, X.Î´_comp_Ïƒ_of_gt', v.comp_Î´_eq_zero_assoc, zero_comp, zsmul_zero]
    Â· simp only [Fin.lt_iff_val_lt_val]
      dsimp [Fin.natAdd, Fin.cast]
      omega
    Â· intro h
      replace h : a + 3 + k = 1 := by simp [Fin.pred_eq_iff_eq_succ, Fin.ext_iff] at h
      omega
    Â· dsimp [Fin.cast, Fin.pred]
      rw [Nat.add_right_comm, Nat.add_sub_assoc (by norm_num : 1 â‰¤ 3)]
      omega
  simp only [assoc]
  conv_lhs =>
    congr
    Â· rw [Fin.sum_univ_castSucc]
    Â· rw [Fin.sum_univ_castSucc, Fin.sum_univ_castSucc]
  dsimp [Fin.cast, Fin.castLE, Fin.castLT]
  /- the purpose of the following `simplif` is to create three subgoals in order
      to finish the proof -/
  have simplif :
    âˆ€ a b c d e f : Y âŸ¶ X _â¦‹n + 1â¦Œ, b = f â†’ d + e = 0 â†’ c + a = 0 â†’ a + b + (c + d + e) = f := by
    intro a b c d e f h1 h2 h3
    rw [add_assoc c d e, h2, add_zero, add_comm a, add_assoc, add_comm a, h3, add_zero, h1]
  apply simplif
  Â· -- b = f
    rw [â† pow_add, Odd.neg_one_pow, neg_smul, one_zsmul]
    exact âŸ¨a, by omegaâŸ©
  Â· -- d + e = 0
    rw [X.Î´_comp_Ïƒ_self' (Fin.castSucc_mk _ _ _).symm,
      X.Î´_comp_Ïƒ_succ' (Fin.succ_mk _ _ _).symm]
    simp only [comp_id, pow_add _ (a + 1) 1, pow_one, mul_neg, mul_one, neg_mul, neg_smul,
      add_neg_cancel]
  Â· -- c + a = 0
    rw [â† Finset.sum_add_distrib]
    apply Finset.sum_eq_zero
    rintro âŸ¨i, hiâŸ© _
    simp only
    have hia : (âŸ¨i, by omegaâŸ© : Fin (n + 2)) â‰¤
        Fin.castSucc (âŸ¨a, by omegaâŸ© : Fin (n + 1)) := by
      rw [Fin.le_iff_val_le_val]
      dsimp
      omega
    rw [â† Fin.succ_mk, â† Fin.castSucc_mk _ i, Î´_comp_Ïƒ_of_le X hia, add_eq_zero_iff_eq_neg,
      â† neg_zsmul]
    congr 2
    ring

theorem comp_HÏƒ_eq_zero {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ} (v : HigherFacesVanish q Ï†)
    (hqn : n < q) : Ï† â‰« (HÏƒ q).f (n + 1) = 0 := by
  simp only [HÏƒ, Homotopy.nullHomotopicMap'_f (c_mk (n + 2) (n + 1) rfl) (c_mk (n + 1) n rfl)]
  rw [hÏƒ'_eq_zero hqn (c_mk (n + 1) n rfl), comp_zero, zero_add]
  by_cases hqn' : n + 1 < q
  Â· rw [hÏƒ'_eq_zero hqn' (c_mk (n + 2) (n + 1) rfl), zero_comp, comp_zero]
  Â· simp only [hÏƒ'_eq (show n + 1 = 0 + q by omega) (c_mk (n + 2) (n + 1) rfl), pow_zero,
      Fin.mk_zero, one_zsmul, eqToHom_refl, comp_id, comp_sum,
      AlternatingFaceMapComplex.obj_d_eq]
    -- All terms of the sum but the first two are zeros
    rw [Fin.sum_univ_succ, Fin.sum_univ_succ, Fintype.sum_eq_zero, add_zero]
    Â· simp only [Fin.val_zero, Fin.val_succ, Fin.coe_castSucc, zero_add, pow_zero, one_smul,
        pow_one, neg_smul, comp_neg, â† Fin.castSucc_zero (n := n + 2), Î´_comp_Ïƒ_self, Î´_comp_Ïƒ_succ,
        add_neg_cancel]
    Â· intro j
      rw [comp_zsmul, comp_zsmul, Î´_comp_Ïƒ_of_gt', v.comp_Î´_eq_zero_assoc, zero_comp, zsmul_zero]
      Â· simp only [Fin.succ_lt_succ_iff, j.succ_pos]
      Â· simp [Fin.succ_ne_zero]
      Â· dsimp
        omega

theorem induction {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ} (v : HigherFacesVanish q Ï†) :
    HigherFacesVanish (q + 1) (Ï† â‰« (ğŸ™ _ + HÏƒ q).f (n + 1)) := by
  intro j hjâ‚
  dsimp
  simp only [comp_add, add_comp, comp_id]
  -- when n < q, the result follows immediately from the assumption
  by_cases hqn : n < q
  Â· rw [v.comp_HÏƒ_eq_zero hqn, zero_comp, add_zero, v j (by omega)]
  -- we now assume that nâ‰¥q, and write n=a+q
  obtain âŸ¨a, haâŸ© := Nat.le.dest (not_lt.mp hqn)
  rw [v.comp_HÏƒ_eq (show n = a + q by omega), neg_comp, add_neg_eq_zero, assoc, assoc]
  rcases n with - | m
  -- the boundary case n=0
  Â· simp only [Nat.eq_zero_of_add_eq_zero_left ha, Fin.eq_zero j, Fin.mk_zero, Fin.mk_one,
      Î´_comp_Ïƒ_succ, comp_id]
    rfl
  -- in the other case, we need to write n as m+1
  -- then, we first consider the particular case j = a
  by_cases hjâ‚‚ : a = (j : â„•)
  Â· simp only [hjâ‚‚, Fin.eta, Î´_comp_Ïƒ_succ, comp_id]
    rfl
  -- now, we assume j â‰  a (i.e. a < j)
  have haj : a < j := (Ne.le_iff_lt hjâ‚‚).mp (by omega)
  have ham : a â‰¤ m := by omega
  rw [X.Î´_comp_Ïƒ_of_gt', j.pred_succ]
  swap
  Â· rw [Fin.lt_iff_val_lt_val]
    simpa only [Fin.val_mk, Fin.val_succ, add_lt_add_iff_right] using haj
  obtain _ | ham'' := ham.lt_or_eq
  Â· -- case where `a<m`
    rw [â† X.Î´_comp_Î´''_assoc]
    swap
    Â· rw [Fin.le_iff_val_le_val]
      dsimp
      omega
    simp only [â† assoc, v j (by omega), zero_comp]
  Â· -- in the last case, a=m, q=1 and j=a+1
    rw [X.Î´_comp_Î´_self'_assoc]
    swap
    Â· ext
      cases j
      dsimp
      dsimp only [Nat.succ_eq_add_one] at *
      omega
    simp only [â† assoc, v j (by omega), zero_comp]

end HigherFacesVanish

end DoldKan

end AlgebraicTopology
