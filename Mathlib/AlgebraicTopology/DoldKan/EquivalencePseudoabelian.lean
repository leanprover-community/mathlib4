/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.EquivalenceAdditive
import Mathlib.AlgebraicTopology.DoldKan.Compatibility
import Mathlib.CategoryTheory.Idempotents.SimplicialObject

#align_import algebraic_topology.dold_kan.equivalence_pseudoabelian from "leanprover-community/mathlib"@"32a7e535287f9c73f2e4d2aef306a39190f0b504"

/-!

# The Dold-Kan correspondence for pseudoabelian categories

In this file, for any idempotent complete additive category `C`,
the Dold-Kan equivalence
`Idempotents.DoldKan.Equivalence C : SimplicialObject C â‰Œ ChainComplex C â„•`
is obtained. It is deduced from the equivalence
`Preadditive.DoldKan.Equivalence` between the respective idempotent
completions of these categories using the fact that when `C` is idempotent complete,
then both `SimplicialObject C` and `ChainComplex C â„•` are idempotent complete.

The construction of `Idempotents.DoldKan.Equivalence` uses the tools
introduced in the file `Compatibility.lean`. Doing so, the functor
`Idempotents.DoldKan.N` of the equivalence is
the composition of `Nâ‚ : SimplicialObject C â¥¤ Karoubi (ChainComplex C â„•)`
(defined in `FunctorN.lean`) and the inverse of the equivalence
`ChainComplex C â„• â‰Œ Karoubi (ChainComplex C â„•)`. The functor
`Idempotents.DoldKan.Î“` of the equivalence is by definition the functor
`Î“â‚€` introduced in `FunctorGamma.lean`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits CategoryTheory.Idempotents

variable {C : Type*} [Category C] [Preadditive C]

namespace CategoryTheory

namespace Idempotents

namespace DoldKan

open AlgebraicTopology.DoldKan

/-- The functor `N` for the equivalence is obtained by composing
`N' : SimplicialObject C â¥¤ Karoubi (ChainComplex C â„•)` and the inverse
of the equivalence `ChainComplex C â„• â‰Œ Karoubi (ChainComplex C â„•)`. -/
@[simps!, nolint unusedArguments]
def N [IsIdempotentComplete C] [HasFiniteCoproducts C] : SimplicialObject C â¥¤ ChainComplex C â„• :=
  Nâ‚ â‹™ (toKaroubiEquivalence _).inverse
set_option linter.uppercaseLean3 false in
#align category_theory.idempotents.dold_kan.N CategoryTheory.Idempotents.DoldKan.N

/-- The functor `Î“` for the equivalence is `Î“'`. -/
@[simps!, nolint unusedArguments]
def Î“ [IsIdempotentComplete C] [HasFiniteCoproducts C] : ChainComplex C â„• â¥¤ SimplicialObject C :=
  Î“â‚€
#align category_theory.idempotents.dold_kan.Î“ CategoryTheory.Idempotents.DoldKan.Î“

variable [IsIdempotentComplete C] [HasFiniteCoproducts C]

/-- A reformulation of the isomorphism `toKaroubi (SimplicialObject C) â‹™ Nâ‚‚ â‰… Nâ‚` -/
def isoNâ‚ :
    (toKaroubiEquivalence (SimplicialObject C)).functor â‹™
      Preadditive.DoldKan.equivalence.functor â‰… Nâ‚ := toKaroubiCompNâ‚‚IsoNâ‚

@[simp]
lemma isoNâ‚_hom_app_f (X : SimplicialObject C) :
    (isoNâ‚.hom.app X).f = PInfty := rfl

/-- A reformulation of the canonical isomorphism
`toKaroubi (ChainComplex C â„•) â‹™ Î“â‚‚ â‰… Î“ â‹™ toKaroubi (SimplicialObject C)`. -/
def isoÎ“â‚€ :
    (toKaroubiEquivalence (ChainComplex C â„•)).functor â‹™ Preadditive.DoldKan.equivalence.inverse â‰…
      Î“ â‹™ (toKaroubiEquivalence _).functor :=
  (functorExtensionâ‚‚CompWhiskeringLeftToKaroubiIso _ _).app Î“â‚€

@[simp]
lemma Nâ‚‚_map_isoÎ“â‚€_hom_app_f (X : ChainComplex C â„•) :
    (Nâ‚‚.map (isoÎ“â‚€.hom.app X)).f = PInfty := by
  ext
  apply comp_id

/-- The Dold-Kan equivalence for pseudoabelian categories given
by the functors `N` and `Î“`. It is obtained by applying the results in
`Compatibility.lean` to the equivalence `Preadditive.DoldKan.Equivalence`. -/
def equivalence : SimplicialObject C â‰Œ ChainComplex C â„• :=
  Compatibility.equivalence isoNâ‚ isoÎ“â‚€
#align category_theory.idempotents.dold_kan.equivalence CategoryTheory.Idempotents.DoldKan.equivalence

theorem equivalence_functor : (equivalence : SimplicialObject C â‰Œ _).functor = N :=
  rfl
#align category_theory.idempotents.dold_kan.equivalence_functor CategoryTheory.Idempotents.DoldKan.equivalence_functor

theorem equivalence_inverse : (equivalence : SimplicialObject C â‰Œ _).inverse = Î“ :=
  rfl
#align category_theory.idempotents.dold_kan.equivalence_inverse CategoryTheory.Idempotents.DoldKan.equivalence_inverse

/-- The natural isomorphism `NÎ“' satisfies the compatibility that is needed
for the construction of our counit isomorphism `Î·` -/
theorem hÎ· :
    Compatibility.Ï„â‚€ =
      Compatibility.Ï„â‚ isoNâ‚ isoÎ“â‚€
        (Nâ‚Î“â‚€ : Î“ â‹™ Nâ‚ â‰… (toKaroubiEquivalence (ChainComplex C â„•)).functor) := by
  ext K : 3
  simp only [Compatibility.Ï„â‚€_hom_app, Compatibility.Ï„â‚_hom_app]
  exact (Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€ K).trans (by simp )
#align category_theory.idempotents.dold_kan.hÎ· CategoryTheory.Idempotents.DoldKan.hÎ·

/-- The counit isomorphism induced by `Nâ‚Î“â‚€` -/
@[simps!]
def Î· : Î“ â‹™ N â‰… ğŸ­ (ChainComplex C â„•) :=
  Compatibility.equivalenceCounitIso
    (Nâ‚Î“â‚€ : (Î“ : ChainComplex C â„• â¥¤ _) â‹™ Nâ‚ â‰… (toKaroubiEquivalence _).functor)
#align category_theory.idempotents.dold_kan.Î· CategoryTheory.Idempotents.DoldKan.Î·

theorem equivalence_counitIso :
    DoldKan.equivalence.counitIso = (Î· : Î“ â‹™ N â‰… ğŸ­ (ChainComplex C â„•)) :=
  Compatibility.equivalenceCounitIso_eq hÎ·
#align category_theory.idempotents.dold_kan.equivalence_counit_iso CategoryTheory.Idempotents.DoldKan.equivalence_counitIso

theorem hÎµ :
    Compatibility.Ï… (isoNâ‚) =
      (Î“â‚‚Nâ‚ : (toKaroubiEquivalence _).functor â‰…
          (Nâ‚ : SimplicialObject C â¥¤ _) â‹™ Preadditive.DoldKan.equivalence.inverse) := by
  dsimp only [isoNâ‚]
  ext1
  rw [â† cancel_epi Î“â‚‚Nâ‚.inv, Iso.inv_hom_id]
  ext X : 2
  rw [NatTrans.comp_app]
  erw [compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚_natTrans X]
  rw [Compatibility.Ï…_hom_app, Preadditive.DoldKan.equivalence_unitIso, Iso.app_inv, assoc]
  erw [â† NatTrans.comp_app_assoc, IsIso.hom_inv_id]
  rw [NatTrans.id_app, id_comp, NatTrans.id_app, Î“â‚‚Nâ‚‚ToKaroubiIso_inv_app]
  dsimp only [Preadditive.DoldKan.equivalence_inverse, Preadditive.DoldKan.Î“]
  rw [â† Î“â‚‚.map_comp, Iso.inv_hom_id_app, Î“â‚‚.map_id]
  rfl
#align category_theory.idempotents.dold_kan.hÎµ CategoryTheory.Idempotents.DoldKan.hÎµ

/-- The unit isomorphism induced by `Î“â‚‚Nâ‚`. -/
def Îµ : ğŸ­ (SimplicialObject C) â‰… N â‹™ Î“ :=
  Compatibility.equivalenceUnitIso isoÎ“â‚€ Î“â‚‚Nâ‚
#align category_theory.idempotents.dold_kan.Îµ CategoryTheory.Idempotents.DoldKan.Îµ

theorem equivalence_unitIso :
    DoldKan.equivalence.unitIso = (Îµ : ğŸ­ (SimplicialObject C) â‰… N â‹™ Î“) :=
  Compatibility.equivalenceUnitIso_eq hÎµ
#align category_theory.idempotents.dold_kan.equivalence_unit_iso CategoryTheory.Idempotents.DoldKan.equivalence_unitIso

end DoldKan

end Idempotents

end CategoryTheory
