/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.Faces
import Mathlib.CategoryTheory.Idempotents.Basic

#align_import algebraic_topology.dold_kan.projections from "leanprover-community/mathlib"@"32a7e535287f9c73f2e4d2aef306a39190f0b504"

/-!

# Construction of projections for the Dold-Kan correspondence

In this file, we construct endomorphisms `P q : K[X] âŸ¶ K[X]` for all
`q : â„•`. We study how they behave with respect to face maps with the lemmas
`HigherFacesVanish.of_P`, `HigherFacesVanish.comp_P_eq_self` and
`comp_P_eq_self_iff`.

Then, we show that they are projections (see `P_f_idem`
and `P_idem`). They are natural transformations (see `natTransP`
and `P_f_naturality`) and are compatible with the application
of additive functors (see `map_P`).

By passing to the limit, these endomorphisms `P q` shall be used in `PInfty.lean`
in order to define `PInfty : K[X] âŸ¶ K[X]`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category CategoryTheory.Limits CategoryTheory.Preadditive
  CategoryTheory.SimplicialObject Opposite CategoryTheory.Idempotents

open Simplicial DoldKan

noncomputable section

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C] {X : SimplicialObject C}

/-- This is the inductive definition of the projections `P q : K[X] âŸ¶ K[X]`,
with `P 0 := ğŸ™ _` and `P (q+1) := P q â‰« (ğŸ™ _ + HÏƒ q)`. -/
noncomputable def P : â„• â†’ (K[X] âŸ¶ K[X])
  | 0 => ğŸ™ _
  | q + 1 => P q â‰« (ğŸ™ _ + HÏƒ q)
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P AlgebraicTopology.DoldKan.P

-- Porting note: `P_zero` and `P_succ` have been added to ease the port, because
-- `unfold P` would sometimes unfold to a `match` rather than the induction formula
lemma P_zero : (P 0 : K[X] âŸ¶ K[X]) = ğŸ™ _ := rfl
lemma P_succ (q : â„•) : (P (q+1) : K[X] âŸ¶ K[X]) = P q â‰« (ğŸ™ _ + HÏƒ q) := rfl

/-- All the `P q` coincide with `ğŸ™ _` in degree 0. -/
@[simp]
theorem P_f_0_eq (q : â„•) : ((P q).f 0 : X _[0] âŸ¶ X _[0]) = ğŸ™ _ := by
  induction' q with q hq
  Â· rfl
  Â· simp only [P_succ, HomologicalComplex.add_f_apply, HomologicalComplex.comp_f,
      HomologicalComplex.id_f, id_comp, hq, HÏƒ_eq_zero, add_zero]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_f_0_eq AlgebraicTopology.DoldKan.P_f_0_eq

/-- `Q q` is the complement projection associated to `P q` -/
def Q (q : â„•) : K[X] âŸ¶ K[X] :=
  ğŸ™ _ - P q
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q AlgebraicTopology.DoldKan.Q

theorem P_add_Q (q : â„•) : P q + Q q = ğŸ™ K[X] := by
  rw [Q]
  abel
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_add_Q AlgebraicTopology.DoldKan.P_add_Q

theorem P_add_Q_f (q n : â„•) : (P q).f n + (Q q).f n = ğŸ™ (X _[n]) :=
  HomologicalComplex.congr_hom (P_add_Q q) n
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_add_Q_f AlgebraicTopology.DoldKan.P_add_Q_f

@[simp]
theorem Q_zero : (Q 0 : K[X] âŸ¶ _) = 0 :=
  sub_self _
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q_eq_zero AlgebraicTopology.DoldKan.Q_zero

theorem Q_succ (q : â„•) : (Q (q + 1) : K[X] âŸ¶ _) = Q q - P q â‰« HÏƒ q := by
  simp only [Q, P_succ, comp_add, comp_id]
  abel
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q_eq AlgebraicTopology.DoldKan.Q_succ

/-- All the `Q q` coincide with `0` in degree 0. -/
@[simp]
theorem Q_f_0_eq (q : â„•) : ((Q q).f 0 : X _[0] âŸ¶ X _[0]) = 0 := by
  simp only [HomologicalComplex.sub_f_apply, HomologicalComplex.id_f, Q, P_f_0_eq, sub_self]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q_f_0_eq AlgebraicTopology.DoldKan.Q_f_0_eq

namespace HigherFacesVanish

/-- This lemma expresses the vanishing of
`(P q).f (n+1) â‰« X.Î´ k : X _[n+1] âŸ¶ X _[n]` when `kâ‰ 0` and `kâ‰¥n-q+2` -/
theorem of_P : âˆ€ q n : â„•, HigherFacesVanish q ((P q).f (n + 1) : X _[n + 1] âŸ¶ X _[n + 1])
  | 0 => fun n j hjâ‚ => by omega
  | q + 1 => fun n => by
    simp only [P_succ]
    exact (of_P q n).induction
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.higher_faces_vanish.of_P AlgebraicTopology.DoldKan.HigherFacesVanish.of_P

@[reassoc]
theorem comp_P_eq_self {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _[n + 1]} (v : HigherFacesVanish q Ï†) :
    Ï† â‰« (P q).f (n + 1) = Ï† := by
  induction' q with q hq
  Â· simp only [P_zero]
    apply comp_id
  Â· simp only [P_succ, comp_add, HomologicalComplex.comp_f, HomologicalComplex.add_f_apply,
      comp_id, â† assoc, hq v.of_succ, add_right_eq_self]
    by_cases hqn : n < q
    Â· exact v.of_succ.comp_HÏƒ_eq_zero hqn
    Â· obtain âŸ¨a, haâŸ© := Nat.le.dest (not_lt.mp hqn)
      have hnaq : n = a + q := by omega
      simp only [v.of_succ.comp_HÏƒ_eq hnaq, neg_eq_zero, â† assoc]
      have eq := v âŸ¨a, by omegaâŸ© (by
        simp only [hnaq, Nat.succ_eq_add_one, add_assoc]
        rfl)
      simp only [Fin.succ_mk] at eq
      simp only [eq, zero_comp]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.higher_faces_vanish.comp_P_eq_self AlgebraicTopology.DoldKan.HigherFacesVanish.comp_P_eq_self

end HigherFacesVanish

theorem comp_P_eq_self_iff {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _[n + 1]} :
    Ï† â‰« (P q).f (n + 1) = Ï† â†” HigherFacesVanish q Ï† := by
  constructor
  Â· intro hÏ†
    rw [â† hÏ†]
    apply HigherFacesVanish.of_comp
    apply HigherFacesVanish.of_P
  Â· exact HigherFacesVanish.comp_P_eq_self
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.comp_P_eq_self_iff AlgebraicTopology.DoldKan.comp_P_eq_self_iff

@[reassoc (attr := simp)]
theorem P_f_idem (q n : â„•) : ((P q).f n : X _[n] âŸ¶ _) â‰« (P q).f n = (P q).f n := by
  rcases n with (_|n)
  Â· rw [P_f_0_eq q, comp_id]
  Â· exact (HigherFacesVanish.of_P q n).comp_P_eq_self
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_f_idem AlgebraicTopology.DoldKan.P_f_idem

@[reassoc (attr := simp)]
theorem Q_f_idem (q n : â„•) : ((Q q).f n : X _[n] âŸ¶ _) â‰« (Q q).f n = (Q q).f n :=
  idem_of_id_sub_idem _ (P_f_idem q n)
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q_f_idem AlgebraicTopology.DoldKan.Q_f_idem

@[reassoc (attr := simp)]
theorem P_idem (q : â„•) : (P q : K[X] âŸ¶ K[X]) â‰« P q = P q := by
  ext n
  exact P_f_idem q n
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_idem AlgebraicTopology.DoldKan.P_idem

@[reassoc (attr := simp)]
theorem Q_idem (q : â„•) : (Q q : K[X] âŸ¶ K[X]) â‰« Q q = Q q := by
  ext n
  exact Q_f_idem q n
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q_idem AlgebraicTopology.DoldKan.Q_idem

/-- For each `q`, `P q` is a natural transformation. -/
@[simps]
def natTransP (q : â„•) : alternatingFaceMapComplex C âŸ¶ alternatingFaceMapComplex C where
  app X := P q
  naturality _ _ f := by
    induction' q with q hq
    Â· dsimp [alternatingFaceMapComplex]
      simp only [P_zero, id_comp, comp_id]
    Â· simp only [P_succ, add_comp, comp_add, assoc, comp_id, hq, reassoc_of% hq]
      erw [(natTransHÏƒ q).naturality f]
      rfl
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.nat_trans_P AlgebraicTopology.DoldKan.natTransP

@[reassoc (attr := simp)]
theorem P_f_naturality (q n : â„•) {X Y : SimplicialObject C} (f : X âŸ¶ Y) :
    f.app (op [n]) â‰« (P q).f n = (P q).f n â‰« f.app (op [n]) :=
  HomologicalComplex.congr_hom ((natTransP q).naturality f) n
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_f_naturality AlgebraicTopology.DoldKan.P_f_naturality

@[reassoc (attr := simp)]
theorem Q_f_naturality (q n : â„•) {X Y : SimplicialObject C} (f : X âŸ¶ Y) :
    f.app (op [n]) â‰« (Q q).f n = (Q q).f n â‰« f.app (op [n]) := by
  simp only [Q, HomologicalComplex.sub_f_apply, HomologicalComplex.id_f, comp_sub, P_f_naturality,
    sub_comp, sub_left_inj]
  dsimp
  simp only [comp_id, id_comp]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Q_f_naturality AlgebraicTopology.DoldKan.Q_f_naturality

/-- For each `q`, `Q q` is a natural transformation. -/
@[simps]
def natTransQ (q : â„•) : alternatingFaceMapComplex C âŸ¶ alternatingFaceMapComplex C where
  app X := Q q
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.nat_trans_Q AlgebraicTopology.DoldKan.natTransQ

theorem map_P {D : Type*} [Category D] [Preadditive D] (G : C â¥¤ D) [G.Additive]
    (X : SimplicialObject C) (q n : â„•) :
    G.map ((P q : K[X] âŸ¶ _).f n) = (P q : K[((whiskering C D).obj G).obj X] âŸ¶ _).f n := by
  induction' q with q hq
  Â· simp only [P_zero]
    apply G.map_id
  Â· simp only [P_succ, comp_add, HomologicalComplex.comp_f, HomologicalComplex.add_f_apply,
      comp_id, Functor.map_add, Functor.map_comp, hq, map_HÏƒ]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.map_P AlgebraicTopology.DoldKan.map_P

theorem map_Q {D : Type*} [Category D] [Preadditive D] (G : C â¥¤ D) [G.Additive]
    (X : SimplicialObject C) (q n : â„•) :
    G.map ((Q q : K[X] âŸ¶ _).f n) = (Q q : K[((whiskering C D).obj G).obj X] âŸ¶ _).f n := by
  rw [â† add_right_inj (G.map ((P q : K[X] âŸ¶ _).f n)), â† G.map_add, map_P G X q n, P_add_Q_f,
    P_add_Q_f]
  apply G.map_id
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.map_Q AlgebraicTopology.DoldKan.map_Q

end DoldKan

end AlgebraicTopology
