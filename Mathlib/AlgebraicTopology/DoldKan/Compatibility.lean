/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Equivalence

#align_import algebraic_topology.dold_kan.compatibility from "leanprover-community/mathlib"@"160f568dcf772b2477791c844fc605f2f91f73d1"

/-! Tools for compatibilities between Dold-Kan equivalences

The purpose of this file is to introduce tools which will enable the
construction of the Dold-Kan equivalence `SimplicialObject C â‰Œ ChainComplex C â„•`
for a pseudoabelian category `C` from the equivalence
`Karoubi (SimplicialObject C) â‰Œ Karoubi (ChainComplex C â„•)` and the two
equivalences `simplicial_object C â‰… Karoubi (SimplicialObject C)` and
`ChainComplex C â„• â‰… Karoubi (ChainComplex C â„•)`.

It is certainly possible to get an equivalence `SimplicialObject C â‰Œ ChainComplex C â„•`
using a compositions of the three equivalences above, but then neither the functor
nor the inverse would have good definitional properties. For example, it would be better
if the inverse functor of the equivalence was exactly the functor
`Î“â‚€ : SimplicialObject C â¥¤ ChainComplex C â„•` which was constructed in `FunctorGamma.lean`.

In this file, given four categories `A`, `A'`, `B`, `B'`, equivalences `eA : A â‰… A'`,
`eB : B â‰… B'`, `e' : A' â‰… B'`, functors `F : A â¥¤ B'`, `G : B â¥¤ A` equipped with certain
compatibilities, we construct successive equivalences:
- `equivalenceâ‚€` from `A` to `B'`, which is the composition of `eA` and `e'`.
- `equivalenceâ‚` from `A` to `B'`, with the same inverse functor as `equivalenceâ‚€`,
but whose functor is `F`.
- `equivalenceâ‚‚` from `A` to `B`, which is the composition of `equivalenceâ‚` and the
inverse of `eB`:
- `equivalence` from `A` to `B`, which has the same functor `F â‹™ eB.inverse` as `equivalenceâ‚‚`,
but whose inverse functor is `G`.

When extra assumptions are given, we shall also provide simplification lemmas for the
unit and counit isomorphisms of `equivalence`. (TODO)

-/


open CategoryTheory CategoryTheory.Category

namespace AlgebraicTopology

namespace DoldKan

namespace Compatibility

variable {A A' B B' : Type _} [Category A] [Category A'] [Category B] [Category B'] (eA : A â‰Œ A')
  (eB : B â‰Œ B') (e' : A' â‰Œ B') {F : A â¥¤ B'} (hF : eA.functor â‹™ e'.functor â‰… F) {G : B â¥¤ A}
  (hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor)

/-- A basic equivalence `A â‰… B'` obtained by composing `eA : A â‰… A'` and `e' : A' â‰… B'`. -/
@[simps! functor inverse unitIso_hom_app]
def equivalenceâ‚€ : A â‰Œ B' :=
  eA.trans e'
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚€ AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚€

variable {eA} {e'}

/-- An intermediate equivalence `A â‰… B'` whose functor is `F` and whose inverse is
`e'.inverse â‹™ eA.inverse`. -/
@[simps! functor]
def equivalenceâ‚ : A â‰Œ B' :=
  letI : IsEquivalence F :=
    IsEquivalence.ofIso hF (IsEquivalence.ofEquivalence (equivalenceâ‚€ eA e'))
  F.asEquivalence
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚ AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚

theorem equivalenceâ‚_inverse : (equivalenceâ‚ hF).inverse = e'.inverse â‹™ eA.inverse :=
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_inverse AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚_inverse

/-- The counit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps!]
def equivalenceâ‚CounitIso : (e'.inverse â‹™ eA.inverse) â‹™ F â‰… ğŸ­ B' :=
  calc
    (e'.inverse â‹™ eA.inverse) â‹™ F â‰… (e'.inverse â‹™ eA.inverse) â‹™ eA.functor â‹™ e'.functor :=
      isoWhiskerLeft _ hF.symm
    _ â‰… e'.inverse â‹™ (eA.inverse â‹™ eA.functor) â‹™ e'.functor := (Iso.refl _)
    _ â‰… e'.inverse â‹™ ğŸ­ _ â‹™ e'.functor := (isoWhiskerLeft _ (isoWhiskerRight eA.counitIso _))
    _ â‰… e'.inverse â‹™ e'.functor := (Iso.refl _)
    _ â‰… ğŸ­ B' := e'.counitIso
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_counit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚CounitIso

theorem equivalenceâ‚CounitIso_eq : (equivalenceâ‚ hF).counitIso = equivalenceâ‚CounitIso hF := by
  ext Y
  dsimp [equivalenceâ‚]
  unfold Functor.asEquivalence
  dsimp [equivalenceâ‚€, IsEquivalence.inverse, IsEquivalence.ofEquivalence]
  simp
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_counit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚CounitIso_eq

/-- The unit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps!]
def equivalenceâ‚UnitIso : ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse :=
  calc
    ğŸ­ A â‰… eA.functor â‹™ eA.inverse := eA.unitIso
    _ â‰… eA.functor â‹™ ğŸ­ A' â‹™ eA.inverse := (Iso.refl _)
    _ â‰… eA.functor â‹™ (e'.functor â‹™ e'.inverse) â‹™ eA.inverse :=
      (isoWhiskerLeft _ (isoWhiskerRight e'.unitIso _))
    _ â‰… (eA.functor â‹™ e'.functor) â‹™ e'.inverse â‹™ eA.inverse := (Iso.refl _)
    _ â‰… F â‹™ e'.inverse â‹™ eA.inverse := isoWhiskerRight hF _
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_unit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚UnitIso

theorem equivalenceâ‚UnitIso_eq : (equivalenceâ‚ hF).unitIso = equivalenceâ‚UnitIso hF := by
  ext X
  dsimp [equivalenceâ‚]
  unfold Functor.asEquivalence
  dsimp [NatIso.hcomp, IsEquivalence.ofEquivalence]
  simp
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_unit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚UnitIso_eq

/-- An intermediate equivalence `A â‰… B` obtained as the composition of `equivalenceâ‚` and
the inverse of `eB : B â‰Œ B'`. -/
@[simps! functor]
def equivalenceâ‚‚ : A â‰Œ B :=
  (equivalenceâ‚ hF).trans eB.symm
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚ AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚

theorem equivalenceâ‚‚_inverse :
    (equivalenceâ‚‚ eB hF).inverse = eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_inverse AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚_inverse

/-- The counit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps!]
def equivalenceâ‚‚CounitIso : (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse â‰… ğŸ­ B :=
  calc
    (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse â‰…
        eB.functor â‹™ (e'.inverse â‹™ eA.inverse â‹™ F) â‹™ eB.inverse :=
      Iso.refl _
    _ â‰… eB.functor â‹™ ğŸ­ _ â‹™ eB.inverse :=
      (isoWhiskerLeft _ (isoWhiskerRight (equivalenceâ‚CounitIso hF) _))
    _ â‰… eB.functor â‹™ eB.inverse := (Iso.refl _)
    _ â‰… ğŸ­ B := eB.unitIso.symm
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_counit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚CounitIso

theorem equivalenceâ‚‚CounitIso_eq :
    (equivalenceâ‚‚ eB hF).counitIso = equivalenceâ‚‚CounitIso eB hF := by
  ext Y'
  dsimp [equivalenceâ‚‚, Iso.refl]
  simp only [equivalenceâ‚CounitIso_eq, equivalenceâ‚‚CounitIso_hom_app,
    equivalenceâ‚CounitIso_hom_app, Functor.map_comp, assoc]
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_counit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚CounitIso_eq

/-- The unit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps!]
def equivalenceâ‚‚UnitIso : ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
  calc
    ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse := equivalenceâ‚UnitIso hF
    _ â‰… F â‹™ ğŸ­ B' â‹™ e'.inverse â‹™ eA.inverse := (Iso.refl _)
    _ â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      (isoWhiskerLeft _ (isoWhiskerRight eB.counitIso.symm _))
    _ â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse := Iso.refl _
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_unit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚UnitIso

theorem equivalenceâ‚‚UnitIso_eq : (equivalenceâ‚‚ eB hF).unitIso = equivalenceâ‚‚UnitIso eB hF := by
  ext X
  dsimp [equivalenceâ‚‚]
  simp only [equivalenceâ‚‚UnitIso_hom_app, equivalenceâ‚UnitIso_eq, equivalenceâ‚UnitIso_hom_app,
      assoc, NatIso.cancel_natIso_hom_left]
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_unit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚UnitIso_eq

variable {eB}

/-- The equivalence `A â‰… B` whose functor is `F â‹™ eB.inverse` and
whose inverse is `G : B â‰… A`. -/
@[simps! inverse]
def equivalence : A â‰Œ B :=
  letI : IsEquivalence G := by
    refine' IsEquivalence.ofIso _ (IsEquivalence.ofEquivalence (equivalenceâ‚‚ eB hF).symm)
    calc
      eB.functor â‹™ e'.inverse â‹™ eA.inverse â‰… (eB.functor â‹™ e'.inverse) â‹™ eA.inverse := Iso.refl _
      _ â‰… (G â‹™ eA.functor) â‹™ eA.inverse := (isoWhiskerRight hG _)
      _ â‰… G â‹™ ğŸ­ A := (isoWhiskerLeft _ eA.unitIso.symm)
      _ â‰… G := Functor.rightUnitor G
  G.asEquivalence.symm
#align algebraic_topology.dold_kan.compatibility.equivalence AlgebraicTopology.DoldKan.Compatibility.equivalence

theorem equivalence_functor : (equivalence hF hG).functor = F â‹™ eB.inverse :=
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalence_functor AlgebraicTopology.DoldKan.Compatibility.equivalence_functor

end Compatibility

end DoldKan

end AlgebraicTopology
