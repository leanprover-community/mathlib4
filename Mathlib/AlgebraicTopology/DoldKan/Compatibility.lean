/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Equivalence

#align_import algebraic_topology.dold_kan.compatibility from "leanprover-community/mathlib"@"32a7e535287f9c73f2e4d2aef306a39190f0b504"

/-! Tools for compatibilities between Dold-Kan equivalences

The purpose of this file is to introduce tools which will enable the
construction of the Dold-Kan equivalence `SimplicialObject C â‰Œ ChainComplex C â„•`
for a pseudoabelian category `C` from the equivalence
`Karoubi (SimplicialObject C) â‰Œ Karoubi (ChainComplex C â„•)` and the two
equivalences `simplicial_object C â‰… Karoubi (SimplicialObject C)` and
`ChainComplex C â„• â‰… Karoubi (ChainComplex C â„•)`.

It is certainly possible to get an equivalence `SimplicialObject C â‰Œ ChainComplex C â„•`
using a compositions of the three equivalences above, but then neither the functor
nor the inverse would have good definitional properties. For example, it would be better
if the inverse functor of the equivalence was exactly the functor
`Î“â‚€ : SimplicialObject C â¥¤ ChainComplex C â„•` which was constructed in `FunctorGamma.lean`.

In this file, given four categories `A`, `A'`, `B`, `B'`, equivalences `eA : A â‰… A'`,
`eB : B â‰… B'`, `e' : A' â‰… B'`, functors `F : A â¥¤ B'`, `G : B â¥¤ A` equipped with certain
compatibilities, we construct successive equivalences:
- `equivalenceâ‚€` from `A` to `B'`, which is the composition of `eA` and `e'`.
- `equivalenceâ‚` from `A` to `B'`, with the same inverse functor as `equivalenceâ‚€`,
but whose functor is `F`.
- `equivalenceâ‚‚` from `A` to `B`, which is the composition of `equivalenceâ‚` and the
inverse of `eB`:
- `equivalence` from `A` to `B`, which has the same functor `F â‹™ eB.inverse` as `equivalenceâ‚‚`,
but whose inverse functor is `G`.

When extra assumptions are given, we shall also provide simplification lemmas for the
unit and counit isomorphisms of `equivalence`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category

namespace AlgebraicTopology

namespace DoldKan

namespace Compatibility

variable {A A' B B' : Type*} [Category A] [Category A'] [Category B] [Category B'] (eA : A â‰Œ A')
  (eB : B â‰Œ B') (e' : A' â‰Œ B') {F : A â¥¤ B'} (hF : eA.functor â‹™ e'.functor â‰… F) {G : B â¥¤ A}
  (hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor)

/-- A basic equivalence `A â‰… B'` obtained by composing `eA : A â‰… A'` and `e' : A' â‰… B'`. -/
@[simps! functor inverse unitIso_hom_app]
def equivalenceâ‚€ : A â‰Œ B' :=
  eA.trans e'
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚€ AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚€

variable {eA} {e'}

/-- An intermediate equivalence `A â‰… B'` whose functor is `F` and whose inverse is
`e'.inverse â‹™ eA.inverse`. -/
@[simps! functor]
def equivalenceâ‚ : A â‰Œ B' := (equivalenceâ‚€ eA e').changeFunctor hF
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚ AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚

theorem equivalenceâ‚_inverse : (equivalenceâ‚ hF).inverse = e'.inverse â‹™ eA.inverse :=
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_inverse AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚_inverse

/-- The counit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps!]
def equivalenceâ‚CounitIso : (e'.inverse â‹™ eA.inverse) â‹™ F â‰… ğŸ­ B' :=
  calc
    (e'.inverse â‹™ eA.inverse) â‹™ F â‰… (e'.inverse â‹™ eA.inverse) â‹™ eA.functor â‹™ e'.functor :=
      isoWhiskerLeft _ hF.symm
    _ â‰… e'.inverse â‹™ (eA.inverse â‹™ eA.functor) â‹™ e'.functor := Iso.refl _
    _ â‰… e'.inverse â‹™ ğŸ­ _ â‹™ e'.functor := isoWhiskerLeft _ (isoWhiskerRight eA.counitIso _)
    _ â‰… e'.inverse â‹™ e'.functor := Iso.refl _
    _ â‰… ğŸ­ B' := e'.counitIso
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_counit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚CounitIso

theorem equivalenceâ‚CounitIso_eq : (equivalenceâ‚ hF).counitIso = equivalenceâ‚CounitIso hF := by
  ext Y
  simp [equivalenceâ‚, equivalenceâ‚€]
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_counit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚CounitIso_eq

/-- The unit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps!]
def equivalenceâ‚UnitIso : ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse :=
  calc
    ğŸ­ A â‰… eA.functor â‹™ eA.inverse := eA.unitIso
    _ â‰… eA.functor â‹™ ğŸ­ A' â‹™ eA.inverse := Iso.refl _
    _ â‰… eA.functor â‹™ (e'.functor â‹™ e'.inverse) â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight e'.unitIso _)
    _ â‰… (eA.functor â‹™ e'.functor) â‹™ e'.inverse â‹™ eA.inverse := Iso.refl _
    _ â‰… F â‹™ e'.inverse â‹™ eA.inverse := isoWhiskerRight hF _
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_unit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚UnitIso

theorem equivalenceâ‚UnitIso_eq : (equivalenceâ‚ hF).unitIso = equivalenceâ‚UnitIso hF := by
  ext X
  simp [equivalenceâ‚]
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚_unit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚UnitIso_eq

/-- An intermediate equivalence `A â‰… B` obtained as the composition of `equivalenceâ‚` and
the inverse of `eB : B â‰Œ B'`. -/
@[simps! functor]
def equivalenceâ‚‚ : A â‰Œ B :=
  (equivalenceâ‚ hF).trans eB.symm
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚ AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚

theorem equivalenceâ‚‚_inverse :
    (equivalenceâ‚‚ eB hF).inverse = eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_inverse AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚_inverse

/-- The counit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps!]
def equivalenceâ‚‚CounitIso : (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse â‰… ğŸ­ B :=
  calc
    (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse â‰…
        eB.functor â‹™ (e'.inverse â‹™ eA.inverse â‹™ F) â‹™ eB.inverse :=
      Iso.refl _
    _ â‰… eB.functor â‹™ ğŸ­ _ â‹™ eB.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight (equivalenceâ‚CounitIso hF) _)
    _ â‰… eB.functor â‹™ eB.inverse := Iso.refl _
    _ â‰… ğŸ­ B := eB.unitIso.symm
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_counit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚CounitIso

theorem equivalenceâ‚‚CounitIso_eq :
    (equivalenceâ‚‚ eB hF).counitIso = equivalenceâ‚‚CounitIso eB hF := by
  ext Y'
  dsimp [equivalenceâ‚‚, Iso.refl]
  simp only [equivalenceâ‚CounitIso_eq, equivalenceâ‚‚CounitIso_hom_app,
    equivalenceâ‚CounitIso_hom_app, Functor.map_comp, assoc]
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_counit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚CounitIso_eq

/-- The unit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps!]
def equivalenceâ‚‚UnitIso : ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
  calc
    ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse := equivalenceâ‚UnitIso hF
    _ â‰… F â‹™ ğŸ­ B' â‹™ e'.inverse â‹™ eA.inverse := Iso.refl _
    _ â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight eB.counitIso.symm _)
    _ â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse := Iso.refl _
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_unit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚UnitIso

theorem equivalenceâ‚‚UnitIso_eq : (equivalenceâ‚‚ eB hF).unitIso = equivalenceâ‚‚UnitIso eB hF := by
  ext X
  dsimp [equivalenceâ‚‚]
  simp only [equivalenceâ‚‚UnitIso_hom_app, equivalenceâ‚UnitIso_eq, equivalenceâ‚UnitIso_hom_app,
      assoc, NatIso.cancel_natIso_hom_left]
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalenceâ‚‚_unit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceâ‚‚UnitIso_eq

variable {eB}

/-- The equivalence `A â‰… B` whose functor is `F â‹™ eB.inverse` and
whose inverse is `G : B â‰… A`. -/
@[simps! inverse]
def equivalence : A â‰Œ B :=
  ((equivalenceâ‚‚ eB hF).changeInverse
    (calc eB.functor â‹™ e'.inverse â‹™ eA.inverse â‰…
        (eB.functor â‹™ e'.inverse) â‹™ eA.inverse := (Functor.associator _ _ _).symm
    _ â‰… (G â‹™ eA.functor) â‹™ eA.inverse := isoWhiskerRight hG _
    _ â‰… G â‹™ ğŸ­ A := isoWhiskerLeft _ eA.unitIso.symm
    _ â‰… G := G.rightUnitor))
#align algebraic_topology.dold_kan.compatibility.equivalence AlgebraicTopology.DoldKan.Compatibility.equivalence

theorem equivalence_functor : (equivalence hF hG).functor = F â‹™ eB.inverse :=
  rfl
#align algebraic_topology.dold_kan.compatibility.equivalence_functor AlgebraicTopology.DoldKan.Compatibility.equivalence_functor

/-- The isomorphism `eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor` deduced
from the counit isomorphism of `e'`. -/
@[simps! hom_app]
def Ï„â‚€ : eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor :=
  calc
    eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor â‹™ ğŸ­ _ := isoWhiskerLeft _ e'.counitIso
    _ â‰… eB.functor := Functor.rightUnitor _
#align algebraic_topology.dold_kan.compatibility.Ï„â‚€ AlgebraicTopology.DoldKan.Compatibility.Ï„â‚€

/-- The isomorphism `eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor` deduced
from the isomorphisms `hF : eA.functor â‹™ e'.functor â‰… F`,
`hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor` and the datum of
an isomorphism `Î· : G â‹™ F â‰… eB.functor`. -/
@[simps! hom_app]
def Ï„â‚ (Î· : G â‹™ F â‰… eB.functor) : eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor :=
  calc
    eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… (eB.functor â‹™ e'.inverse) â‹™ e'.functor :=
        Iso.refl _
    _ â‰… (G â‹™ eA.functor) â‹™ e'.functor := isoWhiskerRight hG _
    _ â‰… G â‹™ eA.functor â‹™ e'.functor := by rfl
    _ â‰… G â‹™ F := isoWhiskerLeft _ hF
    _ â‰… eB.functor := Î·
#align algebraic_topology.dold_kan.compatibility.Ï„â‚ AlgebraicTopology.DoldKan.Compatibility.Ï„â‚

variable (Î· : G â‹™ F â‰… eB.functor) (hÎ· : Ï„â‚€ = Ï„â‚ hF hG Î·)

/-- The counit isomorphism of `equivalence`. -/
@[simps!]
def equivalenceCounitIso : G â‹™ F â‹™ eB.inverse â‰… ğŸ­ B :=
  calc
    G â‹™ F â‹™ eB.inverse â‰… (G â‹™ F) â‹™ eB.inverse := Iso.refl _
    _ â‰… eB.functor â‹™ eB.inverse := isoWhiskerRight Î· _
    _ â‰… ğŸ­ B := eB.unitIso.symm
#align algebraic_topology.dold_kan.compatibility.equivalence_counit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceCounitIso

variable {Î· hF hG}

theorem equivalenceCounitIso_eq : (equivalence hF hG).counitIso = equivalenceCounitIso Î· := by
  ext1; apply NatTrans.ext; ext Y
  dsimp [equivalence]
  simp only [comp_id, id_comp, Functor.map_comp, equivalenceâ‚‚CounitIso_eq,
    equivalenceâ‚‚CounitIso_hom_app, assoc, equivalenceCounitIso_hom_app]
  simp only [â† eB.inverse.map_comp_assoc, â† Ï„â‚€_hom_app, hÎ·, Ï„â‚_hom_app]
  erw [hF.inv.naturality_assoc, hF.inv.naturality_assoc]
  dsimp
  congr 2
  simp only [â† e'.functor.map_comp_assoc, Equivalence.fun_inv_map, assoc,
    Iso.inv_hom_id_app_assoc, hG.inv_hom_id_app]
  dsimp
  rw [comp_id, eA.functor_unitIso_comp, e'.functor.map_id, id_comp, hF.inv_hom_id_app_assoc]
#align algebraic_topology.dold_kan.compatibility.equivalence_counit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceCounitIso_eq

variable (hF)

/-- The isomorphism `eA.functor â‰… F â‹™ e'.inverse` deduced from the
unit isomorphism of `e'` and the isomorphism `hF : eA.functor â‹™ e'.functor â‰… F`. -/
@[simps!]
def Ï… : eA.functor â‰… F â‹™ e'.inverse :=
  calc
    eA.functor â‰… eA.functor â‹™ ğŸ­ A' := (Functor.leftUnitor _).symm
    _ â‰… eA.functor â‹™ e'.functor â‹™ e'.inverse := isoWhiskerLeft _ e'.unitIso
    _ â‰… (eA.functor â‹™ e'.functor) â‹™ e'.inverse := Iso.refl _
    _ â‰… F â‹™ e'.inverse := isoWhiskerRight hF _
#align algebraic_topology.dold_kan.compatibility.Ï… AlgebraicTopology.DoldKan.Compatibility.Ï…

variable (Îµ : eA.functor â‰… F â‹™ e'.inverse) (hÎµ : Ï… hF = Îµ) (hG)

/-- The unit isomorphism of `equivalence`. -/
@[simps!]
def equivalenceUnitIso : ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ G :=
  calc
    ğŸ­ A â‰… eA.functor â‹™ eA.inverse := eA.unitIso
    _ â‰… (F â‹™ e'.inverse) â‹™ eA.inverse := isoWhiskerRight Îµ _
    _ â‰… F â‹™ ğŸ­ B' â‹™ e'.inverse â‹™ eA.inverse := Iso.refl _
    _ â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight eB.counitIso.symm _)
    _ â‰… (F â‹™ eB.inverse) â‹™ (eB.functor â‹™ e'.inverse) â‹™ eA.inverse := Iso.refl _
    _ â‰… (F â‹™ eB.inverse) â‹™ (G â‹™ eA.functor) â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight hG _)
    _ â‰… (F â‹™ eB.inverse â‹™ G) â‹™ eA.functor â‹™ eA.inverse := Iso.refl _
    _ â‰… (F â‹™ eB.inverse â‹™ G) â‹™ ğŸ­ A := isoWhiskerLeft _ eA.unitIso.symm
    _ â‰… (F â‹™ eB.inverse) â‹™ G := Iso.refl _
#align algebraic_topology.dold_kan.compatibility.equivalence_unit_iso AlgebraicTopology.DoldKan.Compatibility.equivalenceUnitIso

variable {Îµ hF hG}

theorem equivalenceUnitIso_eq : (equivalence hF hG).unitIso = equivalenceUnitIso hG Îµ := by
  ext1; apply NatTrans.ext; ext X
  dsimp [equivalence]
  simp only [assoc, comp_id, equivalenceUnitIso_hom_app]
  erw [id_comp]
  simp only [equivalenceâ‚‚UnitIso_eq eB hF, equivalenceâ‚‚UnitIso_hom_app,
    â† eA.inverse.map_comp_assoc, assoc, â† hÎµ, Ï…_hom_app]
#align algebraic_topology.dold_kan.compatibility.equivalence_unit_iso_eq AlgebraicTopology.DoldKan.Compatibility.equivalenceUnitIso_eq

end Compatibility

end DoldKan

end AlgebraicTopology
