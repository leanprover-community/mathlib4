/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Equivalence

/-! Tools for compatibilities between Dold-Kan equivalences

The purpose of this file is to introduce tools which will enable the
construction of the Dold-Kan equivalence `SimplicialObject C â‰Œ ChainComplex C â„•`
for a pseudoabelian category `C` from the equivalence
`Karoubi (SimplicialObject C) â‰Œ Karoubi (ChainComplex C â„•)` and the two
equivalences `simplicial_object C â‰… Karoubi (SimplicialObject C)` and
`ChainComplex C â„• â‰… Karoubi (ChainComplex C â„•)`.

It is certainly possible to get an equivalence `SimplicialObject C â‰Œ ChainComplex C â„•`
using a compositions of the three equivalences above, but then neither the functor
nor the inverse would have good definitional properties. For example, it would be better
if the inverse functor of the equivalence was exactly the functor
`Î“â‚€ : SimplicialObject C â¥¤ ChainComplex C â„•` which was constructed in `FunctorGamma.lean`.

In this file, given four categories `A`, `A'`, `B`, `B'`, equivalences `eA : A â‰… A'`,
`eB : B â‰… B'`, `e' : A' â‰… B'`, functors `F : A â¥¤ B'`, `G : B â¥¤ A` equipped with certain
compatibilities, we construct successive equivalences:
- `equivalenceâ‚€` from `A` to `B'`, which is the composition of `eA` and `e'`.
- `equivalenceâ‚` from `A` to `B'`, with the same inverse functor as `equivalenceâ‚€`,
but whose functor is `F`.
- `equivalenceâ‚‚` from `A` to `B`, which is the composition of `equivalenceâ‚` and the
inverse of `eB`:
- `equivalence` from `A` to `B`, which has the same functor `F â‹™ eB.inverse` as `equivalenceâ‚‚`,
but whose inverse functor is `G`.

When extra assumptions are given, we shall also provide simplification lemmas for the
unit and counit isomorphisms of `equivalence`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category Functor

namespace AlgebraicTopology

namespace DoldKan

namespace Compatibility

variable {A A' B B' : Type*} [Category A] [Category A'] [Category B] [Category B'] (eA : A â‰Œ A')
  (eB : B â‰Œ B') (e' : A' â‰Œ B') {F : A â¥¤ B'} (hF : eA.functor â‹™ e'.functor â‰… F) {G : B â¥¤ A}
  (hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor)

/-- A basic equivalence `A â‰… B'` obtained by composing `eA : A â‰… A'` and `e' : A' â‰… B'`. -/
@[simps! functor inverse unitIso_hom_app]
def equivalenceâ‚€ : A â‰Œ B' :=
  eA.trans e'

variable {eA} {e'}

/-- An intermediate equivalence `A â‰… B'` whose functor is `F` and whose inverse is
`e'.inverse â‹™ eA.inverse`. -/
@[simps! functor]
def equivalenceâ‚ : A â‰Œ B' := (equivalenceâ‚€ eA e').changeFunctor hF

theorem equivalenceâ‚_inverse : (equivalenceâ‚ hF).inverse = e'.inverse â‹™ eA.inverse :=
  rfl

/-- The counit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps!]
def equivalenceâ‚CounitIso : (e'.inverse â‹™ eA.inverse) â‹™ F â‰… ğŸ­ B' :=
  calc
    (e'.inverse â‹™ eA.inverse) â‹™ F â‰… (e'.inverse â‹™ eA.inverse) â‹™ eA.functor â‹™ e'.functor :=
      isoWhiskerLeft _ hF.symm
    _ â‰… e'.inverse â‹™ (eA.inverse â‹™ eA.functor â‹™ e'.functor) := associator _ _ _
    _ â‰… e'.inverse â‹™ (eA.inverse â‹™ eA.functor) â‹™ e'.functor :=
      isoWhiskerLeft _ (associator _ _ _).symm
    _ â‰… e'.inverse â‹™ ğŸ­ _ â‹™ e'.functor := isoWhiskerLeft _ (isoWhiskerRight eA.counitIso _)
    _ â‰… e'.inverse â‹™ e'.functor := isoWhiskerLeft _ (leftUnitor _)
    _ â‰… ğŸ­ B' := e'.counitIso

theorem equivalenceâ‚CounitIso_eq : (equivalenceâ‚ hF).counitIso = equivalenceâ‚CounitIso hF := by
  ext Y
  simp [equivalenceâ‚, equivalenceâ‚€]

/-- The unit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps!]
def equivalenceâ‚UnitIso : ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse :=
  calc
    ğŸ­ A â‰… eA.functor â‹™ eA.inverse := eA.unitIso
    _ â‰… eA.functor â‹™ ğŸ­ A' â‹™ eA.inverse := isoWhiskerLeft _ (leftUnitor _).symm
    _ â‰… eA.functor â‹™ (e'.functor â‹™ e'.inverse) â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight e'.unitIso _)
    _ â‰… eA.functor â‹™ (e'.functor â‹™ e'.inverse â‹™ eA.inverse) :=
      isoWhiskerLeft _ (associator _ _ _)
    _ â‰… (eA.functor â‹™ e'.functor) â‹™ e'.inverse â‹™ eA.inverse := (associator _ _ _).symm
    _ â‰… F â‹™ e'.inverse â‹™ eA.inverse := isoWhiskerRight hF _

theorem equivalenceâ‚UnitIso_eq : (equivalenceâ‚ hF).unitIso = equivalenceâ‚UnitIso hF := by
  ext X
  simp [equivalenceâ‚]

/-- An intermediate equivalence `A â‰… B` obtained as the composition of `equivalenceâ‚` and
the inverse of `eB : B â‰Œ B'`. -/
@[simps! functor]
def equivalenceâ‚‚ : A â‰Œ B :=
  (equivalenceâ‚ hF).trans eB.symm

theorem equivalenceâ‚‚_inverse :
    (equivalenceâ‚‚ eB hF).inverse = eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
  rfl

/-- The counit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps!]
def equivalenceâ‚‚CounitIso : (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse â‰… ğŸ­ B :=
  calc
    (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse
      â‰… eB.functor â‹™ (e'.inverse â‹™ eA.inverse) â‹™ F â‹™ eB.inverse := associator _ _ _
    _ â‰… eB.functor â‹™ ((e'.inverse â‹™ eA.inverse) â‹™ F) â‹™ eB.inverse :=
      isoWhiskerLeft _ (associator _ _ _).symm
    _ â‰… eB.functor â‹™ ğŸ­ _ â‹™ eB.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight (equivalenceâ‚CounitIso hF) _)
    _ â‰… eB.functor â‹™ eB.inverse := isoWhiskerLeft _ (leftUnitor _)
    _ â‰… ğŸ­ B := eB.unitIso.symm

theorem equivalenceâ‚‚CounitIso_eq :
    (equivalenceâ‚‚ eB hF).counitIso = equivalenceâ‚‚CounitIso eB hF := by
  ext Y'
  simp [equivalenceâ‚‚, equivalenceâ‚CounitIso_eq]

/-- The unit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps!]
def equivalenceâ‚‚UnitIso : ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
  calc
    ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse := equivalenceâ‚UnitIso hF
    _ â‰… F â‹™ ğŸ­ B' â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerLeft _ (leftUnitor _).symm
    _ â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight eB.counitIso.symm _)
    _ â‰… (F â‹™ eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      (associator _ _ _).symm
    _ â‰… ((F â‹™ eB.inverse) â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerRight (associator _ _ _).symm _
    _ â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse :=
      associator _ _ _

theorem equivalenceâ‚‚UnitIso_eq : (equivalenceâ‚‚ eB hF).unitIso = equivalenceâ‚‚UnitIso eB hF := by
  ext X
  simp [equivalenceâ‚‚, equivalenceâ‚]

variable {eB}

/-- The equivalence `A â‰… B` whose functor is `F â‹™ eB.inverse` and
whose inverse is `G : B â‰… A`. -/
@[simps! inverse]
def equivalence : A â‰Œ B :=
  ((equivalenceâ‚‚ eB hF).changeInverse
    (calc eB.functor â‹™ e'.inverse â‹™ eA.inverse â‰…
        (eB.functor â‹™ e'.inverse) â‹™ eA.inverse := (associator _ _ _).symm
    _ â‰… (G â‹™ eA.functor) â‹™ eA.inverse := isoWhiskerRight hG _
    _ â‰… G â‹™ eA.functor â‹™ eA.inverse := associator _ _ _
    _ â‰… G â‹™ ğŸ­ A := isoWhiskerLeft _ eA.unitIso.symm
    _ â‰… G := G.rightUnitor))

theorem equivalence_functor : (equivalence hF hG).functor = F â‹™ eB.inverse :=
  rfl

/-- The isomorphism `eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor` deduced
from the counit isomorphism of `e'`. -/
@[simps! hom_app]
def Ï„â‚€ : eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor :=
  calc
    eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor â‹™ ğŸ­ _ := isoWhiskerLeft _ e'.counitIso
    _ â‰… eB.functor := Functor.rightUnitor _

/-- The isomorphism `eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor` deduced
from the isomorphisms `hF : eA.functor â‹™ e'.functor â‰… F`,
`hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor` and the datum of
an isomorphism `Î· : G â‹™ F â‰… eB.functor`. -/
@[simps! hom_app]
def Ï„â‚ (Î· : G â‹™ F â‰… eB.functor) : eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor :=
  calc
    eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… (eB.functor â‹™ e'.inverse) â‹™ e'.functor :=
        (associator _ _ _).symm
    _ â‰… (G â‹™ eA.functor) â‹™ e'.functor := isoWhiskerRight hG _
    _ â‰… G â‹™ eA.functor â‹™ e'.functor := associator _ _ _
    _ â‰… G â‹™ F := isoWhiskerLeft _ hF
    _ â‰… eB.functor := Î·

variable (Î· : G â‹™ F â‰… eB.functor)

/-- The counit isomorphism of `equivalence`. -/
@[simps!]
def equivalenceCounitIso : G â‹™ F â‹™ eB.inverse â‰… ğŸ­ B :=
  calc
    G â‹™ F â‹™ eB.inverse â‰… (G â‹™ F) â‹™ eB.inverse := (associator _ _ _).symm
    _ â‰… eB.functor â‹™ eB.inverse := isoWhiskerRight Î· _
    _ â‰… ğŸ­ B := eB.unitIso.symm

variable {Î· hF hG}

theorem equivalenceCounitIso_eq (hÎ· : Ï„â‚€ = Ï„â‚ hF hG Î·) :
    (equivalence hF hG).counitIso = equivalenceCounitIso Î· := by
  ext1; apply NatTrans.ext; ext Y
  dsimp [equivalence]
  simp only [comp_id, id_comp, Functor.map_comp, equivalenceâ‚‚CounitIso_eq,
    equivalenceâ‚‚CounitIso_hom_app, assoc, equivalenceCounitIso_hom_app]
  simp only [equivalenceâ‚‚_inverse, comp_obj, â† Ï„â‚€_hom_app, hÎ·, Ï„â‚_hom_app, â†
    eB.inverse.map_comp_assoc]
  rw [hF.inv.naturality_assoc, hF.inv.naturality_assoc]
  congr 2
  simp only [â† e'.functor.map_comp_assoc]
  simp only [Functor.comp_map, Equivalence.fun_inv_map, comp_obj, id_obj, map_comp, assoc]
  simp only [â† e'.functor.map_comp_assoc]
  simp only [Iso.inv_hom_id_app_assoc, Iso.inv_hom_id_app, comp_obj, comp_id,
    Equivalence.functor_unit_comp, map_id, id_comp]

variable (hF)

/-- The isomorphism `eA.functor â‰… F â‹™ e'.inverse` deduced from the
unit isomorphism of `e'` and the isomorphism `hF : eA.functor â‹™ e'.functor â‰… F`. -/
@[simps!]
def Ï… : eA.functor â‰… F â‹™ e'.inverse :=
  calc
    eA.functor â‰… eA.functor â‹™ ğŸ­ A' := (rightUnitor _).symm
    _ â‰… eA.functor â‹™ e'.functor â‹™ e'.inverse := isoWhiskerLeft _ e'.unitIso
    _ â‰… (eA.functor â‹™ e'.functor) â‹™ e'.inverse := (associator _ _ _).symm
    _ â‰… F â‹™ e'.inverse := isoWhiskerRight hF _

variable (Îµ : eA.functor â‰… F â‹™ e'.inverse) (hG)

/-- The unit isomorphism of `equivalence`. -/
@[simps!]
def equivalenceUnitIso : ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ G :=
  calc
    ğŸ­ A â‰… eA.functor â‹™ eA.inverse := eA.unitIso
    _ â‰… (F â‹™ e'.inverse) â‹™ eA.inverse := isoWhiskerRight Îµ _
    _ â‰… F â‹™ e'.inverse â‹™ eA.inverse := associator _ _ _
    _ â‰… F â‹™ ğŸ­ B' â‹™ e'.inverse â‹™ eA.inverse := isoWhiskerLeft _ (leftUnitor _).symm
    _ â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight eB.counitIso.symm _)
    _ â‰… (F â‹™ eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse := (associator _ _ _).symm
    _ â‰… ((F â‹™ eB.inverse) â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :=
      isoWhiskerRight (associator _ _ _).symm _
    _ â‰… (F â‹™ eB.inverse) â‹™ eB.functor â‹™ e'.inverse â‹™ eA.inverse := associator _ _ _
    _ â‰… (F â‹™ eB.inverse) â‹™ (eB.functor â‹™ e'.inverse) â‹™ eA.inverse :=
      isoWhiskerLeft _ (associator _ _ _).symm
    _ â‰… (F â‹™ eB.inverse) â‹™ (G â‹™ eA.functor) â‹™ eA.inverse :=
      isoWhiskerLeft _ (isoWhiskerRight hG _)
    _ â‰… ((F â‹™ eB.inverse) â‹™ G â‹™ eA.functor) â‹™ eA.inverse := (associator _ _ _).symm
    _ â‰… (((F â‹™ eB.inverse) â‹™ G) â‹™ eA.functor) â‹™ eA.inverse :=
      isoWhiskerRight (associator _ _ _).symm _
    _ â‰… ((F â‹™ eB.inverse) â‹™ G) â‹™ eA.functor â‹™ eA.inverse := associator _ _ _
    _ â‰… ((F â‹™ eB.inverse) â‹™ G) â‹™ ğŸ­ A := isoWhiskerLeft _ eA.unitIso.symm
    _ â‰… (F â‹™ eB.inverse) â‹™ G := rightUnitor _

variable {Îµ hF hG}

theorem equivalenceUnitIso_eq (hÎµ : Ï… hF = Îµ) :
    (equivalence hF hG).unitIso = equivalenceUnitIso hG Îµ := by
  ext1; apply NatTrans.ext; ext X
  dsimp [equivalence]
  simp only [assoc, comp_id, equivalenceUnitIso_hom_app, equivalenceâ‚‚_inverse, Functor.comp_obj,
    id_comp, equivalenceâ‚‚UnitIso_eq eB hF, equivalenceâ‚‚UnitIso_hom_app,
    â† eA.inverse.map_comp_assoc, assoc, â† hÎµ, Ï…_hom_app]

end Compatibility

end DoldKan

end AlgebraicTopology
