/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.FunctorGamma
import Mathlib.CategoryTheory.Idempotents.HomologicalComplex

#align_import algebraic_topology.dold_kan.gamma_comp_n from "leanprover-community/mathlib"@"5f68029a863bdf76029fa0f7a519e6163c14152e"

/-! The counit isomorphism of the Dold-Kan equivalence

The purpose of this file is to construct natural isomorphisms
`Nâ‚Î“â‚€ : Î“â‚€ â‹™ Nâ‚ â‰… toKaroubi (ChainComplex C â„•)`
and `Nâ‚‚Î“â‚‚ : Î“â‚‚ â‹™ Nâ‚‚ â‰… ğŸ­ (Karoubi (ChainComplex C â„•))`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits
  CategoryTheory.Idempotents Opposite SimplicialObject Simplicial

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C] [HasFiniteCoproducts C]

/-- The isomorphism `(Î“â‚€.splitting K).nondegComplex â‰… K` for all `K : ChainComplex C â„•`. -/
@[simps!]
def Î“â‚€NondegComplexIso (K : ChainComplex C â„•) : (Î“â‚€.splitting K).nondegComplex â‰… K :=
  HomologicalComplex.Hom.isoOfComponents (fun n => Iso.refl _)
    (by
      rintro _ n (rfl : n + 1 = _)
      dsimp
      simp only [id_comp, comp_id, AlternatingFaceMapComplex.obj_d_eq, Preadditive.sum_comp,
        Preadditive.comp_sum]
      rw [Fintype.sum_eq_single (0 : Fin (n + 2))]
      Â· simp only [Fin.val_zero, pow_zero, one_zsmul]
        erw [Î“â‚€.Obj.mapMono_on_summand_id_assoc, Î“â‚€.Obj.Termwise.mapMono_Î´â‚€,
          Splitting.Î¹_Ï€Summand_eq_id, comp_id]
      Â· intro i hi
        dsimp
        simp only [Preadditive.zsmul_comp, Preadditive.comp_zsmul, assoc]
        erw [Î“â‚€.Obj.mapMono_on_summand_id_assoc, Î“â‚€.Obj.Termwise.mapMono_eq_zero, zero_comp,
          zsmul_zero]
        Â· intro h
          replace h := congr_arg SimplexCategory.len h
          change n + 1 = n at h
          linarith
        Â· simpa only [IsÎ´â‚€.iff] using hi)
#align algebraic_topology.dold_kan.Î“â‚€_nondeg_complex_iso AlgebraicTopology.DoldKan.Î“â‚€NondegComplexIso

/-- The natural isomorphism `(Î“â‚€.splitting K).nondegComplex â‰… K` for `K : ChainComplex C â„•`. -/
def Î“â‚€'CompNondegComplexFunctor : Î“â‚€' â‹™ Split.nondegComplexFunctor â‰… ğŸ­ (ChainComplex C â„•) :=
  NatIso.ofComponents Î“â‚€NondegComplexIso
#align algebraic_topology.dold_kan.Î“â‚€'_comp_nondeg_complex_functor AlgebraicTopology.DoldKan.Î“â‚€'CompNondegComplexFunctor

/-- The natural isomorphism `Î“â‚€ â‹™ Nâ‚ â‰… toKaroubi (ChainComplex C â„•)`. -/
def Nâ‚Î“â‚€ : Î“â‚€ â‹™ Nâ‚ â‰… toKaroubi (ChainComplex C â„•) :=
  calc
    Î“â‚€ â‹™ Nâ‚ â‰… Î“â‚€' â‹™ Split.forget C â‹™ Nâ‚ := Functor.associator _ _ _
    _ â‰… Î“â‚€' â‹™ Split.nondegComplexFunctor â‹™ toKaroubi _ :=
      (isoWhiskerLeft Î“â‚€' Split.toKaroubiNondegComplexFunctorIsoNâ‚.symm)
    _ â‰… (Î“â‚€' â‹™ Split.nondegComplexFunctor) â‹™ toKaroubi _ := (Functor.associator _ _ _).symm
    _ â‰… ğŸ­ _ â‹™ toKaroubi (ChainComplex C â„•) := (isoWhiskerRight Î“â‚€'CompNondegComplexFunctor _)
    _ â‰… toKaroubi (ChainComplex C â„•) := Functor.leftUnitor _
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚Î“â‚€ AlgebraicTopology.DoldKan.Nâ‚Î“â‚€

theorem Nâ‚Î“â‚€_app (K : ChainComplex C â„•) :
    Nâ‚Î“â‚€.app K = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.symm â‰ªâ‰«
      (toKaroubi _).mapIso (Î“â‚€NondegComplexIso K) := by
  ext1
  dsimp [Nâ‚Î“â‚€]
  erw [id_comp, comp_id, comp_id]
  rfl
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚Î“â‚€_app AlgebraicTopology.DoldKan.Nâ‚Î“â‚€_app

theorem Nâ‚Î“â‚€_hom_app (K : ChainComplex C â„•) :
    Nâ‚Î“â‚€.hom.app K = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.inv â‰«
        (toKaroubi _).map (Î“â‚€NondegComplexIso K).hom := by
  change (Nâ‚Î“â‚€.app K).hom = _
  simp only [Nâ‚Î“â‚€_app]
  rfl
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚Î“â‚€_hom_app AlgebraicTopology.DoldKan.Nâ‚Î“â‚€_hom_app

theorem Nâ‚Î“â‚€_inv_app (K : ChainComplex C â„•) :
    Nâ‚Î“â‚€.inv.app K = (toKaroubi _).map (Î“â‚€NondegComplexIso K).inv â‰«
        (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.hom := by
  change (Nâ‚Î“â‚€.app K).inv = _
  simp only [Nâ‚Î“â‚€_app]
  rfl
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚Î“â‚€_inv_app AlgebraicTopology.DoldKan.Nâ‚Î“â‚€_inv_app

@[simp]
theorem Nâ‚Î“â‚€_hom_app_f_f (K : ChainComplex C â„•) (n : â„•) :
    (Nâ‚Î“â‚€.hom.app K).f.f n = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.inv.f.f n := by
  rw [Nâ‚Î“â‚€_hom_app]
  apply comp_id
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚Î“â‚€_hom_app_f_f AlgebraicTopology.DoldKan.Nâ‚Î“â‚€_hom_app_f_f

@[simp]
theorem Nâ‚Î“â‚€_inv_app_f_f (K : ChainComplex C â„•) (n : â„•) :
    (Nâ‚Î“â‚€.inv.app K).f.f n = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.hom.f.f n := by
  rw [Nâ‚Î“â‚€_inv_app]
  apply id_comp
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚Î“â‚€_inv_app_f_f AlgebraicTopology.DoldKan.Nâ‚Î“â‚€_inv_app_f_f

-- Porting note: added to speed up elaboration
attribute [irreducible] Nâ‚Î“â‚€

theorem Nâ‚‚Î“â‚‚_toKaroubi : toKaroubi (ChainComplex C â„•) â‹™ Î“â‚‚ â‹™ Nâ‚‚ = Î“â‚€ â‹™ Nâ‚ := by
  have h := Functor.congr_obj (functorExtensionâ‚‚_comp_whiskeringLeft_toKaroubi
    (ChainComplex C â„•) (SimplicialObject C)) Î“â‚€
  have h' := Functor.congr_obj (functorExtensionâ‚_comp_whiskeringLeft_toKaroubi
    (SimplicialObject C) (ChainComplex C â„•)) Nâ‚
  dsimp [Nâ‚‚, Î“â‚‚, functorExtensionâ‚] at h h' âŠ¢
  rw [â† Functor.assoc, h, Functor.assoc, h']
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚‚Î“â‚‚_to_karoubi AlgebraicTopology.DoldKan.Nâ‚‚Î“â‚‚_toKaroubi

/-- Compatibility isomorphism between `toKaroubi _ â‹™ Î“â‚‚ â‹™ Nâ‚‚` and `Î“â‚€ â‹™ Nâ‚` which
are functors `ChainComplex C â„• â¥¤ Karoubi (ChainComplex C â„•)`. -/
@[simps!]
def Nâ‚‚Î“â‚‚ToKaroubiIso : toKaroubi (ChainComplex C â„•) â‹™ Î“â‚‚ â‹™ Nâ‚‚ â‰… Î“â‚€ â‹™ Nâ‚ :=
  eqToIso Nâ‚‚Î“â‚‚_toKaroubi
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚‚Î“â‚‚_to_karoubi_iso AlgebraicTopology.DoldKan.Nâ‚‚Î“â‚‚ToKaroubiIso

-- Porting note: added to speed up elaboration
attribute [irreducible] Nâ‚‚Î“â‚‚ToKaroubiIso

/-- The counit isomorphism of the Dold-Kan equivalence for additive categories. -/
def Nâ‚‚Î“â‚‚ : Î“â‚‚ â‹™ Nâ‚‚ â‰… ğŸ­ (Karoubi (ChainComplex C â„•)) :=
  ((whiskeringLeft _ _ _).obj (toKaroubi (ChainComplex C â„•))).preimageIso
      (Nâ‚‚Î“â‚‚ToKaroubiIso â‰ªâ‰« Nâ‚Î“â‚€)
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚‚Î“â‚‚ AlgebraicTopology.DoldKan.Nâ‚‚Î“â‚‚

@[simp]
theorem Nâ‚‚Î“â‚‚_inv_app_f_f (X : Karoubi (ChainComplex C â„•)) (n : â„•) :
    (Nâ‚‚Î“â‚‚.inv.app X).f.f n =
      X.p.f n â‰« (Î“â‚€.splitting X.X).Î¹Summand (Splitting.IndexSet.id (op [n])) := by
  simp only [Nâ‚‚Î“â‚‚, Functor.preimageIso, Iso.trans,
    whiskeringLeft_obj_preimage_app, Nâ‚‚Î“â‚‚ToKaroubiIso_inv, assoc,
    Functor.id_map, NatTrans.comp_app, eqToHom_app, Karoubi.comp_f,
    Karoubi.eqToHom_f, Karoubi.decompId_p_f, HomologicalComplex.comp_f,
    Nâ‚Î“â‚€_inv_app_f_f, Splitting.toKaroubiNondegComplexIsoNâ‚_hom_f_f,
    Functor.comp_map, Functor.comp_obj, Karoubi.decompId_i_f,
    eqToHom_refl, comp_id, Nâ‚‚_map_f_f, Î“â‚‚_map_f_app, Nâ‚_obj_p,
    PInfty_on_Î“â‚€_splitting_summand_eq_self_assoc, toKaroubi_obj_X,
    Splitting.Î¹_desc, Splitting.IndexSet.id_fst, SimplexCategory.len_mk, unop_op,
    Karoubi.HomologicalComplex.p_idem_assoc]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚‚Î“â‚‚_inv_app_f_f AlgebraicTopology.DoldKan.Nâ‚‚Î“â‚‚_inv_app_f_f

-- porting note: added to ease the proof of `Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€`
lemma whiskerLeft_toKaroubi_Nâ‚‚Î“â‚‚_hom :
    whiskerLeft (toKaroubi (ChainComplex C â„•)) Nâ‚‚Î“â‚‚.hom = Nâ‚‚Î“â‚‚ToKaroubiIso.hom â‰« Nâ‚Î“â‚€.hom := by
  let e : _ â‰… toKaroubi (ChainComplex C â„•) â‹™ ğŸ­ _ := Nâ‚‚Î“â‚‚ToKaroubiIso â‰ªâ‰« Nâ‚Î“â‚€
  have h := ((whiskeringLeft _ _ (Karoubi (ChainComplex C â„•))).obj
    (toKaroubi (ChainComplex C â„•))).image_preimage e.hom
  dsimp only [whiskeringLeft, Nâ‚‚Î“â‚‚, Functor.preimageIso] at h âŠ¢
  exact h

-- Porting note: added to speed up elaboration
attribute [irreducible] Nâ‚‚Î“â‚‚

theorem Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€ (K : ChainComplex C â„•) :
    Nâ‚‚Î“â‚‚.hom.app ((toKaroubi _).obj K) = Nâ‚‚Î“â‚‚ToKaroubiIso.hom.app K â‰« Nâ‚Î“â‚€.hom.app K :=
  congr_app whiskerLeft_toKaroubi_Nâ‚‚Î“â‚‚_hom K
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€ AlgebraicTopology.DoldKan.Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€

end DoldKan

end AlgebraicTopology
