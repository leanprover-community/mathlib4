/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.FunctorGamma
import Mathlib.AlgebraicTopology.DoldKan.SplitSimplicialObject
import Mathlib.CategoryTheory.Idempotents.HomologicalComplex
import Mathlib.Tactic.SuppressCompilation

/-! The counit isomorphism of the Dold-Kan equivalence

The purpose of this file is to construct natural isomorphisms
`Nâ‚Î“â‚€ : Î“â‚€ â‹™ Nâ‚ â‰… toKaroubi (ChainComplex C â„•)`
and `Nâ‚‚Î“â‚‚ : Î“â‚‚ â‹™ Nâ‚‚ â‰… ğŸ­ (Karoubi (ChainComplex C â„•))`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/

suppress_compilation

noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits
  CategoryTheory.Idempotents Opposite SimplicialObject Simplicial

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C] [HasFiniteCoproducts C]

/-- The isomorphism `(Î“â‚€.splitting K).nondegComplex â‰… K` for all `K : ChainComplex C â„•`. -/
@[simps!]
def Î“â‚€NondegComplexIso (K : ChainComplex C â„•) : (Î“â‚€.splitting K).nondegComplex â‰… K :=
  HomologicalComplex.Hom.isoOfComponents (fun _ => Iso.refl _)
    (by
      rintro _ n (rfl : n + 1 = _)
      dsimp
      simp only [id_comp, comp_id, AlternatingFaceMapComplex.obj_d_eq, Preadditive.sum_comp,
        Preadditive.comp_sum]
      rw [Fintype.sum_eq_single (0 : Fin (n + 2))]
      Â· simp only [Fin.val_zero, pow_zero, one_zsmul]
        rw [Î´, Î“â‚€.Obj.mapMono_on_summand_id_assoc, Î“â‚€.Obj.Termwise.mapMono_Î´â‚€,
          Splitting.cofan_inj_Ï€Summand_eq_id]
        dsimp only [Î“â‚€.splitting, Splitting.summand.eq_1, Splitting.IndexSet.id_fst]
        rw [comp_id]
      Â· intro i hi
        dsimp
        simp only [Preadditive.zsmul_comp, Preadditive.comp_zsmul, assoc]
        rw [Î´, Î“â‚€.Obj.mapMono_on_summand_id_assoc, Î“â‚€.Obj.Termwise.mapMono_eq_zero, zero_comp,
          zsmul_zero]
        Â· intro h
          replace h := congr_arg SimplexCategory.len h
          change n + 1 = n at h
          omega
        Â· simpa only [IsÎ´â‚€.iff] using hi)

/-- The natural isomorphism `(Î“â‚€.splitting K).nondegComplex â‰… K` for `K : ChainComplex C â„•`. -/
def Î“â‚€'CompNondegComplexFunctor : Î“â‚€' â‹™ Split.nondegComplexFunctor â‰… ğŸ­ (ChainComplex C â„•) :=
  NatIso.ofComponents Î“â‚€NondegComplexIso

/-- The natural isomorphism `Î“â‚€ â‹™ Nâ‚ â‰… toKaroubi (ChainComplex C â„•)`. -/
def Nâ‚Î“â‚€ : Î“â‚€ â‹™ Nâ‚ â‰… toKaroubi (ChainComplex C â„•) :=
  calc
    Î“â‚€ â‹™ Nâ‚ â‰… Î“â‚€' â‹™ Split.forget C â‹™ Nâ‚ := Functor.associator _ _ _
    _ â‰… Î“â‚€' â‹™ Split.nondegComplexFunctor â‹™ toKaroubi _ :=
      (isoWhiskerLeft Î“â‚€' Split.toKaroubiNondegComplexFunctorIsoNâ‚.symm)
    _ â‰… (Î“â‚€' â‹™ Split.nondegComplexFunctor) â‹™ toKaroubi _ := (Functor.associator _ _ _).symm
    _ â‰… ğŸ­ _ â‹™ toKaroubi (ChainComplex C â„•) := isoWhiskerRight Î“â‚€'CompNondegComplexFunctor _
    _ â‰… toKaroubi (ChainComplex C â„•) := Functor.leftUnitor _

theorem Nâ‚Î“â‚€_app (K : ChainComplex C â„•) :
    Nâ‚Î“â‚€.app K = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.symm â‰ªâ‰«
      (toKaroubi _).mapIso (Î“â‚€NondegComplexIso K) := by
  ext1
  dsimp [Nâ‚Î“â‚€]
  erw [id_comp, comp_id, comp_id]
  rfl

theorem Nâ‚Î“â‚€_hom_app (K : ChainComplex C â„•) :
    Nâ‚Î“â‚€.hom.app K = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.inv â‰«
        (toKaroubi _).map (Î“â‚€NondegComplexIso K).hom := by
  change (Nâ‚Î“â‚€.app K).hom = _
  simp only [Nâ‚Î“â‚€_app]
  rfl

theorem Nâ‚Î“â‚€_inv_app (K : ChainComplex C â„•) :
    Nâ‚Î“â‚€.inv.app K = (toKaroubi _).map (Î“â‚€NondegComplexIso K).inv â‰«
        (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.hom := by
  change (Nâ‚Î“â‚€.app K).inv = _
  simp only [Nâ‚Î“â‚€_app]
  rfl

@[simp]
theorem Nâ‚Î“â‚€_hom_app_f_f (K : ChainComplex C â„•) (n : â„•) :
    (Nâ‚Î“â‚€.hom.app K).f.f n = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.inv.f.f n := by
  rw [Nâ‚Î“â‚€_hom_app]
  apply comp_id

@[simp]
theorem Nâ‚Î“â‚€_inv_app_f_f (K : ChainComplex C â„•) (n : â„•) :
    (Nâ‚Î“â‚€.inv.app K).f.f n = (Î“â‚€.splitting K).toKaroubiNondegComplexIsoNâ‚.hom.f.f n := by
  rw [Nâ‚Î“â‚€_inv_app]
  apply id_comp

/-- Compatibility isomorphism between `toKaroubi _ â‹™ Î“â‚‚ â‹™ Nâ‚‚` and `Î“â‚€ â‹™ Nâ‚` which
are functors `ChainComplex C â„• â¥¤ Karoubi (ChainComplex C â„•)`. -/
def Nâ‚‚Î“â‚‚ToKaroubiIso : toKaroubi (ChainComplex C â„•) â‹™ Î“â‚‚ â‹™ Nâ‚‚ â‰… Î“â‚€ â‹™ Nâ‚ :=
  calc
    toKaroubi (ChainComplex C â„•) â‹™ Î“â‚‚ â‹™ Nâ‚‚ â‰…
      toKaroubi (ChainComplex C â„•) â‹™ (Î“â‚‚ â‹™ Nâ‚‚) := (Functor.associator _ _ _).symm
    _ â‰… (Î“â‚€ â‹™ toKaroubi (SimplicialObject C)) â‹™ Nâ‚‚ :=
        isoWhiskerRight ((functorExtensionâ‚‚CompWhiskeringLeftToKaroubiIso _ _).app Î“â‚€) Nâ‚‚
    _ â‰… Î“â‚€ â‹™ toKaroubi (SimplicialObject C) â‹™ Nâ‚‚ := Functor.associator _ _ _
    _ â‰… Î“â‚€ â‹™ Nâ‚ :=
      isoWhiskerLeft Î“â‚€ ((functorExtensionâ‚CompWhiskeringLeftToKaroubiIso _ _).app Nâ‚)

@[simp]
lemma Nâ‚‚Î“â‚‚ToKaroubiIso_hom_app (X : ChainComplex C â„•) :
    (Nâ‚‚Î“â‚‚ToKaroubiIso.hom.app X).f = PInfty := by
  ext n
  dsimp [Nâ‚‚Î“â‚‚ToKaroubiIso]
  simp only [comp_id, assoc, PInfty_f_idem]
  conv_rhs =>
    rw [â† PInfty_f_idem]
  congr 1
  apply (Î“â‚€.splitting X).hom_ext'
  intro A
  rw [Splitting.Î¹_desc_assoc, assoc]
  apply id_comp

@[simp]
lemma Nâ‚‚Î“â‚‚ToKaroubiIso_inv_app (X : ChainComplex C â„•) :
    (Nâ‚‚Î“â‚‚ToKaroubiIso.inv.app X).f = PInfty := by
  ext n
  dsimp [Nâ‚‚Î“â‚‚ToKaroubiIso]
  simp only [comp_id, PInfty_f_idem_assoc, AlternatingFaceMapComplex.obj_X, Î“â‚€_obj_obj]
  convert comp_id _
  apply (Î“â‚€.splitting X).hom_ext'
  intro A
  rw [Splitting.Î¹_desc]
  erw [comp_id, id_comp]

/-- The counit isomorphism of the Dold-Kan equivalence for additive categories. -/
def Nâ‚‚Î“â‚‚ : Î“â‚‚ â‹™ Nâ‚‚ â‰… ğŸ­ (Karoubi (ChainComplex C â„•)) :=
  ((whiskeringLeft _ _ _).obj (toKaroubi (ChainComplex C â„•))).preimageIso
      (Nâ‚‚Î“â‚‚ToKaroubiIso â‰ªâ‰« Nâ‚Î“â‚€)

@[simp]
theorem Nâ‚‚Î“â‚‚_inv_app_f_f (X : Karoubi (ChainComplex C â„•)) (n : â„•) :
    (Nâ‚‚Î“â‚‚.inv.app X).f.f n =
      X.p.f n â‰« ((Î“â‚€.splitting X.X).cofan _).inj (Splitting.IndexSet.id (op â¦‹nâ¦Œ)) := by
  dsimp [Nâ‚‚Î“â‚‚]
  simp only [whiskeringLeft_obj_preimage_app, NatTrans.comp_app, Functor.comp_map,
    Karoubi.comp_f, Nâ‚‚Î“â‚‚ToKaroubiIso_inv_app, HomologicalComplex.comp_f,
    Nâ‚Î“â‚€_inv_app_f_f, toKaroubi_obj_X, Splitting.toKaroubiNondegComplexIsoNâ‚_hom_f_f,
    Î“â‚€.obj_obj, PInfty_on_Î“â‚€_splitting_summand_eq_self, Nâ‚‚_map_f_f,
    Î“â‚‚_map_f_app, unop_op, Karoubi.decompId_p_f, PInfty_f_idem_assoc,
    PInfty_on_Î“â‚€_splitting_summand_eq_self_assoc, Splitting.IndexSet.id_fst, SimplexCategory.len_mk,
    Splitting.Î¹_desc]
  apply Karoubi.HomologicalComplex.p_idem_assoc

lemma whiskerLeft_toKaroubi_Nâ‚‚Î“â‚‚_hom :
    whiskerLeft (toKaroubi (ChainComplex C â„•)) Nâ‚‚Î“â‚‚.hom = Nâ‚‚Î“â‚‚ToKaroubiIso.hom â‰« Nâ‚Î“â‚€.hom := by
  let e : _ â‰… toKaroubi (ChainComplex C â„•) â‹™ ğŸ­ _ := Nâ‚‚Î“â‚‚ToKaroubiIso â‰ªâ‰« Nâ‚Î“â‚€
  have h := ((whiskeringLeft _ _ (Karoubi (ChainComplex C â„•))).obj
    (toKaroubi (ChainComplex C â„•))).map_preimage e.hom
  dsimp only [whiskeringLeft, Nâ‚‚Î“â‚‚, Functor.preimageIso] at h âŠ¢
  exact h

theorem Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€ (K : ChainComplex C â„•) :
    Nâ‚‚Î“â‚‚.hom.app ((toKaroubi _).obj K) = Nâ‚‚Î“â‚‚ToKaroubiIso.hom.app K â‰« Nâ‚Î“â‚€.hom.app K :=
  congr_app whiskerLeft_toKaroubi_Nâ‚‚Î“â‚‚_hom K

end DoldKan

end AlgebraicTopology
