/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.Decomposition
import Mathlib.Tactic.FinCases

/-!

# Behaviour of P_infty with respect to degeneracies

For any `X : SimplicialObject C` where `C` is an abelian category,
the projector `PInfty : K[X] âŸ¶ K[X]` is supposed to be the projection
on the normalized subcomplex, parallel to the degenerate subcomplex, i.e.
the subcomplex generated by the images of all `X.Ïƒ i`.

In this file, we obtain `degeneracy_comp_P_infty` which states that
if `X : SimplicialObject C` with `C` a preadditive category,
`Î¸ : â¦‹nâ¦Œ âŸ¶ Î”'` is a non-injective map in `SimplexCategory`, then
`X.map Î¸.op â‰« P_infty.f n = 0`. It follows from the more precise
statement vanishing statement `Ïƒ_comp_P_eq_zero` for the `P q`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category CategoryTheory.Limits
  CategoryTheory.Preadditive Simplicial

namespace AlgebraicTopology

namespace DoldKan

variable {C : Type*} [Category C] [Preadditive C]

theorem HigherFacesVanish.comp_Ïƒ {Y : C} {X : SimplicialObject C} {n b q : â„•} {Ï† : Y âŸ¶ X _â¦‹n + 1â¦Œ}
    (v : HigherFacesVanish q Ï†) (hnbq : n + 1 = b + q) :
    HigherFacesVanish q
      (Ï† â‰«
        X.Ïƒ âŸ¨b, by
          simp only [hnbq, Nat.lt_add_one_iff, le_add_iff_nonneg_right, zero_le]âŸ©) :=
  fun j hj => by
  rw [assoc, SimplicialObject.Î´_comp_Ïƒ_of_gt', Fin.pred_succ, v.comp_Î´_eq_zero_assoc _ _ hj,
    zero_comp]
  Â· intro hj'
    simp only [hnbq, add_comm b, add_assoc, hj', Fin.val_zero, zero_add, add_le_iff_nonpos_right,
      nonpos_iff_eq_zero, add_eq_zero, false_and, reduceCtorEq] at hj
  Â· dsimp
    rw [Fin.lt_def, Fin.val_succ]
    linarith

theorem Ïƒ_comp_P_eq_zero (X : SimplicialObject C) {n q : â„•} (i : Fin (n + 1)) (hi : n + 1 â‰¤ i + q) :
    X.Ïƒ i â‰« (P q).f (n + 1) = 0 := by
  induction q generalizing i with
  | zero => cutsat
  | succ q hq =>
    by_cases h : n + 1 â‰¤ (i : â„•) + q
    Â· rw [P_succ, HomologicalComplex.comp_f, â† assoc, hq i h, zero_comp]
    Â· replace hi : n = i + q := by omega
      rcases n with _ | n
      Â· fin_cases i
        dsimp at h hi
        rw [show q = 0 by cutsat]
        change X.Ïƒ 0 â‰« (P 1).f 1 = 0
        simp only [P_succ, HomologicalComplex.add_f_apply, comp_add,
          AlternatingFaceMapComplex.obj_d_eq, HÏƒ,
          HomologicalComplex.comp_f, Homotopy.nullHomotopicMap'_f (c_mk 2 1 rfl) (c_mk 1 0 rfl),
          comp_id]
        rw [hÏƒ'_eq' (zero_add 0).symm, hÏƒ'_eq' (add_zero 1).symm]
        dsimp [P_zero]
        rw [comp_id, Fin.sum_univ_two,
          Fin.sum_univ_succ, Fin.sum_univ_two]
        simp only [Fin.val_zero, pow_zero, pow_one, pow_add, one_smul, neg_smul, Fin.val_succ,
          Fin.val_one, mul_neg, one_mul, neg_mul, neg_neg, id_comp, add_comp, comp_add, neg_comp,
          comp_neg, Fin.succ_zero_eq_one]
        rw [â† Fin.castSucc_one, SimplicialObject.Î´_comp_Ïƒ_self, â† Fin.castSucc_zero (n := 1),
          SimplicialObject.Î´_comp_Ïƒ_self_assoc,
          SimplicialObject.Î´_comp_Ïƒ_succ, comp_id, â† Fin.castSucc_zero (n := 2),
          â† Fin.succ_zero_eq_one,
          SimplicialObject.Î´_comp_Ïƒ_of_le X
            (show (0 : Fin 2) â‰¤ Fin.castSucc 0 by rw [Fin.castSucc_zero]),
          â† Fin.castSucc_zero (n := 1), SimplicialObject.Î´_comp_Ïƒ_self_assoc,
          SimplicialObject.Î´_comp_Ïƒ_succ_assoc]
        simp only [add_neg_cancel, add_zero, zero_add]
      Â· rw [â† id_comp (X.Ïƒ i), â† (P_add_Q_f q n.succ : _ = ğŸ™ (X.obj _)), add_comp, add_comp,
          P_succ]
        have v : HigherFacesVanish q ((P q).f n.succ â‰« X.Ïƒ i) :=
          (HigherFacesVanish.of_P q n).comp_Ïƒ hi
        dsimp only [AlternatingFaceMapComplex.obj_X, Nat.succ_eq_add_one, HomologicalComplex.comp_f,
          HomologicalComplex.add_f_apply, HomologicalComplex.id_f]
        rw [â† assoc, v.comp_P_eq_self, Preadditive.comp_add,
          comp_id, v.comp_HÏƒ_eq hi, assoc, â† Fin.succ_mk _ _ i.2,
          SimplicialObject.Î´_comp_Ïƒ_succ_assoc,
          Fin.eta, decomposition_Q n q, sum_comp, sum_comp, Finset.sum_eq_zero, add_zero,
          add_neg_eq_zero]
        intro j hj
        simp only [Finset.mem_univ, Finset.mem_filter] at hj
        obtain âŸ¨k, hkâŸ© := Nat.le.dest (Nat.lt_succ_iff.mp (Fin.is_lt j))
        rw [add_comm] at hk
        have hi' : i = Fin.castSucc âŸ¨i, by cutsatâŸ© := by
          ext
          simp only [Fin.castSucc_mk, Fin.eta]
        have eq := hq j.rev.succ (by
          simp only [â† hk, Fin.rev_eq j hk.symm, Fin.succ_mk, Fin.val_mk]
          cutsat)
        rw [assoc, assoc, assoc, hi',
          SimplicialObject.Ïƒ_comp_Ïƒ_assoc, reassoc_of% eq, zero_comp, comp_zero, comp_zero,
          comp_zero]
        simp only [Fin.rev_eq j hk.symm, Fin.le_iff_val_le_val]
        cutsat

@[reassoc (attr := simp)]
theorem Ïƒ_comp_PInfty (X : SimplicialObject C) {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« PInfty.f (n + 1) = 0 := by
  rw [PInfty_f, Ïƒ_comp_P_eq_zero X i]
  simp only [le_add_iff_nonneg_left, zero_le]

@[reassoc]
theorem degeneracy_comp_PInfty (X : SimplicialObject C) (n : â„•) {Î”' : SimplexCategory}
    (Î¸ : â¦‹nâ¦Œ âŸ¶ Î”') (hÎ¸ : Â¬Mono Î¸) : X.map Î¸.op â‰« PInfty.f n = 0 := by
  rw [SimplexCategory.mono_iff_injective] at hÎ¸
  cases n
  Â· exfalso
    apply hÎ¸
    intro x y h
    fin_cases x
    fin_cases y
    rfl
  Â· obtain âŸ¨i, Î±, hâŸ© := SimplexCategory.eq_Ïƒ_comp_of_not_injective Î¸ hÎ¸
    rw [h, op_comp, X.map_comp, assoc, show X.map (SimplexCategory.Ïƒ i).op = X.Ïƒ i by rfl,
      Ïƒ_comp_PInfty, comp_zero]

end DoldKan

end AlgebraicTopology
