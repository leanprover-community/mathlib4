/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.Monoidal
import Mathlib.CategoryTheory.Enriched.Basic

/-!
# Simplicial categories

A simplicial category is a category `C` that is enriched over the
category of simplicial sets in such a way that morphisms in
`C` identify to the `0`-simplices of the enriched hom.

## TODO

* construct a simplicial category structure on simplicial objects, so
that it applies in particular to simplicial sets
* obtain the adjunction property `(K âŠ— X âŸ¶ Y) â‰ƒ (K âŸ¶ sHom X Y)` when `K`, `X`, and `Y`
are simplicial sets
* develop the notion of "simplicial tensor" `K âŠ—â‚› X : C` with `K : SSet` and `X : C`
an object in a simplicial category `C`
* define the notion of path between `0`-simplices of simplicial sets
* deduce the notion of homotopy between morphisms in a simplicial category
* obtain that homotopies in simplicial categories can be interpreted as given
by morphisms `Î”[1] âŠ— X âŸ¶ Y`.

## References
* [Daniel G. Quillen, *Homotopical algebra*, II Â§1][quillen-1967]

-/

universe v u

open CategoryTheory Category Simplicial MonoidalCategory

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- A simplicial category is a category `C` that is enriched over the
category of simplicial sets in such a way that morphisms in
`C` identify to the `0`-simplices of the enriched hom. -/
class SimplicialCategory extends EnrichedCategory SSet.{v} C where
  /-- morphisms identify to `0`-simplices of the enriched hom -/
  homEquiv (K L : C) : (K âŸ¶ L) â‰ƒ (ðŸ™_ SSet.{v} âŸ¶ EnrichedCategory.Hom K L)
  homEquiv_id (K : C) : homEquiv K K (ðŸ™ K) = eId SSet K := by aesop_cat
  homEquiv_comp {K L M : C} (f : K âŸ¶ L) (g : L âŸ¶ M) :
    homEquiv K M (f â‰« g) = (Î»_ _).inv â‰« (homEquiv K L f âŠ— homEquiv L M g) â‰«
      eComp SSet K L M := by aesop_cat

namespace SimplicialCategory

variable [SimplicialCategory C]

variable {C}

/-- Abbreviation for the enriched hom of a simplicial category. -/
abbrev sHom (K L : C) : SSet.{v} := EnrichedCategory.Hom K L

/-- Abbreviation for the enriched composition in a simplicial category. -/
abbrev sHomComp (K L M : C) : sHom K L âŠ— sHom L M âŸ¶ sHom K M := eComp SSet K L M

/-- The bijection `(K âŸ¶ L) â‰ƒ sHom K L _[0]` for all objects `K` and `L`
in a simplicial category. -/
def homEquiv' (K L : C) : (K âŸ¶ L) â‰ƒ sHom K L _[0] :=
  (homEquiv K L).trans (sHom K L).unitHomEquiv

/-- The morphism `sHom K' L âŸ¶ sHom K L` induced by a morphism `K âŸ¶ K'`. -/
noncomputable def sHomWhiskerRight {K K' : C} (f : K âŸ¶ K') (L : C) :
    sHom K' L âŸ¶ sHom K L :=
  (Î»_ _).inv â‰« homEquiv K K' f â–· _ â‰« sHomComp K K' L

@[simp]
lemma sHomWhiskerRight_id (K L : C) : sHomWhiskerRight (ðŸ™ K) L = ðŸ™ _ := by
  simp [sHomWhiskerRight, homEquiv_id]

@[simp, reassoc]
lemma sHomWhiskerRight_comp {K K' K'' : C} (f : K âŸ¶ K') (f' : K' âŸ¶ K'') (L : C) :
    sHomWhiskerRight (f â‰« f') L = sHomWhiskerRight f' L â‰« sHomWhiskerRight f L := by
  dsimp [sHomWhiskerRight]
  simp only [assoc, homEquiv_comp, comp_whiskerRight, leftUnitor_inv_whiskerRight, â† e_assoc']
  rfl

/-- The morphism `sHom K L âŸ¶ sHom K L'` induced by a morphism `L âŸ¶ L'`. -/
noncomputable def sHomWhiskerLeft (K : C) {L L' : C} (g : L âŸ¶ L') :
    sHom K L âŸ¶ sHom K L' :=
  (Ï_ _).inv â‰« _ â— homEquiv L L' g â‰« sHomComp K L L'

@[simp]
lemma sHomWhiskerLeft_id (K L : C) : sHomWhiskerLeft K (ðŸ™ L) = ðŸ™ _ := by
  simp [sHomWhiskerLeft, homEquiv_id]

@[simp, reassoc]
lemma sHomWhiskerLeft_comp (K : C) {L L' L'' : C} (g : L âŸ¶ L') (g' : L' âŸ¶ L'') :
    sHomWhiskerLeft K (g â‰« g') = sHomWhiskerLeft K g â‰« sHomWhiskerLeft K g' := by
  dsimp [sHomWhiskerLeft]
  simp only [homEquiv_comp, MonoidalCategory.whiskerLeft_comp, assoc, â† e_assoc]
  rfl

@[reassoc]
lemma sHom_whisker_exchange {K K' L L' : C} (f : K âŸ¶ K') (g : L âŸ¶ L') :
    sHomWhiskerLeft K' g â‰« sHomWhiskerRight f L' =
      sHomWhiskerRight f L â‰« sHomWhiskerLeft K g :=
  ((Ï_ _).inv â‰« _ â— homEquiv L L' g â‰« (Î»_ _).inv â‰« homEquiv K K' f â–· _) â‰«=
    (e_assoc SSet.{v} K K' L L').symm

attribute [local simp] sHom_whisker_exchange

variable (C) in
/-- The bifunctor `Cáµ’áµ– â¥¤ C â¥¤ SSet.{v}` which sends `K : Cáµ’áµ–` and `L : C` to `sHom K.unop L`. -/
@[simps]
noncomputable def sHomFunctor : Cáµ’áµ– â¥¤ C â¥¤ SSet.{v} where
  obj K :=
    { obj := fun L => sHom K.unop L
      map := fun Ï† => sHomWhiskerLeft K.unop Ï† }
  map Ï† :=
    { app := fun L => sHomWhiskerRight Ï†.unop L }

end SimplicialCategory

end CategoryTheory
