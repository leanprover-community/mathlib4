/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Category.ModuleCat.Abelian
import Mathlib.Algebra.Category.ModuleCat.Adjunctions
import Mathlib.AlgebraicTopology.AlternatingFaceMapComplex
import Mathlib.AlgebraicTopology.SingularSet

/-!
# Singular homology

In this file, we define the singular chain complex and singular homology of a topological space.
We also calculate the homology of a totally disconnected space as an example.

-/

noncomputable section

namespace AlgebraicTopology

open CategoryTheory

universe w v u

variable (C : Type u) [Category.{v} C] [Limits.HasCoproducts.{0} C]
variable [Preadditive C] [CategoryWithHomology C] (n : ‚Ñï)

/--
The singular chain complex associated to a simplicial set, with coefficients in `X : C`.
One can recover the ordinary singular chain complex when `C := Ab` and `X := ‚Ñ§`.
-/
def SSet.singularChainComplexFunctor [Limits.HasCoproducts.{w} C] :
    C ‚•§ SSet.{w} ‚•§ ChainComplex C ‚Ñï :=
  (Functor.postcompose‚ÇÇ.obj (AlgebraicTopology.alternatingFaceMapComplex _)).obj
    (Limits.sigmaConst ‚ãô SimplicialObject.whiskering _ _)

/-- The singular chain complex functor with coefficients in `C`. -/
def singularChainComplexFunctor :
    C ‚•§ TopCat.{0} ‚•§ ChainComplex C ‚Ñï :=
  SSet.singularChainComplexFunctor.{0} C ‚ãô (whiskeringLeft _ _ _).obj TopCat.toSSet

/-- The `n`-th singular homology functor with coefficients in `C`. -/
def singularHomologyFunctor : C ‚•§ TopCat.{0} ‚•§ C :=
  singularChainComplexFunctor C ‚ãô
    (whiskeringRight _ _ _).obj (HomologicalComplex.homologyFunctor _ _ n)

section TotallyDisconnectedSpace

variable (R : C) (X : TopCat.{0}) [TotallyDisconnectedSpace X]

/-- If `X` is totally disconnected,
its singular chain complex is given by `R[X] ‚Üê0- R[X] ‚Üêùüô- R[X] ‚Üê0- R[X] ‚ãØ`,
where `R[X]` is the coproduct of copies of `R` indexed by elements of `X`. -/
noncomputable
def singularChainComplexFunctorIsoOfTotallyDisconnectedSpace :
    ((singularChainComplexFunctor C).obj R).obj X ‚âÖ
      (ChainComplex.alternatingConst.obj (‚àê fun _ : X ‚Ü¶ R)) :=
  (AlgebraicTopology.alternatingFaceMapComplex _).mapIso
    (((SimplicialObject.whiskering _ _).obj _).mapIso
    (TopCat.toSSetIsoConst X) ‚â™‚â´ Functor.constComp _ _ _) ‚â™‚â´
    AlgebraicTopology.alternatingFaceMapComplexConst.app _

-- Note: `Limits.HasZeroObject` should follow from `CategoryWithHomology`.
lemma isZero_singularHomologyFunctor_of_totallyDisconnectedSpace [Limits.HasZeroObject C]
    (hn : n ‚â† 0) :
    Limits.IsZero (((singularHomologyFunctor C n).obj R).obj X) :=
  (ChainComplex.isZero_alternatingConst_homology _ _ hn).of_iso
    ((HomologicalComplex.homologyFunctor _ _ n).mapIso
      (singularChainComplexFunctorIsoOfTotallyDisconnectedSpace C R X))

/-- The zeroth singular homology of a totally disconnected space is the
free `R`-module generated by elements of `X`. -/
-- Note: `Limits.HasZeroObject` should follow from `CategoryWithHomology`.
noncomputable
def singularHomologyFunctorZeroOfTotallyDisconnectedSpace [Limits.HasZeroObject C] :
    ((singularHomologyFunctor C 0).obj R).obj X ‚âÖ ‚àê fun _ : X ‚Ü¶ R :=
  (HomologicalComplex.homologyFunctor _ _ 0).mapIso
      (singularChainComplexFunctorIsoOfTotallyDisconnectedSpace C R X) ‚â™‚â´
    ChainComplex.alternatingConstHomologyZero _

end TotallyDisconnectedSpace

end AlgebraicTopology
