/-
Copyright (c) 2024 Mario Carneiro and Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, Jo√´l Riou
-/

import Mathlib.AlgebraicTopology.SimplicialObject.Basic
import Mathlib.AlgebraicTopology.SimplicialSet.Coskeletal
import Mathlib.CategoryTheory.Category.ReflQuiv
import Mathlib.Combinatorics.Quiver.ReflQuiver


/-!

# The homotopy category of a simplicial set

The homotopy category of a simplicial set is defined as a quotient of the free category on its
underlying reflexive quiver (equivalently its one truncation). The quotient imposes an additional
hom relation on this free category, asserting that `f ‚â´ g = h` whenever `f`, `g`, and `h` are
respectively the 2nd, 0th, and 1st faces of a 2-simplex.

In fact, the associated functor

`SSet.hoFunctor : SSet.{u} ‚•§ Cat.{u, u} := SSet.truncation 2 ‚ãô SSet.hoFunctor‚ÇÇ`

is defined by first restricting from simplicial sets to 2-truncated simplicial sets (throwing away
the data that is not used for the construction of the homotopy category) and then composing with an
analogously defined `SSet.hoFunctor‚ÇÇ : SSet.Truncated.{u} 2 ‚•§ Cat.{u,u}` implemented relative to
the syntax of the 2-truncated simplex category.

In the file `Mathlib/AlgebraicTopology/SimplicialSet/NerveAdjunction.lean` we show the functor
`SSet.hoFunctor` to be left adjoint to the nerve by providing an analogous decomposition of the
nerve functor, made by possible by the fact that nerves of categories are 2-coskeletal, and then
composing a pair of adjunctions, which factor through the category of 2-truncated simplicial sets.
-/

namespace SSet
open CategoryTheory Category Limits Functor Opposite Simplicial Nerve
open SimplexCategory.Truncated SimplicialObject.Truncated

universe v u

section

/-- A 2-truncated simplicial set `S` has an underlying refl quiver with `S _‚¶ã0‚¶å‚ÇÇ` as its underlying
type. -/
def OneTruncation‚ÇÇ (S : SSet.Truncated 2) := S _‚¶ã0‚¶å‚ÇÇ

/-- Abbreviations for face maps in the 2-truncated simplex category. -/
abbrev Œ¥‚ÇÇ {n} (i : Fin (n + 2)) (hn := by decide) (hn' := by decide) :
    (‚ü®‚¶ãn‚¶å, hn‚ü© : SimplexCategory.Truncated 2) ‚ü∂ ‚ü®‚¶ãn + 1‚¶å, hn'‚ü© := SimplexCategory.Œ¥ i

/-- Abbreviations for degeneracy maps in the 2-truncated simplex category. -/
abbrev œÉ‚ÇÇ {n} (i : Fin (n + 1)) (hn := by decide) (hn' := by decide) :
    (‚ü®‚¶ãn+1‚¶å, hn‚ü© : SimplexCategory.Truncated 2) ‚ü∂ ‚ü®‚¶ãn‚¶å, hn'‚ü© := SimplexCategory.œÉ i

@[reassoc (attr := simp)]
lemma Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_zero {n} (hn := by decide) (hn' := by decide) :
    Œ¥‚ÇÇ (n := n) 0 hn hn' ‚â´ œÉ‚ÇÇ 0 hn' hn = ùüô _ := SimplexCategory.Œ¥_comp_œÉ_self

@[reassoc]
lemma Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_one : Œ¥‚ÇÇ (0 : Fin 3) ‚â´ œÉ‚ÇÇ 1 = œÉ‚ÇÇ 0 ‚â´ Œ¥‚ÇÇ 0 :=
  SimplexCategory.Œ¥_comp_œÉ_of_le (i := 0) (j := 0) (Fin.zero_le _)

@[reassoc (attr := simp)]
lemma Œ¥‚ÇÇ_one_comp_œÉ‚ÇÇ_zero {n} (hn := by decide) (hn' := by decide) :
    Œ¥‚ÇÇ (n := n) 1 hn hn' ‚â´ œÉ‚ÇÇ 0 hn' hn = ùüô _ := SimplexCategory.Œ¥_comp_œÉ_succ

@[reassoc (attr := simp)]
lemma Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_one : Œ¥‚ÇÇ (2 : Fin 3) ‚â´ œÉ‚ÇÇ 1 = ùüô _ := SimplexCategory.Œ¥_comp_œÉ_succ' (by decide)

@[reassoc]
lemma Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_zero : Œ¥‚ÇÇ (2 : Fin 3) ‚â´ œÉ‚ÇÇ 0 = œÉ‚ÇÇ 0 ‚â´ Œ¥‚ÇÇ 1 :=
  SimplexCategory.Œ¥_comp_œÉ_of_gt' (by decide)

/-- The hom-types of the refl quiver underlying a simplicial set `S` are types of edges in `S _‚¶ã1‚¶å‚ÇÇ`
together with source and target equalities. -/
@[ext]
structure OneTruncation‚ÇÇ.Hom {S : SSet.Truncated 2} (X Y : OneTruncation‚ÇÇ S) where
  /-- An arrow in `OneTruncation‚ÇÇ.Hom X Y` includes the data of a 1-simplex. -/
  edge : S _‚¶ã1‚¶å‚ÇÇ
  /-- An arrow in `OneTruncation‚ÇÇ.Hom X Y` includes a source equality. -/
  src_eq : S.map (Œ¥‚ÇÇ 1).op edge = X
  /-- An arrow in `OneTruncation‚ÇÇ.Hom X Y` includes a target equality. -/
  tgt_eq : S.map (Œ¥‚ÇÇ 0).op edge = Y

/-- A 2-truncated simplicial set `S` has an underlying refl quiver `SSet.OneTruncation‚ÇÇ S`. -/
instance (S : SSet.Truncated 2) : ReflQuiver (OneTruncation‚ÇÇ S) where
  Hom X Y := SSet.OneTruncation‚ÇÇ.Hom X Y
  id X :=
    { edge := S.map (SSet.œÉ‚ÇÇ (n := 0) 0).op X
      src_eq := by
        simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_one_comp_œÉ‚ÇÇ_zero,
          op_id, FunctorToTypes.map_id_apply]
      tgt_eq := by
        simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_zero,
          op_id, FunctorToTypes.map_id_apply] }

@[simp]
lemma OneTruncation‚ÇÇ.id_edge {S : SSet.Truncated 2} (X : OneTruncation‚ÇÇ S) :
    OneTruncation‚ÇÇ.Hom.edge (ùüôrq X) = S.map (SSet.œÉ‚ÇÇ 0).op X := rfl

/-- The functor that carries a 2-truncated simplicial set to its underlying refl quiver. -/
@[simps]
def oneTruncation‚ÇÇ : SSet.Truncated.{u} 2 ‚•§ ReflQuiv.{u, u} where
  obj S := ReflQuiv.of (OneTruncation‚ÇÇ S)
  map {S T} F := {
    obj := F.app (op ‚¶ã0‚¶å‚ÇÇ)
    map := fun f ‚Ü¶
      { edge := F.app _ f.edge
        src_eq := by rw [‚Üê FunctorToTypes.naturality, f.src_eq]
        tgt_eq := by rw [‚Üê FunctorToTypes.naturality, f.tgt_eq] }
    map_id := fun X ‚Ü¶ OneTruncation‚ÇÇ.Hom.ext (by
      dsimp
      rw [‚Üê FunctorToTypes.naturality]) }

@[ext]
lemma OneTruncation‚ÇÇ.hom_ext {S : SSet.Truncated 2} {x y : OneTruncation‚ÇÇ S} {f g : x ‚ü∂ y} :
    f.edge = g.edge ‚Üí f = g := OneTruncation‚ÇÇ.Hom.ext

@[simp]
lemma OneTruncation‚ÇÇ.homOfEq_edge
    {X : SSet.Truncated.{u} 2} {x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : OneTruncation‚ÇÇ X}
    (f : x‚ÇÅ ‚ü∂ y‚ÇÅ) (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ = y‚ÇÇ) :
    (Quiver.homOfEq f hx hy).edge = f.edge := by
  subst hx hy
  rfl

section
variable {C : Type u} [Category.{v} C]

/-- An equivalence between the type of objects underlying a category and the type of 0-simplices in
the 2-truncated nerve. -/
@[simps]
def OneTruncation‚ÇÇ.nerveEquiv :
    OneTruncation‚ÇÇ ((SSet.truncation 2).obj (nerve C)) ‚âÉ C where
  toFun X := X.obj' 0
  invFun X := .mk‚ÇÄ X
  left_inv _ := ComposableArrows.ext‚ÇÄ rfl

/-- A hom equivalence over the function `OneTruncation‚ÇÇ.nerveEquiv`. -/
def OneTruncation‚ÇÇ.nerveHomEquiv (X Y : OneTruncation‚ÇÇ ((SSet.truncation 2).obj (nerve C))) :
  (X ‚ü∂ Y) ‚âÉ (nerveEquiv X ‚ü∂ nerveEquiv Y) where
  toFun œÜ := eqToHom (congr_arg ComposableArrows.left œÜ.src_eq.symm) ‚â´ œÜ.edge.hom ‚â´
      eqToHom (congr_arg ComposableArrows.left œÜ.tgt_eq)
  invFun f :=
    { edge := ComposableArrows.mk‚ÇÅ f
      src_eq := ComposableArrows.ext‚ÇÄ rfl
      tgt_eq := ComposableArrows.ext‚ÇÄ rfl }
  left_inv œÜ := by
    ext
    exact ComposableArrows.ext‚ÇÅ (congr_arg ComposableArrows.left œÜ.src_eq).symm
      (congr_arg ComposableArrows.left œÜ.tgt_eq).symm rfl
  right_inv f := by dsimp; simp only [comp_id, id_comp]; rfl

/-- The refl quiver underlying a nerve is isomorphic to the refl quiver underlying the category. -/
def OneTruncation‚ÇÇ.ofNerve‚ÇÇ (C : Type u) [Category.{u} C] :
    ReflQuiv.of (OneTruncation‚ÇÇ (nerveFunctor‚ÇÇ.obj (Cat.of C))) ‚âÖ ReflQuiv.of C := by
  apply ReflQuiv.isoOfEquiv.{u,u} OneTruncation‚ÇÇ.nerveEquiv OneTruncation‚ÇÇ.nerveHomEquiv ?_
  intro X
  unfold nerveEquiv nerveHomEquiv
  simp only [Cat.of_Œ±, op_obj, ComposableArrows.obj', Fin.zero_eta, Fin.isValue, Equiv.coe_fn_mk,
    nerveEquiv_apply, Nat.reduceAdd, id_edge, SimplexCategory.len_mk, id_eq, eqToHom_refl, comp_id,
    id_comp, ReflQuiver.id_eq_id]
  unfold nerve truncation SimplicialObject.truncation SimplexCategory.Truncated.inclusion
  -- the following was obtained by `simp?`
  simp only [ObjectProperty.Œπ_obj, SimplexCategory.len_mk, Nat.reduceAdd, Fin.isValue,
    SimplexCategory.toCat_map, whiskeringLeft_obj_obj, Functor.comp_map, op_obj, op_map,
    Quiver.Hom.unop_op, ObjectProperty.Œπ_map, ComposableArrows.whiskerLeft_map, Fin.zero_eta,
    Monotone.functor_obj, Fin.mk_one, homOfLE_leOfHom]
  show X.map (ùüô _) = _
  rw [X.map_id]
  rfl

/-- The refl quiver underlying a nerve is naturally isomorphic to the refl quiver underlying the
category. -/
@[simps! hom_app_obj hom_app_map inv_app_obj_obj inv_app_obj_map inv_app_map]
def OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso :
    nerveFunctor‚ÇÇ.{u,u} ‚ãô SSet.oneTruncation‚ÇÇ ‚âÖ ReflQuiv.forget :=
  NatIso.ofComponents (fun C => OneTruncation‚ÇÇ.ofNerve‚ÇÇ C) (by
  ¬∑ intro C D F
    fapply ReflPrefunctor.ext <;> simp
    ¬∑ exact fun _ ‚Ü¶ rfl
    ¬∑ intro X Y f
      obtain ‚ü®f, rfl, rfl‚ü© := f
      unfold SSet.oneTruncation‚ÇÇ nerveFunctor‚ÇÇ SSet.truncation SimplicialObject.truncation
        nerveFunctor toReflPrefunctor
      simp only [comp_obj, whiskeringLeft_obj_obj, ReflQuiv.of_val, Functor.comp_map,
        whiskeringLeft_obj_map, whiskerLeft_app, op_obj, whiskeringRight_obj_obj, ofNerve‚ÇÇ,
        Cat.of_Œ±, nerveEquiv, ComposableArrows.obj', Fin.zero_eta, Fin.isValue,
        ReflQuiv.comp_eq_comp, Nat.reduceAdd, SimplexCategory.len_mk, id_eq, op_map,
        Quiver.Hom.unop_op, nerve_map, SimplexCategory.toCat_map, ReflPrefunctor.comp_obj,
        ReflPrefunctor.comp_map]
      simp [nerveHomEquiv, ReflQuiv.isoOfEquiv, ReflQuiv.isoOfQuivIso, Quiv.isoOfEquiv])

end

section

private lemma map_map_of_eq.{w}  {C : Type u} [Category.{v} C] (V : C·µí·µñ ‚•§ Type w) {X Y Z : C}
    {Œ± : X ‚ü∂ Y} {Œ≤ : Y ‚ü∂ Z} {Œ≥ : X ‚ü∂ Z} {œÜ} :
    Œ± ‚â´ Œ≤ = Œ≥ ‚Üí V.map Œ±.op (V.map Œ≤.op œÜ) = V.map Œ≥.op œÜ := by
  rintro rfl
  simp

variable {V : SSet}

namespace Truncated

/-- The map that picks up the initial vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Œπ0‚ÇÇ : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 0) 1 ‚â´ Œ¥‚ÇÇ (n := 1) 1

/-- The map that picks up the middle vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Œπ1‚ÇÇ : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 0) 0 ‚â´ Œ¥‚ÇÇ (n := 1) 2

/-- The map that picks up the final vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Œπ2‚ÇÇ : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 0) 0 ‚â´ Œ¥‚ÇÇ (n := 1) 1

/-- The initial vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev0‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : OneTruncation‚ÇÇ V := V.map Œπ0‚ÇÇ.op œÜ

/-- The middle vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev1‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : OneTruncation‚ÇÇ V := V.map Œπ1‚ÇÇ.op œÜ

/-- The final vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev2‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : OneTruncation‚ÇÇ V := V.map Œπ2‚ÇÇ.op œÜ

/-- The 0th face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Œ¥0‚ÇÇ : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 1) 0

/-- The 1st face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Œ¥1‚ÇÇ : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 1) 1

/-- The 2nd face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Œ¥2‚ÇÇ : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 1) 2

/-- The arrow in the ReflQuiver `OneTruncation‚ÇÇ V` of a 2-truncated simplicial set arising from the
0th face of a 2-simplex. -/
def ev12‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : ev1‚ÇÇ œÜ ‚ü∂ ev2‚ÇÇ œÜ :=
  ‚ü®V.map Œ¥0‚ÇÇ.op œÜ,
    map_map_of_eq V (SimplexCategory.Œ¥_comp_Œ¥ (i := 0) (j := 1) (by decide)).symm,
    map_map_of_eq V rfl‚ü©

/-- The arrow in the ReflQuiver `OneTruncation‚ÇÇ V` of a 2-truncated simplicial set arising from the
1st face of a 2-simplex. -/
def ev02‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : ev0‚ÇÇ œÜ ‚ü∂ ev2‚ÇÇ œÜ :=
  ‚ü®V.map Œ¥1‚ÇÇ.op œÜ, map_map_of_eq V rfl, map_map_of_eq V rfl‚ü©

/-- The arrow in the ReflQuiver `OneTruncation‚ÇÇ V` of a 2-truncated simplicial set arising from the
2nd face of a 2-simplex. -/
def ev01‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : ev0‚ÇÇ œÜ ‚ü∂ ev1‚ÇÇ œÜ :=
  ‚ü®V.map Œ¥2‚ÇÇ.op œÜ, map_map_of_eq V (SimplexCategory.Œ¥_comp_Œ¥ (j := 1) le_rfl), map_map_of_eq V rfl‚ü©


/-- The 2-simplices in a 2-truncated simplicial set `V` generate a hom relation on the free
category on the underlying refl quiver of `V`. -/
inductive HoRel‚ÇÇ {V : SSet.Truncated 2} :
    (X Y : Cat.FreeRefl (OneTruncation‚ÇÇ V)) ‚Üí (f g : X ‚ü∂ Y) ‚Üí Prop
  | mk (œÜ : V _‚¶ã2‚¶å‚ÇÇ) :
    HoRel‚ÇÇ _ _
      (Quot.mk _ (Quiver.Hom.toPath (ev02‚ÇÇ œÜ)))
      (Quot.mk _ ((Quiver.Hom.toPath (ev01‚ÇÇ œÜ)).comp
        (Quiver.Hom.toPath (ev12‚ÇÇ œÜ))))

/-- A 2-simplex whose faces are identified with certain arrows in `OneTruncation‚ÇÇ V` defines
a term of type `HoRel‚ÇÇ` between those arrows. -/
theorem HoRel‚ÇÇ.mk' {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) {X‚ÇÄ X‚ÇÅ X‚ÇÇ : OneTruncation‚ÇÇ V}
    (f‚ÇÄ‚ÇÅ : X‚ÇÄ ‚ü∂ X‚ÇÅ) (f‚ÇÅ‚ÇÇ : X‚ÇÅ ‚ü∂ X‚ÇÇ) (f‚ÇÄ‚ÇÇ : X‚ÇÄ ‚ü∂ X‚ÇÇ)
    (h‚ÇÄ‚ÇÅ : f‚ÇÄ‚ÇÅ.edge = V.map (Œ¥‚ÇÇ 2).op œÜ) (h‚ÇÅ‚ÇÇ : f‚ÇÅ‚ÇÇ.edge = V.map (Œ¥‚ÇÇ 0).op œÜ)
    (h‚ÇÄ‚ÇÇ : f‚ÇÄ‚ÇÇ.edge = V.map (Œ¥‚ÇÇ 1).op œÜ) :
    HoRel‚ÇÇ _ _ (Quot.mk _ (Quiver.Hom.toPath f‚ÇÄ‚ÇÇ))
      (Quot.mk _ ((Quiver.Hom.toPath f‚ÇÄ‚ÇÅ).comp (Quiver.Hom.toPath f‚ÇÅ‚ÇÇ))) := by
  obtain rfl : X‚ÇÄ = ev0‚ÇÇ œÜ := by
    rw [‚Üê f‚ÇÄ‚ÇÇ.src_eq, h‚ÇÄ‚ÇÇ, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    rfl
  obtain rfl : X‚ÇÅ = ev1‚ÇÇ œÜ := by
    rw [‚Üê f‚ÇÄ‚ÇÅ.tgt_eq, h‚ÇÄ‚ÇÅ, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    rfl
  obtain rfl : X‚ÇÇ = ev2‚ÇÇ œÜ := by
    rw [‚Üê f‚ÇÅ‚ÇÇ.tgt_eq, h‚ÇÅ‚ÇÇ, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    rfl
  obtain rfl : f‚ÇÄ‚ÇÅ = ev01‚ÇÇ œÜ := by ext; assumption
  obtain rfl : f‚ÇÅ‚ÇÇ = ev12‚ÇÇ œÜ := by ext; assumption
  obtain rfl : f‚ÇÄ‚ÇÇ = ev02‚ÇÇ œÜ := by ext; assumption
  constructor

/-- The type underlying the homotopy category of a 2-truncated simplicial set `V`. -/
def _root_.SSet.Truncated.HomotopyCategory (V : SSet.Truncated.{u} 2) : Type u :=
  Quotient (HoRel‚ÇÇ (V := V))

instance (V : SSet.Truncated.{u} 2) : Category.{u} (V.HomotopyCategory) :=
  inferInstanceAs (Category (CategoryTheory.Quotient ..))

/-- A canonical functor from the free category on the refl quiver underlying a 2-truncated
simplicial set `V` to its homotopy category. -/
def _root_.SSet.Truncated.HomotopyCategory.quotientFunctor (V : SSet.Truncated.{u} 2) :
    Cat.FreeRefl (OneTruncation‚ÇÇ V) ‚•§ V.HomotopyCategory :=
  Quotient.functor _

/-- By `Quotient.lift_unique'` (not `Quotient.lift`) we have that `quotientFunctor V` is an
epimorphism. -/
theorem HomotopyCategory.lift_unique' (V : SSet.Truncated.{u} 2) {D} [Category D]
    (F‚ÇÅ F‚ÇÇ : V.HomotopyCategory ‚•§ D)
    (h : HomotopyCategory.quotientFunctor V ‚ãô F‚ÇÅ = HomotopyCategory.quotientFunctor V ‚ãô F‚ÇÇ) :
    F‚ÇÅ = F‚ÇÇ :=
  Quotient.lift_unique' (C := Cat.FreeRefl (OneTruncation‚ÇÇ V))
    (HoRel‚ÇÇ (V := V)) _ _ h

/-- A map of 2-truncated simplicial sets induces a functor between homotopy categories. -/
def mapHomotopyCategory {V W : SSet.Truncated.{u} 2} (F : V ‚ü∂ W) :
    V.HomotopyCategory ‚•§ W.HomotopyCategory :=
  CategoryTheory.Quotient.lift _
    ((oneTruncation‚ÇÇ ‚ãô Cat.freeRefl).map F ‚ãô HomotopyCategory.quotientFunctor W)
    (by
      rintro _ _ _ _ ‚ü®œÜ‚ü©
      apply CategoryTheory.Quotient.sound
      apply HoRel‚ÇÇ.mk' (œÜ := F.app _ œÜ)
        (f‚ÇÄ‚ÇÅ := (oneTruncation‚ÇÇ.map F).map (ev01‚ÇÇ œÜ))
        (f‚ÇÄ‚ÇÇ := (oneTruncation‚ÇÇ.map F).map (ev02‚ÇÇ œÜ))
        (f‚ÇÅ‚ÇÇ := (oneTruncation‚ÇÇ.map F).map (ev12‚ÇÇ œÜ))
      all_goals
        apply FunctorToTypes.naturality)

/-- The functor that takes a 2-truncated simplicial set to its homotopy category. -/
def hoFunctor‚ÇÇ : SSet.Truncated.{u} 2 ‚•§ Cat.{u,u} where
  obj V := Cat.of (V.HomotopyCategory)
  map {S T} F := mapHomotopyCategory F
  map_id S := by
    apply Quotient.lift_unique'
    simp [mapHomotopyCategory, Quotient.lift_spec]
    exact Eq.trans (Functor.id_comp ..) (Functor.comp_id _).symm
  map_comp {S T U} F G := by
    apply Quotient.lift_unique'
    simp [mapHomotopyCategory, SSet.Truncated.HomotopyCategory.quotientFunctor]
    rw [Quotient.lift_spec, Cat.comp_eq_comp, Cat.comp_eq_comp, ‚Üê Functor.assoc, Functor.assoc,
      Quotient.lift_spec, Functor.assoc, Quotient.lift_spec]

theorem hoFunctor‚ÇÇ_naturality {X Y : SSet.Truncated.{u} 2} (f : X ‚ü∂ Y) :
    (oneTruncation‚ÇÇ ‚ãô Cat.freeRefl).map f ‚ãô SSet.Truncated.HomotopyCategory.quotientFunctor Y =
      SSet.Truncated.HomotopyCategory.quotientFunctor X ‚ãô mapHomotopyCategory f := rfl

end Truncated

/-- The functor that takes a simplicial set to its homotopy category by passing through the
2-truncation. -/
def hoFunctor : SSet.{u} ‚•§ Cat.{u, u} := SSet.truncation 2 ‚ãô Truncated.hoFunctor‚ÇÇ

end

end

end SSet
