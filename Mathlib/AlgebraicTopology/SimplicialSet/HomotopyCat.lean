/-
Copyright (c) 2024 Mario Carneiro and Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, JoÃ«l Riou
-/

import Mathlib.AlgebraicTopology.SimplicialObject.Basic
import Mathlib.AlgebraicTopology.SimplicialSet.Coskeletal
import Mathlib.CategoryTheory.Category.Cat.Terminal
import Mathlib.CategoryTheory.Category.ReflQuiv
import Mathlib.Combinatorics.Quiver.ReflQuiver


/-!

# The homotopy category of a simplicial set

The homotopy category of a simplicial set is defined as a quotient of the free category on its
underlying reflexive quiver (equivalently its one truncation). The quotient imposes an additional
hom relation on this free category, asserting that `f â‰« g = h` whenever `f`, `g`, and `h` are
respectively the 2nd, 0th, and 1st faces of a 2-simplex.

In fact, the associated functor

`SSet.hoFunctor : SSet.{u} â¥¤ Cat.{u, u} := SSet.truncation 2 â‹™ SSet.hoFunctorâ‚‚`

is defined by first restricting from simplicial sets to 2-truncated simplicial sets (throwing away
the data that is not used for the construction of the homotopy category) and then composing with an
analogously defined `SSet.hoFunctorâ‚‚ : SSet.Truncated.{u} 2 â¥¤ Cat.{u,u}` implemented relative to
the syntax of the 2-truncated simplex category.

In the file `Mathlib/AlgebraicTopology/SimplicialSet/NerveAdjunction.lean` we show the functor
`SSet.hoFunctor` to be left adjoint to the nerve by providing an analogous decomposition of the
nerve functor, made by possible by the fact that nerves of categories are 2-coskeletal, and then
composing a pair of adjunctions, which factor through the category of 2-truncated simplicial sets.
-/

namespace SSet
open CategoryTheory Category Limits Functor Opposite Simplicial Nerve
open SimplexCategory.Truncated SimplicialObject.Truncated

universe v u

section

/-- A 2-truncated simplicial set `S` has an underlying refl quiver with `S _â¦‹0â¦Œâ‚‚` as its underlying
type. -/
def OneTruncationâ‚‚ (S : SSet.Truncated 2) := S _â¦‹0â¦Œâ‚‚

/-- Abbreviations for face maps in the 2-truncated simplex category. -/
abbrev Î´â‚‚ {n} (i : Fin (n + 2)) (hn := by decide) (hn' := by decide) :
    (âŸ¨â¦‹nâ¦Œ, hnâŸ© : SimplexCategory.Truncated 2) âŸ¶ âŸ¨â¦‹n + 1â¦Œ, hn'âŸ© := SimplexCategory.Î´ i

/-- Abbreviations for degeneracy maps in the 2-truncated simplex category. -/
abbrev Ïƒâ‚‚ {n} (i : Fin (n + 1)) (hn := by decide) (hn' := by decide) :
    (âŸ¨â¦‹n+1â¦Œ, hnâŸ© : SimplexCategory.Truncated 2) âŸ¶ âŸ¨â¦‹nâ¦Œ, hn'âŸ© := SimplexCategory.Ïƒ i

@[reassoc (attr := simp)]
lemma Î´â‚‚_zero_comp_Ïƒâ‚‚_zero {n} (hn := by decide) (hn' := by decide) :
    Î´â‚‚ (n := n) 0 hn hn' â‰« Ïƒâ‚‚ 0 hn' hn = ðŸ™ _ := SimplexCategory.Î´_comp_Ïƒ_self

@[reassoc]
lemma Î´â‚‚_zero_comp_Ïƒâ‚‚_one : Î´â‚‚ (0 : Fin 3) â‰« Ïƒâ‚‚ 1 = Ïƒâ‚‚ 0 â‰« Î´â‚‚ 0 :=
  SimplexCategory.Î´_comp_Ïƒ_of_le (i := 0) (j := 0) (Fin.zero_le _)

@[reassoc (attr := simp)]
lemma Î´â‚‚_one_comp_Ïƒâ‚‚_zero {n} (hn := by decide) (hn' := by decide) :
    Î´â‚‚ (n := n) 1 hn hn' â‰« Ïƒâ‚‚ 0 hn' hn = ðŸ™ _ := SimplexCategory.Î´_comp_Ïƒ_succ

@[reassoc (attr := simp)]
lemma Î´â‚‚_two_comp_Ïƒâ‚‚_one : Î´â‚‚ (2 : Fin 3) â‰« Ïƒâ‚‚ 1 = ðŸ™ _ := SimplexCategory.Î´_comp_Ïƒ_succ' (by decide)

@[reassoc]
lemma Î´â‚‚_two_comp_Ïƒâ‚‚_zero : Î´â‚‚ (2 : Fin 3) â‰« Ïƒâ‚‚ 0 = Ïƒâ‚‚ 0 â‰« Î´â‚‚ 1 :=
  SimplexCategory.Î´_comp_Ïƒ_of_gt' (by decide)

/-- The hom-types of the refl quiver underlying a simplicial set `S` are types of edges in `S _â¦‹1â¦Œâ‚‚`
together with source and target equalities. -/
@[ext]
structure OneTruncationâ‚‚.Hom {S : SSet.Truncated 2} (X Y : OneTruncationâ‚‚ S) where
  /-- An arrow in `OneTruncationâ‚‚.Hom X Y` includes the data of a 1-simplex. -/
  edge : S _â¦‹1â¦Œâ‚‚
  /-- An arrow in `OneTruncationâ‚‚.Hom X Y` includes a source equality. -/
  src_eq : S.map (Î´â‚‚ 1).op edge = X
  /-- An arrow in `OneTruncationâ‚‚.Hom X Y` includes a target equality. -/
  tgt_eq : S.map (Î´â‚‚ 0).op edge = Y

/-- A 2-truncated simplicial set `S` has an underlying refl quiver `SSet.OneTruncationâ‚‚ S`. -/
instance (S : SSet.Truncated 2) : ReflQuiver (OneTruncationâ‚‚ S) where
  Hom X Y := SSet.OneTruncationâ‚‚.Hom X Y
  id X :=
    { edge := S.map (SSet.Ïƒâ‚‚ (n := 0) 0).op X
      src_eq := by
        simp only [â† FunctorToTypes.map_comp_apply, â† op_comp, Î´â‚‚_one_comp_Ïƒâ‚‚_zero,
          op_id, FunctorToTypes.map_id_apply]
      tgt_eq := by
        simp only [â† FunctorToTypes.map_comp_apply, â† op_comp, Î´â‚‚_zero_comp_Ïƒâ‚‚_zero,
          op_id, FunctorToTypes.map_id_apply] }

@[simp]
lemma OneTruncationâ‚‚.id_edge {S : SSet.Truncated 2} (X : OneTruncationâ‚‚ S) :
    OneTruncationâ‚‚.Hom.edge (ðŸ™rq X) = S.map (SSet.Ïƒâ‚‚ 0).op X := rfl

/-- The functor that carries a 2-truncated simplicial set to its underlying refl quiver. -/
@[simps]
def oneTruncationâ‚‚ : SSet.Truncated.{u} 2 â¥¤ ReflQuiv.{u, u} where
  obj S := ReflQuiv.of (OneTruncationâ‚‚ S)
  map {S T} F := {
    obj := F.app (op â¦‹0â¦Œâ‚‚)
    map := fun f â†¦
      { edge := F.app _ f.edge
        src_eq := by rw [â† FunctorToTypes.naturality, f.src_eq]
        tgt_eq := by rw [â† FunctorToTypes.naturality, f.tgt_eq] }
    map_id := fun X â†¦ OneTruncationâ‚‚.Hom.ext (by
      dsimp
      rw [â† FunctorToTypes.naturality]) }

@[ext]
lemma OneTruncationâ‚‚.hom_ext {S : SSet.Truncated 2} {x y : OneTruncationâ‚‚ S} {f g : x âŸ¶ y} :
    f.edge = g.edge â†’ f = g := OneTruncationâ‚‚.Hom.ext

@[simp]
lemma OneTruncationâ‚‚.homOfEq_edge
    {X : SSet.Truncated.{u} 2} {xâ‚ yâ‚ xâ‚‚ yâ‚‚ : OneTruncationâ‚‚ X}
    (f : xâ‚ âŸ¶ yâ‚) (hx : xâ‚ = xâ‚‚) (hy : yâ‚ = yâ‚‚) :
    (Quiver.homOfEq f hx hy).edge = f.edge := by
  subst hx hy
  rfl

section
variable {C : Type u} [Category.{v} C]

/-- An equivalence between the type of objects underlying a category and the type of 0-simplices in
the 2-truncated nerve. -/
@[simps]
def OneTruncationâ‚‚.nerveEquiv :
    OneTruncationâ‚‚ ((SSet.truncation 2).obj (nerve C)) â‰ƒ C where
  toFun X := X.obj' 0
  invFun X := .mkâ‚€ X
  left_inv _ := ComposableArrows.extâ‚€ rfl
  right_inv _ := rfl

/-- A hom equivalence over the function `OneTruncationâ‚‚.nerveEquiv`. -/
def OneTruncationâ‚‚.nerveHomEquiv (X Y : OneTruncationâ‚‚ ((SSet.truncation 2).obj (nerve C))) :
  (X âŸ¶ Y) â‰ƒ (nerveEquiv X âŸ¶ nerveEquiv Y) where
  toFun Ï† := eqToHom (congr_arg ComposableArrows.left Ï†.src_eq.symm) â‰« Ï†.edge.hom â‰«
      eqToHom (congr_arg ComposableArrows.left Ï†.tgt_eq)
  invFun f :=
    { edge := ComposableArrows.mkâ‚ f
      src_eq := ComposableArrows.extâ‚€ rfl
      tgt_eq := ComposableArrows.extâ‚€ rfl }
  left_inv Ï† := by
    ext
    exact ComposableArrows.extâ‚ (congr_arg ComposableArrows.left Ï†.src_eq).symm
      (congr_arg ComposableArrows.left Ï†.tgt_eq).symm rfl
  right_inv f := by dsimp; simp only [comp_id, id_comp]; rfl

/-- The refl quiver underlying a nerve is isomorphic to the refl quiver underlying the category. -/
def OneTruncationâ‚‚.ofNerveâ‚‚ (C : Type u) [Category.{u} C] :
    ReflQuiv.of (OneTruncationâ‚‚ (nerveFunctorâ‚‚.obj (Cat.of C))) â‰… ReflQuiv.of C := by
  apply ReflQuiv.isoOfEquiv.{u,u} OneTruncationâ‚‚.nerveEquiv OneTruncationâ‚‚.nerveHomEquiv ?_
  intro X
  unfold nerveEquiv nerveHomEquiv
  simp only [Cat.of_Î±, op_obj, ComposableArrows.obj', Fin.zero_eta, Fin.isValue, Equiv.coe_fn_mk,
    nerveEquiv_apply, Nat.reduceAdd, id_edge, SimplexCategory.len_mk, id_eq, eqToHom_refl, comp_id,
    id_comp, ReflQuiver.id_eq_id]
  unfold nerve truncation SimplicialObject.truncation SimplexCategory.Truncated.inclusion
  -- the following was obtained by `simp?`
  simp only [ObjectProperty.Î¹_obj, SimplexCategory.len_mk, Nat.reduceAdd, Fin.isValue,
    SimplexCategory.toCat_map, whiskeringLeft_obj_obj, Functor.comp_map, op_obj, op_map,
    Quiver.Hom.unop_op, ObjectProperty.Î¹_map, ComposableArrows.whiskerLeft_map, Fin.zero_eta,
    Monotone.functor_obj, Fin.mk_one, homOfLE_leOfHom]
  show X.map (ðŸ™ _) = _
  rw [X.map_id]
  rfl

/-- The refl quiver underlying a nerve is naturally isomorphic to the refl quiver underlying the
category. -/
@[simps! hom_app_obj hom_app_map inv_app_obj_obj inv_app_obj_map inv_app_map]
def OneTruncationâ‚‚.ofNerveâ‚‚.natIso :
    nerveFunctorâ‚‚.{u,u} â‹™ SSet.oneTruncationâ‚‚ â‰… ReflQuiv.forget :=
  NatIso.ofComponents (fun C => OneTruncationâ‚‚.ofNerveâ‚‚ C) (by
  Â· intro C D F
    fapply ReflPrefunctor.ext <;> simp
    Â· exact fun _ â†¦ rfl
    Â· intro X Y f
      obtain âŸ¨f, rfl, rflâŸ© := f
      unfold SSet.oneTruncationâ‚‚ nerveFunctorâ‚‚ SSet.truncation SimplicialObject.truncation
        nerveFunctor toReflPrefunctor
      simp only [comp_obj, whiskeringLeft_obj_obj, ReflQuiv.of_val, Functor.comp_map,
        whiskeringLeft_obj_map, whiskerLeft_app, op_obj, whiskeringRight_obj_obj, ofNerveâ‚‚,
        Cat.of_Î±, nerveEquiv, ComposableArrows.obj', Fin.zero_eta, Fin.isValue,
        ReflQuiv.comp_eq_comp, Nat.reduceAdd, SimplexCategory.len_mk, id_eq, op_map,
        Quiver.Hom.unop_op, nerve_map, SimplexCategory.toCat_map, ReflPrefunctor.comp_obj,
        ReflPrefunctor.comp_map]
      simp [nerveHomEquiv, ReflQuiv.isoOfEquiv, ReflQuiv.isoOfQuivIso, Quiv.isoOfEquiv])

end

section

private lemma map_map_of_eq.{w}  {C : Type u} [Category.{v} C] (V : Cáµ’áµ– â¥¤ Type w) {X Y Z : C}
    {Î± : X âŸ¶ Y} {Î² : Y âŸ¶ Z} {Î³ : X âŸ¶ Z} {Ï†} :
    Î± â‰« Î² = Î³ â†’ V.map Î±.op (V.map Î².op Ï†) = V.map Î³.op Ï† := by
  rintro rfl
  simp

variable {V : SSet}

namespace Truncated

/-- The map that picks up the initial vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Î¹0â‚‚ : â¦‹0â¦Œâ‚‚ âŸ¶ â¦‹2â¦Œâ‚‚ := Î´â‚‚ (n := 0) 1 â‰« Î´â‚‚ (n := 1) 1

/-- The map that picks up the middle vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Î¹1â‚‚ : â¦‹0â¦Œâ‚‚ âŸ¶ â¦‹2â¦Œâ‚‚ := Î´â‚‚ (n := 0) 0 â‰« Î´â‚‚ (n := 1) 2

/-- The map that picks up the final vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Î¹2â‚‚ : â¦‹0â¦Œâ‚‚ âŸ¶ â¦‹2â¦Œâ‚‚ := Î´â‚‚ (n := 0) 0 â‰« Î´â‚‚ (n := 1) 1

/-- The initial vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev0â‚‚ {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) : OneTruncationâ‚‚ V := V.map Î¹0â‚‚.op Ï†

/-- The middle vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev1â‚‚ {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) : OneTruncationâ‚‚ V := V.map Î¹1â‚‚.op Ï†

/-- The final vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev2â‚‚ {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) : OneTruncationâ‚‚ V := V.map Î¹2â‚‚.op Ï†

/-- The 0th face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Î´0â‚‚ : â¦‹1â¦Œâ‚‚ âŸ¶ â¦‹2â¦Œâ‚‚ := Î´â‚‚ (n := 1) 0

/-- The 1st face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Î´1â‚‚ : â¦‹1â¦Œâ‚‚ âŸ¶ â¦‹2â¦Œâ‚‚ := Î´â‚‚ (n := 1) 1

/-- The 2nd face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Î´2â‚‚ : â¦‹1â¦Œâ‚‚ âŸ¶ â¦‹2â¦Œâ‚‚ := Î´â‚‚ (n := 1) 2

/-- The arrow in the ReflQuiver `OneTruncationâ‚‚ V` of a 2-truncated simplicial set arising from the
0th face of a 2-simplex. -/
def ev12â‚‚ {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) : ev1â‚‚ Ï† âŸ¶ ev2â‚‚ Ï† :=
  âŸ¨V.map Î´0â‚‚.op Ï†,
    map_map_of_eq V (SimplexCategory.Î´_comp_Î´ (i := 0) (j := 1) (by decide)).symm,
    map_map_of_eq V rflâŸ©

/-- The arrow in the ReflQuiver `OneTruncationâ‚‚ V` of a 2-truncated simplicial set arising from the
1st face of a 2-simplex. -/
def ev02â‚‚ {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) : ev0â‚‚ Ï† âŸ¶ ev2â‚‚ Ï† :=
  âŸ¨V.map Î´1â‚‚.op Ï†, map_map_of_eq V rfl, map_map_of_eq V rflâŸ©

/-- The arrow in the ReflQuiver `OneTruncationâ‚‚ V` of a 2-truncated simplicial set arising from the
2nd face of a 2-simplex. -/
def ev01â‚‚ {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) : ev0â‚‚ Ï† âŸ¶ ev1â‚‚ Ï† :=
  âŸ¨V.map Î´2â‚‚.op Ï†, map_map_of_eq V (SimplexCategory.Î´_comp_Î´ (j := 1) le_rfl), map_map_of_eq V rflâŸ©


/-- The 2-simplices in a 2-truncated simplicial set `V` generate a hom relation on the free
category on the underlying refl quiver of `V`. -/
inductive HoRelâ‚‚ {V : SSet.Truncated 2} :
    (X Y : Cat.FreeRefl (OneTruncationâ‚‚ V)) â†’ (f g : X âŸ¶ Y) â†’ Prop
  | mk (Ï† : V _â¦‹2â¦Œâ‚‚) :
    HoRelâ‚‚ _ _
      (Quot.mk _ (Quiver.Hom.toPath (ev02â‚‚ Ï†)))
      (Quot.mk _ ((Quiver.Hom.toPath (ev01â‚‚ Ï†)).comp
        (Quiver.Hom.toPath (ev12â‚‚ Ï†))))

/-- A 2-simplex whose faces are identified with certain arrows in `OneTruncationâ‚‚ V` defines
a term of type `HoRelâ‚‚` between those arrows. -/
theorem HoRelâ‚‚.mk' {V : SSet.Truncated 2} (Ï† : V _â¦‹2â¦Œâ‚‚) {Xâ‚€ Xâ‚ Xâ‚‚ : OneTruncationâ‚‚ V}
    (fâ‚€â‚ : Xâ‚€ âŸ¶ Xâ‚) (fâ‚â‚‚ : Xâ‚ âŸ¶ Xâ‚‚) (fâ‚€â‚‚ : Xâ‚€ âŸ¶ Xâ‚‚)
    (hâ‚€â‚ : fâ‚€â‚.edge = V.map (Î´â‚‚ 2).op Ï†) (hâ‚â‚‚ : fâ‚â‚‚.edge = V.map (Î´â‚‚ 0).op Ï†)
    (hâ‚€â‚‚ : fâ‚€â‚‚.edge = V.map (Î´â‚‚ 1).op Ï†) :
    HoRelâ‚‚ _ _ (Quot.mk _ (Quiver.Hom.toPath fâ‚€â‚‚))
      (Quot.mk _ ((Quiver.Hom.toPath fâ‚€â‚).comp (Quiver.Hom.toPath fâ‚â‚‚))) := by
  obtain rfl : Xâ‚€ = ev0â‚‚ Ï† := by
    rw [â† fâ‚€â‚‚.src_eq, hâ‚€â‚‚, â† FunctorToTypes.map_comp_apply, â† op_comp]
    rfl
  obtain rfl : Xâ‚ = ev1â‚‚ Ï† := by
    rw [â† fâ‚€â‚.tgt_eq, hâ‚€â‚, â† FunctorToTypes.map_comp_apply, â† op_comp]
    rfl
  obtain rfl : Xâ‚‚ = ev2â‚‚ Ï† := by
    rw [â† fâ‚â‚‚.tgt_eq, hâ‚â‚‚, â† FunctorToTypes.map_comp_apply, â† op_comp]
    rfl
  obtain rfl : fâ‚€â‚ = ev01â‚‚ Ï† := by ext; assumption
  obtain rfl : fâ‚â‚‚ = ev12â‚‚ Ï† := by ext; assumption
  obtain rfl : fâ‚€â‚‚ = ev02â‚‚ Ï† := by ext; assumption
  constructor

/-- The type underlying the homotopy category of a 2-truncated simplicial set `V`. -/
def _root_.SSet.Truncated.HomotopyCategory (V : SSet.Truncated.{u} 2) : Type u :=
  Quotient (HoRelâ‚‚ (V := V))

instance (V : SSet.Truncated.{u} 2) : Category.{u} (V.HomotopyCategory) :=
  inferInstanceAs (Category (CategoryTheory.Quotient ..))

/-- A canonical functor from the free category on the refl quiver underlying a 2-truncated
simplicial set `V` to its homotopy category. -/
def _root_.SSet.Truncated.HomotopyCategory.quotientFunctor (V : SSet.Truncated.{u} 2) :
    Cat.FreeRefl (OneTruncationâ‚‚ V) â¥¤ V.HomotopyCategory :=
  Quotient.functor _

/-- By `Quotient.lift_unique'` (not `Quotient.lift`) we have that `quotientFunctor V` is an
epimorphism. -/
theorem HomotopyCategory.lift_unique' (V : SSet.Truncated.{u} 2) {D} [Category D]
    (Fâ‚ Fâ‚‚ : V.HomotopyCategory â¥¤ D)
    (h : HomotopyCategory.quotientFunctor V â‹™ Fâ‚ = HomotopyCategory.quotientFunctor V â‹™ Fâ‚‚) :
    Fâ‚ = Fâ‚‚ :=
  Quotient.lift_unique' (C := Cat.FreeRefl (OneTruncationâ‚‚ V))
    (HoRelâ‚‚ (V := V)) _ _ h

/-- A map of 2-truncated simplicial sets induces a functor between homotopy categories. -/
def mapHomotopyCategory {V W : SSet.Truncated.{u} 2} (F : V âŸ¶ W) :
    V.HomotopyCategory â¥¤ W.HomotopyCategory :=
  CategoryTheory.Quotient.lift _
    ((oneTruncationâ‚‚ â‹™ Cat.freeRefl).map F â‹™ HomotopyCategory.quotientFunctor W)
    (by
      rintro _ _ _ _ âŸ¨Ï†âŸ©
      apply CategoryTheory.Quotient.sound
      apply HoRelâ‚‚.mk' (Ï† := F.app _ Ï†)
        (fâ‚€â‚ := (oneTruncationâ‚‚.map F).map (ev01â‚‚ Ï†))
        (fâ‚€â‚‚ := (oneTruncationâ‚‚.map F).map (ev02â‚‚ Ï†))
        (fâ‚â‚‚ := (oneTruncationâ‚‚.map F).map (ev12â‚‚ Ï†))
      all_goals
        apply FunctorToTypes.naturality)

/-- The functor that takes a 2-truncated simplicial set to its homotopy category. -/
def hoFunctorâ‚‚ : SSet.Truncated.{u} 2 â¥¤ Cat.{u,u} where
  obj V := Cat.of (V.HomotopyCategory)
  map {S T} F := mapHomotopyCategory F
  map_id S := by
    apply Quotient.lift_unique'
    simp [mapHomotopyCategory, Quotient.lift_spec]
    exact Eq.trans (Functor.id_comp ..) (Functor.comp_id _).symm
  map_comp {S T U} F G := by
    apply Quotient.lift_unique'
    simp [mapHomotopyCategory, SSet.Truncated.HomotopyCategory.quotientFunctor]
    rw [Quotient.lift_spec, Cat.comp_eq_comp, Cat.comp_eq_comp, â† Functor.assoc, Functor.assoc,
      Quotient.lift_spec, Functor.assoc, Quotient.lift_spec]

theorem hoFunctorâ‚‚_naturality {X Y : SSet.Truncated.{u} 2} (f : X âŸ¶ Y) :
    (oneTruncationâ‚‚ â‹™ Cat.freeRefl).map f â‹™ SSet.Truncated.HomotopyCategory.quotientFunctor Y =
      SSet.Truncated.HomotopyCategory.quotientFunctor X â‹™ mapHomotopyCategory f := rfl

end Truncated

/-- The functor that takes a simplicial set to its homotopy category by passing through the
2-truncation. -/
def hoFunctor : SSet.{u} â¥¤ Cat.{u, u} := SSet.truncation 2 â‹™ Truncated.hoFunctorâ‚‚

end

end
section

instance instUniqueOneTruncationâ‚‚DeltaZero : Unique (OneTruncationâ‚‚ ((truncation 2).obj Î”[0])) :=
  letI : Unique (â¦‹0â¦Œ âŸ¶ â¦‹0â¦Œ) :=
    Limits.isTerminalEquivUnique _ _ |>.toFun SimplexCategory.isTerminalZero _
  inferInstanceAs (Unique (ULift.{_, 0} (â¦‹0â¦Œ âŸ¶ â¦‹0â¦Œ)))

instance (x y : OneTruncationâ‚‚ ((truncation 2).obj Î”[0])) : Unique (x âŸ¶ y) where
  default := by
    obtain rfl : x = default := Unique.uniq _ _
    obtain rfl : y = default := Unique.uniq _ _
    exact ðŸ™rq instUniqueOneTruncationâ‚‚DeltaZero.default
  uniq _ := by
    letI : Subsingleton (((truncation 2).obj Î”[0]).obj (.op â¦‹1â¦Œâ‚‚)) :=
      letI : Unique (â¦‹1â¦Œ âŸ¶ â¦‹0â¦Œ) :=
        Limits.isTerminalEquivUnique _ _ |>.toFun SimplexCategory.isTerminalZero _
      inferInstanceAs (Subsingleton (ULift.{_, 0} (â¦‹1â¦Œ âŸ¶ â¦‹0â¦Œ)))
    ext
    exact this.allEq _ _

def goal : IsTerminal (hoFunctor.obj (Î”[0])) := by
  letI : Unique ((truncation 2).obj Î”[0]).HomotopyCategory :=
    inferInstanceAs (Unique <| CategoryTheory.Quotient Truncated.HoRelâ‚‚)
  letI sub : Subsingleton ((truncation 2).obj Î”[0]).HomotopyCategory :=  by infer_instance
  letI : IsDiscrete ((truncation 2).obj Î”[0]).HomotopyCategory :=
    { subsingleton X Y := by
        exact inferInstanceAs <| Subsingleton ((_ : CategoryTheory.Quotient Truncated.HoRelâ‚‚) âŸ¶ _)
      eq_of_hom f := sub.allEq _ _ }
  apply Cat.isDiscreteUnique.isTerminal

end


end SSet
