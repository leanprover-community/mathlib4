/-
Copyright (c) 2024 Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.StrictSegal
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Functor.KanExtension.Basic

/-!
# Coskeletal simplicial sets

The identity natural transformation exhibits a simplicial set `X` as a right extension of its
restriction along `(Truncated.inclusion (n := n)).op` recorded by `rightExtensionInclusion X n`.

The simplicial set `X` is *n-coskeletal* if
`(rightExtensionInclusion X n).IsPointwiseRightKanExtension` holds.

In this file, we prove that if `X` is `StrictSegal` then `X` is 2-coskeletal defining

`StrictSegal.cosk2Iso : X â‰… (Truncated.cosk 2).obj ((truncation 2).obj X)`

In particular, nerves of categories are 2-coskeletal.
-/

universe v u

open CategoryTheory Simplicial SimplexCategory Opposite Category Functor Limits

namespace SSet

/-- The identity natural transformation exhibits a simplicial set as a right extension of its
restriction along `(Truncated.inclusion (n := n)).op`.-/
@[simps!]
def rightExtensionInclusion (X : SSet.{u}) (n : â„•) :
    RightExtension (Truncated.inclusion (n := n)).op
      (Functor.op Truncated.inclusion â‹™ X) := RightExtension.mk _ (ðŸ™ _)

section

local notation (priority := high) "[" n "]" => SimplexCategory.mk n

set_option quotPrecheck false
local macro:max (priority := high) "[" n:term "]â‚‚" : term =>
  `((âŸ¨SimplexCategory.mk $n, by decideâŸ© : SimplexCategory.Truncated 2))


namespace StructuredArrow

/-- The object of `StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op` corresponding to the
map [0] âŸ¶ [n] with image `i`. -/
private
def pt {n} (i : Fin (n + 1)) : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op :=
  .mk (Y := op [0]â‚‚) (.op (SimplexCategory.const _ _ i))

/-- The object of `StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op` corresponding to
the map `[1] âŸ¶ [n]` with image `i âŸ¶ i+1`. -/
private
def ar {n} (i : Fin n) : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op :=
  .mk (Y := op [1]â‚‚) (.op (mkOfLe _ _ (Fin.castSucc_le_succ i)))

/-- The object of StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op corresponding to
`ar k`. -/
private
def ar' {n} {i j : Fin (n+1)} (k : i âŸ¶ j) :
    StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op :=
  .mk (Y := op [1]â‚‚) (mkOfLe _ _ k.le).op

/-- An arrow in `StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op` arising from
`const 0 : [0] âŸ¶ [1]`. -/
private
def ar.src {n} (i : Fin n) : (ar i) âŸ¶ (pt i.castSucc) := by
  refine StructuredArrow.homMk (.op (SimplexCategory.const _ _ 0)) ?_
  apply Quiver.Hom.unop_inj
  ext z; revert z
  intro (0 : Fin 1)
  unfold ar pt
  simp only [StructuredArrow.mk_left, const_obj_obj, len_mk, StructuredArrow.mk_right, op_obj,
    StructuredArrow.mk_hom_eq_self, Fin.isValue, op_map, Quiver.Hom.unop_op, unop_comp,
    comp_toOrderHom, OrderHom.comp_coe, Function.comp_apply]
  rfl

/-- An arrow in `StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op` arising from
`const 1 : [0] âŸ¶ [1]`. -/
private
def ar.tgt {n} (i : Fin n) : (ar i) âŸ¶ (pt i.succ) := by
  refine StructuredArrow.homMk (.op (SimplexCategory.const _ _ 1)) ?_
  apply Quiver.Hom.unop_inj
  ext z; revert z
  intro (0 : Fin 1)
  unfold ar pt
  simp only [StructuredArrow.mk_left, const_obj_obj, len_mk, StructuredArrow.mk_right, op_obj,
    StructuredArrow.mk_hom_eq_self, Fin.isValue, op_map, Quiver.Hom.unop_op, unop_comp,
    comp_toOrderHom, OrderHom.comp_coe, Function.comp_apply]
  rfl

/-- For any cone over the diagram
  `(StructuredArrow.proj (op [n])
  (Truncated.inclusion (n := 2)).op â‹™ (Truncated.inclusion (n := 2)).op â‹™ X)`,
the map `ar.src` induces a commutative triangle.-/
private
theorem ranCone_ar_src {X : SSet.{u}} {n} {i : Fin n}
    (s : Cone (StructuredArrow.proj (op [n]) (Truncated.inclusion (n := 2)).op â‹™
      (Truncated.inclusion (n := 2)).op â‹™ X)) (x : s.pt) :
    X.Î´ 1 (s.Ï€.app (ar i) x) = s.Ï€.app (pt i.castSucc) x := by
  have hi := congr_fun (s.Ï€.naturality (ar.src i)) x
  unfold hom at hi
  simp at hi
  rw [hi]
  simp [ar.src, Truncated.inclusion]
  have : Î´ 1 = [0].const [1] 0 := SimplexCategory.eq_const_of_zero _
  rw [â† this]
  rfl

/-- For any cone over the diagram `(StructuredArrow.proj (op [n])`
`(Truncated.inclusion (n := 2)).op â‹™ (Truncated.inclusion (n := 2)).op â‹™ X)`,
the map `ar.tgt` induces a commutative triangle.-/
private
theorem ranCone_ar_tgt {X : SSet.{u}} {n} {i : Fin n}
    (s : Cone (StructuredArrow.proj (op [n]) (Truncated.inclusion (n := 2)).op â‹™
      (Truncated.inclusion (n := 2)).op â‹™ X)) (x : s.pt) :
    X.Î´ 0 (s.Ï€.app (ar i) x) = s.Ï€.app (pt i.succ) x := by
  have hi := congr_fun (s.Ï€.naturality (ar.tgt i)) x
  simp at hi
  rw [hi]
  simp [ar.tgt, Truncated.inclusion]
  have : Î´ 0 = [0].const [1] 1 := SimplexCategory.eq_const_of_zero _
  rw [â† this]
  rfl

/-- An object `j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op`, corresponding to a
map `j.right.unop.obj âŸ¶ [n]` in the simplex category, defines a morphism
`Fin (jlen+1) -> Fin(n+1)`. This calculates the image of `i : Fin(jlen+1)`, which we might think of
as "j(i)"".-/
private
def strArr.homEv {n} (j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op)
    (i : Fin (j.right.unop.obj.len + 1)) :
    Fin (n + 1) := (SimplexCategory.Hom.toOrderHom j.hom.unop) i

/-- This is the unique arrow in `StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op` from
`j` to `j âŸ¶ (pt (strArr.homEv j i))`. This is used to prove that ran.lift defines a factorization
on objects.-/
private
def fact.obj.arr {n}
    (j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op)
    (i : Fin (j.right.unop.obj.len + 1)) : j âŸ¶ (pt (strArr.homEv j i)) :=
  StructuredArrow.homMk (.op (SimplexCategory.const _ _ i)) <| by
    apply Quiver.Hom.unop_inj
    ext z; revert z; intro | 0 => rfl

/-- An object `j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op`, corresponding to a
map `j.right.unop.obj âŸ¶ [n]` in the simplex category, defines a morphism
`Fin (jlen+1) -> Fin(n+1)`. This calculates the image of `i.succ : Fin(jlen+1)`, which we might
think of as j(i.succ). -/
private
def strArr.homEvSucc {n} (j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op)
    (i : Fin j.right.unop.obj.len) :
    Fin (n + 1) := (SimplexCategory.Hom.toOrderHom j.hom.unop) i.succ

/-- The unique arrow `(strArr.homEv j i.castSucc) âŸ¶ (strArr.homEvSucc j i)` in `Fin(n+1)`. -/
private
def strArr.homEv.map {n} (j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op)
    (i : Fin j.right.unop.obj.len) :
    strArr.homEv j i.castSucc âŸ¶ strArr.homEvSucc j i :=
  (Monotone.functor (j.hom.unop.toOrderHom).monotone).map (Fin.hom_succ i)

/-- This is the unique arrow in `StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op` from
`j` to `ar' (strArr.homEv.map j i)`. This is used to prove that ran.lift defines a
factorization on maps.-/
private
def fact.map.arr {n}
    (j : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op)
    (i : Fin (unop j.right).1.len) : j âŸ¶ ar' (strArr.homEv.map j i) := by
  fapply StructuredArrow.homMk
  Â· exact .op (mkOfSucc i : [1] âŸ¶ [(unop j.right).1.len])
  Â· apply Quiver.Hom.unop_inj
    ext z; revert z
    intro |0 | 1 => rfl

end StructuredArrow

open StructuredArrow

/-- Given a term in the cone over the diagram `(StructuredArrow.proj (op [n])`
`(Truncated.inclusion (n := 2)).op â‹™ (Truncated.inclusion (n := 2)).op â‹™ X)` where `X` is
Strict Segal, one can produce an `n`-simplex in `X`.-/
@[simp]
private
noncomputable def ran.lift {X : SSet.{u}} [StrictSegal X] {n}
    (s : Cone (StructuredArrow.proj (op [n]) (Truncated.inclusion (n := 2)).op â‹™
      (Truncated.inclusion (n := 2)).op â‹™ X)) (x : s.pt) : X _[n] :=
  StrictSegal.spineToSimplex {
    vertex := fun i â†¦ s.Ï€.app (pt i) x
    arrow := fun i â†¦ s.Ï€.app (ar i) x
    arrow_src := fun _ â†¦ ranCone_ar_src s x
    arrow_tgt := fun _ â†¦ ranCone_ar_tgt s x
  }

/-- This theorem is used to prove the factorization property of `ran.lift`.-/
private
theorem ran.lift.map {X : SSet.{u}} [StrictSegal X] {n}
    (s : Cone (StructuredArrow.proj (op [n])
    (Truncated.inclusion (n := 2)).op â‹™ (Truncated.inclusion (n := 2)).op â‹™ X)) (x : s.pt)
    (j k : Fin (n + 1)) (hjk : j âŸ¶ k) :
      X.map (mkOfLe j k hjk.le).op (ran.lift s x) = s.Ï€.app (ar' hjk) x := by
  have âŸ¨i, hiâŸ© : âˆƒ (i : â„•), k.1 = j.1 + i := by
    use k.1 - j.1
    simp [Nat.add_sub_cancel' hjk.le, hjk.le]
  induction i generalizing k with
  | zero =>
    simp [Fin.val_inj] at hi; cases hi
    have ceq : mkOfLe j j (by omega) = [1].const [0] 0 â‰« [0].const [n] j := Hom.ext_one_left ..
    rw [ceq]
    let map : pt j âŸ¶ ar' hjk := by
      refine StructuredArrow.homMk ([1].const [0] 0).op ?_
      unfold pt ar'
      dsimp only [StructuredArrow.mk_left, const_obj_obj, Fin.val_zero, Nat.add_zero, id_eq,
        Int.reduceNeg, Int.Nat.cast_ofNat_Int, Int.reduceAdd, Fin.eta, homOfLE_refl,
        StructuredArrow.mk_right, op_obj, StructuredArrow.mk_hom_eq_self, Fin.isValue, op_map]
      rw [ceq]
      rfl
    have nat := congr_fun (s.Ï€.naturality map) x
    dsimp only [Fin.val_zero, Nat.add_zero, id_eq, Int.reduceNeg, Int.Nat.cast_ofNat_Int,
      Int.reduceAdd, Fin.eta, comp_obj, StructuredArrow.proj_obj, op_obj, const_obj_obj,
      const_obj_map, types_comp_apply, types_id_apply, Functor.comp_map, StructuredArrow.proj_map,
      op_map] at nat
    rw [nat]
    simp only [map, StructuredArrow.homMk_right]
    rw [op_comp, Functor.map_comp]
    simp only [types_comp_apply]
    refine congrArg (X.map ([1].const [0] 0).op) ?_
    rw [ran.lift, StrictSegal.spineToSimplex_vertex]
  | succ i ih =>
    obtain âŸ¨kâ‚€, rflâŸ© : âˆƒ kâ‚€ : Fin n, k = Fin.succ kâ‚€ := by
      let âŸ¨k+1, hkâŸ© := k
      exact âŸ¨âŸ¨k, Nat.lt_of_succ_lt_succ hkâŸ©, rflâŸ©
    have hjkâ‚€ : j.1 â‰¤ kâ‚€.1 := by
      rw [Nat.succ_inj.1 hi]
      exact Nat.le_add_right ..
    let tri : ([2] : SimplexCategory) âŸ¶ [n] :=
      mkOfLeComp j kâ‚€.castSucc kâ‚€.succ hjkâ‚€ (Nat.le_succ _)
    let tri' : StructuredArrow (op [n]) (Truncated.inclusion (n := 2)).op :=
      .mk (Y := op [2]â‚‚) tri.op
    let facemapâ‚‚ : tri' âŸ¶ ar' (hjkâ‚€.hom (y := kâ‚€.castSucc)) := by
      refine StructuredArrow.homMk (.op (Î´ 2)) ?_
      dsimp [tri', tri, ar']
      rw [â† op_comp]
      apply Quiver.Hom.unop_inj
      exact Hom.ext_one_left ..
    let facemapâ‚€ : tri' âŸ¶ ar kâ‚€ :=
      StructuredArrow.homMk (.op (Î´ 0)) (Quiver.Hom.unop_inj (Hom.ext_one_left ..))
    let facemapâ‚ : tri' âŸ¶ ar' hjk :=
      StructuredArrow.homMk (.op (Î´ 1)) (Quiver.Hom.unop_inj (Hom.ext_one_left ..))
    let triâ‚€ : tri' âŸ¶ pt j :=
      StructuredArrow.homMk (.op (const [0] _ 0)) (Quiver.Hom.unop_inj (Hom.ext_zero_left ..))
    let triâ‚ : tri' âŸ¶ pt kâ‚€.castSucc :=
      StructuredArrow.homMk (.op (const [0] _ 1)) (Quiver.Hom.unop_inj (Hom.ext_zero_left ..))
    let triâ‚‚ : tri' âŸ¶ pt kâ‚€.succ :=
      StructuredArrow.homMk (.op (const [0] _ 2)) (Quiver.Hom.unop_inj (Hom.ext_zero_left ..))
    have nat := congr_fun (s.Ï€.naturality facemapâ‚) x
    simp only [Int.reduceNeg, id_eq, Int.Nat.cast_ofNat_Int, homOfLE_leOfHom,
      comp_obj, StructuredArrow.proj_obj, op_obj, const_obj_obj, const_obj_map, types_comp_apply,
      types_id_apply, Functor.comp_map, StructuredArrow.proj_map, op_map] at nat
    rw [nat]
    unfold facemapâ‚
    simp only [StructuredArrow.homMk_right, Quiver.Hom.unop_op]
    rw [show mkOfLe _ _ hjk.le = (Î´ 1) â‰« tri from Hom.ext_one_left .., op_comp, Functor.map_comp]
    refine congrArg (X.map (Î´ 1).op) ?_
    apply StrictSegal.spineInjective
    unfold StrictSegal.spineEquiv
    dsimp
    ext i'
    Â· simp only [spine_vertex, â† FunctorToTypes.map_comp_apply, â† op_comp]
      rw [show [0].const [2] i' â‰« tri = [0].const [n] (([0].const [2] i' â‰« tri).toOrderHom 0) from
        eq_const_of_zero _]
      simp only [ran.lift, StrictSegal.spineToSimplex_vertex]
      match i' with
      | 0 => exact congr_fun (s.Ï€.naturality triâ‚€) x
      | 1 => exact congr_fun (s.Ï€.naturality triâ‚) x
      | 2 => exact congr_fun (s.Ï€.naturality triâ‚‚) x
    Â· simp only [spine_arrow, â† FunctorToTypes.map_comp_apply, â† op_comp]
      match i' with
      | 0 =>
        rw [show mkOfSucc 0 â‰« tri = mkOfLe j kâ‚€.castSucc hjkâ‚€ from Hom.ext_one_left ..]
        have eq' := congr_fun (s.Ï€.naturality facemapâ‚‚) x
        unfold facemapâ‚‚ at eq'
        simp only [Int.reduceNeg, homOfLE_leOfHom, comp_obj, StructuredArrow.proj_obj, op_obj,
          const_obj_obj, len_mk, Fin.isValue, const_obj_map, types_comp_apply, types_id_apply,
          Functor.comp_map, StructuredArrow.proj_map, StructuredArrow.homMk_right, op_map,
          Quiver.Hom.unop_op] at eq'
        rw [show (Î´ 2 : [1] âŸ¶ [2]) = mkOfSucc 0 from Hom.ext_one_left ..] at eq'
        simp [Truncated.inclusion] at eq'
        rw [â† eq']
        exact ih kâ‚€.castSucc hjkâ‚€.hom (Nat.succ_inj.1 hi)
      | 1 =>
        rw [show mkOfSucc 1 â‰« tri = mkOfSucc kâ‚€ from Hom.ext_one_left ..]
        simp only [StrictSegal.spineToSimplex_arrow]
        have := congr_fun (s.Ï€.naturality facemapâ‚€) x
        dsimp [facemapâ‚€] at this
        rw [this, show (Î´ 0 : [1] âŸ¶ [2]) = mkOfSucc 1 from Hom.ext_one_left ..]
        rfl

namespace StrictSegal
variable (X : SSet.{u}) [StrictSegal X]

/-- A strict Segal simplicial set is 2-coskeletal. -/
noncomputable def IsPointwiseRightKanExtensionAt (n : â„•) :
    (rightExtensionInclusion X 2).IsPointwiseRightKanExtensionAt âŸ¨[n]âŸ© := by
  show IsLimit _
  unfold rightExtensionInclusion
  simp only [RightExtension.mk, RightExtension.coneAt, Truncated.inclusion,
    CostructuredArrow.mk_left, const_obj_obj, op_obj, fullSubcategoryInclusion.obj,
    comp_obj, StructuredArrow.proj_obj, whiskeringLeft_obj_obj, CostructuredArrow.mk_right,
    CostructuredArrow.mk_hom_eq_self, NatTrans.id_app, comp_id]
  exact {
    lift := fun s x => ran.lift s x
    fac := by
      intro s j
      ext x
      apply StrictSegal.spineInjective (X := X) (n := (unop j.right).1.len)
      unfold spineEquiv
      dsimp
      ext i
      Â· simp only [spine_vertex, id_eq, types_comp_apply, ran.lift]
        simp only [â† FunctorToTypes.map_comp_apply, â† op_comp]
        have ceq : (j.hom â‰« ([0].const [(unop j.right).obj.len] i).op) =
          (const [0] [n] (strArr.homEv j i)).op := rfl
        rw [ceq, StrictSegal.spineToSimplex_vertex]
        have eq := congr_fun (s.Ï€.naturality (fact.obj.arr j i)) x
        unfold pt fact.obj.arr strArr.homEv at eq
        dsimp at eq
        simp only [len_mk, mk_len]
        rw [â† eq]
        rfl
      Â· simp only [spine_arrow, id_eq, types_comp_apply]
        have nat := congr_fun (s.Ï€.naturality (fact.map.arr j i)) x
        dsimp [fact.map.arr] at nat
        rw [â† nat]
        simp only [â† FunctorToTypes.map_comp_apply]
        rw [â† Quiver.Hom.op_unop j.hom]
        simp only [â† op_comp]
        rw [show mkOfSucc i â‰« j.hom.unop = mkOfLe _ _ (strArr.homEv.map j i).le by
          simp [strArr.homEv, strArr.homEvSucc, mkOfSucc, mkOfLe]
          exact Hom.ext_one_left ..]
        exact ran.lift.map s x (strArr.homEv j i.castSucc) (strArr.homEvSucc j i)
          (strArr.homEv.map j i)
    uniq := by
      intro s lift' fact'
      ext x
      apply StrictSegal.spineInjective
      unfold spineEquiv
      dsimp
      rw [StrictSegal.spine_spineToSimplex]
      ext i
      Â· exact congr_fun (fact' (StructuredArrow.mk (Y := op [0]â‚‚) ([0].const [n] i).op)) x
      Â· exact congr_fun (fact' (ar i)) x
  }



/-- Since `rightExtensionInclusionâ‚‚IsPointwiseRightKanExtensionAt X n` proves that the appropriate
cones are limit cones, `rightExtensionInclusion X 2` is a pointwise right Kan extension.-/
noncomputable def isPointwiseRightKanExtension :
    RightExtension.IsPointwiseRightKanExtension (rightExtensionInclusion X 2) :=
  fun Î” => IsPointwiseRightKanExtensionAt X Î”.unop.len

/-- Since `rightExtensionInclusion X 2` is a pointwise right Kan extension,
`rightExtensionInclusion X 2` is universal as a costructured arrow.-/
noncomputable def isPointwiseRightKanExtension.isUniversal :
    CostructuredArrow.IsUniversal (rightExtensionInclusion X 2) :=
  RightExtension.IsPointwiseRightKanExtension.isUniversal (isPointwiseRightKanExtension X)

theorem isRightKanExtension :
    (rightExtensionInclusion X 2).left.IsRightKanExtension (rightExtensionInclusion X 2).hom :=
  RightExtension.IsPointwiseRightKanExtension.isRightKanExtension
    (isPointwiseRightKanExtension X)

/-- There is a map of costructured arrows from `rightExtensionInclusion X 2` to the right extension
of the 2-truncation of `X` defined by the counit of `coskAdj 2`.-/
noncomputable def cosk2RightExtension.hom : rightExtensionInclusion X 2 âŸ¶
    RightExtension.mk _
      ((Truncated.inclusion (n := 2)).op.ranCounit.app ((Truncated.inclusion (n := 2)).op â‹™ X)) :=
  CostructuredArrow.homMk ((coskAdj 2).unit.app X) ((coskAdj 2).left_triangle_components X)

/-- The map `cosk2RightExtension.hom X` is a natural transformation between two right Kan extensions
of the diagram `((Truncated.inclusion (n := 2)).op â‹™ X)` and thus is an isomorphism. -/
instance cosk2RightExtension.hom_isIso : IsIso (cosk2RightExtension.hom X) :=
  isIso_of_isTerminal (isPointwiseRightKanExtension.isUniversal X)
    (((Truncated.cosk 2).obj
      ((Truncated.inclusion (n := 2)).op â‹™ X)).isUniversalOfIsRightKanExtension
        ((Truncated.inclusion (n := 2)).op.ranCounit.app ((Truncated.inclusion (n := 2)).op â‹™ X)))
      (cosk2RightExtension.hom X)

/-- The map `cosk2RightExtension.hom X` is a natural transformation between two right Kan extensions
of the diagram `((Truncated.inclusion (n := 2)).op â‹™ X)` and thus is an isomorphism. -/
noncomputable def cosk2RightExtension.homIso : rightExtensionInclusion X 2 â‰…
   RightExtension.mk _
      ((Truncated.inclusion (n := 2)).op.ranCounit.app ((Truncated.inclusion (n := 2)).op â‹™ X)) :=
  asIso (cosk2RightExtension.hom X)

/-- The isomorphism `X â‰… (Truncated.cosk 2).obj X\` which shows that the nerve is
2-coskeletal.-/
noncomputable def cosk2Iso : X â‰… (Truncated.cosk 2).obj ((truncation 2).obj X) :=
  (CostructuredArrow.proj ((whiskeringLeft _ _ _).obj (Truncated.inclusion (n := 2)).op)
    ((Truncated.inclusion (n := 2)).op â‹™ X)).mapIso (cosk2RightExtension.homIso X)

end StrictSegal

end

end SSet

namespace CategoryTheory

namespace Nerve

open SSet

/-- The essential data of the nerve functor is contained in the 2-truncation, which is
recorded by the composite functor `nerveFunctorâ‚‚`.-/
def nerveFunctorâ‚‚ : Cat.{v, u} â¥¤ SSet.Truncated 2 := nerveFunctor â‹™ truncation 2

/-- The counit of `coskAdj 2` defines a natural transformation from the nerve to the right
Kan extension of the 2-truncated nerve.-/
noncomputable def cosk2NatTrans : nerveFunctor.{u, v} âŸ¶ nerveFunctorâ‚‚ â‹™ Truncated.cosk 2 :=
  whiskerLeft nerveFunctor (coskAdj 2).unit

/-- The natural isomorphism between `nerveFunctor` and `nerveFunctor â‹™ Truncated.cosk 2` whose
components `nerve C â‰…  (Truncated.cosk 2).obj (nerveâ‚‚ C)` shows that nerves of categories are
2-coskeletal.-/
noncomputable def cosk2Iso : nerveFunctor.{u, u} â‰… nerveFunctorâ‚‚.{u, u} â‹™ Truncated.cosk 2 := by
  refine NatIso.ofComponents ?_ ?_
  Â· intro C
    dsimp [nerveFunctor, nerveFunctorâ‚‚]
    exact (StrictSegal.cosk2Iso (nerve C))
  Â· simp only [nerveFunctor_obj, comp_obj, id_eq, Functor.comp_map]
    convert cosk2NatTrans.naturality

end Nerve

end CategoryTheory
