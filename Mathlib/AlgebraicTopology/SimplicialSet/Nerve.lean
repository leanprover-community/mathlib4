/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.Basic
import Mathlib.CategoryTheory.ComposableArrows

/-!

# The nerve of a category

This file provides the definition of the nerve of a category `C`,
which is a simplicial set `nerve C` (see [goerss-jardine-2009], Example I.1.4).
By definition, the type of `n`-simplices of `nerve C` is `ComposableArrows C n`,
which is the category `Fin (n + 1) ‚•§ C`.

## References
* [Paul G. Goerss, John F. Jardine, *Simplicial Homotopy Theory*][goerss-jardine-2009]

-/

open CategoryTheory.Category Simplicial

universe v u

namespace CategoryTheory

/-- The nerve of a category -/
@[simps]
def nerve (C : Type u) [Category.{v} C] : SSet.{max u v} where
  obj Œî := ComposableArrows C (Œî.unop.len)
  map f x := x.whiskerLeft (SimplexCategory.toCat.map f.unop)
  -- `aesop` can prove these but is slow, help it out:
  map_id _ := rfl
  map_comp _ _ := rfl

instance {C : Type*} [Category C] {Œî : SimplexCategory·µí·µñ} : Category ((nerve C).obj Œî) :=
  (inferInstance : Category (ComposableArrows C (Œî.unop.len)))

/-- Given a functor `C ‚•§ D`, we obtain a morphism `nerve C ‚ü∂ nerve D` of simplicial sets. -/
@[simps]
def nerveMap {C D : Type u} [Category.{v} C] [Category.{v} D] (F : C ‚•§ D) : nerve C ‚ü∂ nerve D :=
  { app := fun _ => (F.mapComposableArrows _).obj }

/-- The nerve of a category, as a functor `Cat ‚•§ SSet` -/
@[simps]
def nerveFunctor : Cat.{v, u} ‚•§ SSet where
  obj C := nerve C
  map F := nerveMap F

/-- The 0-simplices of the nerve of a category are equivalent to the objects of the category. -/
def nerveEquiv (C : Type u) [Category.{v} C] : nerve C _‚¶ã0‚¶å ‚âÉ C where
  toFun f := f.obj ‚ü®0, by omega‚ü©
  invFun f := (Functor.const _).obj f
  left_inv f := ComposableArrows.ext‚ÇÄ rfl

namespace nerve

/-- Nerves of finite non-empty ordinals are representable functors. -/
def representableBy {n : ‚Ñï} (Œ± : Type u) [Preorder Œ±] (e : Œ± ‚âÉo Fin (n + 1)) :
    (nerve Œ±).RepresentableBy ‚¶ãn‚¶å where
  homEquiv := SimplexCategory.homEquivFunctor.trans
    { toFun F := F ‚ãô e.symm.monotone.functor
      invFun F := F ‚ãô e.monotone.functor
      left_inv F := Functor.ext (fun x ‚Ü¶ by simp)
      right_inv F := Functor.ext (fun x ‚Ü¶ by simp) }
  homEquiv_comp _ _ := rfl

variable {C : Type*} [Category C] {n : ‚Ñï}

lemma Œ¥_obj {n : ‚Ñï} (i : Fin (n + 2)) (x : (nerve C) _‚¶ãn + 1‚¶å) (j : Fin (n + 1)) :
    ((nerve C).Œ¥ i x).obj j = x.obj (i.succAbove j) :=
  rfl

lemma œÉ_obj {n : ‚Ñï} (i : Fin (n + 1)) (x : (nerve C) _‚¶ãn‚¶å) (j : Fin (n + 2)) :
    ((nerve C).œÉ i x).obj j = x.obj (i.predAbove j) :=
  rfl

lemma Œ¥‚ÇÄ_eq {x : nerve C _‚¶ãn + 1‚¶å} : (nerve C).Œ¥ (0 : Fin (n + 2)) x = x.Œ¥‚ÇÄ := rfl

lemma œÉ‚ÇÄ_mk‚ÇÄ_eq (x : C) : (nerve C).œÉ (0 : Fin 1) (.mk‚ÇÄ x) = .mk‚ÇÅ (ùüô x) :=
  ComposableArrows.ext‚ÇÅ rfl rfl (by simp; rfl)

section

variable {X‚ÇÄ X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ)

theorem Œ¥‚ÇÇ_mk‚ÇÇ_eq : (nerve C).Œ¥ 2 (ComposableArrows.mk‚ÇÇ f g) = ComposableArrows.mk‚ÇÅ f :=
  ComposableArrows.ext‚ÇÅ rfl rfl (by simp; rfl)

theorem Œ¥‚ÇÄ_mk‚ÇÇ_eq : (nerve C).Œ¥ 0 (ComposableArrows.mk‚ÇÇ f g) = ComposableArrows.mk‚ÇÅ g :=
  ComposableArrows.ext‚ÇÅ rfl rfl (by simp; rfl)

theorem Œ¥‚ÇÅ_mk‚ÇÇ_eq : (nerve C).Œ¥ 1 (ComposableArrows.mk‚ÇÇ f g) = ComposableArrows.mk‚ÇÅ (f ‚â´ g) :=
  ComposableArrows.ext‚ÇÅ rfl rfl (by simp; rfl)

end

@[ext]
lemma ext_of_isThin [Quiver.IsThin C] {n : SimplexCategory·µí·µñ} {x y : (nerve C).obj n}
    (h : x.obj = y.obj) :
    x = y :=
  ComposableArrows.ext (by simp [h]) (by subsingleton)

end nerve

end CategoryTheory
