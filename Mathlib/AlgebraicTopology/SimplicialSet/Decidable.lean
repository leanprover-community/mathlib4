/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.SimplicialSet.StdSimplex
public import Mathlib.CategoryTheory.CodiscreteCategory

/-!
# Decidable instances

-/

universe v u

namespace CategoryTheory

def Arrow.decidableEq
    (C : Type u) [Category.{v} C] [DecidableEq C] [‚àÄ (X Y : C), DecidableEq (X ‚ü∂ Y)] :
    DecidableEq (Arrow C) :=
  fun _ _ ‚Ü¶ decidable_of_iff _ (Arrow.mk_eq_mk_iff _ _).symm

instance (C : Type u) {X Y : Codiscrete C} : Subsingleton (X ‚ü∂ Y) where
  allEq _ _ := rfl

instance (C : Type u) [DecidableEq C] : DecidableEq (Arrow (Codiscrete C)) :=
  Arrow.decidableEq _

instance {C : Type u} [Category.{v} C] [DecidableEq (Arrow C)] (n : ‚Ñï) :
    DecidableEq (ComposableArrows C n) := by
  intro D‚ÇÅ D‚ÇÇ
  induction n with
  | zero =>
    have : D‚ÇÅ = D‚ÇÇ ‚Üî (Arrow.mk (ùüô (D‚ÇÅ.obj 0)) = Arrow.mk ((ùüô (D‚ÇÇ.obj 0)))) :=
      ‚ü®by rintro rfl; rfl, fun h ‚Ü¶ ComposableArrows.ext‚ÇÄ (congr_arg Arrow.leftFunc.obj h)‚ü©
    exact decidable_of_iff _ this.symm
  | succ n hn =>
    have : D‚ÇÅ = D‚ÇÇ ‚Üî Arrow.mk (D‚ÇÅ.map' 0 1) = Arrow.mk (D‚ÇÇ.map' 0 1) ‚àß D‚ÇÅ.Œ¥‚ÇÄ = D‚ÇÇ.Œ¥‚ÇÄ :=
      ‚ü®by rintro rfl; tauto, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ by
        rw [Arrow.mk_eq_mk_iff] at h‚ÇÅ
        obtain ‚ü®h‚ÇÄ, _, h‚ü© := h‚ÇÅ
        exact ComposableArrows.ext_succ h‚ÇÄ h‚ÇÇ h‚ü©
    exact decidable_of_iff _ this.symm

end CategoryTheory

open CategoryTheory

namespace SSet

open Simplicial

instance {C : Type u} [Category.{v} C] [DecidableEq (Arrow C)] (n : SimplexCategory·µí·µñ) :
    DecidableEq ((nerve C).obj n) :=
      inferInstanceAs (DecidableEq (ComposableArrows C _))

example {C : Type u} [Category.{v} C] [DecidableEq (Arrow C)] (n : ‚Ñï) :
    DecidableEq (Œî[n] ‚ü∂ nerve C) :=
  inferInstance

example : DecidableEq (Œî[1] ‚ü∂ nerve (Codiscrete (Fin 2))) := inferInstance

abbrev coherentIso := nerve (Codiscrete (Fin 2))

noncomputable def coherentIso.hom : Œî[1] ‚ü∂ coherentIso :=
  yonedaEquiv.symm (ComposableArrows.mk‚ÇÅ (X‚ÇÄ := ‚ü®0‚ü©) (X‚ÇÅ := ‚ü®1‚ü©) ‚ü®‚ü©)

example : stdSimplex.Œ¥ 0 ‚â´ coherentIso.hom = SSet.const (ComposableArrows.mk‚ÇÄ ‚ü®1‚ü©) := by
  decide

end SSet
