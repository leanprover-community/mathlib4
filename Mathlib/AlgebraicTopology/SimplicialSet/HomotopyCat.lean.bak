/-
Copyright (c) 2024 Mario Carneiro and Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.SimplicialObject.Basic
public import Mathlib.AlgebraicTopology.SimplicialSet.Coskeletal
public import Mathlib.AlgebraicTopology.SimplicialSet.CompStruct
public import Mathlib.AlgebraicTopology.SimplexCategory.Truncated
public import Mathlib.CategoryTheory.Category.ReflQuiv
public import Mathlib.Combinatorics.Quiver.ReflQuiver
public import Mathlib.AlgebraicTopology.SimplicialSet.Monoidal
public import Mathlib.CategoryTheory.Category.Cat.Terminal

/-!

# The homotopy category of a simplicial set

The homotopy category of a simplicial set is defined as a quotient of the free category on its
underlying reflexive quiver (equivalently its one truncation). The quotient imposes an additional
hom relation on this free category, asserting that `f ‚â´ g = h` whenever `f`, `g`, and `h` are
respectively the 2nd, 0th, and 1st faces of a 2-simplex.

In fact, the associated functor

`SSet.hoFunctor : SSet.{u} ‚•§ Cat.{u, u} := SSet.truncation 2 ‚ãô SSet.hoFunctor‚ÇÇ`

is defined by first restricting from simplicial sets to 2-truncated simplicial sets (throwing away
the data that is not used for the construction of the homotopy category) and then composing with an
analogously defined `SSet.hoFunctor‚ÇÇ : SSet.Truncated.{u} 2 ‚•§ Cat.{u,u}` implemented relative to
the syntax of the 2-truncated simplex category.

In the file `Mathlib/AlgebraicTopology/SimplicialSet/NerveAdjunction.lean` we show the functor
`SSet.hoFunctor` to be left adjoint to the nerve by providing an analogous decomposition of the
nerve functor, made by possible by the fact that nerves of categories are 2-coskeletal, and then
composing a pair of adjunctions, which factor through the category of 2-truncated simplicial sets.
-/

@[expose] public section

namespace SSet
open CategoryTheory Category Limits Functor Opposite Simplicial Nerve
open SimplexCategory.Truncated SimplicialObject.Truncated

universe v u

/-- A 2-truncated simplicial set `S` has an underlying refl quiver with `S _‚¶ã0‚¶å‚ÇÇ` as its underlying
type. -/
def OneTruncation‚ÇÇ (S : SSet.Truncated 2) := S _‚¶ã0‚¶å‚ÇÇ

@[deprecated (since := "2025-11-01")] alias OneTruncation‚ÇÇ.Hom := Truncated.Edge

namespace OneTruncation‚ÇÇ

/-- A 2-truncated simplicial set `S` has an underlying refl quiver `SSet.OneTruncation‚ÇÇ S`. -/
@[simps -isSimp]
instance reflQuiver (S : SSet.Truncated 2) : ReflQuiver (OneTruncation‚ÇÇ S) where
  Hom := Truncated.Edge
  id := Truncated.Edge.id

@[ext]
lemma hom_ext
    {S : SSet.Truncated 2} {x y : OneTruncation‚ÇÇ S} {f g : x ‚ü∂ y}
    (h : f.edge = g.edge) : f = g :=
  Truncated.Edge.ext h

@[deprecated "Use reflQuiver_id" (since := "2025-11-01")]
lemma id_edge {S : SSet.Truncated 2} (x : OneTruncation‚ÇÇ S) :
    Truncated.Edge.edge (ùüôrq x) = S.map (œÉ‚ÇÇ 0).op x := by
  rfl

/-- The prefunctor on refl quivers `OneTruncation‚ÇÇ` induced by a morphism
of `2`-truncated simplicial sets. -/
@[simps]
def map {S T : SSet.Truncated 2} (f : S ‚ü∂ T) :
    OneTruncation‚ÇÇ S ‚•§rq OneTruncation‚ÇÇ T where
  obj x := f.app _ x
  map e := e.map f
  map_id x := by ext; simp [‚Üê FunctorToTypes.naturality, reflQuiver_id]

end OneTruncation‚ÇÇ

/-- The functor that carries a 2-truncated simplicial set to its underlying refl quiver. -/
@[simps]
def oneTruncation‚ÇÇ : SSet.Truncated.{u} 2 ‚•§ ReflQuiv.{u, u} where
  obj S := ReflQuiv.of (OneTruncation‚ÇÇ S)
  map f := OneTruncation‚ÇÇ.map f

namespace OneTruncation‚ÇÇ

@[simp]
lemma homOfEq_edge
    {X : SSet.Truncated.{u} 2} {x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : OneTruncation‚ÇÇ X}
    (f : x‚ÇÅ ‚ü∂ y‚ÇÅ) (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ = y‚ÇÇ) :
    (Quiver.homOfEq f hx hy).edge = f.edge := by
  subst hx hy
  rfl

section
variable {C : Type u} [Category.{v} C]

/-- An equivalence between the type of objects underlying a category and the type of 0-simplices in
the 2-truncated nerve. -/
@[simps! -isSimp]
def nerveEquiv : OneTruncation‚ÇÇ ((SSet.truncation 2).obj (nerve C)) ‚âÉ C :=
  CategoryTheory.nerveEquiv

/-- A hom equivalence over the function `OneTruncation‚ÇÇ.nerveEquiv`. -/
def nerveHomEquiv {X Y : OneTruncation‚ÇÇ ((SSet.truncation 2).obj (nerve C))} :
    (X ‚ü∂ Y) ‚âÉ (nerveEquiv X ‚ü∂ nerveEquiv Y) :=
  nerve.homEquiv

lemma nerveHomEquiv_apply {X Y : OneTruncation‚ÇÇ ((SSet.truncation 2).obj (nerve C))}
    (f : X ‚ü∂ Y) :
    nerveHomEquiv f = eqToHom (congr_arg ComposableArrows.left f.src_eq.symm) ‚â´
      f.edge.hom ‚â´ eqToHom (congr_arg ComposableArrows.left f.tgt_eq) :=
  rfl

@[simp]
lemma nerveHomEquiv_id (X : OneTruncation‚ÇÇ ((SSet.truncation 2).obj (nerve C))) :
    nerveHomEquiv (ùüôrq X) = ùüô _ :=
  nerve.homEquiv_id _

/-- The refl quiver underlying a nerve is isomorphic to the refl quiver underlying the category. -/
def ofNerve‚ÇÇ (C : Type u) [Category.{u} C] :
    ReflQuiv.of (OneTruncation‚ÇÇ ((truncation 2).obj (nerve C))) ‚âÖ ReflQuiv.of C :=
  ReflQuiv.isoOfEquiv.{u, u} OneTruncation‚ÇÇ.nerveEquiv
    (fun _ _ ‚Ü¶ OneTruncation‚ÇÇ.nerveHomEquiv) nerveHomEquiv_id

lemma nerve_hom_ext {X : (SSet.Truncated 2)} {C : Type u} [Category.{u} C]
    {F G : X ‚ü∂ ((truncation 2).obj (nerve C))}
    (h : OneTruncation‚ÇÇ.map F = OneTruncation‚ÇÇ.map G) : F = G :=
  SSet.Truncated.IsStrictSegal.hom_ext (fun f ‚Ü¶ by
    obtain ‚ü®x‚ÇÄ, x‚ÇÅ, f, rfl‚ü© := Truncated.Edge.exists_of_simplex f
    simpa using congr_arg Truncated.Edge.edge (ReflPrefunctor.congr_hom h f))

@[deprecated (since := "2025-11-06")] alias _root_.CategoryTheory.toNerve‚ÇÇ.ext := nerve_hom_ext

end
end OneTruncation‚ÇÇ

/-- The refl quiver underlying a nerve is naturally isomorphic to the refl quiver underlying the
category. -/
@[simps! hom_app_obj hom_app_map inv_app_obj_obj inv_app_obj_map inv_app_map]
def OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso :
    nerveFunctor‚ÇÇ.{u, u} ‚ãô SSet.oneTruncation‚ÇÇ ‚âÖ ReflQuiv.forget :=
  NatIso.ofComponents (fun C => OneTruncation‚ÇÇ.ofNerve‚ÇÇ C)
    (fun F ‚Ü¶ ReflPrefunctor.ext (by cat_disch) (fun x y f ‚Ü¶ by
      obtain ‚ü®f, rfl, rfl‚ü© := f
      dsimp [ofNerve‚ÇÇ, ReflQuiv.isoOfEquiv, ReflQuiv.isoOfQuivIso,
        Quiv.isoOfEquiv, nerveHomEquiv_apply]
      simp only [comp_id, id_comp]
      rfl))

private lemma map_map_of_eq.{w} {C : Type u} [Category.{v} C] (V : C·µí·µñ ‚•§ Type w) {X Y Z : C}
    {Œ± : X ‚ü∂ Y} {Œ≤ : Y ‚ü∂ Z} {Œ≥ : X ‚ü∂ Z} {œÜ} :
    Œ± ‚â´ Œ≤ = Œ≥ ‚Üí V.map Œ±.op (V.map Œ≤.op œÜ) = V.map Œ≥.op œÜ := by
  rintro rfl
  simp

namespace Truncated

/-- The map that picks up the initial vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Œπ0‚ÇÇ : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 0) 1 ‚â´ Œ¥‚ÇÇ (n := 1) 1

/-- The map that picks up the middle vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Œπ1‚ÇÇ : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 0) 0 ‚â´ Œ¥‚ÇÇ (n := 1) 2

/-- The map that picks up the final vertex of a 2-simplex, as a morphism in the 2-truncated
simplex category. -/
def Œπ2‚ÇÇ : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 0) 0 ‚â´ Œ¥‚ÇÇ (n := 1) 1

/-- The initial vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev0‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : OneTruncation‚ÇÇ V := V.map Œπ0‚ÇÇ.op œÜ

/-- The middle vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev1‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : OneTruncation‚ÇÇ V := V.map Œπ1‚ÇÇ.op œÜ

/-- The final vertex of a 2-simplex in a 2-truncated simplicial set. -/
def ev2‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : OneTruncation‚ÇÇ V := V.map Œπ2‚ÇÇ.op œÜ

/-- The 0th face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Œ¥0‚ÇÇ : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 1) 0

/-- The 1st face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Œ¥1‚ÇÇ : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 1) 1

/-- The 2nd face of a 2-simplex, as a morphism in the 2-truncated simplex category. -/
def Œ¥2‚ÇÇ : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := Œ¥‚ÇÇ (n := 1) 2

/-- The arrow in the ReflQuiver `OneTruncation‚ÇÇ V` of a 2-truncated simplicial set arising from the
0th face of a 2-simplex. -/
def ev12‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : ev1‚ÇÇ œÜ ‚ü∂ ev2‚ÇÇ œÜ :=
  ‚ü®V.map Œ¥0‚ÇÇ.op œÜ,
    map_map_of_eq V (SimplexCategory.Œ¥_comp_Œ¥ (i := 0) (j := 1) (by decide)).symm,
    map_map_of_eq V rfl‚ü©

/-- The arrow in the ReflQuiver `OneTruncation‚ÇÇ V` of a 2-truncated simplicial set arising from the
1st face of a 2-simplex. -/
def ev02‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : ev0‚ÇÇ œÜ ‚ü∂ ev2‚ÇÇ œÜ :=
  ‚ü®V.map Œ¥1‚ÇÇ.op œÜ, map_map_of_eq V rfl, map_map_of_eq V rfl‚ü©

/-- The arrow in the ReflQuiver `OneTruncation‚ÇÇ V` of a 2-truncated simplicial set arising from the
2nd face of a 2-simplex. -/
def ev01‚ÇÇ {V : SSet.Truncated 2} (œÜ : V _‚¶ã2‚¶å‚ÇÇ) : ev0‚ÇÇ œÜ ‚ü∂ ev1‚ÇÇ œÜ :=
  ‚ü®V.map Œ¥2‚ÇÇ.op œÜ, map_map_of_eq V (SimplexCategory.Œ¥_comp_Œ¥ (j := 1) le_rfl), map_map_of_eq V rfl‚ü©

end Truncated

namespace OneTruncation‚ÇÇ

variable (V : SSet.Truncated.{u} 2)

/-- The 2-simplices in a 2-truncated simplicial set `V` generate a hom relation on the free
category on the underlying refl quiver of `V`. -/
inductive HoRel‚ÇÇ : HomRel (Cat.FreeRefl (OneTruncation‚ÇÇ V)) where
  | of_compStruct {x‚ÇÄ x‚ÇÅ x‚ÇÇ : V _‚¶ã0‚¶å‚ÇÇ} {e‚ÇÄ‚ÇÅ : Truncated.Edge x‚ÇÄ x‚ÇÅ}
    {e‚ÇÅ‚ÇÇ : Truncated.Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÄ‚ÇÇ : Truncated.Edge x‚ÇÄ x‚ÇÇ}
    (h : Truncated.Edge.CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ) :
    HoRel‚ÇÇ
      ((Cat.FreeRefl.quotientFunctor (OneTruncation‚ÇÇ V)).map
        (Quiver.Hom.toPath e‚ÇÄ‚ÇÅ ‚â´ Quiver.Hom.toPath e‚ÇÅ‚ÇÇ))
      ((Cat.FreeRefl.quotientFunctor (OneTruncation‚ÇÇ V)).map (Quiver.Hom.toPath e‚ÇÄ‚ÇÇ))

@[deprecated "HoRel‚ÇÇ.of_compStruct" (since := "2025-11-06")]
lemma HoRel‚ÇÇ.mk {x‚ÇÄ x‚ÇÅ x‚ÇÇ : V _‚¶ã0‚¶å‚ÇÇ} {e‚ÇÄ‚ÇÅ : Truncated.Edge x‚ÇÄ x‚ÇÅ}
    {e‚ÇÅ‚ÇÇ : Truncated.Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÄ‚ÇÇ : Truncated.Edge x‚ÇÄ x‚ÇÇ}
    (h : Truncated.Edge.CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ) :
    HoRel‚ÇÇ V
      ((Cat.FreeRefl.quotientFunctor (OneTruncation‚ÇÇ V)).map
        (Quiver.Hom.toPath e‚ÇÄ‚ÇÅ ‚â´ Quiver.Hom.toPath e‚ÇÅ‚ÇÇ))
      ((Cat.FreeRefl.quotientFunctor (OneTruncation‚ÇÇ V)).map (Quiver.Hom.toPath e‚ÇÄ‚ÇÇ)) :=
  HoRel‚ÇÇ.of_compStruct h

end OneTruncation‚ÇÇ

namespace Truncated

variable (V W : SSet.Truncated.{u} 2)

/-- The type underlying the homotopy category of a 2-truncated simplicial set `V`. -/
def HomotopyCategory : Type u :=
  Quotient (OneTruncation‚ÇÇ.HoRel‚ÇÇ V)
  deriving Category.{u}

namespace HomotopyCategory

/-- A canonical functor from the free category on the refl quiver underlying a 2-truncated
simplicial set `V` to its homotopy category. -/
def quotientFunctor :
    Cat.FreeRefl (OneTruncation‚ÇÇ V) ‚•§ V.HomotopyCategory :=
  Quotient.functor _

instance : (quotientFunctor V).Full :=
  Quotient.full_functor _

variable {V}

/-- Constructor for objects of the homotopy category of a `2`-truncated simplicial set. -/
def mk (x : V _‚¶ã0‚¶å‚ÇÇ) : V.HomotopyCategory :=
  (quotientFunctor V).obj (.mk x)

lemma mk_surjective : Function.Surjective (mk (V := V)) := by
  rintro ‚ü®‚ü®x‚ü©‚ü©
  exact ‚ü®x, rfl‚ü©

@[elab_as_elim, cases_eliminator]
protected lemma cases_on {motive : V.HomotopyCategory ‚Üí Prop}
    (h : ‚àÄ (x : V _‚¶ã0‚¶å‚ÇÇ), motive (.mk x))
    (x : V.HomotopyCategory) :
    motive x := by
  obtain ‚ü®x', rfl‚ü© := mk_surjective x
  exact h x'

/-- The morphism in the homotopy category of a `2`-truncated simplicial set that
is induced by an edge. -/
def homMk {x‚ÇÄ x‚ÇÅ : V _‚¶ã0‚¶å‚ÇÇ} (e : Edge x‚ÇÄ x‚ÇÅ) : mk x‚ÇÄ ‚ü∂ mk x‚ÇÅ :=
  (quotientFunctor V).map (Cat.FreeRefl.homMk e)

lemma congr_arrowMk_homMk {x‚ÇÄ x‚ÇÅ : V _‚¶ã0‚¶å‚ÇÇ} (e : Edge x‚ÇÄ x‚ÇÅ)
    {y‚ÇÄ y‚ÇÅ : V _‚¶ã0‚¶å‚ÇÇ} (e' : Edge y‚ÇÄ y‚ÇÅ) (h : e.edge = e'.edge) :
    Arrow.mk (homMk e) = Arrow.mk (homMk e') := by
  obtain rfl : x‚ÇÄ = y‚ÇÄ := by rw [‚Üê e.src_eq, ‚Üê e'.src_eq, h]
  obtain rfl : x‚ÇÅ = y‚ÇÅ := by rw [‚Üê e.tgt_eq, ‚Üê e'.tgt_eq, h]
  obtain rfl : e = e' := by aesop
  rfl

@[simp]
lemma homMk_id (x : V _‚¶ã0‚¶å‚ÇÇ) :
    homMk (.id x) = ùüô (mk x) := by
  rw [homMk, ‚Üê OneTruncation‚ÇÇ.reflQuiver_id, Cat.FreeRefl.homMk_id,
    CategoryTheory.Functor.map_id]
  rfl

@[reassoc]
lemma homMk_comp_homMk {x‚ÇÄ x‚ÇÅ x‚ÇÇ : V _‚¶ã0‚¶å‚ÇÇ} {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ} {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ}
    {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ} (h : Edge.CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ) :
    homMk e‚ÇÄ‚ÇÅ ‚â´ homMk e‚ÇÅ‚ÇÇ = homMk e‚ÇÄ‚ÇÇ := by
  simpa [homMk] using  CategoryTheory.Quotient.sound _
    (OneTruncation‚ÇÇ.HoRel‚ÇÇ.of_compStruct h)

variable (V) in
/-- If `V` is a `2`-truncated simplicial sets, this is the family of
morphisms in `V.HomotopyCategory` corresponding to the edges of `V`.
(Any morphism in `V.HomotopyCategory` is in the multiplicative closure
of this family of morphisms, see `multiplicativeClosure_morphismPropertyHomMk`.) -/
def morphismPropertyHomMk : MorphismProperty V.HomotopyCategory :=
  .ofHoms (fun (e : Œ£ (x y : V _‚¶ã0‚¶å‚ÇÇ), Edge x y) ‚Ü¶ homMk e.2.2)

lemma morphismPropertyHomMk_of_edge {x y : V _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y) :
    morphismPropertyHomMk V (homMk e) := by
  dsimp only [morphismPropertyHomMk]
  rw [MorphismProperty.ofHoms_iff]
  exact ‚ü®‚ü®x, y, e‚ü©, rfl‚ü©

lemma morphismPropertyHomMk_eq_strictMap :
    morphismPropertyHomMk V =
      (Cat.FreeRefl.morphismPropertyHomMk (OneTruncation‚ÇÇ V)).strictMap (quotientFunctor V) := by
  ext _ _ f
  constructor
  ¬∑ rintro ‚ü®_‚ü©
    exact MorphismProperty.map_mem_strictMap _ _ _ ‚ü®_‚ü©
  ¬∑ rintro ‚ü®‚ü®_, _, e‚ü©‚ü©
    exact morphismPropertyHomMk_of_edge e

open MorphismProperty in
lemma multiplicativeClosure_morphismPropertyHomMk :
    (morphismPropertyHomMk V).multiplicativeClosure = ‚ä§ :=
  le_antisymm (by simp) (fun x y f _ ‚Ü¶ by
    obtain ‚ü®f, rfl‚ü© := (quotientFunctor _).map_surjective f
    rw [morphismPropertyHomMk_eq_strictMap]
    refine strictMap_multiplicativeClosure_le _ _ _ ?_
    rw [Cat.FreeRefl.multiplicativeClosure_morphismPropertyHomMk]
    exact map_mem_strictMap _ _ _ (by simp))

lemma morphismProperty_eq_top {W : MorphismProperty V.HomotopyCategory}
    [W.IsMultiplicative]
    (hW : ‚àÄ {x y : V _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y), W (homMk e)) :
    W = ‚ä§ :=
  le_antisymm (by simp) (by
    rw [‚Üê multiplicativeClosure_morphismPropertyHomMk,
      MorphismProperty.multiplicativeClosure_le_iff]
    rintro _ _ _ ‚ü®_, _, e‚ü©
    exact hW e)

section

variable {D : Type*} [Category* D]

section

variable (obj : V _‚¶ã0‚¶å‚ÇÇ ‚Üí D) (map : ‚àÄ {x y : V _‚¶ã0‚¶å‚ÇÇ}, Edge x y ‚Üí (obj x ‚ü∂ obj y))
  (map_id : ‚àÄ (x : V _‚¶ã0‚¶å‚ÇÇ), map (.id x) = ùüô _)
  (map_comp : ‚àÄ {x‚ÇÄ x‚ÇÅ x‚ÇÇ : V _‚¶ã0‚¶å‚ÇÇ}
    {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ} {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ}
    (_ : Edge.CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ), map e‚ÇÄ‚ÇÅ ‚â´ map e‚ÇÅ‚ÇÇ = map e‚ÇÄ‚ÇÇ)

/-- Constructor for functors from the homotopy category. -/
def lift : V.HomotopyCategory ‚•§ D :=
  CategoryTheory.Quotient.lift _
    (Cat.FreeRefl.lift' obj (fun f ‚Ü¶ map f) map_id) (by
      rintro _ _ _ _ ‚ü®h‚ü©
      simp only [Functor.map_comp]
      convert map_comp h <;> apply Cat.FreeRefl.lift'_map)

@[simp]
lemma lift_obj_mk (x : V _‚¶ã0‚¶å‚ÇÇ) : (lift obj map map_id map_comp).obj (mk x) = obj x := rfl

@[simp]
lemma lift_map_homMk {x y : V _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y) :
    (lift obj map map_id map_comp).map (homMk e) = map e :=
  Category.id_comp _

end

variable {F G : V.HomotopyCategory ‚•§ D}

section

variable (œÜ : ‚àÄ (x : V _‚¶ã0‚¶å‚ÇÇ), F.obj (mk x) ‚ü∂ G.obj (mk x))
  (hœÜ : ‚àÄ ‚¶Éx y : V _‚¶ã0‚¶å‚ÇÇ‚¶Ñ (e : Edge x y),
    F.map (homMk e) ‚â´ œÜ y = œÜ x ‚â´ G.map (homMk e) := by cat_disch)

/-- Constructor for natural transformations between functors from `V.HomotopyCategory`. -/
def mkNatTrans : F ‚ü∂ G where
  app _ := œÜ _
  naturality _ _ f := by
    have : MorphismProperty.naturalityProperty (fun (x : V.HomotopyCategory) ‚Ü¶ œÜ _) = ‚ä§ :=
      morphismProperty_eq_top (fun e ‚Ü¶ hœÜ e)
    exact this.symm.le f (by simp)

@[simp]
lemma mkNatTrans_app_mk (v : V _‚¶ã0‚¶å‚ÇÇ) :
    (mkNatTrans œÜ hœÜ).app (mk v) = œÜ v := rfl

end

section

variable (iso : ‚àÄ (x : V _‚¶ã0‚¶å‚ÇÇ), F.obj (mk x) ‚âÖ G.obj (mk x))
  (hiso : ‚àÄ ‚¶Éx y : V _‚¶ã0‚¶å‚ÇÇ‚¶Ñ (e : Edge x y), F.map (homMk e) ‚â´ (iso y).hom =
    (iso x).hom ‚â´ G.map (homMk e) := by cat_disch)

/-- Constructor for natural isomorphisms between functors from `V.HomotopyCategory`. -/
def mkNatIso : F ‚âÖ G :=
  NatIso.ofComponents (fun _ ‚Ü¶ iso _) (fun f ‚Ü¶ (mkNatTrans _ hiso).naturality f)

@[simp]
lemma mkNatIso_hom_app_mk (v : V _‚¶ã0‚¶å‚ÇÇ) :
    (mkNatIso iso hiso).hom.app (mk v) = (iso v).hom := rfl

@[simp]
lemma mkNatIso_inv_app_mk (v : V _‚¶ã0‚¶å‚ÇÇ) :
    (mkNatIso iso hiso).inv.app (mk v) = (iso v).inv := rfl

end

lemma functor_ext {F G : V.HomotopyCategory ‚•§ D}
    (h‚ÇÅ : ‚àÄ (x : V _‚¶ã0‚¶å‚ÇÇ), F.obj (mk x) = G.obj (mk x))
    (h‚ÇÇ : ‚àÄ ‚¶Éx y : V _‚¶ã0‚¶å‚ÇÇ‚¶Ñ (e : Edge x y),
      F.map (homMk e) = eqToHom (h‚ÇÅ x) ‚â´ G.map (homMk e) ‚â´ eqToHom (h‚ÇÅ y).symm) :
    F = G :=
  Functor.ext_of_iso (mkNatIso (fun x ‚Ü¶ eqToIso (h‚ÇÅ x))
    (fun _ _ e ‚Ü¶ by simp [h‚ÇÇ e])) (fun _ ‚Ü¶ h‚ÇÅ _)

end

end HomotopyCategory

section

open HomotopyCategory

variable {V W} (f : V ‚ü∂ W)

/-- A map of 2-truncated simplicial sets induces a functor between homotopy categories. -/
def mapHomotopyCategory :
    V.HomotopyCategory ‚•§ W.HomotopyCategory :=
  CategoryTheory.Quotient.lift _
    ((oneTruncation‚ÇÇ ‚ãô Cat.freeRefl).map f ‚ãô quotientFunctor W) (by
      rintro _ _ _ _ ‚ü®h‚ü©
      exact CategoryTheory.Quotient.sound _ ‚ü®h.map f‚ü©)

@[simp]
lemma mapHomotopyCategory_obj (x : V _‚¶ã0‚¶å‚ÇÇ) :
    (mapHomotopyCategory f).obj (.mk x) = .mk (f.app _ x) := rfl

@[simp]
lemma mapHomotopyCategory_homMk {x y : V _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y) :
    (mapHomotopyCategory f).map (homMk e) = homMk (e.map f) := rfl

end

/-- The functor that takes a 2-truncated simplicial set to its homotopy category. -/
def hoFunctor‚ÇÇ : SSet.Truncated.{u} 2 ‚•§ Cat.{u, u} where
  obj V := Cat.of V.HomotopyCategory
  map F := mapHomotopyCategory F
  map_id _ := HomotopyCategory.functor_ext (by simp) (by cat_disch)
  map_comp _ _ := HomotopyCategory.functor_ext (by simp) (by cat_disch)

theorem hoFunctor‚ÇÇ_naturality {X Y : SSet.Truncated.{u} 2} (f : X ‚ü∂ Y) :
    (oneTruncation‚ÇÇ ‚ãô Cat.freeRefl).map f ‚ãô SSet.Truncated.HomotopyCategory.quotientFunctor Y =
      SSet.Truncated.HomotopyCategory.quotientFunctor X ‚ãô mapHomotopyCategory f := rfl

/-- By `Quotient.lift_unique'` (not `Quotient.lift`) we have that `quotientFunctor V` is an
epimorphism. -/
theorem HomotopyCategory.lift_unique' (V : SSet.Truncated.{u} 2) {D : Type*} [Category* D]
    (F‚ÇÅ F‚ÇÇ : V.HomotopyCategory ‚•§ D)
    (h : HomotopyCategory.quotientFunctor V ‚ãô F‚ÇÅ = HomotopyCategory.quotientFunctor V ‚ãô F‚ÇÇ) :
    F‚ÇÅ = F‚ÇÇ :=
  Quotient.lift_unique' _ _ _ h

end Truncated

/-- The functor that takes a simplicial set to its homotopy category by passing through the
2-truncation. -/
def hoFunctor : SSet.{u} ‚•§ Cat.{u, u} := SSet.truncation 2 ‚ãô Truncated.hoFunctor‚ÇÇ

/-- For a simplicial set `X`, the underlying type of `hoFunctor.obj X` is equivalent to `X _‚¶ã0‚¶å`. -/
def hoFunctor.obj.equiv (X : SSet) : hoFunctor.obj X ‚âÉ X _‚¶ã0‚¶å :=
  (Quotient.equiv.{u, u} _).trans (Quotient.equiv _)

/-- Since `‚¶ã0‚¶å : SimplexCategory` is terminal, `Œî[0]` has a unique point and thus
`OneTruncation‚ÇÇ ((truncation 2).obj Œî[0])` has a unique inhabitant. -/
instance instUniqueOneTruncation‚ÇÇDeltaZero : Unique (OneTruncation‚ÇÇ ((truncation 2).obj Œî[0])) :=
  inferInstanceAs (Unique (ULift.{_, 0} (‚¶ã0‚¶å ‚ü∂ ‚¶ã0‚¶å)))

/-- Since `‚¶ã0‚¶å : SimplexCategory` is terminal, `Œî[0]` has a unique edge and thus the homs of
`OneTruncation‚ÇÇ ((truncation 2).obj Œî[0])` have unique inhabitants. -/
instance (x y : OneTruncation‚ÇÇ ((truncation 2).obj Œî[0])) : Unique (x ‚ü∂ y) where
  default := by
    obtain rfl : x = default := Unique.uniq _ _
    obtain rfl : y = default := Unique.uniq _ _
    exact ùüôrq instUniqueOneTruncation‚ÇÇDeltaZero.default
  uniq _ := by
    letI : Subsingleton (((truncation 2).obj Œî[0]).obj (.op ‚¶ã1‚¶å‚ÇÇ)) :=
      inferInstanceAs (Subsingleton (ULift.{_, 0} (‚¶ã1‚¶å ‚ü∂ ‚¶ã0‚¶å)))
    ext
    exact this.allEq _ _

instance : Unique ((truncation.{u} 2).obj Œî[0]).HomotopyCategory :=
  inferInstanceAs (Unique <| CategoryTheory.Quotient _)

instance : IsDiscrete ((truncation.{u} 2).obj Œî[0]).HomotopyCategory where
  subsingleton x y :=
    inferInstanceAs (Subsingleton ((_ : CategoryTheory.Quotient _) ‚ü∂ _))
  eq_of_hom _ := by subsingleton

/-- The category `hoFunctor.obj (Œî[0])` is terminal. -/
def isTerminalHoFunctorDeltaZero : IsTerminal (hoFunctor.{u}.obj (Œî[0])) :=
  Cat.isTerminalOfUniqueOfIsDiscrete

/-- The homotopy category functor preserves generic terminal objects. -/
noncomputable def hoFunctor.terminalIso : hoFunctor.obj (‚ä§_ SSet) ‚âÖ ‚ä§_ Cat :=
  hoFunctor.mapIso (terminalIsoIsTerminal stdSimplex.isTerminalObj‚ÇÄ) ‚â™‚â´
    (terminalIsoIsTerminal isTerminalHoFunctorDeltaZero).symm

instance hoFunctor.preservesTerminal : PreservesLimit (empty.{0} SSet) hoFunctor :=
  preservesTerminal_of_iso hoFunctor hoFunctor.terminalIso

instance hoFunctor.preservesTerminal' :
    PreservesLimitsOfShape (Discrete PEmpty.{1}) hoFunctor :=
  preservesLimitsOfShape_pempty_of_preservesTerminal _

end SSet
