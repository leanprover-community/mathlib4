/-
Copyright (c) 2024 Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.Horn

/-!
# Paths in simplicial sets

A path in a simplicial set `X` of length `n` is a directed path comprised of `n + 1` 0-simplices
and `n` 1-simplices, together with identifications between 0-simplices and the sources and targets
of the 1-simplices.

An `n`-simplex has a maximal path, the `spine` of the simplex, which is a path of length `n`.
-/

universe v u

namespace SSet

open CategoryTheory

open Simplicial SimplexCategory

variable (X : SSet.{u})

/-- A path in a simplicial set `X` of length `n` is a directed path of `n` edges. -/
@[ext]
structure Path (n : ‚Ñï) where
  /-- A path includes the data of `n+1` 0-simplices in `X`. -/
  vertex (i : Fin (n + 1)) : X _‚¶ã0‚¶å
  /-- A path includes the data of `n` 1-simplices in `X`. -/
  arrow (i : Fin n) : X _‚¶ã1‚¶å
  /-- The sources of the 1-simplices in a path are identified with appropriate 0-simplices. -/
  arrow_src (i : Fin n) : X.Œ¥ 1 (arrow i) = vertex i.castSucc
  /-- The targets of the 1-simplices in a path are identified with appropriate 0-simplices. -/
  arrow_tgt (i : Fin n) : X.Œ¥ 0 (arrow i) = vertex i.succ


variable {X} in
/-- For `j + l ‚â§ n`, a path of length `n` restricts to a path of length `l`, namely the subpath
spanned by the vertices `j ‚â§ i ‚â§ j + l` and edges `j ‚â§ i < j + l`. -/
def Path.interval {n : ‚Ñï} (f : Path X n) (j l : ‚Ñï) (hjl : j + l ‚â§ n) :
    Path X l where
  vertex i := f.vertex ‚ü®j + i, by omega‚ü©
  arrow i := f.arrow ‚ü®j + i, by omega‚ü©
  arrow_src i := f.arrow_src ‚ü®j + i, by omega‚ü©
  arrow_tgt i := f.arrow_tgt ‚ü®j + i, by omega‚ü©

/-- The spine of an `n`-simplex in `X` is the path of edges of length `n` formed by
traversing through its vertices in order. -/
@[simps]
def spine (n : ‚Ñï) (Œî : X _‚¶ãn‚¶å) : X.Path n where
  vertex i := X.map (SimplexCategory.const ‚¶ã0‚¶å ‚¶ãn‚¶å i).op Œî
  arrow i := X.map (SimplexCategory.mkOfSucc i).op Œî
  arrow_src i := by
    dsimp [SimplicialObject.Œ¥]
    simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    rw [SimplexCategory.Œ¥_one_mkOfSucc]
    simp only [len_mk, Fin.coe_castSucc, Fin.coe_eq_castSucc]
  arrow_tgt i := by
    dsimp [SimplicialObject.Œ¥]
    simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    rw [SimplexCategory.Œ¥_zero_mkOfSucc]

lemma spine_map_vertex {n : ‚Ñï} (x : X _‚¶ãn‚¶å) {m : ‚Ñï} (œÜ : ‚¶ãm‚¶å ‚ü∂ ‚¶ãn‚¶å)
    (i : Fin (m + 1)) :
    (spine X m (X.map œÜ.op x)).vertex i = (spine X n x).vertex (œÜ.toOrderHom i) := by
  dsimp [spine]
  rw [‚Üê FunctorToTypes.map_comp_apply]
  rfl

lemma spine_map_subinterval {n : ‚Ñï} (j l : ‚Ñï) (hjl : j + l ‚â§ n) (Œî : X _‚¶ãn‚¶å) :
    X.spine l (X.map (subinterval j l (by omega)).op Œî) =
      (X.spine n Œî).interval j l (by omega) := by
  ext i
  ¬∑ simp only [spine_vertex, Path.interval, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp,
      const_subinterval_eq]
  ¬∑ simp only [spine_arrow, Path.interval, ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp,
      mkOfSucc_subinterval_eq]

/-- Two paths of the same nonzero length are equal if all of their arrows are equal. -/
@[ext]
lemma Path.ext' {n : ‚Ñï} {f g : Path X (n + 1)}
    (h : ‚àÄ i : Fin (n + 1), f.arrow i = g.arrow i) :
    f = g := by
  ext j
  ¬∑ rcases Fin.eq_castSucc_or_eq_last j with ‚ü®k, hk‚ü© | hl
    ¬∑ rw [hk, ‚Üê f.arrow_src k, ‚Üê g.arrow_src k, h]
    ¬∑ simp only [hl, ‚Üê Fin.succ_last]
      rw [‚Üê f.arrow_tgt (Fin.last n), ‚Üê g.arrow_tgt (Fin.last n), h]
  ¬∑ exact h j

/-- Maps of simplicial sets induce maps of paths in a simplicial set. -/
@[simps]
def Path.map {X Y : SSet.{u}} {n : ‚Ñï} (f : X.Path n) (œÉ : X ‚ü∂ Y) : Y.Path n where
  vertex i := œÉ.app (Opposite.op ‚¶ã0‚¶å) (f.vertex i)
  arrow i := œÉ.app (Opposite.op ‚¶ã1‚¶å) (f.arrow i)
  arrow_src i := by
    simp only [‚Üê f.arrow_src i]
    exact congr (œÉ.naturality (Œ¥ 1).op) rfl |>.symm
  arrow_tgt i := by
    simp only [‚Üê f.arrow_tgt i]
    exact congr (œÉ.naturality (Œ¥ 0).op) rfl |>.symm

/-- `Path.map` respects subintervals of paths. -/
lemma map_interval {X Y : SSet.{u}} {n : ‚Ñï} (f : X.Path n) (œÉ : X ‚ü∂ Y)
    (j l : ‚Ñï) (hjl : j + l ‚â§ n) :
    (f.map œÉ).interval j l hjl = (f.interval j l hjl).map œÉ := rfl

/-- The spine of the unique non-degenerate `n`-simplex in `Œî[n]`. -/
def stdSimplex.spineId (n : ‚Ñï) : Path Œî[n] n :=
  spine Œî[n] n (objEquiv.symm (ùüô _))

@[simp]
lemma stdSimplex.spineId_vertex (n : ‚Ñï) (i : Fin (n + 1)) :
    (stdSimplex.spineId n).vertex i = obj‚ÇÄEquiv.symm i := rfl

@[simp]
lemma stdSimplex.spineId_arrow_apply_zero (n : ‚Ñï) (i : Fin n) :
    (stdSimplex.spineId n).arrow i 0 = i.castSucc := rfl

@[simp]
lemma stdSimplex.spineId_arrow_apply_one (n : ‚Ñï) (i : Fin n) :
    (stdSimplex.spineId n).arrow i 1 = i.succ := rfl

/-- A path of a simplicial set can be lifted to a subcomplex if the vertices
and arrows belong to this subcomplex. -/
@[simps]
def Subcomplex.liftPath {X : SSet.{u}} (A : X.Subcomplex) {n : ‚Ñï} (p : Path X n)
    (hp‚ÇÄ : ‚àÄ j, p.vertex j ‚àà A.obj _)
    (hp‚ÇÅ : ‚àÄ j, p.arrow j ‚àà A.obj _) :
    Path A n where
  vertex j := ‚ü®p.vertex j, hp‚ÇÄ _‚ü©
  arrow j := ‚ü®p.arrow j, hp‚ÇÅ _‚ü©
  arrow_src j := by simpa [Subtype.ext_iff] using p.arrow_src j
  arrow_tgt j := by simpa [Subtype.ext_iff] using p.arrow_tgt j

@[simp]
lemma Subcomplex.map_Œπ_liftPath {X : SSet.{u}} (A : X.Subcomplex) {n : ‚Ñï} (p : Path X n)
    (hp‚ÇÄ : ‚àÄ j, p.vertex j ‚àà A.obj _)
    (hp‚ÇÅ : ‚àÄ j, p.arrow j ‚àà A.obj _) :
    (A.liftPath p hp‚ÇÄ hp‚ÇÅ).map A.Œπ = p := rfl

/-- Any inner horn contains the spine of the unique non-degenerate `n`-simplex
in `Œî[n]`. -/
@[simps! vertex_coe arrow_coe]
def horn.spineId {n : ‚Ñï} (i : Fin (n + 3))
    (h‚ÇÄ : 0 < i) (h‚Çô : i < Fin.last (n + 2)) :
    Path (Œõ[n + 2, i] : SSet.{u}) (n + 2) :=
  Œõ[n + 2, i].liftPath (stdSimplex.spineId (n + 2)) (by simp) (fun j ‚Ü¶ by
    convert (horn.primitiveEdge.{u} h‚ÇÄ h‚Çô j).2
    ext a
    fin_cases a <;> rfl)

@[simp]
lemma horn.spineId_map_hornInclusion {n : ‚Ñï} (i : Fin (n + 3))
    (h‚ÇÄ : 0 < i) (h‚Çô : i < Fin.last (n + 2)) :
    Path.map (horn.spineId.{u} i h‚ÇÄ h‚Çô) Œõ[n + 2, i].Œπ =
      stdSimplex.spineId (n + 2) := rfl

end SSet
