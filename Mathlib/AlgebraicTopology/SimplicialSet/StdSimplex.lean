/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz, Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.Subcomplex
import Mathlib.CategoryTheory.Subpresheaf.OfSection
import Mathlib.CategoryTheory.Limits.Types.Shapes
import Mathlib.Data.Fin.VecNotation
import Mathlib.Order.Fin.SuccAboveOrderIso

/-!
# The standard simplex

We define the standard simplices `Œî[n]` as simplicial sets.
See files `SimplicialSet.Boundary` and `SimplicialSet.Horn`
for their boundaries`‚àÇŒî[n]` and horns `Œõ[n, i]`.
(The notations are available via `open Simplicial`.)

-/

universe u

open CategoryTheory Limits Simplicial Opposite

namespace SSet

/-- The functor `SimplexCategory ‚•§ SSet` which sends `SimplexCategory.mk n` to
the standard simplex `Œî[n]` is a cosimplicial object in the category of simplicial sets.
(This functor is essentially given by the Yoneda embedding). -/
def stdSimplex : CosimplicialObject SSet.{u} :=
  yoneda ‚ãô uliftFunctor

@[deprecated (since := "2025-01-23")] alias standardSimplex := stdSimplex

@[inherit_doc SSet.stdSimplex]
scoped[Simplicial] notation3 "Œî[" n "]" => SSet.stdSimplex.obj (SimplexCategory.mk n)

instance : Inhabited SSet :=
  ‚ü®Œî[0]‚ü©

instance {n} : Inhabited (SSet.Truncated n) :=
  ‚ü®(truncation n).obj <| Œî[0]‚ü©

namespace stdSimplex

open Finset Opposite SimplexCategory

@[simp]
lemma map_id (n : SimplexCategory) :
    (SSet.stdSimplex.map (SimplexCategory.Hom.mk OrderHom.id : n ‚ü∂ n)) = ùüô _ :=
  CategoryTheory.Functor.map_id _ _

/-- Simplices of the standard simplex identify to morphisms in `SimplexCategory`. -/
def objEquiv {n : SimplexCategory} {m : SimplexCategory·µí·µñ} :
    (stdSimplex.{u}.obj n).obj m ‚âÉ (m.unop ‚ü∂ n) :=
  Equiv.ulift.{u, 0}

/-- If `x : Œî[n] _‚¶ãd‚¶å` and `i : Fin (d + 1)`, we may evaluate `x i : Fin (n + 1)`. -/
instance (n i : ‚Ñï) : DFunLike (Œî[n] _‚¶ãi‚¶å) (Fin (i + 1)) (fun _ ‚Ü¶ Fin (n + 1)) where
  coe x j := (objEquiv x).toOrderHom j
  coe_injective' _ _ h := objEquiv.injective (by ext : 3; apply congr_fun h)

@[ext]
lemma ext {n d : ‚Ñï} (x y : Œî[n] _‚¶ãd‚¶å) (h : ‚àÄ (i : Fin (d + 1)), x i = y i) : x = y :=
  DFunLike.ext _ _ h

@[simp]
lemma objEquiv_toOrderHom_apply {n i : ‚Ñï}
    (x : (stdSimplex.{u} ^‚¶ãn‚¶å).obj (op (.mk i))) (j : Fin (i + 1)) :
    DFunLike.coe (F := Fin (i + 1) ‚Üío Fin (n + 1))
      ((DFunLike.coe (F := Œî[n].obj (op ‚¶ãi‚¶å) ‚âÉ (‚¶ãi‚¶å ‚ü∂ ‚¶ãn‚¶å))
        objEquiv x)).toOrderHom j = x j :=
  rfl

lemma objEquiv_symm_comp {n n' : SimplexCategory} {m : SimplexCategory·µí·µñ}
    (f : m.unop ‚ü∂ n) (g : n ‚ü∂ n') :
    objEquiv.{u}.symm (f ‚â´ g) =
      (stdSimplex.map g).app _ (objEquiv.{u}.symm f) := rfl

@[simp]
lemma objEquiv_symm_apply {n m : ‚Ñï}
    (f : SimplexCategory.mk m ‚ü∂ SimplexCategory.mk n) (i : Fin (m + 1)) :
    (objEquiv.{u}.symm f : Œî[n] _‚¶ãm‚¶å) i = f.toOrderHom i := rfl

/-- Constructor for simplices of the standard simplex which takes a `OrderHom` as an input. -/
abbrev objMk {n : SimplexCategory} {m : SimplexCategory·µí·µñ}
    (f : Fin (len m.unop + 1) ‚Üío Fin (n.len + 1)) :
    (stdSimplex.{u}.obj n).obj m :=
  objEquiv.symm (Hom.mk f)

@[simp]
lemma objMk_apply {n m : ‚Ñï} (f : Fin (m + 1) ‚Üío Fin (n + 1)) (i : Fin (m + 1)) :
    objMk.{u} (n := .mk n) (m := op (.mk m)) f i = f i :=
  rfl

/-- The `m`-simplices of the `n`-th standard simplex are
the monotone maps from `Fin (m+1)` to `Fin (n+1)`. -/
def asOrderHom {n} {m} (Œ± : Œî[n].obj m) : OrderHom (Fin (m.unop.len + 1)) (Fin (n + 1)) :=
  Œ±.down.toOrderHom

lemma map_apply {m‚ÇÅ m‚ÇÇ : SimplexCategory·µí·µñ} (f : m‚ÇÅ ‚ü∂ m‚ÇÇ) {n : SimplexCategory}
    (x : (stdSimplex.{u}.obj n).obj m‚ÇÅ) :
    (stdSimplex.{u}.obj n).map f x = objEquiv.symm (f.unop ‚â´ objEquiv x) := by
  rfl

/-- The canonical bijection `(stdSimplex.obj n ‚ü∂ X) ‚âÉ X.obj (op n)`. -/
def _root_.SSet.yonedaEquiv {X : SSet.{u}} {n : SimplexCategory} :
    (stdSimplex.obj n ‚ü∂ X) ‚âÉ X.obj (op n) :=
  yonedaCompUliftFunctorEquiv X n

lemma yonedaEquiv_map {n m : SimplexCategory} (f : n ‚ü∂ m) :
    yonedaEquiv.{u} (stdSimplex.map f) = objEquiv.symm f :=
  yonedaEquiv.symm.injective rfl

/-- The (degenerate) `m`-simplex in the standard simplex concentrated in vertex `k`. -/
def const (n : ‚Ñï) (k : Fin (n + 1)) (m : SimplexCategory·µí·µñ) : Œî[n].obj m :=
  objMk (OrderHom.const _ k )

@[simp]
lemma const_down_toOrderHom (n : ‚Ñï) (k : Fin (n+1)) (m : SimplexCategory·µí·µñ) :
    (const n k m).down.toOrderHom = OrderHom.const _ k :=
  rfl

/-- The `0`-simplices of `Œî[n]` identify to the elements in `Fin (n + 1)`. -/
@[simps]
def obj‚ÇÄEquiv {n : ‚Ñï} : Œî[n] _‚¶ã0‚¶å ‚âÉ Fin (n + 1) where
  toFun x := x 0
  invFun i := const _ i _
  left_inv x := by ext i : 1; fin_cases i; rfl

/-- The edge of the standard simplex with endpoints `a` and `b`. -/
def edge (n : ‚Ñï) (a b : Fin (n + 1)) (hab : a ‚â§ b) : Œî[n] _‚¶ã1‚¶å := by
  refine objMk ‚ü®![a, b], ?_‚ü©
  rw [Fin.monotone_iff_le_succ]
  simp only [unop_op, len_mk, Fin.forall_fin_one]
  apply Fin.mk_le_mk.mpr hab

lemma coe_edge_down_toOrderHom (n : ‚Ñï) (a b : Fin (n+1)) (hab : a ‚â§ b) :
    ‚Üë(edge n a b hab).down.toOrderHom = ![a, b] :=
  rfl

/-- The triangle in the standard simplex with vertices `a`, `b`, and `c`. -/
def triangle {n : ‚Ñï} (a b c : Fin (n + 1)) (hab : a ‚â§ b) (hbc : b ‚â§ c) : Œî[n] _‚¶ã2‚¶å := by
  refine objMk ‚ü®![a, b, c], ?_‚ü©
  rw [Fin.monotone_iff_le_succ]
  simp only [unop_op, len_mk, Fin.forall_fin_two]
  dsimp
  simp only [*, Matrix.tail_cons, Matrix.head_cons, true_and]

lemma coe_triangle_down_toOrderHom {n : ‚Ñï} (a b c : Fin (n+1)) (hab : a ‚â§ b) (hbc : b ‚â§ c) :
    ‚Üë(triangle a b c hab hbc).down.toOrderHom = ![a, b, c] :=
  rfl

attribute [local simp] image_subset_iff

/-- Given `S : Finset (Fin (n + 1))`, this is the corresponding face of `Œî[n]`,
as a subcomplex. -/
@[simps -isSimp obj]
def face {n : ‚Ñï} (S : Finset (Fin (n + 1))) : (Œî[n] : SSet.{u}).Subcomplex where
  obj U := setOf (fun f ‚Ü¶ Finset.image (objEquiv f).toOrderHom ‚ä§ ‚â§ S)
  map {U V} i := by aesop

attribute [local simp] face_obj

@[simp]
lemma mem_face_iff {n : ‚Ñï} (S : Finset (Fin (n + 1))) {d : ‚Ñï} (x : (Œî[n] : SSet.{u}) _‚¶ãd‚¶å) :
    x ‚àà (face S).obj _ ‚Üî ‚àÄ (i : Fin (d + 1)), x i ‚àà S := by
  simp

lemma face_inter_face {n : ‚Ñï} (S‚ÇÅ S‚ÇÇ : Finset (Fin (n + 1))) :
    face S‚ÇÅ ‚äì face S‚ÇÇ = face (S‚ÇÅ ‚äì S‚ÇÇ) := by
  aesop

end stdSimplex

lemma yonedaEquiv_comp {X Y : SSet.{u}} {n : SimplexCategory}
    (f : stdSimplex.obj n ‚ü∂ X) (g : X ‚ü∂ Y) :
    yonedaEquiv (f ‚â´ g) = g.app _ (yonedaEquiv f) := rfl

namespace Subcomplex

variable {X : SSet.{u}}

/-- The subcomplex of a simplicial set that is generated by a simplex. -/
abbrev ofSimplex {n : ‚Ñï} (x : X _‚¶ãn‚¶å) : X.Subcomplex := Subpresheaf.ofSection x

lemma range_eq_ofSimplex {n : ‚Ñï} (f : Œî[n] ‚ü∂ X) :
    Subpresheaf.range f = ofSimplex (yonedaEquiv f) :=
  Subpresheaf.range_eq_ofSection' _

lemma mem_ofSimplex_obj {n : ‚Ñï} (x : X _‚¶ãn‚¶å) :
    x ‚àà (ofSimplex x).obj _ :=
  Subpresheaf.mem_ofSection_obj x

lemma ofSimplex_le_iff {n : ‚Ñï} (x : X _‚¶ãn‚¶å) (A : X.Subcomplex) :
     ofSimplex x ‚â§ A ‚Üî x ‚àà A.obj _ :=
  Subpresheaf.ofSection_le_iff _ _

lemma yonedaEquiv_coe {A : X.Subcomplex} {n : SimplexCategory}
    (f : stdSimplex.obj n ‚ü∂ A) :
    (DFunLike.coe (F := ((stdSimplex.obj n ‚ü∂ Subpresheaf.toPresheaf A) ‚âÉ A.obj (op n)))
      yonedaEquiv f).val = yonedaEquiv (f ‚â´ A.Œπ) := by
  rfl

end Subcomplex

namespace stdSimplex

lemma face_eq_ofSimplex {n : ‚Ñï} (S : Finset (Fin (n + 1))) (m : ‚Ñï) (e : Fin (m + 1) ‚âÉo S) :
    face.{u} S =
      Subcomplex.ofSimplex (X := Œî[n])
        (objMk ((OrderHom.Subtype.val _).comp
          e.toOrderEmbedding.toOrderHom)) := by
  apply le_antisymm
  ¬∑ rintro ‚ü®k‚ü© x hx
    induction' k using SimplexCategory.rec with k
    rw [mem_face_iff] at hx
    let œÜ : Fin (k + 1) ‚Üío S :=
      { toFun i := ‚ü®x i, hx i‚ü©
        monotone' := (objEquiv x).toOrderHom.monotone }
    refine ‚ü®Quiver.Hom.op
      (SimplexCategory.Hom.mk ((e.symm.toOrderEmbedding.toOrderHom.comp œÜ))), ?_‚ü©
    obtain ‚ü®f, rfl‚ü© := objEquiv.symm.surjective x
    ext j : 1
    simpa only [Subtype.ext_iff] using e.apply_symm_apply ‚ü®_, hx j‚ü©
  ¬∑ simp [Subcomplex.ofSimplex_le_iff]

/-- If `S : Finset (Fin (n + 1))` is order isomorphic to `Fin (m + 1)`,
then the face `face S` of `Œî[n]` is representable by `m`,
i.e. `face S` is isomorphic to `Œî[m]`, see `stdSimplex.isoOfRepresentableBy`. -/
def faceRepresentableBy {n : ‚Ñï} (S : Finset (Fin (n + 1)))
    (m : ‚Ñï) (e : Fin (m + 1) ‚âÉo S) :
    (face S : SSet.{u}).RepresentableBy (.mk m) where
  homEquiv {j} :=
    { toFun f := ‚ü®objMk ((OrderHom.Subtype.val S.toSet).comp
          (e.toOrderEmbedding.toOrderHom.comp f.toOrderHom)), fun _ ‚Ü¶ by aesop‚ü©
      invFun := fun ‚ü®x, hx‚ü© ‚Ü¶ SimplexCategory.Hom.mk
        { toFun i := e.symm ‚ü®(objEquiv x).toOrderHom i, hx (by aesop)‚ü©
          monotone' i‚ÇÅ i‚ÇÇ h := e.symm.monotone (by
            simp only [Subtype.mk_le_mk]
            exact OrderHom.monotone _ h) }
      left_inv f := by
        ext i : 3
        apply e.symm_apply_apply
      right_inv := fun ‚ü®x, hx‚ü© ‚Ü¶ by
        induction' j using SimplexCategory.rec with j
        dsimp
        ext i : 2
        exact congr_arg Subtype.val
          (e.apply_symm_apply ‚ü®(objEquiv x).toOrderHom i, _‚ü©) }
  homEquiv_comp f g := by aesop

/-- If a simplicial set `X` is representable by `SimplexCategory.mk m` for some `m : ‚Ñï`,
then this is the corresponding isomorphism `Œî[m] ‚âÖ X`. -/
def isoOfRepresentableBy {X : SSet.{u}} {m : ‚Ñï} (h : X.RepresentableBy (.mk m)) :
    Œî[m] ‚âÖ X :=
  NatIso.ofComponents (fun n ‚Ü¶ Equiv.toIso (objEquiv.trans h.homEquiv)) (by
    intros
    ext
    apply h.homEquiv_comp)

lemma ofSimplex_yonedaEquiv_Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) :
    Subcomplex.ofSimplex (yonedaEquiv (stdSimplex.Œ¥ i)) = face.{u} {i}·∂ú :=
  (face_eq_ofSimplex _ _ (Fin.succAboveOrderIso i)).symm

@[simp]
lemma range_Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) :
    Subpresheaf.range (stdSimplex.Œ¥ i) = face.{u} {i}·∂ú := by
  rw [Subcomplex.range_eq_ofSimplex]
  exact ofSimplex_yonedaEquiv_Œ¥ i

end stdSimplex

section Examples

open Simplicial

/-- The simplicial circle. -/
noncomputable def S1 : SSet :=
  Limits.colimit <|
    Limits.parallelPair (stdSimplex.Œ¥ 0 : Œî[0] ‚ü∂ Œî[1]) (stdSimplex.Œ¥ 1)

end Examples

namespace Augmented

/-- The functor which sends `‚¶ãn‚¶å` to the simplicial set `Œî[n]` equipped by
the obvious augmentation towards the terminal object of the category of sets. -/
@[simps]
noncomputable def stdSimplex : SimplexCategory ‚•§ SSet.Augmented.{u} where
  obj Œî :=
    { left := SSet.stdSimplex.obj Œî
      right := terminal _
      hom := { app := fun _ => terminal.from _ } }
  map Œ∏ :=
    { left := SSet.stdSimplex.map Œ∏
      right := terminal.from _ }

end Augmented

@[deprecated (since := "2025-01-26")] alias asOrderHom := stdSimplex.asOrderHom

end SSet
