/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.SimplicialSet.HomotopyCat
public import Mathlib.CategoryTheory.Category.Cat.CartesianClosed
public import Mathlib.CategoryTheory.Functor.CurryingThree
public import Mathlib.CategoryTheory.Products.Associator

/-!
# The homotopy category functor is monoidal

Given `2`-truncated simplicial sets `X` and `Y`, we introduce ad operation
`Truncated.Edge.tensor : Edge x x' ‚Üí Edge y y' ‚Üí Edge (x, y) (x', y')`.
We shall use this in order to construct an equivalence of categories
`(X ‚äó Y).HomotopyCategory ‚âå X.HomotopyCategory √ó Y.HomotopyCategory` (TODO @joelriou).

-/

@[expose] public section

universe u

open CategoryTheory MonoidalCategory Simplicial SimplicialObject.Truncated
  CartesianMonoidalCategory Limits

namespace SSet

namespace Truncated

namespace Edge

variable {X Y X' Y' Z : Truncated.{u} 2}

/-- The external product of edges of `2`-truncated simplicial sets. -/
@[simps]
def tensor {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÇ : Edge y y') :
    Edge (X := X ‚äó Y) (x, y) (x', y') where
  edge := (e‚ÇÅ.edge, e‚ÇÇ.edge)
  src_eq := Prod.ext e‚ÇÅ.src_eq e‚ÇÇ.src_eq
  tgt_eq := Prod.ext e‚ÇÅ.tgt_eq e‚ÇÇ.tgt_eq

lemma tensor_surjective {x x' : X _‚¶ã0‚¶å‚ÇÇ} {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e : Edge (X := X ‚äó Y) (x, y) (x', y')) :
    ‚àÉ (e‚ÇÅ : Edge x x') (e‚ÇÇ : Edge y y'), e‚ÇÅ.tensor e‚ÇÇ = e :=
  ‚ü®e.map (fst _ _), e.map (snd _ _), rfl‚ü©

@[simp]
lemma id_tensor_id (x : X _‚¶ã0‚¶å‚ÇÇ) (y : Y _‚¶ã0‚¶å‚ÇÇ) :
    (id x).tensor (id y) = id (X := X ‚äó Y) (x, y):= rfl

@[simp]
lemma map_tensorHom {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÇ : Edge y y') (f : X ‚ü∂ X') (g : Y ‚ü∂ Y') :
    (e‚ÇÅ.tensor e‚ÇÇ).map (f ‚äó‚Çò g) =
      (e‚ÇÅ.map f).tensor (e‚ÇÇ.map g) := rfl

@[simp]
lemma map_whiskerRight {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÇ : Edge y y') (f : X ‚ü∂ X') :
    (e‚ÇÅ.tensor e‚ÇÇ).map (f ‚ñ∑ _) =
      (e‚ÇÅ.map f).tensor e‚ÇÇ := rfl

@[simp]
lemma map_whiskerLeft {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÇ : Edge y y') (g : Y ‚ü∂ Y') :
    (e‚ÇÅ.tensor e‚ÇÇ).map (_ ‚óÅ g) =
      e‚ÇÅ.tensor (e‚ÇÇ.map g) := rfl

@[simp]
lemma map_associator_hom {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÇ : Edge y y')
    {z z' : Z _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÉ : Edge z z') :
    ((e‚ÇÅ.tensor e‚ÇÇ).tensor e‚ÇÉ).map (Œ±_ _ _ _).hom = e‚ÇÅ.tensor (e‚ÇÇ.tensor e‚ÇÉ) :=
  rfl

@[simp]
lemma map_fst {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÇ : Edge y y') :
    (e‚ÇÅ.tensor e‚ÇÇ).map (fst _ _) = e‚ÇÅ := rfl

@[simp]
lemma map_snd {x x' : X _‚¶ã0‚¶å‚ÇÇ} (e‚ÇÅ : Edge x x') {y y' : Y _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÇ : Edge y y') :
    (e‚ÇÅ.tensor e‚ÇÇ).map (snd _ _) = e‚ÇÇ := rfl

/-- The external product of `CompStruct` between edges of `2`-truncated simplicial sets. -/
@[simps simplex_fst simplex_snd]
def CompStruct.tensor
    {x‚ÇÄ x‚ÇÅ x‚ÇÇ : X _‚¶ã0‚¶å‚ÇÇ} {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ} {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ}
    (hx : CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ)
    {y‚ÇÄ y‚ÇÅ y‚ÇÇ : Y _‚¶ã0‚¶å‚ÇÇ} {e'‚ÇÄ‚ÇÅ : Edge y‚ÇÄ y‚ÇÅ} {e'‚ÇÅ‚ÇÇ : Edge y‚ÇÅ y‚ÇÇ} {e'‚ÇÄ‚ÇÇ : Edge y‚ÇÄ y‚ÇÇ}
    (hy : CompStruct e'‚ÇÄ‚ÇÅ e'‚ÇÅ‚ÇÇ e'‚ÇÄ‚ÇÇ) :
    CompStruct (e‚ÇÄ‚ÇÅ.tensor e'‚ÇÄ‚ÇÅ) (e‚ÇÅ‚ÇÇ.tensor e'‚ÇÅ‚ÇÇ) (e‚ÇÄ‚ÇÇ.tensor e'‚ÇÄ‚ÇÇ) where
  simplex := (hx.simplex, hy.simplex)
  d‚ÇÇ := Prod.ext hx.d‚ÇÇ hy.d‚ÇÇ
  d‚ÇÄ := Prod.ext hx.d‚ÇÄ hy.d‚ÇÄ
  d‚ÇÅ := Prod.ext hx.d‚ÇÅ hy.d‚ÇÅ

end Edge

namespace HomotopyCategory

instance {n : ‚Ñï} (d : (SimplexCategory.Truncated n)·µí·µñ) :
    Unique ((ùüô_ (Truncated.{u} n)).obj d) :=
  inferInstanceAs (Unique PUnit)

/-- If `X : Truncated 2` has a unique `0`-simplex and (at most) one `1`-simplex,
this is the isomorphism `Cat.of X.HomotopyCategory ‚âÖ Cat.chosenTerminal` in `Cat`. -/
def isoTerminal (X : Truncated.{u} 2) [Unique (X _‚¶ã0‚¶å‚ÇÇ)] [Subsingleton (X _‚¶ã1‚¶å‚ÇÇ)] :
    Cat.of X.HomotopyCategory ‚âÖ Cat.chosenTerminal :=
  IsTerminal.uniqueUpToIso (isTerminal _) Cat.chosenTerminalIsTerminal

lemma square {X Y : Truncated.{u} 2}
    {x‚ÇÄ x‚ÇÅ : X _‚¶ã0‚¶å‚ÇÇ} (ex : Edge x‚ÇÄ x‚ÇÅ) {y‚ÇÄ y‚ÇÅ : Y _‚¶ã0‚¶å‚ÇÇ} (ey : Edge y‚ÇÄ y‚ÇÅ) :
    homMk (ex.tensor (.id y‚ÇÄ)) ‚â´ homMk (Edge.tensor (.id x‚ÇÅ) ey) =
      homMk (Edge.tensor (.id x‚ÇÄ) ey) ‚â´ homMk (ex.tensor (.id y‚ÇÅ)) := by
  rw [homMk_comp_homMk ((Edge.CompStruct.idComp ex).tensor (Edge.CompStruct.compId ey)),
    homMk_comp_homMk ((Edge.CompStruct.compId ex).tensor (Edge.CompStruct.idComp ey))]

end HomotopyCategory

end Truncated

end SSet
