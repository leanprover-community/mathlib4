/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.Nerve
import Mathlib.AlgebraicTopology.SimplexCategory.Truncated

/-!
# Edges and "triangles" in (truncated) simplicial sets

-/

universe v u

open CategoryTheory Simplicial SimplicialObject.Truncated
  SimplexCategory.Truncated

namespace SSet

namespace Truncated

variable {X Y : Truncated.{u} 2}

/-- In a `2`-truncated simplicial set, an edge from a vertex `x‚ÇÄ` to `x‚ÇÅ` is
a `1`-simplex with prescribed `0`-dimensional faces. -/
@[ext]
structure Edge (x‚ÇÄ x‚ÇÅ : X _‚¶ã0‚¶å‚ÇÇ) where
  /-- A `1`-simplex -/
  edge : X _‚¶ã1‚¶å‚ÇÇ
  /-- The source of the edge is `x‚ÇÄ`. -/
  src_eq : X.map (Œ¥‚ÇÇ 1).op edge = x‚ÇÄ := by cat_disch
  /-- The target of the edge is `x‚ÇÅ`. -/
  tgt_eq : X.map (Œ¥‚ÇÇ 0).op edge = x‚ÇÅ := by cat_disch

namespace Edge

attribute [simp] src_eq tgt_eq

/-- The edge given by a `1`-simplex. -/
@[simps]
def mk' (s : X _‚¶ã1‚¶å‚ÇÇ) : Edge (X.map (Œ¥‚ÇÇ 1).op s) (X.map (Œ¥‚ÇÇ 0).op s) where
  edge := s

lemma exists_of_simplex (s : X _‚¶ã1‚¶å‚ÇÇ) :
    ‚àÉ (x‚ÇÄ x‚ÇÅ : X _‚¶ã0‚¶å‚ÇÇ) (e : Edge x‚ÇÄ x‚ÇÅ), e.edge = s :=
  ‚ü®_, _, mk' s, rfl‚ü©

/-- The constant edge on a `0`-simplex. -/
@[simps]
def id (x : X _‚¶ã0‚¶å‚ÇÇ) : Edge x x where
  edge := X.map (œÉ‚ÇÇ 0).op x
  src_eq := by simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
  tgt_eq := by simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]

/-- The image of an edge by a morphism of truncated simplicial sets. -/
@[simps]
def map {x‚ÇÄ x‚ÇÅ : X _‚¶ã0‚¶å‚ÇÇ} (e : Edge x‚ÇÄ x‚ÇÅ) (f : X ‚ü∂ Y) :
    Edge (f.app _ x‚ÇÄ) (f.app _ x‚ÇÅ) where
  edge := f.app _ e.edge
  src_eq := by simp [‚Üê FunctorToTypes.naturality]
  tgt_eq := by simp [‚Üê FunctorToTypes.naturality]

@[simp]
lemma map_id (x : X _‚¶ã0‚¶å‚ÇÇ) (f : X ‚ü∂ Y) :
    (Edge.id x).map f = Edge.id (f.app _ x) := by
  ext
  simp [FunctorToTypes.naturality]

/-- Let `x‚ÇÄ`, `x‚ÇÅ`, `x‚ÇÇ` be `0`-simplices of a `2`-truncated simplicial set `X`,
`e‚ÇÄ‚ÇÅ` an edge from `x‚ÇÄ` to `x‚ÇÅ`, `e‚ÇÅ‚ÇÇ` an edge from `x‚ÇÅ` to `x‚ÇÇ`,
`e‚ÇÄ‚ÇÇ` an edge from `x‚ÇÄ` to `x‚ÇÇ`. This is the data of a `2`-simplex whose
faces are respectively `e‚ÇÄ‚ÇÇ`, `e‚ÇÅ‚ÇÇ` and `e‚ÇÄ‚ÇÅ`. Such structures shall provide
relations in the homotopy category of arbitrary (truncated) simplicial sets `X`
(and specialized constructions for quasicategories and Kan complexes.). -/
structure CompStruct {x‚ÇÄ x‚ÇÅ x‚ÇÇ : X _‚¶ã0‚¶å‚ÇÇ}
    (e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ) (e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ) (e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ) where
  /-- A `2`-simplex with prescribed `1`-dimensional faces -/
  simplex : X _‚¶ã2‚¶å‚ÇÇ
  d‚ÇÇ : X.map (Œ¥‚ÇÇ 2).op simplex = e‚ÇÄ‚ÇÅ.edge
  d‚ÇÄ : X.map (Œ¥‚ÇÇ 0).op simplex = e‚ÇÅ‚ÇÇ.edge
  d‚ÇÅ : X.map (Œ¥‚ÇÇ 1).op simplex = e‚ÇÄ‚ÇÇ.edge

namespace CompStruct

attribute [simp] d‚ÇÄ d‚ÇÅ d‚ÇÇ

lemma exists_of_simplex (s : X _‚¶ã2‚¶å‚ÇÇ) :
    ‚àÉ (x‚ÇÄ x‚ÇÅ x‚ÇÇ : X _‚¶ã0‚¶å‚ÇÇ) (e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ) (e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ)
      (e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ) (h : CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ), h.simplex = s := by
  refine ‚ü®X.map (Hom.tr (SimplexCategory.const _ _ 0)).op s,
    X.map (Hom.tr (SimplexCategory.const _ _ 1)).op s,
    X.map (Hom.tr (SimplexCategory.const _ _ 2)).op s,
    .mk _ ?_ ?_, .mk _ ?_ ?_, .mk _ ?_ ?_, .mk s rfl rfl rfl, rfl‚ü©
  all_goals
  ¬∑ rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    apply congr_fun; congr
    ext i; fin_cases i; rfl

/-- The composition of `Edge.id x` with `e : Edge x y` is `e`. -/
def idComp {x y : X _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y) :
    CompStruct (.id x) e e where
  simplex := X.map (œÉ‚ÇÇ 0).op e.edge
  d‚ÇÇ := by
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_zero]
    simp
  d‚ÇÄ := by
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_zero]
    simp
  d‚ÇÅ := by
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_one_comp_œÉ‚ÇÇ_zero]
    simp

/-- The composition of `e : Edge x y` with `Edge.id y` is `e`. -/
def compId {x y : X _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y) :
    CompStruct e (.id y) e where
  simplex := X.map (œÉ‚ÇÇ 1).op e.edge
  d‚ÇÇ := by
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_one]
    simp
  d‚ÇÄ := by
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_one]
    simp
  d‚ÇÅ := by
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_one_comp_œÉ‚ÇÇ_one]
    simp

/-- The image of a `Edge.CompStruct` by a morphism of `2`-truncated
simplicial sets. -/
@[simps]
def map {x‚ÇÄ x‚ÇÅ x‚ÇÇ : X _‚¶ã0‚¶å‚ÇÇ}
    {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ} {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ}
    (h : CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ) (f : X ‚ü∂ Y) :
    CompStruct (e‚ÇÄ‚ÇÅ.map f) (e‚ÇÅ‚ÇÇ.map f) (e‚ÇÄ‚ÇÇ.map f) where
  simplex := f.app _ h.simplex
  d‚ÇÇ := by simp [‚Üê FunctorToTypes.naturality]
  d‚ÇÄ := by simp [‚Üê FunctorToTypes.naturality]
  d‚ÇÅ := by simp [‚Üê FunctorToTypes.naturality]

end CompStruct

end Edge

end Truncated

variable {X : SSet.{u}}

/-- In a simplicial set, an edge from a vertex `x‚ÇÄ` to `x‚ÇÅ` is
a `1`-simplex with prescribed `0`-dimensional faces. -/
def Edge (x y : X _‚¶ã0‚¶å) := Truncated.Edge (X := (truncation 2).obj X) x y

namespace Edge

variable {x y : X _‚¶ã0‚¶å}

def edge (e : Edge x y) : X _‚¶ã1‚¶å := Truncated.Edge.edge e

@[simp]
lemma src_eq (e : Edge x y) : X.Œ¥ 1 e.edge = x := Truncated.Edge.src_eq e

@[simp]
lemma tgt_eq (e : Edge x y) : X.Œ¥ 0 e.edge = y := Truncated.Edge.tgt_eq e

@[ext]
lemma ext {x y : X _‚¶ã0‚¶å} {e e' : Edge x y} (h : e.edge = e'.edge) :
    e = e' := Truncated.Edge.ext h

section

variable {x y : X _‚¶ã0‚¶å} (s : X _‚¶ã1‚¶å) (src_eq : X.Œ¥ 1 s = x) (tgt_eq : X.Œ¥ 0 s = y)

def mk : Edge x y where
  edge := s

@[simp]
lemma mk_edge : (mk s src_eq tgt_eq).edge = s := rfl

end

/-- The constant edge on a `0`-simplex. -/
def id (x : X _‚¶ã0‚¶å) : Edge x x :=
  Truncated.Edge.id _

@[simp]
lemma id_edge (x : X _‚¶ã0‚¶å) :
    (id x).edge = X.œÉ 0 x := rfl

def map (e : Edge x y) {Y : SSet} (f : X ‚ü∂ Y) :
    Edge (f.app _ x) (f.app _ y) :=
  Truncated.Edge.map e ((truncation 2).map f)

@[simp]
def map_edge (e : Edge x y) {Y : SSet} (f : X ‚ü∂ Y) :
    (e.map f).edge = f.app _ e.edge := rfl

section

variable {x‚ÇÄ x‚ÇÅ x‚ÇÇ : X _‚¶ã0‚¶å}
  (e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ) (e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ) (e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ)

def CompStruct := Truncated.Edge.CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ

variable {e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ} in
def CompStruct.mk (simplex : X _‚¶ã2‚¶å)
    (d‚ÇÇ : X.Œ¥ 2 simplex = e‚ÇÄ‚ÇÅ.edge)
    (d‚ÇÄ : X.Œ¥ 0 simplex = e‚ÇÅ‚ÇÇ.edge)
    (d‚ÇÅ : X.Œ¥ 1 simplex = e‚ÇÄ‚ÇÇ.edge) :
    CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ where
  simplex := simplex
  d‚ÇÇ := d‚ÇÇ
  d‚ÇÄ := d‚ÇÄ
  d‚ÇÅ := d‚ÇÅ

end

end Edge

end SSet

namespace CategoryTheory

open SSet

attribute [local ext (iff := false)] ComposableArrows.ext‚ÇÄ ComposableArrows.ext‚ÇÅ

variable {C : Type u} [Category.{v} C]

@[simp]
lemma nerve.left {x y : (nerve C) _‚¶ã0‚¶å} (e : Edge x y) :
    ComposableArrows.left e.edge = nerveEquiv x := by
  simp only [‚Üê e.src_eq]
  rfl

@[simp]
lemma nerve.right {x y : (nerve C) _‚¶ã0‚¶å} (e : Edge x y) :
    ComposableArrows.right (n := 1) e.edge = nerveEquiv y := by
  simp only [‚Üê e.tgt_eq]
  rfl

def nerveHomEquiv {x y : (nerve C) _‚¶ã0‚¶å} :
    Edge x y ‚âÉ (nerveEquiv x ‚ü∂ nerveEquiv y) where
  toFun e := eqToHom (by simp only [nerveEquiv, ‚Üê e.src_eq]; rfl) ‚â´ e.edge.hom ‚â´
    eqToHom (by simp only [nerveEquiv, ‚Üê e.tgt_eq]; rfl)
  invFun f := .mk (ComposableArrows.mk‚ÇÅ f) (ComposableArrows.ext‚ÇÄ rfl) (ComposableArrows.ext‚ÇÄ rfl)
  left_inv e := by cat_disch
  right_inv f := by simp

lemma mk‚ÇÅ_nerveHomEquiv_apply {x y : (nerve C) _‚¶ã0‚¶å} (e : Edge x y) :
    ComposableArrows.mk‚ÇÅ (nerveHomEquiv e) = ComposableArrows.mk‚ÇÅ e.edge.hom := by
  simp [nerveHomEquiv, ComposableArrows.mk‚ÇÅ_eqToHom_comp, ComposableArrows.mk‚ÇÅ_comp_eqToHom]

def Edge.ofHom {x y : C} (f : x ‚ü∂ y) :
    Edge (nerveEquiv.symm x) (nerveEquiv.symm y) :=
  .mk (ComposableArrows.mk‚ÇÅ f) (by cat_disch) (by cat_disch)

@[simp]
lemma Edge.ofHom_id (x : C) :
    Edge.ofHom (ùüô x) = .id _ := by
  aesop

@[simp]
lemma Edge.ofHom_edge {x y : C} (f : x ‚ü∂ y) :
    (Edge.ofHom f).edge = ComposableArrows.mk‚ÇÅ f := rfl


lemma Edge.ofHom_surjective {x y : C} :
    Function.Surjective (Edge.ofHom : (x ‚ü∂ y) ‚Üí _) := by
  intro e
  refine ‚ü®eqToHom (by simp) ‚â´ nerveHomEquiv e ‚â´ eqToHom (by simp), by cat_disch‚ü©

lemma nerve.nonempty_compStruct_iff {x‚ÇÄ x‚ÇÅ x‚ÇÇ : C}
    (f‚ÇÄ‚ÇÅ : x‚ÇÄ ‚ü∂ x‚ÇÅ) (f‚ÇÅ‚ÇÇ : x‚ÇÅ ‚ü∂ x‚ÇÇ) (f‚ÇÄ‚ÇÇ : x‚ÇÄ ‚ü∂ x‚ÇÇ) :
    Nonempty (Edge.CompStruct (Edge.ofHom f‚ÇÄ‚ÇÅ) (Edge.ofHom f‚ÇÅ‚ÇÇ) (Edge.ofHom f‚ÇÄ‚ÇÇ)) ‚Üî
      f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÇ = f‚ÇÄ‚ÇÇ := by
  have h' : Edge.CompStruct (Edge.ofHom f‚ÇÄ‚ÇÅ) (Edge.ofHom f‚ÇÅ‚ÇÇ) (Edge.ofHom (f‚ÇÄ‚ÇÅ ‚â´ f‚ÇÅ‚ÇÇ)) :=
      Edge.CompStruct.mk (ComposableArrows.mk‚ÇÇ f‚ÇÄ‚ÇÅ f‚ÇÅ‚ÇÇ)
        (by cat_disch) (by cat_disch) (by cat_disch)
  refine ‚ü®fun ‚ü®h‚ü© ‚Ü¶ ?_, fun h ‚Ü¶ ‚ü®?_‚ü©‚ü©
  ¬∑ rw [‚Üê Arrow.mk_inj]
    apply ComposableArrows.arrowEquiv.symm.injective
    convert_to ((nerve C).Œ¥ 1) h'.simplex = ((nerve C).Œ¥ 1) h.simplex
    ¬∑ exact (h'.d‚ÇÅ).symm
    ¬∑ exact (h.d‚ÇÅ).symm
    ¬∑ refine congr_arg _ (ComposableArrows.ext‚ÇÇ_of_arrow ?_ ?_)
      ¬∑ apply ComposableArrows.arrowEquiv.symm.injective
        trans ComposableArrows.mk‚ÇÅ f‚ÇÄ‚ÇÅ
        ¬∑ refine Eq.trans ?_ h'.d‚ÇÇ
          exact ComposableArrows.ext‚ÇÅ rfl rfl (by aesop)
        ¬∑ exact Eq.trans h.d‚ÇÇ.symm (ComposableArrows.ext‚ÇÅ rfl rfl (by aesop))
      ¬∑ apply ComposableArrows.arrowEquiv.symm.injective
        trans ComposableArrows.mk‚ÇÅ f‚ÇÅ‚ÇÇ
        ¬∑ refine Eq.trans ?_ h'.d‚ÇÄ
          exact ComposableArrows.ext‚ÇÅ rfl rfl (by aesop)
        ¬∑ exact Eq.trans h.d‚ÇÄ.symm (ComposableArrows.ext‚ÇÅ rfl rfl (by aesop))
  ¬∑ rwa [‚Üê h]

@[simp]
lemma nerveHomEquiv_ofHom {x y : C} (f : x ‚ü∂ y) :
    nerveHomEquiv (Edge.ofHom f) = f :=
  nerveHomEquiv.symm.injective (by cat_disch)

@[simp]
lemma nerveHomEquiv_id (x : (nerve C) _‚¶ã0‚¶å) :
    nerveHomEquiv (Edge.id x) = ùüô _ := by
  obtain ‚ü®x, rfl‚ü© := nerveEquiv.symm.surjective x
  dsimp [nerveHomEquiv]
  cat_disch

lemma nerveHomEquiv_comp {x‚ÇÄ x‚ÇÅ x‚ÇÇ : (nerve C) _‚¶ã0‚¶å} {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ}
    {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ} (h : Edge.CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ) :
    nerveHomEquiv e‚ÇÄ‚ÇÅ ‚â´ nerveHomEquiv e‚ÇÅ‚ÇÇ = nerveHomEquiv e‚ÇÄ‚ÇÇ := by
  obtain ‚ü®x‚ÇÄ, rfl‚ü© := nerveEquiv.symm.surjective x‚ÇÄ
  obtain ‚ü®x‚ÇÅ, rfl‚ü© := nerveEquiv.symm.surjective x‚ÇÅ
  obtain ‚ü®x‚ÇÇ, rfl‚ü© := nerveEquiv.symm.surjective x‚ÇÇ
  obtain ‚ü®f‚ÇÄ‚ÇÅ, rfl‚ü© := Edge.ofHom_surjective e‚ÇÄ‚ÇÅ
  obtain ‚ü®f‚ÇÅ‚ÇÇ, rfl‚ü© := Edge.ofHom_surjective e‚ÇÅ‚ÇÇ
  obtain ‚ü®f‚ÇÄ‚ÇÇ, rfl‚ü© := Edge.ofHom_surjective e‚ÇÄ‚ÇÇ
  convert (nerve.nonempty_compStruct_iff _ _ _).1 ‚ü®h‚ü© <;> apply nerveHomEquiv_ofHom

lemma œÉ_zero_nerveEquiv_symm (x : C) :
    (nerve C).œÉ 0 (nerveEquiv.symm x) = ComposableArrows.mk‚ÇÅ (ùüô x) := by
  cat_disch

@[simp]
lemma nerveHomEquiv_ofHom_map_nerveMap {D : Type u} [Category.{v} D] {x y : C}
    (f : x ‚ü∂ y) (F : C ‚•§ D) :
    nerveHomEquiv ((Edge.ofHom f).map (nerveMap F)) = F.map f := by
  simp [nerveHomEquiv]

end CategoryTheory
