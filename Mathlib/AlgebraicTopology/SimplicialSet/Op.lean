/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialObject.Op
import Mathlib.AlgebraicTopology.SimplicialSet.Basic

/-!
# The covariant involution of the category of simplicial sets

In this file, we define the covariant involution `opFunctor : SSet ‚•§ SSet`
of the category of simplicial sets that is induced by the
covariant involution `SimplexCategory.op : SimplexCategory ‚•§ SimplexCategory`.
We use an abbreviation `X.op` for `opFunctor.obj X`.


## TODO

* Show that this involution sends `Œî[n]` to itself, and that via
this identification, the horn `horn n i` is sent to `horn n i.rev` (@joelriou)
* Construct an isomorphism `nerve C·µí·µñ ‚âÖ (nerve C).op` (@robin-carlier)
* Show that the topological realization of `X.op` identifies to the
topological realization of `X` (@joelriou)

-/

universe u

open CategoryTheory Simplicial

namespace SSet

/-- The covariant involution of the category of simplicial sets that
is induced by `SimplexCategory.rev : SimplexCategory ‚•§ SimplexCategory`. -/
def opFunctor : SSet.{u} ‚•§ SSet.{u} := SimplicialObject.opFunctor

/-- The image of a simplicial set by the involution `opFunctor : SSet ‚•§ SSet`. -/
protected abbrev op (X : SSet.{u}) : SSet.{u} := opFunctor.obj X

/-- The type of `n`-simplices of `X.op` identify to type of `n`-simplices of `X`. -/
def opObjEquiv {X : SSet.{u}} {n : SimplexCategory·µí·µñ} :
    X.op.obj n ‚âÉ X.obj n := Equiv.refl _

lemma opFunctor_map {X Y : SSet.{u}} (f : X ‚ü∂ Y) {n : SimplexCategory·µí·µñ} (x : X.op.obj n) :
    (opFunctor.map f).app n x = opObjEquiv.symm (f.app _ (opObjEquiv x)) :=
  rfl

lemma op_map (X : SSet.{u}) {n m : SimplexCategory·µí·µñ} (f : n ‚ü∂ m) (x : X.op.obj n) :
    X.op.map f x =
      opObjEquiv.symm (X.map (SimplexCategory.rev.map f.unop).op (opObjEquiv x)) :=
  rfl

@[simp]
lemma op_Œ¥ (X : SSet.{u}) {n : ‚Ñï} (i : Fin (n + 2)) (x : X _‚¶ãn + 1‚¶å) :
    X.op.Œ¥ i x = opObjEquiv.symm (X.Œ¥ i.rev (opObjEquiv x)) := by
  simp [SimplicialObject.Œ¥, op_map]

@[simp]
lemma op_œÉ (X : SSet.{u}) {n : ‚Ñï} (i : Fin (n + 1)) (x : X _‚¶ãn‚¶å) :
    X.op.œÉ i x = opObjEquiv.symm (X.œÉ i.rev (opObjEquiv x)) := by
  simp [SimplicialObject.œÉ, op_map]

attribute [local simp] op_map in
/-- The functor `opFunctor : SSet ‚•§ SSet` is an involution. -/
@[simps!]
def opFunctorCompOpFunctorIso : opFunctor.{u} ‚ãô opFunctor ‚âÖ ùü≠ _ :=
  NatIso.ofComponents (fun X ‚Ü¶ NatIso.ofComponents
    (fun n ‚Ü¶ Equiv.toIso (opObjEquiv.trans opObjEquiv)))

/-- The covariant involution `opFunctor : SSet ‚•§ SSet`,
as an equivalence of categories. -/
@[simps]
def opEquivalence : SSet.{u} ‚âå SSet.{u} where
  functor := opFunctor
  inverse := opFunctor
  unitIso := opFunctorCompOpFunctorIso.symm
  counitIso := opFunctorCompOpFunctorIso

end SSet
