/-
Copyright (c) 2024 Mario Carneiro and Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.SimplicialSet.HomotopyCat
import Mathlib.AlgebraicTopology.SimplexCategory.MorphismProperty
import Mathlib.CategoryTheory.MorphismProperty.Composition

/-!

# The adjunction between the nerve and the homotopy category functor.

We define an adjunction `nerveAdjunction : hoFunctor âŠ£ nerveFunctor` between the functor that
takes a simplicial set to its homotopy category and the functor that takes a category to its nerve.

Up to natural isomorphism, this is constructed as the composite of two other adjunctions,
namely `nerveâ‚‚Adj : hoFunctorâ‚‚ âŠ£ nerveFunctorâ‚‚` between analogously-defined functors involving
the category of 2-truncated simplicial sets and `coskAdj 2 : truncation 2 âŠ£ Truncated.cosk 2`. The
aforementioned natural isomorphism

`coskâ‚‚Iso : nerveFunctor â‰… nerveFunctorâ‚‚ â‹™ Truncated.cosk 2`

exists because nerves of categories are 2-coskeletal.

We also prove that `nerveFunctor` is fully faithful, demonstrating that `nerveAdjunction` is
reflective. Since the category of simplicial sets is cocomplete, we conclude in
`CategoryTheory.Category.Cat.Colimit` that the category of categories has colimits.

-/

namespace CategoryTheory

open Category Functor Limits Opposite SimplexCategory Simplicial SSet Nerve Truncated
universe v u

section

set_option quotPrecheck false
local macro:max (priority := high) "[" n:term "]â‚‚" : term =>
  `((âŸ¨SimplexCategory.mk $n, by decideâŸ© : SimplexCategory.Truncated 2))

local macro:1000 (priority := high) X:term " _[" n:term "]â‚‚" : term =>
    `(($X : SSet.Truncated 2).obj (Opposite.op âŸ¨SimplexCategory.mk $n, by decideâŸ©))

/-- The components of the counit of `nerveâ‚‚Adj`. -/
@[simps!]
def nerveâ‚‚Adj.counit.app (C : Type u) [SmallCategory C] :
    (nerveFunctorâ‚‚.obj (Cat.of C)).HomotopyCategory â¥¤ C := by
  fapply Quotient.lift
  Â· exact
      (whiskerRight (OneTruncationâ‚‚.ofNerveâ‚‚.natIso).hom _ â‰« ReflQuiv.adj.{u}.counit).app (Cat.of C)
  Â· intro x y f g rel
    cases rel; rename_i Ï†
    simp [ReflQuiv.adj, Quot.liftOn, Cat.FreeRefl.quotientFunctor, Quotient.functor,
      Quiv.adj, Quiv.id_eq_id]
    simp only [OneTruncationâ‚‚.nerveHomEquiv, Fin.isValue, OneTruncationâ‚‚.nerveEquiv_apply, op_obj,
      ComposableArrows.obj', Fin.zero_eta, Nat.reduceAdd, Equiv.coe_fn_mk, eqToHom_refl, comp_id,
      id_comp]
    exact Ï†.map_comp (X := (0 : Fin 3)) (Y := 1) (Z := 2)
      (homOfLE (by decide)) (homOfLE (by decide))

@[simp]
theorem nerveâ‚‚Adj.counit.app_eq (C : Type u) [SmallCategory C] :
    SSet.Truncated.HomotopyCategory.quotientFunctor (nerveFunctorâ‚‚.obj (Cat.of C)) â‹™
      nerveâ‚‚Adj.counit.app.{u} C =
    (whiskerRight OneTruncationâ‚‚.ofNerveâ‚‚.natIso.hom _ â‰«
      ReflQuiv.adj.{u}.counit).app (Cat.of C) := rfl

/-- Naturality of `nerveâ‚‚Adj.counit.app` is proven using `HomotopyCategory.lift_unique'`. -/
theorem nerveâ‚‚Adj.counit.naturality â¦ƒC D : Type uâ¦„ [SmallCategory C] [SmallCategory D]
    (F : C â¥¤ D) :
    (nerveFunctorâ‚‚ â‹™ hoFunctorâ‚‚).map F â‹™ nerveâ‚‚Adj.counit.app D =
      nerveâ‚‚Adj.counit.app C â‹™ F := by
  apply HomotopyCategory.lift_unique'
  conv => lhs; rw [â† Functor.assoc]; lhs; apply (hoFunctorâ‚‚_naturality _).symm
  simp only [Cat.freeRefl_obj_Î±, ReflQuiv.of_val, comp_obj, Functor.comp_map]
  rw [â† Functor.assoc _ _ F]
  conv => rhs; lhs; exact (nerveâ‚‚Adj.counit.app_eq C)
  let F' : (Cat.of C) âŸ¶ (Cat.of D) := F
  conv => rhs; exact ((whiskerRight OneTruncationâ‚‚.ofNerveâ‚‚.natIso.hom Cat.freeRefl â‰«
    ReflQuiv.adj.counit).naturality F').symm
  simp only [app, Cat.comp_eq_comp, Functor.comp_map, Functor.assoc,
    SSet.Truncated.HomotopyCategory.quotientFunctor]
  rw [Quotient.lift_spec]

/-- The counit of `nerveâ‚‚Adj.` -/
def nerveâ‚‚Adj.counit : nerveFunctorâ‚‚ â‹™ hoFunctorâ‚‚.{u} âŸ¶ (ğŸ­ Cat) where
  app := fun _ â†¦ nerveâ‚‚Adj.counit.app (Cat.of _)
  naturality := fun _ _ F â†¦ nerveâ‚‚Adj.counit.naturality F

local notation (priority := high) "[" n "]" => SimplexCategory.mk n

/-- Because nerves are 2-coskeletal, the components of a map of 2-truncated simplicial sets valued
in a nerve can be recovered from the underlying ReflPrefunctor. -/
def toNerveâ‚‚.mk.app {X : SSet.Truncated 2} {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C)
    (n : SimplexCategory.Truncated 2) :
    X.obj (op n) âŸ¶ (nerveFunctorâ‚‚.obj (Cat.of C)).obj (op n) := by
  obtain âŸ¨n, hnâŸ© := n
  induction' n using SimplexCategory.rec with n
  match n with
  | 0 => exact fun x => .mkâ‚€ (F.obj x)
  | 1 => exact fun f => .mkâ‚ (F.map âŸ¨f, rfl, rflâŸ©)
  | 2 => exact fun Ï† => .mkâ‚‚ (F.map (ev01â‚‚ Ï†)) (F.map (ev12â‚‚ Ï†))

@[simp] theorem toNerveâ‚‚.mk.app_zero {X : SSet.Truncated 2} {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C) (x : X _[0]â‚‚) :
    mk.app F [0]â‚‚ x = .mkâ‚€ (F.obj x) := rfl

@[simp] theorem toNerveâ‚‚.mk.app_one {X : SSet.Truncated 2} {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C) (f : X _[1]â‚‚) :
    mk.app F [1]â‚‚ f = .mkâ‚ (F.map âŸ¨f, rfl, rflâŸ©) := rfl

@[simp] theorem toNerveâ‚‚.mk.app_two {X : SSet.Truncated 2} {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C) (Ï† : X _[2]â‚‚) :
    mk.app F [2]â‚‚ Ï† = .mkâ‚‚ (F.map (ev01â‚‚ Ï†)) (F.map (ev12â‚‚ Ï†)) := rfl

/-- This is similar to one of the famous Segal maps, except valued in a product rather than a
pullback.-/
noncomputable def nerveâ‚‚.seagull (C : Type u) [Category C] :
    (nerveFunctorâ‚‚.obj (Cat.of C)).obj (op [2]â‚‚) âŸ¶
    (nerveFunctorâ‚‚.obj (Cat.of C)).obj (op [1]â‚‚) â¨¯ (nerveFunctorâ‚‚.obj (Cat.of C)).obj (op [1]â‚‚) :=
  prod.lift
    ((nerveFunctorâ‚‚.obj (Cat.of C)).map (.op Î´2â‚‚)) ((nerveFunctorâ‚‚.obj (Cat.of C)).map (.op Î´0â‚‚))

instance (C : Type u) [Category C] : Mono (nerveâ‚‚.seagull C) where
  right_cancellation {X} (f g : X â†’ ComposableArrows C 2) eq := by
    ext x
    simp [nerveâ‚‚.seagull] at eq
    have eq1 := congr($eq â‰« prod.fst)
    have eq2 := congr($eq â‰« prod.snd)
    simp at eq1 eq2
    replace eq1 := congr_fun eq1 x
    replace eq2 := congr_fun eq2 x
    simp at eq1 eq2
    generalize f x = fx at *
    generalize g x = gx at *
    fapply ComposableArrows.extâ‚‚
    Â· exact congrArg (Â·.obj 0) <| eq1
    Â· exact congrArg (Â·.obj 1) <| eq1
    Â· exact congrArg (Â·.obj 1) <| eq2
    Â· exact (conj_eqToHom_iff_heq' _ _ _ _).2 (congr_arg_heq (Â·.hom) <| eq1)
    Â· exact (conj_eqToHom_iff_heq' _ _ _ _).2 (congr_arg_heq (Â·.hom) <| eq2)

/-- Naturality of the components defined by `toNerveâ‚‚.mk.app` as a morphism property of
maps in `SimplexCategory.Truncated 2).` -/
abbrev toNerveâ‚‚.mk.naturalityProperty {X : SSet.Truncated.{u} 2}  {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C) :
    MorphismProperty (SimplexCategory.Truncated 2) :=
  (MorphismProperty.naturalityProperty (fun n => toNerveâ‚‚.mk.app F n.unop)).unop

lemma toNerveâ‚‚.mk_naturality_Ïƒ00 {X : SSet.Truncated.{u} 2}  {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C) :
    toNerveâ‚‚.mk.naturalityProperty F (Ïƒâ‚‚ (n := 0) 0) := by
  ext x
  dsimp only [Nat.reduceAdd, types_comp_apply, mk.app_one]
  fapply ComposableArrows.extâ‚
  Â· simp only [ComposableArrows.mkâ‚_obj, ComposableArrows.Mkâ‚.obj]
    congr 1
    refine congr_fun (?_ : X.map _ â‰« X.map _ = ğŸ™ _) x
    rw [â† map_comp, â† map_id]; congr 1
    apply Quiver.Hom.unop_inj
    apply SimplexCategory.hom_zero_zero
  Â· simp only [ComposableArrows.mkâ‚_obj, ComposableArrows.Mkâ‚.obj]
    congr 1
    refine congr_fun (?_ : X.map _ â‰« X.map _ = ğŸ™ _) x
    rw [â† map_comp, â† map_id]; congr 1
    apply Quiver.Hom.unop_inj
    apply SimplexCategory.hom_zero_zero
  Â· refine eq_of_heq <|
      (?_ : HEq _ (ComposableArrows.mkâ‚ (C := C) (ğŸ™rq (F.obj x))).hom).trans ?_
    Â· have : âˆ€ x' a b (h1 : X.map (Î´â‚‚ 1).op x' = a) (h2 : X.map (Î´â‚‚ 0).op x' = b),
        x = a â†’ x = b â†’ x' = X.map (Ïƒâ‚‚ (n := 0) 0).op x â†’
        HEq (ComposableArrows.mkâ‚ (C := C) (F.map âŸ¨x', h1, h2âŸ©)).hom
          (ComposableArrows.mkâ‚ (C := C) (ğŸ™rq (F.obj x))).hom := by
        rintro _ _ _ _ _ rfl rfl rfl
        exact congr_arg_heq (fun a => (ComposableArrows.mkâ‚ (C := C) a).hom) (F.map_id x)
      apply this
      Â· simp only [SimplexCategory.len_mk]
        refine congr_fun (?_ : X.map _ â‰« X.map _ = ğŸ™ _).symm x
        rw [â† map_comp, â† map_id]; congr 1
        exact Quiver.Hom.unop_inj (SimplexCategory.hom_zero_zero _)
      Â· simp only [SimplexCategory.len_mk]
        refine congr_fun (?_ : X.map _ â‰« X.map _ = ğŸ™ _).symm x
        rw [â† map_comp, â† map_id]; congr 1
        exact Quiver.Hom.unop_inj (SimplexCategory.hom_zero_zero _)
      Â· simp
    Â· simp; rfl

lemma toNerveâ‚‚.mk_naturality_Î´0i {X : SSet.Truncated.{u} 2}  {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C) (i : Fin 2) :
    toNerveâ‚‚.mk.naturalityProperty F (Î´â‚‚ i) := by
  ext x
  apply ComposableArrows.extâ‚€
  fin_cases i <;> rfl

lemma toNerveâ‚‚.mk_naturality_Î´1i {X : SSet.Truncated.{u} 2}  {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C)
    (hyp : (Ï† : X _[2]â‚‚) â†’
      F.map (ev02â‚‚ Ï†) =
        CategoryStruct.comp (obj := C) (F.map (ev01â‚‚ Ï†)) (F.map (ev12â‚‚ Ï†))) (i : Fin 3) :
    toNerveâ‚‚.mk.naturalityProperty F (Î´â‚‚ i) := by
  ext x
  simp only [types_comp_apply, mk.app_two, ComposableArrows.mkâ‚‚]
  fapply ComposableArrows.extâ‚
  Â· simp only [mk.app_one, ComposableArrows.mkâ‚_obj, ComposableArrows.Mkâ‚.obj]
    fin_cases i <;> congr 1 <;> refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
      <;> rw [â† map_comp] <;> (try rfl) <;>
    Â· rw [â† op_comp]; congr 2
      ext âŸ¨j, hjâŸ©; match j with | 0 => rfl
  Â· simp only [mk.app_one, ComposableArrows.mkâ‚_obj, ComposableArrows.Mkâ‚.obj]
    fin_cases i <;>
    Â· congr 1
      refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
      rw [â† map_comp]; rfl
  Â· dsimp only [nerveFunctorâ‚‚, SimplicialObject.truncation,
      SSet.truncation, comp_obj, nerveFunctor_obj,
      whiskeringLeft_obj_obj, Functor.comp_map, nerve_map,
      ComposableArrows.whiskerLeft_map, ComposableArrows.precomp_map,
      Nat.reduceAdd, mk.app_two, ComposableArrows.mkâ‚‚]
    have : âˆ€ {A B A' B' : OneTruncationâ‚‚ X} (xâ‚ : A âŸ¶ B) (xâ‚‚ : A' âŸ¶ B'),
        A = A' â†’ B = B' â†’ xâ‚.1 = xâ‚‚.1 â†’ HEq (F.map xâ‚) (F.map xâ‚‚) := by
        rintro _ _ _ _ âŸ¨âŸ© âŸ¨âŸ© rfl rfl âŸ¨âŸ©; rfl
    fin_cases i <;> [
      show _ = _ â‰« ComposableArrows.Precomp.map _ _ âŸ¨1, _âŸ© âŸ¨2, _âŸ© _ â‰« _;
      show _ = _ â‰« ComposableArrows.Precomp.map _ _ âŸ¨0, _âŸ© âŸ¨2, _âŸ© _ â‰« _;
      show _ = _ â‰« ComposableArrows.Precomp.map _ _ âŸ¨0, _âŸ© âŸ¨1, _âŸ© _ â‰« _]
    all_goals
      rw [ComposableArrows.Precomp.map]
      apply (conj_eqToHom_iff_heq' ..).2
      dsimp only [Fin.isValue, Nat.reduceAdd, Î´â‚‚, ev1â‚‚, homOfLE_leOfHom]
    Â· show HEq _ (F.map _)
      apply this
      Â· refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
        rw [â† map_comp, â† op_comp]; congr 2
        ext (i : Fin 1); match i with | 0 => rfl
      Â· refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
        rw [â† map_comp]; rfl
      Â· rfl
    Â· refine HEq.trans ?_ (heq_of_eq (hyp x))
      apply this
      Â· refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
        rw [â† map_comp]; rfl
      Â· refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
        rw [â† map_comp]; rfl
      Â· rfl
    Â· apply this
      Â· refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
        rw [â† map_comp, â† op_comp]; congr 2
        ext (i : Fin 1); match i with | 0 => rfl
      Â· refine congr_fun (?_ : X.map _ â‰« X.map _ = _) x
        rw [â† map_comp]; rfl
      Â· rfl

lemma toNerveâ‚‚.mk_naturality_Ïƒ1i {X : SSet.Truncated.{u} 2}  {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C)
    (hyp : (Ï† : X _[2]â‚‚) â†’
      F.map (ev02â‚‚ Ï†) =
        CategoryStruct.comp (obj := C) (F.map (ev01â‚‚ Ï†)) (F.map (ev12â‚‚ Ï†))) (i : Fin 2) :
    toNerveâ‚‚.mk.naturalityProperty F (Ïƒâ‚‚ i) := by
  apply (cancel_mono (nerveâ‚‚.seagull _)).1
  simp [nerveâ‚‚.seagull]
  congr 1 <;> rw [â† map_comp, â† op_comp]
  Â· unfold Î´2â‚‚
    rw [â† toNerveâ‚‚.mk_naturality_Î´1i, â† assoc, â† map_comp, â† op_comp]
    change toNerveâ‚‚.mk.naturalityProperty F (Î´â‚‚ 2 â‰« Ïƒâ‚‚ i)
    Â· fin_cases i
      Â· show mk.naturalityProperty F (Î´â‚‚ 2 â‰« Ïƒâ‚‚ 0)
        rw [Î´â‚‚_two_comp_Ïƒâ‚‚_zero]
        exact (toNerveâ‚‚.mk.naturalityProperty F).comp_mem _ _
          (toNerveâ‚‚.mk_naturality_Ïƒ00 F) (toNerveâ‚‚.mk_naturality_Î´0i F _)
      Â· show mk.naturalityProperty F (Î´â‚‚ 2 â‰« Ïƒâ‚‚ 1)
        rw [Î´â‚‚_two_comp_Ïƒâ‚‚_one]
        exact (toNerveâ‚‚.mk.naturalityProperty F).id_mem _
    Â· exact hyp
  Â· unfold Î´0â‚‚
    rw [â† toNerveâ‚‚.mk_naturality_Î´1i, â† assoc, â† map_comp, â† op_comp]
    change toNerveâ‚‚.mk.naturalityProperty F (Î´â‚‚ 0 â‰« Ïƒâ‚‚ i)
    Â· fin_cases i <;> dsimp only [Fin.zero_eta, Fin.isValue, Fin.mk_one]
      Â· rw [Î´â‚‚_zero_comp_Ïƒâ‚‚_zero']
        exact (toNerveâ‚‚.mk.naturalityProperty F).id_mem _
      Â· rw [Î´â‚‚_zero_comp_Ïƒâ‚‚_one]
        exact (toNerveâ‚‚.mk.naturalityProperty F).comp_mem _ _
          (toNerveâ‚‚.mk_naturality_Ïƒ00 F) (toNerveâ‚‚.mk_naturality_Î´0i F _)
    Â· exact hyp

/-- A proof that the components defined by `toNerveâ‚‚.mk.app` are natural. -/
theorem toNerveâ‚‚.mk_naturality {X : SSet.Truncated.{u} 2} {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C)
    (hyp : (Ï† : X _[2]â‚‚) â†’
      F.map (ev02â‚‚ Ï†) =
        CategoryStruct.comp (obj := C) (F.map (ev01â‚‚ Ï†)) (F.map (ev12â‚‚ Ï†))) :
    toNerveâ‚‚.mk.naturalityProperty F = âŠ¤ :=
  Truncated.morphismProperty_eq_top (toNerveâ‚‚.mk.naturalityProperty F)
    (fun
      | 0, _, _ => toNerveâ‚‚.mk_naturality_Î´0i F _
      | 1, _, _ => toNerveâ‚‚.mk_naturality_Î´1i F hyp _)
    (fun
      | 0, _, 0 => toNerveâ‚‚.mk_naturality_Ïƒ00 F
      | 1, _, _ => toNerveâ‚‚.mk_naturality_Ïƒ1i _ hyp _)

/-- Because nerves are 2-coskeletal, a map of 2-truncated simplicial sets valued in a nerve can be
recovered from the underlying ReflPrefunctor. -/
@[simps!]
def toNerveâ‚‚.mk {X : SSet.Truncated.{u} 2} {C : Type u} [SmallCategory C]
    (F : SSet.oneTruncationâ‚‚.obj X âŸ¶ ReflQuiv.of C)
    (hyp : (Ï† : X _[2]â‚‚) â†’
      F.map (ev02â‚‚ Ï†) =
        CategoryStruct.comp (obj := C) (F.map (ev01â‚‚ Ï†)) (F.map (ev12â‚‚ Ï†))) :
    X âŸ¶ nerveFunctorâ‚‚.obj (Cat.of C) where
  app := fun n => toNerveâ‚‚.mk.app F n.unop
  naturality _ _ f := MorphismProperty.of_eq_top (toNerveâ‚‚.mk_naturality F hyp) f.unop

/-- An alternate version of `toNerveâ‚‚.mk`, which constructs a map of 2-truncated simplicial sets
valued in a nerve  from the underlying ReflPrefunctor, where the central hypothesis is conjugated
by the isomorphism `nerveâ‚‚Adj.NatIso.app C`. -/
@[simps!] def toNerveâ‚‚.mk' {X : SSet.Truncated.{u} 2} {C : Type u} [SmallCategory C]
    (f : SSet.oneTruncationâ‚‚.obj X âŸ¶ SSet.oneTruncationâ‚‚.obj (nerveFunctorâ‚‚.obj (Cat.of C)))
    (hyp : (Ï† : X _[2]â‚‚) â†’
      (f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom).map (ev02â‚‚ Ï†)
      = CategoryStruct.comp (obj := (Cat.of C))
        ((f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom).map (ev01â‚‚ Ï†))
        ((f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom).map (ev12â‚‚ Ï†)))
    : X âŸ¶ nerveFunctorâ‚‚.obj (Cat.of C) :=
  toNerveâ‚‚.mk (f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom) hyp

/-- A computation about `toNerveâ‚‚.mk'`. -/
theorem oneTruncationâ‚‚_toNerveâ‚‚Mk' {X : SSet.Truncated 2} {C : Type u} [SmallCategory C]
    (f : SSet.oneTruncationâ‚‚.obj X âŸ¶ SSet.oneTruncationâ‚‚.obj (nerveFunctorâ‚‚.obj (Cat.of C)))
    (hyp : (Ï† : X _[2]â‚‚) â†’
      (f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom).map (ev02â‚‚ Ï†)
      = CategoryStruct.comp (obj := C)
        ((f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom).map (ev01â‚‚ Ï†))
        ((f â‰« (OneTruncationâ‚‚.ofNerveâ‚‚.natIso.app (Cat.of C)).hom).map (ev12â‚‚ Ï†))) :
    oneTruncationâ‚‚.map (toNerveâ‚‚.mk' f hyp) = f := by
  refine ReflPrefunctor.ext (fun _ â†¦ ComposableArrows.extâ‚€ rfl)
    (fun X Y g â†¦ eq_of_heq (heq_eqRec_iff_heq.2 <| heq_eqRec_iff_heq.2 ?_))
  simp [oneTruncationâ‚‚]
  have {A B A' B' : OneTruncationâ‚‚ (nerveFunctorâ‚‚.obj (Cat.of C))}
      : A = A' â†’ B = B' â†’ âˆ€ (x : A âŸ¶ B) (y : A' âŸ¶ B'), x.1 = y.1 â†’ HEq x y := by
    rintro rfl rfl âŸ¨âŸ© âŸ¨âŸ© âŸ¨âŸ©; rfl
  apply this
  Â· exact ComposableArrows.extâ‚€ rfl
  Â· exact ComposableArrows.extâ‚€ rfl
  Â· simp
    fapply ComposableArrows.extâ‚ <;> simp [ReflQuiv.comp_eq_comp]
    Â· rw [g.src_eq]; exact congr_arg (Â·.obj 0) (f.map g).src_eq.symm
    Â· rw [g.tgt_eq]; exact congr_arg (Â·.obj 1) (f.map g).tgt_eq.symm
    Â· refine (conj_eqToHom_iff_heq' _ _ _ _).2 ?_
      simp [OneTruncationâ‚‚.nerveHomEquiv]
      obtain âŸ¨g, rfl, rflâŸ© := g
      rfl

/-- An equality between maps into the 2-truncated nerve is detected by an equality beteween their
underlying refl prefunctors. -/
theorem toNerveâ‚‚.ext {X : SSet.Truncated 2} {C : Type u} [SmallCategory C]
    (f g : X âŸ¶ nerveFunctorâ‚‚.obj (Cat.of C))
    (hyp : SSet.oneTruncationâ‚‚.map f = SSet.oneTruncationâ‚‚.map g) : f = g := by
  have eqâ‚€ x : f.app (op [0]â‚‚) x = g.app (op [0]â‚‚) x := congr(($hyp).obj x)
  have eqâ‚ x : f.app (op [1]â‚‚) x = g.app (op [1]â‚‚) x := congr((($hyp).map âŸ¨x, rfl, rflâŸ©).1)
  ext âŸ¨âŸ¨n, hnâŸ©âŸ© x
  induction' n using SimplexCategory.rec with n
  match n with
  | 0 => apply eqâ‚€
  | 1 => apply eqâ‚
  | 2 =>
    apply Functor.hext (fun i : Fin 3 => ?_) (fun (i j : Fin 3) k => ?_)
    Â· let pt : [0]â‚‚ âŸ¶ [2]â‚‚ := SimplexCategory.const _ _ i
      refine congr(($(congr_fun (f.naturality pt.op) x)).obj 0).symm.trans ?_
      refine .trans ?_ congr(($(congr_fun (g.naturality pt.op) x)).obj 0)
      exact congr($(eqâ‚€ _).obj 0)
    Â· let ar : [1]â‚‚ âŸ¶ [2]â‚‚ := mkOfLe _ _ k.le
      have h1 := congr_arg_heq (fun x => x.map' 0 1) (congr_fun (f.naturality (op ar)) x)
      have h2 := congr_arg_heq (fun x => x.map' 0 1) (congr_fun (g.naturality (op ar)) x)
      exact h1.symm.trans <| .trans (congr_arg_heq (fun x => x.map' 0 1) (eqâ‚ _)) h2

end
end CategoryTheory
