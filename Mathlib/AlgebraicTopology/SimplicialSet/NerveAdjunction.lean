/-
Copyright (c) 2024 Mario Carneiro and Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplexCategory.MorphismProperty
import Mathlib.AlgebraicTopology.SimplicialSet.HomotopyCat
import Mathlib.CategoryTheory.Category.Cat.CartesianClosed
import Mathlib.CategoryTheory.Closed.FunctorToTypes
import Mathlib.CategoryTheory.Limits.Presheaf
/-!

# The adjunction between the nerve and the homotopy category functor.

We define an adjunction `nerveAdjunction : hoFunctor ‚ä£ nerveFunctor` between the functor that
takes a simplicial set to its homotopy category and the functor that takes a category to its nerve.

Up to natural isomorphism, this is constructed as the composite of two other adjunctions,
namely `nerve‚ÇÇAdj : hoFunctor‚ÇÇ ‚ä£ nerveFunctor‚ÇÇ` between analogously-defined functors involving
the category of 2-truncated simplicial sets and `coskAdj 2 : truncation 2 ‚ä£ Truncated.cosk 2`. The
aforementioned natural isomorphism

`cosk‚ÇÇIso : nerveFunctor ‚âÖ nerveFunctor‚ÇÇ ‚ãô Truncated.cosk 2`

exists because nerves of categories are 2-coskeletal.

We also prove that `nerveFunctor` is fully faithful, demonstrating that `nerveAdjunction` is
reflective. Since the category of simplicial sets is cocomplete, we conclude in
`Mathlib/CategoryTheory/Category/Cat/Colimit.lean` that the category of categories has colimits.

Finally we show that `hoFunctor : SSet.{u} ‚•§ Cat.{u, u}` preserves finite cartesian products; note
that it fails to preserve infinite products.
-/

namespace CategoryTheory

open Category Functor Limits Opposite SimplexCategory Simplicial SSet Nerve
open SSet.Truncated SimplexCategory.Truncated SimplicialObject.Truncated
universe v u v' u'

section

/-- The components of the counit of `nerve‚ÇÇAdj`. -/
@[simps!]
def nerve‚ÇÇAdj.counit.app (C : Type u) [SmallCategory C] :
    (nerveFunctor‚ÇÇ.obj (Cat.of C)).HomotopyCategory ‚•§ C := by
  fapply Quotient.lift
  ¬∑ exact
    (whiskerRight (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso).hom _ ‚â´ ReflQuiv.adj.{u}.counit).app (Cat.of C)
  ¬∑ intro x y f g rel
    obtain ‚ü®œÜ‚ü© := rel
    simpa [ReflQuiv.adj, Quot.liftOn, Cat.FreeRefl.quotientFunctor, Quotient.functor,
        pathComposition, Quiv.adj, OneTruncation‚ÇÇ.nerveHomEquiv] using
      œÜ.map_comp (X := 0) (Y := 1) (Z := 2) (homOfLE (by decide)) (homOfLE (by decide))

@[simp]
theorem nerve‚ÇÇAdj.counit.app_eq (C : Type u) [SmallCategory C] :
    SSet.Truncated.HomotopyCategory.quotientFunctor (nerveFunctor‚ÇÇ.obj (Cat.of C)) ‚ãô
      nerve‚ÇÇAdj.counit.app.{u} C =
    (whiskerRight OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.hom _ ‚â´
      ReflQuiv.adj.{u}.counit).app (Cat.of C) := rfl

/-- The naturality of `nerve‚ÇÇAdj.counit.app`. -/
theorem nerve‚ÇÇAdj.counit.naturality {C D : Type u} [SmallCategory C] [SmallCategory D]
    (F : C ‚•§ D) :
    (nerveFunctor‚ÇÇ ‚ãô hoFunctor‚ÇÇ).map F ‚ãô nerve‚ÇÇAdj.counit.app D =
      nerve‚ÇÇAdj.counit.app C ‚ãô F := by
  apply HomotopyCategory.lift_unique'
  change ((oneTruncation‚ÇÇ ‚ãô Cat.freeRefl).map (nerveFunctor‚ÇÇ.map _)) ‚ãô
    HomotopyCategory.quotientFunctor (nerveFunctor‚ÇÇ.obj (Cat.of D)) ‚ãô app D = _
  rw [nerve‚ÇÇAdj.counit.app_eq D]
  rw [‚Üê Functor.assoc _ _ F, nerve‚ÇÇAdj.counit.app_eq C]
  exact (whiskerRight OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.{u}.hom Cat.freeRefl ‚â´
    ReflQuiv.adj.counit).naturality _

/-- The counit of `nerve‚ÇÇAdj.` -/
@[simps]
def nerve‚ÇÇAdj.counit : nerveFunctor‚ÇÇ ‚ãô hoFunctor‚ÇÇ.{u} ‚ü∂ ùü≠ Cat where
  app _ := nerve‚ÇÇAdj.counit.app _
  naturality _ _ _ := nerve‚ÇÇAdj.counit.naturality _

variable {C : Type u} [SmallCategory C] {X : SSet.Truncated.{u} 2}
    (F : SSet.oneTruncation‚ÇÇ.obj X ‚ü∂ ReflQuiv.of C)

/-- Because nerves are 2-coskeletal, the components of a map of 2-truncated simplicial sets valued
in a nerve can be recovered from the underlying ReflPrefunctor. -/
def toNerve‚ÇÇ.mk.app (n : SimplexCategory.Truncated 2) :
    X.obj (op n) ‚ü∂ (nerveFunctor‚ÇÇ.obj (Cat.of C)).obj (op n) := by
  obtain ‚ü®n, hn‚ü© := n
  induction n using SimplexCategory.rec with | _ n
  match n with
  | 0 => exact fun x => .mk‚ÇÄ (F.obj x)
  | 1 => exact fun f => .mk‚ÇÅ (F.map ‚ü®f, rfl, rfl‚ü©)
  | 2 => exact fun œÜ => .mk‚ÇÇ (F.map (ev01‚ÇÇ œÜ)) (F.map (ev12‚ÇÇ œÜ))

@[simp] theorem toNerve‚ÇÇ.mk.app_zero (x : X _‚¶ã0‚¶å‚ÇÇ) : mk.app F ‚¶ã0‚¶å‚ÇÇ x = .mk‚ÇÄ (F.obj x) := rfl

@[simp] theorem toNerve‚ÇÇ.mk.app_one (f : X _‚¶ã1‚¶å‚ÇÇ) :
    mk.app F ‚¶ã1‚¶å‚ÇÇ f = .mk‚ÇÅ (F.map ‚ü®f, rfl, rfl‚ü©) := rfl

@[simp] theorem toNerve‚ÇÇ.mk.app_two (œÜ : X _‚¶ã2‚¶å‚ÇÇ) :
    mk.app F ‚¶ã2‚¶å‚ÇÇ œÜ = .mk‚ÇÇ (F.map (ev01‚ÇÇ œÜ)) (F.map (ev12‚ÇÇ œÜ)) := rfl

/-- This is similar to one of the famous Segal maps, except valued in a product rather than a
pullback. -/
noncomputable def nerve‚ÇÇ.seagull (C : Type u) [Category C] :
    (nerveFunctor‚ÇÇ.obj (Cat.of C)).obj (op ‚¶ã2‚¶å‚ÇÇ) ‚ü∂
    (nerveFunctor‚ÇÇ.obj (Cat.of C)).obj (op ‚¶ã1‚¶å‚ÇÇ) ‚®Ø (nerveFunctor‚ÇÇ.obj (Cat.of C)).obj (op ‚¶ã1‚¶å‚ÇÇ) :=
  prod.lift
    ((nerveFunctor‚ÇÇ.obj (Cat.of C)).map (.op Œ¥2‚ÇÇ)) ((nerveFunctor‚ÇÇ.obj (Cat.of C)).map (.op Œ¥0‚ÇÇ))

instance (C : Type u) [Category C] : Mono (nerve‚ÇÇ.seagull C) where
  right_cancellation {X} (f g : X ‚Üí ComposableArrows C 2) eq := by
    ext x
    simp only [nerve‚ÇÇ.seagull, prod.comp_lift] at eq
    have eq1 := congr($eq ‚â´ prod.fst)
    have eq2 := congr($eq ‚â´ prod.snd)
    simp only [limit.lift_œÄ, BinaryFan.mk_fst, BinaryFan.mk_snd] at eq1 eq2
    replace eq1 := congr_fun eq1 x
    replace eq2 := congr_fun eq2 x
    simp only [types_comp_apply] at eq1 eq2
    generalize f x = fx at *
    generalize g x = gx at *
    fapply ComposableArrows.ext‚ÇÇ
    ¬∑ exact congrArg (¬∑.obj 0) <| eq1
    ¬∑ exact congrArg (¬∑.obj 1) <| eq1
    ¬∑ exact congrArg (¬∑.obj 1) <| eq2
    ¬∑ exact (conj_eqToHom_iff_heq' _ _ _ _).2 (congr_arg_heq (¬∑.hom) <| eq1)
    ¬∑ exact (conj_eqToHom_iff_heq' _ _ _ _).2 (congr_arg_heq (¬∑.hom) <| eq2)

/-- Naturality of the components defined by `toNerve‚ÇÇ.mk.app` as a morphism property of
maps in `SimplexCategory.Truncated 2`. -/
abbrev toNerve‚ÇÇ.mk.naturalityProperty : MorphismProperty (SimplexCategory.Truncated 2) :=
  (MorphismProperty.naturalityProperty (fun n => toNerve‚ÇÇ.mk.app F n.unop)).unop

lemma ReflPrefunctor.congr_mk‚ÇÅ_map
    {Y : Type u'} [ReflQuiver.{v'} Y] {C : Type u} [Category.{v} C]
    (F : ReflPrefunctor Y (ReflQuiv.of C))
    {x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : Y} (f : x‚ÇÅ ‚ü∂ y‚ÇÅ) (g : x‚ÇÇ ‚ü∂ y‚ÇÇ)
    (hx : x‚ÇÅ = x‚ÇÇ) (hy : y‚ÇÅ = y‚ÇÇ) (hfg : Quiver.homOfEq f hx hy = g) :
    ComposableArrows.mk‚ÇÅ (C := C) (F.map f) = ComposableArrows.mk‚ÇÅ (C := C) (F.map g) := by
  subst hx hy hfg; rfl

lemma toNerve‚ÇÇ.mk_naturality_œÉ00 : toNerve‚ÇÇ.mk.naturalityProperty F (œÉ‚ÇÇ (n := 0) 0) := by
  ext x
  refine Eq.trans ?_ (nerve.œÉ‚ÇÄ_mk‚ÇÄ_eq (C := C) (F.obj x)).symm
  have := ReflPrefunctor.map_id F x
  dsimp at this ‚ä¢
  rw [‚Üê this, ‚Üê OneTruncation‚ÇÇ.id_edge x]
  fapply ReflPrefunctor.congr_mk‚ÇÅ_map
  ¬∑ simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
  ¬∑ simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
  ¬∑ aesop

lemma toNerve‚ÇÇ.mk_naturality_Œ¥0i (i : Fin 2) : toNerve‚ÇÇ.mk.naturalityProperty F (Œ¥‚ÇÇ i) := by
  ext x
  apply ComposableArrows.ext‚ÇÄ
  fin_cases i <;> rfl

section
variable
  (hyp : ‚àÄ œÜ, F.map (ev02‚ÇÇ œÜ) = CategoryStruct.comp (obj := C) (F.map (ev01‚ÇÇ œÜ)) (F.map (ev12‚ÇÇ œÜ)))
include hyp

lemma toNerve‚ÇÇ.mk_naturality_Œ¥1i (i : Fin 3) : toNerve‚ÇÇ.mk.naturalityProperty F (Œ¥‚ÇÇ i) := by
  ext x
  simp only [types_comp_apply]
  rw [toNerve‚ÇÇ.mk.app_one]
  unfold nerveFunctor‚ÇÇ truncation SimplicialObject.truncation
  simp only [comp_obj, nerveFunctor_obj, Cat.of_Œ±, whiskeringLeft_obj_obj, op_obj,
    nerve_obj, oneTruncation‚ÇÇ_obj, ReflQuiv.of_val, Nat.reduceAdd, mk.app_two,
    Functor.comp_map, op_map, Quiver.Hom.unop_op]
  unfold Œ¥‚ÇÇ inclusion
  simp only [ObjectProperty.Œπ_map]
  fin_cases i
  ¬∑ simp only [Fin.zero_eta]
    change _ = (nerve C).Œ¥ 0 _
    rw [nerve.Œ¥‚ÇÄ_mk‚ÇÇ_eq]
    fapply ReflPrefunctor.congr_mk‚ÇÅ_map
    ¬∑ unfold ev1‚ÇÇ Œπ1‚ÇÇ Œ¥‚ÇÇ
      simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
      have := Œ¥_comp_Œ¥ (n := 0) (i := 0) (j := 1) (by decide)
      dsimp at this
      exact congrFun (congrArg X.map (congrArg Quiver.Hom.op this.symm)) x
    ¬∑ unfold ev2‚ÇÇ Œπ2‚ÇÇ Œ¥‚ÇÇ
      simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
      have := Œ¥_comp_Œ¥ (n := 0) (i := 0) (j := 0) (by decide)
      dsimp at this
      exact congrFun (congrArg X.map (congrArg Quiver.Hom.op this.symm)) x
    ¬∑ aesop
  ¬∑ simp only [Fin.mk_one]
    change _ = (nerve C).Œ¥ 1 _
    rw [nerve.Œ¥‚ÇÅ_mk‚ÇÇ_eq]
    rw [‚Üê hyp]
    fapply ReflPrefunctor.congr_mk‚ÇÅ_map
    ¬∑ unfold ev0‚ÇÇ Œπ0‚ÇÇ Œ¥‚ÇÇ
      simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    ¬∑ unfold ev2‚ÇÇ Œπ2‚ÇÇ Œ¥‚ÇÇ
      simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    ¬∑ aesop
  ¬∑ simp only [Fin.reduceFinMk]
    change _ = (nerve C).Œ¥ 2 _
    rw [nerve.Œ¥‚ÇÇ_mk‚ÇÇ_eq]
    fapply ReflPrefunctor.congr_mk‚ÇÅ_map
    ¬∑ unfold ev0‚ÇÇ Œπ0‚ÇÇ Œ¥‚ÇÇ
      simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
      have := Œ¥_comp_Œ¥ (n := 0) (i := 1) (j := 1) (by decide)
      dsimp at this
      exact congrFun (congrArg X.map (congrArg Quiver.Hom.op this)) x
    ¬∑ unfold ev1‚ÇÇ Œπ1‚ÇÇ Œ¥‚ÇÇ
      simp [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
    ¬∑ aesop

lemma toNerve‚ÇÇ.mk_naturality_œÉ1i (i : Fin 2) : toNerve‚ÇÇ.mk.naturalityProperty F (œÉ‚ÇÇ i) := by
  apply (cancel_mono (nerve‚ÇÇ.seagull _)).1
  simp only [nerve‚ÇÇ.seagull, prod.comp_lift, assoc]
  congr 1 <;> rw [‚Üê map_comp, ‚Üê op_comp]
  ¬∑ unfold Œ¥2‚ÇÇ
    rw [‚Üê toNerve‚ÇÇ.mk_naturality_Œ¥1i F hyp, ‚Üê assoc, ‚Üê map_comp, ‚Üê op_comp]
    change toNerve‚ÇÇ.mk.naturalityProperty F (Œ¥‚ÇÇ 2 ‚â´ œÉ‚ÇÇ i)
    fin_cases i
    ¬∑ dsimp only [Fin.zero_eta]
      rw [Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_zero]
      exact (toNerve‚ÇÇ.mk.naturalityProperty F).comp_mem _ _
        (toNerve‚ÇÇ.mk_naturality_œÉ00 F) (toNerve‚ÇÇ.mk_naturality_Œ¥0i F _)
    ¬∑ dsimp only [Fin.mk_one]
      rw [Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_one]
      exact (toNerve‚ÇÇ.mk.naturalityProperty F).id_mem _
  ¬∑ unfold Œ¥0‚ÇÇ
    rw [‚Üê toNerve‚ÇÇ.mk_naturality_Œ¥1i F hyp, ‚Üê assoc, ‚Üê map_comp, ‚Üê op_comp]
    change toNerve‚ÇÇ.mk.naturalityProperty F (Œ¥‚ÇÇ 0 ‚â´ œÉ‚ÇÇ i)
    fin_cases i <;> dsimp only [Fin.zero_eta, Fin.isValue, Fin.mk_one]
    ¬∑ rw [Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_zero]
      exact (toNerve‚ÇÇ.mk.naturalityProperty F).id_mem _
    ¬∑ rw [Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_one]
      exact (toNerve‚ÇÇ.mk.naturalityProperty F).comp_mem _ _
        (toNerve‚ÇÇ.mk_naturality_œÉ00 F) (toNerve‚ÇÇ.mk_naturality_Œ¥0i F _)

/-- A proof that the components defined by `toNerve‚ÇÇ.mk.app` are natural. -/
theorem toNerve‚ÇÇ.mk_naturality : toNerve‚ÇÇ.mk.naturalityProperty F = ‚ä§ :=
  Truncated.morphismProperty_eq_top (toNerve‚ÇÇ.mk.naturalityProperty F)
    (fun
      | 0, _, _ => toNerve‚ÇÇ.mk_naturality_Œ¥0i F _
      | 1, _, _ => toNerve‚ÇÇ.mk_naturality_Œ¥1i F hyp _)
    (fun
      | 0, _, 0 => toNerve‚ÇÇ.mk_naturality_œÉ00 F
      | 1, _, _ => toNerve‚ÇÇ.mk_naturality_œÉ1i F hyp _)

/-- The morphism `X ‚ü∂ nerveFunctor‚ÇÇ.obj (Cat.of C)` of 2-truncated simplicial sets that is
constructed from a refl prefunctor `F : SSet.oneTruncation‚ÇÇ.obj X ‚ü∂ ReflQuiv.of C` assuming
`‚àÄ (œÜ : : X _‚¶ã2‚¶å‚ÇÇ), F.map (ev02‚ÇÇ œÜ) = F.map (ev01‚ÇÇ œÜ) ‚â´ F.map (ev12‚ÇÇ œÜ)`. -/
@[simps!]
def toNerve‚ÇÇ.mk : X ‚ü∂ nerveFunctor‚ÇÇ.obj (Cat.of C) where
  app n := toNerve‚ÇÇ.mk.app F n.unop
  naturality _ _ f := MorphismProperty.of_eq_top (toNerve‚ÇÇ.mk_naturality F hyp) f.unop

end

section

variable (F : SSet.oneTruncation‚ÇÇ.obj X ‚ü∂ SSet.oneTruncation‚ÇÇ.obj (nerveFunctor‚ÇÇ.obj (Cat.of C)))
variable (hyp : (œÜ : X _‚¶ã2‚¶å‚ÇÇ) ‚Üí
            (F ‚â´ (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.app (Cat.of C)).hom).map (ev02‚ÇÇ œÜ) =
              CategoryStruct.comp (obj := C)
              ((F ‚â´ (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.app (Cat.of C)).hom).map (ev01‚ÇÇ œÜ))
              ((F ‚â´ (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.app (Cat.of C)).hom).map (ev12‚ÇÇ œÜ)))

/-- An alternate version of `toNerve‚ÇÇ.mk`, which constructs a map of 2-truncated simplicial sets
`X ‚ü∂ nerveFunctor‚ÇÇ.obj (Cat.of C)` from the underlying refl prefunctor under a composition
hypothesis, where that prefunctor the central hypothesis is conjugated by the isomorphism
`nerve‚ÇÇAdj.NatIso.app C`. -/
@[simps!] def toNerve‚ÇÇ.mk' : X ‚ü∂ nerveFunctor‚ÇÇ.obj (Cat.of C) :=
  toNerve‚ÇÇ.mk (F ‚â´ (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.app (Cat.of C)).hom) hyp

/-- A computation about `toNerve‚ÇÇ.mk'`. -/
theorem oneTruncation‚ÇÇ_toNerve‚ÇÇMk' : oneTruncation‚ÇÇ.map (toNerve‚ÇÇ.mk' F hyp) = F := by
  refine ReflPrefunctor.ext (fun _ ‚Ü¶ ComposableArrows.ext‚ÇÄ rfl)
    (fun X Y g ‚Ü¶ eq_of_heq (heq_eqRec_iff_heq.2 <| heq_eqRec_iff_heq.2 ?_))
  simp [oneTruncation‚ÇÇ]
  refine Quiver.heq_of_homOfEq_ext ?_ ?_ (f' := F.map g) ?_
  ¬∑ exact ComposableArrows.ext‚ÇÄ rfl
  ¬∑ exact ComposableArrows.ext‚ÇÄ rfl
  ¬∑ apply OneTruncation‚ÇÇ.Hom.ext
    simp only [oneTruncation‚ÇÇ_obj, ReflQuiv.of_val, OneTruncation‚ÇÇ.homOfEq_edge]
    fapply ComposableArrows.ext‚ÇÅ <;> simp [ReflQuiv.comp_eq_comp]
    ¬∑ rw [g.src_eq]; exact congr_arg (¬∑.obj 0) (F.map g).src_eq.symm
    ¬∑ rw [g.tgt_eq]; exact congr_arg (¬∑.obj 1) (F.map g).tgt_eq.symm
    ¬∑ refine (conj_eqToHom_iff_heq' _ _ _ _).2 ?_
      simp [OneTruncation‚ÇÇ.nerveHomEquiv]
      obtain ‚ü®g, rfl, rfl‚ü© := g
      rfl

end

/-- An equality between maps into the 2-truncated nerve is detected by an equality between their
underlying refl prefunctors. -/
theorem toNerve‚ÇÇ.ext (F G : X ‚ü∂ nerveFunctor‚ÇÇ.obj (Cat.of C))
    (hyp : SSet.oneTruncation‚ÇÇ.map F = SSet.oneTruncation‚ÇÇ.map G) : F = G := by
  have eq‚ÇÄ (x : X _‚¶ã0‚¶å‚ÇÇ) : F.app (op ‚¶ã0‚¶å‚ÇÇ) x = G.app (op ‚¶ã0‚¶å‚ÇÇ) x := congr(($hyp).obj x)
  have eq‚ÇÅ (x : X _‚¶ã1‚¶å‚ÇÇ) : F.app (op ‚¶ã1‚¶å‚ÇÇ) x = G.app (op ‚¶ã1‚¶å‚ÇÇ) x :=
    congr((($hyp).map ‚ü®x, rfl, rfl‚ü©).1)
  ext ‚ü®‚ü®n, hn‚ü©‚ü© x
  induction n using SimplexCategory.rec with | _ n
  match n with
  | 0 => apply eq‚ÇÄ
  | 1 => apply eq‚ÇÅ
  | 2 =>
    apply Functor.hext (fun i : Fin 3 => ?_) (fun (i j : Fin 3) k => ?_)
    ¬∑ let pt : ‚¶ã0‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := SimplexCategory.const _ _ i
      refine congr(($(F.naturality pt.op) x).obj 0).symm.trans ?_
      refine .trans ?_ congr(($(G.naturality pt.op) x).obj 0)
      exact congr($(eq‚ÇÄ _).obj 0)
    ¬∑ let ar : ‚¶ã1‚¶å‚ÇÇ ‚ü∂ ‚¶ã2‚¶å‚ÇÇ := mkOfLe _ _ k.le
      have h1 := congr_arg_heq (fun x => x.map' 0 1) (congr_fun (F.naturality (op ar)) x)
      have h2 := congr_arg_heq (fun x => x.map' 0 1) (congr_fun (G.naturality (op ar)) x)
      exact h1.symm.trans <| .trans (congr_arg_heq (fun x => x.map' 0 1) (eq‚ÇÅ _)) h2

/-- The components of the 2-truncated nerve adjunction unit. -/
def nerve‚ÇÇAdj.unit.app (X : SSet.Truncated.{u} 2) :
    X ‚ü∂ nerveFunctor‚ÇÇ.obj (hoFunctor‚ÇÇ.obj X) := by
  fapply toNerve‚ÇÇ.mk' (C := hoFunctor‚ÇÇ.obj X)
  ¬∑ exact (ReflQuiv.adj.{u}.unit.app (SSet.oneTruncation‚ÇÇ.obj X) ‚ãôrq
      (SSet.Truncated.HomotopyCategory.quotientFunctor X).toReflPrefunctor ‚ãôrq
      (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso).inv.app (hoFunctor‚ÇÇ.obj X))
  ¬∑ exact fun œÜ ‚Ü¶ Quotient.sound _ (HoRel‚ÇÇ.mk œÜ)

theorem nerve‚ÇÇAdj.unit.map_app_eq (X : SSet.Truncated.{u} 2) :
    SSet.oneTruncation‚ÇÇ.map (nerve‚ÇÇAdj.unit.app X) =
    ReflQuiv.adj.{u}.unit.app (SSet.oneTruncation‚ÇÇ.obj X) ‚ãôrq
    (SSet.Truncated.HomotopyCategory.quotientFunctor X).toReflPrefunctor ‚ãôrq
    (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso).inv.app (hoFunctor‚ÇÇ.obj X) := by
  apply oneTruncation‚ÇÇ_toNerve‚ÇÇMk'

@[reassoc]
lemma nerve‚ÇÇAdj.unit.naturality {X Y : SSet.Truncated.{u} 2} (f : X ‚ü∂ Y) :
    f ‚â´ unit.app Y = unit.app X ‚â´ nerveFunctor‚ÇÇ.map (hoFunctor‚ÇÇ.map f) :=
  toNerve‚ÇÇ.ext _ _ (by
    have := (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso).inv.naturality (hoFunctor‚ÇÇ.map f)
    dsimp at this ‚ä¢
    rw [Functor.map_comp, Functor.map_comp, nerve‚ÇÇAdj.unit.map_app_eq,
      nerve‚ÇÇAdj.unit.map_app_eq, ‚Üê ReflQuiv.comp_eq_comp (Y := ReflQuiv.of _),
      ‚Üê ReflQuiv.comp_eq_comp (Y := ReflQuiv.of _), assoc, ‚Üê this]
    rfl)

/-- The 2-truncated nerve adjunction unit. -/
@[simps]
def nerve‚ÇÇAdj.unit : ùü≠ (SSet.Truncated.{u} 2) ‚ü∂ hoFunctor‚ÇÇ ‚ãô nerveFunctor‚ÇÇ where
  app := nerve‚ÇÇAdj.unit.app
  naturality _ _ _ := unit.naturality _

/-- The adjunction between the 2-truncated nerve functor and the 2-truncated homotopy category
functor. -/
nonrec def nerve‚ÇÇAdj : hoFunctor‚ÇÇ.{u} ‚ä£ nerveFunctor‚ÇÇ :=
  Adjunction.mkOfUnitCounit {
    unit := nerve‚ÇÇAdj.unit
    counit := nerve‚ÇÇAdj.counit
    left_triangle := by
      ext X
      apply HomotopyCategory.lift_unique'
      dsimp
      rw [Cat.comp_eq_comp, ‚Üê Functor.assoc]
      dsimp only [hoFunctor‚ÇÇ]
      rw [‚Üê hoFunctor‚ÇÇ_naturality (nerve‚ÇÇAdj.unit.app X)]
      dsimp
      rw [nerve‚ÇÇAdj.unit.map_app_eq X, Functor.assoc, id_comp]
      change _ ‚ãô (HomotopyCategory.quotientFunctor _ ‚ãô nerve‚ÇÇAdj.counit.app (hoFunctor‚ÇÇ.obj X)) = _
      rw [nerve‚ÇÇAdj.counit.app_eq]
      dsimp
      rw [‚Üê Cat.comp_eq_comp, ‚Üê assoc, ‚Üê Cat.freeRefl.map_comp, ReflQuiv.comp_eq_comp,
        ReflPrefunctor.comp_assoc]
      dsimp
      rw [‚Üê ReflQuiv.comp_eq_comp, Iso.inv_hom_id_app, ReflQuiv.id_eq_id]
      dsimp
      rw [ReflPrefunctor.comp_id (V := hoFunctor‚ÇÇ.obj X), ‚Üê ReflQuiv.comp_eq_comp (Z := .of _),
        Cat.freeRefl.map_comp, assoc]
      have := ReflQuiv.adj.counit.naturality
        (X := Cat.freeRefl.obj (ReflQuiv.of (OneTruncation‚ÇÇ X)))
        (Y := hoFunctor‚ÇÇ.obj X) (SSet.Truncated.HomotopyCategory.quotientFunctor X)
      dsimp at this
      rw [this]
      apply Adjunction.left_triangle_components_assoc
    right_triangle := by
      refine NatTrans.ext (funext fun C ‚Ü¶ ?_)
      apply toNerve‚ÇÇ.ext
      dsimp
      simp only [id_comp, map_comp, oneTruncation‚ÇÇ_obj, map_id]
      rw [nerve‚ÇÇAdj.unit.map_app_eq, ReflPrefunctor.comp_assoc]
      rw [‚Üê ReflQuiv.comp_eq_comp,
        ‚Üê ReflQuiv.comp_eq_comp (X := ReflQuiv.of _) (Y := ReflQuiv.of _),
        assoc, assoc, ‚Üê Functor.comp_map, ‚Üê OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.inv.naturality]
      conv => lhs; rhs; rw [‚Üê assoc]
      change _ ‚â´ (ReflQuiv.forget.map _ ‚â´ ReflQuiv.forget.map _) ‚â´ _ = _
      rw [‚Üê ReflQuiv.forget.map_comp]
      dsimp
      conv => lhs; rhs; lhs; rw [Cat.comp_eq_comp]
      have : HomotopyCategory.quotientFunctor (nerveFunctor‚ÇÇ.obj C) ‚ãô _ = _ :=
        nerve‚ÇÇAdj.counit.app_eq C
      rw [this]
      dsimp
      rw [‚Üê assoc, Cat.comp_eq_comp, toReflPrefunctor.map_comp]
      rw [‚Üê ReflQuiv.comp_eq_comp (X := ReflQuiv.of _) (Y := ReflQuiv.of _) (Z := ReflQuiv.of _)]
      have := ReflQuiv.adj.unit.naturality (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.hom.app C)
      dsimp at this ‚ä¢
      rw [‚Üê assoc, ‚Üê this]
      have := ReflQuiv.adj.right_triangle_components C
      dsimp [ReflQuiv.forget] at this
      simp [reassoc_of% this]
  }

instance nerveFunctor‚ÇÇ.faithful : nerveFunctor‚ÇÇ.{u, u}.Faithful :=
  Functor.Faithful.of_comp_iso
    (G := oneTruncation‚ÇÇ) (H := ReflQuiv.forget) OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso

instance nerveFunctor‚ÇÇ.full : nerveFunctor‚ÇÇ.{u, u}.Full where
  map_surjective := by
    intro X Y F
    let uF := SSet.oneTruncation‚ÇÇ.map F
    let uF' : X ‚•§rq Y :=
      OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.inv.app X ‚â´ uF ‚â´ OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.hom.app Y
    have {a b c : X} (h : a ‚ü∂ b) (k : b ‚ü∂ c) :
        uF'.map (h ‚â´ k) = uF'.map h ‚â´ uF'.map k := by
      let hk := ComposableArrows.mk‚ÇÇ h k
      let Fh : ComposableArrows Y 1 := F.app (op ‚¶ã1‚¶å‚ÇÇ) (.mk‚ÇÅ h)
      let Fk : ComposableArrows Y 1 := F.app (op ‚¶ã1‚¶å‚ÇÇ) (.mk‚ÇÅ k)
      let Fhk' : ComposableArrows Y 1 := F.app (op ‚¶ã1‚¶å‚ÇÇ) (.mk‚ÇÅ (h ‚â´ k))
      let Fhk : ComposableArrows Y 2 := F.app (op ‚¶ã2‚¶å‚ÇÇ) hk
      have lem0 := congr_arg_heq (¬∑.map' 0 1) (congr_fun (F.naturality Œ¥0‚ÇÇ.op) hk)
      have lem1 := congr_arg_heq (¬∑.map' 0 1) (congr_fun (F.naturality Œ¥1‚ÇÇ.op) hk)
      have lem2 := congr_arg_heq (¬∑.map' 0 1) (congr_fun (F.naturality Œ¥2‚ÇÇ.op) hk)
      have eq0 : (nerveFunctor‚ÇÇ.obj X).map Œ¥0‚ÇÇ.op hk = .mk‚ÇÅ k := by
        apply ComposableArrows.ext‚ÇÅ rfl rfl
        simp [nerveFunctor‚ÇÇ, SSet.truncation]
      have eq2 : (nerveFunctor‚ÇÇ.obj X).map Œ¥2‚ÇÇ.op hk = .mk‚ÇÅ h := by
        apply ComposableArrows.ext‚ÇÅ (by rfl) (by rfl)
        simp [nerveFunctor‚ÇÇ, SSet.truncation]; rfl
      have eq1 : (nerveFunctor‚ÇÇ.obj X).map Œ¥1‚ÇÇ.op hk = .mk‚ÇÅ (h ‚â´ k) := by
        apply ComposableArrows.ext‚ÇÅ (by rfl) (by rfl)
        simp [nerveFunctor‚ÇÇ, SSet.truncation]; rfl
      dsimp at lem0 lem1 lem2
      rw [eq0] at lem0
      rw [eq1] at lem1
      rw [eq2] at lem2
      replace lem0 : uF'.map k ‚âç Fhk.map' 1 2 := by
        refine HEq.trans (b := Fk.map' 0 1) ?_ lem0
        simp [uF', nerveFunctor‚ÇÇ, SSet.truncation,
          ReflQuiv.comp_eq_comp, OneTruncation‚ÇÇ.nerveHomEquiv, Fk, uF]
      replace lem2 : uF'.map h ‚âç Fhk.map' 0 1 := by
        refine HEq.trans (b := Fh.map' 0 1) ?_ lem2
        simp [uF', nerveFunctor‚ÇÇ, SSet.truncation,
          ReflQuiv.comp_eq_comp, OneTruncation‚ÇÇ.nerveHomEquiv, uF, ComposableArrows.hom, Fh]
      replace lem1 : uF'.map (h ‚â´ k) ‚âç Fhk.map' 0 2 := by
        refine HEq.trans (b := Fhk'.map' 0 1) ?_ lem1
        simp only [Nat.reduceAdd,
          Fin.zero_eta, Fin.isValue, Fin.mk_one,
          ComposableArrows.map', homOfLE_leOfHom, uF, uF']
        simp [nerveFunctor‚ÇÇ, SSet.truncation,
          ReflQuiv.comp_eq_comp, OneTruncation‚ÇÇ.nerveHomEquiv, ComposableArrows.hom, Fhk']
      rw [Fhk.map'_comp 0 1 2] at lem1
      refine eq_of_heq (lem1.trans (heq_comp ?_ ?_ ?_ lem2.symm lem0.symm)) <;>
        simp [uF', nerveFunctor‚ÇÇ, SSet.truncation, ReflQuiv.comp_eq_comp, uF, Fhk] <;>
        [let Œπ := Œπ0‚ÇÇ; let Œπ := Œπ1‚ÇÇ; let Œπ := Œπ2‚ÇÇ] <;>
      ¬∑ replace := congr_arg (¬∑.obj 0) (congr_fun (F.naturality Œπ.op) hk)
        dsimp [oneTruncation‚ÇÇ, ComposableArrows.left, SimplicialObject.truncation,
          nerveFunctor‚ÇÇ, SSet.truncation, forget‚ÇÇ, HasForget‚ÇÇ.forget‚ÇÇ] at this ‚ä¢
        convert this.symm
        apply ComposableArrows.ext‚ÇÄ; rfl
    let fF : X ‚•§ Y := ReflPrefunctor.toFunctor uF' this
    have eq : fF.toReflPrefunctor = uF' := rfl
    refine ‚ü®fF, toNerve‚ÇÇ.ext (nerveFunctor‚ÇÇ.{u,u}.map fF) F ?_‚ü©
    ¬∑ have nat := OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.hom.naturality fF
      simp at nat
      rw [eq] at nat
      simp [uF', uF] at nat
      exact (Iso.cancel_iso_hom_right (oneTruncation‚ÇÇ.map (nerveFunctor‚ÇÇ.map fF))
        (oneTruncation‚ÇÇ.map F) (OneTruncation‚ÇÇ.ofNerve‚ÇÇ.natIso.app Y)).mp nat

/-- The 2-truncated nerve functor is both full and faithful and thus is fully faithful. -/
noncomputable def nerveFunctor‚ÇÇ.fullyfaithful : nerveFunctor‚ÇÇ.FullyFaithful :=
  FullyFaithful.ofFullyFaithful nerveFunctor‚ÇÇ

instance nerve‚ÇÇAdj.reflective : Reflective nerveFunctor‚ÇÇ.{u, u} :=
  Reflective.mk hoFunctor‚ÇÇ nerve‚ÇÇAdj

end

/-- The adjunction between the nerve functor and the homotopy category functor is, up to
isomorphism, the composite of the adjunctions `SSet.coskAdj 2` and `nerve‚ÇÇAdj`. -/
noncomputable def nerveAdjunction : hoFunctor ‚ä£ nerveFunctor :=
  Adjunction.ofNatIsoRight ((SSet.coskAdj 2).comp nerve‚ÇÇAdj) Nerve.cosk‚ÇÇIso.symm

/-- Repleteness exists for full and faithful functors but not fully faithful functors, which is
why we do this inefficiently. -/
instance nerveFunctor.faithful : nerveFunctor.{u, u}.Faithful :=
  have : (Nerve.nerveFunctor‚ÇÇ ‚ãô SSet.Truncated.cosk 2).Faithful :=
    Faithful.comp nerveFunctor‚ÇÇ (SSet.Truncated.cosk 2)
  Functor.Faithful.of_iso Nerve.cosk‚ÇÇIso.symm

instance nerveFunctor.full : nerveFunctor.{u, u}.Full :=
  have : (Nerve.nerveFunctor‚ÇÇ ‚ãô SSet.Truncated.cosk 2).Full :=
    Full.comp nerveFunctor‚ÇÇ (SSet.Truncated.cosk 2)
  Functor.Full.of_iso Nerve.cosk‚ÇÇIso.symm

/-- The nerve functor is both full and faithful and thus is fully faithful. -/
noncomputable def nerveFunctor.fullyfaithful : nerveFunctor.FullyFaithful :=
  FullyFaithful.ofFullyFaithful nerveFunctor

instance nerveAdjunction.isIso_counit : IsIso nerveAdjunction.counit :=
  Adjunction.counit_isIso_of_R_fully_faithful _

/-- The counit map of `nerveAdjunction` is an isomorphism since the nerve functor is fully
faithful. -/
noncomputable def nerveFunctorCompHoFunctorIso : nerveFunctor.{u, u} ‚ãô hoFunctor ‚âÖ ùü≠ Cat :=
  asIso (nerveAdjunction.counit)

noncomputable instance : Reflective nerveFunctor where
  L := hoFunctor
  adj := nerveAdjunction

section

instance (C D : Type v) [Category.{v} C] [Category.{v} D] :
    IsIso (prodComparison (nerveFunctor ‚ãô hoFunctor ‚ãô nerveFunctor)
      (Cat.of C) (Cat.of D)) := by
  let iso : nerveFunctor ‚ãô hoFunctor ‚ãô nerveFunctor ‚âÖ nerveFunctor :=
    (nerveFunctor.associator hoFunctor nerveFunctor).symm ‚â™‚â´
      isoWhiskerRight nerveFunctorCompHoFunctorIso nerveFunctor ‚â™‚â´ nerveFunctor.leftUnitor
  exact IsIso.of_isIso_fac_right (prodComparison_natural_of_natTrans iso.hom).symm

namespace hoFunctor

instance : hoFunctor.IsLeftAdjoint := nerveAdjunction.isLeftAdjoint

instance (C D : Type v) [Category.{v} C] [Category.{v} D] :
    IsIso (prodComparison hoFunctor (nerve C) (nerve D)) := by
  have : IsIso (nerveFunctor.map (prodComparison hoFunctor (nerve C) (nerve D))) := by
    have : IsIso (prodComparison (hoFunctor ‚ãô nerveFunctor) (nerve C) (nerve D)) :=
      IsIso.of_isIso_fac_left
        (prodComparison_comp nerveFunctor (hoFunctor ‚ãô nerveFunctor)
          (A := Cat.of C) (B := Cat.of D)).symm
    exact IsIso.of_isIso_fac_right (prodComparison_comp hoFunctor nerveFunctor).symm
  exact isIso_of_fully_faithful nerveFunctor _

instance isIso_prodComparison_stdSimplex.{w} (n m : ‚Ñï) :
    IsIso (prodComparison hoFunctor (Œî[n] : SSet.{w}) Œî[m]) :=
  IsIso.of_isIso_fac_right (prodComparison_natural
    hoFunctor (stdSimplex.isoNerve n).hom (stdSimplex.isoNerve m).hom).symm

lemma isIso_prodComparison_withSimplex {D : SSet.{u}} (X : SSet.{u})
    (H : ‚àÄ m, IsIso (prodComparison hoFunctor D Œî[m])) :
    IsIso (prodComparison hoFunctor D X) := by
  have : (prod.functor.obj D).IsLeftAdjoint := by
    have : (MonoidalCategory.tensorLeft D).IsLeftAdjoint := inferInstance
    exact Functor.isLeftAdjoint_of_iso (CartesianMonoidalCategory.tensorLeftIsoProd _)
  have : (prod.functor.obj (hoFunctor.obj D)).IsLeftAdjoint :=
    Functor.isLeftAdjoint_of_iso (CartesianMonoidalCategory.tensorLeftIsoProd _)
  have : hoFunctor.IsLeftAdjoint := inferInstance
  have : IsIso (whiskerLeft (CostructuredArrow.proj uliftYoneda X ‚ãô uliftYoneda)
      (prodComparisonNatTrans hoFunctor D)) := by
    rw [NatTrans.isIso_iff_isIso_app]
    exact fun x ‚Ü¶ H (x.left).len
  exact isIso_app_coconePt_of_preservesColimit _ (prodComparisonNatTrans ..) _
    (Presheaf.isColimitTautologicalCocone' X)

instance isIso_prodComparison (X Y : SSet) :
    IsIso (prodComparison hoFunctor X Y) := isIso_prodComparison_withSimplex _ fun m ‚Ü¶ by
  convert_to IsIso (hoFunctor.map (prod.braiding _ _).hom ‚â´
    prodComparison hoFunctor Œî[m] X ‚â´ (prod.braiding _ _).hom)
  ¬∑ ext <;> simp [‚Üê Functor.map_comp]
  suffices IsIso (prodComparison hoFunctor Œî[m] X) by infer_instance
  exact isIso_prodComparison_withSimplex _ (isIso_prodComparison_simplices _)

/-- The functor `hoFunctor : SSet ‚•§ Cat` preserves binary products of simplicial sets `X` and
`Y`. -/
instance preservesBinaryProducts (X Y : SSet) :
    PreservesLimit (pair X Y) hoFunctor :=
  PreservesLimitPair.of_iso_prod_comparison hoFunctor X Y

/-- The functor `hoFunctor : SSet ‚•§ Cat` preserves limits of functors out of
`Discrete Limits.WalkingPair`. -/
instance preservesBinaryProducts' :
    PreservesLimitsOfShape (Discrete Limits.WalkingPair) hoFunctor where
  preservesLimit {F} := preservesLimit_of_iso_diagram hoFunctor (id (diagramIsoPair F).symm)

/-- The functor `hoFunctor : SSet ‚•§ Cat` preserves finite products of simplicial sets. -/
instance preservesFiniteProducts : PreservesFiniteProducts hoFunctor :=
  Limits.PreservesFiniteProducts.of_preserves_binary_and_terminal _

/-- A product preserving functor between cartesian closed categories is monoidal. -/
noncomputable instance Monoidal : Monoidal hoFunctor :=
  Monoidal.ofChosenFiniteProducts hoFunctor

/-- A product preserving functor between cartesian closed categories is lax monoidal. -/
noncomputable instance laxMonoidal : LaxMonoidal hoFunctor := Monoidal.toLaxMonoidal


end hoFunctor

end

end CategoryTheory
