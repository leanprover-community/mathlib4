/-
Copyright (c) 2024 Mario Carneiro and Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Emily Riehl, Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.SimplexCategory.MorphismProperty
public import Mathlib.AlgebraicTopology.SimplicialSet.HomotopyCat
public import Mathlib.CategoryTheory.Category.Cat.CartesianClosed
public import Mathlib.CategoryTheory.Monoidal.Closed.FunctorToTypes
public import Mathlib.CategoryTheory.Limits.Presheaf

/-!
# The adjunction between the nerve and the homotopy category functor

We define an adjunction `nerveAdjunction : hoFunctor ‚ä£ nerveFunctor` between the functor that
takes a simplicial set to its homotopy category and the functor that takes a category to its nerve.

Up to natural isomorphism, this is constructed as the composite of two other adjunctions,
namely `nerve‚ÇÇAdj : hoFunctor‚ÇÇ ‚ä£ nerveFunctor‚ÇÇ` between analogously-defined functors involving
the category of 2-truncated simplicial sets and `coskAdj 2 : truncation 2 ‚ä£ Truncated.cosk 2`. The
aforementioned natural isomorphism

`cosk‚ÇÇIso : nerveFunctor ‚âÖ nerveFunctor‚ÇÇ ‚ãô Truncated.cosk 2`

exists because nerves of categories are 2-coskeletal.

We also prove that `nerveFunctor` is fully faithful, demonstrating that `nerveAdjunction` is
reflective. Since the category of simplicial sets is cocomplete, we conclude in
`Mathlib/CategoryTheory/Category/Cat/Colimit.lean` that the category of categories has colimits.

Finally we show that `hoFunctor : SSet.{u} ‚•§ Cat.{u, u}` preserves finite cartesian products; note
that it fails to preserve infinite products.

-/

@[expose] public section

universe u

open CategoryTheory Nerve Simplicial SimplicialObject.Truncated
  SimplexCategory.Truncated Opposite Limits

namespace SSet

namespace Truncated

section liftOfStrictSegal
/-! The goal of this section is to define `SSet.Truncated.liftOfStrictSegal`
which allows to construct of morphism `X ‚ü∂ Y` of `2`-truncated simplicial sets
from the data of maps on `0`- and `1`-simplices when `Y` is strict Segal.
-/

variable {n : ‚Ñï} {X Y : Truncated.{u} 2} (f‚ÇÄ : X _‚¶ã0‚¶å‚ÇÇ ‚Üí Y _‚¶ã0‚¶å‚ÇÇ) (f‚ÇÅ : X _‚¶ã1‚¶å‚ÇÇ ‚Üí Y _‚¶ã1‚¶å‚ÇÇ)
  (hŒ¥‚ÇÅ : ‚àÄ (x : X _‚¶ã1‚¶å‚ÇÇ), f‚ÇÄ (X.map (Œ¥‚ÇÇ 1).op x) = Y.map (Œ¥‚ÇÇ 1).op (f‚ÇÅ x))
  (hŒ¥‚ÇÄ : ‚àÄ (x : X _‚¶ã1‚¶å‚ÇÇ), f‚ÇÄ (X.map (Œ¥‚ÇÇ 0).op x) = Y.map (Œ¥‚ÇÇ 0).op (f‚ÇÅ x))
  (H : ‚àÄ (x : X _‚¶ã2‚¶å‚ÇÇ) (y : Y _‚¶ã2‚¶å‚ÇÇ), f‚ÇÅ (X.map (Œ¥‚ÇÇ 2).op x) = Y.map (Œ¥‚ÇÇ 2).op y ‚Üí
    f‚ÇÅ (X.map (Œ¥‚ÇÇ 0).op x) = Y.map (Œ¥‚ÇÇ 0).op y ‚Üí
      f‚ÇÅ (X.map (Œ¥‚ÇÇ 1).op x) = Y.map (Œ¥‚ÇÇ 1).op y)
  (hœÉ : ‚àÄ (x : X _‚¶ã0‚¶å‚ÇÇ), f‚ÇÅ (X.map (œÉ‚ÇÇ 0).op x) = Y.map (œÉ‚ÇÇ 0).op (f‚ÇÄ x))
  (hY : Y.StrictSegal)

namespace liftOfStrictSegal

/-- Auxiliary definition for `SSet.Truncated.liftOfStrictSegal`. -/
def f‚ÇÇ (x : X _‚¶ã2‚¶å‚ÇÇ) : Y _‚¶ã2‚¶å‚ÇÇ :=
  (hY.spineEquiv 2).symm
    (.mk‚ÇÇ (Y.spine 1 (by simp) (f‚ÇÅ (X.map (Œ¥‚ÇÇ 2).op x)))
      (Y.spine 1 (by simp) (f‚ÇÅ (X.map (Œ¥‚ÇÇ 0).op x))) (by
        simp only [spine_vertex]
        rw [‚Üê Œ¥‚ÇÇ_one_eq_const, ‚Üê Œ¥‚ÇÇ_zero_eq_const, ‚Üê hŒ¥‚ÇÅ, ‚Üê hŒ¥‚ÇÄ]
        simp only [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_zero_comp_Œ¥‚ÇÇ_two]))

@[simp]
lemma spineEquiv_f‚ÇÇ_arrow_zero (x : X _‚¶ã2‚¶å‚ÇÇ) :
    ((hY.spineEquiv 2) (f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x)).arrow 0 = f‚ÇÅ (X.map (Œ¥‚ÇÇ 2).op x) := by
  simp [f‚ÇÇ]

@[simp]
lemma spineEquiv_f‚ÇÇ_arrow_one (x : X _‚¶ã2‚¶å‚ÇÇ) :
    ((hY.spineEquiv 2) (f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x)).arrow 1 = f‚ÇÅ (X.map (Œ¥‚ÇÇ 0).op x) := by
  simp [f‚ÇÇ]

lemma hŒ¥'‚ÇÄ (x : X _‚¶ã2‚¶å‚ÇÇ) :
    f‚ÇÅ (X.map (Œ¥‚ÇÇ 0).op x) = Y.map (Œ¥‚ÇÇ 0).op (f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x) := by
  simp [‚Üê spineEquiv_f‚ÇÇ_arrow_one f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY, StrictSegal.spineEquiv,
    SimplexCategory.mkOfSucc_one_eq_Œ¥]

lemma hŒ¥'‚ÇÇ (x : X _‚¶ã2‚¶å‚ÇÇ) :
    f‚ÇÅ (X.map (Œ¥‚ÇÇ 2).op x) = Y.map (Œ¥‚ÇÇ 2).op (f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x) := by
  simp [‚Üê spineEquiv_f‚ÇÇ_arrow_zero f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY, StrictSegal.spineEquiv,
    SimplexCategory.mkOfSucc_zero_eq_Œ¥]

include H in
lemma hŒ¥'‚ÇÅ (x : X _‚¶ã2‚¶å‚ÇÇ) :
    f‚ÇÅ (X.map (Œ¥‚ÇÇ 1).op x) = Y.map (Œ¥‚ÇÇ 1).op (f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x) :=
  H x (f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x) (hŒ¥'‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x) (hŒ¥'‚ÇÄ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY x)

include hœÉ in
lemma hœÉ'‚ÇÄ (x : X _‚¶ã1‚¶å‚ÇÇ) :
    f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY (X.map (œÉ‚ÇÇ 0).op x) = Y.map (œÉ‚ÇÇ 0).op (f‚ÇÅ x) := by
  apply (hY.spineEquiv 2).injective
  ext i
  fin_cases i
  ¬∑ dsimp
    rw [spineEquiv_f‚ÇÇ_arrow_zero]
    dsimp [StrictSegal.spineEquiv]
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_zero, op_comp,
      FunctorToTypes.map_comp_apply, hœÉ, SimplexCategory.mkOfSucc_zero_eq_Œ¥,
      ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_two_comp_œÉ‚ÇÇ_zero,
      op_comp, FunctorToTypes.map_comp_apply, hŒ¥‚ÇÅ]
  ¬∑ dsimp
    rw [spineEquiv_f‚ÇÇ_arrow_one]
    simp [StrictSegal.spineEquiv, SimplexCategory.mkOfSucc_one_eq_Œ¥,
      ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]

include hœÉ in
lemma hœÉ'‚ÇÅ (x : X _‚¶ã1‚¶å‚ÇÇ) :
    f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY (X.map (œÉ‚ÇÇ 1).op x) = Y.map (œÉ‚ÇÇ 1).op (f‚ÇÅ x) := by
  apply (hY.spineEquiv 2).injective
  ext i
  fin_cases i
  ¬∑ dsimp
    rw [spineEquiv_f‚ÇÇ_arrow_zero]
    simp [StrictSegal.spineEquiv, SimplexCategory.mkOfSucc_zero_eq_Œ¥,
      ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp]
  ¬∑ dsimp
    rw [spineEquiv_f‚ÇÇ_arrow_one]
    dsimp [StrictSegal.spineEquiv]
    rw [‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_one, op_comp,
      FunctorToTypes.map_comp_apply, hœÉ, SimplexCategory.mkOfSucc_one_eq_Œ¥,
      ‚Üê FunctorToTypes.map_comp_apply, ‚Üê op_comp, Œ¥‚ÇÇ_zero_comp_œÉ‚ÇÇ_one,
      op_comp, FunctorToTypes.map_comp_apply, hŒ¥‚ÇÄ]

/-- Auxiliary definition for `SSet.Truncated.liftOfStrictSegal`. -/
def app (n : (SimplexCategory.Truncated 2)·µí·µñ) : X.obj n ‚ü∂ Y.obj n := by
  obtain ‚ü®n, hn‚ü© := n
  induction n using SimplexCategory.rec with | _ n
  match n with
  | 0 => exact f‚ÇÄ
  | 1 => exact f‚ÇÅ
  | 2 => exact f‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY

/-- The property of morphims in `SimplexCategory.Truncated 2` for
which `liftOfStrictSegal.app` is natural. -/
abbrev naturalityProperty : MorphismProperty (SimplexCategory.Truncated 2) :=
  (MorphismProperty.naturalityProperty (app f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY)).unop

include H hœÉ in
lemma naturalityProperty_eq_top :
    naturalityProperty f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY = ‚ä§ := by
  refine SimplexCategory.Truncated.morphismProperty_eq_top _
    (fun n hn i ‚Ü¶ ?_) (fun n hn i ‚Ü¶ ?_)
  ¬∑ obtain _ | _ | n := n
    ¬∑ fin_cases i
      ¬∑ ext; apply hŒ¥‚ÇÄ
      ¬∑ ext; apply hŒ¥‚ÇÅ
    ¬∑ fin_cases i
      ¬∑ ext; apply hŒ¥'‚ÇÄ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY
      ¬∑ ext; apply hŒ¥'‚ÇÅ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ H hY
      ¬∑ ext; apply hŒ¥'‚ÇÇ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY
    ¬∑ lia
  ¬∑ obtain _ | _ | n := n
    ¬∑ fin_cases i
      ext; apply hœÉ
    ¬∑ fin_cases i
      ¬∑ ext; apply hœÉ'‚ÇÄ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hœÉ hY
      ¬∑ ext; apply hœÉ'‚ÇÅ f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hœÉ hY
    ¬∑ lia

end liftOfStrictSegal

open liftOfStrictSegal in
/-- Constructor for morphisms `X ‚ü∂ Y` between `2`-truncated simplicial sets from
the data of maps on `0`- and `1`-simplices when `Y` is strict Segal. -/
def liftOfStrictSegal : X ‚ü∂ Y where
  app := liftOfStrictSegal.app f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ hY
  naturality _ _ œÜ :=
    (liftOfStrictSegal.naturalityProperty_eq_top f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ H hœÉ hY).symm.le
      œÜ.unop (by simp)

@[simp]
lemma liftOfStrictSegal_app_0 :
    (liftOfStrictSegal f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ H hœÉ hY).app (op ‚¶ã0‚¶å‚ÇÇ) = f‚ÇÄ := rfl

@[simp]
lemma liftOfStrictSegal_app_1 :
    (liftOfStrictSegal f‚ÇÄ f‚ÇÅ hŒ¥‚ÇÅ hŒ¥‚ÇÄ H hœÉ hY).app (op ‚¶ã1‚¶å‚ÇÇ) = f‚ÇÅ := rfl

end liftOfStrictSegal

namespace HomotopyCategory

variable {X : Truncated.{u} 2} {C D : Type u} [SmallCategory C] [SmallCategory D]

/-- Given a `2`-truncated simplicial set `X` and a category `C`,
this is the functor `X.HomotopyCategory ‚•§ C` corresponding to
a morphism `X ‚ü∂ (truncation 2).obj (nerve C)`. -/
def descOfTruncation (œÜ : X ‚ü∂ (truncation 2).obj (nerve C)) :
    X.HomotopyCategory ‚•§ C :=
  lift (fun x ‚Ü¶ nerveEquiv (œÜ.app _ x)) (fun e ‚Ü¶ nerve.homEquiv (e.map œÜ))
    (fun x ‚Ü¶ by simpa using nerve.homEquiv_id (œÜ.app _ x))
      (fun h ‚Ü¶ nerve.homEquiv_comp (h.map œÜ))

@[simp]
lemma descOfTruncation_obj_mk (œÜ : X ‚ü∂ (truncation 2).obj (nerve C)) (x : X _‚¶ã0‚¶å‚ÇÇ) :
    (descOfTruncation œÜ).obj (mk x) = nerveEquiv (œÜ.app _ x) := rfl

@[simp]
lemma descOfTruncation_map_homMk (œÜ : X ‚ü∂ (truncation 2).obj (nerve C))
    {x‚ÇÄ x‚ÇÅ : X _‚¶ã0‚¶å‚ÇÇ} (e : Edge x‚ÇÄ x‚ÇÅ) :
    (descOfTruncation œÜ).map (homMk e) = nerve.homEquiv (e.map œÜ) :=
  Category.id_comp _

lemma descOfTruncation_comp {X' : Truncated.{u} 2} (œà : X ‚ü∂ X')
    (œÜ : X' ‚ü∂ (truncation 2).obj (nerve C)) :
    descOfTruncation (œà ‚â´ œÜ) = mapHomotopyCategory œà ‚ãô descOfTruncation œÜ :=
  functor_ext (fun _ ‚Ü¶ by simp) (by cat_disch)

/-- Given a `2`-truncated simplicial set `X` and a category `C`,
this is the morphism `X ‚ü∂ (truncation 2).obj (nerve C)` corresponding
to a functor `X.HomotopyCategory ‚•§ C`. -/
def homToNerveMk (F : X.HomotopyCategory ‚•§ C) : X ‚ü∂ (truncation 2).obj (nerve C) :=
  liftOfStrictSegal (fun x ‚Ü¶ nerveEquiv.symm (F.obj (mk x)))
    (fun f ‚Ü¶ ComposableArrows.mk‚ÇÅ (F.map (homMk (Truncated.Edge.mk' f))))
    (fun f ‚Ü¶ ComposableArrows.ext‚ÇÄ rfl)
    (fun f ‚Ü¶ ComposableArrows.ext‚ÇÄ rfl)
    (fun x y h‚ÇÇ h‚ÇÄ ‚Ü¶ by
      have h' {a b : X _‚¶ã0‚¶å‚ÇÇ} (e : Edge a b) :
          ComposableArrows.mk‚ÇÅ (F.map (homMk (Edge.mk' e.edge))) =
            ComposableArrows.mk‚ÇÅ (F.map (homMk e)) :=
        ComposableArrows.arrowEquiv.injective
          (congr_arg F.mapArrow.obj (congr_arrowMk_homMk (Edge.mk' e.edge) e rfl))
      obtain ‚ü®x‚ÇÄ, x‚ÇÅ, x‚ÇÇ, e‚ÇÄ‚ÇÅ, e‚ÇÅ‚ÇÇ, e‚ÇÄ‚ÇÇ, h, rfl‚ü© := Edge.CompStruct.exists_of_simplex x
      dsimp at h‚ÇÄ h‚ÇÇ ‚ä¢
      have : ComposableArrows.mk‚ÇÇ (F.map (homMk e‚ÇÄ‚ÇÅ)) (F.map (homMk e‚ÇÅ‚ÇÇ)) = y := by
        rw [h.d‚ÇÇ, h'] at h‚ÇÇ
        rw [h.d‚ÇÄ, h'] at h‚ÇÄ
        refine (spine_bijective (X := (truncation 2).obj (nerve C)) _ _).injective ?_
        ext i
        fin_cases i
        ¬∑ dsimp
          simp only [SimplexCategory.mkOfSucc_zero_eq_Œ¥, ‚Üê h‚ÇÇ]
          apply nerve.Œ¥‚ÇÇ_mk‚ÇÇ_eq
        ¬∑ dsimp
          simp only [SimplexCategory.mkOfSucc_one_eq_Œ¥, ‚Üê h‚ÇÄ]
          apply nerve.Œ¥‚ÇÄ_mk‚ÇÇ_eq
      rw [h.d‚ÇÅ, ‚Üê this]
      have := (nerve.Œ¥‚ÇÅ_mk‚ÇÇ_eq (F.map (homMk e‚ÇÄ‚ÇÅ)) (F.map (homMk e‚ÇÅ‚ÇÇ))).symm
      rwa [‚Üê Functor.map_comp, homMk_comp_homMk h, ‚Üê h'] at this)
    (fun x ‚Ü¶ ComposableArrows.arrowEquiv.injective
      ((congr_arg F.mapArrow.obj
        (congr_arrowMk_homMk (Edge.mk' (X.map (œÉ‚ÇÇ 0).op x)) (Edge.id x) rfl)).trans (by aesop)))
    ((Nerve.strictSegal C).truncation 1)

@[simp]
lemma homToNerveMk_app_zero (F : X.HomotopyCategory ‚•§ C) (x : X _‚¶ã0‚¶å‚ÇÇ) :
    (homToNerveMk F).app _ x = nerveEquiv.symm (F.obj (mk x)) := rfl

lemma homToNerveMk_app_one (F : X.HomotopyCategory ‚•§ C) (f : X _‚¶ã1‚¶å‚ÇÇ) :
    (homToNerveMk F).app _ f =
      ComposableArrows.mk‚ÇÅ (F.map (homMk (Truncated.Edge.mk' f))) :=
  rfl

@[simp]
lemma homToNerveMk_app_edge (F : X.HomotopyCategory ‚•§ C) {x y : X _‚¶ã0‚¶å‚ÇÇ} (e : Edge x y) :
    (homToNerveMk F).app _ e.edge =
      ComposableArrows.mk‚ÇÅ (F.map (homMk e)) := by
  rw [homToNerveMk_app_one]
  exact ComposableArrows.arrowEquiv.injective
    (congr_arg F.mapArrow.obj (congr_arrowMk_homMk (Edge.mk' e.edge) e rfl))

/-- Given a `2`-truncated simplicial set `X` and a category `C`,
this is the bijection between morphism `X.HomotopyCategory ‚•§ C`
and `X ‚ü∂ (truncation 2).obj (nerve C)` which is part of the adjunction
`SSet.Truncated.nerve‚ÇÇAdj`. -/
def functorEquiv :
    (X.HomotopyCategory ‚•§ C) ‚âÉ (X ‚ü∂ (truncation 2).obj (nerve C)) where
  toFun := homToNerveMk
  invFun := descOfTruncation
  left_inv F :=
    functor_ext (fun x ‚Ü¶ by simp) (fun x y f ‚Ü¶ by
      dsimp
      simp only [Category.comp_id, Category.id_comp, descOfTruncation_map_homMk,
        homToNerveMk_app_zero]
      exact nerve.homEquiv.symm.injective (Edge.ext (by cat_disch)))
  right_inv œÜ :=
    IsStrictSegal.hom_ext (fun s ‚Ü¶ by
      obtain ‚ü®x‚ÇÄ, x‚ÇÅ, f, rfl‚ü© := Edge.exists_of_simplex s
      dsimp [nerve.homEquiv]
      simp only [homToNerveMk_app_edge, descOfTruncation_obj_mk,
        descOfTruncation_map_homMk]
      refine ComposableArrows.ext‚ÇÅ ?_ ?_ rfl
      ¬∑ dsimp [nerveEquiv, ComposableArrows.right]
        simp only [‚Üê f.src_eq, FunctorToTypes.naturality]
        rfl
      ¬∑ dsimp [nerveEquiv, ComposableArrows.right]
        simp only [‚Üê f.tgt_eq, FunctorToTypes.naturality]
        rfl)

@[reassoc]
lemma homToNerveMk_comp {D : Type u} [SmallCategory D]
    (F : X.HomotopyCategory ‚•§ C) (G : C ‚•§ D) :
    homToNerveMk (F ‚ãô G) = homToNerveMk F ‚â´ (truncation 2).map (nerveMap G) :=
  IsStrictSegal.hom_ext (fun s ‚Ü¶ by
    obtain ‚ü®x‚ÇÄ, x‚ÇÅ, f, rfl‚ü© := Edge.exists_of_simplex s
    dsimp
    simp only [homToNerveMk_app_edge, Functor.comp_obj, Functor.comp_map]
    exact ComposableArrows.ext‚ÇÅ rfl rfl (by aesop))

end HomotopyCategory

/-- The adjunction between the 2-truncated homotopy category functor
and the 2-truncated nerve functor. -/
def nerve‚ÇÇAdj : hoFunctor‚ÇÇ.{u} ‚ä£ nerveFunctor‚ÇÇ :=
  Adjunction.mkOfHomEquiv
    { homEquiv _ _ := (Cat.Hom.equivFunctor _ _).trans HomotopyCategory.functorEquiv
      homEquiv_naturality_left_symm _ _ := by ext1; exact HomotopyCategory.descOfTruncation_comp _ _
      homEquiv_naturality_right _ _ := HomotopyCategory.homToNerveMk_comp _ _ }

end Truncated

end SSet

namespace CategoryTheory

namespace nerve

variable {C D : Type u} [SmallCategory C] [SmallCategory D]

/-- The functor `C ‚•§ D` that is reconstructed for a morphism
between the `2`-truncated nerves. -/
@[simps]
def functorOfNerveMap (œÜ : nerveFunctor‚ÇÇ.obj (.of C) ‚ü∂ nerveFunctor‚ÇÇ.obj (.of D)) :
    C ‚•§ D where
  obj x := nerveEquiv (œÜ.app (op ‚ü®‚¶ã0‚¶å, by simp‚ü©) (nerveEquiv.symm x))
  map f := nerve.homEquiv ((nerve.edgeMk f).toTruncated.map œÜ)
  map_id x := by
    rw [edgeMk_id, SSet.Edge.toTruncated_id, SSet.Truncated.Edge.map_id]
    exact nerve.homEquiv_id _
  map_comp f g := by
    obtain ‚ü®h‚ü© := (nerve.nonempty_compStruct_iff f g (f ‚â´ g)).2 rfl
    exact (nerve.homEquiv_comp (h.toTruncated.map œÜ)).symm

lemma nerveFunctor‚ÇÇ_map_functorOfNerveMap
    (œÜ : nerveFunctor‚ÇÇ.obj (.of C) ‚ü∂ nerveFunctor‚ÇÇ.obj (.of D)) :
    nerveFunctor‚ÇÇ.map (functorOfNerveMap œÜ).toCatHom = œÜ :=
  SSet.Truncated.IsStrictSegal.hom_ext (fun f ‚Ü¶ by
    obtain ‚ü®x, y, f, rfl‚ü© := ComposableArrows.mk‚ÇÅ_surjective f
    exact (nerveMap_app_mk‚ÇÅ _ _).trans ((nerve.mk‚ÇÅ_homEquiv_apply _).trans
      (ComposableArrows.mk‚ÇÅ_hom _)))

lemma functorOfNerveMap_nerveFunctor‚ÇÇ_map (F : C ‚•§ D) :
    functorOfNerveMap ((SSet.truncation 2).map (nerveMap F)) = F :=
  Functor.ext (fun x ‚Ü¶ by cat_disch) (fun x y f ‚Ü¶ by cat_disch)

/-- The `2`-truncated nerve functor is fully faithful. -/
def fullyFaithfulNerveFunctor‚ÇÇ : nerveFunctor‚ÇÇ.{u, u}.FullyFaithful where
  preimage œÜ := (functorOfNerveMap œÜ).toCatHom
  map_preimage _ := nerveFunctor‚ÇÇ_map_functorOfNerveMap _
  preimage_map _ := by ext1; exact functorOfNerveMap_nerveFunctor‚ÇÇ_map _

instance : nerveFunctor‚ÇÇ.{u, u}.Faithful :=
  (fullyFaithfulNerveFunctor‚ÇÇ).faithful

instance : nerveFunctor‚ÇÇ.{u, u}.Full :=
  (fullyFaithfulNerveFunctor‚ÇÇ).full

instance : Reflective nerveFunctor‚ÇÇ.{u, u} := Reflective.mk _ SSet.Truncated.nerve‚ÇÇAdj

end nerve

open SSet

/-- The adjunction between the nerve functor and the homotopy category functor is, up to
isomorphism, the composite of the adjunctions `SSet.coskAdj 2` and `nerve‚ÇÇAdj`. -/
noncomputable def nerveAdjunction : hoFunctor ‚ä£ nerveFunctor :=
  Adjunction.ofNatIsoRight ((SSet.coskAdj 2).comp Truncated.nerve‚ÇÇAdj) Nerve.cosk‚ÇÇIso.symm


/-- Repleteness exists for full and faithful functors but not fully faithful functors, which is
why we do this inefficiently. -/
instance nerveFunctor.faithful : nerveFunctor.{u, u}.Faithful :=
  Functor.Faithful.of_iso Nerve.cosk‚ÇÇIso.symm

instance nerveFunctor.full : nerveFunctor.{u, u}.Full :=
  Functor.Full.of_iso Nerve.cosk‚ÇÇIso.symm

/-- The nerve functor is both full and faithful and thus is fully faithful. -/
noncomputable def nerveFunctor.fullyfaithful : nerveFunctor.FullyFaithful :=
  Functor.FullyFaithful.ofFullyFaithful _

instance nerveAdjunction.isIso_counit : IsIso nerveAdjunction.counit :=
  Adjunction.counit_isIso_of_R_fully_faithful _

/-- The counit map of `nerveAdjunction` is an isomorphism since the nerve functor is fully
faithful. -/
noncomputable def nerveFunctorCompHoFunctorIso : nerveFunctor.{u, u} ‚ãô hoFunctor ‚âÖ ùü≠ Cat :=
  asIso (nerveAdjunction.counit)

noncomputable instance : Reflective nerveFunctor where
  L := hoFunctor
  adj := nerveAdjunction

section

instance (C D : Type u) [Category.{u} C] [Category.{u} D] :
    IsIso (prodComparison (nerveFunctor ‚ãô hoFunctor ‚ãô nerveFunctor)
      (Cat.of C) (Cat.of D)) := by
  let iso : nerveFunctor ‚ãô hoFunctor ‚ãô nerveFunctor ‚âÖ nerveFunctor :=
    (nerveFunctor.associator hoFunctor nerveFunctor).symm ‚â™‚â´
      Functor.isoWhiskerRight nerveFunctorCompHoFunctorIso nerveFunctor ‚â™‚â´
        nerveFunctor.leftUnitor
  exact IsIso.of_isIso_fac_right (prodComparison_natural_of_natTrans iso.hom).symm

namespace hoFunctor

instance : hoFunctor.IsLeftAdjoint := nerveAdjunction.isLeftAdjoint

instance (C D : Type u) [Category.{u} C] [Category.{u} D] :
    IsIso (prodComparison hoFunctor (nerve C) (nerve D)) := by
  have : IsIso (nerveFunctor.map (prodComparison hoFunctor (nerve C) (nerve D))) := by
    have : IsIso (prodComparison (hoFunctor ‚ãô nerveFunctor) (nerve C) (nerve D)) :=
      IsIso.of_isIso_fac_left
        (prodComparison_comp nerveFunctor (hoFunctor ‚ãô nerveFunctor)
          (A := Cat.of C) (B := Cat.of D)).symm
    exact IsIso.of_isIso_fac_right (prodComparison_comp hoFunctor nerveFunctor).symm
  exact isIso_of_fully_faithful nerveFunctor _

instance isIso_prodComparison_stdSimplex.{w} (n m : ‚Ñï) :
    IsIso (prodComparison hoFunctor (Œî[n] : SSet.{w}) Œî[m]) :=
  IsIso.of_isIso_fac_right (prodComparison_natural
    hoFunctor (stdSimplex.isoNerve n).hom (stdSimplex.isoNerve m).hom).symm

lemma isIso_prodComparison_of_stdSimplex {D : SSet.{u}} (X : SSet.{u})
    (H : ‚àÄ m, IsIso (prodComparison hoFunctor D Œî[m])) :
    IsIso (prodComparison hoFunctor D X) := by
  have : IsIso (Functor.whiskerLeft (CostructuredArrow.proj uliftYoneda X ‚ãô uliftYoneda)
      (prodComparisonNatTrans hoFunctor.{u} D)) := by
    rw [NatTrans.isIso_iff_isIso_app]
    exact fun x ‚Ü¶ H (x.left).len
  exact isIso_app_coconePt_of_preservesColimit _ (prodComparisonNatTrans hoFunctor _) _
    (Presheaf.isColimitTautologicalCocone' X)

instance isIso_prodComparison (X Y : SSet) :
    IsIso (prodComparison hoFunctor X Y) := isIso_prodComparison_of_stdSimplex _ fun m ‚Ü¶ by
  convert_to IsIso (hoFunctor.map (prod.braiding _ _).hom ‚â´
    prodComparison hoFunctor Œî[m] X ‚â´ (prod.braiding _ _).hom)
  ¬∑ ext <;> simp [‚Üê Functor.map_comp]
  suffices IsIso (prodComparison hoFunctor Œî[m] X) by infer_instance
  exact isIso_prodComparison_of_stdSimplex _ (isIso_prodComparison_stdSimplex _)

/-- The functor `hoFunctor : SSet ‚•§ Cat` preserves binary products of simplicial sets `X` and
`Y`. -/
instance preservesBinaryProduct (X Y : SSet) :
    PreservesLimit (pair X Y) hoFunctor :=
  PreservesLimitPair.of_iso_prod_comparison hoFunctor X Y

/-- The functor `hoFunctor : SSet ‚•§ Cat` preserves limits of functors out of
`Discrete WalkingPair`. -/
instance preservesBinaryProducts :
    PreservesLimitsOfShape (Discrete WalkingPair) hoFunctor where
  preservesLimit {F} := preservesLimit_of_iso_diagram hoFunctor (diagramIsoPair F).symm

/-- The functor `hoFunctor : SSet ‚•§ Cat` preserves finite products of simplicial sets. -/
instance preservesFiniteProducts : PreservesFiniteProducts hoFunctor :=
  PreservesFiniteProducts.of_preserves_binary_and_terminal _

/-- The homotopy category functor `hoFunctor : SSet.{u} ‚•§ Cat.{u, u}` is (cartesian) monoidal. -/
noncomputable instance Monoidal : hoFunctor.Monoidal :=
  .ofChosenFiniteProducts hoFunctor

open MonoidalCategory

/-- An equivalence between the vertices of a simplicial set `X` and the
objects of `hoFunctor.obj X`. -/
def unitHomEquiv (X : SSet.{u}) :
    (ùüô_ SSet ‚ü∂ X) ‚âÉ Cat.chosenTerminal ‚•§ hoFunctor.obj X :=
  (SSet.unitHomEquiv X).trans <|
    (hoFunctor.obj.equiv.{u} X).symm.trans Cat.fromChosenTerminalEquiv.symm

theorem unitHomEquiv_eq (X : SSet.{u}) (x : ùüô_ SSet ‚ü∂ X) :
    hoFunctor.unitHomEquiv X x = (Functor.LaxMonoidal.Œµ hoFunctor).toFunctor ‚ãô
      (hoFunctor.map x).toFunctor := by
  simp only [Cat.of_Œ±, unitHomEquiv, Equiv.trans_apply,
    Functor.CoreMonoidal.toMonoidal_toLaxMonoidal]
  rw [Equiv.symm_apply_eq, ‚Üê Equiv.eq_symm_apply]
  rfl

end hoFunctor

end

end CategoryTheory
