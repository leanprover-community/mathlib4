/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz
-/
import Mathlib.AlgebraicTopology.SimplicialObject.Basic
import Mathlib.CategoryTheory.Limits.Types.Colimits
import Mathlib.CategoryTheory.Yoneda
import Mathlib.Tactic.FinCases

/-!
# Simplicial sets

A simplicial set is just a simplicial object in `Type`,
i.e. a `Type`-valued presheaf on the simplex category.

(One might be tempted to call these "simplicial types" when working in type-theoretic foundations,
but this would be unnecessarily confusing given the existing notion of a simplicial type in
homotopy type theory.)

-/

universe v u

open CategoryTheory CategoryTheory.Limits CategoryTheory.Functor

open Simplicial

/-- The category of simplicial sets.
This is the category of contravariant functors from
`SimplexCategory` to `Type u`. -/
def SSet : Type (u + 1) :=
  SimplicialObject (Type u)

namespace SSet

instance largeCategory : LargeCategory SSet := by
  dsimp only [SSet]
  infer_instance

instance hasLimits : HasLimits SSet := by
  dsimp only [SSet]
  infer_instance

instance hasColimits : HasColimits SSet := by
  dsimp only [SSet]
  infer_instance

@[ext]
lemma hom_ext {X Y : SSet} {f g : X ‚ü∂ Y} (w : ‚àÄ n, f.app n = g.app n) : f = g :=
  SimplicialObject.hom_ext _ _ w

@[simp]
lemma id_app (X : SSet) (n : SimplexCategory·µí·µñ) :
    NatTrans.app (ùüô X) n = ùüô _ := rfl

@[simp, reassoc]
lemma comp_app {X Y Z : SSet} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (n : SimplexCategory·µí·µñ) :
    (f ‚â´ g).app n = f.app n ‚â´ g.app n := rfl

/-- The constant map of simplicial sets `X ‚ü∂ Y` induced by a simplex `y : Y _[0]`. -/
@[simps]
def const {X Y : SSet.{u}} (y : Y _‚¶ã0‚¶å) : X ‚ü∂ Y where
  app n _ := Y.map (n.unop.const _ 0).op y
  naturality _ _ _ := by
    ext
    dsimp
    rw [‚Üê FunctorToTypes.map_comp_apply]
    rfl

@[simp]
lemma comp_const {X Y Z : SSet.{u}} (f : X ‚ü∂ Y) (z : Z _‚¶ã0‚¶å) :
    f ‚â´ const z = const z := rfl

@[simp]
lemma const_comp {X Y Z : SSet.{u}} (y : Y _‚¶ã0‚¶å) (g : Y ‚ü∂ Z) :
    const (X := X) y ‚â´ g = const (g.app _ y) := by
  ext m x
  simp [FunctorToTypes.naturality]

/-- The ulift functor `SSet.{u} ‚•§ SSet.{max u v}` on simplicial sets. -/
def uliftFunctor : SSet.{u} ‚•§ SSet.{max u v} :=
  (SimplicialObject.whiskering _ _).obj CategoryTheory.uliftFunctor.{v, u}

/-- Truncated simplicial sets. -/
def Truncated (n : ‚Ñï) :=
  SimplicialObject.Truncated (Type u) n

namespace Truncated

instance largeCategory (n : ‚Ñï) : LargeCategory (Truncated n) := by
  dsimp only [Truncated]
  infer_instance

instance hasLimits {n : ‚Ñï} : HasLimits (Truncated n) := by
  dsimp only [Truncated]
  infer_instance

instance hasColimits {n : ‚Ñï} : HasColimits (Truncated n) := by
  dsimp only [Truncated]
  infer_instance

/-- The ulift functor `SSet.Truncated.{u} ‚•§ SSet.Truncated.{max u v}` on truncated
simplicial sets. -/
def uliftFunctor (k : ‚Ñï) : SSet.Truncated.{u} k ‚•§ SSet.Truncated.{max u v} k :=
  (whiskeringRight _ _ _).obj CategoryTheory.uliftFunctor.{v, u}

@[ext]
lemma hom_ext {n : ‚Ñï} {X Y : Truncated n} {f g : X ‚ü∂ Y} (w : ‚àÄ n, f.app n = g.app n) :
    f = g :=
  NatTrans.ext (funext w)

/-- Further truncation of truncated simplicial sets. -/
abbrev trunc (n m : ‚Ñï) (h : m ‚â§ n := by omega) :
    SSet.Truncated n ‚•§ SSet.Truncated m :=
  SimplicialObject.Truncated.trunc (Type u) n m

end Truncated

/-- The truncation functor on simplicial sets. -/
abbrev truncation (n : ‚Ñï) : SSet ‚•§ SSet.Truncated n := SimplicialObject.truncation n

/-- For all `m ‚â§ n`, `truncation m` factors through `SSet.Truncated n`. -/
def truncationCompTrunc {n m : ‚Ñï} (h : m ‚â§ n) :
    truncation n ‚ãô Truncated.trunc n m ‚âÖ truncation m :=
  Iso.refl _

open SimplexCategory

noncomputable section

/-- The n-skeleton as a functor `SSet.Truncated n ‚•§ SSet`. -/
protected abbrev Truncated.sk (n : ‚Ñï) : SSet.Truncated n ‚•§ SSet.{u} :=
  SimplicialObject.Truncated.sk n

/-- The n-coskeleton as a functor `SSet.Truncated n ‚•§ SSet`. -/
protected abbrev Truncated.cosk (n : ‚Ñï) : SSet.Truncated n ‚•§ SSet.{u} :=
  SimplicialObject.Truncated.cosk n

/-- The n-skeleton as an endofunctor on `SSet`. -/
abbrev sk (n : ‚Ñï) : SSet.{u} ‚•§ SSet.{u} := SimplicialObject.sk n

/-- The n-coskeleton as an endofunctor on `SSet`. -/
abbrev cosk (n : ‚Ñï) : SSet.{u} ‚•§ SSet.{u} := SimplicialObject.cosk n

end

section adjunctions

/-- The adjunction between the n-skeleton and n-truncation. -/
noncomputable def skAdj (n : ‚Ñï) : Truncated.sk n ‚ä£ truncation.{u} n :=
  SimplicialObject.skAdj n

/-- The adjunction between n-truncation and the n-coskeleton. -/
noncomputable def coskAdj (n : ‚Ñï) : truncation.{u} n ‚ä£ Truncated.cosk n :=
  SimplicialObject.coskAdj n

namespace Truncated

instance cosk_reflective (n) : IsIso (coskAdj n).counit :=
  SimplicialObject.Truncated.cosk_reflective n

instance sk_coreflective (n) : IsIso (skAdj n).unit :=
  SimplicialObject.Truncated.sk_coreflective n

/-- Since `Truncated.inclusion` is fully faithful, so is right Kan extension along it. -/
noncomputable def cosk.fullyFaithful (n) :
    (Truncated.cosk n).FullyFaithful :=
  SimplicialObject.Truncated.cosk.fullyFaithful n

instance cosk.full (n) : (Truncated.cosk n).Full :=
  SimplicialObject.Truncated.cosk.full n

instance cosk.faithful (n) : (Truncated.cosk n).Faithful :=
  SimplicialObject.Truncated.cosk.faithful n

noncomputable instance coskAdj.reflective (n) : Reflective (Truncated.cosk n) :=
  SimplicialObject.Truncated.coskAdj.reflective n

/-- Since `Truncated.inclusion` is fully faithful, so is left Kan extension along it. -/
noncomputable def sk.fullyFaithful (n) :
    (Truncated.sk n).FullyFaithful := SimplicialObject.Truncated.sk.fullyFaithful n

instance sk.full (n) : (Truncated.sk n).Full := SimplicialObject.Truncated.sk.full n

instance sk.faithful (n) : (Truncated.sk n).Faithful :=
  SimplicialObject.Truncated.sk.faithful n

noncomputable instance skAdj.coreflective (n) : Coreflective (Truncated.sk n) :=
  SimplicialObject.Truncated.skAdj.coreflective n

end Truncated

end adjunctions

/-- The category of augmented simplicial sets, as a particular case of
augmented simplicial objects. -/
abbrev Augmented :=
  SimplicialObject.Augmented (Type u)

section applications
variable {S : SSet}

lemma Œ¥_comp_Œ¥_apply {n} {i j : Fin (n + 2)} (H : i ‚â§ j) (x : S _‚¶ãn + 2‚¶å) :
    S.Œ¥ i (S.Œ¥ j.succ x) = S.Œ¥ j (S.Œ¥ i.castSucc x) := congr_fun (S.Œ¥_comp_Œ¥ H) x

lemma Œ¥_comp_Œ¥'_apply {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j)
    (x : S _‚¶ãn + 2‚¶å) : S.Œ¥ i (S.Œ¥ j x) =
      S.Œ¥ (j.pred fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) (S.Œ¥ i.castSucc x) :=
  congr_fun (S.Œ¥_comp_Œ¥' H) x

lemma Œ¥_comp_Œ¥''_apply {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i ‚â§ Fin.castSucc j)
    (x : S _‚¶ãn + 2‚¶å) :
    S.Œ¥ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) (S.Œ¥ j.succ x) =
      S.Œ¥ j (S.Œ¥ i x) := congr_fun (S.Œ¥_comp_Œ¥'' H) x

lemma Œ¥_comp_Œ¥_self_apply {n} {i : Fin (n + 2)} (x : S _‚¶ãn + 2‚¶å) :
    S.Œ¥ i (S.Œ¥ i.castSucc x) = S.Œ¥ i (S.Œ¥ i.succ x) := congr_fun S.Œ¥_comp_Œ¥_self x

lemma Œ¥_comp_Œ¥_self'_apply {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = Fin.castSucc i)
    (x : S _‚¶ãn + 2‚¶å) : S.Œ¥ i (S.Œ¥ j x) = S.Œ¥ i (S.Œ¥ i.succ x) := congr_fun (S.Œ¥_comp_Œ¥_self' H) x

lemma Œ¥_comp_œÉ_of_le_apply {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i ‚â§ Fin.castSucc j)
    (x : S _‚¶ãn + 1‚¶å) :
    S.Œ¥ (Fin.castSucc i) (S.œÉ j.succ x) = S.œÉ j (S.Œ¥ i x) := congr_fun (S.Œ¥_comp_œÉ_of_le H) x

@[simp]
lemma Œ¥_comp_œÉ_self_apply {n} (i : Fin (n + 1)) (x : S _‚¶ãn‚¶å) : S.Œ¥ i.castSucc (S.œÉ i x) = x :=
  congr_fun S.Œ¥_comp_œÉ_self x

lemma Œ¥_comp_œÉ_self'_apply {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i)
    (x : S _‚¶ãn‚¶å) : S.Œ¥ j (S.œÉ i x) = x := congr_fun (S.Œ¥_comp_œÉ_self' H) x

@[simp]
lemma Œ¥_comp_œÉ_succ_apply {n} (i : Fin (n + 1)) (x : S _‚¶ãn‚¶å) : S.Œ¥ i.succ (S.œÉ i x) = x :=
  congr_fun S.Œ¥_comp_œÉ_succ x

lemma Œ¥_comp_œÉ_succ'_apply {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) (x : S _‚¶ãn‚¶å) :
    S.Œ¥ j (S.œÉ i x) = x := congr_fun (S.Œ¥_comp_œÉ_succ' H) x

lemma Œ¥_comp_œÉ_of_gt_apply {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i)
    (x : S _‚¶ãn + 1‚¶å) : S.Œ¥ i.succ (S.œÉ (Fin.castSucc j) x) = S.œÉ j (S.Œ¥ i x) :=
  congr_fun (S.Œ¥_comp_œÉ_of_gt H) x

lemma Œ¥_comp_œÉ_of_gt'_apply {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i)
    (x : S _‚¶ãn + 1‚¶å) : S.Œ¥ i (S.œÉ j x) =
      S.œÉ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le)))
        (S.Œ¥ (i.pred fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) x) :=
  congr_fun (S.Œ¥_comp_œÉ_of_gt' H) x

lemma œÉ_comp_œÉ_apply {n} {i j : Fin (n + 1)} (H : i ‚â§ j) (x : S _‚¶ãn‚¶å) :
    S.œÉ i.castSucc (S.œÉ j x) = S.œÉ j.succ (S.œÉ i x) := congr_fun (S.œÉ_comp_œÉ H) x

variable {T : SSet} (f : S ‚ü∂ T)

open Opposite

lemma Œ¥_naturality_apply {n : ‚Ñï} (i : Fin (n + 2)) (x : S _‚¶ãn + 1‚¶å) :
    f.app (op ‚¶ãn‚¶å) (S.Œ¥ i x) = T.Œ¥ i (f.app (op ‚¶ãn + 1‚¶å) x) := by
  change (S.Œ¥ i ‚â´ f.app (op ‚¶ãn‚¶å)) x = (f.app (op ‚¶ãn + 1‚¶å) ‚â´ T.Œ¥ i) x
  exact congr_fun (SimplicialObject.Œ¥_naturality f i) x

lemma œÉ_naturality_apply {n : ‚Ñï} (i : Fin (n + 1)) (x : S _‚¶ãn‚¶å) :
    f.app (op ‚¶ãn + 1‚¶å) (S.œÉ i x) = T.œÉ i (f.app (op ‚¶ãn‚¶å) x) := by
  change (S.œÉ i ‚â´ f.app (op ‚¶ãn + 1‚¶å)) x = (f.app (op ‚¶ãn‚¶å) ‚â´ T.œÉ i) x
  exact congr_fun (SimplicialObject.œÉ_naturality f i) x

end applications

end SSet
