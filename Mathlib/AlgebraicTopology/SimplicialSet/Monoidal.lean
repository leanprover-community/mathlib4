/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Jack McKoen
-/
import Mathlib.AlgebraicTopology.SimplicialSet.StdSimplex
import Mathlib.CategoryTheory.Closed.FunctorToTypes
import Mathlib.CategoryTheory.Monoidal.Cartesian.FunctorCategory

/-!
# The monoidal category structure on simplicial sets

This file defines an instance of chosen finite products
for the category `SSet`. It follows from the fact
the `SSet` if a category of functors to the category
of types and that the category of types have chosen
finite products. As a result, we obtain a monoidal
category structure on `SSet`.

-/

universe u

open Simplicial CategoryTheory MonoidalCategory Limits

namespace SSet

instance : CartesianMonoidalCategory SSet.{u} :=
  (inferInstance : CartesianMonoidalCategory (SimplexCategory·µí·µñ ‚•§ Type u))

instance : MonoidalClosed (SSet.{u}) :=
  inferInstanceAs (MonoidalClosed (SimplexCategory·µí·µñ ‚•§ Type u))

@[simp]
lemma leftUnitor_hom_app_apply (K : SSet.{u}) {Œî : SimplexCategory·µí·µñ} (x : (ùüô_ _ ‚äó K).obj Œî) :
    (Œª_ K).hom.app Œî x = x.2 := rfl

@[simp]
lemma leftUnitor_inv_app_apply (K : SSet.{u}) {Œî : SimplexCategory·µí·µñ} (x : K.obj Œî) :
    (Œª_ K).inv.app Œî x = ‚ü®PUnit.unit, x‚ü© := rfl

@[simp]
lemma rightUnitor_hom_app_apply (K : SSet.{u}) {Œî : SimplexCategory·µí·µñ} (x : (K ‚äó ùüô_ _).obj Œî) :
    (œÅ_ K).hom.app Œî x = x.1 := rfl

@[simp]
lemma rightUnitor_inv_app_apply (K : SSet.{u}) {Œî : SimplexCategory·µí·µñ} (x : K.obj Œî) :
    (œÅ_ K).inv.app Œî x = ‚ü®x, PUnit.unit‚ü© := rfl

@[simp]
lemma tensorHom_app_apply {K K' L L' : SSet.{u}} (f : K ‚ü∂ K') (g : L ‚ü∂ L')
    {Œî : SimplexCategory·µí·µñ} (x : (K ‚äó L).obj Œî) :
    (f ‚äó‚Çò g).app Œî x = ‚ü®f.app Œî x.1, g.app Œî x.2‚ü© := rfl

@[simp]
lemma whiskerLeft_app_apply (K : SSet.{u}) {L L' : SSet.{u}} (g : L ‚ü∂ L')
    {Œî : SimplexCategory·µí·µñ} (x : (K ‚äó L).obj Œî) :
    (K ‚óÅ g).app Œî x = ‚ü®x.1, g.app Œî x.2‚ü© := rfl

@[simp]
lemma whiskerRight_app_apply {K K' : SSet.{u}} (f : K ‚ü∂ K') (L : SSet.{u})
    {Œî : SimplexCategory·µí·µñ} (x : (K ‚äó L).obj Œî) :
    (f ‚ñ∑ L).app Œî x = ‚ü®f.app Œî x.1, x.2‚ü© := rfl

@[simp]
lemma associator_hom_app_apply (K L M : SSet.{u}) {Œî : SimplexCategory·µí·µñ}
    (x : ((K ‚äó L) ‚äó M).obj Œî) :
    (Œ±_ K L M).hom.app Œî x = ‚ü®x.1.1, x.1.2, x.2‚ü© := rfl

@[simp]
lemma associator_inv_app_apply (K L M : SSet.{u}) {Œî : SimplexCategory·µí·µñ}
    (x : (K ‚äó L ‚äó M).obj Œî) :
    (Œ±_ K L M).inv.app Œî x = ‚ü®‚ü®x.1, x.2.1‚ü©, x.2.2‚ü© := rfl

/-- The bijection `(ùüô_ SSet ‚ü∂ K) ‚âÉ K _‚¶ã0‚¶å`. -/
def unitHomEquiv (K : SSet.{u}) : (ùüô_ _ ‚ü∂ K) ‚âÉ K _‚¶ã0‚¶å where
  toFun œÜ := œÜ.app _ PUnit.unit
  invFun x :=
    { app := fun Œî _ => K.map (SimplexCategory.const Œî.unop ‚¶ã0‚¶å 0).op x
      naturality := fun Œî Œî' f => by
        ext ‚ü®‚ü©
        dsimp
        rw [‚Üê FunctorToTypes.map_comp_apply]
        rfl }
  left_inv œÜ := by
    ext Œî ‚ü®‚ü©
    dsimp
    rw [‚Üê FunctorToTypes.naturality]
    rfl
  right_inv x := by simp

/-- The object `Œî[0]` is terminal in `SSet`. -/
def stdSimplex.isTerminalObj‚ÇÄ : IsTerminal (Œî[0] : SSet.{u}) :=
  IsTerminal.ofUniqueHom (fun _ ‚Ü¶ SSet.const (obj‚ÇÄEquiv.symm 0))
    (fun _ _ ‚Ü¶ by
      ext ‚ü®n‚ü©
      exact objEquiv.injective (by ext; simp))

@[ext]
lemma stdSimplex.ext‚ÇÄ {X : SSet.{u}} {f g : X ‚ü∂ Œî[0]} : f = g :=
  isTerminalObj‚ÇÄ.hom_ext _ _

end SSet
