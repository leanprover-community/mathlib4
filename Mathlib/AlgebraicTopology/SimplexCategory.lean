/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison, Adam Topaz
-/
import Mathlib.Tactic.Linarith
import Mathlib.CategoryTheory.Skeletal
import Mathlib.Data.Fintype.Sort
import Mathlib.Order.Category.NonemptyFinLinOrd
import Mathlib.CategoryTheory.Functor.ReflectsIso

#align_import algebraic_topology.simplex_category from "leanprover-community/mathlib"@"e8ac6315bcfcbaf2d19a046719c3b553206dac75"

/-! # The simplex category

We construct a skeletal model of the simplex category, with objects `â„•` and the
morphism `n âŸ¶ m` being the monotone maps from `Fin (n+1)` to `Fin (m+1)`.

We show that this category is equivalent to `NonemptyFinLinOrd`.

## Remarks

The definitions `SimplexCategory` and `SimplexCategory.Hom` are marked as irreducible.

We provide the following functions to work with these objects:
1. `SimplexCategory.mk` creates an object of `SimplexCategory` out of a natural number.
  Use the notation `[n]` in the `Simplicial` locale.
2. `SimplexCategory.len` gives the "length" of an object of `SimplexCategory`, as a natural.
3. `SimplexCategory.Hom.mk` makes a morphism out of a monotone map between `Fin`'s.
4. `SimplexCategory.Hom.toOrderHom` gives the underlying monotone map associated to a
  term of `SimplexCategory.Hom`.

-/


universe v

open CategoryTheory CategoryTheory.Limits

/-- The simplex category:
* objects are natural numbers `n : â„•`
* morphisms from `n` to `m` are monotone functions `Fin (n+1) â†’ Fin (m+1)`
-/
def SimplexCategory :=
  â„•

namespace SimplexCategory

section


-- porting note: the definition of `SimplexCategory` is made irreducible below
/-- Interpret a natural number as an object of the simplex category. -/
def mk (n : â„•) : SimplexCategory :=
  n

/-- the `n`-dimensional simplex can be denoted `[n]` -/
scoped[Simplicial] notation "[" n "]" => SimplexCategory.mk n

-- TODO: Make `len` irreducible.
/-- The length of an object of `SimplexCategory`. -/
def len (n : SimplexCategory) : â„• :=
  n

@[ext]
theorem ext (a b : SimplexCategory) : a.len = b.len â†’ a = b :=
  id

attribute [irreducible] SimplexCategory

open Simplicial

@[simp]
theorem len_mk (n : â„•) : [n].len = n :=
  rfl

@[simp]
theorem mk_len (n : SimplexCategory) : ([n.len] : SimplexCategory) = n :=
  rfl

/-- A recursor for `SimplexCategory`. Use it as `induction Î” using SimplexCategory.rec`. -/
protected def rec {F : âˆ€ _ : SimplexCategory, Sort*} (h : âˆ€ n : â„•, F [n]) : âˆ€ X, F X := fun n =>
  h n.len

-- porting note: removed @[nolint has_nonempty_instance]
/-- Morphisms in the `SimplexCategory`. -/
protected def Hom (a b : SimplexCategory) :=
  Fin (a.len + 1) â†’o Fin (b.len + 1)

namespace Hom

/-- Make a moprhism in `SimplexCategory` from a monotone map of `Fin`'s. -/
def mk {a b : SimplexCategory} (f : Fin (a.len + 1) â†’o Fin (b.len + 1)) : SimplexCategory.Hom a b :=
  f

/-- Recover the monotone map from a morphism in the simplex category. -/
def toOrderHom {a b : SimplexCategory} (f : SimplexCategory.Hom a b) :
    Fin (a.len + 1) â†’o Fin (b.len + 1) :=
  f

theorem ext' {a b : SimplexCategory} (f g : SimplexCategory.Hom a b) :
    f.toOrderHom = g.toOrderHom â†’ f = g :=
  id

attribute [irreducible] SimplexCategory.Hom

@[simp]
theorem mk_toOrderHom {a b : SimplexCategory} (f : SimplexCategory.Hom a b) : mk f.toOrderHom = f :=
  rfl

@[simp]
theorem toOrderHom_mk {a b : SimplexCategory} (f : Fin (a.len + 1) â†’o Fin (b.len + 1)) :
    (mk f).toOrderHom = f :=
  rfl

theorem mk_toOrderHom_apply {a b : SimplexCategory} (f : Fin (a.len + 1) â†’o Fin (b.len + 1))
    (i : Fin (a.len + 1)) : (mk f).toOrderHom i = f i :=
  rfl

/-- Identity morphisms of `SimplexCategory`. -/
@[simp]
def id (a : SimplexCategory) : SimplexCategory.Hom a a :=
  mk OrderHom.id

/-- Composition of morphisms of `SimplexCategory`. -/
@[simp]
def comp {a b c : SimplexCategory} (f : SimplexCategory.Hom b c) (g : SimplexCategory.Hom a b) :
    SimplexCategory.Hom a c :=
  mk <| f.toOrderHom.comp g.toOrderHom

end Hom

@[simps]
instance smallCategory : SmallCategory.{0} SimplexCategory where
  Hom n m := SimplexCategory.Hom n m
  id m := SimplexCategory.Hom.id _
  comp f g := SimplexCategory.Hom.comp g f

-- porting note: added because `Hom.ext'` is not triggered automatically
@[ext]
theorem Hom.ext {a b : SimplexCategory} (f g : a âŸ¶ b) :
    f.toOrderHom = g.toOrderHom â†’ f = g :=
  Hom.ext' _ _

/-- The constant morphism from [0]. -/
def const (x : SimplexCategory) (i : Fin (x.len + 1)) : ([0] : SimplexCategory) âŸ¶ x :=
  Hom.mk <| âŸ¨fun _ => i, by tautoâŸ©

-- porting note: removed @[simp] as the linter complains
theorem const_comp (x y : SimplexCategory) (i : Fin (x.len + 1)) (f : x âŸ¶ y) :
    const x i â‰« f = const y (f.toOrderHom i) :=
  rfl

/-- Make a morphism `[n] âŸ¶ [m]` from a monotone map between fin's.
This is useful for constructing morphisms between `[n]` directly
without identifying `n` with `[n].len`.
-/
@[simp]
def mkHom {n m : â„•} (f : Fin (n + 1) â†’o Fin (m + 1)) : ([n] : SimplexCategory) âŸ¶ [m] :=
  SimplexCategory.Hom.mk f

theorem hom_zero_zero (f : ([0] : SimplexCategory) âŸ¶ [0]) : f = ğŸ™ _ := by
  ext : 3
  apply @Subsingleton.elim (Fin 1)

end

open Simplicial

section Generators

/-!
## Generating maps for the simplex category

TODO: prove that the simplex category is equivalent to
one given by the following generators and relations.
-/


/-- The `i`-th face map from `[n]` to `[n+1]` -/
def Î´ {n} (i : Fin (n + 2)) : ([n] : SimplexCategory) âŸ¶ [n + 1] :=
  mkHom (Fin.succAboveEmb i).toOrderHom

/-- The `i`-th degeneracy map from `[n+1]` to `[n]` -/
def Ïƒ {n} (i : Fin (n + 1)) : ([n + 1] : SimplexCategory) âŸ¶ [n] :=
  mkHom
    { toFun := Fin.predAbove i
      monotone' := Fin.predAbove_right_monotone i }

/-- The generic case of the first simplicial identity -/
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    Î´ i â‰« Î´ j.succ = Î´ j â‰« Î´ (Fin.castSucc i) := by
  ext k
  dsimp [Î´, Fin.succAbove]
  rcases i with âŸ¨i, _âŸ©
  rcases j with âŸ¨j, _âŸ©
  rcases k with âŸ¨k, _âŸ©
  split_ifs <;> Â· simp at * <;> linarith

theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    Î´ i â‰« Î´ j =
      Î´ (j.pred <| fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) â‰«
        Î´ (Fin.castSucc i) := by
  rw [â† Î´_comp_Î´]
  Â· rw [Fin.succ_pred]
  Â· simpa only [Fin.le_iff_val_le_val, â† Nat.lt_succ_iff, Nat.succ_eq_add_one, â† Fin.val_succ,
      j.succ_pred, Fin.lt_iff_val_lt_val] using H

theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) â‰« Î´ j.succ =
      Î´ j â‰« Î´ i := by
  rw [Î´_comp_Î´]
  Â· rfl
  Â· exact H

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} : Î´ i â‰« Î´ (Fin.castSucc i) = Î´ i â‰« Î´ i.succ :=
  (Î´_comp_Î´ (le_refl i)).symm

@[reassoc]
theorem Î´_comp_Î´_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = Fin.castSucc i) :
    Î´ i â‰« Î´ j = Î´ i â‰« Î´ i.succ := by
  subst H
  rw [Î´_comp_Î´_self]

/-- The second simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    Î´ (Fin.castSucc i) â‰« Ïƒ j.succ = Ïƒ j â‰« Î´ i := by
  rcases i with âŸ¨i, hiâŸ©
  rcases j with âŸ¨j, hjâŸ©
  ext âŸ¨k, hkâŸ©
  simp at H hk
  dsimp [Ïƒ, Î´, Fin.predAbove, Fin.succAbove]
  simp only [Fin.lt_iff_val_lt_val, Fin.dite_val, Fin.ite_val, Fin.coe_pred, ge_iff_le,
    Fin.coe_castLT, dite_eq_ite, Fin.coe_castSucc, Fin.val_succ]
  split_ifs
  all_goals try simp <;> linarith
  all_goals cases k <;> simp at * <;> linarith

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} :
    Î´ (Fin.castSucc i) â‰« Ïƒ i = ğŸ™ ([n] : SimplexCategory) := by
  rcases i with âŸ¨i, hiâŸ©
  ext âŸ¨j, hjâŸ©
  simp at hj
  dsimp [Ïƒ, Î´, Fin.predAbove, Fin.succAbove]
  simp only [Fin.lt_iff_val_lt_val, Fin.dite_val, Fin.ite_val, Fin.coe_pred, ge_iff_le,
    Fin.coe_castLT, dite_eq_ite]
  split_ifs
  any_goals simp
  all_goals linarith

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    Î´ j â‰« Ïƒ i = ğŸ™ ([n] : SimplexCategory) := by
  subst H
  rw [Î´_comp_Ïƒ_self]

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : Î´ i.succ â‰« Ïƒ i = ğŸ™ ([n] : SimplexCategory) := by
  ext j
  rcases i with âŸ¨i, _âŸ©
  rcases j with âŸ¨j, _âŸ©
  dsimp [Î´, Ïƒ, Fin.succAbove, Fin.predAbove]
  split_ifs <;> simp <;> simp at * <;> linarith

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} (j : Fin (n + 2)) (i : Fin (n + 1)) (H : j = i.succ) :
    Î´ j â‰« Ïƒ i = ğŸ™ ([n] : SimplexCategory) := by
  subst H
  rw [Î´_comp_Ïƒ_succ]

/-- The fourth simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    Î´ i.succ â‰« Ïƒ (Fin.castSucc j) = Ïƒ j â‰« Î´ i := by
  ext âŸ¨k, hkâŸ©
  rcases i with âŸ¨i, hiâŸ©
  rcases j with âŸ¨j, hjâŸ©
  simp at H hk
  dsimp [Î´, Ïƒ, Fin.predAbove, Fin.succAbove]
  simp only [Fin.lt_iff_val_lt_val, Fin.dite_val, Fin.ite_val, Fin.coe_pred, ge_iff_le,
    Fin.coe_castLT, dite_eq_ite, Fin.coe_castSucc, Fin.val_succ]
  split_ifs
  all_goals try simp <;> linarith
  all_goals cases k <;> simp at * <;> linarith

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    Î´ i â‰« Ïƒ j = Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) â‰«
      Î´ (i.pred <| fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
  rw [â† Î´_comp_Ïƒ_of_gt]
  Â· simp
  Â· rw [Fin.castSucc_castLT, â† Fin.succ_lt_succ_iff, Fin.succ_pred]
    exact H

/-- The fifth simplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    Ïƒ (Fin.castSucc i) â‰« Ïƒ j = Ïƒ j.succ â‰« Ïƒ i := by
  ext âŸ¨k, hkâŸ©
  rcases i with âŸ¨i, hiâŸ©
  rcases j with âŸ¨j, hjâŸ©
  simp at H hk
  dsimp [Ïƒ, Fin.predAbove]
  simp only [Fin.lt_iff_val_lt_val, Fin.ite_val, Fin.coe_pred, ge_iff_le, dite_eq_ite,
    Fin.coe_castLT]
  split_ifs
  all_goals try linarith
  all_goals cases k <;> simp at *; linarith

end Generators

section Skeleton

/-- The functor that exhibits `SimplexCategory` as skeleton
of `NonemptyFinLinOrd` -/
@[simps obj map]
def skeletalFunctor : SimplexCategory â¥¤ NonemptyFinLinOrd where
  obj a := NonemptyFinLinOrd.of (Fin (a.len + 1))
  map f := f.toOrderHom

theorem skeletalFunctor.coe_map {Î”â‚ Î”â‚‚ : SimplexCategory} (f : Î”â‚ âŸ¶ Î”â‚‚) :
    â†‘(skeletalFunctor.map f) = f.toOrderHom :=
  rfl

theorem skeletal : Skeletal SimplexCategory := fun X Y âŸ¨IâŸ© => by
  suffices Fintype.card (Fin (X.len + 1)) = Fintype.card (Fin (Y.len + 1)) by
    ext
    simpa
  apply Fintype.card_congr
  exact ((skeletalFunctor â‹™ forget NonemptyFinLinOrd).mapIso I).toEquiv

namespace SkeletalFunctor

instance : Full skeletalFunctor where
  preimage f := SimplexCategory.Hom.mk f

instance : Faithful skeletalFunctor where
  map_injective {_ _ f g} h := by
    ext1
    exact h

instance : EssSurj skeletalFunctor where
  mem_essImage X :=
    âŸ¨mk (Fintype.card X - 1 : â„•),
      âŸ¨by
        have aux : Fintype.card X = Fintype.card X - 1 + 1 :=
          (Nat.succ_pred_eq_of_pos <| Fintype.card_pos_iff.mpr âŸ¨âŠ¥âŸ©).symm
        let f := monoEquivOfFin X aux
        have hf := (Finset.univ.orderEmbOfFin aux).strictMono
        refine'
          { hom := âŸ¨f, hf.monotoneâŸ©
            inv := âŸ¨f.symm, _âŸ©
            hom_inv_id := by ext1; apply f.symm_apply_apply
            inv_hom_id := by ext1; apply f.apply_symm_apply }
        intro i j h
        show f.symm i â‰¤ f.symm j
        rw [â† hf.le_iff_le]
        show f (f.symm i) â‰¤ f (f.symm j)
        simpa only [OrderIso.apply_symm_apply]âŸ©âŸ©

noncomputable instance isEquivalence : IsEquivalence skeletalFunctor :=
  Equivalence.ofFullyFaithfullyEssSurj skeletalFunctor

end SkeletalFunctor

/-- The equivalence that exhibits `SimplexCategory` as skeleton
of `NonemptyFinLinOrd` -/
noncomputable def skeletalEquivalence : SimplexCategory â‰Œ NonemptyFinLinOrd :=
  Functor.asEquivalence skeletalFunctor

end Skeleton

/-- `SimplexCategory` is a skeleton of `NonemptyFinLinOrd`.
-/
noncomputable def isSkeletonOf :
    IsSkeletonOf NonemptyFinLinOrd SimplexCategory skeletalFunctor where
  skel := skeletal
  eqv := SkeletalFunctor.isEquivalence

/-- The truncated simplex category. -/
def Truncated (n : â„•) :=
  FullSubcategory fun a : SimplexCategory => a.len â‰¤ n

instance (n : â„•) : SmallCategory.{0} (Truncated n) :=
  FullSubcategory.category _

namespace Truncated

instance {n} : Inhabited (Truncated n) :=
  âŸ¨âŸ¨[0], by simpâŸ©âŸ©

/-- The fully faithful inclusion of the truncated simplex category into the usual
simplex category.
-/
def inclusion {n : â„•} : SimplexCategory.Truncated n â¥¤ SimplexCategory :=
  fullSubcategoryInclusion _

instance (n : â„•) : Full (inclusion : Truncated n â¥¤ _) := FullSubcategory.full _
instance (n : â„•) : Faithful (inclusion : Truncated n â¥¤ _) := FullSubcategory.faithful _

end Truncated

section Concrete

instance : ConcreteCategory.{0} SimplexCategory where
  forget :=
    { obj := fun i => Fin (i.len + 1)
      map := fun f => f.toOrderHom }
  forget_faithful := âŸ¨fun h => by ext : 2; exact hâŸ©

end Concrete

section EpiMono

/-- A morphism in `SimplexCategory` is a monomorphism precisely when it is an injective function
-/
theorem mono_iff_injective {n m : SimplexCategory} {f : n âŸ¶ m} :
    Mono f â†” Function.Injective f.toOrderHom := by
  rw [â† Functor.mono_map_iff_mono skeletalEquivalence.functor]
  dsimp only [skeletalEquivalence, Functor.asEquivalence_functor]
  simp only [skeletalFunctor_obj, skeletalFunctor_map,
    NonemptyFinLinOrd.mono_iff_injective, NonemptyFinLinOrd.coe_of]

/-- A morphism in `SimplexCategory` is an epimorphism if and only if it is a surjective function
-/
theorem epi_iff_surjective {n m : SimplexCategory} {f : n âŸ¶ m} :
    Epi f â†” Function.Surjective f.toOrderHom := by
  rw [â† Functor.epi_map_iff_epi skeletalEquivalence.functor]
  dsimp only [skeletalEquivalence, Functor.asEquivalence_functor]
  simp only [skeletalFunctor_obj, skeletalFunctor_map,
    NonemptyFinLinOrd.epi_iff_surjective, NonemptyFinLinOrd.coe_of]

/-- A monomorphism in `SimplexCategory` must increase lengths-/
theorem len_le_of_mono {x y : SimplexCategory} {f : x âŸ¶ y} : Mono f â†’ x.len â‰¤ y.len := by
  intro hyp_f_mono
  have f_inj : Function.Injective f.toOrderHom.toFun := mono_iff_injective.1 hyp_f_mono
  simpa using Fintype.card_le_of_injective f.toOrderHom.toFun f_inj

theorem le_of_mono {n m : â„•} {f : ([n] : SimplexCategory) âŸ¶ [m]} : CategoryTheory.Mono f â†’ n â‰¤ m :=
  len_le_of_mono

/-- An epimorphism in `SimplexCategory` must decrease lengths-/
theorem len_le_of_epi {x y : SimplexCategory} {f : x âŸ¶ y} : Epi f â†’ y.len â‰¤ x.len := by
  intro hyp_f_epi
  have f_surj : Function.Surjective f.toOrderHom.toFun := epi_iff_surjective.1 hyp_f_epi
  simpa using Fintype.card_le_of_surjective f.toOrderHom.toFun f_surj

theorem le_of_epi {n m : â„•} {f : ([n] : SimplexCategory) âŸ¶ [m]} : Epi f â†’ m â‰¤ n :=
  len_le_of_epi

instance {n : â„•} {i : Fin (n + 2)} : Mono (Î´ i) := by
  rw [mono_iff_injective]
  exact Fin.succAbove_right_injective

instance {n : â„•} {i : Fin (n + 1)} : Epi (Ïƒ i) := by
  rw [epi_iff_surjective]
  intro b
  simp only [Ïƒ, mkHom, Hom.toOrderHom_mk, OrderHom.coe_mk]
  by_cases b â‰¤ i
  Â· use b
    -- This was not needed before leanprover/lean4#2644
    dsimp
    rw [Fin.predAbove_below i b (by simpa only [Fin.coe_eq_castSucc] using h)]
    simp only [len_mk, Fin.coe_eq_castSucc, Fin.castPred_castSucc]
  Â· use b.succ
    -- This was not needed before leanprover/lean4#2644
    dsimp
    rw [Fin.predAbove_above i b.succ _, Fin.pred_succ]
    rw [not_le] at h
    rw [Fin.lt_iff_val_lt_val] at h âŠ¢
    simpa only [Fin.val_succ, Fin.coe_castSucc] using Nat.lt.step h

instance : ReflectsIsomorphisms (forget SimplexCategory) :=
  âŸ¨fun f hf =>
    IsIso.of_iso
      { hom := f
        inv := Hom.mk
            { toFun := inv ((forget SimplexCategory).map f)
              monotone' := fun yâ‚ yâ‚‚ h => by
                by_cases h' : yâ‚ < yâ‚‚
                Â· by_contra h''
                  apply not_le.mpr h'
                  convert f.toOrderHom.monotone (le_of_not_ge h'')
                  all_goals
                    exact (congr_hom (Iso.inv_hom_id
                      (asIso ((forget SimplexCategory).map f))) _).symm
                Â· rw [eq_of_le_of_not_lt h h'] }
        hom_inv_id := by
          ext1
          ext1
          exact Iso.hom_inv_id (asIso ((forget _).map f))
        inv_hom_id := by
          ext1
          ext1
          exact Iso.inv_hom_id (asIso ((forget _).map f)) }âŸ©

theorem isIso_of_bijective {x y : SimplexCategory} {f : x âŸ¶ y}
    (hf : Function.Bijective f.toOrderHom.toFun) : IsIso f :=
  haveI : IsIso ((forget SimplexCategory).map f) := (isIso_iff_bijective _).mpr hf
  isIso_of_reflects_iso f (forget SimplexCategory)

/-- An isomorphism in `SimplexCategory` induces an `OrderIso`. -/
@[simp]
def orderIsoOfIso {x y : SimplexCategory} (e : x â‰… y) : Fin (x.len + 1) â‰ƒo Fin (y.len + 1) :=
  Equiv.toOrderIso
    { toFun := e.hom.toOrderHom
      invFun := e.inv.toOrderHom
      left_inv := fun i => by
        simpa only using congr_arg (fun Ï† => (Hom.toOrderHom Ï†) i) e.hom_inv_id
      right_inv := fun i => by
        simpa only using congr_arg (fun Ï† => (Hom.toOrderHom Ï†) i) e.inv_hom_id }
    e.hom.toOrderHom.monotone e.inv.toOrderHom.monotone

theorem iso_eq_iso_refl {x : SimplexCategory} (e : x â‰… x) : e = Iso.refl x := by
  have h : (Finset.univ : Finset (Fin (x.len + 1))).card = x.len + 1 := Finset.card_fin (x.len + 1)
  have eqâ‚ := Finset.orderEmbOfFin_unique' h fun i => Finset.mem_univ ((orderIsoOfIso e) i)
  have eqâ‚‚ :=
    Finset.orderEmbOfFin_unique' h fun i => Finset.mem_univ ((orderIsoOfIso (Iso.refl x)) i)
  -- Porting note: the proof was rewritten from this point in #3414 (reenableeta)
  -- It could be investigated again to see if the original can be restored.
  ext x
  replace eqâ‚ := congr_arg (Â· x) eqâ‚
  replace eqâ‚‚ := congr_arg (Â· x) eqâ‚‚.symm
  simp_all

theorem eq_id_of_isIso {x : SimplexCategory} (f : x âŸ¶ x) [IsIso f] : f = ğŸ™ _ :=
  congr_arg (fun Ï† : _ â‰… _ => Ï†.hom) (iso_eq_iso_refl (asIso f))

theorem eq_Ïƒ_comp_of_not_injective' {n : â„•} {Î”' : SimplexCategory} (Î¸ : mk (n + 1) âŸ¶ Î”')
    (i : Fin (n + 1)) (hi : Î¸.toOrderHom (Fin.castSucc i) = Î¸.toOrderHom i.succ) :
    âˆƒ Î¸' : mk n âŸ¶ Î”', Î¸ = Ïƒ i â‰« Î¸' := by
  use Î´ i.succ â‰« Î¸
  ext1; ext1; ext1 x
  simp only [Hom.toOrderHom_mk, Function.comp_apply, OrderHom.comp_coe, Hom.comp,
    smallCategory_comp, Ïƒ, mkHom, OrderHom.coe_mk]
  by_cases h' : x â‰¤ Fin.castSucc i
  Â· -- This was not needed before leanprover/lean4#2644
    dsimp
    rw [Fin.predAbove_below i x h']
    have eq := Fin.castSucc_castPred (gt_of_gt_of_ge (Fin.castSucc_lt_last i) h')
    dsimp [Î´]
    erw [Fin.succAbove_below i.succ x.castPred _]
    swap
    Â· rwa [eq, â† Fin.le_castSucc_iff]
    rw [eq]
  Â· simp only [not_le] at h'
    let y := x.pred <| by rintro (rfl : x = 0); simp at h'
    have hy : x = y.succ := (Fin.succ_pred x _).symm
    rw [hy] at h' âŠ¢
    -- This was not needed before leanprover/lean4#2644
    conv_rhs => dsimp
    rw [Fin.predAbove_above i y.succ h', Fin.pred_succ]
    by_cases h'' : y = i
    Â· rw [h'']
      refine hi.symm.trans ?_
      congr 1
      dsimp [Î´]
      erw [Fin.succAbove_below i.succ]
      exact Fin.lt_succ
    Â· dsimp [Î´]
      erw [Fin.succAbove_above i.succ _]
      simp only [Fin.lt_iff_val_lt_val, Fin.le_iff_val_le_val, Fin.val_succ, Fin.coe_castSucc,
        Nat.lt_succ_iff, Fin.ext_iff] at h' h'' âŠ¢
      cases' Nat.le.dest h' with c hc
      cases c
      Â· exfalso
        simp only [Nat.zero_eq, add_zero, len_mk, Fin.coe_pred, ge_iff_le] at hc
        rw [hc] at h''
        exact h'' rfl
      Â· rw [â† hc]
        simp only [add_le_add_iff_left, Nat.succ_eq_add_one, le_add_iff_nonneg_left, zero_le]

theorem eq_Ïƒ_comp_of_not_injective {n : â„•} {Î”' : SimplexCategory} (Î¸ : mk (n + 1) âŸ¶ Î”')
    (hÎ¸ : Â¬Function.Injective Î¸.toOrderHom) :
    âˆƒ (i : Fin (n + 1)) (Î¸' : mk n âŸ¶ Î”'), Î¸ = Ïƒ i â‰« Î¸' := by
  simp only [Function.Injective, exists_prop, not_forall] at hÎ¸
  -- as Î¸ is not injective, there exists `x<y` such that `Î¸ x = Î¸ y`
  -- and then, `Î¸ x = Î¸ (x+1)`
  have hÎ¸â‚‚ : âˆƒ x y : Fin (n + 2), (Hom.toOrderHom Î¸) x = (Hom.toOrderHom Î¸) y âˆ§ x < y := by
    rcases hÎ¸ with âŸ¨x, y, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
    by_cases h : x < y
    Â· exact âŸ¨x, y, âŸ¨hâ‚, hâŸ©âŸ©
    Â· refine' âŸ¨y, x, âŸ¨hâ‚.symm, _âŸ©âŸ©
      cases' lt_or_eq_of_le (not_lt.mp h) with h' h'
      Â· exact h'
      Â· exfalso
        exact hâ‚‚ h'.symm
  rcases hÎ¸â‚‚ with âŸ¨x, y, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
  let z := x.castPred
  use z
  rw [â† show Fin.castSucc z = x from
    Fin.castSucc_castPred (lt_of_lt_of_le hâ‚‚ (Fin.le_last y))] at hâ‚ hâ‚‚
  apply eq_Ïƒ_comp_of_not_injective'
  rw [Fin.castSucc_lt_iff_succ_le] at hâ‚‚
  apply le_antisymm
  Â· exact Î¸.toOrderHom.monotone (le_of_lt (Fin.castSucc_lt_succ z))
  Â· rw [hâ‚]
    exact Î¸.toOrderHom.monotone hâ‚‚

theorem eq_comp_Î´_of_not_surjective' {n : â„•} {Î” : SimplexCategory} (Î¸ : Î” âŸ¶ mk (n + 1))
    (i : Fin (n + 2)) (hi : âˆ€ x, Î¸.toOrderHom x â‰  i) : âˆƒ Î¸' : Î” âŸ¶ mk n, Î¸ = Î¸' â‰« Î´ i := by
  by_cases i < Fin.last (n + 1)
  Â· use Î¸ â‰« Ïƒ (Fin.castPred i)
    ext1
    ext1
    ext1 x
    simp only [Hom.toOrderHom_mk, Function.comp_apply, OrderHom.comp_coe, Hom.comp,
      smallCategory_comp]
    by_cases h' : Î¸.toOrderHom x â‰¤ i
    Â· simp only [Ïƒ, mkHom, Hom.toOrderHom_mk, OrderHom.coe_mk]
      -- This was not needed before leanprover/lean4#2644
      dsimp
      -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
      erw [Fin.predAbove_below (Fin.castPred i) (Î¸.toOrderHom x)
          (by simpa [Fin.castSucc_castPred h] using h')]
      dsimp [Î´]
      erw [Fin.succAbove_below i]
      swap
      Â· simp only [Fin.lt_iff_val_lt_val, Fin.coe_castSucc]
        exact
          lt_of_le_of_lt (Fin.coe_castPred_le_self _)
            (Fin.lt_iff_val_lt_val.mp ((Ne.le_iff_lt (hi x)).mp h'))
      rw [Fin.castSucc_castPred]
      apply lt_of_le_of_lt h' h
    Â· simp only [not_le] at h'
      -- The next three tactics used to be a simp only call before leanprover/lean4#2644
      rw [Ïƒ, mkHom, Hom.toOrderHom_mk, OrderHom.coe_mk, OrderHom.coe_mk]
      erw [OrderHom.coe_mk]
      erw [Fin.predAbove_above (Fin.castPred i) (Î¸.toOrderHom x)
          (by simpa only [Fin.castSucc_castPred h] using h')]
      dsimp [Î´]
      rw [Fin.succAbove_above i _]
      -- This was not needed before leanprover/lean4#2644
      conv_rhs => dsimp
      erw [Fin.succ_pred]
      simpa only [Fin.le_iff_val_le_val, Fin.coe_castSucc, Fin.coe_pred] using
        Nat.le_sub_one_of_lt (Fin.lt_iff_val_lt_val.mp h')
  Â· obtain rfl := le_antisymm (Fin.le_last i) (not_lt.mp h)
    use Î¸ â‰« Ïƒ (Fin.last _)
    ext x : 4
    dsimp [Î´, Ïƒ]
    dsimp only [Fin.castPred]
    rw [Fin.predAbove_last, Fin.succAbove_last, Fin.castSucc_castPred]
    exact (Ne.le_iff_lt (hi x)).mp (Fin.le_last _)

theorem eq_comp_Î´_of_not_surjective {n : â„•} {Î” : SimplexCategory} (Î¸ : Î” âŸ¶ mk (n + 1))
    (hÎ¸ : Â¬Function.Surjective Î¸.toOrderHom) :
    âˆƒ (i : Fin (n + 2)) (Î¸' : Î” âŸ¶ mk n), Î¸ = Î¸' â‰« Î´ i := by
  cases' not_forall.mp hÎ¸ with i hi
  use i
  exact eq_comp_Î´_of_not_surjective' Î¸ i (not_exists.mp hi)

theorem eq_id_of_mono {x : SimplexCategory} (i : x âŸ¶ x) [Mono i] : i = ğŸ™ _ := by
  suffices IsIso i by
    apply eq_id_of_isIso
  apply isIso_of_bijective
  dsimp
  rw [Fintype.bijective_iff_injective_and_card i.toOrderHom, â† mono_iff_injective,
    eq_self_iff_true, and_true_iff]
  infer_instance

theorem eq_id_of_epi {x : SimplexCategory} (i : x âŸ¶ x) [Epi i] : i = ğŸ™ _ := by
  suffices IsIso i by
    haveI := this
    apply eq_id_of_isIso
  apply isIso_of_bijective
  dsimp
  rw [Fintype.bijective_iff_surjective_and_card i.toOrderHom, â† epi_iff_surjective,
    eq_self_iff_true, and_true_iff]
  infer_instance

theorem eq_Ïƒ_of_epi {n : â„•} (Î¸ : mk (n + 1) âŸ¶ mk n) [Epi Î¸] : âˆƒ i : Fin (n + 1), Î¸ = Ïƒ i := by
  rcases eq_Ïƒ_comp_of_not_injective Î¸ (by
    by_contra h
    simpa using le_of_mono (mono_iff_injective.mpr h)) with âŸ¨i, Î¸', hâŸ©
  use i
  haveI : Epi (Ïƒ i â‰« Î¸') := by
    rw [â† h]
    infer_instance
  haveI := CategoryTheory.epi_of_epi (Ïƒ i) Î¸'
  rw [h, eq_id_of_epi Î¸', Category.comp_id]

theorem eq_Î´_of_mono {n : â„•} (Î¸ : mk n âŸ¶ mk (n + 1)) [Mono Î¸] : âˆƒ i : Fin (n + 2), Î¸ = Î´ i := by
  rcases eq_comp_Î´_of_not_surjective Î¸ (by
    by_contra h
    simpa using le_of_epi (epi_iff_surjective.mpr h)) with âŸ¨i, Î¸', hâŸ©
  use i
  haveI : Mono (Î¸' â‰« Î´ i) := by
    rw [â† h]
    infer_instance
  haveI := CategoryTheory.mono_of_mono Î¸' (Î´ i)
  rw [h, eq_id_of_mono Î¸', Category.id_comp]

theorem len_lt_of_mono {Î”' Î” : SimplexCategory} (i : Î”' âŸ¶ Î”) [hi : Mono i] (hi' : Î” â‰  Î”') :
    Î”'.len < Î”.len := by
  rcases lt_or_eq_of_le (len_le_of_mono hi) with (h | h)
  Â· exact h
  Â· exfalso
    exact hi' (by ext; exact h.symm)

noncomputable instance : SplitEpiCategory SimplexCategory :=
  skeletalEquivalence.inverse.splitEpiCategoryImpOfIsEquivalence

instance : HasStrongEpiMonoFactorisations SimplexCategory :=
  Functor.hasStrongEpiMonoFactorisations_imp_of_isEquivalence
    SimplexCategory.skeletalEquivalence.inverse

instance : HasStrongEpiImages SimplexCategory :=
  Limits.hasStrongEpiImages_of_hasStrongEpiMonoFactorisations

instance (Î” Î”' : SimplexCategory) (Î¸ : Î” âŸ¶ Î”') : Epi (factorThruImage Î¸) :=
  StrongEpi.epi

theorem image_eq {Î” Î”' Î”'' : SimplexCategory} {Ï† : Î” âŸ¶ Î”''} {e : Î” âŸ¶ Î”'} [Epi e] {i : Î”' âŸ¶ Î”''}
    [Mono i] (fac : e â‰« i = Ï†) : image Ï† = Î”' := by
  haveI := strongEpi_of_epi e
  let e := image.isoStrongEpiMono e i fac
  ext
  exact
    le_antisymm (len_le_of_epi (inferInstance : Epi e.hom))
      (len_le_of_mono (inferInstance : Mono e.hom))

theorem image_Î¹_eq {Î” Î”'' : SimplexCategory} {Ï† : Î” âŸ¶ Î”''} {e : Î” âŸ¶ image Ï†} [Epi e]
    {i : image Ï† âŸ¶ Î”''} [Mono i] (fac : e â‰« i = Ï†) : image.Î¹ Ï† = i := by
  haveI := strongEpi_of_epi e
  rw [â† image.isoStrongEpiMono_hom_comp_Î¹ e i fac,
    SimplexCategory.eq_id_of_isIso (image.isoStrongEpiMono e i fac).hom, Category.id_comp]

theorem factorThruImage_eq {Î” Î”'' : SimplexCategory} {Ï† : Î” âŸ¶ Î”''} {e : Î” âŸ¶ image Ï†} [Epi e]
    {i : image Ï† âŸ¶ Î”''} [Mono i] (fac : e â‰« i = Ï†) : factorThruImage Ï† = e := by
  rw [â† cancel_mono i, fac, â† image_Î¹_eq fac, image.fac]

end EpiMono

/-- This functor `SimplexCategory â¥¤ Cat` sends `[n]` (for `n : â„•`)
to the category attached to the ordered set `{0, 1, ..., n}` -/
@[simps! obj map]
def toCat : SimplexCategory â¥¤ Cat.{0} :=
  SimplexCategory.skeletalFunctor â‹™ forgetâ‚‚ NonemptyFinLinOrd LinOrd â‹™
      forgetâ‚‚ LinOrd Lat â‹™ forgetâ‚‚ Lat PartOrd â‹™
      forgetâ‚‚ PartOrd Preord â‹™ preordToCat

end SimplexCategory
