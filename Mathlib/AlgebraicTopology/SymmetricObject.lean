import Mathlib.AlgebraicTopology.SplitSimplicialObject
import Mathlib.AlgebraicTopology.CechNerve
import Mathlib.CategoryTheory.Endomorphism
import Mathlib.CategoryTheory.Limits.Shapes.WidePullbacks
import Mathlib.GroupTheory.Perm.Basic

universe v u

open CategoryTheory Category Limits

def NonemptyFintypeCat :=
  CategoryTheory.FullSubcategory (fun (X : FintypeCat.{u}) â†¦ Nonempty X)

namespace NonemptyFintypeCat

instance : CoeSort NonemptyFintypeCat.{u} (Type u) where
  coe X := X.1.1

def of (X : Type u) [Fintype X] [Nonempty X] :
    NonemptyFintypeCat.{u} :=
  âŸ¨âŸ¨X, inferInstanceâŸ©, inferInstanceâŸ©

@[simps!]
instance : Category NonemptyFintypeCat.{u} := by
  dsimp [NonemptyFintypeCat]
  infer_instance

@[ext]
lemma hom_ext {X Y : NonemptyFintypeCat.{u}} {f g : X âŸ¶ Y}
    (h : âˆ€ (x : X), f x = g x) : f = g := funext h

lemma isSplitEpi_of_surjective {X Y : NonemptyFintypeCat.{u}} (f : X âŸ¶ Y)
    (hf : Function.Surjective f) : IsSplitEpi f := by
  obtain âŸ¨h, eqâŸ© := Function.surjective_iff_hasRightInverse.1 hf
  exact âŸ¨h, funext eqâŸ©

end NonemptyFintypeCat

@[simps]
def SimplexCategory.toNonemptyFintypeCat :
    SimplexCategory â¥¤ NonemptyFintypeCat.{0} where
  obj Î” := NonemptyFintypeCat.of (Fin (Î”.len + 1))
  map f x := f.toOrderHom x

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

def SymmetricObject := NonemptyFintypeCat.{0}áµ’áµ– â¥¤ C

namespace SymmetricObject

@[simps!]
instance category : Category (SymmetricObject C) := by
  dsimp only [SymmetricObject]
  infer_instance

def toSimplicialObjectFunctor :
    SymmetricObject C â¥¤ SimplicialObject C :=
  (whiskeringLeft _ _ _).obj SimplexCategory.toNonemptyFintypeCat.op

variable {C}

abbrev toSimplicialObject (X : SymmetricObject C) : SimplicialObject C :=
  (toSimplicialObjectFunctor C).obj X

def rep (X : SymmetricObject C) (A : NonemptyFintypeCat) :
  Equiv.Perm A â†’* Aut (X.obj (Opposite.op A)) where
  toFun g :=
    { hom := X.map (Quiver.Hom.op (gâ»Â¹).1)
      inv := X.map (Quiver.Hom.op g.1)
      hom_inv_id := by
        rw [â† X.map_comp, â† X.map_id]
        congr
        apply Quiver.Hom.unop_inj
        ext
        simp
      inv_hom_id := by
        rw [â† X.map_comp, â† X.map_id]
        congr
        apply Quiver.Hom.unop_inj
        ext
        simp }
  map_one' := by
    ext
    apply X.map_id
  map_mul' gâ‚ gâ‚‚ := by
    ext
    dsimp [Aut.Aut_mul_def]
    rw [â† X.map_comp]
    rfl

end SymmetricObject

namespace SymmetricObject

variable {C I : Type*} [Category C]

section

variable {S : C} {X : I â†’ C} (f : âˆ€ i, X i âŸ¶ S)

abbrev HasCechObjectWidePullbacks :=
  âˆ€ (A : NonemptyFintypeCat.{0}) (Ï† : A â†’ I),
    HasWidePullback S (fun a => X (Ï† a)) (fun a => f (Ï† a))

variable [HasCechObjectWidePullbacks f]

abbrev HasCechObjectCoproducts :=
  âˆ€ (A : NonemptyFintypeCat.{0}), HasCoproduct
    (fun (Ï† : A â†’ I) => widePullback S (fun a => X (Ï† a)) (fun a => f (Ï† a)))

noncomputable def cechObjectSummand {A : NonemptyFintypeCat.{0}} (Ï† : A â†’ I) :=
  widePullback S (fun b => X (Ï† b)) (fun b => f (Ï† b))

noncomputable def cechObjectSummandBase {A : NonemptyFintypeCat.{0}} (Ï† : A â†’ I) :
    cechObjectSummand f Ï† âŸ¶ S :=
  WidePullback.base _

noncomputable def cechObjectSummandÏ€ {A : NonemptyFintypeCat.{0}}
    (Ï† : A â†’ I) (a : A) (i : I) (h : Ï† a = i) :
    cechObjectSummand f Ï† âŸ¶ X i := WidePullback.Ï€ _ a â‰« eqToHom (by subst h; rfl)

@[reassoc (attr := simp)]
lemma cechObjectSummandÏ€_f {A : NonemptyFintypeCat.{0}}
    (Ï† : A â†’ I) (a : A) (i : I) (h : Ï† a = i) :
    cechObjectSummandÏ€ f Ï† a i h â‰« f i = cechObjectSummandBase f Ï† := by
  subst h
  dsimp [cechObjectSummandÏ€, cechObjectSummandBase]
  simp only [comp_id, WidePullback.Ï€_arrow]

noncomputable def cechObjectSummandMap {A B : NonemptyFintypeCat.{0}}
    (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I) (h : Ï†.comp g = Ïˆ) :
    cechObjectSummand f Ï† âŸ¶ cechObjectSummand f Ïˆ :=
  WidePullback.lift (cechObjectSummandBase _ _)
    (fun a => cechObjectSummandÏ€ f Ï† (g a) (Ïˆ a) (by subst h; rfl)) (by simp)

@[reassoc]
lemma cechObjectSummandMap_Ï€ {A B : NonemptyFintypeCat.{0}}
    (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I) (h : Ï†.comp g = Ïˆ) (a : A) (i : I)
    (h' : Ïˆ a = i) (b : B) (h'' : g a = b) :
    (cechObjectSummandMap f g Ï† Ïˆ h) â‰« cechObjectSummandÏ€ f Ïˆ a i h' =
      cechObjectSummandÏ€ f Ï† b i (by rw [â† h'', â† h', â† h]; rfl) := by
  subst h h' h''
  dsimp [cechObjectSummandMap, cechObjectSummandÏ€]
  simp

@[reassoc (attr := simp)]
lemma cechObjectSummandMap_base {A B : NonemptyFintypeCat.{0}}
    (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I) (h : Ï†.comp g = Ïˆ) :
    cechObjectSummandMap f g Ï† Ïˆ h â‰« cechObjectSummandBase f Ïˆ = cechObjectSummandBase f Ï† := by
  dsimp [cechObjectSummandMap, cechObjectSummandBase]
  simp

@[ext]
lemma cechObjectSummand_ext {A : NonemptyFintypeCat.{0}} {Ï† : A â†’ I} {Z : C}
    {Î± Î² : Z âŸ¶ cechObjectSummand f Ï†}
    (h : âˆ€ (a : A) (i : I) (h : Ï† a = i),
      Î± â‰« cechObjectSummandÏ€ f Ï† a i h = Î² â‰« cechObjectSummandÏ€ f Ï† a i h) : Î± = Î² := by
  apply WidePullback.hom_ext
  Â· intro a
    simpa [cechObjectSummandÏ€] using h a _ rfl
  Â· have a : A := Nonempty.some A.2
    change Î± â‰« cechObjectSummandBase f Ï† = Î² â‰« cechObjectSummandBase f Ï†
    simp only [â† cechObjectSummandÏ€_f f Ï† a _ rfl, reassoc_of% (h a _ rfl)]

@[simp]
lemma cechObjectSummandMap_id {A : NonemptyFintypeCat.{0}} (Ï† : A â†’ I) :
    cechObjectSummandMap f (ðŸ™ A) Ï† Ï† rfl = ðŸ™ _ := by
  ext a i h
  rw [id_comp, cechObjectSummandMap_Ï€ f (ðŸ™ A) Ï† Ï† rfl a i h a rfl]

lemma cechObjectSummandMap_comp {Aâ‚ Aâ‚‚ Aâ‚ƒ : NonemptyFintypeCat.{0}}
    (gâ‚ : Aâ‚ â†’ Aâ‚‚) (gâ‚‚ : Aâ‚‚ â†’ Aâ‚ƒ) (gâ‚â‚‚ : Aâ‚ â†’ Aâ‚ƒ) (h : gâ‚â‚‚ = gâ‚‚.comp gâ‚)
    (Ï†â‚ƒ : Aâ‚ƒ â†’ I) (Ï†â‚‚ : Aâ‚‚ â†’ I) (Ï†â‚ : Aâ‚ â†’ I) (h' : Ï†â‚ƒ.comp gâ‚‚ = Ï†â‚‚)
    (h'' : Ï†â‚‚.comp gâ‚ = Ï†â‚) :
    cechObjectSummandMap f gâ‚â‚‚ Ï†â‚ƒ Ï†â‚ (by subst h' h'' h; rfl) =
      cechObjectSummandMap f gâ‚‚ Ï†â‚ƒ Ï†â‚‚ h' â‰« cechObjectSummandMap f gâ‚ Ï†â‚‚ Ï†â‚ h'' := by
  subst h
  ext a _ rfl
  rw [assoc, cechObjectSummandMap_Ï€ f gâ‚ Ï†â‚‚ Ï†â‚ h'' a _ rfl _ rfl,
    cechObjectSummandMap_Ï€ f gâ‚‚ Ï†â‚ƒ Ï†â‚‚ h' (gâ‚ a) (Ï†â‚ a) (by rw [â† h'']; rfl) _ rfl,
    cechObjectSummandMap_Ï€ f (gâ‚‚.comp gâ‚) Ï†â‚ƒ Ï†â‚ (by rw [â† h'', â† h']; rfl) a _ rfl (gâ‚‚ (gâ‚ a)) rfl]

variable [HasCechObjectCoproducts f]

instance {A : NonemptyFintypeCat.{0}}  :
    HasCoproduct (fun (Ï† : A â†’ I) => cechObjectSummand f Ï†) := by
  dsimp [cechObjectSummand]
  infer_instance

noncomputable def cechObjectObj (A : NonemptyFintypeCat.{0}) : C :=
  âˆ (fun (Ï† : A â†’ I) => cechObjectSummand f Ï†)

noncomputable def Î¹CechObjectObj {A : NonemptyFintypeCat.{0}} (Ï† : A â†’ I) :
    cechObjectSummand f Ï† âŸ¶ cechObjectObj f A :=
  Sigma.Î¹ _ Ï†

noncomputable def cechObjectDesc {A : NonemptyFintypeCat.{0}} {X : C}
    (Î± : âˆ€ (Ï† : A â†’ I), cechObjectSummand f Ï† âŸ¶ X) :
    cechObjectObj f A âŸ¶ X :=
  Sigma.desc Î±

@[reassoc (attr := simp)]
lemma Î¹_cechObjectDesc {A : NonemptyFintypeCat.{0}} {X : C}
    (Î± : âˆ€ (Ï† : A â†’ I), cechObjectSummand f Ï† âŸ¶ X) (Ï† : A â†’ I) :
    Î¹CechObjectObj f Ï† â‰« cechObjectDesc f Î± = Î± Ï† := by
  dsimp [Î¹CechObjectObj, cechObjectDesc]
  simp

noncomputable def cechObjectMap {A B : NonemptyFintypeCat.{0}} (g : A âŸ¶ B) :
    cechObjectObj f B âŸ¶ cechObjectObj f A :=
  cechObjectDesc f (fun Ï† => cechObjectSummandMap f g Ï† _ rfl â‰« Î¹CechObjectObj f _)

@[reassoc]
lemma Î¹_cechObjectMap {A B : NonemptyFintypeCat.{0}} (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I)
    (h : Ï†.comp g = Ïˆ) :
    Î¹CechObjectObj f Ï† â‰« cechObjectMap f g =
      cechObjectSummandMap f g Ï† Ïˆ h â‰« Î¹CechObjectObj f Ïˆ := by
  subst h
  dsimp only [cechObjectMap]
  simp

@[ext]
lemma cechObjectObj_hom_ext {A : NonemptyFintypeCat.{0}} {Z : C}
    {Î± Î² : cechObjectObj f A âŸ¶ Z}
    (h : âˆ€ (Ï† : A â†’ I), Î¹CechObjectObj f Ï† â‰« Î± = Î¹CechObjectObj f Ï† â‰« Î²) :
    Î± = Î² :=
  Sigma.hom_ext _ _ (fun _ => h _)

@[simps!]
noncomputable def cechObject  : SymmetricObject C where
  obj A := cechObjectObj f A.unop
  map {Aâ‚ Aâ‚‚} g := cechObjectMap f g.unop
  map_id A := by
    ext Ï†
    dsimp
    rw [comp_id, Î¹_cechObjectMap f (ðŸ™ A.unop) Ï† Ï† rfl, cechObjectSummandMap_id, id_comp]
  map_comp := by
    rintro âŸ¨Aâ‚âŸ© âŸ¨Aâ‚‚âŸ© âŸ¨Aâ‚ƒâŸ© âŸ¨fâ‚ : Aâ‚‚ âŸ¶ Aâ‚âŸ© âŸ¨fâ‚‚ : Aâ‚ƒ âŸ¶ Aâ‚‚âŸ©
    ext (Ï† : Aâ‚ â†’ I)
    change Î¹CechObjectObj f Ï† â‰« cechObjectMap f (fâ‚‚ â‰« fâ‚) =
      Î¹CechObjectObj f Ï† â‰« cechObjectMap f fâ‚ â‰« cechObjectMap f fâ‚‚
    rw [Î¹_cechObjectMap f (fâ‚‚ â‰« fâ‚) Ï† ((Ï†.comp fâ‚).comp fâ‚‚) rfl,
      cechObjectSummandMap_comp f fâ‚‚ fâ‚ (fâ‚‚ â‰« fâ‚) rfl Ï† _ _ rfl rfl, assoc,
      Î¹_cechObjectMap_assoc f fâ‚ Ï† _ rfl, Î¹_cechObjectMap f fâ‚‚ (Ï†.comp fâ‚) _ rfl]

section

variable [âˆ€ i, Mono (f i)]

instance {A : NonemptyFintypeCat.{0}} (Ï† : A â†’ I) :
    Mono (cechObjectSummandBase f Ï†) where
  right_cancellation {Z} Î± Î² h := by
    ext a _ rfl
    simp only [â† cancel_mono (f (Ï† a)), assoc, cechObjectSummandÏ€_f f Ï† a, h]

instance {A : NonemptyFintypeCat.{0}} (Ï† : A â†’ I) (a : A) (i : I) (h : Ï† a = i) :
    Mono (cechObjectSummandÏ€ f Ï† a i h) :=
  mono_of_mono_fac (cechObjectSummandÏ€_f f Ï† a i h)

instance {A B : NonemptyFintypeCat.{0}} (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I) (h : Ï†.comp g = Ïˆ) :
    Mono (cechObjectSummandMap f g Ï† Ïˆ h) := by
  have a : A := Nonempty.some A.2
  exact mono_of_mono_fac (cechObjectSummandMap_Ï€ f g Ï† Ïˆ h a _ rfl _ rfl)

lemma isIso_cechObjectMap {A B : NonemptyFintypeCat.{0}}
    (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I) (h : Ï†.comp g = Ïˆ) (hg : Set.range Ï† = Set.range Ïˆ) :
    IsIso (cechObjectSummandMap f g Ï† Ïˆ h) := by
  obtain âŸ¨Ïƒ, hÏƒâŸ© : âˆƒ (Ïƒ : B â†’ A), Ïˆ.comp Ïƒ = Ï† := by
    obtain h : âˆ€ (b : B), Ï† b âˆˆ Set.range Ïˆ :=
      fun b => by simp only [â† hg, Set.mem_range, exists_apply_eq_apply]
    exact âŸ¨fun b â†¦ (h b).choose, funext (fun b â†¦ (h b).choose_spec)âŸ©
  refine' âŸ¨cechObjectSummandMap f Ïƒ Ïˆ Ï† hÏƒ, _, _âŸ©
  all_goals
    simp only [â† cancel_mono (cechObjectSummandBase f _), assoc,
      cechObjectSummandMap_base, id_comp]

lemma isIso_cechObjectMap' {A B : NonemptyFintypeCat.{0}}
    (g : A âŸ¶ B) (Ï† : B â†’ I) (Ïˆ : A â†’ I) (h : Ï†.comp g = Ïˆ) (hg : Function.Surjective g) :
    IsIso (cechObjectSummandMap f g Ï† Ïˆ h) := by
  apply isIso_cechObjectMap
  subst h
  ext i
  constructor
  Â· rintro âŸ¨b, rflâŸ©
    obtain âŸ¨a, rflâŸ© := hg b
    exact âŸ¨a, rflâŸ©
  Â· rintro âŸ¨a, rflâŸ©
    exact âŸ¨g a, rflâŸ©

end

end

section

variable {S : C} {X : Unit â†’ C} (f : âˆ€ i, X i âŸ¶ S)
  [HasCechObjectWidePullbacks f] [HasCechObjectCoproducts f]
  [H : âˆ€ (n : â„•), HasWidePullback (Arrow.mk (f ())).right
    (fun (_ : Fin (n + 1)) â†¦ (Arrow.mk (f ())).left) fun _ â†¦ (Arrow.mk (f ())).hom]

noncomputable def cechObjectSummandIsoCechNerveObj (Î” : SimplexCategory)
    (Ï† : SimplexCategory.toNonemptyFintypeCat.obj Î” â†’ Unit) :
    cechObjectSummand f Ï† â‰… (Arrow.cechNerve (Arrow.mk (f Unit.unit))).obj (Opposite.op Î”) :=
  Iso.refl _

lemma cechObjectSummandMap_toNonemptyFintypeCat_map {Î”â‚ Î”â‚‚ : SimplexCategory} (g : Î”â‚‚ âŸ¶ Î”â‚) :
  cechObjectSummandMap f (SimplexCategory.toNonemptyFintypeCat.map g) (fun _ => Unit.unit)
    (fun _ => Unit.unit) rfl = (Arrow.cechNerve (Arrow.mk (f Unit.unit))).map g.op := by
  ext i âŸ¨âŸ© âŸ¨âŸ©
  rw [cechObjectSummandMap_Ï€ f (SimplexCategory.toNonemptyFintypeCat.map g) (fun _ => Unit.unit)
    (fun _ => Unit.unit) rfl i Unit.unit rfl _ rfl]
  dsimp [cechObjectSummandÏ€]
  simp only [comp_id]
  symm
  apply @WidePullback.lift_Ï€ _ _ _ _ _ _ (H _)

noncomputable def cechObjectToSimplicialObjectIsoApp (Î” : SimplexCategory) :
    (cechObject f).toSimplicialObject.obj (Opposite.op Î”) â‰…
      (Arrow.cechNerve (Arrow.mk (f Unit.unit))).obj (Opposite.op Î”) where
  hom := cechObjectDesc f (fun Ï† => (cechObjectSummandIsoCechNerveObj f Î” Ï†).hom)
  inv := (cechObjectSummandIsoCechNerveObj f Î” (fun _ => Unit.unit)).inv â‰« Î¹CechObjectObj f _
  hom_inv_id := cechObjectObj_hom_ext f (fun Ï† => by
    obtain rfl : Ï† = fun _ => Unit.unit := rfl
    dsimp
    simp only [Î¹_cechObjectDesc_assoc, Iso.hom_inv_id_assoc]
    erw [comp_id])

noncomputable def cechObjectToSimplicialObjectIso :
    (cechObject f).toSimplicialObject â‰… Arrow.cechNerve (Arrow.mk (f Unit.unit)) :=
  NatIso.ofComponents (fun Î” => cechObjectToSimplicialObjectIsoApp f Î”.unop) (by
    rintro âŸ¨Î”â‚âŸ© âŸ¨Î”â‚‚âŸ© âŸ¨g : Î”â‚‚ âŸ¶ Î”â‚âŸ©
    exact cechObjectObj_hom_ext f (fun Ï† => by
      obtain rfl : Ï† = fun _ => Unit.unit := rfl
      dsimp [cechObjectToSimplicialObjectIsoApp, cechObjectSummandIsoCechNerveObj,
        toSimplicialObject, toSimplicialObjectFunctor]
      rw [Î¹_cechObjectDesc_assoc, id_comp, Î¹_cechObjectMap_assoc f _ _ _ rfl,
        Î¹_cechObjectDesc, comp_id]
      exact cechObjectSummandMap_toNonemptyFintypeCat_map f g))

end

end SymmetricObject

end CategoryTheory
