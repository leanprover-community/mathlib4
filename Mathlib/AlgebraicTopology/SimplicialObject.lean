/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz
-/
import Mathlib.AlgebraicTopology.SimplexCategory
import Mathlib.AlgebraicTopology.AugmentedSimplexCategory
import Mathlib.CategoryTheory.Arrow
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Opposites

/-!
# Simplicial objects in a category.

A simplicial object in a category `C` is a `C`-valued presheaf on `SimplexCategory`.
(Similarly a cosimplicial object is functor `SimplexCategory â¥¤ C`.)

Use the notation `X _[n]` in the `Simplicial` locale to obtain the `n`-th term of a
(co)simplicial object `X`, where `n` is a natural number.

Define augmented simplicial objects via the comma category construction, and show that this is
equivalent to functors `AugmentedSimplexCategoryáµ’áµ– â¥¤ C`.

-/

open Opposite

open CategoryTheory

open CategoryTheory.Limits

universe v u v' u'

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- The category of simplicial objects valued in a category `C`.
This is the category of contravariant functors from `SimplexCategory` to `C`. -/
def SimplicialObject :=
  SimplexCategoryáµ’áµ– â¥¤ C

@[simps!]
instance : Category (SimplicialObject C) := by
  dsimp only [SimplicialObject]
  infer_instance

namespace SimplicialObject

set_option quotPrecheck false in
/-- `X _[n]` denotes the `n`th-term of the simplicial object X -/
scoped[Simplicial]
  notation3:1000 X " _[" n "]" =>
    (X : CategoryTheory.SimplicialObject _).obj (Opposite.op (SimplexCategory.mk n))

open Simplicial

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  infer_instance

instance [HasLimits C] : HasLimits (SimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  infer_instance

instance [HasColimits C] : HasColimits (SimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

variable {C}

-- Porting note (#10688): added to ease automation
@[ext]
lemma hom_ext {X Y : SimplicialObject C} (f g : X âŸ¶ Y)
    (h : âˆ€ (n : SimplexCategoryáµ’áµ–), f.app n = g.app n) : f = g :=
  NatTrans.ext (by ext; apply h)

variable (X : SimplicialObject C)

/-- Face maps for a simplicial object. -/
def Î´ {n} (i : Fin (n + 2)) : X _[n + 1] âŸ¶ X _[n] :=
  X.map (SimplexCategory.Î´ i).op

/-- Degeneracy maps for a simplicial object. -/
def Ïƒ {n} (i : Fin (n + 1)) : X _[n] âŸ¶ X _[n + 1] :=
  X.map (SimplexCategory.Ïƒ i).op

/-- Isomorphisms from identities in â„•. -/
def eqToIso {n m : â„•} (h : n = m) : X _[n] â‰… X _[m] :=
  X.mapIso (CategoryTheory.eqToIso (by congr))

@[simp]
theorem eqToIso_refl {n : â„•} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  ext
  simp [eqToIso]

/-- The generic case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    X.Î´ j.succ â‰« X.Î´ i = X.Î´ (Fin.castSucc i) â‰« X.Î´ j := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´ H]

@[reassoc]
theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Î´ j â‰« X.Î´ i =
      X.Î´ (Fin.castSucc i) â‰«
        X.Î´ (j.pred fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´' H]
@[reassoc]
theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ j.succ â‰« X.Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) =
      X.Î´ i â‰« X.Î´ j := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´'' H]

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} :
    X.Î´ (Fin.castSucc i) â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´_self]

@[reassoc]
theorem Î´_comp_Î´_self' {n} {j : Fin (n + 3)} {i : Fin (n + 2)} (H : j = Fin.castSucc i) :
    X.Î´ j â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i := by
  subst H
  rw [Î´_comp_Î´_self]

/-- The second simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    X.Ïƒ j.succ â‰« X.Î´ (Fin.castSucc i) = X.Î´ i â‰« X.Ïƒ j := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_le H]

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} : X.Ïƒ i â‰« X.Î´ (Fin.castSucc i) = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_self, op_id, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Ïƒ i â‰« X.Î´ j = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_self]

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : X.Ïƒ i â‰« X.Î´ i.succ = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_succ, op_id, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Ïƒ i â‰« X.Î´ j = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_succ]

/-- The fourth simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Ïƒ (Fin.castSucc j) â‰« X.Î´ i.succ = X.Î´ i â‰« X.Ïƒ j := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt H]

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Ïƒ j â‰« X.Î´ i =
      X.Î´ (i.pred fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) â‰«
        X.Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt' H]

/-- The fifth simplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    X.Ïƒ j â‰« X.Ïƒ (Fin.castSucc i) = X.Ïƒ i â‰« X.Ïƒ j.succ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Ïƒ_comp_Ïƒ H]

open Simplicial

@[reassoc (attr := simp)]
theorem Î´_naturality {X' X : SimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 2)) :
    X.Î´ i â‰« f.app (op [n]) = f.app (op [n + 1]) â‰« X'.Î´ i :=
  f.naturality _

@[reassoc (attr := simp)]
theorem Ïƒ_naturality {X' X : SimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« f.app (op [n + 1]) = f.app (op [n]) â‰« X'.Ïƒ i :=
  f.naturality _

variable (C)

/-- Functor composition induces a functor on simplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C â¥¤ D) â¥¤ SimplicialObject C â¥¤ SimplicialObject D :=
  whiskeringRight _ _ _

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- Truncated simplicial objects. -/
def Truncated (n : â„•) :=
  (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C

instance {n : â„•} : Category (Truncated C n) := by
  dsimp [Truncated]
  infer_instance

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasLimits C] : HasLimits (SimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasColimits C] : HasColimits (SimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

variable (C)

/-- Functor composition induces a functor on truncated simplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C â¥¤ D) â¥¤ Truncated C n â¥¤ Truncated D n :=
  whiskeringRight _ _ _

variable {C}

end Truncated

section Skeleton

/-- The skeleton functor from simplicial objects to truncated simplicial objects. -/
def sk (n : â„•) : SimplicialObject C â¥¤ SimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj SimplexCategory.Truncated.inclusion.op

end Skeleton

variable (C)

/-- The constant simplicial object is the constant functor. -/
abbrev const : C â¥¤ SimplicialObject C :=
  CategoryTheory.Functor.const _

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- The category of augmented simplicial objects, defined as a comma category. -/
def Augmented :=
  Comma (ğŸ­ (SimplicialObject C)) (const C)

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  infer_instance

variable {C}

namespace Augmented

-- Porting note (#10688): added to ease automation
@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ hâ‚ hâ‚‚

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C â¥¤ SimplicialObject C :=
  Comma.fst _ _

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C â¥¤ C :=
  Comma.snd _ _

/-- The functor from augmented objects to arrows. -/
@[simps]
def toArrow : Augmented C â¥¤ Arrow C where
  obj X :=
    { left := drop.obj X _[0]
      right := point.obj X
      hom := X.hom.app _ }
  map Î· :=
    { left := (drop.map Î·).app _
      right := point.map Î·
      w := by
        dsimp
        rw [â† NatTrans.comp_app]
        erw [Î·.w]
        rfl }

/-- The compatibility of a morphism with the augmentation, on 0-simplices -/
@[reassoc]
theorem wâ‚€ {X Y : Augmented C} (f : X âŸ¶ Y) :
    (Augmented.drop.map f).app (op (SimplexCategory.mk 0)) â‰« Y.hom.app (op (SimplexCategory.mk 0)) =
      X.hom.app (op (SimplexCategory.mk 0)) â‰« Augmented.point.map f := by
  convert congr_app f.w (op (SimplexCategory.mk 0))

variable (C)

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C â¥¤ D) : Augmented C â¥¤ Augmented D where
  obj X :=
    { left := ((whiskering _ _).obj F).obj (drop.obj X)
      right := F.obj (point.obj X)
      hom := whiskerRight X.hom F â‰« (Functor.constComp _ _ _).hom }
  map Î· :=
    { left := whiskerRight Î·.left _
      right := F.map Î·.right
      w := by
        ext
        dsimp [whiskerRight]
        simp only [Category.comp_id, â† F.map_comp, â† NatTrans.comp_app]
        erw [Î·.w]
        rfl }

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C â¥¤ D) â¥¤ Augmented C â¥¤ Augmented D where
  obj := whiskeringObj _ _
  map Î· :=
    { app := fun A =>
        { left := whiskerLeft _ Î·
          right := Î·.app _
          w := by
            ext n
            dsimp
            rw [Category.comp_id, Category.comp_id, Î·.naturality] } }
  map_comp := fun _ _ => by ext <;> rfl

variable {C}

end Augmented

/-- Augment a simplicial object with an object. -/
@[simps]
def augment (X : SimplicialObject C) (Xâ‚€ : C) (f : X _[0] âŸ¶ Xâ‚€)
    (w : âˆ€ (i : SimplexCategory) (gâ‚ gâ‚‚ : ([0] : SimplexCategory) âŸ¶ i),
      X.map gâ‚.op â‰« f = X.map gâ‚‚.op â‰« f) :
    SimplicialObject.Augmented C where
  left := X
  right := Xâ‚€
  hom :=
    { app := fun i => X.map (SimplexCategory.const _ _ 0).op â‰« f
      naturality := by
        intro i j g
        dsimp
        rw [â† g.op_unop]
        simpa only [â† X.map_comp, â† Category.assoc, Category.comp_id, â† op_comp] using w _ _ _ }

-- Porting note: removed @[simp] as the linter complains
theorem augment_hom_zero (X : SimplicialObject C) (Xâ‚€ : C) (f : X _[0] âŸ¶ Xâ‚€) (w) :
    (X.augment Xâ‚€ f w).hom.app (op [0]) = f := by simp




namespace augFuncEquiv
open AugmentedSimplexCategory
namespace functor'
/--The object map for the functor
`(AugmentedSimplexCategoryáµ’áµ– â¥¤ C) â¥¤ Augmented C`-/
def obj' (X : AugmentedSimplexCategoryáµ’áµ– â¥¤ C) :
    SimplicialObject.Augmented C where
    left := SimplexCategory.augment.op â‹™ X
    right := X.obj (op [0]â‚)
    hom :=
    {
      app := fun d => X.map (map_from_initial (SimplexCategory.augment.obj d.unop).len ).op
      naturality := by
        intros
        simp only [Functor.id_obj, Functor.comp_obj, Functor.op_obj, Functor.const_obj_obj,
          Functor.comp_map, Functor.op_map, â† X.map_comp, â† op_comp, smallCategory_comp, Hom.comp,
          Functor.const_obj_map, Category.comp_id]
        apply congrArg X.map âˆ˜ congrArg op
        apply IsInitial.hom_ext zeroIsInitial
    }
/--The morphism map for the functor
 `(AugmentedSimplexCategoryáµ’áµ– â¥¤ C) â¥¤ Augmented C` -/
def map' {X  Y : AugmentedSimplexCategoryáµ’áµ– â¥¤ C} (f : XâŸ¶ Y) : obj' X âŸ¶ obj' Y where
  left := whiskerLeft SimplexCategory.augment.op f
  right := f.app  (op [0]â‚)
  w := by
       ext
       rw [Functor.id_map,NatTrans.comp_app,NatTrans.comp_app]
       simp only [Functor.id_obj, Functor.const_obj_obj, whiskerLeft_app, Functor.op_obj,
         Functor.const_map_app,obj',Functor.id_obj, Functor.op_obj, Functor.comp_obj,
          NatTrans.naturality]

end functor'

/--The functor `(AugmentedSimplexCategoryáµ’áµ– â¥¤ C)â¥¤ Augmented C`-/
def functor' : (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)â¥¤ SimplicialObject.Augmented C  where
  obj := functor'.obj'
  map := functor'.map'

namespace inverse'
namespace obj'
/--The object map for the functor which is the image of `X âˆˆ Augmented C` under the functor
`Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def obj'' (X: SimplicialObject.Augmented C) (Y : AugmentedSimplexCategoryáµ’áµ–  ): C :=
    if Y.unop.len=0 then X.right else X.left.obj (op [Y.unop.len-1])

/--Part of the morphism map for the functor which is the image of `X âˆˆ Augmented C`
 under the functor  `Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def mapTargetZero' (X: SimplicialObject.Augmented C) (Y : AugmentedSimplexCategoryáµ’áµ–  ) :
    obj'.obj'' X Y âŸ¶ X.right :=  by
    by_cases hY : Y.unop.len=0
    Â· exact eqToHom (if_pos hY )
    Â· exact (eqToHom (if_neg hY )) â‰«  X.hom.app (op [Y.unop.len-1])

/--The morphism map for the functor which is the image of `X âˆˆ Augmented C`
 under the functor  `Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def map'' (X: SimplicialObject.Augmented C) {Y Z: AugmentedSimplexCategoryáµ’áµ–} (f: Y âŸ¶ Z) :
    obj'.obj'' X Y âŸ¶ obj'.obj'' X Z :=  by
    by_cases hZ : Z.unop.len =0
    Â· exact  (mapTargetZero' X Y)â‰« (eqToHom (if_pos hZ).symm)
    Â· exact eqToHom (if_neg (strict_initial' f.unop hZ))
       â‰« X.left.map (unaugment.map f.unop hZ).op  â‰« eqToHom (if_neg hZ).symm

end obj'
/--The object map for the functor
`Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def obj' (X: SimplicialObject.Augmented C) : (AugmentedSimplexCategoryáµ’áµ– â¥¤ C) where
  obj := obj'.obj'' X
  map := obj'.map'' X
  map_id := by
    simp only
    intro Y
    unfold obj'.map'' inverse'.obj'.mapTargetZero'
    by_cases hY: Y.unop.len=0
    Â·  rw [dif_pos hY,dif_pos hY,eqToHom_trans,eqToHom_refl]
    Â·  rw [dif_neg hY,unop_id,unaugment.map_id,show
       (ğŸ™ (SimplexCategory.mk (Y.unop.len - 1))).op = ğŸ™ (op (SimplexCategory.mk (Y.unop.len - 1)))
       from rfl,X.left.map_id,â† eqToHom_refl,â† eqToHom_refl,â† eqToHom_refl,eqToHom_trans,
       eqToHom_trans]
       all_goals rfl
  map_comp := by
    intro Y Z W f g
    unfold inverse'.obj'.map'' inverse'.obj'.mapTargetZero'
    dsimp only
    by_cases hW : W.unop.len=0
    Â· rw [dif_pos hW,dif_pos hW]
      by_cases hZ : Z.unop.len=0
      Â· rw [dif_pos hZ,dif_pos hZ]
        simp only [Functor.id_obj, eqToHom_trans, Category.assoc]
      Â· have hx:= X.hom.naturality (unaugment.map f.unop hZ).op
        unfold unaugment.obj at hx
        rw [dif_neg hZ,dif_neg hZ,dif_neg (strict_initial' f.unop hZ),â† Category.assoc,
        â† Category.assoc,â† Category.assoc,Category.assoc _ (eqToHom _) (eqToHom _),eqToHom_trans,
        eqToHom_refl,Category.comp_id,Category.assoc (eqToHom _) (X.left.map _) _,show
        X.left.map (unaugment.map f.unop hZ).op = ((ğŸ­ (SimplicialObject C)).obj X.left).map
        (unaugment.map f.unop hZ).op from rfl, hx]
        simp only [Functor.id_obj, Category.assoc, Functor.const_obj_obj, Functor.const_obj_map,
          Category.comp_id]
    Â· rw [dif_neg hW,dif_neg hW,dif_neg (strict_initial' g.unop hW),unop_comp,unaugment.map_comp,
      op_comp,X.left.map_comp]
      simp only [Functor.id_obj, Category.assoc, eqToHom_trans_assoc, eqToHom_refl,
        Category.id_comp]
/--The morphism map for the functor
`Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def map'  {X1 X2: SimplicialObject.Augmented C}  (f :X1 âŸ¶ X2): obj' X1 âŸ¶ obj' X2 where
  app Y :=by
      by_cases  hY: Y.unop.len=0
      Â· exact eqToHom (if_pos hY) â‰« f.right â‰« eqToHom (if_pos hY).symm
      Â· exact eqToHom (if_neg hY) â‰« f.left.app (op [Y.unop.len-1]) â‰« eqToHom (if_neg hY).symm
  naturality := by
      intro Y Z g
      dsimp
      unfold obj' inverse'.obj'.map''
      dsimp
      by_cases hZ : Z.unop.len =0
      Â· unfold inverse'.obj'.mapTargetZero'
        rw [dif_pos hZ,dif_pos hZ,dif_pos hZ]
        by_cases hY : Y.unop.len=0
        Â· simp only [dif_pos hY,eqToHom_trans, eqToHom_trans_assoc, Category.assoc]
        Â· simp only [dif_neg hY,Functor.id_obj, Category.assoc, eqToHom_trans_assoc, eqToHom_refl,
            Category.id_comp]
          rw [â† Category.assoc,â† Category.assoc, Category.assoc _ _ f.right]
          have h2 := congrFun (congrArg NatTrans.app f.w) (op [Y.unop.len - 1])
          rw [NatTrans.comp_app,NatTrans.comp_app,show ((const C).map f.right).app
            (op [Y.unop.len - 1]) = f.right from rfl] at h2
          simp only [â† h2,Functor.id_obj, Functor.const_obj_obj, Functor.id_map, Category.assoc]
      Â· rw [dif_neg hZ,dif_neg hZ,dif_neg hZ,dif_neg (strict_initial' g.unop hZ )]
        simp only [Category.assoc, eqToHom_trans_assoc, eqToHom_refl, Category.id_comp]
        rw [â† Category.assoc,â† Category.assoc,â† Category.assoc,â† Category.assoc,
        Category.assoc _ _ (X2.left.map _),â† f.left.naturality,Category.assoc,Category.assoc,
        Category.assoc,Category.assoc]
        congr

end inverse'
/--The functor
`Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def inverse' : SimplicialObject.Augmented C â¥¤ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)  where
  obj := inverse'.obj'
  map := inverse'.map'
  map_id := by
    intros
    apply NatTrans.ext
    funext
    simp only [inverse'.map', instCategoryAugmented_id_right, Category.id_comp, eqToHom_trans,
      eqToHom_refl, Functor.id_obj, instCategoryAugmented_id_left_app, dite_eq_ite, ite_self,
      NatTrans.id_app]
  map_comp := by
    intros
    apply NatTrans.ext
    funext Y
    by_cases h: Y.unop.len=0
    Â· simp only [inverse'.map',Functor.id_obj, instCategoryAugmented_comp_right, Category.assoc,
      instCategoryAugmented_comp_left_app, dif_pos h, NatTrans.comp_app, eqToHom_trans_assoc,
      eqToHom_refl, Category.id_comp]
    Â· simp only [inverse'.map',Functor.id_obj, instCategoryAugmented_comp_right, Category.assoc,
      instCategoryAugmented_comp_left_app, dif_neg h, NatTrans.comp_app, eqToHom_trans_assoc,
      eqToHom_refl, Category.id_comp]


namespace unitIso'

lemma app' (X : AugmentedSimplexCategoryáµ’áµ– â¥¤ C) : (functor' â‹™ inverse' ).obj X =X :=by
    rw [Functor.comp_obj]
    apply Functor.ext
    case h_obj =>
      intro Y
      unfold inverse' inverse'.obj' inverse'.obj'.obj'' functor' functor'.obj'
      by_cases hY :Y.unop.len=0
      Â· simp only [Functor.comp_obj, Functor.op_obj, unop_op,if_pos hY,congrArg X.obj]
        exact congrArg X.obj (congrArg op hY.symm)
      Â· nth_rewrite 2 [â† (op_unop Y)]
        rw [â† unaugment_augment_obj hY]
        exact if_neg hY
    case h_map =>
      intro Y Z f
      unfold inverse' inverse'.obj' inverse'.obj'.map'' inverse'.obj'.mapTargetZero'
      by_cases hZ : Z.unop.len =0
      Â· dsimp
        rw [dif_pos hZ]
        by_cases hY : Y.unop.len =0
        Â· let hx:= congrArg X.map
            (congrArg Quiver.Hom.op (map_into_initial_eqToHom (lenZeroIsInitial hY) f.unop))
          rw [eqToHom_op,eqToHom_map X,Quiver.Hom.op_unop] at hx
          simp only [dif_pos hY,eqToHom_trans, hx, op_unop]
        Â· unfold functor' functor'.obj'
          simp only [dif_neg hY,Functor.id_obj,Functor.op_obj, Functor.comp_obj, unop_op]
          rw [show map_from_initial (SimplexCategory.augment.obj [Y.unop.len-1]).len
           =(eqToHom (congrArg op (unaugment_augment_obj hY)) â‰«fâ‰«eqToHom (congrArg op hZ)).unop by
           apply IsInitial.hom_ext zeroIsInitial,Quiver.Hom.op_unop,X.map_comp,X.map_comp,
           eqToHom_map X,eqToHom_map X,eqToHom_trans_assoc,Category.assoc,Category.assoc,
           eqToHom_trans]
      Â· nth_rewrite 2 [â† (Quiver.Hom.op_unop f)]
        rw [â† unaugment_augment_map f.unop hZ,op_comp, op_comp,X.map_comp,X.map_comp,eqToHom_op,
        eqToHom_op,eqToHom_map X,eqToHom_map X,Category.assoc,Category.assoc,eqToHom_trans,
        eqToHom_trans_assoc]
        exact dif_neg hZ


lemma nat' (X1 X2 : AugmentedSimplexCategoryáµ’áµ– â¥¤ C)  (F :X1âŸ¶ X2):
    (ğŸ­ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C)).map F â‰« eqToHom (app' X2).symm
    = eqToHom (app' X1).symm  â‰« (functor' â‹™ inverse').map F:= by
  simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, Functor.comp_map]
  apply NatTrans.ext
  funext d
  unfold functor'  inverse'  inverse'.map'
  simp
  by_cases hd: d.unop.len=0
  Â· rw [dif_pos hd]
    simp only [comp_eqToHom_iff,eqToHom_trans_assoc, Category.assoc, eqToHom_trans]
    exact dcongr_arg F.app (unop_eq_iff_eq_op.mp hd)
  Â· rw [dif_neg hd]
    unfold functor'.map'
    simp only [Functor.id_obj, whiskerLeft_app, Functor.op_obj, unop_op, eqToHom_trans_assoc,
      comp_eqToHom_iff, Category.assoc, eqToHom_trans]
    refine  dcongr_arg F.app ?_
    change op d.unop =_
    apply congrArg
    exact (AugmentedSimplexCategory.unaugment_augment_obj hd).symm

end unitIso'
/--The unit of the equivalance between
`Augmented C` and `(AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def unitIso' : ğŸ­ (AugmentedSimplexCategoryáµ’áµ– â¥¤ C) â‰… functor' â‹™ inverse' where
  hom := {
    app := fun X => eqToHom (unitIso'.app' X).symm
    naturality := fun â¦ƒX Yâ¦„ f â†¦ unitIso'.nat' X Y f
  }
  inv := {
    app := fun X => eqToHom (unitIso'.app' X)
    naturality := by
      intros
      rw [â† eqToHom_comp_iff,â† Category.assoc, comp_eqToHom_iff]
      exact (unitIso'.nat' _ _ _).symm
  }

namespace counitIso'
/--Part of the data of the app map for the counit of the equivalance between
`Augmented C` and `(AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def app' (X : Augmented C) : ((inverse'â‹™ functor' ).obj X) â‰… X:= by
    refine Comma.isoMk (CategoryTheory.eqToIso ?_) (CategoryTheory.eqToIso (by rfl)) ?_
    change  SimplexCategory.augment.op â‹™ inverse'.obj' X = X.left
    apply Functor.ext
    intro Y Z f
    simp only [inverse'.obj', Functor.comp_obj, Functor.op_obj, Functor.comp_map,
      inverse'.obj'.map'', unop_op, Functor.id_obj, eqToHom_refl, Functor.op_map,
      Quiver.Hom.unop_op, Category.comp_id, Category.id_comp,
      dif_neg (SimplexCategory.augment_len Z.unop)]
    exact congrArg X.left.map (congrArg op (SimplexCategory.augment_unaugment_map f.unop))
    intros
    rfl
    apply NatTrans.ext
    funext d
    unfold functor' inverse'  functor'.obj' inverse'.obj' inverse'.obj'.obj''
      inverse'.obj'.map'' inverse'.obj'.mapTargetZero'
    simp only [Functor.id_obj, Functor.op_obj, Functor.comp_obj, unop_op, eqToHom_refl,
      Quiver.Hom.unop_op, Functor.const_obj_obj, eqToIso.hom, Functor.id_map,
      instCategorySimplicialObject_comp_app, Category.id_comp, Category.comp_id,
      CategoryTheory.eqToIso_refl, Iso.refl_hom, Functor.map_id]
    rw [dif_pos (by rfl),eqToHom_app,eqToHom_refl]
    exact Category.id_comp (X.hom.app d)
end counitIso'
/--The counit of the equivalance between
`Augmented C` and `(AugmentedSimplexCategoryáµ’áµ– â¥¤ C)`-/
def counitIso'   : inverse' â‹™  functor' â‰… ğŸ­ (Augmented C) where
  hom := {
    app := fun X => (augFuncEquiv.counitIso'.app' X).hom
    naturality := by
      intro X1 X2 F
      unfold functor' functor'.map' augFuncEquiv.counitIso'.app' inverse' inverse'.map'
      apply Comma.hom_ext
      Â· apply NatTrans.ext
        funext d
        simp [Comma.comp_left]
        rw [dif_neg (SimplexCategory.augment_len d.unop),eqToHom_app,eqToHom_app]
        exact eqToHom_naturality F.left.app (by rfl)
      Â· simp [Comma.comp_right]
        rw [dif_pos (by rfl),show ğŸ™ (functor'.obj (inverse'.obj' X1)).rightâ‰«F.right=F.right
         from Category.id_comp F.right]
        exact Category.comp_id F.right
  }
  inv := {
    app := fun X => (augFuncEquiv.counitIso'.app' X).inv
    naturality := by
        intro X1 X2 F
        unfold functor' functor'.map' augFuncEquiv.counitIso'.app' inverse' inverse'.map'
        apply Comma.hom_ext
        Â· apply NatTrans.ext
          funext d
          simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, CategoryTheory.eqToIso_refl,
            instCategoryAugmented_comp_left_app, Comma.isoMk_inv_left, eqToIso.inv,
            Functor.comp_map, unop_op, eqToHom_refl, Category.comp_id, Category.id_comp,
            whiskerLeft_app, Functor.op_obj]
          rw [dif_neg (SimplexCategory.augment_len d.unop),eqToHom_app,eqToHom_app]
          exact eqToHom_naturality F.left.app rfl
        Â· simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, CategoryTheory.eqToIso_refl,
          instCategoryAugmented_comp_right, Comma.isoMk_inv_right, Iso.refl_inv, Functor.comp_map,
          unop_op, eqToHom_refl, Category.comp_id, Category.id_comp]
          rw [dif_pos (by rfl), show ğŸ™ (functor'.obj (inverse'.obj' X1)).right â‰« F.right =F.right
           from  Category.id_comp F.right]
          exact Category.comp_id F.right
  }
end augFuncEquiv
open augFuncEquiv in
/--The equivelence between `Augmented C` and `AugmentedSimplexCategoryáµ’áµ– â¥¤ C`-/
def augFuncEquiv : AugmentedSimplexCategoryáµ’áµ– â¥¤ C  â‰Œ (Augmented C) where
  functor := functor'
  inverse := inverse'
  unitIso := unitIso'
  counitIso := counitIso'
  functor_unitIso_comp := by
      intros
      unfold  unitIso' counitIso' counitIso'.app'  Comma.isoMk
      dsimp
      apply Comma.hom_ext
      Â· rw [eqToHom_map,Comma.comp_left,Comma.eqToHom_left,Comma.id_left,eqToHom_trans,eqToHom_refl]
      Â· rw [eqToHom_map,Comma.comp_right,Comma.eqToHom_right,eqToHom_refl,Category.comp_id
         ,Comma.id_right]
end SimplicialObject

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- Cosimplicial objects. -/
def CosimplicialObject :=
  SimplexCategory â¥¤ C

@[simps!]
instance : Category (CosimplicialObject C) := by
  dsimp only [CosimplicialObject]
  infer_instance

namespace CosimplicialObject

set_option quotPrecheck false in
/-- `X _[n]` denotes the `n`th-term of the cosimplicial object X -/
scoped[Simplicial]
  notation:1000 X " _[" n "]" =>
    (X : CategoryTheory.CosimplicialObject _).obj (SimplexCategory.mk n)

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  infer_instance

instance [HasLimits C] : HasLimits (CosimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  infer_instance

instance [HasColimits C] : HasColimits (CosimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

variable {C}

-- Porting note (#10688): added to ease automation
@[ext]
lemma hom_ext {X Y : CosimplicialObject C} (f g : X âŸ¶ Y)
    (h : âˆ€ (n : SimplexCategory), f.app n = g.app n) : f = g :=
  NatTrans.ext (by ext; apply h)

variable (X : CosimplicialObject C)

open Simplicial

/-- Coface maps for a cosimplicial object. -/
def Î´ {n} (i : Fin (n + 2)) : X _[n] âŸ¶ X _[n + 1] :=
  X.map (SimplexCategory.Î´ i)

/-- Codegeneracy maps for a cosimplicial object. -/
def Ïƒ {n} (i : Fin (n + 1)) : X _[n + 1] âŸ¶ X _[n] :=
  X.map (SimplexCategory.Ïƒ i)

/-- Isomorphisms from identities in â„•. -/
def eqToIso {n m : â„•} (h : n = m) : X _[n] â‰… X _[m] :=
  X.mapIso (CategoryTheory.eqToIso (by rw [h]))

@[simp]
theorem eqToIso_refl {n : â„•} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  ext
  simp [eqToIso]

/-- The generic case of the first cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    X.Î´ i â‰« X.Î´ j.succ = X.Î´ j â‰« X.Î´ (Fin.castSucc i) := by
  dsimp [Î´]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´ H]

@[reassoc]
theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Î´ i â‰« X.Î´ j =
      X.Î´ (j.pred fun (hj : j = 0) => by simp only [hj, Fin.not_lt_zero] at H) â‰«
        X.Î´ (Fin.castSucc i) := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´' H]

@[reassoc]
theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) â‰« X.Î´ j.succ =
      X.Î´ j â‰« X.Î´ i := by
  dsimp [Î´]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´'' H]

/-- The special case of the first cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} :
    X.Î´ i â‰« X.Î´ (Fin.castSucc i) = X.Î´ i â‰« X.Î´ i.succ := by
  dsimp [Î´]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´_self]

@[reassoc]
theorem Î´_comp_Î´_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = Fin.castSucc i) :
    X.Î´ i â‰« X.Î´ j = X.Î´ i â‰« X.Î´ i.succ := by
  subst H
  rw [Î´_comp_Î´_self]

/-- The second cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ (Fin.castSucc i) â‰« X.Ïƒ j.succ = X.Ïƒ j â‰« X.Î´ i := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_le H]

/-- The first part of the third cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} : X.Î´ (Fin.castSucc i) â‰« X.Ïƒ i = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_self, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Î´ j â‰« X.Ïƒ i = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_self]

/-- The second part of the third cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : X.Î´ i.succ â‰« X.Ïƒ i = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_succ, X.map_id]

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Î´ j â‰« X.Ïƒ i = ğŸ™ _ := by
  subst H
  rw [Î´_comp_Ïƒ_succ]

/-- The fourth cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Î´ i.succ â‰« X.Ïƒ (Fin.castSucc j) = X.Ïƒ j â‰« X.Î´ i := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt H]

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Î´ i â‰« X.Ïƒ j =
      X.Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) â‰«
        X.Î´ (i.pred <|
          fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt' H]

/-- The fifth cosimplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    X.Ïƒ (Fin.castSucc i) â‰« X.Ïƒ j = X.Ïƒ j.succ â‰« X.Ïƒ i := by
  dsimp [Î´, Ïƒ]
  simp only [â† X.map_comp, SimplexCategory.Ïƒ_comp_Ïƒ H]

@[reassoc (attr := simp)]
theorem Î´_naturality {X' X : CosimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 2)) :
    X.Î´ i â‰« f.app (SimplexCategory.mk (n + 1)) = f.app (SimplexCategory.mk n) â‰« X'.Î´ i :=
  f.naturality _

@[reassoc (attr := simp)]
theorem Ïƒ_naturality {X' X : CosimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« f.app (SimplexCategory.mk n) = f.app (SimplexCategory.mk (n + 1)) â‰« X'.Ïƒ i :=
  f.naturality _

variable (C)

/-- Functor composition induces a functor on cosimplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C â¥¤ D) â¥¤ CosimplicialObject C â¥¤ CosimplicialObject D :=
  whiskeringRight _ _ _

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- Truncated cosimplicial objects. -/
def Truncated (n : â„•) :=
  SimplexCategory.Truncated n â¥¤ C

instance {n : â„•} : Category (Truncated C n) := by
  dsimp [Truncated]
  infer_instance

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasLimits C] : HasLimits (CosimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  infer_instance

instance {n} [HasColimits C] : HasColimits (CosimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

variable (C)

/-- Functor composition induces a functor on truncated cosimplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C â¥¤ D) â¥¤ Truncated C n â¥¤ Truncated D n :=
  whiskeringRight _ _ _

variable {C}

end Truncated

section Skeleton

/-- The skeleton functor from cosimplicial objects to truncated cosimplicial objects. -/
def sk (n : â„•) : CosimplicialObject C â¥¤ CosimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj SimplexCategory.Truncated.inclusion

end Skeleton

variable (C)

/-- The constant cosimplicial object. -/
abbrev const : C â¥¤ CosimplicialObject C :=
  CategoryTheory.Functor.const _

-- porting note (#5171): removed @[nolint has_nonempty_instance]
/-- Augmented cosimplicial objects. -/
def Augmented :=
  Comma (const C) (ğŸ­ (CosimplicialObject C))

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  infer_instance

variable {C}

namespace Augmented

-- Porting note (#10688): added to ease automation
@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ hâ‚ hâ‚‚

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C â¥¤ CosimplicialObject C :=
  Comma.snd _ _

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C â¥¤ C :=
  Comma.fst _ _

/-- The functor from augmented objects to arrows. -/
@[simps!]
def toArrow : Augmented C â¥¤ Arrow C where
  obj X :=
    { left := point.obj X
      right := drop.obj X _[0]
      hom := X.hom.app _ }
  map Î· :=
    { left := point.map Î·
      right := (drop.map Î·).app _
      w := by
        dsimp
        rw [â† NatTrans.comp_app]
        erw [â† Î·.w]
        rfl }

variable (C)

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C â¥¤ D) : Augmented C â¥¤ Augmented D where
  obj X :=
    { left := F.obj (point.obj X)
      right := ((whiskering _ _).obj F).obj (drop.obj X)
      hom := (Functor.constComp _ _ _).inv â‰« whiskerRight X.hom F }
  map Î· :=
    { left := F.map Î·.left
      right := whiskerRight Î·.right _
      w := by
        ext
        dsimp
        rw [Category.id_comp, Category.id_comp, â† F.map_comp, â† F.map_comp, â† NatTrans.comp_app]
        erw [â† Î·.w]
        rfl }

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C â¥¤ D) â¥¤ Augmented C â¥¤ Augmented D where
  obj := whiskeringObj _ _
  map Î· :=
    { app := fun A =>
        { left := Î·.app _
          right := whiskerLeft _ Î·
          w := by
            ext n
            dsimp
            rw [Category.id_comp, Category.id_comp, Î·.naturality] }
      naturality := fun _ _ f => by ext <;> dsimp <;> simp }

variable {C}

end Augmented

open Simplicial

/-- Augment a cosimplicial object with an object. -/
@[simps]
def augment (X : CosimplicialObject C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj [0])
    (w : âˆ€ (i : SimplexCategory) (gâ‚ gâ‚‚ : ([0] : SimplexCategory) âŸ¶ i),
      f â‰« X.map gâ‚ = f â‰« X.map gâ‚‚) : CosimplicialObject.Augmented C where
  left := Xâ‚€
  right := X
  hom :=
    { app := fun i => f â‰« X.map (SimplexCategory.const _ _ 0)
      naturality := by
        intro i j g
        dsimp
        rw [Category.id_comp, Category.assoc, â† X.map_comp, w] }

-- Porting note: removed @[simp] as the linter complains
theorem augment_hom_zero (X : CosimplicialObject C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj [0]) (w) :
    (X.augment Xâ‚€ f w).hom.app [0] = f := by simp

end CosimplicialObject

/-- The anti-equivalence between simplicial objects and cosimplicial objects. -/
@[simps!]
def simplicialCosimplicialEquiv : (SimplicialObject C)áµ’áµ– â‰Œ CosimplicialObject Cáµ’áµ– :=
  Functor.leftOpRightOpEquiv _ _

/-- The anti-equivalence between cosimplicial objects and simplicial objects. -/
@[simps!]
def cosimplicialSimplicialEquiv : (CosimplicialObject C)áµ’áµ– â‰Œ SimplicialObject Cáµ’áµ– :=
  Functor.opUnopEquiv _ _

variable {C}

/-- Construct an augmented cosimplicial object in the opposite
category from an augmented simplicial object. -/
@[simps!]
def SimplicialObject.Augmented.rightOp (X : SimplicialObject.Augmented C) :
    CosimplicialObject.Augmented Cáµ’áµ– where
  left := Opposite.op X.right
  right := X.left.rightOp
  hom := NatTrans.rightOp X.hom

/-- Construct an augmented simplicial object from an augmented cosimplicial
object in the opposite category. -/
@[simps!]
def CosimplicialObject.Augmented.leftOp (X : CosimplicialObject.Augmented Cáµ’áµ–) :
    SimplicialObject.Augmented C where
  left := X.right.leftOp
  right := X.left.unop
  hom := NatTrans.leftOp X.hom

/-- Converting an augmented simplicial object to an augmented cosimplicial
object and back is isomorphic to the given object. -/
@[simps!]
def SimplicialObject.Augmented.rightOpLeftOpIso (X : SimplicialObject.Augmented C) :
    X.rightOp.leftOp â‰… X :=
  Comma.isoMk X.left.rightOpLeftOpIso (CategoryTheory.eqToIso <| by aesop_cat)

/-- Converting an augmented cosimplicial object to an augmented simplicial
object and back is isomorphic to the given object. -/
@[simps!]
def CosimplicialObject.Augmented.leftOpRightOpIso (X : CosimplicialObject.Augmented Cáµ’áµ–) :
    X.leftOp.rightOp â‰… X :=
  Comma.isoMk (CategoryTheory.eqToIso <| by simp) X.right.leftOpRightOpIso

variable (C)

/-- A functorial version of `SimplicialObject.Augmented.rightOp`. -/
@[simps]
def simplicialToCosimplicialAugmented :
    (SimplicialObject.Augmented C)áµ’áµ– â¥¤ CosimplicialObject.Augmented Cáµ’áµ– where
  obj X := X.unop.rightOp
  map f :=
    { left := f.unop.right.op
      right := NatTrans.rightOp f.unop.left
      w := by
        ext x
        dsimp
        simp_rw [â† op_comp]
        congr 1
        exact (congr_app f.unop.w (op x)).symm }

/-- A functorial version of `Cosimplicial_object.Augmented.leftOp`. -/
@[simps]
def cosimplicialToSimplicialAugmented :
    CosimplicialObject.Augmented Cáµ’áµ– â¥¤ (SimplicialObject.Augmented C)áµ’áµ– where
  obj X := Opposite.op X.leftOp
  map f :=
    Quiver.Hom.op <|
      { left := NatTrans.leftOp f.right
        right := f.left.unop
        w := by
          ext x
          dsimp
          simp_rw [â† unop_comp]
          congr 1
          exact (congr_app f.w (unop x)).symm }

/-- The contravariant categorical equivalence between augmented simplicial
objects and augmented cosimplicial objects in the opposite category. -/
@[simps! functor inverse]
def simplicialCosimplicialAugmentedEquiv :
    (SimplicialObject.Augmented C)áµ’áµ– â‰Œ CosimplicialObject.Augmented Cáµ’áµ– where
  functor := simplicialToCosimplicialAugmented _
  inverse := cosimplicialToSimplicialAugmented _
  unitIso := NatIso.ofComponents (fun X => X.unop.rightOpLeftOpIso.op) fun f => by
      dsimp
      rw [â† f.op_unop]
      simp_rw [â† op_comp]
      congr 1
      aesop_cat
  counitIso := NatIso.ofComponents fun X => X.leftOpRightOpIso

end CategoryTheory
