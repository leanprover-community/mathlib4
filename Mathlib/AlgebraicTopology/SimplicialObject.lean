/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison, Adam Topaz
-/
import Mathlib.AlgebraicTopology.SimplexCategory
import Mathlib.CategoryTheory.Arrow
import Mathlib.CategoryTheory.Limits.FunctorCategory
import Mathlib.CategoryTheory.Opposites

#align_import algebraic_topology.simplicial_object from "leanprover-community/mathlib"@"5ed51dc37c6b891b79314ee11a50adc2b1df6fd6"

/-!
# Simplicial objects in a category.

A simplicial object in a category `C` is a `C`-valued presheaf on `SimplexCategory`.
(Similarly a cosimplicial object is functor `SimplexCategory â¥¤ C`.)

Use the notation `X _[n]` in the `Simplicial` locale to obtain the `n`-th term of a
(co)simplicial object `X`, where `n` is a natural number.

-/

set_option autoImplicit true


open Opposite

open CategoryTheory

open CategoryTheory.Limits

universe v u v' u'

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

-- porting note: removed @[nolint has_nonempty_instance]
/-- The category of simplicial objects valued in a category `C`.
This is the category of contravariant functors from `SimplexCategory` to `C`. -/
def SimplicialObject :=
  SimplexCategoryáµ’áµ– â¥¤ C
#align category_theory.simplicial_object CategoryTheory.SimplicialObject

@[simps!]
instance : Category (SimplicialObject C) := by
  dsimp only [SimplicialObject]
  -- âŠ¢ Category.{?u.61, max u v} (SimplexCategoryáµ’áµ– â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

namespace SimplicialObject

set_option quotPrecheck false in
/-- `X _[n]` denotes the `n`th-term of the simplicial object X -/
scoped[Simplicial]
  notation:1000 X " _[" n "]" =>
    (X : CategoryTheory.SimplicialObject _).obj (Opposite.op (SimplexCategory.mk n))

open Simplicial

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  -- âŠ¢ HasLimitsOfShape J (SimplexCategoryáµ’áµ– â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance [HasLimits C] : HasLimits (SimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject C) := by
  dsimp [SimplicialObject]
  -- âŠ¢ HasColimitsOfShape J (SimplexCategoryáµ’áµ– â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance [HasColimits C] : HasColimits (SimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

variable {C}

-- porting note: added to ease automation
@[ext]
lemma hom_ext {X Y : SimplicialObject C} (f g : X âŸ¶ Y)
  (h : âˆ€ (n : SimplexCategoryáµ’áµ–), f.app n = g.app n) : f = g :=
  NatTrans.ext _ _ (by ext; apply h)
                       -- âŠ¢ NatTrans.app f xâœ = NatTrans.app g xâœ
                            -- ğŸ‰ no goals

variable (X : SimplicialObject C)

/-- Face maps for a simplicial object. -/
def Î´ {n} (i : Fin (n + 2)) : X _[n + 1] âŸ¶ X _[n] :=
  X.map (SimplexCategory.Î´ i).op
#align category_theory.simplicial_object.Î´ CategoryTheory.SimplicialObject.Î´

/-- Degeneracy maps for a simplicial object. -/
def Ïƒ {n} (i : Fin (n + 1)) : X _[n] âŸ¶ X _[n + 1] :=
  X.map (SimplexCategory.Ïƒ i).op
#align category_theory.simplicial_object.Ïƒ CategoryTheory.SimplicialObject.Ïƒ

/-- Isomorphisms from identities in â„•. -/
def eqToIso {n m : â„•} (h : n = m) : X _[n] â‰… X _[m] :=
  X.mapIso (CategoryTheory.eqToIso (by congr))
                                       -- ğŸ‰ no goals
#align category_theory.simplicial_object.eq_to_iso CategoryTheory.SimplicialObject.eqToIso

@[simp]
theorem eqToIso_refl {n : â„•} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  ext
  -- âŠ¢ (eqToIso X h).hom = (Iso.refl (X.obj (op [n]))).hom
  simp [eqToIso]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.eq_to_iso_refl CategoryTheory.SimplicialObject.eqToIso_refl

/-- The generic case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    X.Î´ j.succ â‰« X.Î´ i = X.Î´ (Fin.castSucc i) â‰« X.Î´ j := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.succ j)).op â‰« X.map (SimplexCategory.Î´ i).op = â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´ H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Î´ CategoryTheory.SimplicialObject.Î´_comp_Î´

@[reassoc]
theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Î´ j â‰« X.Î´ i =
      X.Î´ (Fin.castSucc i) â‰«
        X.Î´ (j.pred <| fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) := by
                                              -- ğŸ‰ no goals
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ j).op â‰« X.map (SimplexCategory.Î´ i).op = X.map (Sim â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´' H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Î´' CategoryTheory.SimplicialObject.Î´_comp_Î´'
@[reassoc]
theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ j.succ â‰« X.Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) =
      X.Î´ i â‰« X.Î´ j := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.succ j)).op â‰« X.map (SimplexCategory.Î´ (Fin.ca â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´'' H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Î´'' CategoryTheory.SimplicialObject.Î´_comp_Î´''

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} :
    X.Î´ (Fin.castSucc i) â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.castSucc i)).op â‰« X.map (SimplexCategory.Î´ i). â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´_self]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Î´_self CategoryTheory.SimplicialObject.Î´_comp_Î´_self

@[reassoc]
theorem Î´_comp_Î´_self' {n} {j : Fin (n + 3)} {i : Fin (n + 2)} (H : j = Fin.castSucc i) :
    X.Î´ j â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i := by
  subst H
  -- âŠ¢ Î´ X (Fin.castSucc i) â‰« Î´ X i = Î´ X (Fin.succ i) â‰« Î´ X i
  rw [Î´_comp_Î´_self]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Î´_self' CategoryTheory.SimplicialObject.Î´_comp_Î´_self'

/-- The second simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    X.Ïƒ j.succ â‰« X.Î´ (Fin.castSucc i) = X.Î´ i â‰« X.Ïƒ j := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ (Fin.succ j)).op â‰« X.map (SimplexCategory.Î´ (Fin.ca â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_le H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_of_le CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_of_le

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} : X.Ïƒ i â‰« X.Î´ (Fin.castSucc i) = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ i).op â‰« X.map (SimplexCategory.Î´ (Fin.castSucc i)). â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_self, op_id, X.map_id]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_self CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_self

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Ïƒ i â‰« X.Î´ j = ğŸ™ _ := by
  subst H
  -- âŠ¢ Ïƒ X i â‰« Î´ X (Fin.castSucc i) = ğŸ™ (X.obj (op [n]))
  rw [Î´_comp_Ïƒ_self]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_self' CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_self'

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : X.Ïƒ i â‰« X.Î´ i.succ = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ i).op â‰« X.map (SimplexCategory.Î´ (Fin.succ i)).op = â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_succ, op_id, X.map_id]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_succ CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_succ

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Ïƒ i â‰« X.Î´ j = ğŸ™ _ := by
  subst H
  -- âŠ¢ Ïƒ X i â‰« Î´ X (Fin.succ i) = ğŸ™ (X.obj (op [n]))
  rw [Î´_comp_Ïƒ_succ]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_succ' CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_succ'

/-- The fourth simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Ïƒ (Fin.castSucc j) â‰« X.Î´ i.succ = X.Î´ i â‰« X.Ïƒ j := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ (Fin.castSucc j)).op â‰« X.map (SimplexCategory.Î´ (Fi â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_of_gt CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_of_gt

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Ïƒ j â‰« X.Î´ i =
      X.Î´ (i.pred <| fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) â‰«
                                            -- ğŸ‰ no goals
        X.Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ j).op â‰« X.map (SimplexCategory.Î´ i).op = X.map (Sim â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt' H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Î´_comp_Ïƒ_of_gt' CategoryTheory.SimplicialObject.Î´_comp_Ïƒ_of_gt'

/-- The fifth simplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    X.Ïƒ j â‰« X.Ïƒ (Fin.castSucc i) = X.Ïƒ i â‰« X.Ïƒ j.succ := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ j).op â‰« X.map (SimplexCategory.Ïƒ (Fin.castSucc i)). â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Ïƒ_comp_Ïƒ H]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.Ïƒ_comp_Ïƒ CategoryTheory.SimplicialObject.Ïƒ_comp_Ïƒ

open Simplicial

@[reassoc (attr := simp)]
theorem Î´_naturality {X' X : SimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 2)) :
    X.Î´ i â‰« f.app (op [n]) = f.app (op [n + 1]) â‰« X'.Î´ i :=
  f.naturality _
#align category_theory.simplicial_object.Î´_naturality CategoryTheory.SimplicialObject.Î´_naturality

@[reassoc (attr := simp)]
theorem Ïƒ_naturality {X' X : SimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« f.app (op [n + 1]) = f.app (op [n]) â‰« X'.Ïƒ i :=
  f.naturality _
#align category_theory.simplicial_object.Ïƒ_naturality CategoryTheory.SimplicialObject.Ïƒ_naturality

variable (C)

/-- Functor composition induces a functor on simplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C â¥¤ D) â¥¤ SimplicialObject C â¥¤ SimplicialObject D :=
  whiskeringRight _ _ _
#align category_theory.simplicial_object.whiskering CategoryTheory.SimplicialObject.whiskering

-- porting note: removed @[nolint has_nonempty_instance]
/-- Truncated simplicial objects. -/
def Truncated (n : â„•) :=
  (SimplexCategory.Truncated n)áµ’áµ– â¥¤ C
#align category_theory.simplicial_object.truncated CategoryTheory.SimplicialObject.Truncated

instance : Category (Truncated C n) := by
  dsimp [Truncated]
  -- âŠ¢ Category.{?u.68325, max u v} ((SimplexCategory.Truncated n)áµ’áµ– â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  -- âŠ¢ HasLimitsOfShape J ((SimplexCategory.Truncated n)áµ’áµ– â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance {n} [HasLimits C] : HasLimits (SimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (SimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  -- âŠ¢ HasColimitsOfShape J ((SimplexCategory.Truncated n)áµ’áµ– â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance {n} [HasColimits C] : HasColimits (SimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

variable (C)

/-- Functor composition induces a functor on truncated simplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C â¥¤ D) â¥¤ Truncated C n â¥¤ Truncated D n :=
  whiskeringRight _ _ _
#align category_theory.simplicial_object.truncated.whiskering CategoryTheory.SimplicialObject.Truncated.whiskering

variable {C}

end Truncated

section Skeleton

/-- The skeleton functor from simplicial objects to truncated simplicial objects. -/
def sk (n : â„•) : SimplicialObject C â¥¤ SimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj SimplexCategory.Truncated.inclusion.op
#align category_theory.simplicial_object.sk CategoryTheory.SimplicialObject.sk

end Skeleton

variable (C)

/-- The constant simplicial object is the constant functor. -/
abbrev const : C â¥¤ SimplicialObject C :=
  CategoryTheory.Functor.const _
#align category_theory.simplicial_object.const CategoryTheory.SimplicialObject.const

-- porting note: removed @[nolint has_nonempty_instance]
/-- The category of augmented simplicial objects, defined as a comma category. -/
def Augmented :=
  Comma (ğŸ­ (SimplicialObject C)) (const C)
#align category_theory.simplicial_object.augmented CategoryTheory.SimplicialObject.Augmented

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  -- âŠ¢ Category.{?u.76503, max u v} (Comma (ğŸ­ (SimplicialObject C)) (const C))
  infer_instance
  -- ğŸ‰ no goals

variable {C}

namespace Augmented

-- porting note: added to ease automation
@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ hâ‚ hâ‚‚

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C â¥¤ SimplicialObject C :=
  Comma.fst _ _
#align category_theory.simplicial_object.augmented.drop CategoryTheory.SimplicialObject.Augmented.drop

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C â¥¤ C :=
  Comma.snd _ _
#align category_theory.simplicial_object.augmented.point CategoryTheory.SimplicialObject.Augmented.point

/-- The functor from augmented objects to arrows. -/
@[simps]
def toArrow : Augmented C â¥¤ Arrow C where
  obj X :=
    { left := drop.obj X _[0]
      right := point.obj X
      hom := X.hom.app _ }
  map Î· :=
    { left := (drop.map Î·).app _
      right := point.map Î·
      w := by
        dsimp
        -- âŠ¢ NatTrans.app Î·.left (op [0]) â‰« NatTrans.app Yâœ.hom (op [0]) = NatTrans.app X â€¦
        rw [â† NatTrans.comp_app]
        -- âŠ¢ NatTrans.app (Î·.left â‰« Yâœ.hom) (op [0]) = NatTrans.app Xâœ.hom (op [0]) â‰« Î·.r â€¦
        erw [Î·.w]
        -- âŠ¢ NatTrans.app (Xâœ.hom â‰« (const C).map Î·.right) (op [0]) = NatTrans.app Xâœ.hom â€¦
        rfl }
        -- ğŸ‰ no goals
#align category_theory.simplicial_object.augmented.to_arrow CategoryTheory.SimplicialObject.Augmented.toArrow

/-- The compatibility of a morphism with the augmentation, on 0-simplices -/
@[reassoc]
theorem wâ‚€ {X Y : Augmented C} (f : X âŸ¶ Y) :
    (Augmented.drop.map f).app (op (SimplexCategory.mk 0)) â‰« Y.hom.app (op (SimplexCategory.mk 0)) =
      X.hom.app (op (SimplexCategory.mk 0)) â‰« Augmented.point.map f :=
  by convert congr_app f.w (op (SimplexCategory.mk 0))
     -- ğŸ‰ no goals
#align category_theory.simplicial_object.augmented.wâ‚€ CategoryTheory.SimplicialObject.Augmented.wâ‚€

variable (C)

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C â¥¤ D) : Augmented C â¥¤ Augmented D where
  obj X :=
    { left := ((whiskering _ _).obj F).obj (drop.obj X)
      right := F.obj (point.obj X)
      hom := whiskerRight X.hom F â‰« (Functor.constComp _ _ _).hom }
  map Î· :=
    { left := whiskerRight Î·.left _
      right := F.map Î·.right
      w := by
        ext
        -- âŠ¢ NatTrans.app ((ğŸ­ (SimplicialObject D)).map (whiskerRight Î·.left F) â‰« ((fun X â€¦
        dsimp [whiskerRight]
        -- âŠ¢ F.map (NatTrans.app Î·.left nâœ) â‰« F.map (NatTrans.app Yâœ.hom nâœ) â‰« ğŸ™ (F.obj Y â€¦
        simp only [Category.comp_id, â† F.map_comp, â† NatTrans.comp_app]
        -- âŠ¢ F.map (NatTrans.app (Î·.left â‰« Yâœ.hom) nâœ) = F.map (NatTrans.app Xâœ.hom nâœ â‰«  â€¦
        erw [Î·.w]
        -- âŠ¢ F.map (NatTrans.app (Xâœ.hom â‰« (const C).map Î·.right) nâœ) = F.map (NatTrans.a â€¦
        rfl }
        -- ğŸ‰ no goals
#align category_theory.simplicial_object.augmented.whiskering_obj CategoryTheory.SimplicialObject.Augmented.whiskeringObj

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C â¥¤ D) â¥¤ Augmented C â¥¤ Augmented D where
  obj := whiskeringObj _ _
  map Î· :=
    { app := fun A =>
        { left := whiskerLeft _ Î·
          right := Î·.app _
          w := by
            ext n
            -- âŠ¢ NatTrans.app ((ğŸ­ (SimplicialObject D)).map (whiskerLeft (drop.obj A) Î·) â‰« (( â€¦
            dsimp
            -- âŠ¢ NatTrans.app Î· (A.left.obj n) â‰« Yâœ.map (NatTrans.app A.hom n) â‰« ğŸ™ (Yâœ.obj A. â€¦
            rw [Category.comp_id, Category.comp_id, Î·.naturality] } }
            -- ğŸ‰ no goals
  map_comp := fun _ _ => by ext <;> rfl
                            -- âŠ¢ NatTrans.app (NatTrans.app ({ obj := whiskeringObj C D, map := fun {X Y} Î· = â€¦
                                    -- ğŸ‰ no goals
                                    -- ğŸ‰ no goals
#align category_theory.simplicial_object.augmented.whiskering CategoryTheory.SimplicialObject.Augmented.whiskering

variable {C}

end Augmented

/-- Augment a simplicial object with an object. -/
@[simps]
def augment (X : SimplicialObject C) (Xâ‚€ : C) (f : X _[0] âŸ¶ Xâ‚€)
    (w : âˆ€ (i : SimplexCategory) (gâ‚ gâ‚‚ : ([0] : SimplexCategory) âŸ¶ i),
      X.map gâ‚.op â‰« f = X.map gâ‚‚.op â‰« f) :
    SimplicialObject.Augmented C where
  left := X
  right := Xâ‚€
  hom :=
    { app := fun i => X.map (SimplexCategory.const i.unop 0).op â‰« f
      naturality := by
        intro i j g
        -- âŠ¢ ((ğŸ­ (SimplicialObject C)).obj X).map g â‰« (fun i => X.map (SimplexCategory.co â€¦
        dsimp
        -- âŠ¢ X.map g â‰« X.map (SimplexCategory.const j.unop 0).op â‰« f = (X.map (SimplexCat â€¦
        rw [â† g.op_unop]
        -- âŠ¢ X.map g.unop.op â‰« X.map (SimplexCategory.const j.unop 0).op â‰« f = (X.map (Si â€¦
        simpa only [â† X.map_comp, â† Category.assoc, Category.comp_id, â† op_comp] using w _ _ _ }
        -- ğŸ‰ no goals
#align category_theory.simplicial_object.augment CategoryTheory.SimplicialObject.augment

-- porting note: removed @[simp] as the linter complains
theorem augment_hom_zero (X : SimplicialObject C) (Xâ‚€ : C) (f : X _[0] âŸ¶ Xâ‚€) (w) :
    (X.augment Xâ‚€ f w).hom.app (op [0]) = f := by
  dsimp
  -- âŠ¢ X.map (SimplexCategory.const [0] 0).op â‰« f = f
  rw [SimplexCategory.hom_zero_zero ([0].const 0), op_id, X.map_id, Category.id_comp]
  -- ğŸ‰ no goals
#align category_theory.simplicial_object.augment_hom_zero CategoryTheory.SimplicialObject.augment_hom_zero

end SimplicialObject

-- porting note: removed @[nolint has_nonempty_instance]
/-- Cosimplicial objects. -/
def CosimplicialObject :=
  SimplexCategory â¥¤ C
#align category_theory.cosimplicial_object CategoryTheory.CosimplicialObject

@[simps!]
instance : Category (CosimplicialObject C) := by
  dsimp only [CosimplicialObject]
  -- âŠ¢ Category.{?u.208707, max u v} (SimplexCategory â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

namespace CosimplicialObject

-- mathport name: cosimplicial_object.at
set_option quotPrecheck false in
/-- `X _[n]` denotes the `n`th-term of the cosimplicial object X -/
scoped[Simplicial]
  notation:1000 X " _[" n "]" =>
    (X : CategoryTheory.CosimplicialObject _).obj (SimplexCategory.mk n)

instance {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  -- âŠ¢ HasLimitsOfShape J (SimplexCategory â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance [HasLimits C] : HasLimits (CosimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

instance {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject C) := by
  dsimp [CosimplicialObject]
  -- âŠ¢ HasColimitsOfShape J (SimplexCategory â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance [HasColimits C] : HasColimits (CosimplicialObject C) :=
  âŸ¨inferInstanceâŸ©

variable {C}

-- porting note: added to ease automation
@[ext]
lemma hom_ext {X Y : CosimplicialObject C} (f g : X âŸ¶ Y)
  (h : âˆ€ (n : SimplexCategory), f.app n = g.app n) : f = g :=
  NatTrans.ext _ _ (by ext; apply h)
                       -- âŠ¢ NatTrans.app f xâœ = NatTrans.app g xâœ
                            -- ğŸ‰ no goals

variable (X : CosimplicialObject C)

open Simplicial

/-- Coface maps for a cosimplicial object. -/
def Î´ {n} (i : Fin (n + 2)) : X _[n] âŸ¶ X _[n + 1] :=
  X.map (SimplexCategory.Î´ i)
#align category_theory.cosimplicial_object.Î´ CategoryTheory.CosimplicialObject.Î´

/-- Codegeneracy maps for a cosimplicial object. -/
def Ïƒ {n} (i : Fin (n + 1)) : X _[n + 1] âŸ¶ X _[n] :=
  X.map (SimplexCategory.Ïƒ i)
#align category_theory.cosimplicial_object.Ïƒ CategoryTheory.CosimplicialObject.Ïƒ

/-- Isomorphisms from identities in â„•. -/
def eqToIso {n m : â„•} (h : n = m) : X _[n] â‰… X _[m] :=
  X.mapIso (CategoryTheory.eqToIso (by rw [h]))
                                       -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.eq_to_iso CategoryTheory.CosimplicialObject.eqToIso

@[simp]
theorem eqToIso_refl {n : â„•} (h : n = n) : X.eqToIso h = Iso.refl _ := by
  ext
  -- âŠ¢ (eqToIso X h).hom = (Iso.refl (X.obj [n])).hom
  simp [eqToIso]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.eq_to_iso_refl CategoryTheory.CosimplicialObject.eqToIso_refl

/-- The generic case of the first cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    X.Î´ i â‰« X.Î´ j.succ = X.Î´ j â‰« X.Î´ (Fin.castSucc i) := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ i) â‰« X.map (SimplexCategory.Î´ (Fin.succ j)) = X.map â€¦
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´ H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Î´ CategoryTheory.CosimplicialObject.Î´_comp_Î´

@[reassoc]
theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : Fin.castSucc i < j) :
    X.Î´ i â‰« X.Î´ j =
      X.Î´ (j.pred <| fun (hj : j = 0) => by simp only [hj, Fin.not_lt_zero] at H) â‰«
                                            -- ğŸ‰ no goals
        X.Î´ (Fin.castSucc i) := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ i) â‰« X.map (SimplexCategory.Î´ j) = X.map (SimplexCa â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´' H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Î´' CategoryTheory.CosimplicialObject.Î´_comp_Î´'

@[reassoc]
theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) â‰« X.Î´ j.succ =
      X.Î´ j â‰« X.Î´ i := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.castLT i (_ : â†‘i < n + 2))) â‰« X.map (SimplexCa â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Î´'' H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Î´'' CategoryTheory.CosimplicialObject.Î´_comp_Î´''

/-- The special case of the first cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} :
    X.Î´ i â‰« X.Î´ (Fin.castSucc i) = X.Î´ i â‰« X.Î´ i.succ := by
  dsimp [Î´]
  -- âŠ¢ X.map (SimplexCategory.Î´ i) â‰« X.map (SimplexCategory.Î´ (Fin.castSucc i)) = X â€¦
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Î´_self]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Î´_self CategoryTheory.CosimplicialObject.Î´_comp_Î´_self

@[reassoc]
theorem Î´_comp_Î´_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = Fin.castSucc i) :
    X.Î´ i â‰« X.Î´ j = X.Î´ i â‰« X.Î´ i.succ := by
  subst H
  -- âŠ¢ Î´ X i â‰« Î´ X (Fin.castSucc i) = Î´ X i â‰« Î´ X (Fin.succ i)
  rw [Î´_comp_Î´_self]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Î´_self' CategoryTheory.CosimplicialObject.Î´_comp_Î´_self'

/-- The second cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ Fin.castSucc j) :
    X.Î´ (Fin.castSucc i) â‰« X.Ïƒ j.succ = X.Ïƒ j â‰« X.Î´ i := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.castSucc i)) â‰« X.map (SimplexCategory.Ïƒ (Fin.s â€¦
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_le H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_of_le CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_of_le

/-- The first part of the third cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} : X.Î´ (Fin.castSucc i) â‰« X.Ïƒ i = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.castSucc i)) â‰« X.map (SimplexCategory.Ïƒ i) = ğŸ™ â€¦
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_self, X.map_id]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_self CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_self

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = Fin.castSucc i) :
    X.Î´ j â‰« X.Ïƒ i = ğŸ™ _ := by
  subst H
  -- âŠ¢ Î´ X (Fin.castSucc i) â‰« Ïƒ X i = ğŸ™ (X.obj [n])
  rw [Î´_comp_Ïƒ_self]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_self' CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_self'

/-- The second part of the third cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : X.Î´ i.succ â‰« X.Ïƒ i = ğŸ™ _ := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.succ i)) â‰« X.map (SimplexCategory.Ïƒ i) = ğŸ™ (X. â€¦
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_succ, X.map_id]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_succ CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_succ

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    X.Î´ j â‰« X.Ïƒ i = ğŸ™ _ := by
  subst H
  -- âŠ¢ Î´ X (Fin.succ i) â‰« Ïƒ X i = ğŸ™ (X.obj [n])
  rw [Î´_comp_Ïƒ_succ]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_succ' CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_succ'

/-- The fourth cosimplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : Fin.castSucc j < i) :
    X.Î´ i.succ â‰« X.Ïƒ (Fin.castSucc j) = X.Ïƒ j â‰« X.Î´ i := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Î´ (Fin.succ i)) â‰« X.map (SimplexCategory.Ïƒ (Fin.castS â€¦
  simp only [â† X.map_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_of_gt CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_of_gt

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    X.Î´ i â‰« X.Ïƒ j =
      X.Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) â‰«
        X.Î´ (i.pred <|
          fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
                                 -- ğŸ‰ no goals
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Î´ i) â‰« X.map (SimplexCategory.Ïƒ j) = X.map (SimplexCa â€¦
  simp only [â† X.map_comp, â† op_comp, SimplexCategory.Î´_comp_Ïƒ_of_gt' H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Î´_comp_Ïƒ_of_gt' CategoryTheory.CosimplicialObject.Î´_comp_Ïƒ_of_gt'

/-- The fifth cosimplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    X.Ïƒ (Fin.castSucc i) â‰« X.Ïƒ j = X.Ïƒ j.succ â‰« X.Ïƒ i := by
  dsimp [Î´, Ïƒ]
  -- âŠ¢ X.map (SimplexCategory.Ïƒ (Fin.castSucc i)) â‰« X.map (SimplexCategory.Ïƒ j) = X â€¦
  simp only [â† X.map_comp, SimplexCategory.Ïƒ_comp_Ïƒ H]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.Ïƒ_comp_Ïƒ CategoryTheory.CosimplicialObject.Ïƒ_comp_Ïƒ

@[reassoc (attr := simp)]
theorem Î´_naturality {X' X : CosimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 2)) :
    X.Î´ i â‰« f.app (SimplexCategory.mk (n + 1)) = f.app (SimplexCategory.mk n) â‰« X'.Î´ i :=
  f.naturality _
#align category_theory.cosimplicial_object.Î´_naturality CategoryTheory.CosimplicialObject.Î´_naturality

@[reassoc (attr := simp)]
theorem Ïƒ_naturality {X' X : CosimplicialObject C} (f : X âŸ¶ X') {n : â„•} (i : Fin (n + 1)) :
    X.Ïƒ i â‰« f.app (SimplexCategory.mk n) = f.app (SimplexCategory.mk (n + 1)) â‰« X'.Ïƒ i :=
  f.naturality _
#align category_theory.cosimplicial_object.Ïƒ_naturality CategoryTheory.CosimplicialObject.Ïƒ_naturality

variable (C)

/-- Functor composition induces a functor on cosimplicial objects. -/
@[simps!]
def whiskering (D : Type*) [Category D] : (C â¥¤ D) â¥¤ CosimplicialObject C â¥¤ CosimplicialObject D :=
  whiskeringRight _ _ _
#align category_theory.cosimplicial_object.whiskering CategoryTheory.CosimplicialObject.whiskering

-- porting note: removed @[nolint has_nonempty_instance]
/-- Truncated cosimplicial objects. -/
def Truncated (n : â„•) :=
  SimplexCategory.Truncated n â¥¤ C
#align category_theory.cosimplicial_object.truncated CategoryTheory.CosimplicialObject.Truncated

instance : Category (Truncated C n) := by
  dsimp [Truncated]
  -- âŠ¢ Category.{?u.295628, max u v} (SimplexCategory.Truncated n â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

variable {C}

namespace Truncated

instance {n} {J : Type v} [SmallCategory J] [HasLimitsOfShape J C] :
    HasLimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  -- âŠ¢ HasLimitsOfShape J (SimplexCategory.Truncated n â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance {n} [HasLimits C] : HasLimits (CosimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

instance {n} {J : Type v} [SmallCategory J] [HasColimitsOfShape J C] :
    HasColimitsOfShape J (CosimplicialObject.Truncated C n) := by
  dsimp [Truncated]
  -- âŠ¢ HasColimitsOfShape J (SimplexCategory.Truncated n â¥¤ C)
  infer_instance
  -- ğŸ‰ no goals

instance {n} [HasColimits C] : HasColimits (CosimplicialObject.Truncated C n) :=
  âŸ¨inferInstanceâŸ©

variable (C)

/-- Functor composition induces a functor on truncated cosimplicial objects. -/
@[simps!]
def whiskering {n} (D : Type*) [Category D] : (C â¥¤ D) â¥¤ Truncated C n â¥¤ Truncated D n :=
  whiskeringRight _ _ _
#align category_theory.cosimplicial_object.truncated.whiskering CategoryTheory.CosimplicialObject.Truncated.whiskering

variable {C}

end Truncated

section Skeleton

/-- The skeleton functor from cosimplicial objects to truncated cosimplicial objects. -/
def sk (n : â„•) : CosimplicialObject C â¥¤ CosimplicialObject.Truncated C n :=
  (whiskeringLeft _ _ _).obj SimplexCategory.Truncated.inclusion
#align category_theory.cosimplicial_object.sk CategoryTheory.CosimplicialObject.sk

end Skeleton

variable (C)

/-- The constant cosimplicial object. -/
abbrev const : C â¥¤ CosimplicialObject C :=
  CategoryTheory.Functor.const _
#align category_theory.cosimplicial_object.const CategoryTheory.CosimplicialObject.const

-- porting note: removed @[nolint has_nonempty_instance]
/-- Augmented cosimplicial objects. -/
def Augmented :=
  Comma (const C) (ğŸ­ (CosimplicialObject C))
#align category_theory.cosimplicial_object.augmented CategoryTheory.CosimplicialObject.Augmented

@[simps!]
instance : Category (Augmented C) := by
  dsimp only [Augmented]
  -- âŠ¢ Category.{?u.303800, max u v} (Comma (const C) (ğŸ­ (CosimplicialObject C)))
  infer_instance
  -- ğŸ‰ no goals

variable {C}

namespace Augmented

-- porting note: added to ease automation
@[ext]
lemma hom_ext {X Y : Augmented C} (f g : X âŸ¶ Y) (hâ‚ : f.left = g.left) (hâ‚‚ : f.right = g.right) :
    f = g :=
  Comma.hom_ext _ _ hâ‚ hâ‚‚

/-- Drop the augmentation. -/
@[simps!]
def drop : Augmented C â¥¤ CosimplicialObject C :=
  Comma.snd _ _
#align category_theory.cosimplicial_object.augmented.drop CategoryTheory.CosimplicialObject.Augmented.drop

/-- The point of the augmentation. -/
@[simps!]
def point : Augmented C â¥¤ C :=
  Comma.fst _ _
#align category_theory.cosimplicial_object.augmented.point CategoryTheory.CosimplicialObject.Augmented.point

/-- The functor from augmented objects to arrows. -/
@[simps!]
def toArrow : Augmented C â¥¤ Arrow C where
  obj X :=
    { left := point.obj X
      right := drop.obj X _[0]
      hom := X.hom.app _ }
  map Î· :=
    { left := point.map Î·
      right := (drop.map Î·).app _
      w := by
        dsimp
        -- âŠ¢ Î·.left â‰« NatTrans.app Yâœ.hom [0] = NatTrans.app Xâœ.hom [0] â‰« NatTrans.app Î·. â€¦
        rw [â† NatTrans.comp_app]
        -- âŠ¢ Î·.left â‰« NatTrans.app Yâœ.hom [0] = NatTrans.app (Xâœ.hom â‰« Î·.right) [0]
        erw [â† Î·.w]
        -- âŠ¢ Î·.left â‰« NatTrans.app Yâœ.hom [0] = NatTrans.app ((const C).map Î·.left â‰« Yâœ.h â€¦
        rfl }
        -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.augmented.to_arrow CategoryTheory.CosimplicialObject.Augmented.toArrow

variable (C)

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simp]
def whiskeringObj (D : Type*) [Category D] (F : C â¥¤ D) : Augmented C â¥¤ Augmented D where
  obj X :=
    { left := F.obj (point.obj X)
      right := ((whiskering _ _).obj F).obj (drop.obj X)
      hom := (Functor.constComp _ _ _).inv â‰« whiskerRight X.hom F }
  map Î· :=
    { left := F.map Î·.left
      right := whiskerRight Î·.right _
      w := by
        ext
        -- âŠ¢ NatTrans.app ((const D).map (F.map Î·.left) â‰« ((fun X => { left := F.obj (poi â€¦
        dsimp
        -- âŠ¢ F.map Î·.left â‰« ğŸ™ (F.obj Yâœ.left) â‰« F.map (NatTrans.app Yâœ.hom nâœ) = (ğŸ™ (F.ob â€¦
        rw [Category.id_comp, Category.id_comp, â† F.map_comp, â† F.map_comp, â† NatTrans.comp_app]
        -- âŠ¢ F.map (Î·.left â‰« NatTrans.app Yâœ.hom nâœ) = F.map (NatTrans.app (Xâœ.hom â‰« Î·.ri â€¦
        erw [â† Î·.w]
        -- âŠ¢ F.map (Î·.left â‰« NatTrans.app Yâœ.hom nâœ) = F.map (NatTrans.app ((const C).map â€¦
        rfl }
        -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.augmented.whiskering_obj CategoryTheory.CosimplicialObject.Augmented.whiskeringObj

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simps]
def whiskering (D : Type u') [Category.{v'} D] : (C â¥¤ D) â¥¤ Augmented C â¥¤ Augmented D where
  obj := whiskeringObj _ _
  map Î· :=
    { app := fun A =>
        { left := Î·.app _
          right := whiskerLeft _ Î·
          w := by
            ext n
            -- âŠ¢ NatTrans.app ((const D).map (NatTrans.app Î· (point.obj A)) â‰« ((whiskeringObj â€¦
            dsimp
            -- âŠ¢ NatTrans.app Î· A.left â‰« ğŸ™ (Yâœ.obj A.left) â‰« Yâœ.map (NatTrans.app A.hom n) =  â€¦
            rw [Category.id_comp, Category.id_comp, Î·.naturality] }
            -- ğŸ‰ no goals
      naturality := fun _ _ f => by ext <;> dsimp <;> simp }
                                    -- âŠ¢ ((whiskeringObj C D Xâœ).map f â‰« (fun A => CommaMorphism.mk (NatTrans.app Î· ( â€¦
                                            -- âŠ¢ Xâœ.map f.left â‰« NatTrans.app Î· xâœ.left = NatTrans.app Î· xâœÂ¹.left â‰« Yâœ.map f. â€¦
                                            -- âŠ¢ Xâœ.map (NatTrans.app f.right nâœ) â‰« NatTrans.app Î· (xâœ.right.obj nâœ) = NatTra â€¦
                                                      -- ğŸ‰ no goals
                                                      -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.augmented.whiskering CategoryTheory.CosimplicialObject.Augmented.whiskering

variable {C}

end Augmented

open Simplicial

/-- Augment a cosimplicial object with an object. -/
@[simps]
def augment (X : CosimplicialObject C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj [0])
    (w : âˆ€ (i : SimplexCategory) (gâ‚ gâ‚‚ : ([0] : SimplexCategory) âŸ¶ i),
      f â‰« X.map gâ‚ = f â‰« X.map gâ‚‚) : CosimplicialObject.Augmented C where
  left := Xâ‚€
  right := X
  hom :=
    { app := fun i => f â‰« X.map (SimplexCategory.const i 0)
      naturality := by
        intro i j g
        -- âŠ¢ ((const C).obj Xâ‚€).map g â‰« (fun i => f â‰« X.map (SimplexCategory.const i 0))  â€¦
        dsimp
        -- âŠ¢ ğŸ™ Xâ‚€ â‰« f â‰« X.map (SimplexCategory.const j 0) = (f â‰« X.map (SimplexCategory.c â€¦
        simpa [â† X.map_comp] using w _ _ _ }
        -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.augment CategoryTheory.CosimplicialObject.augment

-- porting note: removed @[simp] as the linter complains
theorem augment_hom_zero (X : CosimplicialObject C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj [0]) (w) :
    (X.augment Xâ‚€ f w).hom.app [0] = f := by
  dsimp
  -- âŠ¢ f â‰« X.map (SimplexCategory.const [0] 0) = f
  rw [SimplexCategory.hom_zero_zero ([0].const 0), X.map_id, Category.comp_id]
  -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.augment_hom_zero CategoryTheory.CosimplicialObject.augment_hom_zero

end CosimplicialObject

/-- The anti-equivalence between simplicial objects and cosimplicial objects. -/
@[simps!]
def simplicialCosimplicialEquiv : (SimplicialObject C)áµ’áµ– â‰Œ CosimplicialObject Cáµ’áµ– :=
  Functor.leftOpRightOpEquiv _ _
#align category_theory.simplicial_cosimplicial_equiv CategoryTheory.simplicialCosimplicialEquiv

/-- The anti-equivalence between cosimplicial objects and simplicial objects. -/
@[simps!]
def cosimplicialSimplicialEquiv : (CosimplicialObject C)áµ’áµ– â‰Œ SimplicialObject Cáµ’áµ– :=
  Functor.opUnopEquiv _ _
#align category_theory.cosimplicial_simplicial_equiv CategoryTheory.cosimplicialSimplicialEquiv

variable {C}

/-- Construct an augmented cosimplicial object in the opposite
category from an augmented simplicial object. -/
@[simps!]
def SimplicialObject.Augmented.rightOp (X : SimplicialObject.Augmented C) :
    CosimplicialObject.Augmented Cáµ’áµ– where
  left := Opposite.op X.right
  right := X.left.rightOp
  hom := NatTrans.rightOp X.hom
#align category_theory.simplicial_object.augmented.right_op CategoryTheory.SimplicialObject.Augmented.rightOp

/-- Construct an augmented simplicial object from an augmented cosimplicial
object in the opposite category. -/
@[simps!]
def CosimplicialObject.Augmented.leftOp (X : CosimplicialObject.Augmented Cáµ’áµ–) :
    SimplicialObject.Augmented C where
  left := X.right.leftOp
  right := X.left.unop
  hom := NatTrans.leftOp X.hom
#align category_theory.cosimplicial_object.augmented.left_op CategoryTheory.CosimplicialObject.Augmented.leftOp

/-- Converting an augmented simplicial object to an augmented cosimplicial
object and back is isomorphic to the given object. -/
@[simps!]
def SimplicialObject.Augmented.rightOpLeftOpIso (X : SimplicialObject.Augmented C) :
    X.rightOp.leftOp â‰… X :=
  Comma.isoMk X.left.rightOpLeftOpIso (CategoryTheory.eqToIso <| by aesop_cat)
                                                                    -- ğŸ‰ no goals
#align category_theory.simplicial_object.augmented.right_op_left_op_iso CategoryTheory.SimplicialObject.Augmented.rightOpLeftOpIso

/-- Converting an augmented cosimplicial object to an augmented simplicial
object and back is isomorphic to the given object. -/
@[simps!]
def CosimplicialObject.Augmented.leftOpRightOpIso (X : CosimplicialObject.Augmented Cáµ’áµ–) :
    X.leftOp.rightOp â‰… X :=
  Comma.isoMk (CategoryTheory.eqToIso <| by simp) X.right.leftOpRightOpIso
                                            -- ğŸ‰ no goals
#align category_theory.cosimplicial_object.augmented.left_op_right_op_iso CategoryTheory.CosimplicialObject.Augmented.leftOpRightOpIso

variable (C)

/-- A functorial version of `SimplicialObject.Augmented.rightOp`. -/
@[simps]
def simplicialToCosimplicialAugmented :
    (SimplicialObject.Augmented C)áµ’áµ– â¥¤ CosimplicialObject.Augmented Cáµ’áµ– where
  obj X := X.unop.rightOp
  map f :=
    { left := f.unop.right.op
      right := NatTrans.rightOp f.unop.left
      w := by
        ext x
        -- âŠ¢ NatTrans.app ((CosimplicialObject.const Cáµ’áµ–).map f.unop.right.op â‰« ((fun X = â€¦
        dsimp
        -- âŠ¢ f.unop.right.op â‰« (NatTrans.app Yâœ.unop.hom (op x)).op = (NatTrans.app Xâœ.un â€¦
        simp_rw [â† op_comp]
        -- âŠ¢ (NatTrans.app Yâœ.unop.hom (op x) â‰« f.unop.right).op = (NatTrans.app f.unop.l â€¦
        congr 1
        -- âŠ¢ NatTrans.app Yâœ.unop.hom (op x) â‰« f.unop.right = NatTrans.app f.unop.left (o â€¦
        exact (congr_app f.unop.w (op x)).symm }
        -- ğŸ‰ no goals
#align category_theory.simplicial_to_cosimplicial_augmented CategoryTheory.simplicialToCosimplicialAugmented

/-- A functorial version of `Cosimplicial_object.Augmented.leftOp`. -/
@[simps]
def cosimplicialToSimplicialAugmented :
    CosimplicialObject.Augmented Cáµ’áµ– â¥¤ (SimplicialObject.Augmented C)áµ’áµ– where
  obj X := Opposite.op X.leftOp
  map f :=
    Quiver.Hom.op <|
      { left := NatTrans.leftOp f.right
        right := f.left.unop
        w := by
          ext x
          -- âŠ¢ NatTrans.app ((ğŸ­ (SimplicialObject C)).map (NatTrans.leftOp f.right) â‰« (Cosi â€¦
          dsimp
          -- âŠ¢ (NatTrans.app f.right x.unop).unop â‰« (NatTrans.app Xâœ.hom x.unop).unop = (Na â€¦
          simp_rw [â† unop_comp]
          -- âŠ¢ (NatTrans.app Xâœ.hom x.unop â‰« NatTrans.app f.right x.unop).unop = (f.left â‰«  â€¦
          congr 1
          -- âŠ¢ NatTrans.app Xâœ.hom x.unop â‰« NatTrans.app f.right x.unop = f.left â‰« NatTrans â€¦
          exact (congr_app f.w (unop x)).symm }
          -- ğŸ‰ no goals
#align category_theory.cosimplicial_to_simplicial_augmented CategoryTheory.cosimplicialToSimplicialAugmented

/-- The contravariant categorical equivalence between augmented simplicial
objects and augmented cosimplicial objects in the opposite category. -/
@[simps! functor inverse]
def simplicialCosimplicialAugmentedEquiv :
    (SimplicialObject.Augmented C)áµ’áµ– â‰Œ CosimplicialObject.Augmented Cáµ’áµ– :=
  Equivalence.mk (simplicialToCosimplicialAugmented _) (cosimplicialToSimplicialAugmented _)
    (NatIso.ofComponents (fun X => X.unop.rightOpLeftOpIso.op) fun f => by
      dsimp
      -- âŠ¢ f â‰« (SimplicialObject.Augmented.rightOpLeftOpIso Yâœ.unop).hom.op = (Simplici â€¦
      rw [â† f.op_unop]
      -- âŠ¢ f.unop.op â‰« (SimplicialObject.Augmented.rightOpLeftOpIso Yâœ.unop).hom.op = ( â€¦
      simp_rw [â† op_comp]
      -- âŠ¢ ((SimplicialObject.Augmented.rightOpLeftOpIso Yâœ.unop).hom â‰« f.unop).op = (C â€¦
      congr 1
      -- âŠ¢ (SimplicialObject.Augmented.rightOpLeftOpIso Yâœ.unop).hom â‰« f.unop = CommaMo â€¦
      aesop_cat)
      -- ğŸ‰ no goals
    (NatIso.ofComponents fun X => X.leftOpRightOpIso)
#align category_theory.simplicial_cosimplicial_augmented_equiv CategoryTheory.simplicialCosimplicialAugmentedEquiv

end CategoryTheory
