/-
Copyright (c) 2021 Shing Tak Lam. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Shing Tak Lam
-/
module

public import Mathlib.CategoryTheory.Groupoid.Grpd.Basic
public import Mathlib.Topology.Category.TopCat.Basic
public import Mathlib.Topology.Homotopy.Path
public import Mathlib.Data.Set.Subsingleton

/-!
# Fundamental groupoid of a space

Given a topological space `X`, we can define the fundamental groupoid of `X` to be the category with
objects being points of `X`, and morphisms `x ‚ü∂ y` being paths from `x` to `y`, quotiented by
homotopy equivalence. With this, the fundamental group of `X` based at `x` is just the automorphism
group of `x`.
-/

@[expose] public section

open CategoryTheory

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
variable {x‚ÇÄ x‚ÇÅ : X}

noncomputable section

open unitInterval

namespace Path

namespace Homotopy

section

/-- Auxiliary function for `reflTransSymm`. -/
def reflTransSymmAux (x : I √ó I) : ‚Ñù :=
  if (x.2 : ‚Ñù) ‚â§ 1 / 2 then x.1 * 2 * x.2 else x.1 * (2 - 2 * x.2)

@[continuity, fun_prop]
theorem continuous_reflTransSymmAux : Continuous reflTransSymmAux :=
  continuous_if_le (by fun_prop) (by fun_prop) (by fun_prop) (by fun_prop) (by grind)

theorem reflTransSymmAux_mem_I (x : I √ó I) : reflTransSymmAux x ‚àà I := by
  dsimp only [reflTransSymmAux]
  split_ifs
  ¬∑ constructor
    ¬∑ apply mul_nonneg <;> grind
    ¬∑ rw [mul_assoc]
      apply mul_le_one‚ÇÄ <;> grind
  ¬∑ constructor
    ¬∑ apply mul_nonneg <;> grind
    ¬∑ apply mul_le_one‚ÇÄ <;> grind

/-- For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from the constant path based at `x‚ÇÄ` to
  `p.trans p.symm`. -/
def reflTransSymm (p : Path x‚ÇÄ x‚ÇÅ) : Homotopy (Path.refl x‚ÇÄ) (p.trans p.symm) where
  toFun x := p ‚ü®reflTransSymmAux x, reflTransSymmAux_mem_I x‚ü©
  continuous_toFun := by fun_prop
  map_zero_left := by simp [reflTransSymmAux]
  map_one_left x := by
    simp only [reflTransSymmAux, Path.trans]
    cases le_or_gt (x : ‚Ñù) 2‚Åª¬π with
    | inl hx => simp [hx, ‚Üê extend_apply]
    | inr hx =>
      have : p.extend (2 - 2 * ‚Üëx) = p.extend (1 - (2 * ‚Üëx - 1)) := by ring_nf
      simpa [hx.not_ge, ‚Üê extend_apply]
  prop' t := by norm_num [reflTransSymmAux]

/-- For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from the constant path based at `x‚ÇÅ` to
  `p.symm.trans p`. -/
def reflSymmTrans (p : Path x‚ÇÄ x‚ÇÅ) : Homotopy (Path.refl x‚ÇÅ) (p.symm.trans p) :=
  (reflTransSymm p.symm).cast rfl <| congr_arg _ (Path.symm_symm _)

end

section TransRefl

/-- Auxiliary function for `trans_refl_reparam`. -/
def transReflReparamAux (t : I) : ‚Ñù :=
  if (t : ‚Ñù) ‚â§ 1 / 2 then 2 * t else 1

@[continuity, fun_prop]
theorem continuous_transReflReparamAux : Continuous transReflReparamAux :=
  continuous_if_le (by fun_prop) (by fun_prop) (by fun_prop) (by fun_prop) (by grind)

theorem transReflReparamAux_mem_I (t : I) : transReflReparamAux t ‚àà I := by
  unfold transReflReparamAux
  split_ifs <;> constructor <;> linarith [unitInterval.le_one t, unitInterval.nonneg t]

theorem transReflReparamAux_zero : transReflReparamAux 0 = 0 := by
  norm_num [transReflReparamAux]

theorem transReflReparamAux_one : transReflReparamAux 1 = 1 := by
  norm_num [transReflReparamAux]

theorem trans_refl_reparam (p : Path x‚ÇÄ x‚ÇÅ) :
    p.trans (Path.refl x‚ÇÅ) =
      p.reparam (fun t => ‚ü®transReflReparamAux t, transReflReparamAux_mem_I t‚ü©) (by fun_prop)
        (Subtype.ext transReflReparamAux_zero) (Subtype.ext transReflReparamAux_one) := by
  ext
  unfold transReflReparamAux
  simp only [coe_reparam]
  grind

/-- For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from `p.trans (Path.refl x‚ÇÅ)` to `p`. -/
def transRefl (p : Path x‚ÇÄ x‚ÇÅ) : Homotopy (p.trans (Path.refl x‚ÇÅ)) p :=
  ((Homotopy.reparam p (fun t => ‚ü®transReflReparamAux t, transReflReparamAux_mem_I t‚ü©)
          (by fun_prop) (Subtype.ext transReflReparamAux_zero)
          (Subtype.ext transReflReparamAux_one)).cast
      rfl (trans_refl_reparam p).symm).symm

/-- For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from `(Path.refl x‚ÇÄ).trans p` to `p`. -/
def reflTrans (p : Path x‚ÇÄ x‚ÇÅ) : Homotopy ((Path.refl x‚ÇÄ).trans p) p :=
  (transRefl p.symm).symm‚ÇÇ.cast (by simp) (by simp)

end TransRefl

section Assoc

/-- Auxiliary function for `trans_assoc_reparam`. -/
def transAssocReparamAux (t : I) : ‚Ñù :=
  if (t : ‚Ñù) ‚â§ 1 / 4 then 2 * t else if (t : ‚Ñù) ‚â§ 1 / 2 then t + 1 / 4 else 1 / 2 * (t + 1)

@[continuity, fun_prop]
theorem continuous_transAssocReparamAux : Continuous transAssocReparamAux :=
  continuous_if_le (by fun_prop) (by fun_prop) (by fun_prop)
    (continuous_if_le (by fun_prop) (by fun_prop) (by fun_prop) (by fun_prop)
      (by grind)).continuousOn (by grind)

theorem transAssocReparamAux_mem_I (t : I) : transAssocReparamAux t ‚àà I := by
  unfold transAssocReparamAux
  split_ifs <;> constructor <;> linarith [unitInterval.le_one t, unitInterval.nonneg t]

theorem transAssocReparamAux_zero : transAssocReparamAux 0 = 0 := by
  norm_num [transAssocReparamAux]

theorem transAssocReparamAux_one : transAssocReparamAux 1 = 1 := by
  norm_num [transAssocReparamAux]

theorem trans_assoc_reparam {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X} (p : Path x‚ÇÄ x‚ÇÅ) (q : Path x‚ÇÅ x‚ÇÇ) (r : Path x‚ÇÇ x‚ÇÉ) :
    (p.trans q).trans r =
      (p.trans (q.trans r)).reparam
        (fun t => ‚ü®transAssocReparamAux t, transAssocReparamAux_mem_I t‚ü©) (by fun_prop)
        (Subtype.ext transAssocReparamAux_zero) (Subtype.ext transAssocReparamAux_one) := by
  ext x
  simp only [transAssocReparamAux, Path.trans_apply, Function.comp_apply, Path.coe_reparam]
  split_ifs
  iterate 12 grind
  ¬∑ linarith
  ¬∑ linarith
  ¬∑ grind

/-- For paths `p q r`, we have a homotopy from `(p.trans q).trans r` to `p.trans (q.trans r)`. -/
def transAssoc {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X} (p : Path x‚ÇÄ x‚ÇÅ) (q : Path x‚ÇÅ x‚ÇÇ) (r : Path x‚ÇÇ x‚ÇÉ) :
    Homotopy ((p.trans q).trans r) (p.trans (q.trans r)) :=
  ((Homotopy.reparam (p.trans (q.trans r))
          (fun t => ‚ü®transAssocReparamAux t, transAssocReparamAux_mem_I t‚ü©) (by fun_prop)
          (Subtype.ext transAssocReparamAux_zero) (Subtype.ext transAssocReparamAux_one)).cast
      rfl (trans_assoc_reparam p q r).symm).symm

end Assoc

end Homotopy

namespace Homotopic

theorem refl_trans (p : Path x‚ÇÄ x‚ÇÅ) :
    ((Path.refl x‚ÇÄ).trans p).Homotopic p :=
  ‚ü®Homotopy.reflTrans p‚ü©

theorem trans_refl (p : Path x‚ÇÄ x‚ÇÅ) :
    (p.trans (Path.refl x‚ÇÅ)).Homotopic p :=
  ‚ü®Homotopy.transRefl p‚ü©

theorem trans_symm (p : Path x‚ÇÄ x‚ÇÅ) :
    (p.trans p.symm).Homotopic (Path.refl x‚ÇÄ) :=
  ‚ü®(Homotopy.reflTransSymm p).symm‚ü©

theorem symm_trans (p : Path x‚ÇÄ x‚ÇÅ) :
    (p.symm.trans p).Homotopic (Path.refl x‚ÇÅ) :=
  ‚ü®(Homotopy.reflSymmTrans p).symm‚ü©

theorem trans_assoc {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X} (p : Path x‚ÇÄ x‚ÇÅ) (q : Path x‚ÇÅ x‚ÇÇ) (r : Path x‚ÇÇ x‚ÇÉ) :
    ((p.trans q).trans r).Homotopic (p.trans (q.trans r)) :=
  ‚ü®Homotopy.transAssoc p q r‚ü©

namespace Quotient

@[simp, grind =]
theorem refl_trans (Œ≥ : Homotopic.Quotient x‚ÇÄ x‚ÇÅ) :
    trans (refl x‚ÇÄ) Œ≥ = Œ≥ := by
  induction Œ≥ using Quotient.ind with | mk Œ≥ =>
  simpa [‚Üê mk_trans, ‚Üê mk_refl, eq] using Homotopic.refl_trans Œ≥

@[simp, grind =]
theorem trans_refl (Œ≥ : Homotopic.Quotient x‚ÇÄ x‚ÇÅ) :
    trans Œ≥ (refl x‚ÇÅ) = Œ≥ := by
  induction Œ≥ using Quotient.ind with | mk Œ≥ =>
  simpa [‚Üê mk_trans, ‚Üê mk_refl, eq] using Homotopic.trans_refl Œ≥

@[simp, grind =]
theorem trans_symm (Œ≥ : Homotopic.Quotient x‚ÇÄ x‚ÇÅ) :
    trans Œ≥ (symm Œ≥) = refl x‚ÇÄ := by
  induction Œ≥ using Quotient.ind with | mk Œ≥ =>
  simpa [‚Üê mk_trans, ‚Üê mk_symm, ‚Üê mk_refl, eq] using Homotopic.trans_symm Œ≥

@[simp, grind =]
theorem symm_trans (Œ≥ : Homotopic.Quotient x‚ÇÄ x‚ÇÅ) :
    trans (symm Œ≥) Œ≥ = refl x‚ÇÅ := by
  induction Œ≥ using Quotient.ind with | mk Œ≥ =>
  simpa [‚Üê mk_trans, ‚Üê mk_symm, ‚Üê mk_refl, eq] using Homotopic.symm_trans Œ≥

@[simp, grind _=_]
theorem trans_assoc {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X}
    (Œ≥‚ÇÄ : Homotopic.Quotient x‚ÇÄ x‚ÇÅ)
    (Œ≥‚ÇÅ : Homotopic.Quotient x‚ÇÅ x‚ÇÇ)
    (Œ≥‚ÇÇ : Homotopic.Quotient x‚ÇÇ x‚ÇÉ) :
    trans (trans Œ≥‚ÇÄ Œ≥‚ÇÅ) Œ≥‚ÇÇ = trans Œ≥‚ÇÄ (trans Œ≥‚ÇÅ Œ≥‚ÇÇ) := by
  induction Œ≥‚ÇÄ using Quotient.ind with | mk Œ≥‚ÇÄ =>
  induction Œ≥‚ÇÅ using Quotient.ind with | mk Œ≥‚ÇÅ =>
  induction Œ≥‚ÇÇ using Quotient.ind with | mk Œ≥‚ÇÇ =>
  simpa [‚Üê mk_trans, eq] using Homotopic.trans_assoc Œ≥‚ÇÄ Œ≥‚ÇÅ Œ≥‚ÇÇ

end Quotient

end Homotopic

end Path

/-- The fundamental groupoid of a space `X` is defined to be a wrapper around `X`, and we
subsequently put a `CategoryTheory.Groupoid` structure on it. -/
@[ext]
structure FundamentalGroupoid (X : Type*) where
  /-- View a term of `FundamentalGroupoid X` as a term of `X`. -/
  as : X

namespace FundamentalGroupoid

/-- The equivalence between `X` and the underlying type of its fundamental groupoid.
  This is useful for transferring constructions (instances, etc.)
  from `X` to `œÄ‚Çì X`. -/
@[simps]
def equiv (X : Type*) : FundamentalGroupoid X ‚âÉ X where
  toFun x := x.as
  invFun x := .mk x

@[simp]
lemma isEmpty_iff (X : Type*) :
    IsEmpty (FundamentalGroupoid X) ‚Üî IsEmpty X :=
  equiv _ |>.isEmpty_congr

instance (X : Type*) [IsEmpty X] :
    IsEmpty (FundamentalGroupoid X) :=
  equiv _ |>.isEmpty

@[simp]
lemma nonempty_iff (X : Type*) :
    Nonempty (FundamentalGroupoid X) ‚Üî Nonempty X :=
  equiv _ |>.nonempty_congr

instance (X : Type*) [Nonempty X] :
    Nonempty (FundamentalGroupoid X) :=
  equiv _ |>.nonempty

@[simp]
lemma subsingleton_iff (X : Type*) :
    Subsingleton (FundamentalGroupoid X) ‚Üî Subsingleton X :=
  equiv _ |>.subsingleton_congr

instance (X : Type*) [Subsingleton X] :
    Subsingleton (FundamentalGroupoid X) :=
  equiv _ |>.subsingleton

-- TODO: It seems that `Equiv.nontrivial_congr` doesn't exist.
-- Once it is added, please add the corresponding lemma and instance.

instance {X : Type*} [Inhabited X] : Inhabited (FundamentalGroupoid X) :=
  ‚ü®‚ü®default‚ü©‚ü©

instance : Groupoid (FundamentalGroupoid X) where
  Hom x y := Path.Homotopic.Quotient x.as y.as
  id x := ‚ü¶Path.refl x.as‚üß
  comp := Path.Homotopic.Quotient.trans
  id_comp := by rintro _ _ ‚ü®f‚ü©; exact Quotient.sound ‚ü®Path.Homotopy.reflTrans f‚ü©
  comp_id := by rintro _ _ ‚ü®f‚ü©; exact Quotient.sound ‚ü®Path.Homotopy.transRefl f‚ü©
  assoc := by rintro _ _ _ _ ‚ü®f‚ü© ‚ü®g‚ü© ‚ü®h‚ü©; exact Quotient.sound ‚ü®Path.Homotopy.transAssoc f g h‚ü©
  inv := Quotient.lift (fun f ‚Ü¶ ‚ü¶f.symm‚üß) (by rintro a b ‚ü®h‚ü©; exact Quotient.sound ‚ü®h.symm‚ÇÇ‚ü©)
  inv_comp := by rintro _ _ ‚ü®f‚ü©; exact Quotient.sound ‚ü®(Path.Homotopy.reflSymmTrans f).symm‚ü©
  comp_inv := by rintro _ _ ‚ü®f‚ü©; exact Quotient.sound ‚ü®(Path.Homotopy.reflTransSymm f).symm‚ü©

theorem comp_eq (x y z : FundamentalGroupoid X) (p : x ‚ü∂ y) (q : y ‚ü∂ z) : p ‚â´ q = p.trans q := rfl

theorem id_eq_path_refl (x : FundamentalGroupoid X) : ùüô x = ‚ü¶Path.refl x.as‚üß := rfl

/-- The functor on fundamental groupoid induced by a continuous map. -/
@[simps] def map (f : C(X, Y)) : FundamentalGroupoid X ‚•§ FundamentalGroupoid Y where
  obj x := ‚ü®f x.as‚ü©
  map p := p.map f
  map_id _ := rfl
  map_comp := by rintro _ _ _ ‚ü®p‚ü© ‚ü®q‚ü©; exact congr_arg Quotient.mk'' (p.map_trans q f.continuous)

@[simp]
protected theorem map_id : map (.id X) = ùü≠ _ := by
  simp only [map]; congr; ext x y ‚ü®p‚ü©; rfl

@[simp]
protected theorem map_comp {Z : Type*} [TopologicalSpace Z] (g : C(Y, Z)) (f : C(X, Y)) :
    map (g.comp f) = map f ‚ãô map g := by
  simp only [map]; congr; ext x y ‚ü®p‚ü©; rfl

/-- The functor sending a topological space `X` to its fundamental groupoid. -/
def fundamentalGroupoidFunctor : TopCat ‚•§ Grpd where
  obj X := { Œ± := FundamentalGroupoid X }
  map f := map f.hom
  map_id _ := FundamentalGroupoid.map_id
  map_comp _ _ := FundamentalGroupoid.map_comp _ _

@[inherit_doc] scoped notation "œÄ" => FundamentalGroupoid.fundamentalGroupoidFunctor

/-- The fundamental groupoid of a topological space. -/
scoped notation "œÄ‚Çì" => FundamentalGroupoid.fundamentalGroupoidFunctor.obj

/-- The functor between fundamental groupoids induced by a continuous map. -/
scoped notation "œÄ‚Çò" => FundamentalGroupoid.fundamentalGroupoidFunctor.map

theorem map_eq {X Y : TopCat} {x‚ÇÄ x‚ÇÅ : X} (f : C(X, Y)) (p : Path.Homotopic.Quotient x‚ÇÄ x‚ÇÅ) :
    (œÄ‚Çò (TopCat.ofHom f)).map p = p.map f := rfl

/-- Help the typechecker by converting a point in a groupoid back to a point in
the underlying topological space. -/
abbrev toTop {X : TopCat} (x : œÄ‚Çì X) : X := x.as

/-- Help the typechecker by converting a point in a topological space to a
point in the fundamental groupoid of that space. -/
abbrev fromTop {X : TopCat} (x : X) : œÄ‚Çì X := ‚ü®x‚ü©

/-- Help the typechecker by converting an arrow in the fundamental groupoid of
a topological space back to a path in that space (i.e., `Path.Homotopic.Quotient`). -/
abbrev toPath {X : TopCat} {x‚ÇÄ x‚ÇÅ : œÄ‚Çì X} (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
    Path.Homotopic.Quotient x‚ÇÄ.as x‚ÇÅ.as :=
  p

/-- Help the typechecker by converting a path in a topological space to an arrow in the
fundamental groupoid of that space. -/
abbrev fromPath {x‚ÇÄ x‚ÇÅ : X} (p : Path.Homotopic.Quotient x‚ÇÄ x‚ÇÅ) :
    FundamentalGroupoid.mk x‚ÇÄ ‚ü∂ FundamentalGroupoid.mk x‚ÇÅ := p

/-- Two paths are equal in the fundamental groupoid if and only if they are homotopic. -/
theorem fromPath_eq_iff_homotopic {x‚ÇÄ x‚ÇÅ : X} (f : Path x‚ÇÄ x‚ÇÅ) (g : Path x‚ÇÄ x‚ÇÅ) :
    fromPath (Path.Homotopic.Quotient.mk f) = fromPath (Path.Homotopic.Quotient.mk g) ‚Üî
      f.Homotopic g :=
  ‚ü®fun ih ‚Ü¶ Quotient.exact ih, fun h ‚Ü¶ Quotient.sound h‚ü©

lemma eqToHom_eq {x‚ÇÄ x‚ÇÅ : X} (h : x‚ÇÄ = x‚ÇÅ) :
    eqToHom (congr_arg mk h) = ‚ü¶(Path.refl x‚ÇÅ).cast h rfl‚üß := by subst h; rfl

@[reassoc]
lemma conj_eqToHom {x y x' y' : X} {p : Path x y} (hx : x' = x) (hy : y' = y) :
    eqToHom (congr_arg mk hx) ‚â´ .mk p ‚â´ eqToHom (congr_arg mk hy.symm) = .mk (p.cast hx hy) := by
  subst hx hy; simp

end FundamentalGroupoid
