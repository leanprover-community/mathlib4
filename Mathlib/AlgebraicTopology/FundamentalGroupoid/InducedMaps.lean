/-
Copyright (c) 2022 Praneeth Kolichala. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Praneeth Kolichala, Yury Kudryashov
-/
module

public import Mathlib.Topology.Homotopy.Equiv
public import Mathlib.CategoryTheory.Equivalence
public import Mathlib.AlgebraicTopology.FundamentalGroupoid.Product

/-!
# Homotopic maps induce naturally isomorphic functors

## Main definitions

- `FundamentalGroupoidFunctor.homotopicMapsNatIso H` The natural isomorphism
  between the induced functors `f : œÄ(X) ‚•§ œÄ(Y)` and `g : œÄ(X) ‚•§ œÄ(Y)`, given a homotopy
  `H : f ‚àº g`

- `FundamentalGroupoidFunctor.equivOfHomotopyEquiv hequiv` The equivalence of the categories
  `œÄ(X)` and `œÄ(Y)` given a homotopy equivalence `hequiv : X ‚âÉ‚Çï Y` between them.
-/

@[expose] public section

noncomputable section

universe u v

open FundamentalGroupoid CategoryTheory FundamentalGroupoidFunctor
open scoped FundamentalGroupoid unitInterval

/-- Let `F` be a homotopy between two continuous maps `f g : C(X, Y)`.
Given a path `p : Path x‚ÇÅ x‚ÇÇ` in the domain, consider the following two paths in the codomain.
One path goes along the image of `p` under `f`, then along the trajectory of `x‚ÇÇ` under `F`.
The other path goes along the trajectory of `x‚ÇÅ` under `F`, then along the image of `p` under `g`.

These two paths are homotopic. -/
theorem Path.Homotopic.map_trans_evalAt {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {f g : C(X, Y)} (F : f.Homotopy g) {x‚ÇÅ x‚ÇÇ : X} (p : Path x‚ÇÅ x‚ÇÇ) :
    ((p.map (map_continuous f)).trans (F.evalAt x‚ÇÇ)).Homotopic
      ((F.evalAt x‚ÇÅ).trans (p.map (map_continuous g))) := by
  /- Let `G` be the continuous map on the unit square sending `(t, s)` to `F(t, p(s))`.
  Then our homotopy is the image under `G` of a homotopy
  between the two paths from `(0, 0)` to `(1, 1)` along the sides of the square. -/
  set G : C(I √ó I, Y) := F.toContinuousMap.comp (.prodMap (.id _) p)
  set p‚ÇÅ : Path ((0, 0) : I √ó I) (1, 1) := .prod (.trans (.refl _) .id) (.trans .id (.refl _))
  set p‚ÇÇ : Path ((0, 0) : I √ó I) (1, 1) := .prod (.trans .id (.refl _)) (.trans (.refl _) .id)
  set Fsq : p‚ÇÅ.Homotopy p‚ÇÇ :=
    Path.Homotopic.prodHomotopy (.trans (.reflTrans _) (.symm <| .transRefl _))
      (.trans (.transRefl _) (.symm <| .reflTrans _))
  refine ‚ü®((Fsq.map G).pathCast ?H0 ?H1).cast ?hp ?hq‚ü©
  all_goals aesop (add simp Path.trans_apply)

namespace FundamentalGroupoidFunctor

open CategoryTheory
open scoped FundamentalGroupoid ContinuousMap

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
  {f g : C(X, Y)}

set_option pp.proofs.withType true in
/-- Given a homotopy H : f ‚àº g, we have an associated natural isomorphism between the induced
functors `map f` and `map g` on fundamental groupoids. -/
def homotopicMapsNatIso (H : ContinuousMap.Homotopy f g) : map f ‚ü∂ map g where
  app x := ‚ü¶H.evalAt x.as‚üß
  naturality := by
    rintro ‚ü®x‚ü© ‚ü®y‚ü© p
    rcases Path.Homotopic.Quotient.mk_surjective p with ‚ü®p, rfl‚ü©
    simp only [map_map, map_obj_as, Path.Homotopic.Quotient.mk''_eq_mk, comp_eq,
      ‚Üê Path.Homotopic.Quotient.mk_map, ‚Üê Path.Homotopic.Quotient.mk_trans]
    rw [Path.Homotopic.Quotient.eq]
    exact .map_trans_evalAt _ _

instance (H : ContinuousMap.Homotopy f g) : IsIso (homotopicMapsNatIso H) :=
  NatIso.isIso_of_isIso_app _

open scoped ContinuousMap

/-- Homotopy equivalent topological spaces have equivalent fundamental groupoids. -/
def equivOfHomotopyEquiv {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] (hequiv : X ‚âÉ‚Çï Y) :
    œÄ‚Çì (.of X) ‚âå œÄ‚Çì (.of Y) := by
  apply CategoryTheory.Equivalence.mk (map hequiv.toFun) (map hequiv.invFun)
  ¬∑ simpa only [FundamentalGroupoid.map_id, FundamentalGroupoid.map_comp]
      using (asIso (homotopicMapsNatIso hequiv.left_inv.some)).symm
  ¬∑ simpa only [FundamentalGroupoid.map_id, FundamentalGroupoid.map_comp]
      using asIso (homotopicMapsNatIso hequiv.right_inv.some)

end FundamentalGroupoidFunctor

/-!
### Old proof

The rest of the file contains definitions and theorems required to write the same proof
in a slightly different manner.

The proof was rewritten in 2025 for two reasons:

- the new proof is much more straightforward;
- the new proof is fully universe polymorphic.

TODO: review which of these definitions and theorems are useful for other reasons,
then deprecate the rest of them.
-/

namespace unitInterval

/-- The path 0 ‚ü∂ 1 in `I` -/
def path01 : Path (0 : I) 1 where
  toFun := id
  source' := rfl
  target' := rfl

/-- The path 0 ‚ü∂ 1 in `ULift I` -/
def upath01 : Path (ULift.up 0 : ULift.{u} I) (ULift.up 1) where
  toFun := ULift.up
  source' := rfl
  target' := rfl

/-- The homotopy path class of 0 ‚Üí 1 in `ULift I` -/
def uhpath01 : @fromTop (TopCat.of <| ULift.{u} I) (ULift.up (0 : I)) ‚ü∂ fromTop (ULift.up 1) :=
  ‚ü¶upath01‚üß

end unitInterval

namespace ContinuousMap.Homotopy

open unitInterval (uhpath01)

section Casts

/-- Abbreviation for `eqToHom` that accepts points in a topological space -/
abbrev hcast {X : TopCat} {x‚ÇÄ x‚ÇÅ : X} (hx : x‚ÇÄ = x‚ÇÅ) : fromTop x‚ÇÄ ‚ü∂ fromTop x‚ÇÅ :=
  eqToHom <| FundamentalGroupoid.ext hx

@[simp]
theorem hcast_def {X : TopCat} {x‚ÇÄ x‚ÇÅ : X} (hx‚ÇÄ : x‚ÇÄ = x‚ÇÅ) :
    hcast hx‚ÇÄ = eqToHom (FundamentalGroupoid.ext hx‚ÇÄ) :=
  rfl

variable {X‚ÇÅ X‚ÇÇ Y : TopCat.{u}} {f : C(X‚ÇÅ, Y)} {g : C(X‚ÇÇ, Y)} {x‚ÇÄ x‚ÇÅ : X‚ÇÅ} {x‚ÇÇ x‚ÇÉ : X‚ÇÇ}
  {p : Path x‚ÇÄ x‚ÇÅ} {q : Path x‚ÇÇ x‚ÇÉ} (hfg : ‚àÄ t, f (p t) = g (q t))
include hfg

/-- If `f(p(t) = g(q(t))` for two paths `p` and `q`, then the induced path homotopy classes
`f(p)` and `g(p)` are the same as well, despite having a priori different types -/
theorem heq_path_of_eq_image :
    (œÄ‚Çò (TopCat.ofHom f)).map ‚ü¶p‚üß ‚âç (œÄ‚Çò (TopCat.ofHom g)).map ‚ü¶q‚üß := by
  simp only [map_eq]
  apply Path.Homotopic.hpath_hext
  exact hfg

set_option backward.privateInPublic true in
private theorem start_path : f x‚ÇÄ = g x‚ÇÇ := by convert hfg 0 <;> simp only [Path.source]

set_option backward.privateInPublic true in
private theorem end_path : f x‚ÇÅ = g x‚ÇÉ := by convert hfg 1 <;> simp only [Path.target]

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
theorem eq_path_of_eq_image :
    (œÄ‚Çò (TopCat.ofHom f)).map ‚ü¶p‚üß =
        hcast (start_path hfg) ‚â´ (œÄ‚Çò (TopCat.ofHom g)).map ‚ü¶q‚üß ‚â´ hcast (end_path hfg).symm := by
  rw [conj_eqToHom_iff_heq
    ((œÄ‚Çò (TopCat.ofHom f)).map ‚ü¶p‚üß) ((œÄ‚Çò (TopCat.ofHom g)).map ‚ü¶q‚üß)
    (FundamentalGroupoid.ext <| start_path hfg)
    (FundamentalGroupoid.ext <| end_path hfg)]
  exact heq_path_of_eq_image hfg

end Casts

-- We let `X` and `Y` be spaces, and `f` and `g` be homotopic maps between them
variable {X Y : TopCat.{u}} {f g : C(X, Y)} (H : ContinuousMap.Homotopy f g) {x‚ÇÄ x‚ÇÅ : X}
  (p : fromTop x‚ÇÄ ‚ü∂ fromTop x‚ÇÅ)

/-!
These definitions set up the following diagram, for each path `p`:

            f(p)
        *--------*
        | \      |
    H‚ÇÄ  |   \ d  |  H‚ÇÅ
        |     \  |
        *--------*
            g(p)

Here, `H‚ÇÄ = H.evalAt x‚ÇÄ` is the path from `f(x‚ÇÄ)` to `g(x‚ÇÄ)`,
and similarly for `H‚ÇÅ`. Similarly, `f(p)` denotes the
path in Y that the induced map `f` takes `p`, and similarly for `g(p)`.

Finally, `d`, the diagonal path, is H(0 ‚ü∂ 1, p), the result of the induced `H` on
`Path.Homotopic.prod (0 ‚ü∂ 1) p`, where `(0 ‚ü∂ 1)` denotes the path from `0` to `1` in `I`.

It is clear that the diagram commutes (`H‚ÇÄ ‚â´ g(p) = d = f(p) ‚â´ H‚ÇÅ`), but unfortunately,
many of the paths do not have defeq starting/ending points, so we end up needing some casting.
-/


/-- Interpret a homotopy `H : C(I √ó X, Y)` as a map `C(ULift I √ó X, Y)` -/
def uliftMap : C(TopCat.of (ULift.{u} I √ó X), Y) :=
  ‚ü®fun x => H (x.1.down, x.2),
    H.continuous.comp ((continuous_uliftDown.comp continuous_fst).prodMk continuous_snd)‚ü©

theorem ulift_apply (i : ULift.{u} I) (x : X) : H.uliftMap (i, x) = H (i.down, x) :=
  rfl

/-- An abbreviation for `prodToProdTop`, with some types already in place to help the
typechecker. In particular, the first path should be on the ulifted unit interval. -/
abbrev prodToProdTopI {a‚ÇÅ a‚ÇÇ : TopCat.of (ULift I)} {b‚ÇÅ b‚ÇÇ : X} (p‚ÇÅ : fromTop a‚ÇÅ ‚ü∂ fromTop a‚ÇÇ)
    (p‚ÇÇ : fromTop b‚ÇÅ ‚ü∂ fromTop b‚ÇÇ) :=
  (prodToProdTop (TopCat.of <| ULift I) X).map (X := (‚ü®a‚ÇÅ‚ü©, ‚ü®b‚ÇÅ‚ü©)) (Y := (‚ü®a‚ÇÇ‚ü©, ‚ü®b‚ÇÇ‚ü©)) (p‚ÇÅ, p‚ÇÇ)

/-- The diagonal path `d` of a homotopy `H` on a path `p` -/
def diagonalPath : fromTop (H (0, x‚ÇÄ)) ‚ü∂ fromTop (H (1, x‚ÇÅ)) :=
  (œÄ‚Çò (TopCat.ofHom H.uliftMap)).map (prodToProdTopI uhpath01 p)

/-- The diagonal path, but starting from `f x‚ÇÄ` and going to `g x‚ÇÅ` -/
def diagonalPath' : fromTop (f x‚ÇÄ) ‚ü∂ fromTop (g x‚ÇÅ) :=
  hcast (H.apply_zero x‚ÇÄ).symm ‚â´ H.diagonalPath p ‚â´ hcast (H.apply_one x‚ÇÅ)

/-- Proof that `f(p) = H(0 ‚ü∂ 0, p)`, with the appropriate casts -/
theorem apply_zero_path : (œÄ‚Çò (TopCat.ofHom f)).map p = hcast (H.apply_zero x‚ÇÄ).symm ‚â´
    (œÄ‚Çò (TopCat.ofHom H.uliftMap)).map
      (prodToProdTopI (ùüô (@fromTop (TopCat.of _) (ULift.up 0))) p) ‚â´
    hcast (H.apply_zero x‚ÇÅ) :=
  Quotient.inductionOn p fun p' => by
    apply @eq_path_of_eq_image _ _ _ _ H.uliftMap _ _ _ _ _ ((Path.refl (ULift.up _)).prod p')
    intros
    rw [Path.prod_coe, ulift_apply H]
    simp

/-- Proof that `g(p) = H(1 ‚ü∂ 1, p)`, with the appropriate casts -/
theorem apply_one_path : (œÄ‚Çò (TopCat.ofHom g)).map p = hcast (H.apply_one x‚ÇÄ).symm ‚â´
    (œÄ‚Çò (TopCat.ofHom H.uliftMap)).map
      (prodToProdTopI (ùüô (@fromTop (TopCat.of _) (ULift.up 1))) p) ‚â´
    hcast (H.apply_one x‚ÇÅ) :=
  Quotient.inductionOn p fun p' => by
    apply @eq_path_of_eq_image _ _ _ _ H.uliftMap _ _ _ _ _ ((Path.refl (ULift.up _)).prod p')
    intros
    rw [Path.prod_coe, ulift_apply H]
    simp

/-- Proof that `H.evalAt x = H(0 ‚ü∂ 1, x ‚ü∂ x)`, with the appropriate casts -/
theorem evalAt_eq (x : X) : ‚ü¶H.evalAt x‚üß = hcast (H.apply_zero x).symm ‚â´
    (œÄ‚Çò (TopCat.ofHom H.uliftMap)).map (prodToProdTopI uhpath01 (ùüô (fromTop x))) ‚â´
      hcast (H.apply_one x).symm.symm := by
  dsimp only [prodToProdTopI, uhpath01, hcast]
  refine (@conj_eqToHom_iff_heq (œÄ‚Çì Y) _ _ _ _ _ _ _ _
    (FundamentalGroupoid.ext <| H.apply_one x).symm).mpr ?_
  simp only [map_eq]
  apply Path.Homotopic.hpath_hext; intro; rfl

-- Finally, we show `d = f(p) ‚â´ H‚ÇÅ = H‚ÇÄ ‚â´ g(p)`
theorem eq_diag_path : (œÄ‚Çò (TopCat.ofHom f)).map p ‚â´ ‚ü¶H.evalAt x‚ÇÅ‚üß = H.diagonalPath' p ‚àß
    (‚ü¶H.evalAt x‚ÇÄ‚üß ‚â´ (œÄ‚Çò (TopCat.ofHom g)).map p :
    fromTop (f x‚ÇÄ) ‚ü∂ fromTop (g x‚ÇÅ)) = H.diagonalPath' p := by
  rw [H.apply_zero_path, H.apply_one_path, H.evalAt_eq]
  erw [H.evalAt_eq]
  dsimp only [prodToProdTopI]
  constructor
  ¬∑ slice_lhs 2 4 => rw [eqToHom_trans, eqToHom_refl] -- Porting note: this ‚Üì `simp` didn't do this
    slice_lhs 2 4 => simp [‚Üê CategoryTheory.Functor.map_comp]
    rfl
  ¬∑ slice_lhs 2 4 => rw [eqToHom_trans, eqToHom_refl] -- Porting note: this ‚Üì `simp` didn't do this
    slice_lhs 2 4 => simp [‚Üê CategoryTheory.Functor.map_comp]
    rfl

end ContinuousMap.Homotopy
