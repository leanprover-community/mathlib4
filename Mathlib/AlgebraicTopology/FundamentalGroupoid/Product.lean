/-
Copyright (c) 2022 Praneeth Kolichala. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Praneeth Kolichala
-/
import Mathlib.CategoryTheory.Groupoid
import Mathlib.AlgebraicTopology.FundamentalGroupoid.Basic
import Mathlib.Topology.Category.TopCat.Limits.Products
import Mathlib.Topology.Homotopy.Product

#align_import algebraic_topology.fundamental_groupoid.product from "leanprover-community/mathlib"@"178a32653e369dce2da68dc6b2694e385d484ef1"

/-!
# Fundamental groupoid preserves products
In this file, we give the following definitions/theorems:

  - `FundamentalGroupoidFunctor.piIso` An isomorphism between Œ† i, (œÄ X·µ¢) and œÄ (Œ†i, X·µ¢), whose
    inverse is precisely the product of the maps œÄ (Œ† i, X·µ¢) ‚Üí œÄ (X·µ¢), each induced by
    the projection in `Top` Œ† i, X·µ¢ ‚Üí X·µ¢.

  - `FundamentalGroupoidFunctor.prodIso` An isomorphism between œÄX √ó œÄY and œÄ (X √ó Y), whose
    inverse is precisely the product of the maps œÄ (X √ó Y) ‚Üí œÄX and œÄ (X √ó Y) ‚Üí Y, each induced by
    the projections X √ó Y ‚Üí X and X √ó Y ‚Üí Y

  - `FundamentalGroupoidFunctor.preservesProduct` A proof that the fundamental groupoid functor
    preserves all products.
-/

set_option linter.uppercaseLean3 false

noncomputable section

open scoped FundamentalGroupoid CategoryTheory

namespace FundamentalGroupoidFunctor

universe u

section Pi

variable {I : Type u} (X : I ‚Üí TopCat.{u})

/-- The projection map Œ† i, X i ‚Üí X i induces a map œÄ(Œ† i, X i) ‚ü∂ œÄ(X i).
-/
def proj (i : I) : œÄ‚Çì (TopCat.of (‚àÄ i, X i)) ‚•§ œÄ‚Çì (X i) :=
  œÄ‚Çò ‚ü®_, continuous_apply i‚ü©
#align fundamental_groupoid_functor.proj FundamentalGroupoidFunctor.proj

/-- The projection map is precisely `Path.Homotopic.proj` interpreted as a functor -/
@[simp]
theorem proj_map (i : I) (x‚ÇÄ x‚ÇÅ : œÄ‚Çì (TopCat.of (‚àÄ i, X i))) (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
    (proj X i).map p = @Path.Homotopic.proj _ _ _ _ _ i p :=
  rfl
#align fundamental_groupoid_functor.proj_map FundamentalGroupoidFunctor.proj_map

/-- The map taking the pi product of a family of fundamental groupoids to the fundamental
groupoid of the pi product. This is actually an isomorphism (see `piIso`)
-/
@[simps]
def piToPiTop : (‚àÄ i, œÄ‚Çì (X i)) ‚•§ œÄ‚Çì (TopCat.of (‚àÄ i, X i)) where
  obj g := ‚ü®fun i => (g i).as‚ü©
  map p := Path.Homotopic.pi p
  map_id x := by
    change (Path.Homotopic.pi fun i => ‚ü¶_‚üß) = _
    simp only [FundamentalGroupoid.id_eq_path_refl, Path.Homotopic.pi_lift]
    rfl
  map_comp f g := (Path.Homotopic.comp_pi_eq_pi_comp f g).symm
#align fundamental_groupoid_functor.pi_to_pi_Top FundamentalGroupoidFunctor.piToPiTop

/-- Shows `piToPiTop` is an isomorphism, whose inverse is precisely the pi product
of the induced projections. This shows that `fundamentalGroupoidFunctor` preserves products.
-/
@[simps]
def piIso : CategoryTheory.Grpd.of (‚àÄ i : I, œÄ‚Çì (X i)) ‚âÖ œÄ‚Çì (TopCat.of (‚àÄ i, X i)) where
  hom := piToPiTop X
  inv := CategoryTheory.Functor.pi' (proj X)
  hom_inv_id := by
    change piToPiTop X ‚ãô CategoryTheory.Functor.pi' (proj X) = ùü≠ _
    apply CategoryTheory.Functor.ext ?_ ?_
    ¬∑ intros; rfl
    ¬∑ intros; ext; simp
  inv_hom_id := by
    change CategoryTheory.Functor.pi' (proj X) ‚ãô piToPiTop X = ùü≠ _
    apply CategoryTheory.Functor.ext
    ¬∑ intro _ _ f
      suffices Path.Homotopic.pi ((CategoryTheory.Functor.pi' (proj X)).map f) = f by simpa
      change Path.Homotopic.pi (fun i => (CategoryTheory.Functor.pi' (proj X)).map f i) = _
      simp
    ¬∑ intros; rfl
#align fundamental_groupoid_functor.pi_iso FundamentalGroupoidFunctor.piIso

section Preserves

open CategoryTheory

/-- Equivalence between the categories of cones over the objects `œÄ X·µ¢` written in two ways -/
def coneDiscreteComp :
    Limits.Cone (Discrete.functor X ‚ãô œÄ) ‚âå Limits.Cone (Discrete.functor fun i => œÄ‚Çì (X i)) :=
  Limits.Cones.postcomposeEquivalence (Discrete.compNatIsoDiscrete X œÄ)
#align fundamental_groupoid_functor.cone_discrete_comp FundamentalGroupoidFunctor.coneDiscreteComp

theorem coneDiscreteComp_obj_mapCone :
    -- Porting note: check universe parameters here
    (coneDiscreteComp X).functor.obj (Functor.mapCone œÄ (TopCat.piFan.{u,u} X)) =
      Limits.Fan.mk (œÄ‚Çì (TopCat.of (‚àÄ i, X i))) (proj X) :=
  rfl
#align fundamental_groupoid_functor.cone_discrete_comp_obj_map_cone FundamentalGroupoidFunctor.coneDiscreteComp_obj_mapCone

/-- This is `piIso.inv` as a cone morphism (in fact, isomorphism) -/
def piTopToPiCone :
    Limits.Fan.mk (œÄ‚Çì (TopCat.of (‚àÄ i, X i))) (proj X) ‚ü∂ Grpd.piLimitFan fun i : I => œÄ‚Çì (X i) where
  hom := CategoryTheory.Functor.pi' (proj X)
#align fundamental_groupoid_functor.pi_Top_to_pi_cone FundamentalGroupoidFunctor.piTopToPiCone

instance : IsIso (piTopToPiCone X) :=
  haveI : IsIso (piTopToPiCone X).hom := (inferInstance : IsIso (piIso X).inv)
  Limits.Cones.cone_iso_of_hom_iso (piTopToPiCone X)

/-- The fundamental groupoid functor preserves products -/
def preservesProduct : Limits.PreservesLimit (Discrete.functor X) œÄ := by
  -- Porting note: check universe parameters here
  apply Limits.preservesLimitOfPreservesLimitCone (TopCat.piFanIsLimit.{u,u} X)
  apply (Limits.IsLimit.ofConeEquiv (coneDiscreteComp X)).toFun
  simp only [coneDiscreteComp_obj_mapCone]
  apply Limits.IsLimit.ofIsoLimit _ (asIso (piTopToPiCone X)).symm
  exact Grpd.piLimitFanIsLimit _
#align fundamental_groupoid_functor.preserves_product FundamentalGroupoidFunctor.preservesProduct

end Preserves

end Pi

section Prod

variable (A B : TopCat.{u})

/-- The induced map of the left projection map X √ó Y ‚Üí X -/
def projLeft : œÄ‚Çì (TopCat.of (A √ó B)) ‚•§ œÄ‚Çì A :=
  œÄ‚Çò ‚ü®_, continuous_fst‚ü©
#align fundamental_groupoid_functor.proj_left FundamentalGroupoidFunctor.projLeft

/-- The induced map of the right projection map X √ó Y ‚Üí Y -/
def projRight : œÄ‚Çì (TopCat.of (A √ó B)) ‚•§ œÄ‚Çì B :=
  œÄ‚Çò ‚ü®_, continuous_snd‚ü©
#align fundamental_groupoid_functor.proj_right FundamentalGroupoidFunctor.projRight

@[simp]
theorem projLeft_map (x‚ÇÄ x‚ÇÅ : œÄ‚Çì (TopCat.of (A √ó B))) (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
    (projLeft A B).map p = Path.Homotopic.projLeft p :=
  rfl
#align fundamental_groupoid_functor.proj_left_map FundamentalGroupoidFunctor.projLeft_map

@[simp]
theorem projRight_map (x‚ÇÄ x‚ÇÅ : œÄ‚Çì (TopCat.of (A √ó B))) (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
    (projRight A B).map p = Path.Homotopic.projRight p :=
  rfl
#align fundamental_groupoid_functor.proj_right_map FundamentalGroupoidFunctor.projRight_map

/--
The map taking the product of two fundamental groupoids to the fundamental groupoid of the product
of the two topological spaces. This is in fact an isomorphism (see `prodIso`).
-/
@[simps obj]
def prodToProdTop : œÄ‚Çì A √ó œÄ‚Çì B ‚•§ œÄ‚Çì (TopCat.of (A √ó B)) where
  obj g := ‚ü®g.fst.as, g.snd.as‚ü©
  map {x y} p :=
    match x, y, p with
    | (x‚ÇÄ, x‚ÇÅ), (y‚ÇÄ, y‚ÇÅ), (p‚ÇÄ, p‚ÇÅ) => @Path.Homotopic.prod _ _ (_) (_) _ _ _ _ p‚ÇÄ p‚ÇÅ
  map_id := by
    rintro ‚ü®x‚ÇÄ, x‚ÇÅ‚ü©
    simp only [CategoryTheory.prod_id, FundamentalGroupoid.id_eq_path_refl]
    rfl
  map_comp {x y z} f g :=
    match x, y, z, f, g with
    | (x‚ÇÄ, x‚ÇÅ), (y‚ÇÄ, y‚ÇÅ), (z‚ÇÄ, z‚ÇÅ), (f‚ÇÄ, f‚ÇÅ), (g‚ÇÄ, g‚ÇÅ) =>
      (Path.Homotopic.comp_prod_eq_prod_comp f‚ÇÄ f‚ÇÅ g‚ÇÄ g‚ÇÅ).symm
#align fundamental_groupoid_functor.prod_to_prod_Top FundamentalGroupoidFunctor.prodToProdTop

theorem prodToProdTop_map {x‚ÇÄ x‚ÇÅ : œÄ‚Çì A} {y‚ÇÄ y‚ÇÅ : œÄ‚Çì B} (p‚ÇÄ : x‚ÇÄ ‚ü∂ x‚ÇÅ) (p‚ÇÅ : y‚ÇÄ ‚ü∂ y‚ÇÅ) :
    (prodToProdTop A B).map (X := (x‚ÇÄ, y‚ÇÄ)) (Y := (x‚ÇÅ, y‚ÇÅ)) (p‚ÇÄ, p‚ÇÅ) =
      Path.Homotopic.prod p‚ÇÄ p‚ÇÅ :=
  rfl
#align fundamental_groupoid_functor.prod_to_prod_Top_map FundamentalGroupoidFunctor.prodToProdTop_map

/-- Shows `prodToProdTop` is an isomorphism, whose inverse is precisely the product
of the induced left and right projections.
-/
@[simps]
def prodIso : CategoryTheory.Grpd.of (œÄ‚Çì A √ó œÄ‚Çì B) ‚âÖ œÄ‚Çì (TopCat.of (A √ó B)) where
  hom := prodToProdTop A B
  inv := (projLeft A B).prod' (projRight A B)
  hom_inv_id := by
    change prodToProdTop A B ‚ãô (projLeft A B).prod' (projRight A B) = ùü≠ _
    apply CategoryTheory.Functor.hext; ¬∑ intros; ext <;> simp <;> rfl
    rintro ‚ü®x‚ÇÄ, x‚ÇÅ‚ü© ‚ü®y‚ÇÄ, y‚ÇÅ‚ü© ‚ü®f‚ÇÄ, f‚ÇÅ‚ü©
    have : Path.Homotopic.projLeft ((prodToProdTop A B).map (f‚ÇÄ, f‚ÇÅ)) = f‚ÇÄ ‚àß
      Path.Homotopic.projRight ((prodToProdTop A B).map (f‚ÇÄ, f‚ÇÅ)) = f‚ÇÅ :=
        And.intro (Path.Homotopic.projLeft_prod f‚ÇÄ f‚ÇÅ) (Path.Homotopic.projRight_prod f‚ÇÄ f‚ÇÅ)
    simpa
  inv_hom_id := by
    change (projLeft A B).prod' (projRight A B) ‚ãô prodToProdTop A B = ùü≠ _
    apply CategoryTheory.Functor.hext
    ¬∑ intros; apply FundamentalGroupoid.ext; apply Prod.ext <;> simp <;> rfl
    rintro ‚ü®x‚ÇÄ, x‚ÇÅ‚ü© ‚ü®y‚ÇÄ, y‚ÇÅ‚ü© f
    have := Path.Homotopic.prod_projLeft_projRight f
    -- Porting note: was simpa but TopSpace instances might be getting in the way
    simp only [CategoryTheory.Functor.comp_obj, CategoryTheory.Functor.prod'_obj, prodToProdTop_obj,
      CategoryTheory.Functor.comp_map, CategoryTheory.Functor.prod'_map, projLeft_map,
      projRight_map, CategoryTheory.Functor.id_obj, CategoryTheory.Functor.id_map, heq_eq_eq]
    apply this

end Prod
