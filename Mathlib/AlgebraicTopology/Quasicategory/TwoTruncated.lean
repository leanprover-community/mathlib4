/-
Copyright (c) 2025 Julian Komaromy. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julian Komaromy, Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.SimplicialSet.Basic
public import Mathlib.AlgebraicTopology.SimplicialSet.CompStructTruncated

/-!
# 2-truncated quasicategories and homotopy relations

We define 2-truncated quasicategories `Quasicategory‚ÇÇ` by three horn-filling properties,
and the left and right homotopy relations `HomotopicL` and `HomotopicR` on the edges in a
2-truncated simplicial set.

We prove that for 2-truncated quasicategories, both homotopy relations are equivalence
relations, and that the left and right homotopy relations coincide.

For a 2-truncated quasicategory `A`, we define a category `HomotopyCategory‚ÇÇ A` whose
morphisms are given by (left) homotopy classes of edges. The construction of this category
is different from `HomotopyCategory A` in `AlgebraicTopology.SimplicialSet.HomotopyCat`:
* `HomotopyCategory‚ÇÇ A` has morphisms given by homotopy classes of edges
* `HomotopyCategory A` has morphisms given by equivalence classes of paths in the underlying
  reflexive quiver of `A`.

The two constructions agree for 2-truncated quasicategories (TODO: handled by future PR).

## Implementation notes

Throughout this file, we make use of `Edge` and `CompStruct` to conveniently deal with
edges and triangles in a 2-truncated simplicial set.
-/

@[expose] public section

open CategoryTheory SimplicialObject.Truncated

namespace SSet.Truncated
open Edge CompStruct

/--
A 2-truncated quasicategory is a 2-truncated simplicial set with the properties:
* (2, 1)-filling: given two consecutive `Edge`s `e‚ÇÄ‚ÇÅ` and `e‚ÇÅ‚ÇÇ`, there exists a `CompStruct`
  with (0, 1)-edge `e‚ÇÄ‚ÇÅ` and (0, 2)-edge `e‚ÇÅ‚ÇÇ`.
* (3, 1)-filling: given three `CompStruct`s `f‚ÇÉ`, `f‚ÇÄ` and `f‚ÇÇ` which form a (3, 1)-horn,
  there exists a fourth `CompStruct` such that the four faces form the boundary
  ‚àÇŒî[3] of a 3-simplex.
* (3, 2)-filling: given three `CompStruct`s `f‚ÇÉ`, `f‚ÇÄ` and `f‚ÇÅ` which form a (3, 2)-horn,
  there exists a fourth `CompStruct` such that the four faces form the boundary
  ‚àÇŒî[3] of a 3-simplex.
-/
class Quasicategory‚ÇÇ (X : Truncated 2) where
  fill21 {x‚ÇÄ x‚ÇÅ x‚ÇÇ : X _‚¶ã0‚¶å‚ÇÇ}
      (e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ) (e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ) :
      Nonempty (Œ£ e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ, CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ)
  fill31 {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X _‚¶ã0‚¶å‚ÇÇ}
      {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ} {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÇ‚ÇÉ : Edge x‚ÇÇ x‚ÇÉ}
      {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ} {e‚ÇÅ‚ÇÉ : Edge x‚ÇÅ x‚ÇÉ} {e‚ÇÄ‚ÇÉ : Edge x‚ÇÄ x‚ÇÉ}
      (f‚ÇÉ : CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ)
      (f‚ÇÄ : CompStruct e‚ÇÅ‚ÇÇ e‚ÇÇ‚ÇÉ e‚ÇÅ‚ÇÉ)
      (f‚ÇÇ : CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÉ e‚ÇÄ‚ÇÉ) :
      Nonempty (CompStruct e‚ÇÄ‚ÇÇ e‚ÇÇ‚ÇÉ e‚ÇÄ‚ÇÉ)
  fill32 {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X _‚¶ã0‚¶å‚ÇÇ}
      {e‚ÇÄ‚ÇÅ : Edge x‚ÇÄ x‚ÇÅ} {e‚ÇÅ‚ÇÇ : Edge x‚ÇÅ x‚ÇÇ} {e‚ÇÇ‚ÇÉ : Edge x‚ÇÇ x‚ÇÉ}
      {e‚ÇÄ‚ÇÇ : Edge x‚ÇÄ x‚ÇÇ} {e‚ÇÅ‚ÇÉ : Edge x‚ÇÅ x‚ÇÉ} {e‚ÇÄ‚ÇÉ : Edge x‚ÇÄ x‚ÇÉ}
      (f‚ÇÉ : CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÇ e‚ÇÄ‚ÇÇ)
      (f‚ÇÄ : CompStruct e‚ÇÅ‚ÇÇ e‚ÇÇ‚ÇÉ e‚ÇÅ‚ÇÉ)
      (f‚ÇÅ : CompStruct e‚ÇÄ‚ÇÇ e‚ÇÇ‚ÇÉ e‚ÇÄ‚ÇÉ) :
      Nonempty (CompStruct e‚ÇÄ‚ÇÅ e‚ÇÅ‚ÇÉ e‚ÇÄ‚ÇÉ)

/--
Two edges `f` and `g` are left homotopic if there is a `CompStruct` with
(0, 1)-edge `f`, (1, 2)-edge `Edge.id` and (0, 2)-edge `g`. We use `Nonempty` to
have a `Prop` valued `HomotopicL`.
-/
abbrev HomotopicL {X : Truncated 2} {x y : X _‚¶ã0‚¶å‚ÇÇ} (f g : Edge x y) :=
  Nonempty (CompStruct f (id y) g)

/--
Two edges `f` and `g` are right homotopic if there is a `CompStruct` with
(0, 1)-edge `Edge.id`, (1, 2)-edge `f`, and (0, 2)-edge `g`. We use `Nonempty` to
have a `Prop` valued `HomotopicR`.
-/
abbrev HomotopicR {X : Truncated 2} {x y : X _‚¶ã0‚¶å‚ÇÇ} (f g : Edge x y) :=
  Nonempty (CompStruct (id x) f g)

section homotopy_eqrel
variable {X : Truncated 2}

/--
The left homotopy relation is reflexive.
-/
lemma HomotopicL.refl {x y : X _‚¶ã0‚¶å‚ÇÇ} {f : Edge x y} : HomotopicL f f := ‚ü®compId f‚ü©

/--
The left homotopy relation is symmetric.
-/
lemma HomotopicL.symm [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g : Edge x y} (hfg : HomotopicL f g) :
    HomotopicL g f := by
  rcases hfg with ‚ü®hfg‚ü©
  exact Quasicategory‚ÇÇ.fill31 hfg (idComp (id y)) (compId f)

/--
The left homotopy relation is transitive.
-/
lemma HomotopicL.trans [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g h : Edge x y} (hfg : HomotopicL f g)
    (hgh : HomotopicL g h) : HomotopicL f h := by
  rcases hfg with ‚ü®hfg‚ü©
  rcases hgh with ‚ü®hgh‚ü©
  exact Quasicategory‚ÇÇ.fill32 hfg (idComp (id y)) hgh

/--
The right homotopy relation is reflexive.
-/
lemma HomotopicR.refl {x y : X _‚¶ã0‚¶å‚ÇÇ} {f : Edge x y} : HomotopicR f f := ‚ü®idComp f‚ü©

/--
The right homotopy relation is symmetric.
-/
lemma HomotopicR.symm [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g : Edge x y} (hfg : HomotopicR f g) :
    HomotopicR g f := by
  rcases hfg with ‚ü®hfg‚ü©
  exact Quasicategory‚ÇÇ.fill32 (idComp (id x)) hfg (idComp f)

/--
The right homotopy relation is transitive.
-/
lemma HomotopicR.trans [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g h : Edge x y} (hfg : HomotopicR f g)
    (hgh : HomotopicR g h) : HomotopicR f h := by
  rcases hfg with ‚ü®hfg‚ü©
  rcases hgh with ‚ü®hgh‚ü©
  exact Quasicategory‚ÇÇ.fill31 (idComp (id x)) hfg hgh

/--
In a 2-truncated quasicategory, left homotopy implies right homotopy.
-/
lemma HomotopicL.homotopicR [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g : Edge x y}
    (h : HomotopicL f g) : HomotopicR f g := by
  rcases h with ‚ü®h‚ü©
  exact Quasicategory‚ÇÇ.fill32 (idComp f) (compId f) h

/--
In a 2-truncated quasicategory, right homotopy implies left homotopy.
-/
lemma HomotopicR.homotopicL [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g : Edge x y}
    (h : HomotopicR f g) : HomotopicL f g := by
  rcases h with ‚ü®h‚ü©
  exact Quasicategory‚ÇÇ.fill31 (idComp f) (compId f) h

/--
In a 2-truncated quasicategory, the right and left homotopy relations coincide.
-/
theorem homotopicL_iff_homotopicR [Quasicategory‚ÇÇ X] {x y : X _‚¶ã0‚¶å‚ÇÇ} {f g : Edge x y} :
    HomotopicL f g ‚Üî HomotopicR f g :=
  ‚ü®HomotopicL.homotopicR, HomotopicR.homotopicL‚ü©

end homotopy_eqrel

section homotopy_category

variable {A : Truncated 2} [Quasicategory‚ÇÇ A] {x y z : A _‚¶ã0‚¶å‚ÇÇ}

/--
Given `CompStruct f g h` and `CompStruct f' g' h'` with the same vertices and edges such
that `f` ‚âÉ `f'` and `g` ‚âÉ `g'`, then the long diagonal edges `h` and `h'` are also homotopic.
-/
lemma Edge.CompStruct.comp_unique {f f' : Edge x y} {g g' : Edge y z} {h h' : Edge x z}
    (s : CompStruct f g h) (s' : CompStruct f' g' h')
    (hf : HomotopicL f f') (hg : HomotopicL g g') : HomotopicL h h' := by
  rcases hg.homotopicR with ‚ü®hg‚ü©
  rcases hf with ‚ü®hf‚ü©
  let ‚ü®s‚ÇÅ‚ü© := Quasicategory‚ÇÇ.fill32 hf (idComp g') s'
  let ‚ü®s‚ÇÇ‚ü© := Quasicategory‚ÇÇ.fill31 (compId f) hg s‚ÇÅ
  exact Quasicategory‚ÇÇ.fill31 s (compId g) s‚ÇÇ

/--
Given two consecutive edges `f`, `g`  in a 2-truncated quasicategory, nonconstructively choose
an edge that is the diagonal of a 2-simplex with spine given by `f` and `g`. The `CompStruct`
witnessing this property is given by `Edge.composeStruct`.
-/
noncomputable def Edge.comp (f : Edge x y) (g : Edge y z) : Edge x z :=
  (Quasicategory‚ÇÇ.fill21 f g).some.1

/--
See `Edge.comp`
-/
noncomputable def Edge.compStruct (f : Edge x y) (g : Edge y z) : CompStruct f g (f.comp g) :=
  (Quasicategory‚ÇÇ.fill21 f g).some.2

/--
The homotopy category of a 2-truncated quasicategory `A` has as objects the vertices of `A`
-/
structure HomotopyCategory‚ÇÇ (A : Truncated 2) where
  /-- An object of the homotopy category is a vertex of `A`. -/
  pt : A _‚¶ã0‚¶å‚ÇÇ

/--
Left homotopy is an equivalence relation on the edges of `A`.
Remark: We could have equivalently chosen right homotopy, as shown by `homotopicL_iff_homotopicR`.
-/
instance instSetoidEdge (x y : A _‚¶ã0‚¶å‚ÇÇ) : Setoid (Edge x y) where
  r := HomotopicL
  iseqv := ‚ü®fun _ ‚Ü¶ HomotopicL.refl, HomotopicL.symm, HomotopicL.trans‚ü©

namespace HomotopyCategory‚ÇÇ

/--
The morphisms between two vertices `x`, `y` in `HomotopyCategory‚ÇÇ A` are homotopy classes
of edges between `x` and `y`.
-/
def Hom (x y : HomotopyCategory‚ÇÇ A) := Quotient (instSetoidEdge x.pt y.pt)

/--
Composition of morphisms in `HomotopyCategory‚ÇÇ A` is given by lifting the edge
chosen by `composeEdges`.
-/
noncomputable
instance : CategoryStruct (HomotopyCategory‚ÇÇ A) where
  Hom x y := Hom x y
  id x := Quotient.mk' (Edge.id x.pt)
  comp := Quotient.lift‚ÇÇ (fun f g ‚Ü¶ ‚ü¶comp f g‚üß)
    (fun _ _ _ _ hf hg ‚Ü¶ Quotient.sound
      (Edge.CompStruct.comp_unique (compStruct _ _) (compStruct _ _) hf hg))

omit [A.Quasicategory‚ÇÇ] in
/--
The function `HomotopyCategory‚ÇÇ.mk` taking a vertex of `A` and sending it to the corresponding
object of `HomotopyCategory‚ÇÇ A` is surjective.
-/
lemma mk_surjective : Function.Surjective (mk : A _‚¶ã0‚¶å‚ÇÇ ‚Üí _) :=
  fun ‚ü®x‚ü© ‚Ü¶ ‚ü®x, rfl‚ü©

/--
Any edge in the 2-truncated simplicial set `A` defines a morphism in the homotopy category
by taking its equivalence class.
-/
def homMk (f : Edge x y) : mk x ‚ü∂ mk y := ‚ü¶f‚üß

/--
Every morphism in the homotopy category `HomotopyCategory‚ÇÇ A` is the equivalence class of
an edge of `A`.
-/
lemma homMk_surjective : Function.Surjective (homMk : Edge x y ‚Üí _) := Quotient.mk_surjective

/--
The trivial (degenerate) edge at a vertex `x` is a representative for the
identity morphism `x ‚ü∂ x`.
-/
@[simp]
lemma homMk_id (x : HomotopyCategory‚ÇÇ A) : homMk (Edge.id x.pt) = ùüô x := rfl

end HomotopyCategory‚ÇÇ

open HomotopyCategory‚ÇÇ

/--
Left homotopic edges represent the same morphism in the homotopy category.
-/
lemma HomotopicL.congr_homotopyCategory‚ÇÇHomMk {f g : Edge x y} (h : HomotopicL f g) :
    homMk f = homMk g := Quotient.sound h

/--
Right homotopic edges represent the same morphism in the homotopy category.
-/
lemma HomotopicR.congr_homotopyCategory‚ÇÇHomMk {f g : Edge x y} (h : HomotopicR f g) :
    homMk f = homMk g := Quotient.sound h.homotopicL

/--
A `CompStruct f g h` is a witness for the fact that the morphisms represented by
`f` and `g` compose to the morphism represented by `h`.
-/
lemma Edge.CompStruct.homotopyCategory‚ÇÇ_fac {f : Edge x y} {g : Edge y z} {h : Edge x z}
    (s : CompStruct f g h) : homMk f ‚â´ homMk g = homMk h :=
  (comp_unique (compStruct _ _) s .refl .refl).congr_homotopyCategory‚ÇÇHomMk

/--
If we have a factorization `homMk f ‚â´ homMk g = homMk h`, this is the choice
of a structure `CompStruct f g h`.
-/
noncomputable def Edge.CompStruct.ofHomotopyCategory‚ÇÇFac
    {f : Edge x y} {g : Edge y z} {h : Edge x z}
    (fac : homMk f ‚â´ homMk g = homMk h) : CompStruct f g h := by
  dsimp [homMk, CategoryStruct.comp] at fac
  rw [Quotient.eq_iff_equiv] at fac
  exact (Quasicategory‚ÇÇ.fill32 (compStruct f g) (compId g) fac.some).some

/--
Given edges `f`, `g` and `h` of a `2`-truncated quasicategory,
there exists a structure `CompStruct f g h` iff
`homMk f ‚â´ homMk g = homMk h` holds in the homotopy category.
-/
lemma Edge.CompStruct.nonempty_iff {f : Edge x y} {g : Edge y z} {h : Edge x z} :
    Nonempty (CompStruct f g h) ‚Üî homMk f ‚â´ homMk g = homMk h :=
  ‚ü®fun ‚ü®h‚ü© ‚Ü¶ h.homotopyCategory‚ÇÇ_fac, fun h ‚Ü¶ ‚ü®.ofHomotopyCategory‚ÇÇFac h‚ü©‚ü©

noncomputable
instance : Category (HomotopyCategory‚ÇÇ A) where
  id_comp := by
    rintro _ _ ‚ü®f‚ü©
    exact ((compStruct _ f).comp_unique (idComp _) .refl .refl).congr_homotopyCategory‚ÇÇHomMk
  comp_id := by
    rintro _ _ ‚ü®f‚ü©
    exact ((compStruct _ _).comp_unique (compId _) .refl .refl).congr_homotopyCategory‚ÇÇHomMk
  assoc := by
    rintro _ _ _ _ ‚ü®f‚ü© ‚ü®g‚ü© ‚ü®h‚ü©
    exact (Quasicategory‚ÇÇ.fill31 (compStruct f g) (compStruct g h)
      (compStruct _ _)).some.homotopyCategory‚ÇÇ_fac

end homotopy_category

end SSet.Truncated
