/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Adam Topaz, JoÃ«l Riou
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Analysis.Convex.PathConnected
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Algebra.BigOperators.Ring.Finset
import Mathlib.AlgebraicTopology.SimplicialObject.Basic
import Mathlib.Topology.Category.TopCat.ULift
import Mathlib.Topology.Connected.PathConnected

/-!
# Topological simplices

We define the natural functor from `SimplexCategory` to `TopCat` sending `â¦‹nâ¦Œ` to the
topological `n`-simplex.
This is used to define `TopCat.toSSet` in `AlgebraicTopology.SingularSet`.
-/

universe u

open CategoryTheory Simplicial

namespace Convex

section

variable {ğ•œ E Î¹ : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] {s : Set E} (hs : Convex ğ•œ s)
  (t : Finset Î¹) (p : Î¹ â†’ s) (w : Î¹ â†’ ğ•œ) (hwâ‚€ : âˆ€ i, 0 â‰¤ w i) (hwâ‚ : t.sum w = 1)

include hwâ‚€ hwâ‚ hs

lemma finsetAffineCombination_mem :
    Finset.affineCombination ğ•œ t (fun i â†¦ (p i).1) w âˆˆ s :=
  hs.convexHull_eq.subset
    (convexHull_mono (by grind) (affineCombination_mem_convexHull (by tauto) hwâ‚))

@[simps]
noncomputable def affineCombination : s :=
  âŸ¨_, hs.finsetAffineCombination_mem t p w hwâ‚€ hwâ‚âŸ©

end

section

open unitInterval

@[simps]
def fromUnitInterval {E : Type*} [AddCommGroup E] [Module â„ E] {s : Set E} (hs : Convex â„ s)
    (xâ‚€ xâ‚ : s) (t : I) : s :=
  âŸ¨(1 - t.1) â€¢ xâ‚€ + t.1 â€¢ xâ‚, by
    apply hs.starConvex xâ‚€.2 xâ‚.2
    all_goals grindâŸ©

@[continuity]
lemma continuous_fromUnitInterval {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E]
    {s : Set E} (hs : Convex â„ s) (xâ‚€ xâ‚ : s) :
    Continuous (fromUnitInterval hs xâ‚€ xâ‚) :=
  Continuous.subtype_mk (by continuity) _

end

end Convex

namespace FunOnFinite

section

variable {M : Type*} [AddCommMonoid M] {X Y Z : Type*}

noncomputable def map [Finite X] [Finite Y] (f : X â†’ Y) (s : X â†’ M) : (Y â†’ M) :=
  Finsupp.equivFunOnFinite (Finsupp.mapDomain f (Finsupp.equivFunOnFinite.symm s))

lemma map_apply_apply [Fintype X] [Finite Y] [DecidableEq Y] (f : X â†’ Y) (s : X â†’ M) (y : Y) :
    map f s y = (Finset.univ.filter (fun (x : X) â†¦ f x = y)).sum s := by
  obtain âŸ¨s, rflâŸ© := Finsupp.equivFunOnFinite.surjective s
  dsimp [map]
  simp only [Equiv.symm_apply_apply, Finsupp.equivFunOnFinite_apply]
  nth_rw 1 [â† Finsupp.univ_sum_single s]
  rw [Finsupp.mapDomain_finset_sum]
  simp [Finset.sum_filter]
  congr
  ext x
  by_cases hx : f x = y
  Â· simp [hx]
  Â· rw [if_neg hx, Finsupp.single_eq_of_ne hx]

@[simp]
lemma map_piSingle [Finite X] [Finite Y] [DecidableEq X] [DecidableEq Y]
    (f : X â†’ Y) (x : X) (m : M) :
    map f (Pi.single x m) = Pi.single (f x) m := by
  simp [map]

variable (M) in
lemma map_id [Finite X] : map (id : X â†’ X) (M := M) = id := by
  ext s
  simp [map]

lemma map_comp [Finite X] [Finite Y] [Finite Z] (g : Y â†’ Z) (f : X â†’ Y) :
    map (g.comp f) (M := M) = (map g).comp (map f) := by
  ext s
  simp [map, Finsupp.mapDomain_comp]

end

section

variable (R M : Type*) [Semiring R] [AddCommMonoid M] [Module R M] {X Y Z : Type*}

noncomputable def linearMap [Finite X] [Finite Y] (f : X â†’ Y) :
    (X â†’ M) â†’â‚—[R] (Y â†’ M) :=
  (((Finsupp.linearEquivFunOnFinite R M Y)).comp (Finsupp.lmapDomain M R f)).comp
    (Finsupp.linearEquivFunOnFinite R M X).symm.toLinearMap

lemma linearMap_apply_apply
    [Fintype X] [Finite Y] [DecidableEq Y] (f : X â†’ Y) (s : X â†’ M) (y : Y) :
    linearMap R M f s y = (Finset.univ.filter (fun (x : X) â†¦ f x = y)).sum s := by
  apply map_apply_apply

@[simp]
lemma linearMap_piSingle [Finite X] [Finite Y] [DecidableEq X] [DecidableEq Y]
    (f : X â†’ Y) (x : X) (m : M) :
    linearMap R M f (Pi.single x m) = Pi.single (f x) m := by
  apply map_piSingle

variable (X) in
@[simp]
lemma linearMap_id [Finite X] : linearMap R M (id : X â†’ X) = .id := by
  classical
  aesop

lemma linearMap_comp [Finite X] [Finite Y] [Finite Z] (f : X â†’ Y) (g : Y â†’ Z) :
    linearMap R M (g.comp f) = (linearMap R M g).comp (linearMap R M f) := by
  classical
  aesop

end

end FunOnFinite

variable (R : Type*) [Ring R] [PartialOrder R]
  {ğ•œ : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  (X Y Z : Type*) [Fintype X] [Fintype Y] [Fintype Z]

/-- The simplex with a given type of vertices `X`. -/
def topologicalSimplex : Set (X â†’ R) := setOf (fun f â†¦ (âˆ€ i, 0 â‰¤ f i) âˆ§ âˆ‘ i, f i = 1)

variable {X} in
lemma mem_topologicalSimplex_iff (f : X â†’ R) :
    f âˆˆ topologicalSimplex R X â†” (âˆ€ i, 0 â‰¤ f i) âˆ§ âˆ‘ i, f i = 1 := Iff.rfl

/-- The simplex with a given type of vertices `X`, as a type. -/
abbrev TopologicalSimplex : Type _ := topologicalSimplex R X

namespace TopologicalSimplex

variable {R X Y Z}

instance : FunLike (TopologicalSimplex R X) X R where
  coe s := s.val
  coe_injective' := by aesop

@[continuity]
lemma continuous_apply (x : X) : Continuous (fun (s : TopologicalSimplex â„ X) â†¦ s x) :=
  (_root_.continuous_apply x).comp continuous_subtype_val

@[simp]
lemma mem (s : TopologicalSimplex R X) : â‡‘s âˆˆ topologicalSimplex R X := s.2

@[ext high]
lemma ext {s t : TopologicalSimplex R X} (h : (s : X â†’ R) = t) : s = t := by
  ext : 1
  assumption

@[simp]
lemma zero_le (s : TopologicalSimplex R X) (x : X) : 0 â‰¤ s x := s.2.1 x

@[simp]
lemma sum_eq_one (s : TopologicalSimplex R X) : âˆ‘ i, s i = 1 := s.2.2

@[simp]
lemma le_one [IsOrderedRing R] (s : TopologicalSimplex R X) (x : X) : s x â‰¤ 1 := by
  classical
  rw [â† s.sum_eq_one, â† Finset.sum_compl_add_sum {x}, Finset.sum_singleton,
    le_add_iff_nonneg_left]
  exact Finset.sum_nonneg (by simp)

section

variable [IsOrderedRing R]

variable (R X) in
lemma convex : Convex R (topologicalSimplex R X) := by
  rintro f âŸ¨hf, hf'âŸ© g âŸ¨hg, hg'âŸ© a b ha hb h
  refine âŸ¨fun i â†¦ ?_, ?_âŸ©
  Â· replace hf := hf i
    replace hg := hg i
    dsimp
    positivity
  Â· simp [Finset.sum_add_distrib, â† Finset.mul_sum, hf', hg', h]

noncomputable def affineCombination
    {Î¹ : Type*} (t : Finset Î¹) (p : Î¹ â†’ TopologicalSimplex ğ•œ X)
    (w : Î¹ â†’ ğ•œ) (hwâ‚€ : âˆ€ i, 0 â‰¤ w i) (hwâ‚ : t.sum w = 1) :
    TopologicalSimplex ğ•œ X :=
  (convex ğ•œ X).affineCombination t p w hwâ‚€ hwâ‚

lemma affineCombination_coe
    {Î¹ : Type*} (t : Finset Î¹) (p : Î¹ â†’ TopologicalSimplex ğ•œ X)
    (w : Î¹ â†’ ğ•œ) (hwâ‚€ : âˆ€ i, 0 â‰¤ w i) (hwâ‚ : t.sum w = 1) :
    (affineCombination t p w hwâ‚€ hwâ‚ : X â†’ ğ•œ) =
      Finset.affineCombination ğ•œ t (fun i â†¦ (p i).1) w :=
  rfl

@[simp]
lemma affineCombination_apply
    {Î¹ : Type*} (t : Finset Î¹) (p : Î¹ â†’ TopologicalSimplex ğ•œ X)
    (w : Î¹ â†’ ğ•œ) (hwâ‚€ : âˆ€ i, 0 â‰¤ w i) (hwâ‚ : t.sum w = 1) (x : X) :
    affineCombination t p w hwâ‚€ hwâ‚ x = t.sum (fun i â†¦ w i * p i x) := by
  rw [affineCombination_coe]
  aesop

section

variable [DecidableEq X]

variable (R) in
@[simp]
lemma piSingle_mem (x : X) :
    Pi.single x 1 âˆˆ topologicalSimplex R X := by
  rw [mem_topologicalSimplex_iff]
  refine âŸ¨fun y â†¦ ?_, ?_âŸ©
  Â· by_cases hy : y = x
    Â· subst hy
      simp
    Â· rw [Pi.single_eq_of_ne hy]
  Â· rw [Finset.sum_eq_single x, Pi.single_eq_same] <;> aesop

/-- The vertex of `TopologicalSimplex X` corresponding to `x : X`. -/
def vertex (x : X) : TopologicalSimplex R X :=
  âŸ¨_,piSingle_mem R xâŸ©

@[simp]
lemma vertex_eq_piSingle (x : X) :
    â‡‘(vertex (R := R) x) = Pi.single x 1 :=
  rfl

@[simp]
lemma vertex_apply_self (x : X) :
    vertex (R := R) x x = 1 := by
  simp [vertex_eq_piSingle]

lemma vertex_apply_eq_zero {x y : X} (h : y â‰  x) :
    vertex (R := R) x y = 0 := by
  simp [vertex_eq_piSingle, Pi.single_eq_of_ne h]

lemma eq_affineCombination (s : TopologicalSimplex ğ•œ X) :
    s = affineCombination .univ vertex s (by simp) (by simp) := by
  ext x
  simp
  rw [Finset.sum_eq_single x _ (by simp), Pi.single_eq_same, mul_one]
  intro _ _ h
  simp [Pi.single_eq_of_ne' h]

lemma exists_eq_affineCombination (s : TopologicalSimplex ğ•œ X) :
    âˆƒ (w : X â†’ ğ•œ) (hwâ‚€ : âˆ€ (i : X), 0 â‰¤ w i) (hwâ‚ : âˆ‘ i, w i = 1),
      s = affineCombination .univ vertex w hwâ‚€ hwâ‚ :=
  âŸ¨_, _, _, eq_affineCombination sâŸ©

lemma vertex_injective [Nontrivial R] :
    Function.Injective (vertex (R := R) (X := X)) := by
  intro x y h
  replace h := DFunLike.congr_fun h x
  by_contra!
  simp [Pi.single_eq_of_ne this] at h

variable (X) in
lemma eq_convexHull :
    topologicalSimplex ğ•œ X =
      convexHull ğ•œ (Set.range (fun x â†¦ â‡‘(vertex x))) := by
  refine subset_antisymm (fun f hf â†¦ ?_)
    (convexHull_min (by rintro _ âŸ¨x, rflâŸ©; simp) (convex ğ•œ X))
  obtain âŸ¨w, hwâ‚€, hwâ‚, hâŸ© := exists_eq_affineCombination âŸ¨f, hfâŸ©
  rw [Subtype.ext_iff] at h
  subst h
  apply affineCombination_mem_convexHull
  all_goals aesop

end

section

variable [IsStrictOrderedRing R]

instance [Nonempty X] : Nonempty (TopologicalSimplex R X) := by
  classical
  exact âŸ¨vertex (Classical.arbitrary _)âŸ©

instance [Nontrivial X] : Nontrivial (TopologicalSimplex R X) where
  exists_pair_ne := by
    classical
    obtain âŸ¨x, y, hxyâŸ© := exists_pair_ne X
    exact âŸ¨vertex x, vertex y, fun h â†¦ hxy (vertex_injective h)âŸ©

instance [Subsingleton X] : Subsingleton (TopologicalSimplex R X) where
  allEq s t := by
    ext i
    have (u : TopologicalSimplex R X) : u i = 1 := by
      rw [â† u.sum_eq_one, Finset.sum_eq_single i _ (by simp)]
      intro j _ hj
      exact (hj (Subsingleton.elim j i)).elim
    simp [this]

instance [Unique X] : Unique (TopologicalSimplex R X) where
  default := âŸ¨1, by simp, by simpâŸ©
  uniq := by subsingleton

end

instance [Nonempty X] : PathConnectedSpace (TopologicalSimplex â„ X) :=
  isPathConnected_iff_pathConnectedSpace.1
    ((convex â„ X).isPathConnected Set.Nonempty.of_subtype)

-- to be moved
@[continuity]
lemma continuous_linearMap (f : X â†’ Y) : Continuous (FunOnFinite.linearMap â„ â„ f) := by
  classical
  refine continuous_pi (fun y â†¦ ?_)
  simp only [FunOnFinite.linearMap_apply_apply]
  apply continuous_finset_sum
  continuity

lemma linearEquivFunOnFinite_single [DecidableEq X] (x : X) :
    (Finsupp.linearEquivFunOnFinite â„ â„ X) (Finsupp.single x 1) = Pi.single x 1 := by
  ext y
  rw [Finsupp.linearEquivFunOnFinite_apply]
  by_cases hy : y = x
  Â· subst hy
    simp
  Â· rw [Finsupp.single_eq_of_ne (Ne.symm hy), Pi.single_eq_of_ne hy]

lemma image_linearMap (f : X â†’ Y) :
    Set.image (FunOnFinite.linearMap ğ•œ ğ•œ f) (topologicalSimplex ğ•œ X) âŠ† topologicalSimplex ğ•œ Y := by
  classical
  simp only [eq_convexHull, (FunOnFinite.linearMap ğ•œ ğ•œ f).isLinear.image_convexHull]
  apply convexHull_mono
  rintro _ âŸ¨_, âŸ¨x, rflâŸ©, rflâŸ©
  exact âŸ¨f x, by simpâŸ©

noncomputable def map (f : X â†’ Y) (s : TopologicalSimplex ğ•œ X) : TopologicalSimplex ğ•œ Y :=
  âŸ¨FunOnFinite.linearMap ğ•œ ğ•œ f s, image_linearMap f (by aesop)âŸ©

@[simp]
lemma map_coe (f : X â†’ Y) (s : TopologicalSimplex ğ•œ X) :
    â‡‘(map f s) = FunOnFinite.linearMap ğ•œ ğ•œ f s := rfl

@[simp]
lemma map_id_apply (x) : map (ğ•œ := ğ•œ) (id : X â†’ X) x = x := by
  aesop

lemma map_comp_apply (f : X â†’ Y) (g : Y â†’ Z) (x) :
    map (ğ•œ := ğ•œ) g (map f x) = map (g.comp f) x := by
  ext
  simp [FunOnFinite.linearMap_comp]

@[continuity, fun_prop]
lemma continuous_map (f : X â†’ Y) : Continuous (map (ğ•œ := â„) f) :=
  Continuous.subtype_mk ((continuous_linearMap f).comp continuous_induced_dom) _

section

open unitInterval

variable [DecidableEq X] (xâ‚€ xâ‚ : X)

noncomputable def fromUnitInterval (t : I) : TopologicalSimplex â„ X :=
  (convex â„ X).fromUnitInterval (vertex xâ‚€) (vertex xâ‚) t

@[simp]
lemma fromUnitInterval_coe (t : I) :
    â‡‘(fromUnitInterval xâ‚€ xâ‚ t) =
      (1 - t.1) â€¢ (Pi.single xâ‚€ 1 : _ â†’ â„) + t.1 â€¢ (Pi.single xâ‚ 1 : _ â†’ â„) :=
  rfl

@[simp]
lemma fromUnitInterval_zero :
    fromUnitInterval xâ‚€ xâ‚ 0 = vertex xâ‚€ := by
  aesop

@[simp]
lemma fromUnitInterval_one :
    fromUnitInterval xâ‚€ xâ‚ 1 = vertex xâ‚ := by
  aesop

@[continuity]
lemma continuous_fromUnitInterval :
    Continuous (fromUnitInterval xâ‚€ xâ‚) :=
  (convex â„ X).continuous_fromUnitInterval _ _

@[simps]
noncomputable def homeoUnitInterval (h : xâ‚€ â‰  xâ‚) (h' : {xâ‚€, xâ‚} = Finset.univ) :
    TopologicalSimplex â„ X â‰ƒâ‚œ I where
  toFun s := âŸ¨s xâ‚, by simp, by simpâŸ©
  invFun := fromUnitInterval xâ‚€ xâ‚
  left_inv s := by
    ext x
    have : s xâ‚€ + s xâ‚ = 1 := by
      rw [â† s.sum_eq_one, â† h', Finset.sum_insert (by simpa), Finset.sum_singleton]
    have hx := Finset.mem_univ x
    simp only [â† h', Finset.mem_insert, Finset.mem_singleton] at hx
    obtain rfl | rfl := hx
    Â· simp [Pi.single_eq_of_ne h]
      grind
    Â· simp [Pi.single_eq_of_ne' h]
  right_inv t := by
    ext
    simp [Pi.single_eq_of_ne' h]

noncomputable def homeoUnitInterval' : TopologicalSimplex â„ (Fin 2) â‰ƒâ‚œ I :=
  homeoUnitInterval _ _ Fin.zero_ne_one (by aesop)

end
end
end TopologicalSimplex

namespace SimplexCategory

@[simp]
lemma concreteCategoryHom_id (n : SimplexCategory) : ConcreteCategory.hom (ğŸ™ n) = .id := rfl

attribute [local simp] TopologicalSimplex.map_comp_apply

/-- The functor `SimplexCategory â¥¤ TopCat.{0}`
associating the topological `n`-simplex to `â¦‹nâ¦Œ : SimplexCategory`. -/
@[simps obj map]
noncomputable def toTopâ‚€ : CosimplicialObject TopCat.{0} where
  obj n := TopCat.of (TopologicalSimplex â„ (Fin (n.len + 1)))
  map f := TopCat.ofHom âŸ¨_, TopologicalSimplex.continuous_map fâŸ©

/-- The functor `SimplexCategory â¥¤ TopCat.{u}`
associating the topological `n`-simplex to `â¦‹nâ¦Œ : SimplexCategory`. -/
@[simps! obj map, pp_with_univ]
noncomputable def toTop : CosimplicialObject TopCat.{u} :=
  toTopâ‚€ â‹™ TopCat.uliftFunctor

end SimplexCategory


/-
namespace SimplexCategory

open Simplicial NNReal CategoryTheory

/-- The topological simplex associated to `x : SimplexCategory`.
  This is the object part of the functor `SimplexCategory.toTop`. -/
def toTopObj (x : SimplexCategory) := { f : ToType x â†’ â„â‰¥0 | âˆ‘ i, f i = 1 }

instance (x : SimplexCategory) : CoeFun x.toTopObj fun _ => ToType x â†’ â„â‰¥0 :=
  âŸ¨fun f => (f : ToType x â†’ â„â‰¥0)âŸ©

@[ext]
theorem toTopObj.ext {x : SimplexCategory} (f g : x.toTopObj) : (f : ToType x â†’ â„â‰¥0) = g â†’ f = g :=
  Subtype.ext

@[simp]
lemma toTopObj_zero_apply_zero (f : â¦‹0â¦Œ.toTopObj) : f 0 = 1 := by
  simpa [toType_apply] using show âˆ‘ _, _ = _ from f.2

lemma toTopObj_one_add_eq_one (f : â¦‹1â¦Œ.toTopObj) : f 0 + f 1 = 1 := by
  simpa [toType_apply, Finset.sum] using show âˆ‘ _, _ = _ from f.2

lemma toTopObj_one_coe_add_coe_eq_one (f : â¦‹1â¦Œ.toTopObj) : (f 0 : â„) + f 1 = 1 := by
  norm_cast
  rw [toTopObj_one_add_eq_one]

instance (x : SimplexCategory) : Nonempty x.toTopObj :=
  âŸ¨âŸ¨Pi.single 0 1, (show âˆ‘ _, _ = _ by simp)âŸ©âŸ©

instance : Unique â¦‹0â¦Œ.toTopObj :=
  âŸ¨âŸ¨1, show âˆ‘ _, _ = _ by simp [toType_apply]âŸ©, fun f â†¦ by ext i; fin_cases i; simpâŸ©

open unitInterval in
/-- The one-dimensional topological simplex is homeomorphic to the unit interval. -/
def toTopObjOneHomeo : â¦‹1â¦Œ.toTopObj â‰ƒâ‚œ I where
  toFun f := âŸ¨f 0, (f 0).2, toTopObj_one_coe_add_coe_eq_one f â–¸ le_add_of_nonneg_right (f 1).2âŸ©
  invFun x := âŸ¨![toNNReal x, toNNReal (Ïƒ x)],
    show âˆ‘ _, _ = _ by ext; simp [toType_apply, Finset.sum]âŸ©
  left_inv f := by ext i; fin_cases i <;> simp [â† toTopObj_one_coe_add_coe_eq_one f]
  right_inv x := by simp
  continuous_toFun := .subtype_mk (continuous_subtype_val.comp
    ((continuous_apply _).comp continuous_subtype_val)) _
  continuous_invFun := .subtype_mk (continuous_pi fun i â†¦ by fin_cases i <;> dsimp <;> fun_prop) _

open unitInterval in
instance (x : SimplexCategory) : PathConnectedSpace x.toTopObj := by
  refine âŸ¨inferInstance, ?_âŸ©
  intro f g
  dsimp [toTopObj, toType_apply] at f g âŠ¢
  refine âŸ¨âŸ¨fun j â†¦ âŸ¨toNNReal (symm j) â€¢ f.1 + toNNReal j â€¢ g.1, ?_âŸ©, ?_âŸ©, ?_, ?_âŸ©
  Â· ext; simp [Finset.sum_add_distrib, â† Finset.mul_sum, f.2, g.2]
  Â· fun_prop
  Â· ext; simp
  Â· ext; simp

open Classical in
/-- A morphism in `SimplexCategory` induces a map on the associated topological spaces. -/
def toTopMap {x y : SimplexCategory} (f : x âŸ¶ y) (g : x.toTopObj) : y.toTopObj :=
  âŸ¨fun i => âˆ‘ j âˆˆ Finset.univ.filter (f Â· = i), g j, by
    simp only [toTopObj, Set.mem_setOf]
    rw [â† Finset.sum_biUnion]
    Â· have hg : âˆ‘ i : ToType x, g i = 1 := g.2
      convert hg
      simp [Finset.eq_univ_iff_forall]
    Â· convert Set.pairwiseDisjoint_filter _ _ _âŸ©

open Classical in
@[simp]
theorem coe_toTopMap {x y : SimplexCategory} (f : x âŸ¶ y) (g : x.toTopObj) (i : ToType y) :
    toTopMap f g i = âˆ‘ j âˆˆ Finset.univ.filter (f Â· = i), g j :=
  rfl

@[continuity, fun_prop]
theorem continuous_toTopMap {x y : SimplexCategory} (f : x âŸ¶ y) : Continuous (toTopMap f) := by
  refine Continuous.subtype_mk (continuous_pi fun i => ?_) _
  dsimp only [coe_toTopMap]
  exact continuous_finset_sum _ (fun j _ => (continuous_apply _).comp continuous_subtype_val)

/-- The functor `SimplexCategory â¥¤ TopCat.{0}`
associating the topological `n`-simplex to `â¦‹nâ¦Œ : SimplexCategory`. -/
@[simps obj map]
def toTopâ‚€ : SimplexCategory â¥¤ TopCat.{0} where
  obj x := TopCat.of x.toTopObj
  map f := TopCat.ofHom âŸ¨toTopMap f, by fun_propâŸ©
  map_id := by
    classical
    intro Î”
    ext f
    simp [Finset.sum_filter]
  map_comp := fun f g => by
    classical
    ext h : 3
    dsimp
    rw [â† Finset.sum_biUnion]
    Â· apply Finset.sum_congr
      Â· exact Finset.ext (fun j => âŸ¨fun hj => by simpa using hj, fun hj => by simpa using hjâŸ©)
      Â· tauto
    Â· apply Set.pairwiseDisjoint_filter

/-- The functor `SimplexCategory â¥¤ TopCat.{u}`
associating the topological `n`-simplex to `â¦‹nâ¦Œ : SimplexCategory`. -/
@[simps! obj map, pp_with_univ]
def toTop : SimplexCategory â¥¤ TopCat.{u} :=
  toTopâ‚€ â‹™ TopCat.uliftFunctor

end SimplexCategory
-/
