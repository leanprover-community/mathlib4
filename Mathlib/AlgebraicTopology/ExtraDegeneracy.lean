/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.AlternatingFaceMapComplex
import Mathlib.AlgebraicTopology.SimplicialSet.StdSimplex
import Mathlib.AlgebraicTopology.CechNerve
import Mathlib.Algebra.Homology.Homotopy
import Mathlib.Tactic.FinCases

/-!

# Augmented simplicial objects with an extra degeneracy

In simplicial homotopy theory, in order to prove that the connected components
of a simplicial set `X` are contractible, it suffices to construct an extra
degeneracy as it is defined in *Simplicial Homotopy Theory* by Goerss-Jardine p. 190.
It consists of a series of maps `œÄ‚ÇÄ X ‚Üí X _‚¶ã0‚¶å` and `X _‚¶ãn‚¶å ‚Üí X _‚¶ãn+1‚¶å` which
behave formally like an extra degeneracy `œÉ (-1)`. It can be thought as a datum
associated to the augmented simplicial set `X ‚Üí œÄ‚ÇÄ X`.

In this file, we adapt this definition to the case of augmented
simplicial objects in any category.

## Main definitions

- the structure `ExtraDegeneracy X` for any `X : SimplicialObject.Augmented C`
- `ExtraDegeneracy.map`: extra degeneracies are preserved by the application of any
functor `C ‚•§ D`
- `SSet.Augmented.StandardSimplex.extraDegeneracy`: the standard `n`-simplex has
an extra degeneracy
- `Arrow.AugmentedCechNerve.extraDegeneracy`: the ƒåech nerve of a split
epimorphism has an extra degeneracy
- `ExtraDegeneracy.homotopyEquiv`: in the case the category `C` is preadditive,
if we have an extra degeneracy on `X : SimplicialObject.Augmented C`, then
the augmentation on the alternating face map complex of `X` is a homotopy
equivalence.

## References
* [Paul G. Goerss, John F. Jardine, *Simplicial Homotopy Theory*][goerss-jardine-2009]

-/


open CategoryTheory Category SimplicialObject.Augmented Opposite Simplicial

namespace CategoryTheory

namespace SimplicialObject

namespace Augmented

variable {C : Type*} [Category C]

/-- The datum of an extra degeneracy is a technical condition on
augmented simplicial objects. The morphisms `s'` and `s n` of the
structure formally behave like extra degeneracies `œÉ (-1)`. -/
@[ext]
structure ExtraDegeneracy (X : SimplicialObject.Augmented C) where
  /-- a section of the augmentation in dimension `0` -/
  s' : point.obj X ‚ü∂ drop.obj X _‚¶ã0‚¶å
  /-- the extra degeneracy -/
  s : ‚àÄ n : ‚Ñï, drop.obj X _‚¶ãn‚¶å ‚ü∂ drop.obj X _‚¶ãn + 1‚¶å
  s'_comp_Œµ : s' ‚â´ X.hom.app (op ‚¶ã0‚¶å) = ùüô _ := by aesop_cat
  s‚ÇÄ_comp_Œ¥‚ÇÅ : s 0 ‚â´ X.left.Œ¥ 1 = X.hom.app (op ‚¶ã0‚¶å) ‚â´ s' := by aesop_cat
  s_comp_Œ¥‚ÇÄ : ‚àÄ n : ‚Ñï, s n ‚â´ X.left.Œ¥ 0 = ùüô _ := by aesop_cat
  s_comp_Œ¥ :
    ‚àÄ (n : ‚Ñï) (i : Fin (n + 2)), s (n + 1) ‚â´ X.left.Œ¥ i.succ = X.left.Œ¥ i ‚â´ s n := by aesop_cat
  s_comp_œÉ :
    ‚àÄ (n : ‚Ñï) (i : Fin (n + 1)), s n ‚â´ X.left.œÉ i.succ = X.left.œÉ i ‚â´ s (n + 1) := by aesop_cat

namespace ExtraDegeneracy

attribute [reassoc] s‚ÇÄ_comp_Œ¥‚ÇÅ s_comp_Œ¥ s_comp_œÉ
attribute [reassoc (attr := simp)] s'_comp_Œµ s_comp_Œ¥‚ÇÄ

/-- If `ed` is an extra degeneracy for `X : SimplicialObject.Augmented C` and
`F : C ‚•§ D` is a functor, then `ed.map F` is an extra degeneracy for the
augmented simplicial object in `D` obtained by applying `F` to `X`. -/
def map {D : Type*} [Category D] {X : SimplicialObject.Augmented C} (ed : ExtraDegeneracy X)
    (F : C ‚•§ D) : ExtraDegeneracy (((whiskering _ _).obj F).obj X) where
  s' := F.map ed.s'
  s n := F.map (ed.s n)
  s'_comp_Œµ := by
    dsimp
    rw [comp_id, ‚Üê F.map_comp, ed.s'_comp_Œµ]
    dsimp only [point_obj]
    rw [F.map_id]
  s‚ÇÄ_comp_Œ¥‚ÇÅ := by
    dsimp
    rw [comp_id, ‚Üê F.map_comp]
    dsimp [SimplicialObject.whiskering, SimplicialObject.Œ¥]
    rw [‚Üê F.map_comp]
    erw [ed.s‚ÇÄ_comp_Œ¥‚ÇÅ]
  s_comp_Œ¥‚ÇÄ n := by
    dsimp [SimplicialObject.Œ¥]
    rw [‚Üê F.map_comp]
    erw [ed.s_comp_Œ¥‚ÇÄ]
    dsimp
    rw [F.map_id]
  s_comp_Œ¥ n i := by
    dsimp [SimplicialObject.Œ¥]
    rw [‚Üê F.map_comp, ‚Üê F.map_comp]
    erw [ed.s_comp_Œ¥]
    rfl
  s_comp_œÉ n i := by
    dsimp [SimplicialObject.whiskering, SimplicialObject.œÉ]
    rw [‚Üê F.map_comp, ‚Üê F.map_comp]
    erw [ed.s_comp_œÉ]
    rfl

/-- If `X` and `Y` are isomorphic augmented simplicial objects, then an extra
degeneracy for `X` gives also an extra degeneracy for `Y` -/
def ofIso {X Y : SimplicialObject.Augmented C} (e : X ‚âÖ Y) (ed : ExtraDegeneracy X) :
    ExtraDegeneracy Y where
  s' := (point.mapIso e).inv ‚â´ ed.s' ‚â´ (drop.mapIso e).hom.app (op ‚¶ã0‚¶å)
  s n := (drop.mapIso e).inv.app (op ‚¶ãn‚¶å) ‚â´ ed.s n ‚â´ (drop.mapIso e).hom.app (op ‚¶ãn + 1‚¶å)
  s'_comp_Œµ := by
    simpa only [Functor.mapIso, assoc, w‚ÇÄ, ed.s'_comp_Œµ_assoc] using (point.mapIso e).inv_hom_id
  s‚ÇÄ_comp_Œ¥‚ÇÅ := by
    have h := w‚ÇÄ e.inv
    dsimp at h ‚ä¢
    simp only [assoc, ‚Üê SimplicialObject.Œ¥_naturality, ed.s‚ÇÄ_comp_Œ¥‚ÇÅ_assoc, reassoc_of% h]
  s_comp_Œ¥‚ÇÄ n := by
    have h := ed.s_comp_Œ¥‚ÇÄ
    dsimp at h ‚ä¢
    simpa only [assoc, ‚Üê SimplicialObject.Œ¥_naturality, reassoc_of% h] using
      congr_app (drop.mapIso e).inv_hom_id (op ‚¶ãn‚¶å)
  s_comp_Œ¥ n i := by
    have h := ed.s_comp_Œ¥ n i
    dsimp at h ‚ä¢
    simp only [assoc, ‚Üê SimplicialObject.Œ¥_naturality, reassoc_of% h,
      ‚Üê SimplicialObject.Œ¥_naturality_assoc]
  s_comp_œÉ n i := by
    have h := ed.s_comp_œÉ n i
    dsimp at h ‚ä¢
    simp only [assoc, ‚Üê SimplicialObject.œÉ_naturality, reassoc_of% h,
      ‚Üê SimplicialObject.œÉ_naturality_assoc]

end ExtraDegeneracy

end Augmented

end SimplicialObject

end CategoryTheory

namespace SSet

namespace Augmented

namespace StandardSimplex

/-- When `[Zero X]`, the shift of a map `f : Fin n ‚Üí X`
is a map `Fin (n+1) ‚Üí X` which sends `0` to `0` and `i.succ` to `f i`. -/
def shiftFun {n : ‚Ñï} {X : Type*} [Zero X] (f : Fin n ‚Üí X) (i : Fin (n + 1)) : X :=
  Matrix.vecCons 0 f i

@[simp]
theorem shiftFun_zero {n : ‚Ñï} {X : Type*} [Zero X] (f : Fin n ‚Üí X) : shiftFun f 0 = 0 :=
  rfl

@[deprecated (since := "2025-04-19")]
alias shiftFun_0 := shiftFun_zero

@[simp]
theorem shiftFun_succ {n : ‚Ñï} {X : Type*} [Zero X] (f : Fin n ‚Üí X) (i : Fin n) :
    shiftFun f i.succ = f i :=
  rfl

/-- The shift of a morphism `f : ‚¶ãn‚¶å ‚Üí Œî` in `SimplexCategory` corresponds to
the monotone map which sends `0` to `0` and `i.succ` to `f.toOrderHom i`. -/
@[simp]
def shift {n : ‚Ñï} {Œî : SimplexCategory} (f : ‚¶ãn‚¶å ‚ü∂ Œî) : ‚¶ãn + 1‚¶å ‚ü∂ Œî :=
  SimplexCategory.Hom.mk
    { toFun := shiftFun f.toOrderHom
      monotone' := fun i‚ÇÅ i‚ÇÇ hi => by
        by_cases h‚ÇÅ : i‚ÇÅ = 0
        ¬∑ subst h‚ÇÅ
          simp only [shiftFun_zero, Fin.zero_le]
        ¬∑ have h‚ÇÇ : i‚ÇÇ ‚â† 0 := by
            intro h‚ÇÇ
            subst h‚ÇÇ
            exact h‚ÇÅ (le_antisymm hi (Fin.zero_le _))
          obtain ‚ü®j‚ÇÅ, hj‚ÇÅ‚ü© := Fin.eq_succ_of_ne_zero h‚ÇÅ
          obtain ‚ü®j‚ÇÇ, hj‚ÇÇ‚ü© := Fin.eq_succ_of_ne_zero h‚ÇÇ
          substs hj‚ÇÅ hj‚ÇÇ
          simpa only [shiftFun_succ] using f.toOrderHom.monotone (Fin.succ_le_succ_iff.mp hi) }

open SSet.stdSimplex in
/-- The obvious extra degeneracy on the standard simplex. -/
protected noncomputable def extraDegeneracy (Œî : SimplexCategory) :
    SimplicialObject.Augmented.ExtraDegeneracy (stdSimplex.obj Œî) where
  s' _ := objMk (OrderHom.const _ 0)
  s _ f := objEquiv.symm (shift (objEquiv f))
  s'_comp_Œµ := by
    dsimp
    subsingleton
  s‚ÇÄ_comp_Œ¥‚ÇÅ := by
    dsimp
    ext1 x
    apply objEquiv.injective
    ext j
    fin_cases j
    rfl
  s_comp_Œ¥‚ÇÄ n := by
    ext1 œÜ
    apply objEquiv.injective
    apply SimplexCategory.Hom.ext
    ext i : 2
    dsimp [SimplicialObject.Œ¥, SimplexCategory.Œ¥, SSet.stdSimplex,
      objEquiv, Equiv.ulift, uliftFunctor]
  s_comp_Œ¥ n i := by
    ext1 œÜ
    apply objEquiv.injective
    apply SimplexCategory.Hom.ext
    ext j : 2
    dsimp [SimplicialObject.Œ¥, SimplexCategory.Œ¥, SSet.stdSimplex,
      objEquiv, Equiv.ulift, uliftFunctor]
    cases j using Fin.cases <;> simp
  s_comp_œÉ n i := by
    ext1 œÜ
    apply objEquiv.injective
    apply SimplexCategory.Hom.ext
    ext j : 2
    dsimp [SimplicialObject.œÉ, SimplexCategory.œÉ, SSet.stdSimplex, objEquiv, Equiv.ulift,
      uliftFunctor, Function.comp_def]
    cases j using Fin.cases <;> simp

instance nonempty_extraDegeneracy_stdSimplex (Œî : SimplexCategory) :
    Nonempty (SimplicialObject.Augmented.ExtraDegeneracy (stdSimplex.obj Œî)) :=
  ‚ü®StandardSimplex.extraDegeneracy Œî‚ü©

end StandardSimplex

end Augmented

end SSet

namespace CategoryTheory

open Limits

namespace Arrow

namespace AugmentedCechNerve

variable {C : Type*} [Category C] (f : Arrow C)
  [‚àÄ n : ‚Ñï, HasWidePullback f.right (fun _ : Fin (n + 1) => f.left) fun _ => f.hom]
  (S : SplitEpi f.hom)

/-- The extra degeneracy map on the ƒåech nerve of a split epi. It is
given on the `0`-projection by the given section of the split epi,
and by shifting the indices on the other projections. -/
noncomputable def ExtraDegeneracy.s (n : ‚Ñï) :
    f.cechNerve.obj (op ‚¶ãn‚¶å) ‚ü∂ f.cechNerve.obj (op ‚¶ãn + 1‚¶å) :=
  WidePullback.lift (WidePullback.base _)
    (Fin.cases (WidePullback.base _ ‚â´ S.section_) (WidePullback.œÄ _))
    fun i => by
      cases i using Fin.cases <;> simp

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11119): @[simp] removed as the linter complains the LHS is not in normal form
-- The problem is that the type of `ExtraDegeneracy.s` is not in normal form, this causes the `erw`
-- in the proofs below.
theorem ExtraDegeneracy.s_comp_œÄ_0 (n : ‚Ñï) :
    ExtraDegeneracy.s f S n ‚â´ WidePullback.œÄ _ 0 =
      @WidePullback.base _ _ _ f.right (fun _ : Fin (n + 1) => f.left) (fun _ => f.hom) _ ‚â´
        S.section_ := by
  dsimp [ExtraDegeneracy.s]
  simp [WidePullback.lift_œÄ]

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11119): @[simp] removed as the linter complains the LHS is not in normal form
theorem ExtraDegeneracy.s_comp_œÄ_succ (n : ‚Ñï) (i : Fin (n + 1)) :
    ExtraDegeneracy.s f S n ‚â´ WidePullback.œÄ _ i.succ =
      @WidePullback.œÄ _ _ _ f.right (fun _ : Fin (n + 1) => f.left) (fun _ => f.hom) _ i := by
  simp [ExtraDegeneracy.s]

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11119): @[simp] removed as the linter complains the LHS is not in normal form
theorem ExtraDegeneracy.s_comp_base (n : ‚Ñï) :
    ExtraDegeneracy.s f S n ‚â´ WidePullback.base _ = WidePullback.base _ := by
  apply WidePullback.lift_base

/-- The augmented ƒåech nerve associated to a split epimorphism has an extra degeneracy. -/
noncomputable def extraDegeneracy :
    SimplicialObject.Augmented.ExtraDegeneracy f.augmentedCechNerve where
  s' := S.section_ ‚â´ WidePullback.lift f.hom (fun _ => ùüô _) fun i => by rw [id_comp]
  s n := ExtraDegeneracy.s f S n
  s'_comp_Œµ := by
    dsimp
    simp only [augmentedCechNerve_hom_app, assoc, WidePullback.lift_base, SplitEpi.id]
  s‚ÇÄ_comp_Œ¥‚ÇÅ := by
    dsimp [cechNerve, SimplicialObject.Œ¥, SimplexCategory.Œ¥]
    ext j
    ¬∑ fin_cases j
      simpa only [assoc, WidePullback.lift_œÄ, comp_id] using ExtraDegeneracy.s_comp_œÄ_0 f S 0
    ¬∑ simpa only [assoc, WidePullback.lift_base, SplitEpi.id, comp_id] using
        ExtraDegeneracy.s_comp_base f S 0
  s_comp_Œ¥‚ÇÄ n := by
    dsimp [cechNerve, SimplicialObject.Œ¥, SimplexCategory.Œ¥]
    ext j
    ¬∑ simpa only [assoc, WidePullback.lift_œÄ, id_comp] using ExtraDegeneracy.s_comp_œÄ_succ f S n j
    ¬∑ simpa only [assoc, WidePullback.lift_base, id_comp] using ExtraDegeneracy.s_comp_base f S n
  s_comp_Œ¥ n i := by
    dsimp [SimplicialObject.Œ¥, SimplexCategory.Œ¥]
    ext j
    ¬∑ simp only [assoc, WidePullback.lift_œÄ]
      cases j using Fin.cases with
      | zero =>
        rw [Fin.succ_succAbove_zero]
        erw [ExtraDegeneracy.s_comp_œÄ_0, ExtraDegeneracy.s_comp_œÄ_0]
        dsimp
        simp only [WidePullback.lift_base_assoc]
      | succ k =>
        erw [Fin.succ_succAbove_succ, ExtraDegeneracy.s_comp_œÄ_succ,
          ExtraDegeneracy.s_comp_œÄ_succ]
        simp only [WidePullback.lift_œÄ]
    ¬∑ simp only [assoc, WidePullback.lift_base]
      erw [ExtraDegeneracy.s_comp_base, ExtraDegeneracy.s_comp_base]
      dsimp
      simp only [WidePullback.lift_base]
  s_comp_œÉ n i := by
    dsimp [cechNerve, SimplicialObject.œÉ, SimplexCategory.œÉ]
    ext j
    ¬∑ simp only [assoc, WidePullback.lift_œÄ]
      cases j using Fin.cases with
      | zero =>
        erw [ExtraDegeneracy.s_comp_œÄ_0, ExtraDegeneracy.s_comp_œÄ_0]
        dsimp
        simp only [WidePullback.lift_base_assoc]
      | succ k =>
        erw [Fin.succ_predAbove_succ, ExtraDegeneracy.s_comp_œÄ_succ,
          ExtraDegeneracy.s_comp_œÄ_succ]
        simp only [WidePullback.lift_œÄ]
    ¬∑ simp only [assoc, WidePullback.lift_base]
      erw [ExtraDegeneracy.s_comp_base, ExtraDegeneracy.s_comp_base]
      dsimp
      simp only [WidePullback.lift_base]

end AugmentedCechNerve

end Arrow

namespace SimplicialObject

namespace Augmented

namespace ExtraDegeneracy

open AlgebraicTopology CategoryTheory Limits
variable {C : Type*} [Category C]

/-- The constant augmented simplicial object has an extra degeneracy. -/
@[simps]
def const (X : C) : ExtraDegeneracy (Augmented.const.obj X) where
  s' := ùüô _
  s _ := ùüô _

/-- If `C` is a preadditive category and `X` is an augmented simplicial object
in `C` that has an extra degeneracy, then the augmentation on the alternating
face map complex of `X` is a homotopy equivalence. -/
noncomputable def homotopyEquiv [Preadditive C] [HasZeroObject C]
    {X : SimplicialObject.Augmented C} (ed : ExtraDegeneracy X) :
    HomotopyEquiv (AlgebraicTopology.AlternatingFaceMapComplex.obj (drop.obj X))
      ((ChainComplex.single‚ÇÄ C).obj (point.obj X)) where
  hom := AlternatingFaceMapComplex.Œµ.app X
  inv := (ChainComplex.fromSingle‚ÇÄEquiv _ _).symm (by exact ed.s')
  homotopyInvHomId := Homotopy.ofEq (by
    ext
    dsimp
    erw [AlternatingFaceMapComplex.Œµ_app_f_zero,
      ChainComplex.fromSingle‚ÇÄEquiv_symm_apply_f_zero, s'_comp_Œµ]
    rfl)
  homotopyHomInvId :=
    { hom i := Pi.single (i + 1) (-ed.s i)
      zero i j hij := Pi.single_eq_of_ne (Ne.symm hij) _
      comm i := by
        cases i with
        | zero =>
          rw [Homotopy.prevD_chainComplex, Homotopy.dNext_zero_chainComplex, zero_add]
          dsimp
          erw [ChainComplex.fromSingle‚ÇÄEquiv_symm_apply_f_zero]
          simp only [ComplexShape.down_Rel, AlternatingFaceMapComplex.obj_d_eq]
          rw [Fin.sum_univ_two]
          simp [s_comp_Œ¥‚ÇÄ, s‚ÇÄ_comp_Œ¥‚ÇÅ]
        | succ i =>
          rw [Homotopy.prevD_chainComplex, Homotopy.dNext_succ_chainComplex]
          simp [Fin.sum_univ_succ (n := i + 2), s_comp_Œ¥‚ÇÄ, Preadditive.sum_comp,
            Preadditive.zsmul_comp, Preadditive.comp_sum, Preadditive.comp_zsmul,
            s_comp_Œ¥, pow_succ] }

end ExtraDegeneracy

end Augmented

end SimplicialObject

end CategoryTheory
