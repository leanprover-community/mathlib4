/-
Copyright (c) 2025 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Basic
import Mathlib.CategoryTheory.Limits.Final

/-! # Properties of the truncated simplex category

We prove that for `n > 0`, the inclusion functor from the `n`-truncated simplex category to the
untruncated simplex category, and the inclusion functor from the `n`-truncated to the `m`-truncated
simplex category, for `n â‰¤ m` are initial.
-/

open Simplicial CategoryTheory

namespace SimplexCategory.Truncated

/-- For `0 < n`, the inclusion functor from the `n`-truncated simplex category to the untruncated
simplex category is initial. -/
instance initial_inclusion {n : â„•} [NeZero n] : (inclusion n).Initial := by
  have := Nat.pos_of_neZero n
  constructor
  intro Î”
  have : Nonempty (CostructuredArrow (inclusion n) Î”) := âŸ¨âŸ¨â¦‹0â¦Œâ‚™, âŸ¨âŸ¨âŸ©âŸ©, â¦‹0â¦Œ.const _ 0 âŸ©âŸ©
  apply zigzag_isConnected
  rintro âŸ¨âŸ¨Î”â‚, hÎ”â‚âŸ©, âŸ¨âŸ¨âŸ©âŸ©, fâŸ© âŸ¨âŸ¨Î”â‚‚, hÎ”â‚‚âŸ©, âŸ¨âŸ¨âŸ©âŸ©, f'âŸ©
  apply Zigzag.trans (jâ‚‚ := âŸ¨â¦‹0â¦Œâ‚™, âŸ¨âŸ¨âŸ©âŸ©, â¦‹0â¦Œ.const _ (f 0)âŸ©)
    (.of_inv <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 0)
  by_cases hff' : f 0 â‰¤ f' 0
  Â· trans âŸ¨â¦‹1â¦Œâ‚™, âŸ¨âŸ¨âŸ©âŸ©, mkOfLe (n := Î”.len) (f 0) (f' 0) hff'âŸ©
    Â· apply Zigzag.of_hom <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 0
    Â· trans âŸ¨â¦‹0â¦Œâ‚™, âŸ¨âŸ¨âŸ©âŸ©, â¦‹0â¦Œ.const _ (f' 0)âŸ©
      Â· apply Zigzag.of_inv <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 1
      Â· apply Zigzag.of_hom <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 0
  Â· trans âŸ¨â¦‹1â¦Œâ‚™, âŸ¨âŸ¨âŸ©âŸ©, mkOfLe (n := Î”.len) (f' 0) (f 0) (le_of_not_ge hff')âŸ©
    Â· apply Zigzag.of_hom <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 1
    Â· trans âŸ¨â¦‹0â¦Œâ‚™, âŸ¨âŸ¨âŸ©âŸ©, â¦‹0â¦Œ.const _ (f' 0)âŸ©
      Â· apply Zigzag.of_inv <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 0
      Â· apply Zigzag.of_hom <| CostructuredArrow.homMk <| Hom.tr <| â¦‹0â¦Œ.const _ 0

/-- For `0 < n â‰¤ m`, the inclusion functor from the `n`-truncated simplex category to the
`m`-truncated simplex category is initial. -/
theorem initial_incl {n m : â„•} [NeZero n] (hm : n â‰¤ m) : (incl n m).Initial := by
  have : (incl n m hm â‹™ inclusion m).Initial :=
    Functor.initial_of_natIso (inclCompInclusion _).symm
  apply Functor.initial_of_comp_full_faithful _ (inclusion m)

/-- Abbreviation for face maps in the `n`-truncated simplex category. -/
abbrev Î´ (m : Nat) {n} (i : Fin (n + 2)) (hn := by decide) (hn' := by decide) :
  (âŸ¨â¦‹nâ¦Œ, hnâŸ© : SimplexCategory.Truncated m) âŸ¶ âŸ¨â¦‹n + 1â¦Œ, hn'âŸ© := Hom.tr (SimplexCategory.Î´ i)

/-- Abbreviation for degeneracy maps in the `n`-truncated simplex category. -/
abbrev Ïƒ (m : Nat) {n} (i : Fin (n + 1)) (hn := by decide) (hn' := by decide) :
    (âŸ¨â¦‹n + 1â¦Œ, hnâŸ© : SimplexCategory.Truncated m) âŸ¶ âŸ¨â¦‹nâ¦Œ, hn'âŸ© := Hom.tr (SimplexCategory.Ïƒ i)

section Two

/-- Abbreviation for face maps in the 2-truncated simplex category. -/
abbrev Î´â‚‚ {n} (i : Fin (n + 2)) (hn := by decide) (hn' := by decide) := Î´ 2 i hn hn'

/-- Abbreviation for face maps in the 2-truncated simplex category. -/
abbrev Ïƒâ‚‚ {n} (i : Fin (n + 1)) (hn := by decide) (hn' := by decide) := Ïƒ 2 i hn hn'

@[reassoc (attr := simp)]
lemma Î´â‚‚_zero_comp_Ïƒâ‚‚_zero {n} (hn := by decide) (hn' := by decide) :
    Î´â‚‚ (n := n) 0 hn hn' â‰« Ïƒâ‚‚ 0 hn' hn = ðŸ™ _ :=
  ObjectProperty.hom_ext _ (SimplexCategory.Î´_comp_Ïƒ_self)

@[reassoc]
lemma Î´â‚‚_zero_comp_Ïƒâ‚‚_one : Î´â‚‚ (0 : Fin 3) â‰« Ïƒâ‚‚ 1 = Ïƒâ‚‚ 0 â‰« Î´â‚‚ 0 :=
  ObjectProperty.hom_ext _ (SimplexCategory.Î´_comp_Ïƒ_of_le (i := 0) (j := 0) (Fin.zero_le _))

@[reassoc (attr := simp)]
lemma Î´â‚‚_one_comp_Ïƒâ‚‚_zero {n} (hn := by decide) (hn' := by decide) :
    Î´â‚‚ (n := n) 1 hn hn' â‰« Ïƒâ‚‚ 0 hn' hn = ðŸ™ _ :=
  ObjectProperty.hom_ext _ (SimplexCategory.Î´_comp_Ïƒ_succ)

@[reassoc (attr := simp)]
lemma Î´â‚‚_two_comp_Ïƒâ‚‚_one : Î´â‚‚ (2 : Fin 3) â‰« Ïƒâ‚‚ 1 = ðŸ™ _ :=
  ObjectProperty.hom_ext _ (SimplexCategory.Î´_comp_Ïƒ_succ' (by decide))

@[reassoc]
lemma Î´â‚‚_two_comp_Ïƒâ‚‚_zero : Î´â‚‚ (2 : Fin 3) â‰« Ïƒâ‚‚ 0 = Ïƒâ‚‚ 0 â‰« Î´â‚‚ 1 :=
  ObjectProperty.hom_ext _ (SimplexCategory.Î´_comp_Ïƒ_of_gt' (by decide))

end Two

end SimplexCategory.Truncated
