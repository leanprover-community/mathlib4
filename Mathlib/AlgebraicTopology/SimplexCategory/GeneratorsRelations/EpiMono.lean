/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.GeneratorsRelations.Basic
/-! # Epi-mono factorisation in the simplex category presented by generators and relations

This file aims to establish that there is a nice epi-mono factorisation in `SimplexCategoryGenRel`.
More precisely, we introduce two (inductively-defined) morphism property `P_Î´` and `P_Ïƒ` that
single out morphisms that are compositions of `Î´ i` (resp. `Ïƒ i`).

We only define these, and prove various lemmas to help reasoning about them.

## TODOs
 - Prove that every morphism factors as a `P_Ïƒ` followed by a `P_Î´`.

-/

namespace SimplexCategoryGenRel
open CategoryTheory

section EpiMono

/-- `Î´ i` is a split monomorphism thanks to the simplicial identities. -/
def SplitMonoÎ´ {n : â„•} {i : Fin (n + 2)} : SplitMono (Î´ i) where
  retraction := by
    induction i using Fin.lastCases with
    | last => exact Ïƒ n
    | cast i => exact Ïƒ i
  id := by
    cases i using Fin.lastCases
    Â· simp only [Fin.natCast_eq_last, Fin.lastCases_last]
      exact Î´_comp_Ïƒ_succ
    Â· simp only [Fin.natCast_eq_last, Fin.lastCases_castSucc]
      exact Î´_comp_Ïƒ_self

instance {n : â„•} {i : Fin (n + 2)} : IsSplitMono (Î´ i) := .mk' SplitMonoÎ´

/-- `Î´ i` is a split epimorphism thanks to the simplicial identities. -/
def SplitEpiÏƒ {n : â„•} {i : Fin (n + 1)} : SplitEpi (Ïƒ i) where
  section_ := Î´ i.castSucc
  id := Î´_comp_Ïƒ_self

instance {n : â„•} {i : Fin (n + 1)} : IsSplitEpi (Ïƒ i) := .mk' SplitEpiÏƒ

/-- Auxiliary predicate to express that a morphism is purely a composition of `Ïƒ i`s. -/
inductive P_Ïƒ : MorphismProperty SimplexCategoryGenRel
  | Ïƒ {n : â„•} (i : Fin (n + 1)) : P_Ïƒ <| Ïƒ i
  | id {n : â„•} : P_Ïƒ <| ðŸ™ (.mk n)
  | comp {n : â„•} (i : Fin (n + 1)) {a : SimplexCategoryGenRel} (g : a âŸ¶ .mk (n + 1))
    (hg: P_Ïƒ g) : P_Ïƒ <| g â‰« Ïƒ i

/-- A version of `P_Ïƒ` where composition is taken on the right instead. It is equivalent to `P_Ïƒ`
(see `P_Ïƒ_eq_P_Ïƒ'`). -/
inductive P_Ïƒ' : MorphismProperty SimplexCategoryGenRel
  | Ïƒ {n : â„•} (i : Fin (n + 1)) : P_Ïƒ' <| Ïƒ i
  | id {n : â„•} : P_Ïƒ' <| ðŸ™ (.mk n)
  | comp {n : â„•} (i : Fin (n + 1)) {a : SimplexCategoryGenRel} (g :.mk n âŸ¶ a)
    (hg: P_Ïƒ' g) : P_Ïƒ' <| Ïƒ i â‰« g

/-- Auxiliary predicate to express that a morphism is purely a composition of `Î´ i`s. -/
inductive P_Î´ : MorphismProperty SimplexCategoryGenRel
  | Î´ {n : â„•} (i : Fin (n + 2)) : P_Î´ <| Î´ i
  | id {n : â„•} : P_Î´ <| ðŸ™ (.mk n)
  | comp {n : â„•} (i : Fin (n + 2)) {a : SimplexCategoryGenRel} (g : a âŸ¶ .mk n )
    (hg: P_Î´ g) : P_Î´ <| g â‰« Î´ i

/-- A version of `P_Î´` where composition is taken on the right instead. It is equivalent to `P_Î´`
(see `P_Ïƒ_eq_P_Î´'`). -/
inductive P_Î´' : MorphismProperty SimplexCategoryGenRel
  | Î´ {n : â„•} (i : Fin (n + 2)) : P_Î´' <| Î´ i
  | id {n : â„•} : P_Î´' <| ðŸ™ (.mk n)
  | comp {a : SimplexCategoryGenRel} {n : â„•} (i : Fin (n + 2)) (g : .mk (n + 1) âŸ¶ a)
    (hg: P_Î´' g) : P_Î´' <| Î´ i â‰« g

lemma P_Ïƒ_eqToHom {x y : SimplexCategoryGenRel} (h : x = y) : P_Ïƒ <| eqToHom h := by
  subst h
  rw [eqToHom_refl]
  exact P_Ïƒ.id

lemma P_Î´_eqToHom {x y : SimplexCategoryGenRel} (h : x = y) : P_Î´ <| eqToHom h := by
  subst h
  rw [eqToHom_refl]
  exact P_Î´.id

lemma P_Î´_comp {x y z : SimplexCategoryGenRel} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    P_Î´ f â†’ P_Î´ g â†’ P_Î´ (f â‰« g) := by
  intro hf hg
  induction hg with
  | Î´ i => exact P_Î´.comp _ f hf
  | id => rwa [Category.comp_id]
  | comp i b _ h => specialize h f hf
                    rw [â† Category.assoc]
                    exact P_Î´.comp i (f â‰« b) h

lemma P_Ïƒ_comp {x y z : SimplexCategoryGenRel} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    P_Ïƒ f â†’ P_Ïƒ g â†’ P_Ïƒ (f â‰« g) := by
  intro hf hg
  induction hg with
  | Ïƒ i => exact P_Ïƒ.comp _ f hf
  | id => rwa [Category.comp_id]
  | comp i b _ h => specialize h f hf
                    rw [â† Category.assoc]
                    exact P_Ïƒ.comp i (f â‰« b) h

lemma P_Ïƒ'_comp {x y z : SimplexCategoryGenRel} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    P_Ïƒ' f â†’ P_Ïƒ' g â†’ P_Ïƒ' (f â‰« g) := by
  intro hf hg
  induction hf with
  | Ïƒ i => exact P_Ïƒ'.comp _ g hg
  | id => rwa [Category.id_comp]
  | comp i b _ h => specialize h g hg
                    rw [Category.assoc]
                    exact P_Ïƒ'.comp i (b â‰« g) h

lemma P_Î´'_comp {x y z : SimplexCategoryGenRel} (f : x âŸ¶ y) (g : y âŸ¶ z) :
    P_Î´' f â†’ P_Î´' g â†’ P_Î´' (f â‰« g) := by
  intro hf hg
  induction hf with
  | Î´ i => exact P_Î´'.comp _ g hg
  | id => rwa [Category.id_comp]
  | comp i b _ h => specialize h g hg
                    rw [Category.assoc]
                    exact P_Î´'.comp i (b â‰« g) h

/-- The property `P_Ïƒ` is equivalent to `P_Ïƒ'`. -/
lemma P_Ïƒ_eq_P_Ïƒ' : P_Ïƒ = P_Ïƒ' := by
  apply le_antisymm <;> intro x y f h
  Â· induction h with
    | Ïƒ i => exact P_Ïƒ'.Ïƒ i
    | id => exact P_Ïƒ'.id
    | comp i f h h' => exact P_Ïƒ'_comp _ _ h' (P_Ïƒ'.Ïƒ _)
  Â· induction h with
    | Ïƒ i => exact P_Ïƒ.Ïƒ i
    | id => exact P_Ïƒ.id
    | comp i f h h' => exact P_Ïƒ_comp _ _ (P_Ïƒ.Ïƒ _) h'

/-- The property `P_Î´` is equivalent to `P_Î´'`. -/
lemma P_Î´_eq_P_Î´' : P_Î´ = P_Î´' := by
  apply le_antisymm <;> intro x y f h
  Â· induction h with
    | Î´ i => exact P_Î´'.Î´ i
    | id => exact P_Î´'.id
    | comp i f h h' => exact P_Î´'_comp _ _ h' (P_Î´'.Î´ _)
  Â· induction h with
    | Î´ i => exact P_Î´.Î´ i
    | id => exact P_Î´.id
    | comp i f h h' => exact P_Î´_comp _ _ (P_Î´.Î´ _) h'

/-- All `P_Ïƒ` are split epis as composition of such. -/
lemma isSplitEpi_P_Ïƒ {x y : SimplexCategoryGenRel} {e : x âŸ¶ y} (he : P_Ïƒ e) : IsSplitEpi e := by
  induction he <;> infer_instance

/-- All `P_Î´` are split monos as composition of such. -/
lemma isSplitMono_P_Î´ {x y : SimplexCategoryGenRel} {m : x âŸ¶ y} (hm : P_Î´ m) :
    IsSplitMono m := by
  induction hm <;> infer_instance

lemma isSplitEpi_P_Ïƒ_toSimplexCategory {x y : SimplexCategoryGenRel} {e : x âŸ¶ y} (he : P_Ïƒ e)
    : IsSplitEpi <| toSimplexCategory.map e := by
  constructor
  constructor
  apply SplitEpi.map
  exact isSplitEpi_P_Ïƒ he |>.exists_splitEpi.some

lemma isSplitMono_P_Î´_toSimplexCategory {x y : SimplexCategoryGenRel} {m : x âŸ¶ y} (hm : P_Î´ m)
    : IsSplitMono <| toSimplexCategory.map m := by
  constructor
  constructor
  apply SplitMono.map
  exact isSplitMono_P_Î´ hm |>.exists_splitMono.some

lemma eq_or_len_le_of_P_Î´ {x y : SimplexCategoryGenRel} {f : x âŸ¶ y} (h_Î´ : P_Î´ f) :
    (âˆƒ h : x = y, f = eqToHom h) âˆ¨ x.len < y.len := by
  induction h_Î´ with
  | Î´ i => right; simp
  | id => left; use rfl; simp
  | comp i u _ h' =>
    rcases h' with âŸ¨e, _âŸ© | h'
    Â· right; rw [e]; exact Nat.lt_add_one _
    Â· right; exact Nat.lt_succ_of_lt h'

end EpiMono

end SimplexCategoryGenRel
