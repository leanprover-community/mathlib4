/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.GeneratorsRelations.Basic
/-! # Epi-mono factorization in the simplex category presented by generators and relations

This file aims to establish that there is a nice epi-mono factorization in `SimplexCategoryGenRel`.
More precisely, we introduce two morphism properties `P_Î´` and `P_Ïƒ` that
single out morphisms that are compositions of `Î´ i` (resp. `Ïƒ i`).

The main result of this file is `exists_P_Ïƒ_P_Î´_factorization`, which asserts that every
moprhism as a decomposition of a `P_Ïƒ` followed by a `P_Î´`.

-/

namespace SimplexCategoryGenRel
open CategoryTheory

section EpiMono

/-- `Î´ i` is a split monomorphism thanks to the simplicial identities. -/
def splitMonoÎ´ {n : â„•} (i : Fin (n + 2)) : SplitMono (Î´ i) where
  retraction := by
    induction i using Fin.lastCases with
    | last => exact Ïƒ n
    | cast i => exact Ïƒ i
  id := by
    cases i using Fin.lastCases
    Â· simp only [Fin.natCast_eq_last, Fin.lastCases_last]
      exact Î´_comp_Ïƒ_succ
    Â· simp only [Fin.natCast_eq_last, Fin.lastCases_castSucc]
      exact Î´_comp_Ïƒ_self

instance {n : â„•} {i : Fin (n + 2)} : IsSplitMono (Î´ i) := .mk' <| splitMonoÎ´ i

/-- `Î´ i` is a split epimorphism thanks to the simplicial identities. -/
def splitEpiÏƒ {n : â„•} (i : Fin (n + 1)) : SplitEpi (Ïƒ i) where
  section_ := Î´ i.castSucc
  id := Î´_comp_Ïƒ_self

instance {n : â„•} {i : Fin (n + 1)} : IsSplitEpi (Ïƒ i) := .mk' <| splitEpiÏƒ i

/-- Auxiliary predicate to express that a morphism is purely a composition of `Ïƒ i`s. -/
abbrev P_Ïƒ := degeneracies.multiplicativeClosure

/-- Auxiliary predicate to express that a morphism is purely a composition of `Î´ i`s. -/
abbrev P_Î´ := faces.multiplicativeClosure

lemma P_Ïƒ.Ïƒ {n : â„•} (i : Fin (n + 1)) : P_Ïƒ (Ïƒ i) := .of _ (.Ïƒ i)

lemma P_Î´.Î´ {n : â„•} (i : Fin (n + 2)) : P_Î´ (Î´ i) := .of _ (.Î´ i)

/-- All `P_Ïƒ` are split epis as composition of such. -/
lemma isSplitEpi_P_Ïƒ {x y : SimplexCategoryGenRel} {e : x âŸ¶ y} (he : P_Ïƒ e) : IsSplitEpi e := by
  induction he with
  | of x hx => cases hx; infer_instance
  | id => infer_instance
  | comp_of _ _ _ h => cases h; infer_instance

/-- All `P_Î´` are split monos as composition of such. -/
lemma isSplitMono_P_Î´ {x y : SimplexCategoryGenRel} {m : x âŸ¶ y} (hm : P_Î´ m) :
    IsSplitMono m := by
  induction hm with
  | of x hx => cases hx; infer_instance
  | id => infer_instance
  | comp_of _ _ _ h => cases h; infer_instance

lemma isSplitEpi_toSimplexCategory_map_of_P_Ïƒ {x y : SimplexCategoryGenRel} {e : x âŸ¶ y}
    (he : P_Ïƒ e) : IsSplitEpi <| toSimplexCategory.map e := by
  constructor
  constructor
  apply SplitEpi.map
  exact isSplitEpi_P_Ïƒ he |>.exists_splitEpi.some

lemma isSplitMono_toSimplexCategory_map_of_P_Î´ {x y : SimplexCategoryGenRel} {m : x âŸ¶ y}
    (hm : P_Î´ m) : IsSplitMono <| toSimplexCategory.map m := by
  constructor
  constructor
  apply SplitMono.map
  exact isSplitMono_P_Î´ hm |>.exists_splitMono.some

lemma eq_or_len_le_of_P_Î´ {x y : SimplexCategoryGenRel} {f : x âŸ¶ y} (h_Î´ : P_Î´ f) :
    (âˆƒ h : x = y, f = eqToHom h) âˆ¨ x.len < y.len := by
  induction h_Î´ with
  | of _ hx => cases hx; right; simp
  | id => left; use rfl; simp
  | comp_of i u _ hg h' =>
    rcases h' with âŸ¨e, _âŸ© | h' <;>
    apply Or.inr <;>
    cases hg
    Â· rw [e]
      exact Nat.lt_add_one _
    Â· exact Nat.lt_succ_of_lt h'

end EpiMono

section ExistenceOfFactorizations

/-- An auxiliary lemma to show that one can always use the simplicial identities to simplify a term
in the form `Î´ â‰« Ïƒ` into either an identity, or a term of the form `Ïƒ â‰« Î´`. This is the crucial
special case to induct on to get an epi-mono factorization for all morphisms. -/
private lemma switch_Î´_Ïƒ {n : â„•} (i : Fin (n + 2)) (i' : Fin (n + 3)) :
    Î´ i' â‰« Ïƒ i = ğŸ™ _ âˆ¨ âˆƒ j j', Î´ i' â‰« Ïƒ i = Ïƒ j â‰« Î´ j' := by
  obtain h'' | h'' | h'' : i'= i.castSucc âˆ¨ i' < i.castSucc âˆ¨ i.castSucc < i' := by
      simp only [lt_or_lt_iff_ne, ne_eq]
      tauto
  Â· subst h''
    rw [Î´_comp_Ïƒ_self]
    simp
  Â· obtain âŸ¨hâ‚, hâ‚‚âŸ© : i' â‰  Fin.last _ âˆ§ i â‰  0 := by
      constructor
      Â· exact Fin.ne_last_of_lt h''
      Â· rw [Fin.lt_def, Fin.coe_castSucc] at h''
        apply Fin.ne_of_val_ne
        exact Nat.not_eq_zero_of_lt h''
    rw [â† i'.castSucc_castPred hâ‚, â† i.succ_pred hâ‚‚]
    have H : i'.castPred hâ‚ â‰¤ (i.pred hâ‚‚).castSucc := by
      simp only [Fin.le_def, Fin.coe_castPred, Fin.coe_castSucc, Fin.coe_pred]
      rw [Fin.lt_def, Nat.lt_iff_add_one_le] at h''
      exact Nat.le_sub_one_of_lt h''
    rw [Î´_comp_Ïƒ_of_le H]
    right
    use i.pred hâ‚‚, i'.castPred hâ‚
  Â· by_cases h : i.succ = i'
    Â· subst h
      rw [Î´_comp_Ïƒ_succ]
      simp
    Â· obtain âŸ¨hâ‚, hâ‚‚âŸ© : i â‰  Fin.last _ âˆ§ i' â‰  0 := by
        constructor
        Â· by_cases h' : i' = Fin.last _
          Â· simp_all
          Â· rw [â† Fin.val_eq_val] at h' h
            apply Fin.ne_of_val_ne
            rw [Fin.lt_def, Fin.coe_castSucc] at h''
            rcases i with âŸ¨i, hiâŸ©; rcases i' with âŸ¨i', hi'âŸ©
            intro hyp; subst hyp
            rw [Nat.lt_iff_add_one_le] at h'' hi'
            simp_all only [add_le_add_iff_right, Fin.val_last, Fin.succ_mk]
            rw [â† one_add_one_eq_two] at hi'
            exact h (Nat.le_antisymm hi' h'').symm
        Â· exact Fin.ne_zero_of_lt h''
      rw [â† i'.succ_pred hâ‚‚, â† i.castSucc_castPred hâ‚]
      have H : (i.castPred hâ‚).castSucc < i'.pred hâ‚‚ := by
        rcases (Nat.le_iff_lt_or_eq.mp h'') with h' | h'
        Â· simp only [Fin.lt_def, Fin.coe_castSucc, Nat.succ_eq_add_one, Fin.castSucc_castPred,
            Fin.coe_pred] at *
          exact Nat.lt_sub_of_add_lt h'
        Â· exfalso
          exact Fin.val_ne_of_ne h h'
      rw [Î´_comp_Ïƒ_of_gt H]
      right
      use i.castPred hâ‚, i'.pred hâ‚‚

/-- A low-dimensional special case of the previous -/
private lemma switch_Î´_Ïƒâ‚€ (i : Fin 1) (i' : Fin 2) :
    Î´ i' â‰« Ïƒ i = ğŸ™ _ := by
  fin_cases i; fin_cases i'
  Â· exact Î´_comp_Ïƒ_self
  Â· exact Î´_comp_Ïƒ_succ

private lemma factor_Î´_Ïƒ {n : â„•} (i : Fin (n + 1)) (i' : Fin (n + 2)) :
    âˆƒ (z : SimplexCategoryGenRel) (e : mk n âŸ¶ z) (m : z âŸ¶ mk n)
      (_ : P_Ïƒ e) (_ : P_Î´ m), Î´ i' â‰« Ïƒ i = e â‰« m := by
  cases n with
  | zero =>
    rw [switch_Î´_Ïƒâ‚€]
    use mk 0, ğŸ™ _, ğŸ™ _, P_Ïƒ.id_mem _, P_Î´.id_mem _
    simp
  | succ n =>
    obtain h | âŸ¨j, j', hâŸ© := switch_Î´_Ïƒ i i' <;> rw [h]
    Â· use mk (n + 1), ğŸ™ _, ğŸ™ _, P_Ïƒ.id_mem _, P_Î´.id_mem _
      simp
    Â· use mk n, Ïƒ j, Î´ j', P_Ïƒ.Ïƒ _, P_Î´.Î´ _

/-- An auxiliary lemma that shows there exists a factorization as a P_Î´ followed by a P_Ïƒ for
morphisms of the form `P_Î´ â‰« Ïƒ`. -/
private lemma factor_P_Î´_Ïƒ {n : â„•} (i : Fin (n + 1)) {x : SimplexCategoryGenRel}
    (f : x âŸ¶ mk (n + 1)) (hf : P_Î´ f) : âˆƒ (z : SimplexCategoryGenRel) (e : x âŸ¶ z) (m : z âŸ¶ mk n)
      (_ : P_Ïƒ e) (_ : P_Î´ m), f â‰« Ïƒ i = e â‰« m := by
  induction n using Nat.case_strong_induction_on generalizing x with
  | hz => cases hf with
    | of _ h => cases h; exact factor_Î´_Ïƒ _ _
    | id  =>
      rw [Category.id_comp]
      use mk 0, Ïƒ i, ğŸ™ _, P_Ïƒ.Ïƒ _, P_Î´.id_mem _
      simp
    | comp_of j f hf hg =>
      cases hg
      obtain âŸ¨h', hf'âŸ© | hf' := eq_or_len_le_of_P_Î´ hf
      Â· subst h'
        simp only [eqToHom_refl] at hf'
        subst hf'
        rw [Category.id_comp]
        exact factor_Î´_Ïƒ _ _
      Â· simp at hf'
  | hi n h_rec =>
    cases hf with
    | of _ h => cases h; exact factor_Î´_Ïƒ _ _
    | @id n =>
      rw [Category.id_comp]
      use mk (n + 1), Ïƒ i, ğŸ™ _, P_Ïƒ.Ïƒ _, P_Î´.id_mem _
      simp
    | comp_of f g hf hg =>
      obtain âŸ¨h', h''âŸ© | h := eq_or_len_le_of_P_Î´ hf
      Â· subst h'
        cases hg
        rw [eqToHom_refl] at h''; subst h''
        rw [Category.id_comp]
        exact factor_Î´_Ïƒ _ _
      Â· have hg' := hg
        rcases hg' with âŸ¨i'âŸ©
        obtain h' | âŸ¨j, j', h'âŸ© := switch_Î´_Ïƒ i i' <;> rw [Category.assoc, h']
        Â· rw [Category.comp_id]
          use x, ğŸ™ x, f, P_Ïƒ.id_mem _, hf
          simp
        Â· rw [mk_len, Nat.lt_add_one_iff] at h
          obtain âŸ¨z, e, mâ‚, he, hmâ‚, hâŸ© := h_rec n (Nat.le_refl _) j f hf
          rw [reassoc_of% h]
          use z, e, mâ‚ â‰« Î´ j', he, P_Î´.comp_mem _ _ hmâ‚ (P_Î´.Î´ _)

/-- Any morphism in `SimplexCategoryGenRel` can be decomposed as a `P_Ïƒ` followed by a `P_Î´`. -/
theorem exists_P_Ïƒ_P_Î´_factorization {x y : SimplexCategoryGenRel} (f : x âŸ¶ y) :
    âˆƒ (z : SimplexCategoryGenRel) (e : x âŸ¶ z) (m : z âŸ¶ y)
        (_ : P_Ïƒ e) (_ : P_Î´ m), f = e â‰« m := by
  induction f with
  | @id n => use (mk n), (ğŸ™ (mk n)), (ğŸ™ (mk n)), P_Ïƒ.id_mem _, P_Î´.id_mem _; simp
  | @comp_Î´ n n' f j h =>
    obtain âŸ¨z, e, m, âŸ¨he, hm, hâŸ©âŸ© := h
    rw [h, Category.assoc]
    use z, e, m â‰« Î´ j, he, P_Î´.comp_mem _ _ hm (P_Î´.Î´ _)
  | @comp_Ïƒ n n' f j h =>
    obtain âŸ¨z, e, m, âŸ¨he, hm, hâŸ©âŸ© := h
    rw [h]
    cases hm with
    | of g hg =>
      cases hg
      rw [Category.assoc]
      obtain âŸ¨zâ‚, eâ‚, mâ‚, âŸ¨heâ‚, hmâ‚, hâ‚âŸ©âŸ© := factor_Î´_Ïƒ j _
      rw [hâ‚]
      use zâ‚, e â‰« eâ‚, mâ‚, P_Ïƒ.comp_mem _ _ he heâ‚, hmâ‚
      simp
    | @id n =>
      simp only [Category.comp_id]
      use mk n', e â‰« Ïƒ j, ğŸ™ _, P_Ïƒ.comp_mem _ _ he (P_Ïƒ.Ïƒ _), P_Î´.id_mem _
      simp
    | comp_of f g hf hg =>
      rw [Category.assoc, Category.assoc]
      cases n' with
      | zero =>
        cases hg
        rw [switch_Î´_Ïƒâ‚€, Category.comp_id]
        use z, e, f, he, hf
      | succ n =>
        have hg' := hg
        rcases hg' with âŸ¨iâŸ©
        obtain h' | âŸ¨j', j'', h'âŸ© := switch_Î´_Ïƒ j i <;> rw [h']
        Â· rw [Category.comp_id]
          use z, e, f, he, hf
        Â· obtain âŸ¨zâ‚, eâ‚, mâ‚, âŸ¨heâ‚, hmâ‚, hâ‚âŸ©âŸ© := factor_P_Î´_Ïƒ j' f hf
          rw [reassoc_of% hâ‚]
          use zâ‚, e â‰« eâ‚, mâ‚ â‰« Î´ j'', P_Ïƒ.comp_mem _ _ he heâ‚, P_Î´.comp_mem _ _ hmâ‚ (P_Î´.Î´ _)
          simp

instance : MorphismProperty.HasFactorization P_Ïƒ P_Î´ where
  nonempty_mapFactorizationData f := by
    obtain âŸ¨z, e , m, he, hm, facâŸ© := exists_P_Ïƒ_P_Î´_factorization f
    exact âŸ¨âŸ¨z, e , m, fac.symm, he, hmâŸ©âŸ©

end ExistenceOfFactorizations

end SimplexCategoryGenRel
