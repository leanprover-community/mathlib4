/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.GeneratorsRelations.EpiMono
/-! # Normal forms for morphisms in `SimplexCategoryGenRel`.

In this file, we establish that `P_Œ¥` and `P_œÉ` morphisms in `SimplexCategoryGenRel`
each admits a normal form.

In both cases, the normal forms are encoded as an integer `m`, and a strictly increasing
list of integers `[i‚ÇÄ,‚Ä¶,i‚Çô]` such that `i‚Çñ ‚â§ m + k` for all `k`. We define a predicate
`isAdmissible m : List ‚Ñï ‚Üí Prop` encoding this property. And provide some lemmas to help
work with such lists.

Normal forms for `P_œÉ` morphisms are encoded by `m`-admissible lists, in which case the list
`[i‚ÇÄ,‚Ä¶,i‚Çô]` represents the morphism `œÉ i‚Çô ‚â´ ‚ãØ ‚â´ œÉ i‚ÇÄ : .mk (m + n) ‚ü∂ .mk n`.

Normal forms for `P_Œ¥` morphisms are encoded by `(m + 1)`-admissible lists, in which case the list
`[i‚ÇÄ,‚Ä¶,i‚Çô]` represents the morphism `Œ¥ i‚ÇÄ ‚â´ ‚ãØ ‚â´ Œ¥ i‚Çô : .mk n ‚ü∂ .mk (m + n)`.

The results in this file are to be treated as implementation-only, and they only serve as stepping
stones towards proving that the canonical functor
`toSimplexCategory : SimplexCategoryGenRel ‚•§ SimplexCategory` is an equivalence.

## References:
* [Kerodon Tag 04FQ](https://kerodon.net/tag/04FQ)
* [Kerodon Tag 04FT](https://kerodon.net/tag/04FT)

-/

namespace SimplexCategoryGenRel

open CategoryTheory

section AdmissibleLists
-- Impl. note: We are not bundling admissible lists as a subtype of `List ‚Ñï` so that it remains
-- easier to perform inductive constructions and proofs on such lists, and we instead bundle
-- propositions asserting that various List constructions produce admissible lists.

variable (m : ‚Ñï)
/-- A list of natural numbers `[i‚ÇÄ, ‚ãØ, i‚Çô]` is said to be `m`-admissible (for `m : ‚Ñï`) if
`i‚ÇÄ < ‚ãØ < i‚Çô` and `i‚Çñ ‚â§ m + k` for all `k`. This would suggest the definition
`L.IsChain (¬∑ < ¬∑) ‚àß ‚àÄ k, (h : k < L.length) ‚Üí L[k] ‚â§ m + k`.
However, we instead define `IsAdmissible` inductively and show, in
`isAdmissible_iff_isChain_and_le`, that this is equivalent to the non-inductive definition.
-/
@[mk_iff]
inductive IsAdmissible : (m : ‚Ñï) ‚Üí (L : List ‚Ñï) ‚Üí Prop
  | nil (m : ‚Ñï) : IsAdmissible m []
  | singleton {m a} (ha : a ‚â§ m) : IsAdmissible m [a]
  | cons_cons {m a b L'} (hab : a < b) (hbL : IsAdmissible (m + 1) (b :: L'))
      (ha : a ‚â§ m) : IsAdmissible m (a :: b :: L')

attribute [simp, grind ‚Üê] IsAdmissible.nil
attribute [grind ‚Üí] IsAdmissible.singleton
attribute [grind ‚Üí] IsAdmissible.cons_cons

section IsAdmissible

variable {m a b : ‚Ñï} {L : List ‚Ñï}

@[simp, grind =]
theorem isAdmissible_singleton_iff : IsAdmissible m [a] ‚Üî a ‚â§ m :=
  ‚ü®fun | .singleton h => h, .singleton‚ü©

@[simp, grind =]
theorem isAdmissible_cons_cons_iff : IsAdmissible m (a :: b :: L) ‚Üî
    a < b ‚àß IsAdmissible (m + 1) (b :: L) ‚àß a ‚â§ m :=
  ‚ü®fun | .cons_cons hab hbL ha => ‚ü®hab, hbL, ha‚ü©, by grind‚ü©

theorem isAdmissible_cons_iff : IsAdmissible m (a :: L) ‚Üî
    a ‚â§ m ‚àß ((_ : 0 < L.length) ‚Üí a < L[0]) ‚àß IsAdmissible (m + 1) L := by
  cases L <;> grind

theorem isAdmissible_iff_isChain_and_le : IsAdmissible m L ‚Üî
    L.IsChain (¬∑ < ¬∑) ‚àß ‚àÄ k, (h : k < L.length) ‚Üí L[k] ‚â§ m + k := by
  induction L using List.twoStepInduction generalizing m with
  | nil => grind
  | singleton _ => simp
  | cons_cons _ _ _ _ IH =>
    simp_rw [isAdmissible_cons_cons_iff, IH, List.length_cons, and_assoc,
      List.isChain_cons_cons, and_assoc, and_congr_right_iff, and_comm]
    exact fun _ _ => ‚ü®fun h => by grind,
      fun h => ‚ü®h 0 (by grind), fun k _ => (h (k + 1) (by grind)).trans (by grind)‚ü©‚ü©

theorem isAdmissible_iff_pairwise_and_le : IsAdmissible m L ‚Üî
    L.Pairwise (¬∑ < ¬∑) ‚àß ‚àÄ k, (h : k < L.length) ‚Üí L[k] ‚â§ m + k := by
  rw [isAdmissible_iff_isChain_and_le, List.isChain_iff_pairwise]

theorem isAdmissible_of_isChain_of_forall_getElem_le {m L} (hL : L.IsChain (¬∑ < ¬∑))
    (hL‚ÇÇ : ‚àÄ k, (h : k < L.length) ‚Üí L[k] ‚â§ m + k) : IsAdmissible m L :=
  isAdmissible_iff_isChain_and_le.mpr ‚ü®hL, hL‚ÇÇ‚ü©

namespace IsAdmissible

@[grind ‚Üí] theorem isChain {m L} (hL : IsAdmissible m L) :
    L.IsChain (¬∑ < ¬∑) := (isAdmissible_iff_isChain_and_le.mp hL).1

@[grind ‚Üí] theorem le {m} {L : List ‚Ñï} (hL : IsAdmissible m L) : ‚àÄ k (h : k < L.length),
    L[k] ‚â§ m + k := (isAdmissible_iff_isChain_and_le.mp hL).2

/-- The tail of an `m`-admissible list is (m+1)-admissible. -/
@[grind ‚Üí] lemma of_cons {m a L} (h : IsAdmissible m (a :: L)) :
    IsAdmissible (m + 1) L := by cases L <;> grind

@[deprecated (since := "2025-10-15")]
alias tail := IsAdmissible.of_cons

lemma cons {m a L} (hL : IsAdmissible (m + 1) L) (ha : a ‚â§ m)
    (ha' : (_ : 0 < L.length) ‚Üí a < L[0]) : IsAdmissible m (a :: L) := by cases L <;> grind

theorem pairwise {m L} (hL : IsAdmissible m L) : L.Pairwise (¬∑ < ¬∑) :=
  hL.isChain.pairwise

@[deprecated  (since := "2025-10-16")]
alias sorted := pairwise

/-- If `(a :: l)` is `m`-admissible then a is less than all elements of `l` -/
@[grind ‚Üí]
lemma head_lt {m a L} (hL : IsAdmissible m (a :: L)) :
    ‚àÄ a' ‚àà L, a < a' := fun _ => L.rel_of_pairwise_cons hL.pairwise

@[grind ‚Üí] lemma getElem_lt {m L} (hL : IsAdmissible m L)
    {k : ‚Ñï} {hk : k < L.length} : L[k] < m + L.length :=
  (hL.le k hk).trans_lt (Nat.add_lt_add_left hk _)

/-- An element of a `m`-admissible list, as an element of the appropriate `Fin` -/
@[simps]
def getElemAsFin {m L} (hl : IsAdmissible m L) (k : ‚Ñï)
    (hK : k < L.length) : Fin (m + k + 1) :=
  Fin.mk L[k] <| Nat.le_iff_lt_add_one.mp (by grind)

/-- The head of an `m`-admissible list. -/
@[simps!]
def head {m a L} (hl : IsAdmissible m (a :: L)) : Fin (m + 1) :=
  hl.getElemAsFin 0 (by grind)

theorem mono {n} (hmn : m ‚â§ n) (hL : IsAdmissible m L) : IsAdmissible n L :=
  isAdmissible_of_isChain_of_forall_getElem_le (by grind) (by grind)

lemma head_lt' (a : ‚Ñï) (L : List ‚Ñï) (hl : IsAdmissible m (a :: L)) : a < m + 1 :=
  hl.getElemAsFin 0 (by simp)|>.prop

end IsAdmissible

end IsAdmissible

/-- The construction `simplicialInsert` describes inserting an element in a list of integer and
moving it to its "right place" according to the simplicial relations. Somewhat miraculously,
the algorithm is the same for the first or the fifth simplicial relations, making it "valid"
when we treat the list as a normal form for a morphism satisfying `P_Œ¥`, or for a morphism
satisfying `P_œÉ`!

This is similar in nature to `List.orderedInsert`, but note that we increment one of the element
every time we perform an exchange, making it a different construction. -/
@[local grind]
def simplicialInsert (a : ‚Ñï) : List ‚Ñï ‚Üí List ‚Ñï
  | [] => [a]
  | b :: l => if a < b then a :: b :: l else b :: simplicialInsert (a + 1) l

/-- `simplicialInsert` just adds one to the length. -/
lemma simplicialInsert_length (a : ‚Ñï) (L : List ‚Ñï) :
    (simplicialInsert a L).length = L.length + 1 := by
  induction L generalizing a <;> grind

/-- `simplicialInsert` preserves admissibility -/
theorem simplicialInsert_isAdmissible (L : List ‚Ñï) (hL : IsAdmissible (m + 1) L) (j : ‚Ñï)
    (hj : j ‚â§ m) :
    IsAdmissible m <| simplicialInsert j L := by
  induction L generalizing j m with
  | nil => exact IsAdmissible.singleton hj
  | cons a L h_rec => cases L <;> grind

end AdmissibleLists

section NormalFormsP_œÉ

-- Impl note.: The definition is a bit awkward with the extra parameters, but this
-- is necessary in order to avoid some type theory hell when proving that `orderedInsert`
-- behaves as expected...

/-- Given a sequence `L = [ i 0, ..., i b ]`, `standardœÉ m L` i is the morphism
`œÉ (i b) ‚â´ ‚Ä¶ ‚â´ œÉ (i 0)`. The construction is provided for any list of natural numbers,
but it is intended to behave well only when the list is admissible. -/
def standardœÉ (L : List ‚Ñï) {m‚ÇÅ m‚ÇÇ : ‚Ñï} (h : m‚ÇÇ + L.length = m‚ÇÅ) : mk m‚ÇÅ ‚ü∂ mk m‚ÇÇ :=
  match L with
  | .nil => eqToHom (by grind)
  | .cons a t => standardœÉ t (by grind) ‚â´ œÉ (Fin.ofNat _ a)

@[simp]
lemma standardœÉ_nil (m : ‚Ñï) : standardœÉ .nil (by grind) = ùüô (mk m) := rfl

@[simp, reassoc]
lemma standardœÉ_cons (L : List ‚Ñï) (a : ‚Ñï) {m‚ÇÅ m‚ÇÇ : ‚Ñï} (h : m‚ÇÇ + (a :: L).length = m‚ÇÅ) :
    standardœÉ (L.cons a) h = standardœÉ L (by grind) ‚â´ œÉ (Fin.ofNat _ a) := rfl

@[reassoc]
lemma standardœÉ_comp_standardœÉ (L‚ÇÅ L‚ÇÇ : List ‚Ñï) {m‚ÇÅ m‚ÇÇ m‚ÇÉ : ‚Ñï}
    (h : m‚ÇÇ + L‚ÇÅ.length = m‚ÇÅ) (h' : m‚ÇÉ + L‚ÇÇ.length = m‚ÇÇ) :
    standardœÉ L‚ÇÅ h ‚â´ standardœÉ L‚ÇÇ h' = standardœÉ (L‚ÇÇ ++ L‚ÇÅ) (by grind) := by
  induction L‚ÇÇ generalizing L‚ÇÅ m‚ÇÅ m‚ÇÇ m‚ÇÉ with
  | nil =>
    obtain rfl : m‚ÇÉ = m‚ÇÇ := by grind
    simp
  | cons a t H =>
    dsimp at h' ‚ä¢
    obtain rfl : m‚ÇÇ = (m‚ÇÉ + t.length) + 1 := by grind
    simp [reassoc_of% (H L‚ÇÅ (m‚ÇÅ := m‚ÇÅ) (m‚ÇÇ := m‚ÇÉ + t.length + 1) (m‚ÇÉ := m‚ÇÉ + 1)
      (by grind) (by grind))]

variable (m : ‚Ñï) (L : List ‚Ñï)

/-- `simplicialEvalœÉ` is a lift to ‚Ñï of `(toSimplexCategory.map (standardœÉ m L _ _)).toOrderHom`.
Rather than defining it as such, we define it inductively for less painful inductive reasoning,
(see `simplicialEvalœÉ_of_isAdmissible`).
It is expected to produce the correct result only if `L` is admissible, and values for
non-admissible lists should be considered junk values. Similarly, values for out-of-bounds inputs
are junk values. -/
@[local grind]
def simplicialEvalœÉ (L : List ‚Ñï) : ‚Ñï ‚Üí ‚Ñï :=
  fun j ‚Ü¶ match L with
  | [] => j
  | a :: L => if a < simplicialEvalœÉ L j then simplicialEvalœÉ L j - 1 else simplicialEvalœÉ L j

@[grind ‚Üê]
lemma simplicialEvalœÉ_of_le_mem (j : ‚Ñï) (hj : ‚àÄ k ‚àà L, j ‚â§ k) : simplicialEvalœÉ L j = j := by
  induction L with | nil => grind | cons _ _ _ => simp only [List.forall_mem_cons] at hj; grind

@[deprecated  (since := "2025-10-16")]
alias simplicialEvalœÉ_of_lt_mem := simplicialEvalœÉ_of_le_mem

lemma simplicialEvalœÉ_monotone (L : List ‚Ñï) : Monotone (simplicialEvalœÉ L) := by
  induction L <;> grind [Monotone]

variable {m}
/- We prove that `simplicialEvalœÉ` is indeed a lift of
`(toSimplexCategory.map (standardœÉ m L _ _)).toOrderHom` when the list is admissible. -/
lemma simplicialEvalœÉ_of_isAdmissible
    (m‚ÇÅ m‚ÇÇ : ‚Ñï) (hL : IsAdmissible m‚ÇÇ L) (hk : m‚ÇÇ + L.length = m‚ÇÅ)
    (j : ‚Ñï) (hj : j < m‚ÇÅ + 1) :
    (toSimplexCategory.map <| standardœÉ L hk).toOrderHom ‚ü®j, hj‚ü© =
    simplicialEvalœÉ L j := by
  induction L generalizing m‚ÇÅ m‚ÇÇ with
  | nil =>
    obtain rfl : m‚ÇÅ = m‚ÇÇ := by grind
    simp [simplicialEvalœÉ]
  | cons a L h_rec =>
    simp only [List.length_cons] at hk
    subst hk
    set a‚ÇÄ := hL.head
    have aux (t : Fin (m‚ÇÇ + 2)) :
        (a‚ÇÄ.predAbove t : ‚Ñï) = if a < ‚Üët then (t : ‚Ñï) - 1 else ‚Üët := by
      simp only [Fin.predAbove, a‚ÇÄ]
      split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
      ¬∑ rfl
      ¬∑ simp only [Fin.lt_def, Fin.coe_castSucc, IsAdmissible.head_val] at h‚ÇÅ; grind
      ¬∑ simp only [Fin.lt_def, Fin.coe_castSucc, IsAdmissible.head_val, not_lt] at h‚ÇÅ; grind
      ¬∑ rfl
    have := h_rec _ _ hL.of_cons (by grind) hj
    have ha‚ÇÄ : Fin.ofNat (m‚ÇÇ + 1) a = a‚ÇÄ := by ext; simpa [a‚ÇÄ] using hL.head.prop
    simpa only [toSimplexCategory_obj_mk, SimplexCategory.len_mk, standardœÉ_cons, Functor.map_comp,
      toSimplexCategory_map_œÉ, SimplexCategory.œÉ, SimplexCategory.mkHom,
      SimplexCategory.comp_toOrderHom, SimplexCategory.Hom.toOrderHom_mk, OrderHom.comp_coe,
      Function.comp_apply, Fin.predAboveOrderHom_coe, simplicialEvalœÉ, ha‚ÇÄ, ‚Üê this] using aux _

/-- Performing a simplicial insertion in a list is the same as composition on the right by the
corresponding degeneracy operator. -/
lemma standardœÉ_simplicialInsert (hL : IsAdmissible (m + 1) L) (j : ‚Ñï) (hj : j < m + 1)
    (m‚ÇÅ : ‚Ñï) (hm‚ÇÅ : m + L.length + 1 = m‚ÇÅ) :
    standardœÉ (m‚ÇÇ := m) (simplicialInsert j L) (m‚ÇÅ := m‚ÇÅ)
      (by simpa only [simplicialInsert_length, add_assoc]) =
    standardœÉ (m‚ÇÇ := m + 1) L (by grind) ‚â´ œÉ (Fin.ofNat _ j) := by
  induction L generalizing m j with
  | nil => simp [standardœÉ, simplicialInsert]
  | cons a L h_rec =>
    simp only [simplicialInsert]
    split_ifs
    ¬∑ simp
    ¬∑ have : ‚àÄ (j k : ‚Ñï) (h : j < (k + 1)), Fin.ofNat (k + 1) j = j := by simp -- helps grind below
      have : a < m + 2 := by grind -- helps grind below
      have : œÉ (Fin.ofNat (m + 2) a) ‚â´ œÉ (.ofNat _ j) = œÉ (.ofNat _ (j + 1)) ‚â´ œÉ (.ofNat _ a) := by
        convert œÉ_comp_œÉ_nat (n := m) a j (by grind) (by grind) (by grind) <;> grind
      grind [standardœÉ_cons]

attribute [local grind] simplicialInsert_length simplicialInsert_isAdmissible in
/-- Using `standardœÉ_simplicialInsert`, we can prove that every morphism satisfying `P_œÉ` is equal
to some `standardœÉ` for some admissible list of indices. -/
theorem exists_normal_form_P_œÉ {x y : SimplexCategoryGenRel} (f : x ‚ü∂ y) (hf : P_œÉ f) :
    ‚àÉ L : List ‚Ñï,
    ‚àÉ m : ‚Ñï, ‚àÉ b : ‚Ñï,
    ‚àÉ h‚ÇÅ : mk m = y, ‚àÉ h‚ÇÇ : x = mk (m + b), ‚àÉ h : L.length = b,
    IsAdmissible m L ‚àß f = standardœÉ L (by rw [h, h‚ÇÅ.symm, h‚ÇÇ]; rfl) := by
  induction hf with
  | id n =>
    use [], n.len, 0, rfl, rfl, rfl, IsAdmissible.nil _
    rfl
  | of f hf =>
    cases hf with | @œÉ m k =>
    use [k.val], m, 1, rfl, rfl, rfl, IsAdmissible.singleton k.is_le
    simp [standardœÉ]
  | @comp_of _ j x' g g' hg hg' h_rec =>
    cases hg' with | @œÉ m k =>
    obtain ‚ü®L‚ÇÅ, m‚ÇÅ, b‚ÇÅ, h‚ÇÅ', rfl, h', hL‚ÇÅ, e‚ÇÅ‚ü© := h_rec
    obtain rfl : m‚ÇÅ = m + 1 := congrArg (fun x ‚Ü¶ x.len) h‚ÇÅ'
    use simplicialInsert k.val L‚ÇÅ, m, b‚ÇÅ + 1, rfl, by grind, by grind, by grind
    subst_vars
    have := standardœÉ (m‚ÇÅ := m + 1 + L‚ÇÅ.length) [] (by grind) ‚â´=
      (standardœÉ_simplicialInsert L‚ÇÅ hL‚ÇÅ k k.prop _ rfl).symm
    simp_all [Fin.ofNat_eq_cast, Fin.cast_val_eq_self, standardœÉ_comp_standardœÉ_assoc,
      standardœÉ_comp_standardœÉ]

section MemIsAdmissible

@[grind]
lemma IsAdmissible.simplicialEvalœÉ_succ_getElem (hL : IsAdmissible m L)
    {k : ‚Ñï} {hk : k < L.length} : simplicialEvalœÉ L L[k] = simplicialEvalœÉ L (L[k] + 1) := by
  induction L generalizing m k <;> grind

lemma mem_isAdmissible_of_lt_and_eval_eq_eval_add_one (hL : IsAdmissible m L)
    (j : ‚Ñï) (hj‚ÇÅ : j < m + L.length) (hj‚ÇÇ : simplicialEvalœÉ L j = simplicialEvalœÉ L (j + 1)) :
    j ‚àà L := by
  induction L generalizing m with
  | nil => grind
  | cons a L h_rec =>
    have := simplicialEvalœÉ_monotone L (a := a + 1)
    rcases lt_trichotomy j a with h | h | h <;> grind

lemma lt_and_eval_eq_eval_add_one_of_mem_isAdmissible (hL : IsAdmissible m L) (j : ‚Ñï) (hj : j ‚àà L) :
    j < m + L.length ‚àß simplicialEvalœÉ L j = simplicialEvalœÉ L (j + 1) := by
  grind [List.mem_iff_getElem]

/-- We can characterize elements in an admissible list as exactly those for which
`simplicialEvalœÉ` takes the same value twice in a row. -/
lemma mem_isAdmissible_iff (hL : IsAdmissible m L) (j : ‚Ñï) :
    j ‚àà L ‚Üî j < m + L.length ‚àß simplicialEvalœÉ L j = simplicialEvalœÉ L (j + 1) := by
  grind [lt_and_eval_eq_eval_add_one_of_mem_isAdmissible,
    mem_isAdmissible_of_lt_and_eval_eq_eval_add_one]

end MemIsAdmissible

end NormalFormsP_œÉ

section NormalFormsP_Œ¥

/-- Given a sequence `L = [ i 0, ..., i b ]`, `standardŒ¥ n L` i is the morphism
`Œ¥ (i b) ‚â´ ‚Ä¶ ‚â´ Œ¥ (i 0)`. The construction is provided for any list of natural numbers,
but it is intended to behave well only when the list is admissible. -/
def standardŒ¥ (L : List ‚Ñï) {k l : ‚Ñï} (hkl : k + L.length = l) : mk k ‚ü∂ mk l :=
  match L with
  | .nil => eqToHom (by grind)
  | .cons a t => Œ¥ (Fin.ofNat (k + 2) a) ‚â´ standardŒ¥ t (by grind)

@[simp]
lemma standardŒ¥_nil (m : ‚Ñï) : standardŒ¥ .nil (by grind) = ùüô (mk m) := rfl

@[simp, reassoc]
lemma standardŒ¥_cons (L : List ‚Ñï) (a : ‚Ñï) {k l : ‚Ñï}
    (hkl : k + (a::L).length = l) :
    standardŒ¥ (a::L) hkl = Œ¥ (Fin.ofNat _ a) ‚â´ standardŒ¥ L (by grind) := rfl

@[reassoc]
lemma standardŒ¥_comp_standardŒ¥ (L‚ÇÅ L‚ÇÇ : List ‚Ñï) {m‚ÇÅ m‚ÇÇ m‚ÇÉ : ‚Ñï}
    (h : m‚ÇÅ + L‚ÇÅ.length = m‚ÇÇ) (h' : m‚ÇÇ + L‚ÇÇ.length = m‚ÇÉ) :
    standardŒ¥ L‚ÇÅ h ‚â´ standardŒ¥ L‚ÇÇ h' = standardŒ¥ (L‚ÇÅ ++ L‚ÇÇ) (by grind) := by
  induction L‚ÇÅ generalizing L‚ÇÇ m‚ÇÅ m‚ÇÇ m‚ÇÉ with
  | nil =>
    obtain rfl : m‚ÇÅ = m‚ÇÇ := by grind
    simp
  | cons a t H =>
    dsimp at h' ‚ä¢
    grind

/-- `simplicialEvalŒ¥` is a lift to ‚Ñï of `(toSimplexCategory.map (standardŒ¥ m L _ _)).toOrderHom`,
but we define it in an explicit recursive way to enable for less painful inductive reasoning.
It is expected to produce the correct result only if `L` is admissible but it is more convenient
to have it defined for any list of natural numbers.
See `simplicialEvalŒ¥_of_isAdmissible` for the fact that it is the function we claim it is when
the list is admissible. -/
def simplicialEvalŒ¥ (L : List ‚Ñï) : ‚Ñï ‚Üí ‚Ñï :=
  fun j ‚Ü¶ match L with
  | [] => j
  | a :: L => simplicialEvalŒ¥ L (if j < a then j else j + 1)

variable {n : ‚Ñï} (L : List ‚Ñï)

/-- We prove that simplicialEvalŒ¥ is indeed a lift to ‚Ñï of
`(toSimplexCategory.map <| standardŒ¥ L hnl).toOrderHom` when the list is admissible. -/
lemma simplicialEvalŒ¥_of_isAdmissible (hL : IsAdmissible (n + 1) L)
    {l : ‚Ñï} (hnl : n + L.length = l)
    (j : ‚Ñï) (hj : j < n + 1) :
    (toSimplexCategory.map <| standardŒ¥ L hnl).toOrderHom ‚ü®j, hj‚ü© =
    simplicialEvalŒ¥ L j := by
  induction L generalizing j n l with
  | nil =>
    obtain rfl : n = l := by grind
    simp [standardŒ¥, simplicialEvalŒ¥]
  | cons a L h_rec =>
    simp only [List.length_cons] at hnl
    subst hnl
    set a‚ÇÄ := hL.head
    have aux (t : Fin (n + 1)) :
        (a‚ÇÄ.succAbove t : ‚Ñï) = if ‚Üët < a then (t : ‚Ñï) else ‚Üët + 1 := by
      simp only [Fin.succAbove, a‚ÇÄ]
      split_ifs with h‚ÇÅ _ _
      ¬∑ rfl
      ¬∑ simp only [Fin.lt_def, Fin.coe_castSucc, IsAdmissible.head_val] at h‚ÇÅ; grind
      ¬∑ simp only [Fin.lt_def, Fin.coe_castSucc, IsAdmissible.head_val, not_lt] at h‚ÇÅ; grind
      ¬∑ rfl
    have ha‚ÇÄ : a = a‚ÇÄ := by simp [a‚ÇÄ]
    have := h_rec (l := n + (L.length + 1)) hL.of_cons (by grind) ‚Üë(a‚ÇÄ.succAbove ‚ü®j, hj‚ü©)
      (a‚ÇÄ.succAbove ‚ü®j, hj‚ü©).prop
    simp only [toSimplexCategory_obj_mk, SimplexCategory.len_mk, Fin.eta] at this
    simp [standardŒ¥, simplicialEvalŒ¥, SimplexCategory.Œ¥, ha‚ÇÄ, this, aux]

lemma simplicialEvalŒ¥_monotone : Monotone (simplicialEvalŒ¥ L) := by
  intro a b hab
  induction L generalizing a b with
  | nil => exact hab
  | cons head tail h_rec =>
    dsimp only [simplicialEvalŒ¥]
    split_ifs with h h' h' <;> exact h_rec (by grind)

variable (j : ‚Ñï)

lemma le_simplicialEvalŒ¥_self : j ‚â§ simplicialEvalŒ¥ L j := by
  induction L generalizing j with
  | nil => grind [simplicialEvalŒ¥]
  | cons head tail h_rec => grind [simplicialEvalŒ¥]

lemma simplicialEvalŒ¥_eq_self_of_isAdmissible_and_lt (hL : IsAdmissible (n + 1) L)
    (hj : ‚àÄ k ‚àà L, j < k) : simplicialEvalŒ¥ L j = j := by
  induction L generalizing n j with
  | nil => grind [simplicialEvalŒ¥]
  | cons a L h_rec => grind [simplicialEvalŒ¥, ‚Üí IsAdmissible.tail]

lemma simplicialEvalŒ¥_eq_self_of_isAdmissible_cons (a : ‚Ñï)
    (hL : IsAdmissible (n + 1) (a :: L)) : simplicialEvalŒ¥ L a = a :=
  simplicialEvalŒ¥_eq_self_of_isAdmissible_and_lt _ _ hL.of_cons hL.head_lt

/-- Performing a simplicial insertion in a list is the same
as composition on the left by the corresponding face operator. -/
lemma standardŒ¥_simplicialInsert (hL : IsAdmissible (n + 2) L) (j : ‚Ñï) (hj : j < n + 2)
    (m‚ÇÅ : ‚Ñï) (hm‚ÇÅ : n + L.length + 1 = m‚ÇÅ) :
    standardŒ¥ (k := n) (l := m‚ÇÅ) (simplicialInsert j L)
      (by grind [simplicialInsert_length]) =
    Œ¥ (Fin.ofNat _ j) ‚â´ standardŒ¥ L (by grind) := by
  induction L generalizing n j with
  | nil => grind [standardŒ¥, simplicialInsert]
  | cons a L h_rec =>
    simp only [simplicialInsert]
    split_ifs
    ¬∑ simp
    ¬∑ have : ‚àÄ (j k : ‚Ñï) (h : j < (k + 1)), Fin.ofNat (k + 1) j = j := by simp -- helps grind below
      have : a < n + 3 := by grind -- helps grind below
      have : Œ¥ (Fin.ofNat (n + 2) a) ‚â´ Œ¥ (.ofNat _ (j + 1)) = Œ¥ (.ofNat _ j) ‚â´ Œ¥ (.ofNat _ a) := by
        convert Œ¥_comp_Œ¥_nat (n := n) a j (by grind) (by grind) (by grind) <;> grind
      simp only [standardŒ¥_cons, reassoc_of% this, h_rec hL.of_cons (j + 1) (by grind) (by grind)]

attribute [local grind] simplicialInsert_length simplicialInsert_isAdmissible in
/-- Using `standardŒ¥_simplicialInsert`, we can prove that every morphism satisfying
`P_Œ¥` is equal to some `standardŒ¥` for some admissible list of indices, which shows
that every such morphism is equal to a morphism in "normal form". -/
theorem exists_normal_form_P_Œ¥ {x y : SimplexCategoryGenRel} (f : x ‚ü∂ y) (hf : P_Œ¥ f) :
    ‚àÉ L : List ‚Ñï, ‚àÉ m : ‚Ñï, ‚àÉ b : ‚Ñï,
    ‚àÉ h‚ÇÅ : mk m = x, ‚àÉ h‚ÇÇ : y = mk b, ‚àÉ h : m + L.length = b,
    (IsAdmissible (m + 1) L) ‚àß f = standardŒ¥ L (by rwa [‚Üê h‚ÇÅ, h‚ÇÇ]) := by
  dsimp [P_Œ¥] at hf
  rw [MorphismProperty.multiplicativeClosure_eq_multiplicativeClosure'] at hf
  induction hf with
  | id n =>
    use [], n.len, n.len, rfl, rfl, rfl, IsAdmissible.nil _
    exact (standardŒ¥_nil _).symm
  | of f hf =>
    cases hf with | @Œ¥ n j
    use [j.val], n, (n + 1) , rfl, rfl, rfl
    constructor <;> simp [Nat.le_of_lt_add_one j.prop, standardŒ¥]
  | @of_comp x' m j f g hf hg h_rec =>
    cases hf with | @Œ¥ n j
    obtain ‚ü®L‚ÇÅ, m‚ÇÅ, b‚ÇÅ, h‚ÇÅ', rfl, h', hL‚ÇÅ, e‚ÇÅ‚ü© := h_rec
    obtain rfl : m‚ÇÅ = n + 1 := congrArg (fun x ‚Ü¶ x.len) h‚ÇÅ'
    use simplicialInsert j.val L‚ÇÅ, n, n + 1 + L‚ÇÅ.length, rfl, by grind, by grind, by grind
    subst_vars
    symm
    have := standardŒ¥_simplicialInsert L‚ÇÅ hL‚ÇÅ j
    rw [show Fin.ofNat (n + 2) ‚Üëj = j by simp] at this
    apply this (by grind)
    simp +arith [SimplexCategoryGenRel.mk]

/-- An admissible list is fully determined by its length and the attached function
`simplicialEvalŒ¥`, which are both determined by the morphism in the usual `SimplexCategory`
corresponding to `standarŒ¥` of this list.
This essentially shows that the admissible list from `exists_normal_form_P_Œ¥` is unique. -/
lemma eq_of_simplicialEvalŒ¥_eq
    (L‚ÇÅ : List ‚Ñï) (hL‚ÇÅ : IsAdmissible (n + 1) L‚ÇÅ)
    (L‚ÇÇ : List ‚Ñï) (hL‚ÇÇ : IsAdmissible (n + 1) L‚ÇÇ)
    (h : ‚àÄ x < n + 1, simplicialEvalŒ¥ L‚ÇÅ x = simplicialEvalŒ¥ L‚ÇÇ x) :
    (L‚ÇÅ.length = L‚ÇÇ.length) ‚Üí (L‚ÇÅ = L‚ÇÇ) := by
  induction L‚ÇÅ generalizing L‚ÇÇ n with
  | nil => grind [List.eq_nil_of_length_eq_zero]
  | cons a L‚ÇÅ hrec =>
    cases L‚ÇÇ with
    | nil => tauto
    | cons b L‚ÇÇ =>
      obtain rfl : a = b := by
        have ha‚ÇÅ := h a
        have hb‚ÇÅ := h b
        simp only [simplicialEvalŒ¥, lt_self_iff_false, ‚ÜìreduceIte] at ha‚ÇÅ hb‚ÇÅ
        split_ifs at ha‚ÇÅ with ha‚ÇÇ <;> split_ifs at hb‚ÇÅ with hb‚ÇÇ <;>
        grind [‚Üí IsAdmissible.head_lt', le_simplicialEvalŒ¥_self,
          simplicialEvalŒ¥_eq_self_of_isAdmissible_and_lt, ‚Üí IsAdmissible.tail,
          List.sorted_cons]
      intro h_length
      simp only [List.cons.injEq, true_and]
      refine hrec hL‚ÇÅ.of_cons _ hL‚ÇÇ.of_cons (fun x hx => ?_) (by grind)
      have := h (x - 1) (by grind) -- helps grind
      have := Nat.lt_add_one_iff_lt_or_eq.mp hx -- helps grind
      grind [simplicialEvalŒ¥, simplicialEvalŒ¥_eq_self_of_isAdmissible_cons,
          ‚Üí IsAdmissible.head_lt']

end NormalFormsP_Œ¥

end SimplexCategoryGenRel
