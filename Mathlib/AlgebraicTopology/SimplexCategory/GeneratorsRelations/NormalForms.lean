/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.GeneratorsRelations.EpiMono
/-! # Normal forms for morphisms in `SimplexCategoryGenRel`.

In this file, we establish that `P_Î´` and `P_Ïƒ` morphisms in `SimplexCategoryGenRel`
each admits a normal form.

In both cases, the normal forms are encoded as an integer `m`, and a strictly increasing
lists of integers `[iâ‚€,â€¦,iâ‚™]` such that `iâ‚– â‰¤ m + k` for all `k`. We define a predicate
`isAdmissible m : List â„• â†’ Prop` encoding this property. And provide some lemmas to help
work with such lists.

Normal forms for `P_Ïƒ` morphisms are encoded by `m`-admissible lists, in which case the list
`[iâ‚€,â€¦,iâ‚™]` represents the morphism `Ïƒ iâ‚™ â‰« â‹¯ â‰« Ïƒ iâ‚€ : .mk (m + n) âŸ¶ .mk n`.

Normal forms for `P_Î´` morphisms are encoded by `(m + 1)`-admissible lists, in which case the list
`[iâ‚€,â€¦,iâ‚™]` represents the morphism `Î´ iâ‚€ â‰« â‹¯ â‰« Î´ iâ‚™ : .mk n âŸ¶ .mk (m + n)`.

The results in this file are to be treated as implementation-only, and they only serve as stepping
stones towards proving that the canonical functor
`toSimplexCategory : SimplexCategoryGenRel â¥¤ SimplexCategory` is an equivalence.

## References:
* [Kerodon Tag 04FQ](https://kerodon.net/tag/04FQ)
* [Kerodon Tag 04FT](https://kerodon.net/tag/04FT)

## TODOs:
- Show that every `P_Î´` admits a unique normal form.
-/

namespace SimplexCategoryGenRel

open CategoryTheory

section AdmissibleLists
-- Impl. note: We are not bundling admissible lists as a subtype of `List â„•` so that it remains
-- easier to perform inductive constructions and proofs on such lists, and we instead bundle
-- propositions asserting that various List constructions produce admissible lists.

/-- A list of natural numbers [iâ‚€, â‹¯, iâ‚™]) is said to be `m`-admissible (for `m : â„•`) if
`iâ‚€ < â‹¯ < iâ‚™` and `iâ‚– â‰¤ m + k` for all `k`.
-/
@[mk_iff]
inductive IsAdmissible : (m : â„•) â†’ (L : List â„•) â†’ Prop
| nil (m : â„•) : IsAdmissible m []
| singleton {m a} (ha : a â‰¤ m) : IsAdmissible m [a]
| cons_cons {m a b L'} (hab : a < b) (hbL : IsAdmissible (m + 1) (b :: L'))
    (ha : a â‰¤ m) : IsAdmissible m (a :: b :: L')

attribute [simp, grind â†] IsAdmissible.nil

section IsAdmissible

variable {m a b : â„•} {L : List â„•}

@[simp, grind =]
theorem isAdmissible_singleton : IsAdmissible m [a] â†” a â‰¤ m :=
    âŸ¨fun | .singleton h => h, .singletonâŸ©

@[simp, grind =]
theorem isAdmissible_cons_cons : IsAdmissible m (a :: b :: L) â†”
    a < b âˆ§ IsAdmissible (m + 1) (b :: L) âˆ§ a â‰¤ m :=
  âŸ¨fun | .cons_cons hab hbL ha => âŸ¨hab, hbL, haâŸ©, fun âŸ¨hab, hbL, haâŸ© => .cons_cons hab hbL haâŸ©

theorem isAdmissible_cons : IsAdmissible m (a :: L) â†”
    a â‰¤ m âˆ§ ((_ : 0 < L.length) â†’ a < L[0]) âˆ§ IsAdmissible (m + 1) L := by
  cases L <;> grind

theorem isAdmissible_iff_isChain_and_le : IsAdmissible m L â†”
    L.IsChain (Â· < Â·) âˆ§ âˆ€ k, (h : k < L.length) â†’ L[k] â‰¤ m + k := by
  induction L using List.twoStepInduction generalizing m with
  | nil => grind
  | singleton _ => simp
  | cons_cons _ _ _ _ IH =>
    simp_rw [isAdmissible_cons_cons, IH, List.length_cons, and_assoc,
      List.isChain_cons_cons, and_assoc, and_congr_right_iff, and_comm]
    exact fun _ _ => âŸ¨fun h =>
      fun | 0 => fun _ => h.1 | k + 1 => fun _ => (h.2 k _).trans (by grind),
      fun h => âŸ¨h 0 (by grind), fun k _ => (h (k + 1) (by grind)).trans (by grind)âŸ©âŸ©

theorem isAdmissible_iff_pairwise_and_le : IsAdmissible m L â†”
    L.Pairwise (Â· < Â·) âˆ§ âˆ€ k, (h : k < L.length) â†’ L[k] â‰¤ m + k := by
  rw [isAdmissible_iff_isChain_and_le, List.isChain_iff_pairwise]

theorem isAdmissible_iff_sorted_and_le : IsAdmissible m L â†”
    L.Sorted (Â· < Â·) âˆ§ âˆ€ k, (h : k < L.length) â†’ L[k] â‰¤ m + k :=
  isAdmissible_iff_pairwise_and_le

theorem _root_.List.IsChain.isAdmissible_of_forall_getElem_le {m L} (hL : L.IsChain (Â· < Â·))
    (hLâ‚‚ : âˆ€ k, (h : k < L.length) â†’ L[k] â‰¤ m + k) : IsAdmissible m L :=
  (isAdmissible_iff_isChain_and_le.mpr âŸ¨hL, hLâ‚‚âŸ©)

namespace IsAdmissible

theorem isChain {m L} (hL : IsAdmissible m L) :
    L.IsChain (Â· < Â·) := (isAdmissible_iff_isChain_and_le.mp hL).1

theorem le {m} {L : List â„•} (hL : IsAdmissible m L) : âˆ€ k (h : k < L.length),
    L[k] â‰¤ m + k := (isAdmissible_iff_isChain_and_le.mp hL).2

/-- The tail of an `m`-admissible list is (m+1)-admissible. -/
lemma of_cons {m a L} (h : IsAdmissible m (a :: L)) :
    IsAdmissible (m + 1) L := by cases L <;> grind

/-- The tail of an `m`-admissible list is (m+1)-admissible. -/
alias tail := IsAdmissible.of_cons

lemma cons {m a L} (hL : IsAdmissible (m + 1) L) (ha : a â‰¤ m)
    (ha' : (_ : 0 < L.length) â†’ a < L[0]) : IsAdmissible m (a :: L) := by cases L <;> grind

theorem pairwise {m L} (hL : IsAdmissible m L) : L.Pairwise (Â· < Â·) :=
  hL.isChain.pairwise

/-- If `(a :: l)` is `m`-admissible then a is less than all elements of `l` -/
@[grind â†’]
lemma head_lt {m a L} (hL : IsAdmissible m (a :: L)) :
    âˆ€ a' âˆˆ L, a < a' := fun _ => L.rel_of_pairwise_cons hL.pairwise

lemma head_le {m a L} (hL : IsAdmissible m (a :: L)) :
    âˆ€ a' âˆˆ (a :: L), a â‰¤ a' := by grind

theorem sorted {m L} (hL : IsAdmissible m L) : L.Sorted (Â· < Â·) := hL.pairwise

lemma getElem_lt {m L} (hL : IsAdmissible m L)
    {k : â„•} {hk : k < L.length} : L[k] < m + L.length := by
  exact (hL.le k hk).trans_lt (Nat.add_lt_add_left hk _)

/-- An element of a `m`-admissible list, as an element of the appropriate `Fin` -/
@[simps]
def getElemAsFin {m L} (hl : IsAdmissible m L) (k : â„•)
    (hK : k < L.length) : Fin (m + k + 1) :=
  Fin.mk L[k] <| Nat.le_iff_lt_add_one.mp (by simp [hl.le])

/-- The head of an `m`-admissible list. -/
@[simps!]
def head {m a L} (hl : IsAdmissible m (a :: L)) : Fin (m + 1) :=
  hl.getElemAsFin 0 (by simp)

theorem mono {n} (hmn : m â‰¤ n) (hL : IsAdmissible m L) : IsAdmissible n L :=
  hL.isChain.isAdmissible_of_forall_getElem_le
  fun _ _ => (hL.le _ _).trans <| Nat.add_le_add_right hmn _

end IsAdmissible

end IsAdmissible

/-- The construction `simplicialInsert` describes inserting an element in a list of integer and
moving it to its "right place" according to the simplicial relations. Somewhat miraculously,
the algorithm is the same for the first or the fifth simplicial relations, making it "valid"
when we treat the list as a normal form for a morphism satisfying `P_Î´`, or for a morphism
satisfying `P_Ïƒ`!

This is similar in nature to `List.orderedInsert`, but note that we increment one of the element
every time we perform an exchange, making it a different construction. -/
def simplicialInsert (a : â„•) : List â„• â†’ List â„•
  | [] => [a]
  | b :: l => if a < b then a :: b :: l else b :: simplicialInsert (a + 1) l

/-- `simplicialInsert` just adds one to the length. -/
lemma simplicialInsert_length (a : â„•) (L : List â„•) :
    (simplicialInsert a L).length = L.length + 1 := by
  induction L generalizing a with
  | nil => rfl
  | cons head tail h_rec =>
    dsimp only [simplicialInsert, List.length_cons]
    split_ifs with h <;> simp only [List.length_cons, h_rec (a + 1)]

variable (m)

/-- `simplicialInsert` preserves admissibility -/
theorem simplicialInsert_isAdmissible (L : List â„•) (hL : IsAdmissible (m + 1) L) (j : â„•)
    (hj : j â‰¤ m) :
    IsAdmissible m <| simplicialInsert j L := by
  induction L generalizing j m with
  | nil => exact IsAdmissible.singleton hj
  | cons a L h_rec =>
    simp only [simplicialInsert, apply_ite (IsAdmissible m Â·)]
    split_ifs with ha
    Â· exact hL.cons_cons ha hj
    Â· simp_rw [isAdmissible_cons]
      cases L <;> grind [simplicialInsert]

end AdmissibleLists

section NormalFormsP_Ïƒ

-- Impl note.: The definition is a bit awkward with the extra parameters, but this
-- is necessary in order to avoid some type theory hell when proving that `orderedInsert`
-- behaves as expected...

/-- Given a sequence `L = [ i 0, ..., i b ]`, `standardÏƒ m L` i is the morphism
`Ïƒ (i b) â‰« â€¦ â‰« Ïƒ (i 0)`. The construction is provided for any list of natural numbers,
but it is intended to behave well only when the list is admissible. -/
def standardÏƒ (L : List â„•) {mâ‚ mâ‚‚ : â„•} (h : mâ‚‚ + L.length = mâ‚) : mk mâ‚ âŸ¶ mk mâ‚‚ :=
  match L with
  | .nil => eqToHom (by grind)
  | .cons a t => standardÏƒ t (by grind) â‰« Ïƒ (Fin.ofNat _ a)

@[simp]
lemma standardÏƒ_nil (m : â„•) : standardÏƒ .nil (by grind) = ğŸ™ (mk m) := rfl

@[simp, reassoc]
lemma standardÏƒ_cons (L : List â„•) (a : â„•) {mâ‚ mâ‚‚ : â„•} (h : mâ‚‚ + (a :: L).length = mâ‚) :
    standardÏƒ (L.cons a) h = standardÏƒ L (by grind) â‰« Ïƒ (Fin.ofNat _ a) := rfl

@[reassoc]
lemma standardÏƒ_comp_standardÏƒ (Lâ‚ Lâ‚‚ : List â„•) {mâ‚ mâ‚‚ mâ‚ƒ : â„•}
    (h : mâ‚‚ + Lâ‚.length = mâ‚) (h' : mâ‚ƒ + Lâ‚‚.length = mâ‚‚) :
    standardÏƒ Lâ‚ h â‰« standardÏƒ Lâ‚‚ h' = standardÏƒ (Lâ‚‚ ++ Lâ‚) (by grind) := by
  induction Lâ‚‚ generalizing Lâ‚ mâ‚ mâ‚‚ mâ‚ƒ with
  | nil =>
    obtain rfl : mâ‚ƒ = mâ‚‚ := by grind
    simp
  | cons a t H =>
    dsimp at h' âŠ¢
    obtain rfl : mâ‚‚ = (mâ‚ƒ + t.length) + 1 := by grind
    simp [reassoc_of% (H Lâ‚ (mâ‚ := mâ‚) (mâ‚‚ := mâ‚ƒ + t.length + 1) (mâ‚ƒ := mâ‚ƒ + 1)
      (by grind) (by grind))]

variable (m : â„•) (L : List â„•)

/-- `simplicialEvalÏƒ` is a lift to â„• of `(toSimplexCategory.map (standardÏƒ m L _ _)).toOrderHom`.
Rather than defining it as such, we define it inductively for less painful inductive reasoning,
(see `simplicialEvalÏƒ_of_isAdmissible`).
It is expected to produce the correct result only if `L` is admissible, and values for
non-admissible lists should be considered junk values. Similarly, values for out-of-bonds inputs
are junk values. -/
def simplicialEvalÏƒ (L : List â„•) : â„• â†’ â„• :=
  fun j â†¦ match L with
  | [] => j
  | a :: L => if a < simplicialEvalÏƒ L j then simplicialEvalÏƒ L j - 1 else simplicialEvalÏƒ L j

lemma simplicialEvalÏƒ_of_le_mem (j : â„•) (hj : âˆ€ k âˆˆ L, j â‰¤ k) : simplicialEvalÏƒ L j = j := by
  induction L with
  | nil => grind [simplicialEvalÏƒ]
  | cons _ _ _ =>
    simp only [List.mem_cons, forall_eq_or_imp] at hj
    grind [simplicialEvalÏƒ]

@[deprecated  (since := "2025-10-15")]
alias simplicialEvalÏƒ_of_lt_mem := simplicialEvalÏƒ_of_le_mem

lemma simplicialEvalÏƒ_monotone (L : List â„•) : Monotone (simplicialEvalÏƒ L) := by
  intro a b hab
  induction L generalizing a b with
  | nil => exact hab
  | cons head tail h_rec => grind [simplicialEvalÏƒ]

variable {m}
/- We prove that `simplicialEvalÏƒ` is indeed a lift of
`(toSimplexCategory.map (standardÏƒ m L _ _)).toOrderHom` when the list is admissible. -/
lemma simplicialEvalÏƒ_of_isAdmissible
    (mâ‚ mâ‚‚ : â„•) (hL : IsAdmissible mâ‚‚ L) (hk : mâ‚‚ + L.length = mâ‚)
    (j : â„•) (hj : j < mâ‚ + 1) :
    (toSimplexCategory.map <| standardÏƒ L hk).toOrderHom âŸ¨j, hjâŸ© =
    simplicialEvalÏƒ L j := by
  induction L generalizing mâ‚ mâ‚‚ with
  | nil =>
    obtain rfl : mâ‚ = mâ‚‚ := by grind
    simp [simplicialEvalÏƒ]
  | cons a L h_rec =>
    simp only [List.length_cons] at hk
    subst hk
    set aâ‚€ := hL.head
    have aux (t : Fin (mâ‚‚ + 2)) :
        (aâ‚€.predAbove t : â„•) = if a < â†‘t then (t : â„•) - 1 else â†‘t := by
      simp only [Fin.predAbove, aâ‚€]
      split_ifs with hâ‚ hâ‚‚ hâ‚‚
      Â· rfl
      Â· simp only [Fin.lt_def, Fin.coe_castSucc, IsAdmissible.head_val] at hâ‚; grind
      Â· simp only [Fin.lt_def, Fin.coe_castSucc, IsAdmissible.head_val, not_lt] at hâ‚; grind
      Â· rfl
    have := h_rec _ _ hL.of_cons (by grind) hj
    have haâ‚€ : Fin.ofNat (mâ‚‚ + 1) a = aâ‚€ := by ext; simpa [aâ‚€] using hL.head.prop
    simpa only [toSimplexCategory_obj_mk, SimplexCategory.len_mk, standardÏƒ_cons, Functor.map_comp,
      toSimplexCategory_map_Ïƒ, SimplexCategory.Ïƒ, SimplexCategory.mkHom,
      SimplexCategory.comp_toOrderHom, SimplexCategory.Hom.toOrderHom_mk, OrderHom.comp_coe,
      Function.comp_apply, Fin.predAboveOrderHom_coe, simplicialEvalÏƒ, haâ‚€, â† this] using aux _

/-- Performing a simplicial insertion in a list is the same as composition on the right by the
corresponding degeneracy operator. -/
lemma standardÏƒ_simplicialInsert (hL : IsAdmissible (m + 1) L) (j : â„•) (hj : j < m + 1)
    (mâ‚ : â„•) (hmâ‚ : m + L.length + 1 = mâ‚) :
    standardÏƒ (mâ‚‚ := m) (simplicialInsert j L) (mâ‚ := mâ‚)
      (by simpa only [simplicialInsert_length, add_assoc]) =
    standardÏƒ (mâ‚‚ := m + 1) L (by grind) â‰« Ïƒ (Fin.ofNat _ j) := by
  induction L generalizing m j with
  | nil => simp [standardÏƒ, simplicialInsert]
  | cons a L h_rec =>
    simp only [simplicialInsert]
    split_ifs
    Â· simp
    Â· have : âˆ€ (j k : â„•) (h : j < (k + 1)), Fin.ofNat (k + 1) j = j := by simp -- helps grind below
      have : a < m + 2 := by grind -- helps grind below
      have : Ïƒ (Fin.ofNat (m + 2) a) â‰« Ïƒ (.ofNat _ j) = Ïƒ (.ofNat _ (j + 1)) â‰« Ïƒ (.ofNat _ a) := by
        convert Ïƒ_comp_Ïƒ_nat (n := m) a j (by grind) (by grind) (by grind) <;> grind
      simp only [standardÏƒ_cons, Category.assoc, this,
        h_rec hL.of_cons (j + 1) (by grind) (by grind)]

attribute [local grind] simplicialInsert_length simplicialInsert_isAdmissible in
/-- Using `standardÏƒ_simplicialInsert`, we can prove that every morphism satisfying `P_Ïƒ` is equal
to some `standardÏƒ` for some admissible list of indices. -/
theorem exists_normal_form_P_Ïƒ {x y : SimplexCategoryGenRel} (f : x âŸ¶ y) (hf : P_Ïƒ f) :
    âˆƒ L : List â„•,
    âˆƒ m : â„•, âˆƒ b : â„•,
    âˆƒ hâ‚ : mk m = y, âˆƒ hâ‚‚ : x = mk (m + b), âˆƒ h : L.length = b,
    IsAdmissible m L âˆ§ f = standardÏƒ L (by rw [h, hâ‚.symm, hâ‚‚]; rfl) := by
  induction hf with
  | id n =>
    use [], n.len, 0, rfl, rfl, rfl, IsAdmissible.nil _
    rfl
  | of f hf =>
    cases hf with | @Ïƒ m k =>
    use [k.val], m, 1, rfl, rfl, rfl, IsAdmissible.singleton k.is_le
    simp [standardÏƒ]
  | @comp_of _ j x' g g' hg hg' h_rec =>
    cases hg' with | @Ïƒ m k =>
    obtain âŸ¨Lâ‚, mâ‚, bâ‚, hâ‚', rfl, h', hLâ‚, eâ‚âŸ© := h_rec
    obtain rfl : mâ‚ = m + 1 := congrArg (fun x â†¦ x.len) hâ‚'
    use simplicialInsert k.val Lâ‚, m, bâ‚ + 1, rfl, by grind, by grind, by grind
    subst_vars
    have := standardÏƒ (mâ‚ := m + 1 + Lâ‚.length) [] (by grind) â‰«=
      (standardÏƒ_simplicialInsert Lâ‚ hLâ‚ k k.prop _ rfl).symm
    simp_all [Fin.ofNat_eq_cast, Fin.cast_val_eq_self, standardÏƒ_comp_standardÏƒ_assoc,
      standardÏƒ_comp_standardÏƒ]

section MemIsAdmissible

lemma IsAdmissible.simplicialEvalÏƒ_succ_getElem (hL : IsAdmissible m L)
    {k : â„•} {hk : k < L.length} : simplicialEvalÏƒ L L[k] = simplicialEvalÏƒ L (L[k] + 1) := by
  induction L generalizing m k with
  | nil => grind
  | cons a L h_rec =>
    Â· cases k
      Â· grind [simplicialEvalÏƒ, simplicialEvalÏƒ_of_le_mem]
      Â· grind [simplicialEvalÏƒ, isAdmissible_cons]

lemma mem_isAdmissible_of_lt_and_eval_eq_eval_add_one (hL : IsAdmissible m L)
    (j : â„•) (hjâ‚ : j < m + L.length) (hjâ‚‚ : simplicialEvalÏƒ L j = simplicialEvalÏƒ L (j + 1)) :
    j âˆˆ L := by
  induction L generalizing m with
  | nil => grind [simplicialEvalÏƒ]
  | cons a L h_rec =>
    rcases lt_trichotomy j a with h | h | h
    Â· grind [simplicialEvalÏƒ_of_le_mem]
    Â· grind
    Â· have := simplicialEvalÏƒ_of_le_mem L (a + 1) (by grind)
      have := simplicialEvalÏƒ_monotone L (a := a + 1)
      have := h_rec hL.of_cons (by grind)
      grind [simplicialEvalÏƒ]

lemma lt_and_eval_eq_eval_add_one_of_mem_isAdmissible (hL : IsAdmissible m L) (j : â„•) (hj : j âˆˆ L) :
    j < m + L.length âˆ§ simplicialEvalÏƒ L j = simplicialEvalÏƒ L (j + 1) := by
  simp_rw [List.mem_iff_getElem] at hj
  rcases hj with âŸ¨k, hk, rflâŸ©
  exact âŸ¨hL.getElem_lt, hL.simplicialEvalÏƒ_succ_getElemâŸ©

/-- We can characterize elements in an admissible list as exactly those for which
`simplicialEvalÏƒ` takes the same value twice in a row. -/
lemma mem_isAdmissible_iff (hL : IsAdmissible m L) (j : â„•) :
    j âˆˆ L â†” j < m + L.length âˆ§ simplicialEvalÏƒ L j = simplicialEvalÏƒ L (j + 1) := by
  grind [lt_and_eval_eq_eval_add_one_of_mem_isAdmissible,
    mem_isAdmissible_of_lt_and_eval_eq_eval_add_one]

end MemIsAdmissible

end NormalFormsP_Ïƒ

end SimplexCategoryGenRel
