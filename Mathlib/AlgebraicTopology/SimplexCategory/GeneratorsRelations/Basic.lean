/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.CategoryTheory.PathCategory.MorphismProperty
import Mathlib.AlgebraicTopology.SimplexCategory
/-! # Presentation of the simplex category by generator and relations.

We introduce `SimplexCategoryGenRel` as a the category presented by generating
morphisms `Î´ i : [n] âŸ¶ [n + 1]` and `Ïƒ i : [n + 1] âŸ¶ [n]` and subject to the
simplicial identities, and we provide induction principles for reasoning about
objects and morphisms in this category.

This category admits a canonical functor `toSimplexCategory` to the usual simplex category.
The fact that this functor is an equivalence will be recorded in a separate file.
-/
namespace AlgebraicTopology.SimplexCategory

open CategoryTheory

/-- The objects of the free simplex quiver are the natural numbers. -/
def FreeSimplexQuiver := â„•

/-- Making an object of `FreeSimplexQuiver` out of a natural number. -/
def FreeSimplexQuiver.mk (n : â„•) : FreeSimplexQuiver := n

/-- Getting back the natural number from the objects. -/
def FreeSimplexQuiver.len (x : FreeSimplexQuiver) : â„• := x

namespace FreeSimplexQuiver

/-- A morphisms in `FreeSimplexQuiver` is either a face map (`Î´`) or a degeneracy map (`Ïƒ`). -/
inductive Hom : FreeSimplexQuiver â†’ FreeSimplexQuiver â†’ Type
  | Î´ {n : â„•} (i : Fin (n + 2)) : Hom (.mk n) (.mk (n + 1))
  | Ïƒ {n : â„•} (i : Fin (n + 1)) : Hom (.mk (n + 1)) (.mk n)

instance Quiv : Quiver FreeSimplexQuiver where
  Hom := FreeSimplexQuiver.Hom

/-- `FreeSimplexQuiver.Î´ i` represents the `i`-th face map `.mk n âŸ¶ .mk (n + 1)`. -/
abbrev Î´ {n : â„•} (i : Fin (n + 2)) : FreeSimplexQuiver.mk n âŸ¶ .mk (n + 1) :=
  FreeSimplexQuiver.Hom.Î´ i

/-- `FreeSimplexQuiver.Ïƒ i` represents `i`-th degeneracy map `.mk (n + 1) âŸ¶ .mk n`. -/
abbrev Ïƒ {n : â„•} (i : Fin (n + 1)) : FreeSimplexQuiver.mk (n + 1) âŸ¶ .mk n :=
  FreeSimplexQuiver.Hom.Ïƒ i

/-- `FreeSimplexQuiver.homRel` is the relation on morphisms freely generated on the
five simplicial identities. -/
inductive homRel : HomRel (Paths FreeSimplexQuiver)
  | simplicial1 {n : â„•} {i j : Fin (n + 2)} (H : i â‰¤ j) : homRel
    (Paths.of.map (Î´ i) â‰« Paths.of.map (Î´ j.succ))
    (Paths.of.map (Î´ j) â‰« Paths.of.map (Î´ i.castSucc))
  | simplicial2 {n : â„•} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ j.castSucc) : homRel
    (Paths.of.map (Î´ i.castSucc) â‰« Paths.of.map (Ïƒ j.succ))
    (Paths.of.map (Ïƒ j) â‰« Paths.of.map (Î´ i))
  | simplicial3â‚ {n : â„•} {i : Fin (n + 1)} : homRel
    (Paths.of.map (Î´ i.castSucc) â‰« Paths.of.map (Ïƒ i)) (ğŸ™ _)
  | simplicial3â‚‚ {n : â„•} {i : Fin (n + 1)} : homRel
    (Paths.of.map (Î´ i.succ) â‰« Paths.of.map (Ïƒ i)) (ğŸ™ _)
  | simplicial4 {n : â„•} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) : homRel
    (Paths.of.map (Î´ i.succ) â‰« Paths.of.map (Ïƒ j.castSucc))
    (Paths.of.map (Ïƒ j) â‰« Paths.of.map (Î´ i))
  | simplicial5 {n : â„•} {i j : Fin (n + 1)} (H : i â‰¤ j) : homRel
    (Paths.of.map (Ïƒ i.castSucc) â‰« Paths.of.map (Ïƒ j))
    (Paths.of.map (Ïƒ j.succ) â‰« Paths.of.map (Ïƒ i))

end FreeSimplexQuiver

/-- SimplexCategory is the category presented by generators and relation by the simplicial
identities.-/
def SimplexCategoryGenRel := Quotient FreeSimplexQuiver.homRel
  deriving Category

/-- `SimplexCategoryGenRel.mk` is the main constructor for objects of `SimplexCategoryGenRel`. -/
def SimplexCategoryGenRel.mk (n : â„•) : SimplexCategoryGenRel where
  as := Paths.of.obj n

namespace SimplexCategoryGenRel

/-- `SimplexCategoryGenRel.Î´ i` is the `i`-th face map `.mk n âŸ¶ .mk (n + 1)`. -/
abbrev Î´ {n : â„•} (i : Fin (n + 2)) : (SimplexCategoryGenRel.mk n) âŸ¶ .mk (n + 1) :=
  (Quotient.functor FreeSimplexQuiver.homRel).map <| Paths.of.map (.Î´ i)

/-- `SimplexCategoryGenRel.Ïƒ i` is the `i`-th degeneracy map `.mk (n + 1) âŸ¶ .mk n`. -/
abbrev Ïƒ {n : â„•} (i : Fin (n + 1)) :
    (SimplexCategoryGenRel.mk (n + 1)) âŸ¶ (SimplexCategoryGenRel.mk n) :=
  (Quotient.functor FreeSimplexQuiver.homRel).map <| Paths.of.map (.Ïƒ i)

/-- The length of an object of `SimplexCategoryGenRel`. -/
def len (x : SimplexCategoryGenRel) : â„• := by rcases x with âŸ¨nâŸ©; exact n

@[simp]
lemma mk_len (n : â„•) : (len (mk n)) = n := rfl

section InductionPrinciples

/-- An induction principle for reasonning about morphisms properties in SimplexCategoryGenRel. -/
lemma hom_induction (P : MorphismProperty SimplexCategoryGenRel)
    (hi : âˆ€ {n : â„•}, P (ğŸ™ (.mk n)))
    (hcâ‚ : âˆ€ {n m : â„•} (u : .mk n âŸ¶ .mk m) (i : Fin (m + 2)), P u â†’ P (u â‰« Î´ i))
    (hcâ‚‚ : âˆ€ {n m : â„•} (u : .mk n âŸ¶ .mk (m + 1)) (i : Fin (m + 1)), P u â†’ P (u â‰« Ïƒ i))
    {a b : SimplexCategoryGenRel} (f : a âŸ¶ b) :
    P f := by
  apply CategoryTheory.Quotient.induction (P := (fun f â†¦ P f))
  apply Paths.induction
  Â· exact hi
  Â· rintro _ _ _ _ âŸ¨âŸ©
    Â· exact hcâ‚ _ _
    Â· exact hcâ‚‚ _ _

/-- An induction principle for reasonning about morphisms in SimplexCategoryGenRel, where we compose
with generators on the right. -/
lemma hom_induction' (P : MorphismProperty SimplexCategoryGenRel)
    (hi : âˆ€ {n : â„•}, P (ğŸ™ (SimplexCategoryGenRel.mk n)))
    (hcâ‚ : âˆ€ {n m : â„•} (u : SimplexCategoryGenRel.mk (m + 1) âŸ¶ SimplexCategoryGenRel.mk n)
      (i : Fin (m + 2)), P u â†’ P (Î´ i â‰« u))
    (hcâ‚‚ : âˆ€ {n m : â„•} (u : SimplexCategoryGenRel.mk m âŸ¶ SimplexCategoryGenRel.mk n)
      (i : Fin (m + 1)), P u â†’ P (Ïƒ i â‰« u )) {a b : SimplexCategoryGenRel} (f : a âŸ¶ b) :
    P f := by
  apply CategoryTheory.Quotient.induction (P := (fun f â†¦ P f))
  apply Paths.induction'
  Â· exact hi
  Â· rintro _ _ _ âŸ¨âŸ© _
    Â· exact hcâ‚ _ _
    Â· exact hcâ‚‚ _ _

/-- An induction principle for reasonning about morphisms properties in SimplexCategoryGenRel. -/
lemma hom_induction_eq_top (P : MorphismProperty SimplexCategoryGenRel)
    (hi : âˆ€ {n : â„•}, P (ğŸ™ (.mk n)))
    (hcâ‚ : âˆ€ {n m : â„•} (u : .mk n âŸ¶ .mk m) (i : Fin (m + 2)),
      P u â†’ P (u â‰« Î´ i))
    (hcâ‚‚ : âˆ€ {n m : â„•} (u : .mk n âŸ¶ .mk (m + 1))
      (i : Fin (m + 1)), P u â†’ P (u â‰« Ïƒ i)) :
    P = âŠ¤ := by
  ext; constructor
  Â· simp
  Â· intro _
    apply hom_induction <;> assumption

/-- An induction principle for reasonning about morphisms properties in SimplexCategoryGenRel,
where we compose with generators on the right. -/
lemma hom_induction_eq_top' (P : MorphismProperty SimplexCategoryGenRel)
    (hi : âˆ€ {n : â„•}, P (ğŸ™ (.mk n)))
    (hcâ‚ : âˆ€ {n m : â„•} (u : .mk (m + 1) âŸ¶ .mk n) (i : Fin (m + 2)), P u â†’ (P (Î´ i â‰« u)))
    (hcâ‚‚ : âˆ€ {n m : â„•} (u : .mk m âŸ¶ .mk n) (i : Fin (m + 1)), P u â†’ (P (Ïƒ i â‰« u ))) :
    P = âŠ¤ := by
  ext; constructor
  Â· simp
  Â· intro _
    apply hom_induction' <;> assumption

/-- An induction principle for reasonning about objects in SimplexCategoryGenRel. This should be
used instead of identifying an object with `mk` of its len.-/
@[elab_as_elim]
def obj_induction {P : SimplexCategoryGenRel â†’ Sort*}
    (H : âˆ€ n : â„•, P (.mk n)) :
    âˆ€ x : SimplexCategoryGenRel, P x := by
  intro x
  exact H x.len

/-- A basic ext lemma for objects of SimplexCategoryGenRel --/
@[ext]
lemma obj_ext {x y : SimplexCategoryGenRel} (h : x.len = y.len) : x = y := by
  cases x using obj_induction
  cases y using obj_induction
  simp only [mk_len] at h
  congr

end InductionPrinciples

section SimplicialIdentities

@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    Î´ i â‰« Î´ j.succ = Î´ j â‰« Î´ i.castSucc := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.simplicial1 H

@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ j.castSucc) :
    Î´ i.castSucc â‰« Ïƒ j.succ = Ïƒ j â‰« Î´ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.simplicial2 H

@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} :
    Î´ i.castSucc â‰« Ïƒ i = ğŸ™ (mk n) := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.simplicial3â‚

@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : Î´ i.succ â‰« Ïƒ i = ğŸ™ (mk n) := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.simplicial3â‚‚

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) :
    Î´ i.succ â‰« Ïƒ j.castSucc = Ïƒ j â‰« Î´ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.simplicial4 H

@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    Ïƒ i.castSucc â‰« Ïƒ j = Ïƒ j.succ â‰« Ïƒ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.simplicial5 H

/-- A version of Î´_comp_Î´ with indices in â„• satisfying relevant inequalities. -/
lemma Î´_comp_Î´_nat {n} (i j : â„•) (hi : i < n + 2) (hj : j < n + 2) (H : i â‰¤ j) :
    Î´ (i : Fin (n + 2)) â‰« Î´ â†‘(j + 1) = Î´ â†‘j â‰« Î´ â†‘i := by
  let iâ‚ : Fin (n + 2) := âŸ¨i, hiâŸ©
  let jâ‚ : Fin (n + 2) := âŸ¨j, hjâŸ©
  conv_lhs =>
    congr
    Â· right
      equals iâ‚ =>
        congr
        ext
        simp only [Fin.val_natCast, Nat.mod_eq_of_lt hi]
        rfl
    Â· right
      equals jâ‚.succ =>
        congr
        ext
        simp only [Fin.val_natCast]
        rw [Nat.mod_eq_of_lt]
        Â· rfl
        Â· omega
  rw [Î´_comp_Î´ (by simpa)]
  congr
  Â· ext
    simp only [Fin.val_succ, Fin.val_natCast, Nat.mod_eq_of_lt hj,
      Nat.mod_eq_of_lt (Nat.succ_lt_succ hi), Nat.succ_eq_add_one, add_left_inj ]
    rfl
  Â· ext
    simp only [Fin.val_succ, Fin.val_natCast, Nat.succ_eq_add_one, add_left_inj ]
    rw [Nat.mod_eq_of_lt]
    Â· rfl
    Â· omega

/-- A version of Ïƒ_comp_Ïƒ with indices in â„• satisfying relevant inequalities. -/
lemma Ïƒ_comp_Ïƒ_nat {n} (i j : â„•) (hi : i < n + 1) (hj : j < n + 1) (H : i â‰¤ j) :
    Ïƒ (i : Fin (n + 1 + 1)) â‰« Ïƒ (j : Fin (n + 1)) = Ïƒ â†‘(j + 1) â‰« Ïƒ â†‘i := by
  let iâ‚ : Fin (n + 1) := âŸ¨i, hiâŸ©
  let jâ‚ : Fin (n + 1) := âŸ¨j, hjâŸ©
  conv_lhs =>
    congr
    Â· right
      equals iâ‚.castSucc =>
        congr
        ext
        simp only [Fin.val_natCast, Nat.mod_eq_of_lt (Nat.lt_succ_of_lt hi), Fin.coe_castSucc]
        rfl
    Â· right
      equals jâ‚ =>
        congr
        ext
        simp only [Fin.val_natCast, Nat.mod_eq_of_lt hj]
        rfl
  rw [Ïƒ_comp_Ïƒ (by simpa)]
  congr <;> {
    ext
    simp only [Fin.val_succ, Fin.val_natCast, Nat.mod_eq_of_lt hi,
      Nat.mod_eq_of_lt (Nat.succ_lt_succ hj), Nat.succ_eq_add_one, add_left_inj ]
    rfl }

end SimplicialIdentities

/-- The canonical functor from `SimplexCategoryGenRel` to SimplexCategory, which exists as the
simplicial identities hold in `SimplexCategory`. -/
def toSimplexCategory : SimplexCategoryGenRel â¥¤ SimplexCategory := by
  fapply CategoryTheory.Quotient.lift
  Â· fapply Paths.lift
    exact { obj i := .mk i,
            map f := match f with
              | FreeSimplexQuiver.Hom.Î´ i => SimplexCategory.Î´ i
              | FreeSimplexQuiver.Hom.Ïƒ i => SimplexCategory.Ïƒ i }
  Â· intro x y fâ‚ fâ‚‚ h
    cases h with
    | simplicial1 H => exact SimplexCategory.Î´_comp_Î´ H
    | simplicial2 H => exact SimplexCategory.Î´_comp_Ïƒ_of_le H
    | simplicial3â‚ => exact SimplexCategory.Î´_comp_Ïƒ_self
    | simplicial3â‚‚ => exact SimplexCategory.Î´_comp_Ïƒ_succ
    | simplicial4 H => exact SimplexCategory.Î´_comp_Ïƒ_of_gt H
    | simplicial5 H => exact SimplexCategory.Ïƒ_comp_Ïƒ H

@[simp]
lemma toSimplexCategory_obj_mk (n : â„•) : toSimplexCategory.obj (mk n) = .mk n := rfl

@[simp]
lemma toSimplexCategory_map_Î´ {n : â„•} (i : Fin (n + 2)) : toSimplexCategory.map (Î´ i) =
    SimplexCategory.Î´ i := rfl

@[simp]
lemma toSimplexCategory_map_Ïƒ {n : â„•} (i : Fin (n + 1)) : toSimplexCategory.map (Ïƒ i) =
    SimplexCategory.Ïƒ i := rfl

@[simp]
lemma toSimplexCategory_len {x : SimplexCategoryGenRel} : (toSimplexCategory.obj x).len = x.len :=
  rfl

end SimplexCategoryGenRel

end AlgebraicTopology.SimplexCategory
