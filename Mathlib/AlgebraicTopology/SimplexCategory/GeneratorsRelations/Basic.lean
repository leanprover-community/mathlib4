/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Basic
import Mathlib.CategoryTheory.PathCategory.Basic
/-! # Presentation of the simplex category by generators and relations.

We introduce `SimplexCategoryGenRel` as the category presented by generating
morphisms `Œ¥ i : [n] ‚ü∂ [n + 1]` and `œÉ i : [n + 1] ‚ü∂ [n]` and subject to the
simplicial identities, and we provide induction principles for reasoning about
objects and morphisms in this category.

This category admits a canonical functor `toSimplexCategory` to the usual simplex category.
The fact that this functor is an equivalence will be recorded in a separate file.
-/
open CategoryTheory

/-- The objects of the free simplex quiver are the natural numbers. -/
def FreeSimplexQuiver := ‚Ñï

/-- Making an object of `FreeSimplexQuiver` out of a natural number. -/
def FreeSimplexQuiver.mk (n : ‚Ñï) : FreeSimplexQuiver := n

/-- Getting back the natural number from the objects. -/
def FreeSimplexQuiver.len (x : FreeSimplexQuiver) : ‚Ñï := x

namespace FreeSimplexQuiver

/-- A morphism in `FreeSimplexQuiver` is either a face map (`Œ¥`) or a degeneracy map (`œÉ`). -/
inductive Hom : FreeSimplexQuiver ‚Üí FreeSimplexQuiver ‚Üí Type
  | Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) : Hom (.mk n) (.mk (n + 1))
  | œÉ {n : ‚Ñï} (i : Fin (n + 1)) : Hom (.mk (n + 1)) (.mk n)

instance quiv : Quiver FreeSimplexQuiver where
  Hom := FreeSimplexQuiver.Hom

/-- `FreeSimplexQuiver.Œ¥ i` represents the `i`-th face map `.mk n ‚ü∂ .mk (n + 1)`. -/
abbrev Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) : FreeSimplexQuiver.mk n ‚ü∂ .mk (n + 1) :=
  FreeSimplexQuiver.Hom.Œ¥ i

/-- `FreeSimplexQuiver.œÉ i` represents `i`-th degeneracy map `.mk (n + 1) ‚ü∂ .mk n`. -/
abbrev œÉ {n : ‚Ñï} (i : Fin (n + 1)) : FreeSimplexQuiver.mk (n + 1) ‚ü∂ .mk n :=
  FreeSimplexQuiver.Hom.œÉ i

/-- `FreeSimplexQuiver.homRel` is the relation on morphisms freely generated on the
five simplicial identities. -/
inductive homRel : HomRel (Paths FreeSimplexQuiver)
  | Œ¥_comp_Œ¥ {n : ‚Ñï} {i j : Fin (n + 2)} (H : i ‚â§ j) : homRel
    ((Paths.of FreeSimplexQuiver).map (Œ¥ i) ‚â´ (Paths.of FreeSimplexQuiver).map (Œ¥ j.succ))
    ((Paths.of FreeSimplexQuiver).map (Œ¥ j) ‚â´ (Paths.of FreeSimplexQuiver).map (Œ¥ i.castSucc))
  | Œ¥_comp_œÉ_of_le {n : ‚Ñï} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i ‚â§ j.castSucc) : homRel
    ((Paths.of FreeSimplexQuiver).map (Œ¥ i.castSucc) ‚â´ (Paths.of FreeSimplexQuiver).map (œÉ j.succ))
    ((Paths.of FreeSimplexQuiver).map (œÉ j) ‚â´ (Paths.of FreeSimplexQuiver).map (Œ¥ i))
  | Œ¥_comp_œÉ_self {n : ‚Ñï} {i : Fin (n + 1)} : homRel
    ((Paths.of FreeSimplexQuiver).map (Œ¥ i.castSucc) ‚â´ (Paths.of FreeSimplexQuiver).map (œÉ i)) (ùüô _)
  | Œ¥_comp_œÉ_succ {n : ‚Ñï} {i : Fin (n + 1)} : homRel
    ((Paths.of FreeSimplexQuiver).map (Œ¥ i.succ) ‚â´ (Paths.of FreeSimplexQuiver).map (œÉ i)) (ùüô _)
  | Œ¥_comp_œÉ_of_gt {n : ‚Ñï} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) : homRel
    ((Paths.of FreeSimplexQuiver).map (Œ¥ i.succ) ‚â´ (Paths.of FreeSimplexQuiver).map (œÉ j.castSucc))
    ((Paths.of FreeSimplexQuiver).map (œÉ j) ‚â´ (Paths.of FreeSimplexQuiver).map (Œ¥ i))
  | œÉ_comp_œÉ {n : ‚Ñï} {i j : Fin (n + 1)} (H : i ‚â§ j) : homRel
    ((Paths.of FreeSimplexQuiver).map (œÉ i.castSucc) ‚â´ (Paths.of FreeSimplexQuiver).map (œÉ j))
    ((Paths.of FreeSimplexQuiver).map (œÉ j.succ) ‚â´ (Paths.of FreeSimplexQuiver).map (œÉ i))

end FreeSimplexQuiver

/-- SimplexCategory is the category presented by generators and relation by the simplicial
identities. -/
def SimplexCategoryGenRel := Quotient FreeSimplexQuiver.homRel
  deriving Category

/-- `SimplexCategoryGenRel.mk` is the main constructor for objects of `SimplexCategoryGenRel`. -/
def SimplexCategoryGenRel.mk (n : ‚Ñï) : SimplexCategoryGenRel where
  as := (Paths.of FreeSimplexQuiver).obj n

namespace SimplexCategoryGenRel

/-- `SimplexCategoryGenRel.Œ¥ i` is the `i`-th face map `.mk n ‚ü∂ .mk (n + 1)`. -/
abbrev Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) : mk n ‚ü∂ mk (n + 1) :=
  (Quotient.functor FreeSimplexQuiver.homRel).map <| (Paths.of FreeSimplexQuiver).map (.Œ¥ i)

/-- `SimplexCategoryGenRel.œÉ i` is the `i`-th degeneracy map `.mk (n + 1) ‚ü∂ .mk n`. -/
abbrev œÉ {n : ‚Ñï} (i : Fin (n + 1)) : mk (n + 1) ‚ü∂ mk n :=
  (Quotient.functor FreeSimplexQuiver.homRel).map <| (Paths.of FreeSimplexQuiver).map (.œÉ i)

/-- The length of an object of `SimplexCategoryGenRel`. -/
def len (x : SimplexCategoryGenRel) : ‚Ñï := by rcases x with ‚ü®n‚ü©; exact n

@[simp]
lemma mk_len (n : ‚Ñï) : len (mk n) = n := rfl

section InductionPrinciples

/-- A morphism is called a face if it is a `Œ¥ i` for some `i : Fin (n + 2)`. -/
inductive faces : MorphismProperty SimplexCategoryGenRel
  | Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) : faces (Œ¥ i)

/-- A morphism is called a degeneracy if it is a `œÉ i` for some `i : Fin (n + 1)`. -/
inductive degeneracies : MorphismProperty SimplexCategoryGenRel
  | œÉ {n : ‚Ñï} (i : Fin (n + 1)) : degeneracies (œÉ i)

/-- A morphism is a generator if it is either a face or a degeneracy. -/
abbrev generators := faces ‚äî degeneracies

namespace generators

lemma Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) : generators (Œ¥ i) := le_sup_left (a := faces) _ (.Œ¥ i)

lemma œÉ {n : ‚Ñï} (i : Fin (n + 1)) : generators (œÉ i) := le_sup_right (a := faces) _ (.œÉ i)

end generators

/-- A property is true for every morphism iff it holds for generators and is multiplicative. -/
lemma multiplicativeClosure_isGenerator_eq_top : generators.multiplicativeClosure = ‚ä§ := by
  apply le_antisymm (by simp)
  intro x y f _
  apply CategoryTheory.Quotient.induction
  apply Paths.induction
  ¬∑ exact generators.multiplicativeClosure.id_mem _
  ¬∑ rintro _ _ _ _ ‚ü®‚ü© h
    ¬∑ exact generators.multiplicativeClosure.comp_mem _ _ h <| .of _ <| .Œ¥ _
    ¬∑ exact generators.multiplicativeClosure.comp_mem _ _ h <| .of _ <| .œÉ _

/-- An unrolled version of the induction principle obtained in the previous lemma. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
lemma hom_induction (P : MorphismProperty SimplexCategoryGenRel)
    (id : ‚àÄ {n : ‚Ñï}, P (ùüô (mk n)))
    (comp_Œ¥ : ‚àÄ {n m : ‚Ñï} (u : mk n ‚ü∂ mk m) (i : Fin (m + 2)), P u ‚Üí P (u ‚â´ Œ¥ i))
    (comp_œÉ : ‚àÄ {n m : ‚Ñï} (u : mk n ‚ü∂ mk (m + 1)) (i : Fin (m + 1)), P u ‚Üí P (u ‚â´ œÉ i))
    {a b : SimplexCategoryGenRel} (f : a ‚ü∂ b) :
    P f :=
  by
  suffices generators.multiplicativeClosure ‚â§ P by
    rw [multiplicativeClosure_isGenerator_eq_top, top_le_iff] at this
    rw [this]
    apply MorphismProperty.top_apply
  intro _ _ f hf
  induction hf with
  | of f h =>
    rcases h with ‚ü®‚ü®i‚ü©‚ü© | ‚ü®‚ü®i‚ü©‚ü©
    ¬∑ simpa using (comp_Œ¥ (ùüô _) i id)
    ¬∑ simpa using (comp_œÉ (ùüô _) i id)
  | id n => exact id
  | comp_of f g hf hg hrec =>
    rcases hg with ‚ü®‚ü®i‚ü©‚ü© | ‚ü®‚ü®i‚ü©‚ü©
    ¬∑ simpa using (comp_Œ¥ f i hrec)
    ¬∑ simpa using (comp_œÉ f i hrec)

/-- An induction principle for reasoning about morphisms in SimplexCategoryGenRel, where we compose
with generators on the right. -/
lemma hom_induction' (P : MorphismProperty SimplexCategoryGenRel)
    (id : ‚àÄ {n : ‚Ñï}, P (ùüô (mk n)))
    (Œ¥_comp : ‚àÄ {n m : ‚Ñï} (u : mk (m + 1) ‚ü∂ mk n)
      (i : Fin (m + 2)), P u ‚Üí P (Œ¥ i ‚â´ u))
    (œÉ_comp : ‚àÄ {n m : ‚Ñï} (u : mk m ‚ü∂ mk n)
      (i : Fin (m + 1)), P u ‚Üí P (œÉ i ‚â´ u)) {a b : SimplexCategoryGenRel} (f : a ‚ü∂ b) :
    P f := by
  suffices generators.multiplicativeClosure' ‚â§ P by
    rw [‚Üê MorphismProperty.multiplicativeClosure_eq_multiplicativeClosure',
      multiplicativeClosure_isGenerator_eq_top, top_le_iff] at this
    rw [this]
    apply MorphismProperty.top_apply
  intro _ _ f hf
  induction hf with
  | of f h =>
    rcases h with ‚ü®‚ü®i‚ü©‚ü© | ‚ü®‚ü®i‚ü©‚ü©
    ¬∑ simpa using (Œ¥_comp (ùüô _) i id)
    ¬∑ simpa using (œÉ_comp (ùüô _) i id)
  | id n => exact id
  | of_comp f g hf hg hrec =>
    rcases hf with ‚ü®‚ü®i‚ü©‚ü© | ‚ü®‚ü®i‚ü©‚ü©
    ¬∑ simpa using (Œ¥_comp g i hrec)
    ¬∑ simpa using (œÉ_comp g i hrec)

/-- An induction principle for reasoning about objects in `SimplexCategoryGenRel`. This should be
used instead of identifying an object with `mk` of its `len`. -/
@[elab_as_elim, cases_eliminator]
protected def rec {P : SimplexCategoryGenRel ‚Üí Sort*}
    (H : ‚àÄ n : ‚Ñï, P (.mk n)) :
    ‚àÄ x : SimplexCategoryGenRel, P x := by
  intro x
  exact H x.len

/-- A basic `ext` lemma for objects of `SimplexCategoryGenRel`. -/
@[ext]
lemma ext {x y : SimplexCategoryGenRel} (h : x.len = y.len) : x = y := by
  cases x
  cases y
  simp only [mk_len] at h
  congr

end InductionPrinciples

section SimplicialIdentities

@[reassoc]
theorem Œ¥_comp_Œ¥ {n} {i j : Fin (n + 2)} (H : i ‚â§ j) :
    Œ¥ i ‚â´ Œ¥ j.succ = Œ¥ j ‚â´ Œ¥ i.castSucc := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Œ¥_comp_Œ¥ H

@[reassoc]
theorem Œ¥_comp_œÉ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i ‚â§ j.castSucc) :
    Œ¥ i.castSucc ‚â´ œÉ j.succ = œÉ j ‚â´ Œ¥ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Œ¥_comp_œÉ_of_le H

@[reassoc]
theorem Œ¥_comp_œÉ_self {n} {i : Fin (n + 1)} :
    Œ¥ i.castSucc ‚â´ œÉ i = ùüô (mk n) := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Œ¥_comp_œÉ_self

@[reassoc]
theorem Œ¥_comp_œÉ_succ {n} {i : Fin (n + 1)} : Œ¥ i.succ ‚â´ œÉ i = ùüô (mk n) := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Œ¥_comp_œÉ_succ

@[reassoc]
theorem Œ¥_comp_œÉ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) :
    Œ¥ i.succ ‚â´ œÉ j.castSucc = œÉ j ‚â´ Œ¥ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Œ¥_comp_œÉ_of_gt H

@[reassoc]
theorem œÉ_comp_œÉ {n} {i j : Fin (n + 1)} (H : i ‚â§ j) :
    œÉ i.castSucc ‚â´ œÉ j = œÉ j.succ ‚â´ œÉ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.œÉ_comp_œÉ H

/-- A version of Œ¥_comp_Œ¥ with indices in ‚Ñï satisfying relevant inequalities. -/
lemma Œ¥_comp_Œ¥_nat {n} (i j : ‚Ñï) (hi : i < n + 2) (hj : j < n + 2) (H : i ‚â§ j) :
    Œ¥ ‚ü®i, hi‚ü© ‚â´ Œ¥ ‚ü®j + 1, by cutsat‚ü© = Œ¥ ‚ü®j, hj‚ü© ‚â´ Œ¥ ‚ü®i, by cutsat‚ü© :=
  Œ¥_comp_Œ¥ (n := n) (i := ‚ü®i, by cutsat‚ü©) (j := ‚ü®j, by cutsat‚ü©) (by simpa)

/-- A version of œÉ_comp_œÉ with indices in ‚Ñï satisfying relevant inequalities. -/
lemma œÉ_comp_œÉ_nat {n} (i j : ‚Ñï) (hi : i < n + 1) (hj : j < n + 1) (H : i ‚â§ j) :
    œÉ ‚ü®i, by cutsat‚ü© ‚â´ œÉ ‚ü®j, hj‚ü© = œÉ ‚ü®j + 1, by cutsat‚ü© ‚â´ œÉ ‚ü®i, hi‚ü© :=
  œÉ_comp_œÉ (n := n) (i := ‚ü®i, by cutsat‚ü©) (j := ‚ü®j, by cutsat‚ü©) (by simpa)

end SimplicialIdentities

/-- The canonical functor from `SimplexCategoryGenRel` to SimplexCategory, which exists as the
simplicial identities hold in `SimplexCategory`. -/
def toSimplexCategory : SimplexCategoryGenRel ‚•§ SimplexCategory :=
  CategoryTheory.Quotient.lift _
    (Paths.lift
      { obj := .mk
        map f := match f with
          | FreeSimplexQuiver.Hom.Œ¥ i => SimplexCategory.Œ¥ i
          | FreeSimplexQuiver.Hom.œÉ i => SimplexCategory.œÉ i })
    (fun _ _ _ _ h ‚Ü¶ match h with
      | .Œ¥_comp_Œ¥ H => SimplexCategory.Œ¥_comp_Œ¥ H
      | .Œ¥_comp_œÉ_of_le H => SimplexCategory.Œ¥_comp_œÉ_of_le H
      | .Œ¥_comp_œÉ_self => SimplexCategory.Œ¥_comp_œÉ_self
      | .Œ¥_comp_œÉ_succ => SimplexCategory.Œ¥_comp_œÉ_succ
      | .Œ¥_comp_œÉ_of_gt H => SimplexCategory.Œ¥_comp_œÉ_of_gt H
      | .œÉ_comp_œÉ H => SimplexCategory.œÉ_comp_œÉ H)

@[simp]
lemma toSimplexCategory_obj_mk (n : ‚Ñï) : toSimplexCategory.obj (mk n) = .mk n := rfl

@[simp]
lemma toSimplexCategory_map_Œ¥ {n : ‚Ñï} (i : Fin (n + 2)) :
    toSimplexCategory.map (Œ¥ i) = SimplexCategory.Œ¥ i := rfl

@[simp]
lemma toSimplexCategory_map_œÉ {n : ‚Ñï} (i : Fin (n + 1)) :
    toSimplexCategory.map (œÉ i) = SimplexCategory.œÉ i := rfl

@[simp]
lemma toSimplexCategory_len {x : SimplexCategoryGenRel} : (toSimplexCategory.obj x).len = x.len :=
  rfl

end SimplexCategoryGenRel
