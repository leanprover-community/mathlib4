/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Basic
import Mathlib.CategoryTheory.PathCategory.Basic
/-! # Presentation of the simplex category by generators and relations.

We introduce `SimplexCategoryGenRel` as the category presented by generating
morphisms `Î´ i : [n] âŸ¶ [n + 1]` and `Ïƒ i : [n + 1] âŸ¶ [n]` and subject to the
simplicial identities, and we provide induction principles for reasoning about
objects and morphisms in this category.

This category admits a canonical functor `toSimplexCategory` to the usual simplex category.
The fact that this functor is an equivalence will be recorded in a separate file.
-/
open CategoryTheory

/-- The objects of the free simplex quiver are the natural numbers. -/
def FreeSimplexQuiver := â„•

/-- Making an object of `FreeSimplexQuiver` out of a natural number. -/
def FreeSimplexQuiver.mk (n : â„•) : FreeSimplexQuiver := n

/-- Getting back the natural number from the objects. -/
def FreeSimplexQuiver.len (x : FreeSimplexQuiver) : â„• := x

namespace FreeSimplexQuiver

/-- A morphism in `FreeSimplexQuiver` is either a face map (`Î´`) or a degeneracy map (`Ïƒ`). -/
inductive Hom : FreeSimplexQuiver â†’ FreeSimplexQuiver â†’ Type
  | Î´ {n : â„•} (i : Fin (n + 2)) : Hom (.mk n) (.mk (n + 1))
  | Ïƒ {n : â„•} (i : Fin (n + 1)) : Hom (.mk (n + 1)) (.mk n)

instance quiv : Quiver FreeSimplexQuiver where
  Hom := FreeSimplexQuiver.Hom

/-- `FreeSimplexQuiver.Î´ i` represents the `i`-th face map `.mk n âŸ¶ .mk (n + 1)`. -/
abbrev Î´ {n : â„•} (i : Fin (n + 2)) : FreeSimplexQuiver.mk n âŸ¶ .mk (n + 1) :=
  FreeSimplexQuiver.Hom.Î´ i

/-- `FreeSimplexQuiver.Ïƒ i` represents `i`-th degeneracy map `.mk (n + 1) âŸ¶ .mk n`. -/
abbrev Ïƒ {n : â„•} (i : Fin (n + 1)) : FreeSimplexQuiver.mk (n + 1) âŸ¶ .mk n :=
  FreeSimplexQuiver.Hom.Ïƒ i

/-- `FreeSimplexQuiver.homRel` is the relation on morphisms freely generated on the
five simplicial identities. -/
inductive homRel : HomRel (Paths FreeSimplexQuiver)
  | Î´_comp_Î´ {n : â„•} {i j : Fin (n + 2)} (H : i â‰¤ j) : homRel
    (Paths.of.map (Î´ i) â‰« Paths.of.map (Î´ j.succ))
    (Paths.of.map (Î´ j) â‰« Paths.of.map (Î´ i.castSucc))
  | Î´_comp_Ïƒ_of_le {n : â„•} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ j.castSucc) : homRel
    (Paths.of.map (Î´ i.castSucc) â‰« Paths.of.map (Ïƒ j.succ))
    (Paths.of.map (Ïƒ j) â‰« Paths.of.map (Î´ i))
  | Î´_comp_Ïƒ_self {n : â„•} {i : Fin (n + 1)} : homRel
    (Paths.of.map (Î´ i.castSucc) â‰« Paths.of.map (Ïƒ i)) (ğŸ™ _)
  | Î´_comp_Ïƒ_succ {n : â„•} {i : Fin (n + 1)} : homRel
    (Paths.of.map (Î´ i.succ) â‰« Paths.of.map (Ïƒ i)) (ğŸ™ _)
  | Î´_comp_Ïƒ_of_gt {n : â„•} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) : homRel
    (Paths.of.map (Î´ i.succ) â‰« Paths.of.map (Ïƒ j.castSucc))
    (Paths.of.map (Ïƒ j) â‰« Paths.of.map (Î´ i))
  | Ïƒ_comp_Ïƒ {n : â„•} {i j : Fin (n + 1)} (H : i â‰¤ j) : homRel
    (Paths.of.map (Ïƒ i.castSucc) â‰« Paths.of.map (Ïƒ j))
    (Paths.of.map (Ïƒ j.succ) â‰« Paths.of.map (Ïƒ i))

end FreeSimplexQuiver

/-- SimplexCategory is the category presented by generators and relation by the simplicial
identities. -/
def SimplexCategoryGenRel := Quotient FreeSimplexQuiver.homRel
  deriving Category

/-- `SimplexCategoryGenRel.mk` is the main constructor for objects of `SimplexCategoryGenRel`. -/
def SimplexCategoryGenRel.mk (n : â„•) : SimplexCategoryGenRel where
  as := Paths.of.obj n

namespace SimplexCategoryGenRel

/-- `SimplexCategoryGenRel.Î´ i` is the `i`-th face map `.mk n âŸ¶ .mk (n + 1)`. -/
abbrev Î´ {n : â„•} (i : Fin (n + 2)) : mk n âŸ¶ mk (n + 1) :=
  (Quotient.functor FreeSimplexQuiver.homRel).map <| Paths.of.map (.Î´ i)

/-- `SimplexCategoryGenRel.Ïƒ i` is the `i`-th degeneracy map `.mk (n + 1) âŸ¶ .mk n`. -/
abbrev Ïƒ {n : â„•} (i : Fin (n + 1)) : mk (n + 1) âŸ¶ mk n :=
  (Quotient.functor FreeSimplexQuiver.homRel).map <| Paths.of.map (.Ïƒ i)

/-- The length of an object of `SimplexCategoryGenRel`. -/
def len (x : SimplexCategoryGenRel) : â„• := by rcases x with âŸ¨nâŸ©; exact n

@[simp]
lemma mk_len (n : â„•) : len (mk n) = n := rfl

section InductionPrinciples

/-- A morphism is called a face if it is a `Î´ i` for some `i : Fin (n + 2)`. -/
inductive faces : MorphismProperty SimplexCategoryGenRel
  | Î´ {n : â„•} (i : Fin (n + 2)) : faces (Î´ i)

/-- A morphism is called a degeneracy if it is a `Ïƒ i` for some `i : Fin (n + 1)`. -/
inductive degeneracies : MorphismProperty SimplexCategoryGenRel
  | Ïƒ {n : â„•} (i : Fin (n + 1)) : degeneracies (Ïƒ i)

/-- A morphism is a generator if it is either a face or a degeneracy. -/
abbrev generators := faces âŠ” degeneracies

namespace generators

lemma Î´ {n : â„•} (i : Fin (n + 2)) : generators (Î´ i) := le_sup_left (a := faces) _ (.Î´ i)

lemma Ïƒ {n : â„•} (i : Fin (n + 1)) : generators (Ïƒ i) := le_sup_right (a := faces) _ (.Ïƒ i)

end generators

/-- A property is true for every morphism iff it holds for generators and is multiplicative. -/
lemma multiplicativeClosure_isGenerator_eq_top : generators.multiplicativeClosure = âŠ¤ := by
  apply le_antisymm (by simp)
  intro x y f _
  apply CategoryTheory.Quotient.induction
  apply Paths.induction
  Â· exact generators.multiplicativeClosure.id_mem _
  Â· rintro _ _ _ _ âŸ¨âŸ© h
    Â· exact generators.multiplicativeClosure.comp_mem _ _ h <| .of _ <| .Î´ _
    Â· exact generators.multiplicativeClosure.comp_mem _ _ h <| .of _ <| .Ïƒ _

/-- An unrolled version of the induction principle obtained in the previous lemma. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
lemma hom_induction (P : MorphismProperty SimplexCategoryGenRel)
    (id : âˆ€ {n : â„•}, P (ğŸ™ (mk n)))
    (comp_Î´ : âˆ€ {n m : â„•} (u : mk n âŸ¶ mk m) (i : Fin (m + 2)), P u â†’ P (u â‰« Î´ i))
    (comp_Ïƒ : âˆ€ {n m : â„•} (u : mk n âŸ¶ mk (m + 1)) (i : Fin (m + 1)), P u â†’ P (u â‰« Ïƒ i))
    {a b : SimplexCategoryGenRel} (f : a âŸ¶ b) :
    P f :=
  by
  suffices generators.multiplicativeClosure â‰¤ P by
    rw [multiplicativeClosure_isGenerator_eq_top, top_le_iff] at this
    rw [this]
    apply MorphismProperty.top_apply
  intro _ _ f hf
  induction hf with
  | of f h =>
    rcases h with âŸ¨âŸ¨iâŸ©âŸ© | âŸ¨âŸ¨iâŸ©âŸ©
    Â· simpa using (comp_Î´ (ğŸ™ _) i id)
    Â· simpa using (comp_Ïƒ (ğŸ™ _) i id)
  | id n => exact id
  | comp_of f g hf hg hrec =>
    rcases hg with âŸ¨âŸ¨iâŸ©âŸ© | âŸ¨âŸ¨iâŸ©âŸ©
    Â· simpa using (comp_Î´ f i hrec)
    Â· simpa using (comp_Ïƒ f i hrec)

/-- An induction principle for reasonning about morphisms in SimplexCategoryGenRel, where we compose
with generators on the right. -/
lemma hom_induction' (P : MorphismProperty SimplexCategoryGenRel)
    (id : âˆ€ {n : â„•}, P (ğŸ™ (mk n)))
    (Î´_comp : âˆ€ {n m : â„•} (u : mk (m + 1) âŸ¶ mk n)
      (i : Fin (m + 2)), P u â†’ P (Î´ i â‰« u))
    (Ïƒ_comp : âˆ€ {n m : â„•} (u : mk m âŸ¶ mk n)
      (i : Fin (m + 1)), P u â†’ P (Ïƒ i â‰« u )) {a b : SimplexCategoryGenRel} (f : a âŸ¶ b) :
    P f := by
  suffices generators.multiplicativeClosure' â‰¤ P by
    rw [â† MorphismProperty.multiplicativeClosure_eq_multiplicativeClosure',
      multiplicativeClosure_isGenerator_eq_top, top_le_iff] at this
    rw [this]
    apply MorphismProperty.top_apply
  intro _ _ f hf
  induction hf with
  | of f h =>
    rcases h with âŸ¨âŸ¨iâŸ©âŸ© | âŸ¨âŸ¨iâŸ©âŸ©
    Â· simpa using (Î´_comp (ğŸ™ _) i id)
    Â· simpa using (Ïƒ_comp (ğŸ™ _) i id)
  | id n => exact id
  | of_comp f g hf hg hrec =>
    rcases hf with âŸ¨âŸ¨iâŸ©âŸ© | âŸ¨âŸ¨iâŸ©âŸ©
    Â· simpa using (Î´_comp g i hrec)
    Â· simpa using (Ïƒ_comp g i hrec)

/-- An induction principle for reasonning about objects in `SimplexCategoryGenRel`. This should be
used instead of identifying an object with `mk` of its `len`. -/
@[elab_as_elim, cases_eliminator]
protected def rec {P : SimplexCategoryGenRel â†’ Sort*}
    (H : âˆ€ n : â„•, P (.mk n)) :
    âˆ€ x : SimplexCategoryGenRel, P x := by
  intro x
  exact H x.len

/-- A basic `ext` lemma for objects of `SimplexCategoryGenRel`. -/
@[ext]
lemma ext {x y : SimplexCategoryGenRel} (h : x.len = y.len) : x = y := by
  cases x
  cases y
  simp only [mk_len] at h
  congr

end InductionPrinciples

section SimplicialIdentities

@[reassoc]
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    Î´ i â‰« Î´ j.succ = Î´ j â‰« Î´ i.castSucc := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Î´_comp_Î´ H

@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ j.castSucc) :
    Î´ i.castSucc â‰« Ïƒ j.succ = Ïƒ j â‰« Î´ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Î´_comp_Ïƒ_of_le H

@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} :
    Î´ i.castSucc â‰« Ïƒ i = ğŸ™ (mk n) := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Î´_comp_Ïƒ_self

@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : Î´ i.succ â‰« Ïƒ i = ğŸ™ (mk n) := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Î´_comp_Ïƒ_succ

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) :
    Î´ i.succ â‰« Ïƒ j.castSucc = Ïƒ j â‰« Î´ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Î´_comp_Ïƒ_of_gt H

@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    Ïƒ i.castSucc â‰« Ïƒ j = Ïƒ j.succ â‰« Ïƒ i := by
  apply CategoryTheory.Quotient.sound
  exact FreeSimplexQuiver.homRel.Ïƒ_comp_Ïƒ H

/-- A version of Î´_comp_Î´ with indices in â„• satisfying relevant inequalities. -/
lemma Î´_comp_Î´_nat {n} (i j : â„•) (hi : i < n + 2) (hj : j < n + 2) (H : i â‰¤ j) :
    Î´ âŸ¨i, hiâŸ© â‰« Î´ âŸ¨j + 1, by omegaâŸ© = Î´ âŸ¨j, hjâŸ© â‰« Î´ âŸ¨i, by omegaâŸ© :=
  Î´_comp_Î´ (n := n) (i := âŸ¨i, by omegaâŸ©) (j := âŸ¨j, by omegaâŸ©) (by simpa)

/-- A version of Ïƒ_comp_Ïƒ with indices in â„• satisfying relevant inequalities. -/
lemma Ïƒ_comp_Ïƒ_nat {n} (i j : â„•) (hi : i < n + 1) (hj : j < n + 1) (H : i â‰¤ j) :
    Ïƒ âŸ¨i, by omegaâŸ© â‰« Ïƒ âŸ¨j, hjâŸ© = Ïƒ âŸ¨j + 1, by omegaâŸ© â‰« Ïƒ âŸ¨i, hiâŸ© :=
  Ïƒ_comp_Ïƒ (n := n) (i := âŸ¨i, by omegaâŸ©) (j := âŸ¨j, by omegaâŸ©) (by simpa)

end SimplicialIdentities

/-- The canonical functor from `SimplexCategoryGenRel` to SimplexCategory, which exists as the
simplicial identities hold in `SimplexCategory`. -/
def toSimplexCategory : SimplexCategoryGenRel â¥¤ SimplexCategory :=
  CategoryTheory.Quotient.lift _
    (Paths.lift
      { obj := .mk
        map f := match f with
          | FreeSimplexQuiver.Hom.Î´ i => SimplexCategory.Î´ i
          | FreeSimplexQuiver.Hom.Ïƒ i => SimplexCategory.Ïƒ i })
    (fun _ _ _ _ h â†¦ match h with
      | .Î´_comp_Î´ H => SimplexCategory.Î´_comp_Î´ H
      | .Î´_comp_Ïƒ_of_le H => SimplexCategory.Î´_comp_Ïƒ_of_le H
      | .Î´_comp_Ïƒ_self => SimplexCategory.Î´_comp_Ïƒ_self
      | .Î´_comp_Ïƒ_succ => SimplexCategory.Î´_comp_Ïƒ_succ
      | .Î´_comp_Ïƒ_of_gt H => SimplexCategory.Î´_comp_Ïƒ_of_gt H
      | .Ïƒ_comp_Ïƒ H => SimplexCategory.Ïƒ_comp_Ïƒ H)

@[simp]
lemma toSimplexCategory_obj_mk (n : â„•) : toSimplexCategory.obj (mk n) = .mk n := rfl

@[simp]
lemma toSimplexCategory_map_Î´ {n : â„•} (i : Fin (n + 2)) :
    toSimplexCategory.map (Î´ i) = SimplexCategory.Î´ i := rfl

@[simp]
lemma toSimplexCategory_map_Ïƒ {n : â„•} (i : Fin (n + 1)) :
    toSimplexCategory.map (Ïƒ i) = SimplexCategory.Ïƒ i := rfl

@[simp]
lemma toSimplexCategory_len {x : SimplexCategoryGenRel} : (toSimplexCategory.obj x).len = x.len :=
  rfl

end SimplexCategoryGenRel
