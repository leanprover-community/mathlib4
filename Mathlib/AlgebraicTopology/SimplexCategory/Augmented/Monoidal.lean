/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Augmented.Basic
import Mathlib.CategoryTheory.Monoidal.Category

/-!
# Monoidal structure on the augmented simplex category

This file defines a monoidal structure on `AugmentedSimplexCategory`.
The tensor product of objects is characterized by the fact that the initial object `star` is
also the unit, and the fact that `â¦‹mâ¦Œ âŠ— â¦‹nâ¦Œ = â¦‹m + n + 1â¦Œ` for `n m : â„•`.

Through the (not in mathlib) equivalence between `AugmentedSimplexCategory` and the category
of finite ordinals, the tensor products corresponds to ordinal sum.

As the unit of this structure is an initial object, for every `x y : AugmentedSimplexCategory`,
there are maps `AugmentedSimplexCategory.inl x y : x âŸ¶ x âŠ— y` and
`AugmentedSimplexCategory.inr x y : y âŸ¶ x âŠ— y`. The main API for working with the tensor product
of maps is given by  `AugmentedSimplexCategory.tensorObj_hom_ext`, which characterizes maps
`x âŠ— y âŸ¶ z` in terms of their composition with these two maps. We also characterize the behaviour
of the associator isomorphism with respect to these maps.

-/

namespace AugmentedSimplexCategory

attribute [local aesop safe cases (rule_sets := [CategoryTheory])] CategoryTheory.WithInitial

open CategoryTheory MonoidalCategory
open scoped Simplicial

@[simp]
lemma eqToHom_toOrderHom {x y : SimplexCategory} (h : WithInitial.of x = WithInitial.of y) :
    SimplexCategory.Hom.toOrderHom (WithInitial.down <| eqToHom h) =
      (Fin.castOrderIso
        (congrArg (fun t â†¦ t + 1) (by injection h with h; rw [h]))).toOrderEmbedding.toOrderHom :=
  SimplexCategory.eqToHom_toOrderHom (by injection h)

/-- An auxiliary definition for the tensor product of two objects in `AugmentedSimplexCategory`. -/
-- (Impl. note): This definition could easily be inlined in
-- the definition of `tensorObjOf` below, but having it type check directly as an element
-- of `SimplexCategory` avoids having to sprinkle `WithInitial.down` everywhere.
abbrev tensorObjOf (m n : SimplexCategory) : SimplexCategory := .mk (m.len + n.len + 1)

/-- The tensor product of two objects of `AugmentedSimplexCategory`. -/
def tensorObj (m n : AugmentedSimplexCategory) : AugmentedSimplexCategory :=
  match m, n with
  | .of m, .of n => .of <| tensorObjOf m n
  | .star, x => x
  | x, .star => x

/-- The action of the tensor product on maps coming from `SimplexCategory`. -/
def tensorHomOf {xâ‚ yâ‚ xâ‚‚ yâ‚‚ : SimplexCategory} (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) :
    tensorObjOf xâ‚ xâ‚‚ âŸ¶ tensorObjOf yâ‚ yâ‚‚ :=
  letI fâ‚ : Fin ((xâ‚.len + 1) + (xâ‚‚.len + 1)) â†’o Fin ((yâ‚.len + 1) + (yâ‚‚.len + 1)) :=
    { toFun i :=
        Fin.addCases
          (motive := fun _ â†¦ Fin <| (yâ‚.len + 1) + (yâ‚‚.len + 1))
          (fun i â†¦ (fâ‚.toOrderHom i).castAdd _)
          (fun i â†¦ (fâ‚‚.toOrderHom i).natAdd _)
          i
      monotone' i j h := by
        cases i using Fin.addCases <;>
        cases j using Fin.addCases <;>
        rw [Fin.le_def] at h âŠ¢ <;>
        simp [Fin.coe_castAdd, Fin.coe_natAdd, Fin.addCases_left,
          Fin.addCases_right] at h âŠ¢
        Â· case left.left i j => exact fâ‚.toOrderHom.monotone h
        Â· case left.right i j => omega
        Â· case right.left i j => omega
        Â· case right.right i j => exact fâ‚‚.toOrderHom.monotone h }
  (eqToHom (congrArg _ (Nat.succ_add _ _)).symm â‰« (SimplexCategory.mkHom fâ‚) â‰«
    eqToHom (congrArg _ (Nat.succ_add _ _)) : _ âŸ¶ â¦‹yâ‚.len + yâ‚‚.len + 1â¦Œ)

/-- The action of the tensor product on maps of `AugmentedSimplexCategory`. -/
def tensorHom {xâ‚ yâ‚ xâ‚‚ yâ‚‚ : AugmentedSimplexCategory} (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) :
    tensorObj xâ‚ xâ‚‚ âŸ¶ tensorObj yâ‚ yâ‚‚ :=
  match xâ‚, yâ‚, xâ‚‚, yâ‚‚, fâ‚, fâ‚‚ with
  | .of _, .of _, .of _, .of _, fâ‚, fâ‚‚ => tensorHomOf fâ‚ fâ‚‚
  | .of _, .of yâ‚, .star, .of yâ‚‚, fâ‚, _ =>
    fâ‚ â‰« ((SimplexCategory.mkHom <| (Fin.castAddOrderEmb (yâ‚‚.len + 1)).toOrderHom) â‰«
      eqToHom (congrArg _ (Nat.succ_add _ _)) : â¦‹yâ‚.lenâ¦Œ âŸ¶ â¦‹yâ‚.len + yâ‚‚.len + 1â¦Œ)
  | .star, .of yâ‚, .of _, .of yâ‚‚, _, fâ‚‚ =>
    fâ‚‚ â‰« ((SimplexCategory.mkHom <| (Fin.natAddOrderEmb (yâ‚.len + 1)).toOrderHom) â‰«
      eqToHom (congrArg _ (Nat.succ_add _ _)) : â¦‹yâ‚‚.lenâ¦Œ âŸ¶ â¦‹yâ‚.len + yâ‚‚.len + 1â¦Œ)
  | .star, .star, .of _, .of _, _, fâ‚‚ => fâ‚‚
  | .of _, .of _, .star, .star, fâ‚, _ => fâ‚
  | .star, _, .star, _, _, _ => WithInitial.starInitial.to _

/-- The unit for the monoidal structure on `AugmentedSimplexCategory` is the initial object. -/
abbrev tensorUnit : AugmentedSimplexCategory := WithInitial.star

/-- The associator isomorphism for the monoidal structure on `AugmentedSimplexCategory` -/
def associator (x y z : AugmentedSimplexCategory) :
    tensorObj (tensorObj x y) z â‰… tensorObj x (tensorObj y z) :=
  match x, y, z with
  | .of x, .of y, .of z =>
    eqToIso (congrArg (fun j â†¦ WithInitial.of <| SimplexCategory.mk j)
      (by simp +arith))
  | .star, .star, .star => Iso.refl _
  | .star, .of _, .star => Iso.refl _
  | .star, .star, .of _ => Iso.refl _
  | .star, .of _, .of _ => Iso.refl _
  | .of _, .star, .star => Iso.refl _
  | .of _, .star, .of _ => Iso.refl _
  | .of _, .of _, .star => Iso.refl _

/-- The left unitor isomorphism for the monoidal structure in `AugmentedSimplexCategory` -/
def leftUnitor (x : AugmentedSimplexCategory) :
    tensorObj tensorUnit x â‰… x :=
  match x with
  | .of _ => Iso.refl _
  | .star => Iso.refl _

/-- The right unitor isomorphism for the monoidal structure in `AugmentedSimplexCategory` -/
def rightUnitor (x : AugmentedSimplexCategory) :
    tensorObj x tensorUnit â‰… x :=
  match x with
  | .of _ => Iso.refl _
  | .star => Iso.refl _

instance : MonoidalCategoryStruct AugmentedSimplexCategory where
  tensorObj := tensorObj
  tensorHom := tensorHom
  tensorUnit := tensorUnit
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor
  whiskerLeft x _ _ f := tensorHom (ğŸ™ x) f
  whiskerRight f x := tensorHom f (ğŸ™ x)

@[local simp]
lemma id_tensorHom (x : AugmentedSimplexCategory) {yâ‚ yâ‚‚ : AugmentedSimplexCategory}
    (f : yâ‚ âŸ¶ yâ‚‚) : ğŸ™ x âŠ—â‚˜ f = x â— f :=
  rfl

@[local simp]
lemma tensorHom_id {xâ‚ xâ‚‚ : AugmentedSimplexCategory} (y : AugmentedSimplexCategory)
    (f : xâ‚ âŸ¶ xâ‚‚) : f âŠ—â‚˜ ğŸ™ y = f â–· y :=
  rfl

@[local simp]
lemma whiskerLeft_id_star {x : AugmentedSimplexCategory} : x â— ğŸ™ .star = ğŸ™ _ := by
  cases x <;>
  rfl

@[local simp]
lemma id_star_whiskerRight {x : AugmentedSimplexCategory} : ğŸ™ WithInitial.star â–· x = ğŸ™ _ := by
  cases x <;>
  rfl

/-- Thanks to `tensorUnit` being initial in `AugmentedSimplexCategory`, we get
a morphism `Î” âŸ¶ Î” âŠ— Î”'` for every pair of objects `Î”, Î”'`. -/
def inl (x y : AugmentedSimplexCategory) : x âŸ¶ x âŠ— y :=
  (Ï_ x).inv â‰« _ â— (WithInitial.starInitial.to y)

/-- Thanks to `tensorUnit` being initial in `AugmentedSimplexCategory`, we get
a morphism `Î”' âŸ¶ Î” âŠ— Î”'` for every pair of objects `Î”, Î”'`. -/
def inr (x y : AugmentedSimplexCategory) : y âŸ¶ x âŠ— y :=
  (Î»_ y).inv â‰« (WithInitial.starInitial.to x) â–· _

/-- To ease type checking, we also provide a version of inl that lives in
`SimplexCategory`. -/
abbrev inl' (x y : SimplexCategory) : x âŸ¶ tensorObjOf x y := WithInitial.down <| inl (.of x) (.of y)

/-- To ease type checking, we also provide a version of inr that lives in
`SimplexCategory`. -/
abbrev inr' (x y : SimplexCategory) : y âŸ¶ tensorObjOf x y := WithInitial.down <| inr (.of x) (.of y)

lemma inl'_eval (x y : SimplexCategory) (i : Fin (x.len + 1)) :
    (inl' x y).toOrderHom i = (i.castAdd _).cast (Nat.succ_add x.len (y.len + 1)) := by
  dsimp [inl', inl, MonoidalCategoryStruct.rightUnitor, MonoidalCategoryStruct.whiskerLeft,
    tensorHom, WithInitial.down, rightUnitor, tensorObj]
  ext
  simp [OrderEmbedding.toOrderHom]

lemma inr'_eval (x y : SimplexCategory) (i : Fin (y.len + 1)) :
    (inr' x y).toOrderHom i = (i.natAdd _).cast (Nat.succ_add x.len (y.len + 1)) := by
  dsimp [inr', inr, MonoidalCategoryStruct.leftUnitor, MonoidalCategoryStruct.whiskerRight,
    tensorHom, WithInitial.down, leftUnitor, tensorObj]
  ext
  simp [OrderEmbedding.toOrderHom]

/-- We can characterize morphisms out of a tensor product via their precomposition with `inl` and
`inr`. -/
@[ext]
theorem tensorObj_hom_ext {x y z : AugmentedSimplexCategory} (f g : x âŠ— y âŸ¶ z)
    (hâ‚ : inl _ _ â‰« f = inl _ _ â‰« g)
    (hâ‚‚ : inr _ _ â‰« f = inr _ _ â‰« g) : f = g :=
  match x, y, z, f, g with
  | .of x, .of y, .of z, f, g => by
    change (tensorObjOf x y) âŸ¶ z at f g
    change inl' _ _ â‰« f = inl' _ _ â‰« g at hâ‚
    change inr' _ _ â‰« f = inr' _ _ â‰« g at hâ‚‚
    ext i
    let j : Fin ((x.len + 1) + (y.len + 1)) := i.cast (Nat.succ_add x.len (y.len + 1)).symm
    have : i = j.cast (Nat.succ_add x.len (y.len + 1)) := rfl
    rw [this]
    cases j using Fin.addCases (m := x.len + 1) (n := y.len + 1) with
    | left j =>
      rw [SimplexCategory.Hom.ext_iff, OrderHom.ext_iff] at hâ‚
      simpa [â† inl'_eval, ConcreteCategory.hom, Fin.ext_iff] using congrFun hâ‚ j
    | right j =>
      rw [SimplexCategory.Hom.ext_iff, OrderHom.ext_iff] at hâ‚‚
      simpa [â† inr'_eval, ConcreteCategory.hom, Fin.ext_iff] using congrFun hâ‚‚ j
  | .of x, .star, .of z, f, g => by
      simp only [inl, Category.assoc, Iso.cancel_iso_inv_left, Limits.IsInitial.to_self,
        whiskerLeft_id_star] at hâ‚
      simpa [Category.id_comp f, Category.id_comp g] using hâ‚
  | .star, .of y, .of z, f, g => by
      simp only [inr, Category.assoc, Iso.cancel_iso_inv_left, Limits.IsInitial.to_self,
        id_star_whiskerRight] at hâ‚‚
      simpa [Category.id_comp f, Category.id_comp g] using hâ‚‚
  | .star, .star, .of z, f, g => rfl
  | .star, .star, .star, f, g => rfl

@[reassoc (attr := simp)]
lemma inl_comp_tensorHom {xâ‚ yâ‚ xâ‚‚ yâ‚‚ : AugmentedSimplexCategory}
    (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) : inl xâ‚ xâ‚‚ â‰« (fâ‚ âŠ—â‚˜ fâ‚‚) = fâ‚ â‰« inl yâ‚ yâ‚‚ :=
  match xâ‚, yâ‚, xâ‚‚, yâ‚‚, fâ‚, fâ‚‚ with
  | .of xâ‚, .of yâ‚, .of xâ‚‚, .of yâ‚‚, fâ‚, fâ‚‚ => by
    change inl' _ _ â‰« tensorHomOf _ _ = WithInitial.down fâ‚ â‰« inl' _ _
    ext i : 3
    dsimp [tensorHomOf]
    have eâ‚ := inl'_eval xâ‚ xâ‚‚ i
    have eâ‚‚ := inl'_eval yâ‚ yâ‚‚ <| (WithInitial.down fâ‚).toOrderHom i
    simp only [SimplexCategory.len_mk] at eâ‚ eâ‚‚
    rw [eâ‚, eâ‚‚]
    simp only [SimplexCategory.eqToHom_toOrderHom, SimplexCategory.len_mk,
      OrderEmbedding.toOrderHom_coe, OrderIso.coe_toOrderEmbedding, Fin.castOrderIso_apply,
      Fin.cast_cast, Fin.cast_eq_self, Fin.cast_inj]
    conv_lhs =>
      change Fin.addCases
        (fun i â†¦ Fin.castAdd (yâ‚‚.len + 1) (fâ‚.toOrderHom i))
        (fun i â†¦ Fin.natAdd (yâ‚.len + 1) (fâ‚‚.toOrderHom i))
        (Fin.castAdd (xâ‚‚.len + 1) i)
      rw [Fin.addCases_left]
    rfl
  | _, _, .star, _, fâ‚, fâ‚‚ => by cat_disch
  | .star, _, _, _, _, _ => rfl

@[reassoc (attr := simp)]
lemma inr_comp_tensorHom {xâ‚ yâ‚ xâ‚‚ yâ‚‚ : AugmentedSimplexCategory}
    (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) : inr xâ‚ xâ‚‚ â‰« (fâ‚ âŠ—â‚˜ fâ‚‚) = fâ‚‚ â‰« inr yâ‚ yâ‚‚ :=
  match xâ‚, yâ‚, xâ‚‚, yâ‚‚, fâ‚, fâ‚‚ with
  | .of xâ‚, .of yâ‚, .of xâ‚‚, .of yâ‚‚, fâ‚, fâ‚‚ => by
    change inr' _ _ â‰« tensorHomOf _ _ = WithInitial.down fâ‚‚ â‰« inr' _ _
    ext i : 3
    dsimp [tensorHomOf]
    have eâ‚ := inr'_eval xâ‚ xâ‚‚ i
    have eâ‚‚ := inr'_eval yâ‚ yâ‚‚ <| (WithInitial.down fâ‚‚).toOrderHom i
    simp only [SimplexCategory.len_mk] at eâ‚ eâ‚‚
    rw [eâ‚, eâ‚‚]
    simp only [SimplexCategory.eqToHom_toOrderHom, SimplexCategory.len_mk,
      Nat.succ_eq_add_one, OrderEmbedding.toOrderHom_coe,
      OrderIso.coe_toOrderEmbedding, Fin.castOrderIso_apply,
      Fin.cast_cast, Fin.cast_eq_self, Fin.cast_inj]
    conv_lhs =>
      change Fin.addCases
        (fun i â†¦ Fin.castAdd (yâ‚‚.len + 1) (fâ‚.toOrderHom i))
        (fun i â†¦ Fin.natAdd (yâ‚.len + 1) (fâ‚‚.toOrderHom i))
        (Fin.natAdd (xâ‚.len + 1) i)
      rw [Fin.addCases_right]
    rfl
  | .star, _, _, _, fâ‚, fâ‚‚ => by cat_disch
  | _, _, .star, _, _, _ => rfl

@[reassoc (attr := simp)]
lemma inr_comp_associator (x y z : AugmentedSimplexCategory) :
    inr _ _ â‰« (Î±_ x y z).hom = inr _ _ â‰« inr _ _ :=
  match x, y, z with
  | .of x, .of y, .of z => by
    change inr' _ _ â‰« WithInitial.down _ = inr' _ _ â‰« inr' _ _
    ext i : 3
    dsimp [MonoidalCategoryStruct.associator, associator]
    simp only [eqToHom_toOrderHom, SimplexCategory.len_mk, OrderEmbedding.toOrderHom_coe,
      OrderIso.coe_toOrderEmbedding, Fin.castOrderIso_apply]
    have eâ‚ := inr'_eval (tensorObjOf x y) z i
    have eâ‚‚ := inr'_eval y z i
    have eâ‚ƒ := inr'_eval x (tensorObjOf y z) <|
      Fin.cast (by simp +arith) <| i.natAdd (y.len + 1)
    simp only [SimplexCategory.len_mk] at eâ‚ eâ‚‚ eâ‚ƒ
    rw [eâ‚, eâ‚‚, eâ‚ƒ]
    ext; simp +arith
  | .star, _, _ => by cat_disch
  | _, .star, _ => by cat_disch
  | _, _, .star => by cat_disch

@[reassoc (attr := simp)]
lemma inl_comp_inl_comp_associator (x y z : AugmentedSimplexCategory) :
    inl _ _ â‰« inl _ _ â‰« (Î±_ x y z).hom = inl _ _ :=
  match x, y, z with
  | .of x, .of y, .of z => by
    change inl' _ _ â‰« inl' _ _ â‰« WithInitial.down _ = inl' _ _
    ext i : 3
    dsimp [MonoidalCategoryStruct.associator, associator]
    have eâ‚ := inl'_eval x y i
    have eâ‚‚ := inl'_eval x (tensorObjOf y z) i
    have eâ‚ƒ := inl'_eval (tensorObjOf x y) z <| Fin.cast (by simp +arith) <| i.castAdd (y.len + 1)
    simp only [SimplexCategory.len_mk] at eâ‚ eâ‚‚ eâ‚ƒ
    rw [eâ‚, eâ‚‚, eâ‚ƒ]
    ext; simp +arith
  | .star, _, _ => by cat_disch
  | _, .star, _ => by cat_disch
  | _, _, .star => by cat_disch

@[reassoc (attr := simp)]
lemma inr_comp_inl_comp_associator (x y z : AugmentedSimplexCategory) :
    inr _ _ â‰« inl _ _ â‰« (Î±_ x y z).hom = inl _ _ â‰« inr _ _ :=
  match x, y, z with
  | .of x, .of y, .of z => by
    change inr' _ _ â‰« inl' _ _ â‰« WithInitial.down _ = inl' _ _ â‰« inr' _ _
    ext i : 3
    dsimp [MonoidalCategoryStruct.associator, associator]
    have eâ‚ := inl'_eval y z i
    have eâ‚‚ := inr'_eval x y i
    have eâ‚ƒ := inl'_eval (tensorObjOf x y) z <| Fin.cast (by simp +arith) <| i.natAdd (x.len + 1)
    have eâ‚„ := inr'_eval x (tensorObjOf y z) <| Fin.cast (by simp +arith) <| i.castAdd (z.len + 1)
    simp only [SimplexCategory.len_mk] at eâ‚ eâ‚‚ eâ‚ƒ eâ‚„
    rw [eâ‚, eâ‚‚, eâ‚ƒ, eâ‚„]
    ext; simp +arith
  | .star, _, _ => by cat_disch
  | _, .star, _ => by cat_disch
  | _, _, .star => by cat_disch

theorem tensorHom_comp_tensorHom {xâ‚ yâ‚ zâ‚ xâ‚‚ yâ‚‚ zâ‚‚ : AugmentedSimplexCategory}
    (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) (gâ‚ : yâ‚ âŸ¶ zâ‚) (gâ‚‚ : yâ‚‚ âŸ¶ zâ‚‚) :
    (fâ‚ âŠ—â‚˜ fâ‚‚) â‰« (gâ‚ âŠ—â‚˜ gâ‚‚) = (fâ‚ â‰« gâ‚) âŠ—â‚˜ (fâ‚‚ â‰« gâ‚‚) := by
  cat_disch

theorem tensor_id (x y : AugmentedSimplexCategory) : (ğŸ™ x) âŠ—â‚˜ (ğŸ™ y) = ğŸ™ (x âŠ— y) := by
  ext
  Â· simpa [inl, MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.whiskerRight] using
      (tensorHom_comp_tensorHom (ğŸ™ x) (WithInitial.starInitial.to y) (ğŸ™ x) (ğŸ™ y))
  Â· simpa [inr, MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.whiskerRight] using
      (tensorHom_comp_tensorHom (WithInitial.starInitial.to x) (ğŸ™ y) (ğŸ™ x) (ğŸ™ y))

instance : MonoidalCategory AugmentedSimplexCategory :=
  MonoidalCategory.ofTensorHom
    (id_tensorHom_id := tensor_id)
    (tensorHom_comp_tensorHom := tensorHom_comp_tensorHom)
    (pentagon := fun w x y z â†¦ by ext <;> simp [-id_tensorHom, -tensorHom_id])

end AugmentedSimplexCategory
