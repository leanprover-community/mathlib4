/-
Copyright (c) 2025 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/

import Mathlib.AlgebraicTopology.SimplexCategory.Augmented.Basic
import Mathlib.CategoryTheory.Monoidal.Category

/-!
# Monoidal structure on the augmented simplex category

This file defines a monoidal structure on `AugmentedSimplexCategory`.
The tensor product of objects is characterized by the fact that the initial object `star` is
also the unit, and the fact that `â¦‹mâ¦Œ âŠ— â¦‹nâ¦Œ = â¦‹m + n + 1â¦Œ` for `n m : â„•`.

Through the (not in mathlib) equivalence between `AugmentedSimplexCategory` and the category
of finite ordinals, the tensor products corresponds to ordinal sum.

As the unit of this structure is an initial object, for every `x y : AugmentedSimplexCategory`,
there are maps `AugmentedSimplexCategory.Ï†â‚ x y : x âŸ¶ x âŠ— y` and
`AugmentedSimplexCategory.Ï†â‚ x y : y âŸ¶ x âŠ— y`. The main API for working with the tensor product
of maps is given by  `AugmentedSimplexCategory.tensorObj_hom_ext`, which characterizes maps
`x âŠ— y âŸ¶ z` in terms of their composition with these two maps. We also characterize the behaviour
of the associator isomorphism with respect to these maps.

-/

namespace AugmentedSimplexCategory

attribute [local aesop safe cases (rule_sets := [CategoryTheory])] CategoryTheory.WithInitial

open CategoryTheory MonoidalCategory
open scoped Simplicial

@[simp]
lemma eqToHom_toOrderHom {x y : SimplexCategory} (h : WithInitial.of x = WithInitial.of y) :
  SimplexCategory.Hom.toOrderHom (WithInitial.down <| eqToHom h) =
    (Fin.castOrderIso
      (congrArg (fun t â†¦ t + 1) (by injection h with h; rw [h]))).toOrderEmbedding.toOrderHom := by
  injection h with h
  subst h
  rfl

/-- An auxiliary definition for the tensor product of two objects in `AugmentedSimplexCategory`. -/
-- (Impl. note): This definition could easily be inlined in
-- the definition of `tensorObjOf` below, but having it type check directly as an element
-- of `SimplexCategory` avoids having to sprinkle `WithInitial.down` everywhere.
abbrev tensorObjOf (m n : SimplexCategory) : SimplexCategory := .mk (m.len + n.len + 1)

/-- The tensor product of two objects of `AugmentedSimplexCategory`. -/
def tensorObj (m n : AugmentedSimplexCategory) : AugmentedSimplexCategory :=
  match m, n with
  | .of m, .of n => .of <| tensorObjOf m n
  | .star, x => x
  | x, .star => x

/-- The action of the tensor product on maps coming from `SimplexCategory`. -/
def tensorHomOf {xâ‚ yâ‚ xâ‚‚ yâ‚‚: SimplexCategory} (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) :
    tensorObjOf xâ‚ xâ‚‚ âŸ¶ tensorObjOf yâ‚ yâ‚‚ :=
  letI fâ‚ : Fin ((xâ‚.len + 1) + (xâ‚‚.len + 1)) â†’o Fin ((yâ‚.len + 1) + (yâ‚‚.len + 1)) :=
    { toFun i :=
        Fin.addCases
          (motive := fun _ â†¦ Fin <| (yâ‚.len + 1) + (yâ‚‚.len + 1))
          (fun i â†¦ (fâ‚.toOrderHom i).castAdd _)
          (fun i â†¦ (fâ‚‚.toOrderHom i).natAdd _)
          i
      monotone' i j h := by
        cases i using Fin.addCases <;>
        cases j using Fin.addCases <;>
        rw [Fin.le_def] at h âŠ¢ <;>
        simp [Fin.coe_castAdd, Fin.coe_natAdd, Fin.addCases_left,
          Fin.addCases_right] at h âŠ¢
        Â· case left.left i j => exact fâ‚.toOrderHom.monotone h
        Â· case left.right i j => omega
        Â· case right.left i j => omega
        Â· case right.right i j => exact fâ‚‚.toOrderHom.monotone h }
  (eqToHom (congrArg _ (Nat.succ_add _ _)).symm â‰« (SimplexCategory.mkHom fâ‚) â‰«
    eqToHom (congrArg _ (Nat.succ_add _ _)) : _ âŸ¶ â¦‹yâ‚.len + yâ‚‚.len + 1â¦Œ)

/-- The action of the tensor product on maps of `AugmentedSimplexCategory`. -/
def tensorHom {xâ‚ yâ‚ xâ‚‚ yâ‚‚: AugmentedSimplexCategory} (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) :
    tensorObj xâ‚ xâ‚‚ âŸ¶ tensorObj yâ‚ yâ‚‚ :=
  match xâ‚, yâ‚, xâ‚‚, yâ‚‚, fâ‚, fâ‚‚ with
  | .of _, .of _, .of _, .of _, fâ‚, fâ‚‚ => tensorHomOf fâ‚ fâ‚‚
  | .of _, .of yâ‚, .star, .of yâ‚‚, fâ‚, _ =>
    fâ‚ â‰« ((SimplexCategory.mkHom <| (Fin.castAddOrderEmb (yâ‚‚.len + 1)).toOrderHom) â‰«
      eqToHom (congrArg _ (Nat.succ_add _ _)) : â¦‹yâ‚.lenâ¦Œ âŸ¶ â¦‹yâ‚.len + yâ‚‚.len + 1â¦Œ)
  | .star, .of yâ‚, .of _, .of yâ‚‚, _, fâ‚‚ =>
    fâ‚‚ â‰« ((SimplexCategory.mkHom <| (Fin.natAddOrderEmb (yâ‚.len + 1)).toOrderHom) â‰«
      eqToHom (congrArg _ (Nat.succ_add _ _)) : â¦‹yâ‚‚.lenâ¦Œ âŸ¶ â¦‹yâ‚.len + yâ‚‚.len + 1â¦Œ)
  | .star, .star, .of _, .of _, _, fâ‚‚ => fâ‚‚
  | .of _, .of _, .star, .star, fâ‚, _ => fâ‚
  | .star, _, .star, _, _, _ => WithInitial.starInitial.to _

/-- The unit for the monoidal structure on `AugmentedSimplexCategory` is the initial object. -/
abbrev tensorUnit : AugmentedSimplexCategory := WithInitial.star

/-- The associator isomorphism for the monoidal structure on `AugmentedSimplexCategory` -/
def associator (x y z : AugmentedSimplexCategory) :
    tensorObj (tensorObj x y) z â‰… tensorObj x (tensorObj y z) :=
  match x, y, z with
  | .of x, .of y, .of z =>
    eqToIso (congrArg (fun j â†¦ WithInitial.of <| SimplexCategory.mk j)
      (by simp +arith))
  | .star, .star, .star => Iso.refl _
  | .star, .of _, .star => Iso.refl _
  | .star, .star, .of _ => Iso.refl _
  | .star, .of _, .of _ => Iso.refl _
  | .of _, .star, .star => Iso.refl _
  | .of _, .star, .of _ => Iso.refl _
  | .of _, .of _, .star => Iso.refl _

/-- The left unitor isomorphism for the monoidal structure in `AugmentedSimplexCategory` -/
def leftUnitor (x : AugmentedSimplexCategory) :
    tensorObj tensorUnit x â‰… x :=
  match x with
  | .of _ => Iso.refl _
  | .star => Iso.refl _

/-- The right unitor isomorphism for the monoidal structure in `AugmentedSimplexCategory` -/
def rightUnitor (x : AugmentedSimplexCategory) :
    tensorObj x tensorUnit â‰… x :=
  match x with
  | .of _ => Iso.refl _
  | .star => Iso.refl _

instance : MonoidalCategoryStruct AugmentedSimplexCategory where
  tensorObj := tensorObj
  tensorHom := tensorHom
  tensorUnit := tensorUnit
  associator := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor
  whiskerLeft x _ _ f := tensorHom (ğŸ™ x) f
  whiskerRight f x := tensorHom f (ğŸ™ x)

@[local simp]
lemma id_tensorHom (x : AugmentedSimplexCategory) {yâ‚ yâ‚‚ : AugmentedSimplexCategory}
    (f : yâ‚ âŸ¶ yâ‚‚) : ğŸ™ x âŠ— f = x â— f :=
  rfl

@[local simp]
lemma tensorHom_id {xâ‚ xâ‚‚ : AugmentedSimplexCategory} (y : AugmentedSimplexCategory)
    (f : xâ‚ âŸ¶ xâ‚‚) : f âŠ— ğŸ™ y = f â–· y :=
  rfl

@[local simp]
lemma whiskerLeft_id_star {x: AugmentedSimplexCategory} : x â— ğŸ™ WithInitial.star = ğŸ™ _ := by
  cases x <;>
  rfl

@[local simp]
lemma id_star_whiskerRight {x: AugmentedSimplexCategory} : ğŸ™ WithInitial.star â–· x = ğŸ™ _ := by
  cases x <;>
  rfl

/-- Thanks to `tensorUnit` being initial in `AugmentedSimplexCategory`, we get
a morhpim `Î” âŸ¶ Î” âŠ— Î”'` for every pair of objects `Î”, Î”'`. -/
def Ï†â‚ (x y : AugmentedSimplexCategory) : x âŸ¶ x âŠ— y :=
  (Ï_ x).inv â‰« _ â— (WithInitial.starInitial.to y)

/-- Thanks to `tensorUnit` being initial in `AugmentedSimplexCategory`, we get
a morhpim `Î”' âŸ¶ Î” âŠ— Î”'` for every pair of objects `Î”, Î”'`. -/
def Ï†â‚‚ (x y : AugmentedSimplexCategory) : y âŸ¶ x âŠ— y :=
  (Î»_ y).inv â‰« (WithInitial.starInitial.to x) â–· _

/-- Again, to ease type checking, we also provide a version of Ï†â‚ that lives in
`SimplexCategory`. -/
abbrev Ï†â‚' (x y : SimplexCategory) : x âŸ¶ tensorObjOf x y := WithInitial.down <| Ï†â‚ (.of x) (.of y)

/-- Again, to ease type checking, we also provide a version of Ï†â‚‚ that lives in
`SimplexCategory`. -/
abbrev Ï†â‚‚' (x y : SimplexCategory) : y âŸ¶ tensorObjOf x y := WithInitial.down <| Ï†â‚‚ (.of x) (.of y)

lemma Ï†â‚'_eval (x y : SimplexCategory) (i : Fin (x.len + 1)) :
    (Ï†â‚' x y).toOrderHom i = (i.castAdd _).cast (Nat.succ_add x.len (y.len + 1)) := by
  simp only [Ï†â‚', WithInitial.down, Ï†â‚, MonoidalCategoryStruct.rightUnitor, rightUnitor, tensorObj,
    Iso.refl_inv, MonoidalCategoryStruct.whiskerLeft, tensorHom, SimplexCategory.mk_len, Nat.add_eq,
    SimplexCategory.mkHom, Category.id_comp, SimplexCategory.comp_toOrderHom,
    SimplexCategory.len_mk, SimplexCategory.eqToHom_toOrderHom, SimplexCategory.Hom.toOrderHom_mk,
    OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe, OrderIso.coe_toOrderEmbedding,
    Function.comp_apply, Fin.castOrderIso_apply, Fin.cast_inj]
  rfl

lemma Ï†â‚‚'_eval (x y : SimplexCategory) (i : Fin (y.len + 1)) :
    (Ï†â‚‚' x y).toOrderHom i = (i.natAdd _).cast (Nat.succ_add x.len (y.len + 1)) := by
  simp only [SimplexCategory.len_mk, Ï†â‚‚', WithInitial.down, Ï†â‚‚, MonoidalCategoryStruct.leftUnitor,
    leftUnitor, tensorObj, Iso.refl_inv, MonoidalCategoryStruct.whiskerRight, tensorHom,
    SimplexCategory.mk_len, Nat.add_eq, SimplexCategory.mkHom, Category.id_comp,
    SimplexCategory.comp_toOrderHom, SimplexCategory.eqToHom_toOrderHom,
    SimplexCategory.Hom.toOrderHom_mk, OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe,
    OrderIso.coe_toOrderEmbedding, Function.comp_apply, Fin.castOrderIso_apply, Fin.cast_inj]
  rfl

/-- We can characterize morphisms out of a tensor product via their precomposition with `Ï†â‚` and
`Ï†â‚‚`. -/
@[ext]
theorem tensorObj_hom_ext {x y z : AugmentedSimplexCategory} (f g : x âŠ— y âŸ¶ z)
    (hâ‚ : Ï†â‚ _ _ â‰« f = Ï†â‚ _ _ â‰« g)
    (hâ‚‚ : Ï†â‚‚ _ _ â‰« f = Ï†â‚‚ _ _ â‰« g)
    : f = g :=
  match x, y, z, f, g with
  | .of x, .of y, .of z, f, g => by
    change (tensorObjOf x y) âŸ¶ z at f g
    change Ï†â‚' _ _ â‰« f = Ï†â‚' _ _ â‰« g at hâ‚
    change Ï†â‚‚' _ _ â‰« f = Ï†â‚‚' _ _ â‰« g at hâ‚‚
    ext i
    set j : Fin ((x.len + 1) + (y.len + 1)) := i.cast (Nat.succ_add x.len (y.len + 1)).symm
    haveI : i = j.cast (Nat.succ_add x.len (y.len + 1)) := rfl
    rw [this]
    cases j using Fin.addCases (m := x.len + 1) (n := y.len + 1) with
    | left j =>
      rw [SimplexCategory.Hom.ext_iff, OrderHom.ext_iff] at hâ‚
      simpa [â† Ï†â‚'_eval, ConcreteCategory.hom, Fin.ext_iff] using congrFun hâ‚ j
    | right j =>
      rw [SimplexCategory.Hom.ext_iff, OrderHom.ext_iff] at hâ‚‚
      simpa [â† Ï†â‚‚'_eval, ConcreteCategory.hom, Fin.ext_iff] using congrFun hâ‚‚ j
  | .of x, .star, .of z, f, g => by
      simp only [Ï†â‚, Ï†â‚‚, Category.assoc, Iso.cancel_iso_inv_left, Limits.IsInitial.to_self,
        whiskerLeft_id_star] at hâ‚
      simpa [Category.id_comp f, Category.id_comp g] using hâ‚
  | .star, .of y, .of z, f, g => by
      simp only [Ï†â‚, Ï†â‚‚, Category.assoc, Iso.cancel_iso_inv_left, Limits.IsInitial.to_self,
        id_star_whiskerRight] at hâ‚‚
      simpa [Category.id_comp f, Category.id_comp g] using hâ‚‚
  | .star, .star, .of z, f, g => rfl
  | .star, .star, .star, f, g => rfl

@[reassoc (attr := simp)]
lemma Ï†â‚_comp_tensorHom {xâ‚ yâ‚ xâ‚‚ yâ‚‚: AugmentedSimplexCategory}
    (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) : Ï†â‚ xâ‚ xâ‚‚ â‰« (fâ‚ âŠ— fâ‚‚) = fâ‚ â‰« Ï†â‚ yâ‚ yâ‚‚ :=
  match xâ‚, yâ‚, xâ‚‚, yâ‚‚, fâ‚, fâ‚‚ with
  | .of xâ‚, .of yâ‚, .of xâ‚‚, .of yâ‚‚, fâ‚, fâ‚‚ => by
    change Ï†â‚' _ _ â‰« tensorHomOf _ _ = WithInitial.down fâ‚ â‰« Ï†â‚' _ _
    ext i
    simp only [SimplexCategory.len_mk, tensorHomOf, Nat.add_eq, SimplexCategory.mkHom,
      SimplexCategory.comp_toOrderHom, SimplexCategory.eqToHom_toOrderHom,
      SimplexCategory.Hom.toOrderHom_mk, OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe,
      OrderIso.coe_toOrderEmbedding, Function.comp_apply, Fin.castOrderIso_apply, Fin.coe_cast]
    haveI := Ï†â‚'_eval xâ‚ xâ‚‚ i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    haveI := Ï†â‚'_eval yâ‚ yâ‚‚ <| (WithInitial.down fâ‚).toOrderHom i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    conv_lhs =>
      arg 1
      change Fin.addCases
        (fun i â†¦ Fin.castAdd (yâ‚‚.len + 1) (fâ‚.toOrderHom i))
        (fun i â†¦ Fin.natAdd (yâ‚.len + 1) (fâ‚‚.toOrderHom i))
        (Fin.castAdd (xâ‚‚.len + 1) i)
      rw [Fin.addCases_left]
    rfl
  | _, _, .star, _, fâ‚, fâ‚‚ => by aesop_cat
  | .star, _, _, _, _, _ => rfl

@[reassoc (attr := simp)]
lemma Ï†â‚‚_comp_tensorHom {xâ‚ yâ‚ xâ‚‚ yâ‚‚: AugmentedSimplexCategory}
    (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) : Ï†â‚‚ xâ‚ xâ‚‚ â‰« (fâ‚ âŠ— fâ‚‚) = fâ‚‚ â‰« Ï†â‚‚ yâ‚ yâ‚‚ :=
  match xâ‚, yâ‚, xâ‚‚, yâ‚‚, fâ‚, fâ‚‚ with
  | .of xâ‚, .of yâ‚, .of xâ‚‚, .of yâ‚‚, fâ‚, fâ‚‚ => by
    change Ï†â‚‚' _ _ â‰« tensorHomOf _ _ = WithInitial.down fâ‚‚ â‰« Ï†â‚‚' _ _
    ext i
    simp only [SimplexCategory.len_mk, tensorHomOf, Nat.add_eq, SimplexCategory.mkHom,
      SimplexCategory.comp_toOrderHom, SimplexCategory.eqToHom_toOrderHom,
      SimplexCategory.Hom.toOrderHom_mk, OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe,
      OrderIso.coe_toOrderEmbedding, Function.comp_apply, Fin.castOrderIso_apply, Fin.coe_cast]
    haveI := Ï†â‚‚'_eval xâ‚ xâ‚‚ i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    haveI := Ï†â‚‚'_eval yâ‚ yâ‚‚ <| (WithInitial.down fâ‚‚).toOrderHom i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    conv_lhs =>
      arg 1
      rw [Fin.cast_trans, Fin.cast_natAdd_left]
      change Fin.addCases
        (fun i â†¦ Fin.castAdd (yâ‚‚.len + 1) (fâ‚.toOrderHom i))
        (fun i â†¦ Fin.natAdd (yâ‚.len + 1) (fâ‚‚.toOrderHom i))
        (Fin.natAdd (xâ‚.len + 1) i)
      rw [Fin.addCases_right]
    rfl
  | .star, _, _, _, fâ‚, fâ‚‚ => by aesop_cat
  | _, _, .star, _, _, _ => rfl

@[reassoc (attr := simp)]
lemma Ï†â‚‚_comp_associator (x y z : AugmentedSimplexCategory) :
    Ï†â‚‚ _ _ â‰« (Î±_ x y z).hom = Ï†â‚‚ _ _ â‰« Ï†â‚‚ _ _ :=
  match x, y, z with
  | .of x, .of y, .of z => by
    change Ï†â‚‚' _ _ â‰« WithInitial.down _ = Ï†â‚‚' _ _ â‰« Ï†â‚‚' _ _
    ext i
    simp only [SimplexCategory.len_mk, MonoidalCategoryStruct.associator, associator, eqToIso.hom,
      SimplexCategory.comp_toOrderHom, eqToHom_toOrderHom, OrderHom.comp_coe,
      OrderEmbedding.toOrderHom_coe, OrderIso.coe_toOrderEmbedding, Function.comp_apply,
      Fin.castOrderIso_apply, Fin.coe_cast]
    haveI := Ï†â‚‚'_eval (tensorObjOf x y) z i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    haveI := Ï†â‚‚'_eval y z i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    generalize_proofs h h'
    haveI := Ï†â‚‚'_eval x (tensorObjOf y z) <| Fin.cast h' <| i.natAdd (y.len + 1)
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    simp +arith
  | .star, _, _ => by aesop_cat
  | _, .star, _ => by aesop_cat
  | _, _, .star => by aesop_cat

@[reassoc (attr := simp)]
lemma Ï†â‚_comp_Ï†â‚_comp_associator (x y z : AugmentedSimplexCategory) :
    Ï†â‚ _ _ â‰« Ï†â‚ _ _ â‰« (Î±_ x y z).hom = Ï†â‚ _ _ :=
  match x, y, z with
  | .of x, .of y, .of z => by
    change Ï†â‚' _ _ â‰« Ï†â‚' _ _ â‰« WithInitial.down _ = Ï†â‚' _ _
    ext i
    simp only [SimplexCategory.len_mk, MonoidalCategoryStruct.associator, associator, eqToIso.hom,
      SimplexCategory.comp_toOrderHom, eqToHom_toOrderHom, OrderHom.comp_coe,
      OrderEmbedding.toOrderHom_coe, OrderIso.coe_toOrderEmbedding, Function.comp_apply,
      Fin.castOrderIso_apply, Fin.coe_cast]
    haveI := Ï†â‚'_eval x y i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    haveI := Ï†â‚'_eval x (tensorObjOf y z) i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    generalize_proofs h h'
    haveI := Ï†â‚'_eval (tensorObjOf x y) z <| Fin.cast h <| i.castAdd (y.len + 1)
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    simp +arith
  | .star, _, _ => by aesop_cat
  | _, .star, _ => by aesop_cat
  | _, _, .star => by aesop_cat

@[reassoc (attr := simp)]
lemma Ï†â‚‚_comp_Ï†â‚_comp_associator (x y z : AugmentedSimplexCategory) :
    Ï†â‚‚ _ _ â‰« Ï†â‚ _ _ â‰« (Î±_ x y z).hom = Ï†â‚ _ _ â‰« Ï†â‚‚ _ _ :=
  match x, y, z with
  | .of x, .of y, .of z => by
    change Ï†â‚‚' _ _ â‰« Ï†â‚' _ _ â‰« WithInitial.down _ = Ï†â‚' _ _ â‰« Ï†â‚‚' _ _
    ext i
    simp only [SimplexCategory.len_mk, MonoidalCategoryStruct.associator, associator, eqToIso.hom,
      SimplexCategory.comp_toOrderHom, eqToHom_toOrderHom, OrderHom.comp_coe,
      OrderEmbedding.toOrderHom_coe, OrderIso.coe_toOrderEmbedding, Function.comp_apply,
      Fin.castOrderIso_apply, Fin.coe_cast]
    haveI := Ï†â‚'_eval y z i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    haveI := Ï†â‚‚'_eval x y i
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    generalize_proofs h h'
    haveI := Ï†â‚'_eval (tensorObjOf x y) z <| Fin.cast h <| i.natAdd (x.len + 1)
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    haveI := Ï†â‚‚'_eval x (tensorObjOf y z) <| Fin.cast h' <| i.castAdd (z.len + 1)
    simp only [SimplexCategory.len_mk] at this
    rw [this]
    simp +arith
  | .star, _, _ => by aesop_cat
  | _, .star, _ => by aesop_cat
  | _, _, .star => by aesop_cat

theorem tensor_comp {xâ‚ yâ‚ zâ‚ xâ‚‚ yâ‚‚ zâ‚‚ : AugmentedSimplexCategory}
    (fâ‚ : xâ‚ âŸ¶ yâ‚) (fâ‚‚ : xâ‚‚ âŸ¶ yâ‚‚) (gâ‚ : yâ‚ âŸ¶ zâ‚) (gâ‚‚ : yâ‚‚ âŸ¶ zâ‚‚) :
    (fâ‚ â‰« gâ‚) âŠ— (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ— fâ‚‚) â‰« (gâ‚ âŠ— gâ‚‚) := by
  aesop_cat

theorem tensor_id (x y : AugmentedSimplexCategory) : (ğŸ™ x) âŠ— (ğŸ™ y) = ğŸ™ (x âŠ— y) := by
  ext
  Â· simpa [Ï†â‚, MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.whiskerRight] using
      (tensor_comp (ğŸ™ x) (WithInitial.starInitial.to y) (ğŸ™ x) (ğŸ™ y)).symm
  Â· simpa [Ï†â‚‚, MonoidalCategoryStruct.whiskerLeft, MonoidalCategoryStruct.whiskerRight] using
      (tensor_comp (WithInitial.starInitial.to x) (ğŸ™ y) (ğŸ™ x) (ğŸ™ y)).symm

instance : MonoidalCategory AugmentedSimplexCategory :=
  MonoidalCategory.ofTensorHom
    (tensor_id := tensor_id)
    (tensor_comp := tensor_comp)
    (pentagon := fun w x y z â†¦ by
      ext <;>
      simp [â† id_tensorHom, â† tensorHom_id])

end AugmentedSimplexCategory
