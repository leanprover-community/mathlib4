/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz, JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Defs
import Mathlib.Data.Fintype.Sort
import Mathlib.Order.Category.NonemptyFinLinOrd
import Mathlib.Tactic.FinCases
import Mathlib.Tactic.Linarith

/-! # Basic properties of the simplex category

In `Mathlib/AlgebraicTopology/SimplexCategory/Defs.lean`, we define the simplex
category with objects `â„•` and morphisms `n âŸ¶ m` the monotone maps from
`Fin (n + 1)` to `Fin (m + 1)`.

In this file, we define the generating maps for the simplex category, show that
this category is equivalent to `NonemptyFinLinOrd`, and establish basic
properties of its epimorphisms and monomorphisms.
-/

universe v

open Simplicial CategoryTheory Limits

namespace SimplexCategory

section Init

/-- The constant morphism from â¦‹0â¦Œ. -/
def const (x y : SimplexCategory) (i : Fin (y.len + 1)) : x âŸ¶ y :=
  Hom.mk <| âŸ¨fun _ => i, by tautoâŸ©

@[simp]
lemma const_eq_id : const â¦‹0â¦Œ â¦‹0â¦Œ 0 = ğŸ™ _ := by aesop

@[simp]
lemma const_apply (x y : SimplexCategory) (i : Fin (y.len + 1)) (a : Fin (x.len + 1)) :
    (const x y i).toOrderHom a = i := rfl

@[simp]
theorem const_comp (x : SimplexCategory) {y z : SimplexCategory}
    (f : y âŸ¶ z) (i : Fin (y.len + 1)) :
    const x y i â‰« f = const x z (f.toOrderHom i) :=
  rfl

theorem const_fac_thru_zero (n m : SimplexCategory) (i : Fin (m.len + 1)) :
    const n m i = const n â¦‹0â¦Œ 0 â‰« SimplexCategory.const â¦‹0â¦Œ m i := by
  rw [const_comp]; rfl

theorem Hom.ext_zero_left {n : SimplexCategory} (f g : â¦‹0â¦Œ âŸ¶ n)
    (h0 : f.toOrderHom 0 = g.toOrderHom 0 := by rfl) : f = g := by
  ext i; match i with | 0 => exact h0 â–¸ rfl

theorem eq_const_of_zero {n : SimplexCategory} (f : â¦‹0â¦Œ âŸ¶ n) :
    f = const _ n (f.toOrderHom 0) := by
  ext x; match x with | 0 => rfl

theorem exists_eq_const_of_zero {n : SimplexCategory} (f : â¦‹0â¦Œ âŸ¶ n) :
    âˆƒ a, f = const _ n a := âŸ¨_, eq_const_of_zero _âŸ©

theorem eq_const_to_zero {n : SimplexCategory} (f : n âŸ¶ â¦‹0â¦Œ) :
    f = const n _ 0 := by
  ext : 3
  apply @Subsingleton.elim (Fin 1)

theorem Hom.ext_one_left {n : SimplexCategory} (f g : â¦‹1â¦Œ âŸ¶ n)
    (h0 : f.toOrderHom 0 = g.toOrderHom 0 := by rfl)
    (h1 : f.toOrderHom 1 = g.toOrderHom 1 := by rfl) : f = g := by
  ext i
  match i with
  | 0 => exact h0 â–¸ rfl
  | 1 => exact h1 â–¸ rfl

theorem eq_of_one_to_one (f : â¦‹1â¦Œ âŸ¶ â¦‹1â¦Œ) :
    (âˆƒ a, f = const â¦‹1â¦Œ _ a) âˆ¨ f = ğŸ™ _ := by
  match e0 : f.toOrderHom 0, e1 : f.toOrderHom 1 with
  | 0, 0 | 1, 1 =>
    refine .inl âŸ¨f.toOrderHom 0, ?_âŸ©
    ext i : 3
    match i with
    | 0 => rfl
    | 1 => exact e1.trans e0.symm
  | 0, 1 =>
    right
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 1, 0 =>
    have := f.toOrderHom.monotone (by decide : (0 : Fin 2) â‰¤ 1)
    rw [e0, e1] at this
    exact Not.elim (by decide) this

/-- Make a morphism `â¦‹nâ¦Œ âŸ¶ â¦‹mâ¦Œ` from a monotone map between fin's.
This is useful for constructing morphisms between `â¦‹nâ¦Œ` directly
without identifying `n` with `â¦‹nâ¦Œ.len`.
-/
@[simp]
def mkHom {n m : â„•} (f : Fin (n + 1) â†’o Fin (m + 1)) : â¦‹nâ¦Œ âŸ¶ â¦‹mâ¦Œ :=
  SimplexCategory.Hom.mk f

/-- The morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that picks out a specified `h : i â‰¤ j` in `Fin (n+1)`. -/
def mkOfLe {n} (i j : Fin (n + 1)) (h : i â‰¤ j) : â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ :=
  SimplexCategory.mkHom {
    toFun := fun | 0 => i | 1 => j
    monotone' := fun
      | 0, 0, _ | 1, 1, _ => le_rfl
      | 0, 1, _ => h
  }

@[simp]
lemma mkOfLe_refl {n} (j : Fin (n + 1)) :
    mkOfLe j j (by omega) = â¦‹1â¦Œ.const â¦‹nâ¦Œ j := Hom.ext_one_left _ _

/-- The morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that picks out the "diagonal composite" edge -/
def diag (n : â„•) : â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ :=
  mkOfLe 0 (Fin.ofNat (n + 1) n) (Fin.zero_le _)

/-- The morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that picks out the edge spanning the interval from `j` to `j + l`. -/
def intervalEdge {n} (j l : â„•) (hjl : j + l â‰¤ n) : â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ :=
  mkOfLe âŸ¨j, (by omega)âŸ© âŸ¨j + l, (by omega)âŸ© (Nat.le_add_right j l)

/-- The morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that picks out the arrow `i âŸ¶ i+1` in `Fin (n+1)`. -/
def mkOfSucc {n} (i : Fin n) : â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ :=
  SimplexCategory.mkHom {
    toFun := fun | 0 => i.castSucc | 1 => i.succ
    monotone' := fun
      | 0, 0, _ | 1, 1, _ => le_rfl
      | 0, 1, _ => Fin.castSucc_le_succ i
  }

@[simp]
lemma mkOfSucc_homToOrderHom_zero {n} (i : Fin n) :
    DFunLike.coe (F := Fin 2 â†’o Fin (n + 1)) (Hom.toOrderHom (mkOfSucc i)) 0 = i.castSucc := rfl

@[simp]
lemma mkOfSucc_homToOrderHom_one {n} (i : Fin n) :
    DFunLike.coe (F := Fin 2 â†’o Fin (n + 1)) (Hom.toOrderHom (mkOfSucc i)) 1 = i.succ := rfl


/-- The morphism `â¦‹2â¦Œ âŸ¶ â¦‹nâ¦Œ` that picks out a specified composite of morphisms in `Fin (n+1)`. -/
def mkOfLeComp {n} (i j k : Fin (n + 1)) (hâ‚ : i â‰¤ j) (hâ‚‚ : j â‰¤ k) :
    â¦‹2â¦Œ âŸ¶ â¦‹nâ¦Œ :=
  SimplexCategory.mkHom {
    toFun := fun | 0 => i | 1 => j | 2 => k
    monotone' := fun
      | 0, 0, _ | 1, 1, _ | 2, 2, _  => le_rfl
      | 0, 1, _ => hâ‚
      | 1, 2, _ => hâ‚‚
      | 0, 2, _ => Fin.le_trans hâ‚ hâ‚‚
  }

/-- The "inert" morphism associated to a subinterval `j â‰¤ i â‰¤ j + l` of `Fin (n + 1)`. -/
def subinterval {n} (j l : â„•) (hjl : j + l â‰¤ n) :
    â¦‹lâ¦Œ âŸ¶ â¦‹nâ¦Œ :=
  SimplexCategory.mkHom {
    toFun := fun i => âŸ¨i.1 + j, (by omega)âŸ©
    monotone' := fun i i' hii' => by simpa only [Fin.mk_le_mk, add_le_add_iff_right] using hii'
  }

lemma const_subinterval_eq {n} (j l : â„•) (hjl : j + l â‰¤ n) (i : Fin (l + 1)) :
    â¦‹0â¦Œ.const â¦‹lâ¦Œ i â‰« subinterval j l hjl =
    â¦‹0â¦Œ.const â¦‹nâ¦Œ âŸ¨j + i.1, lt_add_of_lt_add_right (Nat.add_lt_add_left i.2 j) hjlâŸ©  := by
  rw [const_comp]
  congr
  ext
  dsimp [subinterval]
  rw [add_comm]

@[simp]
lemma mkOfSucc_subinterval_eq {n} (j l : â„•) (hjl : j + l â‰¤ n) (i : Fin l) :
    mkOfSucc i â‰« subinterval j l hjl =
    mkOfSucc âŸ¨j + i.1, Nat.lt_of_lt_of_le (Nat.add_lt_add_left i.2 j) hjlâŸ© := by
  unfold subinterval mkOfSucc
  ext i
  match i with
  | 0 =>
    simp only [len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom, Hom.toOrderHom_mk,
      OrderHom.mk_comp_mk, Fin.isValue, OrderHom.coe_mk, Function.comp_apply, Fin.castSucc_mk,
      Fin.succ_mk]
    rw [add_comm]
    rfl
  | 1 =>
    simp only [len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom, Hom.toOrderHom_mk,
      OrderHom.mk_comp_mk, Fin.isValue, OrderHom.coe_mk, Function.comp_apply, Fin.castSucc_mk,
      Fin.succ_mk]
    rw [â† Nat.add_comm j _]
    rfl

@[simp]
lemma diag_subinterval_eq {n} (j l : â„•) (hjl : j + l â‰¤ n) :
    diag l â‰« subinterval j l hjl = intervalEdge j l hjl := by
  unfold subinterval intervalEdge diag mkOfLe
  ext i
  match i with
  | 0 =>
    simp only [len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom,
      Hom.toOrderHom_mk, OrderHom.mk_comp_mk, Fin.isValue, OrderHom.coe_mk, Function.comp_apply]
    rw [Nat.add_comm]
    rfl
  | 1 =>
    simp only [len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom,
      Hom.toOrderHom_mk, OrderHom.mk_comp_mk, Fin.isValue, OrderHom.coe_mk, Function.comp_apply]
    rw [Nat.add_comm]
    simp only [Fin.isValue, Fin.ofNat_eq_cast, Fin.natCast_eq_last]
    rfl

instance (Î” : SimplexCategory) : Subsingleton (Î” âŸ¶ â¦‹0â¦Œ) where
  allEq f g := by ext : 3; apply Subsingleton.elim (Î± := Fin 1)

theorem hom_zero_zero (f : â¦‹0â¦Œ âŸ¶ â¦‹0â¦Œ) : f = ğŸ™ _ := by
  apply Subsingleton.elim

end Init

section Generators

/-!
## Generating maps for the simplex category

TODO: prove that the simplex category is equivalent to
one given by the following generators and relations.
-/

/-- The `i`-th face map from `â¦‹nâ¦Œ` to `â¦‹n+1â¦Œ` -/
def Î´ {n} (i : Fin (n + 2)) : â¦‹nâ¦Œ âŸ¶ â¦‹n + 1â¦Œ :=
  mkHom (Fin.succAboveOrderEmb i).toOrderHom

/-- The `i`-th degeneracy map from `â¦‹n+1â¦Œ` to `â¦‹nâ¦Œ` -/
def Ïƒ {n} (i : Fin (n + 1)) : â¦‹n + 1â¦Œ âŸ¶ â¦‹nâ¦Œ :=
  mkHom i.predAboveOrderHom

/-- The generic case of the first simplicial identity -/
theorem Î´_comp_Î´ {n} {i j : Fin (n + 2)} (H : i â‰¤ j) :
    Î´ i â‰« Î´ j.succ = Î´ j â‰« Î´ i.castSucc := by
  ext k
  dsimp [Î´, Fin.succAbove]
  rcases i with âŸ¨i, _âŸ©
  rcases j with âŸ¨j, _âŸ©
  rcases k with âŸ¨k, _âŸ©
  split_ifs <;> Â· simp at * <;> omega

theorem Î´_comp_Î´' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : i.castSucc < j) :
    Î´ i â‰« Î´ j =
      Î´ (j.pred fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) â‰«
        Î´ (Fin.castSucc i) := by
  rw [â† Î´_comp_Î´]
  Â· rw [Fin.succ_pred]
  Â· simpa only [Fin.le_iff_val_le_val, â† Nat.lt_succ_iff, Nat.succ_eq_add_one, â† Fin.val_succ,
      j.succ_pred, Fin.lt_iff_val_lt_val] using H

theorem Î´_comp_Î´'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i â‰¤ Fin.castSucc j) :
    Î´ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) â‰« Î´ j.succ =
      Î´ j â‰« Î´ i := by
  rw [Î´_comp_Î´]
  Â· rfl
  Â· exact H

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Î´_comp_Î´_self {n} {i : Fin (n + 2)} : Î´ i â‰« Î´ i.castSucc = Î´ i â‰« Î´ i.succ :=
  (Î´_comp_Î´ (le_refl i)).symm

@[reassoc]
theorem Î´_comp_Î´_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = i.castSucc) :
    Î´ i â‰« Î´ j = Î´ i â‰« Î´ i.succ := by
  subst H
  rw [Î´_comp_Î´_self]

/-- The second simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i â‰¤ j.castSucc) :
    Î´ i.castSucc â‰« Ïƒ j.succ = Ïƒ j â‰« Î´ i := by
  ext k : 3
  dsimp [Ïƒ, Î´]
  rcases le_or_gt i k with (hik | hik)
  Â· rw [Fin.succAbove_of_le_castSucc _ _ (Fin.castSucc_le_castSucc_iff.mpr hik),
    Fin.succ_predAbove_succ, Fin.succAbove_of_le_castSucc]
    rcases le_or_gt k (j.castSucc) with (hjk | hjk)
    Â· rwa [Fin.predAbove_of_le_castSucc _ _ hjk, Fin.castSucc_castPred]
    Â· rw [Fin.le_castSucc_iff, Fin.predAbove_of_castSucc_lt _ _ hjk, Fin.succ_pred]
      exact H.trans_lt hjk
  Â· rw [Fin.succAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_castSucc_iff.mpr hik)]
    have hjk := H.trans_lt' hik
    rw [Fin.predAbove_of_le_castSucc _ _ (Fin.castSucc_le_castSucc_iff.mpr
      (hjk.trans (Fin.castSucc_lt_succ _)).le),
      Fin.predAbove_of_le_castSucc _ _ hjk.le, Fin.castPred_castSucc, Fin.succAbove_of_castSucc_lt,
      Fin.castSucc_castPred]
    rwa [Fin.castSucc_castPred]

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_self {n} {i : Fin (n + 1)} :
    Î´ (Fin.castSucc i) â‰« Ïƒ i = ğŸ™ â¦‹nâ¦Œ := by
  rcases i with âŸ¨i, hiâŸ©
  ext âŸ¨j, hjâŸ©
  simp? at hj says simp only [len_mk] at hj
  dsimp [Ïƒ, Î´, Fin.predAbove, Fin.succAbove]
  simp only [Fin.lt_iff_val_lt_val, Fin.dite_val, Fin.ite_val, Fin.coe_pred]
  split_ifs
  any_goals simp
  all_goals omega

@[reassoc]
theorem Î´_comp_Ïƒ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.castSucc) :
    Î´ j â‰« Ïƒ i = ğŸ™ â¦‹nâ¦Œ := by
  subst H
  rw [Î´_comp_Ïƒ_self]

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_succ {n} {i : Fin (n + 1)} : Î´ i.succ â‰« Ïƒ i = ğŸ™ â¦‹nâ¦Œ := by
  ext j
  rcases i with âŸ¨i, _âŸ©
  rcases j with âŸ¨j, _âŸ©
  dsimp [Î´, Ïƒ, Fin.succAbove, Fin.predAbove]
  split_ifs <;> simp <;> simp at * <;> omega

@[reassoc]
theorem Î´_comp_Ïƒ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    Î´ j â‰« Ïƒ i = ğŸ™ â¦‹nâ¦Œ := by
  subst H
  rw [Î´_comp_Ïƒ_succ]

/-- The fourth simplicial identity -/
@[reassoc]
theorem Î´_comp_Ïƒ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) :
    Î´ i.succ â‰« Ïƒ j.castSucc = Ïƒ j â‰« Î´ i := by
  ext k : 3
  dsimp [Î´, Ïƒ]
  rcases le_or_gt k i with (hik | hik)
  Â· rw [Fin.succAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_succ_iff.mpr hik)]
    rcases le_or_gt k (j.castSucc) with (hjk | hjk)
    Â· rw [Fin.predAbove_of_le_castSucc _ _
      (Fin.castSucc_le_castSucc_iff.mpr hjk), Fin.castPred_castSucc,
      Fin.predAbove_of_le_castSucc _ _ hjk, Fin.succAbove_of_castSucc_lt, Fin.castSucc_castPred]
      rw [Fin.castSucc_castPred]
      exact hjk.trans_lt H
    Â· rw [Fin.predAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_castSucc_iff.mpr hjk),
      Fin.predAbove_of_castSucc_lt _ _ hjk, Fin.succAbove_of_castSucc_lt,
      Fin.castSucc_pred_eq_pred_castSucc]
      rwa [Fin.castSucc_lt_iff_succ_le, Fin.succ_pred]
  Â· rw [Fin.succAbove_of_le_castSucc _ _ (Fin.succ_le_castSucc_iff.mpr hik)]
    have hjk := H.trans hik
    rw [Fin.predAbove_of_castSucc_lt _ _ hjk, Fin.predAbove_of_castSucc_lt _ _
      (Fin.castSucc_lt_succ_iff.mpr hjk.le),
    Fin.pred_succ, Fin.succAbove_of_le_castSucc, Fin.succ_pred]
    rwa [Fin.le_castSucc_pred_iff]

@[reassoc]
theorem Î´_comp_Ïƒ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    Î´ i â‰« Ïƒ j = Ïƒ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) â‰«
      Î´ (i.pred fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
  rw [â† Î´_comp_Ïƒ_of_gt]
  Â· simp
  Â· rw [Fin.castSucc_castLT, â† Fin.succ_lt_succ_iff, Fin.succ_pred]
    exact H

/-- The fifth simplicial identity -/
@[reassoc]
theorem Ïƒ_comp_Ïƒ {n} {i j : Fin (n + 1)} (H : i â‰¤ j) :
    Ïƒ (Fin.castSucc i) â‰« Ïƒ j = Ïƒ j.succ â‰« Ïƒ i := by
  ext k : 3
  dsimp [Ïƒ]
  cases k using Fin.lastCases with
  | last => simp only [len_mk, Fin.predAbove_right_last]
  | cast k =>
    cases k using Fin.cases with
    | zero =>
      ext
      simp
    | succ k =>
      rcases le_or_gt i k with (h | h)
      Â· simp_rw [Fin.predAbove_of_castSucc_lt i.castSucc _ (Fin.castSucc_lt_castSucc_iff.mpr
        (Fin.castSucc_lt_succ_iff.mpr h)), â† Fin.succ_castSucc, Fin.pred_succ,
        Fin.succ_predAbove_succ]
        rw [Fin.predAbove_of_castSucc_lt i _ (Fin.castSucc_lt_succ_iff.mpr _), Fin.pred_succ]
        rcases le_or_gt k j with (hkj | hkj)
        Â· rwa [Fin.predAbove_of_le_castSucc _ _ (Fin.castSucc_le_castSucc_iff.mpr hkj),
          Fin.castPred_castSucc]
        Â· rw [Fin.predAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_castSucc_iff.mpr hkj),
          Fin.le_pred_iff,
          Fin.succ_le_castSucc_iff]
          exact H.trans_lt hkj
      Â· simp_rw [Fin.predAbove_of_le_castSucc i.castSucc _ (Fin.castSucc_le_castSucc_iff.mpr
        (Fin.succ_le_castSucc_iff.mpr h)), Fin.castPred_castSucc, â† Fin.succ_castSucc,
        Fin.succ_predAbove_succ]
        rw [Fin.predAbove_of_le_castSucc _ k.castSucc
        (Fin.castSucc_le_castSucc_iff.mpr (h.le.trans H)),
        Fin.castPred_castSucc, Fin.predAbove_of_le_castSucc _ k.succ
        (Fin.succ_le_castSucc_iff.mpr (H.trans_lt' h)), Fin.predAbove_of_le_castSucc _ k.succ
        (Fin.succ_le_castSucc_iff.mpr h)]

/--
If `f : â¦‹mâ¦Œ âŸ¶ â¦‹n+1â¦Œ` is a morphism and `j` is not in the range of `f`,
then `factor_Î´ f j` is a morphism `â¦‹mâ¦Œ âŸ¶ â¦‹nâ¦Œ` such that
`factor_Î´ f j â‰« Î´ j = f` (as witnessed by `factor_Î´_spec`).
-/
def factor_Î´ {m n : â„•} (f : â¦‹mâ¦Œ âŸ¶ â¦‹n + 1â¦Œ) (j : Fin (n + 2)) : â¦‹mâ¦Œ âŸ¶ â¦‹nâ¦Œ :=
  f â‰« Ïƒ (Fin.predAbove 0 j)

open Fin in
lemma factor_Î´_spec {m n : â„•} (f : â¦‹mâ¦Œ âŸ¶ â¦‹n + 1â¦Œ) (j : Fin (n + 2))
    (hj : âˆ€ (k : Fin (m + 1)), f.toOrderHom k â‰  j) :
    factor_Î´ f j â‰« Î´ j = f := by
  ext k : 3
  specialize hj k
  dsimp [factor_Î´, Î´, Ïƒ]
  cases j using cases with
  | zero =>
    ext
    unfold predAbove
    simp only [castSucc_zero, lt_self_iff_false, â†“reduceDIte]
    split
    Â· simp
    Â· simp only [zero_succAbove, val_succ, coe_castPred]
      simp_all
  | succ j =>
    rw [predAbove_of_castSucc_lt 0 _ (by simp), pred_succ]
    rcases hj.lt_or_gt with (hj | hj)
    Â· rw [predAbove_of_le_castSucc j _]
      swap
      Â· exact (le_castSucc_iff.mpr hj)
      Â· rw [succAbove_of_castSucc_lt]
        swap
        Â· rwa [castSucc_lt_succ_iff, castPred_le_iff, le_castSucc_iff]
        rw [castSucc_castPred]
    Â· rw [predAbove_of_castSucc_lt]
      swap
      Â· exact (castSucc_lt_succ _).trans hj
      rw [succAbove_of_le_castSucc]
      swap
      Â· rwa [succ_le_castSucc_iff, lt_pred_iff]
      rw [succ_pred]

@[simp]
lemma Î´_zero_mkOfSucc {n : â„•} (i : Fin n) :
    Î´ 0 â‰« mkOfSucc i = SimplexCategory.const _ â¦‹nâ¦Œ i.succ := by
  ext x
  fin_cases x
  rfl

@[simp]
lemma Î´_one_mkOfSucc {n : â„•} (i : Fin n) :
    Î´ 1 â‰« mkOfSucc i = SimplexCategory.const _ â¦‹nâ¦Œ i.castSucc := by
  ext x
  fin_cases x
  aesop

/-- If `i + 1 < j`, `mkOfSucc i â‰« Î´ j` is the morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that
sends `0` and `1` to `i` and `i + 1`, respectively. -/
lemma mkOfSucc_Î´_lt {n : â„•} {i : Fin n} {j : Fin (n + 2)}
    (h : i.succ.castSucc < j) :
    mkOfSucc i â‰« Î´ j = mkOfSucc i.castSucc := by
  ext x
  fin_cases x
  Â· simp [Î´, Fin.succAbove_of_castSucc_lt _ _ (Nat.lt_trans _ h)]
  Â· simp [Î´, Fin.succAbove_of_castSucc_lt _ _ h]

/-- If `i + 1 > j`, `mkOfSucc i â‰« Î´ j` is the morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that
sends `0` and `1` to `i + 1` and `i + 2`, respectively. -/
lemma mkOfSucc_Î´_gt {n : â„•} {i : Fin n} {j : Fin (n + 2)}
    (h : j < i.succ.castSucc) :
    mkOfSucc i â‰« Î´ j = mkOfSucc i.succ := by
  ext x
  simp only [Î´, len_mk, mkHom, comp_toOrderHom, Hom.toOrderHom_mk, OrderHom.comp_coe,
    OrderEmbedding.toOrderHom_coe, Function.comp_apply, Fin.succAboveOrderEmb_apply]
  fin_cases x <;> rw [Fin.succAbove_of_le_castSucc]
  Â· rfl
  Â· exact Nat.le_of_lt_succ h
  Â· rfl
  Â· exact Nat.le_of_lt h

/-- If `i + 1 = j`, `mkOfSucc i â‰« Î´ j` is the morphism `â¦‹1â¦Œ âŸ¶ â¦‹nâ¦Œ` that
sends `0` and `1` to `i` and `i + 2`, respectively. -/
lemma mkOfSucc_Î´_eq {n : â„•} {i : Fin n} {j : Fin (n + 2)}
    (h : j = i.succ.castSucc) :
    mkOfSucc i â‰« Î´ j = intervalEdge i 2 (by omega) := by
  ext x
  fin_cases x
  Â· subst h
    simp only [Î´, len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom, Hom.toOrderHom_mk,
      Fin.zero_eta, OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe, Function.comp_apply,
      mkOfSucc_homToOrderHom_zero, Fin.succAboveOrderEmb_apply,
      Fin.castSucc_succAbove_castSucc, Fin.succAbove_succ_self]
    rfl
  Â· simp only [Î´, len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom, Hom.toOrderHom_mk, Fin.mk_one,
      OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe, Function.comp_apply,
      mkOfSucc_homToOrderHom_one, Fin.succAboveOrderEmb_apply]
    subst h
    rw [Fin.succAbove_castSucc_self]
    rfl

theorem eq_of_one_to_two (f : â¦‹1â¦Œ âŸ¶ â¦‹2â¦Œ) :
    (âˆƒ i, f = (Î´ (n := 1) i)) âˆ¨ âˆƒ a, f = SimplexCategory.const _ _ a := by
  have : f.toOrderHom 0 â‰¤ f.toOrderHom 1 := f.toOrderHom.monotone (by decide : (0 : Fin 2) â‰¤ 1)
  match e0 : f.toOrderHom 0, e1 : f.toOrderHom 1 with
  | 1, 2 =>
    refine .inl âŸ¨0, ?_âŸ©
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 0, 2 =>
    refine .inl âŸ¨1, ?_âŸ©
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 0, 1 =>
    refine .inl âŸ¨2, ?_âŸ©
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 0, 0 | 1, 1 | 2, 2 =>
    refine .inr âŸ¨f.toOrderHom 0, ?_âŸ©
    ext i : 3
    match i with
    | 0 => rfl
    | 1 => exact e1.trans e0.symm
  | 1, 0 | 2, 0 | 2, 1 =>
    rw [e0, e1] at this
    exact Not.elim (by decide) this

theorem eq_of_one_to_two' (f : â¦‹1â¦Œ âŸ¶ â¦‹2â¦Œ) :
    f = (Î´ (n := 1) 0) âˆ¨ f = (Î´ (n := 1) 1) âˆ¨ f = (Î´ (n := 1) 2) âˆ¨
      âˆƒ a, f = SimplexCategory.const _ _ a :=
  match eq_of_one_to_two f with
  | .inl âŸ¨0, hâŸ© => .inl h
  | .inl âŸ¨1, hâŸ© => .inr (.inl h)
  | .inl âŸ¨2, hâŸ© => .inr (.inr (.inl h))
  | .inr h => .inr (.inr (.inr h))

end Generators

section Skeleton

/-- The functor that exhibits `SimplexCategory` as skeleton
of `NonemptyFinLinOrd` -/
@[simps obj map]
def skeletalFunctor : SimplexCategory â¥¤ NonemptyFinLinOrd where
  obj a := NonemptyFinLinOrd.of (Fin (a.len + 1))
  map f := NonemptyFinLinOrd.ofHom f.toOrderHom

theorem skeletalFunctor.coe_map {Î”â‚ Î”â‚‚ : SimplexCategory} (f : Î”â‚ âŸ¶ Î”â‚‚) :
    â†‘(skeletalFunctor.map f).hom = f.toOrderHom :=
  rfl

theorem skeletal : Skeletal SimplexCategory := fun X Y âŸ¨IâŸ© => by
  suffices Fintype.card (Fin (X.len + 1)) = Fintype.card (Fin (Y.len + 1)) by
    ext
    simpa
  apply Fintype.card_congr
  exact ((skeletalFunctor â‹™ forget NonemptyFinLinOrd).mapIso I).toEquiv

namespace SkeletalFunctor

instance : skeletalFunctor.Full where
  map_surjective f := âŸ¨SimplexCategory.Hom.mk f.hom, rflâŸ©

instance : skeletalFunctor.Faithful where
  map_injective {_ _ f g} h := by
    ext : 3
    exact CategoryTheory.congr_fun h _

instance : skeletalFunctor.EssSurj where
  mem_essImage X :=
    âŸ¨mk (Fintype.card X - 1 : â„•),
      âŸ¨by
        have aux : Fintype.card X = Fintype.card X - 1 + 1 :=
          (Nat.succ_pred_eq_of_pos <| Fintype.card_pos_iff.mpr âŸ¨âŠ¥âŸ©).symm
        let f := monoEquivOfFin X aux
        have hf := (Finset.univ.orderEmbOfFin aux).strictMono
        refine
          { hom := LinOrd.ofHom âŸ¨f, hf.monotoneâŸ©
            inv := LinOrd.ofHom âŸ¨f.symm, ?_âŸ©
            hom_inv_id := by ext; apply f.symm_apply_apply
            inv_hom_id := by ext; apply f.apply_symm_apply }
        intro i j h
        change f.symm i â‰¤ f.symm j
        rw [â† hf.le_iff_le]
        change f (f.symm i) â‰¤ f (f.symm j)
        simpa only [OrderIso.apply_symm_apply]âŸ©âŸ©

noncomputable instance isEquivalence : skeletalFunctor.IsEquivalence where

end SkeletalFunctor

/-- The equivalence that exhibits `SimplexCategory` as skeleton
of `NonemptyFinLinOrd` -/
noncomputable def skeletalEquivalence : SimplexCategory â‰Œ NonemptyFinLinOrd :=
  Functor.asEquivalence skeletalFunctor

end Skeleton

/-- `SimplexCategory` is a skeleton of `NonemptyFinLinOrd`.
-/
lemma isSkeletonOf :
    IsSkeletonOf NonemptyFinLinOrd SimplexCategory skeletalFunctor where
  skel := skeletal
  eqv := SkeletalFunctor.isEquivalence

section Concrete

instance : ConcreteCategory SimplexCategory (fun i j => Fin (i.len + 1) â†’o Fin (j.len + 1)) where
  hom := Hom.toOrderHom
  ofHom f := Hom.mk f

instance (x : SimplexCategory) : Fintype (ToType x) :=
  inferInstanceAs (Fintype (Fin _))

instance (x : SimplexCategory) (n : â„•) : OfNat (ToType x) n :=
  inferInstanceAs (OfNat (Fin _) n)

lemma toType_apply (x : SimplexCategory) : ToType x = Fin (x.len + 1) := rfl

end Concrete

section EpiMono

/-- A morphism in `SimplexCategory` is a monomorphism precisely when it is an injective function
-/
theorem mono_iff_injective {n m : SimplexCategory} {f : n âŸ¶ m} :
    Mono f â†” Function.Injective f.toOrderHom := by
  rw [â† Functor.mono_map_iff_mono skeletalEquivalence.functor]
  dsimp only [skeletalEquivalence, Functor.asEquivalence_functor]
  simp only [skeletalFunctor_obj, skeletalFunctor_map,
    NonemptyFinLinOrd.mono_iff_injective, NonemptyFinLinOrd.coe_of, ConcreteCategory.hom_ofHom]

/-- A morphism in `SimplexCategory` is an epimorphism if and only if it is a surjective function
-/
theorem epi_iff_surjective {n m : SimplexCategory} {f : n âŸ¶ m} :
    Epi f â†” Function.Surjective f.toOrderHom := by
  rw [â† Functor.epi_map_iff_epi skeletalEquivalence.functor]
  dsimp only [skeletalEquivalence, Functor.asEquivalence_functor]
  simp only [skeletalFunctor_obj, skeletalFunctor_map,
    NonemptyFinLinOrd.epi_iff_surjective, NonemptyFinLinOrd.coe_of, ConcreteCategory.hom_ofHom]

/-- A monomorphism in `SimplexCategory` must increase lengths -/
theorem len_le_of_mono {x y : SimplexCategory} {f : x âŸ¶ y} : Mono f â†’ x.len â‰¤ y.len := by
  intro hyp_f_mono
  have f_inj : Function.Injective f.toOrderHom.toFun := mono_iff_injective.1 hyp_f_mono
  simpa using Fintype.card_le_of_injective f.toOrderHom.toFun f_inj

theorem le_of_mono {n m : â„•} {f : â¦‹nâ¦Œ âŸ¶ â¦‹mâ¦Œ} : CategoryTheory.Mono f â†’ n â‰¤ m :=
  len_le_of_mono

/-- An epimorphism in `SimplexCategory` must decrease lengths -/
theorem len_le_of_epi {x y : SimplexCategory} {f : x âŸ¶ y} : Epi f â†’ y.len â‰¤ x.len := by
  intro hyp_f_epi
  have f_surj : Function.Surjective f.toOrderHom.toFun := epi_iff_surjective.1 hyp_f_epi
  simpa using Fintype.card_le_of_surjective f.toOrderHom.toFun f_surj

theorem le_of_epi {n m : â„•} {f : â¦‹nâ¦Œ âŸ¶ â¦‹mâ¦Œ} : Epi f â†’ m â‰¤ n := len_le_of_epi

instance {n : â„•} {i : Fin (n + 2)} : Mono (Î´ i) := by
  rw [mono_iff_injective]
  exact Fin.succAbove_right_injective

instance {n : â„•} {i : Fin (n + 1)} : Epi (Ïƒ i) := by
  rw [epi_iff_surjective]
  intro b
  simp only [Ïƒ, mkHom, Hom.toOrderHom_mk]
  by_cases h : b â‰¤ i
  Â· use b.castSucc
    -- This was not needed before https://github.com/leanprover/lean4/pull/2644
    dsimp
    rw [Fin.predAbove_of_le_castSucc i b.castSucc (by simpa only [Fin.coe_eq_castSucc] using h)]
    simp only [len_mk, Fin.castPred_castSucc]
  Â· use b.succ
    -- This was not needed before https://github.com/leanprover/lean4/pull/2644
    dsimp
    rw [Fin.predAbove_of_castSucc_lt i b.succ _, Fin.pred_succ]
    rw [not_le] at h
    rw [Fin.lt_iff_val_lt_val] at h âŠ¢
    simpa only [Fin.val_succ, Fin.coe_castSucc] using Nat.lt.step h

instance : (forget SimplexCategory).ReflectsIsomorphisms :=
  âŸ¨fun f hf =>
    Iso.isIso_hom
      { hom := f
        inv := Hom.mk
            { toFun := inv ((forget SimplexCategory).map f)
              monotone' := fun yâ‚ yâ‚‚ h => by
                by_cases h' : yâ‚ < yâ‚‚
                Â· by_contra h''
                  apply not_le.mpr h'
                  convert f.toOrderHom.monotone (le_of_not_ge h'')
                  all_goals
                    exact (congr_hom (Iso.inv_hom_id
                      (asIso ((forget SimplexCategory).map f))) _).symm
                Â· rw [eq_of_le_of_not_lt h h'] }
        hom_inv_id := by
          ext1
          ext1
          exact Iso.hom_inv_id (asIso ((forget _).map f))
        inv_hom_id := by
          ext1
          ext1
          exact Iso.inv_hom_id (asIso ((forget _).map f)) }âŸ©

theorem isIso_of_bijective {x y : SimplexCategory} {f : x âŸ¶ y}
    (hf : Function.Bijective f.toOrderHom.toFun) : IsIso f :=
  haveI : IsIso ((forget SimplexCategory).map f) := (isIso_iff_bijective _).mpr hf
  isIso_of_reflects_iso f (forget SimplexCategory)

/-- An isomorphism in `SimplexCategory` induces an `OrderIso`. -/
@[simp]
def orderIsoOfIso {x y : SimplexCategory} (e : x â‰… y) : Fin (x.len + 1) â‰ƒo Fin (y.len + 1) :=
  Equiv.toOrderIso
    { toFun := e.hom.toOrderHom
      invFun := e.inv.toOrderHom
      left_inv := fun i => by
        simpa only using congr_arg (fun Ï† => (Hom.toOrderHom Ï†) i) e.hom_inv_id
      right_inv := fun i => by
        simpa only using congr_arg (fun Ï† => (Hom.toOrderHom Ï†) i) e.inv_hom_id }
    e.hom.toOrderHom.monotone e.inv.toOrderHom.monotone

theorem iso_eq_iso_refl {x : SimplexCategory} (e : x â‰… x) : e = Iso.refl x := by
  have h : (Finset.univ : Finset (Fin (x.len + 1))).card = x.len + 1 := Finset.card_fin (x.len + 1)
  have eqâ‚ := Finset.orderEmbOfFin_unique' h fun i => Finset.mem_univ ((orderIsoOfIso e) i)
  have eqâ‚‚ :=
    Finset.orderEmbOfFin_unique' h fun i => Finset.mem_univ ((orderIsoOfIso (Iso.refl x)) i)
  ext : 2
  convert congr_arg (fun Ï† => (OrderEmbedding.toOrderHom Ï†)) (eqâ‚.trans eqâ‚‚.symm)
  ext i : 2
  rfl

theorem eq_id_of_isIso {x : SimplexCategory} (f : x âŸ¶ x) [IsIso f] : f = ğŸ™ _ :=
  congr_arg (fun Ï† : _ â‰… _ => Ï†.hom) (iso_eq_iso_refl (asIso f))

theorem eq_Ïƒ_comp_of_not_injective' {n : â„•} {Î”' : SimplexCategory} (Î¸ : mk (n + 1) âŸ¶ Î”')
    (i : Fin (n + 1)) (hi : Î¸.toOrderHom (Fin.castSucc i) = Î¸.toOrderHom i.succ) :
    âˆƒ Î¸' : mk n âŸ¶ Î”', Î¸ = Ïƒ i â‰« Î¸' := by
  use Î´ i.succ â‰« Î¸
  ext x : 3
  simp only [len_mk, Ïƒ, mkHom, comp_toOrderHom, Hom.toOrderHom_mk, OrderHom.comp_coe,
    Function.comp_apply, Fin.predAboveOrderHom_coe]
  by_cases h' : x â‰¤ Fin.castSucc i
  Â· rw [Fin.predAbove_of_le_castSucc i x h']
    dsimp [Î´]
    rw [Fin.succAbove_of_castSucc_lt _ _ _]
    Â· rw [Fin.castSucc_castPred]
    Â· exact (Fin.castSucc_lt_succ_iff.mpr h')
  Â· simp only [not_le] at h'
    let y := x.pred <| by rintro (rfl : x = 0); simp at h'
    have hy : x = y.succ := (Fin.succ_pred x _).symm
    rw [hy] at h' âŠ¢
    rw [Fin.predAbove_of_castSucc_lt i y.succ h', Fin.pred_succ]
    by_cases h'' : y = i
    Â· rw [h'']
      refine hi.symm.trans ?_
      congr 1
      dsimp [Î´]
      rw [Fin.succAbove_of_castSucc_lt i.succ]
      exact Fin.lt_succ
    Â· dsimp [Î´]
      rw [Fin.succAbove_of_le_castSucc i.succ _]
      simp only [Fin.lt_iff_val_lt_val, Fin.le_iff_val_le_val, Fin.val_succ, Fin.coe_castSucc,
        Nat.lt_succ_iff, Fin.ext_iff] at h' h'' âŠ¢
      omega

theorem eq_Ïƒ_comp_of_not_injective {n : â„•} {Î”' : SimplexCategory} (Î¸ : mk (n + 1) âŸ¶ Î”')
    (hÎ¸ : Â¬Function.Injective Î¸.toOrderHom) :
    âˆƒ (i : Fin (n + 1)) (Î¸' : mk n âŸ¶ Î”'), Î¸ = Ïƒ i â‰« Î¸' := by
  simp only [Function.Injective, exists_prop, not_forall] at hÎ¸
  -- as Î¸ is not injective, there exists `x<y` such that `Î¸ x = Î¸ y`
  -- and then, `Î¸ x = Î¸ (x+1)`
  have hÎ¸â‚‚ : âˆƒ x y : Fin (n + 2), (Hom.toOrderHom Î¸) x = (Hom.toOrderHom Î¸) y âˆ§ x < y := by
    rcases hÎ¸ with âŸ¨x, y, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
    by_cases h : x < y
    Â· exact âŸ¨x, y, âŸ¨hâ‚, hâŸ©âŸ©
    Â· refine âŸ¨y, x, âŸ¨hâ‚.symm, ?_âŸ©âŸ©
      omega
  rcases hÎ¸â‚‚ with âŸ¨x, y, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
  use x.castPred ((Fin.le_last _).trans_lt' hâ‚‚).ne
  apply eq_Ïƒ_comp_of_not_injective'
  apply le_antisymm
  Â· exact Î¸.toOrderHom.monotone (le_of_lt (Fin.castSucc_lt_succ _))
  Â· rw [Fin.castSucc_castPred, hâ‚]
    exact Î¸.toOrderHom.monotone ((Fin.succ_castPred_le_iff _).mpr hâ‚‚)

theorem eq_comp_Î´_of_not_surjective' {n : â„•} {Î” : SimplexCategory} (Î¸ : Î” âŸ¶ mk (n + 1))
    (i : Fin (n + 2)) (hi : âˆ€ x, Î¸.toOrderHom x â‰  i) : âˆƒ Î¸' : Î” âŸ¶ mk n, Î¸ = Î¸' â‰« Î´ i := by
  use Î¸ â‰« Ïƒ (.predAbove (.last n) i)
  ext x : 3
  suffices âˆ€ j â‰  i, i.succAbove (((Fin.last n).predAbove i).predAbove j) = j by
    dsimp [Î´, Ïƒ]
    exact .symm <| this _ (hi _)
  intro j hj
  cases i using Fin.lastCases <;> simp [hj]

theorem eq_comp_Î´_of_not_surjective {n : â„•} {Î” : SimplexCategory} (Î¸ : Î” âŸ¶ mk (n + 1))
    (hÎ¸ : Â¬Function.Surjective Î¸.toOrderHom) :
    âˆƒ (i : Fin (n + 2)) (Î¸' : Î” âŸ¶ mk n), Î¸ = Î¸' â‰« Î´ i := by
  obtain âŸ¨i, hiâŸ© := not_forall.mp hÎ¸
  use i
  exact eq_comp_Î´_of_not_surjective' Î¸ i (not_exists.mp hi)

theorem eq_id_of_mono {x : SimplexCategory} (i : x âŸ¶ x) [Mono i] : i = ğŸ™ _ := by
  suffices IsIso i by
    apply eq_id_of_isIso
  apply isIso_of_bijective
  dsimp
  rw [Fintype.bijective_iff_injective_and_card i.toOrderHom, â† mono_iff_injective,
    eq_self_iff_true, and_true]
  infer_instance

theorem eq_id_of_epi {x : SimplexCategory} (i : x âŸ¶ x) [Epi i] : i = ğŸ™ _ := by
  suffices IsIso i from eq_id_of_isIso _
  apply isIso_of_bijective
  dsimp
  rw [Fintype.bijective_iff_surjective_and_card i.toOrderHom, â† epi_iff_surjective,
    eq_self_iff_true, and_true]
  infer_instance

theorem eq_Ïƒ_of_epi {n : â„•} (Î¸ : mk (n + 1) âŸ¶ mk n) [Epi Î¸] : âˆƒ i : Fin (n + 1), Î¸ = Ïƒ i := by
  rcases eq_Ïƒ_comp_of_not_injective Î¸ (by
    by_contra h
    simpa using le_of_mono (mono_iff_injective.mpr h)) with âŸ¨i, Î¸', hâŸ©
  use i
  haveI : Epi (Ïƒ i â‰« Î¸') := by
    rw [â† h]
    infer_instance
  haveI := CategoryTheory.epi_of_epi (Ïƒ i) Î¸'
  rw [h, eq_id_of_epi Î¸', Category.comp_id]

theorem eq_Î´_of_mono {n : â„•} (Î¸ : mk n âŸ¶ mk (n + 1)) [Mono Î¸] : âˆƒ i : Fin (n + 2), Î¸ = Î´ i := by
  rcases eq_comp_Î´_of_not_surjective Î¸ (by
    by_contra h
    simpa using le_of_epi (epi_iff_surjective.mpr h)) with âŸ¨i, Î¸', hâŸ©
  use i
  haveI : Mono (Î¸' â‰« Î´ i) := by
    rw [â† h]
    infer_instance
  haveI := CategoryTheory.mono_of_mono Î¸' (Î´ i)
  rw [h, eq_id_of_mono Î¸', Category.id_comp]

theorem len_lt_of_mono {Î”' Î” : SimplexCategory} (i : Î”' âŸ¶ Î”) [hi : Mono i] (hi' : Î” â‰  Î”') :
    Î”'.len < Î”.len := by
  rcases lt_or_eq_of_le (len_le_of_mono hi) with (h | h)
  Â· exact h
  Â· exfalso
    exact hi' (by ext; exact h.symm)

noncomputable instance : SplitEpiCategory SimplexCategory :=
  skeletalEquivalence.inverse.splitEpiCategoryImpOfIsEquivalence

instance : HasStrongEpiMonoFactorisations SimplexCategory :=
  Functor.hasStrongEpiMonoFactorisations_imp_of_isEquivalence
    SimplexCategory.skeletalEquivalence.inverse

instance : HasStrongEpiImages SimplexCategory :=
  Limits.hasStrongEpiImages_of_hasStrongEpiMonoFactorisations

instance (Î” Î”' : SimplexCategory) (Î¸ : Î” âŸ¶ Î”') : Epi (factorThruImage Î¸) :=
  StrongEpi.epi

theorem image_eq {Î” Î”' Î”'' : SimplexCategory} {Ï† : Î” âŸ¶ Î”''} {e : Î” âŸ¶ Î”'} [Epi e] {i : Î”' âŸ¶ Î”''}
    [Mono i] (fac : e â‰« i = Ï†) : image Ï† = Î”' := by
  haveI := strongEpi_of_epi e
  let e := image.isoStrongEpiMono e i fac
  ext
  exact
    le_antisymm (len_le_of_epi (inferInstance : Epi e.hom))
      (len_le_of_mono (inferInstance : Mono e.hom))

theorem image_Î¹_eq {Î” Î”'' : SimplexCategory} {Ï† : Î” âŸ¶ Î”''} {e : Î” âŸ¶ image Ï†} [Epi e]
    {i : image Ï† âŸ¶ Î”''} [Mono i] (fac : e â‰« i = Ï†) : image.Î¹ Ï† = i := by
  haveI := strongEpi_of_epi e
  rw [â† image.isoStrongEpiMono_hom_comp_Î¹ e i fac,
    SimplexCategory.eq_id_of_isIso (image.isoStrongEpiMono e i fac).hom, Category.id_comp]

theorem factorThruImage_eq {Î” Î”'' : SimplexCategory} {Ï† : Î” âŸ¶ Î”''} {e : Î” âŸ¶ image Ï†} [Epi e]
    {i : image Ï† âŸ¶ Î”''} [Mono i] (fac : e â‰« i = Ï†) : factorThruImage Ï† = e := by
  rw [â† cancel_mono i, fac, â† image_Î¹_eq fac, image.fac]

end EpiMono

/-- This functor `SimplexCategory â¥¤ Cat` sends `â¦‹nâ¦Œ` (for `n : â„•`)
to the category attached to the ordered set `{0, 1, ..., n}` -/
@[simps! obj map]
def toCat : SimplexCategory â¥¤ Cat.{0} :=
  SimplexCategory.skeletalFunctor â‹™ forgetâ‚‚ NonemptyFinLinOrd LinOrd â‹™
      forgetâ‚‚ LinOrd Lat â‹™ forgetâ‚‚ Lat PartOrd â‹™
      forgetâ‚‚ PartOrd Preord â‹™ preordToCat

theorem toCat.obj_eq_Fin (n : â„•) : toCat.obj â¦‹nâ¦Œ = Fin (n + 1) := rfl

instance uniqueHomToZero {Î” : SimplexCategory} : Unique (Î” âŸ¶ â¦‹0â¦Œ) where
  default := Î”.const _ 0
  uniq := eq_const_to_zero

/-- The object `â¦‹0â¦Œ` is terminal in `SimplexCategory`. -/
def isTerminalZero : IsTerminal (â¦‹0â¦Œ : SimplexCategory) :=
  IsTerminal.ofUnique â¦‹0â¦Œ

instance : HasTerminal SimplexCategory :=
  IsTerminal.hasTerminal isTerminalZero

/-- The isomorphism between the terminal object in `SimplexCategory` and `â¦‹0â¦Œ`. -/
noncomputable def topIsoZero : âŠ¤_ SimplexCategory â‰… â¦‹0â¦Œ :=
  terminalIsoIsTerminal isTerminalZero

end SimplexCategory
