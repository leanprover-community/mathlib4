/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz, Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.SimplexCategory.Defs
import Mathlib.Data.Fintype.Sort
import Mathlib.Order.Category.NonemptyFinLinOrd
import Mathlib.Tactic.FinCases
import Mathlib.Tactic.Linarith

/-! # Basic properties of the simplex category

In `Mathlib/AlgebraicTopology/SimplexCategory/Defs.lean`, we define the simplex
category with objects `‚Ñï` and morphisms `n ‚ü∂ m` the monotone maps from
`Fin (n + 1)` to `Fin (m + 1)`.

In this file, we define the generating maps for the simplex category, show that
this category is equivalent to `NonemptyFinLinOrd`, and establish basic
properties of its epimorphisms and monomorphisms.
-/

universe v

open Simplicial CategoryTheory Limits

namespace SimplexCategory

instance {n m : ‚Ñï} : DecidableEq (‚¶ãn‚¶å ‚ü∂ ‚¶ãm‚¶å) := fun a b =>
  decidable_of_iff (a.toOrderHom = b.toOrderHom) SimplexCategory.Hom.ext_iff.symm

section Init

lemma congr_toOrderHom_apply {a b : SimplexCategory} {f g : a ‚ü∂ b} (h : f = g)
    (x : Fin (a.len + 1)) : f.toOrderHom x = g.toOrderHom x := by rw [h]

/-- The constant morphism from ‚¶ã0‚¶å. -/
def const (x y : SimplexCategory) (i : Fin (y.len + 1)) : x ‚ü∂ y :=
  Hom.mk <| ‚ü®fun _ => i, by tauto‚ü©

@[simp]
lemma const_eq_id : const ‚¶ã0‚¶å ‚¶ã0‚¶å 0 = ùüô _ := by aesop

@[simp]
lemma const_apply (x y : SimplexCategory) (i : Fin (y.len + 1)) (a : Fin (x.len + 1)) :
    (const x y i).toOrderHom a = i := rfl

@[simp]
theorem const_comp (x : SimplexCategory) {y z : SimplexCategory}
    (f : y ‚ü∂ z) (i : Fin (y.len + 1)) :
    const x y i ‚â´ f = const x z (f.toOrderHom i) :=
  rfl

theorem const_fac_thru_zero (n m : SimplexCategory) (i : Fin (m.len + 1)) :
    const n m i = const n ‚¶ã0‚¶å 0 ‚â´ SimplexCategory.const ‚¶ã0‚¶å m i := by
  rw [const_comp]; rfl

theorem Hom.ext_zero_left {n : SimplexCategory} (f g : ‚¶ã0‚¶å ‚ü∂ n)
    (h0 : f.toOrderHom 0 = g.toOrderHom 0 := by rfl) : f = g := by
  ext i; match i with | 0 => exact h0 ‚ñ∏ rfl

theorem eq_const_of_zero {n : SimplexCategory} (f : ‚¶ã0‚¶å ‚ü∂ n) :
    f = const _ n (f.toOrderHom 0) := by
  ext x; match x with | 0 => rfl

theorem exists_eq_const_of_zero {n : SimplexCategory} (f : ‚¶ã0‚¶å ‚ü∂ n) :
    ‚àÉ a, f = const _ n a := ‚ü®_, eq_const_of_zero _‚ü©

theorem eq_const_to_zero {n : SimplexCategory} (f : n ‚ü∂ ‚¶ã0‚¶å) :
    f = const n _ 0 := by
  ext : 3
  apply @Subsingleton.elim (Fin 1)

theorem Hom.ext_one_left {n : SimplexCategory} (f g : ‚¶ã1‚¶å ‚ü∂ n)
    (h0 : f.toOrderHom 0 = g.toOrderHom 0 := by rfl)
    (h1 : f.toOrderHom 1 = g.toOrderHom 1 := by rfl) : f = g := by
  ext i
  match i with
  | 0 => exact h0 ‚ñ∏ rfl
  | 1 => exact h1 ‚ñ∏ rfl

theorem eq_of_one_to_one (f : ‚¶ã1‚¶å ‚ü∂ ‚¶ã1‚¶å) :
    (‚àÉ a, f = const ‚¶ã1‚¶å _ a) ‚à® f = ùüô _ := by
  match e0 : f.toOrderHom 0, e1 : f.toOrderHom 1 with
  | 0, 0 | 1, 1 =>
    refine .inl ‚ü®f.toOrderHom 0, ?_‚ü©
    ext i : 3
    match i with
    | 0 => rfl
    | 1 => exact e1.trans e0.symm
  | 0, 1 =>
    right
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 1, 0 =>
    have := f.toOrderHom.monotone (by decide : (0 : Fin 2) ‚â§ 1)
    rw [e0, e1] at this
    exact Not.elim (by decide) this

/-- Make a morphism `‚¶ãn‚¶å ‚ü∂ ‚¶ãm‚¶å` from a monotone map between fin's.
This is useful for constructing morphisms between `‚¶ãn‚¶å` directly
without identifying `n` with `‚¶ãn‚¶å.len`.
-/
@[simp]
def mkHom {n m : ‚Ñï} (f : Fin (n + 1) ‚Üío Fin (m + 1)) : ‚¶ãn‚¶å ‚ü∂ ‚¶ãm‚¶å :=
  SimplexCategory.Hom.mk f

/-- The morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that picks out a specified `h : i ‚â§ j` in `Fin (n+1)`. -/
def mkOfLe {n} (i j : Fin (n + 1)) (h : i ‚â§ j) : ‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  SimplexCategory.mkHom {
    toFun := fun | 0 => i | 1 => j
    monotone' := fun
      | 0, 0, _ | 1, 1, _ => le_rfl
      | 0, 1, _ => h
  }

@[simp]
lemma mkOfLe_refl {n} (j : Fin (n + 1)) :
    mkOfLe j j (by cutsat) = ‚¶ã1‚¶å.const ‚¶ãn‚¶å j := Hom.ext_one_left _ _

/-- The morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that picks out the "diagonal composite" edge -/
def diag (n : ‚Ñï) : ‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  mkOfLe 0 (Fin.last n) (Fin.zero_le _)

/-- The morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that picks out the edge spanning the interval from `j` to `j + l`. -/
def intervalEdge {n} (j l : ‚Ñï) (hjl : j + l ‚â§ n) : ‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  mkOfLe ‚ü®j, (by cutsat)‚ü© ‚ü®j + l, (by cutsat)‚ü© (Nat.le_add_right j l)

/-- The morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that picks out the arrow `i ‚ü∂ i+1` in `Fin (n+1)`. -/
def mkOfSucc {n} (i : Fin n) : ‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  SimplexCategory.mkHom {
    toFun := fun | 0 => i.castSucc | 1 => i.succ
    monotone' := fun
      | 0, 0, _ | 1, 1, _ => le_rfl
      | 0, 1, _ => Fin.castSucc_le_succ i
  }

@[simp]
lemma mkOfSucc_homToOrderHom_zero {n} (i : Fin n) :
    DFunLike.coe (F := Fin 2 ‚Üío Fin (n + 1)) (Hom.toOrderHom (mkOfSucc i)) 0 = i.castSucc := rfl

@[simp]
lemma mkOfSucc_homToOrderHom_one {n} (i : Fin n) :
    DFunLike.coe (F := Fin 2 ‚Üío Fin (n + 1)) (Hom.toOrderHom (mkOfSucc i)) 1 = i.succ := rfl


/-- The morphism `‚¶ã2‚¶å ‚ü∂ ‚¶ãn‚¶å` that picks out a specified composite of morphisms in `Fin (n+1)`. -/
def mkOfLeComp {n} (i j k : Fin (n + 1)) (h‚ÇÅ : i ‚â§ j) (h‚ÇÇ : j ‚â§ k) :
    ‚¶ã2‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  SimplexCategory.mkHom {
    toFun := fun | 0 => i | 1 => j | 2 => k
    monotone' := fun
      | 0, 0, _ | 1, 1, _ | 2, 2, _  => le_rfl
      | 0, 1, _ => h‚ÇÅ
      | 1, 2, _ => h‚ÇÇ
      | 0, 2, _ => Fin.le_trans h‚ÇÅ h‚ÇÇ
  }

/-- The "inert" morphism associated to a subinterval `j ‚â§ i ‚â§ j + l` of `Fin (n + 1)`. -/
def subinterval {n} (j l : ‚Ñï) (hjl : j + l ‚â§ n) :
    ‚¶ãl‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  SimplexCategory.mkHom {
    toFun := fun i => ‚ü®i.1 + j, (by cutsat)‚ü©
    monotone' := fun i i' hii' => by simpa only [Fin.mk_le_mk, add_le_add_iff_right] using hii'
  }

lemma const_subinterval_eq {n} (j l : ‚Ñï) (hjl : j + l ‚â§ n) (i : Fin (l + 1)) :
    ‚¶ã0‚¶å.const ‚¶ãl‚¶å i ‚â´ subinterval j l hjl =
    ‚¶ã0‚¶å.const ‚¶ãn‚¶å ‚ü®j + i.1, lt_add_of_lt_add_right (Nat.add_lt_add_left i.2 j) hjl‚ü©  := by
  rw [const_comp]
  congr
  ext
  dsimp [subinterval]
  rw [add_comm]

@[simp]
lemma mkOfSucc_subinterval_eq {n} (j l : ‚Ñï) (hjl : j + l ‚â§ n) (i : Fin l) :
    mkOfSucc i ‚â´ subinterval j l hjl =
    mkOfSucc ‚ü®j + i.1, Nat.lt_of_lt_of_le (Nat.add_lt_add_left i.2 j) hjl‚ü© := by
  unfold subinterval mkOfSucc
  ext (i : Fin 2)
  match i with | 0 | 1 => simp; cutsat

@[simp]
lemma diag_subinterval_eq {n} (j l : ‚Ñï) (hjl : j + l ‚â§ n) :
    diag l ‚â´ subinterval j l hjl = intervalEdge j l hjl := by
  unfold subinterval intervalEdge diag mkOfLe
  ext (i : Fin 2)
  match i with | 0 | 1 => simp <;> omega

instance (Œî : SimplexCategory) : Subsingleton (Œî ‚ü∂ ‚¶ã0‚¶å) where
  allEq f g := by ext : 3; apply Subsingleton.elim (Œ± := Fin 1)

theorem hom_zero_zero (f : ‚¶ã0‚¶å ‚ü∂ ‚¶ã0‚¶å) : f = ùüô _ := by
  apply Subsingleton.elim

@[simp]
lemma eqToHom_toOrderHom {x y : SimplexCategory} (h : x = y) :
  SimplexCategory.Hom.toOrderHom (eqToHom h) =
    (Fin.castOrderIso (congrArg (fun t ‚Ü¶ t.len + 1) h)).toOrderEmbedding.toOrderHom := by
  subst h
  rfl

end Init

section Generators

/-!
## Generating maps for the simplex category

TODO: prove that the simplex category is equivalent to
one given by the following generators and relations.
-/

/-- The `i`-th face map from `‚¶ãn‚¶å` to `‚¶ãn+1‚¶å` -/
def Œ¥ {n} (i : Fin (n + 2)) : ‚¶ãn‚¶å ‚ü∂ ‚¶ãn + 1‚¶å :=
  mkHom (Fin.succAboveOrderEmb i).toOrderHom

/-- The `i`-th degeneracy map from `‚¶ãn+1‚¶å` to `‚¶ãn‚¶å` -/
def œÉ {n} (i : Fin (n + 1)) : ‚¶ãn + 1‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  mkHom i.predAboveOrderHom

/-- The generic case of the first simplicial identity -/
theorem Œ¥_comp_Œ¥ {n} {i j : Fin (n + 2)} (H : i ‚â§ j) :
    Œ¥ i ‚â´ Œ¥ j.succ = Œ¥ j ‚â´ Œ¥ i.castSucc := by
  ext k
  dsimp [Œ¥, Fin.succAbove]
  rcases i with ‚ü®i, _‚ü©
  rcases j with ‚ü®j, _‚ü©
  rcases k with ‚ü®k, _‚ü©
  split_ifs <;> ¬∑ simp at * <;> omega

theorem Œ¥_comp_Œ¥' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : i.castSucc < j) :
    Œ¥ i ‚â´ Œ¥ j =
      Œ¥ (j.pred fun (hj : j = 0) => by simp [hj, Fin.not_lt_zero] at H) ‚â´
        Œ¥ (Fin.castSucc i) := by
  rw [‚Üê Œ¥_comp_Œ¥]
  ¬∑ rw [Fin.succ_pred]
  ¬∑ simpa only [Fin.le_iff_val_le_val, ‚Üê Nat.lt_succ_iff, Nat.succ_eq_add_one, ‚Üê Fin.val_succ,
      j.succ_pred, Fin.lt_iff_val_lt_val] using H

theorem Œ¥_comp_Œ¥'' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : i ‚â§ Fin.castSucc j) :
    Œ¥ (i.castLT (Nat.lt_of_le_of_lt (Fin.le_iff_val_le_val.mp H) j.is_lt)) ‚â´ Œ¥ j.succ =
      Œ¥ j ‚â´ Œ¥ i := by
  rw [Œ¥_comp_Œ¥]
  ¬∑ rfl
  ¬∑ exact H

/-- The special case of the first simplicial identity -/
@[reassoc]
theorem Œ¥_comp_Œ¥_self {n} {i : Fin (n + 2)} : Œ¥ i ‚â´ Œ¥ i.castSucc = Œ¥ i ‚â´ Œ¥ i.succ :=
  (Œ¥_comp_Œ¥ (le_refl i)).symm

@[reassoc]
theorem Œ¥_comp_Œ¥_self' {n} {i : Fin (n + 2)} {j : Fin (n + 3)} (H : j = i.castSucc) :
    Œ¥ i ‚â´ Œ¥ j = Œ¥ i ‚â´ Œ¥ i.succ := by
  subst H
  rw [Œ¥_comp_Œ¥_self]

/-- The second simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_of_le {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : i ‚â§ j.castSucc) :
    Œ¥ i.castSucc ‚â´ œÉ j.succ = œÉ j ‚â´ Œ¥ i := by
  ext k : 3
  dsimp [œÉ, Œ¥]
  rcases le_or_gt i k with (hik | hik)
  ¬∑ rw [Fin.succAbove_of_le_castSucc _ _ (Fin.castSucc_le_castSucc_iff.mpr hik),
    Fin.succ_predAbove_succ, Fin.succAbove_of_le_castSucc]
    rcases le_or_gt k (j.castSucc) with (hjk | hjk)
    ¬∑ rwa [Fin.predAbove_of_le_castSucc _ _ hjk, Fin.castSucc_castPred]
    ¬∑ rw [Fin.le_castSucc_iff, Fin.predAbove_of_castSucc_lt _ _ hjk, Fin.succ_pred]
      exact H.trans_lt hjk
  ¬∑ rw [Fin.succAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_castSucc_iff.mpr hik)]
    have hjk := H.trans_lt' hik
    rw [Fin.predAbove_of_le_castSucc _ _ (Fin.castSucc_le_castSucc_iff.mpr
      (hjk.trans (Fin.castSucc_lt_succ _)).le),
      Fin.predAbove_of_le_castSucc _ _ hjk.le, Fin.castPred_castSucc, Fin.succAbove_of_castSucc_lt,
      Fin.castSucc_castPred]
    rwa [Fin.castSucc_castPred]

/-- The first part of the third simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_self {n} {i : Fin (n + 1)} :
    Œ¥ (Fin.castSucc i) ‚â´ œÉ i = ùüô ‚¶ãn‚¶å := by
  rcases i with ‚ü®i, hi‚ü©
  ext ‚ü®j, hj‚ü©
  simp? at hj says simp only [len_mk] at hj
  dsimp [œÉ, Œ¥, Fin.predAbove, Fin.succAbove]
  simp only [Fin.lt_iff_val_lt_val, Fin.dite_val, Fin.ite_val, Fin.coe_pred]
  split_ifs
  any_goals simp
  all_goals cutsat

@[reassoc]
theorem Œ¥_comp_œÉ_self' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.castSucc) :
    Œ¥ j ‚â´ œÉ i = ùüô ‚¶ãn‚¶å := by
  subst H
  rw [Œ¥_comp_œÉ_self]

/-- The second part of the third simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_succ {n} {i : Fin (n + 1)} : Œ¥ i.succ ‚â´ œÉ i = ùüô ‚¶ãn‚¶å := by
  ext j
  rcases i with ‚ü®i, _‚ü©
  rcases j with ‚ü®j, _‚ü©
  dsimp [Œ¥, œÉ, Fin.succAbove, Fin.predAbove]
  split_ifs <;> simp <;> simp at * <;> omega

@[reassoc]
theorem Œ¥_comp_œÉ_succ' {n} {j : Fin (n + 2)} {i : Fin (n + 1)} (H : j = i.succ) :
    Œ¥ j ‚â´ œÉ i = ùüô ‚¶ãn‚¶å := by
  subst H
  rw [Œ¥_comp_œÉ_succ]

/-- The fourth simplicial identity -/
@[reassoc]
theorem Œ¥_comp_œÉ_of_gt {n} {i : Fin (n + 2)} {j : Fin (n + 1)} (H : j.castSucc < i) :
    Œ¥ i.succ ‚â´ œÉ j.castSucc = œÉ j ‚â´ Œ¥ i := by
  ext k : 3
  dsimp [Œ¥, œÉ]
  rcases le_or_gt k i with (hik | hik)
  ¬∑ rw [Fin.succAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_succ_iff.mpr hik)]
    rcases le_or_gt k (j.castSucc) with (hjk | hjk)
    ¬∑ rw [Fin.predAbove_of_le_castSucc _ _
      (Fin.castSucc_le_castSucc_iff.mpr hjk), Fin.castPred_castSucc,
      Fin.predAbove_of_le_castSucc _ _ hjk, Fin.succAbove_of_castSucc_lt, Fin.castSucc_castPred]
      rw [Fin.castSucc_castPred]
      exact hjk.trans_lt H
    ¬∑ rw [Fin.predAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_castSucc_iff.mpr hjk),
      Fin.predAbove_of_castSucc_lt _ _ hjk, Fin.succAbove_of_castSucc_lt,
      Fin.castSucc_pred_eq_pred_castSucc]
      rwa [Fin.castSucc_lt_iff_succ_le, Fin.succ_pred]
  ¬∑ rw [Fin.succAbove_of_le_castSucc _ _ (Fin.succ_le_castSucc_iff.mpr hik)]
    have hjk := H.trans hik
    rw [Fin.predAbove_of_castSucc_lt _ _ hjk, Fin.predAbove_of_castSucc_lt _ _
      (Fin.castSucc_lt_succ_iff.mpr hjk.le),
    Fin.pred_succ, Fin.succAbove_of_le_castSucc, Fin.succ_pred]
    rwa [Fin.le_castSucc_pred_iff]

@[reassoc]
theorem Œ¥_comp_œÉ_of_gt' {n} {i : Fin (n + 3)} {j : Fin (n + 2)} (H : j.succ < i) :
    Œ¥ i ‚â´ œÉ j = œÉ (j.castLT ((add_lt_add_iff_right 1).mp (lt_of_lt_of_le H i.is_le))) ‚â´
      Œ¥ (i.pred fun (hi : i = 0) => by simp only [Fin.not_lt_zero, hi] at H) := by
  rw [‚Üê Œ¥_comp_œÉ_of_gt]
  ¬∑ simp
  ¬∑ rw [Fin.castSucc_castLT, ‚Üê Fin.succ_lt_succ_iff, Fin.succ_pred]
    exact H

/-- The fifth simplicial identity -/
@[reassoc]
theorem œÉ_comp_œÉ {n} {i j : Fin (n + 1)} (H : i ‚â§ j) :
    œÉ (Fin.castSucc i) ‚â´ œÉ j = œÉ j.succ ‚â´ œÉ i := by
  ext k : 3
  dsimp [œÉ]
  cases k using Fin.lastCases with
  | last => simp only [len_mk, Fin.predAbove_right_last]
  | cast k =>
    cases k using Fin.cases with
    | zero =>
      simp
    | succ k =>
      rcases le_or_gt i k with (h | h)
      ¬∑ simp_rw [Fin.predAbove_of_castSucc_lt i.castSucc _ (Fin.castSucc_lt_castSucc_iff.mpr
        (Fin.castSucc_lt_succ_iff.mpr h)), ‚Üê Fin.succ_castSucc, Fin.pred_succ,
        Fin.succ_predAbove_succ]
        rw [Fin.predAbove_of_castSucc_lt i _ (Fin.castSucc_lt_succ_iff.mpr _), Fin.pred_succ]
        rcases le_or_gt k j with (hkj | hkj)
        ¬∑ rwa [Fin.predAbove_of_le_castSucc _ _ (Fin.castSucc_le_castSucc_iff.mpr hkj),
          Fin.castPred_castSucc]
        ¬∑ rw [Fin.predAbove_of_castSucc_lt _ _ (Fin.castSucc_lt_castSucc_iff.mpr hkj),
          Fin.le_pred_iff,
          Fin.succ_le_castSucc_iff]
          exact H.trans_lt hkj
      ¬∑ simp_rw [Fin.predAbove_of_le_castSucc i.castSucc _ (Fin.castSucc_le_castSucc_iff.mpr
        (Fin.succ_le_castSucc_iff.mpr h)), Fin.castPred_castSucc, ‚Üê Fin.succ_castSucc,
        Fin.succ_predAbove_succ]
        rw [Fin.predAbove_of_le_castSucc _ k.castSucc
        (Fin.castSucc_le_castSucc_iff.mpr (h.le.trans H)),
        Fin.castPred_castSucc, Fin.predAbove_of_le_castSucc _ k.succ
        (Fin.succ_le_castSucc_iff.mpr (H.trans_lt' h)), Fin.predAbove_of_le_castSucc _ k.succ
        (Fin.succ_le_castSucc_iff.mpr h)]

/--
If `f : ‚¶ãm‚¶å ‚ü∂ ‚¶ãn+1‚¶å` is a morphism and `j` is not in the range of `f`,
then `factor_Œ¥ f j` is a morphism `‚¶ãm‚¶å ‚ü∂ ‚¶ãn‚¶å` such that
`factor_Œ¥ f j ‚â´ Œ¥ j = f` (as witnessed by `factor_Œ¥_spec`).
-/
def factor_Œ¥ {m n : ‚Ñï} (f : ‚¶ãm‚¶å ‚ü∂ ‚¶ãn + 1‚¶å) (j : Fin (n + 2)) : ‚¶ãm‚¶å ‚ü∂ ‚¶ãn‚¶å :=
  f ‚â´ œÉ (Fin.predAbove 0 j)

lemma factor_Œ¥_spec {m n : ‚Ñï} (f : ‚¶ãm‚¶å ‚ü∂ ‚¶ãn + 1‚¶å) (j : Fin (n + 2))
    (hj : ‚àÄ (k : Fin (m + 1)), f.toOrderHom k ‚â† j) :
    factor_Œ¥ f j ‚â´ Œ¥ j = f := by
  ext k : 3
  cases j using Fin.cases <;> simp_all [factor_Œ¥, Œ¥, œÉ]

@[simp]
lemma Œ¥_zero_mkOfSucc {n : ‚Ñï} (i : Fin n) :
    Œ¥ 0 ‚â´ mkOfSucc i = SimplexCategory.const _ ‚¶ãn‚¶å i.succ := by
  ext x
  fin_cases x
  rfl

@[simp]
lemma Œ¥_one_mkOfSucc {n : ‚Ñï} (i : Fin n) :
    Œ¥ 1 ‚â´ mkOfSucc i = SimplexCategory.const _ ‚¶ãn‚¶å i.castSucc := by
  ext x
  fin_cases x
  rfl

/-- If `i + 1 < j`, `mkOfSucc i ‚â´ Œ¥ j` is the morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that
sends `0` and `1` to `i` and `i + 1`, respectively. -/
lemma mkOfSucc_Œ¥_lt {n : ‚Ñï} {i : Fin n} {j : Fin (n + 2)}
    (h : i.succ.castSucc < j) :
    mkOfSucc i ‚â´ Œ¥ j = mkOfSucc i.castSucc := by
  ext x
  fin_cases x
  ¬∑ simp [Œ¥, Fin.succAbove_of_castSucc_lt _ _ (Nat.lt_trans _ h)]
  ¬∑ simp [Œ¥, Fin.succAbove_of_castSucc_lt _ _ h]

/-- If `i + 1 > j`, `mkOfSucc i ‚â´ Œ¥ j` is the morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that
sends `0` and `1` to `i + 1` and `i + 2`, respectively. -/
lemma mkOfSucc_Œ¥_gt {n : ‚Ñï} {i : Fin n} {j : Fin (n + 2)}
    (h : j < i.succ.castSucc) :
    mkOfSucc i ‚â´ Œ¥ j = mkOfSucc i.succ := by
  ext x
  simp only [Œ¥, len_mk, mkHom, comp_toOrderHom, Hom.toOrderHom_mk, OrderHom.comp_coe,
    OrderEmbedding.toOrderHom_coe, Function.comp_apply, Fin.succAboveOrderEmb_apply]
  fin_cases x <;> rw [Fin.succAbove_of_le_castSucc]
  ¬∑ rfl
  ¬∑ exact Nat.le_of_lt_succ h
  ¬∑ rfl
  ¬∑ exact Nat.le_of_lt h

/-- If `i + 1 = j`, `mkOfSucc i ‚â´ Œ¥ j` is the morphism `‚¶ã1‚¶å ‚ü∂ ‚¶ãn‚¶å` that
sends `0` and `1` to `i` and `i + 2`, respectively. -/
lemma mkOfSucc_Œ¥_eq {n : ‚Ñï} {i : Fin n} {j : Fin (n + 2)}
    (h : j = i.succ.castSucc) :
    mkOfSucc i ‚â´ Œ¥ j = intervalEdge i 2 (by cutsat) := by
  ext x
  fin_cases x
  ¬∑ subst h
    simp only [Œ¥, len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom, Hom.toOrderHom_mk,
      Fin.zero_eta, OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe, Function.comp_apply,
      mkOfSucc_homToOrderHom_zero, Fin.succAboveOrderEmb_apply,
      Fin.castSucc_succAbove_castSucc, Fin.succAbove_succ_self]
    rfl
  ¬∑ simp only [Œ¥, len_mk, Nat.reduceAdd, mkHom, comp_toOrderHom, Hom.toOrderHom_mk, Fin.mk_one,
      OrderHom.comp_coe, OrderEmbedding.toOrderHom_coe, Function.comp_apply,
      mkOfSucc_homToOrderHom_one, Fin.succAboveOrderEmb_apply]
    subst h
    rw [Fin.succAbove_castSucc_self]
    rfl

theorem eq_of_one_to_two (f : ‚¶ã1‚¶å ‚ü∂ ‚¶ã2‚¶å) :
    (‚àÉ i, f = (Œ¥ (n := 1) i)) ‚à® ‚àÉ a, f = SimplexCategory.const _ _ a := by
  have : f.toOrderHom 0 ‚â§ f.toOrderHom 1 := f.toOrderHom.monotone (by decide : (0 : Fin 2) ‚â§ 1)
  match e0 : f.toOrderHom 0, e1 : f.toOrderHom 1 with
  | 1, 2 =>
    refine .inl ‚ü®0, ?_‚ü©
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 0, 2 =>
    refine .inl ‚ü®1, ?_‚ü©
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 0, 1 =>
    refine .inl ‚ü®2, ?_‚ü©
    ext i : 3
    match i with
    | 0 => exact e0
    | 1 => exact e1
  | 0, 0 | 1, 1 | 2, 2 =>
    refine .inr ‚ü®f.toOrderHom 0, ?_‚ü©
    ext i : 3
    match i with
    | 0 => rfl
    | 1 => exact e1.trans e0.symm
  | 1, 0 | 2, 0 | 2, 1 =>
    rw [e0, e1] at this
    exact Not.elim (by decide) this

theorem eq_of_one_to_two' (f : ‚¶ã1‚¶å ‚ü∂ ‚¶ã2‚¶å) :
    f = (Œ¥ (n := 1) 0) ‚à® f = (Œ¥ (n := 1) 1) ‚à® f = (Œ¥ (n := 1) 2) ‚à®
      ‚àÉ a, f = SimplexCategory.const _ _ a :=
  match eq_of_one_to_two f with
  | .inl ‚ü®0, h‚ü© => .inl h
  | .inl ‚ü®1, h‚ü© => .inr (.inl h)
  | .inl ‚ü®2, h‚ü© => .inr (.inr (.inl h))
  | .inr h => .inr (.inr (.inr h))

end Generators

section Skeleton

/-- The functor that exhibits `SimplexCategory` as skeleton
of `NonemptyFinLinOrd` -/
@[simps obj map]
def skeletalFunctor : SimplexCategory ‚•§ NonemptyFinLinOrd where
  obj a := NonemptyFinLinOrd.of (Fin (a.len + 1))
  map f := NonemptyFinLinOrd.ofHom f.toOrderHom

theorem skeletalFunctor.coe_map {Œî‚ÇÅ Œî‚ÇÇ : SimplexCategory} (f : Œî‚ÇÅ ‚ü∂ Œî‚ÇÇ) :
    ‚Üë(skeletalFunctor.map f).hom.hom = f.toOrderHom :=
  rfl

theorem skeletal : Skeletal SimplexCategory := fun X Y ‚ü®I‚ü© => by
  suffices Fintype.card (Fin (X.len + 1)) = Fintype.card (Fin (Y.len + 1)) by
    ext
    simpa
  apply Fintype.card_congr
  exact ((skeletalFunctor ‚ãô forget NonemptyFinLinOrd).mapIso I).toEquiv

namespace SkeletalFunctor

instance : skeletalFunctor.Full where
  map_surjective f := ‚ü®SimplexCategory.Hom.mk f.hom.hom, rfl‚ü©

instance : skeletalFunctor.Faithful where
  map_injective {_ _ f g} h := by
    ext : 3
    exact CategoryTheory.congr_fun h _

instance : skeletalFunctor.EssSurj where
  mem_essImage X :=
    ‚ü®‚¶ã(Fintype.card X - 1 : ‚Ñï)‚¶å,
      ‚ü®by
        have aux : Fintype.card X = Fintype.card X - 1 + 1 :=
          (Nat.succ_pred_eq_of_pos <| Fintype.card_pos_iff.mpr ‚ü®‚ä•‚ü©).symm
        let f := monoEquivOfFin X aux
        have hf := (Finset.univ.orderEmbOfFin aux).strictMono
        refine
          { hom := InducedCategory.homMk (LinOrd.ofHom ‚ü®f, hf.monotone‚ü©)
            inv := InducedCategory.homMk (LinOrd.ofHom ‚ü®f.symm, ?_‚ü©)
            hom_inv_id := by ext; apply f.symm_apply_apply
            inv_hom_id := by ext; apply f.apply_symm_apply }
        intro i j h
        change f.symm i ‚â§ f.symm j
        rw [‚Üê hf.le_iff_le]
        change f (f.symm i) ‚â§ f (f.symm j)
        simpa only [OrderIso.apply_symm_apply]‚ü©‚ü©

noncomputable instance isEquivalence : skeletalFunctor.IsEquivalence where

end SkeletalFunctor

/-- The equivalence that exhibits `SimplexCategory` as skeleton
of `NonemptyFinLinOrd` -/
noncomputable def skeletalEquivalence : SimplexCategory ‚âå NonemptyFinLinOrd :=
  Functor.asEquivalence skeletalFunctor

end Skeleton

/-- `SimplexCategory` is a skeleton of `NonemptyFinLinOrd`.
-/
lemma isSkeletonOf :
    IsSkeletonOf NonemptyFinLinOrd SimplexCategory skeletalFunctor where
  skel := skeletal
  eqv := SkeletalFunctor.isEquivalence

section Concrete

instance : ConcreteCategory SimplexCategory (fun i j => Fin (i.len + 1) ‚Üío Fin (j.len + 1)) where
  hom := Hom.toOrderHom
  ofHom f := Hom.mk f

instance (x : SimplexCategory) : Fintype (ToType x) :=
  inferInstanceAs (Fintype (Fin _))

instance (x : SimplexCategory) (n : ‚Ñï) : OfNat (ToType x) n :=
  inferInstanceAs (OfNat (Fin _) n)

lemma toType_apply (x : SimplexCategory) : ToType x = Fin (x.len + 1) := rfl

@[simp]
lemma concreteCategoryHom_id (n : SimplexCategory) : ConcreteCategory.hom (ùüô n) = .id := rfl

end Concrete

section EpiMono

/-- A morphism in `SimplexCategory` is a monomorphism precisely when it is an injective function
-/
theorem mono_iff_injective {n m : SimplexCategory} {f : n ‚ü∂ m} :
    Mono f ‚Üî Function.Injective f.toOrderHom := by
  rw [‚Üê Functor.mono_map_iff_mono skeletalEquivalence.functor]
  dsimp only [skeletalEquivalence, Functor.asEquivalence_functor]
  simp only [skeletalFunctor_obj, skeletalFunctor_map,
    NonemptyFinLinOrd.mono_iff_injective, NonemptyFinLinOrd.coe_of, ConcreteCategory.hom_ofHom]

/-- A morphism in `SimplexCategory` is an epimorphism if and only if it is a surjective function
-/
theorem epi_iff_surjective {n m : SimplexCategory} {f : n ‚ü∂ m} :
    Epi f ‚Üî Function.Surjective f.toOrderHom := by
  rw [‚Üê Functor.epi_map_iff_epi skeletalEquivalence.functor]
  dsimp only [skeletalEquivalence, Functor.asEquivalence_functor]
  simp only [skeletalFunctor_obj, skeletalFunctor_map,
    NonemptyFinLinOrd.epi_iff_surjective, NonemptyFinLinOrd.coe_of, ConcreteCategory.hom_ofHom]

/-- A monomorphism in `SimplexCategory` must increase lengths -/
theorem len_le_of_mono {x y : SimplexCategory} (f : x ‚ü∂ y) [Mono f] : x.len ‚â§ y.len := by
  simpa using Fintype.card_le_of_injective f.toOrderHom.toFun
    (by dsimp; rwa [‚Üê mono_iff_injective])

theorem le_of_mono {n m : ‚Ñï} (f : ‚¶ãn‚¶å ‚ü∂ ‚¶ãm‚¶å) [Mono f] : n ‚â§ m :=
  len_le_of_mono f

/-- An epimorphism in `SimplexCategory` must decrease lengths -/
theorem len_le_of_epi {x y : SimplexCategory} (f : x ‚ü∂ y) [Epi f] : y.len ‚â§ x.len := by
  simpa using Fintype.card_le_of_surjective f.toOrderHom.toFun
    (by dsimp; rwa [‚Üê epi_iff_surjective])

theorem le_of_epi {n m : ‚Ñï} (f : ‚¶ãn‚¶å ‚ü∂ ‚¶ãm‚¶å) [Epi f] : m ‚â§ n := len_le_of_epi f

lemma len_eq_of_isIso {x y : SimplexCategory} (f : x ‚ü∂ y) [IsIso f] : x.len = y.len :=
  le_antisymm (len_le_of_mono f) (len_le_of_epi f)

lemma eq_of_isIso {n m : ‚Ñï} (f : ‚¶ãn‚¶å ‚ü∂ ‚¶ãm‚¶å) [IsIso f] : n = m :=
  len_eq_of_isIso f

instance {n : ‚Ñï} {i : Fin (n + 1)} : Epi (œÉ i) := by
  simpa only [epi_iff_surjective] using Fin.predAbove_surjective i

instance : (forget SimplexCategory).ReflectsIsomorphisms :=
  ‚ü®fun f hf =>
    Iso.isIso_hom
      { hom := f
        inv := Hom.mk
            { toFun := inv ((forget SimplexCategory).map f)
              monotone' := fun y‚ÇÅ y‚ÇÇ h => by
                by_cases h' : y‚ÇÅ < y‚ÇÇ
                ¬∑ by_contra h''
                  apply not_le.mpr h'
                  convert f.toOrderHom.monotone (le_of_not_ge h'')
                  all_goals
                    exact (congr_hom (Iso.inv_hom_id
                      (asIso ((forget SimplexCategory).map f))) _).symm
                ¬∑ rw [eq_of_le_of_not_lt h h'] }
        hom_inv_id := by
          ext1
          ext1
          exact Iso.hom_inv_id (asIso ((forget _).map f))
        inv_hom_id := by
          ext1
          ext1
          exact Iso.inv_hom_id (asIso ((forget _).map f)) }‚ü©

theorem isIso_of_bijective {x y : SimplexCategory} {f : x ‚ü∂ y}
    (hf : Function.Bijective f.toOrderHom.toFun) : IsIso f :=
  haveI : IsIso ((forget SimplexCategory).map f) := (isIso_iff_bijective _).mpr hf
  isIso_of_reflects_iso f (forget SimplexCategory)

lemma isIso_iff_of_mono {n m : SimplexCategory} (f : n ‚ü∂ m) [hf : Mono f] :
    IsIso f ‚Üî n.len = m.len := by
  refine ‚ü®fun _ ‚Ü¶ len_eq_of_isIso f, fun h ‚Ü¶ ?_‚ü©
  obtain rfl : n = m := by aesop
  rw [mono_iff_injective] at hf
  exact isIso_of_bijective ‚ü®hf, by rwa [‚Üê Finite.injective_iff_surjective]‚ü©

instance {n : ‚Ñï} {i : Fin (n + 2)} : Mono (Œ¥ i) := by
  rw [mono_iff_injective]
  exact Fin.succAbove_right_injective

lemma isIso_iff_of_epi {n m : SimplexCategory} (f : n ‚ü∂ m) [hf : Epi f] :
    IsIso f ‚Üî n.len = m.len := by
  refine ‚ü®fun _ ‚Ü¶ len_eq_of_isIso f, fun h ‚Ü¶ ?_‚ü©
  obtain rfl : n = m := by aesop
  rw [epi_iff_surjective] at hf
  exact isIso_of_bijective ‚ü®by rwa [Finite.injective_iff_surjective], hf‚ü©

instance : Balanced SimplexCategory where
  isIso_of_mono_of_epi f _ _ := by
    rw [isIso_iff_of_epi]
    exact le_antisymm (len_le_of_mono f) (len_le_of_epi f)

/-- An isomorphism in `SimplexCategory` induces an `OrderIso`. -/
@[simp]
def orderIsoOfIso {x y : SimplexCategory} (e : x ‚âÖ y) : Fin (x.len + 1) ‚âÉo Fin (y.len + 1) :=
  Equiv.toOrderIso
    { toFun := e.hom.toOrderHom
      invFun := e.inv.toOrderHom
      left_inv := fun i => by
        simpa only using congr_arg (fun œÜ => (Hom.toOrderHom œÜ) i) e.hom_inv_id
      right_inv := fun i => by
        simpa only using congr_arg (fun œÜ => (Hom.toOrderHom œÜ) i) e.inv_hom_id }
    e.hom.toOrderHom.monotone e.inv.toOrderHom.monotone

theorem iso_eq_iso_refl {x : SimplexCategory} (e : x ‚âÖ x) : e = Iso.refl x := by
  have h : (Finset.univ : Finset (Fin (x.len + 1))).card = x.len + 1 := Finset.card_fin (x.len + 1)
  have eq‚ÇÅ := Finset.orderEmbOfFin_unique' h fun i => Finset.mem_univ ((orderIsoOfIso e) i)
  have eq‚ÇÇ :=
    Finset.orderEmbOfFin_unique' h fun i => Finset.mem_univ ((orderIsoOfIso (Iso.refl x)) i)
  ext : 2
  convert congr_arg (fun œÜ => (OrderEmbedding.toOrderHom œÜ)) (eq‚ÇÅ.trans eq‚ÇÇ.symm)
  ext i : 2
  rfl

theorem eq_id_of_isIso {x : SimplexCategory} (f : x ‚ü∂ x) [IsIso f] : f = ùüô _ :=
  congr_arg (fun œÜ : _ ‚âÖ _ => œÜ.hom) (iso_eq_iso_refl (asIso f))

theorem eq_œÉ_comp_of_not_injective' {n : ‚Ñï} {Œî' : SimplexCategory} (Œ∏ : ‚¶ãn + 1‚¶å ‚ü∂ Œî')
    (i : Fin (n + 1)) (hi : Œ∏.toOrderHom (Fin.castSucc i) = Œ∏.toOrderHom i.succ) :
    ‚àÉ Œ∏' : ‚¶ãn‚¶å ‚ü∂ Œî', Œ∏ = œÉ i ‚â´ Œ∏' := by
  use Œ¥ i.succ ‚â´ Œ∏
  ext x : 3
  simp only [len_mk, œÉ, mkHom, comp_toOrderHom, Hom.toOrderHom_mk, OrderHom.comp_coe,
    Function.comp_apply, Fin.predAboveOrderHom_coe]
  by_cases h' : x ‚â§ Fin.castSucc i
  ¬∑ rw [Fin.predAbove_of_le_castSucc i x h']
    dsimp [Œ¥]
    rw [Fin.succAbove_of_castSucc_lt _ _ _]
    ¬∑ rw [Fin.castSucc_castPred]
    ¬∑ exact (Fin.castSucc_lt_succ_iff.mpr h')
  ¬∑ simp only [not_le] at h'
    let y := x.pred <| by rintro (rfl : x = 0); simp at h'
    have hy : x = y.succ := (Fin.succ_pred x _).symm
    rw [hy] at h' ‚ä¢
    rw [Fin.predAbove_of_castSucc_lt i y.succ h', Fin.pred_succ]
    by_cases h'' : y = i
    ¬∑ rw [h'']
      refine hi.symm.trans ?_
      congr 1
      dsimp [Œ¥]
      rw [Fin.succAbove_of_castSucc_lt i.succ]
      exact Fin.lt_succ
    ¬∑ dsimp [Œ¥]
      rw [Fin.succAbove_of_le_castSucc i.succ _]
      simp only [Fin.lt_iff_val_lt_val, Fin.le_iff_val_le_val, Fin.val_succ, Fin.coe_castSucc,
        Nat.lt_succ_iff, Fin.ext_iff] at h' h'' ‚ä¢
      cutsat

theorem eq_œÉ_comp_of_not_injective {n : ‚Ñï} {Œî' : SimplexCategory} (Œ∏ : ‚¶ãn + 1‚¶å ‚ü∂ Œî')
    (hŒ∏ : ¬¨Function.Injective Œ∏.toOrderHom) :
    ‚àÉ (i : Fin (n + 1)) (Œ∏' : ‚¶ãn‚¶å ‚ü∂ Œî'), Œ∏ = œÉ i ‚â´ Œ∏' := by
  simp only [Function.Injective, exists_prop, not_forall] at hŒ∏
  -- as Œ∏ is not injective, there exists `x<y` such that `Œ∏ x = Œ∏ y`
  -- and then, `Œ∏ x = Œ∏ (x+1)`
  have hŒ∏‚ÇÇ : ‚àÉ x y : Fin (n + 2), (Hom.toOrderHom Œ∏) x = (Hom.toOrderHom Œ∏) y ‚àß x < y := by
    rcases hŒ∏ with ‚ü®x, y, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
    by_cases h : x < y
    ¬∑ exact ‚ü®x, y, ‚ü®h‚ÇÅ, h‚ü©‚ü©
    ¬∑ refine ‚ü®y, x, ‚ü®h‚ÇÅ.symm, ?_‚ü©‚ü©
      cutsat
  rcases hŒ∏‚ÇÇ with ‚ü®x, y, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©‚ü©
  use x.castPred ((Fin.le_last _).trans_lt' h‚ÇÇ).ne
  apply eq_œÉ_comp_of_not_injective'
  apply le_antisymm
  ¬∑ exact Œ∏.toOrderHom.monotone (le_of_lt (Fin.castSucc_lt_succ _))
  ¬∑ rw [Fin.castSucc_castPred, h‚ÇÅ]
    exact Œ∏.toOrderHom.monotone ((Fin.succ_castPred_le_iff _).mpr h‚ÇÇ)

theorem eq_comp_Œ¥_of_not_surjective' {n : ‚Ñï} {Œî : SimplexCategory} (Œ∏ : Œî ‚ü∂ ‚¶ãn + 1‚¶å)
    (i : Fin (n + 2)) (hi : ‚àÄ x, Œ∏.toOrderHom x ‚â† i) : ‚àÉ Œ∏' : Œî ‚ü∂ ‚¶ãn‚¶å, Œ∏ = Œ∏' ‚â´ Œ¥ i := by
  use Œ∏ ‚â´ œÉ (.predAbove (.last n) i)
  ext x : 3
  suffices ‚àÄ j ‚â† i, i.succAbove (((Fin.last n).predAbove i).predAbove j) = j by
    dsimp [Œ¥, œÉ]
    exact .symm <| this _ (hi _)
  intro j hj
  cases i using Fin.lastCases <;> simp [hj]

theorem eq_comp_Œ¥_of_not_surjective {n : ‚Ñï} {Œî : SimplexCategory} (Œ∏ : Œî ‚ü∂ ‚¶ãn + 1‚¶å)
    (hŒ∏ : ¬¨Function.Surjective Œ∏.toOrderHom) :
    ‚àÉ (i : Fin (n + 2)) (Œ∏' : Œî ‚ü∂ ‚¶ãn‚¶å), Œ∏ = Œ∏' ‚â´ Œ¥ i := by
  obtain ‚ü®i, hi‚ü© := not_forall.mp hŒ∏
  use i
  exact eq_comp_Œ¥_of_not_surjective' Œ∏ i (not_exists.mp hi)

theorem eq_id_of_mono {x : SimplexCategory} (i : x ‚ü∂ x) [Mono i] : i = ùüô _ := by
  suffices IsIso i by
    apply eq_id_of_isIso
  apply isIso_of_bijective
  dsimp
  rw [Fintype.bijective_iff_injective_and_card i.toOrderHom, ‚Üê mono_iff_injective,
    eq_self_iff_true, and_true]
  infer_instance

theorem eq_id_of_epi {x : SimplexCategory} (i : x ‚ü∂ x) [Epi i] : i = ùüô _ := by
  suffices IsIso i from eq_id_of_isIso _
  apply isIso_of_bijective
  dsimp
  rw [Fintype.bijective_iff_surjective_and_card i.toOrderHom, ‚Üê epi_iff_surjective,
    eq_self_iff_true, and_true]
  infer_instance

theorem eq_œÉ_of_epi {n : ‚Ñï} (Œ∏ : ‚¶ãn + 1‚¶å ‚ü∂ ‚¶ãn‚¶å) [Epi Œ∏] : ‚àÉ i : Fin (n + 1), Œ∏ = œÉ i := by
  obtain ‚ü®i, Œ∏', h‚ü© := eq_œÉ_comp_of_not_injective Œ∏ (by
    rw [‚Üê mono_iff_injective]
    grind [‚Üí le_of_mono])
  use i
  haveI : Epi (œÉ i ‚â´ Œ∏') := by
    rw [‚Üê h]
    infer_instance
  haveI := CategoryTheory.epi_of_epi (œÉ i) Œ∏'
  rw [h, eq_id_of_epi Œ∏', Category.comp_id]

theorem eq_Œ¥_of_mono {n : ‚Ñï} (Œ∏ : ‚¶ãn‚¶å ‚ü∂ ‚¶ãn + 1‚¶å) [Mono Œ∏] : ‚àÉ i : Fin (n + 2), Œ∏ = Œ¥ i := by
  obtain ‚ü®i, Œ∏', h‚ü© := eq_comp_Œ¥_of_not_surjective Œ∏ (by
    rw [‚Üê epi_iff_surjective]
    grind [‚Üí le_of_epi])
  use i
  haveI : Mono (Œ∏' ‚â´ Œ¥ i) := by
    rw [‚Üê h]
    infer_instance
  haveI := CategoryTheory.mono_of_mono Œ∏' (Œ¥ i)
  rw [h, eq_id_of_mono Œ∏', Category.id_comp]

theorem len_lt_of_mono {Œî' Œî : SimplexCategory} (i : Œî' ‚ü∂ Œî) [Mono i] (hi' : Œî ‚â† Œî') :
    Œî'.len < Œî.len := by
  grind [‚Üí len_le_of_mono, SimplexCategory.ext]

noncomputable instance : SplitEpiCategory SimplexCategory :=
  skeletalEquivalence.inverse.splitEpiCategoryImpOfIsEquivalence

instance : HasStrongEpiMonoFactorisations SimplexCategory :=
  Functor.hasStrongEpiMonoFactorisations_imp_of_isEquivalence
    SimplexCategory.skeletalEquivalence.inverse

instance : HasStrongEpiImages SimplexCategory :=
  Limits.hasStrongEpiImages_of_hasStrongEpiMonoFactorisations

instance (Œî Œî' : SimplexCategory) (Œ∏ : Œî ‚ü∂ Œî') : Epi (factorThruImage Œ∏) :=
  StrongEpi.epi

theorem image_eq {Œî Œî' Œî'' : SimplexCategory} {œÜ : Œî ‚ü∂ Œî''} {e : Œî ‚ü∂ Œî'} [Epi e] {i : Œî' ‚ü∂ Œî''}
    [Mono i] (fac : e ‚â´ i = œÜ) : image œÜ = Œî' := by
  haveI := strongEpi_of_epi e
  let e := image.isoStrongEpiMono e i fac
  ext
  exact le_antisymm (len_le_of_epi e.hom) (len_le_of_mono e.hom)

theorem image_Œπ_eq {Œî Œî'' : SimplexCategory} {œÜ : Œî ‚ü∂ Œî''} {e : Œî ‚ü∂ image œÜ} [Epi e]
    {i : image œÜ ‚ü∂ Œî''} [Mono i] (fac : e ‚â´ i = œÜ) : image.Œπ œÜ = i := by
  haveI := strongEpi_of_epi e
  rw [‚Üê image.isoStrongEpiMono_hom_comp_Œπ e i fac,
    SimplexCategory.eq_id_of_isIso (image.isoStrongEpiMono e i fac).hom, Category.id_comp]

theorem factorThruImage_eq {Œî Œî'' : SimplexCategory} {œÜ : Œî ‚ü∂ Œî''} {e : Œî ‚ü∂ image œÜ} [Epi e]
    {i : image œÜ ‚ü∂ Œî''} [Mono i] (fac : e ‚â´ i = œÜ) : factorThruImage œÜ = e := by
  rw [‚Üê cancel_mono i, fac, ‚Üê image_Œπ_eq fac, image.fac]

end EpiMono

/-- This functor `SimplexCategory ‚•§ Cat` sends `‚¶ãn‚¶å` (for `n : ‚Ñï`)
to the category attached to the ordered set `{0, 1, ..., n}` -/
@[simps! obj map]
def toCat : SimplexCategory ‚•§ Cat.{0} :=
  SimplexCategory.skeletalFunctor ‚ãô forget‚ÇÇ NonemptyFinLinOrd LinOrd ‚ãô
      forget‚ÇÇ LinOrd Lat ‚ãô forget‚ÇÇ Lat PartOrd ‚ãô
      forget‚ÇÇ PartOrd Preord ‚ãô preordToCat

theorem toCat.obj_eq_Fin (n : ‚Ñï) : toCat.obj ‚¶ãn‚¶å = Fin (n + 1) := rfl

instance uniqueHomToZero {Œî : SimplexCategory} : Unique (Œî ‚ü∂ ‚¶ã0‚¶å) where
  default := Œî.const _ 0
  uniq := eq_const_to_zero

/-- The object `‚¶ã0‚¶å` is terminal in `SimplexCategory`. -/
def isTerminalZero : IsTerminal (‚¶ã0‚¶å : SimplexCategory) :=
  IsTerminal.ofUnique ‚¶ã0‚¶å

instance : HasTerminal SimplexCategory :=
  IsTerminal.hasTerminal isTerminalZero

/-- The isomorphism between the terminal object in `SimplexCategory` and `‚¶ã0‚¶å`. -/
noncomputable def topIsoZero : ‚ä§_ SimplexCategory ‚âÖ ‚¶ã0‚¶å :=
  terminalIsoIsTerminal isTerminalZero

lemma Œ¥_injective {n : ‚Ñï} : Function.Injective (Œ¥ (n := n)) := by
  intro i j hij
  rw [‚Üê Fin.succAbove_left_inj]
  ext k : 1
  exact congr($hij k)

lemma œÉ_injective {n : ‚Ñï} : Function.Injective (œÉ (n := n)) := by
  intro i j hij
  rw [‚Üê Fin.predAbove_left_inj]
  ext k : 1
  exact congr($hij k)

end SimplexCategory
