/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz
-/
import Mathlib.CategoryTheory.Opposites
import Mathlib.Order.Fin.Basic

/-! # The simplex category

We construct a skeletal model of the simplex category, with objects `â„•` and the
morphisms `n âŸ¶ m` being the monotone maps from `Fin (n + 1)` to `Fin (m + 1)`.

In `Mathlib/AlgebraicTopology/SimplexCategory/Basic.lean`, we show that this category
is equivalent to `NonemptyFinLinOrd`.

## Remarks

The definitions `SimplexCategory` and `SimplexCategory.Hom` are marked as irreducible.

We provide the following functions to work with these objects:
1. `SimplexCategory.mk` creates an object of `SimplexCategory` out of a natural number.
  Use the notation `â¦‹nâ¦Œ` in the `Simplicial` locale.
2. `SimplexCategory.len` gives the "length" of an object of `SimplexCategory`, as a natural.
3. `SimplexCategory.Hom.mk` makes a morphism out of a monotone map between `Fin`'s.
4. `SimplexCategory.Hom.toOrderHom` gives the underlying monotone map associated to a
  term of `SimplexCategory.Hom`.

## Notations

* `â¦‹nâ¦Œ` denotes the `n`-dimensional simplex. This notation is available with
  `open Simplicial`.
* `â¦‹mâ¦Œâ‚™` (defined in `Mathlib.Tactic.SimplexCategory`) denotes the
  `m`-dimensional simplex in the `n`-truncated simplex category. The truncation
  proof `p : m â‰¤ n` can also be provided using the syntax `â¦‹m, pâ¦Œâ‚™`. This
  notation is available with `open SimplexCategory.Truncated`.
-/

universe v

open CategoryTheory

/-- The simplex category:
* objects are natural numbers `n : â„•`
* morphisms from `n` to `m` are monotone functions `Fin (n+1) â†’ Fin (m+1)`
-/
def SimplexCategory :=
  â„•

namespace SimplexCategory

-- The definition of `SimplexCategory` is made irreducible below.
/-- Interpret a natural number as an object of the simplex category. -/
def mk (n : â„•) : SimplexCategory :=
  n

/-- the `n`-dimensional simplex can be denoted `â¦‹nâ¦Œ` -/
scoped[Simplicial] notation "â¦‹" n "â¦Œ" => SimplexCategory.mk n

-- TODO: Make `len` irreducible.
/-- The length of an object of `SimplexCategory`. -/
def len (n : SimplexCategory) : â„• :=
  n

@[ext]
theorem ext (a b : SimplexCategory) : a.len = b.len â†’ a = b :=
  id

attribute [irreducible] SimplexCategory

open Simplicial

@[simp]
theorem len_mk (n : â„•) : â¦‹nâ¦Œ.len = n :=
  rfl

@[simp]
theorem mk_len (n : SimplexCategory) : â¦‹n.lenâ¦Œ = n :=
  rfl

/-- A recursor for `SimplexCategory`. Use it as `induction Î” using SimplexCategory.rec`. -/
protected def rec {F : SimplexCategory â†’ Sort*} (h : âˆ€ n : â„•, F â¦‹nâ¦Œ) : âˆ€ X, F X := fun n =>
  h n.len

/-- Morphisms in the `SimplexCategory`. -/
protected def Hom (a b : SimplexCategory) :=
  Fin (a.len + 1) â†’o Fin (b.len + 1)

namespace Hom

/-- Make a morphism in `SimplexCategory` from a monotone map of `Fin`'s. -/
def mk {a b : SimplexCategory} (f : Fin (a.len + 1) â†’o Fin (b.len + 1)) : SimplexCategory.Hom a b :=
  f

/-- Recover the monotone map from a morphism in the simplex category. -/
def toOrderHom {a b : SimplexCategory} (f : SimplexCategory.Hom a b) :
    Fin (a.len + 1) â†’o Fin (b.len + 1) :=
  f

theorem ext' {a b : SimplexCategory} (f g : SimplexCategory.Hom a b) :
    f.toOrderHom = g.toOrderHom â†’ f = g :=
  id

attribute [irreducible] SimplexCategory.Hom

@[simp]
theorem mk_toOrderHom {a b : SimplexCategory} (f : SimplexCategory.Hom a b) : mk f.toOrderHom = f :=
  rfl

@[simp]
theorem toOrderHom_mk {a b : SimplexCategory} (f : Fin (a.len + 1) â†’o Fin (b.len + 1)) :
    (mk f).toOrderHom = f :=
  rfl

theorem mk_toOrderHom_apply {a b : SimplexCategory} (f : Fin (a.len + 1) â†’o Fin (b.len + 1))
    (i : Fin (a.len + 1)) : (mk f).toOrderHom i = f i :=
  rfl

/-- Identity morphisms of `SimplexCategory`. -/
@[simp]
def id (a : SimplexCategory) : SimplexCategory.Hom a a :=
  mk OrderHom.id

/-- Composition of morphisms of `SimplexCategory`. -/
@[simp]
def comp {a b c : SimplexCategory} (f : SimplexCategory.Hom b c) (g : SimplexCategory.Hom a b) :
    SimplexCategory.Hom a c :=
  mk <| f.toOrderHom.comp g.toOrderHom

end Hom

instance smallCategory : SmallCategory.{0} SimplexCategory where
  Hom n m := SimplexCategory.Hom n m
  id _ := SimplexCategory.Hom.id _
  comp f g := SimplexCategory.Hom.comp g f

@[simp]
lemma id_toOrderHom (a : SimplexCategory) :
    Hom.toOrderHom (ðŸ™ a) = OrderHom.id := rfl

@[simp]
lemma comp_toOrderHom {a b c : SimplexCategory} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (f â‰« g).toOrderHom = g.toOrderHom.comp f.toOrderHom := rfl

@[ext]
theorem Hom.ext {a b : SimplexCategory} (f g : a âŸ¶ b) :
    f.toOrderHom = g.toOrderHom â†’ f = g :=
  Hom.ext' _ _

/-- The truncated simplex category. -/
def Truncated (n : â„•) :=
  ObjectProperty.FullSubcategory fun a : SimplexCategory => a.len â‰¤ n

instance (n : â„•) : SmallCategory.{0} (Truncated n) :=
  ObjectProperty.FullSubcategory.category _

namespace Truncated

instance {n} : Inhabited (Truncated n) :=
  âŸ¨âŸ¨â¦‹0â¦Œ, by simpâŸ©âŸ©

/-- The fully faithful inclusion of the truncated simplex category into the usual
simplex category.
-/
def inclusion (n : â„•) : SimplexCategory.Truncated n â¥¤ SimplexCategory :=
  ObjectProperty.Î¹ _

instance (n : â„•) : (inclusion n : Truncated n â¥¤ _).Full := ObjectProperty.full_Î¹ _
instance (n : â„•) : (inclusion n : Truncated n â¥¤ _).Faithful := ObjectProperty.faithful_Î¹ _

/-- A proof that the full subcategory inclusion is fully faithful -/
noncomputable def inclusion.fullyFaithful (n : â„•) :
    (inclusion n : Truncated n â¥¤ _).op.FullyFaithful :=
  Functor.FullyFaithful.ofFullyFaithful _

@[ext]
theorem Hom.ext {n} {a b : Truncated n} (f g : a âŸ¶ b) :
    f.toOrderHom = g.toOrderHom â†’ f = g := SimplexCategory.Hom.ext _ _

/-- A quick attempt to prove that `â¦‹mâ¦Œ` is `n`-truncated (`â¦‹mâ¦Œ.len â‰¤ n`). -/
scoped macro "trunc" : tactic =>
  `(tactic| first | assumption | dsimp only [SimplexCategory.len_mk] <;> omega)

/-- Make a morphism in `Truncated n` from a morphism in `SimplexCategory`. This
is equivalent to `@id (â¦‹aâ¦Œâ‚™ âŸ¶ â¦‹bâ¦Œâ‚™) f`. -/
abbrev Hom.tr {n : â„•} {a b : SimplexCategory} (f : a âŸ¶ b)
    (ha : a.len â‰¤ n := by trunc) (hb : b.len â‰¤ n := by trunc) :
    (âŸ¨a, haâŸ© : Truncated n) âŸ¶ âŸ¨b, hbâŸ© :=
  f

lemma Hom.tr_comp {n : â„•} {a b c : SimplexCategory} (f : a âŸ¶ b) (g : b âŸ¶ c)
    (ha : a.len â‰¤ n := by trunc) (hb : b.len â‰¤ n := by trunc)
    (hc : c.len â‰¤ n := by trunc) :
    tr (f â‰« g) = tr f â‰« tr g :=
  rfl

/-- The inclusion of `Truncated n` into `Truncated m` when `n â‰¤ m`. -/
def incl (n m : â„•) (h : n â‰¤ m := by omega) : Truncated n â¥¤ Truncated m where
  obj a := âŸ¨a.1, a.2.trans hâŸ©
  map := id

/-- For all `n â‰¤ m`, `inclusion n` factors through `Truncated m`. -/
def inclCompInclusion {n m : â„•} (h : n â‰¤ m) :
    incl n m â‹™ inclusion m â‰… inclusion n :=
  Iso.refl _

end Truncated

end SimplexCategory
