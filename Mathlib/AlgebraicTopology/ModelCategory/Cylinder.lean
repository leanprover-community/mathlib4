/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.ModelCategory.Basic
import Mathlib.AlgebraicTopology.ModelCategory.IsFibrant

/-!
# Cylinders

We first introduce a notion of "precylinder" for an object `A : C` in a model category.
It consists of an object `I`, a weak equivalence `Ïƒ : I âŸ¶ A` equipped with two sections
`iâ‚€` and `iâ‚`. When the morphism `A â¨¿ A âŸ¶ I` induced by both sections is a cofibration,
we say that this is a cylinder object for `A`. These notions shall be important in
the definition of "left homotopies" in model categories.

## References
* [Daniel G. Quillen, Homotopical algebra][Quillen1967]

-/

universe v u

open CategoryTheory Category Limits

namespace CategoryTheory

lemma IsPushout.of_coprod_inl_with_id {C : Type*} [Category C]
    {A B : C} (f : A âŸ¶ B) (X : C) [HasBinaryCoproduct A X]
    [HasBinaryCoproduct B X] :
    IsPushout coprod.inl f (coprod.map f (ğŸ™ X)) coprod.inl where
  w := by simp
  isColimit' := âŸ¨PushoutCocone.isColimitAux' _ (fun s â†¦ by
    refine âŸ¨coprod.desc s.inr (coprod.inr â‰« s.inl), ?_, ?_, ?_âŸ©
    Â· ext
      Â· simp [PushoutCocone.condition]
      Â· simp
    Â· simp
    Â· intro m hâ‚ hâ‚‚
      dsimp at m hâ‚ hâ‚‚ âŠ¢
      ext
      Â· simpa using hâ‚‚
      Â· simp [â† hâ‚])âŸ©

end CategoryTheory

namespace HomotopicalAlgebra

variable {C : Type u} [Category.{v} C] [ModelCategory C]

/-- In a model category `C`, a precylinder for `A : C` is the data of
a weak equivalence `Ïƒ : I âŸ¶ A` equipped with two sections. `-/
structure Precylinder (A : C) where
  /-- the underlying object of a precylinder -/
  I : C
  /-- the first "inclusion" in the precylinder -/
  iâ‚€ : A âŸ¶ I
  /-- the second "inclusion" in the precylinder -/
  iâ‚ : A âŸ¶ I
  /-- the weak equivalence of the precylinder -/
  Ïƒ : I âŸ¶ A
  iâ‚€_Ïƒ : iâ‚€ â‰« Ïƒ = ğŸ™ A := by aesop_cat
  iâ‚_Ïƒ : iâ‚ â‰« Ïƒ = ğŸ™ A := by aesop_cat
  weakEquivalence_Ïƒ : WeakEquivalence Ïƒ := by infer_instance

namespace Precylinder

attribute [instance] weakEquivalence_Ïƒ
attribute [reassoc (attr := simp)] iâ‚€_Ïƒ iâ‚_Ïƒ

variable {A : C} (P : Precylinder A)

instance : WeakEquivalence P.iâ‚€ :=
  weakEquivalence_of_postcomp_of_fac (P.iâ‚€_Ïƒ)

instance : WeakEquivalence P.iâ‚ :=
  weakEquivalence_of_postcomp_of_fac (P.iâ‚_Ïƒ)

/-- the map from the coproduct of two copies of `A` to `P.I`, when `P` is a precylinder
object for `A`. `P` shall be a cylinder object when this morphism is a cofibration. -/
noncomputable def i : A â¨¿ A âŸ¶ P.I := coprod.desc P.iâ‚€ P.iâ‚

@[reassoc (attr := simp)]
lemma inl_i : coprod.inl â‰« P.i = P.iâ‚€ := by simp [i]

@[reassoc (attr := simp)]
lemma inr_i : coprod.inr â‰« P.i = P.iâ‚ := by simp [i]

/-- The precylinder object obtained by switching the two inclusions. -/
@[simps]
def symm : Precylinder A where
  I := P.I
  iâ‚€ := P.iâ‚
  iâ‚ := P.iâ‚€
  Ïƒ := P.Ïƒ

@[simp, reassoc]
lemma symm_i [HasBinaryCoproducts C] : P.symm.i =
  (coprod.braiding A A).hom â‰« P.i := by aesop_cat

end Precylinder

/-- A cylinder object for `A` is a precylinder object `P` such that the
morphism `P.i : A â¨¿ A âŸ¶ P.I` is a cofibration. -/
structure Cylinder (A : C) extends Precylinder A where
  cofibration_i : Cofibration toPrecylinder.i := by infer_instance

namespace Cylinder

attribute [instance] cofibration_i

variable {A : C}

/-- The cylinder object obtained by switching the two inclusions. -/
def symm (P : Cylinder A) : Cylinder A where
  toPrecylinder := P.toPrecylinder.symm
  cofibration_i := by
    dsimp
    rw [Precylinder.symm_i]
    infer_instance

instance [IsCofibrant A] (P : Cylinder A) : Cofibration P.iâ‚€ := by
  rw [â† P.inl_i]
  infer_instance

instance [IsCofibrant A] (P : Cylinder A) : Cofibration P.iâ‚ := by
  rw [â† P.inr_i]
  infer_instance

instance [IsCofibrant A] (P : Cylinder A) : IsCofibrant P.I :=
  isCofibrant_of_cofibration P.iâ‚€

section

variable (h : MorphismProperty.MapFactorizationData (cofibrations C) (trivialFibrations C)
    (coprod.desc (ğŸ™ A) (ğŸ™ A)))

/-- A cylinder object for `A` can be obtained from a factorization of the obvious
map `A â¨¿ A âŸ¶ A` as a cofibration followed by a trivial fibration. -/
@[simps]
noncomputable def ofFactorizationData : Cylinder A where
  I := h.Z
  iâ‚€ := coprod.inl â‰« h.i
  iâ‚ := coprod.inr â‰« h.i
  Ïƒ := h.p
  cofibration_i := by
    convert inferInstanceAs (Cofibration h.i)
    aesop_cat

@[simp]
lemma ofFactorizationData_i : (ofFactorizationData h).i = h.i := by aesop_cat

@[simp]
lemma ofFactorizationData_p : (ofFactorizationData h).Ïƒ = h.p := rfl

instance : Fibration (ofFactorizationData h).Ïƒ := by
  dsimp
  infer_instance

instance [HasTerminal C] [IsFibrant A] [(fibrations C).IsStableUnderComposition] :
    IsFibrant (ofFactorizationData h).I :=
  isFibrant_of_fibration (ofFactorizationData h).Ïƒ

end

instance : Nonempty (Cylinder A) :=
  âŸ¨ofFactorizationData (MorphismProperty.factorizationData _ _ _)âŸ©

/-- The gluing of two cylinders. -/
@[simps]
noncomputable def trans [IsCofibrant A] (P P' : Cylinder A) : Cylinder A := by
  let Q : Precylinder A :=
    { I := pushout P.iâ‚ P'.iâ‚€
      iâ‚€ := P.iâ‚€ â‰« pushout.inl _ _
      iâ‚ := P'.iâ‚ â‰« pushout.inr _ _
      Ïƒ := pushout.desc P.Ïƒ P'.Ïƒ (by simp)
      weakEquivalence_Ïƒ := by
        have : WeakEquivalence ((P.iâ‚€ â‰« pushout.inl P.iâ‚ P'.iâ‚€) â‰«
            pushout.desc P.Ïƒ P'.Ïƒ (by simp)) := by
          simp only [assoc, colimit.Î¹_desc, PushoutCocone.mk_Î¹_app,
            Precylinder.iâ‚€_Ïƒ]
          infer_instance
        apply weakEquivalence_of_precomp (P.iâ‚€ â‰« pushout.inl _ _) }
  have : Cofibration Q.i := by
    let Ïˆ : P.I â¨¿ A âŸ¶ Q.I := coprod.desc (pushout.inl _ _) (P'.iâ‚ â‰« pushout.inr _ _)
    rw [show Q.i = coprod.map P.iâ‚€ (ğŸ™ A) â‰« Ïˆ by simp [Precylinder.i, Ïˆ, Q]]
    have fac : coprod.map P.iâ‚ (ğŸ™ A) â‰« Ïˆ = P'.i â‰« pushout.inr _ _ := by
      ext
      Â· simp [Ïˆ, pushout.condition]
      Â· simp [Ïˆ]
    have sq : IsPushout P.iâ‚ (coprod.inl â‰« P'.i) (coprod.inl â‰« Ïˆ) (pushout.inr _ _) := by
      simpa [Ïˆ] using IsPushout.of_hasPushout P.iâ‚ P'.iâ‚€
    have : Cofibration Ïˆ := by
      rw [cofibration_iff]
      exact (cofibrations C).of_isPushout
        (IsPushout.of_top sq fac (IsPushout.of_coprod_inl_with_id P.iâ‚ A).flip)
        (by rw [â† cofibration_iff]; infer_instance)
    infer_instance
  exact { toPrecylinder := Q }

end Cylinder

end HomotopicalAlgebra
