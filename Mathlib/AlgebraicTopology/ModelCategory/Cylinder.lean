/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.ModelCategory.Basic
import Mathlib.AlgebraicTopology.ModelCategory.IsFibrant

/-!
# Cylinders

We first introduce a notion of "precylinder" for an object `A : C` in a model category.
It consists of an object `I`, a weak equivalence `œÉ : I ‚ü∂ A` equipped with two sections
`i‚ÇÄ` and `i‚ÇÅ`. When the morphism `A ‚®ø A ‚ü∂ I` induced by both sections is a cofibration,
we say that this is a cylinder object for `A`. These notions shall be important in
the definition of "left homotopies" in model categories.

## References
* [Daniel G. Quillen, Homotopical algebra][Quillen1967]

-/

universe v u

open CategoryTheory Limits

namespace HomotopicalAlgebra

variable {C : Type u} [Category.{v} C] [ModelCategory C]

/-- In a model category `C`, a precylinder for `A : C` is the data of
a weak equivalence `œÉ : I ‚ü∂ A` equipped with two sections. `-/
structure Precylinder (A : C) where
  /-- the underlying object of a precylinder -/
  I : C
  /-- the first "inclusion" in the precylinder -/
  i‚ÇÄ : A ‚ü∂ I
  /-- the second "inclusion" in the precylinder -/
  i‚ÇÅ : A ‚ü∂ I
  /-- the weak equivalence of the precylinder -/
  œÉ : I ‚ü∂ A
  i‚ÇÄ_œÉ : i‚ÇÄ ‚â´ œÉ = ùüô A := by aesop_cat
  i‚ÇÅ_œÉ : i‚ÇÅ ‚â´ œÉ = ùüô A := by aesop_cat
  weakEquivalence_œÉ : WeakEquivalence œÉ := by infer_instance

namespace Precylinder

attribute [instance] weakEquivalence_œÉ
attribute [reassoc (attr := simp)] i‚ÇÄ_œÉ i‚ÇÅ_œÉ

variable {A : C} (P : Precylinder A)

instance : WeakEquivalence P.i‚ÇÄ :=
  weakEquivalence_of_postcomp_of_fac (P.i‚ÇÄ_œÉ)

instance : WeakEquivalence P.i‚ÇÅ :=
  weakEquivalence_of_postcomp_of_fac (P.i‚ÇÅ_œÉ)

/-- the map from the coproduct of two copies of `A` to `P.I`, when `P` is a precylinder
object for `A`. `P` shall be a cylinder object when this morphism is a cofibration. -/
noncomputable def i : A ‚®ø A ‚ü∂ P.I := coprod.desc P.i‚ÇÄ P.i‚ÇÅ

@[reassoc (attr := simp)]
lemma inl_i : coprod.inl ‚â´ P.i = P.i‚ÇÄ := by simp [i]

@[reassoc (attr := simp)]
lemma inr_i : coprod.inr ‚â´ P.i = P.i‚ÇÅ := by simp [i]

/-- The precylinder object obtained by switching the two inclusions. -/
@[simps]
def symm : Precylinder A where
  I := P.I
  i‚ÇÄ := P.i‚ÇÅ
  i‚ÇÅ := P.i‚ÇÄ
  œÉ := P.œÉ

@[simp, reassoc]
lemma symm_i [HasBinaryCoproducts C] : P.symm.i =
  (coprod.braiding A A).hom ‚â´ P.i := by aesop_cat

end Precylinder

/-- A cylinder object for `A` is a precylinder object `P` such that the
morphism `P.i : A ‚®ø A ‚ü∂ P.I` is a cofibration. -/
structure Cylinder (A : C) extends Precylinder A where
  cofibration_i : Cofibration toPrecylinder.i := by infer_instance

namespace Cylinder

attribute [instance] cofibration_i

variable {A : C}

/-- The cylinder object obtained by switching the two inclusions. -/
def symm (P : Cylinder A) : Cylinder A where
  toPrecylinder := P.toPrecylinder.symm
  cofibration_i := by
    dsimp
    rw [Precylinder.symm_i]
    infer_instance

instance [IsCofibrant A] (P : Cylinder A) : Cofibration P.i‚ÇÄ := by
  rw [‚Üê P.inl_i]
  infer_instance

instance [IsCofibrant A] (P : Cylinder A) : Cofibration P.i‚ÇÅ := by
  rw [‚Üê P.inr_i]
  infer_instance

instance [IsCofibrant A] (P : Cylinder A) : IsCofibrant P.I :=
  isCofibrant_of_cofibration P.i‚ÇÄ

section

variable (h : MorphismProperty.MapFactorizationData (cofibrations C) (trivialFibrations C)
    (coprod.desc (ùüô A) (ùüô A)))

/-- A cylinder object for `A` can be obtained from a factorization of the obvious
map `A ‚®ø A ‚ü∂ A` as a cofibration followed by a trivial fibration. -/
@[simps]
noncomputable def ofFactorizationData : Cylinder A where
  I := h.Z
  i‚ÇÄ := coprod.inl ‚â´ h.i
  i‚ÇÅ := coprod.inr ‚â´ h.i
  œÉ := h.p
  cofibration_i := by
    convert inferInstanceAs (Cofibration h.i)
    aesop_cat

@[simp]
lemma ofFactorizationData_i : (ofFactorizationData h).i = h.i := by aesop_cat

@[simp]
lemma ofFactorizationData_p : (ofFactorizationData h).œÉ = h.p := rfl

instance : Fibration (ofFactorizationData h).œÉ := by
  dsimp
  infer_instance

instance [HasTerminal C] [IsFibrant A] [(fibrations C).IsStableUnderComposition] :
    IsFibrant (ofFactorizationData h).I :=
  isFibrant_of_fibration (ofFactorizationData h).œÉ

end

instance : Nonempty (Cylinder A) :=
  ‚ü®ofFactorizationData (MorphismProperty.factorizationData _ _ _)‚ü©

end Cylinder

end HomotopicalAlgebra
