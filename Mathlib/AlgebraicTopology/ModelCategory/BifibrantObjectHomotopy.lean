/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.ModelCategory.DerivabilityStructureCofibrant
public import Mathlib.AlgebraicTopology.ModelCategory.DerivabilityStructureFibrant
public import Mathlib.CategoryTheory.Localization.CalculusOfFractions.OfAdjunction

/-!
# The homotopy category of bifibrant objects

-/

@[expose] public section

open CategoryTheory Limits

namespace HomotopicalAlgebra

variable {C : Type*} [Category* C] [ModelCategory C]

namespace BifibrantObject

variable (C) in
/-- The homotopy relation on the category of bifibrant objects. -/
def homRel : HomRel (BifibrantObject C) :=
  fun _ _ f g ‚Ü¶ RightHomotopyRel f.hom g.hom

lemma homRel_iff_rightHomotopyRel {X Y : BifibrantObject C} {f g : X ‚ü∂ Y} :
    homRel C f g ‚Üî RightHomotopyRel f.hom g.hom := Iff.rfl

lemma homRel_iff_leftHomotopyRel {X Y : BifibrantObject C} {f g : X ‚ü∂ Y} :
    homRel C f g ‚Üî LeftHomotopyRel f.hom g.hom := by
  rw [homRel_iff_rightHomotopyRel, leftHomotopyRel_iff_rightHomotopyRel]

instance : HomRel.IsStableUnderPostcomp (homRel C) where
  comp_right _ h := h.postcomp _

instance : HomRel.IsStableUnderPrecomp (homRel C) where
  comp_left _ _ _ h := h.precomp _

instance : Congruence (homRel C) where
  equivalence :=
    { refl _ := .refl _
      symm h := .symm h
      trans h‚ÇÅ h‚ÇÇ := .trans h‚ÇÅ h‚ÇÇ }

variable (C) in
/-- The homotopy category of bifibrant objects. -/
abbrev œÄ := Quotient (BifibrantObject.homRel C)

/-- The quotient functor from the category of bifibrant objects to its
homotopy category. -/
def toœÄ : BifibrantObject C ‚•§ œÄ C := Quotient.functor _

lemma toœÄ_obj_surjective : Function.Surjective (toœÄ (C := C)).obj :=
  fun ‚ü®_‚ü© ‚Ü¶ ‚ü®_, rfl‚ü©

instance : Functor.Full (toœÄ (C := C)) := by dsimp [toœÄ]; infer_instance

lemma toœÄ_map_eq {X Y : BifibrantObject C} {f g : X ‚ü∂ Y}
    (h : homRel C f g) :
    toœÄ.map f = toœÄ.map g :=
  CategoryTheory.Quotient.sound _ h

lemma toœÄ_map_eq_iff {X Y : BifibrantObject C} (f g : X ‚ü∂ Y) :
    toœÄ.map f = toœÄ.map g ‚Üî homRel C f g :=
  Quotient.functor_map_eq_iff _ _ _

section

variable {D : Type*} [Category* D]

lemma inverts_iff_factors (F : BifibrantObject C ‚•§ D) :
    (weakEquivalences _).IsInvertedBy F ‚Üî
    ‚àÄ ‚¶ÉK L : BifibrantObject C‚¶Ñ (f g : K ‚ü∂ L),
      homRel C f g ‚Üí F.map f = F.map g := by
  refine ‚ü®fun H K L f g h ‚Ü¶ ?_, fun h X Y f hf ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_very_good_pathObject
    have := isCofibrant_of_cofibration P.Œπ
    have : IsIso (F.map (homMk P.Œπ)) := H _ (by
      rw [‚Üê weakEquivalence_iff, weakEquivalence_iff_of_objectProperty]
      exact inferInstanceAs (WeakEquivalence P.Œπ))
    simp only [show f = homMk h.h ‚â´ homMk P.p‚ÇÄ by cat_disch,
      show g = homMk h.h ‚â´ homMk P.p‚ÇÅ by cat_disch, Functor.map_comp]
    congr 1
    simp [‚Üê cancel_epi (F.map (homMk P.Œπ)), ‚Üê Functor.map_comp]
  ¬∑ rw [‚Üê weakEquivalence_iff, weakEquivalence_iff_of_objectProperty] at hf
    obtain ‚ü®g', h‚ÇÅ, h‚ÇÇ‚ü© := RightHomotopyClass.whitehead f.hom
    refine ‚ü®F.map (homMk g'), ?_, ?_‚ü©
    all_goals
      rw [‚Üê F.map_comp, ‚Üê F.map_id]
      apply h
      assumption

/-- The strict universal property of the localization with respect
to weak equivalences for the quotient functor
`toœÄ : BifibrantObject C ‚•§ BifibrantObject.œÄ C`. -/
def strictUniversalPropertyFixedTargetToœÄ :
    Localization.StrictUniversalPropertyFixedTarget
      toœÄ (weakEquivalences (BifibrantObject C)) D where
  inverts := by
    rw [inverts_iff_factors]
    intro K L f g h
    exact CategoryTheory.Quotient.sound _ h
  lift F hF := CategoryTheory.Quotient.lift _ F
    (by rwa [inverts_iff_factors] at hF)
  fac F hF := rfl
  uniq _ _ h := Quotient.lift_unique' _ _ _ h

end

instance : toœÄ.IsLocalization (weakEquivalences (BifibrantObject C)) :=
  .mk' _ _ strictUniversalPropertyFixedTargetToœÄ strictUniversalPropertyFixedTargetToœÄ

instance {X Y : BifibrantObject C} (f : X ‚ü∂ Y) [hf : WeakEquivalence f] :
    IsIso (toœÄ.map f) :=
  Localization.inverts toœÄ (weakEquivalences _) f (by rwa [weakEquivalence_iff] at hf)

section

variable {X Y : C} [IsCofibrant X] [IsCofibrant Y] [IsFibrant X] [IsFibrant Y]

/-- Right homotopy classes of maps between bifibrant objects identify
to morphisms in the homotopy category `BifibrantObject.œÄ`. -/
def œÄ.homEquivRight :
    RightHomotopyClass X Y ‚âÉ (toœÄ.obj (mk X) ‚ü∂ toœÄ.obj (mk Y)) where
  toFun := Quot.lift (fun f ‚Ü¶ toœÄ.map (homMk f)) (fun _ _ h ‚Ü¶ by rwa [toœÄ_map_eq_iff])
  invFun := Quot.lift (fun f ‚Ü¶ .mk f.hom) (fun _ _ h ‚Ü¶ by
    simpa [RightHomotopyClass.mk_eq_mk_iff] using h)
  left_inv := by rintro ‚ü®f‚ü©; rfl
  right_inv := by rintro ‚ü®f‚ü©; rfl

@[simp]
lemma œÄ.homEquivRight_apply (f : X ‚ü∂ Y) :
    œÄ.homEquivRight (.mk f) = toœÄ.map (homMk f) := rfl

@[simp]
lemma œÄ.homEquivRight_symm_apply (f : X ‚ü∂ Y) :
    œÄ.homEquivRight.symm (toœÄ.map (homMk f)) = .mk f := rfl

/-- Left homotopy classes of maps between bifibrant objects identify
to morphisms in the homotopy category `BifibrantObject.œÄ`. -/
def œÄ.homEquivLeft :
    LeftHomotopyClass X Y ‚âÉ (toœÄ.obj (mk X) ‚ü∂ toœÄ.obj (mk Y)) :=
  leftHomotopyClassEquivRightHomotopyClass.trans œÄ.homEquivRight

@[simp]
lemma œÄ.homEquivLeft_apply (f : X ‚ü∂ Y) :
    œÄ.homEquivLeft (.mk f) = toœÄ.map (homMk f) := by
  simp [homEquivLeft]

@[simp]
lemma œÄ.homEquivLeft_symm_apply (f : X ‚ü∂ Y) :
    œÄ.homEquivRight.symm (toœÄ.map (homMk f)) = .mk f := rfl

end

/-- The inclusion functor `BifibrantObject.œÄ C ‚•§ FibrantObject.œÄ C`. -/
def œÄ.ŒπFibrantObject : œÄ C ‚•§ FibrantObject.œÄ C :=
  CategoryTheory.Quotient.lift _
    (BifibrantObject.ŒπFibrantObject ‚ãô FibrantObject.toœÄ) (fun _ _ _ _ h ‚Ü¶ by
      simpa [FibrantObject.toœÄ_map_eq_iff, FibrantObject.homRel_iff_leftHomotopyRel,
        homRel_iff_leftHomotopyRel] using h)

@[simp]
lemma œÄ.ŒπFibrantObject_obj (X : BifibrantObject C) :
    œÄ.ŒπFibrantObject.obj (toœÄ.obj X) =
      FibrantObject.toœÄ.obj (BifibrantObject.ŒπFibrantObject.obj X) :=
  rfl

@[simp]
lemma œÄ.ŒπFibrantObject_map_toœÄ_map {X Y : BifibrantObject C} (f : X ‚ü∂ Y) :
    œÄ.ŒπFibrantObject.map (toœÄ.map f) =
      FibrantObject.toœÄ.map (FibrantObject.homMk f.hom) :=
  rfl

/-- The isomomorphism `toœÄ ‚ãô œÄ.ŒπFibrantObject ‚âÖ ŒπFibrantObject ‚ãô FibrantObject.toœÄ`
between functors `BifibrantObject C ‚•§ FibrantObject.œÄ C`. -/
def toœÄCompŒπFibrantObject :
    toœÄ (C := C) ‚ãô œÄ.ŒπFibrantObject ‚âÖ
      ŒπFibrantObject ‚ãô FibrantObject.toœÄ := Iso.refl _

/-- The inclusion functor `BifibrantObject.œÄ C ‚•§ CofibrantObject.œÄ C`. -/
def œÄ.ŒπCofibrantObject : œÄ C ‚•§ CofibrantObject.œÄ C :=
  CategoryTheory.Quotient.lift _
    (BifibrantObject.ŒπCofibrantObject ‚ãô CofibrantObject.toœÄ) (fun _ _ _ _ h ‚Ü¶ by
      simpa [CofibrantObject.toœÄ_map_eq_iff])

@[simp]
lemma œÄ.ŒπCofibrantObject_obj (X : BifibrantObject C) :
    œÄ.ŒπCofibrantObject.obj (toœÄ.obj X) =
      CofibrantObject.toœÄ.obj (BifibrantObject.ŒπCofibrantObject.obj X) :=
  rfl

@[simp]
lemma œÄ.ŒπCofibrantObject_map_toœÄ_map {X Y : BifibrantObject C} (f : X ‚ü∂ Y) :
    œÄ.ŒπCofibrantObject.map (toœÄ.map f) =
      CofibrantObject.toœÄ.map (CofibrantObject.homMk f.hom) :=
  rfl

/-- The isomomorphism `toœÄ ‚ãô œÄ.ŒπCofibrantObject ‚âÖ ŒπCofibrantObject ‚ãô CofibrantObject.toœÄ`
between functors `BifibrantObject C ‚•§ CofibrantObject.œÄ C`. -/
def toœÄCompŒπCofibrantObject :
    toœÄ (C := C) ‚ãô œÄ.ŒπCofibrantObject ‚âÖ
      ŒπCofibrantObject ‚ãô CofibrantObject.toœÄ := Iso.refl _

end BifibrantObject

namespace CofibrantObject

lemma exists_bifibrant (X : CofibrantObject C) :
    ‚àÉ (Y : BifibrantObject C) (i : X ‚ü∂ BifibrantObject.ŒπCofibrantObject.obj Y),
      Cofibration (Œπ.map i) ‚àß WeakEquivalence (Œπ.map i) := by
  have h := MorphismProperty.factorizationData (trivialCofibrations C) (fibrations C)
      (terminal.from X.obj)
  have := isCofibrant_of_cofibration h.i
  have : IsFibrant h.Z := by
    rw [isFibrant_iff_of_isTerminal h.p terminalIsTerminal]
    infer_instance
  exact ‚ü®BifibrantObject.mk h.Z, homMk h.i, inferInstanceAs (Cofibration h.i),
    inferInstanceAs (WeakEquivalence h.i)‚ü©

/-- Given `X : CofibrantObject C`, this is a choice of bifibrant resolution of `X`. -/
noncomputable def bifibrantResolutionObj (X : CofibrantObject C) :
    BifibrantObject C :=
  (exists_bifibrant X).choose

/-- Given `X : CofibrantObject C`, this is a trivial cofibration
from `X` to a choice of bifibrant resolution. -/
noncomputable def iBifibrantResolutionObj (X : CofibrantObject C) :
    X ‚ü∂ BifibrantObject.ŒπCofibrantObject.obj (bifibrantResolutionObj X) :=
  (exists_bifibrant X).choose_spec.choose

instance (X : CofibrantObject C) :
    Cofibration (iBifibrantResolutionObj X).hom :=
  (exists_bifibrant X).choose_spec.choose_spec.1

instance (X : CofibrantObject C) :
    WeakEquivalence (iBifibrantResolutionObj X).hom :=
  (exists_bifibrant X).choose_spec.choose_spec.2

instance (X : CofibrantObject C) :
    WeakEquivalence (iBifibrantResolutionObj X) := by
  rw [weakEquivalence_iff_of_objectProperty]
  infer_instance

instance (X : BifibrantObject C) :
    IsFibrant (Œπ.obj (BifibrantObject.ŒπCofibrantObject.obj X)) := X.2.2

lemma exists_bifibrant_map {X‚ÇÅ X‚ÇÇ : CofibrantObject C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    ‚àÉ (g : bifibrantResolutionObj X‚ÇÅ ‚ü∂ bifibrantResolutionObj X‚ÇÇ),
      iBifibrantResolutionObj X‚ÇÅ ‚â´ (BifibrantObject.ŒπCofibrantObject.map g) =
      f ‚â´ iBifibrantResolutionObj X‚ÇÇ := by
  have sq : CommSq (Œπ.map (f ‚â´ iBifibrantResolutionObj X‚ÇÇ))
    (iBifibrantResolutionObj X‚ÇÅ).hom (terminal.from _) (terminal.from _) := ‚ü®by simp‚ü©
  exact ‚ü®BifibrantObject.homMk sq.lift, by cat_disch‚ü©

/-- Given a morphism in `CofibrantObject C`, this is a choice of morphism
(well defined only up to homotopy) between the chosen bifibrant resolutions. -/
noncomputable def bifibrantResolutionMap {X‚ÇÅ X‚ÇÇ : CofibrantObject C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    bifibrantResolutionObj X‚ÇÅ ‚ü∂ bifibrantResolutionObj X‚ÇÇ :=
  (exists_bifibrant_map f).choose

@[reassoc (attr := simp)]
lemma bifibrantResolutionMap_fac {X‚ÇÅ X‚ÇÇ : CofibrantObject C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    iBifibrantResolutionObj X‚ÇÅ ‚â´ homMk (bifibrantResolutionMap f).hom  =
      f ‚â´ iBifibrantResolutionObj X‚ÇÇ :=
  (exists_bifibrant_map f).choose_spec

instance {X‚ÇÅ X‚ÇÇ : CofibrantObject C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) [WeakEquivalence f] :
    WeakEquivalence (bifibrantResolutionMap f) := by
  rw [weakEquivalence_iff]
  change weakEquivalences _ (CofibrantObject.homMk (bifibrantResolutionMap f).hom)
  rw [‚Üê weakEquivalence_iff, ‚Üê weakEquivalence_precomp_iff (iBifibrantResolutionObj X‚ÇÅ),
    bifibrantResolutionMap_fac, weakEquivalence_precomp_iff]
  infer_instance

@[reassoc (attr := simp)]
lemma bifibrantResolutionMap_fac' {X‚ÇÅ X‚ÇÇ : CofibrantObject C} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    toœÄ.map X‚ÇÅ.iBifibrantResolutionObj ‚â´
    toœÄ.map (homMk (bifibrantResolutionMap f).hom) =
    toœÄ.map f ‚â´ toœÄ.map X‚ÇÇ.iBifibrantResolutionObj :=
  toœÄ.congr_map (bifibrantResolutionMap_fac f)

lemma bifibrantResolutionObj_hom_ext
    {X : CofibrantObject C} {Y : BifibrantObject.œÄ C} {f g :
      BifibrantObject.toœÄ.obj (bifibrantResolutionObj X) ‚ü∂ Y}
    (h : CofibrantObject.toœÄ.map (iBifibrantResolutionObj X) ‚â´
      BifibrantObject.œÄ.ŒπCofibrantObject.map f =
      CofibrantObject.toœÄ.map (iBifibrantResolutionObj X) ‚â´
        BifibrantObject.œÄ.ŒπCofibrantObject.map g) :
    f = g := by
  obtain ‚ü®Y, rfl‚ü© := BifibrantObject.toœÄ_obj_surjective Y
  obtain ‚ü®f, rfl‚ü© := BifibrantObject.toœÄ.map_surjective f
  obtain ‚ü®g, rfl‚ü© := BifibrantObject.toœÄ.map_surjective g
  change toœÄ.map (X.iBifibrantResolutionObj ‚â´ BifibrantObject.ŒπCofibrantObject.map f) =
    toœÄ.map (X.iBifibrantResolutionObj ‚â´ BifibrantObject.ŒπCofibrantObject.map g) at h
  rw [CofibrantObject.toœÄ_map_eq_iff,
    CofibrantObject.homRel_iff_rightHomotopyRel,
    ‚Üê RightHomotopyClass.mk_eq_mk_iff] at h
  rw [BifibrantObject.toœÄ_map_eq_iff,
    BifibrantObject.homRel_iff_rightHomotopyRel,
    ‚Üê RightHomotopyClass.mk_eq_mk_iff]
  apply (RightHomotopyClass.precomp_bijective_of_cofibration_of_weakEquivalence
    _ (iBifibrantResolutionObj X).hom).1
  simpa only [ObjectProperty.Œπ_obj, ObjectProperty.ŒπOfLE_obj_obj, ObjectProperty.Œπ_map,
    RightHomotopyClass.precomp_mk] using h

/-- The bifibrant resolution functor from the category of cofibrant objects
to the homotopy category of bifibrant objects. -/
@[simps]
noncomputable def œÄ.bifibrantResolution' : CofibrantObject C ‚•§ BifibrantObject.œÄ C where
  obj X := BifibrantObject.toœÄ.obj (bifibrantResolutionObj X)
  map f := BifibrantObject.toœÄ.map (bifibrantResolutionMap f)
  map_id X := bifibrantResolutionObj_hom_ext (by simp)
  map_comp {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f g := bifibrantResolutionObj_hom_ext (by simp)

/-- The bifibrant resolution functor from the homotopy category of
cofibrant objects to the homotopy category of bifibrant objects. -/
noncomputable def œÄ.bifibrantResolution :
    CofibrantObject.œÄ C ‚•§ BifibrantObject.œÄ C :=
  CategoryTheory.Quotient.lift _ CofibrantObject.œÄ.bifibrantResolution' (by
    intro X Y f g h
    apply bifibrantResolutionObj_hom_ext
    simpa [‚Üê Functor.map_comp, toœÄ_map_eq_iff] using h.postcomp _)

@[simp]
lemma œÄ.bifibrantResolution_obj (X : CofibrantObject C) :
    œÄ.bifibrantResolution.obj (CofibrantObject.toœÄ.obj X) =
      BifibrantObject.toœÄ.obj (bifibrantResolutionObj X) := rfl

@[simp]
lemma œÄ.bifibrantResolution_map {X Y : CofibrantObject C} (f : X ‚ü∂ Y) :
    œÄ.bifibrantResolution.map (CofibrantObject.toœÄ.map f) =
      BifibrantObject.toœÄ.map (bifibrantResolutionMap f) := rfl

/-- Auxiliary definition for `CofibrantObject.œÄ.adj`. -/
noncomputable def œÄ.adjUnit :
    ùü≠ (œÄ C) ‚ü∂ œÄ.bifibrantResolution ‚ãô BifibrantObject.œÄ.ŒπCofibrantObject :=
  Quotient.natTransLift _
    { app X := toœÄ.map (iBifibrantResolutionObj X)
      naturality _ _ f := (bifibrantResolutionMap_fac' f).symm }

lemma œÄ.adjUnit_app (X : CofibrantObject C) :
    œÄ.adjUnit.app (toœÄ.obj X) =
      toœÄ.map (iBifibrantResolutionObj X) := rfl

instance (X : CofibrantObject.œÄ C) : WeakEquivalence (œÄ.adjUnit.app X) := by
  obtain ‚ü®X, rfl‚ü© := toœÄ_obj_surjective X
  rw [œÄ.adjUnit_app, weakEquivalence_toœÄ_map_iff,
    weakEquivalence_iff_of_objectProperty]
  infer_instance

/-- Auxiliary definition for `CofibrantObject.œÄ.adj`. -/
noncomputable def œÄ.adjCounit' :
    ùü≠ (BifibrantObject.œÄ C) ‚ü∂ BifibrantObject.œÄ.ŒπCofibrantObject ‚ãô œÄ.bifibrantResolution :=
  Quotient.natTransLift _
    { app X :=
        BifibrantObject.toœÄ.map
          (BifibrantObject.homMk (iBifibrantResolutionObj (.mk X.obj)).hom)
      naturality X‚ÇÅ X‚ÇÇ f := BifibrantObject.toœÄ.congr_map (by
        have := (ObjectProperty.Œπ _).congr_map
          (bifibrantResolutionMap_fac (CofibrantObject.homMk f.hom)).symm
        ext : 1
        dsimp
        exact this ) }

lemma œÄ.adjCounit'_app (X : BifibrantObject C) :
    œÄ.adjCounit'.app (BifibrantObject.toœÄ.obj X) =
      BifibrantObject.toœÄ.map (BifibrantObject.homMk
        (iBifibrantResolutionObj (.mk X.obj)).hom) := rfl

instance (X : BifibrantObject.œÄ C) : IsIso (œÄ.adjCounit'.app X) := by
  obtain ‚ü®X, rfl‚ü© := BifibrantObject.toœÄ_obj_surjective X
  rw [œÄ.adjCounit'_app]
  have : WeakEquivalence (C := BifibrantObject C)
      (BifibrantObject.homMk ((mk X.obj).iBifibrantResolutionObj).hom) := by
    simp only [BifibrantObject.weakEquivalence_homMk_iff]
    infer_instance
  infer_instance

instance : IsIso (œÄ.adjCounit' (C := C)) := NatIso.isIso_of_isIso_app _

/-- Auxiliary definition for `CofibrantObject.œÄ.adj`. -/
noncomputable def œÄ.adjCounitIso :
    BifibrantObject.œÄ.ŒπCofibrantObject ‚ãô bifibrantResolution ‚âÖ ùü≠ (BifibrantObject.œÄ C) :=
  (asIso œÄ.adjCounit').symm

lemma œÄ.adjCounitIso_inv_app (X : BifibrantObject C) :
    œÄ.adjCounitIso.inv.app (BifibrantObject.toœÄ.obj X) =
      BifibrantObject.toœÄ.map (BifibrantObject.homMk
        ((iBifibrantResolutionObj (.mk X.obj))).hom) := rfl

/-- The adjunction between the category `CofibrantObject.œÄ C` and `BifibrantObject.œÄ C`. -/
noncomputable def œÄ.adj :
    œÄ.bifibrantResolution (C := C) ‚ä£ BifibrantObject.œÄ.ŒπCofibrantObject where
  unit := œÄ.adjUnit
  counit := œÄ.adjCounitIso.hom
  left_triangle_components X := by
    obtain ‚ü®X, rfl‚ü© := toœÄ_obj_surjective X
    obtain ‚ü®X, _, rfl‚ü© := CofibrantObject.mk_surjective X
    rw [‚Üê cancel_mono (œÄ.adjCounitIso.inv.app _), Category.assoc, Iso.hom_inv_id_app]
    apply bifibrantResolutionObj_hom_ext
    dsimp
    simp only [œÄ.adjCounitIso_inv_app, Category.comp_id, Category.id_comp,
      BifibrantObject.œÄ.ŒπCofibrantObject_map_toœÄ_map, ObjectProperty.homMk_hom]
    apply bifibrantResolutionMap_fac'
  right_triangle_components X := by
    obtain ‚ü®X, rfl‚ü© := BifibrantObject.toœÄ_obj_surjective X
    rw [‚Üê cancel_mono (BifibrantObject.œÄ.ŒπCofibrantObject.map (œÄ.adjCounitIso.inv.app _)),
      Category.assoc, ‚Üê Functor.map_comp, Iso.hom_inv_id_app]
    cat_disch

instance : IsIso (œÄ.adj (C := C)).counit := by
  dsimp [œÄ.adj]
  infer_instance

instance : (BifibrantObject.œÄ.ŒπCofibrantObject (C := C)).Full :=
  œÄ.adj.fullyFaithfulROfIsIsoCounit.full

instance : (BifibrantObject.œÄ.ŒπCofibrantObject (C := C)).Faithful :=
  œÄ.adj.fullyFaithfulROfIsIsoCounit.faithful

instance (X : CofibrantObject.œÄ C) : WeakEquivalence (œÄ.adj.unit.app X) := by
  obtain ‚ü®X, rfl‚ü© := toœÄ_obj_surjective X
  dsimp [œÄ.adj]
  infer_instance

instance : œÄ.bifibrantResolution.IsLocalization (weakEquivalences (œÄ C)) :=
  œÄ.adj.isLocalization_leftAdjoint _ (by
    intro X Y f hf
    obtain ‚ü®X, rfl‚ü© := toœÄ_obj_surjective X
    obtain ‚ü®Y, rfl‚ü© := toœÄ_obj_surjective Y
    obtain ‚ü®f, rfl‚ü© := toœÄ.map_surjective f
    rw [‚Üê weakEquivalence_iff, weakEquivalence_toœÄ_map_iff] at hf
    rw [œÄ.bifibrantResolution_map]
    apply Localization.inverts _ (weakEquivalences _)
    rw [‚Üê weakEquivalence_iff]
    infer_instance) (fun X ‚Ü¶ by
    rw [‚Üê weakEquivalence_iff]
    dsimp
    infer_instance)

end CofibrantObject

namespace BifibrantObject

variable (C) in
/-- The inclusion `BifibrantObject C ‚•§ C`, as a localizer morphism. -/
def localizerMorphism :
    LocalizerMorphism (weakEquivalences (BifibrantObject C)) (weakEquivalences C) where
  functor := Œπ
  map := by rfl

variable (C) in
/-- The inclusion `BifibrantObject C ‚•§ CofibrantObject C`, as a localizer morphism. -/
@[simps]
def ŒπCofibrantObjectLocalizerMorphism :
    LocalizerMorphism (weakEquivalences (BifibrantObject C))
      (weakEquivalences (CofibrantObject C)) where
  functor := ŒπCofibrantObject
  map _ _ _ h := h

variable (C) in
/-- The inclusion `BifibrantObject C ‚•§ FibrantObject C`, as a localizer morphism. -/
@[simps]
def ŒπFibrantObjectLocalizerMorphism :
    LocalizerMorphism (weakEquivalences (BifibrantObject C))
      (weakEquivalences (FibrantObject C)) where
  functor := ŒπFibrantObject
  map _ _ _ h := h

open Functor

instance : (ŒπCofibrantObjectLocalizerMorphism C).IsLocalizedEquivalence := by
  have : CatCommSq (ŒπCofibrantObjectLocalizerMorphism C).functor toœÄ
      (CofibrantObject.toœÄ ‚ãô CofibrantObject.œÄ.bifibrantResolution) (ùü≠ _) :=
    ‚ü®(associator _ _ _).symm ‚â™‚â´
      isoWhiskerRight toœÄCompŒπCofibrantObject.symm _ ‚â™‚â´
      associator _ _ _ ‚â™‚â´ isoWhiskerLeft _ (asIso CofibrantObject.œÄ.adj.counit)‚ü©
  exact LocalizerMorphism.IsLocalizedEquivalence.mk'
    (ŒπCofibrantObjectLocalizerMorphism C) BifibrantObject.toœÄ
    (CofibrantObject.toœÄ ‚ãô CofibrantObject.œÄ.bifibrantResolution) (ùü≠ _)

instance : (localizerMorphism C).IsLocalizedEquivalence :=
  inferInstanceAs ((ŒπCofibrantObjectLocalizerMorphism C).comp
    (CofibrantObject.localizerMorphism C)).IsLocalizedEquivalence

instance {D : Type*} [Category* D] (L : C ‚•§ D)
    [L.IsLocalization (weakEquivalences C)] :
    (Œπ ‚ãô L).IsLocalization (weakEquivalences (BifibrantObject C)) :=
  inferInstanceAs (((localizerMorphism C).functor ‚ãô L).IsLocalization _)

instance : (ŒπFibrantObjectLocalizerMorphism C).IsLocalizedEquivalence := by
  let L := FibrantObject.Œπ ‚ãô (weakEquivalences C).Q
  have : ((ŒπFibrantObjectLocalizerMorphism C).functor ‚ãô L).IsLocalization
    (weakEquivalences _) :=
    inferInstanceAs ((Œπ ‚ãô (weakEquivalences C).Q).IsLocalization (weakEquivalences _))
  exact LocalizerMorphism.IsLocalizedEquivalence.of_isLocalization_of_isLocalization _ L

instance {D : Type*} [Category D] (L : FibrantObject C ‚•§ D)
    [L.IsLocalization (weakEquivalences _)] :
    (ŒπFibrantObject ‚ãô L).IsLocalization (weakEquivalences _) := by
  change ((ŒπFibrantObjectLocalizerMorphism C).functor ‚ãô L).IsLocalization _
  infer_instance

end BifibrantObject

end HomotopicalAlgebra
