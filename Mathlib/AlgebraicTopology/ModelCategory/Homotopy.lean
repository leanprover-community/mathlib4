/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.ModelCategory.Cylinder
import Mathlib.AlgebraicTopology.ModelCategory.PathObject
import Mathlib.AlgebraicTopology.ModelCategory.BrownLemma
import Mathlib.CategoryTheory.Localization.Quotient
import Mathlib.CategoryTheory.Quotient

/-!
# Homotopies in model categories

-/

open CategoryTheory Limits

namespace HomotopicalAlgebra

variable {C : Type*} [Category C] [ModelCategory C]

namespace Cylinder

variable {X : C} (P : Cylinder X) {Y : C}

structure LeftHomotopy (f g : X ‚ü∂ Y) where
  h : P.I ‚ü∂ Y
  h‚ÇÄ : P.i‚ÇÄ ‚â´ h = f := by aesop_cat
  h‚ÇÅ : P.i‚ÇÅ ‚â´ h = g := by aesop_cat

namespace LeftHomotopy

attribute [reassoc (attr := simp)] h‚ÇÄ h‚ÇÅ

variable {P}

@[simps]
def refl (f : X ‚ü∂ Y) : P.LeftHomotopy f f where
  h := P.œÄ ‚â´ f

@[simps]
def symm {f g : X ‚ü∂ Y} (h : P.LeftHomotopy f g) : P.symm.LeftHomotopy g f where
  h := h.h

@[simps]
noncomputable def trans {f‚ÇÄ f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y} [IsCofibrant X]
    (h : P.LeftHomotopy f‚ÇÄ f‚ÇÅ) {P' : Cylinder X} [P'.IsGood]
    (h' : P'.LeftHomotopy f‚ÇÅ f‚ÇÇ) : (P.trans P').LeftHomotopy f‚ÇÄ f‚ÇÇ where
  h := pushout.desc h.h h'.h (by simp)

@[simps]
def postcomp {f g : X ‚ü∂ Y} (h : P.LeftHomotopy f g) {Z : C} (p : Y ‚ü∂ Z) :
    P.LeftHomotopy (f ‚â´ p) (g ‚â´ p) where
  h := h.h ‚â´ p

lemma exists_good {f g : X ‚ü∂ Y} (h : P.LeftHomotopy f g) :
    ‚àÉ (P' : Cylinder X), P'.IsGood ‚àß Nonempty (P'.LeftHomotopy f g) := by
  have d := MorphismProperty.factorizationData (cofibrations C) (trivialFibrations C) P.i
  exact ‚ü®{
    I := d.Z
    i‚ÇÄ := coprod.inl ‚â´ d.i
    i‚ÇÅ := coprod.inr ‚â´ d.i
    œÄ := d.p ‚â´ P.œÄ }, ‚ü®by
      rw [cofibration_iff]
      convert d.hi
      aesop‚ü©, ‚ü®{ h := d.p ‚â´ h.h }‚ü©‚ü©

lemma weakEquivalence_iff {f‚ÇÄ f‚ÇÅ : X ‚ü∂ Y} (h : P.LeftHomotopy f‚ÇÄ f‚ÇÅ) :
    WeakEquivalence f‚ÇÄ ‚Üî WeakEquivalence f‚ÇÅ := by
  revert P f‚ÇÄ f‚ÇÅ
  suffices ‚àÄ (P : Cylinder X) {f‚ÇÄ f‚ÇÅ : X ‚ü∂ Y} (h : P.LeftHomotopy f‚ÇÄ f‚ÇÅ),
      WeakEquivalence f‚ÇÄ ‚Üí WeakEquivalence f‚ÇÅ
    from fun _ _ _ h ‚Ü¶ ‚ü®this _ h, this _ h.symm‚ü©
  intro P f‚ÇÄ f‚ÇÅ h h‚ÇÄ
  have := weakEquivalence_of_precomp_of_fac h.h‚ÇÄ
  rw [‚Üê h.h‚ÇÅ]
  infer_instance

lemma covering_homotopy {A E B : C} {P : Cylinder A} {f‚ÇÄ f‚ÇÅ : A ‚ü∂ B}
    [IsCofibrant A] [P.IsGood]
    (h : P.LeftHomotopy f‚ÇÄ f‚ÇÅ) (p : E ‚ü∂ B) [Fibration p]
    (l‚ÇÄ : A ‚ü∂ E) (hl‚ÇÄ : l‚ÇÄ ‚â´ p = f‚ÇÄ) :
    ‚àÉ (l‚ÇÅ : A ‚ü∂ E) (h' : P.LeftHomotopy l‚ÇÄ l‚ÇÅ), h'.h ‚â´ p = h.h := by
  have sq : CommSq l‚ÇÄ P.i‚ÇÄ p h.h := ‚ü®by aesop_cat‚ü©
  exact ‚ü®P.i‚ÇÅ ‚â´ sq.lift, { h := sq.lift }, by simp‚ü©

end LeftHomotopy

end Cylinder

namespace PathObject

variable {Y : C} (P : PathObject Y) {X : C}

structure RightHomotopy (f g : X ‚ü∂ Y) where
  h : X ‚ü∂ P.P
  h‚ÇÄ : h ‚â´ P.p‚ÇÄ = f := by aesop_cat
  h‚ÇÅ : h ‚â´ P.p‚ÇÅ = g := by aesop_cat

namespace RightHomotopy

attribute [reassoc (attr := simp)] h‚ÇÄ h‚ÇÅ

variable {P}

@[simps]
def refl (f : X ‚ü∂ Y) : P.RightHomotopy f f where
  h := f ‚â´ P.Œπ

@[simps]
def symm {f g : X ‚ü∂ Y} (h : P.RightHomotopy f g) : P.symm.RightHomotopy g f where
  h := h.h

@[simps]
noncomputable def trans {f‚ÇÄ f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y} [IsFibrant Y]
    (h : P.RightHomotopy f‚ÇÄ f‚ÇÅ) {P' : PathObject Y} [P'.IsGood]
    (h' : P'.RightHomotopy f‚ÇÅ f‚ÇÇ) : (P.trans P').RightHomotopy f‚ÇÄ f‚ÇÇ where
  h := pullback.lift h.h h'.h (by simp)

@[simps]
def precomp {f g : X ‚ü∂ Y} (h : P.RightHomotopy f g) {Z : C} (i : Z ‚ü∂ X) :
    P.RightHomotopy (i ‚â´ f) (i ‚â´ g) where
  h := i ‚â´ h.h

lemma exists_good {f g : X ‚ü∂ Y} (h : P.RightHomotopy f g) :
    ‚àÉ (P' : PathObject Y), P'.IsGood ‚àß Nonempty (P'.RightHomotopy f g) := by
  have d := MorphismProperty.factorizationData (trivialCofibrations C) (fibrations C) P.p
  exact ‚ü®{
    P := d.Z
    p‚ÇÄ := d.p ‚â´ prod.fst
    p‚ÇÅ := d.p ‚â´ prod.snd
    Œπ := P.Œπ ‚â´ d.i }, ‚ü®by
      rw [fibration_iff]
      convert d.hp
      aesop‚ü©, ‚ü®{ h := h.h ‚â´ d.i }‚ü©‚ü©

lemma weakEquivalence_iff {f‚ÇÄ f‚ÇÅ : X ‚ü∂ Y} (h : P.RightHomotopy f‚ÇÄ f‚ÇÅ) :
    WeakEquivalence f‚ÇÄ ‚Üî WeakEquivalence f‚ÇÅ := by
  revert P f‚ÇÄ f‚ÇÅ
  suffices ‚àÄ (P : PathObject Y) {f‚ÇÄ f‚ÇÅ : X ‚ü∂ Y} (h : P.RightHomotopy f‚ÇÄ f‚ÇÅ),
      WeakEquivalence f‚ÇÄ ‚Üí WeakEquivalence f‚ÇÅ
    from fun _ _ _ h ‚Ü¶ ‚ü®this _ h, this _ h.symm‚ü©
  intro P f‚ÇÄ f‚ÇÅ h h‚ÇÄ
  have := weakEquivalence_of_postcomp_of_fac h.h‚ÇÄ
  rw [‚Üê h.h‚ÇÅ]
  infer_instance

lemma homotopy_extension {A B : C} {f‚ÇÄ f‚ÇÅ : A ‚ü∂ Y}
    [IsFibrant Y] [P.IsGood]
    (h : P.RightHomotopy f‚ÇÄ f‚ÇÅ) (i : A ‚ü∂ B) [Cofibration i]
    (l‚ÇÄ : B ‚ü∂ Y) (hl‚ÇÄ : i ‚â´ l‚ÇÄ = f‚ÇÄ) :
    ‚àÉ (l‚ÇÅ : B ‚ü∂ Y) (h' : P.RightHomotopy l‚ÇÄ l‚ÇÅ), i ‚â´ h'.h = h.h := by
  have sq : CommSq h.h i P.p‚ÇÄ l‚ÇÄ := ‚ü®by aesop_cat‚ü©
  exact ‚ü®sq.lift ‚â´ P.p‚ÇÅ, { h := sq.lift }, by simp‚ü©

end RightHomotopy

end PathObject

def LeftHomotopyRel : HomRel C :=
  fun X _ f g ‚Ü¶ ‚àÉ (P : Cylinder X), Nonempty (P.LeftHomotopy f g)

lemma factorsThroughLocalization_leftHomotopyRel :
    LeftHomotopyRel.FactorsThroughLocalization (weakEquivalences C) := by
  rintro X Y f g ‚ü®P, ‚ü®h‚ü©‚ü©
  let L := (weakEquivalences C).Q
  rw [areEqualizedByLocalization_iff L]
  suffices L.map P.i‚ÇÄ = L.map P.i‚ÇÅ by
    simp only [‚Üê h.h‚ÇÄ, ‚Üê h.h‚ÇÅ, L.map_comp, this]
  have := Localization.inverts L (weakEquivalences C) P.œÄ (by
    rw [‚Üê weakEquivalence_iff]
    infer_instance)
  simp only [‚Üê cancel_mono (L.map P.œÄ), ‚Üê L.map_comp, P.i‚ÇÄ_œÄ, P.i‚ÇÅ_œÄ]

lemma Cylinder.LeftHomotopy.leftHomotopyRel {X Y : C} {f g : X ‚ü∂ Y}
    {P : Cylinder X} (h : P.LeftHomotopy f g) :
    LeftHomotopyRel f g :=
  ‚ü®_, ‚ü®h‚ü©‚ü©

namespace LeftHomotopyRel

variable {X Y : C}

lemma refl (f : X ‚ü∂ Y) : LeftHomotopyRel f f :=
  ‚ü®Classical.arbitrary _, ‚ü®.refl _‚ü©‚ü©

lemma postcomp {f g : X ‚ü∂ Y} (h : LeftHomotopyRel f g) {Z : C} (p : Y ‚ü∂ Z) :
    LeftHomotopyRel (f ‚â´ p) (g ‚â´ p) := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  exact (h.postcomp p).leftHomotopyRel

lemma exists_good {f g : X ‚ü∂ Y} (h : LeftHomotopyRel f g) :
    ‚àÉ (P : Cylinder X), P.IsGood ‚àß Nonempty (P.LeftHomotopy f g) := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  exact h.exists_good

lemma exists_very_good {f g : X ‚ü∂ Y} [IsFibrant Y] (h : LeftHomotopyRel f g) :
    ‚àÉ (P : Cylinder X), P.IsVeryGood ‚àß Nonempty (P.LeftHomotopy f g) := by
  obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good
  have fac := MorphismProperty.factorizationData (trivialCofibrations C) (fibrations C) P.œÄ
  let P' : Cylinder X :=
    { I := fac.Z
      i‚ÇÄ := P.i‚ÇÄ ‚â´ fac.i
      i‚ÇÅ := P.i‚ÇÅ ‚â´ fac.i
      œÄ := fac.p
      weakEquivalence_œÄ := weakEquivalence_of_precomp_of_fac fac.fac }
  have : Cofibration P'.i := by
    rw [show P'.i = P.i ‚â´ fac.i by aesop_cat]
    infer_instance
  have sq : CommSq h.h fac.i (terminal.from _) (terminal.from _) := ‚ü®by simp‚ü©
  exact ‚ü®P', { }, ‚ü®{ h := sq.lift }‚ü© ‚ü©

lemma symm {f g : X ‚ü∂ Y} (h : LeftHomotopyRel f g) : LeftHomotopyRel g f := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  exact h.symm.leftHomotopyRel

lemma trans {f‚ÇÄ f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y} [IsCofibrant X] (h : LeftHomotopyRel f‚ÇÄ f‚ÇÅ)
    (h' : LeftHomotopyRel f‚ÇÅ f‚ÇÇ) : LeftHomotopyRel f‚ÇÄ f‚ÇÇ := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  obtain ‚ü®P', _, ‚ü®h'‚ü©‚ü© := h'.exists_good
  exact (h.trans h').leftHomotopyRel

instance equivalence (X Y : C) [IsCofibrant X] :
    _root_.Equivalence (LeftHomotopyRel (X := X) (Y := Y)) where
  refl := .refl
  symm h := h.symm
  trans h h' := h.trans h'

lemma precomp {f g : X ‚ü∂ Y} [IsFibrant Y] (h : LeftHomotopyRel f g)
    {Z : C} (i : Z ‚ü∂ X) : LeftHomotopyRel (i ‚â´ f) (i ‚â´ g) := by
  obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_very_good
  obtain ‚ü®Q, _‚ü© := Cylinder.exists_very_good Z
  have sq : CommSq (coprod.desc (i ‚â´ P.i‚ÇÄ) (i ‚â´ P.i‚ÇÅ)) Q.i P.œÄ (Q.œÄ ‚â´ i) := ‚ü®by aesop_cat‚ü©
  exact ‚ü®Q, ‚ü®{
    h := sq.lift ‚â´ h.h
    h‚ÇÄ := by
      have := coprod.inl ‚â´= sq.fac_left
      simp only [Q.inl_i_assoc, coprod.inl_desc] at this
      simp [reassoc_of% this]
    h‚ÇÅ := by
      have := coprod.inr ‚â´= sq.fac_left
      simp only [Q.inr_i_assoc, coprod.inr_desc] at this
      simp [reassoc_of% this]
  }‚ü©‚ü©

end LeftHomotopyRel

def RightHomotopyRel : HomRel C :=
  fun _ Y f g ‚Ü¶ ‚àÉ (P : PathObject Y), Nonempty (P.RightHomotopy f g)

lemma factorsThroughLocalization_rightHomotopyRel :
    RightHomotopyRel.FactorsThroughLocalization (weakEquivalences C) := by
  rintro X Y f g ‚ü®P, ‚ü®h‚ü©‚ü©
  let L := (weakEquivalences C).Q
  rw [areEqualizedByLocalization_iff L]
  suffices L.map P.p‚ÇÄ = L.map P.p‚ÇÅ by
    simp only [‚Üê h.h‚ÇÄ, ‚Üê h.h‚ÇÅ, L.map_comp, this]
  have := Localization.inverts L (weakEquivalences C) P.Œπ (by
    rw [‚Üê weakEquivalence_iff]
    infer_instance)
  simp only [‚Üê cancel_epi (L.map P.Œπ), ‚Üê L.map_comp, P.Œπ_p‚ÇÄ, P.Œπ_p‚ÇÅ]

lemma PathObject.RightHomotopy.rightHomotopyRel {X Y : C} {f g : X ‚ü∂ Y}
    {P : PathObject Y} (h : P.RightHomotopy f g) :
    RightHomotopyRel f g :=
  ‚ü®_, ‚ü®h‚ü©‚ü©

namespace RightHomotopyRel

variable {X Y : C}

lemma refl (f : X ‚ü∂ Y) : RightHomotopyRel f f :=
  ‚ü®Classical.arbitrary _, ‚ü®.refl _‚ü©‚ü©

lemma precomp {f g : X ‚ü∂ Y} (h : RightHomotopyRel f g) {Z : C} (i : Z ‚ü∂ X) :
    RightHomotopyRel (i ‚â´ f) (i ‚â´ g) := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  exact (h.precomp i).rightHomotopyRel

lemma exists_good {f g : X ‚ü∂ Y} (h : RightHomotopyRel f g) :
    ‚àÉ (P : PathObject Y), P.IsGood ‚àß Nonempty (P.RightHomotopy f g) := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  exact h.exists_good

lemma exists_very_good {f g : X ‚ü∂ Y} [IsCofibrant X] (h : RightHomotopyRel f g) :
    ‚àÉ (P : PathObject Y), P.IsVeryGood ‚àß Nonempty (P.RightHomotopy f g) := by
  obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good
  have fac := MorphismProperty.factorizationData (cofibrations C) (trivialFibrations C) P.Œπ
  let P' : PathObject Y :=
    { P := fac.Z
      p‚ÇÄ := fac.p ‚â´ P.p‚ÇÄ
      p‚ÇÅ := fac.p ‚â´ P.p‚ÇÅ
      Œπ := fac.i
      weakEquivalence_Œπ := weakEquivalence_of_postcomp_of_fac fac.fac }
  have : Fibration P'.p := by
    rw [show P'.p = fac.p ‚â´ P.p by aesop_cat]
    infer_instance
  have sq : CommSq (initial.to _) (initial.to _) fac.p h.h := ‚ü®by simp‚ü©
  exact ‚ü®P', { }, ‚ü®{ h := sq.lift }‚ü© ‚ü©

lemma symm {f g : X ‚ü∂ Y} (h : RightHomotopyRel f g) : RightHomotopyRel g f := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  exact h.symm.rightHomotopyRel

lemma trans {f‚ÇÄ f‚ÇÅ f‚ÇÇ : X ‚ü∂ Y} [IsFibrant Y] (h : RightHomotopyRel f‚ÇÄ f‚ÇÅ)
    (h' : RightHomotopyRel f‚ÇÅ f‚ÇÇ) : RightHomotopyRel f‚ÇÄ f‚ÇÇ := by
  obtain ‚ü®P, ‚ü®h‚ü©‚ü© := h
  obtain ‚ü®P', _, ‚ü®h'‚ü©‚ü© := h'.exists_good
  exact (h.trans h').rightHomotopyRel

instance equivalence (X Y : C) [IsFibrant Y] :
    _root_.Equivalence (RightHomotopyRel (X := X) (Y := Y)) where
  refl := .refl
  symm h := h.symm
  trans h h' := h.trans h'

lemma postcomp {f g : X ‚ü∂ Y} [IsCofibrant X] (h : RightHomotopyRel f g)
    {Z : C} (p : Y ‚ü∂ Z) : RightHomotopyRel (f ‚â´ p) (g ‚â´ p) := by
  obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_very_good
  obtain ‚ü®Q, _‚ü© := PathObject.exists_very_good Z
  have sq : CommSq (p ‚â´ Q.Œπ) P.Œπ Q.p (prod.lift (P.p‚ÇÄ ‚â´ p) (P.p‚ÇÅ ‚â´ p)) := ‚ü®by aesop_cat‚ü©
  exact ‚ü®Q, ‚ü®{
    h := h.h ‚â´ sq.lift
    h‚ÇÄ := by
      have := sq.fac_right =‚â´ prod.fst
      simp only [Category.assoc, Q.p_fst, prod.lift_fst] at this
      simp [this]
    h‚ÇÅ := by
      have := sq.fac_right =‚â´ prod.snd
      simp only [Category.assoc, Q.p_snd, prod.lift_snd] at this
      simp [this]
  }‚ü©‚ü©

end RightHomotopyRel

namespace LeftHomotopyRel

variable {X Y : C} {f g : X ‚ü∂ Y} [IsCofibrant X]

noncomputable def rightHomotopy (h : LeftHomotopyRel f g) (Q : PathObject Y) [Q.IsGood] :
    Q.RightHomotopy f g := by
  apply Nonempty.some
  obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good
  have sq : CommSq (f ‚â´ Q.Œπ) P.i‚ÇÄ Q.p (prod.lift (P.œÄ ‚â´ f) h.h) := ‚ü®by aesop_cat‚ü©
  exact ‚ü®{
    h := P.i‚ÇÅ ‚â´ sq.lift
    h‚ÇÄ := by
      have := sq.fac_right =‚â´ prod.fst
      rw [Category.assoc, Q.p_fst, prod.lift_fst] at this
      simp [this]
    h‚ÇÅ := by
      have := sq.fac_right =‚â´ prod.snd
      rw [Category.assoc, Q.p_snd, prod.lift_snd] at this
      simp [this]
  }‚ü©

lemma rightHomotopyRel (h : LeftHomotopyRel f g) : RightHomotopyRel f g := by
  obtain ‚ü®P, _‚ü© := PathObject.exists_very_good Y
  exact ‚ü®P, ‚ü®h.rightHomotopy P‚ü©‚ü©

end LeftHomotopyRel

namespace RightHomotopyRel

variable {X Y : C} {f g : X ‚ü∂ Y} [IsFibrant Y]

noncomputable def leftHomotopy (h : RightHomotopyRel f g) (Q : Cylinder X) [Q.IsGood] :
    Q.LeftHomotopy f g := by
  apply Nonempty.some
  obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good
  have sq : CommSq (coprod.desc (f ‚â´ P.Œπ) h.h) Q.i P.p‚ÇÄ (Q.œÄ ‚â´ f) := ‚ü®by aesop_cat‚ü©
  exact ‚ü®{
    h := sq.lift ‚â´ P.p‚ÇÅ
    h‚ÇÄ := by
      have := coprod.inl ‚â´= sq.fac_left
      rw [Q.inl_i_assoc, coprod.inl_desc] at this
      simp [reassoc_of% this]
    h‚ÇÅ := by
      have := coprod.inr ‚â´= sq.fac_left
      rw [Q.inr_i_assoc, coprod.inr_desc] at this
      simp [reassoc_of% this]
  }‚ü©

lemma leftHomotopyRel (h : RightHomotopyRel f g) : LeftHomotopyRel f g := by
  obtain ‚ü®P, _‚ü© := Cylinder.exists_very_good X
  exact ‚ü®P, ‚ü®h.leftHomotopy P‚ü©‚ü©

end RightHomotopyRel

lemma leftHomotopyRel_iff_rightHomotopyRel {X Y : C} (f g : X ‚ü∂ Y)
    [IsCofibrant X] [IsFibrant Y] :
    LeftHomotopyRel f g ‚Üî RightHomotopyRel f g :=
  ‚ü®fun h ‚Ü¶ h.rightHomotopyRel, fun h ‚Ü¶ h.leftHomotopyRel‚ü©

variable (X Y Z : C)

def LeftHomotopyClass :=
  _root_.Quot (LeftHomotopyRel (X := X) (Y := Y))

def RightHomotopyClass (X Y : C) :=
  _root_.Quot (RightHomotopyRel (X := X) (Y := Y))

variable {X Y Z}

def LeftHomotopyClass.mk : (X ‚ü∂ Y) ‚Üí LeftHomotopyClass X Y := Quot.mk _

def RightHomotopyClass.mk : (X ‚ü∂ Y) ‚Üí RightHomotopyClass X Y := Quot.mk _

lemma LeftHomotopyClass.mk_surjective : Function.Surjective (mk : (X ‚ü∂ Y) ‚Üí _) :=
  Quot.mk_surjective

lemma RightHomotopyClass.mk_surjective : Function.Surjective (mk : (X ‚ü∂ Y) ‚Üí _) :=
  Quot.mk_surjective

namespace LeftHomotopyClass

lemma sound {f g : X ‚ü∂ Y} (h : LeftHomotopyRel f g) : mk f = mk g := Quot.sound h

def postcomp : LeftHomotopyClass X Y ‚Üí (Y ‚ü∂ Z) ‚Üí LeftHomotopyClass X Z :=
  fun f g ‚Ü¶ Quot.lift (fun f ‚Ü¶ mk (f ‚â´ g)) (fun _ _ h ‚Ü¶ sound (h.postcomp g)) f

@[simp]
lemma postcomp_mk (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (mk f).postcomp g = mk (f ‚â´ g) := rfl

def comp [IsFibrant Z] :
    LeftHomotopyClass X Y ‚Üí LeftHomotopyClass Y Z ‚Üí LeftHomotopyClass X Z :=
  Quot.lift‚ÇÇ (fun f g ‚Ü¶ mk (f ‚â´ g)) (fun f _ _ h ‚Ü¶ sound (h.precomp f))
    (fun _ _ g h ‚Ü¶ sound (h.postcomp g))

@[simp]
lemma mk_comp_mk [IsFibrant Z] (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (mk f).comp (mk g) = mk (f ‚â´ g) := rfl

lemma mk_eq_mk_iff [IsCofibrant X] (f g : X ‚ü∂ Y) :
    mk f = mk g ‚Üî LeftHomotopyRel f g := by
  rw [‚Üê (LeftHomotopyRel.equivalence X Y).eqvGen_iff ]
  exact Quot.eq

variable (X) in
lemma bijective_postcomp_of_fibration_of_weakEquivalence
    [IsCofibrant X] (g : Y ‚ü∂ Z) [Fibration g] [WeakEquivalence g] :
    Function.Bijective (fun (f : LeftHomotopyClass X Y) ‚Ü¶ f.postcomp g) := by
  constructor
  ¬∑ intro f‚ÇÄ f‚ÇÅ h
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := f‚ÇÄ.mk_surjective
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := f‚ÇÅ.mk_surjective
    simp only [postcomp_mk, mk_eq_mk_iff] at h
    obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good
    have sq : CommSq (coprod.desc f‚ÇÄ f‚ÇÅ) P.i g h.h := ‚ü®by aesop_cat‚ü©
    rw [mk_eq_mk_iff]
    exact ‚ü®P, ‚ü®{
      h := sq.lift
      h‚ÇÄ := by
        have := coprod.inl ‚â´= sq.fac_left
        rwa [P.inl_i_assoc, coprod.inl_desc] at this
      h‚ÇÅ := by
        have := coprod.inr ‚â´= sq.fac_left
        rwa [P.inr_i_assoc, coprod.inr_desc] at this
    }‚ü©‚ü©
  ¬∑ intro œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    have sq : CommSq (initial.to Y) (initial.to X) g œÜ := ‚ü®by simp‚ü©
    exact ‚ü®mk sq.lift, by simp‚ü©

variable (X) in
lemma bijective_postcomp_of_weakEquivalence
    [IsCofibrant X] (g : Y ‚ü∂ Z) [IsFibrant Y] [IsFibrant Z] [WeakEquivalence g] :
    Function.Bijective (fun (f : LeftHomotopyClass X Y) ‚Ü¶ f.postcomp g) := by
  have h : FibrantBrownFactorization g := Classical.arbitrary _
  have hi : Function.Bijective (fun (f : LeftHomotopyClass X Y) ‚Ü¶ f.postcomp h.i) := by
    rw [‚Üê Function.Bijective.of_comp_iff'
      (bijective_postcomp_of_fibration_of_weakEquivalence X h.r)]
    convert Function.bijective_id
    ext œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    simp
  convert (bijective_postcomp_of_fibration_of_weakEquivalence X h.j).comp hi using 1
  ext œÜ
  obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
  simp

end LeftHomotopyClass

namespace RightHomotopyClass

lemma sound {f g : X ‚ü∂ Y} (h : RightHomotopyRel f g) : mk f = mk g := Quot.sound h

def precomp : RightHomotopyClass Y Z ‚Üí (X ‚ü∂ Y) ‚Üí RightHomotopyClass X Z :=
  fun f g ‚Ü¶ Quot.lift (fun f ‚Ü¶ mk (g ‚â´ f)) (fun _ _ h ‚Ü¶ sound (h.precomp g)) f

@[simp]
lemma precomp_mk (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (mk g).precomp f = mk (f ‚â´ g) := rfl

def comp [IsCofibrant X] :
    RightHomotopyClass X Y ‚Üí RightHomotopyClass Y Z ‚Üí RightHomotopyClass X Z :=
  Quot.lift‚ÇÇ (fun f g ‚Ü¶ mk (f ‚â´ g)) (fun f _ _ h ‚Ü¶ sound (h.precomp f))
    (fun _ _ g h ‚Ü¶ sound (h.postcomp g))

@[simp]
lemma mk_comp_mk [IsCofibrant X] (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (mk f).comp (mk g) = mk (f ‚â´ g) := rfl

lemma mk_eq_mk_iff [IsFibrant Y] (f g : X ‚ü∂ Y) :
    mk f = mk g ‚Üî RightHomotopyRel f g := by
  rw [‚Üê (RightHomotopyRel.equivalence X Y).eqvGen_iff ]
  exact Quot.eq

variable (Z) in
lemma bijective_precomp_of_cofibration_of_weakEquivalence
    [IsFibrant Z] (f : X ‚ü∂ Y) [Cofibration f] [WeakEquivalence f] :
    Function.Bijective (fun (g : RightHomotopyClass Y Z) ‚Ü¶ g.precomp f) := by
  constructor
  ¬∑ intro f‚ÇÄ f‚ÇÅ h
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := f‚ÇÄ.mk_surjective
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := f‚ÇÅ.mk_surjective
    simp only [precomp_mk, mk_eq_mk_iff] at h
    obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good
    have sq : CommSq h.h f P.p (prod.lift f‚ÇÄ f‚ÇÅ) := ‚ü®by aesop_cat‚ü©
    rw [mk_eq_mk_iff]
    exact ‚ü®P, ‚ü®{
      h := sq.lift
      h‚ÇÄ := by
        have := sq.fac_right =‚â´ prod.fst
        rwa [Category.assoc, P.p_fst, prod.lift_fst] at this
      h‚ÇÅ := by
        have := sq.fac_right =‚â´ prod.snd
        rwa [Category.assoc, P.p_snd, prod.lift_snd] at this
    }‚ü©‚ü©
  ¬∑ intro œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    have sq : CommSq œÜ f (terminal.from _) (terminal.from _) := ‚ü®by simp‚ü©
    exact ‚ü®mk sq.lift, by simp‚ü©

variable (Z) in
lemma bijective_precomp_of_weakEquivalence
    [IsFibrant Z] (f : X ‚ü∂ Y) [IsCofibrant X] [IsCofibrant Y] [WeakEquivalence f] :
    Function.Bijective (fun (g : RightHomotopyClass Y Z) ‚Ü¶ g.precomp f) := by
  have h : CofibrantBrownFactorization f := Classical.arbitrary _
  have hj : Function.Bijective (fun (g : RightHomotopyClass Y Z) ‚Ü¶ g.precomp h.j) := by
    rw [‚Üê Function.Bijective.of_comp_iff'
      (bijective_precomp_of_cofibration_of_weakEquivalence Z h.s)]
    convert Function.bijective_id
    ext œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    simp
  convert (bijective_precomp_of_cofibration_of_weakEquivalence Z h.i).comp hj using 1
  ext œÜ
  obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
  simp

lemma exists_homotopy_inverse [IsCofibrant X] [IsCofibrant Y]
    [IsFibrant X] [IsFibrant Y] (f : X ‚ü∂ Y) [WeakEquivalence f] :
    ‚àÉ (g : Y ‚ü∂ X), RightHomotopyRel (f ‚â´ g) (ùüô X) ‚àß RightHomotopyRel (g ‚â´ f) (ùüô Y) := by
  obtain ‚ü®g, hg‚ü© := (bijective_precomp_of_weakEquivalence X f).2 (.mk (ùüô X))
  obtain ‚ü®g, rfl‚ü© := g.mk_surjective
  dsimp at hg
  refine ‚ü®g, by rwa [‚Üê mk_eq_mk_iff], ?_‚ü©
  rw [‚Üê mk_eq_mk_iff]
  apply (bijective_precomp_of_weakEquivalence Y f).1
  simp only [precomp_mk, Category.comp_id]
  rw [mk_eq_mk_iff, ‚Üê leftHomotopyRel_iff_rightHomotopyRel] at hg ‚ä¢
  simpa using hg.postcomp f

end RightHomotopyClass

lemma LeftHomotopyClass.exists_homotopy_inverse
    [IsCofibrant X] [IsCofibrant Y]
    [IsFibrant X] [IsFibrant Y] (f : X ‚ü∂ Y) [WeakEquivalence f] :
    ‚àÉ (g : Y ‚ü∂ X), LeftHomotopyRel (f ‚â´ g) (ùüô X) ‚àß LeftHomotopyRel (g ‚â´ f) (ùüô Y) := by
  simp only [leftHomotopyRel_iff_rightHomotopyRel]
  apply RightHomotopyClass.exists_homotopy_inverse

section

variable [IsCofibrant X] [IsFibrant Y]

def leftHomotopyClassEquivRightHomotopyClass :
    LeftHomotopyClass X Y ‚âÉ RightHomotopyClass X Y where
  toFun := Quot.lift (fun f ‚Ü¶ .mk f) (fun _ _ h ‚Ü¶ by
    dsimp
    rw [RightHomotopyClass.mk_eq_mk_iff]
    exact h.rightHomotopyRel)
  invFun := Quot.lift (fun f ‚Ü¶ .mk f) (fun _ _ h ‚Ü¶ by
    dsimp
    rw [LeftHomotopyClass.mk_eq_mk_iff]
    exact h.leftHomotopyRel)
  left_inv := by rintro ‚ü®f‚ü©; rfl
  right_inv := by rintro ‚ü®f‚ü©; rfl

@[simp]
lemma leftHomotopyClassEquivRightHomotopyClass_mk (f : X ‚ü∂ Y) :
    leftHomotopyClassEquivRightHomotopyClass (.mk f) = .mk f := rfl

@[simp]
lemma leftHomotopyClassEquivRightHomotopyClass_symm_mk (f : X ‚ü∂ Y) :
    leftHomotopyClassEquivRightHomotopyClass.symm (.mk f) = .mk f := rfl

end

end HomotopicalAlgebra
