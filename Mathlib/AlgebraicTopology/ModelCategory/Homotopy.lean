/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.ModelCategory.BrownLemma
import Mathlib.AlgebraicTopology.ModelCategory.LeftHomotopy
import Mathlib.AlgebraicTopology.ModelCategory.RightHomotopy

/-!
# Homotopies in model categories

In this file, we relate left and right homotopies between
morphisms `X ‚ü∂ Y` in model categories. In particular, if `X` is cofibrant
and `Y` is fibrant, these notions coincide (for arbitrary choices of good
cylinders or good path objects).

Using the factorization lemma by K. S. Brown, we deduce versions of the Whitehead
theorem (`LeftHomotopyClass.whitehead` and `RightHomotopyClass.whitehead`)
which assert that when both `X` and `Y` are fibrant and cofibrant,
then any weak equivalence `X ‚ü∂ Y` is a homotopy equivalence.

## References
* [Daniel G. Quillen, Homotopical algebra, section I.1][Quillen1967]

-/

universe v u

open CategoryTheory Limits

namespace HomotopicalAlgebra

variable {C : Type u} [Category.{v} C] [ModelCategory C] {X Y Z : C}

namespace LeftHomotopyRel

variable {f g : X ‚ü∂ Y} [IsCofibrant X]

/-- When two morphisms `X ‚ü∂ Y` with `X` cofibrant are related by a left homotopy,
this is a choice of a right homotopy relative to any good path object for `Y`. -/
noncomputable def rightHomotopy (h : LeftHomotopyRel f g) (Q : PathObject Y) [Q.IsGood] :
    Q.RightHomotopy f g :=
  let P := h.exists_good_cylinder.choose
  have h := h.exists_good_cylinder.choose_spec.2.some
  have h' := h.exists_good_cylinder.choose_spec.1
  have sq : CommSq (f ‚â´ Q.Œπ) P.i‚ÇÄ Q.p (prod.lift (P.œÄ ‚â´ f) h.h) := { }
  { h := P.i‚ÇÅ ‚â´ sq.lift
    h‚ÇÄ := by
      have := sq.fac_right =‚â´ prod.fst
      rw [Category.assoc, Q.p_fst, prod.lift_fst] at this
      simp [this]
    h‚ÇÅ := by
      have := sq.fac_right =‚â´ prod.snd
      rw [Category.assoc, Q.p_snd, prod.lift_snd] at this
      simp [this] }

lemma rightHomotopyRel (h : LeftHomotopyRel f g) : RightHomotopyRel f g := by
  obtain ‚ü®P, _‚ü© := PathObject.exists_very_good Y
  exact ‚ü®_, ‚ü®h.rightHomotopy P‚ü©‚ü©

end LeftHomotopyRel

namespace RightHomotopyRel

variable {f g : X ‚ü∂ Y} [IsFibrant Y]

/-- When two morphisms `X ‚ü∂ Y` with `Y` fibrant are related by a right homotopy,
this is a choice of a left homotopy relative to any good cylinder object for `X`. -/
noncomputable def leftHomotopy (h : RightHomotopyRel f g) (Q : Cylinder X) [Q.IsGood] :
    Q.LeftHomotopy f g :=
  let P := h.exists_good_pathObject.choose
  have h := h.exists_good_pathObject.choose_spec.2.some
  have h' := h.exists_good_pathObject.choose_spec.1
  have sq : CommSq (coprod.desc (f ‚â´ P.Œπ) h.h) Q.i P.p‚ÇÄ (Q.œÄ ‚â´ f) := { }
  { h := sq.lift ‚â´ P.p‚ÇÅ
    h‚ÇÄ := by
      have := coprod.inl ‚â´= sq.fac_left
      rw [Q.inl_i_assoc, coprod.inl_desc] at this
      simp [reassoc_of% this]
    h‚ÇÅ := by
      have := coprod.inr ‚â´= sq.fac_left
      rw [Q.inr_i_assoc, coprod.inr_desc] at this
      simp [reassoc_of% this, P] }

lemma leftHomotopyRel (h : RightHomotopyRel f g) : LeftHomotopyRel f g := by
  obtain ‚ü®P, _‚ü© := Cylinder.exists_very_good X
  exact ‚ü®P, ‚ü®h.leftHomotopy P‚ü©‚ü©

end RightHomotopyRel

lemma leftHomotopyRel_iff_rightHomotopyRel {X Y : C} (f g : X ‚ü∂ Y)
    [IsCofibrant X] [IsFibrant Y] :
    LeftHomotopyRel f g ‚Üî RightHomotopyRel f g :=
  ‚ü®fun h ‚Ü¶ h.rightHomotopyRel, fun h ‚Ü¶ h.leftHomotopyRel‚ü©

namespace LeftHomotopyClass

variable (X)

lemma postcomp_bijective_of_fibration_of_weakEquivalence
    [IsCofibrant X] (g : Y ‚ü∂ Z) [Fibration g] [WeakEquivalence g] :
    Function.Bijective (fun (f : LeftHomotopyClass X Y) ‚Ü¶ f.postcomp g) := by
  constructor
  ¬∑ intro f‚ÇÄ f‚ÇÅ h
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := f‚ÇÄ.mk_surjective
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := f‚ÇÅ.mk_surjective
    simp only [postcomp_mk, mk_eq_mk_iff] at h
    obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good_cylinder
    have sq : CommSq (coprod.desc f‚ÇÄ f‚ÇÅ) P.i g h.h := { }
    rw [mk_eq_mk_iff]
    exact ‚ü®P,
      ‚ü®{h := sq.lift
        h‚ÇÄ := by
          have := coprod.inl ‚â´= sq.fac_left
          rwa [P.inl_i_assoc, coprod.inl_desc] at this
        h‚ÇÅ := by
          have := coprod.inr ‚â´= sq.fac_left
          rwa [P.inr_i_assoc, coprod.inr_desc] at this }‚ü©‚ü©
  ¬∑ intro œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    have sq : CommSq (initial.to Y) (initial.to X) g œÜ := { }
    exact ‚ü®mk sq.lift, by simp‚ü©

lemma postcomp_bijective_of_weakEquivalence
    [IsCofibrant X] (g : Y ‚ü∂ Z) [IsFibrant Y] [IsFibrant Z] [WeakEquivalence g] :
    Function.Bijective (fun (f : LeftHomotopyClass X Y) ‚Ü¶ f.postcomp g) := by
  let h : FibrantBrownFactorization g := Classical.arbitrary _
  have hi : Function.Bijective (fun (f : LeftHomotopyClass X Y) ‚Ü¶ f.postcomp h.i) := by
    rw [‚Üê Function.Bijective.of_comp_iff'
      (postcomp_bijective_of_fibration_of_weakEquivalence X h.r)]
    convert Function.bijective_id
    ext œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    simp
  convert (postcomp_bijective_of_fibration_of_weakEquivalence X h.p).comp hi using 1
  ext œÜ
  obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
  simp

end LeftHomotopyClass

namespace RightHomotopyClass

variable (Z)

lemma precomp_bijective_of_cofibration_of_weakEquivalence
    [IsFibrant Z] (f : X ‚ü∂ Y) [Cofibration f] [WeakEquivalence f] :
    Function.Bijective (fun (g : RightHomotopyClass Y Z) ‚Ü¶ g.precomp f) := by
  constructor
  ¬∑ intro f‚ÇÄ f‚ÇÅ h
    obtain ‚ü®f‚ÇÄ, rfl‚ü© := f‚ÇÄ.mk_surjective
    obtain ‚ü®f‚ÇÅ, rfl‚ü© := f‚ÇÅ.mk_surjective
    simp only [precomp_mk, mk_eq_mk_iff] at h
    obtain ‚ü®P, _, ‚ü®h‚ü©‚ü© := h.exists_good_pathObject
    have sq : CommSq h.h f P.p (prod.lift f‚ÇÄ f‚ÇÅ) := { }
    rw [mk_eq_mk_iff]
    exact ‚ü®P,
      ‚ü®{h := sq.lift
        h‚ÇÄ := by
          have := sq.fac_right =‚â´ prod.fst
          rwa [Category.assoc, P.p_fst, prod.lift_fst] at this
        h‚ÇÅ := by
          have := sq.fac_right =‚â´ prod.snd
          rwa [Category.assoc, P.p_snd, prod.lift_snd] at this }‚ü©‚ü©
  ¬∑ intro œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    have sq : CommSq œÜ f (terminal.from _) (terminal.from _) := { }
    exact ‚ü®mk sq.lift, by simp‚ü©

lemma precomp_bijective_of_weakEquivalence
    [IsFibrant Z] (f : X ‚ü∂ Y) [IsCofibrant X] [IsCofibrant Y] [WeakEquivalence f] :
    Function.Bijective (fun (g : RightHomotopyClass Y Z) ‚Ü¶ g.precomp f) := by
  let h : CofibrantBrownFactorization f := Classical.arbitrary _
  have hj : Function.Bijective (fun (g : RightHomotopyClass Y Z) ‚Ü¶ g.precomp h.p) := by
    rw [‚Üê Function.Bijective.of_comp_iff'
      (precomp_bijective_of_cofibration_of_weakEquivalence Z h.s)]
    convert Function.bijective_id
    ext œÜ
    obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
    simp
  convert (precomp_bijective_of_cofibration_of_weakEquivalence Z h.i).comp hj using 1
  ext œÜ
  obtain ‚ü®œÜ, rfl‚ü© := œÜ.mk_surjective
  simp

lemma whitehead [IsCofibrant X] [IsCofibrant Y] [IsFibrant X] [IsFibrant Y]
    (f : X ‚ü∂ Y) [WeakEquivalence f] :
    ‚àÉ (g : Y ‚ü∂ X), RightHomotopyRel (f ‚â´ g) (ùüô X) ‚àß RightHomotopyRel (g ‚â´ f) (ùüô Y) := by
  obtain ‚ü®g, hg‚ü© := (precomp_bijective_of_weakEquivalence X f).2 (.mk (ùüô X))
  obtain ‚ü®g, rfl‚ü© := g.mk_surjective
  dsimp at hg
  refine ‚ü®g, by rwa [‚Üê mk_eq_mk_iff], ?_‚ü©
  rw [‚Üê mk_eq_mk_iff]
  apply (precomp_bijective_of_weakEquivalence Y f).1
  simp only [precomp_mk, Category.comp_id]
  rw [mk_eq_mk_iff, ‚Üê leftHomotopyRel_iff_rightHomotopyRel] at hg ‚ä¢
  simpa using hg.postcomp f

end RightHomotopyClass

lemma LeftHomotopyClass.whitehead [IsCofibrant X] [IsCofibrant Y] [IsFibrant X] [IsFibrant Y]
    (f : X ‚ü∂ Y) [WeakEquivalence f] :
    ‚àÉ (g : Y ‚ü∂ X), LeftHomotopyRel (f ‚â´ g) (ùüô X) ‚àß LeftHomotopyRel (g ‚â´ f) (ùüô Y) := by
  simp only [leftHomotopyRel_iff_rightHomotopyRel]
  apply RightHomotopyClass.whitehead

section

variable [IsCofibrant X] [IsFibrant Y]

/-- Left homotopy classes of maps `X ‚ü∂ Y` identify to right homotopy classes
when `X` is cofibrant and `Y` is fibrant. -/
def leftHomotopyClassEquivRightHomotopyClass :
    LeftHomotopyClass X Y ‚âÉ RightHomotopyClass X Y where
  toFun := Quot.lift (fun f ‚Ü¶ .mk f) (fun _ _ h ‚Ü¶ by
    dsimp
    rw [RightHomotopyClass.mk_eq_mk_iff]
    exact h.rightHomotopyRel)
  invFun := Quot.lift (fun f ‚Ü¶ .mk f) (fun _ _ h ‚Ü¶ by
    dsimp
    rw [LeftHomotopyClass.mk_eq_mk_iff]
    exact h.leftHomotopyRel)
  left_inv := by rintro ‚ü®f‚ü©; rfl
  right_inv := by rintro ‚ü®f‚ü©; rfl

@[simp]
lemma leftHomotopyClassEquivRightHomotopyClass_mk (f : X ‚ü∂ Y) :
    leftHomotopyClassEquivRightHomotopyClass (.mk f) = .mk f := rfl

@[simp]
lemma leftHomotopyClassEquivRightHomotopyClass_symm_mk (f : X ‚ü∂ Y) :
    leftHomotopyClassEquivRightHomotopyClass.symm (.mk f) = .mk f := rfl

end

end HomotopicalAlgebra
