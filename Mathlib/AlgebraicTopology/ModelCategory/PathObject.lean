/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.AlgebraicTopology.ModelCategory.Basic
import Mathlib.AlgebraicTopology.ModelCategory.IsCofibrant

/-!
# Path objects

We introduce a notion of path object for an object `A : C` in a model category.
It consists of an object `P`, a weak equivalence `Ïƒ : P âŸ¶ A` equipped with two retractions
`pâ‚€` and `pâ‚`. This notion shall be important in the definition of "right homotopies"
in model categories.

This files dualizes the content of the file `ModelCategory.Cylinder`.

## References
* [Daniel G. Quillen, Homotopical algebra][Quillen1967]
* https://ncatlab.org/nlab/show/cylinder+object

-/

universe v u

open CategoryTheory Category Limits

namespace CategoryTheory

/--
In a category, given a morphism `f : A âŸ¶ B` and an object `X`,
this is the obvious pullback diagram:
```
A â¨¯ X âŸ¶ A
|       |
v       v
B â¨¯ X âŸ¶ B
```
-/
lemma IsPullback.of_prod_fst_with_ind {C : Type*} [Category C]
    {A B : C} (f : A âŸ¶ B) (X : C) [HasBinaryProduct A X]
    [HasBinaryProduct B X] :
    IsPullback prod.fst (prod.map f (ğŸ™ X)) f prod.fst where
  w := by simp
  isLimit' := âŸ¨PullbackCone.isLimitAux' _ (fun s â†¦ by
    refine âŸ¨prod.lift s.fst (s.snd â‰« prod.snd), ?_, ?_, ?_âŸ©
    Â· simp
    Â· ext
      Â· simp [PullbackCone.condition]
      Â· simp
    Â· intro m hâ‚ hâ‚‚
      dsimp at m hâ‚ hâ‚‚ âŠ¢
      ext
      Â· simpa using hâ‚
      Â· simp [â† hâ‚‚])âŸ©

end CategoryTheory

namespace HomotopicalAlgebra

variable {C : Type u} [Category.{v} C] [ModelCategory C]

/-- In a model category `C`, a cylinder for `A : C` is the data of
a weak equivalence `Ïƒ : I âŸ¶ A` equipped with two sections. -/
structure PathObject (A : C) where
  /-- the underlying object of a path object -/
  P : C
  /-- the first "projection" from the path object -/
  pâ‚€ : P âŸ¶ A
  /-- the second "projection" from the path object -/
  pâ‚ : P âŸ¶ A
  /-- the weak equivalence of the path object -/
  Ï : A âŸ¶ P
  Ï_pâ‚€ : Ï â‰« pâ‚€ = ğŸ™ A := by aesop_cat
  Ï_pâ‚ : Ï â‰« pâ‚ = ğŸ™ A := by aesop_cat
  weakEquivalence_Ï : WeakEquivalence Ï := by infer_instance

namespace PathObject

attribute [instance] weakEquivalence_Ï
attribute [reassoc (attr := simp)] Ï_pâ‚€ Ï_pâ‚

variable {A : C} (P : PathObject A)

instance : WeakEquivalence P.pâ‚€ :=
  weakEquivalence_of_precomp_of_fac P.Ï_pâ‚€

instance : WeakEquivalence P.pâ‚ :=
  weakEquivalence_of_precomp_of_fac P.Ï_pâ‚

/-- the map from `P.P` to the product of two copies of `A`, when `P` is
a path object object for `A`. `P` shall be a *good* path object
when this morphism is a fibration. -/
noncomputable def p : P.P âŸ¶ A â¨¯ A := prod.lift P.pâ‚€ P.pâ‚

@[reassoc (attr := simp)]
lemma p_fst : P.p â‰« prod.fst = P.pâ‚€ := by simp [p]

@[reassoc (attr := simp)]
lemma p_snd : P.p â‰« prod.snd = P.pâ‚ := by simp [p]

/-- The cylinder object obtained by switching the two inclusions. -/
@[simps]
def symm : PathObject A where
  P := P.P
  pâ‚€ := P.pâ‚
  pâ‚ := P.pâ‚€
  Ï := P.Ï

@[simp, reassoc]
lemma symm_p : P.symm.p =
  P.p â‰« (prod.braiding A A).hom := by aesop_cat

/-- A path object `P` is good if the morphism
`P.p : P.P âŸ¶ A â¨¯ A` is a fibration. -/
class IsGood : Prop where
  fibration_p : Fibration P.p := by infer_instance

/-- A good path object `P` is very good if `P.Ï` is a (trivial) cofibration. -/
class IsVeryGood : Prop extends P.IsGood where
  cofibration_Ï : Cofibration P.Ï := by infer_instance

attribute [instance] IsGood.fibration_p IsVeryGood.cofibration_Ï

instance [IsFibrant A] [P.IsGood] : Fibration P.pâ‚€ := by
  rw [â† P.p_fst]
  infer_instance

instance [IsFibrant A] [P.IsGood] : Fibration P.pâ‚ := by
  rw [â† P.p_snd]
  infer_instance

instance [IsFibrant A] [P.IsGood] : IsFibrant P.P :=
  isFibrant_of_fibration P.pâ‚€

instance [P.IsGood] : P.symm.IsGood where
  fibration_p := by
    dsimp
    rw [symm_p]
    infer_instance

instance [P.IsVeryGood] : P.symm.IsVeryGood where
  cofibration_Ï := by
    dsimp
    infer_instance

section

variable (h : MorphismProperty.MapFactorizationData (trivialCofibrations C) (fibrations C)
    (prod.lift (ğŸ™ A) (ğŸ™ A)))

/-- A path object for `A` can be obtained from a factorization of the obvious
map `A âŸ¶ A â¨¯ A` as a trivial cofibration followed by a fibration. -/
@[simps]
noncomputable def ofFactorizationData : PathObject A where
  P := h.Z
  pâ‚€ := h.p â‰« prod.fst
  pâ‚ := h.p â‰« prod.snd
  Ï := h.i

@[simp]
lemma ofFactorizationData_p : (ofFactorizationData h).p = h.p := by aesop_cat

instance : (ofFactorizationData h).IsVeryGood where
  fibration_p := by simpa using inferInstanceAs (Fibration h.p)
  cofibration_Ï := by dsimp; infer_instance

instance [HasInitial C] [IsCofibrant A] [(cofibrations C).IsStableUnderComposition] :
    IsCofibrant (ofFactorizationData h).P :=
  isCofibrant_of_cofibration (ofFactorizationData h).Ï

end

variable (A) in
lemma exists_very_good_pathObject :
    âˆƒ (P : PathObject A), P.IsVeryGood :=
  âŸ¨ofFactorizationData (MorphismProperty.factorizationData _ _ _),
    inferInstanceâŸ©

instance : Nonempty (PathObject A) := âŸ¨(exists_very_good_pathObject A).chooseâŸ©

/-- The gluing of two good path objects. -/
@[simps]
noncomputable def trans [IsFibrant A] (P P' : PathObject A) [P'.IsGood] :
    PathObject A where
  P := pullback P.pâ‚ P'.pâ‚€
  pâ‚€ := pullback.fst _ _ â‰« P.pâ‚€
  pâ‚ := pullback.snd _ _ â‰« P'.pâ‚
  Ï := pullback.lift P.Ï P'.Ï (by simp)
  weakEquivalence_Ï := by
    have : WeakEquivalence (pullback.lift P.Ï P'.Ï (by simp) â‰« pullback.fst P.pâ‚ P'.pâ‚€ â‰« P.pâ‚€) := by
      rw [pullback.lift_fst_assoc, Ï_pâ‚€]
      infer_instance
    apply weakEquivalence_of_postcomp _ (pullback.fst _ _ â‰« P.pâ‚€)

instance [IsFibrant A] (P P' : PathObject A) [P.IsGood] [P'.IsGood] :
    (P.trans P').IsGood where
  fibration_p := by
    let Ïˆ : (P.trans P').P âŸ¶ P.P â¨¯ A := prod.lift (pullback.fst _ _) (pullback.snd _ _ â‰« P'.pâ‚)
    rw [show (P.trans P').p = Ïˆ â‰« prod.map P.pâ‚€ (ğŸ™ A) by simp [PathObject.p, Ïˆ]]
    have fac : Ïˆ â‰« prod.map P.pâ‚ (ğŸ™ A) = pullback.snd _ _ â‰« P'.p := by
      ext
      Â· simp [Ïˆ, pullback.condition]
      Â· simp [Ïˆ]
    have sq : IsPullback (Ïˆ â‰« prod.fst) (pullback.snd _ _) P.pâ‚ (P'.p â‰« prod.fst) := by
      simpa [Ïˆ] using IsPullback.of_hasPullback P.pâ‚ P'.pâ‚€
    have : Fibration Ïˆ := by
      rw [fibration_iff]
      exact (fibrations C).of_isPullback (IsPullback.of_right sq fac
        (IsPullback.of_prod_fst_with_ind P.pâ‚ A)).flip
        (by rw [â† fibration_iff]; infer_instance)
    infer_instance

end PathObject

end HomotopicalAlgebra
