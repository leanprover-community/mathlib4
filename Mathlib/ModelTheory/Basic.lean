/-
Copyright (c) 2021 Aaron Anderson, Jesse Michael Han, Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson, Jesse Michael Han, Floris van Doorn
-/
import Mathlib.Data.Fin.VecNotation
import Mathlib.SetTheory.Cardinal.Basic

#align_import model_theory.basic from "leanprover-community/mathlib"@"369525b73f229ccd76a6ec0e0e0bf2be57599768"

/-!
# Basics on First-Order Structures
This file defines first-order languages and structures in the style of the
[Flypitch project](https://flypitch.github.io/), as well as several important maps between
structures.

## Main Definitions
* A `FirstOrder.Language` defines a language as a pair of functions from the natural numbers to
  `Type l`. One sends `n` to the type of `n`-ary functions, and the other sends `n` to the type of
  `n`-ary relations.
* A `FirstOrder.Language.Structure` interprets the symbols of a given `FirstOrder.Language` in the
  context of a given type.
* A `FirstOrder.Language.Hom`, denoted `M ‚Üí[L] N`, is a map from the `L`-structure `M` to the
  `L`-structure `N` that commutes with the interpretations of functions, and which preserves the
  interpretations of relations (although only in the forward direction).
* A `FirstOrder.Language.Embedding`, denoted `M ‚Ü™[L] N`, is an embedding from the `L`-structure `M`
  to the `L`-structure `N` that commutes with the interpretations of functions, and which preserves
  the interpretations of relations in both directions.
* A `FirstOrder.Language.Equiv`, denoted `M ‚âÉ[L] N`, is an equivalence from the `L`-structure `M`
  to the `L`-structure `N` that commutes with the interpretations of functions, and which preserves
  the interpretations of relations in both directions.

## References
For the Flypitch project:
- [J. Han, F. van Doorn, *A formal proof of the independence of the continuum hypothesis*]
[flypitch_cpp]
- [J. Han, F. van Doorn, *A formalization of forcing and the unprovability of
the continuum hypothesis*][flypitch_itp]

-/

set_option autoImplicit true


universe u v u' v' w w'

open Cardinal

open Cardinal

namespace FirstOrder

/-! ### Languages and Structures -/


-- intended to be used with explicit universe parameters
/-- A first-order language consists of a type of functions of every natural-number arity and a
  type of relations of every natural-number arity. -/
@[nolint checkUnivs]
structure Language where
  /-- For every arity, a `Type*` of functions of that arity -/
  Functions : ‚Ñï ‚Üí Type u
  /-- For every arity, a `Type*` of relations of that arity -/
  Relations : ‚Ñï ‚Üí Type v
#align first_order.language FirstOrder.Language

/-- Used to define `FirstOrder.Language‚ÇÇ`. -/
--@[simp]
def Sequence‚ÇÇ (a‚ÇÄ a‚ÇÅ a‚ÇÇ : Type u) : ‚Ñï ‚Üí Type u
  | 0 => a‚ÇÄ
  | 1 => a‚ÇÅ
  | 2 => a‚ÇÇ
  | _ => PEmpty
#align first_order.sequence‚ÇÇ FirstOrder.Sequence‚ÇÇ

namespace Sequence‚ÇÇ

variable (a‚ÇÄ a‚ÇÅ a‚ÇÇ : Type u)

instance inhabited‚ÇÄ [h : Inhabited a‚ÇÄ] : Inhabited (Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ 0) :=
  h
#align first_order.sequence‚ÇÇ.inhabited‚ÇÄ FirstOrder.Sequence‚ÇÇ.inhabited‚ÇÄ

instance inhabited‚ÇÅ [h : Inhabited a‚ÇÅ] : Inhabited (Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ 1) :=
  h
#align first_order.sequence‚ÇÇ.inhabited‚ÇÅ FirstOrder.Sequence‚ÇÇ.inhabited‚ÇÅ

instance inhabited‚ÇÇ [h : Inhabited a‚ÇÇ] : Inhabited (Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ 2) :=
  h
#align first_order.sequence‚ÇÇ.inhabited‚ÇÇ FirstOrder.Sequence‚ÇÇ.inhabited‚ÇÇ

instance {n : ‚Ñï} : IsEmpty (Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ (n + 3)) := inferInstanceAs (IsEmpty PEmpty)

@[simp]
theorem lift_mk {i : ‚Ñï} :
    Cardinal.lift.{v,u} #(Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ i)
      = #(Sequence‚ÇÇ (ULift.{v,u} a‚ÇÄ) (ULift.{v,u} a‚ÇÅ) (ULift.{v,u} a‚ÇÇ) i) := by
  rcases i with (_ | _ | _ | i) <;>
    simp only [Sequence‚ÇÇ, mk_uLift, mk_fintype, Fintype.card_of_isEmpty, Nat.cast_zero, lift_zero]
    -- üéâ no goals
    -- üéâ no goals
    -- üéâ no goals
    -- üéâ no goals
#align first_order.sequence‚ÇÇ.lift_mk FirstOrder.Sequence‚ÇÇ.lift_mk

@[simp]
theorem sum_card : Cardinal.sum (fun i => #(Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ i)) = #a‚ÇÄ + #a‚ÇÅ + #a‚ÇÇ := by
  rw [sum_nat_eq_add_sum_succ, sum_nat_eq_add_sum_succ, sum_nat_eq_add_sum_succ]
  -- ‚ä¢ #(Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ 0) + (#(Sequence‚ÇÇ a‚ÇÄ a‚ÇÅ a‚ÇÇ (0 + 1)) + (#(Sequence‚ÇÇ a‚ÇÄ a ‚Ä¶
  simp [add_assoc, Sequence‚ÇÇ]
  -- üéâ no goals
#align first_order.sequence‚ÇÇ.sum_card FirstOrder.Sequence‚ÇÇ.sum_card

end Sequence‚ÇÇ

namespace Language

/-- A constructor for languages with only constants, unary and binary functions, and
unary and binary relations. -/
@[simps]
protected def mk‚ÇÇ (c f‚ÇÅ f‚ÇÇ : Type u) (r‚ÇÅ r‚ÇÇ : Type v) : Language :=
  ‚ü®Sequence‚ÇÇ c f‚ÇÅ f‚ÇÇ, Sequence‚ÇÇ PEmpty r‚ÇÅ r‚ÇÇ‚ü©
#align first_order.language.mk‚ÇÇ FirstOrder.Language.mk‚ÇÇ

/-- The empty language has no symbols. -/
protected def empty : Language :=
  ‚ü®fun _ => Empty, fun _ => Empty‚ü©
#align first_order.language.empty FirstOrder.Language.empty

instance : Inhabited Language :=
  ‚ü®Language.empty‚ü©

/-- The sum of two languages consists of the disjoint union of their symbols. -/
protected def sum (L : Language.{u, v}) (L' : Language.{u', v'}) : Language :=
  ‚ü®fun n => Sum (L.Functions n) (L'.Functions n), fun n => Sum (L.Relations n) (L'.Relations n)‚ü©
#align first_order.language.sum FirstOrder.Language.sum

variable (L : Language.{u, v})

/-- The type of constants in a given language. -/
-- Porting note: The linter does not exist yet, according to Algebra.Hom.GroupAction.
-- @[nolint has_nonempty_instance]
protected def Constants :=
  L.Functions 0
#align first_order.language.constants FirstOrder.Language.Constants

@[simp]
theorem constants_mk‚ÇÇ (c f‚ÇÅ f‚ÇÇ : Type u) (r‚ÇÅ r‚ÇÇ : Type v) :
    (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).Constants = c :=
  rfl
#align first_order.language.constants_mk‚ÇÇ FirstOrder.Language.constants_mk‚ÇÇ

/-- The type of symbols in a given language. -/
-- Porting note: The linter does not exist yet, according to Algebra.Hom.GroupAction.
-- @[nolint has_nonempty_instance]
def Symbols :=
  Sum (Œ£l, L.Functions l) (Œ£l, L.Relations l)
#align first_order.language.symbols FirstOrder.Language.Symbols

/-- The cardinality of a language is the cardinality of its type of symbols. -/
def card : Cardinal :=
  #L.Symbols
#align first_order.language.card FirstOrder.Language.card

/-- A language is relational when it has no function symbols. -/
class IsRelational : Prop where
  /-- There are no function symbols in the language. -/
  empty_functions : ‚àÄ n, IsEmpty (L.Functions n)
#align first_order.language.is_relational FirstOrder.Language.IsRelational

/-- A language is algebraic when it has no relation symbols. -/
class IsAlgebraic : Prop where
  /-- There are no relation symbols in the language. -/
  empty_relations : ‚àÄ n, IsEmpty (L.Relations n)
#align first_order.language.is_algebraic FirstOrder.Language.IsAlgebraic

variable {L} {L' : Language.{u', v'}}

theorem card_eq_card_functions_add_card_relations :
    L.card =
      (Cardinal.sum fun l => Cardinal.lift.{v} #(L.Functions l)) +
        Cardinal.sum fun l => Cardinal.lift.{u} #(L.Relations l) := by
  simp [card, Symbols]
  -- üéâ no goals
#align first_order.language.card_eq_card_functions_add_card_relations FirstOrder.Language.card_eq_card_functions_add_card_relations

instance [L.IsRelational] {n : ‚Ñï} : IsEmpty (L.Functions n) :=
  IsRelational.empty_functions n

instance [L.IsAlgebraic] {n : ‚Ñï} : IsEmpty (L.Relations n) :=
  IsAlgebraic.empty_relations n

instance isRelational_of_empty_functions {symb : ‚Ñï ‚Üí Type*} :
    IsRelational ‚ü®fun _ => Empty, symb‚ü© :=
  ‚ü®fun _ => instIsEmptyEmpty‚ü©
#align first_order.language.is_relational_of_empty_functions FirstOrder.Language.isRelational_of_empty_functions

instance isAlgebraic_of_empty_relations {symb : ‚Ñï ‚Üí Type*} : IsAlgebraic ‚ü®symb, fun _ => Empty‚ü© :=
  ‚ü®fun _ => instIsEmptyEmpty‚ü©
#align first_order.language.is_algebraic_of_empty_relations FirstOrder.Language.isAlgebraic_of_empty_relations

instance isRelational_empty : IsRelational Language.empty :=
  Language.isRelational_of_empty_functions
#align first_order.language.is_relational_empty FirstOrder.Language.isRelational_empty

instance isAlgebraic_empty : IsAlgebraic Language.empty :=
  Language.isAlgebraic_of_empty_relations
#align first_order.language.is_algebraic_empty FirstOrder.Language.isAlgebraic_empty

instance isRelational_sum [L.IsRelational] [L'.IsRelational] : IsRelational (L.sum L') :=
  ‚ü®fun _ => instIsEmptySum‚ü©
#align first_order.language.is_relational_sum FirstOrder.Language.isRelational_sum

instance isAlgebraic_sum [L.IsAlgebraic] [L'.IsAlgebraic] : IsAlgebraic (L.sum L') :=
  ‚ü®fun _ => instIsEmptySum‚ü©
#align first_order.language.is_algebraic_sum FirstOrder.Language.isAlgebraic_sum

instance isRelational_mk‚ÇÇ {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} [h0 : IsEmpty c] [h1 : IsEmpty f‚ÇÅ]
    [h2 : IsEmpty f‚ÇÇ] : IsRelational (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ) :=
  ‚ü®fun n =>
    Nat.casesOn n h0 fun n => Nat.casesOn n h1 fun n => Nat.casesOn n h2 fun _ =>
      inferInstanceAs (IsEmpty PEmpty)‚ü©
#align first_order.language.is_relational_mk‚ÇÇ FirstOrder.Language.isRelational_mk‚ÇÇ

instance isAlgebraic_mk‚ÇÇ {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} [h1 : IsEmpty r‚ÇÅ] [h2 : IsEmpty r‚ÇÇ] :
    IsAlgebraic (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ) :=
  ‚ü®fun n =>
    Nat.casesOn n (inferInstanceAs (IsEmpty PEmpty)) fun n =>
      Nat.casesOn n h1 fun n => Nat.casesOn n h2 fun _ => inferInstanceAs (IsEmpty PEmpty)‚ü©
#align first_order.language.is_algebraic_mk‚ÇÇ FirstOrder.Language.isAlgebraic_mk‚ÇÇ

instance subsingleton_mk‚ÇÇ_functions {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} [h0 : Subsingleton c]
    [h1 : Subsingleton f‚ÇÅ] [h2 : Subsingleton f‚ÇÇ] {n : ‚Ñï} :
    Subsingleton ((Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).Functions n) :=
  Nat.casesOn n h0 fun n =>
    Nat.casesOn n h1 fun n => Nat.casesOn n h2 fun _ => ‚ü®fun x => PEmpty.elim x‚ü©
#align first_order.language.subsingleton_mk‚ÇÇ_functions FirstOrder.Language.subsingleton_mk‚ÇÇ_functions

instance subsingleton_mk‚ÇÇ_relations {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} [h1 : Subsingleton r‚ÇÅ]
    [h2 : Subsingleton r‚ÇÇ] {n : ‚Ñï} : Subsingleton ((Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).Relations n) :=
  Nat.casesOn n ‚ü®fun x => PEmpty.elim x‚ü© fun n =>
    Nat.casesOn n h1 fun n => Nat.casesOn n h2 fun _ => ‚ü®fun x => PEmpty.elim x‚ü©
#align first_order.language.subsingleton_mk‚ÇÇ_relations FirstOrder.Language.subsingleton_mk‚ÇÇ_relations

@[simp]
theorem empty_card : Language.empty.card = 0 := by simp [card_eq_card_functions_add_card_relations]
                                                   -- üéâ no goals
#align first_order.language.empty_card FirstOrder.Language.empty_card

instance isEmpty_empty : IsEmpty Language.empty.Symbols := by
  simp only [Language.Symbols, isEmpty_sum, isEmpty_sigma]
  -- ‚ä¢ (‚àÄ (a : ‚Ñï), IsEmpty (Functions Language.empty a)) ‚àß ‚àÄ (a : ‚Ñï), IsEmpty (Rela ‚Ä¶
  exact ‚ü®fun _ => inferInstance, fun _ => inferInstance‚ü©
  -- üéâ no goals
#align first_order.language.is_empty_empty FirstOrder.Language.isEmpty_empty

instance Countable.countable_functions [h : Countable L.Symbols] : Countable (Œ£l, L.Functions l) :=
  @Function.Injective.countable _ _ h _ Sum.inl_injective
#align first_order.language.countable.countable_functions FirstOrder.Language.Countable.countable_functions

@[simp]
theorem card_functions_sum (i : ‚Ñï) :
    #((L.sum L').Functions i)
      = (Cardinal.lift.{u'} #(L.Functions i) + Cardinal.lift.{u} #(L'.Functions i) : Cardinal) := by
  simp [Language.sum]
  -- üéâ no goals
#align first_order.language.card_functions_sum FirstOrder.Language.card_functions_sum

@[simp]
theorem card_relations_sum (i : ‚Ñï) :
    #((L.sum L').Relations i) =
      Cardinal.lift.{v'} #(L.Relations i) + Cardinal.lift.{v} #(L'.Relations i) := by
  simp [Language.sum]
  -- üéâ no goals
#align first_order.language.card_relations_sum FirstOrder.Language.card_relations_sum

@[simp]
theorem card_sum :
    (L.sum L').card = Cardinal.lift.{max u' v'} L.card + Cardinal.lift.{max u v} L'.card := by
  simp only [card_eq_card_functions_add_card_relations, card_functions_sum, card_relations_sum,
    sum_add_distrib', lift_add, lift_sum, lift_lift]
  simp only [add_assoc, add_comm (Cardinal.sum fun i => (#(L'.Functions i)).lift)]
  -- üéâ no goals
#align first_order.language.card_sum FirstOrder.Language.card_sum

@[simp]
theorem card_mk‚ÇÇ (c f‚ÇÅ f‚ÇÇ : Type u) (r‚ÇÅ r‚ÇÇ : Type v) :
    (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).card =
      Cardinal.lift.{v} #c + Cardinal.lift.{v} #f‚ÇÅ + Cardinal.lift.{v} #f‚ÇÇ +
          Cardinal.lift.{u} #r‚ÇÅ + Cardinal.lift.{u} #r‚ÇÇ := by
  simp [card_eq_card_functions_add_card_relations, add_assoc]
  -- üéâ no goals
#align first_order.language.card_mk‚ÇÇ FirstOrder.Language.card_mk‚ÇÇ

variable (L) (M : Type w)

/-- A first-order structure on a type `M` consists of interpretations of all the symbols in a given
  language. Each function of arity `n` is interpreted as a function sending tuples of length `n`
  (modeled as `(Fin n ‚Üí M)`) to `M`, and a relation of arity `n` is a function from tuples of length
  `n` to `Prop`. -/
@[ext]
class Structure where
  /-- Interpretation of the function symbols -/
  funMap : ‚àÄ {n}, L.Functions n ‚Üí (Fin n ‚Üí M) ‚Üí M
  /-- Interpretation of the relation symbols -/
  RelMap : ‚àÄ {n}, L.Relations n ‚Üí (Fin n ‚Üí M) ‚Üí Prop
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure FirstOrder.Language.Structure
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.fun_map FirstOrder.Language.Structure.funMap
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.rel_map FirstOrder.Language.Structure.RelMap

variable (N : Type w') [L.Structure M] [L.Structure N]

open Structure

/-- Used for defining `FirstOrder.Language.Theory.ModelType.instInhabited`. -/
def Inhabited.trivialStructure {Œ± : Type*} [Inhabited Œ±] : L.Structure Œ± :=
  ‚ü®default, default‚ü©
#align first_order.language.inhabited.trivial_structure FirstOrder.Language.Inhabited.trivialStructure

/-! ### Maps -/


/-- A homomorphism between first-order structures is a function that commutes with the
  interpretations of functions and maps tuples in one structure where a given relation is true to
  tuples in the second structure where that relation is still true. -/
structure Hom where
  /-- The underlying function of a homomorphism of structures -/
  toFun : M ‚Üí N
  /-- The homomorphism commutes with the interpretations of the function symbols -/
  -- Porting note:
  -- The autoparam here used to be `obviously`. We would like to replace it with `aesop`
  -- but that isn't currently sufficient.
  -- See https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Aesop.20and.20cases
  -- If that can be improved, we should change this to `by aesop` and remove the proofs below.
  map_fun' : ‚àÄ {n} (f : L.Functions n) (x), toFun (funMap f x) = funMap f (toFun ‚àò x) := by
    intros; trivial
  /-- The homomorphism sends related elements to related elements -/
  map_rel' : ‚àÄ {n} (r : L.Relations n) (x), RelMap r x ‚Üí RelMap r (toFun ‚àò x) := by
    -- Porting note: see porting note on `Hom.map_fun'`
    intros; trivial
#align first_order.language.hom FirstOrder.Language.Hom

@[inherit_doc]
scoped[FirstOrder] notation:25 A " ‚Üí[" L "] " B => FirstOrder.Language.Hom L A B

/-- An embedding of first-order structures is an embedding that commutes with the
  interpretations of functions and relations. -/
structure Embedding extends M ‚Ü™ N where
  map_fun' : ‚àÄ {n} (f : L.Functions n) (x), toFun (funMap f x) = funMap f (toFun ‚àò x) := by
    -- Porting note: see porting note on `Hom.map_fun'`
    intros; trivial
  map_rel' : ‚àÄ {n} (r : L.Relations n) (x), RelMap r (toFun ‚àò x) ‚Üî RelMap r x := by
    -- Porting note: see porting note on `Hom.map_fun'`
    intros; trivial
#align first_order.language.embedding FirstOrder.Language.Embedding

@[inherit_doc]
scoped[FirstOrder] notation:25 A " ‚Ü™[" L "] " B => FirstOrder.Language.Embedding L A B

/-- An equivalence of first-order structures is an equivalence that commutes with the
  interpretations of functions and relations. -/
structure Equiv extends M ‚âÉ N where
  map_fun' : ‚àÄ {n} (f : L.Functions n) (x), toFun (funMap f x) = funMap f (toFun ‚àò x) := by
    -- Porting note: see porting note on `Hom.map_fun'`
    intros; trivial
  map_rel' : ‚àÄ {n} (r : L.Relations n) (x), RelMap r (toFun ‚àò x) ‚Üî RelMap r x := by
    -- Porting note: see porting note on `Hom.map_fun'`
    intros; trivial
#align first_order.language.equiv FirstOrder.Language.Equiv

@[inherit_doc]
scoped[FirstOrder] notation:25 A " ‚âÉ[" L "] " B => FirstOrder.Language.Equiv L A B

-- Porting note: was [L.Structure P] and [L.Structure Q]
-- The former reported an error.
variable {L M N} {P : Type*} [Structure L P] {Q : Type*} [Structure L Q]

--Porting note: new definition
/-- Interpretation of a constant symbol -/
@[coe]
def constantMap (c : L.Constants) : M := funMap c default

instance : CoeTC L.Constants M :=
  ‚ü®constantMap‚ü©

theorem funMap_eq_coe_constants {c : L.Constants} {x : Fin 0 ‚Üí M} : funMap c x = c :=
  congr rfl (funext finZeroElim)
#align first_order.language.fun_map_eq_coe_constants FirstOrder.Language.funMap_eq_coe_constants

/-- Given a language with a nonempty type of constants, any structure will be nonempty. This cannot
  be a global instance, because `L` becomes a metavariable. -/
theorem nonempty_of_nonempty_constants [h : Nonempty L.Constants] : Nonempty M :=
  h.map (‚Üë)
#align first_order.language.nonempty_of_nonempty_constants FirstOrder.Language.nonempty_of_nonempty_constants

/-- The function map for `FirstOrder.Language.Structure‚ÇÇ`. -/
def funMap‚ÇÇ {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} (c' : c ‚Üí M) (f‚ÇÅ' : f‚ÇÅ ‚Üí M ‚Üí M)
    (f‚ÇÇ' : f‚ÇÇ ‚Üí M ‚Üí M ‚Üí M) : ‚àÄ {n}, (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).Functions n ‚Üí (Fin n ‚Üí M) ‚Üí M
  | 0, f, _ => c' f
  | 1, f, x => f‚ÇÅ' f (x 0)
  | 2, f, x => f‚ÇÇ' f (x 0) (x 1)
  | _ + 3, f, _ => PEmpty.elim f
#align first_order.language.fun_map‚ÇÇ FirstOrder.Language.funMap‚ÇÇ

/-- The relation map for `FirstOrder.Language.Structure‚ÇÇ`. -/
def RelMap‚ÇÇ {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} (r‚ÇÅ' : r‚ÇÅ ‚Üí Set M) (r‚ÇÇ' : r‚ÇÇ ‚Üí M ‚Üí M ‚Üí Prop) :
    ‚àÄ {n}, (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).Relations n ‚Üí (Fin n ‚Üí M) ‚Üí Prop
  | 0, r, _ => PEmpty.elim r
  | 1, r, x => x 0 ‚àà r‚ÇÅ' r
  | 2, r, x => r‚ÇÇ' r (x 0) (x 1)
  | _ + 3, r, _ => PEmpty.elim r
#align first_order.language.rel_map‚ÇÇ FirstOrder.Language.RelMap‚ÇÇ

/-- A structure constructor to match `FirstOrder.Language‚ÇÇ`. -/
protected def Structure.mk‚ÇÇ {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v} (c' : c ‚Üí M) (f‚ÇÅ' : f‚ÇÅ ‚Üí M ‚Üí M)
    (f‚ÇÇ' : f‚ÇÇ ‚Üí M ‚Üí M ‚Üí M) (r‚ÇÅ' : r‚ÇÅ ‚Üí Set M) (r‚ÇÇ' : r‚ÇÇ ‚Üí M ‚Üí M ‚Üí Prop) :
    (Language.mk‚ÇÇ c f‚ÇÅ f‚ÇÇ r‚ÇÅ r‚ÇÇ).Structure M :=
  ‚ü®funMap‚ÇÇ c' f‚ÇÅ' f‚ÇÇ', RelMap‚ÇÇ r‚ÇÅ' r‚ÇÇ'‚ü©
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.mk‚ÇÇ FirstOrder.Language.Structure.mk‚ÇÇ

namespace Structure

variable {c f‚ÇÅ f‚ÇÇ : Type u} {r‚ÇÅ r‚ÇÇ : Type v}

variable {c' : c ‚Üí M} {f‚ÇÅ' : f‚ÇÅ ‚Üí M ‚Üí M} {f‚ÇÇ' : f‚ÇÇ ‚Üí M ‚Üí M ‚Üí M}

variable {r‚ÇÅ' : r‚ÇÅ ‚Üí Set M} {r‚ÇÇ' : r‚ÇÇ ‚Üí M ‚Üí M ‚Üí Prop}

@[simp]
theorem funMap_apply‚ÇÄ (c‚ÇÄ : c) {x : Fin 0 ‚Üí M} :
    @Structure.funMap _ M (Structure.mk‚ÇÇ c' f‚ÇÅ' f‚ÇÇ' r‚ÇÅ' r‚ÇÇ') 0 c‚ÇÄ x = c' c‚ÇÄ :=
  rfl
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.fun_map_apply‚ÇÄ FirstOrder.Language.Structure.funMap_apply‚ÇÄ

@[simp]
theorem funMap_apply‚ÇÅ (f : f‚ÇÅ) (x : M) :
    @Structure.funMap _ M (Structure.mk‚ÇÇ c' f‚ÇÅ' f‚ÇÇ' r‚ÇÅ' r‚ÇÇ') 1 f ![x] = f‚ÇÅ' f x :=
  rfl
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.fun_map_apply‚ÇÅ FirstOrder.Language.Structure.funMap_apply‚ÇÅ

@[simp]
theorem funMap_apply‚ÇÇ (f : f‚ÇÇ) (x y : M) :
    @Structure.funMap _ M (Structure.mk‚ÇÇ c' f‚ÇÅ' f‚ÇÇ' r‚ÇÅ' r‚ÇÇ') 2 f ![x, y] = f‚ÇÇ' f x y :=
  rfl
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.fun_map_apply‚ÇÇ FirstOrder.Language.Structure.funMap_apply‚ÇÇ

@[simp]
theorem relMap_apply‚ÇÅ (r : r‚ÇÅ) (x : M) :
    @Structure.RelMap _ M (Structure.mk‚ÇÇ c' f‚ÇÅ' f‚ÇÇ' r‚ÇÅ' r‚ÇÇ') 1 r ![x] = (x ‚àà r‚ÇÅ' r) :=
  rfl
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.rel_map_apply‚ÇÅ FirstOrder.Language.Structure.relMap_apply‚ÇÅ

@[simp]
theorem relMap_apply‚ÇÇ (r : r‚ÇÇ) (x y : M) :
    @Structure.RelMap _ M (Structure.mk‚ÇÇ c' f‚ÇÅ' f‚ÇÇ' r‚ÇÅ' r‚ÇÇ') 2 r ![x, y] = r‚ÇÇ' r x y :=
  rfl
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.rel_map_apply‚ÇÇ FirstOrder.Language.Structure.relMap_apply‚ÇÇ

end Structure

/-- `HomClass L F M N` states that `F` is a type of `L`-homomorphisms. You should extend this
  typeclass when you extend `FirstOrder.Language.Hom`. -/
class HomClass (L : outParam Language) (F : Type*) (M N : outParam <| Type*)
  [FunLike F M fun _ => N] [L.Structure M] [L.Structure N] : Prop where
  map_fun : ‚àÄ (œÜ : F) {n} (f : L.Functions n) (x), œÜ (funMap f x) = funMap f (œÜ ‚àò x)
  map_rel : ‚àÄ (œÜ : F) {n} (r : L.Relations n) (x), RelMap r x ‚Üí RelMap r (œÜ ‚àò x)
#align first_order.language.hom_class FirstOrder.Language.HomClass

/-- `StrongHomClass L F M N` states that `F` is a type of `L`-homomorphisms which preserve
  relations in both directions. -/
class StrongHomClass (L : outParam Language) (F : Type*) (M N : outParam <| Type*)
  [FunLike F M fun _ => N] [L.Structure M] [L.Structure N] : Prop where
  map_fun : ‚àÄ (œÜ : F) {n} (f : L.Functions n) (x), œÜ (funMap f x) = funMap f (œÜ ‚àò x)
  map_rel : ‚àÄ (œÜ : F) {n} (r : L.Relations n) (x), RelMap r (œÜ ‚àò x) ‚Üî RelMap r x
#align first_order.language.strong_hom_class FirstOrder.Language.StrongHomClass

--Porting note: using implicit brackets for `Structure` arguments
instance (priority := 100) StrongHomClass.homClass [L.Structure M]
    [L.Structure N] [FunLike F M fun _ => N] [StrongHomClass L F M N] : HomClass L F M N where
  map_fun := StrongHomClass.map_fun
  map_rel œÜ _ R x := (StrongHomClass.map_rel œÜ R x).2
#align first_order.language.strong_hom_class.hom_class FirstOrder.Language.StrongHomClass.homClass

/-- Not an instance to avoid a loop. -/
theorem HomClass.strongHomClassOfIsAlgebraic [L.IsAlgebraic] {F M N} [L.Structure M] [L.Structure N]
    [FunLike F M fun _ => N] [HomClass L F M N] : StrongHomClass L F M N where
  map_fun := HomClass.map_fun
  map_rel _ n R _ := (IsAlgebraic.empty_relations n).elim R
#align first_order.language.hom_class.strong_hom_class_of_is_algebraic FirstOrder.Language.HomClass.strongHomClassOfIsAlgebraic

theorem HomClass.map_constants {F M N} [L.Structure M] [L.Structure N] [FunLike F M fun _ => N]
    [HomClass L F M N] (œÜ : F) (c : L.Constants) : œÜ c = c :=
  (HomClass.map_fun œÜ c default).trans (congr rfl (funext default))
#align first_order.language.hom_class.map_constants FirstOrder.Language.HomClass.map_constants

attribute [inherit_doc FirstOrder.Language.Hom.map_fun'] FirstOrder.Language.Embedding.map_fun'
  FirstOrder.Language.HomClass.map_fun FirstOrder.Language.StrongHomClass.map_fun
  FirstOrder.Language.Equiv.map_fun'

attribute [inherit_doc FirstOrder.Language.Hom.map_rel'] FirstOrder.Language.Embedding.map_rel'
  FirstOrder.Language.HomClass.map_rel FirstOrder.Language.StrongHomClass.map_rel
  FirstOrder.Language.Equiv.map_rel'

namespace Hom

instance funLike : FunLike (M ‚Üí[L] N) M fun _ => N where
  coe := Hom.toFun
  coe_injective' f g h := by cases f; cases g; cases h; rfl
                             -- ‚ä¢ mk toFun‚úù = g
                                      -- ‚ä¢ mk toFun‚úù¬π = mk toFun‚úù
                                               -- ‚ä¢ mk toFun‚úù = mk toFun‚úù
                                                        -- üéâ no goals
#align first_order.language.hom.fun_like FirstOrder.Language.Hom.funLike

instance homClass : HomClass L (M ‚Üí[L] N) M N where
  map_fun := map_fun'
  map_rel := map_rel'
#align first_order.language.hom.hom_class FirstOrder.Language.Hom.homClass

instance [L.IsAlgebraic] : StrongHomClass L (M ‚Üí[L] N) M N :=
  HomClass.strongHomClassOfIsAlgebraic

instance hasCoeToFun : CoeFun (M ‚Üí[L] N) fun _ => M ‚Üí N :=
  FunLike.hasCoeToFun
#align first_order.language.hom.has_coe_to_fun FirstOrder.Language.Hom.hasCoeToFun

@[simp]
theorem toFun_eq_coe {f : M ‚Üí[L] N} : f.toFun = (f : M ‚Üí N) :=
  rfl
#align first_order.language.hom.to_fun_eq_coe FirstOrder.Language.Hom.toFun_eq_coe

@[ext]
theorem ext ‚¶Éf g : M ‚Üí[L] N‚¶Ñ (h : ‚àÄ x, f x = g x) : f = g :=
  FunLike.ext f g h
#align first_order.language.hom.ext FirstOrder.Language.Hom.ext

theorem ext_iff {f g : M ‚Üí[L] N} : f = g ‚Üî ‚àÄ x, f x = g x :=
  FunLike.ext_iff
#align first_order.language.hom.ext_iff FirstOrder.Language.Hom.ext_iff

@[simp]
theorem map_fun (œÜ : M ‚Üí[L] N) {n : ‚Ñï} (f : L.Functions n) (x : Fin n ‚Üí M) :
    œÜ (funMap f x) = funMap f (œÜ ‚àò x) :=
  HomClass.map_fun œÜ f x
#align first_order.language.hom.map_fun FirstOrder.Language.Hom.map_fun

@[simp]
theorem map_constants (œÜ : M ‚Üí[L] N) (c : L.Constants) : œÜ c = c :=
  HomClass.map_constants œÜ c
#align first_order.language.hom.map_constants FirstOrder.Language.Hom.map_constants

@[simp]
theorem map_rel (œÜ : M ‚Üí[L] N) {n : ‚Ñï} (r : L.Relations n) (x : Fin n ‚Üí M) :
    RelMap r x ‚Üí RelMap r (œÜ ‚àò x) :=
  HomClass.map_rel œÜ r x
#align first_order.language.hom.map_rel FirstOrder.Language.Hom.map_rel

variable (L) (M)

/-- The identity map from a structure to itself. -/
@[refl]
def id : M ‚Üí[L] M where
  toFun m := m
#align first_order.language.hom.id FirstOrder.Language.Hom.id

variable {L} {M}

instance : Inhabited (M ‚Üí[L] M) :=
  ‚ü®id L M‚ü©

@[simp]
theorem id_apply (x : M) : id L M x = x :=
  rfl
#align first_order.language.hom.id_apply FirstOrder.Language.Hom.id_apply

/-- Composition of first-order homomorphisms. -/
@[trans]
def comp (hnp : N ‚Üí[L] P) (hmn : M ‚Üí[L] N) : M ‚Üí[L] P where
  toFun := hnp ‚àò hmn
  -- Porting note: should be done by autoparam?
  map_fun' _ _ := by simp; rfl
                     -- ‚ä¢ funMap x‚úù¬π (‚Üëhnp ‚àò ‚Üëhmn ‚àò x‚úù) = funMap x‚úù¬π ((‚Üëhnp ‚àò ‚Üëhmn) ‚àò x‚úù)
                           -- üéâ no goals
  -- Porting note: should be done by autoparam?
  map_rel' _ _ h := map_rel _ _ _ (map_rel _ _ _ h)
#align first_order.language.hom.comp FirstOrder.Language.Hom.comp

@[simp]
theorem comp_apply (g : N ‚Üí[L] P) (f : M ‚Üí[L] N) (x : M) : g.comp f x = g (f x) :=
  rfl
#align first_order.language.hom.comp_apply FirstOrder.Language.Hom.comp_apply

/-- Composition of first-order homomorphisms is associative. -/
theorem comp_assoc (f : M ‚Üí[L] N) (g : N ‚Üí[L] P) (h : P ‚Üí[L] Q) :
    (h.comp g).comp f = h.comp (g.comp f) :=
  rfl
#align first_order.language.hom.comp_assoc FirstOrder.Language.Hom.comp_assoc

end Hom

/-- Any element of a `HomClass` can be realized as a first_order homomorphism. -/
def HomClass.toHom {F M N} [L.Structure M] [L.Structure N] [FunLike F M fun _ => N]
    [HomClass L F M N] : F ‚Üí M ‚Üí[L] N := fun œÜ =>
  ‚ü®œÜ, HomClass.map_fun œÜ, HomClass.map_rel œÜ‚ü©
#align first_order.language.hom_class.to_hom FirstOrder.Language.HomClass.toHom

namespace Embedding

instance embeddingLike : EmbeddingLike (M ‚Ü™[L] N) M N where
  coe f := f.toFun
  injective' f := f.toEmbedding.injective
  coe_injective' f g h := by
    cases f
    -- ‚ä¢ mk toEmbedding‚úù = g
    cases g
    -- ‚ä¢ mk toEmbedding‚úù¬π = mk toEmbedding‚úù
    congr
    -- ‚ä¢ toEmbedding‚úù¬π = toEmbedding‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoEmbedding‚úù¬π x = ‚ÜëtoEmbedding‚úù x
    exact Function.funext_iff.1 h x
    -- üéâ no goals
#align first_order.language.embedding.embedding_like FirstOrder.Language.Embedding.embeddingLike

instance strongHomClass : StrongHomClass L (M ‚Ü™[L] N) M N where
  map_fun := map_fun'
  map_rel := map_rel'
#align first_order.language.embedding.strong_hom_class FirstOrder.Language.Embedding.strongHomClass

instance hasCoeToFun : CoeFun (M ‚Ü™[L] N) fun _ => M ‚Üí N :=
  FunLike.hasCoeToFun
#align first_order.language.embedding.has_coe_to_fun FirstOrder.Language.Embedding.hasCoeToFun

@[simp]
theorem map_fun (œÜ : M ‚Ü™[L] N) {n : ‚Ñï} (f : L.Functions n) (x : Fin n ‚Üí M) :
    œÜ (funMap f x) = funMap f (œÜ ‚àò x) :=
  HomClass.map_fun œÜ f x
#align first_order.language.embedding.map_fun FirstOrder.Language.Embedding.map_fun

@[simp]
theorem map_constants (œÜ : M ‚Ü™[L] N) (c : L.Constants) : œÜ c = c :=
  HomClass.map_constants œÜ c
#align first_order.language.embedding.map_constants FirstOrder.Language.Embedding.map_constants

@[simp]
theorem map_rel (œÜ : M ‚Ü™[L] N) {n : ‚Ñï} (r : L.Relations n) (x : Fin n ‚Üí M) :
    RelMap r (œÜ ‚àò x) ‚Üî RelMap r x :=
  StrongHomClass.map_rel œÜ r x
#align first_order.language.embedding.map_rel FirstOrder.Language.Embedding.map_rel

/-- A first-order embedding is also a first-order homomorphism. -/
def toHom : (M ‚Ü™[L] N) ‚Üí M ‚Üí[L] N :=
  HomClass.toHom
#align first_order.language.embedding.to_hom FirstOrder.Language.Embedding.toHom

@[simp]
theorem coe_toHom {f : M ‚Ü™[L] N} : (f.toHom : M ‚Üí N) = f :=
  rfl
#align first_order.language.embedding.coe_to_hom FirstOrder.Language.Embedding.coe_toHom

theorem coe_injective : @Function.Injective (M ‚Ü™[L] N) (M ‚Üí N) (‚Üë)
  | f, g, h => by
    cases f
    -- ‚ä¢ mk toEmbedding‚úù = g
    cases g
    -- ‚ä¢ mk toEmbedding‚úù¬π = mk toEmbedding‚úù
    congr
    -- ‚ä¢ toEmbedding‚úù¬π = toEmbedding‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoEmbedding‚úù¬π x = ‚ÜëtoEmbedding‚úù x
    exact Function.funext_iff.1 h x
    -- üéâ no goals
#align first_order.language.embedding.coe_injective FirstOrder.Language.Embedding.coe_injective

@[ext]
theorem ext ‚¶Éf g : M ‚Ü™[L] N‚¶Ñ (h : ‚àÄ x, f x = g x) : f = g :=
  coe_injective (funext h)
#align first_order.language.embedding.ext FirstOrder.Language.Embedding.ext

theorem ext_iff {f g : M ‚Ü™[L] N} : f = g ‚Üî ‚àÄ x, f x = g x :=
  ‚ü®fun h _ => h ‚ñ∏ rfl, fun h => ext h‚ü©
#align first_order.language.embedding.ext_iff FirstOrder.Language.Embedding.ext_iff

theorem injective (f : M ‚Ü™[L] N) : Function.Injective f :=
  f.toEmbedding.injective
#align first_order.language.embedding.injective FirstOrder.Language.Embedding.injective

/-- In an algebraic language, any injective homomorphism is an embedding. -/
@[simps!]
def ofInjective [L.IsAlgebraic] {f : M ‚Üí[L] N} (hf : Function.Injective f) : M ‚Ü™[L] N :=
  { f with
    inj' := hf
    map_rel' := fun {_} r x => StrongHomClass.map_rel f r x }
#align first_order.language.embedding.of_injective FirstOrder.Language.Embedding.ofInjective

@[simp]
theorem coeFn_ofInjective [L.IsAlgebraic] {f : M ‚Üí[L] N} (hf : Function.Injective f) :
    (ofInjective hf : M ‚Üí N) = f :=
  rfl
#align first_order.language.embedding.coe_fn_of_injective FirstOrder.Language.Embedding.coeFn_ofInjective

@[simp]
theorem ofInjective_toHom [L.IsAlgebraic] {f : M ‚Üí[L] N} (hf : Function.Injective f) :
    (ofInjective hf).toHom = f := by
  ext; simp
  -- ‚ä¢ ‚Üë(toHom (ofInjective hf)) x‚úù = ‚Üëf x‚úù
       -- üéâ no goals
#align first_order.language.embedding.of_injective_to_hom FirstOrder.Language.Embedding.ofInjective_toHom

variable (L) (M)

/-- The identity embedding from a structure to itself. -/
@[refl]
def refl : M ‚Ü™[L] M where toEmbedding := Function.Embedding.refl M
#align first_order.language.embedding.refl FirstOrder.Language.Embedding.refl

variable {L} {M}

instance : Inhabited (M ‚Ü™[L] M) :=
  ‚ü®refl L M‚ü©

@[simp]
theorem refl_apply (x : M) : refl L M x = x :=
  rfl
#align first_order.language.embedding.refl_apply FirstOrder.Language.Embedding.refl_apply

/-- Composition of first-order embeddings. -/
@[trans]
def comp (hnp : N ‚Ü™[L] P) (hmn : M ‚Ü™[L] N) : M ‚Ü™[L] P where
  toFun := hnp ‚àò hmn
  inj' := hnp.injective.comp hmn.injective
  -- Porting note: should be done by autoparam?
  map_fun' := by intros; simp only [Function.comp_apply, map_fun]; trivial
                 -- ‚ä¢ Function.Embedding.toFun { toFun := ‚Üëhnp ‚àò ‚Üëhmn, inj' := (_ : Function.Injec ‚Ä¶
                         -- ‚ä¢ funMap f‚úù (‚Üëhnp ‚àò ‚Üëhmn ‚àò x‚úù) = funMap f‚úù ((‚Üëhnp ‚àò ‚Üëhmn) ‚àò x‚úù)
                                                                   -- üéâ no goals
  -- Porting note: should be done by autoparam?
  map_rel' := by intros; rw [Function.comp.assoc, map_rel, map_rel]
                 -- ‚ä¢ RelMap r‚úù ({ toFun := ‚Üëhnp ‚àò ‚Üëhmn, inj' := (_ : Function.Injective (‚Üëhnp ‚àò ‚Üë ‚Ä¶
                         -- üéâ no goals
#align first_order.language.embedding.comp FirstOrder.Language.Embedding.comp

@[simp]
theorem comp_apply (g : N ‚Ü™[L] P) (f : M ‚Ü™[L] N) (x : M) : g.comp f x = g (f x) :=
  rfl
#align first_order.language.embedding.comp_apply FirstOrder.Language.Embedding.comp_apply

/-- Composition of first-order embeddings is associative. -/
theorem comp_assoc (f : M ‚Ü™[L] N) (g : N ‚Ü™[L] P) (h : P ‚Ü™[L] Q) :
    (h.comp g).comp f = h.comp (g.comp f) :=
  rfl
#align first_order.language.embedding.comp_assoc FirstOrder.Language.Embedding.comp_assoc

@[simp]
theorem comp_toHom (hnp : N ‚Ü™[L] P) (hmn : M ‚Ü™[L] N) :
    (hnp.comp hmn).toHom = hnp.toHom.comp hmn.toHom := by
  ext
  -- ‚ä¢ ‚Üë(toHom (comp hnp hmn)) x‚úù = ‚Üë(Hom.comp (toHom hnp) (toHom hmn)) x‚úù
  simp only [coe_toHom, comp_apply, Hom.comp_apply]
  -- üéâ no goals
#align first_order.language.embedding.comp_to_hom FirstOrder.Language.Embedding.comp_toHom

end Embedding

/-- Any element of an injective `StrongHomClass` can be realized as a first_order embedding. -/
def StrongHomClass.toEmbedding {F M N} [L.Structure M] [L.Structure N] [EmbeddingLike F M N]
    [StrongHomClass L F M N] : F ‚Üí M ‚Ü™[L] N := fun œÜ =>
  ‚ü®‚ü®œÜ, EmbeddingLike.injective œÜ‚ü©, StrongHomClass.map_fun œÜ, StrongHomClass.map_rel œÜ‚ü©
#align first_order.language.strong_hom_class.to_embedding FirstOrder.Language.StrongHomClass.toEmbedding

namespace Equiv

instance : EquivLike (M ‚âÉ[L] N) M N where
  coe f := f.toFun
  inv f := f.invFun
  left_inv f := f.left_inv
  right_inv f := f.right_inv
  coe_injective' f g h‚ÇÅ h‚ÇÇ := by
    cases f
    -- ‚ä¢ mk toEquiv‚úù = g
    cases g
    -- ‚ä¢ mk toEquiv‚úù¬π = mk toEquiv‚úù
    simp only [mk.injEq]
    -- ‚ä¢ toEquiv‚úù¬π = toEquiv‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoEquiv‚úù¬π x = ‚ÜëtoEquiv‚úù x
    exact Function.funext_iff.1 h‚ÇÅ x
    -- üéâ no goals

instance : StrongHomClass L (M ‚âÉ[L] N) M N where
  map_fun := map_fun'
  map_rel := map_rel'

/-- The inverse of a first-order equivalence is a first-order equivalence. -/
@[symm]
def symm (f : M ‚âÉ[L] N) : N ‚âÉ[L] M :=
  { f.toEquiv.symm with
    map_fun' := fun n f' {x} => by
      simp only [Equiv.toFun_as_coe]
      -- ‚ä¢ ‚Üëf.symm (funMap f' x) = funMap f' (‚Üëf.symm ‚àò x)
      rw [Equiv.symm_apply_eq]
      -- ‚ä¢ funMap f' x = ‚Üëf.toEquiv (funMap f' (‚Üëf.symm ‚àò x))
      refine' Eq.trans _ (f.map_fun' f' (f.toEquiv.symm ‚àò x)).symm
      -- ‚ä¢ funMap f' x = funMap f' (f.toFun ‚àò ‚Üëf.symm ‚àò x)
      rw [‚Üê Function.comp.assoc, Equiv.toFun_as_coe, Equiv.self_comp_symm, Function.comp.left_id]
      -- üéâ no goals
    map_rel' := fun n r {x} => by
      simp only [Equiv.toFun_as_coe]
      -- ‚ä¢ RelMap r (‚Üëf.symm ‚àò x) ‚Üî RelMap r x
      refine' (f.map_rel' r (f.toEquiv.symm ‚àò x)).symm.trans _
      -- ‚ä¢ RelMap r (f.toFun ‚àò ‚Üëf.symm ‚àò x) ‚Üî RelMap r x
      rw [‚Üê Function.comp.assoc, Equiv.toFun_as_coe, Equiv.self_comp_symm, Function.comp.left_id] }
      -- üéâ no goals
#align first_order.language.equiv.symm FirstOrder.Language.Equiv.symm

instance hasCoeToFun : CoeFun (M ‚âÉ[L] N) fun _ => M ‚Üí N :=
  FunLike.hasCoeToFun
#align first_order.language.equiv.has_coe_to_fun FirstOrder.Language.Equiv.hasCoeToFun

@[simp]
theorem apply_symm_apply (f : M ‚âÉ[L] N) (a : N) : f (f.symm a) = a :=
  f.toEquiv.apply_symm_apply a
#align first_order.language.equiv.apply_symm_apply FirstOrder.Language.Equiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (f : M ‚âÉ[L] N) (a : M) : f.symm (f a) = a :=
  f.toEquiv.symm_apply_apply a
#align first_order.language.equiv.symm_apply_apply FirstOrder.Language.Equiv.symm_apply_apply

@[simp]
theorem map_fun (œÜ : M ‚âÉ[L] N) {n : ‚Ñï} (f : L.Functions n) (x : Fin n ‚Üí M) :
    œÜ (funMap f x) = funMap f (œÜ ‚àò x) :=
  HomClass.map_fun œÜ f x
#align first_order.language.equiv.map_fun FirstOrder.Language.Equiv.map_fun

@[simp]
theorem map_constants (œÜ : M ‚âÉ[L] N) (c : L.Constants) : œÜ c = c :=
  HomClass.map_constants œÜ c
#align first_order.language.equiv.map_constants FirstOrder.Language.Equiv.map_constants

@[simp]
theorem map_rel (œÜ : M ‚âÉ[L] N) {n : ‚Ñï} (r : L.Relations n) (x : Fin n ‚Üí M) :
    RelMap r (œÜ ‚àò x) ‚Üî RelMap r x :=
  StrongHomClass.map_rel œÜ r x
#align first_order.language.equiv.map_rel FirstOrder.Language.Equiv.map_rel

/-- A first-order equivalence is also a first-order embedding. -/
def toEmbedding : (M ‚âÉ[L] N) ‚Üí M ‚Ü™[L] N :=
  StrongHomClass.toEmbedding
#align first_order.language.equiv.to_embedding FirstOrder.Language.Equiv.toEmbedding

/-- A first-order equivalence is also a first-order homomorphism. -/
def toHom : (M ‚âÉ[L] N) ‚Üí M ‚Üí[L] N :=
  HomClass.toHom
#align first_order.language.equiv.to_hom FirstOrder.Language.Equiv.toHom

@[simp]
theorem toEmbedding_toHom (f : M ‚âÉ[L] N) : f.toEmbedding.toHom = f.toHom :=
  rfl
#align first_order.language.equiv.to_embedding_to_hom FirstOrder.Language.Equiv.toEmbedding_toHom

@[simp]
theorem coe_toHom {f : M ‚âÉ[L] N} : (f.toHom : M ‚Üí N) = (f : M ‚Üí N) :=
  rfl
#align first_order.language.equiv.coe_to_hom FirstOrder.Language.Equiv.coe_toHom

@[simp]
theorem coe_toEmbedding (f : M ‚âÉ[L] N) : (f.toEmbedding : M ‚Üí N) = (f : M ‚Üí N) :=
  rfl
#align first_order.language.equiv.coe_to_embedding FirstOrder.Language.Equiv.coe_toEmbedding

theorem coe_injective : @Function.Injective (M ‚âÉ[L] N) (M ‚Üí N) (‚Üë) :=
  FunLike.coe_injective
#align first_order.language.equiv.coe_injective FirstOrder.Language.Equiv.coe_injective

@[ext]
theorem ext ‚¶Éf g : M ‚âÉ[L] N‚¶Ñ (h : ‚àÄ x, f x = g x) : f = g :=
  coe_injective (funext h)
#align first_order.language.equiv.ext FirstOrder.Language.Equiv.ext

theorem ext_iff {f g : M ‚âÉ[L] N} : f = g ‚Üî ‚àÄ x, f x = g x :=
  ‚ü®fun h _ => h ‚ñ∏ rfl, fun h => ext h‚ü©
#align first_order.language.equiv.ext_iff FirstOrder.Language.Equiv.ext_iff

theorem bijective (f : M ‚âÉ[L] N) : Function.Bijective f :=
  EquivLike.bijective f
#align first_order.language.equiv.bijective FirstOrder.Language.Equiv.bijective

theorem injective (f : M ‚âÉ[L] N) : Function.Injective f :=
  EquivLike.injective f
#align first_order.language.equiv.injective FirstOrder.Language.Equiv.injective

theorem surjective (f : M ‚âÉ[L] N) : Function.Surjective f :=
  EquivLike.surjective f
#align first_order.language.equiv.surjective FirstOrder.Language.Equiv.surjective

variable (L) (M)

/-- The identity equivalence from a structure to itself. -/
@[refl]
def refl : M ‚âÉ[L] M where toEquiv := _root_.Equiv.refl M
#align first_order.language.equiv.refl FirstOrder.Language.Equiv.refl

variable {L} {M}

instance : Inhabited (M ‚âÉ[L] M) :=
  ‚ü®refl L M‚ü©

@[simp]
theorem refl_apply (x : M) : refl L M x = x := by simp [refl]; rfl
                                                  -- ‚ä¢ ‚Üë(mk (_root_.Equiv.refl M)) x = x
                                                               -- üéâ no goals
#align first_order.language.equiv.refl_apply FirstOrder.Language.Equiv.refl_apply

/-- Composition of first-order equivalences. -/
@[trans]
def comp (hnp : N ‚âÉ[L] P) (hmn : M ‚âÉ[L] N) : M ‚âÉ[L] P :=
  { hmn.toEquiv.trans hnp.toEquiv with
    toFun := hnp ‚àò hmn
    -- Porting note: should be done by autoparam?
    map_fun' := by intros; simp only [Function.comp_apply, map_fun]; trivial
                   -- ‚ä¢ Equiv.toFun { toFun := ‚Üëhnp ‚àò ‚Üëhmn, invFun := src‚úù.invFun, left_inv := (_ :  ‚Ä¶
                           -- ‚ä¢ funMap f‚úù (‚Üëhnp ‚àò ‚Üëhmn ‚àò x‚úù) = funMap f‚úù ((‚Üëhnp ‚àò ‚Üëhmn) ‚àò x‚úù)
                                                                     -- üéâ no goals
    -- Porting note: should be done by autoparam?
    map_rel' := by intros; rw [Function.comp.assoc, map_rel, map_rel] }
                   -- ‚ä¢ RelMap r‚úù ({ toFun := ‚Üëhnp ‚àò ‚Üëhmn, invFun := src‚úù.invFun, left_inv := (_ : F ‚Ä¶
                           -- üéâ no goals
#align first_order.language.equiv.comp FirstOrder.Language.Equiv.comp

@[simp]
theorem comp_apply (g : N ‚âÉ[L] P) (f : M ‚âÉ[L] N) (x : M) : g.comp f x = g (f x) :=
  rfl
#align first_order.language.equiv.comp_apply FirstOrder.Language.Equiv.comp_apply

/-- Composition of first-order homomorphisms is associative. -/
theorem comp_assoc (f : M ‚âÉ[L] N) (g : N ‚âÉ[L] P) (h : P ‚âÉ[L] Q) :
    (h.comp g).comp f = h.comp (g.comp f) :=
  rfl
#align first_order.language.equiv.comp_assoc FirstOrder.Language.Equiv.comp_assoc

end Equiv

/-- Any element of a bijective `StrongHomClass` can be realized as a first_order isomorphism. -/
def StrongHomClass.toEquiv {F M N} [L.Structure M] [L.Structure N] [EquivLike F M N]
    [StrongHomClass L F M N] : F ‚Üí M ‚âÉ[L] N := fun œÜ =>
  ‚ü®‚ü®œÜ, EquivLike.inv œÜ, EquivLike.left_inv œÜ, EquivLike.right_inv œÜ‚ü©, StrongHomClass.map_fun œÜ,
    StrongHomClass.map_rel œÜ‚ü©
#align first_order.language.strong_hom_class.to_equiv FirstOrder.Language.StrongHomClass.toEquiv

section SumStructure

variable (L‚ÇÅ L‚ÇÇ : Language) (S : Type*) [L‚ÇÅ.Structure S] [L‚ÇÇ.Structure S]

instance sumStructure : (L‚ÇÅ.sum L‚ÇÇ).Structure S where
  funMap := Sum.elim funMap funMap
  RelMap := Sum.elim RelMap RelMap
set_option linter.uppercaseLean3 false in
#align first_order.language.sum_Structure FirstOrder.Language.sumStructure

variable {L‚ÇÅ L‚ÇÇ S}

@[simp]
theorem funMap_sum_inl {n : ‚Ñï} (f : L‚ÇÅ.Functions n) :
    @funMap (L‚ÇÅ.sum L‚ÇÇ) S _ n (Sum.inl f) = funMap f :=
  rfl
#align first_order.language.fun_map_sum_inl FirstOrder.Language.funMap_sum_inl

@[simp]
theorem funMap_sum_inr {n : ‚Ñï} (f : L‚ÇÇ.Functions n) :
    @funMap (L‚ÇÅ.sum L‚ÇÇ) S _ n (Sum.inr f) = funMap f :=
  rfl
#align first_order.language.fun_map_sum_inr FirstOrder.Language.funMap_sum_inr

@[simp]
theorem relMap_sum_inl {n : ‚Ñï} (R : L‚ÇÅ.Relations n) :
    @RelMap (L‚ÇÅ.sum L‚ÇÇ) S _ n (Sum.inl R) = RelMap R :=
  rfl
#align first_order.language.rel_map_sum_inl FirstOrder.Language.relMap_sum_inl

@[simp]
theorem relMap_sum_inr {n : ‚Ñï} (R : L‚ÇÇ.Relations n) :
    @RelMap (L‚ÇÅ.sum L‚ÇÇ) S _ n (Sum.inr R) = RelMap R :=
  rfl
#align first_order.language.rel_map_sum_inr FirstOrder.Language.relMap_sum_inr

end SumStructure

section Empty

section

variable [Language.empty.Structure M] [Language.empty.Structure N]

@[simp]
theorem empty.nonempty_embedding_iff :
    Nonempty (M ‚Ü™[Language.empty] N) ‚Üî Cardinal.lift.{w'} #M ‚â§ Cardinal.lift.{w} #N :=
  _root_.trans ‚ü®Nonempty.map fun f => f.toEmbedding, Nonempty.map fun f => { toEmbedding := f }‚ü©
    Cardinal.lift_mk_le'.symm
#align first_order.language.empty.nonempty_embedding_iff FirstOrder.Language.empty.nonempty_embedding_iff

@[simp]
theorem empty.nonempty_equiv_iff :
    Nonempty (M ‚âÉ[Language.empty] N) ‚Üî Cardinal.lift.{w'} #M = Cardinal.lift.{w} #N :=
  _root_.trans ‚ü®Nonempty.map fun f => f.toEquiv, Nonempty.map fun f => { toEquiv := f }‚ü©
    Cardinal.lift_mk_eq'.symm
#align first_order.language.empty.nonempty_equiv_iff FirstOrder.Language.empty.nonempty_equiv_iff

end

instance emptyStructure : Language.empty.Structure M :=
  ‚ü®Empty.elim, Empty.elim‚ü©
set_option linter.uppercaseLean3 false in
#align first_order.language.empty_Structure FirstOrder.Language.emptyStructure

instance : Unique (Language.empty.Structure M) :=
  ‚ü®‚ü®Language.emptyStructure‚ü©, fun a => by
    ext _ f <;> exact Empty.elim f‚ü©
    -- ‚ä¢ funMap f x‚úù = funMap f x‚úù
                -- üéâ no goals
                -- üéâ no goals

instance (priority := 100) strongHomClassEmpty {F M N} [FunLike F M fun _ => N] :
    StrongHomClass Language.empty F M N :=
  ‚ü®fun _ _ f => Empty.elim f, fun _ _ r => Empty.elim r‚ü©
#align first_order.language.strong_hom_class_empty FirstOrder.Language.strongHomClassEmpty

/-- Makes a `Language.empty.Hom` out of any function. -/
@[simps]
def _root_.Function.emptyHom (f : M ‚Üí N) : M ‚Üí[Language.empty] N where toFun := f
#align function.empty_hom Function.emptyHom

/-- Makes a `Language.empty.Embedding` out of any function. -/
--@[simps] Porting note: commented out and lemmas added manually
def _root_.Embedding.empty (f : M ‚Ü™ N) : M ‚Ü™[Language.empty] N where toEmbedding := f
#align embedding.empty Embedding.empty

@[simp]
theorem toFun_embedding_empty (f : M ‚Ü™ N) : (Embedding.empty f : M ‚Üí N) = f :=
  rfl

@[simp]
theorem toEmbedding_embedding_empty (f : M ‚Ü™ N) : (Embedding.empty f).toEmbedding = f :=
  rfl

/-- Makes a `Language.empty.Equiv` out of any function. -/
--@[simps] Porting note: commented out and lemmas added manually
def _root_.Equiv.empty (f : M ‚âÉ N) : M ‚âÉ[Language.empty] N where toEquiv := f
#align equiv.empty Equiv.empty

@[simp]
theorem toFun_equiv_empty (f : M ‚âÉ N) : (Equiv.empty f : M ‚Üí N) = f :=
  rfl

@[simp]
theorem toEquiv_equiv_empty (f : M ‚âÉ N) : (Equiv.empty f).toEquiv = f :=
  rfl

end Empty

end Language

end FirstOrder

namespace Equiv

open FirstOrder FirstOrder.Language FirstOrder.Language.Structure

open FirstOrder

variable {L : Language} {M : Type*} {N : Type*} [L.Structure M]

/-- A structure induced by a bijection. -/
@[simps!]
def inducedStructure (e : M ‚âÉ N) : L.Structure N :=
  ‚ü®fun f x => e (funMap f (e.symm ‚àò x)), fun r x => RelMap r (e.symm ‚àò x)‚ü©
set_option linter.uppercaseLean3 false in
#align equiv.induced_Structure Equiv.inducedStructure

/-- A bijection as a first-order isomorphism with the induced structure on the codomain. -/
--@[simps!] Porting note: commented out and lemmas added manually
def inducedStructureEquiv (e : M ‚âÉ N) : @Language.Equiv L M N _ (inducedStructure e) := by
  letI : L.Structure N := inducedStructure e
  -- ‚ä¢ M ‚âÉ[L] N
  exact
  { e with
    map_fun' := @fun n f x => by simp [‚Üê Function.comp.assoc e.symm e x]
    map_rel' := @fun n r x => by simp [‚Üê Function.comp.assoc e.symm e x] }
set_option linter.uppercaseLean3 false in
#align equiv.induced_Structure_equiv Equiv.inducedStructureEquiv

@[simp]
theorem toEquiv_inducedStructureEquiv (e : M ‚âÉ N) :
    @Language.Equiv.toEquiv L M N _ (inducedStructure e) (inducedStructureEquiv e) = e :=
  rfl

@[simp]
theorem toFun_inducedStructureEquiv (e : M ‚âÉ N) :
    FunLike.coe (@inducedStructureEquiv L M N _ e) = e :=
  rfl

@[simp]
theorem toFun_inducedStructureEquiv_Symm (e : M ‚âÉ N) :
  (by
    letI : L.Structure N := inducedStructure e
    -- ‚ä¢ (a : N) ‚Üí (fun x => M) a
    exact FunLike.coe (@inducedStructureEquiv L M N _ e).symm) = (e.symm : N ‚Üí M) :=
    -- üéâ no goals
  rfl

end Equiv
