/-
Copyright (c) 2022 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson
-/
import Mathlib.ModelTheory.FinitelyGenerated
import Mathlib.ModelTheory.DirectLimit
import Mathlib.ModelTheory.Bundled

#align_import model_theory.fraisse from "leanprover-community/mathlib"@"0602c59878ff3d5f71dea69c2d32ccf2e93e5398"

/-!
# Fra√Øss√© Classes and Fra√Øss√© Limits

This file pertains to the ages of countable first-order structures. The age of a structure is the
class of all finitely-generated structures that embed into it.

Of particular interest are Fra√Øss√© classes, which are exactly the ages of countable
ultrahomogeneous structures. To each is associated a unique (up to nonunique isomorphism)
Fra√Øss√© limit - the countable ultrahomogeneous structure with that age.

## Main Definitions
* `FirstOrder.Language.age` is the class of finitely-generated structures that embed into a
particular structure.
* A class `K` is `FirstOrder.Language.Hereditary` when all finitely-generated
structures that embed into structures in `K` are also in `K`.
* A class `K` has `FirstOrder.Language.JointEmbedding` when for every `M`, `N` in
`K`, there is another structure in `K` into which both `M` and `N` embed.
* A class `K` has `FirstOrder.Language.Amalgamation` when for any pair of embeddings
of a structure `M` in `K` into other structures in `K`, those two structures can be embedded into a
fourth structure in `K` such that the resulting square of embeddings commutes.
* `FirstOrder.Language.IsFraisse` indicates that a class is nonempty, isomorphism-invariant,
essentially countable, and satisfies the hereditary, joint embedding, and amalgamation properties.
* `FirstOrder.Language.IsFraisseLimit` indicates that a structure is a Fra√Øss√© limit for a given
class.

## Main Results
* We show that the age of any structure is isomorphism-invariant and satisfies the hereditary and
joint-embedding properties.
* `FirstOrder.Language.age.countable_quotient` shows that the age of any countable structure is
essentially countable.
* `FirstOrder.Language.exists_countable_is_age_of_iff` gives necessary and sufficient conditions
for a class to be the age of a countable structure in a language with countably many functions.

## Implementation Notes
* Classes of structures are formalized with `Set (Bundled L.Structure)`.
* Some results pertain to countable limit structures, others to countably-generated limit
structures. In the case of a language with countably many function symbols, these are equivalent.

## References
- [W. Hodges, *A Shorter Model Theory*][Hodges97]
- [K. Tent, M. Ziegler, *A Course in Model Theory*][Tent_Ziegler]

## TODO
* Show existence and uniqueness of Fra√Øss√© limits

-/


universe u v w w'

open scoped FirstOrder

open Set CategoryTheory

namespace FirstOrder

namespace Language

open Structure Substructure

variable (L : Language.{u, v})

/-! ### The Age of a Structure and Fra√Øss√© Classes-/


/-- The age of a structure `M` is the class of finitely-generated structures that embed into it. -/
def age (M : Type w) [L.Structure M] : Set (Bundled.{w} L.Structure) :=
  {N | Structure.FG L N ‚àß Nonempty (N ‚Ü™[L] M)}
#align first_order.language.age FirstOrder.Language.age

variable {L} (K : Set (Bundled.{w} L.Structure))

/-- A class `K` has the hereditary property when all finitely-generated structures that embed into
  structures in `K` are also in `K`.  -/
def Hereditary : Prop :=
  ‚àÄ M : Bundled.{w} L.Structure, M ‚àà K ‚Üí L.age M ‚äÜ K
#align first_order.language.hereditary FirstOrder.Language.Hereditary

/-- A class `K` has the joint embedding property when for every `M`, `N` in `K`, there is another
  structure in `K` into which both `M` and `N` embed. -/
def JointEmbedding : Prop :=
  DirectedOn (fun M N : Bundled.{w} L.Structure => Nonempty (M ‚Ü™[L] N)) K
#align first_order.language.joint_embedding FirstOrder.Language.JointEmbedding

/-- A class `K` has the amalgamation property when for any pair of embeddings of a structure `M` in
  `K` into other structures in `K`, those two structures can be embedded into a fourth structure in
  `K` such that the resulting square of embeddings commutes. -/
def Amalgamation : Prop :=
  ‚àÄ (M N P : Bundled.{w} L.Structure) (MN : M ‚Ü™[L] N) (MP : M ‚Ü™[L] P),
    M ‚àà K ‚Üí N ‚àà K ‚Üí P ‚àà K ‚Üí ‚àÉ (Q : Bundled.{w} L.Structure) (NQ : N ‚Ü™[L] Q) (PQ : P ‚Ü™[L] Q),
      Q ‚àà K ‚àß NQ.comp MN = PQ.comp MP
#align first_order.language.amalgamation FirstOrder.Language.Amalgamation

/-- A Fra√Øss√© class is a nonempty, isomorphism-invariant, essentially countable class of structures
satisfying the hereditary, joint embedding, and amalgamation properties. -/
class IsFraisse : Prop where
  is_nonempty : K.Nonempty
  FG : ‚àÄ M : Bundled.{w} L.Structure, M ‚àà K ‚Üí Structure.FG L M
  is_equiv_invariant : ‚àÄ M N : Bundled.{w} L.Structure, Nonempty (M ‚âÉ[L] N) ‚Üí (M ‚àà K ‚Üî N ‚àà K)
  is_essentially_countable : (Quotient.mk' '' K).Countable
  Hereditary : Hereditary K
  JointEmbedding : JointEmbedding K
  Amalgamation : Amalgamation K
#align first_order.language.is_fraisse FirstOrder.Language.IsFraisse

variable {K} (L) (M : Type w) [Structure L M]

theorem age.is_equiv_invariant (N P : Bundled.{w} L.Structure) (h : Nonempty (N ‚âÉ[L] P)) :
    N ‚àà L.age M ‚Üî P ‚àà L.age M :=
  and_congr h.some.fg_iff
    ‚ü®Nonempty.map fun x => Embedding.comp x h.some.symm.toEmbedding,
      Nonempty.map fun x => Embedding.comp x h.some.toEmbedding‚ü©
#align first_order.language.age.is_equiv_invariant FirstOrder.Language.age.is_equiv_invariant

variable {L} {M} {N : Type w} [Structure L N]

theorem Embedding.age_subset_age (MN : M ‚Ü™[L] N) : L.age M ‚äÜ L.age N := fun _ =>
  And.imp_right (Nonempty.map MN.comp)
#align first_order.language.embedding.age_subset_age FirstOrder.Language.Embedding.age_subset_age

theorem Equiv.age_eq_age (MN : M ‚âÉ[L] N) : L.age M = L.age N :=
  le_antisymm MN.toEmbedding.age_subset_age MN.symm.toEmbedding.age_subset_age
#align first_order.language.equiv.age_eq_age FirstOrder.Language.Equiv.age_eq_age

theorem Structure.FG.mem_age_of_equiv {M N : Bundled L.Structure} (h : Structure.FG L M)
    (MN : Nonempty (M ‚âÉ[L] N)) : N ‚àà L.age M :=
  ‚ü®MN.some.fg_iff.1 h, ‚ü®MN.some.symm.toEmbedding‚ü©‚ü©
set_option linter.uppercaseLean3 false in
#align first_order.language.Structure.fg.mem_age_of_equiv FirstOrder.Language.Structure.FG.mem_age_of_equiv

theorem Hereditary.is_equiv_invariant_of_fG (h : Hereditary K)
    (fg : ‚àÄ M : Bundled.{w} L.Structure, M ‚àà K ‚Üí Structure.FG L M) (M N : Bundled.{w} L.Structure)
    (hn : Nonempty (M ‚âÉ[L] N)) : M ‚àà K ‚Üî N ‚àà K :=
  ‚ü®fun MK => h M MK ((fg M MK).mem_age_of_equiv hn), fun NK =>
    h N NK ((fg N NK).mem_age_of_equiv ‚ü®hn.some.symm‚ü©)‚ü©
#align first_order.language.hereditary.is_equiv_invariant_of_fg FirstOrder.Language.Hereditary.is_equiv_invariant_of_fG

variable (M)

theorem age.nonempty : (L.age M).Nonempty :=
  ‚ü®Bundled.of (Substructure.closure L (‚àÖ : Set M)),
    (fg_iff_structure_fg _).1 (fg_closure Set.finite_empty), ‚ü®Substructure.subtype _‚ü©‚ü©
#align first_order.language.age.nonempty FirstOrder.Language.age.nonempty

theorem age.hereditary : Hereditary (L.age M) := fun _ hN _ hP => hN.2.some.age_subset_age hP
#align first_order.language.age.hereditary FirstOrder.Language.age.hereditary

theorem age.jointEmbedding : JointEmbedding (L.age M) := fun _ hN _ hP =>
  ‚ü®Bundled.of (‚Ü•(hN.2.some.toHom.range ‚äî hP.2.some.toHom.range)),
    ‚ü®(fg_iff_structure_fg _).1 ((hN.1.range hN.2.some.toHom).sup (hP.1.range hP.2.some.toHom)),
      ‚ü®Substructure.subtype _‚ü©‚ü©,
    ‚ü®Embedding.comp (inclusion le_sup_left) hN.2.some.equivRange.toEmbedding‚ü©,
    ‚ü®Embedding.comp (inclusion le_sup_right) hP.2.some.equivRange.toEmbedding‚ü©‚ü©
#align first_order.language.age.joint_embedding FirstOrder.Language.age.jointEmbedding

/-- The age of a countable structure is essentially countable (has countably many isomorphism
classes). -/
theorem age.countable_quotient [h : Countable M] : (Quotient.mk' '' L.age M).Countable := by
  classical
  refine' (congr_arg _ (Set.ext <| forall_quotient_iff.2 fun N => _)).mp
    (countable_range fun s : Finset M => ‚ü¶‚ü®closure L (s : Set M), inferInstance‚ü©‚üß)
  constructor
  ¬∑ rintro ‚ü®s, hs‚ü©
    use Bundled.of (closure L (s : Set M))
    exact ‚ü®‚ü®(fg_iff_structure_fg _).1 (fg_closure s.finite_toSet), ‚ü®Substructure.subtype _‚ü©‚ü©, hs‚ü©
  ¬∑ simp only [mem_range, Quotient.eq]
    rintro ‚ü®P, ‚ü®‚ü®s, hs‚ü©, ‚ü®PM‚ü©‚ü©, hP2‚ü©
    have : P ‚âà N := by apply Quotient.eq'.mp; rw [hP2]; rfl -- Porting note: added
    refine' ‚ü®s.image PM, Setoid.trans (b := P) _ this‚ü©
    rw [‚Üê Embedding.coe_toHom, Finset.coe_image, closure_image PM.toHom, hs, ‚Üê Hom.range_eq_map]
    exact ‚ü®PM.equivRange.symm‚ü©
#align first_order.language.age.countable_quotient FirstOrder.Language.age.countable_quotient

/-- The age of a direct limit of structures is the union of the ages of the structures. -/
-- @[simp] -- Porting note: cannot simplify itself
theorem age_directLimit {Œπ : Type w} [Preorder Œπ] [IsDirected Œπ (¬∑ ‚â§ ¬∑)] [Nonempty Œπ]
    (G : Œπ ‚Üí Type max w w') [‚àÄ i, L.Structure (G i)] (f : ‚àÄ i j, i ‚â§ j ‚Üí G i ‚Ü™[L] G j)
    [DirectedSystem G fun i j h => f i j h] : L.age (DirectLimit G f) = ‚ãÉ i : Œπ, L.age (G i) := by
  classical
  ext M
  simp only [mem_iUnion]
  constructor
  ¬∑ rintro ‚ü®Mfg, ‚ü®e‚ü©‚ü©
    obtain ‚ü®s, hs‚ü© := Mfg.range e.toHom
    let out := @Quotient.out _ (DirectLimit.setoid G f)
    obtain ‚ü®i, hi‚ü© := Finset.exists_le (s.image (Sigma.fst ‚àò out))
    have e' := (DirectLimit.of L Œπ G f i).equivRange.symm.toEmbedding
    refine' ‚ü®i, Mfg, ‚ü®e'.comp ((Substructure.inclusion _).comp e.equivRange.toEmbedding)‚ü©‚ü©
    rw [‚Üê hs, closure_le]
    intro x hx
    refine' ‚ü®f (out x).1 i (hi (out x).1 (Finset.mem_image_of_mem _ hx)) (out x).2, _‚ü©
    rw [Embedding.coe_toHom, DirectLimit.of_apply, @Quotient.mk_eq_iff_out _ (_),
      DirectLimit.equiv_iff G f _ (hi (out x).1 (Finset.mem_image_of_mem _ hx)),
      DirectedSystem.map_self]
    rfl
  ¬∑ rintro ‚ü®i, Mfg, ‚ü®e‚ü©‚ü©
    exact ‚ü®Mfg, ‚ü®Embedding.comp (DirectLimit.of L Œπ G f i) e‚ü©‚ü©
#align first_order.language.age_direct_limit FirstOrder.Language.age_directLimit

/-- Sufficient conditions for a class to be the age of a countably-generated structure. -/
theorem exists_cg_is_age_of (hn : K.Nonempty)
    (h : ‚àÄ M N : Bundled.{w} L.Structure, Nonempty (M ‚âÉ[L] N) ‚Üí (M ‚àà K ‚Üî N ‚àà K))
    (hc : (Quotient.mk' '' K).Countable)
    (fg : ‚àÄ M : Bundled.{w} L.Structure, M ‚àà K ‚Üí Structure.FG L M) (hp : Hereditary K)
    (jep : JointEmbedding K) : ‚àÉ M : Bundled.{w} L.Structure, Structure.CG L M ‚àß L.age M = K := by
  obtain ‚ü®F, hF‚ü© := hc.exists_eq_range (hn.image _)
  -- ‚ä¢ ‚àÉ M, Structure.CG L ‚ÜëM ‚àß age L ‚ÜëM = K
  simp only [Set.ext_iff, forall_quotient_iff, mem_image, mem_range, Quotient.eq'] at hF
  -- ‚ä¢ ‚àÉ M, Structure.CG L ‚ÜëM ‚àß age L ‚ÜëM = K
  simp_rw [Quotient.eq_mk_iff_out] at hF
  -- ‚ä¢ ‚àÉ M, Structure.CG L ‚ÜëM ‚àß age L ‚ÜëM = K
  have hF' : ‚àÄ n : ‚Ñï, (F n).out ‚àà K := by
    intro n
    obtain ‚ü®P, hP1, hP2‚ü© := (hF (F n).out).2 ‚ü®n, Setoid.refl _‚ü©
    -- Porting note: fix hP2 because `Quotient.out (Quotient.mk' x) ‚âà a` was not simplified
    -- to `x ‚âà a` in hF
    replace hP2 := Setoid.trans (Setoid.symm (Quotient.mk_out P)) hP2
    exact (h _ _ hP2).1 hP1
  choose P hPK hP hFP using fun (N : K) (n : ‚Ñï) => jep N N.2 (F (n + 1)).out (hF' _)
  -- ‚ä¢ ‚àÉ M, Structure.CG L ‚ÜëM ‚àß age L ‚ÜëM = K
  let G : ‚Ñï ‚Üí K := @Nat.rec (fun _ => K) ‚ü®(F 0).out, hF' 0‚ü© fun n N => ‚ü®P N n, hPK N n‚ü©
  -- ‚ä¢ ‚àÉ M, Structure.CG L ‚ÜëM ‚àß age L ‚ÜëM = K
  -- Poting note: was
  -- let f : ‚àÄ i j, i ‚â§ j ‚Üí G i ‚Ü™[L] G j := DirectedSystem.natLeRec fun n => (hP _ n).some
  let f : ‚àÄ (i j : ‚Ñï), i ‚â§ j ‚Üí (G i).val ‚Ü™[L] (G j).val := by
    refine DirectedSystem.natLERec (G' := fun i => (G i).val) (L := L) ?_
    dsimp only
    exact (fun n => (hP _ n).some)
  have : DirectedSystem (fun n ‚Ü¶ (G n).val) fun i j h ‚Ü¶ ‚Üë(f i j h) := by
    dsimp; infer_instance
  refine ‚ü®Bundled.of (@DirectLimit L _ _ (fun n ‚Ü¶ (G n).val) _ f _ _), ?_, ?_‚ü©
  -- ‚ä¢ Structure.CG L ‚Üë(Bundled.of (DirectLimit (fun n => ‚Üë‚Üë(G n)) f))
  ¬∑ exact DirectLimit.cg _ (fun n => (fg _ (G n).2).cg)
    -- üéâ no goals
  ¬∑ refine (age_directLimit (fun n ‚Ü¶ (G n).val) f).trans
      (subset_antisymm (iUnion_subset fun n N hN => hp (G n).val (G n).2 hN) fun N KN => ?_)
    have : Quotient.out (Quotient.mk' N) ‚âà N := Quotient.eq_mk_iff_out.mp rfl
    -- ‚ä¢ N ‚àà ‚ãÉ (i : ‚Ñï), age L ‚Üë‚Üë(G i)
    obtain ‚ü®n, ‚ü®e‚ü©‚ü© := (hF N).1 ‚ü®N, KN, this‚ü©
    -- ‚ä¢ N ‚àà ‚ãÉ (i : ‚Ñï), age L ‚Üë‚Üë(G i)
    refine mem_iUnion_of_mem n ‚ü®fg _ KN, ‚ü®Embedding.comp ?_ e.symm.toEmbedding‚ü©‚ü©
    -- ‚ä¢ ‚Üë(Quotient.out (F n)) ‚Ü™[L] ‚Üë‚Üë(G n)
    cases' n with n
    -- ‚ä¢ ‚Üë(Quotient.out (F Nat.zero)) ‚Ü™[L] ‚Üë‚Üë(G Nat.zero)
    ¬∑ dsimp; exact Embedding.refl _ _
      -- ‚ä¢ ‚Üë(Quotient.out (F 0)) ‚Ü™[L] ‚Üë(Quotient.out (F 0))
             -- üéâ no goals
    ¬∑ dsimp; exact (hFP _ n).some
      -- ‚ä¢ ‚Üë(Quotient.out (F (Nat.succ n))) ‚Ü™[L] ‚Üë(P (Nat.rec { val := Quotient.out (F  ‚Ä¶
             -- üéâ no goals
#align first_order.language.exists_cg_is_age_of FirstOrder.Language.exists_cg_is_age_of

theorem exists_countable_is_age_of_iff [Countable (Œ£ l, L.Functions l)] :
    (‚àÉ M : Bundled.{w} L.Structure, Countable M ‚àß L.age M = K) ‚Üî
      K.Nonempty ‚àß (‚àÄ M N : Bundled.{w} L.Structure, Nonempty (M ‚âÉ[L] N) ‚Üí (M ‚àà K ‚Üî N ‚àà K)) ‚àß
      (Quotient.mk' '' K).Countable ‚àß (‚àÄ M : Bundled.{w} L.Structure, M ‚àà K ‚Üí Structure.FG L M) ‚àß
      Hereditary K ‚àß JointEmbedding K := by
  constructor
  -- ‚ä¢ (‚àÉ M, Countable ‚ÜëM ‚àß age L ‚ÜëM = K) ‚Üí Set.Nonempty K ‚àß (‚àÄ (M N : Bundled (Str ‚Ä¶
  ¬∑ rintro ‚ü®M, h1, h2, rfl‚ü©
    -- ‚ä¢ Set.Nonempty (age L ‚ÜëM) ‚àß (‚àÄ (M_1 N : Bundled (Structure L)), Nonempty (‚ÜëM_1 ‚Ä¶
    refine' ‚ü®age.nonempty M, age.is_equiv_invariant L M, age.countable_quotient M, fun N hN => hN.1,
      age.hereditary M, age.jointEmbedding M‚ü©
  ¬∑ rintro ‚ü®Kn, eqinv, cq, hfg, hp, jep‚ü©
    -- ‚ä¢ ‚àÉ M, Countable ‚ÜëM ‚àß age L ‚ÜëM = K
    obtain ‚ü®M, hM, rfl‚ü© := exists_cg_is_age_of Kn eqinv cq hfg hp jep
    -- ‚ä¢ ‚àÉ M_1, Countable ‚ÜëM_1 ‚àß age L ‚ÜëM_1 = age L ‚ÜëM
    exact ‚ü®M, Structure.cg_iff_countable.1 hM, rfl‚ü©
    -- üéâ no goals
#align first_order.language.exists_countable_is_age_of_iff FirstOrder.Language.exists_countable_is_age_of_iff

variable (L)

/-- A structure `M` is ultrahomogeneous if every embedding of a finitely generated substructure
into `M` extends to an automorphism of `M`. -/
def IsUltrahomogeneous : Prop :=
  ‚àÄ (S : L.Substructure M) (_ : S.FG) (f : S ‚Ü™[L] M),
    ‚àÉ g : M ‚âÉ[L] M, f = g.toEmbedding.comp S.subtype
#align first_order.language.is_ultrahomogeneous FirstOrder.Language.IsUltrahomogeneous

variable {L} (K)

/-- A structure `M` is a Fra√Øss√© limit for a class `K` if it is countably generated,
ultrahomogeneous, and has age `K`. -/
structure IsFraisseLimit [Countable (Œ£ l, L.Functions l)] [Countable M] : Prop where
  protected ultrahomogeneous : IsUltrahomogeneous L M
  protected age : L.age M = K
#align first_order.language.is_fraisse_limit FirstOrder.Language.IsFraisseLimit

variable {M}

theorem IsUltrahomogeneous.amalgamation_age (h : L.IsUltrahomogeneous M) :
    Amalgamation (L.age M) := by
  rintro N P Q NP NQ ‚ü®Nfg, ‚ü®-‚ü©‚ü© ‚ü®Pfg, ‚ü®PM‚ü©‚ü© ‚ü®Qfg, ‚ü®QM‚ü©‚ü©
  -- ‚ä¢ ‚àÉ Q_1 NQ_1 PQ, Q_1 ‚àà age L M ‚àß Embedding.comp NQ_1 NP = Embedding.comp PQ NQ
  obtain ‚ü®g, hg‚ü© := h (PM.comp NP).toHom.range (Nfg.range _)
    ((QM.comp NQ).comp (PM.comp NP).equivRange.symm.toEmbedding)
  let s := (g.toHom.comp PM.toHom).range ‚äî QM.toHom.range
  -- ‚ä¢ ‚àÉ Q_1 NQ_1 PQ, Q_1 ‚àà age L M ‚àß Embedding.comp NQ_1 NP = Embedding.comp PQ NQ
  refine' ‚ü®Bundled.of s,
    Embedding.comp (Substructure.inclusion le_sup_left)
      (g.toEmbedding.comp PM).equivRange.toEmbedding,
    Embedding.comp (Substructure.inclusion le_sup_right) QM.equivRange.toEmbedding,
    ‚ü®(fg_iff_structure_fg _).1 (FG.sup (Pfg.range _) (Qfg.range _)), ‚ü®Substructure.subtype _‚ü©‚ü©, _‚ü©
  ext n
  -- ‚ä¢ ‚Üë(Embedding.comp (Embedding.comp (Substructure.inclusion (_ : Hom.range (Hom ‚Ä¶
  apply Subtype.ext
  -- ‚ä¢ ‚Üë(‚Üë(Embedding.comp (Embedding.comp (Substructure.inclusion (_ : Hom.range (H ‚Ä¶
  have hgn := (Embedding.ext_iff.1 hg) ((PM.comp NP).equivRange n)
  -- ‚ä¢ ‚Üë(‚Üë(Embedding.comp (Embedding.comp (Substructure.inclusion (_ : Hom.range (H ‚Ä¶
  simp only [Embedding.comp_apply, Equiv.coe_toEmbedding, Equiv.symm_apply_apply,
    Substructure.coeSubtype, Embedding.equivRange_apply] at hgn
  simp only [Embedding.comp_apply, Equiv.coe_toEmbedding]
  -- ‚ä¢ ‚Üë(‚Üë(Substructure.inclusion (_ : Hom.range (Hom.comp (Equiv.toHom g) (Embeddi ‚Ä¶
  erw [Substructure.coe_inclusion, Substructure.coe_inclusion]
  -- ‚ä¢ ‚Üë(Set.inclusion (_ : Hom.range (Hom.comp (Equiv.toHom g) (Embedding.toHom PM ‚Ä¶
  simp only [Embedding.comp_apply, Equiv.coe_toEmbedding, Set.coe_inclusion,
    Embedding.equivRange_apply, hgn]
#align first_order.language.is_ultrahomogeneous.amalgamation_age FirstOrder.Language.IsUltrahomogeneous.amalgamation_age

theorem IsUltrahomogeneous.age_isFraisse [Countable M] (h : L.IsUltrahomogeneous M) :
    IsFraisse (L.age M) :=
  ‚ü®age.nonempty M, fun _ hN => hN.1, age.is_equiv_invariant L M, age.countable_quotient M,
    age.hereditary M, age.jointEmbedding M, h.amalgamation_age‚ü©
#align first_order.language.is_ultrahomogeneous.age_is_fraisse FirstOrder.Language.IsUltrahomogeneous.age_isFraisse

namespace IsFraisseLimit

/-- If a class has a Fra√Øss√© limit, it must be Fra√Øss√©. -/
theorem isFraisse [Countable (Œ£ l, L.Functions l)] [Countable M] (h : IsFraisseLimit K M) :
    IsFraisse K :=
  (congr rfl h.age).mp h.ultrahomogeneous.age_isFraisse
#align first_order.language.is_fraisse_limit.is_fraisse FirstOrder.Language.IsFraisseLimit.isFraisse

end IsFraisseLimit

end Language

end FirstOrder
