/-
Copyright (c) 2022 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson
-/
import Mathlib.ModelTheory.Quotients
import Mathlib.Order.Filter.Germ
import Mathlib.Order.Filter.Ultrafilter

#align_import model_theory.ultraproducts from "leanprover-community/mathlib"@"f1ae620609496a37534c2ab3640b641d5be8b6f0"

/-! # Ultraproducts and ÅoÅ›'s Theorem

## Main Definitions
* `FirstOrder.Language.Ultraproduct.Structure` is the ultraproduct structure on `Filter.Product`.

## Main Results
* ÅoÅ›'s Theorem: `FirstOrder.Language.Ultraproduct.sentence_realize`. An ultraproduct models a
sentence `Ï†` if and only if the set of structures in the product that model `Ï†` is in the
ultrafilter.

## Tags
ultraproduct, Los's theorem

-/


universe u v

variable {Î± : Type*} (M : Î± â†’ Type*) (u : Ultrafilter Î±)

open FirstOrder Filter

open Filter

namespace FirstOrder

namespace Language

open Structure

variable {L : Language.{u, v}} [âˆ€ a, L.Structure (M a)]

namespace Ultraproduct

instance setoidPrestructure : L.Prestructure ((u : Filter Î±).productSetoid M) :=
  { (u : Filter Î±).productSetoid M with
    toStructure :=
      { funMap := fun {n} f x a => funMap f fun i => x i a
        RelMap := fun {n} r x => âˆ€á¶  a : Î± in u, RelMap r fun i => x i a }
    fun_equiv := fun {n} f x y xy => by
      refine' mem_of_superset (iInter_mem.2 xy) fun a ha => _
      -- âŠ¢ a âˆˆ {x_1 | (fun a => funMap f x a = funMap f y a) x_1}
      simp only [Set.mem_iInter, Set.mem_setOf_eq] at ha
      -- âŠ¢ a âˆˆ {x_1 | (fun a => funMap f x a = funMap f y a) x_1}
      simp only [Set.mem_setOf_eq, ha]
      -- ðŸŽ‰ no goals
    rel_equiv := fun {n} r x y xy => by
      rw [â† iff_eq_eq]
      -- âŠ¢ RelMap r x â†” RelMap r y
      refine' âŸ¨fun hx => _, fun hy => _âŸ©
      -- âŠ¢ RelMap r y
      Â· refine' mem_of_superset (inter_mem hx (iInter_mem.2 xy)) _
        -- âŠ¢ {x_1 | (fun a => RelMap r fun i => x i a) x_1} âˆ© â‹‚ (i : Fin n), {x_1 | (fun  â€¦
        rintro a âŸ¨ha1, ha2âŸ©
        -- âŠ¢ a âˆˆ {x | (fun a => RelMap r fun i => y i a) x}
        simp only [Set.mem_iInter, Set.mem_setOf_eq] at *
        -- âŠ¢ RelMap r fun i => y i a
        rw [â† funext ha2]
        -- âŠ¢ RelMap r fun x_1 => x x_1 a
        exact ha1
        -- ðŸŽ‰ no goals
      Â· refine' mem_of_superset (inter_mem hy (iInter_mem.2 xy)) _
        -- âŠ¢ {x | (fun a => RelMap r fun i => y i a) x} âˆ© â‹‚ (i : Fin n), {x_1 | (fun a => â€¦
        rintro a âŸ¨ha1, ha2âŸ©
        -- âŠ¢ a âˆˆ {x_1 | (fun a => RelMap r fun i => x i a) x_1}
        simp only [Set.mem_iInter, Set.mem_setOf_eq] at *
        -- âŠ¢ RelMap r fun i => x i a
        rw [funext ha2]
        -- âŠ¢ RelMap r fun x => y x a
        exact ha1 }
        -- ðŸŽ‰ no goals
#align first_order.language.ultraproduct.setoid_prestructure FirstOrder.Language.Ultraproduct.setoidPrestructure

variable {M} {u}

instance Â«structureÂ» : L.Structure ((u : Filter Î±).Product M) :=
  Language.quotientStructure
set_option linter.uppercaseLean3 false in
#align first_order.language.ultraproduct.Structure FirstOrder.Language.Ultraproduct.structure

theorem funMap_cast {n : â„•} (f : L.Functions n) (x : Fin n â†’ âˆ€ a, M a) :
    (funMap f fun i => (x i : (u : Filter Î±).Product M)) =
      (fun a => funMap f fun i => x i a : (u : Filter Î±).Product M) := by
  apply funMap_quotient_mk'
  -- ðŸŽ‰ no goals
#align first_order.language.ultraproduct.fun_map_cast FirstOrder.Language.Ultraproduct.funMap_cast

theorem term_realize_cast {Î² : Type*} (x : Î² â†’ âˆ€ a, M a) (t : L.Term Î²) :
    (t.realize fun i => (x i : (u : Filter Î±).Product M)) =
      (fun a => t.realize fun i => x i a : (u : Filter Î±).Product M) := by
  convert @Term.realize_quotient_mk' L _ ((u : Filter Î±).productSetoid M)
      (Ultraproduct.setoidPrestructure M u) _ t x using 2
  ext a
  -- âŠ¢ Term.realize (fun i => x i a) t = Term.realize x t a
  induction t
  -- âŠ¢ Term.realize (fun i => x i a) (var _aâœ) = Term.realize x (var _aâœ) a
  case var =>
    rfl
  case func _ _ _ t_ih =>
    simp only [Term.realize, t_ih]
    rfl
#align first_order.language.ultraproduct.term_realize_cast FirstOrder.Language.Ultraproduct.term_realize_cast

variable [âˆ€ a : Î±, Nonempty (M a)]

theorem boundedFormula_realize_cast {Î² : Type*} {n : â„•} (Ï† : L.BoundedFormula Î² n)
    (x : Î² â†’ âˆ€ a, M a) (v : Fin n â†’ âˆ€ a, M a) :
    (Ï†.Realize (fun i : Î² => (x i : (u : Filter Î±).Product M))
        (fun i => (v i : (u : Filter Î±).Product M))) â†”
      âˆ€á¶  a : Î± in u, Ï†.Realize (fun i : Î² => x i a) fun i => v i a := by
  letI := (u : Filter Î±).productSetoid M
  -- âŠ¢ (BoundedFormula.Realize Ï† (fun i => Quotient.mk' (x i)) fun i => Quotient.mk â€¦
  induction' Ï† with _ _ _ _ _ _ _ _ m _ _ ih ih' k Ï† ih
  Â· simp only [BoundedFormula.Realize, eventually_const]
    -- ðŸŽ‰ no goals
  Â· have h2 : âˆ€ a : Î±, (Sum.elim (fun i : Î² => x i a) fun i => v i a) = fun i => Sum.elim x v i a :=
      fun a => funext fun i => Sum.casesOn i (fun i => rfl) fun i => rfl
    simp only [BoundedFormula.Realize, h2, term_realize_cast]
    -- âŠ¢ Term.realize (Sum.elim (fun i => Quotient.mk' (x i)) fun i => Quotient.mk' ( â€¦
    erw [(Sum.comp_elim ((â†‘) : (âˆ€ a, M a) â†’ (u : Filter Î±).Product M) x v).symm,
      term_realize_cast, term_realize_cast]
    exact Quotient.eq''
    -- ðŸŽ‰ no goals
  Â· have h2 : âˆ€ a : Î±, (Sum.elim (fun i : Î² => x i a) fun i => v i a) = fun i => Sum.elim x v i a :=
      fun a => funext fun i => Sum.casesOn i (fun i => rfl) fun i => rfl
    simp only [BoundedFormula.Realize, h2]
    -- âŠ¢ (RelMap Râœ fun i => Term.realize (Sum.elim (fun i => Quotient.mk' (x i)) fun â€¦
    erw [(Sum.comp_elim ((â†‘) : (âˆ€ a, M a) â†’ (u : Filter Î±).Product M) x v).symm]
    -- âŠ¢ (RelMap Râœ fun i => Term.realize (Quotient.mk' âˆ˜ Sum.elim x v) (tsâœ i)) â†” âˆ€á¶  â€¦
    conv_lhs => enter [2, i]; erw [term_realize_cast]
    -- âŠ¢ (RelMap Râœ fun i => Quotient.mk' fun a => Term.realize (fun i => Sum.elim x  â€¦
    apply relMap_quotient_mk'
    -- ðŸŽ‰ no goals
  Â· simp only [BoundedFormula.Realize, ih v, ih' v]
    -- âŠ¢ ((âˆ€á¶  (a : Î±) in â†‘u, BoundedFormula.Realize fâ‚âœ (fun i => x i a) fun i => v i â€¦
    rw [Ultrafilter.eventually_imp]
    -- ðŸŽ‰ no goals
  Â· simp only [BoundedFormula.Realize]
    -- âŠ¢ (âˆ€ (x_1 : Quotient (productSetoid â†‘u fun a => M a)), BoundedFormula.Realize  â€¦
    apply Iff.trans (b := âˆ€ m : âˆ€ a : Î±, M a,
      Ï†.Realize (fun i : Î² => (x i : (u : Filter Î±).Product M))
        (Fin.snoc (((â†‘) : (âˆ€ a, M a) â†’ (u : Filter Î±).Product M) âˆ˜ v)
          (m : (u : Filter Î±).Product M)))
    Â· exact forall_quotient_iff
      -- ðŸŽ‰ no goals
    have h' :
      âˆ€ (m : âˆ€ a, M a) (a : Î±),
        (fun i : Fin (k + 1) => (Fin.snoc v m : _ â†’ âˆ€ a, M a) i a) =
          Fin.snoc (fun i : Fin k => v i a) (m a) := by
      refine' fun m a => funext (Fin.reverseInduction _ fun i _ => _)
      Â· simp only [Fin.snoc_last]
      Â· simp only [Fin.snoc_castSucc]
    simp only [â† Fin.comp_snoc]
    -- âŠ¢ (âˆ€ (m : (a : Î±) â†’ M a), BoundedFormula.Realize Ï† (fun i => Quotient.mk' (x i â€¦
    simp only [Function.comp, ih, h']
    -- âŠ¢ (âˆ€ (m : (a : Î±) â†’ M a), âˆ€á¶  (a : Î±) in â†‘u, BoundedFormula.Realize Ï† (fun i => â€¦
    refine' âŸ¨fun h => _, fun h m => _âŸ©
    -- âŠ¢ âˆ€á¶  (a : Î±) in â†‘u, âˆ€ (x_1 : M a), BoundedFormula.Realize Ï† (fun i => x i a) ( â€¦
    Â· contrapose! h
      -- âŠ¢ âˆƒ m, Â¬âˆ€á¶  (a : Î±) in â†‘u, BoundedFormula.Realize Ï† (fun i => x i a) (Fin.snoc  â€¦
      simp_rw [â† Ultrafilter.eventually_not, not_forall] at h
      -- âŠ¢ âˆƒ m, Â¬âˆ€á¶  (a : Î±) in â†‘u, BoundedFormula.Realize Ï† (fun i => x i a) (Fin.snoc  â€¦
      refine'
        âŸ¨fun a : Î± =>
          Classical.epsilon fun m : M a =>
            Â¬Ï†.Realize (fun i => x i a) (Fin.snoc (fun i => v i a) m),
          _âŸ©
      rw [â† Ultrafilter.eventually_not]
      -- âŠ¢ âˆ€á¶  (x_1 : Î±) in â†‘u, Â¬BoundedFormula.Realize Ï† (fun i => x i x_1) (Fin.snoc ( â€¦
      exact Filter.mem_of_superset h fun a ha => Classical.epsilon_spec ha
      -- ðŸŽ‰ no goals
    Â· rw [Filter.eventually_iff] at *
      -- âŠ¢ {x_1 | BoundedFormula.Realize Ï† (fun i => x i x_1) (Fin.snoc (fun i => v i x â€¦
      exact Filter.mem_of_superset h fun a ha => ha (m a)
      -- ðŸŽ‰ no goals
#align first_order.language.ultraproduct.bounded_formula_realize_cast FirstOrder.Language.Ultraproduct.boundedFormula_realize_cast

theorem realize_formula_cast {Î² : Type*} (Ï† : L.Formula Î²) (x : Î² â†’ âˆ€ a, M a) :
    (Ï†.Realize fun i => (x i : (u : Filter Î±).Product M)) â†”
      âˆ€á¶  a : Î± in u, Ï†.Realize fun i => x i a := by
  simp_rw [Formula.Realize, â† boundedFormula_realize_cast Ï† x, iff_eq_eq]
  -- âŠ¢ BoundedFormula.Realize Ï† (fun i => Quotient.mk' (x i)) default = BoundedForm â€¦
  exact congr rfl (Subsingleton.elim _ _)
  -- ðŸŽ‰ no goals
#align first_order.language.ultraproduct.realize_formula_cast FirstOrder.Language.Ultraproduct.realize_formula_cast

/-- ÅoÅ›'s Theorem : A sentence is true in an ultraproduct if and only if the set of structures it is
  true in is in the ultrafilter. -/
theorem sentence_realize (Ï† : L.Sentence) :
    (u : Filter Î±).Product M âŠ¨ Ï† â†” âˆ€á¶  a : Î± in u, M a âŠ¨ Ï† := by
  simp_rw [Sentence.Realize]
  -- âŠ¢ Formula.Realize Ï† default â†” âˆ€á¶  (a : Î±) in â†‘u, Formula.Realize Ï† default
  erw [â† realize_formula_cast Ï†, iff_eq_eq]
  -- âŠ¢ Formula.Realize Ï† default = Formula.Realize Ï† fun i => Quotient.mk' fun a => â€¦
  exact congr rfl (Subsingleton.elim _ _)
  -- ðŸŽ‰ no goals
#align first_order.language.ultraproduct.sentence_realize FirstOrder.Language.Ultraproduct.sentence_realize

nonrec instance Product.instNonempty : Nonempty ((u : Filter Î±).Product M) :=
  letI : âˆ€ a, Inhabited (M a) := fun _ => Classical.inhabited_of_nonempty'
  instNonempty
#align first_order.language.ultraproduct.product.nonempty FirstOrder.Language.Ultraproduct.Product.instNonempty

end Ultraproduct

end Language

end FirstOrder
