/-
Copyright (c) 2021 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson
-/
import Mathlib.Data.SetLike.Basic
import Mathlib.ModelTheory.Semantics

#align_import model_theory.definability from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Definable Sets
This file defines what it means for a set over a first-order structure to be definable.

## Main Definitions
* `Set.Definable` is defined so that `A.Definable L s` indicates that the
set `s` of a finite cartesian power of `M` is definable with parameters in `A`.
* `Set.Definableâ‚` is defined so that `A.Definableâ‚ L s` indicates that
`(s : Set M)` is definable with parameters in `A`.
* `Set.Definableâ‚‚` is defined so that `A.Definableâ‚‚ L s` indicates that
`(s : Set (M Ã— M))` is definable with parameters in `A`.
* A `FirstOrder.Language.DefinableSet` is defined so that `L.DefinableSet A Î±` is the boolean
  algebra of subsets of `Î± â†’ M` defined by formulas with parameters in `A`.

## Main Results
* `L.DefinableSet A Î±` forms a `BooleanAlgebra`
* `Set.Definable.image_comp` shows that definability is closed under projections in finite
  dimensions.

-/


universe u v w uâ‚

namespace Set

variable {M : Type w} (A : Set M) (L : FirstOrder.Language.{u, v}) [L.Structure M]

open FirstOrder FirstOrder.Language FirstOrder.Language.Structure

variable {Î± : Type uâ‚} {Î² : Type*}

/-- A subset of a finite Cartesian product of a structure is definable over a set `A` when
  membership in the set is given by a first-order formula with parameters from `A`. -/
def Definable (s : Set (Î± â†’ M)) : Prop :=
  âˆƒ Ï† : L[[A]].Formula Î±, s = setOf Ï†.Realize
#align set.definable Set.Definable

variable {L} {A} {B : Set M} {s : Set (Î± â†’ M)}

theorem Definable.map_expansion {L' : FirstOrder.Language} [L'.Structure M] (h : A.Definable L s)
    (Ï† : L â†’á´¸ L') [Ï†.IsExpansionOn M] : A.Definable L' s := by
  obtain âŸ¨Ïˆ, rflâŸ© := h
  -- âŠ¢ Definable A L' (setOf (Formula.Realize Ïˆ))
  refine' âŸ¨(Ï†.addConstants A).onFormula Ïˆ, _âŸ©
  -- âŠ¢ setOf (Formula.Realize Ïˆ) = setOf (Formula.Realize (LHom.onFormula (LHom.add â€¦
  ext x
  -- âŠ¢ x âˆˆ setOf (Formula.Realize Ïˆ) â†” x âˆˆ setOf (Formula.Realize (LHom.onFormula ( â€¦
  simp only [mem_setOf_eq, LHom.realize_onFormula]
  -- ðŸŽ‰ no goals
#align set.definable.map_expansion Set.Definable.map_expansion

theorem empty_definable_iff :
    (âˆ… : Set M).Definable L s â†” âˆƒ Ï† : L.Formula Î±, s = setOf Ï†.Realize := by
  rw [Definable, Equiv.exists_congr_left (LEquiv.addEmptyConstants L (âˆ… : Set M)).onFormula]
  -- âŠ¢ (âˆƒ Ï†, s = setOf (Formula.Realize Ï†)) â†” âˆƒ b, s = setOf (Formula.Realize (â†‘(LE â€¦
  simp [-constantsOn]
  -- ðŸŽ‰ no goals
#align set.empty_definable_iff Set.empty_definable_iff

theorem definable_iff_empty_definable_with_params :
    A.Definable L s â†” (âˆ… : Set M).Definable (L[[A]]) s :=
  empty_definable_iff.symm
#align set.definable_iff_empty_definable_with_params Set.definable_iff_empty_definable_with_params

theorem Definable.mono (hAs : A.Definable L s) (hAB : A âŠ† B) : B.Definable L s := by
  rw [definable_iff_empty_definable_with_params] at *
  -- âŠ¢ Definable âˆ… (L[[â†‘B]]) s
  exact hAs.map_expansion (L.lhomWithConstantsMap (Set.inclusion hAB))
  -- ðŸŽ‰ no goals
#align set.definable.mono Set.Definable.mono

@[simp]
theorem definable_empty : A.Definable L (âˆ… : Set (Î± â†’ M)) :=
  âŸ¨âŠ¥, by
    ext
    -- âŠ¢ xâœ âˆˆ âˆ… â†” xâœ âˆˆ setOf (Formula.Realize âŠ¥)
    simpâŸ©
    -- ðŸŽ‰ no goals
#align set.definable_empty Set.definable_empty

@[simp]
theorem definable_univ : A.Definable L (univ : Set (Î± â†’ M)) :=
  âŸ¨âŠ¤, by
    ext
    -- âŠ¢ xâœ âˆˆ univ â†” xâœ âˆˆ setOf (Formula.Realize âŠ¤)
    simpâŸ©
    -- ðŸŽ‰ no goals
#align set.definable_univ Set.definable_univ

@[simp]
theorem Definable.inter {f g : Set (Î± â†’ M)} (hf : A.Definable L f) (hg : A.Definable L g) :
    A.Definable L (f âˆ© g) := by
  rcases hf with âŸ¨Ï†, rflâŸ©
  -- âŠ¢ Definable A L (setOf (Formula.Realize Ï†) âˆ© g)
  rcases hg with âŸ¨Î¸, rflâŸ©
  -- âŠ¢ Definable A L (setOf (Formula.Realize Ï†) âˆ© setOf (Formula.Realize Î¸))
  refine' âŸ¨Ï† âŠ“ Î¸, _âŸ©
  -- âŠ¢ setOf (Formula.Realize Ï†) âˆ© setOf (Formula.Realize Î¸) = setOf (Formula.Reali â€¦
  ext
  -- âŠ¢ xâœ âˆˆ setOf (Formula.Realize Ï†) âˆ© setOf (Formula.Realize Î¸) â†” xâœ âˆˆ setOf (For â€¦
  simp
  -- ðŸŽ‰ no goals
#align set.definable.inter Set.Definable.inter

@[simp]
theorem Definable.union {f g : Set (Î± â†’ M)} (hf : A.Definable L f) (hg : A.Definable L g) :
    A.Definable L (f âˆª g) := by
  rcases hf with âŸ¨Ï†, hÏ†âŸ©
  -- âŠ¢ Definable A L (f âˆª g)
  rcases hg with âŸ¨Î¸, hÎ¸âŸ©
  -- âŠ¢ Definable A L (f âˆª g)
  refine' âŸ¨Ï† âŠ” Î¸, _âŸ©
  -- âŠ¢ f âˆª g = setOf (Formula.Realize (Ï† âŠ” Î¸))
  ext
  -- âŠ¢ xâœ âˆˆ f âˆª g â†” xâœ âˆˆ setOf (Formula.Realize (Ï† âŠ” Î¸))
  rw [hÏ†, hÎ¸, mem_setOf_eq, Formula.realize_sup, mem_union, mem_setOf_eq, mem_setOf_eq]
  -- ðŸŽ‰ no goals
#align set.definable.union Set.Definable.union

theorem definable_finset_inf {Î¹ : Type*} {f : âˆ€ _ : Î¹, Set (Î± â†’ M)} (hf : âˆ€ i, A.Definable L (f i))
    (s : Finset Î¹) : A.Definable L (s.inf f) := by
  classical
    refine' Finset.induction definable_univ (fun i s _ h => _) s
    rw [Finset.inf_insert]
    exact (hf i).inter h
#align set.definable_finset_inf Set.definable_finset_inf

theorem definable_finset_sup {Î¹ : Type*} {f : âˆ€ _ : Î¹, Set (Î± â†’ M)} (hf : âˆ€ i, A.Definable L (f i))
    (s : Finset Î¹) : A.Definable L (s.sup f) := by
  classical
    refine' Finset.induction definable_empty (fun i s _ h => _) s
    rw [Finset.sup_insert]
    exact (hf i).union h
#align set.definable_finset_sup Set.definable_finset_sup

theorem definable_finset_biInter {Î¹ : Type*} {f : âˆ€ _ : Î¹, Set (Î± â†’ M)}
    (hf : âˆ€ i, A.Definable L (f i)) (s : Finset Î¹) : A.Definable L (â‹‚ i âˆˆ s, f i) := by
  rw [â† Finset.inf_set_eq_iInter]
  -- âŠ¢ Definable A L (Finset.inf s fun i => f i)
  exact definable_finset_inf hf s
  -- ðŸŽ‰ no goals
#align set.definable_finset_bInter Set.definable_finset_biInter

theorem definable_finset_biUnion {Î¹ : Type*} {f : âˆ€ _ : Î¹, Set (Î± â†’ M)}
    (hf : âˆ€ i, A.Definable L (f i)) (s : Finset Î¹) : A.Definable L (â‹ƒ i âˆˆ s, f i) := by
  rw [â† Finset.sup_set_eq_biUnion]
  -- âŠ¢ Definable A L (Finset.sup s fun i => f i)
  exact definable_finset_sup hf s
  -- ðŸŽ‰ no goals
#align set.definable_finset_bUnion Set.definable_finset_biUnion

@[simp]
theorem Definable.compl {s : Set (Î± â†’ M)} (hf : A.Definable L s) : A.Definable L sá¶œ := by
  rcases hf with âŸ¨Ï†, hÏ†âŸ©
  -- âŠ¢ Definable A L sá¶œ
  refine' âŸ¨Ï†.not, _âŸ©
  -- âŠ¢ sá¶œ = setOf (Formula.Realize (Formula.not Ï†))
  ext v
  -- âŠ¢ v âˆˆ sá¶œ â†” v âˆˆ setOf (Formula.Realize (Formula.not Ï†))
  rw [hÏ†, compl_setOf, mem_setOf, mem_setOf, Formula.realize_not]
  -- ðŸŽ‰ no goals
#align set.definable.compl Set.Definable.compl

@[simp]
theorem Definable.sdiff {s t : Set (Î± â†’ M)} (hs : A.Definable L s) (ht : A.Definable L t) :
    A.Definable L (s \ t) :=
  hs.inter ht.compl
#align set.definable.sdiff Set.Definable.sdiff

theorem Definable.preimage_comp (f : Î± â†’ Î²) {s : Set (Î± â†’ M)} (h : A.Definable L s) :
    A.Definable L ((fun g : Î² â†’ M => g âˆ˜ f) â»Â¹' s) := by
  obtain âŸ¨Ï†, rflâŸ© := h
  -- âŠ¢ Definable A L ((fun g => g âˆ˜ f) â»Â¹' setOf (Formula.Realize Ï†))
  refine' âŸ¨Ï†.relabel f, _âŸ©
  -- âŠ¢ (fun g => g âˆ˜ f) â»Â¹' setOf (Formula.Realize Ï†) = setOf (Formula.Realize (For â€¦
  ext
  -- âŠ¢ xâœ âˆˆ (fun g => g âˆ˜ f) â»Â¹' setOf (Formula.Realize Ï†) â†” xâœ âˆˆ setOf (Formula.Re â€¦
  simp only [Set.preimage_setOf_eq, mem_setOf_eq, Formula.realize_relabel]
  -- ðŸŽ‰ no goals
#align set.definable.preimage_comp Set.Definable.preimage_comp

theorem Definable.image_comp_equiv {s : Set (Î² â†’ M)} (h : A.Definable L s) (f : Î± â‰ƒ Î²) :
    A.Definable L ((fun g : Î² â†’ M => g âˆ˜ f) '' s) := by
  refine' (congr rfl _).mp (h.preimage_comp f.symm)
  -- âŠ¢ (fun g => g âˆ˜ â†‘f.symm) â»Â¹' s = (fun g => g âˆ˜ â†‘f) '' s
  rw [image_eq_preimage_of_inverse]
  -- âŠ¢ Function.LeftInverse (fun g => g âˆ˜ â†‘f.symm) fun g => g âˆ˜ â†‘f
  Â· intro i
    -- âŠ¢ (fun g => g âˆ˜ â†‘f.symm) ((fun g => g âˆ˜ â†‘f) i) = i
    ext b
    -- âŠ¢ (fun g => g âˆ˜ â†‘f.symm) ((fun g => g âˆ˜ â†‘f) i) b = i b
    simp only [Function.comp_apply, Equiv.apply_symm_apply]
    -- ðŸŽ‰ no goals
  Â· intro i
    -- âŠ¢ (fun g => g âˆ˜ â†‘f) ((fun g => g âˆ˜ â†‘f.symm) i) = i
    ext a
    -- âŠ¢ (fun g => g âˆ˜ â†‘f) ((fun g => g âˆ˜ â†‘f.symm) i) a = i a
    simp
    -- ðŸŽ‰ no goals
#align set.definable.image_comp_equiv Set.Definable.image_comp_equiv

/-- This lemma is only intended as a helper for `Definable.image_comp`. -/
theorem Definable.image_comp_sum_inl_fin (m : â„•) {s : Set (Sum Î± (Fin m) â†’ M)}
    (h : A.Definable L s) : A.Definable L ((fun g : Sum Î± (Fin m) â†’ M => g âˆ˜ Sum.inl) '' s) := by
  obtain âŸ¨Ï†, rflâŸ© := h
  -- âŠ¢ Definable A L ((fun g => g âˆ˜ Sum.inl) '' setOf (Formula.Realize Ï†))
  refine' âŸ¨(BoundedFormula.relabel id Ï†).exs, _âŸ©
  -- âŠ¢ (fun g => g âˆ˜ Sum.inl) '' setOf (Formula.Realize Ï†) = setOf (Formula.Realize â€¦
  ext x
  -- âŠ¢ x âˆˆ (fun g => g âˆ˜ Sum.inl) '' setOf (Formula.Realize Ï†) â†” x âˆˆ setOf (Formula â€¦
  simp only [Set.mem_image, mem_setOf_eq, BoundedFormula.realize_exs,
    BoundedFormula.realize_relabel, Function.comp.right_id, Fin.castAdd_zero, Fin.castIso_refl]
  constructor
  -- âŠ¢ (âˆƒ x_1, Formula.Realize Ï† x_1 âˆ§ x_1 âˆ˜ Sum.inl = x) â†’ âˆƒ xs, BoundedFormula.Re â€¦
  Â· rintro âŸ¨y, hy, rflâŸ©
    -- âŠ¢ âˆƒ xs, BoundedFormula.Realize Ï† (Sum.elim (y âˆ˜ Sum.inl) (xs âˆ˜ Fin.cast (_ : m â€¦
    exact
      âŸ¨y âˆ˜ Sum.inr, (congr (congr rfl (Sum.elim_comp_inl_inr y).symm) (funext finZeroElim)).mp hyâŸ©
  Â· rintro âŸ¨y, hyâŸ©
    -- âŠ¢ âˆƒ x_1, Formula.Realize Ï† x_1 âˆ§ x_1 âˆ˜ Sum.inl = x
    exact âŸ¨Sum.elim x y, (congr rfl (funext finZeroElim)).mp hy, Sum.elim_comp_inl _ _âŸ©
    -- ðŸŽ‰ no goals
#align set.definable.image_comp_sum_inl_fin Set.Definable.image_comp_sum_inl_fin

/-- Shows that definability is closed under finite projections. -/
theorem Definable.image_comp_embedding {s : Set (Î² â†’ M)} (h : A.Definable L s) (f : Î± â†ª Î²)
    [Finite Î²] : A.Definable L ((fun g : Î² â†’ M => g âˆ˜ f) '' s) := by
  classical
    cases nonempty_fintype Î²
    refine'
      (congr rfl (ext fun x => _)).mp
        (((h.image_comp_equiv (Equiv.Set.sumCompl (range f))).image_comp_equiv
              (Equiv.sumCongr (Equiv.ofInjective f f.injective)
                (Fintype.equivFin (â†¥(range f)á¶œ)).symm)).image_comp_sum_inl_fin
          _)
    simp only [mem_preimage, mem_image, exists_exists_and_eq_and]
    refine' exists_congr fun y => and_congr_right fun _ => Eq.congr_left (funext fun a => _)
    simp
#align set.definable.image_comp_embedding Set.Definable.image_comp_embedding

/-- Shows that definability is closed under finite projections. -/
theorem Definable.image_comp {s : Set (Î² â†’ M)} (h : A.Definable L s) (f : Î± â†’ Î²) [Finite Î±]
    [Finite Î²] : A.Definable L ((fun g : Î² â†’ M => g âˆ˜ f) '' s) := by
  classical
    cases nonempty_fintype Î±
    cases nonempty_fintype Î²
    have h :=
      (((h.image_comp_equiv (Equiv.Set.sumCompl (range f))).image_comp_equiv
                (Equiv.sumCongr (_root_.Equiv.refl _)
                  (Fintype.equivFin _).symm)).image_comp_sum_inl_fin
            _).preimage_comp
        (rangeSplitting f)
    have h' :
      A.Definable L { x : Î± â†’ M | âˆ€ a, x a = x (rangeSplitting f (rangeFactorization f a)) } := by
      have h' : âˆ€ a,
        A.Definable L { x : Î± â†’ M | x a = x (rangeSplitting f (rangeFactorization f a)) } := by
          refine' fun a => âŸ¨(var a).equal (var (rangeSplitting f (rangeFactorization f a))), ext _âŸ©
          simp
      refine' (congr rfl (ext _)).mp (definable_finset_biInter h' Finset.univ)
      simp
    refine' (congr rfl (ext fun x => _)).mp (h.inter h')
    simp only [Equiv.coe_trans, mem_inter_iff, mem_preimage, mem_image, exists_exists_and_eq_and,
      mem_setOf_eq]
    constructor
    Â· rintro âŸ¨âŸ¨y, ys, hyâŸ©, hxâŸ©
      refine' âŸ¨y, ys, _âŸ©
      ext a
      rw [hx a, â† Function.comp_apply (f := x), â† hy]
      simp
    Â· rintro âŸ¨y, ys, rflâŸ©
      refine' âŸ¨âŸ¨y, ys, _âŸ©, fun a => _âŸ©
      Â· ext
        simp [Set.apply_rangeSplitting f]
      Â· rw [Function.comp_apply, Function.comp_apply, apply_rangeSplitting f,
          rangeFactorization_coe]
#align set.definable.image_comp Set.Definable.image_comp

variable (L A)

/-- A 1-dimensional version of `Definable`, for `Set M`. -/
def Definableâ‚ (s : Set M) : Prop :=
  A.Definable L { x : Fin 1 â†’ M | x 0 âˆˆ s }
#align set.definableâ‚ Set.Definableâ‚

/-- A 2-dimensional version of `Definable`, for `Set (M Ã— M)`. -/
def Definableâ‚‚ (s : Set (M Ã— M)) : Prop :=
  A.Definable L { x : Fin 2 â†’ M | (x 0, x 1) âˆˆ s }
#align set.definableâ‚‚ Set.Definableâ‚‚

end Set

namespace FirstOrder

namespace Language

open Set

variable (L : FirstOrder.Language.{u, v}) {M : Type w} [L.Structure M] (A : Set M) (Î± : Type uâ‚)

/-- Definable sets are subsets of finite Cartesian products of a structure such that membership is
  given by a first-order formula. -/
def DefinableSet :=
  { s : Set (Î± â†’ M) // A.Definable L s }
#align first_order.language.definable_set FirstOrder.Language.DefinableSet

namespace DefinableSet

variable {L A Î±} {s t : L.DefinableSet A Î±} {x : Î± â†’ M}

instance instSetLike : SetLike (L.DefinableSet A Î±) (Î± â†’ M) where
  coe := Subtype.val
  coe_injective' := Subtype.val_injective
#align first_order.language.definable_set.pi.set_like FirstOrder.Language.DefinableSet.instSetLike

instance instTop : Top (L.DefinableSet A Î±) :=
  âŸ¨âŸ¨âŠ¤, definable_univâŸ©âŸ©
#align first_order.language.definable_set.has_top FirstOrder.Language.DefinableSet.instTop

instance instBot : Bot (L.DefinableSet A Î±) :=
  âŸ¨âŸ¨âŠ¥, definable_emptyâŸ©âŸ©
#align first_order.language.definable_set.has_bot FirstOrder.Language.DefinableSet.instBot

instance instSup : Sup (L.DefinableSet A Î±) :=
  âŸ¨fun s t => âŸ¨s âˆª t, s.2.union t.2âŸ©âŸ©
#align first_order.language.definable_set.has_sup FirstOrder.Language.DefinableSet.instSup

instance instInf : Inf (L.DefinableSet A Î±) :=
  âŸ¨fun s t => âŸ¨s âˆ© t, s.2.inter t.2âŸ©âŸ©
#align first_order.language.definable_set.has_inf FirstOrder.Language.DefinableSet.instInf

instance instHasCompl : HasCompl (L.DefinableSet A Î±) :=
  âŸ¨fun s => âŸ¨sá¶œ, s.2.complâŸ©âŸ©
#align first_order.language.definable_set.has_compl FirstOrder.Language.DefinableSet.instHasCompl

instance instSDiff : SDiff (L.DefinableSet A Î±) :=
  âŸ¨fun s t => âŸ¨s \ t, s.2.sdiff t.2âŸ©âŸ©
#align first_order.language.definable_set.has_sdiff FirstOrder.Language.DefinableSet.instSDiff

instance instInhabited : Inhabited (L.DefinableSet A Î±) :=
  âŸ¨âŠ¥âŸ©
#align first_order.language.definable_set.inhabited FirstOrder.Language.DefinableSet.instInhabited

theorem le_iff : s â‰¤ t â†” (s : Set (Î± â†’ M)) â‰¤ (t : Set (Î± â†’ M)) :=
  Iff.rfl
#align first_order.language.definable_set.le_iff FirstOrder.Language.DefinableSet.le_iff

@[simp]
theorem mem_top : x âˆˆ (âŠ¤ : L.DefinableSet A Î±) :=
  mem_univ x
#align first_order.language.definable_set.mem_top FirstOrder.Language.DefinableSet.mem_top

@[simp]
theorem not_mem_bot {x : Î± â†’ M} : Â¬x âˆˆ (âŠ¥ : L.DefinableSet A Î±) :=
  not_mem_empty x
#align first_order.language.definable_set.not_mem_bot FirstOrder.Language.DefinableSet.not_mem_bot

@[simp]
theorem mem_sup : x âˆˆ s âŠ” t â†” x âˆˆ s âˆ¨ x âˆˆ t :=
  Iff.rfl
#align first_order.language.definable_set.mem_sup FirstOrder.Language.DefinableSet.mem_sup

@[simp]
theorem mem_inf : x âˆˆ s âŠ“ t â†” x âˆˆ s âˆ§ x âˆˆ t :=
  Iff.rfl
#align first_order.language.definable_set.mem_inf FirstOrder.Language.DefinableSet.mem_inf

@[simp]
theorem mem_compl : x âˆˆ sá¶œ â†” Â¬x âˆˆ s :=
  Iff.rfl
#align first_order.language.definable_set.mem_compl FirstOrder.Language.DefinableSet.mem_compl

@[simp]
theorem mem_sdiff : x âˆˆ s \ t â†” x âˆˆ s âˆ§ Â¬x âˆˆ t :=
  Iff.rfl
#align first_order.language.definable_set.mem_sdiff FirstOrder.Language.DefinableSet.mem_sdiff

@[simp, norm_cast]
theorem coe_top : ((âŠ¤ : L.DefinableSet A Î±) : Set (Î± â†’ M)) = univ :=
  rfl
#align first_order.language.definable_set.coe_top FirstOrder.Language.DefinableSet.coe_top

@[simp, norm_cast]
theorem coe_bot : ((âŠ¥ : L.DefinableSet A Î±) : Set (Î± â†’ M)) = âˆ… :=
  rfl
#align first_order.language.definable_set.coe_bot FirstOrder.Language.DefinableSet.coe_bot

@[simp, norm_cast]
theorem coe_sup (s t : L.DefinableSet A Î±) :
    ((s âŠ” t : L.DefinableSet A Î±) : Set (Î± â†’ M)) = (s : Set (Î± â†’ M)) âˆª (t : Set (Î± â†’ M)) :=
  rfl
#align first_order.language.definable_set.coe_sup FirstOrder.Language.DefinableSet.coe_sup

@[simp, norm_cast]
theorem coe_inf (s t : L.DefinableSet A Î±) :
    ((s âŠ“ t : L.DefinableSet A Î±) : Set (Î± â†’ M)) = (s : Set (Î± â†’ M)) âˆ© (t : Set (Î± â†’ M)) :=
  rfl
#align first_order.language.definable_set.coe_inf FirstOrder.Language.DefinableSet.coe_inf

@[simp, norm_cast]
theorem coe_compl (s : L.DefinableSet A Î±) :
    ((sá¶œ : L.DefinableSet A Î±) : Set (Î± â†’ M)) = (s : Set (Î± â†’ M))á¶œ :=
  rfl
#align first_order.language.definable_set.coe_compl FirstOrder.Language.DefinableSet.coe_compl

@[simp, norm_cast]
theorem coe_sdiff (s t : L.DefinableSet A Î±) :
    ((s \ t : L.DefinableSet A Î±) : Set (Î± â†’ M)) = (s : Set (Î± â†’ M)) \ (t : Set (Î± â†’ M)) :=
  rfl
#align first_order.language.definable_set.coe_sdiff FirstOrder.Language.DefinableSet.coe_sdiff

instance instBooleanAlgebra : BooleanAlgebra (L.DefinableSet A Î±) :=
  Function.Injective.booleanAlgebra (Î± := L.DefinableSet A Î±) _ Subtype.coe_injective
    coe_sup coe_inf coe_top coe_bot coe_compl coe_sdiff
#align first_order.language.definable_set.boolean_algebra FirstOrder.Language.DefinableSet.instBooleanAlgebra

end DefinableSet

end Language

end FirstOrder
