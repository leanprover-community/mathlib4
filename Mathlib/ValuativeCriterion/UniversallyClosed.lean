-- `Mathlib/AlgebraicGeometry/Morphisms/UniversallyClosed
import Mathlib.AlgebraicGeometry.Morphisms.UniversallyClosed
import Mathlib.AlgebraicGeometry.Morphisms.QuasiCompact
import Mathlib.ValuativeCriterion.Fiber

open CategoryTheory CategoryTheory.Limits TopologicalSpace

section TOBEMOVED

lemma _root_.SpecializingMap.comp {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [TopologicalSpace Z] {f : X â†’ Y} {g : Y â†’ Z} (hf : SpecializingMap f)
    (hg : SpecializingMap g) :
    SpecializingMap (g âˆ˜ f) := by
  rw [specializingMap_iff_closure_singleton_subset] at hf hg âŠ¢
  intro x z hz
  obtain âŸ¨y, hy, rflâŸ© := hg (f x) hz
  obtain âŸ¨x', hx', rflâŸ© := hf x hy
  use x', hx'
  simp

namespace AlgebraicGeometry

instance specializingMap_respectsIso : (topologically @SpecializingMap).RespectsIso := by
  apply topologically_respectsIso
  Â· introv
    exact f.isClosedMap.specializingMap
  Â· introv hf hg
    exact hf.comp hg

instance specializingMap_isLocalAtTarget : IsLocalAtTarget (topologically @SpecializingMap) := by
  apply topologically_isLocalAtTarget
  Â· introv _ _ hf
    rw [specializingMap_iff_closure_singleton_subset] at hf âŠ¢
    intro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© hcl
    simp only [closure_subtype, Set.restrictPreimage_mk, Set.image_singleton] at hcl
    obtain âŸ¨a, ha, hayâŸ© := hf x hcl
    rw [â† specializes_iff_mem_closure] at hcl
    exact âŸ¨âŸ¨a, by simp [hay, hy]âŸ©, by simpa [closure_subtype], by simpaâŸ©
  Â· introv hU _ hsp
    simp_rw [specializingMap_iff_closure_singleton_subset] at hsp âŠ¢
    intro x y hy
    have : âˆƒ i, y âˆˆ U i := Opens.mem_iSup.mp (hU â–¸ trivial)
    obtain âŸ¨i, hiâŸ© := this
    rw [â† specializes_iff_mem_closure] at hy
    have hfx : f x âˆˆ U i := (U i).2.stableUnderGeneralization hy hi
    have hy : (âŸ¨y, hiâŸ© : U i) âˆˆ closure {âŸ¨f x, hfxâŸ©} := by
      simp only [closure_subtype, Set.image_singleton]
      rwa [â† specializes_iff_mem_closure]
    obtain âŸ¨a, ha, hayâŸ© := hsp i âŸ¨x, hfxâŸ© hy
    rw [closure_subtype] at ha
    simp only [Opens.carrier_eq_coe, Set.image_singleton] at ha
    apply_fun Subtype.val at hay
    simp only [Opens.carrier_eq_coe, Set.restrictPreimage_coe] at hay
    use a.val, ha, hay

end AlgebraicGeometry

end TOBEMOVED

/--
move to `PrimeSpectrum/Basic`

Uses `Ideal.exists_minimalPrimes_comap_eq` and `Ideal.exists_minimalPrimes_le`

-/
lemma PrimeSpectrum.isClosed_range_of_stableUnderSpecialization
    {R S} [CommRing R] [CommRing S] (f : R â†’+* S)
    (hf : StableUnderSpecialization (Set.range (comap f))) :
    IsClosed (Set.range (comap f)) := by
  rw [isClosed_iff_zeroLocus]
  use (RingHom.ker f)
  ext p
  constructor
  Â· rintro âŸ¨q, rflâŸ©
    exact Ideal.comap_mono bot_le
  Â· intro (hp : RingHom.ker f â‰¤ p.asIdeal)
    obtain âŸ¨q, hq, hqleâŸ© := Ideal.exists_minimalPrimes_le hp
    replace hq : Minimal (fun a â†¦ a.IsPrime âˆ§ RingHom.ker f â‰¤ a) q := hq
    obtain âŸ¨q', hq', hq'câŸ© := Ideal.exists_minimalPrimes_comap_eq f q hq
    replace hq' : Minimal (fun a â†¦ a.IsPrime âˆ§ âŠ¥ â‰¤ a) q' := hq'
    apply hf ((le_iff_specializes âŸ¨q, (Minimal.prop hq).leftâŸ© p).mp hqle)
    use âŸ¨q', (Minimal.prop hq').leftâŸ©
    ext : 1
    exact hq'c

lemma PrimeSpectrum.isClosed_image_of_stableUnderSpecialization'
    {R S} [CommRing R] [CommRing S] (f : R â†’+* S)
    (A : Set (PrimeSpectrum S)) (hA : IsClosed A)
    (hf : StableUnderSpecialization (comap f '' A)) :
    IsClosed (comap f '' A) := by
  rw [isClosed_iff_zeroLocus_ideal] at hA âŠ¢
  obtain âŸ¨I, rflâŸ© := hA
  use I.comap f
  ext p
  constructor
  Â· rintro âŸ¨q, hqle, rflâŸ©
    exact Ideal.comap_mono hqle
  Â· intro (hp : I.comap f â‰¤ p.asIdeal)
    obtain âŸ¨q, hq, hqleâŸ© := Ideal.exists_minimalPrimes_le hp
    replace hq : Minimal (fun a â†¦ a.IsPrime âˆ§ I.comap f â‰¤ a) q := hq
    obtain âŸ¨q', hq', hq'câŸ© := Ideal.exists_minimalPrimes_comap_eq f q hq
    replace hq' : Minimal (fun a â†¦ a.IsPrime âˆ§ I â‰¤ a) q' := hq'
    apply hf ((le_iff_specializes âŸ¨q, (Minimal.prop hq).leftâŸ© p).mp hqle)
    refine âŸ¨âŸ¨q', (Minimal.prop hq').leftâŸ©, (Minimal.prop hq').right, ?_âŸ©
    ext : 1
    exact hq'c

namespace AlgebraicGeometry

open AlgebraicGeometry (topologically)

universe u

variable {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

lemma isClosed_image_of_stableUnderSpecialization_of_isAffine [IsAffine X] [IsAffine Y]
    (A : Set X) (hA : IsClosed A) (hf : StableUnderSpecialization (f.base '' A)) :
    IsClosed (f.base '' A) := by
  let Ï† := f.app âŠ¤
  let iX := X.isoSpec
  let iY := Y.isoSpec
  let f' : Spec Î“(X, f â»Â¹áµ âŠ¤) âŸ¶ Spec Î“(Y, âŠ¤) := Spec.map Ï†
  let A' : Set (Spec Î“(X, f â»Â¹áµ âŠ¤)) := iX.hom.base '' A
  have hA' : IsClosed A' := iX.hom.homeomorph.isClosedMap A hA
  have hf' : StableUnderSpecialization (f'.base '' A') := by
    dsimp only [A']
    rw [â† Set.image_comp]
    erw [â† TopCat.coe_comp]
    rw [â† Scheme.comp_base]
    dsimp only [iX, f', Ï†]
    rw [Scheme.isoSpec_hom_naturality, Scheme.comp_base, TopCat.coe_comp, Set.image_comp]
    exact iY.hom.homeomorph.isClosedMap.specializingMap.stableUnderSpecialization_image hf
  have res' : IsClosed (f'.base '' A') :=
    PrimeSpectrum.isClosed_image_of_stableUnderSpecialization' Ï† A' hA' hf'
  have nat : iX.hom â‰« f' â‰« iY.inv = f := by
    dsimp only [f', Ï†, iX, iY]
    simp [Scheme.isoSpec_hom_naturality_assoc]
  have : f.base '' A = iY.inv.base '' (f'.base '' A') := by
    simp only [A']
    rw [â† Set.image_comp, â† Set.image_comp, â† TopCat.coe_comp, â† Scheme.comp_base]
    erw [â† TopCat.coe_comp]
    rw [â† Scheme.comp_base, nat]
  rw [this]
  have : IsClosedMap iY.inv.base := iY.inv.homeomorph.isClosedMap
  apply this
  exact res'

lemma compactSpace_iff_exists :
    CompactSpace X â†”
      âˆƒ (R : CommRingCat.{u}) (f : Spec R âŸ¶ X), Function.Surjective f.base := by
  constructor
  Â· intro h
    let ğ’° : X.OpenCover := X.affineCover.finiteSubcover
    have (x : ğ’°.J) : IsAffine (ğ’°.obj x) := X.isAffine_affineCover _
    let R : CommRingCat := .of (Î  (x : ğ’°.J), Î“(ğ’°.obj x, âŠ¤))
    let i : Spec R â‰… âˆ (fun x : ğ’°.J â†¦ Spec Î“(ğ’°.obj x, âŠ¤)) := (asIso (sigmaSpec _)).symm
    let p : âˆ (fun x : ğ’°.J â†¦ Spec Î“(ğ’°.obj x, âŠ¤)) âŸ¶ X :=
      Sigma.desc (fun x : ğ’°.J â†¦ (ğ’°.obj x).isoSpec.inv â‰« ğ’°.map x)
    use R, i.hom â‰« p
    simp only [Scheme.comp_coeBase, TopCat.coe_comp]
    apply Function.Surjective.comp ?_ i.hom.surjective
    intro x
    let j : ğ’°.J := ğ’°.f x
    obtain âŸ¨y, hyâŸ© := ğ’°.covers x
    use (Sigma.Î¹ (fun x : ğ’°.J â†¦ Spec Î“(ğ’°.obj x, âŠ¤)) j).base ((ğ’°.obj j).isoSpec.hom.base y)
    rw [â† Scheme.comp_base_apply]
    rw [â† Scheme.comp_base_apply]
    simpa [p]
  Â· intro âŸ¨R, f, hfâŸ©
    constructor
    rw [â† hf.range_eq]
    apply isCompact_range
    exact Scheme.Hom.continuous f

/-- Best proved using #14428 -/
lemma isCompact_iff_exists {U : X.Opens} :
    IsCompact (U : Set X) â†”
      âˆƒ (R : CommRingCat.{u}) (f : Spec R âŸ¶ X), Set.range f.1.base = U := by
  constructor
  Â· intro hU
    have : CompactSpace U.toScheme := isCompact_iff_compactSpace.mp hU
    obtain âŸ¨R, f, hfâŸ© := compactSpace_iff_exists.mp this
    use R, f â‰« U.Î¹
    simp [hf.range_comp]
  Â· intro âŸ¨R, f, hfâŸ©
    have : CompactSpace U.toScheme := by
      rw [compactSpace_iff_exists]
      use R, IsOpenImmersion.lift U.Î¹ f (by simp [hf])
      rw [â† Set.range_iff_surjective]
      have : Function.Injective (U.Î¹.base '' Â·) := Set.image_val_injective
      apply this
      simp only [Set.image_univ, Scheme.Opens.range_Î¹]
      rw [â† Set.range_comp, â† TopCat.coe_comp, â† Scheme.comp_base]
      simpa
    exact isCompact_iff_compactSpace.mpr this

lemma isClosedMap_of_comp {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [TopologicalSpace Z] (f : X â†’ Y) (g : Y â†’ Z) (hf : Continuous f)
    (hfs : Function.Surjective f) (hgf : IsClosedMap (g âˆ˜ f)) :
    IsClosedMap g := by
  intro A hA
  have : g '' A = (g âˆ˜ f) '' (f â»Â¹' A) := by
    rw [Set.image_comp, Set.image_preimage_eq _ hfs]
  rw [this]
  exact hgf _ (hA.preimage hf)

/--
use `isCompact_iff_exists` to reduce to range and use
`PrimeSpectrum.isClosed_range_of_stableUnderSpecialization`.

https://stacks.math.columbia.edu/tag/01K9
-/
private lemma isClosedMap_iff_isSpecializingMap_aux [IsAffine Y] (f : X âŸ¶ Y) [QuasiCompact f] :
    IsClosedMap f.base â†” SpecializingMap f.base := by
  refine âŸ¨fun h â†¦ h.specializingMap, fun h â†¦ ?_âŸ©
  have : CompactSpace X := (quasiCompact_over_affine_iff f).mp inferInstance
  let U : Opens X := âŠ¤
  have hU : IsCompact (U : Set X) := CompactSpace.isCompact_univ
  obtain âŸ¨S, g, hgâŸ© := isCompact_iff_exists.mp hU
  intro A hA
  let A' : Set (Spec S) := g.base â»Â¹' A
  have hgs : Function.Surjective g.base := by
    rwa [â† Set.range_iff_surjective]
  have : IsClosed A' := hA.preimage (Scheme.Hom.continuous g)
  have : f.base '' A = (g â‰« f).base '' A' := by
    rw [Scheme.comp_base, TopCat.coe_comp, Set.image_comp]
    simp only [A', Set.image_preimage_eq _ hgs]
  rw [this]
  apply isClosed_image_of_stableUnderSpecialization_of_isAffine
  Â· exact hA.preimage (Scheme.Hom.continuous g)
  rw [â† this]
  exact h.stableUnderSpecialization_image hA.stableUnderSpecialization

/--
use `isClosedMap_iff_isSpecializingMap_aux` and the fact that both sides are local at target.
(maybe postpone this until refactor lands)

https://stacks.math.columbia.edu/tag/01K9
-/
lemma isClosedMap_iff_specializingMap [QuasiCompact f] :
    IsClosedMap f.1.base â†” SpecializingMap f.1.base := by
  constructor
  Â· intro hf
    exact hf.specializingMap
  intro hf
  wlog h : IsAffine Y generalizing X Y
  Â· show topologically @IsClosedMap f
    rw [IsLocalAtTarget.iff_of_iSup_eq_top (P := topologically @IsClosedMap) _
      (iSup_affineOpens_eq_top _)]
    intro U
    haveI hqc : QuasiCompact (f âˆ£_ U) := IsLocalAtTarget.restrict â€¹QuasiCompact fâ€º U
    apply this (f âˆ£_ U)
    Â· exact IsLocalAtTarget.restrict (P := topologically @SpecializingMap) hf U
    exact U.2
  rwa [isClosedMap_iff_isSpecializingMap_aux]

/-
use `isClosedMap_iff_specializingMap`
-/
lemma universallyClosed_iff_specializingMap [QuasiCompact f] :
    UniversallyClosed f â†” (topologically @SpecializingMap).universally f := by
  rw [universallyClosed_eq]
  constructor
  Â· intro h Z W g iâ‚‚ f' hp
    have : QuasiCompact f' := MorphismProperty.of_isPullback hp.flip inferInstance
    have hcl : IsClosedMap f'.base := h g iâ‚‚ f' hp
    rwa [isClosedMap_iff_specializingMap] at hcl
  Â· intro h Z W g iâ‚‚ f' hp
    have : QuasiCompact f' := MorphismProperty.of_isPullback hp.flip inferInstance
    have hcl : SpecializingMap f'.base := h g iâ‚‚ f' hp
    rwa [â† isClosedMap_iff_specializingMap] at hcl

lemma _root_.AlgebraicGeometry.Scheme.Î“SpecIso_inv_naturality_apply
    {R S : CommRingCat} (f : R âŸ¶ S) (r : R) :
    (Scheme.Î“SpecIso S).inv (f r) = ((Spec.map f).app âŠ¤) ((Scheme.Î“SpecIso R).inv r) := by
  show (f â‰« (Scheme.Î“SpecIso S).inv) r = _
  simp

@[simp]
lemma _root_.CommRingCat.of_apply {R S : Type u} [CommRing R] [CommRing S] (f : R â†’+* S)
    (r : R) :
    CommRingCat.ofHom f r = f r :=
  rfl

instance {K} [Field K] : Unique (Spec (.of K)) :=
  inferInstanceAs <| Unique (PrimeSpectrum K)

@[simp]
lemma default_asIdeal {K} [Field K] : (default : Spec (.of K)).asIdeal = âŠ¥ := rfl

lemma Spec_mem_basicOpen {R : CommRingCat} (f : Î“(Spec R, âŠ¤)) (x : Spec R) :
    x âˆˆ (Spec R).basicOpen f â†” (Scheme.Î“SpecIso R).hom f âˆ‰ x.asIdeal := by
  simp
  rfl

lemma Spec_map_base {R S : CommRingCat} (f : R âŸ¶ S) (x : Spec S) :
    (Spec.map f).base x = PrimeSpectrum.comap f x :=
  rfl

lemma aeval_ite_mem_vars {Ïƒ R : Type*} [CommRing R] [DecidableEq Ïƒ] (q : MvPolynomial Ïƒ R) :
    MvPolynomial.aeval (fun i â†¦ if i âˆˆ q.vars then .X i else 0) q = q := by
  rw [MvPolynomial.as_sum q, MvPolynomial.aeval_sum]
  refine Finset.sum_congr rfl (fun u hu â†¦ ?_)
  rw [MvPolynomial.aeval_monomial, MvPolynomial.monomial_eq]
  congr 1
  refine Finsupp.prod_congr (fun i hi â†¦ ?_)
  have : i âˆˆ q.vars := by
    rw [MvPolynomial.mem_vars]
    use u
  simp [this]

@[simp]
lemma CommRingCat.hom_inv_apply {R S : CommRingCat} (e : R â‰… S) (r : R) :
    e.inv (e.hom r) = r := by
  show (e.hom â‰« e.inv) r = r
  simp

@[simp]
lemma CommRingCat.inv_hom_apply {R S : CommRingCat} (e : R â‰… S) (s : S) :
    e.hom (e.inv s) = s := by
  show (e.inv â‰« e.hom) s = s
  simp

/--
For a (formalizable) proof, see https://imgur.com/a/nTDzDFj.

inspired by
https://mathoverflow.net/questions/23337/is-a-universally-closed-morphism-of-schemes-quasi-compact/23528#23528
-/
lemma compactSpace_of_universallyClosed
    {K} [Field K] (f : X âŸ¶ Spec (.of K)) [UniversallyClosed f] : CompactSpace X := by
  classical
  let ğ’° : X.OpenCover := X.affineCover
  let U (i : ğ’°.J) : X.Opens := ğ’°.map i ''áµ âŠ¤
  let T : Scheme := Spec (.of <| MvPolynomial ğ’°.J K)
  let q : T âŸ¶ Spec (.of K) := Spec.map MvPolynomial.C
  let R : CommRingCat := .of <| MvPolynomial ğ’°.J K
  let Ti (i : ğ’°.J) : T.Opens := T.basicOpen ((Scheme.Î“SpecIso R).inv <| MvPolynomial.X (R := K) i)
  let fT : pullback f q âŸ¶ T := pullback.snd f q
  let p : pullback f q âŸ¶ X := pullback.fst f q
  let V (i : ğ’°.J) : (pullback f q).Opens := fT â»Â¹áµ (Ti i) âŠ“ p â»Â¹áµ (U i)
  let Z : Set (pullback f q).carrier := (iSup V).carrierá¶œ
  have hZ : IsClosed Z := by
    rw [isClosed_compl_iff]
    simp only [Opens.carrier_eq_coe, Opens.coe_iSup, Opens.coe_inf, Opens.map_coe]
    apply isOpen_iUnion
    intro i
    apply IsOpen.inter
    exact (Ti i).2.preimage (Scheme.Hom.continuous fT)
    exact (U i).2.preimage (Scheme.Hom.continuous p)
  have hfT : IsClosedMap fT.base :=
    UniversallyClosed.out p q fT (IsPullback.of_hasPullback f q).flip
  have hfZ : IsClosed (fT.base '' Z) := hfT _ hZ
  let Zc : Opens T := âŸ¨(fT.base '' Z)á¶œ, hfZ.isOpen_complâŸ©
  let Ïˆ : MvPolynomial ğ’°.J K â†’â‚[K] K := MvPolynomial.aeval (fun _ â†¦ 1)
  let h : Spec (.of K) âŸ¶ T := Spec.map Ïˆ.toRingHom
  let t : T := h.base default
  have ht (i : ğ’°.J) : t âˆˆ Ti i := by
    simp only [Ti, t]
    rw [Spec_mem_basicOpen]
    simp only [R]
    erw [CommRingCat.inv_hom_apply]
    rw [Spec_map_base]
    simp [Ïˆ]
  have : t âˆ‰ fT.base '' Z := by
    intro âŸ¨z, hz, hztâŸ©
    apply hz
    simp only [Opens.carrier_eq_coe, Opens.coe_iSup, Opens.coe_inf, Opens.map_coe, Set.mem_iUnion,
      Set.mem_inter_iff, Set.mem_preimage, SetLike.mem_coe]
    refine âŸ¨ğ’°.f (p.base z), ?_, ?_âŸ©
    Â· rw [hzt]
      apply ht
    Â· simpa [U] using ğ’°.covers (p.base z)
  have htZc : t âˆˆ Zc := this
  obtain âŸ¨U', âŸ¨g, rflâŸ©, htU', hU'leâŸ© :=
    Opens.isBasis_iff_nbhd.mp (AlgebraicGeometry.isBasis_basicOpen T) htZc
  let Ïƒ : Finset ğ’°.J := MvPolynomial.vars ((Scheme.Î“SpecIso R).hom g)
  let Ï† : MvPolynomial ğ’°.J K â†’+* MvPolynomial ğ’°.J K :=
    (MvPolynomial.aeval (fun i : ğ’°.J â†¦ if i âˆˆ Ïƒ then MvPolynomial.X i else 0)).toRingHom
  let t' : T := (Spec.map Ï†).base t
  have ht'g : t' âˆˆ T.basicOpen g := by
    simp only [t']
    rw [Spec_mem_basicOpen, Spec_map_base]
    have : Ï† ((Scheme.Î“SpecIso R).hom g) = (Scheme.Î“SpecIso R).hom g :=
      aeval_ite_mem_vars ((Scheme.Î“SpecIso R).hom g)
    simp only [CommRingCat.coe_of, PrimeSpectrum.comap_asIdeal, Ideal.mem_comap, this]
    erw [â† Spec_mem_basicOpen g t]
    exact htU'
  have ht'Zc : t' âˆ‰ fT.base '' Z := hU'le ht'g
  have hÏƒ : â‹ƒ i âˆˆ Ïƒ, (U i).1 = Set.univ := by
    by_contra h
    apply ht'Zc
    rw [Set.iUnion_eq_univ_iff] at h
    simp only [Opens.carrier_eq_coe, Set.mem_iUnion, SetLike.mem_coe, exists_prop, not_forall,
      not_exists, not_and] at h
    obtain âŸ¨x, hxâŸ© := h
    have tri : f.base x = q.base t' := Subsingleton.elim _ _
    obtain âŸ¨z, hzl, hzrâŸ© := Scheme.Pullback.exists_preimage_pullback x t' tri
    refine âŸ¨z, ?_, hzrâŸ©
    simp only [Opens.carrier_eq_coe, Opens.coe_iSup, Opens.coe_inf, Opens.map_coe, Set.compl_iUnion,
      Set.mem_iInter, Set.mem_compl_iff, Set.mem_inter_iff, Set.mem_preimage, SetLike.mem_coe, Z,
      not_and_or]
    intro i
    rw [hzl, hzr]
    by_cases h : i âˆˆ Ïƒ
    Â· exact Or.inr (hx i h)
    Â· apply Or.inl
      simp only [t']
      convert_to t âˆ‰ (Spec.map Ï†) â»Â¹áµ (Ti i)
      simp only [Ti]
      have hÏ†i : (CommRingCat.ofHom Ï†) (MvPolynomial.X i) = 0 := by
        simpa [Ï†]
      have : ((Spec.map <| CommRingCat.ofHom Ï†).app âŠ¤)
          ((Scheme.Î“SpecIso R).inv (MvPolynomial.X i)) = 0 := by
        erw [â† Scheme.Î“SpecIso_inv_naturality_apply]
        erw [hÏ†i]
        simp
      simp only [Scheme.preimage_basicOpen]
      erw [this]
      simp only [Opens.map_top, Scheme.basicOpen_zero]
      intro a
      exact a
  constructor
  rw [â† hÏƒ]
  apply Finset.isCompact_biUnion
  rintro i -
  simp only [Scheme.Hom.image_top_eq_opensRange, Opens.carrier_eq_coe, Scheme.Hom.opensRange_coe, U]
  exact isCompact_range (Scheme.Hom.continuous _)

/--
Use `compactSpace_of_universallyClosed` and `universallyClosed_stableUnderBaseChange` and
`Scheme.Hom.range_fiberÎ¹` and `isProperMap_iff_isClosedMap_and_compact_fibers`
-/
lemma isProperMap_of_universallyClosed [UniversallyClosed f] : IsProperMap f.base := by
  rw [isProperMap_iff_isClosedMap_and_compact_fibers]
  refine âŸ¨Scheme.Hom.continuous f, ?_, ?_âŸ©
  Â· exact MorphismProperty.universally_le (P := topologically @IsClosedMap) _ UniversallyClosed.out
  Â· intro y
    have : UniversallyClosed (f.fiberToResidueField y) :=
      MorphismProperty.of_isPullback (f.fiber_isPullback y) inferInstance
    have : CompactSpace (f.fiber y) := compactSpace_of_universallyClosed (f.fiberToResidueField y)
    rw [â† Scheme.Hom.range_fiberÎ¹]
    exact isCompact_range (Scheme.Hom.continuous _)

/--
Use `IsProperMap.isCompact_preimage`.
This holds for any map between topological spaces. Maybe generalize.
-/
instance (priority := 900) [UniversallyClosed f] : QuasiCompact f where
  isCompact_preimage _ _ hUc :=
    IsProperMap.isCompact_preimage (isProperMap_of_universallyClosed f) hUc

/-- needs the instance above and `universallyClosed_iff_specializingMap`. -/
lemma universallyClosed_eq_universallySpecializing :
    @UniversallyClosed = (topologically @SpecializingMap).universally âŠ“ @QuasiCompact := by
  ext X Y f
  constructor
  Â· intro hf
    exact âŸ¨(universallyClosed_iff_specializingMap f).mp hf, inferInstanceâŸ©
  Â· intro âŸ¨hf, _âŸ©
    exact (universallyClosed_iff_specializingMap f).mpr hf

end AlgebraicGeometry
