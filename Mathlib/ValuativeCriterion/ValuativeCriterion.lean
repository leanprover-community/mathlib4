-- `Mathlib/AlgebraicGeometry/Morphisms/ValuativeCriterion
import Mathlib.ValuativeCriterion.Immersion
import Mathlib.RingTheory.Valuation.ValuationRing
import Mathlib.ValuativeCriterion.ValuationRing
import Mathlib.ValuativeCriterion.Lemmas
import Mathlib.ValuativeCriterion.UniversallyClosed

open CategoryTheory CategoryTheory.Limits

namespace AlgebraicGeometry

universe u

structure ValuativeCommSq {X Y : Scheme.{u}} (f : X ‚ü∂ Y) where
  R : Type u
  [commRing : CommRing R]
  [domain : IsDomain R]
  [valuationRing : ValuationRing R]
  K : Type u
  [field : Field K]
  [algebra : Algebra R K]
  [isFractionRing : IsFractionRing R K]
  (i‚ÇÅ : Spec (.of K) ‚ü∂ X)
  (i‚ÇÇ : Spec (.of R) ‚ü∂ Y)
  (commSq : CommSq i‚ÇÅ (Spec.map (CommRingCat.ofHom (algebraMap R K))) f i‚ÇÇ)

namespace ValuativeCommSq

attribute [instance] commRing domain valuationRing field algebra isFractionRing

end ValuativeCommSq

def ValuativeCriterion.Existence : MorphismProperty Scheme :=
  fun _ _ f ‚Ü¶ ‚àÄ S : ValuativeCommSq f, S.commSq.HasLift

def ValuativeCriterion.Uniqueness : MorphismProperty Scheme :=
  fun _ _ f ‚Ü¶ ‚àÄ S : ValuativeCommSq f, Subsingleton S.commSq.LiftStruct

def ValuativeCriterion : MorphismProperty Scheme :=
  fun _ _ f ‚Ü¶ ‚àÄ S : ValuativeCommSq f, Nonempty (Unique (S.commSq.LiftStruct))

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

namespace Existence

open LocalRing in
/--
Uses `exists_factor_valuationRing` and `Scheme.fromSpecResidueField`.

https://stacks.math.columbia.edu/tag/01KE
-/
lemma specializingMap (H : ValuativeCriterion.Existence f) :
    SpecializingMap f.1.base := by
  intro x' y h
  let stalk_y_to_residue_x' : Y.presheaf.stalk y ‚ü∂ X.residueField x' :=
    Y.presheaf.stalkSpecializes h ‚â´ f.stalkMap x' ‚â´ X.residue x'
  obtain ‚ü®A, hA, hA_local‚ü© := exists_factor_valuationRing stalk_y_to_residue_x'
  let stalk_y_to_A : Y.presheaf.stalk y ‚ü∂ .of A :=
    CommRingCat.ofHom (stalk_y_to_residue_x'.codRestrict _ hA)
  have w : X.fromSpecResidueField x' ‚â´ f =
      Spec.map (CommRingCat.ofHom (algebraMap A (X.residueField x'))) ‚â´
        Spec.map stalk_y_to_A ‚â´ Y.fromSpecStalk y := by
    rw [Scheme.fromSpecResidueField, Category.assoc, ‚Üê Scheme.Spec_map_stalkMap_fromSpecStalk,
      ‚Üê Scheme.Spec_map_stalkSpecializes_fromSpecStalk h]
    simp_rw [‚Üê Spec.map_comp_assoc]
    rfl
  obtain ‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü© := (H { R := A, K := X.residueField x', commSq := ‚ü®w‚ü© }).exists_lift
  dsimp only at hl‚ÇÅ hl‚ÇÇ
  refine ‚ü®l.base (closedPoint A), ?_, ?_‚ü©
  ¬∑ simp_rw [‚Üê Scheme.fromSpecResidueField_apply x' (closedPoint (X.residueField x')), ‚Üê hl‚ÇÅ]
    exact (specializes_closedPoint _).map l.base.2
  ¬∑ rw [‚Üê Scheme.comp_base_apply, hl‚ÇÇ]
    simp only [Scheme.comp_coeBase, TopCat.coe_comp, Function.comp_apply]
    have : (Spec.map stalk_y_to_A).base (closedPoint A) = closedPoint (Y.presheaf.stalk y) :=
      comap_closedPoint (S := A) (stalk_y_to_residue_x'.codRestrict A.toSubring hA)
    rw [this, Y.fromSpecStalk_closedPoint]

open LocalRing in
/--
Uses `bijective_rangeRestrict_comp_of_valuationRing` and `stalkClosedPointTo`

https://stacks.math.columbia.edu/tag/01KE first half
-/
lemma of_specializingMap (H : (topologically @SpecializingMap).universally f) :
    ValuativeCriterion.Existence f := by
  rintro ‚ü®R, K, i‚ÇÅ, i‚ÇÇ, ‚ü®w‚ü©‚ü©
  haveI : IsDomain (CommRingCat.of R) := ‚Äπ_‚Ä∫
  haveI : ValuationRing (CommRingCat.of R) := ‚Äπ_‚Ä∫
  letI : Field (CommRingCat.of K) := ‚Äπ_‚Ä∫
  replace H := H (pullback.snd i‚ÇÇ f) i‚ÇÇ (pullback.fst i‚ÇÇ f) (.of_hasPullback i‚ÇÇ f)
  let lft := pullback.lift (Spec.map (CommRingCat.ofHom (algebraMap R K))) i‚ÇÅ w.symm
  obtain ‚ü®x, h‚ÇÅ, h‚ÇÇ‚ü© := @H (lft.base (closedPoint _)) _ (specializes_closedPoint (R := R) _)
  let e : CommRingCat.of R ‚âÖ (Spec (.of R)).presheaf.stalk ((pullback.fst i‚ÇÇ f).base x) :=
    (stalkClosedPointIso (.of R)).symm ‚â™‚â´
      (Spec (.of R)).presheaf.stalkCongr (.of_eq h‚ÇÇ.symm)
  let Œ± := e.hom ‚â´ (pullback.fst i‚ÇÇ f).stalkMap x
  have : IsLocalHom Œ± := inferInstance
  let Œ≤ := (pullback i‚ÇÇ f).presheaf.stalkSpecializes h‚ÇÅ ‚â´ Scheme.stalkClosedPointTo lft
  have hŒ±Œ≤ : Œ± ‚â´ Œ≤ = CommRingCat.ofHom (algebraMap R K) := by
    simp only [CommRingCat.coe_of, Iso.trans_hom, Iso.symm_hom, TopCat.Presheaf.stalkCongr_hom,
      Category.assoc, Œ±, e, Œ≤, stalkClosedPointIso_inv, StructureSheaf.toStalk]
    show (Scheme.ŒìSpecIso (.of R)).inv ‚â´ (Spec (.of R)).presheaf.germ _ _ _ ‚â´ _ = _
    simp only [TopCat.Presheaf.germ_stalkSpecializes_assoc, Scheme.stalkMap_germ_assoc,
      TopologicalSpace.Opens.map_top]
    erw [Scheme.germ_stalkClosedPointTo lft ‚ä§ trivial,
      ‚Üê Scheme.comp_app_assoc lft (pullback.fst i‚ÇÇ f)]
    rw [pullback.lift_fst]
    simp
  have hbij := (bijective_rangeRestrict_comp_of_valuationRing (R := R) (K := K) Œ± Œ≤ hŒ±Œ≤)
  let œÜ : (pullback i‚ÇÇ f).presheaf.stalk x ‚ü∂ CommRingCat.of R :=
    (RingEquiv.ofBijective _ hbij).symm.toRingHom.comp Œ≤.rangeRestrict
  have hŒ±œÜ : Œ± ‚â´ œÜ = ùüô _ := by ext x; exact (RingEquiv.ofBijective _ hbij).symm_apply_apply x
  have hŒ±œÜ' : (pullback.fst i‚ÇÇ f).stalkMap x ‚â´ œÜ = e.inv := by
    rw [‚Üê cancel_epi e.hom, ‚Üê Category.assoc, hŒ±œÜ, e.hom_inv_id]
  have hœÜŒ≤ : œÜ ‚â´ CommRingCat.ofHom (algebraMap R K) = Œ≤ :=
    hŒ±Œ≤ ‚ñ∏ RingHom.ext fun x ‚Ü¶ congr_arg Subtype.val
      ((RingEquiv.ofBijective _ hbij).apply_symm_apply (Œ≤.rangeRestrict x))
  refine ‚ü®‚ü®‚ü®Spec.map ((pullback.snd i‚ÇÇ f).stalkMap x ‚â´ œÜ) ‚â´ X.fromSpecStalk _, ?_, ?_‚ü©‚ü©‚ü©
  ¬∑ simp only [‚Üê Spec.map_comp_assoc, Category.assoc, hœÜŒ≤]
    simp [Œ≤, Scheme.Spec_map_stalkSpecializes_fromSpecStalk_assoc, -CommRingCat.coe_of,
      Scheme.Spec_stalkClosedPointTo_fromSpecStalk_assoc, lft]
  ¬∑ simp only [Spec.map_comp, Category.assoc, Scheme.Spec_map_stalkMap_fromSpecStalk,
      ‚Üê pullback.condition]
    rw [‚Üê Scheme.Spec_map_stalkMap_fromSpecStalk_assoc, ‚Üê Spec.map_comp_assoc, hŒ±œÜ']
    simp only [Iso.trans_inv, TopCat.Presheaf.stalkCongr_inv, Iso.symm_inv, Spec.map_comp,
      Category.assoc, Scheme.Spec_map_stalkSpecializes_fromSpecStalk_assoc, e]
    rw [‚Üê Spec_stalkClosedPointIso, ‚Üê Spec.map_comp_assoc,
      Iso.inv_hom_id, Spec.map_id, Category.id_comp]

/-- by def -/
lemma stableUnderBaseChange :
    ValuativeCriterion.Existence.StableUnderBaseChange := by
  intros Y' X X' Y  Y'_to_Y f X'_to_X f' hP hf commSq

  let commSq' : ValuativeCommSq f := {
    R := commSq.R
    commRing := by infer_instance
    domain := by infer_instance
    valuationRing := by infer_instance
    K := commSq.K
    field := by infer_instance
    algebra := by infer_instance
    isFractionRing := by infer_instance
    i‚ÇÅ := commSq.i‚ÇÅ ‚â´ X'_to_X
    i‚ÇÇ := commSq.i‚ÇÇ ‚â´ Y'_to_Y
    commSq := {
      w := by
        simp only [Category.assoc]
        rw [hP.w]
        rw [reassoc_of% commSq.commSq.w]
    }
  }

  let lift := (hf commSq').exists_lift.some
  have lift_comm‚ÇÅ := lift.fac_left
  have lift_comm‚ÇÇ := lift.fac_right

  have comm‚ÇÅ : lift.l ‚â´ f = commSq.i‚ÇÇ ‚â´ Y'_to_Y := by
    simp_all only [commSq', lift]

  let l := hP.lift lift.l commSq.i‚ÇÇ comm‚ÇÅ
  have fac_left :
      Spec.map (CommRingCat.ofHom (algebraMap commSq.R commSq.K)) ‚â´ l = commSq.i‚ÇÅ := by
    apply hP.hom_ext
    ¬∑ simpa [l]
    ¬∑ simp only [Category.assoc]
      rw [hP.lift_snd]
      rw [commSq.commSq.w]
  have fac_right : l ‚â´ f' = commSq.i‚ÇÇ := hP.lift_snd _ _ _

  exact ‚ü®‚ü®‚ü®l, fac_left, fac_right‚ü©‚ü©‚ü©

/-- by the three lemmas above -/
lemma eq : ValuativeCriterion.Existence =
    (AlgebraicGeometry.topologically @SpecializingMap).universally := by
  ext
  constructor
  ¬∑ intro _
    apply MorphismProperty.universally_mono
    ¬∑ apply specializingMap
    ¬∑ rwa [MorphismProperty.StableUnderBaseChange.universally_eq stableUnderBaseChange]
  ¬∑ apply of_specializingMap

/-- by `ValuativeCriterion.Existence.eq` and `universallyClosed_iff_specializingMap`. -/
lemma _root_.AlgebraicGeometry.universallyClosed_of_valuativeCriterion [QuasiCompact f]
    (hf : ValuativeCriterion.Existence f) : UniversallyClosed f := by
  rw [eq] at hf
  apply (AlgebraicGeometry.universallyClosed_iff_specializingMap f).mpr
  exact hf

/-- by `ValuativeCriterion.Existence.eq` and `universallyClosed_eq_universallySpecializing`. -/
lemma _root_.AlgebraicGeometry.universallyClosed_eq_valuativeCriterion :
    @UniversallyClosed = ValuativeCriterion.Existence ‚äì @QuasiCompact := by
  ext X Y f
  constructor
  ¬∑ intro hf
    have h‚ÇÅ : ValuativeCriterion.Existence f := by
      apply of_specializingMap
      rwa [‚Üê AlgebraicGeometry.universallyClosed_iff_specializingMap]
    have h‚ÇÇ : QuasiCompact f := by infer_instance
    exact ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  ¬∑ intro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    rw [AlgebraicGeometry.universallyClosed_eq_universallySpecializing]
    have : (topologically @SpecializingMap).universally f := by
      rwa [eq] at h‚ÇÅ
    exact ‚ü®this, h‚ÇÇ‚ü©

end Existence

section Uniqueness

/--
Needs `IsImmersion (pullback.diagonal f)`, `IsClosedImmersion.of_isImmersion`,
`universallyClosed_of_valuativeCriterion`.

https://stacks.math.columbia.edu/tag/01L0
-/
lemma isSeparated_of_valuativeCriterion [QuasiSeparated f]
    (hf : ValuativeCriterion.Uniqueness f) : IsSeparated f where
  diagonal_isClosedImmersion := by
    suffices h : ValuativeCriterion.Existence (pullback.diagonal f) by
      have : QuasiCompact (pullback.diagonal f) :=
        AlgebraicGeometry.QuasiSeparated.diagonalQuasiCompact
      apply IsClosedImmersion.of_isPreimmersion
      apply IsClosedMap.isClosed_range
      apply (topologically @IsClosedMap).universally_le
      exact (universallyClosed_of_valuativeCriterion (pullback.diagonal f) h).out
    intro S
    have hc : CommSq S.i‚ÇÅ (Spec.map (CommRingCat.ofHom (algebraMap S.R S.K)))
        f (S.i‚ÇÇ ‚â´ pullback.fst f f ‚â´ f) := ‚ü®by simp [‚Üê S.commSq.w_assoc]‚ü©
    let S' : ValuativeCommSq f := ‚ü®S.R, S.K, S.i‚ÇÅ, S.i‚ÇÇ ‚â´ pullback.fst f f ‚â´ f, hc‚ü©
    have : Subsingleton S'.commSq.LiftStruct := hf S'
    let S'l‚ÇÅ : S'.commSq.LiftStruct := ‚ü®S.i‚ÇÇ ‚â´ pullback.fst f f,
      by simp [‚Üê S.commSq.w_assoc], by simp‚ü©
    let S'l‚ÇÇ : S'.commSq.LiftStruct := ‚ü®S.i‚ÇÇ ‚â´ pullback.snd f f,
      by simp [‚Üê S.commSq.w_assoc], by simp [pullback.condition]‚ü©
    have h‚ÇÅ‚ÇÇ : S'l‚ÇÅ = S'l‚ÇÇ := Subsingleton.elim _ _
    constructor
    constructor
    refine ‚ü®S.i‚ÇÇ ‚â´ pullback.fst _ _, ?_, ?_‚ü©
    ¬∑ simp [‚Üê S.commSq.w_assoc]
    ¬∑ simp
      apply IsPullback.hom_ext (IsPullback.of_hasPullback _ _)
      ¬∑ simp
      ¬∑ simp only [Category.assoc, pullback.diagonal_snd, Category.comp_id]
        exact congrArg CommSq.LiftStruct.l h‚ÇÅ‚ÇÇ

/--
https://stacks.math.columbia.edu/tag/01KZ
-/
lemma IsSeparated.valuativeCriterion [IsSeparated f] :
    ValuativeCriterion.Uniqueness f := by
  intros S
  constructor
  rintro ‚ü®l‚ÇÅ, hl‚ÇÅ, hl‚ÇÅ'‚ü© ‚ü®l‚ÇÇ, hl‚ÇÇ, hl‚ÇÇ'‚ü©
  ext : 1
  dsimp at *
  have h := hl‚ÇÅ'.trans hl‚ÇÇ'.symm
  let Z := pullback (pullback.diagonal f) (pullback.lift l‚ÇÅ l‚ÇÇ h)
  let g : Z ‚ü∂ Spec (.of S.R) := pullback.snd _ _
  have : IsClosedImmersion g := IsClosedImmersion.stableUnderBaseChange.snd _ _ inferInstance
  have hZ : IsAffine Z := by
    rw [@HasAffineProperty.iff_of_isAffine @IsClosedImmersion] at this
    exact this.left
  suffices IsIso g by
    rw [‚Üê cancel_epi g]
    conv_lhs => rw [‚Üê pullback.lift_fst l‚ÇÅ l‚ÇÇ h, ‚Üê pullback.condition_assoc]
    conv_rhs => rw [‚Üê pullback.lift_snd l‚ÇÅ l‚ÇÇ h, ‚Üê pullback.condition_assoc]
    simp
  suffices h : Function.Bijective (g.app ‚ä§) by
    refine (HasAffineProperty.iff_of_isAffine (P := MorphismProperty.isomorphisms Scheme)).mpr ?_
    exact ‚ü®hZ, (ConcreteCategory.isIso_iff_bijective _).mpr h‚ü©
  constructor
  ¬∑ let l : Spec (.of S.K) ‚ü∂ Z := by
      apply pullback.lift S.i‚ÇÅ (Spec.map (CommRingCat.ofHom (algebraMap S.R S.K)))
      apply IsPullback.hom_ext (IsPullback.of_hasPullback _ _)
      simpa using hl‚ÇÅ.symm
      simpa using hl‚ÇÇ.symm
    have hg : l ‚â´ g = Spec.map (CommRingCat.ofHom (algebraMap S.R S.K)) :=
      pullback.lift_snd _ _ _
    have : Function.Injective ((l ‚â´ g).app ‚ä§) := by
      rw [hg]
      let e := arrowIsoŒìSpecOfIsAffine (CommRingCat.ofHom <| algebraMap S.R S.K)
      let P : MorphismProperty CommRingCat :=
        RingHom.toMorphismProperty <| fun f ‚Ü¶ Function.Injective f
      have : (RingHom.toMorphismProperty <| fun f ‚Ü¶ Function.Injective f).RespectsIso :=
        RingHom.toMorphismProperty_respectsIso_iff.mp RingHom.injective_respectsIso
      show P _
      rw [‚Üê MorphismProperty.arrow_mk_iso_iff (P := P) e]
      exact NoZeroSMulDivisors.algebraMap_injective S.R S.K
    rw [Scheme.comp_app _ _] at this
    exact Function.Injective.of_comp this
  ¬∑ rw [@HasAffineProperty.iff_of_isAffine @IsClosedImmersion] at this
    exact this.right

-- by lemmas above
lemma IsSeparated_eq_valuativeCriterion :
    @IsSeparated = ValuativeCriterion.Uniqueness ‚äì @QuasiSeparated := by
  ext X Y f
  constructor
  ¬∑ intro hf
    exact ‚ü®IsSeparated.valuativeCriterion f, inferInstance‚ü©
  ¬∑ intro ‚ü®hu, _‚ü©
    exact isSeparated_of_valuativeCriterion f hu

end Uniqueness

-- by definition
lemma valuativeCriterion_eq :
    ValuativeCriterion = ValuativeCriterion.Existence ‚äì ValuativeCriterion.Uniqueness := by
  ext X Y f
  constructor
  ¬∑ intro hf
    refine ‚ü®fun S ‚Ü¶ ?_, fun S ‚Ü¶ ?_‚ü©
    ¬∑ obtain ‚ü®(u : Unique (S.commSq.LiftStruct))‚ü© := hf S
      exact CommSq.HasLift.mk' default
    ¬∑ obtain ‚ü®(u : Unique (S.commSq.LiftStruct))‚ü© := hf S
      infer_instance
  ¬∑ intro ‚ü®he, hu‚ü© S
    rw [unique_iff_subsingleton_and_nonempty]
    exact ‚ü®hu S, (he S).1‚ü©

-- by lemmas above and `isProper_eq`
lemma proper_eq_ValuativeCriterion :
    @IsProper = ValuativeCriterion ‚äì @QuasiCompact ‚äì @QuasiSeparated ‚äì @LocallyOfFiniteType := by
  rw [isProper_eq, IsSeparated_eq_valuativeCriterion, valuativeCriterion_eq,
    universallyClosed_eq_valuativeCriterion]
  rw [‚Üê inf_assoc]
  ext X Y f
  constructor
  ¬∑ intro ‚ü®‚ü®‚ü®‚ü®h0, h1‚ü©, h2‚ü©, h3‚ü©, h4‚ü©
    exact ‚ü®‚ü®‚ü®‚ü®h2, h0‚ü©, h3‚ü©, h1‚ü©, h4‚ü©
  ¬∑ intro ‚ü®‚ü®‚ü®‚ü®h2, h0‚ü©, h3‚ü©, h1‚ü©, h4‚ü©
    exact ‚ü®‚ü®‚ü®‚ü®h0, h1‚ü©, h2‚ü©, h3‚ü©, h4‚ü©

end AlgebraicGeometry
