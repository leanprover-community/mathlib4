/-
Copyright (c) 2024 Shogo Saito. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Shogo Saito. Adapted for mathlib by Hunter Monroe
-/
import Mathlib.ProofTheory.Semantics

/-!
# Basic definitions and properties of proof-related notions

This file defines a characterization of the proof/provability/calculus of formulas.
It also defines soundness and completeness.

## Main Definitions
* `Proof`: Proof system of logic.
* `Sound`: Soundness of the proof system.
* `Complete`: Completeness of the proof system.

-/

namespace ProofTheory

universe u

variable {F : Type*} [LogicalConnective F]

/-- Deduction Proof -/
class Proof (F : Type*) where
  /-- Proof of formula by a theory (a set of formulas) -/
  Prf : Set F â†’ F â†’ Type u
  /-- A formula is in a theory if proved by it -/
  axm : âˆ€ {f T}, f âˆˆ T â†’ Prf T f
  /-- A theory is a weakening of another theory if it proves a subset of its formulas -/
  weakening' : âˆ€ {T U f}, T âŠ† U â†’ Prf T f â†’ Prf U f

namespace Proof

variable [ð“‘ : Proof F]

instance : Turnstile F (Type u) := âŸ¨ð“‘.PrfâŸ©

/-- `Provable` abbreviates that theory `T` proves formula `f` -/
abbrev Provable (T : Set F) (f : F) : Prop := Nonempty (T âŠ¢ f)

/-- Infix notation for `Proof.Provable` -/
infix:45 " âŠ¢! " => Proof.Provable

/-- `toProof` noncomputably yields a proof of any formula that is provable -/
noncomputable def Provable.toProof {T : Set F} {f : F} (h : T âŠ¢! f) : T âŠ¢ f := Classical.choice h

/-- `Unprovable` abbreviates that theory `T` does not prove formula `f` -/
abbrev Unprovable (T : Set F) (f : F) : Prop := IsEmpty (T âŠ¢ f)

/-- Infix notation for `Proof.Unprovable` -/
infix:45 " âŠ¬ " => Proof.Unprovable

/-- Theory `T` proves the set of formulas in `U` -/
def PrfTheory (T U : Set F) : Type _ := {f : F} â†’ f âˆˆ U â†’ T âŠ¢ f

/-- Infix notation for `Proof.PrfTheory` -/
infix:45 " âŠ¢* " => Proof.PrfTheory

/-- Set of formulas `U` has a `ProvableTheory` `T` -/
def ProvableTheory (T U : Set F) : Prop := Nonempty (T âŠ¢* U)

/-- Infix notation for `Proof.ProvableTheory` -/
infix:45 " âŠ¢*! " => Proof.ProvableTheory

lemma unprovable_iff_not_provable {T : Set F} {f : F} : T âŠ¬ f â†” Â¬T âŠ¢! f := by
  simp[Proof.Unprovable]

/-- A set of formulas `T` has no `ProvableTheory` if every formula is provable -/
def PrfTheoryEmpty (T : Set F) : T âŠ¢* âˆ… := fun h => by contradiction

/-- Theory `U` is a subset of theory `T` -/
def PrfTheory.ofSubset {T U : Set F} (h : U âŠ† T) : T âŠ¢* U := fun hf => axm (h hf)

/-- A theory is `ProvableTheory` for itself -/
def PrfTheory.refl (T : Set F) : T âŠ¢* T := axm

/-- A theory is `Consistent` if it has not proof of contradiction -/
def Consistent (T : Set F) : Prop := IsEmpty (T âŠ¢ âŠ¥)

/-- Theory `T` is a `weakening` of theory `U` -/
def weakening {T U : Set F} {f : F} (b : T âŠ¢ f) (ss : T âŠ† U) : U âŠ¢ f := weakening' ss b

lemma Consistent.of_subset {T U : Set F} (h : Consistent U) (ss : T âŠ† U) : Consistent T :=
  âŸ¨fun b => h.false (weakening b ss)âŸ©

lemma inconsistent_of_proof {T : Set F} (b : T âŠ¢ âŠ¥) : Â¬Consistent T := by
  simp[Consistent]; exact âŸ¨bâŸ©

lemma inconsistent_of_provable {T : Set F} (b : T âŠ¢! âŠ¥) : Â¬Consistent T := by simp[Consistent]

lemma consistent_iff_unprovable {T : Set F} : Consistent T â†” T âŠ¬ âŠ¥ := by rfl

/-- A proof system is complete -/
protected def Complete (T : Set F) : Prop := âˆ€ f, (T âŠ¢! f) âˆ¨ (T âŠ¢! ~f)

/-- A formula is `Independent` of a theory if it does not prove the formula or its negation -/
def Independent (T : Set F) (f : F) : Prop := T âŠ¬ f âˆ§ T âŠ¬ ~f

lemma incomplete_iff_exists_independent {T : Set F} :
    Â¬Proof.Complete T â†” âˆƒ f, Independent T f := by simp[Proof.Complete, not_or, Independent]

/-- A `theory` is a set of formulas which are `Provable` -/
def theory (T : Set F) : Set F := {p | T âŠ¢! p}

@[simp] lemma subset_theory {T : Set F} : T âŠ† theory T := fun _ h â†¦ âŸ¨Proof.axm hâŸ©

/-- A proof in `T` is noncomputably obtained for any formula in `T` -/
noncomputable def provableTheory_theory {T : Set F} : T âŠ¢* theory T := Î» b â†¦ b.toProof

/-- A `Subtheory` proves a subset of formulas -/
class Subtheory (T U : Set F) where
  /-- If `T` proves `f`, `U` proves `f` -/
  sub : {f : F} â†’ T âŠ¢ f â†’ U âŠ¢ f

/-- Infix notation for `Subtheory` -/
infix:50 " â‰¾ " => Subtheory

/-- Definition of equivalent theories -/
class Equivalent (T U : Set F) where
  /-- If `T` proves `f`, `U` proves `f` -/
  ofLeft : {f : F} â†’ T âŠ¢ f â†’ U âŠ¢ f
  /-- If `U` proves `f`, `T` proves `f` -/
  ofRight : {f : F} â†’ U âŠ¢ f â†’ T âŠ¢ f

namespace Subtheory

variable (T U Tâ‚ Tâ‚‚ Tâ‚ƒ : Set F)

@[refl] instance : T â‰¾ T := âŸ¨idâŸ©

/-- `Subtheory` is transitive -/
@[trans] protected def trans [Tâ‚ â‰¾ Tâ‚‚] [Tâ‚‚ â‰¾ Tâ‚ƒ] : Tâ‚ â‰¾ Tâ‚ƒ :=
  âŸ¨fun {f} b => sub (sub b : Tâ‚‚ âŠ¢ f)âŸ©

variable {T U}

/-- `ofSubset` holds for a `Subtheory` that is a weakening -/
def ofSubset (h : T âŠ† U) : T â‰¾ U := âŸ¨fun b => weakening b hâŸ©

/-- A `prfTheory` is a subset of axioms -/
def prfTheory [T â‰¾ U] : U âŠ¢* T := Î» hp â†¦ sub (axm hp)

end Subtheory

namespace Equivalent

variable (T U Tâ‚ Tâ‚‚ Tâ‚ƒ : Set F)

@[refl] instance : Equivalent T T := âŸ¨id, idâŸ©

@[symm] instance [Equivalent T U] : Equivalent U T := âŸ¨ofRight, ofLeftâŸ©

/-- `Equivalent` is transitive for theories -/
@[trans] protected def trans [Equivalent Tâ‚ Tâ‚‚] [Equivalent Tâ‚‚ Tâ‚ƒ] : Equivalent Tâ‚ Tâ‚ƒ :=
  âŸ¨fun {f} b => ofLeft (ofLeft b : Tâ‚‚ âŠ¢ f), fun {f} b => ofRight (ofRight b : Tâ‚‚ âŠ¢ f)âŸ©

end Equivalent

end Proof

/-- A `Proof.hom` is a homomorphism preserving logical connectives-/
def Proof.hom [Proof F] {G : Type u} [LogicalConnective G] (F : G â†’Ë¡á¶œ F) : Proof G where
  Prf := fun T g => F '' T âŠ¢ F g
  axm := fun h => Proof.axm (Set.mem_image_of_mem F h)
  weakening' := fun h => by simp; exact Proof.weakening' (Set.image_subset F h)

variable (F)
variable [LogicalConnective F] [ð“‘ : Proof F] {Î±: Type*} [ð“¢ : Semantics F Î±]

/-- `Sound` class definition -/
class Sound where
  sound : âˆ€ {T : Set F} {p : F}, T âŠ¢ p â†’ T âŠ¨ p

/-- `SoundOn` class definition for a group of formulas -/
class SoundOn (M : Type*) (a : Î±) (H : Set F) where
  sound : âˆ€ {T : Set F} {p : F}, p âˆˆ H â†’ T âŠ¢ p â†’ a âŠ§ p

/-- `Complete` class is sound and proves any true formula -/
class Complete extends Sound F where
  /-- `T` proves any true formula -/
  complete : âˆ€ {T : Set F} {p : F}, T âŠ¨ p â†’ T âŠ¢ p

variable {F}

namespace Sound

variable [Sound F]
variable {a : Î±}

lemma sound! {T : Set F} {f : F} : T âŠ¢! f â†’ T âŠ¨ f := by rintro âŸ¨bâŸ©; exact sound b

lemma not_provable_of_countermodel {T : Set F} {p : F}
    (hT : a âŠ§* T) (hp : Â¬a âŠ§ p) : IsEmpty (T âŠ¢ p) :=
  âŸ¨fun b => by have : a âŠ§ p := Sound.sound b hT; contradictionâŸ©

lemma consistent_of_model {T : Set F} (hT : a âŠ§* T) : Proof.Consistent T :=
  not_provable_of_countermodel (p := âŠ¥) hT (by simp [Prop.bot_eq_false])

lemma consistent_of_satisfiable {T : Set F} :
    Semantics.SatisfiableTheory T â†’ Proof.Consistent T := by
  rintro âŸ¨_, hâŸ©; exact consistent_of_model h

lemma models_of_proof {T : Set F} {f} (h : a âŠ§* T) (b : T âŠ¢ f) : a âŠ§ f :=
  Sound.sound b h

lemma modelsTheory_of_proofTheory {s} {T U : Set F} (h : s âŠ§* T) (b : T âŠ¢* U) : s âŠ§* U :=
  fun _ hf => models_of_proof h (b hf)

lemma modelsTheory_of_subtheory {s} {T U : Set F} [U â‰¾ T] (h : s âŠ§* T) : s âŠ§* U :=
  modelsTheory_of_proofTheory h Proof.Subtheory.prfTheory

end Sound

namespace Complete

/-- `of!` yields a proof of any formula in a `Complete` theory `T`-/
noncomputable def of! [Sound F] (H : âˆ€ {T : Set F} {p : F}, T âŠ¨ p â†’ T âŠ¢! p) : Complete F where
  complete := fun h â†¦ (H h).toProof

variable [Complete F]

lemma satisfiableTheory_iff_consistent {T : Set F} :
    Semantics.SatisfiableTheory T â†” Proof.Consistent T :=
  âŸ¨Sound.consistent_of_satisfiable,
   by contrapose; intro h
      have : T âŠ¨ âŠ¥ := by intro a hM; have : Semantics.SatisfiableTheory T := âŸ¨a, hMâŸ©; contradiction
      have : T âŠ¢ âŠ¥ := complete this
      exact Proof.inconsistent_of_proof thisâŸ©

lemma not_satisfiable_iff_inconsistent {T : Set F} : Â¬Semantics.SatisfiableTheory T â†” T âŠ¢! âŠ¥ := by
  simp[satisfiableTheory_iff_consistent, Proof.Consistent]

lemma consequence_iff_provable {T : Set F} {f : F} : T âŠ¨ f â†” T âŠ¢! f :=
âŸ¨fun h => âŸ¨complete hâŸ©, by rintro âŸ¨bâŸ©; exact Sound.sound bâŸ©

alias âŸ¨complete!, _âŸ© := consequence_iff_provable

end Complete

namespace Proof

variable [ProofTheory.Complete F]

/-- A semantic `Subtheory` is a proof `Subtheory` -/
def ofSemanticsSubtheory {T U : Set F} (h : Semantics.Subtheory T U) : Proof.Subtheory T U :=
  âŸ¨fun hf => Complete.complete (h (Sound.sound hf))âŸ©

end Proof

namespace Semantics

variable [ProofTheory.Complete F]

lemma ofProofSubtheory (Tâ‚ Tâ‚‚ : Set F) [Proof.Subtheory Tâ‚ Tâ‚‚] : Semantics.Subtheory Tâ‚ Tâ‚‚ :=
  fun hf => (Sound.sound $ Proof.Subtheory.sub $ Complete.complete hf)

end Semantics

end ProofTheory
