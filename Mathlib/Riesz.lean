/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/

import Mathlib.Analysis.NormedSpace.FiniteDimension

/-!
# An introduction to Lean via Riesz' Theorem

I will explain the proof of Riesz' Theorem to the computer.

This theorem asserts that if a real normed vector space has a compact ball, then the space
is finite-dimensional.

We prove the contrapositive: if the space is not finite-dimensional, we will construct a sequence
in the ball of radius `2` whose points are all at distance at least `1`, contradicting the
compactness of the ball.

We construct the sequence by induction. Assume that the first `n` points `xáµ¢` have been constructed.
They span a subspace `F` which is finite-dimensional and therefore complete (by equivalence of
norms), hence closed. Let `x âˆ‰ F`. Denote by `d` its distance to `F` (which is positive by
closedness). Let us choose `y âˆˆ F` with `dist x y < 2 d`. I claim that `dâ»Â¹ * (x - y)` can be
chosen as the next point of the sequence. Its norm is indeed at most `2`. Moreover, as `xâˆ© âˆˆ F`, we
have `y + d * xáµ¢ âˆˆ F`. Therefore,
`d â‰¤ dist x (y + d * xáµ¢)`, i.e., `d â‰¤ â€–d * (dâ»Â¹ * (x - y) - xáµ¢)â€–`,
which gives `1 â‰¤ â€–dâ»Â¹ * (x - y) - xáµ¢â€–` as claimed.

To explain this 10 lines proof to Lean, we will cut it in several sublemmas.
-/

open Filter Metric
open scoped Topology

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

/-- Given a closed subspace which is not the whole space, one can find a point with norm at most `2`
which is at distance `1` of every point in the subspace. -/
lemma exists_point_away_from_subspace
    (F : Submodule â„ E) (hF : âˆƒ x, x âˆ‰ F) (hFc : IsClosed (F : Set E)) :
    âˆƒ (z : E), â€–zâ€– < 2 âˆ§ (âˆ€ y âˆˆ F, 1 â‰¤ â€–z - yâ€–) := by
  obtain âŸ¨x, x_pas_dans_FâŸ© := hF
  let d := infDist x F
  have hFn : (F : Set E).Nonempty := âŸ¨0, F.zero_memâŸ©
  have d_pos : 0 < d := (IsClosed.not_mem_iff_infDist_pos hFc hFn).1 x_pas_dans_F
  obtain âŸ¨yâ‚€, hyâ‚€F, hxyâ‚€âŸ© : âˆƒ y âˆˆ F, dist x y < 2 * d := by
    apply (infDist_lt_iff hFn).1
    exact lt_two_mul_self d_pos
    -- linarith
  let z := dâ»Â¹ â€¢ (x - yâ‚€)
  have Nz : â€–zâ€– < 2 := by
    simpa [z, norm_smul, abs_of_nonneg d_pos.le, â† div_eq_inv_mul, div_lt_iff d_pos, â† dist_eq_norm]
  have I : âˆ€ y âˆˆ F, 1 â‰¤ â€–z - yâ€– := by
    intro y hyF
    have A : d â‰¤ dist x (yâ‚€ + d â€¢ y) := by
      apply infDist_le_dist_of_mem
      exact F.add_mem hyâ‚€F (F.smul_mem _ hyF)
    have B : dâ»Â¹ * d = 1 := by field_simp [d_pos.ne']
    calc
      1 = dâ»Â¹ * d                    := B.symm
      _ â‰¤ dâ»Â¹ * dist x (yâ‚€ + d â€¢ y)  := mul_le_mul_of_nonneg_left A (inv_nonneg.2 d_pos.le)
      _ = dâ»Â¹ * â€–(x - yâ‚€) - d â€¢ yâ€–   := by rw [dist_eq_norm, sub_sub]
      _ = â€–dâ»Â¹ â€¢ ((x - yâ‚€) - d â€¢ y)â€– := by simp [norm_smul, abs_of_nonneg d_pos.le]
      _ = â€–z - yâ€–                    := by simp_rw [z, smul_sub, smul_smul, B, one_smul]
  exact âŸ¨z, Nz, IâŸ©

/-- In an infinite-dimensional real normed vector space, given a finite number of points, one can
find a point with norm at most `2` whose distance to all these points is at least `1`. -/
lemma exists_point_away_from_finite
    (s : Set E) (hs : Set.Finite s) (h : Â¬(FiniteDimensional â„ E)) :
    âˆƒ (z : E), â€–zâ€– < 2 âˆ§ (âˆ€ y âˆˆ s, 1 â‰¤ â€–z - yâ€–) := by
  let F := Submodule.span â„ s
  have : FiniteDimensional â„ F := Module.finite_def.2
    ((Submodule.fg_top _).2 (Submodule.fg_def.2 âŸ¨s, hs, rflâŸ©))
  have Fclosed : IsClosed (F : Set E) := Submodule.closed_of_finiteDimensional _
  have hF : âˆƒ x, x âˆ‰ F := by
    contrapose! h
    have : (âŠ¤ : Submodule â„ E) = F := by ext x; simp [h]
    have : FiniteDimensional â„ (âŠ¤ : Submodule â„ E) := by rwa [this]
    refine Module.finite_def.2 ((Submodule.fg_top _).1 (Module.finite_def.1 this))
  obtain âŸ¨x, x_lt_2, hxâŸ© : âˆƒ (x : E), â€–xâ€– < 2 âˆ§ âˆ€ (y : E), y âˆˆ F â†’ 1 â‰¤ â€–x - yâ€– :=
    exists_point_away_from_subspace F hF Fclosed
  exact âŸ¨x, x_lt_2, fun y hy â†¦ hx _ (Submodule.subset_span hy)âŸ©

/-- In an infinite-dimensional real normed vector space, one can find a sequence of points of norm
at most `2`, all of them separated by at least `1`. -/
lemma exists_sequence_separated (h : Â¬(FiniteDimensional â„ E)) :
    âˆƒ (u : â„• â†’ E), (âˆ€ n, â€–u nâ€– < 2) âˆ§ (âˆ€ m n, m â‰  n â†’ 1 â‰¤ â€–u n - u mâ€–) := by
  have : IsSymm E (fun (x y : E) â†¦ 1 â‰¤ â€–y - xâ€–) := by
    constructor
    intro x y hxy
    rw [â† norm_neg]
    simpa
  apply exists_seq_of_forall_finset_exists' (fun (x : E) â†¦ â€–xâ€– < 2)
    (fun (x : E) (y : E) â†¦ 1 â‰¤ â€–y - xâ€–)
  intro s _hs
  exact exists_point_away_from_finite (s : Set E) s.finite_toSet h

/-- Consider a real normed  vector space in which the closed ball of radius `2` is compact. Then
this space is finite-dimensional. -/
theorem my_riesz_version (h : IsCompact (closedBall (0 : E) 2)) :
    FiniteDimensional â„ E := by
  by_contra hfin
  obtain âŸ¨u, u_lt_two, u_farâŸ© :
    âˆƒ (u : â„• â†’ E), (âˆ€ n, â€–u nâ€– < 2) âˆ§ (âˆ€ m n, m â‰  n â†’ 1 â‰¤ â€–u n - u mâ€–) :=
    exists_sequence_separated hfin
  have A : âˆ€ n, u n âˆˆ closedBall (0 : E) 2 := by
    intro n
    simpa only [norm_smul, dist_zero_right, mem_closedBall] using (u_lt_two n).le
  obtain âŸ¨x, _hx, Ï†, Ï†mono, Ï†limâŸ© : âˆƒ x âˆˆ closedBall (0 : E) 2, âˆƒ (Ï† : â„• â†’ â„•),
    StrictMono Ï† âˆ§ Tendsto (u âˆ˜ Ï†) atTop (ð“ x) := h.tendsto_subseq A
  have B : CauchySeq (u âˆ˜ Ï†) := Ï†lim.cauchySeq
  obtain âŸ¨N, hNâŸ© : âˆƒ (N : â„•), âˆ€ (n : â„•), N â‰¤ n â†’ dist ((u âˆ˜ Ï†) n) ((u âˆ˜ Ï†) N) < 1 :=
    cauchySeq_iff'.1 B 1 zero_lt_one
  apply lt_irrefl (1 : â„)
  calc
  1 â‰¤ dist (u (Ï† (N+1))) (u (Ï† N)) := by
    simp only [dist_eq_norm, â† smul_sub, norm_smul]
    apply u_far
    exact (Ï†mono (Nat.lt_succ_self N)).ne
  _ < 1 := hN (N+1) (Nat.le_succ N)

/- The proof is over. It takes roughly 100 lines, 10 times more than the informal proof. This is
quite typical.  -/

theorem the_real_riesz_version
    (ð•œ : Type*) [NontriviallyNormedField ð•œ] {F : Type*} [NormedAddCommGroup F]
    [NormedSpace ð•œ F] [CompleteSpace ð•œ] {r : â„}
    (r_pos : 0 < r)  {c : F} (hc : IsCompact (closedBall c r)) :
    FiniteDimensional ð•œ F :=
  .of_isCompact_closedBall ð•œ r_pos hc
-- by exact?

/- Pour l'Ã©noncÃ© prÃ©cÃ©dent :
  have : (0 : â„) < 2 := zero_lt_two
  exact?
-/

/- The proofs are checked by the "kernel". But how can one be convinced that the definitions are
good? With wrong definitions, one could prove anything. -/

def IsSGCompact {Î± : Type*} (_s : Set Î±) : Prop := False

theorem riesz_with_isSGCompact (h : IsSGCompact (closedBall (0 : E) 2)) :
  FiniteDimensional â„ E :=
False.elim h

theorem antiriesz_with_isSGCompact (h : IsSGCompact (closedBall (0 : E) 2)) :
  Â¬(FiniteDimensional â„ E) :=
False.elim h

/- We can try unfolding the definitions to see if they look reasonable. -/

#check IsCompact
#check FiniteDimensional

/- We can try to see if the definitions make it possible to prove reasonable theorems. -/

example (n : â„•) : FiniteDimensional â„ (Fin n â†’ â„) := by infer_instance

example (n : â„•) : IsCompact (closedBall (0 : Fin n â†’ â„) 1) := isCompact_closedBall _ _

example : Â¬(IsCompact (Set.univ : Set â„)) := noncompact_univ â„

example {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [Nontrivial E] :
  Â¬(IsCompact (Set.univ : Set E)) := noncompact_univ E
