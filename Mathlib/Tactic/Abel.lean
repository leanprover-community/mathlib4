/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Scott Morrison
-/
import Mathlib.Tactic.NormNum
import Mathlib.Util.AtomM
import Mathlib.Data.Int.Basic

/-!
# The `abel` tactic

Evaluate expressions in the language of additive, commutative monoids and groups.

-/

set_option autoImplicit true

namespace Mathlib.Tactic.Abel
open Lean Elab Meta Tactic Qq

initialize registerTraceClass `abel
initialize registerTraceClass `abel.detail

/-- The `Context` for a call to `abel`.

Stores a few options for this call, and caches some common subexpressions
such as typeclass instances and `0 : Œ±`.
-/
structure Context where
  /-- The type of the ambient additive commutative group or monoid. -/
  Œ±       : Expr
  /-- The universe level for `Œ±`. -/
  univ    : Level
  /-- The expression representing `0 : Œ±`. -/
  Œ±0      : Expr
  /-- Specify whether we are in an additive commutative group or an additive commutative monoid. -/
  isGroup : Bool
  /-- The `AddCommGroup Œ±` or `AddCommMonoid Œ±` expression. -/
  inst    : Expr

/-- Populate a `context` object for evaluating `e`. -/
def mkContext (e : Expr) : MetaM Context := do
  let Œ± ‚Üê inferType e
  let c ‚Üê synthInstance (‚Üê mkAppM ``AddCommMonoid #[Œ±])
  let cg ‚Üê synthInstance? (‚Üê mkAppM ``AddCommGroup #[Œ±])
  let u ‚Üê mkFreshLevelMVar
  _ ‚Üê isDefEq (.sort (.succ u)) (‚Üê inferType Œ±)
  let Œ±0 ‚Üê Expr.ofNat Œ± 0
  match cg with
  | some cg => return ‚ü®Œ±, u, Œ±0, true, cg‚ü©
  | _ => return ‚ü®Œ±, u, Œ±0, false, c‚ü©

/-- The monad for `Abel` contains, in addition to the `AtomM` state,
some information about the current type we are working over, so that we can consistently
use group lemmas or monoid lemmas as appropriate. -/
abbrev M := ReaderT Context AtomM

/-- Apply the function `n : ‚àÄ {Œ±} [inst : AddWhatever Œ±], _` to the
implicit parameters in the context, and the given list of arguments. -/
def Context.app (c : Context) (n : Name) (inst : Expr) : Array Expr ‚Üí Expr :=
  mkAppN (((@Expr.const n [c.univ]).app c.Œ±).app inst)

/-- Apply the function `n : ‚àÄ {Œ±} [inst Œ±], _` to the implicit parameters in the
context, and the given list of arguments.

Compared to `context.app`, this takes the name of the typeclass, rather than an
inferred typeclass instance.
-/
def Context.mkApp (c : Context) (n inst : Name) (l : Array Expr) : MetaM Expr := do
  return c.app n (‚Üê synthInstance ((Expr.const inst [c.univ]).app c.Œ±)) l

/-- Add the letter "g" to the end of the name, e.g. turning `term` into `termg`.

This is used to choose between declarations taking `AddCommMonoid` and those
taking `AddCommGroup` instances.
-/
def addG : Name ‚Üí Name
  | .str p s => .str p (s ++ "g")
  | n => n

/-- Apply the function `n : ‚àÄ {Œ±} [AddComm{Monoid,Group} Œ±]` to the given list of arguments.

Will use the `AddComm{Monoid,Group}` instance that has been cached in the context.
-/
def iapp (n : Name) (xs : Array Expr) : M Expr := do
  let c ‚Üê read
  return c.app (if c.isGroup then addG n else n) c.inst xs

/-- A type synonym used by `abel` to represent `n ‚Ä¢ x + a` in an additive commutative monoid. -/
def term {Œ±} [AddCommMonoid Œ±] (n : ‚Ñï) (x a : Œ±) : Œ± := n ‚Ä¢ x + a
/-- A type synonym used by `abel` to represent `n ‚Ä¢ x + a` in an additive commutative group. -/
def termg {Œ±} [AddCommGroup Œ±] (n : ‚Ñ§) (x a : Œ±) : Œ± := n ‚Ä¢ x + a

/-- Evaluate a term with coefficient `n`, atom `x` and successor terms `a`. -/
def mkTerm (n x a : Expr) : M Expr := iapp ``term #[n, x, a]

/-- Interpret an integer as a coefficient to a term. -/
def intToExpr (n : ‚Ñ§) : M Expr := do
  Expr.ofInt (mkConst (if (‚Üê read).isGroup then ``Int else ``Nat) []) n

/-- A normal form for `abel`.
Expressions are represented as a list of terms of the form `e = n ‚Ä¢ x`,
where `n : ‚Ñ§` and `x` is an arbitrary element of the additive commutative monoid or group.
We explicitly track the `Expr` forms of `e` and `n`, even though they could be reconstructed,
for efficiency. -/
inductive NormalExpr : Type
  | zero (e : Expr) : NormalExpr
  | nterm (e : Expr) (n : Expr √ó ‚Ñ§) (x : ‚Ñï √ó Expr) (a : NormalExpr) : NormalExpr
  deriving Inhabited

/-- Extract the expression from a normal form. -/
def NormalExpr.e : NormalExpr ‚Üí Expr
  | .zero e => e
  | .nterm e .. => e

instance : Coe NormalExpr Expr where coe := NormalExpr.e

/-- Construct the normal form representing a single term. -/
def NormalExpr.term' (n : Expr √ó ‚Ñ§) (x : ‚Ñï √ó Expr) (a : NormalExpr) : M NormalExpr :=
  return .nterm (‚Üê mkTerm n.1 x.2 a) n x a

/-- Construct the normal form representing zero. -/
def NormalExpr.zero' : M NormalExpr := return NormalExpr.zero (‚Üê read).Œ±0

open NormalExpr

theorem const_add_term {Œ±} [AddCommMonoid Œ±] (k n x a a') (h : k + a = a') :
    k + @term Œ± _ n x a = term n x a' := by
  simp [h.symm, term, add_comm, add_assoc]
  -- üéâ no goals

theorem const_add_termg {Œ±} [AddCommGroup Œ±] (k n x a a') (h : k + a = a') :
    k + @termg Œ± _ n x a = termg n x a' := by
  simp [h.symm, termg, add_comm, add_assoc]
  -- üéâ no goals

theorem term_add_const {Œ±} [AddCommMonoid Œ±] (n x a k a') (h : a + k = a') :
    @term Œ± _ n x a + k = term n x a' := by
  simp [h.symm, term, add_assoc]
  -- üéâ no goals

theorem term_add_constg {Œ±} [AddCommGroup Œ±] (n x a k a') (h : a + k = a') :
    @termg Œ± _ n x a + k = termg n x a' := by
  simp [h.symm, termg, add_assoc]
  -- üéâ no goals

theorem term_add_term {Œ±} [AddCommMonoid Œ±] (n‚ÇÅ x a‚ÇÅ n‚ÇÇ a‚ÇÇ n' a') (h‚ÇÅ : n‚ÇÅ + n‚ÇÇ = n')
    (h‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a') : @term Œ± _ n‚ÇÅ x a‚ÇÅ + @term Œ± _ n‚ÇÇ x a‚ÇÇ = term n' x a' := by
  simp [h‚ÇÅ.symm, h‚ÇÇ.symm, term, add_nsmul, add_assoc, add_left_comm]
  -- üéâ no goals

theorem term_add_termg {Œ±} [AddCommGroup Œ±] (n‚ÇÅ x a‚ÇÅ n‚ÇÇ a‚ÇÇ n' a')
    (h‚ÇÅ : n‚ÇÅ + n‚ÇÇ = n') (h‚ÇÇ : a‚ÇÅ + a‚ÇÇ = a') :
    @termg Œ± _ n‚ÇÅ x a‚ÇÅ + @termg Œ± _ n‚ÇÇ x a‚ÇÇ = termg n' x a' := by
  simp [h‚ÇÅ.symm, h‚ÇÇ.symm, termg, add_zsmul]
  -- ‚ä¢ n‚ÇÅ ‚Ä¢ x + a‚ÇÅ + (n‚ÇÇ ‚Ä¢ x + a‚ÇÇ) = n‚ÇÅ ‚Ä¢ x + n‚ÇÇ ‚Ä¢ x + (a‚ÇÅ + a‚ÇÇ)
  exact add_add_add_comm (n‚ÇÅ ‚Ä¢ x) a‚ÇÅ (n‚ÇÇ ‚Ä¢ x) a‚ÇÇ
  -- üéâ no goals

theorem zero_term {Œ±} [AddCommMonoid Œ±] (x a) : @term Œ± _ 0 x a = a := by
  simp [term, zero_nsmul, one_nsmul]
  -- üéâ no goals

theorem zero_termg {Œ±} [AddCommGroup Œ±] (x a) : @termg Œ± _ 0 x a = a := by
  simp [termg, zero_zsmul]
  -- üéâ no goals

/--
Interpret the sum of two expressions in `abel`'s normal form.
-/
partial def evalAdd : NormalExpr ‚Üí NormalExpr ‚Üí M (NormalExpr √ó Expr)
  | zero _, e‚ÇÇ => do
    let p ‚Üê mkAppM ``zero_add #[e‚ÇÇ]
    return (e‚ÇÇ, p)
  | e‚ÇÅ, zero _ => do
    let p ‚Üê mkAppM ``add_zero #[e‚ÇÅ]
    return (e‚ÇÅ, p)
  | he‚ÇÅ@(nterm e‚ÇÅ n‚ÇÅ x‚ÇÅ a‚ÇÅ), he‚ÇÇ@(nterm e‚ÇÇ n‚ÇÇ x‚ÇÇ a‚ÇÇ) => do
    if x‚ÇÅ.1 = x‚ÇÇ.1 then
      let n' ‚Üê Mathlib.Meta.NormNum.eval (‚Üê mkAppM ``HAdd.hAdd #[n‚ÇÅ.1, n‚ÇÇ.1])
      let (a', h‚ÇÇ) ‚Üê evalAdd a‚ÇÅ a‚ÇÇ
      let k := n‚ÇÅ.2 + n‚ÇÇ.2
      let p‚ÇÅ ‚Üê iapp ``term_add_term
        #[n‚ÇÅ.1, x‚ÇÅ.2, a‚ÇÅ, n‚ÇÇ.1, a‚ÇÇ, n'.expr, a', ‚Üê n'.getProof, h‚ÇÇ]
      if k = 0 then do
        let p ‚Üê mkEqTrans p‚ÇÅ (‚Üê iapp ``zero_term #[x‚ÇÅ.2, a'])
        return (a', p)
      else return (‚Üê term' (n'.expr, k) x‚ÇÅ a', p‚ÇÅ)
    else if x‚ÇÅ.1 < x‚ÇÇ.1 then do
      let (a', h) ‚Üê evalAdd a‚ÇÅ he‚ÇÇ
      return (‚Üê term' n‚ÇÅ x‚ÇÅ a', ‚Üê iapp ``term_add_const #[n‚ÇÅ.1, x‚ÇÅ.2, a‚ÇÅ, e‚ÇÇ, a', h])
    else do
      let (a', h) ‚Üê evalAdd he‚ÇÅ a‚ÇÇ
      return (‚Üê term' n‚ÇÇ x‚ÇÇ a', ‚Üê iapp ``const_add_term #[e‚ÇÅ, n‚ÇÇ.1, x‚ÇÇ.2, a‚ÇÇ, a', h])

theorem term_neg {Œ±} [AddCommGroup Œ±] (n x a n' a')
    (h‚ÇÅ : -n = n') (h‚ÇÇ : -a = a') : -@termg Œ± _ n x a = termg n' x a' := by
  simp [h‚ÇÇ.symm, h‚ÇÅ.symm, termg]; exact add_comm _ _
  -- ‚ä¢ -a + -(n ‚Ä¢ x) = -(n ‚Ä¢ x) + -a
                                  -- üéâ no goals

/--
Interpret a negated expression in `abel`'s normal form.
-/
def evalNeg : NormalExpr ‚Üí M (NormalExpr √ó Expr)
  | (zero _) => do
    let p ‚Üê (‚Üê read).mkApp ``neg_zero ``NegZeroClass #[]
    return (‚Üê zero', p)
  | (nterm _ n x a) => do
    let n' ‚Üê Mathlib.Meta.NormNum.eval (‚Üê mkAppM ``Neg.neg #[n.1])
    let (a', h‚ÇÇ) ‚Üê evalNeg a
    return (‚Üê term' (n'.expr, -n.2) x a',
      (‚Üê read).app ``term_neg (‚Üê read).inst #[n.1, x.2, a, n'.expr, a', ‚Üê n'.getProof, h‚ÇÇ])

/-- A synonym for `‚Ä¢`, used internally in `abel`. -/
def smul {Œ±} [AddCommMonoid Œ±] (n : ‚Ñï) (x : Œ±) : Œ± := n ‚Ä¢ x
/-- A synonym for `‚Ä¢`, used internally in `abel`. -/
def smulg {Œ±} [AddCommGroup Œ±] (n : ‚Ñ§) (x : Œ±) : Œ± := n ‚Ä¢ x

theorem zero_smul {Œ±} [AddCommMonoid Œ±] (c) : smul c (0 : Œ±) = 0 := by
  simp [smul, nsmul_zero]
  -- üéâ no goals

theorem zero_smulg {Œ±} [AddCommGroup Œ±] (c) : smulg c (0 : Œ±) = 0 := by
  simp [smulg, zsmul_zero]
  -- üéâ no goals

theorem term_smul {Œ±} [AddCommMonoid Œ±] (c n x a n' a')
  (h‚ÇÅ : c * n = n') (h‚ÇÇ : smul c a = a') :
  smul c (@term Œ± _ n x a) = term n' x a' := by
  simp [h‚ÇÇ.symm, h‚ÇÅ.symm, term, smul, nsmul_add, mul_nsmul']
  -- üéâ no goals

theorem term_smulg {Œ±} [AddCommGroup Œ±] (c n x a n' a')
  (h‚ÇÅ : c * n = n') (h‚ÇÇ : smulg c a = a') :
  smulg c (@termg Œ± _ n x a) = termg n' x a' := by
  simp [h‚ÇÇ.symm, h‚ÇÅ.symm, termg, smulg, zsmul_add, mul_zsmul]
  -- üéâ no goals

/--
Auxiliary function for `evalSMul'`.
-/
def evalSMul (k : Expr √ó ‚Ñ§) : NormalExpr ‚Üí M (NormalExpr √ó Expr)
  | zero _ => return (‚Üê zero', ‚Üê iapp ``zero_smul #[k.1])
  | nterm _ n x a => do
    let n' ‚Üê Mathlib.Meta.NormNum.eval (‚Üê mkAppM ``HMul.hMul #[k.1, n.1])
    let (a', h‚ÇÇ) ‚Üê evalSMul k a
    return (‚Üê term' (n'.expr, k.2 * n.2) x a',
      ‚Üê iapp ``term_smul #[k.1, n.1, x.2, a, n'.expr, a', ‚Üê n'.getProof, h‚ÇÇ])

theorem term_atom {Œ±} [AddCommMonoid Œ±] (x : Œ±) : x = term 1 x 0 := by simp [term]
                                                                       -- üéâ no goals
theorem term_atomg {Œ±} [AddCommGroup Œ±] (x : Œ±) : x = termg 1 x 0 := by simp [termg]
                                                                        -- üéâ no goals
theorem term_atom_pf {Œ±} [AddCommMonoid Œ±] (x x' : Œ±) (h : x = x') : x = term 1 x' 0 := by
  simp [term, h]
  -- üéâ no goals
theorem term_atom_pfg {Œ±} [AddCommGroup Œ±] (x x' : Œ±) (h : x = x') : x = termg 1 x' 0 := by
  simp [termg, h]
  -- üéâ no goals

/-- Interpret an expression as an atom for `abel`'s normal form. -/
def evalAtom (e : Expr) : M (NormalExpr √ó Expr) := do
  let { expr := e', proof?, .. } ‚Üê (‚Üê readThe AtomM.Context).evalAtom e
  let i ‚Üê AtomM.addAtom e'
  let p ‚Üê match proof? with
  | none => iapp ``term_atom #[e]
  | some p => iapp ``term_atom_pf #[e, e', p]
  return (‚Üê term' (‚Üê intToExpr 1, 1) (i, e') (‚Üê zero'), p)

theorem unfold_sub {Œ±} [SubtractionMonoid Œ±] (a b c : Œ±) (h : a + -b = c) : a - b = c := by
  rw [sub_eq_add_neg, h]
  -- üéâ no goals

theorem unfold_smul {Œ±} [AddCommMonoid Œ±] (n) (x y : Œ±)
    (h : smul n x = y) : n ‚Ä¢ x = y := h

theorem unfold_smulg {Œ±} [AddCommGroup Œ±] (n : ‚Ñï) (x y : Œ±)
    (h : smulg (Int.ofNat n) x = y) : (n : ‚Ñ§) ‚Ä¢ x = y := h

theorem unfold_zsmul {Œ±} [AddCommGroup Œ±] (n : ‚Ñ§) (x y : Œ±)
    (h : smulg n x = y) : n ‚Ä¢ x = y := h

lemma subst_into_smul {Œ±} [AddCommMonoid Œ±]
    (l r tl tr t) (prl : l = tl) (prr : r = tr)
    (prt : @smul Œ± _ tl tr = t) : smul l r = t := by simp [prl, prr, prt]
                                                     -- üéâ no goals

lemma subst_into_smulg {Œ±} [AddCommGroup Œ±]
    (l r tl tr t) (prl : l = tl) (prr : r = tr)
    (prt : @smulg Œ± _ tl tr = t) : smulg l r = t := by simp [prl, prr, prt]
                                                       -- üéâ no goals

lemma subst_into_smul_upcast {Œ±} [AddCommGroup Œ±]
    (l r tl zl tr t) (prl‚ÇÅ : l = tl) (prl‚ÇÇ : ‚Üëtl = zl) (prr : r = tr)
    (prt : @smulg Œ± _ zl tr = t) : smul l r = t := by
  simp [‚Üê prt, prl‚ÇÅ, ‚Üê prl‚ÇÇ, prr, smul, smulg, coe_nat_zsmul]
  -- üéâ no goals

lemma subst_into_add {Œ±} [AddCommMonoid Œ±] (l r tl tr t)
    (prl : (l : Œ±) = tl) (prr : r = tr) (prt : tl + tr = t) : l + r = t := by
  rw [prl, prr, prt]
  -- üéâ no goals

lemma subst_into_addg {Œ±} [AddCommGroup Œ±] (l r tl tr t)
    (prl : (l : Œ±) = tl) (prr : r = tr) (prt : tl + tr = t) : l + r = t := by
  rw [prl, prr, prt]
  -- üéâ no goals

lemma subst_into_negg {Œ±} [AddCommGroup Œ±] (a ta t : Œ±)
    (pra : a = ta) (prt : -ta = t) : -a = t := by
  simp [pra, prt]
  -- üéâ no goals

/-- Normalize a term `orig` of the form `smul e‚ÇÅ e‚ÇÇ` or `smulg e‚ÇÅ e‚ÇÇ`.
  Normalized terms use `smul` for monoids and `smulg` for groups,
  so there are actually four cases to handle:
  * Using `smul` in a monoid just simplifies the pieces using `subst_into_smul`
  * Using `smulg` in a group just simplifies the pieces using `subst_into_smulg`
  * Using `smul a b` in a group requires converting `a` from a nat to an int and
    then simplifying `smulg ‚Üëa b` using `subst_into_smul_upcast`
  * Using `smulg` in a monoid is impossible (or at least out of scope),
    because you need a group argument to write a `smulg` term -/
def evalSMul' (eval : Expr ‚Üí M (NormalExpr √ó Expr))
    (is_smulg : Bool) (orig e‚ÇÅ e‚ÇÇ : Expr) : M (NormalExpr √ó Expr) := do
  trace[abel] "Calling NormNum on {e‚ÇÅ}"
  let ‚ü®e‚ÇÅ', p‚ÇÅ, _‚ü© ‚Üê try Meta.NormNum.eval e‚ÇÅ catch _ => pure { expr := e‚ÇÅ }
  let p‚ÇÅ ‚Üê p‚ÇÅ.getDM (mkEqRefl e‚ÇÅ')
  match Meta.NormNum.isIntLit e‚ÇÅ' with
  | some n => do
    let c ‚Üê read
    let (e‚ÇÇ', p‚ÇÇ) ‚Üê eval e‚ÇÇ
    if c.isGroup = is_smulg then do
      let (e', p) ‚Üê evalSMul (e‚ÇÅ', n) e‚ÇÇ'
      return (e', ‚Üê iapp ``subst_into_smul #[e‚ÇÅ, e‚ÇÇ, e‚ÇÅ', e‚ÇÇ', e', p‚ÇÅ, p‚ÇÇ, p])
    else do
      if ¬¨ c.isGroup then throwError "Doesn't make sense to us `smulg` in a monoid. "
      -- We are multiplying by a natural number in an additive group.
      let zl ‚Üê Expr.ofInt q(‚Ñ§) n
      let p‚ÇÅ' ‚Üê mkEqRefl zl
      let (e', p) ‚Üê evalSMul (zl, n) e‚ÇÇ'
      return (e', c.app ``subst_into_smul_upcast c.inst #[e‚ÇÅ, e‚ÇÇ, e‚ÇÅ', zl, e‚ÇÇ', e', p‚ÇÅ, p‚ÇÅ', p‚ÇÇ, p])
  | none => evalAtom orig

/-- Evaluate an expression into its `abel` normal form, by recursing into subexpressions. -/
partial def eval (e : Expr) : M (NormalExpr √ó Expr) := do
  trace[abel.detail] "running eval on {e}"
  trace[abel.detail] "getAppFnArgs: {e.getAppFnArgs}"
  match e.getAppFnArgs with
  | (``HAdd.hAdd, #[_, _, _, _, e‚ÇÅ, e‚ÇÇ]) => do
    let (e‚ÇÅ', p‚ÇÅ) ‚Üê eval e‚ÇÅ
    let (e‚ÇÇ', p‚ÇÇ) ‚Üê eval e‚ÇÇ
    let (e', p') ‚Üê evalAdd e‚ÇÅ' e‚ÇÇ'
    return (e', ‚Üê iapp ``subst_into_add #[e‚ÇÅ, e‚ÇÇ, e‚ÇÅ', e‚ÇÇ', e', p‚ÇÅ, p‚ÇÇ, p'])
  | (``HSub.hSub, #[_, _, _ ,_, e‚ÇÅ, e‚ÇÇ]) => do
    let e‚ÇÇ' ‚Üê mkAppM ``Neg.neg #[e‚ÇÇ]
    let e ‚Üê mkAppM ``HAdd.hAdd #[e‚ÇÅ, e‚ÇÇ']
    let (e', p) ‚Üê eval e
    let p' ‚Üê (‚Üê read).mkApp ``unfold_sub ``SubtractionMonoid #[e‚ÇÅ, e‚ÇÇ, e', p]
    return (e', p')
  | (``Neg.neg, #[_, _, e]) => do
    let (e‚ÇÅ, p‚ÇÅ) ‚Üê eval e
    let (e‚ÇÇ, p‚ÇÇ) ‚Üê evalNeg e‚ÇÅ
    return (e‚ÇÇ, ‚Üê iapp `Mathlib.Tactic.Abel.subst_into_neg #[e, e‚ÇÅ, e‚ÇÇ, p‚ÇÅ, p‚ÇÇ])
  | (`AddMonoid.nsmul, #[_, _, e‚ÇÅ, e‚ÇÇ]) => do
    let n ‚Üê if (‚Üê read).isGroup then mkAppM ``Int.ofNat #[e‚ÇÅ] else pure e‚ÇÅ
    let (e', p) ‚Üê eval <| ‚Üê iapp ``smul #[n, e‚ÇÇ]
    return (e', ‚Üê iapp ``unfold_smul #[e‚ÇÅ, e‚ÇÇ, e', p])
  | (``SubNegMonoid.zsmul, #[_, _, e‚ÇÅ, e‚ÇÇ]) => do
      if ¬¨ (‚Üê read).isGroup then failure
      let (e', p) ‚Üê eval <| ‚Üê iapp ``smul #[e‚ÇÅ, e‚ÇÇ]
      return (e', (‚Üê read).app ``unfold_zsmul (‚Üê read).inst #[e‚ÇÅ, e‚ÇÇ, e', p])
  | (``SMul.smul, #[.const ``Int _, _, _, e‚ÇÅ, e‚ÇÇ]) =>
    evalSMul' eval true e e‚ÇÅ e‚ÇÇ
  | (``SMul.smul, #[.const ``Nat _, _, _, e‚ÇÅ, e‚ÇÇ]) =>
    evalSMul' eval false e e‚ÇÅ e‚ÇÇ
  | (``HSMul.hSMul, #[.const ``Int _, _, _, _, e‚ÇÅ, e‚ÇÇ]) =>
    evalSMul' eval true e e‚ÇÅ e‚ÇÇ
  | (``HSMul.hSMul, #[.const ``Nat _, _, _, _, e‚ÇÅ, e‚ÇÇ]) =>
    evalSMul' eval false e e‚ÇÅ e‚ÇÇ
  | (``smul, #[_, _, e‚ÇÅ, e‚ÇÇ]) => evalSMul' eval false e e‚ÇÅ e‚ÇÇ
  | (``smulg, #[_, _, e‚ÇÅ, e‚ÇÇ]) => evalSMul' eval true e e‚ÇÅ e‚ÇÇ
  | (``OfNat.ofNat, #[_, .lit (.natVal 0), _])
  | (``Zero.zero, #[_, _]) =>
    if ‚Üê isDefEq e (‚Üê read).Œ±0 then
      pure (‚Üê zero', ‚Üê mkEqRefl (‚Üê read).Œ±0)
    else
      evalAtom e
  | _ => evalAtom e

/-- Tactic for solving equations in the language of
*additive*, commutative monoids and groups.
This version of `abel` fails if the target is not an equality
that is provable by the axioms of commutative monoids/groups.

`abel1!` will use a more aggressive reducibility setting to identify atoms.
This can prove goals that `abel` cannot, but is more expensive.
-/
syntax (name := abel1) "abel1" "!"? : tactic

open Lean Elab Meta Tactic

/-- The `abel1` tactic, which solves equations in the language of commutative additive groups
(or monoids). -/
elab_rules : tactic | `(tactic| abel1 $[!%$tk]?) => withMainContext do
  let tm := if tk.isSome then .default else .reducible
  let some (_, e‚ÇÅ, e‚ÇÇ) := (‚Üê whnfR <| ‚Üê getMainTarget).eq?
    | throwError "abel1 requires an equality goal"
  trace[abel] "running on an equality `{e‚ÇÅ} = {e‚ÇÇ}`."
  let c ‚Üê mkContext e‚ÇÅ
  closeMainGoal <| ‚Üê AtomM.run tm <| ReaderT.run (r := c) do
    let (e‚ÇÅ', p‚ÇÅ) ‚Üê eval e‚ÇÅ
    trace[abel] "found `{p‚ÇÅ}`, a proof that `{e‚ÇÅ} = {e‚ÇÅ'.e}`"
    let (e‚ÇÇ', p‚ÇÇ) ‚Üê eval e‚ÇÇ
    trace[abel] "found `{p‚ÇÇ}`, a proof that `{e‚ÇÇ} = {e‚ÇÇ'.e}`"
    unless ‚Üê isDefEq e‚ÇÅ' e‚ÇÇ' do
      throwError "abel1 found that the two sides were not equal"
    trace[abel] "verified that the simplified forms are identical"
    mkEqTrans p‚ÇÅ (‚Üê mkEqSymm p‚ÇÇ)

@[inherit_doc abel1] macro (name := abel1!) "abel1!" : tactic => `(tactic| abel1 !)

theorem term_eq [AddCommMonoid Œ±] (n : ‚Ñï) (x a : Œ±) : term n x a = n ‚Ä¢ x + a := rfl
/-- A type synonym used by `abel` to represent `n ‚Ä¢ x + a` in an additive commutative group. -/
theorem termg_eq [AddCommGroup Œ±] (n : ‚Ñ§) (x a : Œ±) : termg n x a = n ‚Ä¢ x + a := rfl

/-- True if this represents an atomic expression. -/
def NormalExpr.isAtom : NormalExpr ‚Üí Bool
  | .nterm _ (_, 1) _ (.zero _) => true
  | _ => false

/-- The normalization style for `abel_nf`. -/
inductive AbelMode where
  /-- The default form -/
  | term
  /-- Raw form: the representation `abel` uses internally. -/
  | raw

/-- Configuration for `abel_nf`. -/
structure AbelNF.Config where
  /-- the reducibility setting to use when comparing atoms for defeq -/
  red := TransparencyMode.reducible
  /-- if true, atoms inside ring expressions will be reduced recursively -/
  recursive := true
  /-- The normalization style. -/
  mode := AbelMode.term

/-- Function elaborating `AbelNF.Config`. -/
declare_config_elab elabAbelNFConfig AbelNF.Config

/--
The core of `abel_nf`, which rewrites the expression `e` into `abel` normal form.

* `s`: a reference to the mutable state of `abel`, for persisting across calls.
  This ensures that atom ordering is used consistently.
* `cfg`: the configuration options
* `e`: the expression to rewrite
-/
partial def abelNFCore
    (s : IO.Ref AtomM.State) (cfg : AbelNF.Config) (e : Expr) : MetaM Simp.Result := do
  let ctx := {
    simpTheorems := #[‚Üê Elab.Tactic.simpOnlyBuiltins.foldlM (¬∑.addConst ¬∑) {}]
    congrTheorems := ‚Üê getSimpCongrTheorems }
  let simp ‚Üê match cfg.mode with
  | .raw => pure pure
  | .term =>
    let thms := [``term_eq, ``termg_eq, ``add_zero, ``one_nsmul, ``one_zsmul, ``zsmul_zero]
    let ctx' := { ctx with simpTheorems := #[‚Üê thms.foldlM (¬∑.addConst ¬∑) {:_}] }
    pure fun r' : Simp.Result ‚Ü¶ do
      Simp.mkEqTrans r' (‚Üê Simp.main r'.expr ctx' (methods := Simp.DefaultMethods.methods)).1
  let rec
    /-- The recursive case of `abelNF`.
    * `root`: true when the function is called directly from `abelNFCore`
      and false when called by `evalAtom` in recursive mode.
    * `parent`: The input expression to simplify. In `pre` we make use of both `parent` and `e`
      to determine if we are at the top level in order to prevent a loop
      `go -> eval -> evalAtom -> go` which makes no progress.
    -/
    go root parent :=
      let pre e :=
        try
          guard <| root || parent != e -- recursion guard
          let e ‚Üê withReducible <| whnf e
          guard e.isApp -- all interesting group expressions are applications
          let (a, pa) ‚Üê eval e (‚Üê mkContext e) { red := cfg.red, evalAtom } s
          guard !a.isAtom
          let r ‚Üê simp { expr := a, proof? := pa }
          if ‚Üê withReducible <| isDefEq r.expr e then return .done { expr := r.expr }
          pure (.done r)
        catch _ => pure <| .visit { expr := e }
      let post := (Simp.postDefault ¬∑ fun _ ‚Ü¶ none)
      (¬∑.1) <$> Simp.main parent ctx (methods := { pre, post }),
    /-- The `evalAtom` implementation passed to `eval` calls `go` if `cfg.recursive` is true,
    and does nothing otherwise. -/
    evalAtom := if cfg.recursive then go false else fun e ‚Ü¶ pure { expr := e }
  go true e

open Elab.Tactic Parser.Tactic
/-- Use `abel_nf` to rewrite the main goal. -/
def abelNFTarget (s : IO.Ref AtomM.State) (cfg : AbelNF.Config) : TacticM Unit := withMainContext do
  let goal ‚Üê getMainGoal
  let tgt ‚Üê instantiateMVars (‚Üê goal.getType)
  let r ‚Üê abelNFCore s cfg tgt
  if r.expr.isConstOf ``True then
    goal.assign (‚Üê mkOfEqTrue (‚Üê r.getProof))
    replaceMainGoal []
  else
    replaceMainGoal [‚Üê applySimpResultToTarget goal tgt r]

/-- Use `abel_nf` to rewrite hypothesis `h`. -/
def abelNFLocalDecl (s : IO.Ref AtomM.State) (cfg : AbelNF.Config) (fvarId : FVarId) :
    TacticM Unit := withMainContext do
  let tgt ‚Üê instantiateMVars (‚Üê fvarId.getType)
  let goal ‚Üê getMainGoal
  let myres ‚Üê abelNFCore s cfg tgt
  match ‚Üê applySimpResultToLocalDecl goal fvarId myres false with
  | none => replaceMainGoal []
  | some (_, newGoal) => replaceMainGoal [newGoal]

/-- Unsupported legacy syntax from mathlib3, which allowed passing additional terms to `abel`. -/
syntax (name := abel_term) "abel" (&" raw" <|> &" term")? (location)? : tactic
/-- Unsupported legacy syntax from mathlib3, which allowed passing additional terms to `abel!`. -/
syntax (name := abel!_term) "abel!" (&" raw" <|> &" term")? (location)? : tactic

/--
Simplification tactic for expressions in the language of abelian groups,
which rewrites all group expressions into a normal form.
* `abel_nf!` will use a more aggressive reducibility setting to identify atoms.
* `abel_nf (config := cfg)` allows for additional configuration:
  * `red`: the reducibility setting (overridden by `!`)
  * `recursive`: if true, `abel_nf` will also recurse into atoms
* `abel_nf` works as both a tactic and a conv tactic.
  In tactic mode, `abel_nf at h` can be used to rewrite in a hypothesis.
-/
elab (name := abelNF) "abel_nf" tk:"!"? cfg:(config ?) loc:(location)? : tactic => do
  let mut cfg ‚Üê elabAbelNFConfig cfg
  if tk.isSome then cfg := { cfg with red := .default }
  let loc := (loc.map expandLocation).getD (.targets #[] true)
  let s ‚Üê IO.mkRef {}
  withLocation loc (abelNFLocalDecl s cfg) (abelNFTarget s cfg)
    fun _ ‚Ü¶ throwError "abel_nf failed"

@[inherit_doc abelNF] macro "abel_nf!" cfg:(config)? loc:(location)? : tactic =>
  `(tactic| abel_nf ! $(cfg)? $(loc)?)

@[inherit_doc abelNF] syntax (name := abelNFConv) "abel_nf" "!"? (config)? : conv

/-- Elaborator for the `abel_nf` tactic. -/
@[tactic abelNFConv] def elabAbelNFConv : Tactic := fun stx ‚Ü¶ match stx with
  | `(conv| abel_nf $[!%$tk]? $(_cfg)?) => withMainContext do
    let mut cfg ‚Üê elabAbelNFConfig stx[2]
    if tk.isSome then cfg := { cfg with red := .default }
    Conv.applySimpResult (‚Üê abelNFCore (‚Üê IO.mkRef {}) cfg (‚Üê instantiateMVars (‚Üê Conv.getLhs)))
  | _ => Elab.throwUnsupportedSyntax

@[inherit_doc abelNF] macro "abel_nf!" cfg:(config)? : conv => `(conv| abel_nf ! $(cfg)?)

/--
Tactic for evaluating expressions in abelian groups.

* `abel!` will use a more aggressive reducibility setting to determine equality of atoms.
* `abel1` fails if the target is not an equality.

For example:
```
example [AddCommMonoid Œ±] (a b : Œ±) : a + (b + a) = a + a + b := by abel
example [AddCommGroup Œ±] (a : Œ±) : (3 : ‚Ñ§) ‚Ä¢ a = a + (2 : ‚Ñ§) ‚Ä¢ a := by abel
```
-/
macro (name := abel) "abel" : tactic =>
  `(tactic| first | abel1 | abel_nf; trace "Try this: abel_nf")
@[inherit_doc abel] macro "abel!" : tactic =>
  `(tactic| first | abel1! | abel_nf!; trace "Try this: abel_nf!")

/--
The tactic `abel` evaluates expressions in abelian groups.
This is the conv tactic version, which rewrites a target which is an abel equality to `True`.

See also the `abel` tactic.
-/
macro (name := abelConv) "abel" : conv =>
  `(conv| first | discharge => abel1 | abel_nf; tactic => trace "Try this: abel_nf")
@[inherit_doc abelConv] macro "abel!" : conv =>
  `(conv| first | discharge => abel1! | abel_nf!; tactic => trace "Try this: abel_nf!")
