/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Mario Carneiro, Robert Y. Lewis
-/
import Mathlib.Tactic.Basic
import Mathlib.Tactic.NormCast
import Mathlib.Tactic.Zify
import Mathlib.Data.Rat.Cast

/-!
# `qify` tactic

The `qify` tactic is used to shift propositions from `â„•` or `â„¤` to `â„š`.
This is often useful since `â„š` has well-behaved division.
```
example (a b c x y z : â„•) (h : Â¬ x*y*z < 0) : c < a + 3*b := by
  qify
  qify at h
  /-
  h : Â¬â†‘x * â†‘y * â†‘z < 0
  âŠ¢ â†‘c < â†‘a + 3 * â†‘b
  -/
  sorry
```
-/

namespace Mathlib.Tactic.Qify

open Lean
open Lean.Meta
open Lean.Parser.Tactic
open Lean.Elab.Tactic

/--
The `qify` tactic is used to shift propositions from `â„•` or `â„¤` to `â„š`.
This is often useful since `â„š` has well-behaved division.
```
example (a b c x y z : â„•) (h : Â¬ x*y*z < 0) : c < a + 3*b := by
  qify
  qify at h
  /-
  h : Â¬â†‘x * â†‘y * â†‘z < 0
  âŠ¢ â†‘c < â†‘a + 3 * â†‘b
  -/
  sorry
```
`qify` can be given extra lemmas to use in simplification. This is especially useful in the
presence of nat subtraction: passing `â‰¤` arguments will allow `push_cast` to do more work.
```
example (a b c : â„¤) (h : a / b = c) (hab : b âˆ£ a) (hb : b â‰  0) : a = c * b := by
  qify [hab] at h hb âŠ¢
  exact (div_eq_iff hb).1 h
```
`qify` makes use of the `@[zify_simps]` and `@[qify_simps]` attributes to move propositions,
and the `push_cast` tactic to simplify the `â„š`-valued expressions. -/
syntax (name := qify) "qify" (simpArgs)? (location)? : tactic

macro_rules
| `(tactic| qify $[[$simpArgs,*]]? $[at $location]?) =>
  let args := simpArgs.map (Â·.getElems) |>.getD #[]
  `(tactic|
    simp (config := {decide := false}) only [zify_simps, qify_simps, push_cast, $args,*]
      $[at $location]?)

@[qify_simps] lemma int_cast_eq (a b : â„¤) : a = b â†” (a : â„š) = (b : â„š) := by simp only [Int.cast_inj]
                                                                            -- ðŸŽ‰ no goals
@[qify_simps] lemma int_cast_le (a b : â„¤) : a â‰¤ b â†” (a : â„š) â‰¤ (b : â„š) := Int.cast_le.symm
@[qify_simps] lemma int_cast_lt (a b : â„¤) : a < b â†” (a : â„š) < (b : â„š) := Int.cast_lt.symm
@[qify_simps] lemma int_cast_ne (a b : â„¤) : a â‰  b â†” (a : â„š) â‰  (b : â„š) := by
  simp only [ne_eq, Int.cast_inj]
  -- ðŸŽ‰ no goals
