/-
Copyright (c) 2025 Vasilii Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasilii Nesterov
-/
module

public import Mathlib.Topology.Algebra.Order.Field
public import Mathlib.Topology.Maps.Basic
public import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent

/-!
# TODO
-/

universe u v

@[expose] public section

open Filter Topology Asymptotics

namespace ComputeAsymptotics

variable {Î± : Type v} {ğ•œ : Type u} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  {l : Filter Î±} (f : ğ•œ â†’ Î±)

theorem tendsto_bot_of_tendsto_top (h : Tendsto (fun x â†¦ f (-x)) atTop l) :
    Tendsto f atBot l := by
  rw [show f = (f âˆ˜ Neg.neg) âˆ˜ Neg.neg by eta_expand; simp]
  exact Tendsto.comp h tendsto_neg_atBot_atTop

variable [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]

theorem tendsto_zero_right_of_tendsto_top (h : Tendsto (fun x â†¦ f xâ»Â¹) atTop l) :
    Tendsto f (ğ“[>] 0) l := by
  rw [show f = (f âˆ˜ Inv.inv) âˆ˜ Inv.inv by eta_expand; simp]
  apply Tendsto.comp h tendsto_inv_nhdsGT_zero

theorem tendsto_zero_left_of_tendsto_bot (h : Tendsto (fun x â†¦ f xâ»Â¹) atBot l) :
    Tendsto f (ğ“[<] 0) l := by
  rw [show f = (f âˆ˜ Inv.inv) âˆ˜ Inv.inv by eta_expand; simp]
  apply Tendsto.comp h tendsto_inv_nhdsLT_zero

theorem tendsto_zero_left_of_tendsto_top (h : Tendsto (fun x â†¦ f (-xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[<] 0) l := by
  conv at h => arg 1; ext x; arg 1; rw [show -xâ»Â¹ = (-x)â»Â¹ by ring]
  exact tendsto_zero_left_of_tendsto_bot _ (tendsto_bot_of_tendsto_top _ h)

theorem tendsto_zero_punctured_of_tendsto_top (h_pos : Tendsto (fun x â†¦ f xâ»Â¹) atTop l)
    (h_neg : Tendsto (fun x â†¦ f (-xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[â‰ ] 0) l := by
  rw [â† nhdsLT_sup_nhdsGT]
  apply Tendsto.sup
  Â· exact tendsto_zero_left_of_tendsto_top _ h_neg
  Â· exact tendsto_zero_right_of_tendsto_top _ h_pos

private def subHomeomorph {ğ•œ : Type u} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] (c : ğ•œ) : ğ•œ â‰ƒâ‚œ ğ•œ := by
  constructor
  pick_goal 3
  constructor
  pick_goal 3
  Â· exact fun x â†¦ x - c
  pick_goal 3
  Â· exact fun x â†¦ x + c
  Â· simp [Function.LeftInverse]
  Â· simp [Function.RightInverse, Function.LeftInverse]
  Â· exact continuous_sub_right c
  Â· exact continuous_add_right c

variable (c : ğ•œ)

theorem tendsto_nhds_right_of_tendsto_top (h : Tendsto (fun x â†¦ f (c + xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[>] c) l := by
  have : Tendsto (fun x â†¦ x - c) (ğ“[>] c) (ğ“[>] 0) := by
    simp only [Tendsto]
    convert le_refl _
    rw [IsEmbedding.map_nhdsWithin_eq]
    Â· simp
    Â· exact (subHomeomorph c).isEmbedding
  convert Tendsto.comp (g := fun x â†¦ f (c + x)) _ this
  Â· ext x
    simp
  exact tendsto_zero_right_of_tendsto_top _ h

theorem tendsto_nhds_left_of_tendsto_top (h : Tendsto (fun x â†¦ f (c - xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[<] c) l := by
  have : Tendsto (fun x â†¦ x - c) (ğ“[<] c) (ğ“[<] 0) := by
    simp only [Tendsto]
    convert le_refl _
    rw [IsEmbedding.map_nhdsWithin_eq]
    Â· simp
    Â· exact (subHomeomorph c).isEmbedding
  convert Tendsto.comp (g := fun x â†¦ f (c + x)) _ this
  Â· ext x
    simp
  apply tendsto_zero_left_of_tendsto_top
  convert h using 3
  ring

-- set_option maxHeartbeats 0 in
theorem tendsto_nhds_punctured_of_tendsto_top (h_neg : Tendsto (fun x â†¦ f (c - xâ»Â¹)) atTop l)
    (h_pos : Tendsto (fun x â†¦ f (c + xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[â‰ ] c) l := by
  have : Tendsto (fun x â†¦ x - c) (ğ“[â‰ ] c) (ğ“[â‰ ] 0) := by
    simp only [Tendsto]
    convert le_refl _
    rw [IsEmbedding.map_nhdsWithin_eq]
    Â· simp only [sub_self]
      congr
      rw [Set.image_compl_eq]
      Â· simp
      exact (subHomeomorph c).bijective
    Â· exact (subHomeomorph c).isEmbedding
  convert Tendsto.comp (g := fun x â†¦ f (c + x)) _ this
  Â· ext x
    simp
  apply tendsto_zero_punctured_of_tendsto_top
  Â· exact h_pos
  convert h_neg using 3
  ring

theorem tendsto_nhds_punctured_of_tendsto_top_nhds_of_eq
    [TopologicalSpace Î±]
    {a b : Î±}
    (h_neg : Tendsto (fun x â†¦ f (c - xâ»Â¹)) atTop (ğ“ a))
    (h_pos : Tendsto (fun x â†¦ f (c + xâ»Â¹)) atTop (ğ“ b))
    (h_eq : a = b) :
    Tendsto f (ğ“[â‰ ] c) (ğ“ a) := by
  apply tendsto_nhds_punctured_of_tendsto_top _ _ h_neg
  convert h_pos

-- TODO: generalize lemmas below

theorem isLittleO_of_tendsto_top {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atTop) :
    f =o[l] g := by
  rw [isLittleO_iff]
  intro c hc
  apply (Filter.Tendsto.eventually_ge_atTop h câ»Â¹).mono
  intro x hx
  replace hx : câ»Â¹ â‰¤ |g x / f x| := by
    apply hx.trans
    apply le_abs_self
  simp only [abs_div] at hx
  by_cases hf : f x = 0
  Â· simp [hf] at hx
    linarith
  rw [le_div_iffâ‚€ (abs_pos.mpr hf)] at hx
  exact (inv_mul_le_iffâ‚€ hc).mp hx

theorem isLittleO_of_tendsto_bot {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atBot) :
    f =o[l] g := by
  apply IsLittleO.of_neg_left
  apply isLittleO_of_tendsto_top
  rw [â† tendsto_neg_atBot_iff]
  convert h using 1
  ext x
  simp [div_neg_eq_neg_div]

theorem isBigO_of_tendsto_top {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atTop) :
    f =O[l] g :=
  Asymptotics.IsLittleO.isBigO (isLittleO_of_tendsto_top h)

theorem isBigO_of_tendsto_bot {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atBot) :
    f =O[l] g :=
  Asymptotics.IsLittleO.isBigO (isLittleO_of_tendsto_bot h)

theorem isBigO_of_tendsto_nhds {f g : â„ â†’ â„} {l : Filter â„} {c : â„}
    (h : Tendsto (fun x â†¦ g x / f x) l (ğ“ c)) (hc : c â‰  0) :
    f =O[l] g := by
  apply Asymptotics.isBigO_of_div_tendsto_nhds (c := câ»Â¹)
  Â· have : âˆ€á¶  x in l, g x / f x â‰  0 := Filter.Tendsto.eventually_ne h hc
    apply this.mono
    intro x hx hg
    simp [hg] at hx
  Â· rw [â† tendsto_inv_iffâ‚€]
    Â· convert h using 1
      Â· ext x
        simp
      Â· simp
    Â· simpa

-- TODO: upstream
theorem isEquivalent_of_tendsto_one {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ f x / g x) l (ğ“ 1)) :
    f ~[l] g := by
  apply Asymptotics.isEquivalent_of_tendsto_one _ h
  have : âˆ€á¶  x in l, 1 / 2 â‰¤ f x / g x := by
    apply Filter.Tendsto.eventually_const_le _ h
    norm_num
  apply this.mono
  intro x hx hg
  norm_num [hg] at hx

end ComputeAsymptotics
