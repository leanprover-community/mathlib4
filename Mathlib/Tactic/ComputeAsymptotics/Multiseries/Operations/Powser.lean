/-
Copyright (c) 2025 Vasilii Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasilii Nesterov
-/
module

public import Mathlib.Analysis.Calculus.FormalMultilinearSeries
public import Mathlib.Analysis.Analytic.Constructions
public import Mathlib.Analysis.Analytic.OfScalars
public import Mathlib.Tactic.ComputeAsymptotics.Multiseries.Basic
public import Mathlib.Tactic.ComputeAsymptotics.Multiseries.Operations.Mul
public import Mathlib.Tactic.ComputeAsymptotics.Multiseries.Trimming

/-!
# Substituting multiseries into analytic series

-/

set_option linter.flexible false
set_option linter.style.longLine false

@[expose] public section

open Filter Asymptotics Topology Stream'

namespace ComputeAsymptotics

namespace PreMS

/-- Lazy series is a sequence of real numbers constructed as a formal series
`c‚ÇÄ + c‚ÇÅ * x + c‚ÇÇ * x^2 + ...`. -/
abbrev LazySeries := Seq ‚Ñù

namespace LazySeries

open Seq

-- I do not know why it is necessary
/-- Recursion principle for lazy series. -/
@[cases_eliminator]
def recOn {motive : LazySeries ‚Üí Sort*} (s : LazySeries) (nil : motive Seq.nil)
    (cons : ‚àÄ x s, motive (Seq.cons x s)) :
    motive s :=
  Stream'.Seq.recOn s nil cons

/-- Lazy series defined by a function starting from `n`:
```
[f n, f (n + 1), f (n + 2), ...]
```
-/
def ofFnFrom (f : ‚Ñï ‚Üí ‚Ñù) (n : ‚Ñï) : LazySeries :=
  ‚ü®fun i ‚Ü¶ some (f (n + i)), by simp [IsSeq]‚ü©

/-- Lazy series defined by a function:
```
[f 0, f 1, f 2, ...]
```
-/
def ofFn (f : ‚Ñï ‚Üí ‚Ñù) : LazySeries :=
  ofFnFrom f 0

theorem ofFnFrom_eq_cons {f : ‚Ñï ‚Üí ‚Ñù} {n : ‚Ñï} :
    ofFnFrom f n = Seq.cons (f n) (ofFnFrom f (n + 1)) := by
  ext i x
  simp only [ofFnFrom, get?_mk, Option.some.injEq]
  cases i with
  | zero =>
    simp
  | succ i =>
    simp only [get?_cons_succ, get?_mk, Option.some.injEq]
    refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü© <;> (convert h using 2; ring)

@[simp]
theorem ofFnFrom_get {f : ‚Ñï ‚Üí ‚Ñù} {n m : ‚Ñï} : (ofFnFrom f n).get? m = some (f (n + m)) := by
  simp [ofFnFrom]

@[simp]
theorem ofFn_get {f : ‚Ñï ‚Üí ‚Ñù} {n : ‚Ñï} : (ofFn f).get? n = some (f n) := by
  convert ofFnFrom_get
  omega

-- theorems

/-- Coefficient of the lazy series at index `n`. -/
noncomputable def coeff (s : LazySeries) (n : ‚Ñï) : ‚Ñù :=
  (s.get? n).getD 0

/-- Converts a lazy series to a formal multilinear series. -/
noncomputable def toFormalMultilinearSeries (s : LazySeries) : FormalMultilinearSeries ‚Ñù ‚Ñù ‚Ñù :=
  .ofScalars ‚Ñù (coeff s)

@[simp]
theorem toFormalMultilinearSeries_coeff {s : LazySeries} {n : ‚Ñï} :
    s.toFormalMultilinearSeries.coeff n = (s.get? n).getD 0 := by
  unfold FormalMultilinearSeries.coeff toFormalMultilinearSeries
  eta_expand
  simp_rw [Pi.one_apply, FormalMultilinearSeries.ofScalars_apply_eq, coeff]
  simp

@[simp]
theorem toFormalMultilinearSeries_norm {s : LazySeries} {n : ‚Ñï} :
    ‚Äñ(s.toFormalMultilinearSeries) n‚Äñ = |(s.get? n).getD 0| := by
  simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef, Real.norm_eq_abs]
  congr
  exact toFormalMultilinearSeries_coeff

/-- Predicate stating that a lazy series converges on some non-trivial ball. -/
def Analytic (s : LazySeries) : Prop := 0 < s.toFormalMultilinearSeries.radius

open ENNReal in
theorem tail_radius_eq {s_hd : ‚Ñù} {s_tl : LazySeries} :
    (toFormalMultilinearSeries (.cons s_hd s_tl)).radius =
    s_tl.toFormalMultilinearSeries.radius := by
  apply le_antisymm
  ¬∑ refine le_of_forall_nnreal_lt (fun r hr ‚Ü¶ ?_)
    obtain ‚ü®C, ‚ü®_, h_bound‚ü©‚ü© := FormalMultilinearSeries.norm_mul_pow_le_of_lt_radius _ hr
    by_cases hr_pos : r = 0
    ¬∑ rw [hr_pos]
      simp
    replace hr_pos : 0 < r := lt_of_le_of_ne' (zero_le _) hr_pos
    apply FormalMultilinearSeries.le_radius_of_bound (C := C / r)
    intro n
    specialize h_bound (n + 1)
    simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef, toFormalMultilinearSeries_coeff,
      get?_cons_succ, Real.norm_eq_abs, pow_succ, ‚Üê mul_assoc] at h_bound ‚ä¢
    rwa [le_div_iff‚ÇÄ]
    simpa
  ¬∑ refine le_of_forall_nnreal_lt (fun r hr ‚Ü¶ ?_)
    obtain ‚ü®C, ‚ü®_, h_bound‚ü©‚ü© := FormalMultilinearSeries.norm_mul_pow_le_of_lt_radius _ hr
    by_cases hr_pos : r = 0
    ¬∑ rw [hr_pos]
      simp
    replace hr_pos : 0 < r := lt_of_le_of_ne' (zero_le _) hr_pos
    apply FormalMultilinearSeries.le_radius_of_bound (C := (C * r) ‚äî |s_hd|)
    intro n
    cases n with
    | zero =>
      simp [toFormalMultilinearSeries_coeff]
    | succ m =>
      specialize h_bound m
      simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef, toFormalMultilinearSeries_coeff,
        get?_cons_succ, Real.norm_eq_abs, le_sup_iff]
      left
      simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef, toFormalMultilinearSeries_coeff,
        Real.norm_eq_abs, pow_succ, ‚Üê mul_assoc] at h_bound ‚ä¢
      rw [‚Üê div_le_iff‚ÇÄ, mul_div_assoc, ‚Üê NNReal.coe_div, div_self hr_pos.ne.symm] <;> simpa

theorem cons_analytic {s_hd : ‚Ñù} {s_tl : LazySeries} (h_analytic : Analytic s_tl) :
    Analytic (.cons s_hd s_tl) := by
  simp only [Analytic]
  rw [tail_radius_eq]
  exact h_analytic

theorem tail_analytic {s_hd : ‚Ñù} {s_tl : LazySeries}
    (h_analytic : Analytic (.cons s_hd s_tl)) :
    Analytic s_tl := by
  simp only [Analytic] at *
  rwa [‚Üê tail_radius_eq]

/-- The function represented by a lazy series. -/
noncomputable def toFun (s : LazySeries) : ‚Ñù ‚Üí ‚Ñù :=
  s.toFormalMultilinearSeries.sum

@[simp]
theorem toFun_nil : toFun Seq.nil = 0 := by
  simp only [toFun]
  unfold toFormalMultilinearSeries coeff
  simp only [get?_nil, Option.getD_none]
  unfold FormalMultilinearSeries.ofScalars FormalMultilinearSeries.sum
  simp
  rfl

theorem toFun_cons {s_hd : ‚Ñù} {s_tl : LazySeries} {t : ‚Ñù}
    (h : Analytic (Seq.cons s_hd s_tl))
    (ht : t ‚àà EMetric.ball 0 (toFormalMultilinearSeries (Seq.cons s_hd s_tl)).radius) :
    toFun (.cons s_hd s_tl) t = s_hd + t * ((toFun s_tl) t) := by
  have h_tl := tail_analytic h
  have h_hsum := FormalMultilinearSeries.hasFPowerSeriesOnBall _ h
  replace h_hsum := HasFPowerSeriesOnBall.hasSum h_hsum ht
  have h_hsum_tl := FormalMultilinearSeries.hasFPowerSeriesOnBall _ h_tl
  replace h_hsum_tl := HasFPowerSeriesOnBall.hasSum h_hsum_tl (tail_radius_eq ‚ñ∏ ht)
  simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, Finset.prod_const, Finset.card_univ,
    Fintype.card_fin, smul_eq_mul, zero_add] at h_hsum h_hsum_tl
  simp_rw [toFormalMultilinearSeries_coeff] at h_hsum h_hsum_tl
  unfold toFun
  generalize (toFormalMultilinearSeries (Seq.cons s_hd s_tl)).sum t = a at *
  generalize (toFormalMultilinearSeries s_tl).sum t = b at *
  apply HasSum.unique h_hsum
  replace h_hsum_tl := HasSum.mul_right t h_hsum_tl
  ring_nf at h_hsum_tl
  conv at h_hsum_tl =>
    arg 1
    ext i
    rw [‚Üê pow_succ']
    rw [show Seq.get? s_tl i = Seq.get? (.cons s_hd s_tl) (i + 1) by simp]
  have := HasSum.zero_add (f := (fun n ‚Ü¶ t ^ n * (Seq.get? (Seq.cons s_hd s_tl) n).getD 0))
    h_hsum_tl
  convert this using 2
  simp

theorem toFun_cons_eventually_eq {s_hd : ‚Ñù} {s_tl : LazySeries}
    (h : Analytic (Seq.cons s_hd s_tl)) :
    toFun (.cons s_hd s_tl) =·∂†[ùìù 0] (fun t ‚Ü¶ s_hd + t * ((toFun s_tl) t)) := by
  rw [Filter.eventuallyEq_iff_exists_mem]
  use EMetric.ball 0 (toFormalMultilinearSeries (Seq.cons s_hd s_tl)).radius
  refine ‚ü®EMetric.ball_mem_nhds 0 h, fun t ht ‚Ü¶ ?_‚ü©
  rw [toFun_cons h ht]

theorem toFun_of_HasFPowerSeriesAt {s : LazySeries} {f : ‚Ñù ‚Üí ‚Ñù}
    (h : HasFPowerSeriesAt f s.toFormalMultilinearSeries 0) :
    s.toFun =·∂†[ùìù 0] f := by
  simp only [toFun]
  obtain ‚ü®r, h‚ü© := h
  rw [Filter.eventuallyEq_iff_exists_mem]
  use EMetric.ball 0 r
  constructor
  ¬∑ refine EMetric.ball_mem_nhds 0 h.r_pos
  simp only [Set.EqOn]
  intro x hx
  rw [‚Üê HasFPowerSeriesOnBall.sum h hx]
  simp

theorem analytic_of_HasFPowerSeriesAt {s : LazySeries} {f : ‚Ñù ‚Üí ‚Ñù}
    (h : HasFPowerSeriesAt f s.toFormalMultilinearSeries 0) :
    s.Analytic := HasFPowerSeriesAt.radius_pos h

theorem toFun_tendsto_head {s_hd : ‚Ñù} {s_tl : LazySeries}
    (h_analytic : Analytic (.cons s_hd s_tl)) :
    Tendsto (toFun (.cons s_hd s_tl)) (ùìù 0) (ùìù s_hd) := by
  have : (toFun (.cons s_hd s_tl)) 0 = s_hd := by
    simp only [toFun, FormalMultilinearSeries.sum, FormalMultilinearSeries.apply_eq_prod_smul_coeff,
      Finset.prod_const, Finset.card_univ, Fintype.card_fin, smul_eq_mul]
    rw [tsum_eq_zero_add']
    ¬∑ simp only [pow_zero, one_mul, ne_eq, AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false,
      not_false_eq_true, zero_pow, zero_mul, tsum_zero, add_zero]
      unfold toFormalMultilinearSeries
      simp [FormalMultilinearSeries.ofScalars, FormalMultilinearSeries.coeff, coeff]
    ¬∑ simpa using summable_zero
  conv =>
    arg 3
    rw [‚Üê this]
  apply ContinuousAt.tendsto
  have h_hsum := FormalMultilinearSeries.hasFPowerSeriesOnBall _ h_analytic
  replace h_hsum := HasFPowerSeriesOnBall.hasFPowerSeriesAt h_hsum
  exact HasFPowerSeriesAt.continuousAt h_hsum

theorem toFun_IsBigO_one {s : LazySeries} (h_analytic : s.Analytic) {f : ‚Ñù ‚Üí ‚Ñù}
    (hF : Tendsto f atTop (ùìù 0)) : ((toFun s) ‚àò f) =O[atTop] (1 : ‚Ñù ‚Üí ‚Ñù) := by
  cases s with
  | nil =>
    simp only [toFun_nil, Pi.zero_comp]
    apply isBigO_zero
  | cons s_hd s_tl =>
    apply isBigO_const_of_tendsto (y := s_hd) _ (by exact ne_zero_of_eq_one rfl)
    apply Tendsto.comp _ hF
    apply toFun_tendsto_head h_analytic

theorem toFun_majorated_zero {s : LazySeries} (h_analytic : s.Analytic) {f basis_hd : ‚Ñù ‚Üí ‚Ñù}
    (hf : Tendsto f atTop (ùìù 0)) (h_basis : Tendsto basis_hd atTop atTop) :
    majorated ((toFun s) ‚àò f) basis_hd 0 := by
  intro exp h_pos
  apply IsBigO.trans_isLittleO (toFun_IsBigO_one h_analytic hf)
  eta_expand
  simp only [Pi.one_apply, isLittleO_one_left_iff, Real.norm_eq_abs]
  apply Tendsto.comp Filter.tendsto_abs_atTop_atTop
  exact Tendsto.comp (tendsto_rpow_atTop h_pos) h_basis

theorem analytic_of_all_le_one {s : LazySeries} (h : ‚àÄ x ‚àà s, |x| ‚â§ 1) : s.Analytic := by
  simp only [Analytic]
  apply lt_of_lt_of_le (b := 1)
  ¬∑ simp
  apply FormalMultilinearSeries.le_radius_of_bound (C := 1)
  simp only [toFormalMultilinearSeries_norm]
  intro n
  cases h_get : s.get? n with
  | none => simp
  | some val =>
  simp only [Option.getD_some, NNReal.coe_one, one_pow, mul_one]
  apply h
  exact get?_mem h_get

end LazySeries

-- TODO: move
theorem mul_bounded_majorated {f g basis_hd : ‚Ñù ‚Üí ‚Ñù} {exp : ‚Ñù} (hf : majorated f basis_hd exp)
    (hg : g =O[atTop] (fun _ ‚Ü¶ (1 : ‚Ñù))) :
    majorated (f * g) basis_hd exp := by
  simp only [majorated] at *
  intro exp h_exp
  conv =>
    rhs; ext t; rw [‚Üê mul_one (basis_hd t ^ exp)]
  apply IsLittleO.mul_isBigO
  ¬∑ exact hf _ h_exp
  ¬∑ exact hg

/-- Applies a lazy series to a multiseries: `c0 * ms + c1 * ms^2 + c2 * ms^3 + ...`. -/
noncomputable def SeqMS.apply (s : LazySeries) {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    (ms : SeqMS basis_hd basis_tl) : SeqMS basis_hd basis_tl :=
  let T := LazySeries
  let g : T ‚Üí Option (‚Ñù √ó PreMS basis_tl √ó SeqMS basis_hd basis_tl √ó T) := fun s =>
    match s.destruct with
    | none => none
    | some (c, cs) =>
      some (0, PreMS.const _ c, ms, cs)
  SeqMS.gcorec g SeqMS.mul s

noncomputable def apply (s : LazySeries) {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    (ms : PreMS (basis_hd :: basis_tl)) : PreMS (basis_hd :: basis_tl) :=
  mk (SeqMS.apply s ms.seq) (s.toFun ‚àò ms.toFun)

@[simp]
theorem apply_toFun {s : LazySeries} {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    {ms : PreMS (basis_hd :: basis_tl)} :
    (apply s ms).toFun = s.toFun ‚àò ms.toFun :=
  rfl

@[simp]
theorem apply_seq {s : LazySeries} {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    {ms : PreMS (basis_hd :: basis_tl)} :
    (apply s ms).seq = SeqMS.apply s ms.seq :=
  rfl

@[simp]
theorem SeqMS.apply_nil {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis} {ms : SeqMS basis_hd basis_tl} :
    apply .nil ms = .nil := by
  simp [apply, SeqMS.gcorec_nil]

@[simp]
theorem SeqMS.apply_cons {s_hd : ‚Ñù} {s_tl : LazySeries}
    {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis} {ms : SeqMS basis_hd basis_tl} :
    (apply (.cons s_hd s_tl) ms) = .cons 0 (PreMS.const _ s_hd) (ms.mul (apply s_tl ms)) := by
  simp only [apply]
  rw [SeqMS.gcorec_some]
  rfl

theorem SeqMS.apply_leadingExp_le_zero {s : LazySeries} {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    {ms : SeqMS basis_hd basis_tl} :
    (apply s ms).leadingExp ‚â§ 0 := by
  cases s <;> simp

theorem SeqMS.apply_WellOrdered {s : LazySeries} {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    {ms : SeqMS basis_hd basis_tl}
    (h_wo : ms.WellOrdered) (h_neg : ms.leadingExp < 0) :
    (apply s ms).WellOrdered := by
  let motive (X : SeqMS basis_hd basis_tl) : Prop :=
    ‚àÉ s, X = apply s ms
  apply SeqMS.WellOrdered.mul_coind motive
  ¬∑ use s
  intro exp coef tl ‚ü®s, h_eq‚ü©
  cases s with
  | nil => simp at h_eq
  | cons s_hd s_tl =>
  simp only [apply_cons, SeqMS.cons_eq_cons] at h_eq
  simp only [h_eq, PreMS.const_WellOrdered, SeqMS.mul_leadingExp, WithBot.coe_zero, true_and]
  constructor
  ¬∑ generalize ms.leadingExp = x at *
    have : (apply s_tl ms).leadingExp ‚â§ 0 := apply_leadingExp_le_zero
    generalize (apply s_tl ms).leadingExp = y at *
    cases x <;> cases y <;> simp; norm_cast at this h_neg ‚ä¢; linarith
  exact ‚ü®_, _, rfl, h_wo, s_tl, rfl‚ü©

theorem apply_WellOrdered {s : LazySeries} {basis_hd : ‚Ñù ‚Üí ‚Ñù} {basis_tl : Basis}
    {ms : PreMS (basis_hd :: basis_tl)} (h_wo : ms.WellOrdered) (h_neg : ms.leadingExp < 0) :
    (apply s ms).WellOrdered := by
  simp at *
  exact SeqMS.apply_WellOrdered h_wo h_neg

theorem apply_Approximates {s : LazySeries} (h_analytic : s.Analytic) {basis_hd : ‚Ñù ‚Üí ‚Ñù}
    {basis_tl : Basis} {ms : PreMS (basis_hd :: basis_tl)}
    (h_basis : WellFormedBasis (basis_hd :: basis_tl))
    (h_neg : ms.leadingExp < 0) (h_wo : ms.WellOrdered)
    (h_approx : ms.Approximates) : (apply s ms).Approximates := by
  have hf_tendsto_zero : Tendsto ms.toFun atTop (ùìù 0) := by
    apply neg_leadingExp_tendsto_zero h_neg h_approx
  let motive (X : PreMS (basis_hd :: basis_tl)) : Prop :=
    ‚àÉ (s : LazySeries), s.Analytic ‚àß X ‚âà apply s ms
  apply Approximates.mul_coind h_basis motive (apply_WellOrdered h_wo h_neg)
  ¬∑ use s
  rintro X ‚ü®s, h_analytic, h_seq_eq, hf_eq‚ü©
  cases s with
  | nil =>
    simp at hf_eq
    simp [h_seq_eq, hf_eq]
  | cons s_hd s_tl =>
  right
  simp at hf_eq
  simp [h_seq_eq]
  have : LazySeries.toFun (Seq.cons s_hd s_tl) ‚àò ms.toFun =·∂†[atTop] (fun t ‚Ü¶ s_hd + t * ((LazySeries.toFun s_tl) t)) ‚àò ms.toFun := by
    apply Filter.EventuallyEq.comp_tendsto _ hf_tendsto_zero
    exact LazySeries.toFun_cons_eventually_eq h_analytic
  use ms, apply s_tl ms
  simp [const_Approximates h_basis.tail, h_approx, SeqMS.apply_WellOrdered (by simpa using h_wo) h_neg]
  constructorm* _ ‚àß _
  ¬∑ apply majorated_of_EventuallyEq hf_eq
    apply LazySeries.toFun_majorated_zero h_analytic hf_tendsto_zero
    apply basis_tendsto_top h_basis
    simp
  ¬∑ grw [hf_eq, this]
    apply EventuallyEq.of_eq
    ext t
    simp
  simp [motive]
  refine ‚ü®s_tl, LazySeries.tail_analytic h_analytic, rfl, by rfl‚ü©

section Zeros

/-- Infinite sequence of zeros. -/
def zeros : LazySeries := Seq.corec (fun () ‚Ü¶ some (0, ())) ()

open LazySeries

theorem zeros_eq_cons : zeros = .cons 0 zeros := by
  simp only [zeros]
  nth_rw 1 [Seq.corec_cons]
  rfl

@[simp]
theorem zeros_get {n : ‚Ñï} : zeros.get? n = .some 0 := by
  induction n with
  | zero =>
    rw [zeros_eq_cons]
    simp
  | succ =>
    rw [zeros_eq_cons]
    simpa

@[simp]
theorem zeros_toFun : zeros.toFun = 0 := by
  simp only [LazySeries.toFun, toFormalMultilinearSeries]
  unfold FormalMultilinearSeries.sum FormalMultilinearSeries.ofScalars
  simp [coeff, zeros_get]
  rfl

@[simp]
theorem zeros_radius : zeros.toFormalMultilinearSeries.radius = ‚ä§ := by
  apply FormalMultilinearSeries.radius_eq_top_of_summable_norm
  simp
  exact summable_zero

theorem zeros_analytic : Analytic zeros := by
  apply analytic_of_all_le_one
  intro x hx
  apply Seq.all_coind (fun s ‚Ü¶ s = zeros) _ _ _ hx
  ¬∑ rfl
  ¬∑ intro hd tl h_eq
    rw [zeros_eq_cons, Seq.cons_eq_cons] at h_eq
    rw [h_eq.left, h_eq.right]
    simp

-- I am almost sure we don't really need `h_wo` and `h_approx`
theorem zeros_apply_Approximates {basis_hd} {basis_tl} {ms : PreMS (basis_hd :: basis_tl)}
    (h_basis : WellFormedBasis (basis_hd :: basis_tl)) (h_wo : ms.WellOrdered)
    (h_approx : ms.Approximates) (h_neg : ms.leadingExp < 0) :
    (ms.apply zeros).Approximates :=
  apply_Approximates zeros_analytic h_basis h_neg h_wo h_approx

end Zeros


end PreMS

end ComputeAsymptotics
