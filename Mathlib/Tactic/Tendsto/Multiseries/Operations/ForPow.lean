import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.Analysis.SpecialFunctions.Pow.Deriv
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Tactic.MoveAdd
import Mathlib.Analysis.ODE.Gronwall
import Mathlib.RingTheory.Binomial
import Mathlib.Tactic.Linarith.Oracle.FourierMotzkin

set_option linter.style.longLine false

namespace TendstoTactic

namespace ForPow

theorem ascPochhammer_nonneg {a : â„} {n : â„•} (ha : 0 â‰¤ a) : 0 â‰¤ (ascPochhammer â„• n).smeval a := by
  cases n with
  | zero => simp
  | succ m =>
    simp [ascPochhammer_succ_right, Polynomial.smeval_mul, Polynomial.smeval_natCast]
    apply mul_nonneg
    Â· exact ascPochhammer_nonneg ha
    Â· linarith

theorem descPochhammer_bound_ascPochhammer {a : â„} {n : â„•} :
    |(descPochhammer â„¤ n).smeval a| â‰¤ (ascPochhammer â„• n).smeval |a| := by
  cases n with
  | zero => simp
  | succ m =>
    simp [ascPochhammer_succ_right, Polynomial.smeval_mul, Polynomial.smeval_natCast,
      descPochhammer_succ_right, abs_mul]
    apply mul_le_mul
    Â· exact descPochhammer_bound_ascPochhammer
    Â· convert abs_sub a (â†‘m)
      simp
    Â· simp
    Â· apply ascPochhammer_nonneg
      simp

open Filter Asymptotics

open scoped Nat

universe u v w

-- variable (ğ”¸ : Type*) [Ring ğ”¸] [Algebra â„ ğ”¸] [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]

noncomputable def binomialCoef (a : â„) (n : â„•) : â„ := Ring.choose a n

@[simp]
theorem binomialCoef_zero {a : â„} : binomialCoef a 0 = 1 := by
  simp [binomialCoef]

@[simp]
theorem binomialCoef_one {a : â„} : binomialCoef a 1 = a := by
  simp [binomialCoef]

noncomputable def binomialSeries (a : â„) : FormalMultilinearSeries â„ â„ â„ := fun n =>
  (binomialCoef a n) â€¢ ContinuousMultilinearMap.mkPiAlgebraFin â„ n â„

-- variable [NormedAddCommGroup ğ”¸] [NormedSpace â„ ğ”¸] [NormedAddCommGroup ğ”¸] [NormedSpace â„ ğ”¸]

lemma Ring.choose_eq_div {a : â„} {n : â„•} :
    Ring.choose a n = (descPochhammer â„¤ n).smeval a / n.factorial := by
  apply eq_div_of_mul_eq (by simp [Nat.factorial_ne_zero])
  convert_to n ! â€¢ Ring.choose a n = (descPochhammer â„¤ n).smeval a
  Â· ring
  rw [Ring.descPochhammer_eq_factorial_smul_choose]

theorem binomialSeries_radius_ge_one {a : â„} : 1 â‰¤ (binomialSeries a).radius := by
  apply le_of_forall_ge_of_dense
  intro r hr
  cases' r with r <;> simp at hr
  by_cases hr_pos : r = 0
  Â· simp [hr_pos]
  replace hr_pos : 0 < r := lt_of_le_of_ne (zero_le r) (by solve_by_elim)
  apply FormalMultilinearSeries.le_radius_of_isBigO
  have : âˆƒ M : â„•, |a| * r < M * (1 - r) := by
    conv => arg 1; ext M; rw [â† div_lt_iffâ‚€ (by simpa)]
    apply exists_nat_gt
  obtain âŸ¨M, hMâŸ© := this
  have : âˆ€ k, (ascPochhammer â„• (M + k)).smeval |a| / (M + k).factorial * r^k â‰¤
      (ascPochhammer â„• M).smeval |a| / M.factorial := by
    intro k
    induction k with
    | zero => simp
    | succ l ih =>
      simp [â† add_assoc, Nat.factorial, pow_succ, ascPochhammer_succ_right, Polynomial.smeval_mul,
        Polynomial.smeval_natCast] at ih âŠ¢
      convert_to (ascPochhammer â„• (M + l)).smeval |a| / â†‘(M + l)! * â†‘r ^ l * (r * (|a| + (â†‘M + â†‘l)) / (â†‘M + â†‘l + 1)) â‰¤ (ascPochhammer â„• M).smeval |a| / â†‘M !
      Â· simp only [div_eq_mul_inv, mul_inv_rev]
        ring_nf
      trans
      swap
      Â· exact ih
      apply mul_le_of_le_one_right
      Â· apply mul_nonneg
        Â· apply div_nonneg
          Â· apply ascPochhammer_nonneg
            simp
          Â· simp
        Â· simp
      rw [div_le_one (by linarith)]
      ring_nf at hM âŠ¢
      have : (r : â„) * l â‰¤ l := by -- for linarith
        apply mul_le_of_le_one_left
        Â· simp
        Â· simp
          exact hr.le
      linarith
  apply Asymptotics.IsBigO.of_bound (c := r^M * (ascPochhammer â„• M).smeval |a| / â†‘M.factorial)
  simp [binomialSeries]
  use M
  intro b hb
  replace hb := Nat.exists_eq_add_of_le hb
  obtain âŸ¨k, hkâŸ© := hb
  subst hk
  trans â€–binomialCoef a (M + k)â€– * â€–ContinuousMultilinearMap.mkPiAlgebraFin â„ (M + k) â„â€– * â†‘r ^ (M + k)
  Â· rw [mul_le_mul_right]
    Â· apply ContinuousMultilinearMap.opNorm_smul_le
    Â· apply pow_pos
      simpa
  simp [pow_add, div_eq_mul_inv]
  move_mul [r.toReal^M, r.toReal^M]
  apply mul_le_mul_of_nonneg_right _ (by simp)
  simp [binomialCoef, Ring.choose_eq_div, abs_div]
  trans (ascPochhammer â„• (M + k)).smeval |a| * (â†‘(M + k)!)â»Â¹ * â†‘r ^ k
  Â· simp only [div_eq_mul_inv]
    rw [mul_le_mul_right, mul_le_mul_right]
    Â· exact descPochhammer_bound_ascPochhammer
    Â· simp
      linarith [Nat.factorial_pos (M + k)]
    Â· apply pow_pos
      simpa
  simp only [div_eq_mul_inv] at this âŠ¢
  apply this

open ContinuousLinearMap FormalMultilinearSeries

theorem binomialSeries_ODE {a : â„} :
    let dSeries := (binomialSeries a).derivSeries
    a â€¢ binomialSeries a = (compFormalMultilinearSeries (ContinuousLinearMap.apply â„ â„ 1) dSeries) +
      dSeries.unshift 0
    := by
  have h_coeff : âˆ€ k, (binomialSeries a).coeff k = (binomialCoef a k) := by
    intro k
    unfold binomialSeries
    simp [coeff]
    rw [List.prod_eq_one] -- cringe
    Â· simp
    Â· simp [List.forall_mem_ofFn_iff]
  have h_deriv_coeff : âˆ€ k, ((binomialSeries a).derivSeries.coeff k) 1 = (binomialCoef a (k + 1)) * (k + 1) := by
    intro k
    simp [derivSeries]
    unfold coeff
    simp [changeOriginSeries, changeOriginSeriesTerm, h_coeff]
    rw [â† Finset.sum_mul, mul_comm]
    congr 2
    Â· ring
    conv => lhs; arg 2; ext; arg 2; ext; arg 2; change fun _ â†¦ 1
    have : âˆ€ x : Fin k âŠ• Fin 1, Sum.elim (1 : Fin k â†’ â„) (fun x â†¦ 1) x = 1 := by
      intro x
      cases x <;> simp
    conv => lhs; arg 2; ext; arg 2; ext x; rw [this]
    simp [add_comm 1 k]
  simp
  apply FormalMultilinearSeries.ext
  intro n
  simp
  cases n with
  | zero =>
    simp [FormalMultilinearSeries.unshift]
    simp [binomialSeries, binomialCoef]
    apply ContinuousMultilinearMap.ext
    intro m
    simp [h_deriv_coeff]
  | succ k =>
    apply ContinuousMultilinearMap.ext
    intro m
    simp only [ContinuousMultilinearMap.smul_apply, apply_eq_prod_smul_coeff, smul_eq_mul, unshift,
      Nat.succ_eq_add_one, ContinuousMultilinearMap.add_apply, compContinuousMultilinearMap_coe,
      Function.comp_apply, map_smul, apply_apply, continuousMultilinearCurryRightEquiv_symm_apply',
      coe_smul', Pi.smul_apply]
    conv => rhs; arg 2; arg 2; arg 2; rw [show m (Fin.last k) = m (Fin.last k) â€¢ 1 by simp]
    simp only [map_smul, Algebra.mul_smul_comm]
    simp [smul_eq_mul]
    ring_nf
    rw [show m (Fin.last k) * âˆ i : Fin k, Fin.init m i = âˆ i : Fin (k + 1), m i by rw [Fin.prod_univ_castSucc, mul_comm]; rfl]
    trans (âˆ i : Fin (k + 1), m i) * (((binomialSeries a).derivSeries.coeff (1 + k)) 1 +
        ((binomialSeries a).derivSeries.coeff k) 1)
    swap
    Â· ring
    move_mul [a]
    rw [mul_assoc, mul_eq_mul_left_iff]
    left
    simp [h_coeff, h_deriv_coeff]
    simp [binomialCoef, Ring.choose_eq_div]
    conv => rhs; arg 1; simp [descPochhammer_succ_right,  Polynomial.smeval_mul,
      Polynomial.smeval_natCast]
    rw [add_comm 1 k]
    trans (descPochhammer â„¤ (k + 1)).smeval a * ((a - (1 + â†‘k)) / â†‘(k + 1 + 1)! * (1 + â†‘k + 1) +
        1 / â†‘(k + 1)! * (â†‘k + 1))
    swap
    Â· ring_nf
    rw [div_eq_mul_inv, mul_assoc, mul_eq_mul_left_iff]
    left
    rw [Nat.factorial_succ (k + 1)]
    simp [div_eq_mul_inv]
    rw [mul_comm]
    have h : ((k + 1)! : â„) â‰  0 := fun h â†¦ Nat.factorial_ne_zero _ (Nat.cast_eq_zero.mp h)
    rw [mul_inv_eq_iff_eq_mulâ‚€ h]
    rw [add_mul]
    move_mul [((k + 1)!â»Â¹ : â„), ((k + 1)!â»Â¹ : â„)]
    rw [mul_inv_cancel_rightâ‚€ h, mul_inv_cancel_rightâ‚€ h]
    rw [show 1 + (k : â„) + 1 = k + 1 + 1 by ring]
    rw [inv_mul_cancel_rightâ‚€ (by linarith)]
    ring

noncomputable def binomialSum (a : â„) (x : â„) := (binomialSeries a).sum x

-- TODO: move
theorem HasFPowerSeriesOnBall.unique {ğ•œ : Type u} {E : Type v} {F : Type w} [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f g : E â†’ F}
    {p : FormalMultilinearSeries ğ•œ E F} {x : E} {r : ENNReal}
    (hf : HasFPowerSeriesOnBall f p x r)
    (hg : HasFPowerSeriesOnBall g p x r) :
    Set.EqOn f g (EMetric.ball x r) := by
  intro y hy
  have hf_sum := hf.hasSum_sub hy
  have hg_sum := hg.hasSum_sub hy
  apply HasSum.unique hf_sum hg_sum

-- TODO: move
theorem HasFPowerSeriesOnBall.smul {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type v}
    {F : Type w} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    {f : E â†’ F} {c : ğ•œ} {pf : FormalMultilinearSeries ğ•œ E F} {x : E} {r : ENNReal}
    (h : HasFPowerSeriesOnBall f pf x r) :
    HasFPowerSeriesOnBall (c â€¢ f) (c â€¢ pf) x r := by
  constructor
  Â· simp only [radius, FormalMultilinearSeries.smul_apply]
    trans
    Â· exact h.r_le
    simp only [radius]
    apply iSup_mono
    intro r
    apply iSup_mono'
    intro C
    use â€–câ€– * C
    apply iSup_mono'
    intro h
    simp
    intro n
    trans â€–câ€– * â€–pf nâ€– * (r : NNReal) ^ n
    Â· apply mul_le_mul_of_nonneg_right
      Â· apply ContinuousMultilinearMap.opNorm_smul_le
      Â· apply pow_nonneg
        simp
    rw [mul_assoc]
    apply mul_le_mul_of_nonneg_left
    Â· apply h
    Â· simp
  Â· exact h.r_pos
  Â· intro y hy
    apply HasSum.const_smul
    exact h.hasSum hy

-- TODO: move
theorem HasFPowerSeriesOnBall.unshift {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type u}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    {p : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F)} {r : ENNReal} {f : E â†’ (E â†’L[ğ•œ] F)} {x : E} [CompleteSpace F]
    (h : HasFPowerSeriesOnBall f p x r) {z : F} :
    HasFPowerSeriesOnBall (fun y â†¦ z + f y (y - x)) (p.unshift z) x r := by
  constructor
  Â· simp only [radius]
    trans
    Â· exact h.r_le
    simp only [radius]
    apply iSup_mono
    intro r
    apply iSup_mono'
    intro C
    use C * r + â€–zâ€–
    apply iSup_mono'
    intro h
    simp
    intro n
    have hC : 0 â‰¤ C := by
      specialize h 0
      simp at h
      trans â€–p 0â€–
      Â· apply ContinuousMultilinearMap.opNorm_nonneg
      Â· exact h
    cases' n with k
    Â· simp [FormalMultilinearSeries.unshift]
      apply mul_nonneg hC
      simp
    Â· simp [FormalMultilinearSeries.unshift, pow_succ]
      trans C * r
      Â· rw [â† mul_assoc]
        apply mul_le_mul_of_nonneg_right _ (by simp)
        apply h
      Â· simp
  Â· exact h.r_pos
  Â· intro y hy
    apply HasSum.zero_add
    simp_rw [FormalMultilinearSeries.unshift.eq_2]
    simp
    conv => arg 1; ext n; arg 1; arg 2; change fun _ â†¦ y
    have := ContinuousLinearMap.hasSum (ContinuousLinearMap.apply ğ•œ F y) (h.hasSum hy)
    simpa using this

theorem binomialSum_ODE {a : â„} {x : â„} (hx : |x| < 1) :
    HasDerivAt (binomialSum a) (a * binomialSum a x / (1 + x)) x := by
  have h_fun : HasFPowerSeriesOnBall (binomialSum a) (binomialSeries a) 0 1 := by
    apply HasFPowerSeriesOnBall.mono _ (by simp) (@binomialSeries_radius_ge_one a)
    apply FormalMultilinearSeries.hasFPowerSeriesOnBall
    apply lt_of_lt_of_le _ binomialSeries_radius_ge_one
    simp
  have h_afun : HasFPowerSeriesOnBall (a â€¢ binomialSum a) (a â€¢ binomialSeries a) 0 1 := by
    exact HasFPowerSeriesOnBall.smul h_fun
  have h_fderiv := HasFPowerSeriesOnBall.fderiv h_fun
  have h_deriv : HasFPowerSeriesOnBall (deriv (binomialSum a))
    (compFormalMultilinearSeries (ContinuousLinearMap.apply â„ â„ 1) (binomialSeries a).derivSeries) 0 1 := by
    convert ContinuousLinearMap.comp_hasFPowerSeriesOnBall _ h_fderiv
    rfl
  have h_xfderiv : HasFPowerSeriesOnBall (fun x â†¦ fderiv â„ (binomialSum a) x x) ((binomialSeries a).derivSeries.unshift 0) 0 1 := by
    convert HasFPowerSeriesOnBall.unshift h_fderiv using 1
    ext y
    simp
  have h_xderiv : HasFPowerSeriesOnBall (fun x â†¦ x * deriv (binomialSum a) x) ((binomialSeries a).derivSeries.unshift 0) 0 1 := by
    convert h_xfderiv using 1
    ext x
    conv => rhs; arg 2; rw [show x = x â€¢ 1 by simp]
    simp
  rw [binomialSeries_ODE] at h_afun
  have h_rhs := HasFPowerSeriesOnBall.add h_deriv h_xderiv
  have := HasFPowerSeriesOnBall.unique h_afun h_rhs
  have hx_mem : x âˆˆ EMetric.ball 0 1 := by
    simp [EMetric.ball]
    have := coe_nnnorm x
    rw [â† NNReal.coe_lt_coe, coe_nnnorm x]
    rw [Real.norm_eq_abs, NNReal.coe_one]
    rw [abs_lt]
    constructor <;> linarith [le_abs_self x, neg_abs_le x]
  specialize this hx_mem
  simp at this
  convert_to a * binomialSum a x = (1 + x) * deriv (binomialSum a) x at this
  Â· ring
  rw [this, mul_comm, mul_div_cancel_rightâ‚€]
  swap
  Â· linarith [neg_abs_le x]
  apply DifferentiableAt.hasDerivAt
  apply DifferentiableOn.differentiableAt
  pick_goal 2
  Â· refine IsOpen.mem_nhds ?h.hs.hs hx_mem
    exact EMetric.isOpen_ball
  apply AnalyticOnNhd.differentiableOn
  apply HasFPowerSeriesOnBall.analyticOnNhd
  exact h_fun

-- TODO: move
theorem ODE_solution_unique_of_mem_Icc' {E : Type u} [NormedAddCommGroup E] [NormedSpace â„ E]
    {v : â„ â†’ E â†’ E} {s : â„ â†’ Set E} {K : NNReal} {f g : â„ â†’ E} {a b tâ‚€ : â„}
    (hv : âˆ€ t âˆˆ Set.Ioo a b, LipschitzOnWith K (v t) (s t)) (ht : tâ‚€ âˆˆ Set.Ioo a b)
    (hf : ContinuousOn f (Set.Icc a b)) (hf' : âˆ€ t âˆˆ Set.Ioo a b, HasDerivAt f (v t (f t)) t)
    (hfs : âˆ€ t âˆˆ Set.Ioo a b, f t âˆˆ s t) (hg : ContinuousOn g (Set.Icc a b))
    (hg' : âˆ€ t âˆˆ Set.Ioo a b, HasDerivAt g (v t (g t)) t)
    (hgs : âˆ€ t âˆˆ Set.Ioo a b, g t âˆˆ s t) (heq : f tâ‚€ = g tâ‚€) :
    Set.EqOn f g (Set.Icc a b) := by
  let v' : â„ â†’ E â†’ E := fun t x â†¦ if t âˆˆ Set.Ioo a b then v t x else 0
  apply ODE_solution_unique_of_mem_Icc (v := v') (s := s) (tâ‚€ := tâ‚€) (K := K)
  all_goals try assumption
  Â· intro t
    by_cases h : t âˆˆ Set.Ioo a b
    Â· simp only [v', h]
      simp
      apply hv _ h
    Â· simp only [v', h]
      simp
      apply LipschitzWith.lipschitzOnWith
      apply LipschitzWith.const'
  Â· intro t ht
    simp only [v', ht]
    simp
    apply hf' _ ht
  Â· intro t ht
    simp only [v', ht]
    simp
    apply hg' _ ht

theorem binomialSum_eq_rpow_aux {a : â„} {Îµ : â„} (hÎµ : 0 < Îµ) : Set.EqOn (binomialSum a) (fun x â†¦ (1 + x)^a) (Set.Icc (-1 + Îµ) (1 - Îµ)) := by
  have binomialSum_zero : binomialSum a 0 = 1 := by
    simp [binomialSum, FormalMultilinearSeries.sum]
    rw [tsum_eq_zero_add']
    Â· simp
      unfold FormalMultilinearSeries.coeff binomialSeries
      simp [binomialCoef]
    Â· simp
      exact summable_zero
  rcases lt_trichotomy Îµ 1 with (hÎµ' | hÎµ' | hÎµ')
  rotate_left
  Â· simp [hÎµ', binomialSum_zero]
  Â· convert Set.eqOn_empty _ _
    apply Set.Icc_eq_empty
    linarith
  have h_sum_analytic : AnalyticOnNhd â„ (binomialSum a) (EMetric.ball 0 1) := by
    apply AnalyticOnNhd.mono _ (EMetric.ball_subset_ball (@binomialSeries_radius_ge_one a))
    unfold binomialSum
    apply HasFPowerSeriesOnBall.analyticOnNhd (p := binomialSeries a)
    apply FormalMultilinearSeries.hasFPowerSeriesOnBall
    apply lt_of_lt_of_le _ binomialSeries_radius_ge_one
    simp
  let v : â„ â†’ â„ â†’ â„ := fun t x â†¦ a * x / (1 + t)
  let s : â„ â†’ Set â„ := fun _ â†¦ Set.univ
  apply ODE_solution_unique_of_mem_Icc' (v := v) (s := s) (tâ‚€ := 0)
    (K := âŸ¨|a| / Îµ, by apply div_nonneg (by simp); linarithâŸ©)
  Â· intro t ht
    simp at ht
    simp [s, v]
    apply LipschitzWith.weaken (K := âŸ¨|a| / (1 + t), by apply div_nonneg (by simp); linarithâŸ©)
    conv => arg 2; ext x; rw [mul_comm, â† mul_div, mul_comm]; change (a / (1 + t)) â€¢ x
    convert lipschitzWith_smul _ <;> try infer_instance
    Â· simp [nnnorm]
      rw [Subtype.eq_iff]
      simp
      rw [abs_of_nonneg (a := 1 + t)]
      linarith
    Â· rw [â† NNReal.coe_le_coe]
      simp
      exact div_le_div_of_nonneg_left (by simp) hÎµ (by linarith)
  Â· simpa
  Â· apply ContinuousOn.mono (s := EMetric.ball 0 (binomialSeries a).radius)
    Â· unfold binomialSum
      convert FormalMultilinearSeries.continuousOn
      infer_instance -- why asked?
    Â· intro x hx
      simp at hx
      simp [EMetric.ball]
      have := coe_nnnorm x
      apply lt_of_lt_of_le _ binomialSeries_radius_ge_one
      rw [â† ENNReal.coe_one, ENNReal.coe_one, ENNReal.coe_lt_one_iff]
      rw [â† NNReal.coe_lt_coe, coe_nnnorm x]
      rw [Real.norm_eq_abs, NNReal.coe_one]
      rw [abs_lt]
      constructor <;> linarith
  Â· intro t ht
    simp [v]
    apply binomialSum_ODE
    simp at ht
    rw [abs_lt]
    constructor <;> linarith
  Â· simp [s]
  Â· apply ContinuousOn.rpow_const
    Â· apply ContinuousOn.add
      Â· exact continuousOn_const
      Â· apply continuousOn_id
    Â· intro x hx
      left
      simp at hx
      linarith
  Â· intro t ht
    simp at ht
    simp [v]
    conv => arg 2; rw [â† mul_div, â† Real.rpow_sub_one (by linarith)]
    have := HasDerivAt.rpow_const (f := fun x â†¦ 1 + x) (p := a) (x := t) (f' := 1)
    simp only [one_mul] at this
    apply this
    Â· apply HasDerivAt.const_add
      apply hasDerivAt_id
    Â· left
      linarith
  Â· simp [s]
  Â· simp [binomialSum_zero]

theorem binomialSum_eq_rpow {a x : â„} (hx : |x| < 1) : binomialSum a x = (1 + x)^a := by
  let Îµ := (1 - |x|) / 2
  have hÎµ : 0 < Îµ := by dsimp [Îµ]; linarith
  have := binomialSum_eq_rpow_aux (a := a) hÎµ
  apply this
  simp
  rw [abs_lt] at hx
  dsimp [Îµ]
  constructor <;> linarith [le_abs_self x, neg_abs_le x]

end ForPow

end TendstoTactic
