/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import ProofWidgets.Component.PenroseDiagram
import ProofWidgets.Presentation.Expr
import Mathlib.Tactic.CategoryTheory.Coherence


/-!
# String Diagrams

This file provides tactic/meta infrastructure for displaying string diagrams for morphisms
in monoidal categories in the infoview.

-/

namespace Mathlib.Tactic.Widget.StringDiagram

open Lean Meta Elab
open CategoryTheory
open Mathlib.Tactic.Coherence

/-- Expressions for atomic 1-morphisms. -/
structure Atom‚ÇÅ : Type where
  /-- Extract a Lean expression from an `Atom‚ÇÅ` expression. -/
  e : Expr

/-- Expressions for 1-morphisms. -/
inductive Mor‚ÇÅ : Type
  /-- `id` is the expression for `ùüô_ C`. -/
  | id : Mor‚ÇÅ
  /-- `comp X Y` is the expression for `X ‚äó Y` -/
  | comp : Mor‚ÇÅ ‚Üí Mor‚ÇÅ ‚Üí Mor‚ÇÅ
  /-- Construct the expression for an atomic 1-morphism. -/
  | of : Atom‚ÇÅ ‚Üí Mor‚ÇÅ
  deriving Inhabited

/-- Converts a 1-morphism into a list of its underlying expressions. -/
def Mor‚ÇÅ.toList : Mor‚ÇÅ ‚Üí List Expr
  | .id => []
  | .comp f g => f.toList ++ g.toList
  | .of f => [f.e]

/-- Returns `ùüô_ C` if the expression `e` is of the form `ùüô_ C`. -/
def isTensorUnit? (e : Expr) : MetaM (Option Expr) := do
  let v ‚Üê mkFreshLevelMVar
  let u ‚Üê mkFreshLevelMVar
  let C ‚Üê mkFreshExprMVar none
  let instC ‚Üê mkFreshExprMVar none
  let instMC ‚Üê mkFreshExprMVar none
  let unit := mkAppN (.const ``MonoidalCategoryStruct.tensorUnit [v, u]) #[C, instC, instMC]
  if ‚Üê isDefEq e unit then
    return ‚Üê instantiateMVars unit
  else
    return none

/-- Returns `(f, g)` if the expression `e` is of the form `f ‚äó g`. -/
def isTensorObj? (e : Expr) : MetaM (Option (Expr √ó Expr)) := do
  let v ‚Üê mkFreshLevelMVar
  let u ‚Üê mkFreshLevelMVar
  let C ‚Üê mkFreshExprMVar none
  let f ‚Üê mkFreshExprMVar C
  let g ‚Üê mkFreshExprMVar C
  let instC ‚Üê mkFreshExprMVar none
  let instMC ‚Üê mkFreshExprMVar none
  let fg := mkAppN (.const ``MonoidalCategoryStruct.tensorObj [v, u]) #[C, instC, instMC, f, g]
  if ‚Üê withDefault <| isDefEq e fg then
    return (‚Üê instantiateMVars f, ‚Üê instantiateMVars g)
  else
    return none

/-- Construct a `Mor‚ÇÅ` expression from a Lean expression. -/
partial def toMor‚ÇÅ (e : Expr) : MetaM Mor‚ÇÅ := do
  if let some _ ‚Üê isTensorUnit? e then
    return Mor‚ÇÅ.id
  else if let some (f, g) ‚Üê isTensorObj? e then
    return (‚Üê toMor‚ÇÅ f).comp (‚Üê toMor‚ÇÅ g)
  else
    return Mor‚ÇÅ.of ‚ü®e‚ü©

/-- Expressions for atomic structural 2-morphisms. -/
inductive StructuralAtom : Type
  /-- The expression for the associator `(Œ±_ f g h).hom`. -/
  | associator (f g h : Mor‚ÇÅ) : StructuralAtom
  /-- The expression for the inverse of the associator `(Œ±_ f g h).inv`. -/
  | associatorInv (f g h : Mor‚ÇÅ) : StructuralAtom
  /-- The expression for the left unitor `(Œª_ f).hom`. -/
  | leftUnitor (f : Mor‚ÇÅ) : StructuralAtom
  /-- The expression for the inverse of the left unitor `(Œª_ f).inv`. -/
  | leftUnitorInv (f : Mor‚ÇÅ) : StructuralAtom
  /-- The expression for the right unitor `(œÅ_ f).hom`. -/
  | rightUnitor (f : Mor‚ÇÅ) : StructuralAtom
  /-- The expression for the inverse of the right unitor `(œÅ_ f).inv`. -/
  | rightUnitorInv (f : Mor‚ÇÅ) : StructuralAtom
  deriving Inhabited

/-- Construct a `StructuralAtom` expression from a Lean expression. -/
def structuralAtom? (e : Expr) : MetaM (Option StructuralAtom) := do
  match e.getAppFnArgs with
  | (``Iso.hom, #[_, _, _, _, Œ∑]) =>
    match (‚Üê whnfR Œ∑).getAppFnArgs with
    | (``MonoidalCategoryStruct.associator, #[_, _, _, f, g, h]) =>
      return some <| .associator (‚Üê toMor‚ÇÅ f) (‚Üê toMor‚ÇÅ g) (‚Üê toMor‚ÇÅ h)
    | (``MonoidalCategoryStruct.leftUnitor, #[_, _, _, f]) => return some <| .leftUnitor (‚Üê toMor‚ÇÅ f)
    | (``MonoidalCategoryStruct.rightUnitor, #[_, _, _, f]) => return some <| .rightUnitor (‚Üê toMor‚ÇÅ f)
    | _ => return none
  | (``Iso.inv, #[_, _, _, _, Œ∑]) =>
    match (‚Üê whnfR Œ∑).getAppFnArgs with
    | (``MonoidalCategoryStruct.associator, #[_, _, _, f, g, h]) =>
      return some <| .associatorInv (‚Üê toMor‚ÇÅ f) (‚Üê toMor‚ÇÅ g) (‚Üê toMor‚ÇÅ h)
    | (``MonoidalCategoryStruct.leftUnitor, #[_, _, _, f]) => return some <| .leftUnitorInv (‚Üê toMor‚ÇÅ f)
    | (``MonoidalCategoryStruct.rightUnitor, #[_, _, _, f]) => return some <| .rightUnitorInv (‚Üê toMor‚ÇÅ f)
    | _ => return none
  | _ => return none

/-- Expressions for atomic non-structural 2-morphisms. -/
structure Atom where
  /-- Extract a Lean expression from an `Atom` expression. -/
  e : Expr
  deriving Inhabited

/-- Expressions of the form `Œ∑ ‚ñ∑ f‚ÇÅ ‚ñ∑ ... ‚ñ∑ f‚Çô`. -/
inductive WhiskerRightExpr : Type
  /-- Construct the expression for an atomic 2-morphism. -/
  | of (Œ∑ : Atom) : WhiskerRightExpr
  /-- Construct the expression for `Œ∑ ‚ñ∑ f`. -/
  | whisker (Œ∑ : WhiskerRightExpr) (f : Atom‚ÇÅ) : WhiskerRightExpr
  deriving Inhabited

/-- Expressions of the form `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑`. -/
inductive WhiskerLeftExpr : Type
  /-- Construct the expression for a right-whiskered 2-morphism. -/
  | of (Œ∑ : WhiskerRightExpr) : WhiskerLeftExpr
  /-- Construct the expression for `f ‚óÅ Œ∑`. -/
  | whisker (f : Atom‚ÇÅ) (Œ∑ : WhiskerLeftExpr) : WhiskerLeftExpr
  deriving Inhabited

/-- Expressions for structural 2-morphisms. -/
inductive Structural : Type
  /-- Expressions for atomic structural 2-morphisms. -/
  | atom (Œ∑ : StructuralAtom) : Structural
  /-- Expressions for the identity `ùüô f`. -/
  | id (f : Mor‚ÇÅ) : Structural
  /-- Expressions for the composition `Œ∑ ‚â´ Œ∏`. -/
  | comp (Œ± Œ≤ : Structural) : Structural
  /-- Expressions for the left whiskering `f ‚óÅ Œ∑`. -/
  | whiskerLeft (f : Mor‚ÇÅ) (Œ∑ : Structural) : Structural
  /-- Expressions for the right whiskering `Œ∑ ‚ñ∑ f`. -/
  | whiskerRight (Œ∑ : Structural) (f : Mor‚ÇÅ) : Structural
  /-- Expressions for `Œ±` in the monoidal composition `Œ∑ ‚äó‚â´ Œ∏ := Œ∑ ‚â´ Œ± ‚â´ Œ∏`. -/
  | monoidalCoherence (f g : Mor‚ÇÅ) (e : Expr) : Structural
  deriving Inhabited

/-- Normalized expressions for 2-morphisms. -/
inductive NormalExpr : Type
  /-- Construct the expression for a structural 2-morphism. -/
  | nil (Œ± : Structural) : NormalExpr
  /-- Construct the normalized expression of 2-morphisms recursively. -/
  | cons (head_structural : Structural) (head : WhiskerLeftExpr) (tail : NormalExpr) : NormalExpr
  deriving Inhabited

/-- The domain of a morphism. -/
def src (Œ∑ : Expr) : MetaM Mor‚ÇÅ := do
  match (‚Üê inferType Œ∑).getAppFnArgs with
  | (``Quiver.Hom, #[_, _, f, _]) => toMor‚ÇÅ f
  | _ => throwError "{Œ∑} is not a morphism"

/-- The codomain of a morphism. -/
def tar (Œ∑ : Expr) : MetaM Mor‚ÇÅ := do
  match (‚Üê inferType Œ∑).getAppFnArgs with
  | (``Quiver.Hom, #[_, _, _, g]) => toMor‚ÇÅ g
  | _ => throwError "{Œ∑} is not a morphism"

/-- The domain of a 2-morphism. -/
def Atom.src (Œ∑ : Atom) : MetaM Mor‚ÇÅ := do StringDiagram.src Œ∑.e
/-- The codomain of a 2-morphism. -/
def Atom.tar (Œ∑ : Atom) : MetaM Mor‚ÇÅ := do StringDiagram.tar Œ∑.e

/-- The domain of a 2-morphism. -/
def WhiskerRightExpr.src : WhiskerRightExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerRightExpr.of Œ∑ => Œ∑.src
  | WhiskerRightExpr.whisker Œ∑ f => return (‚Üê WhiskerRightExpr.src Œ∑).comp (Mor‚ÇÅ.of f)

/-- The codomain of a 2-morphism. -/
def WhiskerRightExpr.tar : WhiskerRightExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerRightExpr.of Œ∑ => Œ∑.tar
  | WhiskerRightExpr.whisker Œ∑ f => return (‚Üê WhiskerRightExpr.tar Œ∑).comp (Mor‚ÇÅ.of f)

/-- The domain of a 2-morphism. -/
def WhiskerLeftExpr.src : WhiskerLeftExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerLeftExpr.of Œ∑ => WhiskerRightExpr.src Œ∑
  | WhiskerLeftExpr.whisker f Œ∑ => return (Mor‚ÇÅ.of f).comp (‚Üê WhiskerLeftExpr.src Œ∑)

/-- The codomain of a 2-morphism. -/
def WhiskerLeftExpr.tar : WhiskerLeftExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerLeftExpr.of Œ∑ => WhiskerRightExpr.tar Œ∑
  | WhiskerLeftExpr.whisker f Œ∑ => return (Mor‚ÇÅ.of f).comp (‚Üê WhiskerLeftExpr.tar Œ∑)

/-- The domain of a 2-morphism. -/
def StructuralAtom.src : StructuralAtom ‚Üí Mor‚ÇÅ
  | .associator f g h => (f.comp g).comp h
  | .associatorInv f g h => f.comp (g.comp h)
  | .leftUnitor f => Mor‚ÇÅ.id.comp f
  | .leftUnitorInv f => f
  | .rightUnitor f => f.comp Mor‚ÇÅ.id
  | .rightUnitorInv f => f

/-- The codomain of a 2-morphism. -/
def StructuralAtom.tar : StructuralAtom ‚Üí Mor‚ÇÅ
  | .associator f g h => f.comp (g.comp h)
  | .associatorInv f g h => (f.comp g).comp h
  | .leftUnitor f => f
  | .leftUnitorInv f => Mor‚ÇÅ.id.comp f
  | .rightUnitor f => f
  | .rightUnitorInv f => f.comp Mor‚ÇÅ.id

/-- The domain of a 2-morphism. -/
def Structural.src : Structural ‚Üí Mor‚ÇÅ
  | .atom Œ∑ => Œ∑.src
  | .id f => f
  | .comp Œ± _ => Œ±.src
  | .whiskerLeft f Œ∑ => f.comp Œ∑.src
  | .whiskerRight Œ∑ f => Œ∑.src.comp f
  | .monoidalCoherence f _ _ => f

/-- The codomain of a 2-morphism. -/
def Structural.tar : Structural ‚Üí Mor‚ÇÅ
  | .atom Œ∑ => Œ∑.tar
  | .id f => f
  | .comp _ Œ≤ => Œ≤.tar
  | .whiskerLeft f Œ∑ => f.comp Œ∑.tar
  | .whiskerRight Œ∑ f => Œ∑.tar.comp f
  | .monoidalCoherence _ g _ => g

/-- The domain of a 2-morphism. -/
def NormalExpr.src : NormalExpr ‚Üí Mor‚ÇÅ
  | NormalExpr.nil Œ∑ => Œ∑.src
  | NormalExpr.cons Œ± _ _ => Œ±.src

/-- The codomain of a 2-morphism. -/
def NormalExpr.tar : NormalExpr ‚Üí Mor‚ÇÅ
  | NormalExpr.nil Œ∑ => Œ∑.tar
  | NormalExpr.cons _ _ Œ∑s => Œ∑s.tar

/-- The associator as a term of `normalExpr`. -/
def NormalExpr.associator (f g h : Mor‚ÇÅ) : NormalExpr :=
  .nil <| .atom <| .associator f g h

/-- The inverse of the associator as a term of `normalExpr`. -/
def NormalExpr.associatorInv (f g h : Mor‚ÇÅ) : NormalExpr :=
  .nil <| .atom <| .associatorInv f g h

/-- The left unitor as a term of `normalExpr`. -/
def NormalExpr.leftUnitor (f : Mor‚ÇÅ) : NormalExpr :=
  .nil <| .atom <| .leftUnitor f

/-- The inverse of the left unitor as a term of `normalExpr`. -/
def NormalExpr.leftUnitorInv (f : Mor‚ÇÅ) : NormalExpr :=
  .nil <| .atom <| .leftUnitorInv f

/-- The right unitor as a term of `normalExpr`. -/
def NormalExpr.rightUnitor (f : Mor‚ÇÅ) : NormalExpr :=
  .nil <| .atom <| .rightUnitor f

/-- The inverse of the right unitor as a term of `normalExpr`. -/
def NormalExpr.rightUnitorInv (f : Mor‚ÇÅ) : NormalExpr :=
  .nil <| .atom <| .rightUnitorInv f

/-- Return `Œ∑` for `Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def WhiskerRightExpr.atom : WhiskerRightExpr ‚Üí Atom
  | WhiskerRightExpr.of Œ∑ => Œ∑
  | WhiskerRightExpr.whisker Œ∑ _ => Œ∑.atom

/-- Return `Œ∑` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def WhiskerLeftExpr.atom : WhiskerLeftExpr ‚Üí Atom
  | WhiskerLeftExpr.of Œ∑ => Œ∑.atom
  | WhiskerLeftExpr.whisker _ Œ∑ => Œ∑.atom

/-- Construct a `Structural` expression from a Lean expression for a structural 2-morphism. -/
partial def structural? (e : Expr) : MetaM Structural := do
  match (‚Üê whnfR e).getAppFnArgs with
  | (``CategoryStruct.comp, #[_, _, _, Œ±, Œ≤]) =>
    return .comp (‚Üê structural? Œ±) (‚Üê structural? Œ≤)
  | (``CategoryStruct.id, #[_, f]) => return .id (‚Üê toMor‚ÇÅ f)
  | (``MonoidalCategoryStruct.whiskerLeft, #[f, Œ∑]) =>
    return .whiskerLeft (‚Üê toMor‚ÇÅ f) (‚Üê structural? Œ∑)
  | (``MonoidalCategoryStruct.whiskerRight, #[Œ∑, f]) =>
    return .whiskerRight (‚Üê structural? Œ∑) (‚Üê toMor‚ÇÅ f)
  | (``Mathlib.Tactic.Coherence.MonoidalCoherence.hom, #[_, _, f, g, _, _, inst]) =>
    return .monoidalCoherence (‚Üê toMor‚ÇÅ f) (‚Üê toMor‚ÇÅ g) inst
  | _ => match ‚Üê structuralAtom? e with
    | some Œ∑ => return .atom Œ∑
    | none => throwError "not a structural 2-morphism"

/-- Construct a `NormalExpr` expression from a Lean expression for an atomic 2-morphism. -/
def NormalExpr.of (Œ∑ : Expr) : MetaM NormalExpr := do
  return .cons (.id (‚Üê StringDiagram.src Œ∑)) (.of (.of ‚ü®Œ∑‚ü©)) (.nil  (.id (‚Üê StringDiagram.tar Œ∑)))

/-- If `e` is an expression of the form `Œ∑ ‚äó‚â´ Œ∏ := Œ∑ ‚â´ Œ± ‚â´ Œ∏` in the monoidal category `C`,
return the expression for `Œ±` .-/
def structuralOfMonoidalComp (C e : Expr) : MetaM Structural := do
  let v ‚Üê mkFreshLevelMVar
  let u ‚Üê mkFreshLevelMVar
  _ ‚Üê isDefEq (.sort (.succ v)) (‚Üê inferType (‚Üê inferType e))
  _ ‚Üê isDefEq (.sort (.succ u)) (‚Üê inferType C)
  let W ‚Üê mkFreshExprMVar none
  let X ‚Üê mkFreshExprMVar none
  let Y ‚Üê mkFreshExprMVar none
  let Z ‚Üê mkFreshExprMVar none
  let f ‚Üê mkFreshExprMVar none
  let g ‚Üê mkFreshExprMVar none
  let Œ±‚ÇÄ ‚Üê mkFreshExprMVar none
  let instC ‚Üê mkFreshExprMVar none
  let Œ±g := mkAppN (.const ``CategoryStruct.comp [v, u]) #[C, instC, X, Y, Z, Œ±‚ÇÄ, g]
  let fŒ±g := mkAppN (.const ``CategoryStruct.comp [v, u]) #[C, instC, W, X, Z, f, Œ±g]
  _ ‚Üê isDefEq e fŒ±g
  structural? Œ±‚ÇÄ

/-- Evaluate the expression `Œ∑ ‚â´ Œ∏` into a normalized form. -/
partial def evalComp : NormalExpr ‚Üí NormalExpr ‚Üí MetaM NormalExpr
  | .nil Œ±, .cons Œ≤ Œ∑ Œ∑s => do
    return (.cons (Œ±.comp Œ≤) Œ∑ Œ∑s)
  | .nil Œ±, .nil Œ±' => do
    return .nil (Œ±.comp Œ±')
  | .cons Œ± Œ∑ Œ∑s, Œ∏ => do
    let Œπ ‚Üê evalComp Œ∑s Œ∏
    return .cons Œ± Œ∑ Œπ

/-- Evaluate the expression `f ‚óÅ Œ∑` into a normalized form. -/
partial def evalWhiskerLeftExpr : Mor‚ÇÅ ‚Üí NormalExpr ‚Üí MetaM NormalExpr
  | f, .nil Œ± => do
    return .nil (.whiskerLeft f Œ±)
  | .of f, .cons Œ± Œ∑ Œ∑s => do
    let Œ∑' := WhiskerLeftExpr.whisker f Œ∑
    let Œ∏ ‚Üê evalWhiskerLeftExpr (.of f) Œ∑s
    return (.cons (.whiskerLeft (.of f) Œ±) Œ∑' Œ∏)
  | .comp f g, Œ∑ => do
    let Œ∏ ‚Üê evalWhiskerLeftExpr g Œ∑
    let Œπ ‚Üê evalWhiskerLeftExpr f Œ∏
    let h := Œ∑.src
    let h' := Œ∑.tar
    let Œπ' ‚Üê evalComp Œπ (NormalExpr.associatorInv f g h')
    let Œπ'' ‚Üê evalComp (NormalExpr.associator f g h) Œπ'
    return Œπ''
  | .id, Œ∑ => do
    let f := Œ∑.src
    let g := Œ∑.tar
    let Œ∑' ‚Üê evalComp Œ∑ (NormalExpr.leftUnitorInv g)
    let Œ∑'' ‚Üê evalComp (NormalExpr.leftUnitor f) Œ∑'
    return Œ∑''

/-- Evaluate the expression `Œ∑ ‚ñ∑ f` into a normalized form. -/
partial def evalWhiskerRightExpr : NormalExpr ‚Üí Mor‚ÇÅ ‚Üí MetaM NormalExpr
  | .nil Œ±, h => do
    return .nil (.whiskerRight Œ± h)
  | .cons Œ± (.of Œ∑) Œ∑s, .of f => do
    let Œ∏ ‚Üê evalWhiskerRightExpr Œ∑s (.of f)
    return (.cons (.whiskerRight Œ± (.of f)) (.of (.whisker Œ∑ f)) Œ∏)
  | .cons Œ± (.whisker f Œ∑) Œ∑s, h => do
    let g ‚Üê Œ∑.src
    let g' ‚Üê Œ∑.tar
    let Œ∑‚ÇÅ ‚Üê evalWhiskerRightExpr (.cons (.id g) Œ∑ (.nil (.id g'))) h
    let Œ∑‚ÇÇ ‚Üê evalWhiskerLeftExpr (.of f) Œ∑‚ÇÅ
    let Œ∑s‚ÇÅ ‚Üê evalWhiskerRightExpr Œ∑s h
    let Œ±' := .whiskerRight Œ± h
    let Œ∑s‚ÇÇ ‚Üê evalComp (.associatorInv (.of f) g' h) Œ∑s‚ÇÅ
    let Œ∑‚ÇÉ ‚Üê evalComp Œ∑‚ÇÇ Œ∑s‚ÇÇ
    let Œ∑‚ÇÑ ‚Üê evalComp (.associator (.of f) g h) Œ∑‚ÇÉ
    let Œ∑‚ÇÖ ‚Üê evalComp (.nil Œ±') Œ∑‚ÇÑ
    return Œ∑‚ÇÖ
  | Œ∑, .comp g h => do
    let Œ∑‚ÇÅ ‚Üê evalWhiskerRightExpr Œ∑ g
    let Œ∑‚ÇÇ ‚Üê evalWhiskerRightExpr Œ∑‚ÇÅ h
    let f := Œ∑.src
    let f' := Œ∑.tar
    let Œ∑‚ÇÉ ‚Üê evalComp Œ∑‚ÇÇ (.associator f' g h)
    let Œ∑‚ÇÑ ‚Üê evalComp (.associatorInv f g h) Œ∑‚ÇÉ
    return Œ∑‚ÇÑ
  | Œ∑, .id => do
    let f := Œ∑.src
    let g := Œ∑.tar
    let Œ∑‚ÇÅ ‚Üê evalComp Œ∑ (.rightUnitorInv g)
    let Œ∑‚ÇÇ ‚Üê evalComp (.rightUnitor f) Œ∑‚ÇÅ
    return Œ∑‚ÇÇ

/-- Evaluate the expression of a 2-morphism into a normalized form. -/
partial def eval (e : Expr) : MetaM NormalExpr := do
  if let .some e' ‚Üê structuralAtom? e then return .nil <| .atom e' else
    match e.getAppFnArgs with
    | (``CategoryStruct.id, #[_, _, f]) =>
      return .nil (.id (‚Üê toMor‚ÇÅ f))
    | (``CategoryStruct.comp, #[_, _, _, _, _, Œ∑, Œ∏]) =>
      let Œ∑_e ‚Üê eval Œ∑
      let Œ∏_e ‚Üê eval Œ∏
      let Œ∑Œ∏ ‚Üê evalComp Œ∑_e Œ∏_e
      return Œ∑Œ∏
    | (``MonoidalCategoryStruct.whiskerLeft, #[_, _, _, f, _, _, Œ∑]) =>
      evalWhiskerLeftExpr (‚Üê toMor‚ÇÅ f) (‚Üê eval Œ∑)
    | (``MonoidalCategoryStruct.whiskerRight, #[_, _, _, _, _, Œ∑, h]) =>
      evalWhiskerRightExpr (‚Üê eval Œ∑) (‚Üê toMor‚ÇÅ h)
    | (``monoidalComp, #[C, _, _, _, _, _, _, _, _, Œ∑, Œ∏]) =>
      let Œ∑_e ‚Üê eval Œ∑
      let Œ±‚ÇÄ' ‚Üê structuralOfMonoidalComp C e
      let Œ± := NormalExpr.nil Œ±‚ÇÄ'
      let Œ∏_e ‚Üê eval Œ∏
      let Œ±Œ∏ ‚Üê evalComp Œ± Œ∏_e
      let Œ∑Œ±Œ∏ ‚Üê evalComp Œ∑_e Œ±Œ∏
      return Œ∑Œ±Œ∏
    | (``MonoidalCategoryStruct.tensorHom, #[_, _, _, _f‚ÇÅ, g‚ÇÅ, f‚ÇÇ, _g‚ÇÇ, Œ∑, Œ∏]) =>
      /- Evaluate `Œ∑ ‚äó ùüô f` and `ùüô f ‚äó Œ∏` as whiskerings. -/
      match Œ∑.getAppFnArgs, Œ∏.getAppFnArgs with
      | _, (``CategoryStruct.id, #[_, _, f]) =>
        evalWhiskerRightExpr (‚Üê eval Œ∑) (‚Üê toMor‚ÇÅ f)
      | (``CategoryStruct.id, #[_, _, f]), _ =>
        evalWhiskerLeftExpr (‚Üê toMor‚ÇÅ f) (‚Üê eval Œ∏)
      /- Otherwise, expand `tensorHom` by using `tensorHom_def`. -/
      | _, _ =>
        let Œ∑' ‚Üê evalWhiskerRightExpr (‚Üê eval Œ∑) (‚Üê toMor‚ÇÅ f‚ÇÇ)
        let Œ∏' ‚Üê evalWhiskerLeftExpr (‚Üê toMor‚ÇÅ g‚ÇÅ) (‚Üê eval Œ∏)
        evalComp Œ∑' Œ∏'
    | _ => NormalExpr.of e

/-- Convert a `NormalExpr` expression into a list of `WhiskerLeftExpr` expressions. -/
def NormalExpr.toList : NormalExpr ‚Üí List WhiskerLeftExpr
  | NormalExpr.nil _ => []
  | NormalExpr.cons _ Œ∑ Œ∑s => Œ∑ :: NormalExpr.toList Œ∑s

/-- Return `[f‚ÇÅ, ..., f‚Çô]` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def leftMor‚ÇÅList (Œ∑ : WhiskerLeftExpr) : List Expr :=
  match Œ∑ with
  | WhiskerLeftExpr.of _ => []
  | WhiskerLeftExpr.whisker f Œ∑ => f.e :: leftMor‚ÇÅList Œ∑

/-- Return `[g‚Çô, ..., g‚ÇÅ]` for `Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def rightMor‚ÇÅListAux (Œ∑ : WhiskerRightExpr) : List Expr :=
  match Œ∑ with
  | WhiskerRightExpr.of _ => []
  | WhiskerRightExpr.whisker Œ∑ f => f.e :: rightMor‚ÇÅListAux Œ∑

/-- Return `[g‚Çô, ..., g‚ÇÅ]` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def rightMor‚ÇÅListReversed (Œ∑ : WhiskerLeftExpr) : List Expr :=
  match Œ∑ with
  | WhiskerLeftExpr.of Œ∑ => rightMor‚ÇÅListAux Œ∑
  | WhiskerLeftExpr.whisker _ Œ∑ => rightMor‚ÇÅListReversed Œ∑

/-- Return `[g‚ÇÅ, ..., g‚Çô]` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def rightMor‚ÇÅList (Œ∑ : WhiskerLeftExpr) : List Expr :=
  (rightMor‚ÇÅListReversed Œ∑).reverse

/-- Returns domain 1-morphisms as a list of components.` -/
def srcLists (Œ∑ : WhiskerLeftExpr) : MetaM (List Expr √ó List Expr √ó List Expr) := do
  return (leftMor‚ÇÅList Œ∑, (‚Üê Œ∑.atom.src).toList, rightMor‚ÇÅList Œ∑)

/-- Returns codomain 1-morphisms as a list of components.` -/
def tarLists (Œ∑ : WhiskerLeftExpr) : MetaM (List Expr √ó List Expr √ó List Expr) := do
  return (leftMor‚ÇÅList Œ∑, (‚Üê Œ∑.atom.tar).toList, rightMor‚ÇÅList Œ∑)

/-- `pairs [a, b, c, d]` is `[(a, b), (b, c), (c, d)]`. -/
def pairs {Œ± : Type} : List Œ± ‚Üí List (Œ± √ó Œ±)
  | [] => []
  | [_] => []
  | (x :: y :: ys) => (x, y) :: pairs (y :: ys)

/-- A type for Penrose variables. -/
structure PenroseVar : Type where
  /-- The identifier of the variable. -/
  ident : String
  /-- The indices of the variable. -/
  indices : List ‚Ñï
  /-- The underlying expression of the variable. -/
  e : Expr
  deriving Inhabited

instance : BEq PenroseVar := ‚ü®fun x y => x.ident == y.ident && x.indices == y.indices‚ü©

instance : Hashable PenroseVar := ‚ü®fun v ‚Ü¶ hash (v.ident, v.indices)‚ü©

instance : ToString PenroseVar :=
  ‚ü®fun v => v.ident ++ v.indices.foldl (fun s x => s ++ s!"_{x}") ""‚ü©

/-- Expressions to display as labels in a diagram. -/
abbrev ExprEmbeds := Array (String √ó Expr)

/-! ## Widget for general string diagrams -/

open ProofWidgets

/-- The state of a diagram builder. -/
structure DiagramState where
  /-- The Penrose substance program.
  Note that `embeds` are added lazily at the end. -/
  sub : String := ""
  /-- Components to display as labels in the diagram,
  mapped as name ‚Ü¶ (type, html). -/
  embeds : HashMap String (String √ó Html) := .empty
  /-- The start point of a string. -/
  startPoint : HashMap PenroseVar PenroseVar := .empty
  /-- The end point of a string. -/
  endPoint : HashMap PenroseVar PenroseVar := .empty

/-- The monad for building a string diagram. -/
abbrev DiagramBuilderM := StateT DiagramState MetaM

open scoped Jsx in
/-- Build a string diagram from state. -/
def buildDiagram : DiagramBuilderM (Option Html) := do
  let st ‚Üê get
  if st.sub == "" && st.embeds.isEmpty then
    return none
  let mut sub := "AutoLabel All\n"
  let mut embedHtmls := #[]
  for (n, (tp, h)) in st.embeds.toArray do
    sub := sub ++ s!"{tp} {n}\n"
    embedHtmls := embedHtmls.push (n, h)
  sub := sub ++ st.sub
  return <PenroseDiagram
    embeds={embedHtmls}
    dsl={include_str ".."/".."/".."/"widget"/"src"/"penrose"/"monoidal.dsl"}
    sty={include_str ".."/".."/".."/"widget"/"src"/"penrose"/"monoidal.sty"}
    sub={sub} />

/-- Add a substance `nm` of Penrose type `tp`,
labelled by `h` to the substance program. -/
def addEmbed (nm : String) (tp : String) (h : Html) : DiagramBuilderM Unit := do
  modify fun st => { st with embeds := st.embeds.insert nm (tp, h )}

open scoped Jsx in
/-- Add the variable `v` with the type `tp` to the substance program. -/
def addPenroseVar (tp : String) (v : PenroseVar) : DiagramBuilderM Unit := do
  let h := <InteractiveCode fmt={‚Üê Widget.ppExprTagged v.e} />
  addEmbed (toString v) tp h

/-- Add instruction `i` to the substance program. -/
def addInstruction (i : String) : DiagramBuilderM Unit := do
  modify fun st => { st with sub := st.sub ++ s!"{i}\n" }

/-- Add constructor `tp v := nm (vs)` to the substance program. -/
def addConstructor (tp : String) (v : PenroseVar) (nm : String) (vs : List PenroseVar) :
    DiagramBuilderM Unit := do
  let vs' := ", ".intercalate (vs.map (fun v => toString v))
  addInstruction s!"{tp} {v} := {nm} ({vs'})"

/-- Run the program in the diagram builder monad. -/
def DiagramBuilderM.run {Œ± : Type} (x : DiagramBuilderM Œ±) : MetaM Œ± :=
  x.run' {}

open scoped Jsx in
/-- Construct a string diagram from a Penrose `sub`stance program and expressions `embeds` to
display as labels in the diagram. -/
def mkStringDiag (e : Expr) : MetaM Html := do
  DiagramBuilderM.run do
    let l := (‚Üê eval e).toList
    /- Add 2-morphisms. -/
    for (i, x) in l.enumFrom 1 do
      let v : PenroseVar := ‚ü®"E", [i], x.atom.e‚ü©
      addPenroseVar "Atom" v
      let (L, C, R) ‚Üê srcLists x
      let C' := (‚Üê x.atom.tar).toList
      for (j, X) in L.enum do
        let v' : PenroseVar := ‚ü®"I_left", [i, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Left({v'}, {v})"
        let v_mor : PenroseVar := ‚ü®"f", [i, j], X‚ü©
        let v_mor' : PenroseVar := ‚ü®"f", [i + 1, j], X‚ü©
        modify fun st => { st with
          endPoint := st.endPoint.insert v_mor v'
          startPoint := st.startPoint.insert v_mor' v' }
      for (j, X) in R.enum do
        let v' : PenroseVar := ‚ü®"I_right", [i, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Left({v}, {v'})"
        let v_mor : PenroseVar := ‚ü®"f", [i, j + L.length + C.length], X‚ü©
        let v_mor' : PenroseVar := ‚ü®"f", [i + 1, j + L.length + C'.length], X‚ü©
        modify fun st => { st with
          endPoint := st.endPoint.insert v_mor v'
          startPoint := st.startPoint.insert v_mor' v' }
      for (j, X) in C.enum do
        let v_mor : PenroseVar := ‚ü®"f", [i, j + L.length], X‚ü©
        modify fun st => { st with endPoint := st.endPoint.insert v_mor v }
      for (j, X) in C'.enum do
        let v_mor' : PenroseVar := ‚ü®"f", [i + 1, j + L.length], X‚ü©
        modify fun st => { st with startPoint := st.startPoint.insert v_mor' v }
      /- Add constraints. -/
      for (j, (X, Y)) in (pairs L).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_left", [i, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_left", [i, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
      /- Add constraints. -/
      for (j, (X, Y)) in (pairs R).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_right", [i, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_right", [i, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
    /- Add constraints. -/
    for (i, (x, y)) in (pairs l).enumFrom 1 do
      let v‚ÇÅ : PenroseVar := ‚ü®"E", [i], x.atom.e‚ü©
      let v‚ÇÇ : PenroseVar := ‚ü®"E", [i + 1], y.atom.e‚ü©
      addInstruction s!"Above({v‚ÇÅ}, {v‚ÇÇ})"
    /- The top of the diagram. -/
    if let some x‚ÇÄ := l.head? then
      let v‚ÇÄ : PenroseVar := ‚ü®"E", [1], x‚ÇÄ.atom.e‚ü©
      let (L, C, R) ‚Üê srcLists x‚ÇÄ
      for (j, X) in (L ++ C ++ R).enum do
        let v' : PenroseVar := ‚ü®"I_left", [0, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Above({v'}, {v‚ÇÄ})"
        let v_mor : PenroseVar := ‚ü®"f", [1, j], X‚ü©
        modify fun st => { st with startPoint := st.startPoint.insert v_mor v' }
      for (j, (X, Y)) in (pairs (L ++ C ++ R)).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_left", [0, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_left", [0, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
    /- The bottom of the diagram. -/
    if let some x‚Çô := l.getLast? then
      let v‚Çô : PenroseVar := ‚ü®"E", [l.length], x‚Çô.atom.e‚ü©
      let (L, C', R) ‚Üê tarLists x‚Çô
      for (j, X) in (L ++ C' ++ R).enum do
        let v' : PenroseVar := ‚ü®"I_left", [l.length + 1, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Above({v‚Çô}, {v'})"
        let v_mor : PenroseVar := ‚ü®"f", [l.length + 1, j], X‚ü©
        modify fun st => { st with endPoint := st.endPoint.insert v_mor v' }
      for (j, (X, Y)) in (pairs (L ++ C' ++ R)).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_left", [l.length + 1, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_left", [l.length + 1, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
    /- Add 1-morphisms as strings. -/
    for (i, x) in l.enumFrom 1 do
      let (L, C, R) ‚Üê srcLists x
      for (j, X) in (L ++ C ++ R).enum do
        let v : PenroseVar := ‚ü®"f", [i, j], X‚ü©
        let st ‚Üê get
        if let .some vStart := st.startPoint.find? v then
          if let .some vEnd := st.endPoint.find? v then
            addConstructor "Mor1" v "MakeString" [vStart, vEnd]
    /- Add strings in the last row. -/
    if let some x‚Çô := l.getLast? then
      let (L, C', R) ‚Üê tarLists x‚Çô
      for (j, X) in (L ++ C' ++ R).enum do
        let v : PenroseVar := ‚ü®"f", [l.length + 1, j], X‚ü©
        let st ‚Üê get
        if let .some vStart := st.startPoint.find? v then
          if let .some vEnd := st.endPoint.find? v then
            addConstructor "Mor1" v "MakeString" [vStart, vEnd]
    match ‚Üê buildDiagram with
    | some html => return html
    | none => return <span>No 2-morphisms.</span>

/-- Given a 2-morphism, return a string diagram. Otherwise `none`. -/
def stringM? (e : Expr) : MetaM (Option Html) := do
  let e ‚Üê instantiateMVars e
  return some <| ‚Üê mkStringDiag e

/-- Given an equality between 2-morphisms, return a string diagram of the LHS. Otherwise `none`. -/
def stringLeftM? (e : Expr) : MetaM (Option Html) := do
  let e ‚Üê instantiateMVars e
  let some (_, lhs, _) := e.eq? | return none
  return some <| ‚Üê mkStringDiag lhs

/-- Given an equality between 2-morphisms, return a string diagram of the RHS. Otherwise `none`. -/
def stringRightM? (e : Expr) : MetaM (Option Html) := do
  let e ‚Üê instantiateMVars e
  let some (_, _, rhs) := e.eq? | return none
  return some <| ‚Üê mkStringDiag rhs

/-- The string diagram widget. -/
@[expr_presenter]
def stringPresenter : ExprPresenter where
  userName := "String diagram"
  layoutKind := .block
  present type := do
    if let some d ‚Üê stringM? type then
      return d
    throwError "Couldn't find a string diagram."

/-- The string diagram widget. -/
@[expr_presenter]
def stringPresenterLeft : ExprPresenter where
  userName := "String diagram of LHS"
  layoutKind := .block
  present type := do
    if let some d ‚Üê stringLeftM? type then
      return d
    throwError "Couldn't find a string diagram."

/-- The string diagram widget. -/
@[expr_presenter]
def stringPresenterRight : ExprPresenter where
  userName := "String diagram of RHS"
  layoutKind := .block
  present type := do
    if let some d ‚Üê stringRightM? type then
      return d
    throwError "Couldn't find a string diagram."

end Mathlib.Tactic.Widget.StringDiagram
