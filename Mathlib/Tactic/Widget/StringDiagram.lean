/-
Copyright (c) 2024 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import ProofWidgets.Component.PenroseDiagram
import ProofWidgets.Presentation.Expr
import Mathlib.CategoryTheory.Monoidal.Category


/-!
# String Diagrams

This file provides tactic/meta infrastructure for displaying string diagrams for morphisms
in monoidal categories in the infoview.

-/

namespace Mathlib.Tactic.Widget.StringDiagram

open Lean Meta Elab
open CategoryTheory

/-- Expressions for atomic 1-morphisms. -/
structure Atom‚ÇÅ : Type where
  /-- Extract a Lean expression from an `Atom‚ÇÅ` expression. -/
  e : Expr

/-- Expressions for 1-morphisms. -/
inductive Mor‚ÇÅ : Type
  /-- Construct the expression for `ùüô_ C`. -/
  | id : Expr ‚Üí Mor‚ÇÅ
  /-- Construct the expression for `X ‚äó Y` -/
  | comp : Mor‚ÇÅ ‚Üí Mor‚ÇÅ ‚Üí Mor‚ÇÅ
  /-- Construct the expression for an atomic 1-morphism. -/
  | of : Atom‚ÇÅ ‚Üí Mor‚ÇÅ
  deriving Inhabited

/-- Extract a Lean expression from a `Mor‚ÇÅ` expression. -/
def Mor‚ÇÅ.e : Mor‚ÇÅ ‚Üí MetaM Expr
  | .id C => do
    mkAppOptM ``MonoidalCategoryStruct.tensorUnit #[C]
  | .comp f g => do
    mkAppM ``MonoidalCategoryStruct.tensorObj #[‚Üê Mor‚ÇÅ.e f, ‚Üê Mor‚ÇÅ.e g]
  | .of f => return f.e

/-- Converts a 1-morphism into a list of its underlying expressions. -/
def Mor‚ÇÅ.toList : Mor‚ÇÅ ‚Üí List Expr
  | .id _ => []
  | .comp f g => f.toList ++ g.toList
  | .of f => [f.e]

/-- Construct a `Mor‚ÇÅ` expression from a Lean expression. -/
partial def toMor‚ÇÅ (e : Expr) : Mor‚ÇÅ :=
  match e.getAppFnArgs with
  | (``MonoidalCategoryStruct.tensorUnit, #[C, _, _]) => Mor‚ÇÅ.id C
  | (``MonoidalCategoryStruct.tensorObj, #[_, _, _, f, g]) => (toMor‚ÇÅ f).comp (toMor‚ÇÅ g)
  | _ => Mor‚ÇÅ.of ‚ü®e‚ü©

/-- Expressions for atomic structural 2-morphisms. -/
inductive StructuralAtom : Type
  /-- Construct the expression for the associator. -/
  | associator (f g h : Mor‚ÇÅ) : StructuralAtom
  /-- Construct the expression for the inverse of the associator. -/
  | associatorInv (f g h : Mor‚ÇÅ) : StructuralAtom
  /-- Construct the expression for the left unitor. -/
  | leftUnitor (f : Mor‚ÇÅ) : StructuralAtom
  /-- Construct the expression for the inverse of the left unitor. -/
  | leftUnitorInv (f : Mor‚ÇÅ) : StructuralAtom
  /-- Construct the expression for the right unitor. -/
  | rightUnitor (f : Mor‚ÇÅ) : StructuralAtom
  /-- Construct the expression for the inverse of the right unitor. -/
  | rightUnitorInv (f : Mor‚ÇÅ) : StructuralAtom
  deriving Inhabited

/-- Extract a Lean expression from a `StructuralAtom` expression. -/
def StructuralAtom.e : StructuralAtom ‚Üí MetaM Expr
  | .associator f g h => do
    mkAppM ``Iso.hom #[‚Üê mkAppM ``MonoidalCategoryStruct.associator #[‚Üê f.e, ‚Üê g.e, ‚Üê h.e]]
  | .associatorInv f g h => do
    mkAppM ``Iso.inv #[‚Üê mkAppM ``MonoidalCategoryStruct.associator #[‚Üê f.e, ‚Üê g.e, ‚Üê h.e]]
  | .leftUnitor f => do mkAppM ``Iso.hom #[‚Üê mkAppM ``MonoidalCategoryStruct.leftUnitor #[‚Üê f.e]]
  | .leftUnitorInv f => do mkAppM ``Iso.inv #[‚Üê mkAppM ``MonoidalCategoryStruct.leftUnitor #[‚Üê f.e]]
  | .rightUnitor f => do mkAppM ``Iso.hom #[‚Üê mkAppM ``MonoidalCategoryStruct.rightUnitor #[‚Üê f.e]]
  | .rightUnitorInv f => do mkAppM ``Iso.inv #[‚Üê mkAppM ``MonoidalCategoryStruct.rightUnitor #[‚Üê f.e]]

/-- Construct a `StructuralAtom` expression from a Lean expression. -/
def structuralAtom? (e : Expr) : Option StructuralAtom := do
  match e.getAppFnArgs with
  | (``Iso.hom, #[_, _, _, _, Œ∑]) =>
    match Œ∑.getAppFnArgs with
    | (``MonoidalCategoryStruct.associator, #[_, _, _, f, g, h]) => return .associator (toMor‚ÇÅ f) (toMor‚ÇÅ g) (toMor‚ÇÅ h)
    | (``MonoidalCategoryStruct.leftUnitor, #[_, _, _, f]) => return .leftUnitor (toMor‚ÇÅ f)
    | (``MonoidalCategoryStruct.rightUnitor, #[_, _, _, f]) => return .rightUnitor (toMor‚ÇÅ f)
    | _ => none
  | (``Iso.inv, #[_, _, _, _, Œ∑]) =>
    match Œ∑.getAppFnArgs with
    | (``MonoidalCategoryStruct.associator, #[_, _, _, f, g, h]) => return .associatorInv (toMor‚ÇÅ f) (toMor‚ÇÅ g) (toMor‚ÇÅ h)
    | (``MonoidalCategoryStruct.leftUnitor, #[_, _, _, f]) => return .leftUnitorInv (toMor‚ÇÅ f)
    | (``MonoidalCategoryStruct.rightUnitor, #[_, _, _, f]) => return .rightUnitorInv (toMor‚ÇÅ f)
    | _ => none
  | _ => none

/-- Expressions for atomic (non-structural) 2-morphisms. -/
structure Atom where
  /-- Extract a Lean expression from an `Atom` expression. -/
  e : Expr

/-- Expressions for atomic 2-Morphisms. -/
inductive Core : Type
  /-- Construct the expression for a structural 2-morphism. -/
  | ofStructural : StructuralAtom ‚Üí Core
  /-- Construct the expression for an atomic 2-morphism. -/
  | of : Atom ‚Üí Core
  deriving Inhabited

/-- Extract a Lean expression from a `Core` expression. -/
def Core.e : Core ‚Üí MetaM Expr
  | .ofStructural Œ∑ => Œ∑.e
  | .of a => return a.e

/-- Expressions of the form `Œ∑ ‚ñ∑ f‚ÇÅ ‚ñ∑ ... ‚ñ∑ f‚Çô`. -/
inductive WhiskerRightExpr : Type
  /-- Construct the expression for a core 2-morphism. -/
  | of (Œ∑ : Core) : WhiskerRightExpr
  /-- Construct the expression for `Œ∑ ‚ñ∑ f`. -/
  | whisker (Œ∑ : WhiskerRightExpr) (f : Atom‚ÇÅ) : WhiskerRightExpr
  deriving Inhabited

/-- Expressions of the form `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑`. -/
inductive WhiskerLeftExpr : Type
  /-- Construct the expression for a right-whiskered 2-morphism. -/
  | of (Œ∑ : WhiskerRightExpr) : WhiskerLeftExpr
  /-- Construct the expression for `f ‚óÅ Œ∑`. -/
  | whisker (f : Atom‚ÇÅ) (Œ∑ : WhiskerLeftExpr) : WhiskerLeftExpr
  deriving Inhabited

/-- Normalized expressions for 2-morphisms. -/
inductive NormalExpr : Type
  /-- Construct the expression for `ùüô f`. -/
  | id (f : Mor‚ÇÅ) : NormalExpr
  /-- Construct the normalized expression of 2-morphisms recursively. -/
  | cons (head : WhiskerLeftExpr) (tail : NormalExpr) : NormalExpr
  deriving Inhabited

/-- The domain of a morphism. -/
def src (Œ∑ : Expr) : MetaM Mor‚ÇÅ := do
  match (‚Üê inferType Œ∑).getAppFnArgs with
  | (``Quiver.Hom, #[_, _, f, _]) => return toMor‚ÇÅ f
  | _ => throwError "{Œ∑} is not a morphism"

/-- The codomain of a morphism. -/
def tar (Œ∑ : Expr) : MetaM Mor‚ÇÅ := do
  match (‚Üê inferType Œ∑).getAppFnArgs with
  | (``Quiver.Hom, #[_, _, _, g]) => return toMor‚ÇÅ g
  | _ => throwError "{Œ∑} is not a morphism"

/-- The domain of a 2-morphism. -/
def Core.src (Œ∑ : Core) : MetaM Mor‚ÇÅ := do StringDiagram.src (‚Üê Œ∑.e)
/-- The codomain of a 2-morphism. -/
def Core.tar (Œ∑ : Core) : MetaM Mor‚ÇÅ := do StringDiagram.tar (‚Üê Œ∑.e)

/-- Construct a normalized expression from an atomic 2-morphism. -/
def NormalExpr.mk (Œ∑ : Core) : MetaM NormalExpr := do
  return .cons (.of (.of Œ∑)) (.id (‚Üê Œ∑.tar))

/-- The domain of a 2-morphism. -/
def WhiskerRightExpr.src : WhiskerRightExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerRightExpr.of Œ∑ => Œ∑.src
  | WhiskerRightExpr.whisker Œ∑ f => return (‚Üê WhiskerRightExpr.src Œ∑).comp (Mor‚ÇÅ.of f)

/-- The codomain of a 2-morphism. -/
def WhiskerRightExpr.tar : WhiskerRightExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerRightExpr.of Œ∑ => Œ∑.tar
  | WhiskerRightExpr.whisker Œ∑ f => return (‚Üê WhiskerRightExpr.tar Œ∑).comp (Mor‚ÇÅ.of f)

/-- The domain of a 2-morphism. -/
def WhiskerLeftExpr.src : WhiskerLeftExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerLeftExpr.of Œ∑ => WhiskerRightExpr.src Œ∑
  | WhiskerLeftExpr.whisker f Œ∑ => return (Mor‚ÇÅ.of f).comp (‚Üê WhiskerLeftExpr.src Œ∑)

/-- The codomain of a 2-morphism. -/
def WhiskerLeftExpr.tar : WhiskerLeftExpr ‚Üí MetaM Mor‚ÇÅ
  | WhiskerLeftExpr.of Œ∑ => WhiskerRightExpr.tar Œ∑
  | WhiskerLeftExpr.whisker f Œ∑ => return (Mor‚ÇÅ.of f).comp (‚Üê WhiskerLeftExpr.tar Œ∑)

/-- The domain of a 2-morphism. -/
def NormalExpr.src : NormalExpr ‚Üí MetaM Mor‚ÇÅ
  | NormalExpr.id f => return f
  | NormalExpr.cons Œ∑ _ => Œ∑.src

/-- The codomain of a 2-morphism. -/
def NormalExpr.tar : NormalExpr ‚Üí MetaM Mor‚ÇÅ
  | NormalExpr.id f => return f
  | NormalExpr.cons _ Œ∏ => Œ∏.tar

/-- Evaluate the expression `Œ∑ ‚â´ Œ∏` into a normalized form. -/
def evalComp : NormalExpr ‚Üí NormalExpr ‚Üí NormalExpr
  | .id _, e => e
  | e, .id _ => e
  | .cons f g, e => .cons f (evalComp g e)

/-- The associator as a term of `normalExpr`. -/
def NormalExpr.associator (f g h : Mor‚ÇÅ) : MetaM NormalExpr := do
  NormalExpr.mk (.ofStructural <| .associator f g h)

/-- The inverse of the associator as a term of `normalExpr`. -/
def NormalExpr.associatorInv (f g h : Mor‚ÇÅ) : MetaM NormalExpr := do
  NormalExpr.mk (.ofStructural <| .associatorInv f g h)

/-- The left unitor as a term of `normalExpr`. -/
def NormalExpr.leftUnitor (f : Mor‚ÇÅ) : MetaM NormalExpr := do
  NormalExpr.mk (.ofStructural <| .leftUnitor f)

/-- The inverse of the left unitor as a term of `normalExpr`. -/
def NormalExpr.leftUnitorInv (f : Mor‚ÇÅ) : MetaM NormalExpr := do
  NormalExpr.mk (.ofStructural <| .leftUnitorInv f)

/-- The right unitor as a term of `normalExpr`. -/
def NormalExpr.rightUnitor (f : Mor‚ÇÅ) : MetaM NormalExpr := do
  NormalExpr.mk (.ofStructural <| .rightUnitor f)

/-- The inverse of the right unitor as a term of `normalExpr`. -/
def NormalExpr.rightUnitorInv (f : Mor‚ÇÅ) : MetaM NormalExpr := do
  NormalExpr.mk (.ofStructural <| .rightUnitorInv f)

/-- Evaluate the expression `f ‚óÅ Œ∑` into a normalized form. -/
partial def evalWhiskerLeftExpr : Mor‚ÇÅ ‚Üí NormalExpr ‚Üí MetaM NormalExpr
  | f, .id g => do
    return .id (f.comp g)
  | .of f, .cons Œ∑ Œ∏ => do
    let Œ∑' := WhiskerLeftExpr.whisker f Œ∑
    let Œ∏' ‚Üê evalWhiskerLeftExpr (.of f) Œ∏
    return .cons Œ∑' Œ∏'
  | .comp f g, Œ∑ => do
    let Œ∑' ‚Üê evalWhiskerLeftExpr f (‚Üê evalWhiskerLeftExpr g Œ∑)
    let h ‚Üê Œ∑.src
    let h' ‚Üê Œ∑.tar
    return evalComp (‚Üê NormalExpr.associator f g h) (evalComp Œ∑' (‚Üê NormalExpr.associatorInv f g h'))
  | .id _, Œ∑ => do
    let f ‚Üê Œ∑.src
    let g ‚Üê Œ∑.tar
    return evalComp (‚Üê NormalExpr.leftUnitor f) (evalComp Œ∑ (‚Üê NormalExpr.leftUnitorInv g))

/-- Evaluate the expression `Œ∑ ‚ñ∑ f` into a normalized form. -/
partial def evalWhiskerRightExpr : NormalExpr ‚Üí Mor‚ÇÅ ‚Üí MetaM NormalExpr
  | .id f, .of g => do
    return .id (f.comp (.of g))
  | .cons (.of Œ∑) Œ∏, .of f => do
    let Œ∑' := WhiskerRightExpr.whisker Œ∑ f
    let Œ∏' ‚Üê evalWhiskerRightExpr Œ∏ (.of f)
    return .cons (.of Œ∑') Œ∏'
  | .cons (.whisker f Œ∑) Œ∏, .of h => do
    let g ‚Üê Œ∑.src
    let g' ‚Üê Œ∑.tar
    let Œ∑' ‚Üê evalWhiskerLeftExpr (.of f) (‚Üê evalWhiskerRightExpr (.cons Œ∑ (.id g')) (.of h))
    let Œ∏' ‚Üê evalWhiskerRightExpr Œ∏ (.of h)
    return evalComp (‚Üê NormalExpr.associator (.of f) g (.of h)) (evalComp Œ∑' (evalComp (‚Üê NormalExpr.associatorInv (.of f) g' (.of h)) Œ∏'))
  | Œ∑, .comp g h => do
    let Œ∑' ‚Üê evalWhiskerRightExpr (‚Üê evalWhiskerRightExpr Œ∑ g) h
    let f ‚Üê Œ∑.src
    let f' ‚Üê Œ∑.tar
    return evalComp (‚Üê NormalExpr.associatorInv f g h) (evalComp Œ∑' (‚Üê NormalExpr.associator f' g h))
  | Œ∑, .id _ => do
    let f ‚Üê Œ∑.src
    let g ‚Üê Œ∑.tar
    return evalComp (‚Üê NormalExpr.rightUnitor f) (evalComp Œ∑ (‚Üê NormalExpr.rightUnitorInv g))

/-- Extract a Lean expression from a `WhiskerRightExpr` expression. -/
def WhiskerRightExpr.e : WhiskerRightExpr ‚Üí MetaM Expr
  | WhiskerRightExpr.of Œ∑ => Œ∑.e
  | WhiskerRightExpr.whisker Œ∑ f => do
    mkAppM ``MonoidalCategoryStruct.whiskerRight #[‚Üê Œ∑.e, f.e]

/-- Extract a Lean expression from a `WhiskerLeftExpr` expression. -/
def WhiskerLeftExpr.e : WhiskerLeftExpr ‚Üí MetaM Expr
  | WhiskerLeftExpr.of Œ∑ => Œ∑.e
  | WhiskerLeftExpr.whisker f Œ∑ => do
    mkAppM ``MonoidalCategoryStruct.whiskerLeft #[f.e, ‚Üê Œ∑.e]

/-- Extract a Lean expression from a `NormalExpr` expression. -/
def NormalExpr.e : NormalExpr ‚Üí MetaM Expr
  | NormalExpr.id f => do mkAppM ``CategoryStruct.id #[‚Üê f.e]
  | NormalExpr.cons Œ∑ (NormalExpr.id _) => Œ∑.e
  | NormalExpr.cons Œ∑ Œ∏ => do mkAppM ``CategoryStruct.comp #[‚Üê Œ∑.e, ‚Üê Œ∏.e]

/-- Convert a `NormalExpr` expression into a list of `WhiskerLeftExpr` expressions. -/
def NormalExpr.toList : NormalExpr ‚Üí List WhiskerLeftExpr
  | NormalExpr.id _ => []
  | NormalExpr.cons Œ∑ Œ∏ => Œ∑ :: NormalExpr.toList Œ∏

/-- Return `Œ∑` for `Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def WhiskerRightExpr.core : WhiskerRightExpr ‚Üí Core
  | WhiskerRightExpr.of Œ∑ => Œ∑
  | WhiskerRightExpr.whisker Œ∑ _ => Œ∑.core

/-- Return `Œ∑` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def WhiskerLeftExpr.core : WhiskerLeftExpr ‚Üí Core
  | WhiskerLeftExpr.of Œ∑ => Œ∑.core
  | WhiskerLeftExpr.whisker _ Œ∑ => Œ∑.core

/-- Return `ture` if `Œ∑` is a structural 2-morphism. -/
def WhiskerLeftExpr.isStructural (Œ∑ : WhiskerLeftExpr) : Bool :=
  match Œ∑.core with
  | .of _ => false
  | .ofStructural _ => true

/-- Interpret an `Expr` term as a `Core` term. -/
def toCore (e : Expr) : Core :=
  match structuralAtom? e with
  | some Œ∑ => Core.ofStructural Œ∑
  | none => Core.of ‚ü®e‚ü©

/-- Construct a `NormalExpr` expression from a Lean expression for a core 2-morphism. -/
def NormalExpr.of (Œ∑ : Expr) : MetaM NormalExpr := do
  return .cons (.of (.of (toCore Œ∑))) (.id <| ‚Üê StringDiagram.tar Œ∑)

/-- Evaluate the expression of a 2-morphism into a normalized form. -/
partial def eval (e : Expr) : MetaM NormalExpr := do
  match e.getAppFnArgs with
  | (``CategoryStruct.id, #[_, _, f]) => return NormalExpr.id (toMor‚ÇÅ f)
  | (``CategoryStruct.comp, #[_, _, _, _, _, Œ∑, Œ∏]) => return evalComp (‚Üê eval Œ∑) (‚Üê eval Œ∏)
  | (``MonoidalCategoryStruct.whiskerLeft, #[_, _, _, f, _, _, Œ∑]) => evalWhiskerLeftExpr (toMor‚ÇÅ f) (‚Üê eval Œ∑)
  | (``MonoidalCategoryStruct.whiskerRight, #[_, _, _, _, _, Œ∑, h]) => evalWhiskerRightExpr (‚Üê eval Œ∑) (toMor‚ÇÅ h)
  | _ => NormalExpr.of e

/-- Remove structural 2-morphisms. -/
def removeStructural : List WhiskerLeftExpr ‚Üí List WhiskerLeftExpr
  | [] => []
  | Œ∑ :: Œ∑s => match Œ∑.isStructural with
    | true => removeStructural Œ∑s
    | false => Œ∑ :: removeStructural Œ∑s

/-- Return `[f‚ÇÅ, ..., f‚Çô]` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def leftMor‚ÇÅList (Œ∑ : WhiskerLeftExpr) : List Expr :=
  match Œ∑ with
  | WhiskerLeftExpr.of _ => []
  | WhiskerLeftExpr.whisker f Œ∑ => f.e :: leftMor‚ÇÅList Œ∑

/-- Return `[g‚Çô, ..., g‚ÇÅ]` for `Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def rightMor‚ÇÅListAux (Œ∑ : WhiskerRightExpr) : List Expr :=
  match Œ∑ with
  | WhiskerRightExpr.of _ => []
  | WhiskerRightExpr.whisker Œ∑ f => f.e :: rightMor‚ÇÅListAux Œ∑

/-- Return `[g‚Çô, ..., g‚ÇÅ]` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def rightMor‚ÇÅListReversed (Œ∑ : WhiskerLeftExpr) : List Expr :=
  match Œ∑ with
  | WhiskerLeftExpr.of Œ∑ => rightMor‚ÇÅListAux Œ∑
  | WhiskerLeftExpr.whisker _ Œ∑ => rightMor‚ÇÅListReversed Œ∑

/-- Return `[g‚ÇÅ, ..., g‚Çô]` for `f‚ÇÅ ‚óÅ ... ‚óÅ f‚Çô ‚óÅ Œ∑ ‚ñ∑ g‚ÇÅ ‚ñ∑ ... ‚ñ∑ g‚Çô`. -/
def rightMor‚ÇÅList (Œ∑ : WhiskerLeftExpr) : List Expr :=
  (rightMor‚ÇÅListReversed Œ∑).reverse

/-- Returns domain 1-morphisms as a list of components.` -/
def srcLists (Œ∑ : WhiskerLeftExpr) : MetaM (List Expr √ó List Expr √ó List Expr) := do
  return (leftMor‚ÇÅList Œ∑, (‚Üê Œ∑.core.src).toList, rightMor‚ÇÅList Œ∑)

/-- Returns codomain 1-morphisms as a list of components.` -/
def tarLists (Œ∑ : WhiskerLeftExpr) : MetaM (List Expr √ó List Expr √ó List Expr) := do
  return (leftMor‚ÇÅList Œ∑, (‚Üê Œ∑.core.tar).toList, rightMor‚ÇÅList Œ∑)

/-- `pairs [a, b, c, d]` is `[(a, b), (b, c), (c, d)]`. -/
def pairs {Œ± : Type} : List Œ± ‚Üí List (Œ± √ó Œ±)
  | [] => []
  | [_] => []
  | (x :: y :: ys) => (x, y) :: pairs (y :: ys)

/-- A type for Penrose variables. -/
structure PenroseVar : Type where
  /-- The identifier of the variable. -/
  ident : String
  /-- The indices of the variable. -/
  indices : List ‚Ñï
  /-- The underlying expression of the variable. -/
  e : Expr
  deriving Inhabited, BEq, Hashable

instance : ToString PenroseVar :=
  ‚ü®fun v => v.ident ++ v.indices.foldl (fun s x => s ++ s!"_{x}") ""‚ü©

/-- Expressions to display as labels in a diagram. -/
abbrev ExprEmbeds := Array (String √ó Expr)

/-! ## Widget for general string diagrams -/

open ProofWidgets

/-- The state of a diagram builder. -/
structure DiagramState where
  /-- The Penrose substance program.
  Note that `embeds` are added lazily at the end. -/
  sub : String := ""
  /-- Components to display as labels in the diagram,
  mapped as name ‚Ü¶ (type, html). -/
  embeds : HashMap String (String √ó Html) := .empty
  /-- The start point of a string. -/
  startPoint : HashMap PenroseVar PenroseVar := .empty
  /-- The end point of a string. -/
  endPoint : HashMap PenroseVar PenroseVar := .empty

/-- The monad for building a string diagram. -/
abbrev DiagramBuilderM := StateT DiagramState MetaM

open scoped Jsx in
/-- Build a string diagram from state. -/
def buildDiagram : DiagramBuilderM (Option Html) := do
  let st ‚Üê get
  if st.sub == "" && st.embeds.isEmpty then
    return none
  let mut sub := "AutoLabel All\n"
  let mut embedHtmls := #[]
  for (n, (tp, h)) in st.embeds.toArray do
    sub := sub ++ s!"{tp} {n}\n"
    embedHtmls := embedHtmls.push (n, h)
  sub := sub ++ st.sub
  return <PenroseDiagram
    embeds={embedHtmls}
    dsl={include_str ".."/".."/".."/"widget"/"src"/"penrose"/"monoidal.dsl"}
    sty={include_str ".."/".."/".."/"widget"/"src"/"penrose"/"monoidal.sty"}
    sub={sub} />

/-- Add a substance `nm` of Penrose type `tp`,
labelled by `h` to the substance program. -/
def addEmbed (nm : String) (tp : String) (h : Html) : DiagramBuilderM Unit := do
  modify fun st => { st with embeds := st.embeds.insert nm (tp, h )}

open scoped Jsx in
/-- Add the variable `v` with the type `tp` to the substance program. -/
def addPenroseVar (tp : String) (v : PenroseVar) : DiagramBuilderM Unit := do
  let h := <InteractiveCode fmt={‚Üê Widget.ppExprTagged v.e} />
  addEmbed (toString v) tp h

/-- Add instruction `i` to the substance program. -/
def addInstruction (i : String) : DiagramBuilderM Unit := do
  modify fun st => { st with sub := st.sub ++ s!"{i}\n" }

/-- Add constructor `tp v := nm (vs)` to the substance program. -/
def addConstructor (tp : String) (v : PenroseVar) (nm : String) (vs : List PenroseVar) :
    DiagramBuilderM Unit := do
  let vs' := ", ".intercalate (vs.map (fun v => toString v))
  addInstruction s!"{tp} {v} := {nm} ({vs'})"

/-- Run the program in the diagram builder monad. -/
def DiagramBuilderM.run {Œ± : Type} (x : DiagramBuilderM Œ±) : MetaM Œ± :=
  x.run' {}

open scoped Jsx in
/-- Construct a string diagram from a Penrose `sub`stance program and expressions `embeds` to
display as labels in the diagram. -/
def mkStringDiag (e : Expr) : MetaM Html := do
  DiagramBuilderM.run do
    let l := removeStructural (‚Üê eval e).toList
    /- Add 2-morphisms. -/
    for (i, x) in l.enumFrom 1 do
      let v : PenroseVar := ‚ü®"E", [i], ‚Üê x.core.e‚ü©
      addPenroseVar "Core" v
      let (L, C, R) ‚Üê srcLists x
      let C' := (‚Üê x.core.tar).toList
      for (j, X) in L.enum do
        let v' : PenroseVar := ‚ü®"I_left", [i, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Left({v'}, {v})"
        let v_mor : PenroseVar := ‚ü®"f", [i, j], X‚ü©
        let v_mor' : PenroseVar := ‚ü®"f", [i + 1, j], X‚ü©
        modify fun st => { st with
          endPoint := st.endPoint.insert v_mor v'
          startPoint := st.startPoint.insert v_mor' v' }
      for (j, X) in R.enum do
        let v' : PenroseVar := ‚ü®"I_right", [i, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Left({v}, {v'})"
        let v_mor : PenroseVar := ‚ü®"f", [i, j + L.length + C.length], X‚ü©
        let v_mor' : PenroseVar := ‚ü®"f", [i + 1, j + L.length + C'.length], X‚ü©
        modify fun st => { st with
          endPoint := st.endPoint.insert v_mor v'
          startPoint := st.startPoint.insert v_mor' v' }
      for (j, X) in C.enum do
        let v_mor : PenroseVar := ‚ü®"f", [i, j + L.length], X‚ü©
        modify fun st => { st with endPoint := st.endPoint.insert v_mor v }
      for (j, X) in C'.enum do
        let v_mor' : PenroseVar := ‚ü®"f", [i + 1, j + L.length], X‚ü©
        modify fun st => { st with startPoint := st.startPoint.insert v_mor' v }
      /- Add constraints. -/
      for (j, (X, Y)) in (pairs L).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_left", [i, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_left", [i, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
      /- Add constraints. -/
      for (j, (X, Y)) in (pairs R).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_right", [i, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_right", [i, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
    /- Add constraints. -/
    for (i, (x, y)) in (pairs l).enumFrom 1 do
      let v‚ÇÅ : PenroseVar := ‚ü®"E", [i], ‚Üê x.core.e‚ü©
      let v‚ÇÇ : PenroseVar := ‚ü®"E", [i + 1], ‚Üê y.core.e‚ü©
      addInstruction s!"Above({v‚ÇÅ}, {v‚ÇÇ})"
    /- The top of the diagram. -/
    if let some x‚ÇÄ := l.head? then
      let v‚ÇÄ : PenroseVar := ‚ü®"E", [1], ‚Üê x‚ÇÄ.core.e‚ü©
      let (L, C, R) ‚Üê srcLists x‚ÇÄ
      for (j, X) in (L ++ C ++ R).enum do
        let v' : PenroseVar := ‚ü®"I_left", [0, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Above({v'}, {v‚ÇÄ})"
        let v_mor : PenroseVar := ‚ü®"f", [1, j], X‚ü©
        modify fun st => { st with startPoint := st.startPoint.insert v_mor v' }
      for (j, (X, Y)) in (pairs (L ++ C ++ R)).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_left", [0, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_left", [0, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
    /- The bottom of the diagram. -/
    if let some x‚Çô := l.getLast? then
      let v‚Çô : PenroseVar := ‚ü®"E", [l.length], ‚Üê x‚Çô.core.e‚ü©
      let (L, C', R) ‚Üê tarLists x‚Çô
      for (j, X) in (L ++ C' ++ R).enum do
        let v' : PenroseVar := ‚ü®"I_left", [l.length + 1, j], X‚ü©
        addPenroseVar "Id" v'
        addInstruction s!"Above({v‚Çô}, {v'})"
        let v_mor : PenroseVar := ‚ü®"f", [l.length + 1, j], X‚ü©
        modify fun st => { st with endPoint := st.endPoint.insert v_mor v' }
      for (j, (X, Y)) in (pairs (L ++ C' ++ R)).enum do
        let v‚ÇÅ : PenroseVar := ‚ü®"I_left", [l.length + 1, j], X‚ü©
        let v‚ÇÇ : PenroseVar := ‚ü®"I_left", [l.length + 1, j + 1], Y‚ü©
        addInstruction s!"Left({v‚ÇÅ}, {v‚ÇÇ})"
    /- Add 1-morphisms as strings. -/
    for (i, x) in l.enumFrom 1 do
      let (L, C, R) ‚Üê srcLists x
      for (j, X) in (L ++ C ++ R).enum do
        let v : PenroseVar := ‚ü®"f", [i, j], X‚ü©
        let st ‚Üê get
        if let .some vStart := st.startPoint.find? v then
          if let .some vEnd := st.endPoint.find? v then
            addConstructor "Mor1" v "MakeString" [vStart, vEnd]
    /- Add strings in the last row. -/
    if let some x‚Çô := l.getLast? then
      let (L, C', R) ‚Üê tarLists x‚Çô
      for (j, X) in (L ++ C' ++ R).enum do
        let v : PenroseVar := ‚ü®"f", [l.length + 1, j], X‚ü©
        let st ‚Üê get
        if let .some vStart := st.startPoint.find? v then
          if let .some vEnd := st.endPoint.find? v then
            addConstructor "Mor1" v "MakeString" [vStart, vEnd]
    match ‚Üê buildDiagram with
    | some html => return html
    | none => return <span>No 2-morphisms.</span>

/-- Given a 2-morphism, return a string diagram. Otherwise `none`. -/
def stringM? (e : Expr) : MetaM (Option Html) := do
  let e ‚Üê instantiateMVars e
  return some <| ‚Üê mkStringDiag e

/-- Given an equality between 2-morphisms, return a string diagram of the LHS. Otherwise `none`. -/
def stringLeftM? (e : Expr) : MetaM (Option Html) := do
  let e ‚Üê instantiateMVars e
  let some (_, lhs, _) := e.eq? | return none
  return some <| ‚Üê mkStringDiag lhs

/-- Given an equality between 2-morphisms, return a string diagram of the RHS. Otherwise `none`. -/
def stringRightM? (e : Expr) : MetaM (Option Html) := do
  let e ‚Üê instantiateMVars e
  let some (_, _, rhs) := e.eq? | return none
  return some <| ‚Üê mkStringDiag rhs

/-- The string diagram widget. -/
@[expr_presenter]
def stringPresenter : ExprPresenter where
  userName := "String diagram"
  layoutKind := .block
  present type := do
    if let some d ‚Üê stringM? type then
      return d
    throwError "Couldn't find a string diagram."

/-- The string diagram widget. -/
@[expr_presenter]
def stringPresenterLeft : ExprPresenter where
  userName := "String diagram of LHS"
  layoutKind := .block
  present type := do
    if let some d ‚Üê stringLeftM? type then
      return d
    throwError "Couldn't find a string diagram."

/-- The string diagram widget. -/
@[expr_presenter]
def stringPresenterRight : ExprPresenter where
  userName := "String diagram of RHS"
  layoutKind := .block
  present type := do
    if let some d ‚Üê stringRightM? type then
      return d
    throwError "Couldn't find a string diagram."

end Mathlib.Tactic.Widget.StringDiagram
