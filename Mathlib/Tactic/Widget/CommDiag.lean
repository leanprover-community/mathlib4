/-
Copyright (c) 2022 Wojciech Nawrocki. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Wojciech Nawrocki
-/
import Lean.Server.Requests
import Lean.Server.Rpc.Basic
import Lean.Server.Rpc.RequestHandling
import Lean.Elab
import Lean.Widget.UserWidget

-- Please install Node.js and run `lake build widgetCommDiag` before running the rest of this file.
#exit

-- TODO(WN): Remove when category theory is ported
class quiver (V : Type u) where
  hom : V â†’ V â†’ Sort v

infixr:10 " âŸ¶ " => quiver.hom -- type as \h

class category_struct (obj : Type u) extends quiver.{u,v+1} obj : Type (max u (v+1)) where
  id   : âˆ€ X : obj, hom X X
  comp : âˆ€ {X Y Z : obj}, (X âŸ¶ Y) â†’ (Y âŸ¶ Z) â†’ (X âŸ¶ Z)

-- https://github.com/leanprover/lean4/issues/1367
prefix:max "ğŸ™ " => category_struct.id -- type as \b1
infixr:80 " â‰« " => category_struct.comp -- type as \gg

class category (obj : Type u) extends category_struct.{u,v} obj : Type (max u (v+1)) where
  id_comp' : âˆ€ {X Y : obj} (f : hom X Y), ğŸ™ X â‰« f = f
  comp_id' : âˆ€ {X Y : obj} (f : hom X Y), f â‰« ğŸ™ Y = f
  assoc'   : âˆ€ {W X Y Z : obj} (f : hom W X) (g : hom X Y) (h : hom Y Z),
    (f â‰« g) â‰« h = f â‰« (g â‰« h)

instance : category (Type u) where
  hom Î± Î² := Î± â†’ Î²
  id _ := id
  comp f g := g âˆ˜ f
  id_comp' _ := rfl
  comp_id' _ := rfl
  assoc' _ _ _ := rfl

open Lean.Widget in
@[widget]
def squares : UserWidgetDefinition where
  name := "Commutative diagram"
  javascript := include_str ".." / ".." / ".." / "widget" / "dist" / "commDiag.js"

/-- If the current goal is a commutative diagram, displays that in the infoview. -/
syntax (name := squaresTacStx) "squares!" : tactic
open Lean Elab Tactic in
@[tactic squaresTacStx]
def squaresTac : Tactic
  | stx@`(tactic| squares!) => do
    if let some _ := stx.getPos? then
      Lean.Widget.saveWidgetInfo "squares" Json.null stx
  | _ => throwUnsupportedSyntax

open Lean Widget Server

@[inline] def Lean.Expr.app7? (e : Expr) (fName : Name) : Option (Expr Ã— Expr Ã— Expr Ã— Expr Ã— Expr Ã— Expr Ã— Expr) :=
  if e.isAppOfArity fName 7 then
    some (
      e.appFn!.appFn!.appFn!.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appArg!,
      e.appFn!.appArg!,
      e.appArg!
    )
  else
    none

/-- Given a hom `f : Î± âŸ¶ Î²`, return `(Î±, Î²)`. Otherwise `none`. -/
def homTypesM? (f : Expr) : MetaM (Option (Expr Ã— Expr)) := do
  let fTp â† Meta.inferType f >>= instantiateMVars
  let some (_, _, A, B) := fTp.app4? ``quiver.hom | return none
  return (A, B)

/-- Given composed homs `f â‰¡ g â‰« h`, return `(g, h)`. Otherwise `none`. -/
def homComp? (f : Expr) : Option (Expr Ã— Expr) :=
  if let some (_, _, _, _, _, f, g) := f.app7? ``category_struct.comp then
    some (f, g)
  else none

inductive DiagramKind | square | triangle
deriving Inhabited, FromJson, ToJson

/--
Square with `homs = [f,g,h,i]` and `objs = [A,B,C,D]`
```
A f B
i   g
D h C
```
Triangle with `homs = [f,g,h]` and `objs = [A,B,C]`
```
A f B
  h g
    C
```
-/
structure DiagramData where
  objs : Array CodeWithInfos
  homs : Array CodeWithInfos
  kind : DiagramKind
  deriving Inhabited, RpcEncoding

/-- Given a commutative square `e â‰¡ f â‰« g = i â‰« h`, return a square diagram. Otherwise `none`. -/
def homSquareM? (e : Expr) : MetaM (Option DiagramData) := do
  let some (_, lhs, rhs) := e.eq? | return none
  let some (f, g) := homComp? lhs | return none
  let some (i, h) := homComp? rhs | return none
  let some (A, B) â† homTypesM? f | return none
  let some (C, D) â† homTypesM? h | return none
  let pp (e : Expr) := ppExprTagged e
  return some {
    objs := #[â† pp A, â† pp B, â† pp C, â† pp D]
    homs := #[â† pp f, â† pp g, â† pp h, â† pp i]
    kind := .square
  }

/-- Given a commutative triangle `e â‰¡ f â‰« g = h` or `e â‰¡ h = f â‰« g`, return a triangle diagram.
Otherwise `none`. -/
def homTriangleM? (e : Expr) : MetaM (Option DiagramData) := do
  let some (_, lhs, rhs) := e.eq? | return none
  let pp (e : Expr) := ppExprTagged e
  if let some (f, g) := homComp? lhs then
    let some (A, C) â† homTypesM? rhs | return none
    let some (_, B) â† homTypesM? f | return none
    return some {
      objs := #[â† pp A, â† pp B, â† pp C]
      homs := #[â† pp f, â† pp g, â† pp rhs]
      kind := .triangle
    }
  let some (f, g) := homComp? rhs | return none
  let some (A, C) â† homTypesM? lhs | return none
  let some (_, B) â† homTypesM? f | return none
  return some {
    objs := #[â† pp A, â† pp B, â† pp C]
    homs := #[â† pp f, â† pp g, â† pp lhs]
    kind := .triangle
  }

open Lean Server RequestM in
@[serverRpcMethod]
def getCommutativeDiagram (args : Lean.Lsp.Position) : RequestM (RequestTask (Option DiagramData)) := do
  let doc â† readDoc
  let pos := doc.meta.text.lspPosToUtf8Pos args
  withWaitFindSnapAtPos args fun snap => do
    let g :: _ := snap.infoTree.goalsAt? doc.meta.text pos | return none
    let { ctxInfo := ci, tacticInfo := ti, useAfter := useAfter, .. } := g
    let ci := if useAfter then { ci with mctx := ti.mctxAfter } else { ci with mctx := ti.mctxBefore }
    let g :: _ := if useAfter then ti.goalsAfter else ti.goalsBefore | return none
    ci.runMetaM {} <| do
      let some mvarDecl := (â† getMCtx).findDecl? g
        | throwError "unknown goal {g.name}"
      let lctx := mvarDecl.lctx
      let lctx := lctx.sanitizeNames.run' { options := (â† getOptions) }
      Meta.withLCtx lctx mvarDecl.localInstances do
        let type â† g.getType >>= instantiateMVars
        if let some d â† homSquareM? type then
          return some d
        if let some d â† homTriangleM? type then
          return some d
        return none

example {f g : Nat âŸ¶ Bool}: f = g â†’ (f â‰« ğŸ™ Bool) = (g â‰« ğŸ™ Bool) := by
  intro h
  squares!
  exact h

example {f g : Nat âŸ¶ Bool}: f = g â†’ f = (g â‰« ğŸ™ Bool) := by
  intro h
  squares!
  exact h
