/-
Copyright (c) 2022 Wojciech Nawrocki. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Wojciech Nawrocki
-/

import Mathlib.CategoryTheory.Category.Basic

import ProofWidgets.Component.Panel
import ProofWidgets.Component.SelectionPanel
import ProofWidgets.Component.PenroseDiagram

/-! This module defines tactic/meta infrastructure for displaying commutative diagrams in the
infoview. -/

open Lean in
/-- If the expression is a function application of `fName` with 7 arguments, return those arguments.
Otherwise return `none`. -/
@[inline] def _root_.Lean.Expr.app7? (e : Expr) (fName : Name)
    : Option (Expr Ã— Expr Ã— Expr Ã— Expr Ã— Expr Ã— Expr Ã— Expr) :=
  if e.isAppOfArity fName 7 then
    some (
      e.appFn!.appFn!.appFn!.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appFn!.appArg!,
      e.appFn!.appFn!.appArg!,
      e.appFn!.appArg!,
      e.appArg!
    )
  else
    none

namespace Mathlib.Tactic.Widget
open Lean Meta
open ProofWidgets
open CategoryTheory

/-! ## Metaprogramming utilities for breaking down category theory expressions -/

/-- Given a Hom type `Î± âŸ¶ Î²`, return `(Î±, Î²)`. Otherwise `none`. -/
def homType? (e : Expr) : Option (Expr Ã— Expr) := do
  let some (_, _, A, B) := e.app4? ``Quiver.Hom | none
  return (A, B)

/-- Given composed homs `g â‰« h`, return `(g, h)`. Otherwise `none`. -/
def homComp? (f : Expr) : Option (Expr Ã— Expr) := do
  let some (_, _, _, _, _, f, g) := f.app7? ``CategoryStruct.comp | none
  return (f, g)

/-- Expressions to display as labels in a diagram. -/
abbrev ExprEmbeds := Array (String Ã— Expr)

/-! ## Widget for general commutative diagrams -/

open scoped Jsx in
/-- Construct a commutative diagram from a Penrose `sub`stance program and expressions `embeds` to
display as labels in the diagram. -/
def mkCommDiag (sub : String) (embeds : ExprEmbeds) : MetaM Html := do
  let embeds â† embeds.mapM fun (s, h) =>
      return (s, Html.ofTHtml <InteractiveCode fmt={â† Widget.ppExprTagged h} />)
  return Html.ofTHtml
    <PenroseDiagram
      embeds={embeds}
      dsl={include_str ".."/".."/".."/"widget"/"src"/"penrose"/"commutative.dsl"}
      sty={include_str ".."/".."/".."/"widget"/"src"/"penrose"/"commutativeOpt.sty"}
      sub={sub} />

/-! ## Commutative triangles -/

/--
Triangle with `homs = [f,g,h]` and `objs = [A,B,C]`
```
A f B
  h g
    C
``` -/
def subTriangle := include_str ".."/".."/".."/"widget"/"src"/"penrose"/"triangle.sub"

/-- Given a commutative triangle `f â‰« g = h` or `e â‰¡ h = f â‰« g`, return a triangle diagram.
Otherwise `none`. -/
def commTriangleM? (e : Expr) : MetaM (Option Html) := do
  let e â† instantiateMVars e
  let some (_, lhs, rhs) := e.eq? | return none
  if let some (f, g) := homComp? lhs then
    let some (A, C) := homType? (â† inferType rhs) | return none
    let some (_, B) := homType? (â† inferType f) | return none
    return some <| â† mkCommDiag subTriangle
      #[("A", A), ("B", B), ("C", C),
        ("f", f), ("g", g), ("h", rhs)]
  let some (f, g) := homComp? rhs | return none
  let some (A, C) := homType? (â† inferType lhs) | return none
  let some (_, B) := homType? (â† inferType f) | return none
  return some <| â† mkCommDiag subTriangle
    #[("A", A), ("B", B), ("C", C),
      ("f", f), ("g", g), ("h", lhs)]

@[expr_presenter]
def commutativeTrianglePresenter : ExprPresenter where
  userName := "Commutative triangle"
  layoutKind := .block
  present type := do
    if let some d â† commTriangleM? type then
      return d
    throwError "Not a commutative triangle."

/-! ## Commutative squares -/

/--
Square with `homs = [f,g,h,i]` and `objs = [A,B,C,D]`
```
A f B
i   g
D h C
``` -/
def subSquare := include_str ".."/".."/".."/"widget"/"src"/"penrose"/"square.sub"

/-- Given a commutative square `f â‰« g = i â‰« h`, return a square diagram. Otherwise `none`. -/
def commSquareM? (e : Expr) : MetaM (Option Html) := do
  let e â† instantiateMVars e
  let some (_, lhs, rhs) := e.eq? | return none
  let some (f, g) := homComp? lhs | return none
  let some (i, h) := homComp? rhs | return none
  let some (A, B) := homType? (â† inferType f) | return none
  let some (D, C) := homType? (â† inferType g) | return none
  some <$> mkCommDiag subSquare
    #[("A", A), ("B", B), ("C", C), ("D", D),
      ("f", f), ("g", g), ("h", h), ("i", i)]

@[expr_presenter]
def commutativeSquarePresenter : ExprPresenter where
  userName := "Commutative square"
  layoutKind := .block
  present type := do
    if let some d â† commSquareM? type then
      return d
    throwError "Not a commutative square."

/-! ## Example diagrams -/

/-- Local instance to make examples work. -/
local instance : Category (Type u) where
  Hom Î± Î² := Î± â†’ Î²
  id _ := id
  comp f g := g âˆ˜ f
  id_comp _ := rfl
  comp_id _ := rfl
  assoc _ _ _ := rfl

example {f g : Nat âŸ¶ Bool}: f = g â†’ (f â‰« ğŸ™ Bool) = (g â‰« ğŸ™ Bool) := by
  withPanelWidgets [SelectionPanel]
    intro h
    exact h

example {fButActuallyTheNameIsReallyLong g : Nat âŸ¶ Bool}: fButActuallyTheNameIsReallyLong = g â†’
    fButActuallyTheNameIsReallyLong = (g â‰« ğŸ™ Bool) := by
  withPanelWidgets [SelectionPanel]
    intro h
    conv =>
      rhs
      enter [1]
      rw [â† h]

-- from Sina Hazratpour
example {X Y Z : Type} {f g : X âŸ¶ Y} {k : Y âŸ¶ Y} {f' : Y âŸ¶ Z} {i : X âŸ¶ Z}
    (h': g â‰« f' = i) :
    (f â‰« k) = g â†’ ((f â‰« k) â‰« f') = (g â‰« ğŸ™ Y â‰« f') := by
  withPanelWidgets [SelectionPanel]
    intro h
    rw [
      h,
      â† Category.assoc g (ğŸ™ Y) f',
      h',
      Category.comp_id g, h'
    ]

example {X Y Z : Type} {f i : X âŸ¶ Y}
    {g j : Y âŸ¶ Z} {h : X âŸ¶ Z} :
    h = f â‰« g â†’
    i â‰« j = h â†’
    f â‰« g = i â‰« j := by
  withPanelWidgets [SelectionPanel]
    intro hâ‚ hâ‚‚
    rw [â† hâ‚, hâ‚‚]
