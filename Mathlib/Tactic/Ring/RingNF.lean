/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Tim Baanen
-/
import Mathlib.Tactic.Ring.Basic
import Mathlib.Tactic.Conv
import Mathlib.Util.Qq

/-!
# `ring_nf` tactic

A tactic which uses `ring` to rewrite expressions. This can be used non-terminally to normalize
ring expressions in the goal such as `‚ä¢ P (x + x + x)` ~> `‚ä¢ P (x * 3)`, as well as being able to
prove some equations that `ring` cannot because they involve ring reasoning inside a subterm,
such as `sin (x + y) + sin (y + x) = 2 * sin (x + y)`.

-/

set_option autoImplicit true

-- In this file we would like to be able to use multi-character auto-implicits.
set_option relaxedAutoImplicit true

namespace Mathlib.Tactic
open Lean hiding Rat
open Qq Meta

namespace Ring

/-- True if this represents an atomic expression. -/
def ExBase.isAtom : ExBase sŒ± a ‚Üí Bool
  | .atom _ => true
  | _ => false

/-- True if this represents an atomic expression. -/
def ExProd.isAtom : ExProd sŒ± a ‚Üí Bool
  | .mul va‚ÇÅ (.const 1 _) (.const 1 _) => va‚ÇÅ.isAtom
  | _ => false

/-- True if this represents an atomic expression. -/
def ExSum.isAtom : ExSum sŒ± a ‚Üí Bool
  | .add va‚ÇÅ va‚ÇÇ => match va‚ÇÇ with -- FIXME: this takes a while to compile as one match
    | .zero => va‚ÇÅ.isAtom
    | _ => false
  | _ => false

end Ring

namespace RingNF
open Ring

/-- The normalization style for `ring_nf`. -/
inductive RingMode where
  /-- Sum-of-products form, like `x + x * y * 2 + z ^ 2`. -/
  | SOP
  /-- Raw form: the representation `ring` uses internally. -/
  | raw
  deriving Inhabited, BEq, Repr

/-- Configuration for `ring_nf`. -/
structure Config where
  /-- the reducibility setting to use when comparing atoms for defeq -/
  red := TransparencyMode.reducible
  /-- if true, atoms inside ring expressions will be reduced recursively -/
  recursive := true
  /-- The normalization style. -/
  mode := RingMode.SOP
  deriving Inhabited, BEq, Repr

/-- Function elaborating `RingNF.Config`. -/
declare_config_elab elabConfig Config

/-- The read-only state of the `RingNF` monad. -/
structure Context where
  /-- A basically empty simp context, passed to the `simp` traversal in `RingNF.rewrite`. -/
  ctx : Simp.Context
  /-- A cleanup routine, which simplifies normalized polynomials to a more human-friendly
  format. -/
  simp : Simp.Result ‚Üí SimpM Simp.Result

/-- The monad for `RingNF` contains, in addition to the `AtomM` state,
a simp context for the main traversal and a simp function (which has another simp context)
to simplify normalized polynomials. -/
abbrev M := ReaderT Context AtomM

/--
A tactic in the `RingNF.M` monad which will simplify expression `parent` to a normal form.
* `root`: true if this is a direct call to the function.
  `RingNF.M.run` sets this to `false` in recursive mode.
-/
def rewrite (parent : Expr) (root := true) : M Simp.Result :=
  fun nctx rctx s ‚Ü¶ do
    let pre e :=
      try
        guard <| root || parent != e -- recursion guard
        let e ‚Üê withReducible <| whnf e
        guard e.isApp -- all interesting ring expressions are applications
        let ‚ü®u, Œ±, e‚ü© ‚Üê inferTypeQ' e
        let sŒ± ‚Üê synthInstanceQ (q(CommSemiring $Œ±) : Q(Type u))
        let c ‚Üê mkCache sŒ±
        let ‚ü®a, _, pa‚ü© ‚Üê match ‚Üê isAtomOrDerivable sŒ± c e rctx s with
        | none => eval sŒ± c e rctx s -- `none` indicates that `eval` will find something algebraic.
        | some none => failure -- No point rewriting atoms
        | some (some r) => pure r -- Nothing algebraic for `eval` to use, but `norm_num` simplifies.
        let r ‚Üê nctx.simp { expr := a, proof? := pa }
        if ‚Üê withReducible <| isDefEq r.expr e then return .done { expr := r.expr }
        pure (.done r)
      catch _ => pure <| .visit { expr := e }
    let post := (Simp.postDefault ¬∑ fun _ ‚Ü¶ none)
    (¬∑.1) <$> Simp.main parent nctx.ctx (methods := { pre, post })

variable [CommSemiring R]

theorem add_assoc_rev (a b c : R) : a + (b + c) = a + b + c := (add_assoc ..).symm
theorem mul_assoc_rev (a b c : R) : a * (b * c) = a * b * c := (mul_assoc ..).symm
theorem mul_neg {R} [Ring R] (a b : R) : a * -b = -(a * b) := by simp
                                                                 -- üéâ no goals
theorem add_neg {R} [Ring R] (a b : R) : a + -b = a - b := (sub_eq_add_neg ..).symm
theorem nat_rawCast_0 : (Nat.rawCast 0 : R) = 0 := by simp
                                                      -- üéâ no goals
theorem nat_rawCast_1 : (Nat.rawCast 1 : R) = 1 := by simp
                                                      -- üéâ no goals
theorem nat_rawCast_2 [Nat.AtLeastTwo n] : (Nat.rawCast n : R) = OfNat.ofNat n := rfl
theorem int_rawCast_1 {R} [Ring R] : (Int.rawCast (.negOfNat 1) : R) = -1 := by
  simp [Int.negOfNat_eq]
  -- üéâ no goals
theorem int_rawCast_2 {R} [Ring R] [Nat.AtLeastTwo n] :
    (Int.rawCast (.negOfNat n) : R) = -OfNat.ofNat n := by
  simp [Int.negOfNat_eq, OfNat.ofNat]
  -- üéâ no goals
theorem rat_rawCast_2 {R} [DivisionRing R] : (Rat.rawCast n d : R) = n / d := by simp
                                                                                 -- üéâ no goals

/--
Runs a tactic in the `RingNF.M` monad, given initial data:

* `s`: a reference to the mutable state of `ring`, for persisting across calls.
  This ensures that atom ordering is used consistently.
* `cfg`: the configuration options
* `x`: the tactic to run
-/
partial def M.run
    (s : IO.Ref AtomM.State) (cfg : RingNF.Config) (x : M Œ±) : MetaM Œ± := do
  let ctx := {
    simpTheorems := #[‚Üê Elab.Tactic.simpOnlyBuiltins.foldlM (¬∑.addConst ¬∑) {}]
    congrTheorems := ‚Üê getSimpCongrTheorems }
  let simp ‚Üê match cfg.mode with
  | .raw => pure pure
  | .SOP =>
    let thms : SimpTheorems := {}
    let thms ‚Üê [``add_zero, ``add_assoc_rev, ``_root_.mul_one, ``mul_assoc_rev,
      ``_root_.pow_one, ``mul_neg, ``add_neg].foldlM (¬∑.addConst ¬∑) thms
    let thms ‚Üê [``nat_rawCast_0, ``nat_rawCast_1, ``nat_rawCast_2, ``int_rawCast_1, ``int_rawCast_2,
      ``rat_rawCast_2].foldlM (¬∑.addConst ¬∑ (post := false)) thms
    let ctx' := { ctx with simpTheorems := #[thms] }
    pure fun r' : Simp.Result ‚Ü¶ do
      Simp.mkEqTrans r' (‚Üê Simp.main r'.expr ctx' (methods := Simp.DefaultMethods.methods)).1
  let nctx := { ctx, simp }
  let rec
    /-- The recursive context. -/
    rctx := { red := cfg.red, evalAtom },
    /-- The atom evaluator calls either `RingNF.rewrite` recursively,
    or nothing depending on `cfg.recursive`. -/
    evalAtom := if cfg.recursive
      then fun e ‚Ü¶ rewrite e false nctx rctx s
      else fun e ‚Ü¶ pure { expr := e }
  x nctx rctx s

/-- Overrides the default error message in `ring1` to use a prettified version of the goal. -/
initialize ringCleanupRef.set fun e => do
  M.run (‚Üê IO.mkRef {}) { recursive := false } fun nctx _ _ =>
    return (‚Üê nctx.simp { expr := e } nctx.ctx |>.run {}).1.expr

open Elab.Tactic Parser.Tactic
/-- Use `ring_nf` to rewrite the main goal. -/
def ringNFTarget (s : IO.Ref AtomM.State) (cfg : Config) : TacticM Unit := withMainContext do
  let goal ‚Üê getMainGoal
  let tgt ‚Üê instantiateMVars (‚Üê goal.getType)
  let r ‚Üê M.run s cfg <| rewrite tgt
  if r.expr.consumeMData.isConstOf ``True then
    goal.assign (‚Üê mkOfEqTrue (‚Üê r.getProof))
    replaceMainGoal []
  else
    replaceMainGoal [‚Üê applySimpResultToTarget goal tgt r]

/-- Use `ring_nf` to rewrite hypothesis `h`. -/
def ringNFLocalDecl (s : IO.Ref AtomM.State) (cfg : Config) (fvarId : FVarId) :
    TacticM Unit := withMainContext do
  let tgt ‚Üê instantiateMVars (‚Üê fvarId.getType)
  let goal ‚Üê getMainGoal
  let myres ‚Üê M.run s cfg <| rewrite tgt
  match ‚Üê applySimpResultToLocalDecl goal fvarId myres false with
  | none => replaceMainGoal []
  | some (_, newGoal) => replaceMainGoal [newGoal]

/--
Simplification tactic for expressions in the language of commutative (semi)rings,
which rewrites all ring expressions into a normal form.
* `ring_nf!` will use a more aggressive reducibility setting to identify atoms.
* `ring_nf (config := cfg)` allows for additional configuration:
  * `red`: the reducibility setting (overridden by `!`)
  * `recursive`: if true, `ring_nf` will also recurse into atoms
* `ring_nf` works as both a tactic and a conv tactic.
  In tactic mode, `ring_nf at h` can be used to rewrite in a hypothesis.
-/
elab (name := ringNF) "ring_nf" tk:"!"? cfg:(config ?) loc:(location)? : tactic => do
  let mut cfg ‚Üê elabConfig cfg
  if tk.isSome then cfg := { cfg with red := .default }
  let loc := (loc.map expandLocation).getD (.targets #[] true)
  let s ‚Üê IO.mkRef {}
  withLocation loc (ringNFLocalDecl s cfg) (ringNFTarget s cfg)
    fun _ ‚Ü¶ throwError "ring_nf failed"

@[inherit_doc ringNF] macro "ring_nf!" cfg:(config)? loc:(location)? : tactic =>
  `(tactic| ring_nf ! $(cfg)? $(loc)?)

@[inherit_doc ringNF] syntax (name := ringNFConv) "ring_nf" "!"? (config)? : conv

/--
Tactic for solving equations of *commutative* (semi)rings, allowing variables in the exponent.

* This version of `ring1` uses `ring_nf` to simplify in atoms.
* The variant `ring1_nf!` will use a more aggressive reducibility setting
  to determine equality of atoms.
-/
elab (name := ring1NF) "ring1_nf" tk:"!"? cfg:(config ?) : tactic => do
  let mut cfg ‚Üê elabConfig cfg
  if tk.isSome then cfg := { cfg with red := .default }
  let s ‚Üê IO.mkRef {}
  liftMetaMAtMain fun g ‚Ü¶ M.run s cfg <| proveEq g

@[inherit_doc ring1NF] macro "ring1_nf!" cfg:(config)? : tactic => `(tactic| ring1_nf ! $(cfg)?)

/-- Elaborator for the `ring_nf` tactic. -/
@[tactic ringNFConv] def elabRingNFConv : Tactic := fun stx ‚Ü¶ match stx with
  | `(conv| ring_nf $[!%$tk]? $(_cfg)?) => withMainContext do
    let mut cfg ‚Üê elabConfig stx[2]
    if tk.isSome then cfg := { cfg with red := .default }
    let s ‚Üê IO.mkRef {}
    Conv.applySimpResult (‚Üê M.run s cfg <| rewrite (‚Üê instantiateMVars (‚Üê Conv.getLhs)))
  | _ => Elab.throwUnsupportedSyntax

@[inherit_doc ringNF] macro "ring_nf!" cfg:(config)? : conv => `(conv| ring_nf ! $(cfg)?)

/--
Tactic for evaluating expressions in *commutative* (semi)rings, allowing for variables in the
exponent.

* `ring!` will use a more aggressive reducibility setting to determine equality of atoms.
* `ring1` fails if the target is not an equality.

For example:
```
example (n : ‚Ñï) (m : ‚Ñ§) : 2^(n+1) * m = 2 * 2^n * m := by ring
example (a b : ‚Ñ§) (n : ‚Ñï) : (a + b)^(n + 2) = (a^2 + b^2 + a * b + b * a) * (a + b)^n := by ring
example (x y : ‚Ñï) : x + id y = y + id x := by ring!
```
-/
macro (name := ring) "ring" : tactic =>
  `(tactic| first | ring1 | ring_nf; trace "Try this: ring_nf")
@[inherit_doc ring] macro "ring!" : tactic =>
  `(tactic| first | ring1! | ring_nf!; trace "Try this: ring_nf!")

/--
The tactic `ring` evaluates expressions in *commutative* (semi)rings.
This is the conv tactic version, which rewrites a target which is a ring equality to `True`.

See also the `ring` tactic.
-/
macro (name := ringConv) "ring" : conv =>
  `(conv| first | discharge => ring1 | ring_nf; tactic => trace "Try this: ring_nf")
@[inherit_doc ringConv] macro "ring!" : conv =>
  `(conv| first | discharge => ring1! | ring_nf!; tactic => trace "Try this: ring_nf!")
