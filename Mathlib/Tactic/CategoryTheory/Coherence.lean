/-
Copyright (c) 2022. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Yuma Mizuno, Oleksandr Manzyuk
-/
import Mathlib.CategoryTheory.Monoidal.Free.Coherence
import Mathlib.Tactic.CategoryTheory.BicategoryCoherence

#align_import category_theory.monoidal.coherence from "leanprover-community/mathlib"@"f187f1074fa1857c94589cc653c786cadc4c35ff"

/-!
# A `coherence` tactic for monoidal categories, and `‚äó‚â´` (composition up to associators)

We provide a `coherence` tactic,
which proves equations where the two sides differ by replacing
strings of monoidal structural morphisms with other such strings.
(The replacements are always equalities by the monoidal coherence theorem.)

A simpler version of this tactic is `pure_coherence`,
which proves that any two morphisms (with the same source and target)
in a monoidal category which are built out of associators and unitors
are equal.

We also provide `f ‚äó‚â´ g`, the `monoidal_comp` operation,
which automatically inserts associators and unitors as needed
to make the target of `f` match the source of `g`.
-/

set_option autoImplicit true

-- Porting note: restore when ported
-- import Mathlib.CategoryTheory.Bicategory.CoherenceTactic

universe v u

open CategoryTheory FreeMonoidalCategory

-- As the lemmas and typeclasses in this file are not intended for use outside of the tactic,
-- we put everything inside a namespace.
namespace Mathlib.Tactic.Coherence

variable {C : Type u} [Category.{v} C] [MonoidalCategory C]
open scoped MonoidalCategory

noncomputable section lifting

/-- A typeclass carrying a choice of lift of an object from `C` to `FreeMonoidalCategory C`.
It must be the case that `projectObj id (LiftObj.lift x) = x` by defeq. -/
class LiftObj (X : C) where
  protected lift : FreeMonoidalCategory C

instance LiftObj_unit : LiftObj (ùüô_ C) := ‚ü®Unit‚ü©

instance LiftObj_tensor (X Y : C) [LiftObj X] [LiftObj Y] : LiftObj (X ‚äó Y) where
  lift := LiftObj.lift X ‚äó LiftObj.lift Y

instance (priority := 100) LiftObj_of (X : C) : LiftObj X := ‚ü®of X‚ü©

/-- A typeclass carrying a choice of lift of a morphism from `C` to `FreeMonoidalCategory C`.
It must be the case that `projectMap id _ _ (LiftHom.lift f) = f` by defeq. -/
class LiftHom {X Y : C} [LiftObj X] [LiftObj Y] (f : X ‚ü∂ Y) where
  protected lift : LiftObj.lift X ‚ü∂ LiftObj.lift Y

instance LiftHom_id (X : C) [LiftObj X] : LiftHom (ùüô X) := ‚ü®ùüô _‚ü©

instance LiftHom_left_unitor_hom (X : C) [LiftObj X] : LiftHom (Œª_ X).hom where
  lift := (Œª_ (LiftObj.lift X)).hom

instance LiftHom_left_unitor_inv (X : C) [LiftObj X] : LiftHom (Œª_ X).inv where
  lift := (Œª_ (LiftObj.lift X)).inv

instance LiftHom_right_unitor_hom (X : C) [LiftObj X] : LiftHom (œÅ_ X).hom where
  lift := (œÅ_ (LiftObj.lift X)).hom

instance LiftHom_right_unitor_inv (X : C) [LiftObj X] : LiftHom (œÅ_ X).inv where
  lift := (œÅ_ (LiftObj.lift X)).inv

instance LiftHom_associator_hom (X Y Z : C) [LiftObj X] [LiftObj Y] [LiftObj Z] :
    LiftHom (Œ±_ X Y Z).hom where
  lift := (Œ±_ (LiftObj.lift X) (LiftObj.lift Y) (LiftObj.lift Z)).hom

instance LiftHom_associator_inv (X Y Z : C) [LiftObj X] [LiftObj Y] [LiftObj Z] :
    LiftHom (Œ±_ X Y Z).inv where
  lift := (Œ±_ (LiftObj.lift X) (LiftObj.lift Y) (LiftObj.lift Z)).inv

instance LiftHom_comp {X Y Z : C} [LiftObj X] [LiftObj Y] [LiftObj Z] (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [LiftHom f] [LiftHom g] : LiftHom (f ‚â´ g) where
  lift := LiftHom.lift f ‚â´ LiftHom.lift g

instance LiftHom_tensor {W X Y Z : C} [LiftObj W] [LiftObj X] [LiftObj Y] [LiftObj Z]
    (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) [LiftHom f] [LiftHom g] : LiftHom (f ‚äó g) where
  lift := LiftHom.lift f ‚äó LiftHom.lift g

/--
A typeclass carrying a choice of monoidal structural isomorphism between two objects.
Used by the `‚äó‚â´` monoidal composition operator, and the `coherence` tactic.
-/
-- We could likely turn this into a `Prop` valued existential if that proves useful.
class MonoidalCoherence (X Y : C) [LiftObj X] [LiftObj Y] where
  hom : X ‚ü∂ Y
  [isIso : IsIso hom]

attribute [instance] MonoidalCoherence.isIso

namespace MonoidalCoherence

@[simps]
instance refl (X : C) [LiftObj X] : MonoidalCoherence X X := ‚ü®ùüô _‚ü©

@[simps]
instance tensor (X Y Z : C) [LiftObj X] [LiftObj Y] [LiftObj Z] [MonoidalCoherence Y Z] :
    MonoidalCoherence (X ‚äó Y) (X ‚äó Z) :=
  ‚ü®ùüô X ‚äó MonoidalCoherence.hom‚ü©

@[simps]
instance tensor_right (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence (ùüô_ C) Y] :
    MonoidalCoherence X (X ‚äó Y) :=
  ‚ü®(œÅ_ X).inv ‚â´ (ùüô X ‚äó MonoidalCoherence.hom)‚ü©

@[simps]
instance tensor_right' (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence Y (ùüô_ C)] :
    MonoidalCoherence (X ‚äó Y) X :=
  ‚ü®(ùüô X ‚äó MonoidalCoherence.hom) ‚â´ (œÅ_ X).hom‚ü©

@[simps]
instance left (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence X Y] :
    MonoidalCoherence (ùüô_ C ‚äó X) Y :=
  ‚ü®(Œª_ X).hom ‚â´ MonoidalCoherence.hom‚ü©

@[simps]
instance left' (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence X Y] :
    MonoidalCoherence X (ùüô_ C ‚äó Y) :=
  ‚ü®MonoidalCoherence.hom ‚â´ (Œª_ Y).inv‚ü©

@[simps]
instance right (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence X Y] :
    MonoidalCoherence (X ‚äó ùüô_ C) Y :=
  ‚ü®(œÅ_ X).hom ‚â´ MonoidalCoherence.hom‚ü©

@[simps]
instance right' (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence X Y] :
    MonoidalCoherence X (Y ‚äó ùüô_ C) :=
  ‚ü®MonoidalCoherence.hom ‚â´ (œÅ_ Y).inv‚ü©

@[simps]
instance assoc (X Y Z W : C) [LiftObj W] [LiftObj X] [LiftObj Y] [LiftObj Z]
    [MonoidalCoherence (X ‚äó (Y ‚äó Z)) W] : MonoidalCoherence ((X ‚äó Y) ‚äó Z) W :=
  ‚ü®(Œ±_ X Y Z).hom ‚â´ MonoidalCoherence.hom‚ü©

@[simps]
instance assoc' (W X Y Z : C) [LiftObj W] [LiftObj X] [LiftObj Y] [LiftObj Z]
    [MonoidalCoherence W (X ‚äó (Y ‚äó Z))] : MonoidalCoherence W ((X ‚äó Y) ‚äó Z) :=
  ‚ü®MonoidalCoherence.hom ‚â´ (Œ±_ X Y Z).inv‚ü©

end MonoidalCoherence

/-- Construct an isomorphism between two objects in a monoidal category
out of unitors and associators. -/
def monoidalIso (X Y : C) [LiftObj X] [LiftObj Y] [MonoidalCoherence X Y] : X ‚âÖ Y :=
  asIso MonoidalCoherence.hom

example (X : C) : X ‚âÖ (X ‚äó (ùüô_ C ‚äó ùüô_ C)) := monoidalIso _ _

example (X1 X2 X3 X4 X5 X6 X7 X8 X9 : C) :
    (ùüô_ C ‚äó (X1 ‚äó X2 ‚äó ((X3 ‚äó X4) ‚äó X5)) ‚äó X6 ‚äó (X7 ‚äó X8 ‚äó X9)) ‚âÖ
    (X1 ‚äó (X2 ‚äó X3) ‚äó X4 ‚äó (X5 ‚äó (ùüô_ C ‚äó X6) ‚äó X7) ‚äó X8 ‚äó X9) :=
  monoidalIso _ _

/-- Compose two morphisms in a monoidal category,
inserting unitors and associators between as necessary. -/
def monoidalComp {W X Y Z : C} [LiftObj X] [LiftObj Y]
    [MonoidalCoherence X Y] (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) : W ‚ü∂ Z :=
  f ‚â´ MonoidalCoherence.hom ‚â´ g

@[inherit_doc Mathlib.Tactic.Coherence.monoidalComp]
scoped[CategoryTheory.MonoidalCategory] infixr:80 " ‚äó‚â´ " =>
  Mathlib.Tactic.Coherence.monoidalComp -- type as \ot \gg

/-- Compose two isomorphisms in a monoidal category,
inserting unitors and associators between as necessary. -/
noncomputable def monoidalIsoComp {W X Y Z : C} [LiftObj X] [LiftObj Y]
    [MonoidalCoherence X Y] (f : W ‚âÖ X) (g : Y ‚âÖ Z) : W ‚âÖ Z :=
  f ‚â™‚â´ asIso MonoidalCoherence.hom ‚â™‚â´ g

@[inherit_doc Mathlib.Tactic.Coherence.monoidalIsoComp]
scoped[CategoryTheory.MonoidalCategory] infixr:80 " ‚â™‚äó‚â´ " =>
  Mathlib.Tactic.Coherence.monoidalIsoComp -- type as \ll \ot \gg

example {U V W X Y : C} (f : U ‚ü∂ V ‚äó (W ‚äó X)) (g : (V ‚äó W) ‚äó X ‚ü∂ Y) : U ‚ü∂ Y := f ‚äó‚â´ g

-- To automatically insert unitors/associators at the beginning or end,
-- you can use `f ‚äó‚â´ ùüô _`
example {W X Y Z : C} (f : W ‚ü∂ (X ‚äó Y) ‚äó Z) : W ‚ü∂ X ‚äó (Y ‚äó Z) := f ‚äó‚â´ ùüô _

@[simp] lemma monoidalComp_refl {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    f ‚äó‚â´ g = f ‚â´ g := by
  simp [monoidalComp]

example {U V W X Y : C} (f : U ‚ü∂ V ‚äó (W ‚äó X)) (g : (V ‚äó W) ‚äó X ‚ü∂ Y) :
    f ‚äó‚â´ g = f ‚â´ (Œ±_ _ _ _).inv ‚â´ g := by
  simp [monoidalComp]

end lifting

open Lean Meta Elab Tactic

/-- Helper function for throwing exceptions. -/
def exception (g : MVarId) (msg : MessageData) : MetaM Œ± := throwTacticEx `monoidal_coherence g msg

/-- Helper function for throwing exceptions with respect to the main goal. -/
def exception' (msg : MessageData) : TacticM Unit := do
  try
    liftMetaTactic (exception (msg := msg))
  catch _ =>
    -- There might not be any goals
    throwError msg

/-- Auxiliary definition for `monoidal_coherence`. -/
-- We could construct this expression directly without using `elabTerm`,
-- but it would require preparing many implicit arguments by hand.
def mkProjectMapExpr (e : Expr) : TermElabM Expr := do
  Term.elabTerm
    (‚Üê ``(FreeMonoidalCategory.projectMap _root_.id _ _ (LiftHom.lift $(‚Üê Term.exprToSyntax e))))
    none

/-- Coherence tactic for monoidal categories. -/
def monoidal_coherence (g : MVarId) : TermElabM Unit := g.withContext do
  withOptions (fun opts => synthInstance.maxSize.set opts
    (max 256 (synthInstance.maxSize.get opts))) do
  -- TODO: is this `dsimp only` step necessary? It doesn't appear to be in the tests below.
  let (ty, _) ‚Üê dsimp (‚Üê g.getType) (‚Üê Simp.Context.ofNames [] true)
  let some (_, lhs, rhs) := (‚Üê whnfR ty).eq? | exception g "Not an equation of morphisms."
  let projectMap_lhs ‚Üê mkProjectMapExpr lhs
  let projectMap_rhs ‚Üê mkProjectMapExpr rhs
  -- This new equation is defeq to the original by assumption
  -- on the `LiftObj` and `LiftHom` instances.
  let g‚ÇÅ ‚Üê g.change (‚Üê mkEq projectMap_lhs projectMap_rhs)
  let [g‚ÇÇ] ‚Üê g‚ÇÅ.applyConst ``congrArg
    | exception g "congrArg failed in coherence"
  let [] ‚Üê g‚ÇÇ.applyConst ``Subsingleton.elim
    | exception g "This shouldn't happen; Subsingleton.elim does not create goals."

/-- Coherence tactic for monoidal categories.
Use `pure_coherence` instead, which is a frontend to this one. -/
elab "monoidal_coherence" : tactic => do monoidal_coherence (‚Üê getMainGoal)

open Mathlib.Tactic.BicategoryCoherence

/--
`pure_coherence` uses the coherence theorem for monoidal categories to prove the goal.
It can prove any equality made up only of associators, unitors, and identities.
```lean
example {C : Type} [Category C] [MonoidalCategory C] :
  (Œª_ (ùüô_ C)).hom = (œÅ_ (ùüô_ C)).hom :=
by pure_coherence
```

Users will typically just use the `coherence` tactic,
which can also cope with identities of the form
`a ‚â´ f ‚â´ b ‚â´ g ‚â´ c = a' ‚â´ f ‚â´ b' ‚â´ g ‚â´ c'`
where `a = a'`, `b = b'`, and `c = c'` can be proved using `pure_coherence`
-/
elab (name := pure_coherence) "pure_coherence" : tactic => do
  let g ‚Üê getMainGoal
  monoidal_coherence g <|> bicategory_coherence g

/--
Auxiliary simp lemma for the `coherence` tactic:
this moves brackets to the left in order to expose a maximal prefix
built out of unitors and associators.
-/
-- We have unused typeclass arguments here.
-- They are intentional, to ensure that `simp only [assoc_LiftHom]` only left associates
-- monoidal structural morphisms.
@[nolint unusedArguments]
lemma assoc_liftHom {W X Y Z : C} [LiftObj W] [LiftObj X] [LiftObj Y]
    (f : W ‚ü∂ X) (g : X ‚ü∂ Y) (h : Y ‚ü∂ Z) [LiftHom f] [LiftHom g] :
    f ‚â´ (g ‚â´ h) = (f ‚â´ g) ‚â´ h :=
  (Category.assoc _ _ _).symm

/--
Internal tactic used in `coherence`.

Rewrites an equation `f = g` as `f‚ÇÄ ‚â´ f‚ÇÅ = g‚ÇÄ ‚â´ g‚ÇÅ`,
where `f‚ÇÄ` and `g‚ÇÄ` are maximal prefixes of `f` and `g` (possibly after reassociating)
which are "liftable" (i.e. expressible as compositions of unitors and associators).
-/
elab (name := liftable_prefixes) "liftable_prefixes" : tactic => do
  withOptions (fun opts => synthInstance.maxSize.set opts
    (max 256 (synthInstance.maxSize.get opts))) do
  evalTactic (‚Üê `(tactic|
    (simp (config := {failIfUnchanged := false}) only
      [monoidalComp, Category.assoc, MonoidalCoherence.hom]) <;>
    (apply (cancel_epi (ùüô _)).1 <;> try infer_instance) <;>
    (simp (config := {failIfUnchanged := false}) only
      [assoc_liftHom, Mathlib.Tactic.BicategoryCoherence.assoc_liftHom‚ÇÇ])))

lemma insert_id_lhs {C : Type*} [Category C] {X Y : C} (f g : X ‚ü∂ Y) (w : f ‚â´ ùüô _ = g) :
    f = g := by
  simpa using w

lemma insert_id_rhs {C : Type*} [Category C] {X Y : C} (f g : X ‚ü∂ Y) (w : f = g ‚â´ ùüô _) :
    f = g := by
  simpa using w

/-- If either the lhs or rhs is not a composition, compose it on the right with an identity. -/
def insertTrailingIds (g : MVarId) : MetaM MVarId := do
  let some (_, lhs, rhs) := (‚Üê withReducible g.getType').eq? | exception g "Not an equality."
  let mut g := g
  if !(lhs.isAppOf ``CategoryStruct.comp) then
    let [g'] ‚Üê g.applyConst ``insert_id_lhs | exception g "failed to apply insert_id_lhs"
    g := g'
  if !(rhs.isAppOf ``CategoryStruct.comp) then
    let [g'] ‚Üê g.applyConst ``insert_id_rhs | exception g "failed to apply insert_id_rhs"
    g := g'
  return g

/-- The main part of `coherence` tactic. -/
-- Porting note: this is an ugly port, using too many `evalTactic`s.
-- We can refactor later into either a `macro` (but the flow control is awkward)
-- or a `MetaM` tactic.
def coherence_loop (maxSteps := 37) : TacticM Unit :=
  match maxSteps with
  | 0 => exception' "`coherence` tactic reached iteration limit"
  | maxSteps' + 1 => do
    -- To prove an equality `f = g` in a monoidal category,
    -- first try the `pure_coherence` tactic on the entire equation:
    evalTactic (‚Üê `(tactic| pure_coherence)) <|> do
    -- Otherwise, rearrange so we have a maximal prefix of each side
    -- that is built out of unitors and associators:
    evalTactic (‚Üê `(tactic| liftable_prefixes)) <|>
      exception' ("Something went wrong in the `coherence` tactic: " ++
        "is the target an equation in a monoidal category?")
    -- The goal should now look like `f‚ÇÄ ‚â´ f‚ÇÅ = g‚ÇÄ ‚â´ g‚ÇÅ`,
    liftMetaTactic MVarId.congrCore
    -- and now we have two goals `f‚ÇÄ = g‚ÇÄ` and `f‚ÇÅ = g‚ÇÅ`.
    -- Discharge the first using `coherence`,
    evalTactic (‚Üê `(tactic| { pure_coherence })) <|>
      exception' "`coherence` tactic failed, subgoal not true in the free monoidal category"
    -- Then check that either `g‚ÇÄ` is identically `g‚ÇÅ`,
    evalTactic (‚Üê `(tactic| rfl)) <|> do
      -- or that both are compositions,
      liftMetaTactic' insertTrailingIds
      liftMetaTactic MVarId.congrCore
      -- with identical first terms,
      evalTactic (‚Üê `(tactic| rfl)) <|>
        exception' "`coherence` tactic failed, non-structural morphisms don't match"
      -- and whose second terms can be identified by recursively called `coherence`.
      coherence_loop maxSteps'

/--
Use the coherence theorem for monoidal categories to solve equations in a monoidal equation,
where the two sides only differ by replacing strings of monoidal structural morphisms
(that is, associators, unitors, and identities)
with different strings of structural morphisms with the same source and target.

That is, `coherence` can handle goals of the form
`a ‚â´ f ‚â´ b ‚â´ g ‚â´ c = a' ‚â´ f ‚â´ b' ‚â´ g ‚â´ c'`
where `a = a'`, `b = b'`, and `c = c'` can be proved using `pure_coherence`.

(If you have very large equations on which `coherence` is unexpectedly failing,
you may need to increase the typeclass search depth,
using e.g. `set_option synthInstance.maxSize 500`.)
-/
syntax (name := coherence) "coherence" : tactic

@[inherit_doc coherence]
elab_rules : tactic
| `(tactic| coherence) => do
  evalTactic (‚Üê `(tactic|
    (simp (config := {failIfUnchanged := false}) only [bicategoricalComp,
      Mathlib.Tactic.BicategoryCoherence.BicategoricalCoherence.hom,
      Mathlib.Tactic.BicategoryCoherence.BicategoricalCoherence.hom',
      monoidalComp]);
    whisker_simps (config := {failIfUnchanged := false})
    ))
  coherence_loop
