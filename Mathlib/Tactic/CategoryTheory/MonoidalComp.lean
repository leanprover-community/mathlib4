/-
Copyright (c) 2022. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Yuma Mizuno, Oleksandr Manzyuk
-/
import Mathlib.CategoryTheory.Monoidal.Category

/-!
# Monoidal composition `âŠ—â‰«` (composition up to associators)

We provide `f âŠ—â‰« g`, the `monoidal_comp` operation,
which automatically inserts associators and unitors as needed
to make the target of `f` match the source of `g`.
-/

universe v u

open CategoryTheory MonoidalCategory

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] [MonoidalCategory C]

open scoped MonoidalCategory

/--
A typeclass carrying a choice of monoidal structural isomorphism between two objects.
Used by the `âŠ—â‰«` monoidal composition operator, and the `coherence` tactic.
-/
-- We could likely turn this into a `Prop` valued existential if that proves useful.
class MonoidalCoherence (X Y : C) where
  hom : X âŸ¶ Y
  [isIso : IsIso hom]

attribute [instance] MonoidalCoherence.isIso

namespace MonoidalCoherence

@[simps]
instance refl (X : C) : MonoidalCoherence X X := âŸ¨ðŸ™ _âŸ©

@[simps]
instance whiskerLeft (X Y Z : C) [MonoidalCoherence Y Z] :
    MonoidalCoherence (X âŠ— Y) (X âŠ— Z) :=
  âŸ¨ðŸ™ X âŠ— MonoidalCoherence.homâŸ©

@[simps]
instance whiskerRight (X Y Z : C) [MonoidalCoherence X Y] :
    MonoidalCoherence (X âŠ— Z) (Y âŠ— Z) :=
  âŸ¨MonoidalCoherence.hom âŠ— ðŸ™ ZâŸ©

@[simps]
instance tensor_right (X Y : C) [MonoidalCoherence (ðŸ™_ C) Y] :
    MonoidalCoherence X (X âŠ— Y) :=
  âŸ¨(Ï_ X).inv â‰« (ðŸ™ X âŠ—  MonoidalCoherence.hom)âŸ©

@[simps]
instance tensor_right' (X Y : C) [MonoidalCoherence Y (ðŸ™_ C)] :
    MonoidalCoherence (X âŠ— Y) X :=
  âŸ¨(ðŸ™ X âŠ— MonoidalCoherence.hom) â‰« (Ï_ X).homâŸ©

@[simps]
instance left (X Y : C) [MonoidalCoherence X Y] :
    MonoidalCoherence (ðŸ™_ C âŠ— X) Y :=
  âŸ¨(Î»_ X).hom â‰« MonoidalCoherence.homâŸ©

@[simps]
instance left' (X Y : C) [MonoidalCoherence X Y] :
    MonoidalCoherence X (ðŸ™_ C âŠ— Y) :=
  âŸ¨MonoidalCoherence.hom â‰« (Î»_ Y).invâŸ©

@[simps]
instance right (X Y : C) [MonoidalCoherence X Y] :
    MonoidalCoherence (X âŠ— ðŸ™_ C) Y :=
  âŸ¨(Ï_ X).hom â‰« MonoidalCoherence.homâŸ©

@[simps]
instance right' (X Y : C) [MonoidalCoherence X Y] :
    MonoidalCoherence X (Y âŠ— ðŸ™_ C) :=
  âŸ¨MonoidalCoherence.hom â‰« (Ï_ Y).invâŸ©

@[simps]
instance assoc (X Y Z W : C) [MonoidalCoherence (X âŠ— (Y âŠ— Z)) W] :
    MonoidalCoherence ((X âŠ— Y) âŠ— Z) W :=
  âŸ¨(Î±_ X Y Z).hom â‰« MonoidalCoherence.homâŸ©

@[simps]
instance assoc' (W X Y Z : C) [MonoidalCoherence W (X âŠ— (Y âŠ— Z))] :
    MonoidalCoherence W ((X âŠ— Y) âŠ— Z) :=
  âŸ¨MonoidalCoherence.hom â‰« (Î±_ X Y Z).invâŸ©

end MonoidalCoherence

noncomputable section

/-- Construct an isomorphism between two objects in a monoidal category
out of unitors and associators. -/
def monoidalIso (X Y : C) [MonoidalCoherence X Y] : X â‰… Y :=
  asIso MonoidalCoherence.hom

/-- Notation for identities up to unitors and associators. -/
scoped[CategoryTheory.MonoidalCategory] notation " âŠ—ðŸ™ " =>
  MonoidalCoherence.hom -- type as \ot ðŸ™

/-- Compose two morphisms in a monoidal category,
inserting unitors and associators between as necessary. -/
def monoidalComp {W X Y Z : C} [MonoidalCoherence X Y] (f : W âŸ¶ X) (g : Y âŸ¶ Z) : W âŸ¶ Z :=
  f â‰« âŠ—ðŸ™ â‰« g

@[inherit_doc monoidalComp]
scoped[CategoryTheory.MonoidalCategory] infixr:80 " âŠ—â‰« " =>
  monoidalComp -- type as \ot \gg

@[simp] lemma monoidalComp_refl {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    f âŠ—â‰« g = f â‰« g := by
  simp [monoidalComp]

/-- Compose two isomorphisms in a monoidal category,
inserting unitors and associators between as necessary. -/
def monoidalIsoComp {W X Y Z : C} [MonoidalCoherence X Y] (f : W â‰… X) (g : Y â‰… Z) : W â‰… Z :=
  f â‰ªâ‰« asIso MonoidalCoherence.hom â‰ªâ‰« g

@[inherit_doc monoidalIsoComp]
scoped[CategoryTheory.MonoidalCategory] infixr:80 " â‰ªâŠ—â‰« " =>
  monoidalIsoComp -- type as \ll \ot \gg
