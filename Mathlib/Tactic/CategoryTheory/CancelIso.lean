/-
Copyright (c) 2026 Robin Carlier. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robin Carlier
-/
module

public meta import Mathlib.Tactic.Push
public import Mathlib.CategoryTheory.Iso

/-!
# Simproc for canceling morphisms with their inverses

This module implements the `cancelIso` simproc, which triggers on expressions of the form `f â‰« g`.

If `g` is not a composition itself, it checks whether `f` is inverse to `g`,
by checking if `f` has an `IsIso` instance, and then running `push inv` on `inv f` and on `g`.
If the check succeeds, then `f â‰« g` is rewritten to `ğŸ™ _`.

The procedure handles the case of an expression of the `g = h â‰« k` as a special case, in this case,
the procedure checks if `f` and `h` are inverses to each other, and the procedure thus rewrites
`f â‰« g â‰« h` to `h`. This is useful as simp-normal forms in category theory are right-associated.

For instance, the simproc will successfully rewrite expressions such as
`F.map (G.map (inv (H.map (e.hom)))) â‰« F.map (G.map (H.map (e.inv)))` to `ğŸ™ _`
because `CategoyTheory.Functor.map_inv` is a `@[push â†]` lemma, and
`CategoyTheory.IsIso.Iso.inv_hom` is a `[push]` lemma.

This procedure is mostly intended as a post-procedure: it will work better if `f` and `g`
have already been traversed beforehand.

-/

public meta section
open Lean Meta CategoryTheory

namespace Mathlib.Tactic.CategoryTheory.CancelIso

/-- Version of `IsIso.hom_inv_id` for internal use of the `cancelIso` simproc. Do not use. -/
lemma hom_inv_id_of_eq {C : Type*} [Category* C] {x y : C}
    (f : x âŸ¶ y) [IsIso f] (g : y âŸ¶ x) (h : inv f = g) : f â‰« g = ğŸ™ _ := by
  rw [â† h]
  exact IsIso.hom_inv_id f

/-- Version of `IsIso.hom_inv_id_assoc` for internal use of the `cancelIso` simproc. Do not use. -/
lemma hom_inv_id_of_eq_assoc {C : Type*} [Category* C] {x y : C}
    (f : x âŸ¶ y) [IsIso f] (g : y âŸ¶ x) (h : inv f = g) {z : C} (k : x âŸ¶ z) : f â‰« g â‰« k = k := by
  rw [â† h]
  exact IsIso.hom_inv_id_assoc f k

/-- The `cancelIso` simproc triggers on expressions of the form `f â‰« g`.

If `g` is not a composition itself, it checks whether `f` is inverse to `g`
by checking if `f` has an `IsIso` instance and then by running `push inv` on `inv f` and on `g`.
If the check succeeds, then `f â‰« g` is rewritten to `ğŸ™ _`.

The procedure handles the case of an expression of the `g = h â‰« k` as a special case, in this case,
the procedure checks if `f` and `h` are inverses to each other, and the procedure rewrites
`f â‰« g â‰« h` to `h` if that is the case.
This is useful as simp-normal forms in category theory are right-associated.

For instance, the simproc will successfully rewrite expressions such as
`F.map (G.map (inv (H.map (e.hom)))) â‰« F.map (G.map (H.map (e.inv)))` to `ğŸ™ _`
because `CategoyTheory.Functor.map_inv` is a `@[push â†]` lemma, and
`CategoyTheory.IsIso.Iso.inv_hom` is a `[push]` lemma.

This procedure is mostly intended as a post-procedure: it will work better if `f` and `g`
have already been traversed beforehand. -/
def cancelIsoSimproc : Simp.Simproc := fun e => withReducible do -- is withReducible necessary here?
  let e_whnf â† whnf e
  let_expr CategoryStruct.comp C instCat x y t f g := e_whnf | return .continue
  match_expr g with
  -- Right_associated expressions needs their own logic.
  | CategoryStruct.comp _ _ _ z _ g h =>
    -- Canâ€™t expect a cancelation if the objects donâ€™t match
    unless z == x do return .continue
    -- Canâ€™t expect a cancellation if `f` is not an iso.
    let some inst â† synthInstance? <| â† mkAppM ``IsIso #[f] | return .continue
    let inv_f â† mkAppOptM ``CategoryTheory.inv #[none, none, none, none, f, inst]
    let pushed_inv â† Mathlib.Tactic.Push.pushCore (.const ``CategoryTheory.inv) {} none inv_f
    let pushed_g â† Mathlib.Tactic.Push.pushCore (.const ``CategoryTheory.inv) {} none <| g
    unless â† isDefEq pushed_inv.expr pushed_g.expr do return .continue
    -- Builds the proof inv f = g first:
    let pâ‚€ â† mkEqTrans (pushed_inv.proof?.getD (â† mkEqRefl inv_f))
      (â† mkEqSymm <| pushed_g.proof?.getD (â† mkEqRefl g))
    -- Builds the proof that `f â‰« g â‰« h = h.
    let P â† mkAppOptM ``hom_inv_id_of_eq_assoc #[C, none, x, y, f, inst, g, pâ‚€, none, h]
    return .done (.mk h (.some P) false)
  -- Otherwise, same logic but with hom_inv_id_of_eq instead of hom_inv_id_of_eq_assoc
  | _ =>
    unless t == x do return .continue
    let some inst â† synthInstance? <| â† mkAppM ``IsIso #[f] | return .continue
    let inv_f â† mkAppOptM ``CategoryTheory.inv #[none, none, none, none, f, inst]
    let pushed_inv â† Mathlib.Tactic.Push.pushCore (.const ``CategoryTheory.inv) {} none inv_f
    let pushed_g â† Mathlib.Tactic.Push.pushCore (.const ``CategoryTheory.inv) {} none <| g
    unless â† isDefEq pushed_inv.expr pushed_g.expr do return .continue
    let pâ‚€ â† mkEqTrans (pushed_inv.proof?.getD (â† mkEqRefl inv_f))
      (â† mkEqSymm <| pushed_g.proof?.getD (â† mkEqRefl g))
    let P â† mkAppOptM ``hom_inv_id_of_eq #[C, none, x, y, f, inst, g, pâ‚€]
    return .done (.mk (â† mkAppOptM ``CategoryStruct.id #[C, instCat, x]) (.some P) false)

end Mathlib.Tactic.CategoryTheory.CancelIso

/-- The `cancelIso` simproc triggers on expressions of the form `f â‰« g`.

If `g` is not a composition itself, it checks whether `f` is inverse to `g`
by checking if `f` has an `IsIso` instance and then by running `push inv` on `inv f` and on `g`.
If the check succeeds, then `f â‰« g` is rewritten to `ğŸ™ _`.

The procedure handles the case of an expression of the `g = h â‰« k` as a special case, in this case,
the procedure checks if `f` and `h` are inverses to each other, and the procedure rewrites
`f â‰« g â‰« h` to `h` if that is the case.
This is useful as simp-normal forms in category theory are right-associated.

For instance, the simproc will successfully rewrite expressions such as
`F.map (G.map (inv (H.map (e.hom)))) â‰« F.map (G.map (H.map (e.inv)))` to `ğŸ™ _`
because `CategoyTheory.Functor.map_inv` is a `@[push â†]` lemma, and
`CategoyTheory.IsIso.Iso.inv_hom` is a `[push]` lemma.

This procedure is mostly intended as a post-procedure: it will work better if `f` and `g`
have already been traversed beforehand. -/
simproc_decl cancelIso (CategoryStruct.comp (self := ?x) _ _) :=
  Mathlib.Tactic.CategoryTheory.CancelIso.cancelIsoSimproc
