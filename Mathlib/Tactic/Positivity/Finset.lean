/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Finset.Density
import Mathlib.Tactic.Positivity.Core

/-!
# Positivity extensions for finsets

This file provides a few `positivity` extensions that cannot be in either the finset files (because
they don't know about ordered fields) or in `Tactic.Positivity.Basic` (because it doesn't want to
know about finiteness).
-/

namespace Mathlib.Meta.Positivity

open Qq Lean Meta Finset

/-- Extension for `Finset.card`. `s.card` is positive if `s` is nonempty.

It calls `Mathlib.Meta.proveFinsetNonempty` to attempt proving that the finset is nonempty. -/
@[positivity Finset.card _]
def evalFinsetCard : PositivityExt where eval {u Î±} _ _ e := do
  match u, Î±, e with
  | 0, ~q(â„•), ~q(Finset.card $s) =>
    let some ps â† proveFinsetNonempty s | return .none
    assertInstancesCommute
    return .positive q(Finset.Nonempty.card_pos $ps)
  | _ => throwError "not Finset.card"

/-- Extension for `Fintype.card`. `Fintype.card Î±` is positive if `Î±` is nonempty. -/
@[positivity Fintype.card _]
def evalFintypeCard : PositivityExt where eval {u Î±} _ _ e := do
  match u, Î±, e with
  | 0, ~q(â„•), ~q(@Fintype.card $Î² $instÎ²) =>
    let instÎ²no â† synthInstanceQ q(Nonempty $Î²)
    assumeInstancesCommute
    return .positive q(@Fintype.card_pos $Î² $instÎ² $instÎ²no)
  | _ => throwError "not Fintype.card"

/-- Extension for `Finset.dens`. `s.card` is positive if `s` is nonempty.

It calls `Mathlib.Meta.proveFinsetNonempty` to attempt proving that the finset is nonempty. -/
@[positivity Finset.dens _]
def evalFinsetDens : PositivityExt where eval {u ð•œ} _ _ e := do
  match u, ð•œ, e with
  | 0, ~q(â„šâ‰¥0), ~q(@Finset.dens $Î± $instÎ± $s) =>
    let some ps â† proveFinsetNonempty s | return .none
    assumeInstancesCommute
    return .positive q(@Nonempty.dens_pos $Î± $instÎ± $s $ps)
  | _, _, _ => throwError "not Finset.dens"

variable {Î± : Type*} {s : Finset Î±}

example : 0 â‰¤ s.card := by positivity
example (hs : s.Nonempty) : 0 < s.card := by positivity

variable [Fintype Î±]

example : 0 â‰¤ Fintype.card Î± := by positivity
example : 0 â‰¤ dens s := by positivity
example (hs : s.Nonempty) : 0 < dens s := by positivity
example (hs : s.Nonempty) : dens s â‰  0 := by positivity
example [Nonempty Î±] : 0 < (univ : Finset Î±).card := by positivity
example [Nonempty Î±] : 0 < Fintype.card Î± := by positivity
example [Nonempty Î±] : 0 < dens (univ : Finset Î±) := by positivity
example [Nonempty Î±] : dens (univ : Finset Î±) â‰  0 := by positivity

example {G : Type*} {A : Finset G} :
  let f := fun _ : G â†¦ 1; (âˆ€ s, f s ^ 2 = 1) â†’ 0 â‰¤ A.card := by
  intros
  positivity -- Should succeed despite failing to prove `A` is nonempty.

end Mathlib.Meta.Positivity
