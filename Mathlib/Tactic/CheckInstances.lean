/-
Copyright (c) 2023 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Lean.Expr.Traverse
import Mathlib.Control.Basic

/-!
# The `check_instances` tactic

Resynthesizes all typeclasses in the goal,
and reports for each whether this succeeds,
and if so how closely it matches the original.
-/
open Lean Elab Meta Tactic
namespace Lean.Expr

/--
Visit every typeclass instance in the `Expr`, calling a function `f` at each.
-/
def visitInstances (f : Œ± ‚Üí Expr ‚Üí MetaM Œ±) (init : Œ±) (e : Expr) : MetaM Œ± := do
  e.traverse (init := init) fun a i => do
    let ty ‚Üê inferType i
    if ¬¨ ty.isForall && (‚Üê isClass? ty).isSome then
       f a i
    else
      pure a

/--
Result type for `checkInstance`.
Records whether an instance can be resynthesized,
and if so how closely it matches the original.
-/
inductive CheckInstance
| failed
| not_defeq (i : Expr)
| defeq (i : Expr)
| defeq_reducible (i : Expr)
| exact (i : Expr)

/--
Attempt to resynthesize a typeclass, reporting whether this is possible,
and if so how closely it matches the original.
-/
def checkInstance (e : Expr) : MetaM CheckInstance := do
  if let .some t ‚Üê try? (do synthInstance (‚Üê inferType e)) then
    if e == t then
      return .exact t
    if ‚Üê withReducibleAndInstances <| isDefEq e t then
      return .defeq_reducible t
    else if ‚Üê isDefEq e t then
      return .defeq t
    else
      return .not_defeq t
  else
    return .failed

/--
Find all typeclass instances in an expression, and try resynthesizing them,
reporting whether this is possible and if so how closely it matches the original.
-/
def checkInstances (e : Expr) : MetaM (Array (Expr √ó CheckInstance)) :=
  e.visitInstances (init := #[]) fun a i => return a.push ‚ü®i, ‚Üê checkInstance i‚ü©

end Lean.Expr

/--
A tactic for resynthesizing all typeclass instances in the goal,
reporting if they can be resynthesized, and if so how closely they match the original terms.
-/
elab "check_instances" : tactic => do
  let results ‚Üê (‚Üê getMainTarget).checkInstances
  for ‚ü®e, r‚ü© in results do
    match r with
    | .failed =>
        logInfo m!"üí•: failed to resynthesize {‚Üê inferType e}"
    | .not_defeq i =>
        logInfo m!"‚ùå: resynthesized {‚Üê inferType e}, but found\n  {i} != {e}"
    | .defeq i =>
        logInfo m!"üü°: resynthesized {‚Üê inferType e}, up to defeq\n  {i} vs {e}"
    | .defeq_reducible i =>
        logInfo m!"‚úÖ: resynthesized {‚Üê inferType e}, up to reducible defeq:\n  {i} vs {e}"
    | .exact _ =>
        logInfo m!"‚úÖ: resynthesized {‚Üê inferType e}"
