/-
Copyright (c) 2023 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Lean.Linter.Util
import Batteries.Data.Array.Basic
import Batteries.Tactic.Lint
import Mathlib.Tactic.Lemma

/-!
# Linters for Mathlib

In this file we define additional linters for mathlib.

Perhaps these should be moved to Batteries in the future.
-/

namespace Std.Tactic.Lint
open Lean Meta

/--
Linter that checks whether a structure should be in Prop.
-/
@[env_linter] def structureInType : Linter where
  noErrorsFound := "no structures that should be in Prop found."
  errorsFound := "FOUND STRUCTURES THAT SHOULD BE IN PROP."
  test declName := do
    unless isStructure (← getEnv) declName do return none
    -- remark: using `Lean.Meta.isProp` doesn't suffice here, because it doesn't (always?)
    -- recognize predicates as propositional.
    let isProp ← forallTelescopeReducing (← inferType (← mkConstWithLevelParams declName))
      fun _ ty => return ty == .sort .zero
    if isProp then return none
    let projs := (getStructureInfo? (← getEnv) declName).get!.fieldNames
    if projs.isEmpty then return none -- don't flag empty structures
    let allProofs ← projs.allM (do isProof <| ← mkConstWithLevelParams <| declName ++ ·)
    unless allProofs do return none
    return m!"all fields are propositional but the structure isn't."

/-- Linter that check that all `deprecated` tags come with `since` dates. -/
@[env_linter] def deprecatedNoSince : Linter where
  noErrorsFound := "no `deprecated` tags without `since` dates."
  errorsFound := "FOUND `deprecated` tags without `since` dates."
  test declName := do
    let some info := Lean.Linter.deprecatedAttr.getParam? (← getEnv) declName | return none
    match info.since? with
    | some _ => return none -- TODO: enforce `YYYY-MM-DD` format
    | none => return m!"`deprecated` attribute without `since` date"

end Std.Tactic.Lint

namespace Mathlib.Linter

/-!
#  `dupNamespace` linter

The `dupNamespace` linter produces a warning when a declaration contains the same namespace
at least twice consecutively.

For instance, `Nat.Nat.foo` and `One.two.two` trigger a warning, while `Nat.One.Nat` does not.
-/

/--
The `dupNamespace` linter is set on by default.  Lean emits a warning on any declaration that
contains the same namespace at least twice consecutively.

For instance, `Nat.Nat.foo` and `One.two.two` trigger a warning, while `Nat.One.Nat` does not.

*Note.*
This linter will not detect duplication in namespaces of autogenerated declarations
(other than the one whose `declId` is present in the source declaration).
-/
register_option linter.dupNamespace : Bool := {
  defValue := true
  descr := "enable the duplicated namespace linter"
}

namespace DupNamespaceLinter

open Lean Parser Elab Command Meta

/-- Gets the value of the `linter.dupNamespace` option. -/
def getLinterDupNamespace (o : Options) : Bool := Linter.getLinterValue linter.dupNamespace o

/-- `getIds stx` extracts the `declId` nodes from the `Syntax` `stx`.
If `stx` is an `alias` or an `export`, then it extracts an `ident`, instead of a `declId`. -/
partial
def getIds : Syntax → Array Syntax
  | .node _ `Batteries.Tactic.Alias.alias args => args[2:3]
  | .node _ ``Lean.Parser.Command.export args => (args[3:4] : Array Syntax).map (·[0])
  | stx@(.node _ _ args) =>
    ((args.attach.map fun ⟨a, _⟩ => getIds a).foldl (· ++ ·) #[stx]).filter (·.getKind == ``declId)
  | _ => default

@[inherit_doc linter.dupNamespace]
def dupNamespace : Linter where run := withSetOptionIn fun stx => do
  if getLinterDupNamespace (← getOptions) then
    match getIds stx with
      | #[id] =>
        let ns := (← getScope).currNamespace
        let declName := ns ++ (if id.getKind == ``declId then id[0].getId else id.getId)
        let nm := declName.components
        let some (dup, _) := nm.zip (nm.tailD []) |>.find? fun (x, y) => x == y
          | return
        Linter.logLint linter.dupNamespace id
          m!"The namespace '{dup}' is duplicated in the declaration '{declName}'"
      | _ => return

initialize addLinter dupNamespace

end DupNamespaceLinter

/-!
#  The "noRepeatedVariable" linter

The "noRepeatedVariable" linter emits a warning somewhere.
-/

open Lean Elab

/-- The "noRepeatedVariable" linter emits a warning when a variable in scope is repeated in a
command. -/
register_option linter.noRepeatedVariable : Bool := {
  defValue := true
  descr := "enable the noRepeatedVariable linter"
}

/-- `getBinders stx` returns the array of all the im/ex-plicit binders contained in `stx`. -/
partial
def getBinders : Syntax → Array Syntax
  | stx@(.node _ kind args) =>
    let fargs := (args.map getBinders).flatten
    if kind == ``Lean.Parser.Term.implicitBinder || kind == ``Lean.Parser.Term.explicitBinder then
      fargs.push stx else fargs
  | _ => #[]

/-- `getDeclBinders stx` returns the array of all the binders contained in `stx`, assuming that
`stx` is a `theorem`, `lemma`, `example` and the binders are the ones appearing "before the `:`". -/
def getDeclBinders : Syntax → (Array Syntax)
  | `($_dm:declModifiers theorem $_did:declId $ds* : $_t $_dv:declVal) => ds
  | `($_dm:declModifiers lemma   $_did:declId $ds* : $_t $_dv:declVal) => ds
  | `($_dm:declModifiers example              $ds* : $_t $_dv:declVal) => ds
  | `($_dm:declModifiers example              $ds*       $_dv:declVal) => ds
  | _ => #[]

namespace NoRepeatedVariable

/-- Gets the value of the `linter.noRepeatedVariable` option. -/
def getLinterHash (o : Options) : Bool := Linter.getLinterValue linter.noRepeatedVariable o

variable {α} [BEq α] in
/--
`largestSublist a b` extracts the largest initial segment of `a` that is an ordered sublist of `b`.
-/
def largestSublist : List α → List α → List α
  | [], _ => []
  | a::as, l =>
    let l_until_a := l.dropWhile (· != a)
    if ! l_until_a.isEmpty then
      a :: largestSublist as (l_until_a.drop 1)
    else []

open Command in
@[inherit_doc Mathlib.Linter.linter.noRepeatedVariable]
def noRepeatedVariableLinter : Linter where
  run := withSetOptionIn fun stx => do
    unless getLinterHash (← getOptions) do
      return
    if (← MonadState.get).messages.hasErrors then
      return
    if stx.isOfKind ``Lean.Parser.Command.variable then return
    let binders := getDeclBinders stx
    let sc ← getScope
    let vars := sc.varDecls.map (·.raw)
    let repeatedBinders := largestSublist binders.toList vars.toList --binders.filter vars.contains
    for d in repeatedBinders do
      Linter.logLint linter.noRepeatedVariable d m!"repeated: {← `(command| variable $(⟨d⟩))}"

initialize addLinter noRepeatedVariableLinter

end NoRepeatedVariable

/-!
#  `oneLineAlign` linter

The `oneLineAlign` linter checks that `#align` statements span a single line,
which is desirable as it allows them to be parsed by very simple parsers.
-/

/--
The `oneLineAlign` linter is set on by default.  Lean emits a warning on `#align` statements
spanning more than one line.
-/
register_option linter.oneLineAlign : Bool := {
  defValue := true
  descr := "enable the one-line align linter"
}

namespace OneLineAlignLinter

open Lean

/-- Gets the value of the `linter.oneLineAlign` option. -/
def getLinterOneLineAlign (o : Options) : Bool := Linter.getLinterValue linter.oneLineAlign o

@[inherit_doc linter.oneLineAlign]
def oneLineAlign : Linter where run := withSetOptionIn fun stx => do
  if getLinterOneLineAlign (← getOptions) then
    if stx.isOfKind `Mathlib.Prelude.Rename.align then
      if let some spos := stx.getRange? then
        let fm ← getFileMap
        let lines := (fm.toPosition spos.stop).line - (fm.toPosition spos.start).line + 1
        if lines != 1 then
          Linter.logLint linter.oneLineAlign stx
            m!"This `#align` spans {lines} lines, instead of just one.\n\
              Do not worry, the 100 character limit does not apply to `#align` statements!"

initialize addLinter oneLineAlign

end OneLineAlignLinter

/-!
# The "EndOf" linter

The "EndOf" linter emits a warning on non-closed `section`s and `namespace`s.
It allows the "outermost" `noncomputable section` to be left open (whether or not it is named).
-/

open Lean Elab Command

/-- The "EndOf" linter emits a warning on non-closed `section`s and `namespace`s.
It allows the "outermost" `noncomputable section` to be left open (whether or not it is named).
-/
register_option linter.endOf : Bool := {
  defValue := true
  descr := "enable the EndOf linter"
}

namespace EndOf

/-- Gets the value of the `linter.endOf` option. -/
def getLinterHash (o : Options) : Bool := Linter.getLinterValue linter.endOf o

@[inherit_doc Mathlib.Linter.linter.endOf]
def endOfLinter : Linter where run := withSetOptionIn fun stx ↦ do
    -- Only run this linter at the end of a module.
    unless stx.isOfKind ``Lean.Parser.Command.eoi do return
    if getLinterHash (← getOptions) && !(← MonadState.get).messages.hasErrors then
      let sc ← getScopes
      -- The last scope is always the "base scope", corresponding to no active `section`s or
      -- `namespace`s. We are interested in any *other* unclosed scopes.
      if sc.length == 1 then return
      let ends := sc.dropLast.map fun s ↦ (s.header, s.isNoncomputable)
      -- If the outermost scope corresponds to a `noncomputable section`, we ignore it.
      let ends := if ends.getLast!.2 then ends.dropLast else ends
      -- If there are any further un-closed scopes, we emit a warning.
      if !ends.isEmpty then
        let ending := (ends.map Prod.fst).foldl (init := "") fun a b ↦
          a ++ s!"\n\nend{if b == "" then "" else " "}{b}"
        Linter.logLint linter.endOf stx
         m!"unclosed sections or namespaces; expected: '{ending}'"

initialize addLinter endOfLinter

end EndOf

end Mathlib.Linter
