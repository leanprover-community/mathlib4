/-
Copyright (c) 2022 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Kyle Miller
-/
import Mathlib.Data.Nat.Sqrt
import Mathlib.Tactic.NormNum

/-! # `norm_num` extension for `Nat.sqrt`

This module defines a `norm_num` extension for `Nat.sqrt`.
-/

namespace Tactic

namespace NormNum

open Qq Lean Elab.Tactic Mathlib.Meta.NormNum

lemma nat_sqrt_helper {x y r : ‚Ñï} (hr : y * y + r = x) (hle : Nat.ble r (2 * y)) :
    Nat.sqrt x = y := by
  rw [‚Üê hr, ‚Üê pow_two]
  -- ‚ä¢ Nat.sqrt (y ^ 2 + r) = y
  rw [two_mul] at hle
  -- ‚ä¢ Nat.sqrt (y ^ 2 + r) = y
  exact Nat.sqrt_add_eq' _ (Nat.le_of_ble_eq_true hle)
  -- üéâ no goals

theorem isNat_sqrt : {x nx z : ‚Ñï} ‚Üí IsNat x nx ‚Üí Nat.sqrt nx = z ‚Üí IsNat (Nat.sqrt x) z
  | _, _, _, ‚ü®rfl‚ü©, rfl => ‚ü®rfl‚ü©

/-- Given the natural number literal `ex`, returns its square root as a natural number literal
and an equality proof. Panics if `ex` isn't a natural number literal. -/
def proveNatSqrt (ex : Q(‚Ñï)) : (ey : Q(‚Ñï)) √ó Q(Nat.sqrt $ex = $ey) :=
  match ex.natLit! with
  | 0 => show (ey : Q(‚Ñï)) √ó Q(Nat.sqrt 0 = $ey) from ‚ü®mkRawNatLit 0, q(Nat.sqrt_zero)‚ü©
  | 1 => show (ey : Q(‚Ñï)) √ó Q(Nat.sqrt 1 = $ey) from ‚ü®mkRawNatLit 1, q(Nat.sqrt_one)‚ü©
  | x =>
    let y := Nat.sqrt x
    have ey : Q(‚Ñï) := mkRawNatLit y
    have er : Q(‚Ñï) := mkRawNatLit (x - y * y)
    have hr : Q($ey * $ey + $er = $ex) := (q(Eq.refl $ex) : Expr)
    have hle : Q(Nat.ble $er (2 * $ey)) := (q(Eq.refl true) : Expr)
    ‚ü®ey, q(nat_sqrt_helper $hr $hle)‚ü©

/-- Evaluates the `Nat.sqrt` function. -/
@[norm_num Nat.sqrt _]
def evalNatSqrt : NormNumExt where eval {u Œ±} e := do
  let .app _ (x : Q(‚Ñï)) ‚Üê Meta.whnfR e | failure
  let s‚Ñï : Q(AddMonoidWithOne ‚Ñï) := q(instAddMonoidWithOneNat)
  let ‚ü®ex, p‚ü© ‚Üê deriveNat x s‚Ñï
  let ‚ü®ey, pf‚ü© := proveNatSqrt ex
  let pf' : Q(IsNat (Nat.sqrt $x) $ey) := q(isNat_sqrt $p $pf)
  return .isNat s‚Ñï ey pf'

end NormNum

end Tactic
