/-
Copyright (c) 2022 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Std.Lean.Parser
import Std.Lean.Meta.DiscrTree
import Mathlib.Algebra.Invertible
import Mathlib.Data.Rat.Cast
import Mathlib.Data.Nat.Basic
import Mathlib.Data.Int.Basic
import Mathlib.Tactic.Conv
import Mathlib.Util.Qq

/-!
## `norm_num` core functionality

This file sets up the `norm_num` tactic and the `@[norm_num]` attribute,
which allow for plugging in new normalization functionality around a simp-based driver.
The actual behavior is in `@[norm_num]`-tagged definitions in `Tactic.NormNum.Basic`
and elsewhere.
-/

set_option autoImplicit true

open Lean hiding Rat mkRat
open Lean.Meta Qq Lean.Elab Term

/-- Attribute for identifying `norm_num` extensions. -/
syntax (name := norm_num) "norm_num " term,+ : attr

namespace Mathlib
namespace Meta.NormNum

initialize registerTraceClass `Tactic.norm_num

/-- Assert that an element of a semiring is equal to the coercion of some natural number. -/
structure IsNat [AddMonoidWithOne Œ±] (a : Œ±) (n : ‚Ñï) : Prop where
  /-- The element is equal to the coercion of the natural number. -/
  out : a = n

theorem IsNat.raw_refl (n : ‚Ñï) : IsNat n n := ‚ü®rfl‚ü©

/--
A "raw nat cast" is an expression of the form `(Nat.rawCast lit : Œ±)` where `lit` is a raw
natural number literal. These expressions are used by tactics like `ring` to decrease the number
of typeclass arguments required in each use of a number literal at type `Œ±`.
-/
@[simp] def _root_.Nat.rawCast [AddMonoidWithOne Œ±] (n : ‚Ñï) : Œ± := n

theorem IsNat.to_eq [AddMonoidWithOne Œ±] {n} : {a a' : Œ±} ‚Üí IsNat a n ‚Üí n = a' ‚Üí a = a'
  | _, _, ‚ü®rfl‚ü©, rfl => rfl

theorem IsNat.to_raw_eq [AddMonoidWithOne Œ±] : IsNat (a : Œ±) n ‚Üí a = n.rawCast
  | ‚ü®e‚ü© => e

theorem IsNat.of_raw (Œ±) [AddMonoidWithOne Œ±] (n : ‚Ñï) : IsNat (n.rawCast : Œ±) n := ‚ü®rfl‚ü©

@[elab_as_elim]
theorem isNat.natElim {p : ‚Ñï ‚Üí Prop} : {n : ‚Ñï} ‚Üí {n' : ‚Ñï} ‚Üí IsNat n n' ‚Üí p n' ‚Üí p n
  | _, _, ‚ü®rfl‚ü©, h => h

/-- Assert that an element of a ring is equal to the coercion of some integer. -/
structure IsInt [Ring Œ±] (a : Œ±) (n : ‚Ñ§) : Prop where
  /-- The element is equal to the coercion of the integer. -/
  out : a = n

/--
A "raw int cast" is an expression of the form:

* `(Nat.rawCast lit : Œ±)` where `lit` is a raw natural number literal
* `(Int.rawCast (Int.negOfNat lit) : Œ±)` where `lit` is a nonzero raw natural number literal

(That is, we only actually use this function for negative integers.) This representation is used by
tactics like `ring` to decrease the number of typeclass arguments required in each use of a number
literal at type `Œ±`.
-/
@[simp] def _root_.Int.rawCast [Ring Œ±] (n : ‚Ñ§) : Œ± := n

theorem IsInt.to_isNat {Œ±} [Ring Œ±] : ‚àÄ {a : Œ±} {n}, IsInt a (.ofNat n) ‚Üí IsNat a n
  | _, _, ‚ü®rfl‚ü© => ‚ü®by simp‚ü©
                       -- üéâ no goals

theorem IsNat.to_isInt {Œ±} [Ring Œ±] : ‚àÄ {a : Œ±} {n}, IsNat a n ‚Üí IsInt a (.ofNat n)
  | _, _, ‚ü®rfl‚ü© => ‚ü®by simp‚ü©
                       -- üéâ no goals

theorem IsInt.to_raw_eq [Ring Œ±] : IsInt (a : Œ±) n ‚Üí a = n.rawCast
  | ‚ü®e‚ü© => e

theorem IsInt.of_raw (Œ±) [Ring Œ±] (n : ‚Ñ§) : IsInt (n.rawCast : Œ±) n := ‚ü®rfl‚ü©

theorem IsInt.neg_to_eq {Œ±} [Ring Œ±] {n} :
    {a a' : Œ±} ‚Üí IsInt a (.negOfNat n) ‚Üí n = a' ‚Üí a = -a'
  | _, _, ‚ü®rfl‚ü©, rfl => by simp [Int.negOfNat_eq, Int.cast_neg]
                           -- üéâ no goals

theorem IsInt.nonneg_to_eq {Œ±} [Ring Œ±] {n}
    {a a' : Œ±} (h : IsInt a (.ofNat n)) (e : n = a') : a = a' := h.to_isNat.to_eq e

/-- Represent an integer as a typed expression. -/
def mkRawIntLit (n : ‚Ñ§) : Q(‚Ñ§) :=
  let lit : Q(‚Ñï) := mkRawNatLit n.natAbs
  if 0 ‚â§ n then q(.ofNat $lit) else q(.negOfNat $lit)

/-- Extract the integer from a raw integer literal, as produced by
`Mathlib.Meta.NormNum.mkRawIntLit`. -/
def _root_.Lean.Expr.intLit! (e : Expr) : ‚Ñ§ :=
  if e.isAppOfArity ``Int.ofNat 1 then
    e.appArg!.natLit!
  else if e.isAppOfArity ``Int.negOfNat 1 then
    .negOfNat e.appArg!.natLit!
  else
    panic! "not a raw integer literal"

/-- Extract the raw natlit representing the absolute value of a raw integer literal
(of the type produced by `Mathlib.Meta.NormNum.mkRawIntLit`) along with an equality proof. -/
def rawIntLitNatAbs (n : Q(‚Ñ§)) : (m : Q(‚Ñï)) √ó Q(Int.natAbs $n = $m) :=
  if n.isAppOfArity ``Int.ofNat 1 then
    have m : Q(‚Ñï) := n.appArg!
    ‚ü®m, show Q(Int.natAbs (Int.ofNat $m) = $m) from q(Int.natAbs_ofNat $m)‚ü©
  else if n.isAppOfArity ``Int.negOfNat 1 then
    have m : Q(‚Ñï) := n.appArg!
    ‚ü®m, show Q(Int.natAbs (Int.negOfNat $m) = $m) from q(Int.natAbs_neg $m)‚ü©
  else
    panic! "not a raw integer literal"

/-- A shortcut (non)instance for `AddMonoidWithOne ‚Ñï` to shrink generated proofs. -/
def instAddMonoidWithOneNat : AddMonoidWithOne ‚Ñï := inferInstance

/-- A shortcut (non)instance for `Ring ‚Ñ§` to shrink generated proofs. -/
def instRingInt : Ring ‚Ñ§ := inferInstance

/--
Assert that an element of a ring is equal to `num / denom`
(and `denom` is invertible so that this makes sense).
We will usually also have `num` and `denom` coprime,
although this is not part of the definition.
-/
inductive IsRat [Ring Œ±] (a : Œ±) (num : ‚Ñ§) (denom : ‚Ñï) : Prop
  | mk (inv : Invertible (denom : Œ±)) (eq : a = num * ‚Öü(denom : Œ±))

/--
A "raw rat cast" is an expression of the form:

* `(Nat.rawCast lit : Œ±)` where `lit` is a raw natural number literal
* `(Int.rawCast (Int.negOfNat lit) : Œ±)` where `lit` is a nonzero raw natural number literal
* `(Rat.rawCast n d : Œ±)` where `n` is a raw int cast, `d` is a raw nat cast, and `d` is not 1 or 0.

This representation is used by tactics like `ring` to decrease the number of typeclass arguments
required in each use of a number literal at type `Œ±`.
-/
@[simp]
def _root_.Rat.rawCast [DivisionRing Œ±] (n : ‚Ñ§) (d : ‚Ñï) : Œ± := n / d

theorem IsRat.to_isNat {Œ±} [Ring Œ±] : ‚àÄ {a : Œ±} {n}, IsRat a (.ofNat n) (nat_lit 1) ‚Üí IsNat a n
  | _, _, ‚ü®inv, rfl‚ü© => have := @invertibleOne Œ± _; ‚ü®by simp‚ü©
                                                        -- üéâ no goals

theorem IsNat.to_isRat {Œ±} [Ring Œ±] : ‚àÄ {a : Œ±} {n}, IsNat a n ‚Üí IsRat a (.ofNat n) (nat_lit 1)
  | _, _, ‚ü®rfl‚ü© => ‚ü®‚ü®1, by simp, by simp‚ü©, by simp‚ü©
                           -- üéâ no goals
                                    -- üéâ no goals
                                              -- üéâ no goals

theorem IsRat.to_isInt {Œ±} [Ring Œ±] : ‚àÄ {a : Œ±} {n}, IsRat a n (nat_lit 1) ‚Üí IsInt a n
  | _, _, ‚ü®inv, rfl‚ü© => have := @invertibleOne Œ± _; ‚ü®by simp‚ü©
                                                        -- üéâ no goals

theorem IsInt.to_isRat {Œ±} [Ring Œ±] : ‚àÄ {a : Œ±} {n}, IsInt a n ‚Üí IsRat a n (nat_lit 1)
  | _, _, ‚ü®rfl‚ü© => ‚ü®‚ü®1, by simp, by simp‚ü©, by simp‚ü©
                           -- üéâ no goals
                                    -- üéâ no goals
                                              -- üéâ no goals

theorem IsRat.to_raw_eq [DivisionRing Œ±] : ‚àÄ {a}, IsRat (a : Œ±) n d ‚Üí a = Rat.rawCast n d
  | _, ‚ü®inv, rfl‚ü© => by simp [div_eq_mul_inv]
                        -- üéâ no goals

theorem IsRat.neg_to_eq {Œ±} [DivisionRing Œ±] {n d} :
    {a n' d' : Œ±} ‚Üí IsRat a (.negOfNat n) d ‚Üí n = n' ‚Üí d = d' ‚Üí a = -(n' / d')
  | _, _, _, ‚ü®_, rfl‚ü©, rfl, rfl => by simp [div_eq_mul_inv]
                                      -- üéâ no goals

theorem IsRat.nonneg_to_eq {Œ±} [DivisionRing Œ±] {n d} :
    {a n' d' : Œ±} ‚Üí IsRat a (.ofNat n) d ‚Üí n = n' ‚Üí d = d' ‚Üí a = n' / d'
  | _, _, _, ‚ü®_, rfl‚ü©, rfl, rfl => by simp [div_eq_mul_inv]
                                      -- üéâ no goals

theorem IsRat.of_raw (Œ±) [DivisionRing Œ±] (n : ‚Ñ§) (d : ‚Ñï)
    (h : (d : Œ±) ‚â† 0) : IsRat (Rat.rawCast n d : Œ±) n d :=
  have := invertibleOfNonzero h
  ‚ü®this, by simp [div_eq_mul_inv]‚ü©
            -- üéâ no goals

theorem IsRat.den_nz {Œ±} [DivisionRing Œ±] {a n d} : IsRat (a : Œ±) n d ‚Üí (d : Œ±) ‚â† 0
  | ‚ü®_, _‚ü© => nonzero_of_invertible (d : Œ±)

/-- Represent an integer as a typed expression. -/
def mkRawRatLit (q : ‚Ñö) : Q(‚Ñö) :=
  let nlit : Q(‚Ñ§) := mkRawIntLit q.num
  let dlit : Q(‚Ñï) := mkRawNatLit q.den
  q(mkRat $nlit $dlit)

/-- A shortcut (non)instance for `Ring ‚Ñö` to shrink generated proofs. -/
def instRingRat : Ring ‚Ñö := inferInstance

/-- A shortcut (non)instance for `DivisionRing ‚Ñö` to shrink generated proofs. -/
def instDivisionRingRat : DivisionRing ‚Ñö := inferInstance

/-- The result of `norm_num` running on an expression `x` of type `Œ±`.
Untyped version of `Result`. -/
inductive Result' where
  /-- Untyped version of `Result.isBool`. -/
  | isBool (val : Bool) (proof : Expr)
  /-- Untyped version of `Result.isNat`. -/
  | isNat (inst lit proof : Expr)
  /-- Untyped version of `Result.isNegNat`. -/
  | isNegNat (inst lit proof : Expr)
  /-- Untyped version of `Result.isRat`. -/
  | isRat (inst : Expr) (q : Rat) (n d proof : Expr)
  deriving Inhabited

section
set_option linter.unusedVariables false

/-- The result of `norm_num` running on an expression `x` of type `Œ±`. -/
@[nolint unusedArguments] def Result {Œ± : Q(Type u)} (x : Q($Œ±)) := Result'

instance : Inhabited (Result x) := inferInstanceAs (Inhabited Result')

/-- The result is `proof : x`, where `x` is a (true) proposition. -/
@[match_pattern, inline] def Result.isTrue {x : Q(Prop)} :
    ‚àÄ (proof : Q($x)), @Result _ (q(Prop) : Q(Type)) x := Result'.isBool true

/-- The result is `proof : ¬¨x`, where `x` is a (false) proposition. -/
@[match_pattern, inline] def Result.isFalse {x : Q(Prop)} :
    ‚àÄ (proof : Q(¬¨$x)), @Result _ (q(Prop) : Q(Type)) x := Result'.isBool false

/-- The result is `lit : ‚Ñï` (a raw nat literal) and `proof : isNat x lit`. -/
@[match_pattern, inline] def Result.isNat {Œ± : Q(Type u)} {x : Q($Œ±)} :
    ‚àÄ (inst : Q(AddMonoidWithOne $Œ±) := by assumption) (lit : Q(‚Ñï)) (proof : Q(IsNat $x $lit)),
      Result x := Result'.isNat

/-- The result is `-lit` where `lit` is a raw nat literal
and `proof : isInt x (.negOfNat lit)`. -/
@[match_pattern, inline] def Result.isNegNat {Œ± : Q(Type u)} {x : Q($Œ±)} :
    ‚àÄ (inst : Q(Ring $Œ±) := by assumption) (lit : Q(‚Ñï)) (proof : Q(IsInt $x (.negOfNat $lit))),
      Result x := Result'.isNegNat

/-- The result is `proof : isRat x n d`, where `n` is either `.ofNat lit` or `.negOfNat lit`
with `lit` a raw nat literal and `d` is a raw nat literal (not 0 or 1),
and `q` is the value of `n / d`. -/
@[match_pattern, inline] def Result.isRat {Œ± : Q(Type u)} {x : Q($Œ±)} :
    ‚àÄ (inst : Q(DivisionRing $Œ±) := by assumption) (q : Rat) (n : Q(‚Ñ§)) (d : Q(‚Ñï))
      (proof : Q(IsRat $x $n $d)), Result x := Result'.isRat

/-- A shortcut (non)instance for `AddMonoidWithOne Œ±` from `Ring Œ±` to shrink generated proofs. -/
def instAddMonoidWithOne [Ring Œ±] : AddMonoidWithOne Œ± := inferInstance

/-- A shortcut (non)instance for `AddMonoidWithOne Œ±` from `DivisionRing Œ±` to shrink generated
proofs. -/
def instAddMonoidWithOne' [DivisionRing Œ±] : AddMonoidWithOne Œ± := inferInstance

/-- A shortcut (non)instance for `Ring Œ±` from `DivisionRing Œ±` to shrink generated proofs. -/
def instRing [DivisionRing Œ±] : Ring Œ± := inferInstance

/-- The result is `z : ‚Ñ§` and `proof : isNat x z`. -/
-- Note the independent arguments `z : Q(‚Ñ§)` and `n : ‚Ñ§`.
-- We ensure these are "the same" when calling.
def Result.isInt {Œ± : Q(Type u)} {x : Q($Œ±)} (inst : Q(Ring $Œ±) := by assumption)
    (z : Q(‚Ñ§)) (n : ‚Ñ§) (proof : Q(IsInt $x $z)) : Result x :=
  have lit : Q(‚Ñï) := z.appArg!
  if 0 ‚â§ n then
    let proof : Q(IsInt $x (.ofNat $lit)) := proof
    .isNat q(instAddMonoidWithOne) lit q(IsInt.to_isNat $proof)
  else
    .isNegNat inst lit proof

/-- Returns the rational number that is the result of `norm_num` evaluation. -/
def Result.toRat : Result e ‚Üí Option Rat
  | .isBool .. => none
  | .isNat _ lit _ => some lit.natLit!
  | .isNegNat _ lit _ => some (-lit.natLit!)
  | .isRat _ q .. => some q

end

/-- Convert `undef` to `none` to make an `LOption` into an `Option`. -/
def _root_.Lean.LOption.toOption {Œ±} : Lean.LOption Œ± ‚Üí Option Œ±
  | .some a => some a
  | _ => none

/-- Helper function to synthesize a typed `AddMonoidWithOne Œ±` expression. -/
def inferAddMonoidWithOne (Œ± : Q(Type u)) : MetaM Q(AddMonoidWithOne $Œ±) :=
  return ‚Üê synthInstanceQ (q(AddMonoidWithOne $Œ±) : Q(Type u)) <|>
    throwError "not an AddMonoidWithOne"

/-- Helper function to synthesize a typed `Semiring Œ±` expression. -/
def inferSemiring (Œ± : Q(Type u)) : MetaM Q(Semiring $Œ±) :=
  return ‚Üê synthInstanceQ (q(Semiring $Œ±) : Q(Type u)) <|> throwError "not a semiring"

/-- Helper function to synthesize a typed `Ring Œ±` expression. -/
def inferRing (Œ± : Q(Type u)) : MetaM Q(Ring $Œ±) :=
  return ‚Üê synthInstanceQ (q(Ring $Œ±) : Q(Type u)) <|> throwError "not a ring"

/-- Helper function to synthesize a typed `DivisionRing Œ±` expression. -/
def inferDivisionRing (Œ± : Q(Type u)) : MetaM Q(DivisionRing $Œ±) :=
  return ‚Üê synthInstanceQ (q(DivisionRing $Œ±) : Q(Type u)) <|> throwError "not a division ring"

/-- Helper function to synthesize a typed `OrderedSemiring Œ±` expression. -/
def inferOrderedSemiring (Œ± : Q(Type u)) : MetaM Q(OrderedSemiring $Œ±) :=
  return ‚Üê synthInstanceQ (q(OrderedSemiring $Œ±) : Q(Type u)) <|>
    throwError "not an ordered semiring"

/-- Helper function to synthesize a typed `OrderedRing Œ±` expression. -/
def inferOrderedRing (Œ± : Q(Type u)) : MetaM Q(OrderedRing $Œ±) :=
  return ‚Üê synthInstanceQ (q(OrderedRing $Œ±) : Q(Type u)) <|> throwError "not an ordered ring"

/-- Helper function to synthesize a typed `LinearOrderedField Œ±` expression. -/
def inferLinearOrderedField (Œ± : Q(Type u)) : MetaM Q(LinearOrderedField $Œ±) :=
  return ‚Üê synthInstanceQ (q(LinearOrderedField $Œ±) : Q(Type u)) <|>
    throwError "not a linear ordered field"

/-- Helper function to synthesize a typed `CharZero Œ±` expression given `Ring Œ±`. -/
def inferCharZeroOfRing {Œ± : Q(Type u)} (_i : Q(Ring $Œ±) := by with_reducible assumption) :
    MetaM Q(CharZero $Œ±) :=
  return ‚Üê synthInstanceQ (q(CharZero $Œ±) : Q(Prop)) <|>
    throwError "not a characteristic zero ring"

/-- Helper function to synthesize a typed `CharZero Œ±` expression given `Ring Œ±`, if it exists. -/
def inferCharZeroOfRing? {Œ± : Q(Type u)} (_i : Q(Ring $Œ±) := by with_reducible assumption) :
    MetaM (Option Q(CharZero $Œ±)) :=
  return (‚Üê trySynthInstanceQ (q(CharZero $Œ±) : Q(Prop))).toOption

/-- Helper function to synthesize a typed `CharZero Œ±` expression given `AddMonoidWithOne Œ±`. -/
def inferCharZeroOfAddMonoidWithOne {Œ± : Q(Type u)}
    (_i : Q(AddMonoidWithOne $Œ±) := by with_reducible assumption) : MetaM Q(CharZero $Œ±) :=
  return ‚Üê synthInstanceQ (q(CharZero $Œ±) : Q(Prop)) <|>
    throwError "not a characteristic zero AddMonoidWithOne"

/-- Helper function to synthesize a typed `CharZero Œ±` expression given `AddMonoidWithOne Œ±`, if it
exists. -/
def inferCharZeroOfAddMonoidWithOne? {Œ± : Q(Type u)}
    (_i : Q(AddMonoidWithOne $Œ±) := by with_reducible assumption) :
      MetaM (Option Q(CharZero $Œ±)) :=
  return (‚Üê trySynthInstanceQ (q(CharZero $Œ±) : Q(Prop))).toOption

/-- Helper function to synthesize a typed `CharZero Œ±` expression given `DivisionRing Œ±`. -/
def inferCharZeroOfDivisionRing {Œ± : Q(Type u)}
    (_i : Q(DivisionRing $Œ±) := by with_reducible assumption) : MetaM Q(CharZero $Œ±) :=
  return ‚Üê synthInstanceQ (q(CharZero $Œ±) : Q(Prop)) <|>
    throwError "not a characteristic zero division ring"

/-- Helper function to synthesize a typed `OfScientific Œ±` expression given `DivisionRing Œ±`. -/
def inferOfScientific (Œ± : Q(Type u)) : MetaM Q(OfScientific $Œ±) :=
  return ‚Üê synthInstanceQ (q(OfScientific $Œ±) : Q(Type u)) <|>
    throwError "does not support scientific notation"

/-- Helper function to synthesize a typed `CharZero Œ±` expression given `DivisionRing Œ±`, if it
exists. -/
def inferCharZeroOfDivisionRing? {Œ± : Q(Type u)}
    (_i : Q(DivisionRing $Œ±) := by with_reducible assumption) : MetaM (Option Q(CharZero $Œ±)) :=
  return (‚Üê trySynthInstanceQ (q(CharZero $Œ±) : Q(Prop))).toOption

/-- Helper function to synthesize a typed `RatCast Œ±` expression. -/
def inferRatCast (Œ± : Q(Type u)) : MetaM Q(RatCast $Œ±) :=
  return ‚Üê synthInstanceQ (q(RatCast $Œ±) : Q(Type u)) <|> throwError "does not support a rat cast"

/--
Extract from a `Result` the integer value (as both a term and an expression),
and the proof that the original expression is equal to this integer.
-/
def Result.toInt {Œ± : Q(Type u)} {e : Q($Œ±)} (_i : Q(Ring $Œ±) := by with_reducible assumption) :
    Result e ‚Üí Option (‚Ñ§ √ó (lit : Q(‚Ñ§)) √ó Q(IsInt $e $lit))
  | .isNat _ lit proof => do
    have proof : Q(@IsNat _ instAddMonoidWithOne $e $lit) := proof
    pure ‚ü®lit.natLit!, q(.ofNat $lit), q(($proof).to_isInt)‚ü©
  | .isNegNat _ lit proof => pure ‚ü®-lit.natLit!, q(.negOfNat $lit), proof‚ü©
  | _ => failure

/--
Extract from a `Result` the rational value (as both a term and an expression),
and the proof that the original expression is equal to this rational number.
-/
def Result.toRat' {Œ± : Q(Type u)} {e : Q($Œ±)}
    (_i : Q(DivisionRing $Œ±) := by with_reducible assumption) :
    Result e ‚Üí Option (‚Ñö √ó (n : Q(‚Ñ§)) √ó (d : Q(‚Ñï)) √ó Q(IsRat $e $n $d))
  | .isBool .. => none
  | .isNat _ lit proof =>
    have proof : Q(@IsNat _ instAddMonoidWithOne $e $lit) := proof
    some ‚ü®lit.natLit!, q(.ofNat $lit), q(nat_lit 1), q(($proof).to_isRat)‚ü©
  | .isNegNat _ lit proof =>
    have proof : Q(@IsInt _ DivisionRing.toRing $e (.negOfNat $lit)) := proof
    some ‚ü®-lit.natLit!, q(.negOfNat $lit), q(nat_lit 1),
      (q(@IsInt.to_isRat _ DivisionRing.toRing _ _ $proof) : Expr)‚ü©
  | .isRat _ q n d proof => some ‚ü®q, n, d, proof‚ü©

instance : ToMessageData (Result x) where
  toMessageData
  | .isBool true proof => m!"isTrue ({proof})"
  | .isBool false proof => m!"isFalse ({proof})"
  | .isNat _ lit proof => m!"isNat {lit} ({proof})"
  | .isNegNat _ lit proof => m!"isNegNat {lit} ({proof})"
  | .isRat _ q _ _ proof => m!"isRat {q} ({proof})"

/--
Given a `NormNum.Result e` (which uses `IsNat`, `IsInt`, `IsRat` to express equality to a rational
numeral), converts it to an equality `e = Nat.rawCast n`, `e = Int.rawCast n`, or
`e = Rat.rawCast n d` to a raw cast expression, so it can be used for rewriting.
-/
def Result.toRawEq {Œ± : Q(Type u)} {e : Q($Œ±)} : Result e ‚Üí (e' : Q($Œ±)) √ó Q($e = $e')
  | .isBool false p =>
    have e : Q(Prop) := e; have p : Q(¬¨$e) := p
    ‚ü®(q(False) : Expr), (q(eq_false $p) : Expr)‚ü©
  | .isBool true p =>
    have e : Q(Prop) := e; have p : Q($e) := p
    ‚ü®(q(True) : Expr), (q(eq_true $p) : Expr)‚ü©
  | .isNat _ lit p => ‚ü®q(Nat.rawCast $lit), q(IsNat.to_raw_eq $p)‚ü©
  | .isNegNat _ lit p => ‚ü®q(Int.rawCast (.negOfNat $lit)), q(IsInt.to_raw_eq $p)‚ü©
  | .isRat _ _ n d p => ‚ü®q(Rat.rawCast $n $d), q(IsRat.to_raw_eq $p)‚ü©

/--
`Result.toRawEq` but providing an integer. Given a `NormNum.Result e` for something known to be an
integer (which uses `IsNat` or `IsInt` to express equality to an integer numeral), converts it to
an equality `e = Nat.rawCast n` or `e = Int.rawCast n` to a raw cast expression, so it can be used
for rewriting. Gives `none` if not an integer.
-/
def Result.toRawIntEq {Œ± : Q(Type u)} {e : Q($Œ±)} : Result e ‚Üí
    Option (‚Ñ§ √ó (e' : Q($Œ±)) √ó Q($e = $e'))
  | .isNat _ lit p => some ‚ü®lit.natLit!, q(Nat.rawCast $lit), q(IsNat.to_raw_eq $p)‚ü©
  | .isNegNat _ lit p => some ‚ü®-lit.natLit!, q(Int.rawCast (.negOfNat $lit)), q(IsInt.to_raw_eq $p)‚ü©
  | .isRat _ .. | .isBool .. => none

/-- Constructs a `Result` out of a raw nat cast. Assumes `e` is a raw nat cast expression. -/
def Result.ofRawNat {Œ± : Q(Type u)} (e : Q($Œ±)) : Result e := Id.run do
  let .app (.app _ (sŒ± : Q(AddMonoidWithOne $Œ±))) (lit : Q(‚Ñï)) := e | panic! "not a raw nat cast"
  .isNat sŒ± lit (q(IsNat.of_raw $Œ± $lit) : Expr)

/-- Constructs a `Result` out of a raw int cast.
Assumes `e` is a raw int cast expression denoting `n`. -/
def Result.ofRawInt {Œ± : Q(Type u)} (n : ‚Ñ§) (e : Q($Œ±)) : Result e :=
  if 0 ‚â§ n then
    Result.ofRawNat e
  else Id.run do
    let .app (.app _ (rŒ± : Q(Ring $Œ±))) (.app _ (lit : Q(‚Ñï))) := e | panic! "not a raw int cast"
    .isNegNat rŒ± lit (q(IsInt.of_raw $Œ± (.negOfNat $lit)) : Expr)

/-- Constructs a `Result` out of a raw rat cast.
Assumes `e` is a raw rat cast expression denoting `n`. -/
def Result.ofRawRat {Œ± : Q(Type u)} (q : ‚Ñö) (e : Q($Œ±)) (hyp : Option Expr := none) : Result e :=
  if q.den = 1 then
    Result.ofRawInt q.num e
  else Id.run do
    let .app (.app (.app _ (dŒ± : Q(DivisionRing $Œ±))) (n : Q(‚Ñ§))) (d : Q(‚Ñï)) := e
      | panic! "not a raw rat cast"
    let hyp : Q(($d : $Œ±) ‚â† 0) := hyp.get!
    .isRat dŒ± q n d (q(IsRat.of_raw $Œ± $n $d $hyp) : Expr)

/-- The result depends on whether `q : ‚Ñö` happens to be an integer, in which case the result is
`.isInt ..` whereas otherwise it's `.isRat ..`. -/
def Result.isRat' {Œ± : Q(Type u)} {x : Q($Œ±)} (inst : Q(DivisionRing $Œ±) := by assumption)
    (q : Rat) (n : Q(‚Ñ§)) (d : Q(‚Ñï)) (proof : Q(IsRat $x $n $d)) : Result x :=
  if q.den = 1 then
    have proof : Q(IsRat $x $n (nat_lit 1)) := proof
    .isInt q(DivisionRing.toRing) n q.num q(IsRat.to_isInt $proof)
  else
    .isRat inst q n d proof

/-- Returns the rational number that is the result of `norm_num` evaluation, along with a proof
that the denominator is nonzero in the `isRat` case. -/
def Result.toRatNZ : Result e ‚Üí Option (Rat √ó Option Expr)
  | .isBool .. => none
  | .isNat _ lit _ => some (lit.natLit!, none)
  | .isNegNat _ lit _ => some (-lit.natLit!, none)
  | .isRat _ q _ _ p => some (q, q(IsRat.den_nz $p))

/--
Constructs an `ofNat` application `a'` with the canonical instance, together with a proof that
the instance is equal to the result of `Nat.cast` on the given `AddMonoidWithOne` instance.

This function is performance-critical, as many higher level tactics have to construct numerals.
So rather than using typeclass search we hardcode the (relatively small) set of solutions
to the typeclass problem.
-/
def mkOfNat (Œ± : Q(Type u)) (_sŒ± : Q(AddMonoidWithOne $Œ±)) (lit : Q(‚Ñï)) :
    MetaM ((a' : Q($Œ±)) √ó Q($lit = $a')) := do
  if Œ±.isConstOf ``Nat then
    let a' : Q(‚Ñï) := q(OfNat.ofNat $lit : ‚Ñï)
    pure ‚ü®a', (q(Eq.refl $a') : Expr)‚ü©
  else if Œ±.isConstOf ``Int then
    let a' : Q(‚Ñ§) := q(OfNat.ofNat $lit : ‚Ñ§)
    pure ‚ü®a', (q(Eq.refl $a') : Expr)‚ü©
  else if Œ±.isConstOf ``Rat then
    let a' : Q(‚Ñö) := q(OfNat.ofNat $lit : ‚Ñö)
    pure ‚ü®a', (q(Eq.refl $a') : Expr)‚ü©
  else
    let some n := lit.natLit? | failure
    match n with
    | 0 => pure ‚ü®q(0 : $Œ±), (q(Nat.cast_zero (R := $Œ±)) : Expr)‚ü©
    | 1 => pure ‚ü®q(1 : $Œ±), (q(Nat.cast_one (R := $Œ±)) : Expr)‚ü©
    | k+2 =>
      let k : Q(‚Ñï) := mkRawNatLit k
      let _x : Q(Nat.AtLeastTwo $lit) :=
        (q(instNatAtLeastTwo (n := $k)) : Expr)
      let a' : Q($Œ±) := q(OfNat.ofNat $lit)
      pure ‚ü®a', (q(Eq.refl $a') : Expr)‚ü©

/-- Convert a `Result` to a `Simp.Result`. -/
def Result.toSimpResult {Œ± : Q(Type u)} {e : Q($Œ±)} : Result e ‚Üí MetaM Simp.Result
  | r@(.isBool ..) => let ‚ü®expr, proof?‚ü© := r.toRawEq; pure { expr, proof? }
  | .isNat sŒ± lit p => do
    let ‚ü®a', pa'‚ü© ‚Üê mkOfNat Œ± sŒ± lit
    return { expr := a', proof? := q(IsNat.to_eq $p $pa') }
  | .isNegNat _rŒ± lit p => do
    let ‚ü®a', pa'‚ü© ‚Üê mkOfNat Œ± q(AddCommMonoidWithOne.toAddMonoidWithOne) lit
    return { expr := q(-$a'), proof? := q(IsInt.neg_to_eq $p $pa') }
  | .isRat _ q n d p => do
    have lit : Q(‚Ñï) := n.appArg!
    if q < 0 then
      let p : Q(IsRat $e (.negOfNat $lit) $d) := p
      let ‚ü®n', pn'‚ü© ‚Üê mkOfNat Œ± q(AddCommMonoidWithOne.toAddMonoidWithOne) lit
      let ‚ü®d', pd'‚ü© ‚Üê mkOfNat Œ± q(AddCommMonoidWithOne.toAddMonoidWithOne) d
      return { expr := q(-($n' / $d')), proof? := q(IsRat.neg_to_eq $p $pn' $pd') }
    else
      let p : Q(IsRat $e (.ofNat $lit) $d) := p
      let ‚ü®n', pn'‚ü© ‚Üê mkOfNat Œ± q(AddCommMonoidWithOne.toAddMonoidWithOne) lit
      let ‚ü®d', pd'‚ü© ‚Üê mkOfNat Œ± q(AddCommMonoidWithOne.toAddMonoidWithOne) d
      return { expr := q($n' / $d'), proof? := q(IsRat.nonneg_to_eq $p $pn' $pd') }

/--
An extension for `norm_num`.
-/
structure NormNumExt where
  /-- The extension should be run in the `pre` phase when used as simp plugin. -/
  pre := true
  /-- The extension should be run in the `post` phase when used as simp plugin. -/
  post := true
  /-- Attempts to prove an expression is equal to some explicit number of the relevant type. -/
  eval {Œ± : Q(Type u)} (e : Q($Œ±)) : MetaM (Result e)
  /-- The name of the `norm_num` extension. -/
  name : Name := by exact decl_name%

/-- Read a `norm_num` extension from a declaration of the right type. -/
def mkNormNumExt (n : Name) : ImportM NormNumExt := do
  let { env, opts, .. } ‚Üê read
  IO.ofExcept <| unsafe env.evalConstCheck NormNumExt opts ``NormNumExt n

/-- Each `norm_num` extension is labelled with a collection of patterns
which determine the expressions to which it should be applied. -/
abbrev Entry := Array (Array (DiscrTree.Key true)) √ó Name

/-- The state of the `norm_num` extension environment -/
structure NormNums where
  /-- The tree of `norm_num` extensions. -/
  tree   : DiscrTree NormNumExt true := {}
  /-- Erased `norm_num`s. -/
  erased  : PHashSet Name := {}
  deriving Inhabited

/-- Environment extensions for `norm_num` declarations -/
initialize normNumExt : ScopedEnvExtension Entry (Entry √ó NormNumExt) NormNums ‚Üê
  -- we only need this to deduplicate entries in the DiscrTree
  have : BEq NormNumExt := ‚ü®fun _ _ ‚Ü¶ false‚ü©
  /- Insert `v : NormNumExt` into the tree `dt` on all key sequences given in `kss`. -/
  let insert kss v dt := kss.foldl (fun dt ks ‚Ü¶ dt.insertCore ks v) dt
  registerScopedEnvExtension {
    mkInitial := pure {}
    ofOLeanEntry := fun _ e@(_, n) ‚Ü¶ return (e, ‚Üê mkNormNumExt n)
    toOLeanEntry := (¬∑.1)
    addEntry := fun { tree, erased } ((kss, n), ext) ‚Ü¶
      { tree := insert kss ext tree, erased := erased.erase n }
  }

/-- Run each registered `norm_num` extension on an expression, returning a `NormNum.Result`. -/
def derive {Œ± : Q(Type u)} (e : Q($Œ±)) (post := false) : MetaM (Result e) := do
  if e.isNatLit then
    let lit : Q(‚Ñï) := e
    return .isNat (q(instAddMonoidWithOneNat) : Q(AddMonoidWithOne ‚Ñï))
      lit (q(IsNat.raw_refl $lit) : Expr)
  profileitM Exception "norm_num" (‚Üê getOptions) do
    let s ‚Üê saveState
    let normNums := normNumExt.getState (‚Üê getEnv)
    let arr ‚Üê normNums.tree.getMatch e
    for ext in arr do
      if (bif post then ext.post else ext.pre) && ! normNums.erased.contains ext.name then
        try
          let new ‚Üê withReducibleAndInstances <| ext.eval e
          trace[Tactic.norm_num] "{ext.name}:\n{e} ==> {new}"
          return new
        catch err =>
          trace[Tactic.norm_num] "{e} failed: {err.toMessageData}"
          s.restore
    throwError "{e}: no norm_nums apply"

/-- Run each registered `norm_num` extension on a typed expression `e : Œ±`,
returning a typed expression `lit : ‚Ñï`, and a proof of `isNat e lit`. -/
def deriveNat' {Œ± : Q(Type u)} (e : Q($Œ±)) :
    MetaM ((_inst : Q(AddMonoidWithOne $Œ±)) √ó (lit : Q(‚Ñï)) √ó Q(IsNat $e $lit)) := do
  let .isNat inst lit proof ‚Üê derive e | failure
  pure ‚ü®inst, lit, proof‚ü©

/-- Run each registered `norm_num` extension on a typed expression `e : Œ±`,
returning a typed expression `lit : ‚Ñï`, and a proof of `isNat e lit`. -/
def deriveNat {Œ± : Q(Type u)} (e : Q($Œ±))
    (_inst : Q(AddMonoidWithOne $Œ±) := by with_reducible assumption) :
    MetaM ((lit : Q(‚Ñï)) √ó Q(IsNat $e $lit)) := do
  let .isNat _ lit proof ‚Üê derive e | failure
  pure ‚ü®lit, proof‚ü©

/-- Run each registered `norm_num` extension on a typed expression `e : Œ±`,
returning a typed expression `lit : ‚Ñ§`, and a proof of `IsInt e lit` in expression form. -/
def deriveInt {Œ± : Q(Type u)} (e : Q($Œ±))
    (_inst : Q(Ring $Œ±) := by with_reducible assumption) :
    MetaM ((lit : Q(‚Ñ§)) √ó Q(IsInt $e $lit)) := do
  let some ‚ü®_, lit, proof‚ü© := (‚Üê derive e).toInt | failure
  pure ‚ü®lit, proof‚ü©

/-- Run each registered `norm_num` extension on a typed expression `e : Œ±`,
returning a rational number, typed expressions `n : ‚Ñö` and `d : ‚Ñö` for the numerator and
denominator, and a proof of `IsRat e n d` in expression form. -/
def deriveRat {Œ± : Q(Type u)} (e : Q($Œ±))
    (_inst : Q(DivisionRing $Œ±) := by with_reducible assumption) :
    MetaM (‚Ñö √ó (n : Q(‚Ñ§)) √ó (d : Q(‚Ñï)) √ó Q(IsRat $e $n $d)) := do
  let some res := (‚Üê derive e).toRat' | failure
  pure res

/-- Extract the natural number `n` if the expression is of the form `OfNat.ofNat n`. -/
def isNatLit (e : Expr) : Option ‚Ñï := do
  guard <| e.isAppOfArity ``OfNat.ofNat 3
  let .lit (.natVal lit) := e.appFn!.appArg! | none
  lit

/-- Extract the integer `i` if the expression is either a natural number literal
or the negation of one. -/
def isIntLit (e : Expr) : Option ‚Ñ§ :=
  if e.isAppOfArity ``Neg.neg 3 then
    (- ¬∑) <$> isNatLit e.appArg!
  else
    isNatLit e

/-- Extract the numerator `n : ‚Ñ§` and denominator `d : ‚Ñï` if the expression is either
an integer literal, or the division of one integer literal by another. -/
def isRatLit (e : Expr) : Option ‚Ñö := do
  if e.isAppOfArity ``Div.div 4 then
    let d ‚Üê isNatLit e.appArg!
    guard (d ‚â† 1)
    let n ‚Üê isIntLit e.appFn!.appArg!
    let q := mkRat n d
    guard (q.den = d)
    pure q
  else
    isIntLit e

/-- Given `Mathlib.Meta.NormNum.Result.isBool p b`, this is the type of `p`.
  Note that `BoolResult p b` is definitionally equal to `Expr`, and if you write `match b with ...`,
  then in the `true` branch `BoolResult p true` is reducibly equal to `Q($p)` and
  in the `false` branch it is reducibly equal to `Q(¬¨ $p)`. -/
@[reducible]
def BoolResult (p : Q(Prop)) (b : Bool) : Type :=
  Q(Bool.rec (¬¨ $p) ($p) $b)

/-- Run each registered `norm_num` extension on a typed expression `p : Prop`,
and returning the truth or falsity of `p' : Prop` from an equivalence `p ‚Üî p'`. -/
def deriveBool (p : Q(Prop)) : MetaM ((b : Bool) √ó BoolResult p b) := do
  let .isBool b prf ‚Üê derive (Œ± := (q(Prop) : Q(Type))) p | failure
  pure ‚ü®b, prf‚ü©

/-- Obtain a `Result` from a `BoolResult`. -/
def Result.ofBoolResult {p : Q(Prop)} {b : Bool} (prf : BoolResult p b) :
  Result q(Prop) :=
  Result'.isBool b prf

/-- Run each registered `norm_num` extension on a typed expression `p : Prop`,
and returning the truth or falsity of `p' : Prop` from an equivalence `p ‚Üî p'`. -/
def deriveBoolOfIff (p p' : Q(Prop)) (hp : Q($p ‚Üî $p')) :
    MetaM ((b : Bool) √ó BoolResult p' b) := do
  let ‚ü®b, pb‚ü© ‚Üê deriveBool p
  match b with
  | true  => return ‚ü®true, q(Iff.mp $hp $pb)‚ü©
  | false => return ‚ü®false, q((Iff.not $hp).mp $pb)‚ü©

/-- Test if an expression represents an explicit number written in normal form. -/
def isNormalForm : Expr ‚Üí Bool
  | .lit _ => true
  | .mdata _ e => isNormalForm e
  | e => (isRatLit e).isSome

/-- Run each registered `norm_num` extension on an expression,
returning a `Simp.Result`. -/
def eval (e : Expr) (post := false) : MetaM Simp.Result := do
  if isNormalForm e then return { expr := e }
  let ‚ü®_, _, e‚ü© ‚Üê inferTypeQ' e
  (‚Üê derive e post).toSimpResult

/-- Erases a name marked `norm_num` by adding it to the state's `erased` field and
  removing it from the state's list of `Entry`s. -/
def NormNums.eraseCore (d : NormNums) (declName : Name) : NormNums :=
 { d with erased := d.erased.insert declName }

/--
  Erase a name marked as a `norm_num` attribute.

  Check that it does in fact have the `norm_num` attribute by making sure it names a `NormNumExt`
  found somewhere in the state's tree, and is not erased.
-/
def NormNums.erase [Monad m] [MonadError m] (d : NormNums) (declName : Name) : m NormNums := do
  unless d.tree.values.any (¬∑.name == declName) && ! d.erased.contains declName
  do
    throwError "'{declName}' does not have [norm_num] attribute"
  return d.eraseCore declName

initialize registerBuiltinAttribute {
  name := `norm_num
  descr := "adds a norm_num extension"
  applicationTime := .afterCompilation
  add := fun declName stx kind ‚Ü¶ match stx with
    | `(attr| norm_num $es,*) => do
      let env ‚Üê getEnv
      unless (env.getModuleIdxFor? declName).isNone do
        throwError "invalid attribute 'norm_num', declaration is in an imported module"
      if (IR.getSorryDep env declName).isSome then return -- ignore in progress definitions
      let ext ‚Üê mkNormNumExt declName
      let keys ‚Üê MetaM.run' <| es.getElems.mapM fun stx ‚Ü¶ do
        let e ‚Üê TermElabM.run' <| withSaveInfoContext <| withAutoBoundImplicit <|
          withReader ({ ¬∑ with ignoreTCFailures := true }) do
            let e ‚Üê elabTerm stx none
            let (_, _, e) ‚Üê lambdaMetaTelescope (‚Üê mkLambdaFVars (‚Üê getLCtx).getFVars e)
            return e
        DiscrTree.mkPath e
      normNumExt.add ((keys, declName), ext) kind
    | _ => throwUnsupportedSyntax
  erase := fun declName => do
    let s := normNumExt.getState (‚Üê getEnv)
    let s ‚Üê s.erase declName
    modifyEnv fun env => normNumExt.modifyState env fun _ => s
}

/-- A simp plugin which calls `NormNum.eval`. -/
def tryNormNum? (post := false) (e : Expr) : SimpM (Option Simp.Step) := do
  try return some (.done (‚Üê eval e post))
  catch _ => return none

/--
Constructs a proof that the original expression is true
given a simp result which simplifies the target to `True`.
-/
def _root_.Lean.Meta.Simp.Result.ofTrue (r : Simp.Result) : MetaM (Option Expr) :=
  if r.expr.isConstOf ``True then
    some <$> match r.proof? with
    | some proof => mkOfEqTrue proof
    | none => pure (mkConst ``True.intro)
  else
    pure none

variable (ctx : Simp.Context) (useSimp := true) in
mutual
  /-- A discharger which calls `norm_num`. -/
  partial def discharge (e : Expr) : SimpM (Option Expr) := do (‚Üê deriveSimp e).ofTrue

  /-- A `Methods` implementation which calls `norm_num`. -/
  partial def methods : Simp.Methods :=
    if useSimp then {
      pre := fun e ‚Ü¶ do
        Simp.andThen (‚Üê Simp.preDefault e discharge) tryNormNum?
      post := fun e ‚Ü¶ do
        Simp.andThen (‚Üê Simp.postDefault e discharge) (tryNormNum? (post := true))
      discharge? := discharge
    } else {
      pre := fun e ‚Ü¶ Simp.andThen (.visit { expr := e }) tryNormNum?
      post := fun e ‚Ü¶ Simp.andThen (.visit { expr := e }) (tryNormNum? (post := true))
      discharge? := discharge
    }

  /-- Traverses the given expression using simp and normalises any numbers it finds. -/
  partial def deriveSimp (e : Expr) : MetaM Simp.Result :=
    (¬∑.1) <$> Simp.main e ctx (methods := methods)
end

-- FIXME: had to inline a bunch of stuff from `simpGoal` here
/--
The core of `norm_num` as a tactic in `MetaM`.

* `g`: The goal to simplify
* `ctx`: The simp context, constructed by `mkSimpContext` and
  containing any additional simp rules we want to use
* `fvarIdsToSimp`: The selected set of hypotheses used in the location argument
* `simplifyTarget`: true if the target is selected in the location argument
* `useSimp`: true if we used `norm_num` instead of `norm_num1`
-/
def normNumAt (g : MVarId) (ctx : Simp.Context) (fvarIdsToSimp : Array FVarId)
    (simplifyTarget := true) (useSimp := true) :
    MetaM (Option (Array FVarId √ó MVarId)) := g.withContext do
  g.checkNotAssigned `norm_num
  let mut g := g
  let mut toAssert := #[]
  let mut replaced := #[]
  for fvarId in fvarIdsToSimp do
    let localDecl ‚Üê fvarId.getDecl
    let type ‚Üê instantiateMVars localDecl.type
    let ctx := { ctx with simpTheorems := ctx.simpTheorems.eraseTheorem (.fvar localDecl.fvarId) }
    let r ‚Üê deriveSimp ctx useSimp type
    match r.proof? with
    | some _ =>
      let some (value, type) ‚Üê applySimpResultToProp g (mkFVar fvarId) type r
        | return none
      toAssert := toAssert.push { userName := localDecl.userName, type, value }
    | none =>
      if r.expr.isConstOf ``False then
        g.assign (‚Üê mkFalseElim (‚Üê g.getType) (mkFVar fvarId))
        return none
      g ‚Üê g.replaceLocalDeclDefEq fvarId r.expr
      replaced := replaced.push fvarId
  if simplifyTarget then
    let res ‚Üê g.withContext do
      let target ‚Üê instantiateMVars (‚Üê g.getType)
      let r ‚Üê deriveSimp ctx useSimp target
      let some proof ‚Üê r.ofTrue
        | some <$> applySimpResultToTarget g target r
      g.assign proof
      pure none
    let some gNew := res | return none
    g := gNew
  let (fvarIdsNew, gNew) ‚Üê g.assertHypotheses toAssert
  let toClear := fvarIdsToSimp.filter fun fvarId ‚Ü¶ !replaced.contains fvarId
  let gNew ‚Üê gNew.tryClearMany toClear
  return some (fvarIdsNew, gNew)

open Qq Lean Meta Elab Tactic Term

/-- Constructs a simp context from the simp argument syntax. -/
def getSimpContext (args : Syntax) (simpOnly := false) :
    TacticM Simp.Context := do
  let simpTheorems ‚Üê
    if simpOnly then simpOnlyBuiltins.foldlM (¬∑.addConst ¬∑) {} else getSimpTheorems
  let mut { ctx, starArg } ‚Üê elabSimpArgs args[0] (eraseLocal := false) (kind := .simp)
    { simpTheorems := #[simpTheorems], congrTheorems := ‚Üê getSimpCongrTheorems }
  unless starArg do return ctx
  let mut simpTheorems := ctx.simpTheorems
  for h in ‚Üê getPropHyps do
    unless simpTheorems.isErased (.fvar h) do
      simpTheorems ‚Üê simpTheorems.addTheorem (.fvar h) (‚Üê h.getDecl).toExpr
  pure { ctx with simpTheorems }

open Elab.Tactic in
/--
Elaborates a call to `norm_num only? [args]` or `norm_num1`.
* `args`: the `(simpArgs)?` syntax for simp arguments
* `loc`: the `(location)?` syntax for the optional location argument
* `simpOnly`: true if `only` was used in `norm_num`
* `useSimp`: false if `norm_num1` was used, in which case only the structural parts
  of `simp` will be used, not any of the post-processing that `simp only` does without lemmas
-/
-- FIXME: had to inline a bunch of stuff from `mkSimpContext` and `simpLocation` here
def elabNormNum (args : Syntax) (loc : Syntax)
    (simpOnly := false) (useSimp := true) : TacticM Unit := do
  let ctx ‚Üê getSimpContext args (!useSimp || simpOnly)
  let g ‚Üê getMainGoal
  let res ‚Üê match expandOptLocation loc with
  | .targets hyps simplifyTarget => normNumAt g ctx (‚Üê getFVarIds hyps) simplifyTarget useSimp
  | .wildcard => normNumAt g ctx (‚Üê g.getNondepPropHyps) (simplifyTarget := true) useSimp
  match res with
  | none => replaceMainGoal []
  | some (_, g) => replaceMainGoal [g]

end Meta.NormNum

namespace Tactic
open Lean.Parser.Tactic Meta.NormNum

/--
Normalize numerical expressions. Supports the operations `+` `-` `*` `/` `‚Åª¬π` `^` and `%`
over numerical types such as `‚Ñï`, `‚Ñ§`, `‚Ñö`, `‚Ñù`, `‚ÑÇ` and some general algebraic types,
and can prove goals of the form `A = B`, `A ‚â† B`, `A < B` and `A ‚â§ B`, where `A` and `B` are
numerical expressions. It also has a relatively simple primality prover.
-/
elab (name := normNum) "norm_num" only:&" only"? args:(simpArgs ?) loc:(location ?) : tactic =>
  elabNormNum args loc (simpOnly := only.isSome) (useSimp := true)

/-- Basic version of `norm_num` that does not call `simp`. -/
elab (name := normNum1) "norm_num1" loc:(location ?) : tactic =>
  elabNormNum mkNullNode loc (simpOnly := true) (useSimp := false)

open Lean Elab Tactic

@[inherit_doc normNum1] syntax (name := normNum1Conv) "norm_num1" : conv

/-- Elaborator for `norm_num1` conv tactic. -/
@[tactic normNum1Conv] def elabNormNum1Conv : Tactic := fun _ ‚Ü¶ withMainContext do
  let ctx ‚Üê getSimpContext mkNullNode true
  Conv.applySimpResult (‚Üê deriveSimp ctx (‚Üê instantiateMVars (‚Üê Conv.getLhs)) (useSimp := false))

@[inherit_doc normNum] syntax (name := normNumConv) "norm_num" &" only"? (simpArgs)? : conv

/-- Elaborator for `norm_num` conv tactic. -/
@[tactic normNumConv] def elabNormNumConv : Tactic := fun stx ‚Ü¶ withMainContext do
  let ctx ‚Üê getSimpContext stx[2] !stx[1].isNone
  Conv.applySimpResult (‚Üê deriveSimp ctx (‚Üê instantiateMVars (‚Üê Conv.getLhs)) (useSimp := true))

/--
The basic usage is `#norm_num e`, where `e` is an expression,
which will print the `norm_num` form of `e`.

Syntax: `#norm_num` (`only`)? (`[` simp lemma list `]`)? `:`? expression

This accepts the same options as the `#simp` command.
You can specify additional simp lemmas as usual, for example using `#norm_num [f, g] : e`.
(The colon is optional but helpful for the parser.)
The `only` restricts `norm_num` to using only the provided lemmas, and so
`#norm_num only : e` behaves similarly to `norm_num1`.

Unlike `norm_num`, this command does not fail when no simplifications are made.

`#norm_num` understands local variables, so you can use them to introduce parameters.
-/
macro (name := normNumCmd) "#norm_num" o:(&" only")?
    args:(Parser.Tactic.simpArgs)? " :"? ppSpace e:term : command =>
  `(command| #conv norm_num $[only%$o]? $(args)? => $e)
