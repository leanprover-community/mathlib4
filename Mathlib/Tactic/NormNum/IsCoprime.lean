/-
Copyright (c) 2023 Kyle Miller. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kyle Miller
-/
import Mathlib.RingTheory.Coprime.Lemmas
import Mathlib.Tactic.NormNum.GCD

/-! # `norm_num` extension for `IsCoprime`

This module defines a `norm_num` extension for `IsCoprime` over `â„¤`.

(While `IsCoprime` is defined over `â„•`, since it uses Bezout's identity with `â„•` coefficients
it does not correspond to the usual notion of coprime.)
-/

namespace Tactic

namespace NormNum

open Qq Lean Elab.Tactic Mathlib.Meta.NormNum

theorem int_not_isCoprime_helper (x y : â„¤) (d : â„•) (hd : Int.gcd x y = d)
    (h : Nat.beq d 1 = false) : Â¬ IsCoprime x y := by
  rw [Int.isCoprime_iff_gcd_eq_one, hd]
  -- âŠ¢ Â¬d = 1
  exact Nat.ne_of_beq_eq_false h
  -- ðŸŽ‰ no goals

theorem isInt_isCoprime : {x y nx ny : â„¤} â†’
    IsInt x nx â†’ IsInt y ny â†’ IsCoprime nx ny â†’ IsCoprime x y
  | _, _, _, _, âŸ¨rflâŸ©, âŸ¨rflâŸ©, h => h

theorem isInt_not_isCoprime : {x y nx ny : â„¤} â†’
    IsInt x nx â†’ IsInt y ny â†’ Â¬ IsCoprime nx ny â†’ Â¬ IsCoprime x y
  | _, _, _, _, âŸ¨rflâŸ©, âŸ¨rflâŸ©, h => h

/-- Evaluates `IsCoprime` for the given integer number literals.
Panics if `ex` or `ey` aren't integer number literals. -/
def proveIntIsCoprime (ex ey : Q(â„¤)) : Q(IsCoprime $ex $ey) âŠ• Q(Â¬ IsCoprime $ex $ey) :=
  let âŸ¨ed, pfâŸ© := proveIntGCD ex ey
  if ed.natLit! = 1 then
    have pf' : Q(Int.gcd $ex $ey = 1) := pf
    Sum.inl q(Int.isCoprime_iff_gcd_eq_one.mpr $pf')
  else
    have h : Q(Nat.beq $ed 1 = false) := (q(Eq.refl false) : Expr)
    Sum.inr q(int_not_isCoprime_helper $ex $ey $ed $pf $h)

/-- Evaluates the `IsCoprime` predicate over `â„¤`. -/
@[norm_num IsCoprime (_ : â„¤) (_ : â„¤)]
def evalIntIsCoprime : NormNumExt where eval {u Î±} e := do
  let .app (.app _ (x : Q(â„¤))) (y : Q(â„¤)) â† Meta.whnfR e | failure
  let âŸ¨ex, pâŸ© â† deriveInt x _
  let âŸ¨ey, qâŸ© â† deriveInt y _
  match proveIntIsCoprime ex ey with
  | .inl pf =>
    have pf' : Q(IsCoprime $x $y) := q(isInt_isCoprime $p $q $pf)
    return .isTrue pf'
  | .inr pf =>
    have pf' : Q(Â¬ IsCoprime $x $y) := q(isInt_not_isCoprime $p $q $pf)
    return .isFalse pf'

end NormNum

end Tactic
