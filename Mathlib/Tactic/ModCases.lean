/-
Copyright (c) 2022 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Heather Macbeth
-/
import Mathlib.Data.Int.ModEq

/-! # `mod_cases` tactic

The `mod_cases` tactic does case disjunction on `e % n`, where `e : â„¤`, to yield a number of
subgoals in which `e â‰¡ 0 [ZMOD n]`, ..., `e â‰¡ n-1 [ZMOD n]` are assumed.
-/

set_option autoImplicit true

namespace Mathlib.Tactic.ModCases
open Lean Meta Elab Tactic Term Qq Int

/--
`OnModCases n a lb p` represents a partial proof by cases that
there exists `0 â‰¤ z < n` such that `a â‰¡ z (mod n)`.
It asserts that if `âˆƒ z, lb â‰¤ z < n âˆ§ a â‰¡ z (mod n)` holds, then `p`
(where `p` is the current goal).
-/
def OnModCases (n : â„•) (a : â„¤) (lb : â„•) (p : Sort*) :=
âˆ€ z, lb â‰¤ z âˆ§ z < n âˆ§ a â‰¡ â†‘z [ZMOD â†‘n] â†’ p

/--
The first theorem we apply says that `âˆƒ z, 0 â‰¤ z < n âˆ§ a â‰¡ z (mod n)`.
The actual mathematical content of the proof is here.
-/
@[inline] def onModCases_start (p : Sort*) (a : â„¤) (n : â„•) (hn : Nat.ble 1 n = true)
    (H : OnModCases n a (nat_lit 0) p) : p :=
  H (a % â†‘n).toNat <| by
    have := ofNat_pos.2 <| Nat.le_of_ble_eq_true hn
    -- âŠ¢ 0 â‰¤ toNat (a % â†‘n) âˆ§ toNat (a % â†‘n) < n âˆ§ a â‰¡ â†‘(toNat (a % â†‘n)) [ZMOD â†‘n]
    have nonneg := emod_nonneg a <| Int.ne_of_gt this
    -- âŠ¢ 0 â‰¤ toNat (a % â†‘n) âˆ§ toNat (a % â†‘n) < n âˆ§ a â‰¡ â†‘(toNat (a % â†‘n)) [ZMOD â†‘n]
    refine âŸ¨Nat.zero_le _, ?_, ?_âŸ©
    -- âŠ¢ toNat (a % â†‘n) < n
    Â· rw [Int.toNat_lt nonneg]; exact Int.emod_lt_of_pos _ this
      -- âŠ¢ a % â†‘n < â†‘n
                                -- ðŸŽ‰ no goals
    Â· rw [Int.ModEq, Int.toNat_of_nonneg nonneg, emod_emod]
      -- ðŸŽ‰ no goals

/--
The end point is that once we have reduced to `âˆƒ z, n â‰¤ z < n âˆ§ a â‰¡ z (mod n)`
there are no more cases to consider.
-/
@[inline] def onModCases_stop (p : Sort*) (n : â„•) (a : â„¤) : OnModCases n a n p :=
  fun _ h => (Nat.not_lt.2 h.1 h.2.1).elim

/--
The successor case decomposes `âˆƒ z, b â‰¤ z < n âˆ§ a â‰¡ z (mod n)` into
`a â‰¡ b (mod n) âˆ¨ âˆƒ z, b+1 â‰¤ z < n âˆ§ a â‰¡ z (mod n)`,
and the `a â‰¡ b (mod n) â†’ p` case becomes a subgoal.
-/
@[inline] def onModCases_succ {p : Sort*} {n : â„•} {a : â„¤} (b : â„•)
    (h : a â‰¡ OfNat.ofNat b [ZMOD OfNat.ofNat n] â†’ p) (H : OnModCases n a (Nat.add b 1) p) :
    OnModCases n a b p :=
  fun z âŸ¨hâ‚, hâ‚‚âŸ© => if e : b = z then h (e â–¸ hâ‚‚.2) else H _ âŸ¨Nat.lt_of_le_of_ne hâ‚ e, hâ‚‚âŸ©

/--
Proves an expression of the form `OnModCases n a b p` where `n` and `b` are raw nat literals
and `b â‰¤ n`. Returns the list of subgoals `?gi : a â‰¡ i [ZMOD n] â†’ p`.
-/
partial def proveOnModCases (n : Q(â„•)) (a : Q(â„¤)) (b : Q(â„•)) (p : Q(Sort u)) :
    MetaM (Q(OnModCases $n $a $b $p) Ã— List MVarId) := do
  if n.natLit! â‰¤ b.natLit! then
    haveI' : $b =Q $n := âŸ¨âŸ©
    pure (q(onModCases_stop $p $n $a), [])
  else
    let ty := q($a â‰¡ OfNat.ofNat $b [ZMOD OfNat.ofNat $n] â†’ $p)
    let g â† mkFreshExprMVarQ ty
    have b1 : Q(â„•) := mkRawNatLit (b.natLit! + 1)
    haveI' : $b1 =Q ($b).succ := âŸ¨âŸ©
    let (pr, acc) â† proveOnModCases n a b1 p
    pure (q(onModCases_succ $b $g $pr), g.mvarId! :: acc)

/--
* The tactic `mod_cases h : e % 3` will perform a case disjunction on `e : â„¤` and yield subgoals
  containing the assumptions `h : e â‰¡ 0 [ZMOD 3]`, `h : e â‰¡ 1 [ZMOD 3]`, `h : e â‰¡ 2 [ZMOD 3]`
  respectively.
* In general, `mod_cases h : e % n` works
  when `n` is a positive numeral and `e` is an expression of type `â„¤`.
* If `h` is omitted as in `mod_cases e % n`, it will be default-named `H`.
-/
syntax "mod_cases " (atomic(binderIdent " : "))? term:71 " % " num : tactic

elab_rules : tactic
  | `(tactic| mod_cases $[$h :]? $e % $n) => do
    let n := n.getNat
    if n == 0 then Elab.throwUnsupportedSyntax
    let g â† getMainGoal
    g.withContext do
    let âŸ¨u, p, gâŸ© â† inferTypeQ (.mvar g)
    let e : Q(â„¤) â† Tactic.elabTermEnsuringType e q(â„¤)
    let h := h.getD (â† `(binderIdent| _))
    have lit : Q(â„•) := mkRawNatLit n
    let pâ‚ : Nat.ble 1 $lit =Q true := âŸ¨âŸ©
    let (pâ‚‚, gs) â† proveOnModCases lit e (mkRawNatLit 0) p
    let gs â† gs.mapM fun g => do
      let (fvar, g) â† match h with
      | `(binderIdent| $n:ident) => g.intro n.getId
      | _ => g.intro `H
      g.withContext <| (Expr.fvar fvar).addLocalVarInfoForBinderIdent h
      pure g
    g.mvarId!.assign q(onModCases_start $p $e $lit $pâ‚ $pâ‚‚)
    replaceMainGoal gs
