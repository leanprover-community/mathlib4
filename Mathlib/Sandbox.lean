import Mathlib.NumberTheory.Cyclotomic.PrimitiveRoots
import Mathlib.NumberTheory.NumberField.Norm
import Mathlib.RingTheory.Adjoin.Basic
import Mathlib.Data.ZMod.Basic

theorem ZMod.natCast_eq_natCast_iff_dvd_sub (a b : â„•) (c : â„•) :
    (a : ZMod c) = â†‘b â†” (c : â„¤) âˆ£ b - a := by
  rw [â† Int.cast_natCast a, â† Int.cast_natCast b, â† intCast_eq_intCast_iff_dvd_sub]

theorem isPrimitiveRoot_of_mem_rootsOfUnity {M : Type*} [CommMonoid M] {Î¶ : MË£} {n : â„•+}
    (hÎ¶ : Î¶ âˆˆ rootsOfUnity n M) :
    âˆƒ (d : â„•+), d âˆ£ n âˆ§ IsPrimitiveRoot Î¶ d :=
  âŸ¨âŸ¨orderOf Î¶, IsOfFinOrder.orderOf_pos âŸ¨n, n.pos, (isPeriodicPt_mul_iff_pow_eq_one Î¶).mpr hÎ¶âŸ©âŸ©,
    PNat.dvd_iff.mpr <| orderOf_dvd_of_pow_eq_one hÎ¶, IsPrimitiveRoot.orderOf Î¶âŸ©

open NumberField IsPrimitiveRoot

theorem IsPrimitiveRoot.intermediateField_adjoin_isCyclotomicExtension {F K : Type*} [Field F]
    [Field K] [Algebra F K] [Algebra.IsIntegral F K] {n : â„•+} {Î¶ : K} (hÎ¶ : IsPrimitiveRoot Î¶ n) :
    IsCyclotomicExtension {n} F (IntermediateField.adjoin F {Î¶}) := by
  change IsCyclotomicExtension {n} F (IntermediateField.adjoin F {Î¶}).toSubalgebra
  rw [IntermediateField.adjoin_simple_toSubalgebra_of_integral (Algebra.IsIntegral.isIntegral Î¶)]
  exact hÎ¶.adjoin_isCyclotomicExtension F

variable {K : Type*} [Field K] [NumberField K] {Î¶ : (ğ“ K)}

open IntermediateField

-- See the results in `Mathlib.NumberTheory.Cyclotomic.Rat`
theorem reduc {n : â„•+} (hÎ¶ : IsPrimitiveRoot Î¶ n):
    âˆƒ Î¼ : â„šâŸ®(Î¶ : K)âŸ¯, âˆƒ (_ : IsCyclotomicExtension {n} â„š â„šâŸ®(Î¶ : K)âŸ¯),
      IsPrimitiveRoot Î¼ n âˆ§ (Î¶ : K) - 1 = algebraMap â„šâŸ®(Î¶ : K)âŸ¯ K (Î¼ - 1) := by
  refine âŸ¨IntermediateField.AdjoinSimple.gen â„š (Î¶ : K), ?_, ?_, rflâŸ©
  Â· exact (hÎ¶.map_of_injective
      (RingOfIntegers.coe_injective)).intermediateField_adjoin_isCyclotomicExtension
  Â· exact coe_submonoidClass_iff.mp <| hÎ¶.map_of_injective (RingOfIntegers.coe_injective)

theorem IsPrimitiveRoot.norm_int_sub_one_eq_one {n : â„•+} (hÎ¶ : IsPrimitiveRoot Î¶ n) (hâ‚ : 2 < n)
    (hâ‚‚ : âˆ€ {p : â„•}, Nat.Prime p â†’ âˆ€ (k : â„•), p ^ k â‰  n) :
    Algebra.norm â„¤ (Î¶ - 1) = 1 := by
  simp only [â† Rat.intCast_inj, Algebra.coe_norm_int, map_sub, map_one, Int.cast_one]
  obtain âŸ¨Î¼, _, hÎ¼, hâŸ© := reduc hÎ¶
  rw [h, â† Algebra.norm_norm (S := â„šâŸ®(Î¶ : K)âŸ¯), Algebra.norm_algebraMap, map_pow,
    sub_one_norm_eq_eval_cyclotomic hÎ¼ hâ‚ (Polynomial.cyclotomic.irreducible_rat n.pos),
    Polynomial.eval_one_cyclotomic_not_prime_pow hâ‚‚, Int.cast_one, one_pow]

theorem IsPrimitiveRoot.norm_int_sub_one_ne_two {p : â„•+} [Fact (Nat.Prime p)] {k : â„•} (hp : p â‰  2)
    (hÎ¶ : IsPrimitiveRoot Î¶ (p ^ (k + 1) : â„•+)) :
    Algebra.norm â„¤ (Î¶ - 1) =  p ^ Module.finrank (â„šâŸ®(Î¶ : K)âŸ¯) K := by
  simp only [â† Rat.intCast_inj, Algebra.coe_norm_int, map_sub, map_one, Int.cast_one]
  obtain âŸ¨Î¼, hF, hÎ¼, hâŸ© := reduc hÎ¶
  rw [h, â† Algebra.norm_norm (S := â„šâŸ®(Î¶ : K)âŸ¯), Algebra.norm_algebraMap, map_pow,
    hÎ¼.norm_sub_one_of_prime_ne_two (Polynomial.cyclotomic.irreducible_rat (PNat.pos  _)) hp,
    Int.cast_pow, Int.cast_natCast]

theorem IsPrimitiveRoot.norm_int_sub_one_two {k : â„•} (hk : 2 â‰¤ k)
    (hÎ¶ : IsPrimitiveRoot Î¶ (2 ^ k : â„•+)) :
    Algebra.norm â„¤ (Î¶ - 1) =  2 ^ Module.finrank (â„šâŸ®(Î¶ : K)âŸ¯) K := by
  simp only [â† Rat.intCast_inj, Algebra.coe_norm_int, map_sub, map_one, Int.cast_one]
  obtain âŸ¨Î¼, hF, hÎ¼, hâŸ© := reduc hÎ¶
  rw [h, â† Algebra.norm_norm (S := â„šâŸ®(Î¶ : K)âŸ¯), Algebra.norm_algebraMap, map_pow,
    hÎ¼.norm_sub_one_two hk (Polynomial.cyclotomic.irreducible_rat (Nat.two_pow_pos k)),
    Int.cast_pow, Int.cast_ofNat]

theorem IsPrimitiveRoot.prime_dvd_of_dvd_norm_sub_one {n : â„•+} (hn : 2 â‰¤ n) {p : â„•+}
    [hF : Fact (Nat.Prime p)] (hÎ¶ : IsPrimitiveRoot Î¶ n) (hp : â†‘p âˆ£ Algebra.norm â„¤ (Î¶ - 1)) :
    p âˆ£ n := by
  suffices âˆƒ (q : â„•+), âˆƒ (_ : Nat.Prime q), âˆƒ t s, t â‰  0 âˆ§
    (n : â„•) = q ^ t âˆ§ (p : â„¤) âˆ£ (q : â„¤) ^ s by
    obtain âŸ¨q, hq, t, s, htâ‚, htâ‚‚, hsâŸ© := this
    rw [â† Int.natCast_pow] at hs
    rw [Int.natCast_dvd_natCast] at hs
    have := hF.out.dvd_of_dvd_pow hs
    rw [Nat.prime_dvd_prime_iff_eq hF.out hq] at this
    rw [PNat.dvd_iff, this, htâ‚‚]
    exact dvd_pow_self _ htâ‚
  obtain hn | hn := lt_or_eq_of_le hn
  Â· by_cases h :  âˆ€ {q : â„•}, Nat.Prime q â†’ âˆ€ (k : â„•), q ^ k â‰  n
    Â· rw [hÎ¶.norm_int_sub_one_eq_one hn h, Int.natCast_dvd_ofNat, Nat.dvd_one] at hp
      exact (Nat.Prime.ne_one hF.out hp).elim
    Â· simp only [ne_eq, not_forall, Classical.not_imp, Decidable.not_not] at h
      obtain âŸ¨q, hq, t, hn'âŸ© := h
      cases t with
      | zero =>
          rw [â† PNat.coe_lt_coe, â† hn', pow_zero] at hn
          linarith
      | succ r =>
          lift q to â„•+ using hq.pos
          refine âŸ¨q, hq, r + 1, Module.finrank (â„šâŸ®(Î¶ : K)âŸ¯) K, r.add_one_ne_zero, hn'.symm, ?_âŸ©
          by_cases hq' : q = 2
          Â· have hr : 2 â‰¤ r + 1 := by
              contrapose! hn
              rw [Nat.add_lt_iff_lt_sub_right, Nat.succ_sub_one, Nat.lt_one_iff] at hn
              rw [hq', hn, zero_add, pow_one] at hn'
              rw [PNat.eq hn']
            rw [â† hn', hq'] at hÎ¶
            rw [hÎ¶.norm_int_sub_one_two hr] at hp
            rwa [hq', PNat.val_ofNat, Nat.cast_ofNat]
          Â· have : Fact (Nat.Prime q) := { out := hq }
            rw [â† hn'] at hÎ¶
            rwa [hÎ¶.norm_int_sub_one_ne_two hq'] at hp
  Â· rw [â† hn] at hÎ¶
    replace hÎ¶ : Î¶ = - 1 := by exact IsPrimitiveRoot.eq_neg_one_of_two_right hÎ¶
    rw [hÎ¶, show (-1 : ğ“ K) - 1 = algebraMap â„¤ (ğ“ K) (- 2 : â„¤) by simp; norm_num] at hp
    rw [Algebra.norm_algebraMap_of_basis (RingOfIntegers.basis K)] at hp
    rw [neg_eq_neg_one_mul, mul_pow] at hp
    simp only [Int.reduceNeg, ne_eq, Fintype.card_ne_zero, not_false_eq_true, isUnit_pow_iff,
      IsUnit.neg_iff, isUnit_one, IsUnit.dvd_mul_left] at hp
    rw [show (2 : â„¤) = (2 : â„•+) by rfl] at hp
    exact âŸ¨2, Nat.prime_two, 1, _, one_ne_zero, by rw [hn, pow_one], hpâŸ©
