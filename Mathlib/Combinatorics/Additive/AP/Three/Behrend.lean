/-
Copyright (c) 2022 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Combinatorics.Additive.AP.Three.Defs
import Mathlib.Combinatorics.Pigeonhole
import Mathlib.Data.Complex.ExponentialBounds

#align_import combinatorics.additive.behrend from "leanprover-community/mathlib"@"4fa54b337f7d52805480306db1b1439c741848c8"

/-!
# Behrend's bound on Roth numbers

This file proves Behrend's lower bound on Roth numbers. This says that we can find a subset of
`{1, ..., n}` of size `n / exp (O (sqrt (log n)))` which does not contain arithmetic progressions of
length `3`.

The idea is that the sphere (in the `n` dimensional Euclidean space) doesn't contain arithmetic
progressions (literally) because the corresponding ball is strictly convex. Thus we can take
integer points on that sphere and map them onto `‚Ñï` in a way that preserves arithmetic progressions
(`Behrend.map`).

## Main declarations

* `Behrend.sphere`: The intersection of the Euclidean sphere with the positive integer quadrant.
  This is the set that we will map on `‚Ñï`.
* `Behrend.map`: Given a natural number `d`, `Behrend.map d : ‚Ñï‚Åø ‚Üí ‚Ñï` reads off the coordinates as
  digits in base `d`.
* `Behrend.card_sphere_le_rothNumberNat`: Implicit lower bound on Roth numbers in terms of
  `Behrend.sphere`.
* `Behrend.roth_lower_bound`: Behrend's explicit lower bound on Roth numbers.

## References

* [Bryan Gillespie, *Behrend‚Äôs Construction*]
  (http://www.epsilonsmall.com/resources/behrends-construction/behrend.pdf)
* Behrend, F. A., "On sets of integers which contain no three terms in arithmetical progression"
* [Wikipedia, *Salem-Spencer set*](https://en.wikipedia.org/wiki/Salem‚ÄìSpencer_set)

## Tags

3AP-free, Salem-Spencer, Behrend construction, arithmetic progression, sphere, strictly convex
-/

open Nat hiding log
open Finset Metric Real
open scoped Pointwise

/-- The frontier of a closed strictly convex set only contains trivial arithmetic progressions.
The idea is that an arithmetic progression is contained on a line and the frontier of a strictly
convex set does not contain lines. -/
lemma threeAPFree_frontier {ùïú E : Type*} [LinearOrderedField ùïú] [TopologicalSpace E]
    [AddCommMonoid E] [Module ùïú E] {s : Set E} (hs‚ÇÄ : IsClosed s) (hs‚ÇÅ : StrictConvex ùïú s) :
    ThreeAPFree (frontier s) := by
  intro a ha b hb c hc habc
  obtain rfl : (1 / 2 : ùïú) ‚Ä¢ a + (1 / 2 : ùïú) ‚Ä¢ c = b := by
    rwa [‚Üê smul_add, one_div, inv_smul_eq_iff‚ÇÄ (show (2 : ùïú) ‚â† 0 by norm_num), two_smul]
  have :=
    hs‚ÇÅ.eq (hs‚ÇÄ.frontier_subset ha) (hs‚ÇÄ.frontier_subset hc) one_half_pos one_half_pos
      (add_halves _) hb.2
  simp [this, ‚Üê add_smul]
  ring_nf
  simp
#align add_salem_spencer_frontier threeAPFree_frontier

lemma threeAPFree_sphere {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [StrictConvexSpace ‚Ñù E] (x : E) (r : ‚Ñù) : ThreeAPFree (sphere x r) := by
  obtain rfl | hr := eq_or_ne r 0
  ¬∑ rw [sphere_zero]
    exact threeAPFree_singleton _
  ¬∑ convert threeAPFree_frontier isClosed_ball (strictConvex_closedBall ‚Ñù x r)
    exact (frontier_closedBall _ hr).symm
#align add_salem_spencer_sphere threeAPFree_sphere

namespace Behrend

variable {Œ± Œ≤ : Type*} {n d k N : ‚Ñï} {x : Fin n ‚Üí ‚Ñï}

/-!
### Turning the sphere into 3AP-free set

We define `Behrend.sphere`, the intersection of the $L^2$ sphere with the positive quadrant of
integer points. Because the $L^2$ closed ball is strictly convex, the $L^2$ sphere and
`Behrend.sphere` are 3AP-free (`threeAPFree_sphere`). Then we can turn this set in
`Fin n ‚Üí ‚Ñï` into a set in `‚Ñï` using `Behrend.map`, which preserves `ThreeAPFree` because it is
an additive monoid homomorphism.
-/


/-- The box `{0, ..., d - 1}^n` as a `Finset`. -/
def box (n d : ‚Ñï) : Finset (Fin n ‚Üí ‚Ñï) :=
  Fintype.piFinset fun _ => range d
#align behrend.box Behrend.box

theorem mem_box : x ‚àà box n d ‚Üî ‚àÄ i, x i < d := by simp only [box, Fintype.mem_piFinset, mem_range]
#align behrend.mem_box Behrend.mem_box

@[simp]
theorem card_box : (box n d).card = d ^ n := by simp [box]
#align behrend.card_box Behrend.card_box

@[simp]
theorem box_zero : box (n + 1) 0 = ‚àÖ := by simp [box]
#align behrend.box_zero Behrend.box_zero

/-- The intersection of the sphere of radius `‚àök` with the integer points in the positive
quadrant. -/
def sphere (n d k : ‚Ñï) : Finset (Fin n ‚Üí ‚Ñï) :=
  (box n d).filter fun x => ‚àë i, x i ^ 2 = k
#align behrend.sphere Behrend.sphere

theorem sphere_zero_subset : sphere n d 0 ‚äÜ 0 := fun x => by simp [sphere, Function.funext_iff]
#align behrend.sphere_zero_subset Behrend.sphere_zero_subset

@[simp]
theorem sphere_zero_right (n k : ‚Ñï) : sphere (n + 1) 0 k = ‚àÖ := by simp [sphere]
#align behrend.sphere_zero_right Behrend.sphere_zero_right

theorem sphere_subset_box : sphere n d k ‚äÜ box n d :=
  filter_subset _ _
#align behrend.sphere_subset_box Behrend.sphere_subset_box

theorem norm_of_mem_sphere {x : Fin n ‚Üí ‚Ñï} (hx : x ‚àà sphere n d k) :
    ‚Äñ(WithLp.equiv 2 _).symm ((‚Üë) ‚àò x : Fin n ‚Üí ‚Ñù)‚Äñ = ‚àö‚Üëk := by
  rw [EuclideanSpace.norm_eq]
  dsimp
  simp_rw [abs_cast, ‚Üê cast_pow, ‚Üê cast_sum, (mem_filter.1 hx).2]
#align behrend.norm_of_mem_sphere Behrend.norm_of_mem_sphere

theorem sphere_subset_preimage_metric_sphere : (sphere n d k : Set (Fin n ‚Üí ‚Ñï)) ‚äÜ
    (fun x : Fin n ‚Üí ‚Ñï => (WithLp.equiv 2 _).symm ((‚Üë) ‚àò x : Fin n ‚Üí ‚Ñù)) ‚Åª¬π'
      Metric.sphere (0 : PiLp 2 fun _ : Fin n => ‚Ñù) (‚àö‚Üëk) :=
  fun x hx => by rw [Set.mem_preimage, mem_sphere_zero_iff_norm, norm_of_mem_sphere hx]
#align behrend.sphere_subset_preimage_metric_sphere Behrend.sphere_subset_preimage_metric_sphere

/-- The map that appears in Behrend's bound on Roth numbers. -/
@[simps]
def map (d : ‚Ñï) : (Fin n ‚Üí ‚Ñï) ‚Üí+ ‚Ñï where
  toFun a := ‚àë i, a i * d ^ (i : ‚Ñï)
  map_zero' := by simp_rw [Pi.zero_apply, zero_mul, sum_const_zero]
  map_add' a b := by simp_rw [Pi.add_apply, add_mul, sum_add_distrib]
#align behrend.map Behrend.map

-- @[simp] -- Porting note (#10618): simp can prove this
theorem map_zero (d : ‚Ñï) (a : Fin 0 ‚Üí ‚Ñï) : map d a = 0 := by simp [map]
#align behrend.map_zero Behrend.map_zero

theorem map_succ (a : Fin (n + 1) ‚Üí ‚Ñï) :
    map d a = a 0 + (‚àë x : Fin n, a x.succ * d ^ (x : ‚Ñï)) * d := by
  simp [map, Fin.sum_univ_succ, _root_.pow_succ, ‚Üê mul_assoc, ‚Üê sum_mul]
#align behrend.map_succ Behrend.map_succ

theorem map_succ' (a : Fin (n + 1) ‚Üí ‚Ñï) : map d a = a 0 + map d (a ‚àò Fin.succ) * d :=
  map_succ _
#align behrend.map_succ' Behrend.map_succ'

theorem map_monotone (d : ‚Ñï) : Monotone (map d : (Fin n ‚Üí ‚Ñï) ‚Üí ‚Ñï) := fun x y h => by
  dsimp; exact sum_le_sum fun i _ => Nat.mul_le_mul_right _ <| h i
#align behrend.map_monotone Behrend.map_monotone

theorem map_mod (a : Fin n.succ ‚Üí ‚Ñï) : map d a % d = a 0 % d := by
  rw [map_succ, Nat.add_mul_mod_self_right]
#align behrend.map_mod Behrend.map_mod

theorem map_eq_iff {x‚ÇÅ x‚ÇÇ : Fin n.succ ‚Üí ‚Ñï} (hx‚ÇÅ : ‚àÄ i, x‚ÇÅ i < d) (hx‚ÇÇ : ‚àÄ i, x‚ÇÇ i < d) :
    map d x‚ÇÅ = map d x‚ÇÇ ‚Üî x‚ÇÅ 0 = x‚ÇÇ 0 ‚àß map d (x‚ÇÅ ‚àò Fin.succ) = map d (x‚ÇÇ ‚àò Fin.succ) := by
  refine ‚ü®fun h => ?_, fun h => by rw [map_succ', map_succ', h.1, h.2]‚ü©
  have : x‚ÇÅ 0 = x‚ÇÇ 0 := by
    rw [‚Üê mod_eq_of_lt (hx‚ÇÅ _), ‚Üê map_mod, ‚Üê mod_eq_of_lt (hx‚ÇÇ _), ‚Üê map_mod, h]
  rw [map_succ, map_succ, this, add_right_inj, mul_eq_mul_right_iff] at h
  exact ‚ü®this, h.resolve_right (pos_of_gt (hx‚ÇÅ 0)).ne'‚ü©
#align behrend.map_eq_iff Behrend.map_eq_iff

theorem map_injOn : {x : Fin n ‚Üí ‚Ñï | ‚àÄ i, x i < d}.InjOn (map d) := by
  intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ h
  induction' n with n ih
  ¬∑ simp [eq_iff_true_of_subsingleton]
  rw [forall_const] at ih
  ext i
  have x := (map_eq_iff hx‚ÇÅ hx‚ÇÇ).1 h
  refine Fin.cases x.1 (congr_fun <| ih (fun _ => ?_) (fun _ => ?_) x.2) i
  ¬∑ exact hx‚ÇÅ _
  ¬∑ exact hx‚ÇÇ _
#align behrend.map_inj_on Behrend.map_injOn

theorem map_le_of_mem_box (hx : x ‚àà box n d) :
    map (2 * d - 1) x ‚â§ ‚àë i : Fin n, (d - 1) * (2 * d - 1) ^ (i : ‚Ñï) :=
  map_monotone (2 * d - 1) fun _ => Nat.le_sub_one_of_lt <| mem_box.1 hx _
#align behrend.map_le_of_mem_box Behrend.map_le_of_mem_box

nonrec theorem threeAPFree_sphere : ThreeAPFree (sphere n d k : Set (Fin n ‚Üí ‚Ñï)) := by
  set f : (Fin n ‚Üí ‚Ñï) ‚Üí+ EuclideanSpace ‚Ñù (Fin n) :=
    { toFun := fun f => ((‚Üë) : ‚Ñï ‚Üí ‚Ñù) ‚àò f
      map_zero' := funext fun _ => cast_zero
      map_add' := fun _ _ => funext fun _ => cast_add _ _ }
  refine ThreeAPFree.of_image (AddMonoidHomClass.isAddFreimanHom f (Set.mapsTo_image _ _))
    cast_injective.comp_left.injOn (Set.subset_univ _) ?_
  refine (threeAPFree_sphere 0 (‚àö‚Üëk)).mono (Set.image_subset_iff.2 fun x => ?_)
  rw [Set.mem_preimage, mem_sphere_zero_iff_norm]
  exact norm_of_mem_sphere
#align behrend.add_salem_spencer_sphere Behrend.threeAPFree_sphere

theorem threeAPFree_image_sphere :
    ThreeAPFree ((sphere n d k).image (map (2 * d - 1)) : Set ‚Ñï) := by
  rw [coe_image]
  apply ThreeAPFree.image' (Œ± := Fin n ‚Üí ‚Ñï) (Œ≤ := ‚Ñï) (s := sphere n d k) (map (2 * d - 1))
    (map_injOn.mono _) threeAPFree_sphere
  ¬∑ rw [Set.add_subset_iff]
    rintro a ha b hb i
    have hai := mem_box.1 (sphere_subset_box ha) i
    have hbi := mem_box.1 (sphere_subset_box hb) i
    rw [lt_tsub_iff_right, ‚Üê succ_le_iff, two_mul]
    exact (add_add_add_comm _ _ 1 1).trans_le (_root_.add_le_add hai hbi)
  ¬∑ exact x
#align behrend.add_salem_spencer_image_sphere Behrend.threeAPFree_image_sphere

theorem sum_sq_le_of_mem_box (hx : x ‚àà box n d) : ‚àë i : Fin n, x i ^ 2 ‚â§ n * (d - 1) ^ 2 := by
  rw [mem_box] at hx
  have : ‚àÄ i, x i ^ 2 ‚â§ (d - 1) ^ 2 := fun i =>
    Nat.pow_le_pow_left (Nat.le_sub_one_of_lt (hx i)) _
  exact (sum_le_card_nsmul univ _ _ fun i _ => this i).trans (by rw [card_fin, smul_eq_mul])
#align behrend.sum_sq_le_of_mem_box Behrend.sum_sq_le_of_mem_box

theorem sum_eq : (‚àë i : Fin n, d * (2 * d + 1) ^ (i : ‚Ñï)) = ((2 * d + 1) ^ n - 1) / 2 := by
  refine (Nat.div_eq_of_eq_mul_left zero_lt_two ?_).symm
  rw [‚Üê sum_range fun i => d * (2 * d + 1) ^ (i : ‚Ñï), ‚Üê mul_sum, mul_right_comm, mul_comm d, ‚Üê
    geom_sum_mul_add, add_tsub_cancel_right, mul_comm]
#align behrend.sum_eq Behrend.sum_eq

theorem sum_lt : (‚àë i : Fin n, d * (2 * d + 1) ^ (i : ‚Ñï)) < (2 * d + 1) ^ n :=
  sum_eq.trans_lt <| (Nat.div_le_self _ 2).trans_lt <| pred_lt (pow_pos (succ_pos _) _).ne'
#align behrend.sum_lt Behrend.sum_lt

theorem card_sphere_le_rothNumberNat (n d k : ‚Ñï) :
    (sphere n d k).card ‚â§ rothNumberNat ((2 * d - 1) ^ n) := by
  cases n
  ¬∑ dsimp; refine (card_le_univ _).trans_eq ?_; rfl
  cases d
  ¬∑ simp
  apply threeAPFree_image_sphere.le_rothNumberNat _ _ (card_image_of_injOn _)
  ¬∑ intro; assumption
  ¬∑ simp only [subset_iff, mem_image, and_imp, forall_exists_index, mem_range,
      forall_apply_eq_imp_iff‚ÇÇ, sphere, mem_filter]
    rintro _ x hx _ rfl
    exact (map_le_of_mem_box hx).trans_lt sum_lt
  apply map_injOn.mono fun x => ?_
  ¬∑ intro; assumption
  simp only [mem_coe, sphere, mem_filter, mem_box, and_imp, two_mul]
  exact fun h _ i => (h i).trans_le le_self_add
#align behrend.card_sphere_le_roth_number_nat Behrend.card_sphere_le_rothNumberNat

/-!
### Optimization

Now that we know how to turn the integer points of any sphere into a 3AP-free set, we find a
sphere containing many integer points by the pigeonhole principle. This gives us an implicit bound
that we then optimize by tweaking the parameters. The (almost) optimal parameters are
`Behrend.nValue` and `Behrend.dValue`.
-/


theorem exists_large_sphere_aux (n d : ‚Ñï) : ‚àÉ k ‚àà range (n * (d - 1) ^ 2 + 1),
    (‚Üë(d ^ n) / ((n * (d - 1) ^ 2 :) + 1) : ‚Ñù) ‚â§ (sphere n d k).card := by
  refine exists_le_card_fiber_of_nsmul_le_card_of_maps_to (fun x hx => ?_) nonempty_range_succ ?_
  ¬∑ rw [mem_range, Nat.lt_succ_iff]
    exact sum_sq_le_of_mem_box hx
  ¬∑ rw [card_range, _root_.nsmul_eq_mul, mul_div_assoc', cast_add_one, mul_div_cancel_left‚ÇÄ,
      card_box]
    exact (cast_add_one_pos _).ne'
#align behrend.exists_large_sphere_aux Behrend.exists_large_sphere_aux

theorem exists_large_sphere (n d : ‚Ñï) :
    ‚àÉ k, ((d ^ n :) / (n * d ^ 2 :) : ‚Ñù) ‚â§ (sphere n d k).card := by
  obtain ‚ü®k, -, hk‚ü© := exists_large_sphere_aux n d
  refine ‚ü®k, ?_‚ü©
  obtain rfl | hn := n.eq_zero_or_pos
  ¬∑ simp
  obtain rfl | hd := d.eq_zero_or_pos
  ¬∑ simp
  refine (div_le_div_of_nonneg_left ?_ ?_ ?_).trans hk
  ¬∑ exact cast_nonneg _
  ¬∑ exact cast_add_one_pos _
  simp only [‚Üê le_sub_iff_add_le', cast_mul, ‚Üê mul_sub, cast_pow, cast_sub hd, sub_sq, one_pow,
    cast_one, mul_one, sub_add, sub_sub_self]
  apply one_le_mul_of_one_le_of_one_le
  ¬∑ rwa [one_le_cast]
  rw [_root_.le_sub_iff_add_le]
  set_option tactic.skipAssignedInstances false in norm_num
  exact one_le_cast.2 hd
#align behrend.exists_large_sphere Behrend.exists_large_sphere

theorem bound_aux' (n d : ‚Ñï) : ((d ^ n :) / (n * d ^ 2 :) : ‚Ñù) ‚â§ rothNumberNat ((2 * d - 1) ^ n) :=
  let ‚ü®_, h‚ü© := exists_large_sphere n d
  h.trans <| cast_le.2 <| card_sphere_le_rothNumberNat _ _ _
#align behrend.bound_aux' Behrend.bound_aux'

theorem bound_aux (hd : d ‚â† 0) (hn : 2 ‚â§ n) :
    (d ^ (n - 2 :) / n : ‚Ñù) ‚â§ rothNumberNat ((2 * d - 1) ^ n) := by
  convert bound_aux' n d using 1
  rw [cast_mul, cast_pow, mul_comm, ‚Üê div_div, pow_sub‚ÇÄ _ _ hn, ‚Üê div_eq_mul_inv, cast_pow]
  rwa [cast_ne_zero]
#align behrend.bound_aux Behrend.bound_aux

open scoped Filter Topology

open Real

section NumericalBounds

theorem log_two_mul_two_le_sqrt_log_eight : log 2 * 2 ‚â§ ‚àö(log 8) := by
  have : (8 : ‚Ñù) = 2 ^ ((3 : ‚Ñï) : ‚Ñù) := by rw [rpow_natCast]; norm_num
  rw [this, log_rpow zero_lt_two (3 : ‚Ñï)]
  apply le_sqrt_of_sq_le
  rw [mul_pow, sq (log 2), mul_assoc, mul_comm]
  refine mul_le_mul_of_nonneg_right ?_ (log_nonneg one_le_two)
  rw [‚Üê le_div_iff]
  on_goal 1 => apply log_two_lt_d9.le.trans
  all_goals norm_num1
#align behrend.log_two_mul_two_le_sqrt_log_eight Behrend.log_two_mul_two_le_sqrt_log_eight

theorem two_div_one_sub_two_div_e_le_eight : 2 / (1 - 2 / exp 1) ‚â§ 8 := by
  rw [div_le_iff, mul_sub, mul_one, mul_div_assoc', le_sub_comm, div_le_iff (exp_pos _)]
  ¬∑ have : 16 < 6 * (2.7182818283 : ‚Ñù) := by norm_num
    linarith [exp_one_gt_d9]
  rw [sub_pos, div_lt_one] <;> exact exp_one_gt_d9.trans' (by norm_num)
#align behrend.two_div_one_sub_two_div_e_le_eight Behrend.two_div_one_sub_two_div_e_le_eight

theorem le_sqrt_log (hN : 4096 ‚â§ N) : log (2 / (1 - 2 / exp 1)) * (69 / 50) ‚â§ ‚àö(log ‚ÜëN) := by
  have : (12 : ‚Ñï) * log 2 ‚â§ log N := by
    rw [‚Üê log_rpow zero_lt_two, rpow_natCast]
    exact log_le_log (by positivity) (mod_cast hN)
  refine (mul_le_mul_of_nonneg_right (log_le_log ?_ two_div_one_sub_two_div_e_le_eight) <| by
    norm_num1).trans ?_
  ¬∑ refine div_pos zero_lt_two ?_
    rw [sub_pos, div_lt_one (exp_pos _)]
    exact exp_one_gt_d9.trans_le' (by norm_num1)
  have l8 : log 8 = (3 : ‚Ñï) * log 2 := by
    rw [‚Üê log_rpow zero_lt_two, rpow_natCast]
    norm_num
  rw [l8]
  apply le_sqrt_of_sq_le (le_trans _ this)
  rw [mul_right_comm, mul_pow, sq (log 2), ‚Üê mul_assoc]
  apply mul_le_mul_of_nonneg_right _ (log_nonneg one_le_two)
  rw [‚Üê le_div_iff']
  ¬∑ exact log_two_lt_d9.le.trans (by norm_num1)
  exact sq_pos_of_ne_zero (by norm_num1)
#align behrend.le_sqrt_log Behrend.le_sqrt_log

theorem exp_neg_two_mul_le {x : ‚Ñù} (hx : 0 < x) : exp (-2 * x) < exp (2 - ‚åàx‚åâ‚Çä) / ‚åàx‚åâ‚Çä := by
  have h‚ÇÅ := ceil_lt_add_one hx.le
  have h‚ÇÇ : 1 - x ‚â§ 2 - ‚åàx‚åâ‚Çä := by linarith
  calc
    _ ‚â§ exp (1 - x) / (x + 1) := ?_
    _ ‚â§ exp (2 - ‚åàx‚åâ‚Çä) / (x + 1) := by gcongr
    _ < _ := by gcongr
  rw [le_div_iff (add_pos hx zero_lt_one), ‚Üê le_div_iff' (exp_pos _), ‚Üê exp_sub, neg_mul,
    sub_neg_eq_add, two_mul, sub_add_add_cancel, add_comm _ x]
  exact le_trans (le_add_of_nonneg_right zero_le_one) (add_one_le_exp _)
#align behrend.exp_neg_two_mul_le Behrend.exp_neg_two_mul_le

theorem div_lt_floor {x : ‚Ñù} (hx : 2 / (1 - 2 / exp 1) ‚â§ x) : x / exp 1 < (‚åäx / 2‚åã‚Çä : ‚Ñù) := by
  apply lt_of_le_of_lt _ (sub_one_lt_floor _)
  have : 0 < 1 - 2 / exp 1 := by
    rw [sub_pos, div_lt_one (exp_pos _)]
    exact lt_of_le_of_lt (by norm_num) exp_one_gt_d9
  rwa [le_sub_comm, div_eq_mul_one_div x, div_eq_mul_one_div x, ‚Üê mul_sub, div_sub', ‚Üê
    div_eq_mul_one_div, mul_div_assoc', one_le_div, ‚Üê div_le_iff this]
  ¬∑ exact zero_lt_two
  ¬∑ exact two_ne_zero
#align behrend.div_lt_floor Behrend.div_lt_floor

theorem ceil_lt_mul {x : ‚Ñù} (hx : 50 / 19 ‚â§ x) : (‚åàx‚åâ‚Çä : ‚Ñù) < 1.38 * x := by
  refine (ceil_lt_add_one <| hx.trans' <| by norm_num).trans_le ?_
  rw [‚Üê le_sub_iff_add_le', ‚Üê sub_one_mul]
  have : (1.38 : ‚Ñù) = 69 / 50 := by norm_num
  rwa [this, show (69 / 50 - 1 : ‚Ñù) = (50 / 19)‚Åª¬π by norm_num1, ‚Üê
    div_eq_inv_mul, one_le_div]
  norm_num1
#align behrend.ceil_lt_mul Behrend.ceil_lt_mul

end NumericalBounds

/-- The (almost) optimal value of `n` in `Behrend.bound_aux`. -/
noncomputable def nValue (N : ‚Ñï) : ‚Ñï :=
  ‚åà‚àö(log N)‚åâ‚Çä
#align behrend.n_value Behrend.nValue

/-- The (almost) optimal value of `d` in `Behrend.bound_aux`. -/
noncomputable def dValue (N : ‚Ñï) : ‚Ñï := ‚åä(N : ‚Ñù) ^ (nValue N : ‚Ñù)‚Åª¬π / 2‚åã‚Çä
#align behrend.d_value Behrend.dValue

theorem nValue_pos (hN : 2 ‚â§ N) : 0 < nValue N :=
  ceil_pos.2 <| Real.sqrt_pos.2 <| log_pos <| one_lt_cast.2 <| hN
#align behrend.n_value_pos Behrend.nValue_pos

#noalign behrend.two_le_n_value

theorem three_le_nValue (hN : 64 ‚â§ N) : 3 ‚â§ nValue N := by
  rw [nValue, ‚Üê lt_iff_add_one_le, lt_ceil, cast_two]
  apply lt_sqrt_of_sq_lt
  have : (2 : ‚Ñù) ^ ((6 : ‚Ñï) : ‚Ñù) ‚â§ N := by
    rw [rpow_natCast]
    exact (cast_le.2 hN).trans' (by norm_num1)
  apply lt_of_lt_of_le _ (log_le_log (rpow_pos_of_pos zero_lt_two _) this)
  rw [log_rpow zero_lt_two, ‚Üê div_lt_iff']
  ¬∑ exact log_two_gt_d9.trans_le' (by norm_num1)
  ¬∑ norm_num1
#align behrend.three_le_n_value Behrend.three_le_nValue

theorem dValue_pos (hN‚ÇÉ : 8 ‚â§ N) : 0 < dValue N := by
  have hN‚ÇÄ : 0 < (N : ‚Ñù) := cast_pos.2 (succ_pos'.trans_le hN‚ÇÉ)
  rw [dValue, floor_pos, ‚Üê log_le_log_iff zero_lt_one, log_one, log_div _ two_ne_zero, log_rpow hN‚ÇÄ,
    inv_mul_eq_div, sub_nonneg, le_div_iff]
  ¬∑ have : (nValue N : ‚Ñù) ‚â§ 2 * ‚àö(log N) := by
      apply (ceil_lt_add_one <| sqrt_nonneg _).le.trans
      rw [two_mul, add_le_add_iff_left]
      apply le_sqrt_of_sq_le
      rw [one_pow, le_log_iff_exp_le hN‚ÇÄ]
      exact (exp_one_lt_d9.le.trans <| by norm_num).trans (cast_le.2 hN‚ÇÉ)
    apply (mul_le_mul_of_nonneg_left this <| log_nonneg one_le_two).trans _
    rw [‚Üê mul_assoc, ‚Üê le_div_iff (Real.sqrt_pos.2 <| log_pos <| one_lt_cast.2 _), div_sqrt]
    ¬∑ apply log_two_mul_two_le_sqrt_log_eight.trans
      apply Real.sqrt_le_sqrt
      exact log_le_log (by norm_num) (mod_cast hN‚ÇÉ)
    exact hN‚ÇÉ.trans_lt' (by norm_num)
  ¬∑ exact cast_pos.2 (nValue_pos <| hN‚ÇÉ.trans' <| by norm_num)
  ¬∑ exact (rpow_pos_of_pos hN‚ÇÄ _).ne'
  ¬∑ exact div_pos (rpow_pos_of_pos hN‚ÇÄ _) zero_lt_two
#align behrend.d_value_pos Behrend.dValue_pos

theorem le_N (hN : 2 ‚â§ N) : (2 * dValue N - 1) ^ nValue N ‚â§ N := by
  have : (2 * dValue N - 1) ^ nValue N ‚â§ (2 * dValue N) ^ nValue N :=
    Nat.pow_le_pow_left (Nat.sub_le _ _) _
  apply this.trans
  suffices ((2 * dValue N) ^ nValue N : ‚Ñù) ‚â§ N from mod_cast this
  suffices i : (2 * dValue N : ‚Ñù) ‚â§ (N : ‚Ñù) ^ (nValue N : ‚Ñù)‚Åª¬π by
    rw [‚Üê rpow_natCast]
    apply (rpow_le_rpow (mul_nonneg zero_le_two (cast_nonneg _)) i (cast_nonneg _)).trans
    rw [‚Üê rpow_mul (cast_nonneg _), inv_mul_cancel, rpow_one]
    rw [cast_ne_zero]
    apply (nValue_pos hN).ne'
  rw [‚Üê le_div_iff']
  ¬∑ exact floor_le (div_nonneg (rpow_nonneg (cast_nonneg _) _) zero_le_two)
  apply zero_lt_two
set_option linter.uppercaseLean3 false in
#align behrend.le_N Behrend.le_N

theorem bound (hN : 4096 ‚â§ N) : (N : ‚Ñù) ^ (nValue N : ‚Ñù)‚Åª¬π / exp 1 < dValue N := by
  apply div_lt_floor _
  rw [‚Üê log_le_log_iff, log_rpow, mul_comm, ‚Üê div_eq_mul_inv]
  ¬∑ apply le_trans _ (div_le_div_of_nonneg_left _ _ (ceil_lt_mul _).le)
    ¬∑ rw [mul_comm, ‚Üê div_div, div_sqrt, le_div_iff]
      ¬∑ set_option tactic.skipAssignedInstances false in norm_num; exact le_sqrt_log hN
      ¬∑ norm_num1
    ¬∑ apply log_nonneg
      rw [one_le_cast]
      exact hN.trans' (by norm_num1)
    ¬∑ rw [cast_pos, lt_ceil, cast_zero, Real.sqrt_pos]
      refine log_pos ?_
      rw [one_lt_cast]
      exact hN.trans_lt' (by norm_num1)
    apply le_sqrt_of_sq_le
    have : (12 : ‚Ñï) * log 2 ‚â§ log N := by
      rw [‚Üê log_rpow zero_lt_two, rpow_natCast]
      exact log_le_log (by positivity) (mod_cast hN)
    refine le_trans ?_ this
    rw [‚Üê div_le_iff']
    ¬∑ exact log_two_gt_d9.le.trans' (by norm_num1)
    ¬∑ norm_num1
  ¬∑ rw [cast_pos]
    exact hN.trans_lt' (by norm_num1)
  ¬∑ refine div_pos zero_lt_two ?_
    rw [sub_pos, div_lt_one (exp_pos _)]
    exact lt_of_le_of_lt (by norm_num1) exp_one_gt_d9
  positivity
#align behrend.bound Behrend.bound

theorem roth_lower_bound_explicit (hN : 4096 ‚â§ N) :
    (N : ‚Ñù) * exp (-4 * ‚àö(log N)) < rothNumberNat N := by
  let n := nValue N
  have hn : 0 < (n : ‚Ñù) := cast_pos.2 (nValue_pos <| hN.trans' <| by norm_num1)
  have hd : 0 < dValue N := dValue_pos (hN.trans' <| by norm_num1)
  have hN‚ÇÄ : 0 < (N : ‚Ñù) := cast_pos.2 (hN.trans' <| by norm_num1)
  have hn‚ÇÇ : 2 < n := three_le_nValue <| hN.trans' <| by norm_num1
  have : (2 * dValue N - 1) ^ n ‚â§ N := le_N (hN.trans' <| by norm_num1)
  calc
    _ ‚â§ (N ^ (nValue N : ‚Ñù)‚Åª¬π / rexp 1 : ‚Ñù) ^ (n - 2) / n := ?_
    _ < _ := by gcongr; exacts [(tsub_pos_of_lt hn‚ÇÇ).ne', bound hN]
    _ ‚â§ rothNumberNat ((2 * dValue N - 1) ^ n) := bound_aux hd.ne' hn‚ÇÇ.le
    _ ‚â§ rothNumberNat N := mod_cast rothNumberNat.mono this
  rw [‚Üê rpow_natCast, div_rpow (rpow_nonneg hN‚ÇÄ.le _) (exp_pos _).le, ‚Üê rpow_mul hN‚ÇÄ.le,
    inv_mul_eq_div, cast_sub hn‚ÇÇ.le, cast_two, same_sub_div hn.ne', exp_one_rpow,
    div_div, rpow_sub hN‚ÇÄ, rpow_one, div_div, div_eq_mul_inv]
  refine mul_le_mul_of_nonneg_left ?_ (cast_nonneg _)
  rw [mul_inv, mul_inv, ‚Üê exp_neg, ‚Üê rpow_neg (cast_nonneg _), neg_sub, ‚Üê div_eq_mul_inv]
  have : exp (-4 * ‚àö(log N)) = exp (-2 * ‚àö(log N)) * exp (-2 * ‚àö(log N)) := by
    rw [‚Üê exp_add, ‚Üê add_mul]
    norm_num
  rw [this]
  refine mul_le_mul ?_ (exp_neg_two_mul_le <| Real.sqrt_pos.2 <| log_pos ?_).le (exp_pos _).le <|
      rpow_nonneg (cast_nonneg _) _
  ¬∑ rw [‚Üê le_log_iff_exp_le (rpow_pos_of_pos hN‚ÇÄ _), log_rpow hN‚ÇÄ, ‚Üê le_div_iff, mul_div_assoc,
      div_sqrt, neg_mul, neg_le_neg_iff, div_mul_eq_mul_div, div_le_iff hn]
    ¬∑ exact mul_le_mul_of_nonneg_left (le_ceil _) zero_le_two
    refine Real.sqrt_pos.2 (log_pos ?_)
    rw [one_lt_cast]
    exact hN.trans_lt' (by norm_num1)
  ¬∑ rw [one_lt_cast]
    exact hN.trans_lt' (by norm_num1)
#align behrend.roth_lower_bound_explicit Behrend.roth_lower_bound_explicit

theorem exp_four_lt : exp 4 < 64 := by
  rw [show (64 : ‚Ñù) = 2 ^ ((6 : ‚Ñï) : ‚Ñù) by rw [rpow_natCast]; norm_num1,
    ‚Üê lt_log_iff_exp_lt (rpow_pos_of_pos zero_lt_two _), log_rpow zero_lt_two, ‚Üê div_lt_iff']
  ¬∑ exact log_two_gt_d9.trans_le' (by norm_num1)
  ¬∑ norm_num
#align behrend.exp_four_lt Behrend.exp_four_lt

theorem four_zero_nine_six_lt_exp_sixteen : 4096 < exp 16 := by
  rw [‚Üê log_lt_iff_lt_exp (show (0 : ‚Ñù) < 4096 by norm_num), show (4096 : ‚Ñù) = 2 ^ 12 by norm_cast,
    ‚Üê rpow_natCast, log_rpow zero_lt_two, cast_ofNat]
  have : 12 * (0.6931471808 : ‚Ñù) < 16 := by norm_num
  linarith [log_two_lt_d9]
#align behrend.four_zero_nine_six_lt_exp_sixteen Behrend.four_zero_nine_six_lt_exp_sixteen

theorem lower_bound_le_one' (hN : 2 ‚â§ N) (hN' : N ‚â§ 4096) :
    (N : ‚Ñù) * exp (-4 * ‚àö(log N)) ‚â§ 1 := by
  rw [‚Üê log_le_log_iff (mul_pos (cast_pos.2 (zero_lt_two.trans_le hN)) (exp_pos _)) zero_lt_one,
    log_one, log_mul (cast_pos.2 (zero_lt_two.trans_le hN)).ne' (exp_pos _).ne', log_exp, neg_mul, ‚Üê
    sub_eq_add_neg, sub_nonpos, ‚Üê
    div_le_iff (Real.sqrt_pos.2 <| log_pos <| one_lt_cast.2 <| one_lt_two.trans_le hN), div_sqrt,
    sqrt_le_left zero_le_four, log_le_iff_le_exp (cast_pos.2 (zero_lt_two.trans_le hN))]
  norm_num1
  apply le_trans _ four_zero_nine_six_lt_exp_sixteen.le
  exact mod_cast hN'
#align behrend.lower_bound_le_one' Behrend.lower_bound_le_one'

theorem lower_bound_le_one (hN : 1 ‚â§ N) (hN' : N ‚â§ 4096) :
    (N : ‚Ñù) * exp (-4 * ‚àö(log N)) ‚â§ 1 := by
  obtain rfl | hN := hN.eq_or_lt
  ¬∑ norm_num
  ¬∑ exact lower_bound_le_one' hN hN'
#align behrend.lower_bound_le_one Behrend.lower_bound_le_one

theorem roth_lower_bound : (N : ‚Ñù) * exp (-4 * ‚àö(log N)) ‚â§ rothNumberNat N := by
  obtain rfl | hN := Nat.eq_zero_or_pos N
  ¬∑ norm_num
  obtain h‚ÇÅ | h‚ÇÅ := le_or_lt 4096 N
  ¬∑ exact (roth_lower_bound_explicit h‚ÇÅ).le
  ¬∑ apply (lower_bound_le_one hN h‚ÇÅ.le).trans
    simpa using rothNumberNat.monotone hN
#align behrend.roth_lower_bound Behrend.roth_lower_bound

end Behrend
