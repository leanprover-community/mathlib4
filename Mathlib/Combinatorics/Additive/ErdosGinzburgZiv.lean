/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Ring.Finset
import Mathlib.Data.Multiset.Fintype
import Mathlib.FieldTheory.ChevalleyWarning

/-!
# The ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem

This file proves the ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem as a corollary of Chevalley-Warning. This theorem
states that among any (not necessarily distinct) `2 * n - 1` elements of `ZMod n`, we can find `n`
elements of sum zero.

## Main declarations

* `Int.erdos_ginzburg_ziv`: The ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem stated using sequences in `â„¤`
* `ZMod.erdos_ginzburg_ziv`: The ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem stated using sequences in `ZMod n`
-/

open Finset MvPolynomial

variable {Î¹ : Type*}

section prime
variable {p : â„•} [Fact p.Prime] {s : Finset Î¹}

set_option linter.unusedVariables false in
/-- The first multivariate polynomial used in the proof of ErdÅ‘sâ€“Ginzburgâ€“Ziv. -/
private noncomputable def fâ‚ (s : Finset Î¹) (a : Î¹ â†’ ZMod p) : MvPolynomial s (ZMod p) :=
  âˆ‘ i, X i ^ (p - 1)

/-- The second multivariate polynomial used in the proof of ErdÅ‘sâ€“Ginzburgâ€“Ziv. -/
private noncomputable def fâ‚‚ (s : Finset Î¹) (a : Î¹ â†’ ZMod p) : MvPolynomial s (ZMod p) :=
  âˆ‘ i : s, a i â€¢ X i ^ (p - 1)

private lemma totalDegree_fâ‚_add_totalDegree_fâ‚‚ {a : Î¹ â†’ ZMod p} :
    (fâ‚ s a).totalDegree + (fâ‚‚ s a).totalDegree < 2 * p - 1 := by
  calc
    _ â‰¤ (p - 1) + (p - 1) := by
      gcongr <;> apply totalDegree_finsetSum_le <;> rintro i _
      Â· exact (totalDegree_X_pow ..).le
      Â· exact (totalDegree_smul_le ..).trans (totalDegree_X_pow ..).le
    _ < 2 * p - 1 := by have := (Fact.out : p.Prime).two_le; omega

/-- The prime case of the **ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem** for `â„¤/pâ„¤`.

Any sequence of `2 * p - 1` elements of `ZMod p` contains a subsequence of `p` elements whose sum is
zero. -/
private theorem ZMod.erdos_ginzburg_ziv_prime (a : Î¹ â†’ ZMod p) (hs : #s = 2 * p - 1) :
    âˆƒ t âŠ† s, #t = p âˆ§ âˆ‘ i âˆˆ t, a i = 0 := by
  haveI : NeZero p := inferInstance
  classical
  -- Let `N` be the number of common roots of our polynomials `fâ‚` and `fâ‚‚` (`f s ff` and `f s tt`).
  set N := Fintype.card {x // eval x (fâ‚ s a) = 0 âˆ§ eval x (fâ‚‚ s a) = 0}
  -- Zero is a common root to `fâ‚` and `fâ‚‚`, so `N` is nonzero
  let zero_sol : {x // eval x (fâ‚ s a) = 0 âˆ§ eval x (fâ‚‚ s a) = 0} :=
    âŸ¨0, by simp [fâ‚, fâ‚‚, map_sum, (Fact.out : p.Prime).one_lt, tsub_eq_zero_iff_le]âŸ©
  have hNâ‚€ : 0 < N := @Fintype.card_pos _ _ âŸ¨zero_solâŸ©
  have hs' : 2 * p - 1 = Fintype.card s := by simp [hs]
  -- Chevalley-Warning gives us that `p âˆ£ n` because the total degrees of `fâ‚` and `fâ‚‚` are at most
  -- `p - 1`, and we have `2 * p - 1 > 2 * (p - 1)` variables.
  have hpN : p âˆ£ N := char_dvd_card_solutions_of_add_lt p
    (totalDegree_fâ‚_add_totalDegree_fâ‚‚.trans_eq hs')
  -- Hence, `2 â‰¤ p â‰¤ N` and we can make a common root `x â‰  0`.
  obtain âŸ¨x, hxâŸ© := Fintype.exists_ne_of_one_lt_card ((Fact.out : p.Prime).one_lt.trans_le <|
    Nat.le_of_dvd hNâ‚€ hpN) zero_sol
  -- This common root gives us the required subsequence, namely the `i âˆˆ s` such that `x i â‰  0`.
  refine âŸ¨({a | x.1 a â‰  0} : Finset _).map âŸ¨(â†‘), Subtype.val_injectiveâŸ©, ?_, ?_, ?_âŸ©
  Â· simp +contextual [subset_iff]
  -- From `fâ‚ x = 0`, we get that `p` divides the number of `a` such that `x a â‰  0`.
  Â· rw [card_map]
    refine Nat.eq_of_dvd_of_lt_two_mul (Finset.card_pos.2 ?_).ne' ?_ <|
      (Finset.card_filter_le _ _).trans_lt ?_
    -- This number is nonzero because `x â‰  0`.
    Â· rw [â† Subtype.coe_ne_coe, Function.ne_iff] at hx
      exact hx.imp (fun a ha â†¦ mem_filter.2 âŸ¨Finset.mem_attach _ _, haâŸ©)
    Â· rw [â† CharP.cast_eq_zero_iff (ZMod p), â† Finset.sum_boole]
      simpa only [fâ‚, map_sum, ZMod.pow_card_sub_one, map_pow, eval_X] using x.2.1
    -- And it is at most `2 * p - 1`, so it must be `p`.
    Â· rw [univ_eq_attach, card_attach, hs]
      exact tsub_lt_self (mul_pos zero_lt_two (Fact.out : p.Prime).pos) zero_lt_one
  -- From `fâ‚‚ x = 0`, we get that `p` divides the sum of the `a âˆˆ s` such that `x a â‰  0`.
  Â· simpa [fâ‚‚, ZMod.pow_card_sub_one, Finset.sum_filter] using x.2.2

/-- The prime case of the **ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem** for `â„¤`.

Any sequence of `2 * p - 1` elements of `â„¤` contains a subsequence of `p` elements whose sum is
divisible by `p`. -/
private theorem Int.erdos_ginzburg_ziv_prime (a : Î¹ â†’ â„¤) (hs : #s = 2 * p - 1) :
    âˆƒ t âŠ† s, #t = p âˆ§ â†‘p âˆ£ âˆ‘ i âˆˆ t, a i := by
  simpa [â† Int.cast_sum, ZMod.intCast_zmod_eq_zero_iff_dvd]
    using ZMod.erdos_ginzburg_ziv_prime (Int.cast âˆ˜ a) hs

end prime

section composite
variable {n : â„•} {s : Finset Î¹}

/-- The **ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem** for `â„¤`.

Any sequence of at least `2 * n - 1` elements of `â„¤` contains a subsequence of `n` elements whose
sum is divisible by `n`. -/
theorem Int.erdos_ginzburg_ziv (a : Î¹ â†’ â„¤) (hs : 2 * n - 1 â‰¤ #s) :
    âˆƒ t âŠ† s, #t = n âˆ§ â†‘n âˆ£ âˆ‘ i âˆˆ t, a i := by
  classical
  -- Do induction on the prime factorisation of `n`. Note that we will apply the induction
  -- hypothesis with `Î¹ := Finset Î¹`, so we need to generalise.
  induction n using Nat.prime_composite_induction generalizing Î¹ with
  -- When `n := 0`, we can set `t := âˆ…`.
  | zero => exact âŸ¨âˆ…, by simpâŸ©
  -- When `n := 1`, we can take `t` to be any subset of `s` of size `2 * n - 1`.
  | one => simpa using exists_subset_card_eq hs
  -- When `n := p` is prime, we use the prime case `Int.erdos_ginzburg_ziv_prime`.
  | prime p hp =>
    haveI := Fact.mk hp
    obtain âŸ¨t, hts, htâŸ© := exists_subset_card_eq hs
    obtain âŸ¨u, hut, huâŸ© := Int.erdos_ginzburg_ziv_prime a ht
    exact âŸ¨u, hut.trans hts, huâŸ©
  -- When `n := m * n` is composite, we pick (by induction hypothesis on `n`) `2 * m - 1` sets of
  -- size `n` and sums divisible by `n`. Then by induction hypothesis (on `m`) we can pick `m` of
  -- these sets whose sum is divisible by `m * n`.
  | composite m hm ihm n hn ihn =>
     -- First, show that it is enough to have those `2 * m - 1` sets.
    suffices âˆ€ k â‰¤ 2 * m - 1, âˆƒ ğ’œ : Finset (Finset Î¹), #ğ’œ = k âˆ§
      (ğ’œ : Set (Finset Î¹)).Pairwise _root_.Disjoint âˆ§
        âˆ€ â¦ƒtâ¦„, t âˆˆ ğ’œ â†’ t âŠ† s âˆ§ #t = n âˆ§ â†‘n âˆ£ âˆ‘ i âˆˆ t, a i by
     -- Assume `ğ’œ` is a family of `2 * m - 1` sets, each of size `n` and sum divisible by `n`.
      obtain âŸ¨ğ’œ, hğ’œcard, hğ’œdisj, hğ’œâŸ© := this _ le_rfl
      -- By induction hypothesis on `m`, find a subfamily `â„¬` of size `m` such that the sum over
      -- `t âˆˆ â„¬` of `(âˆ‘ i âˆˆ t, a i) / n` is divisible by `m`.
      obtain âŸ¨â„¬, hâ„¬ğ’œ, hâ„¬card, hâ„¬âŸ© := ihm (fun t â†¦ (âˆ‘ i âˆˆ t, a i) / n) hğ’œcard.ge
      -- We are done.
      refine âŸ¨â„¬.biUnion fun x â†¦ x, biUnion_subset.2 fun t ht â†¦ (hğ’œ <| hâ„¬ğ’œ ht).1, ?_, ?_âŸ©
      Â· rw [card_biUnion (hğ’œdisj.mono hâ„¬ğ’œ), sum_const_nat fun t ht â†¦ (hğ’œ <| hâ„¬ğ’œ ht).2.1, hâ„¬card]
      rwa [sum_biUnion, Int.natCast_mul, mul_comm, â† Int.dvd_div_iff_mul_dvd, Int.sum_div]
      Â· exact fun t ht â†¦ (hğ’œ <| hâ„¬ğ’œ ht).2.2
      Â· exact dvd_sum fun t ht â†¦ (hğ’œ <| hâ„¬ğ’œ ht).2.2
      Â· exact hğ’œdisj.mono hâ„¬ğ’œ
    -- Now, let's find those `2 * m - 1` sets.
    rintro k hk
    -- We induct on the size `k â‰¤ 2 * m - 1` of the family we are constructing.
    induction k with
    -- For `k = 0`, the empty family trivially works.
    | zero => exact âŸ¨âˆ…, by simpâŸ©
    | succ k ih =>
    -- At `k + 1`, call `ğ’œ` the existing family of size `k â‰¤ 2 * m - 2`.
    obtain âŸ¨ğ’œ, hğ’œcard, hğ’œdisj, hğ’œâŸ© := ih (Nat.le_of_succ_le hk)
    -- There are at least `2 * (m * n) - 1 - k * n â‰¥ 2 * m - 1` elements in `s` that have not been
    -- taken in any element of `ğ’œ`.
    have : 2 * n - 1 â‰¤ #(s \ ğ’œ.biUnion id) := by
      calc
        _ â‰¤ (2 * m - k) * n - 1 := by gcongr; omega
        _ = (2 * (m * n) - 1) - âˆ‘ t âˆˆ ğ’œ, #t := by
          rw [tsub_mul, mul_assoc, tsub_right_comm, sum_const_nat fun t ht â†¦ (hğ’œ ht).2.1, hğ’œcard]
        _ â‰¤ #s - #(ğ’œ.biUnion id) := by gcongr; exact card_biUnion_le
        _ â‰¤ #(s \ ğ’œ.biUnion id) := le_card_sdiff ..
    -- So by the induction hypothesis on `n` we can find a new set `t` of size `n` and sum divisible
    -- by `n`.
    obtain âŸ¨tâ‚€, htâ‚€, htâ‚€card, htâ‚€sumâŸ© := ihn a this
    -- This set is distinct and disjoint from the previous ones, so we are done.
    have : tâ‚€ âˆ‰ ğ’œ := by
      rintro h
      obtain rfl : n = 0 := by
        simpa [â† card_eq_zero, htâ‚€card] using sdiff_disjoint.mono htâ‚€ <| subset_biUnion_of_mem id h
      omega
    refine âŸ¨ğ’œ.cons tâ‚€ this, by rw [card_cons, hğ’œcard], ?_, ?_âŸ©
    Â· simp only [cons_eq_insert, coe_insert, Set.pairwise_insert_of_symmetric symmetric_disjoint,
        mem_coe, ne_eq]
      exact âŸ¨hğ’œdisj, fun t ht _ â†¦ sdiff_disjoint.mono htâ‚€ <| subset_biUnion_of_mem id htâŸ©
    Â· simp only [cons_eq_insert, mem_insert, forall_eq_or_imp, and_assoc]
      exact âŸ¨htâ‚€.trans sdiff_subset, htâ‚€card, htâ‚€sum, hğ’œâŸ©

/-- The **ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem** for `â„¤/nâ„¤`.

Any sequence of at least `2 * n - 1` elements of `ZMod n` contains a subsequence of `n` elements
whose sum is zero. -/
theorem ZMod.erdos_ginzburg_ziv (a : Î¹ â†’ ZMod n) (hs : 2 * n - 1 â‰¤ #s) :
    âˆƒ t âŠ† s, #t = n âˆ§ âˆ‘ i âˆˆ t, a i = 0 := by
  simpa [â† ZMod.intCast_zmod_eq_zero_iff_dvd] using Int.erdos_ginzburg_ziv (ZMod.cast âˆ˜ a) hs

/-- The **ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem** for `â„¤` for multiset.

Any multiset of at least `2 * n - 1` elements of `â„¤` contains a submultiset of `n` elements whose
sum is divisible by `n`. -/
theorem Int.erdos_ginzburg_ziv_multiset (s : Multiset â„¤) (hs : 2 * n - 1 â‰¤ Multiset.card s) :
    âˆƒ t â‰¤ s, Multiset.card t = n âˆ§ â†‘n âˆ£ t.sum := by
  obtain âŸ¨t, hts, htâŸ© := Int.erdos_ginzburg_ziv (s := s.toEnumFinset) Prod.fst (by simpa using hs)
  exact âŸ¨t.1.map Prod.fst, Multiset.map_fst_le_of_subset_toEnumFinset hts, by simpa using htâŸ©

/-- The **ErdÅ‘sâ€“Ginzburgâ€“Ziv theorem** for `â„¤/nâ„¤` for multiset.

Any multiset of at least `2 * n - 1` elements of `â„¤` contains a submultiset of `n` elements whose
sum is divisible by `n`. -/
theorem ZMod.erdos_ginzburg_ziv_multiset (s : Multiset (ZMod n))
    (hs : 2 * n - 1 â‰¤ Multiset.card s) : âˆƒ t â‰¤ s, Multiset.card t = n âˆ§ t.sum = 0 := by
  obtain âŸ¨t, hts, htâŸ© := ZMod.erdos_ginzburg_ziv (s := s.toEnumFinset) Prod.fst (by simpa using hs)
  exact âŸ¨t.1.map Prod.fst, Multiset.map_fst_le_of_subset_toEnumFinset hts, by simpa using htâŸ©

end composite
