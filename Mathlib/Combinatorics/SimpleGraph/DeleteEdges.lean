/-
Copyright (c) 2020 Aaron Anderson, Jalex Stark, Kyle Miller. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson, Jalex Stark, Kyle Miller, Alena Gusakov, Hunter Monroe
-/
import Mathlib.Algebra.Ring.Defs
import Mathlib.Combinatorics.SimpleGraph.Finite
import Mathlib.Combinatorics.SimpleGraph.Maps
import Mathlib.Data.Int.Cast.Basic

/-!
# Edge deletion

This file defines operations deleting the edges of a simple graph and proves theorems in the finite
case.

## Main definitions

* `SimpleGraph.deleteEdges G s` is the simple graph `G` with the edges `s : Set (Sym2 V)` removed
  from the edge set.

* `SimpleGraph.deleteIncidenceSet G v` is the simple graph `G` with the incidence set of `v`
  removed from the edge set.

* `SimpleGraph.deleteFar G p r` is the predicate that a graph is `r`-*delete-far* from a property
  `p`, that is, at least `r` edges must be deleted to satisfy `p`.
-/


open Finset Fintype

namespace SimpleGraph

variable {V : Type*} {v w : V} (G : SimpleGraph V)

section DeleteEdges

/-- Given a set of vertex pairs, remove all of the corresponding edges from the
graph's edge set, if present.

See also: `SimpleGraph.Subgraph.deleteEdges`. -/
def deleteEdges (s : Set (Sym2 V)) : SimpleGraph V := G \ fromEdgeSet s

variable {G} {H : SimpleGraph V} {s s‚ÇÅ s‚ÇÇ : Set (Sym2 V)}

instance [DecidableRel G.Adj] [DecidablePred (¬∑ ‚àà s)] [DecidableEq V] :
    DecidableRel (G.deleteEdges s).Adj :=
  inferInstanceAs <| DecidableRel (G \ fromEdgeSet s).Adj

@[simp] lemma deleteEdges_adj : (G.deleteEdges s).Adj v w ‚Üî G.Adj v w ‚àß s(v, w) ‚àâ s :=
  and_congr_right fun h ‚Ü¶ (and_iff_left h.ne).not

@[simp] lemma deleteEdges_edgeSet (G G' : SimpleGraph V) : G.deleteEdges G'.edgeSet = G \ G' := by
  ext; simp

@[simp]
theorem deleteEdges_deleteEdges (s s' : Set (Sym2 V)) :
    (G.deleteEdges s).deleteEdges s' = G.deleteEdges (s ‚à™ s') := by simp [deleteEdges, sdiff_sdiff]

@[simp] lemma deleteEdges_empty : G.deleteEdges ‚àÖ = G := by simp [deleteEdges]
@[simp] lemma deleteEdges_univ : G.deleteEdges Set.univ = ‚ä• := by simp [deleteEdges]

lemma deleteEdges_le (s : Set (Sym2 V)) : G.deleteEdges s ‚â§ G := sdiff_le

lemma deleteEdges_anti (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : G.deleteEdges s‚ÇÇ ‚â§ G.deleteEdges s‚ÇÅ :=
  sdiff_le_sdiff_left <| fromEdgeSet_mono h

lemma deleteEdges_mono (h : G ‚â§ H) : G.deleteEdges s ‚â§ H.deleteEdges s := sdiff_le_sdiff_right h

@[simp] lemma deleteEdges_eq_self : G.deleteEdges s = G ‚Üî Disjoint G.edgeSet s := by
  rw [deleteEdges, sdiff_eq_left, disjoint_fromEdgeSet]

theorem deleteEdges_eq_inter_edgeSet (s : Set (Sym2 V)) :
    G.deleteEdges s = G.deleteEdges (s ‚à© G.edgeSet) := by
  ext
  simp +contextual [imp_false]

theorem deleteEdges_sdiff_eq_of_le {H : SimpleGraph V} (h : H ‚â§ G) :
    G.deleteEdges (G.edgeSet \ H.edgeSet) = H := by
  rw [‚Üê edgeSet_sdiff, deleteEdges_edgeSet, sdiff_sdiff_eq_self h]

theorem edgeSet_deleteEdges (s : Set (Sym2 V)) : (G.deleteEdges s).edgeSet = G.edgeSet \ s := by
  simp [deleteEdges]

theorem edgeFinset_deleteEdges [DecidableEq V] [Fintype G.edgeSet] (s : Finset (Sym2 V))
    [Fintype (G.deleteEdges s).edgeSet] :
    (G.deleteEdges s).edgeFinset = G.edgeFinset \ s := by
  ext e
  simp [edgeSet_deleteEdges]

end DeleteEdges

section DeleteIncidenceSet

/-- Given a vertex `x`, remove the edges incident to `x` from the edge set. -/
def deleteIncidenceSet (G : SimpleGraph V) (x : V) : SimpleGraph V :=
  G.deleteEdges (G.incidenceSet x)

lemma deleteIncidenceSet_adj {G : SimpleGraph V} {x v‚ÇÅ v‚ÇÇ : V} :
    (G.deleteIncidenceSet x).Adj v‚ÇÅ v‚ÇÇ ‚Üî G.Adj v‚ÇÅ v‚ÇÇ ‚àß v‚ÇÅ ‚â† x ‚àß v‚ÇÇ ‚â† x := by
  rw [deleteIncidenceSet, deleteEdges_adj, mk'_mem_incidenceSet_iff]
  tauto

lemma deleteIncidenceSet_le (G : SimpleGraph V) (x : V) : G.deleteIncidenceSet x ‚â§ G :=
  deleteEdges_le (G.incidenceSet x)

lemma edgeSet_fromEdgeSet_incidenceSet (G : SimpleGraph V) (x : V) :
    (fromEdgeSet (G.incidenceSet x)).edgeSet = G.incidenceSet x := by
  rw [edgeSet_fromEdgeSet, sdiff_eq_left, ‚Üê Set.subset_compl_iff_disjoint_right, Set.compl_setOf]
  exact (incidenceSet_subset G x).trans G.edgeSet_subset_setOf_not_isDiag

/-- The edge set of `G.deleteIncidenceSet x` is the edge set of `G` set difference the incidence
set of the vertex `x`. -/
theorem edgeSet_deleteIncidenceSet (G : SimpleGraph V) (x : V) :
    (G.deleteIncidenceSet x).edgeSet = G.edgeSet \ G.incidenceSet x := by
  simp_rw [deleteIncidenceSet, deleteEdges, edgeSet_sdiff, edgeSet_fromEdgeSet_incidenceSet]

/-- The support of `G.deleteIncidenceSet x` is a subset of the support of `G` set difference the
singleton set `{x}`. -/
theorem support_deleteIncidenceSet_subset (G : SimpleGraph V) (x : V) :
    (G.deleteIncidenceSet x).support ‚äÜ G.support \ {x} :=
  fun _ ‚Ü¶ by simp_rw [mem_support, deleteIncidenceSet_adj]; tauto

/-- If the vertex `x` is not in the set `s`, then the induced subgraph in `G.deleteIncidenceSet x`
by `s` is equal to the induced subgraph in `G` by `s`. -/
theorem induce_deleteIncidenceSet_of_notMem (G : SimpleGraph V) {s : Set V} {x : V} (h : x ‚àâ s) :
    (G.deleteIncidenceSet x).induce s = G.induce s := by
  ext v‚ÇÅ v‚ÇÇ
  simp_rw [comap_adj, Function.Embedding.coe_subtype, deleteIncidenceSet_adj, and_iff_left_iff_imp]
  exact fun _ ‚Ü¶ ‚ü®v‚ÇÅ.prop.ne_of_notMem h, v‚ÇÇ.prop.ne_of_notMem h‚ü©

@[deprecated (since := "2025-05-23")]
alias induce_deleteIncidenceSet_of_not_mem := induce_deleteIncidenceSet_of_notMem

variable [Fintype V] [DecidableEq V]

instance {G : SimpleGraph V} [DecidableRel G.Adj] {x : V} :
    DecidableRel (G.deleteIncidenceSet x).Adj :=
  inferInstanceAs <| DecidableRel (G.deleteEdges (G.incidenceSet x)).Adj

/-- Deleting the incidence set of the vertex `x` retains the same number of edges as in the induced
subgraph of the vertices `{x}·∂ú`. -/
theorem card_edgeFinset_induce_compl_singleton (G : SimpleGraph V) [DecidableRel G.Adj] (x : V) :
    #(G.induce {x}·∂ú).edgeFinset = #(G.deleteIncidenceSet x).edgeFinset := by
  have h_notMem : x ‚àâ ({x}·∂ú : Set V) := Set.notMem_compl_iff.mpr (Set.mem_singleton x)
  simp_rw [Set.toFinset_card,
    ‚Üê G.induce_deleteIncidenceSet_of_notMem h_notMem, ‚Üê Set.toFinset_card]
  apply card_edgeFinset_induce_of_support_subset
  trans G.support \ {x}
  ¬∑ exact support_deleteIncidenceSet_subset G x
  ¬∑ rw [Set.compl_eq_univ_diff]
    exact Set.diff_subset_diff_left (Set.subset_univ G.support)

/-- The finite edge set of `G.deleteIncidenceSet x` is the finite edge set of the simple graph `G`
set difference the finite incidence set of the vertex `x`. -/
theorem edgeFinset_deleteIncidenceSet_eq_sdiff (G : SimpleGraph V) [DecidableRel G.Adj] (x : V) :
    (G.deleteIncidenceSet x).edgeFinset = G.edgeFinset \ G.incidenceFinset x := by
  rw [incidenceFinset, ‚Üê Set.toFinset_diff, Set.toFinset_inj]
  exact G.edgeSet_deleteIncidenceSet x

/-- Deleting the incident set of the vertex `x` deletes exactly `G.degree x` edges from the edge
set of the simple graph `G`. -/
theorem card_edgeFinset_deleteIncidenceSet (G : SimpleGraph V) [DecidableRel G.Adj] (x : V) :
    #(G.deleteIncidenceSet x).edgeFinset = #G.edgeFinset - G.degree x := by
  simp_rw [‚Üê card_incidenceFinset_eq_degree, ‚Üê card_sdiff_of_subset (G.incidenceFinset_subset x),
    edgeFinset_deleteIncidenceSet_eq_sdiff]

/-- Deleting the incident set of the vertex `x` is equivalent to filtering the edges of the simple
graph `G` that do not contain `x`. -/
theorem edgeFinset_deleteIncidenceSet_eq_filter (G : SimpleGraph V) [DecidableRel G.Adj] (x : V) :
    (G.deleteIncidenceSet x).edgeFinset = G.edgeFinset.filter (x ‚àâ ¬∑) := by
  rw [edgeFinset_deleteIncidenceSet_eq_sdiff, sdiff_eq_filter]
  apply filter_congr
  intro _ h
  rw [incidenceFinset, Set.mem_toFinset, incidenceSet,
    Set.mem_setOf_eq, not_and, Classical.imp_iff_right_iff]
  left
  rwa [mem_edgeFinset] at h

/-- The support of `G.deleteIncidenceSet x` is at most `1` less than the support of the simple
graph `G`. -/
theorem card_support_deleteIncidenceSet
    (G : SimpleGraph V) [DecidableRel G.Adj] {x : V} (hx : x ‚àà G.support) :
    card (G.deleteIncidenceSet x).support ‚â§ card G.support - 1 := by
  rw [‚Üê Set.singleton_subset_iff, ‚Üê Set.toFinset_subset_toFinset] at hx
  simp_rw [‚Üê Set.card_singleton x, ‚Üê Set.toFinset_card, ‚Üê card_sdiff_of_subset hx,
    ‚Üê Set.toFinset_diff]
  apply card_le_card
  rw [Set.toFinset_subset_toFinset]
  exact G.support_deleteIncidenceSet_subset x

end DeleteIncidenceSet

section DeleteFar

variable {ùïú : Type*} [Ring ùïú] [PartialOrder ùïú]
  [Fintype G.edgeSet] {p : SimpleGraph V ‚Üí Prop} {r r‚ÇÅ r‚ÇÇ : ùïú}

/-- A graph is `r`-*delete-far* from a property `p` if we must delete at least `r` edges from it to
get a graph with the property `p`. -/
def DeleteFar (p : SimpleGraph V ‚Üí Prop) (r : ùïú) : Prop :=
  ‚àÄ ‚¶És‚¶Ñ, s ‚äÜ G.edgeFinset ‚Üí p (G.deleteEdges s) ‚Üí r ‚â§ #s

variable {G}

theorem deleteFar_iff [Fintype (Sym2 V)] :
    G.DeleteFar p r ‚Üî ‚àÄ ‚¶ÉH : SimpleGraph _‚¶Ñ [DecidableRel H.Adj],
      H ‚â§ G ‚Üí p H ‚Üí r ‚â§ #G.edgeFinset - #H.edgeFinset := by
  classical
  refine ‚ü®fun h H _ hHG hH ‚Ü¶ ?_, fun h s hs hG ‚Ü¶ ?_‚ü©
  ¬∑ have := h (sdiff_subset (t := H.edgeFinset))
    simp only [deleteEdges_sdiff_eq_of_le hHG, edgeFinset_mono hHG, card_sdiff_of_subset,
      card_le_card, coe_sdiff, coe_edgeFinset, Nat.cast_sub] at this
    exact this hH
  ¬∑ classical
    simpa [card_sdiff_of_subset hs, edgeFinset_deleteEdges, -Set.toFinset_card, Nat.cast_sub,
      card_le_card hs] using h (G.deleteEdges_le s) hG

alias ‚ü®DeleteFar.le_card_sub_card, _‚ü© := deleteFar_iff

theorem DeleteFar.mono (h : G.DeleteFar p r‚ÇÇ) (hr : r‚ÇÅ ‚â§ r‚ÇÇ) : G.DeleteFar p r‚ÇÅ := fun _ hs hG =>
  hr.trans <| h hs hG

end DeleteFar

end SimpleGraph
