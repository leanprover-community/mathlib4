/-
Copyright (c) 2026 Julian Berman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julian Berman, Aaron Hill
-/
module

public import Mathlib.Analysis.RCLike.Lemmas
public import Mathlib.Combinatorics.SimpleGraph.Bipartite
public import Mathlib.Combinatorics.SimpleGraph.CompleteMultipartite
public import Mathlib.Combinatorics.SimpleGraph.EdgeLabeling
public import Mathlib.Data.Matrix.ColumnRowPartitioned

/-!

# The Graham-Pollak theorem

-/

@[expose] public section

namespace SimpleGraph.completeGraph

variable {V Œ± : Type*} [Fintype V] {G : SimpleGraph V} [Fintype Œ±] {ùìÅ : TopEdgeLabeling V Œ±}

open Finset Fintype LinearMap in
open scoped Matrix in
/--
The Graham-Pollak theorem:

In a complete graph on `|V|` vertices, any edge labeling into complete bipartite subgraphs uses
at least `|V| - 1` distinct labels.
-/
theorem card_le_card_add_one_of_forall_IsCompleteBipartite
  (completeBipartiteOf : ‚àÄ a, IsCompleteBipartite <| ùìÅ.labelGraph a) :
    card V ‚â§ card Œ± + 1 := by
  classical
  rcases subsingleton_or_nontrivial V
  ¬∑ grind [card_le_one_iff_subsingleton]
  ¬∑ by_contra! h
    let M : Matrix (Fin 1 ‚äï Œ±) V ‚Ñù := Matrix.fromRows
      (Matrix.replicateCol V ![1])
      (Matrix.of fun m n ‚Ü¶ (completeBipartiteOf m).choose.indicator 1 n)
    obtain ‚ü®c, hc, hc_nezero‚ü© : ‚àÉ x ‚àà ker M.toLin', x ‚â† 0 := (ker _).ne_bot_iff.mp <| by
      apply ker_ne_bot_of_finrank_lt
      simp only [Module.finrank_fintype_fun_eq_card, card_sum, card_unique]
      grind
    have h_sum : ‚àë v, c v = 0 := by
      have : (M *·µ• c) (.inl 0) = 0 := by simp_all
      simp only [M, Matrix.mulVec, dotProduct] at this
      aesop
    have h_disjoint (u : V) :
        ((univ : Finset Œ±) : Set Œ±).PairwiseDisjoint (ùìÅ.labelGraph ¬∑ |>.neighborFinset u) := by
      intro
      grind [Finset.disjoint_left, mem_neighborFinset, ùìÅ.labelGraph_adj]
    have h_partition (u : V) :
        univ.erase u = (univ : Finset Œ±).biUnion (ùìÅ.labelGraph ¬∑ |>.neighborFinset u) := by
      ext v
      simp only [mem_erase, Finset.mem_univ, Finset.mem_biUnion, mem_neighborFinset]
      exact ‚ü®fun _ ‚Ü¶ ‚ü®ùìÅ ‚ü®s(u, v), by tauto‚ü©, by tauto‚ü©,
            by grind only [TopEdgeLabeling.labelGraph_adj]‚ü©
    have h_sq : ‚àë v, c v ^ 2 = -‚àë u, ‚àë v ‚àà univ.erase u, c u * c v :=
      by simp [‚Üê mul_sum, sq, h_sum]
    have h_zero : ‚àë v, c v ^ 2 = 0 := by
      rw [h_sq,
          Finset.sum_congr rfl fun u _ ‚Ü¶ by rw [h_partition, sum_biUnion <| h_disjoint _],
          neg_eq_zero,
          sum_comm]
      refine sum_eq_zero fun v _ ‚Ü¶ ?_
      let cbp := completeBipartiteOf v
      have h_left : ‚àë u ‚àà (completeBipartiteOf v).choose, c u = 0 := by
        suffices ‚àë x, ((completeBipartiteOf v).choose.toFinset : Set _).indicator c x = 0 by
          rwa [sum_indicator_subset _ (by simp)] at this
        have : (M *·µ• c) (.inr v) = 0 := by simp_all
        simp only [M, Matrix.mulVec, dotProduct, Set.indicator_apply] at this
        aesop
      let sum_eq (S : Finset V) := ‚àë x ‚àà S, ‚àë i ‚àà ùìÅ.labelGraph v |>.neighborFinset x, c x * c i = 0
      have h_L_sum : sum_eq cbp.choose.toFinset := by
        dsimp [sum_eq]
        rw [Finset.sum_congr rfl fun _ hv ‚Ü¶ by
          rw [cbp.choose_spec.neighborFinset_eq_of_mem_left hv, ‚Üê mul_sum],
            ‚Üê sum_mul, h_left, zero_mul]
      have h_R_sum : sum_eq cbp.choose·∂ú.toFinset := by
        dsimp [sum_eq]
        rw [Finset.sum_congr rfl fun _ hx ‚Ü¶ by
          rw [cbp.choose_spec.neighborFinset_eq_of_not_mem_left (by grind), ‚Üê mul_sum],
            h_left]
        simp only [mul_zero, sum_const_zero]
      rw [show univ = cbp.choose.toFinset ‚à™ cbp.choose·∂ú.toFinset by simp,
          sum_union <| Finset.disjoint_left.mpr fun v hvL hvR ‚Ü¶
            Set.disjoint_left.mp cbp.choose_spec.bipartite.disjoint
              (Set.mem_toFinset.mp hvL) (Set.mem_toFinset.mp hvR),
          h_L_sum,
          h_R_sum,
          add_zero]
    simp only [Finset.sum_eq_zero_iff_of_nonneg (fun _ _ ‚Ü¶ sq_nonneg _), sq_eq_zero_iff] at h_zero
    bound

end SimpleGraph.completeGraph
