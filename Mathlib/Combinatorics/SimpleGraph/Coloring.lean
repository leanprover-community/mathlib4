/-
Copyright (c) 2021 Arthur Paulino. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Arthur Paulino, Kyle Miller
-/
import Mathlib.Combinatorics.SimpleGraph.Clique
import Mathlib.Data.Nat.Lattice
import Mathlib.Data.Setoid.Partition
import Mathlib.Order.Antichain

#align_import combinatorics.simple_graph.coloring from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Graph Coloring

This module defines colorings of simple graphs (also known as proper
colorings in the literature). A graph coloring is the attribution of
"colors" to all of its vertices such that adjacent vertices have
different colors. A coloring can be represented as a homomorphism into
a complete graph, whose vertices represent the colors.

## Main definitions

* `G.Coloring Î±` is the type of `Î±`-colorings of a simple graph `G`,
  with `Î±` being the set of available colors. The type is defined to
  be homomorphisms from `G` into the complete graph on `Î±`, and
  colorings have a coercion to `V â†’ Î±`.

* `G.Colorable n` is the proposition that `G` is `n`-colorable, which
  is whether there exists a coloring with at most *n* colors.

* `G.chromaticNumber` is the minimal `n` such that `G` is
  `n`-colorable, or `0` if it cannot be colored with finitely many
  colors.

* `C.colorClass c` is the set of vertices colored by `c : Î±` in the
  coloring `C : G.Coloring Î±`.

* `C.colorClasses` is the set containing all color classes.

## Todo:

  * Gather material from:
    * https://github.com/leanprover-community/mathlib/blob/simple_graph_matching/src/combinatorics/simple_graph/coloring.lean
    * https://github.com/kmill/lean-graphcoloring/blob/master/src/graph.lean

  * Trees

  * Planar graphs

  * Chromatic polynomials

  * develop API for partial colorings, likely as colorings of subgraphs (`H.coe.Coloring Î±`)
-/


universe u v

namespace SimpleGraph

variable {V : Type u} (G : SimpleGraph V)

/-- An `Î±`-coloring of a simple graph `G` is a homomorphism of `G` into the complete graph on `Î±`.
This is also known as a proper coloring.
-/
abbrev Coloring (Î± : Type v) := G â†’g (âŠ¤ : SimpleGraph Î±)
#align simple_graph.coloring SimpleGraph.Coloring

variable {G} {Î± : Type v} (C : G.Coloring Î±)

theorem Coloring.valid {v w : V} (h : G.Adj v w) : C v â‰  C w :=
  C.map_rel h
#align simple_graph.coloring.valid SimpleGraph.Coloring.valid

/-- Construct a term of `SimpleGraph.Coloring` using a function that
assigns vertices to colors and a proof that it is as proper coloring.

(Note: this is a definitionally the constructor for `SimpleGraph.Hom`,
but with a syntactically better proper coloring hypothesis.)
-/
@[match_pattern]
def Coloring.mk (color : V â†’ Î±) (valid : âˆ€ {v w : V}, G.Adj v w â†’ color v â‰  color w) :
    G.Coloring Î± :=
  âŸ¨color, @validâŸ©
#align simple_graph.coloring.mk SimpleGraph.Coloring.mk

/-- The color class of a given color.
-/
def Coloring.colorClass (c : Î±) : Set V := { v : V | C v = c }
#align simple_graph.coloring.color_class SimpleGraph.Coloring.colorClass

/-- The set containing all color classes. -/
def Coloring.colorClasses : Set (Set V) := (Setoid.ker C).classes
#align simple_graph.coloring.color_classes SimpleGraph.Coloring.colorClasses

theorem Coloring.mem_colorClass (v : V) : v âˆˆ C.colorClass (C v) := rfl
#align simple_graph.coloring.mem_color_class SimpleGraph.Coloring.mem_colorClass

theorem Coloring.colorClasses_isPartition : Setoid.IsPartition C.colorClasses :=
  Setoid.isPartition_classes (Setoid.ker C)
#align simple_graph.coloring.color_classes_is_partition SimpleGraph.Coloring.colorClasses_isPartition

theorem Coloring.mem_colorClasses {v : V} : C.colorClass (C v) âˆˆ C.colorClasses :=
  âŸ¨v, rflâŸ©
#align simple_graph.coloring.mem_color_classes SimpleGraph.Coloring.mem_colorClasses

theorem Coloring.colorClasses_finite [Finite Î±] : C.colorClasses.Finite :=
  Setoid.finite_classes_ker _
#align simple_graph.coloring.color_classes_finite SimpleGraph.Coloring.colorClasses_finite

theorem Coloring.card_colorClasses_le [Fintype Î±] [Fintype C.colorClasses] :
    Fintype.card C.colorClasses â‰¤ Fintype.card Î± := by
  simp [colorClasses]
  -- âŠ¢ Fintype.card â†‘(Setoid.classes (Setoid.ker â†‘C)) â‰¤ Fintype.card Î±
  -- porting note: brute force instance declaration `[Fintype (Setoid.classes (Setoid.ker C))]`
  haveI : Fintype (Setoid.classes (Setoid.ker C)) := by assumption
  -- âŠ¢ Fintype.card â†‘(Setoid.classes (Setoid.ker â†‘C)) â‰¤ Fintype.card Î±
  convert Setoid.card_classes_ker_le C
  -- ğŸ‰ no goals
#align simple_graph.coloring.card_color_classes_le SimpleGraph.Coloring.card_colorClasses_le

theorem Coloring.not_adj_of_mem_colorClass {c : Î±} {v w : V} (hv : v âˆˆ C.colorClass c)
    (hw : w âˆˆ C.colorClass c) : Â¬G.Adj v w := fun h => C.valid h (Eq.trans hv (Eq.symm hw))
#align simple_graph.coloring.not_adj_of_mem_color_class SimpleGraph.Coloring.not_adj_of_mem_colorClass

theorem Coloring.color_classes_independent (c : Î±) : IsAntichain G.Adj (C.colorClass c) :=
  fun _ hv _ hw _ => C.not_adj_of_mem_colorClass hv hw
#align simple_graph.coloring.color_classes_independent SimpleGraph.Coloring.color_classes_independent

-- TODO make this computable
noncomputable instance [Fintype V] [Fintype Î±] : Fintype (Coloring G Î±) := by
  classical
  change Fintype (RelHom G.Adj (âŠ¤ : SimpleGraph Î±).Adj)
  apply Fintype.ofInjective _ RelHom.coe_fn_injective

variable (G)

/-- Whether a graph can be colored by at most `n` colors. -/
def Colorable (n : â„•) : Prop := Nonempty (G.Coloring (Fin n))
#align simple_graph.colorable SimpleGraph.Colorable

/-- The coloring of an empty graph. -/
def coloringOfIsEmpty [IsEmpty V] : G.Coloring Î± :=
  Coloring.mk isEmptyElim fun {v} => isEmptyElim v
#align simple_graph.coloring_of_is_empty SimpleGraph.coloringOfIsEmpty

theorem colorable_of_isEmpty [IsEmpty V] (n : â„•) : G.Colorable n :=
  âŸ¨G.coloringOfIsEmptyâŸ©
#align simple_graph.colorable_of_is_empty SimpleGraph.colorable_of_isEmpty

theorem isEmpty_of_colorable_zero (h : G.Colorable 0) : IsEmpty V := by
  constructor
  -- âŠ¢ V â†’ False
  intro v
  -- âŠ¢ False
  obtain âŸ¨i, hiâŸ© := h.some v
  -- âŠ¢ False
  exact Nat.not_lt_zero _ hi
  -- ğŸ‰ no goals
#align simple_graph.is_empty_of_colorable_zero SimpleGraph.isEmpty_of_colorable_zero

/-- The "tautological" coloring of a graph, using the vertices of the graph as colors. -/
def selfColoring : G.Coloring V := Coloring.mk id fun {_ _} => G.ne_of_adj
#align simple_graph.self_coloring SimpleGraph.selfColoring

/-- The chromatic number of a graph is the minimal number of colors needed to color it.
If `G` isn't colorable with finitely many colors, this will be 0. -/
noncomputable def chromaticNumber : â„• :=
  sInf { n : â„• | G.Colorable n }
#align simple_graph.chromatic_number SimpleGraph.chromaticNumber

/-- Given an embedding, there is an induced embedding of colorings. -/
def recolorOfEmbedding {Î± Î² : Type*} (f : Î± â†ª Î²) : G.Coloring Î± â†ª G.Coloring Î² where
  toFun C := (Embedding.completeGraph f).toHom.comp C
  inj' := by -- this was strangely painful; seems like missing lemmas about embeddings
    intro C C' h
    -- âŠ¢ C = C'
    dsimp only at h
    -- âŠ¢ C = C'
    ext v
    -- âŠ¢ â†‘C v = â†‘C' v
    apply (Embedding.completeGraph f).inj'
    -- âŠ¢ Function.Embedding.toFun (Embedding.completeGraph f).toEmbedding (â†‘C v) = Fu â€¦
    change ((Embedding.completeGraph f).toHom.comp C) v = _
    -- âŠ¢ â†‘(Hom.comp (Embedding.toHom (Embedding.completeGraph f)) C) v = Function.Emb â€¦
    rw [h]
    -- âŠ¢ â†‘(Hom.comp (Embedding.toHom (Embedding.completeGraph f)) C') v = Function.Em â€¦
    rfl
    -- ğŸ‰ no goals
#align simple_graph.recolor_of_embedding SimpleGraph.recolorOfEmbedding

/-- Given an equivalence, there is an induced equivalence between colorings. -/
def recolorOfEquiv {Î± Î² : Type*} (f : Î± â‰ƒ Î²) : G.Coloring Î± â‰ƒ G.Coloring Î² where
  toFun := G.recolorOfEmbedding f.toEmbedding
  invFun := G.recolorOfEmbedding f.symm.toEmbedding
  left_inv C := by
    ext v
    -- âŠ¢ â†‘(â†‘(recolorOfEmbedding G (Equiv.toEmbedding f.symm)) (â†‘(recolorOfEmbedding G â€¦
    apply Equiv.symm_apply_apply
    -- ğŸ‰ no goals
  right_inv C := by
    ext v
    -- âŠ¢ â†‘(â†‘(recolorOfEmbedding G (Equiv.toEmbedding f)) (â†‘(recolorOfEmbedding G (Equ â€¦
    apply Equiv.apply_symm_apply
    -- ğŸ‰ no goals
#align simple_graph.recolor_of_equiv SimpleGraph.recolorOfEquiv

/-- There is a noncomputable embedding of `Î±`-colorings to `Î²`-colorings if
`Î²` has at least as large a cardinality as `Î±`. -/
noncomputable def recolorOfCardLE {Î± Î² : Type*} [Fintype Î±] [Fintype Î²]
    (hn : Fintype.card Î± â‰¤ Fintype.card Î²) : G.Coloring Î± â†ª G.Coloring Î² :=
  G.recolorOfEmbedding <| (Function.Embedding.nonempty_of_card_le hn).some
#align simple_graph.recolor_of_card_le SimpleGraph.recolorOfCardLE

variable {G}

theorem Colorable.mono {n m : â„•} (h : n â‰¤ m) (hc : G.Colorable n) : G.Colorable m :=
  âŸ¨G.recolorOfCardLE (by simp [h]) hc.someâŸ©
                         -- ğŸ‰ no goals
#align simple_graph.colorable.mono SimpleGraph.Colorable.mono

theorem Coloring.to_colorable [Fintype Î±] (C : G.Coloring Î±) : G.Colorable (Fintype.card Î±) :=
  âŸ¨G.recolorOfCardLE (by simp) CâŸ©
                         -- ğŸ‰ no goals
#align simple_graph.coloring.to_colorable SimpleGraph.Coloring.to_colorable

theorem colorable_of_fintype (G : SimpleGraph V) [Fintype V] : G.Colorable (Fintype.card V) :=
  G.selfColoring.to_colorable
#align simple_graph.colorable_of_fintype SimpleGraph.colorable_of_fintype

/-- Noncomputably get a coloring from colorability. -/
noncomputable def Colorable.toColoring [Fintype Î±] {n : â„•} (hc : G.Colorable n)
    (hn : n â‰¤ Fintype.card Î±) : G.Coloring Î± := by
  rw [â† Fintype.card_fin n] at hn
  -- âŠ¢ Coloring G Î±
  exact G.recolorOfCardLE hn hc.some
  -- ğŸ‰ no goals
#align simple_graph.colorable.to_coloring SimpleGraph.Colorable.toColoring

theorem Colorable.of_embedding {V' : Type*} {G' : SimpleGraph V'} (f : G â†ªg G') {n : â„•}
    (h : G'.Colorable n) : G.Colorable n :=
  âŸ¨(h.toColoring (by simp)).comp fâŸ©
                     -- ğŸ‰ no goals
#align simple_graph.colorable.of_embedding SimpleGraph.Colorable.of_embedding

theorem colorable_iff_exists_bdd_nat_coloring (n : â„•) :
    G.Colorable n â†” âˆƒ C : G.Coloring â„•, âˆ€ v, C v < n := by
  constructor
  -- âŠ¢ Colorable G n â†’ âˆƒ C, âˆ€ (v : V), â†‘C v < n
  Â· rintro hc
    -- âŠ¢ âˆƒ C, âˆ€ (v : V), â†‘C v < n
    have C : G.Coloring (Fin n) := hc.toColoring (by simp)
    -- âŠ¢ âˆƒ C, âˆ€ (v : V), â†‘C v < n
    let f := Embedding.completeGraph (@Fin.valEmbedding n)
    -- âŠ¢ âˆƒ C, âˆ€ (v : V), â†‘C v < n
    use f.toHom.comp C
    -- âŠ¢ âˆ€ (v : V), â†‘(Hom.comp (Embedding.toHom f) C) v < n
    intro v
    -- âŠ¢ â†‘(Hom.comp (Embedding.toHom f) C) v < n
    cases' C with color valid
    -- âŠ¢ â†‘(Hom.comp (Embedding.toHom f) { toFun := color, map_rel' := valid }) v < n
    exact Fin.is_lt (color v)
    -- ğŸ‰ no goals
  Â· rintro âŸ¨C, CfâŸ©
    -- âŠ¢ Colorable G n
    refine' âŸ¨Coloring.mk _ _âŸ©
    -- âŠ¢ V â†’ Fin n
    Â· exact fun v => âŸ¨C v, Cf vâŸ©
      -- ğŸ‰ no goals
    Â· rintro v w hvw
      -- âŠ¢ { val := â†‘C v, isLt := (_ : â†‘C v < n) } â‰  { val := â†‘C w, isLt := (_ : â†‘C w < â€¦
      simp only [Fin.mk_eq_mk, Ne.def]
      -- âŠ¢ Â¬â†‘C v = â†‘C w
      exact C.valid hvw
      -- ğŸ‰ no goals
#align simple_graph.colorable_iff_exists_bdd_nat_coloring SimpleGraph.colorable_iff_exists_bdd_nat_coloring

theorem colorable_set_nonempty_of_colorable {n : â„•} (hc : G.Colorable n) :
    { n : â„• | G.Colorable n }.Nonempty :=
  âŸ¨n, hcâŸ©
#align simple_graph.colorable_set_nonempty_of_colorable SimpleGraph.colorable_set_nonempty_of_colorable

theorem chromaticNumber_bddBelow : BddBelow { n : â„• | G.Colorable n } :=
  âŸ¨0, fun _ _ => zero_le _âŸ©
#align simple_graph.chromatic_number_bdd_below SimpleGraph.chromaticNumber_bddBelow

theorem chromaticNumber_le_of_colorable {n : â„•} (hc : G.Colorable n) : G.chromaticNumber â‰¤ n := by
  rw [chromaticNumber]
  -- âŠ¢ sInf {n | Colorable G n} â‰¤ n
  apply csInf_le chromaticNumber_bddBelow
  -- âŠ¢ n âˆˆ {n | Colorable G n}
  constructor
  -- âŠ¢ Coloring G (Fin n)
  exact Classical.choice hc
  -- ğŸ‰ no goals
#align simple_graph.chromatic_number_le_of_colorable SimpleGraph.chromaticNumber_le_of_colorable

theorem chromaticNumber_le_card [Fintype Î±] (C : G.Coloring Î±) :
    G.chromaticNumber â‰¤ Fintype.card Î± :=
  csInf_le chromaticNumber_bddBelow C.to_colorable
#align simple_graph.chromatic_number_le_card SimpleGraph.chromaticNumber_le_card

theorem colorable_chromaticNumber {m : â„•} (hc : G.Colorable m) : G.Colorable G.chromaticNumber := by
  classical
  dsimp only [chromaticNumber]
  rw [Nat.sInf_def]
  apply Nat.find_spec
  exact colorable_set_nonempty_of_colorable hc
#align simple_graph.colorable_chromatic_number SimpleGraph.colorable_chromaticNumber

theorem colorable_chromaticNumber_of_fintype (G : SimpleGraph V) [Finite V] :
    G.Colorable G.chromaticNumber := by
  cases nonempty_fintype V
  -- âŠ¢ Colorable G (chromaticNumber G)
  exact colorable_chromaticNumber G.colorable_of_fintype
  -- ğŸ‰ no goals
#align simple_graph.colorable_chromatic_number_of_fintype SimpleGraph.colorable_chromaticNumber_of_fintype

theorem chromaticNumber_le_one_of_subsingleton (G : SimpleGraph V) [Subsingleton V] :
    G.chromaticNumber â‰¤ 1 := by
  rw [chromaticNumber]
  -- âŠ¢ sInf {n | Colorable G n} â‰¤ 1
  apply csInf_le chromaticNumber_bddBelow
  -- âŠ¢ 1 âˆˆ {n | Colorable G n}
  constructor
  -- âŠ¢ Coloring G (Fin 1)
  refine' Coloring.mk (fun _ => 0) _
  -- âŠ¢ âˆ€ {v w : V}, Adj G v w â†’ (fun x => 0) v â‰  (fun x => 0) w
  intro v w
  -- âŠ¢ Adj G v w â†’ (fun x => 0) v â‰  (fun x => 0) w
  rw [Subsingleton.elim v w]
  -- âŠ¢ Adj G w w â†’ (fun x => 0) w â‰  (fun x => 0) w
  simp
  -- ğŸ‰ no goals
#align simple_graph.chromatic_number_le_one_of_subsingleton SimpleGraph.chromaticNumber_le_one_of_subsingleton

theorem chromaticNumber_eq_zero_of_isempty (G : SimpleGraph V) [IsEmpty V] :
    G.chromaticNumber = 0 := by
  rw [â† nonpos_iff_eq_zero]
  -- âŠ¢ chromaticNumber G â‰¤ 0
  apply csInf_le chromaticNumber_bddBelow
  -- âŠ¢ 0 âˆˆ {n | Colorable G n}
  apply colorable_of_isEmpty
  -- ğŸ‰ no goals
#align simple_graph.chromatic_number_eq_zero_of_isempty SimpleGraph.chromaticNumber_eq_zero_of_isempty

theorem isEmpty_of_chromaticNumber_eq_zero (G : SimpleGraph V) [Finite V]
    (h : G.chromaticNumber = 0) : IsEmpty V := by
  have h' := G.colorable_chromaticNumber_of_fintype
  -- âŠ¢ IsEmpty V
  rw [h] at h'
  -- âŠ¢ IsEmpty V
  exact G.isEmpty_of_colorable_zero h'
  -- ğŸ‰ no goals
#align simple_graph.is_empty_of_chromatic_number_eq_zero SimpleGraph.isEmpty_of_chromaticNumber_eq_zero

theorem chromaticNumber_pos [Nonempty V] {n : â„•} (hc : G.Colorable n) : 0 < G.chromaticNumber := by
  apply le_csInf (colorable_set_nonempty_of_colorable hc)
  -- âŠ¢ âˆ€ (b : â„•), b âˆˆ {n | Colorable G n} â†’ Nat.succ 0 â‰¤ b
  intro m hm
  -- âŠ¢ Nat.succ 0 â‰¤ m
  by_contra h'
  -- âŠ¢ False
  simp only [not_le] at h'
  -- âŠ¢ False
  obtain âŸ¨i, hiâŸ© := hm.some (Classical.arbitrary V)
  -- âŠ¢ False
  have hâ‚: i < 0 := lt_of_lt_of_le hi (Nat.le_of_lt_succ h')
  -- âŠ¢ False
  exact Nat.not_lt_zero _ hâ‚
  -- ğŸ‰ no goals
#align simple_graph.chromatic_number_pos SimpleGraph.chromaticNumber_pos

theorem colorable_of_chromaticNumber_pos (h : 0 < G.chromaticNumber) :
    G.Colorable G.chromaticNumber := by
  obtain âŸ¨h, hnâŸ© := Nat.nonempty_of_pos_sInf h
  -- âŠ¢ Colorable G (chromaticNumber G)
  exact colorable_chromaticNumber hn
  -- ğŸ‰ no goals
#align simple_graph.colorable_of_chromatic_number_pos SimpleGraph.colorable_of_chromaticNumber_pos

theorem Colorable.mono_left {G' : SimpleGraph V} (h : G â‰¤ G') {n : â„•} (hc : G'.Colorable n) :
    G.Colorable n :=
  âŸ¨hc.some.comp (Hom.mapSpanningSubgraphs h)âŸ©
#align simple_graph.colorable.mono_left SimpleGraph.Colorable.mono_left

theorem Colorable.chromaticNumber_le_of_forall_imp {V' : Type*} {G' : SimpleGraph V'} {m : â„•}
    (hc : G'.Colorable m) (h : âˆ€ n, G'.Colorable n â†’ G.Colorable n) :
    G.chromaticNumber â‰¤ G'.chromaticNumber := by
  apply csInf_le chromaticNumber_bddBelow
  -- âŠ¢ chromaticNumber G' âˆˆ {n | Colorable G n}
  apply h
  -- âŠ¢ Colorable G' (chromaticNumber G')
  apply colorable_chromaticNumber hc
  -- ğŸ‰ no goals
#align simple_graph.colorable.chromatic_number_le_of_forall_imp SimpleGraph.Colorable.chromaticNumber_le_of_forall_imp

theorem Colorable.chromaticNumber_mono (G' : SimpleGraph V) {m : â„•} (hc : G'.Colorable m)
    (h : G â‰¤ G') : G.chromaticNumber â‰¤ G'.chromaticNumber :=
  hc.chromaticNumber_le_of_forall_imp fun _ => Colorable.mono_left h
#align simple_graph.colorable.chromatic_number_mono SimpleGraph.Colorable.chromaticNumber_mono

theorem Colorable.chromaticNumber_mono_of_embedding {V' : Type*} {G' : SimpleGraph V'} {n : â„•}
    (h : G'.Colorable n) (f : G â†ªg G') : G.chromaticNumber â‰¤ G'.chromaticNumber :=
  h.chromaticNumber_le_of_forall_imp fun _ => Colorable.of_embedding f
#align simple_graph.colorable.chromatic_number_mono_of_embedding SimpleGraph.Colorable.chromaticNumber_mono_of_embedding

theorem chromaticNumber_eq_card_of_forall_surj [Fintype Î±] (C : G.Coloring Î±)
    (h : âˆ€ C' : G.Coloring Î±, Function.Surjective C') : G.chromaticNumber = Fintype.card Î± := by
  apply le_antisymm
  -- âŠ¢ chromaticNumber G â‰¤ Fintype.card Î±
  Â· apply chromaticNumber_le_card C
    -- ğŸ‰ no goals
  Â· by_contra hc
    -- âŠ¢ False
    rw [not_le] at hc
    -- âŠ¢ False
    obtain âŸ¨n, cn, hcâŸ© :=
      exists_lt_of_csInf_lt (colorable_set_nonempty_of_colorable C.to_colorable) hc
    rw [â† Fintype.card_fin n] at hc
    -- âŠ¢ False
    have f := (Function.Embedding.nonempty_of_card_le (le_of_lt hc)).some
    -- âŠ¢ False
    have C' := cn.some
    -- âŠ¢ False
    specialize h (G.recolorOfEmbedding f C')
    -- âŠ¢ False
    have h1 : Function.Surjective f := Function.Surjective.of_comp h
    -- âŠ¢ False
    have h2 := Fintype.card_le_of_surjective _ h1
    -- âŠ¢ False
    exact Nat.lt_le_antisymm hc h2
    -- ğŸ‰ no goals
#align simple_graph.chromatic_number_eq_card_of_forall_surj SimpleGraph.chromaticNumber_eq_card_of_forall_surj

theorem chromaticNumber_bot [Nonempty V] : (âŠ¥ : SimpleGraph V).chromaticNumber = 1 := by
  let C : (âŠ¥ : SimpleGraph V).Coloring (Fin 1) :=
      Coloring.mk (fun _ => 0) fun {v w} h => False.elim h
  apply le_antisymm
  -- âŠ¢ chromaticNumber âŠ¥ â‰¤ 1
  Â· exact chromaticNumber_le_card C
    -- ğŸ‰ no goals
  Â· exact chromaticNumber_pos C.to_colorable
    -- ğŸ‰ no goals
#align simple_graph.chromatic_number_bot SimpleGraph.chromaticNumber_bot

@[simp]
theorem chromaticNumber_top [Fintype V] : (âŠ¤ : SimpleGraph V).chromaticNumber = Fintype.card V := by
  apply chromaticNumber_eq_card_of_forall_surj (selfColoring _)
  -- âŠ¢ âˆ€ (C' : Coloring âŠ¤ V), Function.Surjective â†‘C'
  intro C
  -- âŠ¢ Function.Surjective â†‘C
  rw [â† Finite.injective_iff_surjective]
  -- âŠ¢ Function.Injective â†‘C
  intro v w
  -- âŠ¢ â†‘C v = â†‘C w â†’ v = w
  contrapose
  -- âŠ¢ Â¬v = w â†’ Â¬â†‘C v = â†‘C w
  intro h
  -- âŠ¢ Â¬â†‘C v = â†‘C w
  exact C.valid h
  -- ğŸ‰ no goals
#align simple_graph.chromatic_number_top SimpleGraph.chromaticNumber_top

theorem chromaticNumber_top_eq_zero_of_infinite (V : Type*) [Infinite V] :
    (âŠ¤ : SimpleGraph V).chromaticNumber = 0 := by
  let n := (âŠ¤ : SimpleGraph V).chromaticNumber
  -- âŠ¢ chromaticNumber âŠ¤ = 0
  by_contra hc
  -- âŠ¢ False
  replace hc := pos_iff_ne_zero.mpr hc
  -- âŠ¢ False
  apply Nat.not_succ_le_self n
  -- âŠ¢ Nat.succ n â‰¤ n
  convert_to (âŠ¤ : SimpleGraph { m | m < n + 1 }).chromaticNumber â‰¤ _
  -- âŠ¢ Nat.succ n = chromaticNumber âŠ¤
  Â· rw [SimpleGraph.chromaticNumber_top, Fintype.card_ofFinset,
        Finset.card_range, Nat.succ_eq_add_one]
  refine' (colorable_of_chromaticNumber_pos hc).chromaticNumber_mono_of_embedding _
  -- âŠ¢ âŠ¤ â†ªg âŠ¤
  apply Embedding.completeGraph
  -- âŠ¢ â†‘{m | m < n + 1} â†ª V
  exact (Function.Embedding.subtype _).trans (Infinite.natEmbedding V)
  -- ğŸ‰ no goals
#align simple_graph.chromatic_number_top_eq_zero_of_infinite SimpleGraph.chromaticNumber_top_eq_zero_of_infinite

/-- The bicoloring of a complete bipartite graph using whether a vertex
is on the left or on the right. -/
def CompleteBipartiteGraph.bicoloring (V W : Type*) : (completeBipartiteGraph V W).Coloring Bool :=
  Coloring.mk (fun v => v.isRight)
    (by
      intro v w
      -- âŠ¢ Adj (completeBipartiteGraph V W) v w â†’ (fun v => Sum.isRight v) v â‰  (fun v = â€¦
      cases v <;> cases w <;> simp)
      -- âŠ¢ Adj (completeBipartiteGraph V W) (Sum.inl valâœ) w â†’ (fun v => Sum.isRight v) â€¦
                  -- âŠ¢ Adj (completeBipartiteGraph V W) (Sum.inl valâœÂ¹) (Sum.inl valâœ) â†’ (fun v =>  â€¦
                  -- âŠ¢ Adj (completeBipartiteGraph V W) (Sum.inr valâœÂ¹) (Sum.inl valâœ) â†’ (fun v =>  â€¦
                              -- ğŸ‰ no goals
                              -- ğŸ‰ no goals
                              -- ğŸ‰ no goals
                              -- ğŸ‰ no goals
#align simple_graph.complete_bipartite_graph.bicoloring SimpleGraph.CompleteBipartiteGraph.bicoloring

theorem CompleteBipartiteGraph.chromaticNumber {V W : Type*} [Nonempty V] [Nonempty W] :
    (completeBipartiteGraph V W).chromaticNumber = 2 := by
  apply chromaticNumber_eq_card_of_forall_surj (CompleteBipartiteGraph.bicoloring V W)
  -- âŠ¢ âˆ€ (C' : Coloring (completeBipartiteGraph V W) Bool), Function.Surjective â†‘C'
  intro C b
  -- âŠ¢ âˆƒ a, â†‘C a = b
  have v := Classical.arbitrary V
  -- âŠ¢ âˆƒ a, â†‘C a = b
  have w := Classical.arbitrary W
  -- âŠ¢ âˆƒ a, â†‘C a = b
  have h : (completeBipartiteGraph V W).Adj (Sum.inl v) (Sum.inr w) := by simp
  -- âŠ¢ âˆƒ a, â†‘C a = b
  have hn := C.valid h
  -- âŠ¢ âˆƒ a, â†‘C a = b
  by_cases he : C (Sum.inl v) = b
  -- âŠ¢ âˆƒ a, â†‘C a = b
  Â· exact âŸ¨_, heâŸ©
    -- ğŸ‰ no goals
  Â· by_cases he' : C (Sum.inr w) = b
    -- âŠ¢ âˆƒ a, â†‘C a = b
    Â· exact âŸ¨_, he'âŸ©
      -- ğŸ‰ no goals
    Â· exfalso
      -- âŠ¢ False
      cases b <;>
      -- âŠ¢ False
        simp only [Bool.eq_true_eq_not_eq_false, Bool.eq_false_eq_not_eq_true] at he he' <;>
        -- âŠ¢ False
        -- âŠ¢ False
          rw [he, he'] at hn <;>
          -- âŠ¢ False
          -- âŠ¢ False
        contradiction
        -- ğŸ‰ no goals
        -- ğŸ‰ no goals
#align simple_graph.complete_bipartite_graph.chromatic_number SimpleGraph.CompleteBipartiteGraph.chromaticNumber

/-! ### Cliques -/


theorem IsClique.card_le_of_coloring {s : Finset V} (h : G.IsClique s) [Fintype Î±]
    (C : G.Coloring Î±) : s.card â‰¤ Fintype.card Î± := by
  rw [isClique_iff_induce_eq] at h
  -- âŠ¢ Finset.card s â‰¤ Fintype.card Î±
  have f : G.induce â†‘s â†ªg G := Embedding.comap (Function.Embedding.subtype fun x => x âˆˆ â†‘s) G
  -- âŠ¢ Finset.card s â‰¤ Fintype.card Î±
  rw [h] at f
  -- âŠ¢ Finset.card s â‰¤ Fintype.card Î±
  convert Fintype.card_le_of_injective _ (C.comp f.toHom).injective_of_top_hom using 1
  -- âŠ¢ Finset.card s = Fintype.card â†‘â†‘s
  simp
  -- ğŸ‰ no goals
#align simple_graph.is_clique.card_le_of_coloring SimpleGraph.IsClique.card_le_of_coloring

theorem IsClique.card_le_of_colorable {s : Finset V} (h : G.IsClique s) {n : â„•}
    (hc : G.Colorable n) : s.card â‰¤ n := by
  convert h.card_le_of_coloring hc.some
  -- âŠ¢ n = Fintype.card (Fin n)
  simp
  -- ğŸ‰ no goals
#align simple_graph.is_clique.card_le_of_colorable SimpleGraph.IsClique.card_le_of_colorable

-- TODO eliminate `Finite V` constraint once chromatic numbers are refactored.
-- This is just to ensure the chromatic number exists.
theorem IsClique.card_le_chromaticNumber [Finite V] {s : Finset V} (h : G.IsClique s) :
    s.card â‰¤ G.chromaticNumber := by
  cases nonempty_fintype V
  -- âŠ¢ Finset.card s â‰¤ chromaticNumber G
  exact h.card_le_of_colorable G.colorable_chromaticNumber_of_fintype
  -- ğŸ‰ no goals
#align simple_graph.is_clique.card_le_chromatic_number SimpleGraph.IsClique.card_le_chromaticNumber

protected theorem Colorable.cliqueFree {n m : â„•} (hc : G.Colorable n) (hm : n < m) :
    G.CliqueFree m := by
  by_contra h
  -- âŠ¢ False
  simp only [CliqueFree, isNClique_iff, not_forall, Classical.not_not] at h
  -- âŠ¢ False
  obtain âŸ¨s, h, rflâŸ© := h
  -- âŠ¢ False
  exact Nat.lt_le_antisymm hm (h.card_le_of_colorable hc)
  -- ğŸ‰ no goals
#align simple_graph.colorable.clique_free SimpleGraph.Colorable.cliqueFree

-- TODO eliminate `Finite V` constraint once chromatic numbers are refactored.
-- This is just to ensure the chromatic number exists.
theorem cliqueFree_of_chromaticNumber_lt [Finite V] {n : â„•} (hc : G.chromaticNumber < n) :
    G.CliqueFree n :=
  G.colorable_chromaticNumber_of_fintype.cliqueFree hc
#align simple_graph.clique_free_of_chromatic_number_lt SimpleGraph.cliqueFree_of_chromaticNumber_lt

end SimpleGraph
