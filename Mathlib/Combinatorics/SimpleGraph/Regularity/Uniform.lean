/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Combinatorics.SimpleGraph.Density
import Mathlib.Data.Nat.Cast.Field
import Mathlib.Order.Partition.Equipartition
import Mathlib.SetTheory.Ordinal.Basic

#align_import combinatorics.simple_graph.regularity.uniform from "leanprover-community/mathlib"@"bf7ef0e83e5b7e6c1169e97f055e58a2e4e9d52d"

/-!
# Graph uniformity and uniform partitions

In this file we define uniformity of a pair of vertices in a graph and uniformity of a partition of
vertices of a graph. Both are also known as Îµ-regularity.

Finsets of vertices `s` and `t` are `Îµ`-uniform in a graph `G` if their edge density is at most
`Îµ`-far from the density of any big enough `s'` and `t'` where `s' âŠ† s`, `t' âŠ† t`.
The definition is pretty technical, but it amounts to the edges between `s` and `t` being "random"
The literature contains several definitions which are equivalent up to scaling `Îµ` by some constant
when the partition is equitable.

A partition `P` of the vertices is `Îµ`-uniform if the proportion of non `Îµ`-uniform pairs of parts
is less than `Îµ`.

## Main declarations

* `SimpleGraph.IsUniform`: Graph uniformity of a pair of finsets of vertices.
* `SimpleGraph.nonuniformWitness`: `G.nonuniformWitness Îµ s t` and `G.nonuniformWitness Îµ t s`
  together witness the non-uniformity of `s` and `t`.
* `Finpartition.nonUniforms`: Non uniform pairs of parts of a partition.
* `Finpartition.IsUniform`: Uniformity of a partition.
* `Finpartition.nonuniformWitnesses`: For each non-uniform pair of parts of a partition, pick
  witnesses of non-uniformity and dump them all together.

## References

[YaÃ«l Dillies, Bhavik Mehta, *Formalising SzemerÃ©diâ€™s Regularity Lemma in Lean*][srl_itp]
-/


open Finset

variable {Î± ğ•œ : Type*} [LinearOrderedField ğ•œ]

/-! ### Graph uniformity -/


namespace SimpleGraph

variable (G : SimpleGraph Î±) [DecidableRel G.Adj] (Îµ : ğ•œ) {s t : Finset Î±} {a b : Î±}

/-- A pair of finsets of vertices is `Îµ`-uniform (aka `Îµ`-regular) iff their edge density is close
to the density of any big enough pair of subsets. Intuitively, the edges between them are
random-like. -/
def IsUniform (s t : Finset Î±) : Prop :=
  âˆ€ â¦ƒs'â¦„, s' âŠ† s â†’ âˆ€ â¦ƒt'â¦„, t' âŠ† t â†’ (s.card : ğ•œ) * Îµ â‰¤ s'.card â†’
    (t.card : ğ•œ) * Îµ â‰¤ t'.card â†’ |(G.edgeDensity s' t' : ğ•œ) - (G.edgeDensity s t : ğ•œ)| < Îµ
#align simple_graph.is_uniform SimpleGraph.IsUniform

variable {G Îµ}

instance IsUniform.instDecidableRel : DecidableRel (G.IsUniform Îµ) := by
  unfold IsUniform; infer_instance

theorem IsUniform.mono {Îµ' : ğ•œ} (h : Îµ â‰¤ Îµ') (hÎµ : IsUniform G Îµ s t) : IsUniform G Îµ' s t :=
  fun s' hs' t' ht' hs ht => by
  refine (hÎµ hs' ht' (le_trans ?_ hs) (le_trans ?_ ht)).trans_le h <;> gcongr
#align simple_graph.is_uniform.mono SimpleGraph.IsUniform.mono

theorem IsUniform.symm : Symmetric (IsUniform G Îµ) := fun s t h t' ht' s' hs' ht hs => by
  rw [edgeDensity_comm _ t', edgeDensity_comm _ t]
  exact h hs' ht' hs ht
#align simple_graph.is_uniform.symm SimpleGraph.IsUniform.symm

variable (G)

theorem isUniform_comm : IsUniform G Îµ s t â†” IsUniform G Îµ t s :=
  âŸ¨fun h => h.symm, fun h => h.symmâŸ©
#align simple_graph.is_uniform_comm SimpleGraph.isUniform_comm

lemma isUniform_one : G.IsUniform (1 : ğ•œ) s t := by
  intro s' hs' t' ht' hs ht
  rw [mul_one] at hs ht
  rw [eq_of_subset_of_card_le hs' (Nat.cast_le.1 hs),
    eq_of_subset_of_card_le ht' (Nat.cast_le.1 ht), sub_self, abs_zero]
  exact zero_lt_one
#align simple_graph.is_uniform_one SimpleGraph.isUniform_one

variable {G}

lemma IsUniform.pos (hG : G.IsUniform Îµ s t) : 0 < Îµ :=
  not_le.1 fun hÎµ â†¦ (hÎµ.trans $ abs_nonneg _).not_lt $ hG (empty_subset _) (empty_subset _)
    (by simpa using mul_nonpos_of_nonneg_of_nonpos (Nat.cast_nonneg _) hÎµ)
    (by simpa using mul_nonpos_of_nonneg_of_nonpos (Nat.cast_nonneg _) hÎµ)

@[simp] lemma isUniform_singleton : G.IsUniform Îµ {a} {b} â†” 0 < Îµ := by
  refine âŸ¨IsUniform.pos, fun hÎµ s' hs' t' ht' hs ht â†¦ ?_âŸ©
  rw [card_singleton, Nat.cast_one, one_mul] at hs ht
  obtain rfl | rfl := Finset.subset_singleton_iff.1 hs'
  Â· replace hs : Îµ â‰¤ 0 := by simpa using hs
    exact (hÎµ.not_le hs).elim
  obtain rfl | rfl := Finset.subset_singleton_iff.1 ht'
  Â· replace ht : Îµ â‰¤ 0 := by simpa using ht
    exact (hÎµ.not_le ht).elim
  Â· rwa [sub_self, abs_zero]
#align simple_graph.is_uniform_singleton SimpleGraph.isUniform_singleton

theorem not_isUniform_zero : Â¬G.IsUniform (0 : ğ•œ) s t := fun h =>
  (abs_nonneg _).not_lt <| h (empty_subset _) (empty_subset _) (by simp) (by simp)
#align simple_graph.not_is_uniform_zero SimpleGraph.not_isUniform_zero

theorem not_isUniform_iff :
    Â¬G.IsUniform Îµ s t â†” âˆƒ s', s' âŠ† s âˆ§ âˆƒ t', t' âŠ† t âˆ§ â†‘s.card * Îµ â‰¤ s'.card âˆ§
      â†‘t.card * Îµ â‰¤ t'.card âˆ§ Îµ â‰¤ |G.edgeDensity s' t' - G.edgeDensity s t| := by
  unfold IsUniform
  simp only [not_forall, not_lt, exists_prop, exists_and_left, Rat.cast_abs, Rat.cast_sub]
#align simple_graph.not_is_uniform_iff SimpleGraph.not_isUniform_iff

open scoped Classical

variable (G)

/-- An arbitrary pair of subsets witnessing the non-uniformity of `(s, t)`. If `(s, t)` is uniform,
returns `(s, t)`. Witnesses for `(s, t)` and `(t, s)` don't necessarily match. See
`SimpleGraph.nonuniformWitness`. -/
noncomputable def nonuniformWitnesses (Îµ : ğ•œ) (s t : Finset Î±) : Finset Î± Ã— Finset Î± :=
  if h : Â¬G.IsUniform Îµ s t then
    ((not_isUniform_iff.1 h).choose, (not_isUniform_iff.1 h).choose_spec.2.choose)
  else (s, t)
#align simple_graph.nonuniform_witnesses SimpleGraph.nonuniformWitnesses

theorem left_nonuniformWitnesses_subset (h : Â¬G.IsUniform Îµ s t) :
    (G.nonuniformWitnesses Îµ s t).1 âŠ† s := by
  rw [nonuniformWitnesses, dif_pos h]
  exact (not_isUniform_iff.1 h).choose_spec.1
#align simple_graph.left_nonuniform_witnesses_subset SimpleGraph.left_nonuniformWitnesses_subset

theorem left_nonuniformWitnesses_card (h : Â¬G.IsUniform Îµ s t) :
    (s.card : ğ•œ) * Îµ â‰¤ (G.nonuniformWitnesses Îµ s t).1.card := by
  rw [nonuniformWitnesses, dif_pos h]
  exact (not_isUniform_iff.1 h).choose_spec.2.choose_spec.2.1
#align simple_graph.left_nonuniform_witnesses_card SimpleGraph.left_nonuniformWitnesses_card

theorem right_nonuniformWitnesses_subset (h : Â¬G.IsUniform Îµ s t) :
    (G.nonuniformWitnesses Îµ s t).2 âŠ† t := by
  rw [nonuniformWitnesses, dif_pos h]
  exact (not_isUniform_iff.1 h).choose_spec.2.choose_spec.1
#align simple_graph.right_nonuniform_witnesses_subset SimpleGraph.right_nonuniformWitnesses_subset

theorem right_nonuniformWitnesses_card (h : Â¬G.IsUniform Îµ s t) :
    (t.card : ğ•œ) * Îµ â‰¤ (G.nonuniformWitnesses Îµ s t).2.card := by
  rw [nonuniformWitnesses, dif_pos h]
  exact (not_isUniform_iff.1 h).choose_spec.2.choose_spec.2.2.1
#align simple_graph.right_nonuniform_witnesses_card SimpleGraph.right_nonuniformWitnesses_card

theorem nonuniformWitnesses_spec (h : Â¬G.IsUniform Îµ s t) :
    Îµ â‰¤
      |G.edgeDensity (G.nonuniformWitnesses Îµ s t).1 (G.nonuniformWitnesses Îµ s t).2 -
          G.edgeDensity s t| := by
  rw [nonuniformWitnesses, dif_pos h]
  exact (not_isUniform_iff.1 h).choose_spec.2.choose_spec.2.2.2
#align simple_graph.nonuniform_witnesses_spec SimpleGraph.nonuniformWitnesses_spec

/-- Arbitrary witness of non-uniformity. `G.nonuniformWitness Îµ s t` and
`G.nonuniformWitness Îµ t s` form a pair of subsets witnessing the non-uniformity of `(s, t)`. If
`(s, t)` is uniform, returns `s`. -/
noncomputable def nonuniformWitness (Îµ : ğ•œ) (s t : Finset Î±) : Finset Î± :=
  if WellOrderingRel s t then (G.nonuniformWitnesses Îµ s t).1 else (G.nonuniformWitnesses Îµ t s).2
#align simple_graph.nonuniform_witness SimpleGraph.nonuniformWitness

theorem nonuniformWitness_subset (h : Â¬G.IsUniform Îµ s t) : G.nonuniformWitness Îµ s t âŠ† s := by
  unfold nonuniformWitness
  split_ifs
  Â· exact G.left_nonuniformWitnesses_subset h
  Â· exact G.right_nonuniformWitnesses_subset fun i => h i.symm
#align simple_graph.nonuniform_witness_subset SimpleGraph.nonuniformWitness_subset

theorem le_card_nonuniformWitness (h : Â¬G.IsUniform Îµ s t) :
    (s.card : ğ•œ) * Îµ â‰¤ (G.nonuniformWitness Îµ s t).card := by
  unfold nonuniformWitness
  split_ifs
  Â· exact G.left_nonuniformWitnesses_card h
  Â· exact G.right_nonuniformWitnesses_card fun i => h i.symm
#align simple_graph.le_card_nonuniform_witness SimpleGraph.le_card_nonuniformWitness

theorem nonuniformWitness_spec (hâ‚ : s â‰  t) (hâ‚‚ : Â¬G.IsUniform Îµ s t) : Îµ â‰¤ |G.edgeDensity
    (G.nonuniformWitness Îµ s t) (G.nonuniformWitness Îµ t s) - G.edgeDensity s t| := by
  unfold nonuniformWitness
  rcases trichotomous_of WellOrderingRel s t with (lt | rfl | gt)
  Â· rw [if_pos lt, if_neg (asymm lt)]
    exact G.nonuniformWitnesses_spec hâ‚‚
  Â· cases hâ‚ rfl
  Â· rw [if_neg (asymm gt), if_pos gt, edgeDensity_comm, edgeDensity_comm _ s]
    apply G.nonuniformWitnesses_spec fun i => hâ‚‚ i.symm
#align simple_graph.nonuniform_witness_spec SimpleGraph.nonuniformWitness_spec

end SimpleGraph

/-! ### Uniform partitions -/


variable [DecidableEq Î±] {A : Finset Î±} (P : Finpartition A) (G : SimpleGraph Î±)
  [DecidableRel G.Adj] {Îµ Î´ : ğ•œ} {u v : Finset Î±}

namespace Finpartition

/-- The pairs of parts of a partition `P` which are not `Îµ`-dense in a graph `G`. Note that we
dismiss the diagonal. We do not care whether `s` is `Îµ`-dense with itself. -/
def sparsePairs (Îµ : ğ•œ) : Finset (Finset Î± Ã— Finset Î±) :=
  P.parts.offDiag.filter fun (u, v) â†¦ G.edgeDensity u v < Îµ

@[simp]
lemma mk_mem_sparsePairs (u v : Finset Î±) (Îµ : ğ•œ) :
    (u, v) âˆˆ P.sparsePairs G Îµ â†” u âˆˆ P.parts âˆ§ v âˆˆ P.parts âˆ§ u â‰  v âˆ§ G.edgeDensity u v < Îµ := by
  rw [sparsePairs, mem_filter, mem_offDiag, and_assoc, and_assoc]

lemma sparsePairs_mono {Îµ Îµ' : ğ•œ} (h : Îµ â‰¤ Îµ') : P.sparsePairs G Îµ âŠ† P.sparsePairs G Îµ' :=
  monotone_filter_right _ fun _ â†¦ h.trans_lt'

/-- The pairs of parts of a partition `P` which are not `Îµ`-uniform in a graph `G`. Note that we
dismiss the diagonal. We do not care whether `s` is `Îµ`-uniform with itself. -/
def nonUniforms (Îµ : ğ•œ) : Finset (Finset Î± Ã— Finset Î±) :=
  P.parts.offDiag.filter fun (u, v) â†¦ Â¬G.IsUniform Îµ u v
#align finpartition.non_uniforms Finpartition.nonUniforms

@[simp] lemma mk_mem_nonUniforms :
    (u, v) âˆˆ P.nonUniforms G Îµ â†” u âˆˆ P.parts âˆ§ v âˆˆ P.parts âˆ§ u â‰  v âˆ§ Â¬G.IsUniform Îµ u v := by
  rw [nonUniforms, mem_filter, mem_offDiag, and_assoc, and_assoc]
#align finpartition.mk_mem_non_uniforms_iff Finpartition.mk_mem_nonUniforms

theorem nonUniforms_mono {Îµ Îµ' : ğ•œ} (h : Îµ â‰¤ Îµ') : P.nonUniforms G Îµ' âŠ† P.nonUniforms G Îµ :=
  monotone_filter_right _ fun _ => mt <| SimpleGraph.IsUniform.mono h
#align finpartition.non_uniforms_mono Finpartition.nonUniforms_mono

theorem nonUniforms_bot (hÎµ : 0 < Îµ) : (âŠ¥ : Finpartition A).nonUniforms G Îµ = âˆ… := by
  rw [eq_empty_iff_forall_not_mem]
  rintro âŸ¨u, vâŸ©
  simp only [mk_mem_nonUniforms, parts_bot, mem_map, not_and,
    Classical.not_not, exists_imp]; dsimp
  rintro x âŸ¨_, rflâŸ© y âŸ¨_,rflâŸ© _
  rwa [SimpleGraph.isUniform_singleton]
#align finpartition.non_uniforms_bot Finpartition.nonUniforms_bot

/-- A finpartition of a graph's vertex set is `Îµ`-uniform (aka `Îµ`-regular) iff the proportion of
its pairs of parts that are not `Îµ`-uniform is at most `Îµ`. -/
def IsUniform (Îµ : ğ•œ) : Prop :=
  ((P.nonUniforms G Îµ).card : ğ•œ) â‰¤ (P.parts.card * (P.parts.card - 1) : â„•) * Îµ
#align finpartition.is_uniform Finpartition.IsUniform

lemma bot_isUniform (hÎµ : 0 < Îµ) : (âŠ¥ : Finpartition A).IsUniform G Îµ := by
  rw [Finpartition.IsUniform, Finpartition.card_bot, nonUniforms_bot _ hÎµ, Finset.card_empty,
    Nat.cast_zero]
  exact mul_nonneg (Nat.cast_nonneg _) hÎµ.le
#align finpartition.bot_is_uniform Finpartition.bot_isUniform

lemma isUniform_one : P.IsUniform G (1 : ğ•œ) := by
  rw [IsUniform, mul_one, Nat.cast_le]
  refine (card_filter_le _
    (fun uv => Â¬SimpleGraph.IsUniform G 1 (Prod.fst uv) (Prod.snd uv))).trans ?_
  rw [offDiag_card, Nat.mul_sub_left_distrib, mul_one]
#align finpartition.is_uniform_one Finpartition.isUniform_one

variable {P G}

theorem IsUniform.mono {Îµ Îµ' : ğ•œ} (hP : P.IsUniform G Îµ) (h : Îµ â‰¤ Îµ') : P.IsUniform G Îµ' :=
  ((Nat.cast_le.2 <| card_le_card <| P.nonUniforms_mono G h).trans hP).trans <| by gcongr
#align finpartition.is_uniform.mono Finpartition.IsUniform.mono

theorem isUniformOfEmpty (hP : P.parts = âˆ…) : P.IsUniform G Îµ := by
  simp [IsUniform, hP, nonUniforms]
#align finpartition.is_uniform_of_empty Finpartition.isUniformOfEmpty

theorem nonempty_of_not_uniform (h : Â¬P.IsUniform G Îµ) : P.parts.Nonempty :=
  nonempty_of_ne_empty fun hâ‚ => h <| isUniformOfEmpty hâ‚
#align finpartition.nonempty_of_not_uniform Finpartition.nonempty_of_not_uniform

variable (P G Îµ) (s : Finset Î±)

/-- A choice of witnesses of non-uniformity among the parts of a finpartition. -/
noncomputable def nonuniformWitnesses : Finset (Finset Î±) :=
  (P.parts.filter fun t => s â‰  t âˆ§ Â¬G.IsUniform Îµ s t).image (G.nonuniformWitness Îµ s)
#align finpartition.nonuniform_witnesses Finpartition.nonuniformWitnesses

variable {P G Îµ s} {t : Finset Î±}

theorem nonuniformWitness_mem_nonuniformWitnesses (h : Â¬G.IsUniform Îµ s t) (ht : t âˆˆ P.parts)
    (hst : s â‰  t) : G.nonuniformWitness Îµ s t âˆˆ P.nonuniformWitnesses G Îµ s :=
  mem_image_of_mem _ <| mem_filter.2 âŸ¨ht, hst, hâŸ©
#align finpartition.nonuniform_witness_mem_nonuniform_witnesses Finpartition.nonuniformWitness_mem_nonuniformWitnesses

/-! ### Equipartitions -/

open SimpleGraph in
lemma IsEquipartition.card_interedges_sparsePairs_le' (hP : P.IsEquipartition)
    (hÎµ : 0 â‰¤ Îµ) :
    ((P.sparsePairs G Îµ).biUnion fun (U, V) â†¦ G.interedges U V).card â‰¤
      Îµ * (A.card + P.parts.card) ^ 2 := by
  calc
    _ â‰¤ âˆ‘ UV âˆˆ P.sparsePairs G Îµ, ((G.interedges UV.1 UV.2).card : ğ•œ) := mod_cast card_biUnion_le
    _ â‰¤ âˆ‘ UV âˆˆ P.sparsePairs G Îµ, Îµ * (UV.1.card * UV.2.card) := ?_
    _ â‰¤ _ := sum_le_sum_of_subset_of_nonneg (filter_subset _ _) fun i _ _ â†¦ by positivity
    _ = _ := (mul_sum _ _ _).symm
    _ â‰¤ _ := mul_le_mul_of_nonneg_left ?_ hÎµ
  Â· gcongr with UV hUV
    obtain âŸ¨U, VâŸ© := UV
    simp [mk_mem_sparsePairs, â† card_interedges_div_card] at hUV
    refine ((div_lt_iff ?_).1 hUV.2.2.2).le
    exact mul_pos (Nat.cast_pos.2 (P.nonempty_of_mem_parts hUV.1).card_pos)
      (Nat.cast_pos.2 (P.nonempty_of_mem_parts hUV.2.1).card_pos)
  norm_cast
  calc
    (_ : â„•) â‰¤ _ := sum_le_card_nsmul P.parts.offDiag (fun i â†¦ i.1.card * i.2.card)
            ((A.card / P.parts.card + 1)^2 : â„•) ?_
    _ â‰¤ (P.parts.card * (A.card / P.parts.card) + P.parts.card) ^ 2 := ?_
    _ â‰¤ _ := Nat.pow_le_pow_of_le_left (add_le_add_right (Nat.mul_div_le _ _) _) _
  Â· simp only [Prod.forall, Finpartition.mk_mem_nonUniforms, and_imp, mem_offDiag, sq]
    rintro U V hU hV -
    exact_mod_cast Nat.mul_le_mul (hP.card_part_le_average_add_one hU)
      (hP.card_part_le_average_add_one hV)
  Â· rw [smul_eq_mul, offDiag_card, Nat.mul_sub_right_distrib, â† sq, â† mul_pow, mul_add_one (Î± := â„•)]
    exact Nat.sub_le _ _

lemma IsEquipartition.card_interedges_sparsePairs_le (hP : P.IsEquipartition) (hÎµ : 0 â‰¤ Îµ) :
    ((P.sparsePairs G Îµ).biUnion fun (U, V) â†¦ G.interedges U V).card â‰¤ 4 * Îµ * A.card ^ 2 := by
  calc
    _ â‰¤ _ := hP.card_interedges_sparsePairs_le' hÎµ
    _ â‰¤ Îµ * (A.card + A.card)^2 := by gcongr; exact P.card_parts_le_card
    _ = _ := by ring

private lemma aux {i j : â„•} (hj : 0 < j) : j * (j - 1) * (i / j + 1) ^ 2 < (i + j) ^ 2 := by
  have : j * (j - 1) < j ^ 2 := by
    rw [sq]; exact Nat.mul_lt_mul_of_pos_left (Nat.sub_lt hj zero_lt_one) hj
  apply (Nat.mul_lt_mul_of_pos_right this $ pow_pos Nat.succ_pos' _).trans_le
  rw [â† mul_pow]
  exact Nat.pow_le_pow_of_le_left (add_le_add_right (Nat.mul_div_le i j) _) _

lemma IsEquipartition.card_biUnion_offDiag_le' (hP : P.IsEquipartition) :
    ((P.parts.biUnion offDiag).card : ğ•œ) â‰¤ A.card * (A.card + P.parts.card) / P.parts.card := by
  obtain h | h := P.parts.eq_empty_or_nonempty
  Â· simp [h]
  calc
    _ â‰¤ (P.parts.card : ğ•œ) * (â†‘(A.card / P.parts.card) * â†‘(A.card / P.parts.card + 1)) :=
        mod_cast card_biUnion_le_card_mul _ _ _ fun U hU â†¦ ?_
    _ = P.parts.card * â†‘(A.card / P.parts.card) * â†‘(A.card / P.parts.card + 1) := by rw [mul_assoc]
    _ â‰¤ A.card * (A.card / P.parts.card + 1) :=
        mul_le_mul (mod_cast Nat.mul_div_le _ _) ?_ (by positivity) (by positivity)
    _ = _ := by rw [â† div_add_same (mod_cast h.card_pos.ne'), mul_div_assoc]
  Â· simpa using Nat.cast_div_le
  suffices (U.card - 1) * U.card â‰¤ A.card / P.parts.card * (A.card / P.parts.card + 1) by
    rwa [Nat.mul_sub_right_distrib, one_mul, â† offDiag_card] at this
  have := hP.card_part_le_average_add_one hU
  refine Nat.mul_le_mul ((Nat.sub_le_sub_right this 1).trans ?_) this
  simp only [Nat.add_succ_sub_one, add_zero, card_univ, le_rfl]

lemma IsEquipartition.card_biUnion_offDiag_le (hÎµ : 0 < Îµ) (hP : P.IsEquipartition)
    (hP' : 4 / Îµ â‰¤ P.parts.card) : (P.parts.biUnion offDiag).card â‰¤ Îµ / 2 * A.card ^ 2 := by
  obtain rfl | hA : A = âŠ¥ âˆ¨ _ := A.eq_empty_or_nonempty
  Â· simp [Subsingleton.elim P âŠ¥]
  apply hP.card_biUnion_offDiag_le'.trans
  rw [div_le_iff (Nat.cast_pos.2 (P.parts_nonempty hA.ne_empty).card_pos)]
  have : (A.card : ğ•œ) + P.parts.card â‰¤ 2 * A.card := by
    rw [two_mul]; exact add_le_add_left (Nat.cast_le.2 P.card_parts_le_card) _
  refine (mul_le_mul_of_nonneg_left this $ by positivity).trans ?_
  suffices 1 â‰¤ Îµ/4 * P.parts.card by
    rw [mul_left_comm, â† sq]
    convert mul_le_mul_of_nonneg_left this (mul_nonneg zero_le_two $ sq_nonneg (A.card : ğ•œ))
      using 1 <;> ring
  rwa [â† div_le_iff', one_div_div]
  positivity

lemma IsEquipartition.sum_nonUniforms_lt' (hA : A.Nonempty) (hÎµ : 0 < Îµ) (hP : P.IsEquipartition)
    (hG : P.IsUniform G Îµ) :
    âˆ‘ i âˆˆ P.nonUniforms G Îµ, (i.1.card * i.2.card : ğ•œ) < Îµ * (A.card + P.parts.card) ^ 2 := by
  calc
    _ â‰¤ (P.nonUniforms G Îµ).card â€¢ (â†‘(A.card / P.parts.card + 1) : ğ•œ) ^ 2 :=
      sum_le_card_nsmul _ _ _ ?_
    _ = _ := nsmul_eq_mul _ _
    _ â‰¤ _ := mul_le_mul_of_nonneg_right hG $ by positivity
    _ < _ := ?_
  Â· simp only [Prod.forall, Finpartition.mk_mem_nonUniforms, and_imp]
    rintro U V hU hV - -
    rw [sq, â† Nat.cast_mul, â† Nat.cast_mul, Nat.cast_le]
    exact Nat.mul_le_mul (hP.card_part_le_average_add_one hU)
      (hP.card_part_le_average_add_one hV)
  Â· rw [mul_right_comm _ Îµ, mul_comm Îµ]
    apply mul_lt_mul_of_pos_right _ hÎµ
    norm_cast
    exact aux (P.parts_nonempty hA.ne_empty).card_pos

lemma IsEquipartition.sum_nonUniforms_lt (hA : A.Nonempty) (hÎµ : 0 < Îµ) (hP : P.IsEquipartition)
    (hG : P.IsUniform G Îµ) :
    ((P.nonUniforms G Îµ).biUnion fun (U, V) â†¦ U Ã—Ë¢ V).card < 4 * Îµ * A.card ^ 2 := by
  calc
    _ â‰¤ âˆ‘ i âˆˆ P.nonUniforms G Îµ, (i.1.card * i.2.card : ğ•œ) := by
        norm_cast; simp_rw [â† card_product]; exact card_biUnion_le
    _ < _ := hP.sum_nonUniforms_lt' hA hÎµ hG
    _ â‰¤ Îµ * (A.card + A.card) ^ 2 := by gcongr; exact P.card_parts_le_card
    _ = _ := by ring

end Finpartition

/-! ### Reduced graph -/

namespace SimpleGraph

/-- The reduction of the graph `G` along partition `P` has edges between `Îµ`-uniform pairs of parts
that have edge density at least `Î´`. -/
@[simps] def regularityReduced (Îµ Î´ : ğ•œ) : SimpleGraph Î± where
  Adj a b := G.Adj a b âˆ§
    âˆƒ U âˆˆ P.parts, âˆƒ V âˆˆ P.parts, a âˆˆ U âˆ§ b âˆˆ V âˆ§ U â‰  V âˆ§ G.IsUniform Îµ U V âˆ§ Î´ â‰¤ G.edgeDensity U V
  symm a b := by
    rintro âŸ¨ab, U, UP, V, VP, xU, yV, UV, GUV, ÎµUVâŸ©
    refine âŸ¨G.symm ab, V, VP, U, UP, yV, xU, UV.symm, GUV.symm, ?_âŸ©
    rwa [edgeDensity_comm]
  loopless a h := G.loopless a h.1

instance regularityReduced.instDecidableRel_adj : DecidableRel (G.regularityReduced P Îµ Î´).Adj := by
  unfold regularityReduced; infer_instance

variable {G P}

lemma regularityReduced_le : G.regularityReduced P Îµ Î´ â‰¤ G := fun _ _ â†¦ And.left

lemma regularityReduced_mono {Îµâ‚ Îµâ‚‚ : ğ•œ} (hÎµ : Îµâ‚ â‰¤ Îµâ‚‚) :
    G.regularityReduced P Îµâ‚ Î´ â‰¤ G.regularityReduced P Îµâ‚‚ Î´ :=
  fun _a _b âŸ¨hab, U, hU, V, hV, ha, hb, hUV, hGÎµ, hGÎ´âŸ© â†¦
    âŸ¨hab, U, hU, V, hV, ha, hb, hUV, hGÎµ.mono hÎµ, hGÎ´âŸ©

lemma regularityReduced_anti {Î´â‚ Î´â‚‚ : ğ•œ} (hÎ´ : Î´â‚ â‰¤ Î´â‚‚) :
    G.regularityReduced P Îµ Î´â‚‚ â‰¤ G.regularityReduced P Îµ Î´â‚ :=
  fun _a _b âŸ¨hab, U, hU, V, hV, ha, hb, hUV, hUVÎµ, hUVÎ´âŸ© â†¦
    âŸ¨hab, U, hU, V, hV, ha, hb, hUV, hUVÎµ, hÎ´.trans hUVÎ´âŸ©

lemma unreduced_edges_subset :
    (A Ã—Ë¢ A).filter (fun (x, y) â†¦ G.Adj x y âˆ§ Â¬ (G.regularityReduced P (Îµ/8) (Îµ/4)).Adj x y) âŠ†
      (P.nonUniforms G (Îµ/8)).biUnion (fun (U, V) â†¦ U Ã—Ë¢ V) âˆª P.parts.biUnion offDiag âˆª
        (P.sparsePairs G (Îµ/4)).biUnion fun (U, V) â†¦ G.interedges U V := by
  rintro âŸ¨x, yâŸ©
  simp only [mem_sdiff, mem_filter, mem_univ, true_and, regularityReduced_adj, not_and, not_exists,
    not_le, mem_biUnion, mem_union, exists_prop, mem_product, Prod.exists, mem_offDiag, and_imp,
    or_assoc, and_assoc, P.mk_mem_nonUniforms, Finpartition.mk_mem_sparsePairs, mem_interedges_iff]
  intros hx hy h h'
  replace h' := h' h
  obtain âŸ¨U, hU, hxâŸ© := P.exists_mem hx
  obtain âŸ¨V, hV, hyâŸ© := P.exists_mem hy
  obtain rfl | hUV := eq_or_ne U V
  Â· exact Or.inr (Or.inl âŸ¨U, hU, hx, hy, G.ne_of_adj hâŸ©)
  by_cases hâ‚‚ : G.IsUniform (Îµ/8) U V
  Â· exact Or.inr $ Or.inr âŸ¨U, V, hU, hV, hUV, h' _ hU _ hV hx hy hUV hâ‚‚, hx, hy, hâŸ©
  Â· exact Or.inl âŸ¨U, V, hU, hV, hUV, hâ‚‚, hx, hyâŸ©

end SimpleGraph
