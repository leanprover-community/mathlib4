/-
Copyright (c) 2022 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Order.Partition.Equipartition

#align_import combinatorics.simple_graph.regularity.bound from "leanprover-community/mathlib"@"bf7ef0e83e5b7e6c1169e97f055e58a2e4e9d52d"

/-!
# Numerical bounds for Szemer√©di Regularity Lemma

This file gathers the numerical facts required by the proof of Szemer√©di's regularity lemma.

This entire file is internal to the proof of Szemer√©di Regularity Lemma.

## Main declarations

* `SzemerediRegularity.stepBound`: During the inductive step, a partition of size `n` is blown to
  size at most `stepBound n`.
* `SzemerediRegularity.initialBound`: The size of the partition we start the induction with.
* `SzemerediRegularity.bound`: The upper bound on the size of the partition produced by our version
  of Szemer√©di's regularity lemma.

## References

[Ya√´l Dillies, Bhavik Mehta, *Formalising Szemer√©di‚Äôs Regularity Lemma in Lean*][srl_itp]
-/


open Finset Fintype Function Real

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

open BigOperators

namespace SzemerediRegularity

/-- Auxiliary function for Szemer√©di's regularity lemma. Blowing up a partition of size `n` during
the induction results in a partition of size at most `stepBound n`. -/
def stepBound (n : ‚Ñï) : ‚Ñï :=
  n * 4 ^ n
#align szemeredi_regularity.step_bound SzemerediRegularity.stepBound

theorem le_stepBound : id ‚â§ stepBound := fun n => Nat.le_mul_of_pos_right <| pow_pos (by norm_num) n
#align szemeredi_regularity.le_step_bound SzemerediRegularity.le_stepBound

theorem stepBound_mono : Monotone stepBound := fun a b h =>
  Nat.mul_le_mul h <| Nat.pow_le_pow_of_le_right (by norm_num) h
#align szemeredi_regularity.step_bound_mono SzemerediRegularity.stepBound_mono

theorem stepBound_pos_iff {n : ‚Ñï} : 0 < stepBound n ‚Üî 0 < n :=
  zero_lt_mul_right <| by positivity
#align szemeredi_regularity.step_bound_pos_iff SzemerediRegularity.stepBound_pos_iff

alias ‚ü®_, stepBound_pos‚ü© := stepBound_pos_iff
#align szemeredi_regularity.step_bound_pos SzemerediRegularity.stepBound_pos

end SzemerediRegularity

open SzemerediRegularity

variable {Œ± : Type*} [DecidableEq Œ±] [Fintype Œ±] {P : Finpartition (univ : Finset Œ±)}
  {u : Finset Œ±} {Œµ : ‚Ñù}

local notation3 "m" => (card Œ± / stepBound P.parts.card : ‚Ñï)

local notation3 "a" => (card Œ± / P.parts.card - m * 4 ^ P.parts.card : ‚Ñï)

namespace SzemerediRegularity.Positivity

private theorem eps_pos {Œµ : ‚Ñù} {n : ‚Ñï} (h : 100 ‚â§ (4 : ‚Ñù) ^ n * Œµ ^ 5) : 0 < Œµ :=
  (Odd.pow_pos_iff (by norm_num)).mp
    (pos_of_mul_pos_right ((show 0 < (100 : ‚Ñù) by norm_num).trans_le h) (by positivity))

private theorem m_pos [Nonempty Œ±] (hPŒ± : P.parts.card * 16 ^ P.parts.card ‚â§ card Œ±) : 0 < m :=
  Nat.div_pos ((Nat.mul_le_mul_left _ <| Nat.pow_le_pow_of_le_left (by norm_num) _).trans hPŒ±) <|
    stepBound_pos (P.parts_nonempty <| univ_nonempty.ne_empty).card_pos

/-- Local extension for the `positivity` tactic: A few facts that are needed many times for the
proof of Szemer√©di's regularity lemma. -/
-- Porting note: positivity extensions must now be global, and this did not seem like a good
-- match for positivity anymore, so I wrote a new tactic (kmill)
scoped macro "sz_positivity" : tactic =>
  `(tactic|
      { try have := m_pos ‚Äπ_‚Ä∫
        try have := eps_pos ‚Äπ_‚Ä∫
        positivity })

-- Original meta code
/- meta def positivity_szemeredi_regularity : expr ‚Üí tactic strictness
| `(%%n / step_bound (finpartition.parts %%P).card) := do
    p ‚Üê to_expr
      ``((finpartition.parts %%P).card * 16^(finpartition.parts %%P).card ‚â§ %%n)
      >>= find_assumption,
    positive <$> mk_app ``m_pos [p]
| Œµ := do
    typ ‚Üê infer_type Œµ,
    unify typ `(‚Ñù),
    p ‚Üê to_expr ``(100 ‚â§ 4 ^ _ * %%Œµ ^ 5) >>= find_assumption,
    positive <$> mk_app ``eps_pos [p] -/

end SzemerediRegularity.Positivity

namespace SzemerediRegularity

open scoped SzemerediRegularity.Positivity

theorem m_pos [Nonempty Œ±] (hPŒ± : P.parts.card * 16 ^ P.parts.card ‚â§ card Œ±) : 0 < m := by
  sz_positivity
#align szemeredi_regularity.m_pos SzemerediRegularity.m_pos

theorem coe_m_add_one_pos : 0 < (m : ‚Ñù) + 1 := by positivity
#align szemeredi_regularity.coe_m_add_one_pos SzemerediRegularity.coe_m_add_one_pos

theorem one_le_m_coe [Nonempty Œ±] (hPŒ± : P.parts.card * 16 ^ P.parts.card ‚â§ card Œ±) : (1 : ‚Ñù) ‚â§ m :=
  Nat.one_le_cast.2 <| m_pos hPŒ±
#align szemeredi_regularity.one_le_m_coe SzemerediRegularity.one_le_m_coe

theorem eps_pow_five_pos (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ P.parts.card * Œµ ^ 5) : ‚Üë0 < Œµ ^ 5 :=
  pos_of_mul_pos_right ((by norm_num : (0 : ‚Ñù) < 100).trans_le hPŒµ) <| pow_nonneg (by norm_num) _
#align szemeredi_regularity.eps_pow_five_pos SzemerediRegularity.eps_pow_five_pos

theorem eps_pos (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ P.parts.card * Œµ ^ 5) : 0 < Œµ :=
  (Odd.pow_pos_iff (by norm_num)).mp (eps_pow_five_pos hPŒµ)
#align szemeredi_regularity.eps_pos SzemerediRegularity.eps_pos

theorem hundred_div_Œµ_pow_five_le_m [Nonempty Œ±] (hPŒ± : P.parts.card * 16 ^ P.parts.card ‚â§ card Œ±)
    (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ P.parts.card * Œµ ^ 5) : 100 / Œµ ^ 5 ‚â§ m :=
  (div_le_of_nonneg_of_le_mul (eps_pow_five_pos hPŒµ).le (by positivity) hPŒµ).trans
    (by
      norm_cast
      rwa [Nat.le_div_iff_mul_le' (stepBound_pos (P.parts_nonempty <|
        univ_nonempty.ne_empty).card_pos), stepBound, mul_left_comm, ‚Üê mul_pow])
#align szemeredi_regularity.hundred_div_Œµ_pow_five_le_m SzemerediRegularity.hundred_div_Œµ_pow_five_le_m

theorem hundred_le_m [Nonempty Œ±] (hPŒ± : P.parts.card * 16 ^ P.parts.card ‚â§ card Œ±)
    (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ P.parts.card * Œµ ^ 5) (hŒµ : Œµ ‚â§ 1) : 100 ‚â§ m := by
  exact_mod_cast
    (hundred_div_Œµ_pow_five_le_m hPŒ± hPŒµ).trans'
      (le_div_self (by norm_num) (by sz_positivity) <| pow_le_one _ (by sz_positivity) hŒµ)
#align szemeredi_regularity.hundred_le_m SzemerediRegularity.hundred_le_m

theorem a_add_one_le_four_pow_parts_card : a + 1 ‚â§ 4 ^ P.parts.card := by
  have h : 1 ‚â§ 4 ^ P.parts.card := one_le_pow_of_one_le (by norm_num) _
  rw [stepBound, ‚Üê Nat.div_div_eq_div_mul]
  conv_rhs => rw [‚Üê Nat.sub_add_cancel h]
  rw [add_le_add_iff_right, tsub_le_iff_left, ‚Üê Nat.add_sub_assoc h]
  exact Nat.le_pred_of_lt (Nat.lt_div_mul_add h)
#align szemeredi_regularity.a_add_one_le_four_pow_parts_card SzemerediRegularity.a_add_one_le_four_pow_parts_card

theorem card_aux‚ÇÅ (hucard : u.card = m * 4 ^ P.parts.card + a) :
    (4 ^ P.parts.card - a) * m + a * (m + 1) = u.card := by
  rw [hucard, mul_add, mul_one, ‚Üê add_assoc, ‚Üê add_mul,
    Nat.sub_add_cancel ((Nat.le_succ _).trans a_add_one_le_four_pow_parts_card), mul_comm]
#align szemeredi_regularity.card_aux‚ÇÅ SzemerediRegularity.card_aux‚ÇÅ

theorem card_aux‚ÇÇ (hP : P.IsEquipartition) (hu : u ‚àà P.parts)
    (hucard : ¬¨u.card = m * 4 ^ P.parts.card + a) :
    (4 ^ P.parts.card - (a + 1)) * m + (a + 1) * (m + 1) = u.card := by
  have : m * 4 ^ P.parts.card ‚â§ card Œ± / P.parts.card := by
    rw [stepBound, ‚Üê Nat.div_div_eq_div_mul]
    exact Nat.div_mul_le_self _ _
  rw [Nat.add_sub_of_le this] at hucard
  rw [(hP.card_parts_eq_average hu).resolve_left hucard, mul_add, mul_one, ‚Üê add_assoc, ‚Üê add_mul,
    Nat.sub_add_cancel a_add_one_le_four_pow_parts_card, ‚Üê add_assoc, mul_comm,
    Nat.add_sub_of_le this, card_univ]
#align szemeredi_regularity.card_aux‚ÇÇ SzemerediRegularity.card_aux‚ÇÇ

theorem pow_mul_m_le_card_part (hP : P.IsEquipartition) (hu : u ‚àà P.parts) :
    (4 : ‚Ñù) ^ P.parts.card * m ‚â§ u.card := by
  norm_cast
  rw [stepBound, ‚Üê Nat.div_div_eq_div_mul]
  exact (Nat.mul_div_le _ _).trans (hP.average_le_card_part hu)
#align szemeredi_regularity.pow_mul_m_le_card_part SzemerediRegularity.pow_mul_m_le_card_part

variable (P Œµ) (l : ‚Ñï)

/-- Auxiliary function for Szemer√©di's regularity lemma. The size of the partition by which we start
blowing. -/
noncomputable def initialBound : ‚Ñï :=
  max 7 <| max l <| ‚åälog (100 / Œµ ^ 5) / log 4‚åã‚Çä + 1
#align szemeredi_regularity.initial_bound SzemerediRegularity.initialBound

theorem le_initialBound : l ‚â§ initialBound Œµ l :=
  (le_max_left _ _).trans <| le_max_right _ _
#align szemeredi_regularity.le_initial_bound SzemerediRegularity.le_initialBound

theorem seven_le_initialBound : 7 ‚â§ initialBound Œµ l :=
  le_max_left _ _
#align szemeredi_regularity.seven_le_initial_bound SzemerediRegularity.seven_le_initialBound

theorem initialBound_pos : 0 < initialBound Œµ l :=
  Nat.succ_pos'.trans_le <| seven_le_initialBound _ _
#align szemeredi_regularity.initial_bound_pos SzemerediRegularity.initialBound_pos

theorem hundred_lt_pow_initialBound_mul {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (l : ‚Ñï) :
    100 < ‚Üë4 ^ initialBound Œµ l * Œµ ^ 5 := by
  rw [‚Üê rpow_nat_cast 4, ‚Üê div_lt_iff (pow_pos hŒµ 5), lt_rpow_iff_log_lt _ zero_lt_four, ‚Üê
    div_lt_iff, initialBound, Nat.cast_max, Nat.cast_max]
  ¬∑ push_cast
    exact lt_max_of_lt_right (lt_max_of_lt_right <| Nat.lt_floor_add_one _)
  ¬∑ exact log_pos (by norm_num)
  ¬∑ exact div_pos (by norm_num) (pow_pos hŒµ 5)
#align szemeredi_regularity.hundred_lt_pow_initial_bound_mul SzemerediRegularity.hundred_lt_pow_initialBound_mul

/-- An explicit bound on the size of the equipartition whose existence is given by Szemer√©di's
regularity lemma. -/
noncomputable def bound : ‚Ñï :=
  (stepBound^[‚åä4 / Œµ ^ 5‚åã‚Çä] <| initialBound Œµ l) *
    16 ^ (stepBound^[‚åä4 / Œµ ^ 5‚åã‚Çä] <| initialBound Œµ l)
#align szemeredi_regularity.bound SzemerediRegularity.bound

theorem initialBound_le_bound : initialBound Œµ l ‚â§ bound Œµ l :=
  (id_le_iterate_of_id_le le_stepBound _ _).trans <| Nat.le_mul_of_pos_right <| by positivity
#align szemeredi_regularity.initial_bound_le_bound SzemerediRegularity.initialBound_le_bound

theorem le_bound : l ‚â§ bound Œµ l :=
  (le_initialBound Œµ l).trans <| initialBound_le_bound Œµ l
#align szemeredi_regularity.le_bound SzemerediRegularity.le_bound

theorem bound_pos : 0 < bound Œµ l :=
  (initialBound_pos Œµ l).trans_le <| initialBound_le_bound Œµ l
#align szemeredi_regularity.bound_pos SzemerediRegularity.bound_pos

variable {Œπ ùïú : Type*} [LinearOrderedField ùïú] (r : Œπ ‚Üí Œπ ‚Üí Prop) [DecidableRel r] {s t : Finset Œπ}
  {x : ùïú}

theorem mul_sq_le_sum_sq (hst : s ‚äÜ t) (f : Œπ ‚Üí ùïú) (hs : x ^ 2 ‚â§ ((‚àë i in s, f i) / s.card) ^ 2)
    (hs' : (s.card : ùïú) ‚â† 0) : (s.card : ùïú) * x ^ 2 ‚â§ ‚àë i in t, f i ^ 2 :=
  (mul_le_mul_of_nonneg_left (hs.trans sum_div_card_sq_le_sum_sq_div_card) <|
    Nat.cast_nonneg _).trans <| (mul_div_cancel' _ hs').le.trans <|
      sum_le_sum_of_subset_of_nonneg hst fun _ _ _ => sq_nonneg _
#align szemeredi_regularity.mul_sq_le_sum_sq SzemerediRegularity.mul_sq_le_sum_sq

theorem add_div_le_sum_sq_div_card (hst : s ‚äÜ t) (f : Œπ ‚Üí ùïú) (d : ùïú) (hx : 0 ‚â§ x)
    (hs : x ‚â§ |(‚àë i in s, f i) / s.card - (‚àë i in t, f i) / t.card|)
    (ht : d ‚â§ ((‚àë i in t, f i) / t.card) ^ 2) :
    d + s.card / t.card * x ^ 2 ‚â§ (‚àë i in t, f i ^ 2) / t.card := by
  obtain hscard | hscard := (s.card.cast_nonneg : (0 : ùïú) ‚â§ s.card).eq_or_lt
  ¬∑ simpa [‚Üê hscard] using ht.trans sum_div_card_sq_le_sum_sq_div_card
  have htcard : (0 : ùïú) < t.card := hscard.trans_le (Nat.cast_le.2 (card_le_of_subset hst))
  have h‚ÇÅ : x ^ 2 ‚â§ ((‚àë i in s, f i) / s.card - (‚àë i in t, f i) / t.card) ^ 2 :=
    sq_le_sq.2 (by rwa [abs_of_nonneg hx])
  have h‚ÇÇ : x ^ 2 ‚â§ ((‚àë i in s, (f i - (‚àë j in t, f j) / t.card)) / s.card) ^ 2 := by
    apply h‚ÇÅ.trans
    rw [sum_sub_distrib, sum_const, nsmul_eq_mul, sub_div, mul_div_cancel_left _ hscard.ne']
  apply (add_le_add_right ht _).trans
  rw [‚Üê mul_div_right_comm, le_div_iff htcard, add_mul, div_mul_cancel _ htcard.ne']
  have h‚ÇÉ := mul_sq_le_sum_sq hst (fun i => (f i - (‚àë j in t, f j) / t.card)) h‚ÇÇ hscard.ne'
  apply (add_le_add_left h‚ÇÉ _).trans
  -- Porting note: was
  -- `simp [‚Üê mul_div_right_comm _ (t.card : ùïú), sub_div' _ _ _ htcard.ne', ‚Üê sum_div, ‚Üê add_div,`
  -- `  mul_pow, div_le_iff (sq_pos_of_ne_zero _ htcard.ne'), sub_sq, sum_add_distrib, ‚Üê sum_mul, ‚Üê`
  -- `  mul_sum]`
  simp_rw [sub_div' _ _ _ htcard.ne']
  conv_lhs => enter [2, 2, x]; rw [div_pow]
  rw [div_pow, ‚Üê sum_div, ‚Üê mul_div_right_comm _ (t.card : ùïú), ‚Üê add_div,
    div_le_iff (sq_pos_of_ne_zero _ htcard.ne')]
  simp_rw [sub_sq, sum_add_distrib, sum_const, nsmul_eq_mul, sum_sub_distrib, mul_pow, ‚Üê sum_mul,
    ‚Üê mul_sum, ‚Üê sum_mul]
  ring_nf; rfl
#align szemeredi_regularity.add_div_le_sum_sq_div_card SzemerediRegularity.add_div_le_sum_sq_div_card

end SzemerediRegularity

namespace Tactic

open Lean.Meta Qq

/-- Extension for the `positivity` tactic: `SzemerediRegularity.initialBound` is always positive. -/
@[positivity SzemerediRegularity.initialBound _ _]
def evalInitialBound : Mathlib.Meta.Positivity.PositivityExt where eval {_ _} _ _ e := do
  let (.app (.app _ (Œµ : Q(‚Ñù))) (l : Q(‚Ñï))) ‚Üê whnfR e | throwError "not initialBound"
  pure (.positive (q(SzemerediRegularity.initialBound_pos $Œµ $l) : Lean.Expr))

example (Œµ : ‚Ñù) (l : ‚Ñï) : 0 < SzemerediRegularity.initialBound Œµ l := by positivity

/-- Extension for the `positivity` tactic: `SzemerediRegularity.bound` is always positive. -/
@[positivity SzemerediRegularity.bound _ _]
def evalBound : Mathlib.Meta.Positivity.PositivityExt where eval {_ _} _ _ e := do
  let (.app (.app _ (Œµ : Q(‚Ñù))) (l : Q(‚Ñï))) ‚Üê whnfR e | throwError "not bound"
  pure (.positive (q(SzemerediRegularity.bound_pos $Œµ $l) : Lean.Expr))

example (Œµ : ‚Ñù) (l : ‚Ñï) : 0 < SzemerediRegularity.bound Œµ l := by positivity

end Tactic
