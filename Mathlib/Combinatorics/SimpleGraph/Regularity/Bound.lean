/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Order.Partition.Equipartition

#align_import combinatorics.simple_graph.regularity.bound from "leanprover-community/mathlib"@"bf7ef0e83e5b7e6c1169e97f055e58a2e4e9d52d"

/-!
# Numerical bounds for SzemerÃ©di Regularity Lemma

This file gathers the numerical facts required by the proof of SzemerÃ©di's regularity lemma.

This entire file is internal to the proof of SzemerÃ©di Regularity Lemma.

## Main declarations

* `SzemerediRegularity.stepBound`: During the inductive step, a partition of size `n` is blown to
  size at most `stepBound n`.
* `SzemerediRegularity.initialBound`: The size of the partition we start the induction with.
* `SzemerediRegularity.bound`: The upper bound on the size of the partition produced by our version
  of SzemerÃ©di's regularity lemma.

## References

[YaÃ«l Dillies, Bhavik Mehta, *Formalising SzemerÃ©diâ€™s Regularity Lemma in Lean*][srl_itp]
-/


open Finset Fintype Function Real

namespace SzemerediRegularity

/-- Auxiliary function for SzemerÃ©di's regularity lemma. Blowing up a partition of size `n` during
the induction results in a partition of size at most `stepBound n`. -/
def stepBound (n : â„•) : â„• :=
  n * 4 ^ n
#align szemeredi_regularity.step_bound SzemerediRegularity.stepBound

theorem le_stepBound : id â‰¤ stepBound := fun n =>
  Nat.le_mul_of_pos_right _ <| pow_pos (by norm_num) n
#align szemeredi_regularity.le_step_bound SzemerediRegularity.le_stepBound

theorem stepBound_mono : Monotone stepBound := fun a b h =>
  Nat.mul_le_mul h <| Nat.pow_le_pow_of_le_right (by norm_num) h
#align szemeredi_regularity.step_bound_mono SzemerediRegularity.stepBound_mono

theorem stepBound_pos_iff {n : â„•} : 0 < stepBound n â†” 0 < n :=
  mul_pos_iff_of_pos_right <| by positivity
#align szemeredi_regularity.step_bound_pos_iff SzemerediRegularity.stepBound_pos_iff

alias âŸ¨_, stepBound_posâŸ© := stepBound_pos_iff
#align szemeredi_regularity.step_bound_pos SzemerediRegularity.stepBound_pos

@[norm_cast] lemma coe_stepBound {Î± : Type*} [Semiring Î±] (n : â„•) :
    (stepBound n : Î±) = n * 4 ^ n := by unfold stepBound; norm_cast

end SzemerediRegularity

open SzemerediRegularity

variable {Î± : Type*} [DecidableEq Î±] [Fintype Î±] {P : Finpartition (univ : Finset Î±)}
  {u : Finset Î±} {Îµ : â„}

local notation3 "m" => (card Î± / stepBound P.parts.card : â„•)

local notation3 "a" => (card Î± / P.parts.card - m * 4 ^ P.parts.card : â„•)

namespace SzemerediRegularity.Positivity

private theorem eps_pos {Îµ : â„} {n : â„•} (h : 100 â‰¤ (4 : â„) ^ n * Îµ ^ 5) : 0 < Îµ :=
  (Odd.pow_pos_iff (by decide)).mp
    (pos_of_mul_pos_right ((show 0 < (100 : â„) by norm_num).trans_le h) (by positivity))

private theorem m_pos [Nonempty Î±] (hPÎ± : P.parts.card * 16 ^ P.parts.card â‰¤ card Î±) : 0 < m :=
  Nat.div_pos ((Nat.mul_le_mul_left _ <| Nat.pow_le_pow_left (by norm_num) _).trans hPÎ±) <|
    stepBound_pos (P.parts_nonempty <| univ_nonempty.ne_empty).card_pos

/-- Local extension for the `positivity` tactic: A few facts that are needed many times for the
proof of SzemerÃ©di's regularity lemma. -/
-- Porting note: positivity extensions must now be global, and this did not seem like a good
-- match for positivity anymore, so I wrote a new tactic (kmill)
scoped macro "sz_positivity" : tactic =>
  `(tactic|
      { try have := m_pos â€¹_â€º
        try have := eps_pos â€¹_â€º
        positivity })

-- Original meta code
/- meta def positivity_szemeredi_regularity : expr â†’ tactic strictness
| `(%%n / step_bound (finpartition.parts %%P).card) := do
    p â† to_expr
      ``((finpartition.parts %%P).card * 16^(finpartition.parts %%P).card â‰¤ %%n)
      >>= find_assumption,
    positive <$> mk_app ``m_pos [p]
| Îµ := do
    typ â† infer_type Îµ,
    unify typ `(â„),
    p â† to_expr ``(100 â‰¤ 4 ^ _ * %%Îµ ^ 5) >>= find_assumption,
    positive <$> mk_app ``eps_pos [p] -/

end SzemerediRegularity.Positivity

namespace SzemerediRegularity

open scoped SzemerediRegularity.Positivity

theorem m_pos [Nonempty Î±] (hPÎ± : P.parts.card * 16 ^ P.parts.card â‰¤ card Î±) : 0 < m := by
  sz_positivity
#align szemeredi_regularity.m_pos SzemerediRegularity.m_pos

theorem coe_m_add_one_pos : 0 < (m : â„) + 1 := by positivity
#align szemeredi_regularity.coe_m_add_one_pos SzemerediRegularity.coe_m_add_one_pos

theorem one_le_m_coe [Nonempty Î±] (hPÎ± : P.parts.card * 16 ^ P.parts.card â‰¤ card Î±) : (1 : â„) â‰¤ m :=
  Nat.one_le_cast.2 <| m_pos hPÎ±
#align szemeredi_regularity.one_le_m_coe SzemerediRegularity.one_le_m_coe

theorem eps_pow_five_pos (hPÎµ : 100 â‰¤ (4 : â„) ^ P.parts.card * Îµ ^ 5) : â†‘0 < Îµ ^ 5 :=
  pos_of_mul_pos_right ((by norm_num : (0 : â„) < 100).trans_le hPÎµ) <| pow_nonneg (by norm_num) _
#align szemeredi_regularity.eps_pow_five_pos SzemerediRegularity.eps_pow_five_pos

theorem eps_pos (hPÎµ : 100 â‰¤ (4 : â„) ^ P.parts.card * Îµ ^ 5) : 0 < Îµ :=
  (Odd.pow_pos_iff (by decide)).mp (eps_pow_five_pos hPÎµ)
#align szemeredi_regularity.eps_pos SzemerediRegularity.eps_pos

theorem hundred_div_Îµ_pow_five_le_m [Nonempty Î±] (hPÎ± : P.parts.card * 16 ^ P.parts.card â‰¤ card Î±)
    (hPÎµ : 100 â‰¤ (4 : â„) ^ P.parts.card * Îµ ^ 5) : 100 / Îµ ^ 5 â‰¤ m :=
  (div_le_of_nonneg_of_le_mul (eps_pow_five_pos hPÎµ).le (by positivity) hPÎµ).trans <| by
    norm_cast
    rwa [Nat.le_div_iff_mul_le' (stepBound_pos (P.parts_nonempty <|
      univ_nonempty.ne_empty).card_pos), stepBound, mul_left_comm, â† mul_pow]
#align szemeredi_regularity.hundred_div_Îµ_pow_five_le_m SzemerediRegularity.hundred_div_Îµ_pow_five_le_m

theorem hundred_le_m [Nonempty Î±] (hPÎ± : P.parts.card * 16 ^ P.parts.card â‰¤ card Î±)
    (hPÎµ : 100 â‰¤ (4 : â„) ^ P.parts.card * Îµ ^ 5) (hÎµ : Îµ â‰¤ 1) : 100 â‰¤ m :=
  mod_cast
    (hundred_div_Îµ_pow_five_le_m hPÎ± hPÎµ).trans'
      (le_div_self (by norm_num) (by sz_positivity) <| pow_le_one _ (by sz_positivity) hÎµ)
#align szemeredi_regularity.hundred_le_m SzemerediRegularity.hundred_le_m

theorem a_add_one_le_four_pow_parts_card : a + 1 â‰¤ 4 ^ P.parts.card := by
  have h : 1 â‰¤ 4 ^ P.parts.card := one_le_pow_of_one_le (by norm_num) _
  rw [stepBound, â† Nat.div_div_eq_div_mul]
  conv_rhs => rw [â† Nat.sub_add_cancel h]
  rw [add_le_add_iff_right, tsub_le_iff_left, â† Nat.add_sub_assoc h]
  exact Nat.le_sub_one_of_lt (Nat.lt_div_mul_add h)
#align szemeredi_regularity.a_add_one_le_four_pow_parts_card SzemerediRegularity.a_add_one_le_four_pow_parts_card

theorem card_auxâ‚ (hucard : u.card = m * 4 ^ P.parts.card + a) :
    (4 ^ P.parts.card - a) * m + a * (m + 1) = u.card := by
  rw [hucard, mul_add, mul_one, â† add_assoc, â† add_mul,
    Nat.sub_add_cancel ((Nat.le_succ _).trans a_add_one_le_four_pow_parts_card), mul_comm]
#align szemeredi_regularity.card_auxâ‚ SzemerediRegularity.card_auxâ‚

theorem card_auxâ‚‚ (hP : P.IsEquipartition) (hu : u âˆˆ P.parts)
    (hucard : Â¬u.card = m * 4 ^ P.parts.card + a) :
    (4 ^ P.parts.card - (a + 1)) * m + (a + 1) * (m + 1) = u.card := by
  have : m * 4 ^ P.parts.card â‰¤ card Î± / P.parts.card := by
    rw [stepBound, â† Nat.div_div_eq_div_mul]
    exact Nat.div_mul_le_self _ _
  rw [Nat.add_sub_of_le this] at hucard
  rw [(hP.card_parts_eq_average hu).resolve_left hucard, mul_add, mul_one, â† add_assoc, â† add_mul,
    Nat.sub_add_cancel a_add_one_le_four_pow_parts_card, â† add_assoc, mul_comm,
    Nat.add_sub_of_le this, card_univ]
#align szemeredi_regularity.card_auxâ‚‚ SzemerediRegularity.card_auxâ‚‚

theorem pow_mul_m_le_card_part (hP : P.IsEquipartition) (hu : u âˆˆ P.parts) :
    (4 : â„) ^ P.parts.card * m â‰¤ u.card := by
  norm_cast
  rw [stepBound, â† Nat.div_div_eq_div_mul]
  exact (Nat.mul_div_le _ _).trans (hP.average_le_card_part hu)
#align szemeredi_regularity.pow_mul_m_le_card_part SzemerediRegularity.pow_mul_m_le_card_part

variable (P Îµ) (l : â„•)

/-- Auxiliary function for SzemerÃ©di's regularity lemma. The size of the partition by which we start
blowing. -/
noncomputable def initialBound : â„• :=
  max 7 <| max l <| âŒŠlog (100 / Îµ ^ 5) / log 4âŒ‹â‚Š + 1
#align szemeredi_regularity.initial_bound SzemerediRegularity.initialBound

theorem le_initialBound : l â‰¤ initialBound Îµ l :=
  (le_max_left _ _).trans <| le_max_right _ _
#align szemeredi_regularity.le_initial_bound SzemerediRegularity.le_initialBound

theorem seven_le_initialBound : 7 â‰¤ initialBound Îµ l :=
  le_max_left _ _
#align szemeredi_regularity.seven_le_initial_bound SzemerediRegularity.seven_le_initialBound

theorem initialBound_pos : 0 < initialBound Îµ l :=
  Nat.succ_pos'.trans_le <| seven_le_initialBound _ _
#align szemeredi_regularity.initial_bound_pos SzemerediRegularity.initialBound_pos

theorem hundred_lt_pow_initialBound_mul {Îµ : â„} (hÎµ : 0 < Îµ) (l : â„•) :
    100 < â†‘4 ^ initialBound Îµ l * Îµ ^ 5 := by
  rw [â† rpow_natCast 4, â† div_lt_iff (pow_pos hÎµ 5), lt_rpow_iff_log_lt _ zero_lt_four, â†
    div_lt_iff, initialBound, Nat.cast_max, Nat.cast_max]
  Â· push_cast
    exact lt_max_of_lt_right (lt_max_of_lt_right <| Nat.lt_floor_add_one _)
  Â· exact log_pos (by norm_num)
  Â· exact div_pos (by norm_num) (pow_pos hÎµ 5)
#align szemeredi_regularity.hundred_lt_pow_initial_bound_mul SzemerediRegularity.hundred_lt_pow_initialBound_mul

/-- An explicit bound on the size of the equipartition whose existence is given by SzemerÃ©di's
regularity lemma. -/
noncomputable def bound : â„• :=
  (stepBound^[âŒŠ4 / Îµ ^ 5âŒ‹â‚Š] <| initialBound Îµ l) *
    16 ^ (stepBound^[âŒŠ4 / Îµ ^ 5âŒ‹â‚Š] <| initialBound Îµ l)
#align szemeredi_regularity.bound SzemerediRegularity.bound

theorem initialBound_le_bound : initialBound Îµ l â‰¤ bound Îµ l :=
  (id_le_iterate_of_id_le le_stepBound _ _).trans <| Nat.le_mul_of_pos_right _ <| by positivity
#align szemeredi_regularity.initial_bound_le_bound SzemerediRegularity.initialBound_le_bound

theorem le_bound : l â‰¤ bound Îµ l :=
  (le_initialBound Îµ l).trans <| initialBound_le_bound Îµ l
#align szemeredi_regularity.le_bound SzemerediRegularity.le_bound

theorem bound_pos : 0 < bound Îµ l :=
  (initialBound_pos Îµ l).trans_le <| initialBound_le_bound Îµ l
#align szemeredi_regularity.bound_pos SzemerediRegularity.bound_pos

variable {Î¹ ğ•œ : Type*} [LinearOrderedField ğ•œ] (r : Î¹ â†’ Î¹ â†’ Prop) [DecidableRel r] {s t : Finset Î¹}
  {x : ğ•œ}

theorem mul_sq_le_sum_sq (hst : s âŠ† t) (f : Î¹ â†’ ğ•œ) (hs : x ^ 2 â‰¤ ((âˆ‘ i âˆˆ s, f i) / s.card) ^ 2)
    (hs' : (s.card : ğ•œ) â‰  0) : (s.card : ğ•œ) * x ^ 2 â‰¤ âˆ‘ i âˆˆ t, f i ^ 2 :=
  (mul_le_mul_of_nonneg_left (hs.trans sum_div_card_sq_le_sum_sq_div_card) <|
    Nat.cast_nonneg _).trans <| (mul_div_cancelâ‚€ _ hs').le.trans <|
      sum_le_sum_of_subset_of_nonneg hst fun _ _ _ => sq_nonneg _
#align szemeredi_regularity.mul_sq_le_sum_sq SzemerediRegularity.mul_sq_le_sum_sq

theorem add_div_le_sum_sq_div_card (hst : s âŠ† t) (f : Î¹ â†’ ğ•œ) (d : ğ•œ) (hx : 0 â‰¤ x)
    (hs : x â‰¤ |(âˆ‘ i âˆˆ s, f i) / s.card - (âˆ‘ i âˆˆ t, f i) / t.card|)
    (ht : d â‰¤ ((âˆ‘ i âˆˆ t, f i) / t.card) ^ 2) :
    d + s.card / t.card * x ^ 2 â‰¤ (âˆ‘ i âˆˆ t, f i ^ 2) / t.card := by
  obtain hscard | hscard := (s.card.cast_nonneg : (0 : ğ•œ) â‰¤ s.card).eq_or_lt
  Â· simpa [â† hscard] using ht.trans sum_div_card_sq_le_sum_sq_div_card
  have htcard : (0 : ğ•œ) < t.card := hscard.trans_le (Nat.cast_le.2 (card_le_card hst))
  have hâ‚ : x ^ 2 â‰¤ ((âˆ‘ i âˆˆ s, f i) / s.card - (âˆ‘ i âˆˆ t, f i) / t.card) ^ 2 :=
    sq_le_sq.2 (by rwa [abs_of_nonneg hx])
  have hâ‚‚ : x ^ 2 â‰¤ ((âˆ‘ i âˆˆ s, (f i - (âˆ‘ j âˆˆ t, f j) / t.card)) / s.card) ^ 2 := by
    apply hâ‚.trans
    rw [sum_sub_distrib, sum_const, nsmul_eq_mul, sub_div, mul_div_cancel_leftâ‚€ _ hscard.ne']
  apply (add_le_add_right ht _).trans
  rw [â† mul_div_right_comm, le_div_iff htcard, add_mul, div_mul_cancelâ‚€ _ htcard.ne']
  have hâ‚ƒ := mul_sq_le_sum_sq hst (fun i => (f i - (âˆ‘ j âˆˆ t, f j) / t.card)) hâ‚‚ hscard.ne'
  apply (add_le_add_left hâ‚ƒ _).trans
  -- Porting note: was
  -- `simp [â† mul_div_right_comm _ (t.card : ğ•œ), sub_div' _ _ _ htcard.ne', â† sum_div, â† add_div,`
  -- `  mul_pow, div_le_iff (sq_pos_of_ne_zero htcard.ne'), sub_sq, sum_add_distrib, â† sum_mul,`
  -- `  â† mul_sum]`
  simp_rw [sub_div' _ _ _ htcard.ne']
  conv_lhs => enter [2, 2, x]; rw [div_pow]
  rw [div_pow, â† sum_div, â† mul_div_right_comm _ (t.card : ğ•œ), â† add_div,
    div_le_iff (sq_pos_of_ne_zero htcard.ne')]
  simp_rw [sub_sq, sum_add_distrib, sum_const, nsmul_eq_mul, sum_sub_distrib, mul_pow, â† sum_mul,
    â† mul_sum, â† sum_mul]
  ring_nf; rfl
#align szemeredi_regularity.add_div_le_sum_sq_div_card SzemerediRegularity.add_div_le_sum_sq_div_card

end SzemerediRegularity

namespace Mathlib.Meta.Positivity

open Lean.Meta Qq

/-- Extension for the `positivity` tactic: `SzemerediRegularity.initialBound` is always positive. -/
@[positivity SzemerediRegularity.initialBound _ _]
def evalInitialBound : PositivityExt where eval {u Î±} _ _ e := do
  match u, Î±, e with
  | 0, ~q(â„•), ~q(SzemerediRegularity.initialBound $Îµ $l) =>
    assertInstancesCommute
    pure (.positive q(SzemerediRegularity.initialBound_pos $Îµ $l))
  | _, _, _ => throwError "not initialBound"


example (Îµ : â„) (l : â„•) : 0 < SzemerediRegularity.initialBound Îµ l := by positivity

/-- Extension for the `positivity` tactic: `SzemerediRegularity.bound` is always positive. -/
@[positivity SzemerediRegularity.bound _ _]
def evalBound : PositivityExt where eval {u Î±} _ _ e := do
  match u, Î±, e with
  | 0, ~q(â„•), ~q(SzemerediRegularity.bound $Îµ $l) =>
    assertInstancesCommute
    pure (.positive q(SzemerediRegularity.bound_pos $Îµ $l))
  | _, _, _ => throwError "not bound"

example (Îµ : â„) (l : â„•) : 0 < SzemerediRegularity.bound Îµ l := by positivity

end Mathlib.Meta.Positivity
