/-
Copyright (c) 2022 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.BigOperators.Field
import Mathlib.Algebra.Order.Chebyshev
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Order.Partition.Equipartition

/-!
# Numerical bounds for Szemer√©di Regularity Lemma

This file gathers the numerical facts required by the proof of Szemer√©di's regularity lemma.

This entire file is internal to the proof of Szemer√©di Regularity Lemma.

## Main declarations

* `SzemerediRegularity.stepBound`: During the inductive step, a partition of size `n` is blown to
  size at most `stepBound n`.
* `SzemerediRegularity.initialBound`: The size of the partition we start the induction with.
* `SzemerediRegularity.bound`: The upper bound on the size of the partition produced by our version
  of Szemer√©di's regularity lemma.

## References

[Ya√´l Dillies, Bhavik Mehta, *Formalising Szemer√©di‚Äôs Regularity Lemma in Lean*][srl_itp]
-/


open Finset Fintype Function Real

namespace SzemerediRegularity

/-- Auxiliary function for Szemer√©di's regularity lemma. Blowing up a partition of size `n` during
the induction results in a partition of size at most `stepBound n`. -/
def stepBound (n : ‚Ñï) : ‚Ñï :=
  n * 4 ^ n

theorem le_stepBound : id ‚â§ stepBound := fun n =>
  Nat.le_mul_of_pos_right _ <| pow_pos (by simp) n

theorem stepBound_mono : Monotone stepBound := fun _ _ h => by unfold stepBound; gcongr; decide

theorem stepBound_pos_iff {n : ‚Ñï} : 0 < stepBound n ‚Üî 0 < n :=
  mul_pos_iff_of_pos_right <| by positivity

alias ‚ü®_, stepBound_pos‚ü© := stepBound_pos_iff

@[norm_cast] lemma coe_stepBound {Œ± : Type*} [Semiring Œ±] (n : ‚Ñï) :
    (stepBound n : Œ±) = n * 4 ^ n := by unfold stepBound; norm_cast

end SzemerediRegularity

open SzemerediRegularity

variable {Œ± : Type*} [DecidableEq Œ±] [Fintype Œ±] {P : Finpartition (univ : Finset Œ±)}
  {u : Finset Œ±} {Œµ : ‚Ñù}

local notation3 "m" => (card Œ± / stepBound #P.parts : ‚Ñï)

local notation3 "a" => (card Œ± / #P.parts - m * 4 ^ #P.parts : ‚Ñï)

namespace SzemerediRegularity.Positivity

private theorem eps_pos {Œµ : ‚Ñù} {n : ‚Ñï} (h : 100 ‚â§ (4 : ‚Ñù) ^ n * Œµ ^ 5) : 0 < Œµ :=
  (Odd.pow_pos_iff (by decide)).mp
    (pos_of_mul_pos_right ((show 0 < (100 : ‚Ñù) by simp).trans_le h) (by positivity))

private theorem m_pos [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) : 0 < m :=
  Nat.div_pos (hPŒ±.trans' <| by unfold stepBound; gcongr; simp) <|
    stepBound_pos (P.parts_nonempty <| univ_nonempty.ne_empty).card_pos

/-- Local extension for the `positivity` tactic: A few facts that are needed many times for the
proof of Szemer√©di's regularity lemma. -/
scoped macro "sz_positivity" : tactic =>
  `(tactic|
      { try have := m_pos ‚Äπ_‚Ä∫
        try have := eps_pos ‚Äπ_‚Ä∫
        positivity })

-- Original meta code
/- meta def positivity_szemeredi_regularity : expr ‚Üí tactic strictness
| `(%%n / step_bound (finpartition.parts %%P).card) := do
    p ‚Üê to_expr
      ``((finpartition.parts %%P).card * 16^(finpartition.parts %%P).card ‚â§ %%n)
      >>= find_assumption,
    positive <$> mk_app ``m_pos [p]
| Œµ := do
    typ ‚Üê infer_type Œµ,
    unify typ `(‚Ñù),
    p ‚Üê to_expr ``(100 ‚â§ 4 ^ _ * %%Œµ ^ 5) >>= find_assumption,
    positive <$> mk_app ``eps_pos [p] -/

end SzemerediRegularity.Positivity

namespace SzemerediRegularity

open scoped SzemerediRegularity.Positivity

theorem m_pos [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) : 0 < m := by
  sz_positivity

theorem coe_m_add_one_pos : 0 < (m : ‚Ñù) + 1 := by positivity

theorem one_le_m_coe [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) : (1 : ‚Ñù) ‚â§ m :=
  Nat.one_le_cast.2 <| m_pos hPŒ±

theorem eps_pow_five_pos (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ #P.parts * Œµ ^ 5) : ‚Üë0 < Œµ ^ 5 :=
  pos_of_mul_pos_right ((by simp : (0 : ‚Ñù) < 100).trans_le hPŒµ) <| by positivity

theorem eps_pos (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ #P.parts * Œµ ^ 5) : 0 < Œµ :=
  (Odd.pow_pos_iff (by decide)).mp (eps_pow_five_pos hPŒµ)

theorem hundred_div_Œµ_pow_five_le_m [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±)
    (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ #P.parts * Œµ ^ 5) : 100 / Œµ ^ 5 ‚â§ m :=
  (div_le_of_le_mul‚ÇÄ (eps_pow_five_pos hPŒµ).le (by positivity) hPŒµ).trans <| by
    norm_cast
    rwa [Nat.le_div_iff_mul_le (stepBound_pos (P.parts_nonempty <|
      univ_nonempty.ne_empty).card_pos), stepBound, mul_left_comm, ‚Üê mul_pow]

theorem hundred_le_m [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±)
    (hPŒµ : 100 ‚â§ (4 : ‚Ñù) ^ #P.parts * Œµ ^ 5) (hŒµ : Œµ ‚â§ 1) : 100 ‚â§ m :=
  mod_cast
    (hundred_div_Œµ_pow_five_le_m hPŒ± hPŒµ).trans'
      (le_div_self (by simp) (by sz_positivity) <| pow_le_one‚ÇÄ (by sz_positivity) hŒµ)

theorem a_add_one_le_four_pow_parts_card : a + 1 ‚â§ 4 ^ #P.parts := by
  have h : 1 ‚â§ 4 ^ #P.parts := one_le_pow‚ÇÄ (by simp)
  rw [stepBound, ‚Üê Nat.div_div_eq_div_mul]
  conv_rhs => rw [‚Üê Nat.sub_add_cancel h]
  rw [add_le_add_iff_right, tsub_le_iff_left, ‚Üê Nat.add_sub_assoc h]
  exact Nat.le_sub_one_of_lt (Nat.lt_div_mul_add h)

theorem card_aux‚ÇÅ (hucard : #u = m * 4 ^ #P.parts + a) :
    (4 ^ #P.parts - a) * m + a * (m + 1) = #u := by
  rw [hucard, mul_add, mul_one, ‚Üê add_assoc, ‚Üê add_mul,
    Nat.sub_add_cancel ((Nat.le_succ _).trans a_add_one_le_four_pow_parts_card), mul_comm]

theorem card_aux‚ÇÇ (hP : P.IsEquipartition) (hu : u ‚àà P.parts) (hucard : #u ‚â† m * 4 ^ #P.parts + a) :
    (4 ^ #P.parts - (a + 1)) * m + (a + 1) * (m + 1) = #u := by
  have : m * 4 ^ #P.parts ‚â§ card Œ± / #P.parts := by
    rw [stepBound, ‚Üê Nat.div_div_eq_div_mul]
    exact Nat.div_mul_le_self _ _
  rw [Nat.add_sub_of_le this] at hucard
  rw [(hP.card_parts_eq_average hu).resolve_left hucard, mul_add, mul_one, ‚Üê add_assoc, ‚Üê add_mul,
    Nat.sub_add_cancel a_add_one_le_four_pow_parts_card, ‚Üê add_assoc, mul_comm,
    Nat.add_sub_of_le this, card_univ]

theorem pow_mul_m_le_card_part (hP : P.IsEquipartition) (hu : u ‚àà P.parts) :
    (4 : ‚Ñù) ^ #P.parts * m ‚â§ #u := by
  norm_cast
  rw [stepBound, ‚Üê Nat.div_div_eq_div_mul]
  exact (Nat.mul_div_le _ _).trans (hP.average_le_card_part hu)

variable (P Œµ) (l : ‚Ñï)

/-- Auxiliary function for Szemer√©di's regularity lemma. The size of the partition by which we start
blowing. -/
noncomputable def initialBound : ‚Ñï :=
  max 7 <| max l <| ‚åälog (100 / Œµ ^ 5) / log 4‚åã‚Çä + 1

theorem le_initialBound : l ‚â§ initialBound Œµ l :=
  (le_max_left _ _).trans <| le_max_right _ _

theorem seven_le_initialBound : 7 ‚â§ initialBound Œµ l :=
  le_max_left _ _

theorem initialBound_pos : 0 < initialBound Œµ l :=
  Nat.succ_pos'.trans_le <| seven_le_initialBound _ _

theorem hundred_lt_pow_initialBound_mul {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (l : ‚Ñï) :
    100 < ‚Üë4 ^ initialBound Œµ l * Œµ ^ 5 := by
  rw [‚Üê rpow_natCast 4, ‚Üê div_lt_iff‚ÇÄ (pow_pos hŒµ 5), lt_rpow_iff_log_lt _ zero_lt_four, ‚Üê
    div_lt_iff‚ÇÄ, initialBound, Nat.cast_max, Nat.cast_max]
  ¬∑ push_cast
    exact lt_max_of_lt_right (lt_max_of_lt_right <| Nat.lt_floor_add_one _)
  ¬∑ exact log_pos (by simp)
  ¬∑ exact div_pos (by simp) (pow_pos hŒµ 5)

/-- An explicit bound on the size of the equipartition whose existence is given by Szemer√©di's
regularity lemma. -/
noncomputable def bound : ‚Ñï :=
  (stepBound^[‚åä4 / Œµ ^ 5‚åã‚Çä] <| initialBound Œµ l) *
    16 ^ (stepBound^[‚åä4 / Œµ ^ 5‚åã‚Çä] <| initialBound Œµ l)

theorem initialBound_le_bound : initialBound Œµ l ‚â§ bound Œµ l :=
  (id_le_iterate_of_id_le le_stepBound _ _).trans <| Nat.le_mul_of_pos_right _ <| by positivity

theorem le_bound : l ‚â§ bound Œµ l :=
  (le_initialBound Œµ l).trans <| initialBound_le_bound Œµ l

theorem bound_pos : 0 < bound Œµ l :=
  (initialBound_pos Œµ l).trans_le <| initialBound_le_bound Œµ l

variable {Œπ ùïú : Type*} [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú] {s t : Finset Œπ} {x : ùïú}

theorem mul_sq_le_sum_sq (hst : s ‚äÜ t) (f : Œπ ‚Üí ùïú) (hs : x ^ 2 ‚â§ ((‚àë i ‚àà s, f i) / #s) ^ 2)
    (hs' : (#s : ùïú) ‚â† 0) : (#s : ùïú) * x ^ 2 ‚â§ ‚àë i ‚àà t, f i ^ 2 := calc
  _ ‚â§ (#s : ùïú) * ((‚àë i ‚àà s, f i ^ 2) / #s) := by
    gcongr
    exact hs.trans sum_div_card_sq_le_sum_sq_div_card
  _ = ‚àë i ‚àà s, f i ^ 2 := mul_div_cancel‚ÇÄ _ hs'
  _ ‚â§ ‚àë i ‚àà t, f i ^ 2 := by gcongr

theorem add_div_le_sum_sq_div_card (hst : s ‚äÜ t) (f : Œπ ‚Üí ùïú) (d : ùïú) (hx : 0 ‚â§ x)
    (hs : x ‚â§ |(‚àë i ‚àà s, f i) / #s - (‚àë i ‚àà t, f i) / #t|) (ht : d ‚â§ ((‚àë i ‚àà t, f i) / #t) ^ 2) :
    d + #s / #t * x ^ 2 ‚â§ (‚àë i ‚àà t, f i ^ 2) / #t := by
  obtain hscard | hscard := ((#s).cast_nonneg : (0 : ùïú) ‚â§ #s).eq_or_lt
  ¬∑ simpa [‚Üê hscard] using ht.trans sum_div_card_sq_le_sum_sq_div_card
  have htcard : (0 : ùïú) < #t := hscard.trans_le (Nat.cast_le.2 (card_le_card hst))
  have h‚ÇÅ : x ^ 2 ‚â§ ((‚àë i ‚àà s, f i) / #s - (‚àë i ‚àà t, f i) / #t) ^ 2 :=
    sq_le_sq.2 (by rwa [abs_of_nonneg hx])
  have h‚ÇÇ : x ^ 2 ‚â§ ((‚àë i ‚àà s, (f i - (‚àë j ‚àà t, f j) / #t)) / #s) ^ 2 := by
    apply h‚ÇÅ.trans
    rw [sum_sub_distrib, sum_const, nsmul_eq_mul, sub_div, mul_div_cancel_left‚ÇÄ _ hscard.ne']
  apply (add_le_add_right ht _).trans
  rw [‚Üê mul_div_right_comm, le_div_iff‚ÇÄ htcard, add_mul, div_mul_cancel‚ÇÄ _ htcard.ne']
  have h‚ÇÉ := mul_sq_le_sum_sq hst (fun i => (f i - (‚àë j ‚àà t, f j) / #t)) h‚ÇÇ hscard.ne'
  apply (add_le_add_left h‚ÇÉ _).trans
  simp only [sub_div' htcard.ne', div_pow, ‚Üê sum_div, ‚Üê mul_div_right_comm _ (#t : ùïú), ‚Üê add_div,
    div_le_iff‚ÇÄ (sq_pos_of_ne_zero htcard.ne'), sub_sq, sum_add_distrib, sum_const,
    sum_sub_distrib, mul_pow, ‚Üê sum_mul, nsmul_eq_mul, two_mul]
  ring_nf
  rfl

end SzemerediRegularity

namespace Mathlib.Meta.Positivity

open Lean.Meta Qq

/-- Extension for the `positivity` tactic: `SzemerediRegularity.initialBound` is always positive. -/
@[positivity SzemerediRegularity.initialBound _ _]
def evalInitialBound : PositivityExt where eval {u Œ±} _ _ e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñï), ~q(SzemerediRegularity.initialBound $Œµ $l) =>
    assertInstancesCommute
    pure (.positive q(SzemerediRegularity.initialBound_pos $Œµ $l))
  | _, _, _ => throwError "not initialBound"


example (Œµ : ‚Ñù) (l : ‚Ñï) : 0 < SzemerediRegularity.initialBound Œµ l := by positivity

/-- Extension for the `positivity` tactic: `SzemerediRegularity.bound` is always positive. -/
@[positivity SzemerediRegularity.bound _ _]
def evalBound : PositivityExt where eval {u Œ±} _ _ e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñï), ~q(SzemerediRegularity.bound $Œµ $l) =>
    assertInstancesCommute
    pure (.positive q(SzemerediRegularity.bound_pos $Œµ $l))
  | _, _, _ => throwError "not bound"

example (Œµ : ‚Ñù) (l : ‚Ñï) : 0 < SzemerediRegularity.bound Œµ l := by positivity

end Mathlib.Meta.Positivity
