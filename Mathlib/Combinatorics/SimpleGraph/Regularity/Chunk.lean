/-
Copyright (c) 2022 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import Mathlib.Combinatorics.SimpleGraph.Regularity.Bound
import Mathlib.Combinatorics.SimpleGraph.Regularity.Equitabilise
import Mathlib.Combinatorics.SimpleGraph.Regularity.Uniform

/-!
# Chunk of the increment partition for Szemer√©di Regularity Lemma

In the proof of Szemer√©di Regularity Lemma, we need to partition each part of a starting partition
to increase the energy. This file defines those partitions of parts and shows that they locally
increase the energy.

This entire file is internal to the proof of Szemer√©di Regularity Lemma.

## Main declarations

* `SzemerediRegularity.chunk`: The partition of a part of the starting partition.
* `SzemerediRegularity.edgeDensity_chunk_uniform`: `chunk` does not locally decrease the edge
  density between uniform parts too much.
* `SzemerediRegularity.edgeDensity_chunk_not_uniform`: `chunk` locally increases the edge density
  between non-uniform parts.

## TODO

Once ported to mathlib4, this file will be a great golfing ground for Heather's new tactic
`gcongr`.

## References

[Ya√´l Dillies, Bhavik Mehta, *Formalising Szemer√©di‚Äôs Regularity Lemma in Lean*][srl_itp]
-/


open Finpartition Finset Fintype Rel Nat
open scoped SzemerediRegularity.Positivity

namespace SzemerediRegularity

variable {Œ± : Type*} [Fintype Œ±] [DecidableEq Œ±] {P : Finpartition (univ : Finset Œ±)}
  (hP : P.IsEquipartition) (G : SimpleGraph Œ±) [DecidableRel G.Adj] (Œµ : ‚Ñù) {U : Finset Œ±}
  (hU : U ‚àà P.parts) (V : Finset Œ±)

local notation3 "m" => (card Œ± / stepBound #P.parts : ‚Ñï)

/-!
### Definitions

We define `chunk`, the partition of a part, and `star`, the sets of parts of `chunk` that are
contained in the corresponding witness of non-uniformity.
-/


/-- The portion of `SzemerediRegularity.increment` which partitions `U`. -/
noncomputable def chunk : Finpartition U :=
  if hUcard : #U = m * 4 ^ #P.parts + (card Œ± / #P.parts - m * 4 ^ #P.parts) then
    (atomise U <| P.nonuniformWitnesses G Œµ U).equitabilise <| card_aux‚ÇÅ hUcard
  else (atomise U <| P.nonuniformWitnesses G Œµ U).equitabilise <| card_aux‚ÇÇ hP hU hUcard

-- `hP` and `hU` are used to get that `U` has size
-- `m * 4 ^ #P.parts + a or m * 4 ^ #P.parts + a + 1`
/-- The portion of `SzemerediRegularity.chunk` which is contained in the witness of non-uniformity
of `U` and `V`. -/
noncomputable def star (V : Finset Œ±) : Finset (Finset Œ±) :=
  {A ‚àà (chunk hP G Œµ hU).parts | A ‚äÜ G.nonuniformWitness Œµ U V}

/-!
### Density estimates

We estimate the density between parts of `chunk`.
-/


theorem biUnion_star_subset_nonuniformWitness :
    (star hP G Œµ hU V).biUnion id ‚äÜ G.nonuniformWitness Œµ U V :=
  biUnion_subset_iff_forall_subset.2 fun _ hA ‚Ü¶ (mem_filter.1 hA).2

variable {hP G Œµ hU V} {ùíú : Finset (Finset Œ±)} {s : Finset Œ±}

theorem star_subset_chunk : star hP G Œµ hU V ‚äÜ (chunk hP G Œµ hU).parts :=
  filter_subset _ _

private theorem card_nonuniformWitness_sdiff_biUnion_star (hV : V ‚àà P.parts) (hUV : U ‚â† V)
    (h‚ÇÇ : ¬¨G.IsUniform Œµ U V) :
    #(G.nonuniformWitness Œµ U V \ (star hP G Œµ hU V).biUnion id) ‚â§ 2 ^ (#P.parts - 1) * m := by
  have hX : G.nonuniformWitness Œµ U V ‚àà P.nonuniformWitnesses G Œµ U :=
    nonuniformWitness_mem_nonuniformWitnesses h‚ÇÇ hV hUV
  have q : G.nonuniformWitness Œµ U V \ (star hP G Œµ hU V).biUnion id ‚äÜ
      {B ‚àà (atomise U <| P.nonuniformWitnesses G Œµ U).parts |
        B ‚äÜ G.nonuniformWitness Œµ U V ‚àß B.Nonempty}.biUnion
        fun B ‚Ü¶ B \ {A ‚àà (chunk hP G Œµ hU).parts | A ‚äÜ B}.biUnion id := by
    intro x hx
    rw [‚Üê biUnion_filter_atomise hX (G.nonuniformWitness_subset h‚ÇÇ), star, mem_sdiff,
      mem_biUnion] at hx
    simp only [not_exists, mem_biUnion, and_imp, mem_filter,
      not_and, mem_sdiff, id, mem_sdiff] at hx ‚ä¢
    obtain ‚ü®‚ü®B, hB‚ÇÅ, hB‚ÇÇ‚ü©, hx‚ü© := hx
    exact ‚ü®B, hB‚ÇÅ, hB‚ÇÇ, fun A hA AB ‚Ü¶ hx A hA <| AB.trans hB‚ÇÅ.2.1‚ü©
  apply (card_le_card q).trans (card_biUnion_le.trans _)
  trans ‚àë B ‚àà (atomise U <| P.nonuniformWitnesses G Œµ U).parts with
    B ‚äÜ G.nonuniformWitness Œµ U V ‚àß B.Nonempty, m
  ¬∑ suffices ‚àÄ B ‚àà (atomise U <| P.nonuniformWitnesses G Œµ U).parts,
        #(B \ {A ‚àà (chunk hP G Œµ hU).parts | A ‚äÜ B}.biUnion id) ‚â§ m by
      exact sum_le_sum fun B hB ‚Ü¶ this B <| filter_subset _ _ hB
    intro B hB
    unfold chunk
    split_ifs with h‚ÇÅ
    ¬∑ convert card_parts_equitabilise_subset_le _ (card_aux‚ÇÅ h‚ÇÅ) hB
    ¬∑ convert card_parts_equitabilise_subset_le _ (card_aux‚ÇÇ hP hU h‚ÇÅ) hB
  rw [sum_const]
  refine mul_le_mul_right' ?_ _
  have t := card_filter_atomise_le_two_pow (s := U) hX
  refine t.trans (pow_right_mono‚ÇÄ (by simp) <| tsub_le_tsub_right ?_ _)
  exact card_image_le.trans (card_le_card <| filter_subset _ _)

private theorem one_sub_eps_mul_card_nonuniformWitness_le_card_star (hV : V ‚àà P.parts)
    (hUV : U ‚â† V) (hunif : ¬¨G.IsUniform Œµ U V) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    (hŒµ‚ÇÅ : Œµ ‚â§ 1) :
    (1 - Œµ / 10) * #(G.nonuniformWitness Œµ U V) ‚â§ #((star hP G Œµ hU V).biUnion id) := by
  have hP‚ÇÅ : 0 < #P.parts := Finset.card_pos.2 ‚ü®_, hU‚ü©
  have : (‚Üë2 ^ #P.parts : ‚Ñù) * m / (#U * Œµ) ‚â§ Œµ / 10 := by
    rw [‚Üê div_div, div_le_iff‚ÇÄ']
    swap
    ¬∑ sz_positivity
    refine le_of_mul_le_mul_left ?_ (pow_pos zero_lt_two #P.parts)
    calc
      ‚Üë2 ^ #P.parts * ((‚Üë2 ^ #P.parts * m : ‚Ñù) / #U) =
          ((2 : ‚Ñù) * 2) ^ #P.parts * m / #U := by
        rw [mul_pow, ‚Üê mul_div_assoc, mul_assoc]
      _ = ‚Üë4 ^ #P.parts * m / #U := by norm_num
      _ ‚â§ 1 := div_le_one_of_le‚ÇÄ (pow_mul_m_le_card_part hP hU) (cast_nonneg _)
      _ ‚â§ ‚Üë2 ^ #P.parts * Œµ ^ 2 / 10 := by
        refine (one_le_sq_iff‚ÇÄ <| by positivity).1 ?_
        rw [div_pow, mul_pow, pow_right_comm, ‚Üê pow_mul Œµ, one_le_div (by positivity)]
        calc
          (‚Üë10 ^ 2) = 100 := by norm_num
          _ ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5 := hPŒµ
          _ ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 4 := by
            gcongr _ * ?_
            exact pow_le_pow_of_le_one (by sz_positivity) hŒµ‚ÇÅ (by decide)
          _ = (‚Üë2 ^ 2) ^ #P.parts * Œµ ^ (2 * 2) := by norm_num
      _ = ‚Üë2 ^ #P.parts * (Œµ * (Œµ / 10)) := by rw [mul_div_assoc, sq, mul_div_assoc]
  calc
    (‚Üë1 - Œµ / 10) * #(G.nonuniformWitness Œµ U V) ‚â§
        (‚Üë1 - ‚Üë2 ^ #P.parts * m / (#U * Œµ)) * #(G.nonuniformWitness Œµ U V) := by gcongr
    _ = #(G.nonuniformWitness Œµ U V) -
        ‚Üë2 ^ #P.parts * m / (#U * Œµ) * #(G.nonuniformWitness Œµ U V) := by
      rw [sub_mul, one_mul]
    _ ‚â§ #(G.nonuniformWitness Œµ U V) - ‚Üë2 ^ (#P.parts - 1) * m := by
      refine sub_le_sub_left ?_ _
      have : (2 : ‚Ñù) ^ #P.parts = ‚Üë2 ^ (#P.parts - 1) * 2 := by
        rw [‚Üê _root_.pow_succ, tsub_add_cancel_of_le (succ_le_iff.2 hP‚ÇÅ)]
      rw [‚Üê mul_div_right_comm, this, mul_right_comm _ (2 : ‚Ñù), mul_assoc, le_div_iff‚ÇÄ]
      ¬∑ gcongr _ * ?_
        exact (G.le_card_nonuniformWitness hunif).trans
          (le_mul_of_one_le_left (cast_nonneg _) one_le_two)
      have := Finset.card_pos.mpr (P.nonempty_of_mem_parts hU)
      sz_positivity
    _ ‚â§ #((star hP G Œµ hU V).biUnion id) := by
      rw [sub_le_comm, ‚Üê
        cast_sub (card_le_card <| biUnion_star_subset_nonuniformWitness hP G Œµ hU V), ‚Üê
        card_sdiff (biUnion_star_subset_nonuniformWitness hP G Œµ hU V)]
      exact mod_cast card_nonuniformWitness_sdiff_biUnion_star hV hUV hunif

/-! ### `chunk` -/


theorem card_chunk (hm : m ‚â† 0) : #(chunk hP G Œµ hU).parts = 4 ^ #P.parts := by
  unfold chunk
  split_ifs
  ¬∑ rw [card_parts_equitabilise _ _ hm, tsub_add_cancel_of_le]
    exact le_of_lt a_add_one_le_four_pow_parts_card
  ¬∑ rw [card_parts_equitabilise _ _ hm, tsub_add_cancel_of_le a_add_one_le_four_pow_parts_card]

theorem card_eq_of_mem_parts_chunk (hs : s ‚àà (chunk hP G Œµ hU).parts) :
    #s = m ‚à® #s = m + 1 := by
  unfold chunk at hs
  split_ifs at hs <;> exact card_eq_of_mem_parts_equitabilise hs

theorem m_le_card_of_mem_chunk_parts (hs : s ‚àà (chunk hP G Œµ hU).parts) : m ‚â§ #s :=
  (card_eq_of_mem_parts_chunk hs).elim ge_of_eq fun i ‚Ü¶ by simp [i]

theorem card_le_m_add_one_of_mem_chunk_parts (hs : s ‚àà (chunk hP G Œµ hU).parts) : #s ‚â§ m + 1 :=
  (card_eq_of_mem_parts_chunk hs).elim (fun i ‚Ü¶ by simp [i]) fun i ‚Ü¶ i.le

theorem card_biUnion_star_le_m_add_one_card_star_mul :
    (#((star hP G Œµ hU V).biUnion id) : ‚Ñù) ‚â§ #(star hP G Œµ hU V) * (m + 1) :=
  mod_cast card_biUnion_le_card_mul _ _ _ fun _ hs ‚Ü¶
    card_le_m_add_one_of_mem_chunk_parts <| star_subset_chunk hs

private theorem le_sum_card_subset_chunk_parts (hùíú : ùíú ‚äÜ (chunk hP G Œµ hU).parts) (hs : s ‚àà ùíú) :
    (#ùíú : ‚Ñù) * #s * (m / (m + 1)) ‚â§ #(ùíú.sup id) := by
  rw [mul_div_assoc', div_le_iff‚ÇÄ coe_m_add_one_pos, mul_right_comm]
  gcongr
  ¬∑ rw [‚Üê (ofSubset _ hùíú rfl).sum_card_parts, ofSubset_parts, ‚Üê cast_mul, cast_le]
    exact card_nsmul_le_sum _ _ _ fun x hx ‚Ü¶ m_le_card_of_mem_chunk_parts <| hùíú hx
  ¬∑ exact mod_cast card_le_m_add_one_of_mem_chunk_parts (hùíú hs)

private theorem sum_card_subset_chunk_parts_le (m_pos : (0 : ‚Ñù) < m)
    (hùíú : ùíú ‚äÜ (chunk hP G Œµ hU).parts) (hs : s ‚àà ùíú) :
    (#(ùíú.sup id) : ‚Ñù) ‚â§ #ùíú * #s * ((m + 1) / m) := by
  rw [sup_eq_biUnion, mul_div_assoc', le_div_iff‚ÇÄ m_pos, mul_right_comm]
  gcongr
  ¬∑ norm_cast
    refine card_biUnion_le_card_mul _ _ _ fun x hx ‚Ü¶ ?_
    apply card_le_m_add_one_of_mem_chunk_parts (hùíú hx)
  ¬∑ exact mod_cast m_le_card_of_mem_chunk_parts (hùíú hs)

private theorem one_sub_le_m_div_m_add_one_sq [Nonempty Œ±]
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5) :
    ‚Üë1 - Œµ ^ 5 / ‚Üë50 ‚â§ (m / (m + 1 : ‚Ñù)) ^ 2 := by
  have : (m : ‚Ñù) / (m + 1) = 1 - 1 / (m + 1) := by
    rw [one_sub_div coe_m_add_one_pos.ne', add_sub_cancel_right]
  rw [this, sub_sq, one_pow, mul_one]
  refine le_trans ?_ (le_add_of_nonneg_right <| sq_nonneg _)
  rw [sub_le_sub_iff_left, ‚Üê le_div_iff‚ÇÄ' (show (0 : ‚Ñù) < 2 by norm_num), div_div,
    one_div_le coe_m_add_one_pos, one_div_div]
  ¬∑ refine le_trans ?_ (le_add_of_nonneg_right zero_le_one)
    norm_num
    apply hundred_div_Œµ_pow_five_le_m hPŒ± hPŒµ
  sz_positivity

private theorem m_add_one_div_m_le_one_add [Nonempty Œ±]
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5) (hŒµ‚ÇÅ : Œµ ‚â§ 1) :
    ((m + 1 : ‚Ñù) / m) ^ 2 ‚â§ ‚Üë1 + Œµ ^ 5 / 49 := by
  have : 0 ‚â§ Œµ := by sz_positivity
  rw [same_add_div (by sz_positivity)]
  calc
    _ ‚â§ (1 + Œµ ^ 5 / 100) ^ 2 := by
      gcongr (1 + ?_) ^ 2
      rw [‚Üê one_div_div (100 : ‚Ñù)]
      exact one_div_le_one_div_of_le (by sz_positivity) (hundred_div_Œµ_pow_five_le_m hPŒ± hPŒµ)
    _ = 1 + Œµ ^ 5 * (50‚Åª¬π + Œµ ^ 5 / 10000) := by ring
    _ ‚â§ 1 + Œµ ^ 5 * (50‚Åª¬π + 1 ^ 5 / 10000) := by gcongr
    _ ‚â§ 1 + Œµ ^ 5 * 49‚Åª¬π := by gcongr; norm_num
    _ = 1 + Œµ ^ 5 / 49 := by rw [div_eq_mul_inv]

private theorem density_sub_eps_le_sum_density_div_card [Nonempty Œ±]
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    {hU : U ‚àà P.parts} {hV : V ‚àà P.parts} {A B : Finset (Finset Œ±)}
    (hA : A ‚äÜ (chunk hP G Œµ hU).parts) (hB : B ‚äÜ (chunk hP G Œµ hV).parts) :
    (G.edgeDensity (A.biUnion id) (B.biUnion id)) - Œµ ^ 5 / 50 ‚â§
    (‚àë ab ‚àà A.product B, (G.edgeDensity ab.1 ab.2 : ‚Ñù)) / (#A * #B) := by
  have : ‚Üë(G.edgeDensity (A.biUnion id) (B.biUnion id)) - Œµ ^ 5 / ‚Üë50 ‚â§
      (‚Üë1 - Œµ ^ 5 / 50) * G.edgeDensity (A.biUnion id) (B.biUnion id) := by
    rw [sub_mul, one_mul, sub_le_sub_iff_left]
    refine mul_le_of_le_one_right (by sz_positivity) ?_
    exact mod_cast G.edgeDensity_le_one _ _
  refine this.trans ?_
  conv_rhs => -- Porting note: LHS and RHS need separate treatment to get the desired form
    simp only [SimpleGraph.edgeDensity_def, sum_div, Rat.cast_div, div_div]
  conv_lhs =>
    rw [SimpleGraph.edgeDensity_def, SimpleGraph.interedges, ‚Üê sup_eq_biUnion, ‚Üê sup_eq_biUnion,
      Rel.card_interedges_finpartition _ (ofSubset _ hA rfl) (ofSubset _ hB rfl), ofSubset_parts,
      ofSubset_parts]
    simp only [cast_sum, sum_div, mul_sum, Rat.cast_sum, Rat.cast_div,
      mul_div_left_comm ((1 : ‚Ñù) - _)]
  push_cast
  apply sum_le_sum
  simp only [and_imp, Prod.forall, mem_product]
  rintro x y hx hy
  rw [mul_mul_mul_comm, mul_comm (#x : ‚Ñù), mul_comm (#y : ‚Ñù), le_div_iff‚ÇÄ, mul_assoc]
  ¬∑ refine mul_le_of_le_one_right (cast_nonneg _) ?_
    rw [div_mul_eq_mul_div, ‚Üê mul_assoc, mul_assoc]
    refine div_le_one_of_le‚ÇÄ ?_ (by positivity)
    refine (mul_le_mul_of_nonneg_right (one_sub_le_m_div_m_add_one_sq hPŒ± hPŒµ) ?_).trans ?_
    ¬∑ exact mod_cast _root_.zero_le _
    rw [sq, mul_mul_mul_comm, mul_comm ((m : ‚Ñù) / _), mul_comm ((m : ‚Ñù) / _)]
    gcongr
    ¬∑ apply le_sum_card_subset_chunk_parts hA hx
    ¬∑ apply le_sum_card_subset_chunk_parts hB hy
  refine mul_pos (mul_pos ?_ ?_) (mul_pos ?_ ?_) <;> rw [cast_pos, Finset.card_pos]
  exacts [‚ü®_, hx‚ü©, nonempty_of_mem_parts _ (hA hx), ‚ü®_, hy‚ü©, nonempty_of_mem_parts _ (hB hy)]

private theorem sum_density_div_card_le_density_add_eps [Nonempty Œ±]
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    (hŒµ‚ÇÅ : Œµ ‚â§ 1) {hU : U ‚àà P.parts} {hV : V ‚àà P.parts} {A B : Finset (Finset Œ±)}
    (hA : A ‚äÜ (chunk hP G Œµ hU).parts) (hB : B ‚äÜ (chunk hP G Œµ hV).parts) :
    (‚àë ab ‚àà A.product B, G.edgeDensity ab.1 ab.2 : ‚Ñù) / (#A * #B) ‚â§
    G.edgeDensity (A.biUnion id) (B.biUnion id) + Œµ ^ 5 / 49 := by
  have : (‚Üë1 + Œµ ^ 5 / ‚Üë49) * G.edgeDensity (A.biUnion id) (B.biUnion id) ‚â§
      G.edgeDensity (A.biUnion id) (B.biUnion id) + Œµ ^ 5 / 49 := by
    rw [add_mul, one_mul, add_le_add_iff_left]
    refine mul_le_of_le_one_right (by sz_positivity) ?_
    exact mod_cast G.edgeDensity_le_one _ _
  refine le_trans ?_ this
  conv_lhs => -- Porting note: LHS and RHS need separate treatment to get the desired form
    simp only [SimpleGraph.edgeDensity, edgeDensity, sum_div, Rat.cast_div, div_div]
  conv_rhs =>
    rw [SimpleGraph.edgeDensity, edgeDensity, ‚Üê sup_eq_biUnion, ‚Üê sup_eq_biUnion,
      Rel.card_interedges_finpartition _ (ofSubset _ hA rfl) (ofSubset _ hB rfl)]
    simp only [cast_sum, mul_sum, sum_div, Rat.cast_sum, Rat.cast_div,
      mul_div_left_comm ((1 : ‚Ñù) + _)]
  push_cast
  apply sum_le_sum
  simp only [and_imp, Prod.forall, mem_product, show A.product B = A √óÀ¢ B by rfl]
  intro x y hx hy
  rw [mul_mul_mul_comm, mul_comm (#x : ‚Ñù), mul_comm (#y : ‚Ñù), div_le_iff‚ÇÄ, mul_assoc]
  ¬∑ refine le_mul_of_one_le_right (cast_nonneg _) ?_
    rw [div_mul_eq_mul_div, one_le_div]
    ¬∑ refine le_trans ?_ (mul_le_mul_of_nonneg_right (m_add_one_div_m_le_one_add hPŒ± hPŒµ hŒµ‚ÇÅ) ?_)
      ¬∑ rw [sq, mul_mul_mul_comm, mul_comm (_ / (m : ‚Ñù)), mul_comm (_ / (m : ‚Ñù))]
        gcongr
        exacts [sum_card_subset_chunk_parts_le (by sz_positivity) hA hx,
          sum_card_subset_chunk_parts_le (by sz_positivity) hB hy]
      ¬∑ exact mod_cast _root_.zero_le _
    rw [‚Üê cast_mul, cast_pos]
    apply mul_pos <;> rw [Finset.card_pos, sup_eq_biUnion, biUnion_nonempty]
    ¬∑ exact ‚ü®_, hx, nonempty_of_mem_parts _ (hA hx)‚ü©
    ¬∑ exact ‚ü®_, hy, nonempty_of_mem_parts _ (hB hy)‚ü©
  refine mul_pos (mul_pos ?_ ?_) (mul_pos ?_ ?_) <;> rw [cast_pos, Finset.card_pos]
  exacts [‚ü®_, hx‚ü©, nonempty_of_mem_parts _ (hA hx), ‚ü®_, hy‚ü©, nonempty_of_mem_parts _ (hB hy)]

private theorem average_density_near_total_density [Nonempty Œ±]
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    (hŒµ‚ÇÅ : Œµ ‚â§ 1) {hU : U ‚àà P.parts} {hV : V ‚àà P.parts} {A B : Finset (Finset Œ±)}
    (hA : A ‚äÜ (chunk hP G Œµ hU).parts) (hB : B ‚äÜ (chunk hP G Œµ hV).parts) :
    |(‚àë ab ‚àà A.product B, G.edgeDensity ab.1 ab.2 : ‚Ñù) / (#A * #B) -
      G.edgeDensity (A.biUnion id) (B.biUnion id)| ‚â§ Œµ ^ 5 / 49 := by
  rw [abs_sub_le_iff]
  constructor
  ¬∑ rw [sub_le_iff_le_add']
    exact sum_density_div_card_le_density_add_eps hPŒ± hPŒµ hŒµ‚ÇÅ hA hB
  suffices (G.edgeDensity (A.biUnion id) (B.biUnion id) : ‚Ñù) -
      (‚àë ab ‚àà A.product B, (G.edgeDensity ab.1 ab.2 : ‚Ñù)) / (#A * #B) ‚â§ Œµ ^ 5 / 50 by
    apply this.trans
    gcongr <;> [sz_positivity; norm_num]
  rw [sub_le_iff_le_add, ‚Üê sub_le_iff_le_add']
  apply density_sub_eps_le_sum_density_div_card hPŒ± hPŒµ hA hB

private theorem edgeDensity_chunk_aux [Nonempty Œ±] (hP)
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    (hU : U ‚àà P.parts) (hV : V ‚àà P.parts) :
    (G.edgeDensity U V : ‚Ñù) ^ 2 - Œµ ^ 5 / ‚Üë25 ‚â§
    ((‚àë ab ‚àà (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : ‚Ñù)) / ‚Üë16 ^ #P.parts) ^ 2 := by
  obtain hGŒµ | hGŒµ := le_total (G.edgeDensity U V : ‚Ñù) (Œµ ^ 5 / 50)
  ¬∑ refine (sub_nonpos_of_le <| (sq_le ?_ ?_).trans <| hGŒµ.trans ?_).trans (sq_nonneg _)
    ¬∑ exact mod_cast G.edgeDensity_nonneg _ _
    ¬∑ exact mod_cast G.edgeDensity_le_one _ _
    ¬∑ exact div_le_div_of_nonneg_left (by sz_positivity) (by norm_num) (by norm_num)
  rw [‚Üê sub_nonneg] at hGŒµ
  have : 0 ‚â§ Œµ := by sz_positivity
  calc
    _ = G.edgeDensity U V ^ 2 - 1 * Œµ ^ 5 / 25 + 0 ^ 10 / 2500 := by ring
    _ ‚â§ G.edgeDensity U V ^ 2 - G.edgeDensity U V * Œµ ^ 5 / 25 + Œµ ^ 10 / 2500 := by
      gcongr; exact mod_cast G.edgeDensity_le_one ..
    _ = (G.edgeDensity U V - Œµ ^ 5 / 50) ^ 2 := by ring
    _ ‚â§ _ := by
      gcongr
      have rflU := Set.Subset.refl (chunk hP G Œµ hU).parts.toSet
      have rflV := Set.Subset.refl (chunk hP G Œµ hV).parts.toSet
      refine (le_trans ?_ <| density_sub_eps_le_sum_density_div_card hPŒ± hPŒµ rflU rflV).trans ?_
      ¬∑ rw [biUnion_parts, biUnion_parts]
      ¬∑ rw [card_chunk (m_pos hPŒ±).ne', card_chunk (m_pos hPŒ±).ne', ‚Üê cast_mul, ‚Üê mul_pow, cast_pow]
        norm_cast

private theorem abs_density_star_sub_density_le_eps (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    (hŒµ‚ÇÅ : Œµ ‚â§ 1) {hU : U ‚àà P.parts} {hV : V ‚àà P.parts} (hUV' : U ‚â† V) (hUV : ¬¨G.IsUniform Œµ U V) :
    |(G.edgeDensity ((star hP G Œµ hU V).biUnion id) ((star hP G Œµ hV U).biUnion id) : ‚Ñù) -
      G.edgeDensity (G.nonuniformWitness Œµ U V) (G.nonuniformWitness Œµ V U)| ‚â§ Œµ / 5 := by
  convert abs_edgeDensity_sub_edgeDensity_le_two_mul G.Adj
    (biUnion_star_subset_nonuniformWitness hP G Œµ hU V)
    (biUnion_star_subset_nonuniformWitness hP G Œµ hV U) (by sz_positivity)
    (one_sub_eps_mul_card_nonuniformWitness_le_card_star hV hUV' hUV hPŒµ hŒµ‚ÇÅ)
    (one_sub_eps_mul_card_nonuniformWitness_le_card_star hU hUV'.symm (fun hVU ‚Ü¶ hUV hVU.symm)
      hPŒµ hŒµ‚ÇÅ) using 1
  linarith

private theorem eps_le_card_star_div [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±)
    (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5) (hŒµ‚ÇÅ : Œµ ‚â§ 1) (hU : U ‚àà P.parts) (hV : V ‚àà P.parts)
    (hUV : U ‚â† V) (hunif : ¬¨G.IsUniform Œµ U V) :
    ‚Üë4 / ‚Üë5 * Œµ ‚â§ #(star hP G Œµ hU V) / ‚Üë4 ^ #P.parts := by
  have hm : (0 : ‚Ñù) ‚â§ 1 - (‚Üëm)‚Åª¬π := sub_nonneg_of_le (inv_le_one_of_one_le‚ÇÄ <| one_le_m_coe hPŒ±)
  have hŒµ : 0 ‚â§ 1 - Œµ / 10 :=
    sub_nonneg_of_le (div_le_one_of_le‚ÇÄ (hŒµ‚ÇÅ.trans <| by norm_num) <| by norm_num)
  have hŒµ‚ÇÄ : 0 < Œµ := by sz_positivity
  calc
    4 / 5 * Œµ = (1 - 1 / 10) * (1 - 9‚Åª¬π) * Œµ := by norm_num
    _ ‚â§ (1 - Œµ / 10) * (1 - (‚Üëm)‚Åª¬π) * (#(G.nonuniformWitness Œµ U V) / #U) := by
        gcongr
        exacts [mod_cast (show 9 ‚â§ 100 by norm_num).trans (hundred_le_m hPŒ± hPŒµ hŒµ‚ÇÅ),
          (le_div_iff‚ÇÄ' <| cast_pos.2 (P.nonempty_of_mem_parts hU).card_pos).2 <|
           G.le_card_nonuniformWitness hunif]
    _ = (1 - Œµ / 10) * #(G.nonuniformWitness Œµ U V) * ((1 - (‚Üëm)‚Åª¬π) / #U) := by
      rw [mul_assoc, mul_assoc, mul_div_left_comm]
    _ ‚â§ #((star hP G Œµ hU V).biUnion id) * ((1 - (‚Üëm)‚Åª¬π) / #U) := by
      gcongr
      exact one_sub_eps_mul_card_nonuniformWitness_le_card_star hV hUV hunif hPŒµ hŒµ‚ÇÅ
    _ ‚â§ #(star hP G Œµ hU V) * (m + 1) * ((1 - (‚Üëm)‚Åª¬π) / #U) := by
      gcongr
      exact card_biUnion_star_le_m_add_one_card_star_mul
    _ ‚â§ #(star hP G Œµ hU V) * (m + ‚Üë1) * ((‚Üë1 - (‚Üëm)‚Åª¬π) / (‚Üë4 ^ #P.parts * m)) := by
      gcongr
      ¬∑ sz_positivity
      ¬∑ exact pow_mul_m_le_card_part hP hU
    _ ‚â§ #(star hP G Œµ hU V) / ‚Üë4 ^ #P.parts := by
      rw [mul_assoc, mul_comm ((4 : ‚Ñù) ^ #P.parts), ‚Üê div_div, ‚Üê mul_div_assoc, ‚Üê mul_comm_div]
      refine mul_le_of_le_one_right (by positivity) ?_
      have hm : (0 : ‚Ñù) < m := by sz_positivity
      rw [mul_div_assoc', div_le_one hm, ‚Üê one_div, one_sub_div hm.ne', mul_div_assoc',
        div_le_iff‚ÇÄ hm]
      linarith

/-!
### Final bounds

Those inequalities are the end result of all this hard work.
-/


/-- Lower bound on the edge densities between non-uniform parts of `SzemerediRegularity.star`. -/
private theorem edgeDensity_star_not_uniform [Nonempty Œ±]
    (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±) (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5)
    (hŒµ‚ÇÅ : Œµ ‚â§ 1) {hU : U ‚àà P.parts} {hV : V ‚àà P.parts} (hUVne : U ‚â† V) (hUV : ¬¨G.IsUniform Œµ U V) :
    ‚Üë3 / ‚Üë4 * Œµ ‚â§
    |(‚àë ab ‚àà (star hP G Œµ hU V).product (star hP G Œµ hV U), (G.edgeDensity ab.1 ab.2 : ‚Ñù)) /
      (#(star hP G Œµ hU V) * #(star hP G Œµ hV U)) -
        (‚àë ab ‚àà (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts,
          (G.edgeDensity ab.1 ab.2 : ‚Ñù)) / (16 : ‚Ñù) ^ #P.parts| := by
  rw [show (16 : ‚Ñù) = ‚Üë4 ^ 2 by norm_num, pow_right_comm, sq ((4 : ‚Ñù) ^ _)]
  set p : ‚Ñù :=
    (‚àë ab ‚àà (star hP G Œµ hU V).product (star hP G Œµ hV U), (G.edgeDensity ab.1 ab.2 : ‚Ñù)) /
      (#(star hP G Œµ hU V) * #(star hP G Œµ hV U))
  set q : ‚Ñù :=
    (‚àë ab ‚àà (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : ‚Ñù)) / (‚Üë4 ^ #P.parts * ‚Üë4 ^ #P.parts)
  set r : ‚Ñù := ‚Üë(G.edgeDensity ((star hP G Œµ hU V).biUnion id) ((star hP G Œµ hV U).biUnion id))
  set s : ‚Ñù := ‚Üë(G.edgeDensity (G.nonuniformWitness Œµ U V) (G.nonuniformWitness Œµ V U))
  set t : ‚Ñù := ‚Üë(G.edgeDensity U V)
  have hrs : |r - s| ‚â§ Œµ / 5 := abs_density_star_sub_density_le_eps hPŒµ hŒµ‚ÇÅ hUVne hUV
  have hst : Œµ ‚â§ |s - t| := by
    -- After https://github.com/leanprover/lean4/pull/2734, we need to do the zeta reduction before `mod_cast`.
    unfold s t
    exact mod_cast G.nonuniformWitness_spec hUVne hUV
  have hpr : |p - r| ‚â§ Œµ ^ 5 / 49 :=
    average_density_near_total_density hPŒ± hPŒµ hŒµ‚ÇÅ star_subset_chunk star_subset_chunk
  have hqt : |q - t| ‚â§ Œµ ^ 5 / 49 := by
    have := average_density_near_total_density hPŒ± hPŒµ hŒµ‚ÇÅ
      (Subset.refl (chunk hP G Œµ hU).parts) (Subset.refl (chunk hP G Œµ hV).parts)
    simp_rw [‚Üê sup_eq_biUnion, sup_parts, card_chunk (m_pos hPŒ±).ne', cast_pow] at this
    norm_num at this
    exact this
  have hŒµ' : Œµ ^ 5 ‚â§ Œµ := by
    simpa using pow_le_pow_of_le_one (by sz_positivity) hŒµ‚ÇÅ (show 1 ‚â§ 5 by norm_num)
  rw [abs_sub_le_iff] at hrs hpr hqt
  rw [le_abs] at hst ‚ä¢
  cases hst
  ¬∑ left; linarith
  ¬∑ right; linarith

/-- Lower bound on the edge densities between non-uniform parts of `SzemerediRegularity.increment`.
-/
theorem edgeDensity_chunk_not_uniform [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±)
    (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5) (hŒµ‚ÇÅ : Œµ ‚â§ 1) {hU : U ‚àà P.parts} {hV : V ‚àà P.parts}
    (hUVne : U ‚â† V) (hUV : ¬¨G.IsUniform Œµ U V) :
    (G.edgeDensity U V : ‚Ñù) ^ 2 - Œµ ^ 5 / ‚Üë25 + Œµ ^ 4 / ‚Üë3 ‚â§
    (‚àë ab ‚àà (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : ‚Ñù) ^ 2) / ‚Üë16 ^ #P.parts :=
  calc
    ‚Üë(G.edgeDensity U V) ^ 2 - Œµ ^ 5 / 25 + Œµ ^ 4 / ‚Üë3 ‚â§ ‚Üë(G.edgeDensity U V) ^ 2 - Œµ ^ 5 / ‚Üë25 +
        #(star hP G Œµ hU V) * #(star hP G Œµ hV U) / ‚Üë16 ^ #P.parts *
          (‚Üë9 / ‚Üë16) * Œµ ^ 2 := by
      apply add_le_add_left
      have Ul : 4 / 5 * Œµ ‚â§ #(star hP G Œµ hU V) / _ :=
        eps_le_card_star_div hPŒ± hPŒµ hŒµ‚ÇÅ hU hV hUVne hUV
      have Vl : 4 / 5 * Œµ ‚â§ #(star hP G Œµ hV U) / _ :=
        eps_le_card_star_div hPŒ± hPŒµ hŒµ‚ÇÅ hV hU hUVne.symm fun h ‚Ü¶ hUV h.symm
      rw [show (16 : ‚Ñù) = ‚Üë4 ^ 2 by norm_num, pow_right_comm, sq ((4 : ‚Ñù) ^ _), ‚Üê
        _root_.div_mul_div_comm, mul_assoc]
      have : 0 < Œµ := by sz_positivity
      have UVl := mul_le_mul Ul Vl (by positivity) ?_
      swap
      ¬∑ -- This seems faster than `exact div_nonneg (by positivity) (by positivity)` and *much*
        -- (tens of seconds) faster than `positivity` on its own.
        apply div_nonneg <;> positivity
      refine le_trans ?_ (mul_le_mul_of_nonneg_right UVl ?_)
      ¬∑ norm_num
        nlinarith
      ¬∑ norm_num
        positivity
    _ ‚â§ (‚àë ab ‚àà (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts,
        (G.edgeDensity ab.1 ab.2 : ‚Ñù) ^ 2) / ‚Üë16 ^ #P.parts := by
      have t : (star hP G Œµ hU V).product (star hP G Œµ hV U) ‚äÜ
          (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts :=
        product_subset_product star_subset_chunk star_subset_chunk
      have hŒµ : 0 ‚â§ Œµ := by sz_positivity
      have sp : ‚àÄ (a b : Finset (Finset Œ±)), a.product b = a √óÀ¢ b := fun a b ‚Ü¶ rfl
      have := add_div_le_sum_sq_div_card t (fun x ‚Ü¶ (G.edgeDensity x.1 x.2 : ‚Ñù))
        ((G.edgeDensity U V : ‚Ñù) ^ 2 - Œµ ^ 5 / ‚Üë25) (show 0 ‚â§ 3 / 4 * Œµ by linarith) ?_ ?_
      ¬∑ simp_rw [sp, card_product, card_chunk (m_pos hPŒ±).ne', ‚Üê mul_pow, cast_pow, mul_pow,
          div_pow, ‚Üê mul_assoc] at this
        norm_num at this
        exact this
      ¬∑ simp_rw [sp, card_product, card_chunk (m_pos hPŒ±).ne', ‚Üê mul_pow]
        norm_num
        exact edgeDensity_star_not_uniform hPŒ± hPŒµ hŒµ‚ÇÅ hUVne hUV
      ¬∑ rw [sp, card_product]
        apply (edgeDensity_chunk_aux hP hPŒ± hPŒµ hU hV).trans
        ¬∑ rw [card_chunk (m_pos hPŒ±).ne', card_chunk (m_pos hPŒ±).ne', ‚Üê mul_pow]
          norm_num

/-- Lower bound on the edge densities between parts of `SzemerediRegularity.increment`. This is the
blanket lower bound used the uniform parts. -/
theorem edgeDensity_chunk_uniform [Nonempty Œ±] (hPŒ± : #P.parts * 16 ^ #P.parts ‚â§ card Œ±)
    (hPŒµ : ‚Üë100 ‚â§ ‚Üë4 ^ #P.parts * Œµ ^ 5) (hU : U ‚àà P.parts) (hV : V ‚àà P.parts) :
    (G.edgeDensity U V : ‚Ñù) ^ 2 - Œµ ^ 5 / ‚Üë25 ‚â§
    (‚àë ab ‚àà (chunk hP G Œµ hU).parts.product (chunk hP G Œµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : ‚Ñù) ^ 2) / ‚Üë16 ^ #P.parts := by
  apply (edgeDensity_chunk_aux (hP := hP) hPŒ± hPŒµ hU hV).trans
  have key : (16 : ‚Ñù) ^ #P.parts = #((chunk hP G Œµ hU).parts √óÀ¢ (chunk hP G Œµ hV).parts) := by
    rw [card_product, cast_mul, card_chunk (m_pos hPŒ±).ne', card_chunk (m_pos hPŒ±).ne', ‚Üê
      cast_mul, ‚Üê mul_pow]; norm_cast
  simp_rw [key]
  convert sum_div_card_sq_le_sum_sq_div_card (Œ± := ‚Ñù)

end SzemerediRegularity
