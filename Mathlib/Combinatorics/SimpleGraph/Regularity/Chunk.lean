/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Combinatorics.SimpleGraph.Regularity.Bound
import Mathlib.Combinatorics.SimpleGraph.Regularity.Equitabilise
import Mathlib.Combinatorics.SimpleGraph.Regularity.Uniform

/-!
# Chunk of the increment partition for SzemerÃ©di Regularity Lemma

In the proof of SzemerÃ©di Regularity Lemma, we need to partition each part of a starting partition
to increase the energy. This file defines those partitions of parts and shows that they locally
increase the energy.

This entire file is internal to the proof of SzemerÃ©di Regularity Lemma.

## Main declarations

* `SzemerediRegularity.chunk`: The partition of a part of the starting partition.
* `SzemerediRegularity.edgeDensity_chunk_uniform`: `chunk` does not locally decrease the edge
  density between uniform parts too much.
* `SzemerediRegularity.edgeDensity_chunk_not_uniform`: `chunk` locally increases the edge density
  between non-uniform parts.

## TODO

Once ported to mathlib4, this file will be a great golfing ground for Heather's new tactic
`gcongr`.

## References

[YaÃ«l Dillies, Bhavik Mehta, *Formalising SzemerÃ©diâ€™s Regularity Lemma in Lean*][srl_itp]
-/


open Finpartition Finset Fintype Rel Nat
open scoped SzemerediRegularity.Positivity

namespace SzemerediRegularity

variable {Î± : Type*} [Fintype Î±] [DecidableEq Î±] {P : Finpartition (univ : Finset Î±)}
  (hP : P.IsEquipartition) (G : SimpleGraph Î±) [DecidableRel G.Adj] (Îµ : â„) {U : Finset Î±}
  (hU : U âˆˆ P.parts) (V : Finset Î±)

local notation3 "m" => (card Î± / stepBound #P.parts : â„•)

/-!
### Definitions

We define `chunk`, the partition of a part, and `star`, the sets of parts of `chunk` that are
contained in the corresponding witness of non-uniformity.
-/


/-- The portion of `SzemerediRegularity.increment` which partitions `U`. -/
noncomputable def chunk : Finpartition U :=
  if hUcard : #U = m * 4 ^ #P.parts + (card Î± / #P.parts - m * 4 ^ #P.parts) then
    (atomise U <| P.nonuniformWitnesses G Îµ U).equitabilise <| card_auxâ‚ hUcard
  else (atomise U <| P.nonuniformWitnesses G Îµ U).equitabilise <| card_auxâ‚‚ hP hU hUcard

-- `hP` and `hU` are used to get that `U` has size
-- `m * 4 ^ #P.parts + a or m * 4 ^ #P.parts + a + 1`
/-- The portion of `SzemerediRegularity.chunk` which is contained in the witness of non-uniformity
of `U` and `V`. -/
noncomputable def star (V : Finset Î±) : Finset (Finset Î±) :=
  {A âˆˆ (chunk hP G Îµ hU).parts | A âŠ† G.nonuniformWitness Îµ U V}

/-!
### Density estimates

We estimate the density between parts of `chunk`.
-/


theorem biUnion_star_subset_nonuniformWitness :
    (star hP G Îµ hU V).biUnion id âŠ† G.nonuniformWitness Îµ U V :=
  biUnion_subset_iff_forall_subset.2 fun _ hA => (mem_filter.1 hA).2

variable {hP G Îµ hU V} {ğ’œ : Finset (Finset Î±)} {s : Finset Î±}

theorem star_subset_chunk : star hP G Îµ hU V âŠ† (chunk hP G Îµ hU).parts :=
  filter_subset _ _

private theorem card_nonuniformWitness_sdiff_biUnion_star (hV : V âˆˆ P.parts) (hUV : U â‰  V)
    (hâ‚‚ : Â¬G.IsUniform Îµ U V) :
    #(G.nonuniformWitness Îµ U V \ (star hP G Îµ hU V).biUnion id) â‰¤ 2 ^ (#P.parts - 1) * m := by
  have hX : G.nonuniformWitness Îµ U V âˆˆ P.nonuniformWitnesses G Îµ U :=
    nonuniformWitness_mem_nonuniformWitnesses hâ‚‚ hV hUV
  have q : G.nonuniformWitness Îµ U V \ (star hP G Îµ hU V).biUnion id âŠ†
      {B âˆˆ (atomise U <| P.nonuniformWitnesses G Îµ U).parts |
        B âŠ† G.nonuniformWitness Îµ U V âˆ§ B.Nonempty}.biUnion
        fun B => B \ {A âˆˆ (chunk hP G Îµ hU).parts | A âŠ† B}.biUnion id := by
    intro x hx
    rw [â† biUnion_filter_atomise hX (G.nonuniformWitness_subset hâ‚‚), star, mem_sdiff,
      mem_biUnion] at hx
    simp only [not_exists, mem_biUnion, and_imp, mem_filter,
      not_and, mem_sdiff, id, mem_sdiff] at hx âŠ¢
    obtain âŸ¨âŸ¨B, hBâ‚, hBâ‚‚âŸ©, hxâŸ© := hx
    exact âŸ¨B, hBâ‚, hBâ‚‚, fun A hA AB => hx A hA <| AB.trans hBâ‚.2.1âŸ©
  apply (card_le_card q).trans (card_biUnion_le.trans _)
  trans âˆ‘ B âˆˆ (atomise U <| P.nonuniformWitnesses G Îµ U).parts with
    B âŠ† G.nonuniformWitness Îµ U V âˆ§ B.Nonempty, m
  Â· suffices âˆ€ B âˆˆ (atomise U <| P.nonuniformWitnesses G Îµ U).parts,
        #(B \ {A âˆˆ (chunk hP G Îµ hU).parts | A âŠ† B}.biUnion id) â‰¤ m by
      exact sum_le_sum fun B hB => this B <| filter_subset _ _ hB
    intro B hB
    unfold chunk
    split_ifs with hâ‚
    Â· convert card_parts_equitabilise_subset_le _ (card_auxâ‚ hâ‚) hB
    Â· convert card_parts_equitabilise_subset_le _ (card_auxâ‚‚ hP hU hâ‚) hB
  grw [sum_const, smul_eq_mul, card_filter_atomise_le_two_pow (s := U) hX,
    Finpartition.card_nonuniformWitnesses_le, filter_subset] <;> simp

private theorem one_sub_eps_mul_card_nonuniformWitness_le_card_star (hV : V âˆˆ P.parts)
    (hUV : U â‰  V) (hunif : Â¬G.IsUniform Îµ U V) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    (hÎµâ‚ : Îµ â‰¤ 1) :
    (1 - Îµ / 10) * #(G.nonuniformWitness Îµ U V) â‰¤ #((star hP G Îµ hU V).biUnion id) := by
  have hPâ‚ : 0 < #P.parts := Finset.card_pos.2 âŸ¨_, hUâŸ©
  have : (â†‘2 ^ #P.parts : â„) * m / (#U * Îµ) â‰¤ Îµ / 10 := by
    rw [â† div_div, div_le_iffâ‚€']
    swap
    Â· sz_positivity
    refine le_of_mul_le_mul_left ?_ (pow_pos zero_lt_two #P.parts)
    calc
      â†‘2 ^ #P.parts * ((â†‘2 ^ #P.parts * m : â„) / #U) =
          ((2 : â„) * 2) ^ #P.parts * m / #U := by
        rw [mul_pow, â† mul_div_assoc, mul_assoc]
      _ = â†‘4 ^ #P.parts * m / #U := by norm_num
      _ â‰¤ 1 := div_le_one_of_leâ‚€ (pow_mul_m_le_card_part hP hU) (cast_nonneg _)
      _ â‰¤ â†‘2 ^ #P.parts * Îµ ^ 2 / 10 := by
        refine (one_le_sq_iffâ‚€ <| by positivity).1 ?_
        rw [div_pow, mul_pow, pow_right_comm, â† pow_mul Îµ, one_le_div (by positivity)]
        calc
          (â†‘10 ^ 2) = 100 := by norm_num
          _ â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5 := hPÎµ
          _ â‰¤ â†‘4 ^ #P.parts * Îµ ^ 4 := by
            gcongr _ * ?_
            exact pow_le_pow_of_le_one (by sz_positivity) hÎµâ‚ (by decide)
          _ = (â†‘2 ^ 2) ^ #P.parts * Îµ ^ (2 * 2) := by norm_num
      _ = â†‘2 ^ #P.parts * (Îµ * (Îµ / 10)) := by rw [mul_div_assoc, sq, mul_div_assoc]
  calc
    (â†‘1 - Îµ / 10) * #(G.nonuniformWitness Îµ U V) â‰¤
        (â†‘1 - â†‘2 ^ #P.parts * m / (#U * Îµ)) * #(G.nonuniformWitness Îµ U V) := by gcongr
    _ = #(G.nonuniformWitness Îµ U V) -
        â†‘2 ^ #P.parts * m / (#U * Îµ) * #(G.nonuniformWitness Îµ U V) := by
      rw [sub_mul, one_mul]
    _ â‰¤ #(G.nonuniformWitness Îµ U V) - â†‘2 ^ (#P.parts - 1) * m := by
      refine sub_le_sub_left ?_ _
      have : (2 : â„) ^ #P.parts = â†‘2 ^ (#P.parts - 1) * 2 := by
        rw [â† _root_.pow_succ, tsub_add_cancel_of_le (succ_le_iff.2 hPâ‚)]
      rw [â† mul_div_right_comm, this, mul_right_comm _ (2 : â„), mul_assoc, le_div_iffâ‚€]
      Â· gcongr _ * ?_
        exact (G.le_card_nonuniformWitness hunif).trans
          (le_mul_of_one_le_left (cast_nonneg _) one_le_two)
      have := Finset.card_pos.mpr (P.nonempty_of_mem_parts hU)
      sz_positivity
    _ â‰¤ #((star hP G Îµ hU V).biUnion id) := by
      rw [sub_le_comm, â†
        cast_sub (card_le_card <| biUnion_star_subset_nonuniformWitness hP G Îµ hU V), â†
        card_sdiff_of_subset (biUnion_star_subset_nonuniformWitness hP G Îµ hU V)]
      exact mod_cast card_nonuniformWitness_sdiff_biUnion_star hV hUV hunif

/-! ### `chunk` -/


theorem card_chunk (hm : m â‰  0) : #(chunk hP G Îµ hU).parts = 4 ^ #P.parts := by
  unfold chunk
  split_ifs
  Â· rw [card_parts_equitabilise _ _ hm, tsub_add_cancel_of_le]
    exact le_of_lt a_add_one_le_four_pow_parts_card
  Â· rw [card_parts_equitabilise _ _ hm, tsub_add_cancel_of_le a_add_one_le_four_pow_parts_card]

theorem card_eq_of_mem_parts_chunk (hs : s âˆˆ (chunk hP G Îµ hU).parts) :
    #s = m âˆ¨ #s = m + 1 := by
  unfold chunk at hs
  split_ifs at hs <;> exact card_eq_of_mem_parts_equitabilise hs

theorem m_le_card_of_mem_chunk_parts (hs : s âˆˆ (chunk hP G Îµ hU).parts) : m â‰¤ #s :=
  (card_eq_of_mem_parts_chunk hs).elim ge_of_eq fun i => by simp [i]

theorem card_le_m_add_one_of_mem_chunk_parts (hs : s âˆˆ (chunk hP G Îµ hU).parts) : #s â‰¤ m + 1 :=
  (card_eq_of_mem_parts_chunk hs).elim (fun i => by simp [i]) fun i => i.le

theorem card_biUnion_star_le_m_add_one_card_star_mul :
    (#((star hP G Îµ hU V).biUnion id) : â„) â‰¤ #(star hP G Îµ hU V) * (m + 1) :=
  mod_cast card_biUnion_le_card_mul _ _ _ fun _ hs =>
    card_le_m_add_one_of_mem_chunk_parts <| star_subset_chunk hs

private theorem le_sum_card_subset_chunk_parts (hğ’œ : ğ’œ âŠ† (chunk hP G Îµ hU).parts) (hs : s âˆˆ ğ’œ) :
    (#ğ’œ : â„) * #s * (m / (m + 1)) â‰¤ #(ğ’œ.sup id) := by
  rw [mul_div_assoc', div_le_iffâ‚€ coe_m_add_one_pos, mul_right_comm]
  gcongr
  Â· rw [â† (ofSubset _ hğ’œ rfl).sum_card_parts, ofSubset_parts, â† cast_mul, cast_le]
    exact card_nsmul_le_sum _ _ _ fun x hx => m_le_card_of_mem_chunk_parts <| hğ’œ hx
  Â· exact mod_cast card_le_m_add_one_of_mem_chunk_parts (hğ’œ hs)

private theorem sum_card_subset_chunk_parts_le (m_pos : (0 : â„) < m)
    (hğ’œ : ğ’œ âŠ† (chunk hP G Îµ hU).parts) (hs : s âˆˆ ğ’œ) :
    (#(ğ’œ.sup id) : â„) â‰¤ #ğ’œ * #s * ((m + 1) / m) := by
  rw [sup_eq_biUnion, mul_div_assoc', le_div_iffâ‚€ m_pos, mul_right_comm]
  gcongr
  Â· norm_cast
    refine card_biUnion_le_card_mul _ _ _ fun x hx => ?_
    apply card_le_m_add_one_of_mem_chunk_parts (hğ’œ hx)
  Â· exact mod_cast m_le_card_of_mem_chunk_parts (hğ’œ hs)

private theorem one_sub_le_m_div_m_add_one_sq [Nonempty Î±]
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5) :
    â†‘1 - Îµ ^ 5 / â†‘50 â‰¤ (m / (m + 1 : â„)) ^ 2 := by
  have : (m : â„) / (m + 1) = 1 - 1 / (m + 1) := by
    rw [one_sub_div coe_m_add_one_pos.ne', add_sub_cancel_right]
  rw [this, sub_sq, one_pow, mul_one]
  refine le_trans ?_ (le_add_of_nonneg_right <| sq_nonneg _)
  rw [sub_le_sub_iff_left, â† le_div_iffâ‚€' (show (0 : â„) < 2 by simp), div_div,
    one_div_le coe_m_add_one_pos, one_div_div]
  Â· refine le_trans ?_ (le_add_of_nonneg_right zero_le_one)
    norm_num
    apply hundred_div_Îµ_pow_five_le_m hPÎ± hPÎµ
  sz_positivity

private theorem m_add_one_div_m_le_one_add [Nonempty Î±]
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5) (hÎµâ‚ : Îµ â‰¤ 1) :
    ((m + 1 : â„) / m) ^ 2 â‰¤ â†‘1 + Îµ ^ 5 / 49 := by
  have : 0 â‰¤ Îµ := by sz_positivity
  rw [same_add_div (by sz_positivity)]
  calc
    _ â‰¤ (1 + Îµ ^ 5 / 100) ^ 2 := by
      gcongr (1 + ?_) ^ 2
      rw [â† one_div_div (100 : â„)]
      exact one_div_le_one_div_of_le (by sz_positivity) (hundred_div_Îµ_pow_five_le_m hPÎ± hPÎµ)
    _ = 1 + Îµ ^ 5 * (50â»Â¹ + Îµ ^ 5 / 10000) := by ring
    _ â‰¤ 1 + Îµ ^ 5 * (50â»Â¹ + 1 ^ 5 / 10000) := by gcongr
    _ â‰¤ 1 + Îµ ^ 5 * 49â»Â¹ := by gcongr; norm_num
    _ = 1 + Îµ ^ 5 / 49 := by rw [div_eq_mul_inv]

private theorem density_sub_eps_le_sum_density_div_card [Nonempty Î±]
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    {hU : U âˆˆ P.parts} {hV : V âˆˆ P.parts} {A B : Finset (Finset Î±)}
    (hA : A âŠ† (chunk hP G Îµ hU).parts) (hB : B âŠ† (chunk hP G Îµ hV).parts) :
    (G.edgeDensity (A.biUnion id) (B.biUnion id)) - Îµ ^ 5 / 50 â‰¤
    (âˆ‘ ab âˆˆ A.product B, (G.edgeDensity ab.1 ab.2 : â„)) / (#A * #B) := by
  have : â†‘(G.edgeDensity (A.biUnion id) (B.biUnion id)) - Îµ ^ 5 / â†‘50 â‰¤
      (â†‘1 - Îµ ^ 5 / 50) * G.edgeDensity (A.biUnion id) (B.biUnion id) := by
    rw [sub_mul, one_mul, sub_le_sub_iff_left]
    refine mul_le_of_le_one_right (by sz_positivity) ?_
    exact mod_cast G.edgeDensity_le_one _ _
  refine this.trans ?_
  conv_rhs => -- Porting note: LHS and RHS need separate treatment to get the desired form
    simp only [SimpleGraph.edgeDensity_def, sum_div, Rat.cast_div, div_div]
  conv_lhs =>
    rw [SimpleGraph.edgeDensity_def, SimpleGraph.interedges, â† sup_eq_biUnion, â† sup_eq_biUnion,
      Rel.card_interedges_finpartition _ (ofSubset _ hA rfl) (ofSubset _ hB rfl), ofSubset_parts,
      ofSubset_parts]
    simp only [cast_sum, sum_div, mul_sum, Rat.cast_sum, Rat.cast_div,
      mul_div_left_comm ((1 : â„) - _)]
  push_cast
  apply sum_le_sum
  simp only [and_imp, Prod.forall, mem_product]
  rintro x y hx hy
  rw [mul_mul_mul_comm, mul_comm (#x : â„), mul_comm (#y : â„), le_div_iffâ‚€, mul_assoc]
  Â· refine mul_le_of_le_one_right (cast_nonneg _) ?_
    rw [div_mul_eq_mul_div, â† mul_assoc, mul_assoc]
    refine div_le_one_of_leâ‚€ ?_ (by positivity)
    refine (mul_le_mul_of_nonneg_right (one_sub_le_m_div_m_add_one_sq hPÎ± hPÎµ) ?_).trans ?_
    Â· exact mod_cast _root_.zero_le _
    rw [sq, mul_mul_mul_comm, mul_comm ((m : â„) / _), mul_comm ((m : â„) / _)]
    gcongr
    Â· apply le_sum_card_subset_chunk_parts hA hx
    Â· apply le_sum_card_subset_chunk_parts hB hy
  refine mul_pos (mul_pos ?_ ?_) (mul_pos ?_ ?_) <;> rw [cast_pos, Finset.card_pos]
  exacts [âŸ¨_, hxâŸ©, nonempty_of_mem_parts _ (hA hx), âŸ¨_, hyâŸ©, nonempty_of_mem_parts _ (hB hy)]

private theorem sum_density_div_card_le_density_add_eps [Nonempty Î±]
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    (hÎµâ‚ : Îµ â‰¤ 1) {hU : U âˆˆ P.parts} {hV : V âˆˆ P.parts} {A B : Finset (Finset Î±)}
    (hA : A âŠ† (chunk hP G Îµ hU).parts) (hB : B âŠ† (chunk hP G Îµ hV).parts) :
    (âˆ‘ ab âˆˆ A.product B, G.edgeDensity ab.1 ab.2 : â„) / (#A * #B) â‰¤
    G.edgeDensity (A.biUnion id) (B.biUnion id) + Îµ ^ 5 / 49 := by
  have : (â†‘1 + Îµ ^ 5 / â†‘49) * G.edgeDensity (A.biUnion id) (B.biUnion id) â‰¤
      G.edgeDensity (A.biUnion id) (B.biUnion id) + Îµ ^ 5 / 49 := by
    rw [add_mul, one_mul, add_le_add_iff_left]
    refine mul_le_of_le_one_right (by sz_positivity) ?_
    exact mod_cast G.edgeDensity_le_one _ _
  refine le_trans ?_ this
  conv_lhs => -- Porting note: LHS and RHS need separate treatment to get the desired form
    simp only [SimpleGraph.edgeDensity, edgeDensity, sum_div, Rat.cast_div, div_div]
  conv_rhs =>
    rw [SimpleGraph.edgeDensity, edgeDensity, â† sup_eq_biUnion, â† sup_eq_biUnion,
      Rel.card_interedges_finpartition _ (ofSubset _ hA rfl) (ofSubset _ hB rfl)]
    simp only [cast_sum, mul_sum, sum_div, Rat.cast_sum, Rat.cast_div,
      mul_div_left_comm ((1 : â„) + _)]
  push_cast
  apply sum_le_sum
  simp only [and_imp, Prod.forall, mem_product, show A.product B = A Ã—Ë¢ B by rfl]
  intro x y hx hy
  rw [mul_mul_mul_comm, mul_comm (#x : â„), mul_comm (#y : â„), div_le_iffâ‚€, mul_assoc]
  Â· refine le_mul_of_one_le_right (cast_nonneg _) ?_
    rw [div_mul_eq_mul_div, one_le_div]
    Â· refine le_trans ?_ (mul_le_mul_of_nonneg_right (m_add_one_div_m_le_one_add hPÎ± hPÎµ hÎµâ‚) ?_)
      Â· rw [sq, mul_mul_mul_comm, mul_comm (_ / (m : â„)), mul_comm (_ / (m : â„))]
        gcongr
        exacts [sum_card_subset_chunk_parts_le (by sz_positivity) hA hx,
          sum_card_subset_chunk_parts_le (by sz_positivity) hB hy]
      Â· exact mod_cast _root_.zero_le _
    rw [â† cast_mul, cast_pos]
    apply mul_pos <;> rw [Finset.card_pos, sup_eq_biUnion, biUnion_nonempty]
    Â· exact âŸ¨_, hx, nonempty_of_mem_parts _ (hA hx)âŸ©
    Â· exact âŸ¨_, hy, nonempty_of_mem_parts _ (hB hy)âŸ©
  refine mul_pos (mul_pos ?_ ?_) (mul_pos ?_ ?_) <;> rw [cast_pos, Finset.card_pos]
  exacts [âŸ¨_, hxâŸ©, nonempty_of_mem_parts _ (hA hx), âŸ¨_, hyâŸ©, nonempty_of_mem_parts _ (hB hy)]

private theorem average_density_near_total_density [Nonempty Î±]
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    (hÎµâ‚ : Îµ â‰¤ 1) {hU : U âˆˆ P.parts} {hV : V âˆˆ P.parts} {A B : Finset (Finset Î±)}
    (hA : A âŠ† (chunk hP G Îµ hU).parts) (hB : B âŠ† (chunk hP G Îµ hV).parts) :
    |(âˆ‘ ab âˆˆ A.product B, G.edgeDensity ab.1 ab.2 : â„) / (#A * #B) -
      G.edgeDensity (A.biUnion id) (B.biUnion id)| â‰¤ Îµ ^ 5 / 49 := by
  rw [abs_sub_le_iff]
  constructor
  Â· rw [sub_le_iff_le_add']
    exact sum_density_div_card_le_density_add_eps hPÎ± hPÎµ hÎµâ‚ hA hB
  suffices (G.edgeDensity (A.biUnion id) (B.biUnion id) : â„) -
      (âˆ‘ ab âˆˆ A.product B, (G.edgeDensity ab.1 ab.2 : â„)) / (#A * #B) â‰¤ Îµ ^ 5 / 50 by
    apply this.trans
    gcongr <;> [sz_positivity; norm_num]
  rw [sub_le_iff_le_add, â† sub_le_iff_le_add']
  apply density_sub_eps_le_sum_density_div_card hPÎ± hPÎµ hA hB

private theorem edgeDensity_chunk_aux [Nonempty Î±] (hP)
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    (hU : U âˆˆ P.parts) (hV : V âˆˆ P.parts) :
    (G.edgeDensity U V : â„) ^ 2 - Îµ ^ 5 / â†‘25 â‰¤
    ((âˆ‘ ab âˆˆ (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : â„)) / â†‘16 ^ #P.parts) ^ 2 := by
  obtain hGÎµ | hGÎµ := le_total (G.edgeDensity U V : â„) (Îµ ^ 5 / 50)
  Â· refine (sub_nonpos_of_le <| (sq_le ?_ ?_).trans <| hGÎµ.trans ?_).trans (sq_nonneg _)
    Â· exact mod_cast G.edgeDensity_nonneg _ _
    Â· exact mod_cast G.edgeDensity_le_one _ _
    Â· exact div_le_div_of_nonneg_left (by sz_positivity) (by simp) (by norm_num)
  rw [â† sub_nonneg] at hGÎµ
  have : 0 â‰¤ Îµ := by sz_positivity
  calc
    _ = G.edgeDensity U V ^ 2 - 1 * Îµ ^ 5 / 25 + 0 ^ 10 / 2500 := by ring
    _ â‰¤ G.edgeDensity U V ^ 2 - G.edgeDensity U V * Îµ ^ 5 / 25 + Îµ ^ 10 / 2500 := by
      gcongr; exact mod_cast G.edgeDensity_le_one ..
    _ = (G.edgeDensity U V - Îµ ^ 5 / 50) ^ 2 := by ring
    _ â‰¤ _ := by
      gcongr
      have rflU := Subset.refl (chunk hP G Îµ hU).parts
      have rflV := Subset.refl (chunk hP G Îµ hV).parts
      refine (le_trans ?_ <| density_sub_eps_le_sum_density_div_card hPÎ± hPÎµ rflU rflV).trans ?_
      Â· rw [biUnion_parts, biUnion_parts]
      Â· rw [card_chunk (m_pos hPÎ±).ne', card_chunk (m_pos hPÎ±).ne', â† cast_mul, â† mul_pow, cast_pow]
        norm_cast

private theorem abs_density_star_sub_density_le_eps (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    (hÎµâ‚ : Îµ â‰¤ 1) {hU : U âˆˆ P.parts} {hV : V âˆˆ P.parts} (hUV' : U â‰  V) (hUV : Â¬G.IsUniform Îµ U V) :
    |(G.edgeDensity ((star hP G Îµ hU V).biUnion id) ((star hP G Îµ hV U).biUnion id) : â„) -
      G.edgeDensity (G.nonuniformWitness Îµ U V) (G.nonuniformWitness Îµ V U)| â‰¤ Îµ / 5 := by
  convert abs_edgeDensity_sub_edgeDensity_le_two_mul G.Adj
    (biUnion_star_subset_nonuniformWitness hP G Îµ hU V)
    (biUnion_star_subset_nonuniformWitness hP G Îµ hV U) (by sz_positivity)
    (one_sub_eps_mul_card_nonuniformWitness_le_card_star hV hUV' hUV hPÎµ hÎµâ‚)
    (one_sub_eps_mul_card_nonuniformWitness_le_card_star hU hUV'.symm (fun hVU => hUV hVU.symm)
      hPÎµ hÎµâ‚) using 1
  linarith

private theorem eps_le_card_star_div [Nonempty Î±] (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±)
    (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5) (hÎµâ‚ : Îµ â‰¤ 1) (hU : U âˆˆ P.parts) (hV : V âˆˆ P.parts)
    (hUV : U â‰  V) (hunif : Â¬G.IsUniform Îµ U V) :
    â†‘4 / â†‘5 * Îµ â‰¤ #(star hP G Îµ hU V) / â†‘4 ^ #P.parts := by
  have hm : (0 : â„) â‰¤ 1 - (â†‘m)â»Â¹ := sub_nonneg_of_le (inv_le_one_of_one_leâ‚€ <| one_le_m_coe hPÎ±)
  have hÎµ : 0 â‰¤ 1 - Îµ / 10 :=
    sub_nonneg_of_le (div_le_one_of_leâ‚€ (hÎµâ‚.trans <| by simp) <| by norm_num)
  have hÎµâ‚€ : 0 < Îµ := by sz_positivity
  calc
    4 / 5 * Îµ = (1 - 1 / 10) * (1 - 9â»Â¹) * Îµ := by norm_num
    _ â‰¤ (1 - Îµ / 10) * (1 - (â†‘m)â»Â¹) * (#(G.nonuniformWitness Îµ U V) / #U) := by
        gcongr
        exacts [mod_cast (show 9 â‰¤ 100 by simp).trans (hundred_le_m hPÎ± hPÎµ hÎµâ‚),
          (le_div_iffâ‚€' <| cast_pos.2 (P.nonempty_of_mem_parts hU).card_pos).2 <|
           G.le_card_nonuniformWitness hunif]
    _ = (1 - Îµ / 10) * #(G.nonuniformWitness Îµ U V) * ((1 - (â†‘m)â»Â¹) / #U) := by
      rw [mul_assoc, mul_assoc, mul_div_left_comm]
    _ â‰¤ #((star hP G Îµ hU V).biUnion id) * ((1 - (â†‘m)â»Â¹) / #U) := by
      gcongr
      exact one_sub_eps_mul_card_nonuniformWitness_le_card_star hV hUV hunif hPÎµ hÎµâ‚
    _ â‰¤ #(star hP G Îµ hU V) * (m + 1) * ((1 - (â†‘m)â»Â¹) / #U) := by
      gcongr
      exact card_biUnion_star_le_m_add_one_card_star_mul
    _ â‰¤ #(star hP G Îµ hU V) * (m + â†‘1) * ((â†‘1 - (â†‘m)â»Â¹) / (â†‘4 ^ #P.parts * m)) := by
      gcongr
      Â· sz_positivity
      Â· exact pow_mul_m_le_card_part hP hU
    _ â‰¤ #(star hP G Îµ hU V) / â†‘4 ^ #P.parts := by
      rw [mul_assoc, mul_comm ((4 : â„) ^ #P.parts), â† div_div, â† mul_div_assoc, â† mul_comm_div]
      refine mul_le_of_le_one_right (by positivity) ?_
      have hm : (0 : â„) < m := by sz_positivity
      rw [mul_div_assoc', div_le_one hm, â† one_div, one_sub_div hm.ne', mul_div_assoc',
        div_le_iffâ‚€ hm]
      linarith

/-!
### Final bounds

Those inequalities are the end result of all this hard work.
-/


/-- Lower bound on the edge densities between non-uniform parts of `SzemerediRegularity.star`. -/
private theorem edgeDensity_star_not_uniform [Nonempty Î±]
    (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±) (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5)
    (hÎµâ‚ : Îµ â‰¤ 1) {hU : U âˆˆ P.parts} {hV : V âˆˆ P.parts} (hUVne : U â‰  V) (hUV : Â¬G.IsUniform Îµ U V) :
    â†‘3 / â†‘4 * Îµ â‰¤
    |(âˆ‘ ab âˆˆ (star hP G Îµ hU V).product (star hP G Îµ hV U), (G.edgeDensity ab.1 ab.2 : â„)) /
      (#(star hP G Îµ hU V) * #(star hP G Îµ hV U)) -
        (âˆ‘ ab âˆˆ (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts,
          (G.edgeDensity ab.1 ab.2 : â„)) / (16 : â„) ^ #P.parts| := by
  rw [show (16 : â„) = â†‘4 ^ 2 by norm_num, pow_right_comm, sq ((4 : â„) ^ _)]
  set p : â„ :=
    (âˆ‘ ab âˆˆ (star hP G Îµ hU V).product (star hP G Îµ hV U), (G.edgeDensity ab.1 ab.2 : â„)) /
      (#(star hP G Îµ hU V) * #(star hP G Îµ hV U))
  set q : â„ :=
    (âˆ‘ ab âˆˆ (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : â„)) / (â†‘4 ^ #P.parts * â†‘4 ^ #P.parts)
  set r : â„ := â†‘(G.edgeDensity ((star hP G Îµ hU V).biUnion id) ((star hP G Îµ hV U).biUnion id))
  set s : â„ := â†‘(G.edgeDensity (G.nonuniformWitness Îµ U V) (G.nonuniformWitness Îµ V U))
  set t : â„ := â†‘(G.edgeDensity U V)
  have hrs : |r - s| â‰¤ Îµ / 5 := abs_density_star_sub_density_le_eps hPÎµ hÎµâ‚ hUVne hUV
  have hst : Îµ â‰¤ |s - t| := by
    -- After https://github.com/leanprover/lean4/pull/2734, we need to do the zeta reduction before `mod_cast`.
    unfold s t
    exact mod_cast G.nonuniformWitness_spec hUVne hUV
  have hpr : |p - r| â‰¤ Îµ ^ 5 / 49 :=
    average_density_near_total_density hPÎ± hPÎµ hÎµâ‚ star_subset_chunk star_subset_chunk
  have hqt : |q - t| â‰¤ Îµ ^ 5 / 49 := by
    have := average_density_near_total_density hPÎ± hPÎµ hÎµâ‚
      (Subset.refl (chunk hP G Îµ hU).parts) (Subset.refl (chunk hP G Îµ hV).parts)
    simp_rw [â† sup_eq_biUnion, sup_parts, card_chunk (m_pos hPÎ±).ne', cast_pow] at this
    norm_num at this
    exact this
  have hÎµ' : Îµ ^ 5 â‰¤ Îµ := by
    simpa using pow_le_pow_of_le_one (by sz_positivity) hÎµâ‚ (show 1 â‰¤ 5 by simp)
  rw [abs_sub_le_iff] at hrs hpr hqt
  rw [le_abs] at hst âŠ¢
  cases hst
  Â· left; linarith
  Â· right; linarith

/-- Lower bound on the edge densities between non-uniform parts of `SzemerediRegularity.increment`.
-/
theorem edgeDensity_chunk_not_uniform [Nonempty Î±] (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±)
    (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5) (hÎµâ‚ : Îµ â‰¤ 1) {hU : U âˆˆ P.parts} {hV : V âˆˆ P.parts}
    (hUVne : U â‰  V) (hUV : Â¬G.IsUniform Îµ U V) :
    (G.edgeDensity U V : â„) ^ 2 - Îµ ^ 5 / â†‘25 + Îµ ^ 4 / â†‘3 â‰¤
    (âˆ‘ ab âˆˆ (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : â„) ^ 2) / â†‘16 ^ #P.parts :=
  calc
    â†‘(G.edgeDensity U V) ^ 2 - Îµ ^ 5 / 25 + Îµ ^ 4 / â†‘3 â‰¤ â†‘(G.edgeDensity U V) ^ 2 - Îµ ^ 5 / â†‘25 +
        #(star hP G Îµ hU V) * #(star hP G Îµ hV U) / â†‘16 ^ #P.parts *
          (â†‘9 / â†‘16) * Îµ ^ 2 := by
      gcongr
      have Ul : 4 / 5 * Îµ â‰¤ #(star hP G Îµ hU V) / _ :=
        eps_le_card_star_div hPÎ± hPÎµ hÎµâ‚ hU hV hUVne hUV
      have Vl : 4 / 5 * Îµ â‰¤ #(star hP G Îµ hV U) / _ :=
        eps_le_card_star_div hPÎ± hPÎµ hÎµâ‚ hV hU hUVne.symm fun h => hUV h.symm
      rw [show (16 : â„) = â†‘4 ^ 2 by norm_num, pow_right_comm, sq ((4 : â„) ^ _), â†
        _root_.div_mul_div_comm, mul_assoc]
      have : 0 < Îµ := by sz_positivity
      have UVl := mul_le_mul Ul Vl (by positivity) ?_
      swap
      Â· -- This seems faster than `exact div_nonneg (by positivity) (by positivity)` and *much*
        -- (tens of seconds) faster than `positivity` on its own.
        apply div_nonneg <;> positivity
      refine le_trans ?_ (mul_le_mul_of_nonneg_right UVl ?_)
      Â· norm_num
        nlinarith
      Â· norm_num
        positivity
    _ â‰¤ (âˆ‘ ab âˆˆ (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts,
        (G.edgeDensity ab.1 ab.2 : â„) ^ 2) / â†‘16 ^ #P.parts := by
      have t : (star hP G Îµ hU V).product (star hP G Îµ hV U) âŠ†
          (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts :=
        product_subset_product star_subset_chunk star_subset_chunk
      have hÎµ : 0 â‰¤ Îµ := by sz_positivity
      have sp : âˆ€ (a b : Finset (Finset Î±)), a.product b = a Ã—Ë¢ b := fun a b => rfl
      have := add_div_le_sum_sq_div_card t (fun x => (G.edgeDensity x.1 x.2 : â„))
        ((G.edgeDensity U V : â„) ^ 2 - Îµ ^ 5 / â†‘25) (show 0 â‰¤ 3 / 4 * Îµ by linarith) ?_ ?_
      Â· simp_rw [sp, card_product, card_chunk (m_pos hPÎ±).ne', â† mul_pow, cast_pow, mul_pow,
          div_pow, â† mul_assoc] at this
        norm_num at this
        exact this
      Â· simp_rw [sp, card_product, card_chunk (m_pos hPÎ±).ne', â† mul_pow]
        norm_num
        exact edgeDensity_star_not_uniform hPÎ± hPÎµ hÎµâ‚ hUVne hUV
      Â· rw [sp, card_product]
        apply (edgeDensity_chunk_aux hP hPÎ± hPÎµ hU hV).trans
        Â· rw [card_chunk (m_pos hPÎ±).ne', card_chunk (m_pos hPÎ±).ne', â† mul_pow]
          simp

/-- Lower bound on the edge densities between parts of `SzemerediRegularity.increment`. This is the
blanket lower bound used the uniform parts. -/
theorem edgeDensity_chunk_uniform [Nonempty Î±] (hPÎ± : #P.parts * 16 ^ #P.parts â‰¤ card Î±)
    (hPÎµ : â†‘100 â‰¤ â†‘4 ^ #P.parts * Îµ ^ 5) (hU : U âˆˆ P.parts) (hV : V âˆˆ P.parts) :
    (G.edgeDensity U V : â„) ^ 2 - Îµ ^ 5 / â†‘25 â‰¤
    (âˆ‘ ab âˆˆ (chunk hP G Îµ hU).parts.product (chunk hP G Îµ hV).parts,
      (G.edgeDensity ab.1 ab.2 : â„) ^ 2) / â†‘16 ^ #P.parts := by
  apply (edgeDensity_chunk_aux (hP := hP) hPÎ± hPÎµ hU hV).trans
  have key : (16 : â„) ^ #P.parts = #((chunk hP G Îµ hU).parts Ã—Ë¢ (chunk hP G Îµ hV).parts) := by
    rw [card_product, cast_mul, card_chunk (m_pos hPÎ±).ne', card_chunk (m_pos hPÎ±).ne', â†
      cast_mul, â† mul_pow]; norm_cast
  simp_rw [key]
  convert sum_div_card_sq_le_sum_sq_div_card (Î± := â„)

end SzemerediRegularity
