/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Combinatorics.SimpleGraph.Regularity.Increment

#align_import combinatorics.simple_graph.regularity.lemma from "leanprover-community/mathlib"@"1d4d3ca5ec44693640c4f5e407a6b611f77accc8"

/-!
# SzemerÃ©di's Regularity Lemma

In this file, we prove SzemerÃ©di's Regularity Lemma (aka SRL). This is a landmark result in
combinatorics roughly stating that any sufficiently big graph behaves like a random graph. This is
useful because random graphs are well-behaved in many aspects.

More precisely, SRL states that for any `Îµ > 0` and integer `l` there exists a bound `M` such that
any graph on at least `l` vertices can be partitioned into at least `l` parts and at most `M` parts
such that the resulting partitioned graph is `Îµ`-uniform.

This statement is very robust to tweaking and many different versions exist. Here, we prove the
version where the resulting partition is equitable (aka an *equipartition*), namely all parts have
the same size up to a difference of `1`.

The proof we formalise goes as follows:
1. Define an auxiliary measure of edge density, the *energy* of a partition.
2. Start with an arbitrary equipartition of size `l`.
3. Repeatedly break up the parts of the current equipartition in a big but controlled number of
  parts. The key point is to break along the witnesses of non-uniformity, so that a lesser portion
  of the pairs of parts are non-`Îµ`-uniform.
4. Check that this results in an equipartition with an energy greater than the energy of the current
  partition, plus some constant.
5. Since the energy is between zero and one, we can't run this process forever. Check that when the
  process stops we have an `Îµ`-uniform equipartition.

This file only contains the final result. The supporting material is spread across the
`Combinatorics/SimpleGraph/Regularity` folder:
* `Combinatorics/SimpleGraph/Regularity/Bound`: Definition of the bound on the number of parts.
  Numerical inequalities involving the lemma constants.
* `Combinatorics/SimpleGraph/Regularity/Energy`: Definition of the energy of a simple graph along a
  partition.
* `Combinatorics/SimpleGraph/Regularity/Uniform`: Definition of uniformity of a simple graph along
  a pair of parts and along a partition.
* `Combinatorics/SimpleGraph/Regularity/Equitabilise`: Construction of an equipartition with
  a prescribed number of parts of each size and almost refining a given partition.
* `Combinatorics/SimpleGraph/Regularity/Chunk`: Break up one part of the current equipartition.
  Check that density between non-uniform parts increases, and that density between uniform parts
  doesn't decrease too much.
* `Combinatorics/SimpleGraph/Regularity/Increment`: Gather all those broken up parts into the new
  equipartition (aka *increment partition*). Check that energy increases by at least a fixed amount.
* `Combinatorics/SimpleGraph/Regularity/Lemma`: Wrap everything up into an induction on the energy.

## TODO

We currently only prove the equipartition version of SRL.

* Prove the diagonal version.
* Prove the degree version.
* Define the regularity of a partition and prove the corresponding version.

## References

[YaÃ«l Dillies, Bhavik Mehta, *Formalising SzemerÃ©diâ€™s Regularity Lemma in Lean*][srl_itp]
-/


open Finpartition Finset Fintype Function SzemerediRegularity

open scoped Classical

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

variable {Î± : Type*} [Fintype Î±] (G : SimpleGraph Î±) {Îµ : â„} {l : â„•}

/-- Effective **SzemerÃ©di Regularity Lemma**: For any sufficiently large graph, there is an
`Îµ`-uniform equipartition of bounded size (where the bound does not depend on the graph). -/
theorem szemeredi_regularity (hÎµ : 0 < Îµ) (hl : l â‰¤ card Î±) :
    âˆƒ P : Finpartition univ,
      P.IsEquipartition âˆ§ l â‰¤ P.parts.card âˆ§ P.parts.card â‰¤ bound Îµ l âˆ§ P.IsUniform G Îµ := by
  obtain hÎ± | hÎ± := le_total (card Î±) (bound Îµ l)
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ l â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ bou â€¦
  -- If `card Î± â‰¤ bound Îµ l`, then the partition into singletons is acceptable.
  Â· refine' âŸ¨âŠ¥, bot_isEquipartition _, _âŸ©
    -- âŠ¢ l â‰¤ Finset.card âŠ¥.parts âˆ§ Finset.card âŠ¥.parts â‰¤ bound Îµ l âˆ§ IsUniform âŠ¥ G Îµ
    rw [card_bot, card_univ]
    -- âŠ¢ l â‰¤ Fintype.card Î± âˆ§ Fintype.card Î± â‰¤ bound Îµ l âˆ§ IsUniform âŠ¥ G Îµ
    exact âŸ¨hl, hÎ±, botIsUniform _ hÎµâŸ©
    -- ğŸ‰ no goals
  -- Else, let's start from a dummy equipartition of size `initialBound Îµ l`.
  let t := initialBound Îµ l
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ l â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ bou â€¦
  have htÎ± : t â‰¤ (univ : Finset Î±).card :=
    (initialBound_le_bound _ _).trans (by rwa [Finset.card_univ])
  obtain âŸ¨dum, hdumâ‚, hdumâ‚‚âŸ© :=
    exists_equipartition_card_eq (univ : Finset Î±) (initialBound_pos _ _).ne' htÎ±
  obtain hÎµâ‚ | hÎµâ‚ := le_total 1 Îµ
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ l â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ bou â€¦
  -- If `Îµ â‰¥ 1`, then this dummy equipartition is `Îµ`-uniform, so we're done.
  Â· exact âŸ¨dum, hdumâ‚, (le_initialBound Îµ l).trans hdumâ‚‚.ge,
      hdumâ‚‚.le.trans (initialBound_le_bound Îµ l), (dum.isUniformOne G).mono hÎµâ‚âŸ©
  -- Else, set up the induction on energy. We phrase it through the existence for each `i` of an
  -- equipartition of size bounded by `stepBound^[i] (initialBound Îµ l)` and which is either
  -- `Îµ`-uniform or has energy at least `Îµ ^ 5 / 4 * i`.
  have : Nonempty Î± := by
    rw [â† Fintype.card_pos_iff]
    exact (bound_pos _ _).trans_le hÎ±
  suffices h : âˆ€ i, âˆƒ P : Finpartition (univ : Finset Î±), P.IsEquipartition âˆ§ t â‰¤ P.parts.card âˆ§
    P.parts.card â‰¤ stepBound^[i] t âˆ§ (P.IsUniform G Îµ âˆ¨ Îµ ^ 5 / 4 * i â‰¤ P.energy G)
  -- For `i > 4 / Îµ ^ 5` we know that the partition we get can't have energy `â‰¥ Îµ ^ 5 / 4 * i > 1`,
  -- so it must instead be `Îµ`-uniform and we won.
  Â· obtain âŸ¨P, hPâ‚, hPâ‚‚, hPâ‚ƒ, hPâ‚„âŸ© := h (âŒŠ4 / Îµ ^ 5âŒ‹â‚Š + 1)
    -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ l â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ bou â€¦
    refine' âŸ¨P, hPâ‚, (le_initialBound _ _).trans hPâ‚‚, hPâ‚ƒ.trans _,
      hPâ‚„.resolve_right fun hPenergy => lt_irrefl (1 : â„) _âŸ©
    Â· rw [iterate_succ_apply']
      -- âŠ¢ stepBound (stepBound^[âŒŠâ†‘4 / Îµ ^ 5âŒ‹â‚Š] t) â‰¤ bound Îµ l
      exact mul_le_mul_left' (pow_le_pow_of_le_left (by norm_num) (by norm_num) _) _
      -- ğŸ‰ no goals
    calc
      (1 : â„) = Îµ ^ 5 / â†‘4 * (â†‘4 / Îµ ^ 5) := by
        rw [mul_comm, div_mul_div_cancel 4 (pow_pos hÎµ 5).ne']; norm_num
      _ < Îµ ^ 5 / 4 * (âŒŠ4 / Îµ ^ 5âŒ‹â‚Š + 1) :=
        ((mul_lt_mul_left <| by positivity).2 (Nat.lt_floor_add_one _))
      _ â‰¤ (P.energy G : â„) := by rwa [â† Nat.cast_add_one]
      _ â‰¤ 1 := by exact_mod_cast P.energy_le_one G
  -- Let's do the actual induction.
  intro i
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ t â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ ste â€¦
  induction' i with i ih
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ t â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ ste â€¦
  -- For `i = 0`, the dummy equipartition is enough.
  Â· refine' âŸ¨dum, hdumâ‚, hdumâ‚‚.ge, hdumâ‚‚.le, Or.inr _âŸ©
    -- âŠ¢ Îµ ^ 5 / 4 * â†‘Nat.zero â‰¤ â†‘(energy dum G)
    rw [Nat.cast_zero, mul_zero]
    -- âŠ¢ 0 â‰¤ â†‘(energy dum G)
    exact_mod_cast dum.energy_nonneg G
    -- ğŸ‰ no goals
  -- For the induction step at `i + 1`, find `P` the equipartition at `i`.
  obtain âŸ¨P, hPâ‚, hPâ‚‚, hPâ‚ƒ, hPâ‚„âŸ© := ih
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ t â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ ste â€¦
  by_cases huniform : P.IsUniform G Îµ
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ t â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ ste â€¦
  -- If `P` is already uniform, then no need to break it up further. We can just return `P` again.
  Â· refine' âŸ¨P, hPâ‚, hPâ‚‚, _, Or.inl huniformâŸ©
    -- âŠ¢ Finset.card P.parts â‰¤ stepBound^[Nat.succ i] t
    rw [iterate_succ_apply']
    -- âŠ¢ Finset.card P.parts â‰¤ stepBound (stepBound^[i] t)
    exact hPâ‚ƒ.trans (le_stepBound _)
    -- ğŸ‰ no goals
  -- Else, `P` must instead have energy at least `Îµ ^ 5 / 4 * i`.
  replace hPâ‚„ := hPâ‚„.resolve_left huniform
  -- âŠ¢ âˆƒ P, IsEquipartition P âˆ§ t â‰¤ Finset.card P.parts âˆ§ Finset.card P.parts â‰¤ ste â€¦
  -- We gather a few numerical facts.
  have hÎµl' : â†‘100 < â†‘4 ^ P.parts.card * Îµ ^ 5 :=
    (hundred_lt_pow_initialBound_mul hÎµ l).trans_le
      (mul_le_mul_of_nonneg_right (pow_le_pow (by norm_num) hPâ‚‚) <| by positivity)
  have hi : (i : â„) â‰¤ 4 / Îµ ^ 5 := by
    have hi : Îµ ^ 5 / 4 * â†‘i â‰¤ 1 := hPâ‚„.trans (by exact_mod_cast P.energy_le_one G)
    rw [div_mul_eq_mul_div, div_le_iff (show (0 : â„) < 4 by norm_num)] at hi
    norm_num at hi
    rwa [le_div_iff' (pow_pos hÎµ _)]
  have hsize : P.parts.card â‰¤ stepBound^[âŒŠ4 / Îµ ^ 5âŒ‹â‚Š] t :=
    hPâ‚ƒ.trans (monotone_iterate_of_id_le le_stepBound (Nat.le_floor hi) _)
  have hPÎ± : P.parts.card * 16 ^ P.parts.card â‰¤ card Î± :=
    (Nat.mul_le_mul hsize (Nat.pow_le_pow_of_le_right (by norm_num) hsize)).trans hÎ±
  -- We return the increment equipartition of `P`, which has energy `â‰¥ Îµ ^ 5 / 4 * (i + 1)`.
  refine' âŸ¨increment hPâ‚ G Îµ, increment_isEquipartition hPâ‚ G Îµ, _, _, Or.inr <| le_trans _ <|
    energy_increment hPâ‚ ((seven_le_initialBound Îµ l).trans hPâ‚‚) hÎµl' hPÎ± huniform hÎµâ‚âŸ©
  Â· rw [card_increment hPÎ± huniform]
    -- âŠ¢ t â‰¤ stepBound (Finset.card P.parts)
    exact hPâ‚‚.trans (le_stepBound _)
    -- ğŸ‰ no goals
  Â· rw [card_increment hPÎ± huniform, iterate_succ_apply']
    -- âŠ¢ stepBound (Finset.card P.parts) â‰¤ stepBound (stepBound^[i] t)
    exact stepBound_mono hPâ‚ƒ
    -- ğŸ‰ no goals
  Â· rw [Nat.cast_succ, mul_add, mul_one]
    -- âŠ¢ Îµ ^ 5 / 4 * â†‘i + Îµ ^ 5 / 4 â‰¤ â†‘(energy P G) + Îµ ^ 5 / 4
    exact add_le_add_right hPâ‚„ _
    -- ğŸ‰ no goals
#align szemeredi_regularity szemeredi_regularity
