/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.Module.Defs
import Mathlib.Algebra.Order.BigOperators.Group.Finset
import Mathlib.Algebra.Order.Ring.Basic
import Mathlib.Combinatorics.SimpleGraph.Density
import Mathlib.Data.Rat.BigOperators

#align_import combinatorics.simple_graph.regularity.energy from "leanprover-community/mathlib"@"bf7ef0e83e5b7e6c1169e97f055e58a2e4e9d52d"

/-!
# Energy of a partition

This file defines the energy of a partition.

The energy is the auxiliary quantity that drives the induction process in the proof of SzemerÃ©di's
Regularity Lemma. As long as we do not have a suitable equipartition, we will find a new one that
has an energy greater than the previous one plus some fixed constant.

## References

[YaÃ«l Dillies, Bhavik Mehta, *Formalising SzemerÃ©diâ€™s Regularity Lemma in Lean*][srl_itp]
-/


open Finset

variable {Î± : Type*} [DecidableEq Î±] {s : Finset Î±} (P : Finpartition s) (G : SimpleGraph Î±)
  [DecidableRel G.Adj]

namespace Finpartition

/-- The energy of a partition, also known as index. Auxiliary quantity for SzemerÃ©di's regularity
lemma.  -/
def energy : â„š :=
  ((âˆ‘ uv âˆˆ P.parts.offDiag, G.edgeDensity uv.1 uv.2 ^ 2) : â„š) / (P.parts.card : â„š) ^ 2
#align finpartition.energy Finpartition.energy

theorem energy_nonneg : 0 â‰¤ P.energy G := by
  exact div_nonneg (Finset.sum_nonneg fun _ _ => sq_nonneg _) <| sq_nonneg _
#align finpartition.energy_nonneg Finpartition.energy_nonneg

theorem energy_le_one : P.energy G â‰¤ 1 :=
  div_le_of_nonneg_of_le_mul (sq_nonneg _) zero_le_one <|
    calc
      âˆ‘ uv âˆˆ P.parts.offDiag, G.edgeDensity uv.1 uv.2 ^ 2 â‰¤ P.parts.offDiag.card â€¢ (1 : â„š) :=
        sum_le_card_nsmul _ _ 1 fun uv _ =>
          (sq_le_one_iff <| G.edgeDensity_nonneg _ _).2 <| G.edgeDensity_le_one _ _
      _ = P.parts.offDiag.card := Nat.smul_one_eq_cast _
      _ â‰¤ _ := by
        rw [offDiag_card, one_mul]
        norm_cast
        rw [sq]
        exact tsub_le_self
#align finpartition.energy_le_one Finpartition.energy_le_one

@[simp, norm_cast]
theorem coe_energy {ð•œ : Type*} [LinearOrderedField ð•œ] : (P.energy G : ð•œ) =
    (âˆ‘ uv âˆˆ P.parts.offDiag, (G.edgeDensity uv.1 uv.2 : ð•œ) ^ 2) / (P.parts.card : ð•œ) ^ 2 := by
  rw [energy]; norm_cast
#align finpartition.coe_energy Finpartition.coe_energy

end Finpartition
