/-
Copyright (c) 2025 Mitchell Horner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Horner
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Combinatorics.Enumerative.DoubleCounting
import Mathlib.Combinatorics.SimpleGraph.DeleteEdges
import Mathlib.Combinatorics.SimpleGraph.Extremal.Basic
import Mathlib.Data.Nat.Choose.Cast

/-!
# TurÃ¡n density

This file defines the **TurÃ¡n density** of a simple graph.

## Main definitions

* `SimpleGraph.turanDensity H` is the **TurÃ¡n density** of the simple graph `H`, defined as the
  limit of `extremalNumber n H / n.choose 2` as `n` approaches `âˆ`.

* `SimpleGraph.tendsto_turanDensity` is the proof that `SimpleGraph.turanDensity` is well-defined.

* `SimpleGraph.isEquivalent_extremalNumber` is the proof that `extremalNumber n H` is
  asymptotically equivalent to `turanDensity H * n.choose 2` as `n` approaches `âˆ`.
-/


open Asymptotics Filter Finset Fintype Topology

namespace SimpleGraph

variable {V W : Type*} {G : SimpleGraph V} {H : SimpleGraph W}

lemma antitoneOn_extremalNumber_div_choose_two (H : SimpleGraph W) :
    AntitoneOn (fun n â†¦ (extremalNumber n H / n.choose 2 : â„)) (Set.Ici 2) := by
  apply antitoneOn_nat_Ici_of_succ_le
  intro n hn
  conv_lhs =>
    enter [1, 1]
    rw [â† Fintype.card_fin (n+1)]
  rw [div_le_iffâ‚€ (mod_cast Nat.choose_pos (by linarith)),
    extremalNumber_le_iff_of_nonneg H (by positivity)]
  intro G _ h
  rw [mul_comm, â† mul_div_assoc, le_div_iffâ‚€' (mod_cast Nat.choose_pos hn), Nat.cast_choose_two,
    Nat.cast_choose_two, Nat.cast_add_one, add_sub_cancel_right (n : â„) 1,
    mul_comm _ (n-1 : â„), â† mul_div (n-1 : â„), mul_comm _ (n/2 : â„), mul_assoc, mul_comm (n-1 : â„),
    â† mul_div (n+1 : â„), mul_comm _ (n/2 : â„), mul_assoc, mul_le_mul_iff_rightâ‚€ (by positivity),
    â† Nat.cast_pred (by positivity), â†Nat.cast_mul, â†Nat.cast_add_one, â†Nat.cast_mul, Nat.cast_le]
  conv_rhs =>
    rw [â† Fintype.card_fin (n+1), â† card_univ]
  -- double counting `(v, e) â†¦ v âˆ‰ e`
  apply card_nsmul_le_card_nsmul' (r := fun v e â†¦ v âˆ‰ e)
  -- counting `e`
  Â· intro e he
    simp_rw [â† Sym2.mem_toFinset, bipartiteBelow, filter_not, filter_mem_eq_inter, univ_inter,
      â† compl_eq_univ_sdiff, card_compl, Fintype.card_fin, card_toFinset_mem_edgeFinset âŸ¨e, heâŸ©,
      Nat.cast_id, Nat.reduceSubDiff, le_refl]
  -- counting `v`
  Â· intro v hv
    simpa [edgeFinset_deleteIncidenceSet_eq_filter]
      using card_edgeFinset_deleteIncidenceSet_le_extremalNumber h v

/-- The **TurÃ¡n density** of a simple graph `H` is the limit of `extremalNumber n H / n.choose 2`
as `n` approaches `âˆ`.

See `SimpleGraph.tendsto_turanDensity` for proof of existence. -/
noncomputable def turanDensity (H : SimpleGraph W) :=
  limUnder atTop fun n â†¦ (extremalNumber n H / n.choose 2 : â„)

/-- The **TurÃ¡n density** of a simple graph `H` is well-defined. -/
theorem tendsto_turanDensity (H : SimpleGraph W) :
    Tendsto (fun n â†¦ (extremalNumber n H / n.choose 2 : â„)) atTop (ğ“ (turanDensity H)) := by
  let f := fun n â†¦ (extremalNumber n H / n.choose 2 : â„)
  suffices h : âˆƒ x, Tendsto (fun n â†¦ f (n + 2)) atTop (ğ“ x) by
    obtain âŸ¨_, hâŸ© := by simpa [tendsto_add_atTop_iff_nat 2] using h
    simpa [â† Tendsto.limUnder_eq h] using h
  use â¨… n, f (n + 2)
  apply tendsto_atTop_ciInf
  Â· rw [antitone_add_nat_iff_antitoneOn_nat_Ici]
    exact antitoneOn_extremalNumber_div_choose_two H
  Â· use 0
    intro n âŸ¨_, hnâŸ©
    rw [â† hn]
    positivity

/-- `extremalNumber n H` is asymptotically equivalent to `turanDensity H * n.choose 2` as `n`
approaches `âˆ`. -/
theorem isEquivalent_extremalNumber (h : turanDensity H â‰  0) :
    (fun n â†¦ (extremalNumber n H : â„)) ~[atTop] (fun n â†¦ (turanDensity H * n.choose 2 : â„)) := by
  have hÏ€ := tendsto_turanDensity H
  apply Tendsto.const_mul (1/turanDensity H : â„) at hÏ€
  simp_rw [one_div_mul_cancel h, div_mul_div_comm, one_mul] at hÏ€
  have hz : âˆ€á¶  (x : â„•) in atTop, turanDensity H * x.choose 2 â‰  0 := by
    rw [eventually_atTop]
    use 2
    intro n hn
    simp [h, Nat.choose_eq_zero_iff, hn]
  simpa [isEquivalent_iff_tendsto_one hz] using hÏ€

end SimpleGraph
