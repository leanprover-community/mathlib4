/-
Copyright (c) 2025 Mitchell Horner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Horner
-/
module

public import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
public import Mathlib.Combinatorics.Enumerative.DoubleCounting
public import Mathlib.Combinatorics.SimpleGraph.DeleteEdges
public import Mathlib.Combinatorics.SimpleGraph.Extremal.Basic
public import Mathlib.Data.Nat.Choose.Cast

/-!
# Tur√°n density

This file defines the **Tur√°n density** of a simple graph.

## Main definitions

* `SimpleGraph.turanDensity H` is the **Tur√°n density** of the simple graph `H`, defined as the
  limit of `extremalNumber n H / n.choose 2` as `n` approaches `‚àû`.

* `SimpleGraph.tendsto_turanDensity` is the proof that `SimpleGraph.turanDensity` is well-defined.

* `SimpleGraph.isEquivalent_extremalNumber` is the proof that `extremalNumber n H` is
  asymptotically equivalent to `turanDensity H * n.choose 2` as `n` approaches `‚àû`.

* `SimpleGraph.isContained_of_card_edgeFinset` is the proof that `n`-vertex simple graphs having
  at least `(turanDensity H + o(1)) * n ^ 2` edges contain `H`, for sufficently large `n`.
-/

@[expose] public section


open Asymptotics Filter Finset Fintype Topology

namespace SimpleGraph

variable {V W : Type*} {G : SimpleGraph V} {H : SimpleGraph W}

lemma antitoneOn_extremalNumber_div_choose_two (H : SimpleGraph W) :
    AntitoneOn (fun n ‚Ü¶ (extremalNumber n H / n.choose 2 : ‚Ñù)) (Set.Ici 2) := by
  apply antitoneOn_nat_Ici_of_succ_le
  intro n hn
  conv_lhs =>
    enter [1, 1]
    rw [‚Üê Fintype.card_fin (n+1)]
  rw [div_le_iff‚ÇÄ (mod_cast Nat.choose_pos (by linarith)),
    extremalNumber_le_iff_of_nonneg H (by positivity)]
  intro G _ h
  rw [mul_comm, ‚Üê mul_div_assoc, le_div_iff‚ÇÄ' (mod_cast Nat.choose_pos hn), Nat.cast_choose_two,
    Nat.cast_choose_two, Nat.cast_add_one, add_sub_cancel_right (n : ‚Ñù) 1,
    mul_comm _ (n-1 : ‚Ñù), ‚Üê mul_div (n-1 : ‚Ñù), mul_comm _ (n/2 : ‚Ñù), mul_assoc, mul_comm (n-1 : ‚Ñù),
    ‚Üê mul_div (n+1 : ‚Ñù), mul_comm _ (n/2 : ‚Ñù), mul_assoc, mul_le_mul_iff_right‚ÇÄ (by positivity),
    ‚Üê Nat.cast_pred (by positivity), ‚ÜêNat.cast_mul, ‚ÜêNat.cast_add_one, ‚ÜêNat.cast_mul, Nat.cast_le]
  conv_rhs =>
    rw [‚Üê Fintype.card_fin (n+1), ‚Üê card_univ]
  -- double counting `(v, e) ‚Ü¶ v ‚àâ e`
  apply card_nsmul_le_card_nsmul' (r := fun v e ‚Ü¶ v ‚àâ e)
  -- counting `e`
  ¬∑ intro e he
    simp_rw [‚Üê Sym2.mem_toFinset, bipartiteBelow, filter_not, filter_mem_eq_inter, univ_inter,
      ‚Üê compl_eq_univ_sdiff, card_compl, Fintype.card_fin, card_toFinset_mem_edgeFinset ‚ü®e, he‚ü©,
      Nat.cast_id, Nat.reduceSubDiff, le_refl]
  -- counting `v`
  ¬∑ intro v hv
    simpa [edgeFinset_deleteIncidenceSet_eq_filter]
      using card_edgeFinset_deleteIncidenceSet_le_extremalNumber h v

/-- The **Tur√°n density** of a simple graph `H` is the limit of `extremalNumber n H / n.choose 2`
as `n` approaches `‚àû`.

See `SimpleGraph.tendsto_turanDensity` for proof of existence. -/
noncomputable def turanDensity (H : SimpleGraph W) :=
  limUnder atTop fun n ‚Ü¶ (extremalNumber n H / n.choose 2 : ‚Ñù)

theorem isGLB_turanDensity (H : SimpleGraph W) :
    IsGLB { (extremalNumber n H / n.choose 2 : ‚Ñù) | n ‚àà Set.Ici 2 } (turanDensity H) := by
  apply Real.isGLB_limUnder_of_bddBelow_antitoneOn_Ici
  ¬∑ refine ‚ü®0, fun x ‚ü®_, _, hx‚ü© ‚Ü¶ ?_‚ü©
    rw [‚Üê hx]
    positivity
  ¬∑ exact antitoneOn_extremalNumber_div_choose_two H

theorem turanDensity_eq_csInf (H : SimpleGraph W) :
    turanDensity H = sInf { (extremalNumber n H / n.choose 2 : ‚Ñù) | n ‚àà Set.Ici 2 } :=
  have h := isGLB_turanDensity H
  (h.csInf_eq h.nonempty).symm

/-- The **Tur√°n density** of a simple graph `H` is well-defined. -/
theorem tendsto_turanDensity (H : SimpleGraph W) :
    Tendsto (fun n ‚Ü¶ (extremalNumber n H / n.choose 2 : ‚Ñù)) atTop (ùìù (turanDensity H)) := by
  have h_tendsto := Real.tendsto_csInf_of_bddBelow_antitoneOn_Ici
    (isGLB_turanDensity H).bddBelow (antitoneOn_extremalNumber_div_choose_two H)
  rwa [turanDensity, h_tendsto.limUnder_eq]

/-- `extremalNumber n H` is asymptotically equivalent to `turanDensity H * n.choose 2` as `n`
approaches `‚àû`. -/
theorem isEquivalent_extremalNumber (h : turanDensity H ‚â† 0) :
    (fun n ‚Ü¶ (extremalNumber n H : ‚Ñù)) ~[atTop] (fun n ‚Ü¶ (turanDensity H * n.choose 2 : ‚Ñù)) := by
  have hœÄ := tendsto_turanDensity H
  apply Tendsto.const_mul (1/turanDensity H : ‚Ñù) at hœÄ
  simp_rw [one_div_mul_cancel h, div_mul_div_comm, one_mul] at hœÄ
  have hz : ‚àÄ·∂† (x : ‚Ñï) in atTop, turanDensity H * x.choose 2 ‚â† 0 := by
    rw [eventually_atTop]
    refine ‚ü®2, fun n hn ‚Ü¶ ?_‚ü©
    simp [h, Nat.choose_eq_zero_iff, hn]
  simpa [isEquivalent_iff_tendsto_one hz] using hœÄ

/-- `n`-vertex simple graphs having at least `(turanDensity H + o(1)) * n ^ 2` edges contain
`H`, for sufficently large `n`. -/
theorem isContained_of_card_edgeFinset (H : SimpleGraph W) {Œµ : ‚Ñù} (hŒµ_pos : 0 < Œµ) :
    ‚àÉ N, ‚àÄ n ‚â• N, ‚àÄ {G : SimpleGraph (Fin n)} [DecidableRel G.Adj],
      #G.edgeFinset ‚â• (turanDensity H + Œµ) * n.choose 2 ‚Üí H ‚äë G := by
  have hœÄ := (turanDensity_eq_csInf H).ge
  contrapose! hœÄ with h
  apply lt_of_lt_of_le
  ¬∑ exact lt_add_of_pos_right (turanDensity H) hŒµ_pos
  ¬∑ refine le_csInf ?_ (fun x ‚ü®m, hm, hx‚ü© ‚Ü¶ ?_)
    ¬∑ rw [‚Üê Set.image, Set.image_nonempty]
      exact Set.nonempty_Ici
    ¬∑ rw [‚Üê hx]
      have ‚ü®n, hn, G, _, hcard_edges, h_free‚ü© := h m
      replace h_free : H.Free G := by rwa [‚Üê not_nonempty_iff] at h_free
      trans (extremalNumber n H / n.choose 2)
      ¬∑ rw [le_div_iff‚ÇÄ <| mod_cast Nat.choose_pos (hm.trans hn)]
        conv =>
          enter [2, 1, 1]
          rw [‚Üê Fintype.card_fin n]
        exact hcard_edges.trans (mod_cast card_edgeFinset_le_extremalNumber h_free)
      ¬∑ exact antitoneOn_extremalNumber_div_choose_two H hm (hm.trans hn) hn

end SimpleGraph
