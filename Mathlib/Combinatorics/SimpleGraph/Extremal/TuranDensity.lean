/-
Copyright (c) 2025 Mitchell Horner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Horner
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Combinatorics.SimpleGraph.DeleteEdges
import Mathlib.Combinatorics.SimpleGraph.Extremal.Basic
import Mathlib.Data.Nat.Choose.Cast

/-!
# TurÃ¡n density

This files defines the **TurÃ¡n density** of a simple graph.

## Main definitions

* `SimpleGraph.turanDensity H` is the **TurÃ¡n density** of the simple graph `H`, defined as the
  limit of `extremalNumber n H / n.choose 2` as `n` approaches `âˆ`.

* `SimpleGraph.tendsto_turanDensity` is the proof that `SimpleGraph.turanDensity` is well-defined.

* `SimpleGraph.isEquivalent_extremalNumber` is the proof that `extremalNumber n H` is
  asymptotically equivalent to `turanDensity H * n.choose 2` as `n` approaches `âˆ`.
-/


open Asymptotics Filter Finset Fintype Topology

namespace SimpleGraph

variable {V W : Type*} {G : SimpleGraph V} {H : SimpleGraph W}

/-- If `G` is `H.Free`, then `G.deleteIncidenceSet v` is also `H.Free` and has at most
`extremalNumber (card V-1) H` many edges. -/
theorem card_edgeFinset_deleteIncidenceSet_le_extremalNumber
    [Fintype V] [DecidableRel G.Adj] [DecidableEq V] (h : H.Free G) (v : V) :
    #(G.deleteIncidenceSet v).edgeFinset â‰¤ extremalNumber (card V-1) H := by
  rw [â† card_edgeFinset_induce_compl_singleton, â† @card_unique ({v} : Set V), â† card_compl_set]
  apply card_edgeFinset_le_extremalNumber
  contrapose! h
  rw [not_free] at h âŠ¢
  exact h.trans âŸ¨Copy.induce G {v}á¶œâŸ©

lemma extremalNumber_div_choose_two_succ_le {n : â„•} (hn : 2 â‰¤ n) (H : SimpleGraph W) :
    (extremalNumber (n+1) H / (n+1).choose 2 : â„) â‰¤ (extremalNumber n H / n.choose 2 : â„) := by
  conv_lhs =>
    enter [1, 1, 1]
    rw [â† Fintype.card_fin (n+1)]
  rw [div_le_iffâ‚€ (mod_cast Nat.choose_pos (by linarith)),
    extremalNumber_le_iff_of_nonneg H (by positivity)]
  intro G _ h
  rw [mul_comm, â† mul_div_assoc, le_div_iffâ‚€' (mod_cast Nat.choose_pos hn), Nat.cast_choose_two,
    Nat.cast_choose_two, Nat.cast_add_one, add_sub_cancel_right (n : â„) 1,
    mul_comm _ (n-1 : â„), â† mul_div (n-1 : â„), mul_comm _ (n/2 : â„), mul_assoc,
    â† mul_div (n+1 : â„), mul_comm _ (n/2 : â„), mul_assoc, mul_le_mul_left (by positivity),
    â† Nat.cast_pred (by positivity), â†Nat.cast_mul, â†Nat.cast_add_one, â†Nat.cast_mul, Nat.cast_le]
  -- double counting `(v, e) â†¦ v âˆ‰ e`
  let s := (univ Ã—Ë¢ G.edgeFinset).filter fun (v, e) â†¦ v âˆ‰ e
  trans #s
  -- counting `e`
  Â· conv_rhs =>
      rw [card_filter, sum_product_right]
      enter [2, x]
      rw [â† card_filter]
    simp_rw [Sym2.mem_toFinset, filter_not, filter_mem_eq_inter, â† sum_attach G.edgeFinset,
      univ_inter, â† compl_eq_univ_sdiff, card_compl, Fintype.card_fin, card_toFinset_mem_edgeFinset,
      sum_const, card_attach, smul_eq_mul, Nat.succ_sub_succ_eq_sub, mul_comm]
    rfl
  -- counting `v`
  Â· conv_rhs =>
      rw [â† Fintype.card_fin (n+1), â† card_univ, â† smul_eq_mul, â† sum_const]
    conv_lhs =>
      rw [card_filter, sum_product]
      enter [2, x]
      rw [â† card_filter]
    apply sum_le_sum
    intro i _
    simpa [edgeFinset_deleteIncidenceSet_eq_filter]
      using card_edgeFinset_deleteIncidenceSet_le_extremalNumber h i

/-- The limit `extremalNumber (Fin n) H / n.choose 2` as `n` approaches `âˆ` exists. -/
lemma exists_tendsto_extremalNumber_div_choose_two (H : SimpleGraph W) :
    âˆƒ x, Tendsto (fun (n : â„•) â†¦ (extremalNumber n H / n.choose 2 : â„)) atTop (ğ“ x) := by
  let f := fun (n : â„•) â†¦ (extremalNumber n H / n.choose 2 : â„)
  suffices h : âˆƒ x, Tendsto (fun (n : â„•) â†¦ f (n + 2)) atTop (ğ“ x) by
    simpa [tendsto_add_atTop_iff_nat 2] using h
  use â¨… n, f (n + 2)
  apply tendsto_atTop_ciInf
  Â· apply antitone_nat_of_succ_le
    intro n
    apply extremalNumber_div_choose_two_succ_le
    exact le_add_of_nonneg_left n.zero_le
  Â· use 0
    intro n âŸ¨_, hnâŸ©
    rw [â† hn]
    positivity

/-- The **TurÃ¡n density** of a simple graph `H` is the limit of `extremalNumber n H / n.choose 2`
as `n` approaches `âˆ`.

See `SimpleGraph.tendsto_turanDensity` for proof of existence. -/
noncomputable def turanDensity (H : SimpleGraph W) :=
  limUnder atTop fun (n : â„•) â†¦ (extremalNumber n H / n.choose 2 : â„)

/-- The **TurÃ¡n density** of a simple graph `H` is well-defined. -/
theorem tendsto_turanDensity (H : SimpleGraph W) :
    Tendsto (fun (n : â„•) â†¦ (extremalNumber n H / n.choose 2 : â„)) atTop (ğ“ (turanDensity H)) := by
  have âŸ¨_, hâŸ© := exists_tendsto_extremalNumber_div_choose_two H
  rwa [â† Tendsto.limUnder_eq h] at h

/-- `extremalNumber n H` is asymptotically equivalent to `turanDensity H * n.choose 2` as `n`
approaches `âˆ`. -/
theorem isEquivalent_extremalNumber (h : turanDensity H â‰  0) :
    (fun (n : â„•) â†¦ (extremalNumber n H : â„))
      ~[atTop] (fun (n : â„•) â†¦ (turanDensity H * n.choose 2 : â„)) := by
  have hÏ€ := tendsto_turanDensity H
  apply Tendsto.const_mul (1/turanDensity H : â„) at hÏ€
  simp_rw [one_div_mul_cancel h, div_mul_div_comm, one_mul] at hÏ€
  have hz : âˆ€á¶  (x : â„•) in atTop, turanDensity H * x.choose 2 â‰  0 := by
    rw [eventually_atTop]
    use 2
    intro n hn
    simp [h, Nat.choose_eq_zero_iff, hn]
  simpa [isEquivalent_iff_tendsto_one hz] using hÏ€

end SimpleGraph
