/-
Copyright (c) 2022 Anand Rao, RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anand Rao, RÃ©mi Bottinelli
-/
import Mathlib.CategoryTheory.CofilteredSystem
import Mathlib.Combinatorics.SimpleGraph.Connectivity
import Mathlib.Data.Finite.Set

#align_import combinatorics.simple_graph.ends.defs from "leanprover-community/mathlib"@"b99e2d58a5e6861833fa8de11e51a81144258db4"

/-!
# Ends

This file contains a definition of the ends of a simple graph, as sections of the inverse system
assigning, to each finite set of vertices, the connected components of its complement.
-/


universe u

variable {V : Type u} (G : SimpleGraph V) (K L L' M : Set V)

namespace SimpleGraph

/-- The components outside a given set of vertices `K` -/
@[reducible]
def ComponentCompl :=
  (G.induce Ká¶œ).ConnectedComponent
#align simple_graph.component_compl SimpleGraph.ComponentCompl

variable {G} {K L M}

/-- The connected component of `v` in `G.induce Ká¶œ`. -/
@[reducible]
def componentComplMk (G : SimpleGraph V) {v : V} (vK : v âˆ‰ K) : G.ComponentCompl K :=
  connectedComponentMk (G.induce Ká¶œ) âŸ¨v, vKâŸ©
#align simple_graph.component_compl_mk SimpleGraph.componentComplMk

/-- The set of vertices of `G` making up the connected component `C` -/
def ComponentCompl.supp (C : G.ComponentCompl K) : Set V :=
  { v : V | âˆƒ h : v âˆ‰ K, G.componentComplMk h = C }
#align simple_graph.component_compl.supp SimpleGraph.ComponentCompl.supp

@[ext]
theorem ComponentCompl.supp_injective :
    Function.Injective (ComponentCompl.supp : G.ComponentCompl K â†’ Set V) := by
  refine' ConnectedComponent.indâ‚‚ _
  -- âŠ¢ âˆ€ (v w : â†‘Ká¶œ), supp (connectedComponentMk (induce Ká¶œ G) v) = supp (connected â€¦
  rintro âŸ¨v, hvâŸ© âŸ¨w, hwâŸ© h
  -- âŠ¢ connectedComponentMk (induce Ká¶œ G) { val := v, property := hv } = connectedC â€¦
  simp only [Set.ext_iff, ConnectedComponent.eq, Set.mem_setOf_eq, ComponentCompl.supp] at h âŠ¢
  -- âŠ¢ Reachable (induce Ká¶œ G) { val := v, property := hv } { val := w, property := â€¦
  exact ((h v).mp âŸ¨hv, Reachable.refl _âŸ©).choose_spec
  -- ğŸ‰ no goals
#align simple_graph.component_compl.supp_injective SimpleGraph.ComponentCompl.supp_injective

theorem ComponentCompl.supp_inj {C D : G.ComponentCompl K} : C.supp = D.supp â†” C = D :=
  ComponentCompl.supp_injective.eq_iff
#align simple_graph.component_compl.supp_inj SimpleGraph.ComponentCompl.supp_inj

instance ComponentCompl.setLike : SetLike (G.ComponentCompl K) V where
  coe := ComponentCompl.supp
  coe_injective' _ _ := ComponentCompl.supp_inj.mp
#align simple_graph.component_compl.set_like SimpleGraph.ComponentCompl.setLike

@[simp]
theorem ComponentCompl.mem_supp_iff {v : V} {C : ComponentCompl G K} :
    v âˆˆ C â†” âˆƒ vK : v âˆ‰ K, G.componentComplMk vK = C :=
  Iff.rfl
#align simple_graph.component_compl.mem_supp_iff SimpleGraph.ComponentCompl.mem_supp_iff

theorem componentComplMk_mem (G : SimpleGraph V) {v : V} (vK : v âˆ‰ K) : v âˆˆ G.componentComplMk vK :=
  âŸ¨vK, rflâŸ©
#align simple_graph.component_compl_mk_mem SimpleGraph.componentComplMk_mem

theorem componentComplMk_eq_of_adj (G : SimpleGraph V) {v w : V} (vK : v âˆ‰ K) (wK : w âˆ‰ K)
    (a : G.Adj v w) : G.componentComplMk vK = G.componentComplMk wK := by
  rw [ConnectedComponent.eq]
  -- âŠ¢ Reachable (induce Ká¶œ G) { val := v, property := vK } { val := w, property := â€¦
  apply Adj.reachable
  -- âŠ¢ Adj (induce Ká¶œ G) { val := v, property := vK } { val := w, property := wK }
  exact a
  -- ğŸ‰ no goals
#align simple_graph.component_compl_mk_eq_of_adj SimpleGraph.componentComplMk_eq_of_adj

/-- In an infinite graph, the set of components out of a finite set is nonempty. -/
instance componentCompl_nonempty_of_infinite (G : SimpleGraph V) [Infinite V] (K : Finset V) :
    Nonempty (G.ComponentCompl K) :=
  let âŸ¨_, kKâŸ© := K.finite_toSet.infinite_compl.nonempty
  âŸ¨componentComplMk _ kKâŸ©

namespace ComponentCompl

/-- A `ComponentCompl` specialization of `Quot.lift`, where soundness has to be proved only
for adjacent vertices.
-/
protected def lift {Î² : Sort*} (f : âˆ€ â¦ƒvâ¦„ (_ : v âˆ‰ K), Î²)
    (h : âˆ€ â¦ƒv wâ¦„ (hv : v âˆ‰ K) (hw : w âˆ‰ K) (_ : G.Adj v w), f hv = f hw) : G.ComponentCompl K â†’ Î² :=
  ConnectedComponent.lift (fun vv => f vv.prop) fun v w p => by
    induction' p with _ u v w a q ih
    -- âŠ¢ Walk.IsPath Walk.nil â†’ (fun vv => f (_ : â†‘vv âˆˆ Ká¶œ)) uâœ = (fun vv => f (_ : â†‘ â€¦
    Â· rintro _
      -- âŠ¢ (fun vv => f (_ : â†‘vv âˆˆ Ká¶œ)) uâœ = (fun vv => f (_ : â†‘vv âˆˆ Ká¶œ)) uâœ
      rfl
      -- ğŸ‰ no goals
    Â· rintro h'
      -- âŠ¢ (fun vv => f (_ : â†‘vv âˆˆ Ká¶œ)) u = (fun vv => f (_ : â†‘vv âˆˆ Ká¶œ)) w
      exact (h u.prop v.prop a).trans (ih h'.of_cons)
      -- ğŸ‰ no goals
#align simple_graph.component_compl.lift SimpleGraph.ComponentCompl.lift

@[elab_as_elim] -- Porting note: added
protected theorem ind {Î² : G.ComponentCompl K â†’ Prop}
    (f : âˆ€ â¦ƒvâ¦„ (hv : v âˆ‰ K), Î² (G.componentComplMk hv)) : âˆ€ C : G.ComponentCompl K, Î² C := by
  apply ConnectedComponent.ind
  -- âŠ¢ âˆ€ (v : â†‘Ká¶œ), Î² (connectedComponentMk (induce Ká¶œ G) v)
  exact fun âŸ¨v, vnKâŸ© => f vnK
  -- ğŸ‰ no goals
#align simple_graph.component_compl.ind SimpleGraph.ComponentCompl.ind

/-- The induced graph on the vertices `C`. -/
@[reducible]
protected def coeGraph (C : ComponentCompl G K) : SimpleGraph C :=
  G.induce (C : Set V)
#align simple_graph.component_compl.coe_graph SimpleGraph.ComponentCompl.coeGraph

theorem coe_inj {C D : G.ComponentCompl K} : (C : Set V) = (D : Set V) â†” C = D :=
  SetLike.coe_set_eq
#align simple_graph.component_compl.coe_inj SimpleGraph.ComponentCompl.coe_inj

@[simp]
protected theorem nonempty (C : G.ComponentCompl K) : (C : Set V).Nonempty :=
  C.ind fun v vnK => âŸ¨v, vnK, rflâŸ©
#align simple_graph.component_compl.nonempty SimpleGraph.ComponentCompl.nonempty

protected theorem exists_eq_mk (C : G.ComponentCompl K) :
    âˆƒ (v : _) (h : v âˆ‰ K), G.componentComplMk h = C :=
  C.nonempty
#align simple_graph.component_compl.exists_eq_mk SimpleGraph.ComponentCompl.exists_eq_mk

protected theorem disjoint_right (C : G.ComponentCompl K) : Disjoint K C := by
  rw [Set.disjoint_iff]
  -- âŠ¢ K âˆ© â†‘C âŠ† âˆ…
  exact fun v âŸ¨vK, vCâŸ© => vC.choose vK
  -- ğŸ‰ no goals
#align simple_graph.component_compl.disjoint_right SimpleGraph.ComponentCompl.disjoint_right

theorem not_mem_of_mem {C : G.ComponentCompl K} {c : V} (cC : c âˆˆ C) : c âˆ‰ K := fun cK =>
  Set.disjoint_iff.mp C.disjoint_right âŸ¨cK, cCâŸ©
#align simple_graph.component_compl.not_mem_of_mem SimpleGraph.ComponentCompl.not_mem_of_mem

protected theorem pairwise_disjoint :
    Pairwise fun C D : G.ComponentCompl K => Disjoint (C : Set V) (D : Set V) := by
  rintro C D ne
  -- âŠ¢ Disjoint â†‘C â†‘D
  rw [Set.disjoint_iff]
  -- âŠ¢ â†‘C âˆ© â†‘D âŠ† âˆ…
  exact fun u âŸ¨uC, uDâŸ© => ne (uC.choose_spec.symm.trans uD.choose_spec)
  -- ğŸ‰ no goals
#align simple_graph.component_compl.pairwise_disjoint SimpleGraph.ComponentCompl.pairwise_disjoint

/-- Any vertex adjacent to a vertex of `C` and not lying in `K` must lie in `C`.
-/
theorem mem_of_adj : âˆ€ {C : G.ComponentCompl K} (c d : V), c âˆˆ C â†’ d âˆ‰ K â†’ G.Adj c d â†’ d âˆˆ C :=
  fun {C} c d âŸ¨cnK, hâŸ© dnK cd =>
  âŸ¨dnK, by
    rw [â† h, ConnectedComponent.eq]
    -- âŠ¢ Reachable (induce Ká¶œ G) { val := d, property := dnK } { val := c, property : â€¦
    exact Adj.reachable cd.symmâŸ©
    -- ğŸ‰ no goals
#align simple_graph.component_compl.mem_of_adj SimpleGraph.ComponentCompl.mem_of_adj

/--
Assuming `G` is preconnected and `K` not empty, given any connected component `C` outside of `K`,
there exists a vertex `k âˆˆ K` adjacent to a vertex `v âˆˆ C`.
-/
theorem exists_adj_boundary_pair (Gc : G.Preconnected) (hK : K.Nonempty) :
    âˆ€ C : G.ComponentCompl K, âˆƒ ck : V Ã— V, ck.1 âˆˆ C âˆ§ ck.2 âˆˆ K âˆ§ G.Adj ck.1 ck.2 := by
  refine' ComponentCompl.ind fun v vnK => _
  -- âŠ¢ âˆƒ ck, ck.fst âˆˆ componentComplMk G vnK âˆ§ ck.snd âˆˆ K âˆ§ Adj G ck.fst ck.snd
  let C : G.ComponentCompl K := G.componentComplMk vnK
  -- âŠ¢ âˆƒ ck, ck.fst âˆˆ componentComplMk G vnK âˆ§ ck.snd âˆˆ K âˆ§ Adj G ck.fst ck.snd
  let dis := Set.disjoint_iff.mp C.disjoint_right
  -- âŠ¢ âˆƒ ck, ck.fst âˆˆ componentComplMk G vnK âˆ§ ck.snd âˆˆ K âˆ§ Adj G ck.fst ck.snd
  by_contra' h
  -- âŠ¢ False
  suffices Set.univ = (C : Set V) by exact dis âŸ¨hK.choose_spec, this â–¸ Set.mem_univ hK.someâŸ©
  -- âŠ¢ Set.univ = â†‘C
  symm
  -- âŠ¢ â†‘C = Set.univ
  rw [Set.eq_univ_iff_forall]
  -- âŠ¢ âˆ€ (x : V), x âˆˆ â†‘C
  rintro u
  -- âŠ¢ u âˆˆ â†‘C
  by_contra unC
  -- âŠ¢ False
  obtain âŸ¨pâŸ© := Gc v u
  -- âŠ¢ False
  obtain âŸ¨âŸ¨âŸ¨x, yâŸ©, xyâŸ©, -, xC, ynCâŸ© :=
    p.exists_boundary_dart (C : Set V) (G.componentComplMk_mem vnK) unC
  exact ynC (mem_of_adj x y xC (fun yK : y âˆˆ K => h âŸ¨x, yâŸ© xC yK xy) xy)
  -- ğŸ‰ no goals
#align simple_graph.component_compl.exists_adj_boundary_pair SimpleGraph.ComponentCompl.exists_adj_boundary_pair

/--
If `K âŠ† L`, the components outside of `L` are all contained in a single component outside of `K`.
-/
@[reducible]
def hom (h : K âŠ† L) (C : G.ComponentCompl L) : G.ComponentCompl K :=
  C.map <| induceHom Hom.id <| Set.compl_subset_compl.2 h
#align simple_graph.component_compl.hom SimpleGraph.ComponentCompl.hom

theorem subset_hom (C : G.ComponentCompl L) (h : K âŠ† L) : (C : Set V) âŠ† (C.hom h : Set V) := by
  rintro c âŸ¨cL, rflâŸ©
  -- âŠ¢ c âˆˆ â†‘(hom h (componentComplMk G cL))
  exact âŸ¨fun h' => cL (h h'), rflâŸ©
  -- ğŸ‰ no goals
#align simple_graph.component_compl.subset_hom SimpleGraph.ComponentCompl.subset_hom

theorem _root_.SimpleGraph.componentComplMk_mem_hom
    (G : SimpleGraph V) {v : V} (vK : v âˆ‰ K) (h : L âŠ† K) :
    v âˆˆ (G.componentComplMk vK).hom h :=
  subset_hom (G.componentComplMk vK) h (G.componentComplMk_mem vK)
#align simple_graph.component_compl_mk_mem_hom SimpleGraph.componentComplMk_mem_hom

theorem hom_eq_iff_le (C : G.ComponentCompl L) (h : K âŠ† L) (D : G.ComponentCompl K) :
    C.hom h = D â†” (C : Set V) âŠ† (D : Set V) :=
  âŸ¨fun h' => h' â–¸ C.subset_hom h, C.ind fun _ vnL vD => (vD âŸ¨vnL, rflâŸ©).choose_specâŸ©
#align simple_graph.component_compl.hom_eq_iff_le SimpleGraph.ComponentCompl.hom_eq_iff_le

theorem hom_eq_iff_not_disjoint (C : G.ComponentCompl L) (h : K âŠ† L) (D : G.ComponentCompl K) :
    C.hom h = D â†” Â¬Disjoint (C : Set V) (D : Set V) := by
  rw [Set.not_disjoint_iff]
  -- âŠ¢ hom h C = D â†” âˆƒ x, x âˆˆ â†‘C âˆ§ x âˆˆ â†‘D
  constructor
  -- âŠ¢ hom h C = D â†’ âˆƒ x, x âˆˆ â†‘C âˆ§ x âˆˆ â†‘D
  Â· rintro rfl
    -- âŠ¢ âˆƒ x, x âˆˆ â†‘C âˆ§ x âˆˆ â†‘(hom h C)
    refine C.ind fun x xnL => ?_
    -- âŠ¢ âˆƒ x_1, x_1 âˆˆ â†‘(componentComplMk G xnL) âˆ§ x_1 âˆˆ â†‘(hom h (componentComplMk G x â€¦
    exact âŸ¨x, âŸ¨xnL, rflâŸ©, âŸ¨fun xK => xnL (h xK), rflâŸ©âŸ©
    -- ğŸ‰ no goals
  Â· refine C.ind fun x xnL => ?_
    -- âŠ¢ (âˆƒ x_1, x_1 âˆˆ â†‘(componentComplMk G xnL) âˆ§ x_1 âˆˆ â†‘D) â†’ hom h (componentComplM â€¦
    rintro âŸ¨x, âŸ¨_, eâ‚âŸ©, _, rflâŸ©
    -- âŠ¢ hom h (componentComplMk G xnL) = componentComplMk G wâœ
    rw [â† eâ‚]
    -- âŠ¢ hom h (componentComplMk G wâœÂ¹) = componentComplMk G wâœ
    rfl
    -- ğŸ‰ no goals
#align simple_graph.component_compl.hom_eq_iff_not_disjoint SimpleGraph.ComponentCompl.hom_eq_iff_not_disjoint

theorem hom_refl (C : G.ComponentCompl L) : C.hom (subset_refl L) = C := by
  change C.map _ = C
  -- âŠ¢ ConnectedComponent.map (induceHom Hom.id (_ : Lá¶œ âŠ† Lá¶œ)) C = C
  erw [induceHom_id G Lá¶œ, ConnectedComponent.map_id]
  -- ğŸ‰ no goals
#align simple_graph.component_compl.hom_refl SimpleGraph.ComponentCompl.hom_refl

theorem hom_trans (C : G.ComponentCompl L) (h : K âŠ† L) (h' : M âŠ† K) :
    C.hom (h'.trans h) = (C.hom h).hom h' := by
  change C.map _ = (C.map _).map _
  -- âŠ¢ ConnectedComponent.map (induceHom Hom.id (_ : Lá¶œ âŠ† Má¶œ)) C = ConnectedCompone â€¦
  erw [ConnectedComponent.map_comp, induceHom_comp]
  -- âŠ¢ ConnectedComponent.map (induceHom Hom.id (_ : Lá¶œ âŠ† Má¶œ)) C = ConnectedCompone â€¦
  rfl
  -- ğŸ‰ no goals
#align simple_graph.component_compl.hom_trans SimpleGraph.ComponentCompl.hom_trans

theorem hom_mk {v : V} (vnL : v âˆ‰ L) (h : K âŠ† L) :
    (G.componentComplMk vnL).hom h = G.componentComplMk (Set.not_mem_subset h vnL) :=
  rfl
#align simple_graph.component_compl.hom_mk SimpleGraph.ComponentCompl.hom_mk

theorem hom_infinite (C : G.ComponentCompl L) (h : K âŠ† L) (Cinf : (C : Set V).Infinite) :
    (C.hom h : Set V).Infinite :=
  Set.Infinite.mono (C.subset_hom h) Cinf
#align simple_graph.component_compl.hom_infinite SimpleGraph.ComponentCompl.hom_infinite

theorem infinite_iff_in_all_ranges {K : Finset V} (C : G.ComponentCompl K) :
    C.supp.Infinite â†” âˆ€ (L) (h : K âŠ† L), âˆƒ D : G.ComponentCompl L, D.hom h = C := by
  classical
    constructor
    Â· rintro Cinf L h
      obtain âŸ¨v, âŸ¨vK, rflâŸ©, vLâŸ© := Set.Infinite.nonempty (Set.Infinite.diff Cinf L.finite_toSet)
      exact âŸ¨componentComplMk _ vL, rflâŸ©
    Â· rintro h Cfin
      obtain âŸ¨D, eâŸ© := h (K âˆª Cfin.toFinset) (Finset.subset_union_left K Cfin.toFinset)
      obtain âŸ¨v, vDâŸ© := D.nonempty
      let Ddis := D.disjoint_right
      simp_rw [Finset.coe_union, Set.Finite.coe_toFinset, Set.disjoint_union_left,
        Set.disjoint_iff] at Ddis
      exact Ddis.right âŸ¨(ComponentCompl.hom_eq_iff_le _ _ _).mp e vD, vDâŸ©
#align simple_graph.component_compl.infinite_iff_in_all_ranges SimpleGraph.ComponentCompl.infinite_iff_in_all_ranges

end ComponentCompl

/- For a locally finite preconnected graph, the number of components outside of any finite set
is finite. -/
instance componentCompl_finite [LocallyFinite G] [Gpc : Fact G.Preconnected] (K : Finset V) :
    Finite (G.ComponentCompl K) := by
  classical
  rcases K.eq_empty_or_nonempty with rfl | h
  -- If K is empty, then removing K doesn't change the graph, which is connected, hence has a
  -- single connected component
  Â· dsimp [ComponentCompl]
    rw [Finset.coe_empty, Set.compl_empty]
    have := Gpc.out.subsingleton_connectedComponent
    exact Finite.of_equiv _ (induceUnivIso G).connectedComponentEquiv.symm
  -- Otherwise, we consider the function `touch` mapping a connected component to one of its
  -- vertices adjacent to `K`.
  Â· let touch (C : G.ComponentCompl K) : {v : V | âˆƒ k : V, k âˆˆ K âˆ§ G.Adj k v} :=
      let p := C.exists_adj_boundary_pair Gpc.out h
      âŸ¨p.choose.1, p.choose.2, p.choose_spec.2.1, p.choose_spec.2.2.symmâŸ©
    -- `touch` is injective
    have touch_inj : touch.Injective := fun C D h' => ComponentCompl.pairwise_disjoint.eq
      (Set.not_disjoint_iff.mpr âŸ¨touch C, (C.exists_adj_boundary_pair Gpc.out h).choose_spec.1,
                                 h'.symm â–¸ (D.exists_adj_boundary_pair Gpc.out h).choose_spec.1âŸ©)
    -- `touch` has finite range
    have : Finite (Set.range touch) := by
      refine @Subtype.finite _ (Set.Finite.to_subtype ?_) _
      apply Set.Finite.ofFinset (K.biUnion (fun v => G.neighborFinset v))
      simp only [Finset.mem_biUnion, mem_neighborFinset, Set.mem_setOf_eq, implies_true]
    -- hence `touch` has a finite domain
    apply Finite.of_injective_finite_range touch_inj

section Ends

variable (G)

open CategoryTheory

/--
The functor assigning, to a finite set in `V`, the set of connected components in its complement.
-/
@[simps]
def componentComplFunctor : (Finset V)áµ’áµ– â¥¤ Type u where
  obj K := G.ComponentCompl K.unop
  map f := ComponentCompl.hom (le_of_op_hom f)
  map_id _ := funext fun C => C.hom_refl
  map_comp h h' := funext fun C => C.hom_trans (le_of_op_hom h) (le_of_op_hom h')
#align simple_graph.component_compl_functor SimpleGraph.componentComplFunctor

/-- The end of a graph, defined as the sections of the functor `component_compl_functor` . -/
protected def Â«endÂ» :=
  (componentComplFunctor G).sections
#align simple_graph.end SimpleGraph.end

theorem end_hom_mk_of_mk {s} (sec : s âˆˆ G.end) {K L : (Finset V)áµ’áµ–} (h : L âŸ¶ K) {v : V}
    (vnL : v âˆ‰ L.unop) (hs : s L = G.componentComplMk vnL) :
    s K = G.componentComplMk (Set.not_mem_subset (le_of_op_hom h : _ âŠ† _) vnL) := by
  rw [â† sec h, hs]
  -- âŠ¢ (componentComplFunctor G).map h (componentComplMk G vnL) = componentComplMk  â€¦
  apply ComponentCompl.hom_mk _ (le_of_op_hom h : _ âŠ† _)
  -- ğŸ‰ no goals
#align simple_graph.end_hom_mk_of_mk SimpleGraph.end_hom_mk_of_mk

theorem infinite_iff_in_eventualRange {K : (Finset V)áµ’áµ–} (C : G.componentComplFunctor.obj K) :
    C.supp.Infinite â†” C âˆˆ G.componentComplFunctor.eventualRange K := by
  simp only [C.infinite_iff_in_all_ranges, CategoryTheory.Functor.eventualRange, Set.mem_iInter,
    Set.mem_range, componentComplFunctor_map]
  exact
    âŸ¨fun h Lop KL => h Lop.unop (le_of_op_hom KL), fun h L KL =>
      h (Opposite.op L) (opHomOfLE KL)âŸ©
#align simple_graph.infinite_iff_in_eventual_range SimpleGraph.infinite_iff_in_eventualRange

end Ends

end SimpleGraph
