/-
Copyright (c) 2025 Pjotr Buys. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pjotr Buys
-/

import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.Calculus.Deriv.Polynomial
import Mathlib.Analysis.Calculus.FDeriv.Extend
import Mathlib.Analysis.Calculus.LHopital
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.NormedSpace.Connected
import Mathlib.Analysis.SpecialFunctions.Log.NegMulLog
import Mathlib.Combinatorics.SimpleGraph.Clique
import Mathlib.Combinatorics.SimpleGraph.DegreeSum
import Mathlib.Data.Int.Star
import Mathlib.Data.Rat.Star
import Mathlib.Data.Real.StarOrdered
import Mathlib.GroupTheory.MonoidLocalization.Basic
import Mathlib.Order.CompletePartialOrder
import Mathlib.Tactic.Rify
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.GDelta.MetrizableSpace

/-!
# The Independence Number of Triangle-Free Graphs

This file proves Shearer's theorem on the independence number of triangle-free graphs:

If `G` is a triangle-free graph on `n` vertices with average degree `d`,
then its independence number `Œ±` satisfies `Œ± ‚â• n ¬∑ F(d)` where `F` is the Shearer function
defined by `F(x) = (x log x - x + 1) / (x - 1)¬≤` for `x ‚â† 1` and `F(1) = 1/2`.

In this file the theorem is called `triangle_free_independence_bound`.

## Implementation notes

The main challenge is proving that `F` is convex. This goes by defining `F'` and `F''` explicitly,
showing that they are the first and second derivative of `F`, and proving (using the explicit form
of `F''`) that `F''` is nonnegative and thus `F` convex. This is done in section `Analysis`.

The rest of the proof follows the proof of Theorem 1 in [shearer1983] and is done in section
`Proof`.

## Main definitions

* `F` : The function under study, with explicit handling of the singularity at `x = 1`.
* `F'`, `F''` : The first and second derivatives of `F`, also with explicit singularity handling.
* `closedNeighborSet G v` : The closed neighborhood of `V`.
* `puncture G v`: The induced subgraph obtained by removing the closed neighborhood of `v`,
    this is a graph with vertex type the subtype `(G.closedNeighborSet v)·∂ú` of `V`.
* `averageDegree G`: The average degree of vertices in graph `G`, defined as type `‚Ñö`.

## References

* [J. B. Shearer, *A note on the independence number of triangle-free graphs*,
  Discrete Mathematics 46 (1983) 83-87][shearer1983]
-/


variable {x y : ‚Ñù} {f f' g g' : ‚Ñù ‚Üí ‚Ñù} {U : Set ‚Ñù}

section General

open Set Filter

/-!
### Derivative extension lemmas
-/

-- Extension lemma for HasDerivAt
lemma HasDerivAt.congr_on_open {f'x : ‚Ñù} (hU_open : IsOpen U) (hx : x ‚àà U)
    (hf : HasDerivAt f f'x x) (heq : U.EqOn f g) : HasDerivAt g f'x x :=
  HasDerivWithinAt.hasDerivAt (HasDerivWithinAt.congr (HasDerivAt.hasDerivWithinAt hf)
    (fun _ a ‚Ü¶ (heq a).symm) (heq hx).symm) (IsOpen.mem_nhds hU_open hx)

-- Main extension theorem for derivatives
theorem HasDerivAt.extend_to_singularity (hU_open : IsOpen U) (hx : x ‚àà U)
    (g_diff : ‚àÄ y ‚â† x, y ‚àà U ‚Üí HasDerivAt g (g' y) y)
    (hg : ContinuousAt g x) (hg' : ContinuousAt g' x) :
    HasDerivAt g (g' x) x := by
  have hInterval : ‚àÉ a b, x ‚àà Ioo a b ‚àß Icc a b ‚äÜ U := by
    have ‚ü®Œ¥, hŒ¥, hy‚ü© :=
    Metric.eventually_nhds_iff_ball.1 (eventually_closedBall_subset (IsOpen.mem_nhds hU_open hx))
    simp_rw [Real.ball_eq_Ioo, Real.closedBall_eq_Icc] at hy
    refine ‚ü®_, _, ‚ü®mem_Ioo.mpr ‚ü®?_, ?_‚ü©, hy (Œ¥/2) (mem_Ioo.mpr ‚ü®?_, ?_‚ü©)‚ü©‚ü© <;> linarith
  have ‚ü®a,b, hxI ,hIU‚ü© := hInterval
  have h_diff_right : ‚àÄ {y}, y ‚àà Ioo x b ‚Üí HasDerivAt g (g' y) y :=
    fun hyI ‚Ü¶ (g_diff _ ((ne_of_lt hyI.1).symm)
      (hIU (mem_Icc_of_Ioo ((Ioo_subset_Ioo_left (le_of_lt hxI.1)) hyI))))
  have h_diff_left : ‚àÄ {y}, y ‚àà Ioo a x ‚Üí HasDerivAt g (g' y) y :=
    fun hyI ‚Ü¶ (g_diff _ (ne_of_lt hyI.2)
      (hIU (mem_Icc_of_Ioo ((Ioo_subset_Ioo_right (le_of_lt hxI.2)) hyI))))
  have h_right : HasDerivWithinAt g (g' x) (Icc x b) x := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt]
    convert hasFDerivWithinAt_closure_of_tendsto_fderiv ?_ (convex_Ioo x b) isOpen_Ioo ?_ ?_ using 1
    ¬∑ exact (closure_Ioo (ne_of_lt hxI.2)).symm
    ¬∑ exact fun y hyI ‚Ü¶ DifferentiableAt.differentiableWithinAt
        (HasFDerivAt.differentiableAt (h_diff_right hyI))
    ¬∑ rw [closure_Ioo (ne_of_lt hxI.2)]
      intro y hy
      apply ContinuousAt.continuousWithinAt
      by_cases hxy : y = x
      ¬∑ rwa [hxy]
      ¬∑ exact HasFDerivAt.continuousAt
          (g_diff y hxy (hIU (Icc_subset_Icc_left (le_of_lt hxI.1) hy)))
    ¬∑ simp_rw [deriv_fderiv.symm]
      exact Tendsto.comp isBoundedBilinearMap_smulRight.continuous_right.continuousAt
        (Tendsto.congr' (eventuallyEq_nhdsWithin_of_eqOn
          (fun _ hyI ‚Ü¶ (HasDerivAt.deriv (h_diff_right hyI)).symm))
        (Tendsto.mono_left (y := nhdsWithin x (Ioo x b)) hg' nhdsWithin_le_nhds))
  have h_left : HasDerivWithinAt g (g' x) (Icc a x) x := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt]
    convert hasFDerivWithinAt_closure_of_tendsto_fderiv ?_ (convex_Ioo a x) isOpen_Ioo ?_ ?_ using 1
    ¬∑ exact (closure_Ioo (ne_of_lt hxI.1)).symm
    ¬∑ exact fun y hyI ‚Ü¶ DifferentiableAt.differentiableWithinAt
        (HasFDerivAt.differentiableAt (h_diff_left hyI))
    ¬∑ rw [closure_Ioo (ne_of_lt hxI.1)]
      intro y hy
      apply ContinuousAt.continuousWithinAt
      by_cases hxy : y = x
      ¬∑ rwa [hxy]
      ¬∑ exact HasFDerivAt.continuousAt (g_diff y hxy
          (hIU (Icc_subset_Icc_right (le_of_lt hxI.2) hy)))
    ¬∑ simp_rw [deriv_fderiv.symm]
      exact Tendsto.comp isBoundedBilinearMap_smulRight.continuous_right.continuousAt
        (Tendsto.congr' (eventuallyEq_nhdsWithin_of_eqOn
          (fun _ hyI ‚Ü¶ (HasDerivAt.deriv (h_diff_left hyI)).symm))
        (Tendsto.mono_left (y := nhdsWithin x (Ioo a x)) hg' nhdsWithin_le_nhds))
  convert HasDerivWithinAt.hasDerivAt (HasDerivWithinAt.mono (h_left.union h_right) ?_)
    (Ioo_mem_nhds hxI.1 hxI.2)
  exact fun y hy ‚Ü¶ (le_or_gt x y).elim (fun hxy => Or.inr ‚ü®hxy, hy.2.le‚ü©)
    (fun hxy => Or.inl ‚ü®hy.1.le, hxy.le‚ü©)


/-!
### Auxiliary analysis lemmas
-/

lemma pos_of_dist_one_lt_one (hx : dist x 1 < 1) : 0 < x := by
  rw [Real.dist_eq, abs_sub_lt_iff] at hx
  linarith

-- Extract property from neighborhood
lemma eventually_nhds_self {p : ‚Ñù ‚Üí Prop} (h : ‚àÄ·∂† (y : ‚Ñù) in nhds x, p y) : p x := by
  have ‚ü®_, hŒµ, hall‚ü© := (Metric.eventually_nhds_iff_ball.1 h)
  exact hall _ (Metric.mem_ball_self hŒµ)

-- Propagate zeros to derivatives
lemma frequently_zero_of_deriv_zero
    (hx : g x = 0)
    (hDer : ‚àÄ·∂† (y : ‚Ñù) in nhds x, HasDerivAt g (g' y) y)
    (hgZero : ‚àÉ·∂† (y : ‚Ñù) in nhdsWithin x {x}·∂ú, g y = 0) :
    ‚àÉ·∂† (y : ‚Ñù) in nhdsWithin x {x}·∂ú, g' y = 0 := by
  rw [frequently_nhdsWithin_iff, frequently_nhds_iff]
  intro U hxU hUopen
  rw [eventually_nhds_iff] at hDer
  have ‚ü®V, hVder, hVopen, hxV‚ü© := hDer
  have ‚ü®Œµ, hŒµ, hBall‚ü© := Metric.isOpen_iff.1 (hUopen.inter hVopen) _ ‚ü®hxU, hxV‚ü©
  rw [frequently_nhdsWithin_iff, frequently_nhds_iff] at hgZero
  have ‚ü®y, hUy, hy, hynx‚ü© :=  hgZero (Metric.ball x Œµ) (Metric.mem_ball_self hŒµ) Metric.isOpen_ball
  rw [Set.mem_compl_singleton_iff] at hynx
  let a := min x y; let b := max x y
  have rw_cases : a = x ‚àß b = y ‚à® a = y ‚àß b = x := by
    rcases (lt_or_gt_of_ne hynx) with hyx | hxy
    ¬∑ right; exact ‚ü®min_eq_right_of_lt hyx, max_eq_left_of_lt hyx‚ü©
    ¬∑ left; exact ‚ü®min_eq_left_of_lt hxy, max_eq_right_of_lt hxy‚ü©
  have hab : a < b := min_lt_max.2 hynx.symm
  have hsub : Set.Icc a b ‚äÜ U ‚à© V := by
    refine subset_trans ?_ hBall
    refine IsConnected.Icc_subset (Metric.isConnected_ball hŒµ) ?_ ?_
    ¬∑ rcases rw_cases with h | h <;> rw[h.1]
      ¬∑ exact Metric.mem_ball_self hŒµ
      ¬∑ exact hUy
    ¬∑ rcases rw_cases with h | h <;> rw[h.2]
      ¬∑ exact hUy
      ¬∑ exact Metric.mem_ball_self hŒµ
  have hgab : g a = 0 ‚àß g b = 0 := by
    rcases rw_cases with h | h <;> rw[h.1, h.2]
    ¬∑ exact ‚ü®hx, hy‚ü©
    ¬∑ exact ‚ü®hy, hx‚ü©
  have hxnot : x ‚àâ Set.Ioo a b := by
    intro hcontra; rw [Set.mem_Ioo] at hcontra
    rcases rw_cases with h | h <;> (rw[h.1, h.2] at hcontra; linarith)
  have ‚ü®z,hz,hg'z‚ü© := exists_hasDerivAt_eq_slope g g' hab ?_ ?_
  ¬∑ rw [hgab.1, hgab.2, sub_self, zero_div] at hg'z
    use z, (hsub (Set.Ioo_subset_Icc_self hz)).1, hg'z
    intro hcontra
    rw [Set.mem_singleton_iff.1 hcontra] at hz
    exact hxnot hz
  ¬∑ exact HasDerivAt.continuousOn (fun w hw  ‚Ü¶ hVder _ (hsub hw).2)
  ¬∑ exact (fun w hw  ‚Ü¶ hVder _ (hsub (Set.Ioo_subset_Icc_self hw)).2)


-- Continuity determines value from frequent equality
lemma ContinuousAt.eq_of_frequently_eq {a : ‚Ñù} (hZero : ‚àÉ·∂† (y : ‚Ñù) in nhdsWithin x {x}·∂ú, g y = a)
    (hCont : ContinuousAt g x) : g x = a := by
  by_contra hxna
  have ‚ü®Œ¥, hŒ¥pos, hŒ¥‚ü© := Metric.continuousAt_iff.1 hCont _ (abs_sub_pos.mpr hxna)
  rw [frequently_nhdsWithin_iff, frequently_nhds_iff] at hZero
  have ‚ü®y, hUy, hy, hynx‚ü© :=  hZero _ (Metric.mem_ball_self hŒ¥pos) Metric.isOpen_ball
  specialize hŒ¥ hUy
  rw [hy, abs_sub_comm] at hŒ¥
  exact (lt_self_iff_false _).1 hŒ¥

lemma ConvexOn.tangent_line_le (hgc : ConvexOn ‚Ñù U g) (hx : x ‚àà U) (hy : y ‚àà U)
  (hg' : HasDerivAt g (g' y) y) : g y + g' y * (x - y) ‚â§ g x := by
  rcases lt_trichotomy x y with hxy | hxy | hxy
  ¬∑ rw [‚Üêle_sub_iff_add_le', ‚Üêneg_le_neg_iff, neg_sub, mul_comm, neg_mul_eq_neg_mul, neg_sub,
          ‚Üêdiv_le_iff‚ÇÄ' (by linarith), div_eq_inv_mul]
    convert ConvexOn.slope_le_of_hasDerivAt hgc hx hy hxy hg' using 1
  ¬∑ simp only [hxy, sub_self, mul_zero, add_zero, le_refl]
  ¬∑ let œÜ : ‚Ñù ‚Üí ‚Ñù   := fun x ‚Ü¶ -x
    have hgmc   : ConvexOn ‚Ñù (œÜ '' U) (g ‚àò œÜ) := by
      refine ‚ü®Convex.is_linear_image hgc.1 (IsLinearMap.isLinearMap_neg), ?_‚ü©
      intro mv ‚ü®v, hvS, hv‚ü© mw ‚ü®w, hwS, hw‚ü© a b hap hbp hab
      rw [‚Üêhv, ‚Üêhw]
      convert hgc.2 hvS hwS hap hbp hab using 1 <;> unfold œÜ
      ¬∑ simp only [Function.comp_apply, smul_eq_mul, mul_neg, neg_add, neg_neg]
      ¬∑ simp only [Function.comp_apply, neg_neg, smul_eq_mul]
    have hgm'c  : HasDerivAt (g ‚àò œÜ) (œÜ (g' y)) (œÜ y) := by
      convert HasDerivAt.scomp (œÜ y) (h := œÜ) (h' := -1) (g‚ÇÅ := g) (g‚ÇÅ' := g' y) ?_ ?_ using 1
      ¬∑ simp only [smul_eq_mul, neg_mul, one_mul, œÜ]
      ¬∑ convert hg'
        simp [œÜ]
      ¬∑ exact hasDerivAt_neg' _
    rw [‚Üêle_sub_iff_add_le]
    apply (tsub_le_iff_left (c := - (g' y * (x - y)))).1
    rw [neg_mul_eq_neg_mul, ‚Üêmul_inv_le_iff‚ÇÄ (by linarith), mul_comm]
    convert ConvexOn.slope_le_of_hasDerivAt (x := -x) hgmc ?_ ?_ ?_ hgm'c using 1
    ¬∑ rw [slope, sub_neg_eq_add, add_comm]
      congr 1
      simp [œÜ]
    ¬∑ exact (Set.mem_image _ _ _).2 ‚ü®x, hx, rfl‚ü©
    ¬∑ exact (Set.mem_image _ _ _).2 ‚ü®y, hy, rfl‚ü©
    ¬∑ exact neg_lt_neg_iff.mpr hxy

/-!
### L'H√¥pital's rule
-/

-- General iterated L'H√¥pital's rule
lemma ContinuousAt.lhopital_iterated (n : ‚Ñï) (ns ds : List (‚Ñù ‚Üí ‚Ñù))
    (hnslen : ns.length = n + 1) (hdslen : ds.length = n + 1)
    (hnDer : ‚àÄ k (hk : k < n), ‚àÄ·∂† (y : ‚Ñù) in nhds x, HasDerivAt ns[k] (ns[k + 1] y) y)
    (hdDer : ‚àÄ k (hk : k < n), ‚àÄ·∂† (y : ‚Ñù) in nhds x, HasDerivAt ds[k] (ds[k + 1] y) y)
    (hnZero : ‚àÄ k (hk : k < n), ns[k] x = 0)
    (hdZero : ‚àÄ k (hk : k < n), ds[k] x = 0)
    (hnCont : ContinuousAt ns[n] x)
    (hdCont : ContinuousAt ds[n] x)
    (hdFinal : ds[n] x ‚â† 0) :
    ContinuousAt
        (fun y ‚Ü¶ if y = x
          then (ns[n] x) / (ds[n] x)
          else (ns[0] y) / (ds[0] y))
        x := by
  revert ns ds n; intro n
  induction' n with n ih
  ¬∑ intro ns ds _ _ _ _ _ _  hnC hdC hnZ
    refine ContinuousAt.congr (ContinuousAt.div‚ÇÄ hnC hdC hnZ) (Eq.eventuallyEq ?_)
    ext; split <;> simp_all only [ne_eq]
  ¬∑ intro ns ds len_ns len_ds hnDer hdDer hnZero hdZero hnC hdC hnZ
    rw [‚ÜêcontinuousWithinAt_compl_self, ContinuousWithinAt, if_pos rfl]
    apply Filter.Tendsto.congr' (f‚ÇÅ := fun y ‚Ü¶ (ns[0] y) / (ds[0] y) )
      (eventually_nhdsWithin_of_forall fun y hy ‚Ü¶ (if_neg hy).symm)
    have hn : 0 < n + 1 := Nat.zero_lt_succ _
    have hn_tail : ns.tail.length = n + 1 := by
      rw [List.length_tail, len_ns, add_tsub_cancel_right, Nat.add_left_cancel_iff]
    have hd_tail : ds.tail.length = n + 1 := by
      rw [List.length_tail, len_ds, add_tsub_cancel_right, Nat.add_left_cancel_iff]
    refine HasDerivAt.lhopital_zero_nhdsNE (f' := ns[1]) (g' := ds[1]) ?_ ?_ ?_ ?_ ?_ ?_
    ¬∑ exact eventually_nhdsWithin_of_eventually_nhds (hnDer 0 hn)
    ¬∑ exact eventually_nhdsWithin_of_eventually_nhds (hdDer 0 hn)
    ¬∑ by_contra hcontra
      simp_rw [Filter.not_eventually, not_ne_iff] at hcontra
      have hInd : ‚àÄ k (hk : k < n + 1), ‚àÉ·∂† (x : ‚Ñù) in nhdsWithin x {x}·∂ú, ds[k+1] x = 0 := by
        intro k
        induction' k with _ ih
        ¬∑ exact fun _ ‚Ü¶ hcontra
        ¬∑ exact fun _ ‚Ü¶ frequently_zero_of_deriv_zero (hdZero _ (by linarith))
              (hdDer _ (by linarith)) (ih (by linarith))
      specialize hInd n (by linarith)
      exact hnZ (ContinuousAt.eq_of_frequently_eq hInd hdC)
    ¬∑ rw [‚Üê(hnZero 0 hn)]
      refine continuousWithinAt_compl_self.2 ?_
      have this := (eventually_nhds_self (hnDer 0 hn))
      exact HasDerivAt.continuousAt this
    ¬∑ rw [‚Üê(hdZero 0 hn)]
      refine continuousWithinAt_compl_self.2 ?_
      have this := (eventually_nhds_self (hdDer 0 hn))
      exact HasDerivAt.continuousAt this
    ¬∑ specialize ih ns.tail ds.tail hn_tail hd_tail ?_ ?_ ?_ ?_ ?_ ?_ ?_
      ¬∑ intro k hk
        convert hnDer (k+1) (by linarith) using 3
        ¬∑ refine List.getElem_tail (by linarith)
        ¬∑ simp
      ¬∑ intro k hk
        convert hdDer (k+1) (by linarith) using 3
        ¬∑ refine List.getElem_tail (by linarith)
        ¬∑ simp
      ¬∑ exact fun k hk ‚Ü¶ by convert hnZero (k + 1) (by linarith) using 1; simp
      ¬∑ exact fun k hk ‚Ü¶ by convert hdZero (k + 1) (by linarith) using 1; simp
      ¬∑ convert hnC using 1; simp
      ¬∑ convert hdC using 1; simp
      ¬∑ convert hnZ using 1; simp
      ¬∑ rw [‚ÜêcontinuousWithinAt_compl_self, ContinuousWithinAt, if_pos rfl] at ih
        convert Filter.Tendsto.congr' ?_ ih using 2
        ¬∑ simp
        ¬∑ refine eventually_nhdsWithin_of_forall ?_
          intro _ hy
          convert if_neg hy using 1
          simp

end General


/-!
### Function analysis and calculations
-/

noncomputable section Calculations

open Polynomial Set Filter

/-!
### Main definitions
-/

-- Domain
def S : Set ‚Ñù := {x | 0 < x ‚àß x ‚â† 1}

-- Main function `F` and its derivatives.
def F : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶
  if x = 1 then 1/2
  else          (x * Real.log x - x + 1) / (x - 1)^2

def F' : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶
  if x = 1 then -1/6
  else          (-2 + 2*x - Real.log x - x * Real.log x) / (x - 1)^3

def F'' : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶
  if x = 1 then 1/6
  else          (1 + 4*x - 5*x^2 + 4 * x * Real.log x + 2 * x^2 * Real.log x) / ((x - 1)^4 * x)

/-!
### Numerators and denominators of F, F', and F''.
-/
-- `F`
def nF‚ÇÄ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ x * Real.log x - x + 1
def nF‚ÇÅ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ Real.log x
def nF‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 1/x

def dF‚ÇÄ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ (x - 1)^2
def dF‚ÇÅ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 2 * (x - 1)
def dF‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun _ ‚Ü¶ 2

-- `F'`
def nF'‚ÇÄ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -2 + 2*x - Real.log x - x * Real.log x
def nF'‚ÇÅ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 1 - 1/x - Real.log x
def nF'‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 1 / x^2 - 1 / x
def nF'‚ÇÉ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -2 / x^3 + 1 / x^2

def dF'‚ÇÄ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ (x - 1)^3
def dF'‚ÇÅ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 3 * (x - 1)^2
def dF'‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 6 * (x - 1)
def dF'‚ÇÉ : ‚Ñù ‚Üí ‚Ñù := fun _ ‚Ü¶ 6

-- `F''`
def nF''‚ÇÄ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 1 + 4*x - 5*x^2 + 4 * x * Real.log x + 2 * x^2 * Real.log x
def nF''‚ÇÅ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 8 - 8 * x + 4 * Real.log x  + 4 * x * Real.log x
def nF''‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -4 + 4/x + 4 * Real.log x
def nF''‚ÇÉ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -4 / x^2 + 4 / x
def nF''‚ÇÑ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 8 / x^3 - 4 / x^2

def dF''‚ÇÄ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ (x - 1)^4 * x
def dF''‚ÇÅ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 1 - 8 * x + 18 * x^2 - 16 * x^3 + 5 * x^4
def dF''‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -8 + 36 * x - 48 * x^2 + 20 * x^3
def dF''‚ÇÉ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ 36 - 96 * x + 60 * x^2
def dF''‚ÇÑ : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -96 + 120 * x

/-!
### Lemmas about S
-/

lemma S_open : IsOpen S := IsOpen.and (isOpen_lt' 0) (isOpen_ne)

lemma pos_not_in_S_eq_one (hx_pos : 0 < x) (hx_not_in_S : x ‚àâ S) : x = 1 := by
  by_contra hxn1
  exact hx_not_in_S ‚ü®hx_pos, hxn1‚ü©

/-!
### F, F', and F'' equal to rational functions on S
-/


lemma F_not_one (hx : x ‚â† 1) : F x   = nF‚ÇÄ x / dF‚ÇÄ x       := if_neg hx
lemma F'_not_one (hx : x ‚â† 1) : F' x  = nF'‚ÇÄ x / dF'‚ÇÄ x     := if_neg hx
lemma F''_not_one (hx : x ‚â† 1) : F'' x = nF''‚ÇÄ x / dF''‚ÇÄ x   := if_neg hx

lemma F_at_zero : F 0    = 1  := by norm_num [F]
lemma F_at_one : F 1    = 1/2  := if_pos rfl
lemma F'_at_one : F' 1   = -1/6 := if_pos rfl
lemma F''_at_one : F'' 1  = 1/6  := if_pos rfl

lemma F_on_S : S.EqOn F    (fun x ‚Ü¶ nF‚ÇÄ x    / dF‚ÇÄ x)    := fun _ hx ‚Ü¶ if_neg hx.2
lemma F'_on_S : S.EqOn F'   (fun x ‚Ü¶ nF'‚ÇÄ x   / dF'‚ÇÄ x)   := fun _ hx ‚Ü¶ if_neg hx.2
lemma F''_on_S : S.EqOn F''  (fun x ‚Ü¶ nF''‚ÇÄ x  / dF''‚ÇÄ x)  := fun _ hx ‚Ü¶ if_neg hx.2

/-!
### Derivatives of the numerators and denominators of F, F' and F''
-/

/-!
  Two helpful rewrite lemmas about the derivatives of `x ‚Ü¶ 1/x` and `x ‚Ü¶ 1/x^2`.
-/

lemma hasDerivAtInv (hx : 0 < x) : HasDerivAt (fun y ‚Ü¶ 1 / y) (- 1 / x^2) x := by
  convert hasDerivAt_inv (ne_of_lt hx).symm using 1
  ¬∑ ext _; ring
  ¬∑ ring

lemma hasDerivAtInvSq (hx : 0 < x) : HasDerivAt (fun y ‚Ü¶ 1 / y^2) (- 2 / x^3) x := by
  convert HasDerivAt.inv (c := fun y ‚Ü¶ y^2) (c' := 2 * x) ?_ ?_ using 1
  ¬∑ ext _; simp
  ¬∑ field_simp; ring
  ¬∑ convert Polynomial.hasDerivAt (X (R := ‚Ñù) ^ 2) _ <;> norm_num
  ¬∑ positivity

-- `F`

lemma hasDerivAt_nF‚ÇÄ (hx : 0 < x) : HasDerivAt nF‚ÇÄ (nF‚ÇÅ x) x := by
  unfold nF‚ÇÄ nF‚ÇÅ
  convert HasDerivAt.add_const 1 (HasDerivAt.sub (f := fun x ‚Ü¶ x * Real.log x)
    (f' := Real.log x + 1) ?_ (hasDerivAt_id' x)) using 1
  ¬∑ ring
  ¬∑ convert HasDerivAt.mul (hasDerivAt_id' x) (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    field_simp

lemma hasDerivAt_nF‚ÇÅ (hx : 0 < x) : HasDerivAt nF‚ÇÅ (nF‚ÇÇ x) x := by
  unfold nF‚ÇÇ
  convert Real.hasDerivAt_log (ne_of_lt hx).symm using 1
  exact one_div x

lemma hasDerivAt_dF‚ÇÄ : HasDerivAt dF‚ÇÄ (dF‚ÇÅ x) x := by
  convert Polynomial.hasDerivAt (1 - 2 * X (R := ‚Ñù) + X^2) _ <;> norm_num
  ¬∑ unfold dF‚ÇÄ; ring
  ¬∑ unfold dF‚ÇÅ; ring

lemma hasDerivAt_dF‚ÇÅ : HasDerivAt dF‚ÇÅ (dF‚ÇÇ x) x := by
  convert Polynomial.hasDerivAt (-2 + 2 * X (R := ‚Ñù)) _ <;> norm_num
  ¬∑ unfold dF‚ÇÅ; ring
  ¬∑ rfl

-- `F'`

lemma hasDerivAt_nF'‚ÇÄ (hx : 0 < x) : HasDerivAt nF'‚ÇÄ (nF'‚ÇÅ x) x := by
  unfold nF'‚ÇÄ nF'‚ÇÅ
  convert HasDerivAt.sub (f := fun x ‚Ü¶ 2*x - 2) (f' := 2)
    (g := fun x ‚Ü¶ (1 + x) * Real.log x) (g' := 1 + 1/x + Real.log x) ?_ ?_ using 1
  ¬∑ ext _; rw [Pi.sub_apply]; ring
  ¬∑ ring
  ¬∑ convert Polynomial.hasDerivAt (- 2 + 2 * X (R := ‚Ñù)) _ <;> norm_num
    ring
  ¬∑ convert HasDerivAt.mul (c := fun x ‚Ü¶ 1 + x) (c' := 1) ?_
      (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    ¬∑ field_simp; ring
    ¬∑ convert Polynomial.hasDerivAt (1 + X (R := ‚Ñù)) _ <;> norm_num

lemma hasDerivAt_nF'‚ÇÅ (hx : 0 < x) : HasDerivAt nF'‚ÇÅ (nF'‚ÇÇ x) x := by
  unfold nF'‚ÇÅ nF'‚ÇÇ
  convert HasDerivAt.sub (f := fun x ‚Ü¶ 1 - 1/x) (f' := 1 / x^2) ?_
    (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
  ¬∑ field_simp
  ¬∑ convert HasDerivAt.sub (g := fun x ‚Ü¶ 1 / x) (g' := - 1 / x^2)
      (hasDerivAt_const x 1) (hasDerivAtInv hx) using 1
    ring

lemma hasDerivAt_nF'‚ÇÇ (hx : 0 < x) : HasDerivAt nF'‚ÇÇ (nF'‚ÇÉ x) x := by
  unfold nF'‚ÇÇ nF'‚ÇÉ
  convert HasDerivAt.sub (hasDerivAtInvSq hx) (hasDerivAtInv hx) using 1
  field_simp

lemma hasDerivAt_dF'‚ÇÄ : HasDerivAt dF'‚ÇÄ (dF'‚ÇÅ x) x := by
  convert Polynomial.hasDerivAt (- 1 + 3 * X (R := ‚Ñù) - 3 * X^2 + X^3) _ <;> norm_num
  ¬∑ unfold dF'‚ÇÄ; ring
  ¬∑ unfold dF'‚ÇÅ; ring

lemma hasDerivAt_dF'‚ÇÅ : HasDerivAt dF'‚ÇÅ (dF'‚ÇÇ x) x := by
  convert Polynomial.hasDerivAt (3 - 6 * X (R := ‚Ñù) + 3 * X^2) _ <;> norm_num
  ¬∑ unfold dF'‚ÇÅ; ring
  ¬∑ unfold dF'‚ÇÇ; ring

lemma hasDerivAt_dF'‚ÇÇ : HasDerivAt dF'‚ÇÇ (dF'‚ÇÉ x) x := by
  convert Polynomial.hasDerivAt (-6 + 6 * X (R := ‚Ñù)) _ <;> norm_num
  ¬∑ unfold dF'‚ÇÇ; ring
  ¬∑ unfold dF'‚ÇÉ; ring

-- `F''`

lemma hasDerivAt_nF''‚ÇÄ (hx : 0 < x) : HasDerivAt nF''‚ÇÄ (nF''‚ÇÅ x) x := by
  unfold nF''‚ÇÄ nF''‚ÇÅ
  convert HasDerivAt.add
    (f := fun x ‚Ü¶ 1 + 4 * x - 5 * x^2) (f' := 4 - 10*x)
    (g := fun x ‚Ü¶ (4 * x + 2 * x^2) * Real.log x)
    (g' := 4 + 2 * x + 4 * Real.log x + 4 * x * Real.log x) ?_ ?_ using 1
  ¬∑ ext _; rw [Pi.add_apply]; ring
  ¬∑ ring
  ¬∑ convert Polynomial.hasDerivAt (1 + 4 * X (R := ‚Ñù) - 5 * X^2) _ <;> norm_num; ring
  ¬∑ convert HasDerivAt.mul (c := fun x ‚Ü¶ 4 * x + 2 * x^2) (c' := 4 + 4 * x) ?_
      (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    ¬∑ field_simp; ring
    ¬∑ convert Polynomial.hasDerivAt (4 * X (R := ‚Ñù) + 2 * X^2) _ <;> norm_num; ring

lemma hasDerivAt_nF''‚ÇÅ (hx : 0 < x) : HasDerivAt nF''‚ÇÅ (nF''‚ÇÇ x) x := by
  unfold nF''‚ÇÅ nF''‚ÇÇ
  convert HasDerivAt.add
    (f := fun x ‚Ü¶ 8 - 8 * x) (f' := - 8)
    (g := fun x ‚Ü¶ (4 + 4 * x) * Real.log x) (g' := 4 + 4 / x + 4 * Real.log x) ?_ ?_ using 1
  ¬∑ ext _; rw [Pi.add_apply]; ring
  ¬∑ ring
  ¬∑ convert Polynomial.hasDerivAt (8 - 8 * X (R := ‚Ñù)) _ <;> norm_num
  ¬∑ convert HasDerivAt.mul (c := fun x ‚Ü¶ 4 + 4 * x) (c' := 4) ?_
      (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    ¬∑ field_simp; ring
    ¬∑ convert Polynomial.hasDerivAt (4 + 4 * X (R := ‚Ñù)) _ <;> norm_num

lemma hasDerivAt_nF''‚ÇÇ (hx : 0 < x) : HasDerivAt nF''‚ÇÇ (nF''‚ÇÉ x) x := by
  unfold nF''‚ÇÇ nF''‚ÇÉ
  convert HasDerivAt.add
    (f := fun x ‚Ü¶ -4 + 4/x) (f' := -4 / x^2) ?_
    (HasDerivAt.const_mul _ (Real.hasDerivAt_log (ne_of_lt hx).symm)) using 1
  convert HasDerivAt.add (hasDerivAt_const x (-4))
    (HasDerivAt.const_mul 4 (hasDerivAtInv hx)) using 1
  ¬∑ ext _; rw [Pi.add_apply]; ring
  ¬∑ ring

lemma hasDerivAt_nF''‚ÇÉ (hx : 0 < x) : HasDerivAt nF''‚ÇÉ (nF''‚ÇÑ x) x := by
  unfold nF''‚ÇÉ nF''‚ÇÑ
  convert HasDerivAt.add (HasDerivAt.const_mul (-4) (hasDerivAtInvSq hx))
    (HasDerivAt.const_mul 4 (hasDerivAtInv hx)) using 1
  ¬∑ ext _; rw [Pi.add_apply]; ring
  ¬∑ ring

lemma hasDerivAt_dF''‚ÇÄ : HasDerivAt dF''‚ÇÄ (dF''‚ÇÅ x) x := by
  convert Polynomial.hasDerivAt (X (R := ‚Ñù) - 4 * X^2 + 6 * X^3 - 4 * X^4 + X^5) _ <;> norm_num
  ¬∑ unfold dF''‚ÇÄ; ring
  ¬∑ unfold dF''‚ÇÅ; ring

lemma hasDerivAt_dF''‚ÇÅ : HasDerivAt dF''‚ÇÅ (dF''‚ÇÇ x) x := by
  convert Polynomial.hasDerivAt (1 - 8 * X (R := ‚Ñù) + 18 * X^2 - 16 * X^3 + 5 * X^4) _ <;> norm_num
  ¬∑ unfold dF''‚ÇÅ; ring
  ¬∑ unfold dF''‚ÇÇ; ring

lemma hasDerivAt_dF''‚ÇÇ : HasDerivAt dF''‚ÇÇ (dF''‚ÇÉ x) x := by
  convert Polynomial.hasDerivAt (-8 + 36 * X (R := ‚Ñù) - 48 * X^2 + 20 * X^3 ) _ <;> norm_num
  ¬∑ unfold dF''‚ÇÇ; ring
  ¬∑ unfold dF''‚ÇÉ; ring

lemma hasDerivAt_dF''‚ÇÉ : HasDerivAt dF''‚ÇÉ (dF''‚ÇÑ x) x := by
  convert Polynomial.hasDerivAt (36 - 96 * X (R := ‚Ñù) + 60 * X^2) _ <;> norm_num
  ¬∑ unfold dF''‚ÇÉ; ring
  ¬∑ unfold dF''‚ÇÑ; ring




/-!
### Continuity of (derivatives of) denominators and numerators
-/

lemma ContinuousAt_dF‚ÇÄ : ContinuousAt dF‚ÇÄ x := by
  unfold dF‚ÇÄ
  exact ContinuousAt.pow (ContinuousAt.sub (by exact fun ‚¶ÉU‚¶Ñ a ‚Ü¶ a) continuousAt_const) _

lemma ContinuousAt_nF‚ÇÄ : ContinuousAt nF‚ÇÄ x  :=
  ContinuousAt.add (ContinuousAt.sub (Continuous.continuousAt Real.continuous_mul_log ) fun _ a ‚Ü¶ a)
    continuousAt_const

lemma ContinuousAt_nF‚ÇÇ (hx : 0 < x) : ContinuousAt nF‚ÇÇ x := by unfold nF‚ÇÇ; continuity

lemma ContinuousAt_dF‚ÇÇ : ContinuousAt dF‚ÇÇ x := continuousAt_const

lemma ContinuousAt_nF'‚ÇÉ (hx : 0 < x) : ContinuousAt nF'‚ÇÉ x := by
  refine ContinuousAt.add ?_ ?_ <;>
  exact ContinuousAt.div‚ÇÄ continuousAt_const
    (ContinuousAt.pow (by exact fun _ a ‚Ü¶ a) _) (by positivity)

lemma ContinuousAt_dF'‚ÇÉ : ContinuousAt dF'‚ÇÉ x := continuousAt_const

lemma ContinuousAt_nF''‚ÇÑ (hx : 0 < x) : ContinuousAt nF''‚ÇÑ x := by
  refine ContinuousAt.add ?_ (ContinuousAt.neg ?_) <;>
  exact ContinuousAt.div‚ÇÄ continuousAt_const
    (ContinuousAt.pow (by exact fun _ a ‚Ü¶ a) _) (by positivity)

lemma ContinuousAt_dF''‚ÇÑ : ContinuousAt dF''‚ÇÑ x := by
  unfold dF''‚ÇÑ
  exact Continuous.continuousAt (by continuity)


/-!
### Useful (in)equalities involving the denominators and numerators
-/



lemma dF‚ÇÄ_ne_zero (hx : x ‚â† 1) : dF‚ÇÄ x ‚â† 0      := pow_ne_zero 2 (sub_ne_zero_of_ne hx)
lemma dF‚ÇÄ_ne_zero_on_S (hx : x ‚àà S) : dF‚ÇÄ x ‚â† 0 := dF‚ÇÄ_ne_zero hx.2

lemma dF'‚ÇÄ_ne_zero (hx : x ‚â† 1) : dF'‚ÇÄ x ‚â† 0      := pow_ne_zero 3 (sub_ne_zero_of_ne hx)
lemma dF'‚ÇÄ_ne_zero_on_S (hx : x ‚àà S) : dF'‚ÇÄ x ‚â† 0 := dF'‚ÇÄ_ne_zero hx.2
lemma dF'‚ÇÄ_nonneg (hx : 1 < x) : 0 ‚â§ dF'‚ÇÄ x       := pow_nonneg (by linarith) 3

lemma dF''‚ÇÄ_nonneg (hx : 0 < x) : 0 ‚â§ dF''‚ÇÄ x  := by unfold dF''‚ÇÄ; positivity

lemma dF''‚ÇÄ_ne_zero_on_S (hx : x ‚àà S) : dF''‚ÇÄ x ‚â† 0   :=
  (mul_ne_zero_iff_right (ne_of_lt hx.1).symm).mpr (pow_ne_zero 4 (sub_ne_zero_of_ne hx.2))


/-!
  To prove that `nF''‚ÇÄ` is nonnegative we define two types of functions:
  `isType1`: Nonincreasing on (0,1], nondecreasing on [1,‚àû) and zero at x = 1.
  `isType2`: Nonpositive on (0,1] and nonnegative on [1,‚àû).
  We prove that a function `l` with `l 1 = 0` whose derivative is one type is itself the other type.
-/

def isType1 (l : ‚Ñù ‚Üí ‚Ñù) :=  AntitoneOn l (Ioc 0 1) ‚àß MonotoneOn l (Ici 1) ‚àß (l 1 = 0)
def isType2 (l : ‚Ñù ‚Üí ‚Ñù) :=  (‚àÄ {x}, x ‚àà Ioc 0 1 ‚Üí l x ‚â§ 0) ‚àß (‚àÄ {x}, x ‚àà Ici 1 ‚Üí 0 ‚â§ l x)

lemma Type1_nonneg {l : ‚Ñù ‚Üí ‚Ñù} (hType : isType1 l) (hx : 0 < x) : 0 ‚â§ l x := by
  rw [‚ÜêhType.2.2]
  rcases le_or_gt x 1 with hx1 | hx1
  ¬∑ exact hType.1 ‚ü®hx,hx1‚ü© ‚ü®Real.zero_lt_one, le_refl 1‚ü© hx1
  ¬∑ exact hType.2.1 left_mem_Ici (mem_Ici_of_Ioi hx1) (le_of_lt hx1)

lemma Type2_mul_nonneg {l m : ‚Ñù ‚Üí ‚Ñù} (hType : isType2 l) (hm : ‚àÄ {x}, 0 < x ‚Üí 0 ‚â§ m x) :
  isType2 (fun x ‚Ü¶ l x * m x) :=
  ‚ü® fun hx ‚Ü¶ mul_nonpos_of_nonpos_of_nonneg (hType.1 hx) (hm hx.1),
    fun hx ‚Ü¶ Left.mul_nonneg (hType.2 hx) (hm (by linarith [mem_Ici.1 hx]))‚ü©

lemma DerivAt_Type1_imp_Type2 {l l' : ‚Ñù ‚Üí ‚Ñù} (hType : isType1 l')
    (hDer : ‚àÄ {x}, 0 < x ‚Üí HasDerivAt l (l' x) x)
    (h : l 1 = 0) : isType2 l := by
  have h_mon : MonotoneOn l (Ioi 0) := by
    apply monotoneOn_of_hasDerivWithinAt_nonneg (f' := l')
    ¬∑ exact fun _ hx ‚Ü¶ ContinuousAt.continuousWithinAt (HasDerivAt.continuousAt (hDer hx))
    ¬∑ rw [interior_Ioi]
      exact fun y hy ‚Ü¶ HasDerivAt.hasDerivWithinAt (hDer hy)
    ¬∑ rw [interior_Ioi]
      exact fun x hx ‚Ü¶ Type1_nonneg hType hx
    ¬∑ exact convex_Ioi 0
  refine ‚ü®?_,?_‚ü© <;> (intro x hx; rw [‚Üêh])
  ¬∑ exact h_mon (mem_of_mem_inter_left hx) (by norm_num) (by exact hx.2)
  ¬∑ refine h_mon (by norm_num) ?_ (by exact hx)
    rw [mem_Ioi]; rw [mem_Ici] at hx; linarith

lemma DerivAt_Type2_imp_Type1 {l l' : ‚Ñù ‚Üí ‚Ñù} (hType : isType2 l')
    (hDer : ‚àÄ {x}, 0 < x ‚Üí HasDerivAt l (l' x) x)
    (h : l 1 = 0) : isType1 l := by
  refine ‚ü®?_, ‚ü®?_, h‚ü©‚ü©
  ¬∑ apply antitoneOn_of_hasDerivWithinAt_nonpos (f' := l')
    ¬∑ exact fun _ hx ‚Ü¶ ContinuousAt.continuousWithinAt (HasDerivAt.continuousAt (hDer hx.1))
    ¬∑ rw [interior_Ioc]
      exact fun y hy ‚Ü¶ HasDerivAt.hasDerivWithinAt (hDer hy.1)
    ¬∑ rw [interior_Ioc]
      exact fun x hx ‚Ü¶ (hType.1 (mem_Ioc_of_Ioo hx))
    ¬∑ exact convex_Ioc 0 1
  ¬∑ apply monotoneOn_of_hasDerivWithinAt_nonneg (f' := l')
    ¬∑ exact fun _ hx ‚Ü¶ ContinuousAt.continuousWithinAt
        (HasDerivAt.continuousAt (hDer (by linarith [mem_Ici.1 hx])))
    ¬∑ rw [interior_Ici]
      exact fun y hy ‚Ü¶ HasDerivAt.hasDerivWithinAt (hDer (by linarith [mem_Ioi.1 hy]))
    ¬∑ rw [interior_Ici]
      exact fun x hx ‚Ü¶ (hType.2 (mem_Ici_of_Ioi hx))
    ¬∑ exact convex_Ici 1


/-!
  We prove that the first few derivatives of `nF''‚ÇÄ` alternate between these two types.
-/

lemma isType2_nF''‚ÇÉ : isType2 nF''‚ÇÉ := by
  convert Type2_mul_nonneg (l := fun x ‚Ü¶ -4 + 4*x) (m := fun x ‚Ü¶ 1 / x^2) ?_ ?_ using 1
  ¬∑ unfold nF''‚ÇÉ; ext x;
    by_cases hx : x = 0
    ¬∑ rw [hx]; simp
    ¬∑ field_simp; ring
  ¬∑ refine ‚ü®?_,?_‚ü© <;> (intro _ _; simp_all)
  ¬∑ intro _ _; simp [sq_nonneg]

lemma isType1_nF''‚ÇÇ : isType1 nF''‚ÇÇ :=
  DerivAt_Type2_imp_Type1 isType2_nF''‚ÇÉ hasDerivAt_nF''‚ÇÇ (by norm_num [nF''‚ÇÇ])

lemma isType2_nF''‚ÇÅ : isType2 nF''‚ÇÅ :=
  DerivAt_Type1_imp_Type2 isType1_nF''‚ÇÇ hasDerivAt_nF''‚ÇÅ (by norm_num [nF''‚ÇÅ])

lemma isType1_nF''‚ÇÄ : isType1 nF''‚ÇÄ :=
  DerivAt_Type2_imp_Type1 isType2_nF''‚ÇÅ hasDerivAt_nF''‚ÇÄ (by norm_num [nF''‚ÇÄ])

lemma nF''‚ÇÄ_nonneg (hx : 0 < x) : 0 ‚â§ nF''‚ÇÄ x  := Type1_nonneg (isType1_nF''‚ÇÄ) hx

/-!
  As a convenient corollary we find that nF'‚ÇÄ is nonpositive.
-/

lemma nF'‚ÇÄ_nonpos (hx : 1 ‚â§ x) : nF'‚ÇÄ x ‚â§ 0  := by
  have h‚ÇÅ : -4 * nF'‚ÇÄ x = nF''‚ÇÅ x := by unfold nF'‚ÇÄ nF''‚ÇÅ; ring
  have h‚ÇÇ : 0 ‚â§ nF''‚ÇÅ x           := isType2_nF''‚ÇÅ.2 hx
  linarith


/-!
### Continuity of F, F' and F'' at x = 1.
-/

lemma F_continuousAt_one : ContinuousAt F 1 := by
  convert ContinuousAt.lhopital_iterated 2
    [nF‚ÇÄ, nF‚ÇÅ, nF‚ÇÇ] [dF‚ÇÄ, dF‚ÇÅ, dF‚ÇÇ] rfl rfl ?_ ?_ ?_ ?_ ?_ ?_ ?_ using 1
  ¬∑ unfold F nF‚ÇÇ dF‚ÇÇ; congr! 2; norm_num
  ¬∑ intro k _
    interval_cases k <;> (rw [Metric.eventually_nhds_iff]; use 1, Real.zero_lt_one; intro _ hy)
    ¬∑ exact hasDerivAt_nF‚ÇÄ (pos_of_dist_one_lt_one hy)
    ¬∑ exact hasDerivAt_nF‚ÇÅ (pos_of_dist_one_lt_one hy)
  ¬∑ intro k _
    interval_cases k <;> refine Filter.Eventually.of_forall ?_
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF‚ÇÄ)
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF‚ÇÅ)
  ¬∑ intro k _
    interval_cases k <;> norm_num [nF‚ÇÄ, nF‚ÇÅ]
  ¬∑ intro k _
    interval_cases k <;> norm_num [dF‚ÇÄ, dF‚ÇÅ]
  ¬∑ exact ContinuousAt_nF‚ÇÇ (by norm_num)
  ¬∑ exact ContinuousAt_dF‚ÇÇ
  ¬∑ simp [dF‚ÇÇ]

lemma F'_continuousAt_one : ContinuousAt F' 1 := by
  convert ContinuousAt.lhopital_iterated 3
    [nF'‚ÇÄ, nF'‚ÇÅ, nF'‚ÇÇ, nF'‚ÇÉ] [dF'‚ÇÄ, dF'‚ÇÅ, dF'‚ÇÇ, dF'‚ÇÉ] rfl rfl ?_ ?_ ?_ ?_ ?_ ?_ ?_ using 1
  ¬∑ unfold F' nF'‚ÇÉ dF'‚ÇÉ; congr! 2; norm_num
  ¬∑ intro k _
    interval_cases k <;> (rw [Metric.eventually_nhds_iff]; use 1, Real.zero_lt_one; intro _ hy)
    ¬∑ exact hasDerivAt_nF'‚ÇÄ (pos_of_dist_one_lt_one hy)
    ¬∑ exact hasDerivAt_nF'‚ÇÅ (pos_of_dist_one_lt_one hy)
    ¬∑ exact hasDerivAt_nF'‚ÇÇ (pos_of_dist_one_lt_one hy)
  ¬∑ intro k _
    interval_cases k <;> refine Filter.Eventually.of_forall ?_
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF'‚ÇÄ)
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF'‚ÇÅ)
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF'‚ÇÇ)
  ¬∑ intro k _
    interval_cases k <;> norm_num [nF'‚ÇÄ, nF'‚ÇÅ, nF'‚ÇÇ]
  ¬∑ intro k _
    interval_cases k <;> norm_num [dF'‚ÇÄ, dF'‚ÇÅ, dF'‚ÇÇ]
  ¬∑ exact ContinuousAt_nF'‚ÇÉ (by norm_num)
  ¬∑ exact ContinuousAt_dF'‚ÇÉ
  ¬∑ simp [dF'‚ÇÉ]

lemma F''_continuousAt_one : ContinuousAt F'' 1 := by
  convert ContinuousAt.lhopital_iterated 4
    [nF''‚ÇÄ, nF''‚ÇÅ, nF''‚ÇÇ, nF''‚ÇÉ, nF''‚ÇÑ] [dF''‚ÇÄ, dF''‚ÇÅ, dF''‚ÇÇ, dF''‚ÇÉ, dF''‚ÇÑ]
      rfl rfl ?_ ?_ ?_ ?_ ?_ ?_ ?_ using 1
  ¬∑ unfold F'' nF''‚ÇÑ dF''‚ÇÑ; congr! 2; norm_num
  ¬∑ intro k _
    interval_cases k <;> (rw [Metric.eventually_nhds_iff]; use 1, Real.zero_lt_one; intro _ hy)
    ¬∑ exact hasDerivAt_nF''‚ÇÄ (pos_of_dist_one_lt_one hy)
    ¬∑ exact hasDerivAt_nF''‚ÇÅ (pos_of_dist_one_lt_one hy)
    ¬∑ exact hasDerivAt_nF''‚ÇÇ (pos_of_dist_one_lt_one hy)
    ¬∑ exact hasDerivAt_nF''‚ÇÉ (pos_of_dist_one_lt_one hy)
  ¬∑ intro k _
    interval_cases k <;> refine Filter.Eventually.of_forall ?_
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF''‚ÇÄ)
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF''‚ÇÅ)
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF''‚ÇÇ)
    ¬∑ exact (fun x ‚Ü¶ hasDerivAt_dF''‚ÇÉ)
  ¬∑ intro k _
    interval_cases k <;> norm_num [nF''‚ÇÄ, nF''‚ÇÅ, nF''‚ÇÇ, nF''‚ÇÉ]
  ¬∑ intro k _
    interval_cases k <;> norm_num [dF''‚ÇÄ, dF''‚ÇÅ, dF''‚ÇÇ, dF''‚ÇÉ]
  ¬∑ exact ContinuousAt_nF''‚ÇÑ (by norm_num)
  ¬∑ exact ContinuousAt_dF''‚ÇÑ
  ¬∑ norm_num [dF''‚ÇÑ]


/-!
### Derivatives of F, F'.
-/

/-!
  First two auxilliary lemmas giving the derivatives on S.
-/

lemma hasDerivAt_F_on_S (hx : x ‚àà S) : HasDerivAt F (F' x) x := by
  refine HasDerivAt.congr_on_open S_open hx ?_ F_on_S.symm
  convert HasDerivAt.div (hasDerivAt_nF‚ÇÄ hx.1) hasDerivAt_dF‚ÇÄ (dF‚ÇÄ_ne_zero_on_S hx) using 1
  rw [F'_on_S hx]
  field_simp [dF‚ÇÄ_ne_zero_on_S hx, dF'‚ÇÄ_ne_zero_on_S hx]
  unfold dF‚ÇÄ nF‚ÇÄ dF'‚ÇÄ nF'‚ÇÄ nF‚ÇÅ dF‚ÇÅ
  ring

lemma hasDerivAt_F'_on_S (hx : x ‚àà S) : HasDerivAt F' (F'' x) x := by
  refine HasDerivAt.congr_on_open S_open hx ?_ F'_on_S.symm
  convert HasDerivAt.div (hasDerivAt_nF'‚ÇÄ hx.1) hasDerivAt_dF'‚ÇÄ (dF'‚ÇÄ_ne_zero_on_S hx) using 1
  rw [F''_on_S hx]
  field_simp [dF'‚ÇÄ_ne_zero_on_S hx, dF''‚ÇÄ_ne_zero_on_S hx]
  unfold dF'‚ÇÄ nF'‚ÇÄ dF''‚ÇÄ nF''‚ÇÄ nF'‚ÇÅ dF'‚ÇÅ
  field_simp [(ne_of_lt hx.1).symm]
  ring

/-!
  Then the derivatives on (0,‚àû).
-/

lemma hasDerivAt_F (hx : 0 < x) : HasDerivAt F (F' x) x := by
  by_cases hxS : x ‚àà S
  ¬∑ exact hasDerivAt_F_on_S hxS
  ¬∑ rw [pos_not_in_S_eq_one hx hxS]
    exact HasDerivAt.extend_to_singularity (isOpen_Ioi (a := 0)) (by norm_num)
      (fun y hyn1 hy ‚Ü¶ hasDerivAt_F_on_S ‚ü®hy,hyn1‚ü©) (F_continuousAt_one) (F'_continuousAt_one)

lemma hasDerivAt_F' (hx : 0 < x) : HasDerivAt F' (F'' x) x := by
  by_cases hxS : x ‚àà S
  ¬∑ exact hasDerivAt_F'_on_S hxS
  ¬∑ rw [pos_not_in_S_eq_one hx hxS]
    exact HasDerivAt.extend_to_singularity (isOpen_Ioi (a := 0)) (by norm_num)
      (fun y hyn1 hy ‚Ü¶ hasDerivAt_F'_on_S ‚ü®hy,hyn1‚ü©) (F'_continuousAt_one) (F''_continuousAt_one)

/-!
### Continuity of F, F'.
-/

lemma F_Continuous : Continuous F  := by
  rw [continuous_iff_continuousAt]
  intro x
  by_cases hx : x = 1
  ¬∑ rw [hx]
    exact HasDerivAt.continuousAt (hasDerivAt_F (by norm_num))
  ¬∑ rw [‚ÜêcontinuousWithinAt_iff_continuousAt (mem_nhds_iff.2 ‚ü®_, ‚ü®fun _ a ‚Ü¶ a, isOpen_ne, hx‚ü©‚ü©)]
    exact ContinuousWithinAt.congr (f := fun x ‚Ü¶ nF‚ÇÄ x / dF‚ÇÄ x) (
      ContinuousWithinAt.div (ContinuousAt.continuousWithinAt ContinuousAt_nF‚ÇÄ)
        (ContinuousAt.continuousWithinAt ContinuousAt_dF‚ÇÄ ) (pow_ne_zero 2 (sub_ne_zero_of_ne hx)))
        (fun _ hy ‚Ü¶ by simp only [F, if_neg hy, nF‚ÇÄ, dF‚ÇÄ]) (by simp only [F, if_neg hx, nF‚ÇÄ, dF‚ÇÄ])

lemma F_ContinuousAt (hx : 0 < x) : ContinuousAt F x   := HasDerivAt.continuousAt (hasDerivAt_F hx)
lemma F'_ContinuousAt (hx : 0 < x) : ContinuousAt F' x := HasDerivAt.continuousAt (hasDerivAt_F' hx)

/-
### Convexity of F and  nonpositivity of F'.
-/

lemma F''_nonnegative (hx : 0 < x) : 0 ‚â§ F'' x := by
  ¬∑ by_cases hxS : x ‚àà S
    ¬∑ rw [F''_on_S hxS]
      exact div_nonneg (nF''‚ÇÄ_nonneg hxS.1) (dF''‚ÇÄ_nonneg hxS.1)
    ¬∑ rw [F'', pos_not_in_S_eq_one hx hxS]
      norm_num

lemma F_convex : ConvexOn ‚Ñù (Set.Ioi 0) F := by
  refine convexOn_of_hasDerivWithinAt2_nonneg (f := F) (f' := F') (f'' := F'') (convex_Ioi 0)
    (fun x hx ‚Ü¶ ContinuousAt.continuousWithinAt (F_ContinuousAt hx))
    ?_ ?_ ?_ <;> (intro x hx; rw [interior_Ioi] at hx;)
  ¬∑ exact HasDerivAt.hasDerivWithinAt (hasDerivAt_F hx)
  ¬∑ exact HasDerivAt.hasDerivWithinAt (hasDerivAt_F' hx)
  ¬∑ exact F''_nonnegative hx

lemma F_convex_inequality {x y : ‚Ñù} (hx : 0 ‚â§ x) (hy : 0 < y)
    : F x ‚â• F y + F' y * (x - y) := by
  rcases (LE.le.lt_or_eq' hx) with hx | hx
  ¬∑ exact ConvexOn.tangent_line_le F_convex hx hy (hasDerivAt_F hy)
  ¬∑ rw [ge_iff_le, hx]
    refine ContinuousWithinAt.closure_le (s := Ioo 0 1)
      (f := fun x ‚Ü¶ F y + F' y * (x - y)) ?_ ?_ ?_ ?_
    ¬∑ rw [closure_Ioo (by norm_num)]
      exact unitInterval.zero_mem
    ¬∑ exact Continuous.continuousWithinAt (by continuity)
    ¬∑ refine ContinuousWithinAt.congr ?_ (fun _ hy ‚Ü¶ F_on_S ‚ü®hy.1, (ne_of_gt hy.2).symm‚ü©) ?_
      ¬∑ refine ContinuousWithinAt.div ?_ ?_ (by simp [dF‚ÇÄ])
        ¬∑ exact ContinuousAt.continuousWithinAt ContinuousAt_nF‚ÇÄ
        ¬∑ exact ContinuousAt.continuousWithinAt ContinuousAt_dF‚ÇÄ
      ¬∑ simp [F,nF‚ÇÄ,dF‚ÇÄ]
    ¬∑ exact fun _ hx ‚Ü¶ ConvexOn.tangent_line_le F_convex hx.1 hy (hasDerivAt_F hy)

lemma F'_nonpositive (hx : 0 < x) : F' x ‚â§ 0 := by
  have h_mon : MonotoneOn F' (Ioi 0) := by
    refine monotoneOn_of_hasDerivWithinAt_nonneg (f' := F'') (convex_Ioi 0) ?_ ?_ ?_
    ¬∑ exact fun y hy ‚Ü¶ ContinuousAt.continuousWithinAt (F'_ContinuousAt hy)
    ¬∑ rw [interior_Ioi]
      exact fun y hy ‚Ü¶ HasDerivAt.hasDerivWithinAt (hasDerivAt_F' hy)
    ¬∑ rw [interior_Ioi]
      exact fun y hy ‚Ü¶ F''_nonnegative hy
  rcases (le_or_gt x 1) with hx1 | hx1
  ¬∑ exact le_trans (h_mon hx (by norm_num) hx1) (by norm_num [F'])
  ¬∑ rw [F'_on_S ‚ü®by linarith,(ne_of_lt hx1).symm‚ü©]
    exact div_nonpos_of_nonpos_of_nonneg (nF'‚ÇÄ_nonpos (le_of_lt hx1)) (dF'‚ÇÄ_nonneg hx1)


/-
### Finally the differential equation that F satisfies.
-/

lemma F_diff_equation : 1 + (x - x^2) * F' x - (x + 1) * F x = 0 := by
  by_cases hx : x = 1
  ¬∑ rw [hx, F_at_one, F'_at_one]; norm_num
  ¬∑ rw [F_not_one hx, F'_not_one hx, ‚Üêmul_right_inj' (dF‚ÇÄ_ne_zero hx),
        ‚Üêmul_right_inj' (dF'‚ÇÄ_ne_zero hx), mul_zero, mul_zero]
    field_simp [(dF‚ÇÄ_ne_zero hx), (dF'‚ÇÄ_ne_zero hx)]
    unfold dF‚ÇÄ dF'‚ÇÄ nF‚ÇÄ nF'‚ÇÄ
    ring

end Calculations

section Proof

open Finset SimpleGraph BigOperators
open Classical

namespace SimpleGraph

variable {V : Type}

/-!
### Definition of puncture
-/

/-- The closed neighborhood of a vertex consists of the vertex and its neighbors. -/
def closedNeighborSet (G : SimpleGraph V) (v : V) := insert v (G.neighborSet v)

/-- The exterior of v is the set of vertices outside v's closed neighborhood. -/
def exterior (G : SimpleGraph V) (v : V) := (G.closedNeighborSet v)·∂ú

/-- The puncture of G at v is the induced subgraph on vertices outside v's closed neighborhood. -/
def puncture (G : SimpleGraph V) (v : V) := G.induce (G.exterior v)

/-!
### For coercions.
-/

def exteriorEmbedding {G : SimpleGraph V} {v : V} : G.exterior v ‚Ü™ V  :=
  Function.Embedding.subtype _
def punctureEmbedding {G : SimpleGraph V} {v : V} : G.puncture v ‚Ü™g G :=
  Embedding.induce _

lemma finset_coe_subtype {G : SimpleGraph V} {v : V} (S : Finset (G.exterior v)) :
    (S.map exteriorEmbedding : Set V)  = Subtype.val '' (S : Set (G.exterior v)) := coe_map _ _

/-!
### Basic lemmas about G.puncture v without assuming finiteness of V
-/

variable {G : SimpleGraph V} {v : V}

lemma mem_closedNeighborSet_self : v ‚àà G.closedNeighborSet v := Set.mem_insert _ _

lemma not_mem_exterior_self : v ‚àâ G.exterior v := fun hvw ‚Ü¶ hvw mem_closedNeighborSet_self

lemma adj_mem_closedNeighborSet {w : V} (hw : G.Adj v w) : w ‚àà G.closedNeighborSet v :=
  Set.mem_insert_of_mem _ hw

lemma not_adj_of_mem_exterior {w : V} (hw : w ‚àà G.exterior v) : ¬¨ G.Adj v w :=
  fun hvw ‚Ü¶ hw (adj_mem_closedNeighborSet hvw)

lemma not_mem_image_edge_of_neighbor (e : Sym2 (G.exterior v)) :
    ‚àÄ u, u ‚àà G.neighborSet v ‚Üí u ‚àâ (exteriorEmbedding.sym2Map e) := by
  intro _ hu hue
  have ‚ü®u, _, hueq‚ü© := Sym2.mem_map.1 hue
  exact not_adj_of_mem_exterior (by rw [‚Üêhueq]; exact Subtype.coe_prop u) hu

lemma mem_exterior_of_mem_edge_disjoint_neighbors {e : Sym2 V} (he : e ‚àà G.edgeSet)
    (hu_not : ‚àÄ u, u ‚àà G.neighborSet v ‚Üí u ‚àâ e) : ‚àÄ u ‚àà e, u ‚àà G.exterior v := by
  intro u hue hu_clnbrv
  rcases Set.mem_insert_iff.1 hu_clnbrv with huv | hu_nbrv
  ¬∑ rw [huv] at hue
    exact hu_not (Sym2.Mem.other hue) ((mem_incidence_iff_neighbor _).1
      ‚ü®by rwa [Sym2.other_spec hue], Sym2.mem_mk_left v _‚ü©) (Sym2.other_mem hue)
  ¬∑ exact (hu_not _ hu_nbrv) hue

lemma exists_preimage_of_edge_disjoint_neighbors {e : Sym2 V}
    (he : e ‚àà G.edgeSet) (hu_not : ‚àÄ u, u ‚àà G.neighborSet v ‚Üí u ‚àâ e)
    : ‚àÉ e' ‚àà (G.puncture v).edgeSet, (exteriorEmbedding.sym2Map e') = e := by
  have he_eq : exteriorEmbedding.sym2Map
    (e.attachWith (mem_exterior_of_mem_edge_disjoint_neighbors he hu_not)) = e
    := Sym2.attachWith_map_subtypeVal _
  rw [‚Üêhe_eq] at he
  exact ‚ü®_, ‚ü®(Embedding.map_mem_edgeSet_iff punctureEmbedding).mp he, he_eq‚ü©‚ü©

variable (G) (v) in
lemma puncture_edgeSet_image_eq : exteriorEmbedding.sym2Map '' (G.puncture v).edgeSet =
    G.edgeSet \ ‚ãÉ u ‚àà (G.neighborSet v), (G.incidenceSet u) := by
  ext e
  rw [Set.mem_diff, Set.mem_iUnion]
  constructor
  ¬∑ intro ‚ü®_, he', he_eq‚ü©
    rw [‚Üêhe_eq]
    exact ‚ü®(Embedding.map_mem_edgeSet_iff punctureEmbedding).mpr he',
      fun ‚ü®_, _, ‚ü®huv, ht‚ü©, hue‚ü© ‚Ü¶
        (by rw [‚Üêht] at hue; exact not_mem_image_edge_of_neighbor _ _ huv hue.2)‚ü©
  ¬∑ exact fun ‚ü®he, he_not‚ü© ‚Ü¶ (Set.mem_image _ _ _).2
      (exists_preimage_of_edge_disjoint_neighbors he
      fun u hu hue ‚Ü¶ he_not ‚ü®u, Set.mem_iUnion.2 ‚ü®hu, ‚ü®he, hue‚ü©‚ü©‚ü©)

lemma triangle_free_neighbors_pairwise_disjoint_incidenceSet (hT : G.CliqueFree 3) :
    (G.neighborSet v).PairwiseDisjoint (fun u ‚Ü¶ G.incidenceSet u) := by
  rw [Set.pairwiseDisjoint_iff]
  intro u hu w hw ‚ü®e, ‚ü®heu, hew‚ü©‚ü©
  by_contra huw
  exact isIndepSet_neighborSet_of_triangleFree _ hT _ hu hw huw
    (adj_of_mem_incidenceSet _ huw heu hew)

/-The independent set version of `isClique_insert`.-/
lemma isIndepSet_insert {T : Set V}
    : G.IsIndepSet (insert v T) ‚Üî G.IsIndepSet T ‚àß ‚àÄ u ‚àà T, v ‚â† u ‚Üí ¬¨G.Adj v u := by
  simp_all [‚ÜêisClique_compl, ‚ÜêisClique_compl , isClique_insert]


/-!
## Now we assume that V has Fintype. Lemmas about closedNeighborFinset.
-/

variable [Fintype V]

noncomputable instance fintype_puncture : Fintype (G.exterior v) := Fintype.ofFinite _

variable (G) (v) in
noncomputable def closedNeighborFinset := insert v (G.neighborFinset v)

lemma closedNeighborFinset_coe : closedNeighborFinset G v = G.closedNeighborSet v := by
  simp [closedNeighborFinset, closedNeighborSet, neighborFinset_def]

lemma closedNeighborSet_toFinset : (G.closedNeighborSet v).toFinset = closedNeighborFinset G v := by
  rw [‚ÜêclosedNeighborFinset_coe, toFinset_coe]

lemma puncture_verts_toFinset : (G.exterior v).toFinset = univ \ G.closedNeighborFinset v := by
  simp only [exterior, Set.toFinset_compl, closedNeighborSet_toFinset, compl_eq_univ_sdiff]

lemma closedNeighborFinset_card : #(G.closedNeighborFinset v) = G.degree v + 1 :=
  card_insert_of_notMem (notMem_neighborFinset_self _ _)

lemma card_le_degree_succ : Fintype.card V ‚â• G.degree v + 1 := by
  rw [‚ÜêclosedNeighborFinset_card]
  exact card_le_univ _

lemma card_exterior_eq : Fintype.card (G.exterior v) = Fintype.card V - (G.degree v + 1) := by
  rw [‚ÜêSet.toFinset_card, puncture_verts_toFinset, card_univ_diff, closedNeighborFinset_card]

lemma card_exterior_eq_rat :
  (Fintype.card (G.exterior v) : ‚Ñö) = Fintype.card V - (G.degree v + 1) := by
  rw [card_exterior_eq (G := G) (v := v), Nat.cast_sub card_le_degree_succ,
  Nat.cast_add, Nat.cast_one]

lemma card_exterior_eq_lt : Fintype.card (G.exterior v) < Fintype.card V := by
  rw [card_exterior_eq, tsub_lt_self_iff, Fintype.card_pos_iff, add_pos_iff]
  exact ‚ü®Nonempty.intro v, Or.inr Nat.one_pos‚ü©

variable (G) (v) in
theorem indepNum_puncture_succ_le : (puncture G v).indepNum + 1 ‚â§ G.indepNum:= by
  have ‚ü®S,hS‚ü© := exists_isNIndepSet_indepNum (G := (puncture G v))
  have hS_coe : (S.map exteriorEmbedding : Set V) ‚äÜ G.exterior v := map_subtype_subset _
  convert IsIndepSet.card_le_indepNum (t := insert v (S.map exteriorEmbedding)) ?_
  ¬∑ rw [‚ÜêhS.2, card_insert_of_notMem (fun h ‚Ü¶ not_mem_exterior_self (hS_coe h)), card_map]
  ¬∑ rw [coe_insert, isIndepSet_insert]
    refine ‚ü®?_, (fun _ hu _  ‚Ü¶ (not_adj_of_mem_exterior (hS_coe hu)))‚ü©
    have this := hS.1
    rwa [puncture, induce_eq_coe_induce_top, isIndepSet_induce, ‚Üêfinset_coe_subtype] at this


/-!
### The average degree of G.
-/

variable (G) in
/-- The average degree of vertices in a finite graph. -/
noncomputable def averageDegree := ùîº v, (G.degree v : ‚Ñö)

lemma averageDegree_nonneg : 0 ‚â§ averageDegree G := expect_nonneg (fun _ _ ‚Ü¶ Nat.cast_nonneg' _)

lemma averageDegree_eq_twice_card_edges_div_card :
  (averageDegree G) = 2 * #G.edgeFinset / (Fintype.card V : ‚Ñö) := by
  convert Fintype.expect_eq_sum_div_card (fun v ‚Ü¶ (G.degree v : ‚Ñö))
  convert congrArg (Nat.cast (R := ‚Ñö)) (sum_degrees_eq_twice_card_edges G).symm using 1 <;> simp

lemma card_mul_averageDegree_eq_twice_card_edges :
    Fintype.card V  * (averageDegree G) = 2 * #G.edgeFinset := by
  rw [averageDegree_eq_twice_card_edges_div_card]
  rcases Nat.eq_zero_or_pos (Fintype.card V) with h | h
  ¬∑ simp [h, filter_eq_empty_iff]
    rw [Fintype.card_eq_zero_iff, isEmpty_iff] at h
    exact fun x _ ‚Ü¶ h (x.out).1
  ¬∑ field_simp

@[simp]
lemma neighborFinset_coe : (G.neighborFinset v : Set V) = G.neighborSet v := by ext _; simp
@[simp]
lemma incidenceFinset_coe : (G.incidenceFinset v : Set (Sym2 V)) = G.incidenceSet v :=
  by ext _; simp

lemma triangle_free_neighbors_pairwise_disjoint_incidenceFinset (hT : G.CliqueFree 3) :
    (G.neighborSet v).PairwiseDisjoint (fun u ‚Ü¶ G.incidenceFinset u) := by
  have this := triangle_free_neighbors_pairwise_disjoint_incidenceSet (v := v) hT
  rw [Set.pairwiseDisjoint_iff, pairwiseDisjoint_iff] at *
  exact fun u hu w hw ‚ü®e, he‚ü© ‚Ü¶
    (this hu hw ‚ü®e, by rwa [mem_inter, mem_incidenceFinset, mem_incidenceFinset] at he‚ü©)

variable (G) (v) in
lemma puncture_edgeFinset_map_eq : map exteriorEmbedding.sym2Map (G.puncture v).edgeFinset =
    G.edgeFinset \ Finset.biUnion (G.neighborFinset v) (fun u ‚Ü¶ (G.incidenceFinset u)) := by
  rw [‚Üêcoe_inj]; convert puncture_edgeSet_image_eq G v <;> simp

lemma neighborhood_incidenceFinset_sub :
    Finset.biUnion (G.neighborFinset v) (fun u ‚Ü¶ (G.incidenceFinset u)) ‚äÜ G.edgeFinset := by
  intro _ he
  have ‚ü®_,_,h‚ü© := mem_biUnion.1 he
  exact (incidenceFinset_subset _ _ h)

lemma puncture_edge_card (hT : G.CliqueFree 3) : (#(G.puncture v).edgeFinset : ‚Ñö)
    = #G.edgeFinset - ‚àë u ‚àà G.neighborFinset v, (G.degree u : ‚Ñö) := by
  convert congrArg (Nat.cast (R := ‚Ñö)) (congrArg card (puncture_edgeFinset_map_eq G v))
  ¬∑ exact (card_map _).symm
  ¬∑ rw [card_sdiff neighborhood_incidenceFinset_sub,
      Nat.cast_sub (card_le_card neighborhood_incidenceFinset_sub), ‚ÜêNat.cast_sum, card_biUnion ?_]
    ¬∑ simp
    ¬∑ rw [neighborFinset_coe]
      exact triangle_free_neighbors_pairwise_disjoint_incidenceFinset hT

lemma expect_sum_degree_neighbors :
    ùîº v, ‚àë u ‚àà G.neighborFinset v, (G.degree u : ‚Ñö) = ùîº v, (G.degree v : ‚Ñö)^2 := by
  conv => lhs; rhs; intro v ; rw [‚Üê(Fintype.sum_ite_mem _ _)]
  simp_rw [expect_sum_comm]
  conv => lhs; rhs; intro u; rhs; intro i; rw [‚Üêmul_boole]
  conv => lhs; rhs; intro u; rw [‚Üêmul_expect, expect, sum_boole, mul_smul_comm]
  rw [expect, smul_sum]
  congr; ext _; rw [pow_two]; congr 4
  ext _; simp only [mem_neighborFinset, adj_comm, mem_filter, mem_univ, true_and]

lemma expect_puncture_edgeFinset_card (hT : G.CliqueFree 3)
    : (ùîº v, #(G.puncture v).edgeFinset : ‚Ñö) = #G.edgeFinset - (ùîº v, (G.degree v : ‚Ñö)^2):= by
  simp_rw [puncture_edge_card hT, expect_sub_distrib, expect_sum_degree_neighbors]
  congr
  by_cases hV : Nonempty V
  ¬∑ exact expect_const (univ_nonempty_iff.mpr hV) _
  ¬∑ rw [not_nonempty_iff] at hV
    simp

lemma expect_puncture_edgeFinset_card_real (hT : G.CliqueFree 3)
    : (ùîº v, #(G.puncture v).edgeFinset : ‚Ñù) = #G.edgeFinset - (ùîº v, (G.degree v : ‚Ñù)^2):= by
  convert congrArg (Rat.cast (K := ‚Ñù)) (expect_puncture_edgeFinset_card hT)
  ¬∑ exact (algebraMap.coe_expect univ (fun v ‚Ü¶ (#(G.puncture v).edgeFinset : ‚Ñö))).symm
  ¬∑ have this := (algebraMap.coe_expect univ (M := ‚Ñö) (N := ‚Ñù) (fun v ‚Ü¶ (G.degree v : ‚Ñö)^2)).symm
    simp_all

lemma edge_card_zero_iff_average_degree_zero : #G.edgeFinset = 0 ‚Üî G.averageDegree = 0 := by
  rw [‚ÜêRat.natCast_inj, ‚Üê(mul_right_inj_of_invertible 2),
    ‚Üêcard_mul_averageDegree_eq_twice_card_edges, (by rfl: (0: ‚Ñï) = (0 : ‚Ñö)),
    mul_zero, mul_eq_zero, or_iff_right_iff_imp]
  intro hV
  apply expect_eq_zero
  intro i hi
  rw [Rat.natCast_eq_zero, Fintype.card_eq_zero_iff] at hV
  exact False.elim (IsEmpty.false i)

lemma degree_eq_zero_of_averageDegree_eq_zero (h : G.averageDegree = 0) :
    ‚àÄ v, G.degree v = 0 := fun v ‚Ü¶ Rat.natCast_eq_zero.mp
  ((expect_eq_zero_iff_of_nonneg ‚ü®v, mem_univ _‚ü© (fun _ _ ‚Ü¶ Nat.cast_nonneg' _)).1 h v (mem_univ _))

lemma averageDegree_puncture_eq_zero_of_averageDegree_eq_zero (h : G.averageDegree = 0) :
    (G.puncture v).averageDegree = 0 := by
  rw [‚Üêedge_card_zero_iff_average_degree_zero] at *
  have map_eq := puncture_edgeFinset_map_eq G v
  rw [card_eq_zero] at h
  rwa [h, empty_sdiff, map_eq_empty, ‚Üêcard_eq_zero] at map_eq

lemma averageDegree_square_bound : G.averageDegree ^ 2 ‚â§ ùîº v, (G.degree v : ‚Ñö)^2 := by
  convert expect_mul_sq_le_sq_mul_sq (f := fun v ‚Ü¶ (G.degree v : ‚Ñö)) (g := fun _ ‚Ü¶ 1) univ
  ¬∑ simp only [averageDegree, mul_one]
  ¬∑ by_cases h_nonempty : Nonempty V
    ¬∑ rw [expect_const (univ_nonempty_iff.2 h_nonempty), one_pow, mul_one]
    ¬∑ simp_all


/-!
### Proof of the main statement
-/

/- `le` version of `exists_lt_of_lt_expect`-/
lemma exists_ge_of_le_expect {a : ‚Ñù} {g : V ‚Üí ‚Ñù} (h_nonempty : Nonempty V) (h : a ‚â§ ùîº i, g i)
  : ‚àÉ x, a ‚â§ g x := by
  have ‚ü®x, _, h_all‚ü© := exists_max_image (s := univ) (f := g) (univ_nonempty_iff.mpr h_nonempty)
  exact ‚ü®x, le_trans h (expect_le (univ_nonempty_iff.mpr h_nonempty) h_all)‚ü©

lemma exists_good_vertex (h_nonempty : Nonempty V) (hT : CliqueFree G 3) :
    ‚àÉ v, (Fintype.card V) * (F G.averageDegree)
    ‚â§ 1 + (Fintype.card V - (G.degree v + 1)) * (F (G.puncture v).averageDegree) := by
  by_cases hd‚ÇÄ : G.averageDegree = 0
  ¬∑ have ‚ü®v‚ü© := h_nonempty
    use v
    rw [averageDegree_puncture_eq_zero_of_averageDegree_eq_zero hd‚ÇÄ, hd‚ÇÄ, Rat.cast_zero, F_at_zero,
    degree_eq_zero_of_averageDegree_eq_zero hd‚ÇÄ]
    simp only [mul_one, CharP.cast_eq_zero, zero_add, add_sub_cancel, le_refl]
  ¬∑ have hd_pos : 0 < (G.averageDegree : ‚Ñù) :=
      Rat.cast_pos_of_pos (lt_of_le_of_ne averageDegree_nonneg (by rwa [ne_eq, eq_comm]))
    have cast_rw : (ùîº i, (G.degree i : ‚Ñù)) = G.averageDegree :=
      (algebraMap.coe_expect _ (M := ‚Ñö) _).symm
    set n := (Fintype.card V : ‚Ñù) with ‚Üêhn; set d := (G.averageDegree : ‚Ñù) with ‚Üêhd
    apply exists_ge_of_le_expect h_nonempty
    refine le_trans ?_
      (expect_le_expect (f := fun v ‚Ü¶ 1 + (n - (G.degree v + 1)) *
      (F d + F' d * ((G.puncture v).averageDegree - d))) ?_)
    ¬∑ rw [‚Üêsub_nonneg]
      suffices h : 0 ‚â§ 1 - F d * (d + 1) + F' d * (d^2 + d - n * d) +
          F' d * ùîº i, ((n - ((G.degree i) + 1)) * (G.puncture i).averageDegree) by
        convert h using 1
        rw [‚Üêsub_eq_zero]
        simp only [mul_add, add_mul, sub_mul,  expect_sub_distrib, expect_add_distrib,
        expect_const (univ_nonempty_iff.2 h_nonempty), ‚Üêexpect_mul, ‚Üêmul_expect,
        mul_comm (b := F' d), ‚Üêmul_assoc (b := F' d), cast_rw, mul_sub, sub_mul]
        ring_nf; simp_rw [mul_assoc, ‚Üêmul_expect]; ring
      have h_rw : ‚àÄ v, (n - (‚Üë(G.degree v) + 1)) * ‚Üë(G.puncture v).averageDegree
        = 2 * #(G.puncture v).edgeFinset := by
        intro v
        rw [(by simp : 2 * (#((G.puncture v).edgeFinset) : ‚Ñù)
          = (2 * #((G.puncture v).edgeFinset) : ‚Ñö)),
          ‚Üêcard_mul_averageDegree_eq_twice_card_edges, card_exterior_eq_rat]
        simp_all
      conv => rhs; rhs; rhs; rhs; intro v; rw [h_rw]
      rw [‚Üêmul_expect, expect_puncture_edgeFinset_card_real hT, mul_sub, mul_sub,
        (by simp : 2 * (#(G.edgeFinset) : ‚Ñù) = (2 * #(G.edgeFinset) : ‚Ñö)),
        ‚Üêcard_mul_averageDegree_eq_twice_card_edges, ‚Üê@F_diff_equation d, ‚Üêsub_nonneg, Rat.cast_mul,
        Rat.cast_natCast, hn, hd]
      ring_nf; rw [sub_nonneg, mul_comm (a := F' d)]; gcongr ?_ * 2
      refine mul_le_mul_of_nonpos_right ?_ (F'_nonpositive hd_pos)
      convert (Mathlib.Tactic.Rify.ratCast_le _ _).mp (averageDegree_square_bound (G := G))
      ¬∑ simp [hd]
      ¬∑ have := (algebraMap.coe_expect univ (M := ‚Ñö) (N := ‚Ñù) (fun v ‚Ü¶ (G.degree v : ‚Ñö)^2)).symm
        simp_all
    ¬∑ intro v _
      rw [add_le_add_iff_left]
      apply mul_le_mul_of_nonneg_left
      ¬∑ exact F_convex_inequality (Rat.cast_nonneg.mpr averageDegree_nonneg) hd_pos
      ¬∑ convert sub_nonneg.2 (Nat.cast_le (Œ± := ‚Ñù).2 (card_le_degree_succ (G := G) (v := v)))
        rw [Nat.cast_add, Nat.cast_one]

/-- **Shearer's Theorem** [shearer1983]: The independence number of a triangle-free graph on
n vertices with average degree d is at least n ¬∑ F(d) where F is the Shearer function. -/
theorem triangle_free_independence_bound (hT : G.CliqueFree 3)
    : G.indepNum ‚â• (Fintype.card V) * (F G.averageDegree) := by
  suffices h : ‚àÄ n, (‚àÄ {V' : Type} [Fintype V'] {G' : SimpleGraph V'} (hn : n = Fintype.card V')
    (hT : G'.CliqueFree 3), G'.indepNum ‚â• n * (F G'.averageDegree)) from (h _ rfl hT)
  intro n
  induction' n using Nat.strong_induction_on with n hn
  intro V _ G hcard hT
  by_cases hV : Nonempty V
  ¬∑ have ‚ü®v, hv‚ü© := exists_good_vertex hV hT
    rw [‚Üêhcard] at hv
    refine ge_trans (ge_trans (Nat.cast_le.mpr (indepNum_puncture_succ_le G v)) ?_) hv
    specialize hn _ (by convert card_exterior_eq_lt (G := G) (v := v)) rfl
      (CliqueFree.comap punctureEmbedding hT)
    rw [card_exterior_eq, ‚Üêhcard, ge_iff_le, ‚Üê(add_le_add_iff_right (a :=1))] at hn
    convert hn using 1
    ¬∑ rw [‚Üêsub_eq_zero]
      simp only [hcard, Nat.cast_sub card_le_degree_succ, Nat.cast_add, Nat.cast_one]
      ring
    ¬∑ simp
  ¬∑ simp_all

end SimpleGraph

end Proof
