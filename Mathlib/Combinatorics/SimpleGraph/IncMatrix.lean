/-
Copyright (c) 2021 Gabriel Moise. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Moise, YaÃ«l Dillies, Kyle Miller
-/
import Mathlib.Combinatorics.SimpleGraph.Basic
import Mathlib.Data.Matrix.Basic

#align_import combinatorics.simple_graph.inc_matrix from "leanprover-community/mathlib"@"bb168510ef455e9280a152e7f31673cabd3d7496"

/-!
# Incidence matrix of a simple graph

This file defines the unoriented incidence matrix of a simple graph.

## Main definitions

* `SimpleGraph.incMatrix`: `G.incMatrix R` is the incidence matrix of `G` over the ring `R`.

## Main results

* `SimpleGraph.incMatrix_mul_transpose_diag`: The diagonal entries of the product of
  `G.incMatrix R` and its transpose are the degrees of the vertices.
* `SimpleGraph.incMatrix_mul_transpose`: Gives a complete description of the product of
  `G.incMatrix R` and its transpose; the diagonal is the degrees of each vertex, and the
  off-diagonals are 1 or 0 depending on whether or not the vertices are adjacent.
* `SimpleGraph.incMatrix_transpose_mul_diag`: The diagonal entries of the product of the
  transpose of `G.incMatrix R` and `G.inc_matrix R` are `2` or `0` depending on whether or
  not the unordered pair is an edge of `G`.

## Implementation notes

The usual definition of an incidence matrix has one row per vertex and one column per edge.
However, this definition has columns indexed by all of `Sym2 Î±`, where `Î±` is the vertex type.
This appears not to change the theory, and for simple graphs it has the nice effect that every
incidence matrix for each `SimpleGraph Î±` has the same type.

## TODO

* Define the oriented incidence matrices for oriented graphs.
* Define the graph Laplacian of a simple graph using the oriented incidence matrix from an
  arbitrary orientation of a simple graph.
-/


open Finset Matrix SimpleGraph Sym2

open BigOperators Matrix

namespace SimpleGraph

variable (R : Type*) {Î± : Type*} (G : SimpleGraph Î±)

/-- `G.incMatrix R` is the `Î± Ã— Sym2 Î±` matrix whose `(a, e)`-entry is `1` if `e` is incident to
`a` and `0` otherwise. -/
noncomputable def incMatrix [Zero R] [One R] : Matrix Î± (Sym2 Î±) R := fun a =>
  (G.incidenceSet a).indicator 1
#align simple_graph.inc_matrix SimpleGraph.incMatrix

variable {R}

theorem incMatrix_apply [Zero R] [One R] {a : Î±} {e : Sym2 Î±} :
    G.incMatrix R a e = (G.incidenceSet a).indicator 1 e :=
  rfl
#align simple_graph.inc_matrix_apply SimpleGraph.incMatrix_apply

/-- Entries of the incidence matrix can be computed given additional decidable instances. -/
theorem incMatrix_apply' [Zero R] [One R] [DecidableEq Î±] [DecidableRel G.Adj] {a : Î±}
    {e : Sym2 Î±} : G.incMatrix R a e = if e âˆˆ G.incidenceSet a then 1 else 0 := by
  unfold incMatrix Set.indicator -- Porting note: was `convert rfl`
  -- âŠ¢ (if e âˆˆ incidenceSet G a then OfNat.ofNat 1 e else 0) = if e âˆˆ incidenceSet  â€¦
  simp only [Pi.one_apply]
  -- ğŸ‰ no goals
#align simple_graph.inc_matrix_apply' SimpleGraph.incMatrix_apply'

section MulZeroOneClass

variable [MulZeroOneClass R] {a b : Î±} {e : Sym2 Î±}

theorem incMatrix_apply_mul_incMatrix_apply : G.incMatrix R a e * G.incMatrix R b e =
    (G.incidenceSet a âˆ© G.incidenceSet b).indicator 1 e := by
  classical simp only [incMatrix, Set.indicator_apply, â† ite_and_mul_zero, Pi.one_apply, mul_one,
    Set.mem_inter_iff]
#align simple_graph.inc_matrix_apply_mul_inc_matrix_apply SimpleGraph.incMatrix_apply_mul_incMatrix_apply

theorem incMatrix_apply_mul_incMatrix_apply_of_not_adj (hab : a â‰  b) (h : Â¬G.Adj a b) :
    G.incMatrix R a e * G.incMatrix R b e = 0 := by
  rw [incMatrix_apply_mul_incMatrix_apply, Set.indicator_of_not_mem]
  -- âŠ¢ Â¬e âˆˆ incidenceSet G a âˆ© incidenceSet G b
  rw [G.incidenceSet_inter_incidenceSet_of_not_adj h hab]
  -- âŠ¢ Â¬e âˆˆ âˆ…
  exact Set.not_mem_empty e
  -- ğŸ‰ no goals
#align simple_graph.inc_matrix_apply_mul_inc_matrix_apply_of_not_adj SimpleGraph.incMatrix_apply_mul_incMatrix_apply_of_not_adj

theorem incMatrix_of_not_mem_incidenceSet (h : e âˆ‰ G.incidenceSet a) : G.incMatrix R a e = 0 := by
  rw [incMatrix_apply, Set.indicator_of_not_mem h]
  -- ğŸ‰ no goals
#align simple_graph.inc_matrix_of_not_mem_incidence_set SimpleGraph.incMatrix_of_not_mem_incidenceSet

theorem incMatrix_of_mem_incidenceSet (h : e âˆˆ G.incidenceSet a) : G.incMatrix R a e = 1 := by
  rw [incMatrix_apply, Set.indicator_of_mem h, Pi.one_apply]
  -- ğŸ‰ no goals
#align simple_graph.inc_matrix_of_mem_incidence_set SimpleGraph.incMatrix_of_mem_incidenceSet

variable [Nontrivial R]

theorem incMatrix_apply_eq_zero_iff : G.incMatrix R a e = 0 â†” e âˆ‰ G.incidenceSet a := by
  simp only [incMatrix_apply, Set.indicator_apply_eq_zero, Pi.one_apply, one_ne_zero]
  -- ğŸ‰ no goals
#align simple_graph.inc_matrix_apply_eq_zero_iff SimpleGraph.incMatrix_apply_eq_zero_iff

theorem incMatrix_apply_eq_one_iff : G.incMatrix R a e = 1 â†” e âˆˆ G.incidenceSet a := by
  -- Porting note: was `convert one_ne_zero.ite_eq_left_iff; infer_instance`
  unfold incMatrix Set.indicator
  -- âŠ¢ (if e âˆˆ incidenceSet G a then OfNat.ofNat 1 e else 0) = 1 â†” e âˆˆ incidenceSet â€¦
  simp only [Pi.one_apply]
  -- âŠ¢ (if e âˆˆ incidenceSet G a then 1 else 0) = 1 â†” e âˆˆ incidenceSet G a
  apply Iff.intro <;> intro h
  -- âŠ¢ (if e âˆˆ incidenceSet G a then 1 else 0) = 1 â†’ e âˆˆ incidenceSet G a
                      -- âŠ¢ e âˆˆ incidenceSet G a
                      -- âŠ¢ (if e âˆˆ incidenceSet G a then 1 else 0) = 1
  Â· split at h <;> simp_all only [zero_ne_one]
    -- âŠ¢ e âˆˆ incidenceSet G a
                   -- ğŸ‰ no goals
                   -- ğŸ‰ no goals
  Â· simp_all only [ite_true]
    -- ğŸ‰ no goals
#align simple_graph.inc_matrix_apply_eq_one_iff SimpleGraph.incMatrix_apply_eq_one_iff

end MulZeroOneClass

section NonAssocSemiring

variable [Fintype Î±] [NonAssocSemiring R] {a b : Î±} {e : Sym2 Î±}

theorem sum_incMatrix_apply [DecidableEq Î±] [DecidableRel G.Adj] :
    âˆ‘ e, G.incMatrix R a e = G.degree a := by
  simp [incMatrix_apply', sum_boole, Set.filter_mem_univ_eq_toFinset]
  -- ğŸ‰ no goals
#align simple_graph.sum_inc_matrix_apply SimpleGraph.sum_incMatrix_apply

theorem incMatrix_mul_transpose_diag [DecidableEq Î±] [DecidableRel G.Adj] :
    (G.incMatrix R * (G.incMatrix R)áµ€) a a = G.degree a := by
  rw [â† sum_incMatrix_apply]
  -- âŠ¢ (incMatrix R G * (incMatrix R G)áµ€) a a = âˆ‘ e : Sym2 Î±, incMatrix R G a e
  simp only [mul_apply, incMatrix_apply', transpose_apply, mul_ite, mul_one, mul_zero]
  -- âŠ¢ (âˆ‘ x : Sym2 Î±, if x âˆˆ incidenceSet G a then if x âˆˆ incidenceSet G a then 1 e â€¦
  simp_all only [ite_true, sum_boole]
  -- ğŸ‰ no goals
#align simple_graph.inc_matrix_mul_transpose_diag SimpleGraph.incMatrix_mul_transpose_diag

theorem sum_incMatrix_apply_of_mem_edgeSet :
    e âˆˆ G.edgeSet â†’ âˆ‘ a, G.incMatrix R a e = 2 := by
  classical
    refine' e.ind _
    intro a b h
    rw [mem_edgeSet] at h
    rw [â† Nat.cast_two, â† card_doubleton h.ne]
    simp only [incMatrix_apply', sum_boole, mk'_mem_incidenceSet_iff, h, true_and_iff]
    congr 2
    ext e
    simp only [mem_filter, mem_univ, true_and_iff, mem_insert, mem_singleton]
#align simple_graph.sum_inc_matrix_apply_of_mem_edge_set SimpleGraph.sum_incMatrix_apply_of_mem_edgeSet

theorem sum_incMatrix_apply_of_not_mem_edgeSet (h : e âˆ‰ G.edgeSet) :
    âˆ‘ a, G.incMatrix R a e = 0 :=
  sum_eq_zero fun _ _ => G.incMatrix_of_not_mem_incidenceSet fun he => h he.1
#align simple_graph.sum_inc_matrix_apply_of_not_mem_edge_set SimpleGraph.sum_incMatrix_apply_of_not_mem_edgeSet

theorem incMatrix_transpose_mul_diag [DecidableRel G.Adj] :
    ((G.incMatrix R)áµ€ * G.incMatrix R) e e = if e âˆˆ G.edgeSet then 2 else 0 := by
  classical
    simp only [Matrix.mul_apply, incMatrix_apply', transpose_apply, â† ite_and_mul_zero, one_mul,
      sum_boole, and_self_iff]
    split_ifs with h
    Â· revert h
      refine' e.ind _
      intro v w h
      rw [â† Nat.cast_two, â† card_doubleton (G.ne_of_adj h)]
      simp [mk'_mem_incidenceSet_iff, G.mem_edgeSet.mp h]
      congr 2
      ext u
      simp
    Â· revert h
      refine' e.ind _
      intro v w h
      simp [mk'_mem_incidenceSet_iff, G.mem_edgeSet.not.mp h]
#align simple_graph.inc_matrix_transpose_mul_diag SimpleGraph.incMatrix_transpose_mul_diag

end NonAssocSemiring

section Semiring

variable [Fintype (Sym2 Î±)] [Semiring R] {a b : Î±} {e : Sym2 Î±}

theorem incMatrix_mul_transpose_apply_of_adj (h : G.Adj a b) :
    (G.incMatrix R * (G.incMatrix R)áµ€) a b = (1 : R) := by
  classical
    simp_rw [Matrix.mul_apply, Matrix.transpose_apply, incMatrix_apply_mul_incMatrix_apply,
      Set.indicator_apply, Pi.one_apply, sum_boole]
    convert @Nat.cast_one R _
    convert card_singleton âŸ¦(a, b)âŸ§
    rw [â† coe_eq_singleton, coe_filter_univ]
    exact G.incidenceSet_inter_incidenceSet_of_adj h
#align simple_graph.inc_matrix_mul_transpose_apply_of_adj SimpleGraph.incMatrix_mul_transpose_apply_of_adj

theorem incMatrix_mul_transpose [Fintype Î±] [DecidableEq Î±] [DecidableRel G.Adj] :
    G.incMatrix R * (G.incMatrix R)áµ€ = fun a b =>
      if a = b then (G.degree a : R) else if G.Adj a b then 1 else 0 := by
  ext a b
  -- âŠ¢ (incMatrix R G * (incMatrix R G)áµ€) a b = if a = b then â†‘(degree G a) else if â€¦
  split_ifs with h h'
  Â· subst b
    -- âŠ¢ (incMatrix R G * (incMatrix R G)áµ€) a a = â†‘(degree G a)
    rename Semiring R => sr
    -- âŠ¢ (incMatrix R G * (incMatrix R G)áµ€) a a = â†‘(degree G a)
    convert @incMatrix_mul_transpose_diag _ _ _ _ sr.toNonAssocSemiring _ _ _
    -- ğŸ‰ no goals
  Â· exact G.incMatrix_mul_transpose_apply_of_adj h'
    -- ğŸ‰ no goals
  Â· simp only [Matrix.mul_apply, Matrix.transpose_apply,
      G.incMatrix_apply_mul_incMatrix_apply_of_not_adj h h', sum_const_zero]
#align simple_graph.inc_matrix_mul_transpose SimpleGraph.incMatrix_mul_transpose

end Semiring

end SimpleGraph
