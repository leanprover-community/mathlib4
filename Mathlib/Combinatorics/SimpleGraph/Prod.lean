/-
Copyright (c) 2022 George Peter Banyard, YaÃ«l Dillies, Kyle Miller. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: George Peter Banyard, YaÃ«l Dillies, Kyle Miller
-/
import Mathlib.Combinatorics.SimpleGraph.Connectivity

#align_import combinatorics.simple_graph.prod from "leanprover-community/mathlib"@"2985fa3c31a27274aed06c433510bc14b73d6488"

/-!
# Graph products

This file defines the box product of graphs and other product constructions. The box product of `G`
and `H` is the graph on the product of the vertices such that `x` and `y` are related iff they agree
on one component and the other one is related via either `G` or `H`. For example, the box product of
two edges is a square.

## Main declarations

* `SimpleGraph.boxProd`: The box product.

## Notation

* `G â–¡ H`: The box product of `G` and `H`.

## TODO

Define all other graph products!
-/

set_option autoImplicit true


variable {Î± Î² Î³ : Type*}

namespace SimpleGraph

-- Porting note: pruned variables to keep things out of local contexts, which
-- can impact how generalization works, or what aesop does.
variable {G : SimpleGraph Î±} {H : SimpleGraph Î²}

/-- Box product of simple graphs. It relates `(aâ‚, b)` and `(aâ‚‚, b)` if `G` relates `aâ‚` and `aâ‚‚`,
and `(a, bâ‚)` and `(a, bâ‚‚)` if `H` relates `bâ‚` and `bâ‚‚`. -/
def boxProd (G : SimpleGraph Î±) (H : SimpleGraph Î²) : SimpleGraph (Î± Ã— Î²) where
  Adj x y := G.Adj x.1 y.1 âˆ§ x.2 = y.2 âˆ¨ H.Adj x.2 y.2 âˆ§ x.1 = y.1
  symm x y := by simp [and_comm, or_comm, eq_comm, adj_comm]
                 -- ðŸŽ‰ no goals
  loopless x := by simp
                   -- ðŸŽ‰ no goals
#align simple_graph.box_prod SimpleGraph.boxProd

-- mathport name: Â«expr â–¡ Â»
/-- Box product of simple graphs. It relates `(aâ‚, b)` and `(aâ‚‚, b)` if `G` relates `aâ‚` and `aâ‚‚`,
and `(a, bâ‚)` and `(a, bâ‚‚)` if `H` relates `bâ‚` and `bâ‚‚`. -/
infixl:70 " â–¡ " => boxProd

@[simp]
theorem boxProd_adj : (G â–¡ H).Adj x y â†” G.Adj x.1 y.1 âˆ§ x.2 = y.2 âˆ¨ H.Adj x.2 y.2 âˆ§ x.1 = y.1 :=
  Iff.rfl
#align simple_graph.box_prod_adj SimpleGraph.boxProd_adj

--@[simp] porting note: `simp` can prove
theorem boxProd_adj_left : (G â–¡ H).Adj (aâ‚, b) (aâ‚‚, b) â†” G.Adj aâ‚ aâ‚‚ := by
  simp only [boxProd_adj, and_true, SimpleGraph.irrefl, false_and, or_false]
  -- ðŸŽ‰ no goals
#align simple_graph.box_prod_adj_left SimpleGraph.boxProd_adj_left

--@[simp] porting note: `simp` can prove
theorem boxProd_adj_right : (G â–¡ H).Adj (a, bâ‚) (a, bâ‚‚) â†” H.Adj bâ‚ bâ‚‚ := by
  simp only [boxProd_adj, SimpleGraph.irrefl, false_and, and_true, false_or]
  -- ðŸŽ‰ no goals
#align simple_graph.box_prod_adj_right SimpleGraph.boxProd_adj_right

theorem boxProd_neighborSet (x : Î± Ã— Î²) :
    (G â–¡ H).neighborSet x = G.neighborSet x.1 Ã—Ë¢ {x.2} âˆª {x.1} Ã—Ë¢ H.neighborSet x.2 := by
  ext âŸ¨a', b'âŸ©
  -- âŠ¢ (a', b') âˆˆ neighborSet (G â–¡ H) x â†” (a', b') âˆˆ neighborSet G x.fst Ã—Ë¢ {x.snd} â€¦
  simp only [mem_neighborSet, Set.mem_union, boxProd_adj, Set.mem_prod, Set.mem_singleton_iff]
  -- âŠ¢ Adj G x.fst a' âˆ§ x.snd = b' âˆ¨ Adj H x.snd b' âˆ§ x.fst = a' â†” Adj G x.fst a' âˆ§ â€¦
  simp only [eq_comm, and_comm]
  -- ðŸŽ‰ no goals
#align simple_graph.box_prod_neighbor_set SimpleGraph.boxProd_neighborSet

variable (G H)

/-- The box product is commutative up to isomorphism. `Equiv.prodComm` as a graph isomorphism. -/
@[simps!]
def boxProdComm : G â–¡ H â‰ƒg H â–¡ G := âŸ¨Equiv.prodComm _ _, or_commâŸ©
#align simple_graph.box_prod_comm SimpleGraph.boxProdComm

/-- The box product is associative up to isomorphism. `Equiv.prodAssoc` as a graph isomorphism. -/
@[simps!]
def boxProdAssoc (I : SimpleGraph Î³) : G â–¡ H â–¡ I â‰ƒg G â–¡ (H â–¡ I) :=
  âŸ¨Equiv.prodAssoc _ _ _, fun {x y} => by
    simp only [boxProd_adj, Equiv.prodAssoc_apply, or_and_right, or_assoc, Prod.ext_iff,
      and_assoc, @and_comm (x.fst.fst = _)]âŸ©
#align simple_graph.box_prod_assoc SimpleGraph.boxProdAssoc

/-- The embedding of `G` into `G â–¡ H` given by `b`. -/
@[simps]
def boxProdLeft (b : Î²) : G â†ªg G â–¡ H where
  toFun a := (a, b)
  inj' _ _ := congr_arg Prod.fst
  map_rel_iff' {_ _} := boxProd_adj_left
#align simple_graph.box_prod_left SimpleGraph.boxProdLeft

/-- The embedding of `H` into `G â–¡ H` given by `a`. -/
@[simps]
def boxProdRight (a : Î±) : H â†ªg G â–¡ H where
  toFun := Prod.mk a
  inj' _ _ := congr_arg Prod.snd
  map_rel_iff' {_ _} := boxProd_adj_right
#align simple_graph.box_prod_right SimpleGraph.boxProdRight

namespace Walk

variable {G}

/-- Turn a walk on `G` into a walk on `G â–¡ H`. -/
protected def boxProdLeft (b : Î²) : G.Walk aâ‚ aâ‚‚ â†’ (G â–¡ H).Walk (aâ‚, b) (aâ‚‚, b) :=
  Walk.map (G.boxProdLeft H b).toHom
#align simple_graph.walk.box_prod_left SimpleGraph.Walk.boxProdLeft

variable (G) {H}

/-- Turn a walk on `H` into a walk on `G â–¡ H`. -/
protected def boxProdRight (a : Î±) : H.Walk bâ‚ bâ‚‚ â†’ (G â–¡ H).Walk (a, bâ‚) (a, bâ‚‚) :=
  Walk.map (G.boxProdRight H a).toHom
#align simple_graph.walk.box_prod_right SimpleGraph.Walk.boxProdRight

variable {G}

/-- Project a walk on `G â–¡ H` to a walk on `G` by discarding the moves in the direction of `H`. -/
def ofBoxProdLeft [DecidableEq Î²] [DecidableRel G.Adj] {x y : Î± Ã— Î²} :
    (G â–¡ H).Walk x y â†’ G.Walk x.1 y.1
  | nil => nil
  | cons h w =>
    Or.by_cases h
      (fun hG => w.ofBoxProdLeft.cons hG.1)
      (fun hH => hH.2 â–¸ w.ofBoxProdLeft)
#align simple_graph.walk.of_box_prod_left SimpleGraph.Walk.ofBoxProdLeft

/-- Project a walk on `G â–¡ H` to a walk on `H` by discarding the moves in the direction of `G`. -/
def ofBoxProdRight [DecidableEq Î±] [DecidableRel H.Adj] {x y : Î± Ã— Î²} :
    (G â–¡ H).Walk x y â†’ H.Walk x.2 y.2
  | nil => nil
  | cons h w =>
    (Or.symm h).by_cases
      (fun hH => w.ofBoxProdRight.cons hH.1)
      (fun hG => hG.2 â–¸ w.ofBoxProdRight)
#align simple_graph.walk.of_box_prod_right SimpleGraph.Walk.ofBoxProdRight

@[simp]
theorem ofBoxProdLeft_boxProdLeft [DecidableEq Î²] [DecidableRel G.Adj] {aâ‚ aâ‚‚ : Î±} :
    âˆ€ (w : G.Walk aâ‚ aâ‚‚), (w.boxProdLeft H b).ofBoxProdLeft = w
  | nil => rfl
  | cons' x y z h w => by
    rw [Walk.boxProdLeft, map_cons, ofBoxProdLeft, Or.by_cases, dif_pos, â† Walk.boxProdLeft]
    simp [ofBoxProdLeft_boxProdLeft]
    -- âŠ¢ Adj G (x, b).fst (â†‘(Embedding.toHom (boxProdLeft G H b)) y).fst âˆ§ (x, b).snd â€¦
    exact âŸ¨h, rflâŸ©
    -- ðŸŽ‰ no goals
#align simple_graph.walk.of_box_prod_left_box_prod_left SimpleGraph.Walk.ofBoxProdLeft_boxProdLeft

@[simp]
theorem ofBoxProdLeft_boxProdRight [DecidableEq Î±] [DecidableRel G.Adj] {bâ‚ bâ‚‚ : Î±} :
    âˆ€ (w : G.Walk bâ‚ bâ‚‚), (w.boxProdRight G a).ofBoxProdRight = w
  | nil => rfl
  | cons' x y z h w => by
    rw [Walk.boxProdRight, map_cons, ofBoxProdRight, Or.by_cases, dif_pos, â†
      Walk.boxProdRight]
    simp [ofBoxProdLeft_boxProdRight]
    -- âŠ¢ Adj G (a, x).snd (â†‘(Embedding.toHom (boxProdRight G G a)) y).snd âˆ§ (a, x).fs â€¦
    exactâŸ¨h, rflâŸ©
    -- ðŸŽ‰ no goals
#align simple_graph.walk.of_box_prod_left_box_prod_right SimpleGraph.Walk.ofBoxProdLeft_boxProdRight

end Walk

variable {G H}

protected theorem Preconnected.boxProd (hG : G.Preconnected) (hH : H.Preconnected) :
    (G â–¡ H).Preconnected := by
  rintro x y
  -- âŠ¢ Reachable (G â–¡ H) x y
  obtain âŸ¨wâ‚âŸ© := hG x.1 y.1
  -- âŠ¢ Reachable (G â–¡ H) x y
  obtain âŸ¨wâ‚‚âŸ© := hH x.2 y.2
  -- âŠ¢ Reachable (G â–¡ H) x y
  rw [â† @Prod.mk.eta _ _ x, â† @Prod.mk.eta _ _ y]
  -- âŠ¢ Reachable (G â–¡ H) (x.fst, x.snd) (y.fst, y.snd)
  exact âŸ¨(wâ‚.boxProdLeft _ _).append (wâ‚‚.boxProdRight _ _)âŸ©
  -- ðŸŽ‰ no goals
#align simple_graph.preconnected.box_prod SimpleGraph.Preconnected.boxProd

protected theorem Preconnected.ofBoxProdLeft [Nonempty Î²] (h : (G â–¡ H).Preconnected) :
    G.Preconnected := by
  classical
  rintro aâ‚ aâ‚‚
  obtain âŸ¨wâŸ© := h (aâ‚, Classical.arbitrary _) (aâ‚‚, Classical.arbitrary _)
  exact âŸ¨w.ofBoxProdLeftâŸ©
#align simple_graph.preconnected.of_box_prod_left SimpleGraph.Preconnected.ofBoxProdLeft

protected theorem Preconnected.ofBoxProdRight [Nonempty Î±] (h : (G â–¡ H).Preconnected) :
    H.Preconnected := by
  classical
  rintro bâ‚ bâ‚‚
  obtain âŸ¨wâŸ© := h (Classical.arbitrary _, bâ‚) (Classical.arbitrary _, bâ‚‚)
  exact âŸ¨w.ofBoxProdRightâŸ©
#align simple_graph.preconnected.of_box_prod_right SimpleGraph.Preconnected.ofBoxProdRight

protected theorem Connected.boxProd (hG : G.Connected) (hH : H.Connected) : (G â–¡ H).Connected := by
  haveI := hG.nonempty
  -- âŠ¢ Connected (G â–¡ H)
  haveI := hH.nonempty
  -- âŠ¢ Connected (G â–¡ H)
  exact âŸ¨hG.preconnected.boxProd hH.preconnectedâŸ©
  -- ðŸŽ‰ no goals
#align simple_graph.connected.box_prod SimpleGraph.Connected.boxProd

protected theorem Connected.ofBoxProdLeft (h : (G â–¡ H).Connected) : G.Connected := by
  haveI := (nonempty_prod.1 h.nonempty).1
  -- âŠ¢ Connected G
  haveI := (nonempty_prod.1 h.nonempty).2
  -- âŠ¢ Connected G
  exact âŸ¨h.preconnected.ofBoxProdLeftâŸ©
  -- ðŸŽ‰ no goals
#align simple_graph.connected.of_box_prod_left SimpleGraph.Connected.ofBoxProdLeft

protected theorem Connected.ofBoxProdRight (h : (G â–¡ H).Connected) : H.Connected := by
  haveI := (nonempty_prod.1 h.nonempty).1
  -- âŠ¢ Connected H
  haveI := (nonempty_prod.1 h.nonempty).2
  -- âŠ¢ Connected H
  exact âŸ¨h.preconnected.ofBoxProdRightâŸ©
  -- ðŸŽ‰ no goals
#align simple_graph.connected.of_box_prod_right SimpleGraph.Connected.ofBoxProdRight

@[simp]
theorem boxProd_connected : (G â–¡ H).Connected â†” G.Connected âˆ§ H.Connected :=
  âŸ¨fun h => âŸ¨h.ofBoxProdLeft, h.ofBoxProdRightâŸ©, fun h => h.1.boxProd h.2âŸ©
#align simple_graph.box_prod_connected SimpleGraph.boxProd_connected

instance boxProdFintypeNeighborSet (x : Î± Ã— Î²)
    [Fintype (G.neighborSet x.1)] [Fintype (H.neighborSet x.2)] :
    Fintype ((G â–¡ H).neighborSet x) :=
  Fintype.ofEquiv
    ((G.neighborFinset x.1 Ã—Ë¢ {x.2}).disjUnion ({x.1} Ã—Ë¢ H.neighborFinset x.2) <|
        Finset.disjoint_product.mpr <| Or.inl <| neighborFinset_disjoint_singleton _ _)
    ((Equiv.refl _).subtypeEquiv fun y => by
      simp_rw [Finset.mem_disjUnion, Finset.mem_product, Finset.mem_singleton, mem_neighborFinset,
        mem_neighborSet, Equiv.refl_apply, boxProd_adj]
      simp only [eq_comm, and_comm])
      -- ðŸŽ‰ no goals
#align simple_graph.box_prod_fintype_neighbor_set SimpleGraph.boxProdFintypeNeighborSet

theorem boxProd_neighborFinset (x : Î± Ã— Î²)
    [Fintype (G.neighborSet x.1)] [Fintype (H.neighborSet x.2)] [Fintype ((G â–¡ H).neighborSet x)] :
    (G â–¡ H).neighborFinset x =
      (G.neighborFinset x.1 Ã—Ë¢ {x.2}).disjUnion ({x.1} Ã—Ë¢ H.neighborFinset x.2)
        (Finset.disjoint_product.mpr <| Or.inl <| neighborFinset_disjoint_singleton _ _) := by
  -- swap out the fintype instance for the canonical one
  letI : Fintype ((G â–¡ H).neighborSet x) := SimpleGraph.boxProdFintypeNeighborSet _
  -- âŠ¢ neighborFinset (G â–¡ H) x = Finset.disjUnion (neighborFinset G x.fst Ã—Ë¢ {x.sn â€¦
  convert_to (G â–¡ H).neighborFinset x = _ using 2
  -- âŠ¢ neighborFinset (G â–¡ H) x = Finset.disjUnion (neighborFinset G x.fst Ã—Ë¢ {x.sn â€¦
  exact Eq.trans (Finset.map_map _ _ _) Finset.attach_map_val
  -- ðŸŽ‰ no goals
#align simple_graph.box_prod_neighbor_finset SimpleGraph.boxProd_neighborFinset

theorem boxProd_degree (x : Î± Ã— Î²)
    [Fintype (G.neighborSet x.1)] [Fintype (H.neighborSet x.2)] [Fintype ((G â–¡ H).neighborSet x)] :
    (G â–¡ H).degree x = G.degree x.1 + H.degree x.2 := by
  rw [degree, degree, degree, boxProd_neighborFinset, Finset.card_disjUnion]
  -- âŠ¢ Finset.card (neighborFinset G x.fst Ã—Ë¢ {x.snd}) + Finset.card ({x.fst} Ã—Ë¢ ne â€¦
  simp_rw [Finset.card_product, Finset.card_singleton, mul_one, one_mul]
  -- ðŸŽ‰ no goals
#align simple_graph.box_prod_degree SimpleGraph.boxProd_degree

end SimpleGraph
