/-
Copyright (c) 2023 Kyle Miller, R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kyle Miller, R√©mi Bottinelli
-/
import Mathlib.Combinatorics.SimpleGraph.Connectivity

/-!
# Connectivity of subgraphs and induced graphs

## Main definitions

* `SimpleGraph.Subgraph.Preconnected` and `SimpleGraph.Subgraph.Connected` give subgraphs
  connectivity predicates via `SimpleGraph.subgraph.coe`.

-/

set_option autoImplicit true

namespace SimpleGraph

variable {V : Type u} {V' : Type v} {G : SimpleGraph V} {G' : SimpleGraph V'}

namespace Subgraph

/-- A subgraph is preconnected if it is preconnected when coerced to be a simple graph.

Note: This is a structure to make it so one can be precise about how dot notation resolves. -/
protected structure Preconnected (H : G.Subgraph) : Prop where
  protected coe : H.coe.Preconnected

instance {H : G.Subgraph} : Coe H.Preconnected H.coe.Preconnected := ‚ü®Preconnected.coe‚ü©

instance {H : G.Subgraph} : CoeFun H.Preconnected (fun _ => ‚àÄ u v : H.verts, H.coe.Reachable u v) :=
  ‚ü®fun h => h.coe‚ü©

protected lemma preconnected_iff {H : G.Subgraph} :
    H.Preconnected ‚Üî H.coe.Preconnected := ‚ü®fun ‚ü®h‚ü© => h, .mk‚ü©

/-- A subgraph is connected if it is connected when coerced to be a simple graph.

Note: This is a structure to make it so one can be precise about how dot notation resolves. -/
protected structure Connected (H : G.Subgraph) : Prop where
  protected coe : H.coe.Connected
#align simple_graph.subgraph.connected SimpleGraph.Subgraph.Connected

instance {H : G.Subgraph} : Coe H.Connected H.coe.Connected := ‚ü®Connected.coe‚ü©

instance {H : G.Subgraph} : CoeFun H.Connected (fun _ => ‚àÄ u v : H.verts, H.coe.Reachable u v) :=
  ‚ü®fun h => h.coe‚ü©

protected lemma connected_iff' {H : G.Subgraph} :
    H.Connected ‚Üî H.coe.Connected := ‚ü®fun ‚ü®h‚ü© => h, .mk‚ü©

protected lemma connected_iff {H : G.Subgraph} :
    H.Connected ‚Üî H.Preconnected ‚àß H.verts.Nonempty := by
  rw [H.connected_iff', connected_iff, H.preconnected_iff, Set.nonempty_coe_sort]
  -- üéâ no goals

protected lemma Connected.preconnected {H : G.Subgraph} (h : H.Connected) : H.Preconnected := by
  rw [H.connected_iff] at h; exact h.1
  -- ‚ä¢ Subgraph.Preconnected H
                             -- üéâ no goals

protected lemma Connected.nonempty {H : G.Subgraph} (h : H.Connected) : H.verts.Nonempty := by
  rw [H.connected_iff] at h; exact h.2
  -- ‚ä¢ Set.Nonempty H.verts
                             -- üéâ no goals

theorem singletonSubgraph_connected {v : V} : (G.singletonSubgraph v).Connected := by
  refine ‚ü®‚ü®?_‚ü©‚ü©
  -- ‚ä¢ Preconnected (Subgraph.coe (SimpleGraph.singletonSubgraph G v))
  rintro ‚ü®a, ha‚ü© ‚ü®b, hb‚ü©
  -- ‚ä¢ Reachable (Subgraph.coe (SimpleGraph.singletonSubgraph G v)) { val := a, pro ‚Ä¶
  simp only [singletonSubgraph_verts, Set.mem_singleton_iff] at ha hb
  -- ‚ä¢ Reachable (Subgraph.coe (SimpleGraph.singletonSubgraph G v)) { val := a, pro ‚Ä¶
  subst_vars
  -- ‚ä¢ Reachable (Subgraph.coe (SimpleGraph.singletonSubgraph G b)) { val := b, pro ‚Ä¶
  rfl
  -- üéâ no goals
#align simple_graph.singleton_subgraph_connected SimpleGraph.Subgraph.singletonSubgraph_connected

@[simp]
theorem subgraphOfAdj_connected {v w : V} (hvw : G.Adj v w) : (G.subgraphOfAdj hvw).Connected := by
  refine ‚ü®‚ü®?_‚ü©‚ü©
  -- ‚ä¢ Preconnected (Subgraph.coe (subgraphOfAdj G hvw))
  rintro ‚ü®a, ha‚ü© ‚ü®b, hb‚ü©
  -- ‚ä¢ Reachable (Subgraph.coe (subgraphOfAdj G hvw)) { val := a, property := ha }  ‚Ä¶
  simp only [subgraphOfAdj_verts, Set.mem_insert_iff, Set.mem_singleton_iff] at ha hb
  -- ‚ä¢ Reachable (Subgraph.coe (subgraphOfAdj G hvw)) { val := a, property := ha‚úù } ‚Ä¶
  obtain rfl | rfl := ha <;> obtain rfl | rfl := hb <;>
  -- ‚ä¢ Reachable (Subgraph.coe (subgraphOfAdj G hvw)) { val := a, property := ha }  ‚Ä¶
                             -- ‚ä¢ Reachable (Subgraph.coe (subgraphOfAdj G hvw)) { val := b, property := ha }  ‚Ä¶
                             -- ‚ä¢ Reachable (Subgraph.coe (subgraphOfAdj G hvw)) { val := a, property := ha }  ‚Ä¶
    first | rfl | (apply Adj.reachable; simp)
    -- üéâ no goals
    -- üéâ no goals
    -- üéâ no goals
    -- üéâ no goals
#align simple_graph.subgraph_of_adj_connected SimpleGraph.Subgraph.subgraphOfAdj_connected

lemma top_induce_pair_connected_of_adj {u v : V} (huv : G.Adj u v) :
    ((‚ä§ : G.Subgraph).induce {u, v}).Connected := by
  rw [‚Üê subgraphOfAdj_eq_induce huv]
  -- ‚ä¢ Subgraph.Connected (subgraphOfAdj G huv)
  exact subgraphOfAdj_connected huv
  -- üéâ no goals

@[mono]
protected lemma Connected.mono {H H' : G.Subgraph} (hle : H ‚â§ H') (hv : H.verts = H'.verts)
    (h : H.Connected) : H'.Connected := by
  rw [‚Üê Subgraph.copy_eq H' H.verts hv H'.Adj rfl]
  -- ‚ä¢ Subgraph.Connected (copy H' H.verts hv H'.Adj (_ : H'.Adj = H'.Adj))
  refine ‚ü®h.coe.mono ?_‚ü©
  -- ‚ä¢ Subgraph.coe H ‚â§ Subgraph.coe (copy H' H.verts hv H'.Adj (_ : H'.Adj = H'.Ad ‚Ä¶
  rintro ‚ü®v, hv‚ü© ‚ü®w, hw‚ü© hvw
  -- ‚ä¢ SimpleGraph.Adj (Subgraph.coe (copy H' H.verts hv‚úù H'.Adj (_ : H'.Adj = H'.A ‚Ä¶
  exact hle.2 hvw
  -- üéâ no goals

protected lemma Connected.mono' {H H' : G.Subgraph}
    (hle : ‚àÄ v w, H.Adj v w ‚Üí H'.Adj v w) (hv : H.verts = H'.verts)
    (h : H.Connected) : H'.Connected := by
  exact h.mono ‚ü®hv.le, hle‚ü© hv
  -- üéâ no goals

protected lemma Connected.sup {H K : G.Subgraph}
    (hH : H.Connected) (hK : K.Connected) (hn : (H ‚äì K).verts.Nonempty ) :
    (H ‚äî K).Connected := by
  rw [Subgraph.connected_iff', connected_iff_exists_forall_reachable]
  -- ‚ä¢ ‚àÉ v, ‚àÄ (w : ‚Üë(H ‚äî K).verts), Reachable (Subgraph.coe (H ‚äî K)) v w
  obtain ‚ü®u, hu, hu'‚ü© := hn
  -- ‚ä¢ ‚àÉ v, ‚àÄ (w : ‚Üë(H ‚äî K).verts), Reachable (Subgraph.coe (H ‚äî K)) v w
  exists ‚ü®u, Or.inl hu‚ü©
  -- ‚ä¢ ‚àÄ (w : ‚Üë(H ‚äî K).verts), Reachable (Subgraph.coe (H ‚äî K)) { val := u, propert ‚Ä¶
  rintro ‚ü®v, (hv|hv)‚ü©
  -- ‚ä¢ Reachable (Subgraph.coe (H ‚äî K)) { val := u, property := (_ : u ‚àà H.verts ‚à®  ‚Ä¶
  ¬∑ exact Reachable.map (Subgraph.inclusion (le_sup_left : H ‚â§ H ‚äî K)) (hH ‚ü®u, hu‚ü© ‚ü®v, hv‚ü©)
    -- üéâ no goals
  ¬∑ exact Reachable.map (Subgraph.inclusion (le_sup_right : K ‚â§ H ‚äî K)) (hK ‚ü®u, hu'‚ü© ‚ü®v, hv‚ü©)
    -- üéâ no goals

lemma _root_.SimpleGraph.Walk.toSubgraph_connected {u v : V} (p : G.Walk u v) :
    p.toSubgraph.Connected := by
  induction p with
  | nil => apply singletonSubgraph_connected
  | cons h p ih =>
    apply (subgraphOfAdj_connected h).sup ih
    rename_i w _
    exists w
    simp

lemma induce_union_connected {H : G.Subgraph} {s t : Set V}
    (sconn : (H.induce s).Connected) (tconn : (H.induce t).Connected)
    (sintert : (s ‚äì t).Nonempty ) :
    (H.induce (s ‚à™ t)).Connected := by
  refine (sconn.sup tconn sintert).mono ?_ ?_
  -- ‚ä¢ induce H s ‚äî induce H t ‚â§ induce H (s ‚à™ t)
  ¬∑ apply le_induce_union
    -- üéâ no goals
  ¬∑ simp
    -- üéâ no goals

lemma Connected.adj_union {H K : G.Subgraph}
    (Hconn : H.Connected) (Kconn : K.Connected) {u v : V} (uH : u ‚àà H.verts) (vK : v ‚àà K.verts)
    (huv : G.Adj u v) :
    ((‚ä§ : G.Subgraph).induce {u, v} ‚äî H ‚äî K).Connected := by
  refine ((top_induce_pair_connected_of_adj huv).sup Hconn ?_).sup Kconn ?_
  -- ‚ä¢ Set.Nonempty (induce ‚ä§ {u, v} ‚äì H).verts
  ¬∑ exact ‚ü®u, by simp [uH]‚ü©
    -- üéâ no goals
  ¬∑ exact ‚ü®v, by simp [vK]‚ü©
    -- üéâ no goals

lemma preconnected_iff_forall_exists_walk_subgraph (H : G.Subgraph) :
    H.Preconnected ‚Üî ‚àÄ {u v}, u ‚àà H.verts ‚Üí v ‚àà H.verts ‚Üí ‚àÉ p : G.Walk u v, p.toSubgraph ‚â§ H := by
  constructor
  -- ‚ä¢ Subgraph.Preconnected H ‚Üí ‚àÄ {u v : V}, u ‚àà H.verts ‚Üí v ‚àà H.verts ‚Üí ‚àÉ p, Walk ‚Ä¶
  ¬∑ intro hc u v hu hv
    -- ‚ä¢ ‚àÉ p, Walk.toSubgraph p ‚â§ H
    refine (hc ‚ü®_, hu‚ü© ‚ü®_, hv‚ü©).elim fun p => ?_
    -- ‚ä¢ ‚àÉ p, Walk.toSubgraph p ‚â§ H
    exists p.map (Subgraph.hom _)
    -- ‚ä¢ Walk.toSubgraph (Walk.map (Subgraph.hom H) p) ‚â§ H
    simp [coeSubgraph_le]
    -- üéâ no goals
  ¬∑ intro hw
    -- ‚ä¢ Subgraph.Preconnected H
    rw [Subgraph.preconnected_iff]
    -- ‚ä¢ Preconnected (Subgraph.coe H)
    rintro ‚ü®u, hu‚ü© ‚ü®v, hv‚ü©
    -- ‚ä¢ Reachable (Subgraph.coe H) { val := u, property := hu } { val := v, property ‚Ä¶
    obtain ‚ü®p, h‚ü© := hw hu hv
    -- ‚ä¢ Reachable (Subgraph.coe H) { val := u, property := hu } { val := v, property ‚Ä¶
    exact Reachable.map (Subgraph.inclusion h)
      (p.toSubgraph_connected ‚ü®_, p.start_mem_verts_toSubgraph‚ü© ‚ü®_, p.end_mem_verts_toSubgraph‚ü©)

lemma connected_iff_forall_exists_walk_subgraph (H : G.Subgraph) :
    H.Connected ‚Üî H.verts.Nonempty ‚àß ‚àÄ {u v}, u ‚àà H.verts ‚Üí v ‚àà H.verts ‚Üí
                                        ‚àÉ p : G.Walk u v, p.toSubgraph ‚â§ H := by
  rw [H.connected_iff, preconnected_iff_forall_exists_walk_subgraph, and_comm]
  -- üéâ no goals

end Subgraph

section induced_subgraphs

lemma connected_induce_iff : (G.induce s).Connected ‚Üî ((‚ä§ : G.Subgraph).induce s).Connected := by
  rw [induce_eq_coe_induce_top, ‚Üê Subgraph.connected_iff']
  -- üéâ no goals

lemma induce_union_connected {s t : Set V}
    (sconn : (G.induce s).Connected) (tconn : (G.induce t).Connected)
    (sintert : (s ‚à© t).Nonempty ) :
    (G.induce (s ‚à™ t)).Connected := by
  rw [connected_induce_iff] at sconn tconn ‚ä¢
  -- ‚ä¢ Subgraph.Connected (Subgraph.induce ‚ä§ (s ‚à™ t))
  exact Subgraph.induce_union_connected sconn tconn sintert
  -- üéâ no goals

lemma induce_pair_connected_of_adj {u v : V} (huv : G.Adj u v) :
    (G.induce {u, v}).Connected := by
  rw [connected_induce_iff]
  -- ‚ä¢ Subgraph.Connected (Subgraph.induce ‚ä§ {u, v})
  exact Subgraph.top_induce_pair_connected_of_adj huv
  -- üéâ no goals

lemma Subgraph.Connected.induce_verts {H : G.Subgraph} (h : H.Connected) :
    (G.induce H.verts).Connected := by
  rw [connected_induce_iff]
  -- ‚ä¢ Subgraph.Connected (induce ‚ä§ H.verts)
  exact h.mono le_induce_top_verts (by exact rfl)
  -- üéâ no goals

lemma Walk.connected_induce_support {u v : V} (p : G.Walk u v) :
    (G.induce {v | v ‚àà p.support}).Connected := by
  rw [‚Üê p.verts_toSubgraph]
  -- ‚ä¢ Connected (induce (Walk.toSubgraph p).verts G)
  exact p.toSubgraph_connected.induce_verts
  -- üéâ no goals

lemma induce_connected_adj_union {s t : Set V}
    (sconn : (G.induce s).Connected) (tconn : (G.induce t).Connected)
    (hv : v ‚àà s) (hw : w ‚àà t) (ha : G.Adj v w) :
    (G.induce (s ‚à™ t)).Connected := by
  rw [connected_induce_iff] at sconn tconn ‚ä¢
  -- ‚ä¢ Subgraph.Connected (Subgraph.induce ‚ä§ (s ‚à™ t))
  apply (sconn.adj_union tconn hv hw ha).mono
  -- ‚ä¢ Subgraph.induce ‚ä§ {v, w} ‚äî Subgraph.induce ‚ä§ s ‚äî Subgraph.induce ‚ä§ t ‚â§ Subgr ‚Ä¶
  ¬∑ simp only [Set.mem_singleton_iff, sup_le_iff, Subgraph.le_induce_union_left,
      Subgraph.le_induce_union_right, and_true, ‚Üê Subgraph.subgraphOfAdj_eq_induce ha]
    apply subgraphOfAdj_le_of_adj
    -- ‚ä¢ Subgraph.Adj (Subgraph.induce ‚ä§ (s ‚à™ t)) v w
    simp [hv, hw, ha]
    -- üéâ no goals
  ¬∑ simp only [Set.mem_singleton_iff, sup_le_iff, Subgraph.verts_sup, Subgraph.induce_verts]
    -- ‚ä¢ {v, w} ‚à™ s ‚à™ t = s ‚à™ t
    rw [Set.union_assoc]
    -- ‚ä¢ {v, w} ‚à™ (s ‚à™ t) = s ‚à™ t
    simp [Set.insert_subset_iff, Set.singleton_subset_iff, hv, hw]
    -- üéâ no goals

lemma induce_connected_of_patches {s : Set V} (u : V) (hu : u ‚àà s)
    (patches : ‚àÄ {v} (_ : v ‚àà s), ‚àÉ (s' : Set V) (_ : s' ‚äÜ s) (hu' : u ‚àà s') (hv' : v ‚àà s'),
                  (G.induce s').Reachable ‚ü®u, hu'‚ü© ‚ü®v, hv'‚ü©) : (G.induce s).Connected := by
  rw [connected_iff_exists_forall_reachable]
  -- ‚ä¢ ‚àÉ v, ‚àÄ (w : ‚Üës), Reachable (induce s G) v w
  refine ‚ü®‚ü®u, hu‚ü©, ?_‚ü©
  -- ‚ä¢ ‚àÄ (w : ‚Üës), Reachable (induce s G) { val := u, property := hu } w
  rintro ‚ü®v, hv‚ü©
  -- ‚ä¢ Reachable (induce s G) { val := u, property := hu } { val := v, property :=  ‚Ä¶
  obtain ‚ü®sv, svs, hu', hv', uv‚ü© := patches hv
  -- ‚ä¢ Reachable (induce s G) { val := u, property := hu } { val := v, property :=  ‚Ä¶
  exact uv.map (induceHomOfLE _ svs).toHom
  -- üéâ no goals

lemma induce_sUnion_connected_of_pairwise_not_disjoint {S : Set (Set V)} (Sn : S.Nonempty)
    (Snd : ‚àÄ {s t}, s ‚àà S ‚Üí t ‚àà S ‚Üí (s ‚à© t).Nonempty)
    (Sc : ‚àÄ {s}, s ‚àà S ‚Üí (G.induce s).Connected) :
    (G.induce (‚ãÉ‚ÇÄ S)).Connected := by
  obtain ‚ü®s, sS‚ü© := Sn
  -- ‚ä¢ Connected (induce (‚ãÉ‚ÇÄ S) G)
  obtain ‚ü®v, vs‚ü© := (Sc sS).nonempty
  -- ‚ä¢ Connected (induce (‚ãÉ‚ÇÄ S) G)
  apply G.induce_connected_of_patches _ (Set.subset_sUnion_of_mem sS vs)
  -- ‚ä¢ ‚àÄ {v_1 : V}, v_1 ‚àà ‚ãÉ‚ÇÄ S ‚Üí ‚àÉ s' x hu' hv', Reachable (induce s' G) { val := v ‚Ä¶
  rintro w hw
  -- ‚ä¢ ‚àÉ s' x hu' hv', Reachable (induce s' G) { val := v, property := hu' } { val  ‚Ä¶
  simp only [Set.mem_sUnion, exists_prop] at hw
  -- ‚ä¢ ‚àÉ s' x hu' hv', Reachable (induce s' G) { val := v, property := hu' } { val  ‚Ä¶
  obtain ‚ü®t, tS, wt‚ü© := hw
  -- ‚ä¢ ‚àÉ s' x hu' hv', Reachable (induce s' G) { val := v, property := hu' } { val  ‚Ä¶
  refine ‚ü®s ‚à™ t, Set.union_subset (Set.subset_sUnion_of_mem sS) (Set.subset_sUnion_of_mem tS),
          Or.inl vs, Or.inr wt, induce_union_connected (Sc sS) (Sc tS) (Snd sS tS) _ _‚ü©

lemma extend_finset_to_connected (Gpc : G.Preconnected) {t : Finset V} (tn : t.Nonempty) :
    ‚àÉ (t' : Finset V), t ‚äÜ t' ‚àß (G.induce (t' : Set V)).Connected := by
  classical
  obtain ‚ü®u, ut‚ü© := tn
  refine ‚ü®t.biUnion (fun v => (Gpc u v).some.support.toFinset), fun v vt => ?_, ?_‚ü©
  ¬∑ simp only [Finset.mem_biUnion, List.mem_toFinset, exists_prop]
    refine ‚ü®v, vt, Walk.end_mem_support _‚ü©
  ¬∑ apply G.induce_connected_of_patches u
    ¬∑ simp only [Finset.coe_biUnion, Finset.mem_coe, List.coe_toFinset, Set.mem_iUnion,
                 Set.mem_setOf_eq, Walk.start_mem_support, exists_prop, and_true]
      exact ‚ü®u, ut‚ü©
    intros v hv
    simp only [Finset.mem_coe, Finset.mem_biUnion, List.mem_toFinset, exists_prop] at hv
    obtain ‚ü®w, wt, hw‚ü© := hv
    refine ‚ü®{x | x ‚àà (Gpc u w).some.support}, ?_, ?_‚ü©
    ¬∑ simp only [Finset.coe_biUnion, Finset.mem_coe, List.coe_toFinset]
      exact fun x xw => Set.mem_iUnion‚ÇÇ.mpr ‚ü®w,wt,xw‚ü©
    ¬∑ simp only [Set.mem_setOf_eq, Walk.start_mem_support, exists_true_left]
      refine ‚ü®hw, Walk.connected_induce_support _ _ _‚ü©
