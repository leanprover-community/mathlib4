/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Combinatorics.SimpleGraph.Basic
import Mathlib.Data.Rat.Cast.Order
import Mathlib.Order.Partition.Finpartition
import Mathlib.Tactic.GCongr
import Mathlib.Tactic.NormNum
import Mathlib.Tactic.Positivity
import Mathlib.Tactic.Ring

/-!
# Edge density

This file defines the number and density of edges of a relation/graph.

## Main declarations

Between two finsets of vertices,
* `Rel.interedges`: Finset of edges of a relation.
* `Rel.edgeDensity`: Edge density of a relation.
* `SimpleGraph.interedges`: Finset of edges of a graph.
* `SimpleGraph.edgeDensity`: Edge density of a graph.
-/

open Finset

variable {ğ•œ Î¹ Îº Î± Î² : Type*}

/-! ### Density of a relation -/


namespace Rel

section Asymmetric

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  (r : Î± â†’ Î² â†’ Prop) [âˆ€ a, DecidablePred (r a)] {s sâ‚ sâ‚‚ : Finset Î±}
  {t tâ‚ tâ‚‚ : Finset Î²} {a : Î±} {b : Î²} {Î´ : ğ•œ}

/-- Finset of edges of a relation between two finsets of vertices. -/
def interedges (s : Finset Î±) (t : Finset Î²) : Finset (Î± Ã— Î²) := {e âˆˆ s Ã—Ë¢ t | r e.1 e.2}

/-- Edge density of a relation between two finsets of vertices. -/
def edgeDensity (s : Finset Î±) (t : Finset Î²) : â„š := #(interedges r s t) / (#s * #t)

variable {r}

theorem mem_interedges_iff {x : Î± Ã— Î²} : x âˆˆ interedges r s t â†” x.1 âˆˆ s âˆ§ x.2 âˆˆ t âˆ§ r x.1 x.2 := by
  rw [interedges, mem_filter, Finset.mem_product, and_assoc]

theorem mk_mem_interedges_iff : (a, b) âˆˆ interedges r s t â†” a âˆˆ s âˆ§ b âˆˆ t âˆ§ r a b :=
  mem_interedges_iff

@[simp]
theorem interedges_empty_left (t : Finset Î²) : interedges r âˆ… t = âˆ… := by
  rw [interedges, Finset.empty_product, filter_empty]

theorem interedges_mono (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) : interedges r sâ‚‚ tâ‚‚ âŠ† interedges r sâ‚ tâ‚ :=
  fun x â†¦ by
    simp_rw [mem_interedges_iff]
    exact fun h â†¦ âŸ¨hs h.1, ht h.2.1, h.2.2âŸ©

variable (r)

theorem card_interedges_add_card_interedges_compl (s : Finset Î±) (t : Finset Î²) :
    #(interedges r s t) + #(interedges (fun x y â†¦ Â¬r x y) s t) = #s * #t := by
  classical
  rw [â† card_product, interedges, interedges, â† card_union_of_disjoint, filter_union_filter_neg_eq]
  exact disjoint_filter.2 fun _ _ â†¦ Classical.not_not.2

theorem interedges_disjoint_left {s s' : Finset Î±} (hs : Disjoint s s') (t : Finset Î²) :
    Disjoint (interedges r s t) (interedges r s' t) := by
  rw [Finset.disjoint_left] at hs âŠ¢
  intro _ hx hy
  rw [mem_interedges_iff] at hx hy
  exact hs hx.1 hy.1

theorem interedges_disjoint_right (s : Finset Î±) {t t' : Finset Î²} (ht : Disjoint t t') :
    Disjoint (interedges r s t) (interedges r s t') := by
  rw [Finset.disjoint_left] at ht âŠ¢
  intro _ hx hy
  rw [mem_interedges_iff] at hx hy
  exact ht hx.2.1 hy.2.1

section DecidableEq

variable [DecidableEq Î±] [DecidableEq Î²]

lemma interedges_eq_biUnion :
    interedges r s t =
      s.biUnion fun x â†¦ {y âˆˆ t | r x y}.map âŸ¨(x, Â·), Prod.mk_right_injective xâŸ© := by
  ext âŸ¨x, yâŸ©; simp [mem_interedges_iff]

theorem interedges_biUnion_left (s : Finset Î¹) (t : Finset Î²) (f : Î¹ â†’ Finset Î±) :
    interedges r (s.biUnion f) t = s.biUnion fun a â†¦ interedges r (f a) t := by
  ext
  simp only [mem_biUnion, mem_interedges_iff, exists_and_right, â† and_assoc]

theorem interedges_biUnion_right (s : Finset Î±) (t : Finset Î¹) (f : Î¹ â†’ Finset Î²) :
    interedges r s (t.biUnion f) = t.biUnion fun b â†¦ interedges r s (f b) := by
  ext a
  simp only [mem_interedges_iff, mem_biUnion]
  exact âŸ¨fun âŸ¨xâ‚, âŸ¨xâ‚‚, xâ‚ƒ, xâ‚„âŸ©, xâ‚…âŸ© â†¦ âŸ¨xâ‚‚, xâ‚ƒ, xâ‚, xâ‚„, xâ‚…âŸ©,
    fun âŸ¨xâ‚‚, xâ‚ƒ, xâ‚, xâ‚„, xâ‚…âŸ© â†¦ âŸ¨xâ‚, âŸ¨xâ‚‚, xâ‚ƒ, xâ‚„âŸ©, xâ‚…âŸ©âŸ©

theorem interedges_biUnion (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Finset Î±) (g : Îº â†’ Finset Î²) :
    interedges r (s.biUnion f) (t.biUnion g) =
      (s Ã—Ë¢ t).biUnion fun ab â†¦ interedges r (f ab.1) (g ab.2) := by
  simp_rw [product_biUnion, interedges_biUnion_left, interedges_biUnion_right]

end DecidableEq

theorem card_interedges_le_mul (s : Finset Î±) (t : Finset Î²) :
    #(interedges r s t) â‰¤ #s * #t :=
  (card_filter_le _ _).trans (card_product _ _).le

theorem edgeDensity_nonneg (s : Finset Î±) (t : Finset Î²) : 0 â‰¤ edgeDensity r s t := by
  apply div_nonneg <;> exact mod_cast Nat.zero_le _

theorem edgeDensity_le_one (s : Finset Î±) (t : Finset Î²) : edgeDensity r s t â‰¤ 1 := by
  apply div_le_one_of_leâ‚€
  Â· exact mod_cast card_interedges_le_mul r s t
  Â· exact mod_cast Nat.zero_le _

theorem edgeDensity_add_edgeDensity_compl (hs : s.Nonempty) (ht : t.Nonempty) :
    edgeDensity r s t + edgeDensity (fun x y â†¦ Â¬r x y) s t = 1 := by
  rw [edgeDensity, edgeDensity, div_add_div_same, div_eq_one_iff_eq]
  Â· exact mod_cast card_interedges_add_card_interedges_compl r s t
  Â· exact mod_cast (mul_pos hs.card_pos ht.card_pos).ne'

@[simp]
theorem edgeDensity_empty_left (t : Finset Î²) : edgeDensity r âˆ… t = 0 := by
  rw [edgeDensity, Finset.card_empty, Nat.cast_zero, zero_mul, div_zero]

@[simp]
theorem edgeDensity_empty_right (s : Finset Î±) : edgeDensity r s âˆ… = 0 := by
  rw [edgeDensity, Finset.card_empty, Nat.cast_zero, mul_zero, div_zero]

theorem card_interedges_finpartition_left [DecidableEq Î±] (P : Finpartition s) (t : Finset Î²) :
    #(interedges r s t) = âˆ‘ a âˆˆ P.parts, #(interedges r a t) := by
  classical
  simp_rw [â† P.biUnion_parts, interedges_biUnion_left, id]
  rw [card_biUnion]
  exact fun x hx y hy h â†¦ interedges_disjoint_left r (P.disjoint hx hy h) _

theorem card_interedges_finpartition_right [DecidableEq Î²] (s : Finset Î±) (P : Finpartition t) :
    #(interedges r s t) = âˆ‘ b âˆˆ P.parts, #(interedges r s b) := by
  classical
  simp_rw [â† P.biUnion_parts, interedges_biUnion_right, id]
  rw [card_biUnion]
  exact fun x hx y hy h â†¦ interedges_disjoint_right r _ (P.disjoint hx hy h)

theorem card_interedges_finpartition [DecidableEq Î±] [DecidableEq Î²] (P : Finpartition s)
    (Q : Finpartition t) :
    #(interedges r s t) = âˆ‘ ab âˆˆ P.parts Ã—Ë¢ Q.parts, #(interedges r ab.1 ab.2) := by
  rw [card_interedges_finpartition_left _ P, sum_product]
  congr; ext
  rw [card_interedges_finpartition_right]

theorem mul_edgeDensity_le_edgeDensity (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) (hsâ‚‚ : sâ‚‚.Nonempty)
    (htâ‚‚ : tâ‚‚.Nonempty) :
    (#sâ‚‚ : â„š) / #sâ‚ * (#tâ‚‚ / #tâ‚) * edgeDensity r sâ‚‚ tâ‚‚ â‰¤ edgeDensity r sâ‚ tâ‚ := by
  have hst : (#sâ‚‚ : â„š) * #tâ‚‚ â‰  0 := by simp [hsâ‚‚.ne_empty, htâ‚‚.ne_empty]
  rw [edgeDensity, edgeDensity, div_mul_div_comm, mul_comm, div_mul_div_cancelâ‚€ hst]
  gcongr
  exact interedges_mono hs ht

theorem edgeDensity_sub_edgeDensity_le_one_sub_mul (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) (hsâ‚‚ : sâ‚‚.Nonempty)
    (htâ‚‚ : tâ‚‚.Nonempty) :
    edgeDensity r sâ‚‚ tâ‚‚ - edgeDensity r sâ‚ tâ‚ â‰¤ 1 - #sâ‚‚ / #sâ‚ * (#tâ‚‚ / #tâ‚) := by
  refine (sub_le_sub_left (mul_edgeDensity_le_edgeDensity r hs ht hsâ‚‚ htâ‚‚) _).trans ?_
  refine le_trans ?_ (mul_le_of_le_one_right ?_ (edgeDensity_le_one r sâ‚‚ tâ‚‚))
  Â· rw [sub_mul, one_mul]
  refine sub_nonneg_of_le (mul_le_oneâ‚€ ?_ ?_ ?_)
  Â· exact div_le_one_of_leâ‚€ ((@Nat.cast_le â„š).2 (card_le_card hs)) (Nat.cast_nonneg _)
  Â· apply div_nonneg <;> exact mod_cast Nat.zero_le _
  Â· exact div_le_one_of_leâ‚€ ((@Nat.cast_le â„š).2 (card_le_card ht)) (Nat.cast_nonneg _)

theorem abs_edgeDensity_sub_edgeDensity_le_one_sub_mul (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚)
    (hsâ‚‚ : sâ‚‚.Nonempty) (htâ‚‚ : tâ‚‚.Nonempty) :
    |edgeDensity r sâ‚‚ tâ‚‚ - edgeDensity r sâ‚ tâ‚| â‰¤ 1 - #sâ‚‚ / #sâ‚ * (#tâ‚‚ / #tâ‚) := by
  refine abs_sub_le_iff.2 âŸ¨edgeDensity_sub_edgeDensity_le_one_sub_mul r hs ht hsâ‚‚ htâ‚‚, ?_âŸ©
  rw [â† add_sub_cancel_right (edgeDensity r sâ‚ tâ‚) (edgeDensity (fun x y â†¦ Â¬r x y) sâ‚ tâ‚),
    â† add_sub_cancel_right (edgeDensity r sâ‚‚ tâ‚‚) (edgeDensity (fun x y â†¦ Â¬r x y) sâ‚‚ tâ‚‚),
    edgeDensity_add_edgeDensity_compl _ (hsâ‚‚.mono hs) (htâ‚‚.mono ht),
    edgeDensity_add_edgeDensity_compl _ hsâ‚‚ htâ‚‚, sub_sub_sub_cancel_left]
  exact edgeDensity_sub_edgeDensity_le_one_sub_mul _ hs ht hsâ‚‚ htâ‚‚

theorem abs_edgeDensity_sub_edgeDensity_le_two_mul_sub_sq (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚)
    (hÎ´â‚€ : 0 â‰¤ Î´) (hÎ´â‚ : Î´ < 1) (hsâ‚‚ : (1 - Î´) * #sâ‚ â‰¤ #sâ‚‚)
    (htâ‚‚ : (1 - Î´) * #tâ‚ â‰¤ #tâ‚‚) :
    |(edgeDensity r sâ‚‚ tâ‚‚ : ğ•œ) - edgeDensity r sâ‚ tâ‚| â‰¤ 2 * Î´ - Î´ ^ 2 := by
  have hÎ´' : 0 â‰¤ 2 * Î´ - Î´ ^ 2 := by
    rw [sub_nonneg, sq]
    gcongr
    exact hÎ´â‚.le.trans (by norm_num)
  rw [â† sub_pos] at hÎ´â‚
  obtain rfl | hsâ‚‚' := sâ‚‚.eq_empty_or_nonempty
  Â· rw [Finset.card_empty, Nat.cast_zero] at hsâ‚‚
    simpa [edgeDensity, (nonpos_of_mul_nonpos_right hsâ‚‚ hÎ´â‚).antisymm (Nat.cast_nonneg _)] using hÎ´'
  obtain rfl | htâ‚‚' := tâ‚‚.eq_empty_or_nonempty
  Â· rw [Finset.card_empty, Nat.cast_zero] at htâ‚‚
    simpa [edgeDensity, (nonpos_of_mul_nonpos_right htâ‚‚ hÎ´â‚).antisymm (Nat.cast_nonneg _)] using hÎ´'
  have hr : 2 * Î´ - Î´ ^ 2 = 1 - (1 - Î´) * (1 - Î´) := by ring
  rw [hr]
  norm_cast
  refine
    (Rat.cast_le.2 <| abs_edgeDensity_sub_edgeDensity_le_one_sub_mul r hs ht hsâ‚‚' htâ‚‚').trans ?_
  push_cast
  have hâ‚ := hsâ‚‚'.mono hs
  have hâ‚‚ := htâ‚‚'.mono ht
  gcongr
  Â· refine (le_div_iffâ‚€ ?_).2 hsâ‚‚
    exact mod_cast hâ‚.card_pos
  Â· refine (le_div_iffâ‚€ ?_).2 htâ‚‚
    exact mod_cast hâ‚‚.card_pos

/-- If `sâ‚‚ âŠ† sâ‚`, `tâ‚‚ âŠ† tâ‚` and they take up all but a `Î´`-proportion, then the difference in edge
densities is at most `2 * Î´`. -/
theorem abs_edgeDensity_sub_edgeDensity_le_two_mul (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) (hÎ´ : 0 â‰¤ Î´)
    (hscard : (1 - Î´) * #sâ‚ â‰¤ #sâ‚‚) (htcard : (1 - Î´) * #tâ‚ â‰¤ #tâ‚‚) :
    |(edgeDensity r sâ‚‚ tâ‚‚ : ğ•œ) - edgeDensity r sâ‚ tâ‚| â‰¤ 2 * Î´ := by
  rcases lt_or_ge Î´ 1 with h | h
  Â· exact (abs_edgeDensity_sub_edgeDensity_le_two_mul_sub_sq r hs ht hÎ´ h hscard htcard).trans
      ((sub_le_self_iff _).2 <| sq_nonneg Î´)
  rw [two_mul]
  refine (abs_sub _ _).trans (add_le_add (le_trans ?_ h) (le_trans ?_ h)) <;>
    Â· rw [abs_of_nonneg]
      Â· exact mod_cast edgeDensity_le_one r _ _
      Â· exact mod_cast edgeDensity_nonneg r _ _

end Asymmetric

section Symmetric

variable {r : Î± â†’ Î± â†’ Prop} [DecidableRel r] {s t : Finset Î±} {a b : Î±}

@[simp]
theorem swap_mem_interedges_iff (hr : Symmetric r) {x : Î± Ã— Î±} :
    x.swap âˆˆ interedges r s t â†” x âˆˆ interedges r t s := by
  rw [mem_interedges_iff, mem_interedges_iff, hr.iff]
  exact and_left_comm

theorem mk_mem_interedges_comm (hr : Symmetric r) :
    (a, b) âˆˆ interedges r s t â†” (b, a) âˆˆ interedges r t s :=
  @swap_mem_interedges_iff _ _ _ _ _ hr (b, a)

theorem card_interedges_comm (hr : Symmetric r) (s t : Finset Î±) :
    #(interedges r s t) = #(interedges r t s) :=
  Finset.card_bij (fun (x : Î± Ã— Î±) _ â†¦ x.swap) (fun _ â†¦ (swap_mem_interedges_iff hr).2)
    (fun _ _ _ _ h â†¦ Prod.swap_injective h) fun x h â†¦
    âŸ¨x.swap, (swap_mem_interedges_iff hr).2 h, x.swap_swapâŸ©

theorem edgeDensity_comm (hr : Symmetric r) (s t : Finset Î±) :
    edgeDensity r s t = edgeDensity r t s := by
  rw [edgeDensity, mul_comm, card_interedges_comm hr, edgeDensity]

end Symmetric

end Rel

open Rel

/-! ### Density of a graph -/


namespace SimpleGraph

variable (G : SimpleGraph Î±) [DecidableRel G.Adj] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : Finset Î±} {a b : Î±}

/-- Finset of edges of a relation between two finsets of vertices. -/
def interedges (s t : Finset Î±) : Finset (Î± Ã— Î±) :=
  Rel.interedges G.Adj s t

/-- Density of edges of a graph between two finsets of vertices. -/
def edgeDensity : Finset Î± â†’ Finset Î± â†’ â„š :=
  Rel.edgeDensity G.Adj

lemma interedges_def (s t : Finset Î±) : G.interedges s t = {e âˆˆ s Ã—Ë¢ t | G.Adj e.1 e.2} := rfl

lemma edgeDensity_def (s t : Finset Î±) : G.edgeDensity s t = #(G.interedges s t) / (#s * #t) := rfl

theorem card_interedges_div_card (s t : Finset Î±) :
    (#(G.interedges s t) : â„š) / (#s * #t) = G.edgeDensity s t :=
  rfl

theorem mem_interedges_iff {x : Î± Ã— Î±} : x âˆˆ G.interedges s t â†” x.1 âˆˆ s âˆ§ x.2 âˆˆ t âˆ§ G.Adj x.1 x.2 :=
  Rel.mem_interedges_iff

theorem mk_mem_interedges_iff : (a, b) âˆˆ G.interedges s t â†” a âˆˆ s âˆ§ b âˆˆ t âˆ§ G.Adj a b :=
  Rel.mk_mem_interedges_iff

@[simp]
theorem interedges_empty_left (t : Finset Î±) : G.interedges âˆ… t = âˆ… :=
  Rel.interedges_empty_left _

theorem interedges_mono : sâ‚‚ âŠ† sâ‚ â†’ tâ‚‚ âŠ† tâ‚ â†’ G.interedges sâ‚‚ tâ‚‚ âŠ† G.interedges sâ‚ tâ‚ :=
  Rel.interedges_mono

theorem interedges_disjoint_left (hs : Disjoint sâ‚ sâ‚‚) (t : Finset Î±) :
    Disjoint (G.interedges sâ‚ t) (G.interedges sâ‚‚ t) :=
  Rel.interedges_disjoint_left _ hs _

theorem interedges_disjoint_right (s : Finset Î±) (ht : Disjoint tâ‚ tâ‚‚) :
    Disjoint (G.interedges s tâ‚) (G.interedges s tâ‚‚) :=
  Rel.interedges_disjoint_right _ _ ht

section DecidableEq

variable [DecidableEq Î±]

theorem interedges_biUnion_left (s : Finset Î¹) (t : Finset Î±) (f : Î¹ â†’ Finset Î±) :
    G.interedges (s.biUnion f) t = s.biUnion fun a â†¦ G.interedges (f a) t :=
  Rel.interedges_biUnion_left _ _ _ _

theorem interedges_biUnion_right (s : Finset Î±) (t : Finset Î¹) (f : Î¹ â†’ Finset Î±) :
    G.interedges s (t.biUnion f) = t.biUnion fun b â†¦ G.interedges s (f b) :=
  Rel.interedges_biUnion_right _ _ _ _

theorem interedges_biUnion (s : Finset Î¹) (t : Finset Îº) (f : Î¹ â†’ Finset Î±) (g : Îº â†’ Finset Î±) :
    G.interedges (s.biUnion f) (t.biUnion g) =
      (s Ã—Ë¢ t).biUnion fun ab â†¦ G.interedges (f ab.1) (g ab.2) :=
  Rel.interedges_biUnion _ _ _ _ _

theorem card_interedges_add_card_interedges_compl (h : Disjoint s t) :
    #(G.interedges s t) + #(Gá¶œ.interedges s t) = #s * #t := by
  rw [â† card_product, interedges_def, interedges_def]
  have : {e âˆˆ s Ã—Ë¢ t | Gá¶œ.Adj e.1 e.2} = {e âˆˆ s Ã—Ë¢ t | Â¬G.Adj e.1 e.2} := by
    refine filter_congr fun x hx â†¦ ?_
    rw [mem_product] at hx
    rw [compl_adj, and_iff_right (h.forall_ne_finset hx.1 hx.2)]
  rw [this, â† card_union_of_disjoint, filter_union_filter_neg_eq]
  exact disjoint_filter.2 fun _ _ â†¦ Classical.not_not.2

theorem edgeDensity_add_edgeDensity_compl (hs : s.Nonempty) (ht : t.Nonempty) (h : Disjoint s t) :
    G.edgeDensity s t + Gá¶œ.edgeDensity s t = 1 := by
  rw [edgeDensity_def, edgeDensity_def, div_add_div_same, div_eq_one_iff_eq]
  Â· exact mod_cast card_interedges_add_card_interedges_compl _ h
  Â· positivity

end DecidableEq

theorem card_interedges_le_mul (s t : Finset Î±) : #(G.interedges s t) â‰¤ #s * #t :=
  Rel.card_interedges_le_mul _ _ _

theorem edgeDensity_nonneg (s t : Finset Î±) : 0 â‰¤ G.edgeDensity s t :=
  Rel.edgeDensity_nonneg _ _ _

theorem edgeDensity_le_one (s t : Finset Î±) : G.edgeDensity s t â‰¤ 1 :=
  Rel.edgeDensity_le_one _ _ _

@[simp]
theorem edgeDensity_empty_left (t : Finset Î±) : G.edgeDensity âˆ… t = 0 :=
  Rel.edgeDensity_empty_left _ _

@[simp]
theorem edgeDensity_empty_right (s : Finset Î±) : G.edgeDensity s âˆ… = 0 :=
  Rel.edgeDensity_empty_right _ _

@[simp]
theorem swap_mem_interedges_iff {x : Î± Ã— Î±} : x.swap âˆˆ G.interedges s t â†” x âˆˆ G.interedges t s :=
  Rel.swap_mem_interedges_iff G.symm

theorem mk_mem_interedges_comm : (a, b) âˆˆ G.interedges s t â†” (b, a) âˆˆ G.interedges t s :=
  Rel.mk_mem_interedges_comm G.symm

theorem edgeDensity_comm (s t : Finset Î±) : G.edgeDensity s t = G.edgeDensity t s :=
  Rel.edgeDensity_comm G.symm s t

end SimpleGraph

/- Porting note: Commented out `Tactic` namespace.
namespace Tactic

open Positivity

/-- Extension for the `positivity` tactic: `Rel.edgeDensity` and `SimpleGraph.edgeDensity` are
always nonnegative. -/
@[positivity]
unsafe def positivity_edge_density : expr â†’ tactic strictness
  | q(Rel.edgeDensity $(r) $(s) $(t)) =>
    nonnegative <$> mk_mapp `` Rel.edgeDensity_nonneg [none, none, r, none, s, t]
  | q(SimpleGraph.edgeDensity $(G) $(s) $(t)) =>
    nonnegative <$> mk_mapp `` SimpleGraph.edgeDensity_nonneg [none, G, none, s, t]
  | e =>
    pp e >>=
      fail âˆ˜
        format.bracket "The expression `"
          "` isn't of the form `Rel.edgeDensity r s t` nor `SimpleGraph.edgeDensity G s t`"

end Tactic
-/
