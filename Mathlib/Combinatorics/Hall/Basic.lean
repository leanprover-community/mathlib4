/-
Copyright (c) 2021 Alena Gusakov, Bhavik Mehta, Kyle Miller. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alena Gusakov, Bhavik Mehta, Kyle Miller
-/
import Mathlib.Combinatorics.Hall.Finite
import Mathlib.CategoryTheory.CofilteredSystem
import Mathlib.Data.Rel

#align_import combinatorics.hall.basic from "leanprover-community/mathlib"@"8195826f5c428fc283510bc67303dd4472d78498"

/-!
# Hall's Marriage Theorem

Given a list of finite subsets $X_1, X_2, \dots, X_n$ of some given set
$S$, P. Hall in [Hall1935] gave a necessary and sufficient condition for
there to be a list of distinct elements $x_1, x_2, \dots, x_n$ with
$x_i\in X_i$ for each $i$: it is when for each $k$, the union of every
$k$ of these subsets has at least $k$ elements.

Rather than a list of finite subsets, one may consider indexed families
`t : Î¹ â†’ Finset Î±` of finite subsets with `Î¹` a `Fintype`, and then the list
of distinct representatives is given by an injective function `f : Î¹ â†’ Î±`
such that `âˆ€ i, f i âˆˆ t i`, called a *matching*.
This version is formalized as `Finset.all_card_le_biUnion_card_iff_exists_injective'`
in a separate module.

The theorem can be generalized to remove the constraint that `Î¹` be a `Fintype`.
As observed in [Halpern1966], one may use the constrained version of the theorem
in a compactness argument to remove this constraint.
The formulation of compactness we use is that inverse limits of nonempty finite sets
are nonempty (`nonempty_sections_of_finite_inverse_system`), which uses the
Tychonoff theorem.
The core of this module is constructing the inverse system: for every finite subset `Î¹'` of
`Î¹`, we can consider the matchings on the restriction of the indexed family `t` to `Î¹'`.

## Main statements

* `Finset.all_card_le_biUnion_card_iff_exists_injective` is in terms of `t : Î¹ â†’ Finset Î±`.
* `Fintype.all_card_le_rel_image_card_iff_exists_injective` is in terms of a relation
  `r : Î± â†’ Î² â†’ Prop` such that `Rel.image r {a}` is a finite set for all `a : Î±`.
* `Fintype.all_card_le_filter_rel_iff_exists_injective` is in terms of a relation
  `r : Î± â†’ Î² â†’ Prop` on finite types, with the Hall condition given in terms of
  `finset.univ.filter`.

## Todo

* The statement of the theorem in terms of bipartite graphs is in preparation.

## Tags

Hall's Marriage Theorem, indexed families
-/


open Finset CategoryTheory

universe u v

/-- The set of matchings for `t` when restricted to a `Finset` of `Î¹`. -/
def hallMatchingsOn {Î¹ : Type u} {Î± : Type v} (t : Î¹ â†’ Finset Î±) (Î¹' : Finset Î¹) :=
  { f : Î¹' â†’ Î± | Function.Injective f âˆ§ âˆ€ x, f x âˆˆ t x }
#align hall_matchings_on hallMatchingsOn

/-- Given a matching on a finset, construct the restriction of that matching to a subset. -/
def hallMatchingsOn.restrict {Î¹ : Type u} {Î± : Type v} (t : Î¹ â†’ Finset Î±) {Î¹' Î¹'' : Finset Î¹}
    (h : Î¹' âŠ† Î¹'') (f : hallMatchingsOn t Î¹'') : hallMatchingsOn t Î¹' := by
  refine' âŸ¨fun i => f.val âŸ¨i, h i.propertyâŸ©, _âŸ©
  -- âŠ¢ (fun i => â†‘f { val := â†‘i, property := (_ : â†‘i âˆˆ Î¹'') }) âˆˆ hallMatchingsOn t Î¹'
  cases' f.property with hinj hc
  -- âŠ¢ (fun i => â†‘f { val := â†‘i, property := (_ : â†‘i âˆˆ Î¹'') }) âˆˆ hallMatchingsOn t Î¹'
  refine' âŸ¨_, fun i => hc âŸ¨i, h i.propertyâŸ©âŸ©
  -- âŠ¢ Function.Injective fun i => â†‘f { val := â†‘i, property := (_ : â†‘i âˆˆ Î¹'') }
  rintro âŸ¨i, hiâŸ© âŸ¨j, hjâŸ© hh
  -- âŠ¢ { val := i, property := hi } = { val := j, property := hj }
  simpa only [Subtype.mk_eq_mk] using hinj hh
  -- ðŸŽ‰ no goals
#align hall_matchings_on.restrict hallMatchingsOn.restrict

/-- When the Hall condition is satisfied, the set of matchings on a finite set is nonempty.
This is where `Finset.all_card_le_biUnion_card_iff_existsInjective'` comes into the argument. -/
theorem hallMatchingsOn.nonempty {Î¹ : Type u} {Î± : Type v} [DecidableEq Î±] (t : Î¹ â†’ Finset Î±)
    (h : âˆ€ s : Finset Î¹, s.card â‰¤ (s.biUnion t).card) (Î¹' : Finset Î¹) :
    Nonempty (hallMatchingsOn t Î¹') := by
  classical
    refine' âŸ¨Classical.indefiniteDescription _ _âŸ©
    apply (all_card_le_biUnion_card_iff_existsInjective' fun i : Î¹' => t i).mp
    intro s'
    convert h (s'.image (â†‘)) using 1
    simp only [card_image_of_injective s' Subtype.coe_injective]
    rw [image_biUnion]
#align hall_matchings_on.nonempty hallMatchingsOn.nonempty

/-- This is the `hallMatchingsOn` sets assembled into a directed system.
-/
def hallMatchingsFunctor {Î¹ : Type u} {Î± : Type v} (t : Î¹ â†’ Finset Î±) : (Finset Î¹)áµ’áµ– â¥¤ Type max u v
    where
  obj Î¹' := hallMatchingsOn t Î¹'.unop
  map {Î¹' Î¹''} g f := hallMatchingsOn.restrict t (CategoryTheory.leOfHom g.unop) f
#align hall_matchings_functor hallMatchingsFunctor

instance hallMatchingsOn.finite {Î¹ : Type u} {Î± : Type v} (t : Î¹ â†’ Finset Î±) (Î¹' : Finset Î¹) :
    Finite (hallMatchingsOn t Î¹') := by
  classical
    rw [hallMatchingsOn]
    let g : hallMatchingsOn t Î¹' â†’ Î¹' â†’ Î¹'.biUnion t := by
      rintro f i
      refine' âŸ¨f.val i, _âŸ©
      rw [mem_biUnion]
      exact âŸ¨i, i.property, f.property.2 iâŸ©
    apply Finite.of_injective g
    intro f f' h
    ext a
    rw [Function.funext_iff] at h
    simpa using h a
#align hall_matchings_on.finite hallMatchingsOn.finite

/-- This is the version of **Hall's Marriage Theorem** in terms of indexed
families of finite sets `t : Î¹ â†’ Finset Î±`.  It states that there is a
set of distinct representatives if and only if every union of `k` of the
sets has at least `k` elements.

Recall that `s.biUnion t` is the union of all the sets `t i` for `i âˆˆ s`.

This theorem is bootstrapped from `Finset.all_card_le_biUnion_card_iff_exists_injective'`,
which has the additional constraint that `Î¹` is a `Fintype`.
-/
theorem Finset.all_card_le_biUnion_card_iff_exists_injective {Î¹ : Type u} {Î± : Type v}
    [DecidableEq Î±] (t : Î¹ â†’ Finset Î±) :
    (âˆ€ s : Finset Î¹, s.card â‰¤ (s.biUnion t).card) â†”
      âˆƒ f : Î¹ â†’ Î±, Function.Injective f âˆ§ âˆ€ x, f x âˆˆ t x := by
  constructor
  -- âŠ¢ (âˆ€ (s : Finset Î¹), card s â‰¤ card (Finset.biUnion s t)) â†’ âˆƒ f, Function.Injec â€¦
  Â· intro h
    -- âŠ¢ âˆƒ f, Function.Injective f âˆ§ âˆ€ (x : Î¹), f x âˆˆ t x
    -- Set up the functor
    haveI : âˆ€ Î¹' : (Finset Î¹)áµ’áµ–, Nonempty ((hallMatchingsFunctor t).obj Î¹') := fun Î¹' =>
      hallMatchingsOn.nonempty t h Î¹'.unop
    classical
      haveI : âˆ€ Î¹' : (Finset Î¹)áµ’áµ–, Finite ((hallMatchingsFunctor t).obj Î¹') := by
        intro Î¹'
        rw [hallMatchingsFunctor]
        infer_instance
      -- Apply the compactness argument
      obtain âŸ¨u, huâŸ© := nonempty_sections_of_finite_inverse_system (hallMatchingsFunctor t)
      -- Interpret the resulting section of the inverse limit
      refine' âŸ¨_, _, _âŸ©
      Â·-- Build the matching function from the section
        exact fun i =>
          (u (Opposite.op ({i} : Finset Î¹))).val âŸ¨i, by simp only [Opposite.unop_op, mem_singleton]âŸ©
      Â· -- Show that it is injective
        intro i i'
        have subi : ({i} : Finset Î¹) âŠ† {i, i'} := by simp
        have subi' : ({i'} : Finset Î¹) âŠ† {i, i'} := by simp
        rw [â† Finset.le_iff_subset] at subi subi'
        simp only
        rw [â† hu (CategoryTheory.homOfLE subi).op, â† hu (CategoryTheory.homOfLE subi').op]
        let uii' := u (Opposite.op ({i, i'} : Finset Î¹))
        exact fun h => Subtype.mk_eq_mk.mp (uii'.property.1 h)
      Â· -- Show that it maps each index to the corresponding finite set
        intro i
        apply (u (Opposite.op ({i} : Finset Î¹))).property.2
  Â· -- The reverse direction is a straightforward cardinality argument
    rintro âŸ¨f, hfâ‚, hfâ‚‚âŸ© s
    -- âŠ¢ card s â‰¤ card (Finset.biUnion s t)
    rw [â† Finset.card_image_of_injective s hfâ‚]
    -- âŠ¢ card (image f s) â‰¤ card (Finset.biUnion s t)
    apply Finset.card_le_of_subset
    -- âŠ¢ image f s âŠ† Finset.biUnion s t
    intro
    -- âŠ¢ aâœ âˆˆ image f s â†’ aâœ âˆˆ Finset.biUnion s t
    rw [Finset.mem_image, Finset.mem_biUnion]
    -- âŠ¢ (âˆƒ a, a âˆˆ s âˆ§ f a = aâœ) â†’ âˆƒ a, a âˆˆ s âˆ§ aâœ âˆˆ t a
    rintro âŸ¨x, hx, rflâŸ©
    -- âŠ¢ âˆƒ a, a âˆˆ s âˆ§ f x âˆˆ t a
    exact âŸ¨x, hx, hfâ‚‚ xâŸ©
    -- ðŸŽ‰ no goals
#align finset.all_card_le_bUnion_card_iff_exists_injective Finset.all_card_le_biUnion_card_iff_exists_injective

/-- Given a relation such that the image of every singleton set is finite, then the image of every
finite set is finite. -/
instance {Î± : Type u} {Î² : Type v} [DecidableEq Î²] (r : Î± â†’ Î² â†’ Prop)
    [âˆ€ a : Î±, Fintype (Rel.image r {a})] (A : Finset Î±) : Fintype (Rel.image r A) := by
  have h : Rel.image r A = (A.biUnion fun a => (Rel.image r {a}).toFinset : Set Î²) := by
    ext
    -- Porting note: added `Set.mem_toFinset`
    simp [Rel.image, (Set.mem_toFinset)]
  rw [h]
  -- âŠ¢ Fintype â†‘â†‘(Finset.biUnion A fun a => Set.toFinset (Rel.image r {a}))
  apply FinsetCoe.fintype
  -- ðŸŽ‰ no goals

/-- This is a version of **Hall's Marriage Theorem** in terms of a relation
between types `Î±` and `Î²` such that `Î±` is finite and the image of
each `x : Î±` is finite (it suffices for `Î²` to be finite; see
`Fintype.all_card_le_filter_rel_iff_exists_injective`).  There is
a transversal of the relation (an injective function `Î± â†’ Î²` whose graph is
a subrelation of the relation) iff every subset of
`k` terms of `Î±` is related to at least `k` terms of `Î²`.

Note: if `[Fintype Î²]`, then there exist instances for `[âˆ€ (a : Î±), Fintype (Rel.image r {a})]`.
-/
theorem Fintype.all_card_le_rel_image_card_iff_exists_injective {Î± : Type u} {Î² : Type v}
    [DecidableEq Î²] (r : Î± â†’ Î² â†’ Prop) [âˆ€ a : Î±, Fintype (Rel.image r {a})] :
    (âˆ€ A : Finset Î±, A.card â‰¤ Fintype.card (Rel.image r A)) â†”
      âˆƒ f : Î± â†’ Î², Function.Injective f âˆ§ âˆ€ x, r x (f x) := by
  let r' a := (Rel.image r {a}).toFinset
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ card â†‘(Rel.image r â†‘A)) â†” âˆƒ f, Function.I â€¦
  have h : âˆ€ A : Finset Î±, Fintype.card (Rel.image r A) = (A.biUnion r').card := by
    intro A
    rw [â† Set.toFinset_card]
    apply congr_arg
    ext b
    -- Porting note: added `Set.mem_toFinset`
    simp [Rel.image, (Set.mem_toFinset)]
  -- Porting note: added `Set.mem_toFinset`
  have h' : âˆ€ (f : Î± â†’ Î²) (x), r x (f x) â†” f x âˆˆ r' x := by simp [Rel.image, (Set.mem_toFinset)]
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ card â†‘(Rel.image r â†‘A)) â†” âˆƒ f, Function.I â€¦
  simp only [h, h']
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ Finset.card (Finset.biUnion A fun a => Se â€¦
  apply Finset.all_card_le_biUnion_card_iff_exists_injective
  -- ðŸŽ‰ no goals
#align fintype.all_card_le_rel_image_card_iff_exists_injective Fintype.all_card_le_rel_image_card_iff_exists_injective

-- TODO: decidable_pred makes Yael sad. When an appropriate decidable_rel-like exists, fix it.
/-- This is a version of **Hall's Marriage Theorem** in terms of a relation to a finite type.
There is a transversal of the relation (an injective function `Î± â†’ Î²` whose graph is a subrelation
of the relation) iff every subset of `k` terms of `Î±` is related to at least `k` terms of `Î²`.

It is like `Fintype.all_card_le_rel_image_card_iff_exists_injective` but uses `Finset.filter`
rather than `Rel.image`.
-/
theorem Fintype.all_card_le_filter_rel_iff_exists_injective {Î± : Type u} {Î² : Type v} [Fintype Î²]
    (r : Î± â†’ Î² â†’ Prop) [âˆ€ a, DecidablePred (r a)] :
    (âˆ€ A : Finset Î±, A.card â‰¤ (univ.filter fun b : Î² => âˆƒ a âˆˆ A, r a b).card) â†”
      âˆƒ f : Î± â†’ Î², Function.Injective f âˆ§ âˆ€ x, r x (f x) := by
  haveI := Classical.decEq Î²
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ Finset.card (filter (fun b => âˆƒ a, a âˆˆ A  â€¦
  let r' a := univ.filter fun b => r a b
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ Finset.card (filter (fun b => âˆƒ a, a âˆˆ A  â€¦
  have h : âˆ€ A : Finset Î±, (univ.filter fun b : Î² => âˆƒ a âˆˆ A, r a b) = A.biUnion r' := by
    intro A
    ext b
    simp
  have h' : âˆ€ (f : Î± â†’ Î²) (x), r x (f x) â†” f x âˆˆ r' x := by simp
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ Finset.card (filter (fun b => âˆƒ a, a âˆˆ A  â€¦
  simp_rw [h, h']
  -- âŠ¢ (âˆ€ (A : Finset Î±), Finset.card A â‰¤ Finset.card (Finset.biUnion A fun a => fi â€¦
  apply Finset.all_card_le_biUnion_card_iff_exists_injective
  -- ðŸŽ‰ no goals
#align fintype.all_card_le_filter_rel_iff_exists_injective Fintype.all_card_le_filter_rel_iff_exists_injective
