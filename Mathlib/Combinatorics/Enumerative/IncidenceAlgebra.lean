/-
Copyright (c) 2022 Alex J. Best, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex J. Best, YaÃ«l Dillies
-/
import Mathlib.Algebra.Algebra.Defs
import Mathlib.Algebra.BigOperators.Ring.Finset
import Mathlib.Algebra.Module.BigOperators
import Mathlib.Algebra.Module.Pi
import Mathlib.Algebra.Order.BigOperators.Group.LocallyFinite

/-!
# Incidence algebras

Given a locally finite order `Î±` the incidence algebra over `Î±` is the type of functions from
non-empty intervals of `Î±` to some algebraic codomain.

This algebra has a natural multiplication operation whereby the product of two such functions
is defined on an interval by summing over all divisions into two subintervals the product of the
values of the original pair of functions.

This structure allows us to interpret many natural invariants of the intervals (such as their
cardinality) as elements of the incidence algebra. For instance the cardinality function, viewed as
an element of the incidence algebra, is simply the square of the function that takes constant value
one on all intervals. This constant function is called the zeta function, after
its connection with the Riemann zeta function.

The incidence algebra is a good setting for proving many inclusion-exclusion type principles, these
go under the name MÃ¶bius inversion, and are essentially due to the fact that the zeta function has
a multiplicative inverse in the incidence algebra, an inductively definable function called the
MÃ¶bius function that generalizes the MÃ¶bius function in number theory.

## Main definitions

* `1 : IncidenceAlgebra ğ•œ Î±` is the delta function, defined analogously to the identity matrix.
* `f * g` is the incidence algebra product, defined analogously to the matrix product.
* `IncidenceAlgebra.zeta` is the zeta function, defined analogously to the upper triangular matrix
  of ones.
* `IncidenceAlgebra.mu` is the inverse of the zeta function.

## Implementation notes

One has to define `mu` as either the left or right inverse of `zeta`. We define it as the left
inverse, and prove it is also a right inverse by defining `mu'` as the right inverse and using that
left and right inverses agree if they exist.

## TODOs

Here are some additions to this file that could be made in the future:
- Generalize the construction of `mu` to invert any element of the incidence algebra `f` which has
  `f x x` a unit for all `x`.
- Give formulas for higher powers of zeta.
- A formula for the mÃ¶bius function on a pi type similar to the one for products
- More examples / applications to different posets.
- Connection with Galois insertions
- Finsum version of MÃ¶bius inversion that holds even when an order doesn't have top/bot?
- Connect this theory to (infinite) matrices, giving maps of the incidence algebra to matrix rings
- Connect to the more advanced theory of arithmetic functions, and Dirichlet convolution.

## References

* [Aigner, *Combinatorial Theory, Chapter IV*][aigner1997]
* [Jacobson, *Basic Algebra I, 8.6*][jacobson1974]
* [Doubilet, Rota, Stanley, *On the foundations of Combinatorial Theory
  VI*][doubilet_rota_stanley_vi]
* [Spiegel, O'Donnell, *Incidence Algebras*][spiegel_odonnel1997]
* [Kung, Rota, Yan, *Combinatorics: The Rota Way, Chapter 3*][kung_rota_yan2009]
-/

open Finset OrderDual

variable {F ğ•œ ğ• ğ• Î± Î² : Type*}

/-- The `ğ•œ`-incidence algebra over `Î±`. -/
structure IncidenceAlgebra (ğ•œ Î± : Type*) [Zero ğ•œ] [LE Î±] where
  /-- The underlying function of an element of the incidence algebra.

  Do not use this function directly. Instead use the coercion coming from the `FunLike`
  instance. -/
  toFun : Î± â†’ Î± â†’ ğ•œ
  eq_zero_of_not_le' â¦ƒa b : Î±â¦„ : Â¬a â‰¤ b â†’ toFun a b = 0

namespace IncidenceAlgebra
section Zero
variable [Zero ğ•œ] [LE Î±] {a b : Î±}

instance instFunLike : FunLike (IncidenceAlgebra ğ•œ Î±) Î± (Î± â†’ ğ•œ) where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr

lemma apply_eq_zero_of_not_le (h : Â¬a â‰¤ b) (f : IncidenceAlgebra ğ•œ Î±) : f a b = 0 :=
  eq_zero_of_not_le' _ h

lemma le_of_ne_zero {f : IncidenceAlgebra ğ•œ Î±} : f a b â‰  0 â†’ a â‰¤ b :=
  not_imp_comm.1 fun h â†¦ apply_eq_zero_of_not_le h _

section Coes

-- this must come after the `FunLike` instance
initialize_simps_projections IncidenceAlgebra (toFun â†’ apply)

@[simp] lemma toFun_eq_coe (f : IncidenceAlgebra ğ•œ Î±) : f.toFun = f := rfl
@[simp, norm_cast] lemma coe_mk (f : Î± â†’ Î± â†’ ğ•œ) (h) : (mk f h : Î± â†’ Î± â†’ ğ•œ) = f := rfl

lemma coe_inj {f g : IncidenceAlgebra ğ•œ Î±} : (f : Î± â†’ Î± â†’ ğ•œ) = g â†” f = g :=
  DFunLike.coe_injective.eq_iff

@[ext]
lemma ext â¦ƒf g : IncidenceAlgebra ğ•œ Î±â¦„ (h : âˆ€ a b, a â‰¤ b â†’ f a b = g a b) : f = g := by
  refine DFunLike.coe_injective' (funextâ‚‚ fun a b â†¦ ?_)
  by_cases hab : a â‰¤ b
  Â· exact h _ _ hab
  Â· rw [apply_eq_zero_of_not_le hab, apply_eq_zero_of_not_le hab]

@[simp] lemma mk_coe (f : IncidenceAlgebra ğ•œ Î±) (h) : mk f h = f := rfl

end Coes

/-! ### Additive and multiplicative structure -/

instance instZero : Zero (IncidenceAlgebra ğ•œ Î±) := âŸ¨âŸ¨fun _ _ â†¦ 0, fun _ _ _ â†¦ rflâŸ©âŸ©
instance instInhabited : Inhabited (IncidenceAlgebra ğ•œ Î±) := âŸ¨0âŸ©

@[simp, norm_cast] lemma coe_zero : â‡‘(0 : IncidenceAlgebra ğ•œ Î±) = 0 := rfl
lemma zero_apply (a b : Î±) : (0 : IncidenceAlgebra ğ•œ Î±) a b = 0 := rfl

end Zero

section Add
variable [AddZeroClass ğ•œ] [LE Î±]

instance instAdd : Add (IncidenceAlgebra ğ•œ Î±) where
  add f g := âŸ¨f + g, fun a b h â†¦ by simp_rw [Pi.add_apply, apply_eq_zero_of_not_le h, zero_add]âŸ©

@[simp, norm_cast] lemma coe_add (f g : IncidenceAlgebra ğ•œ Î±) : â‡‘(f + g) = f + g := rfl
lemma add_apply (f g : IncidenceAlgebra ğ•œ Î±) (a b : Î±) : (f + g) a b = f a b + g a b := rfl

end Add

section Smul
variable {M : Type*} [Zero ğ•œ] [LE Î±] [SMulZeroClass M ğ•œ]

instance instSmulZeroClassRight : SMulZeroClass M (IncidenceAlgebra ğ•œ Î±) where
  smul c f :=
    âŸ¨c â€¢ â‡‘f, fun a b hab â†¦ by simp_rw [Pi.smul_apply, apply_eq_zero_of_not_le hab, smul_zero]âŸ©
  smul_zero c := by ext; exact smul_zero _

@[simp, norm_cast] lemma coe_constSMul (c : M) (f : IncidenceAlgebra ğ•œ Î±) : â‡‘(c â€¢ f) = c â€¢ â‡‘f := rfl

lemma constSMul_apply (c : M) (f : IncidenceAlgebra ğ•œ Î±) (a b : Î±) : (c â€¢ f) a b = c â€¢ f a b := rfl

end Smul

instance instAddMonoid [AddMonoid ğ•œ] [LE Î±] : AddMonoid (IncidenceAlgebra ğ•œ Î±) :=
  DFunLike.coe_injective.addMonoid _ coe_zero coe_add fun _ _ â†¦ rfl

instance instAddCommMonoid [AddCommMonoid ğ•œ] [LE Î±] : AddCommMonoid (IncidenceAlgebra ğ•œ Î±) :=
  DFunLike.coe_injective.addCommMonoid _ coe_zero coe_add fun _ _ â†¦ rfl

section AddGroup
variable [AddGroup ğ•œ] [LE Î±]

instance instNeg : Neg (IncidenceAlgebra ğ•œ Î±) where
  neg f := âŸ¨-f, fun a b h â†¦ by simp_rw [Pi.neg_apply, apply_eq_zero_of_not_le h, neg_zero]âŸ©

instance instSub : Sub (IncidenceAlgebra ğ•œ Î±) where
  sub f g := âŸ¨f - g, fun a b h â†¦ by simp_rw [Pi.sub_apply, apply_eq_zero_of_not_le h, sub_zero]âŸ©

@[simp, norm_cast] lemma coe_neg (f : IncidenceAlgebra ğ•œ Î±) : â‡‘(-f) = -f := rfl
@[simp, norm_cast] lemma coe_sub (f g : IncidenceAlgebra ğ•œ Î±) : â‡‘(f - g) = f - g := rfl
lemma neg_apply (f : IncidenceAlgebra ğ•œ Î±) (a b : Î±) : (-f) a b = -f a b := rfl
lemma sub_apply (f g : IncidenceAlgebra ğ•œ Î±) (a b : Î±) : (f - g) a b = f a b - g a b := rfl

instance instAddGroup : AddGroup (IncidenceAlgebra ğ•œ Î±) :=
  DFunLike.coe_injective.addGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ â†¦ rfl) fun _ _ â†¦ rfl

end AddGroup

instance instAddCommGroup [AddCommGroup ğ•œ] [LE Î±] : AddCommGroup (IncidenceAlgebra ğ•œ Î±) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ â†¦ rfl)
    fun _ _ â†¦ rfl

section One
variable [Preorder Î±] [DecidableEq Î±] [Zero ğ•œ] [One ğ•œ]

/-- The unit incidence algebra is the delta function, whose entries are `0` except on the diagonal
where they are `1`. -/
instance instOne : One (IncidenceAlgebra ğ•œ Î±) :=
  âŸ¨âŸ¨fun a b â†¦ if a = b then 1 else 0, fun _a _b h â†¦ ite_eq_right_iff.2 fun H â†¦ (h H.le).elimâŸ©âŸ©

@[simp] lemma one_apply (a b : Î±) : (1 : IncidenceAlgebra ğ•œ Î±) a b = if a = b then 1 else 0 := rfl

end One

section Mul
variable [Preorder Î±] [LocallyFiniteOrder Î±] [AddCommMonoid ğ•œ] [Mul ğ•œ]

/--
The multiplication operation in incidence algebras is defined on an interval by summing over
all divisions into two subintervals the product of the values of the original pair of functions.
-/
instance instMul : Mul (IncidenceAlgebra ğ•œ Î±) where
  mul f g :=
    âŸ¨fun a b â†¦ âˆ‘ x âˆˆ Icc a b, f a x * g x b, fun a b h â†¦ by dsimp; rw [Icc_eq_empty h, sum_empty]âŸ©

@[simp] lemma mul_apply (f g : IncidenceAlgebra ğ•œ Î±) (a b : Î±) :
    (f * g) a b = âˆ‘ x âˆˆ Icc a b, f a x * g x b := rfl

end Mul

instance instNonUnitalNonAssocSemiring [Preorder Î±] [LocallyFiniteOrder Î±]
    [NonUnitalNonAssocSemiring ğ•œ] : NonUnitalNonAssocSemiring (IncidenceAlgebra ğ•œ Î±) where
  __ := instAddCommMonoid
  mul := (Â· * Â·)
  zero := 0
  zero_mul := fun f â†¦ by ext; exact sum_eq_zero fun x _ â†¦ MulZeroClass.zero_mul _
  mul_zero := fun f â†¦ by ext; exact sum_eq_zero fun x _ â†¦ MulZeroClass.mul_zero _
  left_distrib := fun f g h â†¦ by
    ext; exact Eq.trans (sum_congr rfl fun x _ â†¦ left_distrib _ _ _) sum_add_distrib
  right_distrib := fun f g h â†¦ by
    ext; exact Eq.trans (sum_congr rfl fun x _ â†¦ right_distrib _ _ _) sum_add_distrib

instance instNonAssocSemiring [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]
    [NonAssocSemiring ğ•œ] : NonAssocSemiring (IncidenceAlgebra ğ•œ Î±) where
  __ := instNonUnitalNonAssocSemiring
  mul := (Â· * Â·)
  zero := 0
  one := 1
  one_mul := fun f â†¦ by ext; simp [*]
  mul_one := fun f â†¦ by ext; simp [*]

instance instSemiring [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±] [Semiring ğ•œ] :
    Semiring (IncidenceAlgebra ğ•œ Î±) where
  __ := instNonAssocSemiring
  mul := (Â· * Â·)
  mul_assoc f g h := by
    ext a b
    simp only [mul_apply, sum_mul, mul_sum, sum_sigma']
    apply sum_nbij' (fun âŸ¨a, bâŸ© â†¦ âŸ¨b, aâŸ©) (fun âŸ¨a, bâŸ© â†¦ âŸ¨b, aâŸ©) <;>
      aesop (add simp mul_assoc) (add unsafe le_trans)
  one := 1
  zero := 0

instance instRing [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±] [Ring ğ•œ] :
    Ring (IncidenceAlgebra ğ•œ Î±) where
  __ := instSemiring
  __ := instAddGroup

/-! ### Scalar multiplication between incidence algebras -/

section SMul
variable [Preorder Î±] [LocallyFiniteOrder Î±] [AddCommMonoid ğ•œ] [AddCommMonoid ğ•] [SMul ğ•œ ğ•]

instance instSMul : SMul (IncidenceAlgebra ğ•œ Î±) (IncidenceAlgebra ğ• Î±) :=
  âŸ¨fun f g â†¦
    âŸ¨fun a b â†¦ âˆ‘ x âˆˆ Icc a b, f a x â€¢ g x b, fun a b h â†¦ by dsimp; rw [Icc_eq_empty h, sum_empty]âŸ©âŸ©

@[simp]
lemma smul_apply (f : IncidenceAlgebra ğ•œ Î±) (g : IncidenceAlgebra ğ• Î±) (a b : Î±) :
    (f â€¢ g) a b = âˆ‘ x âˆˆ Icc a b, f a x â€¢ g x b :=
  rfl

end SMul

instance instIsScalarTower [Preorder Î±] [LocallyFiniteOrder Î±] [AddCommMonoid ğ•œ] [Monoid ğ•œ]
    [Semiring ğ•] [AddCommMonoid ğ•] [SMul ğ•œ ğ•] [Module ğ• ğ•] [DistribMulAction ğ•œ ğ•]
    [IsScalarTower ğ•œ ğ• ğ•] :
    IsScalarTower (IncidenceAlgebra ğ•œ Î±) (IncidenceAlgebra ğ• Î±) (IncidenceAlgebra ğ• Î±) where
  smul_assoc f g h := by
    ext a b
    simp only [smul_apply, sum_smul, smul_sum, sum_sigma']
    apply sum_nbij' (fun âŸ¨a, bâŸ© â†¦ âŸ¨b, aâŸ©) (fun âŸ¨a, bâŸ© â†¦ âŸ¨b, aâŸ©) <;> aesop (add unsafe le_trans)

instance [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±] [Semiring ğ•œ] [Semiring ğ•]
    [Module ğ•œ ğ•] : Module (IncidenceAlgebra ğ•œ Î±) (IncidenceAlgebra ğ• Î±) where
  smul := (Â· â€¢ Â·)
  one_smul f := by ext a b hab; simp [ite_smul, hab]
  mul_smul := smul_assoc
  smul_add f g h := by ext; exact Eq.trans (sum_congr rfl fun x _ â†¦ smul_add _ _ _) sum_add_distrib
  add_smul f g h := by ext; exact Eq.trans (sum_congr rfl fun x _ â†¦ add_smul _ _ _) sum_add_distrib
  zero_smul f := by ext; exact sum_eq_zero fun x _ â†¦ zero_smul _ _
  smul_zero f := by ext; exact sum_eq_zero fun x _ â†¦ smul_zero _

instance smulWithZeroRight [Zero ğ•œ] [Zero ğ•] [SMulWithZero ğ•œ ğ•] [LE Î±] :
    SMulWithZero ğ•œ (IncidenceAlgebra ğ• Î±) :=
  DFunLike.coe_injective.smulWithZero âŸ¨((â‡‘) : IncidenceAlgebra ğ• Î± â†’ Î± â†’ Î± â†’ ğ•), coe_zeroâŸ©
    coe_constSMul

instance moduleRight [Preorder Î±] [Semiring ğ•œ] [AddCommMonoid ğ•] [Module ğ•œ ğ•] :
    Module ğ•œ (IncidenceAlgebra ğ• Î±) :=
  DFunLike.coe_injective.module _ âŸ¨âŸ¨((â‡‘) : IncidenceAlgebra ğ• Î± â†’ Î± â†’ Î± â†’ ğ•), coe_zeroâŸ©, coe_addâŸ©
    coe_constSMul

instance algebraRight [PartialOrder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±] [CommSemiring ğ•œ]
    [CommSemiring ğ•] [Algebra ğ•œ ğ•] : Algebra ğ•œ (IncidenceAlgebra ğ• Î±) where
  algebraMap :=
  { toFun c := algebraMap ğ•œ ğ• c â€¢ (1 : IncidenceAlgebra ğ• Î±)
    map_one' := by
      ext; simp only [mul_boole, one_apply, Algebra.id.smul_eq_mul, constSMul_apply, map_one]
    map_mul' c d := by
        ext a b
        obtain rfl | h := eq_or_ne a b
        Â· simp only [one_apply, Algebra.id.smul_eq_mul, mul_apply,
            constSMul_apply, map_mul,
            eq_comm, Icc_self]
          simp
        Â· simp only [one_apply, mul_one, Algebra.id.smul_eq_mul,
            mul_apply, MulZeroClass.zero_mul, constSMul_apply,
            â† ite_and, ite_mul, mul_ite, map_mul,
            MulZeroClass.mul_zero, if_neg h]
          refine (sum_eq_zero fun x _ â†¦ ?_).symm
          exact if_neg fun hx â†¦ h <| hx.2.trans hx.1
    map_zero' := by rw [map_zero, zero_smul]
    map_add' c d := by rw [map_add, add_smul] }
  commutes' c f := by classical ext a b hab; simp [if_pos hab, constSMul_apply, mul_comm]
  smul_def' c f := by classical ext a b hab; simp [if_pos hab, constSMul_apply, Algebra.smul_def]

/-! ### The Lambda function -/

section Lambda
variable (ğ•œ) [Zero ğ•œ] [One ğ•œ] [Preorder Î±] [DecidableRel (Î± := Î±) (Â· â©¿ Â·)]

/-- The lambda function of the incidence algebra is the function that assigns `1` to every nonempty
interval of cardinality one or two. -/
@[simps]
def lambda : IncidenceAlgebra ğ•œ Î± :=
  âŸ¨fun a b â†¦ if a â©¿ b then 1 else 0, fun _a _b h â†¦ if_neg fun hh â†¦ h hh.leâŸ©

end Lambda

/-! ### The Zeta and MÃ¶bius functions -/

section Zeta
variable (ğ•œ) [Zero ğ•œ] [One ğ•œ] [LE Î±] [DecidableLE Î±] {a b : Î±}

/-- The zeta function of the incidence algebra is the function that assigns 1 to every nonempty
interval, convolution with this function sums functions over intervals. -/
def zeta : IncidenceAlgebra ğ•œ Î± := âŸ¨fun a b â†¦ if a â‰¤ b then 1 else 0, fun _a _b h â†¦ if_neg hâŸ©

variable {ğ•œ}

@[simp] lemma zeta_apply (a b : Î±) : zeta ğ•œ a b = if a â‰¤ b then 1 else 0 := rfl

lemma zeta_of_le (h : a â‰¤ b) : zeta ğ•œ a b = 1 := if_pos h

end Zeta

lemma zeta_mul_zeta [NonAssocSemiring ğ•œ] [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableLE Î±]
    (a b : Î±) : (zeta ğ•œ * zeta ğ•œ : IncidenceAlgebra ğ•œ Î±) a b = (Icc a b).card := by
  rw [mul_apply, card_eq_sum_ones, Nat.cast_sum, Nat.cast_one]
  refine sum_congr rfl fun x hx â†¦ ?_
  rw [mem_Icc] at hx
  rw [zeta_of_le hx.1, zeta_of_le hx.2, one_mul]

lemma zeta_mul_kappa [NonAssocSemiring ğ•œ] [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableLE Î±]
    (a b : Î±) : (zeta ğ•œ * zeta ğ•œ : IncidenceAlgebra ğ•œ Î±) a b = (Icc a b).card := by
  rw [mul_apply, card_eq_sum_ones, Nat.cast_sum, Nat.cast_one]
  refine sum_congr rfl fun x hx â†¦ ?_
  rw [mem_Icc] at hx
  rw [zeta_of_le hx.1, zeta_of_le hx.2, one_mul]

section Mu
variable (ğ•œ) [AddCommGroup ğ•œ] [One ğ•œ] [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]

/-- The MÃ¶bius function of the incidence algebra as a bare function defined recursively. -/
private def muFun (a : Î±) : Î± â†’ ğ•œ
  | b =>
    if a = b then 1
    else
      -âˆ‘ x âˆˆ (Ico a b).attach,
          let h := mem_Ico.1 x.2
          have : (Icc a x).card < (Icc a b).card :=
            card_lt_card (Icc_ssubset_Icc_right (h.1.trans h.2.le) le_rfl h.2)
          muFun a x
termination_by b => (Icc a b).card

private lemma muFun_apply (a b : Î±) :
    muFun ğ•œ a b = if a = b then 1 else -âˆ‘ x âˆˆ (Ico a b).attach, muFun ğ•œ a x := by rw [muFun]

/-- The MÃ¶bius function which inverts `zeta` as an element of the incidence algebra. -/
def mu : IncidenceAlgebra ğ•œ Î± :=
  âŸ¨muFun ğ•œ, fun a b â†¦ not_imp_comm.1 fun h â†¦ by
    rw [muFun_apply] at h
    split_ifs at h with hab
    Â· exact hab.le
    Â· rw [neg_eq_zero] at h
      obtain âŸ¨âŸ¨x, hxâŸ©, -âŸ© := exists_ne_zero_of_sum_ne_zero h
      exact (nonempty_Ico.1 âŸ¨x, hxâŸ©).leâŸ©

variable {ğ•œ} {a b : Î±}

lemma mu_apply (a b : Î±) : mu ğ•œ a b = if a = b then 1 else -âˆ‘ x âˆˆ Ico a b, mu ğ•œ a x := by
  rw [mu, coe_mk, muFun_apply, sum_attach]

@[simp] lemma mu_self (a : Î±) : mu ğ•œ a a = 1 := by simp [mu_apply]

lemma mu_eq_neg_sum_Ico_of_ne (hab : a â‰  b) :
    mu ğ•œ a b = -âˆ‘ x âˆˆ Ico a b, mu ğ•œ a x := by rw [mu_apply, if_neg hab]

variable (ğ•œ Î±)
/-- The Euler characteristic of a finite bounded order. -/
def eulerChar [BoundedOrder Î±] : ğ•œ := mu ğ•œ (âŠ¥ : Î±) âŠ¤

end Mu

section MuSpec
variable [AddCommGroup ğ•œ] [One ğ•œ] [PartialOrder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]

lemma sum_Icc_mu_right (a b : Î±) : âˆ‘ x âˆˆ Icc a b, mu ğ•œ a x = if a = b then 1 else 0 := by
  split_ifs with hab
  Â· simp [hab]
  by_cases hab : a â‰¤ b
  Â· simp [Icc_eq_cons_Ico hab, mu_eq_neg_sum_Ico_of_ne â€¹_â€º]
  Â· exact sum_eq_zero fun x hx â†¦ apply_eq_zero_of_not_le
      (fun hax â†¦ hab <| hax.trans (mem_Icc.1 hx).2) _

end MuSpec

section Mu'
variable (ğ•œ) [AddCommGroup ğ•œ] [One ğ•œ] [Preorder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]

/-- `mu'` as a bare function defined recursively. -/
private def muFun' (b : Î±) : Î± â†’ ğ•œ
  | a =>
    if a = b then 1
    else
      -âˆ‘ x âˆˆ (Ioc a b).attach,
          let h := mem_Ioc.1 x.2
          have : (Icc â†‘x b).card < (Icc a b).card :=
            card_lt_card (Icc_ssubset_Icc_left (h.1.le.trans h.2) h.1 le_rfl)
          muFun' b x
termination_by a => (Icc a b).card

private lemma muFun'_apply (a b : Î±) :
    muFun' ğ•œ b a = if a = b then 1 else -âˆ‘ x âˆˆ (Ioc a b).attach, muFun' ğ•œ b x := by
  rw [muFun']

/-- This is the reversed definition of `mu`, which is equal to `mu` but easiest to prove equal by
showing that `zeta * mu = 1` and `mu' * zeta = 1`. -/
private def mu' : IncidenceAlgebra ğ•œ Î± :=
  âŸ¨fun a b â†¦ muFun' ğ•œ b a, fun a b â†¦
    not_imp_comm.1 fun h â†¦ by
      dsimp only at h
      rw [muFun'_apply] at h
      split_ifs at h with hab
      Â· exact hab.le
      Â· rw [neg_eq_zero] at h
        obtain âŸ¨âŸ¨x, hxâŸ©, -âŸ© := exists_ne_zero_of_sum_ne_zero h
        exact (nonempty_Ioc.1 âŸ¨x, hxâŸ©).leâŸ©

variable {ğ•œ} {a b : Î±}

private lemma mu'_apply (a b : Î±) : mu' ğ•œ a b = if a = b then 1 else -âˆ‘ x âˆˆ Ioc a b, mu' ğ•œ x b := by
  rw [mu', coe_mk, muFun'_apply, sum_attach]

@[simp] private lemma mu'_apply_self (a : Î±) : mu' ğ•œ a a = 1 := by simp [mu'_apply]

private lemma mu'_eq_sum_Ioc_of_ne (h : a â‰  b) : mu' ğ•œ a b = -âˆ‘ x âˆˆ Ioc a b, mu' ğ•œ x b := by
  rw [mu'_apply, if_neg h]

end Mu'

section Mu'Spec
variable [AddCommGroup ğ•œ] [One ğ•œ] [PartialOrder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]

private lemma sum_Icc_mu'_left (a b : Î±) : âˆ‘ x âˆˆ Icc a b, mu' ğ•œ x b = if a = b then 1 else 0 := by
  split_ifs with hab
  Â· simp [hab]
  by_cases hab : a â‰¤ b
  Â· simp [Icc_eq_cons_Ioc hab, mu'_eq_sum_Ioc_of_ne â€¹_â€º]
  Â· exact sum_eq_zero fun x hx â†¦ apply_eq_zero_of_not_le
      (fun hxb â†¦ hab <| (mem_Icc.1 hx).1.trans hxb) _

end Mu'Spec

section MuZeta
variable (ğ•œ Î±) [AddCommGroup ğ•œ] [MulOneClass ğ•œ] [PartialOrder Î±] [LocallyFiniteOrder Î±]
  [DecidableEq Î±] [DecidableLE Î±]

lemma mu_mul_zeta : (mu ğ•œ * zeta ğ•œ : IncidenceAlgebra ğ•œ Î±) = 1 := by
  ext a b
  calc
    _ = âˆ‘ x âˆˆ Icc a b, mu ğ•œ a x := by rw [mul_apply]; congr! with x hx; simp [(mem_Icc.1 hx).2]
    _ = (1 : IncidenceAlgebra ğ•œ Î±) a b := sum_Icc_mu_right ..

private lemma zeta_mul_mu' : (zeta ğ•œ * mu' ğ•œ : IncidenceAlgebra ğ•œ Î±) = 1 := by
  ext a b
  calc
    _ = âˆ‘ x âˆˆ Icc a b, mu' ğ•œ x b := by rw [mul_apply]; congr! with x hx; simp [(mem_Icc.1 hx).1]
    _ = (1 : IncidenceAlgebra ğ•œ Î±) a b := sum_Icc_mu'_left ..

end MuZeta

section MuEqMu'
variable [Ring ğ•œ] [PartialOrder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±] {a b : Î±}

private lemma mu_eq_mu' : (mu ğ•œ : IncidenceAlgebra ğ•œ Î±) = mu' ğ•œ := by
  classical
  exact left_inv_eq_right_inv (mu_mul_zeta _ _) (zeta_mul_mu' _ _)

lemma mu_eq_neg_sum_Ioc_of_ne (hab : a â‰  b) : mu ğ•œ a b = -âˆ‘ x âˆˆ Ioc a b, mu ğ•œ x b := by
  rw [mu_eq_mu', mu'_eq_sum_Ioc_of_ne hab]

lemma zeta_mul_mu [DecidableLE Î±] : (zeta ğ•œ * mu ğ•œ : IncidenceAlgebra ğ•œ Î±) = 1 := by
  rw [mu_eq_mu', zeta_mul_mu']

lemma sum_Icc_mu_left (a b : Î±) : âˆ‘ x âˆˆ Icc a b, mu ğ•œ x b = if a = b then 1 else 0 := by
  rw [mu_eq_mu', sum_Icc_mu'_left]

end MuEqMu'

section OrderDual
variable (ğ•œ) [Ring ğ•œ] [PartialOrder Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]

@[simp]
lemma mu_toDual (a b : Î±) : mu ğ•œ (toDual a) (toDual b) = mu ğ•œ b a := by
  letI : DecidableLE Î± := Classical.decRel _
  let mud : IncidenceAlgebra ğ•œ Î±áµ’áµˆ :=
    { toFun := fun a b â†¦ mu ğ•œ (ofDual b) (ofDual a)
      eq_zero_of_not_le' := fun a b hab â†¦ apply_eq_zero_of_not_le (by exact hab) _ }
  suffices mu ğ•œ = mud by simp_rw [this, mud, coe_mk, ofDual_toDual]
  suffices mud * zeta ğ•œ = 1 by
    rw [â† mu_mul_zeta] at this
    apply_fun (Â· * mu ğ•œ) at this
    symm
    simpa [mul_assoc, zeta_mul_mu] using this
  clear a b
  ext a b
  simp only [mul_boole, one_apply, mul_apply, zeta_apply]
  calc
    âˆ‘ x âˆˆ Icc a b, (if x â‰¤ b then mud a x else 0) = âˆ‘ x âˆˆ Icc a b, mud a x := by
      congr! with x hx; exact if_pos (mem_Icc.1 hx).2
    _ = âˆ‘ x âˆˆ Icc (ofDual b) (ofDual a), mu ğ•œ x (ofDual a) := by simp [Icc_orderDual_def, mud]
    _ = if ofDual b = ofDual a then 1 else 0 := sum_Icc_mu_left ..
    _ = if a = b then 1 else 0 := by simp [eq_comm]

@[simp] lemma mu_ofDual (a b : Î±áµ’áµˆ) : mu ğ•œ (ofDual a) (ofDual b) = mu ğ•œ b a := (mu_toDual ..).symm

@[simp]
lemma eulerChar_orderDual [BoundedOrder Î±] : eulerChar ğ•œ Î±áµ’áµˆ = eulerChar ğ•œ Î± := by
  simp [eulerChar, â† mu_toDual ğ•œ (Î± := Î±)]

end OrderDual

section InversionTop
variable [Ring ğ•œ] [PartialOrder Î±] [OrderTop Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±] {a b : Î±}

/-- A general form of MÃ¶bius inversion. Based on lemma 2.1.2 of Incidence Algebras by Spiegel and
O'Donnell. -/
lemma moebius_inversion_top (f g : Î± â†’ ğ•œ) (h : âˆ€ x, g x = âˆ‘ y âˆˆ Ici x, f y) (x : Î±) :
    f x = âˆ‘ y âˆˆ Ici x, mu ğ•œ x y * g y := by
  letI : DecidableLE Î± := Classical.decRel _
  symm
  calc
    âˆ‘ y âˆˆ Ici x, mu ğ•œ x y * g y = âˆ‘ y âˆˆ Ici x, mu ğ•œ x y * âˆ‘ z âˆˆ Ici y, f z := by simp_rw [h]
    _ = âˆ‘ y âˆˆ Ici x, mu ğ•œ x y * âˆ‘ z âˆˆ Ici y, zeta ğ•œ y z * f z := by
      congr with y
      rw [sum_congr rfl fun z hz â†¦ ?_]
      rw [zeta_apply, if_pos (mem_Ici.mp â€¹_â€º), one_mul]
    _ = âˆ‘ y âˆˆ Ici x, âˆ‘ z âˆˆ Ici y, mu ğ•œ x y * zeta ğ•œ y z * f z := by simp [mul_sum]
    _ = âˆ‘ z âˆˆ Ici x, âˆ‘ y âˆˆ Icc x z, mu ğ•œ x y * zeta ğ•œ y z * f z := by
      rw [sum_sigma' (Ici x) fun y â†¦ Ici y]
      rw [sum_sigma' (Ici x) fun z â†¦ Icc x z]
      simp only [mul_boole, MulZeroClass.zero_mul, ite_mul, zeta_apply]
      apply sum_nbij' (fun âŸ¨a, bâŸ© â†¦ âŸ¨b, aâŸ©) (fun âŸ¨a, bâŸ© â†¦ âŸ¨b, aâŸ©) <;>
        aesop (add simp mul_assoc) (add unsafe le_trans)
    _ = âˆ‘ z âˆˆ Ici x, (mu ğ•œ * zeta ğ•œ : IncidenceAlgebra ğ•œ Î±) x z * f z := by
      simp_rw [mul_apply, sum_mul]
    _ = âˆ‘ y âˆˆ Ici x, âˆ‘ z âˆˆ Ici y, (1 : IncidenceAlgebra ğ•œ Î±) x z * f z := by
      simp only [mu_mul_zeta ğ•œ, one_apply, ite_mul, one_mul, zero_mul, sum_ite_eq, mem_Ici, le_refl,
        â†“reduceIte, â† add_sum_Ioi_eq_sum_Ici, left_eq_add]
      exact sum_eq_zero fun y hy â†¦ if_neg (mem_Ioi.mp hy).not_ge
    _ = f x := by
      simp only [one_apply, ite_mul, one_mul, zero_mul, sum_ite_eq, mem_Ici,
        â† add_sum_Ioi_eq_sum_Ici, le_refl, â†“reduceIte, add_eq_left]
      exact sum_eq_zero fun y hy â†¦ if_neg (mem_Ioi.mp hy).not_ge

end InversionTop

section InversionBot
variable [Ring ğ•œ] [PartialOrder Î±] [OrderBot Î±] [LocallyFiniteOrder Î±] [DecidableEq Î±]

/-- A general form of MÃ¶bius inversion. Based on lemma 2.1.3 of Incidence Algebras by Spiegel and
O'Donnell. -/
lemma moebius_inversion_bot (f g : Î± â†’ ğ•œ) (h : âˆ€ x, g x = âˆ‘ y âˆˆ Iic x, f y) (x : Î±) :
    f x = âˆ‘ y âˆˆ Iic x, mu ğ•œ y x * g y := by
  convert moebius_inversion_top (Î± := Î±áµ’áµˆ) f g h x using 3
  rw [â† mu_toDual]; rfl

end InversionBot

section Prod

section Preorder

section Ring
variable (ğ•œ) [Ring ğ•œ] [Preorder Î±] [Preorder Î²]

section DecidableLe
variable [DecidableLE Î±] [DecidableLE Î²]

lemma zeta_prod_apply (a b : Î± Ã— Î²) : zeta ğ•œ a b = zeta ğ•œ a.1 b.1 * zeta ğ•œ a.2 b.2 := by
  simp [â† ite_and, Prod.le_def, and_comm]

lemma zeta_prod_mk (aâ‚ aâ‚‚ : Î±) (bâ‚ bâ‚‚ : Î²) :
    zeta ğ•œ (aâ‚, bâ‚) (aâ‚‚, bâ‚‚) = zeta ğ•œ aâ‚ aâ‚‚ * zeta ğ•œ bâ‚ bâ‚‚ := zeta_prod_apply _ _ _

end DecidableLe

variable {ğ•œ} (f fâ‚ fâ‚‚ : IncidenceAlgebra ğ•œ Î±) (g gâ‚ gâ‚‚ : IncidenceAlgebra ğ•œ Î²)

/-- The cartesian product of two incidence algebras. -/
protected def prod : IncidenceAlgebra ğ•œ (Î± Ã— Î²) where
  toFun x y := f x.1 y.1 * g x.2 y.2
  eq_zero_of_not_le' x y hxy := by
    rw [Prod.le_def, not_and_or] at hxy
    obtain hxy | hxy := hxy <;> simp [apply_eq_zero_of_not_le hxy]

lemma prod_mk (aâ‚ aâ‚‚ : Î±) (bâ‚ bâ‚‚ : Î²) : f.prod g (aâ‚, bâ‚) (aâ‚‚, bâ‚‚) = f aâ‚ aâ‚‚ * g bâ‚ bâ‚‚ := rfl
@[simp] lemma prod_apply (x y : Î± Ã— Î²) : f.prod g x y = f x.1 y.1 * g x.2 y.2 := rfl

/-- This is a version of `IncidenceAlgebra.prod_mul_prod` that works over non-commutative rings. -/
lemma prod_mul_prod' [LocallyFiniteOrder Î±] [LocallyFiniteOrder Î²] [DecidableLE (Î± Ã— Î²)]
    (h : âˆ€ aâ‚ aâ‚‚ aâ‚ƒ bâ‚ bâ‚‚ bâ‚ƒ,
        fâ‚ aâ‚ aâ‚‚ * gâ‚ bâ‚ bâ‚‚ * (fâ‚‚ aâ‚‚ aâ‚ƒ * gâ‚‚ bâ‚‚ bâ‚ƒ) = fâ‚ aâ‚ aâ‚‚ * fâ‚‚ aâ‚‚ aâ‚ƒ * (gâ‚ bâ‚ bâ‚‚ * gâ‚‚ bâ‚‚ bâ‚ƒ)) :
    fâ‚.prod gâ‚ * fâ‚‚.prod gâ‚‚ = (fâ‚ * fâ‚‚).prod (gâ‚ * gâ‚‚) := by
  ext x y; simp [Icc_prod_def, sum_mul_sum, h, sum_product]

@[simp]
lemma one_prod_one [DecidableEq Î±] [DecidableEq Î²] :
    (.prod 1 1 : IncidenceAlgebra ğ•œ (Î± Ã— Î²)) = 1 := by
  ext x y; simp [Prod.ext_iff, â† ite_and, and_comm]

@[simp]
lemma zeta_prod_zeta [DecidableLE Î±] [DecidableLE Î²] :
    (zeta ğ•œ).prod (zeta ğ•œ) = (zeta ğ•œ : IncidenceAlgebra ğ•œ (Î± Ã— Î²)) := by
  ext x y hxy; simp [hxy, hxy.1, hxy.2]

end Ring

section CommRing
variable [CommRing ğ•œ] [Preorder Î±] [Preorder Î²] [LocallyFiniteOrder Î±] [LocallyFiniteOrder Î²]
  [DecidableLE (Î± Ã— Î²)] (fâ‚ fâ‚‚ : IncidenceAlgebra ğ•œ Î±) (gâ‚ gâ‚‚ : IncidenceAlgebra ğ•œ Î²)

@[simp]
lemma prod_mul_prod : fâ‚.prod gâ‚ * fâ‚‚.prod gâ‚‚ = (fâ‚ * fâ‚‚).prod (gâ‚ * gâ‚‚) :=
  prod_mul_prod' _ _ _ _ fun _ _ _ _ _ _ â†¦ mul_mul_mul_comm ..

end CommRing
end Preorder

section PartialOrder
variable (ğ•œ) [Ring ğ•œ] [PartialOrder Î±] [PartialOrder Î²] [LocallyFiniteOrder Î±]
  [LocallyFiniteOrder Î²] [DecidableEq Î±] [DecidableEq Î²] [DecidableLE Î±] [DecidableLE Î²]

/-- The MÃ¶bius function on a product order. Based on lemma 2.1.13 of Incidence Algebras by Spiegel
and O'Donnell. -/
@[simp]
lemma mu_prod_mu : (mu ğ•œ).prod (mu ğ•œ) = (mu ğ•œ : IncidenceAlgebra ğ•œ (Î± Ã— Î²)) := by
  refine left_inv_eq_right_inv ?_ zeta_mul_mu
  rw [â† zeta_prod_zeta, prod_mul_prod', mu_mul_zeta, mu_mul_zeta, one_prod_one]
  exact fun _ _ _ _ _ _ â†¦ Commute.mul_mul_mul_comm (by simp : _ = _) _ _

@[simp]
lemma eulerChar_prod [BoundedOrder Î±] [BoundedOrder Î²] :
    eulerChar ğ•œ (Î± Ã— Î²) = eulerChar ğ•œ Î± * eulerChar ğ•œ Î² := by simp [eulerChar, â† mu_prod_mu]

end PartialOrder
end Prod
end IncidenceAlgebra
