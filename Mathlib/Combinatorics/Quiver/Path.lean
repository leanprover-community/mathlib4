/-
Copyright (c) 2021 David W√§rn,. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn, Scott Morrison
-/
import Mathlib.Combinatorics.Quiver.Basic
import Mathlib.Logic.Lemmas

#align_import combinatorics.quiver.path from "leanprover-community/mathlib"@"18a5306c091183ac90884daa9373fa3b178e8607"

/-!
# Paths in quivers

Given a quiver `V`, we define the type of paths from `a : V` to `b : V` as an inductive
family. We define composition of paths and the action of prefunctors on paths.
-/

open Function

universe v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

namespace Quiver

/-- `Path a b` is the type of paths from `a` to `b` through the arrows of `G`. -/
inductive Path {V : Type u} [Quiver.{v} V] (a : V) : V ‚Üí Sort max (u + 1) v
  | nil : Path a a
  | cons : ‚àÄ {b c : V}, Path a b ‚Üí (b ‚ü∂ c) ‚Üí Path a c
#align quiver.path Quiver.Path

-- See issue lean4#2049
compile_inductive% Path

/-- An arrow viewed as a path of length one. -/
def Hom.toPath {V} [Quiver V] {a b : V} (e : a ‚ü∂ b) : Path a b :=
  Path.nil.cons e
#align quiver.hom.to_path Quiver.Hom.toPath

namespace Path

variable {V : Type u} [Quiver V] {a b c d : V}

lemma nil_ne_cons (p : Path a b) (e : b ‚ü∂ a) : Path.nil ‚â† p.cons e :=
  fun h => by injection h
              -- üéâ no goals
#align quiver.path.nil_ne_cons Quiver.Path.nil_ne_cons

lemma cons_ne_nil (p : Path a b) (e : b ‚ü∂ a) : p.cons e ‚â† Path.nil :=
  fun h => by injection h
              -- üéâ no goals
#align quiver.path.cons_ne_nil Quiver.Path.cons_ne_nil

lemma obj_eq_of_cons_eq_cons {p : Path a b} {p' : Path a c}
  {e : b ‚ü∂ d} {e' : c ‚ü∂ d} (h : p.cons e = p'.cons e') : b = c := by injection h
                                                                     -- üéâ no goals
#align quiver.path.obj_eq_of_cons_eq_cons Quiver.Path.obj_eq_of_cons_eq_cons

lemma heq_of_cons_eq_cons {p : Path a b} {p' : Path a c}
  {e : b ‚ü∂ d} {e' : c ‚ü∂ d} (h : p.cons e = p'.cons e') : HEq p p' := by injection h
                                                                        -- üéâ no goals
#align quiver.path.heq_of_cons_eq_cons Quiver.Path.heq_of_cons_eq_cons

lemma hom_heq_of_cons_eq_cons {p : Path a b} {p' : Path a c}
  {e : b ‚ü∂ d} {e' : c ‚ü∂ d} (h : p.cons e = p'.cons e') : HEq e e' := by injection h
                                                                        -- üéâ no goals
#align quiver.path.hom_heq_of_cons_eq_cons Quiver.Path.hom_heq_of_cons_eq_cons

/-- The length of a path is the number of arrows it uses. -/
def length {a : V} : ‚àÄ {b : V}, Path a b ‚Üí ‚Ñï
  | _, nil => 0
  | _, cons p _ => p.length + 1
#align quiver.path.length Quiver.Path.length

instance {a : V} : Inhabited (Path a a) :=
  ‚ü®nil‚ü©

@[simp]
theorem length_nil {a : V} : (nil : Path a a).length = 0 :=
  rfl
#align quiver.path.length_nil Quiver.Path.length_nil

@[simp]
theorem length_cons (a b c : V) (p : Path a b) (e : b ‚ü∂ c) : (p.cons e).length = p.length + 1 :=
  rfl
#align quiver.path.length_cons Quiver.Path.length_cons

theorem eq_of_length_zero (p : Path a b) (hzero : p.length = 0) : a = b := by
  cases p
  -- ‚ä¢ a = a
  ¬∑ rfl
    -- üéâ no goals
  ¬∑ cases Nat.succ_ne_zero _ hzero
    -- üéâ no goals
#align quiver.path.eq_of_length_zero Quiver.Path.eq_of_length_zero

/-- Composition of paths. -/
def comp {a b : V} : ‚àÄ {c}, Path a b ‚Üí Path b c ‚Üí Path a c
  | _, p, nil => p
  | _, p, cons q e => (p.comp q).cons e
#align quiver.path.comp Quiver.Path.comp

@[simp]
theorem comp_cons {a b c d : V} (p : Path a b) (q : Path b c) (e : c ‚ü∂ d) :
    p.comp (q.cons e) = (p.comp q).cons e :=
  rfl
#align quiver.path.comp_cons Quiver.Path.comp_cons

@[simp]
theorem comp_nil {a b : V} (p : Path a b) : p.comp Path.nil = p :=
  rfl
#align quiver.path.comp_nil Quiver.Path.comp_nil

@[simp]
theorem nil_comp {a : V} : ‚àÄ {b} (p : Path a b), Path.nil.comp p = p
  | _, nil => rfl
  | _, cons p _ => by rw [comp_cons, nil_comp p]
                      -- üéâ no goals
#align quiver.path.nil_comp Quiver.Path.nil_comp

@[simp]
theorem comp_assoc {a b c : V} :
    ‚àÄ {d} (p : Path a b) (q : Path b c) (r : Path c d), (p.comp q).comp r = p.comp (q.comp r)
  | _, _, _, nil => rfl
  | _, p, q, cons r _ => by rw [comp_cons, comp_cons, comp_cons, comp_assoc p q r]
                            -- üéâ no goals
#align quiver.path.comp_assoc Quiver.Path.comp_assoc

@[simp]
theorem length_comp (p : Path a b) : ‚àÄ {c} (q : Path b c), (p.comp q).length = p.length + q.length
  | _, nil => rfl
  | _, cons _ _ => congr_arg Nat.succ (length_comp _ _)
#align quiver.path.length_comp Quiver.Path.length_comp

theorem comp_inj {p‚ÇÅ p‚ÇÇ : Path a b} {q‚ÇÅ q‚ÇÇ : Path b c} (hq : q‚ÇÅ.length = q‚ÇÇ.length) :
    p‚ÇÅ.comp q‚ÇÅ = p‚ÇÇ.comp q‚ÇÇ ‚Üî p‚ÇÅ = p‚ÇÇ ‚àß q‚ÇÅ = q‚ÇÇ := by
  refine' ‚ü®fun h => _, by rintro ‚ü®rfl, rfl‚ü©; rfl‚ü©
  -- ‚ä¢ p‚ÇÅ = p‚ÇÇ ‚àß q‚ÇÅ = q‚ÇÇ
  induction' q‚ÇÅ with d‚ÇÅ e‚ÇÅ q‚ÇÅ f‚ÇÅ ih <;> obtain _ | ‚ü®q‚ÇÇ, f‚ÇÇ‚ü© := q‚ÇÇ
  -- ‚ä¢ p‚ÇÅ = p‚ÇÇ ‚àß nil = q‚ÇÇ
                                        -- ‚ä¢ p‚ÇÅ = p‚ÇÇ ‚àß nil = nil
                                        -- ‚ä¢ p‚ÇÅ = p‚ÇÇ ‚àß cons q‚ÇÅ f‚ÇÅ = nil
  ¬∑ exact ‚ü®h, rfl‚ü©
    -- üéâ no goals
  ¬∑ cases hq
    -- üéâ no goals
  ¬∑ cases hq
    -- üéâ no goals
  ¬∑ simp only [comp_cons, cons.injEq] at h
    -- ‚ä¢ p‚ÇÅ = p‚ÇÇ ‚àß cons q‚ÇÅ f‚ÇÅ = cons q‚ÇÇ f‚ÇÇ
    obtain rfl := h.1
    -- ‚ä¢ p‚ÇÅ = p‚ÇÇ ‚àß cons q‚ÇÅ f‚ÇÅ = cons q‚ÇÇ f‚ÇÇ
    obtain ‚ü®rfl, rfl‚ü© := ih (Nat.succ.inj hq) h.2.1.eq
    -- ‚ä¢ p‚ÇÅ = p‚ÇÅ ‚àß cons q‚ÇÅ f‚ÇÅ = cons q‚ÇÅ f‚ÇÇ
    rw [h.2.2.eq]
    -- ‚ä¢ p‚ÇÅ = p‚ÇÅ ‚àß cons q‚ÇÅ f‚ÇÇ = cons q‚ÇÅ f‚ÇÇ
    exact ‚ü®rfl, rfl‚ü©
    -- üéâ no goals
#align quiver.path.comp_inj Quiver.Path.comp_inj

theorem comp_inj' {p‚ÇÅ p‚ÇÇ : Path a b} {q‚ÇÅ q‚ÇÇ : Path b c} (h : p‚ÇÅ.length = p‚ÇÇ.length) :
    p‚ÇÅ.comp q‚ÇÅ = p‚ÇÇ.comp q‚ÇÇ ‚Üî p‚ÇÅ = p‚ÇÇ ‚àß q‚ÇÅ = q‚ÇÇ :=
  ‚ü®fun h_eq => (comp_inj <| Nat.add_left_cancel <| by simpa [h] using congr_arg length h_eq).1 h_eq,
                                                      -- üéâ no goals
   by rintro ‚ü®rfl, rfl‚ü©; rfl‚ü©
      -- ‚ä¢ comp p‚ÇÅ q‚ÇÅ = comp p‚ÇÅ q‚ÇÅ
                         -- üéâ no goals
#align quiver.path.comp_inj' Quiver.Path.comp_inj'

theorem comp_injective_left (q : Path b c) : Injective fun p : Path a b => p.comp q :=
  fun _ _ h => ((comp_inj rfl).1 h).1
#align quiver.path.comp_injective_left Quiver.Path.comp_injective_left

theorem comp_injective_right (p : Path a b) : Injective (p.comp : Path b c ‚Üí Path a c) :=
  fun _ _ h => ((comp_inj' rfl).1 h).2
#align quiver.path.comp_injective_right Quiver.Path.comp_injective_right

@[simp]
theorem comp_inj_left {p‚ÇÅ p‚ÇÇ : Path a b} {q : Path b c} : p‚ÇÅ.comp q = p‚ÇÇ.comp q ‚Üî p‚ÇÅ = p‚ÇÇ :=
  q.comp_injective_left.eq_iff
#align quiver.path.comp_inj_left Quiver.Path.comp_inj_left

@[simp]
theorem comp_inj_right {p : Path a b} {q‚ÇÅ q‚ÇÇ : Path b c} : p.comp q‚ÇÅ = p.comp q‚ÇÇ ‚Üî q‚ÇÅ = q‚ÇÇ :=
  p.comp_injective_right.eq_iff
#align quiver.path.comp_inj_right Quiver.Path.comp_inj_right

/-- Turn a path into a list. The list contains `a` at its head, but not `b` a priori. -/
@[simp]
def toList : ‚àÄ {b : V}, Path a b ‚Üí List V
  | _, nil => []
  | _, @cons _ _ _ c _ p _ => c :: p.toList
#align quiver.path.to_list Quiver.Path.toList

/-- `Quiver.Path.toList` is a contravariant functor. The inversion comes from `Quiver.Path` and
`List` having different preferred directions for adding elements. -/
@[simp]
theorem toList_comp (p : Path a b) : ‚àÄ {c} (q : Path b c), (p.comp q).toList = q.toList ++ p.toList
  | _, nil => by simp
                 -- üéâ no goals
  | _, @cons _ _ _ d _ q _ => by simp [toList_comp]
                                 -- üéâ no goals
#align quiver.path.to_list_comp Quiver.Path.toList_comp

theorem toList_chain_nonempty :
    ‚àÄ {b} (p : Path a b), p.toList.Chain (fun x y => Nonempty (y ‚ü∂ x)) b
  | _, nil => List.Chain.nil
  | _, cons p f => p.toList_chain_nonempty.cons ‚ü®f‚ü©
#align quiver.path.to_list_chain_nonempty Quiver.Path.toList_chain_nonempty

variable [‚àÄ a b : V, Subsingleton (a ‚ü∂ b)]

theorem toList_injective (a : V) : ‚àÄ b, Injective (toList : Path a b ‚Üí List V)
  | _, nil, nil, _ => rfl
  | _, nil, @cons _ _ _ c _ p f, h => by cases h
                                         -- üéâ no goals
  | _, @cons _ _ _ c _ p f, nil, h => by cases h
                                         -- üéâ no goals
  | _, @cons _ _ _ c _ p f, @cons _ _ _ t _ C D, h => by
    simp only [toList, List.cons.injEq] at h
    -- ‚ä¢ cons p f = cons C D
    obtain ‚ü®rfl, hAC‚ü© := h
    -- ‚ä¢ cons p f = cons C D
    simp [toList_injective _ _ hAC]
    -- üéâ no goals
#align quiver.path.to_list_injective Quiver.Path.toList_injective

@[simp]
theorem toList_inj {p q : Path a b} : p.toList = q.toList ‚Üî p = q :=
  (toList_injective _ _).eq_iff
#align quiver.path.to_list_inj Quiver.Path.toList_inj

end Path

end Quiver

namespace Prefunctor

open Quiver

variable {V : Type u‚ÇÅ} [Quiver.{v‚ÇÅ} V] {W : Type u‚ÇÇ} [Quiver.{v‚ÇÇ} W] (F : V ‚•§q W)

/-- The image of a path under a prefunctor. -/
def mapPath {a : V} : ‚àÄ {b : V}, Path a b ‚Üí Path (F.obj a) (F.obj b)
  | _, Path.nil => Path.nil
  | _, Path.cons p e => Path.cons (mapPath p) (F.map e)
#align prefunctor.map_path Prefunctor.mapPath

@[simp]
theorem mapPath_nil (a : V) : F.mapPath (Path.nil : Path a a) = Path.nil :=
  rfl
#align prefunctor.map_path_nil Prefunctor.mapPath_nil

@[simp]
theorem mapPath_cons {a b c : V} (p : Path a b) (e : b ‚ü∂ c) :
    F.mapPath (Path.cons p e) = Path.cons (F.mapPath p) (F.map e) :=
  rfl
#align prefunctor.map_path_cons Prefunctor.mapPath_cons

@[simp]
theorem mapPath_comp {a b : V} (p : Path a b) :
    ‚àÄ {c : V} (q : Path b c), F.mapPath (p.comp q) = (F.mapPath p).comp (F.mapPath q)
  | _, Path.nil => rfl
  | c, Path.cons q e => by dsimp; rw [mapPath_comp p q]
                           -- ‚ä¢ Path.cons (mapPath F (Path.comp p q)) (F.map e) = Path.cons (Path.comp (mapP ‚Ä¶
                                  -- üéâ no goals
#align prefunctor.map_path_comp Prefunctor.mapPath_comp

@[simp]
theorem mapPath_toPath {a b : V} (f : a ‚ü∂ b) : F.mapPath f.toPath = (F.map f).toPath :=
  rfl
#align prefunctor.map_path_to_path Prefunctor.mapPath_toPath

end Prefunctor
