/-
Copyright (c) 2021 David WÃ¤rn,. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David WÃ¤rn, Kim Morrison
-/
import Mathlib.Combinatorics.Quiver.Prefunctor
import Mathlib.Logic.Lemmas
import Batteries.Data.List.Basic

/-!
# Paths in quivers

Given a quiver `V`, we define the type of paths from `a : V` to `b : V` as an inductive
family. We define composition of paths and the action of prefunctors on paths.
-/

open Function

universe v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

namespace Quiver

/-- `Path a b` is the type of paths from `a` to `b` through the arrows of `G`. -/
inductive Path {V : Type u} [Quiver.{v} V] (a : V) : V â†’ Sort max (u + 1) v
  | nil : Path a a
  | cons : âˆ€ {b c : V}, Path a b â†’ (b âŸ¶ c) â†’ Path a c

-- See issue https://github.com/leanprover/lean4/issues/2049
compile_inductive% Path

/-- An arrow viewed as a path of length one. -/
def Hom.toPath {V} [Quiver V] {a b : V} (e : a âŸ¶ b) : Path a b :=
  Path.nil.cons e

namespace Path

variable {V : Type u} [Quiver V] {a b c d : V}

lemma nil_ne_cons (p : Path a b) (e : b âŸ¶ a) : Path.nil â‰  p.cons e :=
  fun h => by injection h

lemma cons_ne_nil (p : Path a b) (e : b âŸ¶ a) : p.cons e â‰  Path.nil :=
  fun h => by injection h

lemma obj_eq_of_cons_eq_cons {p : Path a b} {p' : Path a c}
    {e : b âŸ¶ d} {e' : c âŸ¶ d} (h : p.cons e = p'.cons e') : b = c := by injection h

lemma heq_of_cons_eq_cons {p : Path a b} {p' : Path a c}
    {e : b âŸ¶ d} {e' : c âŸ¶ d} (h : p.cons e = p'.cons e') : HEq p p' := by injection h

lemma hom_heq_of_cons_eq_cons {p : Path a b} {p' : Path a c}
    {e : b âŸ¶ d} {e' : c âŸ¶ d} (h : p.cons e = p'.cons e') : HEq e e' := by injection h

/-- The length of a path is the number of arrows it uses. -/
def length {a : V} : âˆ€ {b : V}, Path a b â†’ â„•
  | _, nil => 0
  | _, cons p _ => p.length + 1

instance {a : V} : Inhabited (Path a a) :=
  âŸ¨nilâŸ©

@[simp]
theorem length_nil {a : V} : (nil : Path a a).length = 0 :=
  rfl

@[simp]
theorem length_cons (a b c : V) (p : Path a b) (e : b âŸ¶ c) : (p.cons e).length = p.length + 1 :=
  rfl

theorem eq_of_length_zero (p : Path a b) (hzero : p.length = 0) : a = b := by
  cases p
  Â· rfl
  Â· cases Nat.succ_ne_zero _ hzero

theorem eq_nil_of_length_zero (p : Path a a) (hzero : p.length = 0) : p = nil := by
  cases p
  Â· rfl
  Â· simp at hzero

/-- Composition of paths. -/
def comp {a b : V} : âˆ€ {c}, Path a b â†’ Path b c â†’ Path a c
  | _, p, nil => p
  | _, p, cons q e => (p.comp q).cons e

@[simp]
theorem comp_cons {a b c d : V} (p : Path a b) (q : Path b c) (e : c âŸ¶ d) :
    p.comp (q.cons e) = (p.comp q).cons e :=
  rfl

@[simp]
theorem comp_nil {a b : V} (p : Path a b) : p.comp Path.nil = p :=
  rfl

@[simp]
theorem nil_comp {a : V} : âˆ€ {b} (p : Path a b), Path.nil.comp p = p
  | _, nil => rfl
  | _, cons p _ => by rw [comp_cons, nil_comp p]

@[simp]
theorem comp_assoc {a b c : V} :
    âˆ€ {d} (p : Path a b) (q : Path b c) (r : Path c d), (p.comp q).comp r = p.comp (q.comp r)
  | _, _, _, nil => rfl
  | _, p, q, cons r _ => by rw [comp_cons, comp_cons, comp_cons, comp_assoc p q r]

@[simp]
theorem length_comp (p : Path a b) : âˆ€ {c} (q : Path b c), (p.comp q).length = p.length + q.length
  | _, nil => rfl
  | _, cons _ _ => congr_arg Nat.succ (length_comp _ _)

theorem comp_inj {pâ‚ pâ‚‚ : Path a b} {qâ‚ qâ‚‚ : Path b c} (hq : qâ‚.length = qâ‚‚.length) :
    pâ‚.comp qâ‚ = pâ‚‚.comp qâ‚‚ â†” pâ‚ = pâ‚‚ âˆ§ qâ‚ = qâ‚‚ := by
  refine âŸ¨fun h => ?_, by rintro âŸ¨rfl, rflâŸ©; rflâŸ©
  induction qâ‚ with
  | nil =>
    rcases qâ‚‚ with _ | âŸ¨qâ‚‚, fâ‚‚âŸ©
    Â· exact âŸ¨h, rflâŸ©
    Â· cases hq
  | cons qâ‚ fâ‚ ih =>
    rcases qâ‚‚ with _ | âŸ¨qâ‚‚, fâ‚‚âŸ©
    Â· cases hq
    Â· simp only [comp_cons, cons.injEq] at h
      obtain rfl := h.1
      obtain âŸ¨rfl, rflâŸ© := ih (Nat.succ.inj hq) h.2.1.eq
      rw [h.2.2.eq]
      exact âŸ¨rfl, rflâŸ©

theorem comp_inj' {pâ‚ pâ‚‚ : Path a b} {qâ‚ qâ‚‚ : Path b c} (h : pâ‚.length = pâ‚‚.length) :
    pâ‚.comp qâ‚ = pâ‚‚.comp qâ‚‚ â†” pâ‚ = pâ‚‚ âˆ§ qâ‚ = qâ‚‚ :=
  âŸ¨fun h_eq => (comp_inj <| Nat.add_left_cancel (n := pâ‚‚.length) <|
    by simpa [h] using congr_arg length h_eq).1 h_eq,
   by rintro âŸ¨rfl, rflâŸ©; rflâŸ©

theorem comp_injective_left (q : Path b c) : Injective fun p : Path a b => p.comp q :=
  fun _ _ h => ((comp_inj rfl).1 h).1

theorem comp_injective_right (p : Path a b) : Injective (p.comp : Path b c â†’ Path a c) :=
  fun _ _ h => ((comp_inj' rfl).1 h).2

@[simp]
theorem comp_inj_left {pâ‚ pâ‚‚ : Path a b} {q : Path b c} : pâ‚.comp q = pâ‚‚.comp q â†” pâ‚ = pâ‚‚ :=
  q.comp_injective_left.eq_iff

@[simp]
theorem comp_inj_right {p : Path a b} {qâ‚ qâ‚‚ : Path b c} : p.comp qâ‚ = p.comp qâ‚‚ â†” qâ‚ = qâ‚‚ :=
  p.comp_injective_right.eq_iff

lemma eq_toPath_comp_of_length_eq_succ (p : Path a b) {n : â„•}
    (hp : p.length = n + 1) :
    âˆƒ (c : V) (f : a âŸ¶ c) (q : Quiver.Path c b) (_ : q.length = n),
      p = f.toPath.comp q := by
  induction p generalizing n with
  | nil => simp at hp
  | @cons c d p q h =>
    cases n
    Â· rw [length_cons, Nat.zero_add, Nat.add_eq_right] at hp
      obtain rfl := eq_of_length_zero p hp
      obtain rfl := eq_nil_of_length_zero p hp
      exact âŸ¨d, q, nil, rfl, rflâŸ©
    Â· rw [length_cons, Nat.add_right_cancel_iff] at hp
      obtain âŸ¨x, q'', p'', hl, rflâŸ© := h hp
      exact âŸ¨x, q'', p''.cons q, by simpa, rflâŸ©

/-- Turn a path into a list. The list contains `a` at its head, but not `b` a priori. -/
@[simp]
def toList : âˆ€ {b : V}, Path a b â†’ List V
  | _, nil => []
  | _, @cons _ _ _ c _ p _ => c :: p.toList

/-- `Quiver.Path.toList` is a contravariant functor. The inversion comes from `Quiver.Path` and
`List` having different preferred directions for adding elements. -/
@[simp]
theorem toList_comp (p : Path a b) : âˆ€ {c} (q : Path b c), (p.comp q).toList = q.toList ++ p.toList
  | _, nil => by simp
  | _, @cons _ _ _ d _ q _ => by simp [toList_comp]

theorem toList_chain_nonempty :
    âˆ€ {b} (p : Path a b), p.toList.Chain (fun x y => Nonempty (y âŸ¶ x)) b
  | _, nil => List.Chain.nil
  | _, cons p f => p.toList_chain_nonempty.cons âŸ¨fâŸ©

variable [âˆ€ a b : V, Subsingleton (a âŸ¶ b)]

theorem toList_injective (a : V) : âˆ€ b, Injective (toList : Path a b â†’ List V)
  | _, nil, nil, _ => rfl
  | _, nil, @cons _ _ _ c _ p f, h => by cases h
  | _, @cons _ _ _ c _ p f, nil, h => by cases h
  | _, @cons _ _ _ c _ p f, @cons _ _ _ t _ C D, h => by
    simp only [toList, List.cons.injEq] at h
    obtain âŸ¨rfl, hACâŸ© := h
    simp [toList_injective _ _ hAC, eq_iff_true_of_subsingleton]

@[simp]
theorem toList_inj {p q : Path a b} : p.toList = q.toList â†” p = q :=
  (toList_injective _ _).eq_iff

end Path

end Quiver

namespace Prefunctor

open Quiver

variable {V : Type uâ‚} [Quiver.{vâ‚} V] {W : Type uâ‚‚} [Quiver.{vâ‚‚} W] (F : V â¥¤q W)

/-- The image of a path under a prefunctor. -/
def mapPath {a : V} : âˆ€ {b : V}, Path a b â†’ Path (F.obj a) (F.obj b)
  | _, Path.nil => Path.nil
  | _, Path.cons p e => Path.cons (mapPath p) (F.map e)

@[simp]
theorem mapPath_nil (a : V) : F.mapPath (Path.nil : Path a a) = Path.nil :=
  rfl

@[simp]
theorem mapPath_cons {a b c : V} (p : Path a b) (e : b âŸ¶ c) :
    F.mapPath (Path.cons p e) = Path.cons (F.mapPath p) (F.map e) :=
  rfl

@[simp]
theorem mapPath_comp {a b : V} (p : Path a b) :
    âˆ€ {c : V} (q : Path b c), F.mapPath (p.comp q) = (F.mapPath p).comp (F.mapPath q)
  | _, Path.nil => rfl
  | c, Path.cons q e => by dsimp; rw [mapPath_comp p q]

@[simp]
theorem mapPath_toPath {a b : V} (f : a âŸ¶ b) : F.mapPath f.toPath = (F.map f).toPath :=
  rfl

@[simp]
theorem mapPath_id {a b : V} : (p : Path a b) â†’ (ðŸ­q V).mapPath p = p
  | Path.nil => rfl
  | Path.cons q e => by dsimp; rw [mapPath_id q]

variable {U : Type uâ‚ƒ} [Quiver.{vâ‚ƒ} U] (G : W â¥¤q U)

@[simp]
theorem mapPath_comp_apply {a b : V} (p : Path a b) :
    (F â‹™q G).mapPath p = G.mapPath (F.mapPath p) := by
  induction p with
  | nil => rfl
  | cons x y h => simp [h]

end Prefunctor
