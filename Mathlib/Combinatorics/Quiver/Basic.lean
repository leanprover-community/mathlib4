/-
Copyright (c) 2021 David W√§rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn, Scott Morrison
-/
import Mathlib.Data.Opposite
import Mathlib.Tactic.Cases

#align_import combinatorics.quiver.basic from "leanprover-community/mathlib"@"56adee5b5eef9e734d82272918300fca4f3e7cef"

/-!
# Quivers

This module defines quivers. A quiver on a type `V` of vertices assigns to every
pair `a b : V` of vertices a type `a ‚ü∂ b` of arrows from `a` to `b`. This
is a very permissive notion of directed graph.

## Implementation notes

Currently `Quiver` is defined with `Hom : V ‚Üí V ‚Üí Sort v`.
This is different from the category theory setup,
where we insist that morphisms live in some `Type`.
There's some balance here: it's nice to allow `Prop` to ensure there are no multiple arrows,
but it is also results in error-prone universe signatures when constraints require a `Type`.
-/

open Opposite

-- We use the same universe order as in category theory.
-- See note [CategoryTheory universes]
universe v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

/-- A quiver `G` on a type `V` of vertices assigns to every pair `a b : V` of vertices
a type `a ‚ü∂ b` of arrows from `a` to `b`.

For graphs with no repeated edges, one can use `Quiver.{0} V`, which ensures
`a ‚ü∂ b : Prop`. For multigraphs, one can use `Quiver.{v+1} V`, which ensures
`a ‚ü∂ b : Type v`.

Because `Category` will later extend this class, we call the field `Hom`.
Except when constructing instances, you should rarely see this, and use the `‚ü∂` notation instead.
-/
class Quiver (V : Type u) where
  /-- The type of edges/arrows/morphisms between a given source and target. -/
  Hom : V ‚Üí V ‚Üí Sort v
#align quiver Quiver
#align quiver.hom Quiver.Hom

/--
Notation for the type of edges/arrows/morphisms between a given source and target
in a quiver or category.
-/
infixr:10 " ‚ü∂ " => Quiver.Hom

/-- A morphism of quivers. As we will later have categorical functors extend this structure,
we call it a `Prefunctor`. -/
structure Prefunctor (V : Type u‚ÇÅ) [Quiver.{v‚ÇÅ} V] (W : Type u‚ÇÇ) [Quiver.{v‚ÇÇ} W] where
  /-- The action of a (pre)functor on vertices/objects. -/
  obj : V ‚Üí W
  /-- The action of a (pre)functor on edges/arrows/morphisms. -/
  map : ‚àÄ {X Y : V}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y)
#align prefunctor Prefunctor

namespace Prefunctor

-- Porting note: added during port.
-- These lemmas can not be `@[simp]` because after `whnfR` they have a variable on the LHS.
-- Nevertheless they are sometimes useful when building functors.
lemma mk_obj {V W : Type*} [Quiver V] [Quiver W] {obj : V ‚Üí W} {map} {X : V} :
    (Prefunctor.mk obj map).obj X = obj X := rfl

lemma mk_map {V W : Type*} [Quiver V] [Quiver W] {obj : V ‚Üí W} {map} {X Y : V} {f : X ‚ü∂ Y} :
    (Prefunctor.mk obj map).map f = map f := rfl

@[ext]
theorem ext {V : Type u} [Quiver.{v‚ÇÅ} V] {W : Type u‚ÇÇ} [Quiver.{v‚ÇÇ} W] {F G : Prefunctor V W}
    (h_obj : ‚àÄ X, F.obj X = G.obj X)
    (h_map : ‚àÄ (X Y : V) (f : X ‚ü∂ Y),
      F.map f = Eq.recOn (h_obj Y).symm (Eq.recOn (h_obj X).symm (G.map f))) : F = G := by
  cases' F with F_obj _
  cases' G with G_obj _
  obtain rfl : F_obj = G_obj := by
    ext X
    apply h_obj
  congr
  funext X Y f
  simpa using h_map X Y f
#align prefunctor.ext Prefunctor.ext

/-- The identity morphism between quivers. -/
@[simps]
def id (V : Type*) [Quiver V] : Prefunctor V V where
  obj := fun X => X
  map f := f
#align prefunctor.id Prefunctor.id
#align prefunctor.id_obj Prefunctor.id_obj
#align prefunctor.id_map Prefunctor.id_map

instance (V : Type*) [Quiver V] : Inhabited (Prefunctor V V) :=
  ‚ü®id V‚ü©

/-- Composition of morphisms between quivers. -/
@[simps]
def comp {U : Type*} [Quiver U] {V : Type*} [Quiver V] {W : Type*} [Quiver W]
    (F : Prefunctor U V) (G : Prefunctor V W) : Prefunctor U W where
  obj X := G.obj (F.obj X)
  map f := G.map (F.map f)
#align prefunctor.comp Prefunctor.comp
#align prefunctor.comp_obj Prefunctor.comp_obj
#align prefunctor.comp_map Prefunctor.comp_map

@[simp]
theorem comp_id {U V : Type*} [Quiver U] [Quiver V] (F : Prefunctor U V) :
    F.comp (id _) = F := rfl
#align prefunctor.comp_id Prefunctor.comp_id

@[simp]
theorem id_comp {U V : Type*} [Quiver U] [Quiver V] (F : Prefunctor U V) :
    (id _).comp F = F := rfl
#align prefunctor.id_comp Prefunctor.id_comp

@[simp]
theorem comp_assoc {U V W Z : Type*} [Quiver U] [Quiver V] [Quiver W] [Quiver Z]
    (F : Prefunctor U V) (G : Prefunctor V W) (H : Prefunctor W Z) :
    (F.comp G).comp H = F.comp (G.comp H) :=
  rfl
#align prefunctor.comp_assoc Prefunctor.comp_assoc

/-- Notation for a prefunctor between quivers. -/
infixl:50 " ‚•§q " => Prefunctor

/-- Notation for composition of prefunctors. -/
infixl:60 " ‚ãôq " => Prefunctor.comp

/-- Notation for the identity prefunctor on a quiver. -/
notation "ùü≠q" => id

theorem congr_map {U V : Type*} [Quiver U] [Quiver V] (F : U ‚•§q V) {X Y : U} {f g : X ‚ü∂ Y}
    (h : f = g) : F.map f = F.map g := by
  rw [h]

end Prefunctor

namespace Quiver

/-- `V·µí·µñ` reverses the direction of all arrows of `V`. -/
instance opposite {V} [Quiver V] : Quiver V·µí·µñ :=
  ‚ü®fun a b => (unop b ‚ü∂ unop a)·µí·µñ‚ü©
#align quiver.opposite Quiver.opposite

/-- The opposite of an arrow in `V`. -/
def Hom.op {V} [Quiver V] {X Y : V} (f : X ‚ü∂ Y) : op Y ‚ü∂ op X := ‚ü®f‚ü©
#align quiver.hom.op Quiver.Hom.op

/-- Given an arrow in `V·µí·µñ`, we can take the "unopposite" back in `V`. -/
def Hom.unop {V} [Quiver V] {X Y : V·µí·µñ} (f : X ‚ü∂ Y) : unop Y ‚ü∂ unop X := Opposite.unop f
#align quiver.hom.unop Quiver.Hom.unop

/-- A type synonym for a quiver with no arrows. -/
-- Porting note(#5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
def Empty (V : Type u) : Type u := V
#align quiver.empty Quiver.Empty

instance emptyQuiver (V : Type u) : Quiver.{u} (Empty V) := ‚ü®fun _ _ => PEmpty‚ü©
#align quiver.empty_quiver Quiver.emptyQuiver

@[simp]
theorem empty_arrow {V : Type u} (a b : Empty V) : (a ‚ü∂ b) = PEmpty := rfl
#align quiver.empty_arrow Quiver.empty_arrow

/-- A quiver is thin if it has no parallel arrows. -/
abbrev IsThin (V : Type u) [Quiver V] : Prop := ‚àÄ a b : V, Subsingleton (a ‚ü∂ b)
#align quiver.is_thin Quiver.IsThin

end Quiver
