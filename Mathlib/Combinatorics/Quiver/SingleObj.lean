/-
Copyright (c) 2023 Antoine Labelle. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Labelle
-/
import Mathlib.Combinatorics.Quiver.Cast
import Mathlib.Combinatorics.Quiver.Symmetric

#align_import combinatorics.quiver.single_obj from "leanprover-community/mathlib"@"509de852e1de55e1efa8eacfa11df0823f26f226"

/-!
# Single-object quiver

Single object quiver with a given arrows type.

## Main definitions

Given a type `Œ±`, `SingleObj Œ±` is the `Unit` type, whose single object is called `star Œ±`, with
`Quiver` structure such that `star Œ± ‚ü∂ star Œ±` is the type `Œ±`.
An element `x : Œ±` can be reinterpreted as an element of `star Œ± ‚ü∂ star Œ±` using
`toHom`.
More generally, a list of elements of `a` can be reinterpreted as a path from `star Œ±` to
itself using `pathEquivList`.
-/

namespace Quiver

/-- Type tag on `Unit` used to define single-object quivers. -/
-- Porting note: Removed `deriving Unique`.
@[nolint unusedArguments]
def SingleObj (_ : Type*) : Type :=
  Unit
#align quiver.single_obj Quiver.SingleObj

-- Porting note: `deriving` from above has been moved to below.
instance {Œ± : Type*} : Unique (SingleObj Œ±) where
  default := ‚ü®‚ü©
  uniq := fun _ => rfl

namespace SingleObj

variable (Œ± Œ≤ Œ≥ : Type*)

instance : Quiver (SingleObj Œ±) :=
  ‚ü®fun _ _ => Œ±‚ü©

/-- The single object in `SingleObj Œ±`. -/
def star : SingleObj Œ± :=
  Unit.unit
#align quiver.single_obj.star Quiver.SingleObj.star

instance : Inhabited (SingleObj Œ±) :=
  ‚ü®star Œ±‚ü©

variable {Œ± Œ≤ Œ≥}

lemma ext {x y : SingleObj Œ±} : x = y := Unit.ext x y

-- See note [reducible non-instances]
/-- Equip `SingleObj Œ±` with a reverse operation. -/
abbrev hasReverse (rev : Œ± ‚Üí Œ±) : HasReverse (SingleObj Œ±) := ‚ü®rev‚ü©
#align quiver.single_obj.has_reverse Quiver.SingleObj.hasReverse

-- See note [reducible non-instances]
/-- Equip `SingleObj Œ±` with an involutive reverse operation. -/
abbrev hasInvolutiveReverse (rev : Œ± ‚Üí Œ±) (h : Function.Involutive rev) :
    HasInvolutiveReverse (SingleObj Œ±) where
  toHasReverse := hasReverse rev
  inv' := h
#align quiver.single_obj.has_involutive_reverse Quiver.SingleObj.hasInvolutiveReverse

/-- The type of arrows from `star Œ±` to itself is equivalent to the original type `Œ±`. -/
@[simps!]
def toHom : Œ± ‚âÉ (star Œ± ‚ü∂ star Œ±) :=
  Equiv.refl _
#align quiver.single_obj.to_hom Quiver.SingleObj.toHom
#align quiver.single_obj.to_hom_apply Quiver.SingleObj.toHom_apply
#align quiver.single_obj.to_hom_symm_apply Quiver.SingleObj.toHom_symm_apply

/-- Prefunctors between two `SingleObj` quivers correspond to functions between the corresponding
arrows types.
-/
@[simps]
def toPrefunctor : (Œ± ‚Üí Œ≤) ‚âÉ SingleObj Œ± ‚•§q SingleObj Œ≤ where
  toFun f := ‚ü®id, f‚ü©
  invFun f a := f.map (toHom a)
  left_inv _ := rfl
  right_inv _ := rfl
#align quiver.single_obj.to_prefunctor_symm_apply Quiver.SingleObj.toPrefunctor_symm_apply
#align quiver.single_obj.to_prefunctor_apply_map Quiver.SingleObj.toPrefunctor_apply_map
#align quiver.single_obj.to_prefunctor_apply_obj Quiver.SingleObj.toPrefunctor_apply_obj

#align quiver.single_obj.to_prefunctor Quiver.SingleObj.toPrefunctor

theorem toPrefunctor_id : toPrefunctor id = ùü≠q (SingleObj Œ±) :=
  rfl
#align quiver.single_obj.to_prefunctor_id Quiver.SingleObj.toPrefunctor_id

@[simp]
theorem toPrefunctor_symm_id : toPrefunctor.symm (ùü≠q (SingleObj Œ±)) = id :=
  rfl
#align quiver.single_obj.to_prefunctor_symm_id Quiver.SingleObj.toPrefunctor_symm_id

theorem toPrefunctor_comp (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) :
    toPrefunctor (g ‚àò f) = toPrefunctor f ‚ãôq toPrefunctor g :=
  rfl
#align quiver.single_obj.to_prefunctor_comp Quiver.SingleObj.toPrefunctor_comp

@[simp]
theorem toPrefunctor_symm_comp (f : SingleObj Œ± ‚•§q SingleObj Œ≤) (g : SingleObj Œ≤ ‚•§q SingleObj Œ≥) :
    toPrefunctor.symm (f ‚ãôq g) = toPrefunctor.symm g ‚àò toPrefunctor.symm f := by
  simp only [Equiv.symm_apply_eq, toPrefunctor_comp, Equiv.apply_symm_apply]
#align quiver.single_obj.to_prefunctor_symm_comp Quiver.SingleObj.toPrefunctor_symm_comp

/-- Auxiliary definition for `quiver.SingleObj.pathEquivList`.
Converts a path in the quiver `single_obj Œ±` into a list of elements of type `a`.
-/
def pathToList : ‚àÄ {x : SingleObj Œ±}, Path (star Œ±) x ‚Üí List Œ±
  | _, Path.nil => []
  | _, Path.cons p a => a :: pathToList p
#align quiver.single_obj.path_to_list Quiver.SingleObj.pathToList

/-- Auxiliary definition for `quiver.SingleObj.pathEquivList`.
Converts a list of elements of type `Œ±` into a path in the quiver `SingleObj Œ±`.
-/
@[simp]
def listToPath : List Œ± ‚Üí Path (star Œ±) (star Œ±)
  | [] => Path.nil
  | a :: l => (listToPath l).cons a
#align quiver.single_obj.list_to_path Quiver.SingleObj.listToPath

theorem listToPath_pathToList {x : SingleObj Œ±} (p : Path (star Œ±) x) :
    listToPath (pathToList p) = p.cast rfl ext := by
  induction' p with y z p a ih
  ¬∑ rfl
  ¬∑ dsimp at *; rw [ih]
#align quiver.single_obj.path_to_list_to_path Quiver.SingleObj.listToPath_pathToList

theorem pathToList_listToPath (l : List Œ±) : pathToList (listToPath l) = l := by
  induction' l with a l ih
  ¬∑ rfl
  ¬∑ change a :: pathToList (listToPath l) = a :: l; rw [ih]
#align quiver.single_obj.list_to_path_to_list Quiver.SingleObj.pathToList_listToPath

/-- Paths in `SingleObj Œ±` quiver correspond to lists of elements of type `Œ±`. -/
def pathEquivList : Path (star Œ±) (star Œ±) ‚âÉ List Œ± :=
  ‚ü®pathToList, listToPath, fun p => listToPath_pathToList p, pathToList_listToPath‚ü©
#align quiver.single_obj.path_equiv_list Quiver.SingleObj.pathEquivList

@[simp]
theorem pathEquivList_nil : pathEquivList Path.nil = ([] : List Œ±) :=
  rfl
#align quiver.single_obj.path_equiv_list_nil Quiver.SingleObj.pathEquivList_nil

@[simp]
theorem pathEquivList_cons (p : Path (star Œ±) (star Œ±)) (a : star Œ± ‚ü∂ star Œ±) :
    pathEquivList (Path.cons p a) = a :: pathToList p :=
  rfl
#align quiver.single_obj.path_equiv_list_cons Quiver.SingleObj.pathEquivList_cons

@[simp]
theorem pathEquivList_symm_nil : pathEquivList.symm ([] : List Œ±) = Path.nil :=
  rfl
#align quiver.single_obj.path_equiv_list_symm_nil Quiver.SingleObj.pathEquivList_symm_nil

@[simp]
theorem pathEquivList_symm_cons (l : List Œ±) (a : Œ±) :
    pathEquivList.symm (a :: l) = Path.cons (pathEquivList.symm l) a :=
  rfl
#align quiver.single_obj.path_equiv_list_symm_cons Quiver.SingleObj.pathEquivList_symm_cons

end SingleObj

end Quiver
