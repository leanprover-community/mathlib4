/-
Copyright (c) 2025 Evan Spotte-Smith, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Evan Spotte-Smith, Bhavik Mehta
-/
import Mathlib.Data.Set.Basic
import Mathlib.Data.Set.Card

/-!
# Undirected hypergraphs

An *undirected hypergraph* (here abbreviated as *hypergraph*) `H` is a generalization of a graph
(see `Mathlib.Combinatorics.Graph` or `Mathlib.Combinatorics.SimpleGraph`) and consists of a set of
*vertices*, usually denoted `V` or `V(H)`, and a set of *hyperedges*, here called *edges* and
denoted `E` or `E(H)`. In contrast with a graph, where edges are unordered pairs of vertices, in
hypergraphs, edges are (unordered) sets of vertices; i.e., they are subsets of the vertex set `V`.

A hypergraph where `V = âˆ…` and `E = âˆ…` is *empty*. A hypergraph with a nonempty
vertex set (`V â‰  âˆ…`) and empty edge set is *trivial*.

If a edge `e` contains only one vertex (i.e., `|e| = 1`), then it is a *loop*.

This module defines `Hypergraph Î±` for a vertex type `Î±` (edges are defined as `Set (Set Î±)`).

## Main definitions

For `H : Hypergraph Î±`:

* `V(H)` denotes the vertex set of `H` as a term in `Set Î±`.
* `E(H)` denotes the edge set of `H` as a term in `Set (Set Î±)`. Hyperedges must be subsets of
    `V(H)`.
* `H.Adj x y` means that there exists some edge containing both `x` and `y` (or, in other
    words, `x` and `y` are incident to some shared edge `e`).
* `H.EAdj e f` means that there exists some vertex that is incident to the edges `e` and
    `f : Set Î±`.

## Implementation details

This implementation is heavily inspired by Peter Nelson and Jun Kwon's `Graph` implementation,
which was in turn inspired by `Matroid`.

Paraphrasing `Mathlib.Combinatorics.Graph.Basic`:
"The main tradeoff is that parts of the API will need to care about whether a term
`x : Î±` or `e : Set Î±` is a 'real' vertex or edge of the graph, rather than something outside
the vertex or edge set. This is an issue, but is likely amenable to automation."

Because `edgeSet` is a `Set (Set Î±)`, rather than a multiset, here we are assuming that
all hypergraphs are *without repeated edge*.

## Acknowledgments

Credit to Shreyas Srinivas, GitHub user @NotWearingPants ("Snir" on the Lean Zulip), Ammar
Husain, Aaron Liu, Tristan Figueroa-Reid, and John Talbot for patient guidance and useful feedback
on this implementation.
-/

open Set

variable {Î± Î² Î³ : Type*} {x y : Î±} {e e' f g : Set Î±} {l : Set (Set Î±)}

/--
An undirected hypergraph with vertices of type `Î±` and edges of type `Set Î±`, as described by vertex
and edge sets `vertexSet : Set Î±` and `edgeSet : Set (Set Î±)`.

The requirement `edge_isSubset_vertexSet` ensures that all vertices in edges are part of
`vertexSet`, i.e., all edges are subsets of the `vertexSet`.
-/
@[ext]
structure Hypergraph (Î± : Type*) where
  /-- The vertex set -/
  vertexSet : Set Î±
  /-- The edge set -/
  edgeSet : Set (Set Î±)
  /-- All edges must be subsets of the vertex set -/
  edge_isSubset_vertexSet' : âˆ€ â¦ƒeâ¦„, e âˆˆ edgeSet â†’ e âŠ† vertexSet

namespace Hypergraph

variable {H : Hypergraph Î±}

/-! ## Notation -/

/-- `V(H)` denotes the `vertexSet` of a hypergraph `H` -/
scoped notation "V(" H ")" => Hypergraph.vertexSet H

/-- `E(H)` denotes the `edgeSet` of a hypergraph `H` -/
scoped notation "E(" H ")" => Hypergraph.edgeSet H


/-! ## Vertex-Hyperedge Incidence -/

@[simp]
lemma edge_isSubset_vertexSet (he : e âˆˆ E(H)) : e âŠ† V(H) :=
  H.edge_isSubset_vertexSet' he

lemma _root_.Membership.mem.subset_vertexSet (he : e âˆˆ E(H)) : e âŠ† V(H) :=
  H.edge_isSubset_vertexSet he

lemma edgeSet_subset_powerset_vertexSet {H : Hypergraph Î±} : E(H) âŠ† V(H).powerset := by
  intro e (he : e âˆˆ E(H))
  simpa using he.subset_vertexSet

lemma mem_vertexSet_of_mem_edgeSet (he : e âˆˆ E(H)) (hx : x âˆˆ e) : x âˆˆ V(H) := by
  have h1 : e âŠ† V(H) := by apply H.edge_isSubset_vertexSet he
  apply Set.mem_of_subset_of_mem h1 hx

/--
If edges `e` and `e'` have the same vertices from `G`, then they have all the same vertices.
This could be phrased as `e = e'`, but this formulation is more useful in combination with the `ext`
tactic.
-/
lemma forall_of_forall_verts (he : e âˆˆ E(H)) (he' : e' âˆˆ E(H))
    (h : âˆ€ x âˆˆ V(H), x âˆˆ e â†” x âˆˆ e') : âˆ€ x, x âˆˆ e â†” x âˆˆ e' :=
  fun x â†¦ âŸ¨fun y â†¦ (h x (he.subset_vertexSet y)).1 y,
  fun y â†¦ (h x (he'.subset_vertexSet y)).2 yâŸ©

lemma sUnion_edgeSet_subset_vertexSet : â‹ƒâ‚€ E(H) âŠ† V(H) := by
  refine subset_powerset_iff.mp ?_
  exact edgeSet_subset_powerset_vertexSet

/-! ## Vertex and Hyperedge Adjacency -/

/--
Predicate for adjacency. Two vertices `x` and `y` are adjacent if there is some edge `e âˆˆ E(H)`
where `x` and `y` are both incident to `e`.

Note that we do not need to explicitly check that x, y âˆˆ V(H) here because a vertex that is not in
the vertex set cannot be incident to any edge.
-/
def Adj (H : Hypergraph Î±) (x : Î±) (y : Î±) : Prop :=
  âˆƒ e âˆˆ E(H), x âˆˆ e âˆ§ y âˆˆ e

lemma Adj.symm (h : H.Adj x y) : H.Adj y x := by grind [Adj]

-- Credit: Peter Nelson, Jun Kwon
lemma adj_comm (x y : Î±) : H.Adj x y â†” H.Adj y x := âŸ¨.symm, .symmâŸ©

/--
Predicate for edge adjacency. Analogous to `Hypergraph.Adj`, edges `e` and `f` are
adjacent if there is some vertex `x âˆˆ V(H)` where `x` is incident to both `e` and `f`.
-/
def EAdj (H : Hypergraph Î±) (e : Set Î±) (f : Set Î±) : Prop :=
  e âˆˆ E(H) âˆ§ f âˆˆ E(H) âˆ§ âˆƒ x, x âˆˆ e âˆ§ x âˆˆ f

lemma EAdj.exists_vertex (h : H.EAdj e f) : âˆƒ x âˆˆ V(H), x âˆˆ e âˆ§ x âˆˆ f := by
  unfold EAdj at h
  obtain âŸ¨x, hxâŸ© := h.2.2
  use x
  constructor
  Â· exact mem_vertexSet_of_mem_edgeSet h.1 hx.1
  Â· exact hx

lemma EAdj.symm (h : H.EAdj e f) : H.EAdj f e := by grind [EAdj]

lemma EAdj.inter_nonempty (hef : H.EAdj e f) : (e âˆ© f).Nonempty := by
  unfold EAdj at *
  have h' : âˆƒ x âˆˆ e, x âˆˆ f := by grind
  apply Set.inter_nonempty.mpr h'

-- Credit: Peter Nelson, Jun Kwon
lemma eAdj_comm (e f) : H.EAdj e f â†” H.EAdj f e := âŸ¨.symm, .symmâŸ©

/-! ## Basic Hypergraph Definitions & Predicates-/

/--
The *star* of a vertex is the set of all edges `e âˆˆ E(H)` that a given vertex `x` is incident to
-/
def star (H : Hypergraph Î±) (x : Î±) : Set (Set Î±) := {e âˆˆ E(H) | x âˆˆ e}

/--
We define the *star set* as the set of subsets of `E(H)` that each vertex in `V(H)` is
incident to
-/
def stars (H : Hypergraph Î±) : Set (Set (Set Î±)) := {H.star x | x âˆˆ V(H)}

/--
The *image* of a hypergraph `H : Hypergraph Î±` under function `f : Î± â†’ Î²` is `Há¶  : Hypergraph Î²`.

The vertex set of `Há¶ ` is the image of `V(H)` under `f`, and the edge set of `Há¶ ` is the set of
images of the edges (subsets of vertices) in `E(H)`.
-/
@[simps]
def image (H : Hypergraph Î±) (f : Î± â†’ Î²) : Hypergraph Î² where
  vertexSet := V(H).image f
  edgeSet := E(H).image (Set.image f)
  edge_isSubset_vertexSet' := by
    simp
    intro e he
    have hev : e âŠ† V(H) := by exact Membership.mem.subset_vertexSet he
    refine image_subset_iff.mp ?_
    exact image_mono hev

lemma mem_image {f : Î± â†’ Î²} {e : Set Î²} : e âˆˆ E(H.image f) â†” âˆƒ e' âˆˆ E(H), f '' e' = e := Iff.rfl

lemma image_mem_image {f : Î± â†’ Î²} (he : e âˆˆ E(H)) : e.image f âˆˆ E(H.image f) :=
  mem_image_of_mem _ he

lemma image_image {f : Î± â†’ Î²} {g : Î² â†’ Î³} (H : Hypergraph Î±) :
  (H.image f).image g = H.image (g âˆ˜ f) := by
    ext : 1
    case vertexSet => simp [Set.image_image]
    case edgeSet => simp [Set.image_image]

/--
Predicate to determine if a vertex is isolated, meaning that it is not incident to any edges. Note
that this includes loops, i.e., if vertex `x` is isolated, there is no edge with associated vertex
subset `{x}`.
-/
def IsIsolated (H : Hypergraph Î±) (x : Î±) : Prop := âˆ€ e âˆˆ E(H), x âˆ‰ e

lemma not_exists_isolated_vertex_iff_sUnion_edgeSet_eq_vertexSet :
  â‹ƒâ‚€ E(H) = V(H) â†” âˆ€ x âˆˆ V(H), Â¬IsIsolated H x :=
    Iff.intro
    (by grind [IsIsolated])
    (by
      unfold IsIsolated
      intro h
      have h' : âˆ€ x âˆˆ V(H), âˆƒ e âˆˆ E(H), x âˆˆ e := by grind
      refine Subset.antisymm ?_ h'
      apply Set.sUnion_subset
      exact fun t' a â†¦ H.edge_isSubset_vertexSet a
    )

/--
Predicate to determine if a edge `e` is a loop, meaning that its associated vertex subset `s`
contains only one vertex, i.e., `|s| = 1`
-/
def IsLoop (H : Hypergraph Î±) (e : Set Î±) : Prop := âˆƒ x âˆˆ V(H), e = {x}

lemma isLoop_encard_one (h : H.IsLoop e) : Set.encard e = 1 := by
  unfold IsLoop at h
  refine encard_eq_one.mpr ?_
  obtain âŸ¨x, hxâŸ© := h
  use x
  exact hx.2

/--
Predicate to determine if a hypergraph is empty
-/
def IsEmpty (H : Hypergraph Î±) : Prop := V(H) = âˆ… âˆ§ E(H) = âˆ…

/--
Predicate to determine if a hypergraph is nonempty
-/
def IsNonempty (H : Hypergraph Î±) : Prop := (âˆƒ x, x âˆˆ V(H)) âˆ¨ (âˆƒ e, e âˆˆ E(H))

/--
The empty hypergraph of type Î±
-/
@[simps]
def emptyHypergraph (Î± : Type*) : Hypergraph Î± where
  vertexSet := âˆ…
  edgeSet := âˆ…
  edge_isSubset_vertexSet' := by
    intro e he
    have h1 : e = âˆ… := by exact False.elim he
    exact Set.subset_empty_iff.mpr h1

@[simp] lemma coe_nonempty : V(H).Nonempty â†’ H.IsNonempty := by
  unfold IsNonempty
  unfold Set.Nonempty
  exact fun a â†¦ Or.symm (Or.inr a)

lemma isEmpty_empty_hypergraph : IsEmpty (Hypergraph.emptyHypergraph Î±) := by
  unfold IsEmpty
  exact Prod.mk_inj.mp rfl

lemma isEmpty_eq_empty_hypergraph (h : H.IsEmpty) : emptyHypergraph Î± = H := by
  unfold IsEmpty at h
  have hv : V(emptyHypergraph Î±) = âˆ… := rfl
  have he : E(emptyHypergraph Î±) = âˆ… := rfl
  apply Hypergraph.ext_iff.mpr
  grind

lemma edge_not_mem_empty : e âˆ‰ E(emptyHypergraph Î±) := by simp

lemma IsEmpty.eq (hH : H.IsEmpty) : V(H) = âˆ… âˆ§ E(H) = âˆ… := by exact hH

@[simp]
lemma isEmpty_iff_forall_not_mem : H.IsEmpty â†” (âˆ€ x, x âˆ‰ V(H)) âˆ§ (âˆ€ e, e âˆ‰ E(H)) := by
  grind [IsEmpty, Set.notMem_empty]

lemma IsEmpty.not_mem (hH : H.IsEmpty) {e : Set Î±} : e âˆ‰ E(H) := by
  unfold IsEmpty at hH
  grind

lemma not_isEmpty : Â¬H.IsEmpty â†” H.IsNonempty := by grind [IsEmpty, IsNonempty]

lemma not_isNonempty : Â¬H.IsNonempty â†” H.IsEmpty := not_iff_comm.mp not_isEmpty

alias âŸ¨_, IsEmpty.not_isNonemptyâŸ© := not_isNonempty
alias âŸ¨_, IsNonempty.not_isEmptyâŸ© := not_isEmpty

variable (H) in
lemma isEmpty_or_isNonempty : H.IsEmpty âˆ¨ H.IsNonempty := by grind [IsEmpty, IsNonempty]

/--
Predicate to determine if a hypergraph is trivial

A hypergraph is trivial if it has a nonempty vertex set and an empty edge set
-/
def IsTrivial (H : Hypergraph Î±) : Prop := Set.Nonempty V(H) âˆ§ E(H) = âˆ…

/--
A trivial hypergraph of type Î± with vertex set h
-/
@[simps]
def trivialHypergraph (f : Set Î±) :=
  Hypergraph.mk
  f
  âˆ…
  (by
    intro e he
    exact False.elim he
  )

lemma not_isEmpty_trivial_hypergraph (hh : IsTrivial H) : Â¬IsEmpty H := by
  grind [IsEmpty, IsTrivial, Set.nonempty_iff_ne_empty]

lemma edge_not_mem_trivial (h : H.IsTrivial) : e âˆ‰ E(H) := by grind [IsTrivial]

/--
Predicate to determine is a hypergraph `H` is complete, meaning that each member of the power set of
the vertices (`ğ’« V(H)`) is represented in `E(H)`
-/
def IsComplete (H : Hypergraph Î±) : Prop := âˆ€ e âˆˆ ğ’« V(H), e âˆˆ E(H)

/--
A complete hypergraph with vertex set f
-/
@[simps]
def completeOn (f : Set Î±) : Hypergraph Î± where
  vertexSet := f
  edgeSet := ğ’« f
  edge_isSubset_vertexSet' := by simp

lemma mem_completeOn : e âˆˆ E(completeOn f) â†” e âŠ† f := by simp

lemma isComplete_completeOn (f : Set Î±) : (completeOn f).IsComplete := by exact fun e a â†¦ a

lemma isComplete_not_isEmpty (h : H.IsComplete) : Â¬ H.IsEmpty := by
  unfold IsComplete at h
  unfold IsEmpty
  have h0 : âˆ… âˆˆ ğ’« V(H) := by
    refine mem_powerset ?_
    apply Set.empty_subset
  apply not_and_or.mpr
  right
  grind

lemma completeOn_isNonempty {S : Set Î±} : (completeOn S).IsNonempty := by
  have h : E(completeOn S) = ğ’« S := rfl
  have h' : âˆ… âˆˆ E(completeOn S) := by
    refine mem_completeOn.mpr ?_
    apply Set.empty_subset
  unfold IsNonempty
  right
  use âˆ…

lemma isComplete_not_isTrivial (h : H.IsComplete) : Â¬H.IsTrivial := by
  unfold IsComplete at h
  unfold IsTrivial
  have h' : âˆ… âˆˆ E(H) := by grind
  apply not_and_or.mpr
  right
  exact ne_of_mem_of_not_mem' h' fun a â†¦ a

lemma completeOn_not_isTrivial {S : Set Î±} : Â¬(completeOn S).IsTrivial := by
  unfold IsTrivial
  apply not_and_or.mpr
  right
  exact ne_of_mem_of_not_mem' (fun â¦ƒaâ¦„ a â†¦ a) fun a â†¦ a

end Hypergraph
