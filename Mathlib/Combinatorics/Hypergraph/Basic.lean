/-
Copyright (c) 2025 Evan Spotte-Smith, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Evan Spotte-Smith, Bhavik Mehta
-/
import Mathlib.Data.Set.Basic
import Mathlib.Data.Set.Card
import Mathlib.Data.Fintype.Basic
import Mathlib.Data.Finset.Basic

/-!
# Undirected hypergraphs

An *undirected hypergraph* (here abbreviated as *hypergraph*) `H` is a generalization of a graph
(see `Mathlib.Combinatorics.Graph` or `Mathlib.Combinatorics.SimpleGraph`) and consists of a set of
*vertices*, usually denoted `V` or `V(H)`, and a set of *hyperedges*, denoted `E` or `E(H)`. In
contrast with a graph, where edges are unordered pairs of vertices, in hypergraphs, hyperedges are
(unordered) sets of vertices; i.e., they are subsets of the vertex set `V`.

A hypergraph where `V = âˆ…` and `E = âˆ…` is *empty*. A hypergraph with a nonempty
vertex set (`V â‰  âˆ…`) and empty hyperedge set is *trivial*.

If a hyperedge `e` contains only one vertex (i.e., `|e| = 1`), then it is a *loop*.

This module defines `Hypergraph Î±` for a vertex type `Î±` (hyperedges are defined as `Set (Set Î±)`).

## Main definitions

For `H : Hypergraph Î±`:

* `V(H)` denotes the vertex set of `H` as a term in `Set Î±`.
* `E(H)` denotes the hyperedge set of `H` as a term in `Set (Set Î±)`. Hyperedges must be subsets of
    `V(H)`.
* `H.Adj x y` means that there exists some hyperedge containing both `x` and `y` (or, in other
    words, `x` and `y` are incident on some shared hyperedge `e`).
* `H.EAdj e f` means that there exists some vertex that is incident on both hyperedge `e` and
    hyperedge `f : Set Î±`.

## Implementation details

This implementation is heavily inspired by Peter Nelson and Jun Kwon's `Graph` implementation,
which was in turn inspired by `Matroid`.

Paraphrasing `Mathlib.Combinatorics.Graph.Basic`:
"The main tradeoff is that parts of the API will need to care about whether a term
`x : Î±` or `e : Set Î±` is a 'real' vertex or edge of the graph, rather than something outside
the vertex or edge set. This is an issue, but is likely amenable to automation."

Because `hyperedgeSet` is a `Set (Set Î±)`, rather than a multiset, here we are assuming that
all hypergraphs are *without repeated hyperedge*.

## Acknowledgments

Credit to Shreyas Srinivas, GitHub user @NotWearingPants ("Snir" on the Lean Zulip), Ammar
Husain, Aaron Liu, and Tristan Figueroa-Reid for patient guidance and useful feedback on this
implementation.
-/

open Set

variable {Î± : Type*} {x y : Î±} {e e' f g : Set Î±} {l : Set (Set Î±)}

/--
An undirected hypergraph with vertices of type `Î±` and hyperedges of type `Set Î±`,
as described by vertex and hyperedge sets `vertexSet : Set Î±` and `hyperedgeSet : Set (Set Î±)`.

The requirement `hyperedge_isSubset_vertexSet` ensures that all vertices in hyperedges are part of
`vertexSet`, i.e., all hyperedges are subsets of the `vertexSet`.
-/
@[ext]
structure Hypergraph (Î± : Type*) where
  /-- The vertex set -/
  vertexSet : Set Î±
  /-- The hyperedge set -/
  hyperedgeSet : Set (Set Î±)
  /-- All hyperedges must be subsets of the vertex set -/
  hyperedge_isSubset_vertexSet' : âˆ€ â¦ƒeâ¦„, e âˆˆ hyperedgeSet â†’ e âŠ† vertexSet

namespace Hypergraph

variable {H : Hypergraph Î±}

/-! ## Notation -/

/-- `V(H)` denotes the `vertexSet` of a hypergraph `H` -/
scoped notation "V(" H ")" => Hypergraph.vertexSet H

/-- `E(H)` denotes the `hyperedgeSet` of a hypergraph `H` -/
scoped notation "E(" H ")" => Hypergraph.hyperedgeSet H


instance : Membership (Î±) (Hypergraph Î±) where
  mem H x := x âˆˆ V(H)

instance : Membership (Set Î±) (Hypergraph Î±) where
  mem H e := e âˆˆ E(H)


section Incidence

/-! ## Vertex-Hyperedge Incidence -/

@[simp] lemma hyperedge_isSubset_vertexSet {H : Hypergraph Î±} {e : Set Î±} (he : e âˆˆ E(H)) :
  e âŠ† V(H) := H.hyperedge_isSubset_vertexSet' he

lemma _root_.Membership.mem.isSubset_vertexSet {H : Hypergraph Î±} {e : Set Î±} (he : e âˆˆ E(H)) :
  e âŠ† V(H) := H.hyperedge_isSubset_vertexSet he

lemma coe_isSubset_vertexSet_powerset {H : Hypergraph Î±} : E(H) âŠ† V(H).powerset := by
  intro e (he : e âˆˆ E(H))
  simpa using he.isSubset_vertexSet

lemma vertex_mem_if_mem_hyperedge {H : Hypergraph Î±} {e : Set Î±} {x : Î±}
  (he : e âˆˆ H.hyperedgeSet) (hx : x âˆˆ e) : x âˆˆ H.vertexSet := by
  have h1 : e âŠ† V(H) := by apply H.hyperedge_isSubset_vertexSet he
  apply Set.mem_of_subset_of_mem h1 hx

/--
If edges `e` and `e'` have the same vertices from `G`, then they have all the same vertices.
This could be phrased as `e = e'`, but this formulation is more useful in combination with the `ext`
tactic.
-/
lemma forall_of_forall_verts {e e' : Set Î±} (he : e âˆˆ E(H)) (he' : e' âˆˆ E(H))
    (h : âˆ€ x âˆˆ V(H), x âˆˆ e â†” x âˆˆ e') : âˆ€ x, x âˆˆ e â†” x âˆˆ e' :=
  fun x â†¦ âŸ¨fun y â†¦ (h x (he.isSubset_vertexSet y)).1 y,
  fun y â†¦ (h x (he'.isSubset_vertexSet y)).2 yâŸ©

end Incidence

section Adjacency

/-! ## Vertex and Hyperedge Adjacency -/

/--
Predicate for adjacency. Two vertices `x` and `y` are adjacent if there is some
hyperedge `e âˆˆ E(H)` where `x` and `y` are both incident on `e`.

Note that we do not need to explicitly check that x, y âˆˆ V(H) here because a vertex that is not in
the vertex set cannot be incident on any hyperedge.
-/
def Adj (H : Hypergraph Î±) (x : Î±) (y : Î±) : Prop :=
  âˆƒ e âˆˆ E(H), x âˆˆ e âˆ§ y âˆˆ e

lemma Adj.symm {H : Hypergraph Î±} {x y : Î±} (h : H.Adj x y) : H.Adj y x := by
  unfold Adj at *
  obtain âŸ¨e, heâŸ© := h
  use e
  constructor
  Â· exact he.1
  constructor
  Â· exact he.2.2
  Â· exact he.2.1

-- Credit: Peter Nelson, Jun Kwon
lemma hypergraph_adj_comm (x y) : H.Adj x y â†” H.Adj y x :=
  âŸ¨.symm, .symmâŸ©

/--
Predicate for (hyperedge) adjacency. Analogous to `Hypergraph.Adj`, hyperedges `e` and `f` are
adjacent if there is some vertex `x âˆˆ V(H)` where `x` is incident on both `e` and `f`.
-/
def EAdj (H : Hypergraph Î±) (e : Set Î±) (f : Set Î±) : Prop :=
  e âˆˆ E(H) âˆ§ f âˆˆ E(H) âˆ§ âˆƒ x âˆˆ V(H), x âˆˆ e âˆ§ x âˆˆ f

lemma EAdj.symm {H : Hypergraph Î±} {e f : Set Î±} (h : H.EAdj e f) : H.EAdj f e := by
  unfold EAdj at *
  obtain âŸ¨v, hvâŸ© := h.2.2
  constructor
  Â· exact h.2.1
  constructor
  Â· exact h.1
  Â· use v
    constructor
    Â· exact hv.1
    constructor
    Â· exact hv.2.2
    Â· exact hv.2.1

lemma EAdj.inter_nonempty {H : Hypergraph Î±} {e f : Set Î±} (hef : H.EAdj e f) :
  (e âˆ© f).Nonempty := by
    unfold EAdj at *
    have h' : âˆƒ x âˆˆ e, x âˆˆ f := by grind
    apply Set.inter_nonempty.mpr h'

-- Credit: Peter Nelson, Jun Kwon
lemma hypergraph_eadj_comm (e f) : H.EAdj e f â†” H.EAdj f e :=
  âŸ¨.symm, .symmâŸ©

/--
Neighbors of a vertex `x` in hypergraph `H`

A vertex `y` is a neighbor of vertex `x` if there exists some hyperedge `e âˆˆ E(H)` where `x` and
`y` are both incident on `e`, i.e., if the two vertices are adjacent (see `Hypergraph.Adj`)
-/
def neighbors (H : Hypergraph Î±) (x : Î±) : Set Î± := {y | H.Adj x y}

/--
Neighbors of a hyperedge `e` in hypergraph `H`

A hyperedge `f` is a neighbor of hyperedge `e` if there exists some vertex `x âˆˆ V(H)` where `x` is
incident on both `e` and `f`, i.e., if the two hyperedges are adjacent (see `Hypergraph.EAdj`)
-/
def hyperedgeNeighbors (H : Hypergraph Î±) (e : Set Î±) : Set (Set Î±) := {f | H.EAdj e f}

end Adjacency

section DefsPreds

/-! ## Basic Hypergraph Definitions & Predicates-/

/--
The *star* of a vertex is the set of all hyperedges `e âˆˆ E(H)` that a given vertex `x` is incident
on
-/
def star (H : Hypergraph Î±) (x : Î±) : Set (Set Î±) := {e âˆˆ E(H) | x âˆˆ e}

/--
We define the *star set* as the set of subsets of `E(H)` that each vertex in `V(H)` is
incident upon
-/
def stars (H : Hypergraph Î±) : Set (Set (Set Î±)) :=
  {H.star x | x âˆˆ V(H)}

/--
Predicate to determine if a vertex is isolated, meaning that it is not incident on any hyperedges.
Note that this includes loops, i.e., if vertex `x` is isolated, there is no hyperedge with
associated vertex subset `{x}`
-/
def IsIsolated (H : Hypergraph Î±) (x : Î±) : Prop := âˆ€ e âˆˆ E(H), x âˆ‰ e

lemma not_exists_isolated_vertex_iff_sUnion_hyperedgeSet_eq_vertexSet {H : Hypergraph Î±} :
Set.sUnion E(H) = V(H) â†” âˆ€ x âˆˆ V(H), Â¬IsIsolated H x :=
  Iff.intro
  (by
    unfold IsIsolated
    intro h
    grind
  )
  (by
    unfold IsIsolated
    intro h
    have h' : âˆ€ x âˆˆ V(H), âˆƒ e âˆˆ E(H), x âˆˆ e := by grind
    refine Subset.antisymm ?_ h'
    apply Set.sUnion_subset
    exact fun t' a â†¦ H.hyperedge_isSubset_vertexSet a
  )


/--
Predicate to determine if a hyperedge `e` is a loop, meaning that its associated vertex subset `s`
contains only one vertex, i.e., `|s| = 1`
-/
def IsLoop (H : Hypergraph Î±) (e : Set Î±) : Prop := âˆƒ x âˆˆ V(H), e = {x}

/--
Predicate to determine if a hypergraph is empty
-/
def IsEmpty (H : Hypergraph Î±) : Prop := V(H) = âˆ… âˆ§ E(H) = âˆ…

/--
The empty hypergraph of type Î±
-/
def emptyHypergraph (Î± : Type*) : Hypergraph Î± :=
  Hypergraph.mk
  âˆ…
  âˆ…
  (by
    intro e he
    have h1 : e = âˆ… := by exact False.elim he
    exact Set.subset_empty_iff.mpr h1
  )

lemma isEmpty_empty_hypergraph {Î± : Type*} : IsEmpty (Hypergraph.emptyHypergraph Î±) := by
  unfold IsEmpty
  exact Prod.mk_inj.mp rfl

lemma isEmpty_eq_empty_hypergraph {H : Hypergraph Î±} (h : H.IsEmpty) : H = emptyHypergraph Î± := by
  exact Hypergraph.ext_iff.mpr h

/--
Predicate to determine if a hypergraph is trivial

A hypergraph is trivial if it has a nonempty vertex set and an empty hyperedge set
-/
def IsTrivial (H : Hypergraph Î±) : Prop := Set.Nonempty V(H) âˆ§ E(H) = âˆ…

/--
A trivial hypergraph of type Î± with vertex set h
-/
def trivialHypergraph {Î± : Type*} (h : Set Î±) :=
  Hypergraph.mk
  h
  âˆ…
  (by
    intro e he
    exact False.elim he
  )

lemma not_isEmpty_trivial_hypergraph {H : Hypergraph Î±} (hh : IsTrivial H) : Â¬IsEmpty H := by
  unfold IsEmpty
  unfold IsTrivial at hh
  refine not_and_of_not_or_not ?_
  left
  apply Set.nonempty_iff_ne_empty.mp hh.1

/--
Predicate to determine is a hypergraph `H` is complete, meaning that each member of the power set of
the vertices (`ğ’« V(H)`) is represented in `E(H)`
-/
def IsComplete (H : Hypergraph Î±) : Prop := âˆ€ e âˆˆ ğ’« V(H), e âˆˆ E(H)

/--
Predicate to determine if a hypergraph is simple

A simple hypergraph is one in which, for each hyperedge `e âˆˆ E(H)` (with associated vertex subset
`s : Set Î±`), there is no other hyperedge `f âˆˆ E(H)` (with associated vertex subset `t : Set Î±`)
such that `s âŠ‚ t`.
-/
def IsSimple (H : Hypergraph Î±) : Prop := âˆ€ e âˆˆ E(H), âˆ€ f âˆˆ E(H) \ {e}, Â¬e âŠ† f

end DefsPreds

end Hypergraph
