/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Combinatorics.SetFamily.Shadow

/-!
# UV-compressions

This file defines UV-compression. It is an operation on a set family that reduces its shadow.

UV-compressing `a : Î±` along `u v : Î±` means replacing `a` by `(a âŠ” u) \ v` if `a` and `u` are
disjoint and `v â‰¤ a`. In some sense, it's moving `a` from `v` to `u`.

UV-compressions are immensely useful to prove the Kruskal-Katona theorem. The idea is that
compressing a set family might decrease the size of its shadow, so iterated compressions hopefully
minimise the shadow.

## Main declarations

* `UV.compress`: `compress u v a` is `a` compressed along `u` and `v`.
* `UV.compression`: `compression u v s` is the compression of the set family `s` along `u` and `v`.
  It is the compressions of the elements of `s` whose compression is not already in `s` along with
  the element whose compression is already in `s`. This way of splitting into what moves and what
  does not ensures the compression doesn't squash the set family, which is proved by
  `UV.card_compression`.
* `UV.card_shadow_compression_le`: Compressing reduces the size of the shadow. This is a key fact in
  the proof of Kruskal-Katona.

## Notation

`ğ“’` (typed with `\MCC`) is notation for `UV.compression` in scope `FinsetFamily`.

## Notes

Even though our emphasis is on `Finset Î±`, we define UV-compressions more generally in a generalized
Boolean algebra, so that one can use it for `Set Î±`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

compression, UV-compression, shadow
-/


open Finset

variable {Î± : Type*}

/-- UV-compression is injective on the elements it moves. See `UV.compress`. -/
theorem sup_sdiff_injOn [GeneralizedBooleanAlgebra Î±] (u v : Î±) :
    { x | Disjoint u x âˆ§ v â‰¤ x }.InjOn fun x => (x âŠ” u) \ v := by
  rintro a ha b hb hab
  have h : ((a âŠ” u) \ v) \ u âŠ” v = ((b âŠ” u) \ v) \ u âŠ” v := by
    dsimp at hab
    rw [hab]
  rwa [sdiff_sdiff_comm, ha.1.symm.sup_sdiff_cancel_right, sdiff_sdiff_comm,
    hb.1.symm.sup_sdiff_cancel_right, sdiff_sup_cancel ha.2, sdiff_sup_cancel hb.2] at h

-- The namespace is here to distinguish from other compressions.
namespace UV

/-! ### UV-compression in generalized Boolean algebras -/


section GeneralizedBooleanAlgebra

variable [GeneralizedBooleanAlgebra Î±] [DecidableRel (@Disjoint Î± _ _)]
  [DecidableLE Î±] {s : Finset Î±} {u v a : Î±}

/-- UV-compressing `a` means removing `v` from it and adding `u` if `a` and `u` are disjoint and
`v â‰¤ a` (it replaces the `v` part of `a` by the `u` part). Else, UV-compressing `a` doesn't do
anything. This is most useful when `u` and `v` are disjoint finsets of the same size. -/
def compress (u v a : Î±) : Î± :=
  if Disjoint u a âˆ§ v â‰¤ a then (a âŠ” u) \ v else a

theorem compress_of_disjoint_of_le (hua : Disjoint u a) (hva : v â‰¤ a) :
    compress u v a = (a âŠ” u) \ v :=
  if_pos âŸ¨hua, hvaâŸ©

theorem compress_of_disjoint_of_le' (hva : Disjoint v a) (hua : u â‰¤ a) :
    compress u v ((a âŠ” v) \ u) = a := by
  rw [compress_of_disjoint_of_le disjoint_sdiff_self_right
      (le_sdiff.2 âŸ¨(le_sup_right : v â‰¤ a âŠ” v), hva.mono_right huaâŸ©),
    sdiff_sup_cancel (le_sup_of_le_left hua), hva.symm.sup_sdiff_cancel_right]

@[simp]
theorem compress_self (u a : Î±) : compress u u a = a := by
  unfold compress
  split_ifs with h
  Â· exact h.1.symm.sup_sdiff_cancel_right
  Â· rfl

/-- An element can be compressed to any other element by removing/adding the differences. -/
@[simp]
theorem compress_sdiff_sdiff (a b : Î±) : compress (a \ b) (b \ a) b = a := by
  refine (compress_of_disjoint_of_le disjoint_sdiff_self_left sdiff_le).trans ?_
  rw [sup_sdiff_self_right, sup_sdiff, disjoint_sdiff_self_right.sdiff_eq_left, sup_eq_right]
  exact sdiff_sdiff_le

/-- Compressing an element is idempotent. -/
@[simp]
theorem compress_idem (u v a : Î±) : compress u v (compress u v a) = compress u v a := by
  unfold compress
  split_ifs with h h'
  Â· rw [le_sdiff_right.1 h'.2, sdiff_bot, sdiff_bot, sup_assoc, sup_idem]
  Â· rfl
  Â· rfl

variable [DecidableEq Î±]

/-- To UV-compress a set family, we compress each of its elements, except that we don't want to
reduce the cardinality, so we keep all elements whose compression is already present. -/
def compression (u v : Î±) (s : Finset Î±) :=
  {a âˆˆ s | compress u v a âˆˆ s} âˆª {a âˆˆ s.image <| compress u v | a âˆ‰ s}

@[inherit_doc]
scoped[FinsetFamily] notation "ğ“’ " => UV.compression

open scoped FinsetFamily

/-- `IsCompressed u v s` expresses that `s` is UV-compressed. -/
def IsCompressed (u v : Î±) (s : Finset Î±) :=
  ğ“’ u v s = s

/-- UV-compression is injective on the sets that are not UV-compressed. -/
theorem compress_injOn : Set.InjOn (compress u v) â†‘{a âˆˆ s | compress u v a âˆ‰ s} := by
  intro a ha b hb hab
  rw [mem_coe, mem_filter] at ha hb
  rw [compress] at ha hab
  split_ifs at ha hab with has
  Â· rw [compress] at hb hab
    split_ifs at hb hab with hbs
    Â· exact sup_sdiff_injOn u v has hbs hab
    Â· exact (hb.2 hb.1).elim
  Â· exact (ha.2 ha.1).elim

/-- `a` is in the UV-compressed family iff it's in the original and its compression is in the
original, or it's not in the original but it's the compression of something in the original. -/
theorem mem_compression :
    a âˆˆ ğ“’ u v s â†” a âˆˆ s âˆ§ compress u v a âˆˆ s âˆ¨ a âˆ‰ s âˆ§ âˆƒ b âˆˆ s, compress u v b = a := by
  simp_rw [compression, mem_union, mem_filter, mem_image, and_comm]

protected theorem IsCompressed.eq (h : IsCompressed u v s) : ğ“’ u v s = s := h

@[simp]
theorem compression_self (u : Î±) (s : Finset Î±) : ğ“’ u u s = s := by
  unfold compression
  convert union_empty s
  Â· ext a
    rw [mem_filter, compress_self, and_self_iff]
  Â· refine eq_empty_of_forall_notMem fun a ha â†¦ ?_
    simp_rw [mem_filter, mem_image, compress_self] at ha
    obtain âŸ¨âŸ¨b, hb, rflâŸ©, hb'âŸ© := ha
    exact hb' hb

/-- Any family is compressed along two identical elements. -/
theorem isCompressed_self (u : Î±) (s : Finset Î±) : IsCompressed u u s := compression_self u s

theorem compress_disjoint :
    Disjoint {a âˆˆ s | compress u v a âˆˆ s} {a âˆˆ s.image <| compress u v | a âˆ‰ s} :=
  disjoint_left.2 fun _a haâ‚ haâ‚‚ â†¦ (mem_filter.1 haâ‚‚).2 (mem_filter.1 haâ‚).1

theorem compress_mem_compression (ha : a âˆˆ s) : compress u v a âˆˆ ğ“’ u v s := by
  rw [mem_compression]
  by_cases h : compress u v a âˆˆ s
  Â· rw [compress_idem]
    exact Or.inl âŸ¨h, hâŸ©
  Â· exact Or.inr âŸ¨h, a, ha, rflâŸ©

-- This is a special case of `compress_mem_compression` once we have `compression_idem`.
theorem compress_mem_compression_of_mem_compression (ha : a âˆˆ ğ“’ u v s) :
    compress u v a âˆˆ ğ“’ u v s := by
  rw [mem_compression] at ha âŠ¢
  simp only [compress_idem]
  obtain âŸ¨_, haâŸ© | âŸ¨_, b, hb, rflâŸ© := ha
  Â· exact Or.inl âŸ¨ha, haâŸ©
  Â· exact Or.inr âŸ¨by rwa [compress_idem], b, hb, (compress_idem _ _ _).symmâŸ©

/-- Compressing a family is idempotent. -/
@[simp]
theorem compression_idem (u v : Î±) (s : Finset Î±) : ğ“’ u v (ğ“’ u v s) = ğ“’ u v s := by
  have h : {a âˆˆ ğ“’ u v s | compress u v a âˆ‰ ğ“’ u v s} = âˆ… :=
    filter_false_of_mem fun a ha h â†¦ h <| compress_mem_compression_of_mem_compression ha
  rw [compression, filter_image, h, image_empty, â† h]
  exact filter_union_filter_neg_eq _ (compression u v s)

/-- Compressing a family doesn't change its size. -/
@[simp]
theorem card_compression (u v : Î±) (s : Finset Î±) : #(ğ“’ u v s) = #s := by
  rw [compression, card_union_of_disjoint compress_disjoint, filter_image,
    card_image_of_injOn compress_injOn, â† card_union_of_disjoint (disjoint_filter_filter_neg s _ _),
    filter_union_filter_neg_eq]

theorem le_of_mem_compression_of_notMem (h : a âˆˆ ğ“’ u v s) (ha : a âˆ‰ s) : u â‰¤ a := by
  rw [mem_compression] at h
  obtain h | âŸ¨-, b, hb, hbaâŸ© := h
  Â· cases ha h.1
  unfold compress at hba
  split_ifs at hba with h
  Â· rw [â† hba, le_sdiff]
    exact âŸ¨le_sup_right, h.1.mono_right h.2âŸ©
  Â· cases ne_of_mem_of_not_mem hb ha hba

@[deprecated (since := "2025-05-23")]
alias le_of_mem_compression_of_not_mem := le_of_mem_compression_of_notMem

theorem disjoint_of_mem_compression_of_notMem (h : a âˆˆ ğ“’ u v s) (ha : a âˆ‰ s) : Disjoint v a := by
  rw [mem_compression] at h
  obtain h | âŸ¨-, b, hb, hbaâŸ© := h
  Â· cases ha h.1
  unfold compress at hba
  split_ifs at hba
  Â· rw [â† hba]
    exact disjoint_sdiff_self_right
  Â· cases ne_of_mem_of_not_mem hb ha hba

@[deprecated (since := "2025-05-23")]
alias disjoint_of_mem_compression_of_not_mem := disjoint_of_mem_compression_of_notMem

theorem sup_sdiff_mem_of_mem_compression_of_notMem (h : a âˆˆ ğ“’ u v s) (ha : a âˆ‰ s) :
    (a âŠ” v) \ u âˆˆ s := by
  rw [mem_compression] at h
  obtain h | âŸ¨-, b, hb, hbaâŸ© := h
  Â· cases ha h.1
  unfold compress at hba
  split_ifs at hba with h
  Â· rwa [â† hba, sdiff_sup_cancel (le_sup_of_le_left h.2), sup_sdiff_right_self,
      h.1.symm.sdiff_eq_left]
  Â· cases ne_of_mem_of_not_mem hb ha hba

@[deprecated (since := "2025-05-23")]
alias sup_sdiff_mem_of_mem_compression_of_not_mem := sup_sdiff_mem_of_mem_compression_of_notMem

/-- If `a` is in the family compression and can be compressed, then its compression is in the
original family. -/
theorem sup_sdiff_mem_of_mem_compression (ha : a âˆˆ ğ“’ u v s) (hva : v â‰¤ a) (hua : Disjoint u a) :
    (a âŠ” u) \ v âˆˆ s := by
  rw [mem_compression, compress_of_disjoint_of_le hua hva] at ha
  obtain âŸ¨_, haâŸ© | âŸ¨_, b, hb, rflâŸ© := ha
  Â· exact ha
  have hu : u = âŠ¥ := by
    suffices Disjoint u (u \ v) by rwa [(hua.mono_right hva).sdiff_eq_left, disjoint_self] at this
    refine hua.mono_right ?_
    rw [â† compress_idem, compress_of_disjoint_of_le hua hva]
    exact sdiff_le_sdiff_right le_sup_right
  have hv : v = âŠ¥ := by
    rw [â† disjoint_self]
    apply Disjoint.mono_right hva
    rw [â† compress_idem, compress_of_disjoint_of_le hua hva]
    exact disjoint_sdiff_self_right
  rwa [hu, hv, compress_self, sup_bot_eq, sdiff_bot]

/-- If `a` is in the `u, v`-compression but `v â‰¤ a`, then `a` must have been in the original
family. -/
theorem mem_of_mem_compression (ha : a âˆˆ ğ“’ u v s) (hva : v â‰¤ a) (hvu : v = âŠ¥ â†’ u = âŠ¥) :
    a âˆˆ s := by
  rw [mem_compression] at ha
  obtain ha | âŸ¨_, b, hb, hâŸ© := ha
  Â· exact ha.1
  unfold compress at h
  split_ifs at h
  Â· rw [â† h, le_sdiff_right] at hva
    rwa [â† h, hvu hva, hva, sup_bot_eq, sdiff_bot]
  Â· rwa [â† h]

end GeneralizedBooleanAlgebra

/-! ### UV-compression on finsets -/

open FinsetFamily

variable [DecidableEq Î±] {ğ’œ : Finset (Finset Î±)} {u v : Finset Î±} {r : â„•}

/-- Compressing a finset doesn't change its size. -/
theorem card_compress (huv : #u = #v) (a : Finset Î±) : #(compress u v a) = #a := by
  unfold compress
  split_ifs with h
  Â· rw [card_sdiff_of_subset (h.2.trans le_sup_left), sup_eq_union,
      card_union_of_disjoint h.1.symm, huv, add_tsub_cancel_right]
  Â· rfl

lemma _root_.Set.Sized.uvCompression (huv : #u = #v) (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    (ğ“’ u v ğ’œ : Set (Finset Î±)).Sized r := by
  simp_rw [Set.Sized, mem_coe, mem_compression]
  rintro s (hs | âŸ¨huvt, t, ht, rflâŸ©)
  Â· exact hğ’œ hs.1
  Â· rw [card_compress huv, hğ’œ ht]

private theorem aux (huv : âˆ€ x âˆˆ u, âˆƒ y âˆˆ v, IsCompressed (u.erase x) (v.erase y) ğ’œ) :
    v = âˆ… â†’ u = âˆ… := by
  grind

/-- UV-compression reduces the size of the shadow of `ğ’œ` if, for all `x âˆˆ u` there is `y âˆˆ v` such
that `ğ’œ` is `(u.erase x, v.erase y)`-compressed. This is the key fact about compression for
Kruskal-Katona. -/
theorem shadow_compression_subset_compression_shadow (u v : Finset Î±)
    (huv : âˆ€ x âˆˆ u, âˆƒ y âˆˆ v, IsCompressed (u.erase x) (v.erase y) ğ’œ) :
    âˆ‚ (ğ“’ u v ğ’œ) âŠ† ğ“’ u v (âˆ‚ ğ’œ) := by
  set ğ’œ' := ğ“’ u v ğ’œ
  suffices H : âˆ€ s âˆˆ âˆ‚ ğ’œ',
      s âˆ‰ âˆ‚ ğ’œ â†’ u âŠ† s âˆ§ Disjoint v s âˆ§ (s âˆª v) \ u âˆˆ âˆ‚ ğ’œ âˆ§ (s âˆª v) \ u âˆ‰ âˆ‚ ğ’œ' by
    rintro s hs'
    rw [mem_compression]
    by_cases hs : s âˆˆ ğ’œ.shadow
    swap
    Â· obtain âŸ¨hus, hvs, h, _âŸ© := H _ hs' hs
      exact Or.inr âŸ¨hs, _, h, compress_of_disjoint_of_le' hvs husâŸ©
    refine Or.inl âŸ¨hs, ?_âŸ©
    rw [compress]
    split_ifs with huvs
    swap
    Â· exact hs
    rw [mem_shadow_iff] at hs'
    obtain âŸ¨t, Ht, a, hat, rflâŸ© := hs'
    have hav : a âˆ‰ v := notMem_mono huvs.2 (notMem_erase a t)
    have hvt : v â‰¤ t := huvs.2.trans (erase_subset _ t)
    have ht : t âˆˆ ğ’œ := mem_of_mem_compression Ht hvt (aux huv)
    by_cases hau : a âˆˆ u
    Â· obtain âŸ¨b, hbv, HcompâŸ© := huv a hau
      refine mem_shadow_iff_insert_mem.2 âŸ¨b, notMem_sdiff_of_mem_right hbv, ?_âŸ©
      rw [â† Hcomp.eq] at ht
      have hsb :=
        sup_sdiff_mem_of_mem_compression ht ((erase_subset _ _).trans hvt)
          (disjoint_erase_comm.2 huvs.1)
      rwa [sup_eq_union, sdiff_erase (mem_union_left _ <| hvt hbv), union_erase_of_mem hat, â†
        erase_union_of_mem hau] at hsb
    Â· refine mem_shadow_iff.2
        âŸ¨(t âŠ” u) \ v,
          sup_sdiff_mem_of_mem_compression Ht hvt <| disjoint_of_erase_right hau huvs.1, a, ?_, ?_âŸ©
      Â· rw [sup_eq_union, mem_sdiff, mem_union]
        exact âŸ¨Or.inl hat, havâŸ©
      Â· rw [â† erase_sdiff_comm, sup_eq_union, erase_union_distrib, erase_eq_of_notMem hau]
  intro s hsğ’œ' hsğ’œ
  -- This is going to be useful a couple of times so let's name it.
  have m : âˆ€ y, y âˆ‰ s â†’ insert y s âˆ‰ ğ’œ := fun y h a => hsğ’œ (mem_shadow_iff_insert_mem.2 âŸ¨y, h, aâŸ©)
  obtain âŸ¨x, _, _âŸ© := mem_shadow_iff_insert_mem.1 hsğ’œ'
  have hus : u âŠ† insert x s := le_of_mem_compression_of_notMem â€¹_ âˆˆ ğ’œ'â€º (m _ â€¹x âˆ‰ sâ€º)
  have hvs : Disjoint v (insert x s) := disjoint_of_mem_compression_of_notMem â€¹_â€º (m _ â€¹x âˆ‰ sâ€º)
  have : (insert x s âˆª v) \ u âˆˆ ğ’œ := sup_sdiff_mem_of_mem_compression_of_notMem â€¹_â€º (m _ â€¹x âˆ‰ sâ€º)
  have hsv : Disjoint s v := hvs.symm.mono_left (subset_insert _ _)
  have hvu : Disjoint v u := disjoint_of_subset_right hus hvs
  have hxv : x âˆ‰ v := disjoint_right.1 hvs (mem_insert_self _ _)
  have : v \ u = v := â€¹Disjoint v uâ€º.sdiff_eq_left
  -- The first key part is that `x âˆ‰ u`
  have : x âˆ‰ u := by
    intro hxu
    obtain âŸ¨y, hyv, hxyâŸ© := huv x hxu
    -- If `x âˆˆ u`, we can get `y âˆˆ v` so that `ğ’œ` is `(u.erase x, v.erase y)`-compressed
    apply m y (disjoint_right.1 hsv hyv)
    -- and we will use this `y` to contradict `m`, so we would like to show `insert y s âˆˆ ğ’œ`.
    -- We do this by showing the below
    have : ((insert x s âˆª v) \ u âˆª erase u x) \ erase v y âˆˆ ğ’œ := by
      refine
        sup_sdiff_mem_of_mem_compression (by rwa [hxy.eq]) ?_
          (disjoint_of_subset_left (erase_subset _ _) disjoint_sdiff)
      rw [union_sdiff_distrib, â€¹v \ u = vâ€º]
      exact (erase_subset _ _).trans subset_union_right
    -- and then arguing that it's the same
    convert this using 1
    rw [sdiff_union_erase_cancel (hus.trans subset_union_left) â€¹x âˆˆ uâ€º, erase_union_distrib,
      erase_insert â€¹x âˆ‰ sâ€º, erase_eq_of_notMem â€¹x âˆ‰ vâ€º, sdiff_erase (mem_union_right _ hyv),
      union_sdiff_cancel_right hsv]
  -- Now that this is done, it's immediate that `u âŠ† s`
  have hus : u âŠ† s := by rwa [â† erase_eq_of_notMem â€¹x âˆ‰ uâ€º, â† subset_insert_iff]
  -- and we already had that `v` and `s` are disjoint,
  -- so it only remains to get `(s âˆª v) \ u âˆˆ âˆ‚ ğ’œ \ âˆ‚ ğ’œ'`
  simp_rw [mem_shadow_iff_insert_mem]
  refine âŸ¨hus, hsv.symm, âŸ¨x, ?_, ?_âŸ©, ?_âŸ©
  -- `(s âˆª v) \ u âˆˆ âˆ‚ ğ’œ` is pretty direct:
  Â· exact notMem_sdiff_of_notMem_left (notMem_union.2 âŸ¨â€¹x âˆ‰ sâ€º, â€¹x âˆ‰ vâ€ºâŸ©)
  Â· rwa [â† insert_sdiff_of_notMem _ â€¹x âˆ‰ uâ€º, â† insert_union]
  -- For (s âˆª v) \ u âˆ‰ âˆ‚ ğ’œ', we split up based on w âˆˆ u
  rintro âŸ¨w, hwB, hwğ’œ'âŸ©
  have : v âŠ† insert w ((s âˆª v) \ u) :=
    (subset_sdiff.2 âŸ¨subset_union_right, hvuâŸ©).trans (subset_insert _ _)
  by_cases hwu : w âˆˆ u
  -- If `w âˆˆ u`, we find `z âˆˆ v`, and contradict `m` again
  Â· obtain âŸ¨z, hz, hxyâŸ© := huv w hwu
    apply m z (disjoint_right.1 hsv hz)
    have : insert w ((s âˆª v) \ u) âˆˆ ğ’œ := mem_of_mem_compression hwğ’œ' â€¹_â€º (aux huv)
    have : (insert w ((s âˆª v) \ u) âˆª erase u w) \ erase v z âˆˆ ğ’œ := by
      refine sup_sdiff_mem_of_mem_compression (by rwa [hxy.eq]) ((erase_subset _ _).trans â€¹_â€º) ?_
      rw [â† sdiff_erase (mem_union_left _ <| hus hwu)]
      exact disjoint_sdiff
    convert this using 1
    rw [insert_union_comm, insert_erase â€¹w âˆˆ uâ€º,
      sdiff_union_of_subset (hus.trans subset_union_left),
      sdiff_erase (mem_union_right _ â€¹z âˆˆ vâ€º), union_sdiff_cancel_right hsv]
  -- If `w âˆ‰ u`, we contradict `m` again
  rw [mem_sdiff, â† Classical.not_imp, Classical.not_not] at hwB
  apply m w (hwu âˆ˜ hwB âˆ˜ mem_union_left _)
  have : (insert w ((s âˆª v) \ u) âˆª u) \ v âˆˆ ğ’œ :=
    sup_sdiff_mem_of_mem_compression â€¹insert w ((s âˆª v) \ u) âˆˆ ğ’œ'â€º â€¹_â€º
      (disjoint_insert_right.2 âŸ¨â€¹_â€º, disjoint_sdiffâŸ©)
  convert this using 1
  rw [insert_union, sdiff_union_of_subset (hus.trans subset_union_left),
    insert_sdiff_of_notMem _ (hwu âˆ˜ hwB âˆ˜ mem_union_right _), union_sdiff_cancel_right hsv]

/-- UV-compression reduces the size of the shadow of `ğ’œ` if, for all `x âˆˆ u` there is `y âˆˆ v`
such that `ğ’œ` is `(u.erase x, v.erase y)`-compressed. This is the key UV-compression fact needed for
Kruskal-Katona. -/
theorem card_shadow_compression_le (u v : Finset Î±)
    (huv : âˆ€ x âˆˆ u, âˆƒ y âˆˆ v, IsCompressed (u.erase x) (v.erase y) ğ’œ) :
    #(âˆ‚ (ğ“’ u v ğ’œ)) â‰¤ #(âˆ‚ ğ’œ) :=
  (card_le_card <| shadow_compression_subset_compression_shadow _ _ huv).trans
    (card_compression _ _ _).le

end UV
