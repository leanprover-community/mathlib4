/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Finset.Lattice

#align_import combinatorics.set_family.compression.down from "leanprover-community/mathlib"@"9003f28797c0664a49e4179487267c494477d853"

/-!
# Down-compressions

This file defines down-compression.

Down-compressing `ğ’œ : Finset (Finset Î±)` along `a : Î±` means removing `a` from the elements of `ğ’œ`,
when the resulting set is not already in `ğ’œ`.

## Main declarations

* `Finset.nonMemberSubfamily`: `ğ’œ.nonMemberSubfamily a` is the subfamily of sets not containing
  `a`.
* `Finset.memberSubfamily`: `ğ’œ.memberSubfamily a` is the image of the subfamily of sets containing
  `a` under removing `a`.
* `Down.compression`: Down-compression.

## Notation

`ğ““ a ğ’œ` is notation for `Down.compress a ğ’œ` in locale `SetFamily`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

compression, down-compression
-/


variable {Î± : Type*} [DecidableEq Î±] {ğ’œ â„¬ : Finset (Finset Î±)} {s : Finset Î±} {a : Î±}

namespace Finset

/-- Elements of `ğ’œ` that do not contain `a`. -/
def nonMemberSubfamily (a : Î±) (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  ğ’œ.filter fun s => a âˆ‰ s
#align finset.non_member_subfamily Finset.nonMemberSubfamily

/-- Image of the elements of `ğ’œ` which contain `a` under removing `a`. Finsets that do not contain
`a` such that `insert a s âˆˆ ğ’œ`. -/
def memberSubfamily (a : Î±) (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  (ğ’œ.filter fun s => a âˆˆ s).image fun s => erase s a
#align finset.member_subfamily Finset.memberSubfamily

@[simp]
theorem mem_nonMemberSubfamily : s âˆˆ ğ’œ.nonMemberSubfamily a â†” s âˆˆ ğ’œ âˆ§ a âˆ‰ s := by
  simp [nonMemberSubfamily]
#align finset.mem_non_member_subfamily Finset.mem_nonMemberSubfamily

@[simp]
theorem mem_memberSubfamily : s âˆˆ ğ’œ.memberSubfamily a â†” insert a s âˆˆ ğ’œ âˆ§ a âˆ‰ s := by
  simp_rw [memberSubfamily, mem_image, mem_filter]
  refine âŸ¨?_, fun h => âŸ¨insert a s, âŸ¨h.1, by simpâŸ©, erase_insert h.2âŸ©âŸ©
  rintro âŸ¨s, âŸ¨hs1, hs2âŸ©, rflâŸ©
  rw [insert_erase hs2]
  exact âŸ¨hs1, not_mem_erase _ _âŸ©
#align finset.mem_member_subfamily Finset.mem_memberSubfamily

theorem nonMemberSubfamily_inter (a : Î±) (ğ’œ â„¬ : Finset (Finset Î±)) :
    (ğ’œ âˆ© â„¬).nonMemberSubfamily a = ğ’œ.nonMemberSubfamily a âˆ© â„¬.nonMemberSubfamily a :=
  filter_inter_distrib _ _ _
#align finset.non_member_subfamily_inter Finset.nonMemberSubfamily_inter

theorem memberSubfamily_inter (a : Î±) (ğ’œ â„¬ : Finset (Finset Î±)) :
    (ğ’œ âˆ© â„¬).memberSubfamily a = ğ’œ.memberSubfamily a âˆ© â„¬.memberSubfamily a := by
  unfold memberSubfamily
  rw [filter_inter_distrib, image_inter_of_injOn _ _ ((erase_injOn' _).mono _)]
  simp
#align finset.member_subfamily_inter Finset.memberSubfamily_inter

theorem nonMemberSubfamily_union (a : Î±) (ğ’œ â„¬ : Finset (Finset Î±)) :
    (ğ’œ âˆª â„¬).nonMemberSubfamily a = ğ’œ.nonMemberSubfamily a âˆª â„¬.nonMemberSubfamily a :=
  filter_union _ _ _
#align finset.non_member_subfamily_union Finset.nonMemberSubfamily_union

theorem memberSubfamily_union (a : Î±) (ğ’œ â„¬ : Finset (Finset Î±)) :
    (ğ’œ âˆª â„¬).memberSubfamily a = ğ’œ.memberSubfamily a âˆª â„¬.memberSubfamily a := by
  simp_rw [memberSubfamily, filter_union, image_union]
#align finset.member_subfamily_union Finset.memberSubfamily_union

theorem card_memberSubfamily_add_card_nonMemberSubfamily (a : Î±) (ğ’œ : Finset (Finset Î±)) :
    (ğ’œ.memberSubfamily a).card + (ğ’œ.nonMemberSubfamily a).card = ğ’œ.card := by
  rw [memberSubfamily, nonMemberSubfamily, card_image_of_injOn]
  Â· conv_rhs => rw [â† filter_card_add_filter_neg_card_eq_card (fun s => (a âˆˆ s))]
  Â· apply (erase_injOn' _).mono
    simp
#align finset.card_member_subfamily_add_card_non_member_subfamily Finset.card_memberSubfamily_add_card_nonMemberSubfamily

theorem memberSubfamily_union_nonMemberSubfamily (a : Î±) (ğ’œ : Finset (Finset Î±)) :
    ğ’œ.memberSubfamily a âˆª ğ’œ.nonMemberSubfamily a = ğ’œ.image fun s => s.erase a := by
  ext s
  simp only [mem_union, mem_memberSubfamily, mem_nonMemberSubfamily, mem_image, exists_prop]
  constructor
  Â· rintro (h | h)
    Â· exact âŸ¨_, h.1, erase_insert h.2âŸ©
    Â· exact âŸ¨_, h.1, erase_eq_of_not_mem h.2âŸ©
  Â· rintro âŸ¨s, hs, rflâŸ©
    by_cases ha : a âˆˆ s
    Â· exact Or.inl âŸ¨by rwa [insert_erase ha], not_mem_erase _ _âŸ©
    Â· exact Or.inr âŸ¨by rwa [erase_eq_of_not_mem ha], not_mem_erase _ _âŸ©
#align finset.member_subfamily_union_non_member_subfamily Finset.memberSubfamily_union_nonMemberSubfamily

@[simp]
theorem memberSubfamily_memberSubfamily : (ğ’œ.memberSubfamily a).memberSubfamily a = âˆ… := by
  ext
  simp
#align finset.member_subfamily_member_subfamily Finset.memberSubfamily_memberSubfamily

@[simp]
theorem memberSubfamily_nonMemberSubfamily : (ğ’œ.nonMemberSubfamily a).memberSubfamily a = âˆ… := by
  ext
  simp
#align finset.member_subfamily_non_member_subfamily Finset.memberSubfamily_nonMemberSubfamily

@[simp]
theorem nonMemberSubfamily_memberSubfamily :
    (ğ’œ.memberSubfamily a).nonMemberSubfamily a = ğ’œ.memberSubfamily a := by
  ext
  simp
#align finset.non_member_subfamily_member_subfamily Finset.nonMemberSubfamily_memberSubfamily

@[simp]
theorem nonMemberSubfamily_nonMemberSubfamily :
    (ğ’œ.nonMemberSubfamily a).nonMemberSubfamily a = ğ’œ.nonMemberSubfamily a := by
  ext
  simp
#align finset.non_member_subfamily_non_member_subfamily Finset.nonMemberSubfamily_nonMemberSubfamily

lemma memberSubfamily_image_insert (hğ’œ : âˆ€ s âˆˆ ğ’œ, a âˆ‰ s) :
    (ğ’œ.image <| insert a).memberSubfamily a = ğ’œ := by
  ext s
  simp only [mem_memberSubfamily, mem_image]
  refine âŸ¨?_, fun hs â†¦ âŸ¨âŸ¨s, hs, rflâŸ©, hğ’œ _ hsâŸ©âŸ©
  rintro âŸ¨âŸ¨t, ht, htsâŸ©, hsâŸ©
  rwa [â† insert_erase_invOn.2.injOn (hğ’œ _ ht) hs hts]

@[simp] lemma nonMemberSubfamily_image_insert : (ğ’œ.image <| insert a).nonMemberSubfamily a = âˆ… := by
  simp [eq_empty_iff_forall_not_mem]

@[simp] lemma memberSubfamily_image_erase : (ğ’œ.image (erase Â· a)).memberSubfamily a = âˆ… := by
  simp [eq_empty_iff_forall_not_mem,
    (ne_of_mem_of_not_mem' (mem_insert_self _ _) (not_mem_erase _ _)).symm]

lemma image_insert_memberSubfamily (ğ’œ : Finset (Finset Î±)) (a : Î±) :
    (ğ’œ.memberSubfamily a).image (insert a) = ğ’œ.filter (a âˆˆ Â·) := by
  ext s
  simp only [mem_memberSubfamily, mem_image, mem_filter]
  refine âŸ¨?_, fun âŸ¨hs, haâŸ© â†¦ âŸ¨erase s a, âŸ¨?_, not_mem_erase _ _âŸ©, insert_erase haâŸ©âŸ©
  Â· rintro âŸ¨s, âŸ¨hs, -âŸ©, rflâŸ©
    exact âŸ¨hs, mem_insert_self _ _âŸ©
  Â· rwa [insert_erase ha]

/-- Induction principle for finset families. To prove a statement for every finset family,
it suffices to prove it for
* the empty finset family.
* the finset family which only contains the empty finset.
* `â„¬ âˆª {s âˆª {a} | s âˆˆ ğ’}` assuming the property for `â„¬` and `ğ’`, where `a` is an element of the
  ground type and `ğ’œ` and `â„¬` are families of finsets not containing `a`.
  Note that instead of giving `â„¬` and `ğ’`, the `subfamily` case gives you
  `ğ’œ = â„¬ âˆª {s âˆª {a} | s âˆˆ ğ’}`, so that `â„¬ = ğ’œ.nonMemberSubfamily` and `ğ’ = ğ’œ.memberSubfamily`.

This is a way of formalising induction on `n` where `ğ’œ` is a finset family on `n` elements.

See also `Finset.family_induction_on.`-/
@[elab_as_elim]
lemma memberFamily_induction_on {p : Finset (Finset Î±) â†’ Prop}
    (ğ’œ : Finset (Finset Î±)) (empty : p âˆ…) (singleton_empty : p {âˆ…})
    (subfamily : âˆ€ (a : Î±) â¦ƒğ’œ : Finset (Finset Î±)â¦„,
      p (ğ’œ.nonMemberSubfamily a) â†’ p (ğ’œ.memberSubfamily a) â†’ p ğ’œ) : p ğ’œ := by
  set u := ğ’œ.sup id
  have hu : âˆ€ s âˆˆ ğ’œ, s âŠ† u := fun s â†¦ le_sup (f := id)
  clear_value u
  induction' u using Finset.induction with a u _ ih generalizing ğ’œ
  Â· simp_rw [subset_empty] at hu
    rw [â† subset_singleton_iff', subset_singleton_iff] at hu
    obtain rfl | rfl := hu <;> assumption
  refine subfamily a (ih _ ?_) (ih _ ?_)
  Â· simp only [mem_nonMemberSubfamily, and_imp]
    exact fun s hs has â†¦ (subset_insert_iff_of_not_mem has).1 <| hu _ hs
  Â· simp only [mem_memberSubfamily, and_imp]
    exact fun s hs ha â†¦ (insert_subset_insert_iff ha).1 <| hu _ hs

/-- Induction principle for finset families. To prove a statement for every finset family,
it suffices to prove it for
* the empty finset family.
* the finset family which only contains the empty finset.
* `{s âˆª {a} | s âˆˆ ğ’œ}` assuming the property for `ğ’œ` a family of finsets not containing `a`.
* `â„¬ âˆª ğ’` assuming the property for `â„¬` and `ğ’`, where `a` is an element of the ground type and
  `â„¬`is a family of finsets not containing `a` and `ğ’` a family of finsets containing `a`.
  Note that instead of giving `â„¬` and `ğ’`, the `subfamily` case gives you `ğ’œ = â„¬ âˆª ğ’`, so that
  `â„¬ = ğ’œ.filter (a âˆ‰ Â·)` and `ğ’ = ğ’œ.filter (a âˆˆ Â·)`.

This is a way of formalising induction on `n` where `ğ’œ` is a finset family on `n` elements.

See also `Finset.memberFamily_induction_on.`-/
@[elab_as_elim]
protected lemma family_induction_on {p : Finset (Finset Î±) â†’ Prop}
    (ğ’œ : Finset (Finset Î±)) (empty : p âˆ…) (singleton_empty : p {âˆ…})
    (image_insert : âˆ€ (a : Î±) â¦ƒğ’œ : Finset (Finset Î±)â¦„,
      (âˆ€ s âˆˆ ğ’œ, a âˆ‰ s) â†’ p ğ’œ â†’ p (ğ’œ.image <| insert a))
    (subfamily : âˆ€ (a : Î±) â¦ƒğ’œ : Finset (Finset Î±)â¦„,
      p (ğ’œ.filter (a âˆ‰ Â·)) â†’ p (ğ’œ.filter (a âˆˆ Â·)) â†’ p ğ’œ) : p ğ’œ := by
  refine memberFamily_induction_on ğ’œ empty singleton_empty fun a ğ’œ hğ’œâ‚€ hğ’œâ‚ â†¦ subfamily a hğ’œâ‚€ ?_
  rw [â† image_insert_memberSubfamily]
  exact image_insert _ (by simp) hğ’œâ‚

end Finset

open Finset

-- The namespace is here to distinguish from other compressions.
namespace Down

/-- `a`-down-compressing `ğ’œ` means removing `a` from the elements of `ğ’œ` that contain it, when the
resulting Finset is not already in `ğ’œ`. -/
def compression (a : Î±) (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  (ğ’œ.filter fun s => erase s a âˆˆ ğ’œ).disjUnion
      ((ğ’œ.image fun s => erase s a).filter fun s => s âˆ‰ ğ’œ) <|
    disjoint_left.2 fun s hâ‚ hâ‚‚ => by
      have := (mem_filter.1 hâ‚‚).2
      exact this (mem_filter.1 hâ‚).1
#align down.compression Down.compression

@[inherit_doc]
scoped[FinsetFamily] notation "ğ““ " => Down.compression
-- Porting note: had to open this
open FinsetFamily

/-- `a` is in the down-compressed family iff it's in the original and its compression is in the
original, or it's not in the original but it's the compression of something in the original. -/
theorem mem_compression : s âˆˆ ğ““ a ğ’œ â†” s âˆˆ ğ’œ âˆ§ s.erase a âˆˆ ğ’œ âˆ¨ s âˆ‰ ğ’œ âˆ§ insert a s âˆˆ ğ’œ := by
  simp_rw [compression, mem_disjUnion, mem_filter, mem_image, and_comm (a := (Â¬ s âˆˆ ğ’œ))]
  refine
    or_congr_right
      (and_congr_left fun hs =>
        âŸ¨?_, fun h => âŸ¨_, h, erase_insert <| insert_ne_self.1 <| ne_of_mem_of_not_mem h hsâŸ©âŸ©)
  rintro âŸ¨t, ht, rflâŸ©
  rwa [insert_erase (erase_ne_self.1 (ne_of_mem_of_not_mem ht hs).symm)]
#align down.mem_compression Down.mem_compression

theorem erase_mem_compression (hs : s âˆˆ ğ’œ) : s.erase a âˆˆ ğ““ a ğ’œ := by
  simp_rw [mem_compression, erase_idem, and_self_iff]
  refine (em _).imp_right fun h => âŸ¨h, ?_âŸ©
  rwa [insert_erase (erase_ne_self.1 (ne_of_mem_of_not_mem hs h).symm)]
#align down.erase_mem_compression Down.erase_mem_compression

-- This is a special case of `erase_mem_compression` once we have `compression_idem`.
theorem erase_mem_compression_of_mem_compression : s âˆˆ ğ““ a ğ’œ â†’ s.erase a âˆˆ ğ““ a ğ’œ := by
  simp_rw [mem_compression, erase_idem]
  refine Or.imp (fun h => âŸ¨h.2, h.2âŸ©) fun h => ?_
  rwa [erase_eq_of_not_mem (insert_ne_self.1 <| ne_of_mem_of_not_mem h.2 h.1)]
#align down.erase_mem_compression_of_mem_compression Down.erase_mem_compression_of_mem_compression

theorem mem_compression_of_insert_mem_compression (h : insert a s âˆˆ ğ““ a ğ’œ) : s âˆˆ ğ““ a ğ’œ := by
  by_cases ha : a âˆˆ s
  Â· rwa [insert_eq_of_mem ha] at h
  Â· rw [â† erase_insert ha]
    exact erase_mem_compression_of_mem_compression h
#align down.mem_compression_of_insert_mem_compression Down.mem_compression_of_insert_mem_compression

/-- Down-compressing a family is idempotent. -/
@[simp]
theorem compression_idem (a : Î±) (ğ’œ : Finset (Finset Î±)) : ğ““ a (ğ““ a ğ’œ) = ğ““ a ğ’œ := by
  ext s
  refine mem_compression.trans âŸ¨?_, fun h => Or.inl âŸ¨h, erase_mem_compression_of_mem_compression hâŸ©âŸ©
  rintro (h | h)
  Â· exact h.1
  Â· cases h.1 (mem_compression_of_insert_mem_compression h.2)
#align down.compression_idem Down.compression_idem

/-- Down-compressing a family doesn't change its size. -/
@[simp]
theorem card_compression (a : Î±) (ğ’œ : Finset (Finset Î±)) : (ğ““ a ğ’œ).card = ğ’œ.card := by
  rw [compression, card_disjUnion, filter_image,
    card_image_of_injOn ((erase_injOn' _).mono fun s hs => _), â† card_union_of_disjoint]
  Â· conv_rhs => rw [â† filter_union_filter_neg_eq (fun s => (erase s a âˆˆ ğ’œ)) ğ’œ]
  Â· exact disjoint_filter_filter_neg ğ’œ ğ’œ (fun s => (erase s a âˆˆ ğ’œ))
  intro s hs
  rw [mem_coe, mem_filter] at hs
  exact not_imp_comm.1 erase_eq_of_not_mem (ne_of_mem_of_not_mem hs.1 hs.2).symm
#align down.card_compression Down.card_compression

end Down
