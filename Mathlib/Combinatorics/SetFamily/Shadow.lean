/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import Mathlib.Data.Finset.Grade
import Mathlib.Data.Finset.Sups
import Mathlib.Logic.Function.Iterate

/-!
# Shadows

This file defines shadows of a set family. The shadow of a set family is the set family of sets we
get by removing any element from any set of the original family. If one pictures `Finset Î±` as a big
hypercube (each dimension being membership of a given element), then taking the shadow corresponds
to projecting each finset down once in all available directions.

## Main definitions

* `Finset.shadow`: The shadow of a set family. Everything we can get by removing a new element from
  some set.
* `Finset.upShadow`: The upper shadow of a set family. Everything we can get by adding an element
  to some set.

## Notation

We define notation in scope `FinsetFamily`:
* `âˆ‚ ğ’œ`: Shadow of `ğ’œ`.
* `âˆ‚âº ğ’œ`: Upper shadow of `ğ’œ`.

We also maintain the convention that `a, b : Î±` are elements of the ground type, `s, t : Finset Î±`
are finsets, and `ğ’œ, â„¬ : Finset (Finset Î±)` are finset families.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, set family
-/


open Finset Nat

variable {Î± : Type*}

namespace Finset

section Shadow

variable [DecidableEq Î±] {ğ’œ â„¬ : Finset (Finset Î±)} {s t : Finset Î±} {a : Î±} {k r : â„•}

/-- The shadow of a set family `ğ’œ` is all sets we can get by removing one element from any set in
`ğ’œ`, and the (`k` times) iterated shadow (`shadow^[k]`) is all sets we can get by removing `k`
elements from any set in `ğ’œ`. -/
def shadow (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  ğ’œ.sup fun s => s.image (erase s)

@[inherit_doc] scoped[FinsetFamily] notation:max "âˆ‚ " => Finset.shadow

open FinsetFamily

/-- The shadow of the empty set is empty. -/
@[simp]
theorem shadow_empty : âˆ‚ (âˆ… : Finset (Finset Î±)) = âˆ… :=
  rfl

@[simp] lemma shadow_iterate_empty (k : â„•) : âˆ‚^[k] (âˆ… : Finset (Finset Î±)) = âˆ… := by
  induction k <;> simp [*, shadow_empty]

@[simp]
theorem shadow_singleton_empty : âˆ‚ ({âˆ…} : Finset (Finset Î±)) = âˆ… :=
  rfl

@[simp]
theorem shadow_singleton (a : Î±) : âˆ‚ {{a}} = {âˆ…} := by
  simp [shadow]

/-- The shadow is monotone. -/
@[mono]
theorem shadow_monotone : Monotone (shadow : Finset (Finset Î±) â†’ Finset (Finset Î±)) := fun _ _ =>
  sup_mono

@[gcongr] lemma shadow_mono (hğ’œâ„¬ : ğ’œ âŠ† â„¬) : âˆ‚ ğ’œ âŠ† âˆ‚ â„¬ := shadow_monotone hğ’œâ„¬

/-- `t` is in the shadow of `ğ’œ` iff there is a `s âˆˆ ğ’œ` from which we can remove one element to
get `t`. -/
lemma mem_shadow_iff : t âˆˆ âˆ‚ ğ’œ â†” âˆƒ s âˆˆ ğ’œ, âˆƒ a âˆˆ s, erase s a = t := by
  simp only [shadow, mem_sup, mem_image]

theorem erase_mem_shadow (hs : s âˆˆ ğ’œ) (ha : a âˆˆ s) : erase s a âˆˆ âˆ‚ ğ’œ :=
  mem_shadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©

/-- `t âˆˆ âˆ‚ğ’œ` iff `t` is exactly one element less than something from `ğ’œ`.

See also `Finset.mem_shadow_iff_exists_mem_card_add_one`. -/
lemma mem_shadow_iff_exists_sdiff : t âˆˆ âˆ‚ ğ’œ â†” âˆƒ s âˆˆ ğ’œ, t âŠ† s âˆ§ #(s \ t) = 1 := by
  simp_rw [mem_shadow_iff, â† covBy_iff_card_sdiff_eq_one, covBy_iff_exists_erase]

/-- `t` is in the shadow of `ğ’œ` iff we can add an element to it so that the resulting finset is in
`ğ’œ`. -/
lemma mem_shadow_iff_insert_mem : t âˆˆ âˆ‚ ğ’œ â†” âˆƒ a âˆ‰ t, insert a t âˆˆ ğ’œ := by
  simp_rw [mem_shadow_iff_exists_sdiff, â† covBy_iff_card_sdiff_eq_one, covBy_iff_exists_insert]
  aesop

/-- `s âˆˆ âˆ‚ ğ’œ` iff `s` is exactly one element less than something from `ğ’œ`.

See also `Finset.mem_shadow_iff_exists_sdiff`. -/
lemma mem_shadow_iff_exists_mem_card_add_one : t âˆˆ âˆ‚ ğ’œ â†” âˆƒ s âˆˆ ğ’œ, t âŠ† s âˆ§ #s = #t + 1 := by
  refine mem_shadow_iff_exists_sdiff.trans <| exists_congr fun t â†¦ and_congr_right fun _ â†¦
    and_congr_right fun hst â†¦ ?_
  rw [card_sdiff_of_subset hst, tsub_eq_iff_eq_add_of_le, add_comm]
  exact card_mono hst

lemma mem_shadow_iterate_iff_exists_card :
    t âˆˆ âˆ‚^[k] ğ’œ â†” âˆƒ u : Finset Î±, #u = k âˆ§ Disjoint t u âˆ§ t âˆª u âˆˆ ğ’œ := by
  induction k generalizing t with
  | zero => simp
  | succ k ih =>
    simp only [mem_shadow_iff_insert_mem, ih, Function.iterate_succ_apply', card_eq_succ]
    aesop

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements less than something from `ğ’œ`.

See also `Finset.mem_shadow_iff_exists_mem_card_add`. -/
lemma mem_shadow_iterate_iff_exists_sdiff : t âˆˆ âˆ‚^[k] ğ’œ â†” âˆƒ s âˆˆ ğ’œ, t âŠ† s âˆ§ #(s \ t) = k := by
  rw [mem_shadow_iterate_iff_exists_card]
  constructor
  Â· rintro âŸ¨u, rfl, htu, hsuAâŸ©
    exact âŸ¨_, hsuA, subset_union_left, by rw [union_sdiff_cancel_left htu]âŸ©
  Â· rintro âŸ¨s, hs, hts, rflâŸ©
    refine âŸ¨s \ t, rfl, disjoint_sdiff, ?_âŸ©
    rwa [union_sdiff_self_eq_union, union_eq_right.2 hts]

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements less than something in `ğ’œ`.

See also `Finset.mem_shadow_iterate_iff_exists_sdiff`. -/
lemma mem_shadow_iterate_iff_exists_mem_card_add :
    t âˆˆ âˆ‚^[k] ğ’œ â†” âˆƒ s âˆˆ ğ’œ, t âŠ† s âˆ§ #s = #t + k := by
  refine mem_shadow_iterate_iff_exists_sdiff.trans <| exists_congr fun t â†¦ and_congr_right fun _ â†¦
    and_congr_right fun hst â†¦ ?_
  rw [card_sdiff_of_subset hst, tsub_eq_iff_eq_add_of_le, add_comm]
  exact card_mono hst

/-- The shadow of a family of `r`-sets is a family of `r - 1`-sets. -/
protected theorem _root_.Set.Sized.shadow (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    (âˆ‚ ğ’œ : Set (Finset Î±)).Sized (r - 1) := by
  intro A h
  obtain âŸ¨A, hA, i, hi, rflâŸ© := mem_shadow_iff.1 h
  rw [card_erase_of_mem hi, hğ’œ hA]

/-- The `k`-th shadow of a family of `r`-sets is a family of `r - k`-sets. -/
lemma _root_.Set.Sized.shadow_iterate (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    (âˆ‚^[k] ğ’œ : Set (Finset Î±)).Sized (r - k) := by
  simp_rw [Set.Sized, mem_coe, mem_shadow_iterate_iff_exists_sdiff]
  rintro t âŸ¨s, hs, hts, rflâŸ©
  rw [card_sdiff_of_subset hts, â† hğ’œ hs, Nat.sub_sub_self (card_le_card hts)]

theorem sized_shadow_iff (h : âˆ… âˆ‰ ğ’œ) :
    (âˆ‚ ğ’œ : Set (Finset Î±)).Sized r â†” (ğ’œ : Set (Finset Î±)).Sized (r + 1) := by
  refine âŸ¨fun hğ’œ s hs => ?_, Set.Sized.shadowâŸ©
  obtain âŸ¨a, haâŸ© := nonempty_iff_ne_empty.2 (ne_of_mem_of_not_mem hs h)
  rw [â† hğ’œ (erase_mem_shadow hs ha), card_erase_add_one ha]

/-- Being in the shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
lemma exists_subset_of_mem_shadow (hs : t âˆˆ âˆ‚ ğ’œ) : âˆƒ s âˆˆ ğ’œ, t âŠ† s :=
  let âŸ¨t, ht, hstâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 hs
  âŸ¨t, ht, hst.1âŸ©

end Shadow

open FinsetFamily

section UpShadow

variable [DecidableEq Î±] [Fintype Î±] {ğ’œ : Finset (Finset Î±)} {s t : Finset Î±} {a : Î±} {k r : â„•}

/-- The upper shadow of a set family `ğ’œ` is all sets we can get by adding one element to any set in
`ğ’œ`, and the (`k` times) iterated upper shadow (`upShadow^[k]`) is all sets we can get by adding
`k` elements from any set in `ğ’œ`. -/
def upShadow (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  ğ’œ.sup fun s => sá¶œ.image fun a => insert a s

@[inherit_doc] scoped[FinsetFamily] notation:max "âˆ‚âº " => Finset.upShadow

/-- The upper shadow of the empty set is empty. -/
@[simp]
theorem upShadow_empty : âˆ‚âº (âˆ… : Finset (Finset Î±)) = âˆ… :=
  rfl

/-- The upper shadow is monotone. -/
@[mono]
theorem upShadow_monotone : Monotone (upShadow : Finset (Finset Î±) â†’ Finset (Finset Î±)) :=
  fun _ _ => sup_mono

/-- `t` is in the upper shadow of `ğ’œ` iff there is a `s âˆˆ ğ’œ` from which we can remove one element
to get `t`. -/
lemma mem_upShadow_iff : t âˆˆ âˆ‚âº ğ’œ â†” âˆƒ s âˆˆ ğ’œ, âˆƒ a âˆ‰ s, insert a s = t := by
  simp_rw [upShadow, mem_sup, mem_image, mem_compl]

theorem insert_mem_upShadow (hs : s âˆˆ ğ’œ) (ha : a âˆ‰ s) : insert a s âˆˆ âˆ‚âº ğ’œ :=
  mem_upShadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©

/-- `t` is in the upper shadow of `ğ’œ` iff `t` is exactly one element more than something from `ğ’œ`.

See also `Finset.mem_upShadow_iff_exists_mem_card_add_one`. -/
lemma mem_upShadow_iff_exists_sdiff : t âˆˆ âˆ‚âº ğ’œ â†” âˆƒ s âˆˆ ğ’œ, s âŠ† t âˆ§ #(t \ s) = 1 := by
  simp_rw [mem_upShadow_iff, â† covBy_iff_card_sdiff_eq_one, covBy_iff_exists_insert]

/-- `t` is in the upper shadow of `ğ’œ` iff we can remove an element from it so that the resulting
finset is in `ğ’œ`. -/
lemma mem_upShadow_iff_erase_mem : t âˆˆ âˆ‚âº ğ’œ â†” âˆƒ a, a âˆˆ t âˆ§ erase t a âˆˆ ğ’œ := by
  simp_rw [mem_upShadow_iff_exists_sdiff, â† covBy_iff_card_sdiff_eq_one, covBy_iff_exists_erase]
  aesop

/-- `t` is in the upper shadow of `ğ’œ` iff `t` is exactly one element less than something from `ğ’œ`.

See also `Finset.mem_upShadow_iff_exists_sdiff`. -/
lemma mem_upShadow_iff_exists_mem_card_add_one :
    t âˆˆ âˆ‚âº ğ’œ â†” âˆƒ s âˆˆ ğ’œ, s âŠ† t âˆ§ #t = #s + 1 := by
  refine mem_upShadow_iff_exists_sdiff.trans <| exists_congr fun t â†¦ and_congr_right fun _ â†¦
    and_congr_right fun hst â†¦ ?_
  rw [card_sdiff_of_subset hst, tsub_eq_iff_eq_add_of_le, add_comm]
  exact card_mono hst

lemma mem_upShadow_iterate_iff_exists_card :
    t âˆˆ âˆ‚âº^[k] ğ’œ â†” âˆƒ u : Finset Î±, #u = k âˆ§ u âŠ† t âˆ§ t \ u âˆˆ ğ’œ := by
  induction k generalizing t with
  | zero => simp
  | succ k ih =>
    simp only [mem_upShadow_iff_erase_mem, ih, Function.iterate_succ_apply', card_eq_succ,
      subset_erase, erase_sdiff_comm, â† sdiff_insert]
    constructor
    Â· rintro âŸ¨a, hat, u, rfl, âŸ¨hut, hauâŸ©, htuâŸ©
      exact âŸ¨_, âŸ¨_, _, hau, rfl, rflâŸ©, insert_subset hat hut, htuâŸ©
    Â· rintro âŸ¨_, âŸ¨a, u, hau, rfl, rflâŸ©, hut, htuâŸ©
      rw [insert_subset_iff] at hut
      exact âŸ¨a, hut.1, _, rfl, âŸ¨hut.2, hauâŸ©, htuâŸ©

/-- `t` is in the upper shadow of `ğ’œ` iff `t` is exactly `k` elements less than something from `ğ’œ`.

See also `Finset.mem_upShadow_iff_exists_mem_card_add`. -/
lemma mem_upShadow_iterate_iff_exists_sdiff : t âˆˆ âˆ‚âº^[k] ğ’œ â†” âˆƒ s âˆˆ ğ’œ, s âŠ† t âˆ§ #(t \ s) = k := by
  rw [mem_upShadow_iterate_iff_exists_card]
  constructor
  Â· rintro âŸ¨u, rfl, hut, htuâŸ©
    exact âŸ¨_, htu, sdiff_subset, by rw [sdiff_sdiff_eq_self hut]âŸ©
  Â· rintro âŸ¨s, hs, hst, rflâŸ©
    exact âŸ¨_, rfl, sdiff_subset, by rwa [sdiff_sdiff_eq_self hst]âŸ©

/-- `t âˆˆ âˆ‚âº^k ğ’œ` iff `t` is exactly `k` elements less than something in `ğ’œ`.

See also `Finset.mem_upShadow_iterate_iff_exists_sdiff`. -/
lemma mem_upShadow_iterate_iff_exists_mem_card_add :
    t âˆˆ âˆ‚âº^[k] ğ’œ â†” âˆƒ s âˆˆ ğ’œ, s âŠ† t âˆ§ #t = #s + k := by
  refine mem_upShadow_iterate_iff_exists_sdiff.trans <| exists_congr fun t â†¦ and_congr_right fun _ â†¦
    and_congr_right fun hst â†¦ ?_
  rw [card_sdiff_of_subset hst, tsub_eq_iff_eq_add_of_le, add_comm]
  exact card_mono hst

/-- The upper shadow of a family of `r`-sets is a family of `r + 1`-sets. -/
protected lemma _root_.Set.Sized.upShadow (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    (âˆ‚âº ğ’œ : Set (Finset Î±)).Sized (r + 1) := by
  intro A h
  obtain âŸ¨A, hA, i, hi, rflâŸ© := mem_upShadow_iff.1 h
  rw [card_insert_of_notMem hi, hğ’œ hA]

/-- Being in the upper shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
theorem exists_subset_of_mem_upShadow (hs : s âˆˆ âˆ‚âº ğ’œ) : âˆƒ t âˆˆ ğ’œ, t âŠ† s :=
  let âŸ¨t, ht, hts, _âŸ© := mem_upShadow_iff_exists_mem_card_add_one.1 hs
  âŸ¨t, ht, htsâŸ©

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements more than something in `ğ’œ`. -/
theorem mem_upShadow_iff_exists_mem_card_add :
    s âˆˆ âˆ‚âº ^[k] ğ’œ â†” âˆƒ t âˆˆ ğ’œ, t âŠ† s âˆ§ #t + k = #s := by
  induction k generalizing ğ’œ s with
  | zero =>
    refine âŸ¨fun hs => âŸ¨s, hs, Subset.refl _, rflâŸ©, ?_âŸ©
    rintro âŸ¨t, ht, hst, hcardâŸ©
    rwa [â† eq_of_subset_of_card_le hst hcard.ge]
  | succ k ih =>
    simp only [Function.comp_apply, Function.iterate_succ]
    refine ih.trans ?_
    clear ih
    constructor
    Â· rintro âŸ¨t, ht, hts, hcardstâŸ©
      obtain âŸ¨u, hu, hut, hcardtuâŸ© := mem_upShadow_iff_exists_mem_card_add_one.1 ht
      refine âŸ¨u, hu, hut.trans hts, ?_âŸ©
      rw [â† hcardst, hcardtu, add_right_comm]
      rfl
    Â· rintro âŸ¨t, ht, hts, hcardâŸ©
      obtain âŸ¨u, htu, hus, huâŸ© := Finset.exists_subsuperset_card_eq hts (Nat.le_add_right _ 1)
        (by omega)
      refine âŸ¨u, mem_upShadow_iff_exists_mem_card_add_one.2 âŸ¨t, ht, htu, huâŸ©, hus, ?_âŸ©
      rw [hu, â† hcard, add_right_comm]
      rfl

@[simp] lemma shadow_compls : âˆ‚ ğ’œá¶œË¢ = (âˆ‚âº ğ’œ)á¶œË¢ := by
  ext s
  simp only [mem_shadow_iff, mem_upShadow_iff, mem_compls]
  refine (compl_involutive.toPerm _).exists_congr_left.trans ?_
  simp [â† compl_involutive.eq_iff]

@[simp] lemma upShadow_compls : âˆ‚âº ğ’œá¶œË¢ = (âˆ‚ ğ’œ)á¶œË¢ := by
  ext s
  simp only [mem_shadow_iff, mem_upShadow_iff, mem_compls]
  refine (compl_involutive.toPerm _).exists_congr_left.trans ?_
  simp [â† compl_involutive.eq_iff]

end UpShadow

end Finset
