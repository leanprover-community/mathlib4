/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import Mathlib.Data.Finset.Slice
import Mathlib.Logic.Function.Iterate

#align_import combinatorics.set_family.shadow from "leanprover-community/mathlib"@"f7fc89d5d5ff1db2d1242c7bb0e9062ce47ef47c"

/-!
# Shadows

This file defines shadows of a set family. The shadow of a set family is the set family of sets we
get by removing any element from any set of the original family. If one pictures `Finset Î±` as a big
hypercube (each dimension being membership of a given element), then taking the shadow corresponds
to projecting each finset down once in all available directions.

## Main definitions

* `Finset.shadow`: The shadow of a set family. Everything we can get by removing a new element from
  some set.
* `Finset.up_shadow`: The upper shadow of a set family. Everything we can get by adding an element
  to some set.

## Notation

We define notation in locale `FinsetFamily`:
* `âˆ‚ ğ’œ`: Shadow of `ğ’œ`.
* `âˆ‚âº ğ’œ`: Upper shadow of `ğ’œ`.

We also maintain the convention that `a, b : Î±` are elements of the ground type, `s, t : Finset Î±`
are finsets, and `ğ’œ, â„¬ : Finset (Finset Î±)` are finset families.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, set family
-/


open Finset Nat

variable {Î± : Type*}

namespace Finset

section Shadow

variable [DecidableEq Î±] {ğ’œ : Finset (Finset Î±)} {s t : Finset Î±} {a : Î±} {k r : â„•}

/-- The shadow of a set family `ğ’œ` is all sets we can get by removing one element from any set in
`ğ’œ`, and the (`k` times) iterated shadow (`shadow^[k]`) is all sets we can get by removing `k`
elements from any set in `ğ’œ`. -/
def shadow (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  ğ’œ.sup fun s => s.image (erase s)
#align finset.shadow Finset.shadow

-- Porting note: added `inherit_doc` to calm linter
@[inherit_doc] scoped[FinsetFamily] notation:max "âˆ‚ " => Finset.shadow
-- Porting note: had to open FinsetFamily
open FinsetFamily

/-- The shadow of the empty set is empty. -/
@[simp]
theorem shadow_empty : âˆ‚ (âˆ… : Finset (Finset Î±)) = âˆ… :=
  rfl
#align finset.shadow_empty Finset.shadow_empty

@[simp]
theorem shadow_singleton_empty : âˆ‚ ({âˆ…} : Finset (Finset Î±)) = âˆ… :=
  rfl
#align finset.shadow_singleton_empty Finset.shadow_singleton_empty

--TODO: Prove `âˆ‚ {{a}} = {âˆ…}` quickly using `covers` and `GradeOrder`
/-- The shadow is monotone. -/
@[mono]
theorem shadow_monotone : Monotone (shadow : Finset (Finset Î±) â†’ Finset (Finset Î±)) := fun _ _ =>
  sup_mono
#align finset.shadow_monotone Finset.shadow_monotone

/-- `s` is in the shadow of `ğ’œ` iff there is a `t âˆˆ ğ’œ` from which we can remove one element to
get `s`. -/
theorem mem_shadow_iff : s âˆˆ âˆ‚ ğ’œ â†” âˆƒ t âˆˆ ğ’œ, âˆƒ a âˆˆ t, erase t a = s := by
  simp only [shadow, mem_sup, mem_image]
  -- ğŸ‰ no goals
#align finset.mem_shadow_iff Finset.mem_shadow_iff

theorem erase_mem_shadow (hs : s âˆˆ ğ’œ) (ha : a âˆˆ s) : erase s a âˆˆ âˆ‚ ğ’œ :=
  mem_shadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©
#align finset.erase_mem_shadow Finset.erase_mem_shadow

/-- `t` is in the shadow of `ğ’œ` iff we can add an element to it so that the resulting finset is in
`ğ’œ`. -/
theorem mem_shadow_iff_insert_mem : s âˆˆ âˆ‚ ğ’œ â†” âˆƒ (a : _) (_ : a âˆ‰ s), insert a s âˆˆ ğ’œ := by
  refine' mem_shadow_iff.trans âŸ¨_, _âŸ©
  -- âŠ¢ (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a, a âˆˆ t âˆ§ erase t a = s) â†’ âˆƒ a x, insert a s âˆˆ ğ’œ
  Â· rintro âŸ¨s, hs, a, ha, rflâŸ©
    -- âŠ¢ âˆƒ a_1 x, insert a_1 (erase s a) âˆˆ ğ’œ
    refine' âŸ¨a, not_mem_erase a s, _âŸ©
    -- âŠ¢ insert a (erase s a) âˆˆ ğ’œ
    rwa [insert_erase ha]
    -- ğŸ‰ no goals
  Â· rintro âŸ¨a, ha, hsâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a, a âˆˆ t âˆ§ erase t a = s
    exact âŸ¨insert a s, hs, a, mem_insert_self _ _, erase_insert haâŸ©
    -- ğŸ‰ no goals
#align finset.mem_shadow_iff_insert_mem Finset.mem_shadow_iff_insert_mem

/-- The shadow of a family of `r`-sets is a family of `r - 1`-sets. -/
protected theorem Set.Sized.shadow (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    (âˆ‚ ğ’œ : Set (Finset Î±)).Sized (r - 1) := by
  intro A h
  -- âŠ¢ card A = r - 1
  obtain âŸ¨A, hA, i, hi, rflâŸ© := mem_shadow_iff.1 h
  -- âŠ¢ card (erase A i) = r - 1
  rw [card_erase_of_mem hi, hğ’œ hA]
  -- ğŸ‰ no goals
#align finset.set.sized.shadow Finset.Set.Sized.shadow

theorem sized_shadow_iff (h : âˆ… âˆ‰ ğ’œ) :
    (âˆ‚ ğ’œ : Set (Finset Î±)).Sized r â†” (ğ’œ : Set (Finset Î±)).Sized (r + 1) := by
  refine' âŸ¨fun hğ’œ s hs => _, Set.Sized.shadowâŸ©
  -- âŠ¢ card s = r + 1
  obtain âŸ¨a, haâŸ© := nonempty_iff_ne_empty.2 (ne_of_mem_of_not_mem hs h)
  -- âŠ¢ card s = r + 1
  rw [â† hğ’œ (erase_mem_shadow hs ha), card_erase_add_one ha]
  -- ğŸ‰ no goals
#align finset.sized_shadow_iff Finset.sized_shadow_iff

/-- `s âˆˆ âˆ‚ ğ’œ` iff `s` is exactly one element less than something from `ğ’œ` -/
theorem mem_shadow_iff_exists_mem_card_add_one :
    s âˆˆ âˆ‚ ğ’œ â†” âˆƒ t âˆˆ ğ’œ, s âŠ† t âˆ§ t.card = s.card + 1 := by
  refine' mem_shadow_iff_insert_mem.trans âŸ¨_, _âŸ©
  -- âŠ¢ (âˆƒ a x, insert a s âˆˆ ğ’œ) â†’ âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + 1
  Â· rintro âŸ¨a, ha, hsâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + 1
    exact âŸ¨insert a s, hs, subset_insert _ _, card_insert_of_not_mem haâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨t, ht, hst, hâŸ©
    -- âŠ¢ âˆƒ a x, insert a s âˆˆ ğ’œ
    obtain âŸ¨a, haâŸ© : âˆƒ a, t \ s = {a} :=
      card_eq_one.1 (by rw [card_sdiff hst, h, add_tsub_cancel_left])
    exact
      âŸ¨a, fun hat => not_mem_sdiff_of_mem_right hat (ha.superset <| mem_singleton_self a),
       by rwa [insert_eq a s, â† ha, sdiff_union_of_subset hst]âŸ©
#align finset.mem_shadow_iff_exists_mem_card_add_one Finset.mem_shadow_iff_exists_mem_card_add_one

/-- Being in the shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
theorem exists_subset_of_mem_shadow (hs : s âˆˆ âˆ‚ ğ’œ) : âˆƒ t âˆˆ ğ’œ, s âŠ† t :=
  let âŸ¨t, ht, hstâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 hs
  âŸ¨t, ht, hst.1âŸ©
#align finset.exists_subset_of_mem_shadow Finset.exists_subset_of_mem_shadow

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements less than something in `ğ’œ`. -/
theorem mem_shadow_iff_exists_mem_card_add :
    s âˆˆ âˆ‚ ^[k] ğ’œ â†” âˆƒ t âˆˆ ğ’œ, s âŠ† t âˆ§ t.card = s.card + k := by
  induction' k with k ih generalizing ğ’œ s
  -- âŠ¢ s âˆˆ âˆ‚ ^[zero] ğ’œ â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + zero
  Â· refine' âŸ¨fun hs => âŸ¨s, hs, Subset.refl _, rflâŸ©, _âŸ©
    -- âŠ¢ (âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + zero) â†’ s âˆˆ âˆ‚ ^[zero] ğ’œ
    rintro âŸ¨t, ht, hst, hcardâŸ©
    -- âŠ¢ s âˆˆ âˆ‚ ^[zero] ğ’œ
    rwa [eq_of_subset_of_card_le hst hcard.le]
    -- ğŸ‰ no goals
  simp only [exists_prop, Function.comp_apply, Function.iterate_succ]
  -- âŠ¢ s âˆˆ âˆ‚ ^[k] (âˆ‚ ğ’œ) â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + succ k
  refine' ih.trans _
  -- âŠ¢ (âˆƒ t, t âˆˆ âˆ‚ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + k) â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = â€¦
  clear ih
  -- âŠ¢ (âˆƒ t, t âˆˆ âˆ‚ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + k) â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = â€¦
  constructor
  -- âŠ¢ (âˆƒ t, t âˆˆ âˆ‚ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + k) â†’ âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = â€¦
  Â· rintro âŸ¨t, ht, hst, hcardstâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + succ k
    obtain âŸ¨u, hu, htu, hcardtuâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 ht
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + succ k
    refine' âŸ¨u, hu, hst.trans htu, _âŸ©
    -- âŠ¢ card u = card s + succ k
    rw [hcardtu, hcardst]
    -- âŠ¢ card s + k + 1 = card s + succ k
    rfl
    -- ğŸ‰ no goals
  Â· rintro âŸ¨t, ht, hst, hcardâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ âˆ‚ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + k
    obtain âŸ¨u, hsu, hut, huâŸ© :=
      Finset.exists_intermediate_set k
        (by
          rw [add_comm, hcard]
          exact le_succ _)
        hst
    rw [add_comm] at hu
    -- âŠ¢ âˆƒ t, t âˆˆ âˆ‚ ğ’œ âˆ§ s âŠ† t âˆ§ card t = card s + k
    refine' âŸ¨u, mem_shadow_iff_exists_mem_card_add_one.2 âŸ¨t, ht, hut, _âŸ©, hsu, huâŸ©
    -- âŠ¢ card t = card u + 1
    rw [hcard, hu]
    -- âŠ¢ card s + succ k = card s + k + 1
    rfl
    -- ğŸ‰ no goals
#align finset.mem_shadow_iff_exists_mem_card_add Finset.mem_shadow_iff_exists_mem_card_add

end Shadow

open FinsetFamily

section UpShadow

variable [DecidableEq Î±] [Fintype Î±] {ğ’œ : Finset (Finset Î±)} {s t : Finset Î±} {a : Î±} {k r : â„•}

/-- The upper shadow of a set family `ğ’œ` is all sets we can get by adding one element to any set in
`ğ’œ`, and the (`k` times) iterated upper shadow (`upShadow^[k]`) is all sets we can get by adding
`k` elements from any set in `ğ’œ`. -/
def upShadow (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  ğ’œ.sup fun s => sá¶œ.image fun a => insert a s
#align finset.up_shadow Finset.upShadow

-- Porting note: added `inherit_doc` to calm linter
@[inherit_doc] scoped[FinsetFamily] notation:max "âˆ‚âº " => Finset.upShadow

/-- The upper shadow of the empty set is empty. -/
@[simp]
theorem upShadow_empty : âˆ‚âº (âˆ… : Finset (Finset Î±)) = âˆ… :=
  rfl
#align finset.up_shadow_empty Finset.upShadow_empty

/-- The upper shadow is monotone. -/
@[mono]
theorem upShadow_monotone : Monotone (upShadow : Finset (Finset Î±) â†’ Finset (Finset Î±)) :=
  fun _ _ => sup_mono
#align finset.up_shadow_monotone Finset.upShadow_monotone

/-- `s` is in the upper shadow of `ğ’œ` iff there is a `t âˆˆ ğ’œ` from which we can remove one element
to get `s`. -/
theorem mem_upShadow_iff : s âˆˆ âˆ‚âº ğ’œ â†” âˆƒ t âˆˆ ğ’œ, âˆƒ (a : _) (_ : a âˆ‰ t), insert a t = s := by
  simp_rw [upShadow, mem_sup, mem_image, exists_prop, mem_compl]
  -- ğŸ‰ no goals
#align finset.mem_up_shadow_iff Finset.mem_upShadow_iff

theorem insert_mem_upShadow (hs : s âˆˆ ğ’œ) (ha : a âˆ‰ s) : insert a s âˆˆ âˆ‚âº ğ’œ :=
  mem_upShadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©
#align finset.insert_mem_up_shadow Finset.insert_mem_upShadow

/-- The upper shadow of a family of `r`-sets is a family of `r + 1`-sets. -/
protected theorem Set.Sized.upShadow (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    (âˆ‚âº ğ’œ : Set (Finset Î±)).Sized (r + 1) := by
  intro A h
  -- âŠ¢ card A = r + 1
  obtain âŸ¨A, hA, i, hi, rflâŸ© := mem_upShadow_iff.1 h
  -- âŠ¢ card (insert i A) = r + 1
  rw [card_insert_of_not_mem hi, hğ’œ hA]
  -- ğŸ‰ no goals
#align finset.set.sized.up_shadow Finset.Set.Sized.upShadow

/-- `t` is in the upper shadow of `ğ’œ` iff we can remove an element from it so that the resulting
finset is in `ğ’œ`. -/
theorem mem_upShadow_iff_erase_mem : s âˆˆ âˆ‚âº ğ’œ â†” âˆƒ a âˆˆ s, s.erase a âˆˆ ğ’œ := by
  refine' mem_upShadow_iff.trans âŸ¨_, _âŸ©
  -- âŠ¢ (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a x, insert a t = s) â†’ âˆƒ a, a âˆˆ s âˆ§ erase s a âˆˆ ğ’œ
  Â· rintro âŸ¨s, hs, a, ha, rflâŸ©
    -- âŠ¢ âˆƒ a_1, a_1 âˆˆ insert a s âˆ§ erase (insert a s) a_1 âˆˆ ğ’œ
    refine' âŸ¨a, mem_insert_self a s, _âŸ©
    -- âŠ¢ erase (insert a s) a âˆˆ ğ’œ
    rwa [erase_insert ha]
    -- ğŸ‰ no goals
  Â· rintro âŸ¨a, ha, hsâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a x, insert a t = s
    exact âŸ¨s.erase a, hs, a, not_mem_erase _ _, insert_erase haâŸ©
    -- ğŸ‰ no goals
#align finset.mem_up_shadow_iff_erase_mem Finset.mem_upShadow_iff_erase_mem

/-- `s âˆˆ âˆ‚âº ğ’œ` iff `s` is exactly one element less than something from `ğ’œ`. -/
theorem mem_upShadow_iff_exists_mem_card_add_one :
    s âˆˆ âˆ‚âº ğ’œ â†” âˆƒ t âˆˆ ğ’œ, t âŠ† s âˆ§ t.card + 1 = s.card := by
  refine' mem_upShadow_iff_erase_mem.trans âŸ¨_, _âŸ©
  -- âŠ¢ (âˆƒ a, a âˆˆ s âˆ§ erase s a âˆˆ ğ’œ) â†’ âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + 1 = card s
  Â· rintro âŸ¨a, ha, hsâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + 1 = card s
    exact âŸ¨s.erase a, hs, erase_subset _ _, card_erase_add_one haâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨t, ht, hts, hâŸ©
    -- âŠ¢ âˆƒ a, a âˆˆ s âˆ§ erase s a âˆˆ ğ’œ
    obtain âŸ¨a, haâŸ© : âˆƒ a, s \ t = {a} :=
      card_eq_one.1 (by rw [card_sdiff hts, â† h, add_tsub_cancel_left])
    refine' âŸ¨a, sdiff_subset _ _ ((ha.ge : _ âŠ† _) <| mem_singleton_self a), _âŸ©
    -- âŠ¢ erase s a âˆˆ ğ’œ
    rwa [â† sdiff_singleton_eq_erase, â† ha, sdiff_sdiff_eq_self hts]
    -- ğŸ‰ no goals
#align finset.mem_up_shadow_iff_exists_mem_card_add_one Finset.mem_upShadow_iff_exists_mem_card_add_one

/-- Being in the upper shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
theorem exists_subset_of_mem_upShadow (hs : s âˆˆ âˆ‚âº ğ’œ) : âˆƒ t âˆˆ ğ’œ, t âŠ† s :=
  let âŸ¨t, ht, hts, _âŸ© := mem_upShadow_iff_exists_mem_card_add_one.1 hs
  âŸ¨t, ht, htsâŸ©
#align finset.exists_subset_of_mem_up_shadow Finset.exists_subset_of_mem_upShadow

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements more than something in `ğ’œ`. -/
theorem mem_upShadow_iff_exists_mem_card_add :
    s âˆˆ âˆ‚âº ^[k] ğ’œ â†” âˆƒ t âˆˆ ğ’œ, t âŠ† s âˆ§ t.card + k = s.card := by
  induction' k with k ih generalizing ğ’œ s
  -- âŠ¢ s âˆˆ âˆ‚âº ^[zero] ğ’œ â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + zero = card s
  Â· refine' âŸ¨fun hs => âŸ¨s, hs, Subset.refl _, rflâŸ©, _âŸ©
    -- âŠ¢ (âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + zero = card s) â†’ s âˆˆ âˆ‚âº ^[zero] ğ’œ
    rintro âŸ¨t, ht, hst, hcardâŸ©
    -- âŠ¢ s âˆˆ âˆ‚âº ^[zero] ğ’œ
    rwa [â† eq_of_subset_of_card_le hst hcard.ge]
    -- ğŸ‰ no goals
  simp only [exists_prop, Function.comp_apply, Function.iterate_succ]
  -- âŠ¢ s âˆˆ âˆ‚âº ^[k] (âˆ‚âº ğ’œ) â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + succ k = card s
  refine' ih.trans _
  -- âŠ¢ (âˆƒ t, t âˆˆ âˆ‚âº ğ’œ âˆ§ t âŠ† s âˆ§ card t + k = card s) â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t  â€¦
  clear ih
  -- âŠ¢ (âˆƒ t, t âˆˆ âˆ‚âº ğ’œ âˆ§ t âŠ† s âˆ§ card t + k = card s) â†” âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t  â€¦
  constructor
  -- âŠ¢ (âˆƒ t, t âˆˆ âˆ‚âº ğ’œ âˆ§ t âŠ† s âˆ§ card t + k = card s) â†’ âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t  â€¦
  Â· rintro âŸ¨t, ht, hts, hcardstâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + succ k = card s
    obtain âŸ¨u, hu, hut, hcardtuâŸ© := mem_upShadow_iff_exists_mem_card_add_one.1 ht
    -- âŠ¢ âˆƒ t, t âˆˆ ğ’œ âˆ§ t âŠ† s âˆ§ card t + succ k = card s
    refine' âŸ¨u, hu, hut.trans hts, _âŸ©
    -- âŠ¢ card u + succ k = card s
    rw [â† hcardst, â† hcardtu, add_right_comm]
    -- âŠ¢ card u + succ k = card u + k + 1
    rfl
    -- ğŸ‰ no goals
  Â· rintro âŸ¨t, ht, hts, hcardâŸ©
    -- âŠ¢ âˆƒ t, t âˆˆ âˆ‚âº ğ’œ âˆ§ t âŠ† s âˆ§ card t + k = card s
    obtain âŸ¨u, htu, hus, huâŸ© :=
      Finset.exists_intermediate_set 1
        (by
          rw [add_comm, â† hcard]
          exact add_le_add_left (succ_le_of_lt (zero_lt_succ _)) _)
        hts
    rw [add_comm] at hu
    -- âŠ¢ âˆƒ t, t âˆˆ âˆ‚âº ğ’œ âˆ§ t âŠ† s âˆ§ card t + k = card s
    refine' âŸ¨u, mem_upShadow_iff_exists_mem_card_add_one.2 âŸ¨t, ht, htu, hu.symmâŸ©, hus, _âŸ©
    -- âŠ¢ card u + k = card s
    rw [hu, â† hcard, add_right_comm]
    -- âŠ¢ card t + k + 1 = card t + succ k
    rfl
    -- ğŸ‰ no goals
#align finset.mem_up_shadow_iff_exists_mem_card_add Finset.mem_upShadow_iff_exists_mem_card_add

@[simp]
theorem shadow_image_compl : (âˆ‚ ğ’œ).image compl = âˆ‚âº (ğ’œ.image compl) := by
  ext s
  -- âŠ¢ s âˆˆ image compl (âˆ‚ ğ’œ) â†” s âˆˆ âˆ‚âº (image compl ğ’œ)
  simp only [mem_image, exists_prop, mem_shadow_iff, mem_upShadow_iff]
  -- âŠ¢ (âˆƒ a, (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a_1, a_1 âˆˆ t âˆ§ erase t a_1 = a) âˆ§ aá¶œ = s) â†” âˆƒ t, (âˆƒ a, â€¦
  constructor
  -- âŠ¢ (âˆƒ a, (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a_1, a_1 âˆˆ t âˆ§ erase t a_1 = a) âˆ§ aá¶œ = s) â†’ âˆƒ t, (âˆƒ a, â€¦
  Â· rintro âŸ¨_, âŸ¨s, hs, a, ha, rflâŸ©, rflâŸ©
    -- âŠ¢ âˆƒ t, (âˆƒ a, a âˆˆ ğ’œ âˆ§ aá¶œ = t) âˆ§ âˆƒ a_1, Â¬a_1 âˆˆ t âˆ§ insert a_1 t = (erase s a)á¶œ
    exact âŸ¨sá¶œ, âŸ¨s, hs, rflâŸ©, a, not_mem_compl.2 ha, compl_erase.symmâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨_, âŸ¨s, hs, rflâŸ©, a, ha, rflâŸ©
    -- âŠ¢ âˆƒ a_1, (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a, a âˆˆ t âˆ§ erase t a = a_1) âˆ§ a_1á¶œ = insert a sá¶œ
    exact âŸ¨s.erase a, âŸ¨s, hs, a, not_mem_compl.1 ha, rflâŸ©, compl_eraseâŸ©
    -- ğŸ‰ no goals
#align finset.shadow_image_compl Finset.shadow_image_compl

@[simp]
theorem upShadow_image_compl : (âˆ‚âº ğ’œ).image compl = âˆ‚ (ğ’œ.image compl) := by
  ext s
  -- âŠ¢ s âˆˆ image compl (âˆ‚âº ğ’œ) â†” s âˆˆ âˆ‚ (image compl ğ’œ)
  simp only [mem_image, exists_prop, mem_shadow_iff, mem_upShadow_iff]
  -- âŠ¢ (âˆƒ a, (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a_1, Â¬a_1 âˆˆ t âˆ§ insert a_1 t = a) âˆ§ aá¶œ = s) â†” âˆƒ t, (âˆƒ  â€¦
  constructor
  -- âŠ¢ (âˆƒ a, (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a_1, Â¬a_1 âˆˆ t âˆ§ insert a_1 t = a) âˆ§ aá¶œ = s) â†’ âˆƒ t, (âˆƒ  â€¦
  Â· rintro âŸ¨_, âŸ¨s, hs, a, ha, rflâŸ©, rflâŸ©
    -- âŠ¢ âˆƒ t, (âˆƒ a, a âˆˆ ğ’œ âˆ§ aá¶œ = t) âˆ§ âˆƒ a_1, a_1 âˆˆ t âˆ§ erase t a_1 = (insert a s)á¶œ
    exact âŸ¨sá¶œ, âŸ¨s, hs, rflâŸ©, a, mem_compl.2 ha, compl_insert.symmâŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨_, âŸ¨s, hs, rflâŸ©, a, ha, rflâŸ©
    -- âŠ¢ âˆƒ a_1, (âˆƒ t, t âˆˆ ğ’œ âˆ§ âˆƒ a, Â¬a âˆˆ t âˆ§ insert a t = a_1) âˆ§ a_1á¶œ = erase sá¶œ a
    exact âŸ¨insert a s, âŸ¨s, hs, a, mem_compl.1 ha, rflâŸ©, compl_insertâŸ©
    -- ğŸ‰ no goals
#align finset.up_shadow_image_compl Finset.upShadow_image_compl

end UpShadow

end Finset
