/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Group.Finset.Piecewise
import Mathlib.Algebra.Order.BigOperators.Group.Finset
import Mathlib.Algebra.Order.Pi
import Mathlib.Algebra.Order.Ring.Nat
import Mathlib.Data.Finset.Sups
import Mathlib.Order.Birkhoff
import Mathlib.Order.Booleanisation
import Mathlib.Order.Sublattice
import Mathlib.Tactic.Positivity.Basic
import Mathlib.Tactic.Ring
import Mathlib.Tactic.GCongr

/-!
# The four functions theorem and corollaries

This file proves the four functions theorem. The statement is that if
`fâ‚ a * fâ‚‚ b â‰¤ fâ‚ƒ (a âŠ“ b) * fâ‚„ (a âŠ” b)` for all `a`, `b` in a finite distributive lattice, then
`(âˆ‘ x âˆˆ s, fâ‚ x) * (âˆ‘ x âˆˆ t, fâ‚‚ x) â‰¤ (âˆ‘ x âˆˆ s âŠ¼ t, fâ‚ƒ x) * (âˆ‘ x âˆˆ s âŠ» t, fâ‚„ x)` where
`s âŠ¼ t = {a âŠ“ b | a âˆˆ s, b âˆˆ t}`, `s âŠ» t = {a âŠ” b | a âˆˆ s, b âˆˆ t}`.

The proof uses Birkhoff's representation theorem to restrict to the case where the finite
distributive lattice is in fact a finite powerset algebra, namely `Finset Î±` for some finite `Î±`.
Then it proves this new statement by induction on the size of `Î±`.

## Main declarations

The two versions of the four functions theorem are
* `Finset.four_functions_theorem` for finite powerset algebras.
* `four_functions_theorem` for any finite distributive lattices.

We deduce a number of corollaries:
* `Finset.le_card_infs_mul_card_sups`: Daykin inequality. `|s| |t| â‰¤ |s âŠ¼ t| |s âŠ» t|`
* `holley`: Holley inequality.
* `fkg`: Fortuin-Kastelyn-Ginibre inequality.
* `Finset.card_le_card_diffs`: Marica-SchÃ¶nheim inequality. `|s| â‰¤ |{a \ b | a, b âˆˆ s}|`

## TODO

Prove that lattices in which `Finset.le_card_infs_mul_card_sups` holds are distributive. See
Daykin, *A lattice is distributive iff |A| |B| <= |A âˆ¨ B| |A âˆ§ B|*

Prove the Fishburn-Shepp inequality.

Is `collapse` a construct generally useful for set family inductions? If so, we should move it to an
earlier file and give it a proper API.

## References

[*Applications of the FKG Inequality and Its Relatives*, Graham][Graham1983]
-/

open Finset Fintype Function
open scoped FinsetFamily

variable {Î± Î² : Type*}

section Finset
variable [DecidableEq Î±] [CommSemiring Î²] [LinearOrder Î²] [IsStrictOrderedRing Î²]
  {ğ’œ : Finset (Finset Î±)} {a : Î±} {f fâ‚ fâ‚‚ fâ‚ƒ fâ‚„ : Finset Î± â†’ Î²} {s t u : Finset Î±}

/-- The `n = 1` case of the Ahlswede-Daykin inequality. Note that we can't just expand everything
out and bound termwise since `câ‚€ * dâ‚` appears twice on the RHS of the assumptions while `câ‚ * dâ‚€`
does not appear. -/
private lemma ineq [ExistsAddOfLE Î²] {aâ‚€ aâ‚ bâ‚€ bâ‚ câ‚€ câ‚ dâ‚€ dâ‚ : Î²}
    (haâ‚€ : 0 â‰¤ aâ‚€) (haâ‚ : 0 â‰¤ aâ‚) (hbâ‚€ : 0 â‰¤ bâ‚€) (hbâ‚ : 0 â‰¤ bâ‚)
    (hcâ‚€ : 0 â‰¤ câ‚€) (hcâ‚ : 0 â‰¤ câ‚) (hdâ‚€ : 0 â‰¤ dâ‚€) (hdâ‚ : 0 â‰¤ dâ‚)
    (hâ‚€â‚€ : aâ‚€ * bâ‚€ â‰¤ câ‚€ * dâ‚€) (hâ‚â‚€ : aâ‚ * bâ‚€ â‰¤ câ‚€ * dâ‚)
    (hâ‚€â‚ : aâ‚€ * bâ‚ â‰¤ câ‚€ * dâ‚) (hâ‚â‚ : aâ‚ * bâ‚ â‰¤ câ‚ * dâ‚) :
    (aâ‚€ + aâ‚) * (bâ‚€ + bâ‚) â‰¤ (câ‚€ + câ‚) * (dâ‚€ + dâ‚) := by
  calc
    _ = aâ‚€ * bâ‚€ + (aâ‚€ * bâ‚ + aâ‚ * bâ‚€) + aâ‚ * bâ‚ := by ring
    _ â‰¤ câ‚€ * dâ‚€ + (câ‚€ * dâ‚ + câ‚ * dâ‚€) + câ‚ * dâ‚ := add_le_add_three hâ‚€â‚€ ?_ hâ‚â‚
    _ = (câ‚€ + câ‚) * (dâ‚€ + dâ‚) := by ring
  obtain hcd | hcd := (mul_nonneg hcâ‚€ hdâ‚).eq_or_lt'
  Â· rw [hcd] at hâ‚€â‚ hâ‚â‚€
    rw [hâ‚€â‚.antisymm, hâ‚â‚€.antisymm, add_zero] <;> positivity
  refine le_of_mul_le_mul_right ?_ hcd
  calc (aâ‚€ * bâ‚ + aâ‚ * bâ‚€) * (câ‚€ * dâ‚)
      = aâ‚€ * bâ‚ * (câ‚€ * dâ‚) + câ‚€ * dâ‚ * (aâ‚ * bâ‚€) := by ring
    _ â‰¤ aâ‚€ * bâ‚ * (aâ‚ * bâ‚€) + câ‚€ * dâ‚ * (câ‚€ * dâ‚) := mul_add_mul_le_mul_add_mul hâ‚€â‚ hâ‚â‚€
    _ = aâ‚€ * bâ‚€ * (aâ‚ * bâ‚) + câ‚€ * dâ‚ * (câ‚€ * dâ‚) := by ring
    _ â‰¤ câ‚€ * dâ‚€ * (câ‚ * dâ‚) + câ‚€ * dâ‚ * (câ‚€ * dâ‚) := by gcongr
    _ = (câ‚€ * dâ‚ + câ‚ * dâ‚€) * (câ‚€ * dâ‚) := by ring

private def collapse (ğ’œ : Finset (Finset Î±)) (a : Î±) (f : Finset Î± â†’ Î²) (s : Finset Î±) : Î² :=
  âˆ‘ t âˆˆ ğ’œ with t.erase a = s, f t

private lemma erase_eq_iff (hs : a âˆ‰ s) : t.erase a = s â†” t = s âˆ¨ t = insert a s := by
  grind

private lemma filter_collapse_eq (ha : a âˆ‰ s) (ğ’œ : Finset (Finset Î±)) :
    {t âˆˆ ğ’œ | t.erase a = s} =
      if s âˆˆ ğ’œ then
        (if insert a s âˆˆ ğ’œ then {s, insert a s} else {s})
      else
        (if insert a s âˆˆ ğ’œ then {insert a s} else âˆ…) := by
  ext t; split_ifs <;> simp [erase_eq_iff ha] <;> aesop

omit [LinearOrder Î²] [IsStrictOrderedRing Î²] in
lemma collapse_eq (ha : a âˆ‰ s) (ğ’œ : Finset (Finset Î±)) (f : Finset Î± â†’ Î²) :
    collapse ğ’œ a f s = (if s âˆˆ ğ’œ then f s else 0) +
      if insert a s âˆˆ ğ’œ then f (insert a s) else 0 := by
  rw [collapse, filter_collapse_eq ha]
  split_ifs <;> simp [(ne_of_mem_of_not_mem' (mem_insert_self a s) ha).symm, *]

omit [LinearOrder Î²] [IsStrictOrderedRing Î²] in
lemma collapse_of_mem (ha : a âˆ‰ s) (ht : t âˆˆ ğ’œ) (hu : u âˆˆ ğ’œ) (hts : t = s)
    (hus : u = insert a s) : collapse ğ’œ a f s = f t + f u := by
  subst hts; subst hus; simp_rw [collapse_eq ha, if_pos ht, if_pos hu]

lemma le_collapse_of_mem (ha : a âˆ‰ s) (hf : 0 â‰¤ f) (hts : t = s) (ht : t âˆˆ ğ’œ) :
    f t â‰¤ collapse ğ’œ a f s := by
  subst hts
  rw [collapse_eq ha, if_pos ht]
  split_ifs
  Â· exact le_add_of_nonneg_right <| hf _
  Â· rw [add_zero]

lemma le_collapse_of_insert_mem (ha : a âˆ‰ s) (hf : 0 â‰¤ f) (hts : t = insert a s) (ht : t âˆˆ ğ’œ) :
    f t â‰¤ collapse ğ’œ a f s := by
  rw [collapse_eq ha, â† hts, if_pos ht]
  split_ifs
  Â· exact le_add_of_nonneg_left <| hf _
  Â· rw [zero_add]

lemma collapse_nonneg (hf : 0 â‰¤ f) : 0 â‰¤ collapse ğ’œ a f := fun _s â†¦ sum_nonneg fun _t _ â†¦ hf _

lemma collapse_modular [ExistsAddOfLE Î²]
    (hu : a âˆ‰ u) (hâ‚ : 0 â‰¤ fâ‚) (hâ‚‚ : 0 â‰¤ fâ‚‚) (hâ‚ƒ : 0 â‰¤ fâ‚ƒ) (hâ‚„ : 0 â‰¤ fâ‚„)
    (h : âˆ€ â¦ƒsâ¦„, s âŠ† insert a u â†’ âˆ€ â¦ƒtâ¦„, t âŠ† insert a u â†’ fâ‚ s * fâ‚‚ t â‰¤ fâ‚ƒ (s âˆ© t) * fâ‚„ (s âˆª t))
    (ğ’œ â„¬ : Finset (Finset Î±)) :
    âˆ€ â¦ƒsâ¦„, s âŠ† u â†’ âˆ€ â¦ƒtâ¦„, t âŠ† u â†’ collapse ğ’œ a fâ‚ s * collapse â„¬ a fâ‚‚ t â‰¤
      collapse (ğ’œ âŠ¼ â„¬) a fâ‚ƒ (s âˆ© t) * collapse (ğ’œ âŠ» â„¬) a fâ‚„ (s âˆª t) := by
  rintro s hsu t htu
  -- Gather a bunch of facts we'll need a lot
  have := hsu.trans <| subset_insert a _
  have := htu.trans <| subset_insert a _
  have := insert_subset_insert a hsu
  have := insert_subset_insert a htu
  have has := notMem_mono hsu hu
  have hat := notMem_mono htu hu
  have : a âˆ‰ s âˆ© t := notMem_mono (inter_subset_left.trans hsu) hu
  have := notMem_union.2 âŸ¨has, hatâŸ©
  rw [collapse_eq has]
  split_ifs
  Â· rw [collapse_eq hat]
    split_ifs
    Â· rw [collapse_of_mem â€¹_â€º (inter_mem_infs â€¹_â€º â€¹_â€º) (inter_mem_infs â€¹_â€º â€¹_â€º) rfl
        (insert_inter_distrib _ _ _).symm, collapse_of_mem â€¹_â€º (union_mem_sups â€¹_â€º â€¹_â€º)
        (union_mem_sups â€¹_â€º â€¹_â€º) rfl (insert_union_distrib _ _ _).symm]
      refine ineq (hâ‚ _) (hâ‚ _) (hâ‚‚ _) (hâ‚‚ _) (hâ‚ƒ _) (hâ‚ƒ _) (hâ‚„ _) (hâ‚„ _) (h â€¹_â€º â€¹_â€º) ?_ ?_ ?_
      Â· simpa [*] using h â€¹insert a s âŠ† _â€º â€¹t âŠ† _â€º
      Â· simpa [*] using h â€¹s âŠ† _â€º â€¹insert a t âŠ† _â€º
      Â· simpa [*] using h â€¹insert a s âŠ† _â€º â€¹insert a t âŠ† _â€º
    Â· rw [add_zero, add_mul]
      refine (add_le_add (h â€¹_â€º â€¹_â€º) <| h â€¹_â€º â€¹_â€º).trans ?_
      rw [collapse_of_mem â€¹_â€º (union_mem_sups â€¹_â€º â€¹_â€º) (union_mem_sups â€¹_â€º â€¹_â€º) rfl
        (insert_union _ _ _), insert_inter_of_notMem â€¹_â€º, â† mul_add]
      gcongr
      exacts [add_nonneg (hâ‚„ _) <| hâ‚„ _, le_collapse_of_mem â€¹_â€º hâ‚ƒ rfl <| inter_mem_infs â€¹_â€º â€¹_â€º]
    Â· rw [zero_add, add_mul]
      refine (add_le_add (h â€¹_â€º â€¹_â€º) <| h â€¹_â€º â€¹_â€º).trans ?_
      rw [collapse_of_mem â€¹_â€º (inter_mem_infs â€¹_â€º â€¹_â€º) (inter_mem_infs â€¹_â€º â€¹_â€º)
        (inter_insert_of_notMem â€¹_â€º) (insert_inter_distrib _ _ _).symm, union_insert,
        insert_union_distrib, â† add_mul]
      gcongr
      exacts [add_nonneg (hâ‚ƒ _) <| hâ‚ƒ _,
        le_collapse_of_insert_mem â€¹_â€º hâ‚„ (insert_union_distrib _ _ _).symm (union_mem_sups â€¹_â€º â€¹_â€º)]
    Â· rw [add_zero, mul_zero]
      exact mul_nonneg (collapse_nonneg hâ‚ƒ _) <| collapse_nonneg hâ‚„ _
  Â· rw [add_zero, collapse_eq hat, mul_add]
    split_ifs
    Â· refine (add_le_add (h â€¹_â€º â€¹_â€º) <| h â€¹_â€º â€¹_â€º).trans ?_
      rw [collapse_of_mem â€¹_â€º (union_mem_sups â€¹_â€º â€¹_â€º) (union_mem_sups â€¹_â€º â€¹_â€º) rfl
        (union_insert _ _ _), inter_insert_of_notMem â€¹_â€º, â† mul_add]
      exact mul_le_mul_of_nonneg_right (le_collapse_of_mem â€¹_â€º hâ‚ƒ rfl <| inter_mem_infs â€¹_â€º â€¹_â€º) <|
        add_nonneg (hâ‚„ _) <| hâ‚„ _
    Â· rw [mul_zero, add_zero]
      exact (h â€¹_â€º â€¹_â€º).trans <| mul_le_mul (le_collapse_of_mem â€¹_â€º hâ‚ƒ rfl <|
        inter_mem_infs â€¹_â€º â€¹_â€º) (le_collapse_of_mem â€¹_â€º hâ‚„ rfl <| union_mem_sups â€¹_â€º â€¹_â€º)
        (hâ‚„ _) <| collapse_nonneg hâ‚ƒ _
    Â· rw [mul_zero, zero_add]
      refine (h â€¹_â€º â€¹_â€º).trans <| mul_le_mul ?_ (le_collapse_of_insert_mem â€¹_â€º hâ‚„
        (union_insert _ _ _) <| union_mem_sups â€¹_â€º â€¹_â€º) (hâ‚„ _) <| collapse_nonneg hâ‚ƒ _
      exact le_collapse_of_mem (notMem_mono inter_subset_left â€¹_â€º) hâ‚ƒ
        (inter_insert_of_notMem â€¹_â€º) <| inter_mem_infs â€¹_â€º â€¹_â€º
    Â· simp_rw [mul_zero, add_zero]
      exact mul_nonneg (collapse_nonneg hâ‚ƒ _) <| collapse_nonneg hâ‚„ _
  Â· rw [zero_add, collapse_eq hat, mul_add]
    split_ifs
    Â· refine (add_le_add (h â€¹_â€º â€¹_â€º) <| h â€¹_â€º â€¹_â€º).trans ?_
      rw [collapse_of_mem â€¹_â€º (inter_mem_infs â€¹_â€º â€¹_â€º) (inter_mem_infs â€¹_â€º â€¹_â€º)
        (insert_inter_of_notMem â€¹_â€º) (insert_inter_distrib _ _ _).symm,
        insert_inter_of_notMem â€¹_â€º, â† insert_inter_distrib, insert_union, insert_union_distrib,
        â† add_mul]
      exact mul_le_mul_of_nonneg_left (le_collapse_of_insert_mem â€¹_â€º hâ‚„
        (insert_union_distrib _ _ _).symm <| union_mem_sups â€¹_â€º â€¹_â€º) <| add_nonneg (hâ‚ƒ _) <| hâ‚ƒ _
    Â· rw [mul_zero, add_zero]
      refine (h â€¹_â€º â€¹_â€º).trans <| mul_le_mul (le_collapse_of_mem â€¹_â€º hâ‚ƒ
        (insert_inter_of_notMem â€¹_â€º) <| inter_mem_infs â€¹_â€º â€¹_â€º) (le_collapse_of_insert_mem â€¹_â€º hâ‚„
        (insert_union _ _ _) <| union_mem_sups â€¹_â€º â€¹_â€º) (hâ‚„ _) <| collapse_nonneg hâ‚ƒ _
    Â· rw [mul_zero, zero_add]
      exact (h â€¹_â€º â€¹_â€º).trans <| mul_le_mul (le_collapse_of_insert_mem â€¹_â€º hâ‚ƒ
        (insert_inter_distrib _ _ _).symm <| inter_mem_infs â€¹_â€º â€¹_â€º) (le_collapse_of_insert_mem â€¹_â€º
        hâ‚„ (insert_union_distrib _ _ _).symm <| union_mem_sups â€¹_â€º â€¹_â€º) (hâ‚„ _) <|
        collapse_nonneg hâ‚ƒ _
    Â· simp_rw [mul_zero, add_zero]
      exact mul_nonneg (collapse_nonneg hâ‚ƒ _) <| collapse_nonneg hâ‚„ _
  Â· simp_rw [add_zero, zero_mul]
    exact mul_nonneg (collapse_nonneg hâ‚ƒ _) <| collapse_nonneg hâ‚„ _

omit [LinearOrder Î²] [IsStrictOrderedRing Î²] in
lemma sum_collapse (hğ’œ : ğ’œ âŠ† (insert a u).powerset) (hu : a âˆ‰ u) :
    âˆ‘ s âˆˆ u.powerset, collapse ğ’œ a f s = âˆ‘ s âˆˆ ğ’œ, f s := by
  calc
    _ = âˆ‘ s âˆˆ u.powerset âˆ© ğ’œ, f s + âˆ‘ s âˆˆ u.powerset.image (insert a) âˆ© ğ’œ, f s := ?_
    _ = âˆ‘ s âˆˆ u.powerset âˆ© ğ’œ, f s + âˆ‘ s âˆˆ ((insert a u).powerset \ u.powerset) âˆ© ğ’œ, f s := ?_
    _ = âˆ‘ s âˆˆ ğ’œ, f s := ?_
  Â· rw [â† Finset.sum_ite_mem, â† Finset.sum_ite_mem, sum_image, â† sum_add_distrib]
    Â· exact sum_congr rfl fun s hs â†¦ collapse_eq (notMem_mono (mem_powerset.1 hs) hu) _ _
    Â· exact (insert_erase_invOn.2.injOn).mono fun s hs â†¦ notMem_mono (mem_powerset.1 hs) hu
  Â· congr with s
    simp only [mem_image, mem_powerset, mem_sdiff, subset_insert_iff]
    refine âŸ¨?_, fun h â†¦ âŸ¨_, h.1, ?_âŸ©âŸ©
    Â· rintro âŸ¨s, hs, rflâŸ©
      exact âŸ¨subset_insert_iff.1 <| insert_subset_insert _ hs, fun h â†¦
        hu <| h <| mem_insert_self _ _âŸ©
    Â· rw [insert_erase (erase_ne_self.1 fun hs â†¦ ?_)]
      rw [hs] at h
      exact h.2 h.1
  Â· rw [â† sum_union (disjoint_sdiff_self_right.mono inf_le_left inf_le_left),
      â† union_inter_distrib_right, union_sdiff_of_subset (powerset_mono.2 <| subset_insert _ _),
      inter_eq_right.2 hğ’œ]

variable [ExistsAddOfLE Î²]

/-- The **Four Functions Theorem** on a powerset algebra. See `four_functions_theorem` for the
finite distributive lattice generalisation. -/
protected lemma Finset.four_functions_theorem (u : Finset Î±)
    (hâ‚ : 0 â‰¤ fâ‚) (hâ‚‚ : 0 â‰¤ fâ‚‚) (hâ‚ƒ : 0 â‰¤ fâ‚ƒ) (hâ‚„ : 0 â‰¤ fâ‚„)
    (h : âˆ€ â¦ƒsâ¦„, s âŠ† u â†’ âˆ€ â¦ƒtâ¦„, t âŠ† u â†’ fâ‚ s * fâ‚‚ t â‰¤ fâ‚ƒ (s âˆ© t) * fâ‚„ (s âˆª t))
    {ğ’œ â„¬ : Finset (Finset Î±)} (hğ’œ : ğ’œ âŠ† u.powerset) (hâ„¬ : â„¬ âŠ† u.powerset) :
    (âˆ‘ s âˆˆ ğ’œ, fâ‚ s) * âˆ‘ s âˆˆ â„¬, fâ‚‚ s â‰¤ (âˆ‘ s âˆˆ ğ’œ âŠ¼ â„¬, fâ‚ƒ s) * âˆ‘ s âˆˆ ğ’œ âŠ» â„¬, fâ‚„ s := by
  induction u using Finset.induction generalizing fâ‚ fâ‚‚ fâ‚ƒ fâ‚„ ğ’œ â„¬ with
  | empty =>
    simp only [Finset.powerset_empty, Finset.subset_singleton_iff] at hğ’œ hâ„¬
    obtain rfl | rfl := hğ’œ <;> obtain rfl | rfl := hâ„¬ <;> simp; exact h (subset_refl âˆ…) subset_rfl
  | insert a u hu ih =>
    specialize ih (collapse_nonneg hâ‚) (collapse_nonneg hâ‚‚) (collapse_nonneg hâ‚ƒ)
      (collapse_nonneg hâ‚„) (collapse_modular hu hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ h ğ’œ â„¬) Subset.rfl Subset.rfl
    have : ğ’œ âŠ¼ â„¬ âŠ† powerset (insert a u) := by simpa using infs_subset hğ’œ hâ„¬
    have : ğ’œ âŠ» â„¬ âŠ† powerset (insert a u) := by simpa using sups_subset hğ’œ hâ„¬
    simpa only [powerset_sups_powerset_self, powerset_infs_powerset_self, sum_collapse,
      not_false_eq_true, *] using ih

variable (fâ‚ fâ‚‚ fâ‚ƒ fâ‚„) [Fintype Î±]

private lemma four_functions_theorem_aux (hâ‚ : 0 â‰¤ fâ‚) (hâ‚‚ : 0 â‰¤ fâ‚‚) (hâ‚ƒ : 0 â‰¤ fâ‚ƒ) (hâ‚„ : 0 â‰¤ fâ‚„)
    (h : âˆ€ s t, fâ‚ s * fâ‚‚ t â‰¤ fâ‚ƒ (s âˆ© t) * fâ‚„ (s âˆª t)) (ğ’œ â„¬ : Finset (Finset Î±)) :
    (âˆ‘ s âˆˆ ğ’œ, fâ‚ s) * âˆ‘ s âˆˆ â„¬, fâ‚‚ s â‰¤ (âˆ‘ s âˆˆ ğ’œ âŠ¼ â„¬, fâ‚ƒ s) * âˆ‘ s âˆˆ ğ’œ âŠ» â„¬, fâ‚„ s := by
  refine univ.four_functions_theorem hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ ?_ ?_ ?_ <;> simp [h]

end Finset

section DistribLattice
variable [DistribLattice Î±] [CommSemiring Î²] [LinearOrder Î²] [IsStrictOrderedRing Î²]
  [ExistsAddOfLE Î²] (f fâ‚ fâ‚‚ fâ‚ƒ fâ‚„ g Î¼ : Î± â†’ Î²)

/-- The **Four Functions Theorem**, aka **Ahlswede-Daykin Inequality**. -/
lemma four_functions_theorem [DecidableEq Î±] (hâ‚ : 0 â‰¤ fâ‚) (hâ‚‚ : 0 â‰¤ fâ‚‚) (hâ‚ƒ : 0 â‰¤ fâ‚ƒ) (hâ‚„ : 0 â‰¤ fâ‚„)
    (h : âˆ€ a b, fâ‚ a * fâ‚‚ b â‰¤ fâ‚ƒ (a âŠ“ b) * fâ‚„ (a âŠ” b)) (s t : Finset Î±) :
    (âˆ‘ a âˆˆ s, fâ‚ a) * âˆ‘ a âˆˆ t, fâ‚‚ a â‰¤ (âˆ‘ a âˆˆ s âŠ¼ t, fâ‚ƒ a) * âˆ‘ a âˆˆ s âŠ» t, fâ‚„ a := by
  classical
  set L : Sublattice Î± := âŸ¨latticeClosure (s âˆª t), isSublattice_latticeClosure.1,
    isSublattice_latticeClosure.2âŸ©
  have : Finite L := (s.finite_toSet.union t.finite_toSet).latticeClosure.to_subtype
  set s' : Finset L := s.preimage (â†‘) Subtype.coe_injective.injOn
  set t' : Finset L := t.preimage (â†‘) Subtype.coe_injective.injOn
  have hs' : s'.map âŸ¨L.subtype, Subtype.coe_injectiveâŸ© = s := by
    simp [s', map_eq_image, image_preimage, filter_eq_self]
    exact fun a ha â†¦ subset_latticeClosure <| Set.subset_union_left ha
  have ht' : t'.map âŸ¨L.subtype, Subtype.coe_injectiveâŸ© = t := by
    simp [t', map_eq_image, image_preimage, filter_eq_self]
    exact fun a ha â†¦ subset_latticeClosure <| Set.subset_union_right ha
  clear_value s' t'
  obtain âŸ¨Î², _, _, g, hgâŸ© := exists_birkhoff_representation L
  have := four_functions_theorem_aux (extend g (fâ‚ âˆ˜ (â†‘)) 0) (extend g (fâ‚‚ âˆ˜ (â†‘)) 0)
    (extend g (fâ‚ƒ âˆ˜ (â†‘)) 0) (extend g (fâ‚„ âˆ˜ (â†‘)) 0) (extend_nonneg (fun _ â†¦ hâ‚ _) le_rfl)
    (extend_nonneg (fun _ â†¦ hâ‚‚ _) le_rfl) (extend_nonneg (fun _ â†¦ hâ‚ƒ _) le_rfl)
    (extend_nonneg (fun _ â†¦ hâ‚„ _) le_rfl) ?_ (s'.map âŸ¨g, hgâŸ©) (t'.map âŸ¨g, hgâŸ©)
  Â· simpa only [â† hs', â† ht', â† map_sups, â† map_infs, sum_map, Embedding.coeFn_mk, hg.extend_apply]
      using this
  rintro s t
  classical
  obtain âŸ¨a, rflâŸ© | hs := em (âˆƒ a, g a = s)
  Â· obtain âŸ¨b, rflâŸ© | ht := em (âˆƒ b, g b = t)
    Â· simp_rw [â† sup_eq_union, â† inf_eq_inter, â† map_sup, â† map_inf, hg.extend_apply]
      exact h _ _
    Â· simpa [extend_apply' _ _ _ ht] using mul_nonneg
        (extend_nonneg (fun a : L â†¦ hâ‚ƒ a) le_rfl _) (extend_nonneg (fun a : L â†¦ hâ‚„ a) le_rfl _)
  Â· simpa [extend_apply' _ _ _ hs] using mul_nonneg
      (extend_nonneg (fun a : L â†¦ hâ‚ƒ a) le_rfl _) (extend_nonneg (fun a : L â†¦ hâ‚„ a) le_rfl _)

/-- An inequality of Daykin. Interestingly, any lattice in which this inequality holds is
distributive. -/
lemma Finset.le_card_infs_mul_card_sups [DecidableEq Î±] (s t : Finset Î±) :
    #s * #t â‰¤ #(s âŠ¼ t) * #(s âŠ» t) := by
  simpa using four_functions_theorem (1 : Î± â†’ â„•) 1 1 1 zero_le_one zero_le_one zero_le_one
    zero_le_one (fun _ _ â†¦ le_rfl) s t

variable [Fintype Î±]

/-- Special case of the **Four Functions Theorem** when `s = t = univ`. -/
lemma four_functions_theorem_univ (hâ‚ : 0 â‰¤ fâ‚) (hâ‚‚ : 0 â‰¤ fâ‚‚) (hâ‚ƒ : 0 â‰¤ fâ‚ƒ) (hâ‚„ : 0 â‰¤ fâ‚„)
    (h : âˆ€ a b, fâ‚ a * fâ‚‚ b â‰¤ fâ‚ƒ (a âŠ“ b) * fâ‚„ (a âŠ” b)) :
    (âˆ‘ a, fâ‚ a) * âˆ‘ a, fâ‚‚ a â‰¤ (âˆ‘ a, fâ‚ƒ a) * âˆ‘ a, fâ‚„ a := by
  classical simpa using four_functions_theorem fâ‚ fâ‚‚ fâ‚ƒ fâ‚„ hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ h univ univ

/-- The **Holley Inequality**. -/
lemma holley (hÎ¼â‚€ : 0 â‰¤ Î¼) (hf : 0 â‰¤ f) (hg : 0 â‰¤ g) (hÎ¼ : Monotone Î¼)
    (hfg : âˆ‘ a, f a = âˆ‘ a, g a) (h : âˆ€ a b, f a * g b â‰¤ f (a âŠ“ b) * g (a âŠ” b)) :
    âˆ‘ a, Î¼ a * f a â‰¤ âˆ‘ a, Î¼ a * g a := by
  classical
  obtain rfl | hf := hf.eq_or_lt
  Â· simp only [Pi.zero_apply, sum_const_zero, eq_comm, Fintype.sum_eq_zero_iff_of_nonneg hg] at hfg
    simp [hfg]
  obtain rfl | hg := hg.eq_or_lt
  Â· simp only [Pi.zero_apply, sum_const_zero, Fintype.sum_eq_zero_iff_of_nonneg hf.le] at hfg
    simp [hfg]
  have := four_functions_theorem g (Î¼ * f) f (Î¼ * g) hg.le (mul_nonneg hÎ¼â‚€ hf.le) hf.le
    (mul_nonneg hÎ¼â‚€ hg.le) (fun a b â†¦ ?_) univ univ
  Â· simpa [hfg, sum_pos hg] using this
  Â· simp_rw [Pi.mul_apply, mul_left_comm _ (Î¼ _), mul_comm (g _)]
    rw [sup_comm, inf_comm]
    exact mul_le_mul (hÎ¼ le_sup_left) (h _ _) (mul_nonneg (hf.le _) <| hg.le _) <| hÎ¼â‚€ _

/-- The **Fortuin-Kastelyn-Ginibre Inequality**. -/
lemma fkg (hÎ¼â‚€ : 0 â‰¤ Î¼) (hfâ‚€ : 0 â‰¤ f) (hgâ‚€ : 0 â‰¤ g) (hf : Monotone f) (hg : Monotone g)
    (hÎ¼ : âˆ€ a b, Î¼ a * Î¼ b â‰¤ Î¼ (a âŠ“ b) * Î¼ (a âŠ” b)) :
    (âˆ‘ a, Î¼ a * f a) * âˆ‘ a, Î¼ a * g a â‰¤ (âˆ‘ a, Î¼ a) * âˆ‘ a, Î¼ a * (f a * g a) := by
  refine four_functions_theorem_univ (Î¼ * f) (Î¼ * g) Î¼ _ (mul_nonneg hÎ¼â‚€ hfâ‚€) (mul_nonneg hÎ¼â‚€ hgâ‚€)
    hÎ¼â‚€ (mul_nonneg hÎ¼â‚€ <| mul_nonneg hfâ‚€ hgâ‚€) (fun a b â†¦ ?_)
  dsimp
  rw [mul_mul_mul_comm, â† mul_assoc (Î¼ (a âŠ“ b))]
  exact mul_le_mul (hÎ¼ _ _) (mul_le_mul (hf le_sup_left) (hg le_sup_right) (hgâ‚€ _) <| hfâ‚€ _)
    (mul_nonneg (hfâ‚€ _) <| hgâ‚€ _) <| mul_nonneg (hÎ¼â‚€ _) <| hÎ¼â‚€ _

end DistribLattice

open Booleanisation

variable [DecidableEq Î±] [GeneralizedBooleanAlgebra Î±]

/-- A slight generalisation of the **Marica-SchÃ¶nheim Inequality**. -/
lemma Finset.le_card_diffs_mul_card_diffs (s t : Finset Î±) :
    #s * #t â‰¤ #(s \\ t) * #(t \\ s) := by
  have : âˆ€ s t : Finset Î±, (s \\ t).map âŸ¨_, liftLatticeHom_injectiveâŸ© =
      s.map âŸ¨_, liftLatticeHom_injectiveâŸ© \\ t.map âŸ¨_, liftLatticeHom_injectiveâŸ© := by
    rintro s t
    simp_rw [map_eq_image]
    exact image_imageâ‚‚_distrib fun a b â†¦ rfl
  simpa [â† card_compls (_ âŠ» _), â† map_sup, â† map_inf, â† this] using
    (s.map âŸ¨_, liftLatticeHom_injectiveâŸ©).le_card_infs_mul_card_sups
      (t.map âŸ¨_, liftLatticeHom_injectiveâŸ©)á¶œË¢

/-- The **Marica-SchÃ¶nheim Inequality**. -/
lemma Finset.card_le_card_diffs (s : Finset Î±) : #s â‰¤ #(s \\ s) :=
  le_of_pow_le_pow_leftâ‚€ two_ne_zero (zero_le _) <| by
    simpa [â† sq] using s.le_card_diffs_mul_card_diffs s
