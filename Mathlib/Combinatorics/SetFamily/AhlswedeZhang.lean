/-
Copyright (c) 2023 YaÃ«l Dillies, Vladimir Ivanov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Vladimir Ivanov
-/
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Algebra.BigOperators.Ring.Finset
import Mathlib.Algebra.Order.BigOperators.Group.Finset
import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Data.Finset.Sups
import Mathlib.Tactic.FieldSimp
import Mathlib.Tactic.Positivity
import Mathlib.Tactic.Ring
import Mathlib.Algebra.BigOperators.Group.Finset.Powerset

/-!
# The Ahlswede-Zhang identity

This file proves the Ahlswede-Zhang identity, which is a nontrivial relation between the size of the
"truncated unions" of a set family. It sharpens the Lubell-Yamamoto-Meshalkin inequality
`Finset.lubell_yamamoto_meshalkin_inequality_sum_card_div_choose`, by making explicit the correction
term.

For a set family `ğ’œ` over a ground set of size `n`, the Ahlswede-Zhang identity states that the sum
of `|â‹‚ B âˆˆ ğ’œ, B âŠ† A, B|/(|A| * n.choose |A|)` over all sets `A` is exactly `1`. This implies the LYM
inequality since for an antichain `ğ’œ` and every `A âˆˆ ğ’œ` we have
`|â‹‚ B âˆˆ ğ’œ, B âŠ† A, B|/(|A| * n.choose |A|) = 1 / n.choose |A|`.

## Main declarations

* `Finset.truncatedSup`: `s.truncatedSup a` is the supremum of all `b â‰¥ a` in `ğ’œ` if there are
  some, or `âŠ¤` if there are none.
* `Finset.truncatedInf`: `s.truncatedInf a` is the infimum of all `b â‰¤ a` in `ğ’œ` if there are
  some, or `âŠ¥` if there are none.
* `AhlswedeZhang.infSum`: LHS of the Ahlswede-Zhang identity.
* `AhlswedeZhang.le_infSum`: The sum of `1 / n.choose |A|` over an antichain is less than the RHS of
  the Ahlswede-Zhang identity.
* `AhlswedeZhang.infSum_eq_one`: Ahlswede-Zhang identity.

## References

* [R. Ahlswede, Z. Zhang, *An identity in combinatorial extremal theory*](https://doi.org/10.1016/0001-8708(90)90023-G)
* [D. T. Tru, *An AZ-style identity and BollobÃ¡s deficiency*](https://doi.org/10.1016/j.jcta.2007.03.005)
-/

section
variable (Î± : Type*) [Fintype Î±] [Nonempty Î±] {m n : â„•}

open Finset Fintype Nat

private lemma binomial_sum_eq (h : n < m) :
    âˆ‘ i âˆˆ range (n + 1), (n.choose i * (m - n) / ((m - i) * m.choose i) : â„š) = 1 := by
  set f : â„• â†’ â„š := fun i â†¦ n.choose i * (m.choose i : â„š)â»Â¹ with hf
  suffices âˆ€ i âˆˆ range (n + 1), f i - f (i + 1) = n.choose i * (m - n) / ((m - i) * m.choose i) by
    rw [â† sum_congr rfl this, sum_range_sub', hf]
    simp [choose_zero_right]
  intro i hâ‚
  rw [mem_range] at hâ‚
  have hâ‚ := le_of_lt_succ hâ‚
  have hâ‚‚ := hâ‚.trans_lt h
  have hâ‚ƒ := hâ‚‚.le
  have hiâ‚„ : (i + 1 : â„š) â‰  0 := i.cast_add_one_ne_zero
  have := congr_arg ((â†‘) : â„• â†’ â„š) (choose_succ_right_eq m i)
  push_cast at this
  dsimp [f, hf]
  rw [(eq_mul_inv_iff_mul_eqâ‚€ hiâ‚„).mpr this]
  have := congr_arg ((â†‘) : â„• â†’ â„š) (choose_succ_right_eq n i)
  push_cast at this
  rw [(eq_mul_inv_iff_mul_eqâ‚€ hiâ‚„).mpr this]
  have : (m - i : â„š) â‰  0 := sub_ne_zero_of_ne (cast_lt.mpr hâ‚‚).ne'
  have : (m.choose i : â„š) â‰  0 := cast_ne_zero.2 (choose_pos hâ‚‚.le).ne'
  simp [field, *]

private lemma Fintype.sum_div_mul_card_choose_card :
    âˆ‘ s : Finset Î±, (card Î± / ((card Î± - #s) * (card Î±).choose #s) : â„š) =
      card Î± * âˆ‘ k âˆˆ range (card Î±), (â†‘k)â»Â¹ + 1 := by
  rw [â† powerset_univ, powerset_card_disjiUnion, sum_disjiUnion]
  have : âˆ€ {x : â„•}, âˆ€ s âˆˆ powersetCard x (univ : Finset Î±),
    (card Î± / ((card Î± - #s) * (card Î±).choose #s) : â„š) =
      card Î± / ((card Î± - x) * (card Î±).choose x) := by
    intro n s hs
    rw [mem_powersetCard_univ.1 hs]
  simp_rw [sum_congr rfl this, sum_const, card_powersetCard, card_univ, nsmul_eq_mul, mul_div,
    mul_comm, â† mul_div]
  rw [â† mul_sum, â† mul_inv_cancelâ‚€ (cast_ne_zero.mpr card_ne_zero : (card Î± : â„š) â‰  0), â† mul_add,
    add_comm _ ((card Î±)â»Â¹ : â„š), â† sum_insert (f := fun x : â„• â†¦ (xâ»Â¹ : â„š)) notMem_range_self,
    â† range_add_one]
  have (n) (hn : n âˆˆ range (card Î± + 1)) :
      ((card Î±).choose n / ((card Î± - n) * (card Î±).choose n) : â„š) = (card Î± - n : â„š)â»Â¹ := by
    rw [div_mul_cancel_rightâ‚€]
    exact cast_ne_zero.2 (choose_pos <| mem_range_succ_iff.1 hn).ne'
  simp only [sum_congr rfl this, mul_eq_mul_left_iff, cast_eq_zero]
  convert Or.inl <| sum_range_reflect _ _ with a ha
  rw [add_tsub_cancel_right, cast_sub (mem_range_succ_iff.mp ha)]

end

open scoped FinsetFamily

namespace Finset
variable {Î± Î² : Type*}

/-! ### Truncated supremum, truncated infimum -/

section SemilatticeSup
variable [SemilatticeSup Î±] [SemilatticeSup Î²] [BoundedOrder Î²] {s t : Finset Î±} {a : Î±}

private lemma sup_aux [DecidableLE Î±] : a âˆˆ lowerClosure s â†’ {b âˆˆ s | a â‰¤ b}.Nonempty :=
  fun âŸ¨b, hb, habâŸ© â†¦ âŸ¨b, mem_filter.2 âŸ¨hb, habâŸ©âŸ©

private lemma lower_aux [DecidableEq Î±] :
    a âˆˆ lowerClosure â†‘(s âˆª t) â†” a âˆˆ lowerClosure s âˆ¨ a âˆˆ lowerClosure t := by
  rw [coe_union, lowerClosure_union, LowerSet.mem_sup_iff]

variable [DecidableLE Î±] [OrderTop Î±]

/-- The supremum of the elements of `s` less than `a` if there are some, otherwise `âŠ¤`. -/
def truncatedSup (s : Finset Î±) (a : Î±) : Î± :=
  if h : a âˆˆ lowerClosure s then {b âˆˆ s | a â‰¤ b}.sup' (sup_aux h) id else âŠ¤

lemma truncatedSup_of_mem (h : a âˆˆ lowerClosure s) :
    truncatedSup s a = {b âˆˆ s | a â‰¤ b}.sup' (sup_aux h) id := dif_pos h

lemma truncatedSup_of_notMem (h : a âˆ‰ lowerClosure s) : truncatedSup s a = âŠ¤ := dif_neg h

@[deprecated (since := "2025-05-23")] alias truncatedSup_of_not_mem := truncatedSup_of_notMem

@[simp] lemma truncatedSup_empty (a : Î±) : truncatedSup âˆ… a = âŠ¤ := truncatedSup_of_notMem (by simp)

@[simp] lemma truncatedSup_singleton (b a : Î±) : truncatedSup {b} a = if a â‰¤ b then b else âŠ¤ := by
  simp [truncatedSup]; split_ifs <;> simp [Finset.filter_true_of_mem, *]

lemma le_truncatedSup : a â‰¤ truncatedSup s a := by
  rw [truncatedSup]
  split_ifs with h
  Â· obtain âŸ¨â„¬, hb, hâŸ© := h
    exact h.trans <| le_sup' id <| mem_filter.2 âŸ¨hb, hâŸ©
  Â· exact le_top

lemma map_truncatedSup [DecidableLE Î²] (e : Î± â‰ƒo Î²) (s : Finset Î±) (a : Î±) :
    e (truncatedSup s a) = truncatedSup (s.map e.toEquiv.toEmbedding) (e a) := by
  have : e a âˆˆ lowerClosure (s.map e.toEquiv.toEmbedding : Set Î²) â†” a âˆˆ lowerClosure s := by simp
  simp_rw [truncatedSup, apply_dite e, map_finset_sup', map_top, this]
  congr with h
  simp only [filter_map, Function.comp_def, Equiv.coe_toEmbedding, RelIso.coe_fn_toEquiv,
    OrderIso.le_iff_le, id, sup'_map]

lemma truncatedSup_of_isAntichain (hs : IsAntichain (Â· â‰¤ Â·) (s : Set Î±)) (ha : a âˆˆ s) :
    truncatedSup s a = a := by
  refine le_antisymm ?_ le_truncatedSup
  simp_rw [truncatedSup_of_mem (subset_lowerClosure ha), sup'_le_iff, mem_filter]
  rintro b âŸ¨hb, habâŸ©
  exact (hs.eq ha hb hab).ge

variable [DecidableEq Î±]

lemma truncatedSup_union (hs : a âˆˆ lowerClosure s) (ht : a âˆˆ lowerClosure t) :
    truncatedSup (s âˆª t) a = truncatedSup s a âŠ” truncatedSup t a := by
  simpa only [truncatedSup_of_mem, hs, ht, lower_aux.2 (Or.inl hs), filter_union] using
    sup'_union _ _ _

lemma truncatedSup_union_left (hs : a âˆˆ lowerClosure s) (ht : a âˆ‰ lowerClosure t) :
    truncatedSup (s âˆª t) a = truncatedSup s a := by
  simp only [mem_lowerClosure, mem_coe, not_exists, not_and] at ht
  simp only [truncatedSup_of_mem, hs, filter_union, filter_false_of_mem ht, union_empty,
    lower_aux.2 (Or.inl hs)]

lemma truncatedSup_union_right (hs : a âˆ‰ lowerClosure s) (ht : a âˆˆ lowerClosure t) :
    truncatedSup (s âˆª t) a = truncatedSup t a := by rw [union_comm, truncatedSup_union_left ht hs]

lemma truncatedSup_union_of_notMem (hs : a âˆ‰ lowerClosure s) (ht : a âˆ‰ lowerClosure t) :
    truncatedSup (s âˆª t) a = âŠ¤ := truncatedSup_of_notMem fun h â†¦ (lower_aux.1 h).elim hs ht

@[deprecated (since := "2025-05-23")]
alias truncatedSup_union_of_not_mem := truncatedSup_union_of_notMem

end SemilatticeSup

section SemilatticeInf
variable [SemilatticeInf Î±] [SemilatticeInf Î²]
  [BoundedOrder Î²] [DecidableLE Î²] {s t : Finset Î±} {a : Î±}

private lemma inf_aux [DecidableLE Î±] : a âˆˆ upperClosure s â†’ {b âˆˆ s | b â‰¤ a}.Nonempty :=
  fun âŸ¨b, hb, habâŸ© â†¦ âŸ¨b, mem_filter.2 âŸ¨hb, habâŸ©âŸ©

private lemma upper_aux [DecidableEq Î±] :
    a âˆˆ upperClosure â†‘(s âˆª t) â†” a âˆˆ upperClosure s âˆ¨ a âˆˆ upperClosure t := by
  rw [coe_union, upperClosure_union, UpperSet.mem_inf_iff]

variable [DecidableLE Î±] [BoundedOrder Î±]

/-- The infimum of the elements of `s` less than `a` if there are some, otherwise `âŠ¥`. -/
def truncatedInf (s : Finset Î±) (a : Î±) : Î± :=
  if h : a âˆˆ upperClosure s then {b âˆˆ s | b â‰¤ a}.inf' (inf_aux h) id else âŠ¥

lemma truncatedInf_of_mem (h : a âˆˆ upperClosure s) :
    truncatedInf s a = {b âˆˆ s | b â‰¤ a}.inf' (inf_aux h) id := dif_pos h

lemma truncatedInf_of_notMem (h : a âˆ‰ upperClosure s) : truncatedInf s a = âŠ¥ := dif_neg h

@[deprecated (since := "2025-05-23")] alias truncatedInf_of_not_mem := truncatedInf_of_notMem

lemma truncatedInf_le : truncatedInf s a â‰¤ a := by
  unfold truncatedInf
  split_ifs with h
  Â· obtain âŸ¨b, hb, hbaâŸ© := h
    exact hba.trans' <| inf'_le id <| mem_filter.2 âŸ¨hb, â€¹_â€ºâŸ©
  Â· exact bot_le

@[simp] lemma truncatedInf_empty (a : Î±) : truncatedInf âˆ… a = âŠ¥ := truncatedInf_of_notMem (by simp)

@[simp] lemma truncatedInf_singleton (b a : Î±) : truncatedInf {b} a = if b â‰¤ a then b else âŠ¥ := by
  simp only [truncatedInf, coe_singleton, upperClosure_singleton, UpperSet.mem_Ici_iff,
    id_eq]
  split_ifs <;> simp [Finset.filter_true_of_mem, *]

lemma map_truncatedInf (e : Î± â‰ƒo Î²) (s : Finset Î±) (a : Î±) :
    e (truncatedInf s a) = truncatedInf (s.map e.toEquiv.toEmbedding) (e a) := by
  have : e a âˆˆ upperClosure (s.map e.toEquiv.toEmbedding) â†” a âˆˆ upperClosure s := by simp
  simp_rw [truncatedInf, apply_dite e, map_finset_inf', map_bot, this]
  congr with h
  simp only [filter_map, Function.comp_def, Equiv.coe_toEmbedding, RelIso.coe_fn_toEquiv,
    OrderIso.le_iff_le, id, inf'_map]

lemma truncatedInf_of_isAntichain (hs : IsAntichain (Â· â‰¤ Â·) (s : Set Î±)) (ha : a âˆˆ s) :
    truncatedInf s a = a := by
  refine le_antisymm truncatedInf_le ?_
  simp_rw [truncatedInf_of_mem (subset_upperClosure ha), le_inf'_iff, mem_filter]
  rintro b âŸ¨hb, hbaâŸ©
  exact (hs.eq hb ha hba).ge

variable [DecidableEq Î±]

lemma truncatedInf_union (hs : a âˆˆ upperClosure s) (ht : a âˆˆ upperClosure t) :
    truncatedInf (s âˆª t) a = truncatedInf s a âŠ“ truncatedInf t a := by
  simpa only [truncatedInf_of_mem, hs, ht, upper_aux.2 (Or.inl hs), filter_union] using
    inf'_union _ _ _

lemma truncatedInf_union_left (hs : a âˆˆ upperClosure s) (ht : a âˆ‰ upperClosure t) :
    truncatedInf (s âˆª t) a = truncatedInf s a := by
  simp only [mem_upperClosure, mem_coe, not_exists, not_and] at ht
  simp only [truncatedInf_of_mem, hs, filter_union, filter_false_of_mem ht, union_empty,
    upper_aux.2 (Or.inl hs)]

lemma truncatedInf_union_right (hs : a âˆ‰ upperClosure s) (ht : a âˆˆ upperClosure t) :
    truncatedInf (s âˆª t) a = truncatedInf t a := by
  rw [union_comm, truncatedInf_union_left ht hs]

lemma truncatedInf_union_of_notMem (hs : a âˆ‰ upperClosure s) (ht : a âˆ‰ upperClosure t) :
    truncatedInf (s âˆª t) a = âŠ¥ :=
  truncatedInf_of_notMem <| by rw [coe_union, upperClosure_union]; exact fun h â†¦ h.elim hs ht

@[deprecated (since := "2025-05-23")]
alias truncatedInf_union_of_not_mem := truncatedInf_union_of_notMem

end SemilatticeInf

section DistribLattice
variable [DistribLattice Î±] [DecidableEq Î±] {s t : Finset Î±} {a : Î±}

private lemma infs_aux : a âˆˆ lowerClosure â†‘(s âŠ¼ t) â†” a âˆˆ lowerClosure s âˆ§ a âˆˆ lowerClosure t := by
  rw [coe_infs, lowerClosure_infs, LowerSet.mem_inf_iff]

private lemma sups_aux : a âˆˆ upperClosure â†‘(s âŠ» t) â†” a âˆˆ upperClosure s âˆ§ a âˆˆ upperClosure t := by
  rw [coe_sups, upperClosure_sups, UpperSet.mem_sup_iff]

variable [DecidableLE Î±] [BoundedOrder Î±]

lemma truncatedSup_infs (hs : a âˆˆ lowerClosure s) (ht : a âˆˆ lowerClosure t) :
    truncatedSup (s âŠ¼ t) a = truncatedSup s a âŠ“ truncatedSup t a := by
  simp only [truncatedSup_of_mem, hs, ht, infs_aux.2 âŸ¨hs, htâŸ©, sup'_inf_sup', filter_infs_le]
  simp_rw [â† image_inf_product]
  rw [sup'_image]
  simp [Function.uncurry_def]

lemma truncatedInf_sups (hs : a âˆˆ upperClosure s) (ht : a âˆˆ upperClosure t) :
    truncatedInf (s âŠ» t) a = truncatedInf s a âŠ” truncatedInf t a := by
  simp only [truncatedInf_of_mem, hs, ht, sups_aux.2 âŸ¨hs, htâŸ©, inf'_sup_inf', filter_sups_le]
  simp_rw [â† image_sup_product]
  rw [inf'_image]
  simp [Function.uncurry_def]

lemma truncatedSup_infs_of_notMem (ha : a âˆ‰ lowerClosure s âŠ“ lowerClosure t) :
    truncatedSup (s âŠ¼ t) a = âŠ¤ :=
  truncatedSup_of_notMem <| by rwa [coe_infs, lowerClosure_infs]

@[deprecated (since := "2025-05-23")]
alias truncatedSup_infs_of_not_mem := truncatedSup_infs_of_notMem

lemma truncatedInf_sups_of_notMem (ha : a âˆ‰ upperClosure s âŠ” upperClosure t) :
    truncatedInf (s âŠ» t) a = âŠ¥ :=
  truncatedInf_of_notMem <| by rwa [coe_sups, upperClosure_sups]

@[deprecated (since := "2025-05-23")]
alias truncatedInf_sups_of_not_mem := truncatedInf_sups_of_notMem

end DistribLattice

section BooleanAlgebra
variable [BooleanAlgebra Î±] [DecidableLE Î±]

@[simp] lemma compl_truncatedSup (s : Finset Î±) (a : Î±) :
    (truncatedSup s a)á¶œ = truncatedInf sá¶œË¢ aá¶œ := map_truncatedSup (OrderIso.compl Î±) _ _

@[simp] lemma compl_truncatedInf (s : Finset Î±) (a : Î±) :
    (truncatedInf s a)á¶œ = truncatedSup sá¶œË¢ aá¶œ := map_truncatedInf (OrderIso.compl Î±) _ _

end BooleanAlgebra

variable [DecidableEq Î±] [Fintype Î±]

lemma card_truncatedSup_union_add_card_truncatedSup_infs (ğ’œ â„¬ : Finset (Finset Î±)) (s : Finset Î±) :
    #(truncatedSup (ğ’œ âˆª â„¬) s) + #(truncatedSup (ğ’œ âŠ¼ â„¬) s) =
      #(truncatedSup ğ’œ s) + #(truncatedSup â„¬ s) := by
  by_cases hğ’œ : s âˆˆ lowerClosure (ğ’œ : Set <| Finset Î±) <;>
    by_cases hâ„¬ : s âˆˆ lowerClosure (â„¬ : Set <| Finset Î±)
  Â· rw [truncatedSup_union hğ’œ hâ„¬, truncatedSup_infs hğ’œ hâ„¬]
    exact card_union_add_card_inter _ _
  Â· rw [truncatedSup_union_left hğ’œ hâ„¬, truncatedSup_of_notMem hâ„¬,
      truncatedSup_infs_of_notMem fun h â†¦ hâ„¬ h.2]
  Â· rw [truncatedSup_union_right hğ’œ hâ„¬, truncatedSup_of_notMem hğ’œ,
      truncatedSup_infs_of_notMem fun h â†¦ hğ’œ h.1, add_comm]
  Â· rw [truncatedSup_of_notMem hğ’œ, truncatedSup_of_notMem hâ„¬,
      truncatedSup_union_of_notMem hğ’œ hâ„¬, truncatedSup_infs_of_notMem fun h â†¦ hğ’œ h.1]

lemma card_truncatedInf_union_add_card_truncatedInf_sups (ğ’œ â„¬ : Finset (Finset Î±)) (s : Finset Î±) :
    #(truncatedInf (ğ’œ âˆª â„¬) s) + #(truncatedInf (ğ’œ âŠ» â„¬) s) =
      #(truncatedInf ğ’œ s) + #(truncatedInf â„¬ s) := by
  by_cases hğ’œ : s âˆˆ upperClosure (ğ’œ : Set <| Finset Î±) <;>
    by_cases hâ„¬ : s âˆˆ upperClosure (â„¬ : Set <| Finset Î±)
  Â· rw [truncatedInf_union hğ’œ hâ„¬, truncatedInf_sups hğ’œ hâ„¬]
    exact card_inter_add_card_union _ _
  Â· rw [truncatedInf_union_left hğ’œ hâ„¬, truncatedInf_of_notMem hâ„¬,
      truncatedInf_sups_of_notMem fun h â†¦ hâ„¬ h.2]
  Â· rw [truncatedInf_union_right hğ’œ hâ„¬, truncatedInf_of_notMem hğ’œ,
      truncatedInf_sups_of_notMem fun h â†¦ hğ’œ h.1, add_comm]
  Â· rw [truncatedInf_of_notMem hğ’œ, truncatedInf_of_notMem hâ„¬,
      truncatedInf_union_of_notMem hğ’œ hâ„¬, truncatedInf_sups_of_notMem fun h â†¦ hğ’œ h.1]

end Finset

open Finset hiding card
open Fintype Nat

namespace AhlswedeZhang
variable {Î± : Type*} [Fintype Î±] [DecidableEq Î±] {ğ’œ : Finset (Finset Î±)} {s : Finset Î±}

/-- Weighted sum of the size of the truncated infima of a set family. Relevant to the
Ahlswede-Zhang identity. -/
def infSum (ğ’œ : Finset (Finset Î±)) : â„š :=
  âˆ‘ s, #(truncatedInf ğ’œ s) / (#s * (card Î±).choose #s)

/-- Weighted sum of the size of the truncated suprema of a set family. Relevant to the
Ahlswede-Zhang identity. -/
def supSum (ğ’œ : Finset (Finset Î±)) : â„š :=
  âˆ‘ s, #(truncatedSup ğ’œ s) / ((card Î± - #s) * (card Î±).choose #s)

lemma supSum_union_add_supSum_infs (ğ’œ â„¬ : Finset (Finset Î±)) :
    supSum (ğ’œ âˆª â„¬) + supSum (ğ’œ âŠ¼ â„¬) = supSum ğ’œ + supSum â„¬ := by
  unfold supSum
  rw [â† sum_add_distrib, â† sum_add_distrib, sum_congr rfl fun s _ â†¦ _]
  simp_rw [â† add_div, â† Nat.cast_add, card_truncatedSup_union_add_card_truncatedSup_infs]
  simp

lemma infSum_union_add_infSum_sups (ğ’œ â„¬ : Finset (Finset Î±)) :
    infSum (ğ’œ âˆª â„¬) + infSum (ğ’œ âŠ» â„¬) = infSum ğ’œ + infSum â„¬ := by
  unfold infSum
  rw [â† sum_add_distrib, â† sum_add_distrib, sum_congr rfl fun s _ â†¦ _]
  simp_rw [â† add_div, â† Nat.cast_add, card_truncatedInf_union_add_card_truncatedInf_sups]
  simp

lemma IsAntichain.le_infSum (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) (hğ’œâ‚€ : âˆ… âˆ‰ ğ’œ) :
    âˆ‘ s âˆˆ ğ’œ, ((card Î±).choose #s : â„š)â»Â¹ â‰¤ infSum ğ’œ := by
  calc
    _ = âˆ‘ s âˆˆ ğ’œ, #(truncatedInf ğ’œ s) / (#s * (card Î±).choose #s : â„š) := ?_
    _ â‰¤ _ := sum_le_univ_sum_of_nonneg fun s â†¦ by positivity
  refine sum_congr rfl fun s hs â†¦ ?_
  rw [truncatedInf_of_isAntichain hğ’œ hs, div_mul_cancel_leftâ‚€]
  have := (nonempty_iff_ne_empty.2 <| ne_of_mem_of_not_mem hs hğ’œâ‚€).card_pos
  positivity

variable [Nonempty Î±]

@[simp] lemma supSum_singleton (hs : s â‰  univ) :
    supSum ({s} : Finset (Finset Î±)) = card Î± * âˆ‘ k âˆˆ range (card Î±), (k : â„š)â»Â¹ := by
  have : âˆ€ t : Finset Î±,
    (card Î± - #(truncatedSup {s} t) : â„š) / ((card Î± - #t) * (card Î±).choose #t) =
    if t âŠ† s then (card Î± - #s : â„š) / ((card Î± - #t) * (card Î±).choose #t) else 0 := by
    rintro t
    simp_rw [truncatedSup_singleton, le_iff_subset]
    split_ifs <;> simp
  simp_rw [â† sub_eq_of_eq_add (Fintype.sum_div_mul_card_choose_card Î±), eq_sub_iff_add_eq,
    â† eq_sub_iff_add_eq', supSum, â† sum_sub_distrib, â† sub_div]
  rw [sum_congr rfl fun t _ â†¦ this t, sum_ite, sum_const_zero, add_zero, filter_subset_univ,
    sum_powerset, â† binomial_sum_eq ((card_lt_iff_ne_univ _).2 hs), eq_comm]
  refine sum_congr rfl fun n _ â†¦ ?_
  rw [mul_div_assoc, â† nsmul_eq_mul]
  exact sum_powersetCard n s fun m â†¦ (card Î± - #s : â„š) / ((card Î± - m) * (card Î±).choose m)

/-- The **Ahlswede-Zhang Identity**. -/
lemma infSum_compls_add_supSum (ğ’œ : Finset (Finset Î±)) :
    infSum ğ’œá¶œË¢ + supSum ğ’œ = card Î± * âˆ‘ k âˆˆ range (card Î±), (k : â„š)â»Â¹ + 1 := by
  unfold infSum supSum
  rw [â† @map_univ_of_surjective (Finset Î±) _ _ _ âŸ¨compl, compl_injectiveâŸ© compl_surjective, sum_map]
  simp only [Function.Embedding.coeFn_mk, univ_map_embedding, â† compl_truncatedSup,
    â† sum_add_distrib, card_compl, cast_sub (card_le_univ _), choose_symm (card_le_univ _),
    â† add_div, sub_add_cancel, Fintype.sum_div_mul_card_choose_card]

lemma supSum_of_univ_notMem (hğ’œâ‚ : ğ’œ.Nonempty) (hğ’œâ‚‚ : univ âˆ‰ ğ’œ) :
    supSum ğ’œ = card Î± * âˆ‘ k âˆˆ range (card Î±), (k : â„š)â»Â¹ := by
  set m := ğ’œ.card with hm
  clear_value m
  induction m using Nat.strongRecOn generalizing ğ’œ with | ind m ih => _
  replace ih := fun ğ’œ hğ’œ hğ’œâ‚ hğ’œâ‚‚ â†¦ @ih _ hğ’œ ğ’œ hğ’œâ‚ hğ’œâ‚‚ rfl
  obtain âŸ¨a, rflâŸ© | hğ’œâ‚ƒ := hğ’œâ‚.exists_eq_singleton_or_nontrivial
  Â· refine supSum_singleton ?_
    simpa [eq_comm] using hğ’œâ‚‚
  cases m
  Â· cases hğ’œâ‚.card_pos.ne hm
  obtain âŸ¨s, ğ’œ, hs, rfl, rflâŸ© := card_eq_succ.1 hm.symm
  have hğ’œ : ğ’œ.Nonempty := nonempty_iff_ne_empty.2 (by rintro rfl; simp at hğ’œâ‚ƒ)
  rw [insert_eq, eq_sub_of_add_eq (supSum_union_add_supSum_infs _ _), singleton_infs,
    supSum_singleton (ne_of_mem_of_not_mem (mem_insert_self _ _) hğ’œâ‚‚), ih, ih, add_sub_cancel_right]
  Â· exact card_image_le.trans_lt (lt_add_one _)
  Â· exact hğ’œ.image _
  Â· simpa using fun _ â†¦ ne_of_mem_of_not_mem (mem_insert_self _ _) hğ’œâ‚‚
  Â· exact lt_add_one _
  Â· exact hğ’œ
  Â· exact fun h â†¦ hğ’œâ‚‚ (mem_insert_of_mem h)

@[deprecated (since := "2025-05-23")]
alias supSum_of_not_univ_mem := supSum_of_univ_notMem

/-- The **Ahlswede-Zhang Identity**. -/
lemma infSum_eq_one (hğ’œâ‚ : ğ’œ.Nonempty) (hğ’œâ‚€ : âˆ… âˆ‰ ğ’œ) : infSum ğ’œ = 1 := by
  rw [â† compls_compls ğ’œ, eq_sub_of_add_eq (infSum_compls_add_supSum _),
    supSum_of_univ_notMem hğ’œâ‚.compls, add_sub_cancel_left]
  simpa

end AhlswedeZhang
