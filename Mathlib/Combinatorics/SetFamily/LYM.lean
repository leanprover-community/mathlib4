/-
Copyright (c) 2022 Bhavik Mehta, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import Mathlib.Algebra.Field.Basic
import Mathlib.Algebra.Field.Rat
import Mathlib.Combinatorics.Enumerative.DoubleCounting
import Mathlib.Combinatorics.SetFamily.Shadow
import Mathlib.Data.NNRat.Order
import Mathlib.Data.Nat.Cast.Order.Ring

/-!
# Lubell-Yamamoto-Meshalkin inequality and Sperner's theorem

This file proves the local LYM and LYM inequalities as well as Sperner's theorem.

## Main declarations

* `Finset.local_lubell_yamamoto_meshalkin_inequality_div`: Local Lubell-Yamamoto-Meshalkin
  inequality. The shadow of a set `ğ’œ` in a layer takes a greater proportion of its layer than `ğ’œ`
  does.
* `Finset.lubell_yamamoto_meshalkin_inequality_sum_card_div_choose`: Lubell-Yamamoto-Meshalkin
  inequality. The sum of densities of `ğ’œ` in each layer is at most `1` for any antichain `ğ’œ`.
* `IsAntichain.sperner`: Sperner's theorem. The size of any antichain in `Finset Î±` is at most the
  size of the maximal layer of `Finset Î±`. It is a corollary of
  `lubell_yamamoto_meshalkin_inequality_sum_card_div_choose`.

## TODO

Prove upward local LYM.

Provide equality cases. Local LYM gives that the equality case of LYM and Sperner is precisely when
`ğ’œ` is a middle layer.

`falling` could be useful more generally in grade orders.

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, lym, slice, sperner, antichain
-/

open Finset Nat
open scoped FinsetFamily

variable {ğ•œ Î± : Type*} [Semifield ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

namespace Finset

/-! ### Local LYM inequality -/

section LocalLYM
variable [DecidableEq Î±] [Fintype Î±] {ğ’œ : Finset (Finset Î±)} {r : â„•}

/-- The downward **local LYM inequality**, with cancelled denominators. `ğ’œ` takes up less of `Î±^(r)`
(the finsets of card `r`) than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`. -/
theorem local_lubell_yamamoto_meshalkin_inequality_mul (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    #ğ’œ * r â‰¤ #(âˆ‚ ğ’œ) * (Fintype.card Î± - r + 1) := by
  let i : DecidableRel ((Â· âŠ† Â·) : Finset Î± â†’ Finset Î± â†’ Prop) := fun _ _ => Classical.dec _
  refine card_mul_le_card_mul' (Â· âŠ† Â·) (fun s hs => ?_) (fun s hs => ?_)
  Â· rw [â† hğ’œ hs, â† card_image_of_injOn s.erase_injOn]
    refine card_le_card ?_
    simp_rw [image_subset_iff, mem_bipartiteBelow]
    exact fun a ha => âŸ¨erase_mem_shadow hs ha, erase_subset _ _âŸ©
  refine le_trans ?_ tsub_tsub_le_tsub_add
  rw [â† (Set.Sized.shadow hğ’œ) hs, â† card_compl, â† card_image_of_injOn (insert_inj_on' _)]
  refine card_le_card fun t ht => ?_
  rw [mem_bipartiteAbove] at ht
  have : âˆ… âˆ‰ ğ’œ := by
    rw [â† mem_coe, hğ’œ.empty_mem_iff, coe_eq_singleton]
    rintro rfl
    rw [shadow_singleton_empty] at hs
    exact notMem_empty s hs
  have h := exists_eq_insert_iff.2 âŸ¨ht.2, by
    rw [(sized_shadow_iff this).1 (Set.Sized.shadow hğ’œ) ht.1, (Set.Sized.shadow hğ’œ) hs]âŸ©
  rcases h with âŸ¨a, ha, rflâŸ©
  exact mem_image_of_mem _ (mem_compl.2 ha)

@[inherit_doc local_lubell_yamamoto_meshalkin_inequality_mul]
alias card_mul_le_card_shadow_mul := local_lubell_yamamoto_meshalkin_inequality_mul

/-- The downward **local LYM inequality**. `ğ’œ` takes up less of `Î±^(r)` (the finsets of card `r`)
than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`. -/
theorem local_lubell_yamamoto_meshalkin_inequality_div (hr : r â‰  0)
    (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) : (#ğ’œ : ğ•œ) / (Fintype.card Î±).choose r
    â‰¤ #(âˆ‚ ğ’œ) / (Fintype.card Î±).choose (r - 1) := by
  obtain hr' | hr' := lt_or_ge (Fintype.card Î±) r
  Â· rw [choose_eq_zero_of_lt hr', cast_zero, div_zero]
    exact div_nonneg (cast_nonneg _) (cast_nonneg _)
  replace hğ’œ := local_lubell_yamamoto_meshalkin_inequality_mul hğ’œ
  rw [div_le_div_iffâ‚€] <;> norm_cast
  Â· rcases r with - | r
    Â· exact (hr rfl).elim
    rw [tsub_add_eq_add_tsub hr', add_tsub_add_eq_tsub_right] at hğ’œ
    apply le_of_mul_le_mul_right _ (pos_iff_ne_zero.2 hr)
    convert Nat.mul_le_mul_right ((Fintype.card Î±).choose r) hğ’œ using 1
    Â· simpa [mul_assoc, Nat.choose_succ_right_eq] using Or.inl (mul_comm _ _)
    Â· simp only [mul_assoc, choose_succ_right_eq, mul_eq_mul_left_iff]
      exact Or.inl (mul_comm _ _)
  Â· exact Nat.choose_pos hr'
  Â· exact Nat.choose_pos (r.pred_le.trans hr')

@[inherit_doc local_lubell_yamamoto_meshalkin_inequality_div]
alias card_div_choose_le_card_shadow_div_choose := local_lubell_yamamoto_meshalkin_inequality_div

end LocalLYM

/-! ### LYM inequality -/

section LYM

section Falling

variable [DecidableEq Î±] (k : â„•) (ğ’œ : Finset (Finset Î±))

/-- `falling k ğ’œ` is all the finsets of cardinality `k` which are a subset of something in `ğ’œ`. -/
def falling : Finset (Finset Î±) :=
  ğ’œ.sup <| powersetCard k

variable {ğ’œ k} {s : Finset Î±}

theorem mem_falling : s âˆˆ falling k ğ’œ â†” (âˆƒ t âˆˆ ğ’œ, s âŠ† t) âˆ§ #s = k := by
  simp_rw [falling, mem_sup, mem_powersetCard]
  aesop

variable (ğ’œ k)

theorem sized_falling : (falling k ğ’œ : Set (Finset Î±)).Sized k := fun _ hs => (mem_falling.1 hs).2

theorem slice_subset_falling : ğ’œ # k âŠ† falling k ğ’œ := fun s hs =>
  mem_falling.2 <| (mem_slice.1 hs).imp_left fun h => âŸ¨s, h, Subset.refl _âŸ©

theorem falling_zero_subset : falling 0 ğ’œ âŠ† {âˆ…} :=
  subset_singleton_iff'.2 fun _ ht => card_eq_zero.1 <| sized_falling _ _ ht

theorem slice_union_shadow_falling_succ : ğ’œ # k âˆª âˆ‚ (falling (k + 1) ğ’œ) = falling k ğ’œ := by
  ext s
  simp_rw [mem_union, mem_slice, mem_shadow_iff, mem_falling]
  constructor
  Â· rintro (h | âŸ¨s, âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©, a, ha, rflâŸ©)
    Â· exact âŸ¨âŸ¨s, h.1, Subset.refl _âŸ©, h.2âŸ©
    refine âŸ¨âŸ¨t, ht, (erase_subset _ _).trans hstâŸ©, ?_âŸ©
    rw [card_erase_of_mem ha, hs]
    rfl
  Â· rintro âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©
    by_cases h : s âˆˆ ğ’œ
    Â· exact Or.inl âŸ¨h, hsâŸ©
    obtain âŸ¨a, ha, hstâŸ© := ssubset_iff.1 (ssubset_of_subset_of_ne hst (ht.ne_of_notMem h).symm)
    refine Or.inr âŸ¨insert a s, âŸ¨âŸ¨t, ht, hstâŸ©, ?_âŸ©, a, mem_insert_self _ _, erase_insert haâŸ©
    rw [card_insert_of_notMem ha, hs]

variable {ğ’œ k}

/-- The shadow of `falling m ğ’œ` is disjoint from the `n`-sized elements of `ğ’œ`, thanks to the
antichain property. -/
theorem IsAntichain.disjoint_slice_shadow_falling {m n : â„•}
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) : Disjoint (ğ’œ # m) (âˆ‚ (falling n ğ’œ)) :=
  disjoint_right.2 fun s hâ‚ hâ‚‚ => by
    simp_rw [mem_shadow_iff, mem_falling] at hâ‚
    obtain âŸ¨s, âŸ¨âŸ¨t, ht, hstâŸ©, _âŸ©, a, ha, rflâŸ© := hâ‚
    refine hğ’œ (slice_subset hâ‚‚) ht ?_ ((erase_subset _ _).trans hst)
    rintro rfl
    exact notMem_erase _ _ (hst ha)

/-- A bound on any top part of the sum in LYM in terms of the size of `falling k ğ’œ`. -/
theorem le_card_falling_div_choose [Fintype Î±] (hk : k â‰¤ Fintype.card Î±)
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) :
    (âˆ‘ r âˆˆ range (k + 1),
        (#(ğ’œ # (Fintype.card Î± - r)) : ğ•œ) / (Fintype.card Î±).choose (Fintype.card Î± - r)) â‰¤
      (falling (Fintype.card Î± - k) ğ’œ).card / (Fintype.card Î±).choose (Fintype.card Î± - k) := by
  induction k with
  | zero =>
    simp only [tsub_zero, cast_one, cast_le, sum_singleton, div_one, choose_self, range_one,
      zero_add, range_one, sum_singleton, tsub_zero,
      choose_self, cast_one, div_one, cast_le]
    exact card_le_card (slice_subset_falling _ _)
  | succ k ih =>
    rw [sum_range_succ, â† slice_union_shadow_falling_succ,
      card_union_of_disjoint (IsAntichain.disjoint_slice_shadow_falling hğ’œ),
      cast_add, _root_.add_div, add_comm]
    rw [â† tsub_tsub, tsub_add_cancel_of_le (le_tsub_of_add_le_left hk)]
    grw [ih <| le_of_succ_le hk, local_lubell_yamamoto_meshalkin_inequality_div
      (tsub_pos_iff_lt.2 <| Nat.succ_le_iff.1 hk).ne' <| sized_falling _ _]

end Falling

variable [Fintype Î±] {ğ’œ : Finset (Finset Î±)}

/-- The **Lubell-Yamamoto-Meshalkin inequality**, also known as the **LYM inequality**.

If `ğ’œ` is an antichain, then the sum of the proportion of elements it takes from each layer is less
than `1`. -/
theorem lubell_yamamoto_meshalkin_inequality_sum_card_div_choose
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) :
    âˆ‘ r âˆˆ range (Fintype.card Î± + 1), (#(ğ’œ # r) / (Fintype.card Î±).choose r : ğ•œ) â‰¤ 1 := by
  classical
    rw [â† sum_flip]
    refine (le_card_falling_div_choose le_rfl hğ’œ).trans ?_
    rw [div_le_iffâ‚€] <;> norm_cast
    Â· simpa only [Nat.sub_self, one_mul, Nat.choose_zero_right, falling] using
        Set.Sized.card_le (sized_falling 0 ğ’œ)
    Â· rw [tsub_self, choose_zero_right]
      exact zero_lt_one

@[inherit_doc lubell_yamamoto_meshalkin_inequality_sum_card_div_choose]
alias sum_card_slice_div_choose_le_one := lubell_yamamoto_meshalkin_inequality_sum_card_div_choose

/-- The **Lubell-Yamamoto-Meshalkin inequality**, also known as the **LYM inequality**.

If `ğ’œ` is an antichain, then the sum of `(#Î±.choose #s)â»Â¹` over `s âˆˆ ğ’œ` is less than `1`. -/
theorem lubell_yamamoto_meshalkin_inequality_sum_inv_choose
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (SetLike.coe ğ’œ)) :
    âˆ‘ s âˆˆ ğ’œ, ((Fintype.card Î±).choose #s : ğ•œ)â»Â¹ â‰¤ 1 := by
  calc
    _ = âˆ‘ r âˆˆ range (Fintype.card Î± + 1),
        âˆ‘ s âˆˆ ğ’œ with #s = r, ((Fintype.card Î±).choose r : ğ•œ)â»Â¹ := by
      rw [sum_fiberwise_of_maps_to']; simp [Nat.lt_succ_iff, card_le_univ]
    _ = âˆ‘ r âˆˆ range (Fintype.card Î± + 1), (#(ğ’œ # r) / (Fintype.card Î±).choose r : ğ•œ) := by
      simp [slice, div_eq_mul_inv]
    _ â‰¤ 1 := lubell_yamamoto_meshalkin_inequality_sum_card_div_choose hğ’œ

/-! ### Sperner's theorem -/

/-- **Sperner's theorem**. The size of an antichain in `Finset Î±` is bounded by the size of the
maximal layer in `Finset Î±`. This precisely means that `Finset Î±` is a Sperner order. -/
theorem _root_.IsAntichain.sperner (hğ’œ : IsAntichain (Â· âŠ† Â·) (SetLike.coe ğ’œ)) :
    #ğ’œ â‰¤ (Fintype.card Î±).choose (Fintype.card Î± / 2) := by
  have : 0 < ((Fintype.card Î±).choose (Fintype.card Î± / 2) : â„šâ‰¥0) :=
    Nat.cast_pos.2 <| choose_pos (Nat.div_le_self _ _)
  have h := calc
    âˆ‘ s âˆˆ ğ’œ, ((Fintype.card Î±).choose (Fintype.card Î± / 2) : â„šâ‰¥0)â»Â¹
    _ â‰¤ âˆ‘ s âˆˆ ğ’œ, ((Fintype.card Î±).choose #s : â„šâ‰¥0)â»Â¹ := by
      gcongr with s hs
      Â· exact mod_cast choose_pos s.card_le_univ
      Â· exact choose_le_middle _ _
    _ â‰¤ 1 := lubell_yamamoto_meshalkin_inequality_sum_inv_choose hğ’œ
  simpa [mul_inv_le_iffâ‚€' this] using h

end LYM
end Finset
