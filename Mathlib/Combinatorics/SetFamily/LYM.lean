/-
Copyright (c) 2022 Bhavik Mehta, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Combinatorics.DoubleCounting
import Mathlib.Combinatorics.SetFamily.Shadow
import Mathlib.Data.Rat.Order

#align_import combinatorics.set_family.lym from "leanprover-community/mathlib"@"861a26926586cd46ff80264d121cdb6fa0e35cc1"

/-!
# Lubell-Yamamoto-Meshalkin inequality and Sperner's theorem

This file proves the local LYM and LYM inequalities as well as Sperner's theorem.

## Main declarations

* `Finset.card_div_choose_le_card_shadow_div_choose`: Local Lubell-Yamamoto-Meshalkin inequality.
  The shadow of a set `ğ’œ` in a layer takes a greater proportion of its layer than `ğ’œ` does.
* `Finset.sum_card_slice_div_choose_le_one`: Lubell-Yamamoto-Meshalkin inequality. The sum of
  densities of `ğ’œ` in each layer is at most `1` for any antichain `ğ’œ`.
* `IsAntichain.sperner`: Sperner's theorem. The size of any antichain in `Finset Î±` is at most the
  size of the maximal layer of `Finset Î±`. It is a corollary of `sum_card_slice_div_choose_le_one`.

## TODO

Prove upward local LYM.

Provide equality cases. Local LYM gives that the equality case of LYM and Sperner is precisely when
`ğ’œ` is a middle layer.

`falling` could be useful more generally in grade orders.

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, lym, slice, sperner, antichain
-/


open Finset Nat

open BigOperators FinsetFamily

variable {ğ•œ Î± : Type*} [LinearOrderedField ğ•œ]

namespace Finset

/-! ### Local LYM inequality -/


section LocalLYM
variable [DecidableEq Î±] [Fintype Î±]
  {ğ’œ : Finset (Finset Î±)} {r : â„•}
/-- The downward **local LYM inequality**, with cancelled denominators. `ğ’œ` takes up less of `Î±^(r)`
(the finsets of card `r`) than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`. -/
theorem card_mul_le_card_shadow_mul (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) :
    ğ’œ.card * r â‰¤ (âˆ‚ ğ’œ).card * (Fintype.card Î± - r + 1) := by
  let i : DecidableRel ((Â· âŠ† Â·) : Finset Î± â†’ Finset Î± â†’ Prop) := fun _ _ => Classical.dec _
  -- âŠ¢ card ğ’œ * r â‰¤ card (âˆ‚ ğ’œ) * (Fintype.card Î± - r + 1)
  refine' card_mul_le_card_mul' (Â· âŠ† Â·) (fun s hs => _) (fun s hs => _)
  -- âŠ¢ r â‰¤ card (bipartiteBelow (fun x x_1 => x âŠ† x_1) (âˆ‚ ğ’œ) s)
  Â· rw [â† hğ’œ hs, â† card_image_of_injOn s.erase_injOn]
    -- âŠ¢ card (image (erase s) s) â‰¤ card (bipartiteBelow (fun x x_1 => x âŠ† x_1) (âˆ‚ ğ’œ) â€¦
    refine' card_le_of_subset _
    -- âŠ¢ image (erase s) s âŠ† bipartiteBelow (fun x x_1 => x âŠ† x_1) (âˆ‚ ğ’œ) s
    simp_rw [image_subset_iff, mem_bipartiteBelow]
    -- âŠ¢ âˆ€ (x : Î±), x âˆˆ s â†’ erase s x âˆˆ âˆ‚ ğ’œ âˆ§ erase s x âŠ† s
    exact fun a ha => âŸ¨erase_mem_shadow hs ha, erase_subset _ _âŸ©
    -- ğŸ‰ no goals
  refine' le_trans _ tsub_tsub_le_tsub_add
  -- âŠ¢ card (bipartiteAbove (fun x x_1 => x âŠ† x_1) ğ’œ s) â‰¤ Fintype.card Î± - (r - 1)
  rw [â† (Set.Sized.shadow hğ’œ) hs, â† card_compl, â† card_image_of_injOn (insert_inj_on' _)]
  -- âŠ¢ card (bipartiteAbove (fun x x_1 => x âŠ† x_1) ğ’œ s) â‰¤ card (image (fun a => ins â€¦
  refine' card_le_of_subset fun t ht => _
  -- âŠ¢ t âˆˆ image (fun a => insert a s) sá¶œ
  -- porting note: commented out the following line
  -- infer_instance
  rw [mem_bipartiteAbove] at ht
  -- âŠ¢ t âˆˆ image (fun a => insert a s) sá¶œ
  have : âˆ… âˆ‰ ğ’œ := by
    rw [â† mem_coe, hğ’œ.empty_mem_iff, coe_eq_singleton]
    rintro rfl
    rw [shadow_singleton_empty] at hs
    exact not_mem_empty s hs
  have h := exists_eq_insert_iff.2 âŸ¨ht.2, by
    rw [(sized_shadow_iff this).1 (Set.Sized.shadow hğ’œ) ht.1, (Set.Sized.shadow hğ’œ) hs]âŸ©
  rcases h with âŸ¨a, ha, rflâŸ©
  -- âŠ¢ insert a s âˆˆ image (fun a => insert a s) sá¶œ
  exact mem_image_of_mem _ (mem_compl.2 ha)
  -- ğŸ‰ no goals
#align finset.card_mul_le_card_shadow_mul Finset.card_mul_le_card_shadow_mul

/-- The downward **local LYM inequality**. `ğ’œ` takes up less of `Î±^(r)` (the finsets of card `r`)
than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`. -/
theorem card_div_choose_le_card_shadow_div_choose (hr : r â‰  0)
    (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized r) : (ğ’œ.card : ğ•œ) / (Fintype.card Î±).choose r
    â‰¤ (âˆ‚ ğ’œ).card / (Fintype.card Î±).choose (r - 1) := by
  obtain hr' | hr' := lt_or_le (Fintype.card Î±) r
  -- âŠ¢ â†‘(card ğ’œ) / â†‘(Nat.choose (Fintype.card Î±) r) â‰¤ â†‘(card (âˆ‚ ğ’œ)) / â†‘(Nat.choose  â€¦
  Â· rw [choose_eq_zero_of_lt hr', cast_zero, div_zero]
    -- âŠ¢ 0 â‰¤ â†‘(card (âˆ‚ ğ’œ)) / â†‘(Nat.choose (Fintype.card Î±) (r - 1))
    exact div_nonneg (cast_nonneg _) (cast_nonneg _)
    -- ğŸ‰ no goals
  replace hğ’œ := card_mul_le_card_shadow_mul hğ’œ
  -- âŠ¢ â†‘(card ğ’œ) / â†‘(Nat.choose (Fintype.card Î±) r) â‰¤ â†‘(card (âˆ‚ ğ’œ)) / â†‘(Nat.choose  â€¦
  rw [div_le_div_iff] <;> norm_cast
                          -- âŠ¢ card ğ’œ * Nat.choose (Fintype.card Î±) (r - 1) â‰¤ card (âˆ‚ ğ’œ) * Nat.choose (Fint â€¦
                          -- âŠ¢ 0 < Nat.choose (Fintype.card Î±) r
                          -- âŠ¢ 0 < Nat.choose (Fintype.card Î±) (r - 1)
  Â· cases' r with r
    -- âŠ¢ card ğ’œ * Nat.choose (Fintype.card Î±) (zero - 1) â‰¤ card (âˆ‚ ğ’œ) * Nat.choose (F â€¦
    Â· exact (hr rfl).elim
      -- ğŸ‰ no goals
    rw [Nat.succ_eq_add_one] at *
    -- âŠ¢ card ğ’œ * Nat.choose (Fintype.card Î±) (r + 1 - 1) â‰¤ card (âˆ‚ ğ’œ) * Nat.choose ( â€¦
    rw [tsub_add_eq_add_tsub hr', add_tsub_add_eq_tsub_right] at hğ’œ
    -- âŠ¢ card ğ’œ * Nat.choose (Fintype.card Î±) (r + 1 - 1) â‰¤ card (âˆ‚ ğ’œ) * Nat.choose ( â€¦
    apply le_of_mul_le_mul_right _ (pos_iff_ne_zero.2 hr)
    -- âŠ¢ card ğ’œ * Nat.choose (Fintype.card Î±) (r + 1 - 1) * (r + 1) â‰¤ card (âˆ‚ ğ’œ) * Na â€¦
    convert Nat.mul_le_mul_right ((Fintype.card Î±).choose r) hğ’œ using 1
    -- âŠ¢ card ğ’œ * Nat.choose (Fintype.card Î±) (r + 1 - 1) * (r + 1) = card ğ’œ * (r + 1 â€¦
    Â· simp [mul_assoc, Nat.choose_succ_right_eq]
      -- âŠ¢ Nat.choose (Fintype.card Î±) r * (r + 1) = (r + 1) * Nat.choose (Fintype.card â€¦
      exact Or.inl (mul_comm _ _)
      -- ğŸ‰ no goals
    Â· simp only [mul_assoc, choose_succ_right_eq, mul_eq_mul_left_iff]
      -- âŠ¢ Nat.choose (Fintype.card Î±) r * (Fintype.card Î± - r) = (Fintype.card Î± - r)  â€¦
      exact Or.inl (mul_comm _ _)
      -- ğŸ‰ no goals
  Â· exact Nat.choose_pos hr'
    -- ğŸ‰ no goals
  Â· exact Nat.choose_pos (r.pred_le.trans hr')
    -- ğŸ‰ no goals
#align finset.card_div_choose_le_card_shadow_div_choose Finset.card_div_choose_le_card_shadow_div_choose

end LocalLYM

/-! ### LYM inequality -/


section LYM

section Falling

variable [DecidableEq Î±] (k : â„•) (ğ’œ : Finset (Finset Î±))

/-- `falling k ğ’œ` is all the finsets of cardinality `k` which are a subset of something in `ğ’œ`. -/
def falling : Finset (Finset Î±) :=
  ğ’œ.sup <| powersetLen k
#align finset.falling Finset.falling

variable {ğ’œ k} {s : Finset Î±}

theorem mem_falling : s âˆˆ falling k ğ’œ â†” (âˆƒ t âˆˆ ğ’œ, s âŠ† t) âˆ§ s.card = k := by
  simp_rw [falling, mem_sup, mem_powersetLen]
  -- âŠ¢ (âˆƒ v, v âˆˆ ğ’œ âˆ§ s âŠ† v âˆ§ card s = k) â†” (âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t) âˆ§ card s = k
  aesop
  -- ğŸ‰ no goals
#align finset.mem_falling Finset.mem_falling

variable (ğ’œ k)

theorem sized_falling : (falling k ğ’œ : Set (Finset Î±)).Sized k := fun _ hs => (mem_falling.1 hs).2
#align finset.sized_falling Finset.sized_falling

theorem slice_subset_falling : ğ’œ # k âŠ† falling k ğ’œ := fun s hs =>
  mem_falling.2 <| (mem_slice.1 hs).imp_left fun h => âŸ¨s, h, Subset.refl _âŸ©
#align finset.slice_subset_falling Finset.slice_subset_falling

theorem falling_zero_subset : falling 0 ğ’œ âŠ† {âˆ…} :=
  subset_singleton_iff'.2 fun _ ht => card_eq_zero.1 <| sized_falling _ _ ht
#align finset.falling_zero_subset Finset.falling_zero_subset

theorem slice_union_shadow_falling_succ : ğ’œ # k âˆª âˆ‚ (falling (k + 1) ğ’œ) = falling k ğ’œ := by
  ext s
  -- âŠ¢ s âˆˆ ğ’œ # k âˆª âˆ‚ (falling (k + 1) ğ’œ) â†” s âˆˆ falling k ğ’œ
  simp_rw [mem_union, mem_slice, mem_shadow_iff, mem_falling]
  -- âŠ¢ (s âˆˆ ğ’œ âˆ§ card s = k âˆ¨ âˆƒ t, ((âˆƒ t_1, t_1 âˆˆ ğ’œ âˆ§ t âŠ† t_1) âˆ§ card t = k + 1) âˆ§ âˆƒ â€¦
  constructor
  -- âŠ¢ (s âˆˆ ğ’œ âˆ§ card s = k âˆ¨ âˆƒ t, ((âˆƒ t_1, t_1 âˆˆ ğ’œ âˆ§ t âŠ† t_1) âˆ§ card t = k + 1) âˆ§ âˆƒ â€¦
  Â· rintro (h | âŸ¨s, âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©, a, ha, rflâŸ©)
    -- âŠ¢ (âˆƒ t, t âˆˆ ğ’œ âˆ§ s âŠ† t) âˆ§ card s = k
    Â· exact âŸ¨âŸ¨s, h.1, Subset.refl _âŸ©, h.2âŸ©
      -- ğŸ‰ no goals
    refine' âŸ¨âŸ¨t, ht, (erase_subset _ _).trans hstâŸ©, _âŸ©
    -- âŠ¢ card (erase s a) = k
    rw [card_erase_of_mem ha, hs]
    -- âŠ¢ k + 1 - 1 = k
    rfl
    -- ğŸ‰ no goals
  Â· rintro âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©
    -- âŠ¢ s âˆˆ ğ’œ âˆ§ card s = k âˆ¨ âˆƒ t, ((âˆƒ t_1, t_1 âˆˆ ğ’œ âˆ§ t âŠ† t_1) âˆ§ card t = k + 1) âˆ§ âˆƒ  â€¦
    by_cases h : s âˆˆ ğ’œ
    -- âŠ¢ s âˆˆ ğ’œ âˆ§ card s = k âˆ¨ âˆƒ t, ((âˆƒ t_1, t_1 âˆˆ ğ’œ âˆ§ t âŠ† t_1) âˆ§ card t = k + 1) âˆ§ âˆƒ  â€¦
    Â· exact Or.inl âŸ¨h, hsâŸ©
      -- ğŸ‰ no goals
    obtain âŸ¨a, ha, hstâŸ© := ssubset_iff.1 (ssubset_of_subset_of_ne hst (ht.ne_of_not_mem h).symm)
    -- âŠ¢ s âˆˆ ğ’œ âˆ§ card s = k âˆ¨ âˆƒ t, ((âˆƒ t_1, t_1 âˆˆ ğ’œ âˆ§ t âŠ† t_1) âˆ§ card t = k + 1) âˆ§ âˆƒ  â€¦
    refine' Or.inr âŸ¨insert a s, âŸ¨âŸ¨t, ht, hstâŸ©, _âŸ©, a, mem_insert_self _ _, erase_insert haâŸ©
    -- âŠ¢ card (insert a s) = k + 1
    rw [card_insert_of_not_mem ha, hs]
    -- ğŸ‰ no goals
#align finset.slice_union_shadow_falling_succ Finset.slice_union_shadow_falling_succ

variable {ğ’œ k}

/-- The shadow of `falling m ğ’œ` is disjoint from the `n`-sized elements of `ğ’œ`, thanks to the
antichain property. -/
theorem IsAntichain.disjoint_slice_shadow_falling {m n : â„•}
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) : Disjoint (ğ’œ # m) (âˆ‚ (falling n ğ’œ)) :=
  disjoint_right.2 fun s hâ‚ hâ‚‚ => by
    simp_rw [mem_shadow_iff, mem_falling] at hâ‚
    -- âŠ¢ False
    obtain âŸ¨s, âŸ¨âŸ¨t, ht, hstâŸ©, _âŸ©, a, ha, rflâŸ© := hâ‚
    -- âŠ¢ False
    refine' hğ’œ (slice_subset hâ‚‚) ht _ ((erase_subset _ _).trans hst)
    -- âŠ¢ erase s a â‰  t
    rintro rfl
    -- âŠ¢ False
    exact not_mem_erase _ _ (hst ha)
    -- ğŸ‰ no goals
#align finset.is_antichain.disjoint_slice_shadow_falling Finset.IsAntichain.disjoint_slice_shadow_falling

/-- A bound on any top part of the sum in LYM in terms of the size of `falling k ğ’œ`. -/
theorem le_card_falling_div_choose [Fintype Î±] (hk : k â‰¤ Fintype.card Î±)
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) :
    (âˆ‘ r in range (k + 1),
        ((ğ’œ # (Fintype.card Î± - r)).card : ğ•œ) / (Fintype.card Î±).choose (Fintype.card Î± - r)) â‰¤
      (falling (Fintype.card Î± - k) ğ’œ).card / (Fintype.card Î±).choose (Fintype.card Î± - k) := by
  induction' k with k ih
  -- âŠ¢ âˆ‘ r in range (zero + 1), â†‘(card (ğ’œ # (Fintype.card Î± - r))) / â†‘(Nat.choose ( â€¦
  Â· simp only [tsub_zero, cast_one, cast_le, sum_singleton, div_one, choose_self, range_one,
      zero_eq, zero_add, range_one, ge_iff_le, sum_singleton, nonpos_iff_eq_zero, tsub_zero,
      choose_self, cast_one, div_one, cast_le]
    exact card_le_of_subset (slice_subset_falling _ _)
    -- ğŸ‰ no goals
  rw [succ_eq_add_one] at *
  -- âŠ¢ âˆ‘ r in range (k + 1 + 1), â†‘(card (ğ’œ # (Fintype.card Î± - r))) / â†‘(Nat.choose  â€¦
  rw [sum_range_succ, â† slice_union_shadow_falling_succ,
    card_disjoint_union (IsAntichain.disjoint_slice_shadow_falling hğ’œ), cast_add, _root_.add_div,
    add_comm]
  rw [â† tsub_tsub, tsub_add_cancel_of_le (le_tsub_of_add_le_left hk)]
  -- âŠ¢ â†‘(card (ğ’œ # (Fintype.card Î± - k - 1))) / â†‘(Nat.choose (Fintype.card Î±) (Fint â€¦
  exact
    add_le_add_left
      ((ih <| le_of_succ_le hk).trans <|
        card_div_choose_le_card_shadow_div_choose (tsub_pos_iff_lt.2 <| Nat.succ_le_iff.1 hk).ne' <|
          sized_falling _ _) _
#align finset.le_card_falling_div_choose Finset.le_card_falling_div_choose

end Falling

variable {ğ’œ : Finset (Finset Î±)} {s : Finset Î±} {k : â„•}

/-- The **Lubell-Yamamoto-Meshalkin inequality**. If `ğ’œ` is an antichain, then the sum of the
proportion of elements it takes from each layer is less than `1`. -/
theorem sum_card_slice_div_choose_le_one [Fintype Î±]
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) :
    (âˆ‘ r in range (Fintype.card Î± + 1), ((ğ’œ # r).card : ğ•œ) / (Fintype.card Î±).choose r) â‰¤ 1 := by
  classical
    rw [â† sum_flip]
    refine' (le_card_falling_div_choose le_rfl hğ’œ).trans _
    rw [div_le_iff] <;> norm_cast
    Â· simpa only [Nat.sub_self, one_mul, Nat.choose_zero_right, falling] using
        Set.Sized.card_le (sized_falling 0 ğ’œ)
    Â· rw [tsub_self, choose_zero_right]
      exact zero_lt_one
#align finset.sum_card_slice_div_choose_le_one Finset.sum_card_slice_div_choose_le_one

end LYM

/-! ### Sperner's theorem -/


/-- **Sperner's theorem**. The size of an antichain in `Finset Î±` is bounded by the size of the
maximal layer in `Finset Î±`. This precisely means that `Finset Î±` is a Sperner order. -/
theorem IsAntichain.sperner [Fintype Î±] {ğ’œ : Finset (Finset Î±)}
    (hğ’œ : IsAntichain (Â· âŠ† Â·) (ğ’œ : Set (Finset Î±))) :
    ğ’œ.card â‰¤ (Fintype.card Î±).choose (Fintype.card Î± / 2) := by
  classical
    suffices (âˆ‘ r in Iic (Fintype.card Î±),
        ((ğ’œ # r).card : â„š) / (Fintype.card Î±).choose (Fintype.card Î± / 2)) â‰¤ 1 by
      rw [â† sum_div, â† Nat.cast_sum, div_le_one] at this
      simp only [cast_le] at this
      rwa [sum_card_slice] at this
      simp only [cast_pos]
      exact choose_pos (Nat.div_le_self _ _)
    rw [Iic_eq_Icc, â† Ico_succ_right, bot_eq_zero, Ico_zero_eq_range]
    refine' (sum_le_sum fun r hr => _).trans (sum_card_slice_div_choose_le_one hğ’œ)
    rw [mem_range] at hr
    refine' div_le_div_of_le_left _ _ _ <;> norm_cast
    Â· exact Nat.zero_le _
    Â· exact choose_pos (lt_succ_iff.1 hr)
    Â· exact choose_le_middle _ _
#align finset.is_antichain.sperner Finset.IsAntichain.sperner

end Finset
