/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, YaÃ«l Dillies
-/
import Mathlib.Combinatorics.Colex
import Mathlib.Combinatorics.SetFamily.Compression.UV
import Mathlib.Combinatorics.SetFamily.Intersecting
import Mathlib.Data.Finset.Fin

/-!
# Kruskal-Katona theorem

This file proves the Kruskal-Katona theorem. This is a sharp statement about how many sets of size
`k - 1` are covered by a family of sets of size `k`, given only its size.

## Main declarations

The key results proved here are:
* `Finset.kruskal_katona`: The basic Kruskal-Katona theorem. Given a set family `ğ’œ` consisting of
  `r`-sets, and `ğ’` an initial segment of the colex order of the same size, the shadow of `ğ’` is
  smaller than the shadow of `ğ’œ`. In particular, this shows that the minimum shadow size is
  achieved by initial segments of colex.
* `Finset.iterated_kruskal_katona`: An iterated form of the Kruskal-Katona theorem, stating that the
  minimum iterated shadow size is given by initial segments of colex.

## TODO

* Define the `k`-cascade representation of a natural and prove the corresponding version of
  Kruskal-Katona.
* Abstract away from `Fin n` so that it also applies to `â„•`. Probably `LocallyFiniteOrderBot`
  will help here.
* Characterise the equality case.

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

kruskal-katona, kruskal, katona, shadow, initial segments, intersecting
-/

-- TODO: There's currently a diamond. See https://leanprover.zulipchat.com/#narrow/stream/270676-lean4/topic/DecidableEq.20diamond.20on.20Fin
-- import Mathlib.Order.Fin.Basic
-- example (n : â„•) : instDecidableEqFin n = instDecidableEq_mathlib := rfl
attribute [-instance] instDecidableEqFin

open Nat
open scoped FinsetFamily

namespace Finset
namespace Colex
variable {Î± : Type*} [LinearOrder Î±] {ğ’œ : Finset (Finset Î±)} {s : Finset Î±} {r : â„•}

/-- This is important for iterating Kruskal-Katona: the shadow of an initial segment is also an
initial segment. -/
lemma shadow_initSeg [Fintype Î±] (hs : s.Nonempty) :
    âˆ‚ (initSeg s) = initSeg (erase s <| min' s hs) := by
  -- This is a pretty painful proof, with lots of cases.
  ext t
  simp only [mem_shadow_iff_insert_mem, mem_initSeg]
  constructor
  -- First show that if t âˆª a â‰¤ s, then t â‰¤ s - min s
  Â· rintro âŸ¨a, ha, hst, htsâŸ©
    constructor
    Â· rw [card_erase_of_mem (min'_mem _ _), hst, card_insert_of_notMem ha, add_tsub_cancel_right]
    Â· simpa [ha] using erase_le_erase_min' hts hst.ge (mem_insert_self _ _)
  -- Now show that if t â‰¤ s - min s, there is j such that t âˆª j â‰¤ s
  -- We choose j as the smallest thing not in t
  simp_rw [le_iff_eq_or_lt, lt_iff_exists_filter_lt, mem_sdiff, filter_inj, and_assoc]
  simp only [toColex_inj, and_imp]
  rintro cards' (rfl | âŸ¨k, hks, hkt, zâŸ©)
  -- If t = s - min s, then use j = min s so t âˆª j = s
  Â· refine âŸ¨min' s hs, notMem_erase _ _, ?_âŸ©
    rw [insert_erase (min'_mem _ _)]
    exact âŸ¨rfl, Or.inl rflâŸ©
  set j := min' tá¶œ âŸ¨k, mem_compl.2 hktâŸ©
  -- Assume first t < s - min s, and take k as the colex witness for this
  have hjk : j â‰¤ k := min'_le _ _ (mem_compl.2 â€¹k âˆ‰ tâ€º)
  have : j âˆ‰ t := mem_compl.1 (min'_mem _ _)
  have hcard : #s = #(insert j t) := by
    rw [card_insert_of_notMem â€¹j âˆ‰ tâ€º, â† â€¹_ = #tâ€º, card_erase_add_one (min'_mem _ _)]
  refine âŸ¨j, â€¹_â€º, hcard, ?_âŸ©
  -- Cases on j < k or j = k
  obtain hjk | râ‚ := hjk.lt_or_eq
  -- if j < k, k is our colex witness for t âˆª {j} < s
  Â· refine Or.inr âŸ¨k, mem_of_mem_erase â€¹_â€º, fun hk â†¦ hkt <| mem_of_mem_insert_of_ne hk hjk.ne',
      fun x hx â†¦ ?_âŸ©
    simpa only [mem_insert, z hx, (hjk.trans hx).ne', mem_erase, Ne, false_or,
      and_iff_right_iff_imp] using fun _ â†¦ ((min'_le _ _ <| mem_of_mem_erase hks).trans_lt hx).ne'
  -- if j = k, all of range k is in t so by sizes t âˆª {j} = s
  refine Or.inl (eq_of_subset_of_card_le (fun a ha â†¦ ?_) hcard.ge).symm
  rcases lt_trichotomy k a with (lt | rfl | gt)
  Â· apply mem_insert_of_mem
    rw [z lt]
    refine mem_erase_of_ne_of_mem (lt_of_le_of_lt ?_ lt).ne' ha
    apply min'_le _ _ (mem_of_mem_erase â€¹_â€º)
  Â· rw [râ‚]; apply mem_insert_self
  Â· apply mem_insert_of_mem
    rw [â† râ‚] at gt
    by_contra
    apply (min'_le tá¶œ _ _).not_gt gt
    rwa [mem_compl]

/-- The shadow of an initial segment is also an initial segment. -/
protected lemma IsInitSeg.shadow [Finite Î±] (hâ‚ : IsInitSeg ğ’œ r) : IsInitSeg (âˆ‚ ğ’œ) (r - 1) := by
  cases nonempty_fintype Î±
  obtain rfl | hr := Nat.eq_zero_or_pos r
  Â· have : ğ’œ âŠ† {âˆ…} := fun s hs â†¦ by rw [mem_singleton, â† Finset.card_eq_zero]; exact hâ‚.1 hs
    have := shadow_monotone this
    simp only [subset_empty, le_eq_subset, shadow_singleton_empty] at this
    simp [this]
  obtain rfl | hğ’œ := ğ’œ.eq_empty_or_nonempty
  Â· simp
  obtain âŸ¨s, rfl, rflâŸ© := hâ‚.exists_initSeg hğ’œ
  rw [shadow_initSeg (card_pos.1 hr), â† card_erase_of_mem (min'_mem _ _)]
  exact isInitSeg_initSeg

end Colex

open Colex UV
open scoped FinsetFamily

variable {Î± : Type*} [LinearOrder Î±] {s U V : Finset Î±} {n : â„•}

namespace UV

/-- Applying the compression makes the set smaller in colex. This is intuitive since a portion of
the set is being "shifted down" as `max U < max V`. -/
lemma toColex_compress_lt_toColex {hU : U.Nonempty} {hV : V.Nonempty} (h : max' U hU < max' V hV)
    (hA : compress U V s â‰  s) : toColex (compress U V s) < toColex s := by
  rw [compress, ite_ne_right_iff] at hA
  rw [compress, if_pos hA.1, lt_iff_exists_filter_lt]
  simp_rw [mem_sdiff (s := s), filter_inj, and_assoc]
  refine âŸ¨_, hA.1.2 <| max'_mem _ hV, notMem_sdiff_of_mem_right <| max'_mem _ _, fun a ha â†¦ ?_âŸ©
  have : a âˆ‰ V := fun H â†¦ ha.not_ge (le_max' _ _ H)
  have : a âˆ‰ U := fun H â†¦ ha.not_gt ((le_max' _ _ H).trans_lt h)
  simp [â€¹a âˆ‰ Uâ€º, â€¹a âˆ‰ Vâ€º]

/-- These are the compressions which we will apply to decrease the "measure" of a family of sets. -/
private def UsefulCompression (U V : Finset Î±) : Prop :=
  Disjoint U V âˆ§ #U = #V âˆ§ âˆƒ (HU : U.Nonempty) (HV : V.Nonempty), max' U HU < max' V HV

private instance UsefulCompression.instDecidableRel :
    DecidableRel (Î± := Finset Î±) UsefulCompression :=
  fun _ _ â†¦ inferInstanceAs (Decidable (_ âˆ§ _))

/-- Applying a good compression will decrease measure, keep cardinality, keep sizes and decrease
shadow. In particular, 'good' means it's useful, and every smaller compression won't make a
difference. -/
private lemma compression_improved (ğ’œ : Finset (Finset Î±)) (hâ‚ : UsefulCompression U V)
    (hâ‚‚ : âˆ€ â¦ƒUâ‚ Vâ‚â¦„, UsefulCompression Uâ‚ Vâ‚ â†’ #Uâ‚ < #U â†’ IsCompressed Uâ‚ Vâ‚ ğ’œ) :
    #(âˆ‚ (ğ“’ U V ğ’œ)) â‰¤ #(âˆ‚ ğ’œ) := by
  obtain âŸ¨UVd, same_size, hU, hV, max_ltâŸ© := hâ‚
  refine card_shadow_compression_le _ _ fun x Hx â†¦ âŸ¨min' V hV, min'_mem _ _, ?_âŸ©
  obtain hU' | hU' := eq_or_lt_of_le (succ_le_iff.2 hU.card_pos)
  Â· rw [â† hU'] at same_size
    have : erase U x = âˆ… := by rw [â† Finset.card_eq_zero, card_erase_of_mem Hx, â† hU']
    have : erase V (min' V hV) = âˆ… := by
      rw [â† Finset.card_eq_zero, card_erase_of_mem (min'_mem _ _), â† same_size]
    rw [â€¹erase U x = âˆ…â€º, â€¹erase V (min' V hV) = âˆ…â€º]
    exact isCompressed_self _ _
  refine hâ‚‚ âŸ¨UVd.mono (erase_subset ..) (erase_subset ..), ?_, ?_, ?_, ?_âŸ© (card_erase_lt_of_mem Hx)
  Â· rw [card_erase_of_mem (min'_mem _ _), card_erase_of_mem Hx, same_size]
  Â· rwa [â† card_pos, card_erase_of_mem Hx, tsub_pos_iff_lt]
  Â· rwa [â† Finset.card_pos, card_erase_of_mem (min'_mem _ _), â† same_size, tsub_pos_iff_lt]
  Â· exact (Finset.max'_subset _ <| erase_subset _ _).trans_lt (max_lt.trans_le <| le_max' _ _ <|
      mem_erase.2 âŸ¨(min'_lt_max'_of_card _ (by rwa [â† same_size])).ne', max'_mem _ _âŸ©)

/-- If we're compressed by all useful compressions, then we're an initial segment. This is the other
key Kruskal-Katona part. -/
lemma isInitSeg_of_compressed {â„¬ : Finset (Finset Î±)} {r : â„•} (hâ‚ : (â„¬ : Set (Finset Î±)).Sized r)
    (hâ‚‚ : âˆ€ U V, UsefulCompression U V â†’ IsCompressed U V â„¬) : IsInitSeg â„¬ r := by
  refine âŸ¨hâ‚, ?_âŸ©
  rintro A B hA âŸ¨hBA, sizeAâŸ©
  by_contra hB
  have hAB : A â‰  B := ne_of_mem_of_not_mem hA hB
  have hAB' : #A = #B := (hâ‚ hA).trans sizeA.symm
  have hU : (A \ B).Nonempty := sdiff_nonempty.2 fun h â†¦ hAB <| eq_of_subset_of_card_le h hAB'.ge
  have hV : (B \ A).Nonempty :=
    sdiff_nonempty.2 fun h â†¦ hAB.symm <| eq_of_subset_of_card_le h hAB'.le
  have disj : Disjoint (B \ A) (A \ B) := disjoint_sdiff.mono_left sdiff_subset
  have smaller : max' _ hV < max' _ hU := by
    obtain hlt | heq | hgt := lt_trichotomy (max' _ hU) (max' _ hV)
    Â· rw [â† compress_sdiff_sdiff A B] at hAB hBA
      cases hBA.not_gt <| toColex_compress_lt_toColex hlt hAB
    Â· exact (disjoint_right.1 disj (max'_mem _ hU) <| heq.symm â–¸ max'_mem _ _).elim
    Â· assumption
  refine hB ?_
  rw [â† (hâ‚‚ _ _ âŸ¨disj, card_sdiff_comm hAB'.symm, hV, hU, smallerâŸ©).eq]
  exact mem_compression.2 (Or.inr âŸ¨hB, A, hA, compress_sdiff_sdiff _ _âŸ©)

attribute [-instance] Fintype.decidableForallFintype

/-- This measures roughly how compressed the family is.

Note that this does depend on the order of the ground set, unlike the Kruskal-Katona theorem itself
(although `kruskal_katona` currently is stated in an order-dependent manner). -/
private def familyMeasure (ğ’œ : Finset (Finset (Fin n))) : â„• := âˆ‘ A âˆˆ ğ’œ, âˆ‘ a âˆˆ A, 2 ^ (a : â„•)

/-- Applying a compression strictly decreases the measure. This helps show that "compress until we
can't any more" is a terminating process. -/
private lemma familyMeasure_compression_lt_familyMeasure {U V : Finset (Fin n)} {hU : U.Nonempty}
    {hV : V.Nonempty} (h : max' U hU < max' V hV) {ğ’œ : Finset (Finset (Fin n))} (a : ğ“’ U V ğ’œ â‰  ğ’œ) :
    familyMeasure (ğ“’ U V ğ’œ) < familyMeasure ğ’œ := by
  rw [compression] at a âŠ¢
  have q : âˆ€ Q âˆˆ {A âˆˆ ğ’œ | compress U V A âˆ‰ ğ’œ}, compress U V Q â‰  Q := by grind
  have uA : {A âˆˆ ğ’œ | compress U V A âˆˆ ğ’œ} âˆª {A âˆˆ ğ’œ | compress U V A âˆ‰ ğ’œ} = ğ’œ :=
    filter_union_filter_neg_eq _ _
  have neâ‚‚ : {A âˆˆ ğ’œ | compress U V A âˆ‰ ğ’œ}.Nonempty := by
    refine nonempty_iff_ne_empty.2 fun z â†¦ a ?_
    rw [filter_image, z, image_empty, union_empty]
    rwa [z, union_empty] at uA
  rw [familyMeasure, familyMeasure, sum_union compress_disjoint]
  conv_rhs => rw [â† uA]
  rw [sum_union (disjoint_filter_filter_neg _ _ _), add_lt_add_iff_left, filter_image,
    sum_image compress_injOn]
  refine sum_lt_sum_of_nonempty neâ‚‚ fun A hA â†¦ ?_
  simp_rw [â† sum_image Fin.val_injective.injOn]
  rw [geomSum_lt_geomSum_iff_toColex_lt_toColex le_rfl,
    toColex_image_lt_toColex_image Fin.val_strictMono]
  exact toColex_compress_lt_toColex h <| q _ hA

/-- The main Kruskal-Katona helper: use induction with our measure to keep compressing until
we can't any more, which gives a set family which is fully compressed and has the nice properties we
want. -/
private lemma kruskal_katona_helper {r : â„•} (ğ’œ : Finset (Finset (Fin n)))
    (h : (ğ’œ : Set (Finset (Fin n))).Sized r) :
    âˆƒ â„¬ : Finset (Finset (Fin n)), #(âˆ‚ â„¬) â‰¤ #(âˆ‚ ğ’œ) âˆ§ #ğ’œ = #â„¬ âˆ§
      (â„¬ : Set (Finset (Fin n))).Sized r âˆ§ âˆ€ U V, UsefulCompression U V â†’ IsCompressed U V â„¬ := by
  classical
  -- Are there any compressions we can make now?
  set usable : Finset (Finset (Fin n) Ã— Finset (Fin n)) :=
    {t | UsefulCompression t.1 t.2 âˆ§ Â¬ IsCompressed t.1 t.2 ğ’œ}
  obtain husable | husable := usable.eq_empty_or_nonempty
  -- No. Then where we are is the required set family.
  Â· refine âŸ¨ğ’œ, le_rfl, rfl, h, fun U V hUV â†¦ ?_âŸ©
    rw [eq_empty_iff_forall_notMem] at husable
    by_contra h
    exact husable âŸ¨U, VâŸ© <| mem_filter.2 âŸ¨mem_univ _, hUV, hâŸ©
  -- Yes. Then apply the smallest compression, then keep going
  obtain âŸ¨âŸ¨U, VâŸ©, hUV, tâŸ© := exists_min_image usable (fun t â†¦ #t.1) husable
  rw [mem_filter] at hUV
  have hâ‚‚ : âˆ€ Uâ‚ Vâ‚, UsefulCompression Uâ‚ Vâ‚ â†’ #Uâ‚ < #U â†’ IsCompressed Uâ‚ Vâ‚ ğ’œ := by
    rintro Uâ‚ Vâ‚ huseful hUcard
    by_contra h
    exact hUcard.not_ge <| t âŸ¨Uâ‚, Vâ‚âŸ© <| mem_filter.2 âŸ¨mem_univ _, huseful, hâŸ©
  have p1 : #(âˆ‚ (ğ“’ U V ğ’œ)) â‰¤ #(âˆ‚ ğ’œ) := compression_improved _ hUV.2.1 hâ‚‚
  obtain âŸ¨-, hUV', hu, hv, hmaxâŸ© := hUV.2.1
  have := familyMeasure_compression_lt_familyMeasure hmax hUV.2.2
  obtain âŸ¨t, q1, q2, q3, q4âŸ© := UV.kruskal_katona_helper (ğ“’ U V ğ’œ) (h.uvCompression hUV')
  exact âŸ¨t, q1.trans p1, (card_compression _ _ _).symm.trans q2, q3, q4âŸ©
termination_by familyMeasure ğ’œ

end UV

-- Finally we can prove Kruskal-Katona.
section KK
variable {r k i : â„•} {ğ’œ ğ’ : Finset <| Finset <| Fin n}

/-- The **Kruskal-Katona theorem**.

Given a set family `ğ’œ` consisting of `r`-sets, and `ğ’` an initial segment of the colex order of the
same size, the shadow of `ğ’` is smaller than the shadow of `ğ’œ`. In particular, this gives that the
minimum shadow size is achieved by initial segments of colex. -/
theorem kruskal_katona (hğ’œr : (ğ’œ : Set (Finset (Fin n))).Sized r) (hğ’ğ’œ : #ğ’ â‰¤ #ğ’œ)
    (hğ’ : IsInitSeg ğ’ r) : #(âˆ‚ ğ’) â‰¤ #(âˆ‚ ğ’œ) := by
  -- WLOG `|ğ’œ| = |ğ’|`
  obtain âŸ¨ğ’œ', hğ’œ, hğ’œğ’âŸ© := exists_subset_card_eq hğ’ğ’œ
  -- By `kruskal_katona_helper`, we find a fully compressed family `â„¬` of the same size as `ğ’œ`
  -- whose shadow is no bigger.
  obtain âŸ¨â„¬, hâ„¬ğ’œ, hğ’œâ„¬, hâ„¬r, hâ„¬âŸ© := UV.kruskal_katona_helper ğ’œ' (hğ’œr.mono (by gcongr))
  -- This means that `â„¬` is an initial segment of the same size as `ğ’`. Hence they are equal and
  -- we are done.
  suffices â„¬ = ğ’ by subst ğ’; exact hâ„¬ğ’œ.trans (by gcongr)
  have hcard : #â„¬ = #ğ’ := hğ’œâ„¬.symm.trans hğ’œğ’
  obtain hğ’â„¬ | hâ„¬ğ’ := hğ’.total (UV.isInitSeg_of_compressed hâ„¬r hâ„¬)
  Â· exact (eq_of_subset_of_card_le hğ’â„¬ hcard.le).symm
  Â· exact eq_of_subset_of_card_le hâ„¬ğ’ hcard.ge

/-- An iterated form of the Kruskal-Katona theorem. In particular, the minimum possible iterated
shadow size is attained by initial segments. -/
theorem iterated_kk (hâ‚ : (ğ’œ : Set (Finset (Fin n))).Sized r) (hâ‚‚ : #ğ’ â‰¤ #ğ’œ) (hâ‚ƒ : IsInitSeg ğ’ r) :
    #(âˆ‚^[k] ğ’) â‰¤ #(âˆ‚^[k] ğ’œ) := by
  induction k generalizing r ğ’œ ğ’ with
  | zero => simpa
  | succ _ ih =>
    refine ih hâ‚.shadow (kruskal_katona hâ‚ hâ‚‚ hâ‚ƒ) ?_
    convert hâ‚ƒ.shadow

/-- The **Lovasz formulation of the Kruskal-Katona theorem**.

If `|ğ’œ| â‰¥ k choose r`, (and everything in `ğ’œ` has size `r`) then the initial segment we compare to
is just all the subsets of `{0, ..., k - 1}` of size `r`. The `i`-th iterated shadow of this is all
the subsets of `{0, ..., k - 1}` of size `r - i`, so the `i`-th iterated shadow of `ğ’œ` has at least
`k.choose (r - i)` elements. -/
theorem kruskal_katona_lovasz_form (hir : i â‰¤ r) (hrk : r â‰¤ k) (hkn : k â‰¤ n)
    (hâ‚ : (ğ’œ : Set (Finset (Fin n))).Sized r) (hâ‚‚ : k.choose r â‰¤ #ğ’œ) :
    k.choose (r - i) â‰¤ #(âˆ‚^[i] ğ’œ) := by
  set range'k : Finset (Fin n) :=
    attachFin (range k) fun m â†¦ by rw [mem_range]; apply forall_lt_iff_le.2 hkn
  set ğ’ : Finset (Finset (Fin n)) := powersetCard r range'k
  have : (ğ’ : Set (Finset (Fin n))).Sized r := Set.sized_powersetCard _ _
  calc
    k.choose (r - i)
    _ = #(powersetCard (r - i) range'k) := by rw [card_powersetCard, card_attachFin, card_range]
    _ = #(âˆ‚^[i] ğ’) := by
      congr!
      ext B
      rw [mem_powersetCard, mem_shadow_iterate_iff_exists_sdiff]
      constructor
      Â· rintro âŸ¨hBk, hBâŸ©
        have := exists_subsuperset_card_eq hBk (Nat.le_add_left _ i) <| by
          rwa [hB, card_attachFin, card_range, â† Nat.add_sub_assoc hir, Nat.add_sub_cancel_left]
        obtain âŸ¨C, BsubC, hCrange, hcardâŸ© := this
        rw [hB, â† Nat.add_sub_assoc hir, Nat.add_sub_cancel_left] at hcard
        refine âŸ¨C, mem_powersetCard.2 âŸ¨hCrange, hcardâŸ©, BsubC, ?_âŸ©
        rw [card_sdiff_of_subset BsubC, hcard, hB, Nat.sub_sub_self hir]
      Â· rintro âŸ¨A, Ah, hBA, card_sdiff_iâŸ©
        rw [mem_powersetCard] at Ah
        refine âŸ¨hBA.trans Ah.1, eq_tsub_of_add_eq ?_âŸ©
        rw [â† Ah.2, â† card_sdiff_i, add_comm, card_sdiff_add_card_eq_card hBA]
    _ â‰¤ #(âˆ‚ ^[i] ğ’œ) := by
      refine iterated_kk hâ‚ ?_ âŸ¨â€¹_â€º, ?_âŸ©
      Â· rwa [card_powersetCard, card_attachFin, card_range]
      simp_rw [ğ’, mem_powersetCard]
      rintro A B hA âŸ¨HBâ‚, HBâ‚‚âŸ©
      refine âŸ¨fun t ht â†¦ ?_, â€¹_â€ºâŸ©
      rw [mem_attachFin, mem_range]
      have : toColex (image Fin.val B) < toColex (image Fin.val A) := by
        rwa [toColex_image_lt_toColex_image Fin.val_strictMono]
      apply Colex.forall_lt_mono this.le _ t (mem_image.2 âŸ¨t, ht, rflâŸ©)
      simp_rw [mem_image]
      rintro _ âŸ¨a, ha, habâŸ©
      simpa [range'k, hab] using hA.1 ha

end KK

/-- The **ErdÅ‘sâ€“Koâ€“Rado theorem**.

The maximum size of an intersecting family in `Î±` where all sets have size `r` is bounded by
`(card Î± - 1).choose (r - 1)`. This bound is sharp. -/
theorem erdos_ko_rado {ğ’œ : Finset (Finset (Fin n))} {r : â„•}
    (hğ’œ : (ğ’œ : Set (Finset (Fin n))).Intersecting) (hâ‚‚ : (ğ’œ : Set (Finset (Fin n))).Sized r)
    (hâ‚ƒ : r â‰¤ n / 2) :
    #ğ’œ â‰¤ (n - 1).choose (r - 1) := by
  -- Take care of the r=0 case first: it's not very interesting.
  rcases Nat.eq_zero_or_pos r with b | h1r
  Â· convert Nat.zero_le _
    rw [Finset.card_eq_zero, eq_empty_iff_forall_notMem]
    refine fun A HA â†¦ hğ’œ HA HA ?_
    rw [disjoint_self_iff_empty, â† Finset.card_eq_zero, â† b]
    exact hâ‚‚ HA
  refine le_of_not_gt fun size â†¦ ?_
  -- Consider ğ’œá¶œË¢ = {sá¶œ | s âˆˆ ğ’œ}
  -- Its iterated shadow (âˆ‚^[n-2k] ğ’œá¶œË¢) is disjoint from ğ’œ by intersecting-ness
  have : Disjoint ğ’œ (âˆ‚^[n - 2 * r] ğ’œá¶œË¢) := disjoint_right.2 fun A hAbar hA â†¦ by
    simp only [mem_shadow_iterate_iff_exists_sdiff, mem_compls] at hAbar
    obtain âŸ¨C, hC, hAC, _âŸ© := hAbar
    exact hğ’œ hA hC (disjoint_of_subset_left hAC disjoint_compl_right)
  have : r â‰¤ n := hâ‚ƒ.trans (Nat.div_le_self n 2)
  have : 1 â‰¤ n := â€¹1 â‰¤ râ€º.trans â€¹r â‰¤ nâ€º
  -- We know the size of ğ’œá¶œË¢ since it's the same size as ğ’œ
  have z : (n - 1).choose (n - r) < #ğ’œá¶œË¢ := by
    rwa [card_compls, choose_symm_of_eq_add (tsub_add_tsub_cancel â€¹r â‰¤ nâ€º â€¹1 â‰¤ râ€º).symm]
  -- and everything in ğ’œá¶œË¢ has size n-r.
  have hğ’œbar : (ğ’œá¶œË¢ : Set (Finset (Fin n))).Sized (n - r) := by simpa using hâ‚‚.compls
  -- We can use the Lovasz form of Kruskal-Katona to get |âˆ‚^[n-2k] ğ’œá¶œË¢| â‰¥ (n-1) choose r
  have kk := kruskal_katona_lovasz_form (i := n - 2 * r) (by cutsat)
    ((tsub_le_tsub_iff_left â€¹1 â‰¤ nâ€º).2 h1r) tsub_le_self hğ’œbar z.le
  have : n - r - (n - 2 * r) = r := by omega
  rw [this] at kk
  -- But this gives a contradiction: `n choose r < |ğ’œ| + |âˆ‚^[n-2k] ğ’œá¶œË¢|`
  have := calc
    n.choose r = (n - 1).choose (r - 1) + (n - 1).choose r := by
      convert Nat.choose_succ_succ _ _ using 3 <;> rwa [Nat.sub_one, Nat.succ_pred_eq_of_pos]
    _ < #ğ’œ + #(âˆ‚^[n - 2 * r] ğ’œá¶œË¢) := add_lt_add_of_lt_of_le size kk
    _ = #(ğ’œ âˆª âˆ‚^[n - 2 * r] ğ’œá¶œË¢) := by rw [card_union_of_disjoint â€¹_â€º]
  apply this.not_ge
  convert Set.Sized.card_le _
  Â· rw [Fintype.card_fin]
  rw [coe_union, Set.sized_union]
  refine âŸ¨â€¹_â€º, ?_âŸ©
  convert hğ’œbar.shadow_iterate
  cutsat

end Finset
