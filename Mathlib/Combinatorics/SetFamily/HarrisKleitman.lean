/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.Ring.Nat
import Mathlib.Combinatorics.SetFamily.Compression.Down
import Mathlib.Order.UpperLower.Basic
import Mathlib.Data.Fintype.Powerset

#align_import combinatorics.set_family.harris_kleitman from "leanprover-community/mathlib"@"b363547b3113d350d053abdf2884e9850a56b205"

/-!
# Harris-Kleitman inequality

This file proves the Harris-Kleitman inequality. This relates `ğ’œ.card * â„¬.card` and
`2 ^ card Î± * (ğ’œ âˆ© â„¬).card` where `ğ’œ` and `â„¬` are upward- or downcard-closed finite families of
finsets. This can be interpreted as saying that any two lower sets (resp. any two upper sets)
correlate in the uniform measure.

## Main declarations

* `IsLowerSet.le_card_inter_finset`: One form of the Harris-Kleitman inequality.

## References

* [D. J. Kleitman, *Families of non-disjoint subsets*][kleitman1966]
-/


open Finset

variable {Î± : Type*} [DecidableEq Î±] {ğ’œ â„¬ : Finset (Finset Î±)} {s : Finset Î±} {a : Î±}

theorem IsLowerSet.nonMemberSubfamily (h : IsLowerSet (ğ’œ : Set (Finset Î±))) :
    IsLowerSet (ğ’œ.nonMemberSubfamily a : Set (Finset Î±)) := fun s t hts => by
  simp_rw [mem_coe, mem_nonMemberSubfamily]
  exact And.imp (h hts) (mt <| @hts _)
#align is_lower_set.non_member_subfamily IsLowerSet.nonMemberSubfamily

theorem IsLowerSet.memberSubfamily (h : IsLowerSet (ğ’œ : Set (Finset Î±))) :
    IsLowerSet (ğ’œ.memberSubfamily a : Set (Finset Î±)) := by
  rintro s t hts
  simp_rw [mem_coe, mem_memberSubfamily]
  exact And.imp (h <| insert_subset_insert _ hts) (mt <| @hts _)
#align is_lower_set.member_subfamily IsLowerSet.memberSubfamily

theorem IsLowerSet.memberSubfamily_subset_nonMemberSubfamily (h : IsLowerSet (ğ’œ : Set (Finset Î±))) :
    ğ’œ.memberSubfamily a âŠ† ğ’œ.nonMemberSubfamily a := fun s => by
  rw [mem_memberSubfamily, mem_nonMemberSubfamily]
  exact And.imp_left (h <| subset_insert _ _)
#align is_lower_set.member_subfamily_subset_non_member_subfamily IsLowerSet.memberSubfamily_subset_nonMemberSubfamily

/-- **Harris-Kleitman inequality**: Any two lower sets of finsets correlate. -/
theorem IsLowerSet.le_card_inter_finset' (hğ’œ : IsLowerSet (ğ’œ : Set (Finset Î±)))
    (hâ„¬ : IsLowerSet (â„¬ : Set (Finset Î±))) (hğ’œs : âˆ€ t âˆˆ ğ’œ, t âŠ† s) (hâ„¬s : âˆ€ t âˆˆ â„¬, t âŠ† s) :
    ğ’œ.card * â„¬.card â‰¤ 2 ^ s.card * (ğ’œ âˆ© â„¬).card := by
  induction' s using Finset.induction with a s hs ih generalizing ğ’œ â„¬
  Â· simp_rw [subset_empty, â† subset_singleton_iff', subset_singleton_iff] at hğ’œs hâ„¬s
    obtain rfl | rfl := hğ’œs
    Â· simp only [card_empty, zero_mul, empty_inter, mul_zero, le_refl]
    obtain rfl | rfl := hâ„¬s
    Â· simp only [card_empty, inter_empty, mul_zero, zero_mul, le_refl]
    Â· simp only [card_empty, pow_zero, inter_singleton_of_mem, mem_singleton, card_singleton,
        le_refl]
  rw [card_insert_of_not_mem hs, â† card_memberSubfamily_add_card_nonMemberSubfamily a ğ’œ, â†
    card_memberSubfamily_add_card_nonMemberSubfamily a â„¬, add_mul, mul_add, mul_add,
    add_comm (_ * _), add_add_add_comm]
  refine
    (add_le_add_right
          (mul_add_mul_le_mul_add_mul
              (card_le_card hğ’œ.memberSubfamily_subset_nonMemberSubfamily) <|
            card_le_card hâ„¬.memberSubfamily_subset_nonMemberSubfamily)
          _).trans
      ?_
  rw [â† two_mul, pow_succ', mul_assoc]
  have hâ‚€ : âˆ€ ğ’ : Finset (Finset Î±), (âˆ€ t âˆˆ ğ’, t âŠ† insert a s) â†’
      âˆ€ t âˆˆ ğ’.nonMemberSubfamily a, t âŠ† s := by
    rintro ğ’ hğ’ t ht
    rw [mem_nonMemberSubfamily] at ht
    exact (subset_insert_iff_of_not_mem ht.2).1 (hğ’ _ ht.1)
  have hâ‚ : âˆ€ ğ’ : Finset (Finset Î±), (âˆ€ t âˆˆ ğ’, t âŠ† insert a s) â†’
      âˆ€ t âˆˆ ğ’.memberSubfamily a, t âŠ† s := by
    rintro ğ’ hğ’ t ht
    rw [mem_memberSubfamily] at ht
    exact (subset_insert_iff_of_not_mem ht.2).1 ((subset_insert _ _).trans <| hğ’ _ ht.1)
  refine mul_le_mul_left' ?_ _
  refine (add_le_add (ih hğ’œ.memberSubfamily hâ„¬.memberSubfamily (hâ‚ _ hğ’œs) <| hâ‚ _ hâ„¬s) <|
    ih hğ’œ.nonMemberSubfamily hâ„¬.nonMemberSubfamily (hâ‚€ _ hğ’œs) <| hâ‚€ _ hâ„¬s).trans_eq ?_
  rw [â† mul_add, â† memberSubfamily_inter, â† nonMemberSubfamily_inter,
    card_memberSubfamily_add_card_nonMemberSubfamily]
#align is_lower_set.le_card_inter_finset' IsLowerSet.le_card_inter_finset'

variable [Fintype Î±]

/-- **Harris-Kleitman inequality**: Any two lower sets of finsets correlate. -/
theorem IsLowerSet.le_card_inter_finset (hğ’œ : IsLowerSet (ğ’œ : Set (Finset Î±)))
    (hâ„¬ : IsLowerSet (â„¬ : Set (Finset Î±))) : ğ’œ.card * â„¬.card â‰¤ 2 ^ Fintype.card Î± * (ğ’œ âˆ© â„¬).card :=
hğ’œ.le_card_inter_finset' hâ„¬ (fun _ _ => subset_univ _) fun _ _ => subset_univ _
#align is_lower_set.le_card_inter_finset IsLowerSet.le_card_inter_finset

/-- **Harris-Kleitman inequality**: Upper sets and lower sets of finsets anticorrelate. -/
theorem IsUpperSet.card_inter_le_finset (hğ’œ : IsUpperSet (ğ’œ : Set (Finset Î±)))
    (hâ„¬ : IsLowerSet (â„¬ : Set (Finset Î±))) :
    2 ^ Fintype.card Î± * (ğ’œ âˆ© â„¬).card â‰¤ ğ’œ.card * â„¬.card := by
  rw [â† isLowerSet_compl, â† coe_compl] at hğ’œ
  have := hğ’œ.le_card_inter_finset hâ„¬
  rwa [card_compl, Fintype.card_finset, tsub_mul, tsub_le_iff_tsub_le, â† mul_tsub, â†
    card_sdiff inter_subset_right, sdiff_inter_self_right, sdiff_compl,
    _root_.inf_comm] at this
#align is_upper_set.card_inter_le_finset IsUpperSet.card_inter_le_finset

/-- **Harris-Kleitman inequality**: Lower sets and upper sets of finsets anticorrelate. -/
theorem IsLowerSet.card_inter_le_finset (hğ’œ : IsLowerSet (ğ’œ : Set (Finset Î±)))
    (hâ„¬ : IsUpperSet (â„¬ : Set (Finset Î±))) :
    2 ^ Fintype.card Î± * (ğ’œ âˆ© â„¬).card â‰¤ ğ’œ.card * â„¬.card := by
  rw [inter_comm, mul_comm ğ’œ.card]
  exact hâ„¬.card_inter_le_finset hğ’œ
#align is_lower_set.card_inter_le_finset IsLowerSet.card_inter_le_finset

/-- **Harris-Kleitman inequality**: Any two upper sets of finsets correlate. -/
theorem IsUpperSet.le_card_inter_finset (hğ’œ : IsUpperSet (ğ’œ : Set (Finset Î±)))
    (hâ„¬ : IsUpperSet (â„¬ : Set (Finset Î±))) :
    ğ’œ.card * â„¬.card â‰¤ 2 ^ Fintype.card Î± * (ğ’œ âˆ© â„¬).card := by
  rw [â† isLowerSet_compl, â† coe_compl] at hğ’œ
  have := hğ’œ.card_inter_le_finset hâ„¬
  rwa [card_compl, Fintype.card_finset, tsub_mul, le_tsub_iff_le_tsub, â† mul_tsub, â†
    card_sdiff inter_subset_right, sdiff_inter_self_right, sdiff_compl,
    _root_.inf_comm] at this
  Â· exact mul_le_mul_left' (card_le_card inter_subset_right) _
  Â· rw [â† Fintype.card_finset]
    exact mul_le_mul_right' (card_le_univ _) _
#align is_upper_set.le_card_inter_finset IsUpperSet.le_card_inter_finset
