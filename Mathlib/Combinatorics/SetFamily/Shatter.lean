/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Combinatorics.SetFamily.Compression.Down
import Mathlib.Data.Fintype.Powerset
import Mathlib.Order.Interval.Finset.Nat
import Mathlib.Algebra.BigOperators.Group.Finset.Basic

/-!
# Shattering families

This file defines the shattering property and VC-dimension of set families.

## Main declarations

* `Finset.Shatters`: The shattering property.
* `Finset.shatterer`: The set family of sets shattered by a set family.
* `Finset.vcDim`: The Vapnik-Chervonenkis dimension.

## TODO

* Order-shattering
* Strong shattering
-/

open scoped FinsetFamily

namespace Finset
variable {Î± : Type*} [DecidableEq Î±] {ğ’œ â„¬ : Finset (Finset Î±)} {s t : Finset Î±} {a : Î±}

/-- A set family `ğ’œ` shatters a set `s` if all subsets of `s` can be obtained as the intersection
of `s` and some element of the set family, and we denote this `ğ’œ.Shatters s`. We also say that `s`
is *traced* by `ğ’œ`. -/
def Shatters (ğ’œ : Finset (Finset Î±)) (s : Finset Î±) : Prop := âˆ€ â¦ƒtâ¦„, t âŠ† s â†’ âˆƒ u âˆˆ ğ’œ, s âˆ© u = t

instance : DecidablePred ğ’œ.Shatters := fun _s â†¦ decidableForallOfDecidableSubsets

lemma Shatters.exists_inter_eq_singleton (hs : Shatters ğ’œ s) (ha : a âˆˆ s) : âˆƒ t âˆˆ ğ’œ, s âˆ© t = {a} :=
  hs <| singleton_subset_iff.2 ha

lemma Shatters.mono_left (h : ğ’œ âŠ† â„¬) (hğ’œ : ğ’œ.Shatters s) : â„¬.Shatters s :=
  fun _t ht â†¦ let âŸ¨u, hu, hutâŸ© := hğ’œ ht; âŸ¨u, h hu, hutâŸ©

lemma Shatters.mono_right (h : t âŠ† s) (hs : ğ’œ.Shatters s) : ğ’œ.Shatters t := fun u hu â†¦ by
  obtain âŸ¨v, hv, rflâŸ© := hs (hu.trans h); exact âŸ¨v, hv, inf_congr_right hu <| inf_le_of_left_le hâŸ©

lemma Shatters.exists_superset (h : ğ’œ.Shatters s) : âˆƒ t âˆˆ ğ’œ, s âŠ† t :=
  let âŸ¨t, ht, hstâŸ© := h Subset.rfl; âŸ¨t, ht, inter_eq_left.1 hstâŸ©

lemma shatters_of_forall_subset (h : âˆ€ t, t âŠ† s â†’ t âˆˆ ğ’œ) : ğ’œ.Shatters s :=
  fun t ht â†¦ âŸ¨t, h _ ht, inter_eq_right.2 htâŸ©

protected lemma Shatters.nonempty (h : ğ’œ.Shatters s) : ğ’œ.Nonempty :=
  let âŸ¨t, ht, _âŸ© := h Subset.rfl; âŸ¨t, htâŸ©

@[simp] lemma shatters_empty : ğ’œ.Shatters âˆ… â†” ğ’œ.Nonempty :=
  âŸ¨Shatters.nonempty, fun âŸ¨s, hsâŸ© t ht â†¦ âŸ¨s, hs, by rwa [empty_inter, eq_comm, â† subset_empty]âŸ©âŸ©

protected lemma Shatters.subset_iff (h : ğ’œ.Shatters s) : t âŠ† s â†” âˆƒ u âˆˆ ğ’œ, s âˆ© u = t :=
  âŸ¨fun ht â†¦ h ht, by rintro âŸ¨u, _, rflâŸ©; exact inter_subset_leftâŸ©

lemma shatters_iff : ğ’œ.Shatters s â†” ğ’œ.image (fun t â†¦ s âˆ© t) = s.powerset :=
  âŸ¨fun h â†¦ by ext t; rw [mem_image, mem_powerset, h.subset_iff],
    fun h t ht â†¦ by rwa [â† mem_powerset, â† h, mem_image] at htâŸ©

lemma univ_shatters [Fintype Î±] : univ.Shatters s :=
  shatters_of_forall_subset fun _ _ â†¦ mem_univ _

@[simp] lemma shatters_univ [Fintype Î±] : ğ’œ.Shatters univ â†” ğ’œ = univ := by
  rw [shatters_iff, powerset_univ]; simp_rw [univ_inter, image_id']

/-- The set family of sets that are shattered by `ğ’œ`. -/
def shatterer (ğ’œ : Finset (Finset Î±)) : Finset (Finset Î±) :=
  {s âˆˆ ğ’œ.biUnion powerset | ğ’œ.Shatters s}

@[simp] lemma mem_shatterer : s âˆˆ ğ’œ.shatterer â†” ğ’œ.Shatters s := by
  refine mem_filter.trans <| and_iff_right_of_imp fun h â†¦ ?_
  simp_rw [mem_biUnion, mem_powerset]
  exact h.exists_superset

@[gcongr] lemma shatterer_mono (h : ğ’œ âŠ† â„¬) : ğ’œ.shatterer âŠ† â„¬.shatterer :=
  fun _ â†¦ by simpa using Shatters.mono_left h

lemma subset_shatterer (h : IsLowerSet (ğ’œ : Set (Finset Î±))) : ğ’œ âŠ† ğ’œ.shatterer :=
  fun _s hs â†¦ mem_shatterer.2 fun t ht â†¦ âŸ¨t, h ht hs, inter_eq_right.2 htâŸ©

@[simp] lemma isLowerSet_shatterer (ğ’œ : Finset (Finset Î±)) :
    IsLowerSet (ğ’œ.shatterer : Set (Finset Î±)) := fun s t â†¦ by simpa using Shatters.mono_right

@[simp] lemma shatterer_eq : ğ’œ.shatterer = ğ’œ â†” IsLowerSet (ğ’œ : Set (Finset Î±)) := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ Subset.antisymm (fun s hs â†¦ ?_) <| subset_shatterer hâŸ©
  Â· rw [â† h]
    exact isLowerSet_shatterer _
  Â· obtain âŸ¨t, ht, hstâŸ© := (mem_shatterer.1 hs).exists_superset
    exact h hst ht

@[simp] lemma shatterer_idem : ğ’œ.shatterer.shatterer = ğ’œ.shatterer := by simp

@[simp] lemma shatters_shatterer : ğ’œ.shatterer.Shatters s â†” ğ’œ.Shatters s := by
  simp_rw [â† mem_shatterer, shatterer_idem]

protected alias âŸ¨_, Shatters.shattererâŸ© := shatters_shatterer

private lemma aux (h : âˆ€ t âˆˆ ğ’œ, a âˆ‰ t) (ht : ğ’œ.Shatters t) : a âˆ‰ t := by
  obtain âŸ¨u, hu, htuâŸ© := ht.exists_superset; exact notMem_mono htu <| h u hu

/-- Pajor's variant of the **Sauer-Shelah lemma**. -/
lemma card_le_card_shatterer (ğ’œ : Finset (Finset Î±)) : #ğ’œ â‰¤ #ğ’œ.shatterer := by
  refine memberFamily_induction_on ğ’œ ?_ ?_ ?_
  Â· simp
  Â· rfl
  intro a ğ’œ ihâ‚€ ihâ‚
  set â„¬ : Finset (Finset Î±) :=
    ((memberSubfamily a ğ’œ).shatterer âˆ© (nonMemberSubfamily a ğ’œ).shatterer).image (insert a)
  have hâ„¬ : #â„¬ = #((memberSubfamily a ğ’œ).shatterer âˆ© (nonMemberSubfamily a ğ’œ).shatterer) := by
    refine card_image_of_injOn <| insert_erase_invOn.2.injOn.mono ?_
    simp only [coe_inter, Set.subset_def, Set.mem_inter_iff, mem_coe, Set.mem_setOf_eq, and_imp,
      mem_shatterer]
    exact fun s _ â†¦ aux (fun t ht â†¦ (mem_filter.1 ht).2)
  rw [â† card_memberSubfamily_add_card_nonMemberSubfamily a]
  refine (Nat.add_le_add ihâ‚ ihâ‚€).trans ?_
  rw [â† card_union_add_card_inter, â† hâ„¬, â† card_union_of_disjoint]
  swap
  Â· simp only [â„¬, disjoint_left, mem_union, mem_shatterer, mem_image, not_exists, not_and]
    rintro _ (hs | hs) s - rfl
    Â· exact aux (fun t ht â†¦ (mem_memberSubfamily.1 ht).2) hs <| mem_insert_self _ _
    Â· exact aux (fun t ht â†¦ (mem_nonMemberSubfamily.1 ht).2) hs <| mem_insert_self _ _
  refine card_mono <| union_subset (union_subset ?_ <| shatterer_mono <| filter_subset _ _) ?_
  Â· simp only [subset_iff, mem_shatterer]
    rintro s hs t ht
    obtain âŸ¨u, hu, rflâŸ© := hs ht
    rw [mem_memberSubfamily] at hu
    refine âŸ¨insert a u, hu.1, inter_insert_of_notMem fun ha â†¦ ?_âŸ©
    obtain âŸ¨v, hv, hsvâŸ© := hs.exists_inter_eq_singleton ha
    rw [mem_memberSubfamily] at hv
    rw [â† singleton_subset_iff (a := a), â† hsv] at hv
    exact hv.2 inter_subset_right
  Â· refine forall_mem_image.2 fun s hs â†¦ mem_shatterer.2 fun t ht â†¦ ?_
    simp only [mem_inter, mem_shatterer] at hs
    rw [subset_insert_iff] at ht
    by_cases ha : a âˆˆ t
    Â· obtain âŸ¨u, hu, hsuâŸ© := hs.1 ht
      rw [mem_memberSubfamily] at hu
      refine âŸ¨_, hu.1, ?_âŸ©
      rw [â† insert_inter_distrib, hsu, insert_erase ha]
    Â· obtain âŸ¨u, hu, hsuâŸ© := hs.2 ht
      rw [mem_nonMemberSubfamily] at hu
      refine âŸ¨_, hu.1, ?_âŸ©
      rwa [insert_inter_of_notMem hu.2, hsu, erase_eq_self]

lemma Shatters.of_compression (hs : (ğ““ a ğ’œ).Shatters s) : ğ’œ.Shatters s := by
  intro t ht
  obtain âŸ¨u, hu, rflâŸ© := hs ht
  rw [Down.mem_compression] at hu
  obtain hu | hu := hu
  Â· exact âŸ¨u, hu.1, rflâŸ©
  by_cases ha : a âˆˆ s
  Â· obtain âŸ¨v, hv, hsvâŸ© := hs <| insert_subset ha ht
    rw [Down.mem_compression] at hv
    obtain hv | hv := hv
    Â· refine âŸ¨erase v a, hv.2, ?_âŸ©
      rw [inter_erase, hsv, erase_insert]
      rintro ha
      rw [insert_eq_self.2 (mem_inter.1 ha).2] at hu
      exact hu.1 hu.2
    rw [insert_eq_self.2 <| inter_subset_right (sâ‚ := s) ?_] at hv
    cases hv.1 hv.2
    rw [hsv]
    exact mem_insert_self _ _
  Â· refine âŸ¨insert a u, hu.2, ?_âŸ©
    rw [inter_insert_of_notMem ha]

lemma shatterer_compress_subset_shatterer (a : Î±) (ğ’œ : Finset (Finset Î±)) :
    (ğ““ a ğ’œ).shatterer âŠ† ğ’œ.shatterer := by
  simp only [subset_iff, mem_shatterer]; exact fun s hs â†¦ hs.of_compression

/-! ### Vapnik-Chervonenkis dimension -/

/-- The Vapnik-Chervonenkis dimension of a set family is the maximal size of a set it shatters. -/
def vcDim (ğ’œ : Finset (Finset Î±)) : â„• := ğ’œ.shatterer.sup card

@[gcongr] lemma vcDim_mono (hğ’œâ„¬ : ğ’œ âŠ† â„¬) : ğ’œ.vcDim â‰¤ â„¬.vcDim := by unfold vcDim; gcongr

lemma Shatters.card_le_vcDim (hs : ğ’œ.Shatters s) : #s â‰¤ ğ’œ.vcDim := le_sup <| mem_shatterer.2 hs

/-- Down-compressing decreases the VC-dimension. -/
lemma vcDim_compress_le (a : Î±) (ğ’œ : Finset (Finset Î±)) : (ğ““ a ğ’œ).vcDim â‰¤ ğ’œ.vcDim :=
  sup_mono <| shatterer_compress_subset_shatterer _ _

/-- The **Sauer-Shelah lemma**. -/
lemma card_shatterer_le_sum_vcDim [Fintype Î±] :
    #ğ’œ.shatterer â‰¤ âˆ‘ k âˆˆ Iic ğ’œ.vcDim, (Fintype.card Î±).choose k := by
  simp_rw [â† card_univ, â† card_powersetCard]
  refine (card_le_card fun s hs â†¦ mem_biUnion.2 âŸ¨#s, ?_âŸ©).trans card_biUnion_le
  exact âŸ¨mem_Iic.2 (mem_shatterer.1 hs).card_le_vcDim, mem_powersetCard_univ.2 rflâŸ©

end Finset
