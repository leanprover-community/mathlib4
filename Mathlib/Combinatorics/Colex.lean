/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import Mathlib.Algebra.GeomSum
import Mathlib.Data.Finset.Slice
import Mathlib.Order.SupClosed

#align_import combinatorics.colex from "leanprover-community/mathlib"@"f7fc89d5d5ff1db2d1242c7bb0e9062ce47ef47c"

/-!
# Colexigraphic order

We define the colex order for finite sets, and give a couple of important lemmas and properties
relating to it.

The colex ordering likes to avoid large values: If the biggest element of `t` is bigger than all
elements of `s`, then `s < t`.

In the special case of `â„•`, it can be thought of as the "binary" ordering. That is, order `s` based
on $âˆ‘_{i âˆˆ s} 2^i$. It's defined here on `Finset Î±` for any linear order `Î±`.

In the context of the Kruskal-Katona theorem, we are interested in how colex behaves for sets of a
fixed size. For example, for size 3, the colex order on â„• starts
`012, 013, 023, 123, 014, 024, 124, 034, 134, 234, ...`

## Main statements

* Colex order properties - linearity, decidability and so on.
* `Finset.Colex.forall_lt_mono`: if `s < t` in colex, and everything in `t` is `< a`, then
  everything in `s` is `< a`. This confirms the idea that an enumeration under colex will exhaust
  all sets using elements `< a` before allowing `a` to be included.
* `Finset.toColex_image_le_toColex_image`: Strictly monotone functions preserve colex.
* `Finset.geomSum_le_geomSum_iff_toColex_le_toColex`: Colex for Î± = â„• is the same as binary.
  This also proves binary expansions are unique.

## See also

Related files are:
* `Data.List.Lex`: Lexicographic order on lists.
* `Data.Pi.Lex`: Lexicographic order on `Î â‚— i, Î± i`.
* `Data.PSigma.Order`: Lexicographic order on `Î£' i, Î± i`.
* `Data.Sigma.Order`: Lexicographic order on `Î£ i, Î± i`.
* `Data.Prod.Lex`: Lexicographic order on `Î± Ã— Î²`.

## TODO

* Generalise `Colex.initSeg` so that it applies to `â„•`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

colex, colexicographic, binary
-/

open Finset Function

#align nat.sum_two_pow_lt Nat.geomSum_lt

variable {Î± Î² : Type*}

namespace Finset

/-- Type synonym of `Finset Î±` equipped with the colexicographic order rather than the inclusion
order. -/
@[ext]
structure Colex (Î±) :=
  /-- `toColex` is the "identity" function between `Finset Î±` and `Finset.Colex Î±`. -/
  toColex ::
  /-- `ofColex` is the "identity" function between `Finset.Colex Î±` and `Finset Î±`. -/
  (ofColex : Finset Î±)

-- TODO: Why can't we export?
--export Colex (toColex)

open Colex

instance : Inhabited (Colex Î±) := âŸ¨âŸ¨âˆ…âŸ©âŸ©

@[simp] lemma toColex_ofColex (s : Colex Î±) : toColex (ofColex s) = s := rfl
lemma ofColex_toColex (s : Finset Î±) : ofColex (toColex s) = s := rfl
lemma toColex_inj {s t : Finset Î±} : toColex s = toColex t â†” s = t := by simp
@[simp]
lemma ofColex_inj {s t : Colex Î±} : ofColex s = ofColex t â†” s = t := by cases s; cases t; simp
lemma toColex_ne_toColex {s t : Finset Î±} : toColex s â‰  toColex t â†” s â‰  t := by simp
lemma ofColex_ne_ofColex {s t : Colex Î±} : ofColex s â‰  ofColex t â†” s â‰  t := by simp

lemma toColex_injective : Injective (toColex : Finset Î± â†’ Colex Î±) := fun _ _ â†¦ toColex_inj.1
lemma ofColex_injective : Injective (ofColex : Colex Î± â†’ Finset Î±) := fun _ _ â†¦ ofColex_inj.1

namespace Colex
section PartialOrder
variable [PartialOrder Î±] [PartialOrder Î²] {f : Î± â†’ Î²} {ğ’œ ğ’œâ‚ ğ’œâ‚‚ : Finset (Finset Î±)}
  {s t u : Finset Î±} {a b : Î±}

instance instLE : LE (Colex Î±) where
  le s t := âˆ€ â¦ƒaâ¦„, a âˆˆ ofColex s â†’ a âˆ‰ ofColex t â†’ âˆƒ b, b âˆˆ ofColex t âˆ§ b âˆ‰ ofColex s âˆ§ a â‰¤ b

-- TODO: This lemma is weirdly useful given how strange its statement is.
-- Is there a nicer statement? Should this lemma be made public?
private lemma trans_aux (hst : toColex s â‰¤ toColex t) (htu : toColex t â‰¤ toColex u)
    (has : a âˆˆ s) (hat : a âˆ‰ t) : âˆƒ b, b âˆˆ u âˆ§ b âˆ‰ s âˆ§ a â‰¤ b := by
  classical
  let s' : Finset Î± := s.filter fun b â†¦ b âˆ‰ t âˆ§ a â‰¤ b
  have âŸ¨b, hb, hbmaxâŸ© := exists_maximal s' âŸ¨a, by simp [s', has, hat]âŸ©
  simp only [s', mem_filter, and_imp] at hb hbmax
  have âŸ¨c, hct, hcs, hbcâŸ© := hst hb.1 hb.2.1
  by_cases hcu : c âˆˆ u
  Â· exact âŸ¨c, hcu, hcs, hb.2.2.trans hbcâŸ©
  have âŸ¨d, hdu, hdt, hcdâŸ© := htu hct hcu
  have had : a â‰¤ d := hb.2.2.trans <| hbc.trans hcd
  refine âŸ¨d, hdu, fun hds â†¦ ?_, hadâŸ©
  exact hbmax d hds hdt had <| hbc.trans_lt <| hcd.lt_of_ne <| ne_of_mem_of_not_mem hct hdt

private lemma antisymm_aux (hst : toColex s â‰¤ toColex t) (hts : toColex t â‰¤ toColex s) : s âŠ† t := by
  intro a has
  by_contra! hat
  have âŸ¨_b, hbâ‚, hbâ‚‚, _âŸ© := trans_aux hst hts has hat
  exact hbâ‚‚ hbâ‚

instance instPartialOrder : PartialOrder (Colex Î±) where
  le_refl s a ha ha' := (ha' ha).elim
  le_antisymm s t hst hts := Colex.ext _ _ <| (antisymm_aux hst hts).antisymm (antisymm_aux hts hst)
  le_trans s t u hst htu a has hau := by
    by_cases hat : a âˆˆ ofColex t
    Â· have âŸ¨b, hbu, hbt, habâŸ© := htu hat hau
      by_cases hbs : b âˆˆ ofColex s
      Â· have âŸ¨c, hcu, hcs, hbcâŸ© := trans_aux hst htu hbs hbt
        exact âŸ¨c, hcu, hcs, hab.trans hbcâŸ©
      Â· exact âŸ¨b, hbu, hbs, habâŸ©
    Â· exact trans_aux hst htu has hat

lemma le_def {s t : Colex Î±} :
    s â‰¤ t â†” âˆ€ â¦ƒaâ¦„, a âˆˆ ofColex s â†’ a âˆ‰ ofColex t â†’ âˆƒ b, b âˆˆ ofColex t âˆ§ b âˆ‰ ofColex s âˆ§ a â‰¤ b :=
  Iff.rfl

lemma toColex_le_toColex :
    toColex s â‰¤ toColex t â†” âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ a âˆ‰ t â†’ âˆƒ b, b âˆˆ t âˆ§ b âˆ‰ s âˆ§ a â‰¤ b := Iff.rfl

lemma toColex_lt_toColex :
    toColex s < toColex t â†” s â‰  t âˆ§ âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ a âˆ‰ t â†’ âˆƒ b, b âˆˆ t âˆ§ b âˆ‰ s âˆ§ a â‰¤ b := by
  simp [lt_iff_le_and_ne, toColex_le_toColex, and_comm]

/-- If `s âŠ† t`, then `s â‰¤ t` in the colex order. Note the converse does not hold, as inclusion does
not form a linear order. -/
lemma toColex_mono : Monotone (toColex : Finset Î± â†’ Colex Î±) :=
  fun _s _t hst _a has hat â†¦ (hat <| hst has).elim

/-- If `s âŠ‚ t`, then `s < t` in the colex order. Note the converse does not hold, as inclusion does
not form a linear order. -/
lemma toColex_strictMono : StrictMono (toColex : Finset Î± â†’ Colex Î±) :=
  toColex_mono.strictMono_of_injective toColex_injective

/-- If `s âŠ† t`, then `s â‰¤ t` in the colex order. Note the converse does not hold, as inclusion does
not form a linear order. -/
lemma toColex_le_toColex_of_subset (h : s âŠ† t) : toColex s â‰¤ toColex t := toColex_mono h

/-- If `s âŠ‚ t`, then `s < t` in the colex order. Note the converse does not hold, as inclusion does
not form a linear order. -/
lemma toColex_lt_toColex_of_ssubset (h : s âŠ‚ t) : toColex s < toColex t := toColex_strictMono h

instance instOrderBot : OrderBot (Colex Î±) where
  bot := toColex âˆ…
  bot_le s a ha := by cases ha

@[simp] lemma toColex_empty : toColex (âˆ… : Finset Î±) = âŠ¥ := rfl
@[simp] lemma ofColex_bot : ofColex (âŠ¥ : Colex Î±) = âˆ… := rfl

/-- If `s â‰¤ t` in colex, and all elements in `t` are small, then all elements in `s` are small. -/
lemma forall_le_mono (hst : toColex s â‰¤ toColex t) (ht : âˆ€ b âˆˆ t, b â‰¤ a) : âˆ€ b âˆˆ s, b â‰¤ a := by
  rintro b hb
  by_cases b âˆˆ t
  Â· exact ht _ â€¹_â€º
  Â· obtain âŸ¨c, hct, -, hbcâŸ© := hst hb â€¹_â€º
    exact hbc.trans <| ht _ hct

/-- If `s â‰¤ t` in colex, and all elements in `t` are small, then all elements in `s` are small. -/
lemma forall_lt_mono (hst : toColex s â‰¤ toColex t) (ht : âˆ€ b âˆˆ t, b < a) : âˆ€ b âˆˆ s, b < a := by
  rintro b hb
  by_cases b âˆˆ t
  Â· exact ht _ â€¹_â€º
  Â· obtain âŸ¨c, hct, -, hbcâŸ© := hst hb â€¹_â€º
    exact hbc.trans_lt <| ht _ hct

/-- `s â‰¤ {a}` in colex iff all elements of `s` are strictly less than `a`, except possibly `a` in
which case `s = {a}`. -/
lemma toColex_le_singleton : toColex s â‰¤ toColex {a} â†” âˆ€ b âˆˆ s, b â‰¤ a âˆ§ (a âˆˆ s â†’ b = a) := by
  simp only [toColex_le_toColex, mem_singleton, and_assoc, exists_eq_left]
  refine forallâ‚‚_congr fun b _ â†¦ ?_; obtain rfl | hba := eq_or_ne b a <;> aesop

/-- `s < {a}` in colex iff all elements of `s` are strictly less than `a`. -/
lemma toColex_lt_singleton : toColex s < toColex {a} â†” âˆ€ b âˆˆ s, b < a := by
  rw [lt_iff_le_and_ne, toColex_le_singleton, toColex_ne_toColex]
  refine âŸ¨fun h b hb â†¦ (h.1 _ hb).1.lt_of_ne ?_,
    fun h â†¦ âŸ¨fun b hb â†¦ âŸ¨(h _ hb).le, fun ha â†¦ (lt_irrefl _ <| h _ ha).elimâŸ©, ?_âŸ©âŸ© <;> rintro rfl
  Â· refine h.2 <| eq_singleton_iff_unique_mem.2 âŸ¨hb, fun c hc â†¦ (h.1 _ hc).2 hbâŸ©
  Â· simp at h

/-- `{a} â‰¤ s` in colex iff `s` contains an element greated than or equal to `a`. -/
lemma singleton_le_toColex : (toColex {a} : Colex Î±) â‰¤ toColex s â†” âˆƒ x âˆˆ s, a â‰¤ x := by
  simp [toColex_le_toColex]; by_cases a âˆˆ s <;> aesop

/-- Colex is an extension of the base order. -/
lemma singleton_le_singleton : (toColex {a} : Colex Î±) â‰¤ toColex {b} â†” a â‰¤ b := by
  simp [toColex_le_singleton, eq_comm]

/-- Colex is an extension of the base order. -/
lemma singleton_lt_singleton : (toColex {a} : Colex Î±) < toColex {b} â†” a < b := by
  simp [toColex_lt_singleton]

variable [DecidableEq Î±]

instance instDecidableEq : DecidableEq (Colex Î±) := fun s t â†¦
  decidable_of_iff' (s.ofColex = t.ofColex) <| Colex.ext_iff _ _

instance instDecidableLE [@DecidableRel Î± (Â· â‰¤ Â·)] : @DecidableRel (Colex Î±) (Â· â‰¤ Â·) := fun s t â†¦
  decidable_of_iff'
    (âˆ€ â¦ƒaâ¦„, a âˆˆ ofColex s â†’ a âˆ‰ ofColex t â†’ âˆƒ b, b âˆˆ ofColex t âˆ§ b âˆ‰ ofColex s âˆ§ a â‰¤ b) Iff.rfl

instance instDecidableLT [@DecidableRel Î± (Â· â‰¤ Â·)] : @DecidableRel (Colex Î±) (Â· < Â·) :=
  decidableLTOfDecidableLE

lemma le_iff_sdiff_subset_lowerClosure {s t : Colex Î±} :
    s â‰¤ t â†” (ofColex s : Set Î±) \ ofColex t âŠ† lowerClosure (ofColex t \ ofColex s : Set Î±) := by
  simp [le_def, Set.subset_def, and_assoc]

/-- The colexigraphic order is insensitive to removing the same elements from both sets. -/
lemma toColex_sdiff_le_toColex_sdiff (hus : u âŠ† s) (hut : u âŠ† t) :
    toColex (s \ u) â‰¤ toColex (t \ u) â†” toColex s â‰¤ toColex t := by
  simp_rw [toColex_le_toColex, â† and_imp, â† and_assoc, â† mem_sdiff,
    sdiff_sdiff_sdiff_cancel_right hus, sdiff_sdiff_sdiff_cancel_right hut]

/-- The colexigraphic order is insensitive to removing the same elements from both sets. -/
lemma toColex_sdiff_lt_toColex_sdiff (hus : u âŠ† s) (hut : u âŠ† t) :
    toColex (s \ u) < toColex (t \ u) â†” toColex s < toColex t :=
  lt_iff_lt_of_le_iff_le' (toColex_sdiff_le_toColex_sdiff hut hus) <|
    toColex_sdiff_le_toColex_sdiff hus hut

@[simp] lemma toColex_sdiff_le_toColex_sdiff' :
    toColex (s \ t) â‰¤ toColex (t \ s) â†” toColex s â‰¤ toColex t := by
  simpa using toColex_sdiff_le_toColex_sdiff (inter_subset_left s t) (inter_subset_right s t)

@[simp] lemma toColex_sdiff_lt_toColex_sdiff' :
 toColex (s \ t) < toColex (t \ s) â†” toColex s < toColex t := by
  simpa using toColex_sdiff_lt_toColex_sdiff (inter_subset_left s t) (inter_subset_right s t)

end PartialOrder

variable [LinearOrder Î±] [LinearOrder Î²] {f : Î± â†’ Î²} {ğ’œ ğ’œâ‚ ğ’œâ‚‚ : Finset (Finset Î±)}
  {s t u : Finset Î±} {a b : Î±} {r : â„•}

instance instLinearOrder : LinearOrder (Colex Î±) where
  le_total s t := by
    classical
    obtain rfl | hts := eq_or_ne t s
    Â· simp
    have âŸ¨a, ha, hamaxâŸ© := exists_max_image _ id (symmDiff_nonempty.2 <| ofColex_ne_ofColex.2 hts)
    simp_rw [mem_symmDiff] at ha hamax
    exact ha.imp (fun ha b hbs hbt â†¦ âŸ¨a, ha.1, ha.2, hamax _ <| Or.inr âŸ¨hbs, hbtâŸ©âŸ©)
      (fun ha b hbt hbs â†¦ âŸ¨a, ha.1, ha.2, hamax _ <| Or.inl âŸ¨hbt, hbsâŸ©âŸ©)
  decidableLE := instDecidableLE
  decidableLT := instDecidableLT

open scoped symmDiff

private lemma max_mem_aux {s t : Colex Î±} (hst : s â‰  t) : (ofColex s âˆ† ofColex t).Nonempty := by
  simpa

lemma toColex_lt_toColex_iff_exists_forall_lt :
    toColex s < toColex t â†” âˆƒ a âˆˆ t, a âˆ‰ s âˆ§ âˆ€ b âˆˆ s, b âˆ‰ t â†’ b < a := by
  rw [â† not_le, toColex_le_toColex, not_forall]
  simp only [not_forall, not_exists, not_and, not_le, exists_prop, exists_and_left]

lemma lt_iff_exists_forall_lt {s t : Colex Î±} :
    s < t â†” âˆƒ a âˆˆ ofColex t, a âˆ‰ ofColex s âˆ§ âˆ€ b âˆˆ ofColex s, b âˆ‰ ofColex t â†’ b < a :=
  toColex_lt_toColex_iff_exists_forall_lt

lemma toColex_le_toColex_iff_max'_mem :
    toColex s â‰¤ toColex t â†” âˆ€ hst : s â‰  t, (s âˆ† t).max' (symmDiff_nonempty.2 hst) âˆˆ t := by
  refine âŸ¨fun h hst â†¦ ?_, fun h a has hat â†¦ ?_âŸ©
  Â· set m := (s âˆ† t).max' (symmDiff_nonempty.2 hst)
    by_contra hmt
    have hms : m âˆˆ s := by simpa [mem_symmDiff, hmt] using max'_mem _ <| symmDiff_nonempty.2 hst
    have âŸ¨b, hbt, hbs, hmbâŸ© := h hms hmt
    exact lt_irrefl _ <| (max'_lt_iff _ _).1 (hmb.lt_of_ne <| ne_of_mem_of_not_mem hms hbs) _ <|
      mem_symmDiff.2 <| Or.inr âŸ¨hbt, hbsâŸ©
  Â· have hst : s â‰  t := ne_of_mem_of_not_mem' has hat
    refine âŸ¨_, h hst, ?_, le_max' _ _ <| mem_symmDiff.2 <| Or.inl âŸ¨has, hatâŸ©âŸ©
    simpa [mem_symmDiff, h hst] using max'_mem _ <| symmDiff_nonempty.2 hst

lemma le_iff_max'_mem {s t : Colex Î±} :
    s â‰¤ t â†” âˆ€ h : s â‰  t, (ofColex s âˆ† ofColex t).max' (max_mem_aux h) âˆˆ ofColex t :=
  toColex_le_toColex_iff_max'_mem.trans
    âŸ¨fun h hst â†¦ h <| ofColex_ne_ofColex.2 hst, fun h hst â†¦ h <| ofColex_ne_ofColex.1 hstâŸ©

lemma toColex_lt_toColex_iff_max'_mem :
    toColex s < toColex t â†” âˆƒ hst : s â‰  t, (s âˆ† t).max' (symmDiff_nonempty.2 hst) âˆˆ t := by
  rw [lt_iff_le_and_ne, toColex_le_toColex_iff_max'_mem]; aesop

lemma lt_iff_max'_mem {s t : Colex Î±} :
    s < t â†” âˆƒ h : s â‰  t, (ofColex s âˆ† ofColex t).max' (max_mem_aux h) âˆˆ ofColex t := by
  rw [lt_iff_le_and_ne, le_iff_max'_mem]; aesop

/-- Strictly monotone functions preserve the colex ordering. -/
lemma toColex_image_le_toColex_image (hf : StrictMono f) :
    toColex (s.image f) â‰¤ toColex (t.image f) â†” toColex s â‰¤ toColex t := by
  simp [toColex_le_toColex, hf.le_iff_le, hf.injective.eq_iff]

/-- Strictly monotone functions preserve the colex ordering. -/
lemma toColex_image_lt_toColex_image (hf : StrictMono f) :
    toColex (s.image f) < toColex (t.image f) â†” toColex s < toColex t :=
  lt_iff_lt_of_le_iff_le <| toColex_image_le_toColex_image hf

lemma toColex_image_ofColex_strictMono (hf : StrictMono f) :
    StrictMono fun s â†¦ toColex <| image f <| ofColex s :=
  fun _s _t â†¦ (toColex_image_lt_toColex_image hf).2

/-! ### Initial segments -/

/-- `ğ’œ` is an initial segment of the colexigraphic order on sets of `r`, and that if `t` is below
`s` in colex where `t` has size `r` and `s` is in `ğ’œ`, then `t` is also in `ğ’œ`. In effect, `ğ’œ` is
downwards closed with respect to colex among sets of size `r`. -/
def IsInitSeg (ğ’œ : Finset (Finset Î±)) (r : â„•) : Prop :=
  (ğ’œ : Set (Finset Î±)).Sized r âˆ§
    âˆ€ â¦ƒs t : Finset Î±â¦„, s âˆˆ ğ’œ â†’ toColex t < toColex s âˆ§ t.card = r â†’ t âˆˆ ğ’œ

@[simp] lemma isInitSeg_empty : IsInitSeg (âˆ… : Finset (Finset Î±)) r := by simp [IsInitSeg]

/-- Initial segments are nested in some way. In particular, if they're the same size they're equal.
-/
lemma IsInitSeg.total (hâ‚ : IsInitSeg ğ’œâ‚ r) (hâ‚‚ : IsInitSeg ğ’œâ‚‚ r) : ğ’œâ‚ âŠ† ğ’œâ‚‚ âˆ¨ ğ’œâ‚‚ âŠ† ğ’œâ‚ := by
  classical
  simp_rw [â† sdiff_eq_empty_iff_subset, â† not_nonempty_iff_eq_empty]
  by_contra! h
  have âŸ¨âŸ¨s, hsâŸ©, t, htâŸ© := h
  rw [mem_sdiff] at hs ht
  obtain hst | hst | hts := trichotomous_of (Î± := Colex Î±) (Â· < Â·) (toColex s) (toColex t)
  Â· exact hs.2 <| hâ‚‚.2 ht.1 âŸ¨hst, hâ‚.1 hs.1âŸ©
  Â· simp only [toColex.injEq] at hst
    exact ht.2 <| hst â–¸ hs.1
  Â· exact ht.2 <| hâ‚.2 hs.1 âŸ¨hts, hâ‚‚.1 ht.1âŸ©

variable [Fintype Î±]

/-- The initial segment of the colexicographic order on sets with `s.card` elements and ending at
`s`. -/
def initSeg (s : Finset Î±) : Finset (Finset Î±) :=
  univ.filter fun t â†¦ s.card = t.card âˆ§ toColex t â‰¤ toColex s

@[simp]
lemma mem_initSeg : t âˆˆ initSeg s â†” s.card = t.card âˆ§ toColex t â‰¤ toColex s := by simp [initSeg]

lemma mem_initSeg_self : s âˆˆ initSeg s := by simp
@[simp] lemma initSeg_nonempty : (initSeg s).Nonempty := âŸ¨s, mem_initSeg_selfâŸ©

lemma isInitSeg_initSeg : IsInitSeg (initSeg s) s.card := by
  refine âŸ¨fun t ht => (mem_initSeg.1 ht).1.symm, fun tâ‚ tâ‚‚ htâ‚ htâ‚‚ â†¦ mem_initSeg.2 âŸ¨htâ‚‚.2.symm, ?_âŸ©âŸ©
  rw [mem_initSeg] at htâ‚
  exact htâ‚‚.1.le.trans htâ‚.2

lemma IsInitSeg.exists_initSeg (hğ’œ : IsInitSeg ğ’œ r) (hğ’œâ‚€ : ğ’œ.Nonempty) :
    âˆƒ s : Finset Î±, s.card = r âˆ§ ğ’œ = initSeg s := by
  have hs := sup'_mem (ofColex â»Â¹' ğ’œ) (LinearOrder.supClosed _) ğ’œ hğ’œâ‚€ toColex
    (fun a ha â†¦ by simpa using ha)
  refine âŸ¨_, hğ’œ.1 hs, ?_âŸ©
  ext t
  rw [mem_initSeg]
  refine âŸ¨fun p â†¦ ?_, ?_âŸ©
  Â· rw [hğ’œ.1 p, hğ’œ.1 hs]
    exact âŸ¨rfl, le_sup' _ pâŸ©
  rintro âŸ¨cards, leâŸ©
  obtain p | p := le.eq_or_lt
  Â· rwa [toColex_inj.1 p]
  Â· exact hğ’œ.2 hs âŸ¨p, cards â–¸ hğ’œ.1 hsâŸ©

/-- Being a nonempty initial segment of colex is equivalent to being an `initSeg`. -/
lemma isInitSeg_iff_exists_initSeg :
    IsInitSeg ğ’œ r âˆ§ ğ’œ.Nonempty â†” âˆƒ s : Finset Î±, s.card = r âˆ§ ğ’œ = initSeg s := by
  refine âŸ¨fun hğ’œ â†¦ hğ’œ.1.exists_initSeg hğ’œ.2, ?_âŸ©
  rintro âŸ¨s, rfl, rflâŸ©
  exact âŸ¨isInitSeg_initSeg, initSeg_nonemptyâŸ©

end Colex

open Colex

/-!
### Colex on `â„•`

The colexicographic order agrees with the order induced by interpreting a set of naturals as a
`n`-ary expansion.
-/

section Nat
variable {s t : Finset â„•} {n : â„•}

lemma geomSum_ofColex_strictMono (hn : 2 â‰¤ n) : StrictMono fun s â†¦ âˆ‘ k âˆˆ ofColex s, n ^ k := by
  rintro âŸ¨sâŸ© âŸ¨tâŸ© hst
  rw [toColex_lt_toColex_iff_exists_forall_lt] at hst
  obtain âŸ¨a, hat, has, haâŸ© := hst
  rw [â† sum_sdiff_lt_sum_sdiff]
  exact (Nat.geomSum_lt hn <| by simpa).trans_le <| single_le_sum (fun _ _ â†¦ by positivity) <|
    mem_sdiff.2 âŸ¨hat, hasâŸ©

/-- For finsets of naturals of naturals, the colexicographic order is equivalent to the order
induced by the `n`-ary expansion. -/
lemma geomSum_le_geomSum_iff_toColex_le_toColex (hn : 2 â‰¤ n) :
    âˆ‘ k âˆˆ s, n ^ k â‰¤ âˆ‘ k âˆˆ t, n ^ k â†” toColex s â‰¤ toColex t :=
  (geomSum_ofColex_strictMono hn).le_iff_le

/-- For finsets of naturals of naturals, the colexicographic order is equivalent to the order
induced by the `n`-ary expansion. -/
lemma geomSum_lt_geomSum_iff_toColex_lt_toColex (hn : 2 â‰¤ n) :
    âˆ‘ i âˆˆ s, n ^ i < âˆ‘ i âˆˆ t, n ^ i â†” toColex s < toColex t :=
  (geomSum_ofColex_strictMono hn).lt_iff_lt

-- TODO: Package the above in the `n = 2` case as an order isomorphism `Colex â„• â‰ƒo â„•`

end Nat
end Finset
