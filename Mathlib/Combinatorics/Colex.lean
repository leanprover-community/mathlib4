/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov
-/
import Mathlib.Algebra.GeomSum
import Mathlib.Data.Finset.Slice
import Mathlib.Order.SupClosed

#align_import combinatorics.colex from "leanprover-community/mathlib"@"f7fc89d5d5ff1db2d1242c7bb0e9062ce47ef47c"

/-!
# Colexigraphic order

We define the colex order for finite sets, and give a couple of important lemmas and properties
relating to it.

The colex ordering likes to avoid large values: If the biggest element of `t` is bigger than all elements of `s`, then `s < t`.

In the special case of `â„•`, it can be thought of as the "binary" ordering. That is, order `s` based
on `âˆ‘_{i âˆˆ s} 2^i`. It's defined here on `Finset Î±` for any linear order `Î±`.

In the context of the Kruskal-Katona theorem, we are interested in how colex behaves for sets of a
fixed size. For example, for size 3, the colex order on â„• starts
`123, 124, 134, 234, 125, 135, 235, 145, 245, 345, ...`

## Main statements

* Colex order properties - linearity, decidability and so on.
* `Finset.Colex.forall_lt_mono`: if `s < t` in colex, and everything in `t` is `< a`, then
  everything in `s` is `< a`. This confirms the idea that an enumeration under colex will exhaust
  all sets using elements `< a` before allowing `a` to be included.
* `Finse.toColex_image_lt_toColex_image`: Strictly monotone functions preserve colex.
* `Finset.sum_two_pow_le_iff_colex_le`: colex for Î± = â„• is the same as binary
  (this also proves binary expansions are unique)

## See also

Related files are:
* `Data.List.Lex`: Lexicographic order on lists.
* `Data.Pi.Lex`: Lexicographic order on `Î â‚— i, Î± i`.
* `Data.PSigma.Order`: Lexicographic order on `Î£' i, Î± i`.
* `Data.Sigma.Order`: Lexicographic order on `Î£ i, Î± i`.
* `Data.Prod.Lex`: Lexicographic order on `Î± Ã— Î²`.

## TODO

* Generalise `Colex.initSeg` so that it applies to `â„•`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

colex, colexicographic, binary
-/

open Finset
open scoped BigOperators

/-- If all the elements of a finset `s` of naturals are less than `n`, then the sum of their powers of `2` is less than `2 ^ k`. -/
lemma Nat.sum_two_pow_lt {n : â„•} {s : Finset â„•} (hs : âˆ€ k âˆˆ s, k < n) :
    âˆ‘ k in s, 2 ^ k < 2 ^ n := by
  calc
    âˆ‘ k in s, 2 ^ k â‰¤ âˆ‘ k in range n, 2 ^ k := sum_le_sum_of_subset fun k hk â†¦ mem_range.2 $ hs _ hk
    _ = 2 ^ n - 1 := by
        simp_rw [â†one_add_one_eq_two, â†geom_sum_mul_add 1 n, mul_one, add_tsub_cancel_right]
    _ < 2 ^ n := tsub_lt_self (by positivity) zero_lt_one
#align nat.sum_two_pow_lt Nat.sum_two_pow_lt

variable {Î± Î² : Type*}

namespace Finset

/-- Type synonym of `Finset Î±` equipped with the colexicographic order rather than the inclusion
order. -/
@[ext]
structure Colex (Î±) := toColex :: (ofColex : Finset Î±)

-- TODO: Why can't we export?
--export Colex (toColex)

open Colex

/-- `toColex` is the "identity" function between `Finset Î±` and `Finset.Colex Î±`. -/
add_decl_doc toColex

/-- `ofColex` is the "identity" function between `Finset.Colex Î±` and `Finset Î±`. -/
add_decl_doc ofColex

instance : Inhabited (Colex Î±) := âŸ¨âŸ¨âˆ…âŸ©âŸ©

@[simp] lemma toColex_ofColex (s : Colex Î±) : toColex (ofColex s) = s := rfl
lemma ofColex_toColex (s : Finset Î±) : ofColex (toColex s) = s := rfl
lemma toColex_inj {s t : Finset Î±} : toColex s = toColex t â†” s = t := by simp
@[simp] lemma ofColex_inj {s t : Colex Î±} : ofColex s = ofColex t â†” s = t := by cases s; cases t; simp
lemma toColex_ne_toColex {s t : Finset Î±} : toColex s â‰  toColex t â†” s â‰  t := by simp
lemma ofColex_ne_ofColex {s t : Colex Î±} : ofColex s â‰  ofColex t â†” s â‰  t := by simp

namespace Colex
section LT
variable [LT Î±] {s t u : Finset Î±}

/-- `s` is less than `t` in the colex ordering if the largest thing that's not in both sets is in t.
In other words, `max (s âˆ† t) âˆˆ t` (if the maximum exists). -/
instance instLT : LT (Colex Î±) :=
  âŸ¨fun s t â†¦ âˆƒ a, (âˆ€ â¦ƒxâ¦„, a < x â†’ (x âˆˆ ofColex s â†” x âˆˆ ofColex t)) âˆ§ a âˆ‰ ofColex s âˆ§ a âˆˆ ofColex tâŸ©

/-- We can define (â‰¤) in the obvious way. -/
instance instLE : LE (Colex Î±) := âŸ¨fun s t â†¦ s = t âˆ¨ s < tâŸ©

lemma lt_def {s t : Colex Î±} :
    s < t â†” âˆƒ a, (âˆ€ â¦ƒxâ¦„, a < x â†’ (x âˆˆ ofColex s â†” x âˆˆ ofColex t)) âˆ§ a âˆ‰ ofColex s âˆ§ a âˆˆ ofColex t :=
  Iff.rfl

lemma le_def {s t : Colex Î±} :
    s â‰¤ t â†” s = t âˆ¨
      âˆƒ a, (âˆ€ â¦ƒxâ¦„, a < x â†’ (x âˆˆ ofColex s â†” x âˆˆ ofColex t)) âˆ§ a âˆ‰ ofColex s âˆ§ a âˆˆ ofColex t :=
  Iff.rfl

lemma toColex_lt_toColex :
    toColex s < toColex t â†” âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ s â†” x âˆˆ t)) âˆ§ k âˆ‰ s âˆ§ k âˆˆ t := Iff.rfl

lemma toColex_le_toColex :
    toColex s â‰¤ toColex t â†” s = t âˆ¨ âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ s â†” x âˆˆ t)) âˆ§ k âˆ‰ s âˆ§ k âˆˆ t := by
  simp [le_def]

instance instIsIrrefl : IsIrrefl (Colex Î±) (Â· < Â·) := âŸ¨by simp [lt_def]âŸ©

variable [DecidableEq Î±]

/-- The colexigraphic order is insensitive to removing elements. -/
lemma toColex_sdiff_lt_toColex_sdiff (hus : u âŠ† s) (hut : u âŠ† t) :
    toColex (s \ u) < toColex (t \ u) â†” toColex s < toColex t := by
  simp only [toColex_lt_toColex, toColex_lt_toColex, mem_sdiff, not_and, not_not]
  refine exists_congr fun k â†¦ âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨h, hksu, hkt, hkuâŸ©
    refine âŸ¨fun x hx â†¦ ?_, mt hksu hku, hktâŸ©
    specialize h hx
    tauto
  Â· rintro âŸ¨h, hks, hktâŸ©
    exact âŸ¨fun x hx â†¦ by rw [h hx], fun hks' â†¦ (hks hks').elim, hkt, not_mem_mono hus hksâŸ©

@[simp] lemma toColex_sdiff_lt_toColex_sdiff' :
 toColex (s \ t) < toColex (t \ s) â†” toColex s < toColex t := by
  simpa using toColex_sdiff_lt_toColex_sdiff (inter_subset_left s t) (inter_subset_right s t)

end LT

section LinearOrder
variable [LinearOrder Î±] [LinearOrder Î²] {f : Î± â†’ Î²} {ğ’œ ğ’œâ‚ ğ’œâ‚‚ : Finset (Finset Î±)}
  {s t u : Finset Î±} {a b : Î±} {r : â„•}

instance : IsStrictTotalOrder (Colex Î±) (Â· < Â·) where
  irrefl := irrefl_of (Â· < Â·)
  trans s t u := by
    rintro âŸ¨kâ‚, kâ‚z, notinA, inBâŸ© âŸ¨kâ‚‚, kâ‚‚z, notinB, inCâŸ©
    obtain h | h := (ne_of_mem_of_not_mem inB notinB).lt_or_lt
    Â· refine' âŸ¨kâ‚‚, fun x hx â†¦ _, by rwa [kâ‚z h], inCâŸ©
      rw [â†kâ‚‚z hx]
      exact kâ‚z (h.trans hx)
    Â· refine' âŸ¨kâ‚, fun x hx â†¦ _, notinA, by rwa [â†kâ‚‚z h]âŸ©
      rw [kâ‚z hx]
      exact kâ‚‚z (h.trans hx)
  trichotomous s t := by
    classical
    obtain rfl | hts := eq_or_ne t s
    Â· simp
    obtain âŸ¨k, hk, zâŸ© := exists_max_image _ id (symmDiff_nonempty.2 $ ofColex_ne_ofColex.2 hts)
    refine' (mem_symmDiff.1 hk).imp (fun hk => âŸ¨k, fun a ha â†¦ _, hk.2, hk.1âŸ©) fun hk â†¦
        Or.inr âŸ¨k, fun a ha â†¦ _, hk.2, hk.1âŸ© <;>
      simpa [mem_symmDiff, not_or, iff_iff_implies_and_implies, and_comm, not_imp_not]
        using not_imp_not.2 (z a) ha.not_le

instance instDecidableLT : @DecidableRel (Colex Î±) (Â· < Â·) := fun s t â†¦
  decidable_of_iff'
    (âˆƒ k âˆˆ ofColex t,
      (âˆ€ x âˆˆ ofColex s âˆª ofColex t, k < x â†’ (x âˆˆ ofColex s â†” x âˆˆ ofColex t)) âˆ§ k âˆ‰ ofColex s) $
    exists_congr fun a â†¦ by
      simp only [mem_union, exists_prop, or_imp, @and_comm (_ âˆˆ ofColex t), and_assoc]
      exact and_congr_left' $ forall_congr' $ by tauto

instance instLinearOrder : LinearOrder (Colex Î±) := linearOrderOfSTO (Â· < Â·)

instance instBot : Bot (Colex Î±) where
  bot := toColex âˆ…

@[simp] lemma toColex_empty : toColex (âˆ… : Finset Î±) = âŠ¥ := rfl
@[simp] lemma ofColex_bot : ofColex (âŠ¥ : Colex Î±) = âˆ… := rfl

instance instOrderBot : OrderBot (Colex Î±) where
  bot_le s := by
    induction' s using Finset.Colex.rec with s
    rw [le_def]
    obtain rfl | hs := s.eq_empty_or_nonempty
    Â· simp
    refine' Or.inr âŸ¨max' _ hs, _, by simp, max'_mem _ _âŸ©
    simp only [max'_lt_iff, ofColex_bot, not_mem_empty, ofColex_toColex, false_iff]
    exact fun x hs hx â†¦ lt_irrefl _ $ hs _ hx

/-- The colexigraphic order is insensitive to removing elements. -/
lemma toColex_sdiff_le_toColex_sdiff (hus : u âŠ† s) (hut : u âŠ† t) :
    toColex (s \ u) â‰¤ toColex (t \ u) â†” toColex s â‰¤ toColex t := by
  rw [le_iff_le_iff_lt_iff_lt, toColex_sdiff_lt_toColex_sdiff hut hus]

@[simp] lemma toColex_sdiff_le_toColex_sdiff' :
    toColex (s \ t) â‰¤ toColex (t \ s) â†” toColex s â‰¤ toColex t := by
  rw [le_iff_le_iff_lt_iff_lt, toColex_sdiff_lt_toColex_sdiff']

lemma colex_lt_of_ssubset (h : s âŠ‚ t) : toColex s < toColex t := by
  rw [â†toColex_sdiff_lt_toColex_sdiff', sdiff_eq_empty_iff_subset.2 h.1, toColex_empty,
    bot_lt_iff_ne_bot, â†toColex_empty, toColex_ne_toColex]
  simpa using h.not_subset

/-- If `s âŠ† t`, then `s â‰¤ t` in the colex order. Note the converse does not hold, as `âŠ†` is not a
linear order. -/
lemma colex_le_of_subset (h : s âŠ† t) : toColex s â‰¤ toColex t := by
  rw [â†toColex_sdiff_le_toColex_sdiff', sdiff_eq_empty_iff_subset.2 h, toColex_empty]; exact bot_le

instance [Fintype Î±] : BoundedOrder (Colex Î±) where
    top := toColex univ
    le_top _x := colex_le_of_subset (subset_univ _)

@[simp] lemma toColex_univ [Fintype Î±] : toColex (univ : Finset Î±) = âŠ¤ := rfl
@[simp] lemma ofColex_top [Fintype Î±] : ofColex (âŠ¤ : Colex Î±) = univ := rfl

/-- `s < {a}` in colex iff all elements of `s` are strictly less than `a`. -/
lemma toColex_lt_singleton : toColex s < toColex {a} â†” âˆ€ x âˆˆ s, x < a := by
  simp only [toColex_lt_toColex, mem_singleton, â†and_assoc, exists_eq_right, â†not_le (a := a)]
  refine âŸ¨fun t x hx hax â†¦ ?_, fun h â†¦ âŸ¨fun z hz â†¦ ?_, by simpa using h aâŸ©âŸ©
  Â· obtain hax | rfl := hax.lt_or_eq
    Â· exact hax.ne' $ (t.1 hax).1 hx
    Â· exact t.2 hx
  Â· exact âŸ¨fun i â†¦ ((h _ i) hz.le).elim, fun i â†¦ (hz.ne' i).elimâŸ©

/-- `{a} â‰¤ s` in colex iff `r` contains an element greated than or equal to `a`. -/
lemma singleton_le_toColex : (toColex {a} : Colex Î±) â‰¤ toColex s â†” âˆƒ x âˆˆ s, a â‰¤ x := by
  simp only [â†not_lt, toColex_lt_singleton, not_forall, exists_prop]

/-- Colex is an extension of the base order. -/
lemma singleton_lt_singleton : (toColex {a} : Colex Î±) < toColex {b} â†” a < b := by
  simp [toColex_lt_singleton]

/-- Colex is an extension of the base order. -/
lemma singleton_le_singleton : (toColex {a} : Colex Î±) â‰¤ toColex {b} â†” a â‰¤ b := by
  rw [le_iff_le_iff_lt_iff_lt, singleton_lt_singleton]

/-- If `s` is before `t` in colex, and everything in `t` is small, then everything in `s` is small.
-/
lemma forall_lt_mono (hâ‚ : toColex s â‰¤ toColex t) (hâ‚‚ : âˆ€ x âˆˆ t, x < a) : âˆ€ x âˆˆ s, x < a := by
  obtain rfl | âŸ¨k, z, -, hkâŸ© := toColex_le_toColex.1 hâ‚
  Â· assumption
  Â· refine' fun x hx => lt_of_not_le fun h â†¦ h.not_lt $ hâ‚‚ x _
    rwa [â†z ((hâ‚‚ k hk).trans_le h)]

/-- Strictly monotone functions preserve the colex ordering. -/
lemma toColex_image_lt_toColex_image (hf : StrictMono f) :
    toColex (s.image f) < toColex (t.image f) â†” toColex s < toColex t := by
  simp only [toColex_lt_toColex, not_exists, mem_image, exists_prop, not_and]
  constructor
  Â· rintro âŸ¨k, z, q, k', _, rflâŸ©
    exact âŸ¨k', fun x hx => by simpa [hf.injective.eq_iff] using z (hf hx),
      fun t â†¦ q _ t rfl, â€¹k' âˆˆ tâ€ºâŸ©
  rintro âŸ¨k, z, ka, _âŸ©
  refine' âŸ¨f k, fun x hx â†¦ _, _, k, â€¹k âˆˆ tâ€º, rflâŸ©
  Â· constructor
    all_goals
      rintro âŸ¨x', hx', rflâŸ©
      refine' âŸ¨x', _, rflâŸ©
      first
      | rwa [â†z _]
      | rwa [z _]
      rwa [StrictMono.lt_iff_lt hf] at hx
  Â· simp only [hf.injective, Function.Injective.eq_iff]
    exact fun x hx â†¦ ne_of_mem_of_not_mem hx ka

/-- Strictly monotone functions preserve the colex ordering. -/
lemma toColex_image_le_toColex_image (hf : StrictMono f) :
    toColex (s.image f) â‰¤ toColex (t.image f) â†” toColex s â‰¤ toColex t := by
  rw [le_iff_le_iff_lt_iff_lt, toColex_image_lt_toColex_image hf]

/-! ### Initial segments -/

/-- `ğ’œ` is an initial segment of the colexigraphic order on sets of `r`, and that if `t` is below
`s` in colex where `t` has size `r` and `s` is in `ğ’œ`, then `t` is also in `ğ’œ`. In effect, `ğ’œ` is
downwards closed with respect to colex among sets of size `r`. -/
def IsInitSeg (ğ’œ : Finset (Finset Î±)) (r : â„•) : Prop :=
  (ğ’œ : Set (Finset Î±)).Sized r âˆ§
    âˆ€ â¦ƒs t : Finset Î±â¦„, s âˆˆ ğ’œ â†’ toColex t < toColex s âˆ§ t.card = r â†’ t âˆˆ ğ’œ

@[simp] lemma isInitSeg_empty : IsInitSeg (âˆ… : Finset (Finset Î±)) r := by simp [IsInitSeg]

/-- Initial segments are nested in some way. In particular, if they're the same size they're equal.
-/
lemma IsInitSeg.total (hâ‚ : IsInitSeg ğ’œâ‚ r) (hâ‚‚ : IsInitSeg ğ’œâ‚‚ r) : ğ’œâ‚ âŠ† ğ’œâ‚‚ âˆ¨ ğ’œâ‚‚ âŠ† ğ’œâ‚ := by
  classical
  simp_rw [â†sdiff_eq_empty_iff_subset, â†not_nonempty_iff_eq_empty]
  by_contra' h
  obtain âŸ¨âŸ¨s, hsâŸ©, t, htâŸ© := h
  rw [mem_sdiff] at hs ht
  obtain hst | hst | hts := trichotomous_of (Î± := Colex Î±) (Â· < Â·) (toColex s) (toColex t)
  Â· exact hs.2 $ hâ‚‚.2 ht.1 âŸ¨hst, hâ‚.1 hs.1âŸ©
  Â· simp only [toColex.injEq] at hst
    exact ht.2 $ hst â–¸ hs.1
  Â· exact ht.2 $ hâ‚.2 hs.1 âŸ¨hts, hâ‚‚.1 ht.1âŸ©

variable [Fintype Î±]

/-- Gives all sets up to `s` with the same size as it: this is equivalent to
being an initial segment of colex. -/
def initSeg (s : Finset Î±) : Finset (Finset Î±) :=
  univ.filter fun t â†¦ s.card = t.card âˆ§ toColex t â‰¤ toColex s

@[simp]
lemma mem_initSeg : t âˆˆ initSeg s â†” s.card = t.card âˆ§ toColex t â‰¤ toColex s := by simp [initSeg]

lemma mem_initSeg_self : s âˆˆ initSeg s := by simp
@[simp] lemma initSeg_nonempty : (initSeg s).Nonempty := âŸ¨s, mem_initSeg_selfâŸ©

lemma isInitSeg_initSeg : IsInitSeg (initSeg s) s.card := by
  refine âŸ¨fun t ht => (mem_initSeg.1 ht).1.symm, fun tâ‚ tâ‚‚ htâ‚ htâ‚‚ â†¦ mem_initSeg.2 âŸ¨htâ‚‚.2.symm, ?_âŸ©âŸ©
  rw [mem_initSeg] at htâ‚
  exact htâ‚‚.1.le.trans htâ‚.2

lemma IsInitSeg.exists_initSeg (hğ’œ : IsInitSeg ğ’œ r) (hğ’œâ‚€ : ğ’œ.Nonempty) :
    âˆƒ s : Finset Î±, s.card = r âˆ§ ğ’œ = initSeg s := by
  have hs := sup'_mem (ofColex â»Â¹' ğ’œ) (LinearOrder.supClosed _) ğ’œ hğ’œâ‚€ toColex
    (fun a ha â†¦ by simpa using ha)
  refine' âŸ¨_, hğ’œ.1 hs, _âŸ©
  ext t
  rw [mem_initSeg]
  refine' âŸ¨fun p â†¦ _, _âŸ©
  Â· rw [hğ’œ.1 p, hğ’œ.1 hs]
    exact âŸ¨rfl, le_sup' _ pâŸ©
  rintro âŸ¨cards, leâŸ©
  obtain p | p := le.eq_or_lt
  Â· rwa [toColex_inj.1 p]
  Â· exact hğ’œ.2 hs âŸ¨p, cards â–¸ hğ’œ.1 hsâŸ©

/-- Being a nonempty initial segment of colex if equivalent to being an `initSeg`. -/
lemma isInitSeg_iff_exists_initSeg :
    IsInitSeg ğ’œ r âˆ§ ğ’œ.Nonempty â†” âˆƒ s : Finset Î±, s.card = r âˆ§ ğ’œ = initSeg s := by
  refine âŸ¨fun hğ’œ â†¦ hğ’œ.1.exists_initSeg hğ’œ.2, ?_âŸ©
  rintro âŸ¨s, rfl, rflâŸ©
  exact âŸ¨isInitSeg_initSeg, initSeg_nonemptyâŸ©

end LinearOrder
end Colex

open Colex

/-!
### Colex on `â„•`

The colexicographic order agrees with the order induced by interpreting a set of naturals as a
binary expansion.
-/

section Nat
variable {s t : Finset â„•}

/-- For finsets of naturals of naturals, the colexicographic order is equivalent to the order
induced by the binary expansion. -/
lemma sum_two_pow_lt_iff_colex_lt : âˆ‘ i in s, 2 ^ i < âˆ‘ i in t, 2 ^ i â†” toColex s < toColex t := by
  have z : âˆ€ s t : Finset â„•, toColex s < toColex t â†’ âˆ‘ i in s, 2 ^ i < âˆ‘ i in t, 2 ^ i := by
    intro s t
    rw [â†toColex_sdiff_lt_toColex_sdiff', toColex_lt_toColex]
    rintro âŸ¨a, ha, has, hatâŸ©
    rw [â†sdiff_union_inter s t]
    conv_rhs => rw [â†sdiff_union_inter t s]
    rw [sum_union (disjoint_sdiff_inter _ _), sum_union (disjoint_sdiff_inter _ _), inter_comm,
      add_lt_add_iff_right]
    apply (@Nat.sum_two_pow_lt a (s \ t) _).trans_le
    Â· apply single_le_sum (fun _ _ â†¦ Nat.zero_le _) hat
    intro x hx
    refine' (ne_of_mem_of_not_mem hx has).lt_or_lt.resolve_right $ fun hax â†¦ _
    have := (ha hax).1 hx
    rw [mem_sdiff] at this hx
    exact hx.2 this.1
  refine' âŸ¨fun h â†¦ (lt_trichotomy (toColex s) $ toColex t).resolve_right fun hâ‚ â†¦
    hâ‚.elim _ (not_lt_of_gt h âˆ˜ z _ _), z s tâŸ©
  rw [toColex_inj]
  rintro rfl
  exact irrefl _ h

/-- For finsets of naturals of naturals, the colexicographic order is equivalent to the order
induced by the binary expansion. -/
lemma sum_two_pow_le_iff_colex_le : âˆ‘ i in s, 2 ^ i â‰¤ âˆ‘ i in t, 2 ^ i â†” toColex s â‰¤ toColex t := by
  rw [le_iff_le_iff_lt_iff_lt, sum_two_pow_lt_iff_colex_lt]

end Nat
end Finset
