/-
Copyright (c) 2021 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Algebra.BigOperators.Order
import Mathlib.Combinatorics.Hall.Basic
import Mathlib.Data.Fintype.BigOperators
import Mathlib.SetTheory.Cardinal.Finite

#align_import combinatorics.configuration from "leanprover-community/mathlib"@"d2d8742b0c21426362a9dacebc6005db895ca963"

/-!
# Configurations of Points and lines
This file introduces abstract configurations of points and lines, and proves some basic properties.

## Main definitions
* `Configuration.Nondegenerate`: Excludes certain degenerate configurations,
  and imposes uniqueness of intersection points.
* `Configuration.HasPoints`: A nondegenerate configuration in which
  every pair of lines has an intersection point.
* `Configuration.HasLines`:  A nondegenerate configuration in which
  every pair of points has a line through them.
* `Configuration.lineCount`: The number of lines through a given point.
* `Configuration.pointCount`: The number of lines through a given line.

## Main statements
* `Configuration.HasLines.card_le`: `HasLines` implies `|P| ‚â§ |L|`.
* `Configuration.HasPoints.card_le`: `HasPoints` implies `|L| ‚â§ |P|`.
* `Configuration.HasLines.hasPoints`: `HasLines` and `|P| = |L|` implies `HasPoints`.
* `Configuration.HasPoints.hasLines`: `HasPoints` and `|P| = |L|` implies `HasLines`.
Together, these four statements say that any two of the following properties imply the third:
(a) `HasLines`, (b) `HasPoints`, (c) `|P| = |L|`.

-/


open BigOperators

namespace Configuration

variable (P L : Type*) [Membership P L]

/-- A type synonym. -/
def Dual :=
  P
#align configuration.dual Configuration.Dual

-- porting note: was `this` instead of `h`
instance [h : Inhabited P] : Inhabited (Dual P) :=
  h

instance [Finite P] : Finite (Dual P) :=
  ‚ÄπFinite P‚Ä∫

-- porting note: was `this` instead of `h`
instance [h : Fintype P] : Fintype (Dual P) :=
  h

-- porting note: TODO: figure out if this is needed.
set_option synthInstance.checkSynthOrder false in
instance : Membership (Dual L) (Dual P) :=
  ‚ü®Function.swap (Membership.mem : P ‚Üí L ‚Üí Prop)‚ü©

/-- A configuration is nondegenerate if:
  1) there does not exist a line that passes through all of the points,
  2) there does not exist a point that is on all of the lines,
  3) there is at most one line through any two points,
  4) any two lines have at most one intersection point.
  Conditions 3 and 4 are equivalent. -/
class Nondegenerate : Prop where
  exists_point : ‚àÄ l : L, ‚àÉ p, p ‚àâ l
  exists_line : ‚àÄ p, ‚àÉ l : L, p ‚àâ l
  eq_or_eq : ‚àÄ {p‚ÇÅ p‚ÇÇ : P} {l‚ÇÅ l‚ÇÇ : L}, p‚ÇÅ ‚àà l‚ÇÅ ‚Üí p‚ÇÇ ‚àà l‚ÇÅ ‚Üí p‚ÇÅ ‚àà l‚ÇÇ ‚Üí p‚ÇÇ ‚àà l‚ÇÇ ‚Üí p‚ÇÅ = p‚ÇÇ ‚à® l‚ÇÅ = l‚ÇÇ
#align configuration.nondegenerate Configuration.Nondegenerate

/-- A nondegenerate configuration in which every pair of lines has an intersection point. -/
class HasPoints extends Nondegenerate P L where
  mkPoint : ‚àÄ {l‚ÇÅ l‚ÇÇ : L} (_ : l‚ÇÅ ‚â† l‚ÇÇ), P
  mkPoint_ax : ‚àÄ {l‚ÇÅ l‚ÇÇ : L} (h : l‚ÇÅ ‚â† l‚ÇÇ), mkPoint h ‚àà l‚ÇÅ ‚àß mkPoint h ‚àà l‚ÇÇ
#align configuration.has_points Configuration.HasPoints

/-- A nondegenerate configuration in which every pair of points has a line through them. -/
class HasLines extends Nondegenerate P L where
  mkLine : ‚àÄ {p‚ÇÅ p‚ÇÇ : P} (_ : p‚ÇÅ ‚â† p‚ÇÇ), L
  mkLine_ax : ‚àÄ {p‚ÇÅ p‚ÇÇ : P} (h : p‚ÇÅ ‚â† p‚ÇÇ), p‚ÇÅ ‚àà mkLine h ‚àß p‚ÇÇ ‚àà mkLine h
#align configuration.has_lines Configuration.HasLines

open Nondegenerate

open HasPoints (mkPoint mkPoint_ax)

open HasLines (mkLine mkLine_ax)

instance Dual.Nondegenerate [Nondegenerate P L] : Nondegenerate (Dual L) (Dual P) where
  exists_point := @exists_line P L _ _
  exists_line := @exists_point P L _ _
  eq_or_eq := @fun l‚ÇÅ l‚ÇÇ p‚ÇÅ p‚ÇÇ h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ => (@eq_or_eq P L _ _ p‚ÇÅ p‚ÇÇ l‚ÇÅ l‚ÇÇ h‚ÇÅ h‚ÇÉ h‚ÇÇ h‚ÇÑ).symm

instance Dual.hasLines [HasPoints P L] : HasLines (Dual L) (Dual P) :=
  { Dual.Nondegenerate _ _ with
    mkLine := @mkPoint P L _ _
    mkLine_ax := @mkPoint_ax P L _ _ }

instance Dual.hasPoints [HasLines P L] : HasPoints (Dual L) (Dual P) :=
  { Dual.Nondegenerate _ _ with
    mkPoint := @mkLine P L _ _
    mkPoint_ax := @mkLine_ax P L _ _ }

theorem HasPoints.existsUnique_point [HasPoints P L] (l‚ÇÅ l‚ÇÇ : L) (hl : l‚ÇÅ ‚â† l‚ÇÇ) :
    ‚àÉ! p, p ‚àà l‚ÇÅ ‚àß p ‚àà l‚ÇÇ :=
  ‚ü®mkPoint hl, mkPoint_ax hl, fun _ hp =>
    (eq_or_eq hp.1 (mkPoint_ax hl).1 hp.2 (mkPoint_ax hl).2).resolve_right hl‚ü©
#align configuration.has_points.exists_unique_point Configuration.HasPoints.existsUnique_point

theorem HasLines.existsUnique_line [HasLines P L] (p‚ÇÅ p‚ÇÇ : P) (hp : p‚ÇÅ ‚â† p‚ÇÇ) :
    ‚àÉ! l : L, p‚ÇÅ ‚àà l ‚àß p‚ÇÇ ‚àà l :=
  HasPoints.existsUnique_point (Dual L) (Dual P) p‚ÇÅ p‚ÇÇ hp
#align configuration.has_lines.exists_unique_line Configuration.HasLines.existsUnique_line

variable {P L}

/-- If a nondegenerate configuration has at least as many points as lines, then there exists
  an injective function `f` from lines to points, such that `f l` does not lie on `l`. -/
theorem Nondegenerate.exists_injective_of_card_le [Nondegenerate P L] [Fintype P] [Fintype L]
    (h : Fintype.card L ‚â§ Fintype.card P) : ‚àÉ f : L ‚Üí P, Function.Injective f ‚àß ‚àÄ l, f l ‚àâ l := by
  classical
    let t : L ‚Üí Finset P := fun l => Set.toFinset { p | p ‚àâ l }
    suffices ‚àÄ s : Finset L, s.card ‚â§ (s.biUnion t).card by
      -- Hall's marriage theorem
      obtain ‚ü®f, hf1, hf2‚ü© := (Finset.all_card_le_biUnion_card_iff_exists_injective t).mp this
      exact ‚ü®f, hf1, fun l => Set.mem_toFinset.mp (hf2 l)‚ü©
    intro s
    by_cases hs‚ÇÄ : s.card = 0
    -- If `s = ‚àÖ`, then `s.card = 0 ‚â§ (s.bUnion t).card`
    ¬∑ simp_rw [hs‚ÇÄ, zero_le]
    by_cases hs‚ÇÅ : s.card = 1
    -- If `s = {l}`, then pick a point `p ‚àâ l`
    ¬∑ obtain ‚ü®l, rfl‚ü© := Finset.card_eq_one.mp hs‚ÇÅ
      obtain ‚ü®p, hl‚ü© := exists_point l
      rw [Finset.card_singleton, Finset.singleton_biUnion, Nat.one_le_iff_ne_zero]
      exact Finset.card_ne_zero_of_mem (Set.mem_toFinset.mpr hl)
    suffices (s.biUnion t)·∂ú.card ‚â§ s·∂ú.card by
      -- Rephrase in terms of complements (uses `h`)
      rw [Finset.card_compl, Finset.card_compl, tsub_le_iff_left] at this
      replace := h.trans this
      rwa [‚Üê add_tsub_assoc_of_le s.card_le_univ, le_tsub_iff_left (le_add_left s.card_le_univ),
        add_le_add_iff_right] at this
    have hs‚ÇÇ : (s.biUnion t)·∂ú.card ‚â§ 1 := by
      -- At most one line through two points of `s`
      refine' Finset.card_le_one_iff.mpr @fun p‚ÇÅ p‚ÇÇ hp‚ÇÅ hp‚ÇÇ => _
      simp_rw [Finset.mem_compl, Finset.mem_biUnion, not_exists, not_and,
        Set.mem_toFinset, Set.mem_setOf_eq, Classical.not_not] at hp‚ÇÅ hp‚ÇÇ
      obtain ‚ü®l‚ÇÅ, l‚ÇÇ, hl‚ÇÅ, hl‚ÇÇ, hl‚ÇÉ‚ü© :=
        Finset.one_lt_card_iff.mp (Nat.one_lt_iff_ne_zero_and_ne_one.mpr ‚ü®hs‚ÇÄ, hs‚ÇÅ‚ü©)
      exact (eq_or_eq (hp‚ÇÅ l‚ÇÅ hl‚ÇÅ) (hp‚ÇÇ l‚ÇÅ hl‚ÇÅ) (hp‚ÇÅ l‚ÇÇ hl‚ÇÇ) (hp‚ÇÇ l‚ÇÇ hl‚ÇÇ)).resolve_right hl‚ÇÉ
    by_cases hs‚ÇÉ : s·∂ú.card = 0
    ¬∑ rw [hs‚ÇÉ, le_zero_iff]
      rw [Finset.card_compl, tsub_eq_zero_iff_le, LE.le.le_iff_eq (Finset.card_le_univ _), eq_comm,
        Finset.card_eq_iff_eq_univ] at hs‚ÇÉ ‚ä¢
      rw [hs‚ÇÉ]
      rw [Finset.eq_univ_iff_forall] at hs‚ÇÉ ‚ä¢
      exact fun p =>
        Exists.elim (exists_line p)-- If `s = univ`, then show `s.bUnion t = univ`
        fun l hl => Finset.mem_biUnion.mpr ‚ü®l, Finset.mem_univ l, Set.mem_toFinset.mpr hl‚ü©
    ¬∑ exact hs‚ÇÇ.trans (Nat.one_le_iff_ne_zero.mpr hs‚ÇÉ)
#align configuration.nondegenerate.exists_injective_of_card_le Configuration.Nondegenerate.exists_injective_of_card_le

-- If `s < univ`, then consequence of `hs‚ÇÇ`
-- porting note: left out {P} to avoid redundant binder annotation
variable (L)

/-- Number of points on a given line. -/
noncomputable def lineCount (p : P) : ‚Ñï :=
  Nat.card { l : L // p ‚àà l }
#align configuration.line_count Configuration.lineCount

variable (P) {L}

/-- Number of lines through a given point. -/
noncomputable def pointCount (l : L) : ‚Ñï :=
  Nat.card { p : P // p ‚àà l }
#align configuration.point_count Configuration.pointCount

-- porting note: left out (P) to avoid redundant binder annotation
variable (L)

theorem sum_lineCount_eq_sum_pointCount [Fintype P] [Fintype L] :
    ‚àë p : P, lineCount L p = ‚àë l : L, pointCount P l := by
  classical
    simp only [lineCount, pointCount, Nat.card_eq_fintype_card, ‚Üê Fintype.card_sigma]
    apply Fintype.card_congr
    calc
      (Œ£p, { l : L // p ‚àà l }) ‚âÉ { x : P √ó L // x.1 ‚àà x.2 } :=
        (Equiv.subtypeProdEquivSigmaSubtype (¬∑ ‚àà ¬∑)).symm
      _ ‚âÉ { x : L √ó P // x.2 ‚àà x.1 } := ((Equiv.prodComm P L).subtypeEquiv fun x => Iff.rfl)
      _ ‚âÉ Œ£l, { p // p ‚àà l } := Equiv.subtypeProdEquivSigmaSubtype fun (l : L) (p : P) => p ‚àà l
#align configuration.sum_line_count_eq_sum_point_count Configuration.sum_lineCount_eq_sum_pointCount

variable {P L}

theorem HasLines.pointCount_le_lineCount [HasLines P L] {p : P} {l : L} (h : p ‚àâ l)
    [Finite { l : L // p ‚àà l }] : pointCount P l ‚â§ lineCount L p := by
  by_cases hf : Infinite { p : P // p ‚àà l }
  -- ‚ä¢ pointCount P l ‚â§ lineCount L p
  ¬∑ exact (le_of_eq Nat.card_eq_zero_of_infinite).trans (zero_le (lineCount L p))
    -- üéâ no goals
  haveI := fintypeOfNotInfinite hf
  -- ‚ä¢ pointCount P l ‚â§ lineCount L p
  cases nonempty_fintype { l : L // p ‚àà l }
  -- ‚ä¢ pointCount P l ‚â§ lineCount L p
  rw [lineCount, pointCount, Nat.card_eq_fintype_card, Nat.card_eq_fintype_card]
  -- ‚ä¢ Fintype.card { p // p ‚àà l } ‚â§ Fintype.card { l // p ‚àà l }
  have : ‚àÄ p' : { p // p ‚àà l }, p ‚â† p' := fun p' hp' => h ((congr_arg (¬∑ ‚àà l) hp').mpr p'.2)
  -- ‚ä¢ Fintype.card { p // p ‚àà l } ‚â§ Fintype.card { l // p ‚àà l }
  exact
    Fintype.card_le_of_injective (fun p' => ‚ü®mkLine (this p'), (mkLine_ax (this p')).1‚ü©)
      fun p‚ÇÅ p‚ÇÇ hp =>
      Subtype.ext
        ((eq_or_eq p‚ÇÅ.2 p‚ÇÇ.2 (mkLine_ax (this p‚ÇÅ)).2
              ((congr_arg _ (Subtype.ext_iff.mp hp)).mpr (mkLine_ax (this p‚ÇÇ)).2)).resolve_right
          fun h' => (congr_arg (¬¨p ‚àà ¬∑) h').mp h (mkLine_ax (this p‚ÇÅ)).1)
#align configuration.has_lines.point_count_le_line_count Configuration.HasLines.pointCount_le_lineCount

theorem HasPoints.lineCount_le_pointCount [HasPoints P L] {p : P} {l : L} (h : p ‚àâ l)
    [hf : Finite { p : P // p ‚àà l }] : lineCount L p ‚â§ pointCount P l :=
  @HasLines.pointCount_le_lineCount (Dual L) (Dual P) _ _ l p h hf
#align configuration.has_points.line_count_le_point_count Configuration.HasPoints.lineCount_le_pointCount

variable (P L)

/-- If a nondegenerate configuration has a unique line through any two points, then `|P| ‚â§ |L|`. -/
theorem HasLines.card_le [HasLines P L] [Fintype P] [Fintype L] :
    Fintype.card P ‚â§ Fintype.card L := by
  classical
  by_contra hc‚ÇÇ
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := Nondegenerate.exists_injective_of_card_le (le_of_not_le hc‚ÇÇ)
  have :=
    calc
      ‚àë p, lineCount L p = ‚àë l, pointCount P l := sum_lineCount_eq_sum_pointCount P L
      _ ‚â§ ‚àë l, lineCount L (f l) :=
        (Finset.sum_le_sum fun l _ => HasLines.pointCount_le_lineCount (hf‚ÇÇ l))
      _ = ‚àë p in Finset.univ.image f, lineCount L p :=
        (Finset.sum_bij (fun l _ => f l) (fun l hl => Finset.mem_image_of_mem f hl)
          (fun l _ => rfl) (fun l‚ÇÅ l‚ÇÇ hl‚ÇÅ hl‚ÇÇ hl‚ÇÉ => hf‚ÇÅ hl‚ÇÉ) fun p => by
          rw [Finset.mem_image]
          exact fun ‚ü®a, ‚ü®h, h'‚ü©‚ü© => ‚ü®a, ‚ü®h, h'.symm‚ü©‚ü©)
      _ < ‚àë p, lineCount L p := by
        obtain ‚ü®p, hp‚ü© := not_forall.mp (mt (Fintype.card_le_of_surjective f) hc‚ÇÇ)
        refine'
          Finset.sum_lt_sum_of_subset (Finset.univ.image f).subset_univ (Finset.mem_univ p) _ _
            fun p _ _ => zero_le (lineCount L p)
        ¬∑ simpa only [Finset.mem_image, exists_prop, Finset.mem_univ, true_and_iff]
        ¬∑ rw [lineCount, Nat.card_eq_fintype_card, Fintype.card_pos_iff]
          obtain ‚ü®l, _‚ü© := @exists_line P L _ _ p
          exact
            let this := not_exists.mp hp l
            ‚ü®‚ü®mkLine this, (mkLine_ax this).2‚ü©‚ü©
  exact lt_irrefl _ this
#align configuration.has_lines.card_le Configuration.HasLines.card_le

/-- If a nondegenerate configuration has a unique point on any two lines, then `|L| ‚â§ |P|`. -/
theorem HasPoints.card_le [HasPoints P L] [Fintype P] [Fintype L] :
    Fintype.card L ‚â§ Fintype.card P :=
  @HasLines.card_le (Dual L) (Dual P) _ _ _ _
#align configuration.has_points.card_le Configuration.HasPoints.card_le

variable {P L}

theorem HasLines.exists_bijective_of_card_eq [HasLines P L] [Fintype P] [Fintype L]
    (h : Fintype.card P = Fintype.card L) :
    ‚àÉ f : L ‚Üí P, Function.Bijective f ‚àß ‚àÄ l, pointCount P l = lineCount L (f l) := by
  classical
    obtain ‚ü®f, hf1, hf2‚ü© := Nondegenerate.exists_injective_of_card_le (ge_of_eq h)
    have hf3 := (Fintype.bijective_iff_injective_and_card f).mpr ‚ü®hf1, h.symm‚ü©
    refine'
      ‚ü®f, hf3, fun l =>
        (Finset.sum_eq_sum_iff_of_le fun l _ => HasLines.pointCount_le_lineCount (hf2 l)).mp
          ((sum_lineCount_eq_sum_pointCount P L).symm.trans
            (Finset.sum_bij (fun l _ => f l) (fun l _ => Finset.mem_univ (f l))
                (fun l _ => refl (lineCount L (f l))) (fun l‚ÇÅ l‚ÇÇ hl‚ÇÅ hl‚ÇÇ hl => hf1 hl) fun p hp =>
                _).symm)
          l (Finset.mem_univ l)‚ü©
    obtain ‚ü®l, rfl‚ü© := hf3.2 p
    exact ‚ü®l, Finset.mem_univ l, rfl‚ü©
#align configuration.has_lines.exists_bijective_of_card_eq Configuration.HasLines.exists_bijective_of_card_eq

theorem HasLines.lineCount_eq_pointCount [HasLines P L] [Fintype P] [Fintype L]
    (hPL : Fintype.card P = Fintype.card L) {p : P} {l : L} (hpl : p ‚àâ l) :
    lineCount L p = pointCount P l := by
  classical
    obtain ‚ü®f, hf1, hf2‚ü© := HasLines.exists_bijective_of_card_eq hPL
    let s : Finset (P √ó L) := Set.toFinset { i | i.1 ‚àà i.2 }
    have step1 : ‚àë i : P √ó L, lineCount L i.1 = ‚àë i : P √ó L, pointCount P i.2 := by
      rw [‚Üê Finset.univ_product_univ, Finset.sum_product_right, Finset.sum_product]
      simp_rw [Finset.sum_const, Finset.card_univ, hPL, sum_lineCount_eq_sum_pointCount]
    have step2 : ‚àë i in s, lineCount L i.1 = ‚àë i in s, pointCount P i.2 := by
      rw [s.sum_finset_product Finset.univ fun p => Set.toFinset { l | p ‚àà l }]
      rw [s.sum_finset_product_right Finset.univ fun l => Set.toFinset { p | p ‚àà l }]
      refine'
        (Finset.sum_bij (fun l _ => f l) (fun l _ => Finset.mem_univ (f l)) (fun l hl => _)
            (fun _ _ _ _ h => hf1.1 h) fun p _ => _).symm
      ¬∑ simp_rw [Finset.sum_const, Set.toFinset_card, ‚Üê Nat.card_eq_fintype_card]
        change pointCount P l ‚Ä¢ pointCount P l = lineCount L (f l) ‚Ä¢ lineCount L (f l)
        rw [hf2]
      ¬∑ obtain ‚ü®l, hl‚ü© := hf1.2 p
        exact ‚ü®l, Finset.mem_univ l, hl.symm‚ü©
      all_goals simp_rw [Finset.mem_univ, true_and_iff, Set.mem_toFinset]; exact fun p => Iff.rfl
    have step3 : ‚àë i in s·∂ú, lineCount L i.1 = ‚àë i in s·∂ú, pointCount P i.2 := by
      rwa [‚Üê s.sum_add_sum_compl, ‚Üê s.sum_add_sum_compl, step2, add_left_cancel_iff] at step1
    rw [‚Üê Set.toFinset_compl] at step3
    exact
      ((Finset.sum_eq_sum_iff_of_le fun i hi =>
              HasLines.pointCount_le_lineCount (by exact Set.mem_toFinset.mp hi)).mp
          step3.symm (p, l) (Set.mem_toFinset.mpr hpl)).symm
#align configuration.has_lines.line_count_eq_point_count Configuration.HasLines.lineCount_eq_pointCount

theorem HasPoints.lineCount_eq_pointCount [HasPoints P L] [Fintype P] [Fintype L]
    (hPL : Fintype.card P = Fintype.card L) {p : P} {l : L} (hpl : p ‚àâ l) :
    lineCount L p = pointCount P l :=
  (@HasLines.lineCount_eq_pointCount (Dual L) (Dual P) _ _ _ _ hPL.symm l p hpl).symm
#align configuration.has_points.line_count_eq_point_count Configuration.HasPoints.lineCount_eq_pointCount

/-- If a nondegenerate configuration has a unique line through any two points, and if `|P| = |L|`,
  then there is a unique point on any two lines. -/
noncomputable def HasLines.hasPoints [HasLines P L] [Fintype P] [Fintype L]
    (h : Fintype.card P = Fintype.card L) : HasPoints P L :=
  let this : ‚àÄ l‚ÇÅ l‚ÇÇ : L, l‚ÇÅ ‚â† l‚ÇÇ ‚Üí ‚àÉ p : P, p ‚àà l‚ÇÅ ‚àß p ‚àà l‚ÇÇ := fun l‚ÇÅ l‚ÇÇ hl => by
    classical
      obtain ‚ü®f, _, hf2‚ü© := HasLines.exists_bijective_of_card_eq h
      haveI : Nontrivial L := ‚ü®‚ü®l‚ÇÅ, l‚ÇÇ, hl‚ü©‚ü©
      haveI := Fintype.one_lt_card_iff_nontrivial.mp ((congr_arg _ h).mpr Fintype.one_lt_card)
      have h‚ÇÅ : ‚àÄ p : P, 0 < lineCount L p := fun p =>
        Exists.elim (exists_ne p) fun q hq =>
          (congr_arg _ Nat.card_eq_fintype_card).mpr
            (Fintype.card_pos_iff.mpr ‚ü®‚ü®mkLine hq, (mkLine_ax hq).2‚ü©‚ü©)
      have h‚ÇÇ : ‚àÄ l : L, 0 < pointCount P l := fun l => (congr_arg _ (hf2 l)).mpr (h‚ÇÅ (f l))
      obtain ‚ü®p, hl‚ÇÅ‚ü© := Fintype.card_pos_iff.mp ((congr_arg _ Nat.card_eq_fintype_card).mp (h‚ÇÇ l‚ÇÅ))
      by_cases hl‚ÇÇ : p ‚àà l‚ÇÇ
      exact ‚ü®p, hl‚ÇÅ, hl‚ÇÇ‚ü©
      have key' : Fintype.card { q : P // q ‚àà l‚ÇÇ } = Fintype.card { l : L // p ‚àà l } :=
        ((HasLines.lineCount_eq_pointCount h hl‚ÇÇ).trans Nat.card_eq_fintype_card).symm.trans
          Nat.card_eq_fintype_card
      have : ‚àÄ q : { q // q ‚àà l‚ÇÇ }, p ‚â† q := fun q hq => hl‚ÇÇ ((congr_arg (¬∑ ‚àà l‚ÇÇ) hq).mpr q.2)
      let f : { q : P // q ‚àà l‚ÇÇ } ‚Üí { l : L // p ‚àà l } := fun q =>
        ‚ü®mkLine (this q), (mkLine_ax (this q)).1‚ü©
      have hf : Function.Injective f := fun q‚ÇÅ q‚ÇÇ hq =>
        Subtype.ext
          ((eq_or_eq q‚ÇÅ.2 q‚ÇÇ.2 (mkLine_ax (this q‚ÇÅ)).2
                ((congr_arg _ (Subtype.ext_iff.mp hq)).mpr (mkLine_ax (this q‚ÇÇ)).2)).resolve_right
            fun h => (congr_arg (¬¨p ‚àà ¬∑) h).mp hl‚ÇÇ (mkLine_ax (this q‚ÇÅ)).1)
      have key' := ((Fintype.bijective_iff_injective_and_card f).mpr ‚ü®hf, key'‚ü©).2
      obtain ‚ü®q, hq‚ü© := key' ‚ü®l‚ÇÅ, hl‚ÇÅ‚ü©
      exact ‚ü®q, (congr_arg _ (Subtype.ext_iff.mp hq)).mp (mkLine_ax (this q)).2, q.2‚ü©
  { ‚ÄπHasLines P L‚Ä∫ with
    mkPoint := fun {l‚ÇÅ l‚ÇÇ} hl => Classical.choose (this l‚ÇÅ l‚ÇÇ hl)
    mkPoint_ax := fun {l‚ÇÅ l‚ÇÇ} hl => Classical.choose_spec (this l‚ÇÅ l‚ÇÇ hl) }
#align configuration.has_lines.has_points Configuration.HasLines.hasPoints

/-- If a nondegenerate configuration has a unique point on any two lines, and if `|P| = |L|`,
  then there is a unique line through any two points. -/
noncomputable def HasPoints.hasLines [HasPoints P L] [Fintype P] [Fintype L]
    (h : Fintype.card P = Fintype.card L) : HasLines P L :=
  let this := @HasLines.hasPoints (Dual L) (Dual P) _ _ _ _ h.symm
  { ‚ÄπHasPoints P L‚Ä∫ with
    mkLine := @fun _ _ => this.mkPoint
    mkLine_ax := @fun _ _ => this.mkPoint_ax }
#align configuration.has_points.has_lines Configuration.HasPoints.hasLines

variable (P L)

/-- A projective plane is a nondegenerate configuration in which every pair of lines has
  an intersection point, every pair of points has a line through them,
  and which has three points in general position. -/
class ProjectivePlane extends HasPoints P L, HasLines P L where
  exists_config :
    ‚àÉ (p‚ÇÅ p‚ÇÇ p‚ÇÉ : P) (l‚ÇÅ l‚ÇÇ l‚ÇÉ : L),
      p‚ÇÅ ‚àâ l‚ÇÇ ‚àß p‚ÇÅ ‚àâ l‚ÇÉ ‚àß p‚ÇÇ ‚àâ l‚ÇÅ ‚àß p‚ÇÇ ‚àà l‚ÇÇ ‚àß p‚ÇÇ ‚àà l‚ÇÉ ‚àß p‚ÇÉ ‚àâ l‚ÇÅ ‚àß p‚ÇÉ ‚àà l‚ÇÇ ‚àß p‚ÇÉ ‚àâ l‚ÇÉ
#align configuration.projective_plane Configuration.ProjectivePlane

namespace ProjectivePlane

variable [ProjectivePlane P L]

instance : ProjectivePlane (Dual L) (Dual P) :=
  { Dual.hasPoints _ _, Dual.hasLines _ _ with
    exists_config :=
      let ‚ü®p‚ÇÅ, p‚ÇÇ, p‚ÇÉ, l‚ÇÅ, l‚ÇÇ, l‚ÇÉ, h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÉ, h‚ÇÇ‚ÇÅ, h‚ÇÇ‚ÇÇ, h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÅ, h‚ÇÉ‚ÇÇ, h‚ÇÉ‚ÇÉ‚ü© := @exists_config P L _ _
      ‚ü®l‚ÇÅ, l‚ÇÇ, l‚ÇÉ, p‚ÇÅ, p‚ÇÇ, p‚ÇÉ, h‚ÇÇ‚ÇÅ, h‚ÇÉ‚ÇÅ, h‚ÇÅ‚ÇÇ, h‚ÇÇ‚ÇÇ, h‚ÇÉ‚ÇÇ, h‚ÇÅ‚ÇÉ, h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÉ‚ü© }

/-- The order of a projective plane is one less than the number of lines through an arbitrary point.
Equivalently, it is one less than the number of points on an arbitrary line. -/
noncomputable def order : ‚Ñï :=
  lineCount L (Classical.choose (@exists_config P L _ _)) - 1
#align configuration.projective_plane.order Configuration.ProjectivePlane.order

theorem card_points_eq_card_lines [Fintype P] [Fintype L] : Fintype.card P = Fintype.card L :=
  le_antisymm (HasLines.card_le P L) (HasPoints.card_le P L)
#align configuration.projective_plane.card_points_eq_card_lines Configuration.ProjectivePlane.card_points_eq_card_lines

-- porting note: left out (L) to avoid redundant binder annotation
variable {P}

theorem lineCount_eq_lineCount [Finite P] [Finite L] (p q : P) : lineCount L p = lineCount L q := by
  cases nonempty_fintype P
  -- ‚ä¢ lineCount L p = lineCount L q
  cases nonempty_fintype L
  -- ‚ä¢ lineCount L p = lineCount L q
  obtain ‚ü®p‚ÇÅ, p‚ÇÇ, p‚ÇÉ, l‚ÇÅ, l‚ÇÇ, l‚ÇÉ, h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÉ, h‚ÇÇ‚ÇÅ, h‚ÇÇ‚ÇÇ, h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÅ, h‚ÇÉ‚ÇÇ, h‚ÇÉ‚ÇÉ‚ü© := @exists_config P L _ _
  -- ‚ä¢ lineCount L p = lineCount L q
  have h := card_points_eq_card_lines P L
  -- ‚ä¢ lineCount L p = lineCount L q
  let n := lineCount L p‚ÇÇ
  -- ‚ä¢ lineCount L p = lineCount L q
  have hp‚ÇÇ : lineCount L p‚ÇÇ = n := rfl
  -- ‚ä¢ lineCount L p = lineCount L q
  have hl‚ÇÅ : pointCount P l‚ÇÅ = n := (HasLines.lineCount_eq_pointCount h h‚ÇÇ‚ÇÅ).symm.trans hp‚ÇÇ
  -- ‚ä¢ lineCount L p = lineCount L q
  have hp‚ÇÉ : lineCount L p‚ÇÉ = n := (HasLines.lineCount_eq_pointCount h h‚ÇÉ‚ÇÅ).trans hl‚ÇÅ
  -- ‚ä¢ lineCount L p = lineCount L q
  have hl‚ÇÉ : pointCount P l‚ÇÉ = n := (HasLines.lineCount_eq_pointCount h h‚ÇÉ‚ÇÉ).symm.trans hp‚ÇÉ
  -- ‚ä¢ lineCount L p = lineCount L q
  have hp‚ÇÅ : lineCount L p‚ÇÅ = n := (HasLines.lineCount_eq_pointCount h h‚ÇÅ‚ÇÉ).trans hl‚ÇÉ
  -- ‚ä¢ lineCount L p = lineCount L q
  have hl‚ÇÇ : pointCount P l‚ÇÇ = n := (HasLines.lineCount_eq_pointCount h h‚ÇÅ‚ÇÇ).symm.trans hp‚ÇÅ
  -- ‚ä¢ lineCount L p = lineCount L q
  suffices ‚àÄ p : P, lineCount L p = n by exact (this p).trans (this q).symm
  -- ‚ä¢ ‚àÄ (p : P), lineCount L p = n
  refine' fun p =>
    or_not.elim (fun h‚ÇÇ => _) fun h‚ÇÇ => (HasLines.lineCount_eq_pointCount h h‚ÇÇ).trans hl‚ÇÇ
  refine' or_not.elim (fun h‚ÇÉ => _) fun h‚ÇÉ => (HasLines.lineCount_eq_pointCount h h‚ÇÉ).trans hl‚ÇÉ
  -- ‚ä¢ lineCount L p = n
  rw [(eq_or_eq h‚ÇÇ h‚ÇÇ‚ÇÇ h‚ÇÉ h‚ÇÇ‚ÇÉ).resolve_right fun h =>
      h‚ÇÉ‚ÇÉ ((congr_arg (Membership.mem p‚ÇÉ) h).mp h‚ÇÉ‚ÇÇ)]
#align configuration.projective_plane.line_count_eq_line_count Configuration.ProjectivePlane.lineCount_eq_lineCount

variable (P) {L}

theorem pointCount_eq_pointCount [Finite P] [Finite L] (l m : L) :
    pointCount P l = pointCount P m := by
  apply lineCount_eq_lineCount (Dual P)
  -- üéâ no goals
#align configuration.projective_plane.point_count_eq_point_count Configuration.ProjectivePlane.pointCount_eq_pointCount

-- porting note: left out {L} to avoid redundant binder annotation
variable {P}

theorem lineCount_eq_pointCount [Finite P] [Finite L] (p : P) (l : L) :
    lineCount L p = pointCount P l :=
  Exists.elim (exists_point l) fun q hq =>
    (lineCount_eq_lineCount L p q).trans <| by
      cases nonempty_fintype P
      -- ‚ä¢ lineCount L q = pointCount P l
      cases nonempty_fintype L
      -- ‚ä¢ lineCount L q = pointCount P l
      exact HasLines.lineCount_eq_pointCount (card_points_eq_card_lines P L) hq
      -- üéâ no goals
#align configuration.projective_plane.line_count_eq_point_count Configuration.ProjectivePlane.lineCount_eq_pointCount

variable (P L)

theorem Dual.order [Finite P] [Finite L] : order (Dual L) (Dual P) = order P L :=
  congr_arg (fun n => n - 1) (lineCount_eq_pointCount _ _)
#align configuration.projective_plane.dual.order Configuration.ProjectivePlane.Dual.order

-- porting note: left out (L) to avoid redundant binder annotation
variable {P}

theorem lineCount_eq [Finite P] [Finite L] (p : P) : lineCount L p = order P L + 1 := by
  classical
    obtain ‚ü®q, -, -, l, -, -, -, -, h, -‚ü© := Classical.choose_spec (@exists_config P L _ _)
    cases nonempty_fintype { l : L // q ‚àà l }
    rw [order, lineCount_eq_lineCount L p q, lineCount_eq_lineCount L (Classical.choose _) q,
      lineCount, Nat.card_eq_fintype_card, Nat.sub_add_cancel]
    exact Fintype.card_pos_iff.mpr ‚ü®‚ü®l, h‚ü©‚ü©
#align configuration.projective_plane.line_count_eq Configuration.ProjectivePlane.lineCount_eq

variable (P) {L}

theorem pointCount_eq [Finite P] [Finite L] (l : L) : pointCount P l = order P L + 1 :=
  (lineCount_eq (Dual P) _).trans (congr_arg (fun n => n + 1) (Dual.order P L))
#align configuration.projective_plane.point_count_eq Configuration.ProjectivePlane.pointCount_eq

variable (L)

theorem one_lt_order [Finite P] [Finite L] : 1 < order P L := by
  obtain ‚ü®p‚ÇÅ, p‚ÇÇ, p‚ÇÉ, l‚ÇÅ, l‚ÇÇ, l‚ÇÉ, -, -, h‚ÇÇ‚ÇÅ, h‚ÇÇ‚ÇÇ, h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÅ, h‚ÇÉ‚ÇÇ, h‚ÇÉ‚ÇÉ‚ü© := @exists_config P L _ _
  -- ‚ä¢ 1 < order P L
  cases nonempty_fintype { p : P // p ‚àà l‚ÇÇ }
  -- ‚ä¢ 1 < order P L
  rw [‚Üê add_lt_add_iff_right 1, ‚Üê pointCount_eq _ l‚ÇÇ, pointCount, Nat.card_eq_fintype_card,
    Fintype.two_lt_card_iff]
  simp_rw [Ne, Subtype.ext_iff]
  -- ‚ä¢ ‚àÉ a b c, ¬¨‚Üëa = ‚Üëb ‚àß ¬¨‚Üëa = ‚Üëc ‚àß ¬¨‚Üëb = ‚Üëc
  have h := mkPoint_ax fun h => h‚ÇÇ‚ÇÅ ((congr_arg _ h).mpr h‚ÇÇ‚ÇÇ)
  -- ‚ä¢ ‚àÉ a b c, ¬¨‚Üëa = ‚Üëb ‚àß ¬¨‚Üëa = ‚Üëc ‚àß ¬¨‚Üëb = ‚Üëc
  exact
    ‚ü®‚ü®mkPoint _, h.2‚ü©, ‚ü®p‚ÇÇ, h‚ÇÇ‚ÇÇ‚ü©, ‚ü®p‚ÇÉ, h‚ÇÉ‚ÇÇ‚ü©, ne_of_mem_of_not_mem h.1 h‚ÇÇ‚ÇÅ,
      ne_of_mem_of_not_mem h.1 h‚ÇÉ‚ÇÅ, ne_of_mem_of_not_mem h‚ÇÇ‚ÇÉ h‚ÇÉ‚ÇÉ‚ü©
#align configuration.projective_plane.one_lt_order Configuration.ProjectivePlane.one_lt_order

variable {P}

theorem two_lt_lineCount [Finite P] [Finite L] (p : P) : 2 < lineCount L p := by
  simpa only [lineCount_eq L p, Nat.succ_lt_succ_iff] using one_lt_order P L
  -- üéâ no goals
#align configuration.projective_plane.two_lt_line_count Configuration.ProjectivePlane.two_lt_lineCount

variable (P) {L}

theorem two_lt_pointCount [Finite P] [Finite L] (l : L) : 2 < pointCount P l := by
  simpa only [pointCount_eq P l, Nat.succ_lt_succ_iff] using one_lt_order P L
  -- üéâ no goals
#align configuration.projective_plane.two_lt_point_count Configuration.ProjectivePlane.two_lt_pointCount

-- porting note: left out (P) to avoid redundant binder annotation
variable (L)

theorem card_points [Fintype P] [Finite L] : Fintype.card P = order P L ^ 2 + order P L + 1 := by
  cases nonempty_fintype L
  -- ‚ä¢ Fintype.card P = order P L ^ 2 + order P L + 1
  obtain ‚ü®p, -‚ü© := @exists_config P L _ _
  -- ‚ä¢ Fintype.card P = order P L ^ 2 + order P L + 1
  let œï : { q // q ‚â† p } ‚âÉ Œ£l : { l : L // p ‚àà l }, { q // q ‚àà l.1 ‚àß q ‚â† p } :=
    { toFun := fun q => ‚ü®‚ü®mkLine q.2, (mkLine_ax q.2).2‚ü©, q, (mkLine_ax q.2).1, q.2‚ü©
      invFun := fun lq => ‚ü®lq.2, lq.2.2.2‚ü©
      left_inv := fun q => Subtype.ext rfl
      right_inv := fun lq =>
        Sigma.subtype_ext
          (Subtype.ext
            ((eq_or_eq (mkLine_ax lq.2.2.2).1 (mkLine_ax lq.2.2.2).2 lq.2.2.1 lq.1.2).resolve_left
              lq.2.2.2))
          rfl }
  classical
    have h1 : Fintype.card { q // q ‚â† p } + 1 = Fintype.card P := by
      apply (eq_tsub_iff_add_eq_of_le (Nat.succ_le_of_lt (Fintype.card_pos_iff.mpr ‚ü®p‚ü©))).mp
      convert(Fintype.card_subtype_compl _).trans (congr_arg _ (Fintype.card_subtype_eq p))
    have h2 : ‚àÄ l : { l : L // p ‚àà l }, Fintype.card { q // q ‚àà l.1 ‚àß q ‚â† p } = order P L := by
      intro l
      rw [‚Üê Fintype.card_congr (Equiv.subtypeSubtypeEquivSubtypeInter (¬∑ ‚àà l.val) (¬∑ ‚â† p)),
        Fintype.card_subtype_compl fun x : Subtype (¬∑ ‚àà l.val) => x.val = p, ‚Üê
        Nat.card_eq_fintype_card]
      refine' tsub_eq_of_eq_add ((pointCount_eq P l.1).trans _)
      rw [‚Üê Fintype.card_subtype_eq (‚ü®p, l.2‚ü© : { q : P // q ‚àà l.1 })]
      simp_rw [Subtype.ext_iff_val]
    simp_rw [‚Üê h1, Fintype.card_congr œï, Fintype.card_sigma, h2, Finset.sum_const, Finset.card_univ]
    rw [‚Üê Nat.card_eq_fintype_card, ‚Üê lineCount, lineCount_eq, smul_eq_mul, Nat.succ_mul, sq]
#align configuration.projective_plane.card_points Configuration.ProjectivePlane.card_points

theorem card_lines [Finite P] [Fintype L] : Fintype.card L = order P L ^ 2 + order P L + 1 :=
  (card_points (Dual L) (Dual P)).trans (congr_arg (fun n => n ^ 2 + n + 1) (Dual.order P L))
#align configuration.projective_plane.card_lines Configuration.ProjectivePlane.card_lines

end ProjectivePlane

end Configuration
