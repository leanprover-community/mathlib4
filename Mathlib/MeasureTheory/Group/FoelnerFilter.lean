/-
Copyright (c) 2025 Stefano Rocca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Stefano Rocca
-/
module

public import Mathlib.MeasureTheory.Group.Defs
public import Mathlib.MeasureTheory.Group.Action
public import Mathlib.MeasureTheory.Measure.Typeclasses.Finite

/-!
# F√∏lner filters - definitions and properties

This file defines F√∏lner filters for measurable spaces acted on by a group.

## Definitions

* `IsFoelner G Œº l F` : A F√∏lner sequence with respect to some group `G` acting on
  a measure space `X` is a sequence of sets `F` such that:
    1. Each `s` in `l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Œº ((g ‚Ä¢ F i) ‚àÜ F i) / Œº (F i)` tends to `0`.

* `IsFoelner.mean Œº u F s` : The limit along an ultrafilter `u` of the density of a set `s`
  with respect to a F√∏lner sequence `F` in the measure space `X`.

* `maxFoelner G Œº` : The maximal F√∏lner filter with respect to some group `G` acting on a
  measure space `X` is the pullback of `ùìù 0` along the map `s ‚Ü¶ Œº (g ‚Ä¢ s) / Œº s` over measurable
  sets of finite non-zero measure.

## Main results

* `IsFoelner.amenable` : If there exists a non-trivial F√∏lner filter with respect to some
  group `G` acting on a measure space `X`, then it exists a `G`-invariant finitely additive
  probability measure on `X`.

* `isFoelner_iff_tendsto` : A sequence of sets is F√∏lner if and only if it tends to the
  maximal F√∏lner filter.
  The attribute "maximal" of the latter comes from the direct implication of this theorem :
  if `IsFoelner G Œº l F` then the push-forward filter `(l.map F) ‚â§ maxFoelner G Œº`.

* `amenable_of_maxFoelner_ne_bot` : If the maximal F√∏lner filter is non-trivial,
  then there exists a `G`-invariant finitely additive probability measure on `X`.

## Temporary design adaptations

* In the current version, we refer to the amenability of the action of a group on a measure space
  (e.g. in `IsFoelner.amenable` and `amenable_of_maxFoelner_ne_bot`), even though a definition of
  amenability has not yet been given in Mathlib.
  This is because there are different notions of amenability for groups and for group actions,
  and a Mathlib definition should be provided at the greatest level of generality, on which there
  has not yet been a general consensus.
  At the present moment, `amenable` corresponds to the existence of a `G`-invariant finitely
  additive probability measure.

## Tags

Foelner, F√∏lner filter, amenability, amenable group
-/

@[expose] public section

open MeasureTheory Filter Set Tendsto Ultrafilter
open scoped ENNReal Pointwise symmDiff Topology Filter

variable {G X : Type*} [MeasurableSpace X] {Œº : Measure X} [Group G] [MulAction G X]
variable {Œπ : Type*} {l : Filter Œπ} {u : Ultrafilter Œπ} {F : Œπ ‚Üí Set X}

theorem MeasureTheory.le_measure_symmDiff {s‚ÇÅ s‚ÇÇ : Set X} :
    Œº s‚ÇÅ - Œº s‚ÇÇ ‚â§ Œº (s‚ÇÅ ‚àÜ s‚ÇÇ) :=
  le_trans (le_measure_diff) (measure_mono <| by simp [symmDiff_def])

variable (Œº u F) in
/-- The limit along an ultrafilter of the density of a set with respect to a sequence in `X`. -/
noncomputable def Ultrafilter.mean (s : Set X) :=
  limUnder u (fun i ‚Ü¶ Œº (s ‚à© F i) / Œº (F i))

namespace Filter

variable (G Œº l F) in
/-- A F√∏lner sequence with respect to some group `G` acting on a measure space `X`
    is a sequence of sets `F` such that:
    1. Each `s` in `l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Œº ((g ‚Ä¢ F i) ‚àÜ F i) / Œº (F i)` tends to `0`. -/
@[mk_iff]
structure IsFoelner : Prop where
  eventually_measurableSet : ‚àÄ·∂† i in l, MeasurableSet (F i)
  eventually_meas_ne_zero : ‚àÄ·∂† i in l, Œº (F i) ‚â† 0
  eventually_meas_ne_top : ‚àÄ·∂† i in l, Œº (F i) ‚â† ‚àû
  tendsto_meas_symmDiff (g : G) : Tendsto (fun i ‚Ü¶ Œº ((g ‚Ä¢ F i) ‚àÜ F i) / Œº (F i)) l (ùìù 0)

/-- The constant sequence `X` is F√∏lner if `X` has finite measure. -/
theorem IsFoelner.univ_of_isFiniteMeasure [NeZero Œº] [IsFiniteMeasure Œº] :
    IsFoelner G Œº l (fun _ ‚Ü¶ .univ) where
  eventually_measurableSet := by simp
  eventually_meas_ne_zero := by simp [NeZero.ne]
  eventually_meas_ne_top := by simp
  tendsto_meas_symmDiff := by simp [tendsto_const_nhds]

theorem IsFoelner.mono {l' : Filter Œπ} (hfoel : IsFoelner G Œº l F) (hle : l' ‚â§ l) :
    IsFoelner G Œº l' F where
  eventually_measurableSet := hfoel.eventually_measurableSet.filter_mono hle
  eventually_meas_ne_zero := hfoel.eventually_meas_ne_zero.filter_mono hle
  eventually_meas_ne_top := hfoel.eventually_meas_ne_top.filter_mono hle
  tendsto_meas_symmDiff (g : G) := Tendsto.mono_left (hfoel.tendsto_meas_symmDiff g) hle

theorem IsFoelner.tendsto_nhds_mean (hfoel : IsFoelner G Œº u F) (s : Set X) :
    Tendsto (fun i ‚Ü¶ Œº (s ‚à© F i) / Œº (F i)) u (ùìù (mean Œº u F s)) := by
  have mem_Icc : ‚àÄ·∂† i in u, Œº (s ‚à© F i) / Œº (F i) ‚àà Icc 0 1 := by
    filter_upwards [hfoel.eventually_meas_ne_zero, hfoel.eventually_meas_ne_top] with i hi hi'
    simpa [ENNReal.div_le_iff hi hi'] using Œº.mono inter_subset_right
  obtain ‚ü®x, hx‚ü© := isCompact_Icc.ultrafilter_le_nhds'
    (u.map (fun i ‚Ü¶ Œº (s ‚à© F i) / Œº (F i))) (mem_map.1 mem_Icc)
  exact tendsto_nhds_limUnder (by use x; exact hx.2)

theorem IsFoelner.mean_univ_eq_one (hfoel : IsFoelner G Œº u F) :
    mean Œº u F .univ = 1 := by
  refine tendsto_nhds_unique_of_eventuallyEq (hfoel.tendsto_nhds_mean _) tendsto_const_nhds ?_
  filter_upwards [hfoel.eventually_meas_ne_zero, hfoel.eventually_meas_ne_top] with i hi hi'
  simp [ENNReal.div_self hi hi']

theorem IsFoelner.mean_union_eq_add_of_disjoint (hfoel : IsFoelner G Œº u F)
    (s t : Set X) (ht : MeasurableSet t) (hdisj : Disjoint s t) :
    mean Œº u F (s ‚à™ t) = mean Œº u F s + mean Œº u F t := by
  refine tendsto_nhds_unique_of_eventuallyEq
    (hfoel.tendsto_nhds_mean _) ((hfoel.tendsto_nhds_mean _).add (hfoel.tendsto_nhds_mean _)) ?_
  filter_upwards [hfoel.eventually_measurableSet] with i hi
  rw [union_inter_distrib_right,
    measure_union (hdisj.inter_left _ |>.inter_right _) (ht.inter hi), ENNReal.add_div]

theorem IsFoelner.mean_smul_le_mean_smul [SMulInvariantMeasure G X Œº]
    (hfoel : IsFoelner G Œº u F) (g h : G) (s : Set X) :
    mean Œº u F (g ‚Ä¢ s) ‚â§ mean Œº u F (h ‚Ä¢ s) := by
  rw [‚Üê add_zero <| mean Œº u F (h ‚Ä¢ s)]
  have tendsto‚ÇÄ : Tendsto (fun i ‚Ü¶ Œº ((g‚Åª¬π ‚Ä¢ F i) ‚àÜ (h‚Åª¬π ‚Ä¢ F i)) / Œº (F i)) u (ùìù 0) := by
    simpa [‚Üê smul_smul] using hfoel.tendsto_meas_symmDiff (h * g‚Åª¬π)
  refine le_of_tendsto_of_tendsto
    (hfoel.tendsto_nhds_mean _) ((hfoel.tendsto_nhds_mean _).add tendsto‚ÇÄ) ?_
  filter_upwards [hfoel.eventually_meas_ne_zero] with i hi
  rw [‚Üê tsub_le_iff_left, ‚Üê ENNReal.sub_div (fun _ _ ‚Ü¶ hi)]
  gcongr
  refine le_trans ?_ (measure_mono (@inter_subset_right _ s _))
  simpa [inter_symmDiff_distrib_left, ‚Üê measure_inter_inv_smul] using le_measure_symmDiff

theorem IsFoelner.mean_smul_eq_mean_smul [SMulInvariantMeasure G X Œº]
    (hfoel : IsFoelner G Œº u F) (g h : G) (s : Set X) :
    mean Œº u F (g ‚Ä¢ s) = mean Œº u F (h ‚Ä¢ s) :=
  le_antisymm (hfoel.mean_smul_le_mean_smul g h s) (hfoel.mean_smul_le_mean_smul h g s)

theorem IsFoelner.mean_smul_eq_mean [SMulInvariantMeasure G X Œº]
    (hfoel : IsFoelner G Œº u F) (g : G) (s : Set X) :
    mean Œº u F (g ‚Ä¢ s) = mean Œº u F s := by
  simpa using hfoel.mean_smul_eq_mean_smul g 1 s

/-- If there exists a non-trivial F√∏lner filter with respect to some group `G` acting on a measure
    space `X`, then it exists a `G`-invariant finitely additive probability measure on `X`. -/
theorem IsFoelner.amenable [SMulInvariantMeasure G X Œº] [NeBot l] (hfoel : IsFoelner G Œº l F) :
    ‚àÉ m : Set X ‚Üí ‚Ñù‚â•0‚àû, m .univ = 1 ‚àß
      (‚àÄ s t, MeasurableSet t ‚Üí Disjoint s t ‚Üí m (s ‚à™ t) = m s + m t) ‚àß
        ‚àÄ (g : G) (s : Set X), m (g ‚Ä¢ s) = m s := by
  use mean Œº (Ultrafilter.of l) F
  refine ‚ü®?_, ?_, ?_‚ü©
  ¬∑ exact (hfoel.mono <| Ultrafilter.of_le l).mean_univ_eq_one
  ¬∑ exact (hfoel.mono <| Ultrafilter.of_le l).mean_union_eq_add_of_disjoint
  ¬∑ exact (hfoel.mono <| Ultrafilter.of_le l).mean_smul_eq_mean

variable (G Œº) in
/-- The maximal F√∏lner filter with respect to some group `G` acting on a
    measure space `X` is the pullback of `ùìù 0` along the map `s ‚Ü¶ Œº (g ‚Ä¢ s) / Œº s`
    on measurable sets of finite non-zero measure. -/
def maxFoelner : Filter (Set X) :=
  ùìü {s : Set X | MeasurableSet s ‚àß Œº s ‚â† 0 ‚àß Œº s ‚â† ‚àû} ‚äì
  ‚®Ö (g : G), (comap (fun s ‚Ü¶ Œº ((g ‚Ä¢ s) ‚àÜ s) / Œº s) (ùìù 0))

variable (l F) in
theorem isFoelner_iff_tendsto : IsFoelner G Œº l F ‚Üî Tendsto F l (maxFoelner G Œº) := by
  simp [maxFoelner, tendsto_inf, tendsto_iInf, isFoelner_iff, Function.comp_def, and_assoc]

theorem amenable_of_maxFoelner_ne_bot [SMulInvariantMeasure G X Œº] [NeBot (maxFoelner G Œº)] :
    ‚àÉ m : Set X ‚Üí ‚Ñù‚â•0‚àû, m .univ = 1 ‚àß
      (‚àÄ s t, MeasurableSet t ‚Üí Disjoint s t ‚Üí m (s ‚à™ t) = m s + m t) ‚àß
        ‚àÄ (g : G) (s : Set X), m (g ‚Ä¢ s) = m s :=
  IsFoelner.amenable <| (isFoelner_iff_tendsto _ _).2 <| @tendsto_id _ (maxFoelner G Œº)

end Filter
