/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.MeasureTheory.Group.Action
import Mathlib.MeasureTheory.Integral.SetIntegral

#align_import measure_theory.group.fundamental_domain from "leanprover-community/mathlib"@"eb810cf549db839dadf13353dbe69bac55acdbbc"

/-!
# AEFundamental domain of a group action

A set `s` is said to be a *ae-fundamental domain* of an action of a group `G` on a measurable
space `Œ±` with respect to a measure `Œº` if

* `s` is a measurable set;

* the sets `g ‚Ä¢ s` over all `g : G` cover almost all points of the whole space;

* the sets `g ‚Ä¢ s`, are pairwise a.e. disjoint, i.e., `Œº (g‚ÇÅ ‚Ä¢ s ‚à© g‚ÇÇ ‚Ä¢ s) = 0` whenever `g‚ÇÅ ‚â† g‚ÇÇ`;
  we require this for `g‚ÇÇ = 1` in the definition, then deduce it for any two `g‚ÇÅ ‚â† g‚ÇÇ`.

In this file we prove that in case of a countable group `G` and a measure preserving action, any two
fundamental domains have the same measure, and for a `G`-invariant function, its integrals over any
two fundamental domains are equal to each other.

We also generate additive versions of all theorems in this file using the `to_additive` attribute.

## Main declarations

* `MeasureTheory.IsAEFundamentalDomain`: Predicate for a set to be an ae-fundamental domain of the
  action of a group
* `MeasureTheory.fundamentalFrontier`: Fundamental frontier of a set under the action of a group.
  Elements of `s` that belong to some other translate of `s`.
* `MeasureTheory.fundamentalInterior`: Fundamental interior of a set under the action of a group.
  Elements of `s` that do not belong to any other translate of `s`.
-/


open scoped ENNReal Pointwise Topology NNReal ENNReal MeasureTheory

open MeasureTheory MeasureTheory.Measure Set Function TopologicalSpace Filter

namespace MeasureTheory

/-- A measurable set `s` is an *ae-fundamental domain* for an additive action of an additive
group `G` on a measurable space `Œ±` with respect to a measure `Œ±` if the sets `g +·µ• s`, `g : G`,
are pairwise a.e. disjoint and cover the whole space. -/
structure IsAddAEFundamentalDomain (G : Type _) {Œ± : Type _} [Zero G] [VAdd G Œ±] [MeasurableSpace Œ±]
    (s : Set Œ±) (Œº : Measure Œ± := by volume_tac) : Prop where
  protected nullMeasurableSet : NullMeasurableSet s Œº
  protected ae_covers : ‚àÄ·µê x ‚àÇŒº, ‚àÉ g : G, g +·µ• x ‚àà s
  protected aedisjoint : Pairwise <| (AEDisjoint Œº on fun g : G => g +·µ• s)

/-- A measurable set `s` is an *ae-fundamental domain* for an action of a group `G` on a measurable
space `Œ±` with respect to a measure `Œ±` if the sets `g ‚Ä¢ s`, `g : G`, are pairwise a.e. disjoint
and cover the whole space. -/
@[to_additive IsAddAEFundamentalDomain]
structure IsAEFundamentalDomain (G : Type _) {Œ± : Type _} [One G] [SMul G Œ±] [MeasurableSpace Œ±]
    (s : Set Œ±) (Œº : Measure Œ± := by volume_tac) : Prop where
  protected nullMeasurableSet : NullMeasurableSet s Œº
  protected ae_covers : ‚àÄ·µê x ‚àÇŒº, ‚àÉ g : G, g ‚Ä¢ x ‚àà s
  protected aedisjoint : Pairwise <| (AEDisjoint Œº on fun g : G => g ‚Ä¢ s)

variable {G H Œ± Œ≤ E : Type _}

namespace IsAEFundamentalDomain

variable [Group G] [Group H] [MulAction G Œ±] [MeasurableSpace Œ±] [MulAction H Œ≤] [MeasurableSpace Œ≤]
  [NormedAddCommGroup E] {s t : Set Œ±} {Œº : Measure Œ±}

/-- If for each `x : Œ±`, exactly one of `g ‚Ä¢ x`, `g : G`, belongs to a measurable set `s`, then `s`
is a fundamental domain for the action of `G` on `Œ±`. -/
@[to_additive "If for each `x : Œ±`, exactly one of `g +·µ• x`, `g : G`, belongs to a measurable set
`s`, then `s` is a fundamental domain for the additive action of `G` on `Œ±`."]
theorem mk' (h_meas : NullMeasurableSet s Œº) (h_exists : ‚àÄ x : Œ±, ‚àÉ! g : G, g ‚Ä¢ x ‚àà s) :
    IsAEFundamentalDomain G s Œº where
  nullMeasurableSet := h_meas
  ae_covers := eventually_of_forall fun x => (h_exists x).exists
  aedisjoint a b hab := Disjoint.aedisjoint <| disjoint_left.2 fun x hxa hxb => by
    rw [mem_smul_set_iff_inv_smul_mem] at hxa hxb
    exact hab (inv_injective <| (h_exists x).unique hxa hxb)

/-- For `s` to be a fundamental domain, it's enough to check
`MeasureTheory.AEDisjoint (g ‚Ä¢ s) s` for `g ‚â† 1`. -/
@[to_additive "For `s` to be a fundamental domain, it's enough to check
  `MeasureTheory.AEDisjoint (g +·µ• s) s` for `g ‚â† 0`."]
theorem mk'' (h_meas : NullMeasurableSet s Œº) (h_ae_covers : ‚àÄ·µê x ‚àÇŒº, ‚àÉ g : G, g ‚Ä¢ x ‚àà s)
    (h_ae_disjoint : ‚àÄ g, g ‚â† (1 : G) ‚Üí AEDisjoint Œº (g ‚Ä¢ s) s)
    (h_qmp : ‚àÄ g : G, QuasiMeasurePreserving ((g ‚Ä¢ ¬∑) : Œ± ‚Üí Œ±) Œº Œº) :
    IsAEFundamentalDomain G s Œº where
  nullMeasurableSet := h_meas
  ae_covers := h_ae_covers
  aedisjoint := pairwise_aedisjoint_of_aedisjoint_forall_ne_one h_ae_disjoint h_qmp

/-- If a measurable space has a finite measure `Œº` and a countable group `G` acts
quasi-measure-preservingly, then to show that a set `s` is a fundamental domain, it is sufficient
to check that its translates `g ‚Ä¢ s` are (almost) disjoint and that the sum `‚àë' g, Œº (g ‚Ä¢ s)` is
sufficiently large. -/
@[to_additive
  "If a measurable space has a finite measure `Œº` and a countable additive group `G` acts
  quasi-measure-preservingly, then to show that a set `s` is a fundamental domain, it is sufficient
  to check that its translates `g +·µ• s` are (almost) disjoint and that the sum `‚àë' g, Œº (g +·µ• s)` is
  sufficiently large."]
theorem mk_of_measure_univ_le [IsFiniteMeasure Œº] [Countable G] (h_meas : NullMeasurableSet s Œº)
    (h_ae_disjoint : ‚àÄ (g) (_ : g ‚â† (1 : G)), AEDisjoint Œº (g ‚Ä¢ s) s)
    (h_qmp : ‚àÄ g : G, QuasiMeasurePreserving ((¬∑ ‚Ä¢ ¬∑) g : Œ± ‚Üí Œ±) Œº Œº)
    (h_measure_univ_le : Œº (univ : Set Œ±) ‚â§ ‚àë' g : G, Œº (g ‚Ä¢ s)) : IsAEFundamentalDomain G s Œº :=
  have aedisjoint : Pairwise (AEDisjoint Œº on fun g : G => g ‚Ä¢ s) :=
    pairwise_aedisjoint_of_aedisjoint_forall_ne_one h_ae_disjoint h_qmp
  { nullMeasurableSet := h_meas
    aedisjoint
    ae_covers := by
      replace h_meas : ‚àÄ g : G, NullMeasurableSet (g ‚Ä¢ s) Œº := fun g => by
        rw [‚Üê inv_inv g, ‚Üê preimage_smul]; exact h_meas.preimage (h_qmp g‚Åª¬π)
      have h_meas' : NullMeasurableSet {a | ‚àÉ g : G, g ‚Ä¢ a ‚àà s} Œº := by
        rw [‚Üê iUnion_smul_eq_setOf_exists]; exact .iUnion h_meas
      rw [ae_iff_measure_eq h_meas', ‚Üê iUnion_smul_eq_setOf_exists]
      refine' le_antisymm (measure_mono <| subset_univ _) _
      rw [measure_iUnion‚ÇÄ aedisjoint h_meas]
      exact h_measure_univ_le }

@[to_additive]
theorem iUnion_smul_ae_eq (h : IsAEFundamentalDomain G s Œº) : ‚ãÉ g : G, g ‚Ä¢ s =·µê[Œº] univ :=
  eventuallyEq_univ.2 <| h.ae_covers.mono fun _ ‚ü®g, hg‚ü© =>
    mem_iUnion.2 ‚ü®g‚Åª¬π, _, hg, inv_smul_smul _ _‚ü©

@[to_additive]
theorem mono (h : IsAEFundamentalDomain G s Œº) {ŒΩ : Measure Œ±} (hle : ŒΩ ‚â™ Œº) :
    IsAEFundamentalDomain G s ŒΩ :=
  ‚ü®h.1.mono_ac hle, hle h.2, h.aedisjoint.mono fun _ _ h => hle h‚ü©

@[to_additive]
theorem preimage_of_equiv {ŒΩ : Measure Œ≤} (h : IsAEFundamentalDomain G s Œº) {f : Œ≤ ‚Üí Œ±}
    (hf : QuasiMeasurePreserving f ŒΩ Œº) {e : G ‚Üí H} (he : Bijective e)
    (hef : ‚àÄ g, Semiconj f (e g ‚Ä¢ ¬∑) (g ‚Ä¢ ¬∑)) : IsAEFundamentalDomain H (f ‚Åª¬π' s) ŒΩ where
  nullMeasurableSet := h.nullMeasurableSet.preimage hf
  ae_covers := (hf.ae h.ae_covers).mono fun x ‚ü®g, hg‚ü© => ‚ü®e g, by rwa [mem_preimage, hef g x]‚ü©
  aedisjoint a b hab := by
    lift e to G ‚âÉ H using he
    have : (e.symm a‚Åª¬π)‚Åª¬π ‚â† (e.symm b‚Åª¬π)‚Åª¬π := by simp [hab]
    have := (h.aedisjoint this).preimage hf
    simp only [Semiconj] at hef
    simpa only [onFun, ‚Üê preimage_smul_inv, preimage_preimage, ‚Üê hef, e.apply_symm_apply, inv_inv]
      using this

@[to_additive]
theorem image_of_equiv {ŒΩ : Measure Œ≤} (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚âÉ Œ≤)
    (hf : QuasiMeasurePreserving f.symm ŒΩ Œº) (e : H ‚âÉ G)
    (hef : ‚àÄ g, Semiconj f (e g ‚Ä¢ ¬∑) (g ‚Ä¢ ¬∑)) : IsAEFundamentalDomain H (f '' s) ŒΩ := by
  rw [f.image_eq_preimage]
  refine' h.preimage_of_equiv hf e.symm.bijective fun g x => _
  rcases f.surjective x with ‚ü®x, rfl‚ü©
  rw [‚Üê hef _ _, f.symm_apply_apply, f.symm_apply_apply, e.apply_symm_apply]

@[to_additive]
theorem pairwise_aedisjoint_of_ac {ŒΩ} (h : IsAEFundamentalDomain G s Œº) (hŒΩ : ŒΩ ‚â™ Œº) :
    Pairwise fun g‚ÇÅ g‚ÇÇ : G => AEDisjoint ŒΩ (g‚ÇÅ ‚Ä¢ s) (g‚ÇÇ ‚Ä¢ s) :=
  h.aedisjoint.mono fun _ _ H => hŒΩ H

@[to_additive]
theorem smul_of_comm {G' : Type _} [Group G'] [MulAction G' Œ±] [MeasurableSpace G']
    [MeasurableSMul G' Œ±] [SMulInvariantMeasure G' Œ± Œº] [SMulCommClass G' G Œ±]
    (h : IsAEFundamentalDomain G s Œº) (g : G') : IsAEFundamentalDomain G (g ‚Ä¢ s) Œº :=
  h.image_of_equiv (MulAction.toPerm g) (measurePreserving_smul _ _).quasiMeasurePreserving
    (Equiv.refl _) <| smul_comm g

variable [MeasurableSpace G] [MeasurableSMul G Œ±] [SMulInvariantMeasure G Œ± Œº]

@[to_additive]
theorem nullMeasurableSet_smul (h : IsAEFundamentalDomain G s Œº) (g : G) :
    NullMeasurableSet (g ‚Ä¢ s) Œº :=
  h.nullMeasurableSet.smul g

@[to_additive]
theorem restrict_restrict (h : IsAEFundamentalDomain G s Œº) (g : G) (t : Set Œ±) :
    (Œº.restrict t).restrict (g ‚Ä¢ s) = Œº.restrict (g ‚Ä¢ s ‚à© t) :=
  restrict_restrict‚ÇÄ ((h.nullMeasurableSet_smul g).mono restrict_le_self)

@[to_additive]
theorem smul (h : IsAEFundamentalDomain G s Œº) (g : G) : IsAEFundamentalDomain G (g ‚Ä¢ s) Œº :=
  h.image_of_equiv (MulAction.toPerm g) (measurePreserving_smul _ _).quasiMeasurePreserving
    ‚ü®fun g' => g‚Åª¬π * g' * g, fun g' => g * g' * g‚Åª¬π, fun g' => by simp [mul_assoc], fun g' => by
      simp [mul_assoc]‚ü©
    fun g' x => by simp [smul_smul, mul_assoc]

variable [Countable G] {ŒΩ : Measure Œ±}

@[to_additive]
theorem sum_restrict_of_ac (h : IsAEFundamentalDomain G s Œº) (hŒΩ : ŒΩ ‚â™ Œº) :
    (sum fun g : G => ŒΩ.restrict (g ‚Ä¢ s)) = ŒΩ := by
  rw [‚Üê restrict_iUnion_ae (h.aedisjoint.mono fun i j h => hŒΩ h) fun g =>
      (h.nullMeasurableSet_smul g).mono_ac hŒΩ,
    restrict_congr_set (hŒΩ h.iUnion_smul_ae_eq), restrict_univ]

@[to_additive]
theorem lintegral_eq_tsum_of_ac (h : IsAEFundamentalDomain G s Œº) (hŒΩ : ŒΩ ‚â™ Œº) (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) :
    ‚à´‚Åª x, f x ‚àÇŒΩ = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ s, f x ‚àÇŒΩ := by
  rw [‚Üê lintegral_sum_measure, h.sum_restrict_of_ac hŒΩ]

@[to_additive]
theorem sum_restrict (h : IsAEFundamentalDomain G s Œº) : (sum fun g : G => Œº.restrict (g ‚Ä¢ s)) = Œº :=
  h.sum_restrict_of_ac (refl _)

@[to_additive]
theorem lintegral_eq_tsum (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) :
    ‚à´‚Åª x, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ s, f x ‚àÇŒº :=
  h.lintegral_eq_tsum_of_ac (refl _) f

@[to_additive]
theorem lintegral_eq_tsum' (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) :
    ‚à´‚Åª x, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº :=
  calc
    ‚à´‚Åª x, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ s, f x ‚àÇŒº := h.lintegral_eq_tsum f
    _ = ‚àë' g : G, ‚à´‚Åª x in g‚Åª¬π ‚Ä¢ s, f x ‚àÇŒº := ((Equiv.inv G).tsum_eq _).symm
    _ = ‚àë' g : G, ‚à´‚Åª x in s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº := tsum_congr fun g => Eq.symm <|
      (measurePreserving_smul g‚Åª¬π Œº).set_lintegral_comp_emb (measurableEmbedding_const_smul _) _ _

@[to_additive]
theorem set_lintegral_eq_tsum (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (t : Set Œ±) :
    ‚à´‚Åª x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in t ‚à© g ‚Ä¢ s, f x ‚àÇŒº :=
  calc
    ‚à´‚Åª x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ s, f x ‚àÇŒº.restrict t :=
      h.lintegral_eq_tsum_of_ac restrict_le_self.absolutelyContinuous _
    _ = ‚àë' g : G, ‚à´‚Åª x in t ‚à© g ‚Ä¢ s, f x ‚àÇŒº := by simp only [h.restrict_restrict, inter_comm]

@[to_additive]
theorem set_lintegral_eq_tsum' (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (t : Set Œ±) :
    ‚à´‚Åª x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ t ‚à© s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº :=
  calc
    ‚à´‚Åª x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in t ‚à© g ‚Ä¢ s, f x ‚àÇŒº := h.set_lintegral_eq_tsum f t
    _ = ‚àë' g : G, ‚à´‚Åª x in t ‚à© g‚Åª¬π ‚Ä¢ s, f x ‚àÇŒº := ((Equiv.inv G).tsum_eq _).symm
    _ = ‚àë' g : G, ‚à´‚Åª x in g‚Åª¬π ‚Ä¢ (g ‚Ä¢ t ‚à© s), f x ‚àÇŒº := by simp only [smul_set_inter, inv_smul_smul]
    _ = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ t ‚à© s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº := tsum_congr fun g => Eq.symm <|
      (measurePreserving_smul g‚Åª¬π Œº).set_lintegral_comp_emb (measurableEmbedding_const_smul _) _ _

@[to_additive]
theorem measure_eq_tsum_of_ac (h : IsAEFundamentalDomain G s Œº) (hŒΩ : ŒΩ ‚â™ Œº) (t : Set Œ±) :
    ŒΩ t = ‚àë' g : G, ŒΩ (t ‚à© g ‚Ä¢ s) := by
  have H : ŒΩ.restrict t ‚â™ Œº := Measure.restrict_le_self.absolutelyContinuous.trans hŒΩ
  simpa only [set_lintegral_one, Pi.one_def,
    Measure.restrict_apply‚ÇÄ ((h.nullMeasurableSet_smul _).mono_ac H), inter_comm] using
    h.lintegral_eq_tsum_of_ac H 1

@[to_additive]
theorem measure_eq_tsum' (h : IsAEFundamentalDomain G s Œº) (t : Set Œ±) :
    Œº t = ‚àë' g : G, Œº (t ‚à© g ‚Ä¢ s) :=
  h.measure_eq_tsum_of_ac AbsolutelyContinuous.rfl t

@[to_additive]
theorem measure_eq_tsum (h : IsAEFundamentalDomain G s Œº) (t : Set Œ±) :
    Œº t = ‚àë' g : G, Œº (g ‚Ä¢ t ‚à© s) := by
  simpa only [set_lintegral_one] using h.set_lintegral_eq_tsum' (fun _ => 1) t

@[to_additive]
theorem measure_zero_of_invariant (h : IsAEFundamentalDomain G s Œº) (t : Set Œ±)
    (ht : ‚àÄ g : G, g ‚Ä¢ t = t) (hts : Œº (t ‚à© s) = 0) : Œº t = 0 := by
  rw [measure_eq_tsum h]; simp [ht, hts]

/-- Given a measure space with an action of a finite group `G`, the measure of any `G`-invariant set
is determined by the measure of its intersection with a fundamental domain for the action of `G`. -/
@[to_additive measure_eq_card_smul_of_vadd_ae_eq_self "Given a measure space with an action of a
  finite additive group `G`, the measure of any `G`-invariant set is determined by the measure of
  its intersection with a fundamental domain for the action of `G`."]
theorem measure_eq_card_smul_of_smul_ae_eq_self [Finite G] (h : IsAEFundamentalDomain G s Œº)
    (t : Set Œ±) (ht : ‚àÄ g : G, (g ‚Ä¢ t : Set Œ±) =·µê[Œº] t) : Œº t = Nat.card G ‚Ä¢ Œº (t ‚à© s) := by
  haveI : Fintype G := Fintype.ofFinite G
  rw [h.measure_eq_tsum]
  replace ht : ‚àÄ g : G, (g ‚Ä¢ t ‚à© s : Set Œ±) =·µê[Œº] (t ‚à© s : Set Œ±) := fun g =>
    ae_eq_set_inter (ht g) (ae_eq_refl s)
  simp_rw [measure_congr (ht _), tsum_fintype, Finset.sum_const, Nat.card_eq_fintype_card,
    Finset.card_univ]

@[to_additive]
protected theorem set_lintegral_eq (hs : IsAEFundamentalDomain G s Œº) (ht : IsAEFundamentalDomain G t Œº)
    (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (hf : ‚àÄ (g : G) (x), f (g ‚Ä¢ x) = f x) :
    ‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in t, f x ‚àÇŒº :=
  calc
    ‚à´‚Åª x in s, f x ‚àÇŒº = ‚àë' g : G, ‚à´‚Åª x in s ‚à© g ‚Ä¢ t, f x ‚àÇŒº := ht.set_lintegral_eq_tsum _ _
    _ = ‚àë' g : G, ‚à´‚Åª x in g ‚Ä¢ t ‚à© s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº := by simp only [hf, inter_comm]
    _ = ‚à´‚Åª x in t, f x ‚àÇŒº := (hs.set_lintegral_eq_tsum' _ _).symm

@[to_additive]
theorem measure_set_eq (hs : IsAEFundamentalDomain G s Œº) (ht : IsAEFundamentalDomain G t Œº) {A : Set Œ±}
    (hA‚ÇÄ : MeasurableSet A) (hA : ‚àÄ g : G, (fun x => g ‚Ä¢ x) ‚Åª¬π' A = A) : Œº (A ‚à© s) = Œº (A ‚à© t) := by
  have : ‚à´‚Åª x in s, A.indicator 1 x ‚àÇŒº = ‚à´‚Åª x in t, A.indicator 1 x ‚àÇŒº := by
    refine hs.set_lintegral_eq ht (Set.indicator A fun _ => 1) fun g x ‚Ü¶ ?_
    convert (Set.indicator_comp_right (g ‚Ä¢ ¬∑ : Œ± ‚Üí Œ±) (g := fun _ ‚Ü¶ (1 : ‚Ñù‚â•0‚àû))).symm
    rw [hA g]
  simpa [Measure.restrict_apply hA‚ÇÄ, lintegral_indicator _ hA‚ÇÄ] using this

/-- If `s` and `t` are two fundamental domains of the same action, then their measures are equal. -/
@[to_additive "If `s` and `t` are two fundamental domains of the same action, then their measures
  are equal."]
protected theorem measure_eq (hs : IsAEFundamentalDomain G s Œº) (ht : IsAEFundamentalDomain G t Œº) :
    Œº s = Œº t := by
  simpa only [set_lintegral_one] using hs.set_lintegral_eq ht (fun _ => 1) fun _ _ => rfl

@[to_additive]
protected theorem aEStronglyMeasurable_on_iff {Œ≤ : Type _} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] (hs : IsAEFundamentalDomain G s Œº) (ht : IsAEFundamentalDomain G t Œº)
    {f : Œ± ‚Üí Œ≤} (hf : ‚àÄ (g : G) (x), f (g ‚Ä¢ x) = f x) :
    AEStronglyMeasurable f (Œº.restrict s) ‚Üî AEStronglyMeasurable f (Œº.restrict t) :=
  calc
    AEStronglyMeasurable f (Œº.restrict s) ‚Üî
        AEStronglyMeasurable f (Measure.sum fun g : G => Œº.restrict (g ‚Ä¢ t ‚à© s)) := by
      simp only [‚Üê ht.restrict_restrict,
        ht.sum_restrict_of_ac restrict_le_self.absolutelyContinuous]
    _ ‚Üî ‚àÄ g : G, AEStronglyMeasurable f (Œº.restrict (g ‚Ä¢ (g‚Åª¬π ‚Ä¢ s ‚à© t))) := by
      simp only [smul_set_inter, inter_comm, smul_inv_smul, aestronglyMeasurable_sum_measure_iff]
    _ ‚Üî ‚àÄ g : G, AEStronglyMeasurable f (Œº.restrict (g‚Åª¬π ‚Ä¢ (g‚Åª¬π‚Åª¬π ‚Ä¢ s ‚à© t))) :=
      inv_surjective.forall
    _ ‚Üî ‚àÄ g : G, AEStronglyMeasurable f (Œº.restrict (g‚Åª¬π ‚Ä¢ (g ‚Ä¢ s ‚à© t))) := by simp only [inv_inv]
    _ ‚Üî ‚àÄ g : G, AEStronglyMeasurable f (Œº.restrict (g ‚Ä¢ s ‚à© t)) := by
      refine' forall_congr' fun g => _
      have he : MeasurableEmbedding ((¬∑ ‚Ä¢ ¬∑) g‚Åª¬π : Œ± ‚Üí Œ±) := measurableEmbedding_const_smul _
      rw [‚Üê image_smul, ‚Üê ((measurePreserving_smul g‚Åª¬π Œº).restrict_image_emb he
        _).aestronglyMeasurable_comp_iff he]
      simp only [(¬∑ ‚àò ¬∑), hf]
    _ ‚Üî AEStronglyMeasurable f (Œº.restrict t) := by
      simp only [‚Üê aestronglyMeasurable_sum_measure_iff, ‚Üê hs.restrict_restrict,
        hs.sum_restrict_of_ac restrict_le_self.absolutelyContinuous]

@[to_additive]
protected theorem hasFiniteIntegral_on_iff (hs : IsAEFundamentalDomain G s Œº)
    (ht : IsAEFundamentalDomain G t Œº) {f : Œ± ‚Üí E} (hf : ‚àÄ (g : G) (x), f (g ‚Ä¢ x) = f x) :
    HasFiniteIntegral f (Œº.restrict s) ‚Üî HasFiniteIntegral f (Œº.restrict t) := by
  dsimp only [HasFiniteIntegral]
  rw [hs.set_lintegral_eq ht]
  intro g x; rw [hf]

@[to_additive]
protected theorem integrableOn_iff (hs : IsAEFundamentalDomain G s Œº) (ht : IsAEFundamentalDomain G t Œº)
    {f : Œ± ‚Üí E} (hf : ‚àÄ (g : G) (x), f (g ‚Ä¢ x) = f x) : IntegrableOn f s Œº ‚Üî IntegrableOn f t Œº :=
  and_congr (hs.aEStronglyMeasurable_on_iff ht hf) (hs.hasFiniteIntegral_on_iff ht hf)

variable [NormedSpace ‚Ñù E] [CompleteSpace E]

@[to_additive]
theorem integral_eq_tsum_of_ac (h : IsAEFundamentalDomain G s Œº) (hŒΩ : ŒΩ ‚â™ Œº) (f : Œ± ‚Üí E)
    (hf : Integrable f ŒΩ) : ‚à´ x, f x ‚àÇŒΩ = ‚àë' g : G, ‚à´ x in g ‚Ä¢ s, f x ‚àÇŒΩ := by
  rw [‚Üê MeasureTheory.integral_sum_measure, h.sum_restrict_of_ac hŒΩ]
  rw [h.sum_restrict_of_ac hŒΩ]
  exact hf

@[to_additive]
theorem integral_eq_tsum (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚Üí E) (hf : Integrable f Œº) :
    ‚à´ x, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in g ‚Ä¢ s, f x ‚àÇŒº :=
  integral_eq_tsum_of_ac h (by rfl) f hf

@[to_additive]
theorem integral_eq_tsum' (h : IsAEFundamentalDomain G s Œº) (f : Œ± ‚Üí E) (hf : Integrable f Œº) :
    ‚à´ x, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº :=
  calc
    ‚à´ x, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in g ‚Ä¢ s, f x ‚àÇŒº := h.integral_eq_tsum f hf
    _ = ‚àë' g : G, ‚à´ x in g‚Åª¬π ‚Ä¢ s, f x ‚àÇŒº := ((Equiv.inv G).tsum_eq _).symm
    _ = ‚àë' g : G, ‚à´ x in s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº := tsum_congr fun g =>
      (measurePreserving_smul g‚Åª¬π Œº).set_integral_image_emb (measurableEmbedding_const_smul _) _ _

@[to_additive]
theorem set_integral_eq_tsum (h : IsAEFundamentalDomain G s Œº) {f : Œ± ‚Üí E} {t : Set Œ±}
    (hf : IntegrableOn f t Œº) : ‚à´ x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in t ‚à© g ‚Ä¢ s, f x ‚àÇŒº :=
  calc
    ‚à´ x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in g ‚Ä¢ s, f x ‚àÇŒº.restrict t :=
      h.integral_eq_tsum_of_ac restrict_le_self.absolutelyContinuous f hf
    _ = ‚àë' g : G, ‚à´ x in t ‚à© g ‚Ä¢ s, f x ‚àÇŒº := by
      simp only [h.restrict_restrict, measure_smul, inter_comm]

@[to_additive]
theorem set_integral_eq_tsum' (h : IsAEFundamentalDomain G s Œº) {f : Œ± ‚Üí E} {t : Set Œ±}
    (hf : IntegrableOn f t Œº) : ‚à´ x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in g ‚Ä¢ t ‚à© s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº :=
  calc
    ‚à´ x in t, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in t ‚à© g ‚Ä¢ s, f x ‚àÇŒº := h.set_integral_eq_tsum hf
    _ = ‚àë' g : G, ‚à´ x in t ‚à© g‚Åª¬π ‚Ä¢ s, f x ‚àÇŒº := ((Equiv.inv G).tsum_eq _).symm
    _ = ‚àë' g : G, ‚à´ x in g‚Åª¬π ‚Ä¢ (g ‚Ä¢ t ‚à© s), f x ‚àÇŒº := by simp only [smul_set_inter, inv_smul_smul]
    _ = ‚àë' g : G, ‚à´ x in g ‚Ä¢ t ‚à© s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº :=
      tsum_congr fun g =>
        (measurePreserving_smul g‚Åª¬π Œº).set_integral_image_emb (measurableEmbedding_const_smul _) _ _

@[to_additive]
protected theorem set_integral_eq (hs : IsAEFundamentalDomain G s Œº) (ht : IsAEFundamentalDomain G t Œº)
    {f : Œ± ‚Üí E} (hf : ‚àÄ (g : G) (x), f (g ‚Ä¢ x) = f x) : ‚à´ x in s, f x ‚àÇŒº = ‚à´ x in t, f x ‚àÇŒº := by
  by_cases hfs : IntegrableOn f s Œº
  ¬∑ have hft : IntegrableOn f t Œº := by rwa [ht.integrableOn_iff hs hf]
    calc
      ‚à´ x in s, f x ‚àÇŒº = ‚àë' g : G, ‚à´ x in s ‚à© g ‚Ä¢ t, f x ‚àÇŒº := ht.set_integral_eq_tsum hfs
      _ = ‚àë' g : G, ‚à´ x in g ‚Ä¢ t ‚à© s, f (g‚Åª¬π ‚Ä¢ x) ‚àÇŒº := by simp only [hf, inter_comm]
      _ = ‚à´ x in t, f x ‚àÇŒº := (hs.set_integral_eq_tsum' hft).symm
  ¬∑ rw [integral_undef hfs, integral_undef]
    rwa [hs.integrableOn_iff ht hf] at hfs

/-- If the action of a countable group `G` admits an invariant measure `Œº` with a fundamental domain
`s`, then every null-measurable set `t` such that the sets `g ‚Ä¢ t ‚à© s` are pairwise a.e.-disjoint
has measure at most `Œº s`. -/
@[to_additive "If the additive action of a countable group `G` admits an invariant measure `Œº` with
  a fundamental domain `s`, then every null-measurable set `t` such that the sets `g +·µ• t ‚à© s` are
  pairwise a.e.-disjoint has measure at most `Œº s`."]
theorem measure_le_of_pairwise_disjoint (hs : IsAEFundamentalDomain G s Œº)
    (ht : NullMeasurableSet t Œº) (hd : Pairwise (AEDisjoint Œº on fun g : G => g ‚Ä¢ t ‚à© s)) :
    Œº t ‚â§ Œº s :=
  calc
    Œº t = ‚àë' g : G, Œº (g ‚Ä¢ t ‚à© s) := hs.measure_eq_tsum t
    _ = Œº (‚ãÉ g : G, g ‚Ä¢ t ‚à© s) := Eq.symm <| measure_iUnion‚ÇÄ hd fun _ =>
      (ht.smul _).inter hs.nullMeasurableSet
    _ ‚â§ Œº s := measure_mono (iUnion_subset fun _ => inter_subset_right _ _)

/-- If the action of a countable group `G` admits an invariant measure `Œº` with a fundamental domain
`s`, then every null-measurable set `t` of measure strictly greater than `Œº s` contains two
points `x y` such that `g ‚Ä¢ x = y` for some `g ‚â† 1`. -/
@[to_additive "If the additive action of a countable group `G` admits an invariant measure `Œº` with
  a fundamental domain `s`, then every null-measurable set `t` of measure strictly greater than
  `Œº s` contains two points `x y` such that `g +·µ• x = y` for some `g ‚â† 0`."]
theorem exists_ne_one_smul_eq (hs : IsAEFundamentalDomain G s Œº) (htm : NullMeasurableSet t Œº)
    (ht : Œº s < Œº t) : ‚àÉ x ‚àà t, ‚àÉ y ‚àà t, ‚àÉ g, g ‚â† (1 : G) ‚àß g ‚Ä¢ x = y := by
  contrapose! ht
  refine' hs.measure_le_of_pairwise_disjoint htm (Pairwise.aedisjoint fun g‚ÇÅ g‚ÇÇ hne => _)
  dsimp [Function.onFun]
  refine' (Disjoint.inf_left _ _).inf_right _
  rw [Set.disjoint_left]
  rintro _ ‚ü®x, hx, rfl‚ü© ‚ü®y, hy, hxy : g‚ÇÇ ‚Ä¢ y = g‚ÇÅ ‚Ä¢ x‚ü©
  refine' ht x hx y hy (g‚ÇÇ‚Åª¬π * g‚ÇÅ) (mt inv_mul_eq_one.1 hne.symm) _
  rw [mul_smul, ‚Üê hxy, inv_smul_smul]

/-- If `f` is invariant under the action of a countable group `G`, and `Œº` is a `G`-invariant
  measure with a fundamental domain `s`, then the `essSup` of `f` restricted to `s` is the same as
  that of `f` on all of its domain. -/
@[to_additive "If `f` is invariant under the action of a countable additive group `G`, and `Œº` is a
  `G`-invariant measure with a fundamental domain `s`, then the `ess_sup` of `f` restricted to `s`
  is the same as that of `f` on all of its domain."]
theorem essSup_measure_restrict (hs : IsAEFundamentalDomain G s Œº) {f : Œ± ‚Üí ‚Ñù‚â•0‚àû}
    (hf : ‚àÄ Œ≥ : G, ‚àÄ x : Œ±, f (Œ≥ ‚Ä¢ x) = f x) : essSup f (Œº.restrict s) = essSup f Œº := by
  refine' le_antisymm (essSup_mono_measure' Measure.restrict_le_self) _
  rw [essSup_eq_sInf (Œº.restrict s) f, essSup_eq_sInf Œº f]
  refine' sInf_le_sInf _
  rintro a (ha : (Œº.restrict s) {x : Œ± | a < f x} = 0)
  rw [Measure.restrict_apply‚ÇÄ' hs.nullMeasurableSet] at ha
  refine' measure_zero_of_invariant hs _ _ ha
  intro Œ≥
  ext x
  rw [mem_smul_set_iff_inv_smul_mem]
  simp only [mem_setOf_eq, hf Œ≥‚Åª¬π x]

end IsAEFundamentalDomain

/-! ### Interior/frontier of a fundamental domain -/

section MeasurableSpace

variable (G) [Group G] [MulAction G Œ±] (s : Set Œ±) {x : Œ±}

/-- The boundary of a fundamental domain, those points of the domain that also lie in a nontrivial
translate. -/
@[to_additive MeasureTheory.addFundamentalFrontier "The boundary of a fundamental domain, those
  points of the domain that also lie in a nontrivial translate."]
def fundamentalFrontier : Set Œ± :=
  s ‚à© ‚ãÉ (g : G) (_ : g ‚â† 1), g ‚Ä¢ s

/-- The interior of a fundamental domain, those points of the domain not lying in any translate. -/
@[to_additive MeasureTheory.addFundamentalInterior "The interior of a fundamental domain, those
  points of the domain not lying in any translate."]
def fundamentalInterior : Set Œ± :=
  s \ ‚ãÉ (g : G) (_ : g ‚â† 1), g ‚Ä¢ s

variable {G s}

@[to_additive (attr := simp) MeasureTheory.mem_addFundamentalFrontier]
theorem mem_fundamentalFrontier :
    x ‚àà fundamentalFrontier G s ‚Üî x ‚àà s ‚àß ‚àÉ g : G, g ‚â† 1 ‚àß x ‚àà g ‚Ä¢ s := by
  simp [fundamentalFrontier]

@[to_additive (attr := simp) MeasureTheory.mem_addFundamentalInterior]
theorem mem_fundamentalInterior :
    x ‚àà fundamentalInterior G s ‚Üî x ‚àà s ‚àß ‚àÄ g : G, g ‚â† 1 ‚Üí x ‚àâ g ‚Ä¢ s := by
  simp [fundamentalInterior]

@[to_additive MeasureTheory.addFundamentalFrontier_subset]
theorem fundamentalFrontier_subset : fundamentalFrontier G s ‚äÜ s :=
  inter_subset_left _ _

@[to_additive MeasureTheory.addFundamentalInterior_subset]
theorem fundamentalInterior_subset : fundamentalInterior G s ‚äÜ s :=
  diff_subset _ _

variable (G s)

@[to_additive MeasureTheory.disjoint_addFundamentalInterior_addFundamentalFrontier]
theorem disjoint_fundamentalInterior_fundamentalFrontier :
    Disjoint (fundamentalInterior G s) (fundamentalFrontier G s) :=
  disjoint_sdiff_self_left.mono_right inf_le_right

@[to_additive (attr := simp) MeasureTheory.addFundamentalInterior_union_addFundamentalFrontier]
theorem fundamentalInterior_union_fundamentalFrontier :
    fundamentalInterior G s ‚à™ fundamentalFrontier G s = s :=
  diff_union_inter _ _

@[to_additive (attr := simp) MeasureTheory.addFundamentalFrontier_union_addFundamentalInterior]
theorem fundamentalFrontier_union_fundamentalInterior :
    fundamentalFrontier G s ‚à™ fundamentalInterior G s = s :=
  inter_union_diff _ _
-- porting note: there is a typo in `to_additive` in mathlib3, so there is no additive version

@[to_additive (attr := simp) MeasureTheory.sdiff_addFundamentalInterior]
theorem sdiff_fundamentalInterior : s \ fundamentalInterior G s = fundamentalFrontier G s :=
  sdiff_sdiff_right_self

@[to_additive (attr := simp) MeasureTheory.sdiff_addFundamentalFrontier]
theorem sdiff_fundamentalFrontier : s \ fundamentalFrontier G s = fundamentalInterior G s :=
  diff_self_inter

@[to_additive (attr := simp) MeasureTheory.addFundamentalFrontier_vadd]
theorem fundamentalFrontier_smul [Group H] [MulAction H Œ±] [SMulCommClass H G Œ±] (g : H) :
    fundamentalFrontier G (g ‚Ä¢ s) = g ‚Ä¢ fundamentalFrontier G s := by
  simp_rw [fundamentalFrontier, smul_set_inter, smul_set_Union, smul_comm g (_ : G) (_ : Set Œ±)]

@[to_additive (attr := simp) MeasureTheory.addFundamentalInterior_vadd]
theorem fundamentalInterior_smul [Group H] [MulAction H Œ±] [SMulCommClass H G Œ±] (g : H) :
    fundamentalInterior G (g ‚Ä¢ s) = g ‚Ä¢ fundamentalInterior G s := by
  simp_rw [fundamentalInterior, smul_set_sdiff, smul_set_Union, smul_comm g (_ : G) (_ : Set Œ±)]

@[to_additive MeasureTheory.pairwise_disjoint_addFundamentalInterior]
theorem pairwise_disjoint_fundamentalInterior :
    Pairwise (Disjoint on fun g : G => g ‚Ä¢ fundamentalInterior G s) := by
  refine' fun a b hab => disjoint_left.2 _
  rintro _ ‚ü®x, hx, rfl‚ü© ‚ü®y, hy, hxy‚ü©
  rw [mem_fundamentalInterior] at hx hy
  refine' hx.2 (a‚Åª¬π * b) _ _
  rwa [Ne.def, inv_mul_eq_iff_eq_mul, mul_one, eq_comm]
  simpa [mul_smul, ‚Üê hxy, mem_inv_smul_set_iff] using hy.1

variable [Countable G] [MeasurableSpace G] [MeasurableSpace Œ±] [MeasurableSMul G Œ±] {Œº : Measure Œ±}
  [SMulInvariantMeasure G Œ± Œº]

@[to_additive MeasureTheory.NullMeasurableSet.addFundamentalFrontier]
protected theorem NullMeasurableSet.fundamentalFrontier (hs : NullMeasurableSet s Œº) :
    NullMeasurableSet (fundamentalFrontier G s) Œº :=
  hs.inter <| .iUnion fun _ => .iUnion fun _ => hs.smul _

@[to_additive MeasureTheory.NullMeasurableSet.addFundamentalInterior]
protected theorem NullMeasurableSet.fundamentalInterior (hs : NullMeasurableSet s Œº) :
    NullMeasurableSet (fundamentalInterior G s) Œº :=
  hs.diff <| .iUnion fun _ => .iUnion fun _ => hs.smul _

end MeasurableSpace

namespace IsAEFundamentalDomain

section Group

variable [Countable G] [Group G] [MulAction G Œ±] [MeasurableSpace Œ±] {Œº : Measure Œ±} {s : Set Œ±}
  (hs : IsAEFundamentalDomain G s Œº)

@[to_additive MeasureTheory.IsAddFundamentalDomain.measure_addFundamentalFrontier]
theorem measure_fundamentalFrontier : Œº (fundamentalFrontier G s) = 0 := by
  simpa only [fundamentalFrontier, iUnion‚ÇÇ_inter, measure_iUnion_null_iff', one_smul,
    measure_iUnion_null_iff, inter_comm s, Function.onFun] using fun g (hg : g ‚â† 1) =>
    hs.aedisjoint hg

@[to_additive MeasureTheory.IsAddFundamentalDomain.measure_addFundamentalInterior]
theorem measure_fundamentalInterior : Œº (fundamentalInterior G s) = Œº s :=
  measure_diff_null' hs.measure_fundamentalFrontier

end Group

variable [Countable G] [Group G] [MulAction G Œ±] [MeasurableSpace Œ±] {Œº : Measure Œ±} {s : Set Œ±}
  (hs : IsAEFundamentalDomain G s Œº) [MeasurableSpace G] [MeasurableSMul G Œ±]
  [SMulInvariantMeasure G Œ± Œº]

protected theorem fundamentalInterior : IsAEFundamentalDomain G (fundamentalInterior G s) Œº where
  nullMeasurableSet := hs.nullMeasurableSet.fundamentalInterior _ _
  ae_covers := by
    simp_rw [ae_iff, not_exists, ‚Üê mem_inv_smul_set_iff, setOf_forall, ‚Üê compl_setOf,
      setOf_mem_eq, ‚Üê compl_iUnion]
    have :
      ((‚ãÉ g : G, g‚Åª¬π ‚Ä¢ s) \ ‚ãÉ g : G, g‚Åª¬π ‚Ä¢ fundamentalFrontier G s) ‚äÜ
        ‚ãÉ g : G, g‚Åª¬π ‚Ä¢ fundamentalInterior G s := by
      simp_rw [diff_subset_iff, ‚Üê iUnion_union_distrib, ‚Üê smul_set_union (Œ± := G) (Œ≤ := Œ±),
        fundamentalFrontier_union_fundamentalInterior]; rfl
    refine' eq_bot_mono (Œº.mono <| compl_subset_compl.2 this) _
    simp only [iUnion_inv_smul, compl_sdiff, ENNReal.bot_eq_zero, himp_eq, sup_eq_union,
      @iUnion_smul_eq_setOf_exists _ _ _ _ s]
    exact measure_union_null
      (measure_iUnion_null fun _ => measure_smul_null hs.measure_fundamentalFrontier _) hs.ae_covers
  aedisjoint := (pairwise_disjoint_fundamentalInterior _ _).mono fun _ _ => Disjoint.aedisjoint

end IsAEFundamentalDomain


-- work 6/23/23

section FundamentalDomainMeasure

variable [Group G] [MulAction G Œ±] [MeasurableSpace Œ±]

local notation "Œ±_mod_G" => MulAction.orbitRel G Œ±

local notation "œÄ" => @Quotient.mk _ Œ±_mod_G

attribute [aesop norm unfold] Disjoint

-- if we encounter problems about the quotient measure being badly behaved on non-measurable sets,
-- use Caratheodory construction `outer_measure.to_measure` ...
noncomputable def _root_.Set.quotientMeasure (s : Set Œ±) (Œº : Measure Œ±)
    (hs : MeasurableSet s) : Measure (Quotient Œ±_mod_G) := by
  apply Measure.ofMeasurable (fun U _ => Œº ((œÄ ‚Åª¬π' U) ‚à© s)) (by simp)
  intro f meas_f disjoint_f
  let ff : ‚Ñï ‚Üí Set Œ± := fun n => œÄ ‚Åª¬π' (f n) ‚à© s
  have meas_ff : ‚àÄ n, MeasurableSet (ff n) :=
    fun n => (measurableSet_quotient.mp (meas_f n)).inter hs
  have disjoint_ff : Pairwise (Disjoint on fun (n : ‚Ñï) => ff n)
  ¬∑ intro x y x_ne_y
    have := Disjoint.preimage œÄ (disjoint_f x_ne_y)
    rw [Function.onFun_apply]
    aesop
  convert Œº.m_iUnion meas_ff disjoint_ff using 1
  simp [iUnion_inter]

-- noncomputable def _root_.Set.quotientMeasureSpace (s : Set Œ±) (Œº : Measure Œ±)
--     (hs : MeasurableSet s) : MeasureSpace (Quotient Œ±_mod_G) :=
--   { Quotient.instMeasurableSpace with
--     volume := s.quotientMeasure Œº hs }

lemma _root_.Set.quotientMeasure_apply (s : Set Œ±) (Œº : Measure Œ±)
    (hs : MeasurableSet s) {U : Set (Quotient Œ±_mod_G)} (meas_U : MeasurableSet U) :
    (s.quotientMeasure Œº hs) U = Œº ((œÄ ‚Åª¬π' U) ‚à© s) :=
  MeasureTheory.Measure.ofMeasurable_apply U meas_U

lemma _root_.Set.quotientMeasure_eq_map_restrict (s : Set Œ±) (Œº : Measure Œ±)
    (hs : MeasurableSet s) :
    (s.quotientMeasure Œº hs) = Measure.map œÄ (Œº.restrict s) := by
  ext U meas_U
  rw [Measure.map_apply, Measure.restrict_apply]
  ¬∑ exact quotientMeasure_apply _ _ _ meas_U
  ¬∑ exact measurableSet_quotient.mp meas_U
  ¬∑ intro V hV
    exact measurableSet_quotient.mp hV
  ¬∑ exact meas_U

lemma _root_.Set.quotientMeasure_invariant [Countable G] [MeasurableSpace G] {s t : Set Œ±}
    {Œº : Measure Œ±} [SMulInvariantMeasure G Œ± Œº] [MeasurableSMul G Œ±]
    (fund_dom_s : IsAEFundamentalDomain G s Œº) (fund_dom_t : IsAEFundamentalDomain G t Œº)
    (meas_s : MeasurableSet s) (meas_t : MeasurableSet t) :
    (s.quotientMeasure Œº meas_s : Measure (Quotient Œ±_mod_G)) = t.quotientMeasure Œº meas_t := by
  apply MeasureTheory.Measure.ext
  intro U meas_U
  rw [quotientMeasure_apply _ _ _ meas_U, quotientMeasure_apply _ _ _ meas_U]
  apply MeasureTheory.IsAEFundamentalDomain.measure_set_eq fund_dom_s fund_dom_t
  ¬∑ exact measurableSet_quotient.mp meas_U
  ¬∑ intro g
    simp only
    ext x
    have : Quotient.mk Œ±_mod_G (g ‚Ä¢ x) = Quotient.mk Œ±_mod_G x
    ¬∑ apply Quotient.sound
      use g
    aesop


end FundamentalDomainMeasure

section HasFundamentalDomain

/-- We say a quotient of `Œ±` by `G` `HasFundamentalDomain` if there is a measurable set `s` for
  which `IsAEFundamentalDomain G s` holds -/
class HasFundamentalDomain (G : Type _) (Œ± : Type _) [One G] [SMul G Œ±]
  [MeasureSpace Œ±] : Prop :=
(has_fundamental_domain_characterization :
  ‚àÉ (s : Set Œ±), IsAEFundamentalDomain G s ‚àß MeasurableSet s)

def covolume (G : Type _) (Œ± : Type _) [One G] [SMul G Œ±] [MeasureSpace Œ±]
    [funDom : HasFundamentalDomain G Œ±] : ‚Ñù‚â•0‚àû :=
  volume funDom.has_fundamental_domain_characterization.choose

lemma covolume_eq_volume (G : Type _) (Œ± : Type _) [Group G] [MulAction G Œ±] [MeasurableSpace G]
    [Countable G] [MeasureSpace Œ±] [MeasurableSMul G Œ±] [SMulInvariantMeasure G Œ± volume]
    [funDom : HasFundamentalDomain G Œ±] (s : Set Œ±) (fund_dom_s : IsAEFundamentalDomain G s) :
    covolume G Œ± = volume s := by
  rw [covolume]
  rw [fund_dom_s.measure_eq]
  exact funDom.has_fundamental_domain_characterization.choose_spec.1

end HasFundamentalDomain

section QuotientVolumeEqVolume

variable (G Œ± : Type _) [Group G] [MulAction G Œ±] [MeasureSpace Œ±]

local notation "Œ±_mod_G" => MulAction.orbitRel G Œ±

local notation "œÄ" => @Quotient.mk _ Œ±_mod_G

set_option linter.unusedVariables false in
class QuotientVolumeEqVolumePreimage (Œº : Measure (Quotient Œ±_mod_G)) --[MeasureSpace (Quotient Œ±_mod_G)]
: Prop where
  projection_respects_measure : ‚àÄ (t : Set Œ±) (fund_dom_t : IsAEFundamentalDomain G t)
    (meas_t : MeasurableSet t) (U : Set (Quotient Œ±_mod_G)) (meas_U : MeasurableSet U),
    Œº U = volume (œÄ ‚Åª¬π' U ‚à© t)

/-- If you want to make an instance of the typeclass, you can do it starting from any given
  fundamental domain -/
lemma Set.quotientMeasureSpace.QuotientVolumeEqVolumePreimage [Countable G] [MeasurableSpace G]
    [SMulInvariantMeasure G Œ± volume] [MeasurableSMul G Œ±] (s : Set Œ±) (meas_s : MeasurableSet s)
    (fund_dom_s : IsAEFundamentalDomain G s) :
    @QuotientVolumeEqVolumePreimage G Œ± _ _ _ (s.quotientMeasure volume meas_s) :=
  { projection_respects_measure := by
      intro t fund_dom_t meas_t U meas_U
      rw [quotientMeasure_invariant fund_dom_s fund_dom_t meas_s meas_t,
        quotientMeasure_apply _ _ _ meas_U] }


lemma quotientVolumeEqVolumePreimage_map_restrict [Countable G] [MeasurableSpace G]
    [SMulInvariantMeasure G Œ± volume] [MeasurableSMul G Œ±] (s : Set Œ±) (meas_s : MeasurableSet s)
    (fund_dom_s : IsAEFundamentalDomain G s) :
    @QuotientVolumeEqVolumePreimage G Œ± _ _ _ (Measure.map œÄ (volume.restrict s)) := by
  convert Set.quotientMeasureSpace.QuotientVolumeEqVolumePreimage G Œ± s meas_s fund_dom_s
  exact Eq.symm (quotientMeasure_eq_map_restrict s volume meas_s)


/-- Any two measures satisfying `QuotientVolumeEqVolumePreimage` are equal. -/
lemma QuotientVolumeEqVolumePreimage.unique
    [hasFun : HasFundamentalDomain G Œ±]
    (Œº ŒΩ : Measure (Quotient Œ±_mod_G))
    [QuotientVolumeEqVolumePreimage G Œ± Œº] [QuotientVolumeEqVolumePreimage G Œ± ŒΩ] :
    Œº = ŒΩ := by
  ext U meas_U
  obtain ‚ü®ùìï, hùìï, meas_ùìï‚ü© := hasFun.has_fundamental_domain_characterization
  rw [QuotientVolumeEqVolumePreimage.projection_respects_measure ùìï hùìï meas_ùìï U meas_U,
    QuotientVolumeEqVolumePreimage.projection_respects_measure ùìï hùìï meas_ùìï U meas_U]

/-- Any measure satisfying `QuotientVolumeEqVolumePreimage` is equal to the quotient measure
  associated to the fundamental domain. -/
lemma QuotientVolumeEqVolumePreimage.eq_quotient_measure [Countable G] [MeasurableSpace G]
    [SMulInvariantMeasure G Œ± volume] [MeasurableSMul G Œ±] (s : Set Œ±) (meas_s : MeasurableSet s)
    (fund_dom_s : IsAEFundamentalDomain G s) (Œº : Measure (Quotient Œ±_mod_G))
    [QuotientVolumeEqVolumePreimage G Œ± Œº] :
    Œº = s.quotientMeasure volume meas_s := by
  haveI : HasFundamentalDomain G Œ± := ‚ü®‚ü®s, fund_dom_s, meas_s‚ü©‚ü©
  haveI := Set.quotientMeasureSpace.QuotientVolumeEqVolumePreimage G Œ± s meas_s fund_dom_s
  apply QuotientVolumeEqVolumePreimage.unique

lemma QuotientVolumeEqVolumePreimage.eq_map_restrict [Countable G] [MeasurableSpace G]
    [SMulInvariantMeasure G Œ± volume] [MeasurableSMul G Œ±] (s : Set Œ±) (meas_s : MeasurableSet s)
    (fund_dom_s : IsAEFundamentalDomain G s) (Œº : Measure (Quotient Œ±_mod_G))
    [QuotientVolumeEqVolumePreimage G Œ± Œº] :
    Œº = Measure.map œÄ (volume.restrict s) := by
  rw [QuotientVolumeEqVolumePreimage.eq_quotient_measure G Œ± s meas_s fund_dom_s Œº]
  exact Set.quotientMeasure_eq_map_restrict s volume meas_s


/-- The quotient map to `Œ± ‚ß∏ G` is measure-preserving between the restriction of `volume` to a
  fundamental domain in `Œ±` and a related measure satisfying `QuotientVolumeEqVolumePreimage`. -/
theorem measurePreserving_quotient_mk_of_quotientVolumeEqVolumePreimage
    [Countable G] [MeasurableSpace G] [MeasurableSMul G Œ±]
    [SMulInvariantMeasure G Œ± volume]
    (ùìï : Set Œ±) (hùìï : IsAEFundamentalDomain G ùìï)
    (meas_ùìï : MeasurableSet ùìï) (Œº : Measure (Quotient Œ±_mod_G))
    [QuotientVolumeEqVolumePreimage G Œ± Œº] :
    MeasurePreserving œÄ (volume.restrict ùìï) Œº where
  measurable := measurable_quotient_mk' (s := Œ±_mod_G)
  map_eq := by
    haveI : HasFundamentalDomain G Œ± := ‚ü®ùìï, hùìï, meas_ùìï‚ü©
    rw [QuotientVolumeEqVolumePreimage.eq_map_restrict (Œº := Œº) (s := ùìï)]
    ¬∑ assumption
    ¬∑ assumption

end QuotientVolumeEqVolume

---- NEW 8/8/23

section DirichletPolyhedron

variable {Œ± : Type _} [MetricSpace Œ±]  (G : Type _) [Group G] [Countable G] [MulAction G Œ±]
  [MeasurableSpace Œ±] [OpensMeasurableSpace Œ±]

--local notation "Œ±_mod_G" => MulAction.orbitRel G Œ±

--local notation "œÄ" => @Quotient.mk _ Œ±_mod_G

def DirichletPolyhedron (x : Œ±) : Set Œ± :=
  {y : Œ± | ‚àÄ g : G, dist x y ‚â§ dist (g ‚Ä¢ x) y}

theorem DirichletPolyhedron_eq_Inter (x : Œ±) :
    DirichletPolyhedron G x = ‚ãÇ g : G, {y : Œ± | dist x y ‚â§ dist (g ‚Ä¢ x) y} := by
  ext y
  simp only [DirichletPolyhedron, mem_setOf_eq, mem_iInter]

theorem IsAEFundamentalDomain_of_DirichletPolyhedron --[LocallyCompactSpace Œ±]
    [ProperSpace Œ±] [IsometricSMul G Œ±]
    [i‚ÇÅ : ProperlyDiscontinuousSMul G Œ±] (x : Œ±) (hx : ‚àÄ g : G, g ‚Ä¢ x ‚â† x) {Œº : Measure Œ±} :
    IsAEFundamentalDomain G (DirichletPolyhedron G x) Œº where
  nullMeasurableSet := by
    apply MeasurableSet.nullMeasurableSet
    dsimp [NullMeasurableSet]
    rw [DirichletPolyhedron_eq_Inter]
    apply MeasurableSet.iInter
    intro g
    apply @IsClosed.measurableSet Œ± ({y | dist x y ‚â§ dist (g ‚Ä¢ x) y}) _ _ _
    rw [‚Üê isOpen_compl_iff]
    suffices h : IsOpen {y : Œ± | dist (g ‚Ä¢ x) y < dist x y}
    ¬∑ convert h using 1
      ext y
      simp only [mem_compl_iff, mem_setOf_eq, not_le]
    ¬∑ apply isOpen_lt
      ¬∑ exact @Continuous.dist Œ± Œ± _ _ (fun y ‚Ü¶ (g ‚Ä¢ x)) (fun y ‚Ü¶ y) continuous_const continuous_id
      ¬∑ exact @Continuous.dist Œ± Œ± _ _ (fun y ‚Ü¶ x) (fun y ‚Ü¶ y) continuous_const continuous_id
  ae_covers := by
    filter_upwards
    set s := DirichletPolyhedron G x
    intro y
    let t := Metric.closedBall y (dist x y)
    have comp_t : IsCompact t := isCompact_closedBall y (dist x y)
    have fin_orbit := i‚ÇÅ.finite_disjoint_inter_image comp_t comp_t
    set Œì := {Œ≥ : G | (fun x ‚Ü¶ Œ≥ ‚Ä¢ x) '' t ‚à© t ‚â† ‚àÖ}
    have nonempty_Œì : Set.Nonempty Œì := by
      use 1
      simp only [image_smul, Metric.smul_closedBall, ne_eq, mem_setOf_eq, one_smul, inter_self,
        Metric.closedBall_eq_empty, not_lt]
      exact dist_nonneg
    obtain ‚ü®g, -, hg‚ü© :=
      @Set.exists_min_image G ‚Ñù _ Œì (fun Œ≥ ‚Ü¶ dist x (Œ≥ ‚Ä¢ y)) fin_orbit nonempty_Œì
    use g
    simp only [DirichletPolyhedron, mem_setOf_eq]
    intro Œ≥
    have dist_eq : dist (Œ≥ ‚Ä¢ x) (g ‚Ä¢ y) = dist x ((Œ≥‚Åª¬π * g) ‚Ä¢ y) := by
      convert @dist_smul G Œ± _ _ _ Œ≥ x ((Œ≥‚Åª¬π * g) ‚Ä¢ y) using 2
      rw [‚Üê mul_smul]
      simp
    have := hg (Œ≥‚Åª¬π * g)
    by_cases hŒ≥ : (Œ≥‚Åª¬π * g) ‚àà Œì
    ¬∑ have := this hŒ≥
      convert this using 1
    ¬∑ simp only [image_smul, Metric.smul_closedBall, ne_eq, mem_setOf_eq, not_not] at hŒ≥
      rw [dist_eq]
      sorry

    -- have : (‚ãÉ g : G, (fun y ‚Ü¶ g ‚Ä¢ y) '' s) = univ := by
    --   ext z
    --   simp only [image_smul, mem_iUnion, mem_univ, iff_true]


  aedisjoint := by
    set s := DirichletPolyhedron G x
    intro g‚ÇÅ g‚ÇÇ h
    change Œº (( (fun y ‚Ü¶ g‚ÇÅ ‚Ä¢ y) '' s) ‚à©  (fun y ‚Ü¶ g‚ÇÇ ‚Ä¢ y) '' s) = 0

    sorry




end DirichletPolyhedron

end MeasureTheory
