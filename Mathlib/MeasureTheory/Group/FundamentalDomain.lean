/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Alex Kontorovich, Heather Macbeth
-/
import Mathlib.MeasureTheory.Group.Action
import Mathlib.MeasureTheory.Group.Pointwise
import Mathlib.MeasureTheory.Integral.Lebesgue.Map
import Mathlib.MeasureTheory.Integral.Bochner.Set

/-!
# Fundamental domain of a group action

A set `s` is said to be a *fundamental domain* of an action of a group `G` on a measurable space `Î±`
with respect to a measure `Î¼` if

* `s` is a measurable set;

* the sets `g â€¢ s` over all `g : G` cover almost all points of the whole space;

* the sets `g â€¢ s`, are pairwise a.e. disjoint, i.e., `Î¼ (gâ‚ â€¢ s âˆ© gâ‚‚ â€¢ s) = 0` whenever `gâ‚ â‰  gâ‚‚`;
  we require this for `gâ‚‚ = 1` in the definition, then deduce it for any two `gâ‚ â‰  gâ‚‚`.

In this file we prove that in case of a countable group `G` and a measure-preserving action, any two
fundamental domains have the same measure, and for a `G`-invariant function, its integrals over any
two fundamental domains are equal to each other.

We also generate additive versions of all theorems in this file using the `to_additive` attribute.

* We define the `HasFundamentalDomain` typeclass, in particular to be able to define the `covolume`
  of a quotient of `Î±` by a group `G`, which under reasonable conditions does not depend on the
  choice of fundamental domain.

* We define the `QuotientMeasureEqMeasurePreimage` typeclass to describe a situation in which a
  measure `Î¼` on `Î± â§¸ G` can be computed by taking a measure `Î½` on `Î±` of the intersection of the
  pullback with a fundamental domain.

## Main declarations

* `MeasureTheory.IsFundamentalDomain`: Predicate for a set to be a fundamental domain of the
  action of a group
* `MeasureTheory.fundamentalFrontier`: Fundamental frontier of a set under the action of a group.
  Elements of `s` that belong to some other translate of `s`.
* `MeasureTheory.fundamentalInterior`: Fundamental interior of a set under the action of a group.
  Elements of `s` that do not belong to any other translate of `s`.
-/


open scoped ENNReal Pointwise Topology NNReal ENNReal MeasureTheory

open MeasureTheory MeasureTheory.Measure Set Function TopologicalSpace Filter

namespace MeasureTheory

/-- A measurable set `s` is a *fundamental domain* for an additive action of an additive group `G`
on a measurable space `Î±` with respect to a measure `Î±` if the sets `g +áµ¥ s`, `g : G`, are pairwise
a.e. disjoint and cover the whole space. -/
structure IsAddFundamentalDomain (G : Type*) {Î± : Type*} [Zero G] [VAdd G Î±] [MeasurableSpace Î±]
    (s : Set Î±) (Î¼ : Measure Î± := by volume_tac) : Prop where
  protected nullMeasurableSet : NullMeasurableSet s Î¼
  protected ae_covers : âˆ€áµ x âˆ‚Î¼, âˆƒ g : G, g +áµ¥ x âˆˆ s
  protected aedisjoint : Pairwise <| (AEDisjoint Î¼ on fun g : G => g +áµ¥ s)

/-- A measurable set `s` is a *fundamental domain* for an action of a group `G` on a measurable
space `Î±` with respect to a measure `Î±` if the sets `g â€¢ s`, `g : G`, are pairwise a.e. disjoint and
cover the whole space. -/
@[to_additive IsAddFundamentalDomain]
structure IsFundamentalDomain (G : Type*) {Î± : Type*} [One G] [SMul G Î±] [MeasurableSpace Î±]
    (s : Set Î±) (Î¼ : Measure Î± := by volume_tac) : Prop where
  protected nullMeasurableSet : NullMeasurableSet s Î¼
  protected ae_covers : âˆ€áµ x âˆ‚Î¼, âˆƒ g : G, g â€¢ x âˆˆ s
  protected aedisjoint : Pairwise <| (AEDisjoint Î¼ on fun g : G => g â€¢ s)

variable {G H Î± Î² E : Type*}

namespace IsFundamentalDomain

variable [Group G] [Group H] [MulAction G Î±] [MeasurableSpace Î±] [MulAction H Î²] [MeasurableSpace Î²]
  [NormedAddCommGroup E] {s t : Set Î±} {Î¼ : Measure Î±}

/-- If for each `x : Î±`, exactly one of `g â€¢ x`, `g : G`, belongs to a measurable set `s`, then `s`
is a fundamental domain for the action of `G` on `Î±`. -/
@[to_additive /-- If for each `x : Î±`, exactly one of `g +áµ¥ x`, `g : G`, belongs to a measurable set
`s`, then `s` is a fundamental domain for the additive action of `G` on `Î±`. -/]
theorem mk' (h_meas : NullMeasurableSet s Î¼) (h_exists : âˆ€ x : Î±, âˆƒ! g : G, g â€¢ x âˆˆ s) :
    IsFundamentalDomain G s Î¼ where
  nullMeasurableSet := h_meas
  ae_covers := Eventually.of_forall fun x => (h_exists x).exists
  aedisjoint a b hab := Disjoint.aedisjoint <| disjoint_left.2 fun x hxa hxb => by
    rw [mem_smul_set_iff_inv_smul_mem] at hxa hxb
    exact hab (inv_injective <| (h_exists x).unique hxa hxb)

/-- For `s` to be a fundamental domain, it's enough to check
`MeasureTheory.AEDisjoint (g â€¢ s) s` for `g â‰  1`. -/
@[to_additive /-- For `s` to be a fundamental domain, it's enough to check
  `MeasureTheory.AEDisjoint (g +áµ¥ s) s` for `g â‰  0`. -/]
theorem mk'' (h_meas : NullMeasurableSet s Î¼) (h_ae_covers : âˆ€áµ x âˆ‚Î¼, âˆƒ g : G, g â€¢ x âˆˆ s)
    (h_ae_disjoint : âˆ€ g, g â‰  (1 : G) â†’ AEDisjoint Î¼ (g â€¢ s) s)
    (h_qmp : âˆ€ g : G, QuasiMeasurePreserving ((g â€¢ Â·) : Î± â†’ Î±) Î¼ Î¼) :
    IsFundamentalDomain G s Î¼ where
  nullMeasurableSet := h_meas
  ae_covers := h_ae_covers
  aedisjoint := pairwise_aedisjoint_of_aedisjoint_forall_ne_one h_ae_disjoint h_qmp

/-- If a measurable space has a finite measure `Î¼` and a countable group `G` acts
quasi-measure-preservingly, then to show that a set `s` is a fundamental domain, it is sufficient
to check that its translates `g â€¢ s` are (almost) disjoint and that the sum `âˆ‘' g, Î¼ (g â€¢ s)` is
sufficiently large. -/
@[to_additive
  /-- If a measurable space has a finite measure `Î¼` and a countable additive group `G` acts
  quasi-measure-preservingly, then to show that a set `s` is a fundamental domain, it is sufficient
  to check that its translates `g +áµ¥ s` are (almost) disjoint and that the sum `âˆ‘' g, Î¼ (g +áµ¥ s)` is
  sufficiently large. -/]
theorem mk_of_measure_univ_le [IsFiniteMeasure Î¼] [Countable G] (h_meas : NullMeasurableSet s Î¼)
    (h_ae_disjoint : âˆ€ g â‰  (1 : G), AEDisjoint Î¼ (g â€¢ s) s)
    (h_qmp : âˆ€ g : G, QuasiMeasurePreserving (g â€¢ Â· : Î± â†’ Î±) Î¼ Î¼)
    (h_measure_univ_le : Î¼ (univ : Set Î±) â‰¤ âˆ‘' g : G, Î¼ (g â€¢ s)) : IsFundamentalDomain G s Î¼ :=
  have aedisjoint : Pairwise (AEDisjoint Î¼ on fun g : G => g â€¢ s) :=
    pairwise_aedisjoint_of_aedisjoint_forall_ne_one h_ae_disjoint h_qmp
  { nullMeasurableSet := h_meas
    aedisjoint
    ae_covers := by
      replace h_meas : âˆ€ g : G, NullMeasurableSet (g â€¢ s) Î¼ := fun g => by
        rw [â† inv_inv g, â† preimage_smul]; exact h_meas.preimage (h_qmp gâ»Â¹)
      have h_meas' : NullMeasurableSet {a | âˆƒ g : G, g â€¢ a âˆˆ s} Î¼ := by
        rw [â† iUnion_smul_eq_setOf_exists]; exact .iUnion h_meas
      rw [ae_iff_measure_eq h_meas', â† iUnion_smul_eq_setOf_exists]
      refine le_antisymm (measure_mono <| subset_univ _) ?_
      rw [measure_iUnionâ‚€ aedisjoint h_meas]
      exact h_measure_univ_le }

@[to_additive]
theorem iUnion_smul_ae_eq (h : IsFundamentalDomain G s Î¼) : â‹ƒ g : G, g â€¢ s =áµ[Î¼] univ :=
  eventuallyEq_univ.2 <| h.ae_covers.mono fun _ âŸ¨g, hgâŸ© =>
    mem_iUnion.2 âŸ¨gâ»Â¹, _, hg, inv_smul_smul _ _âŸ©

@[to_additive]
theorem measure_ne_zero [Countable G] [SMulInvariantMeasure G Î± Î¼]
    (hÎ¼ : Î¼ â‰  0) (h : IsFundamentalDomain G s Î¼) : Î¼ s â‰  0 := by
  have hc := measure_univ_pos.mpr hÎ¼
  contrapose! hc
  rw [â† measure_congr h.iUnion_smul_ae_eq]
  refine le_trans (measure_iUnion_le _) ?_
  simp_rw [measure_smul, hc, tsum_zero, le_refl]

@[to_additive]
theorem mono (h : IsFundamentalDomain G s Î¼) {Î½ : Measure Î±} (hle : Î½ â‰ª Î¼) :
    IsFundamentalDomain G s Î½ :=
  âŸ¨h.1.mono_ac hle, hle h.2, h.aedisjoint.mono fun _ _ h => hle hâŸ©

@[to_additive]
theorem preimage_of_equiv {Î½ : Measure Î²} (h : IsFundamentalDomain G s Î¼) {f : Î² â†’ Î±}
    (hf : QuasiMeasurePreserving f Î½ Î¼) {e : G â†’ H} (he : Bijective e)
    (hef : âˆ€ g, Semiconj f (e g â€¢ Â·) (g â€¢ Â·)) : IsFundamentalDomain H (f â»Â¹' s) Î½ where
  nullMeasurableSet := h.nullMeasurableSet.preimage hf
  ae_covers := (hf.ae h.ae_covers).mono fun x âŸ¨g, hgâŸ© => âŸ¨e g, by rwa [mem_preimage, hef g x]âŸ©
  aedisjoint a b hab := by
    lift e to G â‰ƒ H using he
    have : (e.symm aâ»Â¹)â»Â¹ â‰  (e.symm bâ»Â¹)â»Â¹ := by simp [hab]
    have := (h.aedisjoint this).preimage hf
    simp only [Semiconj] at hef
    simpa only [onFun, â† preimage_smul_inv, preimage_preimage, â† hef, e.apply_symm_apply, inv_inv]
      using this

@[to_additive]
theorem image_of_equiv {Î½ : Measure Î²} (h : IsFundamentalDomain G s Î¼) (f : Î± â‰ƒ Î²)
    (hf : QuasiMeasurePreserving f.symm Î½ Î¼) (e : H â‰ƒ G)
    (hef : âˆ€ g, Semiconj f (e g â€¢ Â·) (g â€¢ Â·)) : IsFundamentalDomain H (f '' s) Î½ := by
  rw [f.image_eq_preimage]
  refine h.preimage_of_equiv hf e.symm.bijective fun g x => ?_
  rcases f.surjective x with âŸ¨x, rflâŸ©
  rw [â† hef _ _, f.symm_apply_apply, f.symm_apply_apply, e.apply_symm_apply]

@[to_additive]
theorem pairwise_aedisjoint_of_ac {Î½} (h : IsFundamentalDomain G s Î¼) (hÎ½ : Î½ â‰ª Î¼) :
    Pairwise fun gâ‚ gâ‚‚ : G => AEDisjoint Î½ (gâ‚ â€¢ s) (gâ‚‚ â€¢ s) :=
  h.aedisjoint.mono fun _ _ H => hÎ½ H

@[to_additive]
theorem smul_of_comm {G' : Type*} [Group G'] [MulAction G' Î±] [MeasurableSpace G']
    [MeasurableSMul G' Î±] [SMulInvariantMeasure G' Î± Î¼] [SMulCommClass G' G Î±]
    (h : IsFundamentalDomain G s Î¼) (g : G') : IsFundamentalDomain G (g â€¢ s) Î¼ :=
  h.image_of_equiv (MulAction.toPerm g) (measurePreserving_smul _ _).quasiMeasurePreserving
    (Equiv.refl _) <| smul_comm g

variable [MeasurableSpace G] [MeasurableSMul G Î±] [SMulInvariantMeasure G Î± Î¼]

@[to_additive]
theorem nullMeasurableSet_smul (h : IsFundamentalDomain G s Î¼) (g : G) :
    NullMeasurableSet (g â€¢ s) Î¼ :=
  h.nullMeasurableSet.smul g

@[to_additive]
theorem restrict_restrict (h : IsFundamentalDomain G s Î¼) (g : G) (t : Set Î±) :
    (Î¼.restrict t).restrict (g â€¢ s) = Î¼.restrict (g â€¢ s âˆ© t) :=
  restrict_restrictâ‚€ ((h.nullMeasurableSet_smul g).mono restrict_le_self)

@[to_additive]
theorem smul (h : IsFundamentalDomain G s Î¼) (g : G) : IsFundamentalDomain G (g â€¢ s) Î¼ :=
  h.image_of_equiv (MulAction.toPerm g) (measurePreserving_smul _ _).quasiMeasurePreserving
    âŸ¨fun g' => gâ»Â¹ * g' * g, fun g' => g * g' * gâ»Â¹, fun g' => by simp [mul_assoc], fun g' => by
      simp [mul_assoc]âŸ©
    fun g' x => by simp [smul_smul, mul_assoc]

variable [Countable G] {Î½ : Measure Î±}

@[to_additive]
theorem sum_restrict_of_ac (h : IsFundamentalDomain G s Î¼) (hÎ½ : Î½ â‰ª Î¼) :
    (sum fun g : G => Î½.restrict (g â€¢ s)) = Î½ := by
  rw [â† restrict_iUnion_ae (h.aedisjoint.mono fun i j h => hÎ½ h) fun g =>
      (h.nullMeasurableSet_smul g).mono_ac hÎ½,
    restrict_congr_set (hÎ½ h.iUnion_smul_ae_eq), restrict_univ]

@[to_additive]
theorem lintegral_eq_tsum_of_ac (h : IsFundamentalDomain G s Î¼) (hÎ½ : Î½ â‰ª Î¼) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x, f x âˆ‚Î½ = âˆ‘' g : G, âˆ«â» x in g â€¢ s, f x âˆ‚Î½ := by
  rw [â† lintegral_sum_measure, h.sum_restrict_of_ac hÎ½]

@[to_additive]
theorem sum_restrict (h : IsFundamentalDomain G s Î¼) : (sum fun g : G => Î¼.restrict (g â€¢ s)) = Î¼ :=
  h.sum_restrict_of_ac (refl _)

@[to_additive]
theorem lintegral_eq_tsum (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in g â€¢ s, f x âˆ‚Î¼ :=
  h.lintegral_eq_tsum_of_ac (refl _) f

@[to_additive]
theorem lintegral_eq_tsum' (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ :=
  calc
    âˆ«â» x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in g â€¢ s, f x âˆ‚Î¼ := h.lintegral_eq_tsum f
    _ = âˆ‘' g : G, âˆ«â» x in gâ»Â¹ â€¢ s, f x âˆ‚Î¼ := ((Equiv.inv G).tsum_eq _).symm
    _ = âˆ‘' g : G, âˆ«â» x in s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ := tsum_congr fun g => Eq.symm <|
      (measurePreserving_smul gâ»Â¹ Î¼).setLIntegral_comp_emb (measurableEmbedding_const_smul _) _ _

@[to_additive] lemma lintegral_eq_tsum'' (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in s, f (g â€¢ x) âˆ‚Î¼ :=
  (lintegral_eq_tsum' h f).trans ((Equiv.inv G).tsum_eq (fun g â†¦ âˆ«â» (x : Î±) in s, f (g â€¢ x) âˆ‚Î¼))

@[to_additive]
theorem setLIntegral_eq_tsum (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ â„â‰¥0âˆ) (t : Set Î±) :
    âˆ«â» x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in t âˆ© g â€¢ s, f x âˆ‚Î¼ :=
  calc
    âˆ«â» x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in g â€¢ s, f x âˆ‚Î¼.restrict t :=
      h.lintegral_eq_tsum_of_ac restrict_le_self.absolutelyContinuous _
    _ = âˆ‘' g : G, âˆ«â» x in t âˆ© g â€¢ s, f x âˆ‚Î¼ := by simp only [h.restrict_restrict, inter_comm]

@[to_additive]
theorem setLIntegral_eq_tsum' (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ â„â‰¥0âˆ) (t : Set Î±) :
    âˆ«â» x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in g â€¢ t âˆ© s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ :=
  calc
    âˆ«â» x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in t âˆ© g â€¢ s, f x âˆ‚Î¼ := h.setLIntegral_eq_tsum f t
    _ = âˆ‘' g : G, âˆ«â» x in t âˆ© gâ»Â¹ â€¢ s, f x âˆ‚Î¼ := ((Equiv.inv G).tsum_eq _).symm
    _ = âˆ‘' g : G, âˆ«â» x in gâ»Â¹ â€¢ (g â€¢ t âˆ© s), f x âˆ‚Î¼ := by simp only [smul_set_inter, inv_smul_smul]
    _ = âˆ‘' g : G, âˆ«â» x in g â€¢ t âˆ© s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ := tsum_congr fun g => Eq.symm <|
      (measurePreserving_smul gâ»Â¹ Î¼).setLIntegral_comp_emb (measurableEmbedding_const_smul _) _ _

@[to_additive]
theorem measure_eq_tsum_of_ac (h : IsFundamentalDomain G s Î¼) (hÎ½ : Î½ â‰ª Î¼) (t : Set Î±) :
    Î½ t = âˆ‘' g : G, Î½ (t âˆ© g â€¢ s) := by
  have H : Î½.restrict t â‰ª Î¼ := Measure.restrict_le_self.absolutelyContinuous.trans hÎ½
  simpa only [setLIntegral_one, Pi.one_def,
    Measure.restrict_applyâ‚€ ((h.nullMeasurableSet_smul _).mono_ac H), inter_comm] using
    h.lintegral_eq_tsum_of_ac H 1

@[to_additive]
theorem measure_eq_tsum' (h : IsFundamentalDomain G s Î¼) (t : Set Î±) :
    Î¼ t = âˆ‘' g : G, Î¼ (t âˆ© g â€¢ s) :=
  h.measure_eq_tsum_of_ac AbsolutelyContinuous.rfl t

@[to_additive]
theorem measure_eq_tsum (h : IsFundamentalDomain G s Î¼) (t : Set Î±) :
    Î¼ t = âˆ‘' g : G, Î¼ (g â€¢ t âˆ© s) := by
  simpa only [setLIntegral_one] using h.setLIntegral_eq_tsum' (fun _ => 1) t

@[to_additive]
theorem measure_zero_of_invariant (h : IsFundamentalDomain G s Î¼) (t : Set Î±)
    (ht : âˆ€ g : G, g â€¢ t = t) (hts : Î¼ (t âˆ© s) = 0) : Î¼ t = 0 := by
  rw [measure_eq_tsum h]; simp [ht, hts]

/-- Given a measure space with an action of a finite group `G`, the measure of any `G`-invariant set
is determined by the measure of its intersection with a fundamental domain for the action of `G`. -/
@[to_additive measure_eq_card_smul_of_vadd_ae_eq_self /-- Given a measure space with an action of a
  finite additive group `G`, the measure of any `G`-invariant set is determined by the measure of
  its intersection with a fundamental domain for the action of `G`. -/]
theorem measure_eq_card_smul_of_smul_ae_eq_self [Finite G] (h : IsFundamentalDomain G s Î¼)
    (t : Set Î±) (ht : âˆ€ g : G, (g â€¢ t : Set Î±) =áµ[Î¼] t) : Î¼ t = Nat.card G â€¢ Î¼ (t âˆ© s) := by
  haveI : Fintype G := Fintype.ofFinite G
  rw [h.measure_eq_tsum]
  replace ht : âˆ€ g : G, (g â€¢ t âˆ© s : Set Î±) =áµ[Î¼] (t âˆ© s : Set Î±) := fun g =>
    ae_eq_set_inter (ht g) (ae_eq_refl s)
  simp_rw [measure_congr (ht _), tsum_fintype, Finset.sum_const, Nat.card_eq_fintype_card,
    Finset.card_univ]

@[to_additive]
protected theorem setLIntegral_eq (hs : IsFundamentalDomain G s Î¼) (ht : IsFundamentalDomain G t Î¼)
    (f : Î± â†’ â„â‰¥0âˆ) (hf : âˆ€ (g : G) (x), f (g â€¢ x) = f x) :
    âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in t, f x âˆ‚Î¼ :=
  calc
    âˆ«â» x in s, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ«â» x in s âˆ© g â€¢ t, f x âˆ‚Î¼ := ht.setLIntegral_eq_tsum _ _
    _ = âˆ‘' g : G, âˆ«â» x in g â€¢ t âˆ© s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ := by simp only [hf, inter_comm]
    _ = âˆ«â» x in t, f x âˆ‚Î¼ := (hs.setLIntegral_eq_tsum' _ _).symm

@[to_additive]
theorem measure_set_eq (hs : IsFundamentalDomain G s Î¼) (ht : IsFundamentalDomain G t Î¼) {A : Set Î±}
    (hAâ‚€ : MeasurableSet A) (hA : âˆ€ g : G, (fun x => g â€¢ x) â»Â¹' A = A) : Î¼ (A âˆ© s) = Î¼ (A âˆ© t) := by
  have : âˆ«â» x in s, A.indicator 1 x âˆ‚Î¼ = âˆ«â» x in t, A.indicator 1 x âˆ‚Î¼ := by
    refine hs.setLIntegral_eq ht (Set.indicator A fun _ => 1) fun g x â†¦ ?_
    convert (Set.indicator_comp_right (g â€¢ Â· : Î± â†’ Î±) (g := fun _ â†¦ (1 : â„â‰¥0âˆ))).symm
    rw [hA g]
  simpa [Measure.restrict_apply hAâ‚€, lintegral_indicator hAâ‚€] using this

/-- If `s` and `t` are two fundamental domains of the same action, then their measures are equal. -/
@[to_additive /-- If `s` and `t` are two fundamental domains of the same action, then their measures
  are equal. -/]
protected theorem measure_eq (hs : IsFundamentalDomain G s Î¼) (ht : IsFundamentalDomain G t Î¼) :
    Î¼ s = Î¼ t := by
  simpa only [setLIntegral_one] using hs.setLIntegral_eq ht (fun _ => 1) fun _ _ => rfl

@[to_additive]
protected theorem aestronglyMeasurable_on_iff {Î² : Type*} [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] (hs : IsFundamentalDomain G s Î¼) (ht : IsFundamentalDomain G t Î¼)
    {f : Î± â†’ Î²} (hf : âˆ€ (g : G) (x), f (g â€¢ x) = f x) :
    AEStronglyMeasurable f (Î¼.restrict s) â†” AEStronglyMeasurable f (Î¼.restrict t) :=
  calc
    AEStronglyMeasurable f (Î¼.restrict s) â†”
        AEStronglyMeasurable f (Measure.sum fun g : G => Î¼.restrict (g â€¢ t âˆ© s)) := by
      simp only [â† ht.restrict_restrict,
        ht.sum_restrict_of_ac restrict_le_self.absolutelyContinuous]
    _ â†” âˆ€ g : G, AEStronglyMeasurable f (Î¼.restrict (g â€¢ (gâ»Â¹ â€¢ s âˆ© t))) := by
      simp only [smul_set_inter, inter_comm, smul_inv_smul, aestronglyMeasurable_sum_measure_iff]
    _ â†” âˆ€ g : G, AEStronglyMeasurable f (Î¼.restrict (gâ»Â¹ â€¢ (gâ»Â¹â»Â¹ â€¢ s âˆ© t))) :=
      inv_surjective.forall
    _ â†” âˆ€ g : G, AEStronglyMeasurable f (Î¼.restrict (gâ»Â¹ â€¢ (g â€¢ s âˆ© t))) := by simp only [inv_inv]
    _ â†” âˆ€ g : G, AEStronglyMeasurable f (Î¼.restrict (g â€¢ s âˆ© t)) := by
      refine forall_congr' fun g => ?_
      have he : MeasurableEmbedding (gâ»Â¹ â€¢ Â· : Î± â†’ Î±) := measurableEmbedding_const_smul _
      rw [â† image_smul, â† ((measurePreserving_smul gâ»Â¹ Î¼).restrict_image_emb he
        _).aestronglyMeasurable_comp_iff he]
      simp only [Function.comp_def, hf]
    _ â†” AEStronglyMeasurable f (Î¼.restrict t) := by
      simp only [â† aestronglyMeasurable_sum_measure_iff, â† hs.restrict_restrict,
        hs.sum_restrict_of_ac restrict_le_self.absolutelyContinuous]

@[deprecated (since := "2025-04-09")]
alias aEStronglyMeasurable_on_iff := MeasureTheory.IsFundamentalDomain.aestronglyMeasurable_on_iff
@[deprecated (since := "2025-04-09")]
alias _root_.MeasureTheory.IsAddFundamentalDomain.aEStronglyMeasurable_on_iff :=
  MeasureTheory.IsAddFundamentalDomain.aestronglyMeasurable_on_iff

@[to_additive]
protected theorem hasFiniteIntegral_on_iff (hs : IsFundamentalDomain G s Î¼)
    (ht : IsFundamentalDomain G t Î¼) {f : Î± â†’ E} (hf : âˆ€ (g : G) (x), f (g â€¢ x) = f x) :
    HasFiniteIntegral f (Î¼.restrict s) â†” HasFiniteIntegral f (Î¼.restrict t) := by
  dsimp only [HasFiniteIntegral]
  rw [hs.setLIntegral_eq ht]
  intro g x; rw [hf]

@[to_additive]
protected theorem integrableOn_iff (hs : IsFundamentalDomain G s Î¼) (ht : IsFundamentalDomain G t Î¼)
    {f : Î± â†’ E} (hf : âˆ€ (g : G) (x), f (g â€¢ x) = f x) : IntegrableOn f s Î¼ â†” IntegrableOn f t Î¼ :=
  and_congr (hs.aestronglyMeasurable_on_iff ht hf) (hs.hasFiniteIntegral_on_iff ht hf)

variable [NormedSpace â„ E]

@[to_additive]
theorem integral_eq_tsum_of_ac (h : IsFundamentalDomain G s Î¼) (hÎ½ : Î½ â‰ª Î¼) (f : Î± â†’ E)
    (hf : Integrable f Î½) : âˆ« x, f x âˆ‚Î½ = âˆ‘' g : G, âˆ« x in g â€¢ s, f x âˆ‚Î½ := by
  rw [â† MeasureTheory.integral_sum_measure, h.sum_restrict_of_ac hÎ½]
  rw [h.sum_restrict_of_ac hÎ½]
  exact hf

@[to_additive]
theorem integral_eq_tsum (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ E) (hf : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in g â€¢ s, f x âˆ‚Î¼ :=
  integral_eq_tsum_of_ac h (by rfl) f hf

@[to_additive]
theorem integral_eq_tsum' (h : IsFundamentalDomain G s Î¼) (f : Î± â†’ E) (hf : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ :=
  calc
    âˆ« x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in g â€¢ s, f x âˆ‚Î¼ := h.integral_eq_tsum f hf
    _ = âˆ‘' g : G, âˆ« x in gâ»Â¹ â€¢ s, f x âˆ‚Î¼ := ((Equiv.inv G).tsum_eq _).symm
    _ = âˆ‘' g : G, âˆ« x in s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ := tsum_congr fun g =>
      (measurePreserving_smul gâ»Â¹ Î¼).setIntegral_image_emb (measurableEmbedding_const_smul _) _ _

@[to_additive] lemma integral_eq_tsum'' (h : IsFundamentalDomain G s Î¼)
    (f : Î± â†’ E) (hf : Integrable f Î¼) : âˆ« x, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in s, f (g â€¢ x) âˆ‚Î¼ :=
  (integral_eq_tsum' h f hf).trans ((Equiv.inv G).tsum_eq (fun g â†¦ âˆ« (x : Î±) in s, f (g â€¢ x) âˆ‚Î¼))

@[to_additive]
theorem setIntegral_eq_tsum (h : IsFundamentalDomain G s Î¼) {f : Î± â†’ E} {t : Set Î±}
    (hf : IntegrableOn f t Î¼) : âˆ« x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in t âˆ© g â€¢ s, f x âˆ‚Î¼ :=
  calc
    âˆ« x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in g â€¢ s, f x âˆ‚Î¼.restrict t :=
      h.integral_eq_tsum_of_ac restrict_le_self.absolutelyContinuous f hf
    _ = âˆ‘' g : G, âˆ« x in t âˆ© g â€¢ s, f x âˆ‚Î¼ := by
      simp only [h.restrict_restrict, inter_comm]

@[to_additive]
theorem setIntegral_eq_tsum' (h : IsFundamentalDomain G s Î¼) {f : Î± â†’ E} {t : Set Î±}
    (hf : IntegrableOn f t Î¼) : âˆ« x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in g â€¢ t âˆ© s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ :=
  calc
    âˆ« x in t, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in t âˆ© g â€¢ s, f x âˆ‚Î¼ := h.setIntegral_eq_tsum hf
    _ = âˆ‘' g : G, âˆ« x in t âˆ© gâ»Â¹ â€¢ s, f x âˆ‚Î¼ := ((Equiv.inv G).tsum_eq _).symm
    _ = âˆ‘' g : G, âˆ« x in gâ»Â¹ â€¢ (g â€¢ t âˆ© s), f x âˆ‚Î¼ := by simp only [smul_set_inter, inv_smul_smul]
    _ = âˆ‘' g : G, âˆ« x in g â€¢ t âˆ© s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ :=
      tsum_congr fun g =>
        (measurePreserving_smul gâ»Â¹ Î¼).setIntegral_image_emb (measurableEmbedding_const_smul _) _ _

@[to_additive]
protected theorem setIntegral_eq (hs : IsFundamentalDomain G s Î¼) (ht : IsFundamentalDomain G t Î¼)
    {f : Î± â†’ E} (hf : âˆ€ (g : G) (x), f (g â€¢ x) = f x) : âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in t, f x âˆ‚Î¼ := by
  by_cases hfs : IntegrableOn f s Î¼
  Â· have hft : IntegrableOn f t Î¼ := by rwa [ht.integrableOn_iff hs hf]
    calc
      âˆ« x in s, f x âˆ‚Î¼ = âˆ‘' g : G, âˆ« x in s âˆ© g â€¢ t, f x âˆ‚Î¼ := ht.setIntegral_eq_tsum hfs
      _ = âˆ‘' g : G, âˆ« x in g â€¢ t âˆ© s, f (gâ»Â¹ â€¢ x) âˆ‚Î¼ := by simp only [hf, inter_comm]
      _ = âˆ« x in t, f x âˆ‚Î¼ := (hs.setIntegral_eq_tsum' hft).symm
  Â· rw [integral_undef hfs, integral_undef]
    rwa [hs.integrableOn_iff ht hf] at hfs

/-- If the action of a countable group `G` admits an invariant measure `Î¼` with a fundamental domain
`s`, then every null-measurable set `t` such that the sets `g â€¢ t âˆ© s` are pairwise a.e.-disjoint
has measure at most `Î¼ s`. -/
@[to_additive /-- If the additive action of a countable group `G` admits an invariant measure `Î¼`
  with a fundamental domain `s`, then every null-measurable set `t` such that the sets `g +áµ¥ t âˆ© s`
  are pairwise a.e.-disjoint has measure at most `Î¼ s`. -/]
theorem measure_le_of_pairwise_disjoint (hs : IsFundamentalDomain G s Î¼)
    (ht : NullMeasurableSet t Î¼) (hd : Pairwise (AEDisjoint Î¼ on fun g : G => g â€¢ t âˆ© s)) :
    Î¼ t â‰¤ Î¼ s :=
  calc
    Î¼ t = âˆ‘' g : G, Î¼ (g â€¢ t âˆ© s) := hs.measure_eq_tsum t
    _ = Î¼ (â‹ƒ g : G, g â€¢ t âˆ© s) := Eq.symm <| measure_iUnionâ‚€ hd fun _ =>
      (ht.smul _).inter hs.nullMeasurableSet
    _ â‰¤ Î¼ s := measure_mono (iUnion_subset fun _ => inter_subset_right)

/-- If the action of a countable group `G` admits an invariant measure `Î¼` with a fundamental domain
`s`, then every null-measurable set `t` of measure strictly greater than `Î¼ s` contains two
points `x y` such that `g â€¢ x = y` for some `g â‰  1`. -/
@[to_additive /-- If the additive action of a countable group `G` admits an invariant measure `Î¼`
  with a fundamental domain `s`, then every null-measurable set `t` of measure strictly greater than
  `Î¼ s` contains two points `x y` such that `g +áµ¥ x = y` for some `g â‰  0`. -/]
theorem exists_ne_one_smul_eq (hs : IsFundamentalDomain G s Î¼) (htm : NullMeasurableSet t Î¼)
    (ht : Î¼ s < Î¼ t) : âˆƒ x âˆˆ t, âˆƒ y âˆˆ t, âˆƒ g, g â‰  (1 : G) âˆ§ g â€¢ x = y := by
  contrapose! ht
  refine hs.measure_le_of_pairwise_disjoint htm (Pairwise.aedisjoint fun gâ‚ gâ‚‚ hne => ?_)
  dsimp [Function.onFun]
  refine (Disjoint.inf_left _ ?_).inf_right _
  rw [Set.disjoint_left]
  rintro _ âŸ¨x, hx, rflâŸ© âŸ¨y, hy, hxy : gâ‚‚ â€¢ y = gâ‚ â€¢ xâŸ©
  refine ht x hx y hy (gâ‚‚â»Â¹ * gâ‚) (mt inv_mul_eq_one.1 hne.symm) ?_
  rw [mul_smul, â† hxy, inv_smul_smul]

/-- If `f` is invariant under the action of a countable group `G`, and `Î¼` is a `G`-invariant
  measure with a fundamental domain `s`, then the `essSup` of `f` restricted to `s` is the same as
  that of `f` on all of its domain. -/
@[to_additive /-- If `f` is invariant under the action of a countable additive group `G`, and `Î¼`
  is a `G`-invariant measure with a fundamental domain `s`, then the `essSup` of `f` restricted to
  `s` is the same as that of `f` on all of its domain. -/]
theorem essSup_measure_restrict (hs : IsFundamentalDomain G s Î¼) {f : Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ Î³ : G, âˆ€ x : Î±, f (Î³ â€¢ x) = f x) : essSup f (Î¼.restrict s) = essSup f Î¼ := by
  refine le_antisymm (essSup_mono_measure' Measure.restrict_le_self) ?_
  rw [essSup_eq_sInf (Î¼.restrict s) f, essSup_eq_sInf Î¼ f]
  refine sInf_le_sInf ?_
  rintro a (ha : (Î¼.restrict s) {x : Î± | a < f x} = 0)
  rw [Measure.restrict_applyâ‚€' hs.nullMeasurableSet] at ha
  refine measure_zero_of_invariant hs _ ?_ ha
  intro Î³
  ext x
  rw [mem_smul_set_iff_inv_smul_mem]
  simp only [mem_setOf_eq, hf Î³â»Â¹ x]

end IsFundamentalDomain

/-! ### Interior/frontier of a fundamental domain -/

section MeasurableSpace

variable (G) [Group G] [MulAction G Î±] (s : Set Î±) {x : Î±}

/-- The boundary of a fundamental domain, those points of the domain that also lie in a nontrivial
translate. -/
@[to_additive MeasureTheory.addFundamentalFrontier /-- The boundary of a fundamental domain, those
  points of the domain that also lie in a nontrivial translate. -/]
def fundamentalFrontier : Set Î± :=
  s âˆ© â‹ƒ (g : G) (_ : g â‰  1), g â€¢ s

/-- The interior of a fundamental domain, those points of the domain not lying in any translate. -/
@[to_additive MeasureTheory.addFundamentalInterior /-- The interior of a fundamental domain, those
  points of the domain not lying in any translate. -/]
def fundamentalInterior : Set Î± :=
  s \ â‹ƒ (g : G) (_ : g â‰  1), g â€¢ s

variable {G s}

@[to_additive (attr := simp) MeasureTheory.mem_addFundamentalFrontier]
theorem mem_fundamentalFrontier :
    x âˆˆ fundamentalFrontier G s â†” x âˆˆ s âˆ§ âˆƒ g : G, g â‰  1 âˆ§ x âˆˆ g â€¢ s := by
  simp [fundamentalFrontier]

@[to_additive (attr := simp) MeasureTheory.mem_addFundamentalInterior]
theorem mem_fundamentalInterior :
    x âˆˆ fundamentalInterior G s â†” x âˆˆ s âˆ§ âˆ€ g : G, g â‰  1 â†’ x âˆ‰ g â€¢ s := by
  simp [fundamentalInterior]

@[to_additive MeasureTheory.addFundamentalFrontier_subset]
theorem fundamentalFrontier_subset : fundamentalFrontier G s âŠ† s :=
  inter_subset_left

@[to_additive MeasureTheory.addFundamentalInterior_subset]
theorem fundamentalInterior_subset : fundamentalInterior G s âŠ† s :=
  diff_subset

variable (G s)

@[to_additive MeasureTheory.disjoint_addFundamentalInterior_addFundamentalFrontier]
theorem disjoint_fundamentalInterior_fundamentalFrontier :
    Disjoint (fundamentalInterior G s) (fundamentalFrontier G s) :=
  disjoint_sdiff_self_left.mono_right inf_le_right

@[to_additive (attr := simp) MeasureTheory.addFundamentalInterior_union_addFundamentalFrontier]
theorem fundamentalInterior_union_fundamentalFrontier :
    fundamentalInterior G s âˆª fundamentalFrontier G s = s :=
  diff_union_inter _ _

@[to_additive (attr := simp) MeasureTheory.addFundamentalFrontier_union_addFundamentalInterior]
theorem fundamentalFrontier_union_fundamentalInterior :
    fundamentalFrontier G s âˆª fundamentalInterior G s = s :=
  inter_union_diff _ _

@[to_additive (attr := simp) MeasureTheory.sdiff_addFundamentalInterior]
theorem sdiff_fundamentalInterior : s \ fundamentalInterior G s = fundamentalFrontier G s :=
  sdiff_sdiff_right_self

@[to_additive (attr := simp) MeasureTheory.sdiff_addFundamentalFrontier]
theorem sdiff_fundamentalFrontier : s \ fundamentalFrontier G s = fundamentalInterior G s :=
  diff_self_inter

@[to_additive (attr := simp) MeasureTheory.addFundamentalFrontier_vadd]
theorem fundamentalFrontier_smul [Group H] [MulAction H Î±] [SMulCommClass H G Î±] (g : H) :
    fundamentalFrontier G (g â€¢ s) = g â€¢ fundamentalFrontier G s := by
  simp_rw [fundamentalFrontier, smul_set_inter, smul_set_iUnion, smul_comm g (_ : G) (_ : Set Î±)]

@[to_additive (attr := simp) MeasureTheory.addFundamentalInterior_vadd]
theorem fundamentalInterior_smul [Group H] [MulAction H Î±] [SMulCommClass H G Î±] (g : H) :
    fundamentalInterior G (g â€¢ s) = g â€¢ fundamentalInterior G s := by
  simp_rw [fundamentalInterior, smul_set_sdiff, smul_set_iUnion, smul_comm g (_ : G) (_ : Set Î±)]

@[to_additive MeasureTheory.pairwise_disjoint_addFundamentalInterior]
theorem pairwise_disjoint_fundamentalInterior :
    Pairwise (Disjoint on fun g : G => g â€¢ fundamentalInterior G s) := by
  refine fun a b hab => disjoint_left.2 ?_
  rintro _ âŸ¨x, hx, rflâŸ© âŸ¨y, hy, hxyâŸ©
  rw [mem_fundamentalInterior] at hx hy
  refine hx.2 (aâ»Â¹ * b) ?_ ?_
  Â· rwa [Ne, inv_mul_eq_iff_eq_mul, mul_one, eq_comm]
  Â· simpa [mul_smul, â† hxy, mem_inv_smul_set_iff] using hy.1

variable [Countable G] [MeasurableSpace G] [MeasurableSpace Î±] [MeasurableSMul G Î±] {Î¼ : Measure Î±}
  [SMulInvariantMeasure G Î± Î¼]

@[to_additive MeasureTheory.NullMeasurableSet.addFundamentalFrontier]
protected theorem NullMeasurableSet.fundamentalFrontier (hs : NullMeasurableSet s Î¼) :
    NullMeasurableSet (fundamentalFrontier G s) Î¼ :=
  hs.inter <| .iUnion fun _ => .iUnion fun _ => hs.smul _

@[to_additive MeasureTheory.NullMeasurableSet.addFundamentalInterior]
protected theorem NullMeasurableSet.fundamentalInterior (hs : NullMeasurableSet s Î¼) :
    NullMeasurableSet (fundamentalInterior G s) Î¼ :=
  hs.diff <| .iUnion fun _ => .iUnion fun _ => hs.smul _

end MeasurableSpace

namespace IsFundamentalDomain

variable [Countable G] [Group G] [MulAction G Î±] [MeasurableSpace Î±] {Î¼ : Measure Î±} {s : Set Î±}
  (hs : IsFundamentalDomain G s Î¼)
include hs

section Group


@[to_additive MeasureTheory.IsAddFundamentalDomain.measure_addFundamentalFrontier]
theorem measure_fundamentalFrontier : Î¼ (fundamentalFrontier G s) = 0 := by
  simpa only [fundamentalFrontier, iUnionâ‚‚_inter, one_smul, measure_iUnion_null_iff, inter_comm s,
    Function.onFun] using fun g (hg : g â‰  1) => hs.aedisjoint hg

@[to_additive MeasureTheory.IsAddFundamentalDomain.measure_addFundamentalInterior]
theorem measure_fundamentalInterior : Î¼ (fundamentalInterior G s) = Î¼ s :=
  measure_diff_null' hs.measure_fundamentalFrontier

end Group

variable [MeasurableSpace G] [MeasurableSMul G Î±] [SMulInvariantMeasure G Î± Î¼]

protected theorem fundamentalInterior : IsFundamentalDomain G (fundamentalInterior G s) Î¼ where
  nullMeasurableSet := hs.nullMeasurableSet.fundamentalInterior _ _
  ae_covers := by
    simp_rw [ae_iff, not_exists, â† mem_inv_smul_set_iff, setOf_forall, â† compl_setOf,
      setOf_mem_eq, â† compl_iUnion]
    have :
      ((â‹ƒ g : G, gâ»Â¹ â€¢ s) \ â‹ƒ g : G, gâ»Â¹ â€¢ fundamentalFrontier G s) âŠ†
        â‹ƒ g : G, gâ»Â¹ â€¢ fundamentalInterior G s := by
      simp_rw [diff_subset_iff, â† iUnion_union_distrib, â† smul_set_union (Î± := G) (Î² := Î±),
        fundamentalFrontier_union_fundamentalInterior]; rfl
    refine eq_bot_mono (Î¼.mono <| compl_subset_compl.2 this) ?_
    simp only [iUnion_inv_smul, compl_sdiff, ENNReal.bot_eq_zero, himp_eq, sup_eq_union,
      @iUnion_smul_eq_setOf_exists _ _ _ _ s]
    exact measure_union_null
      (measure_iUnion_null fun _ => measure_smul_null hs.measure_fundamentalFrontier _) hs.ae_covers
  aedisjoint := (pairwise_disjoint_fundamentalInterior _ _).mono fun _ _ => Disjoint.aedisjoint

end IsFundamentalDomain

section FundamentalDomainMeasure

variable (G) [Group G] [MulAction G Î±] [MeasurableSpace Î±]
  (Î¼ : Measure Î±)

local notation "Î±_mod_G" => MulAction.orbitRel G Î±

local notation "Ï€" => @Quotient.mk _ Î±_mod_G

variable {G}

@[to_additive addMeasure_map_restrict_apply]
lemma measure_map_restrict_apply (s : Set Î±) {U : Set (Quotient Î±_mod_G)}
    (meas_U : MeasurableSet U) :
    (Î¼.restrict s).map Ï€ U = Î¼ ((Ï€ â»Â¹' U) âˆ© s) := by
  rw [map_apply (f := Ï€) (fun V hV â†¦ measurableSet_quotient.mp hV) meas_U,
    Measure.restrict_apply (t := (Quotient.mk Î±_mod_G â»Â¹' U)) (measurableSet_quotient.mp meas_U)]

@[to_additive]
lemma IsFundamentalDomain.quotientMeasure_eq [Countable G] [MeasurableSpace G] {s t : Set Î±}
    [SMulInvariantMeasure G Î± Î¼] [MeasurableSMul G Î±] (fund_dom_s : IsFundamentalDomain G s Î¼)
    (fund_dom_t : IsFundamentalDomain G t Î¼) :
    (Î¼.restrict s).map Ï€ = (Î¼.restrict t).map Ï€ := by
  ext U meas_U
  rw [measure_map_restrict_apply (meas_U := meas_U), measure_map_restrict_apply (meas_U := meas_U)]
  apply MeasureTheory.IsFundamentalDomain.measure_set_eq fund_dom_s fund_dom_t
  Â· exact measurableSet_quotient.mp meas_U
  Â· intro g
    ext x
    have : Quotient.mk Î±_mod_G (g â€¢ x) = Quotient.mk Î±_mod_G x := by
      apply Quotient.sound
      use g
    simp only [mem_preimage, this]

end FundamentalDomainMeasure

/-! ## `HasFundamentalDomain` typeclass

We define `HasFundamentalDomain` in order to be able to define the `covolume` of a quotient of `Î±`
by a group `G`, which under reasonable conditions does not depend on the choice of fundamental
domain. Even though any "sensible" action should have a fundamental domain, this is a rather
delicate question which was recently addressed by Misha Kapovich: https://arxiv.org/abs/2301.05325

TODO: Formalize the existence of a Dirichlet domain as in Kapovich's paper.

-/

section HasFundamentalDomain

/-- We say a quotient of `Î±` by `G` `HasAddFundamentalDomain` if there is a measurable set
  `s` for which `IsAddFundamentalDomain G s` holds. -/
class HasAddFundamentalDomain (G Î± : Type*) [Zero G] [VAdd G Î±] [MeasurableSpace Î±]
    (Î½ : Measure Î± := by volume_tac) : Prop where
  ExistsIsAddFundamentalDomain : âˆƒ s : Set Î±, IsAddFundamentalDomain G s Î½

/-- We say a quotient of `Î±` by `G` `HasFundamentalDomain` if there is a measurable set `s` for
  which `IsFundamentalDomain G s` holds. -/
class HasFundamentalDomain (G : Type*) (Î± : Type*) [One G] [SMul G Î±] [MeasurableSpace Î±]
    (Î½ : Measure Î± := by volume_tac) : Prop where
  ExistsIsFundamentalDomain : âˆƒ (s : Set Î±), IsFundamentalDomain G s Î½

attribute [to_additive existing] MeasureTheory.HasFundamentalDomain

open Classical in
/-- The `covolume` of an action of `G` on `Î±` the volume of some fundamental domain, or `0` if
none exists. -/
@[to_additive addCovolume /-- The `addCovolume` of an action of `G` on `Î±` is the volume of some
fundamental domain, or `0` if none exists. -/]
noncomputable def covolume (G Î± : Type*) [One G] [SMul G Î±] [MeasurableSpace Î±]
    (Î½ : Measure Î± := by volume_tac) : â„â‰¥0âˆ :=
  if funDom : HasFundamentalDomain G Î± Î½ then Î½ funDom.ExistsIsFundamentalDomain.choose else 0

variable [Group G] [MulAction G Î±] [MeasurableSpace Î±]

/-- If there is a fundamental domain `s`, then `HasFundamentalDomain` holds. -/
@[to_additive]
lemma IsFundamentalDomain.hasFundamentalDomain (Î½ : Measure Î±) {s : Set Î±}
    (fund_dom_s : IsFundamentalDomain G s Î½) :
    HasFundamentalDomain G Î± Î½ := âŸ¨âŸ¨s, fund_dom_sâŸ©âŸ©

/-- The `covolume` can be computed by taking the `volume` of any given fundamental domain `s`. -/
@[to_additive]
lemma IsFundamentalDomain.covolume_eq_volume (Î½ : Measure Î±) [Countable G]
    [MeasurableSpace G] [MeasurableSMul G Î±] [SMulInvariantMeasure G Î± Î½] {s : Set Î±}
    (fund_dom_s : IsFundamentalDomain G s Î½) : covolume G Î± Î½ = Î½ s := by
  dsimp [covolume]
  simp only [(fund_dom_s.hasFundamentalDomain Î½), â†“reduceDIte]
  rw [fund_dom_s.measure_eq]
  exact (fund_dom_s.hasFundamentalDomain Î½).ExistsIsFundamentalDomain.choose_spec

end HasFundamentalDomain

/-! ## `QuotientMeasureEqMeasurePreimage` typeclass

This typeclass describes a situation in which a measure `Î¼` on `Î± â§¸ G` can be computed by
taking a measure `Î½` on `Î±` of the intersection of the pullback with a fundamental domain.

It's curious that in measure theory, measures can be pushed forward, while in geometry, volumes can
be pulled back. And yet here, we are describing a situation involving measures in a geometric way.

Another viewpoint is that if a set is small enough to fit in a single fundamental domain, then its
`Î½` measure in `Î±` is the same as the `Î¼` measure of its pushforward in `Î± â§¸ G`.

-/

section QuotientMeasureEqMeasurePreimage

section additive

variable [AddGroup G] [AddAction G Î±] [MeasurableSpace Î±]

local notation "Î±_mod_G" => AddAction.orbitRel G Î±

local notation "Ï€" => @Quotient.mk _ Î±_mod_G

/-- A measure `Î¼` on the `AddQuotient` of `Î±` mod `G` satisfies
  `AddQuotientMeasureEqMeasurePreimage` if: for any fundamental domain `t`, and any measurable
  subset `U` of the quotient, `Î¼ U = volume ((Ï€ â»Â¹' U) âˆ© t)`. -/
class AddQuotientMeasureEqMeasurePreimage (Î½ : Measure Î± := by volume_tac)
    (Î¼ : Measure (Quotient Î±_mod_G)) : Prop where
  addProjection_respects_measure' : âˆ€ (t : Set Î±) (_ : IsAddFundamentalDomain G t Î½),
    Î¼ = (Î½.restrict t).map Ï€

end additive

variable [Group G] [MulAction G Î±] [MeasurableSpace Î±]

local notation "Î±_mod_G" => MulAction.orbitRel G Î±

local notation "Ï€" => @Quotient.mk _ Î±_mod_G

/-- Measures `Î½` on `Î±` and `Î¼` on the `Quotient` of `Î±` mod `G` satisfy
  `QuotientMeasureEqMeasurePreimage` if: for any fundamental domain `t`, and any measurable subset
  `U` of the quotient, `Î¼ U = Î½ ((Ï€ â»Â¹' U) âˆ© t)`. -/
class QuotientMeasureEqMeasurePreimage (Î½ : Measure Î± := by volume_tac)
    (Î¼ : Measure (Quotient Î±_mod_G)) : Prop where
  projection_respects_measure' (t : Set Î±) : IsFundamentalDomain G t Î½ â†’ Î¼ = (Î½.restrict t).map Ï€

attribute [to_additive]
  MeasureTheory.QuotientMeasureEqMeasurePreimage

@[to_additive addProjection_respects_measure]
lemma IsFundamentalDomain.projection_respects_measure {Î½ : Measure Î±}
    (Î¼ : Measure (Quotient Î±_mod_G)) [i : QuotientMeasureEqMeasurePreimage Î½ Î¼] {t : Set Î±}
    (fund_dom_t : IsFundamentalDomain G t Î½) : Î¼ = (Î½.restrict t).map Ï€ :=
  i.projection_respects_measure' t fund_dom_t

@[to_additive addProjection_respects_measure_apply]
lemma IsFundamentalDomain.projection_respects_measure_apply {Î½ : Measure Î±}
    (Î¼ : Measure (Quotient Î±_mod_G)) [i : QuotientMeasureEqMeasurePreimage Î½ Î¼] {t : Set Î±}
    (fund_dom_t : IsFundamentalDomain G t Î½) {U : Set (Quotient Î±_mod_G)}
    (meas_U : MeasurableSet U) : Î¼ U = Î½ (Ï€ â»Â¹' U âˆ© t) := by
  rw [fund_dom_t.projection_respects_measure (Î¼ := Î¼), measure_map_restrict_apply Î½ t meas_U]

variable {Î½ : Measure Î±}

/-- Any two measures satisfying `QuotientMeasureEqMeasurePreimage` are equal. -/
@[to_additive]
lemma QuotientMeasureEqMeasurePreimage.unique
    [hasFun : HasFundamentalDomain G Î± Î½] (Î¼ Î¼' : Measure (Quotient Î±_mod_G))
    [QuotientMeasureEqMeasurePreimage Î½ Î¼] [QuotientMeasureEqMeasurePreimage Î½ Î¼'] :
    Î¼ = Î¼' := by
  obtain âŸ¨ğ“•, hğ“•âŸ© := hasFun.ExistsIsFundamentalDomain
  rw [hğ“•.projection_respects_measure (Î¼ := Î¼), hğ“•.projection_respects_measure (Î¼ := Î¼')]

/-- The quotient map to `Î± â§¸ G` is measure-preserving between the restriction of `volume` to a
  fundamental domain in `Î±` and a related measure satisfying `QuotientMeasureEqMeasurePreimage`. -/
@[to_additive IsAddFundamentalDomain.measurePreserving_add_quotient_mk]
theorem IsFundamentalDomain.measurePreserving_quotient_mk
    {ğ“• : Set Î±} (hğ“• : IsFundamentalDomain G ğ“• Î½)
    (Î¼ : Measure (Quotient Î±_mod_G)) [QuotientMeasureEqMeasurePreimage Î½ Î¼] :
    MeasurePreserving Ï€ (Î½.restrict ğ“•) Î¼ where
  measurable := measurable_quotient_mk' (s := Î±_mod_G)
  map_eq := by
    haveI : HasFundamentalDomain G Î± Î½ := âŸ¨ğ“•, hğ“•âŸ©
    rw [hğ“•.projection_respects_measure (Î¼ := Î¼)]

variable [SMulInvariantMeasure G Î± Î½] [Countable G] [MeasurableSpace G] [MeasurableSMul G Î±]

/-- Given a measure upstairs (i.e., on `Î±`), and a choice `s` of fundamental domain, there's always
an artificial way to generate a measure downstairs such that the pair satisfies the
`QuotientMeasureEqMeasurePreimage` typeclass. -/
@[to_additive]
lemma IsFundamentalDomain.quotientMeasureEqMeasurePreimage_quotientMeasure
    {s : Set Î±} (fund_dom_s : IsFundamentalDomain G s Î½) :
    QuotientMeasureEqMeasurePreimage Î½ ((Î½.restrict s).map Ï€) where
  projection_respects_measure' t fund_dom_t := by rw [fund_dom_s.quotientMeasure_eq _ fund_dom_t]

/-- One can prove `QuotientMeasureEqMeasurePreimage` by checking behavior with respect to a single
fundamental domain. -/
@[to_additive]
lemma IsFundamentalDomain.quotientMeasureEqMeasurePreimage {Î¼ : Measure (Quotient Î±_mod_G)}
    {s : Set Î±} (fund_dom_s : IsFundamentalDomain G s Î½) (h : Î¼ = (Î½.restrict s).map Ï€) :
    QuotientMeasureEqMeasurePreimage Î½ Î¼ := by
  simpa [h] using fund_dom_s.quotientMeasureEqMeasurePreimage_quotientMeasure


/-- If a fundamental domain has volume 0, then `QuotientMeasureEqMeasurePreimage` holds. -/
@[to_additive]
theorem IsFundamentalDomain.quotientMeasureEqMeasurePreimage_of_zero
    {s : Set Î±} (fund_dom_s : IsFundamentalDomain G s Î½)
    (vol_s : Î½ s = 0) :
    QuotientMeasureEqMeasurePreimage Î½ (0 : Measure (Quotient Î±_mod_G)) := by
  apply fund_dom_s.quotientMeasureEqMeasurePreimage
  ext U meas_U
  simp only [Measure.coe_zero, Pi.zero_apply]
  convert (measure_inter_null_of_null_right (h := vol_s) (Quotient.mk Î±_mod_G â»Â¹' U)).symm
  rw [measure_map_restrict_apply (meas_U := meas_U)]

/-- If a measure `Î¼` on a quotient satisfies `QuotientMeasureEqMeasurePreimage` with respect to a
sigma-finite measure `Î½`, then it is itself `SigmaFinite`. -/
@[to_additive]
lemma QuotientMeasureEqMeasurePreimage.sigmaFiniteQuotient
    [i : SigmaFinite Î½] [i' : HasFundamentalDomain G Î± Î½]
    (Î¼ : Measure (Quotient Î±_mod_G)) [QuotientMeasureEqMeasurePreimage Î½ Î¼] :
    SigmaFinite Î¼ := by
  rw [sigmaFinite_iff]
  obtain âŸ¨A, hA_meas, hA, hA'âŸ© := Measure.toFiniteSpanningSetsIn (h := i)
  simp only [mem_setOf_eq] at hA_meas
  refine âŸ¨âŸ¨fun n â†¦ Ï€ '' (A n), by simp, fun n â†¦ ?_, ?_âŸ©âŸ©
  Â· obtain âŸ¨s, fund_dom_sâŸ© := i'
    have : Ï€ â»Â¹' (Ï€ '' (A n)) = _ := MulAction.quotient_preimage_image_eq_union_mul (A n) (G := G)
    have measÏ€An : MeasurableSet (Ï€ '' A n) := by
      let _ : Setoid Î± := Î±_mod_G
      rw [measurableSet_quotient, Quotient.mk''_eq_mk, this]
      apply MeasurableSet.iUnion
      exact fun g â†¦ MeasurableSet.const_smul (hA_meas n) g
    rw [fund_dom_s.projection_respects_measure_apply (Î¼ := Î¼) measÏ€An, this, iUnion_inter]
    refine lt_of_le_of_lt ?_ (hA n)
    rw [fund_dom_s.measure_eq_tsum (A n)]
    exact measure_iUnion_le _
  Â· rw [â† image_iUnion, hA']
    refine image_univ_of_surjective (by convert Quotient.mk'_surjective)

/-- A measure `Î¼` on `Î± â§¸ G` satisfying `QuotientMeasureEqMeasurePreimage` and having finite
covolume is a finite measure. -/
@[to_additive]
theorem QuotientMeasureEqMeasurePreimage.isFiniteMeasure_quotient
    (Î¼ : Measure (Quotient Î±_mod_G)) [QuotientMeasureEqMeasurePreimage Î½ Î¼]
    [hasFun : HasFundamentalDomain G Î± Î½] (h : covolume G Î± Î½ â‰  âˆ) :
    IsFiniteMeasure Î¼ := by
  obtain âŸ¨ğ“•, hğ“•âŸ© := hasFun.ExistsIsFundamentalDomain
  rw [hğ“•.projection_respects_measure (Î¼ := Î¼)]
  have : Fact (Î½ ğ“• < âˆ) := by
    apply Fact.mk
    convert Ne.lt_top h
    exact (hğ“•.covolume_eq_volume Î½).symm
  infer_instance

/-- A finite measure `Î¼` on `Î± â§¸ G` satisfying `QuotientMeasureEqMeasurePreimage` has finite
covolume. -/
@[to_additive]
theorem QuotientMeasureEqMeasurePreimage.covolume_ne_top
    (Î¼ : Measure (Quotient Î±_mod_G)) [QuotientMeasureEqMeasurePreimage Î½ Î¼] [IsFiniteMeasure Î¼] :
    covolume G Î± Î½ < âˆ := by
  by_cases hasFun : HasFundamentalDomain G Î± Î½
  Â· obtain âŸ¨ğ“•, hğ“•âŸ© := hasFun.ExistsIsFundamentalDomain
    have H : Î¼ univ < âˆ := IsFiniteMeasure.measure_univ_lt_top
    rw [hğ“•.projection_respects_measure_apply (Î¼ := Î¼) MeasurableSet.univ] at H
    simpa [hğ“•.covolume_eq_volume Î½] using H
  Â· simp [covolume, hasFun]

end QuotientMeasureEqMeasurePreimage

section QuotientMeasureEqMeasurePreimage


variable [Group G] [MulAction G Î±] [MeasureSpace Î±] [Countable G] [MeasurableSpace G]
  [SMulInvariantMeasure G Î± volume] [MeasurableSMul G Î±]

local notation "Î±_mod_G" => MulAction.orbitRel G Î±

local notation "Ï€" => @Quotient.mk _ Î±_mod_G

/-- If a measure `Î¼` on a quotient satisfies `QuotientVolumeEqVolumePreimage` with respect to a
sigma-finite measure, then it is itself `SigmaFinite`. -/
@[to_additive MeasureTheory.instSigmaFiniteAddQuotientOrbitRelInstMeasurableSpaceToMeasurableSpace]
instance [SigmaFinite (volume : Measure Î±)] [HasFundamentalDomain G Î±]
    (Î¼ : Measure (Quotient Î±_mod_G)) [QuotientMeasureEqMeasurePreimage volume Î¼] :
    SigmaFinite Î¼ :=
  QuotientMeasureEqMeasurePreimage.sigmaFiniteQuotient (Î½ := (volume : Measure Î±)) (Î¼ := Î¼)

end QuotientMeasureEqMeasurePreimage

end MeasureTheory
