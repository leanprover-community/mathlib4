/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.MeasureTheory.Group.MeasurableEquiv
import Mathlib.MeasureTheory.Measure.Regular
import Mathlib.Dynamics.Ergodic.MeasurePreserving
import Mathlib.Dynamics.Minimal

#align_import measure_theory.group.action from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Measures invariant under group actions

A measure `Î¼ : Measure Î±` is said to be *invariant* under an action of a group `G` if scalar
multiplication by `c : G` is a measure preserving map for all `c`. In this file we define a
typeclass for measures invariant under action of an (additive or multiplicative) group and prove
some basic properties of such measures.
-/


open ENNReal NNReal Pointwise Topology MeasureTheory MeasureTheory.Measure Set Function

namespace MeasureTheory

universe u v w

variable {G : Type u} {M : Type v} {Î± : Type w} {s : Set Î±}

/-- A measure `Î¼ : Measure Î±` is invariant under an additive action of `M` on `Î±` if for any
measurable set `s : Set Î±` and `c : M`, the measure of its preimage under `fun x => c +áµ¥ x` is equal
to the measure of `s`. -/
class VAddInvariantMeasure (M Î± : Type*) [VAdd M Î±] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) :
  Prop where
  measure_preimage_vadd : âˆ€ (c : M) â¦ƒs : Set Î±â¦„, MeasurableSet s â†’ Î¼ ((fun x => c +áµ¥ x) â»Â¹' s) = Î¼ s
#align measure_theory.vadd_invariant_measure MeasureTheory.VAddInvariantMeasure
#align measure_theory.vadd_invariant_measure.measure_preimage_vadd MeasureTheory.VAddInvariantMeasure.measure_preimage_vadd

/-- A measure `Î¼ : Measure Î±` is invariant under a multiplicative action of `M` on `Î±` if for any
measurable set `s : Set Î±` and `c : M`, the measure of its preimage under `fun x => c â€¢ x` is equal
to the measure of `s`. -/
@[to_additive]
class SMulInvariantMeasure (M Î± : Type*) [SMul M Î±] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) :
  Prop where
  measure_preimage_smul : âˆ€ (c : M) â¦ƒs : Set Î±â¦„, MeasurableSet s â†’ Î¼ ((fun x => c â€¢ x) â»Â¹' s) = Î¼ s
#align measure_theory.smul_invariant_measure MeasureTheory.SMulInvariantMeasure
#align measure_theory.smul_invariant_measure.measure_preimage_smul MeasureTheory.SMulInvariantMeasure.measure_preimage_smul

namespace SMulInvariantMeasure

@[to_additive]
instance zero [MeasurableSpace Î±] [SMul M Î±] : SMulInvariantMeasure M Î± (0 : Measure Î±) :=
  âŸ¨fun _ _ _ => rflâŸ©
#align measure_theory.smul_invariant_measure.zero MeasureTheory.SMulInvariantMeasure.zero
#align measure_theory.vadd_invariant_measure.zero MeasureTheory.VAddInvariantMeasure.zero

variable [SMul M Î±] {m : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±}

@[to_additive]
instance add [SMulInvariantMeasure M Î± Î¼] [SMulInvariantMeasure M Î± Î½] :
    SMulInvariantMeasure M Î± (Î¼ + Î½) :=
  âŸ¨fun c _s hs =>
    show _ + _ = _ + _ from
      congr_argâ‚‚ (Â· + Â·) (measure_preimage_smul c hs) (measure_preimage_smul c hs)âŸ©
#align measure_theory.smul_invariant_measure.add MeasureTheory.SMulInvariantMeasure.add
#align measure_theory.vadd_invariant_measure.add MeasureTheory.VAddInvariantMeasure.add

@[to_additive]
instance smul [SMulInvariantMeasure M Î± Î¼] (c : â„â‰¥0âˆ) : SMulInvariantMeasure M Î± (c â€¢ Î¼) :=
  âŸ¨fun a _s hs => show c â€¢ _ = c â€¢ _ from congr_arg (c â€¢ Â·) (measure_preimage_smul a hs)âŸ©
#align measure_theory.smul_invariant_measure.smul MeasureTheory.SMulInvariantMeasure.smul
#align measure_theory.vadd_invariant_measure.vadd MeasureTheory.VAddInvariantMeasure.vadd

@[to_additive]
instance smul_nnreal [SMulInvariantMeasure M Î± Î¼] (c : â„â‰¥0) : SMulInvariantMeasure M Î± (c â€¢ Î¼) :=
  SMulInvariantMeasure.smul c
#align measure_theory.smul_invariant_measure.smul_nnreal MeasureTheory.SMulInvariantMeasure.smul_nnreal
#align measure_theory.vadd_invariant_measure.vadd_nnreal MeasureTheory.VAddInvariantMeasure.vadd_nnreal

end SMulInvariantMeasure

section MeasurableSMul

variable {m : MeasurableSpace Î±} [MeasurableSpace M] [SMul M Î±] [MeasurableSMul M Î±] (c : M)
  (Î¼ : Measure Î±) [SMulInvariantMeasure M Î± Î¼]

@[to_additive (attr := simp)]
theorem measurePreserving_smul : MeasurePreserving (c â€¢ Â·) Î¼ Î¼ :=
  { measurable := measurable_const_smul c
    map_eq := by
      ext1 s hs
      -- âŠ¢ â†‘â†‘(map (fun x => c â€¢ x) Î¼) s = â†‘â†‘Î¼ s
      rw [map_apply (measurable_const_smul c) hs]
      -- âŠ¢ â†‘â†‘Î¼ ((fun x x_1 => x â€¢ x_1) c â»Â¹' s) = â†‘â†‘Î¼ s
      exact SMulInvariantMeasure.measure_preimage_smul c hs }
      -- ğŸ‰ no goals
#align measure_theory.measure_preserving_smul MeasureTheory.measurePreserving_smul
#align measure_theory.measure_preserving_vadd MeasureTheory.measurePreserving_vadd

@[to_additive (attr := simp)]
theorem map_smul : map (c â€¢ Â·) Î¼ = Î¼ :=
  (measurePreserving_smul c Î¼).map_eq
#align measure_theory.map_smul MeasureTheory.map_smul
#align measure_theory.map_vadd MeasureTheory.map_vadd

end MeasurableSMul

variable (G) {m : MeasurableSpace Î±} [Group G] [MulAction G Î±] [MeasurableSpace G]
  [MeasurableSMul G Î±] (c : G) (Î¼ : Measure Î±)

/-- Equivalent definitions of a measure invariant under a multiplicative action of a group.

- 0: `SMulInvariantMeasure G Î± Î¼`;

- 1: for every `c : G` and a measurable set `s`, the measure of the preimage of `s` under scalar
     multiplication by `c` is equal to the measure of `s`;

- 2: for every `c : G` and a measurable set `s`, the measure of the image `c â€¢ s` of `s` under
     scalar multiplication by `c` is equal to the measure of `s`;

- 3, 4: properties 2, 3 for any set, including non-measurable ones;

- 5: for any `c : G`, scalar multiplication by `c` maps `Î¼` to `Î¼`;

- 6: for any `c : G`, scalar multiplication by `c` is a measure preserving map. -/
@[to_additive]
theorem smulInvariantMeasure_tfae :
    List.TFAE
      [SMulInvariantMeasure G Î± Î¼,
        âˆ€ (c : G) (s), MeasurableSet s â†’ Î¼ ((c â€¢ Â·) â»Â¹' s) = Î¼ s,
        âˆ€ (c : G) (s), MeasurableSet s â†’ Î¼ (c â€¢ s) = Î¼ s,
        âˆ€ (c : G) (s), Î¼ ((c â€¢ Â·) â»Â¹' s) = Î¼ s,
        âˆ€ (c : G) (s), Î¼ (c â€¢ s) = Î¼ s,
        âˆ€ c : G, Measure.map (c â€¢ Â·) Î¼ = Î¼,
        âˆ€ c : G, MeasurePreserving (c â€¢ Â·) Î¼ Î¼] := by
  tfae_have 1 â†” 2
  -- âŠ¢ SMulInvariantMeasure G Î± Î¼ â†” âˆ€ (c : G) (s : Set Î±), MeasurableSet s â†’ â†‘â†‘Î¼ (( â€¦
  Â· exact âŸ¨fun h => h.1, fun h => âŸ¨hâŸ©âŸ©
    -- ğŸ‰ no goals
  tfae_have 1 â†’ 6
  -- âŠ¢ SMulInvariantMeasure G Î± Î¼ â†’ âˆ€ (c : G), map (fun x => c â€¢ x) Î¼ = Î¼
  Â· intro h c
    -- âŠ¢ map (fun x => c â€¢ x) Î¼ = Î¼
    exact (measurePreserving_smul c Î¼).map_eq
    -- ğŸ‰ no goals
  tfae_have 6 â†’ 7
  -- âŠ¢ (âˆ€ (c : G), map (fun x => c â€¢ x) Î¼ = Î¼) â†’ âˆ€ (c : G), MeasurePreserving fun x â€¦
  Â· exact fun H c => âŸ¨measurable_const_smul c, H câŸ©
    -- ğŸ‰ no goals
  tfae_have 7 â†’ 4
  -- âŠ¢ (âˆ€ (c : G), MeasurePreserving fun x => c â€¢ x) â†’ âˆ€ (c : G) (s : Set Î±), â†‘â†‘Î¼ ( â€¦
  Â· exact fun H c => (H c).measure_preimage_emb (measurableEmbedding_const_smul c)
    -- ğŸ‰ no goals
  tfae_have 4 â†’ 5
  -- âŠ¢ (âˆ€ (c : G) (s : Set Î±), â†‘â†‘Î¼ ((fun x => c â€¢ x) â»Â¹' s) = â†‘â†‘Î¼ s) â†’ âˆ€ (c : G) (s â€¦
  Â· exact fun H c s => by
      rw [â† preimage_smul_inv]
      apply H
  tfae_have 5 â†’ 3
  -- âŠ¢ (âˆ€ (c : G) (s : Set Î±), â†‘â†‘Î¼ (c â€¢ s) = â†‘â†‘Î¼ s) â†’ âˆ€ (c : G) (s : Set Î±), Measur â€¦
  Â· exact fun H c s _ => H c s
    -- ğŸ‰ no goals
  tfae_have 3 â†’ 2
  -- âŠ¢ (âˆ€ (c : G) (s : Set Î±), MeasurableSet s â†’ â†‘â†‘Î¼ (c â€¢ s) = â†‘â†‘Î¼ s) â†’ âˆ€ (c : G) ( â€¦
  Â· intro H c s hs
    -- âŠ¢ â†‘â†‘Î¼ ((fun x => c â€¢ x) â»Â¹' s) = â†‘â†‘Î¼ s
    rw [preimage_smul]
    -- âŠ¢ â†‘â†‘Î¼ (câ»Â¹ â€¢ s) = â†‘â†‘Î¼ s
    exact H câ»Â¹ s hs
    -- ğŸ‰ no goals
  tfae_finish
  -- ğŸ‰ no goals
#align measure_theory.smul_invariant_measure_tfae MeasureTheory.smulInvariantMeasure_tfae
#align measure_theory.vadd_invariant_measure_tfae MeasureTheory.vaddInvariantMeasure_tfae

/-- Equivalent definitions of a measure invariant under an additive action of a group.

- 0: `VAddInvariantMeasure G Î± Î¼`;

- 1: for every `c : G` and a measurable set `s`, the measure of the preimage of `s` under
     vector addition `(c +áµ¥ Â·)` is equal to the measure of `s`;

- 2: for every `c : G` and a measurable set `s`, the measure of the image `c +áµ¥ s` of `s` under
     vector addition `(c +áµ¥ Â·)` is equal to the measure of `s`;

- 3, 4: properties 2, 3 for any set, including non-measurable ones;

- 5: for any `c : G`, vector addition of `c` maps `Î¼` to `Î¼`;

- 6: for any `c : G`, vector addition of `c` is a measure preserving map. -/
add_decl_doc vaddInvariantMeasure_tfae

variable {G}
variable [SMulInvariantMeasure G Î± Î¼]

@[to_additive (attr := simp)]
theorem measure_preimage_smul (s : Set Î±) : Î¼ ((c â€¢ Â·) â»Â¹' s) = Î¼ s :=
  ((smulInvariantMeasure_tfae G Î¼).out 0 3 rfl rfl).mp â€¹_â€º c s
#align measure_theory.measure_preimage_smul MeasureTheory.measure_preimage_smul
#align measure_theory.measure_preimage_vadd MeasureTheory.measure_preimage_vadd

@[to_additive (attr := simp)]
theorem measure_smul (s : Set Î±) : Î¼ (c â€¢ s) = Î¼ s :=
  ((smulInvariantMeasure_tfae G Î¼).out 0 4 rfl rfl).mp â€¹_â€º c s
#align measure_theory.measure_smul MeasureTheory.measure_smul
#align measure_theory.measure_vadd MeasureTheory.measure_vadd

variable {Î¼}

@[to_additive]
theorem NullMeasurableSet.smul {s} (hs : NullMeasurableSet s Î¼) (c : G) :
    NullMeasurableSet (c â€¢ s) Î¼ := by
  simpa only [â† preimage_smul_inv] using
    hs.preimage (measurePreserving_smul _ _).quasiMeasurePreserving
#align measure_theory.null_measurable_set.smul MeasureTheory.NullMeasurableSet.smul
#align measure_theory.null_measurable_set.vadd MeasureTheory.NullMeasurableSet.vadd

@[to_additive]
theorem measure_smul_null {s} (h : Î¼ s = 0) (c : G) : Î¼ (c â€¢ s) = 0 := by rwa [measure_smul]
                                                                          -- ğŸ‰ no goals
#align measure_theory.measure_smul_null MeasureTheory.measure_smul_null

section IsMinimal

variable (G)
variable [TopologicalSpace Î±] [ContinuousConstSMul G Î±] [MulAction.IsMinimal G Î±] {K U : Set Î±}

/-- If measure `Î¼` is invariant under a group action and is nonzero on a compact set `K`, then it is
positive on any nonempty open set. In case of a regular measure, one can assume `Î¼ â‰  0` instead of
`Î¼ K â‰  0`, see `MeasureTheory.measure_isOpen_pos_of_smulInvariant_of_ne_zero`. -/
@[to_additive]
theorem measure_isOpen_pos_of_smulInvariant_of_compact_ne_zero (hK : IsCompact K) (hÎ¼K : Î¼ K â‰  0)
    (hU : IsOpen U) (hne : U.Nonempty) : 0 < Î¼ U :=
  let âŸ¨t, htâŸ© := hK.exists_finite_cover_smul G hU hne
  pos_iff_ne_zero.2 fun hÎ¼U =>
    hÎ¼K <|
      measure_mono_null ht <|
        (measure_biUnion_null_iff t.countable_toSet).2 fun _ _ => by rwa [measure_smul]
                                                                     -- ğŸ‰ no goals
#align measure_theory.measure_is_open_pos_of_smul_invariant_of_compact_ne_zero MeasureTheory.measure_isOpen_pos_of_smulInvariant_of_compact_ne_zero
#align measure_theory.measure_is_open_pos_of_vadd_invariant_of_compact_ne_zero MeasureTheory.measure_isOpen_pos_of_vaddInvariant_of_compact_ne_zero

/-- If measure `Î¼` is invariant under an additive group action and is nonzero on a compact set `K`,
then it is positive on any nonempty open set. In case of a regular measure, one can assume `Î¼ â‰  0`
instead of `Î¼ K â‰  0`, see `MeasureTheory.measure_isOpen_pos_of_vaddInvariant_of_ne_zero`. -/
add_decl_doc measure_isOpen_pos_of_vaddInvariant_of_compact_ne_zero

@[to_additive]
theorem isLocallyFiniteMeasure_of_smulInvariant (hU : IsOpen U) (hne : U.Nonempty) (hÎ¼U : Î¼ U â‰  âˆ) :
    IsLocallyFiniteMeasure Î¼ :=
  âŸ¨fun x =>
    let âŸ¨g, hgâŸ© := hU.exists_smul_mem G x hne
    âŸ¨(Â· â€¢ Â·) g â»Â¹' U, (hU.preimage (continuous_id.const_smul _)).mem_nhds hg,
      Ne.lt_top <| by rwa [measure_preimage_smul]âŸ©âŸ©
                      -- ğŸ‰ no goals
#align measure_theory.is_locally_finite_measure_of_smul_invariant MeasureTheory.isLocallyFiniteMeasure_of_smulInvariant
#align measure_theory.is_locally_finite_measure_of_vadd_invariant MeasureTheory.isLocallyFiniteMeasure_of_vaddInvariant

variable [Measure.Regular Î¼]

@[to_additive]
theorem measure_isOpen_pos_of_smulInvariant_of_ne_zero (hÎ¼ : Î¼ â‰  0) (hU : IsOpen U)
    (hne : U.Nonempty) : 0 < Î¼ U :=
  let âŸ¨_K, hK, hÎ¼KâŸ© := Regular.exists_compact_not_null.mpr hÎ¼
  measure_isOpen_pos_of_smulInvariant_of_compact_ne_zero G hK hÎ¼K hU hne
#align measure_theory.measure_is_open_pos_of_smul_invariant_of_ne_zero MeasureTheory.measure_isOpen_pos_of_smulInvariant_of_ne_zero
#align measure_theory.measure_is_open_pos_of_vadd_invariant_of_ne_zero MeasureTheory.measure_isOpen_pos_of_vaddInvariant_of_ne_zero

@[to_additive]
theorem measure_pos_iff_nonempty_of_smulInvariant (hÎ¼ : Î¼ â‰  0) (hU : IsOpen U) :
    0 < Î¼ U â†” U.Nonempty :=
  âŸ¨fun h => nonempty_of_measure_ne_zero h.ne',
    measure_isOpen_pos_of_smulInvariant_of_ne_zero G hÎ¼ hUâŸ©
#align measure_theory.measure_pos_iff_nonempty_of_smul_invariant MeasureTheory.measure_pos_iff_nonempty_of_smulInvariant
#align measure_theory.measure_pos_iff_nonempty_of_vadd_invariant MeasureTheory.measure_pos_iff_nonempty_of_vaddInvariant

@[to_additive]
theorem measure_eq_zero_iff_eq_empty_of_smulInvariant (hÎ¼ : Î¼ â‰  0) (hU : IsOpen U) :
    Î¼ U = 0 â†” U = âˆ… := by
  rw [â† not_iff_not, â† Ne.def, â† pos_iff_ne_zero,
    measure_pos_iff_nonempty_of_smulInvariant G hÎ¼ hU, nonempty_iff_ne_empty]
#align measure_theory.measure_eq_zero_iff_eq_empty_of_smul_invariant MeasureTheory.measure_eq_zero_iff_eq_empty_of_smulInvariant
#align measure_theory.measure_eq_zero_iff_eq_empty_of_vadd_invariant MeasureTheory.measure_eq_zero_iff_eq_empty_of_vaddInvariant

end IsMinimal

theorem smul_ae_eq_self_of_mem_zpowers {x y : G} (hs : (x â€¢ s : Set Î±) =áµ[Î¼] s)
    (hy : y âˆˆ Subgroup.zpowers x) : (y â€¢ s : Set Î±) =áµ[Î¼] s := by
  obtain âŸ¨k, rflâŸ© := Subgroup.mem_zpowers_iff.mp hy
  -- âŠ¢ x ^ k â€¢ s =á¶ [ae Î¼] s
  let e : Î± â‰ƒ Î± := MulAction.toPermHom G Î± x
  -- âŠ¢ x ^ k â€¢ s =á¶ [ae Î¼] s
  have he : QuasiMeasurePreserving e Î¼ Î¼ := (measurePreserving_smul x Î¼).quasiMeasurePreserving
  -- âŠ¢ x ^ k â€¢ s =á¶ [ae Î¼] s
  have he' : QuasiMeasurePreserving e.symm Î¼ Î¼ :=
    (measurePreserving_smul xâ»Â¹ Î¼).quasiMeasurePreserving
  have h := he.image_zpow_ae_eq he' k hs
  -- âŠ¢ x ^ k â€¢ s =á¶ [ae Î¼] s
  simp only [â† MonoidHom.map_zpow] at h
  -- âŠ¢ x ^ k â€¢ s =á¶ [ae Î¼] s
  simpa only [MulAction.toPermHom_apply, MulAction.toPerm_apply, image_smul] using h
  -- ğŸ‰ no goals
#align measure_theory.smul_ae_eq_self_of_mem_zpowers MeasureTheory.smul_ae_eq_self_of_mem_zpowers

theorem vadd_ae_eq_self_of_mem_zmultiples {G : Type u} {Î± : Type w} {s : Set Î±}
    {m : MeasurableSpace Î±} [AddGroup G] [AddAction G Î±] [MeasurableSpace G] [MeasurableVAdd G Î±]
    {Î¼ : Measure Î±} [VAddInvariantMeasure G Î± Î¼] {x y : G}
    (hs : (x +áµ¥ s : Set Î±) =áµ[Î¼] s) (hy : y âˆˆ AddSubgroup.zmultiples x) :
    (y +áµ¥ s : Set Î±) =áµ[Î¼] s := by
  letI : MeasurableSpace (Multiplicative G) := (inferInstanceAs (MeasurableSpace G))
  -- âŠ¢ y +áµ¥ s =á¶ [ae Î¼] s
  letI : SMulInvariantMeasure (Multiplicative G) Î± Î¼ :=
    âŸ¨fun g => VAddInvariantMeasure.measure_preimage_vadd (Multiplicative.toAdd g)âŸ©
  letI : MeasurableSMul (Multiplicative G) Î± :=
    { measurable_const_smul := fun g => measurable_const_vadd (Multiplicative.toAdd g)
      measurable_smul_const := fun a =>
        @measurable_vadd_const (Multiplicative G) Î± (inferInstanceAs (VAdd G Î±)) _ _
          (inferInstanceAs (MeasurableVAdd G Î±)) a }
  exact @smul_ae_eq_self_of_mem_zpowers (Multiplicative G) Î± _ _ _ _ _ _ _ _ _ _ hs hy
  -- ğŸ‰ no goals
#align measure_theory.vadd_ae_eq_self_of_mem_zmultiples MeasureTheory.vadd_ae_eq_self_of_mem_zmultiples

attribute [to_additive existing vadd_ae_eq_self_of_mem_zmultiples] smul_ae_eq_self_of_mem_zpowers

@[to_additive]
theorem inv_smul_ae_eq_self {x : G} (hs : (x â€¢ s : Set Î±) =áµ[Î¼] s) : (xâ»Â¹ â€¢ s : Set Î±) =áµ[Î¼] s :=
  smul_ae_eq_self_of_mem_zpowers hs <| inv_mem (Subgroup.mem_zpowers _)

end MeasureTheory
