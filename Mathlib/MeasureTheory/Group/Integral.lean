/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Integral.Bochner.Basic
import Mathlib.MeasureTheory.Group.Measure

/-!
# Bochner Integration on Groups

We develop properties of integrals with a group as domain.
This file contains properties about integrability and Bochner integration.
-/

namespace MeasureTheory

open Measure TopologicalSpace

open scoped ENNReal

variable {ğ•œ M Î± G E F : Type*} [MeasurableSpace G]
variable [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup F]
variable {Î¼ : Measure G} {f : G â†’ E} {g : G}

section MeasurableInv

variable [Group G] [MeasurableInv G]

@[to_additive]
theorem Integrable.comp_inv [IsInvInvariant Î¼] {f : G â†’ F} (hf : Integrable f Î¼) :
    Integrable (fun t => f tâ»Â¹) Î¼ :=
  (hf.mono_measure (map_inv_eq_self Î¼).le).comp_measurable measurable_inv

@[to_additive]
theorem integral_inv_eq_self (f : G â†’ E) (Î¼ : Measure G) [IsInvInvariant Î¼] :
    âˆ« x, f xâ»Â¹ âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  have h : MeasurableEmbedding fun x : G => xâ»Â¹ := (MeasurableEquiv.inv G).measurableEmbedding
  rw [â† h.integral_map, map_inv_eq_self]

@[to_additive]
theorem IntegrableOn.comp_inv [IsInvInvariant Î¼] {f : G â†’ F} {s : Set G} (hf : IntegrableOn f s Î¼) :
    IntegrableOn (fun x => f xâ»Â¹) sâ»Â¹ Î¼ := by
  apply (integrable_map_equiv (MeasurableEquiv.inv G) f).mp
  have : sâ»Â¹ = MeasurableEquiv.inv G â»Â¹' s := by simp
  rw [this, â† MeasurableEquiv.restrict_map]
  simpa using hf

end MeasurableInv

section MeasurableInvOrder

variable [PartialOrder G] [CommGroup G] [IsOrderedMonoid G] [MeasurableInv G]
variable [IsInvInvariant Î¼]

@[to_additive]
theorem IntegrableOn.comp_inv_Iic {c : G} {f : G â†’ F} (hf : IntegrableOn f (Set.Ici câ»Â¹) Î¼) :
    IntegrableOn (fun x => f xâ»Â¹) (Set.Iic c) Î¼ := by
  simpa using hf.comp_inv

@[to_additive]
theorem IntegrableOn.comp_inv_Ici {c : G} {f : G â†’ F} (hf : IntegrableOn f (Set.Iic câ»Â¹) Î¼) :
    IntegrableOn (fun x => f xâ»Â¹) (Set.Ici c) Î¼ := by
  simpa using hf.comp_inv

@[to_additive]
theorem IntegrableOn.comp_inv_Iio {c : G} {f : G â†’ F} (hf : IntegrableOn f (Set.Ioi câ»Â¹) Î¼) :
    IntegrableOn (fun x => f xâ»Â¹) (Set.Iio c) Î¼ := by
  simpa using hf.comp_inv

@[to_additive]
theorem IntegrableOn.comp_inv_Ioi {c : G} {f : G â†’ F} (hf : IntegrableOn f (Set.Iio câ»Â¹) Î¼) :
    IntegrableOn (fun x => f xâ»Â¹) (Set.Ioi c) Î¼ := by
  simpa using hf.comp_inv

end MeasurableInvOrder

section MeasurableMul

variable [Group G] [MeasurableMul G]

/-- Translating a function by left-multiplication does not change its integral with respect to a
left-invariant measure. -/
@[to_additive
      /-- Translating a function by left-addition does not change its integral with respect to a
      left-invariant measure. -/]
theorem integral_mul_left_eq_self [IsMulLeftInvariant Î¼] (f : G â†’ E) (g : G) :
    (âˆ« x, f (g * x) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  have h_mul : MeasurableEmbedding fun x => g * x := (MeasurableEquiv.mulLeft g).measurableEmbedding
  rw [â† h_mul.integral_map, map_mul_left_eq_self]

/-- Translating a function by right-multiplication does not change its integral with respect to a
right-invariant measure. -/
@[to_additive
      /-- Translating a function by right-addition does not change its integral with respect to a
      right-invariant measure. -/]
theorem integral_mul_right_eq_self [IsMulRightInvariant Î¼] (f : G â†’ E) (g : G) :
    (âˆ« x, f (x * g) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  have h_mul : MeasurableEmbedding fun x => x * g :=
    (MeasurableEquiv.mulRight g).measurableEmbedding
  rw [â† h_mul.integral_map, map_mul_right_eq_self]

@[to_additive]
theorem integral_div_right_eq_self [IsMulRightInvariant Î¼] (f : G â†’ E) (g : G) :
    (âˆ« x, f (x / g) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  simp_rw [div_eq_mul_inv, integral_mul_right_eq_self f gâ»Â¹]

/-- If some left-translate of a function negates it, then the integral of the function with respect
to a left-invariant measure is 0. -/
@[to_additive
      /-- If some left-translate of a function negates it, then the integral of the function with
      respect to a left-invariant measure is 0. -/]
theorem integral_eq_zero_of_mul_left_eq_neg [IsMulLeftInvariant Î¼] (hf' : âˆ€ x, f (g * x) = -f x) :
    âˆ« x, f x âˆ‚Î¼ = 0 := by
  have := noZeroSMulDivisors_nat_iff_isAddTorsionFree.1 <| Nat.noZeroSMulDivisors â„ E
  simp_rw [â† self_eq_neg, â† integral_neg, â† hf', integral_mul_left_eq_self]

/-- If some right-translate of a function negates it, then the integral of the function with respect
to a right-invariant measure is 0. -/
@[to_additive
      /-- If some right-translate of a function negates it, then the integral of the function with
      respect to a right-invariant measure is 0. -/]
theorem integral_eq_zero_of_mul_right_eq_neg [IsMulRightInvariant Î¼] (hf' : âˆ€ x, f (x * g) = -f x) :
    âˆ« x, f x âˆ‚Î¼ = 0 := by
  have := noZeroSMulDivisors_nat_iff_isAddTorsionFree.1 <| Nat.noZeroSMulDivisors â„ E
  simp_rw [â† self_eq_neg, â† integral_neg, â† hf', integral_mul_right_eq_self]

@[to_additive]
theorem Integrable.comp_mul_left {f : G â†’ F} [IsMulLeftInvariant Î¼] (hf : Integrable f Î¼) (g : G) :
    Integrable (fun t => f (g * t)) Î¼ :=
  (hf.mono_measure (map_mul_left_eq_self Î¼ g).le).comp_measurable <| measurable_const_mul g

@[to_additive]
theorem Integrable.comp_mul_right {f : G â†’ F} [IsMulRightInvariant Î¼] (hf : Integrable f Î¼)
    (g : G) : Integrable (fun t => f (t * g)) Î¼ :=
  (hf.mono_measure (map_mul_right_eq_self Î¼ g).le).comp_measurable <| measurable_mul_const g

@[to_additive]
theorem Integrable.comp_div_right {f : G â†’ F} [IsMulRightInvariant Î¼] (hf : Integrable f Î¼)
    (g : G) : Integrable (fun t => f (t / g)) Î¼ := by
  simp_rw [div_eq_mul_inv]
  exact hf.comp_mul_right gâ»Â¹

variable [MeasurableInv G]

@[to_additive]
theorem Integrable.comp_div_left {f : G â†’ F} [IsInvInvariant Î¼] [IsMulLeftInvariant Î¼]
    (hf : Integrable f Î¼) (g : G) : Integrable (fun t => f (g / t)) Î¼ :=
  ((measurePreserving_div_left Î¼ g).integrable_comp hf.aestronglyMeasurable).mpr hf

@[to_additive]
theorem integrable_comp_div_left (f : G â†’ F) [IsInvInvariant Î¼] [IsMulLeftInvariant Î¼] (g : G) :
    Integrable (fun t => f (g / t)) Î¼ â†” Integrable f Î¼ := by
  refine âŸ¨fun h => ?_, fun h => h.comp_div_left gâŸ©
  convert h.comp_inv.comp_mul_left gâ»Â¹
  simp_rw [div_inv_eq_mul, mul_inv_cancel_left]

@[to_additive]
theorem integral_div_left_eq_self (f : G â†’ E) (Î¼ : Measure G) [IsInvInvariant Î¼]
    [IsMulLeftInvariant Î¼] (x' : G) : (âˆ« x, f (x' / x) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  simp_rw [div_eq_mul_inv, integral_inv_eq_self (fun x => f (x' * x)) Î¼,
    integral_mul_left_eq_self f x']

end MeasurableMul

section SMul

variable [Group G] [MeasurableSpace Î±] [MulAction G Î±] [MeasurableSMul G Î±]

@[to_additive]
theorem integral_smul_eq_self {Î¼ : Measure Î±} [SMulInvariantMeasure G Î± Î¼] (f : Î± â†’ E) {g : G} :
    (âˆ« x, f (g â€¢ x) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  have h : MeasurableEmbedding fun x : Î± => g â€¢ x := (MeasurableEquiv.smul g).measurableEmbedding
  rw [â† h.integral_map, MeasureTheory.map_smul]

end SMul

end MeasureTheory
