/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Integral.Bochner.Basic
import Mathlib.MeasureTheory.Group.Measure

/-!
# Bochner Integration on Groups

We develop properties of integrals with a group as domain.
This file contains properties about integrability and Bochner integration.
-/

namespace MeasureTheory

open Measure TopologicalSpace

open scoped ENNReal

variable {ùïú M Œ± G E F : Type*} [MeasurableSpace G]
variable [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedAddCommGroup F]
variable {Œº : Measure G} {f : G ‚Üí E} {g : G}

section MeasurableInv

variable [Group G] [MeasurableInv G]

@[to_additive]
theorem Integrable.comp_inv [IsInvInvariant Œº] {f : G ‚Üí F} (hf : Integrable f Œº) :
    Integrable (fun t => f t‚Åª¬π) Œº :=
  (hf.mono_measure (map_inv_eq_self Œº).le).comp_measurable measurable_inv

@[to_additive]
theorem integral_inv_eq_self (f : G ‚Üí E) (Œº : Measure G) [IsInvInvariant Œº] :
    ‚à´ x, f x‚Åª¬π ‚àÇŒº = ‚à´ x, f x ‚àÇŒº := by
  have h : MeasurableEmbedding fun x : G => x‚Åª¬π := (MeasurableEquiv.inv G).measurableEmbedding
  rw [‚Üê h.integral_map, map_inv_eq_self]

@[to_additive]
theorem IntegrableOn.comp_inv [IsInvInvariant Œº] {f : G ‚Üí F} {s : Set G} (hf : IntegrableOn f s Œº) :
    IntegrableOn (fun x => f x‚Åª¬π) s‚Åª¬π Œº := by
  apply (integrable_map_equiv (MeasurableEquiv.inv G) f).mp
  have : s‚Åª¬π = MeasurableEquiv.inv G ‚Åª¬π' s := by simp
  rw [this, ‚Üê MeasurableEquiv.restrict_map]
  simpa using hf

end MeasurableInv

section MeasurableInvOrder

variable [PartialOrder G] [CommGroup G] [IsOrderedMonoid G] [MeasurableInv G]
variable [IsInvInvariant Œº]

@[to_additive]
theorem IntegrableOn.comp_inv_Iic {c : G} {f : G ‚Üí F} (hf : IntegrableOn f (Set.Ici c‚Åª¬π) Œº) :
    IntegrableOn (fun x => f x‚Åª¬π) (Set.Iic c) Œº := by
  simpa using hf.comp_inv

@[to_additive]
theorem IntegrableOn.comp_inv_Ici {c : G} {f : G ‚Üí F} (hf : IntegrableOn f (Set.Iic c‚Åª¬π) Œº) :
    IntegrableOn (fun x => f x‚Åª¬π) (Set.Ici c) Œº := by
  simpa using hf.comp_inv

@[to_additive]
theorem IntegrableOn.comp_inv_Iio {c : G} {f : G ‚Üí F} (hf : IntegrableOn f (Set.Ioi c‚Åª¬π) Œº) :
    IntegrableOn (fun x => f x‚Åª¬π) (Set.Iio c) Œº := by
  simpa using hf.comp_inv

@[to_additive]
theorem IntegrableOn.comp_inv_Ioi {c : G} {f : G ‚Üí F} (hf : IntegrableOn f (Set.Iio c‚Åª¬π) Œº) :
    IntegrableOn (fun x => f x‚Åª¬π) (Set.Ioi c) Œº := by
  simpa using hf.comp_inv

end MeasurableInvOrder

section MeasurableMul

variable [Group G] [MeasurableMul G]

/-- Translating a function by left-multiplication does not change its integral with respect to a
left-invariant measure. -/
@[to_additive
      "Translating a function by left-addition does not change its integral with respect to a
      left-invariant measure."]
theorem integral_mul_left_eq_self [IsMulLeftInvariant Œº] (f : G ‚Üí E) (g : G) :
    (‚à´ x, f (g * x) ‚àÇŒº) = ‚à´ x, f x ‚àÇŒº := by
  have h_mul : MeasurableEmbedding fun x => g * x := (MeasurableEquiv.mulLeft g).measurableEmbedding
  rw [‚Üê h_mul.integral_map, map_mul_left_eq_self]

/-- Translating a function by right-multiplication does not change its integral with respect to a
right-invariant measure. -/
@[to_additive
      "Translating a function by right-addition does not change its integral with respect to a
      right-invariant measure."]
theorem integral_mul_right_eq_self [IsMulRightInvariant Œº] (f : G ‚Üí E) (g : G) :
    (‚à´ x, f (x * g) ‚àÇŒº) = ‚à´ x, f x ‚àÇŒº := by
  have h_mul : MeasurableEmbedding fun x => x * g :=
    (MeasurableEquiv.mulRight g).measurableEmbedding
  rw [‚Üê h_mul.integral_map, map_mul_right_eq_self]

@[to_additive]
theorem integral_div_right_eq_self [IsMulRightInvariant Œº] (f : G ‚Üí E) (g : G) :
    (‚à´ x, f (x / g) ‚àÇŒº) = ‚à´ x, f x ‚àÇŒº := by
  simp_rw [div_eq_mul_inv, integral_mul_right_eq_self f g‚Åª¬π]

/-- If some left-translate of a function negates it, then the integral of the function with respect
to a left-invariant measure is 0. -/
@[to_additive
      "If some left-translate of a function negates it, then the integral of the function with
      respect to a left-invariant measure is 0."]
theorem integral_eq_zero_of_mul_left_eq_neg [IsMulLeftInvariant Œº] (hf' : ‚àÄ x, f (g * x) = -f x) :
    ‚à´ x, f x ‚àÇŒº = 0 := by
  simp_rw [‚Üê self_eq_neg ‚Ñù E, ‚Üê integral_neg, ‚Üê hf', integral_mul_left_eq_self]

/-- If some right-translate of a function negates it, then the integral of the function with respect
to a right-invariant measure is 0. -/
@[to_additive
      "If some right-translate of a function negates it, then the integral of the function with
      respect to a right-invariant measure is 0."]
theorem integral_eq_zero_of_mul_right_eq_neg [IsMulRightInvariant Œº] (hf' : ‚àÄ x, f (x * g) = -f x) :
    ‚à´ x, f x ‚àÇŒº = 0 := by
  simp_rw [‚Üê self_eq_neg ‚Ñù E, ‚Üê integral_neg, ‚Üê hf', integral_mul_right_eq_self]

@[to_additive]
theorem Integrable.comp_mul_left {f : G ‚Üí F} [IsMulLeftInvariant Œº] (hf : Integrable f Œº) (g : G) :
    Integrable (fun t => f (g * t)) Œº :=
  (hf.mono_measure (map_mul_left_eq_self Œº g).le).comp_measurable <| measurable_const_mul g

@[to_additive]
theorem Integrable.comp_mul_right {f : G ‚Üí F} [IsMulRightInvariant Œº] (hf : Integrable f Œº)
    (g : G) : Integrable (fun t => f (t * g)) Œº :=
  (hf.mono_measure (map_mul_right_eq_self Œº g).le).comp_measurable <| measurable_mul_const g

@[to_additive]
theorem Integrable.comp_div_right {f : G ‚Üí F} [IsMulRightInvariant Œº] (hf : Integrable f Œº)
    (g : G) : Integrable (fun t => f (t / g)) Œº := by
  simp_rw [div_eq_mul_inv]
  exact hf.comp_mul_right g‚Åª¬π

variable [MeasurableInv G]

@[to_additive]
theorem Integrable.comp_div_left {f : G ‚Üí F} [IsInvInvariant Œº] [IsMulLeftInvariant Œº]
    (hf : Integrable f Œº) (g : G) : Integrable (fun t => f (g / t)) Œº :=
  ((measurePreserving_div_left Œº g).integrable_comp hf.aestronglyMeasurable).mpr hf

@[to_additive]
theorem integrable_comp_div_left (f : G ‚Üí F) [IsInvInvariant Œº] [IsMulLeftInvariant Œº] (g : G) :
    Integrable (fun t => f (g / t)) Œº ‚Üî Integrable f Œº := by
  refine ‚ü®fun h => ?_, fun h => h.comp_div_left g‚ü©
  convert h.comp_inv.comp_mul_left g‚Åª¬π
  simp_rw [div_inv_eq_mul, mul_inv_cancel_left]

@[to_additive]
theorem integral_div_left_eq_self (f : G ‚Üí E) (Œº : Measure G) [IsInvInvariant Œº]
    [IsMulLeftInvariant Œº] (x' : G) : (‚à´ x, f (x' / x) ‚àÇŒº) = ‚à´ x, f x ‚àÇŒº := by
  simp_rw [div_eq_mul_inv, integral_inv_eq_self (fun x => f (x' * x)) Œº,
    integral_mul_left_eq_self f x']

end MeasurableMul

section SMul

variable [Group G] [MeasurableSpace Œ±] [MulAction G Œ±] [MeasurableSMul G Œ±]

@[to_additive]
theorem integral_smul_eq_self {Œº : Measure Œ±} [SMulInvariantMeasure G Œ± Œº] (f : Œ± ‚Üí E) {g : G} :
    (‚à´ x, f (g ‚Ä¢ x) ‚àÇŒº) = ‚à´ x, f x ‚àÇŒº := by
  have h : MeasurableEmbedding fun x : Œ± => g ‚Ä¢ x := (MeasurableEquiv.smul g).measurableEmbedding
  rw [‚Üê h.integral_map, MeasureTheory.map_smul]

end SMul

end MeasureTheory
