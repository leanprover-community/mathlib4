/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Algebra.Group.Pointwise.Set.Card
import Mathlib.MeasureTheory.Group.Action
import Mathlib.MeasureTheory.Measure.Prod
import Mathlib.Topology.Algebra.Module.Equiv
import Mathlib.Topology.ContinuousMap.CocompactMap
import Mathlib.Topology.Algebra.ContinuousMonoidHom

/-!
# Measures on Groups

We develop some properties of measures on (topological) groups

* We define properties on measures: measures that are left or right invariant w.r.t. multiplication.
* We define the measure `Œº.inv : A ‚Ü¶ Œº(A‚Åª¬π)` and show that it is right invariant iff
  `Œº` is left invariant.
* We define a class `IsHaarMeasure Œº`, requiring that the measure `Œº` is left-invariant, finite
  on compact sets, and positive on open sets.

We also give analogues of all these notions in the additive world.
-/


noncomputable section

open scoped NNReal ENNReal Pointwise Topology

open Inv Set Function MeasureTheory.Measure Filter

variable {G H : Type*} [MeasurableSpace G] [MeasurableSpace H]

namespace MeasureTheory

section Mul

variable [Mul G] {Œº : Measure G}

@[to_additive]
theorem map_mul_left_eq_self (Œº : Measure G) [IsMulLeftInvariant Œº] (g : G) :
    map (g * ¬∑) Œº = Œº :=
  IsMulLeftInvariant.map_mul_left_eq_self g

@[to_additive]
theorem map_mul_right_eq_self (Œº : Measure G) [IsMulRightInvariant Œº] (g : G) : map (¬∑ * g) Œº = Œº :=
  IsMulRightInvariant.map_mul_right_eq_self g

@[to_additive MeasureTheory.isAddLeftInvariant_smul]
instance isMulLeftInvariant_smul [IsMulLeftInvariant Œº] (c : ‚Ñù‚â•0‚àû) : IsMulLeftInvariant (c ‚Ä¢ Œº) :=
  ‚ü®fun g => by rw [Measure.map_smul, map_mul_left_eq_self]‚ü©

@[to_additive MeasureTheory.isAddRightInvariant_smul]
instance isMulRightInvariant_smul [IsMulRightInvariant Œº] (c : ‚Ñù‚â•0‚àû) :
    IsMulRightInvariant (c ‚Ä¢ Œº) :=
  ‚ü®fun g => by rw [Measure.map_smul, map_mul_right_eq_self]‚ü©

@[to_additive MeasureTheory.isAddLeftInvariant_smul_nnreal]
instance isMulLeftInvariant_smul_nnreal [IsMulLeftInvariant Œº] (c : ‚Ñù‚â•0) :
    IsMulLeftInvariant (c ‚Ä¢ Œº) :=
  MeasureTheory.isMulLeftInvariant_smul (c : ‚Ñù‚â•0‚àû)

@[to_additive MeasureTheory.isAddRightInvariant_smul_nnreal]
instance isMulRightInvariant_smul_nnreal [IsMulRightInvariant Œº] (c : ‚Ñù‚â•0) :
    IsMulRightInvariant (c ‚Ä¢ Œº) :=
  MeasureTheory.isMulRightInvariant_smul (c : ‚Ñù‚â•0‚àû)

section MeasurableMul

variable [MeasurableMul G]

@[to_additive]
theorem measurePreserving_mul_left (Œº : Measure G) [IsMulLeftInvariant Œº] (g : G) :
    MeasurePreserving (g * ¬∑) Œº Œº :=
  ‚ü®measurable_const_mul g, map_mul_left_eq_self Œº g‚ü©

@[to_additive]
theorem MeasurePreserving.mul_left (Œº : Measure G) [IsMulLeftInvariant Œº] (g : G) {X : Type*}
    [MeasurableSpace X] {Œº' : Measure X} {f : X ‚Üí G} (hf : MeasurePreserving f Œº' Œº) :
    MeasurePreserving (fun x => g * f x) Œº' Œº :=
  (measurePreserving_mul_left Œº g).comp hf

@[to_additive]
theorem measurePreserving_mul_right (Œº : Measure G) [IsMulRightInvariant Œº] (g : G) :
    MeasurePreserving (¬∑ * g) Œº Œº :=
  ‚ü®measurable_mul_const g, map_mul_right_eq_self Œº g‚ü©

@[to_additive]
theorem MeasurePreserving.mul_right (Œº : Measure G) [IsMulRightInvariant Œº] (g : G) {X : Type*}
    [MeasurableSpace X] {Œº' : Measure X} {f : X ‚Üí G} (hf : MeasurePreserving f Œº' Œº) :
    MeasurePreserving (fun x => f x * g) Œº' Œº :=
  (measurePreserving_mul_right Œº g).comp hf

@[to_additive]
instance Subgroup.smulInvariantMeasure {G Œ± : Type*} [Group G] [MulAction G Œ±] [MeasurableSpace Œ±]
    {Œº : Measure Œ±} [SMulInvariantMeasure G Œ± Œº] (H : Subgroup G) : SMulInvariantMeasure H Œ± Œº :=
  ‚ü®fun y s hs => by convert SMulInvariantMeasure.measure_preimage_smul (Œº := Œº) (y : G) hs‚ü©

/-- An alternative way to prove that `Œº` is left invariant under multiplication. -/
@[to_additive "An alternative way to prove that `Œº` is left invariant under addition."]
theorem forall_measure_preimage_mul_iff (Œº : Measure G) :
    (‚àÄ (g : G) (A : Set G), MeasurableSet A ‚Üí Œº ((fun h => g * h) ‚Åª¬π' A) = Œº A) ‚Üî
      IsMulLeftInvariant Œº := by
  trans ‚àÄ g, map (g * ¬∑) Œº = Œº
  ¬∑ simp_rw [Measure.ext_iff]
    refine forall_congr' fun g => forall_congr' fun A => forall_congr' fun hA => ?_
    rw [map_apply (measurable_const_mul g) hA]
  exact ‚ü®fun h => ‚ü®h‚ü©, fun h => h.1‚ü©

/-- An alternative way to prove that `Œº` is right invariant under multiplication. -/
@[to_additive "An alternative way to prove that `Œº` is right invariant under addition."]
theorem forall_measure_preimage_mul_right_iff (Œº : Measure G) :
    (‚àÄ (g : G) (A : Set G), MeasurableSet A ‚Üí Œº ((fun h => h * g) ‚Åª¬π' A) = Œº A) ‚Üî
      IsMulRightInvariant Œº := by
  trans ‚àÄ g, map (¬∑ * g) Œº = Œº
  ¬∑ simp_rw [Measure.ext_iff]
    refine forall_congr' fun g => forall_congr' fun A => forall_congr' fun hA => ?_
    rw [map_apply (measurable_mul_const g) hA]
  exact ‚ü®fun h => ‚ü®h‚ü©, fun h => h.1‚ü©

@[to_additive]
instance Measure.prod.instIsMulLeftInvariant [IsMulLeftInvariant Œº] [SFinite Œº] {H : Type*}
    [Mul H] {mH : MeasurableSpace H} {ŒΩ : Measure H} [MeasurableMul H] [IsMulLeftInvariant ŒΩ]
    [SFinite ŒΩ] : IsMulLeftInvariant (Œº.prod ŒΩ) := by
  constructor
  rintro ‚ü®g, h‚ü©
  change map (Prod.map (g * ¬∑) (h * ¬∑)) (Œº.prod ŒΩ) = Œº.prod ŒΩ
  rw [‚Üê map_prod_map _ _ (measurable_const_mul g) (measurable_const_mul h),
    map_mul_left_eq_self Œº g, map_mul_left_eq_self ŒΩ h]

@[to_additive]
instance Measure.prod.instIsMulRightInvariant [IsMulRightInvariant Œº] [SFinite Œº] {H : Type*}
    [Mul H] {mH : MeasurableSpace H} {ŒΩ : Measure H} [MeasurableMul H] [IsMulRightInvariant ŒΩ]
    [SFinite ŒΩ] : IsMulRightInvariant (Œº.prod ŒΩ) := by
  constructor
  rintro ‚ü®g, h‚ü©
  change map (Prod.map (¬∑ * g) (¬∑ * h)) (Œº.prod ŒΩ) = Œº.prod ŒΩ
  rw [‚Üê map_prod_map _ _ (measurable_mul_const g) (measurable_mul_const h),
    map_mul_right_eq_self Œº g, map_mul_right_eq_self ŒΩ h]

@[to_additive]
theorem isMulLeftInvariant_map {H : Type*} [MeasurableSpace H] [Mul H] [MeasurableMul H]
    [IsMulLeftInvariant Œº] (f : G ‚Üí‚Çô* H) (hf : Measurable f) (h_surj : Surjective f) :
    IsMulLeftInvariant (Measure.map f Œº) := by
  refine ‚ü®fun h => ?_‚ü©
  rw [map_map (measurable_const_mul _) hf]
  obtain ‚ü®g, rfl‚ü© := h_surj h
  conv_rhs => rw [‚Üê map_mul_left_eq_self Œº g]
  rw [map_map hf (measurable_const_mul _)]
  congr 2
  ext y
  simp only [comp_apply, map_mul]

end MeasurableMul

end Mul

section Semigroup

variable [Semigroup G] [MeasurableMul G] {Œº : Measure G}

/-- The image of a left invariant measure under a left action is left invariant, assuming that
the action preserves multiplication. -/
@[to_additive "The image of a left invariant measure under a left additive action is left invariant,
assuming that the action preserves addition."]
theorem isMulLeftInvariant_map_smul
    {Œ±} [SMul Œ± G] [SMulCommClass Œ± G G] [MeasurableSpace Œ±] [MeasurableSMul Œ± G]
    [IsMulLeftInvariant Œº] (a : Œ±) :
    IsMulLeftInvariant (map (a ‚Ä¢ ¬∑ : G ‚Üí G) Œº) :=
  (forall_measure_preimage_mul_iff _).1 fun x _ hs =>
    (smulInvariantMeasure_map_smul Œº a).measure_preimage_smul x hs

/-- The image of a right invariant measure under a left action is right invariant, assuming that
the action preserves multiplication. -/
@[to_additive "The image of a right invariant measure under a left additive action is right
invariant, assuming that the action preserves addition."]
theorem isMulRightInvariant_map_smul
    {Œ±} [SMul Œ± G] [SMulCommClass Œ± G·µê·µí·µñ G] [MeasurableSpace Œ±] [MeasurableSMul Œ± G]
    [IsMulRightInvariant Œº] (a : Œ±) :
    IsMulRightInvariant (map (a ‚Ä¢ ¬∑ : G ‚Üí G) Œº) :=
  (forall_measure_preimage_mul_right_iff _).1 fun x _ hs =>
    (smulInvariantMeasure_map_smul Œº a).measure_preimage_smul (MulOpposite.op x) hs

/-- The image of a left invariant measure under right multiplication is left invariant. -/
@[to_additive isMulLeftInvariant_map_add_right
"The image of a left invariant measure under right addition is left invariant."]
instance isMulLeftInvariant_map_mul_right [IsMulLeftInvariant Œº] (g : G) :
    IsMulLeftInvariant (map (¬∑ * g) Œº) :=
  isMulLeftInvariant_map_smul (MulOpposite.op g)

/-- The image of a right invariant measure under left multiplication is right invariant. -/
@[to_additive isMulRightInvariant_map_add_left
"The image of a right invariant measure under left addition is right invariant."]
instance isMulRightInvariant_map_mul_left [IsMulRightInvariant Œº] (g : G) :
    IsMulRightInvariant (map (g * ¬∑) Œº) :=
  isMulRightInvariant_map_smul g

end Semigroup

section DivInvMonoid

variable [DivInvMonoid G]

@[to_additive]
theorem map_div_right_eq_self (Œº : Measure G) [IsMulRightInvariant Œº] (g : G) :
    map (¬∑ / g) Œº = Œº := by simp_rw [div_eq_mul_inv, map_mul_right_eq_self Œº g‚Åª¬π]

end DivInvMonoid

section Group

variable [Group G] [MeasurableMul G]

@[to_additive]
theorem measurePreserving_div_right (Œº : Measure G) [IsMulRightInvariant Œº] (g : G) :
    MeasurePreserving (¬∑ / g) Œº Œº := by simp_rw [div_eq_mul_inv, measurePreserving_mul_right Œº g‚Åª¬π]

/-- We shorten this from `measure_preimage_mul_left`, since left invariant is the preferred option
  for measures in this formalization. -/
@[to_additive (attr := simp)
"We shorten this from `measure_preimage_add_left`, since left invariant is the preferred option for
measures in this formalization."]
theorem measure_preimage_mul (Œº : Measure G) [IsMulLeftInvariant Œº] (g : G) (A : Set G) :
    Œº ((fun h => g * h) ‚Åª¬π' A) = Œº A :=
  calc
    Œº ((fun h => g * h) ‚Åª¬π' A) = map (fun h => g * h) Œº A :=
      ((MeasurableEquiv.mulLeft g).map_apply A).symm
    _ = Œº A := by rw [map_mul_left_eq_self Œº g]

@[to_additive (attr := simp)]
theorem measure_preimage_mul_right (Œº : Measure G) [IsMulRightInvariant Œº] (g : G) (A : Set G) :
    Œº ((fun h => h * g) ‚Åª¬π' A) = Œº A :=
  calc
    Œº ((fun h => h * g) ‚Åª¬π' A) = map (fun h => h * g) Œº A :=
      ((MeasurableEquiv.mulRight g).map_apply A).symm
    _ = Œº A := by rw [map_mul_right_eq_self Œº g]

@[to_additive]
theorem map_mul_left_ae (Œº : Measure G) [IsMulLeftInvariant Œº] (x : G) :
    Filter.map (fun h => x * h) (ae Œº) = ae Œº :=
  ((MeasurableEquiv.mulLeft x).map_ae Œº).trans <| congr_arg ae <| map_mul_left_eq_self Œº x

@[to_additive]
theorem map_mul_right_ae (Œº : Measure G) [IsMulRightInvariant Œº] (x : G) :
    Filter.map (fun h => h * x) (ae Œº) = ae Œº :=
  ((MeasurableEquiv.mulRight x).map_ae Œº).trans <| congr_arg ae <| map_mul_right_eq_self Œº x

@[to_additive]
theorem map_div_right_ae (Œº : Measure G) [IsMulRightInvariant Œº] (x : G) :
    Filter.map (fun t => t / x) (ae Œº) = ae Œº :=
  ((MeasurableEquiv.divRight x).map_ae Œº).trans <| congr_arg ae <| map_div_right_eq_self Œº x

@[to_additive]
theorem eventually_mul_left_iff (Œº : Measure G) [IsMulLeftInvariant Œº] (t : G) {p : G ‚Üí Prop} :
    (‚àÄ·µê x ‚àÇŒº, p (t * x)) ‚Üî ‚àÄ·µê x ‚àÇŒº, p x := by
  conv_rhs => rw [Filter.Eventually, ‚Üê map_mul_left_ae Œº t]
  rfl

@[to_additive]
theorem eventually_mul_right_iff (Œº : Measure G) [IsMulRightInvariant Œº] (t : G) {p : G ‚Üí Prop} :
    (‚àÄ·µê x ‚àÇŒº, p (x * t)) ‚Üî ‚àÄ·µê x ‚àÇŒº, p x := by
  conv_rhs => rw [Filter.Eventually, ‚Üê map_mul_right_ae Œº t]
  rfl

@[to_additive]
theorem eventually_div_right_iff (Œº : Measure G) [IsMulRightInvariant Œº] (t : G) {p : G ‚Üí Prop} :
    (‚àÄ·µê x ‚àÇŒº, p (x / t)) ‚Üî ‚àÄ·µê x ‚àÇŒº, p x := by
  conv_rhs => rw [Filter.Eventually, ‚Üê map_div_right_ae Œº t]
  rfl

end Group

namespace Measure

-- TODO: noncomputable has to be specified explicitly. https://github.com/leanprover-community/mathlib4/issues/1074 (item 8)

/-- The measure `A ‚Ü¶ Œº (A‚Åª¬π)`, where `A‚Åª¬π` is the pointwise inverse of `A`. -/
@[to_additive "The measure `A ‚Ü¶ Œº (- A)`, where `- A` is the pointwise negation of `A`."]
protected noncomputable def inv [Inv G] (Œº : Measure G) : Measure G :=
  Measure.map inv Œº

/-- A measure is invariant under negation if `- Œº = Œº`. Equivalently, this means that for all
measurable `A` we have `Œº (- A) = Œº A`, where `- A` is the pointwise negation of `A`. -/
class IsNegInvariant [Neg G] (Œº : Measure G) : Prop where
  neg_eq_self : Œº.neg = Œº

/-- A measure is invariant under inversion if `Œº‚Åª¬π = Œº`. Equivalently, this means that for all
measurable `A` we have `Œº (A‚Åª¬π) = Œº A`, where `A‚Åª¬π` is the pointwise inverse of `A`. -/
@[to_additive existing]
class IsInvInvariant [Inv G] (Œº : Measure G) : Prop where
  inv_eq_self : Œº.inv = Œº

section Inv

variable [Inv G]

@[to_additive]
theorem inv_def (Œº : Measure G) : Œº.inv = Measure.map inv Œº := rfl

@[to_additive (attr := simp)]
theorem inv_eq_self (Œº : Measure G) [IsInvInvariant Œº] : Œº.inv = Œº :=
  IsInvInvariant.inv_eq_self

@[to_additive (attr := simp)]
theorem map_inv_eq_self (Œº : Measure G) [IsInvInvariant Œº] : map Inv.inv Œº = Œº :=
  IsInvInvariant.inv_eq_self

variable [MeasurableInv G]

@[to_additive]
theorem measurePreserving_inv (Œº : Measure G) [IsInvInvariant Œº] : MeasurePreserving Inv.inv Œº Œº :=
  ‚ü®measurable_inv, map_inv_eq_self Œº‚ü©

@[to_additive]
instance inv.instSFinite (Œº : Measure G) [SFinite Œº] : SFinite Œº.inv := by
  rw [Measure.inv]; infer_instance

end Inv

section InvolutiveInv

variable [InvolutiveInv G] [MeasurableInv G]

@[to_additive (attr := simp)]
theorem inv_apply (Œº : Measure G) (s : Set G) : Œº.inv s = Œº s‚Åª¬π :=
  (MeasurableEquiv.inv G).map_apply s

@[to_additive (attr := simp)]
protected theorem inv_inv (Œº : Measure G) : Œº.inv.inv = Œº :=
  (MeasurableEquiv.inv G).map_symm_map

@[to_additive (attr := simp)]
theorem measure_inv (Œº : Measure G) [IsInvInvariant Œº] (A : Set G) : Œº A‚Åª¬π = Œº A := by
  rw [‚Üê inv_apply, inv_eq_self]

@[to_additive]
theorem measure_preimage_inv (Œº : Measure G) [IsInvInvariant Œº] (A : Set G) :
    Œº (Inv.inv ‚Åª¬π' A) = Œº A :=
  Œº.measure_inv A

@[to_additive]
instance inv.instSigmaFinite (Œº : Measure G) [SigmaFinite Œº] : SigmaFinite Œº.inv :=
  (MeasurableEquiv.inv G).sigmaFinite_map

end InvolutiveInv

section DivisionMonoid

variable [DivisionMonoid G] [MeasurableMul G] [MeasurableInv G] {Œº : Measure G}

@[to_additive]
instance inv.instIsMulRightInvariant [IsMulLeftInvariant Œº] : IsMulRightInvariant Œº.inv := by
  constructor
  intro g
  conv_rhs => rw [‚Üê map_mul_left_eq_self Œº g‚Åª¬π]
  simp_rw [Measure.inv, map_map (measurable_mul_const g) measurable_inv,
    map_map measurable_inv (measurable_const_mul g‚Åª¬π), Function.comp_def, mul_inv_rev, inv_inv]

@[to_additive]
instance inv.instIsMulLeftInvariant [IsMulRightInvariant Œº] : IsMulLeftInvariant Œº.inv := by
  constructor
  intro g
  conv_rhs => rw [‚Üê map_mul_right_eq_self Œº g‚Åª¬π]
  simp_rw [Measure.inv, map_map (measurable_const_mul g) measurable_inv,
    map_map measurable_inv (measurable_mul_const g‚Åª¬π), Function.comp_def, mul_inv_rev, inv_inv]

@[to_additive]
theorem measurePreserving_div_left (Œº : Measure G) [IsInvInvariant Œº] [IsMulLeftInvariant Œº]
    (g : G) : MeasurePreserving (fun t => g / t) Œº Œº := by
  simp_rw [div_eq_mul_inv]
  exact (measurePreserving_mul_left Œº g).comp (measurePreserving_inv Œº)

@[to_additive]
theorem map_div_left_eq_self (Œº : Measure G) [IsInvInvariant Œº] [IsMulLeftInvariant Œº] (g : G) :
    map (fun t => g / t) Œº = Œº :=
  (measurePreserving_div_left Œº g).map_eq

@[to_additive]
theorem measurePreserving_mul_right_inv (Œº : Measure G) [IsInvInvariant Œº] [IsMulLeftInvariant Œº]
    (g : G) : MeasurePreserving (fun t => (g * t)‚Åª¬π) Œº Œº :=
  (measurePreserving_inv Œº).comp <| measurePreserving_mul_left Œº g

@[to_additive]
theorem map_mul_right_inv_eq_self (Œº : Measure G) [IsInvInvariant Œº] [IsMulLeftInvariant Œº]
    (g : G) : map (fun t => (g * t)‚Åª¬π) Œº = Œº :=
  (measurePreserving_mul_right_inv Œº g).map_eq

end DivisionMonoid

section Group

variable [Group G] {Œº : Measure G}

section MeasurableMul

variable [MeasurableMul G]

@[to_additive]
instance : (count : Measure G).IsMulLeftInvariant where
  map_mul_left_eq_self g := by
    ext s hs
    rw [count_apply hs, map_apply (measurable_const_mul _) hs,
      count_apply (measurable_const_mul _ hs),
      encard_preimage_of_bijective (Group.mulLeft_bijective _)]

@[to_additive]
instance : (count : Measure G).IsMulRightInvariant where
  map_mul_right_eq_self g := by
    ext s hs
    rw [count_apply hs, map_apply (measurable_mul_const _) hs,
      count_apply (measurable_mul_const _ hs),
      encard_preimage_of_bijective (Group.mulRight_bijective _)]

@[to_additive]
protected theorem IsMulLeftInvariant.comap {H} [Group H] {mH : MeasurableSpace H} [MeasurableMul H]
    (Œº : Measure H) [IsMulLeftInvariant Œº] {f : G ‚Üí* H} (hf : MeasurableEmbedding f) :
    (Œº.comap f).IsMulLeftInvariant where
  map_mul_left_eq_self g := by
    ext s hs
    rw [map_apply (by fun_prop) hs]
    repeat rw [hf.comap_apply]
    have : f '' ((g * ¬∑) ‚Åª¬π' s) = (f g * ¬∑) ‚Åª¬π' (f '' s) := by
      ext
      constructor
      ¬∑ rintro ‚ü®y, hy, rfl‚ü©
        exact ‚ü®g * y, hy, by simp‚ü©
      ¬∑ intro ‚ü®y, yins, hy‚ü©
        exact ‚ü®g‚Åª¬π * y, by simp [yins], by simp [hy]‚ü©
    rw [this, ‚Üê map_apply (by fun_prop), IsMulLeftInvariant.map_mul_left_eq_self]
    exact hf.measurableSet_image.mpr hs

end MeasurableMul

variable [MeasurableInv G]

@[to_additive]
instance : (count : Measure G).IsInvInvariant where
  inv_eq_self := by ext s hs; rw [count_apply hs, inv_apply, count_apply hs.inv, encard_inv]

variable [MeasurableMul G]

@[to_additive]
theorem map_div_left_ae (Œº : Measure G) [IsMulLeftInvariant Œº] [IsInvInvariant Œº] (x : G) :
    Filter.map (fun t => x / t) (ae Œº) = ae Œº :=
  ((MeasurableEquiv.divLeft x).map_ae Œº).trans <| congr_arg ae <| map_div_left_eq_self Œº x

end Group

end Measure

section IsTopologicalGroup

variable [TopologicalSpace G] [BorelSpace G] {Œº : Measure G} [Group G]

@[to_additive]
instance Measure.IsFiniteMeasureOnCompacts.inv [ContinuousInv G] [IsFiniteMeasureOnCompacts Œº] :
    IsFiniteMeasureOnCompacts Œº.inv :=
  IsFiniteMeasureOnCompacts.map Œº (Homeomorph.inv G)

@[to_additive]
instance Measure.IsOpenPosMeasure.inv [ContinuousInv G] [IsOpenPosMeasure Œº] :
    IsOpenPosMeasure Œº.inv :=
  (Homeomorph.inv G).continuous.isOpenPosMeasure_map (Homeomorph.inv G).surjective

@[to_additive]
instance Measure.Regular.inv [ContinuousInv G] [Regular Œº] : Regular Œº.inv :=
  Regular.map (Homeomorph.inv G)

@[to_additive]
instance Measure.InnerRegular.inv [ContinuousInv G] [InnerRegular Œº] : InnerRegular Œº.inv :=
  InnerRegular.map (Homeomorph.inv G)

/-- The image of an inner regular measure under map of a left action is again inner regular. -/
@[to_additive
   "The image of a inner regular measure under map of a left additive action is again
    inner regular"]
instance innerRegular_map_smul {Œ±} [Monoid Œ±] [MulAction Œ± G] [ContinuousConstSMul Œ± G]
    [InnerRegular Œº] (a : Œ±) : InnerRegular (Measure.map (a ‚Ä¢ ¬∑ : G ‚Üí G) Œº) :=
  InnerRegular.map_of_continuous (continuous_const_smul a)

/-- The image of an inner regular measure under left multiplication is again inner regular. -/
@[to_additive "The image of an inner regular measure under left addition is again inner regular."]
instance innerRegular_map_mul_left [IsTopologicalGroup G] [InnerRegular Œº] (g : G) :
    InnerRegular (Measure.map (g * ¬∑) Œº) := InnerRegular.map_of_continuous (continuous_mul_left g)

/-- The image of an inner regular measure under right multiplication is again inner regular. -/
@[to_additive "The image of an inner regular measure under right addition is again inner regular."]
instance innerRegular_map_mul_right [IsTopologicalGroup G] [InnerRegular Œº] (g : G) :
    InnerRegular (Measure.map (¬∑ * g) Œº) := InnerRegular.map_of_continuous (continuous_mul_right g)

variable [IsTopologicalGroup G]

@[to_additive]
theorem regular_inv_iff : Œº.inv.Regular ‚Üî Œº.Regular :=
  Regular.map_iff (Homeomorph.inv G)

@[to_additive]
theorem innerRegular_inv_iff : Œº.inv.InnerRegular ‚Üî Œº.InnerRegular :=
  InnerRegular.map_iff (Homeomorph.inv G)

/-- Continuity of the measure of translates of a compact set: Given a compact set `k` in a
topological group, for `g` close enough to the origin, `Œº (g ‚Ä¢ k \ k)` is arbitrarily small. -/
@[to_additive]
lemma eventually_nhds_one_measure_smul_diff_lt [LocallyCompactSpace G]
    [IsFiniteMeasureOnCompacts Œº] [InnerRegularCompactLTTop Œº] {k : Set G}
    (hk : IsCompact k) (h'k : IsClosed k) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
    ‚àÄ·∂† g in ùìù (1 : G), Œº (g ‚Ä¢ k \ k) < Œµ := by
  obtain ‚ü®U, hUk, hU, hŒºUk‚ü© : ‚àÉ (U : Set G), k ‚äÜ U ‚àß IsOpen U ‚àß Œº U < Œº k + Œµ :=
    hk.exists_isOpen_lt_add hŒµ
  obtain ‚ü®V, hV1, hVkU‚ü© : ‚àÉ V ‚àà ùìù (1 : G), V * k ‚äÜ U := compact_open_separated_mul_left hk hU hUk
  filter_upwards [hV1] with g hg
  calc
    Œº (g ‚Ä¢ k \ k) ‚â§ Œº (U \ k) := by
      gcongr
      exact (smul_set_subset_smul hg).trans hVkU
    _ < Œµ := measure_diff_lt_of_lt_add h'k.nullMeasurableSet hUk hk.measure_lt_top.ne hŒºUk

/-- Continuity of the measure of translates of a compact set:
Given a closed compact set `k` in a topological group,
the measure of `g ‚Ä¢ k \ k` tends to zero as `g` tends to `1`. -/
@[to_additive]
lemma tendsto_measure_smul_diff_isCompact_isClosed [LocallyCompactSpace G]
    [IsFiniteMeasureOnCompacts Œº] [InnerRegularCompactLTTop Œº] {k : Set G}
    (hk : IsCompact k) (h'k : IsClosed k) :
    Tendsto (fun g : G ‚Ü¶ Œº (g ‚Ä¢ k \ k)) (ùìù 1) (ùìù 0) :=
  ENNReal.nhds_zero_basis.tendsto_right_iff.mpr <| fun _ h ‚Ü¶
    eventually_nhds_one_measure_smul_diff_lt hk h'k h.ne'

section IsMulLeftInvariant
variable [IsMulLeftInvariant Œº]

/-- If a left-invariant measure gives positive mass to a compact set, then it gives positive mass to
any open set. -/
@[to_additive
"If a left-invariant measure gives positive mass to a compact set, then it gives positive mass to
any open set."]
theorem isOpenPosMeasure_of_mulLeftInvariant_of_compact (K : Set G) (hK : IsCompact K)
    (h : Œº K ‚â† 0) : IsOpenPosMeasure Œº := by
  refine ‚ü®fun U hU hne => ?_‚ü©
  contrapose! h
  rw [‚Üê nonpos_iff_eq_zero]
  rw [‚Üê hU.interior_eq] at hne
  obtain ‚ü®t, hKt‚ü© : ‚àÉ t : Finset G, K ‚äÜ ‚ãÉ (g : G) (_ : g ‚àà t), (fun h : G => g * h) ‚Åª¬π' U :=
    compact_covered_by_mul_left_translates hK hne
  calc
    Œº K ‚â§ Œº (‚ãÉ (g : G) (_ : g ‚àà t), (fun h : G => g * h) ‚Åª¬π' U) := measure_mono hKt
    _ ‚â§ ‚àë g ‚àà t, Œº ((fun h : G => g * h) ‚Åª¬π' U) := measure_biUnion_finset_le _ _
    _ = 0 := by simp [measure_preimage_mul, h]

/-- A nonzero left-invariant regular measure gives positive mass to any open set. -/
@[to_additive "A nonzero left-invariant regular measure gives positive mass to any open set."]
instance (priority := 80) isOpenPosMeasure_of_mulLeftInvariant_of_regular [Regular Œº] [NeZero Œº] :
    IsOpenPosMeasure Œº :=
  let ‚ü®K, hK, h2K‚ü© := Regular.exists_isCompact_not_null.mpr (NeZero.ne Œº)
  isOpenPosMeasure_of_mulLeftInvariant_of_compact K hK h2K

/-- A nonzero left-invariant inner regular measure gives positive mass to any open set. -/
@[to_additive "A nonzero left-invariant inner regular measure gives positive mass to any open set."]
instance (priority := 80) isOpenPosMeasure_of_mulLeftInvariant_of_innerRegular
    [InnerRegular Œº] [NeZero Œº] :
    IsOpenPosMeasure Œº :=
  let ‚ü®K, hK, h2K‚ü© := InnerRegular.exists_isCompact_not_null.mpr (NeZero.ne Œº)
  isOpenPosMeasure_of_mulLeftInvariant_of_compact K hK h2K

@[to_additive]
theorem null_iff_of_isMulLeftInvariant [Regular Œº] {s : Set G} (hs : IsOpen s) :
    Œº s = 0 ‚Üî s = ‚àÖ ‚à® Œº = 0 := by
  rcases eq_zero_or_neZero Œº with rfl|hŒº
  ¬∑ simp
  ¬∑ simp only [or_false, hs.measure_eq_zero_iff Œº, NeZero.ne Œº]

@[to_additive]
theorem measure_ne_zero_iff_nonempty_of_isMulLeftInvariant [Regular Œº] (hŒº : Œº ‚â† 0) {s : Set G}
    (hs : IsOpen s) : Œº s ‚â† 0 ‚Üî s.Nonempty := by
  simpa [null_iff_of_isMulLeftInvariant (Œº := Œº) hs, hŒº] using nonempty_iff_ne_empty.symm

@[to_additive]
theorem measure_pos_iff_nonempty_of_isMulLeftInvariant [Regular Œº] (h3Œº : Œº ‚â† 0) {s : Set G}
    (hs : IsOpen s) : 0 < Œº s ‚Üî s.Nonempty :=
  pos_iff_ne_zero.trans <| measure_ne_zero_iff_nonempty_of_isMulLeftInvariant h3Œº hs

/-- If a left-invariant measure gives finite mass to a nonempty open set, then it gives finite mass
to any compact set. -/
@[to_additive
"If a left-invariant measure gives finite mass to a nonempty open set, then it gives finite mass to
any compact set."]
theorem measure_lt_top_of_isCompact_of_isMulLeftInvariant (U : Set G) (hU : IsOpen U)
    (h'U : U.Nonempty) (h : Œº U ‚â† ‚àû) {K : Set G} (hK : IsCompact K) : Œº K < ‚àû := by
  rw [‚Üê hU.interior_eq] at h'U
  obtain ‚ü®t, hKt‚ü© : ‚àÉ t : Finset G, K ‚äÜ ‚ãÉ g ‚àà t, (fun h : G => g * h) ‚Åª¬π' U :=
    compact_covered_by_mul_left_translates hK h'U
  exact (measure_mono hKt).trans_lt <| measure_biUnion_lt_top t.finite_toSet <| by simp [h.lt_top]

/-- If a left-invariant measure gives finite mass to a set with nonempty interior, then
it gives finite mass to any compact set. -/
@[to_additive
"If a left-invariant measure gives finite mass to a set with nonempty interior, then it gives finite
mass to any compact set."]
theorem measure_lt_top_of_isCompact_of_isMulLeftInvariant' {U : Set G}
    (hU : (interior U).Nonempty) (h : Œº U ‚â† ‚àû) {K : Set G} (hK : IsCompact K) : Œº K < ‚àû :=
  measure_lt_top_of_isCompact_of_isMulLeftInvariant (interior U) isOpen_interior hU
    ((measure_mono interior_subset).trans_lt (lt_top_iff_ne_top.2 h)).ne hK

/-- In a noncompact locally compact group, a left-invariant measure which is positive
on open sets has infinite mass. -/
@[to_additive (attr := simp)
"In a noncompact locally compact additive group, a left-invariant measure which is positive on open
sets has infinite mass."]
theorem measure_univ_of_isMulLeftInvariant [WeaklyLocallyCompactSpace G] [NoncompactSpace G]
    (Œº : Measure G) [IsOpenPosMeasure Œº] [Œº.IsMulLeftInvariant] : Œº univ = ‚àû := by
  /- Consider a closed compact set `K` with nonempty interior. For any compact set `L`, one may
    find `g = g (L)` such that `L` is disjoint from `g ‚Ä¢ K`. Iterating this, one finds
    infinitely many translates of `K` which are disjoint from each other. As they all have the
    same positive mass, it follows that the space has infinite measure. -/
  obtain ‚ü®K, K1, hK, Kclosed‚ü© : ‚àÉ K ‚àà ùìù (1 : G), IsCompact K ‚àß IsClosed K :=
    exists_mem_nhds_isCompact_isClosed 1
  have K_pos : 0 < Œº K := measure_pos_of_mem_nhds Œº K1
  have A : ‚àÄ L : Set G, IsCompact L ‚Üí ‚àÉ g : G, Disjoint L (g ‚Ä¢ K) := fun L hL =>
    exists_disjoint_smul_of_isCompact hL hK
  choose! g hg using A
  set L : ‚Ñï ‚Üí Set G := fun n => (fun T => T ‚à™ g T ‚Ä¢ K)^[n] K
  have Lcompact : ‚àÄ n, IsCompact (L n) := fun n ‚Ü¶ by
    induction n with
    | zero => exact hK
    | succ n IH =>
      simp_rw [L, iterate_succ']
      apply IsCompact.union IH (hK.smul (g (L n)))
  have Lclosed : ‚àÄ n, IsClosed (L n) := fun n ‚Ü¶ by
    induction n with
    | zero => exact Kclosed
    | succ n IH =>
      simp_rw [L, iterate_succ']
      apply IsClosed.union IH (Kclosed.smul (g (L n)))
  have M : ‚àÄ n, Œº (L n) = (n + 1 : ‚Ñï) * Œº K := fun n ‚Ü¶ by
    induction n with
    | zero => simp only [L, one_mul, Nat.cast_one, iterate_zero, id, Nat.zero_add]
    | succ n IH =>
      calc
        Œº (L (n + 1)) = Œº (L n) + Œº (g (L n) ‚Ä¢ K) := by
          simp_rw [L, iterate_succ']
          exact measure_union' (hg _ (Lcompact _)) (Lclosed _).measurableSet
        _ = (n + 1 + 1 : ‚Ñï) * Œº K := by
          simp only [IH, measure_smul, add_mul, Nat.cast_add, Nat.cast_one, one_mul]
  have N : Tendsto (fun n => Œº (L n)) atTop (ùìù (‚àû * Œº K)) := by
    simp_rw [M]
    apply ENNReal.Tendsto.mul_const _ (Or.inl ENNReal.top_ne_zero)
    exact ENNReal.tendsto_nat_nhds_top.comp (tendsto_add_atTop_nat _)
  simp only [ENNReal.top_mul', K_pos.ne', if_false] at N
  apply top_le_iff.1
  exact le_of_tendsto' N fun n => measure_mono (subset_univ _)

@[to_additive]
lemma _root_.MeasurableSet.mul_closure_one_eq {s : Set G} (hs : MeasurableSet s) :
    s * (closure {1} : Set G) = s := by
  induction s, hs using MeasurableSet.induction_on_open with
  | isOpen U hU => exact hU.mul_closure_one_eq
  | compl t _ iht => exact compl_mul_closure_one_eq_iff.2 iht
  | iUnion f _ _ ihf => simp_rw [iUnion_mul f, ihf]

@[to_additive (attr := simp)]
lemma measure_mul_closure_one (s : Set G) (Œº : Measure G) :
    Œº (s * (closure {1} : Set G)) = Œº s := by
  apply le_antisymm ?_ (measure_mono (subset_mul_closure_one s))
  conv_rhs => rw [measure_eq_iInf]
  simp only [le_iInf_iff]
  intro t kt t_meas
  apply measure_mono
  rw [‚Üê t_meas.mul_closure_one_eq]
  exact smul_subset_smul_right kt

end IsMulLeftInvariant

@[to_additive]
lemma innerRegularWRT_isCompact_isClosed_measure_ne_top_of_group [h : InnerRegularCompactLTTop Œº] :
    InnerRegularWRT Œº (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) (fun s ‚Ü¶ MeasurableSet s ‚àß Œº s ‚â† ‚àû) := by
  intro s ‚ü®s_meas, Œºs‚ü© r hr
  rcases h.innerRegular ‚ü®s_meas, Œºs‚ü© r hr with ‚ü®K, Ks, K_comp, hK‚ü©
  refine ‚ü®closure K, ?_, ‚ü®K_comp.closure, isClosed_closure‚ü©, ?_‚ü©
  ¬∑ exact IsCompact.closure_subset_measurableSet K_comp s_meas Ks
  ¬∑ rwa [K_comp.measure_closure]

end IsTopologicalGroup

section CommSemigroup

variable [CommSemigroup G]

/-- In an abelian group every left invariant measure is also right-invariant.
  We don't declare the converse as an instance, since that would loop type-class inference, and
  we use `IsMulLeftInvariant` as the default hypothesis in abelian groups. -/
@[to_additive IsAddLeftInvariant.isAddRightInvariant
"In an abelian additive group every left invariant measure is also right-invariant. We don't declare
the converse as an instance, since that would loop type-class inference, and we use
`IsAddLeftInvariant` as the default hypothesis in abelian groups."]
instance (priority := 100) IsMulLeftInvariant.isMulRightInvariant {Œº : Measure G}
    [IsMulLeftInvariant Œº] : IsMulRightInvariant Œº :=
  ‚ü®fun g => by simp_rw [mul_comm, map_mul_left_eq_self]‚ü©

end CommSemigroup

section Haar

namespace Measure

/-- A measure on an additive group is an additive Haar measure if it is left-invariant, and
gives finite mass to compact sets and positive mass to open sets.

Textbooks generally require an additional regularity assumption to ensure nice behavior on
arbitrary locally compact groups. Use `[IsAddHaarMeasure Œº] [Regular Œº]` or
`[IsAddHaarMeasure Œº] [InnerRegular Œº]` in these situations. Note that a Haar measure in our
sense is automatically regular and inner regular on second countable locally compact groups, as
checked just below this definition. -/
class IsAddHaarMeasure {G : Type*} [AddGroup G] [TopologicalSpace G] [MeasurableSpace G]
    (Œº : Measure G) : Prop
    extends IsFiniteMeasureOnCompacts Œº, IsAddLeftInvariant Œº, IsOpenPosMeasure Œº

/-- A measure on a group is a Haar measure if it is left-invariant, and gives finite mass to
compact sets and positive mass to open sets.

Textbooks generally require an additional regularity assumption to ensure nice behavior on
arbitrary locally compact groups. Use `[IsHaarMeasure Œº] [Regular Œº]` or
`[IsHaarMeasure Œº] [InnerRegular Œº]` in these situations. Note that a Haar measure in our
sense is automatically regular and inner regular on second countable locally compact groups, as
checked just below this definition. -/
@[to_additive existing]
class IsHaarMeasure {G : Type*} [Group G] [TopologicalSpace G] [MeasurableSpace G]
    (Œº : Measure G) : Prop
    extends IsFiniteMeasureOnCompacts Œº, IsMulLeftInvariant Œº, IsOpenPosMeasure Œº

variable [Group G] [TopologicalSpace G] (Œº : Measure G) [IsHaarMeasure Œº]

@[to_additive (attr := simp)]
theorem haar_singleton [ContinuousMul G] [BorelSpace G] (g : G) : Œº {g} = Œº {(1 : G)} := by
  convert measure_preimage_mul Œº g‚Åª¬π _
  simp only [mul_one, preimage_mul_left_singleton, inv_inv]

@[to_additive IsAddHaarMeasure.smul]
theorem IsHaarMeasure.smul {c : ‚Ñù‚â•0‚àû} (cpos : c ‚â† 0) (ctop : c ‚â† ‚àû) : IsHaarMeasure (c ‚Ä¢ Œº) :=
  { lt_top_of_isCompact := fun _K hK => ENNReal.mul_lt_top ctop.lt_top hK.measure_lt_top
    toIsOpenPosMeasure := isOpenPosMeasure_smul Œº cpos }

/-- If a left-invariant measure gives positive mass to some compact set with nonempty interior, then
it is a Haar measure. -/
@[to_additive
"If a left-invariant measure gives positive mass to some compact set with nonempty interior, then
it is an additive Haar measure."]
theorem isHaarMeasure_of_isCompact_nonempty_interior [IsTopologicalGroup G] [BorelSpace G]
    (Œº : Measure G) [IsMulLeftInvariant Œº] (K : Set G) (hK : IsCompact K)
    (h'K : (interior K).Nonempty) (h : Œº K ‚â† 0) (h' : Œº K ‚â† ‚àû) : IsHaarMeasure Œº :=
  { lt_top_of_isCompact := fun _L hL =>
      measure_lt_top_of_isCompact_of_isMulLeftInvariant' h'K h' hL
    toIsOpenPosMeasure := isOpenPosMeasure_of_mulLeftInvariant_of_compact K hK h }

/-- The image of a Haar measure under a continuous surjective proper group homomorphism is again
a Haar measure. See also `MulEquiv.isHaarMeasure_map` and `ContinuousMulEquiv.isHaarMeasure_map`. -/
@[to_additive
"The image of an additive Haar measure under a continuous surjective proper additive group
homomorphism is again an additive Haar measure. See also `AddEquiv.isAddHaarMeasure_map`,
`ContinuousAddEquiv.isAddHaarMeasure_map` and `ContinuousLinearEquiv.isAddHaarMeasure_map`."]
theorem isHaarMeasure_map [BorelSpace G] [ContinuousMul G] {H : Type*} [Group H]
    [TopologicalSpace H] [MeasurableSpace H] [BorelSpace H] [IsTopologicalGroup H]
    (f : G ‚Üí* H) (hf : Continuous f) (h_surj : Surjective f)
    (h_prop : Tendsto f (cocompact G) (cocompact H)) : IsHaarMeasure (Measure.map f Œº) :=
  { toIsMulLeftInvariant := isMulLeftInvariant_map f.toMulHom hf.measurable h_surj
    lt_top_of_isCompact := by
      intro K hK
      rw [‚Üê hK.measure_closure, map_apply hf.measurable isClosed_closure.measurableSet]
      set g : CocompactMap G H := ‚ü®‚ü®f, hf‚ü©, h_prop‚ü©
      exact IsCompact.measure_lt_top (g.isCompact_preimage_of_isClosed hK.closure isClosed_closure)
    toIsOpenPosMeasure := hf.isOpenPosMeasure_map h_surj }

protected theorem IsHaarMeasure.comap [BorelSpace G] [MeasurableMul G]
    [Group H] [TopologicalSpace H] [BorelSpace H] {mH : MeasurableMul H}
    (Œº : Measure H) [IsHaarMeasure Œº] {f : G ‚Üí* H} (hf : Topology.IsOpenEmbedding f) :
    (Œº.comap f).IsHaarMeasure where
  map_mul_left_eq_self := (IsMulLeftInvariant.comap Œº hf.measurableEmbedding).map_mul_left_eq_self
  lt_top_of_isCompact := (IsFiniteMeasureOnCompacts.comap' Œº hf.continuous
    hf.measurableEmbedding).lt_top_of_isCompact
  open_pos := (IsOpenPosMeasure.comap Œº hf).open_pos

/-- The image of a finite Haar measure under a continuous surjective group homomorphism is again
a Haar measure. See also `isHaarMeasure_map`. -/
@[to_additive
"The image of a finite additive Haar measure under a continuous surjective additive group
homomorphism is again an additive Haar measure. See also `isAddHaarMeasure_map`."]
theorem isHaarMeasure_map_of_isFiniteMeasure
    [BorelSpace G] [ContinuousMul G] {H : Type*} [Group H]
    [TopologicalSpace H] [MeasurableSpace H] [BorelSpace H] [ContinuousMul H]
    [IsFiniteMeasure Œº] (f : G ‚Üí* H) (hf : Continuous f) (h_surj : Surjective f) :
    IsHaarMeasure (Measure.map f Œº) where
  toIsMulLeftInvariant := isMulLeftInvariant_map f.toMulHom hf.measurable h_surj
  toIsOpenPosMeasure := hf.isOpenPosMeasure_map h_surj

/-- The image of a Haar measure under map of a left action is again a Haar measure. -/
@[to_additive
   "The image of a Haar measure under map of a left additive action is again a Haar measure"]
instance isHaarMeasure_map_smul {Œ±} [BorelSpace G] [IsTopologicalGroup G]
    [Group Œ±] [MulAction Œ± G] [SMulCommClass Œ± G G] [MeasurableSpace Œ±] [MeasurableSMul Œ± G]
    [ContinuousConstSMul Œ± G] (a : Œ±) : IsHaarMeasure (Measure.map (a ‚Ä¢ ¬∑ : G ‚Üí G) Œº) where
  toIsMulLeftInvariant := isMulLeftInvariant_map_smul _
  lt_top_of_isCompact K hK := by
    let F := (Homeomorph.smul a (Œ± := G)).toMeasurableEquiv
    change map F Œº K < ‚àû
    rw [F.map_apply K]
    exact IsCompact.measure_lt_top <| (Homeomorph.isCompact_preimage (Homeomorph.smul a)).2 hK
  toIsOpenPosMeasure :=
    (continuous_const_smul a).isOpenPosMeasure_map (MulAction.surjective a)

/-- The image of a Haar measure under right multiplication is again a Haar measure. -/
@[to_additive isHaarMeasure_map_add_right
  "The image of a Haar measure under right addition is again a Haar measure."]
instance isHaarMeasure_map_mul_right [BorelSpace G] [IsTopologicalGroup G] (g : G) :
    IsHaarMeasure (Measure.map (¬∑ * g) Œº) :=
  isHaarMeasure_map_smul Œº (MulOpposite.op g)

/-- A convenience wrapper for `MeasureTheory.Measure.isHaarMeasure_map`. -/
@[to_additive "A convenience wrapper for `MeasureTheory.Measure.isAddHaarMeasure_map`."]
nonrec theorem _root_.MulEquiv.isHaarMeasure_map [BorelSpace G] [ContinuousMul G] {H : Type*}
    [Group H] [TopologicalSpace H] [MeasurableSpace H] [BorelSpace H]
    [IsTopologicalGroup H] (e : G ‚âÉ* H) (he : Continuous e) (hesymm : Continuous e.symm) :
    IsHaarMeasure (Measure.map e Œº) :=
  let f : G ‚âÉ‚Çú H := .mk e
  -- We need to write `e.toMonoidHom` instead of just `e`, to avoid unification issues.
  isHaarMeasure_map Œº e.toMonoidHom he e.surjective f.isClosedEmbedding.tendsto_cocompact

/--
A convenience wrapper for MeasureTheory.Measure.isHaarMeasure_map.
-/
@[to_additive "A convenience wrapper for MeasureTheory.Measure.isAddHaarMeasure_map.
"]
instance _root_.ContinuousMulEquiv.isHaarMeasure_map [BorelSpace G] [IsTopologicalGroup G]
    {H : Type*} [Group H] [TopologicalSpace H] [MeasurableSpace H] [BorelSpace H]
    [IsTopologicalGroup H] (e : G ‚âÉ‚Çú* H) : (Œº.map e).IsHaarMeasure :=
  e.toMulEquiv.isHaarMeasure_map Œº e.continuous e.symm.continuous

/-- A convenience wrapper for MeasureTheory.Measure.isAddHaarMeasure_map`. -/
instance _root_.ContinuousLinearEquiv.isAddHaarMeasure_map
    {E F R S : Type*} [Semiring R] [Semiring S]
    [AddCommGroup E] [Module R E] [AddCommGroup F] [Module S F]
    [TopologicalSpace E] [IsTopologicalAddGroup E] [TopologicalSpace F]
    [IsTopologicalAddGroup F]
    {œÉ : R ‚Üí+* S} {œÉ' : S ‚Üí+* R} [RingHomInvPair œÉ œÉ'] [RingHomInvPair œÉ' œÉ]
    [MeasurableSpace E] [BorelSpace E] [MeasurableSpace F] [BorelSpace F]
    (L : E ‚âÉSL[œÉ] F) (Œº : Measure E) [IsAddHaarMeasure Œº] :
    IsAddHaarMeasure (Œº.map L) :=
  AddEquiv.isAddHaarMeasure_map _ (L : E ‚âÉ+ F) L.continuous L.symm.continuous

/-- A Haar measure on a œÉ-compact space is œÉ-finite.

See Note [lower instance priority] -/
@[to_additive
"A Haar measure on a œÉ-compact space is œÉ-finite.

See Note [lower instance priority]"]
instance (priority := 100) IsHaarMeasure.sigmaFinite [SigmaCompactSpace G] : SigmaFinite Œº :=
  ‚ü®‚ü®{   set := compactCovering G
        set_mem := fun _ => mem_univ _
        finite := fun n => IsCompact.measure_lt_top <| isCompact_compactCovering G n
        spanning := iUnion_compactCovering G }‚ü©‚ü©

@[to_additive]
instance prod.instIsHaarMeasure {G : Type*} [Group G] [TopologicalSpace G] {_ : MeasurableSpace G}
    {H : Type*} [Group H] [TopologicalSpace H] {_ : MeasurableSpace H} (Œº : Measure G)
    (ŒΩ : Measure H) [IsHaarMeasure Œº] [IsHaarMeasure ŒΩ] [SFinite Œº] [SFinite ŒΩ]
    [MeasurableMul G] [MeasurableMul H] : IsHaarMeasure (Œº.prod ŒΩ) where

/-- If the neutral element of a group is not isolated, then a Haar measure on this group has
no atoms.

The additive version of this instance applies in particular to show that an additive Haar
measure on a nontrivial finite-dimensional real vector space has no atom. -/
@[to_additive
"If the zero element of an additive group is not isolated, then an additive Haar measure on this
group has no atoms.

This applies in particular to show that an additive Haar measure on a nontrivial
finite-dimensional real vector space has no atom."]
instance (priority := 100) IsHaarMeasure.noAtoms [IsTopologicalGroup G] [BorelSpace G] [T1Space G]
    [WeaklyLocallyCompactSpace G] [(ùìù[‚â†] (1 : G)).NeBot] (Œº : Measure G) [Œº.IsHaarMeasure] :
    NoAtoms Œº := by
  cases eq_or_ne (Œº 1) 0 with
  | inl h => constructor; simpa
  | inr h =>
    obtain ‚ü®K, K_compact, K_nhds‚ü© : ‚àÉ K : Set G, IsCompact K ‚àß K ‚àà ùìù 1 := exists_compact_mem_nhds 1
    have K_inf : Set.Infinite K := infinite_of_mem_nhds (1 : G) K_nhds
    exact absurd (K_inf.meas_eq_top ‚ü®_, h, fun x _ ‚Ü¶ (haar_singleton _ _).ge‚ü©)
      K_compact.measure_lt_top.ne

instance IsAddHaarMeasure.domSMul {G A : Type*} [Group G] [AddCommGroup A] [DistribMulAction G A]
    [MeasurableSpace A] [TopologicalSpace A] [BorelSpace A] [IsTopologicalAddGroup A]
    [ContinuousConstSMul G A] {Œº : Measure A} [Œº.IsAddHaarMeasure] (g : G·µà·µê·µÉ) :
    (g ‚Ä¢ Œº).IsAddHaarMeasure :=
  (DistribMulAction.toAddEquiv _ (DomMulAct.mk.symm g‚Åª¬π)).isAddHaarMeasure_map _
    (continuous_const_smul _) (continuous_const_smul _)

end Measure

end Haar

end MeasureTheory
