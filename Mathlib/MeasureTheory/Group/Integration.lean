/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.MeasureTheory.Group.Measure

#align_import measure_theory.group.integration from "leanprover-community/mathlib"@"ec247d43814751ffceb33b758e8820df2372bf6f"

/-!
# Integration on Groups

We develop properties of integrals with a group as domain.
This file contains properties about integrability, Lebesgue integration and Bochner integration.
-/


namespace MeasureTheory

open Measure TopologicalSpace

open scoped ENNReal

variable {ğ•œ M Î± G E F : Type*} [MeasurableSpace G]

variable [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] [NormedAddCommGroup F]

variable {Î¼ : Measure G} {f : G â†’ E} {g : G}

section MeasurableInv

variable [Group G] [MeasurableInv G]

@[to_additive]
theorem Integrable.comp_inv [IsInvInvariant Î¼] {f : G â†’ F} (hf : Integrable f Î¼) :
    Integrable (fun t => f tâ»Â¹) Î¼ :=
  (hf.mono_measure (map_inv_eq_self Î¼).le).comp_measurable measurable_inv
#align measure_theory.integrable.comp_inv MeasureTheory.Integrable.comp_inv
#align measure_theory.integrable.comp_neg MeasureTheory.Integrable.comp_neg

@[to_additive]
theorem integral_inv_eq_self (f : G â†’ E) (Î¼ : Measure G) [IsInvInvariant Î¼] :
    âˆ« x, f xâ»Â¹ âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  have h : MeasurableEmbedding fun x : G => xâ»Â¹ := (MeasurableEquiv.inv G).measurableEmbedding
  rw [â† h.integral_map, map_inv_eq_self]
#align measure_theory.integral_inv_eq_self MeasureTheory.integral_inv_eq_self
#align measure_theory.integral_neg_eq_self MeasureTheory.integral_neg_eq_self

end MeasurableInv

section MeasurableMul

variable [Group G] [MeasurableMul G]

/-- Translating a function by left-multiplication does not change its `MeasureTheory.lintegral`
with respect to a left-invariant measure. -/
@[to_additive
      "Translating a function by left-addition does not change its `MeasureTheory.lintegral` with
      respect to a left-invariant measure."]
theorem lintegral_mul_left_eq_self [IsMulLeftInvariant Î¼] (f : G â†’ â„â‰¥0âˆ) (g : G) :
    (âˆ«â» x, f (g * x) âˆ‚Î¼) = âˆ«â» x, f x âˆ‚Î¼ := by
  convert (lintegral_map_equiv f <| MeasurableEquiv.mulLeft g).symm
  simp [map_mul_left_eq_self Î¼ g]
#align measure_theory.lintegral_mul_left_eq_self MeasureTheory.lintegral_mul_left_eq_self
#align measure_theory.lintegral_add_left_eq_self MeasureTheory.lintegral_add_left_eq_self

/-- Translating a function by right-multiplication does not change its `MeasureTheory.lintegral`
with respect to a right-invariant measure. -/
@[to_additive
      "Translating a function by right-addition does not change its `MeasureTheory.lintegral` with
      respect to a right-invariant measure."]
theorem lintegral_mul_right_eq_self [IsMulRightInvariant Î¼] (f : G â†’ â„â‰¥0âˆ) (g : G) :
    (âˆ«â» x, f (x * g) âˆ‚Î¼) = âˆ«â» x, f x âˆ‚Î¼ := by
  convert (lintegral_map_equiv f <| MeasurableEquiv.mulRight g).symm using 1
  simp [map_mul_right_eq_self Î¼ g]
#align measure_theory.lintegral_mul_right_eq_self MeasureTheory.lintegral_mul_right_eq_self
#align measure_theory.lintegral_add_right_eq_self MeasureTheory.lintegral_add_right_eq_self

@[to_additive] -- Porting note: was `@[simp]`
theorem lintegral_div_right_eq_self [IsMulRightInvariant Î¼] (f : G â†’ â„â‰¥0âˆ) (g : G) :
    (âˆ«â» x, f (x / g) âˆ‚Î¼) = âˆ«â» x, f x âˆ‚Î¼ := by
  simp_rw [div_eq_mul_inv]
  -- Porting note: was `simp_rw`
  rw [lintegral_mul_right_eq_self f gâ»Â¹]
#align measure_theory.lintegral_div_right_eq_self MeasureTheory.lintegral_div_right_eq_self
#align measure_theory.lintegral_sub_right_eq_self MeasureTheory.lintegral_sub_right_eq_self

/-- Translating a function by left-multiplication does not change its integral with respect to a
left-invariant measure. -/
@[to_additive
      "Translating a function by left-addition does not change its integral with respect to a
      left-invariant measure."] -- Porting note: was `@[simp]`
theorem integral_mul_left_eq_self [IsMulLeftInvariant Î¼] (f : G â†’ E) (g : G) :
    (âˆ« x, f (g * x) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  have h_mul : MeasurableEmbedding fun x => g * x := (MeasurableEquiv.mulLeft g).measurableEmbedding
  rw [â† h_mul.integral_map, map_mul_left_eq_self]
#align measure_theory.integral_mul_left_eq_self MeasureTheory.integral_mul_left_eq_self
#align measure_theory.integral_add_left_eq_self MeasureTheory.integral_add_left_eq_self

/-- Translating a function by right-multiplication does not change its integral with respect to a
right-invariant measure. -/
@[to_additive
      "Translating a function by right-addition does not change its integral with respect to a
      right-invariant measure."] -- Porting note: was `@[simp]`
theorem integral_mul_right_eq_self [IsMulRightInvariant Î¼] (f : G â†’ E) (g : G) :
    (âˆ« x, f (x * g) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  have h_mul : MeasurableEmbedding fun x => x * g :=
    (MeasurableEquiv.mulRight g).measurableEmbedding
  rw [â† h_mul.integral_map, map_mul_right_eq_self]
#align measure_theory.integral_mul_right_eq_self MeasureTheory.integral_mul_right_eq_self
#align measure_theory.integral_add_right_eq_self MeasureTheory.integral_add_right_eq_self

@[to_additive] -- Porting note: was `@[simp]`
theorem integral_div_right_eq_self [IsMulRightInvariant Î¼] (f : G â†’ E) (g : G) :
    (âˆ« x, f (x / g) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  simp_rw [div_eq_mul_inv]
  -- Porting note: was `simp_rw`
  rw [integral_mul_right_eq_self f gâ»Â¹]
#align measure_theory.integral_div_right_eq_self MeasureTheory.integral_div_right_eq_self
#align measure_theory.integral_sub_right_eq_self MeasureTheory.integral_sub_right_eq_self

/-- If some left-translate of a function negates it, then the integral of the function with respect
to a left-invariant measure is 0. -/
@[to_additive
      "If some left-translate of a function negates it, then the integral of the function with
      respect to a left-invariant measure is 0."]
theorem integral_eq_zero_of_mul_left_eq_neg [IsMulLeftInvariant Î¼] (hf' : âˆ€ x, f (g * x) = -f x) :
    âˆ« x, f x âˆ‚Î¼ = 0 := by
  simp_rw [â† self_eq_neg â„ E, â† integral_neg, â† hf', integral_mul_left_eq_self]
#align measure_theory.integral_eq_zero_of_mul_left_eq_neg MeasureTheory.integral_eq_zero_of_mul_left_eq_neg
#align measure_theory.integral_eq_zero_of_add_left_eq_neg MeasureTheory.integral_eq_zero_of_add_left_eq_neg

/-- If some right-translate of a function negates it, then the integral of the function with respect
to a right-invariant measure is 0. -/
@[to_additive
      "If some right-translate of a function negates it, then the integral of the function with
      respect to a right-invariant measure is 0."]
theorem integral_eq_zero_of_mul_right_eq_neg [IsMulRightInvariant Î¼] (hf' : âˆ€ x, f (x * g) = -f x) :
    âˆ« x, f x âˆ‚Î¼ = 0 := by
  simp_rw [â† self_eq_neg â„ E, â† integral_neg, â† hf', integral_mul_right_eq_self]
#align measure_theory.integral_eq_zero_of_mul_right_eq_neg MeasureTheory.integral_eq_zero_of_mul_right_eq_neg
#align measure_theory.integral_eq_zero_of_add_right_eq_neg MeasureTheory.integral_eq_zero_of_add_right_eq_neg

@[to_additive]
theorem Integrable.comp_mul_left {f : G â†’ F} [IsMulLeftInvariant Î¼] (hf : Integrable f Î¼) (g : G) :
    Integrable (fun t => f (g * t)) Î¼ :=
  (hf.mono_measure (map_mul_left_eq_self Î¼ g).le).comp_measurable <| measurable_const_mul g
#align measure_theory.integrable.comp_mul_left MeasureTheory.Integrable.comp_mul_left
#align measure_theory.integrable.comp_add_left MeasureTheory.Integrable.comp_add_left

@[to_additive]
theorem Integrable.comp_mul_right {f : G â†’ F} [IsMulRightInvariant Î¼] (hf : Integrable f Î¼)
    (g : G) : Integrable (fun t => f (t * g)) Î¼ :=
  (hf.mono_measure (map_mul_right_eq_self Î¼ g).le).comp_measurable <| measurable_mul_const g
#align measure_theory.integrable.comp_mul_right MeasureTheory.Integrable.comp_mul_right
#align measure_theory.integrable.comp_add_right MeasureTheory.Integrable.comp_add_right

@[to_additive]
theorem Integrable.comp_div_right {f : G â†’ F} [IsMulRightInvariant Î¼] (hf : Integrable f Î¼)
    (g : G) : Integrable (fun t => f (t / g)) Î¼ := by
  simp_rw [div_eq_mul_inv]
  exact hf.comp_mul_right gâ»Â¹
#align measure_theory.integrable.comp_div_right MeasureTheory.Integrable.comp_div_right
#align measure_theory.integrable.comp_sub_right MeasureTheory.Integrable.comp_sub_right

variable [MeasurableInv G]

@[to_additive]
theorem Integrable.comp_div_left {f : G â†’ F} [IsInvInvariant Î¼] [IsMulLeftInvariant Î¼]
    (hf : Integrable f Î¼) (g : G) : Integrable (fun t => f (g / t)) Î¼ :=
  ((measurePreserving_div_left Î¼ g).integrable_comp hf.aestronglyMeasurable).mpr hf
#align measure_theory.integrable.comp_div_left MeasureTheory.Integrable.comp_div_left
#align measure_theory.integrable.comp_sub_left MeasureTheory.Integrable.comp_sub_left

@[to_additive] -- Porting note: was `@[simp]`
theorem integrable_comp_div_left (f : G â†’ F) [IsInvInvariant Î¼] [IsMulLeftInvariant Î¼] (g : G) :
    Integrable (fun t => f (g / t)) Î¼ â†” Integrable f Î¼ := by
  refine' âŸ¨fun h => _, fun h => h.comp_div_left gâŸ©
  convert h.comp_inv.comp_mul_left gâ»Â¹
  simp_rw [div_inv_eq_mul, mul_inv_cancel_left]
#align measure_theory.integrable_comp_div_left MeasureTheory.integrable_comp_div_left
#align measure_theory.integrable_comp_sub_left MeasureTheory.integrable_comp_sub_left

@[to_additive] -- Porting note: was `@[simp]`
theorem integral_div_left_eq_self (f : G â†’ E) (Î¼ : Measure G) [IsInvInvariant Î¼]
    [IsMulLeftInvariant Î¼] (x' : G) : (âˆ« x, f (x' / x) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  simp_rw [div_eq_mul_inv]
  -- Porting note: was `simp_rw`
  rw [integral_inv_eq_self (fun x => f (x' * x)) Î¼, integral_mul_left_eq_self f x']
#align measure_theory.integral_div_left_eq_self MeasureTheory.integral_div_left_eq_self
#align measure_theory.integral_sub_left_eq_self MeasureTheory.integral_sub_left_eq_self

end MeasurableMul

section SMul

variable [Group G] [MeasurableSpace Î±] [MulAction G Î±] [MeasurableSMul G Î±]

@[to_additive] -- Porting note: was `@[simp]`
theorem integral_smul_eq_self {Î¼ : Measure Î±} [SMulInvariantMeasure G Î± Î¼] (f : Î± â†’ E) {g : G} :
    (âˆ« x, f (g â€¢ x) âˆ‚Î¼) = âˆ« x, f x âˆ‚Î¼ := by
  have h : MeasurableEmbedding fun x : Î± => g â€¢ x := (MeasurableEquiv.smul g).measurableEmbedding
  rw [â† h.integral_map, map_smul]
#align measure_theory.integral_smul_eq_self MeasureTheory.integral_smul_eq_self
#align measure_theory.integral_vadd_eq_self MeasureTheory.integral_vadd_eq_self

end SMul

section TopologicalGroup

variable [TopologicalSpace G] [Group G] [TopologicalGroup G] [BorelSpace G] [IsMulLeftInvariant Î¼]

/-- For nonzero regular left invariant measures, the integral of a continuous nonnegative function
  `f` is 0 iff `f` is 0. -/
@[to_additive
      "For nonzero regular left invariant measures, the integral of a continuous nonnegative
      function `f` is 0 iff `f` is 0."]
theorem lintegral_eq_zero_of_isMulLeftInvariant [Regular Î¼] (hÎ¼ : Î¼ â‰  0) {f : G â†’ â„â‰¥0âˆ}
    (hf : Continuous f) : âˆ«â» x, f x âˆ‚Î¼ = 0 â†” f = 0 := by
  haveI := isOpenPosMeasure_of_mulLeftInvariant_of_regular hÎ¼
  rw [lintegral_eq_zero_iff hf.measurable, hf.ae_eq_iff_eq Î¼ continuous_zero]
#align measure_theory.lintegral_eq_zero_of_is_mul_left_invariant MeasureTheory.lintegral_eq_zero_of_isMulLeftInvariant
#align measure_theory.lintegral_eq_zero_of_is_add_left_invariant MeasureTheory.lintegral_eq_zero_of_isAddLeftInvariant

end TopologicalGroup

end MeasureTheory
