/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.Typeclasses

/-!
# Method of exhaustion

If `Î¼, Î½` are two measures with `Î½` finite, then there exists a set `s` such that
`Î¼` is sigma-finite on `s`, and for all sets `t âŠ† sá¶œ`, either `Î½ t = 0` or `Î¼ t = âˆž`.

## Main definitions

* `MeasureTheory.Measure.sigmaFiniteSetWRT`: `Î¼.sigmaFiniteSetWRT Î½` is measurable set such that
  `Î¼.restrict (sigmaFiniteSetWRT Î¼ Î½)` is sigma-finite and `Î½ (sigmaFiniteSetWRT Î¼ Î½)` has maximal
  measure among such sets.

## Main statements

* `measure_eq_top_of_subset_compl_sigmaFiniteSetWRT`: for all measurable sets `s` in
  `(sigmaFiniteSetWRT Î¼ Î½)á¶œ`, if `Î½ s â‰  0` then `Î¼ s = âˆž`.

## References

* [P. R. Halmos, *Measure theory*, 17.3 and 30.11][halmos1950measure]

-/

open scoped NNReal ENNReal Topology

open Filter

namespace MeasureTheory

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} {s t : Set Î±}

/-- Let `C` be the supremum of `Î½ s` over all measurable sets `s` such that `Î¼.restrict s` is
sigma-finite. `C` is finite since `Î½` is a finite measure. Then there exists a measurable set `t`
with `Î¼.restrict t` sigma-finite such that `Î½ t â‰¥ C - 1/n`. -/
lemma exists_isSigmaFiniteSet_measure_ge (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    âˆƒ t, MeasurableSet t âˆ§ SigmaFinite (Î¼.restrict t)
      âˆ§ (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s) - 1/n â‰¤ Î½ t := by
  by_cases hC_lt : 1/n < â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s
  Â· have h_lt_top : â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s < âˆž := by
      refine (?_ : â¨† (s) (_ : MeasurableSet s)
        (_ : SigmaFinite (Î¼.restrict s)), Î½ s â‰¤ Î½ Set.univ).trans_lt (measure_lt_top _ _)
      refine iSup_le (fun s â†¦ ?_)
      exact iSup_le (fun _ â†¦ iSup_le (fun _ â†¦ measure_mono (Set.subset_univ s)))
    obtain âŸ¨t, htâŸ© := exists_lt_of_lt_ciSup
      (ENNReal.sub_lt_self h_lt_top.ne (ne_zero_of_lt hC_lt) (by simp) :
          (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s) - 1/n
        < â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s)
    have ht_meas : MeasurableSet t := by
      by_contra h_not_mem
      simp only [h_not_mem] at ht
      simp at ht
    have ht_mem : SigmaFinite (Î¼.restrict t) := by
      by_contra h_not_mem
      simp only [h_not_mem] at ht
      simp at ht
    refine âŸ¨t, ht_meas, ht_mem, ?_âŸ©
    simp only [ht_meas, ht_mem, iSup_true] at ht
    exact ht.le
  Â· refine âŸ¨âˆ…, MeasurableSet.empty, by rw [Measure.restrict_empty]; infer_instance, ?_âŸ©
    rw [tsub_eq_zero_of_le (not_lt.mp hC_lt)]
    exact zero_le'

/-- A measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)` is sigma finite and
for `C` the supremum of `Î½ s` over all measurable sigma-finite sets `s`,
`Î½ (Î¼.sigmaFiniteSetGE Î½ n) â‰¥ C - 1/n`. -/
def Measure.sigmaFiniteSetGE (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) : Set Î± :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose

lemma measurableSet_sigmaFiniteSetGE (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    MeasurableSet (Î¼.sigmaFiniteSetGE Î½ n) :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose_spec.1

instance [IsFiniteMeasure Î½] (n : â„•) : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)) :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose_spec.2.1

lemma measure_sigmaFiniteSetGE_le (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    Î½ (Î¼.sigmaFiniteSetGE Î½ n)
      â‰¤ â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s := by
  refine (le_iSup (f := fun s â†¦ _)
    (inferInstance : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)))).trans ?_
  exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : SigmaFinite (Î¼.restrict s)), Î½ s) (Î¼.sigmaFiniteSetGE Î½ n)
    (measurableSet_sigmaFiniteSetGE Î¼ Î½ n)

lemma measure_sigmaFiniteSetGE_ge (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s) - 1/n
      â‰¤ Î½ (Î¼.sigmaFiniteSetGE Î½ n) :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose_spec.2.2

lemma tendsto_measure_sigmaFiniteSetGE (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] :
    Tendsto (fun n â†¦ Î½ (Î¼.sigmaFiniteSetGE Î½ n)) atTop
      (ð“ (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s)) := by
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le ?_
    tendsto_const_nhds (measure_sigmaFiniteSetGE_ge Î¼ Î½) (measure_sigmaFiniteSetGE_le Î¼ Î½)
  nth_rewrite 2 [â† tsub_zero (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s)]
  refine ENNReal.Tendsto.sub tendsto_const_nhds ?_ (Or.inr ENNReal.zero_ne_top)
  simp only [one_div]
  exact ENNReal.tendsto_inv_nat_nhds_zero

/-- A measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)` is sigma-finite and
`Î½ (Î¼.sigmaFiniteSetWRT Î½)` has maximal measure among such sets. -/
def Measure.sigmaFiniteSetWRT (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] : Set Î± :=
  â‹ƒ n, Î¼.sigmaFiniteSetGE Î½ n

lemma measurableSet_sigmaFiniteSetWRT (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] :
    MeasurableSet (Î¼.sigmaFiniteSetWRT Î½) :=
  MeasurableSet.iUnion (measurableSet_sigmaFiniteSetGE _ _)

instance [IsFiniteMeasure Î½] : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)) := by
  let f : â„• Ã— â„• â†’ Set Î± := fun p : â„• Ã— â„• â†¦ (Î¼.sigmaFiniteSetWRT Î½)á¶œ
    âˆª (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ p.1)) p.2 âˆ© (Î¼.sigmaFiniteSetGE Î½ p.1))
  suffices (Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)).FiniteSpanningSetsIn (Set.range f) from
    this.sigmaFinite
  let e : â„• â‰ƒ â„• Ã— â„• := Nat.pairEquiv.symm
  refine âŸ¨fun n â†¦ f (e n), fun _ â†¦ by simp, fun n â†¦ ?_, ?_âŸ©
  Â· simp only [Nat.pairEquiv_symm_apply, gt_iff_lt, measure_union_lt_top_iff, f, e]
    rw [Measure.restrict_apply' (measurableSet_sigmaFiniteSetWRT _ _), Set.compl_inter_self,
      Measure.restrict_apply' (measurableSet_sigmaFiniteSetWRT _ _)]
    simp only [measure_empty, ENNReal.zero_lt_top, true_and]
    refine (measure_mono (Set.inter_subset_left _ _)).trans_lt ?_
    rw [â† Measure.restrict_apply' (measurableSet_sigmaFiniteSetGE Î¼ Î½ _)]
    exact measure_spanningSets_lt_top _ _
  Â· simp only [Nat.pairEquiv_symm_apply, f, e]
    rw [â† Set.union_iUnion]
    suffices â‹ƒ n, (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ (Nat.unpair n).1)) n.unpair.2
        âˆ© Î¼.sigmaFiniteSetGE Î½ n.unpair.1) = Î¼.sigmaFiniteSetWRT Î½ by
      rw [this, Set.compl_union_self]
    calc â‹ƒ n, (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ (Nat.unpair n).1)) n.unpair.2
        âˆ© Î¼.sigmaFiniteSetGE Î½ n.unpair.1)
      = â‹ƒ n, â‹ƒ m, (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)) m
            âˆ© Î¼.sigmaFiniteSetGE Î½ n) :=
          Set.iUnion_unpair (fun n m â†¦ spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)) m
            âˆ© Î¼.sigmaFiniteSetGE Î½ n)
    _ = â‹ƒ n, Î¼.sigmaFiniteSetGE Î½ n := by
        refine Set.iUnion_congr (fun n â†¦ ?_)
        rw [â† Set.iUnion_inter, iUnion_spanningSets, Set.univ_inter]
    _ = Î¼.sigmaFiniteSetWRT Î½ := rfl

/-- `Î¼.sigmaFiniteSetWRT Î½` has maximal `Î½`-measure among all measurable sets `s` with sigma-finite
`Î¼.restrict s`. -/
lemma measure_sigmaFiniteSetWRT (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] :
    Î½ (Î¼.sigmaFiniteSetWRT Î½)
      = â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s := by
  apply le_antisymm
  Â· refine (le_iSup (f := fun _ â†¦ _)
      (inferInstance : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)))).trans ?_
    exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : SigmaFinite (Î¼.restrict s)), Î½ s) (Î¼.sigmaFiniteSetWRT Î½)
      (measurableSet_sigmaFiniteSetWRT Î¼ Î½)
  Â· exact le_of_tendsto' (tendsto_measure_sigmaFiniteSetGE Î¼ Î½)
      (fun _ â†¦ measure_mono (Set.subset_iUnion _ _))

/-- For all measurable sets `s` in `(Î¼.sigmaFiniteSetWRT Î½)á¶œ`, if `Î½ s â‰  0` then `Î¼ s = âˆž`. -/
lemma measure_eq_top_of_subset_compl_sigmaFiniteSetWRT [IsFiniteMeasure Î½]
    (hs : MeasurableSet s) (hs_subset_compl : s âŠ† (Î¼.sigmaFiniteSetWRT Î½)á¶œ) (hÎ¼s : Î½ s â‰  0) :
    Î¼ s = âˆž := by
  suffices Â¬ SigmaFinite (Î¼.restrict s) by
    by_contra h
    have h_lt_top : Fact (Î¼ s < âˆž) := âŸ¨Ne.lt_top hâŸ©
    exact this inferInstance
  intro hsÏƒ
  have h_lt : Î½ (Î¼.sigmaFiniteSetWRT Î½) < Î½ (Î¼.sigmaFiniteSetWRT Î½ âˆª s) := by
    rw [measure_union _ hs]
    Â· exact ENNReal.lt_add_right (measure_ne_top _ _) hÎ¼s
    Â· exact disjoint_compl_right.mono_right hs_subset_compl
  have h_le : Î½ (Î¼.sigmaFiniteSetWRT Î½ âˆª s) â‰¤ Î½ (Î¼.sigmaFiniteSetWRT Î½) := by
    conv_rhs => rw [measure_sigmaFiniteSetWRT]
    refine (le_iSup
      (f := fun (_ : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½ âˆª s))) â†¦ _) ?_).trans ?_
    Â· infer_instance
    Â· exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : SigmaFinite (Î¼.restrict _)), Î½ s)
        (Î¼.sigmaFiniteSetWRT Î½ âˆª s) ((measurableSet_sigmaFiniteSetWRT Î¼ Î½).union hs)
  exact h_lt.not_le h_le

lemma restrict_compl_sigmaFiniteSetWRT [IsFiniteMeasure Î½] (hÎ¼Î½ : Î¼ â‰ª Î½) :
    Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)á¶œ = âˆž â€¢ Î½.restrict (Î¼.sigmaFiniteSetWRT Î½)á¶œ := by
  ext s hs
  rw [Measure.restrict_apply' (measurableSet_sigmaFiniteSetWRT _ _).compl,
    Measure.smul_apply, smul_eq_mul,
    Measure.restrict_apply' (measurableSet_sigmaFiniteSetWRT _ _).compl]
  by_cases hÎ½s : Î½ (s âˆ© (Î¼.sigmaFiniteSetWRT Î½)á¶œ) = 0
  Â· rw [hÎ½s, mul_zero]
    exact hÎ¼Î½ hÎ½s
  Â· rw [ENNReal.top_mul hÎ½s, measure_eq_top_of_subset_compl_sigmaFiniteSetWRT
      (hs.inter (measurableSet_sigmaFiniteSetWRT _ _).compl) (Set.inter_subset_right _ _) hÎ½s]

end MeasureTheory
