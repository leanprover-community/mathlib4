/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.Typeclasses

/-!
# Method of exhaustion

If `Î¼, Î¼` are two measures with `Î¼` s-finite, then there exists a set `s` such that
`Î¼` is sigma-finite on `s`, and for all sets `t âŠ† sá¶œ`, either `Î¼ t = 0` or `Î¼ t = âˆž`.

## Main definitions

* `MeasureTheory.Measure.sigmaFiniteSetWRT`: if such a set exists, `Î¼.sigmaFiniteSetWRT Î¼` is
  a measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetWRT Î¼)` is sigma-finite and
  for all sets `t âŠ† (Î¼.sigmaFiniteSetWRT Î¼)á¶œ`, either `Î¼ t = 0` or `Î¼ t = âˆž`.
  If no such set exists (which is only possible if `Î¼` is not s-finite), we define
  `Î¼.sigmaFiniteSetWRT Î¼ = âˆ…`.
* `MeasureTheory.Measure.sigmaFiniteSet`: for an s-finite measure `Î¼`, a measurable set such that
  `Î¼.restrict Î¼.sigmaFiniteSet` is sigma-finite, and for all sets `s âŠ† Î¼.sigmaFiniteSetá¶œ`,
  either `Î¼ s = 0` or `Î¼ s = âˆž`.
  Defined as `Î¼.sigmaFiniteSetWRT Î¼`.

## Main statements

* `measure_eq_top_of_subset_compl_sigmaFiniteSetWRT`: for s-finite `Î¼`, for all sets `s`
  in `(sigmaFiniteSetWRT Î¼ Î¼)á¶œ`, if `Î¼ s â‰  0` then `Î¼ s = âˆž`.
* An instance showing that `Î¼.restrict (sigmaFiniteSetWRT Î¼ Î¼)` is sigma-finite.
* `restrict_compl_sigmaFiniteSetWRT`: if `Î¼ â‰ª Î¼` and `Î¼` is s-finite, then
  `Î¼.restrict (Î¼.sigmaFiniteSetWRT Î¼)á¶œ = âˆž â€¢ Î¼.restrict (Î¼.sigmaFiniteSetWRT Î¼)á¶œ`. As a consequence,
  that restriction is s-finite.

* An instance showing that `Î¼.restrict Î¼.sigmaFiniteSet` is sigma-finite.
* `restrict_compl_sigmaFiniteSet_eq_zero_or_top`: the measure `Î¼.restrict Î¼.sigmaFiniteSetá¶œ` takes
  only two values: 0 and âˆž .
* `measure_compl_sigmaFiniteSet_eq_zero_iff_sigmaFinite`: a measure `Î¼` is sigma-finite
  iff `Î¼ Î¼.sigmaFiniteSetá¶œ = 0`.

## References

* [P. R. Halmos, *Measure theory*, 17.3 and 30.11][halmos1950measure]

-/

open scoped ENNReal Topology

open Filter

namespace MeasureTheory

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±} [IsFiniteMeasure Î¼] {s : Set Î±}

/-! We prove that the condition in the definition of `sigmaFiniteSetWRT` is true for finite
measures. Since every s-finite measure is absolutely continuous with respect to a finite measure,
the condition will then also be true for s-finite measures. -/

/-- Let `p : Set Î± â†’ Prop` be a predicate on sets and let `C` be the supremum of `Î¼ s` over
all measurable sets `s` with property `p s`. `C` is finite since `Î¼` is a finite measure.
Then there exists a measurable set `t` with `p t` such that `Î¼ t â‰¥ C - 1/n`. -/
lemma exists_set_measure_ge (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) (n : â„•) :
    âˆƒ t, MeasurableSet t âˆ§ p t
      âˆ§ (â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s) - 1/n â‰¤ Î¼ t := by
  by_cases hC_lt : 1/n < â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s
  Â· have h_lt_top : â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s < âˆž := by
      refine (?_ : â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s â‰¤ Î¼ Set.univ).trans_lt
        (measure_lt_top _ _)
      refine iSup_le (fun s â†¦ ?_)
      exact iSup_le (fun _ â†¦ iSup_le (fun _ â†¦ measure_mono (Set.subset_univ s)))
    obtain âŸ¨t, htâŸ© := exists_lt_of_lt_ciSup
      (ENNReal.sub_lt_self h_lt_top.ne (ne_zero_of_lt hC_lt) (by simp) :
          (â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s) - 1/n
        < â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s)
    have ht_meas : MeasurableSet t := by
      by_contra h_not_mem
      simp only [h_not_mem] at ht
      simp at ht
    have ht_mem : p t := by
      by_contra h_not_mem
      simp only [h_not_mem] at ht
      simp at ht
    refine âŸ¨t, ht_meas, ht_mem, ?_âŸ©
    simp only [ht_meas, ht_mem, iSup_true] at ht
    exact ht.le
  Â· refine âŸ¨âˆ…, MeasurableSet.empty, hp_empty, ?_âŸ©
    rw [tsub_eq_zero_of_le (not_lt.mp hC_lt)]
    exact zero_le'

/-- A measurable set such that `p (Î¼.pSetGE Î¼ n)` and for `C` the supremum of `Î¼ s` over
all measurable sets `s` with `p s`, `Î¼ (Î¼.pSetGE Î¼ n) â‰¥ C - 1/n`. -/
def Measure.pSetGE (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (p : Set Î± â†’ Prop) (hp_empty : p âˆ…)
    (n : â„•) : Set Î± :=
  (exists_set_measure_ge Î¼ p hp_empty n).choose

lemma measurableSet_pSetGE (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) (n : â„•) :
    MeasurableSet (Î¼.pSetGE p hp_empty n) :=
  (exists_set_measure_ge Î¼ p hp_empty n).choose_spec.1

lemma prop_pSetGE (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) (n : â„•) :
    p (Î¼.pSetGE p hp_empty n) :=
  (exists_set_measure_ge Î¼ p hp_empty n).choose_spec.2.1

lemma measure_pSetGE_le (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) (n : â„•) :
    Î¼ (Î¼.pSetGE p hp_empty n) â‰¤ â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s := by
  refine (le_iSup (f := fun s â†¦ _) (prop_pSetGE Î¼ p hp_empty n)).trans ?_
  exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : p s), Î¼ s) (Î¼.pSetGE p hp_empty n)
    (measurableSet_pSetGE p hp_empty n)

lemma measure_pSetGE_ge (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) (n : â„•) :
    (â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s) - 1/n â‰¤ Î¼ (Î¼.pSetGE p hp_empty n) :=
  (exists_set_measure_ge Î¼ p hp_empty n).choose_spec.2.2

lemma tendsto_measure_pSetGE (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) :
    Tendsto (fun n â†¦ Î¼ (Î¼.pSetGE p hp_empty n)) atTop
      (ð“ (â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s)) := by
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le ?_
    tendsto_const_nhds (measure_pSetGE_ge Î¼ p hp_empty) (measure_pSetGE_le Î¼ p hp_empty)
  nth_rewrite 2 [â† tsub_zero (â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s)]
  refine ENNReal.Tendsto.sub tendsto_const_nhds ?_ (Or.inr ENNReal.zero_ne_top)
  simp only [one_div]
  exact ENNReal.tendsto_inv_nat_nhds_zero

/-- A measurable set such that `p (Î¼.maximalSet p hp_empty)` and the measure
`Î¼ (Î¼.maximalSet p hp_empty)` is maximal among such sets. -/
def Measure.maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) :
    Set Î± :=
  â‹ƒ n, Î¼.pSetGE p hp_empty n

lemma measurableSet_maximalSet (p : Set Î± â†’ Prop) (hp_empty : p âˆ…) :
    MeasurableSet (Î¼.maximalSet p hp_empty) :=
  MeasurableSet.iUnion (measurableSet_pSetGE p hp_empty)

lemma prop_maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…)
    (hp_iUnion : âˆ€ (t : â„• â†’ Set Î±) (_ : âˆ€ n, MeasurableSet (t n)) (_ : âˆ€ n, p (t n)),
      p (â‹ƒ n, t n)) :
    p (Î¼.maximalSet p hp_empty) :=
  hp_iUnion _ (measurableSet_pSetGE p hp_empty) (prop_pSetGE Î¼ p hp_empty)

/-- `Î¼.maximalSet p hp_empty` has maximal `Î¼`-measure among all measurable sets `s` with `p s`. -/
lemma measure_maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…)
    (hp_iUnion : âˆ€ (t : â„• â†’ Set Î±) (_ : âˆ€ n, MeasurableSet (t n)) (_ : âˆ€ n, p (t n)),
      p (â‹ƒ n, t n)) :
    Î¼ (Î¼.maximalSet p hp_empty) = â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s := by
  apply le_antisymm
  Â· refine (le_iSup (f := fun _ â†¦ _) (prop_maximalSet Î¼ p hp_empty hp_iUnion)).trans ?_
    exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : p s), Î¼ s) (Î¼.maximalSet p hp_empty)
      (measurableSet_maximalSet p hp_empty)
  Â· exact le_of_tendsto' (tendsto_measure_pSetGE Î¼ p hp_empty)
      (fun _ â†¦ measure_mono (Set.subset_iUnion _ _))

lemma not_prop_of_subset_compl_maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_empty : p âˆ…)
    (hp_iUnion : âˆ€ (t : â„• â†’ Set Î±) (_ : âˆ€ n, MeasurableSet (t n)) (_ : âˆ€ n, p (t n)),
      p (â‹ƒ n, t n))
    (hs : MeasurableSet s) (hs_subset : s âŠ† (Î¼.maximalSet p hp_empty)á¶œ) (hÎ¼s : Î¼ s â‰  0) :
    Â¬ p s := by
  intro hsp
  have h_lt : Î¼ (Î¼.maximalSet p hp_empty) < Î¼ (Î¼.maximalSet p hp_empty âˆª s) := by
    rw [measure_union _ hs]
    Â· exact ENNReal.lt_add_right (measure_ne_top _ _) hÎ¼s
    Â· exact disjoint_compl_right.mono_right hs_subset
  have h_le : Î¼ (Î¼.maximalSet p hp_empty âˆª s) â‰¤ Î¼ (Î¼.maximalSet p hp_empty) := by
    conv_rhs => rw [measure_maximalSet _ _ hp_empty hp_iUnion]
    refine (le_iSup
      (f := fun (_ : p (Î¼.maximalSet p hp_empty âˆª s)) â†¦ _) ?_).trans ?_
    Â· let t : â„• â†’ Set Î± := fun n â†¦ if n = 0 then (Î¼.maximalSet p hp_empty) else s
      have : Î¼.maximalSet p hp_empty âˆª s = â‹ƒ n, t n := by
        simp only [t, Set.iUnion_ite, Set.iUnion_iUnion_eq_left]
        congr with x
        simp only [Set.mem_iUnion, exists_prop, exists_and_right, iff_and_self]
        exact fun _ â†¦ âŸ¨1, by simpâŸ©
      rw [this]
      refine hp_iUnion t (fun n â†¦ ?_) (fun n â†¦ ?_)
      Â· cases n with
        | zero =>
          simp only [â†“reduceIte, t]
          exact measurableSet_maximalSet p hp_empty
        | succ n =>
            simp only [AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false, â†“reduceIte, t]
            exact hs
      Â· cases n with
        | zero =>
          simp only [â†“reduceIte, t]
          exact prop_maximalSet Î¼ p hp_empty hp_iUnion
        | succ n =>
            simp only [AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false, â†“reduceIte, t]
            exact hsp
    Â· exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : p _), Î¼ s)
        (Î¼.maximalSet p hp_empty âˆª s) ((measurableSet_maximalSet p hp_empty).union hs)
  exact h_lt.not_le h_le

end MeasureTheory
