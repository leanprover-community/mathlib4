/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.Decomposition.SignedHahn
import Mathlib.MeasureTheory.Measure.MutuallySingular

#align_import measure_theory.decomposition.jordan from "leanprover-community/mathlib"@"70a4f2197832bceab57d7f41379b2592d1110570"

/-!
# Jordan decomposition

This file proves the existence and uniqueness of the Jordan decomposition for signed measures.
The Jordan decomposition theorem states that, given a signed measure `s`, there exists a
unique pair of mutually singular measures `Œº` and `ŒΩ`, such that `s = Œº - ŒΩ`.

The Jordan decomposition theorem for measures is a corollary of the Hahn decomposition theorem and
is useful for the Lebesgue decomposition theorem.

## Main definitions

* `MeasureTheory.JordanDecomposition`: a Jordan decomposition of a measurable space is a
  pair of mutually singular finite measures. We say `j` is a Jordan decomposition of a signed
  measure `s` if `s = j.posPart - j.negPart`.
* `MeasureTheory.SignedMeasure.toJordanDecomposition`: the Jordan decomposition of a
  signed measure.
* `MeasureTheory.SignedMeasure.toJordanDecompositionEquiv`: is the `Equiv` between
  `MeasureTheory.SignedMeasure` and `MeasureTheory.JordanDecomposition` formed by
  `MeasureTheory.SignedMeasure.toJordanDecomposition`.

## Main results

* `MeasureTheory.SignedMeasure.toSignedMeasure_toJordanDecomposition` : the Jordan
  decomposition theorem.
* `MeasureTheory.JordanDecomposition.toSignedMeasure_injective` : the Jordan decomposition of a
  signed measure is unique.

## Tags

Jordan decomposition theorem
-/


noncomputable section

open scoped Classical MeasureTheory ENNReal NNReal

variable {Œ± Œ≤ : Type*} [MeasurableSpace Œ±]

namespace MeasureTheory

/-- A Jordan decomposition of a measurable space is a pair of mutually singular,
finite measures. -/
@[ext]
structure JordanDecomposition (Œ± : Type*) [MeasurableSpace Œ±] where
  (posPart negPart : Measure Œ±)
  [posPart_finite : IsFiniteMeasure posPart]
  [negPart_finite : IsFiniteMeasure negPart]
  mutuallySingular : posPart ‚üÇ‚Çò negPart
#align measure_theory.jordan_decomposition MeasureTheory.JordanDecomposition
#align measure_theory.jordan_decomposition.pos_part MeasureTheory.JordanDecomposition.posPart
#align measure_theory.jordan_decomposition.neg_part MeasureTheory.JordanDecomposition.negPart
#align measure_theory.jordan_decomposition.pos_part_finite MeasureTheory.JordanDecomposition.posPart_finite
#align measure_theory.jordan_decomposition.neg_part_finite MeasureTheory.JordanDecomposition.negPart_finite
#align measure_theory.jordan_decomposition.mutually_singular MeasureTheory.JordanDecomposition.mutuallySingular

attribute [instance] JordanDecomposition.posPart_finite

attribute [instance] JordanDecomposition.negPart_finite

namespace JordanDecomposition

open Measure VectorMeasure

variable (j : JordanDecomposition Œ±)

instance instZero : Zero (JordanDecomposition Œ±) where zero := ‚ü®0, 0, MutuallySingular.zero_right‚ü©
#align measure_theory.jordan_decomposition.has_zero MeasureTheory.JordanDecomposition.instZero

instance instInhabited : Inhabited (JordanDecomposition Œ±) where default := 0
#align measure_theory.jordan_decomposition.inhabited MeasureTheory.JordanDecomposition.instInhabited

instance instInvolutiveNeg : InvolutiveNeg (JordanDecomposition Œ±) where
  neg j := ‚ü®j.negPart, j.posPart, j.mutuallySingular.symm‚ü©
  neg_neg _ := JordanDecomposition.ext _ _ rfl rfl
#align measure_theory.jordan_decomposition.has_involutive_neg MeasureTheory.JordanDecomposition.instInvolutiveNeg

instance instSMul : SMul ‚Ñù‚â•0 (JordanDecomposition Œ±) where
  smul r j :=
    ‚ü®r ‚Ä¢ j.posPart, r ‚Ä¢ j.negPart,
      MutuallySingular.smul _ (MutuallySingular.smul _ j.mutuallySingular.symm).symm‚ü©
#align measure_theory.jordan_decomposition.has_smul MeasureTheory.JordanDecomposition.instSMul

instance instSMulReal : SMul ‚Ñù (JordanDecomposition Œ±) where
  smul r j := if 0 ‚â§ r then r.toNNReal ‚Ä¢ j else -((-r).toNNReal ‚Ä¢ j)
#align measure_theory.jordan_decomposition.has_smul_real MeasureTheory.JordanDecomposition.instSMulReal

@[simp]
theorem zero_posPart : (0 : JordanDecomposition Œ±).posPart = 0 :=
  rfl
#align measure_theory.jordan_decomposition.zero_pos_part MeasureTheory.JordanDecomposition.zero_posPart

@[simp]
theorem zero_negPart : (0 : JordanDecomposition Œ±).negPart = 0 :=
  rfl
#align measure_theory.jordan_decomposition.zero_neg_part MeasureTheory.JordanDecomposition.zero_negPart

@[simp]
theorem neg_posPart : (-j).posPart = j.negPart :=
  rfl
#align measure_theory.jordan_decomposition.neg_pos_part MeasureTheory.JordanDecomposition.neg_posPart

@[simp]
theorem neg_negPart : (-j).negPart = j.posPart :=
  rfl
#align measure_theory.jordan_decomposition.neg_neg_part MeasureTheory.JordanDecomposition.neg_negPart

@[simp]
theorem smul_posPart (r : ‚Ñù‚â•0) : (r ‚Ä¢ j).posPart = r ‚Ä¢ j.posPart :=
  rfl
#align measure_theory.jordan_decomposition.smul_pos_part MeasureTheory.JordanDecomposition.smul_posPart

@[simp]
theorem smul_negPart (r : ‚Ñù‚â•0) : (r ‚Ä¢ j).negPart = r ‚Ä¢ j.negPart :=
  rfl
#align measure_theory.jordan_decomposition.smul_neg_part MeasureTheory.JordanDecomposition.smul_negPart

theorem real_smul_def (r : ‚Ñù) (j : JordanDecomposition Œ±) :
    r ‚Ä¢ j = if 0 ‚â§ r then r.toNNReal ‚Ä¢ j else -((-r).toNNReal ‚Ä¢ j) :=
  rfl
#align measure_theory.jordan_decomposition.real_smul_def MeasureTheory.JordanDecomposition.real_smul_def

@[simp]
theorem coe_smul (r : ‚Ñù‚â•0) : (r : ‚Ñù) ‚Ä¢ j = r ‚Ä¢ j := by
  -- Porting note: replaced `show`
  rw [real_smul_def, if_pos (NNReal.coe_nonneg r), Real.toNNReal_coe]
  -- üéâ no goals
#align measure_theory.jordan_decomposition.coe_smul MeasureTheory.JordanDecomposition.coe_smul

theorem real_smul_nonneg (r : ‚Ñù) (hr : 0 ‚â§ r) : r ‚Ä¢ j = r.toNNReal ‚Ä¢ j :=
  dif_pos hr
#align measure_theory.jordan_decomposition.real_smul_nonneg MeasureTheory.JordanDecomposition.real_smul_nonneg

theorem real_smul_neg (r : ‚Ñù) (hr : r < 0) : r ‚Ä¢ j = -((-r).toNNReal ‚Ä¢ j) :=
  dif_neg (not_le.2 hr)
#align measure_theory.jordan_decomposition.real_smul_neg MeasureTheory.JordanDecomposition.real_smul_neg

theorem real_smul_posPart_nonneg (r : ‚Ñù) (hr : 0 ‚â§ r) :
    (r ‚Ä¢ j).posPart = r.toNNReal ‚Ä¢ j.posPart := by
  rw [real_smul_def, ‚Üê smul_posPart, if_pos hr]
  -- üéâ no goals
#align measure_theory.jordan_decomposition.real_smul_pos_part_nonneg MeasureTheory.JordanDecomposition.real_smul_posPart_nonneg

theorem real_smul_negPart_nonneg (r : ‚Ñù) (hr : 0 ‚â§ r) :
    (r ‚Ä¢ j).negPart = r.toNNReal ‚Ä¢ j.negPart := by
  rw [real_smul_def, ‚Üê smul_negPart, if_pos hr]
  -- üéâ no goals
#align measure_theory.jordan_decomposition.real_smul_neg_part_nonneg MeasureTheory.JordanDecomposition.real_smul_negPart_nonneg

theorem real_smul_posPart_neg (r : ‚Ñù) (hr : r < 0) :
    (r ‚Ä¢ j).posPart = (-r).toNNReal ‚Ä¢ j.negPart := by
  rw [real_smul_def, ‚Üê smul_negPart, if_neg (not_le.2 hr), neg_posPart]
  -- üéâ no goals
#align measure_theory.jordan_decomposition.real_smul_pos_part_neg MeasureTheory.JordanDecomposition.real_smul_posPart_neg

theorem real_smul_negPart_neg (r : ‚Ñù) (hr : r < 0) :
    (r ‚Ä¢ j).negPart = (-r).toNNReal ‚Ä¢ j.posPart := by
  rw [real_smul_def, ‚Üê smul_posPart, if_neg (not_le.2 hr), neg_negPart]
  -- üéâ no goals
#align measure_theory.jordan_decomposition.real_smul_neg_part_neg MeasureTheory.JordanDecomposition.real_smul_negPart_neg

/-- The signed measure associated with a Jordan decomposition. -/
def toSignedMeasure : SignedMeasure Œ± :=
  j.posPart.toSignedMeasure - j.negPart.toSignedMeasure
#align measure_theory.jordan_decomposition.to_signed_measure MeasureTheory.JordanDecomposition.toSignedMeasure

theorem toSignedMeasure_zero : (0 : JordanDecomposition Œ±).toSignedMeasure = 0 := by
  ext1 i hi
  -- ‚ä¢ ‚Üë(toSignedMeasure 0) i = ‚Üë0 i
  -- Porting note: replaced `erw` by adding further lemmas
  rw [toSignedMeasure, toSignedMeasure_sub_apply hi, zero_posPart, zero_negPart, sub_self,
    VectorMeasure.coe_zero, Pi.zero_apply]
#align measure_theory.jordan_decomposition.to_signed_measure_zero MeasureTheory.JordanDecomposition.toSignedMeasure_zero

theorem toSignedMeasure_neg : (-j).toSignedMeasure = -j.toSignedMeasure := by
  ext1 i hi
  -- ‚ä¢ ‚Üë(toSignedMeasure (-j)) i = ‚Üë(-toSignedMeasure j) i
  -- Porting note: removed `rfl` after the `rw` by adding further steps.
  rw [neg_apply, toSignedMeasure, toSignedMeasure, toSignedMeasure_sub_apply hi,
    toSignedMeasure_sub_apply hi, neg_sub, neg_posPart, neg_negPart]
#align measure_theory.jordan_decomposition.to_signed_measure_neg MeasureTheory.JordanDecomposition.toSignedMeasure_neg

theorem toSignedMeasure_smul (r : ‚Ñù‚â•0) : (r ‚Ä¢ j).toSignedMeasure = r ‚Ä¢ j.toSignedMeasure := by
  ext1 i hi
  -- ‚ä¢ ‚Üë(toSignedMeasure (r ‚Ä¢ j)) i = ‚Üë(r ‚Ä¢ toSignedMeasure j) i
  -- Porting note: removed `rfl` after the `rw` by adding further steps.
  rw [VectorMeasure.smul_apply, toSignedMeasure, toSignedMeasure,
    toSignedMeasure_sub_apply hi, toSignedMeasure_sub_apply hi, smul_sub, smul_posPart,
    smul_negPart, ‚Üê ENNReal.toReal_smul, ‚Üê ENNReal.toReal_smul, smul_toOuterMeasure,
    OuterMeasure.coe_smul, Pi.smul_apply, smul_toOuterMeasure, OuterMeasure.coe_smul, Pi.smul_apply]
#align measure_theory.jordan_decomposition.to_signed_measure_smul MeasureTheory.JordanDecomposition.toSignedMeasure_smul

/-- A Jordan decomposition provides a Hahn decomposition. -/
theorem exists_compl_positive_negative :
    ‚àÉ S : Set Œ±,
      MeasurableSet S ‚àß
        j.toSignedMeasure ‚â§[S] 0 ‚àß
          0 ‚â§[S·∂ú] j.toSignedMeasure ‚àß j.posPart S = 0 ‚àß j.negPart S·∂ú = 0 := by
  obtain ‚ü®S, hS‚ÇÅ, hS‚ÇÇ, hS‚ÇÉ‚ü© := j.mutuallySingular
  -- ‚ä¢ ‚àÉ S, MeasurableSet S ‚àß VectorMeasure.restrict (toSignedMeasure j) S ‚â§ Vector ‚Ä¶
  refine' ‚ü®S, hS‚ÇÅ, _, _, hS‚ÇÇ, hS‚ÇÉ‚ü©
  -- ‚ä¢ VectorMeasure.restrict (toSignedMeasure j) S ‚â§ VectorMeasure.restrict 0 S
  ¬∑ refine' restrict_le_restrict_of_subset_le _ _ fun A hA hA‚ÇÅ => _
    -- ‚ä¢ ‚Üë(toSignedMeasure j) A ‚â§ ‚Üë0 A
    rw [toSignedMeasure, toSignedMeasure_sub_apply hA,
      show j.posPart A = 0 from nonpos_iff_eq_zero.1 (hS‚ÇÇ ‚ñ∏ measure_mono hA‚ÇÅ), ENNReal.zero_toReal,
      zero_sub, neg_le, zero_apply, neg_zero]
    exact ENNReal.toReal_nonneg
    -- üéâ no goals
  ¬∑ refine' restrict_le_restrict_of_subset_le _ _ fun A hA hA‚ÇÅ => _
    -- ‚ä¢ ‚Üë0 A ‚â§ ‚Üë(toSignedMeasure j) A
    rw [toSignedMeasure, toSignedMeasure_sub_apply hA,
      show j.negPart A = 0 from nonpos_iff_eq_zero.1 (hS‚ÇÉ ‚ñ∏ measure_mono hA‚ÇÅ), ENNReal.zero_toReal,
      sub_zero]
    exact ENNReal.toReal_nonneg
    -- üéâ no goals
#align measure_theory.jordan_decomposition.exists_compl_positive_negative MeasureTheory.JordanDecomposition.exists_compl_positive_negative

end JordanDecomposition

namespace SignedMeasure

open Classical JordanDecomposition Measure Set VectorMeasure

variable {s : SignedMeasure Œ±} {Œº ŒΩ : Measure Œ±} [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ]

/-- Given a signed measure `s`, `s.toJordanDecomposition` is the Jordan decomposition `j`,
such that `s = j.toSignedMeasure`. This property is known as the Jordan decomposition
theorem, and is shown by
`MeasureTheory.SignedMeasure.toSignedMeasure_toJordanDecomposition`. -/
def toJordanDecomposition (s : SignedMeasure Œ±) : JordanDecomposition Œ± :=
  let i := choose s.exists_compl_positive_negative
  let hi := choose_spec s.exists_compl_positive_negative
  { posPart := s.toMeasureOfZeroLE i hi.1 hi.2.1
    negPart := s.toMeasureOfLEZero i·∂ú hi.1.compl hi.2.2
    posPart_finite := inferInstance
    negPart_finite := inferInstance
    mutuallySingular := by
      refine' ‚ü®i·∂ú, hi.1.compl, _, _‚ü©
      -- ‚ä¢ ‚Üë‚Üë(toMeasureOfZeroLE s i (_ : MeasurableSet (choose (_ : ‚àÉ i, MeasurableSet  ‚Ä¶
      -- Porting note: added `‚Üê NNReal.eq_iff`
      ¬∑ rw [toMeasureOfZeroLE_apply _ _ hi.1 hi.1.compl]; simp [‚Üê NNReal.eq_iff]
        -- ‚ä¢ ‚Üë{ val := ‚Üës (choose (_ : ‚àÉ i, MeasurableSet i ‚àß VectorMeasure.restrict 0 i  ‚Ä¶
                                                          -- üéâ no goals
      ¬∑ rw [toMeasureOfLEZero_apply _ _ hi.1.compl hi.1.compl.compl]; simp [‚Üê NNReal.eq_iff] }
        -- ‚ä¢ ‚Üë{ val := -‚Üës ((choose (_ : ‚àÉ i, MeasurableSet i ‚àß VectorMeasure.restrict 0  ‚Ä¶
                                                                      -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition MeasureTheory.SignedMeasure.toJordanDecomposition

theorem toJordanDecomposition_spec (s : SignedMeasure Œ±) :
    ‚àÉ (i : Set Œ±) (hi‚ÇÅ : MeasurableSet i) (hi‚ÇÇ : 0 ‚â§[i] s) (hi‚ÇÉ : s ‚â§[i·∂ú] 0),
      s.toJordanDecomposition.posPart = s.toMeasureOfZeroLE i hi‚ÇÅ hi‚ÇÇ ‚àß
        s.toJordanDecomposition.negPart = s.toMeasureOfLEZero i·∂ú hi‚ÇÅ.compl hi‚ÇÉ := by
  set i := choose s.exists_compl_positive_negative
  -- ‚ä¢ ‚àÉ i hi‚ÇÅ hi‚ÇÇ hi‚ÇÉ, (toJordanDecomposition s).posPart = toMeasureOfZeroLE s i h ‚Ä¶
  obtain ‚ü®hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ‚ü© := choose_spec s.exists_compl_positive_negative
  -- ‚ä¢ ‚àÉ i hi‚ÇÅ hi‚ÇÇ hi‚ÇÉ, (toJordanDecomposition s).posPart = toMeasureOfZeroLE s i h ‚Ä¶
  exact ‚ü®i, hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ, rfl, rfl‚ü©
  -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition_spec MeasureTheory.SignedMeasure.toJordanDecomposition_spec

/-- **The Jordan decomposition theorem**: Given a signed measure `s`, there exists a pair of
mutually singular measures `Œº` and `ŒΩ` such that `s = Œº - ŒΩ`. In this case, the measures `Œº`
and `ŒΩ` are given by `s.toJordanDecomposition.posPart` and
`s.toJordanDecomposition.negPart` respectively.

Note that we use `MeasureTheory.JordanDecomposition.toSignedMeasure` to represent the
signed measure corresponding to
`s.toJordanDecomposition.posPart - s.toJordanDecomposition.negPart`. -/
@[simp]
theorem toSignedMeasure_toJordanDecomposition (s : SignedMeasure Œ±) :
    s.toJordanDecomposition.toSignedMeasure = s := by
  obtain ‚ü®i, hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ, hŒº, hŒΩ‚ü© := s.toJordanDecomposition_spec
  -- ‚ä¢ JordanDecomposition.toSignedMeasure (toJordanDecomposition s) = s
  simp only [JordanDecomposition.toSignedMeasure, hŒº, hŒΩ]
  -- ‚ä¢ Measure.toSignedMeasure (toMeasureOfZeroLE s i hi‚ÇÅ hi‚ÇÇ) - Measure.toSignedMe ‚Ä¶
  ext k hk
  -- ‚ä¢ ‚Üë(Measure.toSignedMeasure (toMeasureOfZeroLE s i hi‚ÇÅ hi‚ÇÇ) - Measure.toSigned ‚Ä¶
  rw [toSignedMeasure_sub_apply hk, toMeasureOfZeroLE_apply _ hi‚ÇÇ hi‚ÇÅ hk,
    toMeasureOfLEZero_apply _ hi‚ÇÉ hi‚ÇÅ.compl hk]
  simp only [ENNReal.coe_toReal, NNReal.coe_mk, ENNReal.some_eq_coe, sub_neg_eq_add]
  -- ‚ä¢ ‚Üës (i ‚à© k) + ‚Üës (i·∂ú ‚à© k) = ‚Üës k
  rw [‚Üê of_union _ (MeasurableSet.inter hi‚ÇÅ hk) (MeasurableSet.inter hi‚ÇÅ.compl hk),
    Set.inter_comm i, Set.inter_comm i·∂ú, Set.inter_union_compl _ _]
  exact (disjoint_compl_right.inf_left _).inf_right _
  -- üéâ no goals
#align measure_theory.signed_measure.to_signed_measure_to_jordan_decomposition MeasureTheory.SignedMeasure.toSignedMeasure_toJordanDecomposition

section

variable {u v w : Set Œ±}

/-- A subset `v` of a null-set `w` has zero measure if `w` is a subset of a positive set `u`. -/
theorem subset_positive_null_set (hu : MeasurableSet u) (hv : MeasurableSet v)
    (hw : MeasurableSet w) (hsu : 0 ‚â§[u] s) (hw‚ÇÅ : s w = 0) (hw‚ÇÇ : w ‚äÜ u) (hwt : v ‚äÜ w) :
    s v = 0 := by
  have : s v + s (w \ v) = 0 := by
    rw [‚Üê hw‚ÇÅ, ‚Üê of_union Set.disjoint_sdiff_right hv (hw.diff hv), Set.union_diff_self,
      Set.union_eq_self_of_subset_left hwt]
  have h‚ÇÅ := nonneg_of_zero_le_restrict _ (restrict_le_restrict_subset _ _ hu hsu (hwt.trans hw‚ÇÇ))
  -- ‚ä¢ ‚Üës v = 0
  have h‚ÇÇ :=
    nonneg_of_zero_le_restrict _
      (restrict_le_restrict_subset _ _ hu hsu ((w.diff_subset v).trans hw‚ÇÇ))
  linarith
  -- üéâ no goals
#align measure_theory.signed_measure.subset_positive_null_set MeasureTheory.SignedMeasure.subset_positive_null_set

/-- A subset `v` of a null-set `w` has zero measure if `w` is a subset of a negative set `u`. -/
theorem subset_negative_null_set (hu : MeasurableSet u) (hv : MeasurableSet v)
    (hw : MeasurableSet w) (hsu : s ‚â§[u] 0) (hw‚ÇÅ : s w = 0) (hw‚ÇÇ : w ‚äÜ u) (hwt : v ‚äÜ w) :
    s v = 0 := by
  rw [‚Üê s.neg_le_neg_iff _ hu, neg_zero] at hsu
  -- ‚ä¢ ‚Üës v = 0
  have := subset_positive_null_set hu hv hw hsu
  -- ‚ä¢ ‚Üës v = 0
  simp only [Pi.neg_apply, neg_eq_zero, coe_neg] at this
  -- ‚ä¢ ‚Üës v = 0
  exact this hw‚ÇÅ hw‚ÇÇ hwt
  -- üéâ no goals
#align measure_theory.signed_measure.subset_negative_null_set MeasureTheory.SignedMeasure.subset_negative_null_set

/-- If the symmetric difference of two positive sets is a null-set, then so are the differences
between the two sets. -/
theorem of_diff_eq_zero_of_symmDiff_eq_zero_positive (hu : MeasurableSet u) (hv : MeasurableSet v)
    (hsu : 0 ‚â§[u] s) (hsv : 0 ‚â§[v] s) (hs : s (u ‚àÜ v) = 0) : s (u \ v) = 0 ‚àß s (v \ u) = 0 := by
  rw [restrict_le_restrict_iff] at hsu hsv
  have a := hsu (hu.diff hv) (u.diff_subset v)
  have b := hsv (hv.diff hu) (v.diff_subset u)
  erw [of_union (Set.disjoint_of_subset_left (u.diff_subset v) disjoint_sdiff_self_right)
      (hu.diff hv) (hv.diff hu)] at hs
  rw [zero_apply] at a b
  constructor
  all_goals first | linarith | infer_instance | assumption
  -- üéâ no goals
#align measure_theory.signed_measure.of_diff_eq_zero_of_symm_diff_eq_zero_positive MeasureTheory.SignedMeasure.of_diff_eq_zero_of_symmDiff_eq_zero_positive

/-- If the symmetric difference of two negative sets is a null-set, then so are the differences
between the two sets. -/
theorem of_diff_eq_zero_of_symmDiff_eq_zero_negative (hu : MeasurableSet u) (hv : MeasurableSet v)
    (hsu : s ‚â§[u] 0) (hsv : s ‚â§[v] 0) (hs : s (u ‚àÜ v) = 0) : s (u \ v) = 0 ‚àß s (v \ u) = 0 := by
  rw [‚Üê s.neg_le_neg_iff _ hu, neg_zero] at hsu
  -- ‚ä¢ ‚Üës (u \ v) = 0 ‚àß ‚Üës (v \ u) = 0
  rw [‚Üê s.neg_le_neg_iff _ hv, neg_zero] at hsv
  -- ‚ä¢ ‚Üës (u \ v) = 0 ‚àß ‚Üës (v \ u) = 0
  have := of_diff_eq_zero_of_symmDiff_eq_zero_positive hu hv hsu hsv
  -- ‚ä¢ ‚Üës (u \ v) = 0 ‚àß ‚Üës (v \ u) = 0
  simp only [Pi.neg_apply, neg_eq_zero, coe_neg] at this
  -- ‚ä¢ ‚Üës (u \ v) = 0 ‚àß ‚Üës (v \ u) = 0
  exact this hs
  -- üéâ no goals
#align measure_theory.signed_measure.of_diff_eq_zero_of_symm_diff_eq_zero_negative MeasureTheory.SignedMeasure.of_diff_eq_zero_of_symmDiff_eq_zero_negative

theorem of_inter_eq_of_symmDiff_eq_zero_positive (hu : MeasurableSet u) (hv : MeasurableSet v)
    (hw : MeasurableSet w) (hsu : 0 ‚â§[u] s) (hsv : 0 ‚â§[v] s) (hs : s (u ‚àÜ v) = 0) :
    s (w ‚à© u) = s (w ‚à© v) := by
  have hwuv : s ((w ‚à© u) ‚àÜ (w ‚à© v)) = 0 := by
    refine'
      subset_positive_null_set (hu.union hv) ((hw.inter hu).symmDiff (hw.inter hv))
        (hu.symmDiff hv) (restrict_le_restrict_union _ _ hu hsu hv hsv) hs
        Set.symmDiff_subset_union _
    rw [‚Üê Set.inter_symmDiff_distrib_left]
    exact Set.inter_subset_right _ _
  obtain ‚ü®huv, hvu‚ü© :=
    of_diff_eq_zero_of_symmDiff_eq_zero_positive (hw.inter hu) (hw.inter hv)
      (restrict_le_restrict_subset _ _ hu hsu (w.inter_subset_right u))
      (restrict_le_restrict_subset _ _ hv hsv (w.inter_subset_right v)) hwuv
  rw [‚Üê of_diff_of_diff_eq_zero (hw.inter hu) (hw.inter hv) hvu, huv, zero_add]
  -- üéâ no goals
#align measure_theory.signed_measure.of_inter_eq_of_symm_diff_eq_zero_positive MeasureTheory.SignedMeasure.of_inter_eq_of_symmDiff_eq_zero_positive

theorem of_inter_eq_of_symmDiff_eq_zero_negative (hu : MeasurableSet u) (hv : MeasurableSet v)
    (hw : MeasurableSet w) (hsu : s ‚â§[u] 0) (hsv : s ‚â§[v] 0) (hs : s (u ‚àÜ v) = 0) :
    s (w ‚à© u) = s (w ‚à© v) := by
  rw [‚Üê s.neg_le_neg_iff _ hu, neg_zero] at hsu
  -- ‚ä¢ ‚Üës (w ‚à© u) = ‚Üës (w ‚à© v)
  rw [‚Üê s.neg_le_neg_iff _ hv, neg_zero] at hsv
  -- ‚ä¢ ‚Üës (w ‚à© u) = ‚Üës (w ‚à© v)
  have := of_inter_eq_of_symmDiff_eq_zero_positive hu hv hw hsu hsv
  -- ‚ä¢ ‚Üës (w ‚à© u) = ‚Üës (w ‚à© v)
  simp only [Pi.neg_apply, neg_inj, neg_eq_zero, coe_neg] at this
  -- ‚ä¢ ‚Üës (w ‚à© u) = ‚Üës (w ‚à© v)
  exact this hs
  -- üéâ no goals
#align measure_theory.signed_measure.of_inter_eq_of_symm_diff_eq_zero_negative MeasureTheory.SignedMeasure.of_inter_eq_of_symmDiff_eq_zero_negative

end

end SignedMeasure

namespace JordanDecomposition

open Measure VectorMeasure SignedMeasure Function

private theorem eq_of_posPart_eq_posPart {j‚ÇÅ j‚ÇÇ : JordanDecomposition Œ±}
    (hj : j‚ÇÅ.posPart = j‚ÇÇ.posPart) (hj' : j‚ÇÅ.toSignedMeasure = j‚ÇÇ.toSignedMeasure) : j‚ÇÅ = j‚ÇÇ := by
  ext1
  -- ‚ä¢ j‚ÇÅ.posPart = j‚ÇÇ.posPart
  ¬∑ exact hj
    -- üéâ no goals
  ¬∑ rw [‚Üê toSignedMeasure_eq_toSignedMeasure_iff]
    -- ‚ä¢ Measure.toSignedMeasure j‚ÇÅ.negPart = Measure.toSignedMeasure j‚ÇÇ.negPart
    -- Porting note: golfed
    unfold toSignedMeasure at hj'
    -- ‚ä¢ Measure.toSignedMeasure j‚ÇÅ.negPart = Measure.toSignedMeasure j‚ÇÇ.negPart
    simp_rw [hj, sub_right_inj] at hj'
    -- ‚ä¢ Measure.toSignedMeasure j‚ÇÅ.negPart = Measure.toSignedMeasure j‚ÇÇ.negPart
    exact hj'
    -- üéâ no goals

/-- The Jordan decomposition of a signed measure is unique. -/
theorem toSignedMeasure_injective : Injective <| @JordanDecomposition.toSignedMeasure Œ± _ := by
  /- The main idea is that two Jordan decompositions of a signed measure provide two
    Hahn decompositions for that measure. Then, from `of_symmDiff_compl_positive_negative`,
    the symmetric difference of the two Hahn decompositions has measure zero, thus, allowing us to
    show the equality of the underlying measures of the Jordan decompositions. -/
  intro j‚ÇÅ j‚ÇÇ hj
  -- ‚ä¢ j‚ÇÅ = j‚ÇÇ
  -- obtain the two Hahn decompositions from the Jordan decompositions
  obtain ‚ü®S, hS‚ÇÅ, hS‚ÇÇ, hS‚ÇÉ, hS‚ÇÑ, hS‚ÇÖ‚ü© := j‚ÇÅ.exists_compl_positive_negative
  -- ‚ä¢ j‚ÇÅ = j‚ÇÇ
  obtain ‚ü®T, hT‚ÇÅ, hT‚ÇÇ, hT‚ÇÉ, hT‚ÇÑ, hT‚ÇÖ‚ü© := j‚ÇÇ.exists_compl_positive_negative
  -- ‚ä¢ j‚ÇÅ = j‚ÇÇ
  rw [‚Üê hj] at hT‚ÇÇ hT‚ÇÉ
  -- ‚ä¢ j‚ÇÅ = j‚ÇÇ
  -- the symmetric differences of the two Hahn decompositions have measure zero
  obtain ‚ü®hST‚ÇÅ, -‚ü© :=
    of_symmDiff_compl_positive_negative hS‚ÇÅ.compl hT‚ÇÅ.compl ‚ü®hS‚ÇÉ, (compl_compl S).symm ‚ñ∏ hS‚ÇÇ‚ü©
      ‚ü®hT‚ÇÉ, (compl_compl T).symm ‚ñ∏ hT‚ÇÇ‚ü©
  -- it suffices to show the Jordan decompositions have the same positive parts
  refine' eq_of_posPart_eq_posPart _ hj
  -- ‚ä¢ j‚ÇÅ.posPart = j‚ÇÇ.posPart
  ext1 i hi
  -- ‚ä¢ ‚Üë‚Üëj‚ÇÅ.posPart i = ‚Üë‚Üëj‚ÇÇ.posPart i
  -- we see that the positive parts of the two Jordan decompositions are equal to their
  -- associated signed measures restricted on their associated Hahn decompositions
  have hŒº‚ÇÅ : (j‚ÇÅ.posPart i).toReal = j‚ÇÅ.toSignedMeasure (i ‚à© S·∂ú) := by
    rw [toSignedMeasure, toSignedMeasure_sub_apply (hi.inter hS‚ÇÅ.compl),
      show j‚ÇÅ.negPart (i ‚à© S·∂ú) = 0 from
        nonpos_iff_eq_zero.1 (hS‚ÇÖ ‚ñ∏ measure_mono (Set.inter_subset_right _ _)),
      ENNReal.zero_toReal, sub_zero]
    conv_lhs => rw [‚Üê Set.inter_union_compl i S]
    rw [measure_union,
      show j‚ÇÅ.posPart (i ‚à© S) = 0 from
        nonpos_iff_eq_zero.1 (hS‚ÇÑ ‚ñ∏ measure_mono (Set.inter_subset_right _ _)),
      zero_add]
    ¬∑ refine'
        Set.disjoint_of_subset_left (Set.inter_subset_right _ _)
          (Set.disjoint_of_subset_right (Set.inter_subset_right _ _) disjoint_compl_right)
    ¬∑ exact hi.inter hS‚ÇÅ.compl
  have hŒº‚ÇÇ : (j‚ÇÇ.posPart i).toReal = j‚ÇÇ.toSignedMeasure (i ‚à© T·∂ú) := by
    rw [toSignedMeasure, toSignedMeasure_sub_apply (hi.inter hT‚ÇÅ.compl),
      show j‚ÇÇ.negPart (i ‚à© T·∂ú) = 0 from
        nonpos_iff_eq_zero.1 (hT‚ÇÖ ‚ñ∏ measure_mono (Set.inter_subset_right _ _)),
      ENNReal.zero_toReal, sub_zero]
    conv_lhs => rw [‚Üê Set.inter_union_compl i T]
    rw [measure_union,
      show j‚ÇÇ.posPart (i ‚à© T) = 0 from
        nonpos_iff_eq_zero.1 (hT‚ÇÑ ‚ñ∏ measure_mono (Set.inter_subset_right _ _)),
      zero_add]
    ¬∑ exact
        Set.disjoint_of_subset_left (Set.inter_subset_right _ _)
          (Set.disjoint_of_subset_right (Set.inter_subset_right _ _) disjoint_compl_right)
    ¬∑ exact hi.inter hT‚ÇÅ.compl
  -- since the two signed measures associated with the Jordan decompositions are the same,
  -- and the symmetric difference of the Hahn decompositions have measure zero, the result follows
  rw [‚Üê ENNReal.toReal_eq_toReal (measure_ne_top _ _) (measure_ne_top _ _), hŒº‚ÇÅ, hŒº‚ÇÇ, ‚Üê hj]
  -- ‚ä¢ ‚Üë(toSignedMeasure j‚ÇÅ) (i ‚à© S·∂ú) = ‚Üë(toSignedMeasure j‚ÇÅ) (i ‚à© T·∂ú)
  exact of_inter_eq_of_symmDiff_eq_zero_positive hS‚ÇÅ.compl hT‚ÇÅ.compl hi hS‚ÇÉ hT‚ÇÉ hST‚ÇÅ
  -- üéâ no goals
#align measure_theory.jordan_decomposition.to_signed_measure_injective MeasureTheory.JordanDecomposition.toSignedMeasure_injective

@[simp]
theorem toJordanDecomposition_toSignedMeasure (j : JordanDecomposition Œ±) :
    j.toSignedMeasure.toJordanDecomposition = j :=
  (@toSignedMeasure_injective _ _ j j.toSignedMeasure.toJordanDecomposition (by simp)).symm
                                                                                -- üéâ no goals
#align measure_theory.jordan_decomposition.to_jordan_decomposition_to_signed_measure MeasureTheory.JordanDecomposition.toJordanDecomposition_toSignedMeasure

end JordanDecomposition

namespace SignedMeasure

open JordanDecomposition

/-- `MeasureTheory.SignedMeasure.toJordanDecomposition` and
`MeasureTheory.JordanDecomposition.toSignedMeasure` form an `Equiv`. -/
@[simps apply symm_apply]
def toJordanDecompositionEquiv (Œ± : Type*) [MeasurableSpace Œ±] :
    SignedMeasure Œ± ‚âÉ JordanDecomposition Œ± where
  toFun := toJordanDecomposition
  invFun := toSignedMeasure
  left_inv := toSignedMeasure_toJordanDecomposition
  right_inv := toJordanDecomposition_toSignedMeasure
#align measure_theory.signed_measure.to_jordan_decomposition_equiv MeasureTheory.SignedMeasure.toJordanDecompositionEquiv
#align measure_theory.signed_measure.to_jordan_decomposition_equiv_apply MeasureTheory.SignedMeasure.toJordanDecompositionEquiv_apply
#align measure_theory.signed_measure.to_jordan_decomposition_equiv_symm_apply MeasureTheory.SignedMeasure.toJordanDecompositionEquiv_symm_apply

theorem toJordanDecomposition_zero : (0 : SignedMeasure Œ±).toJordanDecomposition = 0 := by
  apply toSignedMeasure_injective
  -- ‚ä¢ toSignedMeasure (toJordanDecomposition 0) = toSignedMeasure 0
  simp [toSignedMeasure_zero]
  -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition_zero MeasureTheory.SignedMeasure.toJordanDecomposition_zero

theorem toJordanDecomposition_neg (s : SignedMeasure Œ±) :
    (-s).toJordanDecomposition = -s.toJordanDecomposition := by
  apply toSignedMeasure_injective
  -- ‚ä¢ toSignedMeasure (toJordanDecomposition (-s)) = toSignedMeasure (-toJordanDec ‚Ä¶
  simp [toSignedMeasure_neg]
  -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition_neg MeasureTheory.SignedMeasure.toJordanDecomposition_neg

theorem toJordanDecomposition_smul (s : SignedMeasure Œ±) (r : ‚Ñù‚â•0) :
    (r ‚Ä¢ s).toJordanDecomposition = r ‚Ä¢ s.toJordanDecomposition := by
  apply toSignedMeasure_injective
  -- ‚ä¢ toSignedMeasure (toJordanDecomposition (r ‚Ä¢ s)) = toSignedMeasure (r ‚Ä¢ toJor ‚Ä¶
  simp [toSignedMeasure_smul]
  -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition_smul MeasureTheory.SignedMeasure.toJordanDecomposition_smul

private theorem toJordanDecomposition_smul_real_nonneg (s : SignedMeasure Œ±) (r : ‚Ñù)
    (hr : 0 ‚â§ r) : (r ‚Ä¢ s).toJordanDecomposition = r ‚Ä¢ s.toJordanDecomposition := by
  lift r to ‚Ñù‚â•0 using hr
  -- ‚ä¢ toJordanDecomposition (‚Üër ‚Ä¢ s) = ‚Üër ‚Ä¢ toJordanDecomposition s
  rw [JordanDecomposition.coe_smul, ‚Üê toJordanDecomposition_smul]
  -- ‚ä¢ toJordanDecomposition (‚Üër ‚Ä¢ s) = toJordanDecomposition (r ‚Ä¢ s)
  rfl
  -- üéâ no goals

theorem toJordanDecomposition_smul_real (s : SignedMeasure Œ±) (r : ‚Ñù) :
    (r ‚Ä¢ s).toJordanDecomposition = r ‚Ä¢ s.toJordanDecomposition := by
  by_cases hr : 0 ‚â§ r
  -- ‚ä¢ toJordanDecomposition (r ‚Ä¢ s) = r ‚Ä¢ toJordanDecomposition s
  ¬∑ exact toJordanDecomposition_smul_real_nonneg s r hr
    -- üéâ no goals
  ¬∑ ext1
    -- ‚ä¢ (toJordanDecomposition (r ‚Ä¢ s)).posPart = (r ‚Ä¢ toJordanDecomposition s).posP ‚Ä¶
    ¬∑ rw [real_smul_posPart_neg _ _ (not_le.1 hr),
        show r ‚Ä¢ s = -(-r ‚Ä¢ s) by rw [neg_smul, neg_neg], toJordanDecomposition_neg, neg_posPart,
        toJordanDecomposition_smul_real_nonneg, ‚Üê smul_negPart, real_smul_nonneg]
      all_goals exact Left.nonneg_neg_iff.2 (le_of_lt (not_le.1 hr))
      -- üéâ no goals
    ¬∑ rw [real_smul_negPart_neg _ _ (not_le.1 hr),
        show r ‚Ä¢ s = -(-r ‚Ä¢ s) by rw [neg_smul, neg_neg], toJordanDecomposition_neg, neg_negPart,
        toJordanDecomposition_smul_real_nonneg, ‚Üê smul_posPart, real_smul_nonneg]
      all_goals exact Left.nonneg_neg_iff.2 (le_of_lt (not_le.1 hr))
      -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition_smul_real MeasureTheory.SignedMeasure.toJordanDecomposition_smul_real

theorem toJordanDecomposition_eq {s : SignedMeasure Œ±} {j : JordanDecomposition Œ±}
    (h : s = j.toSignedMeasure) : s.toJordanDecomposition = j := by
  rw [h, toJordanDecomposition_toSignedMeasure]
  -- üéâ no goals
#align measure_theory.signed_measure.to_jordan_decomposition_eq MeasureTheory.SignedMeasure.toJordanDecomposition_eq

/-- The total variation of a signed measure. -/
def totalVariation (s : SignedMeasure Œ±) : Measure Œ± :=
  s.toJordanDecomposition.posPart + s.toJordanDecomposition.negPart
#align measure_theory.signed_measure.total_variation MeasureTheory.SignedMeasure.totalVariation

theorem totalVariation_zero : (0 : SignedMeasure Œ±).totalVariation = 0 := by
  simp [totalVariation, toJordanDecomposition_zero]
  -- üéâ no goals
#align measure_theory.signed_measure.total_variation_zero MeasureTheory.SignedMeasure.totalVariation_zero

theorem totalVariation_neg (s : SignedMeasure Œ±) : (-s).totalVariation = s.totalVariation := by
  simp [totalVariation, toJordanDecomposition_neg, add_comm]
  -- üéâ no goals
#align measure_theory.signed_measure.total_variation_neg MeasureTheory.SignedMeasure.totalVariation_neg

theorem null_of_totalVariation_zero (s : SignedMeasure Œ±) {i : Set Œ±}
    (hs : s.totalVariation i = 0) : s i = 0 := by
  rw [totalVariation, Measure.coe_add, Pi.add_apply, add_eq_zero_iff] at hs
  -- ‚ä¢ ‚Üës i = 0
  rw [‚Üê toSignedMeasure_toJordanDecomposition s, toSignedMeasure, VectorMeasure.coe_sub,
    Pi.sub_apply, Measure.toSignedMeasure_apply, Measure.toSignedMeasure_apply]
  by_cases hi : MeasurableSet i
  -- ‚ä¢ ((if MeasurableSet i then ENNReal.toReal (‚Üë‚Üë(toJordanDecomposition s).posPar ‚Ä¶
  ¬∑ rw [if_pos hi, if_pos hi]; simp [hs.1, hs.2]
    -- ‚ä¢ ENNReal.toReal (‚Üë‚Üë(toJordanDecomposition s).posPart i) - ENNReal.toReal (‚Üë‚Üë( ‚Ä¶
                               -- üéâ no goals
  ¬∑ simp [if_neg hi]
    -- üéâ no goals
#align measure_theory.signed_measure.null_of_total_variation_zero MeasureTheory.SignedMeasure.null_of_totalVariation_zero

theorem absolutelyContinuous_ennreal_iff (s : SignedMeasure Œ±) (Œº : VectorMeasure Œ± ‚Ñù‚â•0‚àû) :
    s ‚â™·µ• Œº ‚Üî s.totalVariation ‚â™ Œº.ennrealToMeasure := by
  constructor <;> intro h
  -- ‚ä¢ s ‚â™·µ• Œº ‚Üí totalVariation s ‚â™ VectorMeasure.ennrealToMeasure Œº
                  -- ‚ä¢ totalVariation s ‚â™ VectorMeasure.ennrealToMeasure Œº
                  -- ‚ä¢ s ‚â™·µ• Œº
  ¬∑ refine' Measure.AbsolutelyContinuous.mk fun S hS‚ÇÅ hS‚ÇÇ => _
    -- ‚ä¢ ‚Üë‚Üë(totalVariation s) S = 0
    obtain ‚ü®i, hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ, hpos, hneg‚ü© := s.toJordanDecomposition_spec
    -- ‚ä¢ ‚Üë‚Üë(totalVariation s) S = 0
    rw [totalVariation, Measure.add_apply, hpos, hneg, toMeasureOfZeroLE_apply _ _ _ hS‚ÇÅ,
      toMeasureOfLEZero_apply _ _ _ hS‚ÇÅ]
    rw [‚Üê VectorMeasure.AbsolutelyContinuous.ennrealToMeasure] at h
    -- ‚ä¢ ‚Üë{ val := ‚Üës (i ‚à© S), property := (_ : 0 ‚â§ ‚Üës (i ‚à© S)) } + ‚Üë{ val := -‚Üës (i·∂ú ‚Ä¶
    -- Porting note: added `‚Üê NNReal.eq_iff`
    simp [h (measure_mono_null (i.inter_subset_right S) hS‚ÇÇ),
      h (measure_mono_null (i·∂ú.inter_subset_right S) hS‚ÇÇ), ‚Üê NNReal.eq_iff]
  ¬∑ refine' VectorMeasure.AbsolutelyContinuous.mk fun S hS‚ÇÅ hS‚ÇÇ => _
    -- ‚ä¢ ‚Üës S = 0
    rw [‚Üê VectorMeasure.ennrealToMeasure_apply hS‚ÇÅ] at hS‚ÇÇ
    -- ‚ä¢ ‚Üës S = 0
    exact null_of_totalVariation_zero s (h hS‚ÇÇ)
    -- üéâ no goals
#align measure_theory.signed_measure.absolutely_continuous_ennreal_iff MeasureTheory.SignedMeasure.absolutelyContinuous_ennreal_iff

theorem totalVariation_absolutelyContinuous_iff (s : SignedMeasure Œ±) (Œº : Measure Œ±) :
    s.totalVariation ‚â™ Œº ‚Üî
      s.toJordanDecomposition.posPart ‚â™ Œº ‚àß s.toJordanDecomposition.negPart ‚â™ Œº := by
  constructor <;> intro h
  -- ‚ä¢ totalVariation s ‚â™ Œº ‚Üí (toJordanDecomposition s).posPart ‚â™ Œº ‚àß (toJordanDeco ‚Ä¶
                  -- ‚ä¢ (toJordanDecomposition s).posPart ‚â™ Œº ‚àß (toJordanDecomposition s).negPart ‚â™ Œº
                  -- ‚ä¢ totalVariation s ‚â™ Œº
  ¬∑ constructor
    -- ‚ä¢ (toJordanDecomposition s).posPart ‚â™ Œº
    all_goals
      refine' Measure.AbsolutelyContinuous.mk fun S _ hS‚ÇÇ => _
      have := h hS‚ÇÇ
      rw [totalVariation, Measure.add_apply, add_eq_zero_iff] at this
    exacts [this.1, this.2]
    -- üéâ no goals
  ¬∑ refine' Measure.AbsolutelyContinuous.mk fun S _ hS‚ÇÇ => _
    -- ‚ä¢ ‚Üë‚Üë(totalVariation s) S = 0
    rw [totalVariation, Measure.add_apply, h.1 hS‚ÇÇ, h.2 hS‚ÇÇ, add_zero]
    -- üéâ no goals
#align measure_theory.signed_measure.total_variation_absolutely_continuous_iff MeasureTheory.SignedMeasure.totalVariation_absolutelyContinuous_iff

-- TODO: Generalize to vector measures once total variation on vector measures is defined
theorem mutuallySingular_iff (s t : SignedMeasure Œ±) :
    s ‚üÇ·µ• t ‚Üî s.totalVariation ‚üÇ‚Çò t.totalVariation := by
  constructor
  -- ‚ä¢ s ‚üÇ·µ• t ‚Üí totalVariation s ‚üÇ‚Çò totalVariation t
  ¬∑ rintro ‚ü®u, hmeas, hu‚ÇÅ, hu‚ÇÇ‚ü©
    -- ‚ä¢ totalVariation s ‚üÇ‚Çò totalVariation t
    obtain ‚ü®i, hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ, hipos, hineg‚ü© := s.toJordanDecomposition_spec
    -- ‚ä¢ totalVariation s ‚üÇ‚Çò totalVariation t
    obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ, hj‚ÇÉ, hjpos, hjneg‚ü© := t.toJordanDecomposition_spec
    -- ‚ä¢ totalVariation s ‚üÇ‚Çò totalVariation t
    refine' ‚ü®u, hmeas, _, _‚ü©
    -- ‚ä¢ ‚Üë‚Üë(totalVariation s) u = 0
    ¬∑ rw [totalVariation, Measure.add_apply, hipos, hineg, toMeasureOfZeroLE_apply _ _ _ hmeas,
        toMeasureOfLEZero_apply _ _ _ hmeas]
      -- Porting note: added `‚Üê NNReal.eq_iff`
      simp [hu‚ÇÅ _ (Set.inter_subset_right _ _), ‚Üê NNReal.eq_iff]
      -- üéâ no goals
    ¬∑ rw [totalVariation, Measure.add_apply, hjpos, hjneg,
        toMeasureOfZeroLE_apply _ _ _ hmeas.compl,
        toMeasureOfLEZero_apply _ _ _ hmeas.compl]
      -- Porting note: added `‚Üê NNReal.eq_iff`
      simp [hu‚ÇÇ _ (Set.inter_subset_right _ _), ‚Üê NNReal.eq_iff]
      -- üéâ no goals
  ¬∑ rintro ‚ü®u, hmeas, hu‚ÇÅ, hu‚ÇÇ‚ü©
    -- ‚ä¢ s ‚üÇ·µ• t
    exact
      ‚ü®u, hmeas, fun t htu => null_of_totalVariation_zero _ (measure_mono_null htu hu‚ÇÅ),
        fun t htv => null_of_totalVariation_zero _ (measure_mono_null htv hu‚ÇÇ)‚ü©
#align measure_theory.signed_measure.mutually_singular_iff MeasureTheory.SignedMeasure.mutuallySingular_iff

theorem mutuallySingular_ennreal_iff (s : SignedMeasure Œ±) (Œº : VectorMeasure Œ± ‚Ñù‚â•0‚àû) :
    s ‚üÇ·µ• Œº ‚Üî s.totalVariation ‚üÇ‚Çò Œº.ennrealToMeasure := by
  constructor
  -- ‚ä¢ s ‚üÇ·µ• Œº ‚Üí totalVariation s ‚üÇ‚Çò VectorMeasure.ennrealToMeasure Œº
  ¬∑ rintro ‚ü®u, hmeas, hu‚ÇÅ, hu‚ÇÇ‚ü©
    -- ‚ä¢ totalVariation s ‚üÇ‚Çò VectorMeasure.ennrealToMeasure Œº
    obtain ‚ü®i, hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ, hpos, hneg‚ü© := s.toJordanDecomposition_spec
    -- ‚ä¢ totalVariation s ‚üÇ‚Çò VectorMeasure.ennrealToMeasure Œº
    refine' ‚ü®u, hmeas, _, _‚ü©
    -- ‚ä¢ ‚Üë‚Üë(totalVariation s) u = 0
    ¬∑ rw [totalVariation, Measure.add_apply, hpos, hneg, toMeasureOfZeroLE_apply _ _ _ hmeas,
        toMeasureOfLEZero_apply _ _ _ hmeas]
      -- Porting note: added `‚Üê NNReal.eq_iff`
      simp [hu‚ÇÅ _ (Set.inter_subset_right _ _), ‚Üê NNReal.eq_iff]
      -- üéâ no goals
    ¬∑ rw [VectorMeasure.ennrealToMeasure_apply hmeas.compl]
      -- ‚ä¢ ‚ÜëŒº u·∂ú = 0
      exact hu‚ÇÇ _ (Set.Subset.refl _)
      -- üéâ no goals
  ¬∑ rintro ‚ü®u, hmeas, hu‚ÇÅ, hu‚ÇÇ‚ü©
    -- ‚ä¢ s ‚üÇ·µ• Œº
    refine'
      VectorMeasure.MutuallySingular.mk u hmeas
        (fun t htu _ => null_of_totalVariation_zero _ (measure_mono_null htu hu‚ÇÅ)) fun t htv hmt =>
        _
    rw [‚Üê VectorMeasure.ennrealToMeasure_apply hmt]
    -- ‚ä¢ ‚Üë‚Üë(VectorMeasure.ennrealToMeasure Œº) t = 0
    exact measure_mono_null htv hu‚ÇÇ
    -- üéâ no goals
#align measure_theory.signed_measure.mutually_singular_ennreal_iff MeasureTheory.SignedMeasure.mutuallySingular_ennreal_iff

theorem totalVariation_mutuallySingular_iff (s : SignedMeasure Œ±) (Œº : Measure Œ±) :
    s.totalVariation ‚üÇ‚Çò Œº ‚Üî
      s.toJordanDecomposition.posPart ‚üÇ‚Çò Œº ‚àß s.toJordanDecomposition.negPart ‚üÇ‚Çò Œº :=
  Measure.MutuallySingular.add_left_iff
#align measure_theory.signed_measure.total_variation_mutually_singular_iff MeasureTheory.SignedMeasure.totalVariation_mutuallySingular_iff

end SignedMeasure

end MeasureTheory
