/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.Data.Set.Countable
import Mathlib.Logic.Encodable.Lattice
import Mathlib.Order.Disjointed
import Mathlib.Tactic.Measurability

#align_import measure_theory.measurable_space_def from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Measurable spaces and measurable functions

This file defines measurable spaces and measurable functions.

A measurable space is a set equipped with a Ïƒ-algebra, a collection of
subsets closed under complementation and countable union. A function
between measurable spaces is measurable if the preimage of each
measurable subset is measurable.

Ïƒ-algebras on a fixed set `Î±` form a complete lattice. Here we order
Ïƒ-algebras by writing `mâ‚ â‰¤ mâ‚‚` if every set which is `mâ‚`-measurable is
also `mâ‚‚`-measurable (that is, `mâ‚` is a subset of `mâ‚‚`). In particular, any
collection of subsets of `Î±` generates a smallest Ïƒ-algebra which
contains all of them.

## References

* <https://en.wikipedia.org/wiki/Measurable_space>
* <https://en.wikipedia.org/wiki/Sigma-algebra>
* <https://en.wikipedia.org/wiki/Dynkin_system>

## Tags

measurable space, Ïƒ-algebra, measurable function
-/


open Set Encodable Function Equiv

open Classical

variable {Î± Î² Î³ Î´ Î´' : Type*} {Î¹ : Sort*} {s t u : Set Î±}

/-- A measurable space is a space equipped with a Ïƒ-algebra. -/
@[class] structure MeasurableSpace (Î± : Type*) where
  /-- Predicate saying that a given set is measurable. Use `MeasurableSet` in the root namespace
  instead. -/
  MeasurableSet' : Set Î± â†’ Prop
  /-- The empty set is a measurable set. Use `MeasurableSet.empty` instead. -/
  measurableSet_empty : MeasurableSet' âˆ…
  /-- The complement of a measurable set is a measurable set. Use `MeasurableSet.compl` instead. -/
  measurableSet_compl : âˆ€ s, MeasurableSet' s â†’ MeasurableSet' sá¶œ
  /-- The union of a sequence of measurable sets is a measurable set. Use a more general
  `MeasurableSet.iUnion` instead. -/
  measurableSet_iUnion : âˆ€ f : â„• â†’ Set Î±, (âˆ€ i, MeasurableSet' (f i)) â†’ MeasurableSet' (â‹ƒ i, f i)
#align measurable_space MeasurableSpace

instance [h : MeasurableSpace Î±] : MeasurableSpace Î±áµ’áµˆ := h

/-- `MeasurableSet s` means that `s` is measurable (in the ambient measure space on `Î±`) -/
def MeasurableSet [MeasurableSpace Î±] (s : Set Î±) : Prop :=
  â€¹MeasurableSpace Î±â€º.MeasurableSet' s
#align measurable_set MeasurableSet

-- porting note: todo: `scoped[MeasureTheory]` doesn't work for unknown reason
namespace MeasureTheory
set_option quotPrecheck false in
/-- Notation for `MeasurableSet` with respect to a non-standanrd Ïƒ-algebra. -/
scoped notation "MeasurableSet[" m "]" => @MeasurableSet _ m

end MeasureTheory
open MeasureTheory

section

@[simp, measurability]
theorem MeasurableSet.empty [MeasurableSpace Î±] : MeasurableSet (âˆ… : Set Î±) :=
  MeasurableSpace.measurableSet_empty _
#align measurable_set.empty MeasurableSet.empty

variable {m : MeasurableSpace Î±}

@[measurability]
protected theorem MeasurableSet.compl : MeasurableSet s â†’ MeasurableSet sá¶œ :=
  MeasurableSpace.measurableSet_compl _ s
#align measurable_set.compl MeasurableSet.compl

protected theorem MeasurableSet.of_compl (h : MeasurableSet sá¶œ) : MeasurableSet s :=
  compl_compl s â–¸ h.compl
#align measurable_set.of_compl MeasurableSet.of_compl

@[simp]
theorem MeasurableSet.compl_iff : MeasurableSet sá¶œ â†” MeasurableSet s :=
  âŸ¨.of_compl, .complâŸ©
#align measurable_set.compl_iff MeasurableSet.compl_iff

@[simp, measurability]
protected theorem MeasurableSet.univ : MeasurableSet (univ : Set Î±) :=
  .of_compl <| by simp
                  -- ğŸ‰ no goals
#align measurable_set.univ MeasurableSet.univ

@[nontriviality, measurability]
theorem Subsingleton.measurableSet [Subsingleton Î±] {s : Set Î±} : MeasurableSet s :=
  Subsingleton.set_cases MeasurableSet.empty MeasurableSet.univ s
#align subsingleton.measurable_set Subsingleton.measurableSet

theorem MeasurableSet.congr {s t : Set Î±} (hs : MeasurableSet s) (h : s = t) : MeasurableSet t := by
  rwa [â† h]
  -- ğŸ‰ no goals
#align measurable_set.congr MeasurableSet.congr

@[measurability]
protected theorem MeasurableSet.iUnion [Countable Î¹] â¦ƒf : Î¹ â†’ Set Î±â¦„
    (h : âˆ€ b, MeasurableSet (f b)) : MeasurableSet (â‹ƒ b, f b) := by
  cases isEmpty_or_nonempty Î¹
  -- âŠ¢ MeasurableSet (â‹ƒ (b : Î¹), f b)
  Â· simp
    -- ğŸ‰ no goals
  Â· rcases exists_surjective_nat Î¹ with âŸ¨e, heâŸ©
    -- âŠ¢ MeasurableSet (â‹ƒ (b : Î¹), f b)
    rw [â† iUnion_congr_of_surjective _ he (fun _ => rfl)]
    -- âŠ¢ MeasurableSet (â‹ƒ (x : â„•), f (e x))
    exact m.measurableSet_iUnion _ fun _ => h _
    -- ğŸ‰ no goals
#align measurable_set.Union MeasurableSet.iUnion

@[deprecated MeasurableSet.iUnion]
theorem MeasurableSet.biUnion_decodeâ‚‚ [Encodable Î²] â¦ƒf : Î² â†’ Set Î±â¦„ (h : âˆ€ b, MeasurableSet (f b))
    (n : â„•) : MeasurableSet (â‹ƒ b âˆˆ decodeâ‚‚ Î² n, f b) :=
  .iUnion fun _ => .iUnion fun _ => h _
#align measurable_set.bUnion_decodeâ‚‚ MeasurableSet.biUnion_decodeâ‚‚

protected theorem MeasurableSet.biUnion {f : Î² â†’ Set Î±} {s : Set Î²} (hs : s.Countable)
    (h : âˆ€ b âˆˆ s, MeasurableSet (f b)) : MeasurableSet (â‹ƒ b âˆˆ s, f b) := by
  rw [biUnion_eq_iUnion]
  -- âŠ¢ MeasurableSet (â‹ƒ (x : â†‘s), f â†‘x)
  have := hs.to_subtype
  -- âŠ¢ MeasurableSet (â‹ƒ (x : â†‘s), f â†‘x)
  exact MeasurableSet.iUnion (by simpa using h)
  -- ğŸ‰ no goals
#align measurable_set.bUnion MeasurableSet.biUnion

theorem Set.Finite.measurableSet_biUnion {f : Î² â†’ Set Î±} {s : Set Î²} (hs : s.Finite)
    (h : âˆ€ b âˆˆ s, MeasurableSet (f b)) : MeasurableSet (â‹ƒ b âˆˆ s, f b) :=
  .biUnion hs.countable h
#align set.finite.measurable_set_bUnion Set.Finite.measurableSet_biUnion

theorem Finset.measurableSet_biUnion {f : Î² â†’ Set Î±} (s : Finset Î²)
    (h : âˆ€ b âˆˆ s, MeasurableSet (f b)) : MeasurableSet (â‹ƒ b âˆˆ s, f b) :=
  s.finite_toSet.measurableSet_biUnion h
#align finset.measurable_set_bUnion Finset.measurableSet_biUnion

protected theorem MeasurableSet.sUnion {s : Set (Set Î±)} (hs : s.Countable)
    (h : âˆ€ t âˆˆ s, MeasurableSet t) : MeasurableSet (â‹ƒâ‚€ s) := by
  rw [sUnion_eq_biUnion]
  -- âŠ¢ MeasurableSet (â‹ƒ (i : Set Î±) (_ : i âˆˆ s), i)
  exact .biUnion hs h
  -- ğŸ‰ no goals
#align measurable_set.sUnion MeasurableSet.sUnion

theorem Set.Finite.measurableSet_sUnion {s : Set (Set Î±)} (hs : s.Finite)
    (h : âˆ€ t âˆˆ s, MeasurableSet t) : MeasurableSet (â‹ƒâ‚€ s) :=
  MeasurableSet.sUnion hs.countable h
#align set.finite.measurable_set_sUnion Set.Finite.measurableSet_sUnion

@[measurability]
theorem MeasurableSet.iInter [Countable Î¹] {f : Î¹ â†’ Set Î±} (h : âˆ€ b, MeasurableSet (f b)) :
    MeasurableSet (â‹‚ b, f b) :=
  .of_compl <| by rw [compl_iInter]; exact .iUnion fun b => (h b).compl
                  -- âŠ¢ MeasurableSet (â‹ƒ (i : Î¹), (f i)á¶œ)
                                     -- ğŸ‰ no goals
#align measurable_set.Inter MeasurableSet.iInter

theorem MeasurableSet.biInter {f : Î² â†’ Set Î±} {s : Set Î²} (hs : s.Countable)
    (h : âˆ€ b âˆˆ s, MeasurableSet (f b)) : MeasurableSet (â‹‚ b âˆˆ s, f b) :=
  .of_compl <| by rw [compl_iInterâ‚‚]; exact .biUnion hs fun b hb => (h b hb).compl
                  -- âŠ¢ MeasurableSet (â‹ƒ (i : Î²) (_ : i âˆˆ s), (f i)á¶œ)
                                      -- ğŸ‰ no goals
#align measurable_set.bInter MeasurableSet.biInter

theorem Set.Finite.measurableSet_biInter {f : Î² â†’ Set Î±} {s : Set Î²} (hs : s.Finite)
    (h : âˆ€ b âˆˆ s, MeasurableSet (f b)) : MeasurableSet (â‹‚ b âˆˆ s, f b) :=
 .biInter hs.countable h
#align set.finite.measurable_set_bInter Set.Finite.measurableSet_biInter

theorem Finset.measurableSet_biInter {f : Î² â†’ Set Î±} (s : Finset Î²)
    (h : âˆ€ b âˆˆ s, MeasurableSet (f b)) : MeasurableSet (â‹‚ b âˆˆ s, f b) :=
  s.finite_toSet.measurableSet_biInter h
#align finset.measurable_set_bInter Finset.measurableSet_biInter

theorem MeasurableSet.sInter {s : Set (Set Î±)} (hs : s.Countable) (h : âˆ€ t âˆˆ s, MeasurableSet t) :
    MeasurableSet (â‹‚â‚€ s) := by
  rw [sInter_eq_biInter]
  -- âŠ¢ MeasurableSet (â‹‚ (i : Set Î±) (_ : i âˆˆ s), i)
  exact MeasurableSet.biInter hs h
  -- ğŸ‰ no goals
#align measurable_set.sInter MeasurableSet.sInter

theorem Set.Finite.measurableSet_sInter {s : Set (Set Î±)} (hs : s.Finite)
    (h : âˆ€ t âˆˆ s, MeasurableSet t) : MeasurableSet (â‹‚â‚€ s) :=
  MeasurableSet.sInter hs.countable h
#align set.finite.measurable_set_sInter Set.Finite.measurableSet_sInter

@[simp, measurability]
protected theorem MeasurableSet.union {sâ‚ sâ‚‚ : Set Î±} (hâ‚ : MeasurableSet sâ‚)
    (hâ‚‚ : MeasurableSet sâ‚‚) : MeasurableSet (sâ‚ âˆª sâ‚‚) := by
  rw [union_eq_iUnion]
  -- âŠ¢ MeasurableSet (â‹ƒ (b : Bool), bif b then sâ‚ else sâ‚‚)
  exact .iUnion (Bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©)
  -- ğŸ‰ no goals
#align measurable_set.union MeasurableSet.union

@[simp, measurability]
protected theorem MeasurableSet.inter {sâ‚ sâ‚‚ : Set Î±} (hâ‚ : MeasurableSet sâ‚)
    (hâ‚‚ : MeasurableSet sâ‚‚) : MeasurableSet (sâ‚ âˆ© sâ‚‚) := by
  rw [inter_eq_compl_compl_union_compl]
  -- âŠ¢ MeasurableSet (sâ‚á¶œ âˆª sâ‚‚á¶œ)á¶œ
  exact (hâ‚.compl.union hâ‚‚.compl).compl
  -- ğŸ‰ no goals
#align measurable_set.inter MeasurableSet.inter

@[simp, measurability]
protected theorem MeasurableSet.diff {sâ‚ sâ‚‚ : Set Î±} (hâ‚ : MeasurableSet sâ‚)
    (hâ‚‚ : MeasurableSet sâ‚‚) : MeasurableSet (sâ‚ \ sâ‚‚) :=
  hâ‚.inter hâ‚‚.compl
#align measurable_set.diff MeasurableSet.diff

@[simp, measurability]
protected theorem MeasurableSet.symmDiff {sâ‚ sâ‚‚ : Set Î±} (hâ‚ : MeasurableSet sâ‚)
    (hâ‚‚ : MeasurableSet sâ‚‚) : MeasurableSet (sâ‚ âˆ† sâ‚‚) :=
  (hâ‚.diff hâ‚‚).union (hâ‚‚.diff hâ‚)
#align measurable_set.symm_diff MeasurableSet.symmDiff

@[simp, measurability]
protected theorem MeasurableSet.ite {t sâ‚ sâ‚‚ : Set Î±} (ht : MeasurableSet t)
    (hâ‚ : MeasurableSet sâ‚) (hâ‚‚ : MeasurableSet sâ‚‚) : MeasurableSet (t.ite sâ‚ sâ‚‚) :=
  (hâ‚.inter ht).union (hâ‚‚.diff ht)
#align measurable_set.ite MeasurableSet.ite

theorem MeasurableSet.ite' {s t : Set Î±} {p : Prop} (hs : p â†’ MeasurableSet s)
    (ht : Â¬p â†’ MeasurableSet t) : MeasurableSet (ite p s t) := by
  split_ifs with h
  -- âŠ¢ MeasurableSet s
  exacts [hs h, ht h]
  -- ğŸ‰ no goals
#align measurable_set.ite' MeasurableSet.ite'

@[simp, measurability]
protected theorem MeasurableSet.cond {sâ‚ sâ‚‚ : Set Î±} (hâ‚ : MeasurableSet sâ‚)
    (hâ‚‚ : MeasurableSet sâ‚‚) {i : Bool} : MeasurableSet (cond i sâ‚ sâ‚‚) := by
  cases i
  -- âŠ¢ MeasurableSet (bif false then sâ‚ else sâ‚‚)
  exacts [hâ‚‚, hâ‚]
  -- ğŸ‰ no goals
#align measurable_set.cond MeasurableSet.cond

@[simp, measurability]
protected theorem MeasurableSet.disjointed {f : â„• â†’ Set Î±} (h : âˆ€ i, MeasurableSet (f i)) (n) :
    MeasurableSet (disjointed f n) :=
  disjointedRec (fun _ _ ht => MeasurableSet.diff ht <| h _) (h n)
#align measurable_set.disjointed MeasurableSet.disjointed

@[simp]
protected theorem MeasurableSet.const (p : Prop) : MeasurableSet { _a : Î± | p } := by
  by_cases p <;> simp [*]
  -- âŠ¢ MeasurableSet {_a | p}
  -- âŠ¢ MeasurableSet {_a | p}
                 -- ğŸ‰ no goals
                 -- ğŸ‰ no goals
#align measurable_set.const MeasurableSet.const

/-- Every set has a measurable superset. Declare this as local instance as needed. -/
theorem nonempty_measurable_superset (s : Set Î±) : Nonempty { t // s âŠ† t âˆ§ MeasurableSet t } :=
  âŸ¨âŸ¨univ, subset_univ s, MeasurableSet.univâŸ©âŸ©
#align nonempty_measurable_superset nonempty_measurable_superset

end

theorem MeasurableSpace.measurableSet_injective : Injective (@MeasurableSet Î±)
  | âŸ¨_, _, _, _âŸ©, âŸ¨_, _, _, _âŸ©, _ => by congr
                                        -- ğŸ‰ no goals

@[ext]
theorem MeasurableSpace.ext {mâ‚ mâ‚‚ : MeasurableSpace Î±}
    (h : âˆ€ s : Set Î±, MeasurableSet[mâ‚] s â†” MeasurableSet[mâ‚‚] s) : mâ‚ = mâ‚‚ :=
  measurableSet_injective <| funext fun s => propext (h s)
#align measurable_space.ext MeasurableSpace.ext

theorem MeasurableSpace.ext_iff {mâ‚ mâ‚‚ : MeasurableSpace Î±} :
    mâ‚ = mâ‚‚ â†” âˆ€ s : Set Î±, MeasurableSet[mâ‚] s â†” MeasurableSet[mâ‚‚] s :=
  âŸ¨fun h _ => h â–¸ Iff.rfl, MeasurableSpace.extâŸ©
#align measurable_space.ext_iff MeasurableSpace.ext_iff

/-- A typeclass mixin for `MeasurableSpace`s such that each singleton is measurable. -/
class MeasurableSingletonClass (Î± : Type*) [MeasurableSpace Î±] : Prop where
  /-- A singleton is a measurable set. -/
  measurableSet_singleton : âˆ€ x, MeasurableSet ({x} : Set Î±)
#align measurable_singleton_class MeasurableSingletonClass

export MeasurableSingletonClass (measurableSet_singleton)

@[simp]
lemma MeasurableSet.singleton [MeasurableSpace Î±] [MeasurableSingletonClass Î±] (a : Î±) :
    MeasurableSet {a} :=
  measurableSet_singleton a

section MeasurableSingletonClass

variable [MeasurableSpace Î±] [MeasurableSingletonClass Î±]

@[measurability]
theorem measurableSet_eq {a : Î±} : MeasurableSet { x | x = a } := .singleton a
#align measurable_set_eq measurableSet_eq

@[measurability]
protected theorem MeasurableSet.insert {s : Set Î±} (hs : MeasurableSet s) (a : Î±) :
    MeasurableSet (insert a s) :=
  .union (.singleton a) hs
#align measurable_set.insert MeasurableSet.insert

@[simp]
theorem measurableSet_insert {a : Î±} {s : Set Î±} : MeasurableSet (insert a s) â†” MeasurableSet s :=
  âŸ¨fun h =>
    if ha : a âˆˆ s then by rwa [â† insert_eq_of_mem ha]
                          -- ğŸ‰ no goals
    else insert_diff_self_of_not_mem ha â–¸ h.diff (.singleton _),
    fun h => h.insert aâŸ©
#align measurable_set_insert measurableSet_insert

theorem Set.Subsingleton.measurableSet {s : Set Î±} (hs : s.Subsingleton) : MeasurableSet s :=
  hs.induction_on .empty .singleton
#align set.subsingleton.measurable_set Set.Subsingleton.measurableSet

theorem Set.Finite.measurableSet {s : Set Î±} (hs : s.Finite) : MeasurableSet s :=
  Finite.induction_on hs MeasurableSet.empty fun _ _ hsm => hsm.insert _
#align set.finite.measurable_set Set.Finite.measurableSet

@[measurability]
protected theorem Finset.measurableSet (s : Finset Î±) : MeasurableSet (â†‘s : Set Î±) :=
  s.finite_toSet.measurableSet
#align finset.measurable_set Finset.measurableSet

theorem Set.Countable.measurableSet {s : Set Î±} (hs : s.Countable) : MeasurableSet s := by
  rw [â† biUnion_of_singleton s]
  -- âŠ¢ MeasurableSet (â‹ƒ (x : Î±) (_ : x âˆˆ s), {x})
  exact .biUnion hs fun b _ => .singleton b
  -- ğŸ‰ no goals
#align set.countable.measurable_set Set.Countable.measurableSet

end MeasurableSingletonClass

namespace MeasurableSpace

/-- Copy of a `MeasurableSpace` with a new `MeasurableSet` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (m : MeasurableSpace Î±) (p : Set Î± â†’ Prop) (h : âˆ€ s, p s â†” MeasurableSet[m] s) :
    MeasurableSpace Î± where
  MeasurableSet' := p
  measurableSet_empty := by simpa only [h] using m.measurableSet_empty
                            -- ğŸ‰ no goals
  measurableSet_compl := by simpa only [h] using m.measurableSet_compl
                            -- ğŸ‰ no goals
  measurableSet_iUnion := by simpa only [h] using m.measurableSet_iUnion
                             -- ğŸ‰ no goals

lemma measurableSet_copy {m : MeasurableSpace Î±} {p : Set Î± â†’ Prop}
    (h : âˆ€ s, p s â†” MeasurableSet[m] s) {s} : MeasurableSet[.copy m p h] s â†” p s :=
  Iff.rfl

lemma copy_eq {m : MeasurableSpace Î±} {p : Set Î± â†’ Prop} (h : âˆ€ s, p s â†” MeasurableSet[m] s) :
    m.copy p h = m :=
  ext h

section CompleteLattice

instance : LE (MeasurableSpace Î±) where le mâ‚ mâ‚‚ := âˆ€ s, MeasurableSet[mâ‚] s â†’ MeasurableSet[mâ‚‚] s

theorem le_def {Î±} {a b : MeasurableSpace Î±} : a â‰¤ b â†” a.MeasurableSet' â‰¤ b.MeasurableSet' :=
  Iff.rfl
#align measurable_space.le_def MeasurableSpace.le_def

instance : PartialOrder (MeasurableSpace Î±) :=
  { PartialOrder.lift (@MeasurableSet Î±) measurableSet_injective with
    le := LE.le
    lt := fun mâ‚ mâ‚‚ => mâ‚ â‰¤ mâ‚‚ âˆ§ Â¬mâ‚‚ â‰¤ mâ‚ }

/-- The smallest Ïƒ-algebra containing a collection `s` of basic sets -/
inductive GenerateMeasurable (s : Set (Set Î±)) : Set Î± â†’ Prop
  | protected basic : âˆ€ u âˆˆ s, GenerateMeasurable s u
  | protected empty : GenerateMeasurable s âˆ…
  | protected compl : âˆ€ t, GenerateMeasurable s t â†’ GenerateMeasurable s tá¶œ
  | protected iUnion : âˆ€ f : â„• â†’ Set Î±, (âˆ€ n, GenerateMeasurable s (f n)) â†’
      GenerateMeasurable s (â‹ƒ i, f i)
#align measurable_space.generate_measurable MeasurableSpace.GenerateMeasurable

/-- Construct the smallest measure space containing a collection of basic sets -/
def generateFrom (s : Set (Set Î±)) : MeasurableSpace Î± where
  MeasurableSet' := GenerateMeasurable s
  measurableSet_empty := .empty
  measurableSet_compl := .compl
  measurableSet_iUnion := .iUnion
#align measurable_space.generate_from MeasurableSpace.generateFrom

theorem measurableSet_generateFrom {s : Set (Set Î±)} {t : Set Î±} (ht : t âˆˆ s) :
    MeasurableSet[generateFrom s] t :=
  .basic t ht
#align measurable_space.measurable_set_generate_from MeasurableSpace.measurableSet_generateFrom

@[elab_as_elim]
theorem generateFrom_induction (p : Set Î± â†’ Prop) (C : Set (Set Î±)) (hC : âˆ€ t âˆˆ C, p t)
    (h_empty : p âˆ…) (h_compl : âˆ€ t, p t â†’ p tá¶œ)
    (h_Union : âˆ€ f : â„• â†’ Set Î±, (âˆ€ n, p (f n)) â†’ p (â‹ƒ i, f i)) {s : Set Î±}
    (hs : MeasurableSet[generateFrom C] s) : p s := by
  induction hs
  exacts [hC _ â€¹_â€º, h_empty, h_compl _ â€¹_â€º, h_Union â€¹_â€º â€¹_â€º]
  -- ğŸ‰ no goals
#align measurable_space.generate_from_induction MeasurableSpace.generateFrom_induction

theorem generateFrom_le {s : Set (Set Î±)} {m : MeasurableSpace Î±}
    (h : âˆ€ t âˆˆ s, MeasurableSet[m] t) : generateFrom s â‰¤ m :=
  fun t (ht : GenerateMeasurable s t) =>
  ht.recOn h .empty (fun _ _ => .compl) fun _ _ hf => .iUnion hf
#align measurable_space.generate_from_le MeasurableSpace.generateFrom_le

theorem generateFrom_le_iff {s : Set (Set Î±)} (m : MeasurableSpace Î±) :
    generateFrom s â‰¤ m â†” s âŠ† { t | MeasurableSet[m] t } :=
  Iff.intro (fun h _ hu => h _ <| measurableSet_generateFrom hu) fun h => generateFrom_le h
#align measurable_space.generate_from_le_iff MeasurableSpace.generateFrom_le_iff

@[simp]
theorem generateFrom_measurableSet [MeasurableSpace Î±] :
    generateFrom { s : Set Î± | MeasurableSet s } = â€¹_â€º :=
  le_antisymm (generateFrom_le fun _ => id) fun _ => measurableSet_generateFrom
#align measurable_space.generate_from_measurable_set MeasurableSpace.generateFrom_measurableSet

theorem forall_generateFrom_mem_iff_mem_iff {S : Set (Set Î±)} {x y : Î±} :
    (âˆ€ s, MeasurableSet[generateFrom S] s â†’ (x âˆˆ s â†” y âˆˆ s)) â†” (âˆ€ s âˆˆ S, x âˆˆ s â†” y âˆˆ s) := by
  refine âŸ¨fun H s hs â†¦ H s (.basic s hs), fun H s â†¦ ?_âŸ©
  -- âŠ¢ MeasurableSet s â†’ (x âˆˆ s â†” y âˆˆ s)
  apply generateFrom_induction
  Â· exact H
    -- ğŸ‰ no goals
  Â· rfl
    -- ğŸ‰ no goals
  Â· exact fun _ â†¦ Iff.not
    -- ğŸ‰ no goals
  Â· intro f hf
    -- âŠ¢ x âˆˆ â‹ƒ (i : â„•), f i â†” y âˆˆ â‹ƒ (i : â„•), f i
    simp only [mem_iUnion, hf]
    -- ğŸ‰ no goals

/-- If `g` is a collection of subsets of `Î±` such that the `Ïƒ`-algebra generated from `g` contains
the same sets as `g`, then `g` was already a `Ïƒ`-algebra. -/
protected def mkOfClosure (g : Set (Set Î±)) (hg : { t | MeasurableSet[generateFrom g] t } = g) :
    MeasurableSpace Î± :=
  (generateFrom g).copy (Â· âˆˆ g) <| Set.ext_iff.1 hg.symm
#align measurable_space.mk_of_closure MeasurableSpace.mkOfClosure

theorem mkOfClosure_sets {s : Set (Set Î±)} {hs : { t | MeasurableSet[generateFrom s] t } = s} :
    MeasurableSpace.mkOfClosure s hs = generateFrom s :=
  copy_eq _
#align measurable_space.mk_of_closure_sets MeasurableSpace.mkOfClosure_sets

/-- We get a Galois insertion between `Ïƒ`-algebras on `Î±` and `Set (Set Î±)` by using `generate_from`
  on one side and the collection of measurable sets on the other side. -/
def giGenerateFrom : GaloisInsertion (@generateFrom Î±) fun m => { t | MeasurableSet[m] t } where
  gc _ := generateFrom_le_iff
  le_l_u _ _ := measurableSet_generateFrom
  choice g hg := MeasurableSpace.mkOfClosure g <| le_antisymm hg <| (generateFrom_le_iff _).1 le_rfl
  choice_eq _ _ := mkOfClosure_sets
#align measurable_space.gi_generate_from MeasurableSpace.giGenerateFrom

instance : CompleteLattice (MeasurableSpace Î±) :=
  giGenerateFrom.liftCompleteLattice

instance : Inhabited (MeasurableSpace Î±) := âŸ¨âŠ¤âŸ©

@[mono]
theorem generateFrom_mono {s t : Set (Set Î±)} (h : s âŠ† t) : generateFrom s â‰¤ generateFrom t :=
  giGenerateFrom.gc.monotone_l h
#align measurable_space.generate_from_mono MeasurableSpace.generateFrom_mono

theorem generateFrom_sup_generateFrom {s t : Set (Set Î±)} :
    generateFrom s âŠ” generateFrom t = generateFrom (s âˆª t) :=
  (@giGenerateFrom Î±).gc.l_sup.symm
#align measurable_space.generate_from_sup_generate_from MeasurableSpace.generateFrom_sup_generateFrom

@[simp]
theorem generateFrom_singleton_empty : generateFrom {âˆ…} = (âŠ¥ : MeasurableSpace Î±) :=
  bot_unique $ generateFrom_le <| by simp [@MeasurableSet.empty Î± âŠ¥]
                                     -- ğŸ‰ no goals
#align measurable_space.generate_from_singleton_empty MeasurableSpace.generateFrom_singleton_empty

@[simp]
theorem generateFrom_singleton_univ : generateFrom {Set.univ} = (âŠ¥ : MeasurableSpace Î±) :=
  bot_unique $ generateFrom_le <| by simp
                                     -- ğŸ‰ no goals
#align measurable_space.generate_from_singleton_univ MeasurableSpace.generateFrom_singleton_univ

@[simp]
theorem generateFrom_insert_univ (S : Set (Set Î±)) :
    generateFrom (insert Set.univ S) = generateFrom S := by
  rw [insert_eq, â† generateFrom_sup_generateFrom, generateFrom_singleton_univ, bot_sup_eq]
  -- ğŸ‰ no goals
#align measurable_space.generate_from_insert_univ MeasurableSpace.generateFrom_insert_univ

@[simp]
theorem generateFrom_insert_empty (S : Set (Set Î±)) :
    generateFrom (insert âˆ… S) = generateFrom S := by
  rw [insert_eq, â† generateFrom_sup_generateFrom, generateFrom_singleton_empty, bot_sup_eq]
  -- ğŸ‰ no goals
#align measurable_space.generate_from_insert_empty MeasurableSpace.generateFrom_insert_empty

theorem measurableSet_bot_iff {s : Set Î±} : MeasurableSet[âŠ¥] s â†” s = âˆ… âˆ¨ s = univ :=
  let b : MeasurableSpace Î± :=
    { MeasurableSet' := fun s => s = âˆ… âˆ¨ s = univ
      measurableSet_empty := Or.inl rfl
      measurableSet_compl := by simp (config := { contextual := true }) [or_imp]
                                -- ğŸ‰ no goals
      measurableSet_iUnion := fun f hf => sUnion_mem_empty_univ (forall_range_iff.2 hf) }
  have : b = âŠ¥ :=
    bot_unique fun s hs =>
      hs.elim (fun s => s.symm â–¸ @measurableSet_empty _ âŠ¥) fun s =>
        s.symm â–¸ @MeasurableSet.univ _ âŠ¥
  this â–¸ Iff.rfl
#align measurable_space.measurable_set_bot_iff MeasurableSpace.measurableSet_bot_iff

@[simp, measurability] theorem measurableSet_top {s : Set Î±} : MeasurableSet[âŠ¤] s := trivial
#align measurable_space.measurable_set_top MeasurableSpace.measurableSet_top

@[simp, nolint simpNF] -- porting note: todo: `simpNF` claims that this lemma doesn't simplify LHS
theorem measurableSet_inf {mâ‚ mâ‚‚ : MeasurableSpace Î±} {s : Set Î±} :
    MeasurableSet[mâ‚ âŠ“ mâ‚‚] s â†” MeasurableSet[mâ‚] s âˆ§ MeasurableSet[mâ‚‚] s :=
  Iff.rfl
#align measurable_space.measurable_set_inf MeasurableSpace.measurableSet_inf

@[simp]
theorem measurableSet_sInf {ms : Set (MeasurableSpace Î±)} {s : Set Î±} :
    MeasurableSet[sInf ms] s â†” âˆ€ m âˆˆ ms, MeasurableSet[m] s :=
  show s âˆˆ â‹‚â‚€ _ â†” _ by simp
                       -- ğŸ‰ no goals
#align measurable_space.measurable_set_Inf MeasurableSpace.measurableSet_sInf

theorem measurableSet_iInf {Î¹} {m : Î¹ â†’ MeasurableSpace Î±} {s : Set Î±} :
    MeasurableSet[iInf m] s â†” âˆ€ i, MeasurableSet[m i] s := by
  rw [iInf, measurableSet_sInf, forall_range_iff]
  -- ğŸ‰ no goals
#align measurable_space.measurable_set_infi MeasurableSpace.measurableSet_iInf

theorem measurableSet_sup {mâ‚ mâ‚‚ : MeasurableSpace Î±} {s : Set Î±} :
    MeasurableSet[mâ‚ âŠ” mâ‚‚] s â†” GenerateMeasurable (MeasurableSet[mâ‚] âˆª MeasurableSet[mâ‚‚]) s :=
  Iff.rfl
#align measurable_space.measurable_set_sup MeasurableSpace.measurableSet_sup

theorem measurableSet_sSup {ms : Set (MeasurableSpace Î±)} {s : Set Î±} :
    MeasurableSet[sSup ms] s â†”
      GenerateMeasurable { s : Set Î± | âˆƒ m âˆˆ ms, MeasurableSet[m] s } s := by
  change GenerateMeasurable (â‹ƒâ‚€ _) _ â†” _
  -- âŠ¢ GenerateMeasurable (â‹ƒâ‚€ ((fun m => {t | MeasurableSet t}) '' ms)) s â†” Generat â€¦
  simp [â† setOf_exists]
  -- ğŸ‰ no goals
#align measurable_space.measurable_set_Sup MeasurableSpace.measurableSet_sSup

theorem measurableSet_iSup {Î¹} {m : Î¹ â†’ MeasurableSpace Î±} {s : Set Î±} :
    MeasurableSet[iSup m] s â†” GenerateMeasurable { s : Set Î± | âˆƒ i, MeasurableSet[m i] s } s :=
  by simp only [iSup, measurableSet_sSup, exists_range_iff]
     -- ğŸ‰ no goals
#align measurable_space.measurable_set_supr MeasurableSpace.measurableSet_iSup

theorem measurableSpace_iSup_eq (m : Î¹ â†’ MeasurableSpace Î±) :
    â¨† n, m n = generateFrom { s | âˆƒ n, MeasurableSet[m n] s } := by
  ext s
  -- âŠ¢ MeasurableSet s â†” MeasurableSet s
  rw [measurableSet_iSup]
  -- âŠ¢ GenerateMeasurable {s | âˆƒ i, MeasurableSet s} s â†” MeasurableSet s
  rfl
  -- ğŸ‰ no goals
#align measurable_space.measurable_space_supr_eq MeasurableSpace.measurableSpace_iSup_eq

theorem generateFrom_iUnion_measurableSet (m : Î¹ â†’ MeasurableSpace Î±) :
    generateFrom (â‹ƒ n, { t | MeasurableSet[m n] t }) = â¨† n, m n :=
  (@giGenerateFrom Î±).l_iSup_u m
#align measurable_space.generate_from_Union_measurable_set MeasurableSpace.generateFrom_iUnion_measurableSet

end CompleteLattice

end MeasurableSpace

/-- A function `f` between measurable spaces is measurable if the preimage of every
  measurable set is measurable. -/
def Measurable [MeasurableSpace Î±] [MeasurableSpace Î²] (f : Î± â†’ Î²) : Prop :=
  âˆ€ â¦ƒt : Set Î²â¦„, MeasurableSet t â†’ MeasurableSet (f â»Â¹' t)
#align measurable Measurable

namespace MeasureTheory

set_option quotPrecheck false in
/-- Notation for `Measurable` with respect to a non-standanrd Ïƒ-algebra in the domain. -/
scoped notation "Measurable[" m "]" => @Measurable _ _ m _

end MeasureTheory

section MeasurableFunctions

@[measurability]
theorem measurable_id {_ : MeasurableSpace Î±} : Measurable (@id Î±) := fun _ => id
#align measurable_id measurable_id

@[measurability]
theorem measurable_id' {_ : MeasurableSpace Î±} : Measurable fun a : Î± => a := measurable_id
#align measurable_id' measurable_id'

protected theorem Measurable.comp {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²}
    {_ : MeasurableSpace Î³} {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Measurable g) (hf : Measurable f) :
    Measurable (g âˆ˜ f) :=
  fun _ h => hf (hg h)
#align measurable.comp Measurable.comp

-- This is needed due to reducibility issues with the `measurability` tactic.
@[aesop safe 50 (rule_sets [Measurable])]
protected theorem Measurable.comp' {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²}
    {_ : MeasurableSpace Î³} {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Measurable g) (hf : Measurable f) :
    Measurable (fun x => g (f x)) := Measurable.comp hg hf

@[simp, measurability]
theorem measurable_const {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²} {a : Î±} :
    Measurable fun _ : Î² => a := fun s _ => .const (a âˆˆ s)
#align measurable_const measurable_const

theorem Measurable.le {Î±} {m m0 : MeasurableSpace Î±} {_ : MeasurableSpace Î²} (hm : m â‰¤ m0)
    {f : Î± â†’ Î²} (hf : Measurable[m] f) : Measurable[m0] f := fun _ hs => hm _ (hf hs)
#align measurable.le Measurable.le

theorem MeasurableSpace.Top.measurable {Î± Î² : Type*} [MeasurableSpace Î²] (f : Î± â†’ Î²) :
    Measurable[âŠ¤] f := fun _ _ => MeasurableSpace.measurableSet_top
#align measurable_space.top.measurable MeasurableSpace.Top.measurable

end MeasurableFunctions
