/-
Copyright (c) 2025 Stefano Rocca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Stefano Rocca
-/
module

public import Mathlib.MeasureTheory.Group.Defs
public import Mathlib.MeasureTheory.Group.Action
public import Mathlib.MeasureTheory.Measure.Typeclasses.Finite

/-!
# FÃ¸lner filters - definitions and properties

This file defines FÃ¸lner filters for measurable spaces acted on by a group.

## Definitions

* `IsFoelner G Î¼ l F` : A FÃ¸lner sequence with respect to some group `G` acting on
  a measure space `X` is a sequence of sets `F` such that:
    1. Each `s` in `l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Î¼ ((g â€¢ F i) âˆ† F i) / Î¼ (F i)` tends to `0`.

* `IsFoelner.mean Î¼ u F s` : The limit along an ultrafilter `u` of the density of a set `s`
  with respect to a FÃ¸lner sequence `F` in the measure space `X`.

* `maxFoelner G Î¼` : The maximal FÃ¸lner filter with respect to some group `G` acting on a
  measure space `X` is the pullback of `ğ“ 0` along the map `s â†¦ Î¼ (g â€¢ s) / Î¼ s` over measurable
  sets of finite non-zero measure.

## Main results

* `IsFoelner.amenable` : If there exists a non-trivial FÃ¸lner filter with respect to some
  group `G` acting on a measure space `X`, then it exists a `G`-invariant finitely additive
  probability measure on `X`.

* `isFoelner_iff_tendsto` : A sequence of sets is FÃ¸lner if and only if it tends to the
  maximal FÃ¸lner filter.
  The attribute "maximal" of the latter comes from the direct implication of this theorem :
  if `IsFoelner G Î¼ l F` then the push-forward filter `(l.map F) â‰¤ maxFoelner G Î¼`.

* `amenable_of_maxFoelner_ne_bot` : If the maximal FÃ¸lner filter is non-trivial,
  then there exists a `G`-invariant finitely additive probability measure on `X`.

## Tags

Foelner, FÃ¸lner filter, amenability, amenable group
-/

@[expose] public section


open MeasureTheory Filter Set Tendsto
open scoped ENNReal Pointwise symmDiff Topology Filter

variable {G X : Type*} [MeasurableSpace X] {Î¼ : Measure X} [Group G] [MulAction G X]
variable {Î¹ : Type*} {l : Filter Î¹} {u : Ultrafilter Î¹} {F : Î¹ â†’ Set X}

namespace Filter


variable (G Î¼ l F) in
/-- A FÃ¸lner sequence with respect to some group `G` acting on a measure space `X`
    is a sequence of sets `F` such that:
      1. Each `s` in `l` is eventually measurable with finite non-zero measure,
      2. For all `g : G`, `Î¼ ((g â€¢ F i) âˆ† F i) / Î¼ (F i)` tends to `0`. -/
@[mk_iff]
structure IsFoelner : Prop where
  eventually_measurableSet : âˆ€á¶  i in l, MeasurableSet (F i)
  eventually_meas_ne_zero : âˆ€á¶  i in l, Î¼ (F i) â‰  0
  eventually_meas_ne_top : âˆ€á¶  i in l, Î¼ (F i) â‰  âˆ
  tendsto_meas_symmDiff (g : G) : Tendsto (fun i â†¦ Î¼ ((g â€¢ F i) âˆ† F i) / Î¼ (F i)) l (ğ“ 0)

/-- The constant sequence `X` is FÃ¸lner if `X` has finite measure. -/
theorem IsFoelner.univ_of_isFiniteMeasure [NeZero Î¼] [IsFiniteMeasure Î¼] :
    IsFoelner G Î¼ l (fun _ â†¦ .univ) where
  eventually_measurableSet := by simp
  eventually_meas_ne_zero := by simp [NeZero.ne]
  eventually_meas_ne_top := by simp
  tendsto_meas_symmDiff := by simp [tendsto_const_nhds]

theorem IsFoelner.mono {l' : Filter Î¹} (hfoel : IsFoelner G Î¼ l F) (hle : l' â‰¤ l) :
    IsFoelner G Î¼ l' F where
  eventually_measurableSet := hfoel.eventually_measurableSet.filter_mono hle
  eventually_meas_ne_zero := hfoel.eventually_meas_ne_zero.filter_mono hle
  eventually_meas_ne_top := hfoel.eventually_meas_ne_top.filter_mono hle
  tendsto_meas_symmDiff (g : G) := Tendsto.mono_left (hfoel.tendsto_meas_symmDiff g) hle

variable (Î¼ u F) in
/-- The limit along an ultrafilter of the density of a set with respect to a
    FÃ¸lner sequence in `X`. -/
noncomputable def IsFoelner.mean (s : Set X) :=
  limUnder u (fun i â†¦ Î¼ (s âˆ© F i) / Î¼ (F i))

theorem IsFoelner.tendsto_nhds_mean (hfoel : IsFoelner G Î¼ u F) (s : Set X) :
    Tendsto (fun i â†¦ Î¼ (s âˆ© F i) / Î¼ (F i)) u (ğ“ (IsFoelner.mean Î¼ u F s)) := by
  have mem_Icc : âˆ€á¶  i in u, Î¼ (s âˆ© F i) / Î¼ (F i) âˆˆ Icc 0 1 := by
    filter_upwards [hfoel.eventually_meas_ne_zero, hfoel.eventually_meas_ne_top] with i hi hi'
    simpa [ENNReal.div_le_iff hi hi'] using Î¼.mono inter_subset_right
  obtain âŸ¨x, hxâŸ© := isCompact_Icc.ultrafilter_le_nhds'
    (u.map (fun i â†¦ Î¼ (s âˆ© F i) / Î¼ (F i))) (mem_map.1 mem_Icc)
  exact tendsto_nhds_limUnder (by use x; exact hx.2)

theorem IsFoelner.mean_univ_eq_one (hfoel : IsFoelner G Î¼ u F) :
    IsFoelner.mean Î¼ u F .univ = 1 := by
  refine tendsto_nhds_unique_of_eventuallyEq (hfoel.tendsto_nhds_mean _) tendsto_const_nhds ?_
  filter_upwards [hfoel.eventually_meas_ne_zero, hfoel.eventually_meas_ne_top] with i hi hi'
  simp [ENNReal.div_self hi hi']

theorem IsFoelner.mean_union_eq_add_of_disjoint (hfoel : IsFoelner G Î¼ u F)
    (s t : Set X) (ht : MeasurableSet t) (hdisj : Disjoint s t) :
    IsFoelner.mean Î¼ u F (s âˆª t) = IsFoelner.mean Î¼ u F s + IsFoelner.mean Î¼ u F t := by
  refine tendsto_nhds_unique_of_eventuallyEq
    (hfoel.tendsto_nhds_mean _) ((hfoel.tendsto_nhds_mean _).add (hfoel.tendsto_nhds_mean _)) ?_
  filter_upwards [hfoel.eventually_measurableSet] with i hi
  rw [union_inter_distrib_right,
    measure_union (hdisj.inter_left _ |>.inter_right _) (ht.inter hi), ENNReal.add_div]

theorem IsFoelner.mean_smul_eq_mean [SMulInvariantMeasure G X Î¼]
    (hfoel : IsFoelner G Î¼ u F) (g : G) (s : Set X) :
    IsFoelner.mean Î¼ u F (g â€¢ s) = IsFoelner.mean Î¼ u F s := by
  suffices h_le : âˆ€ h h', IsFoelner.mean Î¼ u F (h â€¢ s) â‰¤ IsFoelner.mean Î¼ u F (h' â€¢ s) by
    simpa [one_smul] using le_antisymm (h_le g 1) (h_le 1 g)
  intro h h'
  have tendstoâ‚€ : Tendsto (fun i â†¦ Î¼ ((hâ»Â¹ â€¢ F i) âˆ† (h'â»Â¹ â€¢ F i)) / Î¼ (F i)) u (ğ“ 0) := by
    simpa [â† smul_smul] using hfoel.tendsto_meas_symmDiff (h' * hâ»Â¹)
  have h_le_add (i : Î¹) : Î¼ (h â€¢ s âˆ© F i) â‰¤ Î¼ (h' â€¢ s âˆ© F i) + Î¼ ((hâ»Â¹ â€¢ F i) âˆ† (h'â»Â¹ â€¢ F i)) := by
    simp_rw [â† measure_inter_inv_smul]
    set A := s âˆ© hâ»Â¹ â€¢ F i
    set B := s âˆ© h'â»Â¹ â€¢ F i
    calc
      Î¼ A â‰¤ Î¼ B + Î¼ (A \ B) := by
        simpa [Set.inter_union_diff] using
          (measure_union_le (A âˆ© B) (A \ B)).trans <| add_le_add_left (measure_mono (by simp)) _
      _ â‰¤ Î¼ B + Î¼ ((hâ»Â¹ â€¢ F i) âˆ† (h'â»Â¹ â€¢ F i)) :=
        add_le_add_right (by
          rw [â† inter_diff_distrib_left]
          apply measure_mono
          exact inter_subset_right.trans <| by simp [symmDiff_def]) _
  rw [â† add_zero <| mean Î¼ u F (h' â€¢ s)]
  exact le_of_tendsto_of_tendsto'
    (hfoel.tendsto_nhds_mean _)
    ((hfoel.tendsto_nhds_mean _).add tendstoâ‚€)
    (by simp only [â† ENNReal.add_div]; exact fun i â†¦ by gcongr; exact h_le_add i)

/-- If there exists a non-trivial FÃ¸lner filter with respect to some group `G` acting on a measure
    space `X`, then it exists a `G`-invariant finitely additive probability measure on `X`. -/
theorem IsFoelner.amenable [SMulInvariantMeasure G X Î¼] [NeBot l] (hfoel : IsFoelner G Î¼ l F) :
    âˆƒ m : Set X â†’ â„â‰¥0âˆ, m .univ = 1 âˆ§
      (âˆ€ s t, MeasurableSet t â†’ Disjoint s t â†’ m (s âˆª t) = m s + m t) âˆ§
        âˆ€ (g : G) (s : Set X), m (g â€¢ s) = m s := by
  use IsFoelner.mean Î¼ (Ultrafilter.of l) F
  refine âŸ¨?_, ?_, ?_âŸ©
  Â· exact (hfoel.mono <| Ultrafilter.of_le l).mean_univ_eq_one
  Â· exact (hfoel.mono <| Ultrafilter.of_le l).mean_union_eq_add_of_disjoint
  Â· exact (hfoel.mono <| Ultrafilter.of_le l).mean_smul_eq_mean

variable (G Î¼) in
/-- The maximal FÃ¸lner filter with respect to some group `G` acting on a
    measure space `X` is the pullback of `ğ“ 0` along the map `s â†¦ Î¼ (g â€¢ s) / Î¼ s`
    on measurable sets of finite non-zero measure. -/
def maxFoelner : Filter (Set X) :=
  ğ“Ÿ {s : Set X | MeasurableSet s âˆ§ Î¼ s â‰  0 âˆ§ Î¼ s â‰  âˆ} âŠ“
  â¨… (g : G), (comap (fun s â†¦ Î¼ ((g â€¢ s) âˆ† s) / Î¼ s) (ğ“ 0))

variable (l F) in
theorem isFoelner_iff_tendsto : IsFoelner G Î¼ l F â†” Tendsto F l (maxFoelner G Î¼) := by
  simp [maxFoelner, tendsto_inf, tendsto_iInf, isFoelner_iff, Function.comp_def, and_assoc]

theorem amenable_of_maxFoelner_ne_bot [SMulInvariantMeasure G X Î¼] (h : NeBot (maxFoelner G Î¼)) :
    âˆƒ m : Set X â†’ â„â‰¥0âˆ, m .univ = 1 âˆ§
      (âˆ€ s t, MeasurableSet t â†’ Disjoint s t â†’ m (s âˆª t) = m s + m t) âˆ§
        âˆ€ (g : G) (s : Set X), m (g â€¢ s) = m s :=
  IsFoelner.amenable <| (isFoelner_iff_tendsto _ _).2 <| @tendsto_id _ (maxFoelner G Î¼)

end Filter
