/-
Copyright (c) 2025 Stefano Rocca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Stefano Rocca
-/
module

public import Mathlib.MeasureTheory.Group.Defs
public import Mathlib.MeasureTheory.Group.Action
public import Mathlib.MeasureTheory.Measure.Typeclasses.Finite

/-!
# F√∏lner filters - definitions and properties

This file defines F√∏lner filters for measurable spaces acted on by a group.

## Definitions

* `IsFoelner G Œº l F` : A F√∏lner sequence with respect to some group `G` acting on
  a measure space `X` is a sequence of sets `F` such that:
    1. Each `s` in `l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Œº ((g ‚Ä¢ F i) ‚àÜ F i) / Œº (F i)` tends to `0`.

* `IsFoelner.mean Œº l F s` : Given a F√∏lner sequence `F` with respect to some group `G`
  acting on a measure space `X`, the mean of a set `s` is the limit of the sequence
  `Œº (s ‚à© F i) / Œº (F i)` along the filter `l`.

* `maxFoelner G Œº` : The maximal F√∏lner filter with respect to some group `G` acting on a
  measure space `X` is the pullback of `ùìù 0` along the map `s ‚Ü¶ Œº (g ‚Ä¢ s) / Œº s` on measurable
  sets of finite non-zero measure.

## Main results

* `IsFoelner.amenable` : If there exists a non-trivial F√∏lner filter with respect to some
  group `G` acting on a measure space `X`, then it exists a `G`-invariant finitely additive
  probability measure on `X`.

* `isFoelner_iff_tendsto` : A sequence of sets is F√∏lner if and only if it tends to the
  maximal F√∏lner filter.
  The attribute "maximal" of the latter comes from the direct implication of this theorem :
  if `IsFoelner G Œº l F` then the push-forward filter `(l.map F) ‚â§ maxFoelner G Œº`.

* `amenable_of_maxFoelner_ne_bot` : If the maximal F√∏lner filter is non-trivial,
  then there exists a `G`-invariant finitely additive probability measure on `X`.

## Tags

Foelner, F√∏lner filter, amenability, amenable group
-/

@[expose] public section


open MeasureTheory Filter Set Tendsto
open scoped ENNReal Pointwise symmDiff Topology Filter

variable {G X : Type*} [MeasurableSpace X] {Œº : Measure X} [Group G] [MulAction G X]
variable {Œπ : Type*} {l : Filter Œπ} {u : Ultrafilter Œπ} {F : Œπ ‚Üí Set X}

namespace Filter

variable (G Œº l F) in
/-- A F√∏lner sequence with respect to some group `G` acting on a measure space `X`
is a sequence of sets `F` such that:
    1. Each `s` in `l` is eventually measurable with finite non-zero measure,
    2. For all `g : G`, `Œº ((g ‚Ä¢ F i) ‚àÜ F i) / Œº (F i)` tends to `0`. -/
structure IsFoelner : Prop where
  eventually_measurableSet : ‚àÄ·∂† i in l, MeasurableSet (F i)
  eventually_meas_ne_zero : ‚àÄ·∂† i in l, Œº (F i) ‚â† 0
  eventually_meas_ne_top : ‚àÄ·∂† i in l, Œº (F i) ‚â† ‚àû
  tendsto_meas_symmDiff (g : G) : Tendsto (fun i ‚Ü¶ Œº ((g ‚Ä¢ (F i)) ‚àÜ (F i)) / Œº (F i)) l (ùìù 0)

/-- The constant sequence `X` is F√∏lner if `X` has finite measure. -/
theorem IsFoelner.univ_of_isFiniteMeasure [NeZero Œº] [IsFiniteMeasure Œº] :
    IsFoelner G Œº l (fun _ ‚Ü¶ .univ) where
  eventually_measurableSet := by simp
  eventually_meas_ne_zero := by simp [NeZero.ne]
  eventually_meas_ne_top := by simp
  tendsto_meas_symmDiff := by simp [tendsto_const_nhds]

theorem IsFoelner.mono {l' : Filter Œπ} (hfoel : IsFoelner G Œº l F) (hle : l' ‚â§ l) :
    IsFoelner G Œº l' F where
  eventually_measurableSet := Eventually.filter_mono hle hfoel.eventually_measurableSet
  eventually_meas_ne_zero := Eventually.filter_mono hle hfoel.eventually_meas_ne_zero
  eventually_meas_ne_top := Eventually.filter_mono hle hfoel.eventually_meas_ne_top
  tendsto_meas_symmDiff (g : G) := Tendsto.mono_left (hfoel.tendsto_meas_symmDiff g) hle

variable (Œº u F) in
/-- The limit along an ultrafilter of the density of a set
with respect to a F√∏lner sequence in `X`. -/
noncomputable def IsFoelner.mean (s : Set X) :=
  limUnder u (fun i ‚Ü¶ Œº (s ‚à© F i) / Œº (F i))

theorem IsFoelner.tendsto_nhds_mean (hfoel : IsFoelner G Œº u F) (s : Set X) :
    Tendsto (fun i ‚Ü¶ Œº (s ‚à© F i) / Œº (F i)) u (ùìù (IsFoelner.mean Œº u F s)) := by
  have mem_Icc : ‚àÄ·∂† i in u, Œº (s ‚à© F i) / Œº (F i) ‚àà Icc 0 1 :=
      Eventually.mono
        (hfoel.eventually_meas_ne_zero.and hfoel.eventually_meas_ne_top)
        (fun i hi ‚Ü¶ by simpa [ENNReal.div_le_iff hi.1 hi.2] using Œº.mono inter_subset_right)
  obtain ‚ü®x, hx‚ü© :=
    isCompact_iff_ultrafilter_le_nhds'.1
      isCompact_Icc (u.map (fun i ‚Ü¶ Œº (s ‚à© F i) / Œº (F i))) (mem_map.1 mem_Icc)
  exact tendsto_nhds_limUnder (by use x; exact hx.2)

theorem IsFoelner.mean_univ_eq_one (hfoel : IsFoelner G Œº u F) :
    IsFoelner.mean Œº u F .univ = 1 := by
  refine limUnder_eq <| tendsto_congr' ?_|>.mp tendsto_const_nhds
  exact Eventually.mono
    (hfoel.eventually_meas_ne_zero.and hfoel.eventually_meas_ne_top)
    (fun _ hi ‚Ü¶ by simp [ENNReal.div_self hi.1 hi.2])

theorem IsFoelner.mean_union_eq_add_of_disjoint (hfoel : IsFoelner G Œº u F)
    (s t : Set X) (ht : MeasurableSet t) (hdisj : Disjoint s t) :
    IsFoelner.mean Œº u F (s ‚à™ t) = IsFoelner.mean Œº u F s + IsFoelner.mean Œº u F t := by
  dsimp [IsFoelner.mean]
  rw [limUnder_eq <| IsFoelner.tendsto_nhds_mean hfoel s]
  rw [limUnder_eq <| IsFoelner.tendsto_nhds_mean hfoel t]
  simp_rw [union_inter_distrib_right]
  refine limUnder_eq <| Tendsto.congr' ?_
    (Tendsto.add (IsFoelner.tendsto_nhds_mean hfoel s) (IsFoelner.tendsto_nhds_mean hfoel t))
  refine Eventually.mono hfoel.eventually_measurableSet ?_
  intro i hi
  simp_rw [‚Üê ENNReal.add_div]
  rw [‚Üê measure_union
    (Disjoint.mono inter_subset_left inter_subset_left hdisj) (MeasurableSet.inter ht hi)]

theorem IsFoelner.mean_smul_eq_mean [SMulInvariantMeasure G X Œº] (hfoel : IsFoelner G Œº u F)
    (g : G) (s : Set X) : IsFoelner.mean Œº u F (g ‚Ä¢ s) = IsFoelner.mean Œº u F s := by
  suffices h_le : ‚àÄ h h', IsFoelner.mean Œº u F (h ‚Ä¢ s) ‚â§ IsFoelner.mean Œº u F (h' ‚Ä¢ s) by
    simpa [one_smul] using le_antisymm (h_le g 1) (h_le 1 g)
  intro h h'
  have tendsto‚ÇÄ : Tendsto (fun i ‚Ü¶ Œº ((h‚Åª¬π ‚Ä¢ F i) ‚àÜ (h'‚Åª¬π ‚Ä¢ F i)) / Œº (F i)) u (ùìù 0) := by
    simpa [‚Üê smul_smul] using hfoel.tendsto_meas_symmDiff (h' * h‚Åª¬π)
  have h_le_add (i : Œπ) : Œº (h ‚Ä¢ s ‚à© F i) ‚â§ Œº (h' ‚Ä¢ s ‚à© F i) + Œº ((h‚Åª¬π ‚Ä¢ F i) ‚àÜ (h'‚Åª¬π ‚Ä¢ F i)) := by
    simp_rw [‚Üê measure_inter_inv_smul]
    set A := s ‚à© h‚Åª¬π ‚Ä¢ F i
    set B := s ‚à© h'‚Åª¬π ‚Ä¢ F i
    calc
      Œº A ‚â§ Œº B + Œº (A \ B) := by
        simpa [Set.inter_union_diff] using
          (measure_union_le (A ‚à© B) (A \ B)).trans <| add_le_add_left (measure_mono (by simp)) _
      _ ‚â§ Œº B + Œº ((h‚Åª¬π ‚Ä¢ F i) ‚àÜ (h'‚Åª¬π ‚Ä¢ F i)) :=
        add_le_add_right (by
          rw [‚Üê inter_diff_distrib_left]
          apply measure_mono
          exact inter_subset_right.trans <| by simp [symmDiff_def]) _
  dsimp [IsFoelner.mean]
  rw [limUnder_eq <| IsFoelner.tendsto_nhds_mean hfoel (h ‚Ä¢ s)]
  rw [limUnder_eq <| IsFoelner.tendsto_nhds_mean hfoel (h' ‚Ä¢ s)]
  rw [‚Üê add_zero <| mean Œº u F (h' ‚Ä¢ s)]
  exact le_of_tendsto_of_tendsto'
    (IsFoelner.tendsto_nhds_mean hfoel (h ‚Ä¢ s))
    (Tendsto.add (IsFoelner.tendsto_nhds_mean hfoel (h' ‚Ä¢ s)) tendsto‚ÇÄ)
    (by simp only [‚Üê ENNReal.add_div]; exact fun i ‚Ü¶ by gcongr; exact h_le_add i)

/-- If there exists a non-trivial F√∏lner filter with respect to some group `G` acting on a measure
space `X`, then it exists a `G`-invariant finitely additive probability measure on `X`. -/
theorem IsFoelner.amenable [SMulInvariantMeasure G X Œº] [NeBot l] (hfoel : IsFoelner G Œº l F) :
    ‚àÉ m : Set X ‚Üí ‚Ñù‚â•0‚àû, m .univ = 1 ‚àß
      (‚àÄ s t, MeasurableSet t ‚Üí Disjoint s t ‚Üí m (s ‚à™ t) = m s + m t) ‚àß
        ‚àÄ (g : G) (s : Set X), m (g ‚Ä¢ s) = m s := by
  use IsFoelner.mean Œº (Ultrafilter.of l) F
  refine ‚ü®?_, ?_, ?_‚ü©
  ¬∑ exact IsFoelner.mean_univ_eq_one <| IsFoelner.mono hfoel (Ultrafilter.of_le l)
  ¬∑ exact IsFoelner.mean_union_eq_add_of_disjoint <| IsFoelner.mono hfoel (Ultrafilter.of_le l)
  ¬∑ exact IsFoelner.mean_smul_eq_mean <| IsFoelner.mono hfoel (Ultrafilter.of_le l)

variable (G Œº) in
/-- The maximal F√∏lner filter with respect to some group `G` acting on a
measure space `X` is the pullback of `ùìù 0` along the map `s ‚Ü¶ Œº (g ‚Ä¢ s) / Œº s`
on measurable sets of finite non-zero measure. -/
def maxFoelner : Filter (Set X) :=
  ùìü {s : Set X | MeasurableSet s ‚àß Œº s ‚â† 0 ‚àß Œº s ‚â† ‚àû} ‚äì
  ‚®Ö (g : G), (comap (fun s ‚Ü¶ Œº ((g ‚Ä¢ s) ‚àÜ s) / Œº s) (ùìù 0))

variable (l F) in
theorem isFoelner_iff_tendsto : IsFoelner G Œº l F ‚Üî Tendsto F l (maxFoelner G Œº) := by
  dsimp [maxFoelner]
  simp_rw [tendsto_inf, tendsto_iInf, tendsto_principal, tendsto_comap_iff]
-- linters do not allow me to simp anymore, don't know why
  have : (‚àÄ·∂† (i : Œπ) in l, F i ‚àà {s | MeasurableSet s ‚àß ¬¨Œº s = 0 ‚àß ¬¨Œº s = ‚ä§}) ‚Üî
    (‚àÄ·∂† (i : Œπ) in l, MeasurableSet (F i)) ‚àß
    (‚àÄ·∂† (i : Œπ) in l, Œº (F i) ‚â† 0) ‚àß
    (‚àÄ·∂† (i : Œπ) in l, Œº (F i) ‚â† ‚àû) := by simp
  rw [this]
--
  constructor
  all_goals intro h
  ¬∑ refine ‚ü®‚ü®h.eventually_measurableSet, h.eventually_meas_ne_zero, h.eventually_meas_ne_top‚ü©, ?_‚ü©
    exact h.tendsto_meas_symmDiff
  ¬∑ exact {
      eventually_measurableSet := h.1.1
      eventually_meas_ne_zero := h.1.2.1
      eventually_meas_ne_top := h.1.2.2
      tendsto_meas_symmDiff := h.2 }

theorem amenable_of_maxFoelner_ne_bot
    [SMulInvariantMeasure G X Œº] (h : NeBot (maxFoelner G Œº)) :
    ‚àÉ m : Set X ‚Üí ‚Ñù‚â•0‚àû, m .univ = 1 ‚àß
      (‚àÄ s t, MeasurableSet t ‚Üí Disjoint s t ‚Üí m (s ‚à™ t) = m s + m t) ‚àß
        ‚àÄ (g : G) (s : Set X), m (g ‚Ä¢ s) = m s :=
  IsFoelner.amenable <| (isFoelner_iff_tendsto _ _).2 <| @tendsto_id _ (maxFoelner G Œº)

end Filter
