/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Data.Countable.Basic
import Mathlib.Data.Fin.VecNotation
import Mathlib.Order.Disjointed
import Mathlib.MeasureTheory.OuterMeasure.Defs
import Mathlib.Topology.Instances.ENNReal.Lemmas

/-!
# Outer Measures

An outer measure is a function `Œº : Set Œ± ‚Üí ‚Ñù‚â•0‚àû`, from the powerset of a type to the extended
nonnegative real numbers that satisfies the following conditions:
1. `Œº ‚àÖ = 0`;
2. `Œº` is monotone;
3. `Œº` is countably subadditive. This means that the outer measure of a countable union is at most
   the sum of the outer measure on the individual sets.

Note that we do not need `Œ±` to be measurable to define an outer measure.

## References

<https://en.wikipedia.org/wiki/Outer_measure>

## Tags

outer measure
-/


noncomputable section

open Set Function Filter
open scoped NNReal Topology ENNReal

namespace MeasureTheory

section OuterMeasureClass

variable {Œ± Œπ F : Type*} [FunLike F (Set Œ±) ‚Ñù‚â•0‚àû] [OuterMeasureClass F Œ±]
  {Œº : F} {s t : Set Œ±}

@[simp]
theorem measure_empty : Œº ‚àÖ = 0 := OuterMeasureClass.measure_empty Œº

@[mono, gcongr]
theorem measure_mono (h : s ‚äÜ t) : Œº s ‚â§ Œº t :=
  OuterMeasureClass.measure_mono Œº h

theorem measure_mono_null (h : s ‚äÜ t) (ht : Œº t = 0) : Œº s = 0 :=
  eq_bot_mono (measure_mono h) ht

lemma pos_mono ‚¶És t : Set Œ±‚¶Ñ (h : s ‚äÜ t) (hs : 0 < Œº s) :
    0 < Œº t := hs.trans_le <| measure_mono h

lemma measure_eq_top_mono (h : s ‚äÜ t) (hs : Œº s = ‚àû) : Œº t = ‚àû := eq_top_mono (measure_mono h) hs
lemma measure_lt_top_mono (h : s ‚äÜ t) (ht : Œº t < ‚àû) : Œº s < ‚àû := (measure_mono h).trans_lt ht

theorem measure_pos_of_superset (h : s ‚äÜ t) (hs : Œº s ‚â† 0) : 0 < Œº t :=
  hs.bot_lt.trans_le (measure_mono h)

theorem measure_iUnion_le [Countable Œπ] (s : Œπ ‚Üí Set Œ±) : Œº (‚ãÉ i, s i) ‚â§ ‚àë' i, Œº (s i) := by
  refine rel_iSup_tsum Œº measure_empty (¬∑ ‚â§ ¬∑) (fun t ‚Ü¶ ?_) _
  calc
    Œº (‚ãÉ i, t i) = Œº (‚ãÉ i, disjointed t i) := by rw [iUnion_disjointed]
    _ ‚â§ ‚àë' i, Œº (disjointed t i) :=
      OuterMeasureClass.measure_iUnion_nat_le _ _ (disjoint_disjointed _)
    _ ‚â§ ‚àë' i, Œº (t i) := by gcongr; exact disjointed_subset ..

theorem measure_biUnion_le {I : Set Œπ} (Œº : F) (hI : I.Countable) (s : Œπ ‚Üí Set Œ±) :
    Œº (‚ãÉ i ‚àà I, s i) ‚â§ ‚àë' i : I, Œº (s i) := by
  have := hI.to_subtype
  rw [biUnion_eq_iUnion]
  apply measure_iUnion_le

theorem measure_biUnion_finset_le (I : Finset Œπ) (s : Œπ ‚Üí Set Œ±) :
    Œº (‚ãÉ i ‚àà I, s i) ‚â§ ‚àë i ‚àà I, Œº (s i) :=
  (measure_biUnion_le Œº I.countable_toSet s).trans_eq <| I.tsum_subtype (Œº <| s ¬∑)

theorem measure_iUnion_fintype_le [Fintype Œπ] (Œº : F) (s : Œπ ‚Üí Set Œ±) :
    Œº (‚ãÉ i, s i) ‚â§ ‚àë i, Œº (s i) := by
  simpa using measure_biUnion_finset_le Finset.univ s

theorem measure_union_le (s t : Set Œ±) : Œº (s ‚à™ t) ‚â§ Œº s + Œº t := by
  simpa [union_eq_iUnion] using measure_iUnion_fintype_le Œº (cond ¬∑ s t)

lemma measure_univ_le_add_compl (s : Set Œ±) : Œº univ ‚â§ Œº s + Œº s·∂ú :=
  s.union_compl_self ‚ñ∏ measure_union_le s s·∂ú

theorem measure_le_inter_add_diff (Œº : F) (s t : Set Œ±) : Œº s ‚â§ Œº (s ‚à© t) + Œº (s \ t) := by
  simpa using measure_union_le (s ‚à© t) (s \ t)

theorem measure_diff_null (ht : Œº t = 0) : Œº (s \ t) = Œº s :=
  (measure_mono diff_subset).antisymm <| calc
    Œº s ‚â§ Œº (s ‚à© t) + Œº (s \ t) := measure_le_inter_add_diff _ _ _
    _ ‚â§ Œº t + Œº (s \ t) := by gcongr; apply inter_subset_right
    _ = Œº (s \ t) := by simp [ht]

theorem measure_biUnion_null_iff {I : Set Œπ} (hI : I.Countable) {s : Œπ ‚Üí Set Œ±} :
    Œº (‚ãÉ i ‚àà I, s i) = 0 ‚Üî ‚àÄ i ‚àà I, Œº (s i) = 0 := by
  refine ‚ü®fun h i hi ‚Ü¶ measure_mono_null (subset_biUnion_of_mem hi) h, fun h ‚Ü¶ ?_‚ü©
  have _ := hI.to_subtype
  simpa [h] using measure_iUnion_le (Œº := Œº) fun x : I ‚Ü¶ s x

theorem measure_sUnion_null_iff {S : Set (Set Œ±)} (hS : S.Countable) :
    Œº (‚ãÉ‚ÇÄ S) = 0 ‚Üî ‚àÄ s ‚àà S, Œº s = 0 := by
  rw [sUnion_eq_biUnion, measure_biUnion_null_iff hS]

@[simp]
theorem measure_iUnion_null_iff {Œπ : Sort*} [Countable Œπ] {s : Œπ ‚Üí Set Œ±} :
    Œº (‚ãÉ i, s i) = 0 ‚Üî ‚àÄ i, Œº (s i) = 0 := by
  rw [‚Üê sUnion_range, measure_sUnion_null_iff (countable_range s), forall_mem_range]

alias ‚ü®_, measure_iUnion_null‚ü© := measure_iUnion_null_iff

@[simp]
theorem measure_union_null_iff : Œº (s ‚à™ t) = 0 ‚Üî Œº s = 0 ‚àß Œº t = 0 := by
  simp [union_eq_iUnion, and_comm]

theorem measure_union_null (hs : Œº s = 0) (ht : Œº t = 0) : Œº (s ‚à™ t) = 0 := by simp [*]

lemma measure_null_iff_singleton (hs : s.Countable) : Œº s = 0 ‚Üî ‚àÄ x ‚àà s, Œº {x} = 0 := by
  rw [‚Üê measure_biUnion_null_iff hs, biUnion_of_singleton]

/-- Let `Œº` be an (outer) measure; let `s : Œπ ‚Üí Set Œ±` be a sequence of sets, `S = ‚ãÉ n, s n`.
If `Œº (S \ s n)` tends to zero along some nontrivial filter (usually `Filter.atTop` on `Œπ = ‚Ñï`),
then `Œº S = ‚®Ü n, Œº (s n)`. -/
theorem measure_iUnion_of_tendsto_zero {Œπ} (Œº : F) {s : Œπ ‚Üí Set Œ±} (l : Filter Œπ) [NeBot l]
    (h0 : Tendsto (fun k ‚Ü¶ Œº ((‚ãÉ n, s n) \ s k)) l (ùìù 0)) : Œº (‚ãÉ n, s n) = ‚®Ü n, Œº (s n) := by
  refine le_antisymm ?_ <| iSup_le fun n ‚Ü¶ measure_mono <| subset_iUnion _ _
  set S := ‚ãÉ n, s n
  set M := ‚®Ü n, Œº (s n)
  have A : ‚àÄ k, Œº S ‚â§ M + Œº (S \ s k) := fun k ‚Ü¶ calc
    Œº S ‚â§ Œº (S ‚à© s k) + Œº (S \ s k) := measure_le_inter_add_diff _ _ _
    _ ‚â§ Œº (s k) + Œº (S \ s k) := by gcongr; apply inter_subset_right
    _ ‚â§ M + Œº (S \ s k) := by gcongr; exact le_iSup (Œº ‚àò s) k
  have B : Tendsto (fun k ‚Ü¶ M + Œº (S \ s k)) l (ùìù M) := by simpa using tendsto_const_nhds.add h0
  exact ge_of_tendsto' B A

/-- If a set has zero measure in a neighborhood of each of its points, then it has zero measure
in a second-countable space. -/
theorem measure_null_of_locally_null [TopologicalSpace Œ±] [SecondCountableTopology Œ±]
    (s : Set Œ±) (hs : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù[s] x, Œº u = 0) : Œº s = 0 := by
  choose! u hxu hu‚ÇÄ using hs
  choose t ht using TopologicalSpace.countable_cover_nhdsWithin hxu
  rcases ht with ‚ü®ts, t_count, ht‚ü©
  apply measure_mono_null ht
  exact (measure_biUnion_null_iff t_count).2 fun x hx ‚Ü¶ hu‚ÇÄ x (ts hx)

/-- If `m s ‚â† 0`, then for some point `x ‚àà s` and any `t ‚àà ùìù[s] x` we have `0 < m t`. -/
theorem exists_mem_forall_mem_nhdsWithin_pos_measure [TopologicalSpace Œ±]
    [SecondCountableTopology Œ±] {s : Set Œ±} (hs : Œº s ‚â† 0) :
    ‚àÉ x ‚àà s, ‚àÄ t ‚àà ùìù[s] x, 0 < Œº t := by
  contrapose! hs
  simp only [nonpos_iff_eq_zero] at hs
  exact measure_null_of_locally_null s hs

end OuterMeasureClass

namespace OuterMeasure

variable {Œ± Œ≤ : Type*} {m : OuterMeasure Œ±}

/-- If `s : Œπ ‚Üí Set Œ±` is a sequence of sets, `S = ‚ãÉ n, s n`, and `m (S \ s n)` tends to zero along
some nontrivial filter (usually `atTop` on `Œπ = ‚Ñï`), then `m S = ‚®Ü n, m (s n)`. -/
theorem iUnion_of_tendsto_zero {Œπ} (m : OuterMeasure Œ±) {s : Œπ ‚Üí Set Œ±} (l : Filter Œπ) [NeBot l]
    (h0 : Tendsto (fun k ‚Ü¶ m ((‚ãÉ n, s n) \ s k)) l (ùìù 0)) : m (‚ãÉ n, s n) = ‚®Ü n, m (s n) :=
  measure_iUnion_of_tendsto_zero m l h0

/-- If `s : ‚Ñï ‚Üí Set Œ±` is a monotone sequence of sets such that `‚àë' k, m (s (k + 1) \ s k) ‚â† ‚àû`,
then `m (‚ãÉ n, s n) = ‚®Ü n, m (s n)`. -/
theorem iUnion_nat_of_monotone_of_tsum_ne_top (m : OuterMeasure Œ±) {s : ‚Ñï ‚Üí Set Œ±}
    (h_mono : ‚àÄ n, s n ‚äÜ s (n + 1)) (h0 : (‚àë' k, m (s (k + 1) \ s k)) ‚â† ‚àû) :
    m (‚ãÉ n, s n) = ‚®Ü n, m (s n) := by
  classical
  refine measure_iUnion_of_tendsto_zero m atTop ?_
  refine tendsto_nhds_bot_mono' (ENNReal.tendsto_sum_nat_add _ h0) fun n ‚Ü¶ ?_
  refine (m.mono ?_).trans (measure_iUnion_le _)
  -- Current goal: `(‚ãÉ k, s k) \ s n ‚äÜ ‚ãÉ k, s (k + n + 1) \ s (k + n)`
  have h' : Monotone s := @monotone_nat_of_le_succ (Set Œ±) _ _ h_mono
  simp only [diff_subset_iff, iUnion_subset_iff]
  intro i x hx
  have : ‚àÉ i, x ‚àà s i := by exists i
  rcases Nat.findX this with ‚ü®j, hj, hlt‚ü©
  clear hx i
  rcases le_or_gt j n with hjn | hnj
  ¬∑ exact Or.inl (h' hjn hj)
  have : j - (n + 1) + n + 1 = j := by omega
  refine Or.inr (mem_iUnion.2 ‚ü®j - (n + 1), ?_, hlt _ ?_‚ü©)
  ¬∑ rwa [this]
  ¬∑ rw [‚Üê Nat.succ_le_iff, Nat.succ_eq_add_one, this]

theorem coe_fn_injective : Injective fun (Œº : OuterMeasure Œ±) (s : Set Œ±) ‚Ü¶ Œº s :=
  DFunLike.coe_injective

@[ext]
theorem ext {Œº‚ÇÅ Œº‚ÇÇ : OuterMeasure Œ±} (h : ‚àÄ s, Œº‚ÇÅ s = Œº‚ÇÇ s) : Œº‚ÇÅ = Œº‚ÇÇ :=
  DFunLike.ext _ _ h

/-- A version of `MeasureTheory.OuterMeasure.ext` that assumes `Œº‚ÇÅ s = Œº‚ÇÇ s` on all *nonempty*
sets `s`, and gets `Œº‚ÇÅ ‚àÖ = Œº‚ÇÇ ‚àÖ` from `MeasureTheory.OuterMeasure.empty'`. -/
theorem ext_nonempty {Œº‚ÇÅ Œº‚ÇÇ : OuterMeasure Œ±} (h : ‚àÄ s : Set Œ±, s.Nonempty ‚Üí Œº‚ÇÅ s = Œº‚ÇÇ s) :
    Œº‚ÇÅ = Œº‚ÇÇ :=
  ext fun s ‚Ü¶ s.eq_empty_or_nonempty.elim (fun he ‚Ü¶ by simp [he]) (h s)

end OuterMeasure

end MeasureTheory
