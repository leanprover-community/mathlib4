/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.Data.Countable.Basic
import Mathlib.Data.Fin.VecNotation
import Mathlib.Order.Disjointed
import Mathlib.MeasureTheory.OuterMeasure.Defs
import Mathlib.Topology.Instances.ENNReal.Lemmas

/-!
# Outer Measures

An outer measure is a function `Î¼ : Set Î± â†’ â„â‰¥0âˆ`, from the powerset of a type to the extended
nonnegative real numbers that satisfies the following conditions:
1. `Î¼ âˆ… = 0`;
2. `Î¼` is monotone;
3. `Î¼` is countably subadditive. This means that the outer measure of a countable union is at most
   the sum of the outer measure on the individual sets.

Note that we do not need `Î±` to be measurable to define an outer measure.

## References

<https://en.wikipedia.org/wiki/Outer_measure>

## Tags

outer measure
-/


noncomputable section

open Set Function Filter
open scoped NNReal Topology ENNReal

namespace MeasureTheory

section OuterMeasureClass

variable {Î± Î¹ F : Type*} [FunLike F (Set Î±) â„â‰¥0âˆ] [OuterMeasureClass F Î±]
  {Î¼ : F} {s t : Set Î±}

@[simp]
theorem measure_empty : Î¼ âˆ… = 0 := OuterMeasureClass.measure_empty Î¼

@[mono, gcongr]
theorem measure_mono (h : s âŠ† t) : Î¼ s â‰¤ Î¼ t :=
  OuterMeasureClass.measure_mono Î¼ h

theorem measure_mono_null (h : s âŠ† t) (ht : Î¼ t = 0) : Î¼ s = 0 :=
  eq_bot_mono (measure_mono h) ht

lemma measure_eq_top_mono (h : s âŠ† t) (hs : Î¼ s = âˆ) : Î¼ t = âˆ := eq_top_mono (measure_mono h) hs
lemma measure_lt_top_mono (h : s âŠ† t) (ht : Î¼ t < âˆ) : Î¼ s < âˆ := (measure_mono h).trans_lt ht

theorem measure_pos_of_superset (h : s âŠ† t) (hs : Î¼ s â‰  0) : 0 < Î¼ t :=
  hs.bot_lt.trans_le (measure_mono h)

theorem measure_iUnion_le [Countable Î¹] (s : Î¹ â†’ Set Î±) : Î¼ (â‹ƒ i, s i) â‰¤ âˆ‘' i, Î¼ (s i) := by
  refine rel_iSup_tsum Î¼ measure_empty (Â· â‰¤ Â·) (fun t â†¦ ?_) _
  calc
    Î¼ (â‹ƒ i, t i) = Î¼ (â‹ƒ i, disjointed t i) := by rw [iUnion_disjointed]
    _ â‰¤ âˆ‘' i, Î¼ (disjointed t i) :=
      OuterMeasureClass.measure_iUnion_nat_le _ _ (disjoint_disjointed _)
    _ â‰¤ âˆ‘' i, Î¼ (t i) := by gcongr; exact disjointed_subset ..

theorem measure_biUnion_le {I : Set Î¹} (Î¼ : F) (hI : I.Countable) (s : Î¹ â†’ Set Î±) :
    Î¼ (â‹ƒ i âˆˆ I, s i) â‰¤ âˆ‘' i : I, Î¼ (s i) := by
  have := hI.to_subtype
  rw [biUnion_eq_iUnion]
  apply measure_iUnion_le

theorem measure_biUnion_finset_le (I : Finset Î¹) (s : Î¹ â†’ Set Î±) :
    Î¼ (â‹ƒ i âˆˆ I, s i) â‰¤ âˆ‘ i âˆˆ I, Î¼ (s i) :=
  (measure_biUnion_le Î¼ I.countable_toSet s).trans_eq <| I.tsum_subtype (Î¼ <| s Â·)

theorem measure_iUnion_fintype_le [Fintype Î¹] (Î¼ : F) (s : Î¹ â†’ Set Î±) :
    Î¼ (â‹ƒ i, s i) â‰¤ âˆ‘ i, Î¼ (s i) := by
  simpa using measure_biUnion_finset_le Finset.univ s

theorem measure_union_le (s t : Set Î±) : Î¼ (s âˆª t) â‰¤ Î¼ s + Î¼ t := by
  simpa [union_eq_iUnion] using measure_iUnion_fintype_le Î¼ (cond Â· s t)

lemma measure_univ_le_add_compl (s : Set Î±) : Î¼ univ â‰¤ Î¼ s + Î¼ sá¶œ :=
  s.union_compl_self â–¸ measure_union_le s sá¶œ

theorem measure_le_inter_add_diff (Î¼ : F) (s t : Set Î±) : Î¼ s â‰¤ Î¼ (s âˆ© t) + Î¼ (s \ t) := by
  simpa using measure_union_le (s âˆ© t) (s \ t)

theorem measure_diff_null (ht : Î¼ t = 0) : Î¼ (s \ t) = Î¼ s :=
  (measure_mono diff_subset).antisymm <| calc
    Î¼ s â‰¤ Î¼ (s âˆ© t) + Î¼ (s \ t) := measure_le_inter_add_diff _ _ _
    _ â‰¤ Î¼ t + Î¼ (s \ t) := by gcongr; apply inter_subset_right
    _ = Î¼ (s \ t) := by simp [ht]

theorem measure_biUnion_null_iff {I : Set Î¹} (hI : I.Countable) {s : Î¹ â†’ Set Î±} :
    Î¼ (â‹ƒ i âˆˆ I, s i) = 0 â†” âˆ€ i âˆˆ I, Î¼ (s i) = 0 := by
  refine âŸ¨fun h i hi â†¦ measure_mono_null (subset_biUnion_of_mem hi) h, fun h â†¦ ?_âŸ©
  have _ := hI.to_subtype
  simpa [h] using measure_iUnion_le (Î¼ := Î¼) fun x : I â†¦ s x

theorem measure_sUnion_null_iff {S : Set (Set Î±)} (hS : S.Countable) :
    Î¼ (â‹ƒâ‚€ S) = 0 â†” âˆ€ s âˆˆ S, Î¼ s = 0 := by
  rw [sUnion_eq_biUnion, measure_biUnion_null_iff hS]

@[simp]
theorem measure_iUnion_null_iff {Î¹ : Sort*} [Countable Î¹] {s : Î¹ â†’ Set Î±} :
    Î¼ (â‹ƒ i, s i) = 0 â†” âˆ€ i, Î¼ (s i) = 0 := by
  rw [â† sUnion_range, measure_sUnion_null_iff (countable_range s), forall_mem_range]

alias âŸ¨_, measure_iUnion_nullâŸ© := measure_iUnion_null_iff

@[simp]
theorem measure_union_null_iff : Î¼ (s âˆª t) = 0 â†” Î¼ s = 0 âˆ§ Î¼ t = 0 := by
  simp [union_eq_iUnion, and_comm]

theorem measure_union_null (hs : Î¼ s = 0) (ht : Î¼ t = 0) : Î¼ (s âˆª t) = 0 := by simp [*]

lemma measure_null_iff_singleton (hs : s.Countable) : Î¼ s = 0 â†” âˆ€ x âˆˆ s, Î¼ {x} = 0 := by
  rw [â† measure_biUnion_null_iff hs, biUnion_of_singleton]

/-- Let `Î¼` be an (outer) measure; let `s : Î¹ â†’ Set Î±` be a sequence of sets, `S = â‹ƒ n, s n`.
If `Î¼ (S \ s n)` tends to zero along some nontrivial filter (usually `Filter.atTop` on `Î¹ = â„•`),
then `Î¼ S = â¨† n, Î¼ (s n)`. -/
theorem measure_iUnion_of_tendsto_zero {Î¹} (Î¼ : F) {s : Î¹ â†’ Set Î±} (l : Filter Î¹) [NeBot l]
    (h0 : Tendsto (fun k => Î¼ ((â‹ƒ n, s n) \ s k)) l (ğ“ 0)) : Î¼ (â‹ƒ n, s n) = â¨† n, Î¼ (s n) := by
  refine le_antisymm ?_ <| iSup_le fun n â†¦ measure_mono <| subset_iUnion _ _
  set S := â‹ƒ n, s n
  set M := â¨† n, Î¼ (s n)
  have A : âˆ€ k, Î¼ S â‰¤ M + Î¼ (S \ s k) := fun k â†¦ calc
    Î¼ S â‰¤ Î¼ (S âˆ© s k) + Î¼ (S \ s k) := measure_le_inter_add_diff _ _ _
    _ â‰¤ Î¼ (s k) + Î¼ (S \ s k) := by gcongr; apply inter_subset_right
    _ â‰¤ M + Î¼ (S \ s k) := by gcongr; exact le_iSup (Î¼ âˆ˜ s) k
  have B : Tendsto (fun k â†¦ M + Î¼ (S \ s k)) l (ğ“ M) := by simpa using tendsto_const_nhds.add h0
  exact ge_of_tendsto' B A

/-- If a set has zero measure in a neighborhood of each of its points, then it has zero measure
in a second-countable space. -/
theorem measure_null_of_locally_null [TopologicalSpace Î±] [SecondCountableTopology Î±]
    (s : Set Î±) (hs : âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“[s] x, Î¼ u = 0) : Î¼ s = 0 := by
  choose! u hxu huâ‚€ using hs
  choose t ht using TopologicalSpace.countable_cover_nhdsWithin hxu
  rcases ht with âŸ¨ts, t_count, htâŸ©
  apply measure_mono_null ht
  exact (measure_biUnion_null_iff t_count).2 fun x hx => huâ‚€ x (ts hx)

/-- If `m s â‰  0`, then for some point `x âˆˆ s` and any `t âˆˆ ğ“[s] x` we have `0 < m t`. -/
theorem exists_mem_forall_mem_nhdsWithin_pos_measure [TopologicalSpace Î±]
    [SecondCountableTopology Î±] {s : Set Î±} (hs : Î¼ s â‰  0) :
    âˆƒ x âˆˆ s, âˆ€ t âˆˆ ğ“[s] x, 0 < Î¼ t := by
  contrapose! hs
  simp only [nonpos_iff_eq_zero] at hs
  exact measure_null_of_locally_null s hs

end OuterMeasureClass

namespace OuterMeasure

variable {Î± Î² : Type*} {m : OuterMeasure Î±}

/-- If `s : Î¹ â†’ Set Î±` is a sequence of sets, `S = â‹ƒ n, s n`, and `m (S \ s n)` tends to zero along
some nontrivial filter (usually `atTop` on `Î¹ = â„•`), then `m S = â¨† n, m (s n)`. -/
theorem iUnion_of_tendsto_zero {Î¹} (m : OuterMeasure Î±) {s : Î¹ â†’ Set Î±} (l : Filter Î¹) [NeBot l]
    (h0 : Tendsto (fun k => m ((â‹ƒ n, s n) \ s k)) l (ğ“ 0)) : m (â‹ƒ n, s n) = â¨† n, m (s n) :=
  measure_iUnion_of_tendsto_zero m l h0

/-- If `s : â„• â†’ Set Î±` is a monotone sequence of sets such that `âˆ‘' k, m (s (k + 1) \ s k) â‰  âˆ`,
then `m (â‹ƒ n, s n) = â¨† n, m (s n)`. -/
theorem iUnion_nat_of_monotone_of_tsum_ne_top (m : OuterMeasure Î±) {s : â„• â†’ Set Î±}
    (h_mono : âˆ€ n, s n âŠ† s (n + 1)) (h0 : (âˆ‘' k, m (s (k + 1) \ s k)) â‰  âˆ) :
    m (â‹ƒ n, s n) = â¨† n, m (s n) := by
  classical
  refine measure_iUnion_of_tendsto_zero m atTop ?_
  refine tendsto_nhds_bot_mono' (ENNReal.tendsto_sum_nat_add _ h0) fun n => ?_
  refine (m.mono ?_).trans (measure_iUnion_le _)
  -- Current goal: `(â‹ƒ k, s k) \ s n âŠ† â‹ƒ k, s (k + n + 1) \ s (k + n)`
  have h' : Monotone s := @monotone_nat_of_le_succ (Set Î±) _ _ h_mono
  simp only [diff_subset_iff, iUnion_subset_iff]
  intro i x hx
  have : âˆƒi, x âˆˆ s i := by exists i
  rcases Nat.findX this with âŸ¨j, hj, hltâŸ©
  clear hx i
  rcases le_or_gt j n with hjn | hnj
  Â· exact Or.inl (h' hjn hj)
  have : j - (n + 1) + n + 1 = j := by omega
  refine Or.inr (mem_iUnion.2 âŸ¨j - (n + 1), ?_, hlt _ ?_âŸ©)
  Â· rwa [this]
  Â· rw [â† Nat.succ_le_iff, Nat.succ_eq_add_one, this]

theorem coe_fn_injective : Injective fun (Î¼ : OuterMeasure Î±) (s : Set Î±) => Î¼ s :=
  DFunLike.coe_injective

@[ext]
theorem ext {Î¼â‚ Î¼â‚‚ : OuterMeasure Î±} (h : âˆ€ s, Î¼â‚ s = Î¼â‚‚ s) : Î¼â‚ = Î¼â‚‚ :=
  DFunLike.ext _ _ h

/-- A version of `MeasureTheory.OuterMeasure.ext` that assumes `Î¼â‚ s = Î¼â‚‚ s` on all *nonempty*
sets `s`, and gets `Î¼â‚ âˆ… = Î¼â‚‚ âˆ…` from `MeasureTheory.OuterMeasure.empty'`. -/
theorem ext_nonempty {Î¼â‚ Î¼â‚‚ : OuterMeasure Î±} (h : âˆ€ s : Set Î±, s.Nonempty â†’ Î¼â‚ s = Î¼â‚‚ s) :
    Î¼â‚ = Î¼â‚‚ :=
  ext fun s => s.eq_empty_or_nonempty.elim (fun he => by simp [he]) (h s)

end OuterMeasure

end MeasureTheory
