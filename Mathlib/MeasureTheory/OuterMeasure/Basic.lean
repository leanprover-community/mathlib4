/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Data.Countable.Basic
import Mathlib.Data.Fin.VecNotation
import Mathlib.Order.Disjointed
import Mathlib.MeasureTheory.OuterMeasure.Defs

#align_import measure_theory.measure.outer_measure from "leanprover-community/mathlib"@"343e80208d29d2d15f8050b929aa50fe4ce71b55"

/-!
# Outer Measures

An outer measure is a function `Œº : Set Œ± ‚Üí ‚Ñù‚â•0‚àû`, from the powerset of a type to the extended
nonnegative real numbers that satisfies the following conditions:
1. `Œº ‚àÖ = 0`;
2. `Œº` is monotone;
3. `Œº` is countably subadditive. This means that the outer measure of a countable union is at most
   the sum of the outer measure on the individual sets.

Note that we do not need `Œ±` to be measurable to define an outer measure.

## References

<https://en.wikipedia.org/wiki/Outer_measure>

## Tags

outer measure
-/


noncomputable section

open Set Function Filter
open scoped Classical BigOperators NNReal Topology ENNReal

namespace MeasureTheory

section OuterMeasureClass

variable {Œ± Œπ F : Type*} [FunLike F (Set Œ±) ‚Ñù‚â•0‚àû] [OuterMeasureClass F Œ±]
  {Œº : F} {s t : Set Œ±}

@[simp]
theorem measure_empty (Œº : F) : Œº ‚àÖ = 0 := OuterMeasureClass.measure_empty Œº
#align measure_theory.measure_empty MeasureTheory.measure_empty

@[mono, gcongr]
theorem measure_mono (Œº : F) (h : s ‚äÜ t) : Œº s ‚â§ Œº t :=
  OuterMeasureClass.measure_mono Œº h
#align measure_theory.measure_mono MeasureTheory.measure_mono

theorem measure_mono_null (h : s ‚äÜ t) (ht : Œº t = 0) : Œº s = 0 :=
  eq_bot_mono (measure_mono Œº h) ht
#align measure_theory.measure_mono_null MeasureTheory.measure_mono_null

theorem measure_pos_of_superset (h : s ‚äÜ t) (hs : Œº s ‚â† 0) : 0 < Œº t :=
  hs.bot_lt.trans_le (measure_mono Œº h)

theorem measure_iUnion_le (Œº : F) [Countable Œπ] (s : Œπ ‚Üí Set Œ±) : Œº (‚ãÉ i, s i) ‚â§ ‚àë' i, Œº (s i) := by
  refine rel_iSup_tsum m m.empty (¬∑ ‚â§ ¬∑) (fun t ‚Ü¶ ?_) _
  calc
    m (‚ãÉ i, t i) = m (‚ãÉ i, disjointed t i) := by rw [iUnion_disjointed]
    _ ‚â§ ‚àë' i, m (disjointed t i) := measure_iUnion_nat_le _ (disjoint_disjointed _)
    _ ‚â§ ‚àë' i, m (t i) := ENNReal.tsum_le_tsum fun _ ‚Ü¶ measure_mono <| disjointed_subset _ _
#align measure_theory.measure_Union_le MeasureTheory.measure_iUnion_le

theorem measure_biUnion_le {I : Set Œπ} (Œº : F) (hI : I.Countable) (s : Œπ ‚Üí Set Œ±) :
    Œº (‚ãÉ i ‚àà I, s i) ‚â§ ‚àë' i : I, Œº (s i) := by
  have := hI.to_subtype
  rw [biUnion_eq_iUnion]
  apply measure_iUnion_le
#align measure_theory.measure_bUnion_le MeasureTheory.measure_biUnion_le

theorem measure_biUnion_finset_le (Œº : F) (I : Finset Œπ) (s : Œπ ‚Üí Set Œ±) :
    Œº (‚ãÉ i ‚àà I, s i) ‚â§ ‚àë i in I, Œº (s i) :=
  (measure_biUnion_le Œº I.countable_toSet s).trans_eq <| I.tsum_subtype (Œº <| s ¬∑)
#align measure_theory.measure_bUnion_finset_le MeasureTheory.measure_biUnion_finset_le

theorem measure_iUnion_fintype_le [Fintype Œπ] (Œº : F) (s : Œπ ‚Üí Set Œ±) :
    Œº (‚ãÉ i, s i) ‚â§ ‚àë i, Œº (s i) := by
  simpa using measure_biUnion_finset_le Œº Finset.univ s
#align measure_theory.measure_Union_fintype_le MeasureTheory.measure_iUnion_fintype_le

theorem measure_union_le (Œº : F) (s t : Set Œ±) : Œº (s ‚à™ t) ‚â§ Œº s + Œº t := by
  simpa [union_eq_iUnion] using measure_iUnion_fintype_le Œº (cond ¬∑ s t)
#align measure_theory.measure_union_le MeasureTheory.measure_union_le

theorem measure_le_inter_add_diff (Œº : F) (s t : Set Œ±) : Œº s ‚â§ Œº (s ‚à© t) + Œº (s \ t) := by
  simpa using measure_union_le Œº (s ‚à© t) (s \ t)

theorem measure_diff_null (Œº : F) (s : Set Œ±) (ht : Œº t = 0) : Œº (s \ t) = Œº s :=
  (measure_mono Œº <| diff_subset _ _).antisymm <| calc
    Œº s ‚â§ Œº (s ‚à© t) + Œº (s \ t) := measure_le_inter_add_diff _ _ _
    _ ‚â§ Œº t + Œº (s \ t) := by gcongr; apply inter_subset_right
    _ = Œº (s \ t) := by simp [ht]
#align measure_theory.measure_diff_null MeasureTheory.measure_diff_null

theorem measure_biUnion_null_iff {I : Set Œπ} (hI : I.Countable) {s : Œπ ‚Üí Set Œ±} :
    Œº (‚ãÉ i ‚àà I, s i) = 0 ‚Üî ‚àÄ i ‚àà I, Œº (s i) = 0 := by
  refine ‚ü®fun h i hi ‚Ü¶ measure_mono_null (subset_biUnion_of_mem hi) h, fun h ‚Ü¶ ?_‚ü©
  have _ := hI.to_subtype
  simpa [h] using measure_iUnion_le Œº fun x : I ‚Ü¶ s x
#align measure_theory.measure_bUnion_null_iff MeasureTheory.measure_biUnion_null_iff

theorem measure_sUnion_null_iff {S : Set (Set Œ±)} (hS : S.Countable) :
    Œº (‚ãÉ‚ÇÄ S) = 0 ‚Üî ‚àÄ s ‚àà S, Œº s = 0 := by
  rw [sUnion_eq_biUnion, measure_biUnion_null_iff hS]
#align measure_theory.measure_sUnion_null_iff MeasureTheory.measure_sUnion_null_iff

@[simp]
theorem measure_iUnion_null_iff {Œπ : Sort*} [Countable Œπ] {s : Œπ ‚Üí Set Œ±} :
    Œº (‚ãÉ i, s i) = 0 ‚Üî ‚àÄ i, Œº (s i) = 0 := by
  rw [‚Üê sUnion_range, measure_sUnion_null_iff (countable_range s), forall_mem_range]
#align measure_theory.measure_Union_null_iff MeasureTheory.measure_iUnion_null_iff

alias ‚ü®_, measure_iUnion_null‚ü© := measure_iUnion_null_iff
#align measure_theory.measure_Union_null MeasureTheory.measure_iUnion_null

@[simp]
theorem measure_union_null_iff : Œº (s ‚à™ t) = 0 ‚Üî Œº s = 0 ‚àß Œº t = 0 := by
  simp [union_eq_iUnion, and_comm]
#align measure_theory.measure_union_null_iff MeasureTheory.measure_union_null_iff

theorem measure_union_null (hs : Œº s = 0) (ht : Œº t = 0) : Œº (s ‚à™ t) = 0 := by simp [*]
#align measure_theory.measure_union_null MeasureTheory.measure_union_null

/-- Let `Œº` be an (outer) measure; let `s : Œπ ‚Üí Set Œ±` be a sequence of sets, `S = ‚ãÉ n, s n`.
If `Œº (S \ s n)` tends to zero along some nontrivial filter (usually `Filter.atTop` on `Œπ = ‚Ñï`),
then `Œº S = ‚®Ü n, Œº (s n)`. -/
theorem measure_iUnion_of_tendsto_zero {Œπ} (Œº : F) {s : Œπ ‚Üí Set Œ±} (l : Filter Œπ) [NeBot l]
    (h0 : Tendsto (fun k => Œº ((‚ãÉ n, s n) \ s k)) l (ùìù 0)) : Œº (‚ãÉ n, s n) = ‚®Ü n, Œº (s n) := by
  refine le_antisymm ?_ <| iSup_le fun n ‚Ü¶ measure_mono Œº <| subset_iUnion _ _
  set S := ‚ãÉ n, s n
  set M := ‚®Ü n, Œº (s n)
  have A : ‚àÄ k, Œº S ‚â§ M + Œº (S \ s k) := fun k ‚Ü¶ calc
    Œº S ‚â§ Œº (S ‚à© s k) + Œº (S \ s k) := measure_le_inter_add_diff _ _ _
    _ ‚â§ Œº (s k) + Œº (S \ s k) := by gcongr; apply inter_subset_right
    _ ‚â§ M + Œº (S \ s k) := by gcongr; exact le_iSup (Œº ‚àò s) k
  have B : Tendsto (fun k ‚Ü¶ M + Œº (S \ s k)) l (ùìù M) := by simpa using tendsto_const_nhds.add h0
  exact ge_of_tendsto' B A

/-- If a set has zero measure in a neighborhood of each of its points, then it has zero measure
in a second-countable space. -/
theorem measure_null_of_locally_null [TopologicalSpace Œ±] [SecondCountableTopology Œ±]
    (s : Set Œ±) (hs : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù[s] x, Œº u = 0) : Œº s = 0 := by
  choose! u hxu hu‚ÇÄ using hs
  choose t ht using TopologicalSpace.countable_cover_nhdsWithin hxu
  rcases ht with ‚ü®ts, t_count, ht‚ü©
  apply measure_mono_null ht
  exact (measure_biUnion_null_iff t_count).2 fun x hx => hu‚ÇÄ x (ts hx)
#align measure_theory.null_of_locally_null MeasureTheory.measure_null_of_locally_null

/-- If `m s ‚â† 0`, then for some point `x ‚àà s` and any `t ‚àà ùìù[s] x` we have `0 < m t`. -/
theorem exists_mem_forall_mem_nhdsWithin_pos_measure [TopologicalSpace Œ±]
    [SecondCountableTopology Œ±] {s : Set Œ±} (hs : Œº s ‚â† 0) :
    ‚àÉ x ‚àà s, ‚àÄ t ‚àà ùìù[s] x, 0 < Œº t := by
  contrapose! hs
  simp only [nonpos_iff_eq_zero] at hs
  exact measure_null_of_locally_null s hs
#align measure_theory.exists_mem_forall_mem_nhds_within_pos_measure MeasureTheory.exists_mem_forall_mem_nhdsWithin_pos_measure

end OuterMeasureClass

namespace OuterMeasure

variable {Œ± Œ≤ : Type*} {m : OuterMeasure Œ±}

@[deprecated measure_empty (since := "2024-05-14")]
theorem empty' (m : OuterMeasure Œ±) : m ‚àÖ = 0 := measure_empty m
#align measure_theory.outer_measure.empty' MeasureTheory.OuterMeasure.empty'

@[deprecated measure_mono (since := "2024-05-14")]
theorem mono' (m : OuterMeasure Œ±) {s‚ÇÅ s‚ÇÇ} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : m s‚ÇÅ ‚â§ m s‚ÇÇ := by gcongr
#align measure_theory.outer_measure.mono' MeasureTheory.OuterMeasure.mono'

@[deprecated measure_mono_null (since := "2024-05-14")]
theorem mono_null (m : OuterMeasure Œ±) {s t} (h : s ‚äÜ t) (ht : m t = 0) : m s = 0 :=
  measure_mono_null h ht
#align measure_theory.outer_measure.mono_null MeasureTheory.OuterMeasure.mono_null

@[deprecated measure_pos_of_superset (since := "2024-05-14")]
theorem pos_of_subset_ne_zero (m : OuterMeasure Œ±) {a b : Set Œ±} (hs : a ‚äÜ b) (hnz : m a ‚â† 0) :
    0 < m b :=
  measure_pos_of_superset hs hnz
#align measure_theory.outer_measure.pos_of_subset_ne_zero MeasureTheory.OuterMeasure.pos_of_subset_ne_zero

@[deprecated measure_iUnion_le (since := "2024-05-14")]
protected theorem iUnion (m : OuterMeasure Œ±) {Œ≤} [Countable Œ≤] (s : Œ≤ ‚Üí Set Œ±) :
    m (‚ãÉ i, s i) ‚â§ ‚àë' i, m (s i) :=
  measure_iUnion_le m s
#align measure_theory.outer_measure.Union MeasureTheory.OuterMeasure.iUnion

@[deprecated measure_biUnion_null_iff (since := "2024-05-14")]
theorem biUnion_null_iff (m : OuterMeasure Œ±) {s : Set Œ≤} (hs : s.Countable) {t : Œ≤ ‚Üí Set Œ±} :
    m (‚ãÉ i ‚àà s, t i) = 0 ‚Üî ‚àÄ i ‚àà s, m (t i) = 0 :=
  measure_biUnion_null_iff hs
#align measure_theory.outer_measure.bUnion_null_iff MeasureTheory.OuterMeasure.biUnion_null_iff

@[deprecated measure_sUnion_null_iff (since := "2024-05-14")]
theorem sUnion_null_iff (m : OuterMeasure Œ±) {S : Set (Set Œ±)} (hS : S.Countable) :
    m (‚ãÉ‚ÇÄ S) = 0 ‚Üî ‚àÄ s ‚àà S, m s = 0 := measure_sUnion_null_iff hS
#align measure_theory.outer_measure.sUnion_null_iff MeasureTheory.OuterMeasure.sUnion_null_iff

@[deprecated measure_iUnion_null_iff (since := "2024-05-14")]
theorem iUnion_null_iff {Œπ : Sort*} [Countable Œπ] (m : OuterMeasure Œ±) {s : Œπ ‚Üí Set Œ±} :
    m (‚ãÉ i, s i) = 0 ‚Üî ‚àÄ i, m (s i) = 0 :=
  measure_iUnion_null_iff
#align measure_theory.outer_measure.Union_null_iff MeasureTheory.OuterMeasure.iUnion_null_iff

@[deprecated measure_iUnion_null (since := "2024-05-14")]
alias ‚ü®_, iUnion_null‚ü© := iUnion_null_iff
#align measure_theory.outer_measure.Union_null MeasureTheory.OuterMeasure.iUnion_null

@[deprecated (since := "2024-01-14")]
theorem iUnion_null_iff' (m : OuterMeasure Œ±) {Œπ : Prop} {s : Œπ ‚Üí Set Œ±} :
    m (‚ãÉ i, s i) = 0 ‚Üî ‚àÄ i, m (s i) = 0 :=
  m.iUnion_null_iff
#align measure_theory.outer_measure.Union_null_iff' MeasureTheory.OuterMeasure.iUnion_null_iff'

@[deprecated measure_biUnion_finset_le (since := "2024-05-14")]
protected theorem iUnion_finset (m : OuterMeasure Œ±) (s : Œ≤ ‚Üí Set Œ±) (t : Finset Œ≤) :
    m (‚ãÉ i ‚àà t, s i) ‚â§ ‚àë i in t, m (s i) :=
  measure_biUnion_finset_le m t s
#align measure_theory.outer_measure.Union_finset MeasureTheory.OuterMeasure.iUnion_finset

@[deprecated measure_union_le (since := "2024-05-14")]
protected theorem union (m : OuterMeasure Œ±) (s‚ÇÅ s‚ÇÇ : Set Œ±) : m (s‚ÇÅ ‚à™ s‚ÇÇ) ‚â§ m s‚ÇÅ + m s‚ÇÇ :=
  measure_union_le m s‚ÇÅ s‚ÇÇ
#align measure_theory.outer_measure.union MeasureTheory.OuterMeasure.union

/-- If a set has zero measure in a neighborhood of each of its points, then it has zero measure
in a second-countable space. -/
@[deprecated measure_null_of_locally_null (since := "2024-05-14")]
theorem null_of_locally_null [TopologicalSpace Œ±] [SecondCountableTopology Œ±] (m : OuterMeasure Œ±)
    (s : Set Œ±) (hs : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù[s] x, m u = 0) : m s = 0 :=
  measure_null_of_locally_null s hs
#align measure_theory.outer_measure.null_of_locally_null MeasureTheory.OuterMeasure.null_of_locally_null

/-- If `m s ‚â† 0`, then for some point `x ‚àà s` and any `t ‚àà ùìù[s] x` we have `0 < m t`. -/
@[deprecated exists_mem_forall_mem_nhdsWithin_pos_measure (since := "2024-05-14")]
theorem exists_mem_forall_mem_nhds_within_pos [TopologicalSpace Œ±] [SecondCountableTopology Œ±]
    (m : OuterMeasure Œ±) {s : Set Œ±} (hs : m s ‚â† 0) : ‚àÉ x ‚àà s, ‚àÄ t ‚àà ùìù[s] x, 0 < m t :=
  exists_mem_forall_mem_nhdsWithin_pos_measure hs
#align measure_theory.outer_measure.exists_mem_forall_mem_nhds_within_pos MeasureTheory.OuterMeasure.exists_mem_forall_mem_nhds_within_pos

/-- If `s : Œπ ‚Üí Set Œ±` is a sequence of sets, `S = ‚ãÉ n, s n`, and `m (S \ s n)` tends to zero along
some nontrivial filter (usually `atTop` on `Œπ = ‚Ñï`), then `m S = ‚®Ü n, m (s n)`. -/
theorem iUnion_of_tendsto_zero {Œπ} (m : OuterMeasure Œ±) {s : Œπ ‚Üí Set Œ±} (l : Filter Œπ) [NeBot l]
    (h0 : Tendsto (fun k => m ((‚ãÉ n, s n) \ s k)) l (ùìù 0)) : m (‚ãÉ n, s n) = ‚®Ü n, m (s n) :=
  measure_iUnion_of_tendsto_zero m l h0
#align measure_theory.outer_measure.Union_of_tendsto_zero MeasureTheory.OuterMeasure.iUnion_of_tendsto_zero

/-- If `s : ‚Ñï ‚Üí Set Œ±` is a monotone sequence of sets such that `‚àë' k, m (s (k + 1) \ s k) ‚â† ‚àû`,
then `m (‚ãÉ n, s n) = ‚®Ü n, m (s n)`. -/
theorem iUnion_nat_of_monotone_of_tsum_ne_top (m : OuterMeasure Œ±) {s : ‚Ñï ‚Üí Set Œ±}
    (h_mono : ‚àÄ n, s n ‚äÜ s (n + 1)) (h0 : (‚àë' k, m (s (k + 1) \ s k)) ‚â† ‚àû) :
    m (‚ãÉ n, s n) = ‚®Ü n, m (s n) := by
  refine measure_iUnion_of_tendsto_zero m atTop ?_
  refine tendsto_nhds_bot_mono' (ENNReal.tendsto_sum_nat_add _ h0) fun n => ?_
  refine' (m.mono _).trans (m.iUnion _)
  -- Current goal: `(‚ãÉ k, s k) \ s n ‚äÜ ‚ãÉ k, s (k + n + 1) \ s (k + n)`
  have h' : Monotone s := @monotone_nat_of_le_succ (Set Œ±) _ _ h_mono
  simp only [diff_subset_iff, iUnion_subset_iff]
  intro i x hx
  have : ‚àÉi, x ‚àà s i := by exists i
  rcases Nat.findX this with ‚ü®j, hj, hlt‚ü©
  clear hx i
  rcases le_or_lt j n with hjn | hnj
  ¬∑ exact Or.inl (h' hjn hj)
  have : j - (n + 1) + n + 1 = j := by omega
  refine' Or.inr (mem_iUnion.2 ‚ü®j - (n + 1), _, hlt _ _‚ü©)
  ¬∑ rwa [this]
  ¬∑ rw [‚Üê Nat.succ_le_iff, Nat.succ_eq_add_one, this]
#align measure_theory.outer_measure.Union_nat_of_monotone_of_tsum_ne_top MeasureTheory.OuterMeasure.iUnion_nat_of_monotone_of_tsum_ne_top

@[deprecated measure_le_inter_add_diff (since := "2024-05-14")]
theorem le_inter_add_diff {m : OuterMeasure Œ±} {t : Set Œ±} (s : Set Œ±) :
    m t ‚â§ m (t ‚à© s) + m (t \ s) :=
  measure_le_inter_add_diff m t s
#align measure_theory.outer_measure.le_inter_add_diff MeasureTheory.OuterMeasure.le_inter_add_diff

@[deprecated measure_diff_null (since := "2024-05-14")]
theorem diff_null (m : OuterMeasure Œ±) (s : Set Œ±) {t : Set Œ±} (ht : m t = 0) : m (s \ t) = m s :=
  measure_diff_null m s ht
#align measure_theory.outer_measure.diff_null MeasureTheory.OuterMeasure.diff_null

@[deprecated measure_union_null (since := "2024-05-14")]
theorem union_null (m : OuterMeasure Œ±) {s‚ÇÅ s‚ÇÇ : Set Œ±} (h‚ÇÅ : m s‚ÇÅ = 0) (h‚ÇÇ : m s‚ÇÇ = 0) :
    m (s‚ÇÅ ‚à™ s‚ÇÇ) = 0 :=
  measure_union_null h‚ÇÅ h‚ÇÇ
#align measure_theory.outer_measure.union_null MeasureTheory.OuterMeasure.union_null

theorem coe_fn_injective : Injective fun (Œº : OuterMeasure Œ±) (s : Set Œ±) => Œº s :=
  DFunLike.coe_injective
#align measure_theory.outer_measure.coe_fn_injective MeasureTheory.OuterMeasure.coe_fn_injective

@[ext]
theorem ext {Œº‚ÇÅ Œº‚ÇÇ : OuterMeasure Œ±} (h : ‚àÄ s, Œº‚ÇÅ s = Œº‚ÇÇ s) : Œº‚ÇÅ = Œº‚ÇÇ :=
  DFunLike.ext _ _ h
#align measure_theory.outer_measure.ext MeasureTheory.OuterMeasure.ext

/-- A version of `MeasureTheory.OuterMeasure.ext` that assumes `Œº‚ÇÅ s = Œº‚ÇÇ s` on all *nonempty*
sets `s`, and gets `Œº‚ÇÅ ‚àÖ = Œº‚ÇÇ ‚àÖ` from `MeasureTheory.OuterMeasure.empty'`. -/
theorem ext_nonempty {Œº‚ÇÅ Œº‚ÇÇ : OuterMeasure Œ±} (h : ‚àÄ s : Set Œ±, s.Nonempty ‚Üí Œº‚ÇÅ s = Œº‚ÇÇ s) :
    Œº‚ÇÅ = Œº‚ÇÇ :=
  ext fun s => s.eq_empty_or_nonempty.elim (fun he => by simp [he]) (h s)
#align measure_theory.outer_measure.ext_nonempty MeasureTheory.OuterMeasure.ext_nonempty
