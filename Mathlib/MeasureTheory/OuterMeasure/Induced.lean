/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.MeasureTheory.OuterMeasure.Caratheodory

/-!
# Induced Outer Measure

We can extend a function defined on a subset of `Set Î±` to an outer measure.
The underlying function is called `extend`, and the measure it induces is called
`inducedOuterMeasure`.

Some lemmas below are proven twice, once in the general case, and one where the function `m`
is only defined on measurable sets (i.e. when `P = MeasurableSet`). In the latter cases, we can
remove some hypotheses in the statement. The general version has the same name, but with a prime
at the end.

## Tags

outer measure

-/

#align_import measure_theory.measure.outer_measure from "leanprover-community/mathlib"@"343e80208d29d2d15f8050b929aa50fe4ce71b55"

noncomputable section

open Set Function Filter
open scoped Classical NNReal Topology ENNReal

namespace MeasureTheory

open OuterMeasure


section Extend

variable {Î± : Type*} {P : Î± â†’ Prop}
variable (m : âˆ€ s : Î±, P s â†’ â„â‰¥0âˆ)

/-- We can trivially extend a function defined on a subclass of objects (with codomain `â„â‰¥0âˆ`)
  to all objects by defining it to be `âˆ` on the objects not in the class. -/
def extend (s : Î±) : â„â‰¥0âˆ :=
  â¨… h : P s, m s h
#align measure_theory.extend MeasureTheory.extend

theorem extend_eq {s : Î±} (h : P s) : extend m s = m s h := by simp [extend, h]
#align measure_theory.extend_eq MeasureTheory.extend_eq

theorem extend_eq_top {s : Î±} (h : Â¬P s) : extend m s = âˆ := by simp [extend, h]
#align measure_theory.extend_eq_top MeasureTheory.extend_eq_top

theorem smul_extend {R} [Zero R] [SMulWithZero R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]
    [NoZeroSMulDivisors R â„â‰¥0âˆ] {c : R} (hc : c â‰  0) :
    c â€¢ extend m = extend fun s h => c â€¢ m s h := by
  ext1 s
  dsimp [extend]
  by_cases h : P s
  Â· simp [h]
  Â· simp [h, ENNReal.smul_top, hc]
#align measure_theory.smul_extend MeasureTheory.smul_extend

theorem le_extend {s : Î±} (h : P s) : m s h â‰¤ extend m s := by
  simp only [extend, le_iInf_iff]
  intro
  rfl
#align measure_theory.le_extend MeasureTheory.le_extend

-- TODO: why this is a bad `congr` lemma?
theorem extend_congr {Î² : Type*} {Pb : Î² â†’ Prop} {mb : âˆ€ s : Î², Pb s â†’ â„â‰¥0âˆ} {sa : Î±} {sb : Î²}
    (hP : P sa â†” Pb sb) (hm : âˆ€ (ha : P sa) (hb : Pb sb), m sa ha = mb sb hb) :
    extend m sa = extend mb sb :=
  iInf_congr_Prop hP fun _h => hm _ _
#align measure_theory.extend_congr MeasureTheory.extend_congr

@[simp]
theorem extend_top {Î± : Type*} {P : Î± â†’ Prop} : extend (fun _ _ => âˆ : âˆ€ s : Î±, P s â†’ â„â‰¥0âˆ) = âŠ¤ :=
  funext fun _ => iInf_eq_top.mpr fun _ => rfl
#align measure_theory.extend_top MeasureTheory.extend_top

end Extend

section ExtendSet

variable {Î± : Type*} {P : Set Î± â†’ Prop}
variable {m : âˆ€ s : Set Î±, P s â†’ â„â‰¥0âˆ}
variable (P0 : P âˆ…) (m0 : m âˆ… P0 = 0)
variable (PU : âˆ€ â¦ƒf : â„• â†’ Set Î±â¦„ (_hm : âˆ€ i, P (f i)), P (â‹ƒ i, f i))
variable
  (mU :
    âˆ€ â¦ƒf : â„• â†’ Set Î±â¦„ (hm : âˆ€ i, P (f i)),
      Pairwise (Disjoint on f) â†’ m (â‹ƒ i, f i) (PU hm) = âˆ‘' i, m (f i) (hm i))

variable (msU : âˆ€ â¦ƒf : â„• â†’ Set Î±â¦„ (hm : âˆ€ i, P (f i)), m (â‹ƒ i, f i) (PU hm) â‰¤ âˆ‘' i, m (f i) (hm i))
variable (m_mono : âˆ€ â¦ƒsâ‚ sâ‚‚ : Set Î±â¦„ (hsâ‚ : P sâ‚) (hsâ‚‚ : P sâ‚‚), sâ‚ âŠ† sâ‚‚ â†’ m sâ‚ hsâ‚ â‰¤ m sâ‚‚ hsâ‚‚)

theorem extend_empty : extend m âˆ… = 0 :=
  (extend_eq _ P0).trans m0
#align measure_theory.extend_empty MeasureTheory.extend_empty

theorem extend_iUnion_nat {f : â„• â†’ Set Î±} (hm : âˆ€ i, P (f i))
    (mU : m (â‹ƒ i, f i) (PU hm) = âˆ‘' i, m (f i) (hm i)) :
    extend m (â‹ƒ i, f i) = âˆ‘' i, extend m (f i) :=
  (extend_eq _ _).trans <|
    mU.trans <| by
      congr with i
      rw [extend_eq]
#align measure_theory.extend_Union_nat MeasureTheory.extend_iUnion_nat

section Subadditive

theorem extend_iUnion_le_tsum_nat' (s : â„• â†’ Set Î±) :
    extend m (â‹ƒ i, s i) â‰¤ âˆ‘' i, extend m (s i) := by
  by_cases h : âˆ€ i, P (s i)
  Â· rw [extend_eq _ (PU h), congr_arg tsum _]
    Â· apply msU h
    funext i
    apply extend_eq _ (h i)
  Â· cases' not_forall.1 h with i hi
    exact le_trans (le_iInf fun h => hi.elim h) (ENNReal.le_tsum i)
#align measure_theory.extend_Union_le_tsum_nat' MeasureTheory.extend_iUnion_le_tsum_nat'

end Subadditive

section Mono

theorem extend_mono' â¦ƒsâ‚ sâ‚‚ : Set Î±â¦„ (hâ‚ : P sâ‚) (hs : sâ‚ âŠ† sâ‚‚) : extend m sâ‚ â‰¤ extend m sâ‚‚ := by
  refine le_iInf ?_
  intro hâ‚‚
  rw [extend_eq m hâ‚]
  exact m_mono hâ‚ hâ‚‚ hs
#align measure_theory.extend_mono' MeasureTheory.extend_mono'

end Mono

section Unions

theorem extend_iUnion {Î²} [Countable Î²] {f : Î² â†’ Set Î±} (hd : Pairwise (Disjoint on f))
    (hm : âˆ€ i, P (f i)) : extend m (â‹ƒ i, f i) = âˆ‘' i, extend m (f i) := by
  cases nonempty_encodable Î²
  rw [â† Encodable.iUnion_decodeâ‚‚, â† tsum_iUnion_decodeâ‚‚]
  Â· exact
      extend_iUnion_nat PU (fun n => Encodable.iUnion_decodeâ‚‚_cases P0 hm)
        (mU _ (Encodable.iUnion_decodeâ‚‚_disjoint_on hd))
  Â· exact extend_empty P0 m0
#align measure_theory.extend_Union MeasureTheory.extend_iUnion

theorem extend_union {sâ‚ sâ‚‚ : Set Î±} (hd : Disjoint sâ‚ sâ‚‚) (hâ‚ : P sâ‚) (hâ‚‚ : P sâ‚‚) :
    extend m (sâ‚ âˆª sâ‚‚) = extend m sâ‚ + extend m sâ‚‚ := by
  rw [union_eq_iUnion,
    extend_iUnion P0 m0 PU mU (pairwise_disjoint_on_bool.2 hd) (Bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©),
    tsum_fintype]
  simp
#align measure_theory.extend_union MeasureTheory.extend_union

end Unions

variable (m)

/-- Given an arbitrary function on a subset of sets, we can define the outer measure corresponding
  to it (this is the unique maximal outer measure that is at most `m` on the domain of `m`). -/
def inducedOuterMeasure : OuterMeasure Î± :=
  OuterMeasure.ofFunction (extend m) (extend_empty P0 m0)
#align measure_theory.induced_outer_measure MeasureTheory.inducedOuterMeasure

variable {m P0 m0}

theorem le_inducedOuterMeasure {Î¼ : OuterMeasure Î±} :
    Î¼ â‰¤ inducedOuterMeasure m P0 m0 â†” âˆ€ (s) (hs : P s), Î¼ s â‰¤ m s hs :=
  le_ofFunction.trans <| forall_congr' fun _s => le_iInf_iff
#align measure_theory.le_induced_outer_measure MeasureTheory.le_inducedOuterMeasure

/-- If `P u` is `False` for any set `u` that has nonempty intersection both with `s` and `t`, then
`Î¼ (s âˆª t) = Î¼ s + Î¼ t`, where `Î¼ = inducedOuterMeasure m P0 m0`.

E.g., if `Î±` is an (e)metric space and `P u = diam u < r`, then this lemma implies that
`Î¼ (s âˆª t) = Î¼ s + Î¼ t` on any two sets such that `r â‰¤ edist x y` for all `x âˆˆ s` and `y âˆˆ t`. -/
theorem inducedOuterMeasure_union_of_false_of_nonempty_inter {s t : Set Î±}
    (h : âˆ€ u, (s âˆ© u).Nonempty â†’ (t âˆ© u).Nonempty â†’ Â¬P u) :
    inducedOuterMeasure m P0 m0 (s âˆª t) =
      inducedOuterMeasure m P0 m0 s + inducedOuterMeasure m P0 m0 t :=
  ofFunction_union_of_top_of_nonempty_inter fun u hsu htu => @iInf_of_empty _ _ _ âŸ¨h u hsu htuâŸ© _
#align measure_theory.induced_outer_measure_union_of_false_of_nonempty_inter MeasureTheory.inducedOuterMeasure_union_of_false_of_nonempty_inter

theorem inducedOuterMeasure_eq_extend' {s : Set Î±} (hs : P s) :
    inducedOuterMeasure m P0 m0 s = extend m s :=
  ofFunction_eq s (fun _t => extend_mono' m_mono hs) (extend_iUnion_le_tsum_nat' PU msU)
#align measure_theory.induced_outer_measure_eq_extend' MeasureTheory.inducedOuterMeasure_eq_extend'

theorem inducedOuterMeasure_eq' {s : Set Î±} (hs : P s) : inducedOuterMeasure m P0 m0 s = m s hs :=
  (inducedOuterMeasure_eq_extend' PU msU m_mono hs).trans <| extend_eq _ _
#align measure_theory.induced_outer_measure_eq' MeasureTheory.inducedOuterMeasure_eq'

theorem inducedOuterMeasure_eq_iInf (s : Set Î±) :
    inducedOuterMeasure m P0 m0 s = â¨… (t : Set Î±) (ht : P t) (_ : s âŠ† t), m t ht := by
  apply le_antisymm
  Â· simp only [le_iInf_iff]
    intro t ht hs
    refine le_trans (measure_mono hs) ?_
    exact le_of_eq (inducedOuterMeasure_eq' _ msU m_mono _)
  Â· refine le_iInf ?_
    intro f
    refine le_iInf ?_
    intro hf
    refine le_trans ?_ (extend_iUnion_le_tsum_nat' _ msU _)
    refine le_iInf ?_
    intro h2f
    exact iInf_le_of_le _ (iInf_le_of_le h2f <| iInf_le _ hf)
#align measure_theory.induced_outer_measure_eq_infi MeasureTheory.inducedOuterMeasure_eq_iInf

theorem inducedOuterMeasure_preimage (f : Î± â‰ƒ Î±) (Pm : âˆ€ s : Set Î±, P (f â»Â¹' s) â†” P s)
    (mm : âˆ€ (s : Set Î±) (hs : P s), m (f â»Â¹' s) ((Pm _).mpr hs) = m s hs) {A : Set Î±} :
    inducedOuterMeasure m P0 m0 (f â»Â¹' A) = inducedOuterMeasure m P0 m0 A := by
    rw [inducedOuterMeasure_eq_iInf _ msU m_mono, inducedOuterMeasure_eq_iInf _ msU m_mono]; symm
    refine f.injective.preimage_surjective.iInf_congr (preimage f) fun s => ?_
    refine iInf_congr_Prop (Pm s) ?_; intro hs
    refine iInf_congr_Prop f.surjective.preimage_subset_preimage_iff ?_
    intro _; exact mm s hs
#align measure_theory.induced_outer_measure_preimage MeasureTheory.inducedOuterMeasure_preimage

theorem inducedOuterMeasure_exists_set {s : Set Î±} (hs : inducedOuterMeasure m P0 m0 s â‰  âˆ)
    {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ t : Set Î±,
      P t âˆ§ s âŠ† t âˆ§ inducedOuterMeasure m P0 m0 t â‰¤ inducedOuterMeasure m P0 m0 s + Îµ := by
  have h := ENNReal.lt_add_right hs hÎµ
  conv at h =>
    lhs
    rw [inducedOuterMeasure_eq_iInf _ msU m_mono]
  simp only [iInf_lt_iff] at h
  rcases h with âŸ¨t, h1t, h2t, h3tâŸ©
  exact
    âŸ¨t, h1t, h2t, le_trans (le_of_eq <| inducedOuterMeasure_eq' _ msU m_mono h1t) (le_of_lt h3t)âŸ©
#align measure_theory.induced_outer_measure_exists_set MeasureTheory.inducedOuterMeasure_exists_set

/-- To test whether `s` is CarathÃ©odory-measurable we only need to check the sets `t` for which
  `P t` holds. See `ofFunction_caratheodory` for another way to show the CarathÃ©odory-measurability
  of `s`.
-/
theorem inducedOuterMeasure_caratheodory (s : Set Î±) :
    MeasurableSet[(inducedOuterMeasure m P0 m0).caratheodory] s â†”
      âˆ€ t : Set Î±,
        P t â†’
          inducedOuterMeasure m P0 m0 (t âˆ© s) + inducedOuterMeasure m P0 m0 (t \ s) â‰¤
            inducedOuterMeasure m P0 m0 t := by
  rw [isCaratheodory_iff_le]
  constructor
  Â· intro h t _ht
    exact h t
  Â· intro h u
    conv_rhs => rw [inducedOuterMeasure_eq_iInf _ msU m_mono]
    refine le_iInf ?_
    intro t
    refine le_iInf ?_
    intro ht
    refine le_iInf ?_
    intro h2t
    refine le_trans ?_ ((h t ht).trans_eq <| inducedOuterMeasure_eq' _ msU m_mono ht)
    gcongr
#align measure_theory.induced_outer_measure_caratheodory MeasureTheory.inducedOuterMeasure_caratheodory

end ExtendSet

/-! If `P` is `MeasurableSet` for some measurable space, then we can remove some hypotheses of the
  above lemmas. -/


section MeasurableSpace

variable {Î± : Type*} [MeasurableSpace Î±]
variable {m : âˆ€ s : Set Î±, MeasurableSet s â†’ â„â‰¥0âˆ}
variable (m0 : m âˆ… MeasurableSet.empty = 0)
variable
  (mU :
    âˆ€ â¦ƒf : â„• â†’ Set Î±â¦„ (hm : âˆ€ i, MeasurableSet (f i)),
      Pairwise (Disjoint on f) â†’ m (â‹ƒ i, f i) (MeasurableSet.iUnion hm) = âˆ‘' i, m (f i) (hm i))

theorem extend_mono {sâ‚ sâ‚‚ : Set Î±} (hâ‚ : MeasurableSet sâ‚) (hs : sâ‚ âŠ† sâ‚‚) :
    extend m sâ‚ â‰¤ extend m sâ‚‚ := by
  refine le_iInf ?_; intro hâ‚‚
  have :=
    extend_union MeasurableSet.empty m0 MeasurableSet.iUnion mU disjoint_sdiff_self_right hâ‚
      (hâ‚‚.diff hâ‚)
  rw [union_diff_cancel hs] at this
  rw [â† extend_eq m]
  exact le_iff_exists_add.2 âŸ¨_, thisâŸ©
#align measure_theory.extend_mono MeasureTheory.extend_mono

theorem extend_iUnion_le_tsum_nat : âˆ€ s : â„• â†’ Set Î±,
    extend m (â‹ƒ i, s i) â‰¤ âˆ‘' i, extend m (s i) := by
  refine extend_iUnion_le_tsum_nat' MeasurableSet.iUnion ?_; intro f h
  simp (config := { singlePass := true }) only [iUnion_disjointed.symm]
  rw [mU (MeasurableSet.disjointed h) (disjoint_disjointed _)]
  refine ENNReal.tsum_le_tsum fun i => ?_
  rw [â† extend_eq m, â† extend_eq m]
  exact extend_mono m0 mU (MeasurableSet.disjointed h _) (disjointed_le f _)
#align measure_theory.extend_Union_le_tsum_nat MeasureTheory.extend_iUnion_le_tsum_nat

theorem inducedOuterMeasure_eq_extend {s : Set Î±} (hs : MeasurableSet s) :
    inducedOuterMeasure m MeasurableSet.empty m0 s = extend m s :=
  ofFunction_eq s (fun _t => extend_mono m0 mU hs) (extend_iUnion_le_tsum_nat m0 mU)
#align measure_theory.induced_outer_measure_eq_extend MeasureTheory.inducedOuterMeasure_eq_extend

theorem inducedOuterMeasure_eq {s : Set Î±} (hs : MeasurableSet s) :
    inducedOuterMeasure m MeasurableSet.empty m0 s = m s hs :=
  (inducedOuterMeasure_eq_extend m0 mU hs).trans <| extend_eq _ _
#align measure_theory.induced_outer_measure_eq MeasureTheory.inducedOuterMeasure_eq

end MeasurableSpace

namespace OuterMeasure

variable {Î± : Type*} [MeasurableSpace Î±] (m : OuterMeasure Î±)

/-- Given an outer measure `m` we can forget its value on non-measurable sets, and then consider
  `m.trim`, the unique maximal outer measure less than that function. -/
def trim : OuterMeasure Î± :=
  inducedOuterMeasure (fun s _ => m s) MeasurableSet.empty m.empty
#align measure_theory.outer_measure.trim MeasureTheory.OuterMeasure.trim

theorem le_trim_iff {mâ‚ mâ‚‚ : OuterMeasure Î±} :
    mâ‚ â‰¤ mâ‚‚.trim â†” âˆ€ s, MeasurableSet s â†’ mâ‚ s â‰¤ mâ‚‚ s :=
  le_inducedOuterMeasure
#align measure_theory.outer_measure.le_trim_iff MeasureTheory.OuterMeasure.le_trim_iff

theorem le_trim : m â‰¤ m.trim := le_trim_iff.2 fun _ _ â†¦ le_rfl
#align measure_theory.outer_measure.le_trim MeasureTheory.OuterMeasure.le_trim

@[simp] -- Porting note: added `simp`
theorem trim_eq {s : Set Î±} (hs : MeasurableSet s) : m.trim s = m s :=
  inducedOuterMeasure_eq' MeasurableSet.iUnion (fun f _hf => measure_iUnion_le f)
    (fun _ _ _ _ h => measure_mono h) hs
#align measure_theory.outer_measure.trim_eq MeasureTheory.OuterMeasure.trim_eq

theorem trim_congr {mâ‚ mâ‚‚ : OuterMeasure Î±} (H : âˆ€ {s : Set Î±}, MeasurableSet s â†’ mâ‚ s = mâ‚‚ s) :
    mâ‚.trim = mâ‚‚.trim := by
  simp (config := { contextual := true }) only [trim, H]
#align measure_theory.outer_measure.trim_congr MeasureTheory.OuterMeasure.trim_congr

@[mono]
theorem trim_mono : Monotone (trim : OuterMeasure Î± â†’ OuterMeasure Î±) := fun _mâ‚ _mâ‚‚ H _s =>
  iInfâ‚‚_mono fun _f _hs => ENNReal.tsum_le_tsum fun _b => iInf_mono fun _hf => H _
#align measure_theory.outer_measure.trim_mono MeasureTheory.OuterMeasure.trim_mono

/-- `OuterMeasure.trim` is antitone in the Ïƒ-algebra. -/
theorem trim_anti_measurableSpace (m : OuterMeasure Î±) {m0 m1 : MeasurableSpace Î±}
    (h : m0 â‰¤ m1) : @trim _ m1 m â‰¤ @trim _ m0 m := by
  simp only [le_trim_iff]
  intro s hs
  rw [trim_eq _ (h s hs)]

theorem trim_le_trim_iff {mâ‚ mâ‚‚ : OuterMeasure Î±} :
    mâ‚.trim â‰¤ mâ‚‚.trim â†” âˆ€ s, MeasurableSet s â†’ mâ‚ s â‰¤ mâ‚‚ s :=
  le_trim_iff.trans <| forallâ‚‚_congr fun s hs => by rw [trim_eq _ hs]
#align measure_theory.outer_measure.trim_le_trim_iff MeasureTheory.OuterMeasure.trim_le_trim_iff

theorem trim_eq_trim_iff {mâ‚ mâ‚‚ : OuterMeasure Î±} :
    mâ‚.trim = mâ‚‚.trim â†” âˆ€ s, MeasurableSet s â†’ mâ‚ s = mâ‚‚ s := by
  simp only [le_antisymm_iff, trim_le_trim_iff, forall_and]
#align measure_theory.outer_measure.trim_eq_trim_iff MeasureTheory.OuterMeasure.trim_eq_trim_iff

theorem trim_eq_iInf (s : Set Î±) : m.trim s = â¨… (t) (_ : s âŠ† t) (_ : MeasurableSet t), m t := by
  simp (config := { singlePass := true }) only [iInf_comm]
  exact
    inducedOuterMeasure_eq_iInf MeasurableSet.iUnion (fun f _ => measure_iUnion_le f)
      (fun _ _ _ _ h => measure_mono h) s
#align measure_theory.outer_measure.trim_eq_infi MeasureTheory.OuterMeasure.trim_eq_iInf

theorem trim_eq_iInf' (s : Set Î±) : m.trim s = â¨… t : { t // s âŠ† t âˆ§ MeasurableSet t }, m t := by
  simp [iInf_subtype, iInf_and, trim_eq_iInf]
#align measure_theory.outer_measure.trim_eq_infi' MeasureTheory.OuterMeasure.trim_eq_iInf'

theorem trim_trim (m : OuterMeasure Î±) : m.trim.trim = m.trim :=
  trim_eq_trim_iff.2 fun _s => m.trim_eq
#align measure_theory.outer_measure.trim_trim MeasureTheory.OuterMeasure.trim_trim

@[simp]
theorem trim_top : (âŠ¤ : OuterMeasure Î±).trim = âŠ¤ :=
  top_unique <| le_trim _
#align measure_theory.outer_measure.trim_top MeasureTheory.OuterMeasure.trim_top

@[simp]
theorem trim_zero : (0 : OuterMeasure Î±).trim = 0 :=
  ext fun s =>
    le_antisymm
      ((measure_mono (subset_univ s)).trans_eq <| trim_eq _ MeasurableSet.univ)
      (zero_le _)
#align measure_theory.outer_measure.trim_zero MeasureTheory.OuterMeasure.trim_zero

theorem trim_sum_ge {Î¹} (m : Î¹ â†’ OuterMeasure Î±) : (sum fun i => (m i).trim) â‰¤ (sum m).trim :=
  fun s => by
  simp [trim_eq_iInf];
    exact fun t st ht =>
      ENNReal.tsum_le_tsum fun i => iInf_le_of_le t <| iInf_le_of_le st <| iInf_le _ ht
#align measure_theory.outer_measure.trim_sum_ge MeasureTheory.OuterMeasure.trim_sum_ge

theorem exists_measurable_superset_eq_trim (m : OuterMeasure Î±) (s : Set Î±) :
    âˆƒ t, s âŠ† t âˆ§ MeasurableSet t âˆ§ m t = m.trim s := by
  simp only [trim_eq_iInf]; set ms := â¨… (t : Set Î±) (_ : s âŠ† t) (_ : MeasurableSet t), m t
  by_cases hs : ms = âˆ
  Â· simp only [hs]
    simp only [iInf_eq_top, ms] at hs
    exact âŸ¨univ, subset_univ s, MeasurableSet.univ, hs _ (subset_univ s) MeasurableSet.univâŸ©
  Â· have : âˆ€ r > ms, âˆƒ t, s âŠ† t âˆ§ MeasurableSet t âˆ§ m t < r := by
      intro r hs
      have : âˆƒt, MeasurableSet t âˆ§ s âŠ† t âˆ§ m t < r := by simpa [ms, iInf_lt_iff] using hs
      rcases this with âŸ¨t, hmt, hin, hltâŸ©
      exists t
    have : âˆ€ n : â„•, âˆƒ t, s âŠ† t âˆ§ MeasurableSet t âˆ§ m t < ms + (n : â„â‰¥0âˆ)â»Â¹ := by
      intro n
      refine this _ (ENNReal.lt_add_right hs ?_)
      simp
    choose t hsub hm hm' using this
    refine âŸ¨â‹‚ n, t n, subset_iInter hsub, MeasurableSet.iInter hm, ?_âŸ©
    have : Tendsto (fun n : â„• => ms + (n : â„â‰¥0âˆ)â»Â¹) atTop (ğ“ (ms + 0)) :=
      tendsto_const_nhds.add ENNReal.tendsto_inv_nat_nhds_zero
    rw [add_zero] at this
    refine le_antisymm (ge_of_tendsto' this fun n => ?_) ?_
    Â· exact le_trans (measure_mono <| iInter_subset t n) (hm' n).le
    Â· refine iInf_le_of_le (â‹‚ n, t n) ?_
      refine iInf_le_of_le (subset_iInter hsub) ?_
      exact iInf_le _ (MeasurableSet.iInter hm)
#align measure_theory.outer_measure.exists_measurable_superset_eq_trim MeasureTheory.OuterMeasure.exists_measurable_superset_eq_trim

theorem exists_measurable_superset_of_trim_eq_zero {m : OuterMeasure Î±} {s : Set Î±}
    (h : m.trim s = 0) : âˆƒ t, s âŠ† t âˆ§ MeasurableSet t âˆ§ m t = 0 := by
  rcases exists_measurable_superset_eq_trim m s with âŸ¨t, hst, ht, hmâŸ©
  exact âŸ¨t, hst, ht, h â–¸ hmâŸ©
#align measure_theory.outer_measure.exists_measurable_superset_of_trim_eq_zero MeasureTheory.OuterMeasure.exists_measurable_superset_of_trim_eq_zero

/-- If `Î¼ i` is a countable family of outer measures, then for every set `s` there exists
a measurable set `t âŠ‡ s` such that `Î¼ i t = (Î¼ i).trim s` for all `i`. -/
theorem exists_measurable_superset_forall_eq_trim {Î¹} [Countable Î¹] (Î¼ : Î¹ â†’ OuterMeasure Î±)
    (s : Set Î±) : âˆƒ t, s âŠ† t âˆ§ MeasurableSet t âˆ§ âˆ€ i, Î¼ i t = (Î¼ i).trim s := by
  choose t hst ht hÎ¼t using fun i => (Î¼ i).exists_measurable_superset_eq_trim s
  replace hst := subset_iInter hst
  replace ht := MeasurableSet.iInter ht
  refine âŸ¨â‹‚ i, t i, hst, ht, fun i => le_antisymm ?_ ?_âŸ©
  exacts [hÎ¼t i â–¸ (Î¼ i).mono (iInter_subset _ _), (measure_mono hst).trans_eq ((Î¼ i).trim_eq ht)]
#align measure_theory.outer_measure.exists_measurable_superset_forall_eq_trim MeasureTheory.OuterMeasure.exists_measurable_superset_forall_eq_trim

/-- If `mâ‚ s = op (mâ‚‚ s) (mâ‚ƒ s)` for all `s`, then the same is true for `mâ‚.trim`, `mâ‚‚.trim`,
and `mâ‚ƒ s`. -/
theorem trim_binop {mâ‚ mâ‚‚ mâ‚ƒ : OuterMeasure Î±} {op : â„â‰¥0âˆ â†’ â„â‰¥0âˆ â†’ â„â‰¥0âˆ}
    (h : âˆ€ s, mâ‚ s = op (mâ‚‚ s) (mâ‚ƒ s)) (s : Set Î±) : mâ‚.trim s = op (mâ‚‚.trim s) (mâ‚ƒ.trim s) := by
  rcases exists_measurable_superset_forall_eq_trim ![mâ‚, mâ‚‚, mâ‚ƒ] s with âŸ¨t, _hst, _ht, htmâŸ©
  simp only [Fin.forall_fin_succ, Matrix.cons_val_zero, Matrix.cons_val_succ] at htm
  rw [â† htm.1, â† htm.2.1, â† htm.2.2.1, h]
#align measure_theory.outer_measure.trim_binop MeasureTheory.OuterMeasure.trim_binop

/-- If `mâ‚ s = op (mâ‚‚ s)` for all `s`, then the same is true for `mâ‚.trim` and `mâ‚‚.trim`. -/
theorem trim_op {mâ‚ mâ‚‚ : OuterMeasure Î±} {op : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} (h : âˆ€ s, mâ‚ s = op (mâ‚‚ s))
    (s : Set Î±) : mâ‚.trim s = op (mâ‚‚.trim s) :=
  @trim_binop Î± _ mâ‚ mâ‚‚ 0 (fun a _b => op a) h s
#align measure_theory.outer_measure.trim_op MeasureTheory.OuterMeasure.trim_op

/-- `trim` is additive. -/
theorem trim_add (mâ‚ mâ‚‚ : OuterMeasure Î±) : (mâ‚ + mâ‚‚).trim = mâ‚.trim + mâ‚‚.trim :=
  ext <| trim_binop (add_apply mâ‚ mâ‚‚)
#align measure_theory.outer_measure.trim_add MeasureTheory.OuterMeasure.trim_add

/-- `trim` respects scalar multiplication. -/
theorem trim_smul {R : Type*} [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ] (c : R)
    (m : OuterMeasure Î±) : (c â€¢ m).trim = c â€¢ m.trim :=
  ext <| trim_op (smul_apply c m)
#align measure_theory.outer_measure.trim_smul MeasureTheory.OuterMeasure.trim_smul

/-- `trim` sends the supremum of two outer measures to the supremum of the trimmed measures. -/
theorem trim_sup (mâ‚ mâ‚‚ : OuterMeasure Î±) : (mâ‚ âŠ” mâ‚‚).trim = mâ‚.trim âŠ” mâ‚‚.trim :=
  ext fun s => (trim_binop (sup_apply mâ‚ mâ‚‚) s).trans (sup_apply _ _ _).symm
#align measure_theory.outer_measure.trim_sup MeasureTheory.OuterMeasure.trim_sup

/-- `trim` sends the supremum of a countable family of outer measures to the supremum
of the trimmed measures. -/
theorem trim_iSup {Î¹} [Countable Î¹] (Î¼ : Î¹ â†’ OuterMeasure Î±) :
    trim (â¨† i, Î¼ i) = â¨† i, trim (Î¼ i) := by
  simp_rw [â† @iSup_plift_down _ Î¹]
  ext1 s
  obtain âŸ¨t, _, _, hÎ¼tâŸ© :=
    exists_measurable_superset_forall_eq_trim
      (Option.elim' (â¨† i, Î¼ (PLift.down i)) (Î¼ âˆ˜ PLift.down)) s
  simp only [Option.forall, Option.elim'] at hÎ¼t
  simp only [iSup_apply, â† hÎ¼t.1]
  exact iSup_congr hÎ¼t.2
#align measure_theory.outer_measure.trim_supr MeasureTheory.OuterMeasure.trim_iSup

/-- The trimmed property of a measure Î¼ states that `Î¼.toOuterMeasure.trim = Î¼.toOuterMeasure`.
This theorem shows that a restricted trimmed outer measure is a trimmed outer measure. -/
theorem restrict_trim {Î¼ : OuterMeasure Î±} {s : Set Î±} (hs : MeasurableSet s) :
    (restrict s Î¼).trim = restrict s Î¼.trim := by
  refine le_antisymm (fun t => ?_) (le_trim_iff.2 fun t ht => ?_)
  Â· rw [restrict_apply]
    rcases Î¼.exists_measurable_superset_eq_trim (t âˆ© s) with âŸ¨t', htt', ht', hÎ¼t'âŸ©
    rw [â† hÎ¼t']
    rw [inter_subset] at htt'
    refine (measure_mono htt').trans ?_
    rw [trim_eq _ (hs.compl.union ht'), restrict_apply, union_inter_distrib_right, compl_inter_self,
      Set.empty_union]
    exact measure_mono inter_subset_left
  Â· rw [restrict_apply, trim_eq _ (ht.inter hs), restrict_apply]
#align measure_theory.outer_measure.restrict_trim MeasureTheory.OuterMeasure.restrict_trim

end OuterMeasure
