/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.MeasureTheory.Measure.Lebesgue.EqHaar
import Mathlib.MeasureTheory.Covering.Besicovitch
import Mathlib.Tactic.AdaptationNote

#align_import measure_theory.covering.besicovitch_vector_space from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# Satellite configurations for Besicovitch covering lemma in vector spaces

The Besicovitch covering theorem ensures that, in a nice metric space, there exists a number `N`
such that, from any family of balls with bounded radii, one can extract `N` families, each made of
disjoint balls, covering together all the centers of the initial family.

A key tool in the proof of this theorem is the notion of a satellite configuration, i.e., a family
of `N + 1` balls, where the first `N` balls all intersect the last one, but none of them contains
the center of another one and their radii are controlled. This is a technical notion, but it shows
up naturally in the proof of the Besicovitch theorem (which goes through a greedy algorithm): to
ensure that in the end one needs at most `N` families of balls, the crucial property of the
underlying metric space is that there should be no satellite configuration of `N + 1` points.

This file is devoted to the study of this property in vector spaces: we prove the main result
of [F√ºredi and Loeb, On the best constant for the Besicovitch covering theorem][furedi-loeb1994],
which shows that the optimal such `N` in a vector space coincides with the maximal number
of points one can put inside the unit ball of radius `2` under the condition that their distances
are bounded below by `1`.
In particular, this number is bounded by `5 ^ dim` by a straightforward measure argument.

## Main definitions and results

* `multiplicity E` is the maximal number of points one can put inside the unit ball
  of radius `2` in the vector space `E`, under the condition that their distances
  are bounded below by `1`.
* `multiplicity_le E` shows that `multiplicity E ‚â§ 5 ^ (dim E)`.
* `good_œÑ E` is a constant `> 1`, but close enough to `1` that satellite configurations
  with this parameter `œÑ` are not worst than for `œÑ = 1`.
* `isEmpty_satelliteConfig_multiplicity` is the main theorem, saying that there are
  no satellite configurations of `(multiplicity E) + 1` points, for the parameter `goodœÑ E`.
-/


universe u

open Metric Set FiniteDimensional MeasureTheory Filter Fin

open scoped ENNReal Topology

noncomputable section

namespace Besicovitch

variable {E : Type*} [NormedAddCommGroup E]

namespace SatelliteConfig

variable [NormedSpace ‚Ñù E] {N : ‚Ñï} {œÑ : ‚Ñù} (a : SatelliteConfig E N œÑ)

/-- Rescaling a satellite configuration in a vector space, to put the basepoint at `0` and the base
radius at `1`. -/
def centerAndRescale : SatelliteConfig E N œÑ where
  c i := (a.r (last N))‚Åª¬π ‚Ä¢ (a.c i - a.c (last N))
  r i := (a.r (last N))‚Åª¬π * a.r i
  rpos i := by positivity
  h i j hij := by
    simp (disch := positivity) only [dist_smul‚ÇÄ, dist_sub_right, mul_left_comm œÑ,
      Real.norm_of_nonneg]
    rcases a.h hij with (‚ü®H‚ÇÅ, H‚ÇÇ‚ü© | ‚ü®H‚ÇÅ, H‚ÇÇ‚ü©) <;> [left; right] <;> constructor <;> gcongr
  hlast i hi := by
    simp (disch := positivity) only [dist_smul‚ÇÄ, dist_sub_right, mul_left_comm œÑ,
      Real.norm_of_nonneg]
    have ‚ü®H‚ÇÅ, H‚ÇÇ‚ü© := a.hlast i hi
    constructor <;> gcongr
  inter i hi := by
    simp (disch := positivity) only [dist_smul‚ÇÄ, ‚Üê mul_add, dist_sub_right, Real.norm_of_nonneg]
    gcongr
    exact a.inter i hi
#align besicovitch.satellite_config.center_and_rescale Besicovitch.SatelliteConfig.centerAndRescale

theorem centerAndRescale_center : a.centerAndRescale.c (last N) = 0 := by
  simp [SatelliteConfig.centerAndRescale]
#align besicovitch.satellite_config.center_and_rescale_center Besicovitch.SatelliteConfig.centerAndRescale_center

theorem centerAndRescale_radius {N : ‚Ñï} {œÑ : ‚Ñù} (a : SatelliteConfig E N œÑ) :
    a.centerAndRescale.r (last N) = 1 := by
  simp [SatelliteConfig.centerAndRescale, inv_mul_cancel (a.rpos _).ne']
#align besicovitch.satellite_config.center_and_rescale_radius Besicovitch.SatelliteConfig.centerAndRescale_radius

end SatelliteConfig

/-! ### Disjoint balls of radius close to `1` in the radius `2` ball. -/


/-- The maximum cardinality of a `1`-separated set in the ball of radius `2`. This is also the
optimal number of families in the Besicovitch covering theorem. -/
def multiplicity (E : Type*) [NormedAddCommGroup E] :=
  sSup {N | ‚àÉ s : Finset E, s.card = N ‚àß (‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2) ‚àß ‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 ‚â§ ‚Äñc - d‚Äñ}
#align besicovitch.multiplicity Besicovitch.multiplicity

section

variable [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]

/-- Any `1`-separated set in the ball of radius `2` has cardinality at most `5 ^ dim`. This is
useful to show that the supremum in the definition of `Besicovitch.multiplicity E` is
well behaved. -/
theorem card_le_of_separated (s : Finset E) (hs : ‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2)
    (h : ‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 ‚â§ ‚Äñc - d‚Äñ) : s.card ‚â§ 5 ^ finrank ‚Ñù E := by
  /- We consider balls of radius `1/2` around the points in `s`. They are disjoint, and all
    contained in the ball of radius `5/2`. A volume argument gives `s.card * (1/2)^dim ‚â§ (5/2)^dim`,
    i.e., `s.card ‚â§ 5^dim`. -/
  borelize E
  let Œº : Measure E := Measure.addHaar
  let Œ¥ : ‚Ñù := (1 : ‚Ñù) / 2
  let œÅ : ‚Ñù := (5 : ‚Ñù) / 2
  have œÅpos : 0 < œÅ := by norm_num
  set A := ‚ãÉ c ‚àà s, ball (c : E) Œ¥ with hA
  have D : Set.Pairwise (s : Set E) (Disjoint on fun c => ball (c : E) Œ¥) := by
    rintro c hc d hd hcd
    apply ball_disjoint_ball
    rw [dist_eq_norm]
    convert h c hc d hd hcd
    norm_num
  have A_subset : A ‚äÜ ball (0 : E) œÅ := by
    refine iUnion‚ÇÇ_subset fun x hx => ?_
    apply ball_subset_ball'
    calc
      Œ¥ + dist x 0 ‚â§ Œ¥ + 2 := by rw [dist_zero_right]; exact add_le_add le_rfl (hs x hx)
      _ = 5 / 2 := by norm_num
  have I :
    (s.card : ‚Ñù‚â•0‚àû) * ENNReal.ofReal (Œ¥ ^ finrank ‚Ñù E) * Œº (ball 0 1) ‚â§
      ENNReal.ofReal (œÅ ^ finrank ‚Ñù E) * Œº (ball 0 1) :=
    calc
      (s.card : ‚Ñù‚â•0‚àû) * ENNReal.ofReal (Œ¥ ^ finrank ‚Ñù E) * Œº (ball 0 1) = Œº A := by
        rw [hA, measure_biUnion_finset D fun c _ => measurableSet_ball]
        have I : 0 < Œ¥ := by norm_num
        simp only [div_pow, Œº.addHaar_ball_of_pos _ I]
        simp only [one_div, one_pow, Finset.sum_const, nsmul_eq_mul, mul_assoc]
      _ ‚â§ Œº (ball (0 : E) œÅ) := measure_mono A_subset
      _ = ENNReal.ofReal (œÅ ^ finrank ‚Ñù E) * Œº (ball 0 1) := by
        simp only [Œº.addHaar_ball_of_pos _ œÅpos]
  have J : (s.card : ‚Ñù‚â•0‚àû) * ENNReal.ofReal (Œ¥ ^ finrank ‚Ñù E) ‚â§ ENNReal.ofReal (œÅ ^ finrank ‚Ñù E) :=
    (ENNReal.mul_le_mul_right (measure_ball_pos _ _ zero_lt_one).ne' measure_ball_lt_top.ne).1 I
  have K : (s.card : ‚Ñù) ‚â§ (5 : ‚Ñù) ^ finrank ‚Ñù E := by
    have := ENNReal.toReal_le_of_le_ofReal (pow_nonneg œÅpos.le _) J
    simpa [œÅ, Œ¥, div_eq_mul_inv, mul_pow] using this
  exact mod_cast K
#align besicovitch.card_le_of_separated Besicovitch.card_le_of_separated

theorem multiplicity_le : multiplicity E ‚â§ 5 ^ finrank ‚Ñù E := by
  apply csSup_le
  ¬∑ refine ‚ü®0, ‚ü®‚àÖ, by simp‚ü©‚ü©
  ¬∑ rintro _ ‚ü®s, ‚ü®rfl, h‚ü©‚ü©
    exact Besicovitch.card_le_of_separated s h.1 h.2
#align besicovitch.multiplicity_le Besicovitch.multiplicity_le

theorem card_le_multiplicity {s : Finset E} (hs : ‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2)
    (h's : ‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 ‚â§ ‚Äñc - d‚Äñ) : s.card ‚â§ multiplicity E := by
  apply le_csSup
  ¬∑ refine ‚ü®5 ^ finrank ‚Ñù E, ?_‚ü©
    rintro _ ‚ü®s, ‚ü®rfl, h‚ü©‚ü©
    exact Besicovitch.card_le_of_separated s h.1 h.2
  ¬∑ simp only [mem_setOf_eq, Ne]
    exact ‚ü®s, rfl, hs, h's‚ü©
#align besicovitch.card_le_multiplicity Besicovitch.card_le_multiplicity

variable (E)

/-- If `Œ¥` is small enough, a `(1-Œ¥)`-separated set in the ball of radius `2` also has cardinality
at most `multiplicity E`. -/
theorem exists_goodŒ¥ :
    ‚àÉ Œ¥ : ‚Ñù, 0 < Œ¥ ‚àß Œ¥ < 1 ‚àß ‚àÄ s : Finset E, (‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2) ‚Üí
      (‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 - Œ¥ ‚â§ ‚Äñc - d‚Äñ) ‚Üí s.card ‚â§ multiplicity E := by
  classical
  /- This follows from a compactness argument: otherwise, one could extract a converging
    subsequence, to obtain a `1`-separated set in the ball of radius `2` with cardinality
    `N = multiplicity E + 1`. To formalize this, we work with functions `Fin N ‚Üí E`.
     -/
  by_contra! h
  set N := multiplicity E + 1 with hN
  have :
    ‚àÄ Œ¥ : ‚Ñù, 0 < Œ¥ ‚Üí ‚àÉ f : Fin N ‚Üí E, (‚àÄ i : Fin N, ‚Äñf i‚Äñ ‚â§ 2) ‚àß
      Pairwise fun i j => 1 - Œ¥ ‚â§ ‚Äñf i - f j‚Äñ := by
    intro Œ¥ hŒ¥
    rcases lt_or_le Œ¥ 1 with (hŒ¥' | hŒ¥')
    ¬∑ rcases h Œ¥ hŒ¥ hŒ¥' with ‚ü®s, hs, h's, s_card‚ü©
      obtain ‚ü®f, f_inj, hfs‚ü© : ‚àÉ f : Fin N ‚Üí E, Function.Injective f ‚àß range f ‚äÜ ‚Üës := by
        have : Fintype.card (Fin N) ‚â§ s.card := by simp only [Fintype.card_fin]; exact s_card
        rcases Function.Embedding.exists_of_card_le_finset this with ‚ü®f, hf‚ü©
        exact ‚ü®f, f.injective, hf‚ü©
      simp only [range_subset_iff, Finset.mem_coe] at hfs
      exact ‚ü®f, fun i => hs _ (hfs i), fun i j hij => h's _ (hfs i) _ (hfs j) (f_inj.ne hij)‚ü©
    ¬∑ exact
        ‚ü®fun _ => 0, by simp, fun i j _ => by
          simpa only [norm_zero, sub_nonpos, sub_self]‚ü©
  -- For `Œ¥ > 0`, `F Œ¥` is a function from `fin N` to the ball of radius `2` for which two points
  -- in the image are separated by `1 - Œ¥`.
  choose! F hF using this
  -- Choose a converging subsequence when `Œ¥ ‚Üí 0`.
  have : ‚àÉ f : Fin N ‚Üí E, (‚àÄ i : Fin N, ‚Äñf i‚Äñ ‚â§ 2) ‚àß Pairwise fun i j => 1 ‚â§ ‚Äñf i - f j‚Äñ := by
    obtain ‚ü®u, _, zero_lt_u, hu‚ü© :
      ‚àÉ u : ‚Ñï ‚Üí ‚Ñù,
        (‚àÄ m n : ‚Ñï, m < n ‚Üí u n < u m) ‚àß (‚àÄ n : ‚Ñï, 0 < u n) ‚àß Filter.Tendsto u Filter.atTop (ùìù 0) :=
      exists_seq_strictAnti_tendsto (0 : ‚Ñù)
    have A : ‚àÄ n, F (u n) ‚àà closedBall (0 : Fin N ‚Üí E) 2 := by
      intro n
      simp only [pi_norm_le_iff_of_nonneg zero_le_two, mem_closedBall, dist_zero_right,
        (hF (u n) (zero_lt_u n)).left, forall_const]
    obtain ‚ü®f, fmem, œÜ, œÜ_mono, hf‚ü© :
      ‚àÉ f ‚àà closedBall (0 : Fin N ‚Üí E) 2,
        ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß Tendsto ((F ‚àò u) ‚àò œÜ) atTop (ùìù f) :=
      IsCompact.tendsto_subseq (isCompact_closedBall _ _) A
    refine ‚ü®f, fun i => ?_, fun i j hij => ?_‚ü©
    ¬∑ simp only [pi_norm_le_iff_of_nonneg zero_le_two, mem_closedBall, dist_zero_right] at fmem
      exact fmem i
    ¬∑ have A : Tendsto (fun n => ‚ÄñF (u (œÜ n)) i - F (u (œÜ n)) j‚Äñ) atTop (ùìù ‚Äñf i - f j‚Äñ) :=
        ((hf.apply_nhds i).sub (hf.apply_nhds j)).norm
      have B : Tendsto (fun n => 1 - u (œÜ n)) atTop (ùìù (1 - 0)) :=
        tendsto_const_nhds.sub (hu.comp œÜ_mono.tendsto_atTop)
      rw [sub_zero] at B
      exact le_of_tendsto_of_tendsto' B A fun n => (hF (u (œÜ n)) (zero_lt_u _)).2 hij
  rcases this with ‚ü®f, hf, h'f‚ü©
  -- the range of `f` contradicts the definition of `multiplicity E`.
  have finj : Function.Injective f := by
    intro i j hij
    by_contra h
    have : 1 ‚â§ ‚Äñf i - f j‚Äñ := h'f h
    simp only [hij, norm_zero, sub_self] at this
    exact lt_irrefl _ (this.trans_lt zero_lt_one)
  let s := Finset.image f Finset.univ
  have s_card : s.card = N := by rw [Finset.card_image_of_injective _ finj]; exact Finset.card_fin N
  have hs : ‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2 := by
    simp only [s, hf, forall_apply_eq_imp_iff, forall_const, forall_exists_index, Finset.mem_univ,
      Finset.mem_image, true_and]
  have h's : ‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 ‚â§ ‚Äñc - d‚Äñ := by
    simp only [s, forall_apply_eq_imp_iff, forall_exists_index, Finset.mem_univ, Finset.mem_image,
      Ne, exists_true_left, forall_apply_eq_imp_iff, forall_true_left, true_and]
    intro i j hij
    have : i ‚â† j := fun h => by rw [h] at hij; exact hij rfl
    exact h'f this
  have : s.card ‚â§ multiplicity E := card_le_multiplicity hs h's
  rw [s_card, hN] at this
  exact lt_irrefl _ ((Nat.lt_succ_self (multiplicity E)).trans_le this)
#align besicovitch.exists_good_Œ¥ Besicovitch.exists_goodŒ¥

/-- A small positive number such that any `1 - Œ¥`-separated set in the ball of radius `2` has
cardinality at most `Besicovitch.multiplicity E`. -/
def goodŒ¥ : ‚Ñù :=
  (exists_goodŒ¥ E).choose
#align besicovitch.good_Œ¥ Besicovitch.goodŒ¥

theorem goodŒ¥_lt_one : goodŒ¥ E < 1 :=
  (exists_goodŒ¥ E).choose_spec.2.1
#align besicovitch.good_Œ¥_lt_one Besicovitch.goodŒ¥_lt_one

/-- A number `œÑ > 1`, but chosen close enough to `1` so that the construction in the Besicovitch
covering theorem using this parameter `œÑ` will give the smallest possible number of covering
families. -/
def goodœÑ : ‚Ñù :=
  1 + goodŒ¥ E / 4
#align besicovitch.good_œÑ Besicovitch.goodœÑ

theorem one_lt_goodœÑ : 1 < goodœÑ E := by
  dsimp [goodœÑ, goodŒ¥]; linarith [(exists_goodŒ¥ E).choose_spec.1]
#align besicovitch.one_lt_good_œÑ Besicovitch.one_lt_goodœÑ

variable {E}

theorem card_le_multiplicity_of_Œ¥ {s : Finset E} (hs : ‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2)
    (h's : ‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 - goodŒ¥ E ‚â§ ‚Äñc - d‚Äñ) : s.card ‚â§ multiplicity E :=
  (Classical.choose_spec (exists_goodŒ¥ E)).2.2 s hs h's
#align besicovitch.card_le_multiplicity_of_Œ¥ Besicovitch.card_le_multiplicity_of_Œ¥

theorem le_multiplicity_of_Œ¥_of_fin {n : ‚Ñï} (f : Fin n ‚Üí E) (h : ‚àÄ i, ‚Äñf i‚Äñ ‚â§ 2)
    (h' : Pairwise fun i j => 1 - goodŒ¥ E ‚â§ ‚Äñf i - f j‚Äñ) : n ‚â§ multiplicity E := by
  classical
  have finj : Function.Injective f := by
    intro i j hij
    by_contra h
    have : 1 - goodŒ¥ E ‚â§ ‚Äñf i - f j‚Äñ := h' h
    simp only [hij, norm_zero, sub_self] at this
    linarith [goodŒ¥_lt_one E]
  let s := Finset.image f Finset.univ
  have s_card : s.card = n := by rw [Finset.card_image_of_injective _ finj]; exact Finset.card_fin n
  have hs : ‚àÄ c ‚àà s, ‚Äñc‚Äñ ‚â§ 2 := by
    simp only [s, h, forall_apply_eq_imp_iff, forall_const, forall_exists_index, Finset.mem_univ,
      Finset.mem_image, imp_true_iff, true_and]
  have h's : ‚àÄ c ‚àà s, ‚àÄ d ‚àà s, c ‚â† d ‚Üí 1 - goodŒ¥ E ‚â§ ‚Äñc - d‚Äñ := by
    simp only [s, forall_apply_eq_imp_iff, forall_exists_index, Finset.mem_univ, Finset.mem_image,
      Ne, exists_true_left, forall_apply_eq_imp_iff, forall_true_left, true_and]
    intro i j hij
    have : i ‚â† j := fun h => by rw [h] at hij; exact hij rfl
    exact h' this
  have : s.card ‚â§ multiplicity E := card_le_multiplicity_of_Œ¥ hs h's
  rwa [s_card] at this
#align besicovitch.le_multiplicity_of_Œ¥_of_fin Besicovitch.le_multiplicity_of_Œ¥_of_fin

end

namespace SatelliteConfig

/-!
### Relating satellite configurations to separated points in the ball of radius `2`.

We prove that the number of points in a satellite configuration is bounded by the maximal number
of `1`-separated points in the ball of radius `2`. For this, start from a satellite configuration
`c`. Without loss of generality, one can assume that the last ball is centered at `0` and of
radius `1`. Define `c' i = c i` if `‚Äñc i‚Äñ ‚â§ 2`, and `c' i = (2/‚Äñc i‚Äñ) ‚Ä¢ c i` if `‚Äñc i‚Äñ > 2`.
It turns out that these points are `1 - Œ¥`-separated, where `Œ¥` is arbitrarily small if `œÑ` is
close enough to `1`. The number of such configurations is bounded by `multiplicity E` if `Œ¥` is
suitably small.

To check that the points `c' i` are `1 - Œ¥`-separated, one treats separately the cases where
both `‚Äñc i‚Äñ` and `‚Äñc j‚Äñ` are `‚â§ 2`, where one of them is `‚â§ 2` and the other one is `> 2`, and
where both of them are `> 2`.
-/


theorem exists_normalized_aux1 {N : ‚Ñï} {œÑ : ‚Ñù} (a : SatelliteConfig E N œÑ)
    (lastr : a.r (last N) = 1) (hœÑ : 1 ‚â§ œÑ) (Œ¥ : ‚Ñù) (hŒ¥1 : œÑ ‚â§ 1 + Œ¥ / 4) (hŒ¥2 : Œ¥ ‚â§ 1)
    (i j : Fin N.succ) (inej : i ‚â† j) : 1 - Œ¥ ‚â§ ‚Äña.c i - a.c j‚Äñ := by
  have ah :
      Pairwise fun i j => a.r i ‚â§ ‚Äña.c i - a.c j‚Äñ ‚àß a.r j ‚â§ œÑ * a.r i ‚à®
        a.r j ‚â§ ‚Äña.c j - a.c i‚Äñ ‚àß a.r i ‚â§ œÑ * a.r j := by
    simpa only [dist_eq_norm] using a.h
  have Œ¥nonneg : 0 ‚â§ Œ¥ := by linarith only [hœÑ, hŒ¥1]
  have D : 0 ‚â§ 1 - Œ¥ / 4 := by linarith only [hŒ¥2]
  have œÑpos : 0 < œÑ := _root_.zero_lt_one.trans_le hœÑ
  have I : (1 - Œ¥ / 4) * œÑ ‚â§ 1 :=
    calc
      (1 - Œ¥ / 4) * œÑ ‚â§ (1 - Œ¥ / 4) * (1 + Œ¥ / 4) := by gcongr
      _ = (1 : ‚Ñù) - Œ¥ ^ 2 / 16 := by ring
      _ ‚â§ 1 := by linarith only [sq_nonneg Œ¥]
  have J : 1 - Œ¥ ‚â§ 1 - Œ¥ / 4 := by linarith only [Œ¥nonneg]
  have K : 1 - Œ¥ / 4 ‚â§ œÑ‚Åª¬π := by rw [inv_eq_one_div, le_div_iff œÑpos]; exact I
  suffices L : œÑ‚Åª¬π ‚â§ ‚Äña.c i - a.c j‚Äñ by linarith only [J, K, L]
  have hœÑ' : ‚àÄ k, œÑ‚Åª¬π ‚â§ a.r k := by
    intro k
    rw [inv_eq_one_div, div_le_iff œÑpos, ‚Üê lastr, mul_comm]
    exact a.hlast' k hœÑ
  rcases ah inej with (H | H)
  ¬∑ apply le_trans _ H.1
    exact hœÑ' i
  ¬∑ rw [norm_sub_rev]
    apply le_trans _ H.1
    exact hœÑ' j
#align besicovitch.satellite_config.exists_normalized_aux1 Besicovitch.SatelliteConfig.exists_normalized_aux1

variable [NormedSpace ‚Ñù E]

#adaptation_note /-- after v4.7.0-rc1, there is a performance problem in `field_simp`.
(Part of the code was ignoring the `maxDischargeDepth` setting:
 now that we have to increase it, other paths becomes slow.) -/
theorem exists_normalized_aux2 {N : ‚Ñï} {œÑ : ‚Ñù} (a : SatelliteConfig E N œÑ)
    (lastc : a.c (last N) = 0) (lastr : a.r (last N) = 1) (hœÑ : 1 ‚â§ œÑ) (Œ¥ : ‚Ñù) (hŒ¥1 : œÑ ‚â§ 1 + Œ¥ / 4)
    (hŒ¥2 : Œ¥ ‚â§ 1) (i j : Fin N.succ) (inej : i ‚â† j) (hi : ‚Äña.c i‚Äñ ‚â§ 2) (hj : 2 < ‚Äña.c j‚Äñ) :
    1 - Œ¥ ‚â§ ‚Äña.c i - (2 / ‚Äña.c j‚Äñ) ‚Ä¢ a.c j‚Äñ := by
  have ah :
      Pairwise fun i j => a.r i ‚â§ ‚Äña.c i - a.c j‚Äñ ‚àß a.r j ‚â§ œÑ * a.r i ‚à®
        a.r j ‚â§ ‚Äña.c j - a.c i‚Äñ ‚àß a.r i ‚â§ œÑ * a.r j := by
    simpa only [dist_eq_norm] using a.h
  have Œ¥nonneg : 0 ‚â§ Œ¥ := by linarith only [hœÑ, hŒ¥1]
  have D : 0 ‚â§ 1 - Œ¥ / 4 := by linarith only [hŒ¥2]
  have hcrj : ‚Äña.c j‚Äñ ‚â§ a.r j + 1 := by simpa only [lastc, lastr, dist_zero_right] using a.inter' j
  have I : a.r i ‚â§ 2 := by
    rcases lt_or_le i (last N) with (H | H)
    ¬∑ apply (a.hlast i H).1.trans
      simpa only [dist_eq_norm, lastc, sub_zero] using hi
    ¬∑ have : i = last N := top_le_iff.1 H
      rw [this, lastr]
      exact one_le_two
  have J : (1 - Œ¥ / 4) * œÑ ‚â§ 1 :=
    calc
      (1 - Œ¥ / 4) * œÑ ‚â§ (1 - Œ¥ / 4) * (1 + Œ¥ / 4) := by gcongr
      _ = (1 : ‚Ñù) - Œ¥ ^ 2 / 16 := by ring
      _ ‚â§ 1 := by linarith only [sq_nonneg Œ¥]
  have A : a.r j - Œ¥ ‚â§ ‚Äña.c i - a.c j‚Äñ := by
    rcases ah inej.symm with (H | H); ¬∑ rw [norm_sub_rev]; linarith [H.1]
    have C : a.r j ‚â§ 4 :=
      calc
        a.r j ‚â§ œÑ * a.r i := H.2
        _ ‚â§ œÑ * 2 := by gcongr
        _ ‚â§ 5 / 4 * 2 := by gcongr; linarith only [hŒ¥1, hŒ¥2]
        _ ‚â§ 4 := by norm_num
    calc
      a.r j - Œ¥ ‚â§ a.r j - a.r j / 4 * Œ¥ := by
        gcongr _ - ?_
        exact mul_le_of_le_one_left Œ¥nonneg (by linarith only [C])
      _ = (1 - Œ¥ / 4) * a.r j := by ring
      _ ‚â§ (1 - Œ¥ / 4) * (œÑ * a.r i) := mul_le_mul_of_nonneg_left H.2 D
      _ ‚â§ 1 * a.r i := by rw [‚Üê mul_assoc]; gcongr
      _ ‚â§ ‚Äña.c i - a.c j‚Äñ := by rw [one_mul]; exact H.1
  set d := (2 / ‚Äña.c j‚Äñ) ‚Ä¢ a.c j with hd
  have : a.r j - Œ¥ ‚â§ ‚Äña.c i - d‚Äñ + (a.r j - 1) :=
    calc
      a.r j - Œ¥ ‚â§ ‚Äña.c i - a.c j‚Äñ := A
      _ ‚â§ ‚Äña.c i - d‚Äñ + ‚Äñd - a.c j‚Äñ := by simp only [‚Üê dist_eq_norm, dist_triangle]
      _ ‚â§ ‚Äña.c i - d‚Äñ + (a.r j - 1) := by
        apply add_le_add_left
        have A : 0 ‚â§ 1 - 2 / ‚Äña.c j‚Äñ := by simpa [div_le_iff (zero_le_two.trans_lt hj)] using hj.le
        rw [‚Üê one_smul ‚Ñù (a.c j), hd, ‚Üê sub_smul, norm_smul, norm_sub_rev, Real.norm_eq_abs,
          abs_of_nonneg A, sub_mul]
        field_simp [(zero_le_two.trans_lt hj).ne']
        linarith only [hcrj]
  linarith only [this]
#align besicovitch.satellite_config.exists_normalized_aux2 Besicovitch.SatelliteConfig.exists_normalized_aux2

theorem exists_normalized_aux3 {N : ‚Ñï} {œÑ : ‚Ñù} (a : SatelliteConfig E N œÑ)
    (lastc : a.c (last N) = 0) (lastr : a.r (last N) = 1) (hœÑ : 1 ‚â§ œÑ) (Œ¥ : ‚Ñù) (hŒ¥1 : œÑ ‚â§ 1 + Œ¥ / 4)
    (i j : Fin N.succ) (inej : i ‚â† j) (hi : 2 < ‚Äña.c i‚Äñ) (hij : ‚Äña.c i‚Äñ ‚â§ ‚Äña.c j‚Äñ) :
    1 - Œ¥ ‚â§ ‚Äñ(2 / ‚Äña.c i‚Äñ) ‚Ä¢ a.c i - (2 / ‚Äña.c j‚Äñ) ‚Ä¢ a.c j‚Äñ := by
  have ah :
      Pairwise fun i j => a.r i ‚â§ ‚Äña.c i - a.c j‚Äñ ‚àß a.r j ‚â§ œÑ * a.r i ‚à®
        a.r j ‚â§ ‚Äña.c j - a.c i‚Äñ ‚àß a.r i ‚â§ œÑ * a.r j := by
    simpa only [dist_eq_norm] using a.h
  have Œ¥nonneg : 0 ‚â§ Œ¥ := by linarith only [hœÑ, hŒ¥1]
  have hcrj : ‚Äña.c j‚Äñ ‚â§ a.r j + 1 := by simpa only [lastc, lastr, dist_zero_right] using a.inter' j
  have A : a.r i ‚â§ ‚Äña.c i‚Äñ := by
    have : i < last N := by
      apply lt_top_iff_ne_top.2
      intro iN
      change i = last N at iN
      rw [iN, lastc, norm_zero] at hi
      exact lt_irrefl _ (zero_le_two.trans_lt hi)
    convert (a.hlast i this).1 using 1
    rw [dist_eq_norm, lastc, sub_zero]
  have hj : 2 < ‚Äña.c j‚Äñ := hi.trans_le hij
  set s := ‚Äña.c i‚Äñ
  have spos : 0 < s := zero_lt_two.trans hi
  set d := (s / ‚Äña.c j‚Äñ) ‚Ä¢ a.c j with hd
  have I : ‚Äña.c j - a.c i‚Äñ ‚â§ ‚Äña.c j‚Äñ - s + ‚Äñd - a.c i‚Äñ :=
    calc
      ‚Äña.c j - a.c i‚Äñ ‚â§ ‚Äña.c j - d‚Äñ + ‚Äñd - a.c i‚Äñ := by simp [‚Üê dist_eq_norm, dist_triangle]
      _ = ‚Äña.c j‚Äñ - ‚Äña.c i‚Äñ + ‚Äñd - a.c i‚Äñ := by
        nth_rw 1 [‚Üê one_smul ‚Ñù (a.c j)]
        rw [add_left_inj, hd, ‚Üê sub_smul, norm_smul, Real.norm_eq_abs, abs_of_nonneg, sub_mul,
          one_mul, div_mul_cancel‚ÇÄ _ (zero_le_two.trans_lt hj).ne']
        rwa [sub_nonneg, div_le_iff (zero_lt_two.trans hj), one_mul]
  have J : a.r j - ‚Äña.c j - a.c i‚Äñ ‚â§ s / 2 * Œ¥ :=
    calc
      a.r j - ‚Äña.c j - a.c i‚Äñ ‚â§ s * (œÑ - 1) := by
        rcases ah inej.symm with (H | H)
        ¬∑ calc
            a.r j - ‚Äña.c j - a.c i‚Äñ ‚â§ 0 := sub_nonpos.2 H.1
            _ ‚â§ s * (œÑ - 1) := mul_nonneg spos.le (sub_nonneg.2 hœÑ)
        ¬∑ rw [norm_sub_rev] at H
          calc
            a.r j - ‚Äña.c j - a.c i‚Äñ ‚â§ œÑ * a.r i - a.r i := sub_le_sub H.2 H.1
            _ = a.r i * (œÑ - 1) := by ring
            _ ‚â§ s * (œÑ - 1) := mul_le_mul_of_nonneg_right A (sub_nonneg.2 hœÑ)
      _ ‚â§ s * (Œ¥ / 2) := (mul_le_mul_of_nonneg_left (by linarith only [Œ¥nonneg, hŒ¥1]) spos.le)
      _ = s / 2 * Œ¥ := by ring
  have invs_nonneg : 0 ‚â§ 2 / s := div_nonneg zero_le_two (zero_le_two.trans hi.le)
  calc
    1 - Œ¥ = 2 / s * (s / 2 - s / 2 * Œ¥) := by field_simp [spos.ne']; ring
    _ ‚â§ 2 / s * ‚Äñd - a.c i‚Äñ :=
      (mul_le_mul_of_nonneg_left (by linarith only [hcrj, I, J, hi]) invs_nonneg)
    _ = ‚Äñ(2 / s) ‚Ä¢ a.c i - (2 / ‚Äña.c j‚Äñ) ‚Ä¢ a.c j‚Äñ := by
      conv_lhs => rw [norm_sub_rev, ‚Üê abs_of_nonneg invs_nonneg]
      rw [‚Üê Real.norm_eq_abs, ‚Üê norm_smul, smul_sub, hd, smul_smul]
      congr 3
      field_simp [spos.ne']
#align besicovitch.satellite_config.exists_normalized_aux3 Besicovitch.SatelliteConfig.exists_normalized_aux3

theorem exists_normalized {N : ‚Ñï} {œÑ : ‚Ñù} (a : SatelliteConfig E N œÑ) (lastc : a.c (last N) = 0)
    (lastr : a.r (last N) = 1) (hœÑ : 1 ‚â§ œÑ) (Œ¥ : ‚Ñù) (hŒ¥1 : œÑ ‚â§ 1 + Œ¥ / 4) (hŒ¥2 : Œ¥ ‚â§ 1) :
    ‚àÉ c' : Fin N.succ ‚Üí E, (‚àÄ n, ‚Äñc' n‚Äñ ‚â§ 2) ‚àß Pairwise fun i j => 1 - Œ¥ ‚â§ ‚Äñc' i - c' j‚Äñ := by
  let c' : Fin N.succ ‚Üí E := fun i => if ‚Äña.c i‚Äñ ‚â§ 2 then a.c i else (2 / ‚Äña.c i‚Äñ) ‚Ä¢ a.c i
  have norm_c'_le : ‚àÄ i, ‚Äñc' i‚Äñ ‚â§ 2 := by
    intro i
    simp only [c']
    split_ifs with h; ¬∑ exact h
    by_cases hi : ‚Äña.c i‚Äñ = 0 <;> field_simp [norm_smul, hi]
  refine ‚ü®c', fun n => norm_c'_le n, fun i j inej => ?_‚ü©
  -- up to exchanging `i` and `j`, one can assume `‚Äñc i‚Äñ ‚â§ ‚Äñc j‚Äñ`.
  wlog hij : ‚Äña.c i‚Äñ ‚â§ ‚Äña.c j‚Äñ generalizing i j
  ¬∑ rw [norm_sub_rev]; exact this j i inej.symm (le_of_not_le hij)
  rcases le_or_lt ‚Äña.c j‚Äñ 2 with (Hj | Hj)
  -- case `‚Äñc j‚Äñ ‚â§ 2` (and therefore also `‚Äñc i‚Äñ ‚â§ 2`)
  ¬∑ simp_rw [c', Hj, hij.trans Hj, if_true]
    exact exists_normalized_aux1 a lastr hœÑ Œ¥ hŒ¥1 hŒ¥2 i j inej
  -- case `2 < ‚Äñc j‚Äñ`
  ¬∑ have H'j : ‚Äña.c j‚Äñ ‚â§ 2 ‚Üî False := by simpa only [not_le, iff_false_iff] using Hj
    rcases le_or_lt ‚Äña.c i‚Äñ 2 with (Hi | Hi)
    ¬∑ -- case `‚Äñc i‚Äñ ‚â§ 2`
      simp_rw [c', Hi, if_true, H'j, if_false]
      exact exists_normalized_aux2 a lastc lastr hœÑ Œ¥ hŒ¥1 hŒ¥2 i j inej Hi Hj
    ¬∑ -- case `2 < ‚Äñc i‚Äñ`
      have H'i : ‚Äña.c i‚Äñ ‚â§ 2 ‚Üî False := by simpa only [not_le, iff_false_iff] using Hi
      simp_rw [c', H'i, if_false, H'j, if_false]
      exact exists_normalized_aux3 a lastc lastr hœÑ Œ¥ hŒ¥1 i j inej Hi hij
#align besicovitch.satellite_config.exists_normalized Besicovitch.SatelliteConfig.exists_normalized

end SatelliteConfig

variable (E)
variable [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]

/-- In a normed vector space `E`, there can be no satellite configuration with `multiplicity E + 1`
points and the parameter `goodœÑ E`. This will ensure that in the inductive construction to get
the Besicovitch covering families, there will never be more than `multiplicity E` nonempty
families. -/
theorem isEmpty_satelliteConfig_multiplicity :
    IsEmpty (SatelliteConfig E (multiplicity E) (goodœÑ E)) :=
  ‚ü®by
    intro a
    let b := a.centerAndRescale
    rcases b.exists_normalized a.centerAndRescale_center a.centerAndRescale_radius
        (one_lt_goodœÑ E).le (goodŒ¥ E) le_rfl (goodŒ¥_lt_one E).le with
      ‚ü®c', c'_le_two, hc'‚ü©
    exact
      lt_irrefl _ ((Nat.lt_succ_self _).trans_le (le_multiplicity_of_Œ¥_of_fin c' c'_le_two hc'))‚ü©
#align besicovitch.is_empty_satellite_config_multiplicity Besicovitch.isEmpty_satelliteConfig_multiplicity

instance (priority := 100) instHasBesicovitchCovering : HasBesicovitchCovering E :=
  ‚ü®‚ü®multiplicity E, goodœÑ E, one_lt_goodœÑ E, isEmpty_satelliteConfig_multiplicity E‚ü©‚ü©
#align besicovitch.has_besicovitch_covering Besicovitch.instHasBesicovitchCovering

end Besicovitch
