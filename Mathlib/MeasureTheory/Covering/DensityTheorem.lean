/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.MeasureTheory.Measure.Doubling
import Mathlib.MeasureTheory.Covering.Vitali
import Mathlib.MeasureTheory.Covering.Differentiation

/-!
# Uniformly locally doubling measures and Lebesgue's density theorem

Lebesgue's density theorem states that given a set `S` in a sigma compact metric space with
locally-finite uniformly locally doubling measure `Œº` then for almost all points `x` in `S`, for any
sequence of closed balls `B‚ÇÄ, B‚ÇÅ, B‚ÇÇ, ...` containing `x`, the limit `Œº (S ‚à© B‚±º) / Œº (B‚±º) ‚Üí 1` as
`j ‚Üí ‚àû`.

In this file we combine general results about existence of Vitali families for uniformly locally
doubling measures with results about differentiation along a Vitali family to obtain an explicit
form of Lebesgue's density theorem.

## Main results
* `IsUnifLocDoublingMeasure.ae_tendsto_measure_inter_div`: a version of Lebesgue's density
  theorem for sequences of balls converging on a point but whose centres are not required to be
  fixed.

-/


noncomputable section

open Set Filter Metric MeasureTheory TopologicalSpace

open scoped NNReal Topology

namespace IsUnifLocDoublingMeasure

variable {Œ± : Type*} [PseudoMetricSpace Œ±] [MeasurableSpace Œ±] (Œº : Measure Œ±)
  [IsUnifLocDoublingMeasure Œº]

section

variable [SecondCountableTopology Œ±] [BorelSpace Œ±] [IsLocallyFiniteMeasure Œº]

open scoped Topology

/-- A Vitali family in a space with a uniformly locally doubling measure, designed so that the sets
at `x` contain all `closedBall y r` when `dist x y ‚â§ K * r`. -/
irreducible_def vitaliFamily (K : ‚Ñù) : VitaliFamily Œº := by
  /- the Vitali covering theorem gives a family that works well at small scales, thanks to the
    doubling property. We enlarge this family to add large sets, to make sure that all balls and not
    only small ones belong to the family, for convenience. -/
  let R := scalingScaleOf Œº (max (4 * K + 3) 3)
  have Rpos : 0 < R := scalingScaleOf_pos _ _
  have A : ‚àÄ x : Œ±, ‚àÉ·∂† r in ùìù[>] (0 : ‚Ñù),
      Œº (closedBall x (3 * r)) ‚â§ scalingConstantOf Œº (max (4 * K + 3) 3) * Œº (closedBall x r) := by
    intro x
    apply frequently_iff.2 fun {U} hU => ?_
    obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© := mem_nhdsGT_iff_exists_Ioc_subset.1 hU
    refine ‚ü®min Œµ R, hŒµ ‚ü®lt_min Œµpos Rpos, min_le_left _ _‚ü©, ?_‚ü©
    exact measure_mul_le_scalingConstantOf_mul Œº
      ‚ü®zero_lt_three, le_max_right _ _‚ü© (min_le_right _ _)
  exact (Vitali.vitaliFamily Œº (scalingConstantOf Œº (max (4 * K + 3) 3)) A).enlarge (R / 4)
    (by linarith)

/-- In the Vitali family `IsUnifLocDoublingMeasure.vitaliFamily K`, the sets based at `x`
contain all balls `closedBall y r` when `dist x y ‚â§ K * r`. -/
theorem closedBall_mem_vitaliFamily_of_dist_le_mul {K : ‚Ñù} {x y : Œ±} {r : ‚Ñù} (h : dist x y ‚â§ K * r)
    (rpos : 0 < r) : closedBall y r ‚àà (vitaliFamily Œº K).setsAt x := by
  let R := scalingScaleOf Œº (max (4 * K + 3) 3)
  simp only [vitaliFamily, VitaliFamily.enlarge, Vitali.vitaliFamily, mem_union, mem_setOf_eq,
    isClosed_closedBall, true_and, (nonempty_ball.2 rpos).mono ball_subset_interior_closedBall,
    measurableSet_closedBall]
  /- The measure is doubling on scales smaller than `R`. Therefore, we treat differently small
    and large balls. For large balls, this follows directly from the enlargement we used in the
    definition. -/
  by_cases H : closedBall y r ‚äÜ closedBall x (R / 4)
  swap; ¬∑ exact Or.inr H
  left
  /- For small balls, there is the difficulty that `r` could be large but still the ball could be
    small, if the annulus `{y | Œµ ‚â§ dist y x ‚â§ R/4}` is empty. We split between the cases `r ‚â§ R`
    and `r > R`, and use the doubling for the former and rough estimates for the latter. -/
  rcases le_or_gt r R with (hr | hr)
  ¬∑ refine ‚ü®(K + 1) * r, ?_‚ü©
    constructor
    ¬∑ apply closedBall_subset_closedBall'
      rw [dist_comm]
      linarith
    ¬∑ have I1 : closedBall x (3 * ((K + 1) * r)) ‚äÜ closedBall y ((4 * K + 3) * r) := by
        apply closedBall_subset_closedBall'
        linarith
      have I2 : closedBall y ((4 * K + 3) * r) ‚äÜ closedBall y (max (4 * K + 3) 3 * r) := by
        apply closedBall_subset_closedBall
        exact mul_le_mul_of_nonneg_right (le_max_left _ _) rpos.le
      apply (measure_mono (I1.trans I2)).trans
      exact measure_mul_le_scalingConstantOf_mul _
        ‚ü®zero_lt_three.trans_le (le_max_right _ _), le_rfl‚ü© hr
  ¬∑ refine ‚ü®R / 4, H, ?_‚ü©
    have : closedBall x (3 * (R / 4)) ‚äÜ closedBall y r := by
      apply closedBall_subset_closedBall'
      have A : y ‚àà closedBall y r := mem_closedBall_self rpos.le
      have B := mem_closedBall'.1 (H A)
      linarith
    apply (measure_mono this).trans _
    refine le_mul_of_one_le_left (zero_le _) ?_
    exact ENNReal.one_le_coe_iff.2 (le_max_right _ _)

theorem tendsto_closedBall_filterAt {K : ‚Ñù} {x : Œ±} {Œπ : Type*} {l : Filter Œπ} (w : Œπ ‚Üí Œ±)
    (Œ¥ : Œπ ‚Üí ‚Ñù) (Œ¥lim : Tendsto Œ¥ l (ùìù[>] 0)) (xmem : ‚àÄ·∂† j in l, x ‚àà closedBall (w j) (K * Œ¥ j)) :
    Tendsto (fun j => closedBall (w j) (Œ¥ j)) l ((vitaliFamily Œº K).filterAt x) := by
  refine (vitaliFamily Œº K).tendsto_filterAt_iff.mpr ‚ü®?_, fun Œµ hŒµ => ?_‚ü©
  ¬∑ filter_upwards [xmem, Œ¥lim self_mem_nhdsWithin] with j hj h'j
    exact closedBall_mem_vitaliFamily_of_dist_le_mul Œº hj h'j
  ¬∑ rcases l.eq_or_neBot with rfl | h
    ¬∑ simp
    have hK : 0 ‚â§ K := by
      rcases (xmem.and (Œ¥lim self_mem_nhdsWithin)).exists with ‚ü®j, hj, h'j‚ü©
      have : 0 ‚â§ K * Œ¥ j := nonempty_closedBall.1 ‚ü®x, hj‚ü©
      exact (mul_nonneg_iff_left_nonneg_of_pos (mem_Ioi.1 h'j)).1 this
    have Œ¥pos := eventually_mem_of_tendsto_nhdsWithin Œ¥lim
    replace Œ¥lim := tendsto_nhds_of_tendsto_nhdsWithin Œ¥lim
    replace hK : 0 < K + 1 := by linarith
    apply (((Metric.tendsto_nhds.mp Œ¥lim _ (div_pos hŒµ hK)).and Œ¥pos).and xmem).mono
    rintro j ‚ü®‚ü®hjŒµ, hj‚ÇÄ : 0 < Œ¥ j‚ü©, hx‚ü© y hy
    replace hjŒµ : (K + 1) * Œ¥ j < Œµ := by
      simpa [abs_eq_self.mpr hj‚ÇÄ.le] using (lt_div_iff‚ÇÄ' hK).mp hjŒµ
    simp only [mem_closedBall] at hx hy ‚ä¢
    linarith [dist_triangle_right y x (w j)]

end

section Applications

variable [SecondCountableTopology Œ±] [BorelSpace Œ±] [IsLocallyFiniteMeasure Œº] {E : Type*}
  [NormedAddCommGroup E]

/-- A version of **Lebesgue's density theorem** for a sequence of closed balls whose centers are
not required to be fixed.

See also `Besicovitch.ae_tendsto_measure_inter_div`. -/
theorem ae_tendsto_measure_inter_div (S : Set Œ±) (K : ‚Ñù) : ‚àÄ·µê x ‚àÇŒº.restrict S,
    ‚àÄ {Œπ : Type*} {l : Filter Œπ} (w : Œπ ‚Üí Œ±) (Œ¥ : Œπ ‚Üí ‚Ñù) (_ : Tendsto Œ¥ l (ùìù[>] 0))
      (_ : ‚àÄ·∂† j in l, x ‚àà closedBall (w j) (K * Œ¥ j)),
      Tendsto (fun j => Œº (S ‚à© closedBall (w j) (Œ¥ j)) / Œº (closedBall (w j) (Œ¥ j))) l (ùìù 1) := by
  filter_upwards [(vitaliFamily Œº K).ae_tendsto_measure_inter_div S] with x hx Œπ l w Œ¥ Œ¥lim
    xmem using hx.comp (tendsto_closedBall_filterAt Œº _ _ Œ¥lim xmem)

/-- A version of **Lebesgue differentiation theorem** for a sequence of closed balls whose
centers are not required to be fixed. -/
theorem ae_tendsto_average_norm_sub {f : Œ± ‚Üí E} (hf : LocallyIntegrable f Œº) (K : ‚Ñù) : ‚àÄ·µê x ‚àÇŒº,
    ‚àÄ {Œπ : Type*} {l : Filter Œπ} (w : Œπ ‚Üí Œ±) (Œ¥ : Œπ ‚Üí ‚Ñù) (_ : Tendsto Œ¥ l (ùìù[>] 0))
      (_ : ‚àÄ·∂† j in l, x ‚àà closedBall (w j) (K * Œ¥ j)),
      Tendsto (fun j => ‚®ç y in closedBall (w j) (Œ¥ j), ‚Äñf y - f x‚Äñ ‚àÇŒº) l (ùìù 0) := by
  filter_upwards [(vitaliFamily Œº K).ae_tendsto_average_norm_sub hf] with x hx Œπ l w Œ¥ Œ¥lim
    xmem using hx.comp (tendsto_closedBall_filterAt Œº _ _ Œ¥lim xmem)

/-- A version of **Lebesgue differentiation theorem** for a sequence of closed balls whose
centers are not required to be fixed. -/
theorem ae_tendsto_average [NormedSpace ‚Ñù E] [CompleteSpace E]
    {f : Œ± ‚Üí E} (hf : LocallyIntegrable f Œº) (K : ‚Ñù) : ‚àÄ·µê x ‚àÇŒº,
      ‚àÄ {Œπ : Type*} {l : Filter Œπ} (w : Œπ ‚Üí Œ±) (Œ¥ : Œπ ‚Üí ‚Ñù) (_ : Tendsto Œ¥ l (ùìù[>] 0))
        (_ : ‚àÄ·∂† j in l, x ‚àà closedBall (w j) (K * Œ¥ j)),
        Tendsto (fun j => ‚®ç y in closedBall (w j) (Œ¥ j), f y ‚àÇŒº) l (ùìù (f x)) := by
  filter_upwards [(vitaliFamily Œº K).ae_tendsto_average hf] with x hx Œπ l w Œ¥ Œ¥lim xmem using
    hx.comp (tendsto_closedBall_filterAt Œº _ _ Œ¥lim xmem)

end Applications

end IsUnifLocDoublingMeasure
