/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.MeasureTheory.Measure.MeasureSpace

#align_import measure_theory.covering.vitali_family from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Vitali families

On a metric space `X` with a measure `Œº`, consider for each `x : X` a family of measurable sets with
nonempty interiors, called `setsAt x`. This family is a Vitali family if it satisfies the following
property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `setsAt x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.

This file gives the basic definition of Vitali families. More interesting developments of this
notion are deferred to other files:
* constructions of specific Vitali families are provided by the Besicovitch covering theorem, in
`Besicovitch.vitaliFamily`, and by the Vitali covering theorem, in `Vitali.vitaliFamily`.
* The main theorem on differentiation of measures along a Vitali family is proved in
`VitaliFamily.ae_tendsto_rnDeriv`.

## Main definitions

* `VitaliFamily Œº` is a structure made, for each `x : X`, of a family of sets around `x`, such that
one can extract an almost everywhere disjoint covering from any subfamily containing sets of
arbitrarily small diameters.

Let `v` be such a Vitali family.
* `v.FineSubfamilyOn` describes the subfamilies of `v` from which one can extract almost
everywhere disjoint coverings. This property, called
`v.FineSubfamilyOn.exists_disjoint_covering_ae`, is essentially a restatement of the definition
of a Vitali family. We also provide an API to use efficiently such a disjoint covering.
* `v.filterAt x` is a filter on sets of `X`, such that convergence with respect to this filter
means convergence when sets in the Vitali family shrink towards `x`.

## References

* [Herbert Federer, Geometric Measure Theory, Chapter 2.8][Federer1996] (Vitali families are called
Vitali relations there)
-/


open MeasureTheory Metric Set Filter TopologicalSpace MeasureTheory.Measure

open Filter MeasureTheory Topology

variable {Œ± : Type*} [MetricSpace Œ±]

/-- On a metric space `X` with a measure `Œº`, consider for each `x : X` a family of measurable sets
with nonempty interiors, called `setsAt x`. This family is a Vitali family if it satisfies the
following property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `setsAt x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.
-/
-- @[nolint has_nonempty_instance] -- Porting note: This linter does not exist yet.
structure VitaliFamily {m : MeasurableSpace Œ±} (Œº : Measure Œ±) where
  setsAt : ‚àÄ _ : Œ±, Set (Set Œ±)
  MeasurableSet' : ‚àÄ x : Œ±, ‚àÄ a : Set Œ±, a ‚àà setsAt x ‚Üí MeasurableSet a
  nonempty_interior : ‚àÄ x : Œ±, ‚àÄ y : Set Œ±, y ‚àà setsAt x ‚Üí (interior y).Nonempty
  Nontrivial : ‚àÄ (x : Œ±), ‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ y ‚àà setsAt x, y ‚äÜ closedBall x Œµ
  covering : ‚àÄ (s : Set Œ±) (f : ‚àÄ _ : Œ±, Set (Set Œ±)),
    (‚àÄ x ‚àà s, f x ‚äÜ setsAt x) ‚Üí (‚àÄ x ‚àà s, ‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ a ‚àà f x, a ‚äÜ closedBall x Œµ) ‚Üí
    ‚àÉ t : Set (Œ± √ó Set Œ±),
      (‚àÄ p : Œ± √ó Set Œ±, p ‚àà t ‚Üí p.1 ‚àà s) ‚àß (t.PairwiseDisjoint fun p => p.2) ‚àß
      (‚àÄ p : Œ± √ó Set Œ±, p ‚àà t ‚Üí p.2 ‚àà f p.1) ‚àß Œº (s \ ‚ãÉ (p : Œ± √ó Set Œ±) (_ : p ‚àà t), p.2) = 0
#align vitali_family VitaliFamily

namespace VitaliFamily

variable {m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

/-- A Vitali family for a measure `Œº` is also a Vitali family for any measure absolutely continuous
with respect to `Œº`. -/
def mono (v : VitaliFamily Œº) (ŒΩ : Measure Œ±) (hŒΩ : ŒΩ ‚â™ Œº) : VitaliFamily ŒΩ where
  setsAt := v.setsAt
  MeasurableSet' := v.MeasurableSet'
  nonempty_interior := v.nonempty_interior
  Nontrivial := v.Nontrivial
  covering s f h h' := by
    rcases v.covering s f h h' with ‚ü®t, ts, disj, mem_f, hŒº‚ü©
    exact ‚ü®t, ts, disj, mem_f, hŒΩ hŒº‚ü©
#align vitali_family.mono VitaliFamily.mono

/-- Given a Vitali family `v` for a measure `Œº`, a family `f` is a fine subfamily on a set `s` if
every point `x` in `s` belongs to arbitrarily small sets in `v.setsAt x ‚à© f x`. This is precisely
the subfamilies for which the Vitali family definition ensures that one can extract a disjoint
covering of almost all `s`. -/
def FineSubfamilyOn (v : VitaliFamily Œº) (f : Œ± ‚Üí Set (Set Œ±)) (s : Set Œ±) : Prop :=
  ‚àÄ x ‚àà s, ‚àÄ Œµ > 0, ‚àÉ a ‚àà v.setsAt x ‚à© f x, a ‚äÜ closedBall x Œµ
#align vitali_family.fine_subfamily_on VitaliFamily.FineSubfamilyOn

namespace FineSubfamilyOn

variable {v : VitaliFamily Œº} {f : Œ± ‚Üí Set (Set Œ±)} {s : Set Œ±} (h : v.FineSubfamilyOn f s)

theorem exists_disjoint_covering_ae :
    ‚àÉ t : Set (Œ± √ó Set Œ±),
      (‚àÄ p : Œ± √ó Set Œ±, p ‚àà t ‚Üí p.1 ‚àà s) ‚àß
      (t.PairwiseDisjoint fun p => p.2) ‚àß
      (‚àÄ p : Œ± √ó Set Œ±, p ‚àà t ‚Üí p.2 ‚àà v.setsAt p.1 ‚à© f p.1) ‚àß
      Œº (s \ ‚ãÉ (p : Œ± √ó Set Œ±) (_ : p ‚àà t), p.2) = 0 :=
  v.covering s (fun x => v.setsAt x ‚à© f x) (fun _ _ => inter_subset_left _ _) h
#align vitali_family.fine_subfamily_on.exists_disjoint_covering_ae VitaliFamily.FineSubfamilyOn.exists_disjoint_covering_ae

/-- Given `h : v.FineSubfamilyOn f s`, then `h.index` is a set parametrizing a disjoint
covering of almost every `s`. -/
protected def index : Set (Œ± √ó Set Œ±) :=
  h.exists_disjoint_covering_ae.choose
#align vitali_family.fine_subfamily_on.index VitaliFamily.FineSubfamilyOn.index

-- Porting note: Needed to add `(_h : FineSubfamilyOn v f s)`
/-- Given `h : v.FineSubfamilyOn f s`, then `h.covering p` is a set in the family,
for `p ‚àà h.index`, such that these sets form a disjoint covering of almost every `s`. -/
@[nolint unusedArguments]
protected def covering (_h : FineSubfamilyOn v f s) : Œ± √ó Set Œ± ‚Üí Set Œ± :=
  fun p => p.2
#align vitali_family.fine_subfamily_on.covering VitaliFamily.FineSubfamilyOn.covering

theorem index_subset : ‚àÄ p : Œ± √ó Set Œ±, p ‚àà h.index ‚Üí p.1 ‚àà s :=
  h.exists_disjoint_covering_ae.choose_spec.1
#align vitali_family.fine_subfamily_on.index_subset VitaliFamily.FineSubfamilyOn.index_subset

theorem covering_disjoint : h.index.PairwiseDisjoint h.covering :=
  h.exists_disjoint_covering_ae.choose_spec.2.1
#align vitali_family.fine_subfamily_on.covering_disjoint VitaliFamily.FineSubfamilyOn.covering_disjoint

theorem covering_disjoint_subtype : Pairwise (Disjoint on fun x : h.index => h.covering x) :=
  (pairwise_subtype_iff_pairwise_set _ _).2 h.covering_disjoint
#align vitali_family.fine_subfamily_on.covering_disjoint_subtype VitaliFamily.FineSubfamilyOn.covering_disjoint_subtype

theorem covering_mem {p : Œ± √ó Set Œ±} (hp : p ‚àà h.index) : h.covering p ‚àà f p.1 :=
  (h.exists_disjoint_covering_ae.choose_spec.2.2.1 p hp).2
#align vitali_family.fine_subfamily_on.covering_mem VitaliFamily.FineSubfamilyOn.covering_mem

theorem covering_mem_family {p : Œ± √ó Set Œ±} (hp : p ‚àà h.index) : h.covering p ‚àà v.setsAt p.1 :=
  (h.exists_disjoint_covering_ae.choose_spec.2.2.1 p hp).1
#align vitali_family.fine_subfamily_on.covering_mem_family VitaliFamily.FineSubfamilyOn.covering_mem_family

theorem measure_diff_biUnion : Œº (s \ ‚ãÉ p ‚àà h.index, h.covering p) = 0 :=
  h.exists_disjoint_covering_ae.choose_spec.2.2.2
#align vitali_family.fine_subfamily_on.measure_diff_bUnion VitaliFamily.FineSubfamilyOn.measure_diff_biUnion

theorem index_countable [SecondCountableTopology Œ±] : h.index.Countable :=
  h.covering_disjoint.countable_of_nonempty_interior fun _ hx =>
    v.nonempty_interior _ _ (h.covering_mem_family hx)
#align vitali_family.fine_subfamily_on.index_countable VitaliFamily.FineSubfamilyOn.index_countable

protected theorem measurableSet_u {p : Œ± √ó Set Œ±} (hp : p ‚àà h.index) :
    MeasurableSet (h.covering p) :=
  v.MeasurableSet' p.1 _ (h.covering_mem_family hp)
#align vitali_family.fine_subfamily_on.measurable_set_u VitaliFamily.FineSubfamilyOn.measurableSet_u

theorem measure_le_tsum_of_absolutelyContinuous [SecondCountableTopology Œ±] {œÅ : Measure Œ±}
    (hœÅ : œÅ ‚â™ Œº) : œÅ s ‚â§ ‚àë' p : h.index, œÅ (h.covering p) :=
  calc
    œÅ s ‚â§ œÅ ((s \ ‚ãÉ p ‚àà h.index, h.covering p) ‚à™ ‚ãÉ p ‚àà h.index, h.covering p) :=
      measure_mono (by simp only [subset_union_left, diff_union_self])
    _ ‚â§ œÅ (s \ ‚ãÉ p ‚àà h.index, h.covering p) + œÅ (‚ãÉ p ‚àà h.index, h.covering p) :=
      (measure_union_le _ _)
    _ = ‚àë' p : h.index, œÅ (h.covering p) := by
      rw [hœÅ h.measure_diff_biUnion, zero_add,
        measure_biUnion h.index_countable h.covering_disjoint fun x hx => h.measurableSet_u hx]
#align vitali_family.fine_subfamily_on.measure_le_tsum_of_absolutely_continuous VitaliFamily.FineSubfamilyOn.measure_le_tsum_of_absolutelyContinuous

theorem measure_le_tsum [SecondCountableTopology Œ±] : Œº s ‚â§ ‚àë' x : h.index, Œº (h.covering x) :=
  h.measure_le_tsum_of_absolutelyContinuous Measure.AbsolutelyContinuous.rfl
#align vitali_family.fine_subfamily_on.measure_le_tsum VitaliFamily.FineSubfamilyOn.measure_le_tsum

end FineSubfamilyOn

/-- One can enlarge a Vitali family by adding to the sets `f x` at `x` all sets which are not
contained in a `Œ¥`-neighborhood on `x`. This does not change the local filter at a point, but it
can be convenient to get a nicer global behavior. -/
def enlarge (v : VitaliFamily Œº) (Œ¥ : ‚Ñù) (Œ¥pos : 0 < Œ¥) : VitaliFamily Œº where
  setsAt x := v.setsAt x ‚à™ { a | MeasurableSet a ‚àß (interior a).Nonempty ‚àß ¬¨a ‚äÜ closedBall x Œ¥ }
  MeasurableSet' x a ha := by
    cases' ha with ha ha
    exacts [v.MeasurableSet' _ _ ha, ha.1]
  nonempty_interior x a ha := by
    cases' ha with ha ha
    exacts [v.nonempty_interior _ _ ha, ha.2.1]
  Nontrivial := by
    intro x Œµ Œµpos
    rcases v.Nontrivial x Œµ Œµpos with ‚ü®a, ha, h'a‚ü©
    exact ‚ü®a, mem_union_left _ ha, h'a‚ü©
  covering := by
    intro s f fset ffine
    let g : Œ± ‚Üí Set (Set Œ±) := fun x => f x ‚à© v.setsAt x
    have : ‚àÄ x ‚àà s, ‚àÄ Œµ : ‚Ñù, Œµ > 0 ‚Üí ‚àÉ (a : Set Œ±), a ‚àà g x ‚àß a ‚äÜ closedBall x Œµ := by
      intro x hx Œµ Œµpos
      obtain ‚ü®a, af, ha‚ü© : ‚àÉ a ‚àà f x, a ‚äÜ closedBall x (min Œµ Œ¥)
      exact ffine x hx (min Œµ Œ¥) (lt_min Œµpos Œ¥pos)
      rcases fset x hx af with (h'a | h'a)
      ¬∑ exact ‚ü®a, ‚ü®af, h'a‚ü©, ha.trans (closedBall_subset_closedBall (min_le_left _ _))‚ü©
      ¬∑ refine' False.elim (h'a.2.2 _)
        exact ha.trans (closedBall_subset_closedBall (min_le_right _ _))
    rcases v.covering s g (fun x _ => inter_subset_right _ _) this with ‚ü®t, ts, tdisj, tg, Œºt‚ü©
    exact ‚ü®t, ts, tdisj, fun p hp => (tg p hp).1, Œºt‚ü©
#align vitali_family.enlarge VitaliFamily.enlarge

variable (v : VitaliFamily Œº)

/-- Given a vitali family `v`, then `v.filterAt x` is the filter on `Set Œ±` made of those families
that contain all sets of `v.setsAt x` of a sufficiently small diameter. This filter makes it
possible to express limiting behavior when sets in `v.setsAt x` shrink to `x`. -/
def filterAt (x : Œ±) : Filter (Set Œ±) :=
  ‚®Ö Œµ ‚àà Ioi (0 : ‚Ñù), ùìü ({ a ‚àà v.setsAt x | a ‚äÜ closedBall x Œµ })
#align vitali_family.filter_at VitaliFamily.filterAt

theorem mem_filterAt_iff {x : Œ±} {s : Set (Set Œ±)} :
    s ‚àà v.filterAt x ‚Üî ‚àÉ Œµ > (0 : ‚Ñù), ‚àÄ a ‚àà v.setsAt x, a ‚äÜ closedBall x Œµ ‚Üí a ‚àà s := by
  simp only [filterAt, exists_prop, gt_iff_lt]
  rw [mem_biInf_of_directed]
  ¬∑ simp only [subset_def, and_imp, exists_prop, mem_sep_iff, mem_Ioi, mem_principal]
  ¬∑ simp only [DirectedOn, exists_prop, ge_iff_le, le_principal_iff, mem_Ioi, Order.Preimage,
      mem_principal]
    intro x hx y hy
    refine' ‚ü®min x y, lt_min hx hy,
      fun a ha => ‚ü®ha.1, ha.2.trans (closedBall_subset_closedBall (min_le_left _ _))‚ü©,
      fun a ha => ‚ü®ha.1, ha.2.trans (closedBall_subset_closedBall (min_le_right _ _))‚ü©‚ü©
  ¬∑ exact ‚ü®(1 : ‚Ñù), mem_Ioi.2 zero_lt_one‚ü©
#align vitali_family.mem_filter_at_iff VitaliFamily.mem_filterAt_iff

instance filterAt_neBot (x : Œ±) : (v.filterAt x).NeBot := by
  simp only [neBot_iff, ‚Üê empty_mem_iff_bot, mem_filterAt_iff, not_exists, exists_prop,
    mem_empty_iff_false, and_true_iff, gt_iff_lt, not_and, Ne.def, not_false_iff, not_forall]
  intro Œµ Œµpos
  obtain ‚ü®w, w_sets, hw‚ü© : ‚àÉ w ‚àà v.setsAt x, w ‚äÜ closedBall x Œµ := v.Nontrivial x Œµ Œµpos
  exact ‚ü®w, w_sets, hw‚ü©
#align vitali_family.filter_at_ne_bot VitaliFamily.filterAt_neBot

theorem eventually_filterAt_iff {x : Œ±} {P : Set Œ± ‚Üí Prop} :
    (‚àÄ·∂† a in v.filterAt x, P a) ‚Üî ‚àÉ Œµ > (0 : ‚Ñù), ‚àÄ a ‚àà v.setsAt x, a ‚äÜ closedBall x Œµ ‚Üí P a :=
  v.mem_filterAt_iff
#align vitali_family.eventually_filter_at_iff VitaliFamily.eventually_filterAt_iff

theorem eventually_filterAt_mem_sets (x : Œ±) : ‚àÄ·∂† a in v.filterAt x, a ‚àà v.setsAt x := by
  simp (config := { contextual := true }) only [eventually_filterAt_iff, exists_prop, and_true_iff,
    gt_iff_lt, imp_true_iff]
  exact ‚ü®1, zero_lt_one‚ü©
#align vitali_family.eventually_filter_at_mem_sets VitaliFamily.eventually_filterAt_mem_sets

theorem eventually_filterAt_subset_closedBall (x : Œ±) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÄ·∂† a : Set Œ± in v.filterAt x, a ‚äÜ closedBall x Œµ := by
  simp only [v.eventually_filterAt_iff]
  exact ‚ü®Œµ, hŒµ, fun a _ ha' => ha'‚ü©
#align vitali_family.eventually_filter_at_subset_closed_ball VitaliFamily.eventually_filterAt_subset_closedBall

theorem tendsto_filterAt_iff {Œπ : Type*} {l : Filter Œπ} {f : Œπ ‚Üí Set Œ±} {x : Œ±} :
    Tendsto f l (v.filterAt x) ‚Üî
      (‚àÄ·∂† i in l, f i ‚àà v.setsAt x) ‚àß ‚àÄ Œµ > (0 : ‚Ñù), ‚àÄ·∂† i in l, f i ‚äÜ closedBall x Œµ := by
  refine' ‚ü®fun H => ‚ü®H.eventually <| v.eventually_filterAt_mem_sets x,
    fun Œµ hŒµ => H.eventually <| v.eventually_filterAt_subset_closedBall x hŒµ‚ü©,
    fun H s hs => (_ : ‚àÄ·∂† i in l, f i ‚àà s)‚ü©
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© := v.mem_filterAt_iff.mp hs
  filter_upwards [H.1, H.2 Œµ Œµpos] with i hi hiŒµ using hŒµ _ hi hiŒµ
#align vitali_family.tendsto_filter_at_iff VitaliFamily.tendsto_filterAt_iff

theorem eventually_filterAt_measurableSet (x : Œ±) : ‚àÄ·∂† a in v.filterAt x, MeasurableSet a := by
  filter_upwards [v.eventually_filterAt_mem_sets x] with _ ha using v.MeasurableSet' _ _ ha
#align vitali_family.eventually_filter_at_measurable_set VitaliFamily.eventually_filterAt_measurableSet

theorem frequently_filterAt_iff {x : Œ±} {P : Set Œ± ‚Üí Prop} :
    (‚àÉ·∂† a in v.filterAt x, P a) ‚Üî ‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ a ‚àà v.setsAt x, a ‚äÜ closedBall x Œµ ‚àß P a := by
  simp only [Filter.Frequently, eventually_filterAt_iff, not_exists, exists_prop, not_and,
    Classical.not_not, not_forall]
#align vitali_family.frequently_filter_at_iff VitaliFamily.frequently_filterAt_iff

theorem eventually_filterAt_subset_of_nhds {x : Œ±} {o : Set Œ±} (hx : o ‚àà ùìù x) :
    ‚àÄ·∂† a in v.filterAt x, a ‚äÜ o := by
  rw [eventually_filterAt_iff]
  rcases Metric.mem_nhds_iff.1 hx with ‚ü®Œµ, Œµpos, hŒµ‚ü©
  exact ‚ü®Œµ / 2, half_pos Œµpos,
    fun a _ ha => ha.trans ((closedBall_subset_ball (half_lt_self Œµpos)).trans hŒµ)‚ü©
#align vitali_family.eventually_filter_at_subset_of_nhds VitaliFamily.eventually_filterAt_subset_of_nhds

theorem fineSubfamilyOn_of_frequently (v : VitaliFamily Œº) (f : Œ± ‚Üí Set (Set Œ±)) (s : Set Œ±)
    (h : ‚àÄ x ‚àà s, ‚àÉ·∂† a in v.filterAt x, a ‚àà f x) : v.FineSubfamilyOn f s := by
  intro x hx Œµ Œµpos
  obtain ‚ü®a, av, ha, af‚ü© : ‚àÉ (a : Set Œ±) , a ‚àà v.setsAt x ‚àß a ‚äÜ closedBall x Œµ ‚àß a ‚àà f x :=
    v.frequently_filterAt_iff.1 (h x hx) Œµ Œµpos
  exact ‚ü®a, ‚ü®av, af‚ü©, ha‚ü©
#align vitali_family.fine_subfamily_on_of_frequently VitaliFamily.fineSubfamilyOn_of_frequently

end VitaliFamily
