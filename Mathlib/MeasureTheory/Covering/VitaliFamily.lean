/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Measure.MeasureSpace

/-!
# Vitali families

On a metric space `X` with a measure `Î¼`, consider for each `x : X` a family of measurable sets with
nonempty interiors, called `setsAt x`. This family is a Vitali family if it satisfies the following
property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `setsAt x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.

This file gives the basic definition of Vitali families. More interesting developments of this
notion are deferred to other files:
* constructions of specific Vitali families are provided by the Besicovitch covering theorem, in
`Besicovitch.vitaliFamily`, and by the Vitali covering theorem, in `Vitali.vitaliFamily`.
* The main theorem on differentiation of measures along a Vitali family is proved in
`VitaliFamily.ae_tendsto_rnDeriv`.

## Main definitions

* `VitaliFamily Î¼` is a structure made, for each `x : X`, of a family of sets around `x`, such that
one can extract an almost everywhere disjoint covering from any subfamily containing sets of
arbitrarily small diameters.

Let `v` be such a Vitali family.
* `v.FineSubfamilyOn` describes the subfamilies of `v` from which one can extract almost
everywhere disjoint coverings. This property, called
`v.FineSubfamilyOn.exists_disjoint_covering_ae`, is essentially a restatement of the definition
of a Vitali family. We also provide an API to use efficiently such a disjoint covering.
* `v.filterAt x` is a filter on sets of `X`, such that convergence with respect to this filter
means convergence when sets in the Vitali family shrink towards `x`.

## References

* [Herbert Federer, Geometric Measure Theory, Chapter 2.8][Federer1996] (Vitali families are called
Vitali relations there)
-/


open MeasureTheory Set Filter TopologicalSpace MeasureTheory.Measure

open scoped Topology

universe u

/-- On a metric space `X` with a measure `Î¼`, consider for each `x : X` a family of measurable sets
with nonempty interiors, called `setsAt x`. This family is a Vitali family if it satisfies the
following property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `setsAt x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.
-/
-- Porting note (https://github.com/leanprover-community/mathlib4/issues/5171): this linter isn't ported yet.
-- @[nolint has_nonempty_instance]
structure VitaliFamily {X : Type u} [TopologicalSpace X] {m : MeasurableSpace X}
    (Î¼ : Measure X) where
  /-- Sets of the family "centered" at a given point. -/
  setsAt :  X â†’ Set (Set X)
  /-- All sets of the family are measurable. -/
  measurableSet : âˆ€ x : X, âˆ€ s âˆˆ setsAt x, MeasurableSet s
  /-- For any closed ball around `x`, there exists a set of the family contained in this ball. -/
  nontrivial : âˆ€ (x : X), âˆƒá¶  s in (ğ“ x).smallSets, s âˆˆ setsAt x
  /-- Consider a (possibly non-measurable) set `s`,
  and for any `x` in `s` a subfamily `f x` of `setsAt x`
  containing sets of arbitrarily small diameter.
  Then one can extract a disjoint subfamily covering almost all `s`. -/
  covering : âˆ€ (s : Set X) (f : X â†’ Set (Set X)),
    (âˆ€ x, f x âŠ† setsAt x) â†’ (âˆ€ x, âˆƒá¶  t in (ğ“ x).smallSets, t âˆˆ f x) â†’
    âˆƒ (Î¹ : Type u) (c : Î¹ â†’ X) (t : Î¹ â†’ Set X), Countable Î¹ âˆ§
      (âˆ€ i, c i âˆˆ s) âˆ§ Pairwise (Disjoint on t) âˆ§ (âˆ€ i, t i âˆˆ f (c i)) âˆ§ Î¼ (s \ â‹ƒ i, t i) = 0

namespace VitaliFamily

section TopologicalSpace

variable {X : Type u} [TopologicalSpace X] {m0 : MeasurableSpace X} {Î¼ : Measure X}

/-- A Vitali family for a measure `Î¼` is also a Vitali family for any measure absolutely continuous
with respect to `Î¼`. -/
def mono (v : VitaliFamily Î¼) (Î½ : Measure X) (hÎ½ : Î½ â‰ª Î¼) : VitaliFamily Î½ where
  __ := v
  covering s f h h' :=
    let âŸ¨Î¹, c, t, hcount, hc, hd, ht, hÎ¼âŸ© := v.covering s f h h'
    âŸ¨Î¹, c, t, hcount, hc, hd, ht, hÎ½ hÎ¼âŸ©

/-- Given a vitali family `v`, then `v.filterAt x` is the filter on `Set X` made of those families
that contain all sets of `v.setsAt x` of a sufficiently small diameter. This filter makes it
possible to express limiting behavior when sets in `v.setsAt x` shrink to `x`. -/
def filterAt (v : VitaliFamily Î¼) (x : X) : Filter (Set X) := (ğ“ x).smallSets âŠ“ ğ“Ÿ (v.setsAt x)

theorem _root_.Filter.HasBasis.vitaliFamily {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} {x : X}
    (h : (ğ“ x).HasBasis p s) (v : VitaliFamily Î¼) :
    (v.filterAt x).HasBasis p (fun i â†¦ {t âˆˆ v.setsAt x | t âŠ† s i}) := by
  simpa only [â† Set.setOf_inter_eq_sep] using h.smallSets.inf_principal _

instance filterAt_neBot (v : VitaliFamily Î¼) (x : X) : (v.filterAt x).NeBot :=
  frequently_mem_iff_neBot.1 <| v.nontrivial x

theorem eventually_subset_of_nhds (v : VitaliFamily Î¼) {x : X} {U : Set X} (hx : U âˆˆ ğ“ x) :
    âˆ€á¶  a in v.filterAt x, a âŠ† U :=
  (eventually_smallSets_subset.2 hx).filter_mono inf_le_left

theorem eventually_mem_setsAt (v : VitaliFamily Î¼) (x : X) :
    âˆ€á¶  s in v.filterAt x, s âˆˆ v.setsAt x :=
  eventually_inf_principal.2 <| eventually_of_forall fun _ â†¦ id

theorem eventually_measurableSet (v : VitaliFamily Î¼) (x : X) :
    âˆ€á¶  a in v.filterAt x, MeasurableSet a :=
  (v.eventually_mem_setsAt x).mono <| v.measurableSet _

structure FineSubfamilyOn (v : VitaliFamily Î¼) (s : Set X) where
  toFun : X â†’ Set (Set X)
  ae_frequently' : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆƒá¶  t in v.filterAt x, t âˆˆ toFun x

namespace FineSubfamilyOn

variable {v : VitaliFamily Î¼} {s : Set X}

instance : FunLike (FineSubfamilyOn v s) X (Set (Set X)) where
  coe f := f.toFun
  coe_injective' | âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, rfl => rfl

theorem ae_frequently (f : FineSubfamilyOn v s) : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆƒá¶  t in v.filterAt x, t âˆˆ f x :=
  f.ae_frequently'

theorem exists_aecovering (f : FineSubfamilyOn v s) :
    âˆƒ (Î¹ : Type u) (c : Î¹ â†’ X) (t : Î¹ â†’ Set X), Countable Î¹ âˆ§ Î¼ (s \ â‹ƒ i, t i) = 0 âˆ§
      Pairwise (Disjoint on t) âˆ§ âˆ€ i, c i âˆˆ s âˆ§ t i âˆˆ v.setsAt (c i) âˆ§ t i âˆˆ f (c i) := by
  classical
  set s' := {x âˆˆ s | âˆƒá¶  t in v.filterAt x, t âˆˆ f x}
  set f' := fun x â†¦ v.setsAt x âˆ© if x âˆˆ s' then f x else univ
  have H : âˆ€ x, âˆƒá¶  t in smallSets (ğ“ x), t âˆˆ f' x := fun x â†¦ by
    if hx : x âˆˆ s' then simpa only [if_pos hx] using hx.2.of_inf_principal
    else simpa only [if_neg hx, inter_univ] using v.nontrivial x
  obtain âŸ¨Î¹, c, t, hcount, hcs, hd, ht, hÎ¼âŸ© := v.covering s' f' (fun x â†¦ inter_subset_left _ _) H
  refine âŸ¨Î¹, c, t, hcount, ?_, hd, fun i â†¦ âŸ¨(hcs i).1, (ht i).1, ?_âŸ©âŸ©
  Â· rw [â† ae_le_set] at hÎ¼ âŠ¢
    refine EventuallyLE.trans ?_ hÎ¼
    filter_upwards [f.ae_frequently] with x hx hxs using âŸ¨hxs, hx hxsâŸ©
  Â· simpa only [if_pos (hcs i)] using (ht i).2

def Index (f : FineSubfamilyOn v s) : Type u := f.exists_aecovering.choose

noncomputable def center (f : FineSubfamilyOn v s) : f.Index â†’ X :=
  f.exists_aecovering.choose_spec.choose

noncomputable def covering (f : FineSubfamilyOn v s) : f.Index â†’ Set X :=
  f.exists_aecovering.choose_spec.choose_spec.choose

instance (f : FineSubfamilyOn v s) : Countable f.Index :=
  f.exists_aecovering.choose_spec.choose_spec.choose_spec.1

theorem measure_diff_iUnion (f : FineSubfamilyOn v s) : Î¼ (s \ â‹ƒ i, f.covering i) = 0 :=
  f.exists_aecovering.choose_spec.choose_spec.choose_spec.2.1

theorem ae_le_iUnion (f : FineSubfamilyOn v s) : s â‰¤áµ[Î¼] â‹ƒ i, f.covering i :=
  ae_le_set.2 f.measure_diff_iUnion

theorem ae_eq_iUnion_inter (f : FineSubfamilyOn v s) : s =áµ[Î¼] â‹ƒ i, f.covering i âˆ© s := by
  rw [â† iUnion_inter, ae_eq_set]
  simp [f.measure_diff_iUnion, inter_diff_assoc]

theorem pairwise_disjoint (f : FineSubfamilyOn v s) : Pairwise (Disjoint on f.covering) :=
  f.exists_aecovering.choose_spec.choose_spec.choose_spec.2.2.1

theorem disjoint (f : FineSubfamilyOn v s) {i j : f.Index} (h : i â‰  j) :
    Disjoint (f.covering i) (f.covering j) :=
  f.pairwise_disjoint h

theorem center_mem (f : FineSubfamilyOn v s) (i : f.Index) : f.center i âˆˆ s :=
  (f.exists_aecovering.choose_spec.choose_spec.choose_spec.2.2.2 i).1

theorem covering_mem_setsAt (f : FineSubfamilyOn v s) (i : f.Index) :
    f.covering i âˆˆ v.setsAt (f.center i) :=
  (f.exists_aecovering.choose_spec.choose_spec.choose_spec.2.2.2 i).2.1

theorem covering_mem (f : FineSubfamilyOn v s) (i : f.Index) : f.covering i âˆˆ f (f.center i) :=
  (f.exists_aecovering.choose_spec.choose_spec.choose_spec.2.2.2 i).2.2

protected theorem measurableSet_covering (f : FineSubfamilyOn v s) (i : f.Index) :
    MeasurableSet (f.covering i) :=
  v.measurableSet _ _ (f.covering_mem_setsAt _)

theorem outerMeasure_le_tsum_of_absolutelyContinuous (f : FineSubfamilyOn v s)
    {Ï : OuterMeasure X} (hÏ : âˆ€ â¦ƒtâ¦„, Î¼ t = 0 â†’ Ï t = 0) : Ï s â‰¤ âˆ‘' i, Ï (f.covering i) :=
  -- TODO: introduce `OuterMeasure.ae` and golf
  calc
    Ï s â‰¤ Ï ((s \ â‹ƒ i, f.covering i) âˆª â‹ƒ i, f.covering i) := by
      rw [diff_union_self]; exact Ï.mono <| subset_union_left _ _
    _ â‰¤ Ï (s \ â‹ƒ i, f.covering i) + Ï (â‹ƒ i, f.covering i) := Ï.union _ _
    _ â‰¤ Ï (s \ â‹ƒ i, f.covering i) + âˆ‘' i, Ï (f.covering i) := by gcongr; apply Ï.iUnion
    _ = âˆ‘' i, Ï (f.covering i) := by rw [hÏ f.measure_diff_iUnion, zero_add]

theorem measure_le_tsum_of_absolutelyContinuous (f : FineSubfamilyOn v s) {Ï : Measure X}
    (hÏ : Ï â‰ª Î¼) : Ï s â‰¤ âˆ‘' i, Ï (f.covering i) :=
  f.outerMeasure_le_tsum_of_absolutelyContinuous hÏ

theorem measure_le_tsum (f : FineSubfamilyOn v s) : Î¼ s â‰¤ âˆ‘' i, Î¼ (f.covering i) :=
  f.measure_le_tsum_of_absolutelyContinuous Measure.AbsolutelyContinuous.rfl

def restrOpen (f : FineSubfamilyOn v s) (o : Set X) (ho : IsOpen o) (hsub : s âŠ† o) :
    FineSubfamilyOn v s where
  toFun := fun i â†¦ f i âˆ© {t | t âŠ† o}
  ae_frequently' := f.ae_frequently.mono fun _x hx hxs â†¦ (hx hxs).and_eventually <|
    v.eventually_subset_of_nhds <| ho.mem_nhds <| hsub hxs

theorem tsum_measure_covering_restrOpen_le (f : FineSubfamilyOn v s) {o : Set X} (ho : IsOpen o)
    (hsub : s âŠ† o) : âˆ‘' i, Î¼ ((f.restrOpen o ho hsub).covering i) â‰¤ Î¼ o := by
  rw [â† measure_iUnion (pairwise_disjoint _) (FineSubfamilyOn.measurableSet_covering _)]
  exact measure_mono <| iUnion_subset fun i â†¦ (covering_mem _ i).2

end FineSubfamilyOn

/-- For almost every point `x`,
sufficiently small sets in a Vitali family around `x` have positive measure.

This is a nontrivial result, following from the covering property of Vitali families.
-/
theorem ae_eventually_measure_pos (v : VitaliFamily Î¼) :
    âˆ€áµ x âˆ‚Î¼, âˆ€á¶  t in v.filterAt x, 0 < Î¼ t := by
  set s := {x : X | âˆƒá¶  t in v.filterAt x, Î¼ t = 0}
  suffices Î¼ s â‰¤ 0 by simpa [ae_iff] using this
  set f : v.FineSubfamilyOn s := âŸ¨fun _ â†¦ {t | Î¼ t = 0}, ae_of_all _ fun _ â†¦ idâŸ©
  have : âˆ€ i, Î¼ (f.covering i) = 0 := f.covering_mem
  calc
    Î¼ s â‰¤ âˆ‘' i, Î¼ (f.covering i) := f.measure_le_tsum
    _ = 0 := by simp [this]

-- theorem filterAt_basis_closedBall (x : Î±) :
--     (v.filterAt x).HasBasis (0 < Â·) ({a âˆˆ v.setsAt x | a âŠ† closedBall x Â·}) :=
--   nhds_basis_closedBall.vitaliFamily v

-- theorem mem_filterAt_iff {x : Î±} {s : Set (Set Î±)} :
--     s âˆˆ v.filterAt x â†” âˆƒ Îµ > (0 : â„), âˆ€ a âˆˆ v.setsAt x, a âŠ† closedBall x Îµ â†’ a âˆˆ s := by
--   simp only [(v.filterAt_basis_closedBall x).mem_iff, â† and_imp, subset_def, mem_setOf]

-- theorem eventually_filterAt_iff {x : Î±} {P : Set Î± â†’ Prop} :
--     (âˆ€á¶  a in v.filterAt x, P a) â†” âˆƒ Îµ > (0 : â„), âˆ€ a âˆˆ v.setsAt x, a âŠ† closedBall x Îµ â†’ P a :=
--   v.mem_filterAt_iff

-- theorem tendsto_filterAt_iff {Î¹ : Type*} {l : Filter Î¹} {f : Î¹ â†’ Set Î±} {x : Î±} :
--     Tendsto f l (v.filterAt x) â†”
--       (âˆ€á¶  i in l, f i âˆˆ v.setsAt x) âˆ§ âˆ€ Îµ > (0 : â„), âˆ€á¶  i in l, f i âŠ† closedBall x Îµ := by
--   simp only [filterAt, tendsto_inf, nhds_basis_closedBall.smallSets.tendsto_right_iff,
--     tendsto_principal, and_comm, mem_powerset_iff]

-- theorem eventually_filterAt_subset_closedBall (x : Î±) {Îµ : â„} (hÎµ : 0 < Îµ) :
--     âˆ€á¶  a : Set Î± in v.filterAt x, a âŠ† closedBall x Îµ :=
--   (v.tendsto_filterAt_iff.mp tendsto_id).2 Îµ hÎµ

-- theorem frequently_filterAt_iff {x : Î±} {P : Set Î± â†’ Prop} :
--     (âˆƒá¶  a in v.filterAt x, P a) â†” âˆ€ Îµ > (0 : â„), âˆƒ a âˆˆ v.setsAt x, a âŠ† closedBall x Îµ âˆ§ P a := by
--   simp only [(v.filterAt_basis_closedBall x).frequently_iff, â† and_assoc, subset_def, mem_setOf]


-- namespace FineSubfamilyOn

-- variable {v : VitaliFamily Î¼} {f : Î± â†’ Set (Set Î±)} {s : Set Î±} (h : v.FineSubfamilyOn f s)

-- theorem exists_disjoint_covering_ae :
--     âˆƒ t : Set (Î± Ã— Set Î±),
--       (âˆ€ p : Î± Ã— Set Î±, p âˆˆ t â†’ p.1 âˆˆ s) âˆ§
--       (t.PairwiseDisjoint fun p => p.2) âˆ§
--       (âˆ€ p : Î± Ã— Set Î±, p âˆˆ t â†’ p.2 âˆˆ v.setsAt p.1 âˆ© f p.1) âˆ§
--       Î¼ (s \ â‹ƒ (p : Î± Ã— Set Î±) (_ : p âˆˆ t), p.2) = 0 :=
--   v.covering s (fun x => v.setsAt x âˆ© f x) (fun _ _ => inter_subset_left _ _) h

-- /-- Given `h : v.FineSubfamilyOn f s`, then `h.index` is a set parametrizing a disjoint
-- covering of almost every `s`. -/
-- protected def Index : Type _ :=
--   h.exists_disjoint_covering_ae.choose

-- /-- Given `h : v.FineSubfamilyOn f s`, then `h.covering p` is a set in the family,
-- for `p âˆˆ h.index`, such that these sets form a disjoint covering of almost every `s`. -/
-- protected def covering (h : FineSubfamilyOn v f s) : h.Index â†’ Set Î± :=
--   fun p => p.2

-- theorem index_subset : âˆ€ p : Î± Ã— Set Î±, p âˆˆ h.index â†’ p.1 âˆˆ s :=
--   h.exists_disjoint_covering_ae.choose_spec.1

-- theorem covering_disjoint : h.index.PairwiseDisjoint h.covering :=
--   h.exists_disjoint_covering_ae.choose_spec.2.1

-- theorem covering_disjoint_subtype : Pairwise (Disjoint on fun x : h.index => h.covering x) :=
--   (pairwise_subtype_iff_pairwise_set _ _).2 h.covering_disjoint

-- theorem covering_mem {p : Î± Ã— Set Î±} (hp : p âˆˆ h.index) : h.covering p âˆˆ f p.1 :=
--   (h.exists_disjoint_covering_ae.choose_spec.2.2.1 p hp).2

-- theorem covering_mem_family {p : Î± Ã— Set Î±} (hp : p âˆˆ h.index) : h.covering p âˆˆ v.setsAt p.1 :=
--   (h.exists_disjoint_covering_ae.choose_spec.2.2.1 p hp).1

-- theorem measure_diff_biUnion : Î¼ (s \ â‹ƒ p âˆˆ h.index, h.covering p) = 0 :=
--   h.exists_disjoint_covering_ae.choose_spec.2.2.2

-- theorem index_countable [SecondCountableTopology Î±] : h.index.Countable :=
--   h.covering_disjoint.countable_of_nonempty_interior fun _ hx =>
--     v.nonempty_interior _ _ (h.covering_mem_family hx)

-- end FineSubfamilyOn

end TopologicalSpace

section MetricSpace

open Metric

variable {X : Type*} [MetricSpace X] {m : MeasurableSpace X} {Î¼ : Measure X}

theorem eventually_subset_closedBall (v : VitaliFamily Î¼) (x : X) {Î´ : â„} (hÎ´ : 0 < Î´) :
    âˆ€á¶  t in v.filterAt x, t âŠ† closedBall x Î´ :=
  v.eventually_subset_of_nhds <| closedBall_mem_nhds x hÎ´

/-- One can enlarge a Vitali family by adding to the sets `f x` at `x` all sets which are not
contained in a `Î´`-neighborhood on `x`. This does not change the local filter at a point, but it
can be convenient to get a nicer global behavior. -/
def enlarge (v : VitaliFamily Î¼) (Î´ : â„) (Î´pos : 0 < Î´) : VitaliFamily Î¼ where
  setsAt x := v.setsAt x âˆª { a | MeasurableSet a âˆ§ Â¬a âŠ† closedBall x Î´ }
  measurableSet
    | _, _, .inl h => v.measurableSet _ _ h
    | _, _, .inr h => h.1
  nontrivial x := (v.nontrivial x).mono fun _ â†¦ .inl
  covering s f fset ffine := by
    have hf : âˆ€ x âˆˆ s, âˆƒá¶  t in v.filterAt x, t âˆˆ f x := fun x _ â†¦ by
      refine frequently_inf_principal.2 <| (ffine x).mp ?_
      filter_upwards [eventually_smallSets_subset.2 (closedBall_mem_nhds _ Î´pos)] with t htÎ´ htf
      exact âŸ¨(fset x htf).resolve_right fun h â†¦ h.2 htÎ´, htfâŸ©
    let g : v.FineSubfamilyOn s := âŸ¨f, ae_of_all _ hfâŸ©
    exact âŸ¨g.Index, g.center, g.covering, inferInstance, g.center_mem, g.pairwise_disjoint,
      g.covering_mem, g.measure_diff_iUnionâŸ©

@[simp]
theorem enlarge_filterAt (v : VitaliFamily Î¼) {Î´ : â„} (Î´pos : 0 < Î´) :
    (v.enlarge Î´ Î´pos).filterAt = v.filterAt := by
  ext1 x
  refine set_eventuallyEq_iff_inf_principal.1 <| eventuallyEq_set.2 ?_
  filter_upwards [eventually_smallSets_subset.2 (closedBall_mem_nhds _ Î´pos)] with t ht
  simp [enlarge, ht]
