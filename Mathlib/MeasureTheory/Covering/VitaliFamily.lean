/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Measure.AbsolutelyContinuous

/-!
# Vitali families

On a metric space `X` with a measure `Î¼`, consider for each `x : X` a family of measurable sets with
nonempty interiors, called `setsAt x`. This family is a Vitali family if it satisfies the following
property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `setsAt x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.

This file gives the basic definition of Vitali families. More interesting developments of this
notion are deferred to other files:
* constructions of specific Vitali families are provided by the Besicovitch covering theorem, in
`Besicovitch.vitaliFamily`, and by the Vitali covering theorem, in `Vitali.vitaliFamily`.
* The main theorem on differentiation of measures along a Vitali family is proved in
`VitaliFamily.ae_tendsto_rnDeriv`.

## Main definitions

* `VitaliFamily Î¼` is a structure made, for each `x : X`, of a family of sets around `x`, such that
  one can extract an almost everywhere disjoint covering from any subfamily containing sets of
  arbitrarily small diameters.

Let `v` be such a Vitali family.
* `v.FineSubfamilyOn` describes the subfamilies of `v` from which one can extract almost
  everywhere disjoint coverings. This property, called
  `v.FineSubfamilyOn.exists_disjoint_covering_ae`, is essentially a restatement of the definition
  of a Vitali family. We also provide an API to use efficiently such a disjoint covering.
* `v.filterAt x` is a filter on sets of `X`, such that convergence with respect to this filter
  means convergence when sets in the Vitali family shrink towards `x`.

## References

* [Herbert Federer, Geometric Measure Theory, Chapter 2.8][Federer1996]
  (Vitali families are called Vitali relations there)
-/


open MeasureTheory Metric Set Filter TopologicalSpace MeasureTheory.Measure
open scoped Topology

variable {X : Type*} [PseudoMetricSpace X]

/-- On a metric space `X` with a measure `Î¼`, consider for each `x : X` a family of measurable sets
with nonempty interiors, called `setsAt x`. This family is a Vitali family if it satisfies the
following property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `setsAt x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.
-/
structure VitaliFamily {m : MeasurableSpace X} (Î¼ : Measure X) where
  /-- Sets of the family "centered" at a given point. -/
  setsAt :  X â†’ Set (Set X)
  /-- All sets of the family are measurable. -/
  measurableSet : âˆ€ x : X, âˆ€ s âˆˆ setsAt x, MeasurableSet s
  /-- All sets of the family have nonempty interior. -/
  nonempty_interior : âˆ€ x : X, âˆ€ s âˆˆ setsAt x, (interior s).Nonempty
  /-- For any closed ball around `x`, there exists a set of the family contained in this ball. -/
  nontrivial : âˆ€ (x : X), âˆ€ Îµ > (0 : â„), âˆƒ s âˆˆ setsAt x, s âŠ† closedBall x Îµ
  /-- Consider a (possibly non-measurable) set `s`,
  and for any `x` in `s` a subfamily `f x` of `setsAt x`
  containing sets of arbitrarily small diameter.
  Then one can extract a disjoint subfamily covering almost all `s`. -/
  covering : âˆ€ (s : Set X) (f : X â†’ Set (Set X)),
    (âˆ€ x âˆˆ s, f x âŠ† setsAt x) â†’ (âˆ€ x âˆˆ s, âˆ€ Îµ > (0 : â„), âˆƒ t âˆˆ f x, t âŠ† closedBall x Îµ) â†’
    âˆƒ t : Set (X Ã— Set X), (âˆ€ p âˆˆ t, p.1 âˆˆ s) âˆ§ (t.PairwiseDisjoint fun p â†¦ p.2) âˆ§
      (âˆ€ p âˆˆ t, p.2 âˆˆ f p.1) âˆ§ Î¼ (s \ â‹ƒ p âˆˆ t, p.2) = 0

namespace VitaliFamily

variable {m0 : MeasurableSpace X} {Î¼ : Measure X}

/-- A Vitali family for a measure `Î¼` is also a Vitali family for any measure absolutely continuous
with respect to `Î¼`. -/
def mono (v : VitaliFamily Î¼) (Î½ : Measure X) (hÎ½ : Î½ â‰ª Î¼) : VitaliFamily Î½ where
  __ := v
  covering s f h h' :=
    let âŸ¨t, ts, disj, mem_f, hÎ¼âŸ© := v.covering s f h h'
    âŸ¨t, ts, disj, mem_f, hÎ½ hÎ¼âŸ©

/-- Given a Vitali family `v` for a measure `Î¼`, a family `f` is a fine subfamily on a set `s` if
every point `x` in `s` belongs to arbitrarily small sets in `v.setsAt x âˆ© f x`. This is precisely
the subfamilies for which the Vitali family definition ensures that one can extract a disjoint
covering of almost all `s`. -/
def FineSubfamilyOn (v : VitaliFamily Î¼) (f : X â†’ Set (Set X)) (s : Set X) : Prop :=
  âˆ€ x âˆˆ s, âˆ€ Îµ > 0, âˆƒ t âˆˆ v.setsAt x âˆ© f x, t âŠ† closedBall x Îµ

namespace FineSubfamilyOn

variable {v : VitaliFamily Î¼} {f : X â†’ Set (Set X)} {s : Set X} (h : v.FineSubfamilyOn f s)
include h

theorem exists_disjoint_covering_ae :
    âˆƒ t : Set (X Ã— Set X),
      (âˆ€ p : X Ã— Set X, p âˆˆ t â†’ p.1 âˆˆ s) âˆ§
      (t.PairwiseDisjoint fun p => p.2) âˆ§
      (âˆ€ p : X Ã— Set X, p âˆˆ t â†’ p.2 âˆˆ v.setsAt p.1 âˆ© f p.1) âˆ§
      Î¼ (s \ â‹ƒ (p : X Ã— Set X) (_ : p âˆˆ t), p.2) = 0 :=
  v.covering s (fun x => v.setsAt x âˆ© f x) (fun _ _ => inter_subset_left) h

/-- Given `h : v.FineSubfamilyOn f s`, then `h.index` is a set parametrizing a disjoint
covering of almost every `s`. -/
protected def index : Set (X Ã— Set X) :=
  h.exists_disjoint_covering_ae.choose

/-- Given `h : v.FineSubfamilyOn f s`, then `h.covering p` is a set in the family,
for `p âˆˆ h.index`, such that these sets form a disjoint covering of almost every `s`. -/
@[nolint unusedArguments]
protected def covering (_h : FineSubfamilyOn v f s) : X Ã— Set X â†’ Set X :=
  fun p => p.2

theorem index_subset : âˆ€ p : X Ã— Set X, p âˆˆ h.index â†’ p.1 âˆˆ s :=
  h.exists_disjoint_covering_ae.choose_spec.1

theorem covering_disjoint : h.index.PairwiseDisjoint h.covering :=
  h.exists_disjoint_covering_ae.choose_spec.2.1

open scoped Function in -- required for scoped `on` notation
theorem covering_disjoint_subtype : Pairwise (Disjoint on fun x : h.index => h.covering x) :=
  (pairwise_subtype_iff_pairwise_set _ _).2 h.covering_disjoint

theorem covering_mem {p : X Ã— Set X} (hp : p âˆˆ h.index) : h.covering p âˆˆ f p.1 :=
  (h.exists_disjoint_covering_ae.choose_spec.2.2.1 p hp).2

theorem covering_mem_family {p : X Ã— Set X} (hp : p âˆˆ h.index) : h.covering p âˆˆ v.setsAt p.1 :=
  (h.exists_disjoint_covering_ae.choose_spec.2.2.1 p hp).1

theorem measure_diff_biUnion : Î¼ (s \ â‹ƒ p âˆˆ h.index, h.covering p) = 0 :=
  h.exists_disjoint_covering_ae.choose_spec.2.2.2

theorem index_countable [SecondCountableTopology X] : h.index.Countable :=
  h.covering_disjoint.countable_of_nonempty_interior fun _ hx =>
    v.nonempty_interior _ _ (h.covering_mem_family hx)

protected theorem measurableSet_u {p : X Ã— Set X} (hp : p âˆˆ h.index) :
    MeasurableSet (h.covering p) :=
  v.measurableSet p.1 _ (h.covering_mem_family hp)

theorem measure_le_tsum_of_absolutelyContinuous [SecondCountableTopology X] {Ï : Measure X}
    (hÏ : Ï â‰ª Î¼) : Ï s â‰¤ âˆ‘' p : h.index, Ï (h.covering p) :=
  calc
    Ï s â‰¤ Ï ((s \ â‹ƒ p âˆˆ h.index, h.covering p) âˆª â‹ƒ p âˆˆ h.index, h.covering p) :=
      measure_mono (by simp only [subset_union_left, diff_union_self])
    _ â‰¤ Ï (s \ â‹ƒ p âˆˆ h.index, h.covering p) + Ï (â‹ƒ p âˆˆ h.index, h.covering p) :=
      (measure_union_le _ _)
    _ = âˆ‘' p : h.index, Ï (h.covering p) := by
      rw [hÏ h.measure_diff_biUnion, zero_add,
        measure_biUnion h.index_countable h.covering_disjoint fun x hx => h.measurableSet_u hx]

theorem measure_le_tsum [SecondCountableTopology X] : Î¼ s â‰¤ âˆ‘' x : h.index, Î¼ (h.covering x) :=
  h.measure_le_tsum_of_absolutelyContinuous Measure.AbsolutelyContinuous.rfl

end FineSubfamilyOn

/-- One can enlarge a Vitali family by adding to the sets `f x` at `x` all sets which are not
contained in a `Î´`-neighborhood on `x`. This does not change the local filter at a point, but it
can be convenient to get a nicer global behavior. -/
def enlarge (v : VitaliFamily Î¼) (Î´ : â„) (Î´pos : 0 < Î´) : VitaliFamily Î¼ where
  setsAt x := v.setsAt x âˆª {s | MeasurableSet s âˆ§ (interior s).Nonempty âˆ§ Â¬s âŠ† closedBall x Î´}
  measurableSet := by
    rintro x s (hs | hs)
    exacts [v.measurableSet _ _ hs, hs.1]
  nonempty_interior := by
    rintro x s (hs | hs)
    exacts [v.nonempty_interior _ _ hs, hs.2.1]
  nontrivial := by
    intro x Îµ Îµpos
    rcases v.nontrivial x Îµ Îµpos with âŸ¨s, hs, h'sâŸ©
    exact âŸ¨s, mem_union_left _ hs, h'sâŸ©
  covering := by
    intro s f fset ffine
    let g : X â†’ Set (Set X) := fun x => f x âˆ© v.setsAt x
    have : âˆ€ x âˆˆ s, âˆ€ Îµ : â„, Îµ > 0 â†’ âˆƒ t âˆˆ g x, t âŠ† closedBall x Îµ := by
      intro x hx Îµ Îµpos
      obtain âŸ¨t, tf, htâŸ© : âˆƒ t âˆˆ f x, t âŠ† closedBall x (min Îµ Î´) :=
        ffine x hx (min Îµ Î´) (lt_min Îµpos Î´pos)
      rcases fset x hx tf with (h't | h't)
      Â· exact âŸ¨t, âŸ¨tf, h'tâŸ©, ht.trans (closedBall_subset_closedBall (min_le_left _ _))âŸ©
      Â· refine False.elim (h't.2.2 ?_)
        exact ht.trans (closedBall_subset_closedBall (min_le_right _ _))
    rcases v.covering s g (fun x _ => inter_subset_right) this with âŸ¨t, ts, tdisj, tg, Î¼tâŸ©
    exact âŸ¨t, ts, tdisj, fun p hp => (tg p hp).1, Î¼tâŸ©

variable (v : VitaliFamily Î¼)

/-- Given a vitali family `v`, then `v.filterAt x` is the filter on `Set X` made of those families
that contain all sets of `v.setsAt x` of a sufficiently small diameter. This filter makes it
possible to express limiting behavior when sets in `v.setsAt x` shrink to `x`. -/
def filterAt (x : X) : Filter (Set X) := (ğ“ x).smallSets âŠ“ ğ“Ÿ (v.setsAt x)

theorem _root_.Filter.HasBasis.vitaliFamily {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} {x : X}
    (h : (ğ“ x).HasBasis p s) : (v.filterAt x).HasBasis p (fun i â†¦ {t âˆˆ v.setsAt x | t âŠ† s i}) := by
  simpa only [â† Set.setOf_inter_eq_sep] using h.smallSets.inf_principal _

theorem filterAt_basis_closedBall (x : X) :
    (v.filterAt x).HasBasis (0 < Â·) ({t âˆˆ v.setsAt x | t âŠ† closedBall x Â·}) :=
  nhds_basis_closedBall.vitaliFamily v

theorem mem_filterAt_iff {x : X} {s : Set (Set X)} :
    s âˆˆ v.filterAt x â†” âˆƒ Îµ > (0 : â„), âˆ€ t âˆˆ v.setsAt x, t âŠ† closedBall x Îµ â†’ t âˆˆ s := by
  simp only [(v.filterAt_basis_closedBall x).mem_iff, â† and_imp, subset_def, mem_setOf]

instance filterAt_neBot (x : X) : (v.filterAt x).NeBot :=
  (v.filterAt_basis_closedBall x).neBot_iff.2 <| v.nontrivial _ _

theorem eventually_filterAt_iff {x : X} {P : Set X â†’ Prop} :
    (âˆ€á¶  t in v.filterAt x, P t) â†” âˆƒ Îµ > (0 : â„), âˆ€ t âˆˆ v.setsAt x, t âŠ† closedBall x Îµ â†’ P t :=
  v.mem_filterAt_iff

theorem tendsto_filterAt_iff {Î¹ : Type*} {l : Filter Î¹} {f : Î¹ â†’ Set X} {x : X} :
    Tendsto f l (v.filterAt x) â†”
      (âˆ€á¶  i in l, f i âˆˆ v.setsAt x) âˆ§ âˆ€ Îµ > (0 : â„), âˆ€á¶  i in l, f i âŠ† closedBall x Îµ := by
  simp only [filterAt, tendsto_inf, nhds_basis_closedBall.smallSets.tendsto_right_iff,
    tendsto_principal, and_comm, mem_powerset_iff]

theorem eventually_filterAt_mem_setsAt (x : X) : âˆ€á¶  t in v.filterAt x, t âˆˆ v.setsAt x :=
  (v.tendsto_filterAt_iff.mp tendsto_id).1

theorem eventually_filterAt_subset_closedBall (x : X) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆ€á¶  t : Set X in v.filterAt x, t âŠ† closedBall x Îµ :=
  (v.tendsto_filterAt_iff.mp tendsto_id).2 Îµ hÎµ

theorem eventually_filterAt_measurableSet (x : X) : âˆ€á¶  t in v.filterAt x, MeasurableSet t := by
  filter_upwards [v.eventually_filterAt_mem_setsAt x] with _ ha using v.measurableSet _ _ ha

theorem frequently_filterAt_iff {x : X} {P : Set X â†’ Prop} :
    (âˆƒá¶  t in v.filterAt x, P t) â†” âˆ€ Îµ > (0 : â„), âˆƒ t âˆˆ v.setsAt x, t âŠ† closedBall x Îµ âˆ§ P t := by
  simp only [(v.filterAt_basis_closedBall x).frequently_iff, â† and_assoc, subset_def, mem_setOf]

theorem eventually_filterAt_subset_of_nhds {x : X} {o : Set X} (hx : o âˆˆ ğ“ x) :
    âˆ€á¶  t in v.filterAt x, t âŠ† o :=
  (eventually_smallSets_subset.2 hx).filter_mono inf_le_left

theorem fineSubfamilyOn_of_frequently (v : VitaliFamily Î¼) (f : X â†’ Set (Set X)) (s : Set X)
    (h : âˆ€ x âˆˆ s, âˆƒá¶  t in v.filterAt x, t âˆˆ f x) : v.FineSubfamilyOn f s := by
  intro x hx Îµ Îµpos
  obtain âŸ¨t, tv, ht, tfâŸ© : âˆƒ t âˆˆ v.setsAt x, t âŠ† closedBall x Îµ âˆ§ t âˆˆ f x :=
    v.frequently_filterAt_iff.1 (h x hx) Îµ Îµpos
  exact âŸ¨t, âŸ¨tv, tfâŸ©, htâŸ©

end VitaliFamily
