/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic
import Mathlib.MeasureTheory.Covering.VitaliFamily
import Mathlib.Data.Set.Pairwise.Lattice

#align_import measure_theory.covering.vitali from "leanprover-community/mathlib"@"bf6a01357ff5684b1ebcd0f1a13be314fc82c0bf"

/-!
# Vitali covering theorems

The topological Vitali covering theorem, in its most classical version, states the following.
Consider a family of balls `(B (x_i, r_i))_{i âˆˆ I}` in a metric space, with uniformly bounded
radii. Then one can extract a disjoint subfamily indexed by `J âŠ† I`, such that any `B (x_i, r_i)`
is included in a ball `B (x_j, 5 r_j)`.

We prove this theorem in `Vitali.exists_disjoint_subfamily_covering_enlargment_closedBall`.
It is deduced from a more general version, called
`Vitali.exists_disjoint_subfamily_covering_enlargment`, which applies to any family of sets
together with a size function `Î´` (think "radius" or "diameter").

We deduce the measurable Vitali covering theorem. Assume one is given a family `t` of closed sets
with nonempty interior, such that each `a âˆˆ t` is included in a ball `B (x, r)` and covers a
definite proportion of the ball `B (x, 6 r)` for a given measure `Î¼` (think of the situation
where `Î¼` is a doubling measure and `t` is a family of balls). Consider a set `s` at which the
family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`. Then one
can extract from `t` a disjoint subfamily that covers almost all `s`. It is proved in
`Vitali.exists_disjoint_covering_ae`.

A way to restate this theorem is to say that the set of closed sets `a` with nonempty interior
covering a fixed proportion `1/C` of the ball `closedBall x (3 * diam a)` forms a Vitali family.
This version is given in `Vitali.vitaliFamily`.
-/


variable {Î± Î¹ : Type*}

open Set Metric MeasureTheory TopologicalSpace Filter
open scoped NNReal Classical ENNReal Topology

namespace Vitali

/-- **Vitali covering theorem**: given a set `t` of subsets of a type, one may extract a disjoint
subfamily `u` such that the `Ï„`-enlargment of this family covers all elements of `t`, where `Ï„ > 1`
is any fixed number.

When `t` is a family of balls, the `Ï„`-enlargment of `ball x r` is `ball x ((1+2Ï„) r)`. In general,
it is expressed in terms of a function `Î´` (think "radius" or "diameter"), positive and bounded on
all elements of `t`. The condition is that every element `a` of `t` should intersect an
element `b` of `u` of size larger than that of `a` up to `Ï„`, i.e., `Î´ b â‰¥ Î´ a / Ï„`.

We state the lemma slightly more generally, with an indexed family of sets `B a` for `a âˆˆ t`, for
wider applicability.
-/
theorem exists_disjoint_subfamily_covering_enlargment (B : Î¹ â†’ Set Î±) (t : Set Î¹) (Î´ : Î¹ â†’ â„)
    (Ï„ : â„) (hÏ„ : 1 < Ï„) (Î´nonneg : âˆ€ a âˆˆ t, 0 â‰¤ Î´ a) (R : â„) (Î´le : âˆ€ a âˆˆ t, Î´ a â‰¤ R)
    (hne : âˆ€ a âˆˆ t, (B a).Nonempty) :
    âˆƒ u âŠ† t,
      u.PairwiseDisjoint B âˆ§ âˆ€ a âˆˆ t, âˆƒ b âˆˆ u, (B a âˆ© B b).Nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ b := by
  /- The proof could be formulated as a transfinite induction. First pick an element of `t` with `Î´`
  as large as possible (up to a factor of `Ï„`). Then among the remaining elements not intersecting
  the already chosen one, pick another element with large `Î´`. Go on forever (transfinitely) until
  there is nothing left.

  Instead, we give a direct Zorn-based argument. Consider a maximal family `u` of disjoint sets
  with the following property: if an element `a` of `t` intersects some element `b` of `u`, then it
  intersects some `b' âˆˆ u` with `Î´ b' â‰¥ Î´ a / Ï„`. Such a maximal family exists by Zorn. If this
  family did not intersect some element `a âˆˆ t`, then take an element `a' âˆˆ t` which does not
  intersect any element of `u`, with `Î´ a'` almost as large as possible. One checks easily
  that `u âˆª {a'}` still has this property, contradicting the maximality. Therefore, `u`
  intersects all elements of `t`, and by definition it satisfies all the desired properties.
  -/
  let T : Set (Set Î¹) := { u | u âŠ† t âˆ§ u.PairwiseDisjoint B âˆ§
    âˆ€ a âˆˆ t, âˆ€ b âˆˆ u, (B a âˆ© B b).Nonempty â†’ âˆƒ c âˆˆ u, (B a âˆ© B c).Nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ c }
  -- By Zorn, choose a maximal family in the good set `T` of disjoint families.
  obtain âŸ¨u, uT, huâŸ© : âˆƒ u âˆˆ T, âˆ€ v âˆˆ T, u âŠ† v â†’ v = u := by
    refine zorn_subset _ fun U UT hU => ?_
    refine âŸ¨â‹ƒâ‚€ U, ?_, fun s hs => subset_sUnion_of_mem hsâŸ©
    simp only [T, Set.sUnion_subset_iff, and_imp, exists_prop, forall_exists_index, mem_sUnion,
      Set.mem_setOf_eq]
    refine
      âŸ¨fun u hu => (UT hu).1, (pairwiseDisjoint_sUnion hU.directedOn).2 fun u hu => (UT hu).2.1,
        fun a hat b u uU hbu hab => ?_âŸ©
    obtain âŸ¨c, cu, ac, hcâŸ© : âˆƒ c, c âˆˆ u âˆ§ (B a âˆ© B c).Nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ c :=
      (UT uU).2.2 a hat b hbu hab
    exact âŸ¨c, âŸ¨u, uU, cuâŸ©, ac, hcâŸ©
  -- The only nontrivial bit is to check that every `a âˆˆ t` intersects an element `b âˆˆ u` with
  -- comparatively large `Î´ b`. Assume this is not the case, then we will contradict the maximality.
  refine âŸ¨u, uT.1, uT.2.1, fun a hat => ?_âŸ©
  contrapose! hu
  have a_disj : âˆ€ c âˆˆ u, Disjoint (B a) (B c) := by
    intro c hc
    by_contra h
    rw [not_disjoint_iff_nonempty_inter] at h
    obtain âŸ¨d, du, ad, hdâŸ© : âˆƒ d, d âˆˆ u âˆ§ (B a âˆ© B d).Nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ d :=
      uT.2.2 a hat c hc h
    exact lt_irrefl _ ((hu d du ad).trans_le hd)
  -- Let `A` be all the elements of `t` which do not intersect the family `u`. It is nonempty as it
  -- contains `a`. We will pick an element `a'` of `A` with `Î´ a'` almost as large as possible.
  let A := { a' | a' âˆˆ t âˆ§ âˆ€ c âˆˆ u, Disjoint (B a') (B c) }
  have Anonempty : A.Nonempty := âŸ¨a, hat, a_disjâŸ©
  let m := sSup (Î´ '' A)
  have bddA : BddAbove (Î´ '' A) := by
    refine âŸ¨R, fun x xA => ?_âŸ©
    rcases (mem_image _ _ _).1 xA with âŸ¨a', ha', rflâŸ©
    exact Î´le a' ha'.1
  obtain âŸ¨a', a'A, ha'âŸ© : âˆƒ a' âˆˆ A, m / Ï„ â‰¤ Î´ a' := by
    have : 0 â‰¤ m := (Î´nonneg a hat).trans (le_csSup bddA (mem_image_of_mem _ âŸ¨hat, a_disjâŸ©))
    rcases eq_or_lt_of_le this with (mzero | mpos)
    Â· refine âŸ¨a, âŸ¨hat, a_disjâŸ©, ?_âŸ©
      simpa only [â† mzero, zero_div] using Î´nonneg a hat
    Â· have I : m / Ï„ < m := by
        rw [div_lt_iff (zero_lt_one.trans hÏ„)]
        conv_lhs => rw [â† mul_one m]
        exact (mul_lt_mul_left mpos).2 hÏ„
      rcases exists_lt_of_lt_csSup (Anonempty.image _) I with âŸ¨x, xA, hxâŸ©
      rcases (mem_image _ _ _).1 xA with âŸ¨a', ha', rflâŸ©
      exact âŸ¨a', ha', hx.leâŸ©
  clear hat hu a_disj a
  have a'_ne_u : a' âˆ‰ u := fun H => (hne _ a'A.1).ne_empty (disjoint_self.1 (a'A.2 _ H))
  -- we claim that `u âˆª {a'}` still belongs to `T`, contradicting the maximality of `u`.
  refine âŸ¨insert a' u, âŸ¨?_, ?_, ?_âŸ©, subset_insert _ _, (ne_insert_of_not_mem _ a'_ne_u).symmâŸ©
  Â· -- check that `u âˆª {a'}` is made of elements of `t`.
    rw [insert_subset_iff]
    exact âŸ¨a'A.1, uT.1âŸ©
  Â· -- Check that `u âˆª {a'}` is a disjoint family. This follows from the fact that `a'` does not
    -- intersect `u`.
    exact uT.2.1.insert fun b bu _ => a'A.2 b bu
  Â· -- check that every element `c` of `t` intersecting `u âˆª {a'}` intersects an element of this
    -- family with large `Î´`.
    intro c ct b ba'u hcb
    -- if `c` already intersects an element of `u`, then it intersects an element of `u` with
    -- large `Î´` by the assumption on `u`, and there is nothing left to do.
    by_cases H : âˆƒ d âˆˆ u, (B c âˆ© B d).Nonempty
    Â· rcases H with âŸ¨d, du, hdâŸ©
      rcases uT.2.2 c ct d du hd with âŸ¨d', d'u, hd'âŸ©
      exact âŸ¨d', mem_insert_of_mem _ d'u, hd'âŸ©
    Â· -- Otherwise, `c` belongs to `A`. The element of `u âˆª {a'}` that it intersects has to be `a'`.
      -- Moreover, `Î´ c` is smaller than the maximum `m` of `Î´` over `A`, which is `â‰¤ Î´ a' / Ï„`
      -- thanks to the good choice of `a'`. This is the desired inequality.
      push_neg at H
      simp only [â† disjoint_iff_inter_eq_empty] at H
      rcases mem_insert_iff.1 ba'u with (rfl | H')
      Â· refine âŸ¨b, mem_insert _ _, hcb, ?_âŸ©
        calc
          Î´ c â‰¤ m := le_csSup bddA (mem_image_of_mem _ âŸ¨ct, HâŸ©)
          _ = Ï„ * (m / Ï„) := by field_simp [(zero_lt_one.trans hÏ„).ne']
          _ â‰¤ Ï„ * Î´ b := by gcongr
      Â· rw [â† not_disjoint_iff_nonempty_inter] at hcb
        exact (hcb (H _ H')).elim
#align vitali.exists_disjoint_subfamily_covering_enlargment Vitali.exists_disjoint_subfamily_covering_enlargment

/-- Vitali covering theorem, closed balls version: given a family `t` of closed balls, one can
extract a disjoint subfamily `u âŠ† t` so that all balls in `t` are covered by the 5-times
dilations of balls in `u`. -/
theorem exists_disjoint_subfamily_covering_enlargment_closedBall [MetricSpace Î±] (t : Set Î¹)
    (x : Î¹ â†’ Î±) (r : Î¹ â†’ â„) (R : â„) (hr : âˆ€ a âˆˆ t, r a â‰¤ R) :
    âˆƒ u âŠ† t,
      (u.PairwiseDisjoint fun a => closedBall (x a) (r a)) âˆ§
        âˆ€ a âˆˆ t, âˆƒ b âˆˆ u, closedBall (x a) (r a) âŠ† closedBall (x b) (5 * r b) := by
  rcases eq_empty_or_nonempty t with (rfl | _)
  Â· exact âŸ¨âˆ…, Subset.refl _, pairwiseDisjoint_empty, by simpâŸ©
  by_cases ht : âˆ€ a âˆˆ t, r a < 0
  Â· exact âŸ¨t, Subset.rfl, fun a ha b _ _ => by
      -- Adaptation note: nightly-2024-03-16
      -- Previously `Function.onFun` unfolded in the following `simp only`,
      -- but now needs a separate `rw`.
      -- This may be a bug: a no import minimization may be required.
      rw [Function.onFun]
      simp only [Function.onFun, closedBall_eq_empty.2 (ht a ha), empty_disjoint],
      fun a ha => âŸ¨a, ha, by simp only [closedBall_eq_empty.2 (ht a ha), empty_subset]âŸ©âŸ©
  push_neg at ht
  let t' := { a âˆˆ t | 0 â‰¤ r a }
  rcases exists_disjoint_subfamily_covering_enlargment (fun a => closedBall (x a) (r a)) t' r 2
      one_lt_two (fun a ha => ha.2) R (fun a ha => hr a ha.1) fun a ha =>
      âŸ¨x a, mem_closedBall_self ha.2âŸ© with
    âŸ¨u, ut', u_disj, huâŸ©
  have A : âˆ€ a âˆˆ t', âˆƒ b âˆˆ u, closedBall (x a) (r a) âŠ† closedBall (x b) (5 * r b) := by
    intro a ha
    rcases hu a ha with âŸ¨b, bu, hb, rbâŸ©
    refine âŸ¨b, bu, ?_âŸ©
    have : dist (x a) (x b) â‰¤ r a + r b := dist_le_add_of_nonempty_closedBall_inter_closedBall hb
    apply closedBall_subset_closedBall'
    linarith
  refine âŸ¨u, ut'.trans fun a ha => ha.1, u_disj, fun a ha => ?_âŸ©
  rcases le_or_lt 0 (r a) with (h'a | h'a)
  Â· exact A a âŸ¨ha, h'aâŸ©
  Â· rcases ht with âŸ¨b, rbâŸ©
    rcases A b âŸ¨rb.1, rb.2âŸ© with âŸ¨c, cu, _âŸ©
    exact âŸ¨c, cu, by simp only [closedBall_eq_empty.2 h'a, empty_subset]âŸ©
#align vitali.exists_disjoint_subfamily_covering_enlargment_closed_ball Vitali.exists_disjoint_subfamily_covering_enlargment_closedBall

/-- The measurable Vitali covering theorem. Assume one is given a family `t` of closed sets with
nonempty interior, such that each `a âˆˆ t` is included in a ball `B (x, r)` and covers a definite
proportion of the ball `B (x, 3 r)` for a given measure `Î¼` (think of the situation where `Î¼` is
a doubling measure and `t` is a family of balls). Consider a (possibly non-measurable) set `s`
at which the family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`.
Then one can extract from `t` a disjoint subfamily that covers almost all `s`.

For more flexibility, we give a statement with a parameterized family of sets.
-/
theorem exists_disjoint_covering_ae [MetricSpace Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±]
    [SecondCountableTopology Î±] (Î¼ : Measure Î±) [IsLocallyFiniteMeasure Î¼] (s : Set Î±) (t : Set Î¹)
    (C : â„â‰¥0) (r : Î¹ â†’ â„) (c : Î¹ â†’ Î±) (B : Î¹ â†’ Set Î±) (hB : âˆ€ a âˆˆ t, B a âŠ† closedBall (c a) (r a))
    (Î¼B : âˆ€ a âˆˆ t, Î¼ (closedBall (c a) (3 * r a)) â‰¤ C * Î¼ (B a))
    (ht : âˆ€ a âˆˆ t, (interior (B a)).Nonempty) (h't : âˆ€ a âˆˆ t, IsClosed (B a))
    (hf : âˆ€ x âˆˆ s, âˆ€ Îµ > (0 : â„), âˆƒ a âˆˆ t, r a â‰¤ Îµ âˆ§ c a = x) :
    âˆƒ u âŠ† t, u.Countable âˆ§ u.PairwiseDisjoint B âˆ§ Î¼ (s \ â‹ƒ a âˆˆ u, B a) = 0 := by
  /- The idea of the proof is the following. Assume for simplicity that `Î¼` is finite. Applying the
  abstract Vitali covering theorem with `Î´ = r` given by `hf`, one obtains a disjoint subfamily `u`,
  such that any element of `t` intersects an element of `u` with comparable radius. Fix `Îµ > 0`.
  Since the elements of `u` have summable measure, one can remove finitely elements `w_1, ..., w_n`.
  so that the measure of the remaining elements is `< Îµ`. Consider now a point `z` not
  in the `w_i`. There is a small ball around `z` not intersecting the `w_i` (as they are closed),
  an element `a âˆˆ t` contained in this small ball (as the family `t` is fine at `z`) and an element
  `b âˆˆ u` intersecting `a`, with comparable radius (by definition of `u`). Then `z` belongs to the
  enlargement of `b`. This shows that `s \ (w_1 âˆª ... âˆª w_n)` is contained in
  `â‹ƒ (b âˆˆ u \ {w_1, ... w_n}) (enlargement of b)`. The measure of the latter set is bounded by
  `âˆ‘ (b âˆˆ u \ {w_1, ... w_n}) C * Î¼ b` (by the doubling property of the measure), which is at most
  `C Îµ`. Letting `Îµ` tend to `0` shows that `s` is almost everywhere covered by the family `u`.

  For the real argument, the measure is only locally finite. Therefore, we implement the same
  strategy, but locally restricted to balls on which the measure is finite. For this, we do not
  use the whole family `t`, but a subfamily `t'` supported on small balls (which is possible since
  the family is assumed to be fine at every point of `s`).
  -/
  -- choose around each `x` a small ball on which the measure is finite
  have : âˆ€ x, âˆƒ R, 0 < R âˆ§ R â‰¤ 1 âˆ§ Î¼ (closedBall x (20 * R)) < âˆ := fun x â†¦ by
    refine ((eventually_le_nhds one_pos).and ?_).exists_gt
    refine (tendsto_closedBall_smallSets x).comp ?_ (Î¼.finiteAt_nhds x).eventually
    exact Continuous.tendsto' (by fun_prop) _ _ (mul_zero _)
  choose R hR0 hR1 hRÎ¼ using this
  -- we restrict to a subfamily `t'` of `t`, made of elements small enough to ensure that
  -- they only see a finite part of the measure, and with a doubling property
  let t' := { a âˆˆ t | r a â‰¤ R (c a) }
  -- extract a disjoint subfamily `u` of `t'` thanks to the abstract Vitali covering theorem.
  obtain âŸ¨u, ut', u_disj, huâŸ© : âˆƒ u âŠ† t',
      u.PairwiseDisjoint B âˆ§ âˆ€ a âˆˆ t', âˆƒ b âˆˆ u, (B a âˆ© B b).Nonempty âˆ§ r a â‰¤ 2 * r b := by
    have A : âˆ€ a âˆˆ t', r a â‰¤ 1 := by
      intro a ha
      apply ha.2.trans (hR1 (c a))
    have A' : âˆ€ a âˆˆ t', (B a).Nonempty :=
      fun a hat' => Set.Nonempty.mono interior_subset (ht a hat'.1)
    refine exists_disjoint_subfamily_covering_enlargment B t' r 2 one_lt_two (fun a ha => ?_) 1 A A'
    exact nonempty_closedBall.1 ((A' a ha).mono (hB a ha.1))
  have ut : u âŠ† t := fun a hau => (ut' hau).1
  -- As the space is second countable, the family is countable since all its sets have nonempty
  -- interior.
  have u_count : u.Countable := u_disj.countable_of_nonempty_interior fun a ha => ht a (ut ha)
  -- the family `u` will be the desired family
  refine âŸ¨u, fun a hat' => (ut' hat').1, u_count, u_disj, ?_âŸ©
  -- it suffices to show that it covers almost all `s` locally around each point `x`.
  refine measure_null_of_locally_null _ fun x _ => ?_
  -- let `v` be the subfamily of `u` made of those sets intersecting the small ball `ball x (r x)`
  let v := { a âˆˆ u | (B a âˆ© ball x (R x)).Nonempty }
  have vu : v âŠ† u := fun a ha => ha.1
  -- they are all contained in a fixed ball of finite measure, thanks to our choice of `t'`
  obtain âŸ¨K, Î¼K, hKâŸ© : âˆƒ K, Î¼ (closedBall x K) < âˆ âˆ§
      âˆ€ a âˆˆ u, (B a âˆ© ball x (R x)).Nonempty â†’ B a âŠ† closedBall x K := by
    have Idist_v : âˆ€ a âˆˆ v, dist (c a) x â‰¤ r a + R x := by
      intro a hav
      apply dist_le_add_of_nonempty_closedBall_inter_closedBall
      refine hav.2.mono ?_
      apply inter_subset_inter _ ball_subset_closedBall
      exact hB a (ut (vu hav))
    set R0 := sSup (r '' v) with R0_def
    have R0_bdd : BddAbove (r '' v) := by
      refine âŸ¨1, fun r' hr' => ?_âŸ©
      rcases (mem_image _ _ _).1 hr' with âŸ¨b, hb, rflâŸ©
      exact le_trans (ut' (vu hb)).2 (hR1 (c b))
    rcases le_total R0 (R x) with (H | H)
    Â· refine âŸ¨20 * R x, hRÎ¼ x, fun a au hax => ?_âŸ©
      refine (hB a (ut au)).trans ?_
      apply closedBall_subset_closedBall'
      have : r a â‰¤ R0 := le_csSup R0_bdd (mem_image_of_mem _ âŸ¨au, haxâŸ©)
      linarith [Idist_v a âŸ¨au, haxâŸ©, hR0 x]
    Â· have R0pos : 0 < R0 := (hR0 x).trans_le H
      have vnonempty : v.Nonempty := by
        by_contra h
        rw [nonempty_iff_ne_empty, Classical.not_not] at h
        rw [h, image_empty, Real.sSup_empty] at R0_def
        exact lt_irrefl _ (R0pos.trans_le (le_of_eq R0_def))
      obtain âŸ¨a, hav, R0aâŸ© : âˆƒ a âˆˆ v, R0 / 2 < r a := by
        obtain âŸ¨r', r'mem, hr'âŸ© : âˆƒ r' âˆˆ r '' v, R0 / 2 < r' :=
          exists_lt_of_lt_csSup (vnonempty.image _) (half_lt_self R0pos)
        rcases (mem_image _ _ _).1 r'mem with âŸ¨a, hav, rflâŸ©
        exact âŸ¨a, hav, hr'âŸ©
      refine âŸ¨8 * R0, ?_, ?_âŸ©
      Â· apply lt_of_le_of_lt (measure_mono _) (hRÎ¼ (c a))
        apply closedBall_subset_closedBall'
        rw [dist_comm]
        linarith [Idist_v a hav, (ut' (vu hav)).2]
      Â· intro b bu hbx
        refine (hB b (ut bu)).trans ?_
        apply closedBall_subset_closedBall'
        have : r b â‰¤ R0 := le_csSup R0_bdd (mem_image_of_mem _ âŸ¨bu, hbxâŸ©)
        linarith [Idist_v b âŸ¨bu, hbxâŸ©]
  -- we will show that, in `ball x (R x)`, almost all `s` is covered by the family `u`.
  refine âŸ¨_ âˆ© ball x (R x), inter_mem_nhdsWithin _ (ball_mem_nhds _ (hR0 _)),
    nonpos_iff_eq_zero.mp (le_of_forall_le_of_dense fun Îµ Îµpos => ?_)âŸ©
  -- the elements of `v` are disjoint and all contained in a finite volume ball, hence the sum
  -- of their measures is finite.
  have I : (âˆ‘' a : v, Î¼ (B a)) < âˆ := by
    calc
      (âˆ‘' a : v, Î¼ (B a)) = Î¼ (â‹ƒ a âˆˆ v, B a) := by
        rw [measure_biUnion (u_count.mono vu) _ fun a ha => (h't _ (vu.trans ut ha)).measurableSet]
        exact u_disj.subset vu
      _ â‰¤ Î¼ (closedBall x K) := (measure_mono (iUnionâ‚‚_subset fun a ha => hK a (vu ha) ha.2))
      _ < âˆ := Î¼K
  -- we can obtain a finite subfamily of `v`, such that the measures of the remaining elements
  -- add up to an arbitrarily small number, say `Îµ / C`.
  obtain âŸ¨w, hwâŸ© : âˆƒ w : Finset v, (âˆ‘' a : { a // a âˆ‰ w }, Î¼ (B a)) < Îµ / C :=
    haveI : 0 < Îµ / C := by
      simp only [ENNReal.div_pos_iff, Îµpos.ne', ENNReal.coe_ne_top, Ne, not_false_iff,
        and_self_iff]
    ((tendsto_order.1 (ENNReal.tendsto_tsum_compl_atTop_zero I.ne)).2 _ this).exists
  -- main property: the points `z` of `s` which are not covered by `u` are contained in the
  -- enlargements of the elements not in `w`.
  have M : (s \ â‹ƒ a âˆˆ u, B a) âˆ©
      ball x (R x) âŠ† â‹ƒ a : { a // a âˆ‰ w }, closedBall (c a) (3 * r a) := by
    intro z hz
    set k := â‹ƒ (a : v) (_ : a âˆˆ w), B a
    have k_closed : IsClosed k := isClosed_biUnion_finset fun i _ => h't _ (ut (vu i.2))
    have z_notmem_k : z âˆ‰ k := by
      simp only [k, not_exists, exists_prop, mem_iUnion, mem_sep_iff, forall_exists_index,
        SetCoe.exists, not_and, exists_and_right, Subtype.coe_mk]
      intro b hbv _ h'z
      have : z âˆˆ (s \ â‹ƒ a âˆˆ u, B a) âˆ© â‹ƒ a âˆˆ u, B a :=
        mem_inter (mem_of_mem_inter_left hz) (mem_biUnion (vu hbv) h'z)
      simpa only [diff_inter_self]
    -- since the elements of `w` are closed and finitely many, one can find a small ball around `z`
    -- not intersecting them
    have : ball x (R x) \ k âˆˆ ğ“ z := by
      apply IsOpen.mem_nhds (isOpen_ball.sdiff k_closed) _
      exact (mem_diff _).2 âŸ¨mem_of_mem_inter_right hz, z_notmem_kâŸ©
    obtain âŸ¨d, dpos, hdâŸ© : âˆƒ d, 0 < d âˆ§ closedBall z d âŠ† ball x (R x) \ k :=
      nhds_basis_closedBall.mem_iff.1 this
    -- choose an element `a` of the family `t` contained in this small ball
    obtain âŸ¨a, hat, ad, rflâŸ© : âˆƒ a âˆˆ t, r a â‰¤ min d (R z) âˆ§ c a = z :=
      hf z ((mem_diff _).1 (mem_of_mem_inter_left hz)).1 (min d (R z)) (lt_min dpos (hR0 z))
    have ax : B a âŠ† ball x (R x) := by
      refine (hB a hat).trans ?_
      refine Subset.trans ?_ (hd.trans (diff_subset (ball x (R x)) k))
      exact closedBall_subset_closedBall (ad.trans (min_le_left _ _))
    -- it intersects an element `b` of `u` with comparable diameter, by definition of `u`
    obtain âŸ¨b, bu, ab, bdiamâŸ© : âˆƒ b âˆˆ u, (B a âˆ© B b).Nonempty âˆ§ r a â‰¤ 2 * r b :=
      hu a âŸ¨hat, ad.trans (min_le_right _ _)âŸ©
    have bv : b âˆˆ v := by
      refine âŸ¨bu, ab.mono ?_âŸ©
      rw [inter_comm]
      exact inter_subset_inter_right _ ax
    let b' : v := âŸ¨b, bvâŸ©
    -- `b` cannot belong to `w`, as the elements of `w` do not intersect `closedBall z d`,
    -- contrary to `b`
    have b'_notmem_w : b' âˆ‰ w := by
      intro b'w
      have b'k : B b' âŠ† k := @Finset.subset_set_biUnion_of_mem _ _ _ (fun y : v => B y) _ b'w
      have : (ball x (R x) \ k âˆ© k).Nonempty := by
        apply ab.mono (inter_subset_inter _ b'k)
        refine ((hB _ hat).trans ?_).trans hd
        exact closedBall_subset_closedBall (ad.trans (min_le_left _ _))
      simpa only [diff_inter_self, Set.not_nonempty_empty]
    let b'' : { a // a âˆ‰ w } := âŸ¨b', b'_notmem_wâŸ©
    -- since `a` and `b` have comparable diameters, it follows that `z` belongs to the
    -- enlargement of `b`
    have zb : c a âˆˆ closedBall (c b) (3 * r b) := by
      rcases ab with âŸ¨e, âŸ¨ea, ebâŸ©âŸ©
      have A : dist (c a) e â‰¤ r a := mem_closedBall'.1 (hB a hat ea)
      have B : dist e (c b) â‰¤ r b := mem_closedBall.1 (hB b (ut bu) eb)
      simp only [mem_closedBall]
      linarith only [dist_triangle (c a) e (c b), A, B, bdiam]
    suffices H : closedBall (c b'') (3 * r b'')
        âŠ† â‹ƒ a : { a // a âˆ‰ w }, closedBall (c a) (3 * r a) from H zb
    exact subset_iUnion (fun a : { a // a âˆ‰ w } => closedBall (c a) (3 * r a)) b''
  -- now that we have proved our main inclusion, we can use it to estimate the measure of the points
  -- in `ball x (r x)` not covered by `u`.
  haveI : Countable v := (u_count.mono vu).to_subtype
  calc
    Î¼ ((s \ â‹ƒ a âˆˆ u, B a) âˆ© ball x (R x)) â‰¤ Î¼ (â‹ƒ a : { a // a âˆ‰ w }, closedBall (c a) (3 * r a)) :=
      measure_mono M
    _ â‰¤ âˆ‘' a : { a // a âˆ‰ w }, Î¼ (closedBall (c a) (3 * r a)) := measure_iUnion_le _
    _ â‰¤ âˆ‘' a : { a // a âˆ‰ w }, C * Î¼ (B a) := (ENNReal.tsum_le_tsum fun a => Î¼B a (ut (vu a.1.2)))
    _ = C * âˆ‘' a : { a // a âˆ‰ w }, Î¼ (B a) := ENNReal.tsum_mul_left
    _ â‰¤ C * (Îµ / C) := by gcongr
    _ â‰¤ Îµ := ENNReal.mul_div_le
#align vitali.exists_disjoint_covering_ae Vitali.exists_disjoint_covering_ae

/-- Assume that around every point there are arbitrarily small scales at which the measure is
doubling. Then the set of closed sets `a` with nonempty interior contained in `closedBall x r` and
covering a fixed proportion `1/C` of the ball `closedBall x (3 * r)` forms a Vitali family.
This is essentially a restatement of the measurable Vitali theorem. -/
protected def vitaliFamily [MetricSpace Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±]
    [SecondCountableTopology Î±] (Î¼ : Measure Î±) [IsLocallyFiniteMeasure Î¼] (C : â„â‰¥0)
    (h : âˆ€ x, âˆƒá¶  r in ğ“[>] 0, Î¼ (closedBall x (3 * r)) â‰¤ C * Î¼ (closedBall x r)) :
    VitaliFamily Î¼ where
  setsAt x := { a | IsClosed a âˆ§ (interior a).Nonempty âˆ§
    âˆƒ r, a âŠ† closedBall x r âˆ§ Î¼ (closedBall x (3 * r)) â‰¤ C * Î¼ a }
  measurableSet x a ha := ha.1.measurableSet
  nonempty_interior x a ha := ha.2.1
  nontrivial x Îµ Îµpos := by
    obtain âŸ¨r, Î¼r, rpos, rÎµâŸ© :
        âˆƒ r, Î¼ (closedBall x (3 * r)) â‰¤ C * Î¼ (closedBall x r) âˆ§ r âˆˆ Ioc (0 : â„) Îµ :=
      ((h x).and_eventually (Ioc_mem_nhdsWithin_Ioi âŸ¨le_rfl, ÎµposâŸ©)).exists
    refine
      âŸ¨closedBall x r, âŸ¨isClosed_ball, ?_, âŸ¨r, Subset.rfl, Î¼râŸ©âŸ©, closedBall_subset_closedBall rÎµâŸ©
    exact (nonempty_ball.2 rpos).mono ball_subset_interior_closedBall
  covering := by
    intro s f fsubset ffine
    let t : Set (â„ Ã— Î± Ã— Set Î±) :=
      { p | p.2.2 âŠ† closedBall p.2.1 p.1 âˆ§ Î¼ (closedBall p.2.1 (3 * p.1)) â‰¤ C * Î¼ p.2.2 âˆ§
            (interior p.2.2).Nonempty âˆ§ IsClosed p.2.2 âˆ§ p.2.2 âˆˆ f p.2.1 âˆ§ p.2.1 âˆˆ s }
    have A : âˆ€ x âˆˆ s, âˆ€ Îµ : â„, Îµ > 0 â†’ âˆƒ p, p âˆˆ t âˆ§ p.1 â‰¤ Îµ âˆ§ p.2.1 = x := by
      intro x xs Îµ Îµpos
      rcases ffine x xs Îµ Îµpos with âŸ¨a, ha, h'aâŸ©
      rcases fsubset x xs ha with âŸ¨a_closed, a_int, âŸ¨r, ar, Î¼râŸ©âŸ©
      refine âŸ¨âŸ¨min r Îµ, x, aâŸ©, âŸ¨?_, ?_, a_int, a_closed, ha, xsâŸ©, min_le_right _ _, rflâŸ©
      Â· rcases min_cases r Îµ with (h' | h') <;> rwa [h'.1]
      Â· apply le_trans ?_ Î¼r
        gcongr
        apply min_le_left
    rcases exists_disjoint_covering_ae Î¼ s t C (fun p => p.1) (fun p => p.2.1) (fun p => p.2.2)
        (fun p hp => hp.1) (fun p hp => hp.2.1) (fun p hp => hp.2.2.1) (fun p hp => hp.2.2.2.1) A
      with âŸ¨t', t't, _, t'_disj, Î¼t'âŸ©
    refine âŸ¨(fun p : â„ Ã— Î± Ã— Set Î± => p.2) '' t', ?_, ?_, ?_, ?_âŸ©
    Â· rintro - âŸ¨q, hq, rflâŸ©
      exact (t't hq).2.2.2.2.2
    Â· rintro p âŸ¨q, hq, rflâŸ© p' âŸ¨q', hq', rflâŸ© hqq'
      exact t'_disj hq hq' (ne_of_apply_ne _ hqq')
    Â· rintro - âŸ¨q, hq, rflâŸ©
      exact (t't hq).2.2.2.2.1
    Â· convert Î¼t' using 3
      rw [biUnion_image]
#align vitali.vitali_family Vitali.vitaliFamily

end Vitali
