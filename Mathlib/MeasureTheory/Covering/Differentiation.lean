/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Covering.VitaliFamily
import Mathlib.MeasureTheory.Function.AEMeasurableOrder
import Mathlib.MeasureTheory.Integral.Average
import Mathlib.MeasureTheory.Measure.Decomposition.Lebesgue
import Mathlib.MeasureTheory.Measure.Regular

/-!
# Differentiation of measures

On a second countable metric space with a measure `Î¼`, consider a Vitali family (i.e., for each `x`
one has a family of sets shrinking to `x`, with a good behavior with respect to covering theorems).
Consider also another measure `Ï`. Then, for almost every `x`, the ratio `Ï a / Î¼ a` converges when
`a` shrinks to `x` along the Vitali family, towards the Radon-Nikodym derivative of `Ï` with
respect to `Î¼`. This is the main theorem on differentiation of measures.

This theorem is proved in this file, under the name `VitaliFamily.ae_tendsto_rnDeriv`. Note that,
almost surely, `Î¼ a` is eventually positive and finite (see
`VitaliFamily.ae_eventually_measure_pos` and `VitaliFamily.eventually_measure_lt_top`), so the
ratio really makes sense.

For concrete applications, one needs concrete instances of Vitali families, as provided for instance
by `Besicovitch.vitaliFamily` (for balls) or by `Vitali.vitaliFamily` (for doubling measures).

Specific applications to Lebesgue density points and the Lebesgue differentiation theorem are also
derived:
* `VitaliFamily.ae_tendsto_measure_inter_div` states that, for almost every point `x âˆˆ s`,
  then `Î¼ (s âˆ© a) / Î¼ a` tends to `1` as `a` shrinks to `x` along a Vitali family.
* `VitaliFamily.ae_tendsto_average_norm_sub` states that, for almost every point `x`, then the
  average of `y â†¦ â€–f y - f xâ€–` on `a` tends to `0` as `a` shrinks to `x` along a Vitali family.

## Sketch of proof

Let `v` be a Vitali family for `Î¼`. Assume for simplicity that `Ï` is absolutely continuous with
respect to `Î¼`, as the case of a singular measure is easier.

It is easy to see that a set `s` on which `liminf Ï a / Î¼ a < q` satisfies `Ï s â‰¤ q * Î¼ s`, by using
a disjoint subcovering provided by the definition of Vitali families. Similarly for the limsup.
It follows that a set on which `Ï a / Î¼ a` oscillates has measure `0`, and therefore that
`Ï a / Î¼ a` converges almost surely (`VitaliFamily.ae_tendsto_div`). Moreover, on a set where the
limit is close to a constant `c`, one gets `Ï s âˆ¼ c Î¼ s`, using again a covering lemma as above.
It follows that `Ï` is equal to `Î¼.withDensity (v.limRatio Ï x)`, where `v.limRatio Ï x` is the
limit of `Ï a / Î¼ a` at `x` (which is well defined almost everywhere). By uniqueness of the
Radon-Nikodym derivative, one gets `v.limRatio Ï x = Ï.rnDeriv Î¼ x` almost everywhere, completing
the proof.

There is a difficulty in this sketch: this argument works well when `v.limRatio Ï` is measurable,
but there is no guarantee that this is the case, especially if one doesn't make further assumptions
on the Vitali family. We use an indirect argument to show that `v.limRatio Ï` is always
almost everywhere measurable, again based on the disjoint subcovering argument
(see `VitaliFamily.exists_measurable_supersets_limRatio`), and then proceed as sketched above
but replacing `v.limRatio Ï` by a measurable version called `v.limRatioMeas Ï`.

## Counterexample

The standing assumption in this file is that spaces are second countable. Without this assumption,
measures may be zero locally but nonzero globally, which is not compatible with differentiation
theory (which deduces global information from local one). Here is an example displaying this
behavior.

Define a measure `Î¼` by `Î¼ s = 0` if `s` is covered by countably many balls of radius `1`,
and `Î¼ s = âˆ` otherwise. This is indeed a countably additive measure, which is moreover
locally finite and doubling at small scales. It vanishes on every ball of radius `1`, so all the
quantities in differentiation theory (defined as ratios of measures as the radius tends to zero)
make no sense. However, the measure is not globally zero if the space is big enough.

## References

* [Herbert Federer, Geometric Measure Theory, Chapter 2.9][Federer1996]
-/

open MeasureTheory Metric Set Filter TopologicalSpace MeasureTheory.Measure

open scoped Filter ENNReal MeasureTheory NNReal Topology

variable {Î± : Type*} [PseudoMetricSpace Î±] {m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}
  (v : VitaliFamily Î¼)
  {E : Type*} [NormedAddCommGroup E]

namespace VitaliFamily

/-- The limit along a Vitali family of `Ï a / Î¼ a` where it makes sense, and garbage otherwise.
Do *not* use this definition: it is only a temporary device to show that this ratio tends almost
everywhere to the Radon-Nikodym derivative. -/
noncomputable def limRatio (Ï : Measure Î±) (x : Î±) : â„â‰¥0âˆ :=
  limUnder (v.filterAt x) fun a => Ï a / Î¼ a

/-- For almost every point `x`, sufficiently small sets in a Vitali family around `x` have positive
measure. (This is a nontrivial result, following from the covering property of Vitali families). -/
theorem ae_eventually_measure_pos [SecondCountableTopology Î±] :
    âˆ€áµ x âˆ‚Î¼, âˆ€á¶  a in v.filterAt x, 0 < Î¼ a := by
  set s := {x | Â¬âˆ€á¶  a in v.filterAt x, 0 < Î¼ a} with hs
  simp -zeta only [not_lt, not_eventually, nonpos_iff_eq_zero] at hs
  change Î¼ s = 0
  let f : Î± â†’ Set (Set Î±) := fun _ => {a | Î¼ a = 0}
  have h : v.FineSubfamilyOn f s := by
    intro x hx Îµ Îµpos
    rw [hs] at hx
    simp only [frequently_filterAt_iff, gt_iff_lt, mem_setOf_eq] at hx
    rcases hx Îµ Îµpos with âŸ¨a, a_sets, ax, Î¼aâŸ©
    exact âŸ¨a, âŸ¨a_sets, Î¼aâŸ©, axâŸ©
  refine le_antisymm ?_ bot_le
  calc
    Î¼ s â‰¤ âˆ‘' x : h.index, Î¼ (h.covering x) := h.measure_le_tsum
    _ = âˆ‘' x : h.index, 0 := by congr; ext1 x; exact h.covering_mem x.2
    _ = 0 := by simp only [tsum_zero]

/-- For every point `x`, sufficiently small sets in a Vitali family around `x` have finite measure.
(This is a trivial result, following from the fact that the measure is locally finite). -/
theorem eventually_measure_lt_top [IsLocallyFiniteMeasure Î¼] (x : Î±) :
    âˆ€á¶  a in v.filterAt x, Î¼ a < âˆ :=
  (Î¼.finiteAt_nhds x).eventually.filter_mono inf_le_left

/-- If two measures `Ï` and `Î½` have, at every point of a set `s`, arbitrarily small sets in a
Vitali family satisfying `Ï a â‰¤ Î½ a`, then `Ï s â‰¤ Î½ s` if `Ï â‰ª Î¼`. -/
theorem measure_le_of_frequently_le [SecondCountableTopology Î±] [BorelSpace Î±] {Ï : Measure Î±}
    (Î½ : Measure Î±) [IsLocallyFiniteMeasure Î½] (hÏ : Ï â‰ª Î¼) (s : Set Î±)
    (hs : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filterAt x, Ï a â‰¤ Î½ a) : Ï s â‰¤ Î½ s := by
  -- this follows from a covering argument using the sets satisfying `Ï a â‰¤ Î½ a`.
  apply ENNReal.le_of_forall_pos_le_add fun Îµ Îµpos _ => ?_
  obtain âŸ¨U, sU, U_open, Î½UâŸ© : âˆƒ (U : Set Î±), s âŠ† U âˆ§ IsOpen U âˆ§ Î½ U â‰¤ Î½ s + Îµ :=
    exists_isOpen_le_add s Î½ (ENNReal.coe_pos.2 Îµpos).ne'
  let f : Î± â†’ Set (Set Î±) := fun _ => {a | Ï a â‰¤ Î½ a âˆ§ a âŠ† U}
  have h : v.FineSubfamilyOn f s := by
    apply v.fineSubfamilyOn_of_frequently f s fun x hx => ?_
    have :=
      (hs x hx).and_eventually
        ((v.eventually_filterAt_mem_setsAt x).and
          (v.eventually_filterAt_subset_of_nhds (U_open.mem_nhds (sU hx))))
    apply Frequently.mono this
    rintro a âŸ¨Ïa, _, aUâŸ©
    exact âŸ¨Ïa, aUâŸ©
  haveI : Encodable h.index := h.index_countable.toEncodable
  calc
    Ï s â‰¤ âˆ‘' x : h.index, Ï (h.covering x) := h.measure_le_tsum_of_absolutelyContinuous hÏ
    _ â‰¤ âˆ‘' x : h.index, Î½ (h.covering x) := ENNReal.tsum_le_tsum fun x => (h.covering_mem x.2).1
    _ = Î½ (â‹ƒ x : h.index, h.covering x) := by
      rw [measure_iUnion h.covering_disjoint_subtype fun i => h.measurableSet_u i.2]
    _ â‰¤ Î½ U := (measure_mono (iUnion_subset fun i => (h.covering_mem i.2).2))
    _ â‰¤ Î½ s + Îµ := Î½U

theorem eventually_filterAt_integrableOn (x : Î±) {f : Î± â†’ E} (hf : LocallyIntegrable f Î¼) :
    âˆ€á¶  a in v.filterAt x, IntegrableOn f a Î¼ := by
  rcases hf x with âŸ¨w, w_nhds, hwâŸ©
  filter_upwards [v.eventually_filterAt_subset_of_nhds w_nhds] with a ha
  exact hw.mono_set ha

section

variable [SecondCountableTopology Î±] [BorelSpace Î±] [IsLocallyFiniteMeasure Î¼] {Ï : Measure Î±}
  [IsLocallyFiniteMeasure Ï]

/-- If a measure `Ï` is singular with respect to `Î¼`, then for `Î¼` almost every `x`, the ratio
`Ï a / Î¼ a` tends to zero when `a` shrinks to `x` along the Vitali family. This makes sense
as `Î¼ a` is eventually positive by `ae_eventually_measure_pos`. -/
theorem ae_eventually_measure_zero_of_singular (hÏ : Ï âŸ‚â‚˜ Î¼) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ 0) := by
  have A : âˆ€ Îµ > (0 : â„â‰¥0), âˆ€áµ x âˆ‚Î¼, âˆ€á¶  a in v.filterAt x, Ï a < Îµ * Î¼ a := by
    intro Îµ Îµpos
    set s := {x | Â¬âˆ€á¶  a in v.filterAt x, Ï a < Îµ * Î¼ a} with hs
    change Î¼ s = 0
    obtain âŸ¨o, _, Ïo, Î¼oâŸ© : âˆƒ o : Set Î±, MeasurableSet o âˆ§ Ï o = 0 âˆ§ Î¼ oá¶œ = 0 := hÏ
    apply le_antisymm _ bot_le
    calc
      Î¼ s â‰¤ Î¼ (s âˆ© o âˆª oá¶œ) := by
        conv_lhs => rw [â† inter_union_compl s o]
        gcongr
        apply inter_subset_right
      _ â‰¤ Î¼ (s âˆ© o) + Î¼ oá¶œ := measure_union_le _ _
      _ = Î¼ (s âˆ© o) := by rw [Î¼o, add_zero]
      _ = (Îµ : â„â‰¥0âˆ)â»Â¹ * (Îµ â€¢ Î¼) (s âˆ© o) := by
        simp only [coe_nnreal_smul_apply, â† mul_assoc, mul_comm _ (Îµ : â„â‰¥0âˆ)]
        rw [ENNReal.mul_inv_cancel (ENNReal.coe_pos.2 Îµpos).ne' ENNReal.coe_ne_top, one_mul]
      _ â‰¤ (Îµ : â„â‰¥0âˆ)â»Â¹ * Ï (s âˆ© o) := by
        gcongr
        refine v.measure_le_of_frequently_le Ï smul_absolutelyContinuous _ ?_
        intro x hx
        rw [hs] at hx
        simp only [mem_inter_iff, not_lt, not_eventually, mem_setOf_eq] at hx
        exact hx.1
      _ â‰¤ (Îµ : â„â‰¥0âˆ)â»Â¹ * Ï o := by gcongr; apply inter_subset_right
      _ = 0 := by rw [Ïo, mul_zero]
  obtain âŸ¨u, _, u_pos, u_limâŸ© :
    âˆƒ u : â„• â†’ â„â‰¥0, StrictAnti u âˆ§ (âˆ€ n : â„•, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„â‰¥0)
  have B : âˆ€áµ x âˆ‚Î¼, âˆ€ n, âˆ€á¶  a in v.filterAt x, Ï a < u n * Î¼ a :=
    ae_all_iff.2 fun n => A (u n) (u_pos n)
  filter_upwards [B, v.ae_eventually_measure_pos]
  intro x hx h'x
  refine tendsto_order.2 âŸ¨fun z hz => (ENNReal.not_lt_zero hz).elim, fun z hz => ?_âŸ©
  obtain âŸ¨w, w_pos, w_ltâŸ© : âˆƒ w : â„â‰¥0, (0 : â„â‰¥0âˆ) < w âˆ§ (w : â„â‰¥0âˆ) < z :=
    ENNReal.lt_iff_exists_nnreal_btwn.1 hz
  obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < w := ((tendsto_order.1 u_lim).2 w (ENNReal.coe_pos.1 w_pos)).exists
  filter_upwards [hx n, h'x, v.eventually_measure_lt_top x]
  intro a ha Î¼a_pos Î¼a_lt_top
  grw [ENNReal.div_lt_iff (.inl Î¼a_pos.ne') (.inl Î¼a_lt_top.ne), ha, hn]
  gcongr
  exact Î¼a_lt_top.ne

section AbsolutelyContinuous

variable (hÏ : Ï â‰ª Î¼)
include hÏ

/-- A set of points `s` satisfying both `Ï a â‰¤ c * Î¼ a` and `Ï a â‰¥ d * Î¼ a` at arbitrarily small
sets in a Vitali family has measure `0` if `c < d`. Indeed, the first inequality should imply
that `Ï s â‰¤ c * Î¼ s`, and the second one that `Ï s â‰¥ d * Î¼ s`, a contradiction if `0 < Î¼ s`. -/
theorem null_of_frequently_le_of_frequently_ge {c d : â„â‰¥0} (hcd : c < d) (s : Set Î±)
    (hc : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filterAt x, Ï a â‰¤ c * Î¼ a)
    (hd : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filterAt x, (d : â„â‰¥0âˆ) * Î¼ a â‰¤ Ï a) : Î¼ s = 0 := by
  apply measure_null_of_locally_null s fun x _ => ?_
  obtain âŸ¨o, xo, o_open, Î¼oâŸ© : âˆƒ o : Set Î±, x âˆˆ o âˆ§ IsOpen o âˆ§ Î¼ o < âˆ :=
    Measure.exists_isOpen_measure_lt_top Î¼ x
  refine âŸ¨s âˆ© o, inter_mem_nhdsWithin _ (o_open.mem_nhds xo), ?_âŸ©
  let s' := s âˆ© o
  by_contra h
  apply lt_irrefl (Ï s')
  calc
    Ï s' â‰¤ c * Î¼ s' := v.measure_le_of_frequently_le (c â€¢ Î¼) hÏ s' fun x hx => hc x hx.1
    _ < d * Î¼ s' := by
      apply (ENNReal.mul_lt_mul_right h _).2 (ENNReal.coe_lt_coe.2 hcd)
      exact (lt_of_le_of_lt (measure_mono inter_subset_right) Î¼o).ne
    _ â‰¤ Ï s' := v.measure_le_of_frequently_le Ï smul_absolutelyContinuous s' fun x hx â†¦ hd x hx.1

/-- If `Ï` is absolutely continuous with respect to `Î¼`, then for almost every `x`,
the ratio `Ï a / Î¼ a` converges as `a` shrinks to `x` along a Vitali family for `Î¼`. -/
theorem ae_tendsto_div : âˆ€áµ x âˆ‚Î¼, âˆƒ c, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ c) := by
  obtain âŸ¨w, w_count, w_dense, _, w_topâŸ© :
    âˆƒ w : Set â„â‰¥0âˆ, w.Countable âˆ§ Dense w âˆ§ 0 âˆ‰ w âˆ§ âˆ âˆ‰ w :=
    ENNReal.exists_countable_dense_no_zero_top
  have I : âˆ€ x âˆˆ w, x â‰  âˆ := fun x xs hx => w_top (hx â–¸ xs)
  have A : âˆ€ c âˆˆ w, âˆ€ d âˆˆ w, c < d â†’ âˆ€áµ x âˆ‚Î¼,
      Â¬((âˆƒá¶  a in v.filterAt x, Ï a / Î¼ a < c) âˆ§ âˆƒá¶  a in v.filterAt x, d < Ï a / Î¼ a) := by
    intro c hc d hd hcd
    lift c to â„â‰¥0 using I c hc
    lift d to â„â‰¥0 using I d hd
    apply v.null_of_frequently_le_of_frequently_ge hÏ (ENNReal.coe_lt_coe.1 hcd)
    Â· simp only [and_imp, exists_prop, not_frequently, not_and, not_lt, not_le, not_eventually,
        mem_setOf_eq, mem_compl_iff, not_forall]
      intro x h1x _
      apply h1x.mono fun a ha => ?_
      refine (ENNReal.div_le_iff_le_mul ?_ (Or.inr (bot_le.trans_lt ha).ne')).1 ha.le
      simp only [ENNReal.coe_ne_top, Ne, or_true, not_false_iff]
    Â· simp only [and_imp, exists_prop, not_frequently, not_and, not_lt, not_le, not_eventually,
        mem_setOf_eq, mem_compl_iff, not_forall]
      intro x _ h2x
      apply h2x.mono fun a ha => ?_
      exact ENNReal.mul_le_of_le_div ha.le
  have B : âˆ€áµ x âˆ‚Î¼, âˆ€ c âˆˆ w, âˆ€ d âˆˆ w, c < d â†’
      Â¬((âˆƒá¶  a in v.filterAt x, Ï a / Î¼ a < c) âˆ§ âˆƒá¶  a in v.filterAt x, d < Ï a / Î¼ a) := by
    simpa only [ae_ball_iff w_count, ae_all_iff]
  filter_upwards [B]
  intro x hx
  exact tendsto_of_no_upcrossings w_dense hx

theorem ae_tendsto_limRatio :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ (v.limRatio Ï x)) := by
  filter_upwards [v.ae_tendsto_div hÏ]
  intro x hx
  exact tendsto_nhds_limUnder hx

/-- Given two thresholds `p < q`, the sets `{x | v.limRatio Ï x < p}`
and `{x | q < v.limRatio Ï x}` are obviously disjoint. The key to proving that `v.limRatio Ï` is
almost everywhere measurable is to show that these sets have measurable supersets which are also
disjoint, up to zero measure. This is the content of this lemma. -/
theorem exists_measurable_supersets_limRatio {p q : â„â‰¥0} (hpq : p < q) :
    âˆƒ a b, MeasurableSet a âˆ§ MeasurableSet b âˆ§
      {x | v.limRatio Ï x < p} âŠ† a âˆ§ {x | (q : â„â‰¥0âˆ) < v.limRatio Ï x} âŠ† b âˆ§ Î¼ (a âˆ© b) = 0 := by
  /- Here is a rough sketch, assuming that the measure is finite and the limit is well defined
    everywhere. Let `u := {x | v.limRatio Ï x < p}` and `w := {x | q < v.limRatio Ï x}`. They
    have measurable supersets `u'` and `w'` of the same measure. We will show that these satisfy
    the conclusion of the theorem, i.e., `Î¼ (u' âˆ© w') = 0`. For this, note that
    `Ï (u' âˆ© w') = Ï (u âˆ© w')` (as `w'` is measurable, see `measure_toMeasurable_add_inter_left`).
    The latter set is included in the set where the limit of the ratios is `< p`, and therefore
    its measure is `â‰¤ p * Î¼ (u âˆ© w')`. Using the same trick in the other direction gives that this
    is `p * Î¼ (u' âˆ© w')`. We have shown that `Ï (u' âˆ© w') â‰¤ p * Î¼ (u' âˆ© w')`. Arguing in the same
    way but using the `w` part gives `q * Î¼ (u' âˆ© w') â‰¤ Ï (u' âˆ© w')`. If `Î¼ (u' âˆ© w')` were nonzero,
    this would be a contradiction as `p < q`.

    For the rigorous proof, we need to work on a part of the space where the measure is finite
    (provided by `spanningSets (Ï + Î¼)`) and to restrict to the set where the limit is well defined
    (called `s` below, of full measure). Otherwise, the argument goes through.
    -/
  let s := {x | âˆƒ c, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ c)}
  let o : â„• â†’ Set Î± := spanningSets (Ï + Î¼)
  let u n := s âˆ© {x | v.limRatio Ï x < p} âˆ© o n
  let w n := s âˆ© {x | (q : â„â‰¥0âˆ) < v.limRatio Ï x} âˆ© o n
  -- the supersets are obtained by restricting to the set `s` where the limit is well defined, to
  -- a finite measure part `o n`, taking a measurable superset here, and then taking the union over
  -- `n`.
  refine
    âŸ¨toMeasurable Î¼ sá¶œ âˆª â‹ƒ n, toMeasurable (Ï + Î¼) (u n),
      toMeasurable Î¼ sá¶œ âˆª â‹ƒ n, toMeasurable (Ï + Î¼) (w n), ?_, ?_, ?_, ?_, ?_âŸ©
  -- check that these sets are measurable supersets as required
  Â· exact
      (measurableSet_toMeasurable _ _).union
        (MeasurableSet.iUnion fun n => measurableSet_toMeasurable _ _)
  Â· exact
      (measurableSet_toMeasurable _ _).union
        (MeasurableSet.iUnion fun n => measurableSet_toMeasurable _ _)
  Â· intro x hx
    by_cases h : x âˆˆ s
    Â· refine Or.inr (mem_iUnion.2 âŸ¨spanningSetsIndex (Ï + Î¼) x, ?_âŸ©)
      exact subset_toMeasurable _ _ âŸ¨âŸ¨h, hxâŸ©, mem_spanningSetsIndex _ _âŸ©
    Â· exact Or.inl (subset_toMeasurable Î¼ sá¶œ h)
  Â· intro x hx
    by_cases h : x âˆˆ s
    Â· refine Or.inr (mem_iUnion.2 âŸ¨spanningSetsIndex (Ï + Î¼) x, ?_âŸ©)
      exact subset_toMeasurable _ _ âŸ¨âŸ¨h, hxâŸ©, mem_spanningSetsIndex _ _âŸ©
    Â· exact Or.inl (subset_toMeasurable Î¼ sá¶œ h)
  -- it remains to check the nontrivial part that these sets have zero measure intersection.
  -- it suffices to do it for fixed `m` and `n`, as one is taking countable unions.
  suffices H : âˆ€ m n : â„•, Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) = 0 by
    have A :
      (toMeasurable Î¼ sá¶œ âˆª â‹ƒ n, toMeasurable (Ï + Î¼) (u n)) âˆ©
          (toMeasurable Î¼ sá¶œ âˆª â‹ƒ n, toMeasurable (Ï + Î¼) (w n)) âŠ†
        toMeasurable Î¼ sá¶œ âˆª
          â‹ƒ (m) (n), toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n) := by
      simp only [inter_union_distrib_left, union_inter_distrib_right, true_and,
        subset_union_left, union_subset_iff, inter_self]
      refine âŸ¨?_, ?_, ?_âŸ©
      Â· exact inter_subset_right.trans subset_union_left
      Â· exact inter_subset_left.trans subset_union_left
      Â· simp_rw [iUnion_inter, inter_iUnion]; exact subset_union_right
    refine le_antisymm ((measure_mono A).trans ?_) bot_le
    calc
      Î¼ (toMeasurable Î¼ sá¶œ âˆª
        â‹ƒ (m) (n), toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) â‰¤
          Î¼ (toMeasurable Î¼ sá¶œ) +
            Î¼ (â‹ƒ (m) (n), toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) :=
        measure_union_le _ _
      _ = Î¼ (â‹ƒ (m) (n), toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) := by
        have : Î¼ sá¶œ = 0 := v.ae_tendsto_div hÏ; rw [measure_toMeasurable, this, zero_add]
      _ â‰¤ âˆ‘' (m) (n), Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) :=
        ((measure_iUnion_le _).trans (ENNReal.tsum_le_tsum fun m => measure_iUnion_le _))
      _ = 0 := by simp only [H, tsum_zero]
  -- now starts the nontrivial part of the argument. We fix `m` and `n`, and show that the
  -- measurable supersets of `u m` and `w n` have zero measure intersection by using the lemmas
  -- `measure_toMeasurable_add_inter_left` (to reduce to `u m` or `w n` instead of the measurable
  -- superset) and `measure_le_of_frequently_le` to compare their measures for `Ï` and `Î¼`.
  intro m n
  have I : (Ï + Î¼) (u m) â‰  âˆ := by
    apply (lt_of_le_of_lt (measure_mono _) (measure_spanningSets_lt_top (Ï + Î¼) m)).ne
    exact inter_subset_right
  have J : (Ï + Î¼) (w n) â‰  âˆ := by
    apply (lt_of_le_of_lt (measure_mono _) (measure_spanningSets_lt_top (Ï + Î¼) n)).ne
    exact inter_subset_right
  have A :
    Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) â‰¤
      p * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) :=
    calc
      Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) =
          Ï (u m âˆ© toMeasurable (Ï + Î¼) (w n)) :=
        measure_toMeasurable_add_inter_left (measurableSet_toMeasurable _ _) I
      _ â‰¤ (p â€¢ Î¼) (u m âˆ© toMeasurable (Ï + Î¼) (w n)) := by
        refine v.measure_le_of_frequently_le (p â€¢ Î¼) hÏ _ fun x hx => ?_
        have L : Tendsto (fun a : Set Î± => Ï a / Î¼ a) (v.filterAt x) (ğ“ (v.limRatio Ï x)) :=
          tendsto_nhds_limUnder hx.1.1.1
        have I : âˆ€á¶  b : Set Î± in v.filterAt x, Ï b / Î¼ b < p := (tendsto_order.1 L).2 _ hx.1.1.2
        apply I.frequently.mono fun a ha => ?_
        rw [coe_nnreal_smul_apply]
        refine (ENNReal.div_le_iff_le_mul ?_ (Or.inr (bot_le.trans_lt ha).ne')).1 ha.le
        simp only [ENNReal.coe_ne_top, Ne, or_true, not_false_iff]
      _ = p * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) := by
        simp only [coe_nnreal_smul_apply,
          measure_toMeasurable_add_inter_right (measurableSet_toMeasurable _ _) I]
  have B :
    (q : â„â‰¥0âˆ) * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) â‰¤
      Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) :=
    calc
      (q : â„â‰¥0âˆ) * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) =
          (q : â„â‰¥0âˆ) * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© w n) := by
        conv_rhs => rw [inter_comm]
        rw [inter_comm, measure_toMeasurable_add_inter_right (measurableSet_toMeasurable _ _) J]
      _ â‰¤ Ï (toMeasurable (Ï + Î¼) (u m) âˆ© w n) := by
        rw [â† coe_nnreal_smul_apply]
        refine v.measure_le_of_frequently_le _ (.smul_left .rfl _) _ ?_
        intro x hx
        have L : Tendsto (fun a : Set Î± => Ï a / Î¼ a) (v.filterAt x) (ğ“ (v.limRatio Ï x)) :=
          tendsto_nhds_limUnder hx.2.1.1
        have I : âˆ€á¶  b : Set Î± in v.filterAt x, (q : â„â‰¥0âˆ) < Ï b / Î¼ b :=
          (tendsto_order.1 L).1 _ hx.2.1.2
        apply I.frequently.mono fun a ha => ?_
        rw [coe_nnreal_smul_apply]
        exact ENNReal.mul_le_of_le_div ha.le
      _ = Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) := by
        conv_rhs => rw [inter_comm]
        rw [inter_comm]
        exact (measure_toMeasurable_add_inter_left (measurableSet_toMeasurable _ _) J).symm
  by_contra h
  apply lt_irrefl (Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)))
  calc
    Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) â‰¤
        p * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) :=
      A
    _ < q * Î¼ (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) := by
      gcongr
      suffices H : (Ï + Î¼) (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) â‰  âˆ by
        simp only [not_or, ENNReal.add_eq_top, Pi.add_apply, Ne, coe_add] at H
        exact H.2
      apply (lt_of_le_of_lt (measure_mono inter_subset_left) _).ne
      rw [measure_toMeasurable]
      apply lt_of_le_of_lt (measure_mono _) (measure_spanningSets_lt_top (Ï + Î¼) m)
      exact inter_subset_right
    _ â‰¤ Ï (toMeasurable (Ï + Î¼) (u m) âˆ© toMeasurable (Ï + Î¼) (w n)) := B

theorem aemeasurable_limRatio : AEMeasurable (v.limRatio Ï) Î¼ := by
  apply ENNReal.aemeasurable_of_exist_almost_disjoint_supersets _ _ fun p q hpq => ?_
  exact v.exists_measurable_supersets_limRatio hÏ hpq

/-- A measurable version of `v.limRatio Ï`. Do *not* use this definition: it is only a temporary
device to show that `v.limRatio` is almost everywhere equal to the Radon-Nikodym derivative. -/
noncomputable def limRatioMeas : Î± â†’ â„â‰¥0âˆ :=
  (v.aemeasurable_limRatio hÏ).mk _

theorem limRatioMeas_measurable : Measurable (v.limRatioMeas hÏ) :=
  AEMeasurable.measurable_mk _

theorem ae_tendsto_limRatioMeas :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ (v.limRatioMeas hÏ x)) := by
  filter_upwards [v.ae_tendsto_limRatio hÏ, AEMeasurable.ae_eq_mk (v.aemeasurable_limRatio hÏ)]
  intro x hx h'x
  rwa [h'x] at hx

/-- If, for all `x` in a set `s`, one has frequently `Ï a / Î¼ a < p`, then `Ï s â‰¤ p * Î¼ s`, as
proved in `measure_le_of_frequently_le`. Since `Ï a / Î¼ a` tends almost everywhere to
`v.limRatioMeas hÏ x`, the same property holds for sets `s` on which `v.limRatioMeas hÏ < p`. -/
theorem measure_le_mul_of_subset_limRatioMeas_lt {p : â„â‰¥0} {s : Set Î±}
    (h : s âŠ† {x | v.limRatioMeas hÏ x < p}) : Ï s â‰¤ p * Î¼ s := by
  let t := {x : Î± | Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ (v.limRatioMeas hÏ x))}
  have A : Î¼ tá¶œ = 0 := v.ae_tendsto_limRatioMeas hÏ
  suffices H : Ï (s âˆ© t) â‰¤ (p â€¢ Î¼) (s âˆ© t) by calc
    Ï s = Ï (s âˆ© t âˆª s âˆ© tá¶œ) := by rw [inter_union_compl]
    _ â‰¤ Ï (s âˆ© t) + Ï (s âˆ© tá¶œ) := measure_union_le _ _
    _ â‰¤ (p â€¢ Î¼) (s âˆ© t) + Ï tá¶œ := by gcongr; apply inter_subset_right
    _ â‰¤ p * Î¼ (s âˆ© t) := by simp [(hÏ A)]
    _ â‰¤ p * Î¼ s := by gcongr; apply inter_subset_left
  refine v.measure_le_of_frequently_le (p â€¢ Î¼) hÏ _ fun x hx => ?_
  have I : âˆ€á¶  b : Set Î± in v.filterAt x, Ï b / Î¼ b < p := (tendsto_order.1 hx.2).2 _ (h hx.1)
  apply I.frequently.mono fun a ha => ?_
  rw [coe_nnreal_smul_apply]
  refine (ENNReal.div_le_iff_le_mul ?_ (Or.inr (bot_le.trans_lt ha).ne')).1 ha.le
  simp only [ENNReal.coe_ne_top, Ne, or_true, not_false_iff]

/-- If, for all `x` in a set `s`, one has frequently `q < Ï a / Î¼ a`, then `q * Î¼ s â‰¤ Ï s`, as
proved in `measure_le_of_frequently_le`. Since `Ï a / Î¼ a` tends almost everywhere to
`v.limRatioMeas hÏ x`, the same property holds for sets `s` on which `q < v.limRatioMeas hÏ`. -/
theorem mul_measure_le_of_subset_lt_limRatioMeas {q : â„â‰¥0} {s : Set Î±}
    (h : s âŠ† {x | (q : â„â‰¥0âˆ) < v.limRatioMeas hÏ x}) : (q : â„â‰¥0âˆ) * Î¼ s â‰¤ Ï s := by
  let t := {x : Î± | Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ (v.limRatioMeas hÏ x))}
  have A : Î¼ tá¶œ = 0 := v.ae_tendsto_limRatioMeas hÏ
  suffices H : (q â€¢ Î¼) (s âˆ© t) â‰¤ Ï (s âˆ© t) by calc
    (q â€¢ Î¼) s = (q â€¢ Î¼) (s âˆ© t âˆª s âˆ© tá¶œ) := by rw [inter_union_compl]
    _ â‰¤ (q â€¢ Î¼) (s âˆ© t) + (q â€¢ Î¼) (s âˆ© tá¶œ) := measure_union_le _ _
    _ â‰¤ Ï (s âˆ© t) + (q â€¢ Î¼) tá¶œ := by gcongr; apply inter_subset_right
    _ = Ï (s âˆ© t) := by simp [A]
    _ â‰¤ Ï s := by gcongr; apply inter_subset_left
  refine v.measure_le_of_frequently_le _ (.smul_left .rfl _) _ ?_
  intro x hx
  have I : âˆ€á¶  a in v.filterAt x, (q : â„â‰¥0âˆ) < Ï a / Î¼ a := (tendsto_order.1 hx.2).1 _ (h hx.1)
  apply I.frequently.mono fun a ha => ?_
  rw [coe_nnreal_smul_apply]
  exact ENNReal.mul_le_of_le_div ha.le

/-- The points with `v.limRatioMeas hÏ x = âˆ` have measure `0` for `Î¼`. -/
theorem measure_limRatioMeas_top : Î¼ {x | v.limRatioMeas hÏ x = âˆ} = 0 := by
  refine measure_null_of_locally_null _ fun x _ => ?_
  obtain âŸ¨o, xo, o_open, Î¼oâŸ© : âˆƒ o : Set Î±, x âˆˆ o âˆ§ IsOpen o âˆ§ Ï o < âˆ :=
    Measure.exists_isOpen_measure_lt_top Ï x
  let s := {x : Î± | v.limRatioMeas hÏ x = âˆ} âˆ© o
  refine âŸ¨s, inter_mem_nhdsWithin _ (o_open.mem_nhds xo), le_antisymm ?_ bot_leâŸ©
  have Ïs : Ï s â‰  âˆ := ((measure_mono inter_subset_right).trans_lt Î¼o).ne
  have A : âˆ€ q : â„â‰¥0, 1 â‰¤ q â†’ Î¼ s â‰¤ (q : â„â‰¥0âˆ)â»Â¹ * Ï s := by
    intro q hq
    rw [mul_comm, â† div_eq_mul_inv, ENNReal.le_div_iff_mul_le _ (Or.inr Ïs), mul_comm]
    Â· apply v.mul_measure_le_of_subset_lt_limRatioMeas hÏ
      intro y hy
      have : v.limRatioMeas hÏ y = âˆ := hy.1
      simp only [this, ENNReal.coe_lt_top, mem_setOf_eq]
    Â· simp only [(zero_lt_one.trans_le hq).ne', true_or, ENNReal.coe_eq_zero, Ne,
        not_false_iff]
  have B : Tendsto (fun q : â„â‰¥0 => (q : â„â‰¥0âˆ)â»Â¹ * Ï s) atTop (ğ“ (âˆâ»Â¹ * Ï s)) := by
    apply ENNReal.Tendsto.mul_const _ (Or.inr Ïs)
    exact ENNReal.tendsto_inv_iff.2 (ENNReal.tendsto_coe_nhds_top.2 tendsto_id)
  simp only [zero_mul, ENNReal.inv_top] at B
  apply ge_of_tendsto B
  exact eventually_atTop.2 âŸ¨1, AâŸ©

/-- The points with `v.limRatioMeas hÏ x = 0` have measure `0` for `Ï`. -/
theorem measure_limRatioMeas_zero : Ï {x | v.limRatioMeas hÏ x = 0} = 0 := by
  refine measure_null_of_locally_null _ fun x _ => ?_
  obtain âŸ¨o, xo, o_open, Î¼oâŸ© : âˆƒ o : Set Î±, x âˆˆ o âˆ§ IsOpen o âˆ§ Î¼ o < âˆ :=
    Measure.exists_isOpen_measure_lt_top Î¼ x
  let s := {x : Î± | v.limRatioMeas hÏ x = 0} âˆ© o
  refine âŸ¨s, inter_mem_nhdsWithin _ (o_open.mem_nhds xo), le_antisymm ?_ bot_leâŸ©
  have Î¼s : Î¼ s â‰  âˆ := ((measure_mono inter_subset_right).trans_lt Î¼o).ne
  have A : âˆ€ q : â„â‰¥0, 0 < q â†’ Ï s â‰¤ q * Î¼ s := by
    intro q hq
    apply v.measure_le_mul_of_subset_limRatioMeas_lt hÏ
    intro y hy
    have : v.limRatioMeas hÏ y = 0 := hy.1
    simp only [this, mem_setOf_eq, hq, ENNReal.coe_pos]
  have B : Tendsto (fun q : â„â‰¥0 => (q : â„â‰¥0âˆ) * Î¼ s) (ğ“[>] (0 : â„â‰¥0)) (ğ“ ((0 : â„â‰¥0) * Î¼ s)) := by
    apply ENNReal.Tendsto.mul_const _ (Or.inr Î¼s)
    rw [ENNReal.tendsto_coe]
    exact nhdsWithin_le_nhds
  simp only [zero_mul, ENNReal.coe_zero] at B
  apply ge_of_tendsto B
  filter_upwards [self_mem_nhdsWithin] using A

/-- As an intermediate step to show that `Î¼.withDensity (v.limRatioMeas hÏ) = Ï`, we show here
that `Î¼.withDensity (v.limRatioMeas hÏ) â‰¤ t^2 Ï` for any `t > 1`. -/
theorem withDensity_le_mul {s : Set Î±} (hs : MeasurableSet s) {t : â„â‰¥0} (ht : 1 < t) :
    Î¼.withDensity (v.limRatioMeas hÏ) s â‰¤ (t : â„â‰¥0âˆ) ^ 2 * Ï s := by
  /- We cut `s` into the sets where `v.limRatioMeas hÏ = 0`, where `v.limRatioMeas hÏ = âˆ`, and
    where `v.limRatioMeas hÏ âˆˆ [t^n, t^(n+1))` for `n : â„¤`. The first and second have measure `0`.
    For the latter, since `v.limRatioMeas hÏ` fluctuates by at most `t` on this slice, we can use
    `measure_le_mul_of_subset_limRatioMeas_lt` and `mul_measure_le_of_subset_lt_limRatioMeas` to
    show that the two measures are comparable up to `t` (in fact `t^2` for technical reasons of
    strict inequalities). -/
  have t_ne_zero' : t â‰  0 := (zero_lt_one.trans ht).ne'
  have t_ne_zero : (t : â„â‰¥0âˆ) â‰  0 := by simpa only [ENNReal.coe_eq_zero, Ne] using t_ne_zero'
  let Î½ := Î¼.withDensity (v.limRatioMeas hÏ)
  let f := v.limRatioMeas hÏ
  have f_meas : Measurable f := v.limRatioMeas_measurable hÏ
  -- Note(kmill): smul elaborator when used for CoeFun fails to get CoeFun instance to trigger
  -- unless you use the `(... :)` notation. Another fix is using `(2 : Nat)`, so this appears
  -- to be an unpleasant interaction with default instances.
  have A : Î½ (s âˆ© f â»Â¹' {0}) â‰¤ ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) (s âˆ© f â»Â¹' {0}) := by
    apply le_trans _ (zero_le _)
    have M : MeasurableSet (s âˆ© f â»Â¹' {0}) := hs.inter (f_meas (measurableSet_singleton _))
    simp only [f, Î½, nonpos_iff_eq_zero, M, withDensity_apply, lintegral_eq_zero_iff f_meas]
    apply (ae_restrict_iff' M).2
    exact Eventually.of_forall fun x hx => hx.2
  have B : Î½ (s âˆ© f â»Â¹' {âˆ}) â‰¤ ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) (s âˆ© f â»Â¹' {âˆ}) := by
    apply le_trans (le_of_eq _) (zero_le _)
    apply withDensity_absolutelyContinuous Î¼ _
    rw [â† nonpos_iff_eq_zero]
    exact (measure_mono inter_subset_right).trans (v.measure_limRatioMeas_top hÏ).le
  have C :
    âˆ€ n : â„¤,
      Î½ (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) â‰¤
        ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) := by
    intro n
    let I := Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))
    have M : MeasurableSet (s âˆ© f â»Â¹' I) := hs.inter (f_meas measurableSet_Ico)
    simp only [Î½, I, M, withDensity_apply]
    calc
      (âˆ«â» x in s âˆ© f â»Â¹' I, f x âˆ‚Î¼) â‰¤ âˆ«â» _ in s âˆ© f â»Â¹' I, (t : â„â‰¥0âˆ) ^ (n + 1) âˆ‚Î¼ :=
        lintegral_mono_ae ((ae_restrict_iff' M).2 (Eventually.of_forall fun x hx => hx.2.2.le))
      _ = (t : â„â‰¥0âˆ) ^ (n + 1) * Î¼ (s âˆ© f â»Â¹' I) := by
        simp only [lintegral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter]
      _ = (t : â„â‰¥0âˆ) ^ (2 : â„¤) * ((t : â„â‰¥0âˆ) ^ (n - 1) * Î¼ (s âˆ© f â»Â¹' I)) := by
        rw [â† mul_assoc, â† ENNReal.zpow_add t_ne_zero ENNReal.coe_ne_top]
        congr 2
        abel
      _ â‰¤ (t : â„â‰¥0âˆ) ^ (2 : â„¤) * Ï (s âˆ© f â»Â¹' I) := by
        gcongr
        rw [â† ENNReal.coe_zpow (zero_lt_one.trans ht).ne']
        apply v.mul_measure_le_of_subset_lt_limRatioMeas hÏ
        intro x hx
        apply lt_of_lt_of_le _ hx.2.1
        rw [â† ENNReal.coe_zpow (zero_lt_one.trans ht).ne', ENNReal.coe_lt_coe, sub_eq_add_neg,
          zpow_addâ‚€ t_ne_zero']
        conv_rhs => rw [â† mul_one (t ^ n)]
        gcongr
        rw [zpow_neg_one]
        exact inv_lt_one_of_one_ltâ‚€ ht
  calc
    Î½ s =
      Î½ (s âˆ© f â»Â¹' {0}) + Î½ (s âˆ© f â»Â¹' {âˆ}) +
        âˆ‘' n : â„¤, Î½ (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) :=
      measure_eq_measure_preimage_add_measure_tsum_Ico_zpow Î½ f_meas hs ht
    _ â‰¤
        ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) (s âˆ© f â»Â¹' {0}) + ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) (s âˆ© f â»Â¹' {âˆ}) +
          âˆ‘' n : â„¤, ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) (s âˆ© f â»Â¹' Ico (t ^ n) (t ^ (n + 1))) :=
      (add_le_add (add_le_add A B) (ENNReal.tsum_le_tsum C))
    _ = ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï :) s :=
      (measure_eq_measure_preimage_add_measure_tsum_Ico_zpow ((t : â„â‰¥0âˆ) ^ 2 â€¢ Ï) f_meas hs ht).symm

/-- As an intermediate step to show that `Î¼.withDensity (v.limRatioMeas hÏ) = Ï`, we show here
that `Ï â‰¤ t Î¼.withDensity (v.limRatioMeas hÏ)` for any `t > 1`. -/
theorem le_mul_withDensity {s : Set Î±} (hs : MeasurableSet s) {t : â„â‰¥0} (ht : 1 < t) :
    Ï s â‰¤ t * Î¼.withDensity (v.limRatioMeas hÏ) s := by
  /- We cut `s` into the sets where `v.limRatioMeas hÏ = 0`, where `v.limRatioMeas hÏ = âˆ`, and
    where `v.limRatioMeas hÏ âˆˆ [t^n, t^(n+1))` for `n : â„¤`. The first and second have measure `0`.
    For the latter, since `v.limRatioMeas hÏ` fluctuates by at most `t` on this slice, we can use
    `measure_le_mul_of_subset_limRatioMeas_lt` and `mul_measure_le_of_subset_lt_limRatioMeas` to
    show that the two measures are comparable up to `t`. -/
  have t_ne_zero' : t â‰  0 := (zero_lt_one.trans ht).ne'
  have t_ne_zero : (t : â„â‰¥0âˆ) â‰  0 := by simpa only [ENNReal.coe_eq_zero, Ne] using t_ne_zero'
  let Î½ := Î¼.withDensity (v.limRatioMeas hÏ)
  let f := v.limRatioMeas hÏ
  have f_meas : Measurable f := v.limRatioMeas_measurable hÏ
  have A : Ï (s âˆ© f â»Â¹' {0}) â‰¤ (t â€¢ Î½) (s âˆ© f â»Â¹' {0}) := by
    refine le_trans (measure_mono inter_subset_right) (le_trans (le_of_eq ?_) (zero_le _))
    exact v.measure_limRatioMeas_zero hÏ
  have B : Ï (s âˆ© f â»Â¹' {âˆ}) â‰¤ (t â€¢ Î½) (s âˆ© f â»Â¹' {âˆ}) := by
    apply le_trans (le_of_eq _) (zero_le _)
    apply hÏ
    rw [â† nonpos_iff_eq_zero]
    exact (measure_mono inter_subset_right).trans (v.measure_limRatioMeas_top hÏ).le
  have C :
    âˆ€ n : â„¤,
      Ï (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) â‰¤
        (t â€¢ Î½) (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) := by
    intro n
    let I := Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))
    have M : MeasurableSet (s âˆ© f â»Â¹' I) := hs.inter (f_meas measurableSet_Ico)
    simp only [Î½, I, M, withDensity_apply, coe_nnreal_smul_apply]
    calc
      Ï (s âˆ© f â»Â¹' I) â‰¤ (t : â„â‰¥0âˆ) ^ (n + 1) * Î¼ (s âˆ© f â»Â¹' I) := by
        rw [â† ENNReal.coe_zpow t_ne_zero']
        apply v.measure_le_mul_of_subset_limRatioMeas_lt hÏ
        intro x hx
        apply hx.2.2.trans_le (le_of_eq _)
        rw [ENNReal.coe_zpow t_ne_zero']
      _ = âˆ«â» _ in s âˆ© f â»Â¹' I, (t : â„â‰¥0âˆ) ^ (n + 1) âˆ‚Î¼ := by
        simp only [lintegral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter]
      _ â‰¤ âˆ«â» x in s âˆ© f â»Â¹' I, t * f x âˆ‚Î¼ := by
        apply lintegral_mono_ae ((ae_restrict_iff' M).2 (Eventually.of_forall fun x hx => ?_))
        grw [add_comm, ENNReal.zpow_add t_ne_zero ENNReal.coe_ne_top, zpow_one, hx.2.1]
      _ = t * âˆ«â» x in s âˆ© f â»Â¹' I, f x âˆ‚Î¼ := lintegral_const_mul _ f_meas
  calc
    Ï s =
      Ï (s âˆ© f â»Â¹' {0}) + Ï (s âˆ© f â»Â¹' {âˆ}) +
        âˆ‘' n : â„¤, Ï (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) :=
      measure_eq_measure_preimage_add_measure_tsum_Ico_zpow Ï f_meas hs ht
    _ â‰¤
        (t â€¢ Î½) (s âˆ© f â»Â¹' {0}) + (t â€¢ Î½) (s âˆ© f â»Â¹' {âˆ}) +
          âˆ‘' n : â„¤, (t â€¢ Î½) (s âˆ© f â»Â¹' Ico ((t : â„â‰¥0âˆ) ^ n) ((t : â„â‰¥0âˆ) ^ (n + 1))) :=
      (add_le_add (add_le_add A B) (ENNReal.tsum_le_tsum C))
    _ = (t â€¢ Î½) s :=
      (measure_eq_measure_preimage_add_measure_tsum_Ico_zpow (t â€¢ Î½) f_meas hs ht).symm

theorem withDensity_limRatioMeas_eq : Î¼.withDensity (v.limRatioMeas hÏ) = Ï := by
  ext1 s hs
  refine le_antisymm ?_ ?_
  Â· have : Tendsto (fun t : â„â‰¥0 =>
        ((t : â„â‰¥0âˆ) ^ 2 * Ï s : â„â‰¥0âˆ)) (ğ“[>] 1) (ğ“ ((1 : â„â‰¥0âˆ) ^ 2 * Ï s)) := by
      refine ENNReal.Tendsto.mul ?_ ?_ tendsto_const_nhds ?_
      Â· exact ENNReal.Tendsto.pow (ENNReal.tendsto_coe.2 nhdsWithin_le_nhds)
      Â· simp only [one_pow, true_or, Ne, not_false_iff, one_ne_zero]
      Â· simp only [one_pow, Ne, or_true, ENNReal.one_ne_top, not_false_iff]
    simp only [one_pow, one_mul] at this
    refine ge_of_tendsto this ?_
    filter_upwards [self_mem_nhdsWithin] with _ ht
    exact v.withDensity_le_mul hÏ hs ht
  Â· have :
      Tendsto (fun t : â„â‰¥0 => (t : â„â‰¥0âˆ) * Î¼.withDensity (v.limRatioMeas hÏ) s) (ğ“[>] 1)
        (ğ“ ((1 : â„â‰¥0âˆ) * Î¼.withDensity (v.limRatioMeas hÏ) s)) := by
      refine ENNReal.Tendsto.mul_const (ENNReal.tendsto_coe.2 nhdsWithin_le_nhds) ?_
      simp only [true_or, Ne, not_false_iff, one_ne_zero]
    simp only [one_mul] at this
    refine ge_of_tendsto this ?_
    filter_upwards [self_mem_nhdsWithin] with _ ht
    exact v.le_mul_withDensity hÏ hs ht

/-- Weak version of the main theorem on differentiation of measures: given a Vitali family `v`
for a locally finite measure `Î¼`, and another locally finite measure `Ï`, then for `Î¼`-almost
every `x` the ratio `Ï a / Î¼ a` converges, when `a` shrinks to `x` along the Vitali family,
towards the Radon-Nikodym derivative of `Ï` with respect to `Î¼`.

This version assumes that `Ï` is absolutely continuous with respect to `Î¼`. The general version
without this superfluous assumption is `VitaliFamily.ae_tendsto_rnDeriv`.
-/
theorem ae_tendsto_rnDeriv_of_absolutelyContinuous :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ (Ï.rnDeriv Î¼ x)) := by
  have A : (Î¼.withDensity (v.limRatioMeas hÏ)).rnDeriv Î¼ =áµ[Î¼] v.limRatioMeas hÏ :=
    rnDeriv_withDensity Î¼ (v.limRatioMeas_measurable hÏ)
  rw [v.withDensity_limRatioMeas_eq hÏ] at A
  filter_upwards [v.ae_tendsto_limRatioMeas hÏ, A] with _ _ h'x
  rwa [h'x]

end AbsolutelyContinuous

variable (Ï)

/-- Main theorem on differentiation of measures: given a Vitali family `v` for a locally finite
measure `Î¼`, and another locally finite measure `Ï`, then for `Î¼`-almost every `x` the
ratio `Ï a / Î¼ a` converges, when `a` shrinks to `x` along the Vitali family, towards the
Radon-Nikodym derivative of `Ï` with respect to `Î¼`. -/
theorem ae_tendsto_rnDeriv :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Ï a / Î¼ a) (v.filterAt x) (ğ“ (Ï.rnDeriv Î¼ x)) := by
  let t := Î¼.withDensity (Ï.rnDeriv Î¼)
  have eq_add : Ï = Ï.singularPart Î¼ + t := haveLebesgueDecomposition_add _ _
  have A : âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Ï.singularPart Î¼ a / Î¼ a) (v.filterAt x) (ğ“ 0) :=
    v.ae_eventually_measure_zero_of_singular (mutuallySingular_singularPart Ï Î¼)
  have B : âˆ€áµ x âˆ‚Î¼, t.rnDeriv Î¼ x = Ï.rnDeriv Î¼ x :=
    rnDeriv_withDensity Î¼ (measurable_rnDeriv Ï Î¼)
  have C : âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => t a / Î¼ a) (v.filterAt x) (ğ“ (t.rnDeriv Î¼ x)) :=
    v.ae_tendsto_rnDeriv_of_absolutelyContinuous (withDensity_absolutelyContinuous _ _)
  filter_upwards [A, B, C] with _ Ax Bx Cx
  convert Ax.add Cx using 1
  Â· ext1 a
    conv_lhs => rw [eq_add]
    simp only [Pi.add_apply, coe_add, ENNReal.add_div]
  Â· simp only [Bx, zero_add]

/-! ### Lebesgue density points -/


/-- Given a measurable set `s`, then `Î¼ (s âˆ© a) / Î¼ a` converges when `a` shrinks to a typical
point `x` along a Vitali family. The limit is `1` for `x âˆˆ s` and `0` for `x âˆ‰ s`. This shows that
almost every point of `s` is a Lebesgue density point for `s`. A version for non-measurable sets
holds, but it only gives the first conclusion, see `ae_tendsto_measure_inter_div`. -/
theorem ae_tendsto_measure_inter_div_of_measurableSet {s : Set Î±} (hs : MeasurableSet s) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => Î¼ (s âˆ© a) / Î¼ a) (v.filterAt x) (ğ“ (s.indicator 1 x)) := by
  haveI : IsLocallyFiniteMeasure (Î¼.restrict s) :=
    isLocallyFiniteMeasure_of_le restrict_le_self
  filter_upwards [ae_tendsto_rnDeriv v (Î¼.restrict s), rnDeriv_restrict_self Î¼ hs]
  intro x hx h'x
  simpa only [h'x, restrict_apply' hs, inter_comm] using hx

/-- Given an arbitrary set `s`, then `Î¼ (s âˆ© a) / Î¼ a` converges to `1` when `a` shrinks to a
typical point of `s` along a Vitali family. This shows that almost every point of `s` is a
Lebesgue density point for `s`. A stronger version for measurable sets is given
in `ae_tendsto_measure_inter_div_of_measurableSet`. -/
theorem ae_tendsto_measure_inter_div (s : Set Î±) :
    âˆ€áµ x âˆ‚Î¼.restrict s, Tendsto (fun a => Î¼ (s âˆ© a) / Î¼ a) (v.filterAt x) (ğ“ 1) := by
  let t := toMeasurable Î¼ s
  have A :
    âˆ€áµ x âˆ‚Î¼.restrict s,
      Tendsto (fun a => Î¼ (t âˆ© a) / Î¼ a) (v.filterAt x) (ğ“ (t.indicator 1 x)) := by
    apply ae_mono restrict_le_self
    apply ae_tendsto_measure_inter_div_of_measurableSet
    exact measurableSet_toMeasurable _ _
  have B : âˆ€áµ x âˆ‚Î¼.restrict s, t.indicator 1 x = (1 : â„â‰¥0âˆ) := by
    refine ae_restrict_of_ae_restrict_of_subset (subset_toMeasurable Î¼ s) ?_
    filter_upwards [ae_restrict_mem (measurableSet_toMeasurable Î¼ s)] with _ hx
    simp only [t, hx, Pi.one_apply, indicator_of_mem]
  filter_upwards [A, B] with x hx h'x
  rw [h'x] at hx
  apply hx.congr' _
  filter_upwards [v.eventually_filterAt_measurableSet x] with _ ha
  congr 1
  exact measure_toMeasurable_inter_of_sFinite ha _

/-! ### Lebesgue differentiation theorem -/

theorem ae_tendsto_lintegral_div' {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (h'f : (âˆ«â» y, f y âˆ‚Î¼) â‰  âˆ) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => (âˆ«â» y in a, f y âˆ‚Î¼) / Î¼ a) (v.filterAt x) (ğ“ (f x)) := by
  let Ï := Î¼.withDensity f
  have : IsFiniteMeasure Ï := isFiniteMeasure_withDensity h'f
  filter_upwards [ae_tendsto_rnDeriv v Ï, rnDeriv_withDensity Î¼ hf] with x hx h'x
  rw [â† h'x]
  apply hx.congr' _
  filter_upwards [v.eventually_filterAt_measurableSet x] with a ha
  rw [â† withDensity_apply f ha]

theorem ae_tendsto_lintegral_div {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (h'f : (âˆ«â» y, f y âˆ‚Î¼) â‰  âˆ) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => (âˆ«â» y in a, f y âˆ‚Î¼) / Î¼ a) (v.filterAt x) (ğ“ (f x)) := by
  have A : (âˆ«â» y, hf.mk f y âˆ‚Î¼) â‰  âˆ := by
    convert h'f using 1
    apply lintegral_congr_ae
    exact hf.ae_eq_mk.symm
  filter_upwards [v.ae_tendsto_lintegral_div' hf.measurable_mk A, hf.ae_eq_mk] with x hx h'x
  rw [h'x]
  convert hx using 1
  ext1 a
  congr 1
  apply lintegral_congr_ae
  exact ae_restrict_of_ae hf.ae_eq_mk

theorem ae_tendsto_lintegral_enorm_sub_div'_of_integrable {f : Î± â†’ E} (hf : Integrable f Î¼)
    (h'f : StronglyMeasurable f) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => (âˆ«â» y in a, â€–f y - f xâ€–â‚‘ âˆ‚Î¼) / Î¼ a) (v.filterAt x) (ğ“ 0) := by
  /- For every `c`, then `(âˆ«â» y in a, â€–f y - câ€–â‚‘ âˆ‚Î¼) / Î¼ a` tends almost everywhere to `â€–f x - câ€–`.
    We apply this to a countable set of `c` which is dense in the range of `f`, to deduce the
    desired convergence.
    A minor technical inconvenience is that constants are not integrable, so to apply previous
    lemmas we need to replace `c` with the restriction of `c` to a finite measure set `A n` in the
    above sketch. -/
  let A := MeasureTheory.Measure.finiteSpanningSetsInOpen' Î¼
  rcases h'f.isSeparable_range with âŸ¨t, t_count, htâŸ©
  have main :
    âˆ€áµ x âˆ‚Î¼,
      âˆ€áµ‰ (n : â„•) (c âˆˆ t),
        Tendsto (fun a => (âˆ«â» y in a, â€–f y - (A.set n).indicator (fun _ => c) yâ€–â‚‘ âˆ‚Î¼) / Î¼ a)
          (v.filterAt x) (ğ“ â€–f x - (A.set n).indicator (fun _ => c) xâ€–â‚‘) := by
    simp_rw [ae_all_iff, ae_ball_iff t_count]
    intro n c _
    apply ae_tendsto_lintegral_div'
    Â· refine (h'f.sub ?_).enorm
      exact stronglyMeasurable_const.indicator (IsOpen.measurableSet (A.set_mem n))
    Â· apply ne_of_lt
      calc
        âˆ«â» y, â€–f y - (A.set n).indicator (fun _ : Î± => c) yâ€–â‚‘ âˆ‚Î¼
          â‰¤ âˆ«â» y, â€–f yâ€–â‚‘ + â€–(A.set n).indicator (fun _ : Î± => c) yâ€–â‚‘ âˆ‚Î¼ :=
          lintegral_mono fun x â†¦ enorm_sub_le
        _ = âˆ«â» y, â€–f yâ€–â‚‘ âˆ‚Î¼ + âˆ«â» y, â€–(A.set n).indicator (fun _ : Î± => c) yâ€–â‚‘ âˆ‚Î¼ :=
          lintegral_add_left h'f.enorm _
        _ < âˆ + âˆ :=
          haveI I : Integrable ((A.set n).indicator fun _ : Î± => c) Î¼ := by
            simp only [integrable_indicator_iff (IsOpen.measurableSet (A.set_mem n)),
              integrableOn_const_iff (C := c), A.finite n, or_true]
          ENNReal.add_lt_add hf.2 I.2
  filter_upwards [main, v.ae_eventually_measure_pos] with x hx h'x
  have M c (hc : c âˆˆ t) :
      Tendsto (fun a => (âˆ«â» y in a, â€–f y - câ€–â‚‘ âˆ‚Î¼) / Î¼ a) (v.filterAt x) (ğ“ â€–f x - câ€–â‚‘) := by
    obtain âŸ¨n, xnâŸ© : âˆƒ n, x âˆˆ A.set n := by simpa [â† A.spanning] using mem_univ x
    specialize hx n c hc
    simp only [xn, indicator_of_mem] at hx
    apply hx.congr' _
    filter_upwards [v.eventually_filterAt_subset_of_nhds (IsOpen.mem_nhds (A.set_mem n) xn),
      v.eventually_filterAt_measurableSet x] with a ha h'a
    congr 1
    apply setLIntegral_congr_fun h'a (fun y hy â†¦ ?_)
    simp only [ha hy, indicator_of_mem]
  apply ENNReal.tendsto_nhds_zero.2 fun Îµ Îµpos => ?_
  obtain âŸ¨c, ct, xcâŸ© : âˆƒ c âˆˆ t, â€–f x - câ€–â‚‘ < Îµ / 2 := by
    simp_rw [â† edist_eq_enorm_sub]
    have : f x âˆˆ closure t := ht (mem_range_self _)
    exact EMetric.mem_closure_iff.1 this (Îµ / 2) (ENNReal.half_pos (ne_of_gt Îµpos))
  filter_upwards [(tendsto_order.1 (M c ct)).2 (Îµ / 2) xc, h'x, v.eventually_measure_lt_top x] with
    a ha h'a h''a
  apply ENNReal.div_le_of_le_mul
  calc
    (âˆ«â» y in a, â€–f y - f xâ€–â‚‘ âˆ‚Î¼) â‰¤ âˆ«â» y in a, â€–f y - câ€–â‚‘ + â€–f x - câ€–â‚‘ âˆ‚Î¼ := by
      apply lintegral_mono fun x => ?_
      simpa only [â† edist_eq_enorm_sub] using edist_triangle_right _ _ _
    _ = (âˆ«â» y in a, â€–f y - câ€–â‚‘ âˆ‚Î¼) + âˆ«â» _ in a, â€–f x - câ€–â‚‘ âˆ‚Î¼ :=
      (lintegral_add_right _ measurable_const)
    _ â‰¤ Îµ / 2 * Î¼ a + Îµ / 2 * Î¼ a := by
      gcongr
      Â· rw [ENNReal.div_lt_iff (Or.inl h'a.ne') (Or.inl h''a.ne)] at ha
        exact ha.le
      Â· simp only [lintegral_const, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
        gcongr
    _ = Îµ * Î¼ a := by rw [â† add_mul, ENNReal.add_halves]

theorem ae_tendsto_lintegral_enorm_sub_div_of_integrable {f : Î± â†’ E} (hf : Integrable f Î¼) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => (âˆ«â» y in a, â€–f y - f xâ€–â‚‘ âˆ‚Î¼) / Î¼ a) (v.filterAt x) (ğ“ 0) := by
  have I : Integrable (hf.1.mk f) Î¼ := hf.congr hf.1.ae_eq_mk
  filter_upwards [v.ae_tendsto_lintegral_enorm_sub_div'_of_integrable I hf.1.stronglyMeasurable_mk,
    hf.1.ae_eq_mk] with x hx h'x
  apply hx.congr _
  intro a
  congr 1
  apply lintegral_congr_ae
  apply ae_restrict_of_ae
  filter_upwards [hf.1.ae_eq_mk] with y hy
  rw [hy, h'x]

theorem ae_tendsto_lintegral_enorm_sub_div {f : Î± â†’ E} (hf : LocallyIntegrable f Î¼) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => (âˆ«â» y in a, â€–f y - f xâ€–â‚‘ âˆ‚Î¼) / Î¼ a) (v.filterAt x) (ğ“ 0) := by
  rcases hf.exists_nat_integrableOn with âŸ¨u, u_open, u_univ, huâŸ©
  have : âˆ€ n, âˆ€áµ x âˆ‚Î¼,
      Tendsto (fun a => (âˆ«â» y in a, â€–(u n).indicator f y - (u n).indicator f xâ€–â‚‘ âˆ‚Î¼) / Î¼ a)
      (v.filterAt x) (ğ“ 0) := by
    intro n
    apply ae_tendsto_lintegral_enorm_sub_div_of_integrable
    exact (integrable_indicator_iff (u_open n).measurableSet).2 (hu n)
  filter_upwards [ae_all_iff.2 this] with x hx
  obtain âŸ¨n, hnâŸ© : âˆƒ n, x âˆˆ u n := by simpa only [â† u_univ, mem_iUnion] using mem_univ x
  apply Tendsto.congr' _ (hx n)
  filter_upwards [v.eventually_filterAt_subset_of_nhds ((u_open n).mem_nhds hn),
    v.eventually_filterAt_measurableSet x] with a ha h'a
  congr 1
  refine setLIntegral_congr_fun h'a (fun y hy â†¦ ?_)
  rw [indicator_of_mem (ha hy) f, indicator_of_mem hn f]

/-- *Lebesgue differentiation theorem*: for almost every point `x`, the
average of `â€–f y - f xâ€–` on `a` tends to `0` as `a` shrinks to `x` along a Vitali family. -/
theorem ae_tendsto_average_norm_sub {f : Î± â†’ E} (hf : LocallyIntegrable f Î¼) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => â¨ y in a, â€–f y - f xâ€– âˆ‚Î¼) (v.filterAt x) (ğ“ 0) := by
  filter_upwards [v.ae_tendsto_lintegral_enorm_sub_div hf] with x hx
  have := (ENNReal.tendsto_toReal ENNReal.zero_ne_top).comp hx
  simp only [ENNReal.toReal_zero] at this
  apply Tendsto.congr' _ this
  filter_upwards [v.eventually_measure_lt_top x, v.eventually_filterAt_integrableOn x hf]
    with a h'a h''a
  simp only [Function.comp_apply, ENNReal.toReal_div, setAverage_eq, div_eq_inv_mul]
  have A : IntegrableOn (fun y => (â€–f y - f xâ€–â‚Š : â„)) a Î¼ := by
    simp_rw [coe_nnnorm]
    exact (h''a.sub (integrableOn_const h'a.ne)).norm
  dsimp [enorm]
  rw [lintegral_coe_eq_integral _ A, ENNReal.toReal_ofReal (by positivity)]
  simp only [coe_nnnorm, measureReal_def]

/-- *Lebesgue differentiation theorem*: for almost every point `x`, the
average of `f` on `a` tends to `f x` as `a` shrinks to `x` along a Vitali family. -/
theorem ae_tendsto_average [NormedSpace â„ E] [CompleteSpace E] {f : Î± â†’ E}
    (hf : LocallyIntegrable f Î¼) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun a => â¨ y in a, f y âˆ‚Î¼) (v.filterAt x) (ğ“ (f x)) := by
  filter_upwards [v.ae_tendsto_average_norm_sub hf, v.ae_eventually_measure_pos] with x hx h'x
  rw [tendsto_iff_norm_sub_tendsto_zero]
  refine squeeze_zero' (Eventually.of_forall fun a => norm_nonneg _) ?_ hx
  filter_upwards [h'x, v.eventually_measure_lt_top x, v.eventually_filterAt_integrableOn x hf]
    with a ha h'a h''a
  nth_rw 1 [â† setAverage_const ha.ne' h'a.ne (f x)]
  simp_rw [setAverage_eq']
  rw [â† integral_sub]
  Â· exact norm_integral_le_integral_norm _
  Â· exact (integrable_inv_smul_measure ha.ne' h'a.ne).2 h''a
  Â· exact (integrable_inv_smul_measure ha.ne' h'a.ne).2 (integrableOn_const h'a.ne)

end

end VitaliFamily
