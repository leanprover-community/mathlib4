/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.Data.Finset.Update
import Mathlib.Data.Prod.TProd
import Mathlib.GroupTheory.Coset
import Mathlib.Logic.Equiv.Fin
import Mathlib.MeasureTheory.MeasurableSpace.Instances
import Mathlib.Order.LiminfLimsup
import Mathlib.Data.Set.UnionLift
import Mathlib.Order.Filter.SmallSets

/-!
# Measurable spaces and measurable functions

This file provides properties of measurable spaces and the functions and isomorphisms between them.
The definition of a measurable space is in `Mathlib/MeasureTheory/MeasurableSpace/Defs.lean`.

A measurable space is a set equipped with a Ïƒ-algebra, a collection of
subsets closed under complementation and countable union. A function
between measurable spaces is measurable if the preimage of each
measurable subset is measurable.

Ïƒ-algebras on a fixed set `Î±` form a complete lattice. Here we order
Ïƒ-algebras by writing `mâ‚ â‰¤ mâ‚‚` if every set which is `mâ‚`-measurable is
also `mâ‚‚`-measurable (that is, `mâ‚` is a subset of `mâ‚‚`). In particular, any
collection of subsets of `Î±` generates a smallest Ïƒ-algebra which
contains all of them. A function `f : Î± â†’ Î²` induces a Galois connection
between the lattices of Ïƒ-algebras on `Î±` and `Î²`.

We say that a filter `f` is measurably generated if every set `s âˆˆ f` includes a measurable
set `t âˆˆ f`. This property is useful, e.g., to extract a measurable witness of `Filter.Eventually`.

## Implementation notes

Measurability of a function `f : Î± â†’ Î²` between measurable spaces is
defined in terms of the Galois connection induced by f.

## References

* <https://en.wikipedia.org/wiki/Measurable_space>
* <https://en.wikipedia.org/wiki/Sigma-algebra>
* <https://en.wikipedia.org/wiki/Dynkin_system>

## Tags

measurable space, Ïƒ-algebra, measurable function, dynkin system, Ï€-Î» theorem, Ï€-system
-/


open Set Encodable Function Equiv Filter MeasureTheory

universe uÎ¹

variable {Î± Î² Î³ Î´ Î´' : Type*} {Î¹ : Sort uÎ¹} {s t u : Set Î±}

namespace MeasurableSpace

section Functors

variable {m mâ‚ mâ‚‚ : MeasurableSpace Î±} {m' : MeasurableSpace Î²} {f : Î± â†’ Î²} {g : Î² â†’ Î±}

/-- The forward image of a measurable space under a function. `map f m` contains the sets
  `s : Set Î²` whose preimage under `f` is measurable. -/
protected def map (f : Î± â†’ Î²) (m : MeasurableSpace Î±) : MeasurableSpace Î² where
  MeasurableSet' s := MeasurableSet[m] <| f â»Â¹' s
  measurableSet_empty := m.measurableSet_empty
  measurableSet_compl s hs := m.measurableSet_compl _ hs
  measurableSet_iUnion f hf := by simpa only [preimage_iUnion] using m.measurableSet_iUnion _ hf

lemma map_def {s : Set Î²} : MeasurableSet[m.map f] s â†” MeasurableSet[m] (f â»Â¹' s) := Iff.rfl

@[simp]
theorem map_id : m.map id = m :=
  MeasurableSpace.ext fun _ => Iff.rfl

@[simp]
theorem map_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} : (m.map f).map g = m.map (g âˆ˜ f) :=
  MeasurableSpace.ext fun _ => Iff.rfl

/-- The reverse image of a measurable space under a function. `comap f m` contains the sets
  `s : Set Î±` such that `s` is the `f`-preimage of a measurable set in `Î²`. -/
protected def comap (f : Î± â†’ Î²) (m : MeasurableSpace Î²) : MeasurableSpace Î± where
  MeasurableSet' s := âˆƒ s', MeasurableSet[m] s' âˆ§ f â»Â¹' s' = s
  measurableSet_empty := âŸ¨âˆ…, m.measurableSet_empty, rflâŸ©
  measurableSet_compl := fun s âŸ¨s', hâ‚, hâ‚‚âŸ© => âŸ¨s'á¶œ, m.measurableSet_compl _ hâ‚, hâ‚‚ â–¸ rflâŸ©
  measurableSet_iUnion s hs :=
    let âŸ¨s', hs'âŸ© := Classical.axiom_of_choice hs
    âŸ¨â‹ƒ i, s' i, m.measurableSet_iUnion _ fun i => (hs' i).left, by simp [hs']âŸ©

theorem comap_eq_generateFrom (m : MeasurableSpace Î²) (f : Î± â†’ Î²) :
    m.comap f = generateFrom { t | âˆƒ s, MeasurableSet s âˆ§ f â»Â¹' s = t } :=
  (@generateFrom_measurableSet _ (.comap f m)).symm

@[simp]
theorem comap_id : m.comap id = m :=
  MeasurableSpace.ext fun s => âŸ¨fun âŸ¨_, hs', hâŸ© => h â–¸ hs', fun h => âŸ¨s, h, rflâŸ©âŸ©

@[simp]
theorem comap_comp {f : Î² â†’ Î±} {g : Î³ â†’ Î²} : (m.comap f).comap g = m.comap (f âˆ˜ g) :=
  MeasurableSpace.ext fun _ =>
    âŸ¨fun âŸ¨_, âŸ¨u, h, huâŸ©, htâŸ© => âŸ¨u, h, ht â–¸ hu â–¸ rflâŸ©, fun âŸ¨t, h, htâŸ© => âŸ¨f â»Â¹' t, âŸ¨_, h, rflâŸ©, htâŸ©âŸ©

theorem comap_le_iff_le_map {f : Î± â†’ Î²} : m'.comap f â‰¤ m â†” m' â‰¤ m.map f :=
  âŸ¨fun h _s hs => h _ âŸ¨_, hs, rflâŸ©, fun h _s âŸ¨_t, ht, heqâŸ© => heq â–¸ h _ htâŸ©

theorem gc_comap_map (f : Î± â†’ Î²) :
    GaloisConnection (MeasurableSpace.comap f) (MeasurableSpace.map f) := fun _ _ =>
  comap_le_iff_le_map

theorem map_mono (h : mâ‚ â‰¤ mâ‚‚) : mâ‚.map f â‰¤ mâ‚‚.map f :=
  (gc_comap_map f).monotone_u h

theorem monotone_map : Monotone (MeasurableSpace.map f) := fun _ _ => map_mono

theorem comap_mono (h : mâ‚ â‰¤ mâ‚‚) : mâ‚.comap g â‰¤ mâ‚‚.comap g :=
  (gc_comap_map g).monotone_l h

theorem monotone_comap : Monotone (MeasurableSpace.comap g) := fun _ _ h => comap_mono h

@[simp]
theorem comap_bot : (âŠ¥ : MeasurableSpace Î±).comap g = âŠ¥ :=
  (gc_comap_map g).l_bot

@[simp]
theorem comap_sup : (mâ‚ âŠ” mâ‚‚).comap g = mâ‚.comap g âŠ” mâ‚‚.comap g :=
  (gc_comap_map g).l_sup

@[simp]
theorem comap_iSup {m : Î¹ â†’ MeasurableSpace Î±} : (â¨† i, m i).comap g = â¨† i, (m i).comap g :=
  (gc_comap_map g).l_iSup

@[simp]
theorem map_top : (âŠ¤ : MeasurableSpace Î±).map f = âŠ¤ :=
  (gc_comap_map f).u_top

@[simp]
theorem map_inf : (mâ‚ âŠ“ mâ‚‚).map f = mâ‚.map f âŠ“ mâ‚‚.map f :=
  (gc_comap_map f).u_inf

@[simp]
theorem map_iInf {m : Î¹ â†’ MeasurableSpace Î±} : (â¨… i, m i).map f = â¨… i, (m i).map f :=
  (gc_comap_map f).u_iInf

theorem comap_map_le : (m.map f).comap f â‰¤ m :=
  (gc_comap_map f).l_u_le _

theorem le_map_comap : m â‰¤ (m.comap g).map g :=
  (gc_comap_map g).le_u_l _

end Functors

@[simp] theorem map_const {m} (b : Î²) : MeasurableSpace.map (fun _a : Î± â†¦ b) m = âŠ¤ :=
  eq_top_iff.2 <| fun s _ â†¦ by rw [map_def]; by_cases h : b âˆˆ s <;> simp [h]

@[simp] theorem comap_const {m} (b : Î²) : MeasurableSpace.comap (fun _a : Î± => b) m = âŠ¥ :=
  eq_bot_iff.2 <| by rintro _ âŸ¨s, -, rflâŸ©; by_cases b âˆˆ s <;> simp [*]

theorem comap_generateFrom {f : Î± â†’ Î²} {s : Set (Set Î²)} :
    (generateFrom s).comap f = generateFrom (preimage f '' s) :=
  le_antisymm
    (comap_le_iff_le_map.2 <|
      generateFrom_le fun _t hts => GenerateMeasurable.basic _ <| mem_image_of_mem _ <| hts)
    (generateFrom_le fun _t âŸ¨u, hu, EqâŸ© => Eq â–¸ âŸ¨u, GenerateMeasurable.basic _ hu, rflâŸ©)

end MeasurableSpace

section MeasurableFunctions

open MeasurableSpace

theorem measurable_iff_le_map {mâ‚ : MeasurableSpace Î±} {mâ‚‚ : MeasurableSpace Î²} {f : Î± â†’ Î²} :
    Measurable f â†” mâ‚‚ â‰¤ mâ‚.map f :=
  Iff.rfl

alias âŸ¨Measurable.le_map, Measurable.of_le_mapâŸ© := measurable_iff_le_map

theorem measurable_iff_comap_le {mâ‚ : MeasurableSpace Î±} {mâ‚‚ : MeasurableSpace Î²} {f : Î± â†’ Î²} :
    Measurable f â†” mâ‚‚.comap f â‰¤ mâ‚ :=
  comap_le_iff_le_map.symm

alias âŸ¨Measurable.comap_le, Measurable.of_comap_leâŸ© := measurable_iff_comap_le

theorem comap_measurable {m : MeasurableSpace Î²} (f : Î± â†’ Î²) : Measurable[m.comap f] f :=
  fun s hs => âŸ¨s, hs, rflâŸ©

theorem Measurable.mono {ma ma' : MeasurableSpace Î±} {mb mb' : MeasurableSpace Î²} {f : Î± â†’ Î²}
    (hf : @Measurable Î± Î² ma mb f) (ha : ma â‰¤ ma') (hb : mb' â‰¤ mb) : @Measurable Î± Î² ma' mb' f :=
  fun _t ht => ha _ <| hf <| hb _ ht

theorem measurable_id'' {m mÎ± : MeasurableSpace Î±} (hm : m â‰¤ mÎ±) : @Measurable Î± Î± mÎ± m id :=
  measurable_id.mono le_rfl hm

-- Porting note (#11215): TODO: add TC `DiscreteMeasurable` + instances

@[measurability]
theorem measurable_from_top [MeasurableSpace Î²] {f : Î± â†’ Î²} : Measurable[âŠ¤] f := fun _ _ => trivial

theorem measurable_generateFrom [MeasurableSpace Î±] {s : Set (Set Î²)} {f : Î± â†’ Î²}
    (h : âˆ€ t âˆˆ s, MeasurableSet (f â»Â¹' t)) : @Measurable _ _ _ (generateFrom s) f :=
  Measurable.of_le_map <| generateFrom_le h

variable {f g : Î± â†’ Î²}

section TypeclassMeasurableSpace

variable [MeasurableSpace Î±] [MeasurableSpace Î²] [MeasurableSpace Î³]

@[nontriviality, measurability]
theorem Subsingleton.measurable [Subsingleton Î±] : Measurable f := fun _ _ =>
  @Subsingleton.measurableSet Î± _ _ _

@[nontriviality, measurability]
theorem measurable_of_subsingleton_codomain [Subsingleton Î²] (f : Î± â†’ Î²) : Measurable f :=
  fun s _ => Subsingleton.set_cases MeasurableSet.empty MeasurableSet.univ s

@[to_additive (attr := measurability)]
theorem measurable_one [One Î±] : Measurable (1 : Î² â†’ Î±) :=
  @measurable_const _ _ _ _ 1

theorem measurable_of_empty [IsEmpty Î±] (f : Î± â†’ Î²) : Measurable f :=
  Subsingleton.measurable

theorem measurable_of_empty_codomain [IsEmpty Î²] (f : Î± â†’ Î²) : Measurable f :=
  measurable_of_subsingleton_codomain f

/-- A version of `measurable_const` that assumes `f x = f y` for all `x, y`. This version works
for functions between empty types. -/
theorem measurable_const' {f : Î² â†’ Î±} (hf : âˆ€ x y, f x = f y) : Measurable f := by
  nontriviality Î²
  inhabit Î²
  convert @measurable_const Î± Î² _ _ (f default) using 2
  apply hf

@[measurability]
theorem measurable_natCast [NatCast Î±] (n : â„•) : Measurable (n : Î² â†’ Î±) :=
  @measurable_const Î± _ _ _ n

@[measurability]
theorem measurable_intCast [IntCast Î±] (n : â„¤) : Measurable (n : Î² â†’ Î±) :=
  @measurable_const Î± _ _ _ n

theorem measurable_of_countable [Countable Î±] [MeasurableSingletonClass Î±] (f : Î± â†’ Î²) :
    Measurable f := fun s _ =>
  (f â»Â¹' s).to_countable.measurableSet

theorem measurable_of_finite [Finite Î±] [MeasurableSingletonClass Î±] (f : Î± â†’ Î²) : Measurable f :=
  measurable_of_countable f

end TypeclassMeasurableSpace

variable {m : MeasurableSpace Î±}

@[measurability]
theorem Measurable.iterate {f : Î± â†’ Î±} (hf : Measurable f) : âˆ€ n, Measurable f^[n]
  | 0 => measurable_id
  | n + 1 => (Measurable.iterate hf n).comp hf

variable {mÎ² : MeasurableSpace Î²}

@[measurability]
theorem measurableSet_preimage {t : Set Î²} (hf : Measurable f) (ht : MeasurableSet t) :
    MeasurableSet (f â»Â¹' t) :=
  hf ht

protected theorem MeasurableSet.preimage {t : Set Î²} (ht : MeasurableSet t) (hf : Measurable f) :
    MeasurableSet (f â»Â¹' t) :=
  hf ht

@[measurability]
protected theorem Measurable.piecewise {_ : DecidablePred (Â· âˆˆ s)} (hs : MeasurableSet s)
    (hf : Measurable f) (hg : Measurable g) : Measurable (piecewise s f g) := by
  intro t ht
  rw [piecewise_preimage]
  exact hs.ite (hf ht) (hg ht)

/-- This is slightly different from `Measurable.piecewise`. It can be used to show
`Measurable (ite (x=0) 0 1)` by
`exact Measurable.ite (measurableSet_singleton 0) measurable_const measurable_const`,
but replacing `Measurable.ite` by `Measurable.piecewise` in that example proof does not work. -/
theorem Measurable.ite {p : Î± â†’ Prop} {_ : DecidablePred p} (hp : MeasurableSet { a : Î± | p a })
    (hf : Measurable f) (hg : Measurable g) : Measurable fun x => ite (p x) (f x) (g x) :=
  Measurable.piecewise hp hf hg

@[measurability, fun_prop]
theorem Measurable.indicator [Zero Î²] (hf : Measurable f) (hs : MeasurableSet s) :
    Measurable (s.indicator f) :=
  hf.piecewise hs measurable_const

/-- The measurability of a set `A` is equivalent to the measurability of the indicator function
which takes a constant value `b â‰  0` on a set `A` and `0` elsewhere. -/
lemma measurable_indicator_const_iff [Zero Î²] [MeasurableSingletonClass Î²] (b : Î²) [NeZero b] :
    Measurable (s.indicator (fun (_ : Î±) â†¦ b)) â†” MeasurableSet s := by
  constructor <;> intro h
  Â· convert h (MeasurableSet.singleton (0 : Î²)).compl
    ext a
    simp [NeZero.ne b]
  Â· exact measurable_const.indicator h

@[to_additive (attr := measurability)]
theorem measurableSet_mulSupport [One Î²] [MeasurableSingletonClass Î²] (hf : Measurable f) :
    MeasurableSet (mulSupport f) :=
  hf (measurableSet_singleton 1).compl

/-- If a function coincides with a measurable function outside of a countable set, it is
measurable. -/
theorem Measurable.measurable_of_countable_ne [MeasurableSingletonClass Î±] (hf : Measurable f)
    (h : Set.Countable { x | f x â‰  g x }) : Measurable g := by
  intro t ht
  have : g â»Â¹' t = g â»Â¹' t âˆ© { x | f x = g x }á¶œ âˆª g â»Â¹' t âˆ© { x | f x = g x } := by
    simp [â† inter_union_distrib_left]
  rw [this]
  refine (h.mono inter_subset_right).measurableSet.union ?_
  have : g â»Â¹' t âˆ© { x : Î± | f x = g x } = f â»Â¹' t âˆ© { x : Î± | f x = g x } := by
    ext x
    simp (config := { contextual := true })
  rw [this]
  exact (hf ht).inter h.measurableSet.of_compl

end MeasurableFunctions

section Constructions

theorem measurable_to_countable [MeasurableSpace Î±] [Countable Î±] [MeasurableSpace Î²] {f : Î² â†’ Î±}
    (h : âˆ€ y, MeasurableSet (f â»Â¹' {f y})) : Measurable f := fun s _ => by
  rw [â† biUnion_preimage_singleton]
  refine MeasurableSet.iUnion fun y => MeasurableSet.iUnion fun hy => ?_
  by_cases hyf : y âˆˆ range f
  Â· rcases hyf with âŸ¨y, rflâŸ©
    apply h
  Â· simp only [preimage_singleton_eq_empty.2 hyf, MeasurableSet.empty]

theorem measurable_to_countable' [MeasurableSpace Î±] [Countable Î±] [MeasurableSpace Î²] {f : Î² â†’ Î±}
    (h : âˆ€ x, MeasurableSet (f â»Â¹' {x})) : Measurable f :=
  measurable_to_countable fun y => h (f y)

@[measurability]
theorem measurable_unit [MeasurableSpace Î±] (f : Unit â†’ Î±) : Measurable f :=
  measurable_from_top

section ULift
variable [MeasurableSpace Î±]

instance _root_.ULift.instMeasurableSpace : MeasurableSpace (ULift Î±) :=
  â€¹MeasurableSpace Î±â€º.map ULift.up

lemma measurable_down : Measurable (ULift.down : ULift Î± â†’ Î±) := fun _ â†¦ id
lemma measurable_up : Measurable (ULift.up : Î± â†’ ULift Î±) := fun _ â†¦ id

@[simp] lemma measurableSet_preimage_down {s : Set Î±} :
    MeasurableSet (ULift.down â»Â¹' s) â†” MeasurableSet s := Iff.rfl
@[simp] lemma measurableSet_preimage_up {s : Set (ULift Î±)} :
    MeasurableSet (ULift.up â»Â¹' s) â†” MeasurableSet s := Iff.rfl

end ULift

section Nat

variable [MeasurableSpace Î±]

@[measurability]
theorem measurable_from_nat {f : â„• â†’ Î±} : Measurable f :=
  measurable_from_top

theorem measurable_to_nat {f : Î± â†’ â„•} : (âˆ€ y, MeasurableSet (f â»Â¹' {f y})) â†’ Measurable f :=
  measurable_to_countable

theorem measurable_to_bool {f : Î± â†’ Bool} (h : MeasurableSet (f â»Â¹' {true})) : Measurable f := by
  apply measurable_to_countable'
  rintro (- | -)
  Â· convert h.compl
    rw [â† preimage_compl, Bool.compl_singleton, Bool.not_true]
  exact h

theorem measurable_to_prop {f : Î± â†’ Prop} (h : MeasurableSet (f â»Â¹' {True})) : Measurable f := by
  refine measurable_to_countable' fun x => ?_
  by_cases hx : x
  Â· simpa [hx] using h
  Â· simpa only [hx, â† preimage_compl, Prop.compl_singleton, not_true, preimage_singleton_false]
      using h.compl

theorem measurable_findGreatest' {p : Î± â†’ â„• â†’ Prop} [âˆ€ x, DecidablePred (p x)] {N : â„•}
    (hN : âˆ€ k â‰¤ N, MeasurableSet { x | Nat.findGreatest (p x) N = k }) :
    Measurable fun x => Nat.findGreatest (p x) N :=
  measurable_to_nat fun _ => hN _ N.findGreatest_le

theorem measurable_findGreatest {p : Î± â†’ â„• â†’ Prop} [âˆ€ x, DecidablePred (p x)] {N}
    (hN : âˆ€ k â‰¤ N, MeasurableSet { x | p x k }) : Measurable fun x => Nat.findGreatest (p x) N := by
  refine measurable_findGreatest' fun k hk => ?_
  simp only [Nat.findGreatest_eq_iff, setOf_and, setOf_forall, â† compl_setOf]
  repeat' apply_rules [MeasurableSet.inter, MeasurableSet.const, MeasurableSet.iInter,
    MeasurableSet.compl, hN] <;> try intros

theorem measurable_find {p : Î± â†’ â„• â†’ Prop} [âˆ€ x, DecidablePred (p x)] (hp : âˆ€ x, âˆƒ N, p x N)
    (hm : âˆ€ k, MeasurableSet { x | p x k }) : Measurable fun x => Nat.find (hp x) := by
  refine measurable_to_nat fun x => ?_
  rw [preimage_find_eq_disjointed (fun k => {x | p x k})]
  exact MeasurableSet.disjointed hm _

end Nat

section Quotient

variable [MeasurableSpace Î±] [MeasurableSpace Î²]

instance Quot.instMeasurableSpace {Î±} {r : Î± â†’ Î± â†’ Prop} [m : MeasurableSpace Î±] :
    MeasurableSpace (Quot r) :=
  m.map (Quot.mk r)

instance Quotient.instMeasurableSpace {Î±} {s : Setoid Î±} [m : MeasurableSpace Î±] :
    MeasurableSpace (Quotient s) :=
  m.map Quotient.mk''

@[to_additive]
instance QuotientGroup.measurableSpace {G} [Group G] [MeasurableSpace G] (S : Subgroup G) :
    MeasurableSpace (G â§¸ S) :=
  Quotient.instMeasurableSpace

theorem measurableSet_quotient {s : Setoid Î±} {t : Set (Quotient s)} :
    MeasurableSet t â†” MeasurableSet (Quotient.mk'' â»Â¹' t) :=
  Iff.rfl

theorem measurable_from_quotient {s : Setoid Î±} {f : Quotient s â†’ Î²} :
    Measurable f â†” Measurable (f âˆ˜ Quotient.mk'') :=
  Iff.rfl

@[measurability]
theorem measurable_quotient_mk' [s : Setoid Î±] : Measurable (Quotient.mk' : Î± â†’ Quotient s) :=
  fun _ => id

@[measurability]
theorem measurable_quotient_mk'' {s : Setoid Î±} : Measurable (Quotient.mk'' : Î± â†’ Quotient s) :=
  fun _ => id

@[measurability]
theorem measurable_quot_mk {r : Î± â†’ Î± â†’ Prop} : Measurable (Quot.mk r) := fun _ => id

@[to_additive (attr := measurability)]
theorem QuotientGroup.measurable_coe {G} [Group G] [MeasurableSpace G] {S : Subgroup G} :
    Measurable ((â†‘) : G â†’ G â§¸ S) :=
  measurable_quotient_mk''

@[to_additive]
nonrec theorem QuotientGroup.measurable_from_quotient {G} [Group G] [MeasurableSpace G]
    {S : Subgroup G} {f : G â§¸ S â†’ Î±} : Measurable f â†” Measurable (f âˆ˜ ((â†‘) : G â†’ G â§¸ S)) :=
  measurable_from_quotient

end Quotient

section Subtype

instance Subtype.instMeasurableSpace {Î±} {p : Î± â†’ Prop} [m : MeasurableSpace Î±] :
    MeasurableSpace (Subtype p) :=
  m.comap ((â†‘) : _ â†’ Î±)

section

variable [MeasurableSpace Î±]

@[measurability]
theorem measurable_subtype_coe {p : Î± â†’ Prop} : Measurable ((â†‘) : Subtype p â†’ Î±) :=
  MeasurableSpace.le_map_comap

instance Subtype.instMeasurableSingletonClass {p : Î± â†’ Prop} [MeasurableSingletonClass Î±] :
    MeasurableSingletonClass (Subtype p) where
  measurableSet_singleton x :=
    âŸ¨{(x : Î±)}, measurableSet_singleton (x : Î±), by
      rw [â† image_singleton, preimage_image_eq _ Subtype.val_injective]âŸ©

end

variable {m : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}

theorem MeasurableSet.of_subtype_image {s : Set Î±} {t : Set s}
    (h : MeasurableSet (Subtype.val '' t)) : MeasurableSet t :=
  âŸ¨_, h, preimage_image_eq _ Subtype.val_injectiveâŸ©

theorem MeasurableSet.subtype_image {s : Set Î±} {t : Set s} (hs : MeasurableSet s) :
    MeasurableSet t â†’ MeasurableSet (((â†‘) : s â†’ Î±) '' t) := by
  rintro âŸ¨u, hu, rflâŸ©
  rw [Subtype.image_preimage_coe]
  exact hs.inter hu

@[measurability]
theorem Measurable.subtype_coe {p : Î² â†’ Prop} {f : Î± â†’ Subtype p} (hf : Measurable f) :
    Measurable fun a : Î± => (f a : Î²) :=
  measurable_subtype_coe.comp hf

alias Measurable.subtype_val := Measurable.subtype_coe

@[measurability]
theorem Measurable.subtype_mk {p : Î² â†’ Prop} {f : Î± â†’ Î²} (hf : Measurable f) {h : âˆ€ x, p (f x)} :
    Measurable fun x => (âŸ¨f x, h xâŸ© : Subtype p) := fun t âŸ¨s, hsâŸ© =>
  hs.2 â–¸ by simp only [â† preimage_comp, (Â· âˆ˜ Â·), Subtype.coe_mk, hf hs.1]

@[measurability]
protected theorem Measurable.rangeFactorization {f : Î± â†’ Î²} (hf : Measurable f) :
    Measurable (rangeFactorization f) :=
  hf.subtype_mk

theorem Measurable.subtype_map {f : Î± â†’ Î²} {p : Î± â†’ Prop} {q : Î² â†’ Prop} (hf : Measurable f)
    (hpq : âˆ€ x, p x â†’ q (f x)) : Measurable (Subtype.map f hpq) :=
  (hf.comp measurable_subtype_coe).subtype_mk

theorem measurable_inclusion {s t : Set Î±} (h : s âŠ† t) : Measurable (inclusion h) :=
  measurable_id.subtype_map h

theorem MeasurableSet.image_inclusion' {s t : Set Î±} (h : s âŠ† t) {u : Set s}
    (hs : MeasurableSet (Subtype.val â»Â¹' s : Set t)) (hu : MeasurableSet u) :
    MeasurableSet (inclusion h '' u) := by
  rcases hu with âŸ¨u, hu, rflâŸ©
  convert (measurable_subtype_coe hu).inter hs
  ext âŸ¨x, hxâŸ©
  simpa [@and_comm _ (_ = x)] using and_comm

theorem MeasurableSet.image_inclusion {s t : Set Î±} (h : s âŠ† t) {u : Set s}
    (hs : MeasurableSet s) (hu : MeasurableSet u) :
    MeasurableSet (inclusion h '' u) :=
  (measurable_subtype_coe hs).image_inclusion' h hu

theorem MeasurableSet.of_union_cover {s t u : Set Î±} (hs : MeasurableSet s) (ht : MeasurableSet t)
    (h : univ âŠ† s âˆª t) (hsu : MeasurableSet (((â†‘) : s â†’ Î±) â»Â¹' u))
    (htu : MeasurableSet (((â†‘) : t â†’ Î±) â»Â¹' u)) : MeasurableSet u := by
  convert (hs.subtype_image hsu).union (ht.subtype_image htu)
  simp [image_preimage_eq_inter_range, â† inter_union_distrib_left, univ_subset_iff.1 h]

theorem measurable_of_measurable_union_cover {f : Î± â†’ Î²} (s t : Set Î±) (hs : MeasurableSet s)
    (ht : MeasurableSet t) (h : univ âŠ† s âˆª t) (hc : Measurable fun a : s => f a)
    (hd : Measurable fun a : t => f a) : Measurable f := fun _u hu =>
  .of_union_cover hs ht h (hc hu) (hd hu)

theorem measurable_of_restrict_of_restrict_compl {f : Î± â†’ Î²} {s : Set Î±} (hs : MeasurableSet s)
    (hâ‚ : Measurable (s.restrict f)) (hâ‚‚ : Measurable (sá¶œ.restrict f)) : Measurable f :=
  measurable_of_measurable_union_cover s sá¶œ hs hs.compl (union_compl_self s).ge hâ‚ hâ‚‚

theorem Measurable.dite [âˆ€ x, Decidable (x âˆˆ s)] {f : s â†’ Î²} (hf : Measurable f)
    {g : (sá¶œ : Set Î±) â†’ Î²} (hg : Measurable g) (hs : MeasurableSet s) :
    Measurable fun x => if hx : x âˆˆ s then f âŸ¨x, hxâŸ© else g âŸ¨x, hxâŸ© :=
  measurable_of_restrict_of_restrict_compl hs (by simpa) (by simpa)

theorem measurable_of_measurable_on_compl_finite [MeasurableSingletonClass Î±] {f : Î± â†’ Î²}
    (s : Set Î±) (hs : s.Finite) (hf : Measurable (sá¶œ.restrict f)) : Measurable f :=
  have := hs.to_subtype
  measurable_of_restrict_of_restrict_compl hs.measurableSet (measurable_of_finite _) hf

theorem measurable_of_measurable_on_compl_singleton [MeasurableSingletonClass Î±] {f : Î± â†’ Î²} (a : Î±)
    (hf : Measurable ({ x | x â‰  a }.restrict f)) : Measurable f :=
  measurable_of_measurable_on_compl_finite {a} (finite_singleton a) hf

end Subtype

section Atoms

variable [MeasurableSpace Î²]

/-- The *measurable atom* of `x` is the intersection of all the measurable sets countaining `x`.
It is measurable when the space is countable (or more generally when the measurable space is
countably generated). -/
def measurableAtom (x : Î²) : Set Î² :=
  â‹‚ (s : Set Î²) (_h's : x âˆˆ s) (_hs : MeasurableSet s), s

@[simp] lemma mem_measurableAtom_self (x : Î²) : x âˆˆ measurableAtom x := by
  simp (config := {contextual := true}) [measurableAtom]

lemma mem_of_mem_measurableAtom {x y : Î²} (h : y âˆˆ measurableAtom x) {s : Set Î²}
    (hs : MeasurableSet s) (hxs : x âˆˆ s) : y âˆˆ s := by
  simp only [measurableAtom, mem_iInter] at h
  exact h s hxs hs

lemma measurableAtom_subset {s : Set Î²} {x : Î²} (hs : MeasurableSet s) (hx : x âˆˆ s) :
    measurableAtom x âŠ† s :=
  iInterâ‚‚_subset_of_subset s hx fun â¦ƒaâ¦„ â†¦ (by simp [hs])

@[simp] lemma measurableAtom_of_measurableSingletonClass [MeasurableSingletonClass Î²] (x : Î²) :
    measurableAtom x = {x} :=
  Subset.antisymm (measurableAtom_subset (measurableSet_singleton x) rfl) (by simp)

lemma MeasurableSet.measurableAtom_of_countable [Countable Î²] (x : Î²) :
    MeasurableSet (measurableAtom x) := by
  have : âˆ€ (y : Î²), y âˆ‰ measurableAtom x â†’ âˆƒ s, x âˆˆ s âˆ§ MeasurableSet s âˆ§ y âˆ‰ s :=
    fun y hy â†¦ by simpa [measurableAtom] using hy
  choose! s hs using this
  have : measurableAtom x = â‹‚ (y âˆˆ (measurableAtom x)á¶œ), s y := by
    apply Subset.antisymm
    Â· intro z hz
      simp only [mem_iInter, mem_compl_iff]
      intro i hi
      show z âˆˆ s i
      exact mem_of_mem_measurableAtom hz (hs i hi).2.1 (hs i hi).1
    Â· apply compl_subset_compl.1
      intro z hz
      simp only [compl_iInter, mem_iUnion, mem_compl_iff, exists_prop]
      exact âŸ¨z, hz, (hs z hz).2.2âŸ©
  rw [this]
  exact MeasurableSet.biInter (to_countable (measurableAtom x)á¶œ) (fun i hi â†¦ (hs i hi).2.1)

end Atoms

section Prod

/-- A `MeasurableSpace` structure on the product of two measurable spaces. -/
def MeasurableSpace.prod {Î± Î²} (mâ‚ : MeasurableSpace Î±) (mâ‚‚ : MeasurableSpace Î²) :
    MeasurableSpace (Î± Ã— Î²) :=
  mâ‚.comap Prod.fst âŠ” mâ‚‚.comap Prod.snd

instance Prod.instMeasurableSpace {Î± Î²} [mâ‚ : MeasurableSpace Î±] [mâ‚‚ : MeasurableSpace Î²] :
    MeasurableSpace (Î± Ã— Î²) :=
  mâ‚.prod mâ‚‚

@[measurability]
theorem measurable_fst {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²} :
    Measurable (Prod.fst : Î± Ã— Î² â†’ Î±) :=
  Measurable.of_comap_le le_sup_left

@[measurability]
theorem measurable_snd {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²} :
    Measurable (Prod.snd : Î± Ã— Î² â†’ Î²) :=
  Measurable.of_comap_le le_sup_right

variable {m : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²} {mÎ³ : MeasurableSpace Î³}

@[fun_prop]
theorem Measurable.fst {f : Î± â†’ Î² Ã— Î³} (hf : Measurable f) : Measurable fun a : Î± => (f a).1 :=
  measurable_fst.comp hf

@[fun_prop]
theorem Measurable.snd {f : Î± â†’ Î² Ã— Î³} (hf : Measurable f) : Measurable fun a : Î± => (f a).2 :=
  measurable_snd.comp hf

@[measurability]
theorem Measurable.prod {f : Î± â†’ Î² Ã— Î³} (hfâ‚ : Measurable fun a => (f a).1)
    (hfâ‚‚ : Measurable fun a => (f a).2) : Measurable f :=
  Measurable.of_le_map <|
    sup_le
      (by
        rw [MeasurableSpace.comap_le_iff_le_map, MeasurableSpace.map_comp]
        exact hfâ‚)
      (by
        rw [MeasurableSpace.comap_le_iff_le_map, MeasurableSpace.map_comp]
        exact hfâ‚‚)

@[fun_prop]
theorem Measurable.prod_mk {Î² Î³} {_ : MeasurableSpace Î²} {_ : MeasurableSpace Î³} {f : Î± â†’ Î²}
    {g : Î± â†’ Î³} (hf : Measurable f) (hg : Measurable g) : Measurable fun a : Î± => (f a, g a) :=
  Measurable.prod hf hg

theorem Measurable.prod_map [MeasurableSpace Î´] {f : Î± â†’ Î²} {g : Î³ â†’ Î´} (hf : Measurable f)
    (hg : Measurable g) : Measurable (Prod.map f g) :=
  (hf.comp measurable_fst).prod_mk (hg.comp measurable_snd)

theorem measurable_prod_mk_left {x : Î±} : Measurable (@Prod.mk _ Î² x) :=
  measurable_const.prod_mk measurable_id

theorem measurable_prod_mk_right {y : Î²} : Measurable fun x : Î± => (x, y) :=
  measurable_id.prod_mk measurable_const

theorem Measurable.of_uncurry_left {f : Î± â†’ Î² â†’ Î³} (hf : Measurable (uncurry f)) {x : Î±} :
    Measurable (f x) :=
  hf.comp measurable_prod_mk_left

theorem Measurable.of_uncurry_right {f : Î± â†’ Î² â†’ Î³} (hf : Measurable (uncurry f)) {y : Î²} :
    Measurable fun x => f x y :=
  hf.comp measurable_prod_mk_right

theorem measurable_prod {f : Î± â†’ Î² Ã— Î³} :
    Measurable f â†” (Measurable fun a => (f a).1) âˆ§ Measurable fun a => (f a).2 :=
  âŸ¨fun hf => âŸ¨measurable_fst.comp hf, measurable_snd.comp hfâŸ©, fun h => Measurable.prod h.1 h.2âŸ©

@[fun_prop, measurability]
theorem measurable_swap : Measurable (Prod.swap : Î± Ã— Î² â†’ Î² Ã— Î±) :=
  Measurable.prod measurable_snd measurable_fst

theorem measurable_swap_iff {_ : MeasurableSpace Î³} {f : Î± Ã— Î² â†’ Î³} :
    Measurable (f âˆ˜ Prod.swap) â†” Measurable f :=
  âŸ¨fun hf => hf.comp measurable_swap, fun hf => hf.comp measurable_swapâŸ©

@[measurability]
protected theorem MeasurableSet.prod {s : Set Î±} {t : Set Î²} (hs : MeasurableSet s)
    (ht : MeasurableSet t) : MeasurableSet (s Ã—Ë¢ t) :=
  MeasurableSet.inter (measurable_fst hs) (measurable_snd ht)

theorem measurableSet_prod_of_nonempty {s : Set Î±} {t : Set Î²} (h : (s Ã—Ë¢ t).Nonempty) :
    MeasurableSet (s Ã—Ë¢ t) â†” MeasurableSet s âˆ§ MeasurableSet t := by
  rcases h with âŸ¨âŸ¨x, yâŸ©, hx, hyâŸ©
  refine âŸ¨fun hst => ?_, fun h => h.1.prod h.2âŸ©
  have : MeasurableSet ((fun x => (x, y)) â»Â¹' s Ã—Ë¢ t) := measurable_prod_mk_right hst
  have : MeasurableSet (Prod.mk x â»Â¹' s Ã—Ë¢ t) := measurable_prod_mk_left hst
  simp_all

theorem measurableSet_prod {s : Set Î±} {t : Set Î²} :
    MeasurableSet (s Ã—Ë¢ t) â†” MeasurableSet s âˆ§ MeasurableSet t âˆ¨ s = âˆ… âˆ¨ t = âˆ… := by
  rcases (s Ã—Ë¢ t).eq_empty_or_nonempty with h | h
  Â· simp [h, prod_eq_empty_iff.mp h]
  Â· simp [â† not_nonempty_iff_eq_empty, prod_nonempty_iff.mp h, measurableSet_prod_of_nonempty h]

theorem measurableSet_swap_iff {s : Set (Î± Ã— Î²)} :
    MeasurableSet (Prod.swap â»Â¹' s) â†” MeasurableSet s :=
  âŸ¨fun hs => measurable_swap hs, fun hs => measurable_swap hsâŸ©

instance Prod.instMeasurableSingletonClass
    [MeasurableSingletonClass Î±] [MeasurableSingletonClass Î²] :
    MeasurableSingletonClass (Î± Ã— Î²) :=
  âŸ¨fun âŸ¨a, bâŸ© => @singleton_prod_singleton _ _ a b â–¸ .prod (.singleton a) (.singleton b)âŸ©

theorem measurable_from_prod_countable' [Countable Î²]
    {_ : MeasurableSpace Î³} {f : Î± Ã— Î² â†’ Î³} (hf : âˆ€ y, Measurable fun x => f (x, y))
    (h'f : âˆ€ y y' x, y' âˆˆ measurableAtom y â†’ f (x, y') = f (x, y)) :
    Measurable f := fun s hs => by
  have : f â»Â¹' s = â‹ƒ y, ((fun x => f (x, y)) â»Â¹' s) Ã—Ë¢ (measurableAtom y : Set Î²) := by
    ext1 âŸ¨x, yâŸ©
    simp only [mem_preimage, mem_iUnion, mem_prod]
    refine âŸ¨fun h â†¦ âŸ¨y, h, mem_measurableAtom_self yâŸ©, ?_âŸ©
    rintro âŸ¨y', hy's, hy'âŸ©
    rwa [h'f y' y x hy']
  rw [this]
  exact .iUnion (fun y â†¦ (hf y hs).prod (.measurableAtom_of_countable y))

theorem measurable_from_prod_countable [Countable Î²] [MeasurableSingletonClass Î²]
    {_ : MeasurableSpace Î³} {f : Î± Ã— Î² â†’ Î³} (hf : âˆ€ y, Measurable fun x => f (x, y)) :
    Measurable f :=
  measurable_from_prod_countable' hf (by simp (config := {contextual := true}))

/-- A piecewise function on countably many pieces is measurable if all the data is measurable. -/
@[measurability]
theorem Measurable.find {_ : MeasurableSpace Î±} {f : â„• â†’ Î± â†’ Î²} {p : â„• â†’ Î± â†’ Prop}
    [âˆ€ n, DecidablePred (p n)] (hf : âˆ€ n, Measurable (f n)) (hp : âˆ€ n, MeasurableSet { x | p n x })
    (h : âˆ€ x, âˆƒ n, p n x) : Measurable fun x => f (Nat.find (h x)) x :=
  have : Measurable fun p : Î± Ã— â„• => f p.2 p.1 := measurable_from_prod_countable fun n => hf n
  this.comp (Measurable.prod_mk measurable_id (measurable_find h hp))

/-- Let `t i` be a countable covering of a set `T` by measurable sets. Let `f i : t i â†’ Î²` be a
family of functions that agree on the intersections `t i âˆ© t j`. Then the function
`Set.iUnionLift t f _ _ : T â†’ Î²`, defined as `f i âŸ¨x, hxâŸ©` for `hx : x âˆˆ t i`, is measurable. -/
theorem measurable_iUnionLift [Countable Î¹] {t : Î¹ â†’ Set Î±} {f : âˆ€ i, t i â†’ Î²}
    (htf : âˆ€ (i j) (x : Î±) (hxi : x âˆˆ t i) (hxj : x âˆˆ t j), f i âŸ¨x, hxiâŸ© = f j âŸ¨x, hxjâŸ©)
    {T : Set Î±} (hT : T âŠ† â‹ƒ i, t i) (htm : âˆ€ i, MeasurableSet (t i)) (hfm : âˆ€ i, Measurable (f i)) :
    Measurable (iUnionLift t f htf T hT) := fun s hs => by
  rw [preimage_iUnionLift]
  exact .preimage (.iUnion fun i => .image_inclusion _ (htm _) (hfm i hs)) (measurable_inclusion _)

/-- Let `t i` be a countable covering of `Î±` by measurable sets. Let `f i : t i â†’ Î²` be a family of
functions that agree on the intersections `t i âˆ© t j`. Then the function `Set.liftCover t f _ _`,
defined as `f i âŸ¨x, hxâŸ©` for `hx : x âˆˆ t i`, is measurable. -/
theorem measurable_liftCover [Countable Î¹] (t : Î¹ â†’ Set Î±) (htm : âˆ€ i, MeasurableSet (t i))
    (f : âˆ€ i, t i â†’ Î²) (hfm : âˆ€ i, Measurable (f i))
    (hf : âˆ€ (i j) (x : Î±) (hxi : x âˆˆ t i) (hxj : x âˆˆ t j), f i âŸ¨x, hxiâŸ© = f j âŸ¨x, hxjâŸ©)
    (htU : â‹ƒ i, t i = univ) :
    Measurable (liftCover t f hf htU) := fun s hs => by
  rw [preimage_liftCover]
  exact .iUnion fun i => .subtype_image (htm i) <| hfm i hs

/-- Let `t i` be a nonempty countable family of measurable sets in `Î±`. Let `g i : Î± â†’ Î²` be a
family of measurable functions such that `g i` agrees with `g j` on `t i âˆ© t j`. Then there exists
a measurable function `f : Î± â†’ Î²` that agrees with each `g i` on `t i`.

We only need the assumption `[Nonempty Î¹]` to prove `[Nonempty (Î± â†’ Î²)]`. -/
theorem exists_measurable_piecewise {Î¹} [Countable Î¹] [Nonempty Î¹] (t : Î¹ â†’ Set Î±)
    (t_meas : âˆ€ n, MeasurableSet (t n)) (g : Î¹ â†’ Î± â†’ Î²) (hg : âˆ€ n, Measurable (g n))
    (ht : Pairwise fun i j => EqOn (g i) (g j) (t i âˆ© t j)) :
    âˆƒ f : Î± â†’ Î², Measurable f âˆ§ âˆ€ n, EqOn f (g n) (t n) := by
  inhabit Î¹
  set g' : (i : Î¹) â†’ t i â†’ Î² := fun i => g i âˆ˜ (â†‘)
  -- see #2184
  have ht' : âˆ€ (i j) (x : Î±) (hxi : x âˆˆ t i) (hxj : x âˆˆ t j), g' i âŸ¨x, hxiâŸ© = g' j âŸ¨x, hxjâŸ© := by
    intro i j x hxi hxj
    rcases eq_or_ne i j with rfl | hij
    Â· rfl
    Â· exact ht hij âŸ¨hxi, hxjâŸ©
  set f : (â‹ƒ i, t i) â†’ Î² := iUnionLift t g' ht' _ Subset.rfl
  have hfm : Measurable f := measurable_iUnionLift _ _ t_meas
    (fun i => (hg i).comp measurable_subtype_coe)
  classical
    refine âŸ¨fun x => if hx : x âˆˆ â‹ƒ i, t i then f âŸ¨x, hxâŸ© else g default x,
      hfm.dite ((hg default).comp measurable_subtype_coe) (.iUnion t_meas), fun i x hx => ?_âŸ©
    simp only [dif_pos (mem_iUnion.2 âŸ¨i, hxâŸ©)]
    exact iUnionLift_of_mem âŸ¨x, mem_iUnion.2 âŸ¨i, hxâŸ©âŸ© hx

end Prod

section Pi

variable {Ï€ : Î´ â†’ Type*} [MeasurableSpace Î±]

instance MeasurableSpace.pi [m : âˆ€ a, MeasurableSpace (Ï€ a)] : MeasurableSpace (âˆ€ a, Ï€ a) :=
  â¨† a, (m a).comap fun b => b a

variable [âˆ€ a, MeasurableSpace (Ï€ a)] [MeasurableSpace Î³]

theorem measurable_pi_iff {g : Î± â†’ âˆ€ a, Ï€ a} : Measurable g â†” âˆ€ a, Measurable fun x => g x a := by
  simp_rw [measurable_iff_comap_le, MeasurableSpace.pi, MeasurableSpace.comap_iSup,
    MeasurableSpace.comap_comp, Function.comp, iSup_le_iff]

@[fun_prop, aesop safe 100 apply (rule_sets := [Measurable])]
theorem measurable_pi_apply (a : Î´) : Measurable fun f : âˆ€ a, Ï€ a => f a :=
  measurable_pi_iff.1 measurable_id a

@[aesop safe 100 apply (rule_sets := [Measurable])]
theorem Measurable.eval {a : Î´} {g : Î± â†’ âˆ€ a, Ï€ a} (hg : Measurable g) :
    Measurable fun x => g x a :=
  (measurable_pi_apply a).comp hg

@[fun_prop, aesop safe 100 apply (rule_sets := [Measurable])]
theorem measurable_pi_lambda (f : Î± â†’ âˆ€ a, Ï€ a) (hf : âˆ€ a, Measurable fun c => f c a) :
    Measurable f :=
  measurable_pi_iff.mpr hf

/-- The function `(f, x) â†¦ update f a x : (Î  a, Ï€ a) Ã— Ï€ a â†’ Î  a, Ï€ a` is measurable. -/
theorem measurable_update'  {a : Î´} [DecidableEq Î´] :
    Measurable (fun p : (âˆ€ i, Ï€ i) Ã— Ï€ a â†¦ update p.1 a p.2) := by
  rw [measurable_pi_iff]
  intro j
  dsimp [update]
  split_ifs with h
  Â· subst h
    dsimp
    exact measurable_snd
  Â· exact measurable_pi_iff.1 measurable_fst _

theorem measurable_uniqueElim [Unique Î´] [âˆ€ i, MeasurableSpace (Ï€ i)] :
    Measurable (uniqueElim : Ï€ (default : Î´) â†’ âˆ€ i, Ï€ i) := by
  simp_rw [measurable_pi_iff, Unique.forall_iff, uniqueElim_default]; exact measurable_id

theorem measurable_updateFinset [DecidableEq Î´] {s : Finset Î´} {x : âˆ€ i, Ï€ i} :
    Measurable (updateFinset x s) := by
  simp (config := { unfoldPartialApp := true }) only [updateFinset, measurable_pi_iff]
  intro i
  by_cases h : i âˆˆ s <;> simp [h, measurable_pi_apply]

/-- The function `update f a : Ï€ a â†’ Î  a, Ï€ a` is always measurable.
  This doesn't require `f` to be measurable.
  This should not be confused with the statement that `update f a x` is measurable. -/
@[measurability]
theorem measurable_update (f : âˆ€ a : Î´, Ï€ a) {a : Î´} [DecidableEq Î´] : Measurable (update f a) :=
  measurable_update'.comp measurable_prod_mk_left

theorem measurable_update_left {a : Î´} [DecidableEq Î´] {x : Ï€ a} :
    Measurable (update Â· a x) :=
  measurable_update'.comp measurable_prod_mk_right

variable (Ï€) in
theorem measurable_eq_mp {i i' : Î´} (h : i = i') : Measurable (congr_arg Ï€ h).mp := by
  cases h
  exact measurable_id

variable (Ï€) in
theorem Measurable.eq_mp {Î²} [MeasurableSpace Î²] {i i' : Î´} (h : i = i') {f : Î² â†’ Ï€ i}
    (hf : Measurable f) : Measurable fun x => (congr_arg Ï€ h).mp (f x) :=
  (measurable_eq_mp Ï€ h).comp hf

theorem measurable_piCongrLeft (f : Î´' â‰ƒ Î´) : Measurable (piCongrLeft Ï€ f) := by
  rw [measurable_pi_iff]
  intro i
  simp_rw [piCongrLeft_apply_eq_cast]
  exact Measurable.eq_mp Ï€ (f.apply_symm_apply i) <| measurable_pi_apply <| f.symm i

/- Even though we cannot use projection notation, we still keep a dot to be consistent with similar
  lemmas, like `MeasurableSet.prod`. -/
@[measurability]
protected theorem MeasurableSet.pi {s : Set Î´} {t : âˆ€ i : Î´, Set (Ï€ i)} (hs : s.Countable)
    (ht : âˆ€ i âˆˆ s, MeasurableSet (t i)) : MeasurableSet (s.pi t) := by
  rw [pi_def]
  exact MeasurableSet.biInter hs fun i hi => measurable_pi_apply _ (ht i hi)

protected theorem MeasurableSet.univ_pi [Countable Î´] {t : âˆ€ i : Î´, Set (Ï€ i)}
    (ht : âˆ€ i, MeasurableSet (t i)) : MeasurableSet (pi univ t) :=
  MeasurableSet.pi (to_countable _) fun i _ => ht i

theorem measurableSet_pi_of_nonempty {s : Set Î´} {t : âˆ€ i, Set (Ï€ i)} (hs : s.Countable)
    (h : (pi s t).Nonempty) : MeasurableSet (pi s t) â†” âˆ€ i âˆˆ s, MeasurableSet (t i) := by
  classical
    rcases h with âŸ¨f, hfâŸ©
    refine âŸ¨fun hst i hi => ?_, MeasurableSet.pi hsâŸ©
    convert measurable_update f (a := i) hst
    rw [update_preimage_pi hi]
    exact fun j hj _ => hf j hj

theorem measurableSet_pi {s : Set Î´} {t : âˆ€ i, Set (Ï€ i)} (hs : s.Countable) :
    MeasurableSet (pi s t) â†” (âˆ€ i âˆˆ s, MeasurableSet (t i)) âˆ¨ pi s t = âˆ… := by
  rcases (pi s t).eq_empty_or_nonempty with h | h
  Â· simp [h]
  Â· simp [measurableSet_pi_of_nonempty hs, h, â† not_nonempty_iff_eq_empty]

instance Pi.instMeasurableSingletonClass [Countable Î´] [âˆ€ a, MeasurableSingletonClass (Ï€ a)] :
    MeasurableSingletonClass (âˆ€ a, Ï€ a) :=
  âŸ¨fun f => univ_pi_singleton f â–¸ MeasurableSet.univ_pi fun t => measurableSet_singleton (f t)âŸ©

variable (Ï€)

@[measurability]
theorem measurable_piEquivPiSubtypeProd_symm (p : Î´ â†’ Prop) [DecidablePred p] :
    Measurable (Equiv.piEquivPiSubtypeProd p Ï€).symm := by
  refine measurable_pi_iff.2 fun j => ?_
  by_cases hj : p j
  Â· simp only [hj, dif_pos, Equiv.piEquivPiSubtypeProd_symm_apply]
    have : Measurable fun (f : âˆ€ i : { x // p x }, Ï€ i.1) => f âŸ¨j, hjâŸ© :=
      measurable_pi_apply (Ï€ := fun i : {x // p x} => Ï€ i.1) âŸ¨j, hjâŸ©
    exact Measurable.comp this measurable_fst
  Â· simp only [hj, Equiv.piEquivPiSubtypeProd_symm_apply, dif_neg, not_false_iff]
    have : Measurable fun (f : âˆ€ i : { x // Â¬p x }, Ï€ i.1) => f âŸ¨j, hjâŸ© :=
      measurable_pi_apply (Ï€ := fun i : {x // Â¬p x} => Ï€ i.1) âŸ¨j, hjâŸ©
    exact Measurable.comp this measurable_snd

@[measurability]
theorem measurable_piEquivPiSubtypeProd (p : Î´ â†’ Prop) [DecidablePred p] :
    Measurable (Equiv.piEquivPiSubtypeProd p Ï€) :=
  (measurable_pi_iff.2 fun _ => measurable_pi_apply _).prod_mk
    (measurable_pi_iff.2 fun _ => measurable_pi_apply _)

end Pi

instance TProd.instMeasurableSpace (Ï€ : Î´ â†’ Type*) [âˆ€ x, MeasurableSpace (Ï€ x)] :
    âˆ€ l : List Î´, MeasurableSpace (List.TProd Ï€ l)
  | [] => PUnit.instMeasurableSpace
  | _::is => @Prod.instMeasurableSpace _ _ _ (TProd.instMeasurableSpace Ï€ is)

section TProd

open List

variable {Ï€ : Î´ â†’ Type*} [âˆ€ x, MeasurableSpace (Ï€ x)]

theorem measurable_tProd_mk (l : List Î´) : Measurable (@TProd.mk Î´ Ï€ l) := by
  induction' l with i l ih
  Â· exact measurable_const
  Â· exact (measurable_pi_apply i).prod_mk ih

theorem measurable_tProd_elim [DecidableEq Î´] :
    âˆ€ {l : List Î´} {i : Î´} (hi : i âˆˆ l), Measurable fun v : TProd Ï€ l => v.elim hi
  | i::is, j, hj => by
    by_cases hji : j = i
    Â· subst hji
      simpa using measurable_fst
    Â· simp only [TProd.elim_of_ne _ hji]
      rw [mem_cons] at hj
      exact (measurable_tProd_elim (hj.resolve_left hji)).comp measurable_snd

theorem measurable_tProd_elim' [DecidableEq Î´] {l : List Î´} (h : âˆ€ i, i âˆˆ l) :
    Measurable (TProd.elim' h : TProd Ï€ l â†’ âˆ€ i, Ï€ i) :=
  measurable_pi_lambda _ fun i => measurable_tProd_elim (h i)

theorem MeasurableSet.tProd (l : List Î´) {s : âˆ€ i, Set (Ï€ i)} (hs : âˆ€ i, MeasurableSet (s i)) :
    MeasurableSet (Set.tprod l s) := by
  induction' l with i l ih
  Â· exact MeasurableSet.univ
  Â· exact (hs i).prod ih

end TProd

instance Sum.instMeasurableSpace {Î± Î²} [mâ‚ : MeasurableSpace Î±] [mâ‚‚ : MeasurableSpace Î²] :
    MeasurableSpace (Î± âŠ• Î²) :=
  mâ‚.map Sum.inl âŠ“ mâ‚‚.map Sum.inr

section Sum

@[measurability]
theorem measurable_inl [MeasurableSpace Î±] [MeasurableSpace Î²] : Measurable (@Sum.inl Î± Î²) :=
  Measurable.of_le_map inf_le_left

@[measurability]
theorem measurable_inr [MeasurableSpace Î±] [MeasurableSpace Î²] : Measurable (@Sum.inr Î± Î²) :=
  Measurable.of_le_map inf_le_right

variable {m : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}

theorem measurableSet_sum_iff {s : Set (Î± âŠ• Î²)} :
    MeasurableSet s â†” MeasurableSet (Sum.inl â»Â¹' s) âˆ§ MeasurableSet (Sum.inr â»Â¹' s) :=
  Iff.rfl

theorem measurable_sum {_ : MeasurableSpace Î³} {f : Î± âŠ• Î² â†’ Î³} (hl : Measurable (f âˆ˜ Sum.inl))
    (hr : Measurable (f âˆ˜ Sum.inr)) : Measurable f :=
  Measurable.of_comap_le <|
    le_inf (MeasurableSpace.comap_le_iff_le_map.2 <| hl)
      (MeasurableSpace.comap_le_iff_le_map.2 <| hr)

@[measurability]
theorem Measurable.sumElim {_ : MeasurableSpace Î³} {f : Î± â†’ Î³} {g : Î² â†’ Î³} (hf : Measurable f)
    (hg : Measurable g) : Measurable (Sum.elim f g) :=
  measurable_sum hf hg

theorem Measurable.sumMap {_ : MeasurableSpace Î³} {_ : MeasurableSpace Î´} {f : Î± â†’ Î²} {g : Î³ â†’ Î´}
    (hf : Measurable f) (hg : Measurable g) : Measurable (Sum.map f g) :=
  (measurable_inl.comp hf).sumElim (measurable_inr.comp hg)

@[simp] theorem measurableSet_inl_image {s : Set Î±} :
    MeasurableSet (Sum.inl '' s : Set (Î± âŠ• Î²)) â†” MeasurableSet s := by
  simp [measurableSet_sum_iff, Sum.inl_injective.preimage_image]

alias âŸ¨_, MeasurableSet.inl_imageâŸ© := measurableSet_inl_image

@[simp] theorem measurableSet_inr_image {s : Set Î²} :
    MeasurableSet (Sum.inr '' s : Set (Î± âŠ• Î²)) â†” MeasurableSet s := by
  simp [measurableSet_sum_iff, Sum.inr_injective.preimage_image]

alias âŸ¨_, MeasurableSet.inr_imageâŸ© := measurableSet_inr_image

theorem measurableSet_range_inl [MeasurableSpace Î±] :
    MeasurableSet (range Sum.inl : Set (Î± âŠ• Î²)) := by
  rw [â† image_univ]
  exact MeasurableSet.univ.inl_image

theorem measurableSet_range_inr [MeasurableSpace Î±] :
    MeasurableSet (range Sum.inr : Set (Î± âŠ• Î²)) := by
  rw [â† image_univ]
  exact MeasurableSet.univ.inr_image

end Sum

instance Sigma.instMeasurableSpace {Î±} {Î² : Î± â†’ Type*} [m : âˆ€ a, MeasurableSpace (Î² a)] :
    MeasurableSpace (Sigma Î²) :=
  â¨… a, (m a).map (Sigma.mk a)

section prop
variable [MeasurableSpace Î±] {p q : Î± â†’ Prop}

@[simp] theorem measurableSet_setOf : MeasurableSet {a | p a} â†” Measurable p :=
  âŸ¨fun h â†¦ measurable_to_prop <| by simpa only [preimage_singleton_true], fun h => by
    simpa using h (measurableSet_singleton True)âŸ©

@[simp] theorem measurable_mem : Measurable (Â· âˆˆ s) â†” MeasurableSet s := measurableSet_setOf.symm

alias âŸ¨_, Measurable.setOfâŸ© := measurableSet_setOf

alias âŸ¨_, MeasurableSet.memâŸ© := measurable_mem

lemma Measurable.not (hp : Measurable p) : Measurable (Â¬ p Â·) :=
  measurableSet_setOf.1 hp.setOf.compl

lemma Measurable.and (hp : Measurable p) (hq : Measurable q) : Measurable fun a â†¦ p a âˆ§ q a :=
  measurableSet_setOf.1 <| hp.setOf.inter hq.setOf

lemma Measurable.or (hp : Measurable p) (hq : Measurable q) : Measurable fun a â†¦ p a âˆ¨ q a :=
  measurableSet_setOf.1 <| hp.setOf.union hq.setOf

lemma Measurable.imp (hp : Measurable p) (hq : Measurable q) : Measurable fun a â†¦ p a â†’ q a :=
  measurableSet_setOf.1 <| hp.setOf.himp hq.setOf

lemma Measurable.iff (hp : Measurable p) (hq : Measurable q) : Measurable fun a â†¦ p a â†” q a :=
  measurableSet_setOf.1 <| by simp_rw [iff_iff_implies_and_implies]; exact hq.setOf.bihimp hp.setOf

lemma Measurable.forall [Countable Î¹] {p : Î¹ â†’ Î± â†’ Prop} (hp : âˆ€ i, Measurable (p i)) :
    Measurable fun a â†¦ âˆ€ i, p i a :=
  measurableSet_setOf.1 <| by rw [setOf_forall]; exact MeasurableSet.iInter fun i â†¦ (hp i).setOf

lemma Measurable.exists [Countable Î¹] {p : Î¹ â†’ Î± â†’ Prop} (hp : âˆ€ i, Measurable (p i)) :
    Measurable fun a â†¦ âˆƒ i, p i a :=
  measurableSet_setOf.1 <| by rw [setOf_exists]; exact MeasurableSet.iUnion fun i â†¦ (hp i).setOf

end prop

section Set
variable [MeasurableSpace Î²] {g : Î² â†’ Set Î±}

/-- This instance is useful when talking about Bernoulli sequences of random variables or binomial
random graphs. -/
instance Set.instMeasurableSpace : MeasurableSpace (Set Î±) := by unfold Set; infer_instance

instance Set.instMeasurableSingletonClass [Countable Î±] : MeasurableSingletonClass (Set Î±) := by
  unfold Set; infer_instance

lemma measurable_set_iff : Measurable g â†” âˆ€ a, Measurable fun x â†¦ a âˆˆ g x := measurable_pi_iff

@[aesop safe 100 apply (rule_sets := [Measurable])]
lemma measurable_set_mem (a : Î±) : Measurable fun s : Set Î± â†¦ a âˆˆ s := measurable_pi_apply _

@[aesop safe 100 apply (rule_sets := [Measurable])]
lemma measurable_set_not_mem (a : Î±) : Measurable fun s : Set Î± â†¦ a âˆ‰ s :=
  (measurable_discrete Not).comp <| measurable_set_mem a

@[aesop safe 100 apply (rule_sets := [Measurable])]
lemma measurableSet_mem (a : Î±) : MeasurableSet {s : Set Î± | a âˆˆ s} :=
  measurableSet_setOf.2 <| measurable_set_mem _

@[aesop safe 100 apply (rule_sets := [Measurable])]
lemma measurableSet_not_mem (a : Î±) : MeasurableSet {s : Set Î± | a âˆ‰ s} :=
  measurableSet_setOf.2 <| measurable_set_not_mem _

lemma measurable_compl : Measurable ((Â·á¶œ) : Set Î± â†’ Set Î±) :=
  measurable_set_iff.2 fun _ â†¦ measurable_set_not_mem _

end Set
end Constructions

namespace MeasurableSpace

/-- The sigma-algebra generated by a single set `s` is `{âˆ…, s, sá¶œ, univ}`. -/
@[simp] theorem generateFrom_singleton (s : Set Î±) :
    generateFrom {s} = MeasurableSpace.comap (Â· âˆˆ s) âŠ¤ := by
  classical
  letI : MeasurableSpace Î± := generateFrom {s}
  refine le_antisymm (generateFrom_le fun t ht => âŸ¨{True}, trivial, by simp [ht.symm]âŸ©) ?_
  rintro _ âŸ¨u, -, rflâŸ©
  exact (show MeasurableSet s from GenerateMeasurable.basic _ <| mem_singleton s).mem trivial

end MeasurableSpace

namespace Filter

variable [MeasurableSpace Î±]

/-- A filter `f` is measurably generates if each `s âˆˆ f` includes a measurable `t âˆˆ f`. -/
class IsMeasurablyGenerated (f : Filter Î±) : Prop where
  exists_measurable_subset : âˆ€ â¦ƒsâ¦„, s âˆˆ f â†’ âˆƒ t âˆˆ f, MeasurableSet t âˆ§ t âŠ† s

instance isMeasurablyGenerated_bot : IsMeasurablyGenerated (âŠ¥ : Filter Î±) :=
  âŸ¨fun _ _ => âŸ¨âˆ…, mem_bot, MeasurableSet.empty, empty_subset _âŸ©âŸ©

instance isMeasurablyGenerated_top : IsMeasurablyGenerated (âŠ¤ : Filter Î±) :=
  âŸ¨fun _s hs => âŸ¨univ, univ_mem, MeasurableSet.univ, fun x _ => hs xâŸ©âŸ©

theorem Eventually.exists_measurable_mem {f : Filter Î±} [IsMeasurablyGenerated f] {p : Î± â†’ Prop}
    (h : âˆ€á¶  x in f, p x) : âˆƒ s âˆˆ f, MeasurableSet s âˆ§ âˆ€ x âˆˆ s, p x :=
  IsMeasurablyGenerated.exists_measurable_subset h

theorem Eventually.exists_measurable_mem_of_smallSets {f : Filter Î±} [IsMeasurablyGenerated f]
    {p : Set Î± â†’ Prop} (h : âˆ€á¶  s in f.smallSets, p s) : âˆƒ s âˆˆ f, MeasurableSet s âˆ§ p s :=
  let âŸ¨_s, hsf, hsâŸ© := eventually_smallSets.1 h
  let âŸ¨t, htf, htm, htsâŸ© := IsMeasurablyGenerated.exists_measurable_subset hsf
  âŸ¨t, htf, htm, hs t htsâŸ©

instance inf_isMeasurablyGenerated (f g : Filter Î±) [IsMeasurablyGenerated f]
    [IsMeasurablyGenerated g] : IsMeasurablyGenerated (f âŠ“ g) := by
  constructor
  rintro t âŸ¨sf, hsf, sg, hsg, rflâŸ©
  rcases IsMeasurablyGenerated.exists_measurable_subset hsf with âŸ¨s'f, hs'f, hmf, hs'sfâŸ©
  rcases IsMeasurablyGenerated.exists_measurable_subset hsg with âŸ¨s'g, hs'g, hmg, hs'sgâŸ©
  refine âŸ¨s'f âˆ© s'g, inter_mem_inf hs'f hs'g, hmf.inter hmg, ?_âŸ©
  exact inter_subset_inter hs'sf hs'sg

theorem principal_isMeasurablyGenerated_iff {s : Set Î±} :
    IsMeasurablyGenerated (ğ“Ÿ s) â†” MeasurableSet s := by
  refine âŸ¨?_, fun hs => âŸ¨fun t ht => âŸ¨s, mem_principal_self s, hs, htâŸ©âŸ©âŸ©
  rintro âŸ¨hsâŸ©
  rcases hs (mem_principal_self s) with âŸ¨t, ht, htm, htsâŸ©
  have : t = s := hts.antisymm ht
  rwa [â† this]

alias âŸ¨_, _root_.MeasurableSet.principal_isMeasurablyGeneratedâŸ© :=
  principal_isMeasurablyGenerated_iff

instance iInf_isMeasurablyGenerated {f : Î¹ â†’ Filter Î±} [âˆ€ i, IsMeasurablyGenerated (f i)] :
    IsMeasurablyGenerated (â¨… i, f i) := by
  refine âŸ¨fun s hs => ?_âŸ©
  rw [â† Equiv.plift.surjective.iInf_comp, mem_iInf] at hs
  rcases hs with âŸ¨t, ht, âŸ¨V, hVf, rflâŸ©âŸ©
  choose U hUf hU using fun i => IsMeasurablyGenerated.exists_measurable_subset (hVf i)
  refine âŸ¨â‹‚ i : t, U i, ?_, ?_, ?_âŸ©
  Â· rw [â† Equiv.plift.surjective.iInf_comp, mem_iInf]
    exact âŸ¨t, ht, U, hUf, rflâŸ©
  Â· haveI := ht.countable.toEncodable.countable
    exact MeasurableSet.iInter fun i => (hU i).1
  Â· exact iInter_mono fun i => (hU i).2

end Filter

/-- The set of points for which a sequence of measurable functions converges to a given value
is measurable. -/
@[measurability]
lemma measurableSet_tendsto {_ : MeasurableSpace Î²} [MeasurableSpace Î³]
    [Countable Î´] {l : Filter Î´} [l.IsCountablyGenerated]
    (l' : Filter Î³) [l'.IsCountablyGenerated] [hl' : l'.IsMeasurablyGenerated]
    {f : Î´ â†’ Î² â†’ Î³} (hf : âˆ€ i, Measurable (f i)) :
    MeasurableSet { x | Tendsto (fun n â†¦ f n x) l l' } := by
  rcases l.exists_antitone_basis with âŸ¨u, huâŸ©
  rcases (Filter.hasBasis_self.mpr hl'.exists_measurable_subset).exists_antitone_subbasis with
    âŸ¨v, v_meas, hvâŸ©
  simp only [hu.tendsto_iff hv.toHasBasis, true_imp_iff, true_and, setOf_forall, setOf_exists]
  exact .iInter fun n â†¦ .iUnion fun _ â†¦ .biInter (to_countable _) fun i _ â†¦
    (v_meas n).2.preimage (hf i)

/-- We say that a collection of sets is countably spanning if a countable subset spans the
whole type. This is a useful condition in various parts of measure theory. For example, it is
a needed condition to show that the product of two collections generate the product sigma algebra,
see `generateFrom_prod_eq`. -/
def IsCountablySpanning (C : Set (Set Î±)) : Prop :=
  âˆƒ s : â„• â†’ Set Î±, (âˆ€ n, s n âˆˆ C) âˆ§ â‹ƒ n, s n = univ

theorem isCountablySpanning_measurableSet [MeasurableSpace Î±] :
    IsCountablySpanning { s : Set Î± | MeasurableSet s } :=
  âŸ¨fun _ => univ, fun _ => MeasurableSet.univ, iUnion_const _âŸ©

namespace MeasurableSet

/-!
### Typeclasses on `Subtype MeasurableSet`
-/

variable [MeasurableSpace Î±]

instance Subtype.instMembership : Membership Î± (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun a s => a âˆˆ (s : Set Î±)âŸ©

@[simp]
theorem mem_coe (a : Î±) (s : Subtype (MeasurableSet : Set Î± â†’ Prop)) : a âˆˆ (s : Set Î±) â†” a âˆˆ s :=
  Iff.rfl

instance Subtype.instEmptyCollection : EmptyCollection (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨âŸ¨âˆ…, MeasurableSet.emptyâŸ©âŸ©

@[simp]
theorem coe_empty : â†‘(âˆ… : Subtype (MeasurableSet : Set Î± â†’ Prop)) = (âˆ… : Set Î±) :=
  rfl

instance Subtype.instInsert [MeasurableSingletonClass Î±] :
    Insert Î± (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun a s => âŸ¨insert a (s : Set Î±), s.prop.insert aâŸ©âŸ©

@[simp]
theorem coe_insert [MeasurableSingletonClass Î±] (a : Î±)
    (s : Subtype (MeasurableSet : Set Î± â†’ Prop)) :
    â†‘(Insert.insert a s) = (Insert.insert a s : Set Î±) :=
  rfl

instance Subtype.instSingleton [MeasurableSingletonClass Î±] :
    Singleton Î± (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun a => âŸ¨{a}, .singleton _âŸ©âŸ©

@[simp] theorem coe_singleton [MeasurableSingletonClass Î±] (a : Î±) :
    â†‘({a} : Subtype (MeasurableSet : Set Î± â†’ Prop)) = ({a} : Set Î±) :=
  rfl

instance Subtype.instLawfulSingleton [MeasurableSingletonClass Î±] :
    LawfulSingleton Î± (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun _ => Subtype.eq <| insert_emptyc_eq _âŸ©

instance Subtype.instHasCompl : HasCompl (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun x => âŸ¨xá¶œ, x.prop.complâŸ©âŸ©

@[simp]
theorem coe_compl (s : Subtype (MeasurableSet : Set Î± â†’ Prop)) : â†‘sá¶œ = (sá¶œ : Set Î±) :=
  rfl

instance Subtype.instUnion : Union (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun x y => âŸ¨(x : Set Î±) âˆª y, x.prop.union y.propâŸ©âŸ©

@[simp]
theorem coe_union (s t : Subtype (MeasurableSet : Set Î± â†’ Prop)) : â†‘(s âˆª t) = (s âˆª t : Set Î±) :=
  rfl

instance Subtype.instSup : Sup (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun x y => x âˆª yâŸ©

@[simp]
protected theorem sup_eq_union (s t : {s : Set Î± // MeasurableSet s}) : s âŠ” t = s âˆª t := rfl

instance Subtype.instInter : Inter (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun x y => âŸ¨x âˆ© y, x.prop.inter y.propâŸ©âŸ©

@[simp]
theorem coe_inter (s t : Subtype (MeasurableSet : Set Î± â†’ Prop)) : â†‘(s âˆ© t) = (s âˆ© t : Set Î±) :=
  rfl

instance Subtype.instInf : Inf (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun x y => x âˆ© yâŸ©

@[simp]
protected theorem inf_eq_inter (s t : {s : Set Î± // MeasurableSet s}) : s âŠ“ t = s âˆ© t := rfl

instance Subtype.instSDiff : SDiff (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨fun x y => âŸ¨x \ y, x.prop.diff y.propâŸ©âŸ©

@[simp]
theorem coe_sdiff (s t : Subtype (MeasurableSet : Set Î± â†’ Prop)) : â†‘(s \ t) = (s : Set Î±) \ t :=
  rfl

instance Subtype.instBot : Bot (Subtype (MeasurableSet : Set Î± â†’ Prop)) := âŸ¨âˆ…âŸ©

@[simp]
theorem coe_bot : â†‘(âŠ¥ : Subtype (MeasurableSet : Set Î± â†’ Prop)) = (âŠ¥ : Set Î±) :=
  rfl

instance Subtype.instTop : Top (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  âŸ¨âŸ¨Set.univ, MeasurableSet.univâŸ©âŸ©

@[simp]
theorem coe_top : â†‘(âŠ¤ : Subtype (MeasurableSet : Set Î± â†’ Prop)) = (âŠ¤ : Set Î±) :=
  rfl

instance Subtype.instBooleanAlgebra :
    BooleanAlgebra (Subtype (MeasurableSet : Set Î± â†’ Prop)) :=
  Subtype.coe_injective.booleanAlgebra _ (fun _ _ => rfl) (fun _ _ => rfl) rfl rfl (fun _ => rfl)
    fun _ _ => rfl

@[measurability]
theorem measurableSet_blimsup {s : â„• â†’ Set Î±} {p : â„• â†’ Prop} (h : âˆ€ n, p n â†’ MeasurableSet (s n)) :
    MeasurableSet <| blimsup s atTop p := by
  simp only [blimsup_eq_iInf_biSup_of_nat, iSup_eq_iUnion, iInf_eq_iInter]
  exact .iInter fun _ => .iUnion fun m => .iUnion fun hm => h m hm.1

@[measurability]
theorem measurableSet_bliminf {s : â„• â†’ Set Î±} {p : â„• â†’ Prop} (h : âˆ€ n, p n â†’ MeasurableSet (s n)) :
    MeasurableSet <| Filter.bliminf s Filter.atTop p := by
  simp only [Filter.bliminf_eq_iSup_biInf_of_nat, iInf_eq_iInter, iSup_eq_iUnion]
  exact .iUnion fun n => .iInter fun m => .iInter fun hm => h m hm.1

@[measurability]
theorem measurableSet_limsup {s : â„• â†’ Set Î±} (hs : âˆ€ n, MeasurableSet <| s n) :
    MeasurableSet <| Filter.limsup s Filter.atTop := by
  simpa only [â† blimsup_true] using measurableSet_blimsup fun n _ => hs n

@[measurability]
theorem measurableSet_liminf {s : â„• â†’ Set Î±} (hs : âˆ€ n, MeasurableSet <| s n) :
    MeasurableSet <| Filter.liminf s Filter.atTop := by
  simpa only [â† bliminf_true] using measurableSet_bliminf fun n _ => hs n

end MeasurableSet
