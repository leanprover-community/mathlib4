/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Topology.Instances.EReal.Lemmas

/-!
# EReal lemmas
-/

open Filter Topology

open scoped ENNReal

namespace EReal

@[simp]
lemma toENNReal_one : (1 : EReal).toENNReal = 1 := by
  rw [toENNReal_of_ne_top]
  Â· simp
  Â· simpa using natCast_ne_top 1

lemma toENNReal_toEReal (x : â„) : EReal.toENNReal x = ENNReal.ofReal x := rfl

lemma continuousAt_sub {p : EReal Ã— EReal} (h : p.1 â‰  âŠ¤ âˆ¨ p.2 â‰  âŠ¤) (h' : p.1 â‰  âŠ¥ âˆ¨ p.2 â‰  âŠ¥) :
    ContinuousAt (fun p : EReal Ã— EReal â†¦ p.1 - p.2) p := by
  simp_rw [sub_eq_add_neg]
  change ContinuousAt ((fun p : EReal Ã— EReal => p.1 + p.2) âˆ˜ (fun p â†¦ (p.1, -p.2))) p
  exact (EReal.continuousAt_add (by simp [h]) (by simp [h'])).comp (by fun_prop)

lemma continuousAt_const_sub {c x : EReal} (h' : x â‰  âŠ¤ âˆ¨ c â‰  âŠ¤) :
    ContinuousAt (fun x : EReal â†¦ c - x) x := by
  by_cases hc_top : c = âŠ¥
  Â· simpa [hc_top] using continuous_const.continuousAt
  change ContinuousAt ((fun p : EReal Ã— EReal â†¦ p.1 - p.2) âˆ˜ (fun x â†¦ (c, x))) x
  exact (EReal.continuousAt_sub h'.symm (Or.inl hc_top)).comp (by fun_prop)

lemma continuousAt_sub_const {c x : EReal} (h' : x â‰  âŠ¥ âˆ¨ c â‰  âŠ¥) :
    ContinuousAt (fun x : EReal â†¦ x - c) x := by
  by_cases hc_top : c = âŠ¤
  Â· simpa [hc_top] using continuous_const.continuousAt
  change ContinuousAt ((fun p : EReal Ã— EReal â†¦ p.1 - p.2) âˆ˜ (fun x â†¦ (x, c))) x
  exact (EReal.continuousAt_sub (Or.inr hc_top) h').comp (by fun_prop)

lemma continuous_coe_mul {c : â„} : Continuous (fun x : EReal â†¦ c * x) := by
  by_cases hc0 : c = 0
  Â· simpa [hc0] using continuous_const
  rw [continuous_iff_continuousAt]
  intro x
  have h_cont : ContinuousAt (fun p : EReal Ã— EReal â†¦ p.1 * p.2) (c, x) := by
    refine EReal.continuousAt_mul ?_ ?_ ?_ ?_ <;> exact Or.inl (by simp [hc0])
  refine h_cont.comp ?_
  fun_prop

lemma coe_mul_add_of_nonneg {x : â„} (hx_nonneg : 0 â‰¤ x) (y z : EReal) :
    x * (y + z) = x * y + x * z := by
  by_cases hx0 : x = 0
  Â· simp [hx0]
  have hx_pos : 0 < x := hx_nonneg.lt_of_ne' hx0
  induction y
  Â· simp [EReal.coe_mul_bot_of_pos hx_pos]
  Â· induction z
    Â· simp [EReal.coe_mul_bot_of_pos hx_pos]
    Â· norm_cast
      rw [mul_add]
    Â· simp only [coe_add_top, EReal.coe_mul_top_of_pos hx_pos]
      rw [â† EReal.coe_mul, EReal.coe_add_top]
  Â· simp only [EReal.coe_mul_top_of_pos hx_pos]
    induction z
    Â· simp [EReal.coe_mul_bot_of_pos hx_pos]
    Â· simp only [top_add_coe, EReal.coe_mul_top_of_pos hx_pos]
      rw [â† EReal.coe_mul, EReal.top_add_coe]
    Â· simp [EReal.coe_mul_top_of_pos hx_pos]

lemma top_mul_add_of_nonneg {x y : EReal} (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) :
    âŠ¤ * (x + y) = âŠ¤ * x + âŠ¤ * y := by
  induction x, y using EReal.inductionâ‚‚_symm with
  | symm h =>
    rw [add_comm, add_comm (âŠ¤ * _)]
    exact h hy hx
  | top_top => simp
  | top_pos _ h =>
    rw [top_add_coe, top_mul_top, top_mul_of_pos, top_add_top]
    exact mod_cast h
  | top_zero => simp
  | top_neg _ h =>
    refine absurd hy ?_
    exact mod_cast h.not_le
  | top_bot => simp
  | pos_bot => simp
  | coe_coe x y =>
    by_cases hx0 : x = 0
    Â· simp [hx0]
    by_cases hy0 : y = 0
    Â· simp [hy0]
    have hx_pos : 0 < (x : EReal) := by
      refine hx.lt_of_ne' ?_
      exact mod_cast hx0
    have hy_pos : 0 < (y : EReal) := by
      refine hy.lt_of_ne' ?_
      exact mod_cast hy0
    rw [top_mul_of_pos hx_pos, top_mul_of_pos hy_pos, top_mul_of_pos]
    Â· simp
    Â· exact add_pos hx_pos hy_pos
  | zero_bot => simp
  | neg_bot => simp
  | bot_bot => simp

lemma mul_add_coe_of_nonneg (x : EReal) {y z : â„} (hy : 0 â‰¤ y) (hz : 0 â‰¤ z) :
    x * (y + z) = x * y + x * z := by
  by_cases hx_top : x = âŠ¤
  Â· rw [hx_top]
    exact top_mul_add_of_nonneg (mod_cast hy) (mod_cast hz)
  by_cases hx_bot : x = âŠ¥
  Â· rw [hx_bot]
    by_cases hy0 : y = 0
    Â· simp [hy0]
    by_cases hz0 : z = 0
    Â· simp [hz0]
    have hy_pos : 0 < (y : EReal) := lt_of_le_of_ne' (mod_cast hy) (mod_cast hy0)
    have hz_pos : 0 < (z : EReal) := lt_of_le_of_ne' (mod_cast hz) (mod_cast hz0)
    rw [bot_mul_of_pos hy_pos, bot_mul_of_pos hz_pos, bot_mul_of_pos]
    Â· simp
    Â· exact add_pos hy_pos hz_pos
  lift x to â„ using âŸ¨hx_top, hx_botâŸ©
  norm_cast
  rw [mul_add]

lemma coe_add_mul_of_nonneg (x : EReal) {y z : â„} (hy : 0 â‰¤ y) (hz : 0 â‰¤ z) :
    (y + z) * x = y * x + z * x := by
  simp_rw [mul_comm _ x]
  exact EReal.mul_add_coe_of_nonneg x hy hz

lemma sub_add_sub_cancel (b a : EReal) (c : â„) :
    b - c + (c - a) = b - a := by
  induction a <;> induction b
  Â· simp
  Â· simp only [coe_sub_bot]
    rw [â† coe_sub, coe_add_top]
  Â· simp
  Â· simp
  Â· norm_cast
    ring
  Â· simp only [top_sub_coe]
    rw [â† coe_sub, top_add_coe]
  Â· simp
  Â· simp
  Â· simp

lemma top_sub_of_ne_top {x : EReal} (hx : x â‰  âŠ¤) : âŠ¤ - x = âŠ¤ := by
  induction x <;> tauto

lemma toENNReal_sub_le_add (b a c : EReal) :
    (b - a).toENNReal â‰¤ (b - c).toENNReal + (c - a).toENNReal := by
  by_cases hc_top : c = âŠ¤
  Â· simp only [hc_top, sub_top, ne_eq, bot_ne_top, not_false_eq_true,
      toENNReal_of_ne_top, toReal_bot, ENNReal.ofReal_zero, zero_add]
    by_cases ha : a = âŠ¤
    Â· simp [ha]
    Â· simp [top_sub_of_ne_top ha]
  by_cases hc_bot : c = âŠ¥
  Â· simp [hc_bot, sub_eq_add_neg]
    by_cases hb_bot : b = âŠ¥
    Â· simp [hb_bot]
    Â· simp [add_top_of_ne_bot hb_bot]
  refine (toENNReal_le_toENNReal ?_).trans toENNReal_add_le
  lift c to â„ using âŸ¨hc_top, hc_botâŸ© with c
  rw [sub_add_sub_cancel]

lemma toENNReal_sub_add_cancel {b a c : EReal} (hac : a â‰¤ c) (hcb : c â‰¤ b) :
    (b - c).toENNReal + (c - a).toENNReal = (b - a).toENNReal := by
  induction c
  Â· have ha : a = âŠ¥ := eq_bot_iff.mpr hac
    simp [ha]
  Â· rw [â† toENNReal_add, sub_add_sub_cancel]
    Â· rwa [sub_nonneg] <;> simp
    Â· by_cases ha : a = âŠ¥
      Â· simp [ha]
      rwa [sub_nonneg] <;> simp
  Â· have hb : b = âŠ¤ := eq_top_iff.mpr hcb
    simp [hb]

end EReal

@[simp]
lemma ENNReal.nhdsLT_ne_bot_iff {x : â„â‰¥0âˆ} :
    (ğ“[<] x).NeBot â†” x â‰  0 := by
  by_cases hx_zero : x = 0
  Â· simp [hx_zero]
  Â· simp only [ne_eq, hx_zero, not_false_eq_true, iff_true]
    refine nhdsWithin_Iio_self_neBot' ?_
    simp [hx_zero]

@[simp]
lemma ENNReal.nhdsGT_ne_bot_iff {x : â„â‰¥0âˆ} :
    (ğ“[>] x).NeBot â†” x â‰  âˆ := by
  by_cases hx : x = âˆ
  Â· simp [hx]
  Â· simp only [ne_eq, hx, not_false_eq_true, iff_true]
    exact nhdsGT_neBot_of_exists_gt âŸ¨âˆ, Ne.lt_top hxâŸ©
