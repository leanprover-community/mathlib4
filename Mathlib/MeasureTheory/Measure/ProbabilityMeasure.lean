/-
Copyright (c) 2021 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.MeasureTheory.Measure.FiniteMeasure
import Mathlib.MeasureTheory.Integral.Average
import Mathlib.MeasureTheory.Measure.Prod

/-!
# Probability measures

This file defines the type of probability measures on a given measurable space. When the underlying
space has a topology and the measurable space structure (sigma algebra) is finer than the Borel
sigma algebra, then the type of probability measures is equipped with the topology of convergence
in distribution (weak convergence of measures). The topology of convergence in distribution is the
coarsest topology w.r.t. which for every bounded continuous `‚Ñù‚â•0`-valued random variable `X`, the
expected value of `X` depends continuously on the choice of probability measure. This is a special
case of the topology of weak convergence of finite measures.

## Main definitions

The main definitions are
* the type `MeasureTheory.ProbabilityMeasure Œ©` with the topology of convergence in
  distribution (a.k.a. convergence in law, weak convergence of measures);
* `MeasureTheory.ProbabilityMeasure.toFiniteMeasure`: Interpret a probability measure as
  a finite measure;
* `MeasureTheory.FiniteMeasure.normalize`: Normalize a finite measure to a probability measure
  (returns junk for the zero measure).
* `MeasureTheory.ProbabilityMeasure.map`: The push-forward `f* Œº` of a probability measure
  `Œº` on `Œ©` along a measurable function `f : Œ© ‚Üí Œ©'`.

## Main results

* `MeasureTheory.ProbabilityMeasure.tendsto_iff_forall_integral_tendsto`: Convergence of
  probability measures is characterized by the convergence of expected values of all bounded
  continuous random variables. This shows that the chosen definition of topology coincides with
  the common textbook definition of convergence in distribution, i.e., weak convergence of
  measures. A similar characterization by the convergence of expected values (in the
  `MeasureTheory.lintegral` sense) of all bounded continuous nonnegative random variables is
  `MeasureTheory.ProbabilityMeasure.tendsto_iff_forall_lintegral_tendsto`.
* `MeasureTheory.FiniteMeasure.tendsto_normalize_iff_tendsto`: The convergence of finite
  measures to a nonzero limit is characterized by the convergence of the probability-normalized
  versions and of the total masses.
* `MeasureTheory.ProbabilityMeasure.continuous_map`: For a continuous function `f : Œ© ‚Üí Œ©'`, the
  push-forward of probability measures `f* : ProbabilityMeasure Œ© ‚Üí ProbabilityMeasure Œ©'` is
  continuous.
* `MeasureTheory.ProbabilityMeasure.t2Space`: The topology of convergence in distribution is
  Hausdorff on Borel spaces where indicators of closed sets have continuous decreasing
  approximating sequences (in particular on any pseudo-metrizable spaces).

TODO:
* Probability measures form a convex space.

## Implementation notes

The topology of convergence in distribution on `MeasureTheory.ProbabilityMeasure Œ©` is inherited
weak convergence of finite measures via the mapping
`MeasureTheory.ProbabilityMeasure.toFiniteMeasure`.

Like `MeasureTheory.FiniteMeasure Œ©`, the implementation of `MeasureTheory.ProbabilityMeasure Œ©`
is directly as a subtype of `MeasureTheory.Measure Œ©`, and the coercion to a function is the
composition `ENNReal.toNNReal` and the coercion to function of `MeasureTheory.Measure Œ©`.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

convergence in distribution, convergence in law, weak convergence of measures, probability measure

-/


noncomputable section

open Set Filter BoundedContinuousFunction Topology
open scoped ENNReal NNReal

namespace MeasureTheory

section ProbabilityMeasure

/-! ### Probability measures

In this section we define the type of probability measures on a measurable space `Œ©`, denoted by
`MeasureTheory.ProbabilityMeasure Œ©`.

If `Œ©` is moreover a topological space and the sigma algebra on `Œ©` is finer than the Borel sigma
algebra (i.e. `[OpensMeasurableSpace Œ©]`), then `MeasureTheory.ProbabilityMeasure Œ©` is
equipped with the topology of weak convergence of measures. Since every probability measure is a
finite measure, this is implemented as the induced topology from the mapping
`MeasureTheory.ProbabilityMeasure.toFiniteMeasure`.
-/


/-- Probability measures are defined as the subtype of measures that have the property of being
probability measures (i.e., their total mass is one). -/
def ProbabilityMeasure (Œ© : Type*) [MeasurableSpace Œ©] : Type _ :=
  { Œº : Measure Œ© // IsProbabilityMeasure Œº }

namespace ProbabilityMeasure

variable {Œ© : Type*} [MeasurableSpace Œ©]

instance [Inhabited Œ©] : Inhabited (ProbabilityMeasure Œ©) :=
  ‚ü®‚ü®Measure.dirac default, Measure.dirac.isProbabilityMeasure‚ü©‚ü©

/-- Coercion from `MeasureTheory.ProbabilityMeasure Œ©` to `MeasureTheory.Measure Œ©`. -/
@[coe]
def toMeasure : ProbabilityMeasure Œ© ‚Üí Measure Œ© := Subtype.val

/-- A probability measure can be interpreted as a measure. -/
instance : Coe (ProbabilityMeasure Œ©) (MeasureTheory.Measure Œ©) := { coe := toMeasure }

instance (Œº : ProbabilityMeasure Œ©) : IsProbabilityMeasure (Œº : Measure Œ©) :=
  Œº.prop

@[simp, norm_cast] lemma coe_mk (Œº : Measure Œ©) (hŒº) : toMeasure ‚ü®Œº, hŒº‚ü© = Œº := rfl

@[simp]
theorem val_eq_to_measure (ŒΩ : ProbabilityMeasure Œ©) : ŒΩ.val = (ŒΩ : Measure Œ©) := rfl

theorem toMeasure_injective : Function.Injective ((‚Üë) : ProbabilityMeasure Œ© ‚Üí Measure Œ©) :=
  Subtype.coe_injective

instance instFunLike : FunLike (ProbabilityMeasure Œ©) (Set Œ©) ‚Ñù‚â•0 where
  coe Œº s := ((Œº : Measure Œ©) s).toNNReal
  coe_injective' Œº ŒΩ h := toMeasure_injective <| Measure.ext fun s _ ‚Ü¶ by
    simpa [ENNReal.toNNReal_eq_toNNReal_iff, measure_ne_top] using congr_fun h s

lemma coeFn_def (Œº : ProbabilityMeasure Œ©) : Œº = fun s ‚Ü¶ ((Œº : Measure Œ©) s).toNNReal := rfl

lemma coeFn_mk (Œº : Measure Œ©) (hŒº) :
    DFunLike.coe (F := ProbabilityMeasure Œ©) ‚ü®Œº, hŒº‚ü© = fun s ‚Ü¶ (Œº s).toNNReal := rfl

@[simp, norm_cast]
lemma mk_apply (Œº : Measure Œ©) (hŒº) (s : Set Œ©) :
    DFunLike.coe (F := ProbabilityMeasure Œ©) ‚ü®Œº, hŒº‚ü© s = (Œº s).toNNReal := rfl

@[simp, norm_cast]
theorem coeFn_univ (ŒΩ : ProbabilityMeasure Œ©) : ŒΩ univ = 1 :=
  congr_arg ENNReal.toNNReal ŒΩ.prop.measure_univ

@[simp]
theorem coeFn_empty (ŒΩ : ProbabilityMeasure Œ©) : ŒΩ ‚àÖ = 0 := by simp [coeFn_def]

theorem coeFn_univ_ne_zero (ŒΩ : ProbabilityMeasure Œ©) : ŒΩ univ ‚â† 0 := by
  simp only [coeFn_univ, Ne, one_ne_zero, not_false_iff]

@[simp] theorem measureReal_eq_coe_coeFn (ŒΩ : ProbabilityMeasure Œ©) (s : Set Œ©) :
    (ŒΩ : Measure Œ©).real s = ŒΩ s := by
  simp [coeFn_def, Measure.real, ENNReal.toReal]

theorem toNNReal_measureReal_eq_coeFn (ŒΩ : ProbabilityMeasure Œ©) (s : Set Œ©) :
    ((ŒΩ : Measure Œ©).real s).toNNReal = ŒΩ s := by
  simp

/-- A probability measure can be interpreted as a finite measure. -/
def toFiniteMeasure (Œº : ProbabilityMeasure Œ©) : FiniteMeasure Œ© := ‚ü®Œº, inferInstance‚ü©

@[simp] lemma coeFn_toFiniteMeasure (Œº : ProbabilityMeasure Œ©) : ‚áëŒº.toFiniteMeasure = Œº := rfl
lemma toFiniteMeasure_apply (Œº : ProbabilityMeasure Œ©) (s : Set Œ©) :
    Œº.toFiniteMeasure s = Œº s := rfl

@[simp]
theorem toMeasure_comp_toFiniteMeasure_eq_toMeasure (ŒΩ : ProbabilityMeasure Œ©) :
    (ŒΩ.toFiniteMeasure : Measure Œ©) = (ŒΩ : Measure Œ©) := rfl

@[simp]
theorem coeFn_comp_toFiniteMeasure_eq_coeFn (ŒΩ : ProbabilityMeasure Œ©) :
    (ŒΩ.toFiniteMeasure : Set Œ© ‚Üí ‚Ñù‚â•0) = (ŒΩ : Set Œ© ‚Üí ‚Ñù‚â•0) := rfl

@[simp]
theorem toFiniteMeasure_apply_eq_apply (ŒΩ : ProbabilityMeasure Œ©) (s : Set Œ©) :
    ŒΩ.toFiniteMeasure s = ŒΩ s := rfl

@[simp]
theorem ennreal_coeFn_eq_coeFn_toMeasure (ŒΩ : ProbabilityMeasure Œ©) (s : Set Œ©) :
    (ŒΩ s : ‚Ñù‚â•0‚àû) = (ŒΩ : Measure Œ©) s := by
  rw [‚Üê coeFn_comp_toFiniteMeasure_eq_coeFn, FiniteMeasure.ennreal_coeFn_eq_coeFn_toMeasure,
    toMeasure_comp_toFiniteMeasure_eq_toMeasure]

@[simp]
theorem null_iff_toMeasure_null (ŒΩ : ProbabilityMeasure Œ©) (s : Set Œ©) :
    ŒΩ s = 0 ‚Üî (ŒΩ : Measure Œ©) s = 0 :=
  ‚ü®fun h ‚Ü¶ by rw [‚Üê ennreal_coeFn_eq_coeFn_toMeasure, h, ENNReal.coe_zero],
   fun h ‚Ü¶ congrArg ENNReal.toNNReal h‚ü©

theorem apply_mono (Œº : ProbabilityMeasure Œ©) {s‚ÇÅ s‚ÇÇ : Set Œ©} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : Œº s‚ÇÅ ‚â§ Œº s‚ÇÇ := by
  rw [‚Üê coeFn_comp_toFiniteMeasure_eq_coeFn]
  exact FiniteMeasure.apply_mono _ h

theorem apply_union_le (Œº : ProbabilityMeasure Œ©) {s‚ÇÅ s‚ÇÇ : Set Œ©} : Œº (s‚ÇÅ ‚à™ s‚ÇÇ) ‚â§ Œº s‚ÇÅ + Œº s‚ÇÇ := by
  rw [‚Üê coeFn_comp_toFiniteMeasure_eq_coeFn]
  exact FiniteMeasure.apply_union_le _

/-- Continuity from below: the measure of the union of a sequence of (not necessarily measurable)
sets is the limit of the measures of the partial unions. -/
protected lemma tendsto_measure_iUnion_accumulate {Œπ : Type*} [Preorder Œπ]
    [IsCountablyGenerated (atTop : Filter Œπ)] {Œº : ProbabilityMeasure Œ©} {f : Œπ ‚Üí Set Œ©} :
    Tendsto (fun i ‚Ü¶ Œº (Accumulate f i)) atTop (ùìù (Œº (‚ãÉ i, f i))) := by
  simpa [‚Üê ennreal_coeFn_eq_coeFn_toMeasure, ENNReal.tendsto_coe]
    using tendsto_measure_iUnion_accumulate (Œº := Œº.toMeasure)

@[simp] theorem apply_le_one (Œº : ProbabilityMeasure Œ©) (s : Set Œ©) : Œº s ‚â§ 1 := by
  simpa using apply_mono Œº (subset_univ s)

theorem nonempty (Œº : ProbabilityMeasure Œ©) : Nonempty Œ© := by
  by_contra maybe_empty
  have zero : (Œº : Measure Œ©) univ = 0 := by
    rw [univ_eq_empty_iff.mpr (not_nonempty_iff.mp maybe_empty), measure_empty]
  rw [measure_univ] at zero
  exact zero_ne_one zero.symm

@[ext]
theorem eq_of_forall_toMeasure_apply_eq (Œº ŒΩ : ProbabilityMeasure Œ©)
    (h : ‚àÄ s : Set Œ©, MeasurableSet s ‚Üí (Œº : Measure Œ©) s = (ŒΩ : Measure Œ©) s) : Œº = ŒΩ := by
  apply toMeasure_injective
  ext1 s s_mble
  exact h s s_mble

theorem eq_of_forall_apply_eq (Œº ŒΩ : ProbabilityMeasure Œ©)
    (h : ‚àÄ s : Set Œ©, MeasurableSet s ‚Üí Œº s = ŒΩ s) : Œº = ŒΩ := by
  ext1 s s_mble
  simpa [ennreal_coeFn_eq_coeFn_toMeasure] using congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) (h s s_mble)

@[simp]
theorem mass_toFiniteMeasure (Œº : ProbabilityMeasure Œ©) : Œº.toFiniteMeasure.mass = 1 :=
  Œº.coeFn_univ

theorem toFiniteMeasure_nonzero (Œº : ProbabilityMeasure Œ©) : Œº.toFiniteMeasure ‚â† 0 := by
  simp [‚Üê FiniteMeasure.mass_nonzero_iff]

/-- The type of probability measures is a measurable space when equipped with the Giry monad. -/
instance : MeasurableSpace (ProbabilityMeasure Œ©) := Subtype.instMeasurableSpace

lemma measurableSet_isProbabilityMeasure :
    MeasurableSet { Œº : Measure Œ© | IsProbabilityMeasure Œº } := by
  suffices { Œº : Measure Œ© | IsProbabilityMeasure Œº } = (fun Œº => Œº univ) ‚Åª¬π' {1} by
    rw [this]
    exact Measure.measurable_coe MeasurableSet.univ (measurableSet_singleton 1)
  ext _
  apply isProbabilityMeasure_iff

/-- The monoidal product is a measurable function from the product of probability spaces over
`Œ±` and `Œ≤` into the type of probability spaces over `Œ± √ó Œ≤`. Lemma 4.1 of [A synthetic approach to
Markov kernels, conditional independence and theorems on sufficient statistics][fritz2020]. -/
theorem measurable_fun_prod {Œ± Œ≤ : Type*} [MeasurableSpace Œ±] [MeasurableSpace Œ≤] :
    Measurable (fun (Œº : ProbabilityMeasure Œ± √ó ProbabilityMeasure Œ≤)
      ‚Ü¶ Œº.1.toMeasure.prod Œº.2.toMeasure) := by
  apply Measurable.measure_of_isPiSystem_of_isProbabilityMeasure generateFrom_prod.symm
    isPiSystem_prod _
  simp only [mem_image2, mem_setOf_eq, forall_exists_index, and_imp]
  intro _ u Hu v Hv Heq
  simp_rw [‚Üê Heq, Measure.prod_prod]
  apply Measurable.mul
  ¬∑ exact (Measure.measurable_coe Hu).comp (measurable_subtype_coe.comp measurable_fst)
  ¬∑ exact (Measure.measurable_coe Hv).comp (measurable_subtype_coe.comp measurable_snd)

lemma apply_iUnion_le {Œº : ProbabilityMeasure Œ©} {f : ‚Ñï ‚Üí Set Œ©}
    (hf : Summable fun n ‚Ü¶ Œº (f n)) :
    Œº (‚ãÉ n, f n) ‚â§ ‚àë' n, Œº (f n) := by
  simpa [‚Üê ENNReal.coe_le_coe, ENNReal.coe_tsum hf] using MeasureTheory.measure_iUnion_le f

section convergence_in_distribution

variable [TopologicalSpace Œ©] [OpensMeasurableSpace Œ©]

theorem testAgainstNN_lipschitz (Œº : ProbabilityMeasure Œ©) :
    LipschitzWith 1 fun f : Œ© ‚Üí·µá ‚Ñù‚â•0 ‚Ü¶ Œº.toFiniteMeasure.testAgainstNN f :=
  Œº.mass_toFiniteMeasure ‚ñ∏ Œº.toFiniteMeasure.testAgainstNN_lipschitz

/-- The topology of weak convergence on `MeasureTheory.ProbabilityMeasure Œ©`. This is inherited
(induced) from the topology of weak convergence of finite measures via the inclusion
`MeasureTheory.ProbabilityMeasure.toFiniteMeasure`. -/
instance : TopologicalSpace (ProbabilityMeasure Œ©) :=
  TopologicalSpace.induced toFiniteMeasure inferInstance

theorem toFiniteMeasure_continuous :
    Continuous (toFiniteMeasure : ProbabilityMeasure Œ© ‚Üí FiniteMeasure Œ©) :=
  continuous_induced_dom

/-- Probability measures yield elements of the `WeakDual` of bounded continuous nonnegative
functions via `MeasureTheory.FiniteMeasure.testAgainstNN`, i.e., integration. -/
def toWeakDualBCNN : ProbabilityMeasure Œ© ‚Üí WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0) :=
  FiniteMeasure.toWeakDualBCNN ‚àò toFiniteMeasure

@[simp]
theorem coe_toWeakDualBCNN (Œº : ProbabilityMeasure Œ©) :
    ‚áëŒº.toWeakDualBCNN = Œº.toFiniteMeasure.testAgainstNN := rfl

@[simp]
theorem toWeakDualBCNN_apply (Œº : ProbabilityMeasure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.toWeakDualBCNN f = (‚à´‚Åª œâ, f œâ ‚àÇ(Œº : Measure Œ©)).toNNReal := rfl

theorem toWeakDualBCNN_continuous : Continuous fun Œº : ProbabilityMeasure Œ© ‚Ü¶ Œº.toWeakDualBCNN :=
  FiniteMeasure.toWeakDualBCNN_continuous.comp toFiniteMeasure_continuous

/- Integration of (nonnegative bounded continuous) test functions against Borel probability
measures depends continuously on the measure. -/
theorem continuous_testAgainstNN_eval (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Continuous fun Œº : ProbabilityMeasure Œ© ‚Ü¶ Œº.toFiniteMeasure.testAgainstNN f :=
  (FiniteMeasure.continuous_testAgainstNN_eval f).comp toFiniteMeasure_continuous

-- The canonical mapping from probability measures to finite measures is an embedding.
theorem toFiniteMeasure_isEmbedding (Œ© : Type*) [MeasurableSpace Œ©] [TopologicalSpace Œ©]
    [OpensMeasurableSpace Œ©] :
    IsEmbedding (toFiniteMeasure : ProbabilityMeasure Œ© ‚Üí FiniteMeasure Œ©) where
  eq_induced := rfl
  injective _Œº _ŒΩ h := Subtype.ext <| congr_arg FiniteMeasure.toMeasure h

theorem tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds {Œ¥ : Type*} (F : Filter Œ¥)
    {Œºs : Œ¥ ‚Üí ProbabilityMeasure Œ©} {Œº‚ÇÄ : ProbabilityMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº‚ÇÄ) ‚Üî Tendsto (toFiniteMeasure ‚àò Œºs) F (ùìù Œº‚ÇÄ.toFiniteMeasure) :=
  (toFiniteMeasure_isEmbedding Œ©).tendsto_nhds_iff

/-- A characterization of weak convergence of probability measures by the condition that the
integrals of every continuous bounded nonnegative function converge to the integral of the function
against the limit measure. -/
theorem tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí ProbabilityMeasure Œ©} {Œº : ProbabilityMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ‚Ñù‚â•0,
        Tendsto (fun i ‚Ü¶ ‚à´‚Åª œâ, f œâ ‚àÇ(Œºs i : Measure Œ©)) F (ùìù (‚à´‚Åª œâ, f œâ ‚àÇ(Œº : Measure Œ©))) := by
  rw [tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds]
  exact FiniteMeasure.tendsto_iff_forall_lintegral_tendsto

/-- The characterization of weak convergence of probability measures by the usual (defining)
condition that the integrals of every continuous bounded function converge to the integral of the
function against the limit measure. -/
theorem tendsto_iff_forall_integral_tendsto {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí ProbabilityMeasure Œ©} {Œº : ProbabilityMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ‚Ñù,
        Tendsto (fun i ‚Ü¶ ‚à´ œâ, f œâ ‚àÇ(Œºs i : Measure Œ©)) F (ùìù (‚à´ œâ, f œâ ‚àÇ(Œº : Measure Œ©))) := by
  simp [tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds,
    FiniteMeasure.tendsto_iff_forall_integral_tendsto]

theorem tendsto_iff_forall_integral_rclike_tendsto {Œ≥ : Type*} (ùïú : Type*) [RCLike ùïú]
    {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí ProbabilityMeasure Œ©} {Œº : ProbabilityMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ùïú,
        Tendsto (fun i ‚Ü¶ ‚à´ œâ, f œâ ‚àÇ(Œºs i : Measure Œ©)) F (ùìù (‚à´ œâ, f œâ ‚àÇ(Œº : Measure Œ©))) := by
  simp [tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds,
    FiniteMeasure.tendsto_iff_forall_integral_rclike_tendsto ùïú]

lemma continuous_integral_boundedContinuousFunction
    {Œ± : Type*} [TopologicalSpace Œ±] [MeasurableSpace Œ±] [OpensMeasurableSpace Œ±] (f : Œ± ‚Üí·µá ‚Ñù) :
    Continuous fun Œº : ProbabilityMeasure Œ± ‚Ü¶ ‚à´ x, f x ‚àÇŒº := by
  rw [continuous_iff_continuousAt]
  intro Œº
  exact continuousAt_of_tendsto_nhds
    (ProbabilityMeasure.tendsto_iff_forall_integral_tendsto.mp tendsto_id f)

end convergence_in_distribution -- section

section Hausdorff

variable [TopologicalSpace Œ©] [HasOuterApproxClosed Œ©] [BorelSpace Œ©]
variable (Œ©)

/-- On topological spaces where indicators of closed sets have decreasing approximating sequences of
continuous functions (`HasOuterApproxClosed`), the topology of convergence in distribution of Borel
probability measures is Hausdorff (`T2Space`). -/
instance t2Space : T2Space (ProbabilityMeasure Œ©) := (toFiniteMeasure_isEmbedding Œ©).t2Space

end Hausdorff -- section

end ProbabilityMeasure

-- namespace
end ProbabilityMeasure

-- section
section NormalizeFiniteMeasure

/-! ### Normalization of finite measures to probability measures

This section is about normalizing finite measures to probability measures.

The weak convergence of finite measures to nonzero limit measures is characterized by
the convergence of the total mass and the convergence of the normalized probability
measures.
-/

namespace FiniteMeasure

variable {Œ© : Type*} [Nonempty Œ©] {m0 : MeasurableSpace Œ©} (Œº : FiniteMeasure Œ©)

/-- Normalize a finite measure so that it becomes a probability measure, i.e., divide by the
total mass. -/
def normalize : ProbabilityMeasure Œ© :=
  if zero : Œº.mass = 0 then ‚ü®Measure.dirac ‚ÄπNonempty Œ©‚Ä∫.some, Measure.dirac.isProbabilityMeasure‚ü©
  else
    { val := Œº.mass‚Åª¬π ‚Ä¢ (Œº : Measure Œ©)
      property := by
        refine ‚ü®?_‚ü©
        simp only [Measure.coe_smul, Pi.smul_apply, Measure.nnreal_smul_coe_apply,
          ENNReal.coe_inv zero, ennreal_mass]
        rw [‚Üê Ne, ‚Üê ENNReal.coe_ne_zero, ennreal_mass] at zero
        exact ENNReal.inv_mul_cancel zero Œº.prop.measure_univ_lt_top.ne }

@[simp]
theorem self_eq_mass_mul_normalize (s : Set Œ©) : Œº s = Œº.mass * Œº.normalize s := by
  obtain rfl | h := eq_or_ne Œº 0
  ¬∑ simp
  have mass_nonzero : Œº.mass ‚â† 0 := by rwa [Œº.mass_nonzero_iff]
  simp only [normalize, dif_neg mass_nonzero]
  simp [mul_inv_cancel_left‚ÇÄ mass_nonzero, coeFn_def]

theorem self_eq_mass_smul_normalize : Œº = Œº.mass ‚Ä¢ Œº.normalize.toFiniteMeasure := by
  apply eq_of_forall_apply_eq
  intro s _s_mble
  rw [Œº.self_eq_mass_mul_normalize s, smul_apply, smul_eq_mul,
    ProbabilityMeasure.coeFn_comp_toFiniteMeasure_eq_coeFn]

theorem normalize_eq_of_nonzero (nonzero : Œº ‚â† 0) (s : Set Œ©) : Œº.normalize s = Œº.mass‚Åª¬π * Œº s := by
  simp only [Œº.self_eq_mass_mul_normalize, Œº.mass_nonzero_iff.mpr nonzero, inv_mul_cancel_left‚ÇÄ,
    Ne, not_false_iff]

theorem normalize_eq_inv_mass_smul_of_nonzero (nonzero : Œº ‚â† 0) :
    Œº.normalize.toFiniteMeasure = Œº.mass‚Åª¬π ‚Ä¢ Œº := by
  nth_rw 3 [Œº.self_eq_mass_smul_normalize]
  rw [‚Üê smul_assoc]
  simp only [Œº.mass_nonzero_iff.mpr nonzero, Algebra.id.smul_eq_mul, inv_mul_cancel‚ÇÄ, Ne,
    not_false_iff, one_smul]

theorem toMeasure_normalize_eq_of_nonzero (nonzero : Œº ‚â† 0) :
    (Œº.normalize : Measure Œ©) = Œº.mass‚Åª¬π ‚Ä¢ Œº := by
  ext1 s _s_mble
  rw [‚Üê Œº.normalize.ennreal_coeFn_eq_coeFn_toMeasure s, Œº.normalize_eq_of_nonzero nonzero s,
    ENNReal.coe_mul, ennreal_coeFn_eq_coeFn_toMeasure]
  exact Measure.coe_nnreal_smul_apply _ _ _

@[simp]
theorem _root_.ProbabilityMeasure.toFiniteMeasure_normalize_eq_self {m0 : MeasurableSpace Œ©}
    (Œº : ProbabilityMeasure Œ©) : Œº.toFiniteMeasure.normalize = Œº := by
  apply ProbabilityMeasure.eq_of_forall_apply_eq
  intro s _s_mble
  rw [Œº.toFiniteMeasure.normalize_eq_of_nonzero Œº.toFiniteMeasure_nonzero s]
  simp only [ProbabilityMeasure.mass_toFiniteMeasure, inv_one, one_mul, Œº.coeFn_toFiniteMeasure]

/-- Averaging with respect to a finite measure is the same as integrating against
`MeasureTheory.FiniteMeasure.normalize`. -/
theorem average_eq_integral_normalize {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    (nonzero : Œº ‚â† 0) (f : Œ© ‚Üí E) :
    average (Œº : Measure Œ©) f = ‚à´ œâ, f œâ ‚àÇ(Œº.normalize : Measure Œ©) := by
  rw [Œº.toMeasure_normalize_eq_of_nonzero nonzero, average]
  congr
  simp [ENNReal.coe_inv (Œº.mass_nonzero_iff.mpr nonzero), ennreal_mass]

variable [TopologicalSpace Œ©]

theorem testAgainstNN_eq_mass_mul (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.testAgainstNN f = Œº.mass * Œº.normalize.toFiniteMeasure.testAgainstNN f := by
  nth_rw 1 [Œº.self_eq_mass_smul_normalize]
  rw [Œº.normalize.toFiniteMeasure.smul_testAgainstNN_apply Œº.mass f, smul_eq_mul]

theorem normalize_testAgainstNN (nonzero : Œº ‚â† 0) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.normalize.toFiniteMeasure.testAgainstNN f = Œº.mass‚Åª¬π * Œº.testAgainstNN f := by
  simp [Œº.testAgainstNN_eq_mass_mul, inv_mul_cancel_left‚ÇÄ <| Œº.mass_nonzero_iff.mpr nonzero]

variable [OpensMeasurableSpace Œ©]
variable {Œº}

theorem tendsto_testAgainstNN_of_tendsto_normalize_testAgainstNN_of_tendsto_mass {Œ≥ : Type*}
    {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    (Œºs_lim : Tendsto (fun i ‚Ü¶ (Œºs i).normalize) F (ùìù Œº.normalize))
    (mass_lim : Tendsto (fun i ‚Ü¶ (Œºs i).mass) F (ùìù Œº.mass)) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Tendsto (fun i ‚Ü¶ (Œºs i).testAgainstNN f) F (ùìù (Œº.testAgainstNN f)) := by
  by_cases h_mass : Œº.mass = 0
  ¬∑ simp only [Œº.mass_zero_iff.mp h_mass, zero_testAgainstNN_apply, zero_mass] at mass_lim ‚ä¢
    exact tendsto_zero_testAgainstNN_of_tendsto_zero_mass mass_lim f
  simp_rw [fun i ‚Ü¶ (Œºs i).testAgainstNN_eq_mass_mul f, Œº.testAgainstNN_eq_mass_mul f]
  rw [ProbabilityMeasure.tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds] at Œºs_lim
  rw [tendsto_iff_forall_testAgainstNN_tendsto] at Œºs_lim
  have lim_pair :
    Tendsto (fun i ‚Ü¶ (‚ü®(Œºs i).mass, (Œºs i).normalize.toFiniteMeasure.testAgainstNN f‚ü© : ‚Ñù‚â•0 √ó ‚Ñù‚â•0))
      F (ùìù ‚ü®Œº.mass, Œº.normalize.toFiniteMeasure.testAgainstNN f‚ü©) :=
    (Prod.tendsto_iff _ _).mpr ‚ü®mass_lim, Œºs_lim f‚ü©
  exact tendsto_mul.comp lim_pair

theorem tendsto_normalize_testAgainstNN_of_tendsto {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©} (Œºs_lim : Tendsto Œºs F (ùìù Œº)) (nonzero : Œº ‚â† 0) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Tendsto (fun i ‚Ü¶ (Œºs i).normalize.toFiniteMeasure.testAgainstNN f) F
      (ùìù (Œº.normalize.toFiniteMeasure.testAgainstNN f)) := by
  have lim_mass := Œºs_lim.mass
  have aux : {(0 : ‚Ñù‚â•0)}·∂ú ‚àà ùìù Œº.mass :=
    isOpen_compl_singleton.mem_nhds (Œº.mass_nonzero_iff.mpr nonzero)
  have eventually_nonzero : ‚àÄ·∂† i in F, Œºs i ‚â† 0 := by
    simp_rw [‚Üê mass_nonzero_iff]
    exact lim_mass aux
  have eve : ‚àÄ·∂† i in F,
      (Œºs i).normalize.toFiniteMeasure.testAgainstNN f =
        (Œºs i).mass‚Åª¬π * (Œºs i).testAgainstNN f := by
    filter_upwards [eventually_iff.mp eventually_nonzero]
    intro i hi
    apply normalize_testAgainstNN _ hi
  simp_rw [tendsto_congr' eve, Œº.normalize_testAgainstNN nonzero]
  have lim_pair :
    Tendsto (fun i ‚Ü¶ (‚ü®(Œºs i).mass‚Åª¬π, (Œºs i).testAgainstNN f‚ü© : ‚Ñù‚â•0 √ó ‚Ñù‚â•0)) F
      (ùìù ‚ü®Œº.mass‚Åª¬π, Œº.testAgainstNN f‚ü©) := by
    refine (Prod.tendsto_iff _ _).mpr ‚ü®?_, ?_‚ü©
    ¬∑ exact (continuousOn_inv‚ÇÄ.continuousAt aux).tendsto.comp lim_mass
    ¬∑ exact tendsto_iff_forall_testAgainstNN_tendsto.mp Œºs_lim f
  exact tendsto_mul.comp lim_pair

/-- If the normalized versions of finite measures converge weakly and their total masses
also converge, then the finite measures themselves converge weakly. -/
theorem tendsto_of_tendsto_normalize_testAgainstNN_of_tendsto_mass {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©} (Œºs_lim : Tendsto (fun i ‚Ü¶ (Œºs i).normalize) F (ùìù Œº.normalize))
    (mass_lim : Tendsto (fun i ‚Ü¶ (Œºs i).mass) F (ùìù Œº.mass)) : Tendsto Œºs F (ùìù Œº) := by
  rw [tendsto_iff_forall_testAgainstNN_tendsto]
  exact fun f ‚Ü¶
    tendsto_testAgainstNN_of_tendsto_normalize_testAgainstNN_of_tendsto_mass Œºs_lim mass_lim f

/-- If finite measures themselves converge weakly to a nonzero limit measure, then their
normalized versions also converge weakly. -/
theorem tendsto_normalize_of_tendsto {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    (Œºs_lim : Tendsto Œºs F (ùìù Œº)) (nonzero : Œº ‚â† 0) :
    Tendsto (fun i ‚Ü¶ (Œºs i).normalize) F (ùìù Œº.normalize) := by
  rw [ProbabilityMeasure.tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds,
    tendsto_iff_forall_testAgainstNN_tendsto]
  exact fun f ‚Ü¶ tendsto_normalize_testAgainstNN_of_tendsto Œºs_lim nonzero f

/-- The weak convergence of finite measures to a nonzero limit can be characterized by the weak
convergence of both their normalized versions (probability measures) and their total masses. -/
theorem tendsto_normalize_iff_tendsto {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    (nonzero : Œº ‚â† 0) :
    Tendsto (fun i ‚Ü¶ (Œºs i).normalize) F (ùìù Œº.normalize) ‚àß
        Tendsto (fun i ‚Ü¶ (Œºs i).mass) F (ùìù Œº.mass) ‚Üî
      Tendsto Œºs F (ùìù Œº) := by
  constructor
  ¬∑ rintro ‚ü®normalized_lim, mass_lim‚ü©
    exact tendsto_of_tendsto_normalize_testAgainstNN_of_tendsto_mass normalized_lim mass_lim
  ¬∑ intro Œºs_lim
    exact ‚ü®tendsto_normalize_of_tendsto Œºs_lim nonzero, Œºs_lim.mass‚ü©

end FiniteMeasure --namespace

end NormalizeFiniteMeasure -- section

section map

variable {Œ© Œ©' : Type*} [MeasurableSpace Œ©] [MeasurableSpace Œ©']

namespace ProbabilityMeasure

/-- The push-forward of a probability measure by a measurable function. -/
noncomputable def map (ŒΩ : ProbabilityMeasure Œ©) {f : Œ© ‚Üí Œ©'} (f_aemble : AEMeasurable f ŒΩ) :
    ProbabilityMeasure Œ©' :=
  ‚ü®(ŒΩ : Measure Œ©).map f,
   ‚ü®by simp only [Measure.map_apply_of_aemeasurable f_aemble MeasurableSet.univ,
                  preimage_univ, measure_univ]‚ü©‚ü©

@[simp] lemma toMeasure_map (ŒΩ : ProbabilityMeasure Œ©) {f : Œ© ‚Üí Œ©'} (hf : AEMeasurable f ŒΩ) :
    (ŒΩ.map hf).toMeasure = ŒΩ.toMeasure.map f := rfl

/-- Note that this is an equality of elements of `‚Ñù‚â•0‚àû`. See also
`MeasureTheory.ProbabilityMeasure.map_apply` for the corresponding equality as elements of `‚Ñù‚â•0`. -/
lemma map_apply' (ŒΩ : ProbabilityMeasure Œ©) {f : Œ© ‚Üí Œ©'} (f_aemble : AEMeasurable f ŒΩ)
    {A : Set Œ©'} (A_mble : MeasurableSet A) :
    (ŒΩ.map f_aemble : Measure Œ©') A = (ŒΩ : Measure Œ©) (f ‚Åª¬π' A) :=
  Measure.map_apply_of_aemeasurable f_aemble A_mble

lemma map_apply_of_aemeasurable (ŒΩ : ProbabilityMeasure Œ©) {f : Œ© ‚Üí Œ©'}
    (f_aemble : AEMeasurable f ŒΩ) {A : Set Œ©'} (A_mble : MeasurableSet A) :
    (ŒΩ.map f_aemble) A = ŒΩ (f ‚Åª¬π' A) := by
  exact (ENNReal.toNNReal_eq_toNNReal_iff' (measure_ne_top _ _) (measure_ne_top _ _)).mpr <|
    ŒΩ.map_apply' f_aemble A_mble

lemma map_apply (ŒΩ : ProbabilityMeasure Œ©) {f : Œ© ‚Üí Œ©'} (f_aemble : AEMeasurable f ŒΩ)
    {A : Set Œ©'} (A_mble : MeasurableSet A) :
    (ŒΩ.map f_aemble) A = ŒΩ (f ‚Åª¬π' A) :=
  map_apply_of_aemeasurable ŒΩ f_aemble A_mble

variable [TopologicalSpace Œ©] [OpensMeasurableSpace Œ©]
variable [TopologicalSpace Œ©'] [BorelSpace Œ©']

/-- If `f : X ‚Üí Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
convergence (in distribution) of `ProbabilityMeasure`s on `X` implies convergence (in
distribution) of the push-forwards of these measures by `f`. -/
lemma tendsto_map_of_tendsto_of_continuous {Œπ : Type*} {L : Filter Œπ}
    (ŒΩs : Œπ ‚Üí ProbabilityMeasure Œ©) (ŒΩ : ProbabilityMeasure Œ©) (lim : Tendsto ŒΩs L (ùìù ŒΩ))
    {f : Œ© ‚Üí Œ©'} (f_cont : Continuous f) :
    Tendsto (fun i ‚Ü¶ (ŒΩs i).map f_cont.measurable.aemeasurable) L
      (ùìù (ŒΩ.map f_cont.measurable.aemeasurable)) := by
  rw [ProbabilityMeasure.tendsto_iff_forall_lintegral_tendsto] at lim ‚ä¢
  intro g
  convert lim (g.compContinuous ‚ü®f, f_cont‚ü©) <;>
  ¬∑ simp only [map, compContinuous_apply, ContinuousMap.coe_mk]
    refine lintegral_map ?_ f_cont.measurable
    exact (ENNReal.continuous_coe.comp g.continuous).measurable

/-- If `f : X ‚Üí Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
the push-forward of probability measures `f* : ProbabilityMeasure X ‚Üí ProbabilityMeasure Y`
is continuous (in the topologies of convergence in distribution). -/
lemma continuous_map {f : Œ© ‚Üí Œ©'} (f_cont : Continuous f) :
    Continuous (fun ŒΩ ‚Ü¶ ProbabilityMeasure.map ŒΩ f_cont.measurable.aemeasurable) := by
  rw [continuous_iff_continuousAt]
  exact fun _ ‚Ü¶ tendsto_map_of_tendsto_of_continuous _ _ continuous_id.continuousAt f_cont

end ProbabilityMeasure -- namespace

end map -- section

end MeasureTheory -- namespace
