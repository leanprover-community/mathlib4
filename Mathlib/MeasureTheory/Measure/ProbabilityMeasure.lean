/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.MeasureTheory.Measure.FiniteMeasure
import Mathlib.MeasureTheory.Integral.Average
import Mathlib.MeasureTheory.Measure.Prod

/-!
# Probability measures

This file defines the type of probability measures on a given measurable space. When the underlying
space has a topology and the measurable space structure (sigma algebra) is finer than the Borel
sigma algebra, then the type of probability measures is equipped with the topology of convergence
in distribution (weak convergence of measures). The topology of convergence in distribution is the
coarsest topology w.r.t. which for every bounded continuous `â„â‰¥0`-valued random variable `X`, the
expected value of `X` depends continuously on the choice of probability measure. This is a special
case of the topology of weak convergence of finite measures.

## Main definitions

The main definitions are
 * the type `MeasureTheory.ProbabilityMeasure Î©` with the topology of convergence in
   distribution (a.k.a. convergence in law, weak convergence of measures);
 * `MeasureTheory.ProbabilityMeasure.toFiniteMeasure`: Interpret a probability measure as
   a finite measure;
 * `MeasureTheory.FiniteMeasure.normalize`: Normalize a finite measure to a probability measure
   (returns junk for the zero measure).
 * `MeasureTheory.ProbabilityMeasure.map`: The push-forward `f* Î¼` of a probability measure
   `Î¼` on `Î©` along a measurable function `f : Î© â†’ Î©'`.

## Main results

 * `MeasureTheory.ProbabilityMeasure.tendsto_iff_forall_integral_tendsto`: Convergence of
   probability measures is characterized by the convergence of expected values of all bounded
   continuous random variables. This shows that the chosen definition of topology coincides with
   the common textbook definition of convergence in distribution, i.e., weak convergence of
   measures. A similar characterization by the convergence of expected values (in the
   `MeasureTheory.lintegral` sense) of all bounded continuous nonnegative random variables is
   `MeasureTheory.ProbabilityMeasure.tendsto_iff_forall_lintegral_tendsto`.
 * `MeasureTheory.FiniteMeasure.tendsto_normalize_iff_tendsto`: The convergence of finite
   measures to a nonzero limit is characterized by the convergence of the probability-normalized
   versions and of the total masses.
 * `MeasureTheory.ProbabilityMeasure.continuous_map`: For a continuous function `f : Î© â†’ Î©'`, the
   push-forward of probability measures `f* : ProbabilityMeasure Î© â†’ ProbabilityMeasure Î©'` is
   continuous.
 * `MeasureTheory.ProbabilityMeasure.t2Space`: The topology of convergence in distribution is
   Hausdorff on Borel spaces where indicators of closed sets have continuous decreasing
   approximating sequences (in particular on any pseudo-metrizable spaces).

TODO:
 * Probability measures form a convex space.

## Implementation notes

The topology of convergence in distribution on `MeasureTheory.ProbabilityMeasure Î©` is inherited
weak convergence of finite measures via the mapping
`MeasureTheory.ProbabilityMeasure.toFiniteMeasure`.

Like `MeasureTheory.FiniteMeasure Î©`, the implementation of `MeasureTheory.ProbabilityMeasure Î©`
is directly as a subtype of `MeasureTheory.Measure Î©`, and the coercion to a function is the
composition `ENNReal.toNNReal` and the coercion to function of `MeasureTheory.Measure Î©`.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

convergence in distribution, convergence in law, weak convergence of measures, probability measure

-/


noncomputable section

open Set Filter BoundedContinuousFunction Topology
open scoped ENNReal NNReal

namespace MeasureTheory

section ProbabilityMeasure

/-! ### Probability measures

In this section we define the type of probability measures on a measurable space `Î©`, denoted by
`MeasureTheory.ProbabilityMeasure Î©`.

If `Î©` is moreover a topological space and the sigma algebra on `Î©` is finer than the Borel sigma
algebra (i.e. `[OpensMeasurableSpace Î©]`), then `MeasureTheory.ProbabilityMeasure Î©` is
equipped with the topology of weak convergence of measures. Since every probability measure is a
finite measure, this is implemented as the induced topology from the mapping
`MeasureTheory.ProbabilityMeasure.toFiniteMeasure`.
-/


/-- Probability measures are defined as the subtype of measures that have the property of being
probability measures (i.e., their total mass is one). -/
def ProbabilityMeasure (Î© : Type*) [MeasurableSpace Î©] : Type _ :=
  { Î¼ : Measure Î© // IsProbabilityMeasure Î¼ }

namespace ProbabilityMeasure

variable {Î© : Type*} [MeasurableSpace Î©]

instance [Inhabited Î©] : Inhabited (ProbabilityMeasure Î©) :=
  âŸ¨âŸ¨Measure.dirac default, Measure.dirac.isProbabilityMeasureâŸ©âŸ©

/-- Coercion from `MeasureTheory.ProbabilityMeasure Î©` to `MeasureTheory.Measure Î©`. -/
@[coe]
def toMeasure : ProbabilityMeasure Î© â†’ Measure Î© := Subtype.val

/-- A probability measure can be interpreted as a measure. -/
instance : Coe (ProbabilityMeasure Î©) (MeasureTheory.Measure Î©) := { coe := toMeasure }

instance (Î¼ : ProbabilityMeasure Î©) : IsProbabilityMeasure (Î¼ : Measure Î©) :=
  Î¼.prop

@[simp, norm_cast] lemma coe_mk (Î¼ : Measure Î©) (hÎ¼) : toMeasure âŸ¨Î¼, hÎ¼âŸ© = Î¼ := rfl

@[simp]
theorem val_eq_to_measure (Î½ : ProbabilityMeasure Î©) : Î½.val = (Î½ : Measure Î©) := rfl

theorem toMeasure_injective : Function.Injective ((â†‘) : ProbabilityMeasure Î© â†’ Measure Î©) :=
  Subtype.coe_injective

/-- The type of probability measures is a measurable space when equipped with the Giry monad. -/
instance {Î± : Type*} [MeasurableSpace Î±] : MeasurableSpace (ProbabilityMeasure Î±) :=
    Subtype.instMeasurableSpace

lemma measurableSet_isProbabilityMeasure {Î± : Type*} [MeasurableSpace Î±] :
    MeasurableSet { Î¼ : Measure Î± | IsProbabilityMeasure Î¼ } := by
  suffices { Î¼ : Measure Î± | IsProbabilityMeasure Î¼ } = (fun Î¼ => Î¼ univ) â»Â¹' {1} by
    rw [this]
    exact Measure.measurable_coe MeasurableSet.univ (measurableSet_singleton 1)
  ext _
  apply isProbabilityMeasure_iff

instance instFunLike : FunLike (ProbabilityMeasure Î©) (Set Î©) â„â‰¥0 where
  coe Î¼ s := ((Î¼ : Measure Î©) s).toNNReal
  coe_injective' Î¼ Î½ h := toMeasure_injective <| Measure.ext fun s _ â†¦ by
    simpa [ENNReal.toNNReal_eq_toNNReal_iff, measure_ne_top] using congr_fun h s

lemma coeFn_def (Î¼ : ProbabilityMeasure Î©) : Î¼ = fun s â†¦ ((Î¼ : Measure Î©) s).toNNReal := rfl

lemma coeFn_mk (Î¼ : Measure Î©) (hÎ¼) :
    DFunLike.coe (F := ProbabilityMeasure Î©) âŸ¨Î¼, hÎ¼âŸ© = fun s â†¦ (Î¼ s).toNNReal := rfl

@[simp, norm_cast]
lemma mk_apply (Î¼ : Measure Î©) (hÎ¼) (s : Set Î©) :
    DFunLike.coe (F := ProbabilityMeasure Î©) âŸ¨Î¼, hÎ¼âŸ© s = (Î¼ s).toNNReal := rfl

@[simp, norm_cast]
theorem coeFn_univ (Î½ : ProbabilityMeasure Î©) : Î½ univ = 1 :=
  congr_arg ENNReal.toNNReal Î½.prop.measure_univ

theorem coeFn_univ_ne_zero (Î½ : ProbabilityMeasure Î©) : Î½ univ â‰  0 := by
  simp only [coeFn_univ, Ne, one_ne_zero, not_false_iff]

/-- A probability measure can be interpreted as a finite measure. -/
def toFiniteMeasure (Î¼ : ProbabilityMeasure Î©) : FiniteMeasure Î© := âŸ¨Î¼, inferInstanceâŸ©

@[simp] lemma coeFn_toFiniteMeasure (Î¼ : ProbabilityMeasure Î©) : â‡‘Î¼.toFiniteMeasure = Î¼ := rfl
lemma toFiniteMeasure_apply (Î¼ : ProbabilityMeasure Î©) (s : Set Î©) :
    Î¼.toFiniteMeasure s = Î¼ s := rfl

@[simp]
theorem toMeasure_comp_toFiniteMeasure_eq_toMeasure (Î½ : ProbabilityMeasure Î©) :
    (Î½.toFiniteMeasure : Measure Î©) = (Î½ : Measure Î©) := rfl

@[simp]
theorem coeFn_comp_toFiniteMeasure_eq_coeFn (Î½ : ProbabilityMeasure Î©) :
    (Î½.toFiniteMeasure : Set Î© â†’ â„â‰¥0) = (Î½ : Set Î© â†’ â„â‰¥0) := rfl

@[simp]
theorem toFiniteMeasure_apply_eq_apply (Î½ : ProbabilityMeasure Î©) (s : Set Î©) :
    Î½.toFiniteMeasure s = Î½ s := rfl

@[simp]
theorem ennreal_coeFn_eq_coeFn_toMeasure (Î½ : ProbabilityMeasure Î©) (s : Set Î©) :
    (Î½ s : â„â‰¥0âˆ) = (Î½ : Measure Î©) s := by
  rw [â† coeFn_comp_toFiniteMeasure_eq_coeFn, FiniteMeasure.ennreal_coeFn_eq_coeFn_toMeasure,
    toMeasure_comp_toFiniteMeasure_eq_toMeasure]

@[simp]
theorem null_iff_toMeasure_null (Î½ : ProbabilityMeasure Î©) (s : Set Î©) :
    Î½ s = 0 â†” (Î½ : Measure Î©) s = 0 :=
  âŸ¨fun h â†¦ by rw [â† ennreal_coeFn_eq_coeFn_toMeasure, h, ENNReal.coe_zero],
   fun h â†¦ congrArg ENNReal.toNNReal hâŸ©

theorem apply_mono (Î¼ : ProbabilityMeasure Î©) {sâ‚ sâ‚‚ : Set Î©} (h : sâ‚ âŠ† sâ‚‚) : Î¼ sâ‚ â‰¤ Î¼ sâ‚‚ := by
  rw [â† coeFn_comp_toFiniteMeasure_eq_coeFn]
  exact MeasureTheory.FiniteMeasure.apply_mono _ h

@[simp] theorem apply_le_one (Î¼ : ProbabilityMeasure Î©) (s : Set Î©) : Î¼ s â‰¤ 1 := by
  simpa using apply_mono Î¼ (subset_univ s)

theorem nonempty (Î¼ : ProbabilityMeasure Î©) : Nonempty Î© := by
  by_contra maybe_empty
  have zero : (Î¼ : Measure Î©) univ = 0 := by
    rw [univ_eq_empty_iff.mpr (not_nonempty_iff.mp maybe_empty), measure_empty]
  rw [measure_univ] at zero
  exact zero_ne_one zero.symm

@[ext]
theorem eq_of_forall_toMeasure_apply_eq (Î¼ Î½ : ProbabilityMeasure Î©)
    (h : âˆ€ s : Set Î©, MeasurableSet s â†’ (Î¼ : Measure Î©) s = (Î½ : Measure Î©) s) : Î¼ = Î½ := by
  apply toMeasure_injective
  ext1 s s_mble
  exact h s s_mble

theorem eq_of_forall_apply_eq (Î¼ Î½ : ProbabilityMeasure Î©)
    (h : âˆ€ s : Set Î©, MeasurableSet s â†’ Î¼ s = Î½ s) : Î¼ = Î½ := by
  ext1 s s_mble
  simpa [ennreal_coeFn_eq_coeFn_toMeasure] using congr_arg ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) (h s s_mble)

@[simp]
theorem mass_toFiniteMeasure (Î¼ : ProbabilityMeasure Î©) : Î¼.toFiniteMeasure.mass = 1 :=
  Î¼.coeFn_univ

theorem toFiniteMeasure_nonzero (Î¼ : ProbabilityMeasure Î©) : Î¼.toFiniteMeasure â‰  0 := by
  simp [â† FiniteMeasure.mass_nonzero_iff]

section convergence_in_distribution

variable [TopologicalSpace Î©] [OpensMeasurableSpace Î©]

theorem testAgainstNN_lipschitz (Î¼ : ProbabilityMeasure Î©) :
    LipschitzWith 1 fun f : Î© â†’áµ‡ â„â‰¥0 â†¦ Î¼.toFiniteMeasure.testAgainstNN f :=
  Î¼.mass_toFiniteMeasure â–¸ Î¼.toFiniteMeasure.testAgainstNN_lipschitz

/-- The topology of weak convergence on `MeasureTheory.ProbabilityMeasure Î©`. This is inherited
(induced) from the topology of weak convergence of finite measures via the inclusion
`MeasureTheory.ProbabilityMeasure.toFiniteMeasure`. -/
instance : TopologicalSpace (ProbabilityMeasure Î©) :=
  TopologicalSpace.induced toFiniteMeasure inferInstance

theorem toFiniteMeasure_continuous :
    Continuous (toFiniteMeasure : ProbabilityMeasure Î© â†’ FiniteMeasure Î©) :=
  continuous_induced_dom

/-- Probability measures yield elements of the `WeakDual` of bounded continuous nonnegative
functions via `MeasureTheory.FiniteMeasure.testAgainstNN`, i.e., integration. -/
def toWeakDualBCNN : ProbabilityMeasure Î© â†’ WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0) :=
  FiniteMeasure.toWeakDualBCNN âˆ˜ toFiniteMeasure

@[simp]
theorem coe_toWeakDualBCNN (Î¼ : ProbabilityMeasure Î©) :
    â‡‘Î¼.toWeakDualBCNN = Î¼.toFiniteMeasure.testAgainstNN := rfl

@[simp]
theorem toWeakDualBCNN_apply (Î¼ : ProbabilityMeasure Î©) (f : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.toWeakDualBCNN f = (âˆ«â» Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©)).toNNReal := rfl

theorem toWeakDualBCNN_continuous : Continuous fun Î¼ : ProbabilityMeasure Î© â†¦ Î¼.toWeakDualBCNN :=
  FiniteMeasure.toWeakDualBCNN_continuous.comp toFiniteMeasure_continuous

/- Integration of (nonnegative bounded continuous) test functions against Borel probability
measures depends continuously on the measure. -/
theorem continuous_testAgainstNN_eval (f : Î© â†’áµ‡ â„â‰¥0) :
    Continuous fun Î¼ : ProbabilityMeasure Î© â†¦ Î¼.toFiniteMeasure.testAgainstNN f :=
  (FiniteMeasure.continuous_testAgainstNN_eval f).comp toFiniteMeasure_continuous

-- The canonical mapping from probability measures to finite measures is an embedding.
theorem toFiniteMeasure_isEmbedding (Î© : Type*) [MeasurableSpace Î©] [TopologicalSpace Î©]
    [OpensMeasurableSpace Î©] :
    IsEmbedding (toFiniteMeasure : ProbabilityMeasure Î© â†’ FiniteMeasure Î©) where
  eq_induced := rfl
  injective _Î¼ _Î½ h := Subtype.eq <| congr_arg FiniteMeasure.toMeasure h

@[deprecated (since := "2024-10-26")]
alias toFiniteMeasure_embedding := toFiniteMeasure_isEmbedding

theorem tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds {Î´ : Type*} (F : Filter Î´)
    {Î¼s : Î´ â†’ ProbabilityMeasure Î©} {Î¼â‚€ : ProbabilityMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼â‚€) â†” Tendsto (toFiniteMeasure âˆ˜ Î¼s) F (ğ“ Î¼â‚€.toFiniteMeasure) :=
  (toFiniteMeasure_isEmbedding Î©).tendsto_nhds_iff

/-- A characterization of weak convergence of probability measures by the condition that the
integrals of every continuous bounded nonnegative function converge to the integral of the function
against the limit measure. -/
theorem tendsto_iff_forall_lintegral_tendsto {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ ProbabilityMeasure Î©} {Î¼ : ProbabilityMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ â„â‰¥0,
        Tendsto (fun i â†¦ âˆ«â» Ï‰, f Ï‰ âˆ‚(Î¼s i : Measure Î©)) F (ğ“ (âˆ«â» Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©))) := by
  rw [tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds]
  exact FiniteMeasure.tendsto_iff_forall_lintegral_tendsto

/-- The characterization of weak convergence of probability measures by the usual (defining)
condition that the integrals of every continuous bounded function converge to the integral of the
function against the limit measure. -/
theorem tendsto_iff_forall_integral_tendsto {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ ProbabilityMeasure Î©} {Î¼ : ProbabilityMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ â„,
        Tendsto (fun i â†¦ âˆ« Ï‰, f Ï‰ âˆ‚(Î¼s i : Measure Î©)) F (ğ“ (âˆ« Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©))) := by
  rw [tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds]
  rw [FiniteMeasure.tendsto_iff_forall_integral_tendsto]
  rfl

lemma continuous_integral_boundedContinuousFunction
    {Î± : Type*} [TopologicalSpace Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±] (f : Î± â†’áµ‡ â„) :
    Continuous fun Î¼ : ProbabilityMeasure Î± â†¦ âˆ« x, f x âˆ‚Î¼ := by
  rw [continuous_iff_continuousAt]
  intro Î¼
  exact continuousAt_of_tendsto_nhds
    (ProbabilityMeasure.tendsto_iff_forall_integral_tendsto.mp tendsto_id f)

end convergence_in_distribution -- section

section Hausdorff

variable [TopologicalSpace Î©] [HasOuterApproxClosed Î©] [BorelSpace Î©]
variable (Î©)

/-- On topological spaces where indicators of closed sets have decreasing approximating sequences of
continuous functions (`HasOuterApproxClosed`), the topology of convergence in distribution of Borel
probability measures is Hausdorff (`T2Space`). -/
instance t2Space : T2Space (ProbabilityMeasure Î©) := (toFiniteMeasure_isEmbedding Î©).t2Space

end Hausdorff -- section

end ProbabilityMeasure

-- namespace
end ProbabilityMeasure

-- section
section NormalizeFiniteMeasure

/-! ### Normalization of finite measures to probability measures

This section is about normalizing finite measures to probability measures.

The weak convergence of finite measures to nonzero limit measures is characterized by
the convergence of the total mass and the convergence of the normalized probability
measures.
-/

namespace FiniteMeasure

variable {Î© : Type*} [Nonempty Î©] {m0 : MeasurableSpace Î©} (Î¼ : FiniteMeasure Î©)

/-- Normalize a finite measure so that it becomes a probability measure, i.e., divide by the
total mass. -/
def normalize : ProbabilityMeasure Î© :=
  if zero : Î¼.mass = 0 then âŸ¨Measure.dirac â€¹Nonempty Î©â€º.some, Measure.dirac.isProbabilityMeasureâŸ©
  else
    { val := â†‘(Î¼.massâ»Â¹ â€¢ Î¼)
      property := by
        refine âŸ¨?_âŸ©
        simp only [toMeasure_smul, Measure.coe_smul, Pi.smul_apply, Measure.nnreal_smul_coe_apply,
          ENNReal.coe_inv zero, ennreal_mass]
        rw [â† Ne, â† ENNReal.coe_ne_zero, ennreal_mass] at zero
        exact ENNReal.inv_mul_cancel zero Î¼.prop.measure_univ_lt_top.ne }

@[simp]
theorem self_eq_mass_mul_normalize (s : Set Î©) : Î¼ s = Î¼.mass * Î¼.normalize s := by
  obtain rfl | h := eq_or_ne Î¼ 0
  Â· simp
  have mass_nonzero : Î¼.mass â‰  0 := by rwa [Î¼.mass_nonzero_iff]
  simp only [normalize, dif_neg mass_nonzero]
  simp [ProbabilityMeasure.coe_mk, toMeasure_smul, mul_inv_cancel_leftâ‚€ mass_nonzero, coeFn_def]

theorem self_eq_mass_smul_normalize : Î¼ = Î¼.mass â€¢ Î¼.normalize.toFiniteMeasure := by
  apply eq_of_forall_apply_eq
  intro s _s_mble
  rw [Î¼.self_eq_mass_mul_normalize s, smul_apply, smul_eq_mul,
    ProbabilityMeasure.coeFn_comp_toFiniteMeasure_eq_coeFn]

theorem normalize_eq_of_nonzero (nonzero : Î¼ â‰  0) (s : Set Î©) : Î¼.normalize s = Î¼.massâ»Â¹ * Î¼ s := by
  simp only [Î¼.self_eq_mass_mul_normalize, Î¼.mass_nonzero_iff.mpr nonzero, inv_mul_cancel_leftâ‚€,
    Ne, not_false_iff]

theorem normalize_eq_inv_mass_smul_of_nonzero (nonzero : Î¼ â‰  0) :
    Î¼.normalize.toFiniteMeasure = Î¼.massâ»Â¹ â€¢ Î¼ := by
  nth_rw 3 [Î¼.self_eq_mass_smul_normalize]
  rw [â† smul_assoc]
  simp only [Î¼.mass_nonzero_iff.mpr nonzero, Algebra.id.smul_eq_mul, inv_mul_cancelâ‚€, Ne,
    not_false_iff, one_smul]

theorem toMeasure_normalize_eq_of_nonzero (nonzero : Î¼ â‰  0) :
    (Î¼.normalize : Measure Î©) = Î¼.massâ»Â¹ â€¢ Î¼ := by
  ext1 s _s_mble
  rw [â† Î¼.normalize.ennreal_coeFn_eq_coeFn_toMeasure s, Î¼.normalize_eq_of_nonzero nonzero s,
    ENNReal.coe_mul, ennreal_coeFn_eq_coeFn_toMeasure]
  exact Measure.coe_nnreal_smul_apply _ _ _

@[simp]
theorem _root_.ProbabilityMeasure.toFiniteMeasure_normalize_eq_self {m0 : MeasurableSpace Î©}
    (Î¼ : ProbabilityMeasure Î©) : Î¼.toFiniteMeasure.normalize = Î¼ := by
  apply ProbabilityMeasure.eq_of_forall_apply_eq
  intro s _s_mble
  rw [Î¼.toFiniteMeasure.normalize_eq_of_nonzero Î¼.toFiniteMeasure_nonzero s]
  simp only [ProbabilityMeasure.mass_toFiniteMeasure, inv_one, one_mul, Î¼.coeFn_toFiniteMeasure]

/-- Averaging with respect to a finite measure is the same as integrating against
`MeasureTheory.FiniteMeasure.normalize`. -/
theorem average_eq_integral_normalize {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    (nonzero : Î¼ â‰  0) (f : Î© â†’ E) :
    average (Î¼ : Measure Î©) f = âˆ« Ï‰, f Ï‰ âˆ‚(Î¼.normalize : Measure Î©) := by
  rw [Î¼.toMeasure_normalize_eq_of_nonzero nonzero, average]
  congr
  simp [ENNReal.coe_inv (Î¼.mass_nonzero_iff.mpr nonzero), ennreal_mass]

variable [TopologicalSpace Î©]

theorem testAgainstNN_eq_mass_mul (f : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.testAgainstNN f = Î¼.mass * Î¼.normalize.toFiniteMeasure.testAgainstNN f := by
  nth_rw 1 [Î¼.self_eq_mass_smul_normalize]
  rw [Î¼.normalize.toFiniteMeasure.smul_testAgainstNN_apply Î¼.mass f, smul_eq_mul]

theorem normalize_testAgainstNN (nonzero : Î¼ â‰  0) (f : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.normalize.toFiniteMeasure.testAgainstNN f = Î¼.massâ»Â¹ * Î¼.testAgainstNN f := by
  simp [Î¼.testAgainstNN_eq_mass_mul, inv_mul_cancel_leftâ‚€ <| Î¼.mass_nonzero_iff.mpr nonzero]

variable [OpensMeasurableSpace Î©]
variable {Î¼}

theorem tendsto_testAgainstNN_of_tendsto_normalize_testAgainstNN_of_tendsto_mass {Î³ : Type*}
    {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    (Î¼s_lim : Tendsto (fun i â†¦ (Î¼s i).normalize) F (ğ“ Î¼.normalize))
    (mass_lim : Tendsto (fun i â†¦ (Î¼s i).mass) F (ğ“ Î¼.mass)) (f : Î© â†’áµ‡ â„â‰¥0) :
    Tendsto (fun i â†¦ (Î¼s i).testAgainstNN f) F (ğ“ (Î¼.testAgainstNN f)) := by
  by_cases h_mass : Î¼.mass = 0
  Â· simp only [Î¼.mass_zero_iff.mp h_mass, zero_testAgainstNN_apply, zero_mass,
      eq_self_iff_true] at mass_lim âŠ¢
    exact tendsto_zero_testAgainstNN_of_tendsto_zero_mass mass_lim f
  simp_rw [fun i â†¦ (Î¼s i).testAgainstNN_eq_mass_mul f, Î¼.testAgainstNN_eq_mass_mul f]
  rw [ProbabilityMeasure.tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds] at Î¼s_lim
  rw [tendsto_iff_forall_testAgainstNN_tendsto] at Î¼s_lim
  have lim_pair :
    Tendsto (fun i â†¦ (âŸ¨(Î¼s i).mass, (Î¼s i).normalize.toFiniteMeasure.testAgainstNN fâŸ© : â„â‰¥0 Ã— â„â‰¥0))
      F (ğ“ âŸ¨Î¼.mass, Î¼.normalize.toFiniteMeasure.testAgainstNN fâŸ©) :=
    (Prod.tendsto_iff _ _).mpr âŸ¨mass_lim, Î¼s_lim fâŸ©
  exact tendsto_mul.comp lim_pair

theorem tendsto_normalize_testAgainstNN_of_tendsto {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ FiniteMeasure Î©} (Î¼s_lim : Tendsto Î¼s F (ğ“ Î¼)) (nonzero : Î¼ â‰  0) (f : Î© â†’áµ‡ â„â‰¥0) :
    Tendsto (fun i â†¦ (Î¼s i).normalize.toFiniteMeasure.testAgainstNN f) F
      (ğ“ (Î¼.normalize.toFiniteMeasure.testAgainstNN f)) := by
  have lim_mass := Î¼s_lim.mass
  have aux : {(0 : â„â‰¥0)}á¶œ âˆˆ ğ“ Î¼.mass :=
    isOpen_compl_singleton.mem_nhds (Î¼.mass_nonzero_iff.mpr nonzero)
  have eventually_nonzero : âˆ€á¶  i in F, Î¼s i â‰  0 := by
    simp_rw [â† mass_nonzero_iff]
    exact lim_mass aux
  have eve : âˆ€á¶  i in F,
      (Î¼s i).normalize.toFiniteMeasure.testAgainstNN f =
        (Î¼s i).massâ»Â¹ * (Î¼s i).testAgainstNN f := by
    filter_upwards [eventually_iff.mp eventually_nonzero]
    intro i hi
    apply normalize_testAgainstNN _ hi
  simp_rw [tendsto_congr' eve, Î¼.normalize_testAgainstNN nonzero]
  have lim_pair :
    Tendsto (fun i â†¦ (âŸ¨(Î¼s i).massâ»Â¹, (Î¼s i).testAgainstNN fâŸ© : â„â‰¥0 Ã— â„â‰¥0)) F
      (ğ“ âŸ¨Î¼.massâ»Â¹, Î¼.testAgainstNN fâŸ©) := by
    refine (Prod.tendsto_iff _ _).mpr âŸ¨?_, ?_âŸ©
    Â· exact (continuousOn_invâ‚€.continuousAt aux).tendsto.comp lim_mass
    Â· exact tendsto_iff_forall_testAgainstNN_tendsto.mp Î¼s_lim f
  exact tendsto_mul.comp lim_pair

/-- If the normalized versions of finite measures converge weakly and their total masses
also converge, then the finite measures themselves converge weakly. -/
theorem tendsto_of_tendsto_normalize_testAgainstNN_of_tendsto_mass {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ FiniteMeasure Î©} (Î¼s_lim : Tendsto (fun i â†¦ (Î¼s i).normalize) F (ğ“ Î¼.normalize))
    (mass_lim : Tendsto (fun i â†¦ (Î¼s i).mass) F (ğ“ Î¼.mass)) : Tendsto Î¼s F (ğ“ Î¼) := by
  rw [tendsto_iff_forall_testAgainstNN_tendsto]
  exact fun f â†¦
    tendsto_testAgainstNN_of_tendsto_normalize_testAgainstNN_of_tendsto_mass Î¼s_lim mass_lim f

/-- If finite measures themselves converge weakly to a nonzero limit measure, then their
normalized versions also converge weakly. -/
theorem tendsto_normalize_of_tendsto {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    (Î¼s_lim : Tendsto Î¼s F (ğ“ Î¼)) (nonzero : Î¼ â‰  0) :
    Tendsto (fun i â†¦ (Î¼s i).normalize) F (ğ“ Î¼.normalize) := by
  rw [ProbabilityMeasure.tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds,
    tendsto_iff_forall_testAgainstNN_tendsto]
  exact fun f â†¦ tendsto_normalize_testAgainstNN_of_tendsto Î¼s_lim nonzero f

/-- The weak convergence of finite measures to a nonzero limit can be characterized by the weak
convergence of both their normalized versions (probability measures) and their total masses. -/
theorem tendsto_normalize_iff_tendsto {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    (nonzero : Î¼ â‰  0) :
    Tendsto (fun i â†¦ (Î¼s i).normalize) F (ğ“ Î¼.normalize) âˆ§
        Tendsto (fun i â†¦ (Î¼s i).mass) F (ğ“ Î¼.mass) â†”
      Tendsto Î¼s F (ğ“ Î¼) := by
  constructor
  Â· rintro âŸ¨normalized_lim, mass_limâŸ©
    exact tendsto_of_tendsto_normalize_testAgainstNN_of_tendsto_mass normalized_lim mass_lim
  Â· intro Î¼s_lim
    exact âŸ¨tendsto_normalize_of_tendsto Î¼s_lim nonzero, Î¼s_lim.massâŸ©

end FiniteMeasure --namespace

end NormalizeFiniteMeasure -- section

section map

variable {Î© Î©' : Type*} [MeasurableSpace Î©] [MeasurableSpace Î©']

namespace ProbabilityMeasure

/-- The push-forward of a probability measure by a measurable function. -/
noncomputable def map (Î½ : ProbabilityMeasure Î©) {f : Î© â†’ Î©'} (f_aemble : AEMeasurable f Î½) :
    ProbabilityMeasure Î©' :=
  âŸ¨(Î½ : Measure Î©).map f,
   âŸ¨by simp only [Measure.map_apply_of_aemeasurable f_aemble MeasurableSet.univ,
                  preimage_univ, measure_univ]âŸ©âŸ©

@[simp] lemma toMeasure_map (Î½ : ProbabilityMeasure Î©) {f : Î© â†’ Î©'} (hf : AEMeasurable f Î½) :
    (Î½.map hf).toMeasure = Î½.toMeasure.map f := rfl

/-- Note that this is an equality of elements of `â„â‰¥0âˆ`. See also
`MeasureTheory.ProbabilityMeasure.map_apply` for the corresponding equality as elements of `â„â‰¥0`. -/
lemma map_apply' (Î½ : ProbabilityMeasure Î©) {f : Î© â†’ Î©'} (f_aemble : AEMeasurable f Î½)
    {A : Set Î©'} (A_mble : MeasurableSet A) :
    (Î½.map f_aemble : Measure Î©') A = (Î½ : Measure Î©) (f â»Â¹' A) :=
  Measure.map_apply_of_aemeasurable f_aemble A_mble

lemma map_apply_of_aemeasurable (Î½ : ProbabilityMeasure Î©) {f : Î© â†’ Î©'}
    (f_aemble : AEMeasurable f Î½) {A : Set Î©'} (A_mble : MeasurableSet A) :
    (Î½.map f_aemble) A = Î½ (f â»Â¹' A) := by
  exact (ENNReal.toNNReal_eq_toNNReal_iff' (measure_ne_top _ _) (measure_ne_top _ _)).mpr <|
    Î½.map_apply' f_aemble A_mble

lemma map_apply (Î½ : ProbabilityMeasure Î©) {f : Î© â†’ Î©'} (f_aemble : AEMeasurable f Î½)
    {A : Set Î©'} (A_mble : MeasurableSet A) :
    (Î½.map f_aemble) A = Î½ (f â»Â¹' A) :=
  map_apply_of_aemeasurable Î½ f_aemble A_mble

variable [TopologicalSpace Î©] [OpensMeasurableSpace Î©]
variable [TopologicalSpace Î©'] [BorelSpace Î©']

/-- If `f : X â†’ Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
convergence (in distribution) of `ProbabilityMeasure`s on `X` implies convergence (in
distribution) of the push-forwards of these measures by `f`. -/
lemma tendsto_map_of_tendsto_of_continuous {Î¹ : Type*} {L : Filter Î¹}
    (Î½s : Î¹ â†’ ProbabilityMeasure Î©) (Î½ : ProbabilityMeasure Î©) (lim : Tendsto Î½s L (ğ“ Î½))
    {f : Î© â†’ Î©'} (f_cont : Continuous f) :
    Tendsto (fun i â†¦ (Î½s i).map f_cont.measurable.aemeasurable) L
      (ğ“ (Î½.map f_cont.measurable.aemeasurable)) := by
  rw [ProbabilityMeasure.tendsto_iff_forall_lintegral_tendsto] at lim âŠ¢
  intro g
  convert lim (g.compContinuous âŸ¨f, f_contâŸ©) <;>
  Â· simp only [map, compContinuous_apply, ContinuousMap.coe_mk]
    refine lintegral_map ?_ f_cont.measurable
    exact (ENNReal.continuous_coe.comp g.continuous).measurable

/-- If `f : X â†’ Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
the push-forward of probability measures `f* : ProbabilityMeasure X â†’ ProbabilityMeasure Y`
is continuous (in the topologies of convergence in distribution). -/
lemma continuous_map {f : Î© â†’ Î©'} (f_cont : Continuous f) :
    Continuous (fun Î½ â†¦ ProbabilityMeasure.map Î½ f_cont.measurable.aemeasurable) := by
  rw [continuous_iff_continuousAt]
  exact fun _ â†¦ tendsto_map_of_tendsto_of_continuous _ _ continuous_id.continuousAt f_cont

end ProbabilityMeasure -- namespace

end map -- section

section MonoidalProduct

open Measure

/-- The monoidal product is a measurable function from the product of probability spaces over
`Î±` and `Î²` into the type of probability spaces over `Î± Ã— Î²`. Lemma 4.1 of
https://doi.org/10.1016/j.aim.2020.107239. -/
theorem ProbabilityMeasure.measurable_prod {Î± Î² : Type*} [MeasurableSpace Î±] [MeasurableSpace Î²] :
    Measurable (fun (Î¼ : ProbabilityMeasure Î± Ã— ProbabilityMeasure Î²)
      â†¦ Î¼.1.toMeasure.prod Î¼.2.toMeasure) := by
  apply Measurable.measure_of_isPiSystem_of_isProbabilityMeasure generateFrom_prod.symm
    isPiSystem_prod _
  simp only [mem_image2, mem_setOf_eq, forall_exists_index, and_imp]
  intros _ u Hu v Hv Heq
  simp_rw [â† Heq, prod_prod]
  apply Measurable.mul
  Â· exact (measurable_coe Hu).comp (measurable_subtype_coe.comp measurable_fst)
  Â· exact (measurable_coe Hv).comp (measurable_subtype_coe.comp measurable_snd)

/-- The monoidal product is a measurabule function from the product of finite measures over
`Î±` and `Î²` into the type of finite measures over `Î± Ã— Î²`. -/
theorem FiniteMeasure.measurable_prod {Î± Î² : Type*} [MeasurableSpace Î±] [MeasurableSpace Î²] :
    Measurable (fun (Î¼ : FiniteMeasure Î± Ã— FiniteMeasure Î²)
      â†¦ Î¼.1.toMeasure.prod Î¼.2.toMeasure) := by
  apply Measurable.measure_of_isPiSystem generateFrom_prod.symm isPiSystem_prod _
  Â· simp_rw [â† Set.univ_prod_univ, prod_prod]
    apply Measurable.mul
    Â· exact (measurable_coe MeasurableSet.univ).comp (measurable_subtype_coe.comp measurable_fst)
    Â· exact (measurable_coe MeasurableSet.univ).comp (measurable_subtype_coe.comp measurable_snd)
  simp only [mem_image2, mem_setOf_eq, forall_exists_index, and_imp]
  intros _ u Hu v Hv Heq
  simp_rw [â† Heq, prod_prod]
  apply Measurable.mul
  Â· exact (measurable_coe Hu).comp (measurable_subtype_coe.comp measurable_fst)
  Â· exact (measurable_coe Hv).comp (measurable_subtype_coe.comp measurable_snd)

end MonoidalProduct

end MeasureTheory -- namespace
