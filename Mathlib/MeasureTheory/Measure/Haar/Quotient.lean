/-
Copyright (c) 2022 Alex Kontorovich and Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Mathlib.MeasureTheory.Group.FundamentalDomain
import Mathlib.Algebra.Group.Opposite
import Mathlib.MeasureTheory.Constructions.Polish

#align_import measure_theory.measure.haar.quotient from "leanprover-community/mathlib"@"3b52265189f3fb43aa631edffce5d060fafaf82f"

/-!
# Haar quotient measure

In this file, we consider properties of fundamental domains and measures for the action of a
subgroup `Î“` of a topological group `G` on `G` itself. Let `Î¼` be a measure on `G â§¸ Î“`.

## Main results

* `MeasureTheory.QuotientVolumeEqVolumePreimage.smulInvariantMeasure_quotient`: If `Î¼` satisfies
  `QuotientVolumeEqVolumePreimage` relative to a both left- and right-invariant measure on `G`, then
  it is a `G` invariant measure on `G â§¸ Î“`.

The next two results assume that `Î“` is normal, and that `G` is equipped with a left- and
right-invariant measure.

* `MeasureTheory.QuotientVolumeEqVolumePreimage.MulInvariantMeasure_quotient`: If `Î¼` satisfies
  `QuotientVolumeEqVolumePreimage`, then `Î¼` is a left-invariant measure.

* `MeasureTheory.LeftInvariantIsQuotientVolumeEqVolumePreimage`: If `Î¼` is left-invariant, and the
  action of `Î“` on `G` has finite covolume, and `Î¼` satisfies the right scaling condition, then it
  satisfies `QuotientVolumeEqVolumePreimage`. This is a converse to
  `MeasureTheory.QuotientVolumeEqVolumePreimage.MulInvariantMeasure_quotient`.

The last result assumes that `G` is locally compact, that `Î“` is countable and normal, that its
action on `G` has a fundamental domain, and that `Î¼` is a finite measure. We also assume that `G` is
equipped with a sigma-finite Haar measure.

* `MeasureTheory.QuotientVolumeEqVolumePreimage.haarMeasure_quotient`: If `Î¼` satisfies
  `QuotientVolumeEqVolumePreimage`, then it is itself Haar. This is a variant of
  `MeasureTheory.QuotientVolumeEqVolumePreimage.MulInvariantMeasure_quotient`.

Note that a group `G` with Haar measure that is both left and right invariant is called
**unimodular**.
-/

open Set MeasureTheory TopologicalSpace MeasureTheory.Measure

open scoped Pointwise NNReal ENNReal

section

/-- Measurability of the action of the topological group `G` on the left-coset space `G / Î“`. -/
@[to_additive "Measurability of the action of the additive topological group `G` on the left-coset
  space `G / Î“`."]
instance QuotientGroup.measurableSMul {G : Type*} [Group G] {Î“ : Subgroup G} [MeasurableSpace G]
    [TopologicalSpace G] [TopologicalGroup G] [BorelSpace G] [BorelSpace (G â§¸ Î“)] :
    MeasurableSMul G (G â§¸ Î“) where
  measurable_const_smul g := Continuous.measurable (continuous_const_smul g)
  measurable_smul_const x := (QuotientGroup.continuous_smulâ‚ x).measurable
#align quotient_group.has_measurable_smul QuotientGroup.measurableSMul
#align quotient_add_group.has_measurable_vadd QuotientAddGroup.measurableVAdd

end

section smulInvariantMeasure

section additive

variable {G : Type*} [AddGroup G] [MeasureSpace G] [TopologicalSpace G] [TopologicalAddGroup G]
  [BorelSpace G] [PolishSpace G] {Î“ : AddSubgroup G} [Countable Î“] [T2Space (G â§¸ Î“)]
  [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}
  [AddQuotientVolumeEqVolumePreimage Î¼]

---- should not need this!!!?
variable [MeasurableVAdd G (G â§¸ Î“)]

local notation "Ï€" => @QuotientAddGroup.mk G _ Î“

/-- If `Î¼` satisfies `AddQuotientVolumeEqVolumePreimage` relative to a both left- and right-
  invariant measure on `G`, then it is a `G` invariant measure on `G â§¸ Î“`. -/
instance MeasureTheory.AddQuotientVolumeEqVolumePreimage.vaddInvariantMeasure_quotient
    [IsAddLeftInvariant (volume : Measure G)] [IsAddRightInvariant (volume : Measure G)]
    [hasFun : HasAddFundamentalDomain (AddSubgroup.opposite Î“) G] :
    VAddInvariantMeasure G (G â§¸ Î“) Î¼ where
  measure_preimage_vadd g A hA := by
    have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
    have meas_Ï€A : MeasurableSet (Ï€ â»Â¹' A) := measurableSet_preimage meas_Ï€ hA
    obtain âŸ¨ğ“•, hğ“•âŸ© := hasFun.has_add_fundamental_domain_characterization
    have hğ“•_translate_fundom : IsAddFundamentalDomain (AddSubgroup.opposite Î“) (g +áµ¥ ğ“•) volume :=
      hğ“•.vadd_of_comm g
    rw [add_projection_respects_measure hğ“•
      (meas_Ï€ (measurableSet_preimage (measurable_const_vadd g) hA)),
      add_projection_respects_measure hğ“•_translate_fundom hA]
    change volume ((Ï€ â»Â¹' _) âˆ© _) = _
    set Ï€_preA := Ï€ â»Â¹' A
    have : Ï€ â»Â¹' ((fun x : G â§¸ Î“ => g +áµ¥ x) â»Â¹' A) = (g + Â·) â»Â¹' Ï€_preA := by ext1; simp
    rw [this]
    have : volume ((g + Â·) â»Â¹' Ï€_preA âˆ© ğ“•) = volume (Ï€_preA âˆ© ((-g) + Â·) â»Â¹' ğ“•)
    Â· trans volume ((g + Â·) â»Â¹' (Ï€_preA âˆ© ((-g) + Â·) â»Â¹' ğ“•))
      Â· rw [preimage_inter]
        congr 2
        simp [Set.preimage]
      rw [measure_preimage_add]
    rw [this, â† preimage_vadd_neg]; rfl

end additive

variable {G : Type*} [Group G] [MeasureSpace G] [TopologicalSpace G] [TopologicalGroup G]
  [BorelSpace G] [PolishSpace G] {Î“ : Subgroup G} [Countable Î“] [T2Space (G â§¸ Î“)]
  [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}
  [QuotientVolumeEqVolumePreimage Î¼]

local notation "Ï€" => @QuotientGroup.mk G _ Î“

/-- If `Î¼` satisfies `QuotientVolumeEqVolumePreimage` relative to a both left- and right-
  invariant measure on `G`, then it is a `G` invariant measure on `G â§¸ Î“`. -/
instance MeasureTheory.QuotientVolumeEqVolumePreimage.smulInvariantMeasure_quotient
    [IsMulLeftInvariant (volume : Measure G)] [IsMulRightInvariant (volume : Measure G)]
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G] :
    SMulInvariantMeasure G (G â§¸ Î“) Î¼ where
  measure_preimage_smul g A hA := by
    have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
    have meas_Ï€A : MeasurableSet (Ï€ â»Â¹' A) := measurableSet_preimage meas_Ï€ hA
    obtain âŸ¨ğ“•, hğ“•âŸ© := hasFun.has_fundamental_domain_characterization
    have hğ“•_translate_fundom : IsFundamentalDomain (Subgroup.opposite Î“) (g â€¢ ğ“•) volume :=
      hğ“•.smul_of_comm g
    rw [projection_respects_measure hğ“•
      (meas_Ï€ (measurableSet_preimage (measurable_const_smul g) hA)),
      projection_respects_measure hğ“•_translate_fundom hA]
    change volume ((Ï€ â»Â¹' _) âˆ© _) = _
    set Ï€_preA := Ï€ â»Â¹' A
    have : Ï€ â»Â¹' ((fun x : G â§¸ Î“ => g â€¢ x) â»Â¹' A) = (g * Â·) â»Â¹' Ï€_preA := by ext1; simp
    rw [this]
    have : volume ((g * Â·) â»Â¹' Ï€_preA âˆ© ğ“•) = volume (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•)
    Â· trans volume ((g * Â·) â»Â¹' (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•))
      Â· rw [preimage_inter]
        congr 2
        simp [Set.preimage]
      rw [measure_preimage_mul]
    rw [this, â† preimage_smul_inv]; rfl

attribute [to_additive existing]
  MeasureTheory.QuotientVolumeEqVolumePreimage.smulInvariantMeasure_quotient

-- We restate the `SigmaFinite` instance. For some reason, this is needed for typeclass inference
@[to_additive] instance [SigmaFinite (volume : Measure G)]
    [IsMulRightInvariant (volume : Measure G)] [HasFundamentalDomain (Subgroup.opposite Î“) G]
    (Î¼ : Measure (G â§¸ Î“)) [QuotientVolumeEqVolumePreimage Î¼] : SigmaFinite Î¼ :=
  instSigmaFiniteQuotientOrbitRelInstMeasurableSpaceToMeasurableSpace Î¼

/-- Given a subgroup `Î“` of a topological group `G` with right-invariant measure `volume`, with a
  measure 'Î¼' on the quotient `G â§¸ Î“` satisfying `QuotientVolumeEqVolumePreimage`, the restriction
  of `volume` to a fundamental domain is measure-preserving with respect to `Î¼`. -/
@[to_additive measurePreserving_addQuotientGroup_mk_of_addQuotientVolumeEqVolumePreimage]
theorem measurePreserving_quotientGroup_mk_of_quotientVolumeEqVolumePreimage
    [IsMulRightInvariant (volume : Measure G)] {ğ“• : Set G}
    (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•) (Î¼ : Measure (G â§¸ Î“))
    [QuotientVolumeEqVolumePreimage Î¼] :
    MeasurePreserving (@QuotientGroup.mk G _ Î“) (volume.restrict ğ“•) Î¼ :=
  hğ“•.measurePreserving_quotient_mk Î¼

end smulInvariantMeasure

section normal

section additive
variable {G : Type*} [AddGroup G] [MeasureSpace G] [TopologicalSpace G] [TopologicalAddGroup G]
  [BorelSpace G] [PolishSpace G] {Î“ : AddSubgroup G} [Countable Î“] [AddSubgroup.Normal Î“]
  [T2Space (G â§¸ Î“)] [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}
  [IsAddLeftInvariant (volume : Measure G)] [IsAddRightInvariant (volume : Measure G)]
  [SigmaFinite (volume : Measure G)]

section addInvariantMeasure

-- should not be needed??? Why can't it figure this out?
variable [MeasurableAdd (G â§¸ Î“)] [MeasurableVAdd G (G â§¸ Î“)]

/-- If `Î¼` on `G â§¸ Î“` satisfies `AddQuotientVolumeEqVolumePreimage` relative to a both left- and
right-invariant measure on `G` and `Î“` is a normal subgroup, then `Î¼` is a left-invariant measure.-/
instance MeasureTheory.AddQuotientVolumeEqVolumePreimage.addInvariantMeasure_quotient
    [hasFun : HasAddFundamentalDomain (AddSubgroup.opposite Î“) G]
    [AddQuotientVolumeEqVolumePreimage Î¼] : Î¼.IsAddLeftInvariant where
  map_add_left_eq_self x := by
    apply Measure.ext
    intro A hA
    obtain âŸ¨xâ‚, hâŸ© := @Quotient.exists_rep _ (QuotientAddGroup.leftRel Î“) x
    convert measure_preimage_vadd xâ‚ Î¼ A using 1
    rw [â† h, Measure.map_apply (measurable_const_add _) hA]
    rfl

variable [IsAddLeftInvariant Î¼] [SigmaFinite Î¼]

--- should not need this!!!
variable [MeasurableAddâ‚‚ (G â§¸ Î“)] [MeasurableNeg (G â§¸ Î“)]

local notation "Ï€" => @QuotientAddGroup.mk G _ Î“

/-- Assume that a measure `Î¼` is `IsAddLeftInvariant`, that the action of `Î“` on `G` has a
measurable fundamental domain `s` with positive finite volume, and that there is a single measurable
set `V âŠ† G â§¸ Î“` along which the pullback of `Î¼` and `volume` agree (so the scaling is right). Then
`Î¼` satisfies `AddQuotientVolumeEqVolumePreimage`. The main tool of the proof is the uniqueness of
left invariant measures, if normalized by a single positive finite-measured set. -/
theorem MeasureTheory.Measure.IsAddLeftInvariant.addQuotientVolumeEqVolumePreimage_of_set
    {s : Set G} (fund_dom_s : IsAddFundamentalDomain (AddSubgroup.opposite Î“) s) {V : Set (G â§¸ Î“)}
    (meas_V : MeasurableSet V) (neZeroV : Î¼ V â‰  0) (hV : Î¼ V = volume (Ï€ â»Â¹' V âˆ© s))
    (neTopV : Î¼ V â‰  âŠ¤) : AddQuotientVolumeEqVolumePreimage Î¼ := by
  apply fund_dom_s.addQuotientVolumeEqVolumePreimage
  intro U meas_U
  let Î¼' : Measure (G â§¸ Î“) := fund_dom_s.nullMeasurableSet.addQuotientMeasure
    (AddSubgroup.opposite Î“) volume
  haveI has_fund : HasAddFundamentalDomain (AddSubgroup.opposite Î“) G := âŸ¨âŸ¨s, fund_dom_sâŸ©âŸ©
  have : AddQuotientVolumeEqVolumePreimage Î¼' :=
    fund_dom_s.addQuotientVolumeEqVolumePreimage_addQuotientMeasure
  have : Î¼'.IsAddLeftInvariant :=
    MeasureTheory.AddQuotientVolumeEqVolumePreimage.addInvariantMeasure_quotient
  suffices : Î¼ = Î¼'
  Â· rw [this, NullMeasurableSet.addQuotientMeasure_apply]
    exact meas_U
  Â· rw [measure_eq_sub_vadd Î¼' Î¼ meas_V neZeroV neTopV, hV]
    symm
    convert one_smul ENNReal Î¼
    rw [fund_dom_s.nullMeasurableSet.addQuotientMeasure_apply _ meas_V]
    convert ENNReal.div_self ..
    Â· exact trans hV.symm neZeroV
    Â· exact trans hV.symm neTopV

/-- If a measure `Î¼` is left-invariant and satisfies the right scaling condition, then it
  satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem MeasureTheory.LeftInvariantIsAddQuotientVolumeEqVolumePreimage
    [IsFiniteMeasure Î¼] [hasFun : HasAddFundamentalDomain (AddSubgroup.opposite Î“) G]
    (h : addCovolume (AddSubgroup.opposite Î“) G = Î¼ univ) :
    AddQuotientVolumeEqVolumePreimage Î¼ := by
  obtain âŸ¨s, fund_dom_sâŸ© := hasFun.has_add_fundamental_domain_characterization
  have finiteCovol : Î¼ univ < âŠ¤ := measure_lt_top Î¼ univ
  rw [fund_dom_s.covolume_eq_volume] at h
  by_cases meas_s_ne_zero : volume s = 0
  Â· convert fund_dom_s.AddQuotientVolumeEqVolumePreimage_of_volume_zero meas_s_ne_zero
    rw [â† @measure_univ_eq_zero, â†h, meas_s_ne_zero]
  apply IsAddLeftInvariant.addQuotientVolumeEqVolumePreimage_of_set (fund_dom_s := fund_dom_s)
    (meas_V := MeasurableSet.univ)
  Â· rw [â† h]
    exact meas_s_ne_zero
  Â· rw [â† h]
    simp
  Â· rw [â† h]
    convert finiteCovol.ne

end addInvariantMeasure

section addHaarMeasure

variable [SigmaFinite (volume : Measure G)] [IsAddHaarMeasure (volume : Measure G)]
  [IsAddRightInvariant (volume : Measure G)]

---should not be needed???
variable [BorelSpace (G â§¸ Î“)]

local notation "Ï€" => @QuotientAddGroup.mk G _ Î“

/-- If a measure `Î¼` on the quotient `G â§¸ Î“` of an additive group `G` by a discrete normal subgroup
`Î“` having fundamental domain, satisfies `AddQuotientVolumeEqVolumePreimage` relative to a
standardized choice of Haar measure on `G`, and assuming `Î¼` is finite, then `Î¼` is itself Haar.
TODO: Is it possible to drop the assumption that `Î¼` is finite? -/
instance MeasureTheory.AddQuotientVolumeEqVolumePreimage.addHaarMeasure_quotient
    [LocallyCompactSpace G] [AddQuotientVolumeEqVolumePreimage Î¼]
    [i : HasAddFundamentalDomain (AddSubgroup.opposite Î“) G] [IsFiniteMeasure Î¼] :
    IsAddHaarMeasure Î¼ := by
  obtain âŸ¨KâŸ© := PositiveCompacts.nonempty' (Î± := G)
  let K' : PositiveCompacts (G â§¸ Î“) :=
    K.map Ï€ continuous_coinduced_rng (QuotientAddGroup.isOpenMap_coe Î“)
  rw [addHaarMeasure_unique Î¼ K']
  have finiteCovol : addCovolume (AddSubgroup.opposite Î“) G â‰  âŠ¤ :=
    AddQuotientVolumeEqVolumePreimage.covolume_ne_top (Î¼ := Î¼)
  obtain âŸ¨s, fund_dom_sâŸ© := i
  rw [fund_dom_s.covolume_eq_volume] at finiteCovol
  rw [add_projection_respects_measure fund_dom_s K'.isCompact.measurableSet]
  apply IsAddHaarMeasure.smul
  Â· intro h
    haveI i' : IsOpenPosMeasure (volume : Measure G) := inferInstance
    apply IsOpenPosMeasure.open_pos (interior K) (Î¼ := volume) (self := i')
    Â· exact isOpen_interior
    Â· exact K.interior_nonempty
    rw [â†le_zero_iff, â†fund_dom_s.measure_zero_of_invariant _
      (fun g â†¦ QuotientAddGroup.sound _ _ g) h]
    apply measure_mono
    refine interior_subset.trans ?_
    show (K : Set G) âŠ† Ï€ â»Â¹' (Ï€ '' K)
    exact subset_preimage_image Ï€ K
  Â· show volume (Ï€ â»Â¹' (Ï€ '' K) âˆ© s) â‰  âŠ¤
    apply ne_of_lt
    refine lt_of_le_of_lt ?_ finiteCovol.lt_top
    apply measure_mono
    exact inter_subset_right _ s

/- Given a normal subgroup `Î“` of a topological additive group `G` with Haar measure `Î¼`, which is
  also right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `AddQuotientVolumeEqVolumePreimage`. -/
theorem IsAddFundamentalDomain.AddQuotientVolumeEqVolumePreimage_HaarMeasure {ğ“• : Set G}
    (hğ“• : IsAddFundamentalDomain (AddSubgroup.opposite Î“) ğ“•) [IsAddLeftInvariant Î¼] [SigmaFinite Î¼]
    {V : Set (G â§¸ Î“)} (hV : (interior V).Nonempty) (meas_V : MeasurableSet V)
    (hÎ¼K : Î¼ V = volume ((Ï€ â»Â¹' V) âˆ© ğ“•)) (neTopV : Î¼ V â‰  âŠ¤) :
    AddQuotientVolumeEqVolumePreimage Î¼ := by
  apply IsAddLeftInvariant.addQuotientVolumeEqVolumePreimage_of_set (fund_dom_s := hğ“•)
    (meas_V := meas_V)
  Â· rw [hÎ¼K]
    intro c_eq_zero
    apply IsOpenPosMeasure.open_pos (interior (Ï€ â»Â¹' V)) (Î¼ := volume)
    Â· simp
    Â· apply Set.Nonempty.mono (preimage_interior_subset_interior_preimage continuous_coinduced_rng)
      apply hV.preimage'
      simp
    Â· apply measure_mono_null (h := interior_subset)
      apply hğ“•.measure_zero_of_invariant (ht := fun g â†¦ QuotientGroup.sound _ _ g)
      exact c_eq_zero
  Â· exact hÎ¼K
  Â· exact neTopV

variable (K : PositiveCompacts (G â§¸ Î“))

/- Given a normal subgroup `Î“` of a topological additive group `G` with Haar measure `Î¼`, which is
  also right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `AddQuotientVolumeEqVolumePreimage`. -/
theorem IsAddFundamentalDomain.AddQuotientVolumeEqVolumePreimage_bubHaarMeasure {ğ“• : Set G}
    (hğ“• : IsAddFundamentalDomain (AddSubgroup.opposite Î“) ğ“•) (hğ“•_finite : volume ğ“• â‰  âŠ¤) :
    AddQuotientVolumeEqVolumePreimage
      ((volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)) â€¢ addHaarMeasure K) := by
  set c := volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)
  have c_ne_top : c â‰  âŠ¤
  Â· contrapose! hğ“•_finite
    have : volume (Ï€ â»Â¹' â†‘K âˆ© ğ“•) â‰¤ volume ğ“• := measure_mono (Set.inter_subset_right _ _)
    rw [hğ“•_finite] at this
    exact top_unique this
  set Î¼ := c â€¢ addHaarMeasure K
  have hÎ¼K : Î¼ K = c := by simp [addHaarMeasure_self]
  haveI : SigmaFinite Î¼ := by
    clear_value c
    lift c to NNReal using c_ne_top
    exact SMul.sigmaFinite c
  apply IsAddFundamentalDomain.AddQuotientVolumeEqVolumePreimage_HaarMeasure (hğ“• := hğ“•)
    (meas_V := K.isCompact.measurableSet) (Î¼ := Î¼)
  Â· exact K.interior_nonempty
  Â· exact hÎ¼K
  Â· rw [hÎ¼K]
    exact c_ne_top

end addHaarMeasure

end additive

variable {G : Type*} [Group G] [MeasureSpace G] [TopologicalSpace G] [TopologicalGroup G]
  [BorelSpace G] [PolishSpace G]
  {Î“ : Subgroup G} [Countable Î“] [Subgroup.Normal Î“]
  [T2Space (G â§¸ Î“)]
  [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}

section mulInvariantMeasure

variable
  [IsMulLeftInvariant (volume : Measure G)] [IsMulRightInvariant (volume : Measure G)]
  [SigmaFinite (volume : Measure G)]

/-- If `Î¼` on `G â§¸ Î“` satisfies `QuotientVolumeEqVolumePreimage` relative to a both left- and right-
  invariant measure on `G` and `Î“` is a normal subgroup, then `Î¼` is a left-invariant measure.-/
instance MeasureTheory.QuotientVolumeEqVolumePreimage.mulInvariantMeasure_quotient
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G] [QuotientVolumeEqVolumePreimage Î¼] :
    Î¼.IsMulLeftInvariant where
  map_mul_left_eq_self x := by
    apply Measure.ext
    intro A hA
    obtain âŸ¨xâ‚, hâŸ© := @Quotient.exists_rep _ (QuotientGroup.leftRel Î“) x
    convert measure_preimage_smul xâ‚ Î¼ A using 1
    rw [â† h, Measure.map_apply (measurable_const_mul _) hA]
    rfl

attribute [to_additive existing]
  MeasureTheory.QuotientVolumeEqVolumePreimage.mulInvariantMeasure_quotient

variable [IsMulLeftInvariant Î¼] [SigmaFinite Î¼]

local notation "Ï€" => @QuotientGroup.mk G _ Î“

/-- Assume that a measure `Î¼` is `IsMulLeftInvariant`, that the action of `Î“` on `G` has a
measurable fundamental domain `s` with positive finite volume, and that there is a single measurable
set `V âŠ† G â§¸ Î“` along which the pullback of `Î¼` and `volume` agree (so the scaling is right). Then
`Î¼` satisfies `QuotientVolumeEqVolumePreimage`. The main tool of the proof is the uniqueness of left
invariant measures, if normalized by a single positive finite-measured set. -/
theorem MeasureTheory.Measure.IsMulLeftInvariant.quotientVolumeEqVolumePreimage_of_set {s : Set G}
    (fund_dom_s : IsFundamentalDomain (Subgroup.opposite Î“) s) {V : Set (G â§¸ Î“)}
    (meas_V : MeasurableSet V) (neZeroV : Î¼ V â‰  0) (hV : Î¼ V = volume (Ï€ â»Â¹' V âˆ© s))
    (neTopV : Î¼ V â‰  âŠ¤) : QuotientVolumeEqVolumePreimage Î¼ := by
  apply fund_dom_s.quotientVolumeEqVolumePreimage
  intro U meas_U
  let Î¼' : Measure (G â§¸ Î“) := fund_dom_s.nullMeasurableSet.quotientMeasure
    (Subgroup.opposite Î“) volume
  haveI has_fund : HasFundamentalDomain (Subgroup.opposite Î“) G := âŸ¨âŸ¨s, fund_dom_sâŸ©âŸ©
  have : QuotientVolumeEqVolumePreimage Î¼' :=
    fund_dom_s.quotientVolumeEqVolumePreimage_quotientMeasure
  have : Î¼'.IsMulLeftInvariant :=
    MeasureTheory.QuotientVolumeEqVolumePreimage.mulInvariantMeasure_quotient
  suffices : Î¼ = Î¼'
  Â· rw [this, NullMeasurableSet.quotientMeasure_apply]
    exact meas_U
  Â· rw [measure_eq_div_smul Î¼' Î¼ meas_V neZeroV neTopV, hV]
    symm
    convert one_smul ENNReal Î¼
    rw [fund_dom_s.nullMeasurableSet.quotientMeasure_apply _ meas_V]
    convert ENNReal.div_self ..
    Â· exact trans hV.symm neZeroV
    Â· exact trans hV.symm neTopV

attribute [to_additive existing
  MeasureTheory.Measure.IsAddLeftInvariant.addQuotientVolumeEqVolumePreimage_of_set]
  MeasureTheory.Measure.IsMulLeftInvariant.quotientVolumeEqVolumePreimage_of_set

/-- If a measure `Î¼` is left-invariant and satisfies the right scaling condition, then it
  satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem MeasureTheory.LeftInvariantIsQuotientVolumeEqVolumePreimage [IsFiniteMeasure Î¼]
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G]
    (h : covolume (Subgroup.opposite Î“) G = Î¼ univ) : QuotientVolumeEqVolumePreimage Î¼ := by
  obtain âŸ¨s, fund_dom_sâŸ© := hasFun.has_fundamental_domain_characterization
  have finiteCovol : Î¼ univ < âŠ¤ := measure_lt_top Î¼ univ
  rw [fund_dom_s.covolume_eq_volume] at h
  by_cases meas_s_ne_zero : volume s = 0
  Â· convert fund_dom_s.QuotientVolumeEqVolumePreimage_of_volume_zero meas_s_ne_zero
    rw [â† @measure_univ_eq_zero, â†h, meas_s_ne_zero]
  apply IsMulLeftInvariant.quotientVolumeEqVolumePreimage_of_set (fund_dom_s := fund_dom_s)
    (meas_V := MeasurableSet.univ)
  Â· rw [â† h]
    exact meas_s_ne_zero
  Â· rw [â† h]
    simp
  Â· rw [â† h]
    convert finiteCovol.ne

attribute [to_additive existing MeasureTheory.LeftInvariantIsAddQuotientVolumeEqVolumePreimage]
  MeasureTheory.LeftInvariantIsQuotientVolumeEqVolumePreimage

end mulInvariantMeasure

section haarMeasure

variable [SigmaFinite (volume : Measure G)] [IsHaarMeasure (volume : Measure G)]
  [IsMulRightInvariant (volume : Measure G)]

local notation "Ï€" => @QuotientGroup.mk G _ Î“

/-- If a measure `Î¼` on the quotient `G â§¸ Î“` of a group `G` by a discrete normal subgroup `Î“` having
fundamental domain, satisfies `QuotientVolumeEqVolumePreimage` relative to a standardized choice of
Haar measure on `G`, and assuming `Î¼` is finite, then `Î¼` is itself Haar.
TODO: Is it possible to drop the assumption that `Î¼` is finite? -/
instance MeasureTheory.QuotientVolumeEqVolumePreimage.haarMeasure_quotient [LocallyCompactSpace G]
    [QuotientVolumeEqVolumePreimage Î¼] [i : HasFundamentalDomain (Subgroup.opposite Î“) G]
    [IsFiniteMeasure Î¼] : IsHaarMeasure Î¼ := by
  obtain âŸ¨KâŸ© := PositiveCompacts.nonempty' (Î± := G)
  let K' : PositiveCompacts (G â§¸ Î“) :=
    K.map Ï€ continuous_coinduced_rng (QuotientGroup.isOpenMap_coe Î“)
  rw [haarMeasure_unique Î¼ K']
  have finiteCovol : covolume (Subgroup.opposite Î“) G â‰  âŠ¤ :=
    QuotientVolumeEqVolumePreimage.covolume_ne_top (Î¼ := Î¼)
  obtain âŸ¨s, fund_dom_sâŸ© := i
  rw [fund_dom_s.covolume_eq_volume] at finiteCovol
  rw [projection_respects_measure fund_dom_s K'.isCompact.measurableSet]
  apply IsHaarMeasure.smul
  Â· intro h
    haveI i' : IsOpenPosMeasure (volume : Measure G) := inferInstance
    apply IsOpenPosMeasure.open_pos (interior K) (Î¼ := volume) (self := i')
    Â· exact isOpen_interior
    Â· exact K.interior_nonempty
    rw [â†le_zero_iff, â†fund_dom_s.measure_zero_of_invariant _ (fun g â†¦ QuotientGroup.sound _ _ g) h]
    apply measure_mono
    refine interior_subset.trans ?_
    show (K : Set G) âŠ† Ï€ â»Â¹' (Ï€ '' K)
    exact subset_preimage_image Ï€ K
  Â· show volume (Ï€ â»Â¹' (Ï€ '' K) âˆ© s) â‰  âŠ¤
    apply ne_of_lt
    refine lt_of_le_of_lt ?_ finiteCovol.lt_top
    apply measure_mono
    exact inter_subset_right _ s

attribute [to_additive existing]
  MeasureTheory.QuotientVolumeEqVolumePreimage.haarMeasure_quotient

/- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem IsFundamentalDomain.QuotientVolumeEqVolumePreimage_HaarMeasure {ğ“• : Set G}
    (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•) [IsMulLeftInvariant Î¼] [SigmaFinite Î¼]
    {V : Set (G â§¸ Î“)} (hV : (interior V).Nonempty) (meas_V : MeasurableSet V)
    (hÎ¼K : Î¼ V = volume ((Ï€ â»Â¹' V) âˆ© ğ“•)) (neTopV : Î¼ V â‰  âŠ¤) :
    QuotientVolumeEqVolumePreimage Î¼ := by
  apply IsMulLeftInvariant.quotientVolumeEqVolumePreimage_of_set (fund_dom_s := hğ“•)
    (meas_V := meas_V)
  Â· rw [hÎ¼K]
    intro c_eq_zero
    apply IsOpenPosMeasure.open_pos (interior (Ï€ â»Â¹' V)) (Î¼ := volume)
    Â· simp
    Â· apply Set.Nonempty.mono (preimage_interior_subset_interior_preimage continuous_coinduced_rng)
      apply hV.preimage'
      simp
    Â· apply measure_mono_null (h := interior_subset)
      apply hğ“•.measure_zero_of_invariant (ht := fun g â†¦ QuotientGroup.sound _ _ g)
      exact c_eq_zero
  Â· exact hÎ¼K
  Â· exact neTopV

attribute [to_additive existing
  IsAddFundamentalDomain.AddQuotientVolumeEqVolumePreimage_HaarMeasure]
  IsFundamentalDomain.QuotientVolumeEqVolumePreimage_HaarMeasure

variable (K : PositiveCompacts (G â§¸ Î“))

/- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem IsFundamentalDomain.QuotientVolumeEqVolumePreimage_bubHaarMeasure {ğ“• : Set G}
    (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•) (hğ“•_finite : volume ğ“• â‰  âŠ¤) :
    QuotientVolumeEqVolumePreimage
      ((volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)) â€¢ haarMeasure K) := by
  set c := volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)
  have c_ne_top : c â‰  âŠ¤
  Â· contrapose! hğ“•_finite
    have : volume (Ï€ â»Â¹' â†‘K âˆ© ğ“•) â‰¤ volume ğ“• := measure_mono (Set.inter_subset_right _ _)
    rw [hğ“•_finite] at this
    exact top_unique this
  set Î¼ := c â€¢ haarMeasure K
  have hÎ¼K : Î¼ K = c := by simp [haarMeasure_self]
  haveI : SigmaFinite Î¼ := by
    clear_value c
    lift c to NNReal using c_ne_top
    exact SMul.sigmaFinite c
  apply IsFundamentalDomain.QuotientVolumeEqVolumePreimage_HaarMeasure (hğ“• := hğ“•)
    (meas_V := K.isCompact.measurableSet) (Î¼ := Î¼)
  Â· exact K.interior_nonempty
  Â· exact hÎ¼K
  Â· rw [hÎ¼K]
    exact c_ne_top

attribute [to_additive existing
  IsAddFundamentalDomain.AddQuotientVolumeEqVolumePreimage_bubHaarMeasure]
  IsFundamentalDomain.QuotientVolumeEqVolumePreimage_bubHaarMeasure

end haarMeasure

end normal
