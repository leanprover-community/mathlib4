/-
Copyright (c) 2022 Alex Kontorovich and Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Mathlib.MeasureTheory.Group.FundamentalDomain
import Mathlib.Algebra.Group.Opposite

#align_import measure_theory.measure.haar.quotient from "leanprover-community/mathlib"@"3b52265189f3fb43aa631edffce5d060fafaf82f"

/-!
# Haar quotient measure

In this file, we consider properties of fundamental domains and measures for the action of a
subgroup of a group `G` on `G` itself.

## Main results

* `MeasureTheory.IsFundamentalDomain.smulInvariantMeasure_map`: given a subgroup `Î“` of a
  topological group `G`, the pushforward to the coset space `G â§¸ Î“` of the restriction of a both
  left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure
  on `G â§¸ Î“`.

* `MeasureTheory.IsFundamentalDomain.isMulLeftInvariant_map`: given a normal subgroup `Î“` of
  a topological group `G`, the pushforward to the quotient group `G â§¸ Î“` of the restriction of
  a both left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a left-invariant
  measure on `G â§¸ Î“`.

Note that a group `G` with Haar measure that is both left and right invariant is called
**unimodular**.
-/


open Set MeasureTheory TopologicalSpace MeasureTheory.Measure

open scoped Pointwise NNReal ENNReal

variable {G : Type*} [Group G] [MeasurableSpace G] [TopologicalSpace G] [TopologicalGroup G]
  [BorelSpace G] {Î¼ : Measure G} {Î“ : Subgroup G}

/-- Measurability of the action of the topological group `G` on the left-coset space `G/Î“`. -/
@[to_additive "Measurability of the action of the additive topological group `G` on the left-coset
  space `G/Î“`."]
instance QuotientGroup.measurableSMul [MeasurableSpace (G â§¸ Î“)] [BorelSpace (G â§¸ Î“)] :
    MeasurableSMul G (G â§¸ Î“) where
  measurable_const_smul g := (continuous_const_smul g).measurable
  measurable_smul_const x := (QuotientGroup.continuous_smulâ‚ x).measurable
#align quotient_group.has_measurable_smul QuotientGroup.measurableSMul
#align quotient_add_group.has_measurable_vadd QuotientAddGroup.measurableVAdd

variable {ğ“• : Set G} (hğ“• : IsFundamentalDomain Î“.op ğ“• Î¼)

variable [Countable Î“] [MeasurableSpace (G â§¸ Î“)] [BorelSpace (G â§¸ Î“)]

/-- The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and right-
  invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure on `G â§¸ Î“`. -/
@[to_additive "The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and
  right-invariant measure on an additive topological group `G` to a fundamental domain `ğ“•` is a
  `G`-invariant measure on `G â§¸ Î“`."]
theorem MeasureTheory.IsFundamentalDomain.smulInvariantMeasure_map [Î¼.IsMulLeftInvariant]
    [Î¼.IsMulRightInvariant] :
    SMulInvariantMeasure G (G â§¸ Î“) (Measure.map QuotientGroup.mk (Î¼.restrict ğ“•)) where
  measure_preimage_smul g A hA := by
    let Ï€ : G â†’ G â§¸ Î“ := QuotientGroup.mk
    have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
    have ğ“•meas : NullMeasurableSet ğ“• Î¼ := hğ“•.nullMeasurableSet
    have meas_Ï€A : MeasurableSet (Ï€ â»Â¹' A) := measurableSet_preimage meas_Ï€ hA
    rw [Measure.map_apply meas_Ï€ hA,
      Measure.map_apply meas_Ï€ (measurableSet_preimage (measurable_const_smul g) hA),
      Measure.restrict_applyâ‚€' ğ“•meas, Measure.restrict_applyâ‚€' ğ“•meas]
    set Ï€_preA := Ï€ â»Â¹' A
    have : Ï€ â»Â¹' ((fun x : G â§¸ Î“ => g â€¢ x) â»Â¹' A) = (g * Â·) â»Â¹' Ï€_preA := by
      ext1; simp
    rw [this]
    have : Î¼ ((g * Â·) â»Â¹' Ï€_preA âˆ© ğ“•) = Î¼ (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•) := by
      trans Î¼ ((g * Â·) â»Â¹' (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•))
      Â· rw [preimage_inter]
        congr 2
        simp [Set.preimage]
      rw [measure_preimage_mul]
    rw [this]
    have hğ“•_translate_fundom : IsFundamentalDomain Î“.op (g â€¢ ğ“•) Î¼ :=
      hğ“•.smul_of_comm g
    rw [hğ“•.measure_set_eq hğ“•_translate_fundom meas_Ï€A, â† preimage_smul_inv]; rfl
    rintro âŸ¨Î³, Î³_in_Î“âŸ©
    ext x
    have : Ï€ (x * MulOpposite.unop Î³) = Ï€ x := by simpa [QuotientGroup.eq'] using Î³_in_Î“
    simp only [(Â· â€¢ Â·), â† this, mem_preimage]
    rfl
#align measure_theory.is_fundamental_domain.smul_invariant_measure_map MeasureTheory.IsFundamentalDomain.smulInvariantMeasure_map
#align measure_theory.is_add_fundamental_domain.vadd_invariant_measure_map MeasureTheory.IsAddFundamentalDomain.vaddInvariantMeasure_map

/-- Assuming `Î“` is a normal subgroup of a topological group `G`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of a both left- and right-invariant measure on `G` to a
  fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`. -/
@[to_additive "Assuming `Î“` is a normal subgroup of an additive topological group `G`, the
  pushforward to the quotient group `G â§¸ Î“` of the restriction of a both left- and right-invariant
  measure on `G` to a fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`."]
theorem MeasureTheory.IsFundamentalDomain.isMulLeftInvariant_map [Subgroup.Normal Î“]
    [Î¼.IsMulLeftInvariant] [Î¼.IsMulRightInvariant] :
    (Measure.map (QuotientGroup.mk' Î“) (Î¼.restrict ğ“•)).IsMulLeftInvariant where
  map_mul_left_eq_self x := by
    apply Measure.ext
    intro A hA
    obtain âŸ¨xâ‚, hâŸ© := @Quotient.exists_rep _ (QuotientGroup.leftRel Î“) x
    haveI := hğ“•.smulInvariantMeasure_map
    convert measure_preimage_smul xâ‚ ((Measure.map QuotientGroup.mk) (Î¼.restrict ğ“•)) A using 1
    rw [â† h, Measure.map_apply]
    Â· rfl
    Â· exact measurable_const_mul _
    Â· exact hA
#align measure_theory.is_fundamental_domain.is_mul_left_invariant_map MeasureTheory.IsFundamentalDomain.isMulLeftInvariant_map
#align measure_theory.is_add_fundamental_domain.is_add_left_invariant_map MeasureTheory.IsAddFundamentalDomain.isAddLeftInvariant_map

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of `Î¼` to `ğ“•` is a multiple of Haar measure on `G â§¸ Î“`. -/
@[to_additive "Given a normal subgroup `Î“` of an additive topological group `G` with Haar measure
  `Î¼`, which is also right-invariant, and a finite volume fundamental domain `ğ“•`, the pushforward
  to the quotient group `G â§¸ Î“` of the restriction of `Î¼` to `ğ“•` is a multiple of Haar measure on
  `G â§¸ Î“`."]
theorem MeasureTheory.IsFundamentalDomain.map_restrict_quotient  [T2Space (G â§¸ Î“)]
    [SecondCountableTopology (G â§¸ Î“)] (K : PositiveCompacts (G â§¸ Î“)) [Subgroup.Normal Î“]
    [MeasureTheory.Measure.IsHaarMeasure Î¼] [Î¼.IsMulRightInvariant] (hğ“•_finite : Î¼ ğ“• < âŠ¤) :
    Measure.map (QuotientGroup.mk' Î“) (Î¼.restrict ğ“•) =
      Î¼ (ğ“• âˆ© QuotientGroup.mk' Î“ â»Â¹' K) â€¢ MeasureTheory.Measure.haarMeasure K := by
  let Ï€ : G â†’* G â§¸ Î“ := QuotientGroup.mk' Î“
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  have ğ“•meas : NullMeasurableSet ğ“• Î¼ := hğ“•.nullMeasurableSet
  haveI := Fact.mk hğ“•_finite
  -- the measure is left-invariant, so by the uniqueness of Haar measure it's enough to show that
  -- it has the stated size on the reference compact set `K`.
  haveI : (Measure.map (QuotientGroup.mk' Î“) (Î¼.restrict ğ“•)).IsMulLeftInvariant :=
    hğ“•.isMulLeftInvariant_map
  rw [Measure.haarMeasure_unique (Measure.map (QuotientGroup.mk' Î“) (Î¼.restrict ğ“•)) K,
    Measure.map_apply meas_Ï€, Measure.restrict_applyâ‚€' ğ“•meas, inter_comm]
  exact K.isCompact.measurableSet
#align measure_theory.is_fundamental_domain.map_restrict_quotient MeasureTheory.IsFundamentalDomain.map_restrict_quotient
#align measure_theory.is_add_fundamental_domain.map_restrict_quotient MeasureTheory.IsAddFundamentalDomain.map_restrict_quotient

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“` is
  measure-preserving between appropriate multiples of Haar measure on `G` and `G â§¸ Î“`. -/
@[to_additive MeasurePreservingQuotientAddGroup.mk' "Given a normal subgroup `Î“` of an additive
  topological group `G` with Haar measure `Î¼`, which is also right-invariant, and a finite volume
  fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“` is measure-preserving between appropriate
  multiples of Haar measure on `G` and `G â§¸ Î“`."]
theorem MeasurePreservingQuotientGroup.mk' [T2Space (G â§¸ Î“)] [SecondCountableTopology (G â§¸ Î“)]
    (K : PositiveCompacts (G â§¸ Î“)) [Subgroup.Normal Î“] [Î¼.IsHaarMeasure]
    [Î¼.IsMulRightInvariant] (hğ“•_finite : Î¼ ğ“• < âŠ¤) (c : â„â‰¥0)
    (h : Î¼ (ğ“• âˆ© QuotientGroup.mk' Î“ â»Â¹' K) = c) :
    MeasurePreserving (QuotientGroup.mk' Î“) (Î¼.restrict ğ“•)
      (c â€¢ MeasureTheory.Measure.haarMeasure K) where
  measurable := continuous_quotient_mk'.measurable
  map_eq := by rw [hğ“•.map_restrict_quotient K hğ“•_finite, h]; rfl
#align measure_preserving_quotient_group.mk' MeasurePreservingQuotientGroup.mk'
#align measure_preserving_quotient_add_group.mk' MeasurePreservingQuotientAddGroup.mk'

section

local notation "Î¼_ğ“•" => Measure.map (@QuotientGroup.mk G _ Î“) (Î¼.restrict ğ“•)

/-- The `essSup` of a function `g` on the quotient space `G â§¸ Î“` with respect to the pushforward
  of the restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` to a fundamental domain `ğ“•`, is the
  same as the `essSup` of `g`'s lift to the universal cover `G` with respect to `Î¼`. -/
@[to_additive "The `essSup` of a function `g` on the additive quotient space `G â§¸ Î“` with respect
  to the pushforward of the restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` to a fundamental
  domain `ğ“•`, is the same as the `essSup` of `g`'s lift to the universal cover `G` with respect
  to `Î¼`."]
lemma essSup_comp_quotientGroup_mk [Î¼.IsMulRightInvariant] {g : G â§¸ Î“ â†’ â„â‰¥0âˆ}
    (g_ae_measurable : AEMeasurable g Î¼_ğ“•) : essSup g Î¼_ğ“• = essSup (fun (x : G) â†¦ g x) Î¼ := by
  have hÏ€ : Measurable (QuotientGroup.mk : G â†’ G â§¸ Î“) := continuous_quotient_mk'.measurable
  rw [essSup_map_measure g_ae_measurable hÏ€.aemeasurable]
  refine hğ“•.essSup_measure_restrict ?_
  intro âŸ¨Î³, hÎ³âŸ© x
  dsimp
  congr 1
  exact QuotientGroup.mk_mul_of_mem x hÎ³

/-- Given a quotient space `G â§¸ Î“` where `Î“` is `Countable`, and the restriction,
  `Î¼_ğ“•`, of a right-invariant measure `Î¼` on `G` to a fundamental domain `ğ“•`, a set
  in the quotient which has `Î¼_ğ“•`-measure zero, also has measure zero under the
  folding of `Î¼` under the quotient. Note that, if `Î“` is infinite, then the folded map
  will take the value `âˆ` on any open set in the quotient! -/
@[to_additive "Given an additive quotient space `G â§¸ Î“` where `Î“` is `Countable`, and the
  restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` on `G` to a fundamental domain `ğ“•`, a set
  in the quotient which has `Î¼_ğ“•`-measure zero, also has measure zero under the
  folding of `Î¼` under the quotient. Note that, if `Î“` is infinite, then the folded map
  will take the value `âˆ` on any open set in the quotient!"]
lemma _root_.MeasureTheory.IsFundamentalDomain.absolutelyContinuous_map
    [Î¼.IsMulRightInvariant] :
    map (QuotientGroup.mk : G â†’ G â§¸ Î“) Î¼ â‰ª map (QuotientGroup.mk : G â†’ G â§¸ Î“) (Î¼.restrict ğ“•) := by
  set Ï€ : G â†’ G â§¸ Î“ := QuotientGroup.mk
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  apply AbsolutelyContinuous.mk
  intro s s_meas hs
  rw [map_apply meas_Ï€ s_meas] at hs âŠ¢
  rw [Measure.restrict_apply] at hs
  apply hğ“•.measure_zero_of_invariant _ _ hs
  Â· intro Î³
    ext g
    rw [Set.mem_smul_set_iff_inv_smul_mem, mem_preimage, mem_preimage]
    congr! 1
    convert QuotientGroup.mk_mul_of_mem g (Î³â»Â¹).2 using 1
  exact MeasurableSet.preimage s_meas meas_Ï€

attribute [-instance] Quotient.instMeasurableSpace

/-- This is a simple version of the **Unfolding Trick**: Given a subgroup `Î“` of a group `G`, the
  integral of a function `f` on `G` with respect to a right-invariant measure `Î¼` is equal to the
  integral over the quotient `G â§¸ Î“` of the automorphization of `f`. -/
@[to_additive "This is a simple version of the **Unfolding Trick**: Given a subgroup `Î“` of an
  additive  group `G`, the integral of a function `f` on `G` with respect to a right-invariant
  measure `Î¼` is equal to the integral over the quotient `G â§¸ Î“` of the automorphization of `f`."]
lemma QuotientGroup.integral_eq_integral_automorphize {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] [Î¼.IsMulRightInvariant] {f : G â†’ E}
    (hfâ‚ : Integrable f Î¼) (hfâ‚‚ : AEStronglyMeasurable (automorphize f) Î¼_ğ“•) :
    âˆ« x : G, f x âˆ‚Î¼ = âˆ« x : G â§¸ Î“, automorphize f x âˆ‚Î¼_ğ“• := by
  calc âˆ« x : G, f x âˆ‚Î¼ = âˆ‘' Î³ : Î“.op, âˆ« x in ğ“•, f (Î³ â€¢ x) âˆ‚Î¼ :=
    hğ“•.integral_eq_tsum'' f hfâ‚
    _ = âˆ« x in ğ“•, âˆ‘' Î³ : Î“.op, f (Î³ â€¢ x) âˆ‚Î¼ := ?_
    _ = âˆ« x : G â§¸ Î“, automorphize f x âˆ‚Î¼_ğ“• :=
      (integral_map continuous_quotient_mk'.aemeasurable hfâ‚‚).symm
  rw [integral_tsum]
  Â· exact fun i â†¦ (hfâ‚.1.comp_quasiMeasurePreserving
      (measurePreserving_smul i Î¼).quasiMeasurePreserving).restrict
  Â· rw [â† hğ“•.lintegral_eq_tsum'' (fun x â†¦ â€–f xâ€–â‚Š)]
    exact ne_of_lt hfâ‚.2

/-- This is the **Unfolding Trick**: Given a subgroup `Î“` of a group `G`, the integral of a
  function `f` on `G` times the lift to `G` of a function `g` on the quotient `G â§¸ Î“` with respect
  to a right-invariant measure `Î¼` on `G`, is equal to the integral over the quotient of the
  automorphization of `f` times `g`. -/
lemma QuotientGroup.integral_mul_eq_integral_automorphize_mul {K : Type*} [NormedField K]
    [NormedSpace â„ K] [Î¼.IsMulRightInvariant] {f : G â†’ K}
    (f_â„’_1 : Integrable f Î¼) {g : G â§¸ Î“ â†’ K} (hg : AEStronglyMeasurable g Î¼_ğ“•)
    (g_â„’_infinity : essSup (fun x â†¦ â†‘â€–g xâ€–â‚Š) Î¼_ğ“• â‰  âˆ)
    (F_ae_measurable : AEStronglyMeasurable (QuotientGroup.automorphize f) Î¼_ğ“•) :
    âˆ« x : G, g (x : G â§¸ Î“) * (f x) âˆ‚Î¼
      = âˆ« x : G â§¸ Î“, g x * (QuotientGroup.automorphize f x) âˆ‚Î¼_ğ“• := by
  let Ï€ : G â†’ G â§¸ Î“ := QuotientGroup.mk
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  have Hâ‚€ : QuotientGroup.automorphize ((g âˆ˜ Ï€) * f) = g * (QuotientGroup.automorphize f) := by
    exact QuotientGroup.automorphize_smul_left f g
  calc âˆ« (x : G), g (Ï€ x) * (f x) âˆ‚Î¼ =
        âˆ« (x : G â§¸ Î“), QuotientGroup.automorphize ((g âˆ˜ Ï€) * f) x âˆ‚Î¼_ğ“• := ?_
    _ = âˆ« (x : G â§¸ Î“), g x * (QuotientGroup.automorphize f x) âˆ‚Î¼_ğ“• := by simp [Hâ‚€]
  have Hâ‚ : Integrable ((g âˆ˜ Ï€) * f) Î¼
  Â· have : AEStronglyMeasurable (fun (x : G) â†¦ g (x : (G â§¸ Î“))) Î¼
    Â· refine (AEStronglyMeasurable.mono' hg ?_).comp_measurable meas_Ï€
      exact hğ“•.absolutelyContinuous_map
    refine Integrable.essSup_smul f_â„’_1 this ?_
    have hg' : AEStronglyMeasurable (fun x â†¦ (â€–g xâ€–â‚Š : â„â‰¥0âˆ)) Î¼_ğ“• :=
      (ENNReal.continuous_coe.comp continuous_nnnorm).comp_aestronglyMeasurable hg
    rw [â† essSup_comp_quotientGroup_mk hğ“• hg'.aemeasurable]
    exact g_â„’_infinity
  have Hâ‚‚ : AEStronglyMeasurable (QuotientGroup.automorphize ((g âˆ˜ Ï€) * f)) Î¼_ğ“•
  Â· simp_rw [Hâ‚€]
    exact hg.mul F_ae_measurable
  apply QuotientGroup.integral_eq_integral_automorphize hğ“• Hâ‚ Hâ‚‚

end

section

variable {G' : Type*} [AddGroup G'] [MeasurableSpace G'] [TopologicalSpace G']
  [TopologicalAddGroup G'] [BorelSpace G']
  {Î¼' : Measure G'}
  {Î“' : AddSubgroup G'}
  [Countable Î“'] [MeasurableSpace (G' â§¸ Î“')] [BorelSpace (G' â§¸ Î“')]
  {ğ“•' : Set G'}

local notation "Î¼_ğ“•" => Measure.map (@QuotientAddGroup.mk G' _ Î“') (Î¼'.restrict ğ“•')

/-- This is the **Unfolding Trick**: Given an additive subgroup `Î“'` of an additive group `G'`, the
  integral of a function `f` on `G'` times the lift to `G'` of a function `g` on the quotient
  `G' â§¸ Î“'` with respect to a right-invariant measure `Î¼` on `G'`, is equal to the integral over
  the quotient of the automorphization of `f` times `g`. -/
lemma QuotientAddGroup.integral_mul_eq_integral_automorphize_mul {K : Type*} [NormedField K]
    [NormedSpace â„ K] [Î¼'.IsAddRightInvariant] {f : G' â†’ K}
    (f_â„’_1 : Integrable f Î¼') {g : G' â§¸ Î“' â†’ K} (hg : AEStronglyMeasurable g Î¼_ğ“•)
    (g_â„’_infinity : essSup (fun x â†¦ (â€–g xâ€–â‚Š : â„â‰¥0âˆ)) Î¼_ğ“• â‰  âˆ)
    (F_ae_measurable : AEStronglyMeasurable (QuotientAddGroup.automorphize f) Î¼_ğ“•)
    (hğ“• : IsAddFundamentalDomain Î“'.op ğ“•' Î¼') :
    âˆ« x : G', g (x : G' â§¸ Î“') * (f x) âˆ‚Î¼'
      = âˆ« x : G' â§¸ Î“', g x * (QuotientAddGroup.automorphize f x) âˆ‚Î¼_ğ“• := by
  let Ï€ : G' â†’ G' â§¸ Î“' := QuotientAddGroup.mk
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  have Hâ‚€ : QuotientAddGroup.automorphize ((g âˆ˜ Ï€) * f) = g * (QuotientAddGroup.automorphize f) :=
    by exact QuotientAddGroup.automorphize_smul_left f g
  calc âˆ« (x : G'), g (Ï€ x) * f x âˆ‚Î¼' =
    âˆ« (x : G' â§¸ Î“'), QuotientAddGroup.automorphize ((g âˆ˜ Ï€) * f) x âˆ‚Î¼_ğ“• := ?_
    _ = âˆ« (x : G' â§¸ Î“'), g x * (QuotientAddGroup.automorphize f x) âˆ‚Î¼_ğ“• := by simp [Hâ‚€]
  have Hâ‚ : Integrable ((g âˆ˜ Ï€) * f) Î¼'
  Â· have : AEStronglyMeasurable (fun (x : G') â†¦ g (x : (G' â§¸ Î“'))) Î¼'
    Â· refine (AEStronglyMeasurable.mono' hg ?_).comp_measurable meas_Ï€
      exact hğ“•.absolutelyContinuous_map
    refine Integrable.essSup_smul f_â„’_1 this ?_
    have hg' : AEStronglyMeasurable (fun x â†¦ (â€–g xâ€–â‚Š : â„â‰¥0âˆ)) Î¼_ğ“• :=
      (ENNReal.continuous_coe.comp continuous_nnnorm).comp_aestronglyMeasurable hg
    rw [â† essSup_comp_quotientAddGroup_mk hğ“• hg'.aemeasurable]
    exact g_â„’_infinity
  have Hâ‚‚ : AEStronglyMeasurable (QuotientAddGroup.automorphize ((g âˆ˜ Ï€) * f)) Î¼_ğ“•
  Â· simp_rw [Hâ‚€]
    exact hg.mul F_ae_measurable
  apply QuotientAddGroup.integral_eq_integral_automorphize hğ“• Hâ‚ Hâ‚‚

end

attribute [to_additive existing QuotientGroup.integral_mul_eq_integral_automorphize_mul]
  QuotientAddGroup.integral_mul_eq_integral_automorphize_mul
