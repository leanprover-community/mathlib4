/-
Copyright (c) 2022 Alex Kontorovich and Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/
import Mathlib.Algebra.Group.Opposite
import Mathlib.MeasureTheory.Constructions.Polish.Basic
import Mathlib.MeasureTheory.Group.FundamentalDomain
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Measure.Haar.Basic

/-!
# Haar quotient measure

In this file, we consider properties of fundamental domains and measures for the action of a
subgroup `Î“` of a topological group `G` on `G` itself. Let `Î¼` be a measure on `G â§¸ Î“`.

## Main results

* `MeasureTheory.QuotientMeasureEqMeasurePreimage.smulInvariantMeasure_quotient`: If `Î¼` satisfies
  `QuotientMeasureEqMeasurePreimage` relative to a both left- and right-invariant measure on `G`,
  then it is a `G` invariant measure on `G â§¸ Î“`.

The next two results assume that `Î“` is normal, and that `G` is equipped with a left- and
right-invariant measure.

* `MeasureTheory.QuotientMeasureEqMeasurePreimage.mulInvariantMeasure_quotient`: If `Î¼` satisfies
  `QuotientMeasureEqMeasurePreimage`, then `Î¼` is a left-invariant measure.

* `MeasureTheory.leftInvariantIsQuotientMeasureEqMeasurePreimage`: If `Î¼` is left-invariant, and
  the action of `Î“` on `G` has finite covolume, and `Î¼` satisfies the right scaling condition, then
  it satisfies `QuotientMeasureEqMeasurePreimage`. This is a converse to
  `MeasureTheory.QuotientMeasureEqMeasurePreimage.mulInvariantMeasure_quotient`.

The last result assumes that `G` is locally compact, that `Î“` is countable and normal, that its
action on `G` has a fundamental domain, and that `Î¼` is a finite measure. We also assume that `G`
is equipped with a sigma-finite Haar measure.

* `MeasureTheory.QuotientMeasureEqMeasurePreimage.haarMeasure_quotient`: If `Î¼` satisfies
  `QuotientMeasureEqMeasurePreimage`, then it is itself Haar. This is a variant of
  `MeasureTheory.QuotientMeasureEqMeasurePreimage.mulInvariantMeasure_quotient`.

Note that a group `G` with Haar measure that is both left and right invariant is called
**unimodular**.
-/

open Set MeasureTheory TopologicalSpace MeasureTheory.Measure

open scoped Pointwise NNReal ENNReal

section

/-- Measurability of the action of the topological group `G` on the left-coset space `G / Î“`. -/
@[to_additive "Measurability of the action of the additive topological group `G` on the left-coset
  space `G / Î“`."]
instance QuotientGroup.measurableSMul {G : Type*} [Group G] {Î“ : Subgroup G} [MeasurableSpace G]
    [TopologicalSpace G] [IsTopologicalGroup G] [BorelSpace G] [BorelSpace (G â§¸ Î“)] :
    MeasurableSMul G (G â§¸ Î“) where
  measurable_const_smul g := (continuous_const_smul g).measurable
  measurable_smul_const _ := (continuous_id.smul continuous_const).measurable

end

section smulInvariantMeasure

variable {G : Type*} [Group G] [MeasurableSpace G] (Î½ : Measure G) {Î“ : Subgroup G}
  {Î¼ : Measure (G â§¸ Î“)}
  [QuotientMeasureEqMeasurePreimage Î½ Î¼]

/-- Given a subgroup `Î“` of a topological group `G` with measure `Î½`, and a measure 'Î¼' on the
  quotient `G â§¸ Î“` satisfying `QuotientMeasureEqMeasurePreimage`, the restriction
  of `Î½` to a fundamental domain is measure-preserving with respect to `Î¼`. -/
@[to_additive]
theorem measurePreserving_quotientGroup_mk_of_QuotientMeasureEqMeasurePreimage
    {ğ“• : Set G} (hğ“• : IsFundamentalDomain Î“.op ğ“• Î½) (Î¼ : Measure (G â§¸ Î“))
    [QuotientMeasureEqMeasurePreimage Î½ Î¼] :
    MeasurePreserving (@QuotientGroup.mk G _ Î“) (Î½.restrict ğ“•) Î¼ :=
  hğ“•.measurePreserving_quotient_mk Î¼

local notation "Ï€" => @QuotientGroup.mk G _ Î“

variable [TopologicalSpace G] [IsTopologicalGroup G] [BorelSpace G] [PolishSpace G]
  [T2Space (G â§¸ Î“)] [SecondCountableTopology (G â§¸ Î“)]

/-- If `Î¼` satisfies `QuotientMeasureEqMeasurePreimage` relative to a both left- and right-
  invariant measure `Î½` on `G`, then it is a `G` invariant measure on `G â§¸ Î“`. -/
@[to_additive]
lemma MeasureTheory.QuotientMeasureEqMeasurePreimage.smulInvariantMeasure_quotient
    [IsMulLeftInvariant Î½] [hasFun : HasFundamentalDomain Î“.op G Î½] :
    SMulInvariantMeasure G (G â§¸ Î“) Î¼ where
  measure_preimage_smul g A hA := by
    have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
    obtain âŸ¨ğ“•, hğ“•âŸ© := hasFun.ExistsIsFundamentalDomain
    have hğ“•_translate_fundom : IsFundamentalDomain Î“.op (g â€¢ ğ“•) Î½ := hğ“•.smul_of_comm g
    -- TODO: why `rw` fails with both of these rewrites?
    erw [hğ“•.projection_respects_measure_apply (Î¼ := Î¼)
      (meas_Ï€ (measurableSet_preimage (measurable_const_smul g) hA)),
      hğ“•_translate_fundom.projection_respects_measure_apply (Î¼ := Î¼) hA]
    change Î½ ((Ï€ â»Â¹' _) âˆ© _) = Î½ ((Ï€ â»Â¹' _) âˆ© _)
    set Ï€_preA := Ï€ â»Â¹' A
    have : Ï€ â»Â¹' ((fun x : G â§¸ Î“ => g â€¢ x) â»Â¹' A) = (g * Â·) â»Â¹' Ï€_preA := by ext1; simp [Ï€_preA]
    rw [this]
    have : Î½ ((g * Â·) â»Â¹' Ï€_preA âˆ© ğ“•) = Î½ (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•) := by
      trans Î½ ((g * Â·) â»Â¹' (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•))
      Â· rw [preimage_inter]
        congr 2
        simp [Set.preimage]
      rw [measure_preimage_mul]
    rw [this, â† preimage_smul_inv]; rfl

end smulInvariantMeasure

section normal

variable {G : Type*} [Group G] [MeasurableSpace G] [TopologicalSpace G] [IsTopologicalGroup G]
  [BorelSpace G] [PolishSpace G] {Î“ : Subgroup G} [Subgroup.Normal Î“]
  [T2Space (G â§¸ Î“)] [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}

section mulInvariantMeasure

variable (Î½ : Measure G) [IsMulLeftInvariant Î½]

/-- If `Î¼` on `G â§¸ Î“` satisfies `QuotientMeasureEqMeasurePreimage` relative to a both left- and
  right-invariant measure on `G` and `Î“` is a normal subgroup, then `Î¼` is a left-invariant
  measure. -/
@[to_additive "If `Î¼` on `G â§¸ Î“` satisfies `AddQuotientMeasureEqMeasurePreimage` relative to a both
  left- and right-invariant measure on `G` and `Î“` is a normal subgroup, then `Î¼` is a
  left-invariant measure."]
lemma MeasureTheory.QuotientMeasureEqMeasurePreimage.mulInvariantMeasure_quotient
    [hasFun : HasFundamentalDomain Î“.op G Î½] [QuotientMeasureEqMeasurePreimage Î½ Î¼] :
    Î¼.IsMulLeftInvariant where
  map_mul_left_eq_self x := by
    ext A hA
    obtain âŸ¨xâ‚, hâŸ© := @Quotient.exists_rep _ (QuotientGroup.leftRel Î“) x
    convert measure_preimage_smul Î¼ xâ‚ A using 1
    Â· rw [â† h, Measure.map_apply (measurable_const_mul _) hA]
      simp [â† MulAction.Quotient.coe_smul_out, â† Quotient.mk''_eq_mk]
    exact smulInvariantMeasure_quotient Î½

variable [Countable Î“] [IsMulRightInvariant Î½] [SigmaFinite Î½]
  [IsMulLeftInvariant Î¼] [SigmaFinite Î¼]

local notation "Ï€" => @QuotientGroup.mk G _ Î“

/-- Assume that a measure `Î¼` is `IsMulLeftInvariant`, that the action of `Î“` on `G` has a
measurable fundamental domain `s` with positive finite volume, and that there is a single measurable
set `V âŠ† G â§¸ Î“` along which the pullback of `Î¼` and `Î½` agree (so the scaling is right). Then
`Î¼` satisfies `QuotientMeasureEqMeasurePreimage`. The main tool of the proof is the uniqueness of
left invariant measures, if normalized by a single positive finite-measured set. -/
@[to_additive
"Assume that a measure `Î¼` is `IsAddLeftInvariant`, that the action of `Î“` on `G` has a
measurable fundamental domain `s` with positive finite volume, and that there is a single measurable
set `V âŠ† G â§¸ Î“` along which the pullback of `Î¼` and `Î½` agree (so the scaling is right). Then
`Î¼` satisfies `AddQuotientMeasureEqMeasurePreimage`. The main tool of the proof is the uniqueness of
left invariant measures, if normalized by a single positive finite-measured set."]
theorem MeasureTheory.Measure.IsMulLeftInvariant.quotientMeasureEqMeasurePreimage_of_set {s : Set G}
    (fund_dom_s : IsFundamentalDomain Î“.op s Î½) {V : Set (G â§¸ Î“)}
    (meas_V : MeasurableSet V) (neZeroV : Î¼ V â‰  0) (hV : Î¼ V = Î½ (Ï€ â»Â¹' V âˆ© s))
    (neTopV : Î¼ V â‰  âŠ¤) : QuotientMeasureEqMeasurePreimage Î½ Î¼ := by
  apply fund_dom_s.quotientMeasureEqMeasurePreimage
  ext U _
  have meas_Ï€ : Measurable (QuotientGroup.mk : G â†’ G â§¸ Î“) := continuous_quotient_mk'.measurable
  let Î¼' : Measure (G â§¸ Î“) := (Î½.restrict s).map Ï€
  haveI has_fund : HasFundamentalDomain Î“.op G Î½ := âŸ¨âŸ¨s, fund_dom_sâŸ©âŸ©
  have i : QuotientMeasureEqMeasurePreimage Î½ Î¼' :=
    fund_dom_s.quotientMeasureEqMeasurePreimage_quotientMeasure
  have : Î¼'.IsMulLeftInvariant :=
    MeasureTheory.QuotientMeasureEqMeasurePreimage.mulInvariantMeasure_quotient Î½
  suffices Î¼ = Î¼' by
    rw [this]
    rfl
  have : SigmaFinite Î¼' := i.sigmaFiniteQuotient
  rw [measure_eq_div_smul Î¼' Î¼ neZeroV neTopV, hV]
  symm
  suffices (Î¼' V / Î½ (QuotientGroup.mk â»Â¹' V âˆ© s)) = 1 by rw [this, one_smul]
  rw [Measure.map_apply meas_Ï€ meas_V, Measure.restrict_apply]
  Â· convert ENNReal.div_self ..
    Â· exact trans hV.symm neZeroV
    Â· exact trans hV.symm neTopV
  exact measurableSet_quotient.mp meas_V

/-- If a measure `Î¼` is left-invariant and satisfies the right scaling condition, then it
  satisfies `QuotientMeasureEqMeasurePreimage`. -/
@[to_additive "If a measure `Î¼` is
left-invariant and satisfies the right scaling condition, then it satisfies
`AddQuotientMeasureEqMeasurePreimage`."]
theorem MeasureTheory.leftInvariantIsQuotientMeasureEqMeasurePreimage [IsFiniteMeasure Î¼]
    [hasFun : HasFundamentalDomain Î“.op G Î½]
    (h : covolume Î“.op G Î½ = Î¼ univ) : QuotientMeasureEqMeasurePreimage Î½ Î¼ := by
  obtain âŸ¨s, fund_dom_sâŸ© := hasFun.ExistsIsFundamentalDomain
  have finiteCovol : Î¼ univ < âŠ¤ := measure_lt_top Î¼ univ
  rw [fund_dom_s.covolume_eq_volume] at h
  by_cases meas_s_ne_zero : Î½ s = 0
  Â· convert fund_dom_s.quotientMeasureEqMeasurePreimage_of_zero meas_s_ne_zero
    rw [â† @measure_univ_eq_zero, â† h, meas_s_ne_zero]
  apply IsMulLeftInvariant.quotientMeasureEqMeasurePreimage_of_set (fund_dom_s := fund_dom_s)
    (meas_V := MeasurableSet.univ)
  Â· rw [â† h]
    exact meas_s_ne_zero
  Â· rw [â† h]
    simp
  Â· rw [â† h]
    convert finiteCovol.ne

end mulInvariantMeasure

section haarMeasure

variable [Countable Î“] (Î½ : Measure G) [IsHaarMeasure Î½] [IsMulRightInvariant Î½]

local notation "Ï€" => @QuotientGroup.mk G _ Î“

/-- If a measure `Î¼` on the quotient `G â§¸ Î“` of a group `G` by a discrete normal subgroup `Î“` having
fundamental domain, satisfies `QuotientMeasureEqMeasurePreimage` relative to a standardized choice
of Haar measure on `G`, and assuming `Î¼` is finite, then `Î¼` is itself Haar.
TODO: Is it possible to drop the assumption that `Î¼` is finite? -/
@[to_additive "If a measure `Î¼` on the quotient `G â§¸ Î“` of an additive group `G` by a discrete
normal subgroup `Î“` having fundamental domain, satisfies `AddQuotientMeasureEqMeasurePreimage`
relative to a standardized choice of Haar measure on `G`, and assuming `Î¼` is finite, then `Î¼` is
itself Haar."]
theorem MeasureTheory.QuotientMeasureEqMeasurePreimage.haarMeasure_quotient [LocallyCompactSpace G]
    [QuotientMeasureEqMeasurePreimage Î½ Î¼] [i : HasFundamentalDomain Î“.op G Î½]
    [IsFiniteMeasure Î¼] : IsHaarMeasure Î¼ := by
  obtain âŸ¨KâŸ© := PositiveCompacts.nonempty' (Î± := G)
  let K' : PositiveCompacts (G â§¸ Î“) :=
    K.map Ï€ QuotientGroup.continuous_mk QuotientGroup.isOpenMap_coe
  haveI : IsMulLeftInvariant Î¼ :=
    MeasureTheory.QuotientMeasureEqMeasurePreimage.mulInvariantMeasure_quotient Î½
  rw [haarMeasure_unique Î¼ K']
  have finiteCovol : covolume Î“.op G Î½ â‰  âŠ¤ :=
    ne_top_of_lt <| QuotientMeasureEqMeasurePreimage.covolume_ne_top Î¼ (Î½ := Î½)
  obtain âŸ¨s, fund_dom_sâŸ© := i
  rw [fund_dom_s.covolume_eq_volume] at finiteCovol
  -- TODO: why `rw` fails?
  erw [fund_dom_s.projection_respects_measure_apply Î¼ K'.isCompact.measurableSet]
  apply IsHaarMeasure.smul
  Â· intro h
    haveI i' : IsOpenPosMeasure (Î½ : Measure G) := inferInstance
    apply IsOpenPosMeasure.open_pos (interior K) (Î¼ := Î½) (self := i')
    Â· exact isOpen_interior
    Â· exact K.interior_nonempty
    rw [â† le_zero_iff,
      â† fund_dom_s.measure_zero_of_invariant _ (fun g â†¦ QuotientGroup.sound _ _ g) h]
    apply measure_mono
    refine interior_subset.trans ?_
    rw [QuotientGroup.coe_mk']
    show (K : Set G) âŠ† Ï€ â»Â¹' (Ï€ '' K)
    exact subset_preimage_image Ï€ K
  Â· show Î½ (Ï€ â»Â¹' (Ï€ '' K) âˆ© s) â‰  âŠ¤
    apply ne_of_lt
    refine lt_of_le_of_lt ?_ finiteCovol.lt_top
    apply measure_mono
    exact inter_subset_right

variable [SigmaFinite Î½]

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `QuotientMeasureEqMeasurePreimage`. -/
@[to_additive "Given a normal
subgroup `Î“` of an additive topological group `G` with Haar measure `Î¼`, which is also
right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
properly normalized, satisfies `AddQuotientMeasureEqMeasurePreimage`."]
theorem IsFundamentalDomain.QuotientMeasureEqMeasurePreimage_HaarMeasure {ğ“• : Set G}
    (hğ“• : IsFundamentalDomain Î“.op ğ“• Î½) [IsMulLeftInvariant Î¼] [SigmaFinite Î¼]
    {V : Set (G â§¸ Î“)} (hV : (interior V).Nonempty) (meas_V : MeasurableSet V)
    (hÎ¼K : Î¼ V = Î½ ((Ï€ â»Â¹' V) âˆ© ğ“•)) (neTopV : Î¼ V â‰  âŠ¤) :
    QuotientMeasureEqMeasurePreimage Î½ Î¼ := by
  apply IsMulLeftInvariant.quotientMeasureEqMeasurePreimage_of_set (fund_dom_s := hğ“•)
    (meas_V := meas_V)
  Â· rw [hÎ¼K]
    intro c_eq_zero
    apply IsOpenPosMeasure.open_pos (interior (Ï€ â»Â¹' V)) (Î¼ := Î½)
    Â· simp
    Â· apply Set.Nonempty.mono (preimage_interior_subset_interior_preimage continuous_coinduced_rng)
      apply hV.preimage'
      simp
    Â· apply measure_mono_null (h := interior_subset)
      apply hğ“•.measure_zero_of_invariant (ht := fun g â†¦ QuotientGroup.sound _ _ g)
      exact c_eq_zero
  Â· exact hÎ¼K
  Â· exact neTopV

variable (K : PositiveCompacts (G â§¸ Î“))

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `QuotientMeasureEqMeasurePreimage`. -/
@[to_additive "Given a
normal subgroup `Î“` of an additive topological group `G` with Haar measure `Î¼`, which is also
right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
properly normalized, satisfies `AddQuotientMeasureEqMeasurePreimage`."]
theorem IsFundamentalDomain.QuotientMeasureEqMeasurePreimage_smulHaarMeasure {ğ“• : Set G}
    (hğ“• : IsFundamentalDomain Î“.op ğ“• Î½) (hğ“•_finite : Î½ ğ“• â‰  âŠ¤) :
    QuotientMeasureEqMeasurePreimage Î½
      ((Î½ ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)) â€¢ haarMeasure K) := by
  set c := Î½ ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)
  have c_ne_top : c â‰  âˆ := by
    contrapose! hğ“•_finite
    have : c â‰¤ Î½ ğ“• := measure_mono (Set.inter_subset_right)
    rw [hğ“•_finite] at this
    exact top_unique this
  set Î¼ := c â€¢ haarMeasure K
  have hÎ¼K : Î¼ K = c := by simp [Î¼, haarMeasure_self]
  haveI : SigmaFinite Î¼ := by
    clear_value c
    lift c to NNReal using c_ne_top
    exact SMul.sigmaFinite c
  apply IsFundamentalDomain.QuotientMeasureEqMeasurePreimage_HaarMeasure (hğ“• := hğ“•)
    (meas_V := K.isCompact.measurableSet) (Î¼ := Î¼)
  Â· exact K.interior_nonempty
  Â· exact hÎ¼K
  Â· rw [hÎ¼K]
    exact c_ne_top

end haarMeasure

end normal

section UnfoldingTrick

variable {G : Type*} [Group G] [MeasurableSpace G] [TopologicalSpace G] [IsTopologicalGroup G]
  [BorelSpace G] {Î¼ : Measure G} {Î“ : Subgroup G}

variable {ğ“• : Set G} (hğ“• : IsFundamentalDomain Î“.op ğ“• Î¼)
include hğ“•

variable [Countable Î“] [MeasurableSpace (G â§¸ Î“)] [BorelSpace (G â§¸ Î“)]

local notation "Î¼_ğ“•" => Measure.map (@QuotientGroup.mk G _ Î“) (Î¼.restrict ğ“•)

/-- The `essSup` of a function `g` on the quotient space `G â§¸ Î“` with respect to the pushforward
  of the restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` to a fundamental domain `ğ“•`, is the
  same as the `essSup` of `g`'s lift to the universal cover `G` with respect to `Î¼`. -/
@[to_additive "The `essSup` of a function `g` on the additive quotient space `G â§¸ Î“` with respect
  to the pushforward of the restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` to a fundamental
  domain `ğ“•`, is the same as the `essSup` of `g`'s lift to the universal cover `G` with respect
  to `Î¼`."]
lemma essSup_comp_quotientGroup_mk [Î¼.IsMulRightInvariant] {g : G â§¸ Î“ â†’ â„â‰¥0âˆ}
    (g_ae_measurable : AEMeasurable g Î¼_ğ“•) : essSup g Î¼_ğ“• = essSup (fun (x : G) â†¦ g x) Î¼ := by
  have hÏ€ : Measurable (QuotientGroup.mk : G â†’ G â§¸ Î“) := continuous_quotient_mk'.measurable
  rw [essSup_map_measure g_ae_measurable hÏ€.aemeasurable]
  refine hğ“•.essSup_measure_restrict ?_
  intro âŸ¨Î³, hÎ³âŸ© x
  dsimp
  congr 1
  exact QuotientGroup.mk_mul_of_mem x hÎ³

/-- Given a quotient space `G â§¸ Î“` where `Î“` is `Countable`, and the restriction,
  `Î¼_ğ“•`, of a right-invariant measure `Î¼` on `G` to a fundamental domain `ğ“•`, a set
  in the quotient which has `Î¼_ğ“•`-measure zero, also has measure zero under the
  folding of `Î¼` under the quotient. Note that, if `Î“` is infinite, then the folded map
  will take the value `âˆ` on any open set in the quotient! -/
@[to_additive "Given an additive quotient space `G â§¸ Î“` where `Î“` is `Countable`, and the
  restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` on `G` to a fundamental domain `ğ“•`, a set
  in the quotient which has `Î¼_ğ“•`-measure zero, also has measure zero under the
  folding of `Î¼` under the quotient. Note that, if `Î“` is infinite, then the folded map
  will take the value `âˆ` on any open set in the quotient!"]
lemma _root_.MeasureTheory.IsFundamentalDomain.absolutelyContinuous_map
    [Î¼.IsMulRightInvariant] :
    map (QuotientGroup.mk : G â†’ G â§¸ Î“) Î¼ â‰ª map (QuotientGroup.mk : G â†’ G â§¸ Î“) (Î¼.restrict ğ“•) := by
  set Ï€ : G â†’ G â§¸ Î“ := QuotientGroup.mk
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  apply AbsolutelyContinuous.mk
  intro s s_meas hs
  rw [map_apply meas_Ï€ s_meas] at hs âŠ¢
  rw [Measure.restrict_apply] at hs
  Â· apply hğ“•.measure_zero_of_invariant _ _ hs
    intro Î³
    ext g
    rw [Set.mem_smul_set_iff_inv_smul_mem, mem_preimage, mem_preimage]
    congr! 1
    convert QuotientGroup.mk_mul_of_mem g (Î³â»Â¹).2 using 1
  exact MeasurableSet.preimage s_meas meas_Ï€

attribute [-instance] Quotient.instMeasurableSpace

/-- This is a simple version of the **Unfolding Trick**: Given a subgroup `Î“` of a group `G`, the
  integral of a function `f` on `G` with respect to a right-invariant measure `Î¼` is equal to the
  integral over the quotient `G â§¸ Î“` of the automorphization of `f`. -/
@[to_additive "This is a simple version of the **Unfolding Trick**: Given a subgroup `Î“` of an
  additive group `G`, the integral of a function `f` on `G` with respect to a right-invariant
  measure `Î¼` is equal to the integral over the quotient `G â§¸ Î“` of the automorphization of `f`."]
lemma QuotientGroup.integral_eq_integral_automorphize {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] [Î¼.IsMulRightInvariant] {f : G â†’ E}
    (hfâ‚ : Integrable f Î¼) (hfâ‚‚ : AEStronglyMeasurable (automorphize f) Î¼_ğ“•) :
    âˆ« x : G, f x âˆ‚Î¼ = âˆ« x : G â§¸ Î“, automorphize f x âˆ‚Î¼_ğ“• := by
  calc âˆ« x : G, f x âˆ‚Î¼ = âˆ‘' Î³ : Î“.op, âˆ« x in ğ“•, f (Î³ â€¢ x) âˆ‚Î¼ :=
    hğ“•.integral_eq_tsum'' f hfâ‚
    _ = âˆ« x in ğ“•, âˆ‘' Î³ : Î“.op, f (Î³ â€¢ x) âˆ‚Î¼ := ?_
    _ = âˆ« x : G â§¸ Î“, automorphize f x âˆ‚Î¼_ğ“• :=
      (integral_map continuous_quotient_mk'.aemeasurable hfâ‚‚).symm
  rw [integral_tsum]
  Â· exact fun i â†¦ (hfâ‚.1.comp_quasiMeasurePreserving
      (measurePreserving_smul i Î¼).quasiMeasurePreserving).restrict
  Â· rw [â† hğ“•.lintegral_eq_tsum'' (â€–f Â·â€–â‚‘)]
    exact ne_of_lt hfâ‚.2

-- we can't use `to_additive`, because it tries to translate `*` into `+`
/-- This is the **Unfolding Trick**: Given a subgroup `Î“` of a group `G`, the integral of a
  function `f` on `G` times the lift to `G` of a function `g` on the quotient `G â§¸ Î“` with respect
  to a right-invariant measure `Î¼` on `G`, is equal to the integral over the quotient of the
  automorphization of `f` times `g`. -/
lemma QuotientGroup.integral_mul_eq_integral_automorphize_mul {K : Type*} [NormedField K]
    [NormedSpace â„ K] [Î¼.IsMulRightInvariant] {f : G â†’ K}
    (f_â„’_1 : Integrable f Î¼) {g : G â§¸ Î“ â†’ K} (hg : AEStronglyMeasurable g Î¼_ğ“•)
    (g_â„’_infinity : essSup (fun x â†¦ â†‘â€–g xâ€–â‚‘) Î¼_ğ“• â‰  âˆ)
    (F_ae_measurable : AEStronglyMeasurable (QuotientGroup.automorphize f) Î¼_ğ“•) :
    âˆ« x : G, g (x : G â§¸ Î“) * (f x) âˆ‚Î¼
      = âˆ« x : G â§¸ Î“, g x * (QuotientGroup.automorphize f x) âˆ‚Î¼_ğ“• := by
  let Ï€ : G â†’ G â§¸ Î“ := QuotientGroup.mk
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  have Hâ‚€ : QuotientGroup.automorphize ((g âˆ˜ Ï€) * f) = g * (QuotientGroup.automorphize f) := by
    exact QuotientGroup.automorphize_smul_left f g
  calc âˆ« (x : G), g (Ï€ x) * (f x) âˆ‚Î¼ =
        âˆ« (x : G â§¸ Î“), QuotientGroup.automorphize ((g âˆ˜ Ï€) * f) x âˆ‚Î¼_ğ“• := ?_
    _ = âˆ« (x : G â§¸ Î“), g x * (QuotientGroup.automorphize f x) âˆ‚Î¼_ğ“• := by simp [Hâ‚€]
  have Hâ‚ : Integrable ((g âˆ˜ Ï€) * f) Î¼ := by
    have : AEStronglyMeasurable (fun (x : G) â†¦ g (x : (G â§¸ Î“))) Î¼ :=
      (hg.mono_ac hğ“•.absolutelyContinuous_map).comp_measurable meas_Ï€
    refine Integrable.essSup_smul f_â„’_1 this ?_
    have hg' : AEStronglyMeasurable (â€–g Â·â€–â‚‘) Î¼_ğ“• := continuous_enorm.comp_aestronglyMeasurable hg
    rw [â† essSup_comp_quotientGroup_mk hğ“• hg'.aemeasurable]
    exact g_â„’_infinity
  have Hâ‚‚ : AEStronglyMeasurable (QuotientGroup.automorphize ((g âˆ˜ Ï€) * f)) Î¼_ğ“• := by
    simp_rw [Hâ‚€]
    exact hg.mul F_ae_measurable
  apply QuotientGroup.integral_eq_integral_automorphize hğ“• Hâ‚ Hâ‚‚

end UnfoldingTrick

section

variable {G' : Type*} [AddGroup G'] [MeasurableSpace G'] [TopologicalSpace G']
  [IsTopologicalAddGroup G'] [BorelSpace G'] {Î¼' : Measure G'} {Î“' : AddSubgroup G'}
  {ğ“•' : Set G'} (hğ“• : IsAddFundamentalDomain Î“'.op ğ“•' Î¼')
  [Countable Î“'] [MeasurableSpace (G' â§¸ Î“')] [BorelSpace (G' â§¸ Î“')]
include hğ“•

local notation "Î¼_ğ“•" => Measure.map (@QuotientAddGroup.mk G' _ Î“') (Î¼'.restrict ğ“•')

/-- This is the **Unfolding Trick**: Given an additive subgroup `Î“'` of an additive group `G'`, the
  integral of a function `f` on `G'` times the lift to `G'` of a function `g` on the quotient
  `G' â§¸ Î“'` with respect to a right-invariant measure `Î¼` on `G'`, is equal to the integral over
  the quotient of the automorphization of `f` times `g`. -/
lemma QuotientAddGroup.integral_mul_eq_integral_automorphize_mul {K : Type*} [NormedField K]
    [NormedSpace â„ K] [Î¼'.IsAddRightInvariant] {f : G' â†’ K}
    (f_â„’_1 : Integrable f Î¼') {g : G' â§¸ Î“' â†’ K} (hg : AEStronglyMeasurable g Î¼_ğ“•)
    (g_â„’_infinity : essSup (â€–g Â·â€–â‚‘) Î¼_ğ“• â‰  âˆ)
    (F_ae_measurable : AEStronglyMeasurable (QuotientAddGroup.automorphize f) Î¼_ğ“•) :
    âˆ« x : G', g (x : G' â§¸ Î“') * (f x) âˆ‚Î¼'
      = âˆ« x : G' â§¸ Î“', g x * (QuotientAddGroup.automorphize f x) âˆ‚Î¼_ğ“• := by
  let Ï€ : G' â†’ G' â§¸ Î“' := QuotientAddGroup.mk
  have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
  have Hâ‚€ : QuotientAddGroup.automorphize ((g âˆ˜ Ï€) * f) = g * (QuotientAddGroup.automorphize f) :=
    by exact QuotientAddGroup.automorphize_smul_left f g
  calc âˆ« (x : G'), g (Ï€ x) * f x âˆ‚Î¼' =
    âˆ« (x : G' â§¸ Î“'), QuotientAddGroup.automorphize ((g âˆ˜ Ï€) * f) x âˆ‚Î¼_ğ“• := ?_
    _ = âˆ« (x : G' â§¸ Î“'), g x * (QuotientAddGroup.automorphize f x) âˆ‚Î¼_ğ“• := by simp [Hâ‚€]
  have Hâ‚ : Integrable ((g âˆ˜ Ï€) * f) Î¼' := by
    have : AEStronglyMeasurable (fun (x : G') â†¦ g (x : (G' â§¸ Î“'))) Î¼' :=
      (hg.mono_ac hğ“•.absolutelyContinuous_map).comp_measurable meas_Ï€
    refine Integrable.essSup_smul f_â„’_1 this ?_
    have hg' : AEStronglyMeasurable (â€–g Â·â€–â‚‘) Î¼_ğ“• := continuous_enorm.comp_aestronglyMeasurable hg
    rw [â† essSup_comp_quotientAddGroup_mk hğ“• hg'.aemeasurable]
    exact g_â„’_infinity
  have Hâ‚‚ : AEStronglyMeasurable (QuotientAddGroup.automorphize ((g âˆ˜ Ï€) * f)) Î¼_ğ“• := by
    simp_rw [Hâ‚€]
    exact hg.mul F_ae_measurable
  apply QuotientAddGroup.integral_eq_integral_automorphize hğ“• Hâ‚ Hâ‚‚

end
