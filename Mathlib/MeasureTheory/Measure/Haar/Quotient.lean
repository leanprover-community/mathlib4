/-
Copyright (c) 2022 Alex Kontorovich and Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Mathlib.MeasureTheory.Group.FundamentalDomain
import Mathlib.Algebra.Group.Opposite
import Mathlib.MeasureTheory.Constructions.Polish

#align_import measure_theory.measure.haar.quotient from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# Haar quotient measure

In this file, we consider properties of fundamental domains and measures for the action of a
subgroup of a group `G` on `G` itself.

## Main results

* `MeasureTheory.IsFundamentalDomain.smulInvariantMeasure_map `: given a subgroup `Î“` of a
  topological group `G`, the pushforward to the coset space `G â§¸ Î“` of the restriction of a both
  left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure
  on `G â§¸ Î“`.

* `MeasureTheory.IsFundamentalDomain.isMulLeftInvariant_map `: given a normal subgroup `Î“` of
  a topological group `G`, the pushforward to the quotient group `G â§¸ Î“` of the restriction of
  a both left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a left-invariant
  measure on `G â§¸ Î“`.

Note that a group `G` with Haar measure that is both left and right invariant is called
**unimodular**.
-/

open Set MeasureTheory TopologicalSpace MeasureTheory.Measure

open scoped Pointwise NNReal

section

variable {G : Type _} [Group G] [MeasurableSpace G] [TopologicalSpace G] [TopologicalGroup G]
  [BorelSpace G] {Î“ : Subgroup G} [PolishSpace G] [T2Space (G â§¸ Î“)]
  [SecondCountableTopology (G â§¸ Î“)]

--- TODO: move to `measure_theory.constructions.polish`
instance CosetSpace.borelSpace {G : Type _} [TopologicalSpace G] [PolishSpace G]
    [Group G] [MeasurableSpace G] [BorelSpace G] {N : Subgroup G} [T2Space (G â§¸ N)]
    [SecondCountableTopology (G â§¸ N)] : BorelSpace (G â§¸ N) := Quotient.borelSpace

-- TODO : make additive version of the below

/-- Measurability of the action of the topological group `G` on the left-coset space `G / Î“`. -/
--@[to_additive "Measurability of the action of the additive topological group `G` on the left-coset
--  space `G / Î“`."]
instance QuotientGroup.measurableSMul [PolishSpace G] [T2Space (G â§¸ Î“)]
    [SecondCountableTopology (G â§¸ Î“)] : MeasurableSMul G (G â§¸ Î“) where
  measurable_const_smul g := (continuous_const_smul g).measurable
  measurable_smul_const x := (QuotientGroup.continuous_smulâ‚ x).measurable
#align quotient_group.has_measurable_smul QuotientGroup.measurableSMul
--#align quotient_add_group.has_measurable_vadd QuotientAddGroup.measurableVAdd


/-- Any map on the zero measures is `MeasurePreserving` -/
theorem MeasurePreserving.zero {X Y : Type _} {f : X â†’ Y} [MeasurableSpace X] [MeasurableSpace Y]
    (hf : Measurable f) : MeasurePreserving f 0 0 where
      measurable := hf
      map_eq := Measure.map_zero f

/-- Move somewhere -/
theorem QuotientGroup.sound [Subgroup.Normal Î“] (U : Set (G â§¸ Î“)) (g : (Subgroup.opposite Î“)) :
    g â€¢ (QuotientGroup.mk' Î“) â»Â¹' U = (QuotientGroup.mk' Î“) â»Â¹' U := by
  rw [QuotientGroup.coe_mk']
  ext x
  simp only [mem_preimage]
  have := @Set.mem_inv_smul_set_iff (x := x) (A := (mk' Î“) â»Â¹' U) (a := gâ»Â¹) _ _
  simp only [inv_inv, coe_mk', mem_preimage] at this
  convert this using 2
  apply @Quotient.sound (a := x) (s := (QuotientGroup.leftRel Î“)) (b := gâ»Â¹ â€¢ x)
  use g
  simp

end

section smulInvariantMeasure

variable {G : Type _} [Group G] [MeasureSpace G] [TopologicalSpace G] [TopologicalGroup G]
  [BorelSpace G] [PolishSpace G] {Î“ : Subgroup G} [Countable Î“] [T2Space (G â§¸ Î“)]
  [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}
  [QuotientVolumeEqVolumePreimage Î¼]

local notation "Ï€" => @QuotientGroup.mk G _ Î“

-- more beautiful theorem: if you have a measure speace downstairs and the downstairs one is smul invariant
-- then fund dom independent

/-- The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and right-
  invariant measure on `G` to is a `G`-invariant measure on `G â§¸ Î“`. -/
-- @[to_additive "The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and
--   right-invariant measure on an additive topological group `G` to a fundamental domain `ğ“•` is a
--   `G`-invariant measure on `G â§¸ Î“`."]
instance MeasureTheory.QuotientVolumeEqVolumePreimage.smulInvariantMeasure_quotient
    [IsMulLeftInvariant (volume : Measure G)] [IsMulRightInvariant (volume : Measure G)]
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G] :
    SMulInvariantMeasure G (G â§¸ Î“) Î¼ where
  measure_preimage_smul g A hA := by
    have meas_Ï€ : Measurable Ï€ := continuous_quotient_mk'.measurable
    have meas_Ï€A : MeasurableSet (Ï€ â»Â¹' A) := measurableSet_preimage meas_Ï€ hA
    obtain âŸ¨ğ“•, hğ“•, meas_ğ“•âŸ© := hasFun.has_fundamental_domain_characterization
    have meas_gğ“• : MeasurableSet (g â€¢ ğ“•)
    Â· rw [â† preimage_smul_inv]
      exact (@measurable_const_smul G G _ _ _ _ (gâ»Â¹)) meas_ğ“•
    have hğ“•_translate_fundom : IsFundamentalDomain (Subgroup.opposite Î“) (g â€¢ ğ“•) volume :=
      hğ“•.smul_of_comm g
    rw [projection_respects_measure hğ“• meas_ğ“•
      (meas_Ï€ (measurableSet_preimage (measurable_const_smul g) hA)),
      projection_respects_measure hğ“•_translate_fundom meas_gğ“• hA]
    change volume ((Ï€ â»Â¹' _) âˆ© _) = _
    set Ï€_preA := Ï€ â»Â¹' A
    have : Ï€ â»Â¹' ((fun x : G â§¸ Î“ => g â€¢ x) â»Â¹' A) = (g * Â·) â»Â¹' Ï€_preA := by ext1; simp
    rw [this]
    have : volume ((g * Â·) â»Â¹' Ï€_preA âˆ© ğ“•) = volume (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•)
    Â· trans volume ((g * Â·) â»Â¹' (Ï€_preA âˆ© (gâ»Â¹ * Â·) â»Â¹' ğ“•))
      Â· rw [preimage_inter]
        congr 2
        simp [Set.preimage]
      rw [measure_preimage_mul]
    rw [this, â† preimage_smul_inv]; rfl

-- not clear why this restatement is needed
instance [SigmaFinite (volume : Measure G)] [IsMulRightInvariant (volume : Measure G)]
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G]
    (Î¼ : Measure (G â§¸ Î“)) [QuotientVolumeEqVolumePreimage Î¼] :
    SigmaFinite Î¼ :=
  instSigmaFiniteQuotientOrbitRelInstMeasurableSpaceToMeasurableSpace Î¼

/-- Given a subgroup `Î“` of a topological group `G` with right-invariant measure `volume`, with a
  measure 'Î¼' on the quotient `G â§¸ Î“` satisfying `QuotientVolumeEqVolumePreimage`, the restriction
  of `volume` to a fundamental domain is measure-preserving with respect to `Î¼`. -/
theorem measurePreserving_quotientGroup_mk_of_quotientVolumeEqVolumePreimage
    [IsMulRightInvariant (volume : Measure G)]
    {ğ“• : Set G} (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•)
    (meas_ğ“• : MeasurableSet ğ“•) (Î¼ : Measure (G â§¸ Î“))
    [QuotientVolumeEqVolumePreimage Î¼] :
    MeasurePreserving (@QuotientGroup.mk G _ Î“) (volume.restrict ğ“•) Î¼ :=
  measurePreserving_quotient_mk_of_quotientVolumeEqVolumePreimage hğ“• meas_ğ“• Î¼

/-- The quotient measure is finite, assuming the covolume is finite -/
theorem MeasureTheory.QuotientVolumeEqVolumePreimage.Finite_quotient
    [IsMulRightInvariant (volume : Measure G)]
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G] (h : hasFun.covolume â‰  âŠ¤) :
    IsFiniteMeasure Î¼ := by
  obtain âŸ¨ğ“•, hğ“•, meas_ğ“•âŸ© := hasFun.has_fundamental_domain_characterization
  rw [QuotientVolumeEqVolumePreimage.eq_quotientMeasure hğ“• meas_ğ“• Î¼,
    meas_ğ“•.quotientMeasure_eq_map_restrict]
  have : Fact (volume ğ“• < âŠ¤) := by
    apply Fact.mk
    convert Ne.lt_top h
    rw [hğ“•.covolume_eq_volume meas_ğ“•]
  exact inferInstance

/-- The covolume is finite, assuming the quotient measure is finite. -/
theorem MeasureTheory.QuotientVolumeEqVolumePreimage.finite_covolume
    [IsMulRightInvariant (volume : Measure G)]
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G] [IsFiniteMeasure Î¼] :
    hasFun.covolume â‰  âŠ¤ := by
  obtain âŸ¨ğ“•, hğ“•, meas_ğ“•âŸ© := hasFun.has_fundamental_domain_characterization
  have H : Î¼ univ â‰  âŠ¤ := measure_ne_top Î¼ univ
  rw [QuotientVolumeEqVolumePreimage.eq_quotientMeasure hğ“• meas_ğ“• Î¼,
    meas_ğ“•.quotientMeasure_apply _ MeasurableSet.univ] at H
  simpa [hğ“•.covolume_eq_volume meas_ğ“•] using H

end smulInvariantMeasure

section normal

variable {G : Type _} [Group G] [MeasureSpace G] [TopologicalSpace G] [TopologicalGroup G]
  [BorelSpace G] [PolishSpace G]
  {Î“ : Subgroup G} [Countable Î“] [Subgroup.Normal Î“]
  [T2Space (G â§¸ Î“)]
  [SecondCountableTopology (G â§¸ Î“)] {Î¼ : Measure (G â§¸ Î“)}

section mulInvariantMeasure

variable
  [IsMulLeftInvariant (volume : Measure G)] [IsMulRightInvariant (volume : Measure G)]
  [SigmaFinite (volume : Measure G)]

/-- Assuming `Î“` is a normal subgroup of a topological group `G`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of a both left- and right-invariant measure on `G` to a
  fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`. -/
-- @[to_additive "Assuming `Î“` is a normal subgroup of an additive topological group `G`, the
--   pushforward to the quotient group `G â§¸ Î“` of the restriction of a both left- and right-invariant
--   measure on `G` to a fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`."]
instance MeasureTheory.QuotientVolumeEqVolumePreimage.MulInvariantMeasure_quotient
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G] [QuotientVolumeEqVolumePreimage Î¼] :
    Î¼.IsMulLeftInvariant where
  map_mul_left_eq_self x := by
    apply Measure.ext
    intro A hA
    obtain âŸ¨xâ‚, hâŸ© := @Quotient.exists_rep _ (QuotientGroup.leftRel Î“) x
    convert measure_preimage_smul xâ‚ Î¼ A using 1
    rw [â† h, Measure.map_apply (measurable_const_mul _) hA]
    rfl

variable [IsMulLeftInvariant Î¼] [SigmaFinite Î¼]
  -- Note: couldn't get uniqueness without sigma finiteness

local notation "Ï€" => @QuotientGroup.mk G _ Î“

/-- Assume that a measure `Î¼` is `IsMulLeftInvariant`, that the action of `Î“` on `G` has a
measurable fundamental domain `s` with positive finite volume, and that there is a single measurable
set `V âŠ† G â§¸ Î“` along which the pullback of `Î¼` and `volume` agree (so the scaling is right). Then
`Î¼` satisfies `QuotientVolumeEqVolumePreimage`. The main tool of the proof is the uniqueness of left
invariant measures, if normalized by a single positive finite-measured set. -/
theorem MeasureTheory.Measure.IsMulLeftInvariant.QuotientVolumeEqVolumePreimage_ofSet
    {s : Set G} (fund_dom_s : IsFundamentalDomain (Subgroup.opposite Î“) s)
    (meas_s : MeasurableSet s)
    {V : Set (G â§¸ Î“)} (meas_V : MeasurableSet V) (neZeroV : Î¼ V â‰  0)
    (hV : Î¼ V = volume (Ï€ â»Â¹' V âˆ© s)) (neTopV : Î¼ V â‰  âŠ¤) : QuotientVolumeEqVolumePreimage Î¼ := by
  apply fund_dom_s.QuotientVolumeEqVolumePreimage meas_s
  intro U meas_U
  let Î¼' : Measure (G â§¸ Î“) := meas_s.quotientMeasure (Subgroup.opposite Î“) volume
  haveI has_fund : HasFundamentalDomain (Subgroup.opposite Î“) G := âŸ¨âŸ¨s, fund_dom_s, meas_sâŸ©âŸ©
  have : QuotientVolumeEqVolumePreimage Î¼' :=
    fund_dom_s.QuotientVolumeEqVolumePreimage_quotientMeasure meas_s
  have : Î¼'.IsMulLeftInvariant :=
    MeasureTheory.QuotientVolumeEqVolumePreimage.MulInvariantMeasure_quotient
  suffices : Î¼ = Î¼'
  Â· rw [this, MeasurableSet.quotientMeasure_apply]
    exact meas_U
  Â· rw [measure_eq_div_smul Î¼' Î¼ meas_V neZeroV neTopV, hV]
    symm
    convert one_smul ENNReal Î¼
    rw [meas_s.quotientMeasure_apply _ meas_V]
    convert ENNReal.div_self ..
    Â· exact trans hV.symm neZeroV
    Â· exact trans hV.symm neTopV

-- HM, 8 Sep: better to phrase this using `[IsFiniteMeasure Î¼]` than using `hasFun.covolume â‰  âŠ¤`?
/-- If a measure `Î¼` is left-invariant and satisfies the right scaling condition, then it
  satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem MeasureTheory.LeftInvariantIsQuotientVolumeEqVolumePreimage
    [hasFun : HasFundamentalDomain (Subgroup.opposite Î“) G]
    (h : hasFun.covolume = Î¼ univ) (finiteCovol : hasFun.covolume â‰  âŠ¤) :
    QuotientVolumeEqVolumePreimage Î¼ := by
  obtain âŸ¨s, fund_dom_s, meas_sâŸ© := hasFun.has_fundamental_domain_characterization
  rw [fund_dom_s.covolume_eq_volume meas_s] at finiteCovol h
  by_cases meas_s_ne_zero : volume s = 0
  Â· convert fund_dom_s.QuotientVolumeEqVolumePreimage_of_volume_zero meas_s meas_s_ne_zero
    rw [â† @measure_univ_eq_zero, â†h, meas_s_ne_zero]
  apply IsMulLeftInvariant.QuotientVolumeEqVolumePreimage_ofSet (fund_dom_s := fund_dom_s)
    (meas_s := meas_s) (meas_V := MeasurableSet.univ)
  Â· rw [â† h]
    exact meas_s_ne_zero
  Â· rw [â† h]
    simp
  Â· rw [â† h]
    convert finiteCovol

end mulInvariantMeasure

section haarMeasure

variable [SigmaFinite (volume : Measure G)] [IsHaarMeasure (volume : Measure G)]
  [IsMulRightInvariant (volume : Measure G)]

variable (K : PositiveCompacts (G â§¸ Î“))

-- HM, 8 September: delete this one since it follows easily from the infrastructure
variable (Î¼) in
/-- Given a normal cofinite subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is
  also right-invariant, and a measure `Î¼` on `G â§¸ Î“` which is compatible under the quotient map
  with the volume on `G`, that measure `Î¼` is a multiple of Haar measure on `G â§¸ Î“`. -/
theorem MeasureTheory.QuotientVolumeEqVolumePreimage.quotient_is_haar
    [QuotientVolumeEqVolumePreimage Î¼] [HasFundamentalDomain (Subgroup.opposite Î“) G] :
    Î¼ = Î¼ K â€¢ haarMeasure K :=
  haarMeasure_unique _ _

local notation "Ï€" => @QuotientGroup.mk G _ Î“

-- note: is `IsFiniteMeasure` necessary?
instance [LocallyCompactSpace G] [QuotientVolumeEqVolumePreimage Î¼]
    [i : HasFundamentalDomain (Subgroup.opposite Î“) G] [IsFiniteMeasure Î¼] :
    IsHaarMeasure Î¼ := by
  obtain âŸ¨KâŸ© := PositiveCompacts.nonempty' (Î± := G)
  let K' : PositiveCompacts (G â§¸ Î“) := K.map Ï€ continuous_coinduced_rng (QuotientGroup.isOpenMap_coe Î“)
  rw [MeasureTheory.QuotientVolumeEqVolumePreimage.quotient_is_haar Î¼ K']
  have finiteCovol : i.covolume â‰  âŠ¤ :=
    MeasureTheory.QuotientVolumeEqVolumePreimage.finite_covolume (Î¼ := Î¼)
  obtain âŸ¨s, fund_dom_s, meas_sâŸ© := i
  rw [fund_dom_s.covolume_eq_volume meas_s] at finiteCovol
  rw [projection_respects_measure fund_dom_s meas_s K'.isCompact.measurableSet]
  apply IsHaarMeasure.smul
  Â· intro h
    haveI i' : IsOpenPosMeasure (volume : Measure G) := inferInstance
    apply IsOpenPosMeasure.open_pos (interior K) (Î¼ := volume) (self := i')
    Â· exact isOpen_interior
    Â· exact K.interior_nonempty
    rw [â† le_zero_iff, â† fund_dom_s.measure_zero_of_invariant _ (fun g â†¦ QuotientGroup.sound _ _) h]
    apply measure_mono
    refine interior_subset.trans ?_
    show (K : Set G) âŠ† Ï€ â»Â¹' (Ï€ '' K)
    exact subset_preimage_image Ï€ K
  Â· show volume (Ï€ â»Â¹' (Ï€ '' K) âˆ© s) â‰  âŠ¤
    apply ne_of_lt
    refine lt_of_le_of_lt ?_ finiteCovol.lt_top
    apply measure_mono
    exact inter_subset_right _ s

--- 7/21/23
-- Need a lemma about our magic typeclass:
-- Lemma: behavior under scaling



/- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem IsFundamentalDomain.QuotientVolumeEqVolumePreimage_HaarMeasure
    {ğ“• : Set G} (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•)
    (meas_ğ“• : MeasurableSet ğ“•)
    [IsMulLeftInvariant Î¼] [SigmaFinite Î¼]
    {V : Set (G â§¸ Î“)} (hV : (interior V).Nonempty) (meas_V : MeasurableSet V)
    (hÎ¼K : Î¼ V = volume ((Ï€ â»Â¹' V) âˆ© ğ“•)) (neTopV : Î¼ V â‰  âŠ¤) :
    QuotientVolumeEqVolumePreimage Î¼ := by
  apply IsMulLeftInvariant.QuotientVolumeEqVolumePreimage_ofSet (fund_dom_s := hğ“•)
    (meas_s := meas_ğ“•) (meas_V := meas_V)
  Â· rw [hÎ¼K]
    intro c_eq_zero
    apply IsOpenPosMeasure.open_pos (interior (Ï€ â»Â¹' V)) (Î¼ := volume)
    Â· simp
    Â· apply Set.Nonempty.mono (preimage_interior_subset_interior_preimage continuous_coinduced_rng)
      apply hV.preimage'
      simp
    Â· apply measure_mono_null (h := interior_subset)
      apply hğ“•.measure_zero_of_invariant (ht := fun g â†¦ QuotientGroup.sound _ _)
      exact c_eq_zero
  Â· exact hÎ¼K
  Â· exact neTopV

/- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“`,
  properly normalized, satisfies `QuotientVolumeEqVolumePreimage`. -/
theorem IsFundamentalDomain.QuotientVolumeEqVolumePreimage_bubHaarMeasure
    {ğ“• : Set G} (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•)
    (meas_ğ“• : MeasurableSet ğ“•) (hğ“•_finite : volume ğ“• â‰  âŠ¤) :
    QuotientVolumeEqVolumePreimage
      ((volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)) â€¢ haarMeasure K) := by
  set c := volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)
  have c_ne_top : c â‰  âŠ¤
  Â· contrapose! hğ“•_finite
    have : volume (Ï€ â»Â¹' â†‘K âˆ© ğ“•) â‰¤ volume ğ“• := measure_mono (Set.inter_subset_right _ _)
    rw [hğ“•_finite] at this
    exact top_unique this
  set Î¼ := c â€¢ haarMeasure K
  have hÎ¼K : Î¼ K = c := by simp [haarMeasure_self]
  haveI : SigmaFinite Î¼ := by
    clear_value c
    lift c to NNReal using c_ne_top
    exact SMul.sigmaFinite c
  apply IsFundamentalDomain.QuotientVolumeEqVolumePreimage_HaarMeasure (hğ“• := hğ“•)
    (meas_ğ“• := meas_ğ“•) (meas_V := K.isCompact.measurableSet) (Î¼ := Î¼)
  Â· exact K.interior_nonempty
  Â· exact hÎ¼K
  Â· rw [hÎ¼K]
    exact c_ne_top

-- HM, 8 September: delete this one since it follows easily from the infrastructure
/- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“` is
  measure-preserving between appropriate multiples of Haar measure on `G` and `G â§¸ Î“`. -/
theorem MeasurePreserving_QuotientGroup.TestCase2
    {ğ“• : Set G} (hğ“• : IsFundamentalDomain (Subgroup.opposite Î“) ğ“•)
    (meas_ğ“• : MeasurableSet ğ“•) (hğ“•_finite : volume ğ“• â‰  âŠ¤) :
    MeasurePreserving Ï€ (volume.restrict ğ“•)
      ((volume ((Ï€ â»Â¹' (K : Set (G â§¸ Î“))) âˆ© ğ“•)) â€¢ haarMeasure K) := by
  haveI := IsFundamentalDomain.QuotientVolumeEqVolumePreimage_bubHaarMeasure
    K hğ“• meas_ğ“• hğ“•_finite
  apply measurePreserving_quotientGroup_mk_of_quotientVolumeEqVolumePreimage hğ“• meas_ğ“•

end haarMeasure

end normal
