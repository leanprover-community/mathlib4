/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Mathlib.Analysis.Normed.Module.FiniteDimension
import Mathlib.MeasureTheory.Measure.Haar.Unique

/-!
# Pushing a Haar measure by a linear map

We show that the push-forward of an additive Haar measure in a vector space under a surjective
linear map is proportional to the Haar measure on the target space,
in `LinearMap.exists_map_addHaar_eq_smul_addHaar`.

We deduce disintegration properties of the Haar measure: to check that a property is true ae,
it suffices to check that it is true ae along all translates of a given vector subspace.
See `MeasureTheory.ae_mem_of_ae_add_linearMap_mem`.

TODO: this holds more generally in any locally compact group, see
[Fremlin, *Measure Theory* (volume 4, 443Q)][fremlin_vol4]
-/

open MeasureTheory Measure Set

open scoped ENNReal

variable {ğ•œ E F : Type*}
  [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
  [NormedAddCommGroup E] [MeasurableSpace E] [BorelSpace E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [MeasurableSpace F] [BorelSpace F] [NormedSpace ğ•œ F] {L : E â†’â‚—[ğ•œ] F}
  {Î¼ : Measure E} {Î½ : Measure F}
  [IsAddHaarMeasure Î¼] [IsAddHaarMeasure Î½]

variable [LocallyCompactSpace E]
variable (L Î¼ Î½)

/-- The image of an additive Haar measure under a surjective linear map is proportional to a given
additive Haar measure. The proportionality factor will be infinite if the linear map has a
nontrivial kernel. -/
theorem LinearMap.exists_map_addHaar_eq_smul_addHaar' (h : Function.Surjective L) :
    âˆƒ (c : â„â‰¥0âˆ), 0 < c âˆ§ c < âˆ âˆ§ Î¼.map L = (c * addHaar (univ : Set (LinearMap.ker L))) â€¢ Î½ := by
  /- This is true for the second projection in product spaces, as the projection of the Haar
  measure `Î¼S.prod Î¼T` is equal to the Haar measure `Î¼T` multiplied by the total mass of `Î¼S`. This
  is also true for linear equivalences, as they map Haar measure to Haar measure. The general case
  follows from these two and linear algebra, as `L` can be interpreted as the composition of the
  projection `P` on a complement `T` to its kernel `S`, together with a linear equivalence. -/
  have : FiniteDimensional ğ•œ E := .of_locallyCompactSpace ğ•œ
  have : ProperSpace F := by
    rcases subsingleton_or_nontrivial E with hE|hE
    Â· have : Subsingleton F := Function.Surjective.subsingleton h
      infer_instance
    Â· have : ProperSpace ğ•œ := .of_locallyCompact_module ğ•œ E
      have : FiniteDimensional ğ•œ F := Module.Finite.of_surjective L h
      exact FiniteDimensional.proper ğ•œ F
  let S : Submodule ğ•œ E := LinearMap.ker L
  obtain âŸ¨T, hTâŸ© : âˆƒ T : Submodule ğ•œ E, IsCompl S T := Submodule.exists_isCompl S
  let M : (S Ã— T) â‰ƒâ‚—[ğ•œ] E := Submodule.prodEquivOfIsCompl S T hT
  have M_cont : Continuous M.symm := LinearMap.continuous_of_finiteDimensional _
  let P : S Ã— T â†’â‚—[ğ•œ] T := LinearMap.snd ğ•œ S T
  have P_cont : Continuous P := LinearMap.continuous_of_finiteDimensional _
  have I : Function.Bijective (LinearMap.domRestrict L T) :=
    âŸ¨LinearMap.injective_domRestrict_iff.2 (IsCompl.inf_eq_bot hT.symm),
    (LinearMap.surjective_domRestrict_iff h).2 hT.symm.sup_eq_topâŸ©
  let L' : T â‰ƒâ‚—[ğ•œ] F := LinearEquiv.ofBijective (LinearMap.domRestrict L T) I
  have L'_cont : Continuous L' := LinearMap.continuous_of_finiteDimensional _
  have A : L = (L' : T â†’â‚—[ğ•œ] F).comp (P.comp (M.symm : E â†’â‚—[ğ•œ] (S Ã— T))) := by
    ext x
    obtain âŸ¨y, z, hyzâŸ© : âˆƒ (y : S) (z : T), M.symm x = (y, z) := âŸ¨_, _, rflâŸ©
    have : x = M (y, z) := by
      rw [â† hyz]; simp only [LinearEquiv.apply_symm_apply]
    simp [L', P, M, this]
  have I : Î¼.map L = ((Î¼.map M.symm).map P).map L' := by
    rw [Measure.map_map, Measure.map_map, A]
    Â· rfl
    Â· exact L'_cont.measurable.comp P_cont.measurable
    Â· exact M_cont.measurable
    Â· exact L'_cont.measurable
    Â· exact P_cont.measurable
  let Î¼S : Measure S := addHaar
  let Î¼T : Measure T := addHaar
  obtain âŸ¨câ‚€, câ‚€_pos, câ‚€_fin, hâ‚€âŸ© :
      âˆƒ câ‚€ : â„â‰¥0âˆ, câ‚€ â‰  0 âˆ§ câ‚€ â‰  âˆ âˆ§ Î¼.map M.symm = câ‚€ â€¢ Î¼S.prod Î¼T := by
    have : IsAddHaarMeasure (Î¼.map M.symm) :=
      M.toContinuousLinearEquiv.symm.isAddHaarMeasure_map Î¼
    refine âŸ¨addHaarScalarFactor (Î¼.map M.symm) (Î¼S.prod Î¼T), ?_, ENNReal.coe_ne_top,
      isAddLeftInvariant_eq_smul _ _âŸ©
    simpa only [ne_eq, ENNReal.coe_eq_zero] using
      (addHaarScalarFactor_pos_of_isAddHaarMeasure (Î¼.map M.symm) (Î¼S.prod Î¼T)).ne'
  have J : (Î¼S.prod Î¼T).map P = (Î¼S univ) â€¢ Î¼T := map_snd_prod
  obtain âŸ¨câ‚, câ‚_pos, câ‚_fin, hâ‚âŸ© : âˆƒ câ‚ : â„â‰¥0âˆ, câ‚ â‰  0 âˆ§ câ‚ â‰  âˆ âˆ§ Î¼T.map L' = câ‚ â€¢ Î½ := by
    have : IsAddHaarMeasure (Î¼T.map L') :=
      L'.toContinuousLinearEquiv.isAddHaarMeasure_map Î¼T
    refine âŸ¨addHaarScalarFactor (Î¼T.map L') Î½, ?_, ENNReal.coe_ne_top,
      isAddLeftInvariant_eq_smul _ _âŸ©
    simpa only [ne_eq, ENNReal.coe_eq_zero] using
      (addHaarScalarFactor_pos_of_isAddHaarMeasure (Î¼T.map L') Î½).ne'
  refine âŸ¨câ‚€ * câ‚, by simp [pos_iff_ne_zero, câ‚€_pos, câ‚_pos],
    ENNReal.mul_lt_top câ‚€_fin.lt_top câ‚_fin.lt_top, ?_âŸ©
  simp only [I, hâ‚€, Measure.map_smul, J, smul_smul, hâ‚]
  rw [mul_assoc, mul_comm _ câ‚, â† mul_assoc]

/-- The image of an additive Haar measure under a surjective linear map is proportional to a given
additive Haar measure, with a positive (but maybe infinite) factor. -/
theorem LinearMap.exists_map_addHaar_eq_smul_addHaar (h : Function.Surjective L) :
    âˆƒ (c : â„â‰¥0âˆ), 0 < c âˆ§ Î¼.map L = c â€¢ Î½ := by
  rcases L.exists_map_addHaar_eq_smul_addHaar' Î¼ Î½ h with âŸ¨c, c_pos, -, hcâŸ©
  exact âŸ¨_, by simp [c_pos, NeZero.ne addHaar], hcâŸ©

namespace MeasureTheory

/-- Given a surjective linear map `L`, it is equivalent to require a property almost everywhere
in the source or the target spaces of `L`, with respect to additive Haar measures there. -/
lemma ae_comp_linearMap_mem_iff (h : Function.Surjective L) {s : Set F} (hs : MeasurableSet s) :
    (âˆ€áµ x âˆ‚Î¼, L x âˆˆ s) â†” âˆ€áµ y âˆ‚Î½, y âˆˆ s := by
  have : FiniteDimensional ğ•œ E := .of_locallyCompactSpace ğ•œ
  have : AEMeasurable L Î¼ := L.continuous_of_finiteDimensional.aemeasurable
  apply (ae_map_iff this hs).symm.trans
  rcases L.exists_map_addHaar_eq_smul_addHaar Î¼ Î½ h with âŸ¨c, c_pos, hcâŸ©
  rw [hc]
  exact ae_smul_measure_iff c_pos.ne'

/-- Given a linear map `L : E â†’ F`, a property holds almost everywhere in `F` if and only if,
almost everywhere in `F`, it holds almost everywhere along the subspace spanned by the
image of `L`. This is an instance of a disintegration argument for additive Haar measures. -/
lemma ae_ae_add_linearMap_mem_iff [LocallyCompactSpace F] {s : Set F} (hs : MeasurableSet s) :
    (âˆ€áµ y âˆ‚Î½, âˆ€áµ x âˆ‚Î¼, y + L x âˆˆ s) â†” âˆ€áµ y âˆ‚Î½, y âˆˆ s := by
  have : FiniteDimensional ğ•œ E := .of_locallyCompactSpace ğ•œ
  have : FiniteDimensional ğ•œ F := .of_locallyCompactSpace ğ•œ
  have : ProperSpace E := .of_locallyCompactSpace ğ•œ
  have : ProperSpace F := .of_locallyCompactSpace ğ•œ
  let M : F Ã— E â†’â‚—[ğ•œ] F := LinearMap.id.coprod L
  have M_cont : Continuous M := M.continuous_of_finiteDimensional
  -- Note: https://github.com/leanprover-community/mathlib4/pull/8386 had to change `range_eq_top` into
  -- `range_eq_top (f := _)`
  have hM : Function.Surjective M := by
    simp [M, â† LinearMap.range_eq_top (f := _), LinearMap.range_coprod]
  have A : âˆ€ x, M x âˆˆ s â†” x âˆˆ M â»Â¹' s := fun x â†¦ Iff.rfl
  simp_rw [â† ae_comp_linearMap_mem_iff M (Î½.prod Î¼) Î½ hM hs, A]
  rw [Measure.ae_prod_mem_iff_ae_ae_mem]
  Â· simp only [M, mem_preimage, LinearMap.coprod_apply, LinearMap.id_coe, id_eq]
  Â· exact M_cont.measurable hs

/-- To check that a property holds almost everywhere with respect to an additive Haar measure, it
suffices to check it almost everywhere along all translates of a given vector subspace. This is an
instance of a disintegration argument for additive Haar measures. -/
lemma ae_mem_of_ae_add_linearMap_mem [LocallyCompactSpace F] {s : Set F} (hs : MeasurableSet s)
    (h : âˆ€ y, âˆ€áµ x âˆ‚Î¼, y + L x âˆˆ s) : âˆ€áµ y âˆ‚Î½, y âˆˆ s :=
  (ae_ae_add_linearMap_mem_iff L Î¼ Î½ hs).1 (Filter.Eventually.of_forall h)

end MeasureTheory
