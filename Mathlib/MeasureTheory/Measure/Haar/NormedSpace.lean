/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Measure.Haar.InnerProductSpace
import Mathlib.MeasureTheory.Measure.Lebesgue.EqHaar
import Mathlib.MeasureTheory.Integral.SetIntegral

#align_import measure_theory.measure.haar.normed_space from "leanprover-community/mathlib"@"b84aee748341da06a6d78491367e2c0e9f15e8a5"

/-!
# Basic properties of Haar measures on real vector spaces

-/

noncomputable section

open scoped NNReal ENNReal Pointwise Topology

open Inv Set Function MeasureTheory.Measure Filter

open FiniteDimensional

namespace MeasureTheory

namespace Measure

/- The instance `MeasureTheory.Measure.IsAddHaarMeasure.noAtoms` applies in particular to show that
an additive Haar measure on a nontrivial finite-dimensional real vector space has no atom. -/
example {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [Nontrivial E] [FiniteDimensional â„ E]
    [MeasurableSpace E] [BorelSpace E] (Î¼ : Measure E) [IsAddHaarMeasure Î¼] : NoAtoms Î¼ := by
  infer_instance

section ContinuousLinearEquiv

variable {ğ•œ G H : Type*} [MeasurableSpace G] [MeasurableSpace H] [NontriviallyNormedField ğ•œ]
  [TopologicalSpace G] [TopologicalSpace H] [AddCommGroup G] [AddCommGroup H]
  [TopologicalAddGroup G] [TopologicalAddGroup H] [Module ğ•œ G] [Module ğ•œ H] (Î¼ : Measure G)
  [IsAddHaarMeasure Î¼] [BorelSpace G] [BorelSpace H] [T2Space H]

instance MapContinuousLinearEquiv.isAddHaarMeasure (e : G â‰ƒL[ğ•œ] H) : IsAddHaarMeasure (Î¼.map e) :=
  e.toAddEquiv.isAddHaarMeasure_map _ e.continuous e.symm.continuous
#align measure_theory.measure.map_continuous_linear_equiv.is_add_haar_measure MeasureTheory.Measure.MapContinuousLinearEquiv.isAddHaarMeasure

variable [CompleteSpace ğ•œ] [T2Space G] [FiniteDimensional ğ•œ G] [ContinuousSMul ğ•œ G]
  [ContinuousSMul ğ•œ H]

instance MapLinearEquiv.isAddHaarMeasure (e : G â‰ƒâ‚—[ğ•œ] H) : IsAddHaarMeasure (Î¼.map e) :=
  MapContinuousLinearEquiv.isAddHaarMeasure _ e.toContinuousLinearEquiv
#align measure_theory.measure.map_linear_equiv.is_add_haar_measure MeasureTheory.Measure.MapLinearEquiv.isAddHaarMeasure

end ContinuousLinearEquiv

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [MeasurableSpace E] [BorelSpace E]
  [FiniteDimensional â„ E] (Î¼ : Measure E) [IsAddHaarMeasure Î¼] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F]

variable {s : Set E}

/-- The integral of `f (R â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
theorem integral_comp_smul (f : E â†’ F) (R : â„) :
    âˆ« x, f (R â€¢ x) âˆ‚Î¼ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x, f x âˆ‚Î¼ := by
  by_cases hF : CompleteSpace F; swap
  Â· simp [integral, hF]
  rcases eq_or_ne R 0 with (rfl | hR)
  Â· simp only [zero_smul, integral_const]
    rcases Nat.eq_zero_or_pos (finrank â„ E) with (hE | hE)
    Â· have : Subsingleton E := finrank_zero_iff.1 hE
      have : f = fun _ => f 0 := by ext x; rw [Subsingleton.elim x 0]
      conv_rhs => rw [this]
      simp only [hE, pow_zero, inv_one, abs_one, one_smul, integral_const]
    Â· have : Nontrivial E := finrank_pos_iff.1 hE
      simp only [zero_pow hE.ne', measure_univ_of_isAddLeftInvariant, ENNReal.top_toReal, zero_smul,
        inv_zero, abs_zero]
  Â· calc
      (âˆ« x, f (R â€¢ x) âˆ‚Î¼) = âˆ« y, f y âˆ‚Measure.map (fun x => R â€¢ x) Î¼ :=
        (integral_map_equiv (Homeomorph.smul (isUnit_iff_ne_zero.2 hR).unit).toMeasurableEquiv
            f).symm
      _ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x, f x âˆ‚Î¼ := by
        simp only [map_addHaar_smul Î¼ hR, integral_smul_measure, ENNReal.toReal_ofReal, abs_nonneg]
#align measure_theory.measure.integral_comp_smul MeasureTheory.Measure.integral_comp_smul

/-- The integral of `f (R â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
theorem integral_comp_smul_of_nonneg (f : E â†’ F) (R : â„) {hR : 0 â‰¤ R} :
    âˆ« x, f (R â€¢ x) âˆ‚Î¼ = (R ^ finrank â„ E)â»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rw [integral_comp_smul Î¼ f R, abs_of_nonneg (inv_nonneg.2 (pow_nonneg hR _))]
#align measure_theory.measure.integral_comp_smul_of_nonneg MeasureTheory.Measure.integral_comp_smul_of_nonneg

/-- The integral of `f (Râ»Â¹ â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
theorem integral_comp_inv_smul (f : E â†’ F) (R : â„) :
    âˆ« x, f (Râ»Â¹ â€¢ x) âˆ‚Î¼ = |R ^ finrank â„ E| â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rw [integral_comp_smul Î¼ f Râ»Â¹, inv_pow, inv_inv]
#align measure_theory.measure.integral_comp_inv_smul MeasureTheory.Measure.integral_comp_inv_smul

/-- The integral of `f (Râ»Â¹ â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
theorem integral_comp_inv_smul_of_nonneg (f : E â†’ F) {R : â„} (hR : 0 â‰¤ R) :
    âˆ« x, f (Râ»Â¹ â€¢ x) âˆ‚Î¼ = R ^ finrank â„ E â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rw [integral_comp_inv_smul Î¼ f R, abs_of_nonneg (pow_nonneg hR _)]
#align measure_theory.measure.integral_comp_inv_smul_of_nonneg MeasureTheory.Measure.integral_comp_inv_smul_of_nonneg

theorem setIntegral_comp_smul (f : E â†’ F) {R : â„} (s : Set E) (hR : R â‰  0) :
    âˆ« x in s, f (R â€¢ x) âˆ‚Î¼ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x in R â€¢ s, f x âˆ‚Î¼ := by
  let e : E â‰ƒáµ E := (Homeomorph.smul (Units.mk0 R hR)).toMeasurableEquiv
  calc
  âˆ« x in s, f (R â€¢ x) âˆ‚Î¼
    = âˆ« x in e â»Â¹' (e.symm â»Â¹' s), f (e x) âˆ‚Î¼ := by simp [â† preimage_comp]; rfl
  _ = âˆ« y in e.symm â»Â¹' s, f y âˆ‚map (fun x â†¦ R â€¢ x) Î¼ := (setIntegral_map_equiv _ _ _).symm
  _ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« y in e.symm â»Â¹' s, f y âˆ‚Î¼ := by
    simp [map_addHaar_smul Î¼ hR, integral_smul_measure, ENNReal.toReal_ofReal, abs_nonneg]
  _ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x in R â€¢ s, f x âˆ‚Î¼ := by
    congr
    ext y
    rw [mem_smul_set_iff_inv_smul_memâ‚€ hR]
    rfl

@[deprecated (since := "2024-04-17")]
alias set_integral_comp_smul := setIntegral_comp_smul

theorem setIntegral_comp_smul_of_pos (f : E â†’ F) {R : â„} (s : Set E) (hR : 0 < R) :
    âˆ« x in s, f (R â€¢ x) âˆ‚Î¼ = (R ^ finrank â„ E)â»Â¹ â€¢ âˆ« x in R â€¢ s, f x âˆ‚Î¼ := by
  rw [setIntegral_comp_smul Î¼ f s hR.ne', abs_of_nonneg (inv_nonneg.2 (pow_nonneg hR.le _))]

@[deprecated (since := "2024-04-17")]
alias set_integral_comp_smul_of_pos := setIntegral_comp_smul_of_pos

theorem integral_comp_mul_left (g : â„ â†’ F) (a : â„) :
    (âˆ« x : â„, g (a * x)) = |aâ»Â¹| â€¢ âˆ« y : â„, g y := by
  simp_rw [â† smul_eq_mul, Measure.integral_comp_smul, FiniteDimensional.finrank_self, pow_one]
#align measure_theory.measure.integral_comp_mul_left MeasureTheory.Measure.integral_comp_mul_left

theorem integral_comp_inv_mul_left (g : â„ â†’ F) (a : â„) :
    (âˆ« x : â„, g (aâ»Â¹ * x)) = |a| â€¢ âˆ« y : â„, g y := by
  simp_rw [â† smul_eq_mul, Measure.integral_comp_inv_smul, FiniteDimensional.finrank_self, pow_one]
#align measure_theory.measure.integral_comp_inv_mul_left MeasureTheory.Measure.integral_comp_inv_mul_left

theorem integral_comp_mul_right (g : â„ â†’ F) (a : â„) :
    (âˆ« x : â„, g (x * a)) = |aâ»Â¹| â€¢ âˆ« y : â„, g y := by
  simpa only [mul_comm] using integral_comp_mul_left g a
#align measure_theory.measure.integral_comp_mul_right MeasureTheory.Measure.integral_comp_mul_right

theorem integral_comp_inv_mul_right (g : â„ â†’ F) (a : â„) :
    (âˆ« x : â„, g (x * aâ»Â¹)) = |a| â€¢ âˆ« y : â„, g y := by
  simpa only [mul_comm] using integral_comp_inv_mul_left g a
#align measure_theory.measure.integral_comp_inv_mul_right MeasureTheory.Measure.integral_comp_inv_mul_right

theorem integral_comp_div (g : â„ â†’ F) (a : â„) : (âˆ« x : â„, g (x / a)) = |a| â€¢ âˆ« y : â„, g y :=
  integral_comp_inv_mul_right g a
#align measure_theory.measure.integral_comp_div MeasureTheory.Measure.integral_comp_div

end Measure

variable {F : Type*} [NormedAddCommGroup F]

theorem integrable_comp_smul_iff {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [MeasurableSpace E] [BorelSpace E] [FiniteDimensional â„ E] (Î¼ : Measure E) [IsAddHaarMeasure Î¼]
    (f : E â†’ F) {R : â„} (hR : R â‰  0) : Integrable (fun x => f (R â€¢ x)) Î¼ â†” Integrable f Î¼ := by
  -- reduce to one-way implication
  suffices
    âˆ€ {g : E â†’ F} (_ : Integrable g Î¼) {S : â„} (_ : S â‰  0), Integrable (fun x => g (S â€¢ x)) Î¼ by
    refine âŸ¨fun hf => ?_, fun hf => this hf hRâŸ©
    convert this hf (inv_ne_zero hR)
    rw [â† mul_smul, mul_inv_cancel hR, one_smul]
  -- now prove
  intro g hg S hS
  let t := ((Homeomorph.smul (isUnit_iff_ne_zero.2 hS).unit).toMeasurableEquiv : E â‰ƒáµ E)
  refine (integrable_map_equiv t g).mp (?_ : Integrable g (map (S â€¢ Â·) Î¼))
  rwa [map_addHaar_smul Î¼ hS, integrable_smul_measure _ ENNReal.ofReal_ne_top]
  simpa only [Ne, ENNReal.ofReal_eq_zero, not_le, abs_pos] using inv_ne_zero (pow_ne_zero _ hS)
#align measure_theory.integrable_comp_smul_iff MeasureTheory.integrable_comp_smul_iff

theorem Integrable.comp_smul {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [MeasurableSpace E] [BorelSpace E] [FiniteDimensional â„ E] {Î¼ : Measure E} [IsAddHaarMeasure Î¼]
    {f : E â†’ F} (hf : Integrable f Î¼) {R : â„} (hR : R â‰  0) : Integrable (fun x => f (R â€¢ x)) Î¼ :=
  (integrable_comp_smul_iff Î¼ f hR).2 hf
#align measure_theory.integrable.comp_smul MeasureTheory.Integrable.comp_smul

theorem integrable_comp_mul_left_iff (g : â„ â†’ F) {R : â„} (hR : R â‰  0) :
    (Integrable fun x => g (R * x)) â†” Integrable g := by
  simpa only [smul_eq_mul] using integrable_comp_smul_iff volume g hR
#align measure_theory.integrable_comp_mul_left_iff MeasureTheory.integrable_comp_mul_left_iff

theorem Integrable.comp_mul_left' {g : â„ â†’ F} (hg : Integrable g) {R : â„} (hR : R â‰  0) :
    Integrable fun x => g (R * x) :=
  (integrable_comp_mul_left_iff g hR).2 hg
#align measure_theory.integrable.comp_mul_left' MeasureTheory.Integrable.comp_mul_left'

theorem integrable_comp_mul_right_iff (g : â„ â†’ F) {R : â„} (hR : R â‰  0) :
    (Integrable fun x => g (x * R)) â†” Integrable g := by
  simpa only [mul_comm] using integrable_comp_mul_left_iff g hR
#align measure_theory.integrable_comp_mul_right_iff MeasureTheory.integrable_comp_mul_right_iff

theorem Integrable.comp_mul_right' {g : â„ â†’ F} (hg : Integrable g) {R : â„} (hR : R â‰  0) :
    Integrable fun x => g (x * R) :=
  (integrable_comp_mul_right_iff g hR).2 hg
#align measure_theory.integrable.comp_mul_right' MeasureTheory.Integrable.comp_mul_right'

theorem integrable_comp_div_iff (g : â„ â†’ F) {R : â„} (hR : R â‰  0) :
    (Integrable fun x => g (x / R)) â†” Integrable g :=
  integrable_comp_mul_right_iff g (inv_ne_zero hR)
#align measure_theory.integrable_comp_div_iff MeasureTheory.integrable_comp_div_iff

theorem Integrable.comp_div {g : â„ â†’ F} (hg : Integrable g) {R : â„} (hR : R â‰  0) :
    Integrable fun x => g (x / R) :=
  (integrable_comp_div_iff g hR).2 hg
#align measure_theory.integrable.comp_div MeasureTheory.Integrable.comp_div

section InnerProductSpace

variable {E' F' A : Type*}
variable [NormedAddCommGroup E'] [InnerProductSpace â„ E'] [FiniteDimensional â„ E']
  [MeasurableSpace E'] [BorelSpace E']
variable [NormedAddCommGroup F'] [InnerProductSpace â„ F'] [FiniteDimensional â„ F']
  [MeasurableSpace F'] [BorelSpace F']

variable (f : E' â‰ƒâ‚—áµ¢[â„] F')
variable [NormedAddCommGroup A] [NormedSpace â„ A]

theorem integrable_comp (g : F' â†’ A) : Integrable (g âˆ˜ f) â†” Integrable g :=
  f.measurePreserving.integrable_comp_emb f.toMeasureEquiv.measurableEmbedding

theorem integral_comp (g : F' â†’ A) : âˆ« (x : E'), g (f x) = âˆ« (y : F'), g y :=
  f.measurePreserving.integral_comp' (f := f.toMeasureEquiv) g

end InnerProductSpace

end MeasureTheory
