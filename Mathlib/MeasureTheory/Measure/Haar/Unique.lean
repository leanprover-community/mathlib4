/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.MeasureTheory.Function.LocallyIntegrable
public import Mathlib.MeasureTheory.Group.Integral
public import Mathlib.MeasureTheory.Integral.Prod
public import Mathlib.MeasureTheory.Integral.Bochner.Set
public import Mathlib.MeasureTheory.Measure.EverywherePos
public import Mathlib.MeasureTheory.Measure.Haar.Basic
public import Mathlib.Topology.Metrizable.Urysohn
public import Mathlib.Topology.ContinuousMap.Ordered

/-!
# Uniqueness of Haar measure in locally compact groups

## Main results

In a locally compact group, we prove that two left-invariant measures `Î¼'` and `Î¼` which are finite
on compact sets coincide, up to a normalizing scalar that we denote with `haarScalarFactor Î¼' Î¼`,
in the following sense:
* `integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport`: they give the same value to the
  integral of continuous compactly supported functions, up to a scalar.
* `measure_isMulInvariant_eq_smul_of_isCompact_closure`: they give the same value to sets with
  compact closure, up to a scalar.
* `measure_isHaarMeasure_eq_smul_of_isOpen`: they give the same value to open sets, up to a scalar.

To get genuine equality of measures, we typically need additional regularity assumptions:

* `isMulLeftInvariant_eq_smul_of_innerRegular`: two left invariant measures which are
  inner regular coincide up to a scalar.
* `isMulLeftInvariant_eq_smul_of_regular`: two left invariant measures which are
  regular coincide up to a scalar.
* `isHaarMeasure_eq_smul`: in a second countable space, two Haar measures coincide up to a
  scalar.
* `isMulInvariant_eq_smul_of_compactSpace`: two left-invariant measures on a compact group coincide
  up to a scalar.
* `isHaarMeasure_eq_of_isProbabilityMeasure`: two Haar measures which are probability measures
  coincide exactly.

In general, uniqueness statements for Haar measures in the literature make some assumption of
regularity, either regularity or inner regularity. We have tried to minimize the assumptions in the
theorems above, and cover the different results that exist in the literature.

## Implementation

The first result `integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport` is classical. To prove
it, we use a change of variables to express integrals with respect to a left-invariant measure as
integrals with respect to a given right-invariant measure (with a suitable density function).
The uniqueness readily follows.

Uniqueness results for the measure of compact sets and open sets, without any regularity assumption,
are significantly harder. They rely on the completion-regularity of the standard regular Haar
measure. We follow McQuillan's answer at https://mathoverflow.net/questions/456670/.

On second-countable groups, one can arrive to slightly different uniqueness results by using that
the operations are measurable. In particular, one can get uniqueness assuming Ïƒ-finiteness of
the measures but discarding the assumption that they are finite on compact sets. See
`haarMeasure_unique` in the file `Mathlib/MeasureTheory/Measure/Haar/Basic.lean`.

## References

[Halmos, Measure Theory][halmos1950measure]
[Fremlin, *Measure Theory* (volume 4)][fremlin_vol4]
-/

@[expose] public section

open Filter Set TopologicalSpace Function MeasureTheory Measure
open scoped Uniformity Topology ENNReal Pointwise NNReal

namespace MeasureTheory

/-- The parameterized integral `x â†¦ âˆ« y, g (yâ»Â¹ * x) âˆ‚Î¼` depends continuously on `y` when `g` is a
compactly supported continuous function on a topological group `G`, and `Î¼` is finite on compact
sets. -/
@[to_additive]
lemma continuous_integral_apply_inv_mul
    {G : Type*} [TopologicalSpace G] [LocallyCompactSpace G] [Group G] [IsTopologicalGroup G]
    [MeasurableSpace G] [BorelSpace G]
    {Î¼ : Measure G} [IsFiniteMeasureOnCompacts Î¼] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {g : G â†’ E}
    (hg : Continuous g) (h'g : HasCompactSupport g) :
    Continuous (fun (x : G) â†¦ âˆ« y, g (yâ»Â¹ * x) âˆ‚Î¼) := by
  let k := tsupport g
  have k_comp : IsCompact k := h'g
  apply continuous_iff_continuousAt.2 (fun xâ‚€ â†¦ ?_)
  obtain âŸ¨t, t_comp, htâŸ© : âˆƒ t, IsCompact t âˆ§ t âˆˆ ğ“ xâ‚€ := exists_compact_mem_nhds xâ‚€
  let k' : Set G := t â€¢ kâ»Â¹
  have k'_comp : IsCompact k' := t_comp.smul_set k_comp.inv
  have A : ContinuousOn (fun (x : G) â†¦ âˆ« y, g (yâ»Â¹ * x) âˆ‚Î¼) t := by
    apply continuousOn_integral_of_compact_support k'_comp
    Â· exact (hg.comp (continuous_snd.inv.mul continuous_fst)).continuousOn
    Â· intro p x hp hx
      contrapose! hx
      refine âŸ¨p, hp, pâ»Â¹ * x, ?_, by simpâŸ©
      simpa only [Set.mem_inv, mul_inv_rev, inv_inv] using subset_tsupport _ hx
  exact A.continuousAt ht

namespace Measure

section Group

variable {G : Type*} [TopologicalSpace G] [Group G] [IsTopologicalGroup G]
  [MeasurableSpace G] [BorelSpace G]

/-!
### Uniqueness of integrals of compactly supported functions

Two left invariant measures coincide when integrating continuous compactly supported functions,
up to a scalar that we denote with `haarScalarFactor Î¼' Î¼ `.

This is proved by relating the integral for arbitrary left invariant and right invariant measures,
applying a version of Fubini.
As one may use the same right invariant measure, this shows that two different left invariant
measures will give the same integral, up to some fixed scalar.
-/

/-- In a group with a left invariant measure `Î¼` and a right invariant measure `Î½`, one can express
integrals with respect to `Î¼` as integrals with respect to `Î½` up to a constant scaling factor
(given in the statement as `âˆ« x, g x âˆ‚Î¼` where `g` is a fixed reference function) and an
explicit density `y â†¦ 1/âˆ« z, g (zâ»Â¹ * y) âˆ‚Î½`. -/
@[to_additive]
lemma integral_isMulLeftInvariant_isMulRightInvariant_combo
    {Î¼ Î½ : Measure G} [IsFiniteMeasureOnCompacts Î¼] [IsFiniteMeasureOnCompacts Î½]
    [IsMulLeftInvariant Î¼] [IsMulRightInvariant Î½] [IsOpenPosMeasure Î½]
    {f g : G â†’ â„} (hf : Continuous f) (h'f : HasCompactSupport f)
    (hg : Continuous g) (h'g : HasCompactSupport g) (g_nonneg : 0 â‰¤ g) {xâ‚€ : G} (g_pos : g xâ‚€ â‰  0) :
    âˆ« x, f x âˆ‚Î¼ = (âˆ« y, f y * (âˆ« z, g (zâ»Â¹ * y) âˆ‚Î½)â»Â¹ âˆ‚Î½) * âˆ« x, g x âˆ‚Î¼ := by
  -- The group has to be locally compact, otherwise all integrals vanish and the result is trivial.
  rcases h'f.eq_zero_or_locallyCompactSpace_of_group hf with Hf | Hf
  Â· simp [Hf]
  let D : G â†’ â„ := fun (x : G) â†¦ âˆ« y, g (yâ»Â¹ * x) âˆ‚Î½
  have D_cont : Continuous D := continuous_integral_apply_inv_mul hg h'g
  have D_pos : âˆ€ x, 0 < D x := by
    intro x
    have C : Continuous (fun y â†¦ g (yâ»Â¹ * x)) := hg.comp (continuous_inv.mul continuous_const)
    apply (integral_pos_iff_support_of_nonneg _ _).2
    Â· apply C.isOpen_support.measure_pos Î½
      exact âŸ¨x * xâ‚€â»Â¹, by simpa using g_posâŸ©
    Â· exact fun y â†¦ g_nonneg (yâ»Â¹ * x)
    Â· apply C.integrable_of_hasCompactSupport
      exact h'g.comp_homeomorph ((Homeomorph.inv G).trans (Homeomorph.mulRight x))
  calc
  âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x * (D x)â»Â¹ * D x âˆ‚Î¼ := by
    congr with x; rw [mul_assoc, inv_mul_cancelâ‚€ (D_pos x).ne', mul_one]
  _ = âˆ« x, (âˆ« y, f x * (D x)â»Â¹ * g (yâ»Â¹ * x) âˆ‚Î½) âˆ‚Î¼ := by simp_rw [D, integral_const_mul]
  _ = âˆ« y, (âˆ« x, f x * (D x)â»Â¹ * g (yâ»Â¹ * x) âˆ‚Î¼) âˆ‚Î½ := by
      apply integral_integral_swap_of_hasCompactSupport
      Â· apply Continuous.mul
        Â· exact (hf.comp continuous_fst).mul
            ((D_cont.comp continuous_fst).invâ‚€ (fun x â†¦ (D_pos _).ne'))
        Â· exact hg.comp (continuous_snd.inv.mul continuous_fst)
      Â· let K := tsupport f
        have K_comp : IsCompact K := h'f
        let L := tsupport g
        have L_comp : IsCompact L := h'g
        let M := (fun (p : G Ã— G) â†¦ p.1 * p.2â»Â¹) '' (K Ã—Ë¢ L)
        have M_comp : IsCompact M :=
          (K_comp.prod L_comp).image (continuous_fst.mul continuous_snd.inv)
        have M'_comp : IsCompact (closure M) := M_comp.closure
        have : âˆ€ (p : G Ã— G), p âˆ‰ K Ã—Ë¢ closure M â†’ f p.1 * (D p.1)â»Â¹ * g (p.2â»Â¹ * p.1) = 0 := by
          rintro âŸ¨x, yâŸ© hxy
          by_cases H : x âˆˆ K; swap
          Â· simp [image_eq_zero_of_notMem_tsupport H]
          have : g (yâ»Â¹ * x) = 0 := by
            apply image_eq_zero_of_notMem_tsupport
            contrapose! hxy
            simp only [mem_prod, H, true_and]
            apply subset_closure
            simp only [M, mem_image, mem_prod, Prod.exists]
            exact âŸ¨x, yâ»Â¹ * x, âŸ¨H, hxyâŸ©, by groupâŸ©
          simp [this]
        apply HasCompactSupport.intro' (K_comp.prod M'_comp) ?_ this
        exact (isClosed_tsupport f).prod isClosed_closure
  _ = âˆ« y, (âˆ« x, f (y * x) * (D (y * x))â»Â¹ * g x âˆ‚Î¼) âˆ‚Î½ := by
      congr with y
      rw [â† integral_mul_left_eq_self _ y]
      simp
  _ = âˆ« x, (âˆ« y, f (y * x) * (D (y * x))â»Â¹ * g x âˆ‚Î½) âˆ‚Î¼ := by
      apply (integral_integral_swap_of_hasCompactSupport _ _).symm
      Â· apply Continuous.mul ?_ (hg.comp continuous_fst)
        exact (hf.comp (continuous_snd.mul continuous_fst)).mul
          ((D_cont.comp (continuous_snd.mul continuous_fst)).invâ‚€ (fun x â†¦ (D_pos _).ne'))
      Â· let K := tsupport f
        have K_comp : IsCompact K := h'f
        let L := tsupport g
        have L_comp : IsCompact L := h'g
        let M := (fun (p : G Ã— G) â†¦ p.1 * p.2â»Â¹) '' (K Ã—Ë¢ L)
        have M_comp : IsCompact M :=
          (K_comp.prod L_comp).image (continuous_fst.mul continuous_snd.inv)
        have M'_comp : IsCompact (closure M) := M_comp.closure
        have : âˆ€ (p : G Ã— G), p âˆ‰ L Ã—Ë¢ closure M â†’
            f (p.2 * p.1) * (D (p.2 * p.1))â»Â¹ * g p.1 = 0 := by
          rintro âŸ¨x, yâŸ© hxy
          by_cases H : x âˆˆ L; swap
          Â· simp [image_eq_zero_of_notMem_tsupport H]
          have : f (y * x) = 0 := by
            apply image_eq_zero_of_notMem_tsupport
            contrapose! hxy
            simp only [mem_prod, H, true_and]
            apply subset_closure
            simp only [M, mem_image, mem_prod, Prod.exists]
            exact âŸ¨y * x, x, âŸ¨hxy, HâŸ©, by groupâŸ©
          simp [this]
        apply HasCompactSupport.intro' (L_comp.prod M'_comp) ?_ this
        exact (isClosed_tsupport g).prod isClosed_closure
  _ = âˆ« x, (âˆ« y, f y * (D y)â»Â¹ âˆ‚Î½) * g x âˆ‚Î¼ := by
      simp_rw [integral_mul_const]
      congr with x
      conv_rhs => rw [â† integral_mul_right_eq_self _ x]
  _ = (âˆ« y, f y * (D y)â»Â¹ âˆ‚Î½) * âˆ« x, g x âˆ‚Î¼ := integral_const_mul _ _

/-- Given two left-invariant measures which are finite on
compacts, they coincide in the following sense: they give the same value to the integral of
continuous compactly supported functions, up to a multiplicative constant. -/
@[to_additive exists_integral_isAddLeftInvariant_eq_smul_of_hasCompactSupport]
lemma exists_integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport (Î¼' Î¼ : Measure G)
    [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] :
    âˆƒ (c : â„â‰¥0), âˆ€ (f : G â†’ â„), Continuous f â†’ HasCompactSupport f â†’
      âˆ« x, f x âˆ‚Î¼' = âˆ« x, f x âˆ‚(c â€¢ Î¼) := by
  -- The group has to be locally compact, otherwise all integrals vanish and the result is trivial.
  by_cases H : LocallyCompactSpace G; swap
  Â· refine âŸ¨0, fun f f_cont f_comp â†¦ ?_âŸ©
    rcases f_comp.eq_zero_or_locallyCompactSpace_of_group f_cont with hf | hf
    Â· simp [hf]
    Â· exact (H hf).elim
  -- Fix some nonzero continuous function with compact support `g`.
  obtain âŸ¨âŸ¨g, g_contâŸ©, g_comp, g_nonneg, g_oneâŸ© :
    âˆƒ (g : C(G, â„)), HasCompactSupport g âˆ§ 0 â‰¤ g âˆ§ g 1 â‰  0 := exists_continuous_nonneg_pos 1
  have int_g_pos : 0 < âˆ« x, g x âˆ‚Î¼ :=
    g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one
  -- The proportionality constant we are looking for will be the ratio of the integrals of `g`
  -- with respect to `Î¼'` and `Î¼`.
  let c : â„ := (âˆ« x, g x âˆ‚Î¼)â»Â¹ * (âˆ« x, g x âˆ‚Î¼')
  have c_nonneg : 0 â‰¤ c :=
    mul_nonneg (inv_nonneg.2 (integral_nonneg g_nonneg)) (integral_nonneg g_nonneg)
  refine âŸ¨âŸ¨c, c_nonnegâŸ©, fun f f_cont f_comp â†¦ ?_âŸ©
  /- use the lemma `integral_mulLeftInvariant_mulRightInvariant_combo` for `Î¼` and then `Î¼'`
  to reexpress the integral of `f` as the integral of `g` times a factor which only depends
  on a right-invariant measure `Î½`. We use `Î½ = Î¼.inv` for convenience. -/
  let Î½ := Î¼.inv
  have A : âˆ« x, f x âˆ‚Î¼ = (âˆ« y, f y * (âˆ« z, g (zâ»Â¹ * y) âˆ‚Î½)â»Â¹ âˆ‚Î½) * âˆ« x, g x âˆ‚Î¼ :=
    integral_isMulLeftInvariant_isMulRightInvariant_combo f_cont f_comp g_cont g_comp g_nonneg g_one
  rw [â† mul_inv_eq_iff_eq_mulâ‚€ int_g_pos.ne'] at A
  have B : âˆ« x, f x âˆ‚Î¼' = (âˆ« y, f y * (âˆ« z, g (zâ»Â¹ * y) âˆ‚Î½)â»Â¹ âˆ‚Î½) * âˆ« x, g x âˆ‚Î¼' :=
    integral_isMulLeftInvariant_isMulRightInvariant_combo f_cont f_comp g_cont g_comp g_nonneg g_one
  /- Since the `Î½`-factor is the same for `Î¼` and `Î¼'`, this gives the result. -/
  rw [â† A, mul_assoc, mul_comm] at B
  simp [B, integral_smul_nnreal_measure, c, NNReal.smul_def]

open scoped Classical in
/-- Given two left-invariant measures which are finite on compacts, `haarScalarFactor Î¼' Î¼` is a
scalar such that `âˆ« f dÎ¼' = (haarScalarFactor Î¼' Î¼) âˆ« f dÎ¼` for any compactly supported continuous
function `f`.

Note that there is a dissymmetry in the assumptions between `Î¼'` and `Î¼`: the measure `Î¼'` needs
only be finite on compact sets, while `Î¼` has to be finite on compact sets and positive on open
sets, i.e., a Haar measure, to exclude for instance the case where `Î¼ = 0`, where the definition
doesn't make sense. -/
@[to_additive /-- Given two left-invariant measures which are finite on compacts,
`addHaarScalarFactor Î¼' Î¼` is a scalar such that `âˆ« f dÎ¼' = (addHaarScalarFactor Î¼' Î¼) âˆ« f dÎ¼` for
any compactly supported continuous function `f`.

Note that there is a dissymmetry in the assumptions between `Î¼'` and `Î¼`: the measure `Î¼'` needs
only be finite on compact sets, while `Î¼` has to be finite on compact sets and positive on open
sets, i.e., an additive Haar measure, to exclude for instance the case where `Î¼ = 0`, where the
definition doesn't make sense. -/]
noncomputable def haarScalarFactor
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] :
    â„â‰¥0 :=
  if Â¬ LocallyCompactSpace G then 1
  else (exists_integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼' Î¼).choose

/-- Two left invariant measures integrate in the same way continuous compactly supported functions,
up to the scalar `haarScalarFactor Î¼' Î¼`. See also
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which gives the same result for compact
sets, and `measure_isHaarMeasure_eq_smul_of_isOpen` for open sets. -/
@[to_additive integral_isAddLeftInvariant_eq_smul_of_hasCompactSupport
/-- Two left invariant measures integrate in the same way continuous compactly supported functions,
up to the scalar `addHaarScalarFactor Î¼' Î¼`. See also
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which gives the same result for compact
sets, and `measure_isAddHaarMeasure_eq_smul_of_isOpen` for open sets. -/]
theorem integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    {f : G â†’ â„} (hf : Continuous f) (h'f : HasCompactSupport f) :
    âˆ« x, f x âˆ‚Î¼' = âˆ« x, f x âˆ‚(haarScalarFactor Î¼' Î¼ â€¢ Î¼) := by
  classical
  rcases h'f.eq_zero_or_locallyCompactSpace_of_group hf with Hf | Hf
  Â· simp [Hf]
  Â· simp only [haarScalarFactor, Hf, not_true_eq_false, ite_false]
    exact (exists_integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼' Î¼).choose_spec
      f hf h'f

@[to_additive addHaarScalarFactor_eq_integral_div]
lemma haarScalarFactor_eq_integral_div (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼]
    [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] {f : G â†’ â„} (hf : Continuous f)
    (h'f : HasCompactSupport f) (int_nonzero : âˆ« x, f x âˆ‚Î¼ â‰  0) :
    haarScalarFactor Î¼' Î¼ = (âˆ« x, f x âˆ‚Î¼') / âˆ« x, f x âˆ‚Î¼ := by
  have := integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼' Î¼ hf h'f
  rw [integral_smul_nnreal_measure] at this
  exact EuclideanDomain.eq_div_of_mul_eq_left int_nonzero this.symm

@[to_additive (attr := simp) addHaarScalarFactor_smul]
lemma haarScalarFactor_smul [LocallyCompactSpace G] (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼]
    [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] {c : â„â‰¥0} :
    haarScalarFactor (c â€¢ Î¼') Î¼ = c â€¢ haarScalarFactor Î¼' Î¼ := by
  obtain âŸ¨âŸ¨g, g_contâŸ©, g_comp, g_nonneg, g_oneâŸ© :
    âˆƒ g : C(G, â„), HasCompactSupport g âˆ§ 0 â‰¤ g âˆ§ g 1 â‰  0 := exists_continuous_nonneg_pos 1
  have int_g_ne_zero : âˆ« x, g x âˆ‚Î¼ â‰  0 :=
    ne_of_gt (g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one)
  apply NNReal.coe_injective
  calc
    haarScalarFactor (c â€¢ Î¼') Î¼ = (âˆ« x, g x âˆ‚(c â€¢ Î¼')) / âˆ« x, g x âˆ‚Î¼ :=
      haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero
    _ = (c â€¢ (âˆ« x, g x âˆ‚Î¼')) / âˆ« x, g x âˆ‚Î¼ := by simp
    _ = c â€¢ ((âˆ« x, g x âˆ‚Î¼') / âˆ« x, g x âˆ‚Î¼) := smul_div_assoc c _ _
    _ = c â€¢ haarScalarFactor Î¼' Î¼ := by
      rw [â† haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero]

@[to_additive mul_addHaarScalarFactor_smul]
lemma mul_haarScalarFactor_smul [LocallyCompactSpace G] (Î¼' Î¼ : Measure G)
    [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] {c : â„â‰¥0}
    (hc : c â‰  0) :
    haveI : IsHaarMeasure (c â€¢ Î¼) := IsHaarMeasure.nnreal_smul _ hc
    c * haarScalarFactor Î¼' (c â€¢ Î¼) = haarScalarFactor Î¼' Î¼ := by
  have : IsHaarMeasure (c â€¢ Î¼) := IsHaarMeasure.nnreal_smul _ hc
  obtain âŸ¨âŸ¨g, g_contâŸ©, g_comp, g_nonneg, g_oneâŸ© :
    âˆƒ g : C(G, â„), HasCompactSupport g âˆ§ 0 â‰¤ g âˆ§ g 1 â‰  0 := exists_continuous_nonneg_pos 1
  have int_g_ne_zero : âˆ« x, g x âˆ‚Î¼ â‰  0 :=
    ne_of_gt (g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one)
  apply NNReal.coe_injective
  calc
    c * haarScalarFactor Î¼' (c â€¢ Î¼) = c * ((âˆ« x, g x âˆ‚Î¼') / âˆ« x, g x âˆ‚(c â€¢ Î¼)) :=
      by rw [haarScalarFactor_eq_integral_div _ _ g_cont g_comp (by simp [int_g_ne_zero, hc])]
    _ = c * ((âˆ« x, g x âˆ‚Î¼') / (c â€¢ âˆ« x, g x âˆ‚Î¼)) := by simp
    _ = (âˆ« x, g x âˆ‚Î¼') / (âˆ« x, g x âˆ‚Î¼) := by
      rw [NNReal.smul_def, smul_eq_mul, â† mul_div_assoc]
      exact mul_div_mul_left (âˆ« (x : G), g x âˆ‚Î¼') (âˆ« (x : G), g x âˆ‚Î¼) (by simp [hc])
    _ = Î¼'.haarScalarFactor Î¼ :=
      (haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero).symm

@[to_additive addHaarScalarFactor_smul_smul]
lemma haarScalarFactor_smul_smul [LocallyCompactSpace G] (Î¼' Î¼ : Measure G)
    [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] {c : â„â‰¥0}
    (hc : c â‰  0) :
    haveI : IsHaarMeasure (c â€¢ Î¼) := IsHaarMeasure.nnreal_smul _ hc
    haarScalarFactor (c â€¢ Î¼') (c â€¢ Î¼) = haarScalarFactor Î¼' Î¼ := by
  rw [haarScalarFactor_smul, smul_eq_mul, mul_haarScalarFactor_smul _ _ hc]

@[to_additive (attr := simp)]
lemma haarScalarFactor_self (Î¼ : Measure G) [IsHaarMeasure Î¼] :
    haarScalarFactor Î¼ Î¼ = 1 := by
  by_cases hG : LocallyCompactSpace G; swap
  Â· simp [haarScalarFactor, hG]
  obtain âŸ¨âŸ¨g, g_contâŸ©, g_comp, g_nonneg, g_oneâŸ© :
    âˆƒ g : C(G, â„), HasCompactSupport g âˆ§ 0 â‰¤ g âˆ§ g 1 â‰  0 := exists_continuous_nonneg_pos 1
  have int_g_ne_zero : âˆ« x, g x âˆ‚Î¼ â‰  0 :=
    ne_of_gt (g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one)
  apply NNReal.coe_injective
  calc
    haarScalarFactor Î¼ Î¼ = (âˆ« x, g x âˆ‚Î¼) / âˆ« x, g x âˆ‚Î¼ :=
      haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero
    _ = 1 := div_self int_g_ne_zero

@[to_additive addHaarScalarFactor_eq_mul]
lemma haarScalarFactor_eq_mul (Î¼' Î¼ Î½ : Measure G)
    [IsHaarMeasure Î¼] [IsHaarMeasure Î½] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] :
    haarScalarFactor Î¼' Î½ = haarScalarFactor Î¼' Î¼ * haarScalarFactor Î¼ Î½ := by
  -- The group has to be locally compact, otherwise the scalar factor is 1 by definition.
  by_cases hG : LocallyCompactSpace G; swap
  Â· simp [haarScalarFactor, hG]
  -- Fix some nonzero continuous function with compact support `g`.
  obtain âŸ¨âŸ¨g, g_contâŸ©, g_comp, g_nonneg, g_oneâŸ© :
    âˆƒ (g : C(G, â„)), HasCompactSupport g âˆ§ 0 â‰¤ g âˆ§ g 1 â‰  0 := exists_continuous_nonneg_pos 1
  have Z := integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼' Î¼ g_cont g_comp
  simp only [integral_smul_nnreal_measure, smul_smul,
    integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼' Î½ g_cont g_comp,
    integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼ Î½ g_cont g_comp] at Z
  have int_g_pos : 0 < âˆ« x, g x âˆ‚Î½ := by
    apply (integral_pos_iff_support_of_nonneg g_nonneg _).2
    Â· exact IsOpen.measure_pos Î½ g_cont.isOpen_support âŸ¨1, g_oneâŸ©
    Â· exact g_cont.integrable_of_hasCompactSupport g_comp
  change (haarScalarFactor Î¼' Î½ : â„) * âˆ« (x : G), g x âˆ‚Î½ =
    (haarScalarFactor Î¼' Î¼ * haarScalarFactor Î¼ Î½ : â„â‰¥0) * âˆ« (x : G), g x âˆ‚Î½ at Z
  simpa only [mul_eq_mul_right_iff (Mâ‚€ := â„), int_g_pos.ne', or_false, â† NNReal.eq_iff] using Z

/-- The scalar factor between two left-invariant measures is non-zero when both measures are
positive on open sets. -/
@[to_additive]
lemma haarScalarFactor_pos_of_isHaarMeasure (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼]
    [IsHaarMeasure Î¼'] : 0 < haarScalarFactor Î¼' Î¼ :=
  pos_iff_ne_zero.2 (fun H â†¦ by simpa [H] using haarScalarFactor_eq_mul Î¼' Î¼ Î¼')

/-!
### Uniqueness of measure of sets with compact closure

Two left invariant measures give the same measure to sets with compact closure, up to the
scalar `haarScalarFactor Î¼' Î¼`.

This is a tricky argument, typically not done in textbooks (the textbooks version all require one
version of regularity or another). Here is a sketch, based on
McQuillan's answer at https://mathoverflow.net/questions/456670/.

Assume for simplicity that all measures are normalized, so that the scalar factors are all `1`.
First, from the fact that `Î¼` and `Î¼'` integrate in the same way compactly supported functions,
they give the same measure to compact "zero sets", i.e., sets of the form `fâ»Â¹ {1}`
for `f` continuous and compactly supported.
See `measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport`.

If `Î¼` is inner regular, a theorem of Halmos shows that any measurable set `s` of finite measure can
be approximated from inside by a compact zero set `k`. Then `Î¼ s â‰¤ Î¼ k + Îµ = Î¼' k + Îµ â‰¤ Î¼' s + Îµ`.
Letting `Îµ` tend to zero, one gets `Î¼ s â‰¤ Î¼' s`.
See `smul_measure_isMulInvariant_le_of_isCompact_closure`.

Assume now that `s` is a measurable set of compact closure. It is contained in a compact
zero set `t`. The same argument applied to `t - s` gives `Î¼ (t \ s) â‰¤ Î¼' (t \ s)`, i.e.,
`Î¼ t - Î¼ s â‰¤ Î¼' t - Î¼' s`. As `Î¼ t = Î¼' t` (since these are zero sets), we get the inequality
`Î¼' s â‰¤ Î¼ s`. Together with the previous one, this gives `Î¼' s = Î¼ s`.
See `measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop`.

If neither `Î¼` nor `Î¼'` is inner regular, we can use the existence of another inner regular
left-invariant measure `Î½`, so get `Î¼ s = Î½ s = Î¼' s`, by applying twice the previous argument.
Here, the uniqueness argument uses the existence of a Haar measure with a nice behavior!
See `measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet`.

Finally, if `s` has compact closure but is not measurable, its measure is the infimum of the
measures of its measurable supersets, and even of those contained in `closure s`. As `Î¼`
and `Î¼'` coincide on these supersets, this yields `Î¼ s = Î¼' s`.
See `measure_isMulInvariant_eq_smul_of_isCompact_closure`.
-/

/-- Two left invariant measures give the same mass to level sets of continuous compactly supported
functions, up to the scalar `haarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure. -/
@[to_additive measure_preimage_isAddLeftInvariant_eq_smul_of_hasCompactSupport
/-- Two left invariant measures give the same mass to level sets of continuous compactly supported
functions, up to the scalar `addHaarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure. -/]
lemma measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    {f : G â†’ â„} (hf : Continuous f) (h'f : HasCompactSupport f) :
    Î¼' (f â»Â¹' {1}) = haarScalarFactor Î¼' Î¼ â€¢ Î¼ (f â»Â¹' {1}) := by
  /- This follows from the fact that the two measures integrate in the same way continuous
  functions, by approximating the indicator function of `f â»Â¹' {1}` by continuous functions
  (namely `vâ‚™ âˆ˜ f` where `vâ‚™` is equal to `1` at `1`, and `0` outside of a small neighborhood
  `(1 - uâ‚™, 1 + uâ‚™)` where `uâ‚™` is a sequence tending to `0`).
  We use `vâ‚™ = thickenedIndicator uâ‚™ {1}` to take advantage of existing lemmas. -/
  obtain âŸ¨u, -, u_mem, u_limâŸ© : âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), u n âˆˆ Ioo 0 1)
    âˆ§ Tendsto u atTop (ğ“ 0) := exists_seq_strictAnti_tendsto' (zero_lt_one : (0 : â„) < 1)
  let v : â„• â†’ â„ â†’ â„ := fun n x â†¦ thickenedIndicator (u_mem n).1 ({1} : Set â„) x
  have vf_cont n : Continuous ((v n) âˆ˜ f) := by
    apply Continuous.comp (continuous_induced_dom.comp ?_) hf
    exact BoundedContinuousFunction.continuous (thickenedIndicator (u_mem n).left {1})
  have I : âˆ€ (Î½ : Measure G), IsFiniteMeasureOnCompacts Î½ â†’
      Tendsto (fun n â†¦ âˆ« x, v n (f x) âˆ‚Î½) atTop
      (ğ“ (âˆ« x, Set.indicator ({1} : Set â„) (fun _ â†¦ 1) (f x) âˆ‚Î½)) := by
    intro Î½ hÎ½
    apply tendsto_integral_of_dominated_convergence
        (bound := (tsupport f).indicator (fun (_ : G) â†¦ (1 : â„)))
    Â· exact fun n â†¦ (vf_cont n).aestronglyMeasurable
    Â· apply IntegrableOn.integrable_indicator _ (isClosed_tsupport f).measurableSet
      simpa using IsCompact.measure_lt_top h'f
    Â· refine fun n â†¦ Eventually.of_forall (fun x â†¦ ?_)
      by_cases hx : x âˆˆ tsupport f
      Â· simp only [v, Real.norm_eq_abs, NNReal.abs_eq, hx, indicator_of_mem]
        norm_cast
        exact thickenedIndicator_le_one _ _ _
      Â· simp only [v, Real.norm_eq_abs, NNReal.abs_eq, hx, not_false_eq_true, indicator_of_notMem]
        rw [thickenedIndicator_zero]
        Â· simp
        Â· simpa [image_eq_zero_of_notMem_tsupport hx] using (u_mem n).2.le
    Â· filter_upwards with x
      have T := tendsto_pi_nhds.1 (thickenedIndicator_tendsto_indicator_closure
        (fun n â†¦ (u_mem n).1) u_lim ({1} : Set â„)) (f x)
      simp only [thickenedIndicator_apply, closure_singleton] at T
      convert NNReal.tendsto_coe.2 T
      simp
  have M n : âˆ« (x : G), v n (f x) âˆ‚Î¼' = âˆ« (x : G), v n (f x) âˆ‚(haarScalarFactor Î¼' Î¼ â€¢ Î¼) := by
    apply integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Î¼' Î¼ (vf_cont n)
    apply h'f.comp_left
    simp only [v, thickenedIndicator_apply, NNReal.coe_eq_zero]
    rw [thickenedIndicatorAux_zero (u_mem n).1]
    Â· simp only [ENNReal.toNNReal_zero]
    Â· simpa using (u_mem n).2.le
  have I1 := I Î¼' (by infer_instance)
  simp_rw [M] at I1
  have J1 : âˆ« (x : G), indicator {1} (fun _ â†¦ (1 : â„)) (f x) âˆ‚Î¼'
      = âˆ« (x : G), indicator {1} (fun _ â†¦ 1) (f x) âˆ‚(haarScalarFactor Î¼' Î¼ â€¢ Î¼) :=
    tendsto_nhds_unique I1 (I (haarScalarFactor Î¼' Î¼ â€¢ Î¼) (by infer_instance))
  have J2 : Î¼'.real (f â»Â¹' {1})
      = (haarScalarFactor Î¼' Î¼ â€¢ Î¼).real (f â»Â¹' {1}) := by
    have : (fun x â†¦ indicator {1} (fun _ â†¦ (1 : â„)) (f x)) =
        (fun x â†¦ indicator (f â»Â¹' {1}) (fun _ â†¦ (1 : â„)) x) := by
      ext x
      exact (indicator_comp_right f (s := ({1} : Set â„)) (g := (fun _ â†¦ (1 : â„))) (x := x)).symm
    have mf : MeasurableSet (f â»Â¹' {1}) := (isClosed_singleton.preimage hf).measurableSet
    simpa only [this, mf, integral_indicator_const, smul_eq_mul, mul_one, Pi.smul_apply,
      nnreal_smul_coe_apply, ENNReal.toReal_mul, ENNReal.coe_toReal] using J1
  have C : IsCompact (f â»Â¹' {1}) := h'f.isCompact_preimage hf isClosed_singleton (by simp)
  rw [measureReal_eq_measureReal_iff C.measure_lt_top.ne C.measure_lt_top.ne] at J2
  simpa using J2

/-- If an invariant measure is inner regular, then it gives less mass to sets with compact closure
than any other invariant measure, up to the scalar `haarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which gives equality for any
set with compact closure. -/
@[to_additive smul_measure_isAddInvariant_le_of_isCompact_closure
/-- If an invariant measure is inner regular, then it gives less mass to sets with compact closure
than any other invariant measure, up to the scalar `addHaarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which gives equality for any
set with compact closure. -/]
lemma smul_measure_isMulInvariant_le_of_isCompact_closure [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    [InnerRegularCompactLTTop Î¼]
    {s : Set G} (hs : MeasurableSet s) (h's : IsCompact (closure s)) :
    haarScalarFactor Î¼' Î¼ â€¢ Î¼ s â‰¤ Î¼' s := by
  apply le_of_forall_lt (fun r hr â†¦ ?_)
  let Î½ := haarScalarFactor Î¼' Î¼ â€¢ Î¼
  have : Î½ s â‰  âˆ := ((measure_mono subset_closure).trans_lt h's.measure_lt_top).ne
  obtain âŸ¨-, hf, âŸ¨f, f_cont, f_comp, rflâŸ©, Î½fâŸ© :
      âˆƒ K âŠ† s, (âˆƒ f, Continuous f âˆ§ HasCompactSupport f âˆ§ K = f â»Â¹' {1}) âˆ§ r < Î½ K :=
    innerRegularWRT_preimage_one_hasCompactSupport_measure_ne_top_of_group âŸ¨hs, thisâŸ© r
      (by convert hr)
  calc
  r < Î½ (f â»Â¹' {1}) := Î½f
  _ = Î¼' (f â»Â¹' {1}) :=
    (measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport _ _ f_cont f_comp).symm
  _ â‰¤ Î¼' s := measure_mono hf

/-- If an invariant measure is inner regular, then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `haarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure, and removes the inner regularity assumption. -/
@[to_additive measure_isAddInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop
/-- If an invariant measure is inner regular, then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `addHaarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure, and removes the inner regularity assumption. -/]
lemma measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop
    [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    [InnerRegularCompactLTTop Î¼]
    {s : Set G} (hs : MeasurableSet s) (h's : IsCompact (closure s)) :
    Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s := by
  apply le_antisymm ?_ (smul_measure_isMulInvariant_le_of_isCompact_closure Î¼' Î¼ hs h's)
  let Î½ := haarScalarFactor Î¼' Î¼ â€¢ Î¼
  change Î¼' s â‰¤ Î½ s
  obtain âŸ¨âŸ¨f, f_contâŸ©, hf, -, f_comp, -âŸ© : âˆƒ f : C(G, â„), EqOn f 1 (closure s) âˆ§ EqOn f 0 âˆ…
      âˆ§ HasCompactSupport f âˆ§ âˆ€ x, f x âˆˆ Icc (0 : â„) 1 :=
    exists_continuous_one_zero_of_isCompact h's isClosed_empty (disjoint_empty _)
  let t := f â»Â¹' {1}
  have t_closed : IsClosed t := isClosed_singleton.preimage f_cont
  have t_comp : IsCompact t := f_comp.isCompact_preimage f_cont isClosed_singleton (by simp)
  have st : s âŠ† t := (IsClosed.closure_subset_iff t_closed).mp hf
  have A : Î½ (t \ s) â‰¤ Î¼' (t \ s) := by
    apply smul_measure_isMulInvariant_le_of_isCompact_closure _ _ (t_closed.measurableSet.diff hs)
    exact t_comp.closure_of_subset diff_subset
  have B : Î¼' t = Î½ t :=
    measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport _ _ f_cont f_comp
  rwa [measure_diff st hs.nullMeasurableSet, measure_diff st hs.nullMeasurableSet, â† B,
    ENNReal.sub_le_sub_iff_left] at A
  Â· exact measure_mono st
  Â· exact t_comp.measure_lt_top.ne
  Â· exact ((measure_mono st).trans_lt t_comp.measure_lt_top).ne
  Â· exact ((measure_mono st).trans_lt t_comp.measure_lt_top).ne

/-- Given an invariant measure then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `haarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which removes the
measurability assumption. -/
@[to_additive measure_isAddInvariant_eq_smul_of_isCompact_closure_of_measurableSet
/-- Given an invariant measure then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `addHaarScalarFactor Î¼' Î¼`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which removes the
measurability assumption. -/]
lemma measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    {s : Set G} (hs : MeasurableSet s) (h's : IsCompact (closure s)) :
    Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s := by
  let Î½ : Measure G := haar
  have A : Î¼' s = haarScalarFactor Î¼' Î½ â€¢ Î½ s :=
    measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop Î¼' Î½ hs h's
  have B : Î¼ s = haarScalarFactor Î¼ Î½ â€¢ Î½ s :=
    measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop Î¼ Î½ hs h's
  rw [A, B, smul_smul, haarScalarFactor_eq_mul Î¼' Î¼ Î½]

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite on compacts, they coincide in the following
sense: they give the same value to sets with compact closure, up to the multiplicative
constant `haarScalarFactor Î¼' Î¼`. -/
@[to_additive measure_isAddInvariant_eq_smul_of_isCompact_closure
/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite on compacts, they coincide in the following
sense: they give the same value to sets with compact closure, up to the multiplicative
constant `addHaarScalarFactor Î¼' Î¼`. -/]
theorem measure_isMulInvariant_eq_smul_of_isCompact_closure [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    {s : Set G} (h's : IsCompact (closure s)) :
    Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s := by
  let Î½ := haarScalarFactor Î¼' Î¼ â€¢ Î¼
  apply le_antisymm
  Â· calc
    Î¼' s â‰¤ Î¼' ((toMeasurable Î½ s) âˆ© (closure s)) :=
      measure_mono <| subset_inter (subset_toMeasurable Î½ s) subset_closure
    _ = Î½ ((toMeasurable Î½ s) âˆ© (closure s)) := by
      apply measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet _ _ _ _
      Â· exact (measurableSet_toMeasurable Î½ s).inter isClosed_closure.measurableSet
      Â· exact h's.closure_of_subset inter_subset_right
    _ â‰¤ Î½ (toMeasurable Î½ s) := measure_mono inter_subset_left
    _ = Î½ s := measure_toMeasurable s
  Â· calc
    Î½ s â‰¤ Î½ ((toMeasurable Î¼' s) âˆ© (closure s)) :=
      measure_mono <| subset_inter (subset_toMeasurable Î¼' s) subset_closure
    _ = Î¼' ((toMeasurable Î¼' s) âˆ© (closure s)) := by
      apply (measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet _ _ _ _).symm
      Â· exact (measurableSet_toMeasurable Î¼' s).inter isClosed_closure.measurableSet
      Â· exact h's.closure_of_subset inter_subset_right
    _ â‰¤ Î¼' (toMeasurable Î¼' s) := measure_mono inter_subset_left
    _ = Î¼' s := measure_toMeasurable s

/-- **Uniqueness of Haar measures**:
Two Haar measures on a compact group coincide up to a multiplicative factor. -/
@[to_additive isAddInvariant_eq_smul_of_compactSpace]
lemma isMulInvariant_eq_smul_of_compactSpace [CompactSpace G] (Î¼' Î¼ : Measure G)
    [IsHaarMeasure Î¼] [IsMulLeftInvariant Î¼'] [IsFiniteMeasureOnCompacts Î¼'] :
    Î¼' = haarScalarFactor Î¼' Î¼ â€¢ Î¼ := by
  ext s _hs
  exact measure_isMulInvariant_eq_smul_of_isCompact_closure _ _ isClosed_closure.isCompact

@[to_additive]
instance (priority := 100) instInnerRegularOfIsHaarMeasureOfCompactSpace
    [CompactSpace G] (Î¼ : Measure G) [IsMulLeftInvariant Î¼] [IsFiniteMeasureOnCompacts Î¼] :
    InnerRegular Î¼ := by
  rw [isMulInvariant_eq_smul_of_compactSpace Î¼ haar]
  infer_instance

@[to_additive]
instance (priority := 100) instRegularOfIsHaarMeasureOfCompactSpace
    [CompactSpace G] (Î¼ : Measure G) [IsMulLeftInvariant Î¼] [IsFiniteMeasureOnCompacts Î¼] :
    Regular Î¼ := by
  rw [isMulInvariant_eq_smul_of_compactSpace Î¼ haar]
  infer_instance

/-- **Uniqueness of Haar measures**:
Two Haar measures which are probability measures coincide. -/
@[to_additive]
lemma isHaarMeasure_eq_of_isProbabilityMeasure [LocallyCompactSpace G] (Î¼' Î¼ : Measure G)
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] [IsHaarMeasure Î¼] [IsHaarMeasure Î¼'] :
    Î¼' = Î¼ := by
  have : CompactSpace G := by
    by_contra H
    rw [not_compactSpace_iff] at H
    simpa using measure_univ_of_isMulLeftInvariant Î¼
  have A s : Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s :=
    measure_isMulInvariant_eq_smul_of_isCompact_closure _ _ isClosed_closure.isCompact
  have Z := A univ
  simp only [measure_univ, ENNReal.smul_def, smul_eq_mul, mul_one, ENNReal.one_eq_coe] at Z
  ext s _hs
  simp [A s, â† Z]

/-!
### Uniqueness of measure of open sets

Two Haar measures give the same measure to open sets (or more generally to sets which are everywhere
positive), up to the scalar `haarScalarFactor Î¼' Î¼ `.
-/

@[to_additive measure_isAddHaarMeasure_eq_smul_of_isEverywherePos]
theorem measure_isHaarMeasure_eq_smul_of_isEverywherePos [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsHaarMeasure Î¼']
    {s : Set G} (hs : MeasurableSet s) (h's : IsEverywherePos Î¼ s) :
    Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s := by
  let Î½ := haarScalarFactor Î¼' Î¼ â€¢ Î¼
  change Î¼' s = Î½ s
  /- Fix a compact neighborhood `k` of the identity, and consider a maximal disjoint family `m` of
  sets `x â€¢ k` centered at points in `s`. Then `s` is covered by the sets `x â€¢ (k * kâ»Â¹)` by
  maximality. If the family is countable, then since `Î¼'` and `Î½` coincide in compact sets, and
  the measure of a countable disjoint union is the sum of the measures, we get `Î¼' s = Î½ s`.
  Otherwise, the family is uncountable, and each intersection with `s` has positive measure by
  the everywhere positivity assumption, so `Î½ s = âˆ`, and `Î¼' s = âˆ` in the same way. -/
  obtain âŸ¨k, k_comp, k_closed, k_memâŸ© : âˆƒ k, IsCompact k âˆ§ IsClosed k âˆ§ k âˆˆ ğ“ (1 : G) := by
    rcases exists_compact_mem_nhds (1 : G) with âŸ¨k, hk, hmemâŸ©
    exact âŸ¨closure k, hk.closure, isClosed_closure, mem_of_superset hmem subset_closureâŸ©
  have one_k : 1 âˆˆ k := mem_of_mem_nhds k_mem
  let A : Set (Set G) := {t | t âŠ† s âˆ§ PairwiseDisjoint t (fun x â†¦ x â€¢ k)}
  obtain âŸ¨m, m_maxâŸ© : âˆƒ m, Maximal (Â· âˆˆ A) m := by
    apply zorn_subset
    intro c cA hc
    refine âŸ¨â‹ƒ a âˆˆ c, a, âŸ¨?_, ?_âŸ©, ?_âŸ©
    Â· simp only [iUnion_subset_iff]
      intro a ac x hx
      simp only [A, subset_def, mem_setOf_eq] at cA
      exact (cA _ ac).1 x hx
    Â· rintro x hx y hy hxy
      simp only [mem_iUnion, exists_prop] at hx hy
      rcases hx with âŸ¨a, ac, xaâŸ©
      rcases hy with âŸ¨b, bc, ybâŸ©
      obtain âŸ¨m, mc, am, bmâŸ© : âˆƒ m âˆˆ c, a âŠ† m âˆ§ b âŠ† m := hc.directedOn _ ac _ bc
      exact (cA mc).2 (am xa) (bm yb) hxy
    Â· intro a ac
      exact subset_biUnion_of_mem (u := id) ac
  obtain âŸ¨hms : m âŠ† s, hdj : PairwiseDisjoint m (fun x â†¦ x â€¢ k)âŸ© := m_max.prop
  have sm : s âŠ† â‹ƒ x âˆˆ m, x â€¢ (k * kâ»Â¹) := by
    intro y hy
    by_cases h'y : m âˆª {y} âˆˆ A
    Â· have ym : y âˆˆ m := m_max.mem_of_prop_insert (by simpa using h'y)
      have : y âˆˆ y â€¢ (k * kâ»Â¹) := by
        simpa using mem_leftCoset y (Set.mul_mem_mul one_k (Set.inv_mem_inv.mpr one_k))
      exact mem_biUnion ym this
    Â· obtain âŸ¨x, xm, -, z, zy, zxâŸ© : âˆƒ x âˆˆ m, y â‰  x âˆ§ âˆƒ z, z âˆˆ y â€¢ k âˆ§ z âˆˆ x â€¢ k := by
        simpa [A, hms, hy, insert_subset_iff, pairwiseDisjoint_insert, hdj, not_disjoint_iff]
          using h'y
      have : y âˆˆ x â€¢ (k * kâ»Â¹) := by
        rw [show y = x * ((xâ»Â¹ * z) * (yâ»Â¹ * z)â»Â¹) by group]
        have : (xâ»Â¹ * z) * (yâ»Â¹ * z)â»Â¹ âˆˆ k * kâ»Â¹ := Set.mul_mem_mul ((mem_leftCoset_iff x).mp zx)
          (Set.inv_mem_inv.mpr ((mem_leftCoset_iff y).mp zy))
        exact mem_leftCoset x this
      exact mem_biUnion xm this
  rcases eq_empty_or_nonempty m with rfl | hm
  Â· simp only [mem_empty_iff_false, iUnion_of_empty, iUnion_empty, subset_empty_iff] at sm
    simp [sm]
  by_cases h'm : Set.Countable m
  Â· rcases h'm.exists_eq_range hm with âŸ¨f, rflâŸ©
    have M i : MeasurableSet (disjointed (fun n â†¦ s âˆ© f n â€¢ (k * kâ»Â¹)) i) := by
      apply MeasurableSet.disjointed (fun j â†¦ hs.inter ?_)
      have : IsClosed (k â€¢ kâ»Â¹) := IsClosed.smul_left_of_isCompact k_closed.inv k_comp
      exact (IsClosed.smul this (f j)).measurableSet
    simp only [mem_range, iUnion_exists, iUnion_iUnion_eq'] at sm
    have s_eq : s = â‹ƒ n, s âˆ© (f n â€¢ (k * kâ»Â¹)) := by rwa [â† inter_iUnion, eq_comm, inter_eq_left]
    have I : Î¼' s = âˆ‘' n, Î¼' (disjointed (fun n â†¦ s âˆ© f n â€¢ (k * kâ»Â¹)) n) := by
      rw [â† measure_iUnion (disjoint_disjointed _) M, iUnion_disjointed, â† s_eq]
    have J : Î½ s = âˆ‘' n, Î½ (disjointed (fun n â†¦ s âˆ© f n â€¢ (k * kâ»Â¹)) n) := by
      rw [â† measure_iUnion (disjoint_disjointed _) M, iUnion_disjointed, â† s_eq]
    rw [I, J]
    congr with n
    apply measure_isMulInvariant_eq_smul_of_isCompact_closure
    have : IsCompact (f n â€¢ (k * kâ»Â¹)) := IsCompact.smul (f n) (k_comp.mul k_comp.inv)
    exact this.closure_of_subset <| (disjointed_subset _ _).trans inter_subset_right
  Â· have H : âˆ€ (Ï : Measure G), IsEverywherePos Ï s â†’ Ï s = âˆ := by
      intro Ï hÏ
      have M : âˆ€ (i : â†‘m), MeasurableSet (s âˆ© (i : G) â€¢ k) :=
        fun i â†¦ hs.inter (IsClosed.smul k_closed _).measurableSet
      contrapose! h'm
      have : âˆ‘' (x : m), Ï (s âˆ© ((x : G) â€¢ k)) < âˆ := by
        apply lt_of_le_of_lt (MeasureTheory.tsum_meas_le_meas_iUnion_of_disjoint _ M _) _
        Â· have I : PairwiseDisjoint m fun x â†¦ s âˆ© x â€¢ k :=
            hdj.mono (fun x â†¦ inter_subset_right)
          exact I.on_injective Subtype.val_injective (fun x â†¦ x.2)
        Â· exact lt_of_le_of_lt (measure_mono (by simp [inter_subset_left])) h'm.lt_top
      have C : Set.Countable (support fun (i : m) â†¦ Ï (s âˆ© (i : G) â€¢ k)) :=
        Summable.countable_support_ennreal this.ne
      have : support (fun (i : m) â†¦ Ï (s âˆ© (i : G) â€¢ k)) = univ := by
        refine eq_univ_iff_forall.2 fun i â†¦ ?_
        refine ne_of_gt (hÏ (i : G) (hms i.2) _ ?_)
        exact inter_mem_nhdsWithin s (by simpa)
      rw [this] at C
      have : Countable m := countable_univ_iff.mp C
      exact to_countable m
    have HÎ½ : IsEverywherePos Î½ s :=
      h's.smul_measure_nnreal (haarScalarFactor_pos_of_isHaarMeasure _ _).ne'
    have HÎ¼' : IsEverywherePos Î¼' s := by
      apply HÎ½.of_forall_exists_nhds_eq (fun x _hx â†¦ ?_)
      obtain âŸ¨t, t_comp, t_memâŸ© : âˆƒ t, IsCompact t âˆ§ t âˆˆ ğ“ x := exists_compact_mem_nhds x
      refine âŸ¨t, t_mem, fun u hu â†¦ ?_âŸ©
      apply measure_isMulInvariant_eq_smul_of_isCompact_closure
      exact t_comp.closure_of_subset hu
    rw [H Î½ HÎ½, H Î¼' HÎ¼']

/-- **Uniqueness of Haar measures**:
Given two Haar measures, they coincide in the following sense: they give the same value to open
sets, up to the multiplicative constant `haarScalarFactor Î¼' Î¼`. -/
@[to_additive measure_isAddHaarMeasure_eq_smul_of_isOpen
/-- **Uniqueness of Haar measures**:
Given two additive Haar measures, they coincide in the following sense: they give the same value to
open sets, up to the multiplicative constant `addHaarScalarFactor Î¼' Î¼`. -/]
theorem measure_isHaarMeasure_eq_smul_of_isOpen [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsHaarMeasure Î¼'] {s : Set G} (hs : IsOpen s) :
    Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s :=
  measure_isHaarMeasure_eq_smul_of_isEverywherePos Î¼' Î¼ hs.measurableSet hs.isEverywherePos

/-!
### Uniqueness of Haar measure under regularity assumptions.
-/

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite on
compacts and inner regular for finite measure sets with respect to compact sets,
they coincide in the following sense: they give the same value to finite measure sets,
up to a multiplicative constant. -/
@[to_additive]
lemma measure_isMulLeftInvariant_eq_smul_of_ne_top [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    [InnerRegularCompactLTTop Î¼] [InnerRegularCompactLTTop Î¼'] {s : Set G}
    (hs : Î¼ s â‰  âˆ) (h's : Î¼' s â‰  âˆ) : Î¼' s = haarScalarFactor Î¼' Î¼ â€¢ Î¼ s := by
  /- We know that the measures integrate in the same way continuous compactly supported functions,
  up to the factor `c = haarScalarFactor Î¼' Î¼`. -/
  let c := haarScalarFactor Î¼' Î¼
  /- By regularity, every measurable set of finite measure may be approximated by compact sets.
  Therefore, the measures coincide on measurable sets of finite measure. -/
  have B : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ Î¼' s < âˆ â†’ Î¼' s = (c â€¢ Î¼) s := by
    intro s s_meas hs h's
    have : (c â€¢ Î¼) s â‰  âˆ := by simp [ENNReal.mul_eq_top, hs.ne]
    rw [s_meas.measure_eq_iSup_isCompact_of_ne_top h's.ne,
        s_meas.measure_eq_iSup_isCompact_of_ne_top this]
    congr! 4 with K _Ks K_comp
    exact measure_isMulInvariant_eq_smul_of_isCompact_closure Î¼' Î¼ K_comp.closure
  /- Finally, replace an arbitrary finite measure set with a measurable version, and use the
  version for measurable sets. -/
  let t := toMeasurable Î¼' s âˆ© toMeasurable Î¼ s
  have st : s âŠ† t := subset_inter (subset_toMeasurable Î¼' s) (subset_toMeasurable Î¼ s)
  have mu'_t : Î¼' t = Î¼' s := by
    apply le_antisymm
    Â· exact (measure_mono inter_subset_left).trans (measure_toMeasurable s).le
    Â· exact measure_mono st
  have mu_t : Î¼ t = Î¼ s := by
    apply le_antisymm
    Â· exact (measure_mono inter_subset_right).trans (measure_toMeasurable s).le
    Â· exact measure_mono st
  simp only [â† mu'_t, â† mu_t, nnreal_smul_coe_apply]
  apply B
  Â· exact (measurableSet_toMeasurable _ _).inter (measurableSet_toMeasurable _ _)
  Â· exact mu_t.le.trans_lt hs.lt_top
  Â· exact mu'_t.le.trans_lt h's.lt_top

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite
on compacts and inner regular, they coincide up to a multiplicative constant. -/
@[to_additive isAddLeftInvariant_eq_smul_of_innerRegular]
lemma isMulLeftInvariant_eq_smul_of_innerRegular [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    [InnerRegular Î¼] [InnerRegular Î¼'] :
    Î¼' = haarScalarFactor Î¼' Î¼ â€¢ Î¼ := by
  ext s hs
  rw [hs.measure_eq_iSup_isCompact, hs.measure_eq_iSup_isCompact]
  congr! 4 with K _Ks K_comp
  exact measure_isMulLeftInvariant_eq_smul_of_ne_top Î¼' Î¼ K_comp.measure_lt_top.ne
    K_comp.measure_lt_top.ne

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite
on compacts and regular, they coincide up to a multiplicative constant. -/
@[to_additive isAddLeftInvariant_eq_smul_of_regular]
lemma isMulLeftInvariant_eq_smul_of_regular [LocallyCompactSpace G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼']
    [Regular Î¼] [Regular Î¼'] :
    Î¼' = haarScalarFactor Î¼' Î¼ â€¢ Î¼ := by
  have A : âˆ€ U, IsOpen U â†’ Î¼' U = (haarScalarFactor Î¼' Î¼ â€¢ Î¼) U := by
    intro U hU
    rw [hU.measure_eq_iSup_isCompact, hU.measure_eq_iSup_isCompact]
    congr! 4 with K _KU K_comp
    exact measure_isMulLeftInvariant_eq_smul_of_ne_top Î¼' Î¼ K_comp.measure_lt_top.ne
      K_comp.measure_lt_top.ne
  ext s _hs
  rw [s.measure_eq_iInf_isOpen, s.measure_eq_iInf_isOpen]
  congr! 4 with U _sU U_open
  exact A U U_open

/-- **Uniqueness of left-invariant measures**:
Two Haar measures coincide up to a multiplicative constant in a second countable group. -/
@[to_additive isAddLeftInvariant_eq_smul]
lemma isMulLeftInvariant_eq_smul [LocallyCompactSpace G] [SecondCountableTopology G]
    (Î¼' Î¼ : Measure G) [IsHaarMeasure Î¼] [IsFiniteMeasureOnCompacts Î¼'] [IsMulLeftInvariant Î¼'] :
    Î¼' = haarScalarFactor Î¼' Î¼ â€¢ Î¼ :=
  isMulLeftInvariant_eq_smul_of_regular Î¼' Î¼
  -- one could use as well `isMulLeftInvariant_eq_smul_of_innerRegular`, as in a
  -- second countable topological space all Haar measures are regular and inner regular


/-- An invariant Ïƒ-finite measure is absolutely continuous with respect to a Haar measure in a
second countable group. -/
@[to_additive
/-- An invariant measure is absolutely continuous with respect to an additive Haar measure. -/]
theorem absolutelyContinuous_isHaarMeasure [LocallyCompactSpace G]
    [SecondCountableTopology G] (Î¼ Î½ : Measure G)
    [SigmaFinite Î¼] [IsMulLeftInvariant Î¼] [IsHaarMeasure Î½] : Î¼ â‰ª Î½ := by
  have K : PositiveCompacts G := Classical.arbitrary _
  have h : haarMeasure K = (haarScalarFactor (haarMeasure K) Î½ : â„â‰¥0âˆ) â€¢ Î½ :=
    isMulLeftInvariant_eq_smul (haarMeasure K) Î½
  rw [haarMeasure_unique Î¼ K, h, smul_smul]
  exact smul_absolutelyContinuous

/-- A continuous surjective monoid homomorphism of topological groups with compact codomain
is measure preserving, provided that the Haar measures on the domain and on the codomain
have the same total mass.
-/
@[to_additive
  /-- A continuous surjective additive monoid homomorphism of topological groups with compact
  codomain is measure preserving, provided that the Haar measures on the domain and on the codomain
  have the same total mass. -/]
theorem _root_.MonoidHom.measurePreserving
    {H : Type*} [Group H] [TopologicalSpace H] [IsTopologicalGroup H] [CompactSpace H]
    [MeasurableSpace H] [BorelSpace H]
    {Î¼ : Measure G} [IsHaarMeasure Î¼] {Î½ : Measure H} [IsHaarMeasure Î½]
    {f : G â†’* H} (hcont : Continuous f) (hsurj : Surjective f) (huniv : Î¼ univ = Î½ univ) :
    MeasurePreserving f Î¼ Î½ where
  measurable := hcont.measurable
  map_eq := by
    have : IsFiniteMeasure Î¼ := âŸ¨by rw [huniv]; apply measure_lt_topâŸ©
    have : (Î¼.map f).IsHaarMeasure := isHaarMeasure_map_of_isFiniteMeasure Î¼ f hcont hsurj
    set C : â„â‰¥0 := haarScalarFactor (Î¼.map f) Î½
    have hC : Î¼.map f = C â€¢ Î½ := isMulLeftInvariant_eq_smul_of_innerRegular _ _
    suffices C = 1 by rwa [this, one_smul] at hC
    have : C * Î½ univ = 1 * Î½ univ := by
      rw [one_mul, â† smul_eq_mul, â† ENNReal.smul_def, â† smul_apply, â† hC,
        map_apply hcont.measurable .univ, preimage_univ, huniv]
    rwa [ENNReal.mul_left_inj (NeZero.ne _) (measure_ne_top _ _), ENNReal.coe_eq_one] at this

end Group

section CommGroup

variable {G : Type*} [CommGroup G] [TopologicalSpace G] [IsTopologicalGroup G]
  [MeasurableSpace G] [BorelSpace G] (Î¼ : Measure G) [IsHaarMeasure Î¼]

/-- Any regular Haar measure is invariant under inversion in an abelian group. -/
@[to_additive
/-- Any regular additive Haar measure is invariant under negation in an abelian group. -/]
instance (priority := 100) IsHaarMeasure.isInvInvariant_of_regular
    [LocallyCompactSpace G] [Regular Î¼] : IsInvInvariant Î¼ := by
  -- the image measure is a Haar measure. By uniqueness up to multiplication, it is of the form
  -- `c Î¼`. Applying again inversion, one gets the measure `c^2 Î¼`. But since inversion is an
  -- involution, this is also `Î¼`. Hence, `c^2 = 1`, which implies `c = 1`.
  constructor
  let c : â„â‰¥0âˆ := haarScalarFactor Î¼.inv Î¼
  have hc : Î¼.inv = c â€¢ Î¼ := isMulLeftInvariant_eq_smul_of_regular Î¼.inv Î¼
  have : map Inv.inv (map Inv.inv Î¼) = c ^ 2 â€¢ Î¼ := by
    rw [â† inv_def Î¼, hc, Measure.map_smul, â† inv_def Î¼, hc, smul_smul, pow_two]
  have Î¼eq : Î¼ = c ^ 2 â€¢ Î¼ := by
    rw [map_map continuous_inv.measurable continuous_inv.measurable] at this
    simpa only [inv_involutive, Involutive.comp_self, Measure.map_id]
  have K : PositiveCompacts G := Classical.arbitrary _
  have : c ^ 2 * Î¼ K = 1 ^ 2 * Î¼ K := by
    conv_rhs => rw [Î¼eq]
    simp
  have : c ^ 2 = 1 ^ 2 :=
    (ENNReal.mul_left_inj (measure_pos_of_nonempty_interior _ K.interior_nonempty).ne'
          K.isCompact.measure_lt_top.ne).1 this
  have : c = 1 := (ENNReal.pow_right_strictMono two_ne_zero).injective this
  rw [hc, this, one_smul]

/-- Any inner regular Haar measure is invariant under inversion in an abelian group. -/
@[to_additive
/-- Any regular additive Haar measure is invariant under negation in an abelian group. -/]
instance (priority := 100) IsHaarMeasure.isInvInvariant_of_innerRegular
    [LocallyCompactSpace G] [InnerRegular Î¼] : IsInvInvariant Î¼ := by
  -- the image measure is a Haar measure. By uniqueness up to multiplication, it is of the form
  -- `c Î¼`. Applying again inversion, one gets the measure `c^2 Î¼`. But since inversion is an
  -- involution, this is also `Î¼`. Hence, `c^2 = 1`, which implies `c = 1`.
  constructor
  let c : â„â‰¥0âˆ := haarScalarFactor Î¼.inv Î¼
  have hc : Î¼.inv = c â€¢ Î¼ := isMulLeftInvariant_eq_smul_of_innerRegular Î¼.inv Î¼
  have : map Inv.inv (map Inv.inv Î¼) = c ^ 2 â€¢ Î¼ := by
    rw [â† inv_def Î¼, hc, Measure.map_smul, â† inv_def Î¼, hc, smul_smul, pow_two]
  have Î¼eq : Î¼ = c ^ 2 â€¢ Î¼ := by
    rw [map_map continuous_inv.measurable continuous_inv.measurable] at this
    simpa only [inv_involutive, Involutive.comp_self, Measure.map_id]
  have K : PositiveCompacts G := Classical.arbitrary _
  have : c ^ 2 * Î¼ K = 1 ^ 2 * Î¼ K := by
    conv_rhs => rw [Î¼eq]
    simp
  have : c ^ 2 = 1 ^ 2 :=
    (ENNReal.mul_left_inj (measure_pos_of_nonempty_interior _ K.interior_nonempty).ne'
          K.isCompact.measure_lt_top.ne).1 this
  have : c = 1 := (ENNReal.pow_right_strictMono two_ne_zero).injective this
  rw [hc, this, one_smul]

@[to_additive]
theorem measurePreserving_zpow [CompactSpace G] [RootableBy G â„¤] {n : â„¤} (hn : n â‰  0) :
    MeasurePreserving (fun g : G => g ^ n) Î¼ Î¼ :=
  (zpowGroupHom n).measurePreserving (Î¼ := Î¼) (continuous_zpow n)
    (RootableBy.surjective_pow G â„¤ hn) rfl

@[to_additive]
theorem MeasurePreserving.zpow [CompactSpace G] [RootableBy G â„¤]
    {n : â„¤} (hn : n â‰  0) {X : Type*}
    [MeasurableSpace X] {Î¼' : Measure X} {f : X â†’ G} (hf : MeasurePreserving f Î¼' Î¼) :
    MeasurePreserving (fun x => f x ^ n) Î¼' Î¼ :=
  (measurePreserving_zpow Î¼ hn).comp hf

end CommGroup

section DistribMulAction
variable {G A : Type*} [Group G] [AddCommGroup A] [DistribMulAction G A] [MeasurableSpace A]
  [TopologicalSpace A] [BorelSpace A] [IsTopologicalAddGroup A] [LocallyCompactSpace A]
  [ContinuousConstSMul G A] {Î¼ Î½ : Measure A} [Î¼.IsAddHaarMeasure] [Î½.IsAddHaarMeasure] {g : G}

variable (Î¼ Î½) in
lemma addHaarScalarFactor_domSMul (g : Gáµˆáµáµƒ) :
    addHaarScalarFactor (g â€¢ Î¼) (g â€¢ Î½) = addHaarScalarFactor Î¼ Î½ := by
  obtain âŸ¨âŸ¨f, f_contâŸ©, f_comp, f_nonneg, f_zeroâŸ© :
    âˆƒ f : C(A, â„), HasCompactSupport f âˆ§ 0 â‰¤ f âˆ§ f 0 â‰  0 := exists_continuous_nonneg_pos 0
  have int_f_ne_zero : âˆ« x, f x âˆ‚g â€¢ Î½ â‰  0 :=
    (f_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero f_comp f_nonneg f_zero).ne'
  apply NNReal.coe_injective
  rw [addHaarScalarFactor_eq_integral_div (g â€¢ Î¼) (g â€¢ Î½) f_cont f_comp int_f_ne_zero,
    integral_domSMul, integral_domSMul]
  refine (addHaarScalarFactor_eq_integral_div _ _ (by fun_prop) ?_ ?_).symm
  Â· exact f_comp.comp_isClosedEmbedding (Homeomorph.smul _).isClosedEmbedding
  Â· rw [â† integral_domSMul]
    exact (f_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero f_comp f_nonneg f_zero).ne'

variable (Î¼) in
lemma addHaarScalarFactor_smul_congr (g : Gáµˆáµáµƒ) :
    addHaarScalarFactor Î¼ (g â€¢ Î¼) = addHaarScalarFactor Î½ (g â€¢ Î½) := by
  rw [addHaarScalarFactor_eq_mul _ (g â€¢ Î½), addHaarScalarFactor_domSMul,
    mul_comm, â† addHaarScalarFactor_eq_mul]

variable (Î¼) in
lemma addHaarScalarFactor_smul_congr' (g : Gáµˆáµáµƒ) :
    addHaarScalarFactor (g â€¢ Î¼) Î¼ = addHaarScalarFactor (g â€¢ Î½) Î½ := by
  rw [addHaarScalarFactor_eq_mul _ (g â€¢ Î½), addHaarScalarFactor_domSMul,
    mul_comm, â† addHaarScalarFactor_eq_mul]

end DistribMulAction
end Measure

end MeasureTheory
