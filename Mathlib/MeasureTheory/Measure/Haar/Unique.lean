/-
Copyright (c) 2023 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.MeasureTheory.Function.LocallyIntegrable
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Integral.Bochner.Set
import Mathlib.MeasureTheory.Measure.EverywherePos
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Mathlib.Topology.Metrizable.Urysohn
import Mathlib.Topology.UrysohnsLemma
import Mathlib.Topology.ContinuousMap.Ordered

/-!
# Uniqueness of Haar measure in locally compact groups

## Main results

In a locally compact group, we prove that two left-invariant measures `Œº'` and `Œº` which are finite
on compact sets coincide, up to a normalizing scalar that we denote with `haarScalarFactor Œº' Œº`,
in the following sense:
* `integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport`: they give the same value to the
  integral of continuous compactly supported functions, up to a scalar.
* `measure_isMulInvariant_eq_smul_of_isCompact_closure`: they give the same value to sets with
  compact closure, up to a scalar.
* `measure_isHaarMeasure_eq_smul_of_isOpen`: they give the same value to open sets, up to a scalar.

To get genuine equality of measures, we typically need additional regularity assumptions:

* `isMulLeftInvariant_eq_smul_of_innerRegular`: two left invariant measures which are
  inner regular coincide up to a scalar.
* `isMulLeftInvariant_eq_smul_of_regular`: two left invariant measure which are
  regular coincide up to a scalar.
* `isHaarMeasure_eq_smul`: in a second countable space, two Haar measures coincide up to a
  scalar.
* `isMulInvariant_eq_smul_of_compactSpace`: two left-invariant measures on a compact group coincide
  up to a scalar.
* `isHaarMeasure_eq_of_isProbabilityMeasure`: two Haar measures which are probability measures
  coincide exactly.

In general, uniqueness statements for Haar measures in the literature make some assumption of
regularity, either regularity or inner regularity. We have tried to minimize the assumptions in the
theorems above, and cover the different results that exist in the literature.

## Implementation

The first result `integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport` is classical. To prove
it, we use a change of variables to express integrals with respect to a left-invariant measure as
integrals with respect to a given right-invariant measure (with a suitable density function).
The uniqueness readily follows.

Uniqueness results for the measure of compact sets and open sets, without any regularity assumption,
are significantly harder. They rely on the completion-regularity of the standard regular Haar
measure. We follow McQuillan's answer at https://mathoverflow.net/questions/456670/.

On second-countable groups, one can arrive to slightly different uniqueness results by using that
the operations are measurable. In particular, one can get uniqueness assuming œÉ-finiteness of
the measures but discarding the assumption that they are finite on compact sets. See
`haarMeasure_unique` in the file `Mathlib/MeasureTheory/Measure/Haar/Basic.lean`.

## References

[Halmos, Measure Theory][halmos1950measure]
[Fremlin, *Measure Theory* (volume 4)][fremlin_vol4]
-/

open Filter Set TopologicalSpace Function MeasureTheory Measure
open scoped Uniformity Topology ENNReal Pointwise NNReal

/-- In a locally compact regular space with an inner regular measure, the measure of a compact
set `k` is the infimum of the integrals of compactly supported functions equal to `1` on `k`. -/
lemma IsCompact.measure_eq_biInf_integral_hasCompactSupport
    {X : Type*} [TopologicalSpace X] [MeasurableSpace X] [BorelSpace X]
    {k : Set X} (hk : IsCompact k)
    (Œº : Measure X) [IsFiniteMeasureOnCompacts Œº] [InnerRegularCompactLTTop Œº]
    [LocallyCompactSpace X] [RegularSpace X] :
    Œº k = ‚®Ö (f : X ‚Üí ‚Ñù) (_ : Continuous f) (_ : HasCompactSupport f) (_ : EqOn f 1 k)
      (_ : 0 ‚â§ f), ENNReal.ofReal (‚à´ x, f x ‚àÇŒº) := by
  apply le_antisymm
  ¬∑ simp only [le_iInf_iff]
    intro f f_cont f_comp fk f_nonneg
    apply (f_cont.integrable_of_hasCompactSupport f_comp).measure_le_integral
    ¬∑ exact Eventually.of_forall f_nonneg
    ¬∑ exact fun x hx ‚Ü¶ by simp [fk hx]
  ¬∑ apply le_of_forall_gt (fun r hr ‚Ü¶ ?_)
    simp only [iInf_lt_iff, exists_prop]
    obtain ‚ü®U, kU, U_open, mu_U‚ü© : ‚àÉ U, k ‚äÜ U ‚àß IsOpen U ‚àß Œº U < r :=
      hk.exists_isOpen_lt_of_lt r hr
    obtain ‚ü®‚ü®f, f_cont‚ü©, fk, fU, f_comp, f_range‚ü© : ‚àÉ (f : C(X, ‚Ñù)), EqOn f 1 k ‚àß EqOn f 0 U·∂ú
        ‚àß HasCompactSupport f ‚àß ‚àÄ (x : X), f x ‚àà Icc 0 1 := exists_continuous_one_zero_of_isCompact
      hk U_open.isClosed_compl (disjoint_compl_right_iff_subset.mpr kU)
    refine ‚ü®f, f_cont, f_comp, fk, fun x ‚Ü¶ (f_range x).1, ?_‚ü©
    exact (integral_le_measure (fun x _hx ‚Ü¶ (f_range x).2) (fun x hx ‚Ü¶ (fU hx).le)).trans_lt mu_U

namespace MeasureTheory

/-- The parameterized integral `x ‚Ü¶ ‚à´ y, g (y‚Åª¬π * x) ‚àÇŒº` depends continuously on `y` when `g` is a
compactly supported continuous function on a topological group `G`, and `Œº` is finite on compact
sets. -/
@[to_additive]
lemma continuous_integral_apply_inv_mul
    {G : Type*} [TopologicalSpace G] [LocallyCompactSpace G] [Group G] [IsTopologicalGroup G]
    [MeasurableSpace G] [BorelSpace G]
    {Œº : Measure G} [IsFiniteMeasureOnCompacts Œº] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] {g : G ‚Üí E}
    (hg : Continuous g) (h'g : HasCompactSupport g) :
    Continuous (fun (x : G) ‚Ü¶ ‚à´ y, g (y‚Åª¬π * x) ‚àÇŒº) := by
  let k := tsupport g
  have k_comp : IsCompact k := h'g
  apply continuous_iff_continuousAt.2 (fun x‚ÇÄ ‚Ü¶ ?_)
  obtain ‚ü®t, t_comp, ht‚ü© : ‚àÉ t, IsCompact t ‚àß t ‚àà ùìù x‚ÇÄ := exists_compact_mem_nhds x‚ÇÄ
  let k' : Set G := t ‚Ä¢ k‚Åª¬π
  have k'_comp : IsCompact k' := t_comp.smul_set k_comp.inv
  have A : ContinuousOn (fun (x : G) ‚Ü¶ ‚à´ y, g (y‚Åª¬π * x) ‚àÇŒº) t := by
    apply continuousOn_integral_of_compact_support k'_comp
    ¬∑ exact (hg.comp (continuous_snd.inv.mul continuous_fst)).continuousOn
    ¬∑ intro p x hp hx
      contrapose! hx
      refine ‚ü®p, hp, p‚Åª¬π * x, ?_, by simp‚ü©
      simpa only [Set.mem_inv, mul_inv_rev, inv_inv] using subset_tsupport _ hx
  exact A.continuousAt ht

namespace Measure

section Group

variable {G : Type*} [TopologicalSpace G] [Group G] [IsTopologicalGroup G]
  [MeasurableSpace G] [BorelSpace G]

/-!
### Uniqueness of integrals of compactly supported functions

Two left invariant measures coincide when integrating continuous compactly supported functions,
up to a scalar that we denote with `haarScalarFactor Œº' Œº `.

This is proved by relating the integral for arbitrary left invariant and right invariant measures,
applying a version of Fubini.
As one may use the same right invariant measure, this shows that two different left invariant
measures will give the same integral, up to some fixed scalar.
-/

/-- In a group with a left invariant measure `Œº` and a right invariant measure `ŒΩ`, one can express
integrals with respect to `Œº` as integrals with respect to `ŒΩ` up to a constant scaling factor
(given in the statement as `‚à´ x, g x ‚àÇŒº` where `g` is a fixed reference function) and an
explicit density `y ‚Ü¶ 1/‚à´ z, g (z‚Åª¬π * y) ‚àÇŒΩ`. -/
@[to_additive]
lemma integral_isMulLeftInvariant_isMulRightInvariant_combo
    {Œº ŒΩ : Measure G} [IsFiniteMeasureOnCompacts Œº] [IsFiniteMeasureOnCompacts ŒΩ]
    [IsMulLeftInvariant Œº] [IsMulRightInvariant ŒΩ] [IsOpenPosMeasure ŒΩ]
    {f g : G ‚Üí ‚Ñù} (hf : Continuous f) (h'f : HasCompactSupport f)
    (hg : Continuous g) (h'g : HasCompactSupport g) (g_nonneg : 0 ‚â§ g) {x‚ÇÄ : G} (g_pos : g x‚ÇÄ ‚â† 0) :
    ‚à´ x, f x ‚àÇŒº = (‚à´ y, f y * (‚à´ z, g (z‚Åª¬π * y) ‚àÇŒΩ)‚Åª¬π ‚àÇŒΩ) * ‚à´ x, g x ‚àÇŒº := by
  -- The group has to be locally compact, otherwise all integrals vanish and the result is trivial.
  rcases h'f.eq_zero_or_locallyCompactSpace_of_group hf with Hf|Hf
  ¬∑ simp [Hf]
  let D : G ‚Üí ‚Ñù := fun (x : G) ‚Ü¶ ‚à´ y, g (y‚Åª¬π * x) ‚àÇŒΩ
  have D_cont : Continuous D := continuous_integral_apply_inv_mul hg h'g
  have D_pos : ‚àÄ x, 0 < D x := by
    intro x
    have C : Continuous (fun y ‚Ü¶ g (y‚Åª¬π * x)) := hg.comp (continuous_inv.mul continuous_const)
    apply (integral_pos_iff_support_of_nonneg _ _).2
    ¬∑ apply C.isOpen_support.measure_pos ŒΩ
      exact ‚ü®x * x‚ÇÄ‚Åª¬π, by simpa using g_pos‚ü©
    ¬∑ exact fun y ‚Ü¶ g_nonneg (y‚Åª¬π * x)
    ¬∑ apply C.integrable_of_hasCompactSupport
      exact h'g.comp_homeomorph ((Homeomorph.inv G).trans (Homeomorph.mulRight x))
  calc
  ‚à´ x, f x ‚àÇŒº = ‚à´ x, f x * (D x)‚Åª¬π * D x ‚àÇŒº := by
    congr with x; rw [mul_assoc, inv_mul_cancel‚ÇÄ (D_pos x).ne', mul_one]
  _ = ‚à´ x, (‚à´ y, f x * (D x)‚Åª¬π * g (y‚Åª¬π * x) ‚àÇŒΩ) ‚àÇŒº := by simp_rw [D, integral_const_mul]
  _ = ‚à´ y, (‚à´ x, f x * (D x)‚Åª¬π * g (y‚Åª¬π * x) ‚àÇŒº) ‚àÇŒΩ := by
      apply integral_integral_swap_of_hasCompactSupport
      ¬∑ apply Continuous.mul
        ¬∑ exact (hf.comp continuous_fst).mul
            ((D_cont.comp continuous_fst).inv‚ÇÄ (fun x ‚Ü¶ (D_pos _).ne'))
        ¬∑ exact hg.comp (continuous_snd.inv.mul continuous_fst)
      ¬∑ let K := tsupport f
        have K_comp : IsCompact K := h'f
        let L := tsupport g
        have L_comp : IsCompact L := h'g
        let M := (fun (p : G √ó G) ‚Ü¶ p.1 * p.2‚Åª¬π) '' (K √óÀ¢ L)
        have M_comp : IsCompact M :=
          (K_comp.prod L_comp).image (continuous_fst.mul continuous_snd.inv)
        have M'_comp : IsCompact (closure M) := M_comp.closure
        have : ‚àÄ (p : G √ó G), p ‚àâ K √óÀ¢ closure M ‚Üí f p.1 * (D p.1)‚Åª¬π * g (p.2‚Åª¬π * p.1) = 0 := by
          rintro ‚ü®x, y‚ü© hxy
          by_cases H : x ‚àà K; swap
          ¬∑ simp [image_eq_zero_of_notMem_tsupport H]
          have : g (y‚Åª¬π * x) = 0 := by
            apply image_eq_zero_of_notMem_tsupport
            contrapose! hxy
            simp only [mem_prod, H, true_and]
            apply subset_closure
            simp only [M, mem_image, mem_prod, Prod.exists]
            exact ‚ü®x, y‚Åª¬π * x, ‚ü®H, hxy‚ü©, by group‚ü©
          simp [this]
        apply HasCompactSupport.intro' (K_comp.prod M'_comp) ?_ this
        exact (isClosed_tsupport f).prod isClosed_closure
  _ = ‚à´ y, (‚à´ x, f (y * x) * (D (y * x))‚Åª¬π * g x ‚àÇŒº) ‚àÇŒΩ := by
      congr with y
      rw [‚Üê integral_mul_left_eq_self _ y]
      simp
  _ = ‚à´ x, (‚à´ y, f (y * x) * (D (y * x))‚Åª¬π * g x ‚àÇŒΩ) ‚àÇŒº := by
      apply (integral_integral_swap_of_hasCompactSupport _ _).symm
      ¬∑ apply Continuous.mul ?_ (hg.comp continuous_fst)
        exact (hf.comp (continuous_snd.mul continuous_fst)).mul
          ((D_cont.comp (continuous_snd.mul continuous_fst)).inv‚ÇÄ (fun x ‚Ü¶ (D_pos _).ne'))
      ¬∑ let K := tsupport f
        have K_comp : IsCompact K := h'f
        let L := tsupport g
        have L_comp : IsCompact L := h'g
        let M := (fun (p : G √ó G) ‚Ü¶ p.1 * p.2‚Åª¬π) '' (K √óÀ¢ L)
        have M_comp : IsCompact M :=
          (K_comp.prod L_comp).image (continuous_fst.mul continuous_snd.inv)
        have M'_comp : IsCompact (closure M) := M_comp.closure
        have : ‚àÄ (p : G √ó G), p ‚àâ L √óÀ¢ closure M ‚Üí
            f (p.2 * p.1) * (D (p.2 * p.1))‚Åª¬π * g p.1 = 0 := by
          rintro ‚ü®x, y‚ü© hxy
          by_cases H : x ‚àà L; swap
          ¬∑ simp [image_eq_zero_of_notMem_tsupport H]
          have : f (y * x) = 0 := by
            apply image_eq_zero_of_notMem_tsupport
            contrapose! hxy
            simp only [mem_prod, H, true_and]
            apply subset_closure
            simp only [M, mem_image, mem_prod, Prod.exists]
            exact ‚ü®y * x, x, ‚ü®hxy, H‚ü©, by group‚ü©
          simp [this]
        apply HasCompactSupport.intro' (L_comp.prod M'_comp) ?_ this
        exact (isClosed_tsupport g).prod isClosed_closure
  _ = ‚à´ x, (‚à´ y, f y * (D y)‚Åª¬π ‚àÇŒΩ) * g x ‚àÇŒº := by
      simp_rw [integral_mul_const]
      congr with x
      conv_rhs => rw [‚Üê integral_mul_right_eq_self _ x]
  _ = (‚à´ y, f y * (D y)‚Åª¬π ‚àÇŒΩ) * ‚à´ x, g x ‚àÇŒº := integral_const_mul _ _

/-- Given two left-invariant measures which are finite on
compacts, they coincide in the following sense: they give the same value to the integral of
continuous compactly supported functions, up to a multiplicative constant. -/
@[to_additive exists_integral_isAddLeftInvariant_eq_smul_of_hasCompactSupport]
lemma exists_integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport (Œº' Œº : Measure G)
    [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] :
    ‚àÉ (c : ‚Ñù‚â•0), ‚àÄ (f : G ‚Üí ‚Ñù), Continuous f ‚Üí HasCompactSupport f ‚Üí
      ‚à´ x, f x ‚àÇŒº' = ‚à´ x, f x ‚àÇ(c ‚Ä¢ Œº) := by
  -- The group has to be locally compact, otherwise all integrals vanish and the result is trivial.
  by_cases H : LocallyCompactSpace G; swap
  ¬∑ refine ‚ü®0, fun f f_cont f_comp ‚Ü¶ ?_‚ü©
    rcases f_comp.eq_zero_or_locallyCompactSpace_of_group f_cont with hf|hf
    ¬∑ simp [hf]
    ¬∑ exact (H hf).elim
  -- Fix some nonzero continuous function with compact support `g`.
  obtain ‚ü®‚ü®g, g_cont‚ü©, g_comp, g_nonneg, g_one‚ü© :
    ‚àÉ (g : C(G, ‚Ñù)), HasCompactSupport g ‚àß 0 ‚â§ g ‚àß g 1 ‚â† 0 := exists_continuous_nonneg_pos 1
  have int_g_pos : 0 < ‚à´ x, g x ‚àÇŒº :=
    g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one
  -- The proportionality constant we are looking for will be the ratio of the integrals of `g`
  -- with respect to `Œº'` and `Œº`.
  let c : ‚Ñù := (‚à´ x, g x ‚àÇŒº) ‚Åª¬π * (‚à´ x, g x ‚àÇŒº')
  have c_nonneg : 0 ‚â§ c :=
    mul_nonneg (inv_nonneg.2 (integral_nonneg g_nonneg)) (integral_nonneg g_nonneg)
  refine ‚ü®‚ü®c, c_nonneg‚ü©, fun f f_cont f_comp ‚Ü¶ ?_‚ü©
  /- use the lemma `integral_mulLeftInvariant_mulRightInvariant_combo` for `Œº` and then `Œº'`
  to reexpress the integral of `f` as the integral of `g` times a factor which only depends
  on a right-invariant measure `ŒΩ`. We use `ŒΩ = Œº.inv` for convenience. -/
  let ŒΩ := Œº.inv
  have A : ‚à´ x, f x ‚àÇŒº = (‚à´ y, f y * (‚à´ z, g (z‚Åª¬π * y) ‚àÇŒΩ)‚Åª¬π ‚àÇŒΩ) * ‚à´ x, g x ‚àÇŒº :=
    integral_isMulLeftInvariant_isMulRightInvariant_combo f_cont f_comp g_cont g_comp g_nonneg g_one
  rw [‚Üê mul_inv_eq_iff_eq_mul‚ÇÄ int_g_pos.ne'] at A
  have B : ‚à´ x, f x ‚àÇŒº' = (‚à´ y, f y * (‚à´ z, g (z‚Åª¬π * y) ‚àÇŒΩ)‚Åª¬π ‚àÇŒΩ) * ‚à´ x, g x ‚àÇŒº' :=
    integral_isMulLeftInvariant_isMulRightInvariant_combo f_cont f_comp g_cont g_comp g_nonneg g_one
  /- Since the `ŒΩ`-factor is the same for `Œº` and `Œº'`, this gives the result. -/
  rw [‚Üê A, mul_assoc, mul_comm] at B
  simp [B, integral_smul_nnreal_measure, c, NNReal.smul_def]

open scoped Classical in
/-- Given two left-invariant measures which are finite on compacts, `haarScalarFactor Œº' Œº` is a
scalar such that `‚à´ f dŒº' = (haarScalarFactor Œº' Œº) ‚à´ f dŒº` for any compactly supported continuous
function `f`.

Note that there is a dissymmetry in the assumptions between `Œº'` and `Œº`: the measure `Œº'` needs
only be finite on compact sets, while `Œº` has to be finite on compact sets and positive on open
sets, i.e., a Haar measure, to exclude for instance the case where `Œº = 0`, where the definition
doesn't make sense. -/
@[to_additive /-- Given two left-invariant measures which are finite on compacts,
`addHaarScalarFactor Œº' Œº` is a scalar such that `‚à´ f dŒº' = (addHaarScalarFactor Œº' Œº) ‚à´ f dŒº` for
any compactly supported continuous function `f`.

Note that there is a dissymmetry in the assumptions between `Œº'` and `Œº`: the measure `Œº'` needs
only be finite on compact sets, while `Œº` has to be finite on compact sets and positive on open
sets, i.e., an additive Haar measure, to exclude for instance the case where `Œº = 0`, where the
definition doesn't make sense. -/]
noncomputable def haarScalarFactor
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] :
    ‚Ñù‚â•0 :=
  if ¬¨ LocallyCompactSpace G then 1
  else (exists_integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº' Œº).choose

/-- Two left invariant measures integrate in the same way continuous compactly supported functions,
up to the scalar `haarScalarFactor Œº' Œº`. See also
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which gives the same result for compact
sets, and `measure_isHaarMeasure_eq_smul_of_isOpen` for open sets. -/
@[to_additive integral_isAddLeftInvariant_eq_smul_of_hasCompactSupport
/-- Two left invariant measures integrate in the same way continuous compactly supported functions,
up to the scalar `addHaarScalarFactor Œº' Œº`. See also
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which gives the same result for compact
sets, and `measure_isAddHaarMeasure_eq_smul_of_isOpen` for open sets. -/]
theorem integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    {f : G ‚Üí ‚Ñù} (hf : Continuous f) (h'f : HasCompactSupport f) :
    ‚à´ x, f x ‚àÇŒº' = ‚à´ x, f x ‚àÇ(haarScalarFactor Œº' Œº ‚Ä¢ Œº) := by
  classical
  rcases h'f.eq_zero_or_locallyCompactSpace_of_group hf with Hf|Hf
  ¬∑ simp [Hf]
  ¬∑ simp only [haarScalarFactor, Hf, not_true_eq_false, ite_false]
    exact (exists_integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº' Œº).choose_spec
      f hf h'f

@[to_additive addHaarScalarFactor_eq_integral_div]
lemma haarScalarFactor_eq_integral_div (Œº' Œº : Measure G) [IsHaarMeasure Œº]
    [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] {f : G ‚Üí ‚Ñù} (hf : Continuous f)
    (h'f : HasCompactSupport f) (int_nonzero : ‚à´ x, f x ‚àÇŒº ‚â† 0) :
    haarScalarFactor Œº' Œº = (‚à´ x, f x ‚àÇŒº') / ‚à´ x, f x ‚àÇŒº := by
  have := integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº' Œº hf h'f
  rw [integral_smul_nnreal_measure] at this
  exact EuclideanDomain.eq_div_of_mul_eq_left int_nonzero this.symm

@[to_additive (attr := simp) addHaarScalarFactor_smul]
lemma haarScalarFactor_smul [LocallyCompactSpace G] (Œº' Œº : Measure G) [IsHaarMeasure Œº]
    [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] {c : ‚Ñù‚â•0} :
    haarScalarFactor (c ‚Ä¢ Œº') Œº = c ‚Ä¢ haarScalarFactor Œº' Œº := by
  obtain ‚ü®‚ü®g, g_cont‚ü©, g_comp, g_nonneg, g_one‚ü© :
    ‚àÉ g : C(G, ‚Ñù), HasCompactSupport g ‚àß 0 ‚â§ g ‚àß g 1 ‚â† 0 := exists_continuous_nonneg_pos 1
  have int_g_ne_zero : ‚à´ x, g x ‚àÇŒº ‚â† 0 :=
    ne_of_gt (g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one)
  apply NNReal.coe_injective
  calc
    haarScalarFactor (c ‚Ä¢ Œº') Œº = (‚à´ x, g x ‚àÇ(c ‚Ä¢ Œº')) / ‚à´ x, g x ‚àÇŒº :=
      haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero
    _ = (c ‚Ä¢ (‚à´ x, g x ‚àÇŒº')) / ‚à´ x, g x ‚àÇŒº := by simp
    _ = c ‚Ä¢ ((‚à´ x, g x ‚àÇŒº') / ‚à´ x, g x ‚àÇŒº) := smul_div_assoc c _ _
    _ = c ‚Ä¢ haarScalarFactor Œº' Œº := by
      rw [‚Üê haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero]

@[to_additive mul_addHaarScalarFactor_smul]
lemma mul_haarScalarFactor_smul [LocallyCompactSpace G] (Œº' Œº : Measure G)
    [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] {c : ‚Ñù‚â•0}
    (hc : c ‚â† 0) :
    haveI : IsHaarMeasure (c ‚Ä¢ Œº) := IsHaarMeasure.nnreal_smul _ hc
    c * haarScalarFactor Œº' (c ‚Ä¢ Œº) = haarScalarFactor Œº' Œº := by
  have : IsHaarMeasure (c ‚Ä¢ Œº) := IsHaarMeasure.nnreal_smul _ hc
  obtain ‚ü®‚ü®g, g_cont‚ü©, g_comp, g_nonneg, g_one‚ü© :
    ‚àÉ g : C(G, ‚Ñù), HasCompactSupport g ‚àß 0 ‚â§ g ‚àß g 1 ‚â† 0 := exists_continuous_nonneg_pos 1
  have int_g_ne_zero : ‚à´ x, g x ‚àÇŒº ‚â† 0 :=
    ne_of_gt (g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one)
  apply NNReal.coe_injective
  calc
    c * haarScalarFactor Œº' (c ‚Ä¢ Œº) = c * ((‚à´ x, g x ‚àÇŒº') / ‚à´ x, g x ‚àÇ(c ‚Ä¢ Œº)) :=
      by rw [haarScalarFactor_eq_integral_div _ _ g_cont g_comp (by simp [int_g_ne_zero, hc])]
    _ = c * ((‚à´ x, g x ‚àÇŒº') / (c ‚Ä¢ ‚à´ x, g x ‚àÇŒº)) := by simp
    _ = (‚à´ x, g x ‚àÇŒº') / (‚à´ x, g x ‚àÇŒº) := by
      rw [NNReal.smul_def, smul_eq_mul, ‚Üê mul_div_assoc]
      exact mul_div_mul_left (‚à´ (x : G), g x ‚àÇŒº') (‚à´ (x : G), g x ‚àÇŒº) (by simp [hc])
    _ = Œº'.haarScalarFactor Œº :=
      (haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero).symm

@[to_additive addHaarScalarFactor_smul_smul]
lemma haarScalarFactor_smul_smul [LocallyCompactSpace G] (Œº' Œº : Measure G)
    [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] {c : ‚Ñù‚â•0}
    (hc : c ‚â† 0) :
    haveI : IsHaarMeasure (c ‚Ä¢ Œº) := IsHaarMeasure.nnreal_smul _ hc
    haarScalarFactor (c ‚Ä¢ Œº') (c ‚Ä¢ Œº) = haarScalarFactor Œº' Œº := by
  rw [haarScalarFactor_smul, smul_eq_mul, mul_haarScalarFactor_smul _ _ hc]

@[to_additive (attr := simp)]
lemma haarScalarFactor_self (Œº : Measure G) [IsHaarMeasure Œº] :
    haarScalarFactor Œº Œº = 1 := by
  by_cases hG : LocallyCompactSpace G; swap
  ¬∑ simp [haarScalarFactor, hG]
  obtain ‚ü®‚ü®g, g_cont‚ü©, g_comp, g_nonneg, g_one‚ü© :
    ‚àÉ g : C(G, ‚Ñù), HasCompactSupport g ‚àß 0 ‚â§ g ‚àß g 1 ‚â† 0 := exists_continuous_nonneg_pos 1
  have int_g_ne_zero : ‚à´ x, g x ‚àÇŒº ‚â† 0 :=
    ne_of_gt (g_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero g_comp g_nonneg g_one)
  apply NNReal.coe_injective
  calc
    haarScalarFactor Œº Œº = (‚à´ x, g x ‚àÇŒº) / ‚à´ x, g x ‚àÇŒº :=
      haarScalarFactor_eq_integral_div _ _ g_cont g_comp int_g_ne_zero
    _ = 1 := div_self int_g_ne_zero

@[to_additive addHaarScalarFactor_eq_mul]
lemma haarScalarFactor_eq_mul (Œº' Œº ŒΩ : Measure G)
    [IsHaarMeasure Œº] [IsHaarMeasure ŒΩ] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] :
    haarScalarFactor Œº' ŒΩ = haarScalarFactor Œº' Œº * haarScalarFactor Œº ŒΩ := by
  -- The group has to be locally compact, otherwise the scalar factor is 1 by definition.
  by_cases hG : LocallyCompactSpace G; swap
  ¬∑ simp [haarScalarFactor, hG]
  -- Fix some nonzero continuous function with compact support `g`.
  obtain ‚ü®‚ü®g, g_cont‚ü©, g_comp, g_nonneg, g_one‚ü© :
    ‚àÉ (g : C(G, ‚Ñù)), HasCompactSupport g ‚àß 0 ‚â§ g ‚àß g 1 ‚â† 0 := exists_continuous_nonneg_pos 1
  have Z := integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº' Œº g_cont g_comp
  simp only [integral_smul_nnreal_measure, smul_smul,
    integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº' ŒΩ g_cont g_comp,
    integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº ŒΩ g_cont g_comp] at Z
  have int_g_pos : 0 < ‚à´ x, g x ‚àÇŒΩ := by
    apply (integral_pos_iff_support_of_nonneg g_nonneg _).2
    ¬∑ exact IsOpen.measure_pos ŒΩ g_cont.isOpen_support ‚ü®1, g_one‚ü©
    ¬∑ exact g_cont.integrable_of_hasCompactSupport g_comp
  change (haarScalarFactor Œº' ŒΩ : ‚Ñù) * ‚à´ (x : G), g x ‚àÇŒΩ =
    (haarScalarFactor Œº' Œº * haarScalarFactor Œº ŒΩ : ‚Ñù‚â•0) * ‚à´ (x : G), g x ‚àÇŒΩ at Z
  simpa only [mul_eq_mul_right_iff (M‚ÇÄ := ‚Ñù), int_g_pos.ne', or_false, ‚Üê NNReal.eq_iff] using Z

/-- The scalar factor between two left-invariant measures is non-zero when both measures are
positive on open sets. -/
@[to_additive]
lemma haarScalarFactor_pos_of_isHaarMeasure (Œº' Œº : Measure G) [IsHaarMeasure Œº]
    [IsHaarMeasure Œº'] : 0 < haarScalarFactor Œº' Œº :=
  pos_iff_ne_zero.2 (fun H ‚Ü¶ by simpa [H] using haarScalarFactor_eq_mul Œº' Œº Œº')

/-!
### Uniqueness of measure of sets with compact closure

Two left invariant measures give the same measure to sets with compact closure, up to the
scalar `haarScalarFactor Œº' Œº`.

This is a tricky argument, typically not done in textbooks (the textbooks version all require one
version of regularity or another). Here is a sketch, based on
McQuillan's answer at https://mathoverflow.net/questions/456670/.

Assume for simplicity that all measures are normalized, so that the scalar factors are all `1`.
First, from the fact that `Œº` and `Œº'` integrate in the same way compactly supported functions,
they give the same measure to compact "zero sets", i.e., sets of the form `f‚Åª¬π {1}`
for `f` continuous and compactly supported.
See `measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport`.

If `Œº` is inner regular, a theorem of Halmos shows that any measurable set `s` of finite measure can
be approximated from inside by a compact zero set `k`. Then `Œº s ‚â§ Œº k + Œµ = Œº' k + Œµ ‚â§ Œº' s + Œµ`.
Letting `Œµ` tend to zero, one gets `Œº s ‚â§ Œº' s`.
See `smul_measure_isMulInvariant_le_of_isCompact_closure`.

Assume now that `s` is a measurable set of compact closure. It is contained in a compact
zero set `t`. The same argument applied to `t - s` gives `Œº (t \ s) ‚â§ Œº' (t \ s)`, i.e.,
`Œº t - Œº s ‚â§ Œº' t - Œº' s`. As `Œº t = Œº' t` (since these are zero sets), we get the inequality
`Œº' s ‚â§ Œº s`. Together with the previous one, this gives `Œº' s = Œº s`.
See `measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop`.

If neither `Œº` nor `Œº'` is inner regular, we can use the existence of another inner regular
left-invariant measure `ŒΩ`, so get `Œº s = ŒΩ s = Œº' s`, by applying twice the previous argument.
Here, the uniqueness argument uses the existence of a Haar measure with a nice behavior!
See `measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet`.

Finally, if `s` has compact closure but is not measurable, its measure is the infimum of the
measures of its measurable supersets, and even of those contained in `closure s`. As `Œº`
and `Œº'` coincide on these supersets, this yields `Œº s = Œº' s`.
See `measure_isMulInvariant_eq_smul_of_isCompact_closure`.
-/

/-- Two left invariant measures give the same mass to level sets of continuous compactly supported
functions, up to the scalar `haarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure. -/
@[to_additive measure_preimage_isAddLeftInvariant_eq_smul_of_hasCompactSupport
/-- Two left invariant measures give the same mass to level sets of continuous compactly supported
functions, up to the scalar `addHaarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure. -/]
lemma measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    {f : G ‚Üí ‚Ñù} (hf : Continuous f) (h'f : HasCompactSupport f) :
    Œº' (f ‚Åª¬π' {1}) = haarScalarFactor Œº' Œº ‚Ä¢ Œº (f ‚Åª¬π' {1}) := by
  /- This follows from the fact that the two measures integrate in the same way continuous
  functions, by approximating the indicator function of `f ‚Åª¬π' {1}` by continuous functions
  (namely `v‚Çô ‚àò f` where `v‚Çô` is equal to `1` at `1`, and `0` outside of a small neighborhood
  `(1 - u‚Çô, 1 + u‚Çô)` where `u‚Çô` is a sequence tending to `0`).
  We use `v‚Çô = thickenedIndicator u‚Çô {1}` to take advantage of existing lemmas. -/
  obtain ‚ü®u, -, u_mem, u_lim‚ü© : ‚àÉ u, StrictAnti u ‚àß (‚àÄ (n : ‚Ñï), u n ‚àà Ioo 0 1)
    ‚àß Tendsto u atTop (ùìù 0) := exists_seq_strictAnti_tendsto' (zero_lt_one : (0 : ‚Ñù) < 1)
  let v : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚Ñù := fun n x ‚Ü¶ thickenedIndicator (u_mem n).1 ({1} : Set ‚Ñù) x
  have vf_cont n : Continuous ((v n) ‚àò f) := by
    apply Continuous.comp (continuous_induced_dom.comp ?_) hf
    exact BoundedContinuousFunction.continuous (thickenedIndicator (u_mem n).left {1})
  have I : ‚àÄ (ŒΩ : Measure G), IsFiniteMeasureOnCompacts ŒΩ ‚Üí
      Tendsto (fun n ‚Ü¶ ‚à´ x, v n (f x) ‚àÇŒΩ) atTop
      (ùìù (‚à´ x, Set.indicator ({1} : Set ‚Ñù) (fun _ ‚Ü¶ 1) (f x) ‚àÇŒΩ)) := by
    intro ŒΩ hŒΩ
    apply tendsto_integral_of_dominated_convergence
        (bound := (tsupport f).indicator (fun (_ : G) ‚Ü¶ (1 : ‚Ñù)) )
    ¬∑ exact fun n ‚Ü¶ (vf_cont n).aestronglyMeasurable
    ¬∑ apply IntegrableOn.integrable_indicator _ (isClosed_tsupport f).measurableSet
      simpa using IsCompact.measure_lt_top h'f
    ¬∑ refine fun n ‚Ü¶ Eventually.of_forall (fun x ‚Ü¶ ?_)
      by_cases hx : x ‚àà tsupport f
      ¬∑ simp only [v, Real.norm_eq_abs, NNReal.abs_eq, hx, indicator_of_mem]
        norm_cast
        exact thickenedIndicator_le_one _ _ _
      ¬∑ simp only [v, Real.norm_eq_abs, NNReal.abs_eq, hx, not_false_eq_true, indicator_of_notMem]
        rw [thickenedIndicator_zero]
        ¬∑ simp
        ¬∑ simpa [image_eq_zero_of_notMem_tsupport hx] using (u_mem n).2.le
    ¬∑ filter_upwards with x
      have T := tendsto_pi_nhds.1 (thickenedIndicator_tendsto_indicator_closure
        (fun n ‚Ü¶ (u_mem n).1) u_lim ({1} : Set ‚Ñù)) (f x)
      simp only [thickenedIndicator_apply, closure_singleton] at T
      convert NNReal.tendsto_coe.2 T
      simp
  have M n : ‚à´ (x : G), v n (f x) ‚àÇŒº' = ‚à´ (x : G), v n (f x) ‚àÇ(haarScalarFactor Œº' Œº ‚Ä¢ Œº) := by
    apply integral_isMulLeftInvariant_eq_smul_of_hasCompactSupport Œº' Œº (vf_cont n)
    apply h'f.comp_left
    simp only [v, thickenedIndicator_apply, NNReal.coe_eq_zero]
    rw [thickenedIndicatorAux_zero (u_mem n).1]
    ¬∑ simp only [ENNReal.toNNReal_zero]
    ¬∑ simpa using (u_mem n).2.le
  have I1 := I Œº' (by infer_instance)
  simp_rw [M] at I1
  have J1 : ‚à´ (x : G), indicator {1} (fun _ ‚Ü¶ (1 : ‚Ñù)) (f x) ‚àÇŒº'
      = ‚à´ (x : G), indicator {1} (fun _ ‚Ü¶ 1) (f x) ‚àÇ(haarScalarFactor Œº' Œº ‚Ä¢ Œº) :=
    tendsto_nhds_unique I1 (I (haarScalarFactor Œº' Œº ‚Ä¢ Œº) (by infer_instance))
  have J2 : Œº'.real (f ‚Åª¬π' {1})
      = (haarScalarFactor Œº' Œº ‚Ä¢ Œº).real (f ‚Åª¬π' {1}) := by
    have : (fun x ‚Ü¶ indicator {1} (fun _ ‚Ü¶ (1 : ‚Ñù)) (f x)) =
        (fun x ‚Ü¶ indicator (f ‚Åª¬π' {1}) (fun _ ‚Ü¶ (1 : ‚Ñù)) x) := by
      ext x
      exact (indicator_comp_right f (s := ({1} : Set ‚Ñù)) (g := (fun _ ‚Ü¶ (1 : ‚Ñù))) (x := x)).symm
    have mf : MeasurableSet (f ‚Åª¬π' {1}) := (isClosed_singleton.preimage hf).measurableSet
    simpa only [this, mf, integral_indicator_const, smul_eq_mul, mul_one, Pi.smul_apply,
      nnreal_smul_coe_apply, ENNReal.toReal_mul, ENNReal.coe_toReal] using J1
  have C : IsCompact (f ‚Åª¬π' {1}) := h'f.isCompact_preimage hf isClosed_singleton (by simp)
  rw [measureReal_eq_measureReal_iff C.measure_lt_top.ne C.measure_lt_top.ne] at J2
  simpa using J2

/-- If an invariant measure is inner regular, then it gives less mass to sets with compact closure
than any other invariant measure, up to the scalar `haarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which gives equality for any
set with compact closure. -/
@[to_additive smul_measure_isAddInvariant_le_of_isCompact_closure
/-- If an invariant measure is inner regular, then it gives less mass to sets with compact closure
than any other invariant measure, up to the scalar `addHaarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which gives equality for any
set with compact closure. -/]
lemma smul_measure_isMulInvariant_le_of_isCompact_closure [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    [InnerRegularCompactLTTop Œº]
    {s : Set G} (hs : MeasurableSet s) (h's : IsCompact (closure s)) :
    haarScalarFactor Œº' Œº ‚Ä¢ Œº s ‚â§ Œº' s := by
  apply le_of_forall_lt (fun r hr ‚Ü¶ ?_)
  let ŒΩ := haarScalarFactor Œº' Œº ‚Ä¢ Œº
  have : ŒΩ s ‚â† ‚àû := ((measure_mono subset_closure).trans_lt h's.measure_lt_top).ne
  obtain ‚ü®-, hf, ‚ü®f, f_cont, f_comp, rfl‚ü©, ŒΩf‚ü© :
      ‚àÉ K ‚äÜ s, (‚àÉ f, Continuous f ‚àß HasCompactSupport f ‚àß K = f ‚Åª¬π' {1}) ‚àß r < ŒΩ K :=
    innerRegularWRT_preimage_one_hasCompactSupport_measure_ne_top_of_group ‚ü®hs, this‚ü© r
      (by convert hr)
  calc
  r < ŒΩ (f ‚Åª¬π' {1}) := ŒΩf
  _ = Œº' (f ‚Åª¬π' {1}) :=
    (measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport _ _ f_cont f_comp).symm
  _ ‚â§ Œº' s := measure_mono hf

/-- If an invariant measure is inner regular, then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `haarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure, and removes the inner regularity assumption. -/
@[to_additive measure_isAddInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop
/-- If an invariant measure is inner regular, then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `addHaarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which works for any set with
compact closure, and removes the inner regularity assumption. -/]
lemma measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop
    [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    [InnerRegularCompactLTTop Œº]
    {s : Set G} (hs : MeasurableSet s) (h's : IsCompact (closure s)) :
    Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s := by
  apply le_antisymm ?_ (smul_measure_isMulInvariant_le_of_isCompact_closure Œº' Œº hs h's)
  let ŒΩ := haarScalarFactor Œº' Œº ‚Ä¢ Œº
  change Œº' s ‚â§ ŒΩ s
  obtain ‚ü®‚ü®f, f_cont‚ü©, hf, -, f_comp, -‚ü© : ‚àÉ f : C(G, ‚Ñù), EqOn f 1 (closure s) ‚àß EqOn f 0 ‚àÖ
      ‚àß HasCompactSupport f ‚àß ‚àÄ x, f x ‚àà Icc (0 : ‚Ñù) 1 :=
    exists_continuous_one_zero_of_isCompact h's isClosed_empty (disjoint_empty _)
  let t := f ‚Åª¬π' {1}
  have t_closed : IsClosed t := isClosed_singleton.preimage f_cont
  have t_comp : IsCompact t := f_comp.isCompact_preimage f_cont isClosed_singleton (by simp)
  have st : s ‚äÜ t := (IsClosed.closure_subset_iff t_closed).mp hf
  have A : ŒΩ (t \ s) ‚â§ Œº' (t \ s) := by
    apply smul_measure_isMulInvariant_le_of_isCompact_closure _ _ (t_closed.measurableSet.diff hs)
    exact t_comp.closure_of_subset diff_subset
  have B : Œº' t = ŒΩ t :=
    measure_preimage_isMulLeftInvariant_eq_smul_of_hasCompactSupport _ _ f_cont f_comp
  rwa [measure_diff st hs.nullMeasurableSet, measure_diff st hs.nullMeasurableSet, ‚Üê B,
    ENNReal.sub_le_sub_iff_left] at A
  ¬∑ exact measure_mono st
  ¬∑ exact t_comp.measure_lt_top.ne
  ¬∑ exact ((measure_mono st).trans_lt t_comp.measure_lt_top).ne
  ¬∑ exact ((measure_mono st).trans_lt t_comp.measure_lt_top).ne

/-- Given an invariant measure then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `haarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isMulInvariant_eq_smul_of_isCompact_closure`, which removes the
measurability assumption. -/
@[to_additive measure_isAddInvariant_eq_smul_of_isCompact_closure_of_measurableSet
/-- Given an invariant measure then it gives the same mass to measurable sets with
compact closure as any other invariant measure, up to the scalar `addHaarScalarFactor Œº' Œº`.

Auxiliary lemma in the proof of the more general
`measure_isAddInvariant_eq_smul_of_isCompact_closure`, which removes the
measurability assumption. -/]
lemma measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    {s : Set G} (hs : MeasurableSet s) (h's : IsCompact (closure s)) :
    Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s := by
  let ŒΩ : Measure G := haar
  have A : Œº' s = haarScalarFactor Œº' ŒΩ ‚Ä¢ ŒΩ s :=
    measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop Œº' ŒΩ hs h's
  have B : Œº s = haarScalarFactor Œº ŒΩ ‚Ä¢ ŒΩ s :=
    measure_isMulInvariant_eq_smul_of_isCompact_closure_of_innerRegularCompactLTTop Œº ŒΩ hs h's
  rw [A, B, smul_smul, haarScalarFactor_eq_mul Œº' Œº ŒΩ]

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite on compacts, they coincide in the following
sense: they give the same value to sets with compact closure, up to the multiplicative
constant `haarScalarFactor Œº' Œº`. -/
@[to_additive measure_isAddInvariant_eq_smul_of_isCompact_closure
/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite on compacts, they coincide in the following
sense: they give the same value to sets with compact closure, up to the multiplicative
constant `addHaarScalarFactor Œº' Œº`. -/]
theorem measure_isMulInvariant_eq_smul_of_isCompact_closure [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    {s : Set G} (h's : IsCompact (closure s)) :
    Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s := by
  let ŒΩ := haarScalarFactor Œº' Œº ‚Ä¢ Œº
  apply le_antisymm
  ¬∑ calc
    Œº' s ‚â§ Œº' ((toMeasurable ŒΩ s) ‚à© (closure s)) :=
      measure_mono <| subset_inter (subset_toMeasurable ŒΩ s) subset_closure
    _ = ŒΩ ((toMeasurable ŒΩ s) ‚à© (closure s)) := by
      apply measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet _ _ _ _
      ¬∑ exact (measurableSet_toMeasurable ŒΩ s).inter isClosed_closure.measurableSet
      ¬∑ exact h's.closure_of_subset inter_subset_right
    _ ‚â§ ŒΩ (toMeasurable ŒΩ s) := measure_mono inter_subset_left
    _ = ŒΩ s := measure_toMeasurable s
  ¬∑ calc
    ŒΩ s ‚â§ ŒΩ ((toMeasurable Œº' s) ‚à© (closure s)) :=
      measure_mono <| subset_inter (subset_toMeasurable Œº' s) subset_closure
    _ = Œº' ((toMeasurable Œº' s) ‚à© (closure s)) := by
      apply (measure_isMulInvariant_eq_smul_of_isCompact_closure_of_measurableSet _ _ _ _).symm
      ¬∑ exact (measurableSet_toMeasurable Œº' s).inter isClosed_closure.measurableSet
      ¬∑ exact h's.closure_of_subset inter_subset_right
    _ ‚â§ Œº' (toMeasurable Œº' s) := measure_mono inter_subset_left
    _ = Œº' s := measure_toMeasurable s

/-- **Uniqueness of Haar measures**:
Two Haar measures on a compact group coincide up to a multiplicative factor. -/
@[to_additive isAddInvariant_eq_smul_of_compactSpace]
lemma isMulInvariant_eq_smul_of_compactSpace [CompactSpace G] (Œº' Œº : Measure G)
    [IsHaarMeasure Œº] [IsMulLeftInvariant Œº'] [IsFiniteMeasureOnCompacts Œº'] :
    Œº' = haarScalarFactor Œº' Œº ‚Ä¢ Œº := by
  ext s _hs
  exact measure_isMulInvariant_eq_smul_of_isCompact_closure _ _ isClosed_closure.isCompact

@[to_additive]
instance (priority := 100) instInnerRegularOfIsHaarMeasureOfCompactSpace
    [CompactSpace G] (Œº : Measure G) [IsMulLeftInvariant Œº] [IsFiniteMeasureOnCompacts Œº] :
    InnerRegular Œº := by
  rw [isMulInvariant_eq_smul_of_compactSpace Œº haar]
  infer_instance

@[to_additive]
instance (priority := 100) instRegularOfIsHaarMeasureOfCompactSpace
    [CompactSpace G] (Œº : Measure G) [IsMulLeftInvariant Œº] [IsFiniteMeasureOnCompacts Œº] :
    Regular Œº := by
  rw [isMulInvariant_eq_smul_of_compactSpace Œº haar]
  infer_instance

/-- **Uniqueness of Haar measures**:
Two Haar measures which are probability measures coincide. -/
@[to_additive]
lemma isHaarMeasure_eq_of_isProbabilityMeasure [LocallyCompactSpace G] (Œº' Œº : Measure G)
    [IsProbabilityMeasure Œº] [IsProbabilityMeasure Œº'] [IsHaarMeasure Œº] [IsHaarMeasure Œº'] :
    Œº' = Œº := by
  have : CompactSpace G := by
    by_contra H
    rw [not_compactSpace_iff] at H
    simpa using measure_univ_of_isMulLeftInvariant Œº
  have A s : Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s :=
    measure_isMulInvariant_eq_smul_of_isCompact_closure _ _ isClosed_closure.isCompact
  have Z := A univ
  simp only [measure_univ, ENNReal.smul_def, smul_eq_mul, mul_one, ENNReal.one_eq_coe] at Z
  ext s _hs
  simp [A s, ‚Üê Z]

/-!
### Uniqueness of measure of open sets

Two Haar measures give the same measure to open sets (or more generally to sets which are everywhere
positive), up to the scalar `haarScalarFactor Œº' Œº `.
-/

@[to_additive measure_isAddHaarMeasure_eq_smul_of_isEverywherePos]
theorem measure_isHaarMeasure_eq_smul_of_isEverywherePos [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsHaarMeasure Œº']
    {s : Set G} (hs : MeasurableSet s) (h's : IsEverywherePos Œº s) :
    Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s := by
  let ŒΩ := haarScalarFactor Œº' Œº ‚Ä¢ Œº
  change Œº' s = ŒΩ s
  /- Fix a compact neighborhood `k` of the identity, and consider a maximal disjoint family `m` of
  sets `x ‚Ä¢ k` centered at points in `s`. Then `s` is covered by the sets `x ‚Ä¢ (k * k‚Åª¬π)` by
  maximality. If the family is countable, then since `Œº'` and `ŒΩ` coincide in compact sets, and
  the measure of a countable disjoint union is the sum of the measures, we get `Œº' s = ŒΩ s`.
  Otherwise, the family is uncountable, and each intersection with `s` has positive measure by
  the everywhere positivity assumption, so `ŒΩ s = ‚àû`, and `Œº' s = ‚àû` in the same way. -/
  obtain ‚ü®k, k_comp, k_closed, k_mem‚ü© : ‚àÉ k, IsCompact k ‚àß IsClosed k ‚àß k ‚àà ùìù (1 : G) := by
    rcases exists_compact_mem_nhds (1 : G) with ‚ü®k, hk, hmem‚ü©
    exact ‚ü®closure k, hk.closure, isClosed_closure, mem_of_superset hmem subset_closure‚ü©
  have one_k : 1 ‚àà k := mem_of_mem_nhds k_mem
  let A : Set (Set G) := {t | t ‚äÜ s ‚àß PairwiseDisjoint t (fun x ‚Ü¶ x ‚Ä¢ k)}
  obtain ‚ü®m, m_max‚ü© : ‚àÉ m, Maximal (¬∑ ‚àà A) m := by
    apply zorn_subset
    intro c cA hc
    refine ‚ü®‚ãÉ a ‚àà c, a, ‚ü®?_, ?_‚ü©, ?_‚ü©
    ¬∑ simp only [iUnion_subset_iff]
      intro a ac x hx
      simp only [A, subset_def, mem_setOf_eq] at cA
      exact (cA _ ac).1 x hx
    ¬∑ rintro x hx y hy hxy
      simp only [mem_iUnion, exists_prop] at hx hy
      rcases hx with ‚ü®a, ac, xa‚ü©
      rcases hy with ‚ü®b, bc, yb‚ü©
      obtain ‚ü®m, mc, am, bm‚ü© : ‚àÉ m ‚àà c, a ‚äÜ m ‚àß b ‚äÜ m := hc.directedOn _ ac _ bc
      exact (cA mc).2 (am xa) (bm yb) hxy
    ¬∑ intro a ac
      exact subset_biUnion_of_mem (u := id) ac
  obtain ‚ü®hms : m ‚äÜ s, hdj : PairwiseDisjoint m (fun x ‚Ü¶ x ‚Ä¢ k)‚ü© := m_max.prop
  have sm : s ‚äÜ ‚ãÉ x ‚àà m, x ‚Ä¢ (k * k‚Åª¬π) := by
    intro y hy
    by_cases h'y : m ‚à™ {y} ‚àà A
    ¬∑ have ym : y ‚àà m := m_max.mem_of_prop_insert (by simpa using h'y)
      have : y ‚àà y ‚Ä¢ (k * k‚Åª¬π) := by
        simpa using mem_leftCoset y (Set.mul_mem_mul one_k (Set.inv_mem_inv.mpr one_k))
      exact mem_biUnion ym this
    ¬∑ obtain ‚ü®x, xm, -, z, zy, zx‚ü© : ‚àÉ x ‚àà m, y ‚â† x ‚àß ‚àÉ z, z ‚àà y ‚Ä¢ k ‚àß z ‚àà x ‚Ä¢ k := by
        simpa [A, hms, hy, insert_subset_iff, pairwiseDisjoint_insert, hdj, not_disjoint_iff]
          using h'y
      have : y ‚àà x ‚Ä¢ (k * k‚Åª¬π) := by
        rw [show y = x * ((x‚Åª¬π * z) * (y‚Åª¬π * z)‚Åª¬π) by group]
        have : (x‚Åª¬π * z) * (y‚Åª¬π * z)‚Åª¬π ‚àà k * k‚Åª¬π := Set.mul_mem_mul ((mem_leftCoset_iff x).mp zx)
          (Set.inv_mem_inv.mpr ((mem_leftCoset_iff y).mp zy))
        exact mem_leftCoset x this
      exact mem_biUnion xm this
  rcases eq_empty_or_nonempty m with rfl | hm
  ¬∑ simp only [mem_empty_iff_false, iUnion_of_empty, iUnion_empty, subset_empty_iff] at sm
    simp [sm]
  by_cases h'm : Set.Countable m
  ¬∑ rcases h'm.exists_eq_range hm with ‚ü®f, rfl‚ü©
    have M i : MeasurableSet (disjointed (fun n ‚Ü¶ s ‚à© f n ‚Ä¢ (k * k‚Åª¬π)) i) := by
      apply MeasurableSet.disjointed (fun j ‚Ü¶ hs.inter ?_)
      have : IsClosed (k ‚Ä¢ k‚Åª¬π) := IsClosed.smul_left_of_isCompact k_closed.inv k_comp
      exact (IsClosed.smul this (f j)).measurableSet
    simp only [mem_range, iUnion_exists, iUnion_iUnion_eq'] at sm
    have s_eq : s = ‚ãÉ n, s ‚à© (f n ‚Ä¢ (k * k‚Åª¬π)) := by rwa [‚Üê inter_iUnion, eq_comm, inter_eq_left]
    have I : Œº' s = ‚àë' n, Œº' (disjointed (fun n ‚Ü¶ s ‚à© f n ‚Ä¢ (k * k‚Åª¬π)) n) := by
      rw [‚Üê measure_iUnion (disjoint_disjointed _) M, iUnion_disjointed, ‚Üê s_eq]
    have J : ŒΩ s = ‚àë' n, ŒΩ (disjointed (fun n ‚Ü¶ s ‚à© f n ‚Ä¢ (k * k‚Åª¬π)) n) := by
      rw [‚Üê measure_iUnion (disjoint_disjointed _) M, iUnion_disjointed, ‚Üê s_eq]
    rw [I, J]
    congr with n
    apply measure_isMulInvariant_eq_smul_of_isCompact_closure
    have : IsCompact (f n ‚Ä¢ (k * k‚Åª¬π)) := IsCompact.smul (f n) (k_comp.mul k_comp.inv)
    exact this.closure_of_subset <| (disjointed_subset _ _).trans inter_subset_right
  ¬∑ have H : ‚àÄ (œÅ : Measure G), IsEverywherePos œÅ s ‚Üí œÅ s = ‚àû := by
      intro œÅ hœÅ
      have M : ‚àÄ (i : ‚Üëm), MeasurableSet (s ‚à© (i : G) ‚Ä¢ k) :=
        fun i ‚Ü¶ hs.inter (IsClosed.smul k_closed _).measurableSet
      contrapose! h'm
      have : ‚àë' (x : m), œÅ (s ‚à© ((x : G) ‚Ä¢ k)) < ‚àû := by
        apply lt_of_le_of_lt (MeasureTheory.tsum_meas_le_meas_iUnion_of_disjoint _ M _) _
        ¬∑ have I : PairwiseDisjoint m fun x ‚Ü¶ s ‚à© x ‚Ä¢ k :=
            hdj.mono (fun x ‚Ü¶ inter_subset_right)
          exact I.on_injective Subtype.val_injective (fun x ‚Ü¶ x.2)
        ¬∑ exact lt_of_le_of_lt (measure_mono (by simp [inter_subset_left])) h'm.lt_top
      have C : Set.Countable (support fun (i : m) ‚Ü¶ œÅ (s ‚à© (i : G) ‚Ä¢ k)) :=
        Summable.countable_support_ennreal this.ne
      have : support (fun (i : m) ‚Ü¶ œÅ (s ‚à© (i : G) ‚Ä¢ k)) = univ := by
        refine eq_univ_iff_forall.2 fun i ‚Ü¶ ?_
        refine ne_of_gt (hœÅ (i : G) (hms i.2) _ ?_)
        exact inter_mem_nhdsWithin s (by simpa)
      rw [this] at C
      have : Countable m := countable_univ_iff.mp C
      exact to_countable m
    have HŒΩ : IsEverywherePos ŒΩ s :=
      h's.smul_measure_nnreal (haarScalarFactor_pos_of_isHaarMeasure _ _).ne'
    have HŒº' : IsEverywherePos Œº' s := by
      apply HŒΩ.of_forall_exists_nhds_eq (fun x _hx ‚Ü¶ ?_)
      obtain ‚ü®t, t_comp, t_mem‚ü© : ‚àÉ t, IsCompact t ‚àß t ‚àà ùìù x := exists_compact_mem_nhds x
      refine ‚ü®t, t_mem, fun u hu ‚Ü¶ ?_‚ü©
      apply measure_isMulInvariant_eq_smul_of_isCompact_closure
      exact t_comp.closure_of_subset hu
    rw [H ŒΩ HŒΩ, H Œº' HŒº']

/-- **Uniqueness of Haar measures**:
Given two Haar measures, they coincide in the following sense: they give the same value to open
sets, up to the multiplicative constant `haarScalarFactor Œº' Œº`. -/
@[to_additive measure_isAddHaarMeasure_eq_smul_of_isOpen
/-- **Uniqueness of Haar measures**:
Given two additive Haar measures, they coincide in the following sense: they give the same value to
open sets, up to the multiplicative constant `addHaarScalarFactor Œº' Œº`. -/]
theorem measure_isHaarMeasure_eq_smul_of_isOpen [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsHaarMeasure Œº'] {s : Set G} (hs : IsOpen s) :
    Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s :=
  measure_isHaarMeasure_eq_smul_of_isEverywherePos Œº' Œº hs.measurableSet hs.isEverywherePos

/-!
### Uniqueness of Haar measure under regularity assumptions.
-/

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite on
compacts and inner regular for finite measure sets with respect to compact sets,
they coincide in the following sense: they give the same value to finite measure sets,
up to a multiplicative constant. -/
@[to_additive]
lemma measure_isMulLeftInvariant_eq_smul_of_ne_top [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    [InnerRegularCompactLTTop Œº] [InnerRegularCompactLTTop Œº'] {s : Set G}
    (hs : Œº s ‚â† ‚àû) (h's : Œº' s ‚â† ‚àû) : Œº' s = haarScalarFactor Œº' Œº ‚Ä¢ Œº s := by
  /- We know that the measures integrate in the same way continuous compactly supported functions,
  up to the factor `c = haarScalarFactor Œº' Œº`. -/
  let c := haarScalarFactor Œº' Œº
  /- By regularity, every measurable set of finite measure may be approximated by compact sets.
  Therefore, the measures coincide on measurable sets of finite measure. -/
  have B : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí Œº' s < ‚àû ‚Üí Œº' s = (c ‚Ä¢ Œº) s := by
    intro s s_meas hs h's
    have : (c ‚Ä¢ Œº) s ‚â† ‚àû := by simp [ENNReal.mul_eq_top, hs.ne]
    rw [s_meas.measure_eq_iSup_isCompact_of_ne_top h's.ne,
        s_meas.measure_eq_iSup_isCompact_of_ne_top this]
    congr! 4 with K _Ks K_comp
    exact measure_isMulInvariant_eq_smul_of_isCompact_closure Œº' Œº K_comp.closure
  /- Finally, replace an arbitrary finite measure set with a measurable version, and use the
  version for measurable sets. -/
  let t := toMeasurable Œº' s ‚à© toMeasurable Œº s
  have st : s ‚äÜ t := subset_inter (subset_toMeasurable Œº' s) (subset_toMeasurable Œº s)
  have mu'_t : Œº' t = Œº' s := by
    apply le_antisymm
    ¬∑ exact (measure_mono inter_subset_left).trans (measure_toMeasurable s).le
    ¬∑ exact measure_mono st
  have mu_t : Œº t = Œº s := by
    apply le_antisymm
    ¬∑ exact (measure_mono inter_subset_right).trans (measure_toMeasurable s).le
    ¬∑ exact measure_mono st
  simp only [‚Üê mu'_t, ‚Üê mu_t, nnreal_smul_coe_apply]
  apply B
  ¬∑ exact (measurableSet_toMeasurable _ _).inter (measurableSet_toMeasurable _ _)
  ¬∑ exact mu_t.le.trans_lt hs.lt_top
  ¬∑ exact mu'_t.le.trans_lt h's.lt_top

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite
on compacts and inner regular, they coincide up to a multiplicative constant. -/
@[to_additive isAddLeftInvariant_eq_smul_of_innerRegular]
lemma isMulLeftInvariant_eq_smul_of_innerRegular [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    [InnerRegular Œº] [InnerRegular Œº'] :
    Œº' = haarScalarFactor Œº' Œº ‚Ä¢ Œº := by
  ext s hs
  rw [hs.measure_eq_iSup_isCompact, hs.measure_eq_iSup_isCompact]
  congr! 4 with K _Ks K_comp
  exact measure_isMulLeftInvariant_eq_smul_of_ne_top Œº' Œº K_comp.measure_lt_top.ne
    K_comp.measure_lt_top.ne

/-- **Uniqueness of left-invariant measures**:
Given two left-invariant measures which are finite
on compacts and regular, they coincide up to a multiplicative constant. -/
@[to_additive isAddLeftInvariant_eq_smul_of_regular]
lemma isMulLeftInvariant_eq_smul_of_regular [LocallyCompactSpace G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº']
    [Regular Œº] [Regular Œº'] :
    Œº' = haarScalarFactor Œº' Œº ‚Ä¢ Œº := by
  have A : ‚àÄ U, IsOpen U ‚Üí Œº' U = (haarScalarFactor Œº' Œº ‚Ä¢ Œº) U := by
    intro U hU
    rw [hU.measure_eq_iSup_isCompact, hU.measure_eq_iSup_isCompact]
    congr! 4 with K _KU K_comp
    exact measure_isMulLeftInvariant_eq_smul_of_ne_top Œº' Œº K_comp.measure_lt_top.ne
      K_comp.measure_lt_top.ne
  ext s _hs
  rw [s.measure_eq_iInf_isOpen, s.measure_eq_iInf_isOpen]
  congr! 4 with U _sU U_open
  exact A U U_open

/-- **Uniqueness of left-invariant measures**:
Two Haar measures coincide up to a multiplicative constant in a second countable group. -/
@[to_additive isAddLeftInvariant_eq_smul]
lemma isMulLeftInvariant_eq_smul [LocallyCompactSpace G] [SecondCountableTopology G]
    (Œº' Œº : Measure G) [IsHaarMeasure Œº] [IsFiniteMeasureOnCompacts Œº'] [IsMulLeftInvariant Œº'] :
    Œº' = haarScalarFactor Œº' Œº ‚Ä¢ Œº :=
  isMulLeftInvariant_eq_smul_of_regular Œº' Œº
  -- one could use as well `isMulLeftInvariant_eq_smul_of_innerRegular`, as in a
  -- second countable topological space all Haar measures are regular and inner regular


/-- An invariant œÉ-finite measure is absolutely continuous with respect to a Haar measure in a
second countable group. -/
@[to_additive
/-- An invariant measure is absolutely continuous with respect to an additive Haar measure. -/]
theorem absolutelyContinuous_isHaarMeasure [LocallyCompactSpace G]
    [SecondCountableTopology G] (Œº ŒΩ : Measure G)
    [SigmaFinite Œº] [IsMulLeftInvariant Œº] [IsHaarMeasure ŒΩ] : Œº ‚â™ ŒΩ := by
  have K : PositiveCompacts G := Classical.arbitrary _
  have h : haarMeasure K = (haarScalarFactor (haarMeasure K) ŒΩ : ‚Ñù‚â•0‚àû) ‚Ä¢ ŒΩ :=
    isMulLeftInvariant_eq_smul (haarMeasure K) ŒΩ
  rw [haarMeasure_unique Œº K, h, smul_smul]
  exact smul_absolutelyContinuous

/-- A continuous surjective monoid homomorphism of topological groups with compact codomain
is measure preserving, provided that the Haar measures on the domain and on the codomain
have the same total mass.
-/
@[to_additive
  /-- A continuous surjective additive monoid homomorphism of topological groups with compact
  codomain is measure preserving, provided that the Haar measures on the domain and on the codomain
  have the same total mass. -/]
theorem _root_.MonoidHom.measurePreserving
    {H : Type*} [Group H] [TopologicalSpace H] [IsTopologicalGroup H] [CompactSpace H]
    [MeasurableSpace H] [BorelSpace H]
    {Œº : Measure G} [IsHaarMeasure Œº] {ŒΩ : Measure H} [IsHaarMeasure ŒΩ]
    {f : G ‚Üí* H} (hcont : Continuous f) (hsurj : Surjective f) (huniv : Œº univ = ŒΩ univ) :
    MeasurePreserving f Œº ŒΩ where
  measurable := hcont.measurable
  map_eq := by
    have : IsFiniteMeasure Œº := ‚ü®by rw [huniv]; apply measure_lt_top‚ü©
    have : (Œº.map f).IsHaarMeasure := isHaarMeasure_map_of_isFiniteMeasure Œº f hcont hsurj
    set C : ‚Ñù‚â•0 := haarScalarFactor (Œº.map f) ŒΩ
    have hC : Œº.map f = C ‚Ä¢ ŒΩ := isMulLeftInvariant_eq_smul_of_innerRegular _ _
    suffices C = 1 by rwa [this, one_smul] at hC
    have : C * ŒΩ univ = 1 * ŒΩ univ := by
      rw [one_mul, ‚Üê smul_eq_mul, ‚Üê ENNReal.smul_def, ‚Üê smul_apply, ‚Üê hC,
        map_apply hcont.measurable .univ, preimage_univ, huniv]
    rwa [ENNReal.mul_left_inj (NeZero.ne _) (measure_ne_top _ _), ENNReal.coe_eq_one] at this

end Group

section CommGroup

variable {G : Type*} [CommGroup G] [TopologicalSpace G] [IsTopologicalGroup G]
  [MeasurableSpace G] [BorelSpace G] (Œº : Measure G) [IsHaarMeasure Œº]

/-- Any regular Haar measure is invariant under inversion in an abelian group. -/
@[to_additive
/-- Any regular additive Haar measure is invariant under negation in an abelian group. -/]
instance (priority := 100) IsHaarMeasure.isInvInvariant_of_regular
    [LocallyCompactSpace G] [Regular Œº] : IsInvInvariant Œº := by
  -- the image measure is a Haar measure. By uniqueness up to multiplication, it is of the form
  -- `c Œº`. Applying again inversion, one gets the measure `c^2 Œº`. But since inversion is an
  -- involution, this is also `Œº`. Hence, `c^2 = 1`, which implies `c = 1`.
  constructor
  let c : ‚Ñù‚â•0‚àû := haarScalarFactor Œº.inv Œº
  have hc : Œº.inv = c ‚Ä¢ Œº := isMulLeftInvariant_eq_smul_of_regular Œº.inv Œº
  have : map Inv.inv (map Inv.inv Œº) = c ^ 2 ‚Ä¢ Œº := by
    rw [‚Üê inv_def Œº, hc, Measure.map_smul, ‚Üê inv_def Œº, hc, smul_smul, pow_two]
  have Œºeq : Œº = c ^ 2 ‚Ä¢ Œº := by
    rw [map_map continuous_inv.measurable continuous_inv.measurable] at this
    simpa only [inv_involutive, Involutive.comp_self, Measure.map_id]
  have K : PositiveCompacts G := Classical.arbitrary _
  have : c ^ 2 * Œº K = 1 ^ 2 * Œº K := by
    conv_rhs => rw [Œºeq]
    simp
  have : c ^ 2 = 1 ^ 2 :=
    (ENNReal.mul_left_inj (measure_pos_of_nonempty_interior _ K.interior_nonempty).ne'
          K.isCompact.measure_lt_top.ne).1 this
  have : c = 1 := (ENNReal.pow_right_strictMono two_ne_zero).injective this
  rw [hc, this, one_smul]

/-- Any inner regular Haar measure is invariant under inversion in an abelian group. -/
@[to_additive
/-- Any regular additive Haar measure is invariant under negation in an abelian group. -/]
instance (priority := 100) IsHaarMeasure.isInvInvariant_of_innerRegular
    [LocallyCompactSpace G] [InnerRegular Œº] : IsInvInvariant Œº := by
  -- the image measure is a Haar measure. By uniqueness up to multiplication, it is of the form
  -- `c Œº`. Applying again inversion, one gets the measure `c^2 Œº`. But since inversion is an
  -- involution, this is also `Œº`. Hence, `c^2 = 1`, which implies `c = 1`.
  constructor
  let c : ‚Ñù‚â•0‚àû := haarScalarFactor Œº.inv Œº
  have hc : Œº.inv = c ‚Ä¢ Œº := isMulLeftInvariant_eq_smul_of_innerRegular Œº.inv Œº
  have : map Inv.inv (map Inv.inv Œº) = c ^ 2 ‚Ä¢ Œº := by
    rw [‚Üê inv_def Œº, hc, Measure.map_smul, ‚Üê inv_def Œº, hc, smul_smul, pow_two]
  have Œºeq : Œº = c ^ 2 ‚Ä¢ Œº := by
    rw [map_map continuous_inv.measurable continuous_inv.measurable] at this
    simpa only [inv_involutive, Involutive.comp_self, Measure.map_id]
  have K : PositiveCompacts G := Classical.arbitrary _
  have : c ^ 2 * Œº K = 1 ^ 2 * Œº K := by
    conv_rhs => rw [Œºeq]
    simp
  have : c ^ 2 = 1 ^ 2 :=
    (ENNReal.mul_left_inj (measure_pos_of_nonempty_interior _ K.interior_nonempty).ne'
          K.isCompact.measure_lt_top.ne).1 this
  have : c = 1 := (ENNReal.pow_right_strictMono two_ne_zero).injective this
  rw [hc, this, one_smul]

@[to_additive]
theorem measurePreserving_zpow [CompactSpace G] [RootableBy G ‚Ñ§] {n : ‚Ñ§} (hn : n ‚â† 0) :
    MeasurePreserving (fun g : G => g ^ n) Œº Œº :=
  (zpowGroupHom n).measurePreserving (Œº := Œº) (continuous_zpow n)
    (RootableBy.surjective_pow G ‚Ñ§ hn) rfl

@[to_additive]
theorem MeasurePreserving.zpow [CompactSpace G] [RootableBy G ‚Ñ§]
    {n : ‚Ñ§} (hn : n ‚â† 0) {X : Type*}
    [MeasurableSpace X] {Œº' : Measure X} {f : X ‚Üí G} (hf : MeasurePreserving f Œº' Œº) :
    MeasurePreserving (fun x => f x ^ n) Œº' Œº :=
  (measurePreserving_zpow Œº hn).comp hf

end CommGroup

section DistribMulAction
variable {G A : Type*} [Group G] [AddCommGroup A] [DistribMulAction G A] [MeasurableSpace A]
  [TopologicalSpace A] [BorelSpace A] [IsTopologicalAddGroup A] [LocallyCompactSpace A]
  [ContinuousConstSMul G A] {Œº ŒΩ : Measure A} [Œº.IsAddHaarMeasure] [ŒΩ.IsAddHaarMeasure] {g : G}

variable (Œº ŒΩ) in
lemma addHaarScalarFactor_domSMul (g : G·µà·µê·µÉ) :
    addHaarScalarFactor (g ‚Ä¢ Œº) (g ‚Ä¢ ŒΩ) = addHaarScalarFactor Œº ŒΩ := by
  obtain ‚ü®‚ü®f, f_cont‚ü©, f_comp, f_nonneg, f_zero‚ü© :
    ‚àÉ f : C(A, ‚Ñù), HasCompactSupport f ‚àß 0 ‚â§ f ‚àß f 0 ‚â† 0 := exists_continuous_nonneg_pos 0
  have int_f_ne_zero : ‚à´ x, f x ‚àÇg ‚Ä¢ ŒΩ ‚â† 0 :=
    (f_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero f_comp f_nonneg f_zero).ne'
  apply NNReal.coe_injective
  rw [addHaarScalarFactor_eq_integral_div (g ‚Ä¢ Œº) (g ‚Ä¢ ŒΩ) f_cont f_comp int_f_ne_zero,
    integral_domSMul, integral_domSMul]
  refine (addHaarScalarFactor_eq_integral_div _ _ (by fun_prop) ?_ ?_).symm
  ¬∑ exact f_comp.comp_isClosedEmbedding (Homeomorph.smul _).isClosedEmbedding
  ¬∑ rw [‚Üê integral_domSMul]
    exact (f_cont.integral_pos_of_hasCompactSupport_nonneg_nonzero f_comp f_nonneg f_zero).ne'

variable (Œº) in
lemma addHaarScalarFactor_smul_congr (g : G·µà·µê·µÉ) :
    addHaarScalarFactor Œº (g ‚Ä¢ Œº) = addHaarScalarFactor ŒΩ (g ‚Ä¢ ŒΩ) := by
  rw [addHaarScalarFactor_eq_mul _ (g ‚Ä¢ ŒΩ), addHaarScalarFactor_domSMul,
    mul_comm, ‚Üê addHaarScalarFactor_eq_mul]

variable (Œº) in
lemma addHaarScalarFactor_smul_congr' (g : G·µà·µê·µÉ) :
    addHaarScalarFactor (g ‚Ä¢ Œº) Œº = addHaarScalarFactor (g ‚Ä¢ ŒΩ) ŒΩ := by
  rw [addHaarScalarFactor_eq_mul _ (g ‚Ä¢ ŒΩ), addHaarScalarFactor_domSMul,
    mul_comm, ‚Üê addHaarScalarFactor_eq_mul]

end DistribMulAction
end Measure

end MeasureTheory
