/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Measure.Content
import Mathlib.MeasureTheory.Group.Prod
import Mathlib.Topology.Algebra.Group.Compact

/-!
# Haar measure

In this file we prove the existence of Haar measure for a locally compact Hausdorff topological
group.

We follow the write-up by Jonathan Gleason, *Existence and Uniqueness of Haar Measure*.
This is essentially the same argument as in
https://en.wikipedia.org/wiki/Haar_measure#A_construction_using_compact_subsets.

We construct the Haar measure first on compact sets. For this we define `(K : U)` as the (smallest)
number of left-translates of `U` that are needed to cover `K` (`index` in the formalization).
Then we define a function `h` on compact sets as `lim_U (K : U) / (K‚ÇÄ : U)`,
where `U` becomes a smaller and smaller open neighborhood of `1`, and `K‚ÇÄ` is a fixed compact set
with nonempty interior. This function is `chaar` in the formalization, and we define the limit
formally using Tychonoff's theorem.

This function `h` forms a content, which we can extend to an outer measure and then a measure
(`haarMeasure`).
We normalize the Haar measure so that the measure of `K‚ÇÄ` is `1`.

Note that `Œº` need not coincide with `h` on compact sets, according to
[halmos1950measure, ch. X, ¬ß53 p.233]. However, we know that `h(K)` lies between `Œº(K·µí)` and `Œº(K)`,
where `·µí` denotes the interior.

We also give a form of uniqueness of Haar measure, for œÉ-finite measures on second-countable
locally compact groups. For more involved statements not assuming second-countability, see
the file `Mathlib/MeasureTheory/Measure/Haar/Unique.lean`.

## Main Declarations

* `haarMeasure`: the Haar measure on a locally compact Hausdorff group. This is a left invariant
  regular measure. It takes as argument a compact set of the group (with non-empty interior),
  and is normalized so that the measure of the given set is 1.
* `haarMeasure_self`: the Haar measure is normalized.
* `isMulLeftInvariant_haarMeasure`: the Haar measure is left invariant.
* `regular_haarMeasure`: the Haar measure is a regular measure.
* `isHaarMeasure_haarMeasure`: the Haar measure satisfies the `IsHaarMeasure` typeclass, i.e.,
  it is invariant and gives finite mass to compact sets and positive mass to nonempty open sets.
* `haar` : some choice of a Haar measure, on a locally compact Hausdorff group, constructed as
  `haarMeasure K` where `K` is some arbitrary choice of a compact set with nonempty interior.
* `haarMeasure_unique`: Every œÉ-finite left invariant measure on a second-countable locally compact
  Hausdorff group is a scalar multiple of the Haar measure.

## References
* Paul Halmos (1950), Measure Theory, ¬ß53
* Jonathan Gleason, Existence and Uniqueness of Haar Measure
  - Note: step 9, page 8 contains a mistake: the last defined `Œº` does not extend the `Œº` on compact
    sets, see Halmos (1950) p. 233, bottom of the page. This makes some other steps (like step 11)
    invalid.
* https://en.wikipedia.org/wiki/Haar_measure
-/


noncomputable section

open Set Inv Function TopologicalSpace MeasurableSpace

open scoped NNReal ENNReal Pointwise Topology

namespace MeasureTheory

namespace Measure

section Group

variable {G : Type*} [Group G]

/-! We put the internal functions in the construction of the Haar measure in a namespace,
  so that the chosen names don't clash with other declarations.
  We first define a couple of the functions before proving the properties (that require that `G`
  is a topological group). -/


namespace haar

/-- The index or Haar covering number or ratio of `K` w.r.t. `V`, denoted `(K : V)`:
  it is the smallest number of (left) translates of `V` that is necessary to cover `K`.
  It is defined to be 0 if no finite number of translates cover `K`. -/
@[to_additive addIndex "additive version of `MeasureTheory.Measure.haar.index`"]
noncomputable def index (K V : Set G) : ‚Ñï :=
  sInf <| Finset.card '' { t : Finset G | K ‚äÜ ‚ãÉ g ‚àà t, (fun h => g * h) ‚Åª¬π' V }

@[to_additive addIndex_empty]
theorem index_empty {V : Set G} : index ‚àÖ V = 0 := by simp [index]

variable [TopologicalSpace G]

/-- `prehaar K‚ÇÄ U K` is a weighted version of the index, defined as `(K : U)/(K‚ÇÄ : U)`.
  In the applications `K‚ÇÄ` is compact with non-empty interior, `U` is open containing `1`,
  and `K` is any compact set.
  The argument `K` is a (bundled) compact set, so that we can consider `prehaar K‚ÇÄ U` as an
  element of `haarProduct` (below). -/
@[to_additive "additive version of `MeasureTheory.Measure.haar.prehaar`"]
noncomputable def prehaar (K‚ÇÄ U : Set G) (K : Compacts G) : ‚Ñù :=
  (index (K : Set G) U : ‚Ñù) / index K‚ÇÄ U

@[to_additive]
theorem prehaar_empty (K‚ÇÄ : PositiveCompacts G) {U : Set G} : prehaar (K‚ÇÄ : Set G) U ‚ä• = 0 := by
  rw [prehaar, Compacts.coe_bot, index_empty, Nat.cast_zero, zero_div]

@[to_additive]
theorem prehaar_nonneg (K‚ÇÄ : PositiveCompacts G) {U : Set G} (K : Compacts G) :
    0 ‚â§ prehaar (K‚ÇÄ : Set G) U K := by apply div_nonneg <;> norm_cast <;> apply zero_le

/-- `haarProduct K‚ÇÄ` is the product of intervals `[0, (K : K‚ÇÄ)]`, for all compact sets `K`.
  For all `U`, we can show that `prehaar K‚ÇÄ U ‚àà haarProduct K‚ÇÄ`. -/
@[to_additive "additive version of `MeasureTheory.Measure.haar.haarProduct`"]
def haarProduct (K‚ÇÄ : Set G) : Set (Compacts G ‚Üí ‚Ñù) :=
  pi univ fun K => Icc 0 <| index (K : Set G) K‚ÇÄ

@[to_additive (attr := simp)]
theorem mem_prehaar_empty {K‚ÇÄ : Set G} {f : Compacts G ‚Üí ‚Ñù} :
    f ‚àà haarProduct K‚ÇÄ ‚Üî ‚àÄ K : Compacts G, f K ‚àà Icc (0 : ‚Ñù) (index (K : Set G) K‚ÇÄ) := by
  simp only [haarProduct, Set.pi, forall_prop_of_true, mem_univ, mem_setOf_eq]

/-- The closure of the collection of elements of the form `prehaar K‚ÇÄ U`,
  for `U` open neighbourhoods of `1`, contained in `V`. The closure is taken in the space
  `compacts G ‚Üí ‚Ñù`, with the topology of pointwise convergence.
  We show that the intersection of all these sets is nonempty, and the Haar measure
  on compact sets is defined to be an element in the closure of this intersection. -/
@[to_additive "additive version of `MeasureTheory.Measure.haar.clPrehaar`"]
def clPrehaar (K‚ÇÄ : Set G) (V : OpenNhdsOf (1 : G)) : Set (Compacts G ‚Üí ‚Ñù) :=
  closure <| prehaar K‚ÇÄ '' { U : Set G | U ‚äÜ V.1 ‚àß IsOpen U ‚àß (1 : G) ‚àà U }

variable [IsTopologicalGroup G]

/-!
### Lemmas about `index`
-/


/-- If `K` is compact and `V` has nonempty interior, then the index `(K : V)` is well-defined,
  there is a finite set `t` satisfying the desired properties. -/
@[to_additive addIndex_defined
"If `K` is compact and `V` has nonempty interior, then the index `(K : V)` is well-defined, there is
a finite set `t` satisfying the desired properties."]
theorem index_defined {K V : Set G} (hK : IsCompact K) (hV : (interior V).Nonempty) :
    ‚àÉ n : ‚Ñï, n ‚àà Finset.card '' { t : Finset G | K ‚äÜ ‚ãÉ g ‚àà t, (fun h => g * h) ‚Åª¬π' V } := by
  rcases compact_covered_by_mul_left_translates hK hV with ‚ü®t, ht‚ü©; exact ‚ü®t.card, t, ht, rfl‚ü©

@[to_additive addIndex_elim]
theorem index_elim {K V : Set G} (hK : IsCompact K) (hV : (interior V).Nonempty) :
    ‚àÉ t : Finset G, (K ‚äÜ ‚ãÉ g ‚àà t, (fun h => g * h) ‚Åª¬π' V) ‚àß Finset.card t = index K V := by
  have := Nat.sInf_mem (index_defined hK hV); rwa [mem_image] at this

@[to_additive le_addIndex_mul]
theorem le_index_mul (K‚ÇÄ : PositiveCompacts G) (K : Compacts G) {V : Set G}
    (hV : (interior V).Nonempty) :
    index (K : Set G) V ‚â§ index (K : Set G) K‚ÇÄ * index (K‚ÇÄ : Set G) V := by
  classical
  obtain ‚ü®s, h1s, h2s‚ü© := index_elim K.isCompact K‚ÇÄ.interior_nonempty
  obtain ‚ü®t, h1t, h2t‚ü© := index_elim K‚ÇÄ.isCompact hV
  rw [‚Üê h2s, ‚Üê h2t, mul_comm]
  refine le_trans ?_ Finset.card_mul_le
  apply Nat.sInf_le; refine ‚ü®_, ?_, rfl‚ü©; rw [mem_setOf_eq]; refine Subset.trans h1s ?_
  apply iUnion‚ÇÇ_subset; intro g‚ÇÅ hg‚ÇÅ; rw [preimage_subset_iff]; intro g‚ÇÇ hg‚ÇÇ
  have := h1t hg‚ÇÇ
  rcases this with ‚ü®_, ‚ü®g‚ÇÉ, rfl‚ü©, A, ‚ü®hg‚ÇÉ, rfl‚ü©, h2V‚ü©; rw [mem_preimage, ‚Üê mul_assoc] at h2V
  exact mem_biUnion (Finset.mul_mem_mul hg‚ÇÉ hg‚ÇÅ) h2V

@[to_additive addIndex_pos]
theorem index_pos (K : PositiveCompacts G) {V : Set G} (hV : (interior V).Nonempty) :
    0 < index (K : Set G) V := by
  classical
  rw [index, Nat.sInf_def, Nat.find_pos, mem_image]
  ¬∑ rintro ‚ü®t, h1t, h2t‚ü©; rw [Finset.card_eq_zero] at h2t; subst h2t
    obtain ‚ü®g, hg‚ü© := K.interior_nonempty
    show g ‚àà (‚àÖ : Set G)
    convert h1t (interior_subset hg); symm
    simp only [Finset.notMem_empty, iUnion_of_empty, iUnion_empty]
  ¬∑ exact index_defined K.isCompact hV

@[to_additive addIndex_mono]
theorem index_mono {K K' V : Set G} (hK' : IsCompact K') (h : K ‚äÜ K') (hV : (interior V).Nonempty) :
    index K V ‚â§ index K' V := by
  rcases index_elim hK' hV with ‚ü®s, h1s, h2s‚ü©
  apply Nat.sInf_le; rw [mem_image]; exact ‚ü®s, Subset.trans h h1s, h2s‚ü©

@[to_additive addIndex_union_le]
theorem index_union_le (K‚ÇÅ K‚ÇÇ : Compacts G) {V : Set G} (hV : (interior V).Nonempty) :
    index (K‚ÇÅ.1 ‚à™ K‚ÇÇ.1) V ‚â§ index K‚ÇÅ.1 V + index K‚ÇÇ.1 V := by
  classical
  rcases index_elim K‚ÇÅ.2 hV with ‚ü®s, h1s, h2s‚ü©
  rcases index_elim K‚ÇÇ.2 hV with ‚ü®t, h1t, h2t‚ü©
  rw [‚Üê h2s, ‚Üê h2t]
  refine le_trans (Nat.sInf_le ‚ü®_, ?_, rfl‚ü©) (Finset.card_union_le _ _)
  rw [mem_setOf_eq, Finset.set_biUnion_union]
  gcongr

@[to_additive addIndex_union_eq]
theorem index_union_eq (K‚ÇÅ K‚ÇÇ : Compacts G) {V : Set G} (hV : (interior V).Nonempty)
    (h : Disjoint (K‚ÇÅ.1 * V‚Åª¬π) (K‚ÇÇ.1 * V‚Åª¬π)) :
    index (K‚ÇÅ.1 ‚à™ K‚ÇÇ.1) V = index K‚ÇÅ.1 V + index K‚ÇÇ.1 V := by
  classical
  apply le_antisymm (index_union_le K‚ÇÅ K‚ÇÇ hV)
  rcases index_elim (K‚ÇÅ.2.union K‚ÇÇ.2) hV with ‚ü®s, h1s, h2s‚ü©; rw [‚Üê h2s]
  have (K : Set G) (hK : K ‚äÜ ‚ãÉ g ‚àà s, (g * ¬∑) ‚Åª¬π' V) :
      index K V ‚â§ {g ‚àà s | ((g * ¬∑) ‚Åª¬π' V ‚à© K).Nonempty}.card := by
    apply Nat.sInf_le; refine ‚ü®_, ?_, rfl‚ü©; rw [mem_setOf_eq]
    intro g hg; rcases hK hg with ‚ü®_, ‚ü®g‚ÇÄ, rfl‚ü©, _, ‚ü®h1g‚ÇÄ, rfl‚ü©, h2g‚ÇÄ‚ü©
    simp only [mem_preimage] at h2g‚ÇÄ
    simp only [mem_iUnion]; use g‚ÇÄ; constructor; swap
    ¬∑ simp only [Finset.mem_filter, h1g‚ÇÄ, true_and]; use g
      simp [hg, h2g‚ÇÄ]
    exact h2g‚ÇÄ
  refine
    le_trans
      (add_le_add (this K‚ÇÅ.1 <| Subset.trans subset_union_left h1s)
        (this K‚ÇÇ.1 <| Subset.trans subset_union_right h1s)) ?_
  rw [‚Üê Finset.card_union_of_disjoint, Finset.filter_union_right]
  ¬∑ exact s.card_filter_le _
  apply Finset.disjoint_filter.mpr
  rintro g‚ÇÅ _ ‚ü®g‚ÇÇ, h1g‚ÇÇ, h2g‚ÇÇ‚ü© ‚ü®g‚ÇÉ, h1g‚ÇÉ, h2g‚ÇÉ‚ü©
  simp only [mem_preimage] at h1g‚ÇÉ h1g‚ÇÇ
  refine h.le_bot (?_ : g‚ÇÅ‚Åª¬π ‚àà _)
  constructor <;> simp only [Set.mem_inv, Set.mem_mul, exists_exists_and_eq_and, exists_and_left]
  ¬∑ refine ‚ü®_, h2g‚ÇÇ, (g‚ÇÅ * g‚ÇÇ)‚Åª¬π, ?_, ?_‚ü©
    ¬∑ simp only [inv_inv, h1g‚ÇÇ]
    ¬∑ simp only [mul_inv_rev, mul_inv_cancel_left]
  ¬∑ refine ‚ü®_, h2g‚ÇÉ, (g‚ÇÅ * g‚ÇÉ)‚Åª¬π, ?_, ?_‚ü©
    ¬∑ simp only [inv_inv, h1g‚ÇÉ]
    ¬∑ simp only [mul_inv_rev, mul_inv_cancel_left]

@[to_additive add_left_addIndex_le]
theorem mul_left_index_le {K : Set G} (hK : IsCompact K) {V : Set G} (hV : (interior V).Nonempty)
    (g : G) : index ((fun h => g * h) '' K) V ‚â§ index K V := by
  rcases index_elim hK hV with ‚ü®s, h1s, h2s‚ü©; rw [‚Üê h2s]
  apply Nat.sInf_le; rw [mem_image]
  refine ‚ü®s.map (Equiv.mulRight g‚Åª¬π).toEmbedding, ?_, Finset.card_map _‚ü©
  simp only [mem_setOf_eq]; refine Subset.trans (image_subset _ h1s) ?_
  rintro _ ‚ü®g‚ÇÅ, ‚ü®_, ‚ü®g‚ÇÇ, rfl‚ü©, ‚ü®_, ‚ü®hg‚ÇÇ, rfl‚ü©, hg‚ÇÅ‚ü©‚ü©, rfl‚ü©
  simp only [mem_preimage] at hg‚ÇÅ
  simp only [exists_prop, mem_iUnion, Finset.mem_map, Equiv.coe_mulRight,
    exists_exists_and_eq_and, mem_preimage, Equiv.toEmbedding_apply]
  refine ‚ü®_, hg‚ÇÇ, ?_‚ü©; simp only [mul_assoc, hg‚ÇÅ, inv_mul_cancel_left]

@[to_additive is_left_invariant_addIndex]
theorem is_left_invariant_index {K : Set G} (hK : IsCompact K) (g : G) {V : Set G}
    (hV : (interior V).Nonempty) : index ((fun h => g * h) '' K) V = index K V := by
  refine le_antisymm (mul_left_index_le hK hV g) ?_
  convert mul_left_index_le (hK.image <| continuous_mul_left g) hV g‚Åª¬π
  rw [image_image]; symm; convert image_id' _ with h; apply inv_mul_cancel_left

/-!
### Lemmas about `prehaar`
-/


@[to_additive add_prehaar_le_addIndex]
theorem prehaar_le_index (K‚ÇÄ : PositiveCompacts G) {U : Set G} (K : Compacts G)
    (hU : (interior U).Nonempty) : prehaar (K‚ÇÄ : Set G) U K ‚â§ index (K : Set G) K‚ÇÄ := by
  unfold prehaar; rw [div_le_iff‚ÇÄ] <;> norm_cast
  ¬∑ apply le_index_mul K‚ÇÄ K hU
  ¬∑ exact index_pos K‚ÇÄ hU

@[to_additive]
theorem prehaar_pos (K‚ÇÄ : PositiveCompacts G) {U : Set G} (hU : (interior U).Nonempty) {K : Set G}
    (h1K : IsCompact K) (h2K : (interior K).Nonempty) : 0 < prehaar (K‚ÇÄ : Set G) U ‚ü®K, h1K‚ü© := by
  apply div_pos <;> norm_cast
  ¬∑ apply index_pos ‚ü®‚ü®K, h1K‚ü©, h2K‚ü© hU
  ¬∑ exact index_pos K‚ÇÄ hU

@[to_additive]
theorem prehaar_mono {K‚ÇÄ : PositiveCompacts G} {U : Set G} (hU : (interior U).Nonempty)
    {K‚ÇÅ K‚ÇÇ : Compacts G} (h : (K‚ÇÅ : Set G) ‚äÜ K‚ÇÇ.1) :
    prehaar (K‚ÇÄ : Set G) U K‚ÇÅ ‚â§ prehaar (K‚ÇÄ : Set G) U K‚ÇÇ := by
  simp only [prehaar]; rw [div_le_div_iff_of_pos_right]
  ¬∑ exact mod_cast index_mono K‚ÇÇ.2 h hU
  ¬∑ exact mod_cast index_pos K‚ÇÄ hU

@[to_additive]
theorem prehaar_self {K‚ÇÄ : PositiveCompacts G} {U : Set G} (hU : (interior U).Nonempty) :
    prehaar (K‚ÇÄ : Set G) U K‚ÇÄ.toCompacts = 1 :=
  div_self <| ne_of_gt <| mod_cast index_pos K‚ÇÄ hU

@[to_additive]
theorem prehaar_sup_le {K‚ÇÄ : PositiveCompacts G} {U : Set G} (K‚ÇÅ K‚ÇÇ : Compacts G)
    (hU : (interior U).Nonempty) :
    prehaar (K‚ÇÄ : Set G) U (K‚ÇÅ ‚äî K‚ÇÇ) ‚â§ prehaar (K‚ÇÄ : Set G) U K‚ÇÅ + prehaar (K‚ÇÄ : Set G) U K‚ÇÇ := by
  simp only [prehaar]; rw [div_add_div_same, div_le_div_iff_of_pos_right]
  ¬∑ exact mod_cast index_union_le K‚ÇÅ K‚ÇÇ hU
  ¬∑ exact mod_cast index_pos K‚ÇÄ hU

@[to_additive]
theorem prehaar_sup_eq {K‚ÇÄ : PositiveCompacts G} {U : Set G} {K‚ÇÅ K‚ÇÇ : Compacts G}
    (hU : (interior U).Nonempty) (h : Disjoint (K‚ÇÅ.1 * U‚Åª¬π) (K‚ÇÇ.1 * U‚Åª¬π)) :
    prehaar (K‚ÇÄ : Set G) U (K‚ÇÅ ‚äî K‚ÇÇ) = prehaar (K‚ÇÄ : Set G) U K‚ÇÅ + prehaar (K‚ÇÄ : Set G) U K‚ÇÇ := by
  simp only [prehaar]; rw [div_add_div_same]
  -- Porting note: Here was `congr`, but `to_additive` failed to generate a theorem.
  refine congr_arg (fun x : ‚Ñù => x / index K‚ÇÄ U) ?_
  exact mod_cast index_union_eq K‚ÇÅ K‚ÇÇ hU h

@[to_additive]
theorem is_left_invariant_prehaar {K‚ÇÄ : PositiveCompacts G} {U : Set G} (hU : (interior U).Nonempty)
    (g : G) (K : Compacts G) :
    prehaar (K‚ÇÄ : Set G) U (K.map _ <| continuous_mul_left g) = prehaar (K‚ÇÄ : Set G) U K := by
  simp only [prehaar, Compacts.coe_map, is_left_invariant_index K.isCompact _ hU]

/-!
### Lemmas about `haarProduct`
-/

@[to_additive]
theorem prehaar_mem_haarProduct (K‚ÇÄ : PositiveCompacts G) {U : Set G} (hU : (interior U).Nonempty) :
    prehaar (K‚ÇÄ : Set G) U ‚àà haarProduct (K‚ÇÄ : Set G) := by
    rintro ‚ü®K, hK‚ü© _; rw [mem_Icc]; exact ‚ü®prehaar_nonneg K‚ÇÄ _, prehaar_le_index K‚ÇÄ _ hU‚ü©

@[to_additive]
theorem nonempty_iInter_clPrehaar (K‚ÇÄ : PositiveCompacts G) :
    (haarProduct (K‚ÇÄ : Set G) ‚à© ‚ãÇ V : OpenNhdsOf (1 : G), clPrehaar K‚ÇÄ V).Nonempty := by
  have : IsCompact (haarProduct (K‚ÇÄ : Set G)) := by
    apply isCompact_univ_pi; intro K; apply isCompact_Icc
  refine this.inter_iInter_nonempty (clPrehaar K‚ÇÄ) (fun s => isClosed_closure) fun t => ?_
  let V‚ÇÄ := ‚ãÇ V ‚àà t, (V : OpenNhdsOf (1 : G)).carrier
  have h1V‚ÇÄ : IsOpen V‚ÇÄ := isOpen_biInter_finset <| by rintro ‚ü®‚ü®V, hV‚ÇÅ‚ü©, hV‚ÇÇ‚ü© _; exact hV‚ÇÅ
  have h2V‚ÇÄ : (1 : G) ‚àà V‚ÇÄ := by simp only [V‚ÇÄ, mem_iInter]; rintro ‚ü®‚ü®V, hV‚ÇÅ‚ü©, hV‚ÇÇ‚ü© _; exact hV‚ÇÇ
  refine ‚ü®prehaar K‚ÇÄ V‚ÇÄ, ?_‚ü©
  constructor
  ¬∑ apply prehaar_mem_haarProduct K‚ÇÄ; use 1; rwa [h1V‚ÇÄ.interior_eq]
  ¬∑ simp only [mem_iInter]; rintro ‚ü®V, hV‚ü© h2V; apply subset_closure
    apply mem_image_of_mem; rw [mem_setOf_eq]
    exact ‚ü®Subset.trans (iInter_subset _ ‚ü®V, hV‚ü©) (iInter_subset _ h2V), h1V‚ÇÄ, h2V‚ÇÄ‚ü©

/-!
### Lemmas about `chaar`
-/

/-- This is the "limit" of `prehaar K‚ÇÄ U K` as `U` becomes a smaller and smaller open
  neighborhood of `(1 : G)`. More precisely, it is defined to be an arbitrary element
  in the intersection of all the sets `clPrehaar K‚ÇÄ V` in `haarProduct K‚ÇÄ`.
  This is roughly equal to the Haar measure on compact sets,
  but it can differ slightly. We do know that
  `haarMeasure K‚ÇÄ (interior K) ‚â§ chaar K‚ÇÄ K ‚â§ haarMeasure K‚ÇÄ K`. -/
@[to_additive addCHaar "additive version of `MeasureTheory.Measure.haar.chaar`"]
noncomputable def chaar (K‚ÇÄ : PositiveCompacts G) (K : Compacts G) : ‚Ñù :=
  Classical.choose (nonempty_iInter_clPrehaar K‚ÇÄ) K

@[to_additive addCHaar_mem_addHaarProduct]
theorem chaar_mem_haarProduct (K‚ÇÄ : PositiveCompacts G) : chaar K‚ÇÄ ‚àà haarProduct (K‚ÇÄ : Set G) :=
  (Classical.choose_spec (nonempty_iInter_clPrehaar K‚ÇÄ)).1

@[to_additive addCHaar_mem_clAddPrehaar]
theorem chaar_mem_clPrehaar (K‚ÇÄ : PositiveCompacts G) (V : OpenNhdsOf (1 : G)) :
    chaar K‚ÇÄ ‚àà clPrehaar (K‚ÇÄ : Set G) V := by
  have := (Classical.choose_spec (nonempty_iInter_clPrehaar K‚ÇÄ)).2; rw [mem_iInter] at this
  exact this V

@[to_additive addCHaar_nonneg]
theorem chaar_nonneg (K‚ÇÄ : PositiveCompacts G) (K : Compacts G) : 0 ‚â§ chaar K‚ÇÄ K := by
  have := chaar_mem_haarProduct K‚ÇÄ K (mem_univ _); rw [mem_Icc] at this; exact this.1

@[to_additive addCHaar_empty]
theorem chaar_empty (K‚ÇÄ : PositiveCompacts G) : chaar K‚ÇÄ ‚ä• = 0 := by
  let eval : (Compacts G ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun f => f ‚ä•
  have : Continuous eval := continuous_apply ‚ä•
  show chaar K‚ÇÄ ‚àà eval ‚Åª¬π' {(0 : ‚Ñù)}
  apply mem_of_subset_of_mem _ (chaar_mem_clPrehaar K‚ÇÄ ‚ä§)
  unfold clPrehaar; rw [IsClosed.closure_subset_iff]
  ¬∑ rintro _ ‚ü®U, _, rfl‚ü©; apply prehaar_empty
  ¬∑ apply continuous_iff_isClosed.mp this; exact isClosed_singleton

@[to_additive addCHaar_self]
theorem chaar_self (K‚ÇÄ : PositiveCompacts G) : chaar K‚ÇÄ K‚ÇÄ.toCompacts = 1 := by
  let eval : (Compacts G ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun f => f K‚ÇÄ.toCompacts
  have : Continuous eval := continuous_apply _
  show chaar K‚ÇÄ ‚àà eval ‚Åª¬π' {(1 : ‚Ñù)}
  apply mem_of_subset_of_mem _ (chaar_mem_clPrehaar K‚ÇÄ ‚ä§)
  unfold clPrehaar; rw [IsClosed.closure_subset_iff]
  ¬∑ rintro _ ‚ü®U, ‚ü®_, h2U, h3U‚ü©, rfl‚ü©; apply prehaar_self
    rw [h2U.interior_eq]; exact ‚ü®1, h3U‚ü©
  ¬∑ apply continuous_iff_isClosed.mp this; exact isClosed_singleton

@[to_additive addCHaar_mono]
theorem chaar_mono {K‚ÇÄ : PositiveCompacts G} {K‚ÇÅ K‚ÇÇ : Compacts G} (h : (K‚ÇÅ : Set G) ‚äÜ K‚ÇÇ) :
    chaar K‚ÇÄ K‚ÇÅ ‚â§ chaar K‚ÇÄ K‚ÇÇ := by
  let eval : (Compacts G ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun f => f K‚ÇÇ - f K‚ÇÅ
  have : Continuous eval := (continuous_apply K‚ÇÇ).sub (continuous_apply K‚ÇÅ)
  rw [‚Üê sub_nonneg]; show chaar K‚ÇÄ ‚àà eval ‚Åª¬π' Ici (0 : ‚Ñù)
  apply mem_of_subset_of_mem _ (chaar_mem_clPrehaar K‚ÇÄ ‚ä§)
  unfold clPrehaar; rw [IsClosed.closure_subset_iff]
  ¬∑ rintro _ ‚ü®U, ‚ü®_, h2U, h3U‚ü©, rfl‚ü©; simp only [eval, mem_preimage, mem_Ici, sub_nonneg]
    apply prehaar_mono _ h; rw [h2U.interior_eq]; exact ‚ü®1, h3U‚ü©
  ¬∑ apply continuous_iff_isClosed.mp this; exact isClosed_Ici

@[to_additive addCHaar_sup_le]
theorem chaar_sup_le {K‚ÇÄ : PositiveCompacts G} (K‚ÇÅ K‚ÇÇ : Compacts G) :
    chaar K‚ÇÄ (K‚ÇÅ ‚äî K‚ÇÇ) ‚â§ chaar K‚ÇÄ K‚ÇÅ + chaar K‚ÇÄ K‚ÇÇ := by
  let eval : (Compacts G ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun f => f K‚ÇÅ + f K‚ÇÇ - f (K‚ÇÅ ‚äî K‚ÇÇ)
  have : Continuous eval := by
    exact ((continuous_apply K‚ÇÅ).add (continuous_apply K‚ÇÇ)).sub (continuous_apply (K‚ÇÅ ‚äî K‚ÇÇ))
  rw [‚Üê sub_nonneg]; show chaar K‚ÇÄ ‚àà eval ‚Åª¬π' Ici (0 : ‚Ñù)
  apply mem_of_subset_of_mem _ (chaar_mem_clPrehaar K‚ÇÄ ‚ä§)
  unfold clPrehaar; rw [IsClosed.closure_subset_iff]
  ¬∑ rintro _ ‚ü®U, ‚ü®_, h2U, h3U‚ü©, rfl‚ü©; simp only [eval, mem_preimage, mem_Ici, sub_nonneg]
    apply prehaar_sup_le; rw [h2U.interior_eq]; exact ‚ü®1, h3U‚ü©
  ¬∑ apply continuous_iff_isClosed.mp this; exact isClosed_Ici

@[to_additive addCHaar_sup_eq]
theorem chaar_sup_eq {K‚ÇÄ : PositiveCompacts G}
    {K‚ÇÅ K‚ÇÇ : Compacts G} (h : Disjoint K‚ÇÅ.1 K‚ÇÇ.1) (h‚ÇÇ : IsClosed K‚ÇÇ.1) :
    chaar K‚ÇÄ (K‚ÇÅ ‚äî K‚ÇÇ) = chaar K‚ÇÄ K‚ÇÅ + chaar K‚ÇÄ K‚ÇÇ := by
  rcases SeparatedNhds.of_isCompact_isCompact_isClosed K‚ÇÅ.2 K‚ÇÇ.2 h‚ÇÇ h
    with ‚ü®U‚ÇÅ, U‚ÇÇ, h1U‚ÇÅ, h1U‚ÇÇ, h2U‚ÇÅ, h2U‚ÇÇ, hU‚ü©
  rcases compact_open_separated_mul_right K‚ÇÅ.2 h1U‚ÇÅ h2U‚ÇÅ with ‚ü®L‚ÇÅ, h1L‚ÇÅ, h2L‚ÇÅ‚ü©
  rcases mem_nhds_iff.mp h1L‚ÇÅ with ‚ü®V‚ÇÅ, h1V‚ÇÅ, h2V‚ÇÅ, h3V‚ÇÅ‚ü©
  replace h2L‚ÇÅ := Subset.trans (mul_subset_mul_left h1V‚ÇÅ) h2L‚ÇÅ
  rcases compact_open_separated_mul_right K‚ÇÇ.2 h1U‚ÇÇ h2U‚ÇÇ with ‚ü®L‚ÇÇ, h1L‚ÇÇ, h2L‚ÇÇ‚ü©
  rcases mem_nhds_iff.mp h1L‚ÇÇ with ‚ü®V‚ÇÇ, h1V‚ÇÇ, h2V‚ÇÇ, h3V‚ÇÇ‚ü©
  replace h2L‚ÇÇ := Subset.trans (mul_subset_mul_left h1V‚ÇÇ) h2L‚ÇÇ
  let eval : (Compacts G ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun f => f K‚ÇÅ + f K‚ÇÇ - f (K‚ÇÅ ‚äî K‚ÇÇ)
  have : Continuous eval :=
    ((continuous_apply K‚ÇÅ).add (continuous_apply K‚ÇÇ)).sub (continuous_apply (K‚ÇÅ ‚äî K‚ÇÇ))
  rw [eq_comm, ‚Üê sub_eq_zero]; show chaar K‚ÇÄ ‚àà eval ‚Åª¬π' {(0 : ‚Ñù)}
  let V := V‚ÇÅ ‚à© V‚ÇÇ
  apply
    mem_of_subset_of_mem _
      (chaar_mem_clPrehaar K‚ÇÄ
        ‚ü®‚ü®V‚Åª¬π, (h2V‚ÇÅ.inter h2V‚ÇÇ).preimage continuous_inv‚ü©, by
          simp only [V, mem_inv, inv_one, h3V‚ÇÅ, h3V‚ÇÇ, mem_inter_iff, true_and]‚ü©)
  unfold clPrehaar; rw [IsClosed.closure_subset_iff]
  ¬∑ rintro _ ‚ü®U, ‚ü®h1U, h2U, h3U‚ü©, rfl‚ü©
    simp only [eval, mem_preimage, sub_eq_zero, mem_singleton_iff]; rw [eq_comm]
    apply prehaar_sup_eq
    ¬∑ rw [h2U.interior_eq]; exact ‚ü®1, h3U‚ü©
    ¬∑ refine disjoint_of_subset ?_ ?_ hU
      ¬∑ refine Subset.trans (mul_subset_mul Subset.rfl ?_) h2L‚ÇÅ
        exact Subset.trans (inv_subset.mpr h1U) inter_subset_left
      ¬∑ refine Subset.trans (mul_subset_mul Subset.rfl ?_) h2L‚ÇÇ
        exact Subset.trans (inv_subset.mpr h1U) inter_subset_right
  ¬∑ apply continuous_iff_isClosed.mp this; exact isClosed_singleton

@[to_additive is_left_invariant_addCHaar]
theorem is_left_invariant_chaar {K‚ÇÄ : PositiveCompacts G} (g : G) (K : Compacts G) :
    chaar K‚ÇÄ (K.map _ <| continuous_mul_left g) = chaar K‚ÇÄ K := by
  let eval : (Compacts G ‚Üí ‚Ñù) ‚Üí ‚Ñù := fun f => f (K.map _ <| continuous_mul_left g) - f K
  have : Continuous eval := (continuous_apply (K.map _ _)).sub (continuous_apply K)
  rw [‚Üê sub_eq_zero]; show chaar K‚ÇÄ ‚àà eval ‚Åª¬π' {(0 : ‚Ñù)}
  apply mem_of_subset_of_mem _ (chaar_mem_clPrehaar K‚ÇÄ ‚ä§)
  unfold clPrehaar; rw [IsClosed.closure_subset_iff]
  ¬∑ rintro _ ‚ü®U, ‚ü®_, h2U, h3U‚ü©, rfl‚ü©
    simp only [eval, mem_singleton_iff, mem_preimage, sub_eq_zero]
    apply is_left_invariant_prehaar; rw [h2U.interior_eq]; exact ‚ü®1, h3U‚ü©
  ¬∑ apply continuous_iff_isClosed.mp this; exact isClosed_singleton

/-- The function `chaar` interpreted in `‚Ñù‚â•0`, as a content -/
@[to_additive "additive version of `MeasureTheory.Measure.haar.haarContent`"]
noncomputable def haarContent (K‚ÇÄ : PositiveCompacts G) : Content G where
  toFun K := ‚ü®chaar K‚ÇÄ K, chaar_nonneg _ _‚ü©
  mono' K‚ÇÅ K‚ÇÇ h := by simp only [‚Üê NNReal.coe_le_coe, NNReal.toReal, chaar_mono, h]
  sup_disjoint' K‚ÇÅ K‚ÇÇ h _h‚ÇÅ h‚ÇÇ := by simp only [chaar_sup_eq h]; rfl
  sup_le' K‚ÇÅ K‚ÇÇ := by
    simp only [‚Üê NNReal.coe_le_coe, NNReal.coe_add]
    simp only [NNReal.toReal, chaar_sup_le]

/-! We only prove the properties for `haarContent` that we use at least twice below. -/


@[to_additive]
theorem haarContent_apply (K‚ÇÄ : PositiveCompacts G) (K : Compacts G) :
    haarContent K‚ÇÄ K = show NNReal from ‚ü®chaar K‚ÇÄ K, chaar_nonneg _ _‚ü© :=
  rfl

/-- The variant of `chaar_self` for `haarContent` -/
@[to_additive "The variant of `addCHaar_self` for `addHaarContent`."]
theorem haarContent_self {K‚ÇÄ : PositiveCompacts G} : haarContent K‚ÇÄ K‚ÇÄ.toCompacts = 1 := by
  simp_rw [‚Üê ENNReal.coe_one, haarContent_apply, ENNReal.coe_inj, chaar_self]; rfl

/-- The variant of `is_left_invariant_chaar` for `haarContent` -/
@[to_additive "The variant of `is_left_invariant_addCHaar` for `addHaarContent`"]
theorem is_left_invariant_haarContent {K‚ÇÄ : PositiveCompacts G} (g : G) (K : Compacts G) :
    haarContent K‚ÇÄ (K.map _ <| continuous_mul_left g) = haarContent K‚ÇÄ K := by
  simpa only [ENNReal.coe_inj, ‚Üê NNReal.coe_inj, haarContent_apply] using
    is_left_invariant_chaar g K

@[to_additive]
theorem haarContent_outerMeasure_self_pos (K‚ÇÄ : PositiveCompacts G) :
    0 < (haarContent K‚ÇÄ).outerMeasure K‚ÇÄ := by
  refine zero_lt_one.trans_le ?_
  rw [Content.outerMeasure_eq_iInf]
  refine le_iInf‚ÇÇ fun U hU => le_iInf fun hK‚ÇÄ => le_trans ?_ <| le_iSup‚ÇÇ K‚ÇÄ.toCompacts hK‚ÇÄ
  exact haarContent_self.ge

@[to_additive]
theorem haarContent_outerMeasure_closure_pos (K‚ÇÄ : PositiveCompacts G) :
    0 < (haarContent K‚ÇÄ).outerMeasure (closure K‚ÇÄ) :=
  (haarContent_outerMeasure_self_pos K‚ÇÄ).trans_le (OuterMeasure.mono _ subset_closure)

end haar

open haar

/-!
### The Haar measure
-/

variable [TopologicalSpace G] [IsTopologicalGroup G] [MeasurableSpace G] [BorelSpace G]

/-- The Haar measure on the locally compact group `G`, scaled so that `haarMeasure K‚ÇÄ K‚ÇÄ = 1`. -/
@[to_additive
"The Haar measure on the locally compact additive group `G`, scaled so that
`addHaarMeasure K‚ÇÄ K‚ÇÄ = 1`."]
noncomputable def haarMeasure (K‚ÇÄ : PositiveCompacts G) : Measure G :=
  ((haarContent K‚ÇÄ).measure K‚ÇÄ)‚Åª¬π ‚Ä¢ (haarContent K‚ÇÄ).measure

@[to_additive]
theorem haarMeasure_apply {K‚ÇÄ : PositiveCompacts G} {s : Set G} (hs : MeasurableSet s) :
    haarMeasure K‚ÇÄ s = (haarContent K‚ÇÄ).outerMeasure s / (haarContent K‚ÇÄ).measure K‚ÇÄ := by
  change ((haarContent K‚ÇÄ).measure K‚ÇÄ)‚Åª¬π * (haarContent K‚ÇÄ).measure s = _
  simp only [hs, div_eq_mul_inv, mul_comm, Content.measure_apply]

@[to_additive]
instance isMulLeftInvariant_haarMeasure (K‚ÇÄ : PositiveCompacts G) :
    IsMulLeftInvariant (haarMeasure K‚ÇÄ) := by
  rw [‚Üê forall_measure_preimage_mul_iff]
  intro g A hA
  rw [haarMeasure_apply hA, haarMeasure_apply (measurable_const_mul g hA)]
  -- Porting note: Here was `congr 1`, but `to_additive` failed to generate a theorem.
  refine congr_arg (fun x : ‚Ñù‚â•0‚àû => x / (haarContent K‚ÇÄ).measure K‚ÇÄ) ?_
  apply Content.is_mul_left_invariant_outerMeasure
  apply is_left_invariant_haarContent

@[to_additive]
theorem haarMeasure_self {K‚ÇÄ : PositiveCompacts G} : haarMeasure K‚ÇÄ K‚ÇÄ = 1 := by
  haveI : LocallyCompactSpace G := K‚ÇÄ.locallyCompactSpace_of_group
  simp only [haarMeasure, coe_smul, Pi.smul_apply, smul_eq_mul]
  rw [‚Üê K‚ÇÄ.isCompact.measure_closure,
    Content.measure_apply _ isClosed_closure.measurableSet, ENNReal.inv_mul_cancel]
  ¬∑ exact (haarContent_outerMeasure_closure_pos K‚ÇÄ).ne'
  ¬∑ exact (Content.outerMeasure_lt_top_of_isCompact _ K‚ÇÄ.isCompact.closure).ne

/-- The Haar measure is regular. -/
@[to_additive "The additive Haar measure is regular."]
instance regular_haarMeasure {K‚ÇÄ : PositiveCompacts G} : (haarMeasure K‚ÇÄ).Regular := by
  haveI : LocallyCompactSpace G := K‚ÇÄ.locallyCompactSpace_of_group
  apply Regular.smul
  rw [‚Üê K‚ÇÄ.isCompact.measure_closure,
    Content.measure_apply _ isClosed_closure.measurableSet, ENNReal.inv_ne_top]
  exact (haarContent_outerMeasure_closure_pos K‚ÇÄ).ne'

@[to_additive]
theorem haarMeasure_closure_self {K‚ÇÄ : PositiveCompacts G} : haarMeasure K‚ÇÄ (closure K‚ÇÄ) = 1 := by
  rw [K‚ÇÄ.isCompact.measure_closure, haarMeasure_self]

/-- The Haar measure is sigma-finite in a second countable group. -/
@[to_additive "The additive Haar measure is sigma-finite in a second countable group."]
instance sigmaFinite_haarMeasure [SecondCountableTopology G] {K‚ÇÄ : PositiveCompacts G} :
    SigmaFinite (haarMeasure K‚ÇÄ) := by
  haveI : LocallyCompactSpace G := K‚ÇÄ.locallyCompactSpace_of_group; infer_instance

/-- The Haar measure is a Haar measure, i.e., it is invariant and gives finite mass to compact
sets and positive mass to nonempty open sets. -/
@[to_additive
"The additive Haar measure is an additive Haar measure, i.e., it is invariant and gives finite mass
to compact sets and positive mass to nonempty open sets."]
instance isHaarMeasure_haarMeasure (K‚ÇÄ : PositiveCompacts G) : IsHaarMeasure (haarMeasure K‚ÇÄ) := by
  apply
    isHaarMeasure_of_isCompact_nonempty_interior (haarMeasure K‚ÇÄ) K‚ÇÄ K‚ÇÄ.isCompact
      K‚ÇÄ.interior_nonempty
  ¬∑ simp only [haarMeasure_self]; exact one_ne_zero
  ¬∑ simp only [haarMeasure_self, ne_eq, ENNReal.one_ne_top, not_false_eq_true]

/-- `haar` is some choice of a Haar measure, on a locally compact group. -/
@[to_additive
"`addHaar` is some choice of a Haar measure, on a locally compact additive group."]
noncomputable abbrev haar [LocallyCompactSpace G] : Measure G :=
  haarMeasure <| Classical.arbitrary _

/-! Steinhaus theorem: if `E` has positive measure, then `E / E` contains a neighborhood of zero.
Note that this is not true for general regular Haar measures: in `‚Ñù √ó ‚Ñù` where the first factor
has the discrete topology, then `E = ‚Ñù √ó {0}` has infinite measure for the regular Haar measure,
but `E / E` does not contain a neighborhood of zero. On the other hand, it is always true for
inner regular Haar measures (and in particular for any Haar measure on a second countable group).
-/

open Pointwise

@[to_additive]
private lemma steinhaus_mul_aux (Œº : Measure G) [IsHaarMeasure Œº] [Œº.InnerRegularCompactLTTop]
    [LocallyCompactSpace G] (E : Set G) (hE : MeasurableSet E)
    (hEapprox : ‚àÉ K ‚äÜ E, IsCompact K ‚àß 0 < Œº K) : E / E ‚àà ùìù (1 : G) := by
  /- For any measure `Œº` and set `E` containing a compact set `K` of positive measure, there exists
  a neighborhood `V` of the identity such that `v ‚Ä¢ K \ K` has small measure for all `v ‚àà V`, say
  `< Œº K`. Then `v ‚Ä¢ K` and `K` can not be disjoint, as otherwise `Œº (v ‚Ä¢ K \ K) = Œº (v ‚Ä¢ K) = Œº K`.
  This show that `K / K` contains the neighborhood `V` of `1`, and therefore that it is
  itself such a neighborhood. -/
  obtain ‚ü®K, hKE, hK, K_closed, hKpos‚ü© : ‚àÉ K ‚äÜ E, IsCompact K ‚àß IsClosed K ‚àß 0 < Œº K := by
    obtain ‚ü®K, hKE, hK_comp, hK_meas‚ü© := hEapprox
    exact ‚ü®closure K, hK_comp.closure_subset_measurableSet hE hKE, hK_comp.closure,
      isClosed_closure, by rwa [hK_comp.measure_closure]‚ü©
  filter_upwards [eventually_nhds_one_measure_smul_diff_lt hK K_closed hKpos.ne' (Œº := Œº)] with g hg
  obtain ‚ü®_, ‚ü®x, hxK, rfl‚ü©, hgxK‚ü© : ‚àÉ x ‚àà g ‚Ä¢ K, x ‚àà K :=
     not_disjoint_iff.1 fun hd ‚Ü¶ by simp [hd.symm.sdiff_eq_right, measure_smul] at hg
  simpa using div_mem_div (hKE hgxK) (hKE hxK)

/-- **Steinhaus Theorem** for finite mass sets.

In any locally compact group `G` with an Haar measure `Œº` that's inner regular on finite measure
sets, for any measurable set `E` of finite positive measure, the set `E / E` is a neighbourhood of
`1`. -/
@[to_additive
"**Steinhaus Theorem** for finite mass sets.

In any locally compact group `G` with an Haar measure `Œº` that's inner regular on finite measure
sets, for any measurable set `E` of finite positive measure, the set `E - E` is a neighbourhood of
`0`. "]
theorem div_mem_nhds_one_of_haar_pos_ne_top (Œº : Measure G) [IsHaarMeasure Œº]
    [LocallyCompactSpace G] [Œº.InnerRegularCompactLTTop] (E : Set G) (hE : MeasurableSet E)
    (hEpos : 0 < Œº E) (hEfin : Œº E ‚â† ‚àû) : E / E ‚àà ùìù (1 : G) :=
  steinhaus_mul_aux Œº E hE <| hE.exists_lt_isCompact_of_ne_top hEfin hEpos

/-- **Steinhaus Theorem**.

In any locally compact group `G` with an inner regular Haar measure `Œº`,
for any measurable set `E` of positive measure, the set `E / E` is a neighbourhood of `1`. -/
@[to_additive
"**Steinhaus Theorem**.

In any locally compact group `G` with an inner regular Haar measure `Œº`,
for any measurable set `E` of positive measure, the set `E - E` is a neighbourhood of `0`."]
theorem div_mem_nhds_one_of_haar_pos (Œº : Measure G) [IsHaarMeasure Œº] [LocallyCompactSpace G]
    [InnerRegular Œº] (E : Set G) (hE : MeasurableSet E) (hEpos : 0 < Œº E) :
    E / E ‚àà ùìù (1 : G) := steinhaus_mul_aux Œº E hE <| hE.exists_lt_isCompact hEpos

section SecondCountable_SigmaFinite
/-! In this section, we investigate uniqueness of left-invariant measures without assuming that
the measure is finite on compact sets, but assuming œÉ-finiteness instead. We also rely on
second-countability, to ensure that the group operations are measurable: in this case, one can
bypass all topological arguments, and conclude using uniqueness of œÉ-finite left-invariant measures
in measurable groups.

For more general uniqueness statements without second-countability assumptions,
see the file `Mathlib/MeasureTheory/Measure/Haar/Unique.lean`.
-/

variable [SecondCountableTopology G]

/-- **Uniqueness of left-invariant measures**: In a second-countable locally compact group, any
  œÉ-finite left-invariant measure is a scalar multiple of the Haar measure.
  This is slightly weaker than assuming that `Œº` is a Haar measure (in particular we don't require
  `Œº ‚â† 0`).
  See also `isMulLeftInvariant_eq_smul_of_regular`
  for a statement not assuming second-countability. -/
@[to_additive
"**Uniqueness of left-invariant measures**: In a second-countable locally compact additive group,
  any œÉ-finite left-invariant measure is a scalar multiple of the additive Haar measure.
  This is slightly weaker than assuming that `Œº` is a additive Haar measure (in particular we don't
  require `Œº ‚â† 0`).
  See also `isAddLeftInvariant_eq_smul_of_regular`
  for a statement not assuming second-countability."]
theorem haarMeasure_unique (Œº : Measure G) [SigmaFinite Œº] [IsMulLeftInvariant Œº]
    (K‚ÇÄ : PositiveCompacts G) : Œº = Œº K‚ÇÄ ‚Ä¢ haarMeasure K‚ÇÄ := by
  have A : Set.Nonempty (interior (closure (K‚ÇÄ : Set G))) :=
    K‚ÇÄ.interior_nonempty.mono (interior_mono subset_closure)
  have := measure_eq_div_smul Œº (haarMeasure K‚ÇÄ)
    (measure_pos_of_nonempty_interior _ A).ne' K‚ÇÄ.isCompact.closure.measure_ne_top
  rwa [haarMeasure_closure_self, div_one, K‚ÇÄ.isCompact.measure_closure] at this

/-- Let `Œº` be a œÉ-finite left invariant measure on `G`. Then `Œº` is equal to the Haar measure
defined by `K‚ÇÄ` iff `Œº K‚ÇÄ = 1`. -/
@[to_additive]
theorem haarMeasure_eq_iff (K‚ÇÄ : PositiveCompacts G) (Œº : Measure G) [SigmaFinite Œº]
    [IsMulLeftInvariant Œº] :
    haarMeasure K‚ÇÄ = Œº ‚Üî Œº K‚ÇÄ = 1 :=
  ‚ü®fun h => h.symm ‚ñ∏ haarMeasure_self, fun h => by rw [haarMeasure_unique Œº K‚ÇÄ, h, one_smul]‚ü©

example [LocallyCompactSpace G] (Œº : Measure G) [IsHaarMeasure Œº] (K‚ÇÄ : PositiveCompacts G) :
    Œº = Œº K‚ÇÄ.1 ‚Ä¢ haarMeasure K‚ÇÄ :=
  haarMeasure_unique Œº K‚ÇÄ

/-- To show that an invariant œÉ-finite measure is regular it is sufficient to show that it is finite
  on some compact set with non-empty interior. -/
@[to_additive
"To show that an invariant œÉ-finite measure is regular it is sufficient to show that it is finite on
some compact set with non-empty interior."]
theorem regular_of_isMulLeftInvariant {Œº : Measure G} [SigmaFinite Œº] [IsMulLeftInvariant Œº]
    {K : Set G} (hK : IsCompact K) (h2K : (interior K).Nonempty) (hŒºK : Œº K ‚â† ‚àû) : Regular Œº := by
  rw [haarMeasure_unique Œº ‚ü®‚ü®K, hK‚ü©, h2K‚ü©]; exact Regular.smul hŒºK

end SecondCountable_SigmaFinite

end Group

end Measure

end MeasureTheory
