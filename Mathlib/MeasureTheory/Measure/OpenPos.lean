/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic
import Mathlib.MeasureTheory.Measure.Typeclasses.NoAtoms
import Mathlib.MeasureTheory.Measure.Typeclasses.Probability

/-!
# Measures positive on nonempty opens

In this file we define a typeclass for measures that are positive on nonempty opens, see
`MeasureTheory.Measure.IsOpenPosMeasure`. Examples include (additive) Haar measures, as well as
measures that have positive density with respect to a Haar measure. We also prove some basic facts
about these measures.

-/


open Topology ENNReal MeasureTheory

open Set Function Filter

namespace MeasureTheory

namespace Measure

section Basic

variable {X Y : Type*} [TopologicalSpace X] {m : MeasurableSpace X} [TopologicalSpace Y]
  [T2Space Y] (Î¼ Î½ : Measure X)

/-- A measure is said to be `IsOpenPosMeasure` if it is positive on nonempty open sets. -/
class IsOpenPosMeasure : Prop where
  open_pos : âˆ€ U : Set X, IsOpen U â†’ U.Nonempty â†’ Î¼ U â‰  0

variable [IsOpenPosMeasure Î¼] {s U F : Set X} {x : X}

theorem _root_.IsOpen.measure_ne_zero (hU : IsOpen U) (hne : U.Nonempty) : Î¼ U â‰  0 :=
  IsOpenPosMeasure.open_pos U hU hne

theorem _root_.IsOpen.measure_pos (hU : IsOpen U) (hne : U.Nonempty) : 0 < Î¼ U :=
  (hU.measure_ne_zero Î¼ hne).bot_lt

instance (priority := 100) [Nonempty X] : NeZero Î¼ :=
  âŸ¨measure_univ_pos.mp <| isOpen_univ.measure_pos Î¼ univ_nonemptyâŸ©

theorem _root_.IsOpen.measure_pos_iff (hU : IsOpen U) : 0 < Î¼ U â†” U.Nonempty :=
  âŸ¨fun h â†¦ nonempty_iff_ne_empty.2 fun he â†¦ h.ne' <| he.symm â–¸ measure_empty, hU.measure_pos Î¼âŸ©

theorem _root_.IsOpen.measure_eq_zero_iff (hU : IsOpen U) : Î¼ U = 0 â†” U = âˆ… := by
  simpa only [not_lt, nonpos_iff_eq_zero, not_nonempty_iff_eq_empty] using
    not_congr (hU.measure_pos_iff Î¼)

theorem measure_pos_of_nonempty_interior (h : (interior s).Nonempty) : 0 < Î¼ s :=
  (isOpen_interior.measure_pos Î¼ h).trans_le (measure_mono interior_subset)

theorem measure_pos_of_mem_nhds (h : s âˆˆ ð“ x) : 0 < Î¼ s :=
  measure_pos_of_nonempty_interior _ âŸ¨x, mem_interior_iff_mem_nhds.2 hâŸ©

theorem isOpenPosMeasure_smul {c : â„â‰¥0âˆž} (h : c â‰  0) : IsOpenPosMeasure (c â€¢ Î¼) :=
  âŸ¨fun _U Uo Une â†¦ mul_ne_zero h (Uo.measure_ne_zero Î¼ Une)âŸ©

variable {Î¼ Î½}

protected theorem AbsolutelyContinuous.isOpenPosMeasure (h : Î¼ â‰ª Î½) : IsOpenPosMeasure Î½ :=
  âŸ¨fun _U ho hne hâ‚€ â†¦ ho.measure_ne_zero Î¼ hne (h hâ‚€)âŸ©

theorem _root_.LE.le.isOpenPosMeasure (h : Î¼ â‰¤ Î½) : IsOpenPosMeasure Î½ :=
  h.absolutelyContinuous.isOpenPosMeasure

theorem _root_.IsOpen.measure_zero_iff_eq_empty (hU : IsOpen U) :
    Î¼ U = 0 â†” U = âˆ… :=
  âŸ¨fun h â†¦ (hU.measure_eq_zero_iff Î¼).mp h, fun h â†¦ by simp [h]âŸ©

theorem _root_.IsOpen.ae_eq_empty_iff_eq (hU : IsOpen U) :
    U =áµ[Î¼] (âˆ… : Set X) â†” U = âˆ… := by
  rw [ae_eq_empty, hU.measure_zero_iff_eq_empty]

/-- An open null set w.r.t. an `IsOpenPosMeasure` is empty. -/
theorem _root_.IsOpen.eq_empty_of_measure_zero (hU : IsOpen U) (hâ‚€ : Î¼ U = 0) : U = âˆ… :=
  (hU.measure_eq_zero_iff Î¼).mp hâ‚€

theorem _root_.IsClosed.ae_eq_univ_iff_eq (hF : IsClosed F) :
    F =áµ[Î¼] univ â†” F = univ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ by rw [h]âŸ©
  rwa [ae_eq_univ, hF.isOpen_compl.measure_eq_zero_iff Î¼, compl_empty_iff] at h

theorem _root_.IsClosed.measure_eq_univ_iff_eq [OpensMeasurableSpace X] [IsFiniteMeasure Î¼]
    (hF : IsClosed F) :
    Î¼ F = Î¼ univ â†” F = univ := by
  rw [â† ae_eq_univ_iff_measure_eq hF.measurableSet.nullMeasurableSet, hF.ae_eq_univ_iff_eq]

theorem _root_.IsClosed.measure_eq_one_iff_eq_univ [OpensMeasurableSpace X] [IsProbabilityMeasure Î¼]
    (hF : IsClosed F) :
    Î¼ F = 1 â†” F = univ := by
  rw [â† measure_univ (Î¼ := Î¼), hF.measure_eq_univ_iff_eq]

/-- A null set has empty interior. -/
theorem interior_eq_empty_of_null (hs : Î¼ s = 0) : interior s = âˆ… :=
  isOpen_interior.eq_empty_of_measure_zero <| measure_mono_null interior_subset hs

/-- A property satisfied almost everywhere is satisfied on a dense subset. -/
theorem dense_of_ae {p : X â†’ Prop} (hp : âˆ€áµ x âˆ‚Î¼, p x) : Dense {x | p x} := by
  rw [dense_iff_closure_eq, closure_eq_compl_interior_compl, compl_univ_iff]
  exact Î¼.interior_eq_empty_of_null hp

/-- If two functions are a.e. equal on an open set and are continuous on this set, then they are
equal on this set. -/
theorem eqOn_open_of_ae_eq {f g : X â†’ Y} (h : f =áµ[Î¼.restrict U] g) (hU : IsOpen U)
    (hf : ContinuousOn f U) (hg : ContinuousOn g U) : EqOn f g U := by
  replace h := ae_imp_of_ae_restrict h
  simp only [ae_iff, Classical.not_imp] at h
  have : IsOpen (U âˆ© { a | f a â‰  g a }) := by
    refine isOpen_iff_mem_nhds.mpr fun a ha â†¦ inter_mem (hU.mem_nhds ha.1) ?_
    rcases ha with âŸ¨ha : a âˆˆ U, ha' : (f a, g a) âˆˆ (diagonal Y)á¶œâŸ©
    exact
      (hf.continuousAt (hU.mem_nhds ha)).prodMk_nhds (hg.continuousAt (hU.mem_nhds ha))
        (isClosed_diagonal.isOpen_compl.mem_nhds ha')
  replace := (this.eq_empty_of_measure_zero h).le
  exact fun x hx â†¦ Classical.not_not.1 fun h â†¦ this âŸ¨hx, hâŸ©

/-- If two continuous functions are a.e. equal, then they are equal. -/
theorem eq_of_ae_eq {f g : X â†’ Y} (h : f =áµ[Î¼] g) (hf : Continuous f) (hg : Continuous g) : f = g :=
  suffices EqOn f g univ from funext fun _ â†¦ this trivial
  eqOn_open_of_ae_eq (ae_restrict_of_ae h) isOpen_univ hf.continuousOn hg.continuousOn

theorem eqOn_of_ae_eq {f g : X â†’ Y} (h : f =áµ[Î¼.restrict s] g) (hf : ContinuousOn f s)
    (hg : ContinuousOn g s) (hU : s âŠ† closure (interior s)) : EqOn f g s :=
  have : interior s âŠ† s := interior_subset
  (eqOn_open_of_ae_eq (ae_restrict_of_ae_restrict_of_subset this h) isOpen_interior (hf.mono this)
        (hg.mono this)).of_subset_closure
    hf hg this hU

variable (Î¼) in
theorem _root_.Continuous.ae_eq_iff_eq {f g : X â†’ Y} (hf : Continuous f) (hg : Continuous g) :
    f =áµ[Î¼] g â†” f = g :=
  âŸ¨fun h â†¦ eq_of_ae_eq h hf hg, fun h â†¦ h â–¸ EventuallyEq.rflâŸ©

theorem _root_.Continuous.isOpenPosMeasure_map [OpensMeasurableSpace X]
    {Z : Type*} [TopologicalSpace Z] [MeasurableSpace Z] [BorelSpace Z]
    {f : X â†’ Z} (hf : Continuous f) (hf_surj : Function.Surjective f) :
    (Measure.map f Î¼).IsOpenPosMeasure := by
  refine âŸ¨fun U hUo hUne â†¦ ?_âŸ©
  rw [Measure.map_apply hf.measurable hUo.measurableSet]
  exact (hUo.preimage hf).measure_ne_zero Î¼ (hf_surj.nonempty_preimage.mpr hUne)

protected theorem IsOpenPosMeasure.comap [BorelSpace X]
    {Z : Type*} [TopologicalSpace Z] {mZ : MeasurableSpace Z} [BorelSpace Z]
    (Î¼ : Measure Z) [IsOpenPosMeasure Î¼] {f : X â†’ Z} (hf : IsOpenEmbedding f) :
    (Î¼.comap f).IsOpenPosMeasure where
  open_pos U hU Une := by
    rw [hf.measurableEmbedding.comap_apply]
    exact IsOpenPosMeasure.open_pos _ (hf.isOpen_iff_image_isOpen.mp hU) (Une.image f)

end Basic

section LinearOrder

variable {X Y : Type*} [TopologicalSpace X] [LinearOrder X] [OrderTopology X]
  {m : MeasurableSpace X} [TopologicalSpace Y] [T2Space Y] (Î¼ : Measure X) [IsOpenPosMeasure Î¼]

theorem measure_Ioi_pos [NoMaxOrder X] (a : X) : 0 < Î¼ (Ioi a) :=
  isOpen_Ioi.measure_pos Î¼ nonempty_Ioi

theorem measure_Iio_pos [NoMinOrder X] (a : X) : 0 < Î¼ (Iio a) :=
  isOpen_Iio.measure_pos Î¼ nonempty_Iio

theorem measure_Ioo_pos [DenselyOrdered X] {a b : X} : 0 < Î¼ (Ioo a b) â†” a < b :=
  (isOpen_Ioo.measure_pos_iff Î¼).trans nonempty_Ioo

theorem measure_Ioo_eq_zero [DenselyOrdered X] {a b : X} : Î¼ (Ioo a b) = 0 â†” b â‰¤ a :=
  (isOpen_Ioo.measure_eq_zero_iff Î¼).trans (Ioo_eq_empty_iff.trans not_lt)

theorem eqOn_Ioo_of_ae_eq {a b : X} {f g : X â†’ Y} (hfg : f =áµ[Î¼.restrict (Ioo a b)] g)
    (hf : ContinuousOn f (Ioo a b)) (hg : ContinuousOn g (Ioo a b)) : EqOn f g (Ioo a b) :=
  eqOn_of_ae_eq hfg hf hg Ioo_subset_closure_interior

theorem eqOn_Ioc_of_ae_eq [DenselyOrdered X] {a b : X} {f g : X â†’ Y}
    (hfg : f =áµ[Î¼.restrict (Ioc a b)] g) (hf : ContinuousOn f (Ioc a b))
    (hg : ContinuousOn g (Ioc a b)) : EqOn f g (Ioc a b) :=
  eqOn_of_ae_eq hfg hf hg (Ioc_subset_closure_interior _ _)

theorem eqOn_Ico_of_ae_eq [DenselyOrdered X] {a b : X} {f g : X â†’ Y}
    (hfg : f =áµ[Î¼.restrict (Ico a b)] g) (hf : ContinuousOn f (Ico a b))
    (hg : ContinuousOn g (Ico a b)) : EqOn f g (Ico a b) :=
  eqOn_of_ae_eq hfg hf hg (Ico_subset_closure_interior _ _)

theorem eqOn_Icc_of_ae_eq [DenselyOrdered X] {a b : X} (hne : a â‰  b) {f g : X â†’ Y}
    (hfg : f =áµ[Î¼.restrict (Icc a b)] g) (hf : ContinuousOn f (Icc a b))
    (hg : ContinuousOn g (Icc a b)) : EqOn f g (Icc a b) :=
  eqOn_of_ae_eq hfg hf hg (closure_interior_Icc hne).symm.subset

end LinearOrder

end Measure

end MeasureTheory

open MeasureTheory MeasureTheory.Measure

namespace Metric

variable {X : Type*} [PseudoMetricSpace X] {m : MeasurableSpace X} (Î¼ : Measure X)
  [IsOpenPosMeasure Î¼]

theorem measure_ball_pos (x : X) {r : â„} (hr : 0 < r) : 0 < Î¼ (ball x r) :=
  isOpen_ball.measure_pos Î¼ (nonempty_ball.2 hr)

/-- See also `Metric.measure_closedBall_pos_iff`. -/
theorem measure_closedBall_pos (x : X) {r : â„} (hr : 0 < r) : 0 < Î¼ (closedBall x r) :=
  (measure_ball_pos Î¼ x hr).trans_le (measure_mono ball_subset_closedBall)

@[simp] lemma measure_closedBall_pos_iff {X : Type*} [MetricSpace X] {m : MeasurableSpace X}
    (Î¼ : Measure X) [IsOpenPosMeasure Î¼] [NoAtoms Î¼] {x : X} {r : â„} :
    0 < Î¼ (closedBall x r) â†” 0 < r := by
  refine âŸ¨fun h â†¦ ?_, measure_closedBall_pos Î¼ xâŸ©
  contrapose! h
  rw [(subsingleton_closedBall x h).measure_zero Î¼]

end Metric

namespace EMetric

variable {X : Type*} [PseudoEMetricSpace X] {m : MeasurableSpace X} (Î¼ : Measure X)
  [IsOpenPosMeasure Î¼]

theorem measure_ball_pos (x : X) {r : â„â‰¥0âˆž} (hr : r â‰  0) : 0 < Î¼ (ball x r) :=
  isOpen_ball.measure_pos Î¼ âŸ¨x, mem_ball_self hr.bot_ltâŸ©

theorem measure_closedBall_pos (x : X) {r : â„â‰¥0âˆž} (hr : r â‰  0) : 0 < Î¼ (closedBall x r) :=
  (measure_ball_pos Î¼ x hr).trans_le (measure_mono ball_subset_closedBall)

end EMetric

section MeasureZero
/-! ## Meagre sets and measure zero
In general, neither of meagre and measure zero implies the other.
- The set of Liouville numbers is a Lebesgue measure zero subset of â„, but is not meagre.
(In fact, its complement is meagre. See `Real.disjoint_residual_ae`.)

- The complement of the set of Liouville numbers in $[0,1]$ is meagre and has measure 1.
For another counterexample, for all $Î± âˆˆ (0,1)$, there is a generalised Cantor set $C âŠ† [0,1]$
of measure `Î±`. Cantor sets are nowhere dense (hence meagre). Taking a countable union of
fat Cantor sets whose measure approaches 1 even yields a meagre set of measure 1.

However, with respect to a measure which is positive on non-empty open sets, *closed* measure
zero sets are nowhere dense and Ïƒ-compact measure zero sets in a Hausdorff space are meagre.
-/

variable {X : Type*} [TopologicalSpace X] [MeasurableSpace X] {s : Set X}
  {Î¼ : Measure X} [IsOpenPosMeasure Î¼]

/-- A *closed* measure zero subset is nowhere dense. (Closedness is required: for instance, the
rational numbers are countable (thus have measure zero), but are dense (hence not nowhere dense). -/
lemma IsNowhereDense.of_isClosed_null (hâ‚s : IsClosed s) (hâ‚‚s : Î¼ s = 0) :
    IsNowhereDense s := hâ‚s.isNowhereDense_iff.mpr (interior_eq_empty_of_null hâ‚‚s)

/-- A Ïƒ-compact measure zero subset is meagre.
(More generally, every FÏƒ set of measure zero is meagre.) -/
lemma IsMeagre.of_isSigmaCompact_null [T2Space X] (hâ‚s : IsSigmaCompact s) (hâ‚‚s : Î¼ s = 0) :
    IsMeagre s := by
  rcases hâ‚s with âŸ¨K, hcompact, hcoverâŸ©
  have h (n : â„•) : IsNowhereDense (K n) := by
    have : Î¼ (K n) = 0 := measure_mono_null (hcover â–¸ subset_iUnion K n) hâ‚‚s
    exact .of_isClosed_null (hcompact n).isClosed this
  rw [isMeagre_iff_countable_union_isNowhereDense]
  exact âŸ¨range K, fun t âŸ¨n, hnâŸ© â†¦ hn â–¸ h n, countable_range K, hcover.symm.subsetâŸ©

end MeasureZero
