/-
Copyright (c) 2025 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
public import Mathlib.MeasureTheory.Measure.Tight
import Mathlib.MeasureTheory.Integral.Regular
import Mathlib.MeasureTheory.Integral.RieszMarkovKakutani.Real
import Mathlib.Topology.Separation.CompletelyRegular

/-!
# Prokhorov theorem

We prove statements about the compactness of sets of finite measures or probability measures,
notably several versions of Prokhorov theorem on tight sets of probability measures.

## Main statements

* `instCompactSpaceProbabilityMeasure` proves that the space of probability measures on a compact
space is itself compact
* `isCompact_setOf_probabilityMeasure_mass_eq_compl_isCompact_le`: Given a sequence of compact
  sets `Kâ‚™` and a sequence `uâ‚™` tending to zero, the probability measures giving mass at most `uâ‚™`
  to the complement of `Kâ‚™` form a compact set.
* `isCompact_closure_of_isTightMeasureSet`: Given a tight set of probability measures, its closure
  is compact.

Versions are also given for finite measures.

## Implementation

We do not assume second-countability or metrizability.

For the compactness of the space of probability measures in a compact space, we argue that every
ultrafilter converges, using the Riesz-Markov-Kakutani theorem to construct the limiting measure
in terms of its integrals against continuous functions.

For Prokhorov theorem `isCompact_setOf_probabilityMeasure_mass_eq_compl_isCompact_le`,
we rely on the compactness of the space of measures inside each compact set to get convergence
of the restriction there, and argue that the full measure converges to the sum of the individual
limits of the disjointed components. There is a subtlety that the space of finite measures
giving mass `uâ‚™` to `Kâ‚™á¶œ` doesn't have to be closed in our general setting, but we only need to
find *a* limit satisfying this condition. To ensure this, we need a technical condition
(monotonicity of `K` or normality of the space). In the first case, the bound follows readily
from the construction. In the second case, we modify the individual limits
(again using Riesz-Markov-Kakutani) to make sure that they are inner-regular, and then one can
check the condition.
-/

@[expose] public section

open scoped ENNReal NNReal CompactlySupported
open Filter Function Set Topology TopologicalSpace MeasureTheory BoundedContinuousFunction
  MeasureTheory.FiniteMeasure

variable {E : Type*} [MeasurableSpace E] [TopologicalSpace E] [T2Space E] [BorelSpace E]

variable (E) in
/-- In a compact space, the set of finite measures with mass at most `C` is compact. -/
theorem isCompact_setOf_finiteMeasure_le_of_compactSpace [CompactSpace E] (C : â„â‰¥0) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C} := by
  /- To prove the compactness, we will show that any sequence has a converging subsequence, in
  ultrafilters terms as things are not second countable. The integral against any bounded continuous
  function has a limit along the ultrafilter, by compactness of real intervals and the mass control.
  The limit is a monotone linear form. By the Riesz-Markov-Kakutani theorem, it comes from a
  measure. This measure is finite, of mass at most `C`. It provides the desired limit
  for the ultrafilter. -/
  apply isCompact_iff_ultrafilter_le_nhds'.2 (fun f hf â†¦ ?_)
  have L (g : C_c(E, â„)) :
      âˆƒ x âˆˆ Icc (-C * â€–g.toBoundedContinuousFunctionâ€–) (C * â€–g.toBoundedContinuousFunctionâ€–),
      Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ âˆ« x, g x âˆ‚Î¼) f (ğ“ x) := by
    simp only [Tendsto, â† Ultrafilter.coe_map]
    apply IsCompact.ultrafilter_le_nhds' isCompact_Icc
    simp only [neg_mul, Ultrafilter.mem_map]
    filter_upwards [hf] with Î¼ hÎ¼
    simp only [mem_preimage, mem_Icc]
    refine âŸ¨?_, ?_âŸ©
    Â· calc - (C * â€–g.toBoundedContinuousFunctionâ€–)
      _ â‰¤ âˆ« (x : E), - â€–g.toBoundedContinuousFunctionâ€– âˆ‚Î¼ := by
        simp only [integral_const, smul_eq_mul, mul_neg, neg_le_neg_iff]
        gcongr
        exact hÎ¼
      _ â‰¤ âˆ« x, g x âˆ‚Î¼ := by
        gcongr
        Â· simp
        Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
        Â· intro x
          apply neg_le_of_abs_le
          exact g.toBoundedContinuousFunction.norm_coe_le_norm x
    Â· calc âˆ« x, g x âˆ‚Î¼
      _ â‰¤ âˆ« (x : E), â€–g.toBoundedContinuousFunctionâ€– âˆ‚Î¼ := by
        gcongr
        Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
        Â· simp
        Â· intro x
          apply le_of_abs_le
          exact g.toBoundedContinuousFunction.norm_coe_le_norm x
      _ â‰¤ C * â€–g.toBoundedContinuousFunctionâ€– := by
        simp only [integral_const, smul_eq_mul]
        gcongr
        exact hÎ¼
  choose Î› hâ‚€Î› hÎ› using L
  let Î›' : C_c(E, â„) â†’â‚š[â„] â„ :=
  { toFun := Î›
    map_add' g g' := by
      have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ âˆ« x, g x + g' x âˆ‚Î¼) f (ğ“ (Î› g + Î› g')) := by
        convert (hÎ› g).add (hÎ› g')
        rw [integral_add]
        Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
        Â· exact g'.continuous.integrable_of_hasCompactSupport g'.hasCompactSupport
      exact tendsto_nhds_unique (hÎ› (g + g')) this
    map_smul' c g := by
      have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ âˆ« x, c â€¢ g x âˆ‚Î¼) f (ğ“ (c â€¢ Î› g)) := by
        convert (hÎ› g).const_smul c
        rw [integral_smul]
      exact tendsto_nhds_unique (hÎ› (c â€¢ g)) this
    monotone' g g' hgg' := by
      apply le_of_tendsto_of_tendsto' (hÎ› g) (hÎ› g') (fun Î¼ â†¦ ?_)
      apply integral_mono _ _ hgg'
      Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
      Â· exact g'.continuous.integrable_of_hasCompactSupport g'.hasCompactSupport }
  let Î¼lim := RealRMK.rieszMeasure Î›'
  have Î¼lim_le : Î¼lim univ â‰¤ ENNReal.ofReal C := by
    let o : C_c(E, â„) :=
    { toFun := 1
      hasCompactSupport' := HasCompactSupport.of_compactSpace 1 }
    have : Î¼lim univ â‰¤ ENNReal.ofReal (Î›' o) := RealRMK.rieszMeasure_le_of_eq_one Î›'
      (fun x â†¦ by simp [o]) isCompact_univ (fun x â†¦ by simp [o])
    apply this.trans
    gcongr
    apply le_of_tendsto (hÎ› o)
    filter_upwards [hf] with Î¼ hÎ¼ using by simpa [o] using hÎ¼
  let Î¼lim' : FiniteMeasure E := âŸ¨Î¼lim, âŸ¨Î¼lim_le.trans_lt (by simp)âŸ©âŸ©
  refine âŸ¨Î¼lim', ?_, ?_âŸ©
  Â· simp only [mem_setOf_eq, FiniteMeasure.mk_apply, Î¼lim', FiniteMeasure.mass]
    rw [show C = (ENNReal.ofReal â†‘C).toNNReal by simp]
    exact ENNReal.toNNReal_mono (by simp) Î¼lim_le
  change Tendsto id f (ğ“ Î¼lim')
  apply FiniteMeasure.tendsto_of_forall_integral_tendsto (fun g â†¦ ?_)
  let g' : C_c(E, â„) :=
  { toFun := g
    hasCompactSupport' := HasCompactSupport.of_compactSpace _ }
  convert hÎ› g'
  change âˆ« (x : E), g' x âˆ‚Î¼lim' = Î› g'
  simp only [FiniteMeasure.toMeasure_mk, RealRMK.integral_rieszMeasure, Î¼lim', Î¼lim]
  rfl

variable (E) in
/-- In a compact space, the set of finite measures with mass `C` is compact. -/
lemma isCompact_setOf_finiteMeasure_eq_of_compactSpace [CompactSpace E] (C : â„â‰¥0) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass = C} := by
  have : {Î¼ : FiniteMeasure E | Î¼.mass = C} = {Î¼ | Î¼.mass â‰¤ C} âˆ© {Î¼ | Î¼.mass = C} := by grind
  rw [this]
  apply IsCompact.inter_right (isCompact_setOf_finiteMeasure_le_of_compactSpace E C)
  exact isClosed_eq (by fun_prop) (by fun_prop)

/-- In a compact space, the space of probability measures is also compact. -/
instance [CompactSpace E] : CompactSpace (ProbabilityMeasure E) := by
  constructor
  apply (ProbabilityMeasure.toFiniteMeasure_isEmbedding E).isCompact_iff.2
  simpa using isCompact_setOf_finiteMeasure_eq_of_compactSpace E 1

/-- The set of finite measures of mass at most `C` supported on a given compact set `K` is
compact. -/
lemma isCompact_setOf_finiteMeasure_le_of_isCompact
    (C : â„â‰¥0) {K : Set E} (hK : IsCompact K) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ Î¼ Ká¶œ = 0} := by
  let f : K â†’ E := Subtype.val
  have hf : IsClosedEmbedding f := IsClosedEmbedding.subtypeVal hK.isClosed
  have rf : range f = K := Subtype.range_val
  let F : FiniteMeasure K â†’ FiniteMeasure E := fun Î¼ â†¦ Î¼.map f
  let T : Set (FiniteMeasure K) := {Î¼ | Î¼.mass â‰¤ C}
  have : {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ Î¼ Ká¶œ = 0} = F '' T := by
    apply Subset.antisymm
    Â· intro Î¼ hÎ¼
      simp only [mem_image]
      refine âŸ¨Î¼.comap f, (FiniteMeasure.mass_comap_le _ _).trans hÎ¼.1, ?_âŸ©
      ext s hs
      simp only [toMeasure_map, F]
      rw [Measure.map_apply measurable_subtype_coe hs]
      simp only [toMeasure_comap]
      rw [Measure.comap_apply _ (Subtype.val_injective), image_preimage_eq_inter_range]
      Â· rw [â† Measure.restrict_apply hs, Measure.restrict_eq_self_of_ae_mem]
        apply (null_iff_toMeasure_null (â†‘Î¼) (range f)á¶œ).mp
        rw [rf]
        exact hÎ¼.2
      Â· exact fun t ht â†¦ hf.measurableEmbedding.measurableSet_image' ht
      Â· exact hf.continuous.measurable hs
    Â· simp only [null_iff_toMeasure_null, image_subset_iff, preimage_setOf_eq, toMeasure_map,
        setOf_subset_setOf, F, T]
      intro Î¼ hÎ¼
      rw [Measure.map_apply hf.continuous.measurable hK.measurableSet.compl]
      refine âŸ¨(mass_map_le _ _).trans hÎ¼, by simp [f]âŸ©
  rw [this]
  apply IsCompact.image _ (by fun_prop)
  have : CompactSpace K := isCompact_iff_compactSpace.mp hK
  exact isCompact_setOf_finiteMeasure_le_of_compactSpace _ _

/-- **Prokhorov theorem**: Given a sequence of compact sets `Kâ‚™` and a sequence `uâ‚™` tending
to zero, the finite measures of mass at most `C` giving mass at most `uâ‚™` to the complement of `Kâ‚™`
form a compact set. -/
lemma isCompact_setOf_finiteMeasure_mass_le_compl_isCompact_le
    {u : â„• â†’ â„â‰¥0} {K : â„• â†’ Set E} (C : â„â‰¥0)
    (hu : Tendsto u atTop (ğ“ 0)) (hK : âˆ€ n, IsCompact (K n)) (h : NormalSpace E âˆ¨ Monotone K) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} := by
  /- Consider a sequence of measures with mass at most `C` and giving mass at most `uâ‚™` to `Kâ‚™á¶œ`,
  for which we want to find a converging subsequence.
  We want to write `â‹ƒ n, Kâ‚™` as the disjoint union of `disjointed K n`, restrict the measures to
  each of these sets (which is contained in the compact set `Kâ‚™`), extract a converging subsequence
  there, to a limit `Î½â‚™`, and then argue that the sequence converges to `Î¼ := âˆ‘ Î½â‚™`.
  We will implement this rough idea, but there are two technical complications.
  First, we should not use a sequence and subsequences, but a ultrafilter as things are not second
  countable.
  Second, it is not obvious that the limit will satisfy the inequality `Î¼ (Kâ‚™á¶œ) â‰¤ uâ‚™`, as this is
  not a closed condition in the space of measures in general (note that we are not assuming that
  our space is metrizable). To check this inequality, we need the technical condition that the
  space is normal or the sequence `K` is monotone. When the space is normal, the inequality can be
  proved from the weak convergence if we can ensure additionally that `Î¼` is
  inner regular. We will guarantee this by making sure each `Î½â‚™` is inner regular. When the
  sequence `K` is monotone, on the other hand, the bound readily follows from the construction.
  -/
  -- We can decompose a measure as a sum of restrictions to `disjointed K n`, finite version.
  have I (Î¼ : FiniteMeasure E) (n : â„•) :
      âˆ‘ i âˆˆ Finset.range (n + 1), Î¼.restrict (disjointed K i) = Î¼.restrict (partialSups K n) := by
    rw [â† biUnion_range_succ_disjointed, FiniteMeasure.restrict_biUnion_finset]
    Â· exact (disjoint_disjointed K).set_pairwise _
    Â· exact MeasurableSet.disjointed (fun i â†¦ (hK i).measurableSet)
  have A n : IsCompact (partialSups K n) := by
    simpa [partialSups_eq_accumulate] using isCompact_accumulate hK _
  -- start with a ultrafilter `f`, for which we want to prove convergence.
  apply isCompact_iff_ultrafilter_le_nhds'.2 (fun f hf â†¦ ?_)
  -- the restrictions to `disjointed K n` converge along the ultrafilter, and moreover we can
  -- choose the limit to be inner regular.
  have M n : âˆƒ (Î½ : FiniteMeasure E), Measure.InnerRegular (Î½ : Measure E) âˆ§
      Î½ (partialSups K n)á¶œ = 0 âˆ§
      Tendsto (fun (Ï : FiniteMeasure E) â†¦ Ï.restrict (disjointed K n)) f (ğ“ Î½) := by
    -- the existence of a limit follows from the fact that these measures are supported in
    -- the compact set `partialSups K n`.
    obtain âŸ¨Î½, hÎ½, Î½_limâŸ© : âˆƒ Î½ âˆˆ {Ï : FiniteMeasure E | Ï.mass â‰¤ C âˆ§ Ï (partialSups K n)á¶œ = 0},
        Tendsto (fun (Ï : FiniteMeasure E) â†¦ Ï.restrict (disjointed K n)) f (ğ“ Î½) := by
      simp only [Tendsto]
      rw [â† Ultrafilter.coe_map]
      apply IsCompact.ultrafilter_le_nhds'
        (isCompact_setOf_finiteMeasure_le_of_isCompact C (A n))
      simp only [null_iff_toMeasure_null, Ultrafilter.mem_map, preimage_setOf_eq]
      filter_upwards [hf] with Ï hÏ
      simp only [restrict_mass, restrict_measure_eq,
        Measure.restrict_apply (A n).measurableSet.compl]
      refine âŸ¨(apply_le_mass Ï _).trans hÏ.1, ?_âŸ©
      convert measure_empty (Î¼ := (Ï : Measure E))
      apply disjoint_iff.1
      apply disjoint_compl_left.mono_right
      exact le_trans sdiff_le (le_partialSups _ _)
    -- We can find an inner regular measure which coincides with the above limit wrt
    -- integration of bounded continuous functions.
    obtain âŸ¨Î½', Î½'_reg, Î½'_fin, Î½'K, hÎ½'âŸ© : âˆƒ Î½', Î½'.InnerRegular âˆ§ IsFiniteMeasure Î½' âˆ§
        Î½' (partialSups K n)á¶œ = 0 âˆ§ âˆ€ (g : E â†’áµ‡ â„), âˆ« x, g x âˆ‚Î½ = âˆ« x, g x âˆ‚Î½' := by
      apply Measure.exists_innerRegular_eq_of_isCompact _ (A n)
      rw [â† MeasureTheory.FiniteMeasure.null_iff_toMeasure_null]
      exact hÎ½.2
    -- This inner regular measure is also a limit for our ultrafilter
    let Î¼ : FiniteMeasure E := âŸ¨Î½', Î½'_finâŸ©
    refine âŸ¨Î¼, Î½'_reg, by simp [Î¼, Î½'K], ?_âŸ©
    apply tendsto_of_forall_integral_tendsto (fun g â†¦ ?_)
    convert tendsto_iff_forall_integral_tendsto.1 Î½_lim g using 2
    exact (hÎ½' g).symm
  -- let `Î½â‚™` be such nice limits on `disjointed K n`.
  choose! Î½ Î½_reg Î½K hÎ½ using M
  -- their sum is a finite measure, of mass at most `C`.
  have B : (Measure.sum (fun n â†¦ (Î½ n : Measure E))) univ â‰¤ C := by
    -- this follows from the corresponding result for finite sums, where we can use the
    -- continuity of the mass of a finite measure under weak convergence.
    simp only [MeasurableSet.univ, Measure.sum_apply]
    have : Tendsto (fun n â†¦ âˆ‘ i âˆˆ Finset.range (n + 1), (Î½ i : Measure E) univ) atTop
        (ğ“ (âˆ‘' i, (Î½ i : Measure E) univ)) :=
      (ENNReal.tendsto_nat_tsum _).comp (tendsto_add_atTop_nat 1)
    apply le_of_tendsto' this (fun n â†¦ ?_)
    have : âˆ‘ i âˆˆ Finset.range (n + 1), (Î½ i : Measure E) univ
        = (âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i).toMeasure univ := by simp
    rw [this]
    suffices (âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i).mass â‰¤ C by
      convert ENNReal.coe_le_coe.2 this
      simp
    have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦
        (âˆ‘ i âˆˆ Finset.range (n + 1), Î¼.restrict (disjointed K i)).mass) f
        (ğ“ ((âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i).mass)) := by
      apply Tendsto.mass
      exact tendsto_finset_sum _ (fun i hi â†¦ hÎ½ i)
    apply le_of_tendsto this
    filter_upwards [hf] with Î¼ hÎ¼
    rw [I, restrict_mass]
    exact le_trans (apply_mono _ (subset_univ _)) hÎ¼.1
  -- Let `Î¼` be the limiting measure
  let Î¼ : FiniteMeasure E := âŸ¨Measure.sum (fun n â†¦ (Î½ n : Measure E)), âŸ¨B.trans_lt (by simp)âŸ©âŸ©
  -- first, we show that it is indeed a limit of the ultrafilter
  have L : Tendsto id f (ğ“ Î¼) := by
    -- We need to check the convergence of the integral of a bounded continuous function.
    -- Finite sums of restrictions to `disjointed K n` converge obviously to finite sums of `Î½â‚™`,
    -- but we need to control the infinite sums. For this, we split `Îµ` in 3, argue that for `Î¼`
    -- this is the limit of finite sums, and inside the space we can uniformly truncate the sum
    -- also as the tail is controlled by `uâ‚™`. Once we have fixed a truncation level satisfying
    -- both conditions, we can rely on the finite sum convergence to conclude.
    apply tendsto_of_forall_integral_tendsto (fun g â†¦ ?_)
    rw [Metric.tendsto_nhds]
    intro Îµ Îµpos
    -- first, control truncation of the finite sums for the limiting measure
    have I1 : âˆ€á¶  n in atTop,
        dist (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i)) (âˆ« x, g x âˆ‚Î¼) < Îµ / 3 := by
      have : Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range n, Î½ i)) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼)) := by
        simp only [FiniteMeasure.toMeasure_mk, Î¼]
        rw [integral_sum_measure (g.integrable (Î¼ := Î¼))]
        simp_rw [integral_finset_sum_measure (fun i hi â†¦ g.integrable _)]
        apply Summable.tendsto_sum_tsum_nat
        apply (hasSum_integral_measure _).summable
        exact g.integrable (Î¼ := Î¼)
      exact Metric.tendsto_nhds.1 (this.comp (tendsto_add_atTop_nat 1)) _ (by positivity)
    -- second, truncation threshold in terms of tails `uâ‚™` (the relevance of this condition will
    -- appear below).
    have I2 : âˆ€á¶  n in atTop, â€–gâ€– * u n < Îµ / 3 := by
      have := (NNReal.tendsto_coe.2 hu).const_mul (â€–gâ€–)
      simp only [NNReal.coe_zero, mul_zero] at this
      exact (tendsto_order.1 this).2 (Îµ / 3) (by positivity)
    -- fix a large `n` satisfying both truncation conditions
    rcases (I1.and I2).exists with âŸ¨n, hn, h'nâŸ©
    -- the finite sums up to the fixed `n` converge to the limit, by convergence of individual
    -- summands
    have : Tendsto (fun (Ï : FiniteMeasure E) â†¦
        âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i) : FiniteMeasure E)) f
        (ğ“ (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i : FiniteMeasure E))) := by
      apply tendsto_iff_forall_integral_tendsto.1 _ g
      apply tendsto_finset_sum _ (fun i hi â†¦ hÎ½ i)
    -- therefore, after some point the difference is bounded by `Îµ / 3`.
    filter_upwards [Metric.tendsto_nhds.1 this (Îµ / 3) (by positivity), hf] with Ï hÏ h'Ï
    -- let us show that in this case the full integrals differ by at most `Îµ`.
    calc dist (âˆ« (x : E), g x âˆ‚Ï) (âˆ« (x : E), g x âˆ‚Î¼)
    -- we separate away the tails from the sums up to `n`
    _ â‰¤ dist (âˆ« (x : E), g x âˆ‚Ï)
          (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i)))
        + dist (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i)))
          (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i))
        + dist (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i)) (âˆ« (x : E), g x âˆ‚Î¼) :=
      dist_triangle4 _ _ _ _
    -- each term is bounded by `Îµ / 3` by design.
    _ < Îµ / 3 + Îµ / 3 + Îµ / 3 := by
      gcongr
      Â· have : Ï = Ï.restrict (partialSups K n)á¶œ +
            âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i) := by
          rw [I, â† FiniteMeasure.restrict_union disjoint_compl_left (A n).measurableSet]
          simp
        nth_rewrite 1 [this]
        rw [toMeasure_add, integral_add_measure (g.integrable _) (g.integrable _)]
        simp only [toMeasure_sum, dist_add_self_left]
        calc â€–âˆ« x, g x âˆ‚(Ï.restrict ((partialSups K) n)á¶œ)â€–
        _ â‰¤ âˆ« x, â€–g xâ€– âˆ‚(Ï.restrict ((partialSups K) n)á¶œ) := norm_integral_le_integral_norm _
        _ â‰¤ âˆ« x, â€–gâ€– âˆ‚(Ï.restrict ((partialSups K) n)á¶œ : Measure E) := by
          apply integral_mono_of_nonneg
          Â· filter_upwards with x using by positivity
          Â· simp
          Â· filter_upwards with x using norm_coe_le_norm g x
        _ = â€–gâ€– * Ï ((partialSups K) n)á¶œ := by simp [mul_comm]
        _ â‰¤ â€–gâ€– * Ï (K n)á¶œ := by gcongr; apply le_partialSups
        _ â‰¤ â€–gâ€– * u n := by gcongr; exact h'Ï.2 n
        _ < Îµ / 3 := h'n
      Â· simpa using hÏ
    _ = Îµ := by ring
  -- Now that we have proved the convergence, we can finish the proof of the theorem. It remains
  -- to check the mass control of the limit (which we have already done when proving finiteness)
  -- and to show that `Î¼ (Kâ‚™á¶œ) â‰¤ uâ‚™`, which is harder.
  refine âŸ¨Î¼, âŸ¨?_, fun n â†¦ ?_âŸ©, LâŸ©
  Â· simp only [mass, mk_apply, Î¼]
    rw [show C = (C : â„â‰¥0âˆ).toNNReal by simp]
    exact ENNReal.toNNReal_mono (by simp) B
  -- Let us now prove that `Î¼ (Kâ‚™á¶œ) â‰¤ uâ‚™`. We argue differently depending on whether the space is
  -- normal or if the sequence `K` is monotone.
  rcases h with h | h
  Â· -- To show that `Î¼ (Kâ‚™á¶œ) â‰¤ uâ‚™` when the space is normal, we argue that `Î¼ (Kâ‚™á¶œ)` is the
    -- supremum of the integrals of continuous functions supported in `Kâ‚™á¶œ` and bounded by `1`,
    -- as the measure is inner regular. Therefore, we are reduced to a question about integrals of
    -- continuous functions, for which we can take advantage of the weak convergence.
    have : Measure.InnerRegular (Î¼ : Measure E) := by simp only [toMeasure_mk, Î¼]; infer_instance
    rw [â† ENNReal.coe_le_coe, ennreal_coeFn_eq_coeFn_toMeasure,
      (hK n).isClosed.isOpen_compl.measure_eq_biSup_integral_continuous]
    simp only [compl_compl, iSup_le_iff, ENNReal.ofReal_le_coe]
    intro g g_cont gK g_nonneg g_le
    have : Tendsto (fun (Ï : FiniteMeasure E) â†¦ âˆ« x, g x âˆ‚Ï) f (ğ“ (âˆ« x, g x âˆ‚Î¼)) := by
      let g' : E â†’áµ‡ â„ :=
      { toFun := g
        map_bounded' := by
          refine âŸ¨1, fun x y â†¦ ?_âŸ©
          simp only [dist, abs_le, neg_le_sub_iff_le_add, tsub_le_iff_right]
          exact âŸ¨(g_le y).trans (by simpa using g_nonneg x),
            (g_le x).trans (by simpa using g_nonneg y)âŸ© }
      exact tendsto_iff_forall_integral_tendsto.1 L g'
    apply le_of_tendsto this
    filter_upwards [hf] with Ï hÏ
    calc âˆ« x, g x âˆ‚Ï
    _ â‰¤ âˆ« x, indicator (K n)á¶œ 1 x âˆ‚Ï := by
      apply integral_mono_of_nonneg
      Â· filter_upwards [] with x using g_nonneg x
      Â· apply Integrable.indicator (integrable_const _) (hK n).measurableSet.compl
      Â· filter_upwards [] with x
        by_cases hx : x âˆˆ (K n)á¶œ
        Â· simpa [hx] using g_le x
        Â· simp only [hx, not_false_eq_true, indicator_of_notMem]
          apply le_of_eq
          apply gK
          simpa using hx
    _ = Ï (K n)á¶œ := by simp [integral_indicator (hK n).measurableSet.compl]
    _ â‰¤ u n := mod_cast hÏ.2 n
  Â· -- to show that `Î¼ (Kâ‚™á¶œ) â‰¤ uâ‚™` when the sequence `K` is monotone, we argue that the only
    -- contribution to `Î¼ (Kâ‚™á¶œ)` comes from the measures `Î½áµ¢` with `i > n`. Then we restrict to
    -- a finite sum `âˆ‘ i âˆˆ Ioc n m, Î½áµ¢`, and argue that it is the limit of
    -- `âˆ‘ i âˆˆ Ioc n m, Ï.restricted (K i \ K(i - 1))`, i.e., `Ï.restricted (K m \ K n)`. The total
    -- mass converges (thanks to the weak convergence of finite sums), and the total mass of
    -- `Ï.restricted (K m \ K n)` is bounded by `Ï (Kâ‚™á¶œ) â‰¤ uâ‚™`.
    suffices (Î¼ : Measure E) (K n)á¶œ â‰¤ u n by
      apply ENNReal.coe_le_coe.1
      convert this
      simp
    simp only [toMeasure_mk, (hK n).measurableSet.compl, Measure.sum_apply, Î¼]
    have : Tendsto (fun m â†¦ âˆ‘ i âˆˆ Finset.range (m + 1), (Î½ i : Measure E) (K n)á¶œ) atTop
        (ğ“ (âˆ‘' i, (Î½ i : Measure E) (K n)á¶œ)) :=
      (ENNReal.tendsto_nat_tsum _).comp (tendsto_add_atTop_nat 1)
    apply le_of_tendsto this
    filter_upwards [Ici_mem_atTop n] with m (hm : n â‰¤ m)
    have : âˆ‘ i âˆˆ Finset.range (m + 1), (Î½ i : Measure E) (K n)á¶œ
        = âˆ‘ i âˆˆ Finset.Ioc n m, (Î½ i : Measure E) (K n)á¶œ := by
      apply (Finset.sum_subset (by grind) _).symm
      simp +contextual only [Finset.mem_range_succ_iff, Finset.mem_Ioc, not_and,
        not_true_eq_false, imp_false, not_lt, â† null_iff_toMeasure_null]
      intro i hi h'i
      apply (Î½ i).mono_null _ (Î½K i)
      rw [Monotone.partialSups_eq h]
      exact compl_subset_compl.2 (h h'i)
    rw [this]
    suffices (âˆ‘ i âˆˆ Finset.Ioc n m, Î½ i).toMeasure univ â‰¤ u n by
      apply le_trans _ this
      simp only [toMeasure_sum, Measure.coe_finset_sum, Finset.sum_apply]
      gcongr
      simp
    suffices (âˆ‘ i âˆˆ Finset.Ioc n m, Î½ i).mass â‰¤ u n by
      convert ENNReal.coe_le_coe.2 this
      simp
    have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦
        (âˆ‘ i âˆˆ Finset.Ioc n m, Î¼.restrict (disjointed K i)).mass) f
        (ğ“ ((âˆ‘ i âˆˆ Finset.Ioc n m, Î½ i).mass)) := by
      apply Tendsto.mass
      exact tendsto_finset_sum _ (fun i hi â†¦ hÎ½ i)
    apply le_of_tendsto this
    filter_upwards [hf] with Î¼ hÎ¼
    have : âˆ‘ i âˆˆ Finset.Ioc n m, Î¼.restrict (disjointed K i) = Î¼.restrict (K m \ K n) := by
      rw [â† biUnion_Ioc_disjointed_of_monotone h hm, FiniteMeasure.restrict_biUnion_finset]
      Â· exact (disjoint_disjointed K).set_pairwise _
      Â· exact MeasurableSet.disjointed (fun i â†¦ (hK i).measurableSet)
    rw [this, restrict_mass]
    exact le_trans (apply_mono _ (diff_subset_compl (K m) (K n))) (hÎ¼.2 n)

/-- **Prokhorov theorem**: Given a sequence of compact sets `Kâ‚™` and a sequence `uâ‚™` tending to
zero, the finite measures of mass `C` giving mass at most `uâ‚™` to the complement of `Kâ‚™` form a
compact set. -/
lemma isCompact_setOf_finiteMeasure_mass_eq_compl_isCompact_le {u : â„• â†’ â„â‰¥0}
    {K : â„• â†’ Set E} (C : â„â‰¥0) (hu : Tendsto u atTop (ğ“ 0)) (hK : âˆ€ n, IsCompact (K n))
    (h : NormalSpace E âˆ¨ Monotone K) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass = C âˆ§ âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} := by
  have : {Î¼ : FiniteMeasure E | Î¼.mass = C âˆ§ âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} =
    {Î¼ | Î¼.mass â‰¤ C âˆ§ âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} âˆ© {Î¼ | Î¼.mass = C} := by ext; grind
  rw [this]
  apply IsCompact.inter_right (isCompact_setOf_finiteMeasure_mass_le_compl_isCompact_le C hu hK h)
  exact isClosed_eq (by fun_prop) (by fun_prop)

/-- **Prokhorov theorem**: Given a sequence of compact sets `Kâ‚™` and a sequence `uâ‚™` tending to
zero, the probability measures giving mass at most `uâ‚™` to the complement of `Kâ‚™` form a
compact set. -/
lemma isCompact_setOf_probabilityMeasure_mass_eq_compl_isCompact_le {u : â„• â†’ â„â‰¥0}
    {K : â„• â†’ Set E} (hu : Tendsto u atTop (ğ“ 0)) (hK : âˆ€ n, IsCompact (K n))
    (h : NormalSpace E âˆ¨ Monotone K) :
    IsCompact {Î¼ : ProbabilityMeasure E | âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} := by
  apply (ProbabilityMeasure.toFiniteMeasure_isEmbedding E).isCompact_iff.2
  have : ProbabilityMeasure.toFiniteMeasure '' {Î¼ | âˆ€ (n : â„•), Î¼ (K n)á¶œ â‰¤ u n}
      = {Î¼ : FiniteMeasure E | Î¼.mass = 1 âˆ§ âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} := by
    ext Î¼
    simp only [mem_image, mem_setOf_eq]
    refine âŸ¨?_, ?_âŸ©
    Â· rintro âŸ¨Î½, hÎ½, rflâŸ©
      simpa using hÎ½
    Â· rintro âŸ¨hÎ¼, h'Î¼âŸ©
      let Î½ : ProbabilityMeasure E := âŸ¨Î¼, isProbabilityMeasure_iff_real.2 (by simpa using hÎ¼)âŸ©
      have : Î½.toFiniteMeasure = Î¼ := by ext; rfl
      exact âŸ¨Î½, by simpa [â† this] using h'Î¼ , thisâŸ©
  rw [this]
  exact isCompact_setOf_finiteMeasure_mass_eq_compl_isCompact_le 1 hu hK h

/-- **Prokhorov theorem**: the closure of a tight set of probability measures is compact.
We only require the space to be T2. -/
lemma isCompact_closure_of_isTightMeasureSet {S : Set (ProbabilityMeasure E)}
    (hS : IsTightMeasureSet {((Î¼ : ProbabilityMeasure E) : Measure E) | Î¼ âˆˆ S}) :
    IsCompact (closure S) := by
  obtain âŸ¨u, -, u_pos, u_limâŸ© :
      âˆƒ u : â„• â†’ â„â‰¥0, StrictAnti u âˆ§ (âˆ€ n, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto 0
  have A n : âˆƒ (K : Set E), IsCompact K âˆ§ âˆ€ Î¼ âˆˆ S, Î¼ Ká¶œ â‰¤ u n := by
    rcases isTightMeasureSet_iff_exists_isCompact_measure_compl_le.1 hS (u n)
      (by norm_cast; exact u_pos n) with âŸ¨K, K_comp, hKâŸ©
    refine âŸ¨K, K_comp, fun Î¼ hÎ¼ â†¦ ?_âŸ©
    have : (Î¼ : Measure E) Ká¶œ â‰¤ u n := hK _ âŸ¨Î¼, hÎ¼, rflâŸ©
    exact ENNReal.coe_le_coe.1 (by simpa using this)
  choose K K_comp hK using A
  let K' n := â‹ƒ i âˆˆ Iic n, K i
  have h'K : IsCompact {Î¼ : ProbabilityMeasure E | âˆ€ n, Î¼ (K' n)á¶œ â‰¤ u n} := by
    apply isCompact_setOf_probabilityMeasure_mass_eq_compl_isCompact_le u_lim
    Â· exact fun n â†¦ (finite_Iic n).isCompact_biUnion (fun i hi â†¦ K_comp i)
    Â· right
      simp only [Monotone, mem_Iic, le_eq_subset, iUnion_subset_iff, K']
      intro a b hab i hi
      apply subset_biUnion_of_mem
      exact hi.trans hab
  apply IsCompact.closure_of_subset h'K
  intro Î¼ hÎ¼ n
  calc Î¼ (K' n)á¶œ
  _ â‰¤ Î¼ (K n)á¶œ := by
    gcongr
    simp only [mem_Iic, K']
    apply subset_biUnion_of_mem
    exact le_rfl (a := n)
  _ â‰¤ u n := by grind
