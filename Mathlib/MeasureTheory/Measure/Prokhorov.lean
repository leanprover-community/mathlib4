/-
Copyright (c) 2025 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib

/-!
# Prokhorov theorem

-/

@[expose] public section

open scoped ENNReal NNReal
open CompactlySupported CompactlySupportedContinuousMap Filter Function Set Topology
  TopologicalSpace MeasureTheory BoundedContinuousFunction MeasureTheory.FiniteMeasure



@[simps] def CompactlySupportedContinuousMap.toBoundedContinuousFunction {Î± Î² : Type*}
    [TopologicalSpace Î±] [PseudoMetricSpace Î²] [Zero Î²]
    (f : C_c(Î±, Î²)) : Î± â†’áµ‡ Î² where
  toFun := f
  map_bounded' := by
    have : IsCompact (range f) := f.hasCompactSupport.isCompact_range f.continuous
    rcases Metric.isBounded_iff.1 this.isBounded with âŸ¨C, hCâŸ©
    exact âŸ¨C, by grindâŸ©

variable {E : Type*} [MeasurableSpace E] [TopologicalSpace E] [T2Space E] [BorelSpace E]

variable (E) in
/-- In a compact space, the set of finite measures with mass at most `C` is compact. -/
theorem isCompact_setOf_finiteMeasure_le_of_compactSpace [CompactSpace E] (C : â„â‰¥0) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C} := by
  /- To prove the compactness, we will show that any sequence has a converging subsequence, in
  ultrafilters terms as things are not second countable. The integral against any bounded continuous
  function has a limit along the ultrafilter, by compactness of real intervals and the mass control.
  The limit is a monotone linear form. By the Riesz-Markov-Kakutani theorem, it comes from a
  measure. This measure is finite, of mass at most `C`. It provides the desired limit
  for the ultrafilter. -/
  apply isCompact_iff_ultrafilter_le_nhds'.2 (fun f hf â†¦ ?_)
  have L (g : C_c(E, â„)) :
      âˆƒ x âˆˆ Icc (-C * â€–g.toBoundedContinuousFunctionâ€–) (C * â€–g.toBoundedContinuousFunctionâ€–),
      Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ âˆ« x, g x âˆ‚ Î¼) f (ğ“ x) := by
    simp only [Tendsto, â† Ultrafilter.coe_map]
    apply IsCompact.ultrafilter_le_nhds' isCompact_Icc
    simp only [neg_mul, Ultrafilter.mem_map]
    filter_upwards [hf] with Î¼ hÎ¼
    simp only [mem_preimage, mem_Icc]
    refine âŸ¨?_, ?_âŸ©
    Â· calc - (C * â€–g.toBoundedContinuousFunctionâ€–)
      _ â‰¤ âˆ« (x : E), - â€–g.toBoundedContinuousFunctionâ€– âˆ‚Î¼ := by
        simp only [integral_const, smul_eq_mul, mul_neg, neg_le_neg_iff]
        gcongr
        exact hÎ¼
      _ â‰¤ âˆ« (x : E), g x âˆ‚Î¼ := by
        apply integral_mono
        Â· simp
        Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
        Â· intro x
          apply neg_le_of_abs_le
          exact g.toBoundedContinuousFunction.norm_coe_le_norm x
    Â· calc âˆ« (x : E), g x âˆ‚Î¼
      _ â‰¤ âˆ« (x : E), â€–g.toBoundedContinuousFunctionâ€– âˆ‚Î¼ := by
        apply integral_mono
        Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
        Â· simp
        Â· intro x
          apply le_of_abs_le
          exact g.toBoundedContinuousFunction.norm_coe_le_norm x
      _ â‰¤ C * â€–g.toBoundedContinuousFunctionâ€– := by
        simp only [integral_const, smul_eq_mul]
        gcongr
        exact hÎ¼
  choose Î› hâ‚€Î› hÎ› using L
  let Î›' : C_c(E, â„) â†’â‚š[â„] â„ :=
  { toFun := Î›
    map_add' g g' := by
      have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ âˆ« (x : E), g x + g' x âˆ‚Î¼)
          f (ğ“ (Î› g + Î› g')) := by
        convert (hÎ› g).add (hÎ› g')
        rw [integral_add]
        Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
        Â· exact g'.continuous.integrable_of_hasCompactSupport g'.hasCompactSupport
      exact tendsto_nhds_unique (hÎ› (g + g')) this
    map_smul' c g := by
      have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ âˆ« (x : E), c â€¢ g x âˆ‚Î¼)
          f (ğ“ (c â€¢ Î› g)) := by
        convert (hÎ› g).const_smul c
        rw [integral_smul]
      exact tendsto_nhds_unique (hÎ› (c â€¢ g)) this
    monotone' g g' hgg' := by
      apply le_of_tendsto_of_tendsto' (hÎ› g) (hÎ› g') (fun Î¼ â†¦ ?_)
      apply integral_mono _ _ hgg'
      Â· exact g.continuous.integrable_of_hasCompactSupport g.hasCompactSupport
      Â· exact g'.continuous.integrable_of_hasCompactSupport g'.hasCompactSupport }
  let Î¼lim := RealRMK.rieszMeasure Î›'
  have Î¼lim_le : Î¼lim univ â‰¤ ENNReal.ofReal C := by
    let o : C_c(E, â„) :=
    { toFun := 1
      hasCompactSupport' := HasCompactSupport.of_compactSpace 1 }
    have : Î¼lim univ â‰¤ ENNReal.ofReal (Î›' o) := RealRMK.rieszMeasure_le_of_eq_one Î›'
      (fun x â†¦ by simp [o]) isCompact_univ (fun x â†¦ by simp [o])
    apply this.trans
    gcongr
    apply le_of_tendsto (hÎ› o)
    filter_upwards [hf] with Î¼ hÎ¼ using by simpa [o] using hÎ¼
  let Î¼lim' : FiniteMeasure E := âŸ¨Î¼lim, âŸ¨Î¼lim_le.trans_lt (by simp)âŸ©âŸ©
  refine âŸ¨Î¼lim', ?_, ?_âŸ©
  Â· simp only [mem_setOf_eq, FiniteMeasure.mk_apply, Î¼lim', FiniteMeasure.mass]
    rw [show C = (ENNReal.ofReal â†‘C).toNNReal by simp]
    exact ENNReal.toNNReal_mono (by simp) Î¼lim_le
  change Tendsto id f (ğ“ Î¼lim')
  apply FiniteMeasure.tendsto_of_forall_integral_tendsto (fun g â†¦ ?_)
  let g' : C_c(E, â„) :=
  { toFun := g
    hasCompactSupport' := HasCompactSupport.of_compactSpace _ }
  convert hÎ› g'
  change âˆ« (x : E), g' x âˆ‚Î¼lim' = Î› g'
  simp only [FiniteMeasure.toMeasure_mk, RealRMK.integral_rieszMeasure, Î¼lim', Î¼lim]
  rfl

variable (E) in
/-- In a compact space, the set of finite measures with mass `C` is compact. -/
lemma isCompact_setOf_finiteMeasure_eq_of_compactSpace [CompactSpace E] (C : â„â‰¥0) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass = C} := by
  have : {Î¼ : FiniteMeasure E | Î¼.mass = C} = {Î¼ | Î¼.mass â‰¤ C} âˆ©  {Î¼ | Î¼.mass = C} := by grind
  rw [this]
  apply IsCompact.inter_right (isCompact_setOf_finiteMeasure_le_of_compactSpace E C)
  exact isClosed_eq (by fun_prop) (by fun_prop)

/-- In a compact space, the space of probability measures is also compact. -/
instance [CompactSpace E] : CompactSpace (ProbabilityMeasure E) := by
  constructor
  apply (ProbabilityMeasure.toFiniteMeasure_isEmbedding E).isCompact_iff.2
  simp only [image_univ, ProbabilityMeasure.range_toFiniteMeasure]
  apply isCompact_setOf_finiteMeasure_eq_of_compactSpace

/-- The set of finite measures of mass at most `C` supported on a given compact set `K` is
compact. -/
lemma isCompact_setOf_finiteMeasure_le_of_isCompact
    {E : Type*} [MeasurableSpace E] [TopologicalSpace E] [NormalSpace E] [T2Space E] [BorelSpace E]
    (C : â„â‰¥0) {K : Set E} (hK : IsCompact K) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ Î¼ Ká¶œ = 0} := by
  let f : K â†’ E := Subtype.val
  have hf : IsClosedEmbedding f := IsClosedEmbedding.subtypeVal hK.isClosed
  have rf : range f = K := Subtype.range_val
  let F : FiniteMeasure K â†’ FiniteMeasure E := fun Î¼ â†¦ Î¼.map f
  have hF : IsEmbedding F := hK.isClosed.isClosedEmbedding_subtypeVal.isEmbedding_map_finiteMeasure
  let T : Set (FiniteMeasure K) := {Î¼ | Î¼.mass â‰¤ C}
  have : {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ Î¼ Ká¶œ = 0} = F '' T := by
    apply Subset.antisymm
    Â· intro Î¼ hÎ¼
      simp only [mem_image]
      refine âŸ¨Î¼.comap f, (FiniteMeasure.mass_comap_le _ _).trans hÎ¼.1, ?_âŸ©
      ext s hs
      simp only [toMeasure_map, F]
      rw [Measure.map_apply measurable_subtype_coe hs]
      simp only [toMeasure_comap]
      rw [Measure.comap_apply _ (Subtype.val_injective), image_preimage_eq_inter_range]
      Â· rw [â† Measure.restrict_apply hs, Measure.restrict_eq_self_of_ae_mem]
        apply (null_iff_toMeasure_null (â†‘Î¼) (range f)á¶œ).mp
        rw [rf]
        exact hÎ¼.2
      Â· exact fun t ht â†¦ hf.measurableEmbedding.measurableSet_image' ht
      Â· exact hf.continuous.measurable hs
    Â· simp only [null_iff_toMeasure_null, image_subset_iff, preimage_setOf_eq, toMeasure_map,
        setOf_subset_setOf, F, T]
      intro Î¼ hÎ¼
      rw [Measure.map_apply hf.continuous.measurable hK.measurableSet.compl]
      refine âŸ¨(mass_map_le _ _).trans hÎ¼, by simp [f]âŸ©
  rw [this, â† hF.isCompact_iff]
  have : CompactSpace K := isCompact_iff_compactSpace.mp hK
  exact isCompact_setOf_finiteMeasure_le_of_compactSpace _ _

lemma partialSups_eq_accumulate
    {Î± : Type*} (f : â„• â†’ Set Î±) (n : â„•) : partialSups f n = Accumulate f n := by
  simp [partialSups_eq_sup_range, Accumulate, Nat.lt_succ_iff]

open Measure

omit [T2Space E] [BorelSpace E]
instance innerRegular_add {Î¼ Î½ : Measure E} [InnerRegular Î¼] [InnerRegular Î½] :
    InnerRegular (Î¼ + Î½) := by
  constructor
  intro s hs r hr
  simp only [Measure.coe_add, Pi.add_apply] at hr
  rcases eq_or_ne (Î¼ s) 0 with h | h
  Â· simp only [h, zero_add] at hr
    rcases MeasurableSet.exists_lt_isCompact hs hr with âŸ¨K, Ks, hK, h'KâŸ©
    exact âŸ¨K, Ks, hK, h'K.trans_le (by simp)âŸ©
  rcases eq_or_ne (Î½ s) 0 with h' | h'
  Â· simp only [h', add_zero] at hr
    rcases MeasurableSet.exists_lt_isCompact hs hr with âŸ¨K, Ks, hK, h'KâŸ©
    exact âŸ¨K, Ks, hK, h'K.trans_le (by simp)âŸ©
  rcases ENNReal.exists_lt_add_of_lt_add hr h h' with âŸ¨u, hu, v, hv, huvâŸ©
  rcases MeasurableSet.exists_lt_isCompact hs hu with âŸ¨K, Ks, hK, h'KâŸ©
  rcases MeasurableSet.exists_lt_isCompact hs hv with âŸ¨K', K's, hK', h'K'âŸ©
  refine âŸ¨K âˆª K', union_subset Ks K's, hK.union hK', huv.trans_le ?_âŸ©
  apply (add_le_add h'K.le h'K'.le).trans
  simp only [Measure.coe_add, Pi.add_apply]
  gcongr <;> simp

instance innerRegular_sum {Î¹ : Type*} {Î¼ : Î¹ â†’ Measure E} [âˆ€ i, InnerRegular (Î¼ i)] (a : Finset Î¹) :
    InnerRegular (âˆ‘ i âˆˆ a, Î¼ i) := by
  classical
  induction a using Finset.induction with
  | empty => simp only [Finset.sum_empty]; infer_instance
  | insert a s ha ih => simp only [ha, not_false_eq_true, Finset.sum_insert]; infer_instance


instance {Î¹ : Type*} {Î¼ : Î¹ â†’ Measure E} [âˆ€ i, InnerRegular (Î¼ i)] :
    InnerRegular (Measure.sum Î¼) := by
  constructor
  intro s hs r hr
  have : Tendsto (fun (a : Finset Î¹) â†¦ âˆ‘ i âˆˆ a, Î¼ i s) atTop (ğ“ (Measure.sum Î¼ s)) := by
    simp only [hs, Measure.sum_apply]
    exact ENNReal.summable.hasSum
  obtain âŸ¨a, haâŸ© : âˆƒ (a : Finset Î¹), r < (âˆ‘ i âˆˆ a, Î¼ i) s := by
    simp only [coe_finset_sum, Finset.sum_apply]
    exact ((tendsto_order.1 this).1 r hr).exists
  rcases MeasurableSet.exists_lt_isCompact hs ha with âŸ¨K, Ks, hK, h'KâŸ©
  refine âŸ¨K, Ks, hK, h'K.trans_le ?_âŸ©
  simp [hK.measurableSet, ENNReal.sum_le_tsum]

lemma prokh_aux' {E : Type*} [MeasurableSpace E]
    [TopologicalSpace E] [T2Space E] [NormalSpace E] [BorelSpace E] {u : â„• â†’ â„â‰¥0} {K : â„• â†’ Set E}
    (C : â„â‰¥0) (hu : Tendsto u atTop (ğ“ 0)) (hK : âˆ€ n, IsCompact (K n)) :
    IsCompact {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ âˆ€ n, Î¼ (K n)á¶œ â‰¤ u n} := by
  have I (Î¼ : FiniteMeasure E) (n : â„•) :
      âˆ‘ i âˆˆ Finset.range (n + 1), Î¼.restrict (disjointed K i) = Î¼.restrict (partialSups K n) := by
    induction n with
    | zero => simp
    | succ n ih =>
      simp only [Finset.sum_range_succ, ih]
      rw [â† FiniteMeasure.restrict_union]
      Â· simp only [partialSups_add_one_eq_sup_disjointed, sup_eq_union]
      Â· rw [â† Order.succ_eq_add_one, disjointed_succ _ (not_isMax n)]
        exact disjoint_sdiff_right
      Â· apply MeasurableSet.disjointed (fun i â†¦ (hK i).measurableSet)
  apply isCompact_iff_ultrafilter_le_nhds'.2 (fun f hf â†¦ ?_)
  have A n : IsCompact (partialSups K n) := by
    simpa [partialSups_eq_accumulate] using isCompact_accumulate hK _
  have M n : âˆƒ (Î½ : FiniteMeasure E), Measure.InnerRegular (Î½ : Measure E) âˆ§
      Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ Î¼.restrict (disjointed K n)) f (ğ“ Î½) := by
    obtain âŸ¨Î½, hÎ½, Î½_limâŸ© : âˆƒ Î½ âˆˆ {Î¼ : FiniteMeasure E | Î¼.mass â‰¤ C âˆ§ Î¼ (partialSups K n)á¶œ = 0},
        Tendsto (fun (Î¼ : FiniteMeasure E) â†¦ Î¼.restrict (disjointed K n)) f (ğ“ Î½) := by
      simp only [Tendsto]
      rw [â† Ultrafilter.coe_map]
      apply IsCompact.ultrafilter_le_nhds'
        (isCompact_setOf_finiteMeasure_le_of_isCompact C (A n))
      simp only [null_iff_toMeasure_null, Ultrafilter.mem_map, preimage_setOf_eq]
      filter_upwards [hf] with Î¼ hÎ¼
      simp only [restrict_mass, restrict_measure_eq,
        Measure.restrict_apply (A n).measurableSet.compl]
      refine âŸ¨(apply_le_mass Î¼ _).trans hÎ¼.1, ?_âŸ©
      convert measure_empty (Î¼ := (Î¼ : Measure E))
      apply disjoint_iff.1
      apply disjoint_compl_left.mono_right
      exact le_trans sdiff_le (le_partialSups _ _)
    obtain âŸ¨Î½', Î½'_reg, Î½'_fin, hÎ½'âŸ© : âˆƒ Î½', Î½'.InnerRegular âˆ§ IsFiniteMeasure Î½' âˆ§
        âˆ€ (g : E â†’áµ‡ â„), âˆ« x, g x âˆ‚Î½ = âˆ« x, g x âˆ‚Î½' := by
      apply Measure.exists_innerRegular_eq_of_isCompact _ (A n)
      rw [â† MeasureTheory.FiniteMeasure.null_iff_toMeasure_null]
      exact hÎ½.2
    let Î¼ : FiniteMeasure E := âŸ¨Î½', Î½'_finâŸ©
    refine âŸ¨Î¼, Î½'_reg, ?_âŸ©
    apply tendsto_of_forall_integral_tendsto (fun g â†¦ ?_)
    convert tendsto_iff_forall_integral_tendsto.1 Î½_lim g using 2
    exact (hÎ½' g).symm
  choose! Î½ Î½_reg hÎ½ using M
  have B : (Measure.sum (fun n â†¦ (Î½ n : Measure E))) univ â‰¤ C := by
    simp only [MeasurableSet.univ, Measure.sum_apply]
    have : Tendsto (fun n â†¦ âˆ‘ i âˆˆ Finset.range (n + 1), (Î½ i : Measure E) univ) atTop
        (ğ“ (âˆ‘' i, (Î½ i : Measure E) univ)) :=
      (ENNReal.tendsto_nat_tsum _).comp (tendsto_add_atTop_nat 1)
    apply le_of_tendsto' this (fun n â†¦ ?_)
    have : âˆ‘ i âˆˆ Finset.range (n + 1), (Î½ i : Measure E) univ
        = (âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i).toMeasure univ := by
      simp only [toMeasure_sum, Measure.coe_finset_sum, Finset.sum_apply]
    rw [this]
    suffices (âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i).mass â‰¤ C by
      convert ENNReal.coe_le_coe.2 this
      simp
    have : Tendsto (fun (Î¼ : FiniteMeasure E) â†¦
        (âˆ‘ i âˆˆ Finset.range (n + 1), Î¼.restrict (disjointed K i)).mass) f
        (ğ“ ((âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i).mass)) := by
      apply Tendsto.mass
      exact tendsto_finset_sum _ (fun i hi â†¦ hÎ½ i)
    apply le_of_tendsto this
    filter_upwards [hf] with Î¼ hÎ¼
    rw [I, restrict_mass]
    exact le_trans (apply_mono _ (subset_univ _)) hÎ¼.1
  let Î¼ : FiniteMeasure E := âŸ¨Measure.sum (fun n â†¦ (Î½ n : Measure E)), âŸ¨B.trans_lt (by simp)âŸ©âŸ©
  have L : Tendsto id f (ğ“ Î¼) := by
    apply tendsto_of_forall_integral_tendsto (fun g â†¦ ?_)
    rw [Metric.tendsto_nhds]
    intro Îµ Îµpos
    have : Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range n, Î½ i)) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼)) := by
      simp only [FiniteMeasure.toMeasure_mk, Î¼]
      rw [integral_sum_measure (g.integrable (Î¼ := Î¼))]
      simp_rw [integral_finset_sum_measure (fun i hi â†¦ g.integrable _)]
      apply Summable.tendsto_sum_tsum_nat
      apply (hasSum_integral_measure _).summable
      exact g.integrable (Î¼ := Î¼)
    have I1 : âˆ€á¶  n in atTop,
        dist (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i)) (âˆ« x, g x âˆ‚Î¼) < Îµ / 3 :=
      Metric.tendsto_nhds.1 (this.comp (tendsto_add_atTop_nat 1)) _ (by positivity)
    have I2 : âˆ€á¶  n in atTop, â€–gâ€– * u n < Îµ / 3 := by
      have := (NNReal.tendsto_coe.2 hu).const_mul (â€–gâ€–)
      simp only [NNReal.coe_zero, mul_zero] at this
      exact (tendsto_order.1 this).2 (Îµ / 3) (by positivity)
    rcases (I1.and I2).exists with âŸ¨n, hn, h'nâŸ©
    have : Tendsto (fun (Ï : FiniteMeasure E) â†¦
        âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i) : FiniteMeasure E)) f
        (ğ“ (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i : FiniteMeasure E))) := by
      apply tendsto_iff_forall_integral_tendsto.1 _ g
      apply tendsto_finset_sum _ (fun i hi â†¦ hÎ½ i)
    filter_upwards [Metric.tendsto_nhds.1 this (Îµ / 3) (by positivity), hf] with Ï hÏ h'Ï
    calc dist (âˆ« (x : E), g x âˆ‚Ï) (âˆ« (x : E), g x âˆ‚Î¼)
    _ â‰¤ dist (âˆ« (x : E), g x âˆ‚Ï)
          (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i)))
        + dist (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i)))
          (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i))
        + dist (âˆ« x, g x âˆ‚(âˆ‘ i âˆˆ Finset.range (n + 1), Î½ i)) (âˆ« (x : E), g x âˆ‚Î¼) :=
      dist_triangle4 _ _ _ _
    _ < Îµ / 3 + Îµ / 3 + Îµ / 3 := by
      gcongr
      Â· have : Ï = Ï.restrict (partialSups K n)á¶œ +
            âˆ‘ i âˆˆ Finset.range (n + 1), Ï.restrict (disjointed K i) := by
          rw [I, â† FiniteMeasure.restrict_union disjoint_compl_left (A n).measurableSet]
          simp
        nth_rewrite 1 [this]
        rw [toMeasure_add, integral_add_measure (g.integrable _) (g.integrable _)]
        simp only [toMeasure_sum, dist_add_self_left]
        calc â€–âˆ« x, g x âˆ‚(Ï.restrict ((partialSups K) n)á¶œ)â€–
        _ â‰¤ âˆ« x, â€–g xâ€– âˆ‚(Ï.restrict ((partialSups K) n)á¶œ) := norm_integral_le_integral_norm _
        _ â‰¤ âˆ« x, â€–gâ€– âˆ‚(Ï.restrict ((partialSups K) n)á¶œ : Measure E) := by
          apply integral_mono_of_nonneg
          Â· filter_upwards [] with x using by positivity
          Â· simp
          Â· filter_upwards [] with x using norm_coe_le_norm g x
        _ = â€–gâ€– * Ï ((partialSups K) n)á¶œ := by simp [mul_comm]
        _ â‰¤ â€–gâ€– * Ï (K n)á¶œ := by gcongr; apply le_partialSups
        _ â‰¤ â€–gâ€– * u n := by gcongr; exact h'Ï.2 n
        _ < Îµ / 3 := h'n
      Â· simpa using hÏ
    _ = Îµ := by ring
  refine âŸ¨Î¼, âŸ¨?_, fun n â†¦ ?_âŸ©, LâŸ©
  Â· simp only [mass, mk_apply, Î¼]
    rw [show C = (C : â„â‰¥0âˆ).toNNReal by simp]
    exact ENNReal.toNNReal_mono (by simp) B
  have : InnerRegular (Î¼ : Measure E) := by simp only [toMeasure_mk, Î¼]; infer_instance
