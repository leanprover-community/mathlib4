/-
Copyright (c) 2024 Jakob Stiefel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob Stiefel
-/
import Mathlib.Analysis.SpecialFunctions.MulExpNegMulSqIntegral
import Mathlib.MeasureTheory.Measure.HasOuterApproxClosed

/-!
# Extensionality of finite measures

The main Result is `ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable`:
Let `A` be a StarSubalgebra of `C(E, ğ•œ)` that separates points and whose elements are bounded. If
the integrals of all elements `A` with respect to two finite measures `P, P'`coincide, then the
measures coincide. In other words: If a Subalgebra separates points, it separates finite measures.
-/

open MeasureTheory Filter Real RCLike BoundedContinuousFunction

open scoped Topology

variable {E ğ•œ : Type*} [RCLike ğ•œ]

/--
On a star subalgebra of bounded continuous functions, the operations "restrict scalars to â„"
 and "forget that a bounded continuous function is a bounded" commute..
-/
theorem restrict_toContinuousMap_eq_toContinuousMapStar_restrict [PseudoEMetricSpace E]
    {A : StarSubalgebra ğ•œ (E â†’áµ‡ ğ•œ)} :
    ((A.restrictScalars â„).comap
    (ofRealAm.compLeftContinuousBounded â„ lipschitzWith_ofReal)).map (toContinuousMapâ‚ â„) =
    ((A.map (toContinuousMapStarâ‚ ğ•œ)).restrictScalars â„).comap
    (ofRealAm.compLeftContinuous â„ continuous_ofReal) := by
  ext g
  simp only [Subalgebra.mem_map, Subalgebra.mem_comap, Subalgebra.mem_restrictScalars,
    StarSubalgebra.mem_toSubalgebra, toContinuousMapâ‚_apply, StarSubalgebra.mem_map]
  constructor
  Â· intro âŸ¨x, hxA, hxgâŸ©
    use (@ofRealAm ğ•œ _).compLeftContinuousBounded â„ lipschitzWith_ofReal x, hxA
    ext a
    simp only [toContinuousMapStarâ‚_apply_apply, AlgHom.compLeftContinuousBounded_apply_apply,
      ofRealAm_coe, AlgHom.compLeftContinuous_apply_apply, algebraMap.coe_inj]
    exact DFunLike.congr_fun hxg a
  Â· intro âŸ¨x, hxA, hxgâŸ©
    have hg_apply (a : E) := DFunLike.congr_fun hxg a
    simp only [toContinuousMapStarâ‚_apply_apply, AlgHom.compLeftContinuous_apply_apply,
      ofRealAm_coe] at hg_apply
    have h_comp_eq : (@ofRealAm ğ•œ _).compLeftContinuousBounded â„ lipschitzWith_ofReal
        (x.comp reCLM (@reCLM ğ•œ _).lipschitz) = x := by
      ext a
      simp [hg_apply]
    use x.comp reCLM (@reCLM ğ•œ _).lipschitz
    refine âŸ¨by rwa [h_comp_eq], ?_âŸ©
    ext a
    simp [hg_apply]

namespace MeasureTheory

variable [MeasurableSpace E]

theorem ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable
    [PseudoEMetricSpace E] [BorelSpace E] [CompleteSpace E] [SecondCountableTopology E]
    {P P' : Measure E} [IsFiniteMeasure P] [IsFiniteMeasure P']
    {A : StarSubalgebra ğ•œ (E â†’áµ‡ ğ•œ)} (hA : (A.map (toContinuousMapStarâ‚ ğ•œ)).SeparatesPoints)
    (heq : âˆ€ g âˆˆ A, âˆ« x, (g : E â†’ ğ•œ) x âˆ‚P = âˆ« x, (g : E â†’ ğ•œ) x âˆ‚P') : P = P' := by
  --consider the real subalgebra of the purely real-valued elements of A
  let A_toReal := (A.restrictScalars â„).comap
    (ofRealAm.compLeftContinuousBounded â„ lipschitzWith_ofReal)
  --the real subalgebra separates points
  have hA_toReal : (A_toReal.map (toContinuousMapâ‚ â„)).SeparatesPoints := by
    rw [restrict_toContinuousMap_eq_toContinuousMapStar_restrict]
    exact Subalgebra.SeparatesPoints.rclike_to_real hA
  --integrals of elements of the real subalgebra wrt P, P', respectively, coincide
  have heq' : âˆ€ g âˆˆ A_toReal, âˆ« x, (g : E â†’ â„) x âˆ‚P = âˆ« x, (g : E â†’ â„) x âˆ‚P' := by
    intro g hgA_toReal
    rw [â† @ofReal_inj ğ•œ, â† integral_ofReal, â† integral_ofReal]
    exact heq _ hgA_toReal
  apply ext_of_forall_integral_eq_of_IsFiniteMeasure
  intro f
  have h0 : Tendsto (fun Îµ : â„ => 6 * sqrt Îµ) (ğ“[>] 0) (ğ“ 0) := by
    nth_rewrite 3 [â† mul_zero 6]
    apply tendsto_nhdsWithin_of_tendsto_nhds (Tendsto.const_mul 6 _)
    nth_rewrite 2 [â† sqrt_zero]
    exact Continuous.tendsto continuous_sqrt 0
  have lim1 : Tendsto (fun Îµ => |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P - âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P'|)
      (ğ“[>] 0) (ğ“ 0) := by
    apply squeeze_zero' (eventually_nhdsWithin_of_forall (fun x _ => abs_nonneg _))
      (eventually_nhdsWithin_of_forall _) h0
    exact fun Îµ hÎµ => dist_integral_mulExpNegMulSq_comp_le f hA_toReal heq' hÎµ
  have lim2 : Tendsto (fun Îµ => |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P
      - âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P'|) (ğ“[>] 0)
      (ğ“ |âˆ« x, f x âˆ‚â†‘P - âˆ« x, f x âˆ‚â†‘P'|) :=
    Tendsto.abs (Tendsto.sub (tendsto_integral_mulExpNegMulSq_comp f)
      (tendsto_integral_mulExpNegMulSq_comp f))
  exact eq_of_abs_sub_eq_zero (tendsto_nhds_unique lim2 lim1)

theorem ext_of_forall_mem_subalgebra_integral_eq_of_polish [TopologicalSpace E] [PolishSpace E]
    [BorelSpace E] {P P' : Measure E} [IsFiniteMeasure P] [IsFiniteMeasure P']
    {A : StarSubalgebra ğ•œ (E â†’áµ‡ ğ•œ)} (hA : (A.map (toContinuousMapStarâ‚ ğ•œ)).SeparatesPoints)
    --{A : StarSubalgebra ğ•œ C(E, ğ•œ)} (hA : A.SeparatesPoints)
    --(hbound : âˆ€ g âˆˆ A, âˆƒ C, âˆ€ x y : E, dist (g x) (g y) â‰¤ C)
    (heq : âˆ€ g âˆˆ A, âˆ« x, (g : E â†’ ğ•œ) x âˆ‚P = âˆ« x, (g : E â†’ ğ•œ) x âˆ‚P') : P = P' := by
  letI := upgradePolishSpace E
  exact ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable hA heq

end MeasureTheory
