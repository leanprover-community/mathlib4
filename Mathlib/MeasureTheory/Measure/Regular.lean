/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris Van Doorn, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic

#align_import measure_theory.measure.regular from "leanprover-community/mathlib"@"bf6a01357ff5684b1ebcd0f1a13be314fc82c0bf"

/-!
# Regular measures

A measure is `OuterRegular` if the measure of any measurable set `A` is the infimum of `Î¼ U` over
all open sets `U` containing `A`.

A measure is `Regular` if it satisfies the following properties:
* it is finite on compact sets;
* it is outer regular;
* it is inner regular for open sets with respect to compacts sets: the measure of any open set `U`
  is the supremum of `Î¼ K` over all compact sets `K` contained in `U`.

A measure is `WeaklyRegular` if it satisfies the following properties:
* it is outer regular;
* it is inner regular for open sets with respect to closed sets: the measure of any open set `U`
  is the supremum of `Î¼ F` over all closed sets `F` contained in `U`.

In a Hausdorff topological space, regularity implies weak regularity. These three conditions are
registered as typeclasses for a measure `Î¼`, and this implication is recorded as an instance.

In order to avoid code duplication, we also define a measure `Î¼` to be `InnerRegular` for sets
satisfying a predicate `q` with respect to sets satisfying a predicate `p` if for any set
`U âˆˆ {U | q U}` and a number `r < Î¼ U` there exists `F âŠ† U` such that `p F` and `r < Î¼ F`.

We prove that inner regularity for open sets with respect to compact sets or closed sets implies
inner regularity for all measurable sets of finite measure (with respect to
compact sets or closed sets respectively), and register some corollaries for (weakly) regular
measures.

Note that a similar statement for measurable sets of infinite mass can fail. For a counterexample,
consider the group `â„ Ã— â„` where the first factor has the discrete topology and the second one the
usual topology. It is a locally compact Hausdorff topological group, with Haar measure equal to
Lebesgue measure on each vertical fiber. The set `â„ Ã— {0}` has infinite measure (by outer
regularity), but any compact set it contains has zero measure (as it is finite).

Several authors require as a definition of regularity that all measurable sets are inner regular.
We have opted for the slightly weaker definition above as it holds for all Haar measures, it is
enough for essentially all applications, and it is equivalent to the other definition when the
measure is finite.

The interest of the notion of weak regularity is that it is enough for many applications, and it
is automatically satisfied by any finite measure on a metric space.

## Main definitions

* `MeasureTheory.Measure.OuterRegular Î¼`: a typeclass registering that a measure `Î¼` on a
  topological space is outer regular.
* `MeasureTheory.Measure.Regular Î¼`: a typeclass registering that a measure `Î¼` on a topological
  space is regular.
* `MeasureTheory.Measure.WeaklyRegular Î¼`: a typeclass registering that a measure `Î¼` on a
  topological space is weakly regular.
* `MeasureTheory.Measure.InnerRegular Î¼ p q`: a non-typeclass predicate saying that a measure `Î¼`
  is inner regular for sets satisfying `q` with respect to sets satisfying `p`.

## Main results

### Outer regular measures

* `Set.measure_eq_iInf_isOpen` asserts that, when `Î¼` is outer regular, the measure of a
  set is the infimum of the measure of open sets containing it.
* `Set.exists_isOpen_lt_of_lt` asserts that, when `Î¼` is outer regular, for every set `s`
  and `r > Î¼ s` there exists an open superset `U âŠ‡ s` of measure less than `r`.
* push forward of an outer regular measure is outer regular, and scalar multiplication of a regular
  measure by a finite number is outer regular.

### Weakly regular measures

* `IsOpen.measure_eq_iSup_isClosed` asserts that the measure of an open set is the supremum of
  the measure of closed sets it contains.
* `IsOpen.exists_lt_isClosed`: for an open set `U` and `r < Î¼ U`, there exists a closed `F âŠ† U`
  of measure greater than `r`;
* `MeasurableSet.measure_eq_iSup_isClosed_of_ne_top` asserts that the measure of a measurable set
  of finite measure is the supremum of the measure of closed sets it contains.
*  `MeasurableSet.exists_lt_isClosed_of_ne_top` and `MeasurableSet.exists_isClosed_lt_add`:
  a measurable set of finite measure can be approximated by a closed subset (stated as
  `r < Î¼ F` and `Î¼ s < Î¼ F + Îµ`, respectively).
* `MeasureTheory.Measure.WeaklyRegular.of_pseudoEMetricSpace_of_isFiniteMeasure` is an
  instance registering that a finite measure on a metric space is weakly regular (in fact, a pseudo
  emetric space is enough);
* `MeasureTheory.Measure.WeaklyRegular.of_pseudoEMetric_secondCountable_of_locallyFinite`
  is an instance registering that a locally finite measure on a second countable metric space (or
  even a pseudo emetric space) is weakly regular.

### Regular measures

* `IsOpen.measure_eq_iSup_isCompact` asserts that the measure of an open set is the supremum of
  the measure of compact sets it contains.
* `IsOpen.exists_lt_isCompact`: for an open set `U` and `r < Î¼ U`, there exists a compact `K âŠ† U`
  of measure greater than `r`;
* `MeasurableSet.measure_eq_iSup_isCompact_of_ne_top` asserts that the measure of a measurable set
  of finite measure is the supremum of the measure of compact sets it contains.
*  `MeasurableSet.exists_lt_isCompact_of_ne_top` and `MeasurableSet.exists_isCompact_lt_add`:
  a measurable set of finite measure can be approximated by a compact subset (stated as
  `r < Î¼ K` and `Î¼ s < Î¼ K + Îµ`, respectively).
* `MeasureTheory.Measure.Regular.of_sigmaCompactSpace_of_isLocallyFiniteMeasure` is an
  instance registering that a locally finite measure on a `Ïƒ`-compact metric space is regular (in
  fact, an emetric space is enough).

## Implementation notes

The main nontrivial statement is `MeasureTheory.Measure.InnerRegular.weaklyRegular_of_finite`,
expressing that in a finite measure space, if every open set can be approximated from inside by
closed sets, then the measure is in fact weakly regular. To prove that we show that any measurable
set can be approximated from inside by closed sets and from outside by open sets. This statement is
proved by measurable induction, starting from open sets and checking that it is stable by taking
complements (this is the point of this condition, being symmetrical between inside and outside) and
countable disjoint unions.

Once this statement is proved, one deduces results for `Ïƒ`-finite measures from this statement, by
restricting them to finite measure sets (and proving that this restriction is weakly regular, using
again the same statement).

## References

[Halmos, Measure Theory, Â§52][halmos1950measure]. Note that Halmos uses an unusual definition of
Borel sets (for him, they are elements of the `Ïƒ`-algebra generated by compact sets!), so his
proofs or statements do not apply directly.

[Billingsley, Convergence of Probability Measures][billingsley1999]
-/


open Set Filter ENNReal Topology NNReal BigOperators

namespace MeasureTheory

namespace Measure

/-- We say that a measure `Î¼` is *inner regular* with respect to predicates `p q : Set Î± â†’ Prop`,
if for every `U` such that `q U` and `r < Î¼ U`, there exists a subset `K âŠ† U` satisfying `p K`
of measure greater than `r`.

This definition is used to prove some facts about regular and weakly regular measures without
repeating the proofs. -/
def InnerRegular {Î±} {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (p q : Set Î± â†’ Prop) :=
  âˆ€ â¦ƒUâ¦„, q U â†’ âˆ€ r < Î¼ U, âˆƒ K, K âŠ† U âˆ§ p K âˆ§ r < Î¼ K
#align measure_theory.measure.inner_regular MeasureTheory.Measure.InnerRegular

namespace InnerRegular

variable {Î± : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} {p q : Set Î± â†’ Prop} {U : Set Î±}
  {Îµ : â„â‰¥0âˆ}

theorem measure_eq_iSup (H : InnerRegular Î¼ p q) (hU : q U) :
    Î¼ U = â¨† (K) (_ : K âŠ† U) (_ : p K), Î¼ K := by
  refine'
    le_antisymm (le_of_forall_lt fun r hr => _) (iSupâ‚‚_le fun K hK => iSup_le fun _ => Î¼.mono hK)
  simpa only [lt_iSup_iff, exists_prop] using H hU r hr
#align measure_theory.measure.inner_regular.measure_eq_supr MeasureTheory.Measure.InnerRegular.measure_eq_iSup

theorem exists_subset_lt_add (H : InnerRegular Î¼ p q) (h0 : p âˆ…) (hU : q U) (hÎ¼U : Î¼ U â‰  âˆ)
    (hÎµ : Îµ â‰  0) : âˆƒ K, K âŠ† U âˆ§ p K âˆ§ Î¼ U < Î¼ K + Îµ := by
  cases' eq_or_ne (Î¼ U) 0 with hâ‚€ hâ‚€
  Â· refine' âŸ¨âˆ…, empty_subset _, h0, _âŸ©
    rwa [measure_empty, hâ‚€, zero_add, pos_iff_ne_zero]
  Â· rcases H hU _ (ENNReal.sub_lt_self hÎ¼U hâ‚€ hÎµ) with âŸ¨K, hKU, hKc, hrKâŸ©
    exact âŸ¨K, hKU, hKc, ENNReal.lt_add_of_sub_lt_right (Or.inl hÎ¼U) hrKâŸ©
#align measure_theory.measure.inner_regular.exists_subset_lt_add MeasureTheory.Measure.InnerRegular.exists_subset_lt_add

theorem map {Î± Î²} [MeasurableSpace Î±] [MeasurableSpace Î²] {Î¼ : Measure Î±} {pa qa : Set Î± â†’ Prop}
    (H : InnerRegular Î¼ pa qa) (f : Î± â‰ƒ Î²) (hf : AEMeasurable f Î¼) {pb qb : Set Î² â†’ Prop}
    (hAB : âˆ€ U, qb U â†’ qa (f â»Â¹' U)) (hAB' : âˆ€ K, pa K â†’ pb (f '' K))
    (hBâ‚ : âˆ€ K, pb K â†’ MeasurableSet K) (hBâ‚‚ : âˆ€ U, qb U â†’ MeasurableSet U) :
    InnerRegular (map f Î¼) pb qb := by
  intro U hU r hr
  rw [map_apply_of_aemeasurable hf (hBâ‚‚ _ hU)] at hr
  rcases H (hAB U hU) r hr with âŸ¨K, hKU, hKc, hKâŸ©
  refine' âŸ¨f '' K, image_subset_iff.2 hKU, hAB' _ hKc, _âŸ©
  rwa [map_apply_of_aemeasurable hf (hBâ‚ _ <| hAB' _ hKc), f.preimage_image]
#align measure_theory.measure.inner_regular.map MeasureTheory.Measure.InnerRegular.map

theorem smul (H : InnerRegular Î¼ p q) (c : â„â‰¥0âˆ) : InnerRegular (c â€¢ Î¼) p q := by
  intro U hU r hr
  rw [smul_apply, H.measure_eq_iSup hU, smul_eq_mul] at hr
  simpa only [ENNReal.mul_iSup, lt_iSup_iff, exists_prop] using hr
#align measure_theory.measure.inner_regular.smul MeasureTheory.Measure.InnerRegular.smul

theorem trans {q' : Set Î± â†’ Prop} (H : InnerRegular Î¼ p q) (H' : InnerRegular Î¼ q q') :
    InnerRegular Î¼ p q' := by
  intro U hU r hr
  rcases H' hU r hr with âŸ¨F, hFU, hqF, hFâŸ©; rcases H hqF _ hF with âŸ¨K, hKF, hpK, hrKâŸ©
  exact âŸ¨K, hKF.trans hFU, hpK, hrKâŸ©
#align measure_theory.measure.inner_regular.trans MeasureTheory.Measure.InnerRegular.trans

end InnerRegular

variable {Î± Î² : Type*} [MeasurableSpace Î±] [TopologicalSpace Î±] {Î¼ : Measure Î±}

/-- A measure `Î¼` is outer regular if `Î¼(A) = inf {Î¼(U) | A âŠ† U open}` for a measurable set `A`.

This definition implies the same equality for any (not necessarily measurable) set, see
`Set.measure_eq_iInf_isOpen`. -/
class OuterRegular (Î¼ : Measure Î±) : Prop where
  protected outerRegular :
    âˆ€ â¦ƒA : Set Î±â¦„, MeasurableSet A â†’ âˆ€ r > Î¼ A, âˆƒ U, U âŠ‡ A âˆ§ IsOpen U âˆ§ Î¼ U < r
#align measure_theory.measure.outer_regular MeasureTheory.Measure.OuterRegular
#align measure_theory.measure.outer_regular.outer_regular MeasureTheory.Measure.OuterRegular.outerRegular

/-- A measure `Î¼` is regular if
  - it is finite on all compact sets;
  - it is outer regular: `Î¼(A) = inf {Î¼(U) | A âŠ† U open}` for `A` measurable;
  - it is inner regular for open sets, using compact sets:
    `Î¼(U) = sup {Î¼(K) | K âŠ† U compact}` for `U` open. -/
class Regular (Î¼ : Measure Î±) extends IsFiniteMeasureOnCompacts Î¼, OuterRegular Î¼ : Prop where
  innerRegular : InnerRegular Î¼ IsCompact IsOpen
#align measure_theory.measure.regular MeasureTheory.Measure.Regular

/-- A measure `Î¼` is weakly regular if
  - it is outer regular: `Î¼(A) = inf {Î¼(U) | A âŠ† U open}` for `A` measurable;
  - it is inner regular for open sets, using closed sets:
    `Î¼(U) = sup {Î¼(F) | F âŠ† U closed}` for `U` open. -/
class WeaklyRegular (Î¼ : Measure Î±) extends OuterRegular Î¼ : Prop where
  protected innerRegular : InnerRegular Î¼ IsClosed IsOpen
#align measure_theory.measure.weakly_regular MeasureTheory.Measure.WeaklyRegular
#align measure_theory.measure.weakly_regular.inner_regular MeasureTheory.Measure.WeaklyRegular.innerRegular

-- see Note [lower instance priority]
/-- A regular measure is weakly regular. -/
instance (priority := 100) Regular.weaklyRegular [T2Space Î±] [Regular Î¼] : WeaklyRegular Î¼ where
  innerRegular _U hU r hr :=
    let âŸ¨K, hKU, hcK, hKâŸ© := Regular.innerRegular hU r hr
    âŸ¨K, hKU, hcK.isClosed, hKâŸ©
#align measure_theory.measure.regular.weakly_regular MeasureTheory.Measure.Regular.weaklyRegular

namespace OuterRegular

instance zero : OuterRegular (0 : Measure Î±) :=
  âŸ¨fun A _ _r hr => âŸ¨univ, subset_univ A, isOpen_univ, hrâŸ©âŸ©
#align measure_theory.measure.outer_regular.zero MeasureTheory.Measure.OuterRegular.zero

/-- Given `r` larger than the measure of a set `A`, there exists an open superset of `A` with
measure less than `r`. -/
theorem _root_.Set.exists_isOpen_lt_of_lt [OuterRegular Î¼] (A : Set Î±) (r : â„â‰¥0âˆ) (hr : Î¼ A < r) :
    âˆƒ U, U âŠ‡ A âˆ§ IsOpen U âˆ§ Î¼ U < r := by
  rcases OuterRegular.outerRegular (measurableSet_toMeasurable Î¼ A) r
      (by rwa [measure_toMeasurable]) with
    âŸ¨U, hAU, hUo, hUâŸ©
  exact âŸ¨U, (subset_toMeasurable _ _).trans hAU, hUo, hUâŸ©
#align set.exists_is_open_lt_of_lt Set.exists_isOpen_lt_of_lt

/-- For an outer regular measure, the measure of a set is the infimum of the measures of open sets
containing it. -/
theorem _root_.Set.measure_eq_iInf_isOpen (A : Set Î±) (Î¼ : Measure Î±) [OuterRegular Î¼] :
    Î¼ A = â¨… (U : Set Î±) (_ : A âŠ† U) (_ : IsOpen U), Î¼ U := by
  refine' le_antisymm (le_iInfâ‚‚ fun s hs => le_iInf fun _ => Î¼.mono hs) _
  refine' le_of_forall_lt' fun r hr => _
  simpa only [iInf_lt_iff, exists_prop] using A.exists_isOpen_lt_of_lt r hr
#align set.measure_eq_infi_is_open Set.measure_eq_iInf_isOpen

theorem _root_.Set.exists_isOpen_lt_add [OuterRegular Î¼] (A : Set Î±) (hA : Î¼ A â‰  âˆ) {Îµ : â„â‰¥0âˆ}
    (hÎµ : Îµ â‰  0) : âˆƒ U, U âŠ‡ A âˆ§ IsOpen U âˆ§ Î¼ U < Î¼ A + Îµ :=
  A.exists_isOpen_lt_of_lt _ (ENNReal.lt_add_right hA hÎµ)
#align set.exists_is_open_lt_add Set.exists_isOpen_lt_add

theorem _root_.Set.exists_isOpen_le_add (A : Set Î±) (Î¼ : Measure Î±) [OuterRegular Î¼] {Îµ : â„â‰¥0âˆ}
    (hÎµ : Îµ â‰  0) : âˆƒ U, U âŠ‡ A âˆ§ IsOpen U âˆ§ Î¼ U â‰¤ Î¼ A + Îµ := by
  rcases eq_or_ne (Î¼ A) âˆ with (H | H)
  Â· exact âŸ¨univ, subset_univ _, isOpen_univ, by simp only [H, _root_.top_add, le_top]âŸ©
  Â· rcases A.exists_isOpen_lt_add H hÎµ with âŸ¨U, AU, U_open, hUâŸ©
    exact âŸ¨U, AU, U_open, hU.leâŸ©
#align set.exists_is_open_le_add Set.exists_isOpen_le_add

theorem _root_.MeasurableSet.exists_isOpen_diff_lt [OuterRegular Î¼] {A : Set Î±}
    (hA : MeasurableSet A) (hA' : Î¼ A â‰  âˆ) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ U, U âŠ‡ A âˆ§ IsOpen U âˆ§ Î¼ U < âˆ âˆ§ Î¼ (U \ A) < Îµ := by
  rcases A.exists_isOpen_lt_add hA' hÎµ with âŸ¨U, hAU, hUo, hUâŸ©
  use U, hAU, hUo, hU.trans_le le_top
  exact measure_diff_lt_of_lt_add hA hAU hA' hU
#align measurable_set.exists_is_open_diff_lt MeasurableSet.exists_isOpen_diff_lt

protected theorem map [OpensMeasurableSpace Î±] [MeasurableSpace Î²] [TopologicalSpace Î²]
    [BorelSpace Î²] (f : Î± â‰ƒâ‚œ Î²) (Î¼ : Measure Î±) [OuterRegular Î¼] :
    (Measure.map f Î¼).OuterRegular := by
  refine' âŸ¨fun A hA r hr => _âŸ©
  rw [map_apply f.measurable hA, â† f.image_symm] at hr
  rcases Set.exists_isOpen_lt_of_lt _ r hr with âŸ¨U, hAU, hUo, hUâŸ©
  have : IsOpen (f.symm â»Â¹' U) := hUo.preimage f.symm.continuous
  refine' âŸ¨f.symm â»Â¹' U, image_subset_iff.1 hAU, this, _âŸ©
  rwa [map_apply f.measurable this.measurableSet, f.preimage_symm, f.preimage_image]
#align measure_theory.measure.outer_regular.map MeasureTheory.Measure.OuterRegular.map

protected theorem smul (Î¼ : Measure Î±) [OuterRegular Î¼] {x : â„â‰¥0âˆ} (hx : x â‰  âˆ) :
    (x â€¢ Î¼).OuterRegular := by
  rcases eq_or_ne x 0 with (rfl | h0)
  Â· rw [zero_smul]
    exact OuterRegular.zero
  Â· refine' âŸ¨fun A _ r hr => _âŸ©
    rw [smul_apply, A.measure_eq_iInf_isOpen, smul_eq_mul] at hr
    simpa only [ENNReal.mul_iInf_of_ne h0 hx, gt_iff_lt, iInf_lt_iff, exists_prop] using hr
#align measure_theory.measure.outer_regular.smul MeasureTheory.Measure.OuterRegular.smul

end OuterRegular

/-- If a measure `Î¼` admits finite spanning open sets such that the restriction of `Î¼` to each set
is outer regular, then the original measure is outer regular as well. -/
protected theorem FiniteSpanningSetsIn.outerRegular [OpensMeasurableSpace Î±] {Î¼ : Measure Î±}
    (s : Î¼.FiniteSpanningSetsIn { U | IsOpen U âˆ§ OuterRegular (Î¼.restrict U) }) :
    OuterRegular Î¼ := by
  refine' âŸ¨fun A hA r hr => _âŸ©
  have hm : âˆ€ n, MeasurableSet (s.set n) := fun n => (s.set_mem n).1.measurableSet
  haveI : âˆ€ n, OuterRegular (Î¼.restrict (s.set n)) := fun n => (s.set_mem n).2
  -- Note that `A = â‹ƒ n, A âˆ© disjointed s n`. We replace `A` with this sequence.
  obtain âŸ¨A, hAm, hAs, hAd, rflâŸ© :
    âˆƒ A' : â„• â†’ Set Î±,
      (âˆ€ n, MeasurableSet (A' n)) âˆ§
        (âˆ€ n, A' n âŠ† s.set n) âˆ§ Pairwise (Disjoint on A') âˆ§ A = â‹ƒ n, A' n := by
    refine'
      âŸ¨fun n => A âˆ© disjointed s.set n, fun n => hA.inter (MeasurableSet.disjointed hm _), fun n =>
        (inter_subset_right _ _).trans (disjointed_subset _ _),
        (disjoint_disjointed s.set).mono fun k l hkl => hkl.mono inf_le_right inf_le_right, _âŸ©
    rw [â† inter_iUnion, iUnion_disjointed, s.spanning, inter_univ]
  rcases ENNReal.exists_pos_sum_of_countable' (tsub_pos_iff_lt.2 hr).ne' â„• with âŸ¨Î´, Î´0, hÎ´ÎµâŸ©
  rw [lt_tsub_iff_right, add_comm] at hÎ´Îµ
  have : âˆ€ n, âˆƒ (U : _) (_ : U âŠ‡ A n), IsOpen U âˆ§ Î¼ U < Î¼ (A n) + Î´ n := by
    intro n
    have Hâ‚ : âˆ€ t, Î¼.restrict (s.set n) t = Î¼ (t âˆ© s.set n) := fun t => restrict_apply' (hm n)
    have Ht : Î¼.restrict (s.set n) (A n) â‰  âŠ¤ := by
      rw [Hâ‚]
      exact ((measure_mono <| inter_subset_right _ _).trans_lt (s.finite n)).ne
    rcases (A n).exists_isOpen_lt_add Ht (Î´0 n).ne' with âŸ¨U, hAU, hUo, hUâŸ©
    rw [Hâ‚, Hâ‚, inter_eq_self_of_subset_left (hAs _)] at hU
    exact âŸ¨U âˆ© s.set n, subset_inter hAU (hAs _), hUo.inter (s.set_mem n).1, hUâŸ©
  choose U hAU hUo hU using this
  refine' âŸ¨â‹ƒ n, U n, iUnion_mono hAU, isOpen_iUnion hUo, _âŸ©
  calc
    Î¼ (â‹ƒ n, U n) â‰¤ âˆ‘' n, Î¼ (U n) := measure_iUnion_le _
    _ â‰¤ âˆ‘' n, (Î¼ (A n) + Î´ n) := (ENNReal.tsum_le_tsum fun n => (hU n).le)
    _ = âˆ‘' n, Î¼ (A n) + âˆ‘' n, Î´ n := ENNReal.tsum_add
    _ = Î¼ (â‹ƒ n, A n) + âˆ‘' n, Î´ n := (congr_argâ‚‚ (Â· + Â·) (measure_iUnion hAd hAm).symm rfl)
    _ < r := hÎ´Îµ
#align measure_theory.measure.finite_spanning_sets_in.outer_regular MeasureTheory.Measure.FiniteSpanningSetsIn.outerRegular

namespace InnerRegular

variable {p q : Set Î± â†’ Prop} {U s : Set Î±} {Îµ r : â„â‰¥0âˆ}

/-- If a measure is inner regular (using closed or compact sets), then every measurable set of
finite measure can be approximated by a (closed or compact) subset. -/
theorem measurableSet_of_open [OuterRegular Î¼] (H : InnerRegular Î¼ p IsOpen) (h0 : p âˆ…)
    (hd : âˆ€ â¦ƒs Uâ¦„, p s â†’ IsOpen U â†’ p (s \ U)) :
    InnerRegular Î¼ p fun s => MeasurableSet s âˆ§ Î¼ s â‰  âˆ := by
  rintro s âŸ¨hs, hÎ¼sâŸ© r hr
  obtain âŸ¨Îµ, hÎµ, hÎµs, rflâŸ© : âˆƒ (Îµ : _) (_ : Îµ â‰  0), Îµ + Îµ â‰¤ Î¼ s âˆ§ r = Î¼ s - (Îµ + Îµ) := by
    use (Î¼ s - r) / 2
    simp [*, hr.le, ENNReal.add_halves, ENNReal.sub_sub_cancel, le_add_right]
  rcases hs.exists_isOpen_diff_lt hÎ¼s hÎµ with âŸ¨U, hsU, hUo, hUt, hÎ¼UâŸ©
  rcases (U \ s).exists_isOpen_lt_of_lt _ hÎ¼U with âŸ¨U', hsU', hU'o, hÎ¼U'âŸ©
  replace hsU' := diff_subset_comm.1 hsU'
  rcases H.exists_subset_lt_add h0 hUo hUt.ne hÎµ with âŸ¨K, hKU, hKc, hKrâŸ©
  refine' âŸ¨K \ U', fun x hx => hsU' âŸ¨hKU hx.1, hx.2âŸ©, hd hKc hU'o, ENNReal.sub_lt_of_lt_add hÎµs _âŸ©
  calc
    Î¼ s â‰¤ Î¼ U := Î¼.mono hsU
    _ < Î¼ K + Îµ := hKr
    _ â‰¤ Î¼ (K \ U') + Î¼ U' + Îµ := (add_le_add_right (tsub_le_iff_right.1 le_measure_diff) _)
    _ â‰¤ Î¼ (K \ U') + Îµ + Îµ := by
      apply add_le_add_right; apply add_le_add_left
      exact hÎ¼U'.le
    _ = Î¼ (K \ U') + (Îµ + Îµ) := add_assoc _ _ _
#align measure_theory.measure.inner_regular.measurable_set_of_open MeasureTheory.Measure.InnerRegular.measurableSet_of_open

open Finset

/-- In a finite measure space, assume that any open set can be approximated from inside by closed
sets. Then the measure is weakly regular. -/
theorem weaklyRegular_of_finite [BorelSpace Î±] (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (H : InnerRegular Î¼ IsClosed IsOpen) : WeaklyRegular Î¼ := by
  have hfin : âˆ€ {s}, Î¼ s â‰  âŠ¤ := @(measure_ne_top Î¼)
  suffices âˆ€ s, MeasurableSet s â†’ âˆ€ Îµ, Îµ â‰  0 â†’ âˆƒ F, F âŠ† s âˆ§ âˆƒ U, U âŠ‡ s âˆ§
      IsClosed F âˆ§ IsOpen U âˆ§ Î¼ s â‰¤ Î¼ F + Îµ âˆ§ Î¼ U â‰¤ Î¼ s + Îµ by
    refine'
      { outerRegular := fun s hs r hr => _
        innerRegular := H }
    rcases exists_between hr with âŸ¨r', hsr', hr'râŸ©
    rcases this s hs _ (tsub_pos_iff_lt.2 hsr').ne' with âŸ¨-, -, U, hsU, -, hUo, -, HâŸ©
    refine' âŸ¨U, hsU, hUo, _âŸ©
    rw [add_tsub_cancel_of_le hsr'.le] at H
    exact H.trans_lt hr'r
  refine' MeasurableSet.induction_on_open _ _ _
  /- The proof is by measurable induction: we should check that the property is true for the empty
    set, for open sets, and is stable by taking the complement and by taking countable disjoint
    unions. The point of the property we are proving is that it is stable by taking complements
    (exchanging the roles of closed and open sets and thanks to the finiteness of the measure). -/
  -- check for open set
  Â· intro U hU Îµ hÎµ
    rcases H.exists_subset_lt_add isClosed_empty hU hfin hÎµ with âŸ¨F, hsF, hFc, hFâŸ©
    exact âŸ¨F, hsF, U, Subset.rfl, hFc, hU, hF.le, le_self_addâŸ©
  -- check for complements
  Â· rintro s hs H Îµ hÎµ
    rcases H Îµ hÎµ with âŸ¨F, hFs, U, hsU, hFc, hUo, hF, hUâŸ©
    refine'
      âŸ¨Uá¶œ, compl_subset_compl.2 hsU, Fá¶œ, compl_subset_compl.2 hFs, hUo.isClosed_compl,
        hFc.isOpen_compl, _âŸ©
    simp only [measure_compl_le_add_iff, *, hUo.measurableSet, hFc.measurableSet, true_and_iff]
  -- check for disjoint unions
  Â· intro s hsd hsm H Îµ Îµ0
    have Îµ0' : Îµ / 2 â‰  0 := (ENNReal.half_pos Îµ0).ne'
    rcases ENNReal.exists_pos_sum_of_countable' Îµ0' â„• with âŸ¨Î´, Î´0, hÎ´ÎµâŸ©
    choose F hFs U hsU hFc hUo hF hU using fun n => H n (Î´ n) (Î´0 n).ne'
    -- the approximating closed set is constructed by considering finitely many sets `s i`, which
    -- cover all the measure up to `Îµ/2`, approximating each of these by a closed set `F i`, and
    -- taking the union of these (finitely many) `F i`.
    have : Tendsto (fun t => (âˆ‘ k in t, Î¼ (s k)) + Îµ / 2) atTop (ğ“ <| Î¼ (â‹ƒ n, s n) + Îµ / 2) := by
      rw [measure_iUnion hsd hsm]
      exact Tendsto.add ENNReal.summable.hasSum tendsto_const_nhds
    rcases (this.eventually <| lt_mem_nhds <| ENNReal.lt_add_right hfin Îµ0').exists with âŸ¨t, htâŸ©
    -- the approximating open set is constructed by taking for each `s n` an approximating open set
    -- `U n` with measure at most `Î¼ (s n) + Î´ n` for a summable `Î´`, and taking the union of these.
    refine'
      âŸ¨â‹ƒ k âˆˆ t, F k, iUnion_mono fun k => iUnion_subset fun _ => hFs _, â‹ƒ n, U n, iUnion_mono hsU,
        isClosed_biUnion_finset fun k _ => hFc k, isOpen_iUnion hUo, ht.le.trans _, _âŸ©
    Â· calc
        (âˆ‘ k in t, Î¼ (s k)) + Îµ / 2 â‰¤ ((âˆ‘ k in t, Î¼ (F k)) + âˆ‘ k in t, Î´ k) + Îµ / 2 := by
          rw [â† sum_add_distrib]
          exact add_le_add_right (sum_le_sum fun k _ => hF k) _
        _ â‰¤ (âˆ‘ k in t, Î¼ (F k)) + Îµ / 2 + Îµ / 2 :=
          (add_le_add_right (add_le_add_left ((ENNReal.sum_le_tsum _).trans hÎ´Îµ.le) _) _)
        _ = Î¼ (â‹ƒ k âˆˆ t, F k) + Îµ := by
          rw [measure_biUnion_finset, add_assoc, ENNReal.add_halves]
          exacts [fun k _ n _ hkn => (hsd hkn).mono (hFs k) (hFs n),
            fun k _ => (hFc k).measurableSet]
    Â· calc
        Î¼ (â‹ƒ n, U n) â‰¤ âˆ‘' n, Î¼ (U n) := measure_iUnion_le _
        _ â‰¤ âˆ‘' n, (Î¼ (s n) + Î´ n) := (ENNReal.tsum_le_tsum hU)
        _ = Î¼ (â‹ƒ n, s n) + âˆ‘' n, Î´ n := by rw [measure_iUnion hsd hsm, ENNReal.tsum_add]
        _ â‰¤ Î¼ (â‹ƒ n, s n) + Îµ := add_le_add_left (hÎ´Îµ.le.trans ENNReal.half_le_self) _
#align measure_theory.measure.inner_regular.weakly_regular_of_finite MeasureTheory.Measure.InnerRegular.weaklyRegular_of_finite

/-- In a metric space (or even a pseudo emetric space), an open set can be approximated from inside
by closed sets. -/
theorem of_pseudoEMetricSpace {X : Type*} [PseudoEMetricSpace X] [MeasurableSpace X]
    (Î¼ : Measure X) : InnerRegular Î¼ IsClosed IsOpen := by
  intro U hU r hr
  rcases hU.exists_iUnion_isClosed with âŸ¨F, F_closed, -, rfl, F_monoâŸ©
  rw [measure_iUnion_eq_iSup F_mono.directed_le] at hr
  rcases lt_iSup_iff.1 hr with âŸ¨n, hnâŸ©
  exact âŸ¨F n, subset_iUnion _ _, F_closed n, hnâŸ©
#align measure_theory.measure.inner_regular.of_pseudo_emetric_space MeasureTheory.Measure.InnerRegular.of_pseudoEMetricSpace

/-- In a `Ïƒ`-compact space, any closed set can be approximated by a compact subset. -/
theorem isCompact_isClosed {X : Type*} [TopologicalSpace X] [SigmaCompactSpace X]
    [MeasurableSpace X] (Î¼ : Measure X) : InnerRegular Î¼ IsCompact IsClosed := by
  intro F hF r hr
  set B : â„• â†’ Set X := compactCovering X
  have hBc : âˆ€ n, IsCompact (F âˆ© B n) := fun n => (isCompact_compactCovering X n).inter_left hF
  have hBU : â‹ƒ n, F âˆ© B n = F := by rw [â† inter_iUnion, iUnion_compactCovering, Set.inter_univ]
  have : Î¼ F = â¨† n, Î¼ (F âˆ© B n) := by
    rw [â† measure_iUnion_eq_iSup, hBU]
    exact Monotone.directed_le fun m n h => inter_subset_inter_right _ (compactCovering_subset _ h)
  rw [this] at hr
  rcases lt_iSup_iff.1 hr with âŸ¨n, hnâŸ©
  exact âŸ¨_, inter_subset_left _ _, hBc n, hnâŸ©
#align measure_theory.measure.inner_regular.is_compact_is_closed MeasureTheory.Measure.InnerRegular.isCompact_isClosed

end InnerRegular

namespace Regular

instance zero : Regular (0 : Measure Î±) :=
  âŸ¨fun _ _ _r hr => âŸ¨âˆ…, empty_subset _, isCompact_empty, hrâŸ©âŸ©
#align measure_theory.measure.regular.zero MeasureTheory.Measure.Regular.zero

/-- If `Î¼` is a regular measure, then any open set can be approximated by a compact subset. -/
theorem _root_.IsOpen.exists_lt_isCompact [Regular Î¼] â¦ƒU : Set Î±â¦„ (hU : IsOpen U) {r : â„â‰¥0âˆ}
    (hr : r < Î¼ U) : âˆƒ K, K âŠ† U âˆ§ IsCompact K âˆ§ r < Î¼ K :=
  Regular.innerRegular hU r hr
#align is_open.exists_lt_is_compact IsOpen.exists_lt_isCompact

/-- The measure of an open set is the supremum of the measures of compact sets it contains. -/
theorem _root_.IsOpen.measure_eq_iSup_isCompact â¦ƒU : Set Î±â¦„ (hU : IsOpen U) (Î¼ : Measure Î±)
    [Regular Î¼] : Î¼ U = â¨† (K : Set Î±) (_ : K âŠ† U) (_ : IsCompact K), Î¼ K :=
  Regular.innerRegular.measure_eq_iSup hU
#align is_open.measure_eq_supr_is_compact IsOpen.measure_eq_iSup_isCompact

theorem exists_compact_not_null [Regular Î¼] : (âˆƒ K, IsCompact K âˆ§ Î¼ K â‰  0) â†” Î¼ â‰  0 := by
  simp_rw [Ne.def, â† measure_univ_eq_zero, isOpen_univ.measure_eq_iSup_isCompact,
    ENNReal.iSup_eq_zero, not_forall, exists_prop, subset_univ, true_and_iff]
#align measure_theory.measure.regular.exists_compact_not_null MeasureTheory.Measure.Regular.exists_compact_not_null

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `MeasurableSet.exists_isCompact_lt_add` and
`MeasurableSet.exists_lt_isCompact_of_ne_top`. -/
theorem innerRegular_measurable [Regular Î¼] :
    InnerRegular Î¼ IsCompact fun s => MeasurableSet s âˆ§ Î¼ s â‰  âˆ :=
  Regular.innerRegular.measurableSet_of_open isCompact_empty fun _ _ => IsCompact.diff
#align measure_theory.measure.regular.inner_regular_measurable MeasureTheory.Measure.Regular.innerRegular_measurable

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `MeasurableSet.exists_lt_isCompact_of_ne_top`. -/
theorem _root_.MeasurableSet.exists_isCompact_lt_add [Regular Î¼] â¦ƒA : Set Î±â¦„ (hA : MeasurableSet A)
    (h'A : Î¼ A â‰  âˆ) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) : âˆƒ K, K âŠ† A âˆ§ IsCompact K âˆ§ Î¼ A < Î¼ K + Îµ :=
  Regular.innerRegular_measurable.exists_subset_lt_add isCompact_empty âŸ¨hA, h'AâŸ© h'A hÎµ
#align measurable_set.exists_is_compact_lt_add MeasurableSet.exists_isCompact_lt_add

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `MeasurableSet.exists_isCompact_lt_add` and
`MeasurableSet.exists_lt_isCompact_of_ne_top`. -/
theorem _root_.MeasurableSet.exists_isCompact_diff_lt [OpensMeasurableSpace Î±] [T2Space Î±]
    [Regular Î¼] â¦ƒA : Set Î±â¦„ (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ K, K âŠ† A âˆ§ IsCompact K âˆ§ Î¼ (A \ K) < Îµ := by
  rcases hA.exists_isCompact_lt_add h'A hÎµ with âŸ¨K, hKA, hKc, hKâŸ©
  exact
    âŸ¨K, hKA, hKc,
      measure_diff_lt_of_lt_add hKc.measurableSet hKA (ne_top_of_le_ne_top h'A <| measure_mono hKA)
        hKâŸ©
#align measurable_set.exists_is_compact_diff_lt MeasurableSet.exists_isCompact_diff_lt

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `MeasurableSet.exists_isCompact_lt_add`. -/
theorem _root_.MeasurableSet.exists_lt_isCompact_of_ne_top [Regular Î¼] â¦ƒA : Set Î±â¦„
    (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) {r : â„â‰¥0âˆ} (hr : r < Î¼ A) :
    âˆƒ K, K âŠ† A âˆ§ IsCompact K âˆ§ r < Î¼ K :=
  Regular.innerRegular_measurable âŸ¨hA, h'AâŸ© _ hr
#align measurable_set.exists_lt_is_compact_of_ne_top MeasurableSet.exists_lt_isCompact_of_ne_top

/-- Given a regular measure, any measurable set of finite mass can be approximated from
inside by compact sets. -/
theorem _root_.MeasurableSet.measure_eq_iSup_isCompact_of_ne_top [Regular Î¼] â¦ƒA : Set Î±â¦„
    (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) : Î¼ A = â¨† (K) (_ : K âŠ† A) (_ : IsCompact K), Î¼ K :=
  Regular.innerRegular_measurable.measure_eq_iSup âŸ¨hA, h'AâŸ©
#align measurable_set.measure_eq_supr_is_compact_of_ne_top MeasurableSet.measure_eq_iSup_isCompact_of_ne_top

protected theorem map [OpensMeasurableSpace Î±] [MeasurableSpace Î²] [TopologicalSpace Î²] [T2Space Î²]
    [BorelSpace Î²] [Regular Î¼] (f : Î± â‰ƒâ‚œ Î²) : (Measure.map f Î¼).Regular := by
  haveI := OuterRegular.map f Î¼
  haveI := IsFiniteMeasureOnCompacts.map Î¼ f
  exact
    âŸ¨Regular.innerRegular.map f.toEquiv f.measurable.aemeasurable
        (fun U hU => hU.preimage f.continuous) (fun K hK => hK.image f.continuous)
        (fun K hK => hK.measurableSet) fun U hU => hU.measurableSetâŸ©
#align measure_theory.measure.regular.map MeasureTheory.Measure.Regular.map

protected theorem smul [Regular Î¼] {x : â„â‰¥0âˆ} (hx : x â‰  âˆ) : (x â€¢ Î¼).Regular := by
  haveI := OuterRegular.smul Î¼ hx
  haveI := IsFiniteMeasureOnCompacts.smul Î¼ hx
  exact âŸ¨Regular.innerRegular.smul xâŸ©
#align measure_theory.measure.regular.smul MeasureTheory.Measure.Regular.smul

-- Generalized and moved to another file
#align measure_theory.measure.regular.sigma_finite MeasureTheory.SigmaFinite.of_isFiniteMeasureOnCompacts

end Regular

namespace WeaklyRegular

/-- If `Î¼` is a weakly regular measure, then any open set can be approximated by a closed subset. -/
theorem _root_.IsOpen.exists_lt_isClosed [WeaklyRegular Î¼] â¦ƒU : Set Î±â¦„ (hU : IsOpen U) {r : â„â‰¥0âˆ}
    (hr : r < Î¼ U) : âˆƒ F, F âŠ† U âˆ§ IsClosed F âˆ§ r < Î¼ F :=
  WeaklyRegular.innerRegular hU r hr
#align is_open.exists_lt_is_closed IsOpen.exists_lt_isClosed

/-- If `Î¼` is a weakly regular measure, then any open set can be approximated by a closed subset. -/
theorem _root_.IsOpen.measure_eq_iSup_isClosed â¦ƒU : Set Î±â¦„ (hU : IsOpen U) (Î¼ : Measure Î±)
    [WeaklyRegular Î¼] : Î¼ U = â¨† (F) (_ : F âŠ† U) (_ : IsClosed F), Î¼ F :=
  WeaklyRegular.innerRegular.measure_eq_iSup hU
#align is_open.measure_eq_supr_is_closed IsOpen.measure_eq_iSup_isClosed

theorem innerRegular_measurable [WeaklyRegular Î¼] :
    InnerRegular Î¼ IsClosed fun s => MeasurableSet s âˆ§ Î¼ s â‰  âˆ :=
  WeaklyRegular.innerRegular.measurableSet_of_open isClosed_empty fun _ _ hâ‚ hâ‚‚ =>
    hâ‚.inter hâ‚‚.isClosed_compl
#align measure_theory.measure.weakly_regular.inner_regular_measurable MeasureTheory.Measure.WeaklyRegular.innerRegular_measurable

/-- If `s` is a measurable set, a weakly regular measure `Î¼` is finite on `s`, and `Îµ` is a positive
number, then there exist a closed set `K âŠ† s` such that `Î¼ s < Î¼ K + Îµ`. -/
theorem _root_.MeasurableSet.exists_isClosed_lt_add [WeaklyRegular Î¼] {s : Set Î±}
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ K, K âŠ† s âˆ§ IsClosed K âˆ§ Î¼ s < Î¼ K + Îµ :=
  innerRegular_measurable.exists_subset_lt_add isClosed_empty âŸ¨hs, hÎ¼sâŸ© hÎ¼s hÎµ
#align measurable_set.exists_is_closed_lt_add MeasurableSet.exists_isClosed_lt_add

theorem _root_.MeasurableSet.exists_isClosed_diff_lt [OpensMeasurableSpace Î±] [WeaklyRegular Î¼]
    â¦ƒA : Set Î±â¦„ (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ F, F âŠ† A âˆ§ IsClosed F âˆ§ Î¼ (A \ F) < Îµ := by
  rcases hA.exists_isClosed_lt_add h'A hÎµ with âŸ¨F, hFA, hFc, hFâŸ©
  exact
    âŸ¨F, hFA, hFc,
      measure_diff_lt_of_lt_add hFc.measurableSet hFA (ne_top_of_le_ne_top h'A <| measure_mono hFA)
        hFâŸ©
#align measurable_set.exists_is_closed_diff_lt MeasurableSet.exists_isClosed_diff_lt

/-- Given a weakly regular measure, any measurable set of finite mass can be approximated from
inside by closed sets. -/
theorem _root_.MeasurableSet.exists_lt_isClosed_of_ne_top [WeaklyRegular Î¼] â¦ƒA : Set Î±â¦„
    (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) {r : â„â‰¥0âˆ} (hr : r < Î¼ A) :
    âˆƒ K, K âŠ† A âˆ§ IsClosed K âˆ§ r < Î¼ K :=
  innerRegular_measurable âŸ¨hA, h'AâŸ© _ hr
#align measurable_set.exists_lt_is_closed_of_ne_top MeasurableSet.exists_lt_isClosed_of_ne_top

/-- Given a weakly regular measure, any measurable set of finite mass can be approximated from
inside by closed sets. -/
theorem _root_.MeasurableSet.measure_eq_iSup_isClosed_of_ne_top [WeaklyRegular Î¼] â¦ƒA : Set Î±â¦„
    (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) : Î¼ A = â¨† (K) (_ : K âŠ† A) (_ : IsClosed K), Î¼ K :=
  innerRegular_measurable.measure_eq_iSup âŸ¨hA, h'AâŸ©
#align measurable_set.measure_eq_supr_is_closed_of_ne_top MeasurableSet.measure_eq_iSup_isClosed_of_ne_top

/-- The restriction of a weakly regular measure to a measurable set of finite measure is
weakly regular. -/
theorem restrict_of_measurableSet [BorelSpace Î±] [WeaklyRegular Î¼] (A : Set Î±)
    (hA : MeasurableSet A) (h'A : Î¼ A â‰  âˆ) : WeaklyRegular (Î¼.restrict A) := by
  haveI : Fact (Î¼ A < âˆ) := âŸ¨h'A.lt_topâŸ©
  refine' InnerRegular.weaklyRegular_of_finite (Î¼.restrict A) fun V V_open => _
  simp only [restrict_apply' hA]
  intro r hr
  have : Î¼ (V âˆ© A) â‰  âˆ := ne_top_of_le_ne_top h'A (measure_mono <| inter_subset_right _ _)
  rcases (V_open.measurableSet.inter hA).exists_lt_isClosed_of_ne_top this hr with
    âŸ¨F, hFVA, hFc, hFâŸ©
  refine' âŸ¨F, hFVA.trans (inter_subset_left _ _), hFc, _âŸ©
  rwa [inter_eq_self_of_subset_left (hFVA.trans <| inter_subset_right _ _)]
#align measure_theory.measure.weakly_regular.restrict_of_measurable_set MeasureTheory.Measure.WeaklyRegular.restrict_of_measurableSet

-- see Note [lower instance priority]
/-- Any finite measure on a metric space (or even a pseudo emetric space) is weakly regular. -/
instance (priority := 100) of_pseudoEMetricSpace_of_isFiniteMeasure {X : Type*}
    [PseudoEMetricSpace X] [MeasurableSpace X] [BorelSpace X] (Î¼ : Measure X) [IsFiniteMeasure Î¼] :
    WeaklyRegular Î¼ :=
  (InnerRegular.of_pseudoEMetricSpace Î¼).weaklyRegular_of_finite Î¼
#align measure_theory.measure.weakly_regular.of_pseudo_emetric_space_of_is_finite_measure MeasureTheory.Measure.WeaklyRegular.of_pseudoEMetricSpace_of_isFiniteMeasure

-- see Note [lower instance priority]
/-- Any locally finite measure on a second countable metric space (or even a pseudo emetric space)
is weakly regular. -/
instance (priority := 100) of_pseudoEMetric_secondCountable_of_locallyFinite {X : Type*}
    [PseudoEMetricSpace X] [TopologicalSpace.SecondCountableTopology X] [MeasurableSpace X]
    [BorelSpace X] (Î¼ : Measure X) [IsLocallyFiniteMeasure Î¼] : WeaklyRegular Î¼ :=
  haveI : OuterRegular Î¼ := by
    refine' (Î¼.finiteSpanningSetsInOpen'.mono' fun U hU => _).outerRegular
    have : Fact (Î¼ U < âˆ) := âŸ¨hU.2âŸ©
    exact âŸ¨hU.1, inferInstanceâŸ©
  âŸ¨InnerRegular.of_pseudoEMetricSpace Î¼âŸ©
#align measure_theory.measure.weakly_regular.of_pseudo_emetric_second_countable_of_locally_finite MeasureTheory.Measure.WeaklyRegular.of_pseudoEMetric_secondCountable_of_locallyFinite

end WeaklyRegular

attribute [local instance] EMetric.secondCountable_of_sigmaCompact

-- see Note [lower instance priority]
/-- Any locally finite measure on a `Ïƒ`-compact (e)metric space is regular. -/
instance (priority := 100) Regular.of_sigmaCompactSpace_of_isLocallyFiniteMeasure {X : Type*}
    [EMetricSpace X] [SigmaCompactSpace X] [MeasurableSpace X] [BorelSpace X] (Î¼ : Measure X)
    [IsLocallyFiniteMeasure Î¼] : Regular Î¼ where
  lt_top_of_isCompact _K hK := hK.measure_lt_top
  innerRegular := (InnerRegular.isCompact_isClosed Î¼).trans (InnerRegular.of_pseudoEMetricSpace Î¼)
#align measure_theory.measure.regular.of_sigma_compact_space_of_is_locally_finite_measure MeasureTheory.Measure.Regular.of_sigmaCompactSpace_of_isLocallyFiniteMeasure

end Measure

end MeasureTheory
