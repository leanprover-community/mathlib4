/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Measure.MeasureSpaceDef

#align_import measure_theory.measure.ae_disjoint from "leanprover-community/mathlib"@"bc7d81beddb3d6c66f71449c5bc76c38cb77cf9e"

/-!
# Almost everywhere disjoint sets

We say that sets `s` and `t` are `Œº`-a.e. disjoint (see `MeasureTheory.AEDisjoint`) if their
intersection has measure zero. This assumption can be used instead of `Disjoint` in most theorems in
measure theory.
-/


open Set Function

namespace MeasureTheory

variable {Œπ Œ± : Type*} {m : MeasurableSpace Œ±} (Œº : Measure Œ±)

/-- Two sets are said to be `Œº`-a.e. disjoint if their intersection has measure zero. -/
def AEDisjoint (s t : Set Œ±) :=
  Œº (s ‚à© t) = 0
#align measure_theory.ae_disjoint MeasureTheory.AEDisjoint

variable {Œº} {s t u v : Set Œ±}

/-- If `s : Œπ ‚Üí Set Œ±` is a countable family of pairwise a.e. disjoint sets, then there exists a
family of measurable null sets `t i` such that `s i \ t i` are pairwise disjoint. -/
theorem exists_null_pairwise_disjoint_diff [Countable Œπ] {s : Œπ ‚Üí Set Œ±}
    (hd : Pairwise (AEDisjoint Œº on s)) : ‚àÉ t : Œπ ‚Üí Set Œ±, (‚àÄ i, MeasurableSet (t i)) ‚àß
    (‚àÄ i, Œº (t i) = 0) ‚àß Pairwise (Disjoint on fun i => s i \ t i) := by
  refine' ‚ü®fun i => toMeasurable Œº (s i ‚à© ‚ãÉ j ‚àà ({i}·∂ú : Set Œπ), s j), fun i =>
    measurableSet_toMeasurable _ _, fun i => _, _‚ü©
  ¬∑ simp only [measure_toMeasurable, inter_iUnion]
    -- ‚ä¢ ‚Üë‚ÜëŒº (‚ãÉ (i_1 : Œπ) (_ : i_1 ‚àà {i}·∂ú), s i ‚à© s i_1) = 0
    exact (measure_biUnion_null_iff <| to_countable _).2 fun j hj => hd (Ne.symm hj)
    -- üéâ no goals
  ¬∑ simp only [Pairwise, disjoint_left, onFun, mem_diff, not_and, and_imp, Classical.not_not]
    -- ‚ä¢ ‚àÄ ‚¶Éi j : Œπ‚¶Ñ, i ‚â† j ‚Üí ‚àÄ ‚¶Éa : Œ±‚¶Ñ, a ‚àà s i ‚Üí ¬¨a ‚àà toMeasurable Œº (s i ‚à© ‚ãÉ (j :  ‚Ä¶
    intro i j hne x hi hU hj
    -- ‚ä¢ x ‚àà toMeasurable Œº (s j ‚à© ‚ãÉ (j_1 : Œπ) (_ : j_1 ‚àà {j}·∂ú), s j_1)
    replace hU : x ‚àâ s i ‚à© iUnion Œª j => iUnion Œª _ => s j := Œª h => hU (subset_toMeasurable _ _ h)
    -- ‚ä¢ x ‚àà toMeasurable Œº (s j ‚à© ‚ãÉ (j_1 : Œπ) (_ : j_1 ‚àà {j}·∂ú), s j_1)
    simp only [mem_inter_iff, mem_iUnion, not_and, not_exists] at hU
    -- ‚ä¢ x ‚àà toMeasurable Œº (s j ‚à© ‚ãÉ (j_1 : Œπ) (_ : j_1 ‚àà {j}·∂ú), s j_1)
    exact (hU hi j hne.symm hj).elim
    -- üéâ no goals
#align measure_theory.exists_null_pairwise_disjoint_diff MeasureTheory.exists_null_pairwise_disjoint_diff

namespace AEDisjoint

protected theorem eq (h : AEDisjoint Œº s t) : Œº (s ‚à© t) = 0 :=
  h
#align measure_theory.ae_disjoint.eq MeasureTheory.AEDisjoint.eq

@[symm]
protected theorem symm (h : AEDisjoint Œº s t) : AEDisjoint Œº t s := by rwa [AEDisjoint, inter_comm]
                                                                       -- üéâ no goals
#align measure_theory.ae_disjoint.symm MeasureTheory.AEDisjoint.symm

protected theorem symmetric : Symmetric (AEDisjoint Œº) := fun _ _ => AEDisjoint.symm
#align measure_theory.ae_disjoint.symmetric MeasureTheory.AEDisjoint.symmetric

protected theorem comm : AEDisjoint Œº s t ‚Üî AEDisjoint Œº t s :=
  ‚ü®AEDisjoint.symm, AEDisjoint.symm‚ü©
#align measure_theory.ae_disjoint.comm MeasureTheory.AEDisjoint.comm

protected theorem _root_.Disjoint.aedisjoint (h : Disjoint s t) : AEDisjoint Œº s t := by
  rw [AEDisjoint, disjoint_iff_inter_eq_empty.1 h, measure_empty]
  -- üéâ no goals
#align disjoint.ae_disjoint Disjoint.aedisjoint

protected theorem _root_.Pairwise.aedisjoint {f : Œπ ‚Üí Set Œ±} (hf : Pairwise (Disjoint on f)) :
    Pairwise (AEDisjoint Œº on f) :=
  hf.mono fun _i _j h => h.aedisjoint
#align pairwise.ae_disjoint Pairwise.aedisjoint

protected theorem _root_.Set.PairwiseDisjoint.aedisjoint {f : Œπ ‚Üí Set Œ±} {s : Set Œπ}
    (hf : s.PairwiseDisjoint f) : s.Pairwise (AEDisjoint Œº on f) :=
  hf.mono' fun _i _j h => h.aedisjoint
#align set.pairwise_disjoint.ae_disjoint Set.PairwiseDisjoint.aedisjoint

theorem mono_ae (h : AEDisjoint Œº s t) (hu : u ‚â§·µê[Œº] s) (hv : v ‚â§·µê[Œº] t) : AEDisjoint Œº u v :=
  measure_mono_null_ae (hu.inter hv) h
#align measure_theory.ae_disjoint.mono_ae MeasureTheory.AEDisjoint.mono_ae

protected theorem mono (h : AEDisjoint Œº s t) (hu : u ‚äÜ s) (hv : v ‚äÜ t) : AEDisjoint Œº u v :=
  mono_ae h (HasSubset.Subset.eventuallyLE hu) (HasSubset.Subset.eventuallyLE hv)
#align measure_theory.ae_disjoint.mono MeasureTheory.AEDisjoint.mono

protected theorem congr (h : AEDisjoint Œº s t) (hu : u =·µê[Œº] s) (hv : v =·µê[Œº] t) :
    AEDisjoint Œº u v :=
  mono_ae h (Filter.EventuallyEq.le hu) (Filter.EventuallyEq.le hv)
#align measure_theory.ae_disjoint.congr MeasureTheory.AEDisjoint.congr

@[simp]
theorem iUnion_left_iff [Countable Œπ] {s : Œπ ‚Üí Set Œ±} :
    AEDisjoint Œº (‚ãÉ i, s i) t ‚Üî ‚àÄ i, AEDisjoint Œº (s i) t := by
  simp only [AEDisjoint, iUnion_inter, measure_iUnion_null_iff]
  -- üéâ no goals
#align measure_theory.ae_disjoint.Union_left_iff MeasureTheory.AEDisjoint.iUnion_left_iff

@[simp]
theorem iUnion_right_iff [Countable Œπ] {t : Œπ ‚Üí Set Œ±} :
    AEDisjoint Œº s (‚ãÉ i, t i) ‚Üî ‚àÄ i, AEDisjoint Œº s (t i) := by
  simp only [AEDisjoint, inter_iUnion, measure_iUnion_null_iff]
  -- üéâ no goals
#align measure_theory.ae_disjoint.Union_right_iff MeasureTheory.AEDisjoint.iUnion_right_iff

@[simp]
theorem union_left_iff : AEDisjoint Œº (s ‚à™ t) u ‚Üî AEDisjoint Œº s u ‚àß AEDisjoint Œº t u := by
  simp [union_eq_iUnion, and_comm]
  -- üéâ no goals
#align measure_theory.ae_disjoint.union_left_iff MeasureTheory.AEDisjoint.union_left_iff

@[simp]
theorem union_right_iff : AEDisjoint Œº s (t ‚à™ u) ‚Üî AEDisjoint Œº s t ‚àß AEDisjoint Œº s u := by
  simp [union_eq_iUnion, and_comm]
  -- üéâ no goals
#align measure_theory.ae_disjoint.union_right_iff MeasureTheory.AEDisjoint.union_right_iff

theorem union_left (hs : AEDisjoint Œº s u) (ht : AEDisjoint Œº t u) : AEDisjoint Œº (s ‚à™ t) u :=
  union_left_iff.mpr ‚ü®hs, ht‚ü©
#align measure_theory.ae_disjoint.union_left MeasureTheory.AEDisjoint.union_left

theorem union_right (ht : AEDisjoint Œº s t) (hu : AEDisjoint Œº s u) : AEDisjoint Œº s (t ‚à™ u) :=
  union_right_iff.2 ‚ü®ht, hu‚ü©
#align measure_theory.ae_disjoint.union_right MeasureTheory.AEDisjoint.union_right

theorem diff_ae_eq_left (h : AEDisjoint Œº s t) : (s \ t : Set Œ±) =·µê[Œº] s :=
  @diff_self_inter _ s t ‚ñ∏ diff_null_ae_eq_self h
#align measure_theory.ae_disjoint.diff_ae_eq_left MeasureTheory.AEDisjoint.diff_ae_eq_left

theorem diff_ae_eq_right (h : AEDisjoint Œº s t) : (t \ s : Set Œ±) =·µê[Œº] t :=
  diff_ae_eq_left <| AEDisjoint.symm h
#align measure_theory.ae_disjoint.diff_ae_eq_right MeasureTheory.AEDisjoint.diff_ae_eq_right

theorem measure_diff_left (h : AEDisjoint Œº s t) : Œº (s \ t) = Œº s :=
  measure_congr <| AEDisjoint.diff_ae_eq_left h
#align measure_theory.ae_disjoint.measure_diff_left MeasureTheory.AEDisjoint.measure_diff_left

theorem measure_diff_right (h : AEDisjoint Œº s t) : Œº (t \ s) = Œº t :=
  measure_congr <| AEDisjoint.diff_ae_eq_right h
#align measure_theory.ae_disjoint.measure_diff_right MeasureTheory.AEDisjoint.measure_diff_right

/-- If `s` and `t` are `Œº`-a.e. disjoint, then `s \ u` and `t` are disjoint for some measurable null
set `u`. -/
theorem exists_disjoint_diff (h : AEDisjoint Œº s t) :
    ‚àÉ u, MeasurableSet u ‚àß Œº u = 0 ‚àß Disjoint (s \ u) t :=
  ‚ü®toMeasurable Œº (s ‚à© t), measurableSet_toMeasurable _ _, (measure_toMeasurable _).trans h,
    disjoint_sdiff_self_left.mono_left fun x hx => by
      simp; exact ‚ü®hx.1, fun hxt => hx.2 <| subset_toMeasurable _ _ ‚ü®hx.1, hxt‚ü©‚ü©‚ü©
      -- ‚ä¢ x ‚àà ?m.9910 h ‚àß ¬¨x ‚àà t
            -- üéâ no goals
#align measure_theory.ae_disjoint.exists_disjoint_diff MeasureTheory.AEDisjoint.exists_disjoint_diff

theorem of_null_right (h : Œº t = 0) : AEDisjoint Œº s t :=
  measure_mono_null (inter_subset_right _ _) h
#align measure_theory.ae_disjoint.of_null_right MeasureTheory.AEDisjoint.of_null_right

theorem of_null_left (h : Œº s = 0) : AEDisjoint Œº s t :=
  AEDisjoint.symm (of_null_right h)
#align measure_theory.ae_disjoint.of_null_left MeasureTheory.AEDisjoint.of_null_left

end AEDisjoint

theorem aedisjoint_compl_left : AEDisjoint Œº s·∂ú s :=
  (@disjoint_compl_left _ _ s).aedisjoint
#align measure_theory.ae_disjoint_compl_left MeasureTheory.aedisjoint_compl_left

theorem aedisjoint_compl_right : AEDisjoint Œº s s·∂ú :=
  (@disjoint_compl_right _ _ s).aedisjoint
#align measure_theory.ae_disjoint_compl_right MeasureTheory.aedisjoint_compl_right

end MeasureTheory
