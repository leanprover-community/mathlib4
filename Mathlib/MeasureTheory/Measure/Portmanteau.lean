/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
import Mathlib.MeasureTheory.Integral.Layercake
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction

/-!
# Characterizations of weak convergence of finite measures and probability measures

This file will provide portmanteau characterizations of the weak convergence of finite measures
and of probability measures, i.e., the standard characterizations of convergence in distribution.

## Main definitions

The topologies of weak convergence on the types of finite measures and probability measures are
already defined in their corresponding files; no substantial new definitions are introduced here.

## Main results

The main result will be the portmanteau theorem providing various characterizations of the
weak convergence of measures (probability measures or finite measures). Given measures Î¼s
and Î¼ on a topological space Î©, the conditions that will be proven equivalent (under quite
general hypotheses) are:

  (T) The measures Î¼s tend to the measure Î¼ weakly.
  (C) For any closed set F, the limsup of the measures of F under Î¼s is at most
      the measure of F under Î¼, i.e., limsupáµ¢ Î¼sáµ¢(F) â‰¤ Î¼(F).
  (O) For any open set G, the liminf of the measures of G under Î¼s is at least
      the measure of G under Î¼, i.e., Î¼(G) â‰¤ liminfáµ¢ Î¼sáµ¢(G).
  (B) For any Borel set B whose boundary carries no mass under Î¼, i.e. Î¼(âˆ‚B) = 0,
      the measures of B under Î¼s tend to the measure of B under Î¼, i.e., limáµ¢ Î¼sáµ¢(B) = Î¼(B).

The separate implications are:
* `MeasureTheory.FiniteMeasure.limsup_measure_closed_le_of_tendsto` is the implication (T) â†’ (C).
* `MeasureTheory.limsup_measure_closed_le_iff_liminf_measure_open_ge` is the equivalence (C) â†” (O).
* `MeasureTheory.tendsto_measure_of_null_frontier` is the implication (O) â†’ (B).
* `MeasureTheory.limsup_measure_closed_le_of_forall_tendsto_measure` is the implication (B) â†’ (C).
* `MeasureTheory.tendsto_of_forall_isOpen_le_liminf` gives the implication (O) â†’ (T) for
    any sequence of Borel probability measures.

We also deduce a practical convergence criterion for probability measures, in
`IsPiSystem.tendsto_probabilityMeasure_of_tendsto_of_mem`.
Assume that, applied to all the elements of a Ï€-system, a sequence of probability measures
converges to a limiting probability measure. Assume also that the Ï€-system contains arbitrarily
small neighborhoods of any point. Then the sequence of probability measures converges for the
weak topology.

## Implementation notes

Many of the characterizations of weak convergence hold for finite measures and are proven in that
generality and then specialized to probability measures. Some implications hold with slightly
more general assumptions than in the usual statement of portmanteau theorem. The full portmanteau
theorem, however, is most convenient for probability measures on pseudo-emetrizable spaces with
their Borel sigma algebras.

Some specific considerations on the assumptions in the different implications:
* `MeasureTheory.FiniteMeasure.limsup_measure_closed_le_of_tendsto`, i.e., implication (T) â†’ (C),
  assumes that in the underlying topological space, indicator functions of closed sets have
  decreasing bounded continuous pointwise approximating sequences. The assumption is in the form
  of the type class `HasOuterApproxClosed`. Type class inference knows that for example the more
  common assumptions of metrizability or pseudo-emetrizability suffice.
* Where formulations are currently only provided for probability measures, one can obtain the
  finite measure formulations using the characterization of convergence of finite measures by
  their total masses and their probability-normalized versions, i.e., by
  `MeasureTheory.FiniteMeasure.tendsto_normalize_iff_tendsto`.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

weak convergence of measures, convergence in distribution, convergence in law, finite measure,
probability measure

-/


noncomputable section

open MeasureTheory Set Filter BoundedContinuousFunction
open scoped Topology ENNReal NNReal BoundedContinuousFunction

namespace MeasureTheory

section LimsupClosedLEAndLELiminfOpen

/-! ### Portmanteau: limsup condition for closed sets iff liminf condition for open sets

In this section we prove that for a sequence of Borel probability measures on a topological space
and its candidate limit measure, the following two conditions are equivalent:

  (C) For any closed set F, the limsup of the measures of F under Î¼s is at most
      the measure of F under Î¼, i.e., limsupáµ¢ Î¼sáµ¢(F) â‰¤ Î¼(F);
  (O) For any open set G, the liminf of the measures of G under Î¼s is at least
      the measure of G under Î¼, i.e., Î¼(G) â‰¤ liminfáµ¢ Î¼sáµ¢(G).

Either of these will later be shown to be equivalent to the weak convergence of the sequence
of measures.
-/

variable {Î© : Type*} [MeasurableSpace Î©]

theorem le_measure_compl_liminf_of_limsup_measure_le {Î¹ : Type*} {L : Filter Î¹} {Î¼ : Measure Î©}
    {Î¼s : Î¹ â†’ Measure Î©} [IsProbabilityMeasure Î¼] [âˆ€ i, IsProbabilityMeasure (Î¼s i)] {E : Set Î©}
    (E_mble : MeasurableSet E) (h : (L.limsup fun i â†¦ Î¼s i E) â‰¤ Î¼ E) :
    Î¼ Eá¶œ â‰¤ L.liminf fun i â†¦ Î¼s i Eá¶œ := by
  rcases L.eq_or_neBot with rfl | hne
  Â· simp only [liminf_bot, le_top]
  have meas_Ec : Î¼ Eá¶œ = 1 - Î¼ E := by
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top Î¼ E).ne
  have meas_i_Ec : âˆ€ i, Î¼s i Eá¶œ = 1 - Î¼s i E := by
    intro i
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top (Î¼s i) E).ne
  simp_rw [meas_Ec, meas_i_Ec]
  rw [show (L.liminf fun i : Î¹ â†¦ 1 - Î¼s i E) = L.liminf ((fun x â†¦ 1 - x) âˆ˜ fun i : Î¹ â†¦ Î¼s i E)
      from rfl]
  have key := antitone_const_tsub.map_limsup_of_continuousAt (F := L)
    (fun i â†¦ Î¼s i E) (ENNReal.continuous_sub_left ENNReal.one_ne_top).continuousAt
  simpa [â† key] using antitone_const_tsub h

theorem le_measure_liminf_of_limsup_measure_compl_le {Î¹ : Type*} {L : Filter Î¹} {Î¼ : Measure Î©}
    {Î¼s : Î¹ â†’ Measure Î©} [IsProbabilityMeasure Î¼] [âˆ€ i, IsProbabilityMeasure (Î¼s i)] {E : Set Î©}
    (E_mble : MeasurableSet E) (h : (L.limsup fun i â†¦ Î¼s i Eá¶œ) â‰¤ Î¼ Eá¶œ) :
    Î¼ E â‰¤ L.liminf fun i â†¦ Î¼s i E :=
  compl_compl E â–¸ le_measure_compl_liminf_of_limsup_measure_le (MeasurableSet.compl E_mble) h

theorem limsup_measure_compl_le_of_le_liminf_measure {Î¹ : Type*} {L : Filter Î¹} {Î¼ : Measure Î©}
    {Î¼s : Î¹ â†’ Measure Î©} [IsProbabilityMeasure Î¼] [âˆ€ i, IsProbabilityMeasure (Î¼s i)] {E : Set Î©}
    (E_mble : MeasurableSet E) (h : Î¼ E â‰¤ L.liminf fun i â†¦ Î¼s i E) :
    (L.limsup fun i â†¦ Î¼s i Eá¶œ) â‰¤ Î¼ Eá¶œ := by
  rcases L.eq_or_neBot with rfl | hne
  Â· simp only [limsup_bot, bot_le]
  have meas_Ec : Î¼ Eá¶œ = 1 - Î¼ E := by
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top Î¼ E).ne
  have meas_i_Ec : âˆ€ i, Î¼s i Eá¶œ = 1 - Î¼s i E := by
    intro i
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top (Î¼s i) E).ne
  simp_rw [meas_Ec, meas_i_Ec]
  rw [show (L.limsup fun i : Î¹ â†¦ 1 - Î¼s i E) = L.limsup ((fun x â†¦ 1 - x) âˆ˜ fun i : Î¹ â†¦ Î¼s i E)
      from rfl]
  have key := antitone_const_tsub.map_liminf_of_continuousAt (F := L)
    (fun i â†¦ Î¼s i E) (ENNReal.continuous_sub_left ENNReal.one_ne_top).continuousAt
  simpa [â† key] using antitone_const_tsub h

theorem limsup_measure_le_of_le_liminf_measure_compl {Î¹ : Type*} {L : Filter Î¹} {Î¼ : Measure Î©}
    {Î¼s : Î¹ â†’ Measure Î©} [IsProbabilityMeasure Î¼] [âˆ€ i, IsProbabilityMeasure (Î¼s i)] {E : Set Î©}
    (E_mble : MeasurableSet E) (h : Î¼ Eá¶œ â‰¤ L.liminf fun i â†¦ Î¼s i Eá¶œ) :
    (L.limsup fun i â†¦ Î¼s i E) â‰¤ Î¼ E :=
  compl_compl E â–¸ limsup_measure_compl_le_of_le_liminf_measure (MeasurableSet.compl E_mble) h

variable [TopologicalSpace Î©] [OpensMeasurableSpace Î©]

/-- One pair of implications of the portmanteau theorem:
For a sequence of Borel probability measures, the following two are equivalent:

(C) The limsup of the measures of any closed set is at most the measure of the closed set
under a candidate limit measure.

(O) The liminf of the measures of any open set is at least the measure of the open set
under a candidate limit measure.
-/
theorem limsup_measure_closed_le_iff_liminf_measure_open_ge {Î¹ : Type*} {L : Filter Î¹}
    {Î¼ : Measure Î©} {Î¼s : Î¹ â†’ Measure Î©} [IsProbabilityMeasure Î¼]
    [âˆ€ i, IsProbabilityMeasure (Î¼s i)] :
    (âˆ€ F, IsClosed F â†’ (L.limsup fun i â†¦ Î¼s i F) â‰¤ Î¼ F) â†”
      âˆ€ G, IsOpen G â†’ Î¼ G â‰¤ L.liminf fun i â†¦ Î¼s i G := by
  constructor
  Â· intro h G G_open
    exact le_measure_liminf_of_limsup_measure_compl_le
      G_open.measurableSet (h Gá¶œ (isClosed_compl_iff.mpr G_open))
  Â· intro h F F_closed
    exact limsup_measure_le_of_le_liminf_measure_compl
      F_closed.measurableSet (h Fá¶œ (isOpen_compl_iff.mpr F_closed))

end LimsupClosedLEAndLELiminfOpen -- section

section TendstoOfNullFrontier

/-! ### Portmanteau: limit of measures of Borel sets whose boundary carries no mass in the limit

In this section we prove that for a sequence of Borel probability measures on a topological space
and its candidate limit measure, either of the following equivalent conditions:

  (C) For any closed set F, the limsup of the measures of F under Î¼s is at most
      the measure of F under Î¼, i.e., limsupáµ¢ Î¼sáµ¢(F) â‰¤ Î¼(F);
  (O) For any open set G, the liminf of the measures of G under Î¼s is at least
      the measure of G under Î¼, i.e., Î¼(G) â‰¤ liminfáµ¢ Î¼sáµ¢(G).

implies that

  (B) For any Borel set B whose boundary carries no mass under Î¼, i.e. Î¼(âˆ‚B) = 0,
      the measures of B under Î¼s tend to the measure of B under Î¼, i.e., limáµ¢ Î¼sáµ¢(B) = Î¼(B).
-/


variable {Î© : Type*} [MeasurableSpace Î©]

theorem tendsto_measure_of_le_liminf_measure_of_limsup_measure_le {Î¹ : Type*} {L : Filter Î¹}
    {Î¼ : Measure Î©} {Î¼s : Î¹ â†’ Measure Î©} {Eâ‚€ E Eâ‚ : Set Î©} (Eâ‚€_subset : Eâ‚€ âŠ† E) (subset_Eâ‚ : E âŠ† Eâ‚)
    (nulldiff : Î¼ (Eâ‚ \ Eâ‚€) = 0) (h_Eâ‚€ : Î¼ Eâ‚€ â‰¤ L.liminf fun i â†¦ Î¼s i Eâ‚€)
    (h_Eâ‚ : (L.limsup fun i â†¦ Î¼s i Eâ‚) â‰¤ Î¼ Eâ‚) : L.Tendsto (fun i â†¦ Î¼s i E) (ğ“ (Î¼ E)) := by
  apply tendsto_of_le_liminf_of_limsup_le
  Â· have Eâ‚€_ae_eq_E : Eâ‚€ =áµ[Î¼] E :=
      EventuallyLE.antisymm Eâ‚€_subset.eventuallyLE
        (subset_Eâ‚.eventuallyLE.trans (ae_le_set.mpr nulldiff))
    calc
      Î¼ E = Î¼ Eâ‚€ := measure_congr Eâ‚€_ae_eq_E.symm
      _ â‰¤ L.liminf fun i â†¦ Î¼s i Eâ‚€ := h_Eâ‚€
      _ â‰¤ L.liminf fun i â†¦ Î¼s i E :=
        liminf_le_liminf (.of_forall fun _ â†¦ measure_mono Eâ‚€_subset)
  Â· have E_ae_eq_Eâ‚ : E =áµ[Î¼] Eâ‚ :=
      EventuallyLE.antisymm subset_Eâ‚.eventuallyLE
        ((ae_le_set.mpr nulldiff).trans Eâ‚€_subset.eventuallyLE)
    calc
      (L.limsup fun i â†¦ Î¼s i E) â‰¤ L.limsup fun i â†¦ Î¼s i Eâ‚ :=
        limsup_le_limsup (.of_forall fun _ â†¦ measure_mono subset_Eâ‚)
      _ â‰¤ Î¼ Eâ‚ := h_Eâ‚
      _ = Î¼ E := measure_congr E_ae_eq_Eâ‚.symm
  Â· infer_param
  Â· infer_param

variable [TopologicalSpace Î©] [OpensMeasurableSpace Î©]

/-- One implication of the portmanteau theorem:
For a sequence of Borel probability measures, if the liminf of the measures of any open set is at
least the measure of the open set under a candidate limit measure, then for any set whose
boundary carries no probability mass under the candidate limit measure, then its measures under the
sequence converge to its measure under the candidate limit measure.
-/
theorem tendsto_measure_of_null_frontier {Î¹ : Type*} {L : Filter Î¹} {Î¼ : Measure Î©}
    {Î¼s : Î¹ â†’ Measure Î©} [IsProbabilityMeasure Î¼] [âˆ€ i, IsProbabilityMeasure (Î¼s i)]
    (h_opens : âˆ€ G, IsOpen G â†’ Î¼ G â‰¤ L.liminf fun i â†¦ Î¼s i G) {E : Set Î©}
    (E_nullbdry : Î¼ (frontier E) = 0) : L.Tendsto (fun i â†¦ Î¼s i E) (ğ“ (Î¼ E)) :=
  haveI h_closeds : âˆ€ F, IsClosed F â†’ (L.limsup fun i â†¦ Î¼s i F) â‰¤ Î¼ F :=
    limsup_measure_closed_le_iff_liminf_measure_open_ge.mpr h_opens
  tendsto_measure_of_le_liminf_measure_of_limsup_measure_le interior_subset subset_closure
    E_nullbdry (h_opens _ isOpen_interior) (h_closeds _ isClosed_closure)

end TendstoOfNullFrontier --section

section ConvergenceImpliesLimsupClosedLE

/-! ### Portmanteau implication: weak convergence implies a limsup condition for closed sets

In this section we prove, under the assumption that the underlying topological space `Î©` is
pseudo-emetrizable, that

  (T) The measures Î¼s tend to the measure Î¼ weakly

implies

  (C) For any closed set F, the limsup of the measures of F under Î¼s is at most
      the measure of F under Î¼, i.e., limsupáµ¢ Î¼sáµ¢(F) â‰¤ Î¼(F).

Combining with a earlier proven implications, we get that (T) implies also both

  (O) For any open set G, the liminf of the measures of G under Î¼s is at least
      the measure of G under Î¼, i.e., Î¼(G) â‰¤ liminfáµ¢ Î¼sáµ¢(G);
  (B) For any Borel set B whose boundary carries no mass under Î¼, i.e. Î¼(âˆ‚B) = 0,
      the measures of B under Î¼s tend to the measure of B under Î¼, i.e., limáµ¢ Î¼sáµ¢(B) = Î¼(B).
-/


/-- One implication of the portmanteau theorem:
Weak convergence of finite measures implies that the limsup of the measures of any closed set is
at most the measure of the closed set under the limit measure.
-/
theorem FiniteMeasure.limsup_measure_closed_le_of_tendsto {Î© Î¹ : Type*} {L : Filter Î¹}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [HasOuterApproxClosed Î©]
    [OpensMeasurableSpace Î©] {Î¼ : FiniteMeasure Î©}
    {Î¼s : Î¹ â†’ FiniteMeasure Î©} (Î¼s_lim : Tendsto Î¼s L (ğ“ Î¼)) {F : Set Î©} (F_closed : IsClosed F) :
    (L.limsup fun i â†¦ (Î¼s i : Measure Î©) F) â‰¤ (Î¼ : Measure Î©) F := by
  rcases L.eq_or_neBot with rfl | hne
  Â· simp only [limsup_bot, bot_le]
  apply ENNReal.le_of_forall_pos_le_add
  intro Îµ Îµ_pos _
  have Îµ_pos' := (ENNReal.half_pos (ENNReal.coe_ne_zero.mpr Îµ_pos.ne.symm)).ne.symm
  let fs := F_closed.apprSeq
  have keyâ‚ : Tendsto (fun n â†¦ âˆ«â» Ï‰, (fs n Ï‰ : â„â‰¥0âˆ) âˆ‚Î¼) atTop (ğ“ ((Î¼ : Measure Î©) F)) :=
    HasOuterApproxClosed.tendsto_lintegral_apprSeq F_closed (Î¼ : Measure Î©)
  have roomâ‚ : (Î¼ : Measure Î©) F < (Î¼ : Measure Î©) F + Îµ / 2 :=
    ENNReal.lt_add_right (measure_lt_top (Î¼ : Measure Î©) F).ne Îµ_pos'
  obtain âŸ¨M, hMâŸ© := eventually_atTop.mp <| keyâ‚.eventually_lt_const roomâ‚
  have keyâ‚‚ := FiniteMeasure.tendsto_iff_forall_lintegral_tendsto.mp Î¼s_lim (fs M)
  have roomâ‚‚ :
    (lintegral (Î¼ : Measure Î©) fun a â†¦ fs M a) <
      (lintegral (Î¼ : Measure Î©) fun a â†¦ fs M a) + Îµ / 2 :=
    ENNReal.lt_add_right (ne_of_lt ((fs M).lintegral_lt_top_of_nnreal _)) Îµ_pos'
  have ev_near := keyâ‚‚.eventually_le_const roomâ‚‚
  have ev_near' := ev_near.mono
    (fun n â†¦ le_trans (HasOuterApproxClosed.measure_le_lintegral F_closed (Î¼s n) M))
  apply (Filter.limsup_le_limsup ev_near').trans
  rw [limsup_const]
  apply le_trans (add_le_add (hM M rfl.le).le (le_refl (Îµ / 2 : â„â‰¥0âˆ)))
  simp only [add_assoc, ENNReal.add_halves, le_refl]

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that the limsup of the measures of any closed
set is at most the measure of the closed set under the limit probability measure.
-/
theorem ProbabilityMeasure.limsup_measure_closed_le_of_tendsto {Î© Î¹ : Type*} {L : Filter Î¹}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©] [HasOuterApproxClosed Î©]
    {Î¼ : ProbabilityMeasure Î©} {Î¼s : Î¹ â†’ ProbabilityMeasure Î©} (Î¼s_lim : Tendsto Î¼s L (ğ“ Î¼))
    {F : Set Î©} (F_closed : IsClosed F) :
    (L.limsup fun i â†¦ (Î¼s i : Measure Î©) F) â‰¤ (Î¼ : Measure Î©) F := by
  apply FiniteMeasure.limsup_measure_closed_le_of_tendsto
    ((tendsto_nhds_iff_toFiniteMeasure_tendsto_nhds L).mp Î¼s_lim) F_closed

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that the liminf of the measures of any open set
is at least the measure of the open set under the limit probability measure.
-/
theorem ProbabilityMeasure.le_liminf_measure_open_of_tendsto {Î© Î¹ : Type*} {L : Filter Î¹}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©] [HasOuterApproxClosed Î©]
    {Î¼ : ProbabilityMeasure Î©} {Î¼s : Î¹ â†’ ProbabilityMeasure Î©} (Î¼s_lim : Tendsto Î¼s L (ğ“ Î¼))
    {G : Set Î©} (G_open : IsOpen G) :
    (Î¼ : Measure Î©) G â‰¤ L.liminf fun i â†¦ (Î¼s i : Measure Î©) G :=
  haveI h_closeds : âˆ€ F, IsClosed F â†’ (L.limsup fun i â†¦ (Î¼s i : Measure Î©) F) â‰¤ (Î¼ : Measure Î©) F :=
    fun _ F_closed â†¦ limsup_measure_closed_le_of_tendsto Î¼s_lim F_closed
  le_measure_liminf_of_limsup_measure_compl_le G_open.measurableSet
    (h_closeds _ (isClosed_compl_iff.mpr G_open))

theorem ProbabilityMeasure.tendsto_measure_of_null_frontier_of_tendsto' {Î© Î¹ : Type*}
    {L : Filter Î¹} [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©]
    [HasOuterApproxClosed Î©] {Î¼ : ProbabilityMeasure Î©} {Î¼s : Î¹ â†’ ProbabilityMeasure Î©}
    (Î¼s_lim : Tendsto Î¼s L (ğ“ Î¼)) {E : Set Î©} (E_nullbdry : (Î¼ : Measure Î©) (frontier E) = 0) :
    Tendsto (fun i â†¦ (Î¼s i : Measure Î©) E) L (ğ“ ((Î¼ : Measure Î©) E)) :=
  haveI h_opens : âˆ€ G, IsOpen G â†’ (Î¼ : Measure Î©) G â‰¤ L.liminf fun i â†¦ (Î¼s i : Measure Î©) G :=
    fun _ G_open â†¦ le_liminf_measure_open_of_tendsto Î¼s_lim G_open
  tendsto_measure_of_null_frontier h_opens E_nullbdry

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that if the boundary of a Borel set
carries no probability mass under the limit measure, then the limit of the measures of the set
equals the measure of the set under the limit probability measure.

A version with coercions to ordinary `â„â‰¥0âˆ`-valued measures is
`MeasureTheory.ProbabilityMeasure.tendsto_measure_of_null_frontier_of_tendsto'`.
-/
theorem ProbabilityMeasure.tendsto_measure_of_null_frontier_of_tendsto {Î© Î¹ : Type*} {L : Filter Î¹}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©] [HasOuterApproxClosed Î©]
    {Î¼ : ProbabilityMeasure Î©} {Î¼s : Î¹ â†’ ProbabilityMeasure Î©} (Î¼s_lim : Tendsto Î¼s L (ğ“ Î¼))
    {E : Set Î©} (E_nullbdry : Î¼ (frontier E) = 0) : Tendsto (fun i â†¦ Î¼s i E) L (ğ“ (Î¼ E)) := by
  have key := tendsto_measure_of_null_frontier_of_tendsto' Î¼s_lim (by simpa using E_nullbdry)
  exact (ENNReal.tendsto_toNNReal (measure_ne_top (â†‘Î¼) E)).comp key

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that if a set is clopen, then the limit of the
measures of the set equals the measure of the set under the limit probability measure.
-/
theorem ProbabilityMeasure.tendsto_measure_of_isClopen_of_tendsto {Î© Î¹ : Type*} {L : Filter Î¹}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©] [HasOuterApproxClosed Î©]
    {Î¼ : ProbabilityMeasure Î©} {Î¼s : Î¹ â†’ ProbabilityMeasure Î©} (Î¼s_lim : Tendsto Î¼s L (ğ“ Î¼))
    {E : Set Î©} (hE : IsClopen E) : Tendsto (fun i â†¦ Î¼s i E) L (ğ“ (Î¼ E)) :=
  ProbabilityMeasure.tendsto_measure_of_null_frontier_of_tendsto Î¼s_lim (by simp [hE])

end ConvergenceImpliesLimsupClosedLE --section

section LimitBorelImpliesLimsupClosedLE

/-! ### Portmanteau implication: limit condition for Borel sets implies limsup for closed sets


In this section we prove, under the assumption that the underlying topological space `Î©` is
pseudo-emetrizable, that

  (B) For any Borel set B whose boundary carries no mass under Î¼, i.e. Î¼(âˆ‚B) = 0,
      the measures of B under Î¼s tend to the measure of B under Î¼, i.e., limáµ¢ Î¼sáµ¢(B) = Î¼(B)

implies

  (C) For any closed set F, the limsup of the measures of F under Î¼s is at most
      the measure of F under Î¼, i.e., limsupáµ¢ Î¼sáµ¢(F) â‰¤ Î¼(F).

Combining with a earlier proven implications, we get that (B) implies also

  (O) For any open set G, the liminf of the measures of G under Î¼s is at least
      the measure of G under Î¼, i.e., Î¼(G) â‰¤ liminfáµ¢ Î¼sáµ¢(G).

-/

open ENNReal

section PseudoMetricSpace

variable {Î© : Type*} [PseudoMetricSpace Î©] [MeasurableSpace Î©] [OpensMeasurableSpace Î©]

theorem exists_null_frontier_thickening (Î¼ : Measure Î©) [SFinite Î¼] (s : Set Î©) {a b : â„}
    (hab : a < b) : âˆƒ r âˆˆ Ioo a b, Î¼ (frontier (Metric.thickening r s)) = 0 := by
  have mbles : âˆ€ r : â„, MeasurableSet (frontier (Metric.thickening r s)) :=
    fun r â†¦ isClosed_frontier.measurableSet
  have disjs := Metric.frontier_thickening_disjoint s
  have key := Measure.countable_meas_pos_of_disjoint_iUnion (Î¼ := Î¼) mbles disjs
  have aux := measure_diff_null (s := Ioo a b) (Set.Countable.measure_zero key volume)
  have len_pos : 0 < ENNReal.ofReal (b - a) := by simp only [hab, ENNReal.ofReal_pos, sub_pos]
  rw [â† Real.volume_Ioo, â† aux] at len_pos
  rcases nonempty_of_measure_ne_zero len_pos.ne.symm with âŸ¨r, âŸ¨r_in_Ioo, hrâŸ©âŸ©
  refine âŸ¨r, r_in_Ioo, ?_âŸ©
  simpa only [mem_setOf_eq, not_lt, le_zero_iff] using hr

theorem exists_null_frontiers_thickening (Î¼ : Measure Î©) [SFinite Î¼] (s : Set Î©) :
    âˆƒ rs : â„• â†’ â„,
      Tendsto rs atTop (ğ“ 0) âˆ§ âˆ€ n, 0 < rs n âˆ§ Î¼ (frontier (Metric.thickening (rs n) s)) = 0 := by
  rcases exists_seq_strictAnti_tendsto (0 : â„) with âŸ¨Rs, âŸ¨_, âŸ¨Rs_pos, Rs_limâŸ©âŸ©âŸ©
  have obs := fun n : â„• => exists_null_frontier_thickening Î¼ s (Rs_pos n)
  refine âŸ¨fun n : â„• => (obs n).choose, âŸ¨?_, ?_âŸ©âŸ©
  Â· exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds Rs_lim
      (fun n â†¦ (obs n).choose_spec.1.1.le) fun n â†¦ (obs n).choose_spec.1.2.le
  Â· exact fun n â†¦ âŸ¨(obs n).choose_spec.1.1, (obs n).choose_spec.2âŸ©

end PseudoMetricSpace

open TopologicalSpace

/-- One implication of the portmanteau theorem:
Assuming that for all Borel sets E whose boundary âˆ‚E carries no probability mass under a
candidate limit probability measure Î¼ we have convergence of the measures Î¼sáµ¢(E) to Î¼(E),
then for all closed sets F we have the limsup condition limsup Î¼sáµ¢(F) â‰¤ Î¼(F). -/
lemma limsup_measure_closed_le_of_forall_tendsto_measure
    {Î© Î¹ : Type*} {L : Filter Î¹} [MeasurableSpace Î©] [TopologicalSpace Î©]
    [PseudoMetrizableSpace Î©] [OpensMeasurableSpace Î©]
    {Î¼ : Measure Î©} [IsFiniteMeasure Î¼] {Î¼s : Î¹ â†’ Measure Î©}
    (h : âˆ€ {E : Set Î©}, MeasurableSet E â†’ Î¼ (frontier E) = 0 â†’
            Tendsto (fun i â†¦ Î¼s i E) L (ğ“ (Î¼ E)))
    (F : Set Î©) (F_closed : IsClosed F) :
    L.limsup (fun i â†¦ Î¼s i F) â‰¤ Î¼ F := by
  letI : PseudoMetricSpace Î© := TopologicalSpace.pseudoMetrizableSpacePseudoMetric Î©
  rcases L.eq_or_neBot with rfl | _
  Â· simp only [limsup_bot, bot_eq_zero', zero_le]
  have ex := exists_null_frontiers_thickening Î¼ F
  let rs := Classical.choose ex
  have rs_lim : Tendsto rs atTop (ğ“ 0) := (Classical.choose_spec ex).1
  have rs_pos : âˆ€ n, 0 < rs n := fun n â†¦ ((Classical.choose_spec ex).2 n).1
  have rs_null : âˆ€ n, Î¼ (frontier (Metric.thickening (rs n) F)) = 0 :=
    fun n â†¦ ((Classical.choose_spec ex).2 n).2
  have Fthicks_open : âˆ€ n, IsOpen (Metric.thickening (rs n) F) :=
    fun n â†¦ Metric.isOpen_thickening
  have key := fun (n : â„•) â†¦ h (Fthicks_open n).measurableSet (rs_null n)
  apply ENNReal.le_of_forall_pos_le_add
  intros Îµ Îµ_pos Î¼F_finite
  have keyB := tendsto_measure_cthickening_of_isClosed (Î¼ := Î¼) (s := F)
                âŸ¨1, âŸ¨by simp only [gt_iff_lt, zero_lt_one], measure_ne_top _ _âŸ©âŸ© F_closed
  have nhds : Iio (Î¼ F + Îµ) âˆˆ ğ“ (Î¼ F) :=
    Iio_mem_nhds <| ENNReal.lt_add_right Î¼F_finite.ne (ENNReal.coe_pos.mpr Îµ_pos).ne'
  specialize rs_lim (keyB nhds)
  simp only [mem_map, mem_atTop_sets, ge_iff_le, mem_preimage, mem_Iio] at rs_lim
  obtain âŸ¨m, hmâŸ© := rs_lim
  have aux : (fun i â†¦ (Î¼s i F)) â‰¤á¶ [L] (fun i â†¦ Î¼s i (Metric.thickening (rs m) F)) :=
    .of_forall <| fun i â†¦ measure_mono (Metric.self_subset_thickening (rs_pos m) F)
  refine (limsup_le_limsup aux).trans ?_
  rw [Tendsto.limsup_eq (key m)]
  apply (measure_mono (Metric.thickening_subset_cthickening (rs m) F)).trans (hm m rfl.le).le

/-- One implication of the portmanteau theorem:
Assuming that for all Borel sets E whose boundary âˆ‚E carries no probability mass under a
candidate limit probability measure Î¼ we have convergence of the measures Î¼sáµ¢(E) to Î¼(E),
then for all open sets G we have the limsup condition Î¼(G) â‰¤ liminf Î¼sáµ¢(G). -/
lemma le_liminf_measure_open_of_forall_tendsto_measure
    {Î© Î¹ : Type*} {L : Filter Î¹} [MeasurableSpace Î©] [TopologicalSpace Î©]
    [PseudoMetrizableSpace Î©] [OpensMeasurableSpace Î©]
    {Î¼ : Measure Î©} [IsProbabilityMeasure Î¼] {Î¼s : Î¹ â†’ Measure Î©} [âˆ€ i, IsProbabilityMeasure (Î¼s i)]
    (h : âˆ€ {E}, MeasurableSet E â†’ Î¼ (frontier E) = 0 â†’ Tendsto (fun i â†¦ Î¼s i E) L (ğ“ (Î¼ E)))
    (G : Set Î©) (G_open : IsOpen G) :
    Î¼ G â‰¤ L.liminf (fun i â†¦ Î¼s i G) := by
  apply le_measure_liminf_of_limsup_measure_compl_le G_open.measurableSet
  exact limsup_measure_closed_le_of_forall_tendsto_measure h _ (isClosed_compl_iff.mpr G_open)

end LimitBorelImpliesLimsupClosedLE --section

section le_liminf_open_implies_convergence

/-! ### Portmanteau implication: liminf condition for open sets implies weak convergence


In this section we prove for a sequence (Î¼sâ‚™)â‚™ Borel probability measures that

  (O) For any open set G, the liminf of the measures of G under Î¼sâ‚™ is at least
      the measure of G under Î¼, i.e., Î¼(G) â‰¤ liminfâ‚™ Î¼sâ‚™(G).

implies

  (T) The measures Î¼sâ‚™ converge weakly to the measure Î¼.

-/

variable {Î© : Type*} [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©]

lemma lintegral_le_liminf_lintegral_of_forall_isOpen_measure_le_liminf_measure
    {Î¼ : Measure Î©} {Î¼s : â„• â†’ Measure Î©} {f : Î© â†’ â„} (f_cont : Continuous f) (f_nn : 0 â‰¤ f)
    (h_opens : âˆ€ G, IsOpen G â†’ Î¼ G â‰¤ atTop.liminf (fun i â†¦ Î¼s i G)) :
    âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼ â‰¤ atTop.liminf (fun i â†¦ âˆ«â» x, ENNReal.ofReal (f x) âˆ‚(Î¼s i)) := by
  simp_rw [lintegral_eq_lintegral_meas_lt _ (Eventually.of_forall f_nn) f_cont.aemeasurable]
  calc âˆ«â» (t : â„) in Set.Ioi 0, Î¼ {a | t < f a}
      â‰¤ âˆ«â» (t : â„) in Set.Ioi 0, atTop.liminf (fun i â†¦ (Î¼s i) {a | t < f a}) := ?_ -- (i)
    _ â‰¤ atTop.liminf (fun i â†¦ âˆ«â» (t : â„) in Set.Ioi 0, (Î¼s i) {a | t < f a}) := ?_ -- (ii)
  Â· -- (i)
    exact (lintegral_mono (fun t â†¦ h_opens _ (continuous_def.mp f_cont _ isOpen_Ioi))).trans
            (le_refl _)
  Â· -- (ii)
    exact lintegral_liminf_le (fun n â†¦ Antitone.measurable (fun s t hst â†¦
            measure_mono (fun Ï‰ hÏ‰ â†¦ lt_of_le_of_lt hst hÏ‰)))

lemma integral_le_liminf_integral_of_forall_isOpen_measure_le_liminf_measure
    {Î¼ : Measure Î©} {Î¼s : â„• â†’ Measure Î©} [âˆ€ i, IsProbabilityMeasure (Î¼s i)]
    {f : Î© â†’áµ‡ â„} (f_nn : 0 â‰¤ f)
    (h_opens : âˆ€ G, IsOpen G â†’ Î¼ G â‰¤ atTop.liminf (fun i â†¦ Î¼s i G)) :
    âˆ« x, (f x) âˆ‚Î¼ â‰¤ atTop.liminf (fun i â†¦ âˆ« x, (f x) âˆ‚(Î¼s i)) := by
  have same := lintegral_le_liminf_lintegral_of_forall_isOpen_measure_le_liminf_measure
                  f.continuous f_nn h_opens
  rw [@integral_eq_lintegral_of_nonneg_ae Î© _ Î¼ f (Eventually.of_forall f_nn)
        f.continuous.measurable.aestronglyMeasurable]
  convert ENNReal.toReal_mono ?_ same
  Â· simp only [fun i â†¦ @integral_eq_lintegral_of_nonneg_ae Î© _ (Î¼s i) f (Eventually.of_forall f_nn)
                        f.continuous.measurable.aestronglyMeasurable]
    let g := BoundedContinuousFunction.comp _ Real.lipschitzWith_toNNReal f
    have bound : âˆ€ i, âˆ«â» x, ENNReal.ofReal (f x) âˆ‚(Î¼s i) â‰¤ nndist 0 g := fun i â†¦ by
      simpa only [coe_nnreal_ennreal_nndist, measure_univ, mul_one, ge_iff_le] using
            BoundedContinuousFunction.lintegral_le_edist_mul (Î¼ := Î¼s i) g
    apply ENNReal.liminf_toReal_eq ENNReal.coe_ne_top (Eventually.of_forall bound)
  Â· apply ne_of_lt
    have obs := fun (i : â„•) â†¦ @BoundedContinuousFunction.lintegral_nnnorm_le Î© _ _ (Î¼s i) â„ _ f
    simp only [measure_univ, mul_one] at obs
    apply lt_of_le_of_lt _ (show (â€–fâ€–â‚Š : â„â‰¥0âˆ) < âˆ from ENNReal.coe_lt_top)
    apply liminf_le_of_le
    Â· refine âŸ¨0, .of_forall (by simp only [ge_iff_le, zero_le, forall_const])âŸ©
    Â· intro x hx
      obtain âŸ¨i, hiâŸ© := hx.exists
      apply le_trans hi
      convert obs i with x
      have aux := ENNReal.ofReal_eq_coe_nnreal (f_nn x)
      simp only [ContinuousMap.toFun_eq_coe, BoundedContinuousFunction.coe_toContinuousMap] at aux
      rw [aux]
      congr
      exact (Real.norm_of_nonneg (f_nn x)).symm

/-- One implication of the portmanteau theorem:
If for all open sets G we have the liminf condition `Î¼(G) â‰¤ liminf Î¼sâ‚™(G)`, then the measures
Î¼sâ‚™ converge weakly to the measure Î¼.
Superseded by `tendsto_of_forall_isOpen_le_liminf` which works for all countably
generated filters. -/
theorem tendsto_of_forall_isOpen_le_liminf_nat {Î¼ : ProbabilityMeasure Î©}
    {Î¼s : â„• â†’ ProbabilityMeasure Î©}
    (h_opens : âˆ€ G, IsOpen G â†’ Î¼ G â‰¤ atTop.liminf (fun i â†¦ Î¼s i G)) :
    atTop.Tendsto (fun i â†¦ Î¼s i) (ğ“ Î¼) := by
  refine ProbabilityMeasure.tendsto_iff_forall_integral_tendsto.mpr ?_
  apply tendsto_integral_of_forall_integral_le_liminf_integral
  intro f f_nn
  apply integral_le_liminf_integral_of_forall_isOpen_measure_le_liminf_measure (f := f) f_nn
  intro G G_open
  specialize h_opens G G_open
  have aux : ENNReal.ofNNReal (liminf (fun i â†¦ Î¼s i G) atTop) =
          liminf (ENNReal.ofNNReal âˆ˜ fun i â†¦ Î¼s i G) atTop := by
    refine Monotone.map_liminf_of_continuousAt (F := atTop) ENNReal.coe_mono (Î¼s Â· G) ?_ ?_ ?_
    Â· exact ENNReal.continuous_coe.continuousAt
    Â· exact IsBoundedUnder.isCoboundedUnder_ge âŸ¨1, by simpâŸ©
    Â· exact âŸ¨0, by simpâŸ©
  have obs := ENNReal.coe_mono h_opens
  simp only [ProbabilityMeasure.ennreal_coeFn_eq_coeFn_toMeasure, aux] at obs
  convert obs
  simp only [Function.comp_apply, ProbabilityMeasure.ennreal_coeFn_eq_coeFn_toMeasure]

/-- One implication of the portmanteau theorem:
If for all open sets G we have the liminf condition `Î¼(G) â‰¤ liminf Î¼sâ‚™(G)`, then the measures
Î¼sâ‚™ converge weakly to the measure Î¼. Formulated here for countably generated filters. -/
theorem tendsto_of_forall_isOpen_le_liminf {Î¹ : Type*} {Î¼ : ProbabilityMeasure Î©}
    {Î¼s : Î¹ â†’ ProbabilityMeasure Î©} {L : Filter Î¹} [L.IsCountablyGenerated]
    (h_opens : âˆ€ G, IsOpen G â†’ Î¼ G â‰¤ L.liminf (fun i â†¦ Î¼s i G)) :
    L.Tendsto (fun i â†¦ Î¼s i) (ğ“ Î¼) := by
  apply Filter.tendsto_of_seq_tendsto (fun u hu â†¦ ?_)
  apply tendsto_of_forall_isOpen_le_liminf_nat (fun G hG â†¦ ?_)
  apply (h_opens G hG).trans
  change _ â‰¤ atTop.liminf ((fun i â†¦ Î¼s i G) âˆ˜ u)
  rw [liminf_comp]
  refine liminf_le_liminf_of_le hu (by isBoundedDefault) ?_
  exact isBoundedUnder_of âŸ¨1, by simpâŸ© |>.isCoboundedUnder_ge

end le_liminf_open_implies_convergence

section convergenceCriterion

open scoped Finset

variable {Î© Î¹ : Type*} [MeasurableSpace Î©]

/-- Given a Ï€-system, if a sequence of measures converges along all elements of the Ï€-system, then
it also converges along finite unions of elements of the Ï€-system. -/
lemma _root_.IsPiSystem.tendsto_measureReal_biUnion
    {S : Set (Set Î©)} (hS : IsPiSystem S) {Î¼ : Î¹ â†’ Measure Î©} {Î½ : Measure Î©} {l : Filter Î¹}
    {t : Finset (Set Î©)} (ht : âˆ€ s âˆˆ t, s âˆˆ S)
    (hmeas : âˆ€ s âˆˆ S, MeasurableSet s)
    (h : âˆ€ s âˆˆ S, Tendsto (fun i â†¦ (Î¼ i).real s) l (ğ“ (Î½.real s)))
    (hÎ½ : âˆ€ s âˆˆ S, Î½ s â‰  âˆ := by finiteness)
    (hÎ¼ : âˆ€ s âˆˆ S, âˆ€ i, Î¼ i s â‰  âˆ := by finiteness) :
    Tendsto (fun i â†¦ (Î¼ i).real (â‹ƒ s âˆˆ t, s)) l (ğ“ (Î½.real (â‹ƒ s âˆˆ t, s))) := by
  /- This statement is not completely obvious, as `â‹ƒ s âˆˆ t, s` does not belong to the Ï€-system `S`.
  However, thanks to the inclusion-exclusion formula one may express its measure in terms of
  measures of elements of `S`, from which the result follows. -/
  have A (i) : (Î¼ i).real (â‹ƒ s âˆˆ t, s) = âˆ‘ u âˆˆ t.powerset with u.Nonempty,
      (-1 : â„) ^ (#u + 1) * (Î¼ i).real (â‹‚ s âˆˆ u, s) :=
    measureReal_biUnion_eq_sum_powerset (fun s hs â†¦ hmeas _ (ht _ hs))
      (fun s hs â†¦ hÎ¼ _ (ht _ hs) i)
  simp_rw [A, measureReal_biUnion_eq_sum_powerset (fun s hs â†¦ hmeas _ (ht _ hs))
    (fun s hs â†¦ hÎ½ _ (ht _ hs))]
  refine tendsto_finset_sum _ (fun u hu â†¦ ?_)
  simp only [Finset.mem_filter, Finset.mem_powerset] at hu
  apply Filter.Tendsto.const_mul
  rcases eq_empty_or_nonempty (â‹‚ s âˆˆ u, s) with h'u | h'u
  Â· simpa [h'u] using tendsto_const_nhds
  apply h
  exact hS.biInter_mem hu.2 (fun s hs â†¦ ht _ (hu.1 hs)) h'u

/-- Given a Ï€-system, if a sequence of probability measures converges along all elements of
the Ï€-system, then it also converges along finite unions of elements of the Ï€-system. -/
lemma _root_.IsPiSystem.tendsto_probabilityMeasure_biUnion
    {S : Set (Set Î©)} (hS : IsPiSystem S) {Î¼ : Î¹ â†’ ProbabilityMeasure Î©} {Î½ : ProbabilityMeasure Î©}
    {l : Filter Î¹} {t : Finset (Set Î©)} (ht : âˆ€ s âˆˆ t, s âˆˆ S) (hmeas : âˆ€ s âˆˆ S, MeasurableSet s)
    (h : âˆ€ s âˆˆ S, Tendsto (fun i â†¦ Î¼ i s) l (ğ“ (Î½ s))) :
    Tendsto (fun i â†¦ Î¼ i (â‹ƒ s âˆˆ t, s)) l (ğ“ (Î½ (â‹ƒ s âˆˆ t, s))) := by
  have : Tendsto (fun i â†¦ (Î¼ i : Measure Î©).real (â‹ƒ s âˆˆ t, s)) l
      (ğ“ ((Î½ : Measure Î©).real (â‹ƒ s âˆˆ t, s))) := by
    apply hS.tendsto_measureReal_biUnion ht hmeas
    simpa using h
  simpa using this

/-- Consider a set of sets `S` containing arbitrarily small neighborhoods of any point, and a
probability measure. Then any open set can be approximated arbitrarily well in measure from inside
by a finite union of elements of `S`.

This is a technical lemma for `IsPiSystem.tendsto_probabilityMeasure_of_tendsto_of_mem`, which is
why it is formulated for a `ProbabilityMeasure`. If needed, this could be generalized to finite
measures or to general measures.
-/
lemma ProbabilityMeasure.exists_lt_measure_biUnion_of_isOpen
    [TopologicalSpace Î©] [SecondCountableTopology Î©]
    {S : Set (Set Î©)} (Î½ : ProbabilityMeasure Î©)
    (h : âˆ€ (u : Set Î©), IsOpen u â†’ âˆ€ x âˆˆ u, âˆƒ s âˆˆ S, s âˆˆ ğ“ x âˆ§ s âŠ† u)
    {G : Set Î©} (hG : IsOpen G) {r : â„â‰¥0} (hr : r < Î½ G) :
    âˆƒ T : Finset (Set Î©), (âˆ€ t âˆˆ T, t âˆˆ S) âˆ§ (r < Î½ (â‹ƒ t âˆˆ T, t)) âˆ§ (â‹ƒ t âˆˆ T, t) âŠ† G := by
  classical
  obtain âŸ¨T, TS, T_count, hTâŸ© : âˆƒ T : Set (Set Î©), T âŠ† S âˆ§ T.Countable âˆ§ â‹ƒ t âˆˆ T, t = G := by
    have : âˆ€ (x : G), âˆƒ s âˆˆ S, s âˆˆ ğ“ (x : Î©) âˆ§ s âŠ† G := fun x â†¦ h G hG x x.2
    choose! s hsS hs_nhds hsG using this
    rcases TopologicalSpace.isOpen_iUnion_countable (fun i â†¦ interior (s i))
      (fun i â†¦ isOpen_interior) with âŸ¨Tâ‚€, Tâ‚€_count, hTâ‚€âŸ©
    refine âŸ¨s '' Tâ‚€, by grind, Tâ‚€_count.image s, ?_âŸ©
    refine Subset.antisymm (by simp; grind) ?_
    have : G âŠ† â‹ƒ i, interior (s i) := by
      intro y hy
      simpa using âŸ¨y, hy, mem_interior_iff_mem_nhds.2 (hs_nhds âŸ¨y, hyâŸ©)âŸ©
    apply this.trans
    rw [â† hTâ‚€, biUnion_image]
    exact iUnionâ‚‚_mono fun i j â†¦ interior_subset
  have : T.Nonempty := by
    contrapose! hr
    simp [â† hT, hr]
  rcases T_count.exists_eq_range this with âŸ¨f, hfâŸ©
  have G_eq : G = â‹ƒ n, f n := by simp [â† hT, hf]
  have : Tendsto (fun i â†¦ Î½ (Accumulate f i)) atTop (ğ“ (Î½ (â‹ƒ i, f i))) :=
    (ENNReal.tendsto_toNNReal_iff (by simp) (by simp)).2 tendsto_measure_iUnion_accumulate
  rw [â† G_eq] at this
  rcases ((tendsto_order.1 this).1 r hr).exists with âŸ¨n, hnâŸ©
  refine âŸ¨(Finset.range (n + 1)).image f, by simp; grind, ?_, ?_âŸ©
  Â· convert hn
    simp [accumulate_def]
    grind
  Â· simpa [G_eq] using fun i _ â†¦ subset_iUnion f i

/-- Assume that, applied to all the elements of a Ï€-system, a sequence of probability measures
converges to a limiting probability measure. Assume also that the Ï€-system contains arbitrarily
small neighborhoods of any point. Then the sequence of probability measures converges for the
weak topology. -/
lemma _root_.IsPiSystem.tendsto_probabilityMeasure_of_tendsto_of_mem
    [TopologicalSpace Î©] [SecondCountableTopology Î©] [OpensMeasurableSpace Î©]
    {S : Set (Set Î©)} (hS : IsPiSystem S) {Î¼ : Î¹ â†’ ProbabilityMeasure Î©} {Î½ : ProbabilityMeasure Î©}
    {l : Filter Î¹} [l.IsCountablyGenerated]
    (hmeas : âˆ€ s âˆˆ S, MeasurableSet s)
    (h : âˆ€ (u : Set Î©), IsOpen u â†’ âˆ€ x âˆˆ u, âˆƒ s âˆˆ S, s âˆˆ ğ“ x âˆ§ s âŠ† u)
    (h' : âˆ€ s âˆˆ S, Tendsto (fun i â†¦ Î¼ i s) l (ğ“ (Î½ s))) :
    Tendsto Î¼ l (ğ“ Î½) := by
  /- We apply the portmanteau theorem: it suffices to show that, given an open set `G`
  and `r < Î½ G`, then for large `i` one has `r < Î¼áµ¢ G`. For this, we approximate `G` from inside by
  a finite union `G'` of elements of `S`, still with measure `> r`, by Lemma
  `ProbabilityMeasure.exists_lt_measure_biUnion_of_isOpen`. If we have `Î¼áµ¢ G' â†’ Î½ G'`,
  then we deduce `r < Î¼áµ¢ G'` for large `i`, and therefore `r < Î¼áµ¢ G`.

  Our assumption does not give directly `Î¼áµ¢ G' â†’ Î½ G'`, as `G'` does not belong to the Ï€-system `S`.
  However, the inclusion-exclusion formula makes it possible to express `Î¼áµ¢ G'` and `Î½ G'` in terms
  of the measures of intersections of elements of `S`, for which we have the convergence. It follows
  that `Î¼áµ¢ G' â†’ Î½ G'` holds, concluding the proof. This second step is already formalized in the
  lemma `IsPiSystem.tendsto_probabilityMeasure_biUnion`. -/
  rcases l.eq_or_neBot with rfl | hl
  Â· simp
  apply tendsto_of_forall_isOpen_le_liminf
  intro G hG
  refine (le_liminf_iff (isCoboundedUnder_ge_of_le (x := 1) l (by simp)) (by isBoundedDefault)).2
    (fun r hr â†¦ ?_)
  obtain âŸ¨T, TS, T_meas, TGâŸ© :
      âˆƒ T : Finset (Set Î©), (âˆ€ t âˆˆ T, t âˆˆ S) âˆ§ (r < Î½ (â‹ƒ t âˆˆ T, t)) âˆ§ (â‹ƒ t âˆˆ T, t) âŠ† G :=
    Î½.exists_lt_measure_biUnion_of_isOpen h hG hr
  have : Tendsto (fun i â†¦ Î¼ i (â‹ƒ t âˆˆ T, t)) l (ğ“ (Î½ (â‹ƒ t âˆˆ T, t))) :=
    hS.tendsto_probabilityMeasure_biUnion TS hmeas h'
  filter_upwards [(tendsto_order.1 this).1 r T_meas] with i hi
  exact hi.trans_le <| ProbabilityMeasure.apply_mono _ TG

end convergenceCriterion

end MeasureTheory --namespace
