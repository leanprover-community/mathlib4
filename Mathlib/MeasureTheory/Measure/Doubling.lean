/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Analysis.SpecialFunctions.Log.Base
import Mathlib.MeasureTheory.Measure.MeasureSpaceDef

#align_import measure_theory.measure.doubling from "leanprover-community/mathlib"@"5f6e827d81dfbeb6151d7016586ceeb0099b9655"

/-!
# Uniformly locally doubling measures

A uniformly locally doubling measure `Œº` on a metric space is a measure for which there exists a
constant `C` such that for all sufficiently small radii `Œµ`, and for any centre, the measure of a
ball of radius `2 * Œµ` is bounded by `C` times the measure of the concentric ball of radius `Œµ`.

This file records basic facts about uniformly locally doubling measures.

## Main definitions

  * `IsUnifLocDoublingMeasure`: the definition of a uniformly locally doubling measure (as a
  typeclass).
  * `IsUnifLocDoublingMeasure.doublingConstant`: a function yielding the doubling constant `C`
  appearing in the definition of a uniformly locally doubling measure.
-/

noncomputable section

open Set Filter Metric MeasureTheory TopologicalSpace ENNReal NNReal Topology

/-- A measure `Œº` is said to be a uniformly locally doubling measure if there exists a constant `C`
such that for all sufficiently small radii `Œµ`, and for any centre, the measure of a ball of radius
`2 * Œµ` is bounded by `C` times the measure of the concentric ball of radius `Œµ`.

Note: it is important that this definition makes a demand only for sufficiently small `Œµ`. For
example we want hyperbolic space to carry the instance `IsUnifLocDoublingMeasure volume` but
volumes grow exponentially in hyperbolic space. To be really explicit, consider the hyperbolic plane
of curvature -1, the area of a disc of radius `Œµ` is `A(Œµ) = 2œÄ(cosh(Œµ) - 1)` so
`A(2Œµ)/A(Œµ) ~ exp(Œµ)`. -/
class IsUnifLocDoublingMeasure {Œ± : Type*} [MetricSpace Œ±] [MeasurableSpace Œ±]
  (Œº : Measure Œ±) : Prop where
  exists_measure_closedBall_le_mul'' :
    ‚àÉ C : ‚Ñù‚â•0, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (2 * Œµ)) ‚â§ C * Œº (closedBall x Œµ)
#align is_unif_loc_doubling_measure IsUnifLocDoublingMeasure

namespace IsUnifLocDoublingMeasure

variable {Œ± : Type*} [MetricSpace Œ±] [MeasurableSpace Œ±] (Œº : Measure Œ±)
  [IsUnifLocDoublingMeasure Œº]

-- Porting note: added for missing infer kinds
theorem exists_measure_closedBall_le_mul :
    ‚àÉ C : ‚Ñù‚â•0, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (2 * Œµ)) ‚â§ C * Œº (closedBall x Œµ) :=
  exists_measure_closedBall_le_mul''

/-- A doubling constant for a uniformly locally doubling measure.

See also `IsUnifLocDoublingMeasure.scalingConstantOf`. -/
def doublingConstant : ‚Ñù‚â•0 :=
  Classical.choose <| exists_measure_closedBall_le_mul Œº
#align is_unif_loc_doubling_measure.doubling_constant IsUnifLocDoublingMeasure.doublingConstant

theorem exists_measure_closedBall_le_mul' :
    ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (2 * Œµ)) ‚â§ doublingConstant Œº * Œº (closedBall x Œµ) :=
  Classical.choose_spec <| exists_measure_closedBall_le_mul Œº
#align is_unif_loc_doubling_measure.exists_measure_closed_ball_le_mul' IsUnifLocDoublingMeasure.exists_measure_closedBall_le_mul'

theorem exists_eventually_forall_measure_closedBall_le_mul (K : ‚Ñù) :
    ‚àÉ C : ‚Ñù‚â•0,
      ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ (x t) (_ : t ‚â§ K), Œº (closedBall x (t * Œµ)) ‚â§ C * Œº (closedBall x Œµ) := by
  let C := doublingConstant Œº
  have hŒº :
    ‚àÄ n : ‚Ñï, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x,
      Œº (closedBall x ((2 : ‚Ñù) ^ n * Œµ)) ‚â§ ‚Üë(C ^ n) * Œº (closedBall x Œµ) := by
    intro n
    induction' n with n ih
    ¬∑ simp
    replace ih := eventually_nhdsWithin_pos_mul_left (two_pos : 0 < (2 : ‚Ñù)) ih
    refine' (ih.and (exists_measure_closedBall_le_mul' Œº)).mono fun Œµ hŒµ x => _
    calc
      Œº (closedBall x ((2 : ‚Ñù) ^ (n + 1) * Œµ)) = Œº (closedBall x ((2 : ‚Ñù) ^ n * (2 * Œµ))) := by
        rw [pow_succ', mul_assoc]
      _ ‚â§ ‚Üë(C ^ n) * Œº (closedBall x (2 * Œµ)) := (hŒµ.1 x)
      _ ‚â§ ‚Üë(C ^ n) * (C * Œº (closedBall x Œµ)) := by gcongr; exact hŒµ.2 x
      _ = ‚Üë(C ^ (n + 1)) * Œº (closedBall x Œµ) := by rw [‚Üê mul_assoc, pow_succ', ENNReal.coe_mul]
  rcases lt_or_le K 1 with (hK | hK)
  ¬∑ refine' ‚ü®1, _‚ü©
    simp only [ENNReal.coe_one, one_mul]
    exact
      eventually_mem_nhdsWithin.mono fun Œµ hŒµ x t ht =>
        measure_mono <| closedBall_subset_closedBall (by nlinarith [mem_Ioi.mp hŒµ])
  ¬∑ refine'
      ‚ü®C ^ ‚åàReal.logb 2 K‚åâ‚Çä,
        ((hŒº ‚åàReal.logb 2 K‚åâ‚Çä).and eventually_mem_nhdsWithin).mono fun Œµ hŒµ x t ht =>
          le_trans (measure_mono <| closedBall_subset_closedBall _) (hŒµ.1 x)‚ü©
    refine' mul_le_mul_of_nonneg_right (ht.trans _) (mem_Ioi.mp hŒµ.2).le
    conv_lhs => rw [‚Üê Real.rpow_logb two_pos (by norm_num) (by linarith : 0 < K)]
    rw [‚Üê Real.rpow_nat_cast]
    exact Real.rpow_le_rpow_of_exponent_le one_le_two (Nat.le_ceil (Real.logb 2 K))
#align is_unif_loc_doubling_measure.exists_eventually_forall_measure_closed_ball_le_mul IsUnifLocDoublingMeasure.exists_eventually_forall_measure_closedBall_le_mul

/-- A variant of `IsUnifLocDoublingMeasure.doublingConstant` which allows for scaling the
radius by values other than `2`. -/
def scalingConstantOf (K : ‚Ñù) : ‚Ñù‚â•0 :=
  max (Classical.choose <| exists_eventually_forall_measure_closedBall_le_mul Œº K) 1
#align is_unif_loc_doubling_measure.scaling_constant_of IsUnifLocDoublingMeasure.scalingConstantOf

@[simp]
theorem one_le_scalingConstantOf (K : ‚Ñù) : 1 ‚â§ scalingConstantOf Œº K :=
  le_max_of_le_right <| le_refl 1
#align is_unif_loc_doubling_measure.one_le_scaling_constant_of IsUnifLocDoublingMeasure.one_le_scalingConstantOf

theorem eventually_measure_mul_le_scalingConstantOf_mul (K : ‚Ñù) :
    ‚àÉ R : ‚Ñù,
      0 < R ‚àß
        ‚àÄ (x t r) (_ : t ‚àà Ioc 0 K) (_ : r ‚â§ R),
          Œº (closedBall x (t * r)) ‚â§ scalingConstantOf Œº K * Œº (closedBall x r) := by
  have h := Classical.choose_spec (exists_eventually_forall_measure_closedBall_le_mul Œº K)
  rcases mem_nhdsWithin_Ioi_iff_exists_Ioc_subset.1 h with ‚ü®R, Rpos, hR‚ü©
  refine' ‚ü®R, Rpos, fun x t r ht hr => _‚ü©
  rcases lt_trichotomy r 0 with (rneg | rfl | rpos)
  ¬∑ have : t * r < 0 := mul_neg_of_pos_of_neg ht.1 rneg
    simp only [closedBall_eq_empty.2 this, measure_empty, zero_le']
  ¬∑ simp only [mul_zero, closedBall_zero]
    refine' le_mul_of_one_le_of_le _ le_rfl
    apply ENNReal.one_le_coe_iff.2 (le_max_right _ _)
  ¬∑ apply (hR ‚ü®rpos, hr‚ü© x t ht.2).trans _
    exact mul_le_mul_right' (ENNReal.coe_le_coe.2 (le_max_left _ _)) _
#align is_unif_loc_doubling_measure.eventually_measure_mul_le_scaling_constant_of_mul IsUnifLocDoublingMeasure.eventually_measure_mul_le_scalingConstantOf_mul

theorem eventually_measure_le_scaling_constant_mul (K : ‚Ñù) :
    ‚àÄ·∂† r in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (K * r)) ‚â§ scalingConstantOf Œº K * Œº (closedBall x r) := by
  filter_upwards [Classical.choose_spec
      (exists_eventually_forall_measure_closedBall_le_mul Œº K)] with r hr x
  exact (hr x K le_rfl).trans (mul_le_mul_right' (ENNReal.coe_le_coe.2 (le_max_left _ _)) _)
#align is_unif_loc_doubling_measure.eventually_measure_le_scaling_constant_mul IsUnifLocDoublingMeasure.eventually_measure_le_scaling_constant_mul

theorem eventually_measure_le_scaling_constant_mul' (K : ‚Ñù) (hK : 0 < K) :
    ‚àÄ·∂† r in ùìù[>] 0, ‚àÄ x,
      Œº (closedBall x r) ‚â§ scalingConstantOf Œº K‚Åª¬π * Œº (closedBall x (K * r)) := by
  convert eventually_nhdsWithin_pos_mul_left hK (eventually_measure_le_scaling_constant_mul Œº K‚Åª¬π)
  simp [inv_mul_cancel_left‚ÇÄ hK.ne']
#align is_unif_loc_doubling_measure.eventually_measure_le_scaling_constant_mul' IsUnifLocDoublingMeasure.eventually_measure_le_scaling_constant_mul'

/-- A scale below which the doubling measure `Œº` satisfies good rescaling properties when one
multiplies the radius of balls by at most `K`, as stated
in `IsUnifLocDoublingMeasure.measure_mul_le_scalingConstantOf_mul`. -/
def scalingScaleOf (K : ‚Ñù) : ‚Ñù :=
  (eventually_measure_mul_le_scalingConstantOf_mul Œº K).choose
#align is_unif_loc_doubling_measure.scaling_scale_of IsUnifLocDoublingMeasure.scalingScaleOf

theorem scalingScaleOf_pos (K : ‚Ñù) : 0 < scalingScaleOf Œº K :=
  (eventually_measure_mul_le_scalingConstantOf_mul Œº K).choose_spec.1
#align is_unif_loc_doubling_measure.scaling_scale_of_pos IsUnifLocDoublingMeasure.scalingScaleOf_pos

theorem measure_mul_le_scalingConstantOf_mul {K : ‚Ñù} {x : Œ±} {t r : ‚Ñù} (ht : t ‚àà Ioc 0 K)
    (hr : r ‚â§ scalingScaleOf Œº K) :
    Œº (closedBall x (t * r)) ‚â§ scalingConstantOf Œº K * Œº (closedBall x r) :=
  (eventually_measure_mul_le_scalingConstantOf_mul Œº K).choose_spec.2 x t r ht hr
#align is_unif_loc_doubling_measure.measure_mul_le_scaling_constant_of_mul IsUnifLocDoublingMeasure.measure_mul_le_scalingConstantOf_mul

end IsUnifLocDoublingMeasure
