/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.MeasureTheory.Measure.Tight
import Mathlib.Order.CompletePartialOrder

/-!
# Tight sets of measures in normed spaces

Criteria for tightness of sets of measures in normed and inner product spaces.

## Main statements

* `isTightMeasureSet_iff_tendsto_measure_norm_gt`: in a proper normed group, a set of measures `S`
  is tight if and only if the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}` tends to `0` at infinity.
* `isTightMeasureSet_iff_inner_tendsto`: in a finite-dimensional inner product space,
  a set of measures `S` is tight if and only if the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < |âŸªy, xâŸ«|}`
  tends to `0` at infinity for all `y`.

-/

open Filter

open scoped Topology ENNReal InnerProductSpace

namespace MeasureTheory

variable {E : Type*} {mE : MeasurableSpace E} {S : Set (Measure E)}

section PseudoMetricSpace

variable [PseudoMetricSpace E]

lemma tendsto_measure_compl_closedBall_of_isTightMeasureSet (hS : IsTightMeasureSet S) (x : E) :
    Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ (Metric.closedBall x r)á¶œ) atTop (ð“ 0) := by
  suffices Tendsto ((â¨† Î¼ âˆˆ S, Î¼) âˆ˜ (fun r â†¦ (Metric.closedBall x r)á¶œ)) atTop (ð“ 0) by
    convert this with r
    simp
  refine hS.comp <| .mono_right ?_ <| monotone_smallSets Metric.cobounded_le_cocompact
  exact (Metric.hasAntitoneBasis_cobounded_compl_closedBall _).tendsto_smallSets

lemma isTightMeasureSet_of_tendsto_measure_compl_closedBall [ProperSpace E] {x : E}
    (h : Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ (Metric.closedBall x r)á¶œ) atTop (ð“ 0)) :
    IsTightMeasureSet S := by
  refine IsTightMeasureSet_iff_exists_isCompact_measure_compl_le.mpr fun Îµ hÎµ â†¦ ?_
  rw [ENNReal.tendsto_atTop_zero] at h
  obtain âŸ¨r, hâŸ© := h Îµ hÎµ
  exact âŸ¨Metric.closedBall x r, isCompact_closedBall x r, by simpa using h r le_rflâŸ©

/-- In a proper pseudo-metric space, a set of measures `S` is tight if and only if
the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ (Metric.closedBall x r)á¶œ` tends to `0` at infinity. -/
lemma isTightMeasureSet_iff_tendsto_measure_compl_closedBall [ProperSpace E] (x : E) :
    IsTightMeasureSet S â†” Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ (Metric.closedBall x r)á¶œ) atTop (ð“ 0) :=
  âŸ¨fun hS â†¦ tendsto_measure_compl_closedBall_of_isTightMeasureSet hS x,
    isTightMeasureSet_of_tendsto_measure_compl_closedBallâŸ©

end PseudoMetricSpace

section NormedAddCommGroup

variable [NormedAddCommGroup E]

lemma tendsto_measure_norm_gt_of_isTightMeasureSet (hS : IsTightMeasureSet S) :
    Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}) atTop (ð“ 0) := by
  have h := tendsto_measure_compl_closedBall_of_isTightMeasureSet hS 0
  convert h using 6 with r
  ext
  simp

lemma isTightMeasureSet_of_tendsto_measure_norm_gt [ProperSpace E]
    (h : Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}) atTop (ð“ 0)) :
    IsTightMeasureSet S := by
  refine isTightMeasureSet_of_tendsto_measure_compl_closedBall (x := 0) ?_
  convert h using 6 with r
  ext
  simp

/-- In a proper normed group, a set of measures `S` is tight if and only if
the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}` tends to `0` at infinity. -/
lemma isTightMeasureSet_iff_tendsto_measure_norm_gt [ProperSpace E] :
    IsTightMeasureSet S â†” Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}) atTop (ð“ 0) :=
  âŸ¨tendsto_measure_norm_gt_of_isTightMeasureSet, isTightMeasureSet_of_tendsto_measure_norm_gtâŸ©

section InnerProductSpace

variable {ð•œ Î¹ : Type*} [RCLike ð•œ] [Fintype Î¹] [InnerProductSpace ð•œ E] [FiniteDimensional ð•œ E]

lemma isTightMeasureSet_of_forall_basis_tendsto (b : OrthonormalBasis Î¹ ð•œ E)
    (h : âˆ€ i, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–âŸªb i, xâŸ«_ð•œâ€–}) atTop (ð“ 0)) :
    IsTightMeasureSet S := by
  rcases subsingleton_or_nontrivial E with hE | hE
  Â· simp only [IsTightMeasureSet, cocompact_eq_bot, smallSets_bot]
    convert tendsto_pure_nhds (a := âˆ…) _
    simp
  have h_rank : (0 : â„) < Fintype.card Î¹ := by
    simpa [â† Module.finrank_eq_card_basis b.toBasis, Module.finrank_pos_iff]
  have : Nonempty Î¹ := by simpa [Fintype.card_pos_iff] using h_rank
  have : ProperSpace E := FiniteDimensional.proper ð•œ E
  refine isTightMeasureSet_of_tendsto_measure_norm_gt ?_
  have h_le : (fun r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–})
      â‰¤ fun r â†¦ âˆ‘ i, â¨† Î¼ âˆˆ S, Î¼ {x | r / âˆš(Fintype.card Î¹) < â€–âŸªb i, xâŸ«_ð•œâ€–} := by
    intro r
    calc â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}
    _ â‰¤ â¨† Î¼ âˆˆ S, Î¼ (â‹ƒ i, {x : E | r / âˆš(Fintype.card Î¹) < â€–âŸªb i, xâŸ«_ð•œâ€–}) := by
      gcongr with Î¼ hÎ¼S
      intro x hx
      simp only [Set.mem_setOf_eq, Set.mem_iUnion] at hx âŠ¢
      have hx' : r < âˆš(Fintype.card Î¹) * â¨† i, â€–âŸªb i, xâŸ«_ð•œâ€– :=
        hx.trans_le (b.norm_le_card_mul_iSup_norm_inner x)
      rw [â† div_lt_iffâ‚€' (by positivity)] at hx'
      by_contra! h_le
      exact lt_irrefl (r / âˆš(Fintype.card Î¹)) (hx'.trans_le (ciSup_le h_le))
    _ â‰¤ â¨† Î¼ âˆˆ S, âˆ‘ i, Î¼ {x : E | r / âˆš(Fintype.card Î¹) < â€–âŸªb i, xâŸ«_ð•œâ€–} := by
      gcongr with Î¼ hÎ¼S
      exact measure_iUnion_fintype_le Î¼ _
    _ â‰¤ âˆ‘ i, â¨† Î¼ âˆˆ S, Î¼ {x | r / âˆš(Fintype.card Î¹) < â€–âŸªb i, xâŸ«_ð•œâ€–} := by
      refine iSup_le fun Î¼ â†¦ (iSup_le fun hÎ¼S â†¦ ?_)
      gcongr with i
      exact le_biSup (fun Î¼ â†¦ Î¼ {x | r / âˆš(Fintype.card Î¹) < â€–âŸªb i, xâŸ«_ð•œâ€–}) hÎ¼S
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds ?_ (fun _ â†¦ zero_le') h_le
  rw [â† Finset.sum_const_zero]
  refine tendsto_finset_sum Finset.univ fun i _ â†¦ (h i).comp ?_
  exact tendsto_id.atTop_div_const (by positivity)

lemma isTightMeasureSet_of_inner_tendsto
    (h : âˆ€ y, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–âŸªy, xâŸ«_ð•œâ€–}) atTop (ð“ 0)) :
    IsTightMeasureSet S :=
  isTightMeasureSet_of_forall_basis_tendsto (stdOrthonormalBasis ð•œ E)
    fun i â†¦ h (stdOrthonormalBasis ð•œ E i)

variable (ð•œ) in
/-- In a finite-dimensional inner product space,
a set of measures `S` is tight if and only if the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < |âŸªy, xâŸ«|}`
tends to `0` at infinity for all `y`. -/
lemma isTightMeasureSet_iff_inner_tendsto :
    IsTightMeasureSet S
      â†” âˆ€ y, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–âŸªy, xâŸ«_ð•œâ€–}) atTop (ð“ 0) := by
  refine âŸ¨fun h y â†¦ ?_, isTightMeasureSet_of_inner_tendstoâŸ©
  have : ProperSpace E := FiniteDimensional.proper ð•œ E
  rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h
  by_cases hy : y = 0
  Â· simp only [hy, inner_zero_left, abs_zero]
    refine (tendsto_congr' ?_).mpr tendsto_const_nhds
    filter_upwards [eventually_ge_atTop 0] with r hr
    simp [not_lt.mpr hr]
  have h' : Tendsto (fun r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r * â€–yâ€–â»Â¹ < â€–xâ€–}) atTop (ð“ 0) :=
    h.comp <| (tendsto_mul_const_atTop_of_pos (by positivity)).mpr tendsto_id
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h' (fun _ â†¦ zero_le') ?_
  intro r
  have h_le (Î¼ : Measure E) : Î¼ {x | r < â€–âŸªy, xâŸ«_ð•œâ€–} â‰¤ Î¼ {x | r * â€–yâ€–â»Â¹ < â€–xâ€–} := by
    refine measure_mono fun x hx â†¦ ?_
    simp only [Set.mem_setOf_eq] at hx âŠ¢
    rw [mul_inv_lt_iffâ‚€]
    Â· rw [mul_comm]
      exact hx.trans_le (norm_inner_le_norm y x)
    Â· positivity
  refine iSupâ‚‚_le_iff.mpr fun Î¼ hÎ¼S â†¦ ?_
  exact le_iSup_of_le (i := Î¼) <| by simp [hÎ¼S, h_le]

end InnerProductSpace

end NormedAddCommGroup

end MeasureTheory
