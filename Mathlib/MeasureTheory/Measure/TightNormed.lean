/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.Normed.Module.FiniteDimension
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.MeasureTheory.Measure.Tight
import Mathlib.Order.CompletePartialOrder
import Mathlib.MeasureTheory.Measure.RegularityCompacts
import Mathlib.Data.Fintype.Order
import Mathlib.Analysis.InnerProductSpace.PiL2

/-!
# Tight sets of measures in normed spaces

Criteria for tightness of sets of measures in normed and inner product spaces.

## Main statements

* `isTightMeasureSet_iff_tendsto_measure_norm_gt`: in a finite dimensional real normed space,
  a set of measures `S` is tight if and only if the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}`
  tends to `0` at infinity.

-/

open Filter

open scoped Topology ENNReal RealInnerProductSpace

namespace MeasureTheory

variable {E : Type*} [NormedAddCommGroup E] {mE : MeasurableSpace E} {S : Set (Measure E)}

lemma isTightMeasureSet_singleton {Î± : Type*} {mÎ± : MeasurableSpace Î±}
  [PseudoEMetricSpace Î±] [CompleteSpace Î±] [SecondCountableTopology Î±] [BorelSpace Î±]
  {Î¼ : Measure Î±} [IsFiniteMeasure Î¼] :
    IsTightMeasureSet {Î¼} :=
  isTightMeasureSet_singleton_of_innerRegularWRT
    (innerRegular_isCompact_isClosed_measurableSet_of_finite _)

lemma tendsto_iSup_of_tendsto_limsup {u : â„• â†’ â„ â†’ â„â‰¥0âˆž}
    (h_all : âˆ€ n, Tendsto (u n) atTop (ð“ 0))
    (h_tendsto : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ u n r) atTop) atTop (ð“ 0))
    (h_anti : âˆ€ n, Antitone (u n)) :
    Tendsto (fun r : â„ â†¦ â¨† n, u n r) atTop (ð“ 0) := by
  simp_rw [ENNReal.tendsto_atTop_zero] at h_tendsto h_all âŠ¢
  intro Îµ hÎµ
  by_cases hÎµ_top : Îµ = âˆž
  Â· refine âŸ¨0, fun _ _ â†¦ by simp [hÎµ_top]âŸ©
  simp only [gt_iff_lt, ge_iff_le] at h_tendsto h_all hÎµ
  obtain âŸ¨r, hâŸ© := h_tendsto (Îµ / 2) (ENNReal.half_pos hÎµ.ne')
  have h' x (hx : r â‰¤ x) y (hy : Îµ / 2 < y) : âˆ€á¶  n in atTop, u n x < y := by
    specialize h x hx
    rw [limsup_le_iff] at h
    exact h y hy
  replace h' : âˆ€ x, r â‰¤ x â†’ âˆ€á¶  n in atTop, u n x < Îµ :=
    fun x hx â†¦ h' x hx Îµ (ENNReal.half_lt_self hÎµ.ne' hÎµ_top)
  simp only [eventually_atTop, ge_iff_le] at h'
  obtain âŸ¨N, hâŸ© := h' r le_rfl
  replace h_all : âˆ€ Îµ > 0, âˆ€ n, âˆƒ N, âˆ€ n_1 â‰¥ N, u n n_1 â‰¤ Îµ := fun Îµ hÎµ n â†¦ h_all n Îµ hÎµ
  choose rs hrs using h_all Îµ hÎµ
  refine âŸ¨r âŠ” â¨† n : Finset.range N, rs n, fun v hv â†¦ ?_âŸ©
  simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff, forall_apply_eq_imp_iff]
  intro n
  by_cases hn : n < N
  Â· refine hrs n v ?_
    calc rs n
    _ = rs (âŸ¨n, by simp [hn]âŸ© : Finset.range N) := rfl
    _ â‰¤ â¨† n : Finset.range N, rs n := by
      refine le_ciSup (f := fun (x : Finset.range N) â†¦ rs x) ?_ (âŸ¨n, by simp [hn]âŸ© : Finset.range N)
      exact Finite.bddAbove_range _
    _ â‰¤ r âŠ” â¨† n : Finset.range N, rs n := le_max_right _ _
    _ â‰¤ v := hv
  Â· have hn_le : N â‰¤ n := not_lt.mp hn
    specialize h n hn_le
    refine (h_anti n ?_).trans h.le
    calc r
    _ â‰¤ r âŠ” â¨† n : Finset.range N, rs n := le_max_left _ _
    _ â‰¤ v := hv

lemma iSup_set_seq {E : Type*} {_ : MeasurableSpace E} (Î¼ : â„• â†’ Measure E) {s : Set E} :
    â¨† Î¼' âˆˆ {Î¼ n | n}, Î¼' s = â¨† n, Î¼ n s := by
  apply le_antisymm
  Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff, forall_apply_eq_imp_iff]
    intro n
    exact le_iSup (fun i â†¦ Î¼ i s) n
  Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff]
    intro n
    calc Î¼ n s
    _ â‰¤ â¨† i, â¨† (_ : Î¼ i = Î¼ n), Î¼ i s := le_biSup (fun i â†¦ Î¼ i s) rfl
    _ = â¨† i, â¨† (_ : Î¼ i = Î¼ n), Î¼ n s := by
      convert rfl using 4 with m hm
      rw [hm]
    _ â‰¤ â¨† Î¼', â¨† i, â¨† (_ : Î¼ i = Î¼'), Î¼' s :=
      le_iSup (fun Î¼' â†¦ â¨† i, â¨† (_ : Î¼ i = Î¼'), Î¼' s) (Î¼ n)

section NormedSpace

lemma tendsto_measure_norm_gt_of_isTightMeasureSet (hS : IsTightMeasureSet S) :
    Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}) atTop (ð“ 0) := by
  suffices Tendsto ((â¨† Î¼ âˆˆ S, Î¼) âˆ˜ (fun r â†¦ {x | r < â€–xâ€–})) atTop (ð“ 0) by
    convert this with r
    simp
  refine hS.comp ?_
  simp only [tendsto_smallSets_iff, mem_cocompact, eventually_atTop, ge_iff_le, forall_exists_index,
    and_imp]
  intro s t ht_compact hts
  rcases Set.eq_empty_or_nonempty t with rfl | ht_nonempty
  Â· simp only [Set.compl_empty, Set.univ_subset_iff] at hts
    simp [hts]
  obtain âŸ¨r, h_subsetâŸ© : âˆƒ r, t âŠ† {x | â€–xâ€– â‰¤ r} := by
    obtain âŸ¨xmax, _, hxmaxâŸ© : âˆƒ x âˆˆ t, IsMaxOn (fun x â†¦ â€–xâ€–) t x :=
      ht_compact.exists_isMaxOn (f := fun x : E â†¦ â€–xâ€–) ht_nonempty (by fun_prop)
    exact âŸ¨â€–xmaxâ€–, fun x hxK â†¦ hxmax hxKâŸ©
  refine âŸ¨r, fun u hu â†¦ subset_trans ?_ htsâŸ©
  simp_rw [â† not_le]
  refine Set.compl_subset_compl.mp ?_
  simp only [compl_compl, not_le]
  refine h_subset.trans fun x â†¦ ?_
  simp only [Set.mem_setOf_eq, Set.mem_compl_iff, not_lt]
  exact fun hx â†¦ hx.trans hu

section FiniteDimensional

variable [NormedSpace â„ E] [FiniteDimensional â„ E]

lemma isTightMeasureSet_of_tendsto_measure_norm_gt
    (h : Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}) atTop (ð“ 0)) :
    IsTightMeasureSet S := by
  refine IsTightMeasureSet_iff_exists_isCompact_measure_compl_le.mpr fun Îµ hÎµ â†¦ ?_
  rw [ENNReal.tendsto_atTop_zero] at h
  obtain âŸ¨r, hâŸ© := h Îµ hÎµ
  refine âŸ¨Metric.closedBall 0 r, isCompact_closedBall 0 r, ?_âŸ©
  specialize h r le_rfl
  simp only [iSup_le_iff] at h
  convert h using 4 with Î¼ hÎ¼
  ext
  simp

/-- In a finite dimensional real normed space, a set of measures `S` is tight if and only if
the function `r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}` tends to `0` at infinity. -/
lemma isTightMeasureSet_iff_tendsto_measure_norm_gt :
    IsTightMeasureSet S â†” Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}) atTop (ð“ 0) :=
  âŸ¨tendsto_measure_norm_gt_of_isTightMeasureSet, isTightMeasureSet_of_tendsto_measure_norm_gtâŸ©

lemma isTightMeasureSet_of_tendsto_limsup_measure_norm_gt [BorelSpace E]
    {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)]
    (h : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < â€–xâ€–}) atTop) atTop (ð“ 0)) :
    IsTightMeasureSet {Î¼ n | n} := by
  refine isTightMeasureSet_of_tendsto_measure_norm_gt ?_
  convert tendsto_iSup_of_tendsto_limsup (fun n â†¦ ?_) h fun n u v huv â†¦ ?_ with y
  Â· exact iSup_set_seq Î¼
  Â· have h_tight : IsTightMeasureSet {Î¼ n} :=
      isTightMeasureSet_singleton_of_innerRegularWRT
        (innerRegular_isCompact_isClosed_measurableSet_of_finite (Î¼ n))
    rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h_tight
    simpa using h_tight
  Â· refine measure_mono fun x hx â†¦ ?_
    simp only [Set.mem_setOf_eq] at hx âŠ¢
    exact huv.trans_lt hx

lemma isTightMeasureSet_iff_tendsto_limsup_measure_norm_gt [BorelSpace E]
    {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)] :
    IsTightMeasureSet {Î¼ n | n}
      â†” Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < â€–xâ€–}) atTop) atTop (ð“ 0) := by
  refine âŸ¨fun h â†¦ ?_, isTightMeasureSet_of_tendsto_limsup_measure_norm_gtâŸ©
  have h_sup := tendsto_measure_norm_gt_of_isTightMeasureSet h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h_sup (fun _ â†¦ zero_le') ?_
  intro r
  simp_rw [iSup_set_seq]
  exact limsup_le_iSup

end FiniteDimensional

end NormedSpace

section InnerProductSpace

variable [InnerProductSpace â„ E] [FiniteDimensional â„ E] {Î¹ : Type*} [Fintype Î¹]

lemma norm_le_mul_iSup_abs_inner (b : OrthonormalBasis Î¹ â„ E) (x : E) :
    â€–xâ€– â‰¤ âˆš(Fintype.card Î¹) * â¨† i, |âŸªb i, xâŸ«| := by
  rcases subsingleton_or_nontrivial E with hE | hE
  Â· have : x = 0 := Subsingleton.elim x 0
    simp [this]
  have h_rank : (0 : â„) < Fintype.card Î¹ := by
    simp only [â† Module.finrank_eq_card_basis b.toBasis, Nat.cast_pos, Module.finrank_pos_iff]
    infer_instance
  have : Nonempty Î¹ := by simpa [Fintype.card_pos_iff] using h_rank
  calc â€–xâ€–
  _ = âˆš(âˆ‘ i, âŸªb i, xâŸ« ^ 2) := by
    simp_rw [norm_eq_sqrt_real_inner, â† OrthonormalBasis.sum_inner_mul_inner b x x,
      real_inner_comm _ x, â† pow_two]
  _ = âˆš(âˆ‘ i, |âŸªb i, xâŸ«| ^ 2) := by simp
  _ â‰¤ âˆš(âˆ‘ _ : Î¹, (â¨† j, |âŸªb j, xâŸ«|) ^ 2) := by
    gcongr with i
    exact le_ciSup (f := fun j â†¦ |âŸªb j, xâŸ«|) (by simp) i
  _ = âˆš(Fintype.card Î¹) * â¨† i, |âŸªb i, xâŸ«| := by
    simp only [Finset.sum_const, Finset.card_univ, nsmul_eq_mul, Nat.cast_nonneg, Real.sqrt_mul]
    congr
    rw [Real.sqrt_sq]
    exact le_ciSup_of_le (by simp) (Nonempty.some this) (by positivity)

lemma isTightMeasureSet_of_forall_basis_tendsto (b : OrthonormalBasis Î¹ â„ E)
    (h : âˆ€ i, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < |âŸªb i, xâŸ«|}) atTop (ð“ 0)) :
    IsTightMeasureSet S := by
  rcases subsingleton_or_nontrivial E with hE | hE
  Â· simp only [IsTightMeasureSet, cocompact_eq_bot, smallSets_bot]
    convert tendsto_pure_nhds (a := âˆ…) _
    simp
  have h_rank : (0 : â„) < Fintype.card Î¹ := by
    simp only [â† Module.finrank_eq_card_basis b.toBasis, Nat.cast_pos, Module.finrank_pos_iff]
    infer_instance
  have : Nonempty Î¹ := by simpa [Fintype.card_pos_iff] using h_rank
  refine isTightMeasureSet_of_tendsto_measure_norm_gt ?_
  have h_le : (fun r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–})
      â‰¤ fun r â†¦ âˆ‘ i, â¨† Î¼ âˆˆ S, Î¼ {x | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|} := by
    intro r
    calc â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}
    _ â‰¤ â¨† Î¼ âˆˆ S, Î¼ (â‹ƒ i, {x : E | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|}) := by
      gcongr with Î¼ hÎ¼S
      intro x hx
      simp only [Set.mem_setOf_eq, Set.mem_iUnion] at hx âŠ¢
      have hx' : r < âˆš(Fintype.card Î¹) * â¨† i, |âŸªb i, xâŸ«| :=
        hx.trans_le (norm_le_mul_iSup_abs_inner b x)
      rw [â† div_lt_iffâ‚€' (by positivity)] at hx'
      by_contra! h_le
      exact lt_irrefl (r / âˆš(Fintype.card Î¹)) (hx'.trans_le (ciSup_le h_le))
    _ â‰¤ â¨† Î¼ âˆˆ S, âˆ‘ i, Î¼ {x : E | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|} := by
      gcongr with Î¼ hÎ¼S
      exact measure_iUnion_fintype_le Î¼ _
    _ â‰¤ âˆ‘ i, â¨† Î¼ âˆˆ S, Î¼ {x | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|} := by
      refine iSup_le fun Î¼ â†¦ (iSup_le fun hÎ¼S â†¦ ?_)
      gcongr with i
      exact le_biSup (fun Î¼ â†¦ Î¼ {x | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|}) hÎ¼S
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds ?_ (fun _ â†¦ zero_le') h_le
  have : âˆ‘ i : Î¹, (0 : â„â‰¥0âˆž) = 0 := by simp
  rw [â† this]
  refine tendsto_finset_sum Finset.univ fun i _ â†¦ ?_
  refine (h i).comp ?_
  exact Tendsto.atTop_div_const (by positivity) tendsto_id

lemma isTightMeasureSet_iff_forall_basis_tendsto (b : OrthonormalBasis Î¹ â„ E) :
    IsTightMeasureSet S
      â†” âˆ€ i, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < |âŸªb i, xâŸ«|}) atTop (ð“ 0) := by
  refine âŸ¨fun h i â†¦ ?_, isTightMeasureSet_of_forall_basis_tendsto bâŸ©
  rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h (fun _ â†¦ zero_le') ?_
  intro r
  have h_le (Î¼ : Measure E) : Î¼ {x | r < |âŸªb i, xâŸ«|} â‰¤ Î¼ {x | r < â€–xâ€–} := by
    refine measure_mono fun x hx â†¦ ?_
    simp only [Set.mem_setOf_eq] at hx âŠ¢
    refine hx.trans_le ?_
    refine (abs_real_inner_le_norm _ _).trans ?_
    simp
  simp only [iSup_le_iff]
  intro Î¼ hÎ¼S
  refine le_iSup_of_le (i := Î¼) ?_
  simp only [hÎ¼S, iSup_pos]
  exact h_le Î¼

lemma isTightMeasureSet_of_forall_basis_tendsto_limsup [BorelSpace E]
    {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)] (b : OrthonormalBasis Î¹ â„ E)
    (h : âˆ€ i, Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªb i, xâŸ«|}) atTop) atTop (ð“ 0)) :
    IsTightMeasureSet {Î¼ n | n} := by
  refine isTightMeasureSet_of_forall_basis_tendsto b fun i â†¦ ?_
  convert tendsto_iSup_of_tendsto_limsup (fun n â†¦ ?_) (h i) fun n u v huv â†¦ ?_ with y
  Â· apply le_antisymm
    Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff, forall_apply_eq_imp_iff]
      exact fun n â†¦ le_iSup (fun j â†¦ Î¼ j {x | y < |âŸªb i, xâŸ«|}) n
    Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff]
      intro n
      calc Î¼ n {x | y < |âŸªb i, xâŸ«|}
      _ â‰¤ â¨† j, â¨† (_ : Î¼ j = Î¼ n), Î¼ j {x | y < |âŸªb i, xâŸ«|} :=
          le_biSup (fun j â†¦ Î¼ j {x | y < |âŸªb i, xâŸ«|}) rfl
      _ = â¨† j, â¨† (_ : Î¼ j = Î¼ n), Î¼ n {x | y < |âŸªb i, xâŸ«|} := by
        convert rfl using 4 with m hm
        rw [hm]
      _ â‰¤ â¨† Î¼', â¨† j, â¨† (_ : Î¼ j = Î¼'), Î¼' {x | y < |âŸªb i, xâŸ«|} :=
        le_iSup (fun Î¼' â†¦ â¨† j, â¨† (_ : Î¼ j = Î¼'), Î¼' {x | y < |âŸªb i, xâŸ«|}) (Î¼ n)
  Â· have h_tight : IsTightMeasureSet {(Î¼ n).map (fun x â†¦ âŸªb i, xâŸ«)} :=
      isTightMeasureSet_singleton
    rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h_tight
    have h_map r : (Î¼ n).map (fun x â†¦ inner ((b) i) x) {x | r < |x|}
        = Î¼ n {x | r < |âŸªb i, xâŸ«|} := by
      rw [Measure.map_apply (by fun_prop)]
      Â· simp
      Â· exact MeasurableSet.preimage measurableSet_Ioi (by fun_prop)
    simpa [h_map] using h_tight
  Â· exact measure_mono fun x hx â†¦ huv.trans_lt hx

lemma isTightMeasureSet_iff_forall_basis_tendsto_limsup [BorelSpace E]
    {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)] (b : OrthonormalBasis Î¹ â„ E) :
    IsTightMeasureSet {Î¼ n | n}
      â†” âˆ€ i, Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªb i, xâŸ«|}) atTop) atTop (ð“ 0) := by
  refine âŸ¨fun h i â†¦ ?_, isTightMeasureSet_of_forall_basis_tendsto_limsup bâŸ©
  rw [isTightMeasureSet_iff_forall_basis_tendsto b] at h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds (h i) (fun _ â†¦ zero_le') ?_
  intro r
  simp_rw [iSup_set_seq]
  exact limsup_le_iSup

end InnerProductSpace

end MeasureTheory
