/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Measure.Trim
import Mathlib.MeasureTheory.MeasurableSpace.CountablyGenerated

/-!
# Almost everywhere measurable functions

A function is almost everywhere measurable if it coincides almost everywhere with a measurable
function. This property, called `AEMeasurable f Î¼`, is defined in the file `MeasureSpaceDef`.
We discuss several of its properties that are analogous to properties of measurable functions.
-/

open MeasureTheory MeasureTheory.Measure Filter Set Function ENNReal

variable {Î¹ Î± Î² Î³ Î´ R : Type*} {m0 : MeasurableSpace Î±} [MeasurableSpace Î²] [MeasurableSpace Î³]
  [MeasurableSpace Î´] {f g : Î± â†’ Î²} {Î¼ Î½ : Measure Î±}

section

@[nontriviality, measurability]
theorem Subsingleton.aemeasurable [Subsingleton Î±] : AEMeasurable f Î¼ :=
  Subsingleton.measurable.aemeasurable

@[nontriviality, measurability]
theorem aemeasurable_of_subsingleton_codomain [Subsingleton Î²] : AEMeasurable f Î¼ :=
  (measurable_of_subsingleton_codomain f).aemeasurable

@[simp, fun_prop, measurability]
theorem aemeasurable_zero_measure : AEMeasurable f (0 : Measure Î±) := by
  nontriviality Î±; inhabit Î±
  exact âŸ¨fun _ => f default, measurable_const, rflâŸ©

theorem aemeasurable_id'' (Î¼ : Measure Î±) {m : MeasurableSpace Î±} (hm : m â‰¤ m0) :
    @AEMeasurable Î± Î± m m0 id Î¼ :=
  @Measurable.aemeasurable Î± Î± m0 m id Î¼ (measurable_id'' hm)

lemma aemeasurable_of_map_neZero {Î¼ : Measure Î±}
    {f : Î± â†’ Î²} (h : NeZero (Î¼.map f)) :
    AEMeasurable f Î¼ := by
  by_contra h'
  simp [h'] at h

namespace AEMeasurable

lemma mono_ac (hf : AEMeasurable f Î½) (hÎ¼Î½ : Î¼ â‰ª Î½) : AEMeasurable f Î¼ :=
  âŸ¨hf.mk f, hf.measurable_mk, hÎ¼Î½.ae_le hf.ae_eq_mkâŸ©

theorem mono_measure (h : AEMeasurable f Î¼) (h' : Î½ â‰¤ Î¼) : AEMeasurable f Î½ :=
  mono_ac h h'.absolutelyContinuous

theorem mono_set {s t} (h : s âŠ† t) (ht : AEMeasurable f (Î¼.restrict t)) :
    AEMeasurable f (Î¼.restrict s) :=
  ht.mono_measure (restrict_mono h le_rfl)

@[fun_prop]
protected theorem mono' (h : AEMeasurable f Î¼) (h' : Î½ â‰ª Î¼) : AEMeasurable f Î½ :=
  âŸ¨h.mk f, h.measurable_mk, h' h.ae_eq_mkâŸ©

theorem ae_mem_imp_eq_mk {s} (h : AEMeasurable f (Î¼.restrict s)) :
    âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = h.mk f x :=
  ae_imp_of_ae_restrict h.ae_eq_mk

theorem ae_inf_principal_eq_mk {s} (h : AEMeasurable f (Î¼.restrict s)) : f =á¶ [ae Î¼ âŠ“ ğ“Ÿ s] h.mk f :=
  le_ae_restrict h.ae_eq_mk

@[measurability]
theorem sum_measure [Countable Î¹] {Î¼ : Î¹ â†’ Measure Î±} (h : âˆ€ i, AEMeasurable f (Î¼ i)) :
    AEMeasurable f (sum Î¼) := by
  classical
  nontriviality Î²
  inhabit Î²
  set s : Î¹ â†’ Set Î± := fun i => toMeasurable (Î¼ i) { x | f x â‰  (h i).mk f x }
  have hsÎ¼ : âˆ€ i, Î¼ i (s i) = 0 := by
    intro i
    rw [measure_toMeasurable]
    exact (h i).ae_eq_mk
  have hsm : MeasurableSet (â‹‚ i, s i) :=
    MeasurableSet.iInter fun i => measurableSet_toMeasurable _ _
  have hs : âˆ€ i x, x âˆ‰ s i â†’ f x = (h i).mk f x := by
    intro i x hx
    contrapose! hx
    exact subset_toMeasurable _ _ hx
  set g : Î± â†’ Î² := (â‹‚ i, s i).piecewise (const Î± default) f
  refine âŸ¨g, measurable_of_restrict_of_restrict_compl hsm ?_ ?_, ae_sum_iff.mpr fun i => ?_âŸ©
  Â· rw [restrict_piecewise]
    simp only [s, Set.restrict, const]
    exact measurable_const
  Â· rw [restrict_piecewise_compl, compl_iInter]
    intro t ht
    refine âŸ¨â‹ƒ i, (h i).mk f â»Â¹' t âˆ© (s i)á¶œ, MeasurableSet.iUnion fun i â†¦
      (measurable_mk _ ht).inter (measurableSet_toMeasurable _ _).compl, ?_âŸ©
    ext âŸ¨x, hxâŸ©
    simp only [mem_preimage, mem_iUnion, Subtype.coe_mk, Set.restrict, mem_inter_iff,
      mem_compl_iff] at hx âŠ¢
    constructor
    Â· rintro âŸ¨i, hxt, hxsâŸ©
      rwa [hs _ _ hxs]
    Â· rcases hx with âŸ¨i, hiâŸ©
      rw [hs _ _ hi]
      exact fun h => âŸ¨i, h, hiâŸ©
  Â· refine measure_mono_null (fun x (hx : f x â‰  g x) => ?_) (hsÎ¼ i)
    contrapose! hx
    refine (piecewise_eq_of_notMem _ _ _ ?_).symm
    exact fun h => hx (mem_iInter.1 h i)

@[simp]
theorem _root_.aemeasurable_sum_measure_iff [Countable Î¹] {Î¼ : Î¹ â†’ Measure Î±} :
    AEMeasurable f (sum Î¼) â†” âˆ€ i, AEMeasurable f (Î¼ i) :=
  âŸ¨fun h _ => h.mono_measure (le_sum _ _), sum_measureâŸ©

@[simp]
theorem _root_.aemeasurable_add_measure_iff :
    AEMeasurable f (Î¼ + Î½) â†” AEMeasurable f Î¼ âˆ§ AEMeasurable f Î½ := by
  rw [â† sum_cond, aemeasurable_sum_measure_iff, Bool.forall_bool, and_comm]
  rfl

@[measurability]
theorem add_measure {f : Î± â†’ Î²} (hÎ¼ : AEMeasurable f Î¼) (hÎ½ : AEMeasurable f Î½) :
    AEMeasurable f (Î¼ + Î½) :=
  aemeasurable_add_measure_iff.2 âŸ¨hÎ¼, hÎ½âŸ©

@[measurability]
protected theorem iUnion [Countable Î¹] {s : Î¹ â†’ Set Î±}
    (h : âˆ€ i, AEMeasurable f (Î¼.restrict (s i))) : AEMeasurable f (Î¼.restrict (â‹ƒ i, s i)) :=
  (sum_measure h).mono_measure <| restrict_iUnion_le

@[simp]
theorem _root_.aemeasurable_iUnion_iff [Countable Î¹] {s : Î¹ â†’ Set Î±} :
    AEMeasurable f (Î¼.restrict (â‹ƒ i, s i)) â†” âˆ€ i, AEMeasurable f (Î¼.restrict (s i)) :=
  âŸ¨fun h _ => h.mono_measure <| restrict_mono (subset_iUnion _ _) le_rfl, AEMeasurable.iUnionâŸ©

@[simp]
theorem _root_.aemeasurable_union_iff {s t : Set Î±} :
    AEMeasurable f (Î¼.restrict (s âˆª t)) â†”
      AEMeasurable f (Î¼.restrict s) âˆ§ AEMeasurable f (Î¼.restrict t) := by
  simp only [union_eq_iUnion, aemeasurable_iUnion_iff, Bool.forall_bool, cond, and_comm]

@[measurability]
theorem smul_measure [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]
    (h : AEMeasurable f Î¼) (c : R) : AEMeasurable f (c â€¢ Î¼) :=
  âŸ¨h.mk f, h.measurable_mk, ae_smul_measure h.ae_eq_mk câŸ©

theorem comp_aemeasurable {f : Î± â†’ Î´} {g : Î´ â†’ Î²} (hg : AEMeasurable g (Î¼.map f))
    (hf : AEMeasurable f Î¼) : AEMeasurable (g âˆ˜ f) Î¼ :=
  âŸ¨hg.mk g âˆ˜ hf.mk f, hg.measurable_mk.comp hf.measurable_mk,
    (ae_eq_comp hf hg.ae_eq_mk).trans (hf.ae_eq_mk.fun_comp (mk g hg))âŸ©

@[fun_prop]
theorem comp_aemeasurable' {f : Î± â†’ Î´} {g : Î´ â†’ Î²} (hg : AEMeasurable g (Î¼.map f))
    (hf : AEMeasurable f Î¼) : AEMeasurable (fun x â†¦ g (f x)) Î¼ := comp_aemeasurable hg hf

theorem comp_measurable {f : Î± â†’ Î´} {g : Î´ â†’ Î²} (hg : AEMeasurable g (Î¼.map f))
    (hf : Measurable f) : AEMeasurable (g âˆ˜ f) Î¼ :=
  hg.comp_aemeasurable hf.aemeasurable

@[fun_prop]
theorem comp_quasiMeasurePreserving {Î½ : Measure Î´} {f : Î± â†’ Î´} {g : Î´ â†’ Î²} (hg : AEMeasurable g Î½)
    (hf : QuasiMeasurePreserving f Î¼ Î½) : AEMeasurable (g âˆ˜ f) Î¼ :=
  (hg.mono' hf.absolutelyContinuous).comp_measurable hf.measurable

theorem map_map_of_aemeasurable {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : AEMeasurable g (Measure.map f Î¼))
    (hf : AEMeasurable f Î¼) : (Î¼.map f).map g = Î¼.map (g âˆ˜ f) := by
  ext1 s hs
  rw [map_apply_of_aemeasurable hg hs, map_applyâ‚€ hf (hg.nullMeasurable hs),
    map_apply_of_aemeasurable (hg.comp_aemeasurable hf) hs, preimage_comp]

@[fun_prop, measurability]
protected theorem fst {f : Î± â†’ Î² Ã— Î³} (hf : AEMeasurable f Î¼) :
    AEMeasurable (fun x â†¦ (f x).1) Î¼ :=
  measurable_fst.comp_aemeasurable hf

@[fun_prop, measurability]
protected theorem snd {f : Î± â†’ Î² Ã— Î³} (hf : AEMeasurable f Î¼) :
    AEMeasurable (fun x â†¦ (f x).2) Î¼ :=
  measurable_snd.comp_aemeasurable hf

@[fun_prop, measurability]
theorem prodMk {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) :
    AEMeasurable (fun x => (f x, g x)) Î¼ :=
  âŸ¨fun a => (hf.mk f a, hg.mk g a), hf.measurable_mk.prodMk hg.measurable_mk,
    hf.ae_eq_mk.prodMk hg.ae_eq_mkâŸ©

@[deprecated (since := "2025-03-05")]
alias prod_mk := prodMk

theorem exists_ae_eq_range_subset (H : AEMeasurable f Î¼) {t : Set Î²} (ht : âˆ€áµ x âˆ‚Î¼, f x âˆˆ t)
    (hâ‚€ : t.Nonempty) : âˆƒ g, Measurable g âˆ§ range g âŠ† t âˆ§ f =áµ[Î¼] g := by
  classical
  let s : Set Î± := toMeasurable Î¼ { x | f x = H.mk f x âˆ§ f x âˆˆ t }á¶œ
  let g : Î± â†’ Î² := piecewise s (fun _ => hâ‚€.some) (H.mk f)
  refine âŸ¨g, ?_, ?_, ?_âŸ©
  Â· exact Measurable.piecewise (measurableSet_toMeasurable _ _) measurable_const H.measurable_mk
  Â· rintro _ âŸ¨x, rflâŸ©
    by_cases hx : x âˆˆ s
    Â· simpa [g, hx] using hâ‚€.some_mem
    Â· simp only [g, hx, piecewise_eq_of_notMem, not_false_iff]
      contrapose! hx
      apply subset_toMeasurable
      simp +contextual only [hx, mem_compl_iff, mem_setOf_eq, not_and,
        not_false_iff, imp_true_iff]
  Â· have A : Î¼ (toMeasurable Î¼ { x | f x = H.mk f x âˆ§ f x âˆˆ t }á¶œ) = 0 := by
      rw [measure_toMeasurable, â† compl_mem_ae_iff, compl_compl]
      exact H.ae_eq_mk.and ht
    filter_upwards [compl_mem_ae_iff.2 A] with x hx
    rw [mem_compl_iff] at hx
    simp only [s, g, hx, piecewise_eq_of_notMem, not_false_iff]
    contrapose! hx
    apply subset_toMeasurable
    simp only [hx, mem_compl_iff, mem_setOf_eq, false_and, not_false_iff]

theorem exists_measurable_nonneg {Î²} [Preorder Î²] [Zero Î²] {mÎ² : MeasurableSpace Î²} {f : Î± â†’ Î²}
    (hf : AEMeasurable f Î¼) (f_nn : âˆ€áµ t âˆ‚Î¼, 0 â‰¤ f t) : âˆƒ g, Measurable g âˆ§ 0 â‰¤ g âˆ§ f =áµ[Î¼] g := by
  obtain âŸ¨G, hG_meas, hG_mem, hG_ae_eqâŸ© := hf.exists_ae_eq_range_subset f_nn âŸ¨0, le_rflâŸ©
  exact âŸ¨G, hG_meas, fun x => hG_mem (mem_range_self x), hG_ae_eqâŸ©

theorem subtype_mk (h : AEMeasurable f Î¼) {s : Set Î²} {hfs : âˆ€ x, f x âˆˆ s} :
    AEMeasurable (codRestrict f s hfs) Î¼ := by
  nontriviality Î±; inhabit Î±
  obtain âŸ¨g, g_meas, hg, fgâŸ© : âˆƒ g : Î± â†’ Î², Measurable g âˆ§ range g âŠ† s âˆ§ f =áµ[Î¼] g :=
    h.exists_ae_eq_range_subset (Eventually.of_forall hfs) âŸ¨_, hfs defaultâŸ©
  refine âŸ¨codRestrict g s fun x => hg (mem_range_self _), Measurable.subtype_mk g_meas, ?_âŸ©
  filter_upwards [fg] with x hx
  simpa [Subtype.ext_iff]

end AEMeasurable

theorem aemeasurable_const' (h : âˆ€áµ (x) (y) âˆ‚Î¼, f x = f y) : AEMeasurable f Î¼ := by
  rcases eq_or_ne Î¼ 0 with (rfl | hÎ¼)
  Â· exact aemeasurable_zero_measure
  Â· haveI := ae_neBot.2 hÎ¼
    rcases h.exists with âŸ¨x, hxâŸ©
    exact âŸ¨const Î± (f x), measurable_const, EventuallyEq.symm hxâŸ©

open scoped Interval in
theorem aemeasurable_uIoc_iff [LinearOrder Î±] {f : Î± â†’ Î²} {a b : Î±} :
    (AEMeasurable f <| Î¼.restrict <| Î™ a b) â†”
      (AEMeasurable f <| Î¼.restrict <| Ioc a b) âˆ§ (AEMeasurable f <| Î¼.restrict <| Ioc b a) := by
  rw [uIoc_eq_union, aemeasurable_union_iff]

theorem aemeasurable_iff_measurable [Î¼.IsComplete] : AEMeasurable f Î¼ â†” Measurable f :=
  âŸ¨fun h => h.nullMeasurable.measurable_of_complete, fun h => h.aemeasurableâŸ©

theorem MeasurableEmbedding.aemeasurable_map_iff {g : Î² â†’ Î³} (hf : MeasurableEmbedding f) :
    AEMeasurable g (Î¼.map f) â†” AEMeasurable (g âˆ˜ f) Î¼ := by
  refine âŸ¨fun H => H.comp_measurable hf.measurable, ?_âŸ©
  rintro âŸ¨gâ‚, hgmâ‚, heqâŸ©
  rcases hf.exists_measurable_extend hgmâ‚ fun x => âŸ¨g xâŸ© with âŸ¨gâ‚‚, hgmâ‚‚, rflâŸ©
  exact âŸ¨gâ‚‚, hgmâ‚‚, hf.ae_map_iff.2 heqâŸ©

theorem MeasurableEmbedding.aemeasurable_comp_iff {g : Î² â†’ Î³} (hg : MeasurableEmbedding g)
    {Î¼ : Measure Î±} : AEMeasurable (g âˆ˜ f) Î¼ â†” AEMeasurable f Î¼ := by
  refine âŸ¨fun H => ?_, hg.measurable.comp_aemeasurableâŸ©
  suffices AEMeasurable ((rangeSplitting g âˆ˜ rangeFactorization g) âˆ˜ f) Î¼ by
    rwa [(rightInverse_rangeSplitting hg.injective).comp_eq_id] at this
  exact hg.measurable_rangeSplitting.comp_aemeasurable H.subtype_mk

theorem aemeasurable_restrict_iff_comap_subtype {s : Set Î±} (hs : MeasurableSet s) {Î¼ : Measure Î±}
    {f : Î± â†’ Î²} : AEMeasurable f (Î¼.restrict s) â†” AEMeasurable (f âˆ˜ (â†‘) : s â†’ Î²) (comap (â†‘) Î¼) := by
  rw [â† map_comap_subtype_coe hs, (MeasurableEmbedding.subtype_coe hs).aemeasurable_map_iff]

@[to_additive]
theorem aemeasurable_one [One Î²] : AEMeasurable (fun _ : Î± => (1 : Î²)) Î¼ :=
  measurable_one.aemeasurable

@[simp]
theorem aemeasurable_smul_measure_iff {c : â„â‰¥0âˆ} (hc : c â‰  0) :
    AEMeasurable f (c â€¢ Î¼) â†” AEMeasurable f Î¼ :=
  âŸ¨fun h => âŸ¨h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).1 h.ae_eq_mkâŸ©, fun h =>
    âŸ¨h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).2 h.ae_eq_mkâŸ©âŸ©

theorem aemeasurable_of_aemeasurable_trim {Î±} {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}
    (hm : m â‰¤ m0) {f : Î± â†’ Î²} (hf : AEMeasurable f (Î¼.trim hm)) : AEMeasurable f Î¼ :=
  âŸ¨hf.mk f, Measurable.mono hf.measurable_mk hm le_rfl, ae_eq_of_ae_eq_trim hf.ae_eq_mkâŸ©

theorem aemeasurable_restrict_of_measurable_subtype {s : Set Î±} (hs : MeasurableSet s)
    (hf : Measurable fun x : s => f x) : AEMeasurable f (Î¼.restrict s) :=
  (aemeasurable_restrict_iff_comap_subtype hs).2 hf.aemeasurable

theorem aemeasurable_map_equiv_iff (e : Î± â‰ƒáµ Î²) {f : Î² â†’ Î³} :
    AEMeasurable f (Î¼.map e) â†” AEMeasurable (f âˆ˜ e) Î¼ :=
  e.measurableEmbedding.aemeasurable_map_iff

end

theorem AEMeasurable.restrict (hfm : AEMeasurable f Î¼) {s} : AEMeasurable f (Î¼.restrict s) :=
  âŸ¨AEMeasurable.mk f hfm, hfm.measurable_mk, ae_restrict_of_ae hfm.ae_eq_mkâŸ©

theorem aemeasurable_Ioi_of_forall_Ioc {Î²} {mÎ² : MeasurableSpace Î²} [LinearOrder Î±]
    [(atTop : Filter Î±).IsCountablyGenerated] {x : Î±} {g : Î± â†’ Î²}
    (g_meas : âˆ€ t > x, AEMeasurable g (Î¼.restrict (Ioc x t))) :
    AEMeasurable g (Î¼.restrict (Ioi x)) := by
  haveI : Nonempty Î± := âŸ¨xâŸ©
  obtain âŸ¨u, hu_tendstoâŸ© := exists_seq_tendsto (atTop : Filter Î±)
  have Ioi_eq_iUnion : Ioi x = â‹ƒ n : â„•, Ioc x (u n) := by
    rw [iUnion_Ioc_eq_Ioi_self_iff.mpr _]
    exact fun y _ => (hu_tendsto.eventually (eventually_ge_atTop y)).exists
  rw [Ioi_eq_iUnion, aemeasurable_iUnion_iff]
  intro n
  rcases lt_or_ge x (u n) with h | h
  Â· exact g_meas (u n) h
  Â· rw [Ioc_eq_empty (not_lt.mpr h), Measure.restrict_empty]
    exact aemeasurable_zero_measure

section Zero

variable [Zero Î²]

theorem aemeasurable_indicator_iff {s} (hs : MeasurableSet s) :
    AEMeasurable (indicator s f) Î¼ â†” AEMeasurable f (Î¼.restrict s) := by
  constructor
  Â· intro h
    exact (h.mono_measure Measure.restrict_le_self).congr (indicator_ae_eq_restrict hs)
  Â· intro h
    refine âŸ¨indicator s (h.mk f), h.measurable_mk.indicator hs, ?_âŸ©
    have A : s.indicator f =áµ[Î¼.restrict s] s.indicator (AEMeasurable.mk f h) :=
      (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans <| (indicator_ae_eq_restrict hs).symm)
    have B : s.indicator f =áµ[Î¼.restrict sá¶œ] s.indicator (AEMeasurable.mk f h) :=
      (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm
    exact ae_of_ae_restrict_of_ae_restrict_compl _ A B

theorem aemeasurable_indicator_iffâ‚€ {s} (hs : NullMeasurableSet s Î¼) :
    AEMeasurable (indicator s f) Î¼ â†” AEMeasurable f (Î¼.restrict s) := by
  rcases hs with âŸ¨t, ht, hstâŸ©
  rw [â† aemeasurable_congr (indicator_ae_eq_of_ae_eq_set hst.symm), aemeasurable_indicator_iff ht,
      restrict_congr_set hst]

/-- A characterization of the a.e.-measurability of the indicator function which takes a constant
value `b` on a set `A` and `0` elsewhere. -/
lemma aemeasurable_indicator_const_iff {s} [MeasurableSingletonClass Î²] (b : Î²) [NeZero b] :
    AEMeasurable (s.indicator (fun _ â†¦ b)) Î¼ â†” NullMeasurableSet s Î¼ := by
  classical
  constructor <;> intro h
  Â· convert h.nullMeasurable (MeasurableSet.singleton (0 : Î²)).compl
    rw [indicator_const_preimage_eq_union s {0}á¶œ b]
    simp [NeZero.ne b]
  Â· exact (aemeasurable_indicator_iffâ‚€ h).mpr aemeasurable_const

@[measurability]
theorem AEMeasurable.indicator (hfm : AEMeasurable f Î¼) {s} (hs : MeasurableSet s) :
    AEMeasurable (s.indicator f) Î¼ :=
  (aemeasurable_indicator_iff hs).mpr hfm.restrict

theorem AEMeasurable.indicatorâ‚€ (hfm : AEMeasurable f Î¼) {s} (hs : NullMeasurableSet s Î¼) :
    AEMeasurable (s.indicator f) Î¼ :=
  (aemeasurable_indicator_iffâ‚€ hs).mpr hfm.restrict

end Zero

theorem MeasureTheory.Measure.restrict_map_of_aemeasurable {f : Î± â†’ Î´} (hf : AEMeasurable f Î¼)
    {s : Set Î´} (hs : MeasurableSet s) : (Î¼.map f).restrict s = (Î¼.restrict <| f â»Â¹' s).map f :=
  calc
    (Î¼.map f).restrict s = (Î¼.map (hf.mk f)).restrict s := by
      congr 1
      apply Measure.map_congr hf.ae_eq_mk
    _ = (Î¼.restrict <| hf.mk f â»Â¹' s).map (hf.mk f) := Measure.restrict_map hf.measurable_mk hs
    _ = (Î¼.restrict <| hf.mk f â»Â¹' s).map f :=
      (Measure.map_congr (ae_restrict_of_ae hf.ae_eq_mk.symm))
    _ = (Î¼.restrict <| f â»Â¹' s).map f := by
      apply congr_arg
      ext1 t ht
      simp only [ht, Measure.restrict_apply]
      apply measure_congr
      apply (EventuallyEq.refl _ _).inter (hf.ae_eq_mk.symm.preimage s)

theorem MeasureTheory.Measure.map_mono_of_aemeasurable {f : Î± â†’ Î´} (h : Î¼ â‰¤ Î½)
    (hf : AEMeasurable f Î½) : Î¼.map f â‰¤ Î½.map f :=
  le_iff.2 fun s hs â†¦ by simpa [hf, hs, hf.mono_measure h] using h (f â»Â¹' s)

/-- If the `Ïƒ`-algebra of the codomain of a null measurable function is countably generated,
then the function is a.e.-measurable. -/
lemma MeasureTheory.NullMeasurable.aemeasurable {f : Î± â†’ Î²}
    [hc : MeasurableSpace.CountablyGenerated Î²] (h : NullMeasurable f Î¼) : AEMeasurable f Î¼ := by
  classical
  nontriviality Î²; inhabit Î²
  rcases hc.1 with âŸ¨S, hSc, rflâŸ©
  choose! T hTf hTm hTeq using fun s hs â†¦ (h <| .basic s hs).exists_measurable_subset_ae_eq
  choose! U hUf hUm hUeq using fun s hs â†¦ (h <| .basic s hs).exists_measurable_superset_ae_eq
  set v := â‹ƒ s âˆˆ S, U s \ T s
  have hvm : MeasurableSet v := .biUnion hSc fun s hs â†¦ (hUm s hs).diff (hTm s hs)
  have hvÎ¼ : Î¼ v = 0 := (measure_biUnion_null_iff hSc).2 fun s hs â†¦ ae_le_set.1 <|
    ((hUeq s hs).trans (hTeq s hs).symm).le
  refine âŸ¨v.piecewise (fun _ â†¦ default) f, ?_, measure_mono_null (fun x â†¦
    not_imp_comm.2 fun hxv â†¦ (piecewise_eq_of_notMem _ _ _ hxv).symm) hvÎ¼âŸ©
  refine measurable_of_restrict_of_restrict_compl hvm ?_ ?_
  Â· rw [restrict_piecewise]
    apply measurable_const
  Â· rw [restrict_piecewise_compl, restrict_eq]
    refine measurable_generateFrom fun s hs â†¦ .of_subtype_image ?_
    rw [preimage_comp, Subtype.image_preimage_coe]
    convert (hTm s hs).diff hvm using 1
    rw [inter_comm]
    refine Set.ext fun x â†¦ and_congr_left fun hxv â†¦ âŸ¨fun hx â†¦ ?_, fun hx â†¦ hTf s hs hxâŸ©
    exact by_contra fun hx' â†¦ hxv <| mem_biUnion hs âŸ¨hUf s hs hx, hx'âŸ©

/-- Let `f : Î± â†’ Î²` be a null measurable function
such that a.e. all values of `f` belong to a set `t`
such that the restriction of the `Ïƒ`-algebra in the codomain to `t` is countably generated,
then `f` is a.e.-measurable. -/
lemma MeasureTheory.NullMeasurable.aemeasurable_of_aerange {f : Î± â†’ Î²} {t : Set Î²}
    [MeasurableSpace.CountablyGenerated t] (h : NullMeasurable f Î¼) (hft : âˆ€áµ x âˆ‚Î¼, f x âˆˆ t) :
    AEMeasurable f Î¼ := by
  rcases eq_empty_or_nonempty t with rfl | hne
  Â· obtain rfl : Î¼ = 0 := by simpa using hft
    apply aemeasurable_zero_measure
  Â· rw [â† Î¼.ae_completion] at hft
    obtain âŸ¨f', hf'm, hf't, hff'âŸ© :
        âˆƒ f' : Î± â†’ Î², NullMeasurable f' Î¼ âˆ§ range f' âŠ† t âˆ§ f =áµ[Î¼] f' :=
      h.measurable'.aemeasurable.exists_ae_eq_range_subset hft hne
    rw [range_subset_iff] at hf't
    lift f' to Î± â†’ t using hf't
    replace hf'm : NullMeasurable f' Î¼ := hf'm.measurable'.subtype_mk
    exact (measurable_subtype_coe.comp_aemeasurable hf'm.aemeasurable).congr hff'.symm

namespace MeasureTheory
namespace Measure

lemma map_sum {Î¹ : Type*} {m : Î¹ â†’ Measure Î±} {f : Î± â†’ Î²} (hf : AEMeasurable f (Measure.sum m)) :
    Measure.map f (Measure.sum m) = Measure.sum (fun i â†¦ Measure.map f (m i)) := by
  ext s hs
  rw [map_apply_of_aemeasurable hf hs, sum_applyâ‚€ _ (hf.nullMeasurable hs), sum_apply _ hs]
  have M i : AEMeasurable f (m i) := hf.mono_measure (le_sum m i)
  simp_rw [map_apply_of_aemeasurable (M _) hs]

instance (Î¼ : Measure Î±) (f : Î± â†’ Î²) [SFinite Î¼] : SFinite (Î¼.map f) := by
  by_cases H : AEMeasurable f Î¼
  Â· rw [â† sum_sfiniteSeq Î¼] at H âŠ¢
    rw [map_sum H]
    infer_instance
  Â· rw [map_of_not_aemeasurable H]
    infer_instance

end Measure
end MeasureTheory
