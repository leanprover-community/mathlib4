/-
Copyright (c) 2024 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Josha Dekker, Arav Bhattacharyya
-/
module

public import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
public import Mathlib.Topology.Metrizable.CompletelyMetrizable
import Mathlib.MeasureTheory.Measure.LevyProkhorovMetric
import Mathlib.MeasureTheory.Measure.RegularityCompacts

/-!
# Tight sets of measures

A set of measures is tight if for all `0 < Îµ`, there exists a compact set `K` such that for all
measures in the set, the complement of `K` has measure at most `Îµ`.

## Main definitions

* `MeasureTheory.IsTightMeasureSet`: A set of measures `S` is tight if for all `0 < Îµ`, there exists
  a compact set `K` such that for all `Î¼ âˆˆ S`, `Î¼ Ká¶œ â‰¤ Îµ`.
  The definition uses an equivalent formulation with filters: `â¨† Î¼ âˆˆ S, Î¼` tends to `0` along the
  filter of cocompact sets.
  `isTightMeasureSet_iff_exists_isCompact_measure_compl_le` establishes equivalence between
  the two definitions.

## Main statements

* `isTightMeasureSet_singleton_of_innerRegularWRT`: every finite, inner-regular measure is tight.
* `isTightMeasureSet_of_isCompact_closure`: every relatively compact set of measures is tight.


-/

@[expose] public section

open Filter Set Metric ENNReal NNReal MeasureTheory ProbabilityMeasure TopologicalSpace

open scoped ENNReal NNReal Topology FiniteMeasure ProbabilityMeasure

namespace MeasureTheory

variable {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§}
  {Î¼ Î½ : Measure ğ“§} {S T : Set (Measure ğ“§)}

section Basic

variable [TopologicalSpace ğ“§]

/-- A set of measures `S` is tight if for all `0 < Îµ`, there exists a compact set `K` such that
for all `Î¼ âˆˆ S`, `Î¼ Ká¶œ â‰¤ Îµ`.
This is formulated in terms of filters, and proven equivalent to the definition above
in `IsTightMeasureSet_iff_exists_isCompact_measure_compl_le`. -/
def IsTightMeasureSet (S : Set (Measure ğ“§)) : Prop :=
  Tendsto (â¨† Î¼ âˆˆ S, Î¼) (cocompact ğ“§).smallSets (ğ“ 0)

/-- A set of measures `S` is tight if for all `0 < Îµ`, there exists a compact set `K` such that
for all `Î¼ âˆˆ S`, `Î¼ Ká¶œ â‰¤ Îµ`. -/
lemma isTightMeasureSet_iff_exists_isCompact_measure_compl_le :
    IsTightMeasureSet S â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ K : Set ğ“§, IsCompact K âˆ§ âˆ€ Î¼ âˆˆ S, Î¼ (Ká¶œ) â‰¤ Îµ := by
  simp only [IsTightMeasureSet, ENNReal.tendsto_nhds ENNReal.zero_ne_top, gt_iff_lt, zero_add,
    iSup_apply, mem_Icc, tsub_le_iff_right, zero_le, iSup_le_iff, true_and, eventually_smallSets,
    mem_cocompact]
  refine âŸ¨fun h Îµ hÎµ â†¦ ?_, fun h Îµ hÎµ â†¦ ?_âŸ©
  Â· obtain âŸ¨A, âŸ¨K, h1, h2âŸ©, hAâŸ© := h Îµ hÎµ
    exact âŸ¨K, h1, hA Ká¶œ h2âŸ©
  Â· obtain âŸ¨K, h1, h2âŸ© := h Îµ hÎµ
    exact âŸ¨Ká¶œ, âŸ¨K, h1, subset_rflâŸ©, fun A hA Î¼ hÎ¼S â†¦ (Î¼.mono hA).trans (h2 Î¼ hÎ¼S)âŸ©

@[deprecated (since := "2025-12-13")] alias
IsTightMeasureSet_iff_exists_isCompact_measure_compl_le :=
isTightMeasureSet_iff_exists_isCompact_measure_compl_le

/-- Finite measures that are inner regular with respect to closed compact sets are tight. -/
theorem isTightMeasureSet_singleton_of_innerRegularWRT [OpensMeasurableSpace ğ“§] [IsFiniteMeasure Î¼]
    (h : Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) MeasurableSet) :
    IsTightMeasureSet {Î¼} := by
  rw [isTightMeasureSet_iff_exists_isCompact_measure_compl_le]
  intro Îµ hÎµ
  let r := Î¼ Set.univ
  cases lt_or_ge Îµ r with
  | inl hÎµr =>
    have hÎµr' : r - Îµ < r := ENNReal.sub_lt_self (measure_ne_top Î¼ _) hÎµr.ne_bot hÎµ.ne'
    obtain âŸ¨K, _, âŸ¨hK_compact, hK_closedâŸ©, hKÎ¼âŸ© := h .univ (r - Îµ) hÎµr'
    refine âŸ¨K, hK_compact, ?_âŸ©
    simp only [mem_singleton_iff, forall_eq]
    rw [measure_compl hK_closed.measurableSet (measure_ne_top Î¼ _), tsub_le_iff_right]
    rw [ENNReal.sub_lt_iff_lt_right (ne_top_of_lt hÎµr) hÎµr.le, add_comm] at hKÎ¼
    exact hKÎ¼.le
  | inr hÎµr => exact âŸ¨âˆ…, isCompact_empty, by simpaâŸ©

/-- Inner regular finite measures on T2 spaces are tight. -/
lemma isTightMeasureSet_singleton_of_innerRegular [T2Space ğ“§] [OpensMeasurableSpace ğ“§]
    [IsFiniteMeasure Î¼] [h : Î¼.InnerRegular] :
    IsTightMeasureSet {Î¼} := by
  refine isTightMeasureSet_singleton_of_innerRegularWRT ?_
  intro s hs r hr
  obtain âŸ¨K, hKs, hK_compact, hÎ¼KâŸ© := h.innerRegular hs r hr
  exact âŸ¨K, hKs, âŸ¨hK_compact, hK_compact.isClosedâŸ©, hÎ¼KâŸ©

/-- In a complete second-countable pseudo-metric space, finite measures are tight. -/
theorem isTightMeasureSet_singleton {Î± : Type*} [MeasurableSpace Î±] [TopologicalSpace Î±]
    [IsCompletelyPseudoMetrizableSpace Î±] [SecondCountableTopology Î±] [BorelSpace Î±]
    {Î¼ : Measure Î±} [IsFiniteMeasure Î¼] :
    IsTightMeasureSet {Î¼} :=
  isTightMeasureSet_singleton_of_innerRegularWRT
    (innerRegular_isCompact_isClosed_measurableSet_of_finite _)

namespace IsTightMeasureSet

/-- In a compact space, every set of measures is tight. -/
lemma of_compactSpace [CompactSpace ğ“§] : IsTightMeasureSet S := by
  simp only [IsTightMeasureSet, cocompact_eq_bot, smallSets_bot, tendsto_pure_left, iSup_apply,
    measure_empty, ENNReal.iSup_zero, ciSup_const]
  exact fun _ â†¦ mem_of_mem_nhds

protected lemma subset (hT : IsTightMeasureSet T) (hST : S âŠ† T) :
    IsTightMeasureSet S :=
  tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hT (fun _ â†¦ by simp)
    (iSup_le_iSup_of_subset hST)

protected lemma union (hS : IsTightMeasureSet S) (hT : IsTightMeasureSet T) :
    IsTightMeasureSet (S âˆª T) := by
  rw [IsTightMeasureSet, iSup_union]
  convert Tendsto.sup_nhds hS hT
  simp

protected lemma inter (hS : IsTightMeasureSet S) (T : Set (Measure ğ“§)) :
    IsTightMeasureSet (S âˆ© T) :=
  hS.subset inter_subset_left

lemma map [TopologicalSpace ğ“¨] [MeasurableSpace ğ“¨] [OpensMeasurableSpace ğ“¨] [T2Space ğ“¨]
    (hS : IsTightMeasureSet S) {f : ğ“§ â†’ ğ“¨} (hf : Continuous f) :
    IsTightMeasureSet (Measure.map f '' S) := by
  rw [isTightMeasureSet_iff_exists_isCompact_measure_compl_le] at hS âŠ¢
  simp only [mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
  intro Îµ hÎµ
  obtain âŸ¨K, hK_compact, hKSâŸ© := hS Îµ hÎµ
  refine âŸ¨f '' K, hK_compact.image hf, fun Î¼ hÎ¼S â†¦ ?_âŸ©
  by_cases hf_meas : AEMeasurable f Î¼
  swap; Â· simp [Measure.map_of_not_aemeasurable hf_meas]
  rw [Measure.map_apply_of_aemeasurable hf_meas (hK_compact.image hf).measurableSet.compl]
  refine (measure_mono ?_).trans (hKS Î¼ hÎ¼S)
  simp only [preimage_compl, compl_subset_compl]
  exact subset_preimage_image f K

end IsTightMeasureSet
end Basic

variable [PseudoMetricSpace ğ“§] [OpensMeasurableSpace ğ“§] [SecondCountableTopology ğ“§]
  {S : Set (ProbabilityMeasure ğ“§)}

lemma exists_measure_iUnion_gt_of_isCompact_closure
    (U : â„• â†’ Set ğ“§) (O : âˆ€ i, IsOpen (U i)) (Cov : â‹ƒ i, U i = univ) (hcomp : IsCompact (closure S))
    (Îµ : â„â‰¥0âˆ) (hÎµ : 0 < Îµ) (hÎµbound : Îµ â‰¤ 1) :
    âˆƒ (k : â„•), âˆ€ Î¼ âˆˆ S, 1 - Îµ < Î¼ (â‹ƒ i â‰¤ k, U i) := by
  have Îµfin : Îµ â‰  âˆ := ne_top_of_le_ne_top (by simp) hÎµbound
  lift Îµ to â„â‰¥0 using Îµfin
  obtain âŸ¨Îµ, hÎµ'âŸ© := Îµ
  simp only [ENNReal.coe_pos, â† NNReal.coe_lt_coe, NNReal.coe_zero, coe_mk, coe_le_one_iff,
      â† NNReal.coe_le_coe, NNReal.coe_one] at hÎµ hÎµbound
  by_contra! nh
  choose Î¼ hÎ¼InS hcontradiction using nh
  obtain âŸ¨Î¼lim, _, sub, hsubmono, hÎ¼convergesâŸ© :=
      hcomp.isSeqCompact (fun n â†¦ subset_closure <| hÎ¼InS n)
  have Measurebound n : (Î¼lim (â‹ƒ (i â‰¤ n), U i) : â„) â‰¤ 1 - Îµ := calc
    (Î¼lim (â‹ƒ (i â‰¤ n), U i) : â„)
    _ â‰¤ liminf (fun k â†¦ (Î¼ (sub k) (â‹ƒ (i â‰¤ n), U i) : â„)) atTop := by
      have hopen : IsOpen (â‹ƒ i â‰¤ n, U i) := isOpen_biUnion fun i a â†¦ O i
      have := ProbabilityMeasure.le_liminf_measure_open_of_tendsto hÎ¼converges hopen
      simp_rw [Function.comp_apply, â† ProbabilityMeasure.ennreal_coeFn_eq_coeFn_toMeasure] at this
      rw [â† ofNNReal_liminf] at this
      Â· exact mod_cast this
      use 1
      simpa [ge_iff_le, eventually_map, eventually_atTop, forall_exists_index] using fun _ x h â†¦
          (h x (by simp)).trans <| ProbabilityMeasure.apply_le_one (Î¼ (sub x)) (â‹ƒ i â‰¤ n, U i)
    _ â‰¤ liminf (fun k â†¦ (Î¼ (sub k) (â‹ƒ (i â‰¤ sub k), U i) : â„)) atTop := by
      apply Filter.liminf_le_liminf
      Â· simp only [NNReal.coe_le_coe, eventually_atTop, ge_iff_le]
        use n + 1
        intro b hypo
        refine (Î¼ (sub b)).apply_mono
            <| Set.biUnion_mono (fun i (hi : i â‰¤ n) â†¦ hi.trans ?_) fun _ _ â†¦ le_rfl
        exact le_trans (Nat.le_add_right n 1) (le_trans hypo (StrictMono.le_apply hsubmono))
      Â· use 0; simp
      Â· use 1
        simpa [ge_iff_le, eventually_map, eventually_atTop, ge_iff_le, forall_exists_index] using
            fun _ d hyp â†¦ (hyp d (by simp)).trans (by simp)
    _ â‰¤ 1 - Îµ := by
      apply Filter.liminf_le_of_le
      Â· use 0; simp
      simp only [eventually_atTop, ge_iff_le, forall_exists_index]
      intro b c h
      apply le_trans (h c le_rfl)
      refine (ofReal_le_ofReal_iff (by rw [sub_nonneg]; exact hÎµbound)).mp ?_
      rw [ofReal_coe_nnreal]
      apply le_trans (hcontradiction (sub c))
      norm_cast
  have accumulation : Tendsto (fun n â†¦ Î¼lim (â‹ƒ i â‰¤ n, U i)) atTop (ğ“ (Î¼lim (â‹ƒ i, U i))) := by
    simp_rw [â† Set.accumulate_def, ProbabilityMeasure.tendsto_measure_iUnion_accumulate]
  rw [Cov, coeFn_univ, â† NNReal.tendsto_coe] at accumulation
  have exceeds_bound : âˆ€á¶  n in atTop, (1 - Îµ / 2 : â„) â‰¤ Î¼lim (â‹ƒ i â‰¤ n, U i) :=
      Tendsto.eventually_const_le (v := 1) (by simp only [sub_lt_self_iff, Nat.ofNat_pos,
                    div_pos_iff_of_pos_right]; positivity) accumulation
  suffices âˆ€á¶  n : â„• in atTop, False from this.exists.choose_spec
  filter_upwards [exceeds_bound] with n hn
  linarith [hn.trans <| Measurebound n]

variable [CompleteSpace ğ“§]

/-- In a second countable complete metric space, a set of probability measures with compact closure
is tight. -/
theorem isTightMeasureSet_of_isCompact_closure (hcomp : IsCompact (closure S)) :
    IsTightMeasureSet {((Î¼ : ProbabilityMeasure ğ“§) : Measure ğ“§) | Î¼ âˆˆ S} := by
  rw [isTightMeasureSet_iff_exists_isCompact_measure_compl_le]
  rcases isEmpty_or_nonempty ğ“§ with hempty | hnonempty
  Â· rw [â† univ_eq_empty_iff] at hempty
    exact fun Îµ Îµpos â†¦ âŸ¨âˆ…, isCompact_empty, by simp [hempty]âŸ©
  obtain âŸ¨D, hDâŸ© := exists_dense_seq ğ“§
  obtain âŸ¨u, hu_anti, hu_pos, huâŸ© : âˆƒ u, StrictAnti u âˆ§ (âˆ€ n, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  have hcov (m : â„•) : â‹ƒ i, ball (D i) (u m) = univ := by
    rw [denseRange_iff] at hD
    ext p
    exact âŸ¨fun a â†¦ trivial, fun _ â†¦ mem_iUnion.mpr <| hD p (u m) (hu_pos m)âŸ©
  intro Îµ Îµpos
  rcases lt_or_ge 1 Îµ with hÎµbound | hÎµbound
  Â· refine âŸ¨âˆ…, isCompact_empty, fun Î¼ hÎ¼ â†¦ ?_âŸ©
    simp only [mem_setOf_eq] at hÎ¼
    obtain âŸ¨Î¼', hÎ¼', rflâŸ© := hÎ¼
    rw [compl_empty,measure_univ]
    exact le_of_lt hÎµbound
  have byclaim (m : â„•) : âˆƒ k, âˆ€ Î¼ âˆˆ S, 1 - (Îµ * 2 ^ (- m : â„¤) : â„â‰¥0âˆ) <
      Î¼ (â‹ƒ i â‰¤ k, ball (D i) (u m)) := by
    refine exists_measure_iUnion_gt_of_isCompact_closure
      (fun i â†¦ ball (D i) (u m)) (fun _ â†¦ isOpen_ball) (hcov m) hcomp (Îµ * 2 ^ (-m : â„¤)) ?_ ?_
    Â· simpa using âŸ¨Îµpos, (ENNReal.zpow_pos (by simp) (by simp) (-â†‘m))âŸ©
    Â· exact Left.mul_le_one hÎµbound <| zpow_le_one_of_nonpos (by linarith) (by simp)
  choose! km hbound using byclaim
  -- This is a set we can construct to show tightness
  let bigK := â‹‚ m, â‹ƒ (i â‰¤ km (m + 1)), closure (ball (D i) (u m))
  have bigcalc (Î¼ : ProbabilityMeasure ğ“§) (hs : Î¼ âˆˆ S) : Î¼.toMeasure bigKá¶œ â‰¤ Îµ := calc
    Î¼.toMeasure bigKá¶œ
    _ = Î¼.toMeasure (â‹ƒ m,(â‹ƒ (i â‰¤ km (m + 1)), closure (ball (D i) (u m)))á¶œ) := by simp [bigK]
    _ â‰¤ âˆ‘' m, Î¼.toMeasure (â‹ƒ (i â‰¤ km (m + 1)), closure (ball (D i) (u m)))á¶œ :=
      measure_iUnion_le _
    _ = âˆ‘' m, (1 - Î¼.toMeasure (â‹ƒ (i â‰¤ km (m + 1)), closure (ball (D i) (u m)))) := by
      congr! with m; rw [measure_compl (by measurability) (by simp)]; simp
    _ â‰¤ (âˆ‘' (m : â„•), (Îµ : â„â‰¥0âˆ) * 2 ^ (-(m + 1) : â„¤)) := by
      refine ENNReal.tsum_le_tsum fun m â†¦ tsub_le_iff_tsub_le.mp ?_
      replace hbound := (hbound (m + 1) Î¼ hs).le
      simp_all only [neg_add_rev, Int.reduceNeg, tsub_le_iff_right, Nat.cast_add, Nat.cast_one,
          â† coe_ofNat, â† ennreal_coeFn_eq_coeFn_toMeasure]
      grw [hbound]
      gcongr with i hi
      grw [â† subset_closure (s := ball (D i) (u m)), ball_subset_ball]
      exact hu_anti.antitone (by grind)
    _ = Îµ := by
      rw [ENNReal.tsum_mul_left]
      nth_rw 2 [â† mul_one (a := Îµ)]
      congr
      ring_nf
      exact tsum_two_zpow_neg_add_one
  -- Final proof
  refine âŸ¨bigK, ?_, by simpaâŸ©
  -- Compactness first
  refine TotallyBounded.isCompact_of_isClosed ?_ ?_
  --Totally bounded
  Â· refine Metric.totallyBounded_iff.mpr fun Î´ Î´pos â†¦ ?_
    have âŸ¨Î´_inv, hÎ´_invâŸ© : âˆƒ x, u x < Î´ := (Tendsto.eventually_lt_const Î´pos hu).exists
    refine âŸ¨D '' .Iic (km (Î´_inv + 1)), (Set.finite_Iic _).image _, ?_âŸ©
    -- t should be image under D of the set of numbers less than km of Î´_inv
    simp only [mem_image, iUnion_exists, biUnion_and', iUnion_iUnion_eq_right, bigK]
    calc
        â‹‚ m, â‹ƒ i â‰¤ km (m + 1), closure (ball (D i) (u m))
    _ âŠ† â‹ƒ i â‰¤ km (Î´_inv + 1), closure (ball (D i) (u Î´_inv)) := iInter_subset ..
    _ âŠ† â‹ƒ i â‰¤ km (Î´_inv + 1), ball (D i) Î´ := by
        gcongr
        exact closure_ball_subset_closedBall.trans <| closedBall_subset_ball <| hÎ´_inv
  -- Closedness
  Â· refine isClosed_iInter fun n â†¦ Finite.isClosed_biUnion ?_ (fun _ _ â†¦ isClosed_closure)
    refine Finite.ofFinset (Finset.Iic (km (n + 1))) fun x â†¦ ?_
    simp only [Finset.mem_Iic, Nat.le_eq]
    rfl


end MeasureTheory
