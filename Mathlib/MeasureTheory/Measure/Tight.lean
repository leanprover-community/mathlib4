/-
Copyright (c) 2024 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Josha Dekker
-/
import Mathlib.MeasureTheory.Measure.RegularityCompacts

/-!
# Tight sets of measures

A set of measures is tight if for all `0 < Œµ`, there exists a compact set `K` such that for all
measures in the set, the complement of `K` has measure at most `Œµ`.

## Main definitions

* `MeasureTheory.IsTightMeasureSet`: A set of measures `S` is tight if for all `0 < Œµ`, there exists
  a compact set `K` such that for all `Œº ‚àà S`, `Œº K·∂ú ‚â§ Œµ`.
  The definition uses an equivalent formulation with filters: `‚®Ü Œº ‚àà S, Œº` tends to `0` along the
  filter of cocompact sets.
  `isTightMeasureSet_iff_exists_isCompact_measure_compl_le` establishes equivalence between
  the two definitions.

## Main statements

* `isTightMeasureSet_singleton_of_innerRegularWRT`: every finite, inner-regular measure is tight.

-/

open Filter Set

open scoped ENNReal NNReal Topology

namespace MeasureTheory

variable {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] {mŒ± : MeasurableSpace Œ±} {Œº ŒΩ : Measure Œ±}
  {S T : Set (Measure Œ±)}

/-- A set of measures `S` is tight if for all `0 < Œµ`, there exists a compact set `K` such that
for all `Œº ‚àà S`, `Œº K·∂ú ‚â§ Œµ`.
This is formulated in terms of filters, and proven equivalent to the definition above
in `IsTightMeasureSet_iff_exists_isCompact_measure_compl_le`. -/
def IsTightMeasureSet (S : Set (Measure Œ±)) : Prop :=
  Tendsto (‚®Ü Œº ‚àà S, Œº) (cocompact Œ±).smallSets (ùìù 0)

/-- A set of measures `S` is tight if for all `0 < Œµ`, there exists a compact set `K` such that
for all `Œº ‚àà S`, `Œº K·∂ú ‚â§ Œµ`. -/
lemma IsTightMeasureSet_iff_exists_isCompact_measure_compl_le :
    IsTightMeasureSet S ‚Üî ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ K : Set Œ±, IsCompact K ‚àß ‚àÄ Œº ‚àà S, Œº (K·∂ú) ‚â§ Œµ := by
  simp only [IsTightMeasureSet, ENNReal.tendsto_nhds ENNReal.zero_ne_top, gt_iff_lt, zero_add,
    iSup_apply, mem_Icc, tsub_le_iff_right, zero_le, iSup_le_iff, true_and, eventually_smallSets,
    mem_cocompact]
  refine ‚ü®fun h Œµ hŒµ ‚Ü¶ ?_, fun h Œµ hŒµ ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®A, ‚ü®K, h1, h2‚ü©, hA‚ü© := h Œµ hŒµ
    exact ‚ü®K, h1, hA K·∂ú h2‚ü©
  ¬∑ obtain ‚ü®K, h1, h2‚ü© := h Œµ hŒµ
    exact ‚ü®K·∂ú, ‚ü®K, h1, subset_rfl‚ü©, fun A hA Œº hŒºS ‚Ü¶ (Œº.mono hA).trans (h2 Œº hŒºS)‚ü©

/-- Finite measures that are inner regular with respect to closed compact sets are tight. -/
theorem isTightMeasureSet_singleton_of_innerRegularWRT [OpensMeasurableSpace Œ±] [IsFiniteMeasure Œº]
    (h : Œº.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) MeasurableSet) :
    IsTightMeasureSet {Œº} := by
  rw [IsTightMeasureSet_iff_exists_isCompact_measure_compl_le]
  intro Œµ hŒµ
  let r := Œº Set.univ
  cases lt_or_ge Œµ r with
  | inl hŒµr =>
    have hŒµr' : r - Œµ < r := ENNReal.sub_lt_self (measure_ne_top Œº _) (zero_le'.trans_lt hŒµr).ne'
      hŒµ.ne'
    obtain ‚ü®K, _, ‚ü®hK_compact, hK_closed‚ü©, hKŒº‚ü© := h .univ (r - Œµ) hŒµr'
    refine ‚ü®K, hK_compact, ?_‚ü©
    simp only [mem_singleton_iff, forall_eq]
    rw [measure_compl hK_closed.measurableSet (measure_ne_top Œº _), tsub_le_iff_right]
    rw [ENNReal.sub_lt_iff_lt_right (ne_top_of_lt hŒµr) hŒµr.le, add_comm] at hKŒº
    exact hKŒº.le
  | inr hŒµr => exact ‚ü®‚àÖ, isCompact_empty, by simpa‚ü©

/-- Inner regular finite measures on T2 spaces are tight. -/
lemma isTightMeasureSet_singleton_of_innerRegular [T2Space Œ±] [OpensMeasurableSpace Œ±]
    [IsFiniteMeasure Œº] [h : Œº.InnerRegular] :
    IsTightMeasureSet {Œº} := by
  refine isTightMeasureSet_singleton_of_innerRegularWRT ?_
  intro s hs r hr
  obtain ‚ü®K, hKs, hK_compact, hŒºK‚ü© := h.innerRegular hs r hr
  exact ‚ü®K, hKs, ‚ü®hK_compact, hK_compact.isClosed‚ü©, hŒºK‚ü©

/-- In a complete second-countable pseudo-metric space, finite measures are tight. -/
theorem isTightMeasureSet_singleton {Œ± : Type*} {mŒ± : MeasurableSpace Œ±}
    [PseudoEMetricSpace Œ±] [CompleteSpace Œ±] [SecondCountableTopology Œ±] [BorelSpace Œ±]
    {Œº : Measure Œ±} [IsFiniteMeasure Œº] :
    IsTightMeasureSet {Œº} :=
  isTightMeasureSet_singleton_of_innerRegularWRT
    (innerRegular_isCompact_isClosed_measurableSet_of_finite _)

namespace IsTightMeasureSet

/-- In a compact space, every set of measures is tight. -/
lemma of_compactSpace [CompactSpace Œ±] : IsTightMeasureSet S := by
  simp only [IsTightMeasureSet, cocompact_eq_bot, smallSets_bot, tendsto_pure_left, iSup_apply,
    measure_empty, ENNReal.iSup_zero, ciSup_const]
  exact fun _ ‚Ü¶ mem_of_mem_nhds

protected lemma subset (hT : IsTightMeasureSet T) (hST : S ‚äÜ T) :
    IsTightMeasureSet S :=
  tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hT (fun _ ‚Ü¶ by simp)
    (iSup_le_iSup_of_subset hST)

protected lemma union (hS : IsTightMeasureSet S) (hT : IsTightMeasureSet T) :
    IsTightMeasureSet (S ‚à™ T) := by
  rw [IsTightMeasureSet, iSup_union]
  convert Tendsto.sup_nhds hS hT
  simp

protected lemma inter (hS : IsTightMeasureSet S) (T : Set (Measure Œ±)) :
    IsTightMeasureSet (S ‚à© T) :=
  hS.subset inter_subset_left

end IsTightMeasureSet

end MeasureTheory
