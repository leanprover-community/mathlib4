/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
module

public import Mathlib.MeasureTheory.Measure.Restrict

/-!
# Measures having no atoms

A measure `Œº` has no atoms if the measure of each singleton is zero.

## TODO

Should `NoAtoms` be redefined as `‚àÄ s, 0 < Œº s ‚Üí ‚àÉ t ‚äÜ s, 0 < Œº t ‚àß Œº t < Œº s`?
-/

@[expose] public section

namespace MeasureTheory

open Set Measure

variable {Œ± : Type*} {m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {s : Set Œ±}

/-- Measure `Œº` *has no atoms* if the measure of each singleton is zero.

NB: Wikipedia assumes that for any measurable set `s` with positive `Œº`-measure,
there exists a measurable `t ‚äÜ s` such that `0 < Œº t < Œº s`. While this implies `Œº {x} = 0`,
the converse is not true. -/
class NoAtoms {m0 : MeasurableSpace Œ±} (Œº : Measure Œ±) : Prop where
  measure_singleton : ‚àÄ x, Œº {x} = 0

export MeasureTheory.NoAtoms (measure_singleton)

attribute [simp] measure_singleton

variable [NoAtoms Œº]

theorem _root_.Set.Subsingleton.measure_zero (hs : s.Subsingleton) (Œº : Measure Œ±) [NoAtoms Œº] :
    Œº s = 0 :=
  hs.induction_on (p := fun s => Œº s = 0) measure_empty measure_singleton

theorem Measure.restrict_singleton' {a : Œ±} : Œº.restrict {a} = 0 := by
  simp only [measure_singleton, Measure.restrict_eq_zero]

instance Measure.restrict.instNoAtoms (s : Set Œ±) : NoAtoms (Œº.restrict s) := by
  refine ‚ü®fun x => ?_‚ü©
  obtain ‚ü®t, hxt, ht1, ht2‚ü© := exists_measurable_superset_of_null (measure_singleton x : Œº {x} = 0)
  apply measure_mono_null hxt
  rw [Measure.restrict_apply ht1]
  apply measure_mono_null inter_subset_left ht2

theorem _root_.Set.Countable.measure_zero (h : s.Countable) (Œº : Measure Œ±) [NoAtoms Œº] :
    Œº s = 0 := by
  rw [‚Üê biUnion_of_singleton s, measure_biUnion_null_iff h]
  simp

theorem _root_.Set.Countable.ae_notMem (h : s.Countable) (Œº : Measure Œ±) [NoAtoms Œº] :
    ‚àÄ·µê x ‚àÇŒº, x ‚àâ s := by
  simpa only [ae_iff, Classical.not_not] using h.measure_zero Œº

@[deprecated (since := "2025-05-23")]
alias _root_.Set.Countable.ae_not_mem := _root_.Set.Countable.ae_notMem

lemma Measure.ae_ne (Œº : Measure Œ±) [NoAtoms Œº] (a : Œ±) : ‚àÄ·µê x ‚àÇŒº, x ‚â† a :=
  (countable_singleton a).ae_notMem Œº

lemma _root_.Set.Countable.measure_restrict_compl (h : s.Countable) (Œº : Measure Œ±) [NoAtoms Œº] :
    Œº.restrict s·∂ú = Œº :=
  restrict_eq_self_of_ae_mem <| h.ae_notMem Œº

@[simp]
lemma restrict_compl_singleton (a : Œ±) : Œº.restrict ({a}·∂ú) = Œº :=
  (countable_singleton _).measure_restrict_compl Œº

theorem _root_.Set.Finite.measure_zero (h : s.Finite) (Œº : Measure Œ±) [NoAtoms Œº] : Œº s = 0 :=
  h.countable.measure_zero Œº

theorem _root_.Finset.measure_zero (s : Finset Œ±) (Œº : Measure Œ±) [NoAtoms Œº] : Œº s = 0 :=
  s.finite_toSet.measure_zero Œº

theorem insert_ae_eq_self (a : Œ±) (s : Set Œ±) : (insert a s : Set Œ±) =·µê[Œº] s :=
  union_ae_eq_right.2 <| measure_mono_null diff_subset (measure_singleton _)

section

variable [PartialOrder Œ±] {a b : Œ±}

theorem Iio_ae_eq_Iic : Iio a =·µê[Œº] Iic a :=
  Iio_ae_eq_Iic' (measure_singleton a)

theorem Ioi_ae_eq_Ici : Ioi a =·µê[Œº] Ici a :=
  Ioi_ae_eq_Ici' (measure_singleton a)

theorem Ioo_ae_eq_Ioc : Ioo a b =·µê[Œº] Ioc a b :=
  Ioo_ae_eq_Ioc' (measure_singleton b)

theorem Ioc_ae_eq_Icc : Ioc a b =·µê[Œº] Icc a b :=
  Ioc_ae_eq_Icc' (measure_singleton a)

theorem Ioo_ae_eq_Ico : Ioo a b =·µê[Œº] Ico a b :=
  Ioo_ae_eq_Ico' (measure_singleton a)

theorem Ioo_ae_eq_Icc : Ioo a b =·µê[Œº] Icc a b :=
  Ioo_ae_eq_Icc' (measure_singleton a) (measure_singleton b)

theorem Ico_ae_eq_Icc : Ico a b =·µê[Œº] Icc a b :=
  Ico_ae_eq_Icc' (measure_singleton b)

theorem Ico_ae_eq_Ioc : Ico a b =·µê[Œº] Ioc a b :=
  Ico_ae_eq_Ioc' (measure_singleton a) (measure_singleton b)

theorem restrict_Iio_eq_restrict_Iic : Œº.restrict (Iio a) = Œº.restrict (Iic a) :=
  restrict_congr_set Iio_ae_eq_Iic

theorem restrict_Ioi_eq_restrict_Ici : Œº.restrict (Ioi a) = Œº.restrict (Ici a) :=
  restrict_congr_set Ioi_ae_eq_Ici

theorem restrict_Ioo_eq_restrict_Ioc : Œº.restrict (Ioo a b) = Œº.restrict (Ioc a b) :=
  restrict_congr_set Ioo_ae_eq_Ioc

theorem restrict_Ioc_eq_restrict_Icc : Œº.restrict (Ioc a b) = Œº.restrict (Icc a b) :=
  restrict_congr_set Ioc_ae_eq_Icc

theorem restrict_Ioo_eq_restrict_Ico : Œº.restrict (Ioo a b) = Œº.restrict (Ico a b) :=
  restrict_congr_set Ioo_ae_eq_Ico

theorem restrict_Ioo_eq_restrict_Icc : Œº.restrict (Ioo a b) = Œº.restrict (Icc a b) :=
  restrict_congr_set Ioo_ae_eq_Icc

theorem restrict_Ico_eq_restrict_Icc : Œº.restrict (Ico a b) = Œº.restrict (Icc a b) :=
  restrict_congr_set Ico_ae_eq_Icc

theorem restrict_Ico_eq_restrict_Ioc : Œº.restrict (Ico a b) = Œº.restrict (Ioc a b) :=
  restrict_congr_set Ico_ae_eq_Ioc

open Filter TopologicalSpace

variable {X : Type*} [EMetricSpace X] [MeasurableSpace X]

/-- If a set has positive measure under an atomless measure, then it has an accumulation point. -/
theorem exists_accPt_of_noAtoms {X : Type*} {E : Set X}
    [EMetricSpace X] [MeasurableSpace X]
    (Œº : Measure X) [NoAtoms Œº] (h_sep : TopologicalSpace.IsSeparable E) (hE : 0 < Œº E) :
    ‚àÉ x, AccPt x (ùìü E) := by
  by_contra! h
  have h_discrete : DiscreteTopology E := by
    have h_isolated : ‚àÄ x ‚àà E, ‚àÉ U : Set X, IsOpen U ‚àß x ‚àà U ‚àß U ‚à© E = {x} := by
      intro x hx
      specialize h x
      rw [accPt_iff_frequently] at h
      simp only [ne_eq, not_frequently, not_and] at h
      obtain ‚ü®w, hw, hsep‚ü© := EMetric.mem_nhds_iff.mp h
      use EMetric.ball x w, EMetric.isOpen_ball, EMetric.mem_ball_self hw
      ext y; simp only [mem_inter_iff, mem_singleton_iff]
      refine ‚ü®fun ‚ü®hy, hyE‚ü© => by_contra fun hne => hsep (EMetric.mem_ball.mp hy) hne hyE,
              fun hy => by rw [hy]; exact ‚ü®EMetric.mem_ball_self hw, hx‚ü©‚ü©
    refine discreteTopology_iff_isOpen_singleton.mpr fun x => ?_
    obtain ‚ü®U, hU_open, hxU, hU_eq‚ü© := h_isolated x x.2
    refine ‚ü®U, hU_open, ?_‚ü©
    ext y
    simp only [mem_preimage, mem_singleton_iff, Subtype.ext_iff]
    constructor
    ¬∑ intro hy
      have : (y : X) ‚àà U ‚à© E := ‚ü®hy, y.2‚ü©
      rw [hU_eq] at this
      exact this
    ¬∑ intro hy
      rw [hy]
      exact hxU
  have h_countable : Countable E := by
    classical
    have hsepE : SeparableSpace E := h_sep.separableSpace
    simpa using (TopologicalSpace.separableSpace_iff_countable (Œ± := E)).1 hsepE
  have : Œº E = 0 := E.countable_coe_iff.mp h_countable |>.measure_zero Œº
  exact hE.ne' this

end

open Interval

open scoped Interval in
theorem uIoc_ae_eq_interval [LinearOrder Œ±] {a b : Œ±} : Œô a b =·µê[Œº] [[a, b]] :=
  Ioc_ae_eq_Icc

end MeasureTheory
