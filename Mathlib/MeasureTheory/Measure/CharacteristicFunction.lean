/-
Copyright (c) 2024 Jakob Stiefel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob Stiefel, R√©my Degenne, Thomas Zhu
-/
import Mathlib.Analysis.Fourier.BoundedContinuousFunctionChar
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.MeasureTheory.Measure.FiniteMeasureExt
import Mathlib.MeasureTheory.Group.Convolution
import Mathlib.Analysis.Normed.Module.Dual

/-!
# Characteristic Function of a Finite Measure

This file defines the characteristic function of a finite measure on a topological vector space `V`.

The characteristic function of a finite measure `P` on `V` is the mapping
`W ‚Üí ‚ÑÇ, w => ‚à´ v, e (L v w) ‚àÇP`,
where `e` is a continuous additive character and `L : V ‚Üí‚Çó[‚Ñù] W ‚Üí‚Çó[‚Ñù] ‚Ñù` is a bilinear map.

A typical example is `V = W = ‚Ñù` and `L v w = v * w`.

The integral is expressed as `‚à´ v, char he hL w v ‚àÇP`, where `char he hL w` is the
bounded continuous function `fun v ‚Ü¶ e (L v w)` and `he`, `hL` are continuity hypotheses on `e`
and `L`.

## Main definitions

* `innerProbChar`: the bounded continuous map `x ‚Ü¶ exp(‚ü™x, t‚ü´ * I)` in an inner product space.
  This is `char` for the inner product bilinear map and the additive character `e = probChar`.
* `charFun Œº t`: the characteristic function of a measure `Œº` at `t` in an inner product space `E`.
  This is defined as `‚à´ x, exp (‚ü™x, t‚ü´ * I) ‚àÇŒº`, where `‚ü™x, t‚ü´` is the inner product on `E`.
  It is equal to `‚à´ v, innerProbChar w v ‚àÇP` (see `charFun_eq_integral_innerProbChar`).

## Main statements

* `ext_of_integral_char_eq`: Assume `e` and `L` are non-trivial. If the integrals of `char`
  with respect to two finite measures `P` and `P'` coincide, then `P = P'`.
* `Measure.ext_of_charFun`: If the characteristic functions `charFun` of two finite measures
  `Œº` and `ŒΩ` on a complete second-countable inner product space coincide, then `Œº = ŒΩ`.

-/

open BoundedContinuousFunction RealInnerProductSpace Real Complex ComplexConjugate

namespace BoundedContinuousFunction

variable {E : Type*} [SeminormedAddCommGroup E] [InnerProductSpace ‚Ñù E]

/-- The bounded continuous map `x ‚Ü¶ exp(‚ü™x, t‚ü´ * I)`. -/
noncomputable
def innerProbChar (t : E) : E ‚Üí·µá ‚ÑÇ :=
  char continuous_probChar (L := bilinFormOfRealInner) continuous_inner t

lemma innerProbChar_apply (t x : E) : innerProbChar t x = exp (‚ü™x, t‚ü´ * I) := rfl

@[simp]
lemma innerProbChar_zero : innerProbChar (0 : E) = 1 := by simp [innerProbChar]

end BoundedContinuousFunction

namespace MeasureTheory

variable {W : Type*} [AddCommGroup W] [Module ‚Ñù W] [TopologicalSpace W]
    {e : AddChar ‚Ñù Circle}

section ext

variable {V : Type*} [AddCommGroup V] [Module ‚Ñù V] [PseudoEMetricSpace V] [MeasurableSpace V]
    [BorelSpace V] [CompleteSpace V] [SecondCountableTopology V] {L : V ‚Üí‚Çó[‚Ñù] W ‚Üí‚Çó[‚Ñù] ‚Ñù}

/-- If the integrals of `char` with respect to two finite measures `P` and `P'` coincide, then
`P = P'`. -/
theorem ext_of_integral_char_eq (he : Continuous e) (he' : e ‚â† 1)
    (hL' : ‚àÄ v ‚â† 0, L v ‚â† 0) (hL : Continuous fun p : V √ó W ‚Ü¶ L p.1 p.2)
    {P P' : Measure V} [IsFiniteMeasure P] [IsFiniteMeasure P']
    (h : ‚àÄ w, ‚à´ v, char he hL w v ‚àÇP = ‚à´ v, char he hL w v ‚àÇP') :
    P = P' := by
  apply ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable
      (separatesPoints_charPoly he he' hL hL')
  intro _ hg
  simp only [mem_charPoly] at hg
  obtain ‚ü®w, hw‚ü© := hg
  rw [hw]
  have hsum (P : Measure V) [IsFiniteMeasure P] :
      ‚à´ v, ‚àë a ‚àà w.support, w a * e (L v a) ‚àÇP = ‚àë a ‚àà w.support, ‚à´ v, w a * e (L v a) ‚àÇP :=
    integral_finset_sum w.support
      fun a ha => Integrable.const_mul (integrable P (char he hL a)) _
  rw [hsum P, hsum P']
  apply Finset.sum_congr rfl fun i _ => ?_
  simp only [smul_eq_mul, MeasureTheory.integral_const_mul, mul_eq_mul_left_iff]
  exact Or.inl (h i)

end ext

section InnerProductSpace

variable {E : Type*} {mE : MeasurableSpace E} {Œº : Measure E} {t : E}

/-- The characteristic function of a measure in an inner product space. -/
noncomputable def charFun [Inner ‚Ñù E] (Œº : Measure E) (t : E) : ‚ÑÇ := ‚à´ x, exp (‚ü™x, t‚ü´ * I) ‚àÇŒº

lemma charFun_apply [Inner ‚Ñù E] (t : E) : charFun Œº t = ‚à´ x, exp (‚ü™x, t‚ü´ * I) ‚àÇŒº := rfl

lemma charFun_apply_real {Œº : Measure ‚Ñù} (t : ‚Ñù) :
    charFun Œº t = ‚à´ x, exp (t * x * I) ‚àÇŒº := by simp [charFun_apply]

variable [SeminormedAddCommGroup E] [InnerProductSpace ‚Ñù E]

@[simp]
lemma charFun_zero (Œº : Measure E) : charFun Œº 0 = Œº.real Set.univ := by
  simp [charFun_apply]

@[simp]
lemma charFun_zero_measure : charFun (0 : Measure E) t = 0 := by simp [charFun_apply]

@[simp]
lemma charFun_neg (t : E) : charFun Œº (-t) = conj (charFun Œº t) := by
  simp [charFun_apply, ‚Üê integral_conj, ‚Üê exp_conj]

/-- `charFun` as the integral of a bounded continuous function. -/
lemma charFun_eq_integral_innerProbChar : charFun Œº t = ‚à´ v, innerProbChar t v ‚àÇŒº := by
  simp [charFun_apply, innerProbChar_apply]

lemma charFun_eq_integral_probChar (t : E) : charFun Œº t = ‚à´ x, (probChar ‚ü™x, t‚ü´ : ‚ÑÇ) ‚àÇŒº := by
  simp [charFun_apply, probChar_apply]

/-- `charFun` is a Fourier integral for the inner product and the character `probChar`. -/
lemma charFun_eq_fourierIntegral (t : E) :
    charFun Œº t = VectorFourier.fourierIntegral probChar Œº bilinFormOfRealInner 1 (-t) := by
  simp [charFun_apply, VectorFourier.fourierIntegral_probChar]

/-- `charFun` is a Fourier integral for the inner product and the character `fourierChar`. -/
lemma charFun_eq_fourierIntegral' (t : E) :
    charFun Œº t
      = VectorFourier.fourierIntegral fourierChar Œº bilinFormOfRealInner 1 (-(2 * œÄ)‚Åª¬π ‚Ä¢ t) := by
  simp only [charFun_apply, VectorFourier.fourierIntegral, neg_smul,
    bilinFormOfRealInner_apply_apply, inner_neg_right, inner_smul_right, neg_neg,
    fourierChar_apply', Pi.ofNat_apply, Circle.smul_def, Circle.coe_exp, ofReal_mul, ofReal_ofNat,
    ofReal_inv, smul_eq_mul, mul_one]
  congr with x
  rw [‚Üê mul_assoc, mul_inv_cancel‚ÇÄ (by simp [pi_ne_zero]), one_mul]

lemma norm_charFun_le (t : E) : ‚ÄñcharFun Œº t‚Äñ ‚â§ Œº.real Set.univ := by
  rw [charFun_eq_fourierIntegral]
  exact (VectorFourier.norm_fourierIntegral_le_integral_norm _ _ _ _ _).trans_eq (by simp)

lemma norm_charFun_le_one [IsProbabilityMeasure Œº] (t : E) : ‚ÄñcharFun Œº t‚Äñ ‚â§ 1 :=
  (norm_charFun_le _).trans_eq (by simp)

lemma norm_one_sub_charFun_le_two [IsProbabilityMeasure Œº] : ‚Äñ1 - charFun Œº t‚Äñ ‚â§ 2 :=
  calc ‚Äñ1 - charFun Œº t‚Äñ
  _ ‚â§ ‚Äñ(1 : ‚ÑÇ)‚Äñ + ‚ÄñcharFun Œº t‚Äñ := norm_sub_le _ _
  _ ‚â§ 1 + 1 := by simp [norm_charFun_le_one]
  _ = 2 := by norm_num

@[measurability]
lemma stronglyMeasurable_charFun [OpensMeasurableSpace E] [SecondCountableTopology E] [SFinite Œº] :
    StronglyMeasurable (charFun Œº) :=
  (Measurable.stronglyMeasurable (by fun_prop)).integral_prod_left

@[fun_prop, measurability]
lemma measurable_charFun [OpensMeasurableSpace E] [SecondCountableTopology E] [SFinite Œº] :
    Measurable (charFun Œº) :=
  stronglyMeasurable_charFun.measurable

lemma intervalIntegrable_charFun {Œº : Measure ‚Ñù} [IsFiniteMeasure Œº] {a b : ‚Ñù} :
    IntervalIntegrable (charFun Œº) volume a b :=
  IntervalIntegrable.mono_fun' (g := fun _ ‚Ü¶ Œº.real Set.univ) (by simp)
    stronglyMeasurable_charFun.aestronglyMeasurable (ae_of_all _ norm_charFun_le)

lemma charFun_map_smul [BorelSpace E] [SecondCountableTopology E] (r : ‚Ñù) (t : E) :
    charFun (Œº.map (r ‚Ä¢ ¬∑)) t = charFun Œº (r ‚Ä¢ t) := by
  rw [charFun_apply, charFun_apply,
    integral_map (by fun_prop) (Measurable.aestronglyMeasurable <| by fun_prop)]
  simp_rw [inner_smul_right, ‚Üê real_inner_smul_left]

lemma charFun_map_mul {Œº : Measure ‚Ñù} (r t : ‚Ñù) :
    charFun (Œº.map (r * ¬∑)) t = charFun Œº (r * t) := charFun_map_smul r t

lemma integral_conv {E F : Type*} [AddMonoid E] [MeasurableSpace E] [MeasurableAdd‚ÇÇ E]
    {Œº ŒΩ : Measure E} [SFinite Œº] [SFinite ŒΩ]
    [NormedAddCommGroup F] [NormedSpace ‚Ñù F] {f : E ‚Üí F} (hf : Integrable f (Œº ‚àó ŒΩ)) :
    ‚à´ x, f x ‚àÇ(Œº ‚àó ŒΩ) = ‚à´ x, ‚à´ y, f (x + y) ‚àÇŒΩ ‚àÇŒº := by
  unfold Measure.conv
  rw [integral_map (by fun_prop) hf.1, integral_prod]
  exact (integrable_map_measure hf.1 (by fun_prop)).mp hf

variable {E : Type*} [MeasurableSpace E] {Œº ŒΩ : Measure E} {t : E}
  [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] [BorelSpace E] [SecondCountableTopology E]

/-- If the characteristic functions `charFun` of two finite measures `Œº` and `ŒΩ` on
a complete second-countable inner product space coincide, then `Œº = ŒΩ`. -/
theorem Measure.ext_of_charFun [CompleteSpace E]
    [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] (h : charFun Œº = charFun ŒΩ) :
    Œº = ŒΩ := by
  simp_rw [funext_iff, charFun_eq_integral_innerProbChar] at h
  refine ext_of_integral_char_eq continuous_probChar probChar_ne_one (L := bilinFormOfRealInner)
    ?_ ?_ h
  ¬∑ exact fun v hv ‚Ü¶ DFunLike.ne_iff.mpr ‚ü®v, inner_self_ne_zero.mpr hv‚ü©
  ¬∑ exact continuous_inner

/-- The characteristic function of a convolution of measures
is the product of the respective characteristic functions. -/
lemma charFun_conv [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] (t : E) :
    charFun (Œº ‚àó ŒΩ) t = charFun Œº t * charFun ŒΩ t := by
  simp_rw [charFun_apply]
  rw [integral_conv]
  ¬∑ simp_rw [inner_add_left]
    push_cast
    simp_rw [add_mul, Complex.exp_add, integral_const_mul, integral_mul_const]
  ¬∑ -- todo: extract lemma about integrability wrt conv?
    unfold Measure.conv
    rw [integrable_map_measure]
    ¬∑ apply (integrable_const (1 : ‚Ñù)).mono
      ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
      ¬∑ simp
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
    ¬∑ fun_prop

end InnerProductSpace

lemma _root_.IsBoundedBilinearMap.symm {E F G ùïú : Type*} [NontriviallyNormedField ùïú]
    [SeminormedAddCommGroup E] [NormedSpace ùïú E] [SeminormedAddCommGroup F] [NormedSpace ùïú F]
    [SeminormedAddCommGroup G] [NormedSpace ùïú G]
    {f : E √ó F ‚Üí G} (h : IsBoundedBilinearMap ùïú f) :
    IsBoundedBilinearMap ùïú (fun p ‚Ü¶ f (p.2, p.1)) where
  add_left x‚ÇÅ x‚ÇÇ y := h.add_right _ _ _
  smul_left c x y := h.smul_right _ _ _
  add_right x y‚ÇÅ y‚ÇÇ := h.add_left _ _ _
  smul_right c x y := h.smul_left _ _ _
  bound := by
    obtain ‚ü®C, hC_pos, hC‚ü© := h.bound
    exact ‚ü®C, hC_pos, fun x y ‚Ü¶ (hC y x).trans_eq (by ring)‚ü©

lemma _root_.ContinuousLinearMap.comp_inl_add_comp_inr
    {E F : Type*} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [SeminormedAddCommGroup F] [NormedSpace ‚Ñù F]
    (L : E √ó F ‚ÜíL[‚Ñù] ‚Ñù) (v : E √ó F) :
    L.comp (.inl ‚Ñù E F) v.1 + L.comp (.inr ‚Ñù E F) v.2 = L v := by
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.comp_apply,
    ContinuousLinearMap.inl_apply, ContinuousLinearMap.inr_apply, ‚Üê ContinuousLinearMap.map_add]
  simp

namespace BoundedContinuousFunction

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-- The bounded continuous function `x ‚Ü¶ exp (L x * I)`, for a continuous linear form `L`. -/
noncomputable
def probCharCLM (L : E ‚ÜíL[‚Ñù] ‚Ñù) : E ‚Üí·µá ‚ÑÇ :=
  char continuous_probChar (L := isBoundedBilinearMap_apply.symm.toContinuousLinearMap.toLinearMap‚ÇÇ)
    isBoundedBilinearMap_apply.symm.continuous L

lemma probCharCLM_apply (L : E ‚ÜíL[‚Ñù] ‚Ñù) (x : E) : probCharCLM L x = exp (L x * I) := rfl

@[simp]
lemma probCharCLM_zero : probCharCLM (0 : E ‚ÜíL[‚Ñù] ‚Ñù) = 1 := by simp [probCharCLM]

end BoundedContinuousFunction

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {mE : MeasurableSpace E}
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F] {mF : MeasurableSpace F}
  {Œº : Measure E} {ŒΩ : Measure F}

/-- The characteristic function of a measure in a normed space, function from `E ‚ÜíL[‚Ñù] ‚Ñù` to `‚ÑÇ`
with `charFunCLM Œº L = ‚à´ v, exp (L v * I) ‚àÇŒº`. -/
noncomputable
def charFunCLM (Œº : Measure E) (L : E ‚ÜíL[‚Ñù] ‚Ñù) : ‚ÑÇ :=
  ‚à´ v, BoundedContinuousFunction.probCharCLM L v ‚àÇŒº

lemma charFunCLM_apply (L : E ‚ÜíL[‚Ñù] ‚Ñù) : charFunCLM Œº L = ‚à´ v, exp (L v * I) ‚àÇŒº := rfl

@[simp]
lemma charFunCLM_dirac [OpensMeasurableSpace E] {x : E} (L : E ‚ÜíL[‚Ñù] ‚Ñù) :
    charFunCLM (Measure.dirac x) L = cexp (L x * I) := by
  rw [charFunCLM_apply, integral_dirac]

lemma charFunCLM_prod [SFinite Œº] [SFinite ŒΩ] (L : E √ó F ‚ÜíL[‚Ñù] ‚Ñù) :
    charFunCLM (Œº.prod ŒΩ) L
      = charFunCLM Œº (L.comp (.inl ‚Ñù E F)) * charFunCLM ŒΩ (L.comp (.inr ‚Ñù E F)) := by
  let L‚ÇÅ : E ‚ÜíL[‚Ñù] ‚Ñù := L.comp (.inl ‚Ñù E F)
  let L‚ÇÇ : F ‚ÜíL[‚Ñù] ‚Ñù := L.comp (.inr ‚Ñù E F)
  simp_rw [charFunCLM_apply, ‚Üê L.comp_inl_add_comp_inr, ofReal_add, add_mul,
    Complex.exp_add]
  rw [integral_prod_mul (f := fun x ‚Ü¶ cexp ((L‚ÇÅ x * I))) (g := fun x ‚Ü¶ cexp ((L‚ÇÇ x * I)))]

lemma charFunCLM_eq_charFun_map_one [OpensMeasurableSpace E] (L : E ‚ÜíL[‚Ñù] ‚Ñù) :
    charFunCLM Œº L = charFun (Œº.map L) 1 := by
  rw [charFunCLM_apply]
  have : ‚à´ x, cexp (L x * I) ‚àÇŒº = ‚à´ x, cexp (x * I) ‚àÇ(Œº.map L) := by
    rw [integral_map]
    ¬∑ fun_prop
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
  rw [this, charFun_apply]
  simp

lemma charFun_map_eq_charFunCLM_smul [OpensMeasurableSpace E] (L : E ‚ÜíL[‚Ñù] ‚Ñù) (u : ‚Ñù) :
    charFun (Œº.map L) u = charFunCLM Œº (u ‚Ä¢ L) := by
  rw [charFunCLM_apply]
  have : ‚à´ x, cexp ((u ‚Ä¢ L) x * I) ‚àÇŒº = ‚à´ x, cexp (u * x * I) ‚àÇ(Œº.map L) := by
    rw [integral_map]
    ¬∑ simp
    ¬∑ fun_prop
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
  rw [this, charFun_apply]
  simp

lemma charFunCLM_map [OpensMeasurableSpace E] [BorelSpace F] {Œº : Measure E}
    (L : E ‚ÜíL[‚Ñù] F) (L' : F ‚ÜíL[‚Ñù] ‚Ñù) :
    charFunCLM (Œº.map L) L' = charFunCLM Œº (L'.comp L) := by
  rw [charFunCLM_eq_charFun_map_one, charFunCLM_eq_charFun_map_one,
    Measure.map_map (by fun_prop) (by fun_prop)]
  simp

variable [CompleteSpace E] [BorelSpace E] [SecondCountableTopology E]

/-- If two finite measures have the same characteristic function, then they are equal. -/
theorem ext_of_charFunCLM {Œº ŒΩ : Measure E} [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ]
    (h : charFunCLM Œº = charFunCLM ŒΩ) :
    Œº = ŒΩ := by
  refine ext_of_integral_char_eq continuous_probChar probChar_ne_one
    ?_ ?_ (fun L ‚Ü¶ funext_iff.mp h L)
  ¬∑ intro v hv
    rw [ne_eq, LinearMap.ext_iff]
    simp only [ContinuousLinearMap.toLinearMap‚ÇÇ_apply, LinearMap.zero_apply, not_forall]
    change ‚àÉ L : E ‚ÜíL[‚Ñù] ‚Ñù, L v ‚â† 0
    by_contra! h
    exact hv (NormedSpace.eq_zero_of_forall_dual_eq_zero _ h)
  ¬∑ exact isBoundedBilinearMap_apply.symm.continuous

end MeasureTheory
