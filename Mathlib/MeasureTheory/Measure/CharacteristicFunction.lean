/-
Copyright (c) 2024 Jakob Stiefel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob Stiefel
-/
import Mathlib.Analysis.Fourier.Char
import Mathlib.MeasureTheory.Measure.FiniteMeasureExt

/-!
# Characteristic Function of a Finite Measure

This file defines the characteristic function of a finite measure on a topological vector space
`V`.

The characteristic function of a finite measure `P` on `V` is the mapping
`W â†’ â„‚, w => âˆ« v, e (-L v w) âˆ‚P`,
where `e` is a continuous additive character and `L : V â†’â‚—[â„] W â†’â‚—[â„] â„` is a bilinear map.

A typical example is `V = W = â„` and `L v w = v * w`.

## Main definition

`charFun P hL : W â†’ â„‚`: The characteristic function of a Measure `P`, evaluated at `w`, is the
integral of `char he hL w` with respect to `P`, for the standard choice of `e = Circle.expAddChar`.

## Main statements

- `ext_of_integral_char_eq`: Assume `e` and `L` are non-trivial. If the integrals of `char`
  with respect to two finite measures `P` and `P'` coincide, then `P = P'`.
- `ext_of_charFun_eq`: If the characteristic functions of two finite measures `P` and `P'` are
  equal, then `P = P'`. In other words, characteristic functions separate finite measures.

-/

open Filter BoundedContinuousFunction Complex

namespace MeasureTheory

variable {V W : Type*} [AddCommGroup V] [Module â„ V] [TopologicalSpace V]
    [AddCommGroup W] [Module â„ W] [TopologicalSpace W]
    {e : AddChar â„ Circle} {L : V â†’â‚—[â„] W â†’â‚—[â„] â„}
    {he : Continuous e} {hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2}

/--
The characteristic function of a Measure `P`, evaluated at `w` is the integral of
`fun v â†¦ e (L v w)` with respect to `P` for the standard choice of `e = Circle.exp`.
-/
noncomputable
def charFun [MeasurableSpace V] (P : Measure V) (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2)
    (w : W) : â„‚ :=
  âˆ« v, char Circle.continuous_expAddChar hL w v âˆ‚P

section ext

variable {V : Type*} [AddCommGroup V] [Module â„ V] [PseudoEMetricSpace V] [MeasurableSpace V]
    [BorelSpace V] [CompleteSpace V] [SecondCountableTopology V] {L : V â†’â‚—[â„] W â†’â‚—[â„] â„}
    {ð•œ : Type*} [RCLike ð•œ]

/--
If the integrals of `char` with respect to two finite measures `P` and `P'` coincide, then
`P = P'`.
-/
theorem ext_of_integral_char_eq (he : Continuous e) (he' : e â‰  1)
    (hL' : âˆ€ v â‰  0, L v â‰  0) (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2)
    (P P' : Measure V) [IsFiniteMeasure P] [IsFiniteMeasure P']
    (h : âˆ€ w, âˆ« v, char he hL w v âˆ‚P = âˆ« v, char he hL w v âˆ‚P') :
    P = P' := by
  apply ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable
      (separatesPoints_charPoly he he' hL hL')
  intro _ hg
  simp only [mem_charPoly] at hg
  obtain âŸ¨w, hwâŸ© := hg
  rw [hw]
  have hsum (P : Measure V) [IsFiniteMeasure P] :
      âˆ« v, âˆ‘ a âˆˆ w.support, w a * e (L v a) âˆ‚P = âˆ‘ a âˆˆ w.support, âˆ« v, w a * e (L v a) âˆ‚P :=
    integral_finset_sum w.support
      fun a ha => Integrable.const_mul (integrable P (char he hL a)) _
  rw [hsum P, hsum P']
  apply Finset.sum_congr rfl fun i _ => ?_
  simp only [smul_eq_mul, MeasureTheory.integral_mul_left, mul_eq_mul_left_iff]
  exact Or.inl (h i)

lemma expAddChar_ne_one : Circle.expAddChar â‰  1 := by
  rw [DFunLike.ne_iff]
  use Real.pi
  simp only [Circle.expAddChar, AddChar.coe_mk, AddChar.one_apply]
  intro h
  have heq := congrArg Subtype.val h
  rw [Circle.coe_exp Real.pi, Complex.exp_pi_mul_I] at heq
  norm_num at heq

/--
If the characteristic functions of two finite measures `P` and `P'` are equal, then `P = P'`. In
other words, characteristic functions separate finite measures.
-/
theorem ext_of_charFun_eq
    (hL' : âˆ€ v â‰  0, L v â‰  0) (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2)
    (P P' : Measure V) [IsFiniteMeasure P] [IsFiniteMeasure P'] (h : charFun P hL = charFun P' hL) :
    P = P' :=
  ext_of_integral_char_eq Circle.continuous_expAddChar expAddChar_ne_one hL' hL P P'
    (fun w => congrFun h w)

end ext

end MeasureTheory
