/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Measure.MeasureSpace
import Mathlib.MeasureTheory.Measure.Regular
import Mathlib.Topology.Sets.Compacts

#align_import measure_theory.measure.content from "leanprover-community/mathlib"@"d39590fc8728fbf6743249802486f8c91ffe07bc"

/-!
# Contents

In this file we work with *contents*. A content `Î»` is a function from a certain class of subsets
(such as the compact subsets) to `â„â‰¥0` that is
* additive: If `Kâ‚` and `Kâ‚‚` are disjoint sets in the domain of `Î»`,
  then `Î»(Kâ‚ âˆª Kâ‚‚) = Î»(Kâ‚) + Î»(Kâ‚‚)`;
* subadditive: If `Kâ‚` and `Kâ‚‚` are in the domain of `Î»`, then `Î»(Kâ‚ âˆª Kâ‚‚) â‰¤ Î»(Kâ‚) + Î»(Kâ‚‚)`;
* monotone: If `Kâ‚ âŠ† Kâ‚‚` are in the domain of `Î»`, then `Î»(Kâ‚) â‰¤ Î»(Kâ‚‚)`.

We show that:
* Given a content `Î»` on compact sets, let us define a function `Î»*` on open sets, by letting
  `Î»* U` be the supremum of `Î» K` for `K` included in `U`. This is a countably subadditive map that
  vanishes at `âˆ…`. In Halmos (1950) this is called the *inner content* `Î»*` of `Î»`, and formalized
  as `innerContent`.
* Given an inner content, we define an outer measure `Î¼*`, by letting `Î¼* E` be the infimum of
  `Î»* U` over the open sets `U` containing `E`. This is indeed an outer measure. It is formalized
  as `outerMeasure`.
* Restricting this outer measure to Borel sets gives a regular measure `Î¼`.

We define bundled contents as `Content`.
In this file we only work on contents on compact sets, and inner contents on open sets, and both
contents and inner contents map into the extended nonnegative reals. However, in other applications
other choices can be made, and it is not a priori clear what the best interface should be.

## Main definitions

For `Î¼ : Content G`, we define
* `Î¼.innerContent` : the inner content associated to `Î¼`.
* `Î¼.outerMeasure` : the outer measure associated to `Î¼`.
* `Î¼.measure`      : the Borel measure associated to `Î¼`.

We prove that, on a locally compact space, the measure `Î¼.measure` is regular.

## References

* Paul Halmos (1950), Measure Theory, Â§53
* <https://en.wikipedia.org/wiki/Content_(measure_theory)>
-/


universe u v w

noncomputable section

open Set TopologicalSpace

open NNReal ENNReal MeasureTheory

namespace MeasureTheory

variable {G : Type w} [TopologicalSpace G]

/-- A content is an additive function on compact sets taking values in `â„â‰¥0`. It is a device
from which one can define a measure. -/
structure Content (G : Type w) [TopologicalSpace G] where
  toFun : Compacts G â†’ â„â‰¥0
  mono' : âˆ€ Kâ‚ Kâ‚‚ : Compacts G, (Kâ‚ : Set G) âŠ† Kâ‚‚ â†’ toFun Kâ‚ â‰¤ toFun Kâ‚‚
  sup_disjoint' :
    âˆ€ Kâ‚ Kâ‚‚ : Compacts G, Disjoint (Kâ‚ : Set G) Kâ‚‚ â†’ toFun (Kâ‚ âŠ” Kâ‚‚) = toFun Kâ‚ + toFun Kâ‚‚
  sup_le' : âˆ€ Kâ‚ Kâ‚‚ : Compacts G, toFun (Kâ‚ âŠ” Kâ‚‚) â‰¤ toFun Kâ‚ + toFun Kâ‚‚
#align measure_theory.content MeasureTheory.Content

instance : Inhabited (Content G) :=
  âŸ¨{  toFun := fun _ => 0
      mono' := by simp
                  -- ğŸ‰ no goals
      sup_disjoint' := by simp
                          -- ğŸ‰ no goals
      sup_le' := by simp }âŸ©
                    -- ğŸ‰ no goals

/-- Although the `toFun` field of a content takes values in `â„â‰¥0`, we register a coercion to
functions taking values in `â„â‰¥0âˆ` as most constructions below rely on taking iSups and iInfs, which
is more convenient in a complete lattice, and aim at constructing a measure. -/
instance : CoeFun (Content G) fun _ => Compacts G â†’ â„â‰¥0âˆ :=
  âŸ¨fun Î¼ s => Î¼.toFun sâŸ©

namespace Content

variable (Î¼ : Content G)

theorem apply_eq_coe_toFun (K : Compacts G) : Î¼ K = Î¼.toFun K :=
  rfl
#align measure_theory.content.apply_eq_coe_to_fun MeasureTheory.Content.apply_eq_coe_toFun

theorem mono (Kâ‚ Kâ‚‚ : Compacts G) (h : (Kâ‚ : Set G) âŠ† Kâ‚‚) : Î¼ Kâ‚ â‰¤ Î¼ Kâ‚‚ := by
  simp [apply_eq_coe_toFun, Î¼.mono' _ _ h]
  -- ğŸ‰ no goals
#align measure_theory.content.mono MeasureTheory.Content.mono

theorem sup_disjoint (Kâ‚ Kâ‚‚ : Compacts G) (h : Disjoint (Kâ‚ : Set G) Kâ‚‚) :
    Î¼ (Kâ‚ âŠ” Kâ‚‚) = Î¼ Kâ‚ + Î¼ Kâ‚‚ := by
  simp [apply_eq_coe_toFun, Î¼.sup_disjoint' _ _ h]
  -- ğŸ‰ no goals
#align measure_theory.content.sup_disjoint MeasureTheory.Content.sup_disjoint

theorem sup_le (Kâ‚ Kâ‚‚ : Compacts G) : Î¼ (Kâ‚ âŠ” Kâ‚‚) â‰¤ Î¼ Kâ‚ + Î¼ Kâ‚‚ := by
  simp only [apply_eq_coe_toFun]
  -- âŠ¢ â†‘(toFun Î¼ (Kâ‚ âŠ” Kâ‚‚)) â‰¤ â†‘(toFun Î¼ Kâ‚) + â†‘(toFun Î¼ Kâ‚‚)
  norm_cast
  -- âŠ¢ toFun Î¼ (Kâ‚ âŠ” Kâ‚‚) â‰¤ toFun Î¼ Kâ‚ + toFun Î¼ Kâ‚‚
  exact Î¼.sup_le' _ _
  -- ğŸ‰ no goals
#align measure_theory.content.sup_le MeasureTheory.Content.sup_le

theorem lt_top (K : Compacts G) : Î¼ K < âˆ :=
  ENNReal.coe_lt_top
#align measure_theory.content.lt_top MeasureTheory.Content.lt_top

theorem empty : Î¼ âŠ¥ = 0 := by
  have := Î¼.sup_disjoint' âŠ¥ âŠ¥
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) âŠ¥ = 0
  simpa [apply_eq_coe_toFun] using this
  -- ğŸ‰ no goals
#align measure_theory.content.empty MeasureTheory.Content.empty

/-- Constructing the inner content of a content. From a content defined on the compact sets, we
  obtain a function defined on all open sets, by taking the supremum of the content of all compact
  subsets. -/
def innerContent (U : Opens G) : â„â‰¥0âˆ :=
  â¨† (K : Compacts G) (_ : (K : Set G) âŠ† U), Î¼ K
#align measure_theory.content.inner_content MeasureTheory.Content.innerContent

theorem le_innerContent (K : Compacts G) (U : Opens G) (h2 : (K : Set G) âŠ† U) :
    Î¼ K â‰¤ Î¼.innerContent U :=
  le_iSup_of_le K <| le_iSup (fun _ â†¦ (Î¼.toFun K : â„â‰¥0âˆ)) h2
#align measure_theory.content.le_inner_content MeasureTheory.Content.le_innerContent

theorem innerContent_le (U : Opens G) (K : Compacts G) (h2 : (U : Set G) âŠ† K) :
    Î¼.innerContent U â‰¤ Î¼ K :=
  iSupâ‚‚_le fun _ hK' => Î¼.mono _ _ (Subset.trans hK' h2)
#align measure_theory.content.inner_content_le MeasureTheory.Content.innerContent_le

theorem innerContent_of_isCompact {K : Set G} (h1K : IsCompact K) (h2K : IsOpen K) :
    Î¼.innerContent âŸ¨K, h2KâŸ© = Î¼ âŸ¨K, h1KâŸ© :=
  le_antisymm (iSupâ‚‚_le fun _ hK' => Î¼.mono _ âŸ¨K, h1KâŸ© hK') (Î¼.le_innerContent _ _ Subset.rfl)
#align measure_theory.content.inner_content_of_is_compact MeasureTheory.Content.innerContent_of_isCompact

theorem innerContent_bot : Î¼.innerContent âŠ¥ = 0 := by
  refine' le_antisymm _ (zero_le _)
  -- âŠ¢ innerContent Î¼ âŠ¥ â‰¤ 0
  rw [â† Î¼.empty]
  -- âŠ¢ innerContent Î¼ âŠ¥ â‰¤ (fun s => â†‘(toFun Î¼ s)) âŠ¥
  refine' iSupâ‚‚_le fun K hK => _
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) K â‰¤ (fun s => â†‘(toFun Î¼ s)) âŠ¥
  have : K = âŠ¥ := by
    ext1
    rw [subset_empty_iff.mp hK, Compacts.coe_bot]
  rw [this]
  -- ğŸ‰ no goals
#align measure_theory.content.inner_content_bot MeasureTheory.Content.innerContent_bot

/-- This is "unbundled", because that is required for the API of `inducedOuterMeasure`. -/
theorem innerContent_mono â¦ƒU V : Set Gâ¦„ (hU : IsOpen U) (hV : IsOpen V) (h2 : U âŠ† V) :
    Î¼.innerContent âŸ¨U, hUâŸ© â‰¤ Î¼.innerContent âŸ¨V, hVâŸ© :=
  biSup_mono fun _ hK => hK.trans h2
#align measure_theory.content.inner_content_mono MeasureTheory.Content.innerContent_mono

theorem innerContent_exists_compact {U : Opens G} (hU : Î¼.innerContent U â‰  âˆ) {Îµ : â„â‰¥0}
    (hÎµ : Îµ â‰  0) : âˆƒ K : Compacts G, (K : Set G) âŠ† U âˆ§ Î¼.innerContent U â‰¤ Î¼ K + Îµ := by
  have h'Îµ := ENNReal.coe_ne_zero.2 hÎµ
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
  cases' le_or_lt (Î¼.innerContent U) Îµ with h h
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
  Â· exact âŸ¨âŠ¥, empty_subset _, le_add_left hâŸ©
    -- ğŸ‰ no goals
  have hâ‚‚ := ENNReal.sub_lt_self hU h.ne_bot h'Îµ
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
  conv at hâ‚‚ => rhs; rw [innerContent]
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
  simp only [lt_iSup_iff] at hâ‚‚
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
  rcases hâ‚‚ with âŸ¨U, h1U, h2UâŸ©; refine' âŸ¨U, h1U, _âŸ©
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘Uâœ âˆ§ innerContent Î¼ Uâœ â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
                                -- âŠ¢ innerContent Î¼ Uâœ â‰¤ (fun s => â†‘(toFun Î¼ s)) U + â†‘Îµ
  rw [â† tsub_le_iff_right]; exact le_of_lt h2U
  -- âŠ¢ innerContent Î¼ Uâœ - â†‘Îµ â‰¤ (fun s => â†‘(toFun Î¼ s)) U
                            -- ğŸ‰ no goals
#align measure_theory.content.inner_content_exists_compact MeasureTheory.Content.innerContent_exists_compact

/-- The inner content of a supremum of opens is at most the sum of the individual inner contents. -/
theorem innerContent_iSup_nat [T2Space G] (U : â„• â†’ Opens G) :
    Î¼.innerContent (â¨† i : â„•, U i) â‰¤ âˆ‘' i : â„•, Î¼.innerContent (U i) := by
  have h3 : âˆ€ (t : Finset â„•) (K : â„• â†’ Compacts G), Î¼ (t.sup K) â‰¤ t.sum fun i => Î¼ (K i) := by
    intro t K
    refine' Finset.induction_on t _ _
    Â· simp only [Î¼.empty, nonpos_iff_eq_zero, Finset.sum_empty, Finset.sup_empty]
    Â· intro n s hn ih
      rw [Finset.sup_insert, Finset.sum_insert hn]
      exact le_trans (Î¼.sup_le _ _) (add_le_add_left ih _)
  refine' iSupâ‚‚_le fun K hK => _
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) K â‰¤ âˆ‘' (i : â„•), innerContent Î¼ (U i)
  obtain âŸ¨t, htâŸ© :=
    K.isCompact.elim_finite_subcover _ (fun i => (U i).isOpen) (by rwa [â† Opens.coe_iSup])
  rcases K.isCompact.finite_compact_cover t (SetLike.coe âˆ˜ U) (fun i _ => (U i).isOpen) ht with
    âŸ¨K', h1K', h2K', h3K'âŸ©
  let L : â„• â†’ Compacts G := fun n => âŸ¨K' n, h1K' nâŸ©
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) K â‰¤ âˆ‘' (i : â„•), innerContent Î¼ (U i)
  convert le_trans (h3 t L) _
  -- âŠ¢ K = Finset.sup t L
  Â· ext1
    -- âŠ¢ â†‘K = â†‘(Finset.sup t L)
    rw [Compacts.coe_finset_sup, Finset.sup_eq_iSup]
    -- âŠ¢ â†‘K = â¨† (a : â„•) (_ : a âˆˆ t), â†‘(L a)
    exact h3K'
    -- ğŸ‰ no goals
  refine' le_trans (Finset.sum_le_sum _) (ENNReal.sum_le_tsum t)
  -- âŠ¢ âˆ€ (i : â„•), i âˆˆ t â†’ (fun s => â†‘(toFun Î¼ s)) (L i) â‰¤ innerContent Î¼ (U i)
  intro i _
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) (L i) â‰¤ innerContent Î¼ (U i)
  refine' le_trans _ (le_iSup _ (L i))
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) (L i) â‰¤ â¨† (_ : â†‘(L i) âŠ† â†‘(U i)), (fun s => â†‘(toFun Î¼ â€¦
  refine' le_trans _ (le_iSup _ (h2K' i))
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) (L i) â‰¤ (fun s => â†‘(toFun Î¼ s)) (L i)
  rfl
  -- ğŸ‰ no goals
#align measure_theory.content.inner_content_Sup_nat MeasureTheory.Content.innerContent_iSup_nat

/-- The inner content of a union of sets is at most the sum of the individual inner contents.
  This is the "unbundled" version of `innerContent_iSup_nat`.
  It is required for the API of `inducedOuterMeasure`. -/
theorem innerContent_iUnion_nat [T2Space G] â¦ƒU : â„• â†’ Set Gâ¦„ (hU : âˆ€ i : â„•, IsOpen (U i)) :
    Î¼.innerContent âŸ¨â‹ƒ i : â„•, U i, isOpen_iUnion hUâŸ© â‰¤ âˆ‘' i : â„•, Î¼.innerContent âŸ¨U i, hU iâŸ© := by
  have := Î¼.innerContent_iSup_nat fun i => âŸ¨U i, hU iâŸ©
  -- âŠ¢ innerContent Î¼ { carrier := â‹ƒ (i : â„•), U i, is_open' := (_ : IsOpen (â‹ƒ (i :  â€¦
  rwa [Opens.iSup_def] at this
  -- ğŸ‰ no goals
#align measure_theory.content.inner_content_Union_nat MeasureTheory.Content.innerContent_iUnion_nat

theorem innerContent_comap (f : G â‰ƒâ‚œ G) (h : âˆ€ â¦ƒK : Compacts Gâ¦„, Î¼ (K.map f f.continuous) = Î¼ K)
    (U : Opens G) : Î¼.innerContent (Opens.comap f.toContinuousMap U) = Î¼.innerContent U := by
  refine' (Compacts.equiv f).surjective.iSup_congr _ fun K => iSup_congr_Prop image_subset_iff _
  -- âŠ¢ â†‘K âŠ† â†‘(â†‘(Opens.comap (Homeomorph.toContinuousMap f)) U) â†’ (fun s => â†‘(toFun  â€¦
  intro hK
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) (â†‘(Compacts.equiv f) K) = (fun s => â†‘(toFun Î¼ s)) K
  simp only [Equiv.coe_fn_mk, Subtype.mk_eq_mk, Compacts.equiv]
  -- âŠ¢ â†‘(toFun Î¼ (Compacts.map â†‘f (_ : Continuous â†‘f) K)) = â†‘(toFun Î¼ K)
  apply h
  -- ğŸ‰ no goals
#align measure_theory.content.inner_content_comap MeasureTheory.Content.innerContent_comap

@[to_additive]
theorem is_mul_left_invariant_innerContent [Group G] [TopologicalGroup G]
    (h : âˆ€ (g : G) {K : Compacts G}, Î¼ (K.map _ <| continuous_mul_left g) = Î¼ K) (g : G)
    (U : Opens G) :
    Î¼.innerContent (Opens.comap (Homeomorph.mulLeft g).toContinuousMap U) = Î¼.innerContent U := by
  convert Î¼.innerContent_comap (Homeomorph.mulLeft g) (fun K => h g) U
  -- ğŸ‰ no goals
#align measure_theory.content.is_mul_left_invariant_inner_content MeasureTheory.Content.is_mul_left_invariant_innerContent
#align measure_theory.content.is_add_left_invariant_inner_content MeasureTheory.Content.is_add_left_invariant_innerContent

@[to_additive]
theorem innerContent_pos_of_is_mul_left_invariant [T2Space G] [Group G] [TopologicalGroup G]
    (h3 : âˆ€ (g : G) {K : Compacts G}, Î¼ (K.map _ <| continuous_mul_left g) = Î¼ K) (K : Compacts G)
    (hK : Î¼ K â‰  0) (U : Opens G) (hU : (U : Set G).Nonempty) : 0 < Î¼.innerContent U := by
  have : (interior (U : Set G)).Nonempty
  -- âŠ¢ Set.Nonempty (interior â†‘U)
  rwa [U.isOpen.interior_eq]
  -- âŠ¢ 0 < innerContent Î¼ U
  rcases compact_covered_by_mul_left_translates K.2 this with âŸ¨s, hsâŸ©
  -- âŠ¢ 0 < innerContent Î¼ U
  suffices Î¼ K â‰¤ s.card * Î¼.innerContent U by
    exact (ENNReal.mul_pos_iff.mp <| hK.bot_lt.trans_le this).2
  have : (K : Set G) âŠ† â†‘(â¨† g âˆˆ s, Opens.comap (Homeomorph.mulLeft g).toContinuousMap U) := by
    simpa only [Opens.iSup_def, Opens.coe_comap, Subtype.coe_mk]
  refine' (Î¼.le_innerContent _ _ this).trans _
  -- âŠ¢ innerContent Î¼ (â¨† (g : G) (_ : g âˆˆ s), â†‘(Opens.comap (Homeomorph.toContinuou â€¦
  refine'
    (rel_iSup_sum Î¼.innerContent Î¼.innerContent_bot (Â· â‰¤ Â·) Î¼.innerContent_iSup_nat _ _).trans _
  simp only [Î¼.is_mul_left_invariant_innerContent h3, Finset.sum_const, nsmul_eq_mul, le_refl]
  -- ğŸ‰ no goals
#align measure_theory.content.inner_content_pos_of_is_mul_left_invariant MeasureTheory.Content.innerContent_pos_of_is_mul_left_invariant
#align measure_theory.content.inner_content_pos_of_is_add_left_invariant MeasureTheory.Content.innerContent_pos_of_is_add_left_invariant

theorem innerContent_mono' â¦ƒU V : Set Gâ¦„ (hU : IsOpen U) (hV : IsOpen V) (h2 : U âŠ† V) :
    Î¼.innerContent âŸ¨U, hUâŸ© â‰¤ Î¼.innerContent âŸ¨V, hVâŸ© :=
  biSup_mono fun _ hK => hK.trans h2
#align measure_theory.content.inner_content_mono' MeasureTheory.Content.innerContent_mono'

section OuterMeasure

/-- Extending a content on compact sets to an outer measure on all sets. -/
protected def outerMeasure : OuterMeasure G :=
  inducedOuterMeasure (fun U hU => Î¼.innerContent âŸ¨U, hUâŸ©) isOpen_empty Î¼.innerContent_bot
#align measure_theory.content.outer_measure MeasureTheory.Content.outerMeasure

variable [T2Space G]

theorem outerMeasure_opens (U : Opens G) : Î¼.outerMeasure U = Î¼.innerContent U :=
  inducedOuterMeasure_eq' (fun _ => isOpen_iUnion) Î¼.innerContent_iUnion_nat Î¼.innerContent_mono U.2
#align measure_theory.content.outer_measure_opens MeasureTheory.Content.outerMeasure_opens

theorem outerMeasure_of_isOpen (U : Set G) (hU : IsOpen U) :
    Î¼.outerMeasure U = Î¼.innerContent âŸ¨U, hUâŸ© :=
  Î¼.outerMeasure_opens âŸ¨U, hUâŸ©
#align measure_theory.content.outer_measure_of_is_open MeasureTheory.Content.outerMeasure_of_isOpen

theorem outerMeasure_le (U : Opens G) (K : Compacts G) (hUK : (U : Set G) âŠ† K) :
    Î¼.outerMeasure U â‰¤ Î¼ K :=
  (Î¼.outerMeasure_opens U).le.trans <| Î¼.innerContent_le U K hUK
#align measure_theory.content.outer_measure_le MeasureTheory.Content.outerMeasure_le

theorem le_outerMeasure_compacts (K : Compacts G) : Î¼ K â‰¤ Î¼.outerMeasure K := by
  rw [Content.outerMeasure, inducedOuterMeasure_eq_iInf]
  Â· exact le_iInf fun U => le_iInf fun hU => le_iInf <| Î¼.le_innerContent K âŸ¨U, hUâŸ©
    -- ğŸ‰ no goals
  Â· exact fun U hU => isOpen_iUnion hU
    -- ğŸ‰ no goals
  Â· exact Î¼.innerContent_iUnion_nat
    -- ğŸ‰ no goals
  Â· exact Î¼.innerContent_mono
    -- ğŸ‰ no goals
#align measure_theory.content.le_outer_measure_compacts MeasureTheory.Content.le_outerMeasure_compacts

theorem outerMeasure_eq_iInf (A : Set G) :
    Î¼.outerMeasure A = â¨… (U : Set G) (hU : IsOpen U) (_ : A âŠ† U), Î¼.innerContent âŸ¨U, hUâŸ© :=
  inducedOuterMeasure_eq_iInf _ Î¼.innerContent_iUnion_nat Î¼.innerContent_mono A
#align measure_theory.content.outer_measure_eq_infi MeasureTheory.Content.outerMeasure_eq_iInf

theorem outerMeasure_interior_compacts (K : Compacts G) : Î¼.outerMeasure (interior K) â‰¤ Î¼ K :=
  (Î¼.outerMeasure_opens <| Opens.interior K).le.trans <| Î¼.innerContent_le _ _ interior_subset
#align measure_theory.content.outer_measure_interior_compacts MeasureTheory.Content.outerMeasure_interior_compacts

theorem outerMeasure_exists_compact {U : Opens G} (hU : Î¼.outerMeasure U â‰  âˆ) {Îµ : â„â‰¥0}
    (hÎµ : Îµ â‰  0) : âˆƒ K : Compacts G, (K : Set G) âŠ† U âˆ§ Î¼.outerMeasure U â‰¤ Î¼.outerMeasure K + Îµ := by
  rw [Î¼.outerMeasure_opens] at hU âŠ¢
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ â†‘(Content.outerMeasure Î¼) â†‘K + â†‘Îµ
  rcases Î¼.innerContent_exists_compact hU hÎµ with âŸ¨K, h1K, h2KâŸ©
  -- âŠ¢ âˆƒ K, â†‘K âŠ† â†‘U âˆ§ innerContent Î¼ U â‰¤ â†‘(Content.outerMeasure Î¼) â†‘K + â†‘Îµ
  exact âŸ¨K, h1K, le_trans h2K <| add_le_add_right (Î¼.le_outerMeasure_compacts K) _âŸ©
  -- ğŸ‰ no goals
#align measure_theory.content.outer_measure_exists_compact MeasureTheory.Content.outerMeasure_exists_compact

theorem outerMeasure_exists_open {A : Set G} (hA : Î¼.outerMeasure A â‰  âˆ) {Îµ : â„â‰¥0} (hÎµ : Îµ â‰  0) :
    âˆƒ U : Opens G, A âŠ† U âˆ§ Î¼.outerMeasure U â‰¤ Î¼.outerMeasure A + Îµ := by
  rcases inducedOuterMeasure_exists_set _ Î¼.innerContent_iUnion_nat Î¼.innerContent_mono hA
      (ENNReal.coe_ne_zero.2 hÎµ) with
    âŸ¨U, hU, h2U, h3UâŸ©
  exact âŸ¨âŸ¨U, hUâŸ©, h2U, h3UâŸ©
  -- ğŸ‰ no goals
#align measure_theory.content.outer_measure_exists_open MeasureTheory.Content.outerMeasure_exists_open

theorem outerMeasure_preimage (f : G â‰ƒâ‚œ G) (h : âˆ€ â¦ƒK : Compacts Gâ¦„, Î¼ (K.map f f.continuous) = Î¼ K)
    (A : Set G) : Î¼.outerMeasure (f â»Â¹' A) = Î¼.outerMeasure A := by
  refine' inducedOuterMeasure_preimage _ Î¼.innerContent_iUnion_nat Î¼.innerContent_mono _
    (fun _ => f.isOpen_preimage) _
  intro s hs
  -- âŠ¢ innerContent Î¼ { carrier := â†‘f.toEquiv â»Â¹' s, is_open' := (_ : IsOpen (â†‘f.to â€¦
  convert Î¼.innerContent_comap f h âŸ¨s, hsâŸ©
  -- ğŸ‰ no goals
#align measure_theory.content.outer_measure_preimage MeasureTheory.Content.outerMeasure_preimage

theorem outerMeasure_lt_top_of_isCompact [LocallyCompactSpace G] {K : Set G} (hK : IsCompact K) :
    Î¼.outerMeasure K < âˆ := by
  rcases exists_compact_superset hK with âŸ¨F, h1F, h2FâŸ©
  -- âŠ¢ â†‘(Content.outerMeasure Î¼) K < âŠ¤
  calc
    Î¼.outerMeasure K â‰¤ Î¼.outerMeasure (interior F) := OuterMeasure.mono' _ h2F
    _ â‰¤ Î¼ âŸ¨F, h1FâŸ© := by
      apply Î¼.outerMeasure_le âŸ¨interior F, isOpen_interiorâŸ© âŸ¨F, h1FâŸ© interior_subset
    _ < âŠ¤ := Î¼.lt_top _
#align measure_theory.content.outer_measure_lt_top_of_is_compact MeasureTheory.Content.outerMeasure_lt_top_of_isCompact

@[to_additive]
theorem is_mul_left_invariant_outerMeasure [Group G] [TopologicalGroup G]
    (h : âˆ€ (g : G) {K : Compacts G}, Î¼ (K.map _ <| continuous_mul_left g) = Î¼ K) (g : G)
    (A : Set G) : Î¼.outerMeasure ((g * Â·) â»Â¹' A) = Î¼.outerMeasure A := by
  convert Î¼.outerMeasure_preimage (Homeomorph.mulLeft g) (fun K => h g) A
  -- ğŸ‰ no goals
#align measure_theory.content.is_mul_left_invariant_outer_measure MeasureTheory.Content.is_mul_left_invariant_outerMeasure
#align measure_theory.content.is_add_left_invariant_outer_measure MeasureTheory.Content.is_add_left_invariant_outerMeasure

theorem outerMeasure_caratheodory (A : Set G) :
    MeasurableSet[Î¼.outerMeasure.caratheodory] A â†”
      âˆ€ U : Opens G, Î¼.outerMeasure (U âˆ© A) + Î¼.outerMeasure (U \ A) â‰¤ Î¼.outerMeasure U := by
  rw [Opens.forall]
  -- âŠ¢ MeasurableSet A â†” âˆ€ (U : Set G) (hU : IsOpen U), â†‘(Content.outerMeasure Î¼) ( â€¦
  apply inducedOuterMeasure_caratheodory
  apply innerContent_iUnion_nat
  -- âŠ¢ âˆ€ â¦ƒsâ‚ sâ‚‚ : Set Gâ¦„ (hsâ‚ : IsOpen sâ‚) (hsâ‚‚ : IsOpen sâ‚‚), sâ‚ âŠ† sâ‚‚ â†’ innerConten â€¦
  apply innerContent_mono'
  -- ğŸ‰ no goals
#align measure_theory.content.outer_measure_caratheodory MeasureTheory.Content.outerMeasure_caratheodory

@[to_additive]
theorem outerMeasure_pos_of_is_mul_left_invariant [Group G] [TopologicalGroup G]
    (h3 : âˆ€ (g : G) {K : Compacts G}, Î¼ (K.map _ <| continuous_mul_left g) = Î¼ K) (K : Compacts G)
    (hK : Î¼ K â‰  0) {U : Set G} (h1U : IsOpen U) (h2U : U.Nonempty) : 0 < Î¼.outerMeasure U := by
  convert Î¼.innerContent_pos_of_is_mul_left_invariant h3 K hK âŸ¨U, h1UâŸ© h2U
  -- âŠ¢ â†‘(Content.outerMeasure Î¼) U = innerContent Î¼ { carrier := U, is_open' := h1U }
  exact Î¼.outerMeasure_opens âŸ¨U, h1UâŸ©
  -- ğŸ‰ no goals
#align measure_theory.content.outer_measure_pos_of_is_mul_left_invariant MeasureTheory.Content.outerMeasure_pos_of_is_mul_left_invariant
#align measure_theory.content.outer_measure_pos_of_is_add_left_invariant MeasureTheory.Content.outerMeasure_pos_of_is_add_left_invariant

variable [S : MeasurableSpace G] [BorelSpace G]

/-- For the outer measure coming from a content, all Borel sets are measurable. -/
theorem borel_le_caratheodory : S â‰¤ Î¼.outerMeasure.caratheodory := by
  rw [@BorelSpace.measurable_eq G _ _]
  -- âŠ¢ borel G â‰¤ OuterMeasure.caratheodory (Content.outerMeasure Î¼)
  refine' MeasurableSpace.generateFrom_le _
  -- âŠ¢ âˆ€ (t : Set G), t âˆˆ {s | IsOpen s} â†’ MeasurableSet t
  intro U hU
  -- âŠ¢ MeasurableSet U
  rw [Î¼.outerMeasure_caratheodory]
  -- âŠ¢ âˆ€ (U_1 : Opens G), â†‘(Content.outerMeasure Î¼) (â†‘U_1 âˆ© U) + â†‘(Content.outerMea â€¦
  intro U'
  -- âŠ¢ â†‘(Content.outerMeasure Î¼) (â†‘U' âˆ© U) + â†‘(Content.outerMeasure Î¼) (â†‘U' \ U) â‰¤  â€¦
  rw [Î¼.outerMeasure_of_isOpen ((U' : Set G) âˆ© U) (U'.isOpen.inter hU)]
  -- âŠ¢ innerContent Î¼ { carrier := â†‘U' âˆ© U, is_open' := (_ : IsOpen (â†‘U' âˆ© U)) } +  â€¦
  simp only [innerContent, iSup_subtype']
  -- âŠ¢ (â¨† (x : { i // â†‘i âŠ† â†‘{ carrier := â†‘U' âˆ© U, is_open' := (_ : IsOpen (â†‘U' âˆ© U) â€¦
  rw [Opens.coe_mk]
  -- âŠ¢ (â¨† (x : { i // â†‘i âŠ† â†‘U' âˆ© U }), â†‘(toFun Î¼ â†‘x)) + â†‘(Content.outerMeasure Î¼) ( â€¦
  haveI : Nonempty { L : Compacts G // (L : Set G) âŠ† U' âˆ© U } := âŸ¨âŸ¨âŠ¥, empty_subset _âŸ©âŸ©
  -- âŠ¢ (â¨† (x : { i // â†‘i âŠ† â†‘U' âˆ© U }), â†‘(toFun Î¼ â†‘x)) + â†‘(Content.outerMeasure Î¼) ( â€¦
  rw [ENNReal.iSup_add]
  -- âŠ¢ â¨† (b : { i // â†‘i âŠ† â†‘U' âˆ© U }), â†‘(toFun Î¼ â†‘b) + â†‘(Content.outerMeasure Î¼) (â†‘U â€¦
  refine' iSup_le _
  -- âŠ¢ âˆ€ (i : { i // â†‘i âŠ† â†‘U' âˆ© U }), â†‘(toFun Î¼ â†‘i) + â†‘(Content.outerMeasure Î¼) (â†‘U â€¦
  rintro âŸ¨L, hLâŸ©
  -- âŠ¢ â†‘(toFun Î¼ â†‘{ val := L, property := hL }) + â†‘(Content.outerMeasure Î¼) (â†‘U' \  â€¦
  simp only [subset_inter_iff] at hL
  -- âŠ¢ â†‘(toFun Î¼ â†‘{ val := L, property := hLâœ }) + â†‘(Content.outerMeasure Î¼) (â†‘U' \ â€¦
  have : â†‘U' \ U âŠ† U' \ L := diff_subset_diff_right hL.2
  -- âŠ¢ â†‘(toFun Î¼ â†‘{ val := L, property := hLâœ }) + â†‘(Content.outerMeasure Î¼) (â†‘U' \ â€¦
  refine' le_trans (add_le_add_left (Î¼.outerMeasure.mono' this) _) _
  -- âŠ¢ â†‘(toFun Î¼ â†‘{ val := L, property := hLâœ }) + â†‘(Content.outerMeasure Î¼) (â†‘U' \ â€¦
  rw [Î¼.outerMeasure_of_isOpen (â†‘U' \ L) (IsOpen.sdiff U'.2 L.2.isClosed)]
  -- âŠ¢ â†‘(toFun Î¼ â†‘{ val := L, property := hLâœ }) + innerContent Î¼ { carrier := â†‘U'  â€¦
  simp only [innerContent, iSup_subtype']
  -- âŠ¢ â†‘(toFun Î¼ L) + â¨† (x : { i // â†‘i âŠ† â†‘{ carrier := â†‘U' \ â†‘L, is_open' := (_ : I â€¦
  rw [Opens.coe_mk]
  -- âŠ¢ â†‘(toFun Î¼ L) + â¨† (x : { i // â†‘i âŠ† â†‘U' \ â†‘L }), â†‘(toFun Î¼ â†‘x) â‰¤ â†‘(Content.out â€¦
  haveI : Nonempty { M : Compacts G // (M : Set G) âŠ† â†‘U' \ L } := âŸ¨âŸ¨âŠ¥, empty_subset _âŸ©âŸ©
  -- âŠ¢ â†‘(toFun Î¼ L) + â¨† (x : { i // â†‘i âŠ† â†‘U' \ â†‘L }), â†‘(toFun Î¼ â†‘x) â‰¤ â†‘(Content.out â€¦
  rw [ENNReal.add_iSup]
  -- âŠ¢ â¨† (b : { i // â†‘i âŠ† â†‘U' \ â†‘L }), â†‘(toFun Î¼ L) + â†‘(toFun Î¼ â†‘b) â‰¤ â†‘(Content.out â€¦
  refine' iSup_le _
  -- âŠ¢ âˆ€ (i : { i // â†‘i âŠ† â†‘U' \ â†‘L }), â†‘(toFun Î¼ L) + â†‘(toFun Î¼ â†‘i) â‰¤ â†‘(Content.out â€¦
  rintro âŸ¨M, hMâŸ©
  -- âŠ¢ â†‘(toFun Î¼ L) + â†‘(toFun Î¼ â†‘{ val := M, property := hM }) â‰¤ â†‘(Content.outerMea â€¦
  simp only [subset_diff] at hM
  -- âŠ¢ â†‘(toFun Î¼ L) + â†‘(toFun Î¼ â†‘{ val := M, property := hMâœ }) â‰¤ â†‘(Content.outerMe â€¦
  have : (â†‘(L âŠ” M) : Set G) âŠ† U' := by
    simp only [union_subset_iff, Compacts.coe_sup, hM, hL, and_self_iff]
  rw [Î¼.outerMeasure_of_isOpen (â†‘U') U'.2]
  -- âŠ¢ â†‘(toFun Î¼ L) + â†‘(toFun Î¼ â†‘{ val := M, property := hMâœ }) â‰¤ innerContent Î¼ {  â€¦
  refine' le_trans (ge_of_eq _) (Î¼.le_innerContent _ _ this)
  -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) (L âŠ” M) = â†‘(toFun Î¼ L) + â†‘(toFun Î¼ â†‘{ val := M, prop â€¦
  exact Î¼.sup_disjoint _ _ hM.2.symm
  -- ğŸ‰ no goals
#align measure_theory.content.borel_le_caratheodory MeasureTheory.Content.borel_le_caratheodory

/-- The measure induced by the outer measure coming from a content, on the Borel sigma-algebra. -/
protected def measure : Measure G :=
  Î¼.outerMeasure.toMeasure Î¼.borel_le_caratheodory
#align measure_theory.content.measure MeasureTheory.Content.measure

theorem measure_apply {s : Set G} (hs : MeasurableSet s) : Î¼.measure s = Î¼.outerMeasure s :=
  toMeasure_apply _ _ hs
#align measure_theory.content.measure_apply MeasureTheory.Content.measure_apply

/-- In a locally compact space, any measure constructed from a content is regular. -/
instance regular [LocallyCompactSpace G] : Î¼.measure.Regular := by
  have : Î¼.measure.OuterRegular := by
    refine' âŸ¨fun A hA r (hr : _ < _) => _âŸ©
    rw [Î¼.measure_apply hA, outerMeasure_eq_iInf] at hr
    simp only [iInf_lt_iff] at hr
    rcases hr with âŸ¨U, hUo, hAU, hrâŸ©
    rw [â† Î¼.outerMeasure_of_isOpen U hUo, â† Î¼.measure_apply hUo.measurableSet] at hr
    exact âŸ¨U, hAU, hUo, hrâŸ©
  have : IsFiniteMeasureOnCompacts Î¼.measure := by
    refine' âŸ¨fun K hK => _âŸ©
    rw [measure_apply _ hK.measurableSet]
    exact Î¼.outerMeasure_lt_top_of_isCompact hK
  refine' âŸ¨fun U hU r hr => _âŸ©
  -- âŠ¢ âˆƒ K, K âŠ† U âˆ§ IsCompact K âˆ§ r < â†‘â†‘(Content.measure Î¼) K
  rw [measure_apply _ hU.measurableSet, Î¼.outerMeasure_of_isOpen U hU] at hr
  -- âŠ¢ âˆƒ K, K âŠ† U âˆ§ IsCompact K âˆ§ r < â†‘â†‘(Content.measure Î¼) K
  simp only [innerContent, lt_iSup_iff] at hr
  -- âŠ¢ âˆƒ K, K âŠ† U âˆ§ IsCompact K âˆ§ r < â†‘â†‘(Content.measure Î¼) K
  rcases hr with âŸ¨K, hKU, hrâŸ©
  -- âŠ¢ âˆƒ K, K âŠ† U âˆ§ IsCompact K âˆ§ r < â†‘â†‘(Content.measure Î¼) K
  refine' âŸ¨K, hKU, K.2, hr.trans_le _âŸ©
  -- âŠ¢ â†‘(toFun Î¼ K) â‰¤ â†‘â†‘(Content.measure Î¼) â†‘K
  exact (Î¼.le_outerMeasure_compacts K).trans (le_toMeasure_apply _ _ _)
  -- ğŸ‰ no goals
#align measure_theory.content.regular MeasureTheory.Content.regular

end OuterMeasure

section RegularContents

/-- A content `Î¼` is called regular if for every compact set `K`,
  `Î¼(K) = inf {Î¼(K') : K âŠ‚ int K' âŠ‚ K'}`. See Paul Halmos (1950), Measure Theory, Â§54-/
def ContentRegular :=
  âˆ€ â¦ƒK : TopologicalSpace.Compacts Gâ¦„,
    Î¼ K = â¨… (K' : TopologicalSpace.Compacts G) (_ : (K : Set G) âŠ† interior (K' : Set G)), Î¼ K'
#align measure_theory.content.content_regular MeasureTheory.Content.ContentRegular

theorem contentRegular_exists_compact (H : ContentRegular Î¼) (K : TopologicalSpace.Compacts G)
    {Îµ : NNReal} (hÎµ : Îµ â‰  0) :
    âˆƒ K' : TopologicalSpace.Compacts G, K.carrier âŠ† interior K'.carrier âˆ§ Î¼ K' â‰¤ Î¼ K + Îµ := by
  by_contra hc
  -- âŠ¢ False
  simp only [not_exists, not_and, not_le] at hc
  -- âŠ¢ False
  have lower_bound_iInf : Î¼ K + Îµ â‰¤
      â¨… (K' : TopologicalSpace.Compacts G) (_ : (K : Set G) âŠ† interior (K' : Set G)), Î¼ K' :=
    le_iInf fun K' => le_iInf fun K'_hyp => le_of_lt (hc K' K'_hyp)
  rw [â† H] at lower_bound_iInf
  -- âŠ¢ False
  exact (lt_self_iff_false (Î¼ K)).mp (lt_of_le_of_lt' lower_bound_iInf
    (ENNReal.lt_add_right (ne_top_of_lt (Î¼.lt_top K)) (ENNReal.coe_ne_zero.mpr hÎµ)))
#align measure_theory.content.content_regular_exists_compact MeasureTheory.Content.contentRegular_exists_compact

variable [MeasurableSpace G] [T2Space G] [BorelSpace G]

/-- If `Î¼` is a regular content, then the measure induced by `Î¼` will agree with `Î¼`
  on compact sets. -/
theorem measure_eq_content_of_regular (H : MeasureTheory.Content.ContentRegular Î¼)
    (K : TopologicalSpace.Compacts G) : Î¼.measure â†‘K = Î¼ K := by
  refine' le_antisymm _ _
  -- âŠ¢ â†‘â†‘(Content.measure Î¼) â†‘K â‰¤ (fun s => â†‘(toFun Î¼ s)) K
  Â· apply ENNReal.le_of_forall_pos_le_add
    -- âŠ¢ âˆ€ (Îµ : â„â‰¥0), 0 < Îµ â†’ (fun s => â†‘(toFun Î¼ s)) K < âŠ¤ â†’ â†‘â†‘(Content.measure Î¼) â†‘ â€¦
    intro Îµ Îµpos _
    -- âŠ¢ â†‘â†‘(Content.measure Î¼) â†‘K â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
    obtain âŸ¨K', K'_hypâŸ© := contentRegular_exists_compact Î¼ H K (ne_bot_of_gt Îµpos)
    -- âŠ¢ â†‘â†‘(Content.measure Î¼) â†‘K â‰¤ (fun s => â†‘(toFun Î¼ s)) K + â†‘Îµ
    calc
      Î¼.measure â†‘K â‰¤ Î¼.measure (interior â†‘K') := by
        rw [Î¼.measure_apply isOpen_interior.measurableSet,
          Î¼.measure_apply K.isCompact.measurableSet]
        exact Î¼.outerMeasure.mono K'_hyp.left
      _ â‰¤ Î¼ K' := by
        rw [Î¼.measure_apply (IsOpen.measurableSet isOpen_interior)]
        exact Î¼.outerMeasure_interior_compacts K'
      _ â‰¤ Î¼ K + Îµ := K'_hyp.right
  Â· rw [Î¼.measure_apply (IsCompact.measurableSet K.isCompact)]
    -- âŠ¢ (fun s => â†‘(toFun Î¼ s)) K â‰¤ â†‘(Content.outerMeasure Î¼) â†‘K
    exact Î¼.le_outerMeasure_compacts K
    -- ğŸ‰ no goals
#align measure_theory.content.measure_eq_content_of_regular MeasureTheory.Content.measure_eq_content_of_regular

end RegularContents

end Content

end MeasureTheory
