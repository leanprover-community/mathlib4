/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Measure.HasOuterApproxClosed

/-!
# Characterization of a finite measure by the integrals of products of bounded functions

Given two Borel spaces `X` and `Y` satisfying `HasOuterApproxClosed`, a finite measure `Î¼`
over `X Ã— Y` is determined by the values `âˆ« z, f z.1 * g z.2 âˆ‚Î¼`, for `f : X â†’ â„` and `g : Y â†’ â„`
any bounded continuous functions.

In particular, If `Î¼` and `Î½` and two finite measures over `X` and `Y` respectively, then their
product is the only finite measure `Î¾` over `X Ã— Y` such that for any two bounded continuous
functions `f : X â†’ â„` and `g : Y â†’ â„` we have
`âˆ« z, f z.1 * g z.2 âˆ‚Î¾ = (âˆ« x, f x âˆ‚Î¼) * (âˆ« y, g y âˆ‚Î½)`.

# Main statements

* `ext_of_integral_mul_boundedContinuousFunction`: A finite measure on a product space is
  characterized by the integrals of products of real bounded continuous functions.
* `eq_prod_of_integral_mul_boundedContinuousFunction`: The product of two finite measures `Î¼` and
  `Î½` is the only finite measure `Î¾` such that for all real bounded continuous functions
  `f` and `g` we have `âˆ« z, f z.1 * g z.2 âˆ‚Î¾ = âˆ« x, f x âˆ‚Î¼ * âˆ« y, g y âˆ‚Î½`.

# Tags

bounded continuous function, product measure
-/

open BoundedContinuousFunction MeasureTheory Topology Filter Set ENNReal NNReal MeasurableSpace
open scoped Topology ENNReal NNReal

namespace Measure

variable {X Y : Type*}
  {mX : MeasurableSpace X} [TopologicalSpace X] [BorelSpace X] [HasOuterApproxClosed X]
  {mY : MeasurableSpace Y} [TopologicalSpace Y] [BorelSpace Y] [HasOuterApproxClosed Y]
  {Î¼ Î½ : Measure (X Ã— Y)}

/-- A finite measure on a product space is characterized by the integrals of products of
bounded nonnegative continuous functions. -/
lemma ext_of_lintegral_mul_boundedContinuousFunction [IsFiniteMeasure Î¼]
    (h : âˆ€ (f : X â†’áµ‡ â„â‰¥0) (g : Y â†’áµ‡ â„â‰¥0),
      âˆ«â» z, f z.1 * g z.2 âˆ‚Î¼ = âˆ«â» z, f z.1 * g z.2 âˆ‚Î½) :
    Î¼ = Î½ := by
  have hÎ¼Î½ : Î¼ univ = Î½ univ := by convert h 1 1 <;> simp
  have : IsFiniteMeasure Î½ := âŸ¨by simp [â† hÎ¼Î½]âŸ©
  let Ï€ : Set (Set (X Ã— Y)) :=
    {s | âˆƒ (F : Set X) (G : Set Y), IsClosed F âˆ§ IsClosed G âˆ§ s = F Ã—Ë¢ G}
  have hÏ€1 : IsPiSystem Ï€ := by
    rintro - âŸ¨sâ‚, sâ‚‚, hsâ‚, hsâ‚‚, rflâŸ© - âŸ¨tâ‚, tâ‚‚, htâ‚, htâ‚‚, rflâŸ© -
    exact âŸ¨sâ‚ âˆ© tâ‚, sâ‚‚ âˆ© tâ‚‚, hsâ‚.inter htâ‚, hsâ‚‚.inter htâ‚‚, Set.prod_inter_prodâŸ©
  have hÏ€2 : mX.prod mY = generateFrom Ï€ := by
    refine le_antisymm ?_ (generateFrom_le ?_)
    Â· simp_rw [BorelSpace.measurable_eq, borel_eq_generateFrom_isClosed, MeasurableSpace.prod,
        comap_generateFrom]
      refine sup_le (generateFrom_le ?_) (generateFrom_le ?_)
      Â· rintro - âŸ¨s, hs, rflâŸ©
        exact measurableSet_generateFrom âŸ¨s, Set.univ, hs, isClosed_univ, by rw [Set.prod_univ]âŸ©
      Â· rintro - âŸ¨t, ht, rflâŸ©
        exact measurableSet_generateFrom âŸ¨Set.univ, t, isClosed_univ, ht, by rw [Set.univ_prod]âŸ©
    Â· rintro - âŸ¨sâ‚, sâ‚‚, hsâ‚, hsâ‚‚, rflâŸ©
      exact hsâ‚.measurableSet.prod hsâ‚‚.measurableSet
  refine ext_of_generate_finite Ï€ hÏ€2 hÏ€1 ?_ hÎ¼Î½
  rintro - âŸ¨sâ‚, sâ‚‚, hsâ‚, hsâ‚‚, rflâŸ©
  have (z : X Ã— Y) := ENNReal.continuous_coe.tendsto _ |>.comp <|
    (tendsto_pi_nhds.1 (HasOuterApproxClosed.tendsto_apprSeq hsâ‚) z.1).mul
    (tendsto_pi_nhds.1 (HasOuterApproxClosed.tendsto_apprSeq hsâ‚‚) z.2)
  simp_rw [show (fun _ â†¦ 1 : X â†’ â„â‰¥0) = 1 from rfl, show (fun _ â†¦ 1 : Y â†’ â„â‰¥0) = 1 from rfl,
    â† Set.indicator_prod_one] at this
  have h1 : Tendsto (fun n â†¦ âˆ«â» z, (hsâ‚.apprSeq n z.1 * hsâ‚‚.apprSeq n z.2 : â„â‰¥0) âˆ‚Î¼)
      atTop (ğ“ (âˆ«â» z, ((sâ‚ Ã—Ë¢ sâ‚‚).indicator 1 z : â„â‰¥0) âˆ‚Î¼)) := by
    refine tendsto_lintegral_filter_of_dominated_convergence 1
      (Eventually.of_forall <| by fun_prop) (Eventually.of_forall fun n â†¦ ae_of_all _ fun Ï‰ â†¦ ?_)
      (by simp) (ae_of_all _ this)
    grw [HasOuterApproxClosed.apprSeq_apply_le_one, HasOuterApproxClosed.apprSeq_apply_le_one]
    simp
  have h2 : Tendsto (fun n â†¦ âˆ«â» z, (hsâ‚.apprSeq n z.1 * hsâ‚‚.apprSeq n z.2 : â„â‰¥0) âˆ‚Î¼)
      atTop (ğ“ (âˆ«â» z, ((sâ‚ Ã—Ë¢ sâ‚‚).indicator 1 z : â„â‰¥0) âˆ‚Î½)) := by
    simp_rw [coe_mul, h]
    refine tendsto_lintegral_filter_of_dominated_convergence 1
      (Eventually.of_forall <| by fun_prop) (Eventually.of_forall fun n â†¦ ae_of_all _ fun Ï‰ â†¦ ?_)
      (by simp) (ae_of_all _ this)
    grw [HasOuterApproxClosed.apprSeq_apply_le_one, HasOuterApproxClosed.apprSeq_apply_le_one]
    simp
  convert tendsto_nhds_unique h1 h2 <;> simp [hsâ‚.measurableSet.prod hsâ‚‚.measurableSet]

/-- A finite measure on a product space is characterized by the integrals of products of
real and bounded continuous functions. -/
lemma ext_of_integral_mul_boundedContinuousFunction [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (h : âˆ€ (f : X â†’áµ‡ â„) (g : Y â†’áµ‡ â„),
      âˆ« z, f z.1 * g z.2 âˆ‚Î¼ = âˆ« z, f z.1 * g z.2 âˆ‚Î½) :
    Î¼ = Î½ := by
  refine ext_of_lintegral_mul_boundedContinuousFunction fun f g â†¦ ?_
  apply (toReal_eq_toReal_iff' (lintegral_lt_top_of_nnreal Î¼
    ((f.compContinuous âŸ¨@Prod.fst X Y, continuous_fstâŸ©) *
      (g.compContinuous âŸ¨@Prod.snd X Y, continuous_sndâŸ©))).ne
    (lintegral_lt_top_of_nnreal Î½
    ((f.compContinuous âŸ¨@Prod.fst X Y, continuous_fstâŸ©) *
      (g.compContinuous âŸ¨@Prod.snd X Y, continuous_sndâŸ©))).ne).1
  simp only [BoundedContinuousFunction.coe_mul, coe_compContinuous, ContinuousMap.coe_mk,
    Pi.mul_apply, Function.comp_apply, ENNReal.coe_mul]
  have {Î¼ : Measure (X Ã— Y)} :
      (âˆ«â» z, f z.1 * g z.2 âˆ‚Î¼).toReal = âˆ« z, (f z.1).toReal * (g z.2).toReal âˆ‚Î¼ := by
    rw [integral_eq_lintegral_of_nonneg_ae]
    Â· simp
    Â· exact Eventually.of_forall fun _ â†¦ by positivity
    exact AEStronglyMeasurable.mul
      (continuous_coe.aestronglyMeasurable.comp_measurable (by fun_prop))
      (continuous_coe.aestronglyMeasurable.comp_measurable (by fun_prop))
  simp_rw [this]
  exact h âŸ¨âŸ¨fun x â†¦ (f x), by fun_propâŸ©, f.map_bounded'âŸ©
    âŸ¨âŸ¨fun x â†¦ (g x), by fun_propâŸ©, g.map_bounded'âŸ©

variable {Î¼ : Measure X} [IsFiniteMeasure Î¼] {Î½ : Measure Y} [IsFiniteMeasure Î½]
  {Î¾ : Measure (X Ã— Y)} [IsFiniteMeasure Î¾]

/-- The product of two finite measures `Î¼` and `Î½` is the only finite measure `Î¾` such that
for all real bounded continuous functions `f` and `g` we have
`âˆ« z, f z.1 * g z.2 âˆ‚Î¾ = âˆ« x, f x âˆ‚Î¼ * âˆ« y, g y âˆ‚Î½`. -/
lemma eq_prod_of_integral_mul_boundedContinuousFunction
    (h : âˆ€ (f : X â†’áµ‡ â„) (g : Y â†’áµ‡ â„),
      âˆ« z, f z.1 * g z.2 âˆ‚Î¾ = (âˆ« x, f x âˆ‚Î¼) * (âˆ« y, g y âˆ‚Î½)) :
    Î¾ = Î¼.prod Î½ :=
  ext_of_integral_mul_boundedContinuousFunction fun f g â†¦ by rw [h, integral_prod_mul]

end Measure
