/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.MeasureTheory.Measure.Comap
import Mathlib.MeasureTheory.Measure.QuasiMeasurePreserving
import Mathlib.Data.Set.Card

/-!
# Restricting a measure to a subset or a subtype

Given a measure `Î¼` on a type `Î±` and a subset `s` of `Î±`, we define a measure `Î¼.restrict s` as
the restriction of `Î¼` to `s` (still as a measure on `Î±`).

We investigate how this notion interacts with usual operations on measures (sum, pushforward,
pullback), and on sets (inclusion, union, Union).

We also study the relationship between the restriction of a measure to a subtype (given by the
pullback under `Subtype.val`) and the restriction to a set as above.
-/

open scoped ENNReal NNReal Topology
open Set MeasureTheory Measure Filter MeasurableSpace ENNReal Function

variable {R Î± Î² Î´ Î³ Î¹ : Type*}

namespace MeasureTheory

variable {m0 : MeasurableSpace Î±} [MeasurableSpace Î²] [MeasurableSpace Î³]
variable {Î¼ Î¼â‚ Î¼â‚‚ Î¼â‚ƒ Î½ Î½' Î½â‚ Î½â‚‚ : Measure Î±} {s s' t : Set Î±}

namespace Measure

/-! ### Restricting a measure -/

/-- Restrict a measure `Î¼` to a set `s` as an `â„â‰¥0âˆ`-linear map. -/
noncomputable def restrictâ‚— {m0 : MeasurableSpace Î±} (s : Set Î±) : Measure Î± â†’â‚—[â„â‰¥0âˆ] Measure Î± :=
  liftLinear (OuterMeasure.restrict s) fun Î¼ s' hs' t => by
    suffices Î¼ (s âˆ© t) = Î¼ (s âˆ© t âˆ© s') + Î¼ ((s âˆ© t) \ s') by
      simpa [â† Set.inter_assoc, Set.inter_comm _ s, â† inter_diff_assoc]
    exact le_toOuterMeasure_caratheodory _ _ hs' _

/-- Restrict a measure `Î¼` to a set `s`. -/
noncomputable def restrict {_m0 : MeasurableSpace Î±} (Î¼ : Measure Î±) (s : Set Î±) : Measure Î± :=
  restrictâ‚— s Î¼

@[simp]
theorem restrictâ‚—_apply {_m0 : MeasurableSpace Î±} (s : Set Î±) (Î¼ : Measure Î±) :
    restrictâ‚— s Î¼ = Î¼.restrict s :=
  rfl

/-- This lemma shows that `restrict` and `toOuterMeasure` commute. Note that the LHS has a
restrict on measures and the RHS has a restrict on outer measures. -/
theorem restrict_toOuterMeasure_eq_toOuterMeasure_restrict (h : MeasurableSet s) :
    (Î¼.restrict s).toOuterMeasure = OuterMeasure.restrict s Î¼.toOuterMeasure := by
  simp_rw [restrict, restrictâ‚—, liftLinear, LinearMap.coe_mk, AddHom.coe_mk,
    toMeasure_toOuterMeasure, OuterMeasure.restrict_trim h, Î¼.trimmed]

theorem restrict_applyâ‚€ (ht : NullMeasurableSet t (Î¼.restrict s)) : Î¼.restrict s t = Î¼ (t âˆ© s) := by
  rw [â† restrictâ‚—_apply, restrictâ‚—, liftLinear_applyâ‚€ _ ht, OuterMeasure.restrict_apply,
    coe_toOuterMeasure]

/-- If `t` is a measurable set, then the measure of `t` with respect to the restriction of
  the measure to `s` equals the outer measure of `t âˆ© s`. An alternate version requiring that `s`
  be measurable instead of `t` exists as `Measure.restrict_apply'`. -/
@[simp]
theorem restrict_apply (ht : MeasurableSet t) : Î¼.restrict s t = Î¼ (t âˆ© s) :=
  restrict_applyâ‚€ ht.nullMeasurableSet

/-- Restriction of a measure to a subset is monotone both in set and in measure. -/
theorem restrict_mono' {_m0 : MeasurableSpace Î±} â¦ƒs s' : Set Î±â¦„ â¦ƒÎ¼ Î½ : Measure Î±â¦„ (hs : s â‰¤áµ[Î¼] s')
    (hÎ¼Î½ : Î¼ â‰¤ Î½) : Î¼.restrict s â‰¤ Î½.restrict s' :=
  Measure.le_iff.2 fun t ht => calc
    Î¼.restrict s t = Î¼ (t âˆ© s) := restrict_apply ht
    _ â‰¤ Î¼ (t âˆ© s') := (measure_mono_ae <| hs.mono fun _x hx âŸ¨hxt, hxsâŸ© => âŸ¨hxt, hx hxsâŸ©)
    _ â‰¤ Î½ (t âˆ© s') := le_iff'.1 hÎ¼Î½ (t âˆ© s')
    _ = Î½.restrict s' t := (restrict_apply ht).symm

/-- Restriction of a measure to a subset is monotone both in set and in measure. -/
@[mono, gcongr]
theorem restrict_mono {_m0 : MeasurableSpace Î±} â¦ƒs s' : Set Î±â¦„ (hs : s âŠ† s') â¦ƒÎ¼ Î½ : Measure Î±â¦„
    (hÎ¼Î½ : Î¼ â‰¤ Î½) : Î¼.restrict s â‰¤ Î½.restrict s' :=
  restrict_mono' (ae_of_all _ hs) hÎ¼Î½

theorem restrict_mono_measure {_ : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} (h : Î¼ â‰¤ Î½) (s : Set Î±) :
    Î¼.restrict s â‰¤ Î½.restrict s :=
  restrict_mono subset_rfl h

theorem restrict_mono_set {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) {s t : Set Î±} (h : s âŠ† t) :
    Î¼.restrict s â‰¤ Î¼.restrict t :=
  restrict_mono h le_rfl

theorem restrict_mono_ae (h : s â‰¤áµ[Î¼] t) : Î¼.restrict s â‰¤ Î¼.restrict t :=
  restrict_mono' h (le_refl Î¼)

theorem restrict_congr_set (h : s =áµ[Î¼] t) : Î¼.restrict s = Î¼.restrict t :=
  le_antisymm (restrict_mono_ae h.le) (restrict_mono_ae h.symm.le)

/-- If `s` is a measurable set, then the outer measure of `t` with respect to the restriction of
the measure to `s` equals the outer measure of `t âˆ© s`. This is an alternate version of
`Measure.restrict_apply`, requiring that `s` is measurable instead of `t`. -/
@[simp]
theorem restrict_apply' (hs : MeasurableSet s) : Î¼.restrict s t = Î¼ (t âˆ© s) := by
  rw [â† toOuterMeasure_apply,
    Measure.restrict_toOuterMeasure_eq_toOuterMeasure_restrict hs,
    OuterMeasure.restrict_apply s t _, toOuterMeasure_apply]

theorem restrict_applyâ‚€' (hs : NullMeasurableSet s Î¼) : Î¼.restrict s t = Î¼ (t âˆ© s) := by
  rw [â† restrict_congr_set hs.toMeasurable_ae_eq,
    restrict_apply' (measurableSet_toMeasurable _ _),
    measure_congr ((ae_eq_refl t).inter hs.toMeasurable_ae_eq)]

theorem restrict_le_self : Î¼.restrict s â‰¤ Î¼ :=
  Measure.le_iff.2 fun t ht => calc
    Î¼.restrict s t = Î¼ (t âˆ© s) := restrict_apply ht
    _ â‰¤ Î¼ t := measure_mono inter_subset_left

theorem absolutelyContinuous_restrict : Î¼.restrict s â‰ª Î¼ :=
  Measure.absolutelyContinuous_of_le Measure.restrict_le_self

variable (Î¼)

theorem restrict_eq_self (h : s âŠ† t) : Î¼.restrict t s = Î¼ s :=
  (le_iff'.1 restrict_le_self s).antisymm <|
    calc
      Î¼ s â‰¤ Î¼ (toMeasurable (Î¼.restrict t) s âˆ© t) :=
        measure_mono (subset_inter (subset_toMeasurable _ _) h)
      _ = Î¼.restrict t s := by
        rw [â† restrict_apply (measurableSet_toMeasurable _ _), measure_toMeasurable]

@[simp]
theorem restrict_apply_self (s : Set Î±) : (Î¼.restrict s) s = Î¼ s :=
  restrict_eq_self Î¼ Subset.rfl

variable {Î¼}

theorem restrict_apply_univ (s : Set Î±) : Î¼.restrict s univ = Î¼ s := by
  rw [restrict_apply MeasurableSet.univ, Set.univ_inter]

theorem le_restrict_apply (s t : Set Î±) : Î¼ (t âˆ© s) â‰¤ Î¼.restrict s t :=
  calc
    Î¼ (t âˆ© s) = Î¼.restrict s (t âˆ© s) := (restrict_eq_self Î¼ inter_subset_right).symm
    _ â‰¤ Î¼.restrict s t := measure_mono inter_subset_left

theorem restrict_apply_le (s t : Set Î±) : Î¼.restrict s t â‰¤ Î¼ t :=
  Measure.le_iff'.1 restrict_le_self _

theorem restrict_apply_superset (h : s âŠ† t) : Î¼.restrict s t = Î¼ s :=
  ((measure_mono (subset_univ _)).trans_eq <| restrict_apply_univ _).antisymm
    ((restrict_apply_self Î¼ s).symm.trans_le <| measure_mono h)

@[simp]
theorem restrict_add {_m0 : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±) (s : Set Î±) :
    (Î¼ + Î½).restrict s = Î¼.restrict s + Î½.restrict s :=
  (restrictâ‚— s).map_add Î¼ Î½

@[simp]
theorem restrict_zero {_m0 : MeasurableSpace Î±} (s : Set Î±) : (0 : Measure Î±).restrict s = 0 :=
  (restrictâ‚— s).map_zero

@[simp]
theorem restrict_smul {_m0 : MeasurableSpace Î±} {R : Type*} [SMul R â„â‰¥0âˆ]
    [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ] (c : R) (Î¼ : Measure Î±) (s : Set Î±) :
    (c â€¢ Î¼).restrict s = c â€¢ Î¼.restrict s := by
  simpa only [smul_one_smul] using (restrictâ‚— s).map_smul (c â€¢ 1) Î¼

theorem restrict_restrictâ‚€ (hs : NullMeasurableSet s (Î¼.restrict t)) :
    (Î¼.restrict t).restrict s = Î¼.restrict (s âˆ© t) :=
  ext fun u hu => by
    simp only [Set.inter_assoc, restrict_apply hu,
      restrict_applyâ‚€ (hu.nullMeasurableSet.inter hs)]

@[simp]
theorem restrict_restrict (hs : MeasurableSet s) : (Î¼.restrict t).restrict s = Î¼.restrict (s âˆ© t) :=
  restrict_restrictâ‚€ hs.nullMeasurableSet

theorem restrict_restrict_of_subset (h : s âŠ† t) : (Î¼.restrict t).restrict s = Î¼.restrict s := by
  ext1 u hu
  rw [restrict_apply hu, restrict_apply hu, restrict_eq_self]
  exact inter_subset_right.trans h

theorem restrict_restrictâ‚€' (ht : NullMeasurableSet t Î¼) :
    (Î¼.restrict t).restrict s = Î¼.restrict (s âˆ© t) :=
  ext fun u hu => by simp only [restrict_apply hu, restrict_applyâ‚€' ht, inter_assoc]

theorem restrict_restrict' (ht : MeasurableSet t) :
    (Î¼.restrict t).restrict s = Î¼.restrict (s âˆ© t) :=
  restrict_restrictâ‚€' ht.nullMeasurableSet

theorem restrict_comm (hs : MeasurableSet s) :
    (Î¼.restrict t).restrict s = (Î¼.restrict s).restrict t := by
  rw [restrict_restrict hs, restrict_restrict' hs, inter_comm]

theorem restrict_apply_eq_zero (ht : MeasurableSet t) : Î¼.restrict s t = 0 â†” Î¼ (t âˆ© s) = 0 := by
  rw [restrict_apply ht]

theorem measure_inter_eq_zero_of_restrict (h : Î¼.restrict s t = 0) : Î¼ (t âˆ© s) = 0 :=
  nonpos_iff_eq_zero.1 (h â–¸ le_restrict_apply _ _)

theorem restrict_apply_eq_zero' (hs : MeasurableSet s) : Î¼.restrict s t = 0 â†” Î¼ (t âˆ© s) = 0 := by
  rw [restrict_apply' hs]

@[simp]
theorem restrict_eq_zero : Î¼.restrict s = 0 â†” Î¼ s = 0 := by
  rw [â† measure_univ_eq_zero, restrict_apply_univ]

/-- If `Î¼ s â‰  0`, then `Î¼.restrict s â‰  0`, in terms of `NeZero` instances. -/
instance restrict.neZero [NeZero (Î¼ s)] : NeZero (Î¼.restrict s) :=
  âŸ¨mt restrict_eq_zero.mp <| NeZero.ne _âŸ©

theorem restrict_zero_set {s : Set Î±} (h : Î¼ s = 0) : Î¼.restrict s = 0 :=
  restrict_eq_zero.2 h

@[simp]
theorem restrict_empty : Î¼.restrict âˆ… = 0 :=
  restrict_zero_set measure_empty

@[simp]
theorem restrict_univ : Î¼.restrict univ = Î¼ :=
  ext fun s hs => by simp [hs]

theorem restrict_inter_add_diffâ‚€ (s : Set Î±) (ht : NullMeasurableSet t Î¼) :
    Î¼.restrict (s âˆ© t) + Î¼.restrict (s \ t) = Î¼.restrict s := by
  ext1 u hu
  simp only [add_apply, restrict_apply hu, â† inter_assoc, diff_eq]
  exact measure_inter_add_diffâ‚€ (u âˆ© s) ht

theorem restrict_inter_add_diff (s : Set Î±) (ht : MeasurableSet t) :
    Î¼.restrict (s âˆ© t) + Î¼.restrict (s \ t) = Î¼.restrict s :=
  restrict_inter_add_diffâ‚€ s ht.nullMeasurableSet

theorem restrict_union_add_interâ‚€ (s : Set Î±) (ht : NullMeasurableSet t Î¼) :
    Î¼.restrict (s âˆª t) + Î¼.restrict (s âˆ© t) = Î¼.restrict s + Î¼.restrict t := by
  rw [â† restrict_inter_add_diffâ‚€ (s âˆª t) ht, union_inter_cancel_right, union_diff_right, â†
    restrict_inter_add_diffâ‚€ s ht, add_comm, â† add_assoc, add_right_comm]

theorem restrict_union_add_inter (s : Set Î±) (ht : MeasurableSet t) :
    Î¼.restrict (s âˆª t) + Î¼.restrict (s âˆ© t) = Î¼.restrict s + Î¼.restrict t :=
  restrict_union_add_interâ‚€ s ht.nullMeasurableSet

theorem restrict_union_add_inter' (hs : MeasurableSet s) (t : Set Î±) :
    Î¼.restrict (s âˆª t) + Î¼.restrict (s âˆ© t) = Î¼.restrict s + Î¼.restrict t := by
  simpa only [union_comm, inter_comm, add_comm] using restrict_union_add_inter t hs

theorem restrict_unionâ‚€ (h : AEDisjoint Î¼ s t) (ht : NullMeasurableSet t Î¼) :
    Î¼.restrict (s âˆª t) = Î¼.restrict s + Î¼.restrict t := by
  simp [â† restrict_union_add_interâ‚€ s ht, restrict_zero_set h]

theorem restrict_union (h : Disjoint s t) (ht : MeasurableSet t) :
    Î¼.restrict (s âˆª t) = Î¼.restrict s + Î¼.restrict t :=
  restrict_unionâ‚€ h.aedisjoint ht.nullMeasurableSet

theorem restrict_union' (h : Disjoint s t) (hs : MeasurableSet s) :
    Î¼.restrict (s âˆª t) = Î¼.restrict s + Î¼.restrict t := by
  rw [union_comm, restrict_union h.symm hs, add_comm]

@[simp]
theorem restrict_add_restrict_compl (hs : MeasurableSet s) :
    Î¼.restrict s + Î¼.restrict sá¶œ = Î¼ := by
  rw [â† restrict_union (@disjoint_compl_right (Set Î±) _ _) hs.compl, union_compl_self,
    restrict_univ]

@[simp]
theorem restrict_compl_add_restrict (hs : MeasurableSet s) : Î¼.restrict sá¶œ + Î¼.restrict s = Î¼ := by
  rw [add_comm, restrict_add_restrict_compl hs]

theorem restrict_union_le (s s' : Set Î±) : Î¼.restrict (s âˆª s') â‰¤ Î¼.restrict s + Î¼.restrict s' :=
  le_iff.2 fun t ht â†¦ by
    simpa [ht, inter_union_distrib_left] using measure_union_le (t âˆ© s) (t âˆ© s')

theorem restrict_iUnion_apply_ae [Countable Î¹] {s : Î¹ â†’ Set Î±} (hd : Pairwise (AEDisjoint Î¼ on s))
    (hm : âˆ€ i, NullMeasurableSet (s i) Î¼) {t : Set Î±} (ht : MeasurableSet t) :
    Î¼.restrict (â‹ƒ i, s i) t = âˆ‘' i, Î¼.restrict (s i) t := by
  simp only [restrict_apply, ht, inter_iUnion]
  exact
    measure_iUnionâ‚€ (hd.mono fun i j h => h.mono inter_subset_right inter_subset_right)
      fun i => ht.nullMeasurableSet.inter (hm i)

theorem restrict_iUnion_apply [Countable Î¹] {s : Î¹ â†’ Set Î±} (hd : Pairwise (Disjoint on s))
    (hm : âˆ€ i, MeasurableSet (s i)) {t : Set Î±} (ht : MeasurableSet t) :
    Î¼.restrict (â‹ƒ i, s i) t = âˆ‘' i, Î¼.restrict (s i) t :=
  restrict_iUnion_apply_ae hd.aedisjoint (fun i => (hm i).nullMeasurableSet) ht

theorem restrict_iUnion_apply_eq_iSup [Countable Î¹] {s : Î¹ â†’ Set Î±} (hd : Directed (Â· âŠ† Â·) s)
    {t : Set Î±} (ht : MeasurableSet t) : Î¼.restrict (â‹ƒ i, s i) t = â¨† i, Î¼.restrict (s i) t := by
  simp only [restrict_apply ht, inter_iUnion]
  rw [Directed.measure_iUnion]
  exacts [hd.mono_comp _ fun sâ‚ sâ‚‚ => inter_subset_inter_right _]

/-- The restriction of the pushforward measure is the pushforward of the restriction. For a version
assuming only `AEMeasurable`, see `restrict_map_of_aemeasurable`. -/
theorem restrict_map {f : Î± â†’ Î²} (hf : Measurable f) {s : Set Î²} (hs : MeasurableSet s) :
    (Î¼.map f).restrict s = (Î¼.restrict <| f â»Â¹' s).map f :=
  ext fun t ht => by simp [*, hf ht]

theorem restrict_toMeasurable (h : Î¼ s â‰  âˆ) : Î¼.restrict (toMeasurable Î¼ s) = Î¼.restrict s :=
  ext fun t ht => by
    rw [restrict_apply ht, restrict_apply ht, inter_comm, measure_toMeasurable_inter ht h,
      inter_comm]

theorem restrict_eq_self_of_ae_mem {_m0 : MeasurableSpace Î±} â¦ƒs : Set Î±â¦„ â¦ƒÎ¼ : Measure Î±â¦„
    (hs : âˆ€áµ x âˆ‚Î¼, x âˆˆ s) : Î¼.restrict s = Î¼ :=
  calc
    Î¼.restrict s = Î¼.restrict univ := restrict_congr_set (eventuallyEq_univ.mpr hs)
    _ = Î¼ := restrict_univ

theorem restrict_congr_meas (hs : MeasurableSet s) :
    Î¼.restrict s = Î½.restrict s â†” âˆ€ t âŠ† s, MeasurableSet t â†’ Î¼ t = Î½ t :=
  âŸ¨fun H t hts ht => by
    rw [â† inter_eq_self_of_subset_left hts, â† restrict_apply ht, H, restrict_apply ht], fun H =>
    ext fun t ht => by
      rw [restrict_apply ht, restrict_apply ht, H _ inter_subset_right (ht.inter hs)]âŸ©

theorem restrict_congr_mono (hs : s âŠ† t) (h : Î¼.restrict t = Î½.restrict t) :
    Î¼.restrict s = Î½.restrict s := by
  rw [â† restrict_restrict_of_subset hs, h, restrict_restrict_of_subset hs]

/-- If two measures agree on all measurable subsets of `s` and `t`, then they agree on all
measurable subsets of `s âˆª t`. -/
theorem restrict_union_congr :
    Î¼.restrict (s âˆª t) = Î½.restrict (s âˆª t) â†”
      Î¼.restrict s = Î½.restrict s âˆ§ Î¼.restrict t = Î½.restrict t := by
  refine âŸ¨fun h â†¦ âŸ¨restrict_congr_mono subset_union_left h,
    restrict_congr_mono subset_union_right hâŸ©, ?_âŸ©
  rintro âŸ¨hs, htâŸ©
  ext1 u hu
  simp only [restrict_apply hu, inter_union_distrib_left]
  rcases exists_measurable_supersetâ‚‚ Î¼ Î½ (u âˆ© s) with âŸ¨US, hsub, hm, hÎ¼, hÎ½âŸ©
  calc
    Î¼ (u âˆ© s âˆª u âˆ© t) = Î¼ (US âˆª u âˆ© t) :=
      measure_union_congr_of_subset hsub hÎ¼.le Subset.rfl le_rfl
    _ = Î¼ US + Î¼ ((u âˆ© t) \ US) := (measure_add_diff hm.nullMeasurableSet _).symm
    _ = restrict Î¼ s u + restrict Î¼ t (u \ US) := by
      simp only [restrict_apply, hu, hu.diff hm, hÎ¼, â† inter_comm t, inter_diff_assoc]
    _ = restrict Î½ s u + restrict Î½ t (u \ US) := by rw [hs, ht]
    _ = Î½ US + Î½ ((u âˆ© t) \ US) := by
      simp only [restrict_apply, hu, hu.diff hm, hÎ½, â† inter_comm t, inter_diff_assoc]
    _ = Î½ (US âˆª u âˆ© t) := measure_add_diff hm.nullMeasurableSet _
    _ = Î½ (u âˆ© s âˆª u âˆ© t) := .symm <| measure_union_congr_of_subset hsub hÎ½.le Subset.rfl le_rfl

theorem restrict_biUnion_finset_congr {s : Finset Î¹} {t : Î¹ â†’ Set Î±} :
    Î¼.restrict (â‹ƒ i âˆˆ s, t i) = Î½.restrict (â‹ƒ i âˆˆ s, t i) â†”
      âˆ€ i âˆˆ s, Î¼.restrict (t i) = Î½.restrict (t i) := by
  classical
  induction s using Finset.induction_on with
  | empty => simp
  | insert i s _ hs =>
    simp only [forall_eq_or_imp, iUnion_iUnion_eq_or_left, Finset.mem_insert]
    rw [restrict_union_congr, â† hs]

@[deprecated (since := "2025-08-28")]
alias restrict_finset_biUnion_congr := restrict_biUnion_finset_congr

theorem restrict_iUnion_congr [Countable Î¹] {s : Î¹ â†’ Set Î±} :
    Î¼.restrict (â‹ƒ i, s i) = Î½.restrict (â‹ƒ i, s i) â†” âˆ€ i, Î¼.restrict (s i) = Î½.restrict (s i) := by
  refine âŸ¨fun h i => restrict_congr_mono (subset_iUnion _ _) h, fun h => ?_âŸ©
  ext1 t ht
  have D : Directed (Â· âŠ† Â·) fun t : Finset Î¹ => â‹ƒ i âˆˆ t, s i :=
    Monotone.directed_le fun tâ‚ tâ‚‚ ht => biUnion_subset_biUnion_left ht
  rw [iUnion_eq_iUnion_finset]
  simp only [restrict_iUnion_apply_eq_iSup D ht, restrict_biUnion_finset_congr.2 fun i _ => h i]

theorem restrict_biUnion_congr {s : Set Î¹} {t : Î¹ â†’ Set Î±} (hc : s.Countable) :
    Î¼.restrict (â‹ƒ i âˆˆ s, t i) = Î½.restrict (â‹ƒ i âˆˆ s, t i) â†”
      âˆ€ i âˆˆ s, Î¼.restrict (t i) = Î½.restrict (t i) := by
  haveI := hc.toEncodable
  simp only [biUnion_eq_iUnion, SetCoe.forall', restrict_iUnion_congr]

theorem restrict_sUnion_congr {S : Set (Set Î±)} (hc : S.Countable) :
    Î¼.restrict (â‹ƒâ‚€ S) = Î½.restrict (â‹ƒâ‚€ S) â†” âˆ€ s âˆˆ S, Î¼.restrict s = Î½.restrict s := by
  rw [sUnion_eq_biUnion, restrict_biUnion_congr hc]

/-- This lemma shows that `Inf` and `restrict` commute for measures. -/
theorem restrict_sInf_eq_sInf_restrict {m0 : MeasurableSpace Î±} {m : Set (Measure Î±)}
    (hm : m.Nonempty) (ht : MeasurableSet t) :
    (sInf m).restrict t = sInf ((fun Î¼ : Measure Î± => Î¼.restrict t) '' m) := by
  ext1 s hs
  simp_rw [sInf_apply hs, restrict_apply hs, sInf_apply (MeasurableSet.inter hs ht),
    Set.image_image, restrict_toOuterMeasure_eq_toOuterMeasure_restrict ht, â†
    Set.image_image _ toOuterMeasure, â† OuterMeasure.restrict_sInf_eq_sInf_restrict _ (hm.image _),
    OuterMeasure.restrict_apply]

theorem exists_mem_of_measure_ne_zero_of_ae (hs : Î¼ s â‰  0) {p : Î± â†’ Prop}
    (hp : âˆ€áµ x âˆ‚Î¼.restrict s, p x) : âˆƒ x, x âˆˆ s âˆ§ p x := by
  rw [â† Î¼.restrict_apply_self, â† frequently_ae_mem_iff] at hs
  exact (hs.and_eventually hp).exists

/-- If a quasi measure preserving map `f` maps a set `s` to a set `t`,
then it is quasi measure preserving with respect to the restrictions of the measures. -/
theorem QuasiMeasurePreserving.restrict {Î½ : Measure Î²} {f : Î± â†’ Î²}
    (hf : QuasiMeasurePreserving f Î¼ Î½) {t : Set Î²} (hmaps : MapsTo f s t) :
    QuasiMeasurePreserving f (Î¼.restrict s) (Î½.restrict t) where
  measurable := hf.measurable
  absolutelyContinuous := by
    refine AbsolutelyContinuous.mk fun u hum â†¦ ?_
    suffices Î½ (u âˆ© t) = 0 â†’ Î¼ (f â»Â¹' u âˆ© s) = 0 by simpa [hum, hf.measurable, hf.measurable hum]
    refine fun hu â†¦ measure_mono_null ?_ (hf.preimage_null hu)
    rw [preimage_inter]
    gcongr
    assumption

/-! ### Extensionality results -/

/-- Two measures are equal if they have equal restrictions on a spanning collection of sets
  (formulated using `Union`). -/
theorem ext_iff_of_iUnion_eq_univ [Countable Î¹] {s : Î¹ â†’ Set Î±} (hs : â‹ƒ i, s i = univ) :
    Î¼ = Î½ â†” âˆ€ i, Î¼.restrict (s i) = Î½.restrict (s i) := by
  rw [â† restrict_iUnion_congr, hs, restrict_univ, restrict_univ]

alias âŸ¨_, ext_of_iUnion_eq_univâŸ© := ext_iff_of_iUnion_eq_univ

/-- Two measures are equal if they have equal restrictions on a spanning collection of sets
  (formulated using `biUnion`). -/
theorem ext_iff_of_biUnion_eq_univ {S : Set Î¹} {s : Î¹ â†’ Set Î±} (hc : S.Countable)
    (hs : â‹ƒ i âˆˆ S, s i = univ) : Î¼ = Î½ â†” âˆ€ i âˆˆ S, Î¼.restrict (s i) = Î½.restrict (s i) := by
  rw [â† restrict_biUnion_congr hc, hs, restrict_univ, restrict_univ]

alias âŸ¨_, ext_of_biUnion_eq_univâŸ© := ext_iff_of_biUnion_eq_univ

/-- Two measures are equal if they have equal restrictions on a spanning collection of sets
  (formulated using `sUnion`). -/
theorem ext_iff_of_sUnion_eq_univ {S : Set (Set Î±)} (hc : S.Countable) (hs : â‹ƒâ‚€ S = univ) :
    Î¼ = Î½ â†” âˆ€ s âˆˆ S, Î¼.restrict s = Î½.restrict s :=
  ext_iff_of_biUnion_eq_univ hc <| by rwa [â† sUnion_eq_biUnion]

alias âŸ¨_, ext_of_sUnion_eq_univâŸ© := ext_iff_of_sUnion_eq_univ

theorem ext_of_generateFrom_of_cover {S T : Set (Set Î±)} (h_gen : â€¹_â€º = generateFrom S)
    (hc : T.Countable) (h_inter : IsPiSystem S) (hU : â‹ƒâ‚€ T = univ) (htop : âˆ€ t âˆˆ T, Î¼ t â‰  âˆ)
    (ST_eq : âˆ€ t âˆˆ T, âˆ€ s âˆˆ S, Î¼ (s âˆ© t) = Î½ (s âˆ© t)) (T_eq : âˆ€ t âˆˆ T, Î¼ t = Î½ t) : Î¼ = Î½ := by
  refine ext_of_sUnion_eq_univ hc hU fun t ht => ?_
  ext1 u hu
  simp only [restrict_apply hu]
  induction u, hu using induction_on_inter h_gen h_inter with
  | empty => simp only [Set.empty_inter, measure_empty]
  | basic u hu => exact ST_eq _ ht _ hu
  | compl u hu ihu =>
    have := T_eq t ht
    rw [Set.inter_comm] at ihu âŠ¢
    rwa [â† measure_inter_add_diff t hu, â† measure_inter_add_diff t hu, â† ihu,
      ENNReal.add_right_inj] at this
    exact ne_top_of_le_ne_top (htop t ht) (measure_mono Set.inter_subset_left)
  | iUnion f hfd hfm ihf =>
    simp only [â† restrict_apply (hfm _), â† restrict_apply (MeasurableSet.iUnion hfm)] at ihf âŠ¢
    simp only [measure_iUnion hfd hfm, ihf]

/-- Two measures are equal if they are equal on the Ï€-system generating the Ïƒ-algebra,
  and they are both finite on an increasing spanning sequence of sets in the Ï€-system.
  This lemma is formulated using `sUnion`. -/
theorem ext_of_generateFrom_of_cover_subset {S T : Set (Set Î±)} (h_gen : â€¹_â€º = generateFrom S)
    (h_inter : IsPiSystem S) (h_sub : T âŠ† S) (hc : T.Countable) (hU : â‹ƒâ‚€ T = univ)
    (htop : âˆ€ s âˆˆ T, Î¼ s â‰  âˆ) (h_eq : âˆ€ s âˆˆ S, Î¼ s = Î½ s) : Î¼ = Î½ := by
  refine ext_of_generateFrom_of_cover h_gen hc h_inter hU htop ?_ fun t ht => h_eq t (h_sub ht)
  intro t ht s hs; rcases (s âˆ© t).eq_empty_or_nonempty with H | H
  Â· simp only [H, measure_empty]
  Â· exact h_eq _ (h_inter _ hs _ (h_sub ht) H)

/-- Two measures are equal if they are equal on the Ï€-system generating the Ïƒ-algebra,
  and they are both finite on an increasing spanning sequence of sets in the Ï€-system.
  This lemma is formulated using `iUnion`.
  `FiniteSpanningSetsIn.ext` is a reformulation of this lemma. -/
theorem ext_of_generateFrom_of_iUnion (C : Set (Set Î±)) (B : â„• â†’ Set Î±) (hA : â€¹_â€º = generateFrom C)
    (hC : IsPiSystem C) (h1B : â‹ƒ i, B i = univ) (h2B : âˆ€ i, B i âˆˆ C) (hÎ¼B : âˆ€ i, Î¼ (B i) â‰  âˆ)
    (h_eq : âˆ€ s âˆˆ C, Î¼ s = Î½ s) : Î¼ = Î½ := by
  refine ext_of_generateFrom_of_cover_subset hA hC ?_ (countable_range B) h1B ?_ h_eq
  Â· rintro _ âŸ¨i, rflâŸ©
    apply h2B
  Â· rintro _ âŸ¨i, rflâŸ©
    apply hÎ¼B

@[simp]
theorem restrict_sum (Î¼ : Î¹ â†’ Measure Î±) {s : Set Î±} (hs : MeasurableSet s) :
    (sum Î¼).restrict s = sum fun i => (Î¼ i).restrict s :=
  ext fun t ht => by simp only [sum_apply, restrict_apply, ht, ht.inter hs]

@[simp]
theorem restrict_sum_of_countable [Countable Î¹] (Î¼ : Î¹ â†’ Measure Î±) (s : Set Î±) :
    (sum Î¼).restrict s = sum fun i => (Î¼ i).restrict s := by
  ext t ht
  simp_rw [sum_apply _ ht, restrict_apply ht, sum_apply_of_countable]

lemma AbsolutelyContinuous.restrict (h : Î¼ â‰ª Î½) (s : Set Î±) : Î¼.restrict s â‰ª Î½.restrict s := by
  refine Measure.AbsolutelyContinuous.mk (fun t ht htÎ½ â†¦ ?_)
  rw [restrict_apply ht] at htÎ½ âŠ¢
  exact h htÎ½

theorem restrict_iUnion_ae [Countable Î¹] {s : Î¹ â†’ Set Î±} (hd : Pairwise (AEDisjoint Î¼ on s))
    (hm : âˆ€ i, NullMeasurableSet (s i) Î¼) : Î¼.restrict (â‹ƒ i, s i) = sum fun i => Î¼.restrict (s i) :=
  ext fun t ht => by simp only [sum_apply _ ht, restrict_iUnion_apply_ae hd hm ht]

theorem restrict_iUnion [Countable Î¹] {s : Î¹ â†’ Set Î±} (hd : Pairwise (Disjoint on s))
    (hm : âˆ€ i, MeasurableSet (s i)) : Î¼.restrict (â‹ƒ i, s i) = sum fun i => Î¼.restrict (s i) :=
  restrict_iUnion_ae hd.aedisjoint fun i => (hm i).nullMeasurableSet

theorem restrict_biUnion {s : Î¹ â†’ Set Î±} {T : Set Î¹} (hT : Countable T)
    (hd : T.Pairwise (Disjoint on s)) (hm : âˆ€ i, MeasurableSet (s i)) :
    Î¼.restrict (â‹ƒ i âˆˆ T, s i) = sum fun (i : T) => Î¼.restrict (s i) := by
  rw [Set.biUnion_eq_iUnion]
  exact restrict_iUnion (fun i j hij â†¦ hd i.coe_prop j.coe_prop (Subtype.coe_ne_coe.mpr hij)) (hm Â·)

theorem restrict_biUnion_finset {s : Î¹ â†’ Set Î±} {T : Finset Î¹}
    (hd : T.toSet.Pairwise (Disjoint on s)) (hm : âˆ€ i, MeasurableSet (s i)) :
    Î¼.restrict (â‹ƒ i âˆˆ T, s i) = sum fun (i : T) => Î¼.restrict (s i) :=
  restrict_biUnion (T := T.toSet) Finite.to_countable hd hm

theorem restrict_iUnion_le [Countable Î¹] {s : Î¹ â†’ Set Î±} :
    Î¼.restrict (â‹ƒ i, s i) â‰¤ sum fun i => Î¼.restrict (s i) :=
  le_iff.2 fun t ht â†¦ by simpa [ht, inter_iUnion] using measure_iUnion_le (t âˆ© s Â·)

theorem restrict_biUnion_le {s : Î¹ â†’ Set Î±} {T : Set Î¹} (hT : Countable T) :
    Î¼.restrict (â‹ƒ i âˆˆ T, s i) â‰¤ sum fun (i : T) => Î¼.restrict (s i) :=
  le_iff.2 fun t ht â†¦ by simpa [ht, inter_iUnion] using measure_biUnion_le Î¼ hT (t âˆ© s Â·)

end Measure

@[simp]
theorem ae_restrict_iUnion_eq [Countable Î¹] (s : Î¹ â†’ Set Î±) :
    ae (Î¼.restrict (â‹ƒ i, s i)) = â¨† i, ae (Î¼.restrict (s i)) :=
  le_antisymm ((ae_sum_eq fun i => Î¼.restrict (s i)) â–¸ ae_mono restrict_iUnion_le) <|
    iSup_le fun i => ae_mono <| restrict_mono (subset_iUnion s i) le_rfl

@[simp]
theorem ae_restrict_union_eq (s t : Set Î±) :
    ae (Î¼.restrict (s âˆª t)) = ae (Î¼.restrict s) âŠ” ae (Î¼.restrict t) := by
  simp [union_eq_iUnion, iSup_bool_eq]

theorem ae_restrict_biUnion_eq (s : Î¹ â†’ Set Î±) {t : Set Î¹} (ht : t.Countable) :
    ae (Î¼.restrict (â‹ƒ i âˆˆ t, s i)) = â¨† i âˆˆ t, ae (Î¼.restrict (s i)) := by
  haveI := ht.to_subtype
  rw [biUnion_eq_iUnion, ae_restrict_iUnion_eq, â† iSup_subtype'']

theorem ae_restrict_biUnion_finset_eq (s : Î¹ â†’ Set Î±) (t : Finset Î¹) :
    ae (Î¼.restrict (â‹ƒ i âˆˆ t, s i)) = â¨† i âˆˆ t, ae (Î¼.restrict (s i)) :=
  ae_restrict_biUnion_eq s t.countable_toSet

theorem ae_restrict_iUnion_iff [Countable Î¹] (s : Î¹ â†’ Set Î±) (p : Î± â†’ Prop) :
    (âˆ€áµ x âˆ‚Î¼.restrict (â‹ƒ i, s i), p x) â†” âˆ€ i, âˆ€áµ x âˆ‚Î¼.restrict (s i), p x := by simp

theorem ae_restrict_union_iff (s t : Set Î±) (p : Î± â†’ Prop) :
    (âˆ€áµ x âˆ‚Î¼.restrict (s âˆª t), p x) â†” (âˆ€áµ x âˆ‚Î¼.restrict s, p x) âˆ§ âˆ€áµ x âˆ‚Î¼.restrict t, p x := by simp

theorem ae_restrict_biUnion_iff (s : Î¹ â†’ Set Î±) {t : Set Î¹} (ht : t.Countable) (p : Î± â†’ Prop) :
    (âˆ€áµ x âˆ‚Î¼.restrict (â‹ƒ i âˆˆ t, s i), p x) â†” âˆ€ i âˆˆ t, âˆ€áµ x âˆ‚Î¼.restrict (s i), p x := by
  simp_rw [Filter.Eventually, ae_restrict_biUnion_eq s ht, mem_iSup]

@[simp]
theorem ae_restrict_biUnion_finset_iff (s : Î¹ â†’ Set Î±) (t : Finset Î¹) (p : Î± â†’ Prop) :
    (âˆ€áµ x âˆ‚Î¼.restrict (â‹ƒ i âˆˆ t, s i), p x) â†” âˆ€ i âˆˆ t, âˆ€áµ x âˆ‚Î¼.restrict (s i), p x := by
  simp_rw [Filter.Eventually, ae_restrict_biUnion_finset_eq s, mem_iSup]

theorem ae_eq_restrict_iUnion_iff [Countable Î¹] (s : Î¹ â†’ Set Î±) (f g : Î± â†’ Î´) :
    f =áµ[Î¼.restrict (â‹ƒ i, s i)] g â†” âˆ€ i, f =áµ[Î¼.restrict (s i)] g := by
  simp_rw [EventuallyEq, ae_restrict_iUnion_eq, eventually_iSup]

theorem ae_eq_restrict_biUnion_iff (s : Î¹ â†’ Set Î±) {t : Set Î¹} (ht : t.Countable) (f g : Î± â†’ Î´) :
    f =áµ[Î¼.restrict (â‹ƒ i âˆˆ t, s i)] g â†” âˆ€ i âˆˆ t, f =áµ[Î¼.restrict (s i)] g := by
  simp_rw [ae_restrict_biUnion_eq s ht, EventuallyEq, eventually_iSup]

theorem ae_eq_restrict_biUnion_finset_iff (s : Î¹ â†’ Set Î±) (t : Finset Î¹) (f g : Î± â†’ Î´) :
    f =áµ[Î¼.restrict (â‹ƒ i âˆˆ t, s i)] g â†” âˆ€ i âˆˆ t, f =áµ[Î¼.restrict (s i)] g :=
  ae_eq_restrict_biUnion_iff s t.countable_toSet f g

open scoped Interval in
theorem ae_restrict_uIoc_eq [LinearOrder Î±] (a b : Î±) :
    ae (Î¼.restrict (Î™ a b)) = ae (Î¼.restrict (Ioc a b)) âŠ” ae (Î¼.restrict (Ioc b a)) := by
  simp only [uIoc_eq_union, ae_restrict_union_eq]

open scoped Interval in
/-- See also `MeasureTheory.ae_uIoc_iff`. -/
theorem ae_restrict_uIoc_iff [LinearOrder Î±] {a b : Î±} {P : Î± â†’ Prop} :
    (âˆ€áµ x âˆ‚Î¼.restrict (Î™ a b), P x) â†”
      (âˆ€áµ x âˆ‚Î¼.restrict (Ioc a b), P x) âˆ§ âˆ€áµ x âˆ‚Î¼.restrict (Ioc b a), P x := by
  rw [ae_restrict_uIoc_eq, eventually_sup]

theorem ae_restrict_iffâ‚€ {p : Î± â†’ Prop} (hp : NullMeasurableSet { x | p x } (Î¼.restrict s)) :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ p x := by
  simp only [ae_iff, â† compl_setOf, Measure.restrict_applyâ‚€ hp.compl]
  rw [iff_iff_eq]; congr with x; simp [and_comm]

theorem ae_restrict_iff {p : Î± â†’ Prop} (hp : MeasurableSet { x | p x }) :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ p x :=
  ae_restrict_iffâ‚€ hp.nullMeasurableSet

theorem ae_imp_of_ae_restrict {s : Set Î±} {p : Î± â†’ Prop} (h : âˆ€áµ x âˆ‚Î¼.restrict s, p x) :
    âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ p x := by
  simp only [ae_iff] at h âŠ¢
  simpa [setOf_and, inter_comm] using measure_inter_eq_zero_of_restrict h

theorem ae_restrict_iff'â‚€ {p : Î± â†’ Prop} (hs : NullMeasurableSet s Î¼) :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ p x := by
  simp only [ae_iff, â† compl_setOf, restrict_applyâ‚€' hs]
  rw [iff_iff_eq]; congr with x; simp [and_comm]

theorem ae_restrict_iff' {p : Î± â†’ Prop} (hs : MeasurableSet s) :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ p x :=
  ae_restrict_iff'â‚€ hs.nullMeasurableSet

theorem _root_.Filter.EventuallyEq.restrict {f g : Î± â†’ Î´} {s : Set Î±} (hfg : f =áµ[Î¼] g) :
    f =áµ[Î¼.restrict s] g := by
  -- note that we cannot use `ae_restrict_iff` since we do not require measurability
  refine hfg.filter_mono ?_
  rw [Measure.ae_le_iff_absolutelyContinuous]
  exact absolutelyContinuous_restrict

theorem ae_restrict_memâ‚€ (hs : NullMeasurableSet s Î¼) : âˆ€áµ x âˆ‚Î¼.restrict s, x âˆˆ s :=
  (ae_restrict_iff'â‚€ hs).2 (Filter.Eventually.of_forall fun _ => id)

theorem ae_restrict_mem (hs : MeasurableSet s) : âˆ€áµ x âˆ‚Î¼.restrict s, x âˆˆ s :=
  ae_restrict_memâ‚€ hs.nullMeasurableSet

theorem ae_restrict_of_forall_mem {Î¼ : Measure Î±} {s : Set Î±}
    (hs : MeasurableSet s) {p : Î± â†’ Prop} (h : âˆ€ x âˆˆ s, p x) : âˆ€áµ (x : Î±) âˆ‚Î¼.restrict s, p x :=
  (ae_restrict_mem hs).mono h

theorem ae_restrict_of_ae {s : Set Î±} {p : Î± â†’ Prop} (h : âˆ€áµ x âˆ‚Î¼, p x) : âˆ€áµ x âˆ‚Î¼.restrict s, p x :=
  h.filter_mono (ae_mono Measure.restrict_le_self)

theorem ae_restrict_of_ae_restrict_of_subset {s t : Set Î±} {p : Î± â†’ Prop} (hst : s âŠ† t)
    (h : âˆ€áµ x âˆ‚Î¼.restrict t, p x) : âˆ€áµ x âˆ‚Î¼.restrict s, p x :=
  h.filter_mono (ae_mono <| Measure.restrict_mono hst (le_refl Î¼))

theorem ae_of_ae_restrict_of_ae_restrict_compl (t : Set Î±) {p : Î± â†’ Prop}
    (ht : âˆ€áµ x âˆ‚Î¼.restrict t, p x) (htc : âˆ€áµ x âˆ‚Î¼.restrict tá¶œ, p x) : âˆ€áµ x âˆ‚Î¼, p x :=
  nonpos_iff_eq_zero.1 <|
    calc
      Î¼ { x | Â¬p x } â‰¤ Î¼ ({ x | Â¬p x } âˆ© t) + Î¼ ({ x | Â¬p x } âˆ© tá¶œ) :=
        measure_le_inter_add_diff _ _ _
      _ â‰¤ Î¼.restrict t { x | Â¬p x } + Î¼.restrict tá¶œ { x | Â¬p x } :=
        add_le_add (le_restrict_apply _ _) (le_restrict_apply _ _)
      _ = 0 := by rw [ae_iff.1 ht, ae_iff.1 htc, zero_add]

theorem mem_map_restrict_ae_iff {Î²} {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} (hs : MeasurableSet s) :
    t âˆˆ Filter.map f (ae (Î¼.restrict s)) â†” Î¼ ((f â»Â¹' t)á¶œ âˆ© s) = 0 := by
  rw [mem_map, mem_ae_iff, Measure.restrict_apply' hs]

theorem ae_add_measure_iff {p : Î± â†’ Prop} {Î½} :
    (âˆ€áµ x âˆ‚Î¼ + Î½, p x) â†” (âˆ€áµ x âˆ‚Î¼, p x) âˆ§ âˆ€áµ x âˆ‚Î½, p x :=
  add_eq_zero

theorem ae_eq_comp' {Î½ : Measure Î²} {f : Î± â†’ Î²} {g g' : Î² â†’ Î´} (hf : AEMeasurable f Î¼)
    (h : g =áµ[Î½] g') (h2 : Î¼.map f â‰ª Î½) : g âˆ˜ f =áµ[Î¼] g' âˆ˜ f :=
  (tendsto_ae_map hf).mono_right h2.ae_le h

theorem Measure.QuasiMeasurePreserving.ae_eq_comp {Î½ : Measure Î²} {f : Î± â†’ Î²} {g g' : Î² â†’ Î´}
    (hf : QuasiMeasurePreserving f Î¼ Î½) (h : g =áµ[Î½] g') : g âˆ˜ f =áµ[Î¼] g' âˆ˜ f :=
  ae_eq_comp' hf.aemeasurable h hf.absolutelyContinuous

theorem ae_eq_comp {f : Î± â†’ Î²} {g g' : Î² â†’ Î´} (hf : AEMeasurable f Î¼) (h : g =áµ[Î¼.map f] g') :
    g âˆ˜ f =áµ[Î¼] g' âˆ˜ f :=
  ae_eq_comp' hf h AbsolutelyContinuous.rfl

@[to_additive]
theorem div_ae_eq_one {Î²} [Group Î²] (f g : Î± â†’ Î²) : f / g =áµ[Î¼] 1 â†” f =áµ[Î¼] g := by
  refine âŸ¨fun h â†¦ h.mono fun x hx â†¦ ?_, fun h â†¦ h.mono fun x hx â†¦ ?_âŸ©
  Â· rwa [Pi.div_apply, Pi.one_apply, div_eq_one] at hx
  Â· rwa [Pi.div_apply, Pi.one_apply, div_eq_one]

@[to_additive sub_nonneg_ae]
lemma one_le_div_ae {Î² : Type*} [Group Î²] [LE Î²] [MulRightMono Î²] (f g : Î± â†’ Î²) :
    1 â‰¤áµ[Î¼] g / f â†” f â‰¤áµ[Î¼] g := by
  refine âŸ¨fun h â†¦ h.mono fun a ha â†¦ ?_, fun h â†¦ h.mono fun a ha â†¦ ?_âŸ©
  Â· rwa [Pi.one_apply, Pi.div_apply, one_le_div'] at ha
  Â· rwa [Pi.one_apply, Pi.div_apply, one_le_div']

theorem le_ae_restrict : ae Î¼ âŠ“ ğ“Ÿ s â‰¤ ae (Î¼.restrict s) := fun _s hs =>
  eventually_inf_principal.2 (ae_imp_of_ae_restrict hs)

@[simp]
theorem ae_restrict_eq (hs : MeasurableSet s) : ae (Î¼.restrict s) = ae Î¼ âŠ“ ğ“Ÿ s := by
  ext t
  simp only [mem_inf_principal, mem_ae_iff, restrict_apply_eq_zero' hs, compl_setOf,
    Classical.not_imp, fun a => and_comm (a := a âˆˆ s) (b := a âˆ‰ t)]
  rfl

lemma ae_restrict_le : ae (Î¼.restrict s) â‰¤ ae Î¼ :=
  ae_mono restrict_le_self

theorem ae_restrict_eq_bot {s} : ae (Î¼.restrict s) = âŠ¥ â†” Î¼ s = 0 :=
  ae_eq_bot.trans restrict_eq_zero

theorem ae_restrict_neBot {s} : (ae <| Î¼.restrict s).NeBot â†” Î¼ s â‰  0 :=
  neBot_iff.trans ae_restrict_eq_bot.not

theorem self_mem_ae_restrict {s} (hs : MeasurableSet s) : s âˆˆ ae (Î¼.restrict s) := by
  simp only [ae_restrict_eq hs, mem_principal, mem_inf_iff]
  exact âŸ¨_, univ_mem, s, Subset.rfl, (univ_inter s).symmâŸ©

/-- If two measurable sets are ae_eq then any proposition that is almost everywhere true on one
is almost everywhere true on the other -/
theorem ae_restrict_of_ae_eq_of_ae_restrict {s t} (hst : s =áµ[Î¼] t) {p : Î± â†’ Prop} :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†’ âˆ€áµ x âˆ‚Î¼.restrict t, p x := by simp [Measure.restrict_congr_set hst]

/-- If two measurable sets are ae_eq then any proposition that is almost everywhere true on one
is almost everywhere true on the other -/
theorem ae_restrict_congr_set {s t} (hst : s =áµ[Î¼] t) {p : Î± â†’ Prop} :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†” âˆ€áµ x âˆ‚Î¼.restrict t, p x :=
  âŸ¨ae_restrict_of_ae_eq_of_ae_restrict hst, ae_restrict_of_ae_eq_of_ae_restrict hst.symmâŸ©

lemma NullMeasurable.measure_preimage_eq_measure_restrict_preimage_of_ae_compl_eq_const
    {Î² : Type*} [MeasurableSpace Î²] {b : Î²} {f : Î± â†’ Î²} {s : Set Î±}
    (f_mble : NullMeasurable f (Î¼.restrict s)) (hs : f =áµ[Measure.restrict Î¼ sá¶œ] (fun _ â†¦ b))
    {t : Set Î²} (t_mble : MeasurableSet t) (ht : b âˆ‰ t) :
    Î¼ (f â»Â¹' t) = Î¼.restrict s (f â»Â¹' t) := by
  rw [Measure.restrict_applyâ‚€ (f_mble t_mble)]
  rw [EventuallyEq, ae_iff, Measure.restrict_applyâ‚€] at hs
  Â· apply le_antisymm _ (measure_mono inter_subset_left)
    apply (measure_mono (Eq.symm (inter_union_compl (f â»Â¹' t) s)).le).trans
    apply (measure_union_le _ _).trans
    have obs : Î¼ ((f â»Â¹' t) âˆ© sá¶œ) = 0 := by
      apply le_antisymm _ (zero_le _)
      rw [â† hs]
      apply measure_mono (inter_subset_inter_left _ _)
      intro x hx hfx
      simp only [mem_preimage] at hx hfx
      exact ht (hfx â–¸ hx)
    simp only [obs, add_zero, le_refl]
  Â· exact NullMeasurableSet.of_null hs

lemma nullMeasurableSet_restrict (hs : NullMeasurableSet s Î¼) {t : Set Î±} :
    NullMeasurableSet t (Î¼.restrict s) â†” NullMeasurableSet (t âˆ© s) Î¼ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· obtain âŸ¨t', -, ht', t'tâŸ© : âˆƒ t' âŠ‡ t, MeasurableSet t' âˆ§ t' =áµ[Î¼.restrict s] t :=
      h.exists_measurable_superset_ae_eq
    have A : (t' âˆ© s : Set Î±) =áµ[Î¼] (t âˆ© s : Set Î±) := by
      have : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ (x âˆˆ t') = (x âˆˆ t) :=
        (ae_restrict_iff'â‚€ hs).1 t't
      filter_upwards [this] with y hy
      change (y âˆˆ t' âˆ© s) = (y âˆˆ t âˆ© s)
      simpa only [eq_iff_iff, mem_inter_iff, and_congr_left_iff] using hy
    obtain âŸ¨s', -, hs', s'sâŸ© : âˆƒ s' âŠ‡ s, MeasurableSet s' âˆ§ s' =áµ[Î¼] s :=
      hs.exists_measurable_superset_ae_eq
    have B : (t' âˆ© s' : Set Î±) =áµ[Î¼] (t' âˆ© s : Set Î±) :=
      ae_eq_set_inter (EventuallyEq.refl _ _) s's
    exact (ht'.inter hs').nullMeasurableSet.congr (B.trans A)
  Â· have A : NullMeasurableSet (t \ s) (Î¼.restrict s) := by
      apply NullMeasurableSet.of_null
      rw [Measure.restrict_applyâ‚€' hs]
      simp
    have B : NullMeasurableSet (t âˆ© s) (Î¼.restrict s) :=
      h.mono_ac absolutelyContinuous_restrict
    simpa using A.union B

lemma nullMeasurableSet_restrict_of_subset {t : Set Î±} (ht : t âŠ† s) :
    NullMeasurableSet t (Î¼.restrict s) â†” NullMeasurableSet t Î¼ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h.mono_ac absolutelyContinuous_restrictâŸ©
  obtain âŸ¨t', t'_subs, ht', t'tâŸ© : âˆƒ t' âŠ† t, MeasurableSet t' âˆ§ t' =áµ[Î¼.restrict s] t :=
    h.exists_measurable_subset_ae_eq
  have : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ (x âˆˆ t' â†” x âˆˆ t) := by
    apply ae_imp_of_ae_restrict
    filter_upwards [t't] with x hx using by simpa using hx
  have : t' =áµ[Î¼] t := by
    filter_upwards [this] with x hx
    change (x âˆˆ t') = (x âˆˆ t)
    simp only [eq_iff_iff]
    tauto
  exact ht'.nullMeasurableSet.congr this

namespace Measure

section Subtype

/-! ### Subtype of a measure space -/

section ComapAnyMeasure

theorem MeasurableSet.nullMeasurableSet_subtype_coe {t : Set s} (hs : NullMeasurableSet s Î¼)
    (ht : MeasurableSet t) : NullMeasurableSet ((â†‘) '' t) Î¼ := by
  rw [Subtype.instMeasurableSpace, comap_eq_generateFrom] at ht
  induction t, ht using generateFrom_induction with
  | hC t' ht' =>
    obtain âŸ¨s', hs', rflâŸ© := ht'
    rw [Subtype.image_preimage_coe]
    exact hs.inter (hs'.nullMeasurableSet)
  | empty => simp only [image_empty, nullMeasurableSet_empty]
  | compl t' _ ht' =>
    simp only [â† range_diff_image Subtype.coe_injective, Subtype.range_coe_subtype, setOf_mem_eq]
    exact hs.diff ht'
  | iUnion f _ hf =>
    dsimp only []
    rw [image_iUnion]
    exact .iUnion hf

theorem NullMeasurableSet.subtype_coe {t : Set s} (hs : NullMeasurableSet s Î¼)
    (ht : NullMeasurableSet t (Î¼.comap Subtype.val)) : NullMeasurableSet (((â†‘) : s â†’ Î±) '' t) Î¼ :=
  NullMeasurableSet.image _ Î¼ Subtype.coe_injective
    (fun _ => MeasurableSet.nullMeasurableSet_subtype_coe hs) ht

theorem measure_subtype_coe_le_comap (hs : NullMeasurableSet s Î¼) (t : Set s) :
    Î¼ (((â†‘) : s â†’ Î±) '' t) â‰¤ Î¼.comap Subtype.val t :=
  le_comap_apply _ _ Subtype.coe_injective (fun _ =>
    MeasurableSet.nullMeasurableSet_subtype_coe hs) _

theorem measure_subtype_coe_eq_zero_of_comap_eq_zero (hs : NullMeasurableSet s Î¼) {t : Set s}
    (ht : Î¼.comap Subtype.val t = 0) : Î¼ (((â†‘) : s â†’ Î±) '' t) = 0 :=
  eq_bot_iff.mpr <| (measure_subtype_coe_le_comap hs t).trans ht.le

end ComapAnyMeasure

section MeasureSpace

variable {u : Set Î´} [MeasureSpace Î´] {p : Î´ â†’ Prop}

/-- In a measure space, one can restrict the measure to a subtype to get a new measure space.
Not registered as an instance, as there are other natural choices such as the normalized restriction
for a probability measure, or the subspace measure when restricting to a vector subspace. Enable
locally if needed with `attribute [local instance] Measure.Subtype.measureSpace`. -/
noncomputable def Subtype.measureSpace : MeasureSpace (Subtype p) where
  volume := Measure.comap Subtype.val volume

attribute [local instance] Subtype.measureSpace

theorem Subtype.volume_def : (volume : Measure u) = volume.comap Subtype.val :=
  rfl

theorem Subtype.volume_univ (hu : NullMeasurableSet u) : volume (univ : Set u) = volume u := by
  rw [Subtype.volume_def, comap_applyâ‚€ _ _ _ _ MeasurableSet.univ.nullMeasurableSet]
  Â· simp only [image_univ, Subtype.range_coe_subtype, setOf_mem_eq]
  Â· exact Subtype.coe_injective
  Â· exact fun t => MeasurableSet.nullMeasurableSet_subtype_coe hu

theorem volume_subtype_coe_le_volume (hu : NullMeasurableSet u) (t : Set u) :
    volume (((â†‘) : u â†’ Î´) '' t) â‰¤ volume t :=
  measure_subtype_coe_le_comap hu t

theorem volume_subtype_coe_eq_zero_of_volume_eq_zero (hu : NullMeasurableSet u) {t : Set u}
    (ht : volume t = 0) : volume (((â†‘) : u â†’ Î´) '' t) = 0 :=
  measure_subtype_coe_eq_zero_of_comap_eq_zero hu ht

end MeasureSpace

end Subtype

end Measure

end MeasureTheory

open MeasureTheory Measure

namespace MeasurableEmbedding

variable {m0 : MeasurableSpace Î±} {m1 : MeasurableSpace Î²} {f : Î± â†’ Î²}

section
variable (hf : MeasurableEmbedding f)
include hf

theorem map_comap (Î¼ : Measure Î²) : (comap f Î¼).map f = Î¼.restrict (range f) := by
  ext1 t ht
  rw [hf.map_apply, comap_apply f hf.injective hf.measurableSet_image' _ (hf.measurable ht),
    image_preimage_eq_inter_range, Measure.restrict_apply ht]

theorem comap_apply (Î¼ : Measure Î²) (s : Set Î±) : comap f Î¼ s = Î¼ (f '' s) :=
  calc
    comap f Î¼ s = comap f Î¼ (f â»Â¹' (f '' s)) := by rw [hf.injective.preimage_image]
    _ = (comap f Î¼).map f (f '' s) := (hf.map_apply _ _).symm
    _ = Î¼ (f '' s) := by
      rw [hf.map_comap, restrict_apply' hf.measurableSet_range,
        inter_eq_self_of_subset_left (image_subset_range _ _)]

theorem comap_map (Î¼ : Measure Î±) : (map f Î¼).comap f = Î¼ := by
  ext t _
  rw [hf.comap_apply, hf.map_apply, preimage_image_eq _ hf.injective]

theorem ae_map_iff {p : Î² â†’ Prop} {Î¼ : Measure Î±} : (âˆ€áµ x âˆ‚Î¼.map f, p x) â†” âˆ€áµ x âˆ‚Î¼, p (f x) := by
  simp only [ae_iff, hf.map_apply, preimage_setOf_eq]

theorem restrict_map (Î¼ : Measure Î±) (s : Set Î²) :
    (Î¼.map f).restrict s = (Î¼.restrict <| f â»Â¹' s).map f :=
  Measure.ext fun t ht => by simp [hf.map_apply, ht, hf.measurable ht]

protected theorem comap_preimage (Î¼ : Measure Î²) (s : Set Î²) :
    Î¼.comap f (f â»Â¹' s) = Î¼ (s âˆ© range f) := by
  rw [â† hf.map_apply, hf.map_comap, restrict_apply' hf.measurableSet_range]

lemma comap_restrict (Î¼ : Measure Î²) (s : Set Î²) :
    (Î¼.restrict s).comap f = (Î¼.comap f).restrict (f â»Â¹' s) := by
  ext t ht
  rw [Measure.restrict_apply ht, comap_apply hf, comap_apply hf,
    Measure.restrict_apply (hf.measurableSet_image.2 ht), image_inter_preimage]

lemma restrict_comap (Î¼ : Measure Î²) (s : Set Î±) :
    (Î¼.comap f).restrict s = (Î¼.restrict (f '' s)).comap f := by
  rw [comap_restrict hf, preimage_image_eq _ hf.injective]

end

theorem _root_.MeasurableEquiv.restrict_map (e : Î± â‰ƒáµ Î²) (Î¼ : Measure Î±) (s : Set Î²) :
    (Î¼.map e).restrict s = (Î¼.restrict <| e â»Â¹' s).map e :=
  e.measurableEmbedding.restrict_map _ _

end MeasurableEmbedding

section Subtype

theorem comap_subtype_coe_apply {_m0 : MeasurableSpace Î±} {s : Set Î±} (hs : MeasurableSet s)
    (Î¼ : Measure Î±) (t : Set s) : comap (â†‘) Î¼ t = Î¼ ((â†‘) '' t) :=
  (MeasurableEmbedding.subtype_coe hs).comap_apply _ _

theorem map_comap_subtype_coe {m0 : MeasurableSpace Î±} {s : Set Î±} (hs : MeasurableSet s)
    (Î¼ : Measure Î±) : (comap (â†‘) Î¼).map ((â†‘) : s â†’ Î±) = Î¼.restrict s := by
  rw [(MeasurableEmbedding.subtype_coe hs).map_comap, Subtype.range_coe]

theorem ae_restrict_iff_subtype {m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {s : Set Î±}
    (hs : MeasurableSet s) {p : Î± â†’ Prop} :
    (âˆ€áµ x âˆ‚Î¼.restrict s, p x) â†” âˆ€áµ (x : s) âˆ‚comap ((â†‘) : s â†’ Î±) Î¼, p x := by
  rw [â† map_comap_subtype_coe hs, (MeasurableEmbedding.subtype_coe hs).ae_map_iff]

variable [MeasureSpace Î±] {s t : Set Î±}

/-!
### Volume on `s : Set Î±`

Note the instance is provided earlier as `Subtype.measureSpace`.
-/
attribute [local instance] Subtype.measureSpace

theorem volume_set_coe_def (s : Set Î±) : (volume : Measure s) = comap ((â†‘) : s â†’ Î±) volume :=
  rfl

theorem MeasurableSet.map_coe_volume {s : Set Î±} (hs : MeasurableSet s) :
    volume.map ((â†‘) : s â†’ Î±) = restrict volume s := by
  rw [volume_set_coe_def, (MeasurableEmbedding.subtype_coe hs).map_comap volume, Subtype.range_coe]

theorem volume_image_subtype_coe {s : Set Î±} (hs : MeasurableSet s) (t : Set s) :
    volume ((â†‘) '' t : Set Î±) = volume t :=
  (comap_subtype_coe_apply hs volume t).symm

@[simp]
theorem volume_preimage_coe (hs : NullMeasurableSet s) (ht : MeasurableSet t) :
    volume (((â†‘) : s â†’ Î±) â»Â¹' t) = volume (t âˆ© s) := by
  rw [volume_set_coe_def,
    comap_applyâ‚€ _ _ Subtype.coe_injective
      (fun h => MeasurableSet.nullMeasurableSet_subtype_coe hs)
      (measurable_subtype_coe ht).nullMeasurableSet,
    image_preimage_eq_inter_range, Subtype.range_coe]

end Subtype

section Piecewise

variable [MeasurableSpace Î±] {Î¼ : Measure Î±} {s t : Set Î±} {f g : Î± â†’ Î²}

theorem piecewise_ae_eq_restrict [DecidablePred (Â· âˆˆ s)] (hs : MeasurableSet s) :
    piecewise s f g =áµ[Î¼.restrict s] f := by
  rw [ae_restrict_eq hs]
  exact (piecewise_eqOn s f g).eventuallyEq.filter_mono inf_le_right

theorem piecewise_ae_eq_restrict_compl [DecidablePred (Â· âˆˆ s)] (hs : MeasurableSet s) :
    piecewise s f g =áµ[Î¼.restrict sá¶œ] g := by
  rw [ae_restrict_eq hs.compl]
  exact (piecewise_eqOn_compl s f g).eventuallyEq.filter_mono inf_le_right

theorem piecewise_ae_eq_of_ae_eq_set [DecidablePred (Â· âˆˆ s)] [DecidablePred (Â· âˆˆ t)]
    (hst : s =áµ[Î¼] t) : s.piecewise f g =áµ[Î¼] t.piecewise f g :=
  hst.mem_iff.mono fun x hx => by simp [piecewise, hx]

end Piecewise

section IndicatorFunction

variable [MeasurableSpace Î±] {Î¼ : Measure Î±} {s t : Set Î±} {f : Î± â†’ Î²}

theorem mem_map_indicator_ae_iff_mem_map_restrict_ae_of_zero_mem [Zero Î²] {t : Set Î²}
    (ht : (0 : Î²) âˆˆ t) (hs : MeasurableSet s) :
    t âˆˆ Filter.map (s.indicator f) (ae Î¼) â†” t âˆˆ Filter.map f (ae <| Î¼.restrict s) := by
  classical
  simp_rw [mem_map, mem_ae_iff]
  rw [Measure.restrict_apply' hs, Set.indicator_preimage, Set.ite]
  simp_rw [Set.compl_union, Set.compl_inter]
  change Î¼ (((f â»Â¹' t)á¶œ âˆª sá¶œ) âˆ© ((fun _ => (0 : Î²)) â»Â¹' t \ s)á¶œ) = 0 â†” Î¼ ((f â»Â¹' t)á¶œ âˆ© s) = 0
  simp only [ht, â† Set.compl_eq_univ_diff, compl_compl, if_true,
    Set.preimage_const]
  simp_rw [Set.union_inter_distrib_right, Set.compl_inter_self s, Set.union_empty]

theorem mem_map_indicator_ae_iff_of_zero_notMem [Zero Î²] {t : Set Î²} (ht : (0 : Î²) âˆ‰ t) :
    t âˆˆ Filter.map (s.indicator f) (ae Î¼) â†” Î¼ ((f â»Â¹' t)á¶œ âˆª sá¶œ) = 0 := by
  classical
  rw [mem_map, mem_ae_iff, Set.indicator_preimage, Set.ite, Set.compl_union, Set.compl_inter]
  change Î¼ (((f â»Â¹' t)á¶œ âˆª sá¶œ) âˆ© ((fun _ => (0 : Î²)) â»Â¹' t \ s)á¶œ) = 0 â†” Î¼ ((f â»Â¹' t)á¶œ âˆª sá¶œ) = 0
  simp only [ht, if_false, Set.compl_empty, Set.empty_diff, Set.inter_univ, Set.preimage_const]

@[deprecated (since := "2025-05-24")]
alias mem_map_indicator_ae_iff_of_zero_nmem := mem_map_indicator_ae_iff_of_zero_notMem

theorem map_restrict_ae_le_map_indicator_ae [Zero Î²] (hs : MeasurableSet s) :
    Filter.map f (ae <| Î¼.restrict s) â‰¤ Filter.map (s.indicator f) (ae Î¼) := by
  intro t
  by_cases ht : (0 : Î²) âˆˆ t
  Â· rw [mem_map_indicator_ae_iff_mem_map_restrict_ae_of_zero_mem ht hs]
    exact id
  rw [mem_map_indicator_ae_iff_of_zero_notMem ht, mem_map_restrict_ae_iff hs]
  exact fun h => measure_mono_null (Set.inter_subset_left.trans Set.subset_union_left) h

variable [Zero Î²]

theorem indicator_ae_eq_restrict (hs : MeasurableSet s) : indicator s f =áµ[Î¼.restrict s] f := by
  classical exact piecewise_ae_eq_restrict hs

theorem indicator_ae_eq_restrict_compl (hs : MeasurableSet s) :
    indicator s f =áµ[Î¼.restrict sá¶œ] 0 := by
  classical exact piecewise_ae_eq_restrict_compl hs

theorem indicator_ae_eq_of_restrict_compl_ae_eq_zero (hs : MeasurableSet s)
    (hf : f =áµ[Î¼.restrict sá¶œ] 0) : s.indicator f =áµ[Î¼] f := by
  rw [Filter.EventuallyEq, ae_restrict_iff' hs.compl] at hf
  filter_upwards [hf] with x hx
  by_cases hxs : x âˆˆ s
  Â· simp only [hxs, Set.indicator_of_mem]
  Â· simp only [hx hxs, Pi.zero_apply, Set.indicator_apply_eq_zero, imp_true_iff]

theorem indicator_ae_eq_zero_of_restrict_ae_eq_zero (hs : MeasurableSet s)
    (hf : f =áµ[Î¼.restrict s] 0) : s.indicator f =áµ[Î¼] 0 := by
  rw [Filter.EventuallyEq, ae_restrict_iff' hs] at hf
  filter_upwards [hf] with x hx
  by_cases hxs : x âˆˆ s
  Â· simp only [hxs, hx hxs, Set.indicator_of_mem]
  Â· simp [hxs]

theorem indicator_ae_eq_of_ae_eq_set (hst : s =áµ[Î¼] t) : s.indicator f =áµ[Î¼] t.indicator f := by
  classical exact piecewise_ae_eq_of_ae_eq_set hst

theorem indicator_meas_zero (hs : Î¼ s = 0) : indicator s f =áµ[Î¼] 0 :=
  indicator_empty' f â–¸ indicator_ae_eq_of_ae_eq_set (ae_eq_empty.2 hs)

theorem ae_eq_restrict_iff_indicator_ae_eq {g : Î± â†’ Î²} (hs : MeasurableSet s) :
    f =áµ[Î¼.restrict s] g â†” s.indicator f =áµ[Î¼] s.indicator g := by
  rw [Filter.EventuallyEq, ae_restrict_iff' hs]
  refine âŸ¨fun h => ?_, fun h => ?_âŸ© <;> filter_upwards [h] with x hx
  Â· by_cases hxs : x âˆˆ s
    Â· simp [hxs, hx hxs]
    Â· simp [hxs]
  Â· intro hxs
    simpa [hxs] using hx

end IndicatorFunction

section Sum

open Finset in
/-- An upper bound on a sum of restrictions of a measure `Î¼`. This can be used to compare
`âˆ« x âˆˆ X, f x âˆ‚Î¼` with `âˆ‘ i, âˆ« x âˆˆ (s i), f x âˆ‚Î¼`, where `s` is a cover of `X`. -/
lemma MeasureTheory.Measure.sum_restrict_le {_ : MeasurableSpace Î±}
    {Î¼ : Measure Î±} {s : Î¹ â†’ Set Î±} {M : â„•} (hs_meas : âˆ€ i, MeasurableSet (s i))
    (hs : âˆ€ y, {i | y âˆˆ s i}.encard â‰¤ M) :
    Measure.sum (fun i â†¦ Î¼.restrict (s i)) â‰¤ M â€¢ Î¼.restrict (â‹ƒ i, s i) := by
  classical
  refine le_iff.mpr (fun t ht â†¦ le_of_eq_of_le (sum_apply _ ht) ?_)
  refine ENNReal.summable.tsum_le_of_sum_le (fun F â†¦ ?_)
  -- `P` is a partition of `â‹ƒ i âˆˆ F, s i` indexed by `C âˆˆ Cs` (nonempty subsets of `F`).
  -- `P` is a partition of `s i` when restricted to `C âˆˆ G i` (subsets of `F` containing `i`).
  let P (C : Finset Î¹) := (â‹‚ i âˆˆ C, s i) âˆ© (â‹‚ i âˆˆ (F \ C), (s i)á¶œ)
  let Cs := F.powerset \ {âˆ…}
  let G (i : Î¹) := { C | C âˆˆ F.powerset âˆ§ i âˆˆ C }
  have P_meas C : MeasurableSet (P C) :=
    measurableSet_biInter C (fun i _ â†¦ hs_meas i) |>.inter <|
      measurableSet_biInter _ (fun i _ â†¦ (hs_meas i).compl)
  have P_cover {i : Î¹} (hi : i âˆˆ F) : s i âŠ† â‹ƒ C âˆˆ G i, P C := by
    refine fun x hx â†¦ Set.mem_biUnion (x := F.filter (x âˆˆ s Â·)) ?_ ?_
    Â· exact âŸ¨Finset.mem_powerset.mpr (filter_subset _ F), mem_filter.mpr âŸ¨hi, hxâŸ©âŸ©
    Â· simp_rw [P, mem_inter_iff, mem_iInter, mem_sdiff, mem_filter]; tauto
  have iUnion_P : â‹ƒ C âˆˆ Cs, P C âŠ† â‹ƒ i, s i := by
    intro x hx
    simp_rw [Cs, toFinset_diff, mem_sdiff, mem_iUnion] at hx
    have âŸ¨C, âŸ¨_, C_nonemptyâŸ©, hxCâŸ© := hx
    have âŸ¨i, hiâŸ© := Finset.nonempty_iff_ne_empty.mpr <| Finset.notMem_singleton.mp C_nonempty
    exact âŸ¨s i, âŸ¨i, rflâŸ©, hxC.1 (s i) âŸ¨i, by simp [hi]âŸ©âŸ©
  have P_subset_s {i : Î¹} {C : Finset Î¹} (hiC : i âˆˆ C) : P C âŠ† s i := by
    intro x hx
    simp only [P, mem_inter_iff, mem_iInter] at hx
    exact hx.1 i hiC
  have mem_C {i} (hi : i âˆˆ F) {C : Finset Î¹} {x : Î±} (hx : x âˆˆ P C) (hxs : x âˆˆ s i) : i âˆˆ C := by
    rw [mem_inter_iff, mem_iInterâ‚‚, mem_iInterâ‚‚] at hx
    exact of_not_not fun h â†¦ hx.2 i (mem_sdiff.mpr âŸ¨hi, hâŸ©) hxs
  have C_subset_C {Câ‚ Câ‚‚} (hCâ‚ : Câ‚ âˆˆ Cs) {x : Î±} (hx : x âˆˆ P Câ‚ âˆ© P Câ‚‚) : Câ‚ âŠ† Câ‚‚ :=
    fun i hi â†¦ mem_C (mem_powerset.mp (sdiff_subset hCâ‚) hi) hx.2 <| P_subset_s hi hx.1
  calc âˆ‘ i âˆˆ F, (Î¼.restrict (s i)) t
    _ â‰¤ âˆ‘ i âˆˆ F, Measure.sum (fun (C : G i) â†¦ Î¼.restrict (P C)) t :=
      F.sum_le_sum fun i hi â†¦ (restrict_mono_set Î¼ (P_cover hi) t).trans <|
        restrict_biUnion_le ((finite_toSet F.powerset).subset (sep_subset _ _)).countable t
    _ = âˆ‘ i âˆˆ F, âˆ‘' (C : G i), Î¼.restrict (P C) t := by simp_rw [Measure.sum_apply _ ht]
    _ = âˆ‘' C, âˆ‘ i âˆˆ F, (G i).indicator (fun C â†¦ Î¼.restrict (P C) t) C := by
      rw [Summable.tsum_finsetSum (fun _ _ â†¦ ENNReal.summable)]
      congr with i
      rw [tsum_subtype (G i) (fun C â†¦ (Î¼.restrict (P C)) t)]
    _ = âˆ‘ C âˆˆ Cs, âˆ‘ i âˆˆ F, C.toSet.indicator (fun _ â†¦ (Î¼.restrict (P C)) t) i := by
      rw [sum_eq_tsum_indicator]
      congr with C
      by_cases hC : C âˆˆ F.powerset <;> by_cases hC' : C = âˆ… <;>
        simp [hC, hC', Cs, G, indicator, -Finset.mem_powerset, -coe_powerset]
    _ = âˆ‘ C âˆˆ Cs, {a âˆˆ F | a âˆˆ C}.card â€¢ Î¼.restrict (P C) t := by simp [indicator]; rfl
    _ â‰¤ âˆ‘ C âˆˆ Cs, M â€¢ Î¼.restrict (P C) t := by
      refine sum_le_sum fun C hC â†¦ ?_
      by_cases hPC : P C = âˆ…
      Â· simp [hPC]
      have hCM : C.toSet.encard â‰¤ M :=
        have âŸ¨x, hxâŸ© := Set.nonempty_iff_ne_empty.mpr hPC
        (encard_mono (mem_iInterâ‚‚.mp hx.1)).trans (hs x)
      exact nsmul_le_nsmul_left (zero_le _) <| calc {a âˆˆ F | a âˆˆ C}.card
        _ â‰¤ C.card := card_mono <| fun i hi â†¦ (F.mem_filter.mp hi).2
        _ = C.toSet.ncard := (ncard_coe_finset C).symm
        _ â‰¤ M := ENat.toNat_le_of_le_coe hCM
    _ = M â€¢ (Î¼.restrict (â‹ƒ C âˆˆ Cs, (P C)) t) := by
      rw [â† smul_sum, â† Cs.tsum_subtype, Î¼.restrict_biUnion_finset _ P_meas, Measure.sum_apply _ ht]
      refine fun Câ‚ hCâ‚ Câ‚‚ hCâ‚‚ hC â†¦ Set.disjoint_iff.mpr fun x hx â†¦ hC <| ?_
      exact subset_antisymm (C_subset_C hCâ‚ hx) (C_subset_C hCâ‚‚ (Set.inter_comm _ _ â–¸ hx))
    _ â‰¤ (M â€¢ Î¼.restrict (â‹ƒ i, s i)) t := by
      rw [Measure.smul_apply]
      exact nsmul_le_nsmul_right (Î¼.restrict_mono_set iUnion_P t) M

end Sum
