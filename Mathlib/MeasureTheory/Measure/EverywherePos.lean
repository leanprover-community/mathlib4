/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Group.Measure
import Mathlib.Tactic.Group
import Mathlib.Topology.UrysohnsLemma

/-!
# Everywhere positive sets in measure spaces

A set `s` in a topological space with a measure `Î¼` is *everywhere positive* (also called
*self-supporting*) if any neighborhood `n` of any point of `s` satisfies `Î¼ (s âˆ© n) > 0`.

## Main definitions and results

* `Î¼.IsEverywherePos s` registers that, for any point in `s`, all its neighborhoods have positive
  measure inside `s`.
* `Î¼.everywherePosSubset s` is the subset of `s` made of those points all of whose neighborhoods
  have positive measure inside `s`.
* `everywherePosSubset_ae_eq` shows that `s` and `Î¼.everywherePosSubset s` coincide almost
  everywhere if `Î¼` is inner regular and `s` is measurable.
* `isEverywherePos_everywherePosSubset` shows that `Î¼.everywherePosSubset s` satisfies the property
  `Î¼.IsEverywherePos` if `Î¼` is inner regular and `s` is measurable.

The latter two statements have also versions when `Î¼` is inner regular for finite measure sets,
assuming additionally that `s` has finite measure.

* `IsEverywherePos.IsGÎ´` proves that an everywhere positive compact closed set is a GÎ´ set,
  in a topological group with a left-invariant measure. This is a nontrivial statement, used
  crucially in the study of the uniqueness of Haar measures.
* `innerRegularWRT_preimage_one_hasCompactSupport_measure_ne_top`: for a Haar measure, any
  finite measure set can be approximated from inside by level sets of continuous
  compactly supported functions. This property is also known as completion-regularity of Haar
  measures.
-/

open scoped Topology ENNReal NNReal
open Set Filter

namespace MeasureTheory.Measure

variable {Î± : Type*} [TopologicalSpace Î±] [MeasurableSpace Î±]

/-- A set `s` is *everywhere positive* (also called *self-supporting*) with respect to a
measure `Î¼` if it has positive measure around each of its points, i.e., if all neighborhoods `n`
of points of `s` satisfy `Î¼ (s âˆ© n) > 0`. -/
def IsEverywherePos (Î¼ : Measure Î±) (s : Set Î±) : Prop :=
  âˆ€ x âˆˆ s, âˆ€ n âˆˆ ğ“[s] x, 0 < Î¼ n

/-- The everywhere positive subset of a set is the subset made of those points all of whose
neighborhoods have positive measure inside the set. -/
def everywherePosSubset (Î¼ : Measure Î±) (s : Set Î±) : Set Î± :=
  {x | x âˆˆ s âˆ§ âˆ€ n âˆˆ ğ“[s] x, 0 < Î¼ n}

lemma everywherePosSubset_subset (Î¼ : Measure Î±) (s : Set Î±) : Î¼.everywherePosSubset s âŠ† s :=
  fun _x hx â†¦ hx.1

/-- The everywhere positive subset of a set is obtained by removing an open set. -/
lemma exists_isOpen_everywherePosSubset_eq_diff (Î¼ : Measure Î±) (s : Set Î±) :
    âˆƒ u, IsOpen u âˆ§ Î¼.everywherePosSubset s = s \ u := by
  refine âŸ¨{x | âˆƒ n âˆˆ ğ“[s] x, Î¼ n = 0}, ?_, by ext x; simp [everywherePosSubset, zero_lt_iff]âŸ©
  rw [isOpen_iff_mem_nhds]
  intro x âŸ¨n, ns, hxâŸ©
  rcases mem_nhdsWithin_iff_exists_mem_nhds_inter.1 ns with âŸ¨v, vx, hvâŸ©
  rcases mem_nhds_iff.1 vx with âŸ¨w, wv, w_open, xwâŸ©
  have A : w âŠ† {x | âˆƒ n âˆˆ ğ“[s] x, Î¼ n = 0} := by
    intro y yw
    refine âŸ¨s âˆ© w, inter_mem_nhdsWithin _ (w_open.mem_nhds yw), measure_mono_null ?_ hxâŸ©
    rw [inter_comm]
    exact (inter_subset_inter_left _ wv).trans hv
  have B : w âˆˆ ğ“ x := w_open.mem_nhds xw
  exact mem_of_superset B A

variable {Î¼ Î½ : Measure Î±} {s k : Set Î±}

protected lemma _root_.MeasurableSet.everywherePosSubset [OpensMeasurableSpace Î±]
    (hs : MeasurableSet s) :
    MeasurableSet (Î¼.everywherePosSubset s) := by
  rcases exists_isOpen_everywherePosSubset_eq_diff Î¼ s with âŸ¨u, u_open, huâŸ©
  rw [hu]
  exact hs.diff u_open.measurableSet

protected lemma _root_.IsClosed.everywherePosSubset (hs : IsClosed s) :
    IsClosed (Î¼.everywherePosSubset s) := by
  rcases exists_isOpen_everywherePosSubset_eq_diff Î¼ s with âŸ¨u, u_open, huâŸ©
  rw [hu]
  exact hs.sdiff u_open

protected lemma _root_.IsCompact.everywherePosSubset (hs : IsCompact s) :
    IsCompact (Î¼.everywherePosSubset s) := by
  rcases exists_isOpen_everywherePosSubset_eq_diff Î¼ s with âŸ¨u, u_open, huâŸ©
  rw [hu]
  exact hs.diff u_open

/-- Any compact set contained in `s \ Î¼.everywherePosSubset s` has zero measure. -/
lemma measure_eq_zero_of_subset_diff_everywherePosSubset
    (hk : IsCompact k) (h'k : k âŠ† s \ Î¼.everywherePosSubset s) : Î¼ k = 0 := by
  apply hk.induction_on (p := fun t â†¦ Î¼ t = 0)
  Â· exact measure_empty
  Â· exact fun s t hst ht â†¦ measure_mono_null hst ht
  Â· exact fun s t hs ht â†¦ measure_union_null hs ht
  Â· intro x hx
    obtain âŸ¨u, ux, huâŸ© : âˆƒ u âˆˆ ğ“[s] x, Î¼ u = 0 := by
      simpa [everywherePosSubset, (h'k hx).1] using (h'k hx).2
    exact âŸ¨u, nhdsWithin_mono x (h'k.trans diff_subset) ux, huâŸ©

/-- In a space with an inner regular measure, any measurable set coincides almost everywhere with
its everywhere positive subset. -/
lemma everywherePosSubset_ae_eq [OpensMeasurableSpace Î±] [InnerRegular Î¼] (hs : MeasurableSet s) :
    Î¼.everywherePosSubset s =áµ[Î¼] s := by
  simp only [ae_eq_set, diff_eq_empty.mpr (everywherePosSubset_subset Î¼ s), measure_empty,
    true_and, (hs.diff hs.everywherePosSubset).measure_eq_iSup_isCompact, ENNReal.iSup_eq_zero]
  intro k hk h'k
  exact measure_eq_zero_of_subset_diff_everywherePosSubset h'k hk

/-- In a space with an inner regular measure for finite measure sets, any measurable set of finite
measure coincides almost everywhere with its everywhere positive subset. -/
lemma everywherePosSubset_ae_eq_of_measure_ne_top
    [OpensMeasurableSpace Î±] [InnerRegularCompactLTTop Î¼] (hs : MeasurableSet s) (h's : Î¼ s â‰  âˆ) :
    Î¼.everywherePosSubset s =áµ[Î¼] s := by
  have A : Î¼ (s \ Î¼.everywherePosSubset s) â‰  âˆ :=
    ((measure_mono diff_subset).trans_lt h's.lt_top).ne
  simp only [ae_eq_set, diff_eq_empty.mpr (everywherePosSubset_subset Î¼ s), measure_empty,
    true_and, (hs.diff hs.everywherePosSubset).measure_eq_iSup_isCompact_of_ne_top A,
    ENNReal.iSup_eq_zero]
  intro k hk h'k
  exact measure_eq_zero_of_subset_diff_everywherePosSubset h'k hk

/-- In a space with an inner regular measure, the everywhere positive subset of a measurable set
is itself everywhere positive. This is not obvious as `Î¼.everywherePosSubset s` is defined as
the points whose neighborhoods intersect `s` along positive measure subsets, but this does not
say they also intersect `Î¼.everywherePosSubset s` along positive measure subsets. -/
lemma isEverywherePos_everywherePosSubset
    [OpensMeasurableSpace Î±] [InnerRegular Î¼] (hs : MeasurableSet s) :
    Î¼.IsEverywherePos (Î¼.everywherePosSubset s) := by
  intro x hx n hn
  rcases mem_nhdsWithin_iff_exists_mem_nhds_inter.1 hn with âŸ¨u, u_mem, huâŸ©
  have A : 0 < Î¼ (u âˆ© s) := by
    have : u âˆ© s âˆˆ ğ“[s] x := by rw [inter_comm]; exact inter_mem_nhdsWithin s u_mem
    exact hx.2 _ this
  have B : (u âˆ© Î¼.everywherePosSubset s : Set Î±) =áµ[Î¼] (u âˆ© s : Set Î±) :=
    ae_eq_set_inter (ae_eq_refl _) (everywherePosSubset_ae_eq hs)
  rw [â† B.measure_eq] at A
  exact A.trans_le (measure_mono hu)

/-- In a space with an inner regular measure for finite measure sets, the everywhere positive subset
of a measurable set of finite measure is itself everywhere positive. This is not obvious as
`Î¼.everywherePosSubset s` is defined as the points whose neighborhoods intersect `s` along positive
measure subsets, but this does not say they also intersect `Î¼.everywherePosSubset s` along positive
measure subsets. -/
lemma isEverywherePos_everywherePosSubset_of_measure_ne_top
    [OpensMeasurableSpace Î±] [InnerRegularCompactLTTop Î¼] (hs : MeasurableSet s) (h's : Î¼ s â‰  âˆ) :
    Î¼.IsEverywherePos (Î¼.everywherePosSubset s) := by
  intro x hx n hn
  rcases mem_nhdsWithin_iff_exists_mem_nhds_inter.1 hn with âŸ¨u, u_mem, huâŸ©
  have A : 0 < Î¼ (u âˆ© s) := by
    have : u âˆ© s âˆˆ ğ“[s] x := by rw [inter_comm]; exact inter_mem_nhdsWithin s u_mem
    exact hx.2 _ this
  have B : (u âˆ© Î¼.everywherePosSubset s : Set Î±) =áµ[Î¼] (u âˆ© s : Set Î±) :=
    ae_eq_set_inter (ae_eq_refl _) (everywherePosSubset_ae_eq_of_measure_ne_top hs h's)
  rw [â† B.measure_eq] at A
  exact A.trans_le (measure_mono hu)

lemma IsEverywherePos.smul_measure (hs : IsEverywherePos Î¼ s) {c : â„â‰¥0âˆ} (hc : c â‰  0) :
    IsEverywherePos (c â€¢ Î¼) s :=
  fun x hx n hn â†¦ by simpa [hc.bot_lt, hs x hx n hn] using hc.bot_lt

lemma IsEverywherePos.smul_measure_nnreal (hs : IsEverywherePos Î¼ s) {c : â„â‰¥0} (hc : c â‰  0) :
    IsEverywherePos (c â€¢ Î¼) s :=
  hs.smul_measure (by simpa using hc)

/-- If two measures coincide locally, then a set which is everywhere positive for the former is
also everywhere positive for the latter. -/
lemma IsEverywherePos.of_forall_exists_nhds_eq (hs : IsEverywherePos Î¼ s)
    (h : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“ x, âˆ€ u âŠ† t, Î½ u = Î¼ u) : IsEverywherePos Î½ s := by
  intro x hx n hn
  rcases h x hx with âŸ¨t, t_mem, htâŸ©
  refine lt_of_lt_of_le ?_ (measure_mono (inter_subset_left (t := t)))
  rw [ht (n âˆ© t) inter_subset_right]
  exact hs x hx _ (inter_mem hn (mem_nhdsWithin_of_mem_nhds t_mem))

/-- If two measures coincide locally, then a set is everywhere positive for the former iff it is
everywhere positive for the latter. -/
lemma isEverywherePos_iff_of_forall_exists_nhds_eq (h : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“ x, âˆ€ u âŠ† t, Î½ u = Î¼ u) :
    IsEverywherePos Î½ s â†” IsEverywherePos Î¼ s := by
  refine âŸ¨fun H â†¦ H.of_forall_exists_nhds_eq ?_, fun H â†¦ H.of_forall_exists_nhds_eq hâŸ©
  intro x hx
  rcases h x hx with âŸ¨t, ht, h'tâŸ©
  exact âŸ¨t, ht, fun u hu â†¦ (h't u hu).symmâŸ©

/-- An open set is everywhere positive for a measure which is positive on open sets. -/
lemma _root_.IsOpen.isEverywherePos [IsOpenPosMeasure Î¼] (hs : IsOpen s) : IsEverywherePos Î¼ s := by
  intro x xs n hn
  rcases mem_nhdsWithin.1 hn with âŸ¨u, u_open, xu, huâŸ©
  apply lt_of_lt_of_le _ (measure_mono hu)
  exact (u_open.inter hs).measure_pos Î¼ âŸ¨x, âŸ¨xu, xsâŸ©âŸ©

section IsTopologicalGroup

variable {G : Type*} [Group G] [TopologicalSpace G] [IsTopologicalGroup G]
  [LocallyCompactSpace G] [MeasurableSpace G] [BorelSpace G] {Î¼ : Measure G}
  [IsMulLeftInvariant Î¼] [IsFiniteMeasureOnCompacts Î¼] [InnerRegularCompactLTTop Î¼]

open Pointwise

/-- If a compact closed set is everywhere positive with respect to a left-invariant measure on a
topological group, then it is a GÎ´ set. This is nontrivial, as there is no second-countability or
metrizability assumption in the statement, so a general compact closed set has no reason to be
a countable intersection of open sets. -/
@[to_additive]
lemma IsEverywherePos.IsGdelta_of_isMulLeftInvariant
    {k : Set G} (h : Î¼.IsEverywherePos k) (hk : IsCompact k) (h'k : IsClosed k) :
    IsGÎ´ k := by
  /- Consider a decreasing sequence of open neighborhoods `Vâ‚™` of the identity, such that `g k \ k`
  has small measure for all `g âˆˆ Vâ‚™`. We claim that `k = â‹‚ Vâ‚™ k`, which proves
  the lemma as the sets on the right are open. The inclusion `âŠ†` is trivial.
  Let us show the converse. Take `x` in the intersection. For each `n`, write `x = vâ‚™ yâ‚™` with
  `vâ‚™ âˆˆ Vâ‚™` and `yâ‚™ âˆˆ k`. Let `z âˆˆ k` be a cluster value of `yâ‚™`, by compactness. As multiplication
  by `vâ‚™ = x yâ‚™â»Â¹ âˆˆ Vâ‚™` changes the measure of `k` by very little, passing to the limit we get
  `Î¼ (x zâ»Â¹ k \ k) = 0`. By invariance of the measure under `z x â»Â¹`, we get `Î¼ (k \ z xâ»Â¹ k) = 0`.
  Assume `x âˆ‰ k`. Then `z âˆˆ k \ z xâ»Â¹ k`. Even more, this set is a neighborhood of `z` within `k`
  (as `z xâ»Â¹ k` is closed), and it has zero measure. This contradicts the fact that `k` has
  positive measure around the point `z`. -/
  obtain âŸ¨u, -, u_mem, u_limâŸ© : âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), u n âˆˆ Ioo 0 1)
    âˆ§ Tendsto u atTop (ğ“ 0) := exists_seq_strictAnti_tendsto' (zero_lt_one : (0 : â„â‰¥0âˆ) < 1)
  have : âˆ€ n, âˆƒ (W : Set G), IsOpen W âˆ§ 1 âˆˆ W âˆ§ âˆ€ g âˆˆ W * W, Î¼ ((g â€¢ k) \ k) < u n :=
    fun n â†¦ exists_open_nhds_one_mul_subset
      (eventually_nhds_one_measure_smul_diff_lt hk h'k (u_mem n).1.ne')
  choose W W_open mem_W hW using this
  let V n := â‹‚ i âˆˆ Finset.range n, W i
  suffices â‹‚ n, V n * k âŠ† k by
    replace : k = â‹‚ n, V n * k := by
      apply Subset.antisymm (subset_iInter_iff.2 (fun n â†¦ ?_)) this
      exact subset_mul_right k (by simp [V, mem_W])
    rw [this]
    refine .iInter_of_isOpen fun n â†¦ ?_
    exact .mul_right (isOpen_biInter_finset (fun i _hi â†¦ W_open i))
  intro x hx
  choose v hv y hy hvy using mem_iInter.1 hx
  obtain âŸ¨z, zk, hzâŸ© : âˆƒ z âˆˆ k, MapClusterPt z atTop y := hk.exists_mapClusterPt (by simp [hy])
  have A n : Î¼ (((x * z â»Â¹) â€¢ k) \ k) â‰¤ u n := by
    apply le_of_lt (hW _ _ ?_)
    have : W n * {z} âˆˆ ğ“ z := (IsOpen.mul_right (W_open n)).mem_nhds (by simp [mem_W])
    obtain âŸ¨i, hi, niâŸ© : âˆƒ i, y i âˆˆ W n * {z} âˆ§ n < i :=
      ((hz.frequently this).and_eventually (eventually_gt_atTop n)).exists
    refine âŸ¨x * (y i) â»Â¹, ?_, y i * zâ»Â¹, by simpa using hi, by groupâŸ©
    have I : V i âŠ† W n := iInterâ‚‚_subset n (by simp [ni])
    have J : x * (y i) â»Â¹ âˆˆ V i := by simpa [â† hvy i] using hv i
    exact I J
  have B : Î¼ (((x * z â»Â¹) â€¢ k) \ k) = 0 :=
    le_antisymm (ge_of_tendsto u_lim (Eventually.of_forall A)) bot_le
  have C : Î¼ (k \ (z * xâ»Â¹) â€¢ k) = 0 := by
    have : Î¼ ((z * xâ»Â¹) â€¢ (((x * z â»Â¹) â€¢ k) \ k)) = 0 := by rwa [measure_smul]
    rw [â† this, smul_set_sdiff, smul_smul]
    group
    simp
  by_contra H
  have : k âˆ© ((z * xâ»Â¹) â€¢ k)á¶œ âˆˆ ğ“[k] z := by
    apply inter_mem_nhdsWithin k
    apply IsOpen.mem_nhds (by simpa using h'k.smul _)
    simp only [mem_compl_iff]
    contrapose! H
    simpa [mem_smul_set_iff_inv_smul_mem] using H
  have : 0 < Î¼ (k \ ((z * xâ»Â¹) â€¢ k)) := h z zk _ this
  exact lt_irrefl _ (C.le.trans_lt this)

/-- **Halmos' theorem: Haar measure is completion regular.** More precisely, any finite measure
set can be approximated from inside by a level set of a continuous function with compact support. -/
@[to_additive innerRegularWRT_preimage_one_hasCompactSupport_measure_ne_top_of_addGroup]
theorem innerRegularWRT_preimage_one_hasCompactSupport_measure_ne_top_of_group :
    InnerRegularWRT Î¼ (fun s â†¦ âˆƒ (f : G â†’ â„), Continuous f âˆ§ HasCompactSupport f âˆ§ s = f â»Â¹' {1})
    (fun s â†¦ MeasurableSet s âˆ§ Î¼ s â‰  âˆ) := by
  /- First, approximate a measurable set from inside by a compact closed set `K`. Then notice that
  the everywhere positive subset of `K` is a GÎ´,
  by Lemma `IsEverywherePos.IsGdelta_of_isMulLeftInvariant`, and therefore the level set of a
  continuous compactly supported function. Moreover, it has the same measure as `K`. -/
  apply InnerRegularWRT.trans _ innerRegularWRT_isCompact_isClosed_measure_ne_top_of_group
  intro K âŸ¨K_comp, K_closedâŸ© r hr
  let L := Î¼.everywherePosSubset K
  have L_comp : IsCompact L := K_comp.everywherePosSubset
  have L_closed : IsClosed L := K_closed.everywherePosSubset
  refine âŸ¨L, everywherePosSubset_subset Î¼ K, ?_, ?_âŸ©
  Â· have : Î¼.IsEverywherePos L :=
      isEverywherePos_everywherePosSubset_of_measure_ne_top K_closed.measurableSet
      K_comp.measure_lt_top.ne
    have L_GÎ´ : IsGÎ´ L := this.IsGdelta_of_isMulLeftInvariant L_comp L_closed
    obtain âŸ¨âŸ¨f, f_contâŸ©, Lf, -, f_comp, -âŸ© : âˆƒ f : C(G, â„), L = f â»Â¹' {1} âˆ§ EqOn f 0 âˆ…
        âˆ§ HasCompactSupport f âˆ§ âˆ€ x, f x âˆˆ Icc (0 : â„) 1 :=
      exists_continuous_one_zero_of_isCompact_of_isGÎ´ L_comp L_GÎ´ isClosed_empty
        (disjoint_empty L)
    exact âŸ¨f, f_cont, f_comp, LfâŸ©
  Â· convert hr using 1
    apply measure_congr
    exact everywherePosSubset_ae_eq_of_measure_ne_top K_closed.measurableSet
      K_comp.measure_lt_top.ne

end IsTopologicalGroup

end Measure

end MeasureTheory
