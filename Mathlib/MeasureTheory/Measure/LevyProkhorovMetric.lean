/-
Copyright (c) 2023 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import Mathlib.MeasureTheory.Integral.Layercake
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction

/-!
# The L√©vy-Prokhorov distance on spaces of finite measures and probability measures

## Main definitions

* `MeasureTheory.levyProkhorovEDist`: The L√©vy-Prokhorov edistance between two measures.
* `MeasureTheory.levyProkhorovDist`: The L√©vy-Prokhorov distance between two finite measures.

## Main results

* `levyProkhorovDist_pseudoMetricSpace_finiteMeasure`: The L√©vy-Prokhorov distance is a
  pseudoemetric on the space of finite measures.
* `levyProkhorovDist_pseudoMetricSpace_probabilityMeasure`: The L√©vy-Prokhorov distance is a
  pseudoemetric on the space of probability measures.

## Todo

* Show that in Borel spaces, the L√©vy-Prokhorov distance is a metric; not just a pseudometric.
* Prove that if `X` is metrizable and separable, then the L√©vy-Prokhorov distance metrizes the
  topology of weak convergence.

## Tags

finite measure, probability measure, weak convergence, convergence in distribution, metrizability
-/

open Topology Metric Filter Set ENNReal NNReal

namespace MeasureTheory

open scoped Topology ENNReal NNReal BoundedContinuousFunction

section Levy_Prokhorov

/-! ### L√©vy-Prokhorov metric -/

variable {Œπ : Type*} {Œ© : Type*} [MeasurableSpace Œ©] [MetricSpace Œ©]

/-- The L√©vy-Prokhorov edistance between measures:
`d(Œº,ŒΩ) = inf {r ‚â• 0 | ‚àÄ B, Œº B ‚â§ ŒΩ B·µ£ + r ‚àß ŒΩ B ‚â§ Œº B·µ£ + r}`. -/
noncomputable def levyProkhorovEDist (Œº ŒΩ : Measure Œ©) : ‚Ñù‚â•0‚àû :=
  sInf {Œµ | ‚àÄ B, MeasurableSet B ‚Üí
            Œº B ‚â§ ŒΩ (thickening Œµ.toReal B) + Œµ ‚àß ŒΩ B ‚â§ Œº (thickening Œµ.toReal B) + Œµ}

/- This result is not placed in earlier more generic files, since it is rather specialized;
it mixes measure and metric in a very particular way. -/
lemma meas_le_of_le_of_forall_le_meas_thickening_add {Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù‚â•0‚àû} (Œº ŒΩ : Measure Œ©)
    (h_le : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) {B : Set Œ©} (hŒµ‚ÇÅ : Œº B ‚â§ ŒΩ (thickening Œµ‚ÇÅ.toReal B) + Œµ‚ÇÅ):
    Œº B ‚â§ ŒΩ (thickening Œµ‚ÇÇ.toReal B) + Œµ‚ÇÇ := by
  by_cases Œµ_top : Œµ‚ÇÇ = ‚àû
  ¬∑ simp only [ne_eq, FiniteMeasure.ennreal_coeFn_eq_coeFn_toMeasure, Œµ_top, top_toReal,
                add_top, le_top]
  apply hŒµ‚ÇÅ.trans (add_le_add ?_ h_le)
  exact measure_mono (Œº := ŒΩ) (thickening_mono (toReal_mono Œµ_top h_le) B)

lemma left_measure_le_of_levyProkhorovEDist_lt {Œº ŒΩ : Measure Œ©} {c : ‚Ñù‚â•0‚àû}
    (h : levyProkhorovEDist Œº ŒΩ < c) {B : Set Œ©} (B_mble : MeasurableSet B) :
    Œº B ‚â§ ŒΩ (thickening c.toReal B) + c := by
  obtain ‚ü®c', ‚ü®hc', lt_c‚ü©‚ü© := sInf_lt_iff.mp h
  exact meas_le_of_le_of_forall_le_meas_thickening_add Œº ŒΩ lt_c.le (hc' B B_mble).1

lemma right_measure_le_of_levyProkhorovEDist_lt {Œº ŒΩ : Measure Œ©} {c : ‚Ñù‚â•0‚àû}
    (h : levyProkhorovEDist Œº ŒΩ < c) {B : Set Œ©} (B_mble : MeasurableSet B) :
    ŒΩ B ‚â§ Œº (thickening c.toReal B) + c := by
  obtain ‚ü®c', ‚ü®hc', lt_c‚ü©‚ü© := sInf_lt_iff.mp h
  exact meas_le_of_le_of_forall_le_meas_thickening_add ŒΩ Œº lt_c.le (hc' B B_mble).2

lemma levyProkhorovEDist_le_of_forall_add_pos_le (Œº ŒΩ : Measure Œ©) (Œ¥ : ‚Ñù‚â•0‚àû)
    (h : ‚àÄ Œµ B, 0 < Œµ ‚Üí Œµ < ‚àû ‚Üí MeasurableSet B ‚Üí
      Œº B ‚â§ ŒΩ (thickening (Œ¥ + Œµ).toReal B) + Œ¥ + Œµ ‚àß
      ŒΩ B ‚â§ Œº (thickening (Œ¥ + Œµ).toReal B) + Œ¥ + Œµ) :
    levyProkhorovEDist Œº ŒΩ ‚â§ Œ¥ := by
  apply ENNReal.le_of_forall_pos_le_add
  intro Œµ hŒµ _
  by_cases Œµ_top : Œµ = ‚àû
  ¬∑ simp only [Œµ_top, add_top, le_top]
  apply sInf_le
  intro B B_mble
  simpa only [add_assoc] using h Œµ B (coe_pos.mpr hŒµ) coe_lt_top B_mble

lemma levyProkhorovEDist_le_of_forall (Œº ŒΩ : Measure Œ©) (Œ¥ : ‚Ñù‚â•0‚àû)
    (h : ‚àÄ Œµ B, Œ¥ < Œµ ‚Üí Œµ < ‚àû ‚Üí MeasurableSet B ‚Üí
        Œº B ‚â§ ŒΩ (thickening Œµ.toReal B) + Œµ ‚àß ŒΩ B ‚â§ Œº (thickening Œµ.toReal B) + Œµ) :
    levyProkhorovEDist Œº ŒΩ ‚â§ Œ¥ := by
  by_cases Œ¥_top : Œ¥ = ‚àû
  ¬∑ simp only [Œ¥_top, add_top, le_top]
  apply levyProkhorovEDist_le_of_forall_add_pos_le
  intro x B x_pos x_lt_top B_mble
  simpa only [‚Üê add_assoc] using h (Œ¥ + x) B (ENNReal.lt_add_right Œ¥_top x_pos.ne.symm)
    (by simp only [add_lt_top, Ne.lt_top Œ¥_top, x_lt_top, and_self]) B_mble

lemma levyProkhorovEDist_le_max_measure_univ (Œº ŒΩ : Measure Œ©) :
    levyProkhorovEDist Œº ŒΩ ‚â§ max (Œº univ) (ŒΩ univ) := by
  refine sInf_le fun B _ ‚Ü¶ ‚ü®?_, ?_‚ü© <;> apply le_add_left <;> simp [measure_mono]

lemma levyProkhorovEDist_lt_top (Œº ŒΩ : Measure Œ©) [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] :
    levyProkhorovEDist Œº ŒΩ < ‚àû :=
  (levyProkhorovEDist_le_max_measure_univ Œº ŒΩ).trans_lt <| by simp [measure_lt_top]

-- ...otherwise `exact?` does not find the useful lemma.
lemma levyProkhorovEDist_ne_top (Œº ŒΩ : Measure Œ©) [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] :
    levyProkhorovEDist Œº ŒΩ ‚â† ‚àû := (levyProkhorovEDist_lt_top Œº ŒΩ).ne

lemma levyProkhorovEDist_self (Œº : Measure Œ©) :
    levyProkhorovEDist Œº Œº = 0 := by
  rw [‚Üê nonpos_iff_eq_zero, ‚Üê csInf_Ioo zero_lt_top]
  refine sInf_le_sInf fun Œµ ‚ü®hŒµ‚ÇÄ, hŒµ_top‚ü© B _ ‚Ü¶ and_self_iff.2 ?_
  refine le_add_right <| measure_mono <| self_subset_thickening ?_ _
  exact ENNReal.toReal_pos hŒµ‚ÇÄ.ne' hŒµ_top.ne

lemma levyProkhorovEDist_comm (Œº ŒΩ : Measure Œ©) :
    levyProkhorovEDist Œº ŒΩ = levyProkhorovEDist ŒΩ Œº := by
  simp only [levyProkhorovEDist, and_comm]

lemma levyProkhorovEDist_triangle [OpensMeasurableSpace Œ©] (Œº ŒΩ Œ∫ : Measure Œ©) :
    levyProkhorovEDist Œº Œ∫ ‚â§ levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫ := by
  by_cases LPŒºŒΩ_finite : levyProkhorovEDist Œº ŒΩ = ‚àû
  ¬∑ simp [LPŒºŒΩ_finite]
  by_cases LPŒΩŒ∫_finite : levyProkhorovEDist ŒΩ Œ∫ = ‚àû
  ¬∑ simp [LPŒΩŒ∫_finite]
  apply levyProkhorovEDist_le_of_forall_add_pos_le
  intro Œµ B Œµ_pos Œµ_lt_top B_mble
  have half_Œµ_pos : 0 < Œµ / 2 := ENNReal.div_pos Œµ_pos.ne' two_ne_top
  have half_Œµ_lt_top : Œµ / 2 < ‚àû := ENNReal.div_lt_top Œµ_lt_top.ne two_ne_zero
  let r := levyProkhorovEDist Œº ŒΩ + Œµ / 2
  let s := levyProkhorovEDist ŒΩ Œ∫ + Œµ / 2
  have lt_r : levyProkhorovEDist Œº ŒΩ < r := lt_add_right LPŒºŒΩ_finite half_Œµ_pos.ne'
  have lt_s : levyProkhorovEDist ŒΩ Œ∫ < s := lt_add_right LPŒΩŒ∫_finite half_Œµ_pos.ne'
  have hs_add_r : s + r = levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫ + Œµ := by
    simp_rw [add_assoc, add_comm (Œµ / 2), add_assoc, ENNReal.add_halves, ‚Üê add_assoc,
      add_comm (levyProkhorovEDist Œº ŒΩ)]
  have hs_add_r' : s.toReal + r.toReal
      = (levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫ + Œµ).toReal := by
    rw [‚Üê hs_add_r, ‚Üê ENNReal.toReal_add]
    ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®LPŒΩŒ∫_finite, half_Œµ_lt_top.ne‚ü©
    ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®LPŒºŒΩ_finite, half_Œµ_lt_top.ne‚ü©
  rw [‚Üê hs_add_r', add_assoc, ‚Üê hs_add_r, add_assoc _ _ Œµ, ‚Üê hs_add_r]
  refine ‚ü®?_, ?_‚ü©
  ¬∑ calc Œº B ‚â§ ŒΩ (thickening r.toReal B) + r :=
      left_measure_le_of_levyProkhorovEDist_lt lt_r B_mble
    _ ‚â§ Œ∫ (thickening s.toReal (thickening r.toReal B)) + s + r :=
      add_le_add_right
        (left_measure_le_of_levyProkhorovEDist_lt lt_s isOpen_thickening.measurableSet) _
    _ = Œ∫ (thickening s.toReal (thickening r.toReal B)) + (s + r) := add_assoc _ _ _
    _ ‚â§ Œ∫ (thickening (s.toReal + r.toReal) B) + (s + r) :=
      add_le_add_right (measure_mono (thickening_thickening_subset _ _ _)) _
  ¬∑ calc Œ∫ B ‚â§ ŒΩ (thickening s.toReal B) + s :=
      right_measure_le_of_levyProkhorovEDist_lt lt_s B_mble
    _ ‚â§ Œº (thickening r.toReal (thickening s.toReal B)) + r + s :=
      add_le_add_right
        (right_measure_le_of_levyProkhorovEDist_lt lt_r isOpen_thickening.measurableSet) s
    _ = Œº (thickening r.toReal (thickening s.toReal B)) + (s + r) := by rw [add_assoc, add_comm r]
    _ ‚â§ Œº (thickening (r.toReal + s.toReal) B) + (s + r) :=
      add_le_add_right (measure_mono (thickening_thickening_subset _ _ _)) _
    _ = Œº (thickening (s.toReal + r.toReal) B) + (s + r) := by rw [add_comm r.toReal]

/-- The L√©vy-Prokhorov distance between finite measures:
`d(Œº,ŒΩ) = inf {r ‚â• 0 | ‚àÄ B, Œº B ‚â§ ŒΩ B·µ£ + r ‚àß ŒΩ B ‚â§ Œº B·µ£ + r}`. -/
noncomputable def levyProkhorovDist (Œº ŒΩ : Measure Œ©) : ‚Ñù :=
  (levyProkhorovEDist Œº ŒΩ).toReal

lemma levyProkhorovDist_self (Œº : Measure Œ©) :
    levyProkhorovDist Œº Œº = 0 := by
  simp only [levyProkhorovDist, levyProkhorovEDist_self, zero_toReal]

lemma levyProkhorovDist_comm (Œº ŒΩ : Measure Œ©) :
    levyProkhorovDist Œº ŒΩ = levyProkhorovDist ŒΩ Œº := by
  simp only [levyProkhorovDist, levyProkhorovEDist_comm]

lemma levyProkhorovDist_triangle [OpensMeasurableSpace Œ©] (Œº ŒΩ Œ∫ : Measure Œ©)
    [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] [IsFiniteMeasure Œ∫] :
    levyProkhorovDist Œº Œ∫ ‚â§ levyProkhorovDist Œº ŒΩ + levyProkhorovDist ŒΩ Œ∫ := by
  have dŒºŒ∫_finite := (levyProkhorovEDist_lt_top Œº Œ∫).ne
  have dŒºŒΩ_finite := (levyProkhorovEDist_lt_top Œº ŒΩ).ne
  have dŒΩŒ∫_finite := (levyProkhorovEDist_lt_top ŒΩ Œ∫).ne
  convert (ENNReal.toReal_le_toReal (a := levyProkhorovEDist Œº Œ∫)
    (b := levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫)
    _ _).mpr <| levyProkhorovEDist_triangle Œº ŒΩ Œ∫
  ¬∑ simp only [levyProkhorovDist, ENNReal.toReal_add dŒºŒΩ_finite dŒΩŒ∫_finite]
  ¬∑ exact dŒºŒ∫_finite
  ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®dŒºŒΩ_finite, dŒΩŒ∫_finite‚ü©

/-- A type synonym, to be used for `Measure Œ±`, `FiniteMeasure Œ±`, or `ProbabilityMeasure Œ±`,
when they are to be equipped with the L√©vy-Prokhorov distance. -/
def LevyProkhorov (Œ± : Type*) := Œ±

variable [OpensMeasurableSpace Œ©]

/-- The L√©vy-Prokhorov distance `levyProkhorovEDist` makes `Measure Œ©` a pseudoemetric
space. The instance is recorded on the type synonym `LevyProkhorov (Measure Œ©) := Measure Œ©`. -/
noncomputable instance : PseudoEMetricSpace (LevyProkhorov (Measure Œ©)) where
  edist := levyProkhorovEDist
  edist_self := levyProkhorovEDist_self
  edist_comm := levyProkhorovEDist_comm
  edist_triangle := levyProkhorovEDist_triangle

/-- The L√©vy-Prokhorov distance `levyProkhorovDist` makes `FiniteMeasure Œ©` a pseudometric
space. The instance is recorded on the type synonym
`LevyProkhorov (FiniteMeasure Œ©) := FiniteMeasure Œ©`. -/
noncomputable instance levyProkhorovDist_pseudoMetricSpace_finiteMeasure :
    PseudoMetricSpace (LevyProkhorov (FiniteMeasure Œ©)) where
  dist Œº ŒΩ := levyProkhorovDist Œº.toMeasure ŒΩ.toMeasure
  dist_self Œº := levyProkhorovDist_self _
  dist_comm Œº ŒΩ := levyProkhorovDist_comm _ _
  dist_triangle Œº ŒΩ Œ∫ := levyProkhorovDist_triangle _ _ _
  edist_dist Œº ŒΩ := by simp [‚Üê ENNReal.ofReal_coe_nnreal]

/-- The L√©vy-Prokhorov distance `levyProkhorovDist` makes `ProbabilityMeasure Œ©` a pseudoemetric
space. The instance is recorded on the type synonym
`LevyProkhorov (ProbabilityMeasure Œ©) := ProbabilityMeasure Œ©`. -/
noncomputable instance levyProkhorovDist_pseudoMetricSpace_probabilityMeasure :
    PseudoMetricSpace (LevyProkhorov (ProbabilityMeasure Œ©)) where
  dist Œº ŒΩ := levyProkhorovDist Œº.toMeasure ŒΩ.toMeasure
  dist_self Œº := levyProkhorovDist_self _
  dist_comm Œº ŒΩ := levyProkhorovDist_comm _ _
  dist_triangle Œº ŒΩ Œ∫ := levyProkhorovDist_triangle _ _ _
  edist_dist Œº ŒΩ := by simp [‚Üê ENNReal.ofReal_coe_nnreal]

lemma levyProkhorov_dist_def (Œº ŒΩ : LevyProkhorov (ProbabilityMeasure Œ©)) :
    dist Œº ŒΩ = levyProkhorovDist Œº.toMeasure ŒΩ.toMeasure := rfl

end Levy_Prokhorov --section

section Levy_Prokhorov_comparison

open BoundedContinuousFunction

variable {Œπ : Type*} (Œ© : Type*) [MeasurableSpace Œ©]

def levyProkhorov_to_probabilityMeasure (Œº : LevyProkhorov (ProbabilityMeasure Œ©)) :
    ProbabilityMeasure Œ© := Œº

def levyProkhorov_to_finiteMeasure (Œº : LevyProkhorov (FiniteMeasure Œ©)) :
    FiniteMeasure Œ© := Œº

variable [MetricSpace Œ©] [OpensMeasurableSpace Œ©]

example : TopologicalSpace (ProbabilityMeasure Œ©) := by exact
  ProbabilityMeasure.instTopologicalSpaceProbabilityMeasure

variable {Œ©}

/-
#check lintegral_eq_lintegral_meas_le
#check set_integral_eq_of_subset_of_ae_diff_eq_zero

-- TODO: Add in `MeasureTheory.Integral.SetIntegral`?
/-- If a function vanishes almost everywhere on `t \ s` with `s ‚äÜ t`, then its (Lebesgue) integrals
on `s` and `t` coincide if `t` is null-measurable. -/
theorem set_lintegral_eq_of_subset_of_ae_diff_eq_zero {Œ± : Type*} [MeasurableSpace Œ±] (f : Œ± ‚Üí ‚Ñù‚â•0‚àû)
    (Œº : Measure Œ±) (s t : Set Œ±) (ht : NullMeasurableSet t Œº) (hts : s ‚äÜ t)
    (h't : ‚àÄ·µê x ‚àÇŒº, x ‚àà t \ s ‚Üí f x = 0) :
    ‚à´‚Åª x in t, f x ‚àÇŒº = ‚à´‚Åª x in s, f x ‚àÇŒº := by
  sorry

-- TODO: Add to layercake
lemma lintegral_eq_lintegral_Icc_meas_le {Œ± : Type*} [MeasurableSpace Œ±] {f : Œ± ‚Üí ‚Ñù}
    (Œº : Measure Œ±) (f_nn : 0 ‚â§·µê[Œº] f)
    {M : ‚Ñù} (f_bdd : f ‚â§·µê[Œº] (fun _ ‚Ü¶ M)) (f_mble : AEMeasurable f Œº) :
    (‚à´‚Åª œâ, ENNReal.ofReal (f œâ) ‚àÇŒº) = ‚à´‚Åª t in Ioc 0 M, Œº {a : Œ± | t ‚â§ f a} := by
  rw [lintegral_eq_lintegral_meas_le Œº f_nn f_mble]
  rw [set_lintegral_eq_of_subset_of_ae_diff_eq_zero _ _ _ _
      measurableSet_Ioi.nullMeasurableSet Ioc_subset_Ioi_self ?_]
  apply eventually_of_forall
  intro t ht
  have htM : M < t := by simp_all only [mem_diff, mem_Ioi, mem_Ioc, not_and, not_le]
  have obs : Œº {a | M < f a} = 0 := by
    rw [measure_zero_iff_ae_nmem]
    filter_upwards [f_bdd] with a ha
    exact not_lt.mpr ha
  exact measure_mono_null (fun a ha ‚Ü¶ lt_of_lt_of_le htM ha) obs

lemma BoundedContinuousFunction.lintegral_eq_lintegral_meas_le {Œ± : Type*} [MeasurableSpace Œ±]
    [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    (f : Œ± ‚Üí·µá ‚Ñù) (Œº : Measure Œ±) (f_nn : 0 ‚â§·µê[Œº] f) :
    (‚à´‚Åª œâ, ENNReal.ofReal (f œâ) ‚àÇŒº) = ‚à´‚Åª t in Ioc 0 ‚Äñf‚Äñ, Œº {a : Œ± | t ‚â§ f a} := by
  rw [@lintegral_eq_lintegral_Icc_meas_le _ _ _ Œº f_nn ‚Äñf‚Äñ ?_ f.continuous.measurable.aemeasurable]
  exact eventually_of_forall (fun x ‚Ü¶ BoundedContinuousFunction.apply_le_norm f x)
 -/

lemma integral_eq_integral_Icc_meas_le {Œ± : Type*} [MeasurableSpace Œ±] {f : Œ± ‚Üí ‚Ñù}
    (Œº : Measure Œ±) (f_nn : 0 ‚â§·µê[Œº] f)
    {M : ‚Ñù} (f_bdd : f ‚â§·µê[Œº] (fun _ ‚Ü¶ M)) (f_intble : Integrable f Œº) :
    (‚à´ œâ, f œâ ‚àÇŒº) = ‚à´ t in Ioc 0 M, ENNReal.toReal (Œº {a : Œ± | t ‚â§ f a}) := by
  rw [f_intble.integral_eq_integral_meas_le f_nn]
  rw [set_integral_eq_of_subset_of_ae_diff_eq_zero
      measurableSet_Ioi.nullMeasurableSet Ioc_subset_Ioi_self ?_]
  apply eventually_of_forall
  intro t ht
  have htM : M < t := by simp_all only [mem_diff, mem_Ioi, mem_Ioc, not_and, not_le]
  have obs : Œº {a | M < f a} = 0 := by
    rw [measure_zero_iff_ae_nmem]
    filter_upwards [f_bdd] with a ha
    exact not_lt.mpr ha
  rw [toReal_eq_zero_iff]
  exact Or.inl <| measure_mono_null (fun a ha ‚Ü¶ lt_of_lt_of_le htM ha) obs

lemma BoundedContinuousFunction.integral_eq_integral_meas_le {Œ± : Type*} [MeasurableSpace Œ±]
    [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    (f : Œ± ‚Üí·µá ‚Ñù) (Œº : Measure Œ±) (f_nn : 0 ‚â§·µê[Œº] f) (hf : HasFiniteIntegral f Œº) :
    (‚à´ œâ, f œâ ‚àÇŒº) = ‚à´ t in Ioc 0 ‚Äñf‚Äñ, ENNReal.toReal (Œº {a : Œ± | t ‚â§ f a}) := by
  rw [integral_eq_integral_Icc_meas_le Œº f_nn (M := ‚Äñf‚Äñ) ?_ ?_]
  ¬∑ exact eventually_of_forall (fun x ‚Ü¶ BoundedContinuousFunction.apply_le_norm f x)
  ¬∑ refine ‚ü®f.continuous.measurable.aestronglyMeasurable, hf‚ü©

lemma BoundedContinuousFunction.integral_eq_integral_meas_le' {Œ± : Type*} [MeasurableSpace Œ±]
    [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    (f : Œ± ‚Üí·µá ‚Ñù) (Œº : Measure Œ±) [IsFiniteMeasure Œº] (f_nn : 0 ‚â§·µê[Œº] f) :
    (‚à´ œâ, f œâ ‚àÇŒº) = ‚à´ t in Ioc 0 ‚Äñf‚Äñ, ENNReal.toReal (Œº {a : Œ± | t ‚â§ f a}) :=
  integral_eq_integral_meas_le _ _ f_nn (f.integrable Œº).2

lemma integrableOn_of_meas_lt_top_of_norm_le {Œ± : Type*} [MeasurableSpace Œ±]
    {E : Type*} [NormedAddCommGroup E] [SecondCountableTopology E]
    (Œº : Measure Œ±) {A : Set Œ±} (A_mble : MeasurableSet A) (A_finite : Œº A < ‚àû)
    {f : Œ± ‚Üí E} (f_mble : AEStronglyMeasurable f Œº)
    {M : ‚Ñù} (f_bdd : ‚àÄ a ‚àà A, ‚Äñf a‚Äñ ‚â§ M) :
    IntegrableOn f A Œº := by
  refine ‚ü®AEStronglyMeasurable.restrict f_mble, ?_‚ü©
  rw [HasFiniteIntegral]
  have obs : ‚à´‚Åª _ in A, ENNReal.ofReal M ‚àÇŒº < ‚ä§ := by
    rw [lintegral_const]
    apply mul_lt_top ofReal_ne_top
    simpa only [MeasurableSet.univ, Measure.restrict_apply, univ_inter] using A_finite.ne
  apply lt_of_le_of_lt (a := ‚à´‚Åª (a : Œ±) in A, ‚Üë‚Äñf a‚Äñ‚Çä ‚àÇŒº) ?_ obs
  apply set_lintegral_mono' A_mble
  intro a a_in_A
  convert ENNReal.ofReal_le_ofReal <| f_bdd a a_in_A
  exact (ofReal_norm_eq_coe_nnnorm (f a)).symm

-- This might be the more useful variant.
lemma BoundedContinuousFunction.integral_le_of_levyProkhorovEDist_lt (Œº ŒΩ : Measure Œ©)
    [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] {Œµ : ‚Ñù} (Œµ_pos : 0 < Œµ)
    (hŒºŒΩ : levyProkhorovEDist Œº ŒΩ < ENNReal.ofReal Œµ) (f : Œ© ‚Üí·µá ‚Ñù) (f_nn : 0 ‚â§·µê[Œº] f) :
    ‚à´ œâ, f œâ ‚àÇŒº
      ‚â§ (‚à´ t in Ioc 0 ‚Äñf‚Äñ, ENNReal.toReal (ŒΩ (thickening Œµ {a | t ‚â§ f a}))) + Œµ * ‚Äñf‚Äñ := by
  rw [BoundedContinuousFunction.integral_eq_integral_meas_le' f Œº f_nn]
  have key : (fun (t : ‚Ñù) ‚Ü¶ ENNReal.toReal (Œº {a | t ‚â§ f a}))
              ‚â§ (fun (t : ‚Ñù) ‚Ü¶ ENNReal.toReal (ŒΩ (thickening Œµ {a | t ‚â§ f a})) + Œµ) := by
    intro t
    convert (ENNReal.toReal_le_toReal (measure_ne_top _ _) ?_).mpr
            <| left_measure_le_of_levyProkhorovEDist_lt hŒºŒΩ (B := {a | t ‚â§ f a})
                (f.continuous.measurable measurableSet_Ici)
    ¬∑ rw [ENNReal.toReal_add (measure_ne_top ŒΩ _) ofReal_ne_top, ENNReal.toReal_ofReal Œµ_pos.le]
    ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®measure_ne_top ŒΩ _, ofReal_ne_top‚ü©
  have intble‚ÇÅ : IntegrableOn (fun t ‚Ü¶ ENNReal.toReal (Œº {a | t ‚â§ f a})) (Ioc 0 ‚Äñf‚Äñ) := by
    apply integrableOn_of_meas_lt_top_of_norm_le (M := ENNReal.toReal (Œº univ)) _
          measurableSet_Ioc measure_Ioc_lt_top
    ¬∑ apply Measurable.aestronglyMeasurable
      refine Measurable.ennreal_toReal ?_
      apply Antitone.measurable
      exact fun _ _ hst ‚Ü¶ measure_mono (fun _ h ‚Ü¶ hst.trans h)
    ¬∑ intro t _
      simp only [Real.norm_eq_abs, abs_toReal]
      exact (ENNReal.toReal_le_toReal (measure_ne_top _ _) (measure_ne_top _ _)).mpr
            <| measure_mono (subset_univ _)
  have intble‚ÇÇ : IntegrableOn (fun t ‚Ü¶ ENNReal.toReal (ŒΩ (thickening Œµ {a | t ‚â§ f a}))) (Ioc 0 ‚Äñf‚Äñ) := by
    apply integrableOn_of_meas_lt_top_of_norm_le (M := ENNReal.toReal (ŒΩ univ)) _
          measurableSet_Ioc measure_Ioc_lt_top
    ¬∑ apply Measurable.aestronglyMeasurable
      refine Measurable.ennreal_toReal ?_
      apply Antitone.measurable
      exact fun _ _ hst ‚Ü¶ measure_mono <| thickening_subset_of_subset Œµ (fun _ h ‚Ü¶ hst.trans h)
    ¬∑ intro t _
      simp only [Real.norm_eq_abs, abs_toReal]
      apply (ENNReal.toReal_le_toReal (measure_ne_top _ _) (measure_ne_top _ _)).mpr
            <| measure_mono (subset_univ _)
  apply le_trans (set_integral_mono (s :=Ioc 0 ‚Äñf‚Äñ) ?_ ?_ key)
  rw [integral_add]
  ¬∑ apply add_le_add rfl.le
    simp only [integral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter,
                Real.volume_Ioc, sub_zero, norm_nonneg, toReal_ofReal, smul_eq_mul]
    exact (mul_comm ‚Äñf‚Äñ Œµ).le
  ¬∑ exact intble‚ÇÇ
  ¬∑ exact integrable_const Œµ
  ¬∑ exact intble‚ÇÅ
  ¬∑ exact intble‚ÇÇ.add <| integrable_const Œµ

lemma tendsto_integral_meas_thickening_le (f : Œ© ‚Üí·µá ‚Ñù)
    (Œµs : ‚Ñï ‚Üí ‚Ñù) (Œµs_lim : Tendsto Œµs atTop (ùìù[>] 0))
    {A : Set ‚Ñù} (A_finmeas : volume A ‚â† ‚àû) (Œº : ProbabilityMeasure Œ©) :
    Tendsto (fun n ‚Ü¶ (‚à´ t in A, ENNReal.toReal (Œº (thickening (Œµs n) {a | t ‚â§ f a})))) atTop
      (ùìù (‚à´ t in A, ENNReal.toReal (Œº {a | t ‚â§ f a}))) := by
  apply tendsto_integral_of_dominated_convergence (G := ‚Ñù) (Œº := volume.restrict A)
      (F := fun n t ‚Ü¶ (Œº (thickening (Œµs n) {a | t ‚â§ f a}))) (f := fun t ‚Ü¶ (Œº {a | t ‚â§ f a})) 1
  ¬∑ apply fun n ‚Ü¶ Measurable.aestronglyMeasurable ?_
    simp only [measurable_coe_nnreal_real_iff]
    apply measurable_toNNReal.comp <| Antitone.measurable (fun s t hst ‚Ü¶ ?_)
    exact measure_mono <| thickening_subset_of_subset _ <| fun œâ h ‚Ü¶ hst.trans h
  ¬∑ have aux : IsFiniteMeasure (volume.restrict A) := ‚ü®by simp [lt_top_iff_ne_top, A_finmeas]‚ü©
    apply integrable_const
  ¬∑ intro n
    apply eventually_of_forall (fun t ‚Ü¶ ?_)
    simp only [Real.norm_eq_abs, NNReal.abs_eq, Pi.one_apply]
    have obs : (Œº.toMeasure (thickening (Œµs n) {a | t ‚â§ f a})) ‚â§ 1 := prob_le_one
    exact (ENNReal.toReal_le_toReal (measure_ne_top _ _) one_ne_top).mpr obs
  ¬∑ apply eventually_of_forall (fun t ‚Ü¶ ?_)
    simp only [NNReal.tendsto_coe]
    apply (ENNReal.tendsto_toNNReal _).comp
    apply (tendsto_measure_thickening_of_isClosed ?_ ?_).comp Œµs_lim
    ¬∑ exact ‚ü®1, ‚ü®Real.zero_lt_one, measure_ne_top _ _‚ü©‚ü©
    ¬∑ exact isClosed_le continuous_const f.continuous
    ¬∑ exact measure_ne_top _ _

lemma continuous_levyProkhorov_to_probabilityMeasure :
    Continuous (levyProkhorov_to_probabilityMeasure Œ©) := by
  refine SeqContinuous.continuous ?_
  intro Œºs ŒΩ hŒºs
  set P := levyProkhorov_to_probabilityMeasure Œ© ŒΩ -- more palatable notation
  set Ps := fun n ‚Ü¶ levyProkhorov_to_probabilityMeasure Œ© (Œºs n) -- more palatable notation
  rw [ProbabilityMeasure.tendsto_iff_forall_integral_tendsto]
  refine fun f ‚Ü¶ @tendsto_integral_of_forall_limsup_integral_le_integral Œ© _ _ _ ‚Ñï atTop
    P _ (fun n ‚Ü¶ Ps n) _ ?_ f
  intro f f_nn
  by_cases f_zero : ‚Äñf‚Äñ = 0
  ¬∑ simp only [norm_eq_zero] at f_zero
    simp [f_zero, limsup_const]
  have norm_f_pos : 0 < ‚Äñf‚Äñ := lt_of_le_of_ne (norm_nonneg _) (fun a => f_zero a.symm)
  apply _root_.le_of_forall_pos_le_add
  intro Œ¥ Œ¥_pos
  apply limsup_le_of_le ?_
  ¬∑ obtain ‚ü®Œµs, ‚ü®_, ‚ü®Œµs_pos, Œµs_lim‚ü©‚ü©‚ü© := exists_seq_strictAnti_tendsto (0 : ‚Ñù)
    have Œµ_of_room := Tendsto.add (tendsto_iff_dist_tendsto_zero.mp hŒºs) Œµs_lim
    rw [add_zero] at Œµ_of_room
    have key := tendsto_integral_meas_thickening_le f (fun n ‚Ü¶ dist (Œºs n) ŒΩ + Œµs n)
                 ?_ (A := Ioc 0 ‚Äñf‚Äñ) (by simp) P
    ¬∑ have aux : ‚àÄ (z : ‚Ñù), Iio (z + Œ¥/2) ‚àà ùìù z := fun z ‚Ü¶ Iio_mem_nhds (by linarith)
      filter_upwards [key (aux _), Œµ_of_room <| Iio_mem_nhds <| half_pos <|
                        Real.mul_pos (inv_pos.mpr norm_f_pos) Œ¥_pos]
        with n hn hn'
      simp only [gt_iff_lt, eventually_atTop, ge_iff_le, ne_eq, mem_map,
        mem_atTop_sets, mem_preimage, mem_Iio] at *
      specialize Œµs_pos n
      have bound := BoundedContinuousFunction.integral_le_of_levyProkhorovEDist_lt
                      (Ps n) P (Œµ := dist (Œºs n) ŒΩ + Œµs n) ?_ ?_ f ?_
      ¬∑ refine bound.trans ?_
        apply (add_le_add hn.le rfl.le).trans
        rw [BoundedContinuousFunction.integral_eq_integral_meas_le']
        ¬∑ simp only [ProbabilityMeasure.ennreal_coeFn_eq_coeFn_toMeasure]
          rw [add_assoc]
          have also : (dist (Œºs n) ŒΩ + Œµs n) * ‚Äñf‚Äñ ‚â§ Œ¥/2 := by
            rw [mul_comm]
            apply (mul_le_mul (a := ‚Äñf‚Äñ) rfl.le hn'.le
                    (by positivity) (by exact norm_nonneg f)).trans (le_of_eq _)
            field_simp
            ring
          apply add_le_add rfl.le <| (add_le_add rfl.le also).trans <| by linarith
        ¬∑ exact eventually_of_forall f_nn
      ¬∑ positivity
      ¬∑ rw [ENNReal.ofReal_add (by positivity) (by positivity), ‚Üê add_zero (levyProkhorovEDist _ _)]
        apply ENNReal.add_lt_add_of_le_of_lt (levyProkhorovEDist_ne_top _ _)
              (le_of_eq ?_) (ofReal_pos.mpr Œµs_pos)
        rw [levyProkhorov_dist_def, levyProkhorovDist,
            ofReal_toReal (levyProkhorovEDist_ne_top _ _)]
        rfl
      ¬∑ exact eventually_of_forall f_nn
    ¬∑ apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ Œµ_of_room
      apply eventually_of_forall (fun n ‚Ü¶ ?_)
      simp only [mem_Ioi]
      specialize Œµs_pos n
      positivity
  ¬∑ simp only [IsCoboundedUnder, IsCobounded, eventually_map, eventually_atTop,
               ge_iff_le, forall_exists_index]
    refine ‚ü®0, fun a i hia ‚Ü¶ le_trans (integral_nonneg f_nn) (hia i rfl.le)‚ü©

theorem levyProkhorov_le_convergenceInDistribution :
    TopologicalSpace.coinduced (levyProkhorov_to_probabilityMeasure Œ©) inferInstance
      ‚â§ (inferInstance : TopologicalSpace (ProbabilityMeasure Œ©)) :=
  (continuous_levyProkhorov_to_probabilityMeasure).coinduced_le

end Levy_Prokhorov_comparison

end MeasureTheory -- namespace
