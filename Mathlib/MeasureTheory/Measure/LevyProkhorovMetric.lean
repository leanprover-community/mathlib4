/-
Copyright (c) 2023 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.MeasureTheory.Measure.Portmanteau
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Integral.Layercake
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction

/-!
# The L√©vy-Prokhorov distance on spaces of finite measures and probability measures

## Main definitions

* `MeasureTheory.levyProkhorovEDist`: The L√©vy-Prokhorov edistance between two measures.
* `MeasureTheory.levyProkhorovDist`: The L√©vy-Prokhorov distance between two finite measures.

## Main results

* `levyProkhorovDist_pseudoMetricSpace_finiteMeasure`: The L√©vy-Prokhorov distance is a
  pseudoemetric on the space of finite measures.
* `levyProkhorovDist_pseudoMetricSpace_probabilityMeasure`: The L√©vy-Prokhorov distance is a
  pseudoemetric on the space of probability measures.
* `levyProkhorov_le_convergenceInDistribution`: The topology of the L√©vy-Prokhorov metric on
  probability measures is always at least as fine as the topology of convergence in distribution.
* `levyProkhorov_eq_convergenceInDistribution`: The topology of the L√©vy-Prokhorov metric on
  probability measures on a separable space coincides with the topology of convergence in
  distribution, and in particular convergence in distribution is then pseudometrizable.

## Todo

* Show that in Borel spaces, the L√©vy-Prokhorov distance is a metric; not just a pseudometric.

## Tags

finite measure, probability measure, weak convergence, convergence in distribution, metrizability
-/

open Topology Metric Filter Set ENNReal NNReal

namespace MeasureTheory

open scoped Topology ENNReal NNReal BoundedContinuousFunction

section Levy_Prokhorov

/-! ### L√©vy-Prokhorov metric -/

variable {Œπ : Type*} {Œ© : Type*} [MeasurableSpace Œ©] [PseudoEMetricSpace Œ©]

/-- The L√©vy-Prokhorov edistance between measures:
`d(Œº,ŒΩ) = inf {r ‚â• 0 | ‚àÄ B, Œº B ‚â§ ŒΩ B·µ£ + r ‚àß ŒΩ B ‚â§ Œº B·µ£ + r}`. -/
noncomputable def levyProkhorovEDist (Œº ŒΩ : Measure Œ©) : ‚Ñù‚â•0‚àû :=
  sInf {Œµ | ‚àÄ B, MeasurableSet B ‚Üí
            Œº B ‚â§ ŒΩ (thickening Œµ.toReal B) + Œµ ‚àß ŒΩ B ‚â§ Œº (thickening Œµ.toReal B) + Œµ}

/- This result is not placed in earlier more generic files, since it is rather specialized;
it mixes measure and metric in a very particular way. -/
lemma meas_le_of_le_of_forall_le_meas_thickening_add {Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù‚â•0‚àû} (Œº ŒΩ : Measure Œ©)
    (h_le : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) {B : Set Œ©} (hŒµ‚ÇÅ : Œº B ‚â§ ŒΩ (thickening Œµ‚ÇÅ.toReal B) + Œµ‚ÇÅ) :
    Œº B ‚â§ ŒΩ (thickening Œµ‚ÇÇ.toReal B) + Œµ‚ÇÇ := by
  by_cases Œµ_top : Œµ‚ÇÇ = ‚àû
  ¬∑ simp only [ne_eq, FiniteMeasure.ennreal_coeFn_eq_coeFn_toMeasure, Œµ_top, top_toReal,
                add_top, le_top]
  apply hŒµ‚ÇÅ.trans (add_le_add ?_ h_le)
  exact measure_mono (Œº := ŒΩ) (thickening_mono (toReal_mono Œµ_top h_le) B)

lemma left_measure_le_of_levyProkhorovEDist_lt {Œº ŒΩ : Measure Œ©} {c : ‚Ñù‚â•0‚àû}
    (h : levyProkhorovEDist Œº ŒΩ < c) {B : Set Œ©} (B_mble : MeasurableSet B) :
    Œº B ‚â§ ŒΩ (thickening c.toReal B) + c := by
  obtain ‚ü®c', ‚ü®hc', lt_c‚ü©‚ü© := sInf_lt_iff.mp h
  exact meas_le_of_le_of_forall_le_meas_thickening_add Œº ŒΩ lt_c.le (hc' B B_mble).1

lemma right_measure_le_of_levyProkhorovEDist_lt {Œº ŒΩ : Measure Œ©} {c : ‚Ñù‚â•0‚àû}
    (h : levyProkhorovEDist Œº ŒΩ < c) {B : Set Œ©} (B_mble : MeasurableSet B) :
    ŒΩ B ‚â§ Œº (thickening c.toReal B) + c := by
  obtain ‚ü®c', ‚ü®hc', lt_c‚ü©‚ü© := sInf_lt_iff.mp h
  exact meas_le_of_le_of_forall_le_meas_thickening_add ŒΩ Œº lt_c.le (hc' B B_mble).2

/-- A general sufficient condition for bounding `levyProkhorovEDist` from above. -/
lemma levyProkhorovEDist_le_of_forall_add_pos_le (Œº ŒΩ : Measure Œ©) (Œ¥ : ‚Ñù‚â•0‚àû)
    (h : ‚àÄ Œµ B, 0 < Œµ ‚Üí Œµ < ‚àû ‚Üí MeasurableSet B ‚Üí
      Œº B ‚â§ ŒΩ (thickening (Œ¥ + Œµ).toReal B) + Œ¥ + Œµ ‚àß
      ŒΩ B ‚â§ Œº (thickening (Œ¥ + Œµ).toReal B) + Œ¥ + Œµ) :
    levyProkhorovEDist Œº ŒΩ ‚â§ Œ¥ := by
  apply ENNReal.le_of_forall_pos_le_add
  intro Œµ hŒµ _
  by_cases Œµ_top : Œµ = ‚àû
  ¬∑ simp only [Œµ_top, add_top, le_top]
  apply sInf_le
  intro B B_mble
  simpa only [add_assoc] using h Œµ B (coe_pos.mpr hŒµ) coe_lt_top B_mble

/-- A simple general sufficient condition for bounding `levyProkhorovEDist` from above. -/
lemma levyProkhorovEDist_le_of_forall (Œº ŒΩ : Measure Œ©) (Œ¥ : ‚Ñù‚â•0‚àû)
    (h : ‚àÄ Œµ B, Œ¥ < Œµ ‚Üí Œµ < ‚àû ‚Üí MeasurableSet B ‚Üí
        Œº B ‚â§ ŒΩ (thickening Œµ.toReal B) + Œµ ‚àß ŒΩ B ‚â§ Œº (thickening Œµ.toReal B) + Œµ) :
    levyProkhorovEDist Œº ŒΩ ‚â§ Œ¥ := by
  by_cases Œ¥_top : Œ¥ = ‚àû
  ¬∑ simp only [Œ¥_top, add_top, le_top]
  apply levyProkhorovEDist_le_of_forall_add_pos_le
  intro x B x_pos x_lt_top B_mble
  simpa only [‚Üê add_assoc] using h (Œ¥ + x) B (ENNReal.lt_add_right Œ¥_top x_pos.ne.symm)
    (by simp only [add_lt_top, Ne.lt_top Œ¥_top, x_lt_top, and_self]) B_mble

lemma levyProkhorovEDist_le_max_measure_univ (Œº ŒΩ : Measure Œ©) :
    levyProkhorovEDist Œº ŒΩ ‚â§ max (Œº univ) (ŒΩ univ) := by
  refine sInf_le fun B _ ‚Ü¶ ‚ü®?_, ?_‚ü© <;> apply le_add_left <;> simp [measure_mono]

lemma levyProkhorovEDist_lt_top (Œº ŒΩ : Measure Œ©) [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] :
    levyProkhorovEDist Œº ŒΩ < ‚àû :=
  (levyProkhorovEDist_le_max_measure_univ Œº ŒΩ).trans_lt <| by simp [measure_lt_top]

lemma levyProkhorovEDist_ne_top (Œº ŒΩ : Measure Œ©) [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] :
    levyProkhorovEDist Œº ŒΩ ‚â† ‚àû := (levyProkhorovEDist_lt_top Œº ŒΩ).ne

lemma levyProkhorovEDist_self (Œº : Measure Œ©) :
    levyProkhorovEDist Œº Œº = 0 := by
  rw [‚Üê nonpos_iff_eq_zero, ‚Üê csInf_Ioo zero_lt_top]
  refine sInf_le_sInf fun Œµ ‚ü®hŒµ‚ÇÄ, hŒµ_top‚ü© B _ ‚Ü¶ and_self_iff.2 ?_
  refine le_add_right <| measure_mono <| self_subset_thickening ?_ _
  exact ENNReal.toReal_pos hŒµ‚ÇÄ.ne' hŒµ_top.ne

lemma levyProkhorovEDist_comm (Œº ŒΩ : Measure Œ©) :
    levyProkhorovEDist Œº ŒΩ = levyProkhorovEDist ŒΩ Œº := by
  simp only [levyProkhorovEDist, and_comm]

lemma levyProkhorovEDist_triangle [OpensMeasurableSpace Œ©] (Œº ŒΩ Œ∫ : Measure Œ©) :
    levyProkhorovEDist Œº Œ∫ ‚â§ levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫ := by
  by_cases LPŒºŒΩ_finite : levyProkhorovEDist Œº ŒΩ = ‚àû
  ¬∑ simp [LPŒºŒΩ_finite]
  by_cases LPŒΩŒ∫_finite : levyProkhorovEDist ŒΩ Œ∫ = ‚àû
  ¬∑ simp [LPŒΩŒ∫_finite]
  apply levyProkhorovEDist_le_of_forall_add_pos_le
  intro Œµ B Œµ_pos Œµ_lt_top B_mble
  have half_Œµ_pos : 0 < Œµ / 2 := ENNReal.div_pos Œµ_pos.ne' two_ne_top
  have half_Œµ_lt_top : Œµ / 2 < ‚àû := ENNReal.div_lt_top Œµ_lt_top.ne two_ne_zero
  let r := levyProkhorovEDist Œº ŒΩ + Œµ / 2
  let s := levyProkhorovEDist ŒΩ Œ∫ + Œµ / 2
  have lt_r : levyProkhorovEDist Œº ŒΩ < r := lt_add_right LPŒºŒΩ_finite half_Œµ_pos.ne'
  have lt_s : levyProkhorovEDist ŒΩ Œ∫ < s := lt_add_right LPŒΩŒ∫_finite half_Œµ_pos.ne'
  have hs_add_r : s + r = levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫ + Œµ := by
    simp_rw [s, r, add_assoc, add_comm (Œµ / 2), add_assoc, ENNReal.add_halves, ‚Üê add_assoc,
      add_comm (levyProkhorovEDist Œº ŒΩ)]
  have hs_add_r' : s.toReal + r.toReal
      = (levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫ + Œµ).toReal := by
    rw [‚Üê hs_add_r, ‚Üê ENNReal.toReal_add]
    ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®LPŒΩŒ∫_finite, half_Œµ_lt_top.ne‚ü©
    ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®LPŒºŒΩ_finite, half_Œµ_lt_top.ne‚ü©
  rw [‚Üê hs_add_r', add_assoc, ‚Üê hs_add_r, add_assoc _ _ Œµ, ‚Üê hs_add_r]
  refine ‚ü®?_, ?_‚ü©
  ¬∑ calc Œº B ‚â§ ŒΩ (thickening r.toReal B) + r :=
      left_measure_le_of_levyProkhorovEDist_lt lt_r B_mble
    _ ‚â§ Œ∫ (thickening s.toReal (thickening r.toReal B)) + s + r :=
      add_le_add_right
        (left_measure_le_of_levyProkhorovEDist_lt lt_s isOpen_thickening.measurableSet) _
    _ = Œ∫ (thickening s.toReal (thickening r.toReal B)) + (s + r) := add_assoc _ _ _
    _ ‚â§ Œ∫ (thickening (s.toReal + r.toReal) B) + (s + r) :=
      add_le_add_right (measure_mono (thickening_thickening_subset _ _ _)) _
  ¬∑ calc Œ∫ B ‚â§ ŒΩ (thickening s.toReal B) + s :=
      right_measure_le_of_levyProkhorovEDist_lt lt_s B_mble
    _ ‚â§ Œº (thickening r.toReal (thickening s.toReal B)) + r + s :=
      add_le_add_right
        (right_measure_le_of_levyProkhorovEDist_lt lt_r isOpen_thickening.measurableSet) s
    _ = Œº (thickening r.toReal (thickening s.toReal B)) + (s + r) := by rw [add_assoc, add_comm r]
    _ ‚â§ Œº (thickening (r.toReal + s.toReal) B) + (s + r) :=
      add_le_add_right (measure_mono (thickening_thickening_subset _ _ _)) _
    _ = Œº (thickening (s.toReal + r.toReal) B) + (s + r) := by rw [add_comm r.toReal]

/-- The L√©vy-Prokhorov distance between finite measures:
`d(Œº,ŒΩ) = inf {r ‚â• 0 | ‚àÄ B, Œº B ‚â§ ŒΩ B·µ£ + r ‚àß ŒΩ B ‚â§ Œº B·µ£ + r}`. -/
noncomputable def levyProkhorovDist (Œº ŒΩ : Measure Œ©) : ‚Ñù :=
  (levyProkhorovEDist Œº ŒΩ).toReal

lemma levyProkhorovDist_self (Œº : Measure Œ©) :
    levyProkhorovDist Œº Œº = 0 := by
  simp only [levyProkhorovDist, levyProkhorovEDist_self, zero_toReal]

lemma levyProkhorovDist_comm (Œº ŒΩ : Measure Œ©) :
    levyProkhorovDist Œº ŒΩ = levyProkhorovDist ŒΩ Œº := by
  simp only [levyProkhorovDist, levyProkhorovEDist_comm]

lemma levyProkhorovDist_triangle [OpensMeasurableSpace Œ©] (Œº ŒΩ Œ∫ : Measure Œ©)
    [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] [IsFiniteMeasure Œ∫] :
    levyProkhorovDist Œº Œ∫ ‚â§ levyProkhorovDist Œº ŒΩ + levyProkhorovDist ŒΩ Œ∫ := by
  have dŒºŒ∫_finite := (levyProkhorovEDist_lt_top Œº Œ∫).ne
  have dŒºŒΩ_finite := (levyProkhorovEDist_lt_top Œº ŒΩ).ne
  have dŒΩŒ∫_finite := (levyProkhorovEDist_lt_top ŒΩ Œ∫).ne
  convert (ENNReal.toReal_le_toReal (a := levyProkhorovEDist Œº Œ∫)
    (b := levyProkhorovEDist Œº ŒΩ + levyProkhorovEDist ŒΩ Œ∫)
    _ _).mpr <| levyProkhorovEDist_triangle Œº ŒΩ Œ∫
  ¬∑ simp only [levyProkhorovDist, ENNReal.toReal_add dŒºŒΩ_finite dŒΩŒ∫_finite]
  ¬∑ exact dŒºŒ∫_finite
  ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®dŒºŒΩ_finite, dŒΩŒ∫_finite‚ü©

/-- A type synonym, to be used for `Measure Œ±`, `FiniteMeasure Œ±`, or `ProbabilityMeasure Œ±`,
when they are to be equipped with the L√©vy-Prokhorov distance. -/
def LevyProkhorov (Œ± : Type*) := Œ±

variable [OpensMeasurableSpace Œ©]

/-- The L√©vy-Prokhorov distance `levyProkhorovEDist` makes `Measure Œ©` a pseudoemetric
space. The instance is recorded on the type synonym `LevyProkhorov (Measure Œ©) := Measure Œ©`. -/
noncomputable instance : PseudoEMetricSpace (LevyProkhorov (Measure Œ©)) where
  edist := levyProkhorovEDist
  edist_self := levyProkhorovEDist_self
  edist_comm := levyProkhorovEDist_comm
  edist_triangle := levyProkhorovEDist_triangle

/-- The L√©vy-Prokhorov distance `levyProkhorovDist` makes `FiniteMeasure Œ©` a pseudometric
space. The instance is recorded on the type synonym
`LevyProkhorov (FiniteMeasure Œ©) := FiniteMeasure Œ©`. -/
noncomputable instance levyProkhorovDist_pseudoMetricSpace_finiteMeasure :
    PseudoMetricSpace (LevyProkhorov (FiniteMeasure Œ©)) where
  dist Œº ŒΩ := levyProkhorovDist Œº.toMeasure ŒΩ.toMeasure
  dist_self Œº := levyProkhorovDist_self _
  dist_comm Œº ŒΩ := levyProkhorovDist_comm _ _
  dist_triangle Œº ŒΩ Œ∫ := levyProkhorovDist_triangle _ _ _
  edist_dist Œº ŒΩ := by simp [‚Üê ENNReal.ofReal_coe_nnreal]

/-- The L√©vy-Prokhorov distance `levyProkhorovDist` makes `ProbabilityMeasure Œ©` a pseudoemetric
space. The instance is recorded on the type synonym
`LevyProkhorov (ProbabilityMeasure Œ©) := ProbabilityMeasure Œ©`. -/
noncomputable instance levyProkhorovDist_pseudoMetricSpace_probabilityMeasure :
    PseudoMetricSpace (LevyProkhorov (ProbabilityMeasure Œ©)) where
  dist Œº ŒΩ := levyProkhorovDist Œº.toMeasure ŒΩ.toMeasure
  dist_self Œº := levyProkhorovDist_self _
  dist_comm Œº ŒΩ := levyProkhorovDist_comm _ _
  dist_triangle Œº ŒΩ Œ∫ := levyProkhorovDist_triangle _ _ _
  edist_dist Œº ŒΩ := by simp [‚Üê ENNReal.ofReal_coe_nnreal]

lemma LevyProkhorov.dist_def (Œº ŒΩ : LevyProkhorov (ProbabilityMeasure Œ©)) :
    dist Œº ŒΩ = levyProkhorovDist Œº.toMeasure ŒΩ.toMeasure := rfl

/-- A simple sufficient condition for bounding `levyProkhorovEDist` between probability measures
from above. The condition involves only one of two natural bounds, the other bound is for free. -/
lemma levyProkhorovEDist_le_of_forall_le
    (Œº ŒΩ : Measure Œ©) [IsProbabilityMeasure Œº] [IsProbabilityMeasure ŒΩ] (Œ¥ : ‚Ñù‚â•0‚àû)
    (h : ‚àÄ Œµ B, Œ¥ < Œµ ‚Üí Œµ < ‚àû ‚Üí MeasurableSet B ‚Üí Œº B ‚â§ ŒΩ (thickening Œµ.toReal B) + Œµ) :
    levyProkhorovEDist Œº ŒΩ ‚â§ Œ¥ := by
  apply levyProkhorovEDist_le_of_forall Œº ŒΩ Œ¥
  intro Œµ B Œµ_gt Œµ_lt_top B_mble
  refine ‚ü®h Œµ B Œµ_gt Œµ_lt_top B_mble, ?_‚ü©
  have B_subset := subset_compl_thickening_compl_thickening_self Œµ.toReal B
  apply (measure_mono (Œº := ŒΩ) B_subset).trans
  rw [prob_compl_eq_one_sub isOpen_thickening.measurableSet]
  have Tc_mble := (isOpen_thickening (Œ¥ := Œµ.toReal) (E := B)).isClosed_compl.measurableSet
  specialize h Œµ (thickening Œµ.toReal B)·∂ú Œµ_gt Œµ_lt_top Tc_mble
  rw [prob_compl_eq_one_sub isOpen_thickening.measurableSet] at h
  have almost := add_le_add (c := Œº (thickening Œµ.toReal B)) h rfl.le
  rw [tsub_add_cancel_of_le prob_le_one, add_assoc] at almost
  apply (tsub_le_tsub_right almost _).trans
  rw [ENNReal.add_sub_cancel_left (measure_ne_top ŒΩ _), add_comm Œµ]

/-- A simple sufficient condition for bounding `levyProkhorovDist` between probability measures
from above. The condition involves only one of two natural bounds, the other bound is for free. -/
lemma levyProkhorovDist_le_of_forall_le
    (Œº ŒΩ : Measure Œ©) [IsProbabilityMeasure Œº] [IsProbabilityMeasure ŒΩ] {Œ¥ : ‚Ñù} (Œ¥_nn : 0 ‚â§ Œ¥)
    (h : ‚àÄ Œµ B, Œ¥ < Œµ ‚Üí MeasurableSet B ‚Üí Œº B ‚â§ ŒΩ (thickening Œµ B) + ENNReal.ofReal Œµ) :
    levyProkhorovDist Œº ŒΩ ‚â§ Œ¥ := by
  apply toReal_le_of_le_ofReal Œ¥_nn
  apply levyProkhorovEDist_le_of_forall_le
  intro Œµ B Œµ_gt Œµ_lt_top B_mble
  have Œµ_gt' : Œ¥ < Œµ.toReal := by
    refine (ofReal_lt_ofReal_iff ?_).mp ?_
    ¬∑ exact ENNReal.toReal_pos (ne_zero_of_lt Œµ_gt) Œµ_lt_top.ne
    ¬∑ simpa [ofReal_toReal_eq_iff.mpr Œµ_lt_top.ne] using Œµ_gt
  convert h Œµ.toReal B Œµ_gt' B_mble
  exact (ENNReal.ofReal_toReal Œµ_lt_top.ne).symm

end Levy_Prokhorov --section

section Levy_Prokhorov_is_finer

/-! ### The L√©vy-Prokhorov topology is at least as fine as convergence in distribution -/

open BoundedContinuousFunction

variable {Œπ : Type*} {Œ© : Type*} [MeasurableSpace Œ©]

/-- Coercion from the type synonym `LevyProkhorov (ProbabilityMeasure Œ©)`
to `ProbabilityMeasure Œ©`. -/
def LevyProkhorov.toProbabilityMeasure (Œº : LevyProkhorov (ProbabilityMeasure Œ©)) :
    ProbabilityMeasure Œ© := Œº

/-- Coercion to the type synonym `LevyProkhorov (ProbabilityMeasure Œ©)`
from `ProbabilityMeasure Œ©`. -/
def ProbabilityMeasure.toLevyProkhorov (Œº : ProbabilityMeasure Œ©) :
    LevyProkhorov (ProbabilityMeasure Œ©) := Œº

/-- Coercion from the type synonym `LevyProkhorov (FiniteMeasure Œ©)` to `FiniteMeasure Œ©`. -/
def LevyProkhorov.finiteMeasure (Œº : LevyProkhorov (FiniteMeasure Œ©)) :
    FiniteMeasure Œ© := Œº

variable [PseudoMetricSpace Œ©] [OpensMeasurableSpace Œ©]

/-- A version of the layer cake formula for bounded continuous functions which have finite integral:
‚à´ f dŒº = ‚à´ t in (0, ‚Äñf‚Äñ], Œº {x | f(x) ‚â• t} dt. -/
lemma BoundedContinuousFunction.integral_eq_integral_meas_le_of_hasFiniteIntegral
    {Œ± : Type*} [MeasurableSpace Œ±] [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    (f : Œ± ‚Üí·µá ‚Ñù) (Œº : Measure Œ±) (f_nn : 0 ‚â§·µê[Œº] f) (hf : HasFiniteIntegral f Œº) :
    ‚à´ œâ, f œâ ‚àÇŒº = ‚à´ t in Ioc 0 ‚Äñf‚Äñ, ENNReal.toReal (Œº {a : Œ± | t ‚â§ f a}) := by
  rw [Integrable.integral_eq_integral_Ioc_meas_le (M := ‚Äñf‚Äñ) ?_ f_nn ?_]
  ¬∑ refine ‚ü®f.continuous.measurable.aestronglyMeasurable, hf‚ü©
  ¬∑ exact eventually_of_forall (fun x ‚Ü¶ BoundedContinuousFunction.apply_le_norm f x)

/-- A version of the layer cake formula for bounded continuous functions and finite measures:
‚à´ f dŒº = ‚à´ t in (0, ‚Äñf‚Äñ], Œº {x | f(x) ‚â• t} dt. -/
lemma BoundedContinuousFunction.integral_eq_integral_meas_le
    {Œ± : Type*} [MeasurableSpace Œ±] [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    (f : Œ± ‚Üí·µá ‚Ñù) (Œº : Measure Œ±) [IsFiniteMeasure Œº] (f_nn : 0 ‚â§·µê[Œº] f) :
    ‚à´ œâ, f œâ ‚àÇŒº = ‚à´ t in Ioc 0 ‚Äñf‚Äñ, ENNReal.toReal (Œº {a : Œ± | t ‚â§ f a}) :=
  integral_eq_integral_meas_le_of_hasFiniteIntegral _ _ f_nn (f.integrable Œº).2

/-- Assuming `levyProkhorovEDist Œº ŒΩ < Œµ`, we can bound `‚à´ f ‚àÇŒº` in terms of
`‚à´ t in (0, ‚Äñf‚Äñ], ŒΩ (thickening Œµ {x | f(x) ‚â• t}) dt` and `‚Äñf‚Äñ`. -/
lemma BoundedContinuousFunction.integral_le_of_levyProkhorovEDist_lt (Œº ŒΩ : Measure Œ©)
    [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] {Œµ : ‚Ñù} (Œµ_pos : 0 < Œµ)
    (hŒºŒΩ : levyProkhorovEDist Œº ŒΩ < ENNReal.ofReal Œµ) (f : Œ© ‚Üí·µá ‚Ñù) (f_nn : 0 ‚â§·µê[Œº] f) :
    ‚à´ œâ, f œâ ‚àÇŒº
      ‚â§ (‚à´ t in Ioc 0 ‚Äñf‚Äñ, ENNReal.toReal (ŒΩ (thickening Œµ {a | t ‚â§ f a}))) + Œµ * ‚Äñf‚Äñ := by
  rw [BoundedContinuousFunction.integral_eq_integral_meas_le f Œº f_nn]
  have key : (fun (t : ‚Ñù) ‚Ü¶ ENNReal.toReal (Œº {a | t ‚â§ f a}))
              ‚â§ (fun (t : ‚Ñù) ‚Ü¶ ENNReal.toReal (ŒΩ (thickening Œµ {a | t ‚â§ f a})) + Œµ) := by
    intro t
    convert (ENNReal.toReal_le_toReal (measure_ne_top _ _) ?_).mpr
            <| left_measure_le_of_levyProkhorovEDist_lt hŒºŒΩ (B := {a | t ‚â§ f a})
                (f.continuous.measurable measurableSet_Ici)
    ¬∑ rw [ENNReal.toReal_add (measure_ne_top ŒΩ _) ofReal_ne_top, ENNReal.toReal_ofReal Œµ_pos.le]
    ¬∑ exact ENNReal.add_ne_top.mpr ‚ü®measure_ne_top ŒΩ _, ofReal_ne_top‚ü©
  have intble‚ÇÅ : IntegrableOn (fun t ‚Ü¶ ENNReal.toReal (Œº {a | t ‚â§ f a})) (Ioc 0 ‚Äñf‚Äñ) := by
    apply Measure.integrableOn_of_bounded (M := ENNReal.toReal (Œº univ)) measure_Ioc_lt_top.ne
    ¬∑ apply (Measurable.ennreal_toReal (Antitone.measurable ?_)).aestronglyMeasurable
      exact fun _ _ hst ‚Ü¶ measure_mono (fun _ h ‚Ü¶ hst.trans h)
    ¬∑ apply eventually_of_forall <| fun t ‚Ü¶ ?_
      simp only [Real.norm_eq_abs, abs_toReal]
      exact (ENNReal.toReal_le_toReal (measure_ne_top _ _) (measure_ne_top _ _)).mpr
            <| measure_mono (subset_univ _)
  have intble‚ÇÇ : IntegrableOn
                  (fun t ‚Ü¶ ENNReal.toReal (ŒΩ (thickening Œµ {a | t ‚â§ f a}))) (Ioc 0 ‚Äñf‚Äñ) := by
    apply Measure.integrableOn_of_bounded (M := ENNReal.toReal (ŒΩ univ)) measure_Ioc_lt_top.ne
    ¬∑ apply (Measurable.ennreal_toReal (Antitone.measurable ?_)).aestronglyMeasurable
      exact fun _ _ hst ‚Ü¶ measure_mono <| thickening_subset_of_subset Œµ (fun _ h ‚Ü¶ hst.trans h)
    ¬∑ apply eventually_of_forall <| fun t ‚Ü¶ ?_
      simp only [Real.norm_eq_abs, abs_toReal]
      exact (ENNReal.toReal_le_toReal (measure_ne_top _ _) (measure_ne_top _ _)).mpr
            <| measure_mono (subset_univ _)
  apply le_trans (setIntegral_mono (s := Ioc 0 ‚Äñf‚Äñ) ?_ ?_ key)
  ¬∑ rw [integral_add]
    ¬∑ apply add_le_add_left
      simp only [integral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter,
                  Real.volume_Ioc, sub_zero, norm_nonneg, toReal_ofReal, smul_eq_mul,
                  (mul_comm _ Œµ).le]
    ¬∑ exact intble‚ÇÇ
    ¬∑ exact integrable_const Œµ
  ¬∑ exact intble‚ÇÅ
  ¬∑ exact intble‚ÇÇ.add <| integrable_const Œµ

/-- A monotone decreasing convergence lemma for integrals of measures of thickenings:
`‚à´ t in (0, ‚Äñf‚Äñ], Œº (thickening Œµ {x | f(x) ‚â• t}) dt` tends to
`‚à´ t in (0, ‚Äñf‚Äñ], Œº {x | f(x) ‚â• t} dt` as `Œµ ‚Üí 0`. -/
lemma tendsto_integral_meas_thickening_le (f : Œ© ‚Üí·µá ‚Ñù)
    {A : Set ‚Ñù} (A_finmeas : volume A ‚â† ‚àû) (Œº : ProbabilityMeasure Œ©) :
    Tendsto (fun Œµ ‚Ü¶ ‚à´ t in A, ENNReal.toReal (Œº (thickening Œµ {a | t ‚â§ f a}))) (ùìù[>] (0 : ‚Ñù))
      (ùìù (‚à´ t in A, ENNReal.toReal (Œº {a | t ‚â§ f a}))) := by
  apply tendsto_integral_filter_of_dominated_convergence (G := ‚Ñù) (Œº := volume.restrict A)
        (F := fun Œµ t ‚Ü¶ (Œº (thickening Œµ {a | t ‚â§ f a}))) (f := fun t ‚Ü¶ (Œº {a | t ‚â§ f a})) 1
  ¬∑ apply eventually_of_forall fun n ‚Ü¶ Measurable.aestronglyMeasurable ?_
    simp only [measurable_coe_nnreal_real_iff]
    apply measurable_toNNReal.comp <| Antitone.measurable (fun s t hst ‚Ü¶ ?_)
    exact measure_mono <| thickening_subset_of_subset _ <| fun œâ h ‚Ü¶ hst.trans h
  ¬∑ apply eventually_of_forall (fun i ‚Ü¶ ?_)
    apply eventually_of_forall (fun t ‚Ü¶ ?_)
    simp only [Real.norm_eq_abs, NNReal.abs_eq, Pi.one_apply]
    exact (ENNReal.toReal_le_toReal (measure_ne_top _ _) one_ne_top).mpr prob_le_one
  ¬∑ have aux : IsFiniteMeasure (volume.restrict A) := ‚ü®by simp [lt_top_iff_ne_top, A_finmeas]‚ü©
    apply integrable_const
  ¬∑ apply eventually_of_forall (fun t ‚Ü¶ ?_)
    simp only [NNReal.tendsto_coe]
    apply (ENNReal.tendsto_toNNReal _).comp
    ¬∑ apply tendsto_measure_thickening_of_isClosed ?_ ?_
      ¬∑ exact ‚ü®1, ‚ü®Real.zero_lt_one, measure_ne_top _ _‚ü©‚ü©
      ¬∑ exact isClosed_le continuous_const f.continuous
    ¬∑ exact measure_ne_top _ _

/-- The coercion `LevyProkhorov (ProbabilityMeasure Œ©) ‚Üí ProbabilityMeasure Œ©` is continuous. -/
lemma LevyProkhorov.continuous_toProbabilityMeasure :
    Continuous (LevyProkhorov.toProbabilityMeasure (Œ© := Œ©)) := by
  refine SeqContinuous.continuous ?_
  intro Œºs ŒΩ hŒºs
  set P := ŒΩ.toProbabilityMeasure -- more palatable notation
  set Ps := fun n ‚Ü¶ (Œºs n).toProbabilityMeasure -- more palatable notation
  rw [ProbabilityMeasure.tendsto_iff_forall_integral_tendsto]
  refine fun f ‚Ü¶ tendsto_integral_of_forall_limsup_integral_le_integral ?_ f
  intro f f_nn
  by_cases f_zero : ‚Äñf‚Äñ = 0
  ¬∑ simp only [norm_eq_zero] at f_zero
    simp [f_zero, limsup_const]
  have norm_f_pos : 0 < ‚Äñf‚Äñ := lt_of_le_of_ne (norm_nonneg _) (fun a => f_zero a.symm)
  apply _root_.le_of_forall_pos_le_add
  intro Œ¥ Œ¥_pos
  apply limsup_le_of_le ?_
  ¬∑ obtain ‚ü®Œµs, ‚ü®_, ‚ü®Œµs_pos, Œµs_lim‚ü©‚ü©‚ü© := exists_seq_strictAnti_tendsto (0 : ‚Ñù)
    have Œµ_of_room := Tendsto.add (tendsto_iff_dist_tendsto_zero.mp hŒºs) Œµs_lim
    have Œµ_of_room' : Tendsto (fun n ‚Ü¶ dist (Œºs n) ŒΩ + Œµs n) atTop (ùìù[>] 0) := by
      rw [tendsto_nhdsWithin_iff]
      refine ‚ü®by simpa using Œµ_of_room, eventually_of_forall fun n ‚Ü¶ ?_‚ü©
      ¬∑ rw [mem_Ioi]
        linarith [Œµs_pos n, dist_nonneg (x := Œºs n) (y := ŒΩ)]
    rw [add_zero] at Œµ_of_room
    have key := (tendsto_integral_meas_thickening_le f (A := Ioc 0 ‚Äñf‚Äñ) (by simp) P).comp Œµ_of_room'
    have aux : ‚àÄ (z : ‚Ñù), Iio (z + Œ¥/2) ‚àà ùìù z := fun z ‚Ü¶ Iio_mem_nhds (by linarith)
    filter_upwards [key (aux _), Œµ_of_room <| Iio_mem_nhds <| half_pos <|
                      Real.mul_pos (inv_pos.mpr norm_f_pos) Œ¥_pos]
      with n hn hn'
    simp only [gt_iff_lt, eventually_atTop, ge_iff_le, ne_eq, mem_map,
               mem_atTop_sets, mem_preimage, mem_Iio] at *
    specialize Œµs_pos n
    have bound := BoundedContinuousFunction.integral_le_of_levyProkhorovEDist_lt
                    (Ps n) P (Œµ := dist (Œºs n) ŒΩ + Œµs n) ?_ ?_ f ?_
    ¬∑ refine bound.trans ?_
      apply (add_le_add_right hn.le _).trans
      rw [BoundedContinuousFunction.integral_eq_integral_meas_le]
      ¬∑ simp only [ProbabilityMeasure.ennreal_coeFn_eq_coeFn_toMeasure]
        rw [add_assoc, mul_comm]
        gcongr
        calc
          Œ¥ / 2 + ‚Äñf‚Äñ * (dist (Œºs n) ŒΩ + Œµs n)
          _ ‚â§ Œ¥ / 2 + ‚Äñf‚Äñ * (‚Äñf‚Äñ‚Åª¬π * Œ¥ / 2) := by gcongr
          _ = Œ¥ := by field_simp; ring
      ¬∑ exact eventually_of_forall f_nn
    ¬∑ positivity
    ¬∑ rw [ENNReal.ofReal_add (by positivity) (by positivity), ‚Üê add_zero (levyProkhorovEDist _ _)]
      apply ENNReal.add_lt_add_of_le_of_lt (levyProkhorovEDist_ne_top _ _)
            (le_of_eq ?_) (ofReal_pos.mpr Œµs_pos)
      rw [LevyProkhorov.dist_def, levyProkhorovDist,
          ofReal_toReal (levyProkhorovEDist_ne_top _ _)]
      simp only [Ps, P, LevyProkhorov.toProbabilityMeasure]
    ¬∑ exact eventually_of_forall f_nn
  ¬∑ simp only [IsCoboundedUnder, IsCobounded, eventually_map, eventually_atTop,
               ge_iff_le, forall_exists_index]
    refine ‚ü®0, fun a i hia ‚Ü¶ le_trans (integral_nonneg f_nn) (hia i le_rfl)‚ü©

/-- The topology of the L√©vy-Prokhorov metric is at least as fine as the topology of convergence in
distribution. -/
theorem levyProkhorov_le_convergenceInDistribution :
    TopologicalSpace.coinduced (LevyProkhorov.toProbabilityMeasure (Œ© := Œ©)) inferInstance
      ‚â§ (inferInstance : TopologicalSpace (ProbabilityMeasure Œ©)) :=
  (LevyProkhorov.continuous_toProbabilityMeasure).coinduced_le

end Levy_Prokhorov_is_finer

section Levy_Prokhorov_metrizes_convergence_in_distribution

/-! ### On separable spaces the L√©vy-Prokhorov distance metrizes convergence in distribution -/

open BoundedContinuousFunction TopologicalSpace

variable {Œπ : Type*} (Œ© : Type*) [PseudoMetricSpace Œ©] [SeparableSpace Œ©]
variable [MeasurableSpace Œ©] [OpensMeasurableSpace Œ©]

/-- In a separable pseudometric space, for any Œµ > 0 there exists a countable collection of
disjoint Borel measurable subsets of diameter at most Œµ that cover the whole space. -/
lemma SeparableSpace.exists_measurable_partition_diam_le {Œµ : ‚Ñù} (Œµ_pos : 0 < Œµ) :
    ‚àÉ (As : ‚Ñï ‚Üí Set Œ©), (‚àÄ n, MeasurableSet (As n)) ‚àß (‚àÄ n, Bornology.IsBounded (As n)) ‚àß
        (‚àÄ n, diam (As n) ‚â§ Œµ) ‚àß (‚ãÉ n, As n = univ) ‚àß
        (Pairwise (fun (n m : ‚Ñï) ‚Ü¶ Disjoint (As n) (As m))) := by
  by_cases X_emp : IsEmpty Œ©
  ¬∑ refine ‚ü®fun _ ‚Ü¶ ‚àÖ, fun _ ‚Ü¶ MeasurableSet.empty, fun _ ‚Ü¶ Bornology.isBounded_empty, ?_, ?_,
            fun _ _ _ ‚Ü¶ disjoint_of_subsingleton‚ü©
    ¬∑ intro n
      simpa only [diam_empty] using LT.lt.le Œµ_pos
    ¬∑ simp only [iUnion_empty]
      apply Eq.symm
      simp only [univ_eq_empty_iff, X_emp]
  rw [not_isEmpty_iff] at X_emp
  obtain ‚ü®xs, xs_dense‚ü© := exists_dense_seq Œ©
  have half_Œµ_pos : 0 < Œµ / 2 := half_pos Œµ_pos
  set Bs := fun n ‚Ü¶ Metric.ball (xs n) (Œµ/2)
  set As := disjointed Bs
  refine ‚ü®As, ?_, ?_, ?_, ?_, ?_‚ü©
  ¬∑ exact MeasurableSet.disjointed (fun n ‚Ü¶ measurableSet_ball)
  ¬∑ exact fun n ‚Ü¶ Bornology.IsBounded.subset isBounded_ball <| disjointed_subset Bs n
  ¬∑ intro n
    apply (diam_mono (disjointed_subset Bs n) isBounded_ball).trans
    convert diam_ball half_Œµ_pos.le
    ring
  ¬∑ have aux : ‚ãÉ n, Bs n = univ := by
      convert DenseRange.iUnion_uniformity_ball xs_dense <| Metric.dist_mem_uniformity half_Œµ_pos
      exact (ball_eq_ball' _ _).symm
    simpa only [‚Üê aux] using iUnion_disjointed
  ¬∑ exact disjoint_disjointed Bs

variable {Œ©}

lemma ProbabilityMeasure.toMeasure_add_pos_gt_mem_nhds (P : ProbabilityMeasure Œ©)
    {G : Set Œ©} (G_open : IsOpen G) {Œµ : ‚Ñù‚â•0‚àû} (Œµ_pos : 0 < Œµ) :
    {Q | P.toMeasure G < Q.toMeasure G + Œµ} ‚àà ùìù P := by
  by_cases easy : P.toMeasure G < Œµ
  ¬∑ exact eventually_of_forall (fun _ ‚Ü¶ lt_of_lt_of_le easy le_add_self)
  by_cases Œµ_top : Œµ = ‚àû
  ¬∑ simp [Œµ_top, measure_lt_top]
  simp only [not_lt] at easy
  have aux : P.toMeasure G - Œµ < liminf (fun Q ‚Ü¶ Q.toMeasure G) (ùìù P) := by
    apply lt_of_lt_of_le (ENNReal.sub_lt_self (measure_lt_top _ _).ne _ _)
        <| ProbabilityMeasure.le_liminf_measure_open_of_tendsto tendsto_id G_open
    ¬∑ exact (lt_of_lt_of_le Œµ_pos easy).ne.symm
    ¬∑ exact Œµ_pos.ne.symm
  filter_upwards [gt_mem_sets_of_limsInf_gt (Œ± := ‚Ñù‚â•0‚àû) isBounded_ge_of_bot
      (show P.toMeasure G - Œµ < limsInf ((ùìù P).map (fun Q ‚Ü¶ Q.toMeasure G)) from aux)] with Q hQ
  simp only [preimage_setOf_eq, mem_setOf_eq] at hQ
  convert ENNReal.add_lt_add_right Œµ_top hQ
  exact (tsub_add_cancel_of_le easy).symm

lemma ProbabilityMeasure.continuous_toLevyProkhorov :
    Continuous (ProbabilityMeasure.toLevyProkhorov (Œ© := Œ©)) := by
  -- We check continuity of `id : ProbabilityMeasure Œ© ‚Üí LevyProkhorov (ProbabilityMeasure Œ©)` at
  -- each point `P : ProbabilityMeasure Œ©`.
  rw [continuous_iff_continuousAt]
  intro P
  -- To check continuity, fix `Œµ > 0`. To leave some wiggle room, be ready to use `Œµ/3 > 0` instead.
  rw [continuousAt_iff']
  intro Œµ Œµ_pos
  have third_Œµ_pos : 0 < Œµ / 3 := by linarith
  have third_Œµ_pos' : 0 < ENNReal.ofReal (Œµ / 3) := ofReal_pos.mpr third_Œµ_pos
  -- First use separability to choose a countable partition of `Œ©` into measurable
  -- subsets `Es n ‚äÜ Œ©` of small diamater, `diam (Es n) < Œµ/3`.
  obtain ‚ü®Es, Es_mble, Es_bdd, Es_diam, Es_cover, Es_disjoint‚ü© :=
    SeparableSpace.exists_measurable_partition_diam_le Œ© third_Œµ_pos
  -- Instead of the whole space `Œ© = ‚ãÉ n ‚àà ‚Ñï, Es n`, focus on a large but finite
  -- union `‚ãÉ n < N, Es n`, chosen in such a way that the complement has small `P`-mass,
  -- `P (‚ãÉ n < N, Es n)·∂ú < Œµ/3`.
  obtain ‚ü®N, hN‚ü© : ‚àÉ N, P.toMeasure (‚ãÉ j ‚àà Iio N, Es j)·∂ú < ENNReal.ofReal (Œµ/3) := by
    have exhaust := @tendsto_measure_biUnion_Ici_zero_of_pairwise_disjoint Œ© _ P.toMeasure _
                    Es Es_mble Es_disjoint
    simp only [tendsto_atTop_nhds, Function.comp_apply] at exhaust
    obtain ‚ü®N, hN‚ü© := exhaust (Iio (ENNReal.ofReal (Œµ / 3))) third_Œµ_pos' isOpen_Iio
    refine ‚ü®N, ?_‚ü©
    have rewr : ‚ãÉ i, ‚ãÉ (_ : N ‚â§ i), Es i = (‚ãÉ i, ‚ãÉ (_ : i < N), Es i)·∂ú := by
      simpa only [mem_Iio, compl_Iio, mem_Ici] using
        (biUnion_compl_eq_of_pairwise_disjoint_of_iUnion_eq_univ Es_cover Es_disjoint (Iio N)).symm
    simpa only [mem_Iio, ‚Üê rewr, gt_iff_lt] using hN N le_rfl
  -- With the finite `N` fixed above, consider the finite collection of open sets of the form
  -- `Gs J = thickening (Œµ/3) (‚ãÉ j ‚àà J, Es j)`, where `J ‚äÜ {0, 1, ..., N-1}`.
  have Js_finite : Set.Finite {J | J ‚äÜ Iio N} := Finite.finite_subsets <| finite_Iio N
  set Gs := (fun (J : Set ‚Ñï) ‚Ü¶ thickening (Œµ/3) (‚ãÉ j ‚àà J, Es j)) '' {J | J ‚äÜ Iio N}
  have Gs_open : ‚àÄ (J : Set ‚Ñï), IsOpen (thickening (Œµ/3) (‚ãÉ j ‚àà J, Es j)) :=
    fun J ‚Ü¶ isOpen_thickening
  -- Any open set `G ‚äÜ Œ©` determines a neighborhood of `P` consisting of those `Q` that
  -- satisfy `P G < Q G + Œµ/3`.
  have mem_nhds_P (G : Set Œ©) (G_open : IsOpen G) :
      {Q | P.toMeasure G < Q.toMeasure G + ENNReal.ofReal (Œµ/3)} ‚àà ùìù P :=
    P.toMeasure_add_pos_gt_mem_nhds G_open third_Œµ_pos'
  -- Assume that `Q` is in the neighborhood of `P` such that for each `J ‚äÜ {0, 1, ..., N-1}`
  -- we have `P (Gs J) < Q (Gs J) + Œµ/3`.
  filter_upwards [(Finset.iInter_mem_sets Js_finite.toFinset).mpr <|
                    fun J _ ‚Ü¶ mem_nhds_P _ (Gs_open J)] with Q hQ
  simp only [Finite.mem_toFinset, mem_setOf_eq, thickening_iUnion, mem_iInter] at hQ
  -- Note that in order to show that the L√©vy-Prokhorov distance `LPdist P Q` is small (`‚â§ 2*Œµ/3`),
  -- it suffices to show that for arbitrary subsets `B ‚äÜ Œ©`, the measure `P B` is bounded above up
  -- to a small error by the `Q`-measure of a small thickening of `B`.
  apply lt_of_le_of_lt ?_ (show 2*(Œµ/3) < Œµ by linarith)
  rw [dist_comm]
  -- Fix an arbitrary set `B ‚äÜ Œ©`, and an arbitrary `Œ¥ > 2*Œµ/3` to gain some room for error
  -- and for thickening.
  apply levyProkhorovDist_le_of_forall_le _ _ (by linarith) (fun Œ¥ B Œ¥_gt _ ‚Ü¶ ?_)
  -- Let `JB ‚äÜ {0, 1, ..., N-1}` consist of those indices `j` such that `B` intersects `Es j`.
  -- Then the open set `Gs JB` approximates `B` rather well:
  -- except for what happens in the small complement `(‚ãÉ n < N, Es n)·∂ú`, the set `B` is
  -- contained in `Gs JB`, and conversely `Gs JB` only contains points within `Œ¥` from `B`.
  set JB := {i | B ‚à© Es i ‚â† ‚àÖ ‚àß i ‚àà Iio N}
  have B_subset : B ‚äÜ (‚ãÉ i ‚àà JB, thickening (Œµ/3) (Es i)) ‚à™ (‚ãÉ j ‚àà Iio N, Es j)·∂ú := by
    suffices B ‚äÜ (‚ãÉ i ‚àà JB, thickening (Œµ/3) (Es i)) ‚à™ (‚ãÉ j ‚àà Ici N, Es j) by
      refine this.trans <| union_subset_union le_rfl ?_
      intro œâ hœâ
      simp only [mem_Ici, mem_iUnion, exists_prop] at hœâ
      obtain ‚ü®i, i_large, œâ_in_Esi‚ü© := hœâ
      by_contra con
      simp only [mem_Iio, compl_iUnion, mem_iInter, mem_compl_iff, not_forall, not_not,
                  exists_prop] at con
      obtain ‚ü®j, j_small, œâ_in_Esj‚ü© := con
      exact disjoint_left.mp (Es_disjoint (show j ‚â† i by linarith)) œâ_in_Esj œâ_in_Esi
    intro œâ œâ_in_B
    obtain ‚ü®i, hi‚ü© := show ‚àÉ n, œâ ‚àà Es n by simp only [‚Üê mem_iUnion, Es_cover, mem_univ]
    simp only [mem_Ici, mem_union, mem_iUnion, exists_prop]
    by_cases i_small : i ‚àà Iio N
    ¬∑ refine Or.inl ‚ü®i, ?_, self_subset_thickening third_Œµ_pos _ hi‚ü©
      simp only [mem_Iio, mem_setOf_eq, JB]
      refine ‚ü®nonempty_iff_ne_empty.mp <| Set.nonempty_of_mem <| mem_inter œâ_in_B hi, i_small‚ü©
    ¬∑ exact Or.inr ‚ü®i, by simpa only [mem_Iio, not_lt] using i_small, hi‚ü©
  have subset_thickB : ‚ãÉ i ‚àà JB, thickening (Œµ / 3) (Es i) ‚äÜ thickening Œ¥ B := by
    intro œâ œâ_in_U
    simp only [mem_setOf_eq, mem_iUnion, exists_prop] at œâ_in_U
    obtain ‚ü®k, ‚ü®B_intersects, _‚ü©, œâ_in_thEk‚ü© := œâ_in_U
    rw [mem_thickening_iff] at œâ_in_thEk ‚ä¢
    obtain ‚ü®w, w_in_Ek, w_near‚ü© := œâ_in_thEk
    obtain ‚ü®z, ‚ü®z_in_B, z_in_Ek‚ü©‚ü© := nonempty_iff_ne_empty.mpr B_intersects
    refine ‚ü®z, z_in_B, lt_of_le_of_lt (dist_triangle œâ w z) ?_‚ü©
    apply lt_of_le_of_lt (add_le_add w_near.le <|
            (dist_le_diam_of_mem (Es_bdd k) w_in_Ek z_in_Ek).trans <| Es_diam k)
    linarith
  -- We use the resulting upper bound `P B ‚â§ P (Gs JB) + P (small complement)`.
  apply (measure_mono B_subset).trans ((measure_union_le _ _).trans ?_)
  -- From the choice of `Q` in a suitable neighborhood, we have `P (Gs JB) < Q (Gs JB) + Œµ/3`.
  specialize hQ _ (show JB ‚äÜ Iio N from fun _ h ‚Ü¶ h.2)
  -- Now it remains to add the pieces and use the above estimates.
  apply (add_le_add hQ.le hN.le).trans
  rw [add_assoc, ‚Üê ENNReal.ofReal_add third_Œµ_pos.le third_Œµ_pos.le, ‚Üê two_mul]
  apply add_le_add (measure_mono subset_thickB) (ofReal_le_ofReal _)
  exact Œ¥_gt.le

/-- The topology of the L√©vy-Prokhorov metric on probability measures on a separable space
coincides with the topology of convergence in distribution. -/
theorem levyProkhorov_eq_convergenceInDistribution :
    (inferInstance : TopologicalSpace (ProbabilityMeasure Œ©))
      = TopologicalSpace.coinduced (LevyProkhorov.toProbabilityMeasure (Œ© := Œ©)) inferInstance :=
  le_antisymm (ProbabilityMeasure.continuous_toLevyProkhorov (Œ© := Œ©)).coinduced_le
              levyProkhorov_le_convergenceInDistribution

/-- The identity map is a homeomorphism from `ProbabilityMeasure Œ©` with the topology of
convergence in distribution to `ProbabilityMeasure Œ©` with the L√©vy-Prokhorov (pseudo)metric. -/
def homeomorph_probabilityMeasure_levyProkhorov :
    ProbabilityMeasure Œ© ‚âÉ‚Çú LevyProkhorov (ProbabilityMeasure Œ©) where
  toFun := ProbabilityMeasure.toLevyProkhorov (Œ© := Œ©)
  invFun := LevyProkhorov.toProbabilityMeasure (Œ© := Œ©)
  left_inv := congrFun rfl
  right_inv := congrFun rfl
  continuous_toFun := ProbabilityMeasure.continuous_toLevyProkhorov
  continuous_invFun := LevyProkhorov.continuous_toProbabilityMeasure

/-- The topology of convergence in distribution on a separable space is pseudo-metrizable. -/
instance (X : Type*) [TopologicalSpace X] [PseudoMetrizableSpace X] [SeparableSpace X]
    [MeasurableSpace X] [OpensMeasurableSpace X] :
    PseudoMetrizableSpace (ProbabilityMeasure X) :=
  letI : PseudoMetricSpace X := TopologicalSpace.pseudoMetrizableSpacePseudoMetric X
  (homeomorph_probabilityMeasure_levyProkhorov (Œ© := X)).inducing.pseudoMetrizableSpace

end Levy_Prokhorov_metrizes_convergence_in_distribution

end MeasureTheory -- namespace
