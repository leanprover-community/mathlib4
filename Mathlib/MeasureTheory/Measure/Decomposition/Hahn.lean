/-
Copyright (c) 2019 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Loic Simon
-/
import Mathlib.MeasureTheory.Measure.Typeclasses.Finite

/-!
# Unsigned Hahn decomposition theorem

This file proves the unsigned version of the Hahn decomposition theorem.

## Main definitions

* `MeasureTheory.IsHahnDecomposition`: characterizes a set where `Œº ‚â§ ŒΩ` (and the
  reverse inequality on the complement),

## Main statements

* `hahn_decomposition` : Given two finite measures `Œº` and `ŒΩ`, there exists a measurable set `s`
  such that any measurable set `t` included in `s` satisfies `ŒΩ t ‚â§ Œº t`, and any
  measurable set `u` included in the complement of `s` satisfies `Œº u ‚â§ ŒΩ u`.
* `exists_isHahnDecomposition` : reformulation of `hahn_decomposition` using the
  `IsHahnDecomposition` structure which relies on the measure restriction.

## Tags

Hahn decomposition
-/

assert_not_exists MeasureTheory.Measure.rnDeriv
assert_not_exists MeasureTheory.VectorMeasure

open Set Filter Topology ENNReal

namespace MeasureTheory

variable {Œ± : Type*} {mŒ± : MeasurableSpace Œ±}

/-- **Hahn decomposition theorem** -/
theorem hahn_decomposition (Œº ŒΩ : Measure Œ±) [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] :
    ‚àÉ s, MeasurableSet s ‚àß (‚àÄ t, MeasurableSet t ‚Üí t ‚äÜ s ‚Üí ŒΩ t ‚â§ Œº t) ‚àß
      ‚àÄ t, MeasurableSet t ‚Üí t ‚äÜ s·∂ú ‚Üí Œº t ‚â§ ŒΩ t := by
  let d : Set Œ± ‚Üí ‚Ñù := fun s ‚Ü¶ ((Œº s).toNNReal : ‚Ñù) - (ŒΩ s).toNNReal
  let c : Set ‚Ñù := d '' { s | MeasurableSet s }
  let Œ≥ : ‚Ñù := sSup c
  have hŒº : ‚àÄ s, Œº s ‚â† ‚àû := measure_ne_top Œº
  have hŒΩ : ‚àÄ s, ŒΩ s ‚â† ‚àû := measure_ne_top ŒΩ
  have to_nnreal_Œº : ‚àÄ s, ((Œº s).toNNReal : ‚Ñù‚â•0‚àû) = Œº s := fun s ‚Ü¶ ENNReal.coe_toNNReal <| hŒº _
  have to_nnreal_ŒΩ : ‚àÄ s, ((ŒΩ s).toNNReal : ‚Ñù‚â•0‚àû) = ŒΩ s := fun s ‚Ü¶ ENNReal.coe_toNNReal <| hŒΩ _
  have d_split s t (ht : MeasurableSet t) : d s = d (s \ t) + d (s ‚à© t) := by
    dsimp only [d]
    rw [‚Üê measure_inter_add_diff s ht, ‚Üê measure_inter_add_diff s ht,
      ENNReal.toNNReal_add (hŒº _) (hŒº _), ENNReal.toNNReal_add (hŒΩ _) (hŒΩ _), NNReal.coe_add,
      NNReal.coe_add]
    simp only [sub_eq_add_neg, neg_add]
    abel
  have d_Union (s : ‚Ñï ‚Üí Set Œ±) (hm : Monotone s) :
    Tendsto (fun n ‚Ü¶ d (s n)) atTop (ùìù (d (‚ãÉ n, s n))) := by
    refine Tendsto.sub ?_ ?_ <;>
      refine NNReal.tendsto_coe.2 <| (ENNReal.tendsto_toNNReal ?_).comp <|
        tendsto_measure_iUnion_atTop hm
    ¬∑ exact hŒº _
    ¬∑ exact hŒΩ _
  have d_Inter (s : ‚Ñï ‚Üí Set Œ±) (hs : ‚àÄ n, MeasurableSet (s n)) (hm : ‚àÄ n m, n ‚â§ m ‚Üí s m ‚äÜ s n) :
        Tendsto (fun n ‚Ü¶ d (s n)) atTop (ùìù (d (‚ãÇ n, s n))) := by
    refine Tendsto.sub ?_ ?_ <;>
      refine NNReal.tendsto_coe.2 <| (ENNReal.tendsto_toNNReal <| ?_).comp <|
        tendsto_measure_iInter_atTop (fun n ‚Ü¶ (hs n).nullMeasurableSet) hm ?_
    exacts [hŒº _, ‚ü®0, hŒº _‚ü©, hŒΩ _, ‚ü®0, hŒΩ _‚ü©]
  have bdd_c : BddAbove c := by
    use (Œº univ).toNNReal
    rintro r ‚ü®s, _hs, rfl‚ü©
    refine le_trans (sub_le_self _ <| NNReal.coe_nonneg _) ?_
    rw [NNReal.coe_le_coe, ‚Üê ENNReal.coe_le_coe, to_nnreal_Œº, to_nnreal_Œº]
    exact measure_mono (subset_univ _)
  have c_nonempty : c.Nonempty := Nonempty.image _ ‚ü®_, MeasurableSet.empty‚ü©
  have d_le_Œ≥ : ‚àÄ s, MeasurableSet s ‚Üí d s ‚â§ Œ≥ := fun s hs ‚Ü¶ le_csSup bdd_c ‚ü®s, hs, rfl‚ü©
  have (n : ‚Ñï) : ‚àÉ s : Set Œ±, MeasurableSet s ‚àß Œ≥ - (1 / 2) ^ n < d s := by
    have : Œ≥ - (1 / 2) ^ n < Œ≥ := sub_lt_self Œ≥ (pow_pos (half_pos zero_lt_one) n)
    rcases exists_lt_of_lt_csSup c_nonempty this with ‚ü®r, ‚ü®s, hs, rfl‚ü©, hlt‚ü©
    exact ‚ü®s, hs, hlt‚ü©
  rcases Classical.axiom_of_choice this with ‚ü®e, he‚ü©
  change ‚Ñï ‚Üí Set Œ± at e
  have he‚ÇÅ : ‚àÄ n, MeasurableSet (e n) := fun n ‚Ü¶ (he n).1
  have he‚ÇÇ : ‚àÄ n, Œ≥ - (1 / 2) ^ n < d (e n) := fun n ‚Ü¶ (he n).2
  let f : ‚Ñï ‚Üí ‚Ñï ‚Üí Set Œ± := fun n m ‚Ü¶ (Finset.Ico n (m + 1)).inf e
  have hf n m : MeasurableSet (f n m) := by
    simp only [f, Finset.inf_eq_iInf]
    exact MeasurableSet.biInter (to_countable _) fun i _ ‚Ü¶ he‚ÇÅ _
  have f_subset_f {a b c d} (hab : a ‚â§ b) (hcd : c ‚â§ d) : f a d ‚äÜ f b c := by
    simp_rw [f, Finset.inf_eq_iInf]
    exact biInter_subset_biInter_left (Finset.Ico_subset_Ico hab <| Nat.succ_le_succ hcd)
  have f_succ n m (hnm : n ‚â§ m) : f n (m + 1) = f n m ‚à© e (m + 1) := by
    have : n ‚â§ m + 1 := le_of_lt (Nat.succ_le_succ hnm)
    simp_rw [f, ‚Üê Finset.insert_Ico_right_eq_Ico_add_one this, Finset.inf_insert,
      Set.inter_comm]
    rfl
  have le_d_f n m (h : m ‚â§ n) : Œ≥ - 2 * (1 / 2) ^ m + (1 / 2) ^ n ‚â§ d (f m n) := by
    refine Nat.le_induction ?_ ?_ n h
    ¬∑ have := he‚ÇÇ m
      simp_rw [f, Nat.Ico_succ_singleton, Finset.inf_singleton]
      linarith
    ¬∑ intro n (hmn : m ‚â§ n) ih
      have : Œ≥ + (Œ≥ - 2 * (1 / 2) ^ m + (1 / 2) ^ (n + 1)) ‚â§ Œ≥ + d (f m (n + 1)) := by
        calc
          Œ≥ + (Œ≥ - 2 * (1 / 2) ^ m + (1 / 2) ^ (n + 1)) =
              Œ≥ + (Œ≥ - 2 * (1 / 2) ^ m + ((1 / 2) ^ n - (1 / 2) ^ (n + 1))) := by
            rw [pow_succ, mul_one_div, _root_.sub_half]
          _ = Œ≥ - (1 / 2) ^ (n + 1) + (Œ≥ - 2 * (1 / 2) ^ m + (1 / 2) ^ n) := by
            simp only [sub_eq_add_neg]; abel
          _ ‚â§ d (e (n + 1)) + d (f m n) := add_le_add (le_of_lt <| he‚ÇÇ _) ih
          _ ‚â§ d (e (n + 1)) + d (f m n \ e (n + 1)) + d (f m (n + 1)) := by
            rw [f_succ _ _ hmn, d_split (f m n) (e (n + 1)) (he‚ÇÅ _), add_assoc]
          _ = d (e (n + 1) ‚à™ f m n) + d (f m (n + 1)) := by
            rw [d_split (e (n + 1) ‚à™ f m n) (e (n + 1)), union_diff_left, union_inter_cancel_left]
            ¬∑ abel
            ¬∑ exact he‚ÇÅ _
          _ ‚â§ Œ≥ + d (f m (n + 1)) := add_le_add_right (d_le_Œ≥ _ <| (he‚ÇÅ _).union (hf _ _)) _
      exact (add_le_add_iff_left Œ≥).1 this
  let s := ‚ãÉ m, ‚ãÇ n, f m n
  have Œ≥_le_d_s : Œ≥ ‚â§ d s := by
    have hŒ≥ : Tendsto (fun m : ‚Ñï ‚Ü¶ Œ≥ - 2 * (1 / 2) ^ m) atTop (ùìù Œ≥) := by
      suffices Tendsto (fun m : ‚Ñï ‚Ü¶ Œ≥ - 2 * (1 / 2) ^ m) atTop (ùìù (Œ≥ - 2 * 0)) by
        simpa only [mul_zero, tsub_zero]
      exact
        tendsto_const_nhds.sub <|
          tendsto_const_nhds.mul <|
            tendsto_pow_atTop_nhds_zero_of_lt_one (le_of_lt <| half_pos <| zero_lt_one)
              (half_lt_self zero_lt_one)
    have hd : Tendsto (fun m ‚Ü¶ d (‚ãÇ n, f m n)) atTop (ùìù (d (‚ãÉ m, ‚ãÇ n, f m n))) := by
      refine d_Union _ ?_
      exact fun n m hnm ‚Ü¶
        subset_iInter fun i ‚Ü¶ Subset.trans (iInter_subset (f n) i) <| f_subset_f hnm <| le_rfl
    refine le_of_tendsto_of_tendsto' hŒ≥ hd fun m ‚Ü¶ ?_
    have : Tendsto (fun n ‚Ü¶ d (f m n)) atTop (ùìù (d (‚ãÇ n, f m n))) := by
      refine d_Inter _ ?_ ?_
      ¬∑ intro n
        exact hf _ _
      ¬∑ intro n m hnm
        exact f_subset_f le_rfl hnm
    refine ge_of_tendsto this (eventually_atTop.2 ‚ü®m, fun n hmn ‚Ü¶ ?_‚ü©)
    change Œ≥ - 2 * (1 / 2) ^ m ‚â§ d (f m n)
    refine le_trans ?_ (le_d_f _ _ hmn)
    exact le_add_of_le_of_nonneg le_rfl (pow_nonneg (le_of_lt <| half_pos <| zero_lt_one) _)
  have hs : MeasurableSet s := MeasurableSet.iUnion fun n ‚Ü¶ MeasurableSet.iInter fun m ‚Ü¶ hf _ _
  refine ‚ü®s, hs, ?_, ?_‚ü©
  ¬∑ intro t ht hts
    have : 0 ‚â§ d t :=
      (add_le_add_iff_left Œ≥).1 <|
        calc
          Œ≥ + 0 ‚â§ d s := by rw [add_zero]; exact Œ≥_le_d_s
          _ = d (s \ t) + d t := by rw [d_split s _ ht, inter_eq_self_of_subset_right hts]
          _ ‚â§ Œ≥ + d t := add_le_add (d_le_Œ≥ _ (hs.diff ht)) le_rfl
    rw [‚Üê to_nnreal_Œº, ‚Üê to_nnreal_ŒΩ, ENNReal.coe_le_coe, ‚Üê NNReal.coe_le_coe]
    simpa only [d, le_sub_iff_add_le, zero_add] using this
  ¬∑ intro t ht hts
    have : d t ‚â§ 0 :=
      (add_le_add_iff_left Œ≥).1 <|
        calc
          Œ≥ + d t ‚â§ d s + d t := by gcongr
          _ = d (s ‚à™ t) := by
            rw [d_split (s ‚à™ t) _ ht, union_diff_right, union_inter_cancel_right,
              (subset_compl_iff_disjoint_left.1 hts).sdiff_eq_left]
          _ ‚â§ Œ≥ + 0 := by rw [add_zero]; exact d_le_Œ≥ _ (hs.union ht)
    rw [‚Üê to_nnreal_Œº, ‚Üê to_nnreal_ŒΩ, ENNReal.coe_le_coe, ‚Üê NNReal.coe_le_coe]
    simpa only [d, sub_le_iff_le_add, zero_add] using this


/-- A set where `Œº ‚â§ ŒΩ` (and the reverse inequality on the complement),
defined via measurable set and measure restriction comparisons. -/
structure IsHahnDecomposition (Œº ŒΩ : Measure Œ±) (s : Set Œ±) : Prop where
  measurableSet : MeasurableSet s
  le_on : Œº.restrict s ‚â§ ŒΩ.restrict s
  ge_on_compl : ŒΩ.restrict s·∂ú ‚â§ Œº.restrict s·∂ú

lemma IsHahnDecomposition.compl {Œº ŒΩ : Measure Œ±} {s : Set Œ±}
    (h : IsHahnDecomposition Œº ŒΩ s) : IsHahnDecomposition ŒΩ Œº s·∂ú where
  measurableSet := h.measurableSet.compl
  le_on := h.ge_on_compl
  ge_on_compl := by simpa using h.le_on

lemma exists_isHahnDecomposition (Œº ŒΩ : Measure Œ±) [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ] :
    ‚àÉ s : Set Œ±, IsHahnDecomposition Œº ŒΩ s := by
  obtain ‚ü®s, hs, h‚ÇÅ, h‚ÇÇ‚ü© := hahn_decomposition ŒΩ Œº
  refine ‚ü®s, hs, ?_, ?_‚ü©
  all_goals
    rw [Measure.le_iff]
    intros
    simp [*]

end MeasureTheory
