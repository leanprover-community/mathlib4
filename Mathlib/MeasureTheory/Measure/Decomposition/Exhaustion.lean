/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.Typeclasses.SFinite

/-!
# Method of exhaustion

If `Î¼, Î½` are two measures with `Î½` s-finite, then there exists a set `s` such that
`Î¼` is sigma-finite on `s`, and for all sets `t âŠ† sá¶œ`, either `Î½ t = 0` or `Î¼ t = âˆ`.

## Main definitions

* `MeasureTheory.Measure.sigmaFiniteSetWRT`: if such a set exists, `Î¼.sigmaFiniteSetWRT Î½` is
  a measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)` is sigma-finite and
  for all sets `t âŠ† (Î¼.sigmaFiniteSetWRT Î½)á¶œ`, either `Î½ t = 0` or `Î¼ t = âˆ`.
  If no such set exists (which is only possible if `Î½` is not s-finite), we define
  `Î¼.sigmaFiniteSetWRT Î½ = âˆ…`.
* `MeasureTheory.Measure.sigmaFiniteSet`: for an s-finite measure `Î¼`, a measurable set such that
  `Î¼.restrict Î¼.sigmaFiniteSet` is sigma-finite, and for all sets `s âŠ† Î¼.sigmaFiniteSetá¶œ`,
  either `Î¼ s = 0` or `Î¼ s = âˆ`.
  Defined as `Î¼.sigmaFiniteSetWRT Î¼`.

## Main statements

* `measure_eq_top_of_subset_compl_sigmaFiniteSetWRT`: for s-finite `Î½`, for all sets `s`
  in `(sigmaFiniteSetWRT Î¼ Î½)á¶œ`, if `Î½ s â‰  0` then `Î¼ s = âˆ`.
* An instance showing that `Î¼.restrict (sigmaFiniteSetWRT Î¼ Î½)` is sigma-finite.
* `restrict_compl_sigmaFiniteSetWRT`: if `Î¼ â‰ª Î½` and `Î½` is s-finite, then
  `Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)á¶œ = âˆ â€¢ Î½.restrict (Î¼.sigmaFiniteSetWRT Î½)á¶œ`. As a consequence,
  that restriction is s-finite.

* An instance showing that `Î¼.restrict Î¼.sigmaFiniteSet` is sigma-finite.
* `restrict_compl_sigmaFiniteSet_eq_zero_or_top`: the measure `Î¼.restrict Î¼.sigmaFiniteSetá¶œ` takes
  only two values: 0 and âˆ .
* `measure_compl_sigmaFiniteSet_eq_zero_iff_sigmaFinite`: a measure `Î¼` is sigma-finite
  iff `Î¼ Î¼.sigmaFiniteSetá¶œ = 0`.

## References

* [P. R. Halmos, *Measure theory*, 17.3 and 30.11][halmos1950measure]

-/

assert_not_exists MeasureTheory.Measure.rnDeriv
assert_not_exists MeasureTheory.VectorMeasure

open scoped ENNReal Topology

open Filter

namespace MeasureTheory

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} {s t : Set Î±}

open Classical in
/-- A measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)` is sigma-finite and for all
measurable sets `t âŠ† sá¶œ`, either `Î½ t = 0` or `Î¼ t = âˆ`. -/
def Measure.sigmaFiniteSetWRT (Î¼ Î½ : Measure Î±) : Set Î± :=
  if h : âˆƒ s : Set Î±, MeasurableSet s âˆ§ SigmaFinite (Î¼.restrict s)
    âˆ§ (âˆ€ t, t âŠ† sá¶œ â†’ Î½ t â‰  0 â†’ Î¼ t = âˆ)
  then h.choose
  else âˆ…

@[measurability]
lemma measurableSet_sigmaFiniteSetWRT :
    MeasurableSet (Î¼.sigmaFiniteSetWRT Î½) := by
  rw [Measure.sigmaFiniteSetWRT]
  split_ifs with h
  Â· exact h.choose_spec.1
  Â· exact MeasurableSet.empty

instance : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)) := by
  rw [Measure.sigmaFiniteSetWRT]
  split_ifs with h
  Â· exact h.choose_spec.2.1
  Â· rw [Measure.restrict_empty]
    infer_instance

section IsFiniteMeasure

/-! We prove that the condition in the definition of `sigmaFiniteSetWRT` is true for finite
measures. Since every s-finite measure is absolutely continuous with respect to a finite measure,
the condition will then also be true for s-finite measures. -/

/-- Let `C` be the supremum of `Î½ s` over all measurable sets `s` such that `Î¼.restrict s` is
sigma-finite. `C` is finite since `Î½` is a finite measure. Then there exists a measurable set `t`
with `Î¼.restrict t` sigma-finite such that `Î½ t â‰¥ C - 1/n`. -/
lemma exists_isSigmaFiniteSet_measure_ge (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    âˆƒ t, MeasurableSet t âˆ§ SigmaFinite (Î¼.restrict t)
      âˆ§ (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s) - 1/n â‰¤ Î½ t := by
  by_cases hC_lt : 1/n < â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s
  Â· have h_lt_top : â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s < âˆ := by
      refine (?_ : â¨† (s) (_ : MeasurableSet s)
        (_ : SigmaFinite (Î¼.restrict s)), Î½ s â‰¤ Î½ Set.univ).trans_lt (measure_lt_top _ _)
      refine iSup_le (fun s â†¦ ?_)
      exact iSup_le (fun _ â†¦ iSup_le (fun _ â†¦ measure_mono (Set.subset_univ s)))
    obtain âŸ¨t, htâŸ© := exists_lt_of_lt_ciSup
      (ENNReal.sub_lt_self h_lt_top.ne (ne_zero_of_lt hC_lt) (by simp) :
          (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s) - 1/n
        < â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s)
    have ht_meas : MeasurableSet t := by
      by_contra h_notMem
      simp only [h_notMem] at ht
      simp at ht
    have ht_mem : SigmaFinite (Î¼.restrict t) := by
      by_contra h_notMem
      simp only [h_notMem] at ht
      simp at ht
    refine âŸ¨t, ht_meas, ht_mem, ?_âŸ©
    simp only [ht_meas, ht_mem, iSup_true] at ht
    exact ht.le
  Â· refine âŸ¨âˆ…, MeasurableSet.empty, by rw [Measure.restrict_empty]; infer_instance, ?_âŸ©
    rw [tsub_eq_zero_of_le (not_lt.mp hC_lt)]
    exact zero_le'

/-- A measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)` is sigma-finite and
for `C` the supremum of `Î½ s` over all measurable sets `s` with `Î¼.restrict s` sigma-finite,
`Î½ (Î¼.sigmaFiniteSetGE Î½ n) â‰¥ C - 1/n`. -/
def Measure.sigmaFiniteSetGE (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) : Set Î± :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose

lemma measurableSet_sigmaFiniteSetGE [IsFiniteMeasure Î½] (n : â„•) :
    MeasurableSet (Î¼.sigmaFiniteSetGE Î½ n) :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose_spec.1

lemma sigmaFinite_restrict_sigmaFiniteSetGE (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)) :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose_spec.2.1

lemma measure_sigmaFiniteSetGE_le (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    Î½ (Î¼.sigmaFiniteSetGE Î½ n)
      â‰¤ â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s := by
  refine (le_iSup (f := fun s â†¦ _)
    (sigmaFinite_restrict_sigmaFiniteSetGE Î¼ Î½ n)).trans ?_
  exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : SigmaFinite (Î¼.restrict s)), Î½ s) (Î¼.sigmaFiniteSetGE Î½ n)
    (measurableSet_sigmaFiniteSetGE n)

lemma measure_sigmaFiniteSetGE_ge (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] (n : â„•) :
    (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s) - 1/n
      â‰¤ Î½ (Î¼.sigmaFiniteSetGE Î½ n) :=
  (exists_isSigmaFiniteSet_measure_ge Î¼ Î½ n).choose_spec.2.2

lemma tendsto_measure_sigmaFiniteSetGE (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] :
    Tendsto (fun n â†¦ Î½ (Î¼.sigmaFiniteSetGE Î½ n)) atTop
      (ğ“ (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s)) := by
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le ?_
    tendsto_const_nhds (measure_sigmaFiniteSetGE_ge Î¼ Î½) (measure_sigmaFiniteSetGE_le Î¼ Î½)
  nth_rewrite 2 [â† tsub_zero (â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s)]
  refine ENNReal.Tendsto.sub tendsto_const_nhds ?_ (Or.inr ENNReal.zero_ne_top)
  simp only [one_div]
  exact ENNReal.tendsto_inv_nat_nhds_zero

/-- A measurable set such that `Î¼.restrict (Î¼.sigmaFiniteSetWRT' Î½)` is sigma-finite and
`Î½ (Î¼.sigmaFiniteSetWRT' Î½)` has maximal measure among such sets. -/
def Measure.sigmaFiniteSetWRT' (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] : Set Î± :=
  â‹ƒ n, Î¼.sigmaFiniteSetGE Î½ n

lemma measurableSet_sigmaFiniteSetWRT' [IsFiniteMeasure Î½] :
    MeasurableSet (Î¼.sigmaFiniteSetWRT' Î½) :=
  MeasurableSet.iUnion measurableSet_sigmaFiniteSetGE

lemma sigmaFinite_restrict_sigmaFiniteSetWRT' (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] :
    SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetWRT' Î½)) := by
  have := sigmaFinite_restrict_sigmaFiniteSetGE Î¼ Î½
  let f : â„• Ã— â„• â†’ Set Î± := fun p : â„• Ã— â„• â†¦ (Î¼.sigmaFiniteSetWRT' Î½)á¶œ
    âˆª (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ p.1)) p.2 âˆ© (Î¼.sigmaFiniteSetGE Î½ p.1))
  suffices (Î¼.restrict (Î¼.sigmaFiniteSetWRT' Î½)).FiniteSpanningSetsIn (Set.range f) from
    this.sigmaFinite
  let e : â„• â‰ƒ â„• Ã— â„• := Nat.pairEquiv.symm
  refine âŸ¨fun n â†¦ f (e n), fun _ â†¦ by simp, fun n â†¦ ?_, ?_âŸ©
  Â· simp only [Nat.pairEquiv_symm_apply, measure_union_lt_top_iff, f, e]
    rw [Measure.restrict_apply' measurableSet_sigmaFiniteSetWRT', Set.compl_inter_self,
      Measure.restrict_apply' measurableSet_sigmaFiniteSetWRT']
    simp only [measure_empty, ENNReal.zero_lt_top, true_and]
    refine (measure_mono Set.inter_subset_left).trans_lt ?_
    rw [â† Measure.restrict_apply' (measurableSet_sigmaFiniteSetGE _)]
    exact measure_spanningSets_lt_top _ _
  Â· simp only [Nat.pairEquiv_symm_apply, f, e]
    rw [â† Set.union_iUnion]
    suffices â‹ƒ n, (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ (Nat.unpair n).1)) n.unpair.2
        âˆ© Î¼.sigmaFiniteSetGE Î½ n.unpair.1) = Î¼.sigmaFiniteSetWRT' Î½ by
      rw [this, Set.compl_union_self]
    calc â‹ƒ n, (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ (Nat.unpair n).1)) n.unpair.2
        âˆ© Î¼.sigmaFiniteSetGE Î½ n.unpair.1)
      = â‹ƒ n, â‹ƒ m, (spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)) m
            âˆ© Î¼.sigmaFiniteSetGE Î½ n) :=
          Set.iUnion_unpair (fun n m â†¦ spanningSets (Î¼.restrict (Î¼.sigmaFiniteSetGE Î½ n)) m
            âˆ© Î¼.sigmaFiniteSetGE Î½ n)
    _ = â‹ƒ n, Î¼.sigmaFiniteSetGE Î½ n := by
        refine Set.iUnion_congr (fun n â†¦ ?_)
        rw [â† Set.iUnion_inter, iUnion_spanningSets, Set.univ_inter]
    _ = Î¼.sigmaFiniteSetWRT' Î½ := rfl

/-- `Î¼.sigmaFiniteSetWRT' Î½` has maximal `Î½`-measure among all measurable sets `s` with sigma-finite
`Î¼.restrict s`. -/
lemma measure_sigmaFiniteSetWRT' (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î½] :
    Î½ (Î¼.sigmaFiniteSetWRT' Î½)
      = â¨† (s) (_ : MeasurableSet s) (_ : SigmaFinite (Î¼.restrict s)), Î½ s := by
  apply le_antisymm
  Â· refine (le_iSup (f := fun _ â†¦ _)
      (sigmaFinite_restrict_sigmaFiniteSetWRT' Î¼ Î½)).trans ?_
    exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : SigmaFinite (Î¼.restrict s)), Î½ s) (Î¼.sigmaFiniteSetWRT' Î½)
      measurableSet_sigmaFiniteSetWRT'
  Â· exact le_of_tendsto' (tendsto_measure_sigmaFiniteSetGE Î¼ Î½)
      (fun _ â†¦ measure_mono (Set.subset_iUnion _ _))

/-- Auxiliary lemma for `measure_eq_top_of_subset_compl_sigmaFiniteSetWRT'`. -/
lemma measure_eq_top_of_subset_compl_sigmaFiniteSetWRT'_of_measurableSet [IsFiniteMeasure Î½]
    (hs : MeasurableSet s) (hs_subset : s âŠ† (Î¼.sigmaFiniteSetWRT' Î½)á¶œ) (hÎ½s : Î½ s â‰  0) :
    Î¼ s = âˆ := by
  suffices Â¬ SigmaFinite (Î¼.restrict s) by
    by_contra h
    have h_lt_top : Fact (Î¼ s < âˆ) := âŸ¨Ne.lt_top hâŸ©
    exact this inferInstance
  intro hsÏƒ
  have h_lt : Î½ (Î¼.sigmaFiniteSetWRT' Î½) < Î½ (Î¼.sigmaFiniteSetWRT' Î½ âˆª s) := by
    rw [measure_union _ hs]
    Â· exact ENNReal.lt_add_right (measure_ne_top _ _) hÎ½s
    Â· exact disjoint_compl_right.mono_right hs_subset
  have h_le : Î½ (Î¼.sigmaFiniteSetWRT' Î½ âˆª s) â‰¤ Î½ (Î¼.sigmaFiniteSetWRT' Î½) := by
    conv_rhs => rw [measure_sigmaFiniteSetWRT']
    refine (le_iSup
      (f := fun (_ : SigmaFinite (Î¼.restrict (Î¼.sigmaFiniteSetWRT' Î½ âˆª s))) â†¦ _) ?_).trans ?_
    Â· have := sigmaFinite_restrict_sigmaFiniteSetWRT' Î¼ Î½
      infer_instance
    Â· exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : SigmaFinite (Î¼.restrict _)), Î½ s)
        (Î¼.sigmaFiniteSetWRT' Î½ âˆª s) (measurableSet_sigmaFiniteSetWRT'.union hs)
  exact h_lt.not_ge h_le

/-- For all sets `s` in `(Î¼.sigmaFiniteSetWRT Î½)á¶œ`, if `Î½ s â‰  0` then `Î¼ s = âˆ`. -/
lemma measure_eq_top_of_subset_compl_sigmaFiniteSetWRT' [IsFiniteMeasure Î½]
    (hs_subset : s âŠ† (Î¼.sigmaFiniteSetWRT' Î½)á¶œ) (hÎ½s : Î½ s â‰  0) :
    Î¼ s = âˆ := by
  rw [measure_eq_iInf]
  simp_rw [iInf_eq_top]
  suffices âˆ€ t, t âŠ† (Î¼.sigmaFiniteSetWRT' Î½)á¶œ â†’ s âŠ† t â†’ MeasurableSet t â†’ Î¼ t = âˆ by
    intro t hts ht
    suffices Î¼ (t âˆ© (Î¼.sigmaFiniteSetWRT' Î½)á¶œ) = âˆ from
      measure_mono_top Set.inter_subset_left this
    have hs_subset_t : s âŠ† t âˆ© (Î¼.sigmaFiniteSetWRT' Î½)á¶œ := Set.subset_inter hts hs_subset
    exact this (t âˆ© (Î¼.sigmaFiniteSetWRT' Î½)á¶œ) Set.inter_subset_right hs_subset_t
      (ht.inter measurableSet_sigmaFiniteSetWRT'.compl)
  intro t ht_subset hst ht
  refine measure_eq_top_of_subset_compl_sigmaFiniteSetWRT'_of_measurableSet ht ht_subset ?_
  exact fun hÎ½t â†¦ hÎ½s (measure_mono_null hst hÎ½t)

end IsFiniteMeasure

section SFinite

/-- For all sets `s` in `(Î¼.sigmaFiniteSetWRT Î½)á¶œ`, if `Î½ s â‰  0` then `Î¼ s = âˆ`. -/
lemma measure_eq_top_of_subset_compl_sigmaFiniteSetWRT [SFinite Î½]
    (hs_subset : s âŠ† (Î¼.sigmaFiniteSetWRT Î½)á¶œ) (hÎ½s : Î½ s â‰  0) :
    Î¼ s = âˆ := by
  have âŸ¨Î½', hÎ½', hÎ½Î½', _âŸ© := exists_isFiniteMeasure_absolutelyContinuous Î½
  have h : âˆƒ s : Set Î±, MeasurableSet s âˆ§ SigmaFinite (Î¼.restrict s)
      âˆ§ (âˆ€ t âŠ† sá¶œ, Î½ t â‰  0 â†’ Î¼ t = âˆ) := by
    refine âŸ¨Î¼.sigmaFiniteSetWRT' Î½', measurableSet_sigmaFiniteSetWRT',
      sigmaFinite_restrict_sigmaFiniteSetWRT' _ _,
      fun t ht_subset hÎ½t â†¦ measure_eq_top_of_subset_compl_sigmaFiniteSetWRT' ht_subset ?_âŸ©
    exact fun hÎ½'t â†¦ hÎ½t (hÎ½Î½' hÎ½'t)
  rw [Measure.sigmaFiniteSetWRT, dif_pos h] at hs_subset
  exact h.choose_spec.2.2 s hs_subset hÎ½s

lemma restrict_compl_sigmaFiniteSetWRT [SFinite Î½] (hÎ¼Î½ : Î¼ â‰ª Î½) :
    Î¼.restrict (Î¼.sigmaFiniteSetWRT Î½)á¶œ = âˆ â€¢ Î½.restrict (Î¼.sigmaFiniteSetWRT Î½)á¶œ := by
  ext s
  rw [Measure.restrict_apply' measurableSet_sigmaFiniteSetWRT.compl,
    Measure.smul_apply, smul_eq_mul,
    Measure.restrict_apply' measurableSet_sigmaFiniteSetWRT.compl]
  by_cases hÎ½s : Î½ (s âˆ© (Î¼.sigmaFiniteSetWRT Î½)á¶œ) = 0
  Â· rw [hÎ½s, mul_zero]
    exact hÎ¼Î½ hÎ½s
  Â· rw [ENNReal.top_mul hÎ½s, measure_eq_top_of_subset_compl_sigmaFiniteSetWRT
      Set.inter_subset_right hÎ½s]

end SFinite

@[simp]
lemma measure_compl_sigmaFiniteSetWRT (hÎ¼Î½ : Î¼ â‰ª Î½) [SigmaFinite Î¼] [SFinite Î½] :
    Î½ (Î¼.sigmaFiniteSetWRT Î½)á¶œ = 0 := by
  have h : Î½ (Î¼.sigmaFiniteSetWRT Î½)á¶œ â‰  0 â†’ Î¼ (Î¼.sigmaFiniteSetWRT Î½)á¶œ = âˆ :=
    measure_eq_top_of_subset_compl_sigmaFiniteSetWRT subset_rfl
  by_contra h0
  refine ENNReal.top_ne_zero ?_
  rw [â† h h0, â† Measure.iSup_restrict_spanningSets]
  simp_rw [Measure.restrict_apply' (measurableSet_spanningSets Î¼ _), ENNReal.iSup_eq_zero]
  intro i
  by_contra h_ne_zero
  have h_zero_top := measure_eq_top_of_subset_compl_sigmaFiniteSetWRT
    (Set.inter_subset_left : (Î¼.sigmaFiniteSetWRT Î½)á¶œ âˆ© spanningSets Î¼ i âŠ† _) ?_
  swap; Â· exact fun h â†¦ h_ne_zero (hÎ¼Î½ h)
  refine absurd h_zero_top (ne_of_lt ?_)
  exact (measure_mono Set.inter_subset_right).trans_lt (measure_spanningSets_lt_top Î¼ i)

section SigmaFiniteSet

/-- A measurable set such that `Î¼.restrict Î¼.sigmaFiniteSet` is sigma-finite,
  and for all measurable sets `s âŠ† Î¼.sigmaFiniteSetá¶œ`, either `Î¼ s = 0` or `Î¼ s = âˆ`. -/
def Measure.sigmaFiniteSet (Î¼ : Measure Î±) : Set Î± := Î¼.sigmaFiniteSetWRT Î¼

@[measurability]
lemma measurableSet_sigmaFiniteSet : MeasurableSet Î¼.sigmaFiniteSet :=
  measurableSet_sigmaFiniteSetWRT

lemma measure_eq_zero_or_top_of_subset_compl_sigmaFiniteSet [SFinite Î¼]
    (ht_subset : t âŠ† Î¼.sigmaFiniteSetá¶œ) :
    Î¼ t = 0 âˆ¨ Î¼ t = âˆ := by
  rw [or_iff_not_imp_left]
  exact measure_eq_top_of_subset_compl_sigmaFiniteSetWRT ht_subset

/-- The measure `Î¼.restrict Î¼.sigmaFiniteSetá¶œ` takes only two values: 0 and âˆ . -/
lemma restrict_compl_sigmaFiniteSet_eq_zero_or_top (Î¼ : Measure Î±) [SFinite Î¼] (s : Set Î±) :
    Î¼.restrict Î¼.sigmaFiniteSetá¶œ s = 0 âˆ¨ Î¼.restrict Î¼.sigmaFiniteSetá¶œ s = âˆ := by
  rw [Measure.restrict_apply' measurableSet_sigmaFiniteSet.compl]
  exact measure_eq_zero_or_top_of_subset_compl_sigmaFiniteSet Set.inter_subset_right

/-- The restriction of an s-finite measure `Î¼` to `Î¼.sigmaFiniteSet` is sigma-finite. -/
instance : SigmaFinite (Î¼.restrict Î¼.sigmaFiniteSet) := by
  rw [Measure.sigmaFiniteSet]
  infer_instance

lemma sigmaFinite_of_measure_compl_sigmaFiniteSet_eq_zero (h : Î¼ Î¼.sigmaFiniteSetá¶œ = 0) :
    SigmaFinite Î¼ := by
  rw [â† Measure.restrict_add_restrict_compl (Î¼ := Î¼) (measurableSet_sigmaFiniteSet (Î¼ := Î¼)),
    Measure.restrict_eq_zero.mpr h, add_zero]
  infer_instance

@[simp]
lemma measure_compl_sigmaFiniteSet (Î¼ : Measure Î±) [SigmaFinite Î¼] : Î¼ Î¼.sigmaFiniteSetá¶œ = 0 :=
  measure_compl_sigmaFiniteSetWRT Measure.AbsolutelyContinuous.rfl

/-- An s-finite measure `Î¼` is sigma-finite iff `Î¼ Î¼.sigmaFiniteSetá¶œ = 0`. -/
lemma measure_compl_sigmaFiniteSet_eq_zero_iff_sigmaFinite (Î¼ : Measure Î±) :
    Î¼ Î¼.sigmaFiniteSetá¶œ = 0 â†” SigmaFinite Î¼ :=
  âŸ¨sigmaFinite_of_measure_compl_sigmaFiniteSet_eq_zero, fun _ â†¦ measure_compl_sigmaFiniteSet Î¼âŸ©

end SigmaFiniteSet

end MeasureTheory
