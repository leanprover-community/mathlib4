/-
Copyright (c) 2025 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.MeasureTheory.Measure.MeasureSpace
import Mathlib.MeasureTheory.Measure.OpenPos
import Mathlib.MeasureTheory.Measure.MeasureSpaceDef
import Mathlib.Topology.Defs.Filter
import Mathlib.Order.Filter.SmallSets

/-!
# Support of a Measure

This file develops the theory of the **topological support** of a measure `Î¼` on a
topological measurable space. The support is defined as the set of points whose every open
neighborhood has positive measure. We give equivalent characterizations, prove basic
measure-theoretic properties, and study interactions with sums, restrictions, and
absolute continuity. Under various LindelÃ¶f conditions, the support is conull,
and various descriptions of the complement of the support are provided.

## Main definitions

* `Measure.support` : the support of a measure `Î¼`, defined as
  `{x | âˆƒá¶  u in (ğ“ x).smallSets, 0 < Î¼ u}` â€” equivalently, every neighborhood of `x`
  has positive `Î¼`-measure.

## Main results

* `mem_support_iff` and `mem_support_iff_forall` : filter-theoretic and
  neighborhood characterizations of membership in the support.
* `notMem_support_iff` and `notMem_support_iff_exists` : characterizations of lying
  outside the support.
* `support_eq_univ` : if `IsOpenPosMeasure Î¼`, then the support of `Î¼` is the whole space.
* `support_zero` : the support of the zero measure is empty.
* `support_mono` and `AbsolutelyContinuous.support_mono` : monotonicity of support under
  measure domination and absolute continuity.
* `support_add` : the support of the sum of two measures is the union of their supports.
* `support_eq_forall_isOpen` : description of the support via open sets.
* `compl_support_eq_sUnion` and `support_eq_sInter` : the complement of the support is the
  union of open measure-zero sets, and the support is the intersection of closed sets whose
  complements have measure zero.
* `isClosed_support` / `isOpen_compl_support` : topological closure properties of the support.
* `support_mem_ae_of_isLindelof` and `support_mem_ae` : under LindelÃ¶f (or hereditarily
  LindelÃ¶f) hypotheses, the support is conull.
* `measure_compl_support` and its corollaries (`nullMeasurableSet_support`, `measure_support`,
  `nonempty_support`, `nonempty_support_iff`) : measure-theoretic consequences, including that
  the complement has measure zero and nonemptiness criteria.
* `support_restrict_subset_closure`, `mem_support_restrict`, and
  `interior_inter_support` : interaction of support with restriction to a set and the
  relation between `interior s âˆ© Î¼.support` and the support of `Î¼.restrict s`.

## Notation

* `Î¼.support` : the support of measure `Î¼`.
* `(ğ“ x).smallSets` : the frequently-small-sets filter used in the filter-theoretic definition.

## Implementation notes

Have to go through and resolve some of these, and remove the associated bullets!

* TO DO: Rename `MeasureTheory.measure_mono_null` as `MeasureTheory.Measure.mono_null`
  to enable dot notation. (In a separate PR?)
* The lemma `support_restrict_subset_closure_inter_support` is currently a placeholder and
  explicitly marked for replacement: it should be proved directly without relying on the
  existing commented strategy.
* The file mixes several related but conceptually separate themes (`Add`, `Restrict`,
  `AbsolutelyContinuous`); consider reorganizing so that core support theory is grouped, with
  extensions in well-delineated subsections or submodules.
* Some proofs contain â€œgolfâ€ comments or ad hoc constructionsâ€”adding focused docstrings and
  cleaning those proofs (and their invariants) will improve maintainability.

## Tags

measure theory, support, topological support, filter, LindelÃ¶f, hereditarily LindelÃ¶f,
absolute continuity, restriction, sum of measures, null measurable, conull
-/

section Support

namespace MeasureTheory

namespace Measure

open scoped Topology

variable {X : Type*} [TopologicalSpace X] [MeasurableSpace X]

/-- A point `x` is in the support of `Î¼` if any open neighborhood of `x` has positive measure.
We provide the definition in terms of the filter-theoretic equivalent
`âˆƒá¶  u in (ğ“ x).smallSets, 0 < Î¼ u`. -/
protected def support (Î¼ : Measure X) : Set X := {x : X | âˆƒá¶  u in (ğ“ x).smallSets, 0 < Î¼ u}

variable {Î¼ : Measure X}

lemma pos_mono {Î± : Type*} [MeasurableSpace Î±]
    (Î¼ : Measure Î±) â¦ƒs t : Set Î±â¦„ (h : s âŠ† t) (hs : 0 < Î¼ s) :
    0 < Î¼ t :=
  hs.trans_le <| Î¼.mono h

theorem _root_.Filter.HasBasis.mem_measureSupport {Î¹ : Sort*} {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set X} {x : X} (hl : (ğ“ x).HasBasis p s) :
    x âˆˆ Î¼.support â†” âˆ€ (i : Î¹), p i â†’ 0 < Î¼ (s i) :=
  Filter.HasBasis.frequently_smallSets (hl := hl) Î¼.pos_mono

/-- A point `x` is in the support of measure `Î¼` iff any neighborhood of `x` contains a
subset with positive measure. -/
lemma mem_support_iff {x : X} : x âˆˆ Î¼.support â†”
    âˆƒá¶  u in (ğ“ x).smallSets, 0 < Î¼ u := Iff.rfl

/-- A point `x` is in the support of measure `Î¼` iff every neighborhood of `x` has positive
measure. -/
lemma mem_support_iff_forall (x : X) : x âˆˆ Î¼.support â†” âˆ€ U âˆˆ ğ“ x, 0 < Î¼ U :=
  Filter.HasBasis.mem_measureSupport <| (ğ“ x).basis_sets

lemma support_eq_univ [Î¼.IsOpenPosMeasure] : Î¼.support = Set.univ :=
  Set.ext fun x â†¦ (mem_support_iff_forall x).trans (Iff.intro (fun _ â†¦ trivial)
    (fun _ _ â†¦ measure_pos_of_mem_nhds Î¼))

lemma support_mono {Î½ : Measure X} (h : Î¼ â‰¤ Î½) : Î¼.support â‰¤ Î½.support :=
  fun _ hx â†¦ mem_support_iff_forall _ |>.mpr fun _ hU â†¦
    lt_of_lt_of_le (mem_support_iff_forall _ |>.mp hx _ hU) (h _)

lemma AbsolutelyContinuous.support_mono {Î¼ Î½ : Measure X} (hÎ¼Î½ : Î¼ â‰ª Î½) :
     Î¼.support â‰¤ Î½.support := by
  intro x
  contrapose
  simp only [mem_support_iff, Filter.not_frequently, not_lt, nonpos_iff_eq_zero] at *
  exact fun a â†¦ Filter.Eventually.mono a hÎ¼Î½

/-- A point `x` lies outside the support of `Î¼` iff all of the subsets of one of its neighborhoods
have measure zero. -/
lemma notMem_support_iff {x : X} : x âˆ‰ Î¼.support â†” âˆ€á¶  u in (ğ“ x).smallSets, Î¼ u = 0 := by
  simp [mem_support_iff]

theorem _root_.Filter.HasBasis.notMem_measureSupport {Î¹ : Sort*} {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set X} {x : X} (hl : (ğ“ x).HasBasis p s) :
    x âˆ‰ Î¼.support â†” âˆƒ i, p i âˆ§ Î¼ (s i) = 0 := by
  simp only [hl.mem_measureSupport, not_forall, not_lt, nonpos_iff_eq_zero, bex_def]

@[simp]
lemma support_zero : (0 : Measure X).support = âˆ… := by
  ext; simp only [Set.mem_empty_iff_false, iff_false, notMem_support_iff]
  exact Filter.Eventually.of_forall (congrFun rfl)

/-- The support of the sum of two measures is the union of the supports. -/
lemma support_add (Î¼ Î½ : Measure X) :
  (Î¼ + Î½).support = Î¼.support âˆª Î½.support := by
  ext; simp [mem_support_iff]

/-- A point `x` lies outside the support of `Î¼` iff some neighborhood of `x` has measure zero. -/
lemma notMem_support_iff_exists {x : X} : x âˆ‰ Î¼.support â†” âˆƒ U âˆˆ ğ“ x, Î¼ U = 0 := by
  simp [mem_support_iff_forall]

/-- The support of a measure equals the set of points whose open neighborhoods
all have positive measure. -/
lemma support_eq_forall_isOpen : Î¼.support =
    {x : X | âˆ€ u : Set X, x âˆˆ u â†’ IsOpen u â†’ 0 < Î¼ u} := by
  simp [Set.ext_iff, (nhds_basis_opens _).mem_measureSupport]

lemma isClosed_support {Î¼ : Measure X} : IsClosed Î¼.support := by
  simp_rw [isClosed_iff_frequently, (nhds_basis_opens _).mem_measureSupport,
    (nhds_basis_opens _).frequently_iff]
  grind

lemma isOpen_compl_support {Î¼ : Measure X} : IsOpen Î¼.supportá¶œ :=
  isOpen_compl_iff.mpr Î¼.isClosed_support

lemma subset_compl_support_of_isOpen {t : Set X} (ht : IsOpen t) (h : Î¼ t = 0) :
  t âŠ† Î¼.supportá¶œ := fun _ hx â†¦
  notMem_support_iff_exists.mpr âŸ¨t, ht.mem_nhds hx, hâŸ©

lemma compl_support_eq_sUnion : Î¼.supportá¶œ = â‹ƒâ‚€ {t : Set X | IsOpen t âˆ§ Î¼ t = 0} := by
  ext x; simp only [Set.mem_compl_iff, Set.mem_sUnion, Set.mem_setOf_eq, and_right_comm,
     (nhds_basis_opens x).notMem_measureSupport, fun t â†¦ and_comm (b := x âˆˆ t)]

lemma support_eq_sInter : Î¼.support = â‹‚â‚€ {t : Set X | IsClosed t âˆ§ Î¼ tá¶œ = 0} := by
  ext x
  simp only [(nhds_basis_opens x).mem_measureSupport, and_imp, Set.mem_sInter, Set.mem_setOf_eq]
  rw [â† not_iff_not]
  push_neg
  constructor
  Â· rintro âŸ¨t, ht, htc, htc1âŸ©; use tá¶œ; rw [â† compl_compl t] at htc1 ht
    exact âŸ¨htc.isClosed_compl, nonpos_iff_eq_zero.mp htc1, (Set.mem_compl_iff tá¶œ x).mp htâŸ©
  Â· rintro âŸ¨t, ht, htc, htc1âŸ©; use tá¶œ
    exact âŸ¨Set.mem_compl htc1, ht.isOpen_compl, le_of_eq htcâŸ©

section Lindelof

/-- If the complement of the support is LindelÃ¶f, then the support of a measure is conull. -/
lemma support_mem_ae_of_isLindelof (h : IsLindelof Î¼.supportá¶œ) : Î¼.support âˆˆ ae Î¼ := by
  refine compl_compl Î¼.support â–¸ h.compl_mem_sets_of_nhdsWithin fun s hs â†¦ ?_
  simpa [compl_mem_ae_iff, isOpen_compl_support.nhdsWithin_eq hs]
    using notMem_support_iff_exists.mp hs

variable [HereditarilyLindelofSpace X]

/-- In a hereditarily LindelÃ¶f space, the support of a measure is conull. -/
lemma support_mem_ae : Î¼.support âˆˆ ae Î¼ :=
  support_mem_ae_of_isLindelof <| HereditarilyLindelof_LindelofSets Î¼.supportá¶œ

@[simp]
lemma measure_compl_support : Î¼ (Î¼.support)á¶œ = 0 := support_mem_ae

open Set

lemma nonempty_inter_support_of_pos {s : Set X} (hÎ¼ : 0 < Î¼ s) :
    (s âˆ© Î¼.support).Nonempty := by
  by_contra H
  have :=  LE.le.not_gt <| (OuterMeasureClass.measure_mono Î¼ <| Disjoint.subset_compl_right
    <| disjoint_iff_inter_eq_empty.mpr <| Set.not_nonempty_iff_eq_empty.mp H).trans
      <| le_of_eq (measure_compl_support)
  contradiction

lemma nullMeasurableSet_compl_support : NullMeasurableSet (Î¼.supportá¶œ) Î¼ :=
  NullMeasurableSet.of_null measure_compl_support

@[simp]
lemma nullMeasurableSet_support : NullMeasurableSet Î¼.support Î¼ :=
  NullMeasurableSet.compl_iff.mp (nullMeasurableSet_compl_support)

@[simp]
lemma measure_support : Î¼ Î¼.support = Î¼ Set.univ :=
  measure_of_measure_compl_eq_zero measure_compl_support

lemma nonempty_support (hÎ¼ : Î¼ â‰  0) : Î¼.support.Nonempty :=
   Nonempty.right <| nonempty_inter_support_of_pos <| measure_univ_pos.mpr hÎ¼

lemma nonempty_support_iff : Î¼.support.Nonempty â†” Î¼ â‰  0 :=
  âŸ¨fun h e â†¦ (not_nonempty_iff_eq_empty.mpr <| congrArg Measure.support e|>.trans
    <| support_zero) h, fun h â†¦ nonempty_support hâŸ©

end Lindelof
section Restrict

lemma support_restrict_subset_closure [OpensMeasurableSpace X] {s : Set X} :
    (Î¼.restrict s).support âŠ† closure s := by
  intro x hx
  simp only [mem_closure_iff_nhds]
  rw [nhds_basis_opens x |>.forall_iff <| fun _ _ h â†¦ Set.Nonempty.mono <| by gcongr]
  intro U âŸ¨hxU, hUâŸ©
  by_cases H : (s âˆ© U).Nonempty
  Â· exact Set.inter_nonempty_iff_exists_right.mpr H
  Â· have h_restr : (Î¼.restrict s) U = Î¼ (U âˆ© s) := by
      simp [Measure.restrict_apply hU.measurableSet, Set.inter_comm]
    rw [nhds_basis_opens x |>.mem_measureSupport] at hx
    exact MeasureTheory.nonempty_of_measure_ne_zero
      (ne_of_gt (h_restr â–¸ hx U âŸ¨hxU, hUâŸ©))

lemma mem_support_restrict [OpensMeasurableSpace X] {s : Set X} {x : X} :
    x âˆˆ (Î¼.restrict s).support â†” âˆƒá¶  u in (ğ“[s] x).smallSets, 0 < Î¼ u := by
  rw [(nhds_basis_opens x).mem_measureSupport,
    Filter.HasBasis.frequently_smallSets (hl := nhdsWithin_basis_open x s) (hq := pos_mono Î¼)] at *
  constructor
  all_goals
  Â· intro h i hi
    have D := h i hi
    rw [restrict_apply] at *
    Â· exact D
    Â· exact IsOpen.measurableSet hi.2

lemma interior_inter_support [OpensMeasurableSpace X] {s : Set X} :
    interior s âˆ© Î¼.support âŠ† (Î¼.restrict s).support := by
  intro x âŸ¨âŸ¨y, hy1, hy2âŸ©, hxpâŸ©
  apply mem_support_restrict.mpr
  rw [Filter.HasBasis.frequently_smallSets (hl := nhdsWithin_basis_open x s) (hq := pos_mono Î¼)]
  intro V âŸ¨hs1, hs2âŸ©
  rw [(nhds_basis_opens x).mem_measureSupport] at hxp
  exact lt_of_lt_of_le (hxp (V âˆ© y) âŸ¨Set.mem_inter hs1 hy2, IsOpen.inter hs2 hy1.1âŸ©)
    <| OuterMeasureClass.measure_mono Î¼ <| Set.inter_subset_inter (fun â¦ƒaâ¦„ a â†¦ a) hy1.2

-- Prove the following directly, without appeal to `support_restrict_subset_closure`

-- SO THE PROOF BELOW MUST BE REPLACED!

lemma support_restrict_subset_closure_inter_support [OpensMeasurableSpace X] {s : Set X} :
  (Î¼.restrict s).support âŠ† closure s âˆ© Î¼.support :=
  Set.subset_inter (support_restrict_subset_closure) (support_mono restrict_le_self)

end Restrict

end Measure

end MeasureTheory

end Support
