/-
Copyright (c) 2023 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Peter Pfaffelhuber
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.MeasureTheory.Measure.Regular
import Mathlib.Topology.MetricSpace.Polish
import Mathlib.Topology.UniformSpace.Cauchy

/-!
# Inner regularity of finite measures

The main result of this file is
`InnerRegularCompactLTTop_of_pseudoEMetricSpace_completeSpace_secondCountable`:
A finite measure `Œº` on a `PseudoEMetricSpace E` and `CompleteSpace E` with
`SecondCountableTopology E` is inner regular with respect to compact sets. In other
words, a finite measure on such a space is a tight measure.

Finite measures on Polish spaces are an important special case, which makes the result
`MeasureTheory.PolishSpace.innerRegular_isCompact_isClosed_measurableSet` an important result in
probability.
-/

open Set MeasureTheory

open scoped ENNReal Uniformity

namespace MeasureTheory

variable {Œ± : Type*} [MeasurableSpace Œ±] {Œº : Measure Œ±}

theorem innerRegularWRT_isCompact_closure_iff [TopologicalSpace Œ±] [R1Space Œ±] :
    Œº.InnerRegularWRT (IsCompact ‚àò closure) IsClosed ‚Üî Œº.InnerRegularWRT IsCompact IsClosed := by
  constructor <;> intro h A hA r hr
  ¬∑ rcases h hA r hr with ‚ü®K, ‚ü®hK1, hK2, hK3‚ü©‚ü©
    exact ‚ü®closure K, closure_minimal hK1 hA, hK2, hK3.trans_le (measure_mono subset_closure)‚ü©
  ¬∑ rcases h hA r hr with ‚ü®K, ‚ü®hK1, hK2, hK3‚ü©‚ü©
    refine ‚ü®closure K, closure_minimal hK1 hA, ?_, ?_‚ü©
    ¬∑ simpa only [closure_closure, Function.comp_apply] using hK2.closure
    ¬∑ exact hK3.trans_le (measure_mono subset_closure)

lemma innerRegularWRT_isCompact_isClosed_iff_innerRegularWRT_isCompact_closure
    [TopologicalSpace Œ±] [R1Space Œ±] :
    Œº.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) IsClosed
      ‚Üî Œº.InnerRegularWRT (IsCompact ‚àò closure) IsClosed := by
  constructor <;> intro h A hA r hr
  ¬∑ obtain ‚ü®K, hK1, ‚ü®hK2, _‚ü©, hK4‚ü© := h hA r hr
    refine ‚ü®K, hK1, ?_, hK4‚ü©
    simp only [Function.comp_apply]
    exact hK2.closure
  ¬∑ obtain ‚ü®K, hK1, hK2, hK3‚ü© := h hA r hr
    refine ‚ü®closure K, closure_minimal hK1 hA, ?_, ?_‚ü©
    ¬∑ simpa only [isClosed_closure, and_true]
    ¬∑ exact hK3.trans_le (measure_mono subset_closure)

lemma innerRegularWRT_isCompact_isClosed_iff [TopologicalSpace Œ±] [R1Space Œ±] :
    Œº.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) IsClosed
      ‚Üî Œº.InnerRegularWRT IsCompact IsClosed :=
  innerRegularWRT_isCompact_isClosed_iff_innerRegularWRT_isCompact_closure.trans
    innerRegularWRT_isCompact_closure_iff

/--
If predicate `p` is preserved under intersections with sets satisfying predicate `q`, and sets
satisfying `p` cover the space arbitrarily well, then `Œº` is inner regular with respect to
predicates `p` and `q`.
-/
theorem innerRegularWRT_of_exists_compl_lt {p q : Set Œ± ‚Üí Prop} (hpq : ‚àÄ A B, p A ‚Üí q B ‚Üí p (A ‚à© B))
    (hŒº : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ K, p K ‚àß Œº K·∂ú < Œµ) :
    Œº.InnerRegularWRT p q := by
  intro A hA r hr
  obtain ‚ü®K, hK, hK_subset, h_lt‚ü© : ‚àÉ K, p K ‚àß K ‚äÜ A ‚àß Œº (A \ K) < Œº A - r := by
    obtain ‚ü®K', hpK', hK'_lt‚ü© := hŒº (Œº A - r) (tsub_pos_of_lt hr)
    refine ‚ü®K' ‚à© A, hpq K' A hpK' hA, inter_subset_right, ?_‚ü©
    ¬∑ refine (measure_mono fun x ‚Ü¶ ?_).trans_lt hK'_lt
      simp only [diff_inter_self_eq_diff, mem_diff, mem_compl_iff, and_imp, imp_self, imp_true_iff]
  refine ‚ü®K, hK_subset, hK, ?_‚ü©
  have h_lt' : Œº A - Œº K < Œº A - r := le_measure_diff.trans_lt h_lt
  exact lt_of_tsub_lt_tsub_left h_lt'

theorem innerRegularWRT_isCompact_closure_of_univ [TopologicalSpace Œ±]
    (hŒº : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ K, IsCompact (closure K) ‚àß Œº (K·∂ú) < Œµ) :
    Œº.InnerRegularWRT (IsCompact ‚àò closure) IsClosed := by
  refine innerRegularWRT_of_exists_compl_lt (fun s t hs ht ‚Ü¶ ?_) hŒº
  have : IsCompact (closure s ‚à© t) := hs.inter_right ht
  refine this.of_isClosed_subset isClosed_closure ?_
  refine (closure_inter_subset_inter_closure _ _).trans_eq ?_
  rw [IsClosed.closure_eq ht]

theorem exists_isCompact_closure_measure_compl_lt [UniformSpace Œ±] [CompleteSpace Œ±]
    [SecondCountableTopology Œ±] [(uniformity Œ±).IsCountablyGenerated]
    [OpensMeasurableSpace Œ±] (P : Measure Œ±) [IsFiniteMeasure P] (Œµ : ‚Ñù‚â•0‚àû) (hŒµ : 0 < Œµ) :
    ‚àÉ K, IsCompact (closure K) ‚àß P K·∂ú < Œµ := by
  /-
  If Œ± is empty, the result is trivial.

  Otherwise, fix a dense sequence `seq` and an antitone basis `t` of entourages. We find a sequence
  of natural numbers `u n`, such that `interUnionBalls seq u t`, which is the intersection over
  `n` of the `t n`-neighborhood of `seq 1, ..., seq (u n)`, covers the space arbitrarily well.
  -/
  cases isEmpty_or_nonempty Œ±
  case inl =>
    refine ‚ü®‚àÖ, by simp, ?_‚ü©
    rwa [Set.eq_empty_of_isEmpty ‚àÖ·∂ú, measure_empty]
  case inr =>
    let seq := TopologicalSpace.denseSeq Œ±
    have hseq_dense : DenseRange seq := TopologicalSpace.denseRange_denseSeq Œ±
    obtain ‚ü®t : ‚Ñï ‚Üí SetRel Œ± Œ±,
        ht : ‚àÄ i, t i ‚àà ùì§ Œ± ‚àß IsOpen (t i) ‚àß (t i).IsSymm,
        h_basis : (uniformity Œ±).HasAntitoneBasis t‚ü© :=
      (@uniformity_hasBasis_open_symmetric Œ± _).exists_antitone_subbasis
    choose htu hto _ using ht
    let f : ‚Ñï ‚Üí ‚Ñï ‚Üí Set Œ± := fun n m ‚Ü¶ UniformSpace.ball (seq m) (t n)
    have h_univ n : (‚ãÉ m, f n m) = univ := hseq_dense.iUnion_uniformity_ball (htu n)
    have h3 n (Œµ : ‚Ñù‚â•0‚àû) (hŒµ : 0 < Œµ) : ‚àÉ m, P (‚ãÇ m' ‚â§ m, (f n m')·∂ú) < Œµ := by
      refine exists_measure_iInter_lt (fun m ‚Ü¶ ?_) hŒµ ‚ü®0, measure_ne_top P _‚ü© ?_
      ¬∑ exact (measurable_prodMk_left (hto n).measurableSet).compl.nullMeasurableSet
      ¬∑ rw [‚Üê compl_iUnion, h_univ, compl_univ]
    choose! s' s'bound using h3
    rcases ENNReal.exists_pos_sum_of_countable' (ne_of_gt hŒµ) ‚Ñï with ‚ü®Œ¥, hŒ¥1, hŒ¥2‚ü©
    classical
    let u : ‚Ñï ‚Üí ‚Ñï := fun n ‚Ü¶ s' n (Œ¥ n)
    refine ‚ü®interUnionBalls seq u t, isCompact_closure_interUnionBalls h_basis.toHasBasis seq u, ?_‚ü©
    rw [interUnionBalls, Set.compl_iInter]
    refine ((measure_iUnion_le _).trans ?_).trans_lt hŒ¥2
    refine ENNReal.tsum_le_tsum (fun n ‚Ü¶ ?_)
    have h'' n : Prod.swap ‚Åª¬π' t n = t n := by ext; exact (t n).comm
    simp only [h'', compl_iUnion, ge_iff_le]
    exact (s'bound n (Œ¥ n) (hŒ¥1 n)).le

theorem innerRegularWRT_isCompact_closure [UniformSpace Œ±] [CompleteSpace Œ±]
    [SecondCountableTopology Œ±] [(uniformity Œ±).IsCountablyGenerated]
    [OpensMeasurableSpace Œ±] (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (IsCompact ‚àò closure) IsClosed :=
  innerRegularWRT_isCompact_closure_of_univ
    (exists_isCompact_closure_measure_compl_lt P)

theorem innerRegularWRT_isCompact_isClosed [UniformSpace Œ±] [CompleteSpace Œ±]
    [SecondCountableTopology Œ±] [(uniformity Œ±).IsCountablyGenerated]
    [OpensMeasurableSpace Œ±] (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) IsClosed := by
  rw [innerRegularWRT_isCompact_isClosed_iff_innerRegularWRT_isCompact_closure]
  exact innerRegularWRT_isCompact_closure P

theorem innerRegularWRT_isCompact [UniformSpace Œ±] [CompleteSpace Œ±]
    [SecondCountableTopology Œ±] [(uniformity Œ±).IsCountablyGenerated]
    [OpensMeasurableSpace Œ±] (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT IsCompact IsClosed := by
  rw [‚Üê innerRegularWRT_isCompact_closure_iff]
  exact innerRegularWRT_isCompact_closure P

theorem innerRegularWRT_isCompact_isClosed_isOpen [PseudoEMetricSpace Œ±]
    [CompleteSpace Œ±] [SecondCountableTopology Œ±] [OpensMeasurableSpace Œ±]
    (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) IsOpen :=
  (innerRegularWRT_isCompact_isClosed P).trans
    (Measure.InnerRegularWRT.of_pseudoMetrizableSpace P)

theorem innerRegularWRT_isCompact_isOpen [PseudoEMetricSpace Œ±]
    [CompleteSpace Œ±] [SecondCountableTopology Œ±] [OpensMeasurableSpace Œ±]
    (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT IsCompact IsOpen :=
  (innerRegularWRT_isCompact P).trans
    (Measure.InnerRegularWRT.of_pseudoMetrizableSpace P)

/--
A finite measure `Œº` on a `PseudoEMetricSpace E` and `CompleteSpace E` with
`SecondCountableTopology E` is inner regular. In other words, a finite measure
on such a space is a tight measure.
-/
instance InnerRegular_of_pseudoEMetricSpace_completeSpace_secondCountable [PseudoEMetricSpace Œ±]
    [CompleteSpace Œ±] [SecondCountableTopology Œ±] [BorelSpace Œ±]
    (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegular := by
  suffices P.InnerRegularCompactLTTop from inferInstance
  refine ‚ü®Measure.InnerRegularWRT.measurableSet_of_isOpen ?_ ?_‚ü©
  ¬∑ exact innerRegularWRT_isCompact_isOpen P
  ¬∑ exact fun s t hs_compact ht_open ‚Ü¶ hs_compact.inter_right ht_open.isClosed_compl

/--
A special case of `innerRegular_of_pseudoEMetricSpace_completeSpace_secondCountable` for Polish
spaces: A finite measure on a Polish space is a tight measure.
-/
instance InnerRegular_of_polishSpace [TopologicalSpace Œ±]
    [PolishSpace Œ±] [BorelSpace Œ±] (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegular := by
  letI := TopologicalSpace.upgradeIsCompletelyMetrizable Œ±
  exact InnerRegular_of_pseudoEMetricSpace_completeSpace_secondCountable P

/--
A measure `Œº` on a `PseudoEMetricSpace E` and `CompleteSpace E` with `SecondCountableTopology E`
is inner regular for finite measure sets with respect to compact sets.
-/
instance InnerRegularCompactLTTop_of_pseudoEMetricSpace_completeSpace_secondCountable
    [PseudoEMetricSpace Œ±] [CompleteSpace Œ±] [SecondCountableTopology Œ±] [BorelSpace Œ±]
    (Œº : Measure Œ±) :
    Œº.InnerRegularCompactLTTop := by
  constructor
  intro A ‚ü®hA1, hA2‚ü© r hr
  have := Fact.mk hA2.lt_top
  have hA2' : (Œº.restrict A) A ‚â† ‚ä§ := by
    rwa [Measure.restrict_apply_self]
  have hr' : r < Œº.restrict A A := by
    rwa [Measure.restrict_apply_self]
  obtain ‚ü®K, ‚ü®hK1, hK2, hK3‚ü©‚ü© := MeasurableSet.exists_lt_isCompact_of_ne_top hA1 hA2' hr'
  use K, hK1, hK2
  rwa [Measure.restrict_eq_self Œº hK1] at hK3

/--
A special case of `innerRegularCompactLTTop_of_pseudoEMetricSpace_completeSpace_secondCountable`
for Polish spaces: A measure `Œº` on a Polish space inner regular for finite measure sets with
respect to compact sets.
-/
instance InnerRegularCompactLTTop_of_polishSpace
    [TopologicalSpace Œ±] [PolishSpace Œ±] [BorelSpace Œ±] (Œº : Measure Œ±) :
    Œº.InnerRegularCompactLTTop := by
  letI := TopologicalSpace.upgradeIsCompletelyMetrizable Œ±
  exact InnerRegularCompactLTTop_of_pseudoEMetricSpace_completeSpace_secondCountable Œº

theorem innerRegular_isCompact_isClosed_measurableSet_of_finite [PseudoEMetricSpace Œ±]
    [CompleteSpace Œ±] [SecondCountableTopology Œ±] [BorelSpace Œ±]
    (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) MeasurableSet := by
  suffices P.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s)
      fun s ‚Ü¶ MeasurableSet s ‚àß P s ‚â† ‚àû by
    convert this
    simp only [iff_self_and]
    exact fun _ ‚Ü¶ measure_ne_top P _
  refine Measure.InnerRegularWRT.measurableSet_of_isOpen ?_ ?_
  ¬∑ exact innerRegularWRT_isCompact_isClosed_isOpen P
  ¬∑ rintro s t ‚ü®hs_compact, hs_closed‚ü© ht_open
    rw [diff_eq]
    exact ‚ü®hs_compact.inter_right ht_open.isClosed_compl,
      hs_closed.inter (isClosed_compl_iff.mpr ht_open)‚ü©

/--
On a Polish space, any finite measure is regular with respect to compact and closed sets. In
particular, a finite measure on a Polish space is a tight measure.
-/
theorem PolishSpace.innerRegular_isCompact_isClosed_measurableSet [TopologicalSpace Œ±]
    [PolishSpace Œ±] [BorelSpace Œ±] (P : Measure Œ±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s ‚Ü¶ IsCompact s ‚àß IsClosed s) MeasurableSet := by
  letI := TopologicalSpace.upgradeIsCompletelyMetrizable Œ±
  exact innerRegular_isCompact_isClosed_measurableSet_of_finite P

end MeasureTheory
