/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp
import Mathlib.MeasureTheory.SetAlgebra

/-!
# Separable measure spaces

The goal of this file is to give a sufficient condition on the measure space `(X, Î¼)` and the
`NormedAddCommGroup E` for the space `MeasureTheory.Lp E p Î¼` to have `SecondCountableTopology` when
`1 â‰¤ p < âˆ`. To do so we define the notion of a `MeasureTheory.MeasureDense` family and a
`MeasureTheory.SeparableMeasureSpace`.
We prove that if `X` is `MeasurableSpace.CountablyGenerated` and `Î¼` is `Ïƒ`-finite, then `(X, Î¼)`
is separable. We then prove that if `(X, Î¼)` is separable and `E` is second-countable,
then `Lp E p Î¼` is second-countable.

A family `ğ’œ` of `(X, Î¼)` is said to be **measure-dense** if it contains only measurable sets and
can approximate any measurable set with finite measure, in the sense that
for any measurable set `s` such that `Î¼ s â‰  âˆ`, `Î¼ (s âˆ† t)` can be made
arbitrarily small when `t âˆˆ ğ’œ`. We show below that such a family can be chosen to contain only
sets with finite measure.
The term "measure-dense" is justified by the fact that the approximating condition translates
to the usual notion of density in the metric space made by constant indicators of measurable sets
equipped with the `LÂ¹` norm.

`(X, Î¼)` is **separable** if it admits a countable and measure-dense family of sets.
The term "separable" is justified by the fact that the definition translates to the usual notion
of separability in the metric space made by constant indicators equipped with the `LÂ¹` norm.

## Main definitions

* `MeasureTheory.Measure.Î¼.MeasureDense ğ’œ`: `ğ’œ` is a measure-dense family if it only contains
  measurable sets and if the following condition is satisfied: if `s` is measurable with finite
  measure, then for any `Îµ > 0` there exists `t âˆˆ ğ’œ` such that `Î¼ (s âˆ† t) < Îµ `.
* `MeasureTheory.SeparableMeasureSpace`: A measure space is separable if it admits a countable and
  measure-dense family.

## Main statements

* `MeasureTheory.instSecondCountableLp`: If `(X, Î¼)` is separable, `E` is second-countable and
  `1 â‰¤ p < âˆ` then `Lp E p Î¼` is second-countable. This is in particular true if `X` is countably
  generated and `Î¼` is `Ïƒ`-finite.

## Implementation notes

Through the whole file we consider a measurable space `X` equipped with a measure `Î¼`, and also
a normed commutative group `E`. We also consider an extended non-negative real `p` such that
`1 â‰¤ p < âˆ`. This is registered as instances via `one_le_p : Fact (1 â‰¤ p)` and
`p_ne_top : Fact (p â‰  âˆ)`, so the properties are accessible via `one_le_p.elim` and `p_ne_top.elim`.

Through the whole file, when we write that an extended non-negative real is finite, it is always
written `â‰  âˆ` rather than `< âˆ`. See `Ne.lt_top` and `ne_of_lt` to switch from one to the other.

## References

* [D. L. Cohn, *Measure Theory*][cohn2013measure]

## Tags

separable measure space, measure-dense, Lp space, second-countable
-/

open MeasurableSpace Set ENNReal TopologicalSpace BigOperators symmDiff Filter Real

namespace MeasureTheory

variable {X E : Type*} [m : MeasurableSpace X] [NormedAddCommGroup E] {Î¼ : Measure X}
variable {p : â„â‰¥0âˆ} [one_le_p : Fact (1 â‰¤ p)] [p_ne_top : Fact (p â‰  âˆ)] {ğ’œ : Set (Set X)}

section MeasureDense

/-! ### Definition of a measure-dense family, basic properties and sufficient conditions -/

/-- A family `ğ’œ` of sets of a measure space is said to be measure-dense if it contains only
measurable sets and can approximate any measurable set with finite measure, in the sense that
for any measurable set `s` with finite measure the symmetric difference `s âˆ† t` can be made
arbitrarily small when `t âˆˆ ğ’œ`. We show below that such a family can be chosen to contain only
sets with finite measures.

The term "measure-dense" is justified by the fact that the approximating condition translates
to the usual notion of density in the metric space made by constant indicators of measurable sets
equipped with the `LÂ¹` norm. -/
structure Measure.MeasureDense (Î¼ : Measure X) (ğ’œ : Set (Set X)) : Prop :=
  /-- Each set has to be measurable. -/
  measurable : âˆ€ s âˆˆ ğ’œ, MeasurableSet s
  /-- Any measurable set can be approximated by sets in the family. -/
  approx : âˆ€ s, MeasurableSet s â†’ Î¼ s â‰  âˆ â†’ âˆ€ (Îµ : â„),
    0 < Îµ â†’ âˆƒ t âˆˆ ğ’œ, Î¼ (s âˆ† t) < ENNReal.ofReal Îµ

/-- The set of measurable sets is measure-dense. -/
theorem measurable_measureDense : Î¼.MeasureDense {s | MeasurableSet s} where
  measurable := fun _ h â†¦ h
  approx := fun s hs _ Îµ Îµ_pos â†¦ âŸ¨s, hs, by simpaâŸ©

/-- If a family of sets `ğ’œ` is measure-dense in `X`, then any measurable set with finite measure
can be approximated by sets in `ğ’œ` with finite measure. -/
lemma fin_meas_approx_of_measureDense (hğ’œ : Î¼.MeasureDense ğ’œ) {s : Set X}
    (ms : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (Îµ : â„) (Îµ_pos : 0 < Îµ) :
    âˆƒ t âˆˆ ğ’œ, Î¼ t â‰  âˆ âˆ§ Î¼ (s âˆ† t) < ENNReal.ofReal Îµ := by
  rcases hğ’œ.approx s ms hÎ¼s Îµ Îµ_pos with âŸ¨t, t_mem, htâŸ©
  exact âŸ¨t, t_mem, (measure_ne_top_iff_of_symmDiff <| ne_top_of_lt ht).1 hÎ¼s, htâŸ©

/-- If a family of sets `ğ’œ` is measure-dense in `X`, then it is also the case for the sets in `ğ’œ`
with finite measure. -/
theorem fin_meas_measureDense_of_measureDense (hğ’œ : Î¼.MeasureDense ğ’œ) :
    Î¼.MeasureDense {s | s âˆˆ ğ’œ âˆ§ Î¼ s â‰  âˆ} where
  measurable := fun s h â†¦ hğ’œ.measurable s h.1
  approx := by
    intro s ms hÎ¼s Îµ Îµ_pos
    rcases fin_meas_approx_of_measureDense hğ’œ ms hÎ¼s Îµ Îµ_pos with âŸ¨t, t_mem, hÎ¼t, hÎ¼stâŸ©
    exact âŸ¨t, âŸ¨t_mem, hÎ¼tâŸ©, hÎ¼stâŸ©

/-- If a measurable space equipped with a finite measure is generated by an algebra of sets, then
this algebra of sets is measure-dense. -/
theorem measureDense_of_generateFrom_setAglebra_of_finite [IsFiniteMeasure Î¼] (hğ’œ : IsSetAlgebra ğ’œ)
    (hgen : m = MeasurableSpace.generateFrom ğ’œ) : Î¼.MeasureDense ğ’œ where
  measurable := fun s hs â†¦ hgen â–¸ measurableSet_generateFrom hs
  approx := by
    -- We want to show that any measurable set can be approximated by sets in `ğ’œ`. To do so, it is
    -- enough to show that such sets constitute a `Ïƒ`-algebra containing `ğ’œ`. This is contained in
    -- the theorem `generateFrom_induction`.
    intro s ms
    have : MeasurableSet s âˆ§ âˆ€ (Îµ : â„), 0 < Îµ â†’ âˆƒ t âˆˆ ğ’œ, (Î¼ (s âˆ† t)).toReal < Îµ := by
      apply generateFrom_induction
        (p := fun s â†¦ MeasurableSet s âˆ§ âˆ€ (Îµ : â„), 0 < Îµ â†’ âˆƒ t âˆˆ ğ’œ, (Î¼ (s âˆ† t)).toReal < Îµ)
        (C := ğ’œ) (hs := hgen â–¸ ms)
      Â· -- If `t âˆˆ ğ’œ`, then `Î¼ (t âˆ† t) = 0` which is less than any `Îµ > 0`.
        exact fun t t_mem â†¦ âŸ¨hgen â–¸ measurableSet_generateFrom t_mem,
          fun Îµ Îµ_pos â†¦ âŸ¨t, t_mem, by simpaâŸ©âŸ©
      Â· -- `âˆ… âˆˆ ğ’œ` and `Î¼ (âˆ… âˆ† âˆ…) = 0` which is less than any `Îµ > 0`.
        exact âŸ¨MeasurableSet.empty, fun Îµ Îµ_pos â†¦ âŸ¨âˆ…, hğ’œ.empty_mem, by simpaâŸ©âŸ©
      Â· -- If `s` is measurable and `t âˆˆ ğ’œ` such that `Î¼ (s âˆ† t) < Îµ`, then `tá¶œ âˆˆ ğ’œ` and
        -- `Î¼ (sá¶œ âˆ† tá¶œ) = Î¼ (s âˆ† t) < Îµ` so `sá¶œ` can be approximated.
        refine fun t âŸ¨mt, htâŸ© â†¦ âŸ¨mt.compl, fun Îµ Îµ_pos â†¦ ?_âŸ©
        rcases ht Îµ Îµ_pos with âŸ¨u, u_mem, hÎ¼tcuâŸ©
        exact âŸ¨uá¶œ, hğ’œ.compl_mem u_mem, by rwa [compl_symmDiff_compl]âŸ©
      Â· -- Let `(fâ‚™)` be a sequence of measurable sets and `Îµ > 0`.
        refine fun f hf â†¦ âŸ¨MeasurableSet.iUnion (fun n â†¦ (hf n).1), fun Îµ Îµ_pos â†¦ ?_âŸ©
        -- We have  `Î¼ (â‹ƒ n â‰¤ N, fâ‚™) âŸ¶ Î¼ (â‹ƒ n, fâ‚™)`.
        have := tendsto_measure_iUnion' (Î¼ := Î¼) (f := f)
        rw [â† tendsto_toReal_iff (fun _ â†¦ measure_ne_top _ _) (measure_ne_top _ _)] at this
        -- So there exists `N` such that `Î¼ (â‹ƒ n, fâ‚™) - Î¼ (â‹ƒ n â‰¤ N, fâ‚™) < Îµ/2`.
        rcases Metric.tendsto_atTop.1 this (Îµ / 2) (by linarith [Îµ_pos]) with âŸ¨N, hNâŸ©
        -- For any `n â‰¤ N` there exists `gâ‚™ âˆˆ ğ’œ` such that `Î¼ (fâ‚™ âˆ† gâ‚™) < Îµ/(2*(N+1))`.
        choose g g_mem hg using fun n â†¦ (hf n).2 (Îµ / (2 * (N + 1))) (div_pos Îµ_pos (by linarith))
        -- Therefore we have
        -- `Î¼ ((â‹ƒ n, fâ‚™) âˆ† (â‹ƒ n â‰¤ N, gâ‚™))`
        --   `â‰¤ Î¼ ((â‹ƒ n, fâ‚™) âˆ† (â‹ƒ n â‰¤ N, fâ‚™)) + Î¼ ((â‹ƒ n â‰¤ N, fâ‚™) âˆ† (â‹ƒ n â‰¤ N, gâ‚™))`
        --   `< Îµ/2 + âˆ‘ n â‰¤ N, Î¼ (fâ‚™ âˆ† gâ‚™)` (see `biSup_symmDiff_biSup_le`)
        --   `< Îµ/2 + (N+1)*Îµ/(2*(N+1)) = Îµ/2`.
        refine âŸ¨â‹ƒ n âˆˆ Finset.range (N + 1), g n, hğ’œ.biUnion_mem _ (fun i _ â†¦ g_mem i), ?_âŸ©
        calc
          (Î¼ ((â‹ƒ n, f n) âˆ† (â‹ƒ n âˆˆ (Finset.range (N + 1)), g n))).toReal
            â‰¤ (Î¼ ((â‹ƒ n, f n) \ ((â‹ƒ n âˆˆ (Finset.range (N + 1)), f n)) âˆª
              ((â‹ƒ n âˆˆ (Finset.range (N + 1)), f n)
              âˆ† (â‹ƒ n âˆˆ (Finset.range (N + 1)), g â†‘n)))).toReal :=
                toReal_mono (measure_ne_top _ _)
                  (measure_mono <| symmDiff_of_ge (iUnion_subset <|
                  fun i â†¦ iUnion_subset (fun _ â†¦ subset_iUnion f i)) â–¸ symmDiff_triangle ..)
          _ â‰¤ (Î¼ ((â‹ƒ n, f n) \
              ((â‹ƒ n âˆˆ (Finset.range (N + 1)), f n)))).toReal +
              (Î¼ ((â‹ƒ n âˆˆ (Finset.range (N + 1)), f n) âˆ†
              (â‹ƒ n âˆˆ (Finset.range (N + 1)), g â†‘n))).toReal := by
                rw [â† toReal_add (measure_ne_top _ _) (measure_ne_top _ _)]
                exact toReal_mono (add_ne_top.2 âŸ¨measure_ne_top _ _, measure_ne_top _ _âŸ©) <|
                  measure_union_le _ _
          _ < Îµ := by
                rw [â† add_halves Îµ]
                apply _root_.add_lt_add
                Â· rw [measure_diff (h_fin := measure_ne_top _ _),
                    toReal_sub_of_le (ha := measure_ne_top _ _)]
                  apply lt_of_le_of_lt (sub_le_dist ..)
                  simp only [Finset.mem_range, Nat.lt_add_one_iff]
                  exact (dist_comm (Î± := â„) .. â–¸ hN N (le_refl N))
                  exact (measure_mono <| iUnion_subset <|
                    fun i â†¦ iUnion_subset (fun _ â†¦ subset_iUnion f i))
                  exact iUnion_subset <| fun i â†¦ iUnion_subset (fun _ â†¦ subset_iUnion f i)
                  exact MeasurableSet.biUnion (countable_coe_iff.1 inferInstance)
                    (fun n _ â†¦ (hf n).1)
                Â· calc
                    (Î¼ ((â‹ƒ n âˆˆ (Finset.range (N + 1)), f n) âˆ†
                    (â‹ƒ n âˆˆ (Finset.range (N + 1)), g â†‘n))).toReal
                      â‰¤ (Î¼ (â‹ƒ n âˆˆ (Finset.range (N + 1)), f n âˆ† g n)).toReal :=
                          toReal_mono (measure_ne_top _ _) (measure_mono biSup_symmDiff_biSup_le)
                    _ â‰¤ âˆ‘ n in (Finset.range (N + 1)), (Î¼ (f n âˆ† g n)).toReal := by
                          rw [â† toReal_sum (fun _ _ â†¦ measure_ne_top _ _)]
                          exact toReal_mono (ne_of_lt <| sum_lt_top fun _ _ â†¦ measure_ne_top Î¼ _)
                            (measure_biUnion_finset_le _ _)
                    _ < âˆ‘ n in (Finset.range (N + 1)), (Îµ / (2 * (N + 1))) :=
                          Finset.sum_lt_sum (fun i _ â†¦ le_of_lt (hg i)) âŸ¨0, by simp, hg 0âŸ©
                    _ â‰¤ Îµ / 2 := by
                          simp only [Finset.sum_const, Finset.card_range, nsmul_eq_mul,
                            Nat.cast_add, Nat.cast_one]
                          rw [div_mul_eq_div_mul_one_div, â† mul_assoc, mul_comm ((N : â„) + 1),
                            mul_assoc]
                          exact mul_le_of_le_one_right (by linarith [Îµ_pos]) <|
                            le_of_eq <| mul_one_div_cancel <| Nat.cast_add_one_ne_zero _
    rintro - Îµ Îµ_pos
    rcases this.2 Îµ Îµ_pos with âŸ¨t, t_mem, hÎ¼stâŸ©
    exact âŸ¨t, t_mem, (lt_ofReal_iff_toReal_lt (measure_ne_top _ _)).2 hÎ¼stâŸ©

/-- If a measure space `X` is generated by an algebra of sets which contains a monotone countable
family of sets with finite measure spanning `X` (thus the measure is `Ïƒ`-finite), then this algebra
of sets is measure-dense. -/
theorem measureDense_of_generateFrom_setAglebra_of_sigmaFinite (hğ’œ : IsSetAlgebra ğ’œ)
    (S : Î¼.FiniteSpanningSetsIn ğ’œ) (hgen : m = MeasurableSpace.generateFrom ğ’œ) :
    Î¼.MeasureDense ğ’œ where
  measurable s hs := hgen â–¸ measurableSet_generateFrom hs
  approx := by
    -- We use partial unions of (Sâ‚™) to get a monotone family spanning `X`.
    let T := Accumulate S.set
    have T_mem : âˆ€ n, T n âˆˆ ğ’œ := fun n â†¦ by
      simpa using hğ’œ.biUnion_mem {k | k â‰¤ n}.toFinset (fun k _ â†¦ S.set_mem k)
    have T_finite : âˆ€ n, Î¼ (T n) < âˆ := fun n â†¦ by
      simpa using measure_biUnion_lt_top {k | k â‰¤ n}.toFinset.finite_toSet
        (fun k _ â†¦ ne_of_lt (S.finite k))
    have T_spanning : â‹ƒ n, T n = univ := S.spanning â–¸ iUnion_accumulate
    -- We use the fact that we already know this is true for finite measures. As `â‹ƒ n, T n = X`,
    -- we have that `Î¼ ((T n) âˆ© s) âŸ¶ Î¼ s`.
    intro s ms hÎ¼s Îµ Îµ_pos
    have mono : Monotone (fun n â†¦ (T n) âˆ© s) := fun m n hmn â†¦ inter_subset_inter_left s
        (biUnion_subset_biUnion_left fun k hkm â†¦ Nat.le_trans hkm hmn)
    have := tendsto_measure_iUnion (Î¼ := Î¼) mono
    rw [â† tendsto_toReal_iff] at this
    Â· -- We can therefore choose `N` such that `Î¼ s - Î¼ ((S N) âˆ© s) < Îµ/2`.
      rcases Metric.tendsto_atTop.1 this (Îµ / 2) (by linarith [Îµ_pos]) with âŸ¨N, hNâŸ©
      have : Fact (Î¼ (T N) < âˆ) := Fact.mk <| T_finite N
      -- Then we can apply the previous result to the measure `Î¼ ((S N) âˆ© â€¢)`.
      -- There exists `t âˆˆ ğ’œ` such that `Î¼ ((S N) âˆ© (s âˆ† t)) < Îµ/2`.
      rcases (measureDense_of_generateFrom_setAglebra_of_finite
        (Î¼ := Î¼.restrict (T N)) hğ’œ hgen).approx s ms
        (ne_of_lt (lt_of_le_of_lt (Î¼.restrict_apply_le _ s) hÎ¼s.lt_top))
        (Îµ / 2) (by linarith [Îµ_pos])
        with âŸ¨t, t_mem, htâŸ©
      -- We can then use `t âˆ© (S N)`, because `S N âˆˆ ğ’œ` by hypothesis.
      -- `Î¼ (s âˆ† (t âˆ© S N))`
      --   `â‰¤ Î¼ (s âˆ† (s âˆ© S N)) + Î¼ ((s âˆ© S N) âˆ† (t âˆ© S N))`
      --   `= Î¼ s - Î¼ (s âˆ© S N) + Î¼ (s âˆ† t) âˆ© S N) < Îµ`.
      refine âŸ¨t âˆ© T N, hğ’œ.inter_mem t_mem (T_mem N), ?_âŸ©
      calc
        Î¼ (s âˆ† (t âˆ© T N))
          â‰¤ Î¼ (s \ (s âˆ© T N)) + Î¼ ((s âˆ† t) âˆ© T N) := by
              rw [â† symmDiff_of_le (inter_subset_left ..), symmDiff_comm _ s,
                inter_symmDiff_distrib_right]
              exact measure_symmDiff_le _ _ _
        _ < ENNReal.ofReal (Îµ / 2) + ENNReal.ofReal (Îµ / 2) := by
              apply ENNReal.add_lt_add
              Â· rw [measure_diff
                    (inter_subset_left ..)
                    (ms.inter (hgen â–¸ measurableSet_generateFrom (T_mem N)))
                    (ne_top_of_le_ne_top hÎ¼s (measure_mono (inter_subset_left ..))),
                  lt_ofReal_iff_toReal_lt (sub_ne_top hÎ¼s),
                  toReal_sub_of_le (measure_mono (inter_subset_left ..)) hÎ¼s]
                apply lt_of_le_of_lt (sub_le_dist ..)
                nth_rw 1 [â† univ_inter s]
                rw [inter_comm s, dist_comm, â† T_spanning, iUnion_inter _ T]
                apply hN N (le_refl _)
              Â· rwa [â† Î¼.restrict_apply' (hgen â–¸ measurableSet_generateFrom (T_mem N))]
        _ = ENNReal.ofReal Îµ := by
              rw [â† ofReal_add (by linarith [Îµ_pos]) (by linarith [Îµ_pos]), add_halves]
    Â· exact fun n â†¦ ne_top_of_le_ne_top hÎ¼s (measure_mono (inter_subset_right ..))
    Â· exact ne_top_of_le_ne_top hÎ¼s
        (measure_mono (iUnion_subset (fun i â†¦ inter_subset_right ..)))

end MeasureDense

section SeparableMeasureSpace

/-! ### Definition of a separable measure space, sufficient condition -/

/-- A measure space `X` is separable if it admits a countable and measure-dense family of sets.

The term "separable" is justified by the fact that the definition translates to the usual notion
of separability in the metric space made by constant indicators equipped with the `LÂ¹` norm. -/
class SeparableMeasureSpace (Î¼ : Measure X) : Prop :=
  exists_countable_measureDense : âˆƒ ğ’œ, ğ’œ.Countable âˆ§ Î¼.MeasureDense ğ’œ

/-- By definition, a separable measure space admits a countable and measure-dense family of sets. -/
theorem exists_countable_measureDense [SeparableMeasureSpace Î¼] :
    âˆƒ ğ’œ, ğ’œ.Countable âˆ§ Î¼.MeasureDense ğ’œ :=
  SeparableMeasureSpace.exists_countable_measureDense

/-- If a measurable space is countably generated and equipped with a `Ïƒ`-finite measure, then it
is separable. -/
instance instSeparableMeasureSapaceCountablyGeneratedSigmaFinite [CountablyGenerated X]
    [SigmaFinite Î¼] : SeparableMeasureSpace Î¼ where
  exists_countable_measureDense := by
    have h := countable_countableGeneratingSet (Î± := X)
    have hgen := generateFrom_countableGeneratingSet (Î± := X)
    let ğ’œ := (countableGeneratingSet X) âˆª {Î¼.toFiniteSpanningSetsIn.set n | n : â„•}
    have count_ğ’œ : ğ’œ.Countable :=
      countable_union.2 âŸ¨h, countable_iff_exists_subset_range.2
        âŸ¨Î¼.toFiniteSpanningSetsIn.set, fun _ hx â†¦ hxâŸ©âŸ©
    refine âŸ¨generateSetAlgebra ğ’œ, countable_generateSetAlgebra count_ğ’œ,
      measureDense_of_generateFrom_setAglebra_of_sigmaFinite isSetAlgebra_generateSetAlgebra
      {
        set := Î¼.toFiniteSpanningSetsIn.set
        set_mem := fun n â†¦ self_subset_generateSetAlgebra (ğ’œ := ğ’œ) <| Or.inr âŸ¨n, rflâŸ©
        finite := Î¼.toFiniteSpanningSetsIn.finite
        spanning := Î¼.toFiniteSpanningSetsIn.spanning
      }
      (le_antisymm ?_ (generateFrom_le (fun s hs â†¦ ?_)))âŸ©
    Â· rw [â† hgen]
      exact generateFrom_mono <| le_trans self_subset_generateSetAlgebra <|
        generateSetAlgebra_mono <| subset_union_left ..
    Â· induction hs with
      | @base t t_mem =>
        rcases t_mem with t_mem | âŸ¨n, rflâŸ©
        Â· exact hgen â–¸ measurableSet_generateFrom t_mem
        Â· exact Î¼.toFiniteSpanningSetsIn.set_mem n
      | empty => exact MeasurableSet.empty
      | @compl t _ t_mem => exact MeasurableSet.compl t_mem
      | @union t u _ _ t_mem u_mem => exact MeasurableSet.union t_mem u_mem

end SeparableMeasureSpace

section SecondCountableLp

/-! ### A sufficient condition for $L^p$ spaces to be second-countable -/

/-- If a measure space `X` is separable (in particular if it is countably generated and `Ïƒ`-finite),
if `E` is a second-countable `NormedAddCommGroup`, and if `1 â‰¤ p < +âˆ`,
then the associated `Láµ–` space is second-countable. -/
instance instSecondCountableLp [SeparableMeasureSpace Î¼] [SecondCountableTopology E] :
    SecondCountableTopology (Lp E p Î¼) := by
  -- It is enough to show that the space is separable, i.e. admits a countable and dense susbet.
  refine @UniformSpace.secondCountable_of_separable _ _ _ ?_
  -- There exists a countable and measure-dense family, and we can keep only the sets with finite
  -- measure while preserving the two properties. This family is denoted `ğ’œâ‚€`.
  rcases exists_countable_measureDense (Î¼ := Î¼) with âŸ¨ğ’œ, count_ğ’œ, hğ’œâŸ©
  have hğ’œâ‚€ := fin_meas_measureDense_of_measureDense hğ’œ
  set ğ’œâ‚€ := {s | s âˆˆ ğ’œ âˆ§ Î¼ s â‰  âˆ}
  have count_ğ’œâ‚€ : ğ’œâ‚€.Countable := count_ğ’œ.mono fun _ âŸ¨h, _âŸ© â†¦ h
  -- `1 â‰¤ p` so `p â‰  0`, we prove it now as it is often needed.
  have p_ne_zero : p â‰  0 := ne_of_gt <| lt_of_lt_of_le (by norm_num) one_le_p.elim
  -- `E` is second-countable, therefore separable and admits a countable and dense subset `u`.
  rcases exists_countable_dense E with âŸ¨u, countable_u, dense_uâŸ©
  -- The countable and dense subset of `Láµ–` we are going to build is the set of finite sums of
  -- constant indicators with support in `ğ’œâ‚€`, and is denoted `D`. To make manipulations easier,
  -- we define the function `key` which given an integer `n` and two families of `n` elements
  -- in `u` and `ğ’œâ‚€` associates the corresponding sum.
  let key : (n : â„•) â†’ (Fin n â†’ u) â†’ (Fin n â†’ ğ’œâ‚€) â†’ (Lp E p Î¼) :=
    fun n d s â†¦ âˆ‘ i, indicatorConstLp p (hğ’œâ‚€.measurable (s i) (Subtype.mem (s i)))
      (s i).2.2 (d i : E)
  let D := {s : Lp E p Î¼ | âˆƒ n d A, s = key n d A}
  refine âŸ¨D, ?_, ?_âŸ©
  Â· -- Countability directly follows from countability of `u` and `ğ’œâ‚€`. The function `f` below
    -- is the uncurryfied version of `key`, which is easier to manipulate as countability of the
    -- domain is automatically infered.
    let f : (Î£ n : â„•, (Fin n â†’ u) Ã— (Fin n â†’ ğ’œâ‚€)) â†’ Lp E p Î¼ := fun ndA â†¦ key ndA.1 ndA.2.1 ndA.2.2
    have := count_ğ’œâ‚€.to_subtype
    have := countable_u.to_subtype
    have : D âŠ† range f := by
      rintro - âŸ¨n, d, A, rflâŸ©
      use âŸ¨n, (d, A)âŸ©
    exact (countable_range f).mono this
  Â· -- Let's turn to the density. Thanks to the density of simple functions in `Láµ–`, it is enough
    -- to show that the closure of `D` contains constant indicators which are in `Láµ–` (i. e. the
    -- set has finite measure), is closed by sum and closed.
    -- This is given by `Lp.induction`.
    intro f
    apply Lp.induction p_ne_top.elim (P := fun f â†¦ f âˆˆ closure D)
    Â· intro a s ms hÎ¼s
      -- We want to approximate `a â€¢ ğŸ™â‚›`.
      apply ne_of_lt at hÎ¼s
      rw [SeminormedAddCommGroup.mem_closure_iff]
      intro Îµ Îµ_pos
      have Î¼s_pow_nonneg : 0 â‰¤ (Î¼ s).toReal ^ (1 / p.toReal) :=
        Real.rpow_nonneg ENNReal.toReal_nonneg _
      -- To do so, we first pick `b âˆˆ u` such that `â€–a - bâ€– < Îµ / (3 * (1 + (Î¼ s)^(1/p)))`.
      have approx_a_pos : 0 < Îµ / (3 * (1 + (Î¼ s).toReal ^ (1 / p.toReal))) :=
        div_pos Îµ_pos (by linarith [Î¼s_pow_nonneg])
      have âŸ¨b, b_mem, hbâŸ© := SeminormedAddCommGroup.mem_closure_iff.1 (dense_u a) _ approx_a_pos
      -- Then we pick `t âˆˆ ğ’œâ‚€` such that `Î¼ (s âˆ† t) < (Îµ / 3 * (1 + â€–bâ€–))^p`.
      have approx_s_pos : 0 < (Îµ / (3 * (1 + â€–bâ€–))) ^ p.toReal :=
        Real.rpow_pos_of_pos (div_pos Îµ_pos (by linarith [norm_nonneg b])) _
      rcases hğ’œâ‚€.approx s ms hÎ¼s _ approx_s_pos with âŸ¨t, ht, hÎ¼stâŸ©
      have mt := hğ’œâ‚€.measurable t ht
      have hÎ¼t := ht.2
      -- We now show that `â€–a â€¢ ğŸ™â‚› - b â€¢ ğŸ™â‚œâ€–â‚š < Îµ`, as follows:
      -- `â€–a â€¢ ğŸ™â‚› - b â€¢ ğŸ™â‚œâ€–â‚š`
      --   `= â€–a â€¢ ğŸ™â‚› - b â€¢ ğŸ™â‚› + b â€¢ ğŸ™â‚› - b â€¢ ğŸ™â‚œâ€–â‚š`
      --   `â‰¤ â€–a - bâ€– * â€–ğŸ™â‚›â€–â‚š + â€–bâ€– * â€–ğŸ™â‚› - ğŸ™â‚œâ€–â‚š`
      --   `= â€–a - bâ€– * (Î¼ s)^(1/p) + â€–bâ€– * (Î¼ (s âˆ† t))^(1/p)`
      --   `< Îµ * (Î¼ s)^(1/p) / (3 * (1 + (Î¼ s)^(1/p))) + â€–bâ€– * ((Îµ / (3 * (1 + â€–bâ€–)))^p)^(1/p)`
      --   `â‰¤ Îµ / 3 + Îµ / 3 < Îµ`.
      refine âŸ¨indicatorConstLp p mt hÎ¼t b,
        âŸ¨1, fun _ â†¦ âŸ¨b, b_memâŸ©, fun _ â†¦ âŸ¨t, htâŸ©, by simp [key]âŸ©, ?_âŸ©
      rw [Lp.simpleFunc.coe_indicatorConst,
        â† sub_add_sub_cancel _ (indicatorConstLp p ms hÎ¼s b)]
      refine lt_of_le_of_lt (b := Îµ / 3 + Îµ / 3) (norm_add_le_of_le ?_ ?_) (by linarith [Îµ_pos])
      Â· rw [indicatorConstLp_sub, norm_indicatorConstLp p_ne_zero p_ne_top.elim]
        calc
          â€–a - bâ€– * (Î¼ s).toReal ^ (1 / p.toReal)
            â‰¤ (Îµ / (3 * (1 + (Î¼ s).toReal ^ (1 / p.toReal)))) * (Î¼ s).toReal ^ (1 / p.toReal) :=
                mul_le_mul_of_nonneg_right (le_of_lt hb) Î¼s_pow_nonneg
          _ â‰¤ Îµ / 3 := by
              rw [â† mul_one (Îµ / 3), div_mul_eq_div_mul_one_div, mul_assoc, one_div_mul_eq_div]
              exact mul_le_mul_of_nonneg_left
                ((div_le_one (by linarith [Î¼s_pow_nonneg])).2 (by linarith))
                (by linarith [Îµ_pos])
      Â· rw [norm_indicatorConstLp_sub mt hÎ¼t p_ne_zero p_ne_top.elim]
        have : (Î¼ (s âˆ† t)).toReal ^ (1 / p.toReal) â‰¤ Îµ / (3 * ( 1 + â€–bâ€–)) := by
          rw [â† rpow_le_rpow_iff (rpow_nonneg toReal_nonneg _)
              (div_nonneg (le_of_lt Îµ_pos) (by linarith [norm_nonneg b]))
              (toReal_pos p_ne_zero p_ne_top.elim), one_div,
            rpow_inv_rpow toReal_nonneg (toReal_ne_zero.2 âŸ¨p_ne_zero, p_ne_top.elimâŸ©),
            â† toReal_ofReal <| le_of_lt approx_s_pos]
          exact toReal_mono ofReal_ne_top (le_of_lt hÎ¼st)
        calc
          â€–bâ€– * (Î¼ (s âˆ† t)).toReal ^ ( 1 / p.toReal)
            â‰¤ â€–bâ€– * (Îµ / (3 * ( 1 + â€–bâ€–))) := mul_le_mul_of_nonneg_left this (norm_nonneg ..)
          _ â‰¤ Îµ / 3 := by
              rw [â† mul_one (Îµ / 3), div_mul_eq_div_mul_one_div, â† mul_assoc, mul_comm â€–bâ€–,
                mul_assoc, mul_one_div]
              exact mul_le_mul_of_nonneg_left
                ((div_le_one (by linarith [norm_nonneg b])).2 (by linarith))
                (by linarith [Îµ_pos])
    Â· -- Now we have to show that the closure of `D` is closed by sum. Let `f` and `g` be two
      -- functions in `Láµ–` which are also in the closure of `D`.
      rintro f g hf hg - f_mem g_mem
      rw [SeminormedAddCommGroup.mem_closure_iff] at *
      intro Îµ Îµ_pos
      -- For `Îµ > 0`, there exists `bf, bg âˆˆ D` such that `â€–f - bfâ€–â‚š < Îµ/2` and `â€–g - bgâ€–â‚š < Îµ/2`.
      rcases f_mem (Îµ / 2) (by linarith [Îµ_pos]) with âŸ¨bf, âŸ¨nf, df, sf, bf_eqâŸ©, hbfâŸ©
      rcases g_mem (Îµ / 2) (by linarith [Îµ_pos]) with âŸ¨bg, âŸ¨ng, dg, sg, bg_eqâŸ©, hbgâŸ©
      -- It is obvious that `D` is closed by sum, it suffices to concatenate the family of
      -- elements of `u` and the family of elements of `ğ’œâ‚€`.
      let d := fun i : Fin (nf + ng) â†¦ if h : i < nf
        then df (Fin.castLT i h)
        else dg (Fin.subNat nf (Fin.cast (Nat.add_comm ..) i) (le_of_not_gt h))
      let s := fun i : Fin (nf + ng) â†¦ if h : i < nf
        then sf (Fin.castLT i h)
        else sg (Fin.subNat nf (Fin.cast (Nat.add_comm ..) i) (le_of_not_gt h))
      -- So we can use `bf + bg`.
      refine âŸ¨bf + bg, âŸ¨nf + ng, d, s, ?_âŸ©, ?_âŸ©
      Â· simp [key, d, s, Fin.sum_univ_add, bf_eq, bg_eq]
      Â· -- We have
        -- `â€–f + g - (bf + bg)â€–â‚š`
        --   `â‰¤ â€–f - bfâ€–â‚š + â€–g - bgâ€–â‚š`
        --   `< Îµ/2 + Îµ/2 = Îµ`.
        calc
          â€–Memâ„’p.toLp f hf + Memâ„’p.toLp g hg - (bf + bg)â€–
            = â€–(Memâ„’p.toLp f hf) - bf + ((Memâ„’p.toLp g hg) - bg)â€– := by congr; abel
          _ â‰¤ â€–(Memâ„’p.toLp f hf) - bfâ€– + â€–(Memâ„’p.toLp g hg) - bgâ€– := norm_add_le ..
          _ < Îµ := by linarith [hbf, hbg]
    Â· -- Obviously the closure of `D` is closed.
      exact isClosed_closure

end SecondCountableLp

end MeasureTheory
