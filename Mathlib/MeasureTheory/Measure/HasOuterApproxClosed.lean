/-
Copyright (c) 2022 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction
import Mathlib.Topology.MetricSpace.ThickenedIndicator

/-!
# Spaces where indicators of closed sets have decreasing approximations by continuous functions

In this file we define a typeclass `HasOuterApproxClosed` for topological spaces in which indicator
functions of closed sets have sequences of bounded continuous functions approximating them from
above. All pseudo-emetrizable spaces have this property, see `instHasOuterApproxClosed`.

In spaces with the `HasOuterApproxClosed` property, finite Borel measures are uniquely characterized
by the integrals of bounded continuous functions. Also weak convergence of finite measures and
convergence in distribution for random variables behave somewhat well in spaces with this property.

## Main definitions

* `HasOuterApproxClosed`: the typeclass for topological spaces in which indicator functions of
  closed sets have sequences of bounded continuous functions approximating them.
* `IsClosed.apprSeq`: a (non-constructive) choice of an approximating sequence to the indicator
  function of a closed set.

## Main results

* `instHasOuterApproxClosed`: Any pseudo-emetrizable space has the property `HasOuterApproxClosed`.
* `tendsto_lintegral_apprSeq`: The integrals of the approximating functions to the indicator of a
  closed set tend to the measure of the set.
* `ext_of_forall_lintegral_eq_of_IsFiniteMeasure`: Two finite measures are equal if the integrals
  of all bounded continuous functions with respect to both agree.

-/

open BoundedContinuousFunction MeasureTheory Topology Metric Filter Set ENNReal NNReal
open scoped Topology ENNReal NNReal BoundedContinuousFunction

section auxiliary

namespace MeasureTheory

variable {Î© : Type*} [TopologicalSpace Î©] [MeasurableSpace Î©] [OpensMeasurableSpace Î©]

/-- A bounded convergence theorem for a finite measure:
If bounded continuous non-negative functions are uniformly bounded by a constant and tend to a
limit, then their integrals against the finite measure tend to the integral of the limit.
This formulation assumes:
* the functions tend to a limit along a countably generated filter;
* the limit is in the almost everywhere sense;
* boundedness holds almost everywhere;
* integration is `MeasureTheory.lintegral`, i.e., the functions and their integrals are
  `â„â‰¥0âˆ`-valued.
-/
theorem tendsto_lintegral_nn_filter_of_le_const {Î¹ : Type*} {L : Filter Î¹} [L.IsCountablyGenerated]
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] {fs : Î¹ â†’ Î© â†’áµ‡ â„â‰¥0} {c : â„â‰¥0}
    (fs_le_const : âˆ€á¶  i in L, âˆ€áµ Ï‰ : Î© âˆ‚Î¼, fs i Ï‰ â‰¤ c) {f : Î© â†’ â„â‰¥0}
    (fs_lim : âˆ€áµ Ï‰ : Î© âˆ‚Î¼, Tendsto (fun i â†¦ fs i Ï‰) L (ğ“ (f Ï‰))) :
    Tendsto (fun i â†¦ âˆ«â» Ï‰, fs i Ï‰ âˆ‚Î¼) L (ğ“ (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼)) := by
  refine tendsto_lintegral_filter_of_dominated_convergence (fun _ â†¦ c)
    (Eventually.of_forall fun i â†¦ (ENNReal.continuous_coe.comp (fs i).continuous).measurable) ?_
    (@lintegral_const_lt_top _ _ Î¼ _ _ (@ENNReal.coe_ne_top c)).ne ?_
  Â· simpa only [Function.comp_apply, ENNReal.coe_le_coe] using fs_le_const
  Â· simpa only [Function.comp_apply, ENNReal.tendsto_coe] using fs_lim

/-- If bounded continuous functions tend to the indicator of a measurable set and are
uniformly bounded, then their integrals against a finite measure tend to the measure of the set.
This formulation assumes:
* the functions tend to a limit along a countably generated filter;
* the limit is in the almost everywhere sense;
* boundedness holds almost everywhere.
-/
theorem measure_of_cont_bdd_of_tendsto_filter_indicator {Î¹ : Type*} {L : Filter Î¹}
    [L.IsCountablyGenerated] (Î¼ : Measure Î©)
    [IsFiniteMeasure Î¼] {c : â„â‰¥0} {E : Set Î©} (E_mble : MeasurableSet E) (fs : Î¹ â†’ Î© â†’áµ‡ â„â‰¥0)
    (fs_bdd : âˆ€á¶  i in L, âˆ€áµ Ï‰ : Î© âˆ‚Î¼, fs i Ï‰ â‰¤ c)
    (fs_lim : âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun i â†¦ fs i Ï‰) L (ğ“ (indicator E (fun _ â†¦ (1 : â„â‰¥0)) Ï‰))) :
    Tendsto (fun n â†¦ lintegral Î¼ fun Ï‰ â†¦ fs n Ï‰) L (ğ“ (Î¼ E)) := by
  convert tendsto_lintegral_nn_filter_of_le_const Î¼ fs_bdd fs_lim
  have aux : âˆ€ Ï‰, indicator E (fun _ â†¦ (1 : â„â‰¥0âˆ)) Ï‰ = â†‘(indicator E (fun _ â†¦ (1 : â„â‰¥0)) Ï‰) :=
    fun Ï‰ â†¦ by simp only [ENNReal.coe_indicator, ENNReal.coe_one]
  simp_rw [â† aux, lintegral_indicator E_mble]
  simp only [lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]

/-- If a sequence of bounded continuous functions tends to the indicator of a measurable set and
the functions are uniformly bounded, then their integrals against a finite measure tend to the
measure of the set.

A similar result with more general assumptions is
`MeasureTheory.measure_of_cont_bdd_of_tendsto_filter_indicator`.
-/
theorem measure_of_cont_bdd_of_tendsto_indicator
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] {c : â„â‰¥0} {E : Set Î©} (E_mble : MeasurableSet E)
    (fs : â„• â†’ Î© â†’áµ‡ â„â‰¥0) (fs_bdd : âˆ€ n Ï‰, fs n Ï‰ â‰¤ c)
    (fs_lim : Tendsto (fun n Ï‰ â†¦ fs n Ï‰) atTop (ğ“ (indicator E fun _ â†¦ (1 : â„â‰¥0)))) :
    Tendsto (fun n â†¦ lintegral Î¼ fun Ï‰ â†¦ fs n Ï‰) atTop (ğ“ (Î¼ E)) := by
  have fs_lim' :
    âˆ€ Ï‰, Tendsto (fun n : â„• â†¦ (fs n Ï‰ : â„â‰¥0)) atTop (ğ“ (indicator E (fun _ â†¦ (1 : â„â‰¥0)) Ï‰)) := by
    rw [tendsto_pi_nhds] at fs_lim
    exact fun Ï‰ â†¦ fs_lim Ï‰
  apply measure_of_cont_bdd_of_tendsto_filter_indicator Î¼ E_mble fs
    (Eventually.of_forall fun n â†¦ Eventually.of_forall (fs_bdd n)) (Eventually.of_forall fs_lim')

/-- The integrals of thickened indicators of a closed set against a finite measure tend to the
measure of the closed set if the thickening radii tend to zero. -/
theorem tendsto_lintegral_thickenedIndicator_of_isClosed {Î© : Type*} [MeasurableSpace Î©]
    [PseudoEMetricSpace Î©] [OpensMeasurableSpace Î©] (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] {F : Set Î©}
    (F_closed : IsClosed F) {Î´s : â„• â†’ â„} (Î´s_pos : âˆ€ n, 0 < Î´s n)
    (Î´s_lim : Tendsto Î´s atTop (ğ“ 0)) :
    Tendsto (fun n â†¦ lintegral Î¼ fun Ï‰ â†¦ (thickenedIndicator (Î´s_pos n) F Ï‰ : â„â‰¥0âˆ)) atTop
      (ğ“ (Î¼ F)) := by
  apply measure_of_cont_bdd_of_tendsto_indicator Î¼ F_closed.measurableSet
    (fun n â†¦ thickenedIndicator (Î´s_pos n) F) fun n Ï‰ â†¦ thickenedIndicator_le_one (Î´s_pos n) F Ï‰
  have key := thickenedIndicator_tendsto_indicator_closure Î´s_pos Î´s_lim F
  rwa [F_closed.closure_eq] at key

end MeasureTheory -- namespace

end auxiliary -- section

section HasOuterApproxClosed

/-- A type class for topological spaces in which the indicator functions of closed sets can be
approximated pointwise from above by a sequence of bounded continuous functions. -/
class HasOuterApproxClosed (X : Type*) [TopologicalSpace X] : Prop where
  exAppr : âˆ€ (F : Set X), IsClosed F â†’ âˆƒ (fseq : â„• â†’ (X â†’áµ‡ â„â‰¥0)),
    (âˆ€ n x, fseq n x â‰¤ 1) âˆ§ (âˆ€ n x, x âˆˆ F â†’ 1 â‰¤ fseq n x) âˆ§
    Tendsto (fun n : â„• â†¦ (fun x â†¦ fseq n x)) atTop (ğ“ (indicator F fun _ â†¦ (1 : â„â‰¥0)))

namespace HasOuterApproxClosed

variable {X : Type*} [TopologicalSpace X] [HasOuterApproxClosed X]
variable {F : Set X} (hF : IsClosed F)

/-- A sequence of continuous functions `X â†’ [0,1]` tending to the indicator of a closed set. -/
noncomputable def _root_.IsClosed.apprSeq : â„• â†’ (X â†’áµ‡ â„â‰¥0) :=
  Exists.choose (HasOuterApproxClosed.exAppr F hF)

lemma apprSeq_apply_le_one (n : â„•) (x : X) :
    hF.apprSeq n x â‰¤ 1 :=
  (Exists.choose_spec (HasOuterApproxClosed.exAppr F hF)).1 n x

lemma apprSeq_apply_eq_one (n : â„•) {x : X} (hxF : x âˆˆ F) :
    hF.apprSeq n x = 1 :=
  le_antisymm (apprSeq_apply_le_one _ _ _)
    ((Exists.choose_spec (HasOuterApproxClosed.exAppr F hF)).2.1 n x hxF)

lemma tendsto_apprSeq :
    Tendsto (fun n : â„• â†¦ (fun x â†¦ hF.apprSeq n x)) atTop (ğ“ (indicator F fun _ â†¦ (1 : â„â‰¥0))) :=
  (Exists.choose_spec (HasOuterApproxClosed.exAppr F hF)).2.2

lemma indicator_le_apprSeq (n : â„•) :
    indicator F (fun _ â†¦ 1) â‰¤ hF.apprSeq n := by
  intro x
  by_cases hxF : x âˆˆ F
  Â· simp only [hxF, indicator_of_mem, apprSeq_apply_eq_one hF n, le_refl]
  Â· simp only [hxF, not_false_eq_true, indicator_of_notMem, zero_le]

/-- The measure of a closed set is at most the integral of any function in a decreasing
approximating sequence to the indicator of the set. -/
theorem measure_le_lintegral [MeasurableSpace X] [OpensMeasurableSpace X] (Î¼ : Measure X) (n : â„•) :
    Î¼ F â‰¤ âˆ«â» x, (hF.apprSeq n x : â„â‰¥0âˆ) âˆ‚Î¼ := by
  convert_to âˆ«â» x, (F.indicator (fun _ â†¦ (1 : â„â‰¥0âˆ))) x âˆ‚Î¼ â‰¤ âˆ«â» x, hF.apprSeq n x âˆ‚Î¼
  Â· rw [lintegral_indicator hF.measurableSet]
    simp only [lintegral_one, MeasurableSet.univ, Measure.restrict_apply, univ_inter]
  Â· apply lintegral_mono
    intro x
    by_cases hxF : x âˆˆ F
    Â· simp only [hxF, indicator_of_mem, apprSeq_apply_eq_one hF n hxF, ENNReal.coe_one, le_refl]
    Â· simp only [hxF, not_false_eq_true, indicator_of_notMem, zero_le]

/-- The integrals along a decreasing approximating sequence to the indicator of a closed set
tend to the measure of the closed set. -/
lemma tendsto_lintegral_apprSeq [MeasurableSpace X] [OpensMeasurableSpace X]
    (Î¼ : Measure X) [IsFiniteMeasure Î¼] :
    Tendsto (fun n â†¦ âˆ«â» x, hF.apprSeq n x âˆ‚Î¼) atTop (ğ“ ((Î¼ : Measure X) F)) :=
  measure_of_cont_bdd_of_tendsto_indicator Î¼ hF.measurableSet hF.apprSeq
    (apprSeq_apply_le_one hF) (tendsto_apprSeq hF)

end HasOuterApproxClosed --namespace

noncomputable instance (X : Type*) [TopologicalSpace X]
    [TopologicalSpace.PseudoMetrizableSpace X] : HasOuterApproxClosed X := by
  letI : PseudoMetricSpace X := TopologicalSpace.pseudoMetrizableSpacePseudoMetric X
  refine âŸ¨fun F hF â†¦ ?_âŸ©
  use fun n â†¦ thickenedIndicator (Î´ := (1 : â„) / (n + 1)) Nat.one_div_pos_of_nat F
  refine âŸ¨?_, âŸ¨?_, ?_âŸ©âŸ©
  Â· exact fun n x â†¦ thickenedIndicator_le_one Nat.one_div_pos_of_nat F x
  Â· exact fun n x hxF â†¦ one_le_thickenedIndicator_apply X Nat.one_div_pos_of_nat hxF
  Â· have key := thickenedIndicator_tendsto_indicator_closure
              (Î´seq := fun (n : â„•) â†¦ (1 : â„) / (n + 1))
              (fun _ â†¦ Nat.one_div_pos_of_nat) tendsto_one_div_add_atTop_nhds_zero_nat F
    rw [tendsto_pi_nhds] at *
    intro x
    nth_rw 2 [â† IsClosed.closure_eq hF]
    exact key x

namespace MeasureTheory

/-- Two finite measures give equal values to all closed sets if the integrals of all bounded
continuous functions with respect to the two measures agree. -/
theorem measure_isClosed_eq_of_forall_lintegral_eq_of_isFiniteMeasure {Î© : Type*}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [HasOuterApproxClosed Î©]
    [OpensMeasurableSpace Î©] {Î¼ Î½ : Measure Î©} [IsFiniteMeasure Î¼]
    (h : âˆ€ (f : Î© â†’áµ‡ â„â‰¥0), âˆ«â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î½) {F : Set Î©} (F_closed : IsClosed F) :
    Î¼ F = Î½ F := by
  have Î½_finite : IsFiniteMeasure Î½ := by
    constructor
    have whole := h 1
    simp only [BoundedContinuousFunction.coe_one, Pi.one_apply, ENNReal.coe_one, lintegral_const,
      one_mul] at whole
    simp [â† whole]
  have obs_Î¼ := HasOuterApproxClosed.tendsto_lintegral_apprSeq F_closed Î¼
  have obs_Î½ := HasOuterApproxClosed.tendsto_lintegral_apprSeq F_closed Î½
  simp_rw [h] at obs_Î¼
  exact tendsto_nhds_unique obs_Î¼ obs_Î½

/-- Two finite Borel measures are equal if the integrals of all non-negative bounded continuous
functions with respect to both agree. -/
theorem ext_of_forall_lintegral_eq_of_IsFiniteMeasure {Î© : Type*}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [HasOuterApproxClosed Î©]
    [BorelSpace Î©] {Î¼ Î½ : Measure Î©} [IsFiniteMeasure Î¼]
    (h : âˆ€ (f : Î© â†’áµ‡ â„â‰¥0), âˆ«â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î½) :
    Î¼ = Î½ := by
  have key := @measure_isClosed_eq_of_forall_lintegral_eq_of_isFiniteMeasure Î© _ _ _ _ Î¼ Î½ _ h
  apply ext_of_generate_finite _ ?_ isPiSystem_isClosed
  Â· exact fun F F_closed â†¦ key F_closed
  Â· exact key isClosed_univ
  Â· rw [BorelSpace.measurable_eq (Î± := Î©), borel_eq_generateFrom_isClosed]

/-- Two finite Borel measures are equal if the integrals of all bounded continuous functions with
respect to both agree. -/
theorem ext_of_forall_integral_eq_of_IsFiniteMeasure {Î© : Type*}
    [MeasurableSpace Î©] [TopologicalSpace Î©] [HasOuterApproxClosed Î©]
    [BorelSpace Î©] {Î¼ Î½ : Measure Î©} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (h : âˆ€ (f : Î© â†’áµ‡ â„), âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î½) :
    Î¼ = Î½ := by
  apply ext_of_forall_lintegral_eq_of_IsFiniteMeasure
  intro f
  apply (ENNReal.toReal_eq_toReal_iff' (lintegral_lt_top_of_nnreal Î¼ f).ne
      (lintegral_lt_top_of_nnreal Î½ f).ne).mp
  rw [toReal_lintegral_coe_eq_integral f Î¼, toReal_lintegral_coe_eq_integral f Î½]
  exact h âŸ¨âŸ¨fun x => (f x).toReal, Continuous.comp' NNReal.continuous_coe f.continuousâŸ©,
      f.map_bounded'âŸ©

section Prod

open MeasurableSpace

namespace Measure

variable {X Y : Type*}
  {mX : MeasurableSpace X} [TopologicalSpace X] [BorelSpace X] [HasOuterApproxClosed X]
  {mY : MeasurableSpace Y} [TopologicalSpace Y] [BorelSpace Y] [HasOuterApproxClosed Y]
  {Î¼ : Measure X} [IsFiniteMeasure Î¼] {Î½ : Measure Y} [IsFiniteMeasure Î½]
  {Î¾ : Measure (X Ã— Y)}

/-- The product of two finite measures is the only measure `Î¾` such that for all nonnegative
bounded continuous functions `f` and `g` we have
`âˆ«â» z, f z.1 * g z.2 âˆ‚Î¾ = âˆ«â» x, f x âˆ‚Î¼ * âˆ«â» y, g y âˆ‚Î½`. -/
lemma eq_prod_of_boundedContinuousFunction_nnreal
    (h : âˆ€ (f : X â†’áµ‡ â„â‰¥0) (g : Y â†’áµ‡ â„â‰¥0),
      âˆ«â» Ï‰, f Ï‰.1 * g Ï‰.2 âˆ‚Î¾ = (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼) * (âˆ«â» Ï‰, g Ï‰ âˆ‚Î½)) :
    Î¾ = Î¼.prod Î½ := by
  have hÎ¾ : Î¾ univ = (Î¼.prod Î½) univ := by convert h 1 1 <;> simp [â† prod_prod]
  have : IsFiniteMeasure Î¾ := âŸ¨by simp [hÎ¾]âŸ©
  let Ï€ : Set (Set (X Ã— Y)) :=
    {s | âˆƒ (F : Set X) (G : Set Y), IsClosed F âˆ§ IsClosed G âˆ§ s = F Ã—Ë¢ G}
  have hÏ€1 : IsPiSystem Ï€ := by
    rintro - âŸ¨sâ‚, sâ‚‚, hsâ‚, hsâ‚‚, rflâŸ© - âŸ¨tâ‚, tâ‚‚, htâ‚, htâ‚‚, rflâŸ© -
    exact âŸ¨sâ‚ âˆ© tâ‚, sâ‚‚ âˆ© tâ‚‚, hsâ‚.inter htâ‚, hsâ‚‚.inter htâ‚‚, Set.prod_inter_prodâŸ©
  have hÏ€2 : mX.prod mY = generateFrom Ï€ := by
    refine le_antisymm ?_ (generateFrom_le ?_)
    Â· simp_rw [BorelSpace.measurable_eq, borel_eq_generateFrom_isClosed, MeasurableSpace.prod,
        comap_generateFrom]
      refine sup_le (generateFrom_le ?_) (generateFrom_le ?_)
      Â· rintro - âŸ¨s, hs, rflâŸ©
        exact measurableSet_generateFrom âŸ¨s, Set.univ, hs, isClosed_univ, by rw [Set.prod_univ]âŸ©
      Â· rintro - âŸ¨t, ht, rflâŸ©
        exact measurableSet_generateFrom âŸ¨Set.univ, t, isClosed_univ, ht, by rw [Set.univ_prod]âŸ©
    Â· rintro - âŸ¨sâ‚, sâ‚‚, hsâ‚, hsâ‚‚, rflâŸ©
      exact hsâ‚.measurableSet.prod hsâ‚‚.measurableSet
  refine ext_of_generate_finite Ï€ hÏ€2 hÏ€1 ?_ hÎ¾
  rintro - âŸ¨sâ‚, sâ‚‚, hsâ‚, hsâ‚‚, rflâŸ©
  rw [prod_prod]
  have := ENNReal.Tendsto.mul (HasOuterApproxClosed.tendsto_lintegral_apprSeq hsâ‚ Î¼) (by simp)
    (HasOuterApproxClosed.tendsto_lintegral_apprSeq hsâ‚‚ Î½) (by simp)
  refine (tendsto_nhds_unique this ?_).symm
  simp_rw [â† h, â† ENNReal.coe_mul]
  have : Î¾ (sâ‚ Ã—Ë¢ sâ‚‚) = âˆ«â» Ï‰, (sâ‚.indicator 1 Ï‰.1 * sâ‚‚.indicator 1 Ï‰.2 : â„â‰¥0) âˆ‚Î¾ := by
    simp_rw [â† Set.indicator_prod_one,
      â† lintegral_indicator_one (hsâ‚.measurableSet.prod hsâ‚‚.measurableSet)]
    congr with
    simp only [Prod.mk.eta, ENNReal.coe_indicator, Pi.one_apply, ENNReal.coe_one]
    rfl
  rw [this]
  refine tendsto_lintegral_filter_of_dominated_convergence 1 (Eventually.of_forall <| by fun_prop)
    (Eventually.of_forall fun n â†¦ ae_of_all _ fun Ï‰ â†¦ ?_) (by simp) (ae_of_all _ fun _ â†¦ ?_)
  Â· grw [HasOuterApproxClosed.apprSeq_apply_le_one, HasOuterApproxClosed.apprSeq_apply_le_one]
    simp
  exact (ENNReal.continuous_coe.tendsto _).comp <|
    ((tendsto_pi_nhds.1 <| HasOuterApproxClosed.tendsto_apprSeq hsâ‚) _).mul
    ((tendsto_pi_nhds.1 <| HasOuterApproxClosed.tendsto_apprSeq hsâ‚‚) _)

/-- The product of two finite measures is the only finite measure `Î¾` such that for all real
bounded continuous functions `f` and `g` we have
`âˆ« z, f z.1 * g z.2 âˆ‚Î¾ = âˆ« x, f x âˆ‚Î¼ * âˆ« y, g y âˆ‚Î½`. -/
lemma eq_prod_of_boundedContinuousFunction [IsFiniteMeasure Î¾]
    (h : âˆ€ (f : X â†’áµ‡ â„) (g : Y â†’áµ‡ â„),
      âˆ« Ï‰, f Ï‰.1 * g Ï‰.2 âˆ‚Î¾ = (âˆ« Ï‰, f Ï‰ âˆ‚Î¼) * (âˆ« Ï‰, g Ï‰ âˆ‚Î½)) :
    Î¾ = Î¼.prod Î½ := by
  refine eq_prod_of_boundedContinuousFunction_nnreal fun f g â†¦ ?_
  apply (toReal_eq_toReal_iff' (lintegral_lt_top_of_nnreal Î¾
    ((f.compContinuous âŸ¨@Prod.fst X Y, continuous_fstâŸ©) *
      (g.compContinuous âŸ¨@Prod.snd X Y, continuous_sndâŸ©))).ne
    (mul_lt_top (lintegral_lt_top_of_nnreal Î¼ _) (lintegral_lt_top_of_nnreal Î½ _)).ne).1
  simp only [BoundedContinuousFunction.coe_mul, coe_compContinuous, ContinuousMap.coe_mk,
    Pi.mul_apply, Function.comp_apply, ENNReal.coe_mul, toReal_mul]
  have : (âˆ«â» Ï‰, f Ï‰.1 * g Ï‰.2 âˆ‚Î¾).toReal = âˆ« Ï‰, (f Ï‰.1).toReal * (g Ï‰.2).toReal âˆ‚Î¾ := by
    rw [integral_eq_lintegral_of_nonneg_ae]
    Â· simp
    Â· exact Eventually.of_forall fun _ â†¦ by positivity
    exact AEStronglyMeasurable.mul
      (continuous_coe.aestronglyMeasurable.comp_measurable (by fun_prop))
      (continuous_coe.aestronglyMeasurable.comp_measurable (by fun_prop))
  rw [this, toReal_lintegral_coe_eq_integral, toReal_lintegral_coe_eq_integral]
  exact h âŸ¨âŸ¨fun x â†¦ (f x), by fun_propâŸ©, f.map_bounded'âŸ©
    âŸ¨âŸ¨fun x â†¦ (g x), by fun_propâŸ©, g.map_bounded'âŸ©

end Measure

end Prod

end MeasureTheory -- namespace

end HasOuterApproxClosed -- section
