/-
Copyright (c) 2022 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.MeasureTheory.Integral.Lebesgue
import Mathlib.Topology.MetricSpace.ThickenedIndicator

/-!
# Spaces where indicators of closed sets have sequences of continuous approximating from above

In this file we define a typeclass `HasOuterApproxClosed` for topological spaces in which indicator
functions of closed sets have sequences of bounded continuous functions approximating them from
above. All pseudo-emetrizable spaces have this property, see `instHasOuterApproxClosed`.

In spaces with the `HasOuterApproxClosed` property, finite Borel measures are uniquely characterized
by the integrals of bounded continuous functions. Also weak convergence of finite measures and
convergence in distribution for random variables behave somewhat well in spaces with this property.

## Main definitions

 * `HasOuterApproxClosed`: the typeclass for topological spaces in which indicator functions of
   closed sets have sequences of bounded continuous functions approximating them.
 * `IsClosed.apprSeq`: a (non-constructive) choice of an approximating sequence to the indicator
   function of a closed set.

## Main results

 * `instHasOuterApproxClosed`: Any pseudo-emetrizable space has the property `HasOuterApproxClosed`.
 * `tendsto_lintegral_apprSeq`: The integrals of the approximating functions to the indicator of a
   closed set tend to the measure of the set.
 * `ext_of_forall_lintegral_eq_of_IsFiniteMeasure`: Two finite measures are equal if the integrals
   of all bounded continuous functions with respect to both agree.

-/

open MeasureTheory Topology Metric Filter Set ENNReal NNReal
open scoped Topology ENNReal NNReal BoundedContinuousFunction

section auxiliary

namespace MeasureTheory

variable {Œ© : Type*} [TopologicalSpace Œ©] [MeasurableSpace Œ©] [OpensMeasurableSpace Œ©]

/-- A bounded convergence theorem for a finite measure:
If bounded continuous non-negative functions are uniformly bounded by a constant and tend to a
limit, then their integrals against the finite measure tend to the integral of the limit.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere;
 * integration is `MeasureTheory.lintegral`, i.e., the functions and their integrals are
   `‚Ñù‚â•0‚àû`-valued.
-/
theorem tendsto_lintegral_nn_filter_of_le_const {Œπ : Type*} {L : Filter Œπ} [L.IsCountablyGenerated]
    (Œº : Measure Œ©) [IsFiniteMeasure Œº] {fs : Œπ ‚Üí Œ© ‚Üí·µá ‚Ñù‚â•0} {c : ‚Ñù‚â•0}
    (fs_le_const : ‚àÄ·∂† i in L, ‚àÄ·µê œâ : Œ© ‚àÇŒº, fs i œâ ‚â§ c) {f : Œ© ‚Üí ‚Ñù‚â•0}
    (fs_lim : ‚àÄ·µê œâ : Œ© ‚àÇŒº, Tendsto (fun i => fs i œâ) L (ùìù (f œâ))) :
    Tendsto (fun i => ‚à´‚Åª œâ, fs i œâ ‚àÇŒº) L (ùìù (‚à´‚Åª œâ, f œâ ‚àÇŒº)) := by
  refine tendsto_lintegral_filter_of_dominated_convergence (fun _ => c)
    (eventually_of_forall fun i => (ENNReal.continuous_coe.comp (fs i).continuous).measurable) ?_
    (@lintegral_const_lt_top _ _ Œº _ _ (@ENNReal.coe_ne_top c)).ne ?_
  ¬∑ simpa only [Function.comp_apply, ENNReal.coe_le_coe] using fs_le_const
  ¬∑ simpa only [Function.comp_apply, ENNReal.tendsto_coe] using fs_lim
#align measure_theory.finite_measure.tendsto_lintegral_nn_filter_of_le_const MeasureTheory.tendsto_lintegral_nn_filter_of_le_const

/-- If bounded continuous functions tend to the indicator of a measurable set and are
uniformly bounded, then their integrals against a finite measure tend to the measure of the set.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere.
-/
theorem measure_of_cont_bdd_of_tendsto_filter_indicator {Œπ : Type*} {L : Filter Œπ}
    [L.IsCountablyGenerated] [TopologicalSpace Œ©] [OpensMeasurableSpace Œ©] (Œº : Measure Œ©)
    [IsFiniteMeasure Œº] {c : ‚Ñù‚â•0} {E : Set Œ©} (E_mble : MeasurableSet E) (fs : Œπ ‚Üí Œ© ‚Üí·µá ‚Ñù‚â•0)
    (fs_bdd : ‚àÄ·∂† i in L, ‚àÄ·µê œâ : Œ© ‚àÇŒº, fs i œâ ‚â§ c)
    (fs_lim : ‚àÄ·µê œâ : Œ© ‚àÇŒº, Tendsto (fun i : Œπ => ((‚áë) : (Œ© ‚Üí·µá ‚Ñù‚â•0) ‚Üí Œ© ‚Üí ‚Ñù‚â•0) (fs i) œâ) L
      (ùìù (indicator E (fun _ => (1 : ‚Ñù‚â•0)) œâ))) :
    Tendsto (fun n => lintegral Œº fun œâ => fs n œâ) L (ùìù (Œº E)) := by
  convert tendsto_lintegral_nn_filter_of_le_const Œº fs_bdd fs_lim
  have aux : ‚àÄ œâ, indicator E (fun _ => (1 : ‚Ñù‚â•0‚àû)) œâ = ‚Üë(indicator E (fun _ => (1 : ‚Ñù‚â•0)) œâ) :=
    fun œâ => by simp only [ENNReal.coe_indicator, ENNReal.coe_one]
  simp_rw [‚Üê aux, lintegral_indicator _ E_mble]
  simp only [lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
#align measure_theory.measure_of_cont_bdd_of_tendsto_filter_indicator MeasureTheory.measure_of_cont_bdd_of_tendsto_filter_indicator

/-- If a sequence of bounded continuous functions tends to the indicator of a measurable set and
the functions are uniformly bounded, then their integrals against a finite measure tend to the
measure of the set.

A similar result with more general assumptions is
`MeasureTheory.measure_of_cont_bdd_of_tendsto_filter_indicator`.
-/
theorem measure_of_cont_bdd_of_tendsto_indicator [OpensMeasurableSpace Œ©]
    (Œº : Measure Œ©) [IsFiniteMeasure Œº] {c : ‚Ñù‚â•0} {E : Set Œ©} (E_mble : MeasurableSet E)
    (fs : ‚Ñï ‚Üí Œ© ‚Üí·µá ‚Ñù‚â•0) (fs_bdd : ‚àÄ n œâ, fs n œâ ‚â§ c)
    (fs_lim : Tendsto (fun n : ‚Ñï => ((‚áë) : (Œ© ‚Üí·µá ‚Ñù‚â•0) ‚Üí Œ© ‚Üí ‚Ñù‚â•0) (fs n)) atTop
      (ùìù (indicator E fun _ => (1 : ‚Ñù‚â•0)))) :
    Tendsto (fun n => lintegral Œº fun œâ => fs n œâ) atTop (ùìù (Œº E)) := by
  have fs_lim' :
    ‚àÄ œâ, Tendsto (fun n : ‚Ñï => (fs n œâ : ‚Ñù‚â•0)) atTop (ùìù (indicator E (fun _ => (1 : ‚Ñù‚â•0)) œâ)) := by
    rw [tendsto_pi_nhds] at fs_lim
    exact fun œâ => fs_lim œâ
  apply measure_of_cont_bdd_of_tendsto_filter_indicator Œº E_mble fs
    (eventually_of_forall fun n => eventually_of_forall (fs_bdd n)) (eventually_of_forall fs_lim')
#align measure_theory.measure_of_cont_bdd_of_tendsto_indicator MeasureTheory.measure_of_cont_bdd_of_tendsto_indicator

/-- The integrals of thickened indicators of a closed set against a finite measure tend to the
measure of the closed set if the thickening radii tend to zero.
-/
theorem tendsto_lintegral_thickenedIndicator_of_isClosed {Œ© : Type*} [MeasurableSpace Œ©]
    [PseudoEMetricSpace Œ©] [OpensMeasurableSpace Œ©] (Œº : Measure Œ©) [IsFiniteMeasure Œº] {F : Set Œ©}
    (F_closed : IsClosed F) {Œ¥s : ‚Ñï ‚Üí ‚Ñù} (Œ¥s_pos : ‚àÄ n, 0 < Œ¥s n)
    (Œ¥s_lim : Tendsto Œ¥s atTop (ùìù 0)) :
    Tendsto (fun n => lintegral Œº fun œâ => (thickenedIndicator (Œ¥s_pos n) F œâ : ‚Ñù‚â•0‚àû)) atTop
      (ùìù (Œº F)) := by
  apply measure_of_cont_bdd_of_tendsto_indicator Œº F_closed.measurableSet
    (fun n => thickenedIndicator (Œ¥s_pos n) F) fun n œâ => thickenedIndicator_le_one (Œ¥s_pos n) F œâ
  have key := thickenedIndicator_tendsto_indicator_closure Œ¥s_pos Œ¥s_lim F
  rwa [F_closed.closure_eq] at key
#align measure_theory.tendsto_lintegral_thickened_indicator_of_is_closed MeasureTheory.tendsto_lintegral_thickenedIndicator_of_isClosed

end MeasureTheory -- namespace

end auxiliary -- section

section HasOuterApproxClosed

/-- A type class for topological spaces in which the indicator functions of closed sets can be
approximated pointwise from above by a sequence of bounded continuous functions. -/
class HasOuterApproxClosed (X : Type _) [TopologicalSpace X] where
  exAppr : ‚àÄ (F : Set X), IsClosed F ‚Üí ‚àÉ (fseq: ‚Ñï ‚Üí (X ‚Üí·µá ‚Ñù‚â•0)),
    (‚àÄ n x, fseq n x ‚â§ 1) ‚àß (‚àÄ n x, x ‚àà F ‚Üí 1 ‚â§ fseq n x) ‚àß
    Tendsto (fun n : ‚Ñï ‚Ü¶ (fun x ‚Ü¶ fseq n x)) atTop (ùìù (indicator F fun _ ‚Ü¶ (1 : ‚Ñù‚â•0)))

namespace HasOuterApproxClosed

variable {X : Type _} [TopologicalSpace X] [HasOuterApproxClosed X]
variable {F : Set X} (hF : IsClosed F)

/-- A sequence of continuous functions `X ‚Üí [0,1]` tending to the indicator of a closed set. -/
noncomputable def _root_.IsClosed.apprSeq : ‚Ñï ‚Üí (X ‚Üí·µá ‚Ñù‚â•0) :=
  Exists.choose (HasOuterApproxClosed.exAppr F hF)

lemma apprSeq_apply_le_one (n : ‚Ñï) (x : X) :
    hF.apprSeq n x ‚â§ 1 :=
  (Exists.choose_spec (HasOuterApproxClosed.exAppr F hF)).1 n x

lemma one_le_apprSeq_apply (n : ‚Ñï) {x : X} (hxF : x ‚àà F) :
    1 ‚â§ hF.apprSeq n x :=
  (Exists.choose_spec (HasOuterApproxClosed.exAppr F hF)).2.1 n x hxF

lemma tendsto_apprSeq :
    Tendsto (fun n : ‚Ñï ‚Ü¶ (fun x ‚Ü¶ hF.apprSeq n x)) atTop (ùìù (indicator F fun _ ‚Ü¶ (1 : ‚Ñù‚â•0))) :=
  (Exists.choose_spec (HasOuterApproxClosed.exAppr F hF)).2.2

lemma indicator_le_apprSeq (n : ‚Ñï) :
    indicator F (fun _ ‚Ü¶ 1) ‚â§ hF.apprSeq n := by
  intro x
  by_cases hxF : x ‚àà F
  ¬∑ simp only [hxF, indicator_of_mem, one_le_apprSeq_apply hF n]
  ¬∑ simp only [hxF, not_false_eq_true, indicator_of_not_mem, zero_le]

lemma apprSeq_apply_eq_one (n : ‚Ñï) {x : X} (hx : x ‚àà F) :
    hF.apprSeq n x = 1 :=
  le_antisymm (apprSeq_apply_le_one hF n x) (one_le_apprSeq_apply hF n hx)

/-- The measure of a closed set is at most the integral of any function in a decreasing
approximating sequence to the indicator of the set. -/
theorem measure_le_lintegral [MeasurableSpace X] [OpensMeasurableSpace X] (Œº : Measure X) (n : ‚Ñï) :
    Œº F ‚â§ ‚à´‚Åª x, (hF.apprSeq n x : ‚Ñù‚â•0‚àû) ‚àÇŒº := by
  convert_to ‚à´‚Åª x, (F.indicator (fun _ ‚Ü¶ (1 : ‚Ñù‚â•0‚àû))) x ‚àÇŒº ‚â§ ‚à´‚Åª x, hF.apprSeq n x ‚àÇŒº
  ¬∑ rw [lintegral_indicator _ hF.measurableSet]
    simp only [lintegral_one, MeasurableSet.univ, Measure.restrict_apply, univ_inter]
  ¬∑ apply lintegral_mono
    intro x
    by_cases hxF : x ‚àà F
    ¬∑ simpa only [hxF, indicator_of_mem, one_le_coe_iff] using one_le_apprSeq_apply hF n hxF
    ¬∑ simp only [hxF, not_false_eq_true, indicator_of_not_mem, zero_le]

/-- The integrals along a decreasing approximating sequence to the indicator of a closed set
tend to the measure of the closed set. -/
lemma tendsto_lintegral_apprSeq [MeasurableSpace X] [OpensMeasurableSpace X]
    (Œº : Measure X) [IsFiniteMeasure Œº] :
    Tendsto (fun n ‚Ü¶ ‚à´‚Åª x, hF.apprSeq n x ‚àÇŒº) atTop (ùìù ((Œº : Measure X) F)) :=
  measure_of_cont_bdd_of_tendsto_indicator Œº hF.measurableSet hF.apprSeq
    (apprSeq_apply_le_one hF) (tendsto_apprSeq hF)

end HasOuterApproxClosed --namespace

noncomputable instance (X : Type _) [TopologicalSpace X]
    [TopologicalSpace.PseudoMetrizableSpace X] : HasOuterApproxClosed X := by
  letI : PseudoMetricSpace X := TopologicalSpace.pseudoMetrizableSpacePseudoMetric X
  refine ‚ü®fun F hF ‚Ü¶ ?_‚ü©
  ¬∑ use fun n ‚Ü¶ thickenedIndicator (Œ¥ := (1 : ‚Ñù) / (n + 1)) Nat.one_div_pos_of_nat F
    refine ‚ü®?_, ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ exact fun n x ‚Ü¶ thickenedIndicator_le_one Nat.one_div_pos_of_nat F x
    ¬∑ exact fun n x hxF ‚Ü¶ one_le_thickenedIndicator_apply X Nat.one_div_pos_of_nat hxF
    ¬∑ have key := thickenedIndicator_tendsto_indicator_closure
                (Œ¥seq := fun (n : ‚Ñï) ‚Ü¶ (1 : ‚Ñù) / (n + 1))
                (fun _ ‚Ü¶ Nat.one_div_pos_of_nat) tendsto_one_div_add_atTop_nhds_0_nat F
      rw [tendsto_pi_nhds] at *
      intro x
      nth_rw 2 [‚ÜêIsClosed.closure_eq hF]
      exact key x

namespace MeasureTheory

/-- Two finite measures give equal values to all closed sets if the integrals of all bounded
continuous functions with respect to the two measures agree. -/
theorem measure_isClosed_eq_of_forall_lintegral_eq_of_isFiniteMeasure {Œ© : Type*}
    [MeasurableSpace Œ©] [TopologicalSpace Œ©] [HasOuterApproxClosed Œ©]
    [OpensMeasurableSpace Œ©] {Œº ŒΩ : Measure Œ©} [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ]
    (h : ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù‚â•0), ‚à´‚Åª x, f x ‚àÇŒº = ‚à´‚Åª x, f x ‚àÇŒΩ) {F : Set Œ©} (F_closed : IsClosed F) :
    Œº F = ŒΩ F := by
  have obs_Œº := HasOuterApproxClosed.tendsto_lintegral_apprSeq F_closed Œº
  have obs_ŒΩ := HasOuterApproxClosed.tendsto_lintegral_apprSeq F_closed ŒΩ
  simp_rw [h] at obs_Œº
  exact tendsto_nhds_unique obs_Œº obs_ŒΩ

/-- Two finite Borel measures are equal if the integrals of all bounded continuous functions with
respect to both agree. -/
theorem ext_of_forall_lintegral_eq_of_IsFiniteMeasure {Œ© : Type*}
    [MeasurableSpace Œ©] [TopologicalSpace Œ©] [HasOuterApproxClosed Œ©]
    [BorelSpace Œ©] {Œº ŒΩ : Measure Œ©} [IsFiniteMeasure Œº] [IsFiniteMeasure ŒΩ]
    (h : ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù‚â•0), ‚à´‚Åª x, f x ‚àÇŒº = ‚à´‚Åª x, f x ‚àÇŒΩ) :
    Œº = ŒΩ := by
  have key := @measure_isClosed_eq_of_forall_lintegral_eq_of_isFiniteMeasure Œ© _ _ _ _ Œº ŒΩ _ _ h
  apply ext_of_generate_finite _ ?_ isPiSystem_set_isClosed
  ¬∑ exact fun F F_closed ‚Ü¶ key F_closed
  ¬∑ exact key isClosed_univ
  ¬∑ rw [BorelSpace.measurable_eq (Œ± := Œ©), borel_eq_generateFrom_isClosed]
    rfl

end MeasureTheory -- namespace

end HasOuterApproxClosed -- section
