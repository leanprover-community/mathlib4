/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Topology.Algebra.Module.WeakDual
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction
import Mathlib.MeasureTheory.Measure.HasOuterApproxClosed

#align_import measure_theory.measure.finite_measure from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Finite measures

This file defines the type of finite measures on a given measurable space. When the underlying
space has a topology and the measurable space structure (sigma algebra) is finer than the Borel
sigma algebra, then the type of finite measures is equipped with the topology of weak convergence
of measures. The topology of weak convergence is the coarsest topology w.r.t. which
for every bounded continuous `â„â‰¥0`-valued function `f`, the integration of `f` against the
measure is continuous.

## Main definitions

The main definitions are
 * `MeasureTheory.FiniteMeasure Î©`: The type of finite measures on `Î©` with the topology of weak
   convergence of measures.
 * `MeasureTheory.FiniteMeasure.toWeakDualBCNN : FiniteMeasure Î© â†’ (WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0))`:
   Interpret a finite measure as a continuous linear functional on the space of
   bounded continuous nonnegative functions on `Î©`. This is used for the definition of the
   topology of weak convergence.
 * `MeasureTheory.FiniteMeasure.map`: The push-forward `f* Î¼` of a finite measure `Î¼` on `Î©`
   along a measurable function `f : Î© â†’ Î©'`.
 * `MeasureTheory.FiniteMeasure.mapCLM`: The push-forward along a given continuous `f : Î© â†’ Î©'`
   as a continuous linear map `f* : FiniteMeasure Î© â†’L[â„â‰¥0] FiniteMeasure Î©'`.

## Main results

 * Finite measures `Î¼` on `Î©` give rise to continuous linear functionals on the space of
   bounded continuous nonnegative functions on `Î©` via integration:
   `MeasureTheory.FiniteMeasure.toWeakDualBCNN : FiniteMeasure Î© â†’ (WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0))`
 * `MeasureTheory.FiniteMeasure.tendsto_iff_forall_integral_tendsto`: Convergence of finite
   measures is characterized by the convergence of integrals of all bounded continuous functions.
   This shows that the chosen definition of topology coincides with the common textbook definition
   of weak convergence of measures. A similar characterization by the convergence of integrals (in
   the `MeasureTheory.lintegral` sense) of all bounded continuous nonnegative functions is
   `MeasureTheory.FiniteMeasure.tendsto_iff_forall_lintegral_tendsto`.
 * `MeasureTheory.FiniteMeasure.continuous_map`: For a continuous function `f : Î© â†’ Î©'`, the
   push-forward of finite measures `f* : FiniteMeasure Î© â†’ FiniteMeasure Î©'` is continuous.
 * `MeasureTheory.FiniteMeasure.t2Space`: The topology of weak convergence of finite Borel measures
   is Hausdorff on spaces where indicators of closed sets have continuous decreasing approximating
   sequences (in particular on any pseudo-metrizable spaces).

## Implementation notes

The topology of weak convergence of finite Borel measures is defined using a mapping from
`MeasureTheory.FiniteMeasure Î©` to `WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0)`, inheriting the topology from the
latter.

The implementation of `MeasureTheory.FiniteMeasure Î©` and is directly as a subtype of
`MeasureTheory.Measure Î©`, and the coercion to a function is the composition `ENNReal.toNNReal`
and the coercion to function of `MeasureTheory.Measure Î©`. Another alternative would have been to
use a bijection with `MeasureTheory.VectorMeasure Î© â„â‰¥0` as an intermediate step. Some
considerations:
 * Potential advantages of using the `NNReal`-valued vector measure alternative:
   * The coercion to function would avoid need to compose with `ENNReal.toNNReal`, the
     `NNReal`-valued API could be more directly available.
 * Potential drawbacks of the vector measure alternative:
   * The coercion to function would lose monotonicity, as non-measurable sets would be defined to
     have measure 0.
   * No integration theory directly. E.g., the topology definition requires
     `MeasureTheory.lintegral` w.r.t. a coercion to `MeasureTheory.Measure Î©` in any case.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

weak convergence of measures, finite measure

-/


noncomputable section

open MeasureTheory

open Set

open Filter

open BoundedContinuousFunction

open scoped Topology ENNReal NNReal BoundedContinuousFunction

namespace MeasureTheory

namespace FiniteMeasure

section FiniteMeasure

/-! ### Finite measures

In this section we define the `Type` of `MeasureTheory.FiniteMeasure Î©`, when `Î©` is a measurable
space. Finite measures on `Î©` are a module over `â„â‰¥0`.

If `Î©` is moreover a topological space and the sigma algebra on `Î©` is finer than the Borel sigma
algebra (i.e. `[OpensMeasurableSpace Î©]`), then `MeasureTheory.FiniteMeasure Î©` is equipped with
the topology of weak convergence of measures. This is implemented by defining a pairing of finite
measures `Î¼` on `Î©` with continuous bounded nonnegative functions `f : Î© â†’áµ‡ â„â‰¥0` via integration,
and using the associated weak topology (essentially the weak-star topology on the dual of
`Î© â†’áµ‡ â„â‰¥0`).
-/


variable {Î© : Type*} [MeasurableSpace Î©]

/-- Finite measures are defined as the subtype of measures that have the property of being finite
measures (i.e., their total mass is finite). -/
def _root_.MeasureTheory.FiniteMeasure (Î© : Type*) [MeasurableSpace Î©] : Type _ :=
  { Î¼ : Measure Î© // IsFiniteMeasure Î¼ }
#align measure_theory.finite_measure MeasureTheory.FiniteMeasure

-- Porting note: as with other subtype synonyms (e.g., `â„â‰¥0`, we need a new function for the
-- coercion instead of relying on `Subtype.val`.
/-- Coercion from `MeasureTheory.FiniteMeasure Î©` to `MeasureTheory.Measure Î©`. -/
@[coe]
def toMeasure : FiniteMeasure Î© â†’ Measure Î© := Subtype.val

/-- A finite measure can be interpreted as a measure. -/
instance instCoe : Coe (FiniteMeasure Î©) (MeasureTheory.Measure Î©) where
  coe := toMeasure

instance isFiniteMeasure (Î¼ : FiniteMeasure Î©) : IsFiniteMeasure (Î¼ : Measure Î©) :=
  Î¼.prop
#align measure_theory.finite_measure.is_finite_measure MeasureTheory.FiniteMeasure.isFiniteMeasure

@[simp]
theorem val_eq_toMeasure (Î½ : FiniteMeasure Î©) : Î½.val = (Î½ : Measure Î©) :=
  rfl
#align measure_theory.finite_measure.val_eq_to_measure MeasureTheory.FiniteMeasure.val_eq_toMeasure

theorem toMeasure_injective : Function.Injective ((â†‘) : FiniteMeasure Î© â†’ Measure Î©) :=
  Subtype.coe_injective
#align measure_theory.finite_measure.coe_injective MeasureTheory.FiniteMeasure.toMeasure_injective

instance instFunLike : FunLike (FiniteMeasure Î©) (Set Î©) â„â‰¥0 where
  coe Î¼ s := ((Î¼ : Measure Î©) s).toNNReal
  coe_injective' Î¼ Î½ h := toMeasure_injective $ Measure.ext fun s _ â†¦ by
    simpa [ENNReal.toNNReal_eq_toNNReal_iff, measure_ne_top] using congr_fun h s

lemma coeFn_def (Î¼ : FiniteMeasure Î©) : Î¼ = fun s â†¦ ((Î¼ : Measure Î©) s).toNNReal := rfl
#align measure_theory.finite_measure.coe_fn_eq_to_nnreal_coe_fn_to_measure MeasureTheory.FiniteMeasure.coeFn_def

lemma coeFn_mk (Î¼ : Measure Î©) (hÎ¼) :
    DFunLike.coe (F := FiniteMeasure Î©) âŸ¨Î¼, hÎ¼âŸ© = fun s â†¦ (Î¼ s).toNNReal := rfl

@[simp, norm_cast]
lemma mk_apply (Î¼ : Measure Î©) (hÎ¼) (s : Set Î©) :
    DFunLike.coe (F := FiniteMeasure Î©) âŸ¨Î¼, hÎ¼âŸ© s = (Î¼ s).toNNReal := rfl

@[simp]
theorem ennreal_coeFn_eq_coeFn_toMeasure (Î½ : FiniteMeasure Î©) (s : Set Î©) :
    (Î½ s : â„â‰¥0âˆ) = (Î½ : Measure Î©) s :=
  ENNReal.coe_toNNReal (measure_lt_top (â†‘Î½) s).ne
#align measure_theory.finite_measure.ennreal_coe_fn_eq_coe_fn_to_measure MeasureTheory.FiniteMeasure.ennreal_coeFn_eq_coeFn_toMeasure

theorem apply_mono (Î¼ : FiniteMeasure Î©) {sâ‚ sâ‚‚ : Set Î©} (h : sâ‚ âŠ† sâ‚‚) : Î¼ sâ‚ â‰¤ Î¼ sâ‚‚ := by
  change ((Î¼ : Measure Î©) sâ‚).toNNReal â‰¤ ((Î¼ : Measure Î©) sâ‚‚).toNNReal
  have key : (Î¼ : Measure Î©) sâ‚ â‰¤ (Î¼ : Measure Î©) sâ‚‚ := (Î¼ : Measure Î©).mono h
  apply (ENNReal.toNNReal_le_toNNReal (measure_ne_top _ sâ‚) (measure_ne_top _ sâ‚‚)).mpr key
#align measure_theory.finite_measure.apply_mono MeasureTheory.FiniteMeasure.apply_mono

/-- The (total) mass of a finite measure `Î¼` is `Î¼ univ`, i.e., the cast to `NNReal` of
`(Î¼ : measure Î©) univ`. -/
def mass (Î¼ : FiniteMeasure Î©) : â„â‰¥0 :=
  Î¼ univ
#align measure_theory.finite_measure.mass MeasureTheory.FiniteMeasure.mass

@[simp] theorem apply_le_mass (Î¼ : FiniteMeasure Î©) (s : Set Î©) : Î¼ s â‰¤ Î¼.mass := by
  simpa using apply_mono Î¼ (subset_univ s)

@[simp]
theorem ennreal_mass {Î¼ : FiniteMeasure Î©} : (Î¼.mass : â„â‰¥0âˆ) = (Î¼ : Measure Î©) univ :=
  ennreal_coeFn_eq_coeFn_toMeasure Î¼ Set.univ
#align measure_theory.finite_measure.ennreal_mass MeasureTheory.FiniteMeasure.ennreal_mass

instance instZero : Zero (FiniteMeasure Î©) where zero := âŸ¨0, MeasureTheory.isFiniteMeasureZeroâŸ©
#align measure_theory.finite_measure.has_zero MeasureTheory.FiniteMeasure.instZero

@[simp, norm_cast] lemma coeFn_zero : â‡‘(0 : FiniteMeasure Î©) = 0 := rfl
#align measure_theory.finite_measure.coe_fn_zero MeasureTheory.FiniteMeasure.coeFn_zero

@[simp]
theorem zero_mass : (0 : FiniteMeasure Î©).mass = 0 :=
  rfl
#align measure_theory.finite_measure.zero.mass MeasureTheory.FiniteMeasure.zero_mass

@[simp]
theorem mass_zero_iff (Î¼ : FiniteMeasure Î©) : Î¼.mass = 0 â†” Î¼ = 0 := by
  refine âŸ¨fun Î¼_mass => ?_, fun hÎ¼ => by simp only [hÎ¼, zero_mass]âŸ©
  apply toMeasure_injective
  apply Measure.measure_univ_eq_zero.mp
  rwa [â† ennreal_mass, ENNReal.coe_eq_zero]
#align measure_theory.finite_measure.mass_zero_iff MeasureTheory.FiniteMeasure.mass_zero_iff

theorem mass_nonzero_iff (Î¼ : FiniteMeasure Î©) : Î¼.mass â‰  0 â†” Î¼ â‰  0 := by
  rw [not_iff_not]
  exact FiniteMeasure.mass_zero_iff Î¼
#align measure_theory.finite_measure.mass_nonzero_iff MeasureTheory.FiniteMeasure.mass_nonzero_iff

@[ext]
theorem eq_of_forall_toMeasure_apply_eq (Î¼ Î½ : FiniteMeasure Î©)
    (h : âˆ€ s : Set Î©, MeasurableSet s â†’ (Î¼ : Measure Î©) s = (Î½ : Measure Î©) s) : Î¼ = Î½ := by
  apply Subtype.ext
  ext1 s s_mble
  exact h s s_mble
#align measure_theory.finite_measure.eq_of_forall_measure_apply_eq MeasureTheory.FiniteMeasure.eq_of_forall_toMeasure_apply_eq

theorem eq_of_forall_apply_eq (Î¼ Î½ : FiniteMeasure Î©)
    (h : âˆ€ s : Set Î©, MeasurableSet s â†’ Î¼ s = Î½ s) : Î¼ = Î½ := by
  ext1 s s_mble
  simpa [ennreal_coeFn_eq_coeFn_toMeasure] using congr_arg ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) (h s s_mble)
#align measure_theory.finite_measure.eq_of_forall_apply_eq MeasureTheory.FiniteMeasure.eq_of_forall_apply_eq

instance instInhabited : Inhabited (FiniteMeasure Î©) :=
  âŸ¨0âŸ©

instance instAdd : Add (FiniteMeasure Î©) where add Î¼ Î½ := âŸ¨Î¼ + Î½, MeasureTheory.isFiniteMeasureAddâŸ©

variable {R : Type*} [SMul R â„â‰¥0] [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0 â„â‰¥0âˆ]
  [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]

instance instSMul : SMul R (FiniteMeasure Î©) where
  smul (c : R) Î¼ := âŸ¨c â€¢ (Î¼ : Measure Î©), MeasureTheory.isFiniteMeasureSMulOfNNRealTowerâŸ©

@[simp, norm_cast]
theorem toMeasure_zero : ((â†‘) : FiniteMeasure Î© â†’ Measure Î©) 0 = 0 :=
  rfl
#align measure_theory.finite_measure.coe_zero MeasureTheory.FiniteMeasure.toMeasure_zero

-- Porting note: with `simp` here the `coeFn` lemmas below fall prey to `simpNF`: the LHS simplifies
@[norm_cast]
theorem toMeasure_add (Î¼ Î½ : FiniteMeasure Î©) : â†‘(Î¼ + Î½) = (â†‘Î¼ + â†‘Î½ : Measure Î©) :=
  rfl
#align measure_theory.finite_measure.coe_add MeasureTheory.FiniteMeasure.toMeasure_add

@[simp, norm_cast]
theorem toMeasure_smul (c : R) (Î¼ : FiniteMeasure Î©) : â†‘(c â€¢ Î¼) = c â€¢ (Î¼ : Measure Î©) :=
  rfl
#align measure_theory.finite_measure.coe_smul MeasureTheory.FiniteMeasure.toMeasure_smul

@[simp, norm_cast]
theorem coeFn_add (Î¼ Î½ : FiniteMeasure Î©) : (â‡‘(Î¼ + Î½) : Set Î© â†’ â„â‰¥0) = (â‡‘Î¼ + â‡‘Î½ : Set Î© â†’ â„â‰¥0) := by
  funext
  simp only [Pi.add_apply, â† ENNReal.coe_inj, ne_eq, ennreal_coeFn_eq_coeFn_toMeasure,
    ENNReal.coe_add]
  norm_cast
#align measure_theory.finite_measure.coe_fn_add MeasureTheory.FiniteMeasure.coeFn_add

@[simp, norm_cast]
theorem coeFn_smul [IsScalarTower R â„â‰¥0 â„â‰¥0] (c : R) (Î¼ : FiniteMeasure Î©) :
    (â‡‘(c â€¢ Î¼) : Set Î© â†’ â„â‰¥0) = c â€¢ (â‡‘Î¼ : Set Î© â†’ â„â‰¥0) := by
  funext; simp [â† ENNReal.coe_inj, ENNReal.coe_smul]
#align measure_theory.finite_measure.coe_fn_smul MeasureTheory.FiniteMeasure.coeFn_smul

instance instAddCommMonoid : AddCommMonoid (FiniteMeasure Î©) :=
  toMeasure_injective.addCommMonoid (â†‘) toMeasure_zero toMeasure_add fun _ _ => toMeasure_smul _ _

/-- Coercion is an `AddMonoidHom`. -/
@[simps]
def toMeasureAddMonoidHom : FiniteMeasure Î© â†’+ Measure Î© where
  toFun := (â†‘)
  map_zero' := toMeasure_zero
  map_add' := toMeasure_add
#align measure_theory.finite_measure.coe_add_monoid_hom MeasureTheory.FiniteMeasure.toMeasureAddMonoidHom

instance {Î© : Type*} [MeasurableSpace Î©] : Module â„â‰¥0 (FiniteMeasure Î©) :=
  Function.Injective.module _ toMeasureAddMonoidHom toMeasure_injective toMeasure_smul

@[simp]
theorem smul_apply [IsScalarTower R â„â‰¥0 â„â‰¥0] (c : R) (Î¼ : FiniteMeasure Î©) (s : Set Î©) :
    (c â€¢ Î¼) s = c â€¢ Î¼ s := by
  rw [coeFn_smul, Pi.smul_apply]
#align measure_theory.finite_measure.coe_fn_smul_apply MeasureTheory.FiniteMeasure.smul_apply

/-- Restrict a finite measure Î¼ to a set A. -/
def restrict (Î¼ : FiniteMeasure Î©) (A : Set Î©) : FiniteMeasure Î© where
  val := (Î¼ : Measure Î©).restrict A
  property := MeasureTheory.isFiniteMeasureRestrict (Î¼ : Measure Î©) A
#align measure_theory.finite_measure.restrict MeasureTheory.FiniteMeasure.restrict

theorem restrict_measure_eq (Î¼ : FiniteMeasure Î©) (A : Set Î©) :
    (Î¼.restrict A : Measure Î©) = (Î¼ : Measure Î©).restrict A :=
  rfl
#align measure_theory.finite_measure.restrict_measure_eq MeasureTheory.FiniteMeasure.restrict_measure_eq

theorem restrict_apply_measure (Î¼ : FiniteMeasure Î©) (A : Set Î©) {s : Set Î©}
    (s_mble : MeasurableSet s) : (Î¼.restrict A : Measure Î©) s = (Î¼ : Measure Î©) (s âˆ© A) :=
  Measure.restrict_apply s_mble
#align measure_theory.finite_measure.restrict_apply_measure MeasureTheory.FiniteMeasure.restrict_apply_measure

theorem restrict_apply (Î¼ : FiniteMeasure Î©) (A : Set Î©) {s : Set Î©} (s_mble : MeasurableSet s) :
    (Î¼.restrict A) s = Î¼ (s âˆ© A) := by
  apply congr_arg ENNReal.toNNReal
  exact Measure.restrict_apply s_mble
#align measure_theory.finite_measure.restrict_apply MeasureTheory.FiniteMeasure.restrict_apply

theorem restrict_mass (Î¼ : FiniteMeasure Î©) (A : Set Î©) : (Î¼.restrict A).mass = Î¼ A := by
  simp only [mass, restrict_apply Î¼ A MeasurableSet.univ, univ_inter]
#align measure_theory.finite_measure.restrict_mass MeasureTheory.FiniteMeasure.restrict_mass

theorem restrict_eq_zero_iff (Î¼ : FiniteMeasure Î©) (A : Set Î©) : Î¼.restrict A = 0 â†” Î¼ A = 0 := by
  rw [â† mass_zero_iff, restrict_mass]
#align measure_theory.finite_measure.restrict_eq_zero_iff MeasureTheory.FiniteMeasure.restrict_eq_zero_iff

theorem restrict_nonzero_iff (Î¼ : FiniteMeasure Î©) (A : Set Î©) : Î¼.restrict A â‰  0 â†” Î¼ A â‰  0 := by
  rw [â† mass_nonzero_iff, restrict_mass]
#align measure_theory.finite_measure.restrict_nonzero_iff MeasureTheory.FiniteMeasure.restrict_nonzero_iff

variable [TopologicalSpace Î©]

/-- Two finite Borel measures are equal if the integrals of all bounded continuous functions with
respect to both agree. -/
theorem ext_of_forall_lintegral_eq [HasOuterApproxClosed Î©] [BorelSpace Î©]
    {Î¼ Î½ : FiniteMeasure Î©} (h : âˆ€ (f : Î© â†’áµ‡ â„â‰¥0), âˆ«â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î½) :
    Î¼ = Î½ := by
  apply Subtype.ext
  change (Î¼ : Measure Î©) = (Î½ : Measure Î©)
  exact ext_of_forall_lintegral_eq_of_IsFiniteMeasure h

/-- The pairing of a finite (Borel) measure `Î¼` with a nonnegative bounded continuous
function is obtained by (Lebesgue) integrating the (test) function against the measure.
This is `MeasureTheory.FiniteMeasure.testAgainstNN`. -/
def testAgainstNN (Î¼ : FiniteMeasure Î©) (f : Î© â†’áµ‡ â„â‰¥0) : â„â‰¥0 :=
  (âˆ«â» Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©)).toNNReal
#align measure_theory.finite_measure.test_against_nn MeasureTheory.FiniteMeasure.testAgainstNN

@[simp]
theorem testAgainstNN_coe_eq {Î¼ : FiniteMeasure Î©} {f : Î© â†’áµ‡ â„â‰¥0} :
    (Î¼.testAgainstNN f : â„â‰¥0âˆ) = âˆ«â» Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©) :=
  ENNReal.coe_toNNReal (f.lintegral_lt_top_of_nnreal _).ne
#align measure_theory.finite_measure.test_against_nn_coe_eq MeasureTheory.FiniteMeasure.testAgainstNN_coe_eq

theorem testAgainstNN_const (Î¼ : FiniteMeasure Î©) (c : â„â‰¥0) :
    Î¼.testAgainstNN (BoundedContinuousFunction.const Î© c) = c * Î¼.mass := by
  simp [â† ENNReal.coe_inj]
#align measure_theory.finite_measure.test_against_nn_const MeasureTheory.FiniteMeasure.testAgainstNN_const

theorem testAgainstNN_mono (Î¼ : FiniteMeasure Î©) {f g : Î© â†’áµ‡ â„â‰¥0} (f_le_g : (f : Î© â†’ â„â‰¥0) â‰¤ g) :
    Î¼.testAgainstNN f â‰¤ Î¼.testAgainstNN g := by
  simp only [â† ENNReal.coe_le_coe, testAgainstNN_coe_eq]
  gcongr
  apply f_le_g
#align measure_theory.finite_measure.test_against_nn_mono MeasureTheory.FiniteMeasure.testAgainstNN_mono

@[simp]
theorem testAgainstNN_zero (Î¼ : FiniteMeasure Î©) : Î¼.testAgainstNN 0 = 0 := by
  simpa only [zero_mul] using Î¼.testAgainstNN_const 0
#align measure_theory.finite_measure.test_against_nn_zero MeasureTheory.FiniteMeasure.testAgainstNN_zero

@[simp]
theorem testAgainstNN_one (Î¼ : FiniteMeasure Î©) : Î¼.testAgainstNN 1 = Î¼.mass := by
  simp only [testAgainstNN, coe_one, Pi.one_apply, ENNReal.coe_one, lintegral_one]
  rfl
#align measure_theory.finite_measure.test_against_nn_one MeasureTheory.FiniteMeasure.testAgainstNN_one

@[simp]
theorem zero_testAgainstNN_apply (f : Î© â†’áµ‡ â„â‰¥0) : (0 : FiniteMeasure Î©).testAgainstNN f = 0 := by
  simp only [testAgainstNN, toMeasure_zero, lintegral_zero_measure, ENNReal.zero_toNNReal]
#align measure_theory.finite_measure.zero.test_against_nn_apply MeasureTheory.FiniteMeasure.zero_testAgainstNN_apply

theorem zero_testAgainstNN : (0 : FiniteMeasure Î©).testAgainstNN = 0 := by
  funext;
  simp only [zero_testAgainstNN_apply, Pi.zero_apply]
#align measure_theory.finite_measure.zero.test_against_nn MeasureTheory.FiniteMeasure.zero_testAgainstNN

@[simp]
theorem smul_testAgainstNN_apply (c : â„â‰¥0) (Î¼ : FiniteMeasure Î©) (f : Î© â†’áµ‡ â„â‰¥0) :
    (c â€¢ Î¼).testAgainstNN f = c â€¢ Î¼.testAgainstNN f := by
  simp only [testAgainstNN, toMeasure_smul, smul_eq_mul, â† ENNReal.smul_toNNReal, ENNReal.smul_def,
    lintegral_smul_measure]
#align measure_theory.finite_measure.smul_test_against_nn_apply MeasureTheory.FiniteMeasure.smul_testAgainstNN_apply

section weak_convergence

variable [OpensMeasurableSpace Î©]

theorem testAgainstNN_add (Î¼ : FiniteMeasure Î©) (fâ‚ fâ‚‚ : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.testAgainstNN (fâ‚ + fâ‚‚) = Î¼.testAgainstNN fâ‚ + Î¼.testAgainstNN fâ‚‚ := by
  simp only [â† ENNReal.coe_inj, BoundedContinuousFunction.coe_add, ENNReal.coe_add, Pi.add_apply,
    testAgainstNN_coe_eq]
  exact lintegral_add_left (BoundedContinuousFunction.measurable_coe_ennreal_comp _) _
#align measure_theory.finite_measure.test_against_nn_add MeasureTheory.FiniteMeasure.testAgainstNN_add

theorem testAgainstNN_smul [IsScalarTower R â„â‰¥0 â„â‰¥0] [PseudoMetricSpace R] [Zero R]
    [BoundedSMul R â„â‰¥0] (Î¼ : FiniteMeasure Î©) (c : R) (f : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.testAgainstNN (c â€¢ f) = c â€¢ Î¼.testAgainstNN f := by
  simp only [â† ENNReal.coe_inj, BoundedContinuousFunction.coe_smul, testAgainstNN_coe_eq,
    ENNReal.coe_smul]
  simp_rw [â† smul_one_smul â„â‰¥0âˆ c (f _ : â„â‰¥0âˆ), â† smul_one_smul â„â‰¥0âˆ c (lintegral _ _ : â„â‰¥0âˆ),
    smul_eq_mul]
  exact
    @lintegral_const_mul _ _ (Î¼ : Measure Î©) (c â€¢ (1 : â„â‰¥0âˆ)) _ f.measurable_coe_ennreal_comp
#align measure_theory.finite_measure.test_against_nn_smul MeasureTheory.FiniteMeasure.testAgainstNN_smul

theorem testAgainstNN_lipschitz_estimate (Î¼ : FiniteMeasure Î©) (f g : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.testAgainstNN f â‰¤ Î¼.testAgainstNN g + nndist f g * Î¼.mass := by
  simp only [â† Î¼.testAgainstNN_const (nndist f g), â† testAgainstNN_add, â† ENNReal.coe_le_coe,
    BoundedContinuousFunction.coe_add, const_apply, ENNReal.coe_add, Pi.add_apply,
    coe_nnreal_ennreal_nndist, testAgainstNN_coe_eq]
  apply lintegral_mono
  have le_dist : âˆ€ Ï‰, dist (f Ï‰) (g Ï‰) â‰¤ nndist f g := BoundedContinuousFunction.dist_coe_le_dist
  intro Ï‰
  have le' : f Ï‰ â‰¤ g Ï‰ + nndist f g := by
    apply (NNReal.le_add_nndist (f Ï‰) (g Ï‰)).trans
    rw [add_le_add_iff_left]
    exact dist_le_coe.mp (le_dist Ï‰)
  have le : (f Ï‰ : â„â‰¥0âˆ) â‰¤ (g Ï‰ : â„â‰¥0âˆ) + nndist f g := by
    rw [â† ENNReal.coe_add];
    exact ENNReal.coe_mono le'
  rwa [coe_nnreal_ennreal_nndist] at le
#align measure_theory.finite_measure.test_against_nn_lipschitz_estimate MeasureTheory.FiniteMeasure.testAgainstNN_lipschitz_estimate

theorem testAgainstNN_lipschitz (Î¼ : FiniteMeasure Î©) :
    LipschitzWith Î¼.mass fun f : Î© â†’áµ‡ â„â‰¥0 => Î¼.testAgainstNN f := by
  rw [lipschitzWith_iff_dist_le_mul]
  intro fâ‚ fâ‚‚
  suffices abs (Î¼.testAgainstNN fâ‚ - Î¼.testAgainstNN fâ‚‚ : â„) â‰¤ Î¼.mass * dist fâ‚ fâ‚‚ by
    rwa [NNReal.dist_eq]
  apply abs_le.mpr
  constructor
  Â· have key' := Î¼.testAgainstNN_lipschitz_estimate fâ‚‚ fâ‚
    rw [mul_comm] at key'
    suffices â†‘(Î¼.testAgainstNN fâ‚‚) â‰¤ â†‘(Î¼.testAgainstNN fâ‚) + â†‘Î¼.mass * dist fâ‚ fâ‚‚ by linarith
    have key := NNReal.coe_mono key'
    rwa [NNReal.coe_add, NNReal.coe_mul, nndist_comm] at key
  Â· have key' := Î¼.testAgainstNN_lipschitz_estimate fâ‚ fâ‚‚
    rw [mul_comm] at key'
    suffices â†‘(Î¼.testAgainstNN fâ‚) â‰¤ â†‘(Î¼.testAgainstNN fâ‚‚) + â†‘Î¼.mass * dist fâ‚ fâ‚‚ by linarith
    have key := NNReal.coe_mono key'
    rwa [NNReal.coe_add, NNReal.coe_mul] at key
#align measure_theory.finite_measure.test_against_nn_lipschitz MeasureTheory.FiniteMeasure.testAgainstNN_lipschitz

/-- Finite measures yield elements of the `WeakDual` of bounded continuous nonnegative
functions via `MeasureTheory.FiniteMeasure.testAgainstNN`, i.e., integration. -/
def toWeakDualBCNN (Î¼ : FiniteMeasure Î©) : WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0) where
  toFun f := Î¼.testAgainstNN f
  map_add' := testAgainstNN_add Î¼
  map_smul' := testAgainstNN_smul Î¼
  cont := Î¼.testAgainstNN_lipschitz.continuous
#align measure_theory.finite_measure.to_weak_dual_bcnn MeasureTheory.FiniteMeasure.toWeakDualBCNN

@[simp]
theorem coe_toWeakDualBCNN (Î¼ : FiniteMeasure Î©) : â‡‘Î¼.toWeakDualBCNN = Î¼.testAgainstNN :=
  rfl
#align measure_theory.finite_measure.coe_to_weak_dual_bcnn MeasureTheory.FiniteMeasure.coe_toWeakDualBCNN

@[simp]
theorem toWeakDualBCNN_apply (Î¼ : FiniteMeasure Î©) (f : Î© â†’áµ‡ â„â‰¥0) :
    Î¼.toWeakDualBCNN f = (âˆ«â» x, f x âˆ‚(Î¼ : Measure Î©)).toNNReal :=
  rfl
#align measure_theory.finite_measure.to_weak_dual_bcnn_apply MeasureTheory.FiniteMeasure.toWeakDualBCNN_apply

/-- The topology of weak convergence on `MeasureTheory.FiniteMeasure Î©` is inherited (induced)
from the weak-* topology on `WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0)` via the function
`MeasureTheory.FiniteMeasure.toWeakDualBCNN`. -/
instance instTopologicalSpace : TopologicalSpace (FiniteMeasure Î©) :=
  TopologicalSpace.induced toWeakDualBCNN inferInstance

theorem toWeakDualBCNN_continuous : Continuous (@toWeakDualBCNN Î© _ _ _) :=
  continuous_induced_dom
#align measure_theory.finite_measure.to_weak_dual_bcnn_continuous MeasureTheory.FiniteMeasure.toWeakDualBCNN_continuous

/-- Integration of (nonnegative bounded continuous) test functions against finite Borel measures
depends continuously on the measure. -/
theorem continuous_testAgainstNN_eval (f : Î© â†’áµ‡ â„â‰¥0) :
    Continuous fun Î¼ : FiniteMeasure Î© => Î¼.testAgainstNN f := by
  show Continuous ((fun Ï† : WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0) => Ï† f) âˆ˜ toWeakDualBCNN)
  refine Continuous.comp ?_ (toWeakDualBCNN_continuous (Î© := Î©))
  exact WeakBilin.eval_continuous (ğ•œ := â„â‰¥0) (E := (Î© â†’áµ‡ â„â‰¥0) â†’L[â„â‰¥0] â„â‰¥0) _ _
  /- porting note: without explicitly providing `ğ•œ` and `E` TC synthesis times
  out trying to find `Module â„â‰¥0 ((Î© â†’áµ‡ â„â‰¥0) â†’L[â„â‰¥0] â„â‰¥0)`, but it can find it with enough time:
  `set_option synthInstance.maxHeartbeats 47000` was sufficient. -/
#align measure_theory.finite_measure.continuous_test_against_nn_eval MeasureTheory.FiniteMeasure.continuous_testAgainstNN_eval

/-- The total mass of a finite measure depends continuously on the measure. -/
theorem continuous_mass : Continuous fun Î¼ : FiniteMeasure Î© => Î¼.mass := by
  simp_rw [â† testAgainstNN_one]; exact continuous_testAgainstNN_eval 1
#align measure_theory.finite_measure.continuous_mass MeasureTheory.FiniteMeasure.continuous_mass

/-- Convergence of finite measures implies the convergence of their total masses. -/
theorem _root_.Filter.Tendsto.mass {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    {Î¼ : FiniteMeasure Î©} (h : Tendsto Î¼s F (ğ“ Î¼)) : Tendsto (fun i => (Î¼s i).mass) F (ğ“ Î¼.mass) :=
  (continuous_mass.tendsto Î¼).comp h
#align filter.tendsto.mass Filter.Tendsto.mass

theorem tendsto_iff_weak_star_tendsto {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    {Î¼ : FiniteMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†” Tendsto (fun i => (Î¼s i).toWeakDualBCNN) F (ğ“ Î¼.toWeakDualBCNN) :=
  Inducing.tendsto_nhds_iff âŸ¨rflâŸ©
#align measure_theory.finite_measure.tendsto_iff_weak_star_tendsto MeasureTheory.FiniteMeasure.tendsto_iff_weak_star_tendsto

theorem tendsto_iff_forall_toWeakDualBCNN_tendsto {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ FiniteMeasure Î©} {Î¼ : FiniteMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ â„â‰¥0, Tendsto (fun i => (Î¼s i).toWeakDualBCNN f) F (ğ“ (Î¼.toWeakDualBCNN f)) := by
  rw [tendsto_iff_weak_star_tendsto, tendsto_iff_forall_eval_tendsto_topDualPairing]; rfl
#align measure_theory.finite_measure.tendsto_iff_forall_to_weak_dual_bcnn_tendsto MeasureTheory.FiniteMeasure.tendsto_iff_forall_toWeakDualBCNN_tendsto

theorem tendsto_iff_forall_testAgainstNN_tendsto {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ FiniteMeasure Î©} {Î¼ : FiniteMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ â„â‰¥0, Tendsto (fun i => (Î¼s i).testAgainstNN f) F (ğ“ (Î¼.testAgainstNN f)) := by
  rw [FiniteMeasure.tendsto_iff_forall_toWeakDualBCNN_tendsto]; rfl
#align measure_theory.finite_measure.tendsto_iff_forall_test_against_nn_tendsto MeasureTheory.FiniteMeasure.tendsto_iff_forall_testAgainstNN_tendsto

/-- If the total masses of finite measures tend to zero, then the measures tend to
zero. This formulation concerns the associated functionals on bounded continuous
nonnegative test functions. See `MeasureTheory.FiniteMeasure.tendsto_zero_of_tendsto_zero_mass` for
a formulation stating the weak convergence of measures. -/
theorem tendsto_zero_testAgainstNN_of_tendsto_zero_mass {Î³ : Type*} {F : Filter Î³}
    {Î¼s : Î³ â†’ FiniteMeasure Î©} (mass_lim : Tendsto (fun i => (Î¼s i).mass) F (ğ“ 0)) (f : Î© â†’áµ‡ â„â‰¥0) :
    Tendsto (fun i => (Î¼s i).testAgainstNN f) F (ğ“ 0) := by
  apply tendsto_iff_dist_tendsto_zero.mpr
  have obs := fun i => (Î¼s i).testAgainstNN_lipschitz_estimate f 0
  simp_rw [testAgainstNN_zero, zero_add] at obs
  simp_rw [show âˆ€ i, dist ((Î¼s i).testAgainstNN f) 0 = (Î¼s i).testAgainstNN f by
      simp only [dist_nndist, NNReal.nndist_zero_eq_val', eq_self_iff_true, imp_true_iff]]
  refine squeeze_zero (fun i => NNReal.coe_nonneg _) obs ?_
  have lim_pair : Tendsto (fun i => (âŸ¨nndist f 0, (Î¼s i).massâŸ© : â„ Ã— â„)) F (ğ“ âŸ¨nndist f 0, 0âŸ©) := by
    refine (Prod.tendsto_iff _ _).mpr âŸ¨tendsto_const_nhds, ?_âŸ©
    exact (NNReal.continuous_coe.tendsto 0).comp mass_lim
  have key := tendsto_mul.comp lim_pair
  rwa [mul_zero] at key
#align measure_theory.finite_measure.tendsto_zero_test_against_nn_of_tendsto_zero_mass MeasureTheory.FiniteMeasure.tendsto_zero_testAgainstNN_of_tendsto_zero_mass

/-- If the total masses of finite measures tend to zero, then the measures tend to zero. -/
theorem tendsto_zero_of_tendsto_zero_mass {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    (mass_lim : Tendsto (fun i => (Î¼s i).mass) F (ğ“ 0)) : Tendsto Î¼s F (ğ“ 0) := by
  rw [tendsto_iff_forall_testAgainstNN_tendsto]
  intro f
  convert tendsto_zero_testAgainstNN_of_tendsto_zero_mass mass_lim f
  rw [zero_testAgainstNN_apply]
#align measure_theory.finite_measure.tendsto_zero_of_tendsto_zero_mass MeasureTheory.FiniteMeasure.tendsto_zero_of_tendsto_zero_mass

/-- A characterization of weak convergence in terms of integrals of bounded continuous
nonnegative functions. -/
theorem tendsto_iff_forall_lintegral_tendsto {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    {Î¼ : FiniteMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ â„â‰¥0,
        Tendsto (fun i => âˆ«â» x, f x âˆ‚(Î¼s i : Measure Î©)) F (ğ“ (âˆ«â» x, f x âˆ‚(Î¼ : Measure Î©))) := by
  rw [tendsto_iff_forall_toWeakDualBCNN_tendsto]
  simp_rw [toWeakDualBCNN_apply _ _, â† testAgainstNN_coe_eq, ENNReal.tendsto_coe,
    ENNReal.toNNReal_coe]
#align measure_theory.finite_measure.tendsto_iff_forall_lintegral_tendsto MeasureTheory.FiniteMeasure.tendsto_iff_forall_lintegral_tendsto

end weak_convergence -- section

section Hausdorff

variable [HasOuterApproxClosed Î©] [BorelSpace Î©]

open Function

/-- The mapping `toWeakDualBCNN` from finite Borel measures to the weak dual of `Î© â†’áµ‡ â„â‰¥0` is
injective, if in the underlying space `Î©`, indicator functions of closed sets have decreasing
approximations by sequences of continuous functions (in particular if `Î©` is pseudometrizable). -/
lemma injective_toWeakDualBCNN :
    Injective (toWeakDualBCNN : FiniteMeasure Î© â†’ WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0)) := by
  intro Î¼ Î½ hÎ¼Î½
  apply ext_of_forall_lintegral_eq
  intro f
  have key := congr_fun (congrArg DFunLike.coe hÎ¼Î½) f
  apply (ENNReal.toNNReal_eq_toNNReal_iff' ?_ ?_).mp key
  Â· exact (lintegral_lt_top_of_nnreal Î¼ f).ne
  Â· exact (lintegral_lt_top_of_nnreal Î½ f).ne

variable (Î©)

lemma embedding_toWeakDualBCNN :
    Embedding (toWeakDualBCNN : FiniteMeasure Î© â†’ WeakDual â„â‰¥0 (Î© â†’áµ‡ â„â‰¥0)) where
  induced := rfl
  inj := injective_toWeakDualBCNN

/-- On topological spaces where indicators of closed sets have decreasing approximating sequences of
continuous functions (`HasOuterApproxClosed`), the topology of weak convergence of finite Borel
measures is Hausdorff (`T2Space`). -/
instance t2Space : T2Space (FiniteMeasure Î©) :=
  Embedding.t2Space (embedding_toWeakDualBCNN Î©)

end Hausdorff -- section

end FiniteMeasure

-- section
section FiniteMeasureBoundedConvergence

/-! ### Bounded convergence results for finite measures

This section is about bounded convergence theorems for finite measures.
-/


variable {Î© : Type*} [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©]

/-- A bounded convergence theorem for a finite measure:
If a sequence of bounded continuous non-negative functions are uniformly bounded by a constant
and tend pointwise to a limit, then their integrals (`MeasureTheory.lintegral`) against the finite
measure tend to the integral of the limit.

A related result with more general assumptions is
`MeasureTheory.tendsto_lintegral_nn_filter_of_le_const`.
-/
theorem tendsto_lintegral_nn_of_le_const (Î¼ : FiniteMeasure Î©) {fs : â„• â†’ Î© â†’áµ‡ â„â‰¥0} {c : â„â‰¥0}
    (fs_le_const : âˆ€ n Ï‰, fs n Ï‰ â‰¤ c) {f : Î© â†’ â„â‰¥0}
    (fs_lim : âˆ€ Ï‰, Tendsto (fun n => fs n Ï‰) atTop (ğ“ (f Ï‰))) :
    Tendsto (fun n => âˆ«â» Ï‰, fs n Ï‰ âˆ‚(Î¼ : Measure Î©)) atTop (ğ“ (âˆ«â» Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©))) :=
  tendsto_lintegral_nn_filter_of_le_const Î¼
    (eventually_of_forall fun n => eventually_of_forall (fs_le_const n))
    (eventually_of_forall fs_lim)
#align measure_theory.finite_measure.tendsto_lintegral_nn_of_le_const MeasureTheory.FiniteMeasure.tendsto_lintegral_nn_of_le_const

/-- A bounded convergence theorem for a finite measure:
If bounded continuous non-negative functions are uniformly bounded by a constant and tend to a
limit, then their integrals against the finite measure tend to the integral of the limit.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere;
 * integration is the pairing against non-negative continuous test functions
   (`MeasureTheory.FiniteMeasure.testAgainstNN`).

A related result using `MeasureTheory.lintegral` for integration is
`MeasureTheory.FiniteMeasure.tendsto_lintegral_nn_filter_of_le_const`.
-/
theorem tendsto_testAgainstNN_filter_of_le_const {Î¹ : Type*} {L : Filter Î¹}
    [L.IsCountablyGenerated] {Î¼ : FiniteMeasure Î©} {fs : Î¹ â†’ Î© â†’áµ‡ â„â‰¥0} {c : â„â‰¥0}
    (fs_le_const : âˆ€á¶  i in L, âˆ€áµ Ï‰ : Î© âˆ‚(Î¼ : Measure Î©), fs i Ï‰ â‰¤ c) {f : Î© â†’áµ‡ â„â‰¥0}
    (fs_lim : âˆ€áµ Ï‰ : Î© âˆ‚(Î¼ : Measure Î©), Tendsto (fun i => fs i Ï‰) L (ğ“ (f Ï‰))) :
    Tendsto (fun i => Î¼.testAgainstNN (fs i)) L (ğ“ (Î¼.testAgainstNN f)) := by
  apply (ENNReal.tendsto_toNNReal (f.lintegral_lt_top_of_nnreal (Î¼ : Measure Î©)).ne).comp
  exact tendsto_lintegral_nn_filter_of_le_const Î¼ fs_le_const fs_lim
#align measure_theory.finite_measure.tendsto_test_against_nn_filter_of_le_const MeasureTheory.FiniteMeasure.tendsto_testAgainstNN_filter_of_le_const

/-- A bounded convergence theorem for a finite measure:
If a sequence of bounded continuous non-negative functions are uniformly bounded by a constant and
tend pointwise to a limit, then their integrals (`MeasureTheory.FiniteMeasure.testAgainstNN`)
against the finite measure tend to the integral of the limit.

Related results:
 * `MeasureTheory.FiniteMeasure.tendsto_testAgainstNN_filter_of_le_const`:
   more general assumptions
 * `MeasureTheory.FiniteMeasure.tendsto_lintegral_nn_of_le_const`:
   using `MeasureTheory.lintegral` for integration.
-/
theorem tendsto_testAgainstNN_of_le_const {Î¼ : FiniteMeasure Î©} {fs : â„• â†’ Î© â†’áµ‡ â„â‰¥0} {c : â„â‰¥0}
    (fs_le_const : âˆ€ n Ï‰, fs n Ï‰ â‰¤ c) {f : Î© â†’áµ‡ â„â‰¥0}
    (fs_lim : âˆ€ Ï‰, Tendsto (fun n => fs n Ï‰) atTop (ğ“ (f Ï‰))) :
    Tendsto (fun n => Î¼.testAgainstNN (fs n)) atTop (ğ“ (Î¼.testAgainstNN f)) :=
  tendsto_testAgainstNN_filter_of_le_const
    (eventually_of_forall fun n => eventually_of_forall (fs_le_const n))
    (eventually_of_forall fs_lim)
#align measure_theory.finite_measure.tendsto_test_against_nn_of_le_const MeasureTheory.FiniteMeasure.tendsto_testAgainstNN_of_le_const

end FiniteMeasureBoundedConvergence

-- section
section FiniteMeasureConvergenceByBoundedContinuousFunctions

/-! ### Weak convergence of finite measures with bounded continuous real-valued functions

In this section we characterize the weak convergence of finite measures by the usual (defining)
condition that the integrals of all bounded continuous real-valued functions converge.
-/


variable {Î© : Type*} [MeasurableSpace Î©] [TopologicalSpace Î©] [OpensMeasurableSpace Î©]

theorem tendsto_of_forall_integral_tendsto {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    {Î¼ : FiniteMeasure Î©}
    (h :
      âˆ€ f : Î© â†’áµ‡ â„,
        Tendsto (fun i => âˆ« x, f x âˆ‚(Î¼s i : Measure Î©)) F (ğ“ (âˆ« x, f x âˆ‚(Î¼ : Measure Î©)))) :
    Tendsto Î¼s F (ğ“ Î¼) := by
  apply (@tendsto_iff_forall_lintegral_tendsto Î© _ _ _ Î³ F Î¼s Î¼).mpr
  intro f
  have key :=
    @ENNReal.tendsto_toReal_iff _ F _
      (fun i => (f.lintegral_lt_top_of_nnreal (Î¼s i)).ne) _ (f.lintegral_lt_top_of_nnreal Î¼).ne
  simp only [ENNReal.ofReal_coe_nnreal] at key
  apply key.mp
  have lip : LipschitzWith 1 ((â†‘) : â„â‰¥0 â†’ â„) := isometry_subtype_coe.lipschitz
  set fâ‚€ := BoundedContinuousFunction.comp _ lip f with _def_fâ‚€
  have fâ‚€_eq : â‡‘fâ‚€ = ((â†‘) : â„â‰¥0 â†’ â„) âˆ˜ â‡‘f := rfl
  have fâ‚€_nn : 0 â‰¤ â‡‘fâ‚€ := fun _ => by
    simp only [fâ‚€_eq, Pi.zero_apply, Function.comp_apply, NNReal.zero_le_coe]
  have fâ‚€_ae_nn : 0 â‰¤áµ[(Î¼ : Measure Î©)] â‡‘fâ‚€ := eventually_of_forall fâ‚€_nn
  have fâ‚€_ae_nns : âˆ€ i, 0 â‰¤áµ[(Î¼s i : Measure Î©)] â‡‘fâ‚€ := fun i => eventually_of_forall fâ‚€_nn
  have aux :=
    integral_eq_lintegral_of_nonneg_ae fâ‚€_ae_nn fâ‚€.continuous.measurable.aestronglyMeasurable
  have auxs := fun i =>
    integral_eq_lintegral_of_nonneg_ae (fâ‚€_ae_nns i) fâ‚€.continuous.measurable.aestronglyMeasurable
  simp_rw [fâ‚€_eq, Function.comp_apply, ENNReal.ofReal_coe_nnreal] at aux auxs
  simpa only [â† aux, â† auxs] using h fâ‚€
#align measure_theory.finite_measure.tendsto_of_forall_integral_tendsto MeasureTheory.FiniteMeasure.tendsto_of_forall_integral_tendsto

/-- A characterization of weak convergence in terms of integrals of bounded continuous
real-valued functions. -/
theorem tendsto_iff_forall_integral_tendsto {Î³ : Type*} {F : Filter Î³} {Î¼s : Î³ â†’ FiniteMeasure Î©}
    {Î¼ : FiniteMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ â„,
        Tendsto (fun i => âˆ« x, f x âˆ‚(Î¼s i : Measure Î©)) F (ğ“ (âˆ« x, f x âˆ‚(Î¼ : Measure Î©))) := by
  refine âŸ¨?_, tendsto_of_forall_integral_tendstoâŸ©
  rw [tendsto_iff_forall_lintegral_tendsto]
  intro h f
  simp_rw [BoundedContinuousFunction.integral_eq_integral_nnrealPart_sub]
  set f_pos := f.nnrealPart with _def_f_pos
  set f_neg := (-f).nnrealPart with _def_f_neg
  have tends_pos := (ENNReal.tendsto_toReal (f_pos.lintegral_lt_top_of_nnreal Î¼).ne).comp (h f_pos)
  have tends_neg := (ENNReal.tendsto_toReal (f_neg.lintegral_lt_top_of_nnreal Î¼).ne).comp (h f_neg)
  have aux :
    âˆ€ g : Î© â†’áµ‡ â„â‰¥0,
      (ENNReal.toReal âˆ˜ fun i : Î³ => âˆ«â» x : Î©, â†‘(g x) âˆ‚(Î¼s i : Measure Î©)) = fun i : Î³ =>
        (âˆ«â» x : Î©, â†‘(g x) âˆ‚(Î¼s i : Measure Î©)).toReal :=
    fun _ => rfl
  simp_rw [aux, BoundedContinuousFunction.toReal_lintegral_coe_eq_integral] at tends_pos tends_neg
  exact Tendsto.sub tends_pos tends_neg
#align measure_theory.finite_measure.tendsto_iff_forall_integral_tendsto MeasureTheory.FiniteMeasure.tendsto_iff_forall_integral_tendsto

end FiniteMeasureConvergenceByBoundedContinuousFunctions -- section

section map

variable {Î© Î©' : Type*} [MeasurableSpace Î©] [MeasurableSpace Î©']

/-- The push-forward of a finite measure by a function between measurable spaces. -/
noncomputable def map (Î½ : FiniteMeasure Î©) (f : Î© â†’ Î©') : FiniteMeasure Î©' :=
  âŸ¨(Î½ : Measure Î©).map f, by
    constructor
    by_cases f_aemble : AEMeasurable f Î½
    Â· rw [Measure.map_apply_of_aemeasurable f_aemble MeasurableSet.univ]
      exact measure_lt_top (â†‘Î½) (f â»Â¹' univ)
    Â· simp [Measure.map, f_aemble]âŸ©

@[simp] lemma toMeasure_map (Î½ : FiniteMeasure Î©) (f : Î© â†’ Î©') :
    (Î½.map f).toMeasure = Î½.toMeasure.map f := rfl

/-- Note that this is an equality of elements of `â„â‰¥0âˆ`. See also
`MeasureTheory.FiniteMeasure.map_apply` for the corresponding equality as elements of `â„â‰¥0`. -/
lemma map_apply' (Î½ : FiniteMeasure Î©) {f : Î© â†’ Î©'} (f_aemble : AEMeasurable f Î½)
    {A : Set Î©'} (A_mble : MeasurableSet A) :
    (Î½.map f : Measure Î©') A = (Î½ : Measure Î©) (f â»Â¹' A) :=
  Measure.map_apply_of_aemeasurable f_aemble A_mble

lemma map_apply_of_aemeasurable (Î½ : FiniteMeasure Î©) {f : Î© â†’ Î©'} (f_aemble : AEMeasurable f Î½)
    {A : Set Î©'} (A_mble : MeasurableSet A) :
    Î½.map f A = Î½ (f â»Â¹' A) := by
  have := Î½.map_apply' f_aemble A_mble
  exact (ENNReal.toNNReal_eq_toNNReal_iff' (measure_ne_top _ _) (measure_ne_top _ _)).mpr this

lemma map_apply (Î½ : FiniteMeasure Î©) {f : Î© â†’ Î©'} (f_mble : Measurable f)
    {A : Set Î©'} (A_mble : MeasurableSet A) :
    Î½.map f A = Î½ (f â»Â¹' A) :=
  map_apply_of_aemeasurable Î½ f_mble.aemeasurable A_mble

@[simp] lemma map_add {f : Î© â†’ Î©'} (f_mble : Measurable f) (Î½â‚ Î½â‚‚ : FiniteMeasure Î©) :
    (Î½â‚ + Î½â‚‚).map f = Î½â‚.map f + Î½â‚‚.map f := by
  ext s s_mble
  simp only [map_apply' _ f_mble.aemeasurable s_mble, toMeasure_add, Measure.add_apply]

@[simp] lemma map_smul {f : Î© â†’ Î©'} (c : â„â‰¥0) (Î½ : FiniteMeasure Î©) :
    (c â€¢ Î½).map f = c â€¢ (Î½.map f) := by
  ext s _
  simp [toMeasure_smul]

/-- The push-forward of a finite measure by a function between measurable spaces as a linear map. -/
noncomputable def mapHom {f : Î© â†’ Î©'} (f_mble : Measurable f) :
    FiniteMeasure Î© â†’â‚—[â„â‰¥0] FiniteMeasure Î©' where
  toFun := fun Î½ â†¦ Î½.map f
  map_add' := map_add f_mble
  map_smul' := map_smul

variable [TopologicalSpace Î©] [OpensMeasurableSpace Î©]
variable [TopologicalSpace Î©'] [BorelSpace Î©']

/-- If `f : X â†’ Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
(weak) convergence of `FiniteMeasure`s on `X` implies (weak) convergence of the push-forwards
of these measures by `f`. -/
lemma tendsto_map_of_tendsto_of_continuous {Î¹ : Type*} {L : Filter Î¹}
    (Î½s : Î¹ â†’ FiniteMeasure Î©) (Î½ : FiniteMeasure Î©) (lim : Tendsto Î½s L (ğ“ Î½))
    {f : Î© â†’ Î©'} (f_cont : Continuous f) :
    Tendsto (fun i â†¦ (Î½s i).map f) L (ğ“ (Î½.map f)) := by
  rw [FiniteMeasure.tendsto_iff_forall_lintegral_tendsto] at lim âŠ¢
  intro g
  convert lim (g.compContinuous âŸ¨f, f_contâŸ©) <;>
  Â· simp only [map, compContinuous_apply, ContinuousMap.coe_mk]
    refine lintegral_map ?_ f_cont.measurable
    exact (ENNReal.continuous_coe.comp g.continuous).measurable

/-- If `f : X â†’ Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
the push-forward of finite measures `f* : FiniteMeasure X â†’ FiniteMeasure Y` is continuous
(in the topologies of weak convergence of measures). -/
lemma continuous_map {f : Î© â†’ Î©'} (f_cont : Continuous f) :
    Continuous (fun Î½ â†¦ FiniteMeasure.map Î½ f) := by
  rw [continuous_iff_continuousAt]
  exact fun _ â†¦ tendsto_map_of_tendsto_of_continuous _ _ continuous_id.continuousAt f_cont

/-- The push-forward of a finite measure by a continuous function between Borel spaces as
a continuous linear map. -/
noncomputable def mapCLM {f : Î© â†’ Î©'} (f_cont : Continuous f) :
    FiniteMeasure Î© â†’L[â„â‰¥0] FiniteMeasure Î©' where
  toFun := fun Î½ â†¦ Î½.map f
  map_add' := map_add f_cont.measurable
  map_smul' := map_smul
  cont := continuous_map f_cont

end map -- section

end FiniteMeasure -- namespace

end MeasureTheory -- namespace
