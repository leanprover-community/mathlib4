/-
Copyright (c) 2021 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.Topology.Algebra.Module.WeakDual
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction
import Mathlib.MeasureTheory.Measure.HasOuterApproxClosed

/-!
# Finite measures

This file defines the type of finite measures on a given measurable space. When the underlying
space has a topology and the measurable space structure (sigma algebra) is finer than the Borel
sigma algebra, then the type of finite measures is equipped with the topology of weak convergence
of measures. The topology of weak convergence is the coarsest topology w.r.t. which
for every bounded continuous `‚Ñù‚â•0`-valued function `f`, the integration of `f` against the
measure is continuous.

## Main definitions

The main definitions are
 * `MeasureTheory.FiniteMeasure Œ©`: The type of finite measures on `Œ©` with the topology of weak
   convergence of measures.
 * `MeasureTheory.FiniteMeasure.toWeakDualBCNN : FiniteMeasure Œ© ‚Üí (WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0))`:
   Interpret a finite measure as a continuous linear functional on the space of
   bounded continuous nonnegative functions on `Œ©`. This is used for the definition of the
   topology of weak convergence.
 * `MeasureTheory.FiniteMeasure.map`: The push-forward `f* Œº` of a finite measure `Œº` on `Œ©`
   along a measurable function `f : Œ© ‚Üí Œ©'`.
 * `MeasureTheory.FiniteMeasure.mapCLM`: The push-forward along a given continuous `f : Œ© ‚Üí Œ©'`
   as a continuous linear map `f* : FiniteMeasure Œ© ‚ÜíL[‚Ñù‚â•0] FiniteMeasure Œ©'`.

## Main results

 * Finite measures `Œº` on `Œ©` give rise to continuous linear functionals on the space of
   bounded continuous nonnegative functions on `Œ©` via integration:
   `MeasureTheory.FiniteMeasure.toWeakDualBCNN : FiniteMeasure Œ© ‚Üí (WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0))`
 * `MeasureTheory.FiniteMeasure.tendsto_iff_forall_integral_tendsto`: Convergence of finite
   measures is characterized by the convergence of integrals of all bounded continuous functions.
   This shows that the chosen definition of topology coincides with the common textbook definition
   of weak convergence of measures. A similar characterization by the convergence of integrals (in
   the `MeasureTheory.lintegral` sense) of all bounded continuous nonnegative functions is
   `MeasureTheory.FiniteMeasure.tendsto_iff_forall_lintegral_tendsto`.
 * `MeasureTheory.FiniteMeasure.continuous_map`: For a continuous function `f : Œ© ‚Üí Œ©'`, the
   push-forward of finite measures `f* : FiniteMeasure Œ© ‚Üí FiniteMeasure Œ©'` is continuous.
 * `MeasureTheory.FiniteMeasure.t2Space`: The topology of weak convergence of finite Borel measures
   is Hausdorff on spaces where indicators of closed sets have continuous decreasing approximating
   sequences (in particular on any pseudo-metrizable spaces).

## Implementation notes

The topology of weak convergence of finite Borel measures is defined using a mapping from
`MeasureTheory.FiniteMeasure Œ©` to `WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0)`, inheriting the topology from the
latter.

The implementation of `MeasureTheory.FiniteMeasure Œ©` and is directly as a subtype of
`MeasureTheory.Measure Œ©`, and the coercion to a function is the composition `ENNReal.toNNReal`
and the coercion to function of `MeasureTheory.Measure Œ©`. Another alternative would have been to
use a bijection with `MeasureTheory.VectorMeasure Œ© ‚Ñù‚â•0` as an intermediate step. Some
considerations:
 * Potential advantages of using the `NNReal`-valued vector measure alternative:
   * The coercion to function would avoid need to compose with `ENNReal.toNNReal`, the
     `NNReal`-valued API could be more directly available.
 * Potential drawbacks of the vector measure alternative:
   * The coercion to function would lose monotonicity, as non-measurable sets would be defined to
     have measure 0.
   * No integration theory directly. E.g., the topology definition requires
     `MeasureTheory.lintegral` w.r.t. a coercion to `MeasureTheory.Measure Œ©` in any case.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

weak convergence of measures, finite measure

-/


noncomputable section

open BoundedContinuousFunction Filter MeasureTheory Set Topology
open scoped ENNReal NNReal

namespace MeasureTheory

namespace FiniteMeasure

section FiniteMeasure

/-! ### Finite measures

In this section we define the `Type` of `MeasureTheory.FiniteMeasure Œ©`, when `Œ©` is a measurable
space. Finite measures on `Œ©` are a module over `‚Ñù‚â•0`.

If `Œ©` is moreover a topological space and the sigma algebra on `Œ©` is finer than the Borel sigma
algebra (i.e. `[OpensMeasurableSpace Œ©]`), then `MeasureTheory.FiniteMeasure Œ©` is equipped with
the topology of weak convergence of measures. This is implemented by defining a pairing of finite
measures `Œº` on `Œ©` with continuous bounded nonnegative functions `f : Œ© ‚Üí·µá ‚Ñù‚â•0` via integration,
and using the associated weak topology (essentially the weak-star topology on the dual of
`Œ© ‚Üí·µá ‚Ñù‚â•0`).
-/


variable {Œ© : Type*} [MeasurableSpace Œ©]

/-- Finite measures are defined as the subtype of measures that have the property of being finite
measures (i.e., their total mass is finite). -/
def _root_.MeasureTheory.FiniteMeasure (Œ© : Type*) [MeasurableSpace Œ©] : Type _ :=
  { Œº : Measure Œ© // IsFiniteMeasure Œº }

-- Porting note: as with other subtype synonyms (e.g., `‚Ñù‚â•0`, we need a new function for the
-- coercion instead of relying on `Subtype.val`.
/-- Coercion from `MeasureTheory.FiniteMeasure Œ©` to `MeasureTheory.Measure Œ©`. -/
@[coe]
def toMeasure : FiniteMeasure Œ© ‚Üí Measure Œ© := Subtype.val

/-- A finite measure can be interpreted as a measure. -/
instance instCoe : Coe (FiniteMeasure Œ©) (MeasureTheory.Measure Œ©) := { coe := toMeasure }

instance isFiniteMeasure (Œº : FiniteMeasure Œ©) : IsFiniteMeasure (Œº : Measure Œ©) := Œº.prop

@[simp]
theorem val_eq_toMeasure (ŒΩ : FiniteMeasure Œ©) : ŒΩ.val = (ŒΩ : Measure Œ©) := rfl

theorem toMeasure_injective : Function.Injective ((‚Üë) : FiniteMeasure Œ© ‚Üí Measure Œ©) :=
  Subtype.coe_injective

instance instFunLike : FunLike (FiniteMeasure Œ©) (Set Œ©) ‚Ñù‚â•0 where
  coe Œº s := ((Œº : Measure Œ©) s).toNNReal
  coe_injective' Œº ŒΩ h := toMeasure_injective <| Measure.ext fun s _ ‚Ü¶ by
    simpa [ENNReal.toNNReal_eq_toNNReal_iff, measure_ne_top] using congr_fun h s

lemma coeFn_def (Œº : FiniteMeasure Œ©) : Œº = fun s ‚Ü¶ ((Œº : Measure Œ©) s).toNNReal := rfl

lemma coeFn_mk (Œº : Measure Œ©) (hŒº) :
    DFunLike.coe (F := FiniteMeasure Œ©) ‚ü®Œº, hŒº‚ü© = fun s ‚Ü¶ (Œº s).toNNReal := rfl

@[simp, norm_cast]
lemma mk_apply (Œº : Measure Œ©) (hŒº) (s : Set Œ©) :
    DFunLike.coe (F := FiniteMeasure Œ©) ‚ü®Œº, hŒº‚ü© s = (Œº s).toNNReal := rfl

@[simp]
theorem ennreal_coeFn_eq_coeFn_toMeasure (ŒΩ : FiniteMeasure Œ©) (s : Set Œ©) :
    (ŒΩ s : ‚Ñù‚â•0‚àû) = (ŒΩ : Measure Œ©) s :=
  ENNReal.coe_toNNReal (measure_lt_top (‚ÜëŒΩ) s).ne

@[simp]
theorem null_iff_toMeasure_null (ŒΩ : FiniteMeasure Œ©) (s : Set Œ©) :
    ŒΩ s = 0 ‚Üî (ŒΩ : Measure Œ©) s = 0 :=
  ‚ü®fun h ‚Ü¶ by rw [‚Üê ennreal_coeFn_eq_coeFn_toMeasure, h, ENNReal.coe_zero],
   fun h ‚Ü¶ congrArg ENNReal.toNNReal h‚ü©

theorem apply_mono (Œº : FiniteMeasure Œ©) {s‚ÇÅ s‚ÇÇ : Set Œ©} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : Œº s‚ÇÅ ‚â§ Œº s‚ÇÇ :=
  ENNReal.toNNReal_mono (measure_ne_top _ s‚ÇÇ) ((Œº : Measure Œ©).mono h)

/-- The (total) mass of a finite measure `Œº` is `Œº univ`, i.e., the cast to `NNReal` of
`(Œº : measure Œ©) univ`. -/
def mass (Œº : FiniteMeasure Œ©) : ‚Ñù‚â•0 := Œº univ

@[simp] theorem apply_le_mass (Œº : FiniteMeasure Œ©) (s : Set Œ©) : Œº s ‚â§ Œº.mass := by
  simpa using apply_mono Œº (subset_univ s)

@[simp]
theorem ennreal_mass {Œº : FiniteMeasure Œ©} : (Œº.mass : ‚Ñù‚â•0‚àû) = (Œº : Measure Œ©) univ :=
  ennreal_coeFn_eq_coeFn_toMeasure Œº Set.univ

instance instZero : Zero (FiniteMeasure Œ©) where zero := ‚ü®0, MeasureTheory.isFiniteMeasureZero‚ü©

@[simp, norm_cast] lemma coeFn_zero : ‚áë(0 : FiniteMeasure Œ©) = 0 := rfl

@[simp]
theorem zero_mass : (0 : FiniteMeasure Œ©).mass = 0 := rfl

@[simp]
theorem mass_zero_iff (Œº : FiniteMeasure Œ©) : Œº.mass = 0 ‚Üî Œº = 0 := by
  refine ‚ü®fun Œº_mass => ?_, fun hŒº => by simp only [hŒº, zero_mass]‚ü©
  apply toMeasure_injective
  apply Measure.measure_univ_eq_zero.mp
  rwa [‚Üê ennreal_mass, ENNReal.coe_eq_zero]

theorem mass_nonzero_iff (Œº : FiniteMeasure Œ©) : Œº.mass ‚â† 0 ‚Üî Œº ‚â† 0 :=
  not_iff_not.mpr <| FiniteMeasure.mass_zero_iff Œº

@[ext]
theorem eq_of_forall_toMeasure_apply_eq (Œº ŒΩ : FiniteMeasure Œ©)
    (h : ‚àÄ s : Set Œ©, MeasurableSet s ‚Üí (Œº : Measure Œ©) s = (ŒΩ : Measure Œ©) s) : Œº = ŒΩ := by
  apply Subtype.ext
  ext1 s s_mble
  exact h s s_mble

theorem eq_of_forall_apply_eq (Œº ŒΩ : FiniteMeasure Œ©)
    (h : ‚àÄ s : Set Œ©, MeasurableSet s ‚Üí Œº s = ŒΩ s) : Œº = ŒΩ := by
  ext1 s s_mble
  simpa [ennreal_coeFn_eq_coeFn_toMeasure] using congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) (h s s_mble)

instance instInhabited : Inhabited (FiniteMeasure Œ©) := ‚ü®0‚ü©

instance instAdd : Add (FiniteMeasure Œ©) where add Œº ŒΩ := ‚ü®Œº + ŒΩ, MeasureTheory.isFiniteMeasureAdd‚ü©

variable {R : Type*} [SMul R ‚Ñù‚â•0] [SMul R ‚Ñù‚â•0‚àû] [IsScalarTower R ‚Ñù‚â•0 ‚Ñù‚â•0‚àû]
  [IsScalarTower R ‚Ñù‚â•0‚àû ‚Ñù‚â•0‚àû]

instance instSMul : SMul R (FiniteMeasure Œ©) where
  smul (c : R) Œº := ‚ü®c ‚Ä¢ (Œº : Measure Œ©), MeasureTheory.isFiniteMeasureSMulOfNNRealTower‚ü©

@[simp, norm_cast]
theorem toMeasure_zero : ((‚Üë) : FiniteMeasure Œ© ‚Üí Measure Œ©) 0 = 0 := rfl

-- Porting note: with `simp` here the `coeFn` lemmas below fall prey to `simpNF`: the LHS simplifies
@[norm_cast]
theorem toMeasure_add (Œº ŒΩ : FiniteMeasure Œ©) : ‚Üë(Œº + ŒΩ) = (‚ÜëŒº + ‚ÜëŒΩ : Measure Œ©) := rfl

@[simp, norm_cast]
theorem toMeasure_smul (c : R) (Œº : FiniteMeasure Œ©) : ‚Üë(c ‚Ä¢ Œº) = c ‚Ä¢ (Œº : Measure Œ©) :=
  rfl

@[simp, norm_cast]
theorem coeFn_add (Œº ŒΩ : FiniteMeasure Œ©) : (‚áë(Œº + ŒΩ) : Set Œ© ‚Üí ‚Ñù‚â•0) = (‚áëŒº + ‚áëŒΩ : Set Œ© ‚Üí ‚Ñù‚â•0) := by
  funext
  simp only [Pi.add_apply, ‚Üê ENNReal.coe_inj, ne_eq, ennreal_coeFn_eq_coeFn_toMeasure,
    ENNReal.coe_add]
  norm_cast

@[simp, norm_cast]
theorem coeFn_smul [IsScalarTower R ‚Ñù‚â•0 ‚Ñù‚â•0] (c : R) (Œº : FiniteMeasure Œ©) :
    (‚áë(c ‚Ä¢ Œº) : Set Œ© ‚Üí ‚Ñù‚â•0) = c ‚Ä¢ (‚áëŒº : Set Œ© ‚Üí ‚Ñù‚â•0) := by
  funext; simp [‚Üê ENNReal.coe_inj, ENNReal.coe_smul]

instance instAddCommMonoid : AddCommMonoid (FiniteMeasure Œ©) :=
  toMeasure_injective.addCommMonoid _ toMeasure_zero toMeasure_add fun _ _ ‚Ü¶ toMeasure_smul _ _

/-- Coercion is an `AddMonoidHom`. -/
@[simps]
def toMeasureAddMonoidHom : FiniteMeasure Œ© ‚Üí+ Measure Œ© where
  toFun := (‚Üë)
  map_zero' := toMeasure_zero
  map_add' := toMeasure_add

instance {Œ© : Type*} [MeasurableSpace Œ©] : Module ‚Ñù‚â•0 (FiniteMeasure Œ©) :=
  Function.Injective.module _ toMeasureAddMonoidHom toMeasure_injective toMeasure_smul

@[simp]
theorem smul_apply [IsScalarTower R ‚Ñù‚â•0 ‚Ñù‚â•0] (c : R) (Œº : FiniteMeasure Œ©) (s : Set Œ©) :
    (c ‚Ä¢ Œº) s = c ‚Ä¢ Œº s := by
  rw [coeFn_smul, Pi.smul_apply]

/-- Restrict a finite measure Œº to a set A. -/
def restrict (Œº : FiniteMeasure Œ©) (A : Set Œ©) : FiniteMeasure Œ© where
  val := (Œº : Measure Œ©).restrict A
  property := MeasureTheory.isFiniteMeasureRestrict (Œº : Measure Œ©) A

theorem restrict_measure_eq (Œº : FiniteMeasure Œ©) (A : Set Œ©) :
    (Œº.restrict A : Measure Œ©) = (Œº : Measure Œ©).restrict A := rfl

theorem restrict_apply_measure (Œº : FiniteMeasure Œ©) (A : Set Œ©) {s : Set Œ©}
    (s_mble : MeasurableSet s) : (Œº.restrict A : Measure Œ©) s = (Œº : Measure Œ©) (s ‚à© A) :=
  Measure.restrict_apply s_mble

theorem restrict_apply (Œº : FiniteMeasure Œ©) (A : Set Œ©) {s : Set Œ©} (s_mble : MeasurableSet s) :
    (Œº.restrict A) s = Œº (s ‚à© A) := by
  apply congr_arg ENNReal.toNNReal
  exact Measure.restrict_apply s_mble

theorem restrict_mass (Œº : FiniteMeasure Œ©) (A : Set Œ©) : (Œº.restrict A).mass = Œº A := by
  simp only [mass, restrict_apply Œº A MeasurableSet.univ, univ_inter]

theorem restrict_eq_zero_iff (Œº : FiniteMeasure Œ©) (A : Set Œ©) : Œº.restrict A = 0 ‚Üî Œº A = 0 := by
  rw [‚Üê mass_zero_iff, restrict_mass]

theorem restrict_nonzero_iff (Œº : FiniteMeasure Œ©) (A : Set Œ©) : Œº.restrict A ‚â† 0 ‚Üî Œº A ‚â† 0 := by
  rw [‚Üê mass_nonzero_iff, restrict_mass]

variable [TopologicalSpace Œ©]

/-- Two finite Borel measures are equal if the integrals of all bounded continuous functions with
respect to both agree. -/
theorem ext_of_forall_lintegral_eq [HasOuterApproxClosed Œ©] [BorelSpace Œ©]
    {Œº ŒΩ : FiniteMeasure Œ©} (h : ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù‚â•0), ‚à´‚Åª x, f x ‚àÇŒº = ‚à´‚Åª x, f x ‚àÇŒΩ) :
    Œº = ŒΩ := by
  apply Subtype.ext
  change (Œº : Measure Œ©) = (ŒΩ : Measure Œ©)
  exact ext_of_forall_lintegral_eq_of_IsFiniteMeasure h

/-- The pairing of a finite (Borel) measure `Œº` with a nonnegative bounded continuous
function is obtained by (Lebesgue) integrating the (test) function against the measure.
This is `MeasureTheory.FiniteMeasure.testAgainstNN`. -/
def testAgainstNN (Œº : FiniteMeasure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) : ‚Ñù‚â•0 :=
  (‚à´‚Åª œâ, f œâ ‚àÇ(Œº : Measure Œ©)).toNNReal

@[simp]
theorem testAgainstNN_coe_eq {Œº : FiniteMeasure Œ©} {f : Œ© ‚Üí·µá ‚Ñù‚â•0} :
    (Œº.testAgainstNN f : ‚Ñù‚â•0‚àû) = ‚à´‚Åª œâ, f œâ ‚àÇ(Œº : Measure Œ©) :=
  ENNReal.coe_toNNReal (f.lintegral_lt_top_of_nnreal _).ne

theorem testAgainstNN_const (Œº : FiniteMeasure Œ©) (c : ‚Ñù‚â•0) :
    Œº.testAgainstNN (BoundedContinuousFunction.const Œ© c) = c * Œº.mass := by
  simp [‚Üê ENNReal.coe_inj]

theorem testAgainstNN_mono (Œº : FiniteMeasure Œ©) {f g : Œ© ‚Üí·µá ‚Ñù‚â•0} (f_le_g : (f : Œ© ‚Üí ‚Ñù‚â•0) ‚â§ g) :
    Œº.testAgainstNN f ‚â§ Œº.testAgainstNN g := by
  simp only [‚Üê ENNReal.coe_le_coe, testAgainstNN_coe_eq]
  gcongr
  apply f_le_g

@[simp]
theorem testAgainstNN_zero (Œº : FiniteMeasure Œ©) : Œº.testAgainstNN 0 = 0 := by
  simpa only [zero_mul] using Œº.testAgainstNN_const 0

@[simp]
theorem testAgainstNN_one (Œº : FiniteMeasure Œ©) : Œº.testAgainstNN 1 = Œº.mass := by
  simp only [testAgainstNN, coe_one, Pi.one_apply, ENNReal.coe_one, lintegral_one]
  rfl

@[simp]
theorem zero_testAgainstNN_apply (f : Œ© ‚Üí·µá ‚Ñù‚â•0) : (0 : FiniteMeasure Œ©).testAgainstNN f = 0 := by
  simp only [testAgainstNN, toMeasure_zero, lintegral_zero_measure, ENNReal.zero_toNNReal]

theorem zero_testAgainstNN : (0 : FiniteMeasure Œ©).testAgainstNN = 0 := by
  funext
  simp only [zero_testAgainstNN_apply, Pi.zero_apply]

@[simp]
theorem smul_testAgainstNN_apply (c : ‚Ñù‚â•0) (Œº : FiniteMeasure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    (c ‚Ä¢ Œº).testAgainstNN f = c ‚Ä¢ Œº.testAgainstNN f := by
  simp only [testAgainstNN, toMeasure_smul, smul_eq_mul, ‚Üê ENNReal.smul_toNNReal, ENNReal.smul_def,
    lintegral_smul_measure]

section weak_convergence

variable [OpensMeasurableSpace Œ©]

theorem testAgainstNN_add (Œº : FiniteMeasure Œ©) (f‚ÇÅ f‚ÇÇ : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.testAgainstNN (f‚ÇÅ + f‚ÇÇ) = Œº.testAgainstNN f‚ÇÅ + Œº.testAgainstNN f‚ÇÇ := by
  simp only [‚Üê ENNReal.coe_inj, BoundedContinuousFunction.coe_add, ENNReal.coe_add, Pi.add_apply,
    testAgainstNN_coe_eq]
  exact lintegral_add_left (BoundedContinuousFunction.measurable_coe_ennreal_comp _) _

theorem testAgainstNN_smul [IsScalarTower R ‚Ñù‚â•0 ‚Ñù‚â•0] [PseudoMetricSpace R] [Zero R]
    [BoundedSMul R ‚Ñù‚â•0] (Œº : FiniteMeasure Œ©) (c : R) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.testAgainstNN (c ‚Ä¢ f) = c ‚Ä¢ Œº.testAgainstNN f := by
  simp only [‚Üê ENNReal.coe_inj, BoundedContinuousFunction.coe_smul, testAgainstNN_coe_eq,
    ENNReal.coe_smul]
  simp_rw [‚Üê smul_one_smul ‚Ñù‚â•0‚àû c (f _ : ‚Ñù‚â•0‚àû), ‚Üê smul_one_smul ‚Ñù‚â•0‚àû c (lintegral _ _ : ‚Ñù‚â•0‚àû),
    smul_eq_mul]
  exact lintegral_const_mul (c ‚Ä¢ (1 : ‚Ñù‚â•0‚àû)) f.measurable_coe_ennreal_comp

theorem testAgainstNN_lipschitz_estimate (Œº : FiniteMeasure Œ©) (f g : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.testAgainstNN f ‚â§ Œº.testAgainstNN g + nndist f g * Œº.mass := by
  simp only [‚Üê Œº.testAgainstNN_const (nndist f g), ‚Üê testAgainstNN_add, ‚Üê ENNReal.coe_le_coe,
    BoundedContinuousFunction.coe_add, const_apply, ENNReal.coe_add, Pi.add_apply,
    coe_nnreal_ennreal_nndist, testAgainstNN_coe_eq]
  apply lintegral_mono
  have le_dist : ‚àÄ œâ, dist (f œâ) (g œâ) ‚â§ nndist f g := BoundedContinuousFunction.dist_coe_le_dist
  intro œâ
  have le' : f œâ ‚â§ g œâ + nndist f g := by
    calc f œâ
     _ ‚â§ g œâ + nndist (f œâ) (g œâ)     := NNReal.le_add_nndist (f œâ) (g œâ)
     _ ‚â§ g œâ + nndist f g             := (add_le_add_iff_left (g œâ)).mpr (le_dist œâ)
  have le : (f œâ : ‚Ñù‚â•0‚àû) ‚â§ (g œâ : ‚Ñù‚â•0‚àû) + nndist f g := by
    simpa only [‚Üê ENNReal.coe_add] using (by exact_mod_cast le')
  rwa [coe_nnreal_ennreal_nndist] at le

theorem testAgainstNN_lipschitz (Œº : FiniteMeasure Œ©) :
    LipschitzWith Œº.mass fun f : Œ© ‚Üí·µá ‚Ñù‚â•0 ‚Ü¶ Œº.testAgainstNN f := by
  rw [lipschitzWith_iff_dist_le_mul]
  intro f‚ÇÅ f‚ÇÇ
  suffices abs (Œº.testAgainstNN f‚ÇÅ - Œº.testAgainstNN f‚ÇÇ : ‚Ñù) ‚â§ Œº.mass * dist f‚ÇÅ f‚ÇÇ by
    rwa [NNReal.dist_eq]
  apply abs_le.mpr
  constructor
  ¬∑ have key := Œº.testAgainstNN_lipschitz_estimate f‚ÇÇ f‚ÇÅ
    rw [mul_comm] at key
    suffices ‚Üë(Œº.testAgainstNN f‚ÇÇ) ‚â§ ‚Üë(Œº.testAgainstNN f‚ÇÅ) + ‚ÜëŒº.mass * dist f‚ÇÅ f‚ÇÇ by linarith
    simpa [nndist_comm] using NNReal.coe_mono key
  ¬∑ have key := Œº.testAgainstNN_lipschitz_estimate f‚ÇÅ f‚ÇÇ
    rw [mul_comm] at key
    suffices ‚Üë(Œº.testAgainstNN f‚ÇÅ) ‚â§ ‚Üë(Œº.testAgainstNN f‚ÇÇ) + ‚ÜëŒº.mass * dist f‚ÇÅ f‚ÇÇ by linarith
    simpa using NNReal.coe_mono key

/-- Finite measures yield elements of the `WeakDual` of bounded continuous nonnegative
functions via `MeasureTheory.FiniteMeasure.testAgainstNN`, i.e., integration. -/
def toWeakDualBCNN (Œº : FiniteMeasure Œ©) : WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0) where
  toFun f := Œº.testAgainstNN f
  map_add' := testAgainstNN_add Œº
  map_smul' := testAgainstNN_smul Œº
  cont := Œº.testAgainstNN_lipschitz.continuous

@[simp]
theorem coe_toWeakDualBCNN (Œº : FiniteMeasure Œ©) : ‚áëŒº.toWeakDualBCNN = Œº.testAgainstNN :=
  rfl

@[simp]
theorem toWeakDualBCNN_apply (Œº : FiniteMeasure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Œº.toWeakDualBCNN f = (‚à´‚Åª x, f x ‚àÇ(Œº : Measure Œ©)).toNNReal := rfl

/-- The topology of weak convergence on `MeasureTheory.FiniteMeasure Œ©` is inherited (induced)
from the weak-* topology on `WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0)` via the function
`MeasureTheory.FiniteMeasure.toWeakDualBCNN`. -/
instance instTopologicalSpace : TopologicalSpace (FiniteMeasure Œ©) :=
  TopologicalSpace.induced toWeakDualBCNN inferInstance

theorem toWeakDualBCNN_continuous : Continuous (@toWeakDualBCNN Œ© _ _ _) :=
  continuous_induced_dom

/-- Integration of (nonnegative bounded continuous) test functions against finite Borel measures
depends continuously on the measure. -/
theorem continuous_testAgainstNN_eval (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Continuous fun Œº : FiniteMeasure Œ© ‚Ü¶ Œº.testAgainstNN f := by
  show Continuous ((fun œÜ : WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0) ‚Ü¶ œÜ f) ‚àò toWeakDualBCNN)
  refine Continuous.comp ?_ (toWeakDualBCNN_continuous (Œ© := Œ©))
  exact WeakBilin.eval_continuous (ùïú := ‚Ñù‚â•0) (E := (Œ© ‚Üí·µá ‚Ñù‚â•0) ‚ÜíL[‚Ñù‚â•0] ‚Ñù‚â•0) _ _
  /- porting note: without explicitly providing `ùïú` and `E` TC synthesis times
  out trying to find `Module ‚Ñù‚â•0 ((Œ© ‚Üí·µá ‚Ñù‚â•0) ‚ÜíL[‚Ñù‚â•0] ‚Ñù‚â•0)`, but it can find it with enough time:
  `set_option synthInstance.maxHeartbeats 47000` was sufficient. -/

/-- The total mass of a finite measure depends continuously on the measure. -/
theorem continuous_mass : Continuous fun Œº : FiniteMeasure Œ© ‚Ü¶ Œº.mass := by
  simp_rw [‚Üê testAgainstNN_one]; exact continuous_testAgainstNN_eval 1

/-- Convergence of finite measures implies the convergence of their total masses. -/
theorem _root_.Filter.Tendsto.mass {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    {Œº : FiniteMeasure Œ©} (h : Tendsto Œºs F (ùìù Œº)) : Tendsto (fun i ‚Ü¶ (Œºs i).mass) F (ùìù Œº.mass) :=
  (continuous_mass.tendsto Œº).comp h

theorem tendsto_iff_weakDual_tendsto {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    {Œº : FiniteMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî Tendsto (fun i ‚Ü¶ (Œºs i).toWeakDualBCNN) F (ùìù Œº.toWeakDualBCNN) :=
  IsInducing.tendsto_nhds_iff ‚ü®rfl‚ü©

theorem tendsto_iff_forall_toWeakDualBCNN_tendsto {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©} {Œº : FiniteMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ‚Ñù‚â•0, Tendsto (fun i ‚Ü¶ (Œºs i).toWeakDualBCNN f) F (ùìù (Œº.toWeakDualBCNN f)) := by
  rw [tendsto_iff_weakDual_tendsto, tendsto_iff_forall_eval_tendsto_topDualPairing]; rfl

theorem tendsto_iff_forall_testAgainstNN_tendsto {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©} {Œº : FiniteMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ‚Ñù‚â•0, Tendsto (fun i ‚Ü¶ (Œºs i).testAgainstNN f) F (ùìù (Œº.testAgainstNN f)) := by
  rw [FiniteMeasure.tendsto_iff_forall_toWeakDualBCNN_tendsto]; rfl

/-- If the total masses of finite measures tend to zero, then the measures tend to
zero. This formulation concerns the associated functionals on bounded continuous
nonnegative test functions. See `MeasureTheory.FiniteMeasure.tendsto_zero_of_tendsto_zero_mass` for
a formulation stating the weak convergence of measures. -/
theorem tendsto_zero_testAgainstNN_of_tendsto_zero_mass {Œ≥ : Type*} {F : Filter Œ≥}
    {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©} (mass_lim : Tendsto (fun i ‚Ü¶ (Œºs i).mass) F (ùìù 0)) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
    Tendsto (fun i ‚Ü¶ (Œºs i).testAgainstNN f) F (ùìù 0) := by
  apply tendsto_iff_dist_tendsto_zero.mpr
  have obs := fun i ‚Ü¶ (Œºs i).testAgainstNN_lipschitz_estimate f 0
  simp_rw [testAgainstNN_zero, zero_add] at obs
  simp_rw [show ‚àÄ i, dist ((Œºs i).testAgainstNN f) 0 = (Œºs i).testAgainstNN f by
      simp only [dist_nndist, NNReal.nndist_zero_eq_val', eq_self_iff_true, imp_true_iff]]
  apply squeeze_zero (fun i ‚Ü¶ NNReal.coe_nonneg _) obs
  have lim_pair : Tendsto (fun i ‚Ü¶ (‚ü®nndist f 0, (Œºs i).mass‚ü© : ‚Ñù √ó ‚Ñù)) F (ùìù ‚ü®nndist f 0, 0‚ü©) :=
    (Prod.tendsto_iff _ _).mpr ‚ü®tendsto_const_nhds, (NNReal.continuous_coe.tendsto 0).comp mass_lim‚ü©
  simpa using tendsto_mul.comp lim_pair

/-- If the total masses of finite measures tend to zero, then the measures tend to zero. -/
theorem tendsto_zero_of_tendsto_zero_mass {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    (mass_lim : Tendsto (fun i ‚Ü¶ (Œºs i).mass) F (ùìù 0)) : Tendsto Œºs F (ùìù 0) := by
  rw [tendsto_iff_forall_testAgainstNN_tendsto]
  intro f
  convert tendsto_zero_testAgainstNN_of_tendsto_zero_mass mass_lim f
  rw [zero_testAgainstNN_apply]

/-- A characterization of weak convergence in terms of integrals of bounded continuous
nonnegative functions. -/
theorem tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    {Œº : FiniteMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ‚Ñù‚â•0,
        Tendsto (fun i ‚Ü¶ ‚à´‚Åª x, f x ‚àÇ(Œºs i : Measure Œ©)) F (ùìù (‚à´‚Åª x, f x ‚àÇ(Œº : Measure Œ©))) := by
  rw [tendsto_iff_forall_toWeakDualBCNN_tendsto]
  simp_rw [toWeakDualBCNN_apply _ _, ‚Üê testAgainstNN_coe_eq, ENNReal.tendsto_coe,
    ENNReal.toNNReal_coe]

end weak_convergence -- section

section Hausdorff

variable [HasOuterApproxClosed Œ©] [BorelSpace Œ©]

open Function

/-- The mapping `toWeakDualBCNN` from finite Borel measures to the weak dual of `Œ© ‚Üí·µá ‚Ñù‚â•0` is
injective, if in the underlying space `Œ©`, indicator functions of closed sets have decreasing
approximations by sequences of continuous functions (in particular if `Œ©` is pseudometrizable). -/
lemma injective_toWeakDualBCNN :
    Injective (toWeakDualBCNN : FiniteMeasure Œ© ‚Üí WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0)) := by
  intro Œº ŒΩ hŒºŒΩ
  apply ext_of_forall_lintegral_eq
  intro f
  have key := congr_fun (congrArg DFunLike.coe hŒºŒΩ) f
  apply (ENNReal.toNNReal_eq_toNNReal_iff' ?_ ?_).mp key
  ¬∑ exact (lintegral_lt_top_of_nnreal Œº f).ne
  ¬∑ exact (lintegral_lt_top_of_nnreal ŒΩ f).ne

variable (Œ©)

lemma isEmbedding_toWeakDualBCNN :
    IsEmbedding (toWeakDualBCNN : FiniteMeasure Œ© ‚Üí WeakDual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0)) where
  eq_induced := rfl
  injective := injective_toWeakDualBCNN

@[deprecated (since := "2024-10-26")]
alias embedding_toWeakDualBCNN := isEmbedding_toWeakDualBCNN

/-- On topological spaces where indicators of closed sets have decreasing approximating sequences of
continuous functions (`HasOuterApproxClosed`), the topology of weak convergence of finite Borel
measures is Hausdorff (`T2Space`). -/
instance t2Space : T2Space (FiniteMeasure Œ©) := (isEmbedding_toWeakDualBCNN Œ©).t2Space

end Hausdorff -- section

end FiniteMeasure

-- section
section FiniteMeasureBoundedConvergence

/-! ### Bounded convergence results for finite measures

This section is about bounded convergence theorems for finite measures.
-/


variable {Œ© : Type*} [MeasurableSpace Œ©] [TopologicalSpace Œ©] [OpensMeasurableSpace Œ©]

/-- A bounded convergence theorem for a finite measure:
If a sequence of bounded continuous non-negative functions are uniformly bounded by a constant
and tend pointwise to a limit, then their integrals (`MeasureTheory.lintegral`) against the finite
measure tend to the integral of the limit.

A related result with more general assumptions is
`MeasureTheory.tendsto_lintegral_nn_filter_of_le_const`.
-/
theorem tendsto_lintegral_nn_of_le_const (Œº : FiniteMeasure Œ©) {fs : ‚Ñï ‚Üí Œ© ‚Üí·µá ‚Ñù‚â•0} {c : ‚Ñù‚â•0}
    (fs_le_const : ‚àÄ n œâ, fs n œâ ‚â§ c) {f : Œ© ‚Üí ‚Ñù‚â•0}
    (fs_lim : ‚àÄ œâ, Tendsto (fun n ‚Ü¶ fs n œâ) atTop (ùìù (f œâ))) :
    Tendsto (fun n ‚Ü¶ ‚à´‚Åª œâ, fs n œâ ‚àÇ(Œº : Measure Œ©)) atTop (ùìù (‚à´‚Åª œâ, f œâ ‚àÇ(Œº : Measure Œ©))) :=
  tendsto_lintegral_nn_filter_of_le_const Œº
    (.of_forall fun n ‚Ü¶ .of_forall (fs_le_const n))
    (.of_forall fs_lim)

/-- A bounded convergence theorem for a finite measure:
If bounded continuous non-negative functions are uniformly bounded by a constant and tend to a
limit, then their integrals against the finite measure tend to the integral of the limit.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere;
 * integration is the pairing against non-negative continuous test functions
   (`MeasureTheory.FiniteMeasure.testAgainstNN`).

A related result using `MeasureTheory.lintegral` for integration is
`MeasureTheory.FiniteMeasure.tendsto_lintegral_nn_filter_of_le_const`.
-/
theorem tendsto_testAgainstNN_filter_of_le_const {Œπ : Type*} {L : Filter Œπ}
    [L.IsCountablyGenerated] {Œº : FiniteMeasure Œ©} {fs : Œπ ‚Üí Œ© ‚Üí·µá ‚Ñù‚â•0} {c : ‚Ñù‚â•0}
    (fs_le_const : ‚àÄ·∂† i in L, ‚àÄ·µê œâ : Œ© ‚àÇ(Œº : Measure Œ©), fs i œâ ‚â§ c) {f : Œ© ‚Üí·µá ‚Ñù‚â•0}
    (fs_lim : ‚àÄ·µê œâ : Œ© ‚àÇ(Œº : Measure Œ©), Tendsto (fun i ‚Ü¶ fs i œâ) L (ùìù (f œâ))) :
    Tendsto (fun i ‚Ü¶ Œº.testAgainstNN (fs i)) L (ùìù (Œº.testAgainstNN f)) := by
  apply (ENNReal.tendsto_toNNReal (f.lintegral_lt_top_of_nnreal (Œº : Measure Œ©)).ne).comp
  exact tendsto_lintegral_nn_filter_of_le_const (Œ© := Œ©) Œº fs_le_const fs_lim

/-- A bounded convergence theorem for a finite measure:
If a sequence of bounded continuous non-negative functions are uniformly bounded by a constant and
tend pointwise to a limit, then their integrals (`MeasureTheory.FiniteMeasure.testAgainstNN`)
against the finite measure tend to the integral of the limit.

Related results:
 * `MeasureTheory.FiniteMeasure.tendsto_testAgainstNN_filter_of_le_const`:
   more general assumptions
 * `MeasureTheory.FiniteMeasure.tendsto_lintegral_nn_of_le_const`:
   using `MeasureTheory.lintegral` for integration.
-/
theorem tendsto_testAgainstNN_of_le_const {Œº : FiniteMeasure Œ©} {fs : ‚Ñï ‚Üí Œ© ‚Üí·µá ‚Ñù‚â•0} {c : ‚Ñù‚â•0}
    (fs_le_const : ‚àÄ n œâ, fs n œâ ‚â§ c) {f : Œ© ‚Üí·µá ‚Ñù‚â•0}
    (fs_lim : ‚àÄ œâ, Tendsto (fun n ‚Ü¶ fs n œâ) atTop (ùìù (f œâ))) :
    Tendsto (fun n ‚Ü¶ Œº.testAgainstNN (fs n)) atTop (ùìù (Œº.testAgainstNN f)) :=
  tendsto_testAgainstNN_filter_of_le_const
    (.of_forall fun n ‚Ü¶ .of_forall (fs_le_const n))
    (.of_forall fs_lim)

end FiniteMeasureBoundedConvergence

-- section
section FiniteMeasureConvergenceByBoundedContinuousFunctions

/-! ### Weak convergence of finite measures with bounded continuous real-valued functions

In this section we characterize the weak convergence of finite measures by the usual (defining)
condition that the integrals of all bounded continuous real-valued functions converge.
-/


variable {Œ© : Type*} [MeasurableSpace Œ©] [TopologicalSpace Œ©] [OpensMeasurableSpace Œ©]

theorem tendsto_of_forall_integral_tendsto {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    {Œº : FiniteMeasure Œ©}
    (h : ‚àÄ f : Œ© ‚Üí·µá ‚Ñù,
          Tendsto (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ(Œºs i : Measure Œ©)) F (ùìù (‚à´ x, f x ‚àÇ(Œº : Measure Œ©)))) :
    Tendsto Œºs F (ùìù Œº) := by
  apply tendsto_iff_forall_lintegral_tendsto.mpr
  intro f
  apply (ENNReal.tendsto_toReal_iff (fi := F)
      (fun i ‚Ü¶ (f.lintegral_lt_top_of_nnreal (Œºs i)).ne) (f.lintegral_lt_top_of_nnreal Œº).ne).mp
  have lip : LipschitzWith 1 ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) := isometry_subtype_coe.lipschitz
  set f‚ÇÄ := BoundedContinuousFunction.comp _ lip f with _def_f‚ÇÄ
  have f‚ÇÄ_eq : ‚áëf‚ÇÄ = ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò ‚áëf := rfl
  have f‚ÇÄ_nn : 0 ‚â§ ‚áëf‚ÇÄ := fun _ ‚Ü¶ by
    simp only [f‚ÇÄ_eq, Pi.zero_apply, Function.comp_apply, NNReal.zero_le_coe]
  have f‚ÇÄ_ae_nn : 0 ‚â§·µê[(Œº : Measure Œ©)] ‚áëf‚ÇÄ := .of_forall f‚ÇÄ_nn
  have f‚ÇÄ_ae_nns : ‚àÄ i, 0 ‚â§·µê[(Œºs i : Measure Œ©)] ‚áëf‚ÇÄ := fun i ‚Ü¶ .of_forall f‚ÇÄ_nn
  have aux :=
    integral_eq_lintegral_of_nonneg_ae f‚ÇÄ_ae_nn f‚ÇÄ.continuous.measurable.aestronglyMeasurable
  have auxs := fun i ‚Ü¶
    integral_eq_lintegral_of_nonneg_ae (f‚ÇÄ_ae_nns i) f‚ÇÄ.continuous.measurable.aestronglyMeasurable
  simp_rw [f‚ÇÄ_eq, Function.comp_apply, ENNReal.ofReal_coe_nnreal] at aux auxs
  simpa only [‚Üê aux, ‚Üê auxs] using h f‚ÇÄ

/-- A characterization of weak convergence in terms of integrals of bounded continuous
real-valued functions. -/
theorem tendsto_iff_forall_integral_tendsto {Œ≥ : Type*} {F : Filter Œ≥} {Œºs : Œ≥ ‚Üí FiniteMeasure Œ©}
    {Œº : FiniteMeasure Œ©} :
    Tendsto Œºs F (ùìù Œº) ‚Üî
      ‚àÄ f : Œ© ‚Üí·µá ‚Ñù,
        Tendsto (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ(Œºs i : Measure Œ©)) F (ùìù (‚à´ x, f x ‚àÇ(Œº : Measure Œ©))) := by
  refine ‚ü®?_, tendsto_of_forall_integral_tendsto‚ü©
  rw [tendsto_iff_forall_lintegral_tendsto]
  intro h f
  simp_rw [BoundedContinuousFunction.integral_eq_integral_nnrealPart_sub]
  set f_pos := f.nnrealPart with _def_f_pos
  set f_neg := (-f).nnrealPart with _def_f_neg
  have tends_pos := (ENNReal.tendsto_toReal (f_pos.lintegral_lt_top_of_nnreal Œº).ne).comp (h f_pos)
  have tends_neg := (ENNReal.tendsto_toReal (f_neg.lintegral_lt_top_of_nnreal Œº).ne).comp (h f_neg)
  have aux :
    ‚àÄ g : Œ© ‚Üí·µá ‚Ñù‚â•0,
      (ENNReal.toReal ‚àò fun i : Œ≥ ‚Ü¶ ‚à´‚Åª x : Œ©, ‚Üë(g x) ‚àÇ(Œºs i : Measure Œ©)) =
        fun i : Œ≥ ‚Ü¶ (‚à´‚Åª x : Œ©, ‚Üë(g x) ‚àÇ(Œºs i : Measure Œ©)).toReal :=
    fun _ ‚Ü¶ rfl
  simp_rw [aux, BoundedContinuousFunction.toReal_lintegral_coe_eq_integral] at tends_pos tends_neg
  exact Tendsto.sub tends_pos tends_neg

lemma continuous_integral_boundedContinuousFunction
    {Œ± : Type*} [TopologicalSpace Œ±] [MeasurableSpace Œ±] [OpensMeasurableSpace Œ±] (f : Œ± ‚Üí·µá ‚Ñù) :
    Continuous fun Œº : FiniteMeasure Œ± ‚Ü¶ ‚à´ x, f x ‚àÇŒº := by
  rw [continuous_iff_continuousAt]
  intro Œº
  exact continuousAt_of_tendsto_nhds
    (FiniteMeasure.tendsto_iff_forall_integral_tendsto.mp tendsto_id f)

end FiniteMeasureConvergenceByBoundedContinuousFunctions -- section

section map

variable {Œ© Œ©' : Type*} [MeasurableSpace Œ©] [MeasurableSpace Œ©']

/-- The push-forward of a finite measure by a function between measurable spaces. -/
noncomputable def map (ŒΩ : FiniteMeasure Œ©) (f : Œ© ‚Üí Œ©') : FiniteMeasure Œ©' :=
  ‚ü®(ŒΩ : Measure Œ©).map f, by
    constructor
    by_cases f_aemble : AEMeasurable f ŒΩ
    ¬∑ rw [Measure.map_apply_of_aemeasurable f_aemble MeasurableSet.univ]
      exact measure_lt_top (‚ÜëŒΩ) (f ‚Åª¬π' univ)
    ¬∑ simp [Measure.map, f_aemble]‚ü©

@[simp] lemma toMeasure_map (ŒΩ : FiniteMeasure Œ©) (f : Œ© ‚Üí Œ©') :
    (ŒΩ.map f).toMeasure = ŒΩ.toMeasure.map f := rfl

/-- Note that this is an equality of elements of `‚Ñù‚â•0‚àû`. See also
`MeasureTheory.FiniteMeasure.map_apply` for the corresponding equality as elements of `‚Ñù‚â•0`. -/
lemma map_apply' (ŒΩ : FiniteMeasure Œ©) {f : Œ© ‚Üí Œ©'} (f_aemble : AEMeasurable f ŒΩ)
    {A : Set Œ©'} (A_mble : MeasurableSet A) :
    (ŒΩ.map f : Measure Œ©') A = (ŒΩ : Measure Œ©) (f ‚Åª¬π' A) :=
  Measure.map_apply_of_aemeasurable f_aemble A_mble

lemma map_apply_of_aemeasurable (ŒΩ : FiniteMeasure Œ©) {f : Œ© ‚Üí Œ©'} (f_aemble : AEMeasurable f ŒΩ)
    {A : Set Œ©'} (A_mble : MeasurableSet A) :
    ŒΩ.map f A = ŒΩ (f ‚Åª¬π' A) := by
  have key := ŒΩ.map_apply' f_aemble A_mble
  exact (ENNReal.toNNReal_eq_toNNReal_iff' (measure_ne_top _ _) (measure_ne_top _ _)).mpr key

lemma map_apply (ŒΩ : FiniteMeasure Œ©) {f : Œ© ‚Üí Œ©'} (f_mble : Measurable f)
    {A : Set Œ©'} (A_mble : MeasurableSet A) :
    ŒΩ.map f A = ŒΩ (f ‚Åª¬π' A) :=
  map_apply_of_aemeasurable ŒΩ f_mble.aemeasurable A_mble

@[simp] lemma map_add {f : Œ© ‚Üí Œ©'} (f_mble : Measurable f) (ŒΩ‚ÇÅ ŒΩ‚ÇÇ : FiniteMeasure Œ©) :
    (ŒΩ‚ÇÅ + ŒΩ‚ÇÇ).map f = ŒΩ‚ÇÅ.map f + ŒΩ‚ÇÇ.map f := by
  ext s s_mble
  simp only [map_apply' _ f_mble.aemeasurable s_mble, toMeasure_add, Measure.add_apply]

@[simp] lemma map_smul {f : Œ© ‚Üí Œ©'} (c : ‚Ñù‚â•0) (ŒΩ : FiniteMeasure Œ©) :
    (c ‚Ä¢ ŒΩ).map f = c ‚Ä¢ (ŒΩ.map f) := by
  ext s _
  simp [toMeasure_smul]

/-- The push-forward of a finite measure by a function between measurable spaces as a linear map. -/
noncomputable def mapHom {f : Œ© ‚Üí Œ©'} (f_mble : Measurable f) :
    FiniteMeasure Œ© ‚Üí‚Çó[‚Ñù‚â•0] FiniteMeasure Œ©' where
  toFun := fun ŒΩ ‚Ü¶ ŒΩ.map f
  map_add' := map_add f_mble
  map_smul' := map_smul

variable [TopologicalSpace Œ©] [OpensMeasurableSpace Œ©]
variable [TopologicalSpace Œ©'] [BorelSpace Œ©']

/-- If `f : X ‚Üí Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
(weak) convergence of `FiniteMeasure`s on `X` implies (weak) convergence of the push-forwards
of these measures by `f`. -/
lemma tendsto_map_of_tendsto_of_continuous {Œπ : Type*} {L : Filter Œπ}
    (ŒΩs : Œπ ‚Üí FiniteMeasure Œ©) (ŒΩ : FiniteMeasure Œ©) (lim : Tendsto ŒΩs L (ùìù ŒΩ))
    {f : Œ© ‚Üí Œ©'} (f_cont : Continuous f) :
    Tendsto (fun i ‚Ü¶ (ŒΩs i).map f) L (ùìù (ŒΩ.map f)) := by
  rw [FiniteMeasure.tendsto_iff_forall_lintegral_tendsto] at lim ‚ä¢
  intro g
  convert lim (g.compContinuous ‚ü®f, f_cont‚ü©) <;>
  ¬∑ simp only [map, compContinuous_apply, ContinuousMap.coe_mk]
    refine lintegral_map ?_ f_cont.measurable
    exact (ENNReal.continuous_coe.comp g.continuous).measurable

/-- If `f : X ‚Üí Y` is continuous and `Y` is equipped with the Borel sigma algebra, then
the push-forward of finite measures `f* : FiniteMeasure X ‚Üí FiniteMeasure Y` is continuous
(in the topologies of weak convergence of measures). -/
lemma continuous_map {f : Œ© ‚Üí Œ©'} (f_cont : Continuous f) :
    Continuous (fun ŒΩ ‚Ü¶ FiniteMeasure.map ŒΩ f) := by
  rw [continuous_iff_continuousAt]
  exact fun _ ‚Ü¶ tendsto_map_of_tendsto_of_continuous _ _ continuous_id.continuousAt f_cont

/-- The push-forward of a finite measure by a continuous function between Borel spaces as
a continuous linear map. -/
noncomputable def mapCLM {f : Œ© ‚Üí Œ©'} (f_cont : Continuous f) :
    FiniteMeasure Œ© ‚ÜíL[‚Ñù‚â•0] FiniteMeasure Œ©' where
  toFun := fun ŒΩ ‚Ü¶ ŒΩ.map f
  map_add' := map_add f_cont.measurable
  map_smul' := map_smul
  cont := continuous_map f_cont

end map -- section

end FiniteMeasure -- namespace

end MeasureTheory -- namespace
