/-
Copyright (c) 2021 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, S√©bastien Gou√´zel
-/
import Mathlib.LinearAlgebra.FiniteDimensional
import Mathlib.MeasureTheory.Group.Pointwise
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Mathlib.MeasureTheory.Measure.Doubling
import Mathlib.MeasureTheory.Constructions.BorelSpace.Metric

#align_import measure_theory.measure.lebesgue.eq_haar from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# Relationship between the Haar and Lebesgue measures

We prove that the Haar measure and Lebesgue measure are equal on `‚Ñù` and on `‚Ñù^Œπ`, in
`MeasureTheory.addHaarMeasure_eq_volume` and `MeasureTheory.addHaarMeasure_eq_volume_pi`.

We deduce basic properties of any Haar measure on a finite dimensional real vector space:
* `map_linearMap_addHaar_eq_smul_addHaar`: a linear map rescales the Haar measure by the
  absolute value of its determinant.
* `addHaar_preimage_linearMap` : when `f` is a linear map with nonzero determinant, the measure
  of `f ‚Åª¬π' s` is the measure of `s` multiplied by the absolute value of the inverse of the
  determinant of `f`.
* `addHaar_image_linearMap` : when `f` is a linear map, the measure of `f '' s` is the
  measure of `s` multiplied by the absolute value of the determinant of `f`.
* `addHaar_submodule` : a strict submodule has measure `0`.
* `addHaar_smul` : the measure of `r ‚Ä¢ s` is `|r| ^ dim * Œº s`.
* `addHaar_ball`: the measure of `ball x r` is `r ^ dim * Œº (ball 0 1)`.
* `addHaar_closedBall`: the measure of `closedBall x r` is `r ^ dim * Œº (ball 0 1)`.
* `addHaar_sphere`: spheres have zero measure.

This makes it possible to associate a Lebesgue measure to an `n`-alternating map in dimension `n`.
This measure is called `AlternatingMap.measure`. Its main property is
`œâ.measure_parallelepiped v`, stating that the associated measure of the parallelepiped spanned
by vectors `v‚ÇÅ, ..., v‚Çô` is given by `|œâ v|`.

We also show that a Lebesgue density point `x` of a set `s` (with respect to closed balls) has
density one for the rescaled copies `{x} + r ‚Ä¢ t` of a given set `t` with positive measure, in
`tendsto_addHaar_inter_smul_one_of_density_one`. In particular, `s` intersects `{x} + r ‚Ä¢ t` for
small `r`, see `eventually_nonempty_inter_smul_of_density_one`.

Statements on integrals of functions with respect to an additive Haar measure can be found in
`MeasureTheory.Measure.Haar.NormedSpace`.
-/

assert_not_exists MeasureTheory.integral

open TopologicalSpace Set Filter Metric Bornology

open scoped ENNReal Pointwise Topology NNReal

/-- The interval `[0,1]` as a compact set with non-empty interior. -/
def TopologicalSpace.PositiveCompacts.Icc01 : PositiveCompacts ‚Ñù where
  carrier := Icc 0 1
  isCompact' := isCompact_Icc
  interior_nonempty' := by simp_rw [interior_Icc, nonempty_Ioo, zero_lt_one]
#align topological_space.positive_compacts.Icc01 TopologicalSpace.PositiveCompacts.Icc01

universe u

/-- The set `[0,1]^Œπ` as a compact set with non-empty interior. -/
def TopologicalSpace.PositiveCompacts.piIcc01 (Œπ : Type*) [Finite Œπ] :
    PositiveCompacts (Œπ ‚Üí ‚Ñù) where
  carrier := pi univ fun _ => Icc 0 1
  isCompact' := isCompact_univ_pi fun _ => isCompact_Icc
  interior_nonempty' := by
    simp only [interior_pi_set, Set.toFinite, interior_Icc, univ_pi_nonempty_iff, nonempty_Ioo,
      imp_true_iff, zero_lt_one]
#align topological_space.positive_compacts.pi_Icc01 TopologicalSpace.PositiveCompacts.piIcc01

/-- The parallelepiped formed from the standard basis for `Œπ ‚Üí ‚Ñù` is `[0,1]^Œπ` -/
theorem Basis.parallelepiped_basisFun (Œπ : Type*) [Fintype Œπ] :
    (Pi.basisFun ‚Ñù Œπ).parallelepiped = TopologicalSpace.PositiveCompacts.piIcc01 Œπ :=
  SetLike.coe_injective <| by
    refine Eq.trans ?_ ((uIcc_of_le ?_).trans (Set.pi_univ_Icc _ _).symm)
    ¬∑ classical convert parallelepiped_single (Œπ := Œπ) 1
    ¬∑ exact zero_le_one
#align basis.parallelepiped_basis_fun Basis.parallelepiped_basisFun

/-- A parallelepiped can be expressed on the standard basis. -/
theorem Basis.parallelepiped_eq_map  {Œπ E : Type*} [Fintype Œπ] [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] (b : Basis Œπ ‚Ñù E) :
    b.parallelepiped = (PositiveCompacts.piIcc01 Œπ).map b.equivFun.symm
      b.equivFunL.symm.continuous b.equivFunL.symm.isOpenMap := by
  classical
  rw [‚Üê Basis.parallelepiped_basisFun, ‚Üê Basis.parallelepiped_map]
  congr with x
  simp

open MeasureTheory MeasureTheory.Measure

theorem Basis.map_addHaar {Œπ E F : Type*} [Fintype Œπ] [NormedAddCommGroup E] [NormedAddCommGroup F]
    [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù F] [MeasurableSpace E] [MeasurableSpace F] [BorelSpace E]
    [BorelSpace F] [SecondCountableTopology F] [SigmaCompactSpace F]
    (b : Basis Œπ ‚Ñù E) (f : E ‚âÉL[‚Ñù] F) :
    map f b.addHaar = (b.map f.toLinearEquiv).addHaar := by
  have : IsAddHaarMeasure (map f b.addHaar) :=
    AddEquiv.isAddHaarMeasure_map b.addHaar f.toAddEquiv f.continuous f.symm.continuous
  rw [eq_comm, Basis.addHaar_eq_iff, Measure.map_apply f.continuous.measurable
    (PositiveCompacts.isCompact _).measurableSet, Basis.coe_parallelepiped, Basis.coe_map]
  erw [‚Üê image_parallelepiped, f.toEquiv.preimage_image, addHaar_self]

namespace MeasureTheory

open Measure TopologicalSpace.PositiveCompacts FiniteDimensional

/-!
### The Lebesgue measure is a Haar measure on `‚Ñù` and on `‚Ñù^Œπ`.
-/

/-- The Haar measure equals the Lebesgue measure on `‚Ñù`. -/
theorem addHaarMeasure_eq_volume : addHaarMeasure Icc01 = volume := by
  convert (addHaarMeasure_unique volume Icc01).symm; simp [Icc01]
#align measure_theory.add_haar_measure_eq_volume MeasureTheory.addHaarMeasure_eq_volume

/-- The Haar measure equals the Lebesgue measure on `‚Ñù^Œπ`. -/
theorem addHaarMeasure_eq_volume_pi (Œπ : Type*) [Fintype Œπ] :
    addHaarMeasure (piIcc01 Œπ) = volume := by
  convert (addHaarMeasure_unique volume (piIcc01 Œπ)).symm
  simp only [piIcc01, volume_pi_pi fun _ => Icc (0 : ‚Ñù) 1, PositiveCompacts.coe_mk,
    Compacts.coe_mk, Finset.prod_const_one, ENNReal.ofReal_one, Real.volume_Icc, one_smul, sub_zero]
#align measure_theory.add_haar_measure_eq_volume_pi MeasureTheory.addHaarMeasure_eq_volume_pi

-- Porting note (#11215): TODO: remove this instance?
instance isAddHaarMeasure_volume_pi (Œπ : Type*) [Fintype Œπ] :
    IsAddHaarMeasure (volume : Measure (Œπ ‚Üí ‚Ñù)) :=
  inferInstance
#align measure_theory.is_add_haar_measure_volume_pi MeasureTheory.isAddHaarMeasure_volume_pi

namespace Measure

/-!
### Strict subspaces have zero measure
-/


/-- If a set is disjoint of its translates by infinitely many bounded vectors, then it has measure
zero. This auxiliary lemma proves this assuming additionally that the set is bounded. -/
theorem addHaar_eq_zero_of_disjoint_translates_aux {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E] [FiniteDimensional ‚Ñù E] (Œº : Measure E)
    [IsAddHaarMeasure Œº] {s : Set E} (u : ‚Ñï ‚Üí E) (sb : IsBounded s) (hu : IsBounded (range u))
    (hs : Pairwise (Disjoint on fun n => {u n} + s)) (h's : MeasurableSet s) : Œº s = 0 := by
  by_contra h
  apply lt_irrefl ‚àû
  calc
    ‚àû = ‚àë' _ : ‚Ñï, Œº s := (ENNReal.tsum_const_eq_top_of_ne_zero h).symm
    _ = ‚àë' n : ‚Ñï, Œº ({u n} + s) := by
      congr 1; ext1 n; simp only [image_add_left, measure_preimage_add, singleton_add]
    _ = Œº (‚ãÉ n, {u n} + s) := Eq.symm <| measure_iUnion hs fun n => by
      simpa only [image_add_left, singleton_add] using measurable_id.const_add _ h's
    _ = Œº (range u + s) := by rw [‚Üê iUnion_add, iUnion_singleton_eq_range]
    _ < ‚àû := (hu.add sb).measure_lt_top
#align measure_theory.measure.add_haar_eq_zero_of_disjoint_translates_aux MeasureTheory.Measure.addHaar_eq_zero_of_disjoint_translates_aux

/-- If a set is disjoint of its translates by infinitely many bounded vectors, then it has measure
zero. -/
theorem addHaar_eq_zero_of_disjoint_translates {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E] [FiniteDimensional ‚Ñù E] (Œº : Measure E)
    [IsAddHaarMeasure Œº] {s : Set E} (u : ‚Ñï ‚Üí E) (hu : IsBounded (range u))
    (hs : Pairwise (Disjoint on fun n => {u n} + s)) (h's : MeasurableSet s) : Œº s = 0 := by
  suffices H : ‚àÄ R, Œº (s ‚à© closedBall 0 R) = 0 by
    apply le_antisymm _ (zero_le _)
    calc
      Œº s ‚â§ ‚àë' n : ‚Ñï, Œº (s ‚à© closedBall 0 n) := by
        conv_lhs => rw [‚Üê iUnion_inter_closedBall_nat s 0]
        exact measure_iUnion_le _
      _ = 0 := by simp only [H, tsum_zero]
  intro R
  apply addHaar_eq_zero_of_disjoint_translates_aux Œº u
    (isBounded_closedBall.subset (inter_subset_right _ _)) hu _ (h's.inter measurableSet_closedBall)
  refine pairwise_disjoint_mono hs fun n => ?_
  exact add_subset_add Subset.rfl (inter_subset_left _ _)
#align measure_theory.measure.add_haar_eq_zero_of_disjoint_translates MeasureTheory.Measure.addHaar_eq_zero_of_disjoint_translates

/-- A strict vector subspace has measure zero. -/
theorem addHaar_submodule {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E]
    [BorelSpace E] [FiniteDimensional ‚Ñù E] (Œº : Measure E) [IsAddHaarMeasure Œº] (s : Submodule ‚Ñù E)
    (hs : s ‚â† ‚ä§) : Œº s = 0 := by
  obtain ‚ü®x, hx‚ü© : ‚àÉ x, x ‚àâ s := by
    simpa only [Submodule.eq_top_iff', not_exists, Ne, not_forall] using hs
  obtain ‚ü®c, cpos, cone‚ü© : ‚àÉ c : ‚Ñù, 0 < c ‚àß c < 1 := ‚ü®1 / 2, by norm_num, by norm_num‚ü©
  have A : IsBounded (range fun n : ‚Ñï => c ^ n ‚Ä¢ x) :=
    have : Tendsto (fun n : ‚Ñï => c ^ n ‚Ä¢ x) atTop (ùìù ((0 : ‚Ñù) ‚Ä¢ x)) :=
      (tendsto_pow_atTop_nhds_zero_of_lt_one cpos.le cone).smul_const x
    isBounded_range_of_tendsto _ this
  apply addHaar_eq_zero_of_disjoint_translates Œº _ A _
    (Submodule.closed_of_finiteDimensional s).measurableSet
  intro m n hmn
  simp only [Function.onFun, image_add_left, singleton_add, disjoint_left, mem_preimage,
    SetLike.mem_coe]
  intro y hym hyn
  have A : (c ^ n - c ^ m) ‚Ä¢ x ‚àà s := by
    convert s.sub_mem hym hyn using 1
    simp only [sub_smul, neg_sub_neg, add_sub_add_right_eq_sub]
  have H : c ^ n - c ^ m ‚â† 0 := by
    simpa only [sub_eq_zero, Ne] using (pow_right_strictAnti cpos cone).injective.ne hmn.symm
  have : x ‚àà s := by
    convert s.smul_mem (c ^ n - c ^ m)‚Åª¬π A
    rw [smul_smul, inv_mul_cancel H, one_smul]
  exact hx this
#align measure_theory.measure.add_haar_submodule MeasureTheory.Measure.addHaar_submodule

/-- A strict affine subspace has measure zero. -/
theorem addHaar_affineSubspace {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [MeasurableSpace E] [BorelSpace E] [FiniteDimensional ‚Ñù E] (Œº : Measure E) [IsAddHaarMeasure Œº]
    (s : AffineSubspace ‚Ñù E) (hs : s ‚â† ‚ä§) : Œº s = 0 := by
  rcases s.eq_bot_or_nonempty with (rfl | hne)
  ¬∑ rw [AffineSubspace.bot_coe, measure_empty]
  rw [Ne, ‚Üê AffineSubspace.direction_eq_top_iff_of_nonempty hne] at hs
  rcases hne with ‚ü®x, hx : x ‚àà s‚ü©
  simpa only [AffineSubspace.coe_direction_eq_vsub_set_right hx, vsub_eq_sub, sub_eq_add_neg,
    image_add_right, neg_neg, measure_preimage_add_right] using addHaar_submodule Œº s.direction hs
#align measure_theory.measure.add_haar_affine_subspace MeasureTheory.Measure.addHaar_affineSubspace

/-!
### Applying a linear map rescales Haar measure by the determinant

We first prove this on `Œπ ‚Üí ‚Ñù`, using that this is already known for the product Lebesgue
measure (thanks to matrices computations). Then, we extend this to any finite-dimensional real
vector space by using a linear equiv with a space of the form `Œπ ‚Üí ‚Ñù`, and arguing that such a
linear equiv maps Haar measure to Haar measure.
-/

theorem map_linearMap_addHaar_pi_eq_smul_addHaar {Œπ : Type*} [Finite Œπ] {f : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] Œπ ‚Üí ‚Ñù}
    (hf : LinearMap.det f ‚â† 0) (Œº : Measure (Œπ ‚Üí ‚Ñù)) [IsAddHaarMeasure Œº] :
    Measure.map f Œº = ENNReal.ofReal (abs (LinearMap.det f)‚Åª¬π) ‚Ä¢ Œº := by
  cases nonempty_fintype Œπ
  /- We have already proved the result for the Lebesgue product measure, using matrices.
    We deduce it for any Haar measure by uniqueness (up to scalar multiplication). -/
  have := addHaarMeasure_unique Œº (piIcc01 Œπ)
  rw [this, addHaarMeasure_eq_volume_pi, Measure.map_smul,
    Real.map_linearMap_volume_pi_eq_smul_volume_pi hf, smul_comm]
#align measure_theory.measure.map_linear_map_add_haar_pi_eq_smul_add_haar MeasureTheory.Measure.map_linearMap_addHaar_pi_eq_smul_addHaar

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
  [FiniteDimensional ‚Ñù E] (Œº : Measure E) [IsAddHaarMeasure Œº] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace ‚Ñù F] [CompleteSpace F]

theorem map_linearMap_addHaar_eq_smul_addHaar {f : E ‚Üí‚Çó[‚Ñù] E} (hf : LinearMap.det f ‚â† 0) :
    Measure.map f Œº = ENNReal.ofReal |(LinearMap.det f)‚Åª¬π| ‚Ä¢ Œº := by
  -- we reduce to the case of `E = Œπ ‚Üí ‚Ñù`, for which we have already proved the result using
  -- matrices in `map_linearMap_addHaar_pi_eq_smul_addHaar`.
  let Œπ := Fin (finrank ‚Ñù E)
  haveI : FiniteDimensional ‚Ñù (Œπ ‚Üí ‚Ñù) := by infer_instance
  have : finrank ‚Ñù E = finrank ‚Ñù (Œπ ‚Üí ‚Ñù) := by simp [Œπ]
  have e : E ‚âÉ‚Çó[‚Ñù] Œπ ‚Üí ‚Ñù := LinearEquiv.ofFinrankEq E (Œπ ‚Üí ‚Ñù) this
  -- next line is to avoid `g` getting reduced by `simp`.
  obtain ‚ü®g, hg‚ü© : ‚àÉ g, g = (e : E ‚Üí‚Çó[‚Ñù] Œπ ‚Üí ‚Ñù).comp (f.comp (e.symm : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E)) := ‚ü®_, rfl‚ü©
  have gdet : LinearMap.det g = LinearMap.det f := by rw [hg]; exact LinearMap.det_conj f e
  rw [‚Üê gdet] at hf ‚ä¢
  have fg : f = (e.symm : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E).comp (g.comp (e : E ‚Üí‚Çó[‚Ñù] Œπ ‚Üí ‚Ñù)) := by
    ext x
    simp only [LinearEquiv.coe_coe, Function.comp_apply, LinearMap.coe_comp,
      LinearEquiv.symm_apply_apply, hg]
  simp only [fg, LinearEquiv.coe_coe, LinearMap.coe_comp]
  have Ce : Continuous e := (e : E ‚Üí‚Çó[‚Ñù] Œπ ‚Üí ‚Ñù).continuous_of_finiteDimensional
  have Cg : Continuous g := LinearMap.continuous_of_finiteDimensional g
  have Cesymm : Continuous e.symm := (e.symm : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E).continuous_of_finiteDimensional
  rw [‚Üê map_map Cesymm.measurable (Cg.comp Ce).measurable, ‚Üê map_map Cg.measurable Ce.measurable]
  haveI : IsAddHaarMeasure (map e Œº) := (e : E ‚âÉ+ (Œπ ‚Üí ‚Ñù)).isAddHaarMeasure_map Œº Ce Cesymm
  have ecomp : e.symm ‚àò e = id := by
    ext x; simp only [id, Function.comp_apply, LinearEquiv.symm_apply_apply]
  rw [map_linearMap_addHaar_pi_eq_smul_addHaar hf (map e Œº), Measure.map_smul,
    map_map Cesymm.measurable Ce.measurable, ecomp, Measure.map_id]
#align measure_theory.measure.map_linear_map_add_haar_eq_smul_add_haar MeasureTheory.Measure.map_linearMap_addHaar_eq_smul_addHaar

/-- The preimage of a set `s` under a linear map `f` with nonzero determinant has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp]
theorem addHaar_preimage_linearMap {f : E ‚Üí‚Çó[‚Ñù] E} (hf : LinearMap.det f ‚â† 0) (s : Set E) :
    Œº (f ‚Åª¬π' s) = ENNReal.ofReal |(LinearMap.det f)‚Åª¬π| * Œº s :=
  calc
    Œº (f ‚Åª¬π' s) = Measure.map f Œº s :=
      ((f.equivOfDetNeZero hf).toContinuousLinearEquiv.toHomeomorph.toMeasurableEquiv.map_apply
          s).symm
    _ = ENNReal.ofReal |(LinearMap.det f)‚Åª¬π| * Œº s := by
      rw [map_linearMap_addHaar_eq_smul_addHaar Œº hf]; rfl
#align measure_theory.measure.add_haar_preimage_linear_map MeasureTheory.Measure.addHaar_preimage_linearMap

/-- The preimage of a set `s` under a continuous linear map `f` with nonzero determinant has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp]
theorem addHaar_preimage_continuousLinearMap {f : E ‚ÜíL[‚Ñù] E}
    (hf : LinearMap.det (f : E ‚Üí‚Çó[‚Ñù] E) ‚â† 0) (s : Set E) :
    Œº (f ‚Åª¬π' s) = ENNReal.ofReal (abs (LinearMap.det (f : E ‚Üí‚Çó[‚Ñù] E))‚Åª¬π) * Œº s :=
  addHaar_preimage_linearMap Œº hf s
#align measure_theory.measure.add_haar_preimage_continuous_linear_map MeasureTheory.Measure.addHaar_preimage_continuousLinearMap

/-- The preimage of a set `s` under a linear equiv `f` has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp]
theorem addHaar_preimage_linearEquiv (f : E ‚âÉ‚Çó[‚Ñù] E) (s : Set E) :
    Œº (f ‚Åª¬π' s) = ENNReal.ofReal |LinearMap.det (f.symm : E ‚Üí‚Çó[‚Ñù] E)| * Œº s := by
  have A : LinearMap.det (f : E ‚Üí‚Çó[‚Ñù] E) ‚â† 0 := (LinearEquiv.isUnit_det' f).ne_zero
  convert addHaar_preimage_linearMap Œº A s
  simp only [LinearEquiv.det_coe_symm]
#align measure_theory.measure.add_haar_preimage_linear_equiv MeasureTheory.Measure.addHaar_preimage_linearEquiv

/-- The preimage of a set `s` under a continuous linear equiv `f` has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp]
theorem addHaar_preimage_continuousLinearEquiv (f : E ‚âÉL[‚Ñù] E) (s : Set E) :
    Œº (f ‚Åª¬π' s) = ENNReal.ofReal |LinearMap.det (f.symm : E ‚Üí‚Çó[‚Ñù] E)| * Œº s :=
  addHaar_preimage_linearEquiv Œº _ s
#align measure_theory.measure.add_haar_preimage_continuous_linear_equiv MeasureTheory.Measure.addHaar_preimage_continuousLinearEquiv

/-- The image of a set `s` under a linear map `f` has measure
equal to `Œº s` times the absolute value of the determinant of `f`. -/
@[simp]
theorem addHaar_image_linearMap (f : E ‚Üí‚Çó[‚Ñù] E) (s : Set E) :
    Œº (f '' s) = ENNReal.ofReal |LinearMap.det f| * Œº s := by
  rcases ne_or_eq (LinearMap.det f) 0 with (hf | hf)
  ¬∑ let g := (f.equivOfDetNeZero hf).toContinuousLinearEquiv
    change Œº (g '' s) = _
    rw [ContinuousLinearEquiv.image_eq_preimage g s, addHaar_preimage_continuousLinearEquiv]
    congr
  ¬∑ simp only [hf, zero_mul, ENNReal.ofReal_zero, abs_zero]
    have : Œº (LinearMap.range f) = 0 :=
      addHaar_submodule Œº _ (LinearMap.range_lt_top_of_det_eq_zero hf).ne
    exact le_antisymm (le_trans (measure_mono (image_subset_range _ _)) this.le) (zero_le _)
#align measure_theory.measure.add_haar_image_linear_map MeasureTheory.Measure.addHaar_image_linearMap

/-- The image of a set `s` under a continuous linear map `f` has measure
equal to `Œº s` times the absolute value of the determinant of `f`. -/
@[simp]
theorem addHaar_image_continuousLinearMap (f : E ‚ÜíL[‚Ñù] E) (s : Set E) :
    Œº (f '' s) = ENNReal.ofReal |LinearMap.det (f : E ‚Üí‚Çó[‚Ñù] E)| * Œº s :=
  addHaar_image_linearMap Œº _ s
#align measure_theory.measure.add_haar_image_continuous_linear_map MeasureTheory.Measure.addHaar_image_continuousLinearMap

/-- The image of a set `s` under a continuous linear equiv `f` has measure
equal to `Œº s` times the absolute value of the determinant of `f`. -/
@[simp]
theorem addHaar_image_continuousLinearEquiv (f : E ‚âÉL[‚Ñù] E) (s : Set E) :
    Œº (f '' s) = ENNReal.ofReal |LinearMap.det (f : E ‚Üí‚Çó[‚Ñù] E)| * Œº s :=
  Œº.addHaar_image_linearMap (f : E ‚Üí‚Çó[‚Ñù] E) s
#align measure_theory.measure.add_haar_image_continuous_linear_equiv MeasureTheory.Measure.addHaar_image_continuousLinearEquiv

theorem LinearMap.quasiMeasurePreserving (f : E ‚Üí‚Çó[‚Ñù] E) (hf : LinearMap.det f ‚â† 0) :
    QuasiMeasurePreserving f Œº Œº := by
  refine ‚ü®f.continuous_of_finiteDimensional.measurable, ?_‚ü©
  rw [map_linearMap_addHaar_eq_smul_addHaar Œº hf]
  exact smul_absolutelyContinuous

theorem ContinuousLinearMap.quasiMeasurePreserving (f : E ‚ÜíL[‚Ñù] E) (hf : f.det ‚â† 0) :
    QuasiMeasurePreserving f Œº Œº :=
  LinearMap.quasiMeasurePreserving Œº (f : E ‚Üí‚Çó[‚Ñù] E) hf

/-!
### Basic properties of Haar measures on real vector spaces
-/


theorem map_addHaar_smul {r : ‚Ñù} (hr : r ‚â† 0) :
    Measure.map (r ‚Ä¢ ¬∑) Œº = ENNReal.ofReal (abs (r ^ finrank ‚Ñù E)‚Åª¬π) ‚Ä¢ Œº := by
  let f : E ‚Üí‚Çó[‚Ñù] E := r ‚Ä¢ (1 : E ‚Üí‚Çó[‚Ñù] E)
  change Measure.map f Œº = _
  have hf : LinearMap.det f ‚â† 0 := by
    simp only [f, mul_one, LinearMap.det_smul, Ne, MonoidHom.map_one]
    intro h
    exact hr (pow_eq_zero h)
  simp only [f, map_linearMap_addHaar_eq_smul_addHaar Œº hf, mul_one, LinearMap.det_smul, map_one]
#align measure_theory.measure.map_add_haar_smul MeasureTheory.Measure.map_addHaar_smul

theorem quasiMeasurePreserving_smul {r : ‚Ñù} (hr : r ‚â† 0) :
    QuasiMeasurePreserving (r ‚Ä¢ ¬∑) Œº Œº := by
  refine ‚ü®measurable_const_smul r, ?_‚ü©
  rw [map_addHaar_smul Œº hr]
  exact smul_absolutelyContinuous

@[simp]
theorem addHaar_preimage_smul {r : ‚Ñù} (hr : r ‚â† 0) (s : Set E) :
    Œº ((r ‚Ä¢ ¬∑) ‚Åª¬π' s) = ENNReal.ofReal (abs (r ^ finrank ‚Ñù E)‚Åª¬π) * Œº s :=
  calc
    Œº ((r ‚Ä¢ ¬∑) ‚Åª¬π' s) = Measure.map (r ‚Ä¢ ¬∑) Œº s :=
      ((Homeomorph.smul (isUnit_iff_ne_zero.2 hr).unit).toMeasurableEquiv.map_apply s).symm
    _ = ENNReal.ofReal (abs (r ^ finrank ‚Ñù E)‚Åª¬π) * Œº s := by
      rw [map_addHaar_smul Œº hr, coe_smul, Pi.smul_apply, smul_eq_mul]
#align measure_theory.measure.add_haar_preimage_smul MeasureTheory.Measure.addHaar_preimage_smul

/-- Rescaling a set by a factor `r` multiplies its measure by `abs (r ^ dim)`. -/
@[simp]
theorem addHaar_smul (r : ‚Ñù) (s : Set E) :
    Œº (r ‚Ä¢ s) = ENNReal.ofReal (abs (r ^ finrank ‚Ñù E)) * Œº s := by
  rcases ne_or_eq r 0 with (h | rfl)
  ¬∑ rw [‚Üê preimage_smul_inv‚ÇÄ h, addHaar_preimage_smul Œº (inv_ne_zero h), inv_pow, inv_inv]
  rcases eq_empty_or_nonempty s with (rfl | hs)
  ¬∑ simp only [measure_empty, mul_zero, smul_set_empty]
  rw [zero_smul_set hs, ‚Üê singleton_zero]
  by_cases h : finrank ‚Ñù E = 0
  ¬∑ haveI : Subsingleton E := finrank_zero_iff.1 h
    simp only [h, one_mul, ENNReal.ofReal_one, abs_one, Subsingleton.eq_univ_of_nonempty hs,
      pow_zero, Subsingleton.eq_univ_of_nonempty (singleton_nonempty (0 : E))]
  ¬∑ haveI : Nontrivial E := nontrivial_of_finrank_pos (bot_lt_iff_ne_bot.2 h)
    simp only [h, zero_mul, ENNReal.ofReal_zero, abs_zero, Ne, not_false_iff,
      zero_pow, measure_singleton]
#align measure_theory.measure.add_haar_smul MeasureTheory.Measure.addHaar_smul

theorem addHaar_smul_of_nonneg {r : ‚Ñù} (hr : 0 ‚â§ r) (s : Set E) :
    Œº (r ‚Ä¢ s) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº s := by
  rw [addHaar_smul, abs_pow, abs_of_nonneg hr]
#align measure_theory.measure.add_haar_smul_of_nonneg MeasureTheory.Measure.addHaar_smul_of_nonneg

variable {Œº} {s : Set E}

-- Note: We might want to rename this once we acquire the lemma corresponding to
-- `MeasurableSet.const_smul`
theorem NullMeasurableSet.const_smul (hs : NullMeasurableSet s Œº) (r : ‚Ñù) :
    NullMeasurableSet (r ‚Ä¢ s) Œº := by
  obtain rfl | hs' := s.eq_empty_or_nonempty
  ¬∑ simp
  obtain rfl | hr := eq_or_ne r 0
  ¬∑ simpa [zero_smul_set hs'] using nullMeasurableSet_singleton _
  obtain ‚ü®t, ht, hst‚ü© := hs
  refine ‚ü®_, ht.const_smul_of_ne_zero hr, ?_‚ü©
  rw [‚Üê measure_symmDiff_eq_zero_iff] at hst ‚ä¢
  rw [‚Üê smul_set_symmDiff‚ÇÄ hr, addHaar_smul Œº, hst, mul_zero]
#align measure_theory.measure.null_measurable_set.const_smul MeasureTheory.Measure.NullMeasurableSet.const_smul

variable (Œº)

@[simp]
theorem addHaar_image_homothety (x : E) (r : ‚Ñù) (s : Set E) :
    Œº (AffineMap.homothety x r '' s) = ENNReal.ofReal (abs (r ^ finrank ‚Ñù E)) * Œº s :=
  calc
    Œº (AffineMap.homothety x r '' s) = Œº ((fun y => y + x) '' (r ‚Ä¢ (fun y => y + -x) '' s)) := by
      simp only [‚Üê image_smul, image_image, ‚Üê sub_eq_add_neg]; rfl
    _ = ENNReal.ofReal (abs (r ^ finrank ‚Ñù E)) * Œº s := by
      simp only [image_add_right, measure_preimage_add_right, addHaar_smul]
#align measure_theory.measure.add_haar_image_homothety MeasureTheory.Measure.addHaar_image_homothety

/-! We don't need to state `map_addHaar_neg` here, because it has already been proved for
general Haar measures on general commutative groups. -/


/-! ### Measure of balls -/

theorem addHaar_ball_center {E : Type*} [NormedAddCommGroup E] [MeasurableSpace E] [BorelSpace E]
    (Œº : Measure E) [IsAddHaarMeasure Œº] (x : E) (r : ‚Ñù) : Œº (ball x r) = Œº (ball (0 : E) r) := by
  have : ball (0 : E) r = (x + ¬∑) ‚Åª¬π' ball x r := by simp [preimage_add_ball]
  rw [this, measure_preimage_add]
#align measure_theory.measure.add_haar_ball_center MeasureTheory.Measure.addHaar_ball_center

theorem addHaar_closedBall_center {E : Type*} [NormedAddCommGroup E] [MeasurableSpace E]
    [BorelSpace E] (Œº : Measure E) [IsAddHaarMeasure Œº] (x : E) (r : ‚Ñù) :
    Œº (closedBall x r) = Œº (closedBall (0 : E) r) := by
  have : closedBall (0 : E) r = (x + ¬∑) ‚Åª¬π' closedBall x r := by simp [preimage_add_closedBall]
  rw [this, measure_preimage_add]
#align measure_theory.measure.add_haar_closed_ball_center MeasureTheory.Measure.addHaar_closedBall_center

theorem addHaar_ball_mul_of_pos (x : E) {r : ‚Ñù} (hr : 0 < r) (s : ‚Ñù) :
    Œº (ball x (r * s)) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (ball 0 s) := by
  have : ball (0 : E) (r * s) = r ‚Ä¢ ball (0 : E) s := by
    simp only [_root_.smul_ball hr.ne' (0 : E) s, Real.norm_eq_abs, abs_of_nonneg hr.le, smul_zero]
  simp only [this, addHaar_smul, abs_of_nonneg hr.le, addHaar_ball_center, abs_pow]
#align measure_theory.measure.add_haar_ball_mul_of_pos MeasureTheory.Measure.addHaar_ball_mul_of_pos

theorem addHaar_ball_of_pos (x : E) {r : ‚Ñù} (hr : 0 < r) :
    Œº (ball x r) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (ball 0 1) := by
  rw [‚Üê addHaar_ball_mul_of_pos Œº x hr, mul_one]
#align measure_theory.measure.add_haar_ball_of_pos MeasureTheory.Measure.addHaar_ball_of_pos

theorem addHaar_ball_mul [Nontrivial E] (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) (s : ‚Ñù) :
    Œº (ball x (r * s)) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (ball 0 s) := by
  rcases hr.eq_or_lt with (rfl | h)
  ¬∑ simp only [zero_pow (finrank_pos (R := ‚Ñù) (M := E)).ne', measure_empty, zero_mul,
      ENNReal.ofReal_zero, ball_zero]
  ¬∑ exact addHaar_ball_mul_of_pos Œº x h s
#align measure_theory.measure.add_haar_ball_mul MeasureTheory.Measure.addHaar_ball_mul

theorem addHaar_ball [Nontrivial E] (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    Œº (ball x r) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (ball 0 1) := by
  rw [‚Üê addHaar_ball_mul Œº x hr, mul_one]
#align measure_theory.measure.add_haar_ball MeasureTheory.Measure.addHaar_ball

theorem addHaar_closedBall_mul_of_pos (x : E) {r : ‚Ñù} (hr : 0 < r) (s : ‚Ñù) :
    Œº (closedBall x (r * s)) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (closedBall 0 s) := by
  have : closedBall (0 : E) (r * s) = r ‚Ä¢ closedBall (0 : E) s := by
    simp [smul_closedBall' hr.ne' (0 : E), abs_of_nonneg hr.le]
  simp only [this, addHaar_smul, abs_of_nonneg hr.le, addHaar_closedBall_center, abs_pow]
#align measure_theory.measure.add_haar_closed_ball_mul_of_pos MeasureTheory.Measure.addHaar_closedBall_mul_of_pos

theorem addHaar_closedBall_mul (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) {s : ‚Ñù} (hs : 0 ‚â§ s) :
    Œº (closedBall x (r * s)) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (closedBall 0 s) := by
  have : closedBall (0 : E) (r * s) = r ‚Ä¢ closedBall (0 : E) s := by
    simp [smul_closedBall r (0 : E) hs, abs_of_nonneg hr]
  simp only [this, addHaar_smul, abs_of_nonneg hr, addHaar_closedBall_center, abs_pow]
#align measure_theory.measure.add_haar_closed_ball_mul MeasureTheory.Measure.addHaar_closedBall_mul

/-- The measure of a closed ball can be expressed in terms of the measure of the closed unit ball.
Use instead `addHaar_closedBall`, which uses the measure of the open unit ball as a standard
form. -/
theorem addHaar_closedBall' (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    Œº (closedBall x r) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (closedBall 0 1) := by
  rw [‚Üê addHaar_closedBall_mul Œº x hr zero_le_one, mul_one]
#align measure_theory.measure.add_haar_closed_ball' MeasureTheory.Measure.addHaar_closedBall'

theorem addHaar_closed_unit_ball_eq_addHaar_unit_ball :
    Œº (closedBall (0 : E) 1) = Œº (ball 0 1) := by
  apply le_antisymm _ (measure_mono ball_subset_closedBall)
  have A : Tendsto
      (fun r : ‚Ñù => ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (closedBall (0 : E) 1)) (ùìù[<] 1)
        (ùìù (ENNReal.ofReal ((1 : ‚Ñù) ^ finrank ‚Ñù E) * Œº (closedBall (0 : E) 1))) := by
    refine ENNReal.Tendsto.mul ?_ (by simp) tendsto_const_nhds (by simp)
    exact ENNReal.tendsto_ofReal ((tendsto_id'.2 nhdsWithin_le_nhds).pow _)
  simp only [one_pow, one_mul, ENNReal.ofReal_one] at A
  refine le_of_tendsto A ?_
  refine mem_nhdsWithin_Iio_iff_exists_Ioo_subset.2 ‚ü®(0 : ‚Ñù), by simp, fun r hr => ?_‚ü©
  dsimp
  rw [‚Üê addHaar_closedBall' Œº (0 : E) hr.1.le]
  exact measure_mono (closedBall_subset_ball hr.2)
#align measure_theory.measure.add_haar_closed_unit_ball_eq_add_haar_unit_ball MeasureTheory.Measure.addHaar_closed_unit_ball_eq_addHaar_unit_ball

theorem addHaar_closedBall (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    Œº (closedBall x r) = ENNReal.ofReal (r ^ finrank ‚Ñù E) * Œº (ball 0 1) := by
  rw [addHaar_closedBall' Œº x hr, addHaar_closed_unit_ball_eq_addHaar_unit_ball]
#align measure_theory.measure.add_haar_closed_ball MeasureTheory.Measure.addHaar_closedBall

theorem addHaar_closedBall_eq_addHaar_ball [Nontrivial E] (x : E) (r : ‚Ñù) :
    Œº (closedBall x r) = Œº (ball x r) := by
  by_cases h : r < 0
  ¬∑ rw [Metric.closedBall_eq_empty.mpr h, Metric.ball_eq_empty.mpr h.le]
  push_neg at h
  rw [addHaar_closedBall Œº x h, addHaar_ball Œº x h]
#align measure_theory.measure.add_haar_closed_ball_eq_add_haar_ball MeasureTheory.Measure.addHaar_closedBall_eq_addHaar_ball

theorem addHaar_sphere_of_ne_zero (x : E) {r : ‚Ñù} (hr : r ‚â† 0) : Œº (sphere x r) = 0 := by
  rcases hr.lt_or_lt with (h | h)
  ¬∑ simp only [empty_diff, measure_empty, ‚Üê closedBall_diff_ball, closedBall_eq_empty.2 h]
  ¬∑ rw [‚Üê closedBall_diff_ball,
      measure_diff ball_subset_closedBall measurableSet_ball measure_ball_lt_top.ne,
      addHaar_ball_of_pos Œº _ h, addHaar_closedBall Œº _ h.le, tsub_self]
#align measure_theory.measure.add_haar_sphere_of_ne_zero MeasureTheory.Measure.addHaar_sphere_of_ne_zero

theorem addHaar_sphere [Nontrivial E] (x : E) (r : ‚Ñù) : Œº (sphere x r) = 0 := by
  rcases eq_or_ne r 0 with (rfl | h)
  ¬∑ rw [sphere_zero, measure_singleton]
  ¬∑ exact addHaar_sphere_of_ne_zero Œº x h
#align measure_theory.measure.add_haar_sphere MeasureTheory.Measure.addHaar_sphere

theorem addHaar_singleton_add_smul_div_singleton_add_smul {r : ‚Ñù} (hr : r ‚â† 0) (x y : E)
    (s t : Set E) : Œº ({x} + r ‚Ä¢ s) / Œº ({y} + r ‚Ä¢ t) = Œº s / Œº t :=
  calc
    Œº ({x} + r ‚Ä¢ s) / Œº ({y} + r ‚Ä¢ t) = ENNReal.ofReal (|r| ^ finrank ‚Ñù E) * Œº s *
        (ENNReal.ofReal (|r| ^ finrank ‚Ñù E) * Œº t)‚Åª¬π := by
      simp only [div_eq_mul_inv, addHaar_smul, image_add_left, measure_preimage_add, abs_pow,
        singleton_add]
    _ = ENNReal.ofReal (|r| ^ finrank ‚Ñù E) * (ENNReal.ofReal (|r| ^ finrank ‚Ñù E))‚Åª¬π *
          (Œº s * (Œº t)‚Åª¬π) := by
      rw [ENNReal.mul_inv]
      ¬∑ ring
      ¬∑ simp only [pow_pos (abs_pos.mpr hr), ENNReal.ofReal_eq_zero, not_le, Ne, true_or_iff]
      ¬∑ simp only [ENNReal.ofReal_ne_top, true_or_iff, Ne, not_false_iff]
    _ = Œº s / Œº t := by
      rw [ENNReal.mul_inv_cancel, one_mul, div_eq_mul_inv]
      ¬∑ simp only [pow_pos (abs_pos.mpr hr), ENNReal.ofReal_eq_zero, not_le, Ne]
      ¬∑ simp only [ENNReal.ofReal_ne_top, Ne, not_false_iff]
#align measure_theory.measure.add_haar_singleton_add_smul_div_singleton_add_smul MeasureTheory.Measure.addHaar_singleton_add_smul_div_singleton_add_smul

instance (priority := 100) isUnifLocDoublingMeasureOfIsAddHaarMeasure :
    IsUnifLocDoublingMeasure Œº := by
  refine ‚ü®‚ü®(2 : ‚Ñù‚â•0) ^ finrank ‚Ñù E, ?_‚ü©‚ü©
  filter_upwards [self_mem_nhdsWithin] with r hr x
  rw [addHaar_closedBall_mul Œº x zero_le_two (le_of_lt hr), addHaar_closedBall_center Œº x,
    ENNReal.ofReal, Real.toNNReal_pow zero_le_two]
  simp only [Real.toNNReal_ofNat, le_refl]
#align measure_theory.measure.is_unif_loc_doubling_measure_of_is_add_haar_measure MeasureTheory.Measure.isUnifLocDoublingMeasureOfIsAddHaarMeasure

section

/-!
### The Lebesgue measure associated to an alternating map
-/

variable {Œπ G : Type*} [Fintype Œπ] [DecidableEq Œπ] [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  [MeasurableSpace G] [BorelSpace G]

theorem addHaar_parallelepiped (b : Basis Œπ ‚Ñù G) (v : Œπ ‚Üí G) :
    b.addHaar (parallelepiped v) = ENNReal.ofReal |b.det v| := by
  have : FiniteDimensional ‚Ñù G := FiniteDimensional.of_fintype_basis b
  have A : parallelepiped v = b.constr ‚Ñï v '' parallelepiped b := by
    rw [image_parallelepiped]
    -- Porting note: was `congr 1 with i` but Lean 4 `congr` applies `ext` first
    refine congr_arg _ <| funext fun i ‚Ü¶ ?_
    exact (b.constr_basis ‚Ñï v i).symm
  rw [A, addHaar_image_linearMap, b.addHaar_self, mul_one, ‚Üê LinearMap.det_toMatrix b,
    ‚Üê Basis.toMatrix_eq_toMatrix_constr, Basis.det_apply]
#align measure_theory.measure.add_haar_parallelepiped MeasureTheory.Measure.addHaar_parallelepiped

variable [FiniteDimensional ‚Ñù G] {n : ‚Ñï} [_i : Fact (finrank ‚Ñù G = n)]

/-- The Lebesgue measure associated to an alternating map. It gives measure `|œâ v|` to the
parallelepiped spanned by the vectors `v‚ÇÅ, ..., v‚Çô`. Note that it is not always a Haar measure,
as it can be zero, but it is always locally finite and translation invariant. -/
noncomputable irreducible_def _root_.AlternatingMap.measure (œâ : G [‚ãÄ^Fin n]‚Üí‚Çó[‚Ñù] ‚Ñù) :
    Measure G :=
  ‚Äñœâ (finBasisOfFinrankEq ‚Ñù G _i.out)‚Äñ‚Çä ‚Ä¢ (finBasisOfFinrankEq ‚Ñù G _i.out).addHaar
#align alternating_map.measure AlternatingMap.measure

theorem _root_.AlternatingMap.measure_parallelepiped (œâ : G [‚ãÄ^Fin n]‚Üí‚Çó[‚Ñù] ‚Ñù)
    (v : Fin n ‚Üí G) : œâ.measure (parallelepiped v) = ENNReal.ofReal |œâ v| := by
  conv_rhs => rw [œâ.eq_smul_basis_det (finBasisOfFinrankEq ‚Ñù G _i.out)]
  simp only [addHaar_parallelepiped, AlternatingMap.measure, coe_nnreal_smul_apply,
    AlternatingMap.smul_apply, Algebra.id.smul_eq_mul, abs_mul, ENNReal.ofReal_mul (abs_nonneg _),
    Real.ennnorm_eq_ofReal_abs]
#align alternating_map.measure_parallelepiped AlternatingMap.measure_parallelepiped

instance (œâ : G [‚ãÄ^Fin n]‚Üí‚Çó[‚Ñù] ‚Ñù) : IsAddLeftInvariant œâ.measure := by
  rw [AlternatingMap.measure]; infer_instance

instance (œâ : G [‚ãÄ^Fin n]‚Üí‚Çó[‚Ñù] ‚Ñù) : IsLocallyFiniteMeasure œâ.measure := by
  rw [AlternatingMap.measure]; infer_instance

end

/-!
### Density points

Besicovitch covering theorem ensures that, for any locally finite measure on a finite-dimensional
real vector space, almost every point of a set `s` is a density point, i.e.,
`Œº (s ‚à© closedBall x r) / Œº (closedBall x r)` tends to `1` as `r` tends to `0`
(see `Besicovitch.ae_tendsto_measure_inter_div`).
When `Œº` is a Haar measure, one can deduce the same property for any rescaling sequence of sets,
of the form `{x} + r ‚Ä¢ t` where `t` is a set with positive finite measure, instead of the sequence
of closed balls.

We argue first for the dual property, i.e., if `s` has density `0` at `x`, then
`Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t)` tends to `0`. First when `t` is contained in the ball
of radius `1`, in `tendsto_addHaar_inter_smul_zero_of_density_zero_aux1`,
(by arguing by inclusion). Then when `t` is bounded, reducing to the previous one by rescaling, in
`tendsto_addHaar_inter_smul_zero_of_density_zero_aux2`.
Then for a general set `t`, by cutting it into a bounded part and a part with small measure, in
`tendsto_addHaar_inter_smul_zero_of_density_zero`.
Going to the complement, one obtains the desired property at points of density `1`, first when
`s` is measurable in `tendsto_addHaar_inter_smul_one_of_density_one_aux`, and then without this
assumption in `tendsto_addHaar_inter_smul_one_of_density_one` by applying the previous lemma to
the measurable hull `toMeasurable Œº s`
-/

theorem tendsto_addHaar_inter_smul_zero_of_density_zero_aux1 (s : Set E) (x : E)
    (h : Tendsto (fun r => Œº (s ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 0)) (t : Set E)
    (u : Set E) (h'u : Œº u ‚â† 0) (t_bound : t ‚äÜ closedBall 0 1) :
    Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ u)) (ùìù[>] 0) (ùìù 0) := by
  have A : Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 0) := by
    apply
      tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds h
        (eventually_of_forall fun b => zero_le _)
    filter_upwards [self_mem_nhdsWithin]
    rintro r (rpos : 0 < r)
    rw [‚Üê affinity_unitClosedBall rpos.le, singleton_add, ‚Üê image_vadd]
    gcongr
    exact smul_set_mono t_bound
  have B :
    Tendsto (fun r : ‚Ñù => Œº (closedBall x r) / Œº ({x} + r ‚Ä¢ u)) (ùìù[>] 0)
      (ùìù (Œº (closedBall x 1) / Œº ({x} + u))) := by
    apply tendsto_const_nhds.congr' _
    filter_upwards [self_mem_nhdsWithin]
    rintro r (rpos : 0 < r)
    have : closedBall x r = {x} + r ‚Ä¢ closedBall (0 : E) 1 := by
      simp only [_root_.smul_closedBall, Real.norm_of_nonneg rpos.le, zero_le_one, add_zero,
        mul_one, singleton_add_closedBall, smul_zero]
    simp only [this, addHaar_singleton_add_smul_div_singleton_add_smul Œº rpos.ne']
    simp only [addHaar_closedBall_center, image_add_left, measure_preimage_add, singleton_add]
  have C : Tendsto (fun r : ‚Ñù =>
        Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº (closedBall x r) * (Œº (closedBall x r) / Œº ({x} + r ‚Ä¢ u)))
      (ùìù[>] 0) (ùìù (0 * (Œº (closedBall x 1) / Œº ({x} + u)))) := by
    apply ENNReal.Tendsto.mul A _ B (Or.inr ENNReal.zero_ne_top)
    simp only [ne_eq, not_true, singleton_add, image_add_left, measure_preimage_add, false_or,
      ENNReal.div_eq_top, h'u, false_or_iff, not_and, and_false_iff]
    intro aux
    exact (measure_closedBall_lt_top.ne aux).elim
    -- Porting note: it used to be enough to pass `measure_closedBall_lt_top.ne` to `simp`
    -- and avoid the `intro; exact` dance.
  simp only [zero_mul] at C
  apply C.congr' _
  filter_upwards [self_mem_nhdsWithin]
  rintro r (rpos : 0 < r)
  calc
    Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº (closedBall x r) * (Œº (closedBall x r) / Œº ({x} + r ‚Ä¢ u)) =
        Œº (closedBall x r) * (Œº (closedBall x r))‚Åª¬π * (Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ u)) :=
      by simp only [div_eq_mul_inv]; ring
    _ = Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ u) := by
      rw [ENNReal.mul_inv_cancel (measure_closedBall_pos Œº x rpos).ne'
          measure_closedBall_lt_top.ne,
        one_mul]
#align measure_theory.measure.tendsto_add_haar_inter_smul_zero_of_density_zero_aux1 MeasureTheory.Measure.tendsto_addHaar_inter_smul_zero_of_density_zero_aux1

theorem tendsto_addHaar_inter_smul_zero_of_density_zero_aux2 (s : Set E) (x : E)
    (h : Tendsto (fun r => Œº (s ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 0)) (t : Set E)
    (u : Set E) (h'u : Œº u ‚â† 0) (R : ‚Ñù) (Rpos : 0 < R) (t_bound : t ‚äÜ closedBall 0 R) :
    Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ u)) (ùìù[>] 0) (ùìù 0) := by
  set t' := R‚Åª¬π ‚Ä¢ t with ht'
  set u' := R‚Åª¬π ‚Ä¢ u with hu'
  have A : Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t')) / Œº ({x} + r ‚Ä¢ u')) (ùìù[>] 0) (ùìù 0) := by
    apply tendsto_addHaar_inter_smul_zero_of_density_zero_aux1 Œº s x h t' u'
    ¬∑ simp only [u', h'u, (pow_pos Rpos _).ne', abs_nonpos_iff, addHaar_smul, not_false_iff,
        ENNReal.ofReal_eq_zero, inv_eq_zero, inv_pow, Ne, or_self_iff, mul_eq_zero]
    ¬∑ refine (smul_set_mono t_bound).trans_eq ?_
      rw [smul_closedBall _ _ Rpos.le, smul_zero, Real.norm_of_nonneg (inv_nonneg.2 Rpos.le),
        inv_mul_cancel Rpos.ne']
  have B : Tendsto (fun r : ‚Ñù => R * r) (ùìù[>] 0) (ùìù[>] (R * 0)) := by
    apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within
    ¬∑ exact (tendsto_const_nhds.mul tendsto_id).mono_left nhdsWithin_le_nhds
    ¬∑ filter_upwards [self_mem_nhdsWithin]
      intro r rpos
      rw [mul_zero]
      exact mul_pos Rpos rpos
  rw [mul_zero] at B
  apply (A.comp B).congr' _
  filter_upwards [self_mem_nhdsWithin]
  rintro r -
  have T : (R * r) ‚Ä¢ t' = r ‚Ä¢ t := by
    rw [mul_comm, ht', smul_smul, mul_assoc, mul_inv_cancel Rpos.ne', mul_one]
  have U : (R * r) ‚Ä¢ u' = r ‚Ä¢ u := by
    rw [mul_comm, hu', smul_smul, mul_assoc, mul_inv_cancel Rpos.ne', mul_one]
  dsimp
  rw [T, U]
#align measure_theory.measure.tendsto_add_haar_inter_smul_zero_of_density_zero_aux2 MeasureTheory.Measure.tendsto_addHaar_inter_smul_zero_of_density_zero_aux2

/-- Consider a point `x` at which a set `s` has density zero, with respect to closed balls. Then it
also has density zero with respect to any measurable set `t`: the proportion of points in `s`
belonging to a rescaled copy `{x} + r ‚Ä¢ t` of `t` tends to zero as `r` tends to zero. -/
theorem tendsto_addHaar_inter_smul_zero_of_density_zero (s : Set E) (x : E)
    (h : Tendsto (fun r => Œº (s ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 0)) (t : Set E)
    (ht : MeasurableSet t) (h''t : Œº t ‚â† ‚àû) :
    Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t)) (ùìù[>] 0) (ùìù 0) := by
  refine tendsto_order.2 ‚ü®fun a' ha' => (ENNReal.not_lt_zero ha').elim, fun Œµ (Œµpos : 0 < Œµ) => ?_‚ü©
  rcases eq_or_ne (Œº t) 0 with (h't | h't)
  ¬∑ filter_upwards with r
    suffices H : Œº (s ‚à© ({x} + r ‚Ä¢ t)) = 0 by
      rw [H]; simpa only [ENNReal.zero_div] using Œµpos
    apply le_antisymm _ (zero_le _)
    calc
      Œº (s ‚à© ({x} + r ‚Ä¢ t)) ‚â§ Œº ({x} + r ‚Ä¢ t) := measure_mono (inter_subset_right _ _)
      _ = 0 := by
        simp only [h't, addHaar_smul, image_add_left, measure_preimage_add, singleton_add,
          mul_zero]
  obtain ‚ü®n, npos, hn‚ü© : ‚àÉ n : ‚Ñï, 0 < n ‚àß Œº (t \ closedBall 0 n) < Œµ / 2 * Œº t := by
    have A :
      Tendsto (fun n : ‚Ñï => Œº (t \ closedBall 0 n)) atTop
        (ùìù (Œº (‚ãÇ n : ‚Ñï, t \ closedBall 0 n))) := by
      have N : ‚àÉ n : ‚Ñï, Œº (t \ closedBall 0 n) ‚â† ‚àû :=
        ‚ü®0, ((measure_mono (diff_subset t _)).trans_lt h''t.lt_top).ne‚ü©
      refine tendsto_measure_iInter (fun n ‚Ü¶ ht.diff measurableSet_closedBall) (fun m n hmn ‚Ü¶ ?_) N
      exact diff_subset_diff Subset.rfl (closedBall_subset_closedBall (Nat.cast_le.2 hmn))
    have : ‚ãÇ n : ‚Ñï, t \ closedBall 0 n = ‚àÖ := by
      simp_rw [diff_eq, ‚Üê inter_iInter, iInter_eq_compl_iUnion_compl, compl_compl,
        iUnion_closedBall_nat, compl_univ, inter_empty]
    simp only [this, measure_empty] at A
    have I : 0 < Œµ / 2 * Œº t := ENNReal.mul_pos (ENNReal.half_pos Œµpos.ne').ne' h't
    exact (Eventually.and (Ioi_mem_atTop 0) ((tendsto_order.1 A).2 _ I)).exists
  have L :
    Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ (t ‚à© closedBall 0 n))) / Œº ({x} + r ‚Ä¢ t)) (ùìù[>] 0)
      (ùìù 0) :=
    tendsto_addHaar_inter_smul_zero_of_density_zero_aux2 Œº s x h _ t h't n (Nat.cast_pos.2 npos)
      (inter_subset_right _ _)
  filter_upwards [(tendsto_order.1 L).2 _ (ENNReal.half_pos Œµpos.ne'), self_mem_nhdsWithin]
  rintro r hr (rpos : 0 < r)
  have I :
    Œº (s ‚à© ({x} + r ‚Ä¢ t)) ‚â§
      Œº (s ‚à© ({x} + r ‚Ä¢ (t ‚à© closedBall 0 n))) + Œº ({x} + r ‚Ä¢ (t \ closedBall 0 n)) :=
    calc
      Œº (s ‚à© ({x} + r ‚Ä¢ t)) =
          Œº (s ‚à© ({x} + r ‚Ä¢ (t ‚à© closedBall 0 n)) ‚à™ s ‚à© ({x} + r ‚Ä¢ (t \ closedBall 0 n))) := by
        rw [‚Üê inter_union_distrib_left, ‚Üê add_union, ‚Üê smul_set_union, inter_union_diff]
      _ ‚â§ Œº (s ‚à© ({x} + r ‚Ä¢ (t ‚à© closedBall 0 n))) + Œº (s ‚à© ({x} + r ‚Ä¢ (t \ closedBall 0 n))) :=
        measure_union_le _ _
      _ ‚â§ Œº (s ‚à© ({x} + r ‚Ä¢ (t ‚à© closedBall 0 n))) + Œº ({x} + r ‚Ä¢ (t \ closedBall 0 n)) := by
        gcongr; apply inter_subset_right
  calc
    Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t) ‚â§
        (Œº (s ‚à© ({x} + r ‚Ä¢ (t ‚à© closedBall 0 n))) + Œº ({x} + r ‚Ä¢ (t \ closedBall 0 n))) /
          Œº ({x} + r ‚Ä¢ t) := by gcongr
    _ < Œµ / 2 + Œµ / 2 := by
      rw [ENNReal.add_div]
      apply ENNReal.add_lt_add hr _
      rwa [addHaar_singleton_add_smul_div_singleton_add_smul Œº rpos.ne',
        ENNReal.div_lt_iff (Or.inl h't) (Or.inl h''t)]
    _ = Œµ := ENNReal.add_halves _
#align measure_theory.measure.tendsto_add_haar_inter_smul_zero_of_density_zero MeasureTheory.Measure.tendsto_addHaar_inter_smul_zero_of_density_zero

theorem tendsto_addHaar_inter_smul_one_of_density_one_aux (s : Set E) (hs : MeasurableSet s)
    (x : E) (h : Tendsto (fun r => Œº (s ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 1))
    (t : Set E) (ht : MeasurableSet t) (h't : Œº t ‚â† 0) (h''t : Œº t ‚â† ‚àû) :
    Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t)) (ùìù[>] 0) (ùìù 1) := by
  have I : ‚àÄ u v, Œº u ‚â† 0 ‚Üí Œº u ‚â† ‚àû ‚Üí MeasurableSet v ‚Üí
    Œº u / Œº u - Œº (v·∂ú ‚à© u) / Œº u = Œº (v ‚à© u) / Œº u := by
    intro u v uzero utop vmeas
    simp_rw [div_eq_mul_inv]
    rw [‚Üê ENNReal.sub_mul]; swap
    ¬∑ simp only [uzero, ENNReal.inv_eq_top, imp_true_iff, Ne, not_false_iff]
    congr 1
    apply
      ENNReal.sub_eq_of_add_eq (ne_top_of_le_ne_top utop (measure_mono (inter_subset_right _ _)))
    rw [inter_comm _ u, inter_comm _ u]
    exact measure_inter_add_diff u vmeas
  have L : Tendsto (fun r => Œº (s·∂ú ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 0) := by
    have A : Tendsto (fun r => Œº (closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 1) := by
      apply tendsto_const_nhds.congr' _
      filter_upwards [self_mem_nhdsWithin]
      intro r hr
      rw [div_eq_mul_inv, ENNReal.mul_inv_cancel]
      ¬∑ exact (measure_closedBall_pos Œº _ hr).ne'
      ¬∑ exact measure_closedBall_lt_top.ne
    have B := ENNReal.Tendsto.sub A h (Or.inl ENNReal.one_ne_top)
    simp only [tsub_self] at B
    apply B.congr' _
    filter_upwards [self_mem_nhdsWithin]
    rintro r (rpos : 0 < r)
    convert I (closedBall x r) s·∂ú (measure_closedBall_pos Œº _ rpos).ne'
      measure_closedBall_lt_top.ne hs.compl
    rw [compl_compl]
  have L' : Tendsto (fun r : ‚Ñù => Œº (s·∂ú ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t)) (ùìù[>] 0) (ùìù 0) :=
    tendsto_addHaar_inter_smul_zero_of_density_zero Œº s·∂ú x L t ht h''t
  have L'' : Tendsto (fun r : ‚Ñù => Œº ({x} + r ‚Ä¢ t) / Œº ({x} + r ‚Ä¢ t)) (ùìù[>] 0) (ùìù 1) := by
    apply tendsto_const_nhds.congr' _
    filter_upwards [self_mem_nhdsWithin]
    rintro r (rpos : 0 < r)
    rw [addHaar_singleton_add_smul_div_singleton_add_smul Œº rpos.ne', ENNReal.div_self h't h''t]
  have := ENNReal.Tendsto.sub L'' L' (Or.inl ENNReal.one_ne_top)
  simp only [tsub_zero] at this
  apply this.congr' _
  filter_upwards [self_mem_nhdsWithin]
  rintro r (rpos : 0 < r)
  refine I ({x} + r ‚Ä¢ t) s ?_ ?_ hs
  ¬∑ simp only [h't, abs_of_nonneg rpos.le, pow_pos rpos, addHaar_smul, image_add_left,
      ENNReal.ofReal_eq_zero, not_le, or_false_iff, Ne, measure_preimage_add, abs_pow,
      singleton_add, mul_eq_zero]
  ¬∑ simp [h''t, ENNReal.ofReal_ne_top, addHaar_smul, image_add_left, ENNReal.mul_eq_top,
      Ne, not_false_iff, measure_preimage_add, singleton_add, and_false_iff, false_and_iff,
      or_self_iff]
#align measure_theory.measure.tendsto_add_haar_inter_smul_one_of_density_one_aux MeasureTheory.Measure.tendsto_addHaar_inter_smul_one_of_density_one_aux

/-- Consider a point `x` at which a set `s` has density one, with respect to closed balls (i.e.,
a Lebesgue density point of `s`). Then `s` has also density one at `x` with respect to any
measurable set `t`: the proportion of points in `s` belonging to a rescaled copy `{x} + r ‚Ä¢ t`
of `t` tends to one as `r` tends to zero. -/
theorem tendsto_addHaar_inter_smul_one_of_density_one (s : Set E) (x : E)
    (h : Tendsto (fun r => Œº (s ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 1)) (t : Set E)
    (ht : MeasurableSet t) (h't : Œº t ‚â† 0) (h''t : Œº t ‚â† ‚àû) :
    Tendsto (fun r : ‚Ñù => Œº (s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t)) (ùìù[>] 0) (ùìù 1) := by
  have : Tendsto (fun r : ‚Ñù => Œº (toMeasurable Œº s ‚à© ({x} + r ‚Ä¢ t)) / Œº ({x} + r ‚Ä¢ t))
    (ùìù[>] 0) (ùìù 1) := by
    apply
      tendsto_addHaar_inter_smul_one_of_density_one_aux Œº _ (measurableSet_toMeasurable _ _) _ _
        t ht h't h''t
    apply tendsto_of_tendsto_of_tendsto_of_le_of_le' h tendsto_const_nhds
    ¬∑ refine eventually_of_forall fun r ‚Ü¶ ?_
      gcongr
      apply subset_toMeasurable
    ¬∑ filter_upwards [self_mem_nhdsWithin]
      rintro r -
      apply ENNReal.div_le_of_le_mul
      rw [one_mul]
      exact measure_mono (inter_subset_right _ _)
  refine this.congr fun r => ?_
  congr 1
  apply measure_toMeasurable_inter_of_sFinite
  simp only [image_add_left, singleton_add]
  apply (continuous_add_left (-x)).measurable (ht.const_smul‚ÇÄ r)
#align measure_theory.measure.tendsto_add_haar_inter_smul_one_of_density_one MeasureTheory.Measure.tendsto_addHaar_inter_smul_one_of_density_one

/-- Consider a point `x` at which a set `s` has density one, with respect to closed balls (i.e.,
a Lebesgue density point of `s`). Then `s` intersects the rescaled copies `{x} + r ‚Ä¢ t` of a given
set `t` with positive measure, for any small enough `r`. -/
theorem eventually_nonempty_inter_smul_of_density_one (s : Set E) (x : E)
    (h : Tendsto (fun r => Œº (s ‚à© closedBall x r) / Œº (closedBall x r)) (ùìù[>] 0) (ùìù 1)) (t : Set E)
    (ht : MeasurableSet t) (h't : Œº t ‚â† 0) :
    ‚àÄ·∂† r in ùìù[>] (0 : ‚Ñù), (s ‚à© ({x} + r ‚Ä¢ t)).Nonempty := by
  obtain ‚ü®t', t'_meas, t't, t'pos, t'top‚ü© : ‚àÉ t', MeasurableSet t' ‚àß t' ‚äÜ t ‚àß 0 < Œº t' ‚àß Œº t' < ‚ä§ :=
    exists_subset_measure_lt_top ht h't.bot_lt
  filter_upwards [(tendsto_order.1
          (tendsto_addHaar_inter_smul_one_of_density_one Œº s x h t' t'_meas t'pos.ne' t'top.ne)).1
      0 zero_lt_one]
  intro r hr
  have : Œº (s ‚à© ({x} + r ‚Ä¢ t')) ‚â† 0 := fun h' => by
    simp only [ENNReal.not_lt_zero, ENNReal.zero_div, h'] at hr
  have : (s ‚à© ({x} + r ‚Ä¢ t')).Nonempty := nonempty_of_measure_ne_zero this
  apply this.mono (inter_subset_inter Subset.rfl _)
  exact add_subset_add Subset.rfl (smul_set_mono t't)
#align measure_theory.measure.eventually_nonempty_inter_smul_of_density_one MeasureTheory.Measure.eventually_nonempty_inter_smul_of_density_one

end Measure

end MeasureTheory
