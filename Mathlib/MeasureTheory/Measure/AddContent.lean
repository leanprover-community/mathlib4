/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Peter Pfaffelhuber
-/
import Mathlib.MeasureTheory.SetSemiring
import Mathlib.Topology.Instances.ENNReal

/-!
# Additive Contents

An additive content `m` on a set of sets `C` is a set function with value 0 at the empty set which
is finitely additive on `C`. That means that for any finset `I` of pairwise disjoint sets in `C`
such that `â‹ƒâ‚€ I âˆˆ C`, `m (â‹ƒâ‚€ I) = âˆ‘ s âˆˆ I, m s`.

Mathlib also has a definition of contents over compact sets: see `MeasureTheory.Content`.
A `Content` is in particular an `AddContent` on the set of compact sets.

TODO: refactor `Content` to use properties of `AddContent`.

## Main definitions

* `MeasureTheory.AddContent C`: additive contents over the set of sets `C`.

## Main statements

Let `m` be an `AddContent C`. If `C` is a set semi-ring (`IsSetSemiring C`) we have the properties

* `MeasureTheory.AddContent.sum_le_of_subset`: if `I` is a finset of pairwise disjoint sets in `C`
  and `â‹ƒâ‚€ I âŠ† t` for `t âˆˆ C`, then `âˆ‘ s in I, m s â‰¤ m t`.
* `MeasureTheory.AddContent.mono`: if `s âŠ† t` for two sets in `C`, then `m s â‰¤ m t`.
* `MeasureTheory.AddContent.union'`: if `s, t âˆˆ C` are disjoint and `s âˆª t âˆˆ C`,
  then `m (s âˆª t) = m s + m t`.
  If `C` is a set ring (`IsSetRing`), then `AddContent.union` gives the same conclusion without the
  hypothesis `s âˆª t âˆˆ C` (since it is a consequence of `IsSetRing C`).

If `C` is a set ring (`MeasureTheory.IsSetRing C`), we have, for `s, t âˆˆ C`,

* `MeasureTheory.AddContent.union_le`: `m (s âˆª t) â‰¤ m s + m t`
* `MeasureTheory.AddContent.le_sdiff`: `m s - m t â‰¤ m (s \ t)`

### Ïƒ-additive and Ïƒ-subadditive contents

We say that an additive content `m` is Ïƒ-additive on `C` if for all sequences of pairwise disjoints
sets `s : â„• â†’ Set Î±` in `C`, `m (â‹ƒ i, s i) = âˆ‘' i, m (s i)`.
We say that an additive content is Ïƒ-subadditive on `C` if for all sequences of sets of `C` (not
necessarily disjoint) `s : â„• â†’ Set Î±`, `m (â‹ƒ i, s i) â‰¤ âˆ‘' i, m (s i)`.

* `AddContent.iUnion_eq_tsum_of_disjoint_of_iUnion_le`: if an `AddContent` is Ïƒ-subadditive on
  a semi-ring of sets, then it is Ïƒ-additive.
* `AddContent.iUnion_le_of_iUnion_eq_tsum`: if an `AddContent` is Ïƒ-additive on a ring of sets,
  then it is Ïƒ-subadditive.

* `AddContent.tendsto_atTop_iUnion_of_iUnion_eq_tsum`: if an additive content is Ïƒ-additive on a
  ring of sets, then the content of a monotone sequence of sets tends to the content of the union.

-/

open Set Finset Filter

open scoped ENNReal BigOperators Topology

namespace MeasureTheory

variable {Î± : Type*} {C : Set (Set Î±)} {s t : Set Î±} {I : Finset (Set Î±)}

/-- An additive content is a set function with value 0 at the empty set which is finitely additive
on a given set of sets. -/
structure AddContent (C : Set (Set Î±)) where
  /-- The value of the content on a set. -/
  toFun : Set Î± â†’ â„â‰¥0âˆ
  empty' : toFun âˆ… = 0
  sUnion_eq_sum' (I : Finset (Set Î±)) (_h_ss : â†‘I âŠ† C)
      (_h_dis : PairwiseDisjoint (I : Set (Set Î±)) id) (_h_mem : â‹ƒâ‚€ â†‘I âˆˆ C) :
    toFun (â‹ƒâ‚€ I) = âˆ‘ u in I, toFun u

instance : Inhabited (AddContent C) :=
  âŸ¨{toFun := fun _ â†¦ 0
    empty' := by simp
    sUnion_eq_sum' := by simp }âŸ©

instance : FunLike (AddContent C) (Set Î±) (fun _ â†¦ â„â‰¥0âˆ) where
  coe := fun m s â†¦ m.toFun s
  coe_injective' := by
    intro m m' h
    cases m
    cases m'
    congr

namespace AddContent

@[ext] protected lemma ext {m m' : AddContent C} (h : âˆ€ s, m s = m' s) : m = m' := FunLike.ext _ _ h

protected lemma ext_iff (m m' : AddContent C) : m = m' â†” âˆ€ s, m s = m' s := FunLike.ext_iff

@[simp] protected lemma apply_empty {m : AddContent C} : m âˆ… = 0 := m.empty'

protected lemma sUnion_eq_sum (m : AddContent C) (h_ss : â†‘I âŠ† C)
    (h_dis : PairwiseDisjoint (I : Set (Set Î±)) id) (h_mem : â‹ƒâ‚€ â†‘I âˆˆ C) :
    m (â‹ƒâ‚€ I) = âˆ‘ u in I, m u :=
  m.sUnion_eq_sum' I h_ss h_dis h_mem

protected lemma union' (m : AddContent C) (hs : s âˆˆ C) (ht : t âˆˆ C) (hst : s âˆª t âˆˆ C)
    (h_dis : Disjoint s t) :
    m (s âˆª t) = m s + m t := by
  by_cases hs_empty : s = âˆ…
  Â· simp only [hs_empty, Set.empty_union, m.apply_empty, zero_add]
  classical
  have h := m.sUnion_eq_sum (I := {s, t}) ?_ ?_ ?_
  rotate_left
  Â· simp only [coe_pair, Set.insert_subset_iff, hs, ht, Set.singleton_subset_iff, and_self_iff]
  Â· simp only [coe_pair, Set.pairwiseDisjoint_insert, pairwiseDisjoint_singleton,
      mem_singleton_iff, Ne.def, id.def, forall_eq, true_and_iff]
    exact fun _ â†¦ h_dis
  Â· simp only [coe_pair, sUnion_insert, sUnion_singleton]
    exact hst
  convert h
  Â· simp only [coe_pair, sUnion_insert, sUnion_singleton]
  Â· rw [sum_insert, sum_singleton]
    simp only [Finset.mem_singleton]
    refine fun hs_eq_t â†¦ hs_empty ?_
    rw [â† hs_eq_t] at h_dis
    exact Disjoint.eq_bot_of_self h_dis

section IsSetSemiring

lemma eq_add_diffFinsetâ‚€_of_subset (m : AddContent C) (hC : IsSetSemiring C)
    (hs : s âˆˆ C) (hI : â†‘I âŠ† C) (hI_ss : â‹ƒâ‚€ â†‘I âŠ† s)
    (h_dis : PairwiseDisjoint (I : Set (Set Î±)) id) :
    m s = âˆ‘ i in I, m i + âˆ‘ i in hC.diffFinsetâ‚€ hs hI, m i := by
  classical
  conv_lhs => rw [â† hC.sUnion_union_diffFinsetâ‚€_of_subset hs hI hI_ss]
  rw [m.sUnion_eq_sum]
  Â· rw [sum_union]
    exact hC.disjoint_diffFinsetâ‚€ hs hI
  Â· rw [coe_union]
    exact Set.union_subset hI (hC.diffFinsetâ‚€_subset hs hI)
  Â· rw [coe_union]
    exact hC.pairwiseDisjoint_union_diffFinsetâ‚€ hs hI h_dis
  Â· rwa [hC.sUnion_union_diffFinsetâ‚€_of_subset hs hI hI_ss]

protected lemma sum_le_of_subset (m : AddContent C) (hC : IsSetSemiring C)
    (h_ss : â†‘I âŠ† C) (h_dis : PairwiseDisjoint (I : Set (Set Î±)) id)
    (ht : t âˆˆ C) (hJt : â‹ƒâ‚€ â†‘I âŠ† t) :
    âˆ‘ u in I, m u â‰¤ m t := by
  classical
  rw [m.eq_add_diffFinsetâ‚€_of_subset hC ht h_ss hJt h_dis]
  exact le_add_right le_rfl

protected lemma mono (m : AddContent C) (hC : IsSetSemiring C) (hs : s âˆˆ C) (ht : t âˆˆ C)
    (hst : s âŠ† t) :
    m s â‰¤ m t := by
  have h := m.sum_le_of_subset hC (I := {s}) ?_ ?_ ht ?_
  Â· simpa only [sum_singleton] using h
  Â· rwa [singleton_subset_set_iff]
  Â· simp only [coe_singleton, pairwiseDisjoint_singleton]
  Â· simp only [coe_singleton, sUnion_singleton]
    exact hst

lemma sUnion_le_sum (m : AddContent C) (hC : IsSetSemiring C)
    (J : Finset (Set Î±)) (h_ss : â†‘J âŠ† C) (h_mem : â‹ƒâ‚€ â†‘J âˆˆ C) :
    m (â‹ƒâ‚€ â†‘J) â‰¤ âˆ‘ u in J, m u := by
  classical
  rw [â† hC.sUnion_allDiffFinsetâ‚€ J h_ss, m.add]
  rotate_left
  Â· exact hC.allDiffFinsetâ‚€_subset J h_ss
  Â· exact hC.pairwiseDisjoint_allDiffFinsetâ‚€ J h_ss
  Â· rwa [hC.sUnion_allDiffFinsetâ‚€ J h_ss]
  rw [IsSetSemiring.allDiffFinsetâ‚€, sum_disjiUnion, â† sum_ordered J]
  refine sum_le_sum fun i _ â†¦ m.sum_le_of_subset hC ?_ ?_ ?_ ?_
  Â· exact hC.indexedDiffFinsetâ‚€_subset J h_ss i
  Â· exact hC.pairwiseDisjoint_indexedDiffFinsetâ‚€' J h_ss i
  Â· exact ordered_mem' h_ss i
  Â· exact hC.sUnion_indexedDiffFinsetâ‚€_subset J h_ss i

lemma le_sum_of_subset_sUnion (m : AddContent C) (hC : IsSetSemiring C)
    (J : Finset (Set Î±)) (h_ss : â†‘J âŠ† C) (ht : t âˆˆ C) (htJ : t âŠ† â‹ƒâ‚€ â†‘J) :
    m t â‰¤ âˆ‘ u in J, m u := by
  classical
  let Jt := Finset.image (fun u â†¦ t âˆ© u) J
  have ht_eq : t = â‹ƒâ‚€ Jt := by
    rw [coe_image, sUnion_image, â† inter_iUnionâ‚‚, inter_eq_self_of_subset_left]
    rwa [â† sUnion_eq_biUnion]
  rw [ht_eq]
  refine' (m.sUnion_le_sum hC Jt _ _).trans _
  Â· intro s
    simp only [coe_image, Set.mem_image, mem_coe, forall_exists_index, and_imp]
    rintro u hu rfl
    exact hC.inter_mem _ ht _ (h_ss hu)
  Â· rwa [â† ht_eq]
  refine (Finset.sum_image_le J _ m fun _ _ â†¦ zero_le _).trans ?_
  refine sum_le_sum fun u hu â†¦ ?_
  exact m.mono hC (hC.inter_mem _ ht _ (h_ss hu)) (h_ss hu) (inter_subset_right _ _)

/-- If an `AddContent` is Ïƒ-subadditive on a semi-ring of sets, then it is Ïƒ-additive. -/
theorem iUnion_eq_tsum_of_disjoint_of_iUnion_le (m : AddContent C) (hC : IsSetSemiring C)
    (m_subadd : âˆ€ (f : â„• â†’ Set Î±) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : â‹ƒ i, f i âˆˆ C)
      (_hf_disj : Pairwise (Disjoint on f)), m (â‹ƒ i, f i) â‰¤ âˆ‘' i, m (f i))
    (f : â„• â†’ Set Î±) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : (â‹ƒ i, f i) âˆˆ C)
    (hf_disj : Pairwise (Disjoint on f)) :
    m (â‹ƒ i, f i) = âˆ‘' i, m (f i) := by
  refine le_antisymm (m_subadd f hf hf_Union hf_disj) ?_
  refine tsum_le_of_sum_le ENNReal.summable fun I â†¦ ?_
  classical
  rw [â† Finset.sum_image_of_disjoint m m.empty f _ (hf_disj.pairwiseDisjoint _)]
  refine m.sum_le_of_subset hC (I := I.image f) ?_ ?_ ?_ ?_
  Â· simp only [coe_image, Set.image_subset_iff]
    refine (subset_preimage_image f I).trans (preimage_mono ?_)
    rintro i âŸ¨j, _, rflâŸ©
    exact hf j
  Â· simp only [coe_image]
    intro s hs t ht hst
    rw [Set.mem_image] at hs ht
    obtain âŸ¨i, _, rflâŸ© := hs
    obtain âŸ¨j, _, rflâŸ© := ht
    have hij : i â‰  j := by intro h_eq; rw [h_eq] at hst; exact hst rfl
    exact hf_disj hij
  Â· exact hf_Union
  Â· simp only [coe_image, sUnion_image, mem_coe, iUnion_subset_iff]
    exact fun i _ â†¦ subset_iUnion _ i

end IsSetSemiring

section IsSetRing

protected lemma union (m : AddContent C) (hC : IsSetRing C) (hs : s âˆˆ C) (ht : t âˆˆ C)
    (h_dis : Disjoint s t) :
    m (s âˆª t) = m s + m t :=
  m.union' hs ht (hC.union_mem hs ht) h_dis

protected lemma union_le (m : AddContent C) (hC : IsSetRing C) (hs : s âˆˆ C) (ht : t âˆˆ C) :
    m (s âˆª t) â‰¤ m s + m t := by
  rw [â† union_diff_self, m.union hC hs (hC.diff_mem ht hs)]
  Â· exact add_le_add le_rfl (m.mono hC.isSetSemiring (hC.diff_mem ht hs) ht (diff_subset _ _))
  Â· rw [Set.disjoint_iff_inter_eq_empty, inter_diff_self]

protected lemma biUnion_le {Î¹ : Type*} (m : AddContent C) (hC : IsSetRing C) {s : Î¹ â†’ Set Î±}
    {S : Finset Î¹} (hs : âˆ€ n âˆˆ S, s n âˆˆ C) :
    m (â‹ƒ i âˆˆ S, s i) â‰¤ âˆ‘ i in S, m (s i) := by
  classical
  revert hs
  refine Finset.induction ?_ ?_ S
  Â· simp
  Â· intro i S hiS h hs
    rw [Finset.sum_insert hiS]
    simp_rw [â† Finset.mem_coe, Finset.coe_insert, Set.biUnion_insert]
    simp only [Finset.mem_insert, forall_eq_or_imp] at hs
    refine (m.union_le hC hs.1 (hC.biUnion_mem S hs.2)).trans ?_
    exact add_le_add le_rfl (h hs.2)

protected lemma le_sdiff (m : AddContent C) (hC : IsSetRing C) (hs : s âˆˆ C) (ht : t âˆˆ C) :
    m s - m t â‰¤ m (s \ t) := by
  conv_lhs => rw [â† inter_union_diff s t]
  rw [m.union hC (hC.inter_mem hs ht) (hC.diff_mem hs ht) disjoint_inf_sdiff, add_comm]
  refine add_tsub_le_assoc.trans_eq ?_
  rw [tsub_eq_zero_of_le (m.mono hC.isSetSemiring (hC.inter_mem hs ht) ht (inter_subset_right _ _)),
    add_zero]

/-- If an additive content is Ïƒ-additive on a set ring, then the content of a monotone sequence of
sets tends to the content of the union. -/
theorem tendsto_atTop_iUnion_of_iUnion_eq_tsum (m : AddContent C) (hC : IsSetRing C)
    (m_add : âˆ€ (f : â„• â†’ Set Î±) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : (â‹ƒ i, f i) âˆˆ C)
        (_hf_disj : Pairwise (Disjoint on f)), m (â‹ƒ i, f i) = âˆ‘' i, m (f i))
    (f : â„• â†’ Set Î±) (hf_mono : Monotone f) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : â‹ƒ i, f i âˆˆ C) :
    Tendsto (fun n â†¦ m (f n)) atTop (ğ“ (m (â‹ƒ i, f i))) := by
  classical
  let g := disjointed f
  have hg_Union : (â‹ƒ i, g i) = â‹ƒ i, f i := iUnion_disjointed
  specialize m_add g (hC.disjointed_mem hf) _ (disjoint_disjointed f)
  Â· rwa [hg_Union]
  rw [â† hg_Union]
  simp_rw [m_add]
  have h : âˆ€ n, m (f n) = âˆ‘ i in range (n + 1), m (g i) := by
    intro n
    have h1 : f n = â‹ƒâ‚€ Finset.image g (range (n + 1)) := by
      rw [â† Monotone.partialSups_eq hf_mono, â† partialSups_disjointed, â†
        partialSups_eq_sUnion_image g]
    rw [h1, m.add]
    rotate_left
    Â· intro s
      rw [mem_coe, Finset.mem_image]
      rintro âŸ¨i, _, rflâŸ©
      exact hC.disjointed_mem hf i
    Â· intro s hs t ht hst
      rw [mem_coe, Finset.mem_image] at hs ht
      obtain âŸ¨i, _, rflâŸ© := hs
      obtain âŸ¨j, _, rflâŸ© := ht
      have hij : i â‰  j := by intro h_eq; rw [h_eq] at hst; exact hst rfl
      exact disjoint_disjointed f hij
    Â· rw [â† h1]; exact hf n
    rw [sum_image_of_disjoint m m.empty g _ ((disjoint_disjointed f).pairwiseDisjoint _)]
  simp_rw [h]
  change Tendsto (fun n â†¦ (fun k â†¦ âˆ‘ i in range k, m (g i)) (n + 1)) atTop (ğ“ (âˆ‘' i, m (g i)))
  rw [tendsto_add_atTop_iff_nat (f := (fun k â†¦ âˆ‘ i in range k, m (g i))) 1]
  exact ENNReal.tendsto_nat_tsum _

/-- If an additive content is Ïƒ-additive on a set ring, then it is Ïƒ-subadditive. -/
theorem iUnion_le_of_iUnion_eq_tsum (m : AddContent C) (hC : IsSetRing C)
    (m_add : âˆ€ (f : â„• â†’ Set Î±) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : (â‹ƒ i, f i) âˆˆ C)
      (_hf_disj : Pairwise (Disjoint on f)), m (â‹ƒ i, f i) = âˆ‘' i, m (f i))
    (f : â„• â†’ Set Î±) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : â‹ƒ i, f i âˆˆ C) :
    m (â‹ƒ i, f i) â‰¤ âˆ‘' i, m (f i) := by
  classical
  have h_tendsto : Tendsto (fun n â†¦ m (partialSups f n)) atTop (ğ“ (m (â‹ƒ i, f i))) := by
    rw [â† iSup_eq_iUnion, â† iSup_partialSups_eq]
    refine m.tendsto_atTop_iUnion_of_iUnion_eq_tsum hC m_add (partialSups f)
      (monotone_partialSups f) (hC.partialSups_mem hf) ?_
    rwa [â† iSup_eq_iUnion, iSup_partialSups_eq]
  have h_tendsto' :
      Tendsto (fun n â†¦ âˆ‘ i in range (n + 1), m (f i)) atTop (ğ“ (âˆ‘' i, m (f i))) := by
    rw [tendsto_add_atTop_iff_nat (f := (fun k â†¦ âˆ‘ i in range k, m (f i))) 1]
    exact ENNReal.tendsto_nat_tsum _
  refine le_of_tendsto_of_tendsto' h_tendsto h_tendsto' fun n â†¦ ?_
  rw [partialSups_eq_sUnion_image]
  refine (m.le_sum_of_subset_sUnion hC.isSetSemiring
    ((Finset.range (n + 1)).image f) ?_ ?_ subset_rfl).trans ?_
  Â· intro s
    rw [mem_coe, Finset.mem_image]
    rintro âŸ¨i, _, rflâŸ©
    exact hf i
  Â· rw [â† partialSups_eq_sUnion_image]
    exact hC.partialSups_mem hf n
  Â· exact Finset.sum_image_le _ _ _ fun _ _ â†¦ zero_le _

end IsSetRing

end AddContent

end MeasureTheory
