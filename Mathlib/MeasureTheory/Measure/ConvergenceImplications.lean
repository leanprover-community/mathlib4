/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.Portmanteau

/-!
# Implications between different notions of convergence of measures

This file contains results relating convergence in probability (`TendstoInMeasure`)
and convergence in distribution (`Tendsto` in the `ProbabilityMeasure` type of the law of the
random variable).

## Main statements

* `tendsto_of_tendstoInMeasure_sub_of_tendsto`: the main technical tool for the next results.
  Let `f, f'` be two sequences of measurable functions such that `f n` converges in distribution
  to `g`, and `f' n - f n` converges in probability to `0`.
  Then `f' n` converges in distribution to `g`.
* `tendsto_map_of_tendstoInMeasure`: convergence in probability implies convergence in distribution.
* `tendsto_prodMk_of_tendstoInMeasure_const_of_tendsto`: **Slutsky's theorem**.
  If `f n` converges in distribution to `g`, and `f' n` converges in probability to a constant `c`,
  then the pair `(f n, f' n)` converges in distribution to `(g, c)`.

-/

open Filter
open scoped Topology

namespace MeasureTheory.ProbabilityMeasure

variable {Î± Î¹ E : Type*} {m : MeasurableSpace Î±} [NormedAddCommGroup E] [MeasurableSpace E]
  [BorelSpace E] [SecondCountableTopology E] {Î¼ : Measure Î±} [IsProbabilityMeasure Î¼]
  {f f' : Î¹ â†’ Î± â†’ E} {g : Î± â†’ E} {l : Filter Î¹}

/-- Let `f, f'` be two sequences of measurable functions such that `f n` converges in distribution
to `g`, and `f' n - f n` converges in probability to `0`.
Then `f' n` converges in distribution to `g`. -/
lemma tendsto_of_tendstoInMeasure_sub_of_tendsto [l.IsCountablyGenerated]
    (hf' : âˆ€ i, AEMeasurable (f' i) Î¼) (hf : âˆ€ i, AEMeasurable (f i) Î¼)
    (hg : AEMeasurable g Î¼) (hff' : TendstoInMeasure Î¼ (fun n â†¦ f' n - f n) l 0)
    (hfg : Tendsto (Î² := ProbabilityMeasure E)
      (fun n â†¦ âŸ¨Î¼.map (f n), Measure.isProbabilityMeasure_map (hf n)âŸ©) l
      (ğ“ âŸ¨Î¼.map g, Measure.isProbabilityMeasure_map hgâŸ©)) :
    Tendsto (Î² := ProbabilityMeasure E)
      (fun n â†¦ âŸ¨Î¼.map (f' n), Measure.isProbabilityMeasure_map (hf' n)âŸ©) l
      (ğ“ âŸ¨Î¼.map g, Measure.isProbabilityMeasure_map hgâŸ©) := by
  rcases isEmpty_or_nonempty E with hE | hE
  Â· simp only [Subsingleton.elim _ (0 : Measure E)]
    exact tendsto_const_nhds
  let xâ‚€ : E := hE.some
  -- We show convergence in distribution by verifying the convergence of integrals of any bounded
  -- Lipschitz function `F`
  suffices âˆ€ (F : E â†’ â„) (hF_bounded : âˆƒ (C : â„), âˆ€ x y, dist (F x) (F y) â‰¤ C)
      (hF_lip : âˆƒ L, LipschitzWith L F),
      Tendsto (fun n â†¦ âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f' n))) l (ğ“ (âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g))) by
    rwa [tendsto_iff_forall_lipschitz_integral_tendsto]
  rintro F âŸ¨M, hF_boundedâŸ© âŸ¨L, hF_lipâŸ©
  have hF_cont : Continuous F := hF_lip.continuous
  -- If `F` is 0-Lipschitz, then it is constant, and all integrals are equal to that constant
  by_cases hL : L = 0
  Â· simp only [hL, LipschitzWith.zero_iff] at hF_lip
    specialize hF_lip xâ‚€
    simp_rw [eq_comm (a := F xâ‚€)] at hF_lip
    simp only [hF_lip, integral_const, smul_eq_mul]
    have h_prob n : IsProbabilityMeasure (Î¼.map (f' n)) := Measure.isProbabilityMeasure_map (hf' n)
    have : IsProbabilityMeasure (Î¼.map g) := Measure.isProbabilityMeasure_map hg
    simp only [measureReal_univ_eq_one, one_mul]
    exact tendsto_const_nhds
  -- now `F` is `L`-Lipschitz with `L > 0`
  replace hL : 0 < L := lt_of_le_of_ne L.2 (Ne.symm hL)
  rw [Metric.tendsto_nhds]
  simp_rw [Real.dist_eq]
  suffices âˆ€ Îµ > 0, âˆ€á¶  n in l, |âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f' n)) - âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g)| < L * Îµ by
    intro Îµ hÎµ
    specialize this (Îµ / L) (by positivity)
    convert this
    field_simp
  intro Îµ hÎµ
  -- We cut the difference into three pieces, two of which are small by the convergence assumptions
  have h_le n : |âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f' n)) - âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g)|
      â‰¤ L * (Îµ / 2) + M * Î¼.real {Ï‰ | Îµ / 2 â‰¤ â€–f' n Ï‰ - f n Ï‰â€–}
        + |âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f n)) - âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g)| := by
    refine (dist_triangle (âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f' n))) (âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f n)))
      (âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g))).trans ?_
    gcongr
    swap; Â· rw [Real.dist_eq]
    rw [Real.dist_eq]
    -- `âŠ¢ |âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f' n)) - âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f n))|`
    -- `    â‰¤ L * (Îµ / 2) + M * Î¼.real {Ï‰ | Îµ / 2 â‰¤ â€–f' n Ï‰ - f n Ï‰â€–}`
    -- We prove integrability of the functions involved to be able to manipulate the integrals.
    have h_int_f' : Integrable (fun x â†¦ F (f' n x)) Î¼ := by
      refine Integrable.of_bound (by fun_prop) (â€–F xâ‚€â€– + M) (ae_of_all _ fun a â†¦ ?_)
      specialize hF_bounded (f' n a) xâ‚€
      rw [â† sub_le_iff_le_add']
      exact (abs_sub_abs_le_abs_sub (F (f' n a)) (F xâ‚€)).trans hF_bounded
    have h_int_f : Integrable (fun x â†¦ F (f n x)) Î¼ := by
      refine Integrable.of_bound (by fun_prop) (â€–F xâ‚€â€– + M) (ae_of_all _ fun a â†¦ ?_)
      specialize hF_bounded (f n a) xâ‚€
      rw [â† sub_le_iff_le_add']
      exact (abs_sub_abs_le_abs_sub (F (f n a)) (F xâ‚€)).trans hF_bounded
    have h_int_sub : Integrable (fun a â†¦ â€–F (f' n a) - F (f n a)â€–) Î¼ := by
      rw [integrable_norm_iff (by fun_prop)]
      exact h_int_f'.sub h_int_f
    -- Now we prove the inequality
    rw [integral_map (by fun_prop) (by fun_prop), integral_map (by fun_prop) (by fun_prop),
      â† integral_sub h_int_f' h_int_f]
    rw [â† Real.norm_eq_abs]
    calc â€–âˆ« a, F (f' n a) - F (f n a) âˆ‚Î¼â€–
    _ â‰¤ âˆ« a, â€–F (f' n a) - F (f n a)â€– âˆ‚Î¼ := norm_integral_le_integral_norm _
    -- Either `â€–f' n x - f n xâ€–` is smaller than `Îµ / 2`, or it is not
    _ = âˆ« a in {x | â€–f' n x - f n xâ€– < Îµ / 2}, â€–F (f' n a) - F (f n a)â€– âˆ‚Î¼
        + âˆ« a in {x | Îµ / 2 â‰¤ â€–f' n x - f n xâ€–}, â€–F (f' n a) - F (f n a)â€– âˆ‚Î¼ := by
      symm
      simp_rw [â† not_lt]
      refine integral_add_complâ‚€ ?_ h_int_sub
      exact nullMeasurableSet_lt (by fun_prop) (by fun_prop)
    -- If it is smaller, we use the Lipschitz property of `F`
    -- If not, we use the boundedness of `F`.
    _ â‰¤ âˆ« a in {x | â€–f' n x - f n xâ€– < Îµ / 2}, L * (Îµ / 2) âˆ‚Î¼
        + âˆ« a in {x | Îµ / 2 â‰¤ â€–f' n x - f n xâ€–}, M âˆ‚Î¼ := by
      gcongr ?_ + ?_
      Â· refine setIntegral_mono_on' h_int_sub.integrableOn integrableOn_const ?_ ?_
        Â· exact nullMeasurableSet_lt (by fun_prop) (by fun_prop)
        Â· intro x hx
          simp only [Set.mem_setOf_eq] at hx
          rw [â† dist_eq_norm] at hx âŠ¢
          exact hF_lip.dist_le_mul_of_le hx.le
      Â· refine setIntegral_mono h_int_sub.integrableOn integrableOn_const fun a â†¦ ?_
        rw [â† dist_eq_norm]
        convert hF_bounded _ _
    -- The goal is now a simple computation
    _ = L * (Îµ / 2) * Î¼.real {x | â€–f' n x - f n xâ€– < Îµ / 2}
        + M * Î¼.real {Ï‰ | Îµ / 2 â‰¤ â€–f' n Ï‰ - f n Ï‰â€–} := by
      simp only [integral_const, MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter,
        smul_eq_mul]
      ring
    _ â‰¤ L * (Îµ / 2) + M * Î¼.real {Ï‰ | Îµ / 2 â‰¤ â€–f' n Ï‰ - f n Ï‰â€–} := by
      rw [mul_assoc]
      gcongr
      conv_rhs => rw [â† mul_one (Îµ / 2)]
      gcongr
      exact measureReal_le_one
  -- We finally show that the right-hand side tends to `L * Îµ / 2`, which is smaller than `L * Îµ`
  have h_tendsto :
      Tendsto (fun n â†¦ L * (Îµ / 2) + M * Î¼.real {Ï‰ | Îµ / 2 â‰¤ â€–f' n Ï‰ - f n Ï‰â€–}
        + |âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f n)) - âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g)|) l (ğ“ (L * Îµ / 2)) := by
    suffices Tendsto (fun n â†¦ L * (Îµ / 2) + M * Î¼.real {Ï‰ | Îµ / 2 â‰¤ â€–f' n Ï‰ - f n Ï‰â€–}
        + |âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map (f n)) - âˆ« Ï‰, F Ï‰ âˆ‚(Î¼.map g)|) l (ğ“ (L * Îµ / 2 + M * 0 + 0)) by
      simpa
    refine (Tendsto.add ?_ (Tendsto.const_mul _ ?_)).add ?_
    Â· rw [mul_div_assoc]
      exact tendsto_const_nhds
    Â· simp only [tendstoInMeasure_iff_norm, Pi.zero_apply, sub_zero] at hff'
      have h_tendsto := hff' (Îµ / 2) (by positivity) -- the result, up to `Î¼.real` vs `Î¼`
      refine Tendsto.comp ?_ h_tendsto
      exact ENNReal.tendsto_toReal (ENNReal.zero_ne_top)
    Â· simp_rw [tendsto_iff_forall_lipschitz_integral_tendsto] at hfg
      simpa [tendsto_iff_dist_tendsto_zero] using hfg F âŸ¨M, hF_boundedâŸ© âŸ¨L, hF_lipâŸ©
  have h_lt : L * Îµ / 2 < L * Îµ := by
    rw [mul_div_assoc]
    gcongr
    exact half_lt_self hÎµ
  filter_upwards [h_tendsto.eventually_lt_const h_lt] with n hn using (h_le n).trans_lt hn

/-- Convergence in probability (`TendstoInMeasure`) implies convergence in distribution
(`Tendsto` in the `ProbabilityMeasure` type). -/
lemma tendsto_map_of_tendstoInMeasure [l.IsCountablyGenerated]
    (hf : âˆ€ i, AEMeasurable (f i) Î¼) (hg : AEMeasurable g Î¼) (h : TendstoInMeasure Î¼ f l g) :
    Tendsto (Î² := ProbabilityMeasure E)
      (fun n â†¦ âŸ¨Î¼.map (f n), Measure.isProbabilityMeasure_map (hf n)âŸ©) l
      (ğ“ âŸ¨Î¼.map g, Measure.isProbabilityMeasure_map hgâŸ©) := by
  refine ProbabilityMeasure.tendsto_of_tendstoInMeasure_sub_of_tendsto hf (fun _ â†¦ hg) hg ?_
    tendsto_const_nhds
  simpa [tendstoInMeasure_iff_norm] using h

/-- **Slutsky's theorem**: if `f n` converges in distribution to `g`, and `f' n` converges in
probability to a constant `c`, then the pair `(f n, f' n)` converges in distribution to `(g, c)`. -/
lemma tendsto_prodMk_of_tendstoInMeasure_const_of_tendsto
    [l.IsCountablyGenerated] (hf' : âˆ€ i, AEMeasurable (f' i) Î¼) (hf : âˆ€ i, AEMeasurable (f i) Î¼)
    {c : E} (hg : AEMeasurable g Î¼) (hff' : TendstoInMeasure Î¼ (fun n â†¦ f' n) l (fun _ â†¦ c))
    (hfg : Tendsto (Î² := ProbabilityMeasure E)
      (fun n â†¦ âŸ¨Î¼.map (f n), Measure.isProbabilityMeasure_map (hf n)âŸ©) l
      (ğ“ âŸ¨Î¼.map g, Measure.isProbabilityMeasure_map hgâŸ©)) :
    Tendsto (Î² := ProbabilityMeasure (E Ã— E))
      (fun n â†¦ âŸ¨Î¼.map (fun Ï‰ â†¦ (f n Ï‰, f' n Ï‰)),
        Measure.isProbabilityMeasure_map ((hf n).prodMk (hf' n))âŸ©) l
      (ğ“ âŸ¨Î¼.map (fun Ï‰ â†¦ (g Ï‰, c)),
        Measure.isProbabilityMeasure_map (hg.prodMk (by fun_prop))âŸ©) := by
  refine ProbabilityMeasure.tendsto_of_tendstoInMeasure_sub_of_tendsto (f := fun n Ï‰ â†¦ (f n Ï‰, c))
    (f' := fun n Ï‰ â†¦ (f n Ï‰, f' n Ï‰)) (g := fun Ï‰ â†¦ (g Ï‰, c)) (Î¼ := Î¼) (l := l)
    (by fun_prop) (by fun_prop) (by fun_prop) ?_ ?_
  Â· suffices TendstoInMeasure Î¼ (fun n Ï‰ â†¦ ((0 : E), f' n Ï‰ - c)) l 0 by
      convert this with n Ï‰
      simp
    simpa [tendstoInMeasure_iff_norm] using hff'
  Â· rw [tendsto_iff_forall_lipschitz_integral_tendsto] at hfg âŠ¢
    intro F âŸ¨M, hF_boundedâŸ© âŸ¨L, hF_lipâŸ©
    simp only [coe_mk]
    have hFc_lip : LipschitzWith L (fun x â†¦ F (x, c)) := by
      intro x y
      specialize hF_lip (x, c) (y, c)
      refine hF_lip.trans ?_
      simp [edist_eq_enorm_sub, enorm_eq_nnnorm]
    have h_eq (f : Î± â†’ E) (hf : AEMeasurable f Î¼) :
        âˆ« Ï‰, F Ï‰ âˆ‚Î¼.map (fun Ï‰ â†¦ (f Ï‰, c)) = âˆ« Ï‰, (fun x â†¦ F (x, c)) Ï‰ âˆ‚(Î¼.map f) := by
      rw [integral_map (by fun_prop), integral_map (by fun_prop)]
      Â· exact hFc_lip.continuous.stronglyMeasurable.aestronglyMeasurable
      Â· exact hF_lip.continuous.stronglyMeasurable.aestronglyMeasurable
    simp_rw [h_eq (f _) (hf _), h_eq g hg]
    specialize hfg (fun x â†¦ F (x, c)) âŸ¨M, ?_âŸ© âŸ¨L, hFc_lipâŸ©
    Â· exact fun x y â†¦ hF_bounded (x, c) (y, c)
    Â· simpa

end MeasureTheory.ProbabilityMeasure
