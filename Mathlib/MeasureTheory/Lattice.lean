/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.AEMeasurable

#align_import measure_theory.lattice from "leanprover-community/mathlib"@"a95b442734d137aef46c1871e147089877fd0f62"

/-!
# Typeclasses for measurability of lattice operations

In this file we define classes `MeasurableSup` and `MeasurableInf` and prove dot-style
lemmas (`Measurable.sup`, `AEMeasurable.sup` etc). For binary operations we define two typeclasses:

- `MeasurableSup` says that both left and right sup are measurable;
- `MeasurableSupâ‚‚` says that `fun p : Î± Ã— Î± => p.1 âŠ” p.2` is measurable,

and similarly for other binary operations. The reason for introducing these classes is that in case
of topological space `Î±` equipped with the Borel `Ïƒ`-algebra, instances for `MeasurableSupâ‚‚`
etc require `Î±` to have a second countable topology.

For instances relating, e.g., `ContinuousSup` to `MeasurableSup` see file
`MeasureTheory.BorelSpace`.

## Tags

measurable function, lattice operation

-/


open MeasureTheory

/-- We say that a type has `MeasurableSup` if `(c âŠ” Â·)` and `(Â· âŠ” c)` are measurable functions.
For a typeclass assuming measurability of `uncurry (Â· âŠ” Â·)` see `MeasurableSupâ‚‚`. -/
class MeasurableSup (M : Type*) [MeasurableSpace M] [Sup M] : Prop where
  measurable_const_sup : âˆ€ c : M, Measurable (c âŠ” Â·)
  measurable_sup_const : âˆ€ c : M, Measurable (Â· âŠ” c)
#align has_measurable_sup MeasurableSup
#align has_measurable_sup.measurable_const_sup MeasurableSup.measurable_const_sup
#align has_measurable_sup.measurable_sup_const MeasurableSup.measurable_sup_const

/-- We say that a type has `MeasurableSupâ‚‚` if `uncurry (Â· âŠ” Â·)` is a measurable functions.
For a typeclass assuming measurability of `(c âŠ” Â·)` and `(Â· âŠ” c)` see `MeasurableSup`. -/
class MeasurableSupâ‚‚ (M : Type*) [MeasurableSpace M] [Sup M] : Prop where
  measurable_sup : Measurable fun p : M Ã— M => p.1 âŠ” p.2
#align has_measurable_supâ‚‚ MeasurableSupâ‚‚
#align has_measurable_supâ‚‚.measurable_sup MeasurableSupâ‚‚.measurable_sup

export MeasurableSupâ‚‚ (measurable_sup)

export MeasurableSup (measurable_const_sup measurable_sup_const)

/-- We say that a type has `MeasurableInf` if `(c âŠ“ Â·)` and `(Â· âŠ“ c)` are measurable functions.
For a typeclass assuming measurability of `uncurry (Â· âŠ“ Â·)` see `MeasurableInfâ‚‚`. -/
class MeasurableInf (M : Type*) [MeasurableSpace M] [Inf M] : Prop where
  measurable_const_inf : âˆ€ c : M, Measurable (c âŠ“ Â·)
  measurable_inf_const : âˆ€ c : M, Measurable (Â· âŠ“ c)
#align has_measurable_inf MeasurableInf
#align has_measurable_inf.measurable_const_inf MeasurableInf.measurable_const_inf
#align has_measurable_inf.measurable_inf_const MeasurableInf.measurable_inf_const

/-- We say that a type has `MeasurableInfâ‚‚` if `uncurry (Â· âŠ“ Â·)` is a measurable functions.
For a typeclass assuming measurability of `(c âŠ“ Â·)` and `(Â· âŠ“ c)` see `MeasurableInf`. -/
class MeasurableInfâ‚‚ (M : Type*) [MeasurableSpace M] [Inf M] : Prop where
  measurable_inf : Measurable fun p : M Ã— M => p.1 âŠ“ p.2
#align has_measurable_infâ‚‚ MeasurableInfâ‚‚
#align has_measurable_infâ‚‚.measurable_inf MeasurableInfâ‚‚.measurable_inf

export MeasurableInfâ‚‚ (measurable_inf)

export MeasurableInf (measurable_const_inf measurable_inf_const)

variable {M : Type*} [MeasurableSpace M]

section OrderDual

instance (priority := 100) OrderDual.instMeasurableSup [Inf M] [MeasurableInf M] :
    MeasurableSup Máµ’áµˆ :=
  âŸ¨@measurable_const_inf M _ _ _, @measurable_inf_const M _ _ _âŸ©
#align order_dual.has_measurable_sup OrderDual.instMeasurableSup

instance (priority := 100) OrderDual.instMeasurableInf [Sup M] [MeasurableSup M] :
    MeasurableInf Máµ’áµˆ :=
  âŸ¨@measurable_const_sup M _ _ _, @measurable_sup_const M _ _ _âŸ©
#align order_dual.has_measurable_inf OrderDual.instMeasurableInf

instance (priority := 100) OrderDual.instMeasurableSupâ‚‚ [Inf M] [MeasurableInfâ‚‚ M] :
    MeasurableSupâ‚‚ Máµ’áµˆ :=
  âŸ¨@measurable_inf M _ _ _âŸ©
#align order_dual.has_measurable_supâ‚‚ OrderDual.instMeasurableSupâ‚‚

instance (priority := 100) OrderDual.instMeasurableInfâ‚‚ [Sup M] [MeasurableSupâ‚‚ M] :
    MeasurableInfâ‚‚ Máµ’áµˆ :=
  âŸ¨@measurable_sup M _ _ _âŸ©
#align order_dual.has_measurable_infâ‚‚ OrderDual.instMeasurableInfâ‚‚

end OrderDual

variable {Î± : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} {f g : Î± â†’ M}

section Sup

variable [Sup M]

section MeasurableSup

variable [MeasurableSup M]

@[measurability]
theorem Measurable.const_sup (hf : Measurable f) (c : M) : Measurable fun x => c âŠ” f x :=
  (measurable_const_sup c).comp hf
#align measurable.const_sup Measurable.const_sup

@[measurability]
theorem AEMeasurable.const_sup (hf : AEMeasurable f Î¼) (c : M) :
    AEMeasurable (fun x => c âŠ” f x) Î¼ :=
  (MeasurableSup.measurable_const_sup c).comp_aemeasurable hf
#align ae_measurable.const_sup AEMeasurable.const_sup

@[measurability]
theorem Measurable.sup_const (hf : Measurable f) (c : M) : Measurable fun x => f x âŠ” c :=
  (measurable_sup_const c).comp hf
#align measurable.sup_const Measurable.sup_const

@[measurability]
theorem AEMeasurable.sup_const (hf : AEMeasurable f Î¼) (c : M) :
    AEMeasurable (fun x => f x âŠ” c) Î¼ :=
  (measurable_sup_const c).comp_aemeasurable hf
#align ae_measurable.sup_const AEMeasurable.sup_const

end MeasurableSup

section MeasurableSupâ‚‚

variable [MeasurableSupâ‚‚ M]

@[measurability]
theorem Measurable.sup' (hf : Measurable f) (hg : Measurable g) : Measurable (f âŠ” g) :=
  measurable_sup.comp (hf.prod_mk hg)
#align measurable.sup' Measurable.sup'

@[measurability]
theorem Measurable.sup (hf : Measurable f) (hg : Measurable g) : Measurable fun a => f a âŠ” g a :=
  measurable_sup.comp (hf.prod_mk hg)
#align measurable.sup Measurable.sup

@[measurability]
theorem AEMeasurable.sup' (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) :
    AEMeasurable (f âŠ” g) Î¼ :=
  measurable_sup.comp_aemeasurable (hf.prod_mk hg)
#align ae_measurable.sup' AEMeasurable.sup'

@[measurability]
theorem AEMeasurable.sup (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) :
    AEMeasurable (fun a => f a âŠ” g a) Î¼ :=
  measurable_sup.comp_aemeasurable (hf.prod_mk hg)
#align ae_measurable.sup AEMeasurable.sup

instance (priority := 100) MeasurableSupâ‚‚.toMeasurableSup : MeasurableSup M :=
  âŸ¨fun _ => measurable_const.sup measurable_id, fun _ => measurable_id.sup measurable_constâŸ©
#align has_measurable_supâ‚‚.to_has_measurable_sup MeasurableSupâ‚‚.toMeasurableSup

end MeasurableSupâ‚‚

end Sup

section Inf

variable [Inf M]

section MeasurableInf

variable [MeasurableInf M]

@[measurability]
theorem Measurable.const_inf (hf : Measurable f) (c : M) : Measurable fun x => c âŠ“ f x :=
  (measurable_const_inf c).comp hf
#align measurable.const_inf Measurable.const_inf

@[measurability]
theorem AEMeasurable.const_inf (hf : AEMeasurable f Î¼) (c : M) :
    AEMeasurable (fun x => c âŠ“ f x) Î¼ :=
  (MeasurableInf.measurable_const_inf c).comp_aemeasurable hf
#align ae_measurable.const_inf AEMeasurable.const_inf

@[measurability]
theorem Measurable.inf_const (hf : Measurable f) (c : M) : Measurable fun x => f x âŠ“ c :=
  (measurable_inf_const c).comp hf
#align measurable.inf_const Measurable.inf_const

@[measurability]
theorem AEMeasurable.inf_const (hf : AEMeasurable f Î¼) (c : M) :
    AEMeasurable (fun x => f x âŠ“ c) Î¼ :=
  (measurable_inf_const c).comp_aemeasurable hf
#align ae_measurable.inf_const AEMeasurable.inf_const

end MeasurableInf

section MeasurableInfâ‚‚

variable [MeasurableInfâ‚‚ M]

@[measurability]
theorem Measurable.inf' (hf : Measurable f) (hg : Measurable g) : Measurable (f âŠ“ g) :=
  measurable_inf.comp (hf.prod_mk hg)
#align measurable.inf' Measurable.inf'

@[measurability]
theorem Measurable.inf (hf : Measurable f) (hg : Measurable g) : Measurable fun a => f a âŠ“ g a :=
  measurable_inf.comp (hf.prod_mk hg)
#align measurable.inf Measurable.inf

@[measurability]
theorem AEMeasurable.inf' (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) :
    AEMeasurable (f âŠ“ g) Î¼ :=
  measurable_inf.comp_aemeasurable (hf.prod_mk hg)
#align ae_measurable.inf' AEMeasurable.inf'

@[measurability]
theorem AEMeasurable.inf (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) :
    AEMeasurable (fun a => f a âŠ“ g a) Î¼ :=
  measurable_inf.comp_aemeasurable (hf.prod_mk hg)
#align ae_measurable.inf AEMeasurable.inf

instance (priority := 100) MeasurableInfâ‚‚.to_hasMeasurableInf : MeasurableInf M :=
  âŸ¨fun _ => measurable_const.inf measurable_id, fun _ => measurable_id.inf measurable_constâŸ©
#align has_measurable_infâ‚‚.to_has_measurable_inf MeasurableInfâ‚‚.to_hasMeasurableInf

end MeasurableInfâ‚‚

end Inf

section SemilatticeSup

open Finset

variable {Î´ : Type*} [MeasurableSpace Î´] [SemilatticeSup Î±] [MeasurableSupâ‚‚ Î±]

@[measurability]
theorem Finset.measurable_sup' {Î¹ : Type*} {s : Finset Î¹} (hs : s.Nonempty) {f : Î¹ â†’ Î´ â†’ Î±}
    (hf : âˆ€ n âˆˆ s, Measurable (f n)) : Measurable (s.sup' hs f) :=
  Finset.sup'_induction hs _ (fun _f hf _g hg => hf.sup hg) fun n hn => hf n hn
#align finset.measurable_sup' Finset.measurable_sup'

@[measurability]
theorem Finset.measurable_range_sup' {f : â„• â†’ Î´ â†’ Î±} {n : â„•} (hf : âˆ€ k â‰¤ n, Measurable (f k)) :
    Measurable ((range (n + 1)).sup' nonempty_range_succ f) := by
  simp_rw [â† Nat.lt_succ_iff] at hf
  -- âŠ¢ Measurable (sup' (range (n + 1)) (_ : Finset.Nonempty (range (n + 1))) f)
  refine' Finset.measurable_sup' _ _
  -- âŠ¢ âˆ€ (n_1 : â„•), n_1 âˆˆ range (n + 1) â†’ Measurable (f n_1)
  simpa [Finset.mem_range]
  -- ğŸ‰ no goals
#align finset.measurable_range_sup' Finset.measurable_range_sup'

@[measurability]
theorem Finset.measurable_range_sup'' {f : â„• â†’ Î´ â†’ Î±} {n : â„•} (hf : âˆ€ k â‰¤ n, Measurable (f k)) :
    Measurable fun x => (range (n + 1)).sup' nonempty_range_succ fun k => f k x := by
  convert Finset.measurable_range_sup' hf using 1
  -- âŠ¢ (fun x => sup' (range (n + 1)) (_ : Finset.Nonempty (range (n + 1))) fun k = â€¦
  ext x
  -- âŠ¢ (sup' (range (n + 1)) (_ : Finset.Nonempty (range (n + 1))) fun k => f k x)  â€¦
  simp
  -- ğŸ‰ no goals
#align finset.measurable_range_sup'' Finset.measurable_range_sup''

end SemilatticeSup
