/-
Copyright (c) 2025 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.Normed.Module.Dual
import Mathlib.Data.Real.StarOrdered
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.Order.CompletePartialOrder

/-! # HÃ¶lder triples and actions on `MeasureTheory.Lp` spaces

This file defines a new class: `ENNReal.HolderTriple` which takes arguments `p q r : â„â‰¥0âˆ`,
with `r` marked as a `semiOutParam`, and states that `pâ»Â¹ + qâ»Â¹ = râ»Â¹`. This is exactly the
condition for which **HÃ¶lder's inequality** is valid (see `MeasureTheory.Memâ„’p.smul`).
This allows us to declare a heterogeneous scalar multiplication (`HSMul`) instance on
`MeasureTheory.Lp` spaces.

More generally, given a continuous bilinear map `B : E â†’L[ğ•œ] F â†’L[ğ•œ] G`, we define an
associated map `MeasureTheory.Lp.ofBilin : Lp E p Î¼ â†’ Lp F q Î¼ â†’ Lp G r Î¼` where `p q r` are
a HÃ¶lder triple. We bundle this into a bilinear map `ContinuousLinearMap.holderâ‚—` and a continuous
bilinear map `ContinuousLinearMap.holderL`.

When `p q : â„â‰¥0âˆ` are HÃ¶lder conjugate (i.e., `HolderTriple p q 1`), we can construct the
natural continuous linear map `Lp.toDualCLM : Lp (Dual ğ•œ E) p Î¼ â†’L[ğ•œ] Dual ğ•œ (Lp E q Î¼)` given by
`fun Ï† f â†¦ âˆ« x, (Ï† x) (f x) âˆ‚Î¼`.
 -/

open ENNReal

/-! ### HÃ¶lder triples -/

namespace ENNReal

/-- A class stating that `p q r : â„â‰¥0âˆ` satisfy `pâ»Â¹ + qâ»Â¹ = râ»Â¹`.
This is exactly the condition for which **HÃ¶lder's inequality** is valid
(see `MeasureTheory.Memâ„’p.smul`).

When `r := 1`, one generally says that `p q` are **HÃ¶lder conjugate**.

This class exists so that we can define a heterogeneous multiplication
on `MeasureTheory.Lp`, and this is why `r` must be marked as a
`semiOutParam`. We don't mark it as an `outParam` because this would
prevent Lean from using `HolderTriple p q r` and `HolderTriple p q r'`
within a single proof, as may be occasionally convenient. -/
class HolderTriple (p q : â„â‰¥0âˆ) (r : semiOutParam â„â‰¥0âˆ) : Prop where
  inv_add_inv : pâ»Â¹ + qâ»Â¹ = râ»Â¹

namespace HolderTriple

lemma eq {p q r : â„â‰¥0âˆ} [ENNReal.HolderTriple p q r] :
    pâ»Â¹ + qâ»Â¹ = râ»Â¹ :=
  inv_add_inv

lemma div_eq {p q r : â„â‰¥0âˆ} [ENNReal.HolderTriple p q r] :
    1 / p + 1 / q = 1 / r := by
  simpa using eq

lemma div_eq' {p q r : â„â‰¥0âˆ} [ENNReal.HolderTriple p q r] :
    1 / r = 1 / p + 1 / q :=
  div_eq.symm

lemma inv_inv_add_inv (p q r : â„â‰¥0âˆ) [ENNReal.HolderTriple p q r] :
    (pâ»Â¹ + qâ»Â¹)â»Â¹ = r := by
  simp [@eq p q r]

/-- This is not marked as an instance so that Lean doesn't always find this one
and a more canonical value of `r` can be used. -/
lemma of (p q : â„â‰¥0âˆ) : HolderTriple p q (pâ»Â¹ + qâ»Â¹)â»Â¹ where
  inv_add_inv := inv_inv _ |>.symm

instance instTwoTwoOne : HolderTriple 2 2 1 where
  inv_add_inv := by
    rw [â† two_mul, ENNReal.mul_inv_cancel]
    all_goals norm_num

/- This instance causes a trivial loop, but this is exactly the kind of loop that
Lean should be able to detect and avoid. -/
instance symm {p q r : â„â‰¥0âˆ} [hpqr : HolderTriple p q r] : HolderTriple q p r where
  inv_add_inv := add_comm pâ»Â¹ qâ»Â¹ â–¸ hpqr.eq

instance instInfty {p : â„â‰¥0âˆ} : HolderTriple p âˆ p where
  inv_add_inv := by simp

instance instZero {p : â„â‰¥0âˆ} : HolderTriple p 0 0 where
  inv_add_inv := by simp

end HolderTriple
end ENNReal

noncomputable section

namespace MeasureTheory
namespace Lp

/-! ### Induced bilinear maps -/

section Bilinear

open scoped NNReal

variable {Î± ğ•œ E F G : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±}
    {p q r : ENNReal} [hpqr : HolderTriple p q r] [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]

theorem _root_.MeasureTheory.Memâ„’p.of_bilin (b : E â†’ F â†’ G) (c : â„â‰¥0)
    {f : Î± â†’ E} {g : Î± â†’ F} (hf : Memâ„’p f p Î¼) (hg : Memâ„’p g q Î¼)
    (h : AEStronglyMeasurable (fun x â†¦ b (f x) (g x)) Î¼)
    (hb : âˆ€áµ (x : Î±) âˆ‚Î¼, â€–b (f x) (g x)â€–â‚Š â‰¤ c * â€–f xâ€–â‚Š * â€–g xâ€–â‚Š) :
    Memâ„’p (fun x â†¦ b (f x) (g x)) r Î¼ :=
  .intro h <| (eLpNorm_le_eLpNorm_mul_eLpNorm_of_nnnorm hf.1 hg.1 b c hb hpqr.div_eq').trans_lt <|
    by have := hf.2; have := hg.2; finiteness

variable [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] [NormedSpace ğ•œ G]
    (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G)

/-- The map between `MeasuryTheory.Lp` spaces satisfying the `ENNReal.HolderTriple`
condition induced by a continuous bilinear map on the underlying spaces. -/
def ofBilin (f : Lp E p Î¼) (g : Lp F q Î¼) : Lp G r Î¼ :=
  Memâ„’p.toLp (fun x â†¦ B (f x) (g x)) <| by
    refine .of_bilin (B Â· Â·) â€–Bâ€–â‚Š (Lp.memâ„’p f) (Lp.memâ„’p g) ?_ <|
      .of_forall fun _ â†¦ B.le_opNormâ‚‚ _ _
    exact B.aestronglyMeasurable_compâ‚‚ (Lp.memâ„’p f).1 (Lp.memâ„’p g).1

lemma coeFn_ofBilin (f : Lp E p Î¼) (g : Lp F q Î¼) :
    (ofBilin B f g : Lp G r Î¼) =áµ[Î¼] fun x â†¦ B (f x) (g x) := by
  rw [ofBilin]
  exact Memâ„’p.coeFn_toLp _

lemma nnnorm_ofBilin_apply_apply_le (f : Lp E p Î¼) (g : Lp F q Î¼) :
    â€–(ofBilin B f g : Lp G r Î¼)â€–â‚Š â‰¤ â€–Bâ€–â‚Š * â€–fâ€–â‚Š * â€–gâ€–â‚Š := by
  simp_rw [â† ENNReal.coe_le_coe, ENNReal.coe_mul, â† enorm_eq_nnnorm, Lp.enorm_def]
  apply eLpNorm_congr_ae (coeFn_ofBilin B f g) |>.trans_le
  exact eLpNorm_le_eLpNorm_mul_eLpNorm_of_nnnorm (Lp.memâ„’p f).1 (Lp.memâ„’p g).1 (B Â· Â·) â€–Bâ€–â‚Š
    (.of_forall fun _ â†¦ B.le_opNormâ‚‚ _ _) hpqr.div_eq'

lemma norm_ofBilin_apply_apply_le (f : Lp E p Î¼) (g : Lp F q Î¼) :
    â€–(ofBilin B f g : Lp G r Î¼)â€– â‰¤ â€–Bâ€– * â€–fâ€– * â€–gâ€– :=
  NNReal.coe_le_coe.mpr <| nnnorm_ofBilin_apply_apply_le B f g

lemma ofBilin_add_left (fâ‚ fâ‚‚ : Lp E p Î¼) (g : Lp F q Î¼) :
    (ofBilin B (fâ‚ + fâ‚‚) g : Lp G r Î¼) = ofBilin B fâ‚ g + ofBilin B fâ‚‚ g := by
  simp only [ofBilin, â† Memâ„’p.toLp_add]
  apply Memâ„’p.toLp_congr
  filter_upwards [AEEqFun.coeFn_add fâ‚.val fâ‚‚.val] with x hx
  simp [hx]

lemma ofBilin_add_right (f : Lp E p Î¼) (gâ‚ gâ‚‚ : Lp F q Î¼) :
    (ofBilin B f (gâ‚ + gâ‚‚) : Lp G r Î¼) = ofBilin B f gâ‚ + ofBilin B f gâ‚‚ := by
  simp only [ofBilin, â† Memâ„’p.toLp_add]
  apply Memâ„’p.toLp_congr
  filter_upwards [AEEqFun.coeFn_add gâ‚.val gâ‚‚.val] with x hx
  simp [hx]

lemma ofBilin_smul_left (c : ğ•œ) (f : Lp E p Î¼) (g : Lp F q Î¼) :
    (ofBilin B (c â€¢ f) g : Lp G r Î¼) = c â€¢ ofBilin B f g := by
  simp only [ofBilin, â† Memâ„’p.toLp_const_smul]
  apply Memâ„’p.toLp_congr
  filter_upwards [Lp.coeFn_smul c f] with x hx
  simp [hx]

lemma ofBilin_smul_right (c : ğ•œ) (f : Lp E p Î¼) (g : Lp F q Î¼) :
    (ofBilin B f (c â€¢ g) : Lp G r Î¼) = c â€¢ ofBilin B f g := by
  simp only [ofBilin, â† Memâ„’p.toLp_const_smul]
  apply Memâ„’p.toLp_congr
  filter_upwards [Lp.coeFn_smul c g] with x hx
  simp [hx]

variable (Î¼ p q r) in
/-- `MeasureTheory.Lp.ofBilin` as a bilinear map. -/
@[simps! apply_apply]
def _root_.ContinuousLinearMap.holderâ‚— : Lp E p Î¼ â†’â‚—[ğ•œ] Lp F q Î¼ â†’â‚—[ğ•œ] Lp G r Î¼ :=
  .mkâ‚‚ ğ•œ (ofBilin B) (ofBilin_add_left B) (ofBilin_smul_left B)
    (ofBilin_add_right B) (ofBilin_smul_right B)

variable [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] [Fact (1 â‰¤ r)]

variable (Î¼ p q r) in
/-- `MeasureTheory.Lp.ofBilin` as a continuous bilinear map. -/
@[simps! apply_apply]
def _root_.ContinuousLinearMap.holderL : Lp E p Î¼ â†’L[ğ•œ] Lp F q Î¼ â†’L[ğ•œ] Lp G r Î¼ :=
  LinearMap.mkContinuousâ‚‚ (B.holderâ‚— Î¼ p q r) â€–Bâ€– (norm_ofBilin_apply_apply_le B)

lemma _root_.ContinuousLinearMap.norm_holderL_le : â€–(B.holderL Î¼ p q r)â€– â‰¤ â€–Bâ€– :=
  LinearMap.mkContinuousâ‚‚_norm_le _ (norm_nonneg B) _

end Bilinear

section Dual

open NormedSpace

variable {Î± ğ•œ E : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±}
    {p q : ENNReal} [hpqr : HolderTriple p q 1] [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)]
    [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

variable (ğ•œ Î¼ p q E) in
@[simps!]
def toDualCLM : Lp (Dual ğ•œ E) p Î¼ â†’L[ğ•œ] Dual ğ•œ (Lp E q Î¼) :=
  (L1.integralCLM' ğ•œ |>.postcomp <| Lp E q Î¼) âˆ˜L ((inclusionInDoubleDual ğ•œ E).flip.holderL Î¼ p q 1)

end Dual


/-! ### Heterogeneous scalar multiplication

While the previous section is *nominally* more general than this one, and indeed, we could
use the constructions of the previous section to define the scalar multiplication herein,
we would lose some slight generality as we would need to require that `ğ•œ` is a nontrivially
normed field everywhere. Moreover, it would only simplify a few proofs.
-/

section SMul

variable {Î± ğ•œ' ğ•œ E : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±}
    {p q r : â„â‰¥0âˆ} [hpqr : HolderTriple p q r]

section MulActionWithZero

variable [NormedRing ğ•œ] [NormedAddCommGroup E] [MulActionWithZero ğ•œ E] [BoundedSMul ğ•œ E]

/-- Heterogeneous scalar multiplication of `MeasureTheory.Lp` functions by `MeasureTheory.Lp`
functions when the exponents satisfy `ENNReal.HolderTriple p q r`. -/
instance : HSMul (Lp ğ•œ p Î¼) (Lp E q Î¼) (Lp E r Î¼) where
  hSMul f g := (Lp.memâ„’p g).smul (Lp.memâ„’p f) hpqr.div_eq' |>.toLp (â‡‘f â€¢ â‡‘g)

lemma smul_def {f : Lp ğ•œ p Î¼} {g : Lp E q Î¼} :
    f â€¢ g = ((Lp.memâ„’p g).smul (Lp.memâ„’p f) hpqr.div_eq').toLp (â‡‘f â€¢ â‡‘g) :=
  rfl

lemma coeFn_lp_smul (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    (f â€¢ g : Lp E r Î¼) =áµ[Î¼] â‡‘f â€¢ g := by
  rw [smul_def]
  exact Memâ„’p.coeFn_toLp _

protected lemma norm_smul_le (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    â€–f â€¢ gâ€– â‰¤ â€–fâ€– * â€–gâ€– := by
  simp only [Lp.norm_def, â† ENNReal.toReal_mul, coeFn_lp_smul]
  refine ENNReal.toReal_mono ?_ ?_
  Â· exact ENNReal.mul_ne_top (eLpNorm_ne_top f) (eLpNorm_ne_top g)
  Â· rw [eLpNorm_congr_ae (coeFn_lp_smul f g)]
    exact eLpNorm_smul_le_mul_eLpNorm (Lp.aestronglyMeasurable g)
      (Lp.aestronglyMeasurable f) hpqr.div_eq'

end MulActionWithZero

section Module

variable [NormedRing ğ•œ] [NormedAddCommGroup E] [Module ğ•œ E] [BoundedSMul ğ•œ E]

protected lemma smul_add (fâ‚ fâ‚‚ : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    (fâ‚ + fâ‚‚) â€¢ g = fâ‚ â€¢ g + fâ‚‚ â€¢ g := by
  simp only [smul_def, â† Memâ„’p.toLp_add]
  apply Memâ„’p.toLp_congr
  filter_upwards [AEEqFun.coeFn_add fâ‚.val fâ‚‚.val] with x hx
  simp [hx, add_smul]

protected lemma add_smul (f : Lp ğ•œ p Î¼) (gâ‚ gâ‚‚  : Lp E q Î¼) :
    f â€¢ (gâ‚ + gâ‚‚) = f â€¢ gâ‚ + f â€¢ gâ‚‚ := by
  simp only [smul_def, â† Memâ„’p.toLp_add]
  apply Memâ„’p.toLp_congr _ _ ?_
  filter_upwards [AEEqFun.coeFn_add gâ‚.val gâ‚‚.val] with x hx
  simp [hx, smul_add]

variable (E q) in
@[simp]
protected lemma smul_zero (f : Lp ğ•œ p Î¼) :
    f â€¢ (0 : Lp E q Î¼) = (0 : Lp E r Î¼) := by
  convert Memâ„’p.zero.toLp_zero
  apply Memâ„’p.toLp_congr _ _ ?_
  filter_upwards [Lp.coeFn_zero E q Î¼] with x hx
  rw [Pi.smul_apply', hx]
  simp

variable (ğ•œ p) in
@[simp]
protected lemma zero_smul (f : Lp E q Î¼) :
    (0 : Lp ğ•œ p Î¼) â€¢ f = (0 : Lp E r Î¼) := by
  convert Memâ„’p.zero.toLp_zero
  apply Memâ„’p.toLp_congr _ _ ?_
  filter_upwards [Lp.coeFn_zero ğ•œ p Î¼] with x hx
  rw [Pi.smul_apply', hx]
  simp

@[simp]
protected lemma smul_neg (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    f â€¢ -g = -(f â€¢ g) := by
  simp [eq_neg_iff_add_eq_zero, â† Lp.add_smul]

@[simp]
protected lemma neg_smul (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    -f â€¢ g = -(f â€¢ g) := by
  simp [eq_neg_iff_add_eq_zero, â† Lp.smul_add]

protected lemma neg_smul_neg (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    -f â€¢ -g = f â€¢ g := by
  simp

variable [NormedRing ğ•œ'] [Module ğ•œ' E] [Module ğ•œ' ğ•œ] [BoundedSMul ğ•œ' E] [BoundedSMul ğ•œ' ğ•œ]

protected lemma smul_smul_assoc [IsScalarTower ğ•œ' ğ•œ E]
    (c : ğ•œ') (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    (c â€¢ f) â€¢ g = c â€¢ (f â€¢ g) := by
  simp only [smul_def, â† Memâ„’p.toLp_const_smul]
  apply Memâ„’p.toLp_congr
  filter_upwards [Lp.coeFn_smul c f] with x hx
  simp [- smul_eq_mul, hx]

protected lemma smul_comm [SMulCommClass ğ•œ' ğ•œ E]
    (c : ğ•œ') (f : Lp ğ•œ p Î¼) (g : Lp E q Î¼) :
    c â€¢ f â€¢ g = f â€¢ c â€¢ g := by
  simp only [smul_def, â† Memâ„’p.toLp_const_smul]
  apply Memâ„’p.toLp_congr
  filter_upwards [Lp.coeFn_smul c f, Lp.coeFn_smul c g] with x hfx hgx
  simp [smul_comm, hfx, hgx]

end Module

end SMul

end Lp
end MeasureTheory
