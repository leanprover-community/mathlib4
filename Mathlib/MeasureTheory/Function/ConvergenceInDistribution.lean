/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure

/-!
# Convergence in distribution

We introduce a definition of convergence in distribution of random variables: this is the
weak convergence of the laws of the random variables. In Mathlib terms this is a `Tendsto` in the
`ProbabilityMeasure` type.

The definition assumes that the random variables are defined on the same probability space, which
is the most common setting in applications. Convergence in distribution for random variables
on different probability spaces can be talked about using the `ProbabilityMeasure` type directly.

## Main definitions

* `TendstoInDistribution X l Z Œº` : the sequence of random variables `X n` converges in
  distribution to the random variable `Z` along the filter `l` with respect to the probability
  measure `Œº`.

## Main statements

* `TendstoInDistribution.continuous_comp`: **Continuous mapping theorem**.
  If `X n` tends to `Z` in distribution and `g` is continuous, then `g ‚àò X n` tends to `g ‚àò Z`
  in distribution.
-/

open Filter
open scoped Topology

namespace MeasureTheory

variable {Œ© Œπ E : Type*} {m : MeasurableSpace Œ©} {Œº : Measure Œ©} [IsProbabilityMeasure Œº]
  [TopologicalSpace E] {mE : MeasurableSpace E} {X Y : Œπ ‚Üí Œ© ‚Üí E} {Z : Œ© ‚Üí E} {l : Filter Œπ}

section TendstoInDistribution

/-- Convergence in distribution of random variables.
This is the weak convergence of the laws of the random variables: `Tendsto` in the
`ProbabilityMeasure` type. -/
structure TendstoInDistribution [OpensMeasurableSpace E] (X : Œπ ‚Üí Œ© ‚Üí E) (l : Filter Œπ) (Z : Œ© ‚Üí E)
    (Œº : Measure Œ© := by volume_tac) [IsProbabilityMeasure Œº] : Prop where
  forall_aeMeasurable : ‚àÄ i, AEMeasurable (X i) Œº
  aeMeasurable_limit : AEMeasurable Z Œº := by fun_prop
  tendsto : Tendsto (Œ≤ := ProbabilityMeasure E)
      (fun n ‚Ü¶ ‚ü®Œº.map (X n), Measure.isProbabilityMeasure_map (forall_aeMeasurable n)‚ü©) l
      (ùìù ‚ü®Œº.map Z, Measure.isProbabilityMeasure_map aeMeasurable_limit‚ü©)

lemma tendstoInDistribution_const [OpensMeasurableSpace E] (hZ : AEMeasurable Z Œº) :
    TendstoInDistribution (fun _ ‚Ü¶ Z) l Z Œº where
  forall_aeMeasurable := fun _ ‚Ü¶ by fun_prop
  tendsto := tendsto_const_nhds

lemma tendstoInDistribution_unique [HasOuterApproxClosed E] [BorelSpace E]
    (X : Œπ ‚Üí Œ© ‚Üí E) {Z W : Œ© ‚Üí E} [l.NeBot]
    (h1 : TendstoInDistribution X l Z Œº) (h2 : TendstoInDistribution X l W Œº) :
    Œº.map Z = Œº.map W := by
  have h_eq := tendsto_nhds_unique h1.tendsto h2.tendsto
  rw [Subtype.ext_iff] at h_eq
  simpa using h_eq

/-- **Continuous mapping theorem**: if `X n` tends to `Z` in distribution and `g` is continuous,
then `g ‚àò X n` tends to `g ‚àò Z` in distribution. -/
theorem TendstoInDistribution.continuous_comp {F : Type*} [OpensMeasurableSpace E]
    [TopologicalSpace F] [MeasurableSpace F] [BorelSpace F] {g : E ‚Üí F} (hg : Continuous g)
    (h : TendstoInDistribution X l Z Œº) :
    TendstoInDistribution (fun n ‚Ü¶ g ‚àò X n) l (g ‚àò Z) Œº where
  forall_aeMeasurable := fun n ‚Ü¶ hg.measurable.comp_aemeasurable (h.forall_aeMeasurable n)
  aeMeasurable_limit := hg.measurable.comp_aemeasurable h.aeMeasurable_limit
  tendsto := by
    have h_tendsto := h.tendsto
    have hX := h.forall_aeMeasurable
    have hZ := h.aeMeasurable_limit
    rw [ProbabilityMeasure.tendsto_iff_forall_integral_tendsto] at h_tendsto ‚ä¢
    intro f
    specialize h_tendsto (f.compContinuous ‚ü®g, hg‚ü©)
    simp only [ProbabilityMeasure.coe_mk, BoundedContinuousFunction.compContinuous_apply,
      ContinuousMap.coe_mk] at h_tendsto
    simp only [ProbabilityMeasure.coe_mk]
    rw [‚Üê AEMeasurable.map_map_of_aemeasurable (by fun_prop) hZ,
      integral_map (by fun_prop) (by fun_prop)]
    convert h_tendsto with n
    rw [integral_map (by fun_prop) (by fun_prop), integral_map (by fun_prop)]
    ¬∑ simp
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop

end TendstoInDistribution

end MeasureTheory
