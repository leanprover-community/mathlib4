/-
Copyright (c) 2020 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Normed.Group.Hom
import Mathlib.MeasureTheory.Function.LpSeminorm
import Mathlib.MeasureTheory.Measure.OpenPos
import Mathlib.Topology.ContinuousFunction.Compact

#align_import measure_theory.function.lp_space from "leanprover-community/mathlib"@"c4015acc0a223449d44061e27ddac1835a3852b9"

/-!
# Lp space

This file provides the space `Lp E p Î¼` as the subtype of elements of `Î± â†’â‚˜[Î¼] E` (see ae_eq_fun)
such that `snorm f p Î¼` is finite. For `1 â‰¤ p`, `snorm` defines a norm and `Lp` is a complete metric
space.

## Main definitions

* `Lp E p Î¼` : elements of `Î± â†’â‚˜[Î¼] E` (see ae_eq_fun) such that `snorm f p Î¼` is finite. Defined
  as an `AddSubgroup` of `Î± â†’â‚˜[Î¼] E`.

Lipschitz functions vanishing at zero act by composition on `Lp`. We define this action, and prove
that it is continuous. In particular,
* `ContinuousLinearMap.compLp` defines the action on `Lp` of a continuous linear map.
* `Lp.posPart` is the positive part of an `Lp` function.
* `Lp.negPart` is the negative part of an `Lp` function.

When `Î±` is a topological space equipped with a finite Borel measure, there is a bounded linear map
from the normed space of bounded continuous functions (`Î± â†’áµ‡ E`) to `Lp E p Î¼`.  We construct this
as `BoundedContinuousFunction.toLp`.

## Notations

* `Î± â†’â‚[Î¼] E` : the type `Lp E 1 Î¼`.
* `Î± â†’â‚‚[Î¼] E` : the type `Lp E 2 Î¼`.

## Implementation

Since `Lp` is defined as an `AddSubgroup`, dot notation does not work. Use `Lp.Measurable f` to
say that the coercion of `f` to a genuine function is measurable, instead of the non-working
`f.Measurable`.

To prove that two `Lp` elements are equal, it suffices to show that their coercions to functions
coincide almost everywhere (this is registered as an `ext` rule). This can often be done using
`filter_upwards`. For instance, a proof from first principles that `f + (g + h) = (f + g) + h`
could read (in the `Lp` namespace)
```
example (f g h : Lp E p Î¼) : (f + g) + h = f + (g + h) := by
  ext1
  filter_upwards [coeFn_add (f + g) h, coeFn_add f g, coeFn_add f (g + h), coeFn_add g h]
    with _ ha1 ha2 ha3 ha4
  simp only [ha1, ha2, ha3, ha4, add_assoc]
```
The lemma `coeFn_add` states that the coercion of `f + g` coincides almost everywhere with the sum
of the coercions of `f` and `g`. All such lemmas use `coeFn` in their name, to distinguish the
function coercion from the coercion to almost everywhere defined functions.
-/


noncomputable section

set_option linter.uppercaseLean3 false

open TopologicalSpace MeasureTheory Filter

open scoped NNReal ENNReal BigOperators Topology MeasureTheory Uniformity

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

variable {Î± E F G : Type*} {m m0 : MeasurableSpace Î±} {p : â„â‰¥0âˆ} {q : â„} {Î¼ Î½ : Measure Î±}
  [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]

namespace MeasureTheory

/-!
### Lp space

The space of equivalence classes of measurable functions for which `snorm f p Î¼ < âˆ`.
-/


@[simp]
theorem snorm_aeeqFun {Î± E : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±} [NormedAddCommGroup E]
    {p : â„â‰¥0âˆ} {f : Î± â†’ E} (hf : AEStronglyMeasurable f Î¼) :
    snorm (AEEqFun.mk f hf) p Î¼ = snorm f p Î¼ :=
  snorm_congr_ae (AEEqFun.coeFn_mk _ _)
#align measure_theory.snorm_ae_eq_fun MeasureTheory.snorm_aeeqFun

theorem Memâ„’p.snorm_mk_lt_top {Î± E : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±}
    [NormedAddCommGroup E] {p : â„â‰¥0âˆ} {f : Î± â†’ E} (hfp : Memâ„’p f p Î¼) :
    snorm (AEEqFun.mk f hfp.1) p Î¼ < âˆ := by simp [hfp.2]
#align measure_theory.mem_â„’p.snorm_mk_lt_top MeasureTheory.Memâ„’p.snorm_mk_lt_top

/-- Lp space -/
def Lp {Î±} (E : Type*) {m : MeasurableSpace Î±} [NormedAddCommGroup E] (p : â„â‰¥0âˆ)
    (Î¼ : Measure Î± := by volume_tac) : AddSubgroup (Î± â†’â‚˜[Î¼] E) where
  carrier := { f | snorm f p Î¼ < âˆ }
  zero_mem' := by simp [snorm_congr_ae AEEqFun.coeFn_zero, snorm_zero]
  add_mem' {f g} hf hg := by
    simp [snorm_congr_ae (AEEqFun.coeFn_add f g),
      snorm_add_lt_top âŸ¨f.aestronglyMeasurable, hfâŸ© âŸ¨g.aestronglyMeasurable, hgâŸ©]
  neg_mem' {f} hf := by rwa [Set.mem_setOf_eq, snorm_congr_ae (AEEqFun.coeFn_neg f), snorm_neg]
#align measure_theory.Lp MeasureTheory.Lp

-- Porting note: calling the first argument `Î±` breaks the `(Î± := Â·)` notation
scoped notation:25 Î±' " â†’â‚[" Î¼ "] " E => MeasureTheory.Lp (Î± := Î±') E 1 Î¼
scoped notation:25 Î±' " â†’â‚‚[" Î¼ "] " E => MeasureTheory.Lp (Î± := Î±') E 2 Î¼

namespace Memâ„’p

/-- make an element of Lp from a function verifying `Memâ„’p` -/
def toLp (f : Î± â†’ E) (h_mem_â„’p : Memâ„’p f p Î¼) : Lp E p Î¼ :=
  âŸ¨AEEqFun.mk f h_mem_â„’p.1, h_mem_â„’p.snorm_mk_lt_topâŸ©
#align measure_theory.mem_â„’p.to_Lp MeasureTheory.Memâ„’p.toLp

theorem coeFn_toLp {f : Î± â†’ E} (hf : Memâ„’p f p Î¼) : hf.toLp f =áµ[Î¼] f :=
  AEEqFun.coeFn_mk _ _
#align measure_theory.mem_â„’p.coe_fn_to_Lp MeasureTheory.Memâ„’p.coeFn_toLp

theorem toLp_congr {f g : Î± â†’ E} (hf : Memâ„’p f p Î¼) (hg : Memâ„’p g p Î¼) (hfg : f =áµ[Î¼] g) :
    hf.toLp f = hg.toLp g := by simp [toLp, hfg]
#align measure_theory.mem_â„’p.to_Lp_congr MeasureTheory.Memâ„’p.toLp_congr

@[simp]
theorem toLp_eq_toLp_iff {f g : Î± â†’ E} (hf : Memâ„’p f p Î¼) (hg : Memâ„’p g p Î¼) :
    hf.toLp f = hg.toLp g â†” f =áµ[Î¼] g := by simp [toLp]
#align measure_theory.mem_â„’p.to_Lp_eq_to_Lp_iff MeasureTheory.Memâ„’p.toLp_eq_toLp_iff

@[simp]
theorem toLp_zero (h : Memâ„’p (0 : Î± â†’ E) p Î¼) : h.toLp 0 = 0 :=
  rfl
#align measure_theory.mem_â„’p.to_Lp_zero MeasureTheory.Memâ„’p.toLp_zero

theorem toLp_add {f g : Î± â†’ E} (hf : Memâ„’p f p Î¼) (hg : Memâ„’p g p Î¼) :
    (hf.add hg).toLp (f + g) = hf.toLp f + hg.toLp g :=
  rfl
#align measure_theory.mem_â„’p.to_Lp_add MeasureTheory.Memâ„’p.toLp_add

theorem toLp_neg {f : Î± â†’ E} (hf : Memâ„’p f p Î¼) : hf.neg.toLp (-f) = -hf.toLp f :=
  rfl
#align measure_theory.mem_â„’p.to_Lp_neg MeasureTheory.Memâ„’p.toLp_neg

theorem toLp_sub {f g : Î± â†’ E} (hf : Memâ„’p f p Î¼) (hg : Memâ„’p g p Î¼) :
    (hf.sub hg).toLp (f - g) = hf.toLp f - hg.toLp g :=
  rfl
#align measure_theory.mem_â„’p.to_Lp_sub MeasureTheory.Memâ„’p.toLp_sub

end Memâ„’p

namespace Lp

instance instCoeFun : CoeFun (Lp E p Î¼) (fun _ => Î± â†’ E) :=
  âŸ¨fun f => ((f : Î± â†’â‚˜[Î¼] E) : Î± â†’ E)âŸ©
#align measure_theory.Lp.has_coe_to_fun MeasureTheory.Lp.instCoeFun

@[ext high]
theorem ext {f g : Lp E p Î¼} (h : f =áµ[Î¼] g) : f = g := by
  cases f
  cases g
  simp only [Subtype.mk_eq_mk]
  exact AEEqFun.ext h
#align measure_theory.Lp.ext MeasureTheory.Lp.ext

theorem ext_iff {f g : Lp E p Î¼} : f = g â†” f =áµ[Î¼] g :=
  âŸ¨fun h => by rw [h], fun h => ext hâŸ©
#align measure_theory.Lp.ext_iff MeasureTheory.Lp.ext_iff

theorem mem_Lp_iff_snorm_lt_top {f : Î± â†’â‚˜[Î¼] E} : f âˆˆ Lp E p Î¼ â†” snorm f p Î¼ < âˆ := Iff.rfl
#align measure_theory.Lp.mem_Lp_iff_snorm_lt_top MeasureTheory.Lp.mem_Lp_iff_snorm_lt_top

theorem mem_Lp_iff_memâ„’p {f : Î± â†’â‚˜[Î¼] E} : f âˆˆ Lp E p Î¼ â†” Memâ„’p f p Î¼ := by
  simp [mem_Lp_iff_snorm_lt_top, Memâ„’p, f.stronglyMeasurable.aestronglyMeasurable]
#align measure_theory.Lp.mem_Lp_iff_mem_â„’p MeasureTheory.Lp.mem_Lp_iff_memâ„’p

protected theorem antitone [IsFiniteMeasure Î¼] {p q : â„â‰¥0âˆ} (hpq : p â‰¤ q) : Lp E q Î¼ â‰¤ Lp E p Î¼ :=
  fun f hf => (Memâ„’p.memâ„’p_of_exponent_le âŸ¨f.aestronglyMeasurable, hfâŸ© hpq).2
#align measure_theory.Lp.antitone MeasureTheory.Lp.antitone

@[simp]
theorem coeFn_mk {f : Î± â†’â‚˜[Î¼] E} (hf : snorm f p Î¼ < âˆ) : ((âŸ¨f, hfâŸ© : Lp E p Î¼) : Î± â†’ E) = f :=
  rfl
#align measure_theory.Lp.coe_fn_mk MeasureTheory.Lp.coeFn_mk

-- @[simp] -- Porting note: dsimp can prove this
theorem coe_mk {f : Î± â†’â‚˜[Î¼] E} (hf : snorm f p Î¼ < âˆ) : ((âŸ¨f, hfâŸ© : Lp E p Î¼) : Î± â†’â‚˜[Î¼] E) = f :=
  rfl
#align measure_theory.Lp.coe_mk MeasureTheory.Lp.coe_mk

@[simp]
theorem toLp_coeFn (f : Lp E p Î¼) (hf : Memâ„’p f p Î¼) : hf.toLp f = f := by
  cases f
  simp [Memâ„’p.toLp]
#align measure_theory.Lp.to_Lp_coe_fn MeasureTheory.Lp.toLp_coeFn

theorem snorm_lt_top (f : Lp E p Î¼) : snorm f p Î¼ < âˆ :=
  f.prop
#align measure_theory.Lp.snorm_lt_top MeasureTheory.Lp.snorm_lt_top

theorem snorm_ne_top (f : Lp E p Î¼) : snorm f p Î¼ â‰  âˆ :=
  (snorm_lt_top f).ne
#align measure_theory.Lp.snorm_ne_top MeasureTheory.Lp.snorm_ne_top

@[measurability]
protected theorem stronglyMeasurable (f : Lp E p Î¼) : StronglyMeasurable f :=
  f.val.stronglyMeasurable
#align measure_theory.Lp.strongly_measurable MeasureTheory.Lp.stronglyMeasurable

@[measurability]
protected theorem aestronglyMeasurable (f : Lp E p Î¼) : AEStronglyMeasurable f Î¼ :=
  f.val.aestronglyMeasurable
#align measure_theory.Lp.ae_strongly_measurable MeasureTheory.Lp.aestronglyMeasurable

protected theorem memâ„’p (f : Lp E p Î¼) : Memâ„’p f p Î¼ :=
  âŸ¨Lp.aestronglyMeasurable f, f.propâŸ©
#align measure_theory.Lp.mem_â„’p MeasureTheory.Lp.memâ„’p

variable (E p Î¼)

theorem coeFn_zero : â‡‘(0 : Lp E p Î¼) =áµ[Î¼] 0 :=
  AEEqFun.coeFn_zero
#align measure_theory.Lp.coe_fn_zero MeasureTheory.Lp.coeFn_zero

variable {E p Î¼}

theorem coeFn_neg (f : Lp E p Î¼) : â‡‘(-f) =áµ[Î¼] -f :=
  AEEqFun.coeFn_neg _
#align measure_theory.Lp.coe_fn_neg MeasureTheory.Lp.coeFn_neg

theorem coeFn_add (f g : Lp E p Î¼) : â‡‘(f + g) =áµ[Î¼] f + g :=
  AEEqFun.coeFn_add _ _
#align measure_theory.Lp.coe_fn_add MeasureTheory.Lp.coeFn_add

theorem coeFn_sub (f g : Lp E p Î¼) : â‡‘(f - g) =áµ[Î¼] f - g :=
  AEEqFun.coeFn_sub _ _
#align measure_theory.Lp.coe_fn_sub MeasureTheory.Lp.coeFn_sub

theorem const_mem_Lp (Î±) {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (c : E) [IsFiniteMeasure Î¼] :
    @AEEqFun.const Î± _ _ Î¼ _ c âˆˆ Lp E p Î¼ :=
  (memâ„’p_const c).snorm_mk_lt_top
#align measure_theory.Lp.mem_Lp_const MeasureTheory.Lp.const_mem_Lp

instance instNorm : Norm (Lp E p Î¼) where norm f := ENNReal.toReal (snorm f p Î¼)
#align measure_theory.Lp.has_norm MeasureTheory.Lp.instNorm

-- note: we need this to be defeq to the instance from `SeminormedAddGroup.toNNNorm`, so
-- can't use `ENNReal.toNNReal (snorm f p Î¼)`
instance instNNNorm : NNNorm (Lp E p Î¼) where nnnorm f := âŸ¨â€–fâ€–, ENNReal.toReal_nonnegâŸ©
#align measure_theory.Lp.has_nnnorm MeasureTheory.Lp.instNNNorm

instance instDist : Dist (Lp E p Î¼) where dist f g := â€–f - gâ€–
#align measure_theory.Lp.has_dist MeasureTheory.Lp.instDist

instance instEDist : EDist (Lp E p Î¼) where edist f g := snorm (â‡‘f - â‡‘g) p Î¼
#align measure_theory.Lp.has_edist MeasureTheory.Lp.instEDist

theorem norm_def (f : Lp E p Î¼) : â€–fâ€– = ENNReal.toReal (snorm f p Î¼) :=
  rfl
#align measure_theory.Lp.norm_def MeasureTheory.Lp.norm_def

theorem nnnorm_def (f : Lp E p Î¼) : â€–fâ€–â‚Š = ENNReal.toNNReal (snorm f p Î¼) :=
  rfl
#align measure_theory.Lp.nnnorm_def MeasureTheory.Lp.nnnorm_def

@[simp, norm_cast]
protected theorem coe_nnnorm (f : Lp E p Î¼) : (â€–fâ€–â‚Š : â„) = â€–fâ€– :=
  rfl
#align measure_theory.Lp.coe_nnnorm MeasureTheory.Lp.coe_nnnorm

@[simp, norm_cast]
theorem nnnorm_coe_ennreal (f : Lp E p Î¼) : (â€–fâ€–â‚Š : â„â‰¥0âˆ) = snorm f p Î¼ :=
  ENNReal.coe_toNNReal <| Lp.snorm_ne_top f

@[simp]
theorem norm_toLp (f : Î± â†’ E) (hf : Memâ„’p f p Î¼) : â€–hf.toLp fâ€– = ENNReal.toReal (snorm f p Î¼) := by
  erw [norm_def, snorm_congr_ae (Memâ„’p.coeFn_toLp hf)]
#align measure_theory.Lp.norm_to_Lp MeasureTheory.Lp.norm_toLp

@[simp]
theorem nnnorm_toLp (f : Î± â†’ E) (hf : Memâ„’p f p Î¼) :
    â€–hf.toLp fâ€–â‚Š = ENNReal.toNNReal (snorm f p Î¼) :=
  NNReal.eq <| norm_toLp f hf
#align measure_theory.Lp.nnnorm_to_Lp MeasureTheory.Lp.nnnorm_toLp

theorem coe_nnnorm_toLp {f : Î± â†’ E} (hf : Memâ„’p f p Î¼) : (â€–hf.toLp fâ€–â‚Š : â„â‰¥0âˆ) = snorm f p Î¼ := by
  rw [nnnorm_toLp f hf, ENNReal.coe_toNNReal hf.2.ne]

theorem dist_def (f g : Lp E p Î¼) : dist f g = (snorm (â‡‘f - â‡‘g) p Î¼).toReal := by
  simp_rw [dist, norm_def]
  refine congr_arg _ ?_
  apply snorm_congr_ae (coeFn_sub _ _)
#align measure_theory.Lp.dist_def MeasureTheory.Lp.dist_def

theorem edist_def (f g : Lp E p Î¼) : edist f g = snorm (â‡‘f - â‡‘g) p Î¼ :=
  rfl
#align measure_theory.Lp.edist_def MeasureTheory.Lp.edist_def

protected theorem edist_dist (f g : Lp E p Î¼) : edist f g = .ofReal (dist f g) := by
  rw [edist_def, dist_def, â† snorm_congr_ae (coeFn_sub _ _),
    ENNReal.ofReal_toReal (snorm_ne_top (f - g))]

protected theorem dist_edist (f g : Lp E p Î¼) : dist f g = (edist f g).toReal :=
  MeasureTheory.Lp.dist_def ..

@[simp]
theorem edist_toLp_toLp (f g : Î± â†’ E) (hf : Memâ„’p f p Î¼) (hg : Memâ„’p g p Î¼) :
    edist (hf.toLp f) (hg.toLp g) = snorm (f - g) p Î¼ := by
  rw [edist_def]
  exact snorm_congr_ae (hf.coeFn_toLp.sub hg.coeFn_toLp)
#align measure_theory.Lp.edist_to_Lp_to_Lp MeasureTheory.Lp.edist_toLp_toLp

@[simp]
theorem edist_toLp_zero (f : Î± â†’ E) (hf : Memâ„’p f p Î¼) : edist (hf.toLp f) 0 = snorm f p Î¼ := by
  convert edist_toLp_toLp f 0 hf zero_memâ„’p
  simp
#align measure_theory.Lp.edist_to_Lp_zero MeasureTheory.Lp.edist_toLp_zero

@[simp]
theorem nnnorm_zero : â€–(0 : Lp E p Î¼)â€–â‚Š = 0 := by
  rw [nnnorm_def]
  change (snorm (â‡‘(0 : Î± â†’â‚˜[Î¼] E)) p Î¼).toNNReal = 0
  simp [snorm_congr_ae AEEqFun.coeFn_zero, snorm_zero]
#align measure_theory.Lp.nnnorm_zero MeasureTheory.Lp.nnnorm_zero

@[simp]
theorem norm_zero : â€–(0 : Lp E p Î¼)â€– = 0 :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) nnnorm_zero
#align measure_theory.Lp.norm_zero MeasureTheory.Lp.norm_zero

@[simp]
theorem norm_measure_zero (f : Lp E p (0 : MeasureTheory.Measure Î±)) : â€–fâ€– = 0 := by
  simp [norm_def]

@[simp] theorem norm_exponent_zero (f : Lp E 0 Î¼) : â€–fâ€– = 0 := by simp [norm_def]

theorem nnnorm_eq_zero_iff {f : Lp E p Î¼} (hp : 0 < p) : â€–fâ€–â‚Š = 0 â†” f = 0 := by
  refine' âŸ¨fun hf => _, fun hf => by simp [hf]âŸ©
  rw [nnnorm_def, ENNReal.toNNReal_eq_zero_iff] at hf
  cases hf with
  | inl hf =>
    rw [snorm_eq_zero_iff (Lp.aestronglyMeasurable f) hp.ne.symm] at hf
    exact Subtype.eq (AEEqFun.ext (hf.trans AEEqFun.coeFn_zero.symm))
  | inr hf =>
    exact absurd hf (snorm_ne_top f)
#align measure_theory.Lp.nnnorm_eq_zero_iff MeasureTheory.Lp.nnnorm_eq_zero_iff

theorem norm_eq_zero_iff {f : Lp E p Î¼} (hp : 0 < p) : â€–fâ€– = 0 â†” f = 0 :=
  Iff.symm <| (nnnorm_eq_zero_iff hp).symm.trans <| (NNReal.coe_eq_zero _).symm
#align measure_theory.Lp.norm_eq_zero_iff MeasureTheory.Lp.norm_eq_zero_iff

theorem eq_zero_iff_ae_eq_zero {f : Lp E p Î¼} : f = 0 â†” f =áµ[Î¼] 0 := by
  rw [â† (Lp.memâ„’p f).toLp_eq_toLp_iff zero_memâ„’p, Memâ„’p.toLp_zero, toLp_coeFn]
#align measure_theory.Lp.eq_zero_iff_ae_eq_zero MeasureTheory.Lp.eq_zero_iff_ae_eq_zero

@[simp]
theorem nnnorm_neg (f : Lp E p Î¼) : â€–-fâ€–â‚Š = â€–fâ€–â‚Š := by
  rw [nnnorm_def, nnnorm_def, snorm_congr_ae (coeFn_neg _), snorm_neg]
#align measure_theory.Lp.nnnorm_neg MeasureTheory.Lp.nnnorm_neg

@[simp]
theorem norm_neg (f : Lp E p Î¼) : â€–-fâ€– = â€–fâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) (nnnorm_neg f)
#align measure_theory.Lp.norm_neg MeasureTheory.Lp.norm_neg

theorem nnnorm_le_mul_nnnorm_of_ae_le_mul {c : â„â‰¥0} {f : Lp E p Î¼} {g : Lp F p Î¼}
    (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€–â‚Š â‰¤ c * â€–g xâ€–â‚Š) : â€–fâ€–â‚Š â‰¤ c * â€–gâ€–â‚Š := by
  simp only [nnnorm_def]
  have := snorm_le_nnreal_smul_snorm_of_ae_le_mul h p
  rwa [â† ENNReal.toNNReal_le_toNNReal, ENNReal.smul_def, smul_eq_mul, ENNReal.toNNReal_mul,
    ENNReal.toNNReal_coe] at this
  Â· exact (Lp.memâ„’p _).snorm_ne_top
  Â· exact ENNReal.mul_ne_top ENNReal.coe_ne_top (Lp.memâ„’p _).snorm_ne_top
#align measure_theory.Lp.nnnorm_le_mul_nnnorm_of_ae_le_mul MeasureTheory.Lp.nnnorm_le_mul_nnnorm_of_ae_le_mul

theorem norm_le_mul_norm_of_ae_le_mul {c : â„} {f : Lp E p Î¼} {g : Lp F p Î¼}
    (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ c * â€–g xâ€–) : â€–fâ€– â‰¤ c * â€–gâ€– := by
  cases' le_or_lt 0 c with hc hc
  Â· lift c to â„â‰¥0 using hc
    exact NNReal.coe_le_coe.mpr (nnnorm_le_mul_nnnorm_of_ae_le_mul h)
  Â· simp only [norm_def]
    have := snorm_eq_zero_and_zero_of_ae_le_mul_neg h hc p
    simp [this]
#align measure_theory.Lp.norm_le_mul_norm_of_ae_le_mul MeasureTheory.Lp.norm_le_mul_norm_of_ae_le_mul

theorem norm_le_norm_of_ae_le {f : Lp E p Î¼} {g : Lp F p Î¼} (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ â€–g xâ€–) :
    â€–fâ€– â‰¤ â€–gâ€– := by
  rw [norm_def, norm_def, ENNReal.toReal_le_toReal (snorm_ne_top _) (snorm_ne_top _)]
  exact snorm_mono_ae h
#align measure_theory.Lp.norm_le_norm_of_ae_le MeasureTheory.Lp.norm_le_norm_of_ae_le

theorem mem_Lp_of_nnnorm_ae_le_mul {c : â„â‰¥0} {f : Î± â†’â‚˜[Î¼] E} {g : Lp F p Î¼}
    (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€–â‚Š â‰¤ c * â€–g xâ€–â‚Š) : f âˆˆ Lp E p Î¼ :=
  mem_Lp_iff_memâ„’p.2 <| Memâ„’p.of_nnnorm_le_mul (Lp.memâ„’p g) f.aestronglyMeasurable h
#align measure_theory.Lp.mem_Lp_of_nnnorm_ae_le_mul MeasureTheory.Lp.mem_Lp_of_nnnorm_ae_le_mul

theorem mem_Lp_of_ae_le_mul {c : â„} {f : Î± â†’â‚˜[Î¼] E} {g : Lp F p Î¼}
    (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ c * â€–g xâ€–) : f âˆˆ Lp E p Î¼ :=
  mem_Lp_iff_memâ„’p.2 <| Memâ„’p.of_le_mul (Lp.memâ„’p g) f.aestronglyMeasurable h
#align measure_theory.Lp.mem_Lp_of_ae_le_mul MeasureTheory.Lp.mem_Lp_of_ae_le_mul

theorem mem_Lp_of_nnnorm_ae_le {f : Î± â†’â‚˜[Î¼] E} {g : Lp F p Î¼} (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€–â‚Š â‰¤ â€–g xâ€–â‚Š) :
    f âˆˆ Lp E p Î¼ :=
  mem_Lp_iff_memâ„’p.2 <| Memâ„’p.of_le (Lp.memâ„’p g) f.aestronglyMeasurable h
#align measure_theory.Lp.mem_Lp_of_nnnorm_ae_le MeasureTheory.Lp.mem_Lp_of_nnnorm_ae_le

theorem mem_Lp_of_ae_le {f : Î± â†’â‚˜[Î¼] E} {g : Lp F p Î¼} (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ â€–g xâ€–) :
    f âˆˆ Lp E p Î¼ :=
  mem_Lp_of_nnnorm_ae_le h
#align measure_theory.Lp.mem_Lp_of_ae_le MeasureTheory.Lp.mem_Lp_of_ae_le

theorem mem_Lp_of_ae_nnnorm_bound [IsFiniteMeasure Î¼] {f : Î± â†’â‚˜[Î¼] E} (C : â„â‰¥0)
    (hfC : âˆ€áµ x âˆ‚Î¼, â€–f xâ€–â‚Š â‰¤ C) : f âˆˆ Lp E p Î¼ :=
  mem_Lp_iff_memâ„’p.2 <| Memâ„’p.of_bound f.aestronglyMeasurable _ hfC
#align measure_theory.Lp.mem_Lp_of_ae_nnnorm_bound MeasureTheory.Lp.mem_Lp_of_ae_nnnorm_bound

theorem mem_Lp_of_ae_bound [IsFiniteMeasure Î¼] {f : Î± â†’â‚˜[Î¼] E} (C : â„) (hfC : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ C) :
    f âˆˆ Lp E p Î¼ :=
  mem_Lp_iff_memâ„’p.2 <| Memâ„’p.of_bound f.aestronglyMeasurable _ hfC
#align measure_theory.Lp.mem_Lp_of_ae_bound MeasureTheory.Lp.mem_Lp_of_ae_bound

theorem nnnorm_le_of_ae_bound [IsFiniteMeasure Î¼] {f : Lp E p Î¼} {C : â„â‰¥0}
    (hfC : âˆ€áµ x âˆ‚Î¼, â€–f xâ€–â‚Š â‰¤ C) : â€–fâ€–â‚Š â‰¤ measureUnivNNReal Î¼ ^ p.toRealâ»Â¹ * C := by
  by_cases hÎ¼ : Î¼ = 0
  Â· by_cases hp : p.toRealâ»Â¹ = 0
    Â· simp [hp, hÎ¼, nnnorm_def]
    Â· simp [hÎ¼, nnnorm_def, Real.zero_rpow hp]
  rw [â† ENNReal.coe_le_coe, nnnorm_def, ENNReal.coe_toNNReal (snorm_ne_top _)]
  refine' (snorm_le_of_ae_nnnorm_bound hfC).trans_eq _
  rw [â† coe_measureUnivNNReal Î¼, ENNReal.coe_rpow_of_ne_zero (measureUnivNNReal_pos hÎ¼).ne',
    ENNReal.coe_mul, mul_comm, ENNReal.smul_def, smul_eq_mul]
#align measure_theory.Lp.nnnorm_le_of_ae_bound MeasureTheory.Lp.nnnorm_le_of_ae_bound

theorem norm_le_of_ae_bound [IsFiniteMeasure Î¼] {f : Lp E p Î¼} {C : â„} (hC : 0 â‰¤ C)
    (hfC : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ C) : â€–fâ€– â‰¤ measureUnivNNReal Î¼ ^ p.toRealâ»Â¹ * C := by
  lift C to â„â‰¥0 using hC
  have := nnnorm_le_of_ae_bound hfC
  rwa [â† NNReal.coe_le_coe, NNReal.coe_mul, NNReal.coe_rpow] at this
#align measure_theory.Lp.norm_le_of_ae_bound MeasureTheory.Lp.norm_le_of_ae_bound

instance instNormedAddCommGroup [hp : Fact (1 â‰¤ p)] : NormedAddCommGroup (Lp E p Î¼) :=
  { AddGroupNorm.toNormedAddCommGroup
      { toFun := (norm : Lp E p Î¼ â†’ â„)
        map_zero' := norm_zero
        neg' := by simp
        add_le' := fun f g => by
          suffices (â€–f + gâ€–â‚Š : â„â‰¥0âˆ) â‰¤ â€–fâ€–â‚Š + â€–gâ€–â‚Š by exact_mod_cast this
          simp only [Lp.nnnorm_coe_ennreal]
          exact (snorm_congr_ae (AEEqFun.coeFn_add _ _)).trans_le
            (snorm_add_le (Lp.aestronglyMeasurable _) (Lp.aestronglyMeasurable _) hp.out)
        eq_zero_of_map_eq_zero' := fun f =>
          (norm_eq_zero_iff <| zero_lt_one.trans_le hp.1).1 } with
    edist := edist
    edist_dist := Lp.edist_dist }
#align measure_theory.Lp.normed_add_comm_group MeasureTheory.Lp.instNormedAddCommGroup

-- check no diamond is created
example [Fact (1 â‰¤ p)] : PseudoEMetricSpace.toEDist = (Lp.instEDist : EDist (Lp E p Î¼)) :=
  rfl

example [Fact (1 â‰¤ p)] : SeminormedAddGroup.toNNNorm = (Lp.instNNNorm : NNNorm (Lp E p Î¼)) :=
  rfl

section BoundedSMul

variable {ğ•œ ğ•œ' : Type*}

variable [NormedRing ğ•œ] [NormedRing ğ•œ'] [Module ğ•œ E] [Module ğ•œ' E]

variable [BoundedSMul ğ•œ E] [BoundedSMul ğ•œ' E]

theorem const_smul_mem_Lp (c : ğ•œ) (f : Lp E p Î¼) : c â€¢ (f : Î± â†’â‚˜[Î¼] E) âˆˆ Lp E p Î¼ := by
  rw [mem_Lp_iff_snorm_lt_top, snorm_congr_ae (AEEqFun.coeFn_smul _ _)]
  refine' (snorm_const_smul_le _ _).trans_lt _
  rw [ENNReal.smul_def, smul_eq_mul, ENNReal.mul_lt_top_iff]
  exact Or.inl âŸ¨ENNReal.coe_lt_top, f.propâŸ©
#align measure_theory.Lp.mem_Lp_const_smul MeasureTheory.Lp.const_smul_mem_Lp

variable (E p Î¼ ğ•œ)

/-- The `ğ•œ`-submodule of elements of `Î± â†’â‚˜[Î¼] E` whose `Lp` norm is finite.  This is `Lp E p Î¼`,
with extra structure. -/
def LpSubmodule : Submodule ğ•œ (Î± â†’â‚˜[Î¼] E) :=
  { Lp E p Î¼ with smul_mem' := fun c f hf => by simpa using const_smul_mem_Lp c âŸ¨f, hfâŸ© }
#align measure_theory.Lp.Lp_submodule MeasureTheory.Lp.LpSubmodule

variable {E p Î¼ ğ•œ}

theorem coe_LpSubmodule : (LpSubmodule E p Î¼ ğ•œ).toAddSubgroup = Lp E p Î¼ :=
  rfl
#align measure_theory.Lp.coe_Lp_submodule MeasureTheory.Lp.coe_LpSubmodule

instance instModule : Module ğ•œ (Lp E p Î¼) :=
  { (LpSubmodule E p Î¼ ğ•œ).module with }
#align measure_theory.Lp.module MeasureTheory.Lp.instModule

theorem coeFn_smul (c : ğ•œ) (f : Lp E p Î¼) : â‡‘(c â€¢ f) =áµ[Î¼] c â€¢ â‡‘f :=
  AEEqFun.coeFn_smul _ _
#align measure_theory.Lp.coe_fn_smul MeasureTheory.Lp.coeFn_smul

instance instIsCentralScalar [Module ğ•œáµáµ’áµ– E] [BoundedSMul ğ•œáµáµ’áµ– E] [IsCentralScalar ğ•œ E] :
    IsCentralScalar ğ•œ (Lp E p Î¼) where
  op_smul_eq_smul k f := Subtype.ext <| op_smul_eq_smul k (f : Î± â†’â‚˜[Î¼] E)
#align measure_theory.Lp.is_central_scalar MeasureTheory.Lp.instIsCentralScalar

instance instSMulCommClass [SMulCommClass ğ•œ ğ•œ' E] : SMulCommClass ğ•œ ğ•œ' (Lp E p Î¼) where
  smul_comm k k' f := Subtype.ext <| smul_comm k k' (f : Î± â†’â‚˜[Î¼] E)
#align measure_theory.Lp.smul_comm_class MeasureTheory.Lp.instSMulCommClass

instance instIsScalarTower [SMul ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' E] : IsScalarTower ğ•œ ğ•œ' (Lp E p Î¼) where
  smul_assoc k k' f := Subtype.ext <| smul_assoc k k' (f : Î± â†’â‚˜[Î¼] E)

instance instBoundedSMul [Fact (1 â‰¤ p)] : BoundedSMul ğ•œ (Lp E p Î¼) :=
  -- TODO: add `BoundedSMul.of_nnnorm_smul_le`
  BoundedSMul.of_norm_smul_le fun r f => by
    suffices (â€–r â€¢ fâ€–â‚Š : â„â‰¥0âˆ) â‰¤ â€–râ€–â‚Š * â€–fâ€–â‚Š by exact_mod_cast this
    rw [nnnorm_def, nnnorm_def, ENNReal.coe_toNNReal (Lp.snorm_ne_top _),
      snorm_congr_ae (coeFn_smul _ _), ENNReal.coe_toNNReal (Lp.snorm_ne_top _)]
    exact snorm_const_smul_le r f
#align measure_theory.Lp.has_bounded_smul MeasureTheory.Lp.instBoundedSMul

end BoundedSMul

section NormedSpace

variable {ğ•œ : Type*} [NormedField ğ•œ] [NormedSpace ğ•œ E]

instance instNormedSpace [Fact (1 â‰¤ p)] : NormedSpace ğ•œ (Lp E p Î¼) where
  norm_smul_le _ _ := norm_smul_le _ _
#align measure_theory.Lp.normed_space MeasureTheory.Lp.instNormedSpace

end NormedSpace

end Lp

namespace Memâ„’p

variable {ğ•œ : Type*} [NormedRing ğ•œ] [Module ğ•œ E] [BoundedSMul ğ•œ E]

theorem toLp_const_smul {f : Î± â†’ E} (c : ğ•œ) (hf : Memâ„’p f p Î¼) :
    (hf.const_smul c).toLp (c â€¢ f) = c â€¢ hf.toLp f :=
  rfl
#align measure_theory.mem_â„’p.to_Lp_const_smul MeasureTheory.Memâ„’p.toLp_const_smul

end Memâ„’p

/-! ### Indicator of a set as an element of Láµ–

For a set `s` with `(hs : MeasurableSet s)` and `(hÎ¼s : Î¼ s < âˆ)`, we build
`indicatorConstLp p hs hÎ¼s c`, the element of `Lp` corresponding to `s.indicator (fun _ => c)`.
-/


section Indicator

set_option autoImplicit true

variable {c : E} {f : Î± â†’ E} {hf : AEStronglyMeasurable f Î¼}

theorem snormEssSup_indicator_le (s : Set Î±) (f : Î± â†’ G) :
    snormEssSup (s.indicator f) Î¼ â‰¤ snormEssSup f Î¼ := by
  refine' essSup_mono_ae (eventually_of_forall fun x => _)
  rw [ENNReal.coe_le_coe, nnnorm_indicator_eq_indicator_nnnorm]
  exact Set.indicator_le_self s _ x
#align measure_theory.snorm_ess_sup_indicator_le MeasureTheory.snormEssSup_indicator_le

theorem snormEssSup_indicator_const_le (s : Set Î±) (c : G) :
    snormEssSup (s.indicator fun _ : Î± => c) Î¼ â‰¤ â€–câ€–â‚Š := by
  by_cases hÎ¼0 : Î¼ = 0
  Â· rw [hÎ¼0, snormEssSup_measure_zero]
    exact zero_le _
  Â· exact (snormEssSup_indicator_le s fun _ => c).trans (snormEssSup_const c hÎ¼0).le
#align measure_theory.snorm_ess_sup_indicator_const_le MeasureTheory.snormEssSup_indicator_const_le

theorem snormEssSup_indicator_const_eq (s : Set Î±) (c : G) (hÎ¼s : Î¼ s â‰  0) :
    snormEssSup (s.indicator fun _ : Î± => c) Î¼ = â€–câ€–â‚Š := by
  refine' le_antisymm (snormEssSup_indicator_const_le s c) _
  by_contra' h
  have h' := ae_iff.mp (ae_lt_of_essSup_lt h)
  push_neg at h'
  refine' hÎ¼s (measure_mono_null (fun x hx_mem => _) h')
  rw [Set.mem_setOf_eq, Set.indicator_of_mem hx_mem]
#align measure_theory.snorm_ess_sup_indicator_const_eq MeasureTheory.snormEssSup_indicator_const_eq

theorem snorm_indicator_le (f : Î± â†’ E) {s : Set Î±} :
    snorm (s.indicator f) p Î¼ â‰¤ snorm f p Î¼ := by
  refine' snorm_mono_ae (eventually_of_forall fun x => _)
  suffices â€–s.indicator f xâ€–â‚Š â‰¤ â€–f xâ€–â‚Š by exact NNReal.coe_mono this
  rw [nnnorm_indicator_eq_indicator_nnnorm]
  exact s.indicator_le_self _ x
#align measure_theory.snorm_indicator_le MeasureTheory.snorm_indicator_le

theorem snorm_indicator_constâ‚€ {c : G} (hs : NullMeasurableSet s Î¼) (hp : p â‰  0) (hp_top : p â‰  âˆ) :
    snorm (s.indicator fun _ => c) p Î¼ = â€–câ€–â‚Š * Î¼ s ^ (1 / p.toReal) :=
  have hp_pos : 0 < p.toReal := ENNReal.toReal_pos hp hp_top
  calc
    snorm (s.indicator fun _ => c) p Î¼
      = (âˆ«â» x, ((â€–(s.indicator fun _ â†¦ c) xâ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal) âˆ‚Î¼) ^ (1 / p.toReal) :=
          snorm_eq_lintegral_rpow_nnnorm hp hp_top
    _ = (âˆ«â» x, (s.indicator fun _ â†¦ (â€–câ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal) x âˆ‚Î¼) ^ (1 / p.toReal) := by
      congr 2
      refine (Set.comp_indicator_const c (fun x : G â†¦ (â€–xâ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal) ?_)
      simp [hp_pos]
    _ = â€–câ€–â‚Š * Î¼ s ^ (1 / p.toReal) := by
      rw [lintegral_indicator_constâ‚€ hs, ENNReal.mul_rpow_of_nonneg, â† ENNReal.rpow_mul,
        mul_one_div_cancel hp_pos.ne', ENNReal.rpow_one]
      positivity

theorem snorm_indicator_const {c : G} (hs : MeasurableSet s) (hp : p â‰  0) (hp_top : p â‰  âˆ) :
    snorm (s.indicator fun _ => c) p Î¼ = â€–câ€–â‚Š * Î¼ s ^ (1 / p.toReal) :=
  snorm_indicator_constâ‚€ hs.nullMeasurableSet hp hp_top
#align measure_theory.snorm_indicator_const MeasureTheory.snorm_indicator_const

theorem snorm_indicator_const' {c : G} (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  0) (hp : p â‰  0) :
    snorm (s.indicator fun _ => c) p Î¼ = â€–câ€–â‚Š * Î¼ s ^ (1 / p.toReal) := by
  by_cases hp_top : p = âˆ
  Â· simp [hp_top, snormEssSup_indicator_const_eq s c hÎ¼s]
  Â· exact snorm_indicator_const hs hp hp_top
#align measure_theory.snorm_indicator_const' MeasureTheory.snorm_indicator_const'

theorem snorm_indicator_const_le (c : G) (p : â„â‰¥0âˆ) :
    snorm (s.indicator fun _ => c) p Î¼ â‰¤ â€–câ€–â‚Š * Î¼ s ^ (1 / p.toReal) := by
  rcases eq_or_ne p 0 with (rfl | hp)
  Â· simp only [snorm_exponent_zero, zero_le']
  rcases eq_or_ne p âˆ with (rfl | h'p)
  Â· simp only [snorm_exponent_top, ENNReal.top_toReal, _root_.div_zero, ENNReal.rpow_zero, mul_one]
    exact snormEssSup_indicator_const_le _ _
  let t := toMeasurable Î¼ s
  calc
    snorm (s.indicator fun _ => c) p Î¼ â‰¤ snorm (t.indicator fun _ => c) p Î¼ :=
      snorm_mono (norm_indicator_le_of_subset (subset_toMeasurable _ _) _)
    _ = â€–câ€–â‚Š * Î¼ t ^ (1 / p.toReal) :=
      (snorm_indicator_const (measurableSet_toMeasurable _ _) hp h'p)
    _ = â€–câ€–â‚Š * Î¼ s ^ (1 / p.toReal) := by rw [measure_toMeasurable]
#align measure_theory.snorm_indicator_const_le MeasureTheory.snorm_indicator_const_le

theorem Memâ„’p.indicator (hs : MeasurableSet s) (hf : Memâ„’p f p Î¼) : Memâ„’p (s.indicator f) p Î¼ :=
  âŸ¨hf.aestronglyMeasurable.indicator hs, lt_of_le_of_lt (snorm_indicator_le f) hf.snorm_lt_topâŸ©
#align measure_theory.mem_â„’p.indicator MeasureTheory.Memâ„’p.indicator

theorem snormEssSup_indicator_eq_snormEssSup_restrict {f : Î± â†’ F} (hs : MeasurableSet s) :
    snormEssSup (s.indicator f) Î¼ = snormEssSup f (Î¼.restrict s) := by
  simp_rw [snormEssSup, nnnorm_indicator_eq_indicator_nnnorm, ENNReal.coe_indicator]
  by_cases hs_null : Î¼ s = 0
  Â· rw [Measure.restrict_zero_set hs_null]
    simp only [essSup_measure_zero, ENNReal.essSup_eq_zero_iff, ENNReal.bot_eq_zero]
    have hs_empty : s =áµ[Î¼] (âˆ… : Set Î±) := by rw [ae_eq_set]; simpa using hs_null
    refine' (indicator_ae_eq_of_ae_eq_set hs_empty).trans _
    rw [Set.indicator_empty]
    rfl
  rw [essSup_indicator_eq_essSup_restrict (eventually_of_forall fun x => ?_) hs hs_null]
  rw [Pi.zero_apply]
  exact zero_le _
#align measure_theory.snorm_ess_sup_indicator_eq_snorm_ess_sup_restrict MeasureTheory.snormEssSup_indicator_eq_snormEssSup_restrict

theorem snorm_indicator_eq_snorm_restrict {f : Î± â†’ F} (hs : MeasurableSet s) :
    snorm (s.indicator f) p Î¼ = snorm f p (Î¼.restrict s) := by
  by_cases hp_zero : p = 0
  Â· simp only [hp_zero, snorm_exponent_zero]
  by_cases hp_top : p = âˆ
  Â· simp_rw [hp_top, snorm_exponent_top]
    exact snormEssSup_indicator_eq_snormEssSup_restrict hs
  simp_rw [snorm_eq_lintegral_rpow_nnnorm hp_zero hp_top]
  suffices (âˆ«â» x, (â€–s.indicator f xâ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal âˆ‚Î¼) =
      âˆ«â» x in s, (â€–f xâ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal âˆ‚Î¼ by rw [this]
  rw [â† lintegral_indicator _ hs]
  congr
  simp_rw [nnnorm_indicator_eq_indicator_nnnorm, ENNReal.coe_indicator]
  have h_zero : (fun x => x ^ p.toReal) (0 : â„â‰¥0âˆ) = 0 := by
    simp [ENNReal.toReal_pos hp_zero hp_top]
  -- Porting note: The implicit argument should be specified because the elaborator can't deal with
  --               `âˆ˜` well.
  exact (Set.indicator_comp_of_zero (g := fun x : â„â‰¥0âˆ => x ^ p.toReal) h_zero).symm
#align measure_theory.snorm_indicator_eq_snorm_restrict MeasureTheory.snorm_indicator_eq_snorm_restrict

theorem memâ„’p_indicator_iff_restrict (hs : MeasurableSet s) :
    Memâ„’p (s.indicator f) p Î¼ â†” Memâ„’p f p (Î¼.restrict s) := by
  simp [Memâ„’p, aestronglyMeasurable_indicator_iff hs, snorm_indicator_eq_snorm_restrict hs]
#align measure_theory.mem_â„’p_indicator_iff_restrict MeasureTheory.memâ„’p_indicator_iff_restrict

/-- If a function is supported on a finite-measure set and belongs to `â„’^p`, then it belongs to
`â„’^q` for any `q â‰¤ p`. -/
theorem Memâ„’p.memâ„’p_of_exponent_le_of_measure_support_ne_top
    {p q : â„â‰¥0âˆ} {f : Î± â†’ E} (hfq : Memâ„’p f q Î¼) {s : Set Î±} (hf : âˆ€ x, x âˆ‰ s â†’ f x = 0)
    (hs : Î¼ s â‰  âˆ) (hpq : p â‰¤ q) : Memâ„’p f p Î¼ := by
  have : (toMeasurable Î¼ s).indicator f = f := by
    apply Set.indicator_eq_self.2
    apply Function.support_subset_iff'.2 (fun x hx â†¦ hf x ?_)
    contrapose! hx
    exact subset_toMeasurable Î¼ s hx
  rw [â† this, memâ„’p_indicator_iff_restrict (measurableSet_toMeasurable Î¼ s)] at hfq âŠ¢
  have : Fact (Î¼ (toMeasurable Î¼ s) < âˆ) := âŸ¨by simpa [lt_top_iff_ne_top] using hsâŸ©
  exact memâ„’p_of_exponent_le hfq hpq

theorem memâ„’p_indicator_const (p : â„â‰¥0âˆ) (hs : MeasurableSet s) (c : E) (hÎ¼sc : c = 0 âˆ¨ Î¼ s â‰  âˆ) :
    Memâ„’p (s.indicator fun _ => c) p Î¼ := by
  rw [memâ„’p_indicator_iff_restrict hs]
  rcases hÎ¼sc with rfl | hÎ¼
  Â· exact zero_memâ„’p
  Â· have := Fact.mk hÎ¼.lt_top
    apply memâ„’p_const
#align measure_theory.mem_â„’p_indicator_const MeasureTheory.memâ„’p_indicator_const

/-- The `â„’^p` norm of the indicator of a set is uniformly small if the set itself has small measure,
for any `p < âˆ`. Given here as an existential `âˆ€ Îµ > 0, âˆƒ Î· > 0, ...` to avoid later
management of `â„â‰¥0âˆ`-arithmetic. -/
theorem exists_snorm_indicator_le (hp : p â‰  âˆ) (c : E) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ âˆ€ s : Set Î±, Î¼ s â‰¤ Î· â†’ snorm (s.indicator fun _ => c) p Î¼ â‰¤ Îµ := by
  rcases eq_or_ne p 0 with (rfl | h'p)
  Â· exact âŸ¨1, zero_lt_one, fun s _ => by simpâŸ©
  have hpâ‚€ : 0 < p := bot_lt_iff_ne_bot.2 h'p
  have hpâ‚€' : 0 â‰¤ 1 / p.toReal := div_nonneg zero_le_one ENNReal.toReal_nonneg
  have hpâ‚€'' : 0 < p.toReal := ENNReal.toReal_pos hpâ‚€.ne' hp
  obtain âŸ¨Î·, hÎ·_pos, hÎ·_leâŸ© :
      âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ (â€–câ€–â‚Š : â„â‰¥0âˆ) * (Î· : â„â‰¥0âˆ) ^ (1 / p.toReal) â‰¤ Îµ := by
    have :
      Filter.Tendsto (fun x : â„â‰¥0 => ((â€–câ€–â‚Š * x ^ (1 / p.toReal) : â„â‰¥0) : â„â‰¥0âˆ)) (ğ“ 0)
        (ğ“ (0 : â„â‰¥0)) := by
      rw [ENNReal.tendsto_coe]
      convert (NNReal.continuousAt_rpow_const (Or.inr hpâ‚€')).tendsto.const_mul _
      simp [hpâ‚€''.ne']
    have hÎµ' : 0 < Îµ := hÎµ.bot_lt
    obtain âŸ¨Î´, hÎ´, hÎ´Îµ'âŸ© :=
      NNReal.nhds_zero_basis.eventually_iff.mp (eventually_le_of_tendsto_lt hÎµ' this)
    obtain âŸ¨Î·, hÎ·, hÎ·Î´âŸ© := exists_between hÎ´
    refine' âŸ¨Î·, hÎ·, _âŸ©
    rw [ENNReal.coe_rpow_of_nonneg _ hpâ‚€', â† ENNReal.coe_mul]
    exact hÎ´Îµ' hÎ·Î´
  refine' âŸ¨Î·, hÎ·_pos, fun s hs => _âŸ©
  refine' (snorm_indicator_const_le _ _).trans (le_trans _ hÎ·_le)
  exact mul_le_mul_left' (ENNReal.rpow_le_rpow hs hpâ‚€') _
#align measure_theory.exists_snorm_indicator_le MeasureTheory.exists_snorm_indicator_le

end Indicator

section IndicatorConstLp

open Set Function

variable {s : Set Î±} {hs : MeasurableSet s} {hÎ¼s : Î¼ s â‰  âˆ} {c : E}

/-- Indicator of a set as an element of `Lp`. -/
def indicatorConstLp (p : â„â‰¥0âˆ) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) : Lp E p Î¼ :=
  Memâ„’p.toLp (s.indicator fun _ => c) (memâ„’p_indicator_const p hs c (Or.inr hÎ¼s))
#align measure_theory.indicator_const_Lp MeasureTheory.indicatorConstLp

theorem indicatorConstLp_coeFn : â‡‘(indicatorConstLp p hs hÎ¼s c) =áµ[Î¼] s.indicator fun _ => c :=
  Memâ„’p.coeFn_toLp (memâ„’p_indicator_const p hs c (Or.inr hÎ¼s))
#align measure_theory.indicator_const_Lp_coe_fn MeasureTheory.indicatorConstLp_coeFn

theorem indicatorConstLp_coeFn_mem : âˆ€áµ x : Î± âˆ‚Î¼, x âˆˆ s â†’ indicatorConstLp p hs hÎ¼s c x = c :=
  indicatorConstLp_coeFn.mono fun _x hx hxs => hx.trans (Set.indicator_of_mem hxs _)
#align measure_theory.indicator_const_Lp_coe_fn_mem MeasureTheory.indicatorConstLp_coeFn_mem

theorem indicatorConstLp_coeFn_nmem : âˆ€áµ x : Î± âˆ‚Î¼, x âˆ‰ s â†’ indicatorConstLp p hs hÎ¼s c x = 0 :=
  indicatorConstLp_coeFn.mono fun _x hx hxs => hx.trans (Set.indicator_of_not_mem hxs _)
#align measure_theory.indicator_const_Lp_coe_fn_nmem MeasureTheory.indicatorConstLp_coeFn_nmem

theorem norm_indicatorConstLp (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ) :
    â€–indicatorConstLp p hs hÎ¼s câ€– = â€–câ€– * (Î¼ s).toReal ^ (1 / p.toReal) := by
  rw [Lp.norm_def, snorm_congr_ae indicatorConstLp_coeFn,
    snorm_indicator_const hs hp_ne_zero hp_ne_top, ENNReal.toReal_mul, ENNReal.toReal_rpow,
    ENNReal.coe_toReal, coe_nnnorm]
#align measure_theory.norm_indicator_const_Lp MeasureTheory.norm_indicatorConstLp

theorem norm_indicatorConstLp_top (hÎ¼s_ne_zero : Î¼ s â‰  0) :
    â€–indicatorConstLp âˆ hs hÎ¼s câ€– = â€–câ€– := by
  rw [Lp.norm_def, snorm_congr_ae indicatorConstLp_coeFn,
    snorm_indicator_const' hs hÎ¼s_ne_zero ENNReal.top_ne_zero, ENNReal.top_toReal, _root_.div_zero,
    ENNReal.rpow_zero, mul_one, ENNReal.coe_toReal, coe_nnnorm]
#align measure_theory.norm_indicator_const_Lp_top MeasureTheory.norm_indicatorConstLp_top

theorem norm_indicatorConstLp' (hp_pos : p â‰  0) (hÎ¼s_pos : Î¼ s â‰  0) :
    â€–indicatorConstLp p hs hÎ¼s câ€– = â€–câ€– * (Î¼ s).toReal ^ (1 / p.toReal) := by
  by_cases hp_top : p = âˆ
  Â· rw [hp_top, ENNReal.top_toReal, _root_.div_zero, Real.rpow_zero, mul_one]
    exact norm_indicatorConstLp_top hÎ¼s_pos
  Â· exact norm_indicatorConstLp hp_pos hp_top
#align measure_theory.norm_indicator_const_Lp' MeasureTheory.norm_indicatorConstLp'

theorem norm_indicatorConstLp_le :
    â€–indicatorConstLp p hs hÎ¼s câ€– â‰¤ â€–câ€– * (Î¼ s).toReal ^ (1 / p.toReal) := by
  rw [indicatorConstLp, Lp.norm_toLp]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) ?_
  refine (snorm_indicator_const_le _ _).trans_eq ?_
  rw [â† coe_nnnorm, ENNReal.ofReal_mul (NNReal.coe_nonneg _), ENNReal.ofReal_coe_nnreal,
    ENNReal.toReal_rpow, ENNReal.ofReal_toReal]
  exact ENNReal.rpow_ne_top_of_nonneg (by positivity) hÎ¼s

theorem edist_indicatorConstLp_eq_nnnorm {t : Set Î±} (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) :
    edist (indicatorConstLp p hs hÎ¼s c) (indicatorConstLp p ht hÎ¼t c) =
      â€–indicatorConstLp p (hs.symmDiff ht) (measure_symmDiff_ne_top hÎ¼s hÎ¼t) câ€–â‚Š := by
  unfold indicatorConstLp
  rw [Lp.edist_toLp_toLp, snorm_indicator_sub_indicator, Lp.coe_nnnorm_toLp]

theorem dist_indicatorConstLp_eq_norm {t : Set Î±} (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) :
    dist (indicatorConstLp p hs hÎ¼s c) (indicatorConstLp p ht hÎ¼t c) =
      â€–indicatorConstLp p (hs.symmDiff ht) (measure_symmDiff_ne_top hÎ¼s hÎ¼t) câ€– := by
  rw [Lp.dist_edist, edist_indicatorConstLp_eq_nnnorm, ENNReal.coe_toReal, Lp.coe_nnnorm]

@[simp]
theorem indicatorConstLp_empty :
    indicatorConstLp p MeasurableSet.empty (by simp : Î¼ âˆ… â‰  âˆ) c = 0 := by
  rw [Lp.eq_zero_iff_ae_eq_zero]
  convert indicatorConstLp_coeFn (E := E)
  simp [Set.indicator_empty', Pi.zero_def]
#align measure_theory.indicator_const_empty MeasureTheory.indicatorConstLp_empty

theorem memâ„’p_add_of_disjoint {f g : Î± â†’ E} (h : Disjoint (support f) (support g))
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    Memâ„’p (f + g) p Î¼ â†” Memâ„’p f p Î¼ âˆ§ Memâ„’p g p Î¼ := by
  borelize E
  refine' âŸ¨fun hfg => âŸ¨_, _âŸ©, fun h => h.1.add h.2âŸ©
  Â· rw [â† Set.indicator_add_eq_left h]; exact hfg.indicator (measurableSet_support hf.measurable)
  Â· rw [â† Set.indicator_add_eq_right h]; exact hfg.indicator (measurableSet_support hg.measurable)
#align measure_theory.mem_â„’p_add_of_disjoint MeasureTheory.memâ„’p_add_of_disjoint

/-- The indicator of a disjoint union of two sets is the sum of the indicators of the sets. -/
theorem indicatorConstLp_disjoint_union {s t : Set Î±} (hs : MeasurableSet s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (hst : s âˆ© t = âˆ…) (c : E) :
    indicatorConstLp p (hs.union ht) (measure_union_ne_top hÎ¼s hÎ¼t) c =
      indicatorConstLp p hs hÎ¼s c + indicatorConstLp p ht hÎ¼t c := by
  ext1
  refine' indicatorConstLp_coeFn.trans (EventuallyEq.trans _ (Lp.coeFn_add _ _).symm)
  refine'
    EventuallyEq.trans _
      (EventuallyEq.add indicatorConstLp_coeFn.symm indicatorConstLp_coeFn.symm)
  rw [Set.indicator_union_of_disjoint (Set.disjoint_iff_inter_eq_empty.mpr hst) _]
#align measure_theory.indicator_const_Lp_disjoint_union MeasureTheory.indicatorConstLp_disjoint_union

end IndicatorConstLp

section const

variable (Î¼ p)
variable [IsFiniteMeasure Î¼] (c : E)

/-- Constant function as an element of `MeasureTheory.Lp` for a finite measure. -/
protected def Lp.const : E â†’+ Lp E p Î¼ where
  toFun c := âŸ¨AEEqFun.const Î± c, const_mem_Lp Î± Î¼ câŸ©
  map_zero' := rfl
  map_add' _ _ := rfl

lemma Lp.coeFn_const : Lp.const p Î¼ c =áµ[Î¼] Function.const Î± c :=
  AEEqFun.coeFn_const Î± c

@[simp] lemma Lp.const_val : (Lp.const p Î¼ c).1 = AEEqFun.const Î± c := rfl

@[simp]
lemma Memâ„’p.toLp_const : Memâ„’p.toLp _ (memâ„’p_const c) = Lp.const p Î¼ c := rfl

@[simp]
lemma indicatorConstLp_univ :
    indicatorConstLp p .univ (measure_ne_top Î¼ _) c = Lp.const p Î¼ c := by
  rw [â† Memâ„’p.toLp_const, indicatorConstLp]
  simp only [Set.indicator_univ, Function.const]

theorem Lp.norm_const [NeZero Î¼] (hp_zero : p â‰  0) :
    â€–Lp.const p Î¼ câ€– = â€–câ€– * (Î¼ Set.univ).toReal ^ (1 / p.toReal) := by
  have := NeZero.ne Î¼
  rw [â† Memâ„’p.toLp_const, Lp.norm_toLp, snorm_const] <;> try assumption
  rw [ENNReal.toReal_mul, ENNReal.coe_toReal, â† ENNReal.toReal_rpow, coe_nnnorm]

theorem Lp.norm_const' (hp_zero : p â‰  0) (hp_top : p â‰  âˆ) :
    â€–Lp.const p Î¼ câ€– = â€–câ€– * (Î¼ Set.univ).toReal ^ (1 / p.toReal) := by
  rw [â† Memâ„’p.toLp_const, Lp.norm_toLp, snorm_const'] <;> try assumption
  rw [ENNReal.toReal_mul, ENNReal.coe_toReal, â† ENNReal.toReal_rpow, coe_nnnorm]

theorem Lp.norm_const_le : â€–Lp.const p Î¼ câ€– â‰¤ â€–câ€– * (Î¼ Set.univ).toReal ^ (1 / p.toReal) := by
  rw [â† indicatorConstLp_univ]
  exact norm_indicatorConstLp_le

/-- `MeasureTheory.Lp.const` as a `LinearMap`. -/
@[simps] protected def Lp.constâ‚— (ğ•œ : Type*) [NormedRing ğ•œ] [Module ğ•œ E] [BoundedSMul ğ•œ E] :
    E â†’â‚—[ğ•œ] Lp E p Î¼ where
  toFun := Lp.const p Î¼
  map_add' := map_add _
  map_smul' _ _ := rfl

@[simps! apply]
protected def Lp.constL (ğ•œ : Type*) [NormedField ğ•œ] [NormedSpace ğ•œ E] [Fact (1 â‰¤ p)] :
    E â†’L[ğ•œ] Lp E p Î¼ :=
  (Lp.constâ‚— p Î¼ ğ•œ).mkContinuous ((Î¼ Set.univ).toReal ^ (1 / p.toReal)) <| fun _ â†¦
    (Lp.norm_const_le _ _ _).trans_eq (mul_comm _ _)

theorem Lp.norm_constL_le (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]
    [Fact (1 â‰¤ p)] :
    â€–(Lp.constL p Î¼ ğ•œ : E â†’L[ğ•œ] Lp E p Î¼)â€– â‰¤ (Î¼ Set.univ).toReal ^ (1 / p.toReal) :=
  LinearMap.mkContinuous_norm_le _ (by positivity) _

end const

theorem Memâ„’p.norm_rpow_div {f : Î± â†’ E} (hf : Memâ„’p f p Î¼) (q : â„â‰¥0âˆ) :
    Memâ„’p (fun x : Î± => â€–f xâ€– ^ q.toReal) (p / q) Î¼ := by
  refine' âŸ¨(hf.1.norm.aemeasurable.pow_const q.toReal).aestronglyMeasurable, _âŸ©
  by_cases q_top : q = âˆ
  Â· simp [q_top]
  by_cases q_zero : q = 0
  Â· simp [q_zero]
    by_cases p_zero : p = 0
    Â· simp [p_zero]
    rw [ENNReal.div_zero p_zero]
    exact (memâ„’p_top_const (1 : â„)).2
  rw [snorm_norm_rpow _ (ENNReal.toReal_pos q_zero q_top)]
  apply ENNReal.rpow_lt_top_of_nonneg ENNReal.toReal_nonneg
  rw [ENNReal.ofReal_toReal q_top, div_eq_mul_inv, mul_assoc, ENNReal.inv_mul_cancel q_zero q_top,
    mul_one]
  exact hf.2.ne
#align measure_theory.mem_â„’p.norm_rpow_div MeasureTheory.Memâ„’p.norm_rpow_div

theorem memâ„’p_norm_rpow_iff {q : â„â‰¥0âˆ} {f : Î± â†’ E} (hf : AEStronglyMeasurable f Î¼) (q_zero : q â‰  0)
    (q_top : q â‰  âˆ) : Memâ„’p (fun x : Î± => â€–f xâ€– ^ q.toReal) (p / q) Î¼ â†” Memâ„’p f p Î¼ := by
  refine' âŸ¨fun h => _, fun h => h.norm_rpow_div qâŸ©
  apply (memâ„’p_norm_iff hf).1
  convert h.norm_rpow_div qâ»Â¹ using 1
  Â· ext x
    rw [Real.norm_eq_abs, Real.abs_rpow_of_nonneg (norm_nonneg _), â† Real.rpow_mul (abs_nonneg _),
      ENNReal.toReal_inv, mul_inv_cancel, abs_of_nonneg (norm_nonneg _), Real.rpow_one]
    simp [ENNReal.toReal_eq_zero_iff, not_or, q_zero, q_top]
  Â· rw [div_eq_mul_inv, inv_inv, div_eq_mul_inv, mul_assoc, ENNReal.inv_mul_cancel q_zero q_top,
      mul_one]
#align measure_theory.mem_â„’p_norm_rpow_iff MeasureTheory.memâ„’p_norm_rpow_iff

theorem Memâ„’p.norm_rpow {f : Î± â†’ E} (hf : Memâ„’p f p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ) :
    Memâ„’p (fun x : Î± => â€–f xâ€– ^ p.toReal) 1 Î¼ := by
  convert hf.norm_rpow_div p
  rw [div_eq_mul_inv, ENNReal.mul_inv_cancel hp_ne_zero hp_ne_top]
#align measure_theory.mem_â„’p.norm_rpow MeasureTheory.Memâ„’p.norm_rpow

theorem AEEqFun.compMeasurePreserving_mem_Lp {Î² : Type*} [MeasurableSpace Î²]
    {Î¼b : MeasureTheory.Measure Î²} {g : Î² â†’â‚˜[Î¼b] E} (hg : g âˆˆ Lp E p Î¼b) {f : Î± â†’ Î²}
    (hf : MeasurePreserving f Î¼ Î¼b) :
    g.compMeasurePreserving f hf âˆˆ Lp E p Î¼ := by
  rw [Lp.mem_Lp_iff_snorm_lt_top] at hg âŠ¢
  rwa [snorm_compMeasurePreserving]

namespace Lp

/-! ### Composition with a measure preserving function -/

variable {Î² : Type*} [MeasurableSpace Î²] {Î¼b : MeasureTheory.Measure Î²} {f : Î± â†’ Î²}

/-- Composition of an `L^p` function with a measure preserving function is an `L^p` function. -/
def compMeasurePreserving (f : Î± â†’ Î²) (hf : MeasurePreserving f Î¼ Î¼b) :
    Lp E p Î¼b â†’+ Lp E p Î¼ where
  toFun g := âŸ¨g.1.compMeasurePreserving f hf, g.1.compMeasurePreserving_mem_Lp g.2 hfâŸ©
  map_zero' := rfl
  map_add' := by rintro âŸ¨âŸ¨_âŸ©, _âŸ© âŸ¨âŸ¨_âŸ©, _âŸ©; rfl

@[simp]
theorem compMeasurePreserving_val (g : Lp E p Î¼b) (hf : MeasurePreserving f Î¼ Î¼b) :
    (compMeasurePreserving f hf g).1 = g.1.compMeasurePreserving f hf :=
  rfl

theorem coeFn_compMeasurePreserving (g : Lp E p Î¼b) (hf : MeasurePreserving f Î¼ Î¼b) :
    compMeasurePreserving f hf g =áµ[Î¼] g âˆ˜ f :=
  g.1.coeFn_compMeasurePreserving hf

@[simp]
theorem norm_compMeasurePreserving (g : Lp E p Î¼b) (hf : MeasurePreserving f Î¼ Î¼b) :
    â€–compMeasurePreserving f hf gâ€– = â€–gâ€– :=
  congr_arg ENNReal.toReal <| g.1.snorm_compMeasurePreserving hf

variable (ğ•œ : Type*) [NormedRing ğ•œ] [Module ğ•œ E] [BoundedSMul ğ•œ E]

/-- `MeasureTheory.Lp.compMeasurePreserving` as a linear map. -/
@[simps]
def compMeasurePreservingâ‚— (f : Î± â†’ Î²) (hf : MeasurePreserving f Î¼ Î¼b) :
    Lp E p Î¼b â†’â‚—[ğ•œ] Lp E p Î¼ where
  __ := compMeasurePreserving f hf
  map_smul' c g := by rcases g with âŸ¨âŸ¨_âŸ©, _âŸ©; rfl

/-- `MeasureTheory.Lp.compMeasurePreserving` as a linear isometry. -/
@[simps!]
def compMeasurePreservingâ‚—áµ¢ [Fact (1 â‰¤ p)] (f : Î± â†’ Î²) (hf : MeasurePreserving f Î¼ Î¼b) :
    Lp E p Î¼b â†’â‚—áµ¢[ğ•œ] Lp E p Î¼ where
  toLinearMap := compMeasurePreservingâ‚— ğ•œ f hf
  norm_map' := (norm_compMeasurePreserving Â· hf)

end Lp

end MeasureTheory

open MeasureTheory

/-!
### Composition on `L^p`

We show that Lipschitz functions vanishing at zero act by composition on `L^p`, and specialize
this to the composition with continuous linear maps, and to the definition of the positive
part of an `L^p` function.
-/


section Composition

variable {g : E â†’ F} {c : â„â‰¥0}

theorem LipschitzWith.comp_memâ„’p {Î± E F} {K} [MeasurableSpace Î±] {Î¼ : Measure Î±}
    [NormedAddCommGroup E] [NormedAddCommGroup F] {f : Î± â†’ E} {g : E â†’ F} (hg : LipschitzWith K g)
    (g0 : g 0 = 0) (hL : Memâ„’p f p Î¼) : Memâ„’p (g âˆ˜ f) p Î¼ :=
  have : âˆ€ x, â€–g (f x)â€– â‰¤ K * â€–f xâ€– := fun x â†¦ by
    -- TODO: add `LipschitzWith.nnnorm_sub_le` and `LipschitzWith.nnnorm_le`
    simpa [g0] using hg.norm_sub_le (f x) 0
  hL.of_le_mul (hg.continuous.comp_aestronglyMeasurable hL.1) (eventually_of_forall this)
#align lipschitz_with.comp_mem_â„’p LipschitzWith.comp_memâ„’p

theorem MeasureTheory.Memâ„’p.of_comp_antilipschitzWith {Î± E F} {K'} [MeasurableSpace Î±]
    {Î¼ : Measure Î±} [NormedAddCommGroup E] [NormedAddCommGroup F] {f : Î± â†’ E} {g : E â†’ F}
    (hL : Memâ„’p (g âˆ˜ f) p Î¼) (hg : UniformContinuous g) (hg' : AntilipschitzWith K' g)
    (g0 : g 0 = 0) : Memâ„’p f p Î¼ := by
  have A : âˆ€ x, â€–f xâ€– â‰¤ K' * â€–g (f x)â€– := by
    intro x
    -- TODO: add `AntilipschitzWith.le_mul_nnnorm_sub` and `AntilipschitzWith.le_mul_norm`
    rw [â† dist_zero_right, â† dist_zero_right, â† g0]
    apply hg'.le_mul_dist
  have B : AEStronglyMeasurable f Î¼ :=
    (hg'.uniformEmbedding hg).embedding.aestronglyMeasurable_comp_iff.1 hL.1
  exact hL.of_le_mul B (Filter.eventually_of_forall A)
#align measure_theory.mem_â„’p.of_comp_antilipschitz_with MeasureTheory.Memâ„’p.of_comp_antilipschitzWith

namespace LipschitzWith

theorem memâ„’p_comp_iff_of_antilipschitz {Î± E F} {K K'} [MeasurableSpace Î±] {Î¼ : Measure Î±}
    [NormedAddCommGroup E] [NormedAddCommGroup F] {f : Î± â†’ E} {g : E â†’ F} (hg : LipschitzWith K g)
    (hg' : AntilipschitzWith K' g) (g0 : g 0 = 0) : Memâ„’p (g âˆ˜ f) p Î¼ â†” Memâ„’p f p Î¼ :=
  âŸ¨fun h => h.of_comp_antilipschitzWith hg.uniformContinuous hg' g0, fun h => hg.comp_memâ„’p g0 hâŸ©
#align lipschitz_with.mem_â„’p_comp_iff_of_antilipschitz LipschitzWith.memâ„’p_comp_iff_of_antilipschitz

/-- When `g` is a Lipschitz function sending `0` to `0` and `f` is in `Lp`, then `g âˆ˜ f` is well
defined as an element of `Lp`. -/
def compLp (hg : LipschitzWith c g) (g0 : g 0 = 0) (f : Lp E p Î¼) : Lp F p Î¼ :=
  âŸ¨AEEqFun.comp g hg.continuous (f : Î± â†’â‚˜[Î¼] E), by
    suffices âˆ€áµ x âˆ‚Î¼, â€–AEEqFun.comp g hg.continuous (f : Î± â†’â‚˜[Î¼] E) xâ€– â‰¤ c * â€–f xâ€– from
      Lp.mem_Lp_of_ae_le_mul this
    filter_upwards [AEEqFun.coeFn_comp g hg.continuous (f : Î± â†’â‚˜[Î¼] E)] with a ha
    simp only [ha]
    rw [â† dist_zero_right, â† dist_zero_right, â† g0]
    exact hg.dist_le_mul (f a) 0âŸ©
#align lipschitz_with.comp_Lp LipschitzWith.compLp

theorem coeFn_compLp (hg : LipschitzWith c g) (g0 : g 0 = 0) (f : Lp E p Î¼) :
    hg.compLp g0 f =áµ[Î¼] g âˆ˜ f :=
  AEEqFun.coeFn_comp _ hg.continuous _
#align lipschitz_with.coe_fn_comp_Lp LipschitzWith.coeFn_compLp

@[simp]
theorem compLp_zero (hg : LipschitzWith c g) (g0 : g 0 = 0) : hg.compLp g0 (0 : Lp E p Î¼) = 0 := by
  rw [Lp.eq_zero_iff_ae_eq_zero]
  apply (coeFn_compLp _ _ _).trans
  filter_upwards [Lp.coeFn_zero E p Î¼] with _ ha
  simp only [ha, g0, Function.comp_apply, Pi.zero_apply]
#align lipschitz_with.comp_Lp_zero LipschitzWith.compLp_zero

theorem norm_compLp_sub_le (hg : LipschitzWith c g) (g0 : g 0 = 0) (f f' : Lp E p Î¼) :
    â€–hg.compLp g0 f - hg.compLp g0 f'â€– â‰¤ c * â€–f - f'â€– := by
  apply Lp.norm_le_mul_norm_of_ae_le_mul
  filter_upwards [hg.coeFn_compLp g0 f, hg.coeFn_compLp g0 f',
    Lp.coeFn_sub (hg.compLp g0 f) (hg.compLp g0 f'), Lp.coeFn_sub f f'] with a ha1 ha2 ha3 ha4
  simp only [ha1, ha2, ha3, ha4, â† dist_eq_norm, Pi.sub_apply, Function.comp_apply]
  exact hg.dist_le_mul (f a) (f' a)
#align lipschitz_with.norm_comp_Lp_sub_le LipschitzWith.norm_compLp_sub_le

theorem norm_compLp_le (hg : LipschitzWith c g) (g0 : g 0 = 0) (f : Lp E p Î¼) :
    â€–hg.compLp g0 fâ€– â‰¤ c * â€–fâ€– := by simpa using hg.norm_compLp_sub_le g0 f 0
#align lipschitz_with.norm_comp_Lp_le LipschitzWith.norm_compLp_le

theorem lipschitzWith_compLp [Fact (1 â‰¤ p)] (hg : LipschitzWith c g) (g0 : g 0 = 0) :
    LipschitzWith c (hg.compLp g0 : Lp E p Î¼ â†’ Lp F p Î¼) :=
  LipschitzWith.of_dist_le_mul fun f g => by simp [dist_eq_norm, norm_compLp_sub_le]
#align lipschitz_with.lipschitz_with_comp_Lp LipschitzWith.lipschitzWith_compLp

theorem continuous_compLp [Fact (1 â‰¤ p)] (hg : LipschitzWith c g) (g0 : g 0 = 0) :
    Continuous (hg.compLp g0 : Lp E p Î¼ â†’ Lp F p Î¼) :=
  (lipschitzWith_compLp hg g0).continuous
#align lipschitz_with.continuous_comp_Lp LipschitzWith.continuous_compLp

end LipschitzWith

namespace ContinuousLinearMap

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]

/-- Composing `f : Lp ` with `L : E â†’L[ğ•œ] F`. -/
def compLp (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) : Lp F p Î¼ :=
  L.lipschitz.compLp (map_zero L) f
#align continuous_linear_map.comp_Lp ContinuousLinearMap.compLp

theorem coeFn_compLp (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) : âˆ€áµ a âˆ‚Î¼, (L.compLp f) a = L (f a) :=
  LipschitzWith.coeFn_compLp _ _ _
#align continuous_linear_map.coe_fn_comp_Lp ContinuousLinearMap.coeFn_compLp

theorem coeFn_compLp' (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) : L.compLp f =áµ[Î¼] fun a => L (f a) :=
  L.coeFn_compLp f
#align continuous_linear_map.coe_fn_comp_Lp' ContinuousLinearMap.coeFn_compLp'

theorem comp_memâ„’p (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) : Memâ„’p (L âˆ˜ f) p Î¼ :=
  (Lp.memâ„’p (L.compLp f)).ae_eq (L.coeFn_compLp' f)
#align continuous_linear_map.comp_mem_â„’p ContinuousLinearMap.comp_memâ„’p

theorem comp_memâ„’p' (L : E â†’L[ğ•œ] F) {f : Î± â†’ E} (hf : Memâ„’p f p Î¼) : Memâ„’p (L âˆ˜ f) p Î¼ :=
  (L.comp_memâ„’p (hf.toLp f)).ae_eq (EventuallyEq.fun_comp hf.coeFn_toLp _)
#align continuous_linear_map.comp_mem_â„’p' ContinuousLinearMap.comp_memâ„’p'

section IsROrC

variable {K : Type*} [IsROrC K]

theorem _root_.MeasureTheory.Memâ„’p.ofReal {f : Î± â†’ â„} (hf : Memâ„’p f p Î¼) :
    Memâ„’p (fun x => (f x : K)) p Î¼ :=
  (@IsROrC.ofRealClm K _).comp_memâ„’p' hf
#align measure_theory.mem_â„’p.of_real MeasureTheory.Memâ„’p.ofReal

theorem _root_.MeasureTheory.memâ„’p_re_im_iff {f : Î± â†’ K} :
    Memâ„’p (fun x => IsROrC.re (f x)) p Î¼ âˆ§ Memâ„’p (fun x => IsROrC.im (f x)) p Î¼ â†” Memâ„’p f p Î¼ := by
  refine' âŸ¨_, fun hf => âŸ¨hf.re, hf.imâŸ©âŸ©
  rintro âŸ¨hre, himâŸ©
  convert MeasureTheory.Memâ„’p.add (E := K) hre.ofReal (him.ofReal.const_mul IsROrC.I)
  ext1 x
  rw [Pi.add_apply, mul_comm, IsROrC.re_add_im]
#align measure_theory.mem_â„’p_re_im_iff MeasureTheory.memâ„’p_re_im_iff

end IsROrC

theorem add_compLp (L L' : E â†’L[ğ•œ] F) (f : Lp E p Î¼) :
    (L + L').compLp f = L.compLp f + L'.compLp f := by
  ext1
  refine' (coeFn_compLp' (L + L') f).trans _
  refine' EventuallyEq.trans _ (Lp.coeFn_add _ _).symm
  refine'
    EventuallyEq.trans _ (EventuallyEq.add (L.coeFn_compLp' f).symm (L'.coeFn_compLp' f).symm)
  refine' eventually_of_forall fun x => _
  rw [coe_add', Pi.add_def]
#align continuous_linear_map.add_comp_Lp ContinuousLinearMap.add_compLp

theorem smul_compLp {ğ•œ'} [NormedRing ğ•œ'] [Module ğ•œ' F] [BoundedSMul ğ•œ' F] [SMulCommClass ğ•œ ğ•œ' F]
    (c : ğ•œ') (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) : (c â€¢ L).compLp f = c â€¢ L.compLp f := by
  ext1
  refine' (coeFn_compLp' (c â€¢ L) f).trans _
  refine' EventuallyEq.trans _ (Lp.coeFn_smul _ _).symm
  refine' (L.coeFn_compLp' f).mono fun x hx => _
  rw [Pi.smul_apply, hx, coe_smul', Pi.smul_def]
#align continuous_linear_map.smul_comp_Lp ContinuousLinearMap.smul_compLp

theorem norm_compLp_le (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) : â€–L.compLp fâ€– â‰¤ â€–Lâ€– * â€–fâ€– :=
  LipschitzWith.norm_compLp_le _ _ _
#align continuous_linear_map.norm_comp_Lp_le ContinuousLinearMap.norm_compLp_le

variable (Î¼ p)

/-- Composing `f : Lp E p Î¼` with `L : E â†’L[ğ•œ] F`, seen as a `ğ•œ`-linear map on `Lp E p Î¼`. -/
def compLpâ‚— (L : E â†’L[ğ•œ] F) : Lp E p Î¼ â†’â‚—[ğ•œ] Lp F p Î¼ where
  toFun f := L.compLp f
  map_add' f g := by
    ext1
    filter_upwards [Lp.coeFn_add f g, coeFn_compLp L (f + g), coeFn_compLp L f,
      coeFn_compLp L g, Lp.coeFn_add (L.compLp f) (L.compLp g)]
    intro a ha1 ha2 ha3 ha4 ha5
    simp only [ha1, ha2, ha3, ha4, ha5, map_add, Pi.add_apply]
  map_smul' c f := by
    dsimp
    ext1
    filter_upwards [Lp.coeFn_smul c f, coeFn_compLp L (c â€¢ f), Lp.coeFn_smul c (L.compLp f),
      coeFn_compLp L f] with _ ha1 ha2 ha3 ha4
    simp only [ha1, ha2, ha3, ha4, SMulHomClass.map_smul, Pi.smul_apply]
#align continuous_linear_map.comp_Lpâ‚— ContinuousLinearMap.compLpâ‚—

/-- Composing `f : Lp E p Î¼` with `L : E â†’L[ğ•œ] F`, seen as a continuous `ğ•œ`-linear map on
`Lp E p Î¼`. See also the similar
* `LinearMap.compLeft` for functions,
* `ContinuousLinearMap.compLeftContinuous` for continuous functions,
* `ContinuousLinearMap.compLeftContinuousBounded` for bounded continuous functions,
* `ContinuousLinearMap.compLeftContinuousCompact` for continuous functions on compact spaces.
-/
def compLpL [Fact (1 â‰¤ p)] (L : E â†’L[ğ•œ] F) : Lp E p Î¼ â†’L[ğ•œ] Lp F p Î¼ :=
  LinearMap.mkContinuous (L.compLpâ‚— p Î¼) â€–Lâ€– L.norm_compLp_le
#align continuous_linear_map.comp_LpL ContinuousLinearMap.compLpL

variable {Î¼ p}

theorem coeFn_compLpL [Fact (1 â‰¤ p)] (L : E â†’L[ğ•œ] F) (f : Lp E p Î¼) :
    L.compLpL p Î¼ f =áµ[Î¼] fun a => L (f a) :=
  L.coeFn_compLp f
#align continuous_linear_map.coe_fn_comp_LpL ContinuousLinearMap.coeFn_compLpL

theorem add_compLpL [Fact (1 â‰¤ p)] (L L' : E â†’L[ğ•œ] F) :
    (L + L').compLpL p Î¼ = L.compLpL p Î¼ + L'.compLpL p Î¼ := by ext1 f; exact add_compLp L L' f
#align continuous_linear_map.add_comp_LpL ContinuousLinearMap.add_compLpL

theorem smul_compLpL [Fact (1 â‰¤ p)] {ğ•œ'} [NormedRing ğ•œ'] [Module ğ•œ' F] [BoundedSMul ğ•œ' F]
    [SMulCommClass ğ•œ ğ•œ' F] (c : ğ•œ') (L : E â†’L[ğ•œ] F) : (c â€¢ L).compLpL p Î¼ = c â€¢ L.compLpL p Î¼ := by
  ext1 f; exact smul_compLp c L f
#align continuous_linear_map.smul_comp_LpL ContinuousLinearMap.smul_compLpL

theorem norm_compLpL_le [Fact (1 â‰¤ p)] (L : E â†’L[ğ•œ] F) : â€–L.compLpL p Î¼â€– â‰¤ â€–Lâ€– :=
  LinearMap.mkContinuous_norm_le _ (norm_nonneg _) _
#align continuous_linear_map.norm_compLpL_le ContinuousLinearMap.norm_compLpL_le

end ContinuousLinearMap

namespace MeasureTheory

theorem indicatorConstLp_eq_toSpanSingleton_compLp {s : Set Î±} [NormedSpace â„ F]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : F) :
    indicatorConstLp 2 hs hÎ¼s x =
      (ContinuousLinearMap.toSpanSingleton â„ x).compLp (indicatorConstLp 2 hs hÎ¼s (1 : â„)) := by
  ext1
  refine' indicatorConstLp_coeFn.trans _
  have h_compLp :=
    (ContinuousLinearMap.toSpanSingleton â„ x).coeFn_compLp (indicatorConstLp 2 hs hÎ¼s (1 : â„))
  rw [â† EventuallyEq] at h_compLp
  refine' EventuallyEq.trans _ h_compLp.symm
  refine' (@indicatorConstLp_coeFn _ _ _ 2 Î¼ _ s hs hÎ¼s (1 : â„)).mono fun y hy => _
  dsimp only
  rw [hy]
  simp_rw [ContinuousLinearMap.toSpanSingleton_apply]
  by_cases hy_mem : y âˆˆ s <;> simp [hy_mem, ContinuousLinearMap.lsmul_apply]
#align measure_theory.indicator_const_Lp_eq_to_span_singleton_comp_Lp MeasureTheory.indicatorConstLp_eq_toSpanSingleton_compLp

namespace Lp

section PosPart

theorem lipschitzWith_pos_part : LipschitzWith 1 fun x : â„ => max x 0 :=
  LipschitzWith.of_dist_le_mul fun x y => by simp [Real.dist_eq, abs_max_sub_max_le_abs]
#align measure_theory.Lp.lipschitz_with_pos_part MeasureTheory.Lp.lipschitzWith_pos_part

theorem _root_.MeasureTheory.Memâ„’p.pos_part {f : Î± â†’ â„} (hf : Memâ„’p f p Î¼) :
    Memâ„’p (fun x => max (f x) 0) p Î¼ :=
  lipschitzWith_pos_part.comp_memâ„’p (max_eq_right le_rfl) hf
#align measure_theory.mem_â„’p.pos_part MeasureTheory.Memâ„’p.pos_part

theorem _root_.MeasureTheory.Memâ„’p.neg_part {f : Î± â†’ â„} (hf : Memâ„’p f p Î¼) :
    Memâ„’p (fun x => max (-f x) 0) p Î¼ :=
  lipschitzWith_pos_part.comp_memâ„’p (max_eq_right le_rfl) hf.neg
#align measure_theory.mem_â„’p.neg_part MeasureTheory.Memâ„’p.neg_part

/-- Positive part of a function in `L^p`. -/
def posPart (f : Lp â„ p Î¼) : Lp â„ p Î¼ :=
  lipschitzWith_pos_part.compLp (max_eq_right le_rfl) f
#align measure_theory.Lp.pos_part MeasureTheory.Lp.posPart

/-- Negative part of a function in `L^p`. -/
def negPart (f : Lp â„ p Î¼) : Lp â„ p Î¼ :=
  posPart (-f)
#align measure_theory.Lp.neg_part MeasureTheory.Lp.negPart

@[norm_cast]
theorem coe_posPart (f : Lp â„ p Î¼) : (posPart f : Î± â†’â‚˜[Î¼] â„) = (f : Î± â†’â‚˜[Î¼] â„).posPart :=
  rfl
#align measure_theory.Lp.coe_pos_part MeasureTheory.Lp.coe_posPart

theorem coeFn_posPart (f : Lp â„ p Î¼) : â‡‘(posPart f) =áµ[Î¼] fun a => max (f a) 0 :=
  AEEqFun.coeFn_posPart _
#align measure_theory.Lp.coe_fn_pos_part MeasureTheory.Lp.coeFn_posPart

theorem coeFn_negPart_eq_max (f : Lp â„ p Î¼) : âˆ€áµ a âˆ‚Î¼, negPart f a = max (-f a) 0 := by
  rw [negPart]
  filter_upwards [coeFn_posPart (-f), coeFn_neg f] with _ hâ‚ hâ‚‚
  rw [hâ‚, hâ‚‚, Pi.neg_apply]
#align measure_theory.Lp.coe_fn_neg_part_eq_max MeasureTheory.Lp.coeFn_negPart_eq_max

theorem coeFn_negPart (f : Lp â„ p Î¼) : âˆ€áµ a âˆ‚Î¼, negPart f a = -min (f a) 0 :=
  (coeFn_negPart_eq_max f).mono fun a h => by rw [h, â† max_neg_neg, neg_zero]
#align measure_theory.Lp.coe_fn_neg_part MeasureTheory.Lp.coeFn_negPart

theorem continuous_posPart [Fact (1 â‰¤ p)] : Continuous fun f : Lp â„ p Î¼ => posPart f :=
  LipschitzWith.continuous_compLp _ _
#align measure_theory.Lp.continuous_pos_part MeasureTheory.Lp.continuous_posPart

theorem continuous_negPart [Fact (1 â‰¤ p)] : Continuous fun f : Lp â„ p Î¼ => negPart f := by
  unfold negPart
  exact continuous_posPart.comp continuous_neg
#align measure_theory.Lp.continuous_neg_part MeasureTheory.Lp.continuous_negPart

end PosPart

end Lp

end MeasureTheory

end Composition

/-!
## `L^p` is a complete space

We show that `L^p` is a complete space for `1 â‰¤ p`.
-/


section CompleteSpace

namespace MeasureTheory

namespace Lp

theorem snorm'_lim_eq_lintegral_liminf {Î¹} [Nonempty Î¹] [LinearOrder Î¹] {f : Î¹ â†’ Î± â†’ G} {p : â„}
    (hp_nonneg : 0 â‰¤ p) {f_lim : Î± â†’ G}
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    snorm' f_lim p Î¼ = (âˆ«â» a, atTop.liminf fun m => (â€–f m aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) ^ (1 / p) := by
  suffices h_no_pow :
      (âˆ«â» a, (â€–f_lim aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) = âˆ«â» a, atTop.liminf fun m => (â€–f m aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼ by
    rw [snorm', h_no_pow]
  refine' lintegral_congr_ae (h_lim.mono fun a ha => _)
  dsimp only
  rw [Tendsto.liminf_eq]
  simp_rw [ENNReal.coe_rpow_of_nonneg _ hp_nonneg, ENNReal.tendsto_coe]
  refine' ((NNReal.continuous_rpow_const hp_nonneg).tendsto â€–f_lim aâ€–â‚Š).comp _
  exact (continuous_nnnorm.tendsto (f_lim a)).comp ha
#align measure_theory.Lp.snorm'_lim_eq_lintegral_liminf MeasureTheory.Lp.snorm'_lim_eq_lintegral_liminf

theorem snorm'_lim_le_liminf_snorm' {E} [NormedAddCommGroup E] {f : â„• â†’ Î± â†’ E} {p : â„}
    (hp_pos : 0 < p) (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) {f_lim : Î± â†’ E}
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    snorm' f_lim p Î¼ â‰¤ atTop.liminf fun n => snorm' (f n) p Î¼ := by
  rw [snorm'_lim_eq_lintegral_liminf hp_pos.le h_lim]
  rw [â† ENNReal.le_rpow_one_div_iff (by simp [hp_pos] : 0 < 1 / p), one_div_one_div]
  refine (lintegral_liminf_le' fun m => (hf m).ennnorm.pow_const _).trans_eq ?_
  have h_pow_liminf :
    (atTop.liminf fun n => snorm' (f n) p Î¼) ^ p = atTop.liminf fun n => snorm' (f n) p Î¼ ^ p := by
    have h_rpow_mono := ENNReal.strictMono_rpow_of_pos hp_pos
    have h_rpow_surj := (ENNReal.rpow_left_bijective hp_pos.ne.symm).2
    refine' (h_rpow_mono.orderIsoOfSurjective _ h_rpow_surj).liminf_apply _ _ _ _
    all_goals isBoundedDefault
  rw [h_pow_liminf]
  simp_rw [snorm', â† ENNReal.rpow_mul, one_div, inv_mul_cancel hp_pos.ne.symm, ENNReal.rpow_one]
#align measure_theory.Lp.snorm'_lim_le_liminf_snorm' MeasureTheory.Lp.snorm'_lim_le_liminf_snorm'

theorem snorm_exponent_top_lim_eq_essSup_liminf {Î¹} [Nonempty Î¹] [LinearOrder Î¹] {f : Î¹ â†’ Î± â†’ G}
    {f_lim : Î± â†’ G} (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    snorm f_lim âˆ Î¼ = essSup (fun x => atTop.liminf fun m => (â€–f m xâ€–â‚Š : â„â‰¥0âˆ)) Î¼ := by
  rw [snorm_exponent_top, snormEssSup]
  refine' essSup_congr_ae (h_lim.mono fun x hx => _)
  dsimp only
  apply (Tendsto.liminf_eq ..).symm
  rw [ENNReal.tendsto_coe]
  exact (continuous_nnnorm.tendsto (f_lim x)).comp hx
#align measure_theory.Lp.snorm_exponent_top_lim_eq_ess_sup_liminf MeasureTheory.Lp.snorm_exponent_top_lim_eq_essSup_liminf

theorem snorm_exponent_top_lim_le_liminf_snorm_exponent_top {Î¹} [Nonempty Î¹] [Countable Î¹]
    [LinearOrder Î¹] {f : Î¹ â†’ Î± â†’ F} {f_lim : Î± â†’ F}
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    snorm f_lim âˆ Î¼ â‰¤ atTop.liminf fun n => snorm (f n) âˆ Î¼ := by
  rw [snorm_exponent_top_lim_eq_essSup_liminf h_lim]
  simp_rw [snorm_exponent_top, snormEssSup]
  exact ENNReal.essSup_liminf_le fun n => fun x => (â€–f n xâ€–â‚Š : â„â‰¥0âˆ)
#align measure_theory.Lp.snorm_exponent_top_lim_le_liminf_snorm_exponent_top MeasureTheory.Lp.snorm_exponent_top_lim_le_liminf_snorm_exponent_top

theorem snorm_lim_le_liminf_snorm {E} [NormedAddCommGroup E] {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (f_lim : Î± â†’ E)
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    snorm f_lim p Î¼ â‰¤ atTop.liminf fun n => snorm (f n) p Î¼ := by
  obtain rfl|hp0 := eq_or_ne p 0
  Â· simp
  by_cases hp_top : p = âˆ
  Â· simp_rw [hp_top]
    exact snorm_exponent_top_lim_le_liminf_snorm_exponent_top h_lim
  simp_rw [snorm_eq_snorm' hp0 hp_top]
  have hp_pos : 0 < p.toReal := ENNReal.toReal_pos hp0 hp_top
  exact snorm'_lim_le_liminf_snorm' hp_pos hf h_lim
#align measure_theory.Lp.snorm_lim_le_liminf_snorm MeasureTheory.Lp.snorm_lim_le_liminf_snorm

/-! ### `Lp` is complete iff Cauchy sequences of `â„’p` have limits in `â„’p` -/


theorem tendsto_Lp_iff_tendsto_â„’p' {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] (f : Î¹ â†’ Lp E p Î¼)
    (f_lim : Lp E p Î¼) :
    fi.Tendsto f (ğ“ f_lim) â†” fi.Tendsto (fun n => snorm (â‡‘(f n) - â‡‘f_lim) p Î¼) (ğ“ 0) := by
  rw [tendsto_iff_dist_tendsto_zero]
  simp_rw [dist_def]
  rw [â† ENNReal.zero_toReal, ENNReal.tendsto_toReal_iff (fun n => ?_) ENNReal.zero_ne_top]
  rw [snorm_congr_ae (Lp.coeFn_sub _ _).symm]
  exact Lp.snorm_ne_top _
#align measure_theory.Lp.tendsto_Lp_iff_tendsto_â„’p' MeasureTheory.Lp.tendsto_Lp_iff_tendsto_â„’p'

theorem tendsto_Lp_iff_tendsto_â„’p {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] (f : Î¹ â†’ Lp E p Î¼)
    (f_lim : Î± â†’ E) (f_lim_â„’p : Memâ„’p f_lim p Î¼) :
    fi.Tendsto f (ğ“ (f_lim_â„’p.toLp f_lim)) â†”
      fi.Tendsto (fun n => snorm (â‡‘(f n) - f_lim) p Î¼) (ğ“ 0) := by
  rw [tendsto_Lp_iff_tendsto_â„’p']
  suffices h_eq :
      (fun n => snorm (â‡‘(f n) - â‡‘(Memâ„’p.toLp f_lim f_lim_â„’p)) p Î¼) =
        (fun n => snorm (â‡‘(f n) - f_lim) p Î¼) by
    rw [h_eq]
  exact funext fun n => snorm_congr_ae (EventuallyEq.rfl.sub (Memâ„’p.coeFn_toLp f_lim_â„’p))
#align measure_theory.Lp.tendsto_Lp_iff_tendsto_â„’p MeasureTheory.Lp.tendsto_Lp_iff_tendsto_â„’p

theorem tendsto_Lp_iff_tendsto_â„’p'' {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] (f : Î¹ â†’ Î± â†’ E)
    (f_â„’p : âˆ€ n, Memâ„’p (f n) p Î¼) (f_lim : Î± â†’ E) (f_lim_â„’p : Memâ„’p f_lim p Î¼) :
    fi.Tendsto (fun n => (f_â„’p n).toLp (f n)) (ğ“ (f_lim_â„’p.toLp f_lim)) â†”
      fi.Tendsto (fun n => snorm (f n - f_lim) p Î¼) (ğ“ 0) := by
  rw [Lp.tendsto_Lp_iff_tendsto_â„’p' (fun n => (f_â„’p n).toLp (f n)) (f_lim_â„’p.toLp f_lim)]
  refine Filter.tendsto_congr fun n => ?_
  apply snorm_congr_ae
  filter_upwards [((f_â„’p n).sub f_lim_â„’p).coeFn_toLp,
    Lp.coeFn_sub ((f_â„’p n).toLp (f n)) (f_lim_â„’p.toLp f_lim)] with _ hxâ‚ hxâ‚‚
  rw [â† hxâ‚‚]
  exact hxâ‚
#align measure_theory.Lp.tendsto_Lp_iff_tendsto_â„’p'' MeasureTheory.Lp.tendsto_Lp_iff_tendsto_â„’p''

theorem tendsto_Lp_of_tendsto_â„’p {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] {f : Î¹ â†’ Lp E p Î¼}
    (f_lim : Î± â†’ E) (f_lim_â„’p : Memâ„’p f_lim p Î¼)
    (h_tendsto : fi.Tendsto (fun n => snorm (â‡‘(f n) - f_lim) p Î¼) (ğ“ 0)) :
    fi.Tendsto f (ğ“ (f_lim_â„’p.toLp f_lim)) :=
  (tendsto_Lp_iff_tendsto_â„’p f f_lim f_lim_â„’p).mpr h_tendsto
#align measure_theory.Lp.tendsto_Lp_of_tendsto_â„’p MeasureTheory.Lp.tendsto_Lp_of_tendsto_â„’p

theorem cauchySeq_Lp_iff_cauchySeq_â„’p {Î¹} [Nonempty Î¹] [SemilatticeSup Î¹] [hp : Fact (1 â‰¤ p)]
    (f : Î¹ â†’ Lp E p Î¼) :
    CauchySeq f â†” Tendsto (fun n : Î¹ Ã— Î¹ => snorm (â‡‘(f n.fst) - â‡‘(f n.snd)) p Î¼) atTop (ğ“ 0) := by
  simp_rw [cauchySeq_iff_tendsto_dist_atTop_0, dist_def]
  rw [â† ENNReal.zero_toReal, ENNReal.tendsto_toReal_iff (fun n => ?_) ENNReal.zero_ne_top]
  rw [snorm_congr_ae (Lp.coeFn_sub _ _).symm]
  exact snorm_ne_top _
#align measure_theory.Lp.cauchy_seq_Lp_iff_cauchy_seq_â„’p MeasureTheory.Lp.cauchySeq_Lp_iff_cauchySeq_â„’p

theorem completeSpace_lp_of_cauchy_complete_â„’p [hp : Fact (1 â‰¤ p)]
    (H :
      âˆ€ (f : â„• â†’ Î± â†’ E) (hf : âˆ€ n, Memâ„’p (f n) p Î¼) (B : â„• â†’ â„â‰¥0âˆ) (hB : âˆ‘' i, B i < âˆ)
        (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm (f n - f m) p Î¼ < B N),
        âˆƒ (f_lim : Î± â†’ E), Memâ„’p f_lim p Î¼ âˆ§
          atTop.Tendsto (fun n => snorm (f n - f_lim) p Î¼) (ğ“ 0)) :
    CompleteSpace (Lp E p Î¼) := by
  let B := fun n : â„• => ((1 : â„) / 2) ^ n
  have hB_pos : âˆ€ n, 0 < B n := fun n => pow_pos (div_pos zero_lt_one zero_lt_two) n
  refine' Metric.complete_of_convergent_controlled_sequences B hB_pos fun f hf => _
  rsuffices âŸ¨f_lim, hf_lim_meas, h_tendstoâŸ© :
    âˆƒ (f_lim : Î± â†’ E), Memâ„’p f_lim p Î¼ âˆ§
      atTop.Tendsto (fun n => snorm (â‡‘(f n) - f_lim) p Î¼) (ğ“ 0)
  Â· exact âŸ¨hf_lim_meas.toLp f_lim, tendsto_Lp_of_tendsto_â„’p f_lim hf_lim_meas h_tendstoâŸ©
  obtain âŸ¨M, hBâŸ© : Summable B := summable_geometric_two
  let B1 n := ENNReal.ofReal (B n)
  have hB1_has : HasSum B1 (ENNReal.ofReal M) := by
    have h_tsum_B1 : âˆ‘' i, B1 i = ENNReal.ofReal M := by
      change (âˆ‘' n : â„•, ENNReal.ofReal (B n)) = ENNReal.ofReal M
      rw [â† hB.tsum_eq]
      exact (ENNReal.ofReal_tsum_of_nonneg (fun n => le_of_lt (hB_pos n)) hB.summable).symm
    have h_sum := (@ENNReal.summable _ B1).hasSum
    rwa [h_tsum_B1] at h_sum
  have hB1 : âˆ‘' i, B1 i < âˆ := by
    rw [hB1_has.tsum_eq]
    exact ENNReal.ofReal_lt_top
  let f1 : â„• â†’ Î± â†’ E := fun n => f n
  refine' H f1 (fun n => Lp.memâ„’p (f n)) B1 hB1 fun N n m hn hm => _
  specialize hf N n m hn hm
  rw [dist_def] at hf
  dsimp only
  rwa [ENNReal.lt_ofReal_iff_toReal_lt]
  rw [snorm_congr_ae (Lp.coeFn_sub _ _).symm]
  exact Lp.snorm_ne_top _
#align measure_theory.Lp.complete_space_Lp_of_cauchy_complete_â„’p MeasureTheory.Lp.completeSpace_lp_of_cauchy_complete_â„’p

/-! ### Prove that controlled Cauchy sequences of `â„’p` have limits in `â„’p` -/


private theorem snorm'_sum_norm_sub_le_tsum_of_cauchy_snorm' {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ}
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm' (f n - f m) p Î¼ < B N) (n : â„•) :
    snorm' (fun x => âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) p Î¼ â‰¤ âˆ‘' i, B i := by
  let f_norm_diff i x := â€–f (i + 1) x - f i xâ€–
  have hgf_norm_diff :
    âˆ€ n,
      (fun x => âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) =
        âˆ‘ i in Finset.range (n + 1), f_norm_diff i :=
    fun n => funext fun x => by simp
  rw [hgf_norm_diff]
  refine' (snorm'_sum_le (fun i _ => ((hf (i + 1)).sub (hf i)).norm) hp1).trans _
  simp_rw [snorm'_norm]
  refine' (Finset.sum_le_sum _).trans (sum_le_tsum _ (fun m _ => zero_le _) ENNReal.summable)
  exact fun m _ => (h_cau m (m + 1) m (Nat.le_succ m) (le_refl m)).le

private theorem lintegral_rpow_sum_coe_nnnorm_sub_le_rpow_tsum
    {f : â„• â†’ Î± â†’ E} {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (n : â„•)
    (hn : snorm' (fun x => âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) p Î¼ â‰¤ âˆ‘' i, B i) :
    (âˆ«â» a, (âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) â‰¤
      (âˆ‘' i, B i) ^ p := by
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1
  rw [â† one_div_one_div p, @ENNReal.le_rpow_one_div_iff _ _ (1 / p) (by simp [hp_pos]),
    one_div_one_div p]
  simp_rw [snorm'] at hn
  have h_nnnorm_nonneg :
    (fun a => (â€–âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â€–â‚Š : â„â‰¥0âˆ) ^ p) = fun a =>
      (âˆ‘ i in Finset.range (n + 1), (â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ)) ^ p := by
    ext1 a
    congr
    simp_rw [â† ofReal_norm_eq_coe_nnnorm]
    rw [â† ENNReal.ofReal_sum_of_nonneg]
    Â· rw [Real.norm_of_nonneg _]
      exact Finset.sum_nonneg fun x _ => norm_nonneg _
    Â· exact fun x _ => norm_nonneg _
  change
    (âˆ«â» a, (fun x => â†‘â€–âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–â€–â‚Š ^ p) a âˆ‚Î¼) ^ (1 / p) â‰¤
      âˆ‘' i, B i at hn
  rwa [h_nnnorm_nonneg] at hn

private theorem lintegral_rpow_tsum_coe_nnnorm_sub_le_tsum {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ}
    (h :
      âˆ€ n,
        (âˆ«â» a, (âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) â‰¤
          (âˆ‘' i, B i) ^ p) :
    (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) ^ (1 / p) â‰¤ âˆ‘' i, B i := by
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1
  suffices h_pow : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) â‰¤ (âˆ‘' i, B i) ^ p
  Â· rwa [â† ENNReal.le_rpow_one_div_iff (by simp [hp_pos] : 0 < 1 / p), one_div_one_div]
  have h_tsum_1 :
    âˆ€ g : â„• â†’ â„â‰¥0âˆ, âˆ‘' i, g i = atTop.liminf fun n => âˆ‘ i in Finset.range (n + 1), g i := by
    intro g
    rw [ENNReal.tsum_eq_liminf_sum_nat, â† liminf_nat_add _ 1]
  simp_rw [h_tsum_1 _]
  rw [â† h_tsum_1]
  have h_liminf_pow :
    (âˆ«â» a, (atTop.liminf
      fun n => âˆ‘ i in Finset.range (n + 1), (â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ)) ^ p âˆ‚Î¼) =
      âˆ«â» a, atTop.liminf
        fun n => (âˆ‘ i in Finset.range (n + 1), (â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ)) ^ p âˆ‚Î¼ := by
    refine' lintegral_congr fun x => _
    have h_rpow_mono := ENNReal.strictMono_rpow_of_pos (zero_lt_one.trans_le hp1)
    have h_rpow_surj := (ENNReal.rpow_left_bijective hp_pos.ne.symm).2
    refine' (h_rpow_mono.orderIsoOfSurjective _ h_rpow_surj).liminf_apply _ _ _ _
    all_goals isBoundedDefault
  rw [h_liminf_pow]
  refine' (lintegral_liminf_le' _).trans _
  Â· exact fun n =>
      (Finset.aemeasurable_sum (Finset.range (n + 1)) fun i _ =>
            ((hf (i + 1)).sub (hf i)).ennnorm).pow_const
        _
  Â· exact liminf_le_of_frequently_le' (frequently_of_forall h)

private theorem tsum_nnnorm_sub_ae_lt_top {f : â„• â†’ Î± â†’ E} (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) ^ (1 / p) â‰¤ âˆ‘' i, B i) :
    âˆ€áµ x âˆ‚Î¼, (âˆ‘' i, â€–f (i + 1) x - f i xâ€–â‚Š : â„â‰¥0âˆ) < âˆ := by
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1
  have h_integral : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) < âˆ := by
    have h_tsum_lt_top : (âˆ‘' i, B i) ^ p < âˆ := ENNReal.rpow_lt_top_of_nonneg hp_pos.le hB
    refine' lt_of_le_of_lt _ h_tsum_lt_top
    rwa [â† ENNReal.le_rpow_one_div_iff (by simp [hp_pos] : 0 < 1 / p), one_div_one_div] at h
  have rpow_ae_lt_top : âˆ€áµ x âˆ‚Î¼, (âˆ‘' i, â€–f (i + 1) x - f i xâ€–â‚Š : â„â‰¥0âˆ) ^ p < âˆ := by
    refine' ae_lt_top' (AEMeasurable.pow_const _ _) h_integral.ne
    exact AEMeasurable.ennreal_tsum fun n => ((hf (n + 1)).sub (hf n)).ennnorm
  refine' rpow_ae_lt_top.mono fun x hx => _
  rwa [â† ENNReal.lt_rpow_one_div_iff hp_pos,
    ENNReal.top_rpow_of_pos (by simp [hp_pos] : 0 < 1 / p)] at hx

theorem ae_tendsto_of_cauchy_snorm' [CompleteSpace E] {f : â„• â†’ Î± â†’ E} {p : â„}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm' (f n - f m) p Î¼ < B N) :
    âˆ€áµ x âˆ‚Î¼, âˆƒ l : E, atTop.Tendsto (fun n => f n x) (ğ“ l) := by
  have h_summable : âˆ€áµ x âˆ‚Î¼, Summable fun i : â„• => f (i + 1) x - f i x := by
    have h1 :
      âˆ€ n, snorm' (fun x => âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) p Î¼ â‰¤ âˆ‘' i, B i :=
      snorm'_sum_norm_sub_le_tsum_of_cauchy_snorm' hf hp1 h_cau
    have h2 :
      âˆ€ n,
        (âˆ«â» a, (âˆ‘ i in Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) â‰¤
          (âˆ‘' i, B i) ^ p :=
      fun n => lintegral_rpow_sum_coe_nnnorm_sub_le_rpow_tsum hp1 n (h1 n)
    have h3 : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼) ^ (1 / p) â‰¤ âˆ‘' i, B i :=
      lintegral_rpow_tsum_coe_nnnorm_sub_le_tsum hf hp1 h2
    have h4 : âˆ€áµ x âˆ‚Î¼, (âˆ‘' i, â€–f (i + 1) x - f i xâ€–â‚Š : â„â‰¥0âˆ) < âˆ :=
      tsum_nnnorm_sub_ae_lt_top hf hp1 hB h3
    exact
      h4.mono fun x hx =>
        summable_of_summable_nnnorm
          (ENNReal.tsum_coe_ne_top_iff_summable.mp (lt_top_iff_ne_top.mp hx))
  have h :
    âˆ€áµ x âˆ‚Î¼, âˆƒ l : E,
      atTop.Tendsto (fun n => âˆ‘ i in Finset.range n, (f (i + 1) x - f i x)) (ğ“ l) := by
    refine' h_summable.mono fun x hx => _
    let hx_sum := hx.hasSum.tendsto_sum_nat
    exact âŸ¨âˆ‘' i, (f (i + 1) x - f i x), hx_sumâŸ©
  refine' h.mono fun x hx => _
  cases' hx with l hx
  have h_rw_sum :
      (fun n => âˆ‘ i in Finset.range n, (f (i + 1) x - f i x)) = fun n => f n x - f 0 x := by
    ext1 n
    change
      (âˆ‘ i : â„• in Finset.range n, ((fun m => f m x) (i + 1) - (fun m => f m x) i)) = f n x - f 0 x
    rw [Finset.sum_range_sub (fun m => f m x)]
  rw [h_rw_sum] at hx
  have hf_rw : (fun n => f n x) = fun n => f n x - f 0 x + f 0 x := by
    ext1 n
    abel
  rw [hf_rw]
  exact âŸ¨l + f 0 x, Tendsto.add_const _ hxâŸ©
#align measure_theory.Lp.ae_tendsto_of_cauchy_snorm' MeasureTheory.Lp.ae_tendsto_of_cauchy_snorm'

theorem ae_tendsto_of_cauchy_snorm [CompleteSpace E] {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hp : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm (f n - f m) p Î¼ < B N) :
    âˆ€áµ x âˆ‚Î¼, âˆƒ l : E, atTop.Tendsto (fun n => f n x) (ğ“ l) := by
  by_cases hp_top : p = âˆ
  Â· simp_rw [hp_top] at *
    have h_cau_ae : âˆ€áµ x âˆ‚Î¼, âˆ€ N n m, N â‰¤ n â†’ N â‰¤ m â†’ (â€–(f n - f m) xâ€–â‚Š : â„â‰¥0âˆ) < B N := by
      simp_rw [ae_all_iff]
      exact fun N n m hnN hmN => ae_lt_of_essSup_lt (h_cau N n m hnN hmN)
    simp_rw [snorm_exponent_top, snormEssSup] at h_cau
    refine' h_cau_ae.mono fun x hx => cauchySeq_tendsto_of_complete _
    refine' cauchySeq_of_le_tendsto_0 (fun n => (B n).toReal) _ _
    Â· intro n m N hnN hmN
      specialize hx N n m hnN hmN
      rw [dist_eq_norm, â† ENNReal.toReal_ofReal (norm_nonneg _),
        ENNReal.toReal_le_toReal ENNReal.ofReal_ne_top (ENNReal.ne_top_of_tsum_ne_top hB N)]
      rw [â† ofReal_norm_eq_coe_nnnorm] at hx
      exact hx.le
    Â· rw [â† ENNReal.zero_toReal]
      exact
        Tendsto.comp (g := ENNReal.toReal) (ENNReal.tendsto_toReal ENNReal.zero_ne_top)
          (ENNReal.tendsto_atTop_zero_of_tsum_ne_top hB)
  have hp1 : 1 â‰¤ p.toReal := by
    rw [â† ENNReal.ofReal_le_iff_le_toReal hp_top, ENNReal.ofReal_one]
    exact hp
  have h_cau' : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm' (f n - f m) p.toReal Î¼ < B N := by
    intro N n m hn hm
    specialize h_cau N n m hn hm
    rwa [snorm_eq_snorm' (zero_lt_one.trans_le hp).ne.symm hp_top] at h_cau
  exact ae_tendsto_of_cauchy_snorm' hf hp1 hB h_cau'
#align measure_theory.Lp.ae_tendsto_of_cauchy_snorm MeasureTheory.Lp.ae_tendsto_of_cauchy_snorm

theorem cauchy_tendsto_of_tendsto {f : â„• â†’ Î± â†’ E} (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (f_lim : Î± â†’ E) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm (f n - f m) p Î¼ < B N)
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    atTop.Tendsto (fun n => snorm (f n - f_lim) p Î¼) (ğ“ 0) := by
  rw [ENNReal.tendsto_atTop_zero]
  intro Îµ hÎµ
  have h_B : âˆƒ N : â„•, B N â‰¤ Îµ := by
    suffices h_tendsto_zero : âˆƒ N : â„•, âˆ€ n : â„•, N â‰¤ n â†’ B n â‰¤ Îµ from
      âŸ¨h_tendsto_zero.choose, h_tendsto_zero.choose_spec _ le_rflâŸ©
    exact (ENNReal.tendsto_atTop_zero.mp (ENNReal.tendsto_atTop_zero_of_tsum_ne_top hB)) Îµ hÎµ
  cases' h_B with N h_B
  refine' âŸ¨N, fun n hn => _âŸ©
  have h_sub : snorm (f n - f_lim) p Î¼ â‰¤ atTop.liminf fun m => snorm (f n - f m) p Î¼ := by
    refine' snorm_lim_le_liminf_snorm (fun m => (hf n).sub (hf m)) (f n - f_lim) _
    refine' h_lim.mono fun x hx => _
    simp_rw [sub_eq_add_neg]
    exact Tendsto.add tendsto_const_nhds (Tendsto.neg hx)
  refine' h_sub.trans _
  refine' liminf_le_of_frequently_le' (frequently_atTop.mpr _)
  refine' fun N1 => âŸ¨max N N1, le_max_right _ _, _âŸ©
  exact (h_cau N n (max N N1) hn (le_max_left _ _)).le.trans h_B
#align measure_theory.Lp.cauchy_tendsto_of_tendsto MeasureTheory.Lp.cauchy_tendsto_of_tendsto

theorem memâ„’p_of_cauchy_tendsto (hp : 1 â‰¤ p) {f : â„• â†’ Î± â†’ E} (hf : âˆ€ n, Memâ„’p (f n) p Î¼)
    (f_lim : Î± â†’ E) (h_lim_meas : AEStronglyMeasurable f_lim Î¼)
    (h_tendsto : atTop.Tendsto (fun n => snorm (f n - f_lim) p Î¼) (ğ“ 0)) : Memâ„’p f_lim p Î¼ := by
  refine' âŸ¨h_lim_meas, _âŸ©
  rw [ENNReal.tendsto_atTop_zero] at h_tendsto
  cases' h_tendsto 1 zero_lt_one with N h_tendsto_1
  specialize h_tendsto_1 N (le_refl N)
  have h_add : f_lim = f_lim - f N + f N := by abel
  rw [h_add]
  refine' lt_of_le_of_lt (snorm_add_le (h_lim_meas.sub (hf N).1) (hf N).1 hp) _
  rw [ENNReal.add_lt_top]
  constructor
  Â· refine' lt_of_le_of_lt _ ENNReal.one_lt_top
    have h_neg : f_lim - f N = -(f N - f_lim) := by simp
    rwa [h_neg, snorm_neg]
  Â· exact (hf N).2
#align measure_theory.Lp.mem_â„’p_of_cauchy_tendsto MeasureTheory.Lp.memâ„’p_of_cauchy_tendsto

theorem cauchy_complete_â„’p [CompleteSpace E] (hp : 1 â‰¤ p) {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, Memâ„’p (f n) p Î¼) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ snorm (f n - f m) p Î¼ < B N) :
    âˆƒ (f_lim : Î± â†’ E), Memâ„’p f_lim p Î¼ âˆ§
      atTop.Tendsto (fun n => snorm (f n - f_lim) p Î¼) (ğ“ 0) := by
  obtain âŸ¨f_lim, h_f_lim_meas, h_limâŸ© :
      âˆƒ (f_lim : Î± â†’ E) (_ : StronglyMeasurable f_lim),
        âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (nhds (f_lim x)) :=
    exists_stronglyMeasurable_limit_of_tendsto_ae (fun n => (hf n).1)
      (ae_tendsto_of_cauchy_snorm (fun n => (hf n).1) hp hB h_cau)
  have h_tendsto' : atTop.Tendsto (fun n => snorm (f n - f_lim) p Î¼) (ğ“ 0) :=
    cauchy_tendsto_of_tendsto (fun m => (hf m).1) f_lim hB h_cau h_lim
  have h_â„’p_lim : Memâ„’p f_lim p Î¼ :=
    memâ„’p_of_cauchy_tendsto hp hf f_lim h_f_lim_meas.aestronglyMeasurable h_tendsto'
  exact âŸ¨f_lim, h_â„’p_lim, h_tendsto'âŸ©
#align measure_theory.Lp.cauchy_complete_â„’p MeasureTheory.Lp.cauchy_complete_â„’p

/-! ### `Lp` is complete for `1 â‰¤ p` -/

instance instCompleteSpace [CompleteSpace E] [hp : Fact (1 â‰¤ p)] : CompleteSpace (Lp E p Î¼) :=
  completeSpace_lp_of_cauchy_complete_â„’p fun _f hf _B hB h_cau =>
    cauchy_complete_â„’p hp.elim hf hB.ne h_cau
#align measure_theory.Lp.complete_space MeasureTheory.Lp.instCompleteSpace

end Lp

end MeasureTheory

end CompleteSpace

/-! ### Continuous functions in `Lp` -/


open scoped BoundedContinuousFunction

open BoundedContinuousFunction

section

variable [TopologicalSpace Î±] [BorelSpace Î±] [SecondCountableTopologyEither Î± E]

variable (E p Î¼)

/-- An additive subgroup of `Lp E p Î¼`, consisting of the equivalence classes which contain a
bounded continuous representative. -/
def MeasureTheory.Lp.boundedContinuousFunction : AddSubgroup (Lp E p Î¼) :=
  AddSubgroup.addSubgroupOf
    ((ContinuousMap.toAEEqFunAddHom Î¼).comp (toContinuousMapAddHom Î± E)).range (Lp E p Î¼)
#align measure_theory.Lp.bounded_continuous_function MeasureTheory.Lp.boundedContinuousFunction

variable {E p Î¼}

/-- By definition, the elements of `Lp.boundedContinuousFunction E p Î¼` are the elements of
`Lp E p Î¼` which contain a bounded continuous representative. -/
theorem MeasureTheory.Lp.mem_boundedContinuousFunction_iff {f : Lp E p Î¼} :
    f âˆˆ MeasureTheory.Lp.boundedContinuousFunction E p Î¼ â†”
      âˆƒ fâ‚€ : Î± â†’áµ‡ E, fâ‚€.toContinuousMap.toAEEqFun Î¼ = (f : Î± â†’â‚˜[Î¼] E) :=
  AddSubgroup.mem_addSubgroupOf
#align measure_theory.Lp.mem_bounded_continuous_function_iff MeasureTheory.Lp.mem_boundedContinuousFunction_iff

namespace BoundedContinuousFunction

variable [IsFiniteMeasure Î¼]

/-- A bounded continuous function on a finite-measure space is in `Lp`. -/
theorem mem_Lp (f : Î± â†’áµ‡ E) : f.toContinuousMap.toAEEqFun Î¼ âˆˆ Lp E p Î¼ := by
  refine' Lp.mem_Lp_of_ae_bound â€–fâ€– _
  filter_upwards [f.toContinuousMap.coeFn_toAEEqFun Î¼] with x _
  convert f.norm_coe_le_norm x using 2
#align bounded_continuous_function.mem_Lp BoundedContinuousFunction.mem_Lp

/-- The `Lp`-norm of a bounded continuous function is at most a constant (depending on the measure
of the whole space) times its sup-norm. -/
theorem Lp_nnnorm_le (f : Î± â†’áµ‡ E) :
    â€–(âŸ¨f.toContinuousMap.toAEEqFun Î¼, mem_Lp fâŸ© : Lp E p Î¼)â€–â‚Š â‰¤
      measureUnivNNReal Î¼ ^ p.toRealâ»Â¹ * â€–fâ€–â‚Š := by
  apply Lp.nnnorm_le_of_ae_bound
  refine' (f.toContinuousMap.coeFn_toAEEqFun Î¼).mono _
  intro x hx
  rw [â† NNReal.coe_le_coe, coe_nnnorm, coe_nnnorm]
  convert f.norm_coe_le_norm x using 2
#align bounded_continuous_function.Lp_nnnorm_le BoundedContinuousFunction.Lp_nnnorm_le

/-- The `Lp`-norm of a bounded continuous function is at most a constant (depending on the measure
of the whole space) times its sup-norm. -/
theorem Lp_norm_le (f : Î± â†’áµ‡ E) :
    â€–(âŸ¨f.toContinuousMap.toAEEqFun Î¼, mem_Lp fâŸ© : Lp E p Î¼)â€– â‰¤
      measureUnivNNReal Î¼ ^ p.toRealâ»Â¹ * â€–fâ€– :=
  Lp_nnnorm_le f
#align bounded_continuous_function.Lp_norm_le BoundedContinuousFunction.Lp_norm_le

variable (p Î¼)

/-- The normed group homomorphism of considering a bounded continuous function on a finite-measure
space as an element of `Lp`. -/
def toLpHom [Fact (1 â‰¤ p)] : NormedAddGroupHom (Î± â†’áµ‡ E) (Lp E p Î¼) :=
  { AddMonoidHom.codRestrict ((ContinuousMap.toAEEqFunAddHom Î¼).comp (toContinuousMapAddHom Î± E))
      (Lp E p Î¼) mem_Lp with
    bound' := âŸ¨_, Lp_norm_leâŸ© }
#align bounded_continuous_function.to_Lp_hom BoundedContinuousFunction.toLpHom

theorem range_toLpHom [Fact (1 â‰¤ p)] :
    ((toLpHom p Î¼).range : AddSubgroup (Lp E p Î¼)) =
      MeasureTheory.Lp.boundedContinuousFunction E p Î¼ := by
  symm
  convert AddMonoidHom.addSubgroupOf_range_eq_of_le
      ((ContinuousMap.toAEEqFunAddHom Î¼).comp (toContinuousMapAddHom Î± E))
      (by rintro - âŸ¨f, rflâŸ©; exact mem_Lp f : _ â‰¤ Lp E p Î¼)
#align bounded_continuous_function.range_to_Lp_hom BoundedContinuousFunction.range_toLpHom

variable (ğ•œ : Type*) [Fact (1 â‰¤ p)]

/-- The bounded linear map of considering a bounded continuous function on a finite-measure space
as an element of `Lp`. -/
def toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] : (Î± â†’áµ‡ E) â†’L[ğ•œ] Lp E p Î¼ :=
  LinearMap.mkContinuous
    (LinearMap.codRestrict (Lp.LpSubmodule E p Î¼ ğ•œ)
      ((ContinuousMap.toAEEqFunLinearMap Î¼).comp (toContinuousMapLinearMap Î± E ğ•œ)) mem_Lp)
    _ Lp_norm_le
#align bounded_continuous_function.to_Lp BoundedContinuousFunction.toLp

theorem coeFn_toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] (f : Î± â†’áµ‡ E) :
    toLp (E := E) p Î¼ ğ•œ f =áµ[Î¼] f :=
  AEEqFun.coeFn_mk f _
#align bounded_continuous_function.coe_fn_to_Lp BoundedContinuousFunction.coeFn_toLp

variable {ğ•œ}

theorem range_toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] :
    (LinearMap.range (toLp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] Lp E p Î¼)).toAddSubgroup =
      MeasureTheory.Lp.boundedContinuousFunction E p Î¼ :=
  range_toLpHom p Î¼
#align bounded_continuous_function.range_to_Lp BoundedContinuousFunction.range_toLp

variable {p}

theorem toLp_norm_le [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E] :
    â€–(toLp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] Lp E p Î¼)â€– â‰¤ measureUnivNNReal Î¼ ^ p.toRealâ»Â¹ :=
  LinearMap.mkContinuous_norm_le _ (measureUnivNNReal Î¼ ^ p.toRealâ»Â¹).coe_nonneg _
#align bounded_continuous_function.to_Lp_norm_le BoundedContinuousFunction.toLp_norm_le

theorem toLp_inj {f g : Î± â†’áµ‡ E} [Î¼.IsOpenPosMeasure] [NormedField ğ•œ] [NormedSpace ğ•œ E] :
    toLp (E := E) p Î¼ ğ•œ f = toLp (E := E) p Î¼ ğ•œ g â†” f = g := by
  refine' âŸ¨fun h => _, by tautoâŸ©
  rw [â† FunLike.coe_fn_eq, â† (map_continuous f).ae_eq_iff_eq Î¼ (map_continuous g)]
  refine' (coeFn_toLp p Î¼ ğ•œ f).symm.trans (EventuallyEq.trans _ <| coeFn_toLp p Î¼ ğ•œ g)
  rw [h]
#align bounded_continuous_function.to_Lp_inj BoundedContinuousFunction.toLp_inj

theorem toLp_injective [Î¼.IsOpenPosMeasure] [NormedField ğ•œ] [NormedSpace ğ•œ E] :
    Function.Injective (â‡‘(toLp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] Lp E p Î¼)) :=
  fun _f _g hfg => (toLp_inj Î¼).mp hfg
#align bounded_continuous_function.to_Lp_injective BoundedContinuousFunction.toLp_injective

end BoundedContinuousFunction

namespace ContinuousMap

variable [CompactSpace Î±] [IsFiniteMeasure Î¼]

variable (ğ•œ : Type*) (p Î¼) [Fact (1 â‰¤ p)]

/-- The bounded linear map of considering a continuous function on a compact finite-measure
space `Î±` as an element of `Lp`.  By definition, the norm on `C(Î±, E)` is the sup-norm, transferred
from the space `Î± â†’áµ‡ E` of bounded continuous functions, so this construction is just a matter of
transferring the structure from `BoundedContinuousFunction.toLp` along the isometry. -/
def toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] : C(Î±, E) â†’L[ğ•œ] Lp E p Î¼ :=
  (BoundedContinuousFunction.toLp p Î¼ ğ•œ).comp
    (linearIsometryBoundedOfCompact Î± E ğ•œ).toLinearIsometry.toContinuousLinearMap
#align continuous_map.to_Lp ContinuousMap.toLp

variable {ğ•œ}

theorem range_toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] :
    (LinearMap.range (toLp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] Lp E p Î¼)).toAddSubgroup =
      MeasureTheory.Lp.boundedContinuousFunction E p Î¼ := by
  refine' SetLike.ext' _
  have := (linearIsometryBoundedOfCompact Î± E ğ•œ).surjective
  convert Function.Surjective.range_comp this (BoundedContinuousFunction.toLp (E := E) p Î¼ ğ•œ)
  rw [â† BoundedContinuousFunction.range_toLp p Î¼ (ğ•œ := ğ•œ), Submodule.coe_toAddSubgroup,
    LinearMap.range_coe]
#align continuous_map.range_to_Lp ContinuousMap.range_toLp

variable {p}

theorem coeFn_toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] (f : C(Î±, E)) :
    toLp (E := E) p Î¼ ğ•œ f =áµ[Î¼] f :=
  AEEqFun.coeFn_mk f _
#align continuous_map.coe_fn_to_Lp ContinuousMap.coeFn_toLp

theorem toLp_def [NormedField ğ•œ] [NormedSpace ğ•œ E] (f : C(Î±, E)) :
    toLp (E := E) p Î¼ ğ•œ f =
      BoundedContinuousFunction.toLp (E := E) p Î¼ ğ•œ (linearIsometryBoundedOfCompact Î± E ğ•œ f) :=
  rfl
#align continuous_map.to_Lp_def ContinuousMap.toLp_def

@[simp]
theorem toLp_comp_toContinuousMap [NormedField ğ•œ] [NormedSpace ğ•œ E] (f : Î± â†’áµ‡ E) :
    toLp (E := E) p Î¼ ğ•œ f.toContinuousMap = BoundedContinuousFunction.toLp (E := E) p Î¼ ğ•œ f :=
  rfl
#align continuous_map.to_Lp_comp_to_continuous_map ContinuousMap.toLp_comp_toContinuousMap

@[simp]
theorem coe_toLp [NormedField ğ•œ] [NormedSpace ğ•œ E] (f : C(Î±, E)) :
    (toLp (E := E) p Î¼ ğ•œ f : Î± â†’â‚˜[Î¼] E) = f.toAEEqFun Î¼ :=
  rfl
#align continuous_map.coe_to_Lp ContinuousMap.coe_toLp

theorem toLp_injective [Î¼.IsOpenPosMeasure] [NormedField ğ•œ] [NormedSpace ğ•œ E] :
    Function.Injective (â‡‘(toLp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] Lp E p Î¼)) :=
  (BoundedContinuousFunction.toLp_injective _).comp (linearIsometryBoundedOfCompact Î± E ğ•œ).injective
#align continuous_map.to_Lp_injective ContinuousMap.toLp_injective

theorem toLp_inj {f g : C(Î±, E)} [Î¼.IsOpenPosMeasure] [NormedField ğ•œ] [NormedSpace ğ•œ E] :
    toLp (E := E) p Î¼ ğ•œ f = toLp (E := E) p Î¼ ğ•œ g â†” f = g :=
  (toLp_injective Î¼).eq_iff
#align continuous_map.to_Lp_inj ContinuousMap.toLp_inj

variable {Î¼}

/-- If a sum of continuous functions `g n` is convergent, and the same sum converges in `Láµ–` to `h`,
then in fact `g n` converges uniformly to `h`.  -/
theorem hasSum_of_hasSum_Lp {Î² : Type*} [Î¼.IsOpenPosMeasure] [NormedField ğ•œ] [NormedSpace ğ•œ E]
    {g : Î² â†’ C(Î±, E)} {f : C(Î±, E)} (hg : Summable g)
    (hg2 : HasSum (toLp (E := E) p Î¼ ğ•œ âˆ˜ g) (toLp (E := E) p Î¼ ğ•œ f)) : HasSum g f := by
  convert Summable.hasSum hg
  exact toLp_injective Î¼ (hg2.unique ((toLp p Î¼ ğ•œ).hasSum <| Summable.hasSum hg))
#align continuous_map.has_sum_of_has_sum_Lp ContinuousMap.hasSum_of_hasSum_Lp

variable (Î¼) [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]

theorem toLp_norm_eq_toLp_norm_coe :
    â€–(toLp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] Lp E p Î¼)â€– =
      â€–(BoundedContinuousFunction.toLp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] Lp E p Î¼)â€– :=
  ContinuousLinearMap.op_norm_comp_linearIsometryEquiv _ _
#align continuous_map.to_Lp_norm_eq_to_Lp_norm_coe ContinuousMap.toLp_norm_eq_toLp_norm_coe

/-- Bound for the operator norm of `ContinuousMap.toLp`. -/
theorem toLp_norm_le :
    â€–(toLp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] Lp E p Î¼)â€– â‰¤ measureUnivNNReal Î¼ ^ p.toRealâ»Â¹ := by
  rw [toLp_norm_eq_toLp_norm_coe]
  exact BoundedContinuousFunction.toLp_norm_le Î¼
#align continuous_map.to_Lp_norm_le ContinuousMap.toLp_norm_le

end ContinuousMap

end

namespace MeasureTheory

namespace Lp

theorem pow_mul_meas_ge_le_norm (f : Lp E p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ) (Îµ : â„â‰¥0âˆ) :
    (Îµ * Î¼ { x | Îµ â‰¤ (â€–f xâ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal }) ^ (1 / p.toReal) â‰¤ ENNReal.ofReal â€–fâ€– :=
  (ENNReal.ofReal_toReal (snorm_ne_top f)).symm â–¸
    pow_mul_meas_ge_le_snorm Î¼ hp_ne_zero hp_ne_top (Lp.aestronglyMeasurable f) Îµ
#align measure_theory.Lp.pow_mul_meas_ge_le_norm MeasureTheory.Lp.pow_mul_meas_ge_le_norm

theorem mul_meas_ge_le_pow_norm (f : Lp E p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ) (Îµ : â„â‰¥0âˆ) :
    Îµ * Î¼ { x | Îµ â‰¤ (â€–f xâ€–â‚Š : â„â‰¥0âˆ) ^ p.toReal } â‰¤ ENNReal.ofReal â€–fâ€– ^ p.toReal :=
  (ENNReal.ofReal_toReal (snorm_ne_top f)).symm â–¸
    mul_meas_ge_le_pow_snorm Î¼ hp_ne_zero hp_ne_top (Lp.aestronglyMeasurable f) Îµ
#align measure_theory.Lp.mul_meas_ge_le_pow_norm MeasureTheory.Lp.mul_meas_ge_le_pow_norm

/-- A version of Markov's inequality with elements of Lp. -/
theorem mul_meas_ge_le_pow_norm' (f : Lp E p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
    (Îµ : â„â‰¥0âˆ) : Îµ ^ p.toReal * Î¼ { x | Îµ â‰¤ â€–f xâ€–â‚Š } â‰¤ ENNReal.ofReal â€–fâ€– ^ p.toReal :=
  (ENNReal.ofReal_toReal (snorm_ne_top f)).symm â–¸
    mul_meas_ge_le_pow_snorm' Î¼ hp_ne_zero hp_ne_top (Lp.aestronglyMeasurable f) Îµ
#align measure_theory.Lp.mul_meas_ge_le_pow_norm' MeasureTheory.Lp.mul_meas_ge_le_pow_norm'

theorem meas_ge_le_mul_pow_norm (f : Lp E p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ) {Îµ : â„â‰¥0âˆ}
    (hÎµ : Îµ â‰  0) : Î¼ { x | Îµ â‰¤ â€–f xâ€–â‚Š } â‰¤ Îµâ»Â¹ ^ p.toReal * ENNReal.ofReal â€–fâ€– ^ p.toReal :=
  (ENNReal.ofReal_toReal (snorm_ne_top f)).symm â–¸
    meas_ge_le_mul_pow_snorm Î¼ hp_ne_zero hp_ne_top (Lp.aestronglyMeasurable f) hÎµ
#align measure_theory.Lp.meas_ge_le_mul_pow_norm MeasureTheory.Lp.meas_ge_le_mul_pow_norm

end Lp

end MeasureTheory
