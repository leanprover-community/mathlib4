/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic

/-!
# Egorov theorem

This file contains the Egorov theorem which states that an almost everywhere convergent
sequence on a finite measure space converges uniformly except on an arbitrarily small set.
This theorem is useful for the Vitali convergence theorem as well as theorems regarding
convergence in measure.

## Main results

* `MeasureTheory.tendstoUniformlyOn_of_ae_tendsto`: Egorov's theorem which shows that a sequence of
  almost everywhere convergent functions converges uniformly except on an arbitrarily small set.

-/


noncomputable section

open MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

open Set Filter TopologicalSpace

variable {Î± Î² Î¹ : Type*} {m : MeasurableSpace Î±} [PseudoEMetricSpace Î²] {Î¼ : Measure Î±}

namespace Egorov

/-- Given a sequence of functions `f` and a function `g`, `notConvergentSeq f g n j` is the
set of elements such that `f k x` and `g x` are separated by at least `1 / (n + 1)` for some
`k â‰¥ j`.

This definition is useful for Egorov's theorem. -/
def notConvergentSeq [Preorder Î¹] (f : Î¹ â†’ Î± â†’ Î²) (g : Î± â†’ Î²) (n : â„•) (j : Î¹) : Set Î± :=
  â‹ƒ (k) (_ : j â‰¤ k), { x | (n : â„â‰¥0âˆ)â»Â¹ < edist (f k x) (g x) }

variable {n : â„•} {j : Î¹} {s : Set Î±} {Îµ : â„} {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²}

theorem mem_notConvergentSeq_iff [Preorder Î¹] {x : Î±} :
    x âˆˆ notConvergentSeq f g n j â†” âˆƒ k â‰¥ j, (n : â„â‰¥0âˆ)â»Â¹ < edist (f k x) (g x) := by
  simp_rw [notConvergentSeq, Set.mem_iUnion, exists_prop, mem_setOf]

theorem notConvergentSeq_antitone [Preorder Î¹] : Antitone (notConvergentSeq f g n) :=
  fun _ _ hjk => Set.iUnionâ‚‚_mono' fun l hl => âŸ¨l, le_trans hjk hl, Set.Subset.rflâŸ©

theorem measure_inter_notConvergentSeq_eq_zero [SemilatticeSup Î¹] [Nonempty Î¹]
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    Î¼ (s âˆ© â‹‚ j, notConvergentSeq f g n j) = 0 := by
  simp_rw [EMetric.tendsto_atTop, ae_iff] at hfg
  rw [â† nonpos_iff_eq_zero, â† hfg]
  refine measure_mono fun x => ?_
  simp only [Set.mem_inter_iff, Set.mem_iInter, mem_notConvergentSeq_iff]
  push_neg
  rintro âŸ¨hmem, hxâŸ©
  refine âŸ¨hmem, (n : â„â‰¥0âˆ)â»Â¹, by simp, fun N => ?_âŸ©
  obtain âŸ¨n, hnâ‚, hnâ‚‚âŸ© := hx N
  exact âŸ¨n, hnâ‚, hnâ‚‚.leâŸ©

theorem notConvergentSeq_measurableSet [Preorder Î¹] [Countable Î¹]
    (hf : âˆ€ n, StronglyMeasurable[m] (f n)) (hg : StronglyMeasurable g) :
    MeasurableSet (notConvergentSeq f g n j) :=
  MeasurableSet.iUnion fun k =>
    MeasurableSet.iUnion fun _ =>
      StronglyMeasurable.measurableSet_lt stronglyMeasurable_const <| (hf k).edist hg

theorem measure_notConvergentSeq_tendsto_zero [SemilatticeSup Î¹] [Countable Î¹]
    (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g) (hsm : MeasurableSet s)
    (hs : Î¼ s â‰  âˆ) (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    Tendsto (fun j => Î¼ (s âˆ© notConvergentSeq f g n j)) atTop (ğ“ 0) := by
  rcases isEmpty_or_nonempty Î¹ with h | h
  Â· have : (fun j => Î¼ (s âˆ© notConvergentSeq f g n j)) = fun j => 0 := by
      simp only [eq_iff_true_of_subsingleton]
    rw [this]
    exact tendsto_const_nhds
  rw [â† measure_inter_notConvergentSeq_eq_zero hfg n, Set.inter_iInter]
  refine tendsto_measure_iInter_atTop
    (fun n â†¦ (hsm.inter <| notConvergentSeq_measurableSet hf hg).nullMeasurableSet)
    (fun k l hkl => Set.inter_subset_inter_right _ <| notConvergentSeq_antitone hkl)
    âŸ¨h.some, ne_top_of_le_ne_top hs (measure_mono Set.inter_subset_left)âŸ©

variable [SemilatticeSup Î¹] [Nonempty Î¹] [Countable Î¹]

theorem exists_notConvergentSeq_lt (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    âˆƒ j : Î¹, Î¼ (s âˆ© notConvergentSeq f g n j) â‰¤ ENNReal.ofReal (Îµ * 2â»Â¹ ^ n) := by
  have âŸ¨N, hNâŸ© := (ENNReal.tendsto_atTop ENNReal.zero_ne_top).1
    (measure_notConvergentSeq_tendsto_zero hf hg hsm hs hfg n) (.ofReal (Îµ * 2â»Â¹ ^ n))
      (by positivity)
  rw [zero_add] at hN
  exact âŸ¨N, (hN N le_rfl).2âŸ©

/-- Given some `Îµ > 0`, `notConvergentSeqLTIndex` provides the index such that
`notConvergentSeq` (intersected with a set of finite measure) has measure less than
`Îµ * 2â»Â¹ ^ n`.

This definition is useful for Egorov's theorem. -/
def notConvergentSeqLTIndex (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) : Î¹ :=
  Classical.choose <| exists_notConvergentSeq_lt hÎµ hf hg hsm hs hfg n

theorem notConvergentSeqLTIndex_spec (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    Î¼ (s âˆ© notConvergentSeq f g n (notConvergentSeqLTIndex hÎµ hf hg hsm hs hfg n)) â‰¤
      ENNReal.ofReal (Îµ * 2â»Â¹ ^ n) :=
  Classical.choose_spec <| exists_notConvergentSeq_lt hÎµ hf hg hsm hs hfg n

/-- Given some `Îµ > 0`, `iUnionNotConvergentSeq` is the union of `notConvergentSeq` with
specific indices such that `iUnionNotConvergentSeq` has measure less equal than `Îµ`.

This definition is useful for Egorov's theorem. -/
def iUnionNotConvergentSeq (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) : Set Î± :=
  â‹ƒ n, s âˆ© notConvergentSeq f g n (notConvergentSeqLTIndex (half_pos hÎµ) hf hg hsm hs hfg n)

theorem iUnionNotConvergentSeq_measurableSet (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    MeasurableSet <| iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg :=
  MeasurableSet.iUnion fun _ => hsm.inter <| notConvergentSeq_measurableSet hf hg

theorem measure_iUnionNotConvergentSeq (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    Î¼ (iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg) â‰¤ ENNReal.ofReal Îµ := by
  refine le_trans (measure_iUnion_le _) (le_trans
    (ENNReal.tsum_le_tsum <| notConvergentSeqLTIndex_spec (half_pos hÎµ) hf hg hsm hs hfg) ?_)
  simp_rw [ENNReal.ofReal_mul (half_pos hÎµ).le]
  rw [ENNReal.tsum_mul_left, â† ENNReal.ofReal_tsum_of_nonneg, inv_eq_one_div, tsum_geometric_two,
    â† ENNReal.ofReal_mul (half_pos hÎµ).le, div_mul_cancelâ‚€ Îµ two_ne_zero]
  Â· intro n; positivity
  Â· rw [inv_eq_one_div]
    exact summable_geometric_two

theorem iUnionNotConvergentSeq_subset (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg âŠ† s := by
  rw [iUnionNotConvergentSeq, â† Set.inter_iUnion]
  exact Set.inter_subset_left

theorem tendstoUniformlyOn_diff_iUnionNotConvergentSeq (hÎµ : 0 < Îµ)
    (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g) (hsm : MeasurableSet s)
    (hs : Î¼ s â‰  âˆ) (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    TendstoUniformlyOn f g atTop (s \ Egorov.iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg) := by
  rw [EMetric.tendstoUniformlyOn_iff]
  intro Î´ hÎ´
  obtain âŸ¨N, hNâŸ© := ENNReal.exists_inv_nat_lt hÎ´.ne'
  rw [eventually_atTop]
  refine âŸ¨Egorov.notConvergentSeqLTIndex (half_pos hÎµ) hf hg hsm hs hfg N, fun n hn x hx => ?_âŸ©
  simp only [Set.mem_diff, Egorov.iUnionNotConvergentSeq, not_exists, Set.mem_iUnion,
    Set.mem_inter_iff, not_and, exists_and_left] at hx
  obtain âŸ¨hxs, hxâŸ© := hx
  specialize hx hxs N
  rw [Egorov.mem_notConvergentSeq_iff] at hx
  push_neg at hx
  rw [edist_comm]
  exact lt_of_le_of_lt (hx n hn) hN

end Egorov

variable [SemilatticeSup Î¹] [Nonempty Î¹] [Countable Î¹]
  {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²} {s : Set Î±}

/-- **Egorov's theorem**: If `f : Î¹ â†’ Î± â†’ Î²` is a sequence of strongly measurable functions that
converges to `g : Î± â†’ Î²` almost everywhere on a measurable set `s` of finite measure,
then for all `Îµ > 0`, there exists a subset `t âŠ† s` such that `Î¼ t â‰¤ Îµ` and `f` converges to `g`
uniformly on `s \ t`. We require the index type `Î¹` to be countable, and usually `Î¹ = â„•`.

In other words, a sequence of almost everywhere convergent functions converges uniformly except on
an arbitrarily small set. -/
theorem tendstoUniformlyOn_of_ae_tendsto (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ t âŠ† s, MeasurableSet t âˆ§ Î¼ t â‰¤ ENNReal.ofReal Îµ âˆ§ TendstoUniformlyOn f g atTop (s \ t) :=
  âŸ¨Egorov.iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg,
    Egorov.iUnionNotConvergentSeq_subset hÎµ hf hg hsm hs hfg,
    Egorov.iUnionNotConvergentSeq_measurableSet hÎµ hf hg hsm hs hfg,
    Egorov.measure_iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg,
    Egorov.tendstoUniformlyOn_diff_iUnionNotConvergentSeq hÎµ hf hg hsm hs hfgâŸ©

/-- Egorov's theorem for finite measure spaces. -/
theorem tendstoUniformlyOn_of_ae_tendsto' [IsFiniteMeasure Î¼] (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) {Îµ : â„}
    (hÎµ : 0 < Îµ) :
    âˆƒ t, MeasurableSet t âˆ§ Î¼ t â‰¤ ENNReal.ofReal Îµ âˆ§ TendstoUniformlyOn f g atTop tá¶œ := by
  have âŸ¨t, _, ht, htendstoâŸ© := tendstoUniformlyOn_of_ae_tendsto hf hg MeasurableSet.univ
    (measure_ne_top Î¼ Set.univ) (by filter_upwards [hfg] with _ htendsto _ using htendsto) hÎµ
  refine âŸ¨_, ht, ?_âŸ©
  rwa [Set.compl_eq_univ_diff]

end MeasureTheory
