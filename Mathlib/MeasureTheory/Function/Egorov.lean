/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic

#align_import measure_theory.function.egorov from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Egorov theorem

This file contains the Egorov theorem which states that an almost everywhere convergent
sequence on a finite measure space converges uniformly except on an arbitrarily small set.
This theorem is useful for the Vitali convergence theorem as well as theorems regarding
convergence in measure.

## Main results

* `MeasureTheory.Egorov`: Egorov's theorem which shows that a sequence of almost everywhere
  convergent functions converges uniformly except on an arbitrarily small set.

-/


noncomputable section

open Classical MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

open Set Filter TopologicalSpace

variable {Î± Î² Î¹ : Type*} {m : MeasurableSpace Î±} [MetricSpace Î²] {Î¼ : Measure Î±}

namespace Egorov

/-- Given a sequence of functions `f` and a function `g`, `notConvergentSeq f g n j` is the
set of elements such that `f k x` and `g x` are separated by at least `1 / (n + 1)` for some
`k â‰¥ j`.

This definition is useful for Egorov's theorem. -/
def notConvergentSeq [Preorder Î¹] (f : Î¹ â†’ Î± â†’ Î²) (g : Î± â†’ Î²) (n : â„•) (j : Î¹) : Set Î± :=
  â‹ƒ (k) (_ : j â‰¤ k), { x | 1 / (n + 1 : â„) < dist (f k x) (g x) }
#align measure_theory.egorov.not_convergent_seq MeasureTheory.Egorov.notConvergentSeq

variable {n : â„•} {i j : Î¹} {s : Set Î±} {Îµ : â„} {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²}

theorem mem_notConvergentSeq_iff [Preorder Î¹] {x : Î±} :
    x âˆˆ notConvergentSeq f g n j â†” âˆƒ (k : _) (_ : j â‰¤ k), 1 / (n + 1 : â„) < dist (f k x) (g x) := by
  simp_rw [notConvergentSeq, Set.mem_iUnion]
  -- âŠ¢ (âˆƒ i i_1, x âˆˆ {x | 1 / (â†‘n + 1) < dist (f i x) (g x)}) â†” âˆƒ k x_1, 1 / (â†‘n +  â€¦
  rfl
  -- ğŸ‰ no goals
#align measure_theory.egorov.mem_not_convergent_seq_iff MeasureTheory.Egorov.mem_notConvergentSeq_iff

theorem notConvergentSeq_antitone [Preorder Î¹] : Antitone (notConvergentSeq f g n) :=
  fun _ _ hjk => Set.iUnionâ‚‚_mono' fun l hl => âŸ¨l, le_trans hjk hl, Set.Subset.rflâŸ©
#align measure_theory.egorov.not_convergent_seq_antitone MeasureTheory.Egorov.notConvergentSeq_antitone

theorem measure_inter_notConvergentSeq_eq_zero [SemilatticeSup Î¹] [Nonempty Î¹]
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    Î¼ (s âˆ© â‹‚ j, notConvergentSeq f g n j) = 0 := by
  simp_rw [Metric.tendsto_atTop, ae_iff] at hfg
  -- âŠ¢ â†‘â†‘Î¼ (s âˆ© â‹‚ (j : Î¹), notConvergentSeq f g n j) = 0
  rw [â† nonpos_iff_eq_zero, â† hfg]
  -- âŠ¢ â†‘â†‘Î¼ (s âˆ© â‹‚ (j : Î¹), notConvergentSeq f g n j) â‰¤ â†‘â†‘Î¼ {a | Â¬(a âˆˆ s â†’ âˆ€ (Îµ : â„) â€¦
  refine' measure_mono fun x => _
  -- âŠ¢ x âˆˆ s âˆ© â‹‚ (j : Î¹), notConvergentSeq f g n j â†’ x âˆˆ {a | Â¬(a âˆˆ s â†’ âˆ€ (Îµ : â„),  â€¦
  simp only [Set.mem_inter_iff, Set.mem_iInter, ge_iff_le, mem_notConvergentSeq_iff]
  -- âŠ¢ (x âˆˆ s âˆ§ âˆ€ (i : Î¹), âˆƒ k x_1, 1 / (â†‘n + 1) < dist (f k x) (g x)) â†’ x âˆˆ {a | Â¬ â€¦
  push_neg
  -- âŠ¢ (x âˆˆ s âˆ§ âˆ€ (i : Î¹), âˆƒ k x_1, 1 / (â†‘n + 1) < dist (f k x) (g x)) â†’ x âˆˆ {a | a â€¦
  rintro âŸ¨hmem, hxâŸ©
  -- âŠ¢ x âˆˆ {a | a âˆˆ s âˆ§ âˆƒ Îµ, Îµ > 0 âˆ§ âˆ€ (N : Î¹), âˆƒ n, N â‰¤ n âˆ§ Îµ â‰¤ dist (f n a) (g a)}
  refine' âŸ¨hmem, 1 / (n + 1 : â„), Nat.one_div_pos_of_nat, fun N => _âŸ©
  -- âŠ¢ âˆƒ n_1, N â‰¤ n_1 âˆ§ 1 / (â†‘n + 1) â‰¤ dist (f n_1 x) (g x)
  obtain âŸ¨n, hnâ‚, hnâ‚‚âŸ© := hx N
  -- âŠ¢ âˆƒ n, N â‰¤ n âˆ§ 1 / (â†‘nâœ + 1) â‰¤ dist (f n x) (g x)
  exact âŸ¨n, hnâ‚, hnâ‚‚.leâŸ©
  -- ğŸ‰ no goals
#align measure_theory.egorov.measure_inter_not_convergent_seq_eq_zero MeasureTheory.Egorov.measure_inter_notConvergentSeq_eq_zero

theorem notConvergentSeq_measurableSet [Preorder Î¹] [Countable Î¹]
    (hf : âˆ€ n, StronglyMeasurable[m] (f n)) (hg : StronglyMeasurable g) :
    MeasurableSet (notConvergentSeq f g n j) :=
  MeasurableSet.iUnion fun k =>
    MeasurableSet.iUnion fun _ =>
      StronglyMeasurable.measurableSet_lt stronglyMeasurable_const <| (hf k).dist hg
#align measure_theory.egorov.not_convergent_seq_measurable_set MeasureTheory.Egorov.notConvergentSeq_measurableSet

theorem measure_notConvergentSeq_tendsto_zero [SemilatticeSup Î¹] [Countable Î¹]
    (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g) (hsm : MeasurableSet s)
    (hs : Î¼ s â‰  âˆ) (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    Tendsto (fun j => Î¼ (s âˆ© notConvergentSeq f g n j)) atTop (ğ“ 0) := by
  cases' isEmpty_or_nonempty Î¹ with h h
  -- âŠ¢ Tendsto (fun j => â†‘â†‘Î¼ (s âˆ© notConvergentSeq f g n j)) atTop (ğ“ 0)
  Â· have : (fun j => Î¼ (s âˆ© notConvergentSeq f g n j)) = fun j => 0 := by
      simp only [eq_iff_true_of_subsingleton]
    rw [this]
    -- âŠ¢ Tendsto (fun j => 0) atTop (ğ“ 0)
    exact tendsto_const_nhds
    -- ğŸ‰ no goals
  rw [â† measure_inter_notConvergentSeq_eq_zero hfg n, Set.inter_iInter]
  -- âŠ¢ Tendsto (fun j => â†‘â†‘Î¼ (s âˆ© notConvergentSeq f g n j)) atTop (ğ“ (â†‘â†‘Î¼ (â‹‚ (i :  â€¦
  refine' tendsto_measure_iInter (fun n => hsm.inter <| notConvergentSeq_measurableSet hf hg)
    (fun k l hkl => Set.inter_subset_inter_right _ <| notConvergentSeq_antitone hkl)
    âŸ¨h.some,
      (lt_of_le_of_lt (measure_mono <| Set.inter_subset_left _ _) (lt_top_iff_ne_top.2 hs)).neâŸ©
#align measure_theory.egorov.measure_not_convergent_seq_tendsto_zero MeasureTheory.Egorov.measure_notConvergentSeq_tendsto_zero

variable [SemilatticeSup Î¹] [Nonempty Î¹] [Countable Î¹]

theorem exists_notConvergentSeq_lt (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    âˆƒ j : Î¹, Î¼ (s âˆ© notConvergentSeq f g n j) â‰¤ ENNReal.ofReal (Îµ * 2â»Â¹ ^ n) := by
  have âŸ¨N, hNâŸ© := (ENNReal.tendsto_atTop ENNReal.zero_ne_top).1
    (measure_notConvergentSeq_tendsto_zero hf hg hsm hs hfg n) (ENNReal.ofReal (Îµ * 2â»Â¹ ^ n)) (by
      rw [gt_iff_lt, ENNReal.ofReal_pos]
      exact mul_pos hÎµ (pow_pos (by norm_num) n))
  rw [zero_add] at hN
  -- âŠ¢ âˆƒ j, â†‘â†‘Î¼ (s âˆ© notConvergentSeq f g n j) â‰¤ ENNReal.ofReal (Îµ * 2â»Â¹ ^ n)
  exact âŸ¨N, (hN N le_rfl).2âŸ©
  -- ğŸ‰ no goals
#align measure_theory.egorov.exists_not_convergent_seq_lt MeasureTheory.Egorov.exists_notConvergentSeq_lt

/-- Given some `Îµ > 0`, `notConvergentSeqLTIndex` provides the index such that
`notConvergentSeq` (intersected with a set of finite measure) has measure less than
`Îµ * 2â»Â¹ ^ n`.

This definition is useful for Egorov's theorem. -/
def notConvergentSeqLTIndex (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) : Î¹ :=
  Classical.choose <| exists_notConvergentSeq_lt hÎµ hf hg hsm hs hfg n
#align measure_theory.egorov.not_convergent_seq_lt_index MeasureTheory.Egorov.notConvergentSeqLTIndex

theorem notConvergentSeqLTIndex_spec (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) (n : â„•) :
    Î¼ (s âˆ© notConvergentSeq f g n (notConvergentSeqLTIndex hÎµ hf hg hsm hs hfg n)) â‰¤
      ENNReal.ofReal (Îµ * 2â»Â¹ ^ n) :=
  Classical.choose_spec <| exists_notConvergentSeq_lt hÎµ hf hg hsm hs hfg n
#align measure_theory.egorov.not_convergent_seq_lt_index_spec MeasureTheory.Egorov.notConvergentSeqLTIndex_spec

/-- Given some `Îµ > 0`, `iUnionNotConvergentSeq` is the union of `notConvergentSeq` with
specific indices such that `iUnionNotConvergentSeq` has measure less equal than `Îµ`.

This definition is useful for Egorov's theorem. -/
def iUnionNotConvergentSeq (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) : Set Î± :=
  â‹ƒ n, s âˆ© notConvergentSeq f g n (notConvergentSeqLTIndex (half_pos hÎµ) hf hg hsm hs hfg n)
#align measure_theory.egorov.Union_not_convergent_seq MeasureTheory.Egorov.iUnionNotConvergentSeq

theorem iUnionNotConvergentSeq_measurableSet (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    MeasurableSet <| iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg :=
  MeasurableSet.iUnion fun _ => hsm.inter <| notConvergentSeq_measurableSet hf hg
#align measure_theory.egorov.Union_not_convergent_seq_measurable_set MeasureTheory.Egorov.iUnionNotConvergentSeq_measurableSet

theorem measure_iUnionNotConvergentSeq (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    Î¼ (iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg) â‰¤ ENNReal.ofReal Îµ := by
  refine' le_trans (measure_iUnion_le _) (le_trans
    (ENNReal.tsum_le_tsum <| notConvergentSeqLTIndex_spec (half_pos hÎµ) hf hg hsm hs hfg) _)
  simp_rw [ENNReal.ofReal_mul (half_pos hÎµ).le]
  -- âŠ¢ âˆ‘' (a : â„•), ENNReal.ofReal (Îµ / 2) * ENNReal.ofReal (2â»Â¹ ^ a) â‰¤ ENNReal.ofRe â€¦
  rw [ENNReal.tsum_mul_left, â† ENNReal.ofReal_tsum_of_nonneg, inv_eq_one_div, tsum_geometric_two,
    â† ENNReal.ofReal_mul (half_pos hÎµ).le, div_mul_cancel Îµ two_ne_zero]
  Â· exact fun n => pow_nonneg (by norm_num) _
    -- ğŸ‰ no goals
  Â· rw [inv_eq_one_div]
    -- âŠ¢ Summable fun i => (1 / 2) ^ i
    exact summable_geometric_two
    -- ğŸ‰ no goals
#align measure_theory.egorov.measure_Union_not_convergent_seq MeasureTheory.Egorov.measure_iUnionNotConvergentSeq

theorem iUnionNotConvergentSeq_subset (hÎµ : 0 < Îµ) (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg âŠ† s := by
  rw [iUnionNotConvergentSeq, â† Set.inter_iUnion]
  -- âŠ¢ s âˆ© â‹ƒ (i : â„•), notConvergentSeq (fun n => f n) g i (notConvergentSeqLTIndex  â€¦
  exact Set.inter_subset_left _ _
  -- ğŸ‰ no goals
#align measure_theory.egorov.Union_not_convergent_seq_subset MeasureTheory.Egorov.iUnionNotConvergentSeq_subset

theorem tendstoUniformlyOn_diff_iUnionNotConvergentSeq (hÎµ : 0 < Îµ)
    (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g) (hsm : MeasurableSet s)
    (hs : Î¼ s â‰  âˆ) (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    TendstoUniformlyOn f g atTop (s \ Egorov.iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg) := by
  rw [Metric.tendstoUniformlyOn_iff]
  -- âŠ¢ âˆ€ (Îµ_1 : â„), Îµ_1 > 0 â†’ âˆ€á¶  (n : Î¹) in atTop, âˆ€ (x : Î±), x âˆˆ s \ iUnionNotConv â€¦
  intro Î´ hÎ´
  -- âŠ¢ âˆ€á¶  (n : Î¹) in atTop, âˆ€ (x : Î±), x âˆˆ s \ iUnionNotConvergentSeq hÎµ hf hg hsm  â€¦
  obtain âŸ¨N, hNâŸ© := exists_nat_one_div_lt hÎ´
  -- âŠ¢ âˆ€á¶  (n : Î¹) in atTop, âˆ€ (x : Î±), x âˆˆ s \ iUnionNotConvergentSeq hÎµ hf hg hsm  â€¦
  rw [eventually_atTop]
  -- âŠ¢ âˆƒ a, âˆ€ (b : Î¹), b â‰¥ a â†’ âˆ€ (x : Î±), x âˆˆ s \ iUnionNotConvergentSeq hÎµ hf hg h â€¦
  refine' âŸ¨Egorov.notConvergentSeqLTIndex (half_pos hÎµ) hf hg hsm hs hfg N, fun n hn x hx => _âŸ©
  -- âŠ¢ dist (g x) (f n x) < Î´
  simp only [Set.mem_diff, Egorov.iUnionNotConvergentSeq, not_exists, Set.mem_iUnion,
    Set.mem_inter_iff, not_and, exists_and_left] at hx
  obtain âŸ¨hxs, hxâŸ© := hx
  -- âŠ¢ dist (g x) (f n x) < Î´
  specialize hx hxs N
  -- âŠ¢ dist (g x) (f n x) < Î´
  rw [Egorov.mem_notConvergentSeq_iff] at hx
  -- âŠ¢ dist (g x) (f n x) < Î´
  push_neg at hx
  -- âŠ¢ dist (g x) (f n x) < Î´
  rw [dist_comm]
  -- âŠ¢ dist (f n x) (g x) < Î´
  exact lt_of_le_of_lt (hx n hn) hN
  -- ğŸ‰ no goals
#align measure_theory.egorov.tendsto_uniformly_on_diff_Union_not_convergent_seq MeasureTheory.Egorov.tendstoUniformlyOn_diff_iUnionNotConvergentSeq

end Egorov

variable [SemilatticeSup Î¹] [Nonempty Î¹] [Countable Î¹] {Î³ : Type*} [TopologicalSpace Î³]
  {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²} {s : Set Î±}

/-- **Egorov's theorem**: If `f : Î¹ â†’ Î± â†’ Î²` is a sequence of strongly measurable functions that
converges to `g : Î± â†’ Î²` almost everywhere on a measurable set `s` of finite measure,
then for all `Îµ > 0`, there exists a subset `t âŠ† s` such that `Î¼ t â‰¤ Îµ` and `f` converges to `g`
uniformly on `s \ t`. We require the index type `Î¹` to be countable, and usually `Î¹ = â„•`.

In other words, a sequence of almost everywhere convergent functions converges uniformly except on
an arbitrarily small set. -/
theorem tendstoUniformlyOn_of_ae_tendsto (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hsm : MeasurableSet s) (hs : Î¼ s â‰  âˆ)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ Tendsto (fun n => f n x) atTop (ğ“ (g x))) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ (t : _) (_ : t âŠ† s),
      MeasurableSet t âˆ§ Î¼ t â‰¤ ENNReal.ofReal Îµ âˆ§ TendstoUniformlyOn f g atTop (s \ t) :=
  âŸ¨Egorov.iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg,
    Egorov.iUnionNotConvergentSeq_subset hÎµ hf hg hsm hs hfg,
    Egorov.iUnionNotConvergentSeq_measurableSet hÎµ hf hg hsm hs hfg,
    Egorov.measure_iUnionNotConvergentSeq hÎµ hf hg hsm hs hfg,
    Egorov.tendstoUniformlyOn_diff_iUnionNotConvergentSeq hÎµ hf hg hsm hs hfgâŸ©
#align measure_theory.tendsto_uniformly_on_of_ae_tendsto MeasureTheory.tendstoUniformlyOn_of_ae_tendsto

/-- Egorov's theorem for finite measure spaces. -/
theorem tendstoUniformlyOn_of_ae_tendsto' [IsFiniteMeasure Î¼] (hf : âˆ€ n, StronglyMeasurable (f n))
    (hg : StronglyMeasurable g) (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) {Îµ : â„}
    (hÎµ : 0 < Îµ) :
    âˆƒ t, MeasurableSet t âˆ§ Î¼ t â‰¤ ENNReal.ofReal Îµ âˆ§ TendstoUniformlyOn f g atTop tá¶œ := by
  have âŸ¨t, _, ht, htendstoâŸ© := tendstoUniformlyOn_of_ae_tendsto hf hg MeasurableSet.univ
    (measure_ne_top Î¼ Set.univ) (by filter_upwards [hfg] with _ htendsto _ using htendsto) hÎµ
  refine' âŸ¨_, ht, _âŸ©
  -- âŠ¢ â†‘â†‘Î¼ t â‰¤ ENNReal.ofReal Îµ âˆ§ TendstoUniformlyOn f g atTop tá¶œ
  rwa [Set.compl_eq_univ_diff]
  -- ğŸ‰ no goals
#align measure_theory.tendsto_uniformly_on_of_ae_tendsto' MeasureTheory.tendstoUniformlyOn_of_ae_tendsto'

end MeasureTheory
