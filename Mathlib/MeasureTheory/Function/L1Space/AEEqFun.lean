/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import Mathlib.MeasureTheory.Function.L1Space.Integrable

/-!
# `LÂ¹` space

In this file we establish an API between `Integrable` and the space `LÂ¹` of equivalence
classes of integrable functions, already defined as a special case of `L^p` spaces for `p = 1`.

## Notation

* `Î± â†’â‚[Î¼] Î²` is the type of `LÂ¹` space, where `Î±` is a `MeasureSpace` and `Î²` is a
  `NormedAddCommGroup`. `f : Î± â†’â‚˜ Î²` is a "function" in `LÂ¹`.
  In comments, `[f]` is also used to denote an `LÂ¹` function.

  `â‚` can be typed as `\1`.

## Tags

function space, l1

-/

noncomputable section

open EMetric ENNReal Filter MeasureTheory NNReal Set

variable {Î± Î² Îµ Îµ' : Type*} {m : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±}
variable [NormedAddCommGroup Î²] [TopologicalSpace Îµ] [ContinuousENorm Îµ]
  [TopologicalSpace Îµ'] [ENormedAddMonoid Îµ']

namespace MeasureTheory

namespace AEEqFun

section

/-- A class of almost everywhere equal functions is `Integrable` if its function representative
is integrable. -/
def Integrable (f : Î± â†’â‚˜[Î¼] Îµ) : Prop :=
  MeasureTheory.Integrable f Î¼

theorem integrable_mk {f : Î± â†’ Îµ} (hf : AEStronglyMeasurable f Î¼) :
    Integrable (mk f hf : Î± â†’â‚˜[Î¼] Îµ) â†” MeasureTheory.Integrable f Î¼ := by
  simp only [Integrable]
  apply integrable_congr
  exact coeFn_mk f hf

theorem integrable_coeFn {f : Î± â†’â‚˜[Î¼] Îµ} : MeasureTheory.Integrable f Î¼ â†” Integrable f := by
  rw [â† integrable_mk, mk_coeFn]

theorem integrable_zero : Integrable (0 : Î± â†’â‚˜[Î¼] Îµ') :=
  (MeasureTheory.integrable_zero Î± Îµ' Î¼).congr (coeFn_mk _ _).symm

end

section

theorem Integrable.neg {f : Î± â†’â‚˜[Î¼] Î²} : Integrable f â†’ Integrable (-f) :=
  induction_on f fun _f hfm hfi => (integrable_mk _).2 ((integrable_mk hfm).1 hfi).neg

section

theorem integrable_iff_mem_L1 {f : Î± â†’â‚˜[Î¼] Î²} : Integrable f â†” f âˆˆ (Î± â†’â‚[Î¼] Î²) := by
  rw [â† integrable_coeFn, â† memLp_one_iff_integrable, Lp.mem_Lp_iff_memLp]

-- TODO: generalise these lemmas to `ENormedSpace` or similar
theorem Integrable.add {f g : Î± â†’â‚˜[Î¼] Î²} : Integrable f â†’ Integrable g â†’ Integrable (f + g) := by
  refine induction_onâ‚‚ f g fun f hf g hg hfi hgi => ?_
  simp only [integrable_mk, mk_add_mk] at hfi hgi âŠ¢
  exact hfi.add hgi

theorem Integrable.sub {f g : Î± â†’â‚˜[Î¼] Î²} (hf : Integrable f) (hg : Integrable g) :
    Integrable (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

end

section IsBoundedSMul

variable {ğ•œ : Type*} [NormedRing ğ•œ] [Module ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]

theorem Integrable.smul {c : ğ•œ} {f : Î± â†’â‚˜[Î¼] Î²} : Integrable f â†’ Integrable (c â€¢ f) :=
  induction_on f fun _f hfm hfi => (integrable_mk _).2 <|
    by simpa using ((integrable_mk hfm).1 hfi).smul c

end IsBoundedSMul

end

end AEEqFun

namespace L1


theorem integrable_coeFn (f : Î± â†’â‚[Î¼] Î²) : Integrable f Î¼ := by
  rw [â† memLp_one_iff_integrable]
  exact Lp.memLp f

theorem hasFiniteIntegral_coeFn (f : Î± â†’â‚[Î¼] Î²) : HasFiniteIntegral f Î¼ :=
  (integrable_coeFn f).hasFiniteIntegral

@[fun_prop]
theorem stronglyMeasurable_coeFn (f : Î± â†’â‚[Î¼] Î²) : StronglyMeasurable f :=
  Lp.stronglyMeasurable f

@[fun_prop]
theorem measurable_coeFn [MeasurableSpace Î²] [BorelSpace Î²] (f : Î± â†’â‚[Î¼] Î²) : Measurable f :=
  (Lp.stronglyMeasurable f).measurable

@[fun_prop]
theorem aestronglyMeasurable_coeFn (f : Î± â†’â‚[Î¼] Î²) : AEStronglyMeasurable f Î¼ :=
  Lp.aestronglyMeasurable f

@[fun_prop]
theorem aemeasurable_coeFn [MeasurableSpace Î²] [BorelSpace Î²] (f : Î± â†’â‚[Î¼] Î²) : AEMeasurable f Î¼ :=
  (Lp.stronglyMeasurable f).measurable.aemeasurable

theorem edist_def (f g : Î± â†’â‚[Î¼] Î²) : edist f g = âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ := by
  simp only [Lp.edist_def, eLpNorm, one_ne_zero, eLpNorm'_eq_lintegral_enorm, Pi.sub_apply,
    toReal_one, ENNReal.rpow_one, ne_eq, not_false_eq_true, div_self, ite_false]
  simp [edist_eq_enorm_sub]

theorem dist_def (f g : Î± â†’â‚[Î¼] Î²) : dist f g = (âˆ«â» a, edist (f a) (g a) âˆ‚Î¼).toReal := by
  simp_rw [dist_edist, edist_def]

theorem norm_def (f : Î± â†’â‚[Î¼] Î²) : â€–fâ€– = (âˆ«â» a, â€–f aâ€–â‚‘ âˆ‚Î¼).toReal := by
  simp [Lp.norm_def, eLpNorm, eLpNorm'_eq_lintegral_enorm]

/-- Computing the norm of a difference between two LÂ¹-functions. Note that this is not a
  special case of `norm_def` since `(f - g) x` and `f x - g x` are not equal
  (but only a.e.-equal). -/
theorem norm_sub_eq_lintegral (f g : Î± â†’â‚[Î¼] Î²) : â€–f - gâ€– = (âˆ«â» x, â€–f x - g xâ€–â‚‘ âˆ‚Î¼).toReal := by
  rw [norm_def]
  congr 1
  rw [lintegral_congr_ae]
  filter_upwards [Lp.coeFn_sub f g] with _ ha
  simp only [ha, Pi.sub_apply]

theorem ofReal_norm_eq_lintegral (f : Î± â†’â‚[Î¼] Î²) : ENNReal.ofReal â€–fâ€– = âˆ«â» x, â€–f xâ€–â‚‘ âˆ‚Î¼ := by
  rw [norm_def, ENNReal.ofReal_toReal]
  exact ne_of_lt (hasFiniteIntegral_coeFn f)

/-- Computing the norm of a difference between two LÂ¹-functions. Note that this is not a
  special case of `ofReal_norm_eq_lintegral` since `(f - g) x` and `f x - g x` are not equal
  (but only a.e.-equal). -/
theorem ofReal_norm_sub_eq_lintegral (f g : Î± â†’â‚[Î¼] Î²) :
    ENNReal.ofReal â€–f - gâ€– = âˆ«â» x, â€–f x - g xâ€–â‚‘ âˆ‚Î¼ := by
  simp_rw [ofReal_norm_eq_lintegral, â† edist_zero_eq_enorm]
  apply lintegral_congr_ae
  filter_upwards [Lp.coeFn_sub f g] with _ ha
  simp only [ha, Pi.sub_apply]

end L1

namespace Integrable


/-- Construct the equivalence class `[f]` of an integrable function `f`, as a member of the
space `Lp Î² 1 Î¼`. -/
def toL1 (f : Î± â†’ Î²) (hf : Integrable f Î¼) : Î± â†’â‚[Î¼] Î² :=
  (memLp_one_iff_integrable.2 hf).toLp f

@[simp]
theorem toL1_coeFn (f : Î± â†’â‚[Î¼] Î²) (hf : Integrable f Î¼) : hf.toL1 f = f := by
  simp [Integrable.toL1]

theorem coeFn_toL1 {f : Î± â†’ Î²} (hf : Integrable f Î¼) : hf.toL1 f =áµ[Î¼] f :=
  AEEqFun.coeFn_mk _ _

@[simp]
theorem toL1_zero (h : Integrable (0 : Î± â†’ Î²) Î¼) : h.toL1 0 = 0 :=
  rfl

@[simp]
theorem toL1_eq_mk (f : Î± â†’ Î²) (hf : Integrable f Î¼) :
    (hf.toL1 f : Î± â†’â‚˜[Î¼] Î²) = AEEqFun.mk f hf.aestronglyMeasurable :=
  rfl

@[simp]
theorem toL1_eq_toL1_iff (f g : Î± â†’ Î²) (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    toL1 f hf = toL1 g hg â†” f =áµ[Î¼] g :=
  MemLp.toLp_eq_toLp_iff _ _

theorem toL1_add (f g : Î± â†’ Î²) (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    toL1 (f + g) (hf.add hg) = toL1 f hf + toL1 g hg :=
  rfl

theorem toL1_neg (f : Î± â†’ Î²) (hf : Integrable f Î¼) : toL1 (-f) (Integrable.neg hf) = -toL1 f hf :=
  rfl

theorem toL1_sub (f g : Î± â†’ Î²) (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    toL1 (f - g) (hf.sub hg) = toL1 f hf - toL1 g hg :=
  rfl

theorem norm_toL1 (f : Î± â†’ Î²) (hf : Integrable f Î¼) :
    â€–hf.toL1 fâ€– = (âˆ«â» a, edist (f a) 0 âˆ‚Î¼).toReal := by
  simp [toL1, Lp.norm_toLp, eLpNorm, eLpNorm'_eq_lintegral_enorm]

theorem enorm_toL1 {f : Î± â†’ Î²} (hf : Integrable f Î¼) : â€–hf.toL1 fâ€–â‚‘ = âˆ«â» a, â€–f aâ€–â‚‘ âˆ‚Î¼ := by
  simp only [Lp.enorm_def, toL1_eq_mk, eLpNorm_aeeqFun]
  simp [eLpNorm, eLpNorm']

theorem norm_toL1_eq_lintegral_norm (f : Î± â†’ Î²) (hf : Integrable f Î¼) :
    â€–hf.toL1 fâ€– = ENNReal.toReal (âˆ«â» a, ENNReal.ofReal â€–f aâ€– âˆ‚Î¼) := by
  rw [norm_toL1, lintegral_norm_eq_lintegral_edist]

@[simp]
theorem edist_toL1_toL1 (f g : Î± â†’ Î²) (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    edist (hf.toL1 f) (hg.toL1 g) = âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ := by
  simp only [toL1, Lp.edist_toLp_toLp, eLpNorm, one_ne_zero, eLpNorm'_eq_lintegral_enorm,
    Pi.sub_apply, toReal_one, ENNReal.rpow_one, ne_eq, not_false_eq_true, div_self, ite_false]
  simp [edist_eq_enorm_sub]

theorem edist_toL1_zero (f : Î± â†’ Î²) (hf : Integrable f Î¼) :
    edist (hf.toL1 f) 0 = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ := by
  simp only [edist_zero_right, Lp.enorm_def, toL1_eq_mk, eLpNorm_aeeqFun]
  apply eLpNorm_one_eq_lintegral_enorm

variable {ğ•œ : Type*} [NormedRing ğ•œ] [Module ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]

theorem toL1_smul (f : Î± â†’ Î²) (hf : Integrable f Î¼) (k : ğ•œ) :
    toL1 (fun a => k â€¢ f a) (hf.smul k) = k â€¢ toL1 f hf :=
  rfl

theorem toL1_smul' (f : Î± â†’ Î²) (hf : Integrable f Î¼) (k : ğ•œ) :
    toL1 (k â€¢ f) (hf.smul k) = k â€¢ toL1 f hf :=
  rfl

end Integrable

end MeasureTheory
