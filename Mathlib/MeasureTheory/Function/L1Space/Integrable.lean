/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import Mathlib.MeasureTheory.Function.L1Space.HasFiniteIntegral
import Mathlib.MeasureTheory.Function.LpOrder
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Lemmas

/-!
# Integrable functions

In this file, the predicate `Integrable` is defined and basic properties of
integrable functions are proved.

Such a predicate is already available under the name `MemLp 1`. We give a direct definition which
is easier to use, and show that it is equivalent to `MemLp 1`.

## Main definition

* Let `f : Î± â†’ Î²` be a function, where `Î±` is a `MeasureSpace` and `Î²` a `NormedAddCommGroup`
  which also a `MeasurableSpace`. Then `f` is called `Integrable` if
  `f` is `Measurable` and `HasFiniteIntegral f` holds.

## Implementation notes

To prove something for an arbitrary integrable function, a useful theorem is
`Integrable.induction` in the file `SetIntegral`.

## Tags

integrable

-/


noncomputable section

open EMetric ENNReal Filter MeasureTheory NNReal Set TopologicalSpace

open scoped Topology

variable {Î± Î² Î³ Î´ Îµ Îµ' Îµ'' : Type*} {m : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} [MeasurableSpace Î´]
variable [NormedAddCommGroup Î²] [NormedAddCommGroup Î³]
  [TopologicalSpace Îµ] [ContinuousENorm Îµ] [TopologicalSpace Îµ'] [ContinuousENorm Îµ'] [ENorm Îµ'']

namespace MeasureTheory

/-! ### The predicate `Integrable` -/

/-- `Integrable f Î¼` means that `f` is measurable and that the integral `âˆ«â» a, â€–f aâ€– âˆ‚Î¼` is finite.
  `Integrable f` means `Integrable f volume`. -/
@[fun_prop]
def Integrable {Î±} {_ : MeasurableSpace Î±} (f : Î± â†’ Îµ)
    (Î¼ : Measure Î± := by volume_tac) : Prop :=
  AEStronglyMeasurable f Î¼ âˆ§ HasFiniteIntegral f Î¼

/-- Notation for `Integrable` with respect to a non-standard Ïƒ-algebra. -/
scoped notation "Integrable[" mÎ± "]" => @Integrable _ _ _ _ mÎ±

theorem memLp_one_iff_integrable {f : Î± â†’ Îµ} : MemLp f 1 Î¼ â†” Integrable f Î¼ := by
  simp_rw [Integrable, hasFiniteIntegral_iff_enorm, MemLp, eLpNorm_one_eq_lintegral_enorm]

@[fun_prop]
theorem Integrable.aestronglyMeasurable {f : Î± â†’ Îµ} (hf : Integrable f Î¼) :
    AEStronglyMeasurable f Î¼ :=
  hf.1

@[fun_prop]
theorem Integrable.aemeasurable [MeasurableSpace Îµ] [BorelSpace Îµ] [PseudoMetrizableSpace Îµ]
    {f : Î± â†’ Îµ} (hf : Integrable f Î¼) : AEMeasurable f Î¼ :=
  hf.aestronglyMeasurable.aemeasurable

@[fun_prop]
theorem Integrable.hasFiniteIntegral {f : Î± â†’ Îµ} (hf : Integrable f Î¼) : HasFiniteIntegral f Î¼ :=
  hf.2

theorem Integrable.mono_enorm {f : Î± â†’ Îµ} {g : Î± â†’ Îµ'} (hg : Integrable g Î¼)
    (hf : AEStronglyMeasurable f Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€–â‚‘ â‰¤ â€–g aâ€–â‚‘) : Integrable f Î¼ :=
  âŸ¨hf, hg.hasFiniteIntegral.mono_enorm hâŸ©

theorem Integrable.mono {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hg : Integrable g Î¼)
    (hf : AEStronglyMeasurable f Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€– â‰¤ â€–g aâ€–) : Integrable f Î¼ :=
  âŸ¨hf, hg.hasFiniteIntegral.mono hâŸ©

theorem Integrable.mono'_enorm {f : Î± â†’ Îµ} {g : Î± â†’ â„â‰¥0âˆ} (hg : Integrable g Î¼)
    (hf : AEStronglyMeasurable f Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€–â‚‘ â‰¤ g a) : Integrable f Î¼ :=
  âŸ¨hf, hg.hasFiniteIntegral.mono_enorm hâŸ©

theorem Integrable.mono' {f : Î± â†’ Î²} {g : Î± â†’ â„} (hg : Integrable g Î¼)
    (hf : AEStronglyMeasurable f Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€– â‰¤ g a) : Integrable f Î¼ :=
  âŸ¨hf, hg.hasFiniteIntegral.mono' hâŸ©

theorem Integrable.congr'_enorm {f : Î± â†’ Îµ} {g : Î± â†’ Îµ'} (hf : Integrable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€–â‚‘ = â€–g aâ€–â‚‘) : Integrable g Î¼ :=
  âŸ¨hg, hf.hasFiniteIntegral.congr'_enorm hâŸ©

theorem Integrable.congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : Integrable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€– = â€–g aâ€–) : Integrable g Î¼ :=
  âŸ¨hg, hf.hasFiniteIntegral.congr' hâŸ©

theorem integrable_congr'_enorm {f : Î± â†’ Îµ} {g : Î± â†’ Îµ'}
    (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€–â‚‘ = â€–g aâ€–â‚‘) :
    Integrable f Î¼ â†” Integrable g Î¼ :=
  âŸ¨fun h2f => h2f.congr'_enorm hg h, fun h2g => h2g.congr'_enorm hf <| EventuallyEq.symm hâŸ©

theorem integrable_congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–f aâ€– = â€–g aâ€–) :
    Integrable f Î¼ â†” Integrable g Î¼ :=
  integrable_congr'_enorm hf hg <| h.mono fun _x hx â†¦ enorm_eq_iff_norm_eq.mpr hx

theorem Integrable.congr {f g : Î± â†’ Îµ} (hf : Integrable f Î¼) (h : f =áµ[Î¼] g) : Integrable g Î¼ :=
  âŸ¨hf.1.congr h, hf.2.congr hâŸ©

theorem integrable_congr {f g : Î± â†’ Îµ} (h : f =áµ[Î¼] g) : Integrable f Î¼ â†” Integrable g Î¼ :=
  âŸ¨fun hf => hf.congr h, fun hg => hg.congr h.symmâŸ©

theorem integrable_const_iff_enorm {c : Îµ} (hc : â€–câ€–â‚‘ â‰  âˆ) :
    Integrable (fun _ : Î± => c) Î¼ â†” â€–câ€–â‚‘ = 0 âˆ¨ IsFiniteMeasure Î¼ := by
  have : AEStronglyMeasurable (fun _ : Î± => c) Î¼ := aestronglyMeasurable_const
  rw [Integrable, and_iff_right this, hasFiniteIntegral_const_iff_enorm hc]

lemma integrable_const_iff {c : Î²} : Integrable (fun _ : Î± => c) Î¼ â†” c = 0 âˆ¨ IsFiniteMeasure Î¼ := by
  rw [integrable_const_iff_enorm enorm_ne_top]
  simp

lemma integrable_const_iff_isFiniteMeasure_enorm {c : Îµ} (hc : â€–câ€–â‚‘ â‰  0) (hc' : â€–câ€–â‚‘ â‰  âˆ) :
    Integrable (fun _ â†¦ c) Î¼ â†” IsFiniteMeasure Î¼ := by
  simp [integrable_const_iff_enorm hc', hc, isFiniteMeasure_iff]

lemma integrable_const_iff_isFiniteMeasure {c : Î²} (hc : c â‰  0) :
    Integrable (fun _ â†¦ c) Î¼ â†” IsFiniteMeasure Î¼ := by
  simp [integrable_const_iff, hc, isFiniteMeasure_iff]

theorem Integrable.of_mem_Icc_enorm [IsFiniteMeasure Î¼]
    {a b : â„â‰¥0âˆ} (ha : a â‰  âˆ) (hb : b â‰  âˆ) {X : Î± â†’ â„â‰¥0âˆ} (hX : AEMeasurable X Î¼)
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) :
    Integrable X Î¼ :=
  âŸ¨hX.aestronglyMeasurable, .of_mem_Icc_of_ne_top ha hb hâŸ©

theorem Integrable.of_mem_Icc [IsFiniteMeasure Î¼] (a b : â„) {X : Î± â†’ â„} (hX : AEMeasurable X Î¼)
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) :
    Integrable X Î¼ :=
  âŸ¨hX.aestronglyMeasurable, .of_mem_Icc a b hâŸ©

@[simp, fun_prop]
theorem integrable_const_enorm [IsFiniteMeasure Î¼] {c : Îµ} (hc : â€–câ€–â‚‘ â‰  âˆ) :
    Integrable (fun _ : Î± â†¦ c) Î¼ :=
  (integrable_const_iff_enorm hc).2 <| .inr â€¹_â€º

@[fun_prop]
theorem integrable_const [IsFiniteMeasure Î¼] (c : Î²) : Integrable (fun _ : Î± => c) Î¼ :=
  integrable_const_iff.2 <| .inr â€¹_â€º

-- TODO: an `ENorm`-version of this lemma requires `HasFiniteIntegral.of_finite`
@[fun_prop, simp]
lemma Integrable.of_finite [Finite Î±] [MeasurableSingletonClass Î±] [IsFiniteMeasure Î¼] {f : Î± â†’ Î²} :
    Integrable f Î¼ := âŸ¨.of_discrete, .of_finiteâŸ©

/-- This lemma is a special case of `Integrable.of_finite`. -/
lemma Integrable.of_isEmpty [IsEmpty Î±] {f : Î± â†’ Î²} : Integrable f Î¼ := .of_finite

/-- This lemma is a special case of `Integrable.of_finite`. -/
lemma Integrable.of_subsingleton [Subsingleton Î±] [IsFiniteMeasure Î¼] {f : Î± â†’ Î²} :
    Integrable f Î¼ :=
  .of_finite

theorem MemLp.integrable_enorm_rpow {f : Î± â†’ Îµ} {p : â„â‰¥0âˆ} (hf : MemLp f p Î¼) (hp_ne_zero : p â‰  0)
    (hp_ne_top : p â‰  âˆ) : Integrable (fun x : Î± => â€–f xâ€–â‚‘ ^ p.toReal) Î¼ := by
  rw [â† memLp_one_iff_integrable]
  exact hf.enorm_rpow hp_ne_zero hp_ne_top

theorem MemLp.integrable_norm_rpow {f : Î± â†’ Î²} {p : â„â‰¥0âˆ} (hf : MemLp f p Î¼) (hp_ne_zero : p â‰  0)
    (hp_ne_top : p â‰  âˆ) : Integrable (fun x : Î± => â€–f xâ€– ^ p.toReal) Î¼ := by
  rw [â† memLp_one_iff_integrable]
  exact hf.norm_rpow hp_ne_zero hp_ne_top

theorem MemLp.integrable_enorm_rpow' [IsFiniteMeasure Î¼] {f : Î± â†’ Îµ} {p : â„â‰¥0âˆ} (hf : MemLp f p Î¼) :
    Integrable (fun x : Î± => â€–f xâ€–â‚‘ ^ p.toReal) Î¼ := by
  by_cases h_zero : p = 0
  Â· simp [h_zero]
  by_cases h_top : p = âˆ
  Â· simp [h_top]
  exact hf.integrable_enorm_rpow h_zero h_top

theorem MemLp.integrable_norm_rpow' [IsFiniteMeasure Î¼] {f : Î± â†’ Î²} {p : â„â‰¥0âˆ} (hf : MemLp f p Î¼) :
    Integrable (fun x : Î± => â€–f xâ€– ^ p.toReal) Î¼ := by
  by_cases h_zero : p = 0
  Â· simp [h_zero]
  by_cases h_top : p = âˆ
  Â· simp [h_top]
  exact hf.integrable_norm_rpow h_zero h_top

lemma MemLp.integrable_enorm_pow {f : Î± â†’ Îµ} {p : â„•} (hf : MemLp f p Î¼) (hp : p â‰  0) :
    Integrable (fun x : Î± â†¦ â€–f xâ€–â‚‘ ^ p) Î¼ := by
  simpa using hf.integrable_enorm_rpow (mod_cast hp) (by simp)

lemma MemLp.integrable_norm_pow {f : Î± â†’ Î²} {p : â„•} (hf : MemLp f p Î¼) (hp : p â‰  0) :
    Integrable (fun x : Î± => â€–f xâ€– ^ p) Î¼ := by
  simpa using hf.integrable_norm_rpow (mod_cast hp) (by simp)

lemma MemLp.integrable_enorm_pow' [IsFiniteMeasure Î¼] {f : Î± â†’ Îµ} {p : â„•} (hf : MemLp f p Î¼) :
    Integrable (fun x : Î± â†¦ â€–f xâ€–â‚‘ ^ p) Î¼ := by simpa using hf.integrable_enorm_rpow'

lemma MemLp.integrable_norm_pow' [IsFiniteMeasure Î¼] {f : Î± â†’ Î²} {p : â„•} (hf : MemLp f p Î¼) :
    Integrable (fun x : Î± => â€–f xâ€– ^ p) Î¼ := by simpa using hf.integrable_norm_rpow'

lemma integrable_enorm_rpow_iff {f : Î± â†’ Îµ} {p : â„â‰¥0âˆ}
    (hf : AEStronglyMeasurable f Î¼) (p_zero : p â‰  0) (p_top : p â‰  âˆ) :
    Integrable (fun x : Î± => â€–f xâ€–â‚‘ ^ p.toReal) Î¼ â†” MemLp f p Î¼ := by
  rw [â† memLp_enorm_rpow_iff (q := p) hf p_zero p_top, â† memLp_one_iff_integrable,
    ENNReal.div_self p_zero p_top]

lemma integrable_norm_rpow_iff {f : Î± â†’ Î²} {p : â„â‰¥0âˆ}
    (hf : AEStronglyMeasurable f Î¼) (p_zero : p â‰  0) (p_top : p â‰  âˆ) :
    Integrable (fun x : Î± => â€–f xâ€– ^ p.toReal) Î¼ â†” MemLp f p Î¼ := by
  rw [â† memLp_norm_rpow_iff (q := p) hf p_zero p_top, â† memLp_one_iff_integrable,
    ENNReal.div_self p_zero p_top]

theorem Integrable.mono_measure {f : Î± â†’ Îµ} (h : Integrable f Î½) (hÎ¼ : Î¼ â‰¤ Î½) : Integrable f Î¼ :=
  âŸ¨h.aestronglyMeasurable.mono_measure hÎ¼, h.hasFiniteIntegral.mono_measure hÎ¼âŸ©

theorem Integrable.of_measure_le_smul {Îµ} [TopologicalSpace Îµ] [ESeminormedAddMonoid Îµ]
    {Î¼' : Measure Î±} {c : â„â‰¥0âˆ} (hc : c â‰  âˆ) (hÎ¼'_le : Î¼' â‰¤ c â€¢ Î¼)
    {f : Î± â†’ Îµ} (hf : Integrable f Î¼) : Integrable f Î¼' := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact hf.of_measure_le_smul hc hÎ¼'_le

@[fun_prop]
theorem Integrable.add_measure [PseudoMetrizableSpace Îµ]
    {f : Î± â†’ Îµ} (hÎ¼ : Integrable f Î¼) (hÎ½ : Integrable f Î½) :
    Integrable f (Î¼ + Î½) := by
  simp_rw [â† memLp_one_iff_integrable] at hÎ¼ hÎ½ âŠ¢
  refine âŸ¨hÎ¼.aestronglyMeasurable.add_measure hÎ½.aestronglyMeasurable, ?_âŸ©
  rw [eLpNorm_one_add_measure, ENNReal.add_lt_top]
  exact âŸ¨hÎ¼.eLpNorm_lt_top, hÎ½.eLpNorm_lt_topâŸ©

theorem Integrable.left_of_add_measure {f : Î± â†’ Îµ} (h : Integrable f (Î¼ + Î½)) : Integrable f Î¼ := by
  rw [â† memLp_one_iff_integrable] at h âŠ¢
  exact h.left_of_add_measure

theorem Integrable.right_of_add_measure {f : Î± â†’ Îµ} (h : Integrable f (Î¼ + Î½)) :
    Integrable f Î½ := by
  rw [â† memLp_one_iff_integrable] at h âŠ¢
  exact h.right_of_add_measure

@[simp]
theorem integrable_add_measure [PseudoMetrizableSpace Îµ] {f : Î± â†’ Îµ} :
    Integrable f (Î¼ + Î½) â†” Integrable f Î¼ âˆ§ Integrable f Î½ :=
  âŸ¨fun h => âŸ¨h.left_of_add_measure, h.right_of_add_measureâŸ©, fun h => h.1.add_measure h.2âŸ©

@[simp]
theorem integrable_zero_measure {f : Î± â†’ Îµ} : Integrable f (0 : Measure Î±) := by
  constructor <;> fun_prop

/-- In a measurable space with measurable singletons, every function is integrable with respect to
a Dirac measure.
See `integrable_dirac'` for a version which requires `f` to be strongly measurable but does not
need singletons to be measurable. -/
@[fun_prop]
lemma integrable_dirac [MeasurableSingletonClass Î±] {a : Î±} {f : Î± â†’ Îµ} (hfa : â€–f aâ€–â‚‘ < âˆ) :
    Integrable f (Measure.dirac a) :=
  âŸ¨aestronglyMeasurable_dirac, by simpa [HasFiniteIntegral]âŸ©

/-- Every strongly measurable function is integrable with respect to a Dirac measure.
See `integrable_dirac` for a version which requires that singletons are measurable sets but has no
hypothesis on `f`. -/
@[fun_prop]
lemma integrable_dirac' {a : Î±} {f : Î± â†’ Îµ} (hf : StronglyMeasurable f) (hfa : â€–f aâ€–â‚‘ < âˆ) :
    Integrable f (Measure.dirac a) :=
  âŸ¨hf.aestronglyMeasurable, by simpa [HasFiniteIntegral, lintegral_dirac' _ hf.enorm]âŸ©

theorem integrable_finset_sum_measure [PseudoMetrizableSpace Îµ]
    {Î¹} {m : MeasurableSpace Î±} {f : Î± â†’ Îµ} {Î¼ : Î¹ â†’ Measure Î±}
    {s : Finset Î¹} : Integrable f (âˆ‘ i âˆˆ s, Î¼ i) â†” âˆ€ i âˆˆ s, Integrable f (Î¼ i) := by
  classical
  induction s using Finset.induction_on <;> simp [*]

section

variable {Îµ : Type*} [TopologicalSpace Îµ] [ESeminormedAddMonoid Îµ]

@[fun_prop]
theorem Integrable.smul_measure {f : Î± â†’ Îµ} (h : Integrable f Î¼) {c : â„â‰¥0âˆ} (hc : c â‰  âˆ) :
    Integrable f (c â€¢ Î¼) := by
  rw [â† memLp_one_iff_integrable] at h âŠ¢
  exact h.smul_measure hc

@[fun_prop]
theorem Integrable.smul_measure_nnreal {f : Î± â†’ Îµ} (h : Integrable f Î¼) {c : â„â‰¥0} :
    Integrable f (c â€¢ Î¼) := by
  apply h.smul_measure
  simp

theorem integrable_smul_measure {f : Î± â†’ Îµ} {c : â„â‰¥0âˆ} (hâ‚ : c â‰  0) (hâ‚‚ : c â‰  âˆ) :
    Integrable f (c â€¢ Î¼) â†” Integrable f Î¼ :=
  âŸ¨fun h => by
    simpa only [smul_smul, ENNReal.inv_mul_cancel hâ‚ hâ‚‚, one_smul] using
      h.smul_measure (ENNReal.inv_ne_top.2 hâ‚),
    fun h => h.smul_measure hâ‚‚âŸ©

theorem integrable_inv_smul_measure {f : Î± â†’ Îµ} {c : â„â‰¥0âˆ} (hâ‚ : c â‰  0) (hâ‚‚ : c â‰  âˆ) :
    Integrable f (câ»Â¹ â€¢ Î¼) â†” Integrable f Î¼ :=
  integrable_smul_measure (by simpa using hâ‚‚) (by simpa using hâ‚)

theorem Integrable.to_average {f : Î± â†’ Îµ} (h : Integrable f Î¼) : Integrable f ((Î¼ univ)â»Â¹ â€¢ Î¼) := by
  rcases eq_or_ne Î¼ 0 with (rfl | hne)
  Â· rwa [smul_zero]
  Â· apply h.smul_measure
    simpa

open scoped Classical in
theorem integrable_average [IsFiniteMeasure Î¼] {f : Î± â†’ Îµ} :
    Integrable f ((Î¼ univ)â»Â¹ â€¢ Î¼) â†” Integrable f Î¼ :=
  (eq_or_ne Î¼ 0).by_cases (fun h => by simp [h]) fun h =>
    integrable_smul_measure (ENNReal.inv_ne_zero.2 <| measure_ne_top _ _)
      (ENNReal.inv_ne_top.2 <| mt Measure.measure_univ_eq_zero.1 h)

end

section

variable {Î±' : Type*} [MeasurableSpace Î±']

theorem integrable_map_measure {f : Î± â†’ Î±'} {g : Î±' â†’ Îµ}
    (hg : AEStronglyMeasurable g (Measure.map f Î¼)) (hf : AEMeasurable f Î¼) :
    Integrable g (Measure.map f Î¼) â†” Integrable (g âˆ˜ f) Î¼ := by
  simp_rw [â† memLp_one_iff_integrable]
  exact memLp_map_measure_iff hg hf

theorem Integrable.comp_aemeasurable {f : Î± â†’ Î±'} {g : Î±' â†’ Îµ}
    (hg : Integrable g (Measure.map f Î¼)) (hf : AEMeasurable f Î¼) : Integrable (g âˆ˜ f) Î¼ :=
  (integrable_map_measure hg.aestronglyMeasurable hf).mp hg

theorem Integrable.comp_measurable {f : Î± â†’ Î±'} {g : Î±' â†’ Îµ} (hg : Integrable g (Measure.map f Î¼))
    (hf : Measurable f) : Integrable (g âˆ˜ f) Î¼ :=
  hg.comp_aemeasurable hf.aemeasurable

end

theorem _root_.MeasurableEmbedding.integrable_map_iff {f : Î± â†’ Î´} (hf : MeasurableEmbedding f)
    {g : Î´ â†’ Îµ} : Integrable g (Measure.map f Î¼) â†” Integrable (g âˆ˜ f) Î¼ := by
  simp_rw [â† memLp_one_iff_integrable]
  exact hf.memLp_map_measure_iff

theorem integrable_map_equiv (f : Î± â‰ƒáµ Î´) (g : Î´ â†’ Îµ) :
    Integrable g (Measure.map f Î¼) â†” Integrable (g âˆ˜ f) Î¼ := by
  simp_rw [â† memLp_one_iff_integrable]
  exact f.memLp_map_measure_iff

theorem MeasurePreserving.integrable_comp {Î½ : Measure Î´} {g : Î´ â†’ Îµ} {f : Î± â†’ Î´}
    (hf : MeasurePreserving f Î¼ Î½) (hg : AEStronglyMeasurable g Î½) :
    Integrable (g âˆ˜ f) Î¼ â†” Integrable g Î½ := by
  rw [â† hf.map_eq] at hg âŠ¢
  exact (integrable_map_measure hg hf.measurable.aemeasurable).symm

theorem MeasurePreserving.integrable_comp_of_integrable {Î½ : Measure Î´} {g : Î´ â†’ Îµ} {f : Î± â†’ Î´}
    (hf : MeasurePreserving f Î¼ Î½) (hg : Integrable g Î½) :
    Integrable (g âˆ˜ f) Î¼ :=
  hf.integrable_comp hg.aestronglyMeasurable |>.mpr hg

theorem MeasurePreserving.integrable_comp_emb {f : Î± â†’ Î´} {Î½} (hâ‚ : MeasurePreserving f Î¼ Î½)
    (hâ‚‚ : MeasurableEmbedding f) {g : Î´ â†’ Îµ} : Integrable (g âˆ˜ f) Î¼ â†” Integrable g Î½ :=
  hâ‚.map_eq â–¸ Iff.symm hâ‚‚.integrable_map_iff

theorem lintegral_edist_lt_top {f g : Î± â†’ Î²} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    (âˆ«â» a, edist (f a) (g a) âˆ‚Î¼) < âˆ :=
  lt_of_le_of_lt (lintegral_edist_triangle hf.aestronglyMeasurable aestronglyMeasurable_zero)
    (ENNReal.add_lt_top.2 <| by
      simp_rw [Pi.zero_apply, â† hasFiniteIntegral_iff_edist]
      exact âŸ¨hf.hasFiniteIntegral, hg.hasFiniteIntegralâŸ©)

section ESeminormedAddMonoid

variable {Îµ' : Type*} [TopologicalSpace Îµ'] [ESeminormedAddMonoid Îµ']

variable (Î± Îµ') in
@[simp]
theorem integrable_zero (Î¼ : Measure Î±) : Integrable (fun _ => (0 : Îµ')) Î¼ := by
  simp [Integrable, aestronglyMeasurable_const]

theorem Integrable.add' {f g : Î± â†’ Îµ'} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    HasFiniteIntegral (f + g) Î¼ :=
  calc
    âˆ«â» a, â€–f a + g aâ€–â‚‘ âˆ‚Î¼ â‰¤ âˆ«â» a, â€–f aâ€–â‚‘ + â€–g aâ€–â‚‘ âˆ‚Î¼ := lintegral_mono fun _ â†¦ enorm_add_le _ _
    _ = _ := lintegral_enorm_add_left hf.aestronglyMeasurable _
    _ < âˆ := add_lt_top.2 âŸ¨hf.hasFiniteIntegral, hg.hasFiniteIntegralâŸ©

@[fun_prop]
theorem Integrable.add [ContinuousAdd Îµ']
    {f g : Î± â†’ Îµ'} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Integrable (f + g) Î¼ :=
  âŸ¨hf.aestronglyMeasurable.add hg.aestronglyMeasurable, hf.add' hgâŸ©

@[fun_prop]
theorem Integrable.add'' [ContinuousAdd Îµ']
    {f g : Î± â†’ Îµ'} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Integrable (fun x â†¦ f x + g x) Î¼ := hf.add hg

@[simp]
lemma Integrable.of_subsingleton_codomain [Subsingleton Îµ'] {f : Î± â†’ Îµ'} :
    Integrable f Î¼ :=
  integrable_zero _ _ _ |>.congr <| .of_forall fun _ â†¦ Subsingleton.elim _ _

end ESeminormedAddMonoid

section ESeminormedAddCommMonoid

variable {Îµ' : Type*} [TopologicalSpace Îµ'] [ESeminormedAddCommMonoid Îµ'] [ContinuousAdd Îµ']

@[fun_prop]
theorem integrable_finset_sum' {Î¹} (s : Finset Î¹) {f : Î¹ â†’ Î± â†’ Îµ'}
    (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) : Integrable (âˆ‘ i âˆˆ s, f i) Î¼ :=
  Finset.sum_induction f (fun g => Integrable g Î¼) (fun _ _ => Integrable.add)
    (integrable_zero _ _ _) hf

@[fun_prop]
theorem integrable_finset_sum {Î¹} (s : Finset Î¹) {f : Î¹ â†’ Î± â†’ Îµ'}
    (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) : Integrable (fun a => âˆ‘ i âˆˆ s, f i a) Î¼ := by
  simpa only [â† Finset.sum_apply] using integrable_finset_sum' s hf

end ESeminormedAddCommMonoid

/-- If `f` is integrable, then so is `-f`.
See `Integrable.neg'` for the same statement, but formulated with `x â†¦ - f x` instead of `-f`. -/
@[fun_prop]
theorem Integrable.neg {f : Î± â†’ Î²} (hf : Integrable f Î¼) : Integrable (-f) Î¼ :=
  âŸ¨hf.aestronglyMeasurable.neg, by fun_propâŸ©

/-- If `f` is integrable, then so is `fun x â†¦ - f x`.
See `Integrable.neg` for the same statement, but formulated with `-f` instead of `fun x â†¦ - f x`. -/
@[fun_prop]
theorem Integrable.neg' {f : Î± â†’ Î²} (hf : Integrable f Î¼) : Integrable (fun x â†¦ - f x) Î¼ :=
  âŸ¨hf.aestronglyMeasurable.neg, hf.hasFiniteIntegral.negâŸ©

@[simp]
theorem integrable_neg_iff {f : Î± â†’ Î²} : Integrable (-f) Î¼ â†” Integrable f Î¼ :=
  âŸ¨fun h => neg_neg f â–¸ h.neg, Integrable.negâŸ©

/-- if `f` is integrable, then `f + g` is integrable iff `g` is.
See `integrable_add_iff_integrable_right'` for the same statement with `fun x â†¦ f x + g x` instead
of `f + g`. -/
@[simp]
lemma integrable_add_iff_integrable_right {f g : Î± â†’ Î²} (hf : Integrable f Î¼) :
    Integrable (f + g) Î¼ â†” Integrable g Î¼ :=
  âŸ¨fun h â†¦ show g = f + g + (-f) by simp only [add_neg_cancel_comm] â–¸ h.add hf.neg,
    fun h â†¦ hf.add hâŸ©

/-- if `f` is integrable, then `fun x â†¦ f x + g x` is integrable iff `g` is.
See `integrable_add_iff_integrable_right` for the same statement with `f + g` instead
of `fun x â†¦ f x + g x`. -/
@[simp]
lemma integrable_add_iff_integrable_right' {f g : Î± â†’ Î²} (hf : Integrable f Î¼) :
    Integrable (fun x â†¦ f x + g x) Î¼ â†” Integrable g Î¼ :=
  integrable_add_iff_integrable_right hf

/-- if `f` is integrable, then `g + f` is integrable iff `g` is.
See `integrable_add_iff_integrable_left'` for the same statement with `fun x â†¦ g x + f x` instead
of `g + f`. -/
@[simp]
lemma integrable_add_iff_integrable_left {f g : Î± â†’ Î²} (hf : Integrable f Î¼) :
    Integrable (g + f) Î¼ â†” Integrable g Î¼ := by
  rw [add_comm, integrable_add_iff_integrable_right hf]

/-- if `f` is integrable, then `fun x â†¦ g x + f x` is integrable iff `g` is.
See `integrable_add_iff_integrable_left'` for the same statement with `g + f` instead
of `fun x â†¦ g x + f x`. -/
@[simp]
lemma integrable_add_iff_integrable_left' {f g : Î± â†’ Î²} (hf : Integrable f Î¼) :
    Integrable (fun x â†¦ g x + f x) Î¼ â†” Integrable g Î¼ :=
  integrable_add_iff_integrable_left hf

lemma integrable_left_of_integrable_add_of_nonneg {f g : Î± â†’ â„}
    (h_meas : AEStronglyMeasurable f Î¼) (hf : 0 â‰¤áµ[Î¼] f) (hg : 0 â‰¤áµ[Î¼] g)
    (h_int : Integrable (f + g) Î¼) : Integrable f Î¼ := by
  refine h_int.mono' h_meas ?_
  filter_upwards [hf, hg] with a haf hag
  exact (Real.norm_of_nonneg haf).symm â–¸ le_add_of_nonneg_right hag

lemma integrable_right_of_integrable_add_of_nonneg {f g : Î± â†’ â„}
    (h_meas : AEStronglyMeasurable f Î¼) (hf : 0 â‰¤áµ[Î¼] f) (hg : 0 â‰¤áµ[Î¼] g)
    (h_int : Integrable (f + g) Î¼) : Integrable g Î¼ :=
  integrable_left_of_integrable_add_of_nonneg
    ((AEStronglyMeasurable.add_iff_right h_meas).mp h_int.aestronglyMeasurable)
      hg hf (add_comm f g â–¸ h_int)

lemma integrable_add_iff_of_nonneg {f g : Î± â†’ â„} (h_meas : AEStronglyMeasurable f Î¼)
    (hf : 0 â‰¤áµ[Î¼] f) (hg : 0 â‰¤áµ[Î¼] g) :
    Integrable (f + g) Î¼ â†” Integrable f Î¼ âˆ§ Integrable g Î¼ :=
  âŸ¨fun h â†¦ âŸ¨integrable_left_of_integrable_add_of_nonneg h_meas hf hg h,
    integrable_right_of_integrable_add_of_nonneg h_meas hf hg hâŸ©, fun âŸ¨hf, hgâŸ© â†¦ hf.add hgâŸ©

lemma integrable_add_iff_of_nonpos {f g : Î± â†’ â„} (h_meas : AEStronglyMeasurable f Î¼)
    (hf : f â‰¤áµ[Î¼] 0) (hg : g â‰¤áµ[Î¼] 0) :
    Integrable (f + g) Î¼ â†” Integrable f Î¼ âˆ§ Integrable g Î¼ := by
  rw [â† integrable_neg_iff, â† integrable_neg_iff (f := f), â† integrable_neg_iff (f := g), neg_add]
  exact integrable_add_iff_of_nonneg h_meas.neg (hf.mono (fun _ â†¦ neg_nonneg_of_nonpos))
    (hg.mono (fun _ â†¦ neg_nonneg_of_nonpos))

lemma integrable_add_const_iff [IsFiniteMeasure Î¼] {f : Î± â†’ Î²} {c : Î²} :
    Integrable (fun x â†¦ f x + c) Î¼ â†” Integrable f Î¼ :=
  integrable_add_iff_integrable_left (integrable_const _)

lemma integrable_const_add_iff [IsFiniteMeasure Î¼] {f : Î± â†’ Î²} {c : Î²} :
    Integrable (fun x â†¦ c + f x) Î¼ â†” Integrable f Î¼ :=
  integrable_add_iff_integrable_right (integrable_const _)

-- TODO: generalise these lemmas to an `ENormedAddCommSubMonoid`
@[fun_prop]
theorem Integrable.sub {f g : Î± â†’ Î²} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Integrable (f - g) Î¼ := by simpa only [sub_eq_add_neg] using hf.add hg.neg

@[fun_prop]
theorem Integrable.sub' {f g : Î± â†’ Î²} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Integrable (fun a â†¦ f a - g a) Î¼ := by simpa only [sub_eq_add_neg] using hf.add hg.neg

@[fun_prop]
theorem Integrable.enorm {f : Î± â†’ Îµ} (hf : Integrable f Î¼) : Integrable (â€–f Â·â€–â‚‘) Î¼ := by
  constructor <;> fun_prop

@[fun_prop]
theorem Integrable.norm {f : Î± â†’ Î²} (hf : Integrable f Î¼) : Integrable (fun a => â€–f aâ€–) Î¼ := by
  constructor <;> fun_prop

@[fun_prop]
theorem Integrable.inf {Î²}
    [NormedAddCommGroup Î²] [Lattice Î²] [HasSolidNorm Î²] [IsOrderedAddMonoid Î²]
    {f g : Î± â†’ Î²} (hf : Integrable f Î¼)
    (hg : Integrable g Î¼) : Integrable (f âŠ“ g) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf hg âŠ¢
  exact hf.inf hg

@[fun_prop]
theorem Integrable.sup {Î²}
    [NormedAddCommGroup Î²] [Lattice Î²] [HasSolidNorm Î²] [IsOrderedAddMonoid Î²]
    {f g : Î± â†’ Î²} (hf : Integrable f Î¼)
    (hg : Integrable g Î¼) : Integrable (f âŠ” g) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf hg âŠ¢
  exact hf.sup hg

@[fun_prop]
theorem Integrable.abs {Î²}
    [NormedAddCommGroup Î²] [Lattice Î²] [HasSolidNorm Î²] [IsOrderedAddMonoid Î²]
    {f : Î± â†’ Î²} (hf : Integrable f Î¼) :
    Integrable (fun a => |f a|) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact hf.abs

-- TODO: generalise this to enorms, once there is an `ENormedDivisionRing` class
theorem Integrable.bdd_mul {F : Type*} [NormedDivisionRing F] {f g : Î± â†’ F} (hint : Integrable g Î¼)
    (hm : AEStronglyMeasurable f Î¼) (hfbdd : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C) :
    Integrable (fun x => f x * g x) Î¼ := by
  rcases isEmpty_or_nonempty Î± with hÎ± | hÎ±
  Â· rw [Î¼.eq_zero_of_isEmpty]
    exact integrable_zero_measure
  Â· refine âŸ¨hm.mul hint.1, ?_âŸ©
    obtain âŸ¨C, hCâŸ© := hfbdd
    have hCnonneg : 0 â‰¤ C := le_trans (norm_nonneg _) (hC hÎ±.some)
    have : (fun x => â€–f x * g xâ€–â‚Š) â‰¤ fun x => âŸ¨C, hCnonnegâŸ© * â€–g xâ€–â‚Š := by
      intro x
      simp only [nnnorm_mul]
      exact mul_le_mul_of_nonneg_right (hC x) (zero_le _)
    refine lt_of_le_of_lt (lintegral_mono_nnreal this) ?_
    simp only [ENNReal.coe_mul]
    rw [lintegral_const_mul' _ _ ENNReal.coe_ne_top]
    exact ENNReal.mul_lt_top ENNReal.coe_lt_top hint.2

-- TODO: generalise the following lemmas to enorm classes

/-- **HÃ¶lder's inequality for integrable functions**: the scalar multiplication of an integrable
vector-valued function by a scalar function with finite essential supremum is integrable. -/
theorem Integrable.essSup_smul {R : Type*} [NormedRing R] [Module R Î²] [IsBoundedSMul R Î²]
    {f : Î± â†’ Î²} (hf : Integrable f Î¼) {g : Î± â†’ R}
    (g_aestronglyMeasurable : AEStronglyMeasurable g Î¼) (ess_sup_g : essSup (â€–g Â·â€–â‚‘) Î¼ â‰  âˆ) :
    Integrable (fun x : Î± => g x â€¢ f x) Î¼ := by
  rw [â† memLp_one_iff_integrable] at *
  refine âŸ¨g_aestronglyMeasurable.smul hf.1, ?_âŸ©
  have hg' : eLpNorm g âˆ Î¼ â‰  âˆ := by rwa [eLpNorm_exponent_top]
  calc
    eLpNorm (fun x : Î± => g x â€¢ f x) 1 Î¼ â‰¤ _ := by
      simpa using MeasureTheory.eLpNorm_smul_le_mul_eLpNorm hf.1 g_aestronglyMeasurable
        (p := âˆ) (q := 1)
    _ < âˆ := ENNReal.mul_lt_top hg'.lt_top hf.2

/-- HÃ¶lder's inequality for integrable functions: the scalar multiplication of an integrable
scalar-valued function by a vector-value function with finite essential supremum is integrable. -/
theorem Integrable.smul_essSup {ğ•œ : Type*} [NormedRing ğ•œ] [MulActionWithZero ğ•œ Î²]
    [IsBoundedSMul ğ•œ Î²] {f : Î± â†’ ğ•œ} (hf : Integrable f Î¼) {g : Î± â†’ Î²}
    (g_aestronglyMeasurable : AEStronglyMeasurable g Î¼) (ess_sup_g : essSup (â€–g Â·â€–â‚‘) Î¼ â‰  âˆ) :
    Integrable (fun x : Î± => f x â€¢ g x) Î¼ := by
  rw [â† memLp_one_iff_integrable] at *
  refine âŸ¨hf.1.smul g_aestronglyMeasurable, ?_âŸ©
  have hg' : eLpNorm g âˆ Î¼ â‰  âˆ := by rwa [eLpNorm_exponent_top]
  calc
    eLpNorm (fun x : Î± => f x â€¢ g x) 1 Î¼ â‰¤ _ := by
      simpa using MeasureTheory.eLpNorm_smul_le_mul_eLpNorm g_aestronglyMeasurable hf.1
        (p := 1) (q := âˆ)
    _ < âˆ := ENNReal.mul_lt_top hf.2 hg'.lt_top

theorem integrable_enorm_iff {f : Î± â†’ Îµ} (hf : AEStronglyMeasurable f Î¼) :
    Integrable (â€–f Â·â€–â‚‘) Î¼ â†” Integrable f Î¼ := by
  simp_rw [Integrable, and_iff_right hf, and_iff_right hf.enorm.aestronglyMeasurable,
    hasFiniteIntegral_enorm_iff]

theorem integrable_norm_iff {f : Î± â†’ Î²} (hf : AEStronglyMeasurable f Î¼) :
    Integrable (fun a => â€–f aâ€–) Î¼ â†” Integrable f Î¼ := by
  simp_rw [Integrable, and_iff_right hf, and_iff_right hf.norm, hasFiniteIntegral_norm_iff]

-- TODO: generalise this lemma to an `ENormedAddCommSubMonoid`
theorem integrable_of_norm_sub_le {fâ‚€ fâ‚ : Î± â†’ Î²} {g : Î± â†’ â„} (hfâ‚_m : AEStronglyMeasurable fâ‚ Î¼)
    (hfâ‚€_i : Integrable fâ‚€ Î¼) (hg_i : Integrable g Î¼) (h : âˆ€áµ a âˆ‚Î¼, â€–fâ‚€ a - fâ‚ aâ€– â‰¤ g a) :
    Integrable fâ‚ Î¼ :=
  haveI : âˆ€áµ a âˆ‚Î¼, â€–fâ‚ aâ€– â‰¤ â€–fâ‚€ aâ€– + g a := by
    apply h.mono
    intro a ha
    calc
      â€–fâ‚ aâ€– â‰¤ â€–fâ‚€ aâ€– + â€–fâ‚€ a - fâ‚ aâ€– := norm_le_insert _ _
      _ â‰¤ â€–fâ‚€ aâ€– + g a := by gcongr
  Integrable.mono' (hfâ‚€_i.norm.add hg_i) hfâ‚_m this

lemma integrable_of_le_of_le {f gâ‚ gâ‚‚ : Î± â†’ â„} (hf : AEStronglyMeasurable f Î¼)
    (h_leâ‚ : gâ‚ â‰¤áµ[Î¼] f) (h_leâ‚‚ : f â‰¤áµ[Î¼] gâ‚‚)
    (h_intâ‚ : Integrable gâ‚ Î¼) (h_intâ‚‚ : Integrable gâ‚‚ Î¼) :
    Integrable f Î¼ := by
  have : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ max â€–gâ‚ xâ€– â€–gâ‚‚ xâ€– := by
    filter_upwards [h_leâ‚, h_leâ‚‚] with x hx1 hx2
    simp only [Real.norm_eq_abs]
    exact abs_le_max_abs_abs hx1 hx2
  have h_le_add : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ â€–â€–gâ‚ xâ€– + â€–gâ‚‚ xâ€–â€– := by
    filter_upwards [this] with x hx
    refine hx.trans ?_
    conv_rhs => rw [Real.norm_of_nonneg (by positivity)]
    exact max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)
  exact Integrable.mono (by fun_prop) hf h_le_add

-- TODO: generalising this to enorms requires defining a product instance for enormed monoids first
@[fun_prop]
theorem Integrable.prodMk {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Integrable (fun x => (f x, g x)) Î¼ :=
  âŸ¨by fun_prop,
    (hf.norm.add' hg.norm).mono <|
      Eventually.of_forall fun x =>
        calc
          max â€–f xâ€– â€–g xâ€– â‰¤ â€–f xâ€– + â€–g xâ€– := max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)
          _ â‰¤ â€–â€–f xâ€– + â€–g xâ€–â€– := le_abs_self _âŸ©

@[deprecated (since := "2025-03-05")]
alias Integrable.prod_mk := Integrable.prodMk

theorem MemLp.integrable {q : â„â‰¥0âˆ} (hq1 : 1 â‰¤ q) {f : Î± â†’ Îµ} [IsFiniteMeasure Î¼]
    (hfq : MemLp f q Î¼) : Integrable f Î¼ :=
  memLp_one_iff_integrable.mp (hfq.mono_exponent hq1)

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `â€–f xâ€–â‚‘ â‰¥ Îµ` is finite for all positive `Îµ`. -/
theorem Integrable.measure_enorm_ge_lt_top {E : Type*} [TopologicalSpace E] [ContinuousENorm E]
    {f : Î± â†’ E} (hf : Integrable f Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : 0 < Îµ) (hÎµ' : Îµ â‰  âˆ) :
    Î¼ { x | Îµ â‰¤ â€–f xâ€–â‚‘ } < âˆ := by
  refine meas_ge_le_mul_pow_eLpNorm_enorm Î¼ one_ne_zero one_ne_top hf.1 hÎµ.ne' (by simp [hÎµ'])
    |>.trans_lt ?_
  apply ENNReal.mul_lt_top
  Â· simpa only [ENNReal.toReal_one, ENNReal.rpow_one, ENNReal.inv_lt_top, ENNReal.ofReal_pos]
      using hÎµ
  Â· simpa only [ENNReal.toReal_one, ENNReal.rpow_one] using
      (memLp_one_iff_integrable.2 hf).eLpNorm_lt_top

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `â€–f xâ€– â‰¥ Îµ` is finite for all positive `Îµ`. -/
theorem Integrable.measure_norm_ge_lt_top {f : Î± â†’ Î²} (hf : Integrable f Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
    Î¼ { x | Îµ â‰¤ â€–f xâ€– } < âˆ := by
  convert Integrable.measure_enorm_ge_lt_top hf (ofReal_pos.mpr hÎµ) ofReal_ne_top with x
  rw [â† Real.enorm_of_nonneg hÎµ.le, enorm_le_iff_norm_le, Real.norm_of_nonneg hÎµ.le]

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `â€–f xâ€–â‚‘ > Îµ` is finite for all positive `Îµ`. -/
lemma Integrable.measure_norm_gt_lt_top_enorm {E : Type*} [TopologicalSpace E] [ContinuousENorm E]
    {f : Î± â†’ E} (hf : Integrable f Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : 0 < Îµ) : Î¼ {x | Îµ < â€–f xâ€–â‚‘} < âˆ := by
  by_cases hÎµ' : Îµ = âˆ
  Â· simp [hÎµ']
  exact lt_of_le_of_lt (measure_mono (fun _ h â†¦ (Set.mem_setOf_eq â–¸ h).le))
    (hf.measure_enorm_ge_lt_top hÎµ hÎµ')

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `â€–f xâ€– > Îµ` is finite for all positive `Îµ`. -/
lemma Integrable.measure_norm_gt_lt_top {f : Î± â†’ Î²} (hf : Integrable f Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
    Î¼ {x | Îµ < â€–f xâ€–} < âˆ :=
  lt_of_le_of_lt (measure_mono (fun _ h â†¦ (Set.mem_setOf_eq â–¸ h).le)) (hf.measure_norm_ge_lt_top hÎµ)

-- TODO: try generalising all lemmas below to enorm classes

/-- If `f` is `â„`-valued and integrable, then for any `c > 0` the set `{x | f x â‰¥ c}` has finite
measure. -/
lemma Integrable.measure_ge_lt_top {f : Î± â†’ â„} (hf : Integrable f Î¼) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    Î¼ {a : Î± | Îµ â‰¤ f a} < âˆ := by
  refine lt_of_le_of_lt (measure_mono ?_) (hf.measure_norm_ge_lt_top Îµ_pos)
  intro x hx
  simp only [Real.norm_eq_abs, Set.mem_setOf_eq] at hx âŠ¢
  exact hx.trans (le_abs_self _)

/-- If `f` is `â„`-valued and integrable, then for any `c < 0` the set `{x | f x â‰¤ c}` has finite
measure. -/
lemma Integrable.measure_le_lt_top {f : Î± â†’ â„} (hf : Integrable f Î¼) {c : â„} (c_neg : c < 0) :
    Î¼ {a : Î± | f a â‰¤ c} < âˆ := by
  refine lt_of_le_of_lt (measure_mono ?_) (hf.measure_norm_ge_lt_top (show 0 < -c by linarith))
  intro x hx
  simp only [Real.norm_eq_abs, Set.mem_setOf_eq] at hx âŠ¢
  exact (show -c â‰¤ - f x by linarith).trans (neg_le_abs _)

/-- If `f` is `â„`-valued and integrable, then for any `c > 0` the set `{x | f x > c}` has finite
measure. -/
lemma Integrable.measure_gt_lt_top {f : Î± â†’ â„} (hf : Integrable f Î¼) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    Î¼ {a : Î± | Îµ < f a} < âˆ :=
  lt_of_le_of_lt (measure_mono (fun _ hx â†¦ (Set.mem_setOf_eq â–¸ hx).le))
    (Integrable.measure_ge_lt_top hf Îµ_pos)

/-- If `f` is `â„`-valued and integrable, then for any `c < 0` the set `{x | f x < c}` has finite
measure. -/
lemma Integrable.measure_lt_lt_top {f : Î± â†’ â„} (hf : Integrable f Î¼) {c : â„} (c_neg : c < 0) :
    Î¼ {a : Î± | f a < c} < âˆ :=
  lt_of_le_of_lt (measure_mono (fun _ hx â†¦ (Set.mem_setOf_eq â–¸ hx).le))
    (Integrable.measure_le_lt_top hf c_neg)

theorem LipschitzWith.integrable_comp_iff_of_antilipschitz {K K'} {f : Î± â†’ Î²} {g : Î² â†’ Î³}
    (hg : LipschitzWith K g) (hg' : AntilipschitzWith K' g) (g0 : g 0 = 0) :
    Integrable (g âˆ˜ f) Î¼ â†” Integrable f Î¼ := by
  simp [â† memLp_one_iff_integrable, hg.memLp_comp_iff_of_antilipschitz hg' g0]

@[fun_prop]
theorem Integrable.real_toNNReal {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    Integrable (fun x => ((f x).toNNReal : â„)) Î¼ := by
  refine âŸ¨by fun_prop, ?_âŸ©
  rw [hasFiniteIntegral_iff_norm]
  refine lt_of_le_of_lt ?_ ((hasFiniteIntegral_iff_norm _).1 hf.hasFiniteIntegral)
  apply lintegral_mono
  intro x
  simp [abs_le, le_abs_self]

theorem ofReal_toReal_ae_eq {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€áµ x âˆ‚Î¼, f x < âˆ) :
    (fun x => ENNReal.ofReal (f x).toReal) =áµ[Î¼] f := by
  filter_upwards [hf]
  intro x hx
  simp only [hx.ne, ofReal_toReal, Ne, not_false_iff]

theorem coe_toNNReal_ae_eq {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€áµ x âˆ‚Î¼, f x < âˆ) :
    (fun x => ((f x).toNNReal : â„â‰¥0âˆ)) =áµ[Î¼] f := by
  filter_upwards [hf]
  intro x hx
  simp only [hx.ne, Ne, not_false_iff, coe_toNNReal]

section count

variable [MeasurableSingletonClass Î±] {f : Î± â†’ Î²}

/-- A function is integrable for the counting measure iff its norm is summable. -/
lemma integrable_count_iff :
    Integrable f Measure.count â†” Summable (â€–f Â·â€–) := by
  -- Note: this proof would be much easier if we assumed `SecondCountableTopology G`. Without
  -- this we have to justify the claim that `f` lands a.e. in a separable subset, which is true
  -- (because summable functions have countable range) but slightly tedious to check.
  rw [Integrable, hasFiniteIntegral_count_iff, and_iff_right_iff_imp]
  intro hs
  have hs' : (Function.support f).Countable := by
    simpa only [Ne, Pi.zero_apply, eq_comm, Function.support, norm_eq_zero]
      using hs.countable_support
  letI : MeasurableSpace Î² := borel Î²
  haveI : BorelSpace Î² := âŸ¨rflâŸ©
  refine aestronglyMeasurable_iff_aemeasurable_separable.mpr âŸ¨?_, ?_âŸ©
  Â· refine (measurable_zero.measurable_of_countable_ne ?_).aemeasurable
    simpa only [Ne, Pi.zero_apply, eq_comm, Function.support] using hs'
  Â· refine âŸ¨f '' univ, ?_, ae_of_all _ fun a â†¦ âŸ¨a, âŸ¨mem_univ _, rflâŸ©âŸ©âŸ©
    suffices f '' univ âŠ† (f '' f.support) âˆª {0} from
      (((hs'.image f).union (countable_singleton 0)).mono this).isSeparable
    grind [Function.mem_support]

end count

section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

theorem integrable_withDensity_iff_integrable_coe_smul {f : Î± â†’ â„â‰¥0} (hf : Measurable f)
    {g : Î± â†’ E} :
    Integrable g (Î¼.withDensity fun x => f x) â†” Integrable (fun x => (f x : â„) â€¢ g x) Î¼ := by
  by_cases H : AEStronglyMeasurable (fun x : Î± => (f x : â„) â€¢ g x) Î¼
  Â· simp only [Integrable, aestronglyMeasurable_withDensity_iff hf, hasFiniteIntegral_iff_enorm, H,
      true_and]
    rw [lintegral_withDensity_eq_lintegral_mulâ‚€' hf.coe_nnreal_ennreal.aemeasurable]
    Â· simp [enorm_smul]
    Â· simpa [aemeasurable_withDensity_ennreal_iff hf, enorm_smul] using H.enorm
  Â· simp only [Integrable, aestronglyMeasurable_withDensity_iff hf, H, false_and]

theorem integrable_withDensity_iff_integrable_smul {f : Î± â†’ â„â‰¥0} (hf : Measurable f) {g : Î± â†’ E} :
    Integrable g (Î¼.withDensity fun x => f x) â†” Integrable (fun x => f x â€¢ g x) Î¼ :=
  integrable_withDensity_iff_integrable_coe_smul hf

theorem integrable_withDensity_iff_integrable_smul' {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f)
    (hflt : âˆ€áµ x âˆ‚Î¼, f x < âˆ) {g : Î± â†’ E} :
    Integrable g (Î¼.withDensity f) â†” Integrable (fun x => (f x).toReal â€¢ g x) Î¼ := by
  rw [â† withDensity_congr_ae (coe_toNNReal_ae_eq hflt),
    integrable_withDensity_iff_integrable_smul]
  Â· simp_rw [NNReal.smul_def, ENNReal.toReal]
  Â· exact hf.ennreal_toNNReal

theorem integrable_withDensity_iff_integrable_coe_smulâ‚€ {f : Î± â†’ â„â‰¥0} (hf : AEMeasurable f Î¼)
    {g : Î± â†’ E} :
    Integrable g (Î¼.withDensity fun x => f x) â†” Integrable (fun x => (f x : â„) â€¢ g x) Î¼ :=
  calc
    Integrable g (Î¼.withDensity fun x => f x) â†”
        Integrable g (Î¼.withDensity fun x => (hf.mk f x : â„â‰¥0)) := by
      suffices (fun x => (f x : â„â‰¥0âˆ)) =áµ[Î¼] (fun x => (hf.mk f x : â„â‰¥0)) by
        rw [withDensity_congr_ae this]
      filter_upwards [hf.ae_eq_mk] with x hx
      simp [hx]
    _ â†” Integrable (fun x => ((hf.mk f x : â„â‰¥0) : â„) â€¢ g x) Î¼ :=
      integrable_withDensity_iff_integrable_coe_smul hf.measurable_mk
    _ â†” Integrable (fun x => (f x : â„) â€¢ g x) Î¼ := by
      apply integrable_congr
      filter_upwards [hf.ae_eq_mk] with x hx
      simp [hx]

theorem integrable_withDensity_iff_integrable_smulâ‚€' {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hflt : âˆ€áµ x âˆ‚Î¼, f x < âˆ) {g : Î± â†’ E} :
    Integrable g (Î¼.withDensity f) â†” Integrable (fun x => (f x).toReal â€¢ g x) Î¼ := by
  rw [â† withDensity_congr_ae (coe_toNNReal_ae_eq hflt),
    integrable_withDensity_iff_integrable_coe_smulâ‚€]
  Â· congr!
  Â· exact hf.ennreal_toNNReal

theorem integrable_withDensity_iff_integrable_smulâ‚€ {f : Î± â†’ â„â‰¥0} (hf : AEMeasurable f Î¼)
    {g : Î± â†’ E} : Integrable g (Î¼.withDensity fun x => f x) â†” Integrable (fun x => f x â€¢ g x) Î¼ :=
  integrable_withDensity_iff_integrable_coe_smulâ‚€ hf

end

theorem integrable_withDensity_iff {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (hflt : âˆ€áµ x âˆ‚Î¼, f x < âˆ)
    {g : Î± â†’ â„} : Integrable g (Î¼.withDensity f) â†” Integrable (fun x => g x * (f x).toReal) Î¼ := by
  have : (fun x => g x * (f x).toReal) = fun x => (f x).toReal â€¢ g x := by simp [mul_comm]
  rw [this]
  exact integrable_withDensity_iff_integrable_smul' hf hflt

section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

theorem memL1_smul_of_L1_withDensity {f : Î± â†’ â„â‰¥0} (f_meas : Measurable f)
    (u : Lp E 1 (Î¼.withDensity fun x => f x)) : MemLp (fun x => f x â€¢ u x) 1 Î¼ :=
  memLp_one_iff_integrable.2 <|
    (integrable_withDensity_iff_integrable_smul f_meas).1 <| memLp_one_iff_integrable.1 (Lp.memLp u)

variable (Î¼)

/-- The map `u â†¦ f â€¢ u` is an isometry between the `L^1` spaces for `Î¼.withDensity f` and `Î¼`. -/
noncomputable def withDensitySMulLI {f : Î± â†’ â„â‰¥0} (f_meas : Measurable f) :
    Lp E 1 (Î¼.withDensity fun x => f x) â†’â‚—áµ¢[â„] Lp E 1 Î¼ where
  toFun u := (memL1_smul_of_L1_withDensity f_meas u).toLp _
  map_add' := by
    intro u v
    ext1
    filter_upwards [(memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp,
      (memL1_smul_of_L1_withDensity f_meas v).coeFn_toLp,
      (memL1_smul_of_L1_withDensity f_meas (u + v)).coeFn_toLp,
      Lp.coeFn_add ((memL1_smul_of_L1_withDensity f_meas u).toLp _)
        ((memL1_smul_of_L1_withDensity f_meas v).toLp _),
      (ae_withDensity_iff f_meas.coe_nnreal_ennreal).1 (Lp.coeFn_add u v)]
    intro x hu hv huv h' h''
    rw [huv, h', Pi.add_apply, hu, hv]
    rcases eq_or_ne (f x) 0 with (hx | hx)
    Â· simp only [hx, zero_smul, add_zero]
    Â· rw [h'' _, Pi.add_apply, smul_add]
      simpa only [Ne, ENNReal.coe_eq_zero] using hx
  map_smul' := by
    intro r u
    ext1
    filter_upwards [(ae_withDensity_iff f_meas.coe_nnreal_ennreal).1 (Lp.coeFn_smul r u),
      (memL1_smul_of_L1_withDensity f_meas (r â€¢ u)).coeFn_toLp,
      Lp.coeFn_smul r ((memL1_smul_of_L1_withDensity f_meas u).toLp _),
      (memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp]
    intro x h h' h'' h'''
    rw [RingHom.id_apply, h', h'', Pi.smul_apply, h''']
    rcases eq_or_ne (f x) 0 with (hx | hx)
    Â· simp only [hx, zero_smul, smul_zero]
    Â· rw [h _, smul_comm, Pi.smul_apply]
      simpa only [Ne, ENNReal.coe_eq_zero] using hx
  norm_map' := by
    intro u
    simp only [eLpNorm, LinearMap.coe_mk, AddHom.coe_mk,
      one_ne_zero, ENNReal.one_ne_top, ENNReal.toReal_one, if_false, eLpNorm', ENNReal.rpow_one,
      _root_.div_one, Lp.norm_def]
    rw [lintegral_withDensity_eq_lintegral_mul_non_measurable _ f_meas.coe_nnreal_ennreal
        (Filter.Eventually.of_forall fun x => ENNReal.coe_lt_top)]
    congr 1
    apply lintegral_congr_ae
    filter_upwards [(memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp] with x hx
    rw [hx]
    simp [NNReal.smul_def, enorm_smul]

@[simp]
theorem withDensitySMulLI_apply {f : Î± â†’ â„â‰¥0} (f_meas : Measurable f)
    (u : Lp E 1 (Î¼.withDensity fun x => f x)) :
    withDensitySMulLI Î¼ (E := E) f_meas u =
      (memL1_smul_of_L1_withDensity f_meas u).toLp fun x => f x â€¢ u x :=
  rfl

end

section ENNReal

theorem mem_L1_toReal_of_lintegral_ne_top {f : Î± â†’ â„â‰¥0âˆ} (hfm : AEMeasurable f Î¼)
    (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) : MemLp (fun x â†¦ (f x).toReal) 1 Î¼ := by
  rw [MemLp, eLpNorm_one_eq_lintegral_enorm]
  exact âŸ¨(AEMeasurable.ennreal_toReal hfm).aestronglyMeasurable,
    hasFiniteIntegral_toReal_of_lintegral_ne_top hfiâŸ©

theorem integrable_toReal_of_lintegral_ne_top {f : Î± â†’ â„â‰¥0âˆ} (hfm : AEMeasurable f Î¼)
    (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) : Integrable (fun x â†¦ (f x).toReal) Î¼ :=
  memLp_one_iff_integrable.1 <| mem_L1_toReal_of_lintegral_ne_top hfm hfi

lemma integrable_toReal_iff {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hf_ne_top : âˆ€áµ x âˆ‚Î¼, f x â‰  âˆ) :
    Integrable (fun x â†¦ (f x).toReal) Î¼ â†” âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ := by
  rw [Integrable, hasFiniteIntegral_toReal_iff hf_ne_top]
  simp only [hf.ennreal_toReal.aestronglyMeasurable, ne_eq, true_and]

lemma lintegral_ofReal_ne_top_iff_integrable {f : Î± â†’ â„}
    (hfm : AEStronglyMeasurable f Î¼) (hf : 0 â‰¤áµ[Î¼] f) :
    âˆ«â» a, ENNReal.ofReal (f a) âˆ‚Î¼ â‰  âˆ â†” Integrable f Î¼ := by
  rw [Integrable, hasFiniteIntegral_iff_ofReal hf]
  simp [hfm]

end ENNReal

section PosPart

/-! ### Lemmas used for defining the positive part of an `LÂ¹` function -/


@[fun_prop]
theorem Integrable.pos_part {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    Integrable (fun a => max (f a) 0) Î¼ := by
  constructor <;> fun_prop

@[fun_prop]
theorem Integrable.neg_part {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    Integrable (fun a => max (-f a) 0) Î¼ :=
  hf.neg.pos_part

end PosPart

section IsBoundedSMul

variable {ğ•œ : Type*}
  {Îµ : Type*} [TopologicalSpace Îµ] [ESeminormedAddMonoid Îµ]

@[fun_prop]
theorem Integrable.smul [NormedAddCommGroup ğ•œ] [SMulZeroClass ğ•œ Î²] [IsBoundedSMul ğ•œ Î²] (c : ğ•œ)
    {f : Î± â†’ Î²} (hf : Integrable f Î¼) : Integrable (c â€¢ f) Î¼ := by
  constructor <;> fun_prop

@[fun_prop]
theorem Integrable.fun_smul [NormedAddCommGroup ğ•œ] [SMulZeroClass ğ•œ Î²] [IsBoundedSMul ğ•œ Î²] (c : ğ•œ)
    {f : Î± â†’ Î²} (hf : Integrable f Î¼) : Integrable (fun x â†¦ c â€¢ f x) Î¼ :=
  hf.smul c

@[fun_prop]
theorem Integrable.smul_enorm
    [NormedAddCommGroup ğ•œ] [SMul ğ•œ Îµ] [ContinuousConstSMul ğ•œ Îµ] [ENormSMulClass ğ•œ Îµ] (c : ğ•œ)
    {f : Î± â†’ Îµ} (hf : Integrable f Î¼) : Integrable (c â€¢ f) Î¼ := by
  constructor <;> fun_prop

theorem Integrable.fun_smul_enorm
    [NormedAddCommGroup ğ•œ] [SMul ğ•œ Îµ] [ContinuousConstSMul ğ•œ Îµ] [ENormSMulClass ğ•œ Îµ] (c : ğ•œ)
    {f : Î± â†’ Îµ} (hf : Integrable f Î¼) : Integrable (fun x â†¦ c â€¢ f x) Î¼ :=
  hf.smul_enorm c

theorem _root_.IsUnit.integrable_smul_iff [NormedRing ğ•œ] [MulActionWithZero ğ•œ Î²]
    [IsBoundedSMul ğ•œ Î²] {c : ğ•œ} (hc : IsUnit c) (f : Î± â†’ Î²) :
    Integrable (c â€¢ f) Î¼ â†” Integrable f Î¼ :=
  and_congr hc.aestronglyMeasurable_const_smul_iff (hasFiniteIntegral_smul_iff hc f)

theorem integrable_smul_iff [NormedDivisionRing ğ•œ] [MulActionWithZero ğ•œ Î²]
    [IsBoundedSMul ğ•œ Î²] {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
    Integrable (c â€¢ f) Î¼ â†” Integrable f Î¼ :=
  (IsUnit.mk0 _ hc).integrable_smul_iff f

theorem integrable_fun_smul_iff [NormedDivisionRing ğ•œ] [MulActionWithZero ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]
    {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
    Integrable (fun x â†¦ c â€¢ f x) Î¼ â†” Integrable f Î¼ :=
  integrable_smul_iff hc f

variable [NormedRing ğ•œ] [Module ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]

theorem Integrable.smul_of_top_right {f : Î± â†’ Î²} {Ï† : Î± â†’ ğ•œ} (hf : Integrable f Î¼)
    (hÏ† : MemLp Ï† âˆ Î¼) : Integrable (Ï† â€¢ f) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact MemLp.smul hf hÏ†

theorem Integrable.smul_of_top_left {f : Î± â†’ Î²} {Ï† : Î± â†’ ğ•œ} (hÏ† : Integrable Ï† Î¼)
    (hf : MemLp f âˆ Î¼) : Integrable (Ï† â€¢ f) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hÏ† âŠ¢
  exact MemLp.smul hf hÏ†

@[fun_prop]
theorem Integrable.smul_const {f : Î± â†’ ğ•œ} (hf : Integrable f Î¼) (c : Î²) :
    Integrable (fun x => f x â€¢ c) Î¼ :=
  hf.smul_of_top_left (memLp_top_const c)

end IsBoundedSMul

section NormedSpaceOverCompleteField

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem integrable_smul_const {f : Î± â†’ ğ•œ} {c : E} (hc : c â‰  0) :
    Integrable (fun x => f x â€¢ c) Î¼ â†” Integrable f Î¼ := by
  simp_rw [Integrable, aestronglyMeasurable_smul_const_iff (f := f) hc, and_congr_right_iff,
    hasFiniteIntegral_iff_enorm, enorm_smul]
  intro _; rw [lintegral_mul_const' _ _ enorm_ne_top, ENNReal.mul_lt_top_iff]
  have : âˆ€ x : â„â‰¥0âˆ, x = 0 â†’ x < âˆ := by simp
  simp [hc, or_iff_left_of_imp (this _)]

end NormedSpaceOverCompleteField

section NormedRing

variable {ğ•œ : Type*} [NormedRing ğ•œ] {f : Î± â†’ ğ•œ}

@[fun_prop]
theorem Integrable.const_mul {f : Î± â†’ ğ•œ} (h : Integrable f Î¼) (c : ğ•œ) :
    Integrable (fun x => c * f x) Î¼ :=
  h.smul c

theorem Integrable.const_mul' {f : Î± â†’ ğ•œ} (h : Integrable f Î¼) (c : ğ•œ) :
    Integrable ((fun _ : Î± => c) * f) Î¼ :=
  Integrable.const_mul h c

@[fun_prop]
theorem Integrable.mul_const {f : Î± â†’ ğ•œ} (h : Integrable f Î¼) (c : ğ•œ) :
    Integrable (fun x => f x * c) Î¼ :=
  h.smul (MulOpposite.op c)

theorem Integrable.mul_const' {f : Î± â†’ ğ•œ} (h : Integrable f Î¼) (c : ğ•œ) :
    Integrable (f * fun _ : Î± => c) Î¼ :=
  Integrable.mul_const h c

theorem integrable_const_mul_iff {c : ğ•œ} (hc : IsUnit c) (f : Î± â†’ ğ•œ) :
    Integrable (fun x => c * f x) Î¼ â†” Integrable f Î¼ :=
  hc.integrable_smul_iff f

theorem integrable_mul_const_iff {c : ğ•œ} (hc : IsUnit c) (f : Î± â†’ ğ•œ) :
    Integrable (fun x => f x * c) Î¼ â†” Integrable f Î¼ :=
  hc.op.integrable_smul_iff f

theorem Integrable.bdd_mul' {f g : Î± â†’ ğ•œ} {c : â„} (hg : Integrable g Î¼)
    (hf : AEStronglyMeasurable f Î¼) (hf_bound : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ c) :
    Integrable (fun x => f x * g x) Î¼ := by
  refine Integrable.mono' (hg.norm.smul c) (hf.mul hg.1) ?_
  filter_upwards [hf_bound] with x hx
  rw [Pi.smul_apply, smul_eq_mul]
  exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right hx (norm_nonneg _))

theorem Integrable.mul_of_top_right {f : Î± â†’ ğ•œ} {Ï† : Î± â†’ ğ•œ} (hf : Integrable f Î¼)
    (hÏ† : MemLp Ï† âˆ Î¼) : Integrable (Ï† * f) Î¼ :=
  hf.smul_of_top_right hÏ†

theorem Integrable.mul_of_top_left {f : Î± â†’ ğ•œ} {Ï† : Î± â†’ ğ•œ} (hÏ† : Integrable Ï† Î¼)
    (hf : MemLp f âˆ Î¼) : Integrable (Ï† * f) Î¼ :=
  hÏ†.smul_of_top_left hf

lemma MemLp.integrable_mul {p q : â„â‰¥0âˆ} {f g : Î± â†’ ğ•œ} (hf : MemLp f p Î¼) (hg : MemLp g q Î¼)
    [HolderTriple p q 1] :
    Integrable (f * g) Î¼ :=
  memLp_one_iff_integrable.1 <| hg.mul hf

end NormedRing

section NormedDivisionRing

variable {ğ•œ : Type*} [NormedDivisionRing ğ•œ] {f : Î± â†’ ğ•œ}

@[fun_prop]
theorem Integrable.div_const {f : Î± â†’ ğ•œ} (h : Integrable f Î¼) (c : ğ•œ) :
    Integrable (fun x => f x / c) Î¼ := by simp_rw [div_eq_mul_inv, h.mul_const]

end NormedDivisionRing

section RCLike

variable {ğ•œ : Type*} [RCLike ğ•œ] {f : Î± â†’ ğ•œ}

@[fun_prop]
theorem Integrable.ofReal {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    Integrable (fun x => (f x : ğ•œ)) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact hf.ofReal

theorem Integrable.re_im_iff :
    Integrable (fun x => RCLike.re (f x)) Î¼ âˆ§ Integrable (fun x => RCLike.im (f x)) Î¼ â†”
      Integrable f Î¼ := by
  simp_rw [â† memLp_one_iff_integrable]
  exact memLp_re_im_iff

@[fun_prop]
theorem Integrable.re (hf : Integrable f Î¼) : Integrable (fun x => RCLike.re (f x)) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact hf.re

@[fun_prop]
theorem Integrable.im (hf : Integrable f Î¼) : Integrable (fun x => RCLike.im (f x)) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact hf.im

end RCLike

section Trim

variable {H : Type*} [NormedAddCommGroup H] {m0 : MeasurableSpace Î±} {Î¼' : Measure Î±} {f : Î± â†’ H}

theorem Integrable.trim (hm : m â‰¤ m0) (hf_int : Integrable f Î¼') (hf : StronglyMeasurable[m] f) :
    Integrable f (Î¼'.trim hm) := by
  refine âŸ¨hf.aestronglyMeasurable, ?_âŸ©
  rw [HasFiniteIntegral, lintegral_trim hm _]
  Â· exact hf_int.2
  Â· fun_prop

theorem integrable_of_integrable_trim (hm : m â‰¤ m0) (hf_int : Integrable f (Î¼'.trim hm)) :
    Integrable f Î¼' := by
  obtain âŸ¨hf_meas_ae, hfâŸ© := hf_int
  refine âŸ¨aestronglyMeasurable_of_aestronglyMeasurable_trim hm hf_meas_ae, ?_âŸ©
  simpa [HasFiniteIntegral, lintegral_trim_ae hm hf_meas_ae.enorm] using hf

end Trim

section SigmaFinite

variable {E : Type*} {m0 : MeasurableSpace Î±} [NormedAddCommGroup E]
  {Îµ : Type*} [TopologicalSpace Îµ] [ContinuousENorm Îµ]

theorem integrable_of_forall_fin_meas_le' {Î¼ : Measure Î±} (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    (C : â„â‰¥0âˆ) (hC : C < âˆ) {f : Î± â†’ Îµ} (hf_meas : AEStronglyMeasurable f Î¼)
    (hf : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s â‰  âˆ â†’ âˆ«â» x in s, â€–f xâ€–â‚‘ âˆ‚Î¼ â‰¤ C) : Integrable f Î¼ :=
  âŸ¨hf_meas, (lintegral_le_of_forall_fin_meas_trim_le hm C hf).trans_lt hCâŸ©

theorem integrable_of_forall_fin_meas_le [SigmaFinite Î¼] (C : â„â‰¥0âˆ) (hC : C < âˆ) {f : Î± â†’ Îµ}
    (hf_meas : AEStronglyMeasurable[m] f Î¼)
    (hf : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s â‰  âˆ â†’ âˆ«â» x in s, â€–f xâ€–â‚‘ âˆ‚Î¼ â‰¤ C) :
    Integrable f Î¼ :=
  have : SigmaFinite (Î¼.trim le_rfl) := by rwa [@trim_eq_self _ m]
  integrable_of_forall_fin_meas_le' le_rfl C hC hf_meas hf

end SigmaFinite

section restrict

variable {Îµ : Type*} [TopologicalSpace Îµ] [ContinuousENorm Îµ] {f : Î± â†’ Îµ}

/-- One should usually use `MeasureTheory.Integrable.integrableOn` instead. -/
lemma Integrable.restrict (hf : Integrable f Î¼) {s : Set Î±} : Integrable f (Î¼.restrict s) :=
  hf.mono_measure Measure.restrict_le_self

end restrict

end MeasureTheory

section ContinuousLinearMap

open MeasureTheory

variable {E H : Type*} [NormedAddCommGroup E] [NormedAddCommGroup H]
  {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ']
  [NormedSpace ğ•œ' E] [NormedSpace ğ•œ H]

variable {Ïƒ : ğ•œ â†’+* ğ•œ'} {Ïƒ' : ğ•œ' â†’+* ğ•œ} [RingHomIsometric Ïƒ] [RingHomIsometric Ïƒ']
  [RingHomInvPair Ïƒ Ïƒ'] [RingHomInvPair Ïƒ' Ïƒ]

@[fun_prop]
theorem ContinuousLinearMap.integrable_comp {Ï† : Î± â†’ H} (L : H â†’SL[Ïƒ] E) (Ï†_int : Integrable Ï† Î¼) :
    Integrable (fun a : Î± => L (Ï† a)) Î¼ :=
  ((Integrable.norm Ï†_int).const_mul â€–Lâ€–).mono'
    (by fun_prop)
    (Eventually.of_forall fun a => L.le_opNorm (Ï† a))

@[simp]
theorem ContinuousLinearEquiv.integrable_comp_iff {Ï† : Î± â†’ H} (L : H â‰ƒSL[Ïƒ] E) :
    Integrable (fun a : Î± â†¦ L (Ï† a)) Î¼ â†” Integrable Ï† Î¼ :=
  âŸ¨fun h â†¦ by simpa using ContinuousLinearMap.integrable_comp (L.symm : E â†’SL[Ïƒ'] H) h,
  fun h â†¦ ContinuousLinearMap.integrable_comp (L : H â†’SL[Ïƒ] E) hâŸ©

@[simp]
theorem LinearIsometryEquiv.integrable_comp_iff {Ï† : Î± â†’ H} (L : H â‰ƒâ‚›â‚—áµ¢[Ïƒ] E) :
    Integrable (fun a : Î± â†¦ L (Ï† a)) Î¼ â†” Integrable Ï† Î¼ :=
  ContinuousLinearEquiv.integrable_comp_iff (L : H â‰ƒSL[Ïƒ] E)

theorem MeasureTheory.Integrable.apply_continuousLinearMap {Ï† : Î± â†’ H â†’SL[Ïƒ] E}
    (Ï†_int : Integrable Ï† Î¼) (v : H) : Integrable (fun a => Ï† a v) Î¼ :=
  (ContinuousLinearMap.apply' E Ïƒ v).integrable_comp Ï†_int

end ContinuousLinearMap

namespace MeasureTheory

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F]

@[fun_prop]
lemma Integrable.fst {f : Î± â†’ E Ã— F} (hf : Integrable f Î¼) : Integrable (fun x â†¦ (f x).1) Î¼ :=
  (ContinuousLinearMap.fst â„ E F).integrable_comp hf

@[fun_prop]
lemma Integrable.snd {f : Î± â†’ E Ã— F} (hf : Integrable f Î¼) : Integrable (fun x â†¦ (f x).2) Î¼ :=
  (ContinuousLinearMap.snd â„ E F).integrable_comp hf

lemma integrable_prod {f : Î± â†’ E Ã— F} :
    Integrable f Î¼ â†” Integrable (fun x â†¦ (f x).1) Î¼ âˆ§ Integrable (fun x â†¦ (f x).2) Î¼ :=
  âŸ¨fun h â†¦ âŸ¨h.fst, h.sndâŸ©, fun h â†¦ h.1.prodMk h.2âŸ©

section Limit

/-- If `G n` tends to `f` a.e. and each `â€–G n Â·â€–â‚‘` is `AEMeasurable`, then the lower Lebesgue
integral of `â€–f Â·â€–â‚‘` is at most the liminf of the lower Lebesgue integral of `â€–G n Â·â€–â‚‘`. -/
theorem lintegral_enorm_le_liminf_of_tendsto
    {G : â„• â†’ â„ â†’ â„} {f : â„ â†’ â„} {Î¼ : Measure â„}
    (hGf : âˆ€áµ x âˆ‚Î¼, Tendsto (fun (n : â„•) â†¦ G n x) atTop (ğ“ (f x)))
    (hG : âˆ€ (n : â„•), AEMeasurable (fun x â†¦ â€–G n xâ€–â‚‘) Î¼) :
    âˆ«â» x, â€–f xâ€–â‚‘ âˆ‚Î¼ â‰¤ liminf (fun n â†¦ âˆ«â» x, â€–G n xâ€–â‚‘ âˆ‚Î¼) atTop :=
  lintegral_congr_ae (by filter_upwards [hGf] with x hx using hx.enorm.liminf_eq) â–¸
    (MeasureTheory.lintegral_liminf_le' hG)

/-- If `G n` tends to `f` a.e. and each `G n` is `AEStronglyMeasurable`, then the lower Lebesgue
integral of `â€–f Â·â€–â‚‘` is at most the liminf of the lower Lebesgue integral of `â€–G n Â·â€–â‚‘`. -/
theorem lintegral_enorm_le_liminf_of_tendsto'
    {G : â„• â†’ â„ â†’ â„} {f : â„ â†’ â„} {Î¼ : Measure â„}
    (hGf : âˆ€áµ x âˆ‚Î¼, Tendsto (fun (n : â„•) â†¦ G n x) atTop (ğ“ (f x)))
    (hG : âˆ€ (n : â„•), AEStronglyMeasurable (G n) Î¼) :
    âˆ«â» x, â€–f xâ€–â‚‘ âˆ‚Î¼ â‰¤ liminf (fun n â†¦ âˆ«â» x, â€–G n xâ€–â‚‘ âˆ‚Î¼) atTop :=
  lintegral_enorm_le_liminf_of_tendsto hGf (fun n â†¦ (hG n).aemeasurable.enorm)

/-- If `G n` tends to `f` a.e., each `G n` is `AEStronglyMeasurable` and the liminf of the lower
Lebesgue integral of `â€–G n Â·â€–â‚‘` is finite, then `f` is Lebesgue integrable. -/
theorem integrable_of_tendsto
    {G : â„• â†’ â„ â†’ â„} {f : â„ â†’ â„} {Î¼ : Measure â„}
    (hGf : âˆ€áµ x âˆ‚Î¼, Tendsto (fun (n : â„•) â†¦ G n x) atTop (ğ“ (f x)))
    (hG : âˆ€ (n : â„•), AEStronglyMeasurable (G n) Î¼)
    (hG' : liminf (fun n â†¦ âˆ«â» x, â€–G n xâ€–â‚‘ âˆ‚Î¼) atTop â‰  âŠ¤) :
    Integrable f Î¼ :=
  âŸ¨aestronglyMeasurable_of_tendsto_ae _ hG hGf,
   lt_of_le_of_lt (lintegral_enorm_le_liminf_of_tendsto' hGf hG) hG'.lt_topâŸ©

end Limit

end MeasureTheory
