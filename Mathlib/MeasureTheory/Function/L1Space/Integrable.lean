/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import Mathlib.MeasureTheory.Function.L1Space.HasFiniteIntegral
import Mathlib.MeasureTheory.Function.LpOrder
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Lemmas

/-!
# Integrable functions

In this file, the predicate `Integrable` is defined and basic properties of
integrable functions are proved.

Such a predicate is already available under the name `MemLp 1`. We give a direct definition which
is easier to use, and show that it is equivalent to `MemLp 1`.

## Main definition

* Let `f : Œ± ‚Üí Œ≤` be a function, where `Œ±` is a `MeasureSpace` and `Œ≤` a `NormedAddCommGroup`
  which also a `MeasurableSpace`. Then `f` is called `Integrable` if
  `f` is `Measurable` and `HasFiniteIntegral f` holds.

## Implementation notes

To prove something for an arbitrary integrable function, a useful theorem is
`Integrable.induction` in the file `SetIntegral`.

## Tags

integrable

-/


noncomputable section

open EMetric ENNReal Filter MeasureTheory NNReal Set TopologicalSpace

variable {Œ± Œ≤ Œ≥ Œ¥ Œµ Œµ' Œµ'' : Type*} {m : MeasurableSpace Œ±} {Œº ŒΩ : Measure Œ±} [MeasurableSpace Œ¥]
variable [NormedAddCommGroup Œ≤] [NormedAddCommGroup Œ≥]
  [TopologicalSpace Œµ] [ContinuousENorm Œµ] [TopologicalSpace Œµ'] [ContinuousENorm Œµ'] [ENorm Œµ'']

namespace MeasureTheory

/-! ### The predicate `Integrable` -/

/-- `Integrable f Œº` means that `f` is measurable and that the integral `‚à´‚Åª a, ‚Äñf a‚Äñ ‚àÇŒº` is finite.
  `Integrable f` means `Integrable f volume`. -/
@[fun_prop]
def Integrable {Œ±} {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí Œµ)
    (Œº : Measure Œ± := by volume_tac) : Prop :=
  AEStronglyMeasurable f Œº ‚àß HasFiniteIntegral f Œº

/-- Notation for `Integrable` with respect to a non-standard œÉ-algebra. -/
scoped notation "Integrable[" mŒ± "]" => @Integrable _ _ _ _ mŒ±

theorem memLp_one_iff_integrable {f : Œ± ‚Üí Œµ} : MemLp f 1 Œº ‚Üî Integrable f Œº := by
  simp_rw [Integrable, hasFiniteIntegral_iff_enorm, MemLp, eLpNorm_one_eq_lintegral_enorm]

@[fun_prop]
theorem Integrable.aestronglyMeasurable {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) :
    AEStronglyMeasurable f Œº :=
  hf.1

@[fun_prop]
theorem Integrable.aemeasurable [MeasurableSpace Œµ] [BorelSpace Œµ] [PseudoMetrizableSpace Œµ]
    {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) : AEMeasurable f Œº :=
  hf.aestronglyMeasurable.aemeasurable

theorem Integrable.hasFiniteIntegral {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) : HasFiniteIntegral f Œº :=
  hf.2

theorem Integrable.mono_enorm {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} (hg : Integrable g Œº)
    (hf : AEStronglyMeasurable f Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ‚Çë ‚â§ ‚Äñg a‚Äñ‚Çë) : Integrable f Œº :=
  ‚ü®hf, hg.hasFiniteIntegral.mono_enorm h‚ü©

theorem Integrable.mono {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hg : Integrable g Œº)
    (hf : AEStronglyMeasurable f Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ ‚â§ ‚Äñg a‚Äñ) : Integrable f Œº :=
  ‚ü®hf, hg.hasFiniteIntegral.mono h‚ü©

theorem Integrable.mono'_enorm {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hg : Integrable g Œº)
    (hf : AEStronglyMeasurable f Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ‚Çë ‚â§ g a) : Integrable f Œº :=
  ‚ü®hf, hg.hasFiniteIntegral.mono_enorm h‚ü©

theorem Integrable.mono' {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí ‚Ñù} (hg : Integrable g Œº)
    (hf : AEStronglyMeasurable f Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ ‚â§ g a) : Integrable f Œº :=
  ‚ü®hf, hg.hasFiniteIntegral.mono' h‚ü©

theorem Integrable.congr'_enorm {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'} (hf : Integrable f Œº)
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ‚Çë = ‚Äñg a‚Äñ‚Çë) : Integrable g Œº :=
  ‚ü®hg, hf.hasFiniteIntegral.congr'_enorm h‚ü©

theorem Integrable.congr' {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : Integrable f Œº)
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ = ‚Äñg a‚Äñ) : Integrable g Œº :=
  ‚ü®hg, hf.hasFiniteIntegral.congr' h‚ü©

theorem integrable_congr'_enorm {f : Œ± ‚Üí Œµ} {g : Œ± ‚Üí Œµ'}
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ‚Çë = ‚Äñg a‚Äñ‚Çë) :
    Integrable f Œº ‚Üî Integrable g Œº :=
  ‚ü®fun h2f => h2f.congr'_enorm hg h, fun h2g => h2g.congr'_enorm hf <| EventuallyEq.symm h‚ü©

theorem integrable_congr' {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ = ‚Äñg a‚Äñ) :
    Integrable f Œº ‚Üî Integrable g Œº :=
  integrable_congr'_enorm hf hg <| h.mono fun _x hx ‚Ü¶ enorm_eq_iff_norm_eq.mpr hx

theorem Integrable.congr {f g : Œ± ‚Üí Œµ} (hf : Integrable f Œº) (h : f =·µê[Œº] g) : Integrable g Œº :=
  ‚ü®hf.1.congr h, hf.2.congr h‚ü©

theorem integrable_congr {f g : Œ± ‚Üí Œµ} (h : f =·µê[Œº] g) : Integrable f Œº ‚Üî Integrable g Œº :=
  ‚ü®fun hf => hf.congr h, fun hg => hg.congr h.symm‚ü©

theorem integrable_const_iff_enorm {c : Œµ} (hc : ‚Äñc‚Äñ‚Çë ‚â† ‚àû) :
    Integrable (fun _ : Œ± => c) Œº ‚Üî ‚Äñc‚Äñ‚Çë = 0 ‚à® IsFiniteMeasure Œº := by
  have : AEStronglyMeasurable (fun _ : Œ± => c) Œº := aestronglyMeasurable_const
  rw [Integrable, and_iff_right this, hasFiniteIntegral_const_iff_enorm hc]

lemma integrable_const_iff {c : Œ≤} : Integrable (fun _ : Œ± => c) Œº ‚Üî c = 0 ‚à® IsFiniteMeasure Œº := by
  rw [integrable_const_iff_enorm enorm_ne_top]
  simp

lemma integrable_const_iff_isFiniteMeasure_enorm {c : Œµ} (hc : ‚Äñc‚Äñ‚Çë ‚â† 0) (hc' : ‚Äñc‚Äñ‚Çë ‚â† ‚àû) :
    Integrable (fun _ ‚Ü¶ c) Œº ‚Üî IsFiniteMeasure Œº := by
  simp [integrable_const_iff_enorm hc', hc, isFiniteMeasure_iff]

lemma integrable_const_iff_isFiniteMeasure {c : Œ≤} (hc : c ‚â† 0) :
    Integrable (fun _ ‚Ü¶ c) Œº ‚Üî IsFiniteMeasure Œº := by
  simp [integrable_const_iff, hc, isFiniteMeasure_iff]

theorem Integrable.of_mem_Icc_enorm [IsFiniteMeasure Œº]
    {a b : ‚Ñù‚â•0‚àû} (ha : a ‚â† ‚àû) (hb : b ‚â† ‚àû) {X : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hX : AEMeasurable X Œº)
    (h : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚àà Set.Icc a b) :
    Integrable X Œº :=
  ‚ü®hX.aestronglyMeasurable, .of_mem_Icc_of_ne_top ha hb h‚ü©

theorem Integrable.of_mem_Icc [IsFiniteMeasure Œº] (a b : ‚Ñù) {X : Œ± ‚Üí ‚Ñù} (hX : AEMeasurable X Œº)
    (h : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚àà Set.Icc a b) :
    Integrable X Œº :=
  ‚ü®hX.aestronglyMeasurable, .of_mem_Icc a b h‚ü©

@[simp, fun_prop]
theorem integrable_const_enorm [IsFiniteMeasure Œº] {c : Œµ} (hc : ‚Äñc‚Äñ‚Çë ‚â† ‚àû) :
    Integrable (fun _ : Œ± ‚Ü¶ c) Œº :=
  (integrable_const_iff_enorm hc).2 <| .inr ‚Äπ_‚Ä∫

@[fun_prop]
theorem integrable_const [IsFiniteMeasure Œº] (c : Œ≤) : Integrable (fun _ : Œ± => c) Œº :=
  integrable_const_iff.2 <| .inr ‚Äπ_‚Ä∫

-- TODO: an `ENorm`-version of this lemma requires `HasFiniteIntegral.of_finite`
@[simp]
lemma Integrable.of_finite [Finite Œ±] [MeasurableSingletonClass Œ±] [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œ≤} :
    Integrable f Œº := ‚ü®.of_discrete, .of_finite‚ü©

/-- This lemma is a special case of `Integrable.of_finite`. -/
lemma Integrable.of_isEmpty [IsEmpty Œ±] {f : Œ± ‚Üí Œ≤} : Integrable f Œº := .of_finite

/-- This lemma is a special case of `Integrable.of_finite`. -/
lemma Integrable.of_subsingleton [Subsingleton Œ±] [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œ≤} :
    Integrable f Œº :=
  .of_finite

theorem MemLp.integrable_enorm_rpow {f : Œ± ‚Üí Œµ} {p : ‚Ñù‚â•0‚àû} (hf : MemLp f p Œº) (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) : Integrable (fun x : Œ± => ‚Äñf x‚Äñ‚Çë ^ p.toReal) Œº := by
  rw [‚Üê memLp_one_iff_integrable]
  exact hf.enorm_rpow hp_ne_zero hp_ne_top

theorem MemLp.integrable_norm_rpow {f : Œ± ‚Üí Œ≤} {p : ‚Ñù‚â•0‚àû} (hf : MemLp f p Œº) (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) : Integrable (fun x : Œ± => ‚Äñf x‚Äñ ^ p.toReal) Œº := by
  rw [‚Üê memLp_one_iff_integrable]
  exact hf.norm_rpow hp_ne_zero hp_ne_top

theorem MemLp.integrable_enorm_rpow' [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œµ} {p : ‚Ñù‚â•0‚àû} (hf : MemLp f p Œº) :
    Integrable (fun x : Œ± => ‚Äñf x‚Äñ‚Çë ^ p.toReal) Œº := by
  by_cases h_zero : p = 0
  ¬∑ simp [h_zero]
  by_cases h_top : p = ‚àû
  ¬∑ simp [h_top]
  exact hf.integrable_enorm_rpow h_zero h_top

theorem MemLp.integrable_norm_rpow' [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œ≤} {p : ‚Ñù‚â•0‚àû} (hf : MemLp f p Œº) :
    Integrable (fun x : Œ± => ‚Äñf x‚Äñ ^ p.toReal) Œº := by
  by_cases h_zero : p = 0
  ¬∑ simp [h_zero]
  by_cases h_top : p = ‚àû
  ¬∑ simp [h_top]
  exact hf.integrable_norm_rpow h_zero h_top

lemma MemLp.integrable_enorm_pow {f : Œ± ‚Üí Œµ} {p : ‚Ñï} (hf : MemLp f p Œº) (hp : p ‚â† 0) :
    Integrable (fun x : Œ± ‚Ü¶ ‚Äñf x‚Äñ‚Çë ^ p) Œº := by
  simpa using hf.integrable_enorm_rpow (mod_cast hp) (by simp)

lemma MemLp.integrable_norm_pow {f : Œ± ‚Üí Œ≤} {p : ‚Ñï} (hf : MemLp f p Œº) (hp : p ‚â† 0) :
    Integrable (fun x : Œ± => ‚Äñf x‚Äñ ^ p) Œº := by
  simpa using hf.integrable_norm_rpow (mod_cast hp) (by simp)

lemma MemLp.integrable_enorm_pow' [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œµ} {p : ‚Ñï} (hf : MemLp f p Œº) :
    Integrable (fun x : Œ± ‚Ü¶ ‚Äñf x‚Äñ‚Çë ^ p) Œº := by simpa using hf.integrable_enorm_rpow'

lemma MemLp.integrable_norm_pow' [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œ≤} {p : ‚Ñï} (hf : MemLp f p Œº) :
    Integrable (fun x : Œ± => ‚Äñf x‚Äñ ^ p) Œº := by simpa using hf.integrable_norm_rpow'

lemma integrable_enorm_rpow_iff {f : Œ± ‚Üí Œµ} {p : ‚Ñù‚â•0‚àû}
    (hf : AEStronglyMeasurable f Œº) (p_zero : p ‚â† 0) (p_top : p ‚â† ‚àû) :
    Integrable (fun x : Œ± => ‚Äñf x‚Äñ‚Çë ^ p.toReal) Œº ‚Üî MemLp f p Œº := by
  rw [‚Üê memLp_enorm_rpow_iff (q := p) hf p_zero p_top, ‚Üê memLp_one_iff_integrable,
    ENNReal.div_self p_zero p_top]

lemma integrable_norm_rpow_iff {f : Œ± ‚Üí Œ≤} {p : ‚Ñù‚â•0‚àû}
    (hf : AEStronglyMeasurable f Œº) (p_zero : p ‚â† 0) (p_top : p ‚â† ‚àû) :
    Integrable (fun x : Œ± => ‚Äñf x‚Äñ ^ p.toReal) Œº ‚Üî MemLp f p Œº := by
  rw [‚Üê memLp_norm_rpow_iff (q := p) hf p_zero p_top, ‚Üê memLp_one_iff_integrable,
    ENNReal.div_self p_zero p_top]

theorem Integrable.mono_measure {f : Œ± ‚Üí Œµ} (h : Integrable f ŒΩ) (hŒº : Œº ‚â§ ŒΩ) : Integrable f Œº :=
  ‚ü®h.aestronglyMeasurable.mono_measure hŒº, h.hasFiniteIntegral.mono_measure hŒº‚ü©

theorem Integrable.of_measure_le_smul {Œµ} [TopologicalSpace Œµ] [ENormedAddMonoid Œµ]
    {Œº' : Measure Œ±} {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚àû) (hŒº'_le : Œº' ‚â§ c ‚Ä¢ Œº)
    {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) : Integrable f Œº' := by
  rw [‚Üê memLp_one_iff_integrable] at hf ‚ä¢
  exact hf.of_measure_le_smul hc hŒº'_le

@[fun_prop]
theorem Integrable.add_measure [PseudoMetrizableSpace Œµ]
    {f : Œ± ‚Üí Œµ} (hŒº : Integrable f Œº) (hŒΩ : Integrable f ŒΩ) :
    Integrable f (Œº + ŒΩ) := by
  simp_rw [‚Üê memLp_one_iff_integrable] at hŒº hŒΩ ‚ä¢
  refine ‚ü®hŒº.aestronglyMeasurable.add_measure hŒΩ.aestronglyMeasurable, ?_‚ü©
  rw [eLpNorm_one_add_measure, ENNReal.add_lt_top]
  exact ‚ü®hŒº.eLpNorm_lt_top, hŒΩ.eLpNorm_lt_top‚ü©

theorem Integrable.left_of_add_measure {f : Œ± ‚Üí Œµ} (h : Integrable f (Œº + ŒΩ)) : Integrable f Œº := by
  rw [‚Üê memLp_one_iff_integrable] at h ‚ä¢
  exact h.left_of_add_measure

theorem Integrable.right_of_add_measure {f : Œ± ‚Üí Œµ} (h : Integrable f (Œº + ŒΩ)) :
    Integrable f ŒΩ := by
  rw [‚Üê memLp_one_iff_integrable] at h ‚ä¢
  exact h.right_of_add_measure

@[simp]
theorem integrable_add_measure [PseudoMetrizableSpace Œµ] {f : Œ± ‚Üí Œµ} :
    Integrable f (Œº + ŒΩ) ‚Üî Integrable f Œº ‚àß Integrable f ŒΩ :=
  ‚ü®fun h => ‚ü®h.left_of_add_measure, h.right_of_add_measure‚ü©, fun h => h.1.add_measure h.2‚ü©

@[simp]
theorem integrable_zero_measure {f : Œ± ‚Üí Œµ} :
    Integrable f (0 : Measure Œ±) :=
  ‚ü®aestronglyMeasurable_zero_measure f, hasFiniteIntegral_zero_measure f‚ü©

/-- In a measurable space with measurable singletons, every function is integrable with respect to
a Dirac measure.
See `integrable_dirac'` for a version which requires `f` to be strongly measurable but does not
need singletons to be measurable. -/
@[fun_prop]
lemma integrable_dirac [MeasurableSingletonClass Œ±] {a : Œ±} {f : Œ± ‚Üí Œµ} (hfa : ‚Äñf a‚Äñ‚Çë < ‚àû) :
    Integrable f (Measure.dirac a) :=
  ‚ü®aestronglyMeasurable_dirac, by simpa [HasFiniteIntegral]‚ü©

/-- Every strongly measurable function is integrable with respect to a Dirac measure.
See `integrable_dirac` for a version which requires that singletons are measurable sets but has no
hypothesis on `f`. -/
@[fun_prop]
lemma integrable_dirac' {a : Œ±} {f : Œ± ‚Üí Œµ} (hf : StronglyMeasurable f) (hfa : ‚Äñf a‚Äñ‚Çë < ‚àû) :
    Integrable f (Measure.dirac a) :=
  ‚ü®hf.aestronglyMeasurable, by simpa [HasFiniteIntegral, lintegral_dirac' _ hf.enorm]‚ü©

theorem integrable_finset_sum_measure [PseudoMetrizableSpace Œµ]
    {Œπ} {m : MeasurableSpace Œ±} {f : Œ± ‚Üí Œµ} {Œº : Œπ ‚Üí Measure Œ±}
    {s : Finset Œπ} : Integrable f (‚àë i ‚àà s, Œº i) ‚Üî ‚àÄ i ‚àà s, Integrable f (Œº i) := by
  classical
  induction s using Finset.induction_on <;> simp [*]

section

variable {Œµ : Type*} [TopologicalSpace Œµ] [ENormedAddMonoid Œµ]

theorem Integrable.smul_measure {f : Œ± ‚Üí Œµ} (h : Integrable f Œº) {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚àû) :
    Integrable f (c ‚Ä¢ Œº) := by
  rw [‚Üê memLp_one_iff_integrable] at h ‚ä¢
  exact h.smul_measure hc

@[fun_prop]
theorem Integrable.smul_measure_nnreal {f : Œ± ‚Üí Œµ} (h : Integrable f Œº) {c : ‚Ñù‚â•0} :
    Integrable f (c ‚Ä¢ Œº) := by
  apply h.smul_measure
  simp

theorem integrable_smul_measure {f : Œ± ‚Üí Œµ} {c : ‚Ñù‚â•0‚àû} (h‚ÇÅ : c ‚â† 0) (h‚ÇÇ : c ‚â† ‚àû) :
    Integrable f (c ‚Ä¢ Œº) ‚Üî Integrable f Œº :=
  ‚ü®fun h => by
    simpa only [smul_smul, ENNReal.inv_mul_cancel h‚ÇÅ h‚ÇÇ, one_smul] using
      h.smul_measure (ENNReal.inv_ne_top.2 h‚ÇÅ),
    fun h => h.smul_measure h‚ÇÇ‚ü©

theorem integrable_inv_smul_measure {f : Œ± ‚Üí Œµ} {c : ‚Ñù‚â•0‚àû} (h‚ÇÅ : c ‚â† 0) (h‚ÇÇ : c ‚â† ‚àû) :
    Integrable f (c‚Åª¬π ‚Ä¢ Œº) ‚Üî Integrable f Œº :=
  integrable_smul_measure (by simpa using h‚ÇÇ) (by simpa using h‚ÇÅ)

theorem Integrable.to_average {f : Œ± ‚Üí Œµ} (h : Integrable f Œº) : Integrable f ((Œº univ)‚Åª¬π ‚Ä¢ Œº) := by
  rcases eq_or_ne Œº 0 with (rfl | hne)
  ¬∑ rwa [smul_zero]
  ¬∑ apply h.smul_measure
    simpa

open scoped Classical in
theorem integrable_average [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œµ} :
    Integrable f ((Œº univ)‚Åª¬π ‚Ä¢ Œº) ‚Üî Integrable f Œº :=
  (eq_or_ne Œº 0).by_cases (fun h => by simp [h]) fun h =>
    integrable_smul_measure (ENNReal.inv_ne_zero.2 <| measure_ne_top _ _)
      (ENNReal.inv_ne_top.2 <| mt Measure.measure_univ_eq_zero.1 h)

end

section

variable {Œ±' : Type*} [MeasurableSpace Œ±']

theorem integrable_map_measure {f : Œ± ‚Üí Œ±'} {g : Œ±' ‚Üí Œµ}
    (hg : AEStronglyMeasurable g (Measure.map f Œº)) (hf : AEMeasurable f Œº) :
    Integrable g (Measure.map f Œº) ‚Üî Integrable (g ‚àò f) Œº := by
  simp_rw [‚Üê memLp_one_iff_integrable]
  exact memLp_map_measure_iff hg hf

theorem Integrable.comp_aemeasurable {f : Œ± ‚Üí Œ±'} {g : Œ±' ‚Üí Œµ}
    (hg : Integrable g (Measure.map f Œº)) (hf : AEMeasurable f Œº) : Integrable (g ‚àò f) Œº :=
  (integrable_map_measure hg.aestronglyMeasurable hf).mp hg

theorem Integrable.comp_measurable {f : Œ± ‚Üí Œ±'} {g : Œ±' ‚Üí Œµ} (hg : Integrable g (Measure.map f Œº))
    (hf : Measurable f) : Integrable (g ‚àò f) Œº :=
  hg.comp_aemeasurable hf.aemeasurable

end

theorem _root_.MeasurableEmbedding.integrable_map_iff {f : Œ± ‚Üí Œ¥} (hf : MeasurableEmbedding f)
    {g : Œ¥ ‚Üí Œµ} : Integrable g (Measure.map f Œº) ‚Üî Integrable (g ‚àò f) Œº := by
  simp_rw [‚Üê memLp_one_iff_integrable]
  exact hf.memLp_map_measure_iff

theorem integrable_map_equiv (f : Œ± ‚âÉ·µê Œ¥) (g : Œ¥ ‚Üí Œµ) :
    Integrable g (Measure.map f Œº) ‚Üî Integrable (g ‚àò f) Œº := by
  simp_rw [‚Üê memLp_one_iff_integrable]
  exact f.memLp_map_measure_iff

theorem MeasurePreserving.integrable_comp {ŒΩ : Measure Œ¥} {g : Œ¥ ‚Üí Œµ} {f : Œ± ‚Üí Œ¥}
    (hf : MeasurePreserving f Œº ŒΩ) (hg : AEStronglyMeasurable g ŒΩ) :
    Integrable (g ‚àò f) Œº ‚Üî Integrable g ŒΩ := by
  rw [‚Üê hf.map_eq] at hg ‚ä¢
  exact (integrable_map_measure hg hf.measurable.aemeasurable).symm

theorem MeasurePreserving.integrable_comp_emb {f : Œ± ‚Üí Œ¥} {ŒΩ} (h‚ÇÅ : MeasurePreserving f Œº ŒΩ)
    (h‚ÇÇ : MeasurableEmbedding f) {g : Œ¥ ‚Üí Œµ} : Integrable (g ‚àò f) Œº ‚Üî Integrable g ŒΩ :=
  h‚ÇÅ.map_eq ‚ñ∏ Iff.symm h‚ÇÇ.integrable_map_iff

theorem lintegral_edist_lt_top {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    (‚à´‚Åª a, edist (f a) (g a) ‚àÇŒº) < ‚àû :=
  lt_of_le_of_lt (lintegral_edist_triangle hf.aestronglyMeasurable aestronglyMeasurable_zero)
    (ENNReal.add_lt_top.2 <| by
      simp_rw [Pi.zero_apply, ‚Üê hasFiniteIntegral_iff_edist]
      exact ‚ü®hf.hasFiniteIntegral, hg.hasFiniteIntegral‚ü©)

section ENormedAddMonoid

variable {Œµ' : Type*} [TopologicalSpace Œµ'] [ENormedAddMonoid Œµ']

variable (Œ± Œµ') in
@[simp]
theorem integrable_zero (Œº : Measure Œ±) : Integrable (fun _ => (0 : Œµ')) Œº := by
  simp [Integrable, aestronglyMeasurable_const]

theorem Integrable.add' {f g : Œ± ‚Üí Œµ'} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    HasFiniteIntegral (f + g) Œº :=
  calc
    ‚à´‚Åª a, ‚Äñf a + g a‚Äñ‚Çë ‚àÇŒº ‚â§ ‚à´‚Åª a, ‚Äñf a‚Äñ‚Çë + ‚Äñg a‚Äñ‚Çë ‚àÇŒº := lintegral_mono fun _ ‚Ü¶ enorm_add_le _ _
    _ = _ := lintegral_enorm_add_left hf.aestronglyMeasurable _
    _ < ‚àû := add_lt_top.2 ‚ü®hf.hasFiniteIntegral, hg.hasFiniteIntegral‚ü©

@[fun_prop]
theorem Integrable.add [ContinuousAdd Œµ']
    {f g : Œ± ‚Üí Œµ'} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    Integrable (f + g) Œº :=
  ‚ü®hf.aestronglyMeasurable.add hg.aestronglyMeasurable, hf.add' hg‚ü©

@[fun_prop]
theorem Integrable.add'' [ContinuousAdd Œµ']
    {f g : Œ± ‚Üí Œµ'} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    Integrable (fun x ‚Ü¶ f x + g x) Œº := hf.add hg

@[simp]
lemma Integrable.of_subsingleton_codomain [Subsingleton Œµ'] {f : Œ± ‚Üí Œµ'} :
    Integrable f Œº :=
  integrable_zero _ _ _ |>.congr <| .of_forall fun _ ‚Ü¶ Subsingleton.elim _ _

end ENormedAddMonoid

section ENormedAddCommMonoid

variable {Œµ' : Type*} [TopologicalSpace Œµ'] [ENormedAddCommMonoid Œµ'] [ContinuousAdd Œµ']

@[fun_prop]
theorem integrable_finset_sum' {Œπ} (s : Finset Œπ) {f : Œπ ‚Üí Œ± ‚Üí Œµ'}
    (hf : ‚àÄ i ‚àà s, Integrable (f i) Œº) : Integrable (‚àë i ‚àà s, f i) Œº :=
  Finset.sum_induction f (fun g => Integrable g Œº) (fun _ _ => Integrable.add)
    (integrable_zero _ _ _) hf

@[fun_prop]
theorem integrable_finset_sum {Œπ} (s : Finset Œπ) {f : Œπ ‚Üí Œ± ‚Üí Œµ'}
    (hf : ‚àÄ i ‚àà s, Integrable (f i) Œº) : Integrable (fun a => ‚àë i ‚àà s, f i a) Œº := by
  simpa only [‚Üê Finset.sum_apply] using integrable_finset_sum' s hf

end ENormedAddCommMonoid

/-- If `f` is integrable, then so is `-f`.
See `Integrable.neg'` for the same statement, but formulated with `x ‚Ü¶ - f x` instead of `-f`. -/
@[fun_prop]
theorem Integrable.neg {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) : Integrable (-f) Œº :=
  ‚ü®hf.aestronglyMeasurable.neg, hf.hasFiniteIntegral.neg‚ü©

/-- If `f` is integrable, then so is `fun x ‚Ü¶ - f x`.
See `Integrable.neg` for the same statement, but formulated with `-f` instead of `fun x ‚Ü¶ - f x`. -/
@[fun_prop]
theorem Integrable.neg' {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) : Integrable (fun x ‚Ü¶ - f x) Œº :=
  ‚ü®hf.aestronglyMeasurable.neg, hf.hasFiniteIntegral.neg‚ü©

@[simp]
theorem integrable_neg_iff {f : Œ± ‚Üí Œ≤} : Integrable (-f) Œº ‚Üî Integrable f Œº :=
  ‚ü®fun h => neg_neg f ‚ñ∏ h.neg, Integrable.neg‚ü©

/-- if `f` is integrable, then `f + g` is integrable iff `g` is.
See `integrable_add_iff_integrable_right'` for the same statement with `fun x ‚Ü¶ f x + g x` instead
of `f + g`. -/
@[simp]
lemma integrable_add_iff_integrable_right {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) :
    Integrable (f + g) Œº ‚Üî Integrable g Œº :=
  ‚ü®fun h ‚Ü¶ show g = f + g + (-f) by simp only [add_neg_cancel_comm] ‚ñ∏ h.add hf.neg,
    fun h ‚Ü¶ hf.add h‚ü©

/-- if `f` is integrable, then `fun x ‚Ü¶ f x + g x` is integrable iff `g` is.
See `integrable_add_iff_integrable_right` for the same statement with `f + g` instead
of `fun x ‚Ü¶ f x + g x`. -/
@[simp]
lemma integrable_add_iff_integrable_right' {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) :
    Integrable (fun x ‚Ü¶ f x + g x) Œº ‚Üî Integrable g Œº :=
  integrable_add_iff_integrable_right hf

/-- if `f` is integrable, then `g + f` is integrable iff `g` is.
See `integrable_add_iff_integrable_left'` for the same statement with `fun x ‚Ü¶ g x + f x` instead
of `g + f`. -/
@[simp]
lemma integrable_add_iff_integrable_left {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) :
    Integrable (g + f) Œº ‚Üî Integrable g Œº := by
  rw [add_comm, integrable_add_iff_integrable_right hf]

/-- if `f` is integrable, then `fun x ‚Ü¶ g x + f x` is integrable iff `g` is.
See `integrable_add_iff_integrable_left'` for the same statement with `g + f` instead
of `fun x ‚Ü¶ g x + f x`. -/
@[simp]
lemma integrable_add_iff_integrable_left' {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) :
    Integrable (fun x ‚Ü¶ g x + f x) Œº ‚Üî Integrable g Œº :=
  integrable_add_iff_integrable_left hf

lemma integrable_left_of_integrable_add_of_nonneg {f g : Œ± ‚Üí ‚Ñù}
    (h_meas : AEStronglyMeasurable f Œº) (hf : 0 ‚â§·µê[Œº] f) (hg : 0 ‚â§·µê[Œº] g)
    (h_int : Integrable (f + g) Œº) : Integrable f Œº := by
  refine h_int.mono' h_meas ?_
  filter_upwards [hf, hg] with a haf hag
  exact (Real.norm_of_nonneg haf).symm ‚ñ∏ le_add_of_nonneg_right hag

lemma integrable_right_of_integrable_add_of_nonneg {f g : Œ± ‚Üí ‚Ñù}
    (h_meas : AEStronglyMeasurable f Œº) (hf : 0 ‚â§·µê[Œº] f) (hg : 0 ‚â§·µê[Œº] g)
    (h_int : Integrable (f + g) Œº) : Integrable g Œº :=
  integrable_left_of_integrable_add_of_nonneg
    ((AEStronglyMeasurable.add_iff_right h_meas).mp h_int.aestronglyMeasurable)
      hg hf (add_comm f g ‚ñ∏ h_int)

lemma integrable_add_iff_of_nonneg {f g : Œ± ‚Üí ‚Ñù} (h_meas : AEStronglyMeasurable f Œº)
    (hf : 0 ‚â§·µê[Œº] f) (hg : 0 ‚â§·µê[Œº] g) :
    Integrable (f + g) Œº ‚Üî Integrable f Œº ‚àß Integrable g Œº :=
  ‚ü®fun h ‚Ü¶ ‚ü®integrable_left_of_integrable_add_of_nonneg h_meas hf hg h,
    integrable_right_of_integrable_add_of_nonneg h_meas hf hg h‚ü©, fun ‚ü®hf, hg‚ü© ‚Ü¶ hf.add hg‚ü©

lemma integrable_add_iff_of_nonpos {f g : Œ± ‚Üí ‚Ñù} (h_meas : AEStronglyMeasurable f Œº)
    (hf : f ‚â§·µê[Œº] 0) (hg : g ‚â§·µê[Œº] 0) :
    Integrable (f + g) Œº ‚Üî Integrable f Œº ‚àß Integrable g Œº := by
  rw [‚Üê integrable_neg_iff, ‚Üê integrable_neg_iff (f := f), ‚Üê integrable_neg_iff (f := g), neg_add]
  exact integrable_add_iff_of_nonneg h_meas.neg (hf.mono (fun _ ‚Ü¶ neg_nonneg_of_nonpos))
    (hg.mono (fun _ ‚Ü¶ neg_nonneg_of_nonpos))

lemma integrable_add_const_iff [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œ≤} {c : Œ≤} :
    Integrable (fun x ‚Ü¶ f x + c) Œº ‚Üî Integrable f Œº :=
  integrable_add_iff_integrable_left (integrable_const _)

lemma integrable_const_add_iff [IsFiniteMeasure Œº] {f : Œ± ‚Üí Œ≤} {c : Œ≤} :
    Integrable (fun x ‚Ü¶ c + f x) Œº ‚Üî Integrable f Œº :=
  integrable_add_iff_integrable_right (integrable_const _)

-- TODO: generalise these lemmas to an `ENormedAddCommSubMonoid`
@[fun_prop]
theorem Integrable.sub {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    Integrable (f - g) Œº := by simpa only [sub_eq_add_neg] using hf.add hg.neg

@[fun_prop]
theorem Integrable.sub' {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    Integrable (fun a ‚Ü¶ f a - g a) Œº := by simpa only [sub_eq_add_neg] using hf.add hg.neg

@[fun_prop]
theorem Integrable.enorm {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) : Integrable (‚Äñf ¬∑‚Äñ‚Çë) Œº :=
  ‚ü®hf.aestronglyMeasurable.enorm.aestronglyMeasurable, hf.hasFiniteIntegral.enorm‚ü©

@[fun_prop]
theorem Integrable.norm {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) : Integrable (fun a => ‚Äñf a‚Äñ) Œº :=
  ‚ü®hf.aestronglyMeasurable.norm, hf.hasFiniteIntegral.norm‚ü©

@[fun_prop]
theorem Integrable.inf {Œ≤}
    [NormedAddCommGroup Œ≤] [Lattice Œ≤] [HasSolidNorm Œ≤] [IsOrderedAddMonoid Œ≤]
    {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº)
    (hg : Integrable g Œº) : Integrable (f ‚äì g) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf hg ‚ä¢
  exact hf.inf hg

@[fun_prop]
theorem Integrable.sup {Œ≤}
    [NormedAddCommGroup Œ≤] [Lattice Œ≤] [HasSolidNorm Œ≤] [IsOrderedAddMonoid Œ≤]
    {f g : Œ± ‚Üí Œ≤} (hf : Integrable f Œº)
    (hg : Integrable g Œº) : Integrable (f ‚äî g) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf hg ‚ä¢
  exact hf.sup hg

@[fun_prop]
theorem Integrable.abs {Œ≤}
    [NormedAddCommGroup Œ≤] [Lattice Œ≤] [HasSolidNorm Œ≤] [IsOrderedAddMonoid Œ≤]
    {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) :
    Integrable (fun a => |f a|) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf ‚ä¢
  exact hf.abs

-- TODO: generalise this to enorms, once there is an `ENormedDivisionRing` class
theorem Integrable.bdd_mul {F : Type*} [NormedDivisionRing F] {f g : Œ± ‚Üí F} (hint : Integrable g Œº)
    (hm : AEStronglyMeasurable f Œº) (hfbdd : ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) :
    Integrable (fun x => f x * g x) Œº := by
  rcases isEmpty_or_nonempty Œ± with hŒ± | hŒ±
  ¬∑ rw [Œº.eq_zero_of_isEmpty]
    exact integrable_zero_measure
  ¬∑ refine ‚ü®hm.mul hint.1, ?_‚ü©
    obtain ‚ü®C, hC‚ü© := hfbdd
    have hCnonneg : 0 ‚â§ C := le_trans (norm_nonneg _) (hC hŒ±.some)
    have : (fun x => ‚Äñf x * g x‚Äñ‚Çä) ‚â§ fun x => ‚ü®C, hCnonneg‚ü© * ‚Äñg x‚Äñ‚Çä := by
      intro x
      simp only [nnnorm_mul]
      exact mul_le_mul_of_nonneg_right (hC x) (zero_le _)
    refine lt_of_le_of_lt (lintegral_mono_nnreal this) ?_
    simp only [ENNReal.coe_mul]
    rw [lintegral_const_mul' _ _ ENNReal.coe_ne_top]
    exact ENNReal.mul_lt_top ENNReal.coe_lt_top hint.2

-- TODO: generalise the following lemmas to enorm classes

/-- **H√∂lder's inequality for integrable functions**: the scalar multiplication of an integrable
vector-valued function by a scalar function with finite essential supremum is integrable. -/
theorem Integrable.essSup_smul {R : Type*} [NormedRing R] [Module R Œ≤] [IsBoundedSMul R Œ≤]
    {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) {g : Œ± ‚Üí R}
    (g_aestronglyMeasurable : AEStronglyMeasurable g Œº) (ess_sup_g : essSup (‚Äñg ¬∑‚Äñ‚Çë) Œº ‚â† ‚àû) :
    Integrable (fun x : Œ± => g x ‚Ä¢ f x) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at *
  refine ‚ü®g_aestronglyMeasurable.smul hf.1, ?_‚ü©
  have hg' : eLpNorm g ‚àû Œº ‚â† ‚àû := by rwa [eLpNorm_exponent_top]
  calc
    eLpNorm (fun x : Œ± => g x ‚Ä¢ f x) 1 Œº ‚â§ _ := by
      simpa using MeasureTheory.eLpNorm_smul_le_mul_eLpNorm hf.1 g_aestronglyMeasurable
        (p := ‚àû) (q := 1)
    _ < ‚àû := ENNReal.mul_lt_top hg'.lt_top hf.2

/-- H√∂lder's inequality for integrable functions: the scalar multiplication of an integrable
scalar-valued function by a vector-value function with finite essential supremum is integrable. -/
theorem Integrable.smul_essSup {ùïú : Type*} [NormedRing ùïú] [MulActionWithZero ùïú Œ≤]
    [IsBoundedSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú} (hf : Integrable f Œº) {g : Œ± ‚Üí Œ≤}
    (g_aestronglyMeasurable : AEStronglyMeasurable g Œº) (ess_sup_g : essSup (‚Äñg ¬∑‚Äñ‚Çë) Œº ‚â† ‚àû) :
    Integrable (fun x : Œ± => f x ‚Ä¢ g x) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at *
  refine ‚ü®hf.1.smul g_aestronglyMeasurable, ?_‚ü©
  have hg' : eLpNorm g ‚àû Œº ‚â† ‚àû := by rwa [eLpNorm_exponent_top]
  calc
    eLpNorm (fun x : Œ± => f x ‚Ä¢ g x) 1 Œº ‚â§ _ := by
      simpa using MeasureTheory.eLpNorm_smul_le_mul_eLpNorm g_aestronglyMeasurable hf.1
        (p := 1) (q := ‚àû)
    _ < ‚àû := ENNReal.mul_lt_top hf.2 hg'.lt_top

theorem integrable_enorm_iff {f : Œ± ‚Üí Œµ} (hf : AEStronglyMeasurable f Œº) :
    Integrable (‚Äñf ¬∑‚Äñ‚Çë) Œº ‚Üî Integrable f Œº := by
  simp_rw [Integrable, and_iff_right hf, and_iff_right hf.enorm.aestronglyMeasurable,
    hasFiniteIntegral_enorm_iff]

theorem integrable_norm_iff {f : Œ± ‚Üí Œ≤} (hf : AEStronglyMeasurable f Œº) :
    Integrable (fun a => ‚Äñf a‚Äñ) Œº ‚Üî Integrable f Œº := by
  simp_rw [Integrable, and_iff_right hf, and_iff_right hf.norm, hasFiniteIntegral_norm_iff]

-- TODO: generalise this lemma to an `ENormedAddCommSubMonoid`
theorem integrable_of_norm_sub_le {f‚ÇÄ f‚ÇÅ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí ‚Ñù} (hf‚ÇÅ_m : AEStronglyMeasurable f‚ÇÅ Œº)
    (hf‚ÇÄ_i : Integrable f‚ÇÄ Œº) (hg_i : Integrable g Œº) (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf‚ÇÄ a - f‚ÇÅ a‚Äñ ‚â§ g a) :
    Integrable f‚ÇÅ Œº :=
  haveI : ‚àÄ·µê a ‚àÇŒº, ‚Äñf‚ÇÅ a‚Äñ ‚â§ ‚Äñf‚ÇÄ a‚Äñ + g a := by
    apply h.mono
    intro a ha
    calc
      ‚Äñf‚ÇÅ a‚Äñ ‚â§ ‚Äñf‚ÇÄ a‚Äñ + ‚Äñf‚ÇÄ a - f‚ÇÅ a‚Äñ := norm_le_insert _ _
      _ ‚â§ ‚Äñf‚ÇÄ a‚Äñ + g a := add_le_add_left ha _
  Integrable.mono' (hf‚ÇÄ_i.norm.add hg_i) hf‚ÇÅ_m this

lemma integrable_of_le_of_le {f g‚ÇÅ g‚ÇÇ : Œ± ‚Üí ‚Ñù} (hf : AEStronglyMeasurable f Œº)
    (h_le‚ÇÅ : g‚ÇÅ ‚â§·µê[Œº] f) (h_le‚ÇÇ : f ‚â§·µê[Œº] g‚ÇÇ)
    (h_int‚ÇÅ : Integrable g‚ÇÅ Œº) (h_int‚ÇÇ : Integrable g‚ÇÇ Œº) :
    Integrable f Œº := by
  have : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ max ‚Äñg‚ÇÅ x‚Äñ ‚Äñg‚ÇÇ x‚Äñ := by
    filter_upwards [h_le‚ÇÅ, h_le‚ÇÇ] with x hx1 hx2
    simp only [Real.norm_eq_abs]
    exact abs_le_max_abs_abs hx1 hx2
  have h_le_add : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ ‚Äñ‚Äñg‚ÇÅ x‚Äñ + ‚Äñg‚ÇÇ x‚Äñ‚Äñ := by
    filter_upwards [this] with x hx
    refine hx.trans ?_
    conv_rhs => rw [Real.norm_of_nonneg (by positivity)]
    exact max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)
  exact Integrable.mono (by fun_prop) hf h_le_add

-- TODO: generalising this to enorms requires defining a product instance for enormed monoids first
@[fun_prop]
theorem Integrable.prodMk {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    Integrable (fun x => (f x, g x)) Œº :=
  ‚ü®by fun_prop,
    (hf.norm.add' hg.norm).mono <|
      Eventually.of_forall fun x =>
        calc
          max ‚Äñf x‚Äñ ‚Äñg x‚Äñ ‚â§ ‚Äñf x‚Äñ + ‚Äñg x‚Äñ := max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)
          _ ‚â§ ‚Äñ‚Äñf x‚Äñ + ‚Äñg x‚Äñ‚Äñ := le_abs_self _‚ü©

@[deprecated (since := "2025-03-05")]
alias Integrable.prod_mk := Integrable.prodMk

theorem MemLp.integrable {q : ‚Ñù‚â•0‚àû} (hq1 : 1 ‚â§ q) {f : Œ± ‚Üí Œµ} [IsFiniteMeasure Œº]
    (hfq : MemLp f q Œº) : Integrable f Œº :=
  memLp_one_iff_integrable.mp (hfq.mono_exponent hq1)

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `‚Äñf x‚Äñ‚Çë ‚â• Œµ` is finite for all positive `Œµ`. -/
theorem Integrable.measure_enorm_ge_lt_top {E : Type*} [TopologicalSpace E] [ContinuousENorm E]
    {f : Œ± ‚Üí E} (hf : Integrable f Œº) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : 0 < Œµ) (hŒµ' : Œµ ‚â† ‚àû) :
    Œº { x | Œµ ‚â§ ‚Äñf x‚Äñ‚Çë } < ‚àû := by
  refine meas_ge_le_mul_pow_eLpNorm_enorm Œº one_ne_zero one_ne_top hf.1 hŒµ.ne' (by simp [hŒµ'])
    |>.trans_lt ?_
  apply ENNReal.mul_lt_top
  ¬∑ simpa only [ENNReal.toReal_one, ENNReal.rpow_one, ENNReal.inv_lt_top, ENNReal.ofReal_pos]
      using hŒµ
  ¬∑ simpa only [ENNReal.toReal_one, ENNReal.rpow_one] using
      (memLp_one_iff_integrable.2 hf).eLpNorm_lt_top

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `‚Äñf x‚Äñ ‚â• Œµ` is finite for all positive `Œµ`. -/
theorem Integrable.measure_norm_ge_lt_top {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    Œº { x | Œµ ‚â§ ‚Äñf x‚Äñ } < ‚àû := by
  convert Integrable.measure_enorm_ge_lt_top hf (ofReal_pos.mpr hŒµ) ofReal_ne_top with x
  rw [‚Üê Real.enorm_of_nonneg hŒµ.le, enorm_le_iff_norm_le, Real.norm_of_nonneg hŒµ.le]

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `‚Äñf x‚Äñ‚Çë > Œµ` is finite for all positive `Œµ`. -/
lemma Integrable.measure_norm_gt_lt_top_enorm {E : Type*} [TopologicalSpace E] [ContinuousENorm E]
    {f : Œ± ‚Üí E} (hf : Integrable f Œº) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : 0 < Œµ) : Œº {x | Œµ < ‚Äñf x‚Äñ‚Çë} < ‚àû := by
  by_cases hŒµ' : Œµ = ‚àû
  ¬∑ simp [hŒµ']
  exact lt_of_le_of_lt (measure_mono (fun _ h ‚Ü¶ (Set.mem_setOf_eq ‚ñ∏ h).le))
    (hf.measure_enorm_ge_lt_top hŒµ hŒµ')

/-- A non-quantitative version of Markov inequality for integrable functions: the measure of points
where `‚Äñf x‚Äñ > Œµ` is finite for all positive `Œµ`. -/
lemma Integrable.measure_norm_gt_lt_top {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    Œº {x | Œµ < ‚Äñf x‚Äñ} < ‚àû :=
  lt_of_le_of_lt (measure_mono (fun _ h ‚Ü¶ (Set.mem_setOf_eq ‚ñ∏ h).le)) (hf.measure_norm_ge_lt_top hŒµ)

-- TODO: try generalising all lemmas below to enorm classes

/-- If `f` is `‚Ñù`-valued and integrable, then for any `c > 0` the set `{x | f x ‚â• c}` has finite
measure. -/
lemma Integrable.measure_ge_lt_top {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) {Œµ : ‚Ñù} (Œµ_pos : 0 < Œµ) :
    Œº {a : Œ± | Œµ ‚â§ f a} < ‚àû := by
  refine lt_of_le_of_lt (measure_mono ?_) (hf.measure_norm_ge_lt_top Œµ_pos)
  intro x hx
  simp only [Real.norm_eq_abs, Set.mem_setOf_eq] at hx ‚ä¢
  exact hx.trans (le_abs_self _)

/-- If `f` is `‚Ñù`-valued and integrable, then for any `c < 0` the set `{x | f x ‚â§ c}` has finite
measure. -/
lemma Integrable.measure_le_lt_top {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) {c : ‚Ñù} (c_neg : c < 0) :
    Œº {a : Œ± | f a ‚â§ c} < ‚àû := by
  refine lt_of_le_of_lt (measure_mono ?_) (hf.measure_norm_ge_lt_top (show 0 < -c by linarith))
  intro x hx
  simp only [Real.norm_eq_abs, Set.mem_setOf_eq] at hx ‚ä¢
  exact (show -c ‚â§ - f x by linarith).trans (neg_le_abs _)

/-- If `f` is `‚Ñù`-valued and integrable, then for any `c > 0` the set `{x | f x > c}` has finite
measure. -/
lemma Integrable.measure_gt_lt_top {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) {Œµ : ‚Ñù} (Œµ_pos : 0 < Œµ) :
    Œº {a : Œ± | Œµ < f a} < ‚àû :=
  lt_of_le_of_lt (measure_mono (fun _ hx ‚Ü¶ (Set.mem_setOf_eq ‚ñ∏ hx).le))
    (Integrable.measure_ge_lt_top hf Œµ_pos)

/-- If `f` is `‚Ñù`-valued and integrable, then for any `c < 0` the set `{x | f x < c}` has finite
measure. -/
lemma Integrable.measure_lt_lt_top {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) {c : ‚Ñù} (c_neg : c < 0) :
    Œº {a : Œ± | f a < c} < ‚àû :=
  lt_of_le_of_lt (measure_mono (fun _ hx ‚Ü¶ (Set.mem_setOf_eq ‚ñ∏ hx).le))
    (Integrable.measure_le_lt_top hf c_neg)

theorem LipschitzWith.integrable_comp_iff_of_antilipschitz {K K'} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥}
    (hg : LipschitzWith K g) (hg' : AntilipschitzWith K' g) (g0 : g 0 = 0) :
    Integrable (g ‚àò f) Œº ‚Üî Integrable f Œº := by
  simp [‚Üê memLp_one_iff_integrable, hg.memLp_comp_iff_of_antilipschitz hg' g0]

@[fun_prop]
theorem Integrable.real_toNNReal {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) :
    Integrable (fun x => ((f x).toNNReal : ‚Ñù)) Œº := by
  refine ‚ü®by fun_prop, ?_‚ü©
  rw [hasFiniteIntegral_iff_norm]
  refine lt_of_le_of_lt ?_ ((hasFiniteIntegral_iff_norm _).1 hf.hasFiniteIntegral)
  apply lintegral_mono
  intro x
  simp [abs_le, le_abs_self]

theorem ofReal_toReal_ae_eq {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : ‚àÄ·µê x ‚àÇŒº, f x < ‚àû) :
    (fun x => ENNReal.ofReal (f x).toReal) =·µê[Œº] f := by
  filter_upwards [hf]
  intro x hx
  simp only [hx.ne, ofReal_toReal, Ne, not_false_iff]

theorem coe_toNNReal_ae_eq {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : ‚àÄ·µê x ‚àÇŒº, f x < ‚àû) :
    (fun x => ((f x).toNNReal : ‚Ñù‚â•0‚àû)) =·µê[Œº] f := by
  filter_upwards [hf]
  intro x hx
  simp only [hx.ne, Ne, not_false_iff, coe_toNNReal]

section count

variable [MeasurableSingletonClass Œ±] {f : Œ± ‚Üí Œ≤}

/-- A function is integrable for the counting measure iff its norm is summable. -/
lemma integrable_count_iff :
    Integrable f Measure.count ‚Üî Summable (‚Äñf ¬∑‚Äñ) := by
  -- Note: this proof would be much easier if we assumed `SecondCountableTopology G`. Without
  -- this we have to justify the claim that `f` lands a.e. in a separable subset, which is true
  -- (because summable functions have countable range) but slightly tedious to check.
  rw [Integrable, hasFiniteIntegral_count_iff, and_iff_right_iff_imp]
  intro hs
  have hs' : (Function.support f).Countable := by
    simpa only [Ne, Pi.zero_apply, eq_comm, Function.support, norm_eq_zero]
      using hs.countable_support
  letI : MeasurableSpace Œ≤ := borel Œ≤
  haveI : BorelSpace Œ≤ := ‚ü®rfl‚ü©
  refine aestronglyMeasurable_iff_aemeasurable_separable.mpr ‚ü®?_, ?_‚ü©
  ¬∑ refine (measurable_zero.measurable_of_countable_ne ?_).aemeasurable
    simpa only [Ne, Pi.zero_apply, eq_comm, Function.support] using hs'
  ¬∑ refine ‚ü®f '' univ, ?_, ae_of_all _ fun a ‚Ü¶ ‚ü®a, ‚ü®mem_univ _, rfl‚ü©‚ü©‚ü©
    suffices f '' univ ‚äÜ (f '' f.support) ‚à™ {0} from
      (((hs'.image f).union (countable_singleton 0)).mono this).isSeparable
    intro g hg
    rcases eq_or_ne g 0 with rfl | hg'
    ¬∑ exact Or.inr (mem_singleton _)
    ¬∑ obtain ‚ü®x, -, rfl‚ü© := (mem_image ..).mp hg
      exact Or.inl ‚ü®x, hg', rfl‚ü©

end count

section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

theorem integrable_withDensity_iff_integrable_coe_smul {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : Measurable f)
    {g : Œ± ‚Üí E} :
    Integrable g (Œº.withDensity fun x => f x) ‚Üî Integrable (fun x => (f x : ‚Ñù) ‚Ä¢ g x) Œº := by
  by_cases H : AEStronglyMeasurable (fun x : Œ± => (f x : ‚Ñù) ‚Ä¢ g x) Œº
  ¬∑ simp only [Integrable, aestronglyMeasurable_withDensity_iff hf, hasFiniteIntegral_iff_enorm, H,
      true_and]
    rw [lintegral_withDensity_eq_lintegral_mul‚ÇÄ' hf.coe_nnreal_ennreal.aemeasurable]
    ¬∑ simp [enorm_smul]
    ¬∑ simpa [aemeasurable_withDensity_ennreal_iff hf, enorm_smul] using H.enorm
  ¬∑ simp only [Integrable, aestronglyMeasurable_withDensity_iff hf, H, false_and]

theorem integrable_withDensity_iff_integrable_smul {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : Measurable f) {g : Œ± ‚Üí E} :
    Integrable g (Œº.withDensity fun x => f x) ‚Üî Integrable (fun x => f x ‚Ä¢ g x) Œº :=
  integrable_withDensity_iff_integrable_coe_smul hf

theorem integrable_withDensity_iff_integrable_smul' {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : Measurable f)
    (hflt : ‚àÄ·µê x ‚àÇŒº, f x < ‚àû) {g : Œ± ‚Üí E} :
    Integrable g (Œº.withDensity f) ‚Üî Integrable (fun x => (f x).toReal ‚Ä¢ g x) Œº := by
  rw [‚Üê withDensity_congr_ae (coe_toNNReal_ae_eq hflt),
    integrable_withDensity_iff_integrable_smul]
  ¬∑ simp_rw [NNReal.smul_def, ENNReal.toReal]
  ¬∑ exact hf.ennreal_toNNReal

theorem integrable_withDensity_iff_integrable_coe_smul‚ÇÄ {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : AEMeasurable f Œº)
    {g : Œ± ‚Üí E} :
    Integrable g (Œº.withDensity fun x => f x) ‚Üî Integrable (fun x => (f x : ‚Ñù) ‚Ä¢ g x) Œº :=
  calc
    Integrable g (Œº.withDensity fun x => f x) ‚Üî
        Integrable g (Œº.withDensity fun x => (hf.mk f x : ‚Ñù‚â•0)) := by
      suffices (fun x => (f x : ‚Ñù‚â•0‚àû)) =·µê[Œº] (fun x => (hf.mk f x : ‚Ñù‚â•0)) by
        rw [withDensity_congr_ae this]
      filter_upwards [hf.ae_eq_mk] with x hx
      simp [hx]
    _ ‚Üî Integrable (fun x => ((hf.mk f x : ‚Ñù‚â•0) : ‚Ñù) ‚Ä¢ g x) Œº :=
      integrable_withDensity_iff_integrable_coe_smul hf.measurable_mk
    _ ‚Üî Integrable (fun x => (f x : ‚Ñù) ‚Ä¢ g x) Œº := by
      apply integrable_congr
      filter_upwards [hf.ae_eq_mk] with x hx
      simp [hx]

theorem integrable_withDensity_iff_integrable_smul‚ÇÄ' {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº)
    (hflt : ‚àÄ·µê x ‚àÇŒº, f x < ‚àû) {g : Œ± ‚Üí E} :
    Integrable g (Œº.withDensity f) ‚Üî Integrable (fun x => (f x).toReal ‚Ä¢ g x) Œº := by
  rw [‚Üê withDensity_congr_ae (coe_toNNReal_ae_eq hflt),
    integrable_withDensity_iff_integrable_coe_smul‚ÇÄ]
  ¬∑ congr!
  ¬∑ exact hf.ennreal_toNNReal

theorem integrable_withDensity_iff_integrable_smul‚ÇÄ {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : AEMeasurable f Œº)
    {g : Œ± ‚Üí E} : Integrable g (Œº.withDensity fun x => f x) ‚Üî Integrable (fun x => f x ‚Ä¢ g x) Œº :=
  integrable_withDensity_iff_integrable_coe_smul‚ÇÄ hf

end

theorem integrable_withDensity_iff {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : Measurable f) (hflt : ‚àÄ·µê x ‚àÇŒº, f x < ‚àû)
    {g : Œ± ‚Üí ‚Ñù} : Integrable g (Œº.withDensity f) ‚Üî Integrable (fun x => g x * (f x).toReal) Œº := by
  have : (fun x => g x * (f x).toReal) = fun x => (f x).toReal ‚Ä¢ g x := by simp [mul_comm]
  rw [this]
  exact integrable_withDensity_iff_integrable_smul' hf hflt

section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

theorem memL1_smul_of_L1_withDensity {f : Œ± ‚Üí ‚Ñù‚â•0} (f_meas : Measurable f)
    (u : Lp E 1 (Œº.withDensity fun x => f x)) : MemLp (fun x => f x ‚Ä¢ u x) 1 Œº :=
  memLp_one_iff_integrable.2 <|
    (integrable_withDensity_iff_integrable_smul f_meas).1 <| memLp_one_iff_integrable.1 (Lp.memLp u)

@[deprecated (since := "2025-02-21")]
alias memL1_smul_of_‚Ñí1_withDensity := memL1_smul_of_L1_withDensity

variable (Œº)

/-- The map `u ‚Ü¶ f ‚Ä¢ u` is an isometry between the `L^1` spaces for `Œº.withDensity f` and `Œº`. -/
noncomputable def withDensitySMulLI {f : Œ± ‚Üí ‚Ñù‚â•0} (f_meas : Measurable f) :
    Lp E 1 (Œº.withDensity fun x => f x) ‚Üí‚Çó·µ¢[‚Ñù] Lp E 1 Œº where
  toFun u := (memL1_smul_of_L1_withDensity f_meas u).toLp _
  map_add' := by
    intro u v
    ext1
    filter_upwards [(memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp,
      (memL1_smul_of_L1_withDensity f_meas v).coeFn_toLp,
      (memL1_smul_of_L1_withDensity f_meas (u + v)).coeFn_toLp,
      Lp.coeFn_add ((memL1_smul_of_L1_withDensity f_meas u).toLp _)
        ((memL1_smul_of_L1_withDensity f_meas v).toLp _),
      (ae_withDensity_iff f_meas.coe_nnreal_ennreal).1 (Lp.coeFn_add u v)]
    intro x hu hv huv h' h''
    rw [huv, h', Pi.add_apply, hu, hv]
    rcases eq_or_ne (f x) 0 with (hx | hx)
    ¬∑ simp only [hx, zero_smul, add_zero]
    ¬∑ rw [h'' _, Pi.add_apply, smul_add]
      simpa only [Ne, ENNReal.coe_eq_zero] using hx
  map_smul' := by
    intro r u
    ext1
    filter_upwards [(ae_withDensity_iff f_meas.coe_nnreal_ennreal).1 (Lp.coeFn_smul r u),
      (memL1_smul_of_L1_withDensity f_meas (r ‚Ä¢ u)).coeFn_toLp,
      Lp.coeFn_smul r ((memL1_smul_of_L1_withDensity f_meas u).toLp _),
      (memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp]
    intro x h h' h'' h'''
    rw [RingHom.id_apply, h', h'', Pi.smul_apply, h''']
    rcases eq_or_ne (f x) 0 with (hx | hx)
    ¬∑ simp only [hx, zero_smul, smul_zero]
    ¬∑ rw [h _, smul_comm, Pi.smul_apply]
      simpa only [Ne, ENNReal.coe_eq_zero] using hx
  norm_map' := by
    intro u
    simp only [eLpNorm, LinearMap.coe_mk, AddHom.coe_mk,
      one_ne_zero, ENNReal.one_ne_top, ENNReal.toReal_one, if_false, eLpNorm', ENNReal.rpow_one,
      _root_.div_one, Lp.norm_def]
    rw [lintegral_withDensity_eq_lintegral_mul_non_measurable _ f_meas.coe_nnreal_ennreal
        (Filter.Eventually.of_forall fun x => ENNReal.coe_lt_top)]
    congr 1
    apply lintegral_congr_ae
    filter_upwards [(memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp] with x hx
    rw [hx]
    simp [NNReal.smul_def, enorm_smul]

@[simp]
theorem withDensitySMulLI_apply {f : Œ± ‚Üí ‚Ñù‚â•0} (f_meas : Measurable f)
    (u : Lp E 1 (Œº.withDensity fun x => f x)) :
    withDensitySMulLI Œº (E := E) f_meas u =
      (memL1_smul_of_L1_withDensity f_meas u).toLp fun x => f x ‚Ä¢ u x :=
  rfl

end

section ENNReal

theorem mem_L1_toReal_of_lintegral_ne_top {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hfm : AEMeasurable f Œº)
    (hfi : ‚à´‚Åª x, f x ‚àÇŒº ‚â† ‚àû) : MemLp (fun x ‚Ü¶ (f x).toReal) 1 Œº := by
  rw [MemLp, eLpNorm_one_eq_lintegral_enorm]
  exact ‚ü®(AEMeasurable.ennreal_toReal hfm).aestronglyMeasurable,
    hasFiniteIntegral_toReal_of_lintegral_ne_top hfi‚ü©

@[deprecated (since := "2025-02-21")]
alias mem_‚Ñí1_toReal_of_lintegral_ne_top := mem_L1_toReal_of_lintegral_ne_top

theorem integrable_toReal_of_lintegral_ne_top {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hfm : AEMeasurable f Œº)
    (hfi : ‚à´‚Åª x, f x ‚àÇŒº ‚â† ‚àû) : Integrable (fun x ‚Ü¶ (f x).toReal) Œº :=
  memLp_one_iff_integrable.1 <| mem_L1_toReal_of_lintegral_ne_top hfm hfi

lemma integrable_toReal_iff {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº) (hf_ne_top : ‚àÄ·µê x ‚àÇŒº, f x ‚â† ‚àû) :
    Integrable (fun x ‚Ü¶ (f x).toReal) Œº ‚Üî ‚à´‚Åª x, f x ‚àÇŒº ‚â† ‚àû := by
  rw [Integrable, hasFiniteIntegral_toReal_iff hf_ne_top]
  simp only [hf.ennreal_toReal.aestronglyMeasurable, ne_eq, true_and]

lemma lintegral_ofReal_ne_top_iff_integrable {f : Œ± ‚Üí ‚Ñù}
    (hfm : AEStronglyMeasurable f Œº) (hf : 0 ‚â§·µê[Œº] f) :
    ‚à´‚Åª a, ENNReal.ofReal (f a) ‚àÇŒº ‚â† ‚àû ‚Üî Integrable f Œº := by
  rw [Integrable, hasFiniteIntegral_iff_ofReal hf]
  simp [hfm]

end ENNReal

section PosPart

/-! ### Lemmas used for defining the positive part of an `L¬π` function -/


@[fun_prop]
theorem Integrable.pos_part {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) :
    Integrable (fun a => max (f a) 0) Œº :=
  ‚ü®(hf.aestronglyMeasurable.aemeasurable.max aemeasurable_const).aestronglyMeasurable,
    hf.hasFiniteIntegral.max_zero‚ü©

@[fun_prop]
theorem Integrable.neg_part {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) :
    Integrable (fun a => max (-f a) 0) Œº :=
  hf.neg.pos_part

end PosPart

section IsBoundedSMul

variable {ùïú : Type*}
  {Œµ : Type*} [TopologicalSpace Œµ] [ENormedAddMonoid Œµ]

@[fun_prop]
theorem Integrable.smul [NormedAddCommGroup ùïú] [SMulZeroClass ùïú Œ≤] [IsBoundedSMul ùïú Œ≤] (c : ùïú)
    {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) : Integrable (c ‚Ä¢ f) Œº :=
  ‚ü®by fun_prop, hf.hasFiniteIntegral.smul c‚ü©

@[fun_prop]
theorem Integrable.fun_smul [NormedAddCommGroup ùïú] [SMulZeroClass ùïú Œ≤] [IsBoundedSMul ùïú Œ≤] (c : ùïú)
    {f : Œ± ‚Üí Œ≤} (hf : Integrable f Œº) : Integrable (fun x ‚Ü¶ c ‚Ä¢ f x) Œº :=
  hf.smul c

theorem Integrable.smul_enorm
    [NormedAddCommGroup ùïú] [SMul ùïú Œµ] [ContinuousConstSMul ùïú Œµ] [ENormSMulClass ùïú Œµ] (c : ùïú)
    {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) : Integrable (c ‚Ä¢ f) Œº :=
  ‚ü®by fun_prop, by fun_prop‚ü©

theorem Integrable.fun_smul_enorm
    [NormedAddCommGroup ùïú] [SMul ùïú Œµ] [ContinuousConstSMul ùïú Œµ] [ENormSMulClass ùïú Œµ] (c : ùïú)
    {f : Œ± ‚Üí Œµ} (hf : Integrable f Œº) : Integrable (fun x ‚Ü¶ c ‚Ä¢ f x) Œº :=
  hf.smul_enorm c

theorem _root_.IsUnit.integrable_smul_iff [NormedRing ùïú] [MulActionWithZero ùïú Œ≤]
    [IsBoundedSMul ùïú Œ≤] {c : ùïú} (hc : IsUnit c) (f : Œ± ‚Üí Œ≤) :
    Integrable (c ‚Ä¢ f) Œº ‚Üî Integrable f Œº :=
  and_congr hc.aestronglyMeasurable_const_smul_iff (hasFiniteIntegral_smul_iff hc f)

theorem integrable_smul_iff [NormedDivisionRing ùïú] [MulActionWithZero ùïú Œ≤]
    [IsBoundedSMul ùïú Œ≤] {c : ùïú} (hc : c ‚â† 0) (f : Œ± ‚Üí Œ≤) :
    Integrable (c ‚Ä¢ f) Œº ‚Üî Integrable f Œº :=
  (IsUnit.mk0 _ hc).integrable_smul_iff f

theorem integrable_fun_smul_iff [NormedDivisionRing ùïú] [MulActionWithZero ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]
    {c : ùïú} (hc : c ‚â† 0) (f : Œ± ‚Üí Œ≤) :
    Integrable (fun x ‚Ü¶ c ‚Ä¢ f x) Œº ‚Üî Integrable f Œº :=
  integrable_smul_iff hc f

variable [NormedRing ùïú] [Module ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]

theorem Integrable.smul_of_top_right {f : Œ± ‚Üí Œ≤} {œÜ : Œ± ‚Üí ùïú} (hf : Integrable f Œº)
    (hœÜ : MemLp œÜ ‚àû Œº) : Integrable (œÜ ‚Ä¢ f) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf ‚ä¢
  exact MemLp.smul hf hœÜ

theorem Integrable.smul_of_top_left {f : Œ± ‚Üí Œ≤} {œÜ : Œ± ‚Üí ùïú} (hœÜ : Integrable œÜ Œº)
    (hf : MemLp f ‚àû Œº) : Integrable (œÜ ‚Ä¢ f) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hœÜ ‚ä¢
  exact MemLp.smul hf hœÜ

@[fun_prop]
theorem Integrable.smul_const {f : Œ± ‚Üí ùïú} (hf : Integrable f Œº) (c : Œ≤) :
    Integrable (fun x => f x ‚Ä¢ c) Œº :=
  hf.smul_of_top_left (memLp_top_const c)

end IsBoundedSMul

section NormedSpaceOverCompleteField

variable {ùïú : Type*} [NontriviallyNormedField ùïú] [CompleteSpace ùïú]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]

theorem integrable_smul_const {f : Œ± ‚Üí ùïú} {c : E} (hc : c ‚â† 0) :
    Integrable (fun x => f x ‚Ä¢ c) Œº ‚Üî Integrable f Œº := by
  simp_rw [Integrable, aestronglyMeasurable_smul_const_iff (f := f) hc, and_congr_right_iff,
    hasFiniteIntegral_iff_enorm, enorm_smul]
  intro _; rw [lintegral_mul_const' _ _ enorm_ne_top, ENNReal.mul_lt_top_iff]
  have : ‚àÄ x : ‚Ñù‚â•0‚àû, x = 0 ‚Üí x < ‚àû := by simp
  simp [hc, or_iff_left_of_imp (this _)]

end NormedSpaceOverCompleteField

section NormedRing

variable {ùïú : Type*} [NormedRing ùïú] {f : Œ± ‚Üí ùïú}

@[fun_prop]
theorem Integrable.const_mul {f : Œ± ‚Üí ùïú} (h : Integrable f Œº) (c : ùïú) :
    Integrable (fun x => c * f x) Œº :=
  h.smul c

theorem Integrable.const_mul' {f : Œ± ‚Üí ùïú} (h : Integrable f Œº) (c : ùïú) :
    Integrable ((fun _ : Œ± => c) * f) Œº :=
  Integrable.const_mul h c

@[fun_prop]
theorem Integrable.mul_const {f : Œ± ‚Üí ùïú} (h : Integrable f Œº) (c : ùïú) :
    Integrable (fun x => f x * c) Œº :=
  h.smul (MulOpposite.op c)

theorem Integrable.mul_const' {f : Œ± ‚Üí ùïú} (h : Integrable f Œº) (c : ùïú) :
    Integrable (f * fun _ : Œ± => c) Œº :=
  Integrable.mul_const h c

theorem integrable_const_mul_iff {c : ùïú} (hc : IsUnit c) (f : Œ± ‚Üí ùïú) :
    Integrable (fun x => c * f x) Œº ‚Üî Integrable f Œº :=
  hc.integrable_smul_iff f

theorem integrable_mul_const_iff {c : ùïú} (hc : IsUnit c) (f : Œ± ‚Üí ùïú) :
    Integrable (fun x => f x * c) Œº ‚Üî Integrable f Œº :=
  hc.op.integrable_smul_iff f

theorem Integrable.bdd_mul' {f g : Œ± ‚Üí ùïú} {c : ‚Ñù} (hg : Integrable g Œº)
    (hf : AEStronglyMeasurable f Œº) (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    Integrable (fun x => f x * g x) Œº := by
  refine Integrable.mono' (hg.norm.smul c) (hf.mul hg.1) ?_
  filter_upwards [hf_bound] with x hx
  rw [Pi.smul_apply, smul_eq_mul]
  exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right hx (norm_nonneg _))

theorem Integrable.mul_of_top_right {f : Œ± ‚Üí ùïú} {œÜ : Œ± ‚Üí ùïú} (hf : Integrable f Œº)
    (hœÜ : MemLp œÜ ‚àû Œº) : Integrable (œÜ * f) Œº :=
  hf.smul_of_top_right hœÜ

theorem Integrable.mul_of_top_left {f : Œ± ‚Üí ùïú} {œÜ : Œ± ‚Üí ùïú} (hœÜ : Integrable œÜ Œº)
    (hf : MemLp f ‚àû Œº) : Integrable (œÜ * f) Œº :=
  hœÜ.smul_of_top_left hf

lemma MemLp.integrable_mul {p q : ‚Ñù‚â•0‚àû} {f g : Œ± ‚Üí ùïú} (hf : MemLp f p Œº) (hg : MemLp g q Œº)
    [HolderTriple p q 1] :
    Integrable (f * g) Œº :=
  memLp_one_iff_integrable.1 <| hg.mul hf

end NormedRing

section NormedDivisionRing

variable {ùïú : Type*} [NormedDivisionRing ùïú] {f : Œ± ‚Üí ùïú}

@[fun_prop]
theorem Integrable.div_const {f : Œ± ‚Üí ùïú} (h : Integrable f Œº) (c : ùïú) :
    Integrable (fun x => f x / c) Œº := by simp_rw [div_eq_mul_inv, h.mul_const]

end NormedDivisionRing

section RCLike

variable {ùïú : Type*} [RCLike ùïú] {f : Œ± ‚Üí ùïú}

@[fun_prop]
theorem Integrable.ofReal {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) :
    Integrable (fun x => (f x : ùïú)) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf ‚ä¢
  exact hf.ofReal

theorem Integrable.re_im_iff :
    Integrable (fun x => RCLike.re (f x)) Œº ‚àß Integrable (fun x => RCLike.im (f x)) Œº ‚Üî
      Integrable f Œº := by
  simp_rw [‚Üê memLp_one_iff_integrable]
  exact memLp_re_im_iff

@[fun_prop]
theorem Integrable.re (hf : Integrable f Œº) : Integrable (fun x => RCLike.re (f x)) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf ‚ä¢
  exact hf.re

@[fun_prop]
theorem Integrable.im (hf : Integrable f Œº) : Integrable (fun x => RCLike.im (f x)) Œº := by
  rw [‚Üê memLp_one_iff_integrable] at hf ‚ä¢
  exact hf.im

end RCLike

section Trim

variable {H : Type*} [NormedAddCommGroup H] {m0 : MeasurableSpace Œ±} {Œº' : Measure Œ±} {f : Œ± ‚Üí H}

theorem Integrable.trim (hm : m ‚â§ m0) (hf_int : Integrable f Œº') (hf : StronglyMeasurable[m] f) :
    Integrable f (Œº'.trim hm) := by
  refine ‚ü®hf.aestronglyMeasurable, ?_‚ü©
  rw [HasFiniteIntegral, lintegral_trim hm _]
  ¬∑ exact hf_int.2
  ¬∑ fun_prop

theorem integrable_of_integrable_trim (hm : m ‚â§ m0) (hf_int : Integrable f (Œº'.trim hm)) :
    Integrable f Œº' := by
  obtain ‚ü®hf_meas_ae, hf‚ü© := hf_int
  refine ‚ü®aestronglyMeasurable_of_aestronglyMeasurable_trim hm hf_meas_ae, ?_‚ü©
  simpa [HasFiniteIntegral, lintegral_trim_ae hm hf_meas_ae.enorm] using hf

end Trim

section SigmaFinite

variable {E : Type*} {m0 : MeasurableSpace Œ±} [NormedAddCommGroup E]
  {Œµ : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ]

theorem integrable_of_forall_fin_meas_le' {Œº : Measure Œ±} (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    (C : ‚Ñù‚â•0‚àû) (hC : C < ‚àû) {f : Œ± ‚Üí Œµ} (hf_meas : AEStronglyMeasurable f Œº)
    (hf : ‚àÄ s, MeasurableSet[m] s ‚Üí Œº s ‚â† ‚àû ‚Üí ‚à´‚Åª x in s, ‚Äñf x‚Äñ‚Çë ‚àÇŒº ‚â§ C) : Integrable f Œº :=
  ‚ü®hf_meas, (lintegral_le_of_forall_fin_meas_trim_le hm C hf).trans_lt hC‚ü©

theorem integrable_of_forall_fin_meas_le [SigmaFinite Œº] (C : ‚Ñù‚â•0‚àû) (hC : C < ‚àû) {f : Œ± ‚Üí Œµ}
    (hf_meas : AEStronglyMeasurable[m] f Œº)
    (hf : ‚àÄ s : Set Œ±, MeasurableSet[m] s ‚Üí Œº s ‚â† ‚àû ‚Üí ‚à´‚Åª x in s, ‚Äñf x‚Äñ‚Çë ‚àÇŒº ‚â§ C) :
    Integrable f Œº :=
  have : SigmaFinite (Œº.trim le_rfl) := by rwa [@trim_eq_self _ m]
  integrable_of_forall_fin_meas_le' le_rfl C hC hf_meas hf

end SigmaFinite

section restrict

variable {Œµ : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ] {f : Œ± ‚Üí Œµ}

/-- One should usually use `MeasureTheory.Integrable.integrableOn` instead. -/
lemma Integrable.restrict (hf : Integrable f Œº) {s : Set Œ±} : Integrable f (Œº.restrict s) :=
  hf.mono_measure Measure.restrict_le_self

end restrict

end MeasureTheory

section ContinuousLinearMap

open MeasureTheory

variable {E : Type*} [NormedAddCommGroup E] {ùïú : Type*} [NontriviallyNormedField ùïú]
  [NormedSpace ùïú E] {H : Type*} [NormedAddCommGroup H] [NormedSpace ùïú H]

@[fun_prop]
theorem ContinuousLinearMap.integrable_comp {œÜ : Œ± ‚Üí H} (L : H ‚ÜíL[ùïú] E) (œÜ_int : Integrable œÜ Œº) :
    Integrable (fun a : Œ± => L (œÜ a)) Œº :=
  ((Integrable.norm œÜ_int).const_mul ‚ÄñL‚Äñ).mono'
    (by fun_prop)
    (Eventually.of_forall fun a => L.le_opNorm (œÜ a))

@[simp]
theorem ContinuousLinearEquiv.integrable_comp_iff {œÜ : Œ± ‚Üí H} (L : H ‚âÉL[ùïú] E) :
    Integrable (fun a : Œ± ‚Ü¶ L (œÜ a)) Œº ‚Üî Integrable œÜ Œº :=
  ‚ü®fun h ‚Ü¶ by simpa using ContinuousLinearMap.integrable_comp (L.symm : E ‚ÜíL[ùïú] H) h,
  fun h ‚Ü¶ ContinuousLinearMap.integrable_comp (L : H ‚ÜíL[ùïú] E) h‚ü©

@[simp]
theorem LinearIsometryEquiv.integrable_comp_iff {œÜ : Œ± ‚Üí H} (L : H ‚âÉ‚Çó·µ¢[ùïú] E) :
    Integrable (fun a : Œ± ‚Ü¶ L (œÜ a)) Œº ‚Üî Integrable œÜ Œº :=
  ContinuousLinearEquiv.integrable_comp_iff (L : H ‚âÉL[ùïú] E)

theorem MeasureTheory.Integrable.apply_continuousLinearMap {œÜ : Œ± ‚Üí H ‚ÜíL[ùïú] E}
    (œÜ_int : Integrable œÜ Œº) (v : H) : Integrable (fun a => œÜ a v) Œº :=
  (ContinuousLinearMap.apply ùïú _ v).integrable_comp œÜ_int

end ContinuousLinearMap

namespace MeasureTheory

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]

@[fun_prop]
lemma Integrable.fst {f : Œ± ‚Üí E √ó F} (hf : Integrable f Œº) : Integrable (fun x ‚Ü¶ (f x).1) Œº :=
  (ContinuousLinearMap.fst ‚Ñù E F).integrable_comp hf

@[fun_prop]
lemma Integrable.snd {f : Œ± ‚Üí E √ó F} (hf : Integrable f Œº) : Integrable (fun x ‚Ü¶ (f x).2) Œº :=
  (ContinuousLinearMap.snd ‚Ñù E F).integrable_comp hf

lemma integrable_prod {f : Œ± ‚Üí E √ó F} :
    Integrable f Œº ‚Üî Integrable (fun x ‚Ü¶ (f x).1) Œº ‚àß Integrable (fun x ‚Ü¶ (f x).2) Œº :=
  ‚ü®fun h ‚Ü¶ ‚ü®h.fst, h.snd‚ü©, fun h ‚Ü¶ h.1.prodMk h.2‚ü©

end MeasureTheory
