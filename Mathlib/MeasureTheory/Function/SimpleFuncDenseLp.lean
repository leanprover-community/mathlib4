/-
Copyright (c) 2022 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.MeasureTheory.Function.SimpleFuncDense

#align_import measure_theory.function.simple_func_dense_lp from "leanprover-community/mathlib"@"5a2df4cd59cb31e97a516d4603a14bed5c2f9425"

/-!
# Density of simple functions

Show that each `L·µñ` Borel measurable function can be approximated in `L·µñ` norm
by a sequence of simple functions.

## Main definitions

* `MeasureTheory.Lp.simpleFunc`, the type of `Lp` simple functions
* `coeToLp`, the embedding of `Lp.simpleFunc E p Œº` into `Lp E p Œº`

## Main results

* `tendsto_approxOn_Lp_snorm` (L·µñ convergence): If `E` is a `NormedAddCommGroup` and `f` is
  measurable and `Mem‚Ñíp` (for `p < ‚àû`), then the simple functions
  `SimpleFunc.approxOn f hf s 0 h‚ÇÄ n` may be considered as elements of `Lp E p Œº`, and they tend
  in L·µñ to `f`.
* `Lp.simpleFunc.denseEmbedding`: the embedding `coeToLp` of the `Lp` simple functions into
  `Lp` is dense.
* `Lp.simpleFunc.induction`, `Lp.induction`, `Mem‚Ñíp.induction`, `Integrable.induction`: to prove
  a predicate for all elements of one of these classes of functions, it suffices to check that it
  behaves correctly on simple functions.

## TODO

For `E` finite-dimensional, simple functions `Œ± ‚Üí‚Çõ E` are dense in L^‚àû -- prove this.

## Notations

* `Œ± ‚Üí‚Çõ Œ≤` (local notation): the type of simple functions `Œ± ‚Üí Œ≤`.
* `Œ± ‚Üí‚ÇÅ‚Çõ[Œº] E`: the type of `L1` simple functions `Œ± ‚Üí Œ≤`.
-/


noncomputable section

set_option linter.uppercaseLean3 false

open Set Function Filter TopologicalSpace ENNReal EMetric Finset

open scoped Classical Topology ENNReal MeasureTheory

variable {Œ± Œ≤ Œπ E F ùïú : Type*}

namespace MeasureTheory

local infixr:25 " ‚Üí‚Çõ " => SimpleFunc

namespace SimpleFunc

/-! ### Lp approximation by simple functions -/

section Lp

variable [MeasurableSpace Œ≤] [MeasurableSpace E] [NormedAddCommGroup E] [NormedAddCommGroup F]
  {q : ‚Ñù} {p : ‚Ñù‚â•0‚àû}

theorem nnnorm_approxOn_le [OpensMeasurableSpace E] {f : Œ≤ ‚Üí E} (hf : Measurable f) {s : Set E}
    {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s] (x : Œ≤) (n : ‚Ñï) :
    ‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä ‚â§ ‚Äñf x - y‚ÇÄ‚Äñ‚Çä := by
  have := edist_approxOn_le hf h‚ÇÄ x n
  rw [edist_comm y‚ÇÄ] at this
  simp only [edist_nndist, nndist_eq_nnnorm] at this
  exact mod_cast this
#align measure_theory.simple_func.nnnorm_approx_on_le MeasureTheory.SimpleFunc.nnnorm_approxOn_le

theorem norm_approxOn_y‚ÇÄ_le [OpensMeasurableSpace E] {f : Œ≤ ‚Üí E} (hf : Measurable f) {s : Set E}
    {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s] (x : Œ≤) (n : ‚Ñï) :
    ‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ‚Äñ ‚â§ ‚Äñf x - y‚ÇÄ‚Äñ + ‚Äñf x - y‚ÇÄ‚Äñ := by
  have := edist_approxOn_y0_le hf h‚ÇÄ x n
  repeat rw [edist_comm y‚ÇÄ, edist_eq_coe_nnnorm_sub] at this
  exact mod_cast this
#align measure_theory.simple_func.norm_approx_on_y‚ÇÄ_le MeasureTheory.SimpleFunc.norm_approxOn_y‚ÇÄ_le

theorem norm_approxOn_zero_le [OpensMeasurableSpace E] {f : Œ≤ ‚Üí E} (hf : Measurable f) {s : Set E}
    (h‚ÇÄ : (0 : E) ‚àà s) [SeparableSpace s] (x : Œ≤) (n : ‚Ñï) :
    ‚ÄñapproxOn f hf s 0 h‚ÇÄ n x‚Äñ ‚â§ ‚Äñf x‚Äñ + ‚Äñf x‚Äñ := by
  have := edist_approxOn_y0_le hf h‚ÇÄ x n
  simp [edist_comm (0 : E), edist_eq_coe_nnnorm] at this
  exact mod_cast this
#align measure_theory.simple_func.norm_approx_on_zero_le MeasureTheory.SimpleFunc.norm_approxOn_zero_le

theorem tendsto_approxOn_Lp_snorm [OpensMeasurableSpace E] {f : Œ≤ ‚Üí E} (hf : Measurable f)
    {s : Set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s] (hp_ne_top : p ‚â† ‚àû) {Œº : Measure Œ≤}
    (hŒº : ‚àÄ·µê x ‚àÇŒº, f x ‚àà closure s) (hi : snorm (fun x => f x - y‚ÇÄ) p Œº < ‚àû) :
    Tendsto (fun n => snorm (‚áë(approxOn f hf s y‚ÇÄ h‚ÇÄ n) - f) p Œº) atTop (ùìù 0) := by
  by_cases hp_zero : p = 0
  ¬∑ simpa only [hp_zero, snorm_exponent_zero] using tendsto_const_nhds
  have hp : 0 < p.toReal := toReal_pos hp_zero hp_ne_top
  suffices
      Tendsto (fun n => ‚à´‚Åª x, (‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº) atTop
        (ùìù 0) by
    simp only [snorm_eq_lintegral_rpow_nnnorm hp_zero hp_ne_top]
    convert continuous_rpow_const.continuousAt.tendsto.comp this
    simp [zero_rpow_of_pos (_root_.inv_pos.mpr hp)]
  -- We simply check the conditions of the Dominated Convergence Theorem:
  -- (1) The function "`p`-th power of distance between `f` and the approximation" is measurable
  have hF_meas :
    ‚àÄ n, Measurable fun x => (‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal := by
    simpa only [‚Üê edist_eq_coe_nnnorm_sub] using fun n =>
      (approxOn f hf s y‚ÇÄ h‚ÇÄ n).measurable_bind (fun y x => edist y (f x) ^ p.toReal) fun y =>
        (measurable_edist_right.comp hf).pow_const p.toReal
  -- (2) The functions "`p`-th power of distance between `f` and the approximation" are uniformly
  -- bounded, at any given point, by `fun x => ‚Äñf x - y‚ÇÄ‚Äñ ^ p.toReal`
  have h_bound :
    ‚àÄ n, (fun x => (‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal) ‚â§·µê[Œº] fun x =>
        (‚Äñf x - y‚ÇÄ‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal :=
    fun n =>
    eventually_of_forall fun x =>
      rpow_le_rpow (coe_mono (nnnorm_approxOn_le hf h‚ÇÄ x n)) toReal_nonneg
  -- (3) The bounding function `fun x => ‚Äñf x - y‚ÇÄ‚Äñ ^ p.toReal` has finite integral
  have h_fin : (‚à´‚Åª a : Œ≤, (‚Äñf a - y‚ÇÄ‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº) ‚â† ‚ä§ :=
    (lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top hp_zero hp_ne_top hi).ne
  -- (4) The functions "`p`-th power of distance between `f` and the approximation" tend pointwise
  -- to zero
  have h_lim :
    ‚àÄ·µê a : Œ≤ ‚àÇŒº,
      Tendsto (fun n => (‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n a - f a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal) atTop (ùìù 0) := by
    filter_upwards [hŒº] with a ha
    have : Tendsto (fun n => (approxOn f hf s y‚ÇÄ h‚ÇÄ n) a - f a) atTop (ùìù (f a - f a)) :=
      (tendsto_approxOn hf h‚ÇÄ ha).sub tendsto_const_nhds
    convert continuous_rpow_const.continuousAt.tendsto.comp (tendsto_coe.mpr this.nnnorm)
    simp [zero_rpow_of_pos hp]
  -- Then we apply the Dominated Convergence Theorem
  simpa using tendsto_lintegral_of_dominated_convergence _ hF_meas h_bound h_fin h_lim
#align measure_theory.simple_func.tendsto_approx_on_Lp_snorm MeasureTheory.SimpleFunc.tendsto_approxOn_Lp_snorm

theorem mem‚Ñíp_approxOn [BorelSpace E] {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} (fmeas : Measurable f)
    (hf : Mem‚Ñíp f p Œº) {s : Set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s]
    (hi‚ÇÄ : Mem‚Ñíp (fun _ => y‚ÇÄ) p Œº) (n : ‚Ñï) : Mem‚Ñíp (approxOn f fmeas s y‚ÇÄ h‚ÇÄ n) p Œº := by
  refine ‚ü®(approxOn f fmeas s y‚ÇÄ h‚ÇÄ n).aestronglyMeasurable, ?_‚ü©
  suffices snorm (fun x => approxOn f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) p Œº < ‚ä§ by
    have : Mem‚Ñíp (fun x => approxOn f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) p Œº :=
      ‚ü®(approxOn f fmeas s y‚ÇÄ h‚ÇÄ n - const Œ≤ y‚ÇÄ).aestronglyMeasurable, this‚ü©
    convert snorm_add_lt_top this hi‚ÇÄ
    ext x
    simp
  have hf' : Mem‚Ñíp (fun x => ‚Äñf x - y‚ÇÄ‚Äñ) p Œº := by
    have h_meas : Measurable fun x => ‚Äñf x - y‚ÇÄ‚Äñ := by
      simp only [‚Üê dist_eq_norm]
      exact (continuous_id.dist continuous_const).measurable.comp fmeas
    refine ‚ü®h_meas.aemeasurable.aestronglyMeasurable, ?_‚ü©
    rw [snorm_norm]
    convert snorm_add_lt_top hf hi‚ÇÄ.neg with x
    simp [sub_eq_add_neg]
  have : ‚àÄ·µê x ‚àÇŒº, ‚ÄñapproxOn f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ‚Äñ ‚â§ ‚Äñ‚Äñf x - y‚ÇÄ‚Äñ + ‚Äñf x - y‚ÇÄ‚Äñ‚Äñ := by
    filter_upwards with x
    convert norm_approxOn_y‚ÇÄ_le fmeas h‚ÇÄ x n using 1
    rw [Real.norm_eq_abs, abs_of_nonneg]
    positivity
  calc
    snorm (fun x => approxOn f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) p Œº ‚â§
        snorm (fun x => ‚Äñf x - y‚ÇÄ‚Äñ + ‚Äñf x - y‚ÇÄ‚Äñ) p Œº :=
      snorm_mono_ae this
    _ < ‚ä§ := snorm_add_lt_top hf' hf'
#align measure_theory.simple_func.mem_‚Ñíp_approx_on MeasureTheory.SimpleFunc.mem‚Ñíp_approxOn

theorem tendsto_approxOn_range_Lp_snorm [BorelSpace E] {f : Œ≤ ‚Üí E} (hp_ne_top : p ‚â† ‚àû)
    {Œº : Measure Œ≤} (fmeas : Measurable f) [SeparableSpace (range f ‚à™ {0} : Set E)]
    (hf : snorm f p Œº < ‚àû) :
    Tendsto (fun n => snorm (‚áë(approxOn f fmeas (range f ‚à™ {0}) 0 (by simp) n) - f) p Œº)
      atTop (ùìù 0) := by
  refine tendsto_approxOn_Lp_snorm fmeas _ hp_ne_top ?_ ?_
  ¬∑ filter_upwards with x using subset_closure (by simp)
  ¬∑ simpa using hf
#align measure_theory.simple_func.tendsto_approx_on_range_Lp_snorm MeasureTheory.SimpleFunc.tendsto_approxOn_range_Lp_snorm

theorem mem‚Ñíp_approxOn_range [BorelSpace E] {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} (fmeas : Measurable f)
    [SeparableSpace (range f ‚à™ {0} : Set E)] (hf : Mem‚Ñíp f p Œº) (n : ‚Ñï) :
    Mem‚Ñíp (approxOn f fmeas (range f ‚à™ {0}) 0 (by simp) n) p Œº :=
  mem‚Ñíp_approxOn fmeas hf (y‚ÇÄ := 0) (by simp) zero_mem‚Ñíp n
#align measure_theory.simple_func.mem_‚Ñíp_approx_on_range MeasureTheory.SimpleFunc.mem‚Ñíp_approxOn_range

theorem tendsto_approxOn_range_Lp [BorelSpace E] {f : Œ≤ ‚Üí E} [hp : Fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû)
    {Œº : Measure Œ≤} (fmeas : Measurable f) [SeparableSpace (range f ‚à™ {0} : Set E)]
    (hf : Mem‚Ñíp f p Œº) :
    Tendsto
      (fun n =>
        (mem‚Ñíp_approxOn_range fmeas hf n).toLp (approxOn f fmeas (range f ‚à™ {0}) 0 (by simp) n))
      atTop (ùìù (hf.toLp f)) := by
  simpa only [Lp.tendsto_Lp_iff_tendsto_‚Ñíp''] using
    tendsto_approxOn_range_Lp_snorm hp_ne_top fmeas hf.2
#align measure_theory.simple_func.tendsto_approx_on_range_Lp MeasureTheory.SimpleFunc.tendsto_approxOn_range_Lp

/-- Any function in `‚Ñíp` can be approximated by a simple function if `p < ‚àû`. -/
theorem _root_.MeasureTheory.Mem‚Ñíp.exists_simpleFunc_snorm_sub_lt {E : Type*}
    [NormedAddCommGroup E] {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} (hf : Mem‚Ñíp f p Œº) (hp_ne_top : p ‚â† ‚àû)
    {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) : ‚àÉ g : Œ≤ ‚Üí‚Çõ E, snorm (f - ‚áëg) p Œº < Œµ ‚àß Mem‚Ñíp g p Œº := by
  borelize E
  let f' := hf.1.mk f
  rsuffices ‚ü®g, hg, g_mem‚ü© : ‚àÉ g : Œ≤ ‚Üí‚Çõ E, snorm (f' - ‚áëg) p Œº < Œµ ‚àß Mem‚Ñíp g p Œº
  ¬∑ refine ‚ü®g, ?_, g_mem‚ü©
    suffices snorm (f - ‚áëg) p Œº = snorm (f' - ‚áëg) p Œº by rwa [this]
    apply snorm_congr_ae
    filter_upwards [hf.1.ae_eq_mk] with x hx
    simpa only [Pi.sub_apply, sub_left_inj] using hx
  have hf' : Mem‚Ñíp f' p Œº := hf.ae_eq hf.1.ae_eq_mk
  have f'meas : Measurable f' := hf.1.measurable_mk
  have : SeparableSpace (range f' ‚à™ {0} : Set E) :=
    StronglyMeasurable.separableSpace_range_union_singleton hf.1.stronglyMeasurable_mk
  rcases ((tendsto_approxOn_range_Lp_snorm hp_ne_top f'meas hf'.2).eventually <|
    gt_mem_nhds hŒµ.bot_lt).exists with ‚ü®n, hn‚ü©
  rw [‚Üê snorm_neg, neg_sub] at hn
  exact ‚ü®_, hn, mem‚Ñíp_approxOn_range f'meas hf' _‚ü©
#align measure_theory.mem_‚Ñíp.exists_simple_func_snorm_sub_lt MeasureTheory.Mem‚Ñíp.exists_simpleFunc_snorm_sub_lt

end Lp

/-! ### L1 approximation by simple functions -/


section Integrable

variable [MeasurableSpace Œ≤]
variable [MeasurableSpace E] [NormedAddCommGroup E]

theorem tendsto_approxOn_L1_nnnorm [OpensMeasurableSpace E] {f : Œ≤ ‚Üí E} (hf : Measurable f)
    {s : Set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s] {Œº : Measure Œ≤}
    (hŒº : ‚àÄ·µê x ‚àÇŒº, f x ‚àà closure s) (hi : HasFiniteIntegral (fun x => f x - y‚ÇÄ) Œº) :
    Tendsto (fun n => ‚à´‚Åª x, ‚ÄñapproxOn f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä ‚àÇŒº) atTop (ùìù 0) := by
  simpa [snorm_one_eq_lintegral_nnnorm] using
    tendsto_approxOn_Lp_snorm hf h‚ÇÄ one_ne_top hŒº
      (by simpa [snorm_one_eq_lintegral_nnnorm] using hi)
#align measure_theory.simple_func.tendsto_approx_on_L1_nnnorm MeasureTheory.SimpleFunc.tendsto_approxOn_L1_nnnorm

theorem integrable_approxOn [BorelSpace E] {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} (fmeas : Measurable f)
    (hf : Integrable f Œº) {s : Set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s]
    (hi‚ÇÄ : Integrable (fun _ => y‚ÇÄ) Œº) (n : ‚Ñï) : Integrable (approxOn f fmeas s y‚ÇÄ h‚ÇÄ n) Œº := by
  rw [‚Üê mem‚Ñíp_one_iff_integrable] at hf hi‚ÇÄ ‚ä¢
  exact mem‚Ñíp_approxOn fmeas hf h‚ÇÄ hi‚ÇÄ n
#align measure_theory.simple_func.integrable_approx_on MeasureTheory.SimpleFunc.integrable_approxOn

theorem tendsto_approxOn_range_L1_nnnorm [OpensMeasurableSpace E] {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤}
    [SeparableSpace (range f ‚à™ {0} : Set E)] (fmeas : Measurable f) (hf : Integrable f Œº) :
    Tendsto (fun n => ‚à´‚Åª x, ‚ÄñapproxOn f fmeas (range f ‚à™ {0}) 0 (by simp) n x - f x‚Äñ‚Çä ‚àÇŒº) atTop
      (ùìù 0) := by
  apply tendsto_approxOn_L1_nnnorm fmeas
  ¬∑ filter_upwards with x using subset_closure (by simp)
  ¬∑ simpa using hf.2
#align measure_theory.simple_func.tendsto_approx_on_range_L1_nnnorm MeasureTheory.SimpleFunc.tendsto_approxOn_range_L1_nnnorm

theorem integrable_approxOn_range [BorelSpace E] {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} (fmeas : Measurable f)
    [SeparableSpace (range f ‚à™ {0} : Set E)] (hf : Integrable f Œº) (n : ‚Ñï) :
    Integrable (approxOn f fmeas (range f ‚à™ {0}) 0 (by simp) n) Œº :=
  integrable_approxOn fmeas hf _ (integrable_zero _ _ _) n
#align measure_theory.simple_func.integrable_approx_on_range MeasureTheory.SimpleFunc.integrable_approxOn_range

end Integrable

section SimpleFuncProperties

variable [MeasurableSpace Œ±]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable {Œº : Measure Œ±} {p : ‚Ñù‚â•0‚àû}

/-!
### Properties of simple functions in `Lp` spaces

A simple function `f : Œ± ‚Üí‚Çõ E` into a normed group `E` verifies, for a measure `Œº`:
- `Mem‚Ñíp f 0 Œº` and `Mem‚Ñíp f ‚àû Œº`, since `f` is a.e.-measurable and bounded,
- for `0 < p < ‚àû`,
  `Mem‚Ñíp f p Œº ‚Üî Integrable f Œº ‚Üî f.FinMeasSupp Œº ‚Üî ‚àÄ y, y ‚â† 0 ‚Üí Œº (f ‚Åª¬π' {y}) < ‚àû`.
-/


theorem exists_forall_norm_le (f : Œ± ‚Üí‚Çõ F) : ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C :=
  exists_forall_le (f.map fun x => ‚Äñx‚Äñ)
#align measure_theory.simple_func.exists_forall_norm_le MeasureTheory.SimpleFunc.exists_forall_norm_le

theorem mem‚Ñíp_zero (f : Œ± ‚Üí‚Çõ E) (Œº : Measure Œ±) : Mem‚Ñíp f 0 Œº :=
  mem‚Ñíp_zero_iff_aestronglyMeasurable.mpr f.aestronglyMeasurable
#align measure_theory.simple_func.mem_‚Ñíp_zero MeasureTheory.SimpleFunc.mem‚Ñíp_zero

theorem mem‚Ñíp_top (f : Œ± ‚Üí‚Çõ E) (Œº : Measure Œ±) : Mem‚Ñíp f ‚àû Œº :=
  let ‚ü®C, hfC‚ü© := f.exists_forall_norm_le
  mem‚Ñíp_top_of_bound f.aestronglyMeasurable C <| eventually_of_forall hfC
#align measure_theory.simple_func.mem_‚Ñíp_top MeasureTheory.SimpleFunc.mem‚Ñíp_top

protected theorem snorm'_eq {p : ‚Ñù} (f : Œ± ‚Üí‚Çõ F) (Œº : Measure Œ±) :
    snorm' f p Œº = (‚àë y ‚àà f.range, (‚Äñy‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p * Œº (f ‚Åª¬π' {y})) ^ (1 / p) := by
  have h_map : (fun a => (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p) = f.map fun a : F => (‚Äña‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p := by
    simp; rfl
  rw [snorm', h_map, lintegral_eq_lintegral, map_lintegral]
#align measure_theory.simple_func.snorm'_eq MeasureTheory.SimpleFunc.snorm'_eq

theorem measure_preimage_lt_top_of_mem‚Ñíp (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) (f : Œ± ‚Üí‚Çõ E)
    (hf : Mem‚Ñíp f p Œº) (y : E) (hy_ne : y ‚â† 0) : Œº (f ‚Åª¬π' {y}) < ‚àû := by
  have hp_pos_real : 0 < p.toReal := ENNReal.toReal_pos hp_pos hp_ne_top
  have hf_snorm := Mem‚Ñíp.snorm_lt_top hf
  rw [snorm_eq_snorm' hp_pos hp_ne_top, f.snorm'_eq, ‚Üê
    @ENNReal.lt_rpow_one_div_iff _ _ (1 / p.toReal) (by simp [hp_pos_real]),
    @ENNReal.top_rpow_of_pos (1 / (1 / p.toReal)) (by simp [hp_pos_real]),
    ENNReal.sum_lt_top_iff] at hf_snorm
  by_cases hyf : y ‚àà f.range
  swap
  ¬∑ suffices h_empty : f ‚Åª¬π' {y} = ‚àÖ by
      rw [h_empty, measure_empty]; exact ENNReal.coe_lt_top
    ext1 x
    rw [Set.mem_preimage, Set.mem_singleton_iff, mem_empty_iff_false, iff_false_iff]
    refine fun hxy => hyf ?_
    rw [mem_range, Set.mem_range]
    exact ‚ü®x, hxy‚ü©
  specialize hf_snorm y hyf
  rw [ENNReal.mul_lt_top_iff] at hf_snorm
  cases hf_snorm with
  | inl hf_snorm => exact hf_snorm.2
  | inr hf_snorm =>
    cases hf_snorm with
    | inl hf_snorm =>
      refine absurd ?_ hy_ne
      simpa [hp_pos_real] using hf_snorm
    | inr hf_snorm => simp [hf_snorm]
#align measure_theory.simple_func.measure_preimage_lt_top_of_mem_‚Ñíp MeasureTheory.SimpleFunc.measure_preimage_lt_top_of_mem‚Ñíp

theorem mem‚Ñíp_of_finite_measure_preimage (p : ‚Ñù‚â•0‚àû) {f : Œ± ‚Üí‚Çõ E}
    (hf : ‚àÄ y, y ‚â† 0 ‚Üí Œº (f ‚Åª¬π' {y}) < ‚àû) : Mem‚Ñíp f p Œº := by
  by_cases hp0 : p = 0
  ¬∑ rw [hp0, mem‚Ñíp_zero_iff_aestronglyMeasurable]; exact f.aestronglyMeasurable
  by_cases hp_top : p = ‚àû
  ¬∑ rw [hp_top]; exact mem‚Ñíp_top f Œº
  refine ‚ü®f.aestronglyMeasurable, ?_‚ü©
  rw [snorm_eq_snorm' hp0 hp_top, f.snorm'_eq]
  refine ENNReal.rpow_lt_top_of_nonneg (by simp) (ENNReal.sum_lt_top_iff.mpr fun y _ => ?_).ne
  by_cases hy0 : y = 0
  ¬∑ simp [hy0, ENNReal.toReal_pos hp0 hp_top]
  ¬∑ refine ENNReal.mul_lt_top ?_ (hf y hy0).ne
    exact (ENNReal.rpow_lt_top_of_nonneg ENNReal.toReal_nonneg ENNReal.coe_ne_top).ne
#align measure_theory.simple_func.mem_‚Ñíp_of_finite_measure_preimage MeasureTheory.SimpleFunc.mem‚Ñíp_of_finite_measure_preimage

theorem mem‚Ñíp_iff {f : Œ± ‚Üí‚Çõ E} (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
    Mem‚Ñíp f p Œº ‚Üî ‚àÄ y, y ‚â† 0 ‚Üí Œº (f ‚Åª¬π' {y}) < ‚àû :=
  ‚ü®fun h => measure_preimage_lt_top_of_mem‚Ñíp hp_pos hp_ne_top f h, fun h =>
    mem‚Ñíp_of_finite_measure_preimage p h‚ü©
#align measure_theory.simple_func.mem_‚Ñíp_iff MeasureTheory.SimpleFunc.mem‚Ñíp_iff

theorem integrable_iff {f : Œ± ‚Üí‚Çõ E} : Integrable f Œº ‚Üî ‚àÄ y, y ‚â† 0 ‚Üí Œº (f ‚Åª¬π' {y}) < ‚àû :=
  mem‚Ñíp_one_iff_integrable.symm.trans <| mem‚Ñíp_iff one_ne_zero ENNReal.coe_ne_top
#align measure_theory.simple_func.integrable_iff MeasureTheory.SimpleFunc.integrable_iff

theorem mem‚Ñíp_iff_integrable {f : Œ± ‚Üí‚Çõ E} (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
    Mem‚Ñíp f p Œº ‚Üî Integrable f Œº :=
  (mem‚Ñíp_iff hp_pos hp_ne_top).trans integrable_iff.symm
#align measure_theory.simple_func.mem_‚Ñíp_iff_integrable MeasureTheory.SimpleFunc.mem‚Ñíp_iff_integrable

theorem mem‚Ñíp_iff_finMeasSupp {f : Œ± ‚Üí‚Çõ E} (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
    Mem‚Ñíp f p Œº ‚Üî f.FinMeasSupp Œº :=
  (mem‚Ñíp_iff hp_pos hp_ne_top).trans finMeasSupp_iff.symm
#align measure_theory.simple_func.mem_‚Ñíp_iff_fin_meas_supp MeasureTheory.SimpleFunc.mem‚Ñíp_iff_finMeasSupp

theorem integrable_iff_finMeasSupp {f : Œ± ‚Üí‚Çõ E} : Integrable f Œº ‚Üî f.FinMeasSupp Œº :=
  integrable_iff.trans finMeasSupp_iff.symm
#align measure_theory.simple_func.integrable_iff_fin_meas_supp MeasureTheory.SimpleFunc.integrable_iff_finMeasSupp

theorem FinMeasSupp.integrable {f : Œ± ‚Üí‚Çõ E} (h : f.FinMeasSupp Œº) : Integrable f Œº :=
  integrable_iff_finMeasSupp.2 h
#align measure_theory.simple_func.fin_meas_supp.integrable MeasureTheory.SimpleFunc.FinMeasSupp.integrable

theorem integrable_pair {f : Œ± ‚Üí‚Çõ E} {g : Œ± ‚Üí‚Çõ F} :
    Integrable f Œº ‚Üí Integrable g Œº ‚Üí Integrable (pair f g) Œº := by
  simpa only [integrable_iff_finMeasSupp] using FinMeasSupp.pair
#align measure_theory.simple_func.integrable_pair MeasureTheory.SimpleFunc.integrable_pair

theorem mem‚Ñíp_of_isFiniteMeasure (f : Œ± ‚Üí‚Çõ E) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) [IsFiniteMeasure Œº] :
    Mem‚Ñíp f p Œº :=
  let ‚ü®C, hfC‚ü© := f.exists_forall_norm_le
  Mem‚Ñíp.of_bound f.aestronglyMeasurable C <| eventually_of_forall hfC
#align measure_theory.simple_func.mem_‚Ñíp_of_is_finite_measure MeasureTheory.SimpleFunc.mem‚Ñíp_of_isFiniteMeasure

theorem integrable_of_isFiniteMeasure [IsFiniteMeasure Œº] (f : Œ± ‚Üí‚Çõ E) : Integrable f Œº :=
  mem‚Ñíp_one_iff_integrable.mp (f.mem‚Ñíp_of_isFiniteMeasure 1 Œº)
#align measure_theory.simple_func.integrable_of_is_finite_measure MeasureTheory.SimpleFunc.integrable_of_isFiniteMeasure

theorem measure_preimage_lt_top_of_integrable (f : Œ± ‚Üí‚Çõ E) (hf : Integrable f Œº) {x : E}
    (hx : x ‚â† 0) : Œº (f ‚Åª¬π' {x}) < ‚àû :=
  integrable_iff.mp hf x hx
#align measure_theory.simple_func.measure_preimage_lt_top_of_integrable MeasureTheory.SimpleFunc.measure_preimage_lt_top_of_integrable

theorem measure_support_lt_top [Zero Œ≤] (f : Œ± ‚Üí‚Çõ Œ≤) (hf : ‚àÄ y, y ‚â† 0 ‚Üí Œº (f ‚Åª¬π' {y}) < ‚àû) :
    Œº (support f) < ‚àû := by
  rw [support_eq]
  refine (measure_biUnion_finset_le _ _).trans_lt (ENNReal.sum_lt_top_iff.mpr fun y hy => ?_)
  rw [Finset.mem_filter] at hy
  exact hf y hy.2
#align measure_theory.simple_func.measure_support_lt_top MeasureTheory.SimpleFunc.measure_support_lt_top

theorem measure_support_lt_top_of_mem‚Ñíp (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) : Œº (support f) < ‚àû :=
  f.measure_support_lt_top ((mem‚Ñíp_iff hp_ne_zero hp_ne_top).mp hf)
#align measure_theory.simple_func.measure_support_lt_top_of_mem_‚Ñíp MeasureTheory.SimpleFunc.measure_support_lt_top_of_mem‚Ñíp

theorem measure_support_lt_top_of_integrable (f : Œ± ‚Üí‚Çõ E) (hf : Integrable f Œº) :
    Œº (support f) < ‚àû :=
  f.measure_support_lt_top (integrable_iff.mp hf)
#align measure_theory.simple_func.measure_support_lt_top_of_integrable MeasureTheory.SimpleFunc.measure_support_lt_top_of_integrable

theorem measure_lt_top_of_mem‚Ñíp_indicator (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) {c : E} (hc : c ‚â† 0)
    {s : Set Œ±} (hs : MeasurableSet s) (hcs : Mem‚Ñíp ((const Œ± c).piecewise s hs (const Œ± 0)) p Œº) :
    Œº s < ‚ä§ := by
  have : Function.support (const Œ± c) = Set.univ := Function.support_const hc
  simpa only [mem‚Ñíp_iff_finMeasSupp hp_pos hp_ne_top, finMeasSupp_iff_support,
    support_indicator, Set.inter_univ, this] using hcs
#align measure_theory.simple_func.measure_lt_top_of_mem_‚Ñíp_indicator MeasureTheory.SimpleFunc.measure_lt_top_of_mem‚Ñíp_indicator

end SimpleFuncProperties

end SimpleFunc

/-! Construction of the space of `Lp` simple functions, and its dense embedding into `Lp`. -/


namespace Lp

open AEEqFun

variable [MeasurableSpace Œ±] [NormedAddCommGroup E] [NormedAddCommGroup F] (p : ‚Ñù‚â•0‚àû)
  (Œº : Measure Œ±)

variable (E)

-- Porting note: the proofs were rewritten in tactic mode to avoid an
-- "unknown free variable '_uniq.546677'" error.
/-- `Lp.simpleFunc` is a subspace of Lp consisting of equivalence classes of an integrable simple
    function. -/
def simpleFunc : AddSubgroup (Lp E p Œº) where
  carrier := { f : Lp E p Œº | ‚àÉ s : Œ± ‚Üí‚Çõ E, (AEEqFun.mk s s.aestronglyMeasurable : Œ± ‚Üí‚Çò[Œº] E) = f }
  zero_mem' := ‚ü®0, rfl‚ü©
  add_mem' := by
    rintro f g ‚ü®s, hs‚ü© ‚ü®t, ht‚ü©
    use s + t
    simp only [‚Üê hs, ‚Üê ht, AEEqFun.mk_add_mk, AddSubgroup.coe_add, AEEqFun.mk_eq_mk,
      SimpleFunc.coe_add]
  neg_mem' := by
    rintro f ‚ü®s, hs‚ü©
    use -s
    simp only [‚Üê hs, AEEqFun.neg_mk, SimpleFunc.coe_neg, AEEqFun.mk_eq_mk, AddSubgroup.coe_neg]
#align measure_theory.Lp.simple_func MeasureTheory.Lp.simpleFunc

variable {E p Œº}

namespace simpleFunc

section Instances

/-! Simple functions in Lp space form a `NormedSpace`. -/


#noalign measure_theory.Lp.simple_func.coe_coe

protected theorem eq' {f g : Lp.simpleFunc E p Œº} : (f : Œ± ‚Üí‚Çò[Œº] E) = (g : Œ± ‚Üí‚Çò[Œº] E) ‚Üí f = g :=
  Subtype.eq ‚àò Subtype.eq
#align measure_theory.Lp.simple_func.eq' MeasureTheory.Lp.simpleFunc.eq'

/-! Implementation note:  If `Lp.simpleFunc E p Œº` were defined as a `ùïú`-submodule of `Lp E p Œº`,
then the next few lemmas, putting a normed `ùïú`-group structure on `Lp.simpleFunc E p Œº`, would be
unnecessary.  But instead, `Lp.simpleFunc E p Œº` is defined as an `AddSubgroup` of `Lp E p Œº`,
which does not permit this (but has the advantage of working when `E` itself is a normed group,
i.e. has no scalar action). -/


variable [NormedRing ùïú] [Module ùïú E] [BoundedSMul ùïú E]

/-- If `E` is a normed space, `Lp.simpleFunc E p Œº` is a `SMul`. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def smul : SMul ùïú (Lp.simpleFunc E p Œº) :=
  ‚ü®fun k f =>
    ‚ü®k ‚Ä¢ (f : Lp E p Œº), by
      rcases f with ‚ü®f, ‚ü®s, hs‚ü©‚ü©
      use k ‚Ä¢ s
      apply Eq.trans (AEEqFun.smul_mk k s s.aestronglyMeasurable).symm _
      rw [hs]
      rfl‚ü©‚ü©
#align measure_theory.Lp.simple_func.has_smul MeasureTheory.Lp.simpleFunc.smul

attribute [local instance] simpleFunc.smul

@[simp, norm_cast]
theorem coe_smul (c : ùïú) (f : Lp.simpleFunc E p Œº) :
    ((c ‚Ä¢ f : Lp.simpleFunc E p Œº) : Lp E p Œº) = c ‚Ä¢ (f : Lp E p Œº) :=
  rfl
#align measure_theory.Lp.simple_func.coe_smul MeasureTheory.Lp.simpleFunc.coe_smul

/-- If `E` is a normed space, `Lp.simpleFunc E p Œº` is a module. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def module : Module ùïú (Lp.simpleFunc E p Œº) where
  one_smul f := by ext1; exact one_smul _ _
  mul_smul x y f := by ext1; exact mul_smul _ _ _
  smul_add x f g := by ext1; exact smul_add _ _ _
  smul_zero x := by ext1; exact smul_zero _
  add_smul x y f := by ext1; exact add_smul _ _ _
  zero_smul f := by ext1; exact zero_smul _ _
#align measure_theory.Lp.simple_func.module MeasureTheory.Lp.simpleFunc.module

attribute [local instance] simpleFunc.module

/-- If `E` is a normed space, `Lp.simpleFunc E p Œº` is a normed space. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected theorem boundedSMul [Fact (1 ‚â§ p)] : BoundedSMul ùïú (Lp.simpleFunc E p Œº) :=
  BoundedSMul.of_norm_smul_le fun r f => (norm_smul_le r (f : Lp E p Œº) : _)
#align measure_theory.Lp.simple_func.has_bounded_smul MeasureTheory.Lp.simpleFunc.boundedSMul

attribute [local instance] simpleFunc.boundedSMul

/-- If `E` is a normed space, `Lp.simpleFunc E p Œº` is a normed space. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def normedSpace {ùïú} [NormedField ùïú] [NormedSpace ùïú E] [Fact (1 ‚â§ p)] :
    NormedSpace ùïú (Lp.simpleFunc E p Œº) :=
  ‚ü®norm_smul_le (Œ± := ùïú) (Œ≤ := Lp.simpleFunc E p Œº)‚ü©
#align measure_theory.Lp.simple_func.normed_space MeasureTheory.Lp.simpleFunc.normedSpace

end Instances

attribute [local instance] simpleFunc.module simpleFunc.normedSpace simpleFunc.boundedSMul

section ToLp

/-- Construct the equivalence class `[f]` of a simple function `f` satisfying `Mem‚Ñíp`. -/
abbrev toLp (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) : Lp.simpleFunc E p Œº :=
  ‚ü®hf.toLp f, ‚ü®f, rfl‚ü©‚ü©
#align measure_theory.Lp.simple_func.to_Lp MeasureTheory.Lp.simpleFunc.toLp

theorem toLp_eq_toLp (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) : (toLp f hf : Lp E p Œº) = hf.toLp f :=
  rfl
#align measure_theory.Lp.simple_func.to_Lp_eq_to_Lp MeasureTheory.Lp.simpleFunc.toLp_eq_toLp

theorem toLp_eq_mk (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) :
    (toLp f hf : Œ± ‚Üí‚Çò[Œº] E) = AEEqFun.mk f f.aestronglyMeasurable :=
  rfl
#align measure_theory.Lp.simple_func.to_Lp_eq_mk MeasureTheory.Lp.simpleFunc.toLp_eq_mk

theorem toLp_zero : toLp (0 : Œ± ‚Üí‚Çõ E) zero_mem‚Ñíp = (0 : Lp.simpleFunc E p Œº) :=
  rfl
#align measure_theory.Lp.simple_func.to_Lp_zero MeasureTheory.Lp.simpleFunc.toLp_zero

theorem toLp_add (f g : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) (hg : Mem‚Ñíp g p Œº) :
    toLp (f + g) (hf.add hg) = toLp f hf + toLp g hg :=
  rfl
#align measure_theory.Lp.simple_func.to_Lp_add MeasureTheory.Lp.simpleFunc.toLp_add

theorem toLp_neg (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) : toLp (-f) hf.neg = -toLp f hf :=
  rfl
#align measure_theory.Lp.simple_func.to_Lp_neg MeasureTheory.Lp.simpleFunc.toLp_neg

theorem toLp_sub (f g : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) (hg : Mem‚Ñíp g p Œº) :
    toLp (f - g) (hf.sub hg) = toLp f hf - toLp g hg := by
  simp only [sub_eq_add_neg, ‚Üê toLp_neg, ‚Üê toLp_add]
#align measure_theory.Lp.simple_func.to_Lp_sub MeasureTheory.Lp.simpleFunc.toLp_sub

variable [NormedRing ùïú] [Module ùïú E] [BoundedSMul ùïú E]

theorem toLp_smul (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) (c : ùïú) :
    toLp (c ‚Ä¢ f) (hf.const_smul c) = c ‚Ä¢ toLp f hf :=
  rfl
#align measure_theory.Lp.simple_func.to_Lp_smul MeasureTheory.Lp.simpleFunc.toLp_smul

nonrec theorem norm_toLp [Fact (1 ‚â§ p)] (f : Œ± ‚Üí‚Çõ E) (hf : Mem‚Ñíp f p Œº) :
    ‚ÄñtoLp f hf‚Äñ = ENNReal.toReal (snorm f p Œº) :=
  norm_toLp f hf
#align measure_theory.Lp.simple_func.norm_to_Lp MeasureTheory.Lp.simpleFunc.norm_toLp

end ToLp

section ToSimpleFunc

/-- Find a representative of a `Lp.simpleFunc`. -/
def toSimpleFunc (f : Lp.simpleFunc E p Œº) : Œ± ‚Üí‚Çõ E :=
  Classical.choose f.2
#align measure_theory.Lp.simple_func.to_simple_func MeasureTheory.Lp.simpleFunc.toSimpleFunc

/-- `(toSimpleFunc f)` is measurable. -/
@[measurability]
protected theorem measurable [MeasurableSpace E] (f : Lp.simpleFunc E p Œº) :
    Measurable (toSimpleFunc f) :=
  (toSimpleFunc f).measurable
#align measure_theory.Lp.simple_func.measurable MeasureTheory.Lp.simpleFunc.measurable

protected theorem stronglyMeasurable (f : Lp.simpleFunc E p Œº) :
    StronglyMeasurable (toSimpleFunc f) :=
  (toSimpleFunc f).stronglyMeasurable
#align measure_theory.Lp.simple_func.strongly_measurable MeasureTheory.Lp.simpleFunc.stronglyMeasurable

@[measurability]
protected theorem aemeasurable [MeasurableSpace E] (f : Lp.simpleFunc E p Œº) :
    AEMeasurable (toSimpleFunc f) Œº :=
  (simpleFunc.measurable f).aemeasurable
#align measure_theory.Lp.simple_func.ae_measurable MeasureTheory.Lp.simpleFunc.aemeasurable

protected theorem aestronglyMeasurable (f : Lp.simpleFunc E p Œº) :
    AEStronglyMeasurable (toSimpleFunc f) Œº :=
  (simpleFunc.stronglyMeasurable f).aestronglyMeasurable
#align measure_theory.Lp.simple_func.ae_strongly_measurable MeasureTheory.Lp.simpleFunc.aestronglyMeasurable

theorem toSimpleFunc_eq_toFun (f : Lp.simpleFunc E p Œº) : toSimpleFunc f =·µê[Œº] f :=
  show ‚áë(toSimpleFunc f) =·µê[Œº] ‚áë(f : Œ± ‚Üí‚Çò[Œº] E) by
    convert (AEEqFun.coeFn_mk (toSimpleFunc f)
          (toSimpleFunc f).aestronglyMeasurable).symm using 2
    exact (Classical.choose_spec f.2).symm
#align measure_theory.Lp.simple_func.to_simple_func_eq_to_fun MeasureTheory.Lp.simpleFunc.toSimpleFunc_eq_toFun

/-- `toSimpleFunc f` satisfies the predicate `Mem‚Ñíp`. -/
protected theorem mem‚Ñíp (f : Lp.simpleFunc E p Œº) : Mem‚Ñíp (toSimpleFunc f) p Œº :=
  Mem‚Ñíp.ae_eq (toSimpleFunc_eq_toFun f).symm <| mem_Lp_iff_mem‚Ñíp.mp (f : Lp E p Œº).2
#align measure_theory.Lp.simple_func.mem_‚Ñíp MeasureTheory.Lp.simpleFunc.mem‚Ñíp

theorem toLp_toSimpleFunc (f : Lp.simpleFunc E p Œº) :
    toLp (toSimpleFunc f) (simpleFunc.mem‚Ñíp f) = f :=
  simpleFunc.eq' (Classical.choose_spec f.2)
#align measure_theory.Lp.simple_func.to_Lp_to_simple_func MeasureTheory.Lp.simpleFunc.toLp_toSimpleFunc

theorem toSimpleFunc_toLp (f : Œ± ‚Üí‚Çõ E) (hfi : Mem‚Ñíp f p Œº) : toSimpleFunc (toLp f hfi) =·µê[Œº] f := by
  rw [‚Üê AEEqFun.mk_eq_mk]; exact Classical.choose_spec (toLp f hfi).2
#align measure_theory.Lp.simple_func.to_simple_func_to_Lp MeasureTheory.Lp.simpleFunc.toSimpleFunc_toLp

variable (E Œº)

theorem zero_toSimpleFunc : toSimpleFunc (0 : Lp.simpleFunc E p Œº) =·µê[Œº] 0 := by
  filter_upwards [toSimpleFunc_eq_toFun (0 : Lp.simpleFunc E p Œº),
    Lp.coeFn_zero E 1 Œº] with _ h‚ÇÅ _
  rwa [h‚ÇÅ]
#align measure_theory.Lp.simple_func.zero_to_simple_func MeasureTheory.Lp.simpleFunc.zero_toSimpleFunc

variable {E Œº}

theorem add_toSimpleFunc (f g : Lp.simpleFunc E p Œº) :
    toSimpleFunc (f + g) =·µê[Œº] toSimpleFunc f + toSimpleFunc g := by
  filter_upwards [toSimpleFunc_eq_toFun (f + g), toSimpleFunc_eq_toFun f,
    toSimpleFunc_eq_toFun g, Lp.coeFn_add (f : Lp E p Œº) g] with _
  simp only [AddSubgroup.coe_add, Pi.add_apply]
  iterate 4 intro h; rw [h]
#align measure_theory.Lp.simple_func.add_to_simple_func MeasureTheory.Lp.simpleFunc.add_toSimpleFunc

theorem neg_toSimpleFunc (f : Lp.simpleFunc E p Œº) : toSimpleFunc (-f) =·µê[Œº] -toSimpleFunc f := by
  filter_upwards [toSimpleFunc_eq_toFun (-f), toSimpleFunc_eq_toFun f,
    Lp.coeFn_neg (f : Lp E p Œº)] with _
  simp only [Pi.neg_apply, AddSubgroup.coe_neg]
  repeat intro h; rw [h]
#align measure_theory.Lp.simple_func.neg_to_simple_func MeasureTheory.Lp.simpleFunc.neg_toSimpleFunc

theorem sub_toSimpleFunc (f g : Lp.simpleFunc E p Œº) :
    toSimpleFunc (f - g) =·µê[Œº] toSimpleFunc f - toSimpleFunc g := by
  filter_upwards [toSimpleFunc_eq_toFun (f - g), toSimpleFunc_eq_toFun f,
    toSimpleFunc_eq_toFun g, Lp.coeFn_sub (f : Lp E p Œº) g] with _
  simp only [AddSubgroup.coe_sub, Pi.sub_apply]
  repeat' intro h; rw [h]
#align measure_theory.Lp.simple_func.sub_to_simple_func MeasureTheory.Lp.simpleFunc.sub_toSimpleFunc

variable [NormedRing ùïú] [Module ùïú E] [BoundedSMul ùïú E]

theorem smul_toSimpleFunc (k : ùïú) (f : Lp.simpleFunc E p Œº) :
    toSimpleFunc (k ‚Ä¢ f) =·µê[Œº] k ‚Ä¢ ‚áë(toSimpleFunc f) := by
  filter_upwards [toSimpleFunc_eq_toFun (k ‚Ä¢ f), toSimpleFunc_eq_toFun f,
    Lp.coeFn_smul k (f : Lp E p Œº)] with _
  simp only [Pi.smul_apply, coe_smul]
  repeat intro h; rw [h]
#align measure_theory.Lp.simple_func.smul_to_simple_func MeasureTheory.Lp.simpleFunc.smul_toSimpleFunc

theorem norm_toSimpleFunc [Fact (1 ‚â§ p)] (f : Lp.simpleFunc E p Œº) :
    ‚Äñf‚Äñ = ENNReal.toReal (snorm (toSimpleFunc f) p Œº) := by
  simpa [toLp_toSimpleFunc] using norm_toLp (toSimpleFunc f) (simpleFunc.mem‚Ñíp f)
#align measure_theory.Lp.simple_func.norm_to_simple_func MeasureTheory.Lp.simpleFunc.norm_toSimpleFunc

end ToSimpleFunc

section Induction

variable (p)

/-- The characteristic function of a finite-measure measurable set `s`, as an `Lp` simple function.
-/
def indicatorConst {s : Set Œ±} (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
    Lp.simpleFunc E p Œº :=
  toLp ((SimpleFunc.const _ c).piecewise s hs (SimpleFunc.const _ 0))
    (mem‚Ñíp_indicator_const p hs c (Or.inr hŒºs))
#align measure_theory.Lp.simple_func.indicator_const MeasureTheory.Lp.simpleFunc.indicatorConst

variable {p}

@[simp]
theorem coe_indicatorConst {s : Set Œ±} (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
    (‚Üë(indicatorConst p hs hŒºs c) : Lp E p Œº) = indicatorConstLp p hs hŒºs c :=
  rfl
#align measure_theory.Lp.simple_func.coe_indicator_const MeasureTheory.Lp.simpleFunc.coe_indicatorConst

theorem toSimpleFunc_indicatorConst {s : Set Œ±} (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
    toSimpleFunc (indicatorConst p hs hŒºs c) =·µê[Œº]
      (SimpleFunc.const _ c).piecewise s hs (SimpleFunc.const _ 0) :=
  Lp.simpleFunc.toSimpleFunc_toLp _ _
#align measure_theory.Lp.simple_func.to_simple_func_indicator_const MeasureTheory.Lp.simpleFunc.toSimpleFunc_indicatorConst

/-- To prove something for an arbitrary `Lp` simple function, with `0 < p < ‚àû`, it suffices to show
that the property holds for (multiples of) characteristic functions of finite-measure measurable
sets and is closed under addition (of functions with disjoint support). -/
@[elab_as_elim]
protected theorem induction (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) {P : Lp.simpleFunc E p Œº ‚Üí Prop}
    (h_ind :
      ‚àÄ (c : E) {s : Set Œ±} (hs : MeasurableSet s) (hŒºs : Œº s < ‚àû),
        P (Lp.simpleFunc.indicatorConst p hs hŒºs.ne c))
    (h_add :
      ‚àÄ ‚¶Éf g : Œ± ‚Üí‚Çõ E‚¶Ñ,
        ‚àÄ hf : Mem‚Ñíp f p Œº,
          ‚àÄ hg : Mem‚Ñíp g p Œº,
            Disjoint (support f) (support g) ‚Üí
              P (Lp.simpleFunc.toLp f hf) ‚Üí
                P (Lp.simpleFunc.toLp g hg) ‚Üí P (Lp.simpleFunc.toLp f hf + Lp.simpleFunc.toLp g hg))
    (f : Lp.simpleFunc E p Œº) : P f := by
  suffices ‚àÄ f : Œ± ‚Üí‚Çõ E, ‚àÄ hf : Mem‚Ñíp f p Œº, P (toLp f hf) by
    rw [‚Üê toLp_toSimpleFunc f]
    apply this
  clear f
  apply SimpleFunc.induction
  ¬∑ intro c s hs hf
    by_cases hc : c = 0
    ¬∑ convert h_ind 0 MeasurableSet.empty (by simp) using 1
      ext1
      simp [hc]
    exact h_ind c hs (SimpleFunc.measure_lt_top_of_mem‚Ñíp_indicator hp_pos hp_ne_top hc hs hf)
  ¬∑ intro f g hfg hf hg hfg'
    obtain ‚ü®hf', hg'‚ü© : Mem‚Ñíp f p Œº ‚àß Mem‚Ñíp g p Œº :=
      (mem‚Ñíp_add_of_disjoint hfg f.stronglyMeasurable g.stronglyMeasurable).mp hfg'
    exact h_add hf' hg' hfg (hf hf') (hg hg')
#align measure_theory.Lp.simple_func.induction MeasureTheory.Lp.simpleFunc.induction

end Induction

section CoeToLp

variable [Fact (1 ‚â§ p)]

protected theorem uniformContinuous : UniformContinuous ((‚Üë) : Lp.simpleFunc E p Œº ‚Üí Lp E p Œº) :=
  uniformContinuous_comap
#align measure_theory.Lp.simple_func.uniform_continuous MeasureTheory.Lp.simpleFunc.uniformContinuous

protected theorem uniformEmbedding : UniformEmbedding ((‚Üë) : Lp.simpleFunc E p Œº ‚Üí Lp E p Œº) :=
  uniformEmbedding_comap Subtype.val_injective
#align measure_theory.Lp.simple_func.uniform_embedding MeasureTheory.Lp.simpleFunc.uniformEmbedding

protected theorem uniformInducing : UniformInducing ((‚Üë) : Lp.simpleFunc E p Œº ‚Üí Lp E p Œº) :=
  simpleFunc.uniformEmbedding.toUniformInducing
#align measure_theory.Lp.simple_func.uniform_inducing MeasureTheory.Lp.simpleFunc.uniformInducing

protected theorem denseEmbedding (hp_ne_top : p ‚â† ‚àû) :
    DenseEmbedding ((‚Üë) : Lp.simpleFunc E p Œº ‚Üí Lp E p Œº) := by
  borelize E
  apply simpleFunc.uniformEmbedding.denseEmbedding
  intro f
  rw [mem_closure_iff_seq_limit]
  have hfi' : Mem‚Ñíp f p Œº := Lp.mem‚Ñíp f
  haveI : SeparableSpace (range f ‚à™ {0} : Set E) :=
    (Lp.stronglyMeasurable f).separableSpace_range_union_singleton
  refine
    ‚ü®fun n =>
      toLp
        (SimpleFunc.approxOn f (Lp.stronglyMeasurable f).measurable (range f ‚à™ {0}) 0 _ n)
        (SimpleFunc.mem‚Ñíp_approxOn_range (Lp.stronglyMeasurable f).measurable hfi' n),
      fun n => mem_range_self _, ?_‚ü©
  convert SimpleFunc.tendsto_approxOn_range_Lp hp_ne_top (Lp.stronglyMeasurable f).measurable hfi'
  rw [toLp_coeFn f (Lp.mem‚Ñíp f)]
#align measure_theory.Lp.simple_func.dense_embedding MeasureTheory.Lp.simpleFunc.denseEmbedding

protected theorem denseInducing (hp_ne_top : p ‚â† ‚àû) :
    DenseInducing ((‚Üë) : Lp.simpleFunc E p Œº ‚Üí Lp E p Œº) :=
  (simpleFunc.denseEmbedding hp_ne_top).toDenseInducing
#align measure_theory.Lp.simple_func.dense_inducing MeasureTheory.Lp.simpleFunc.denseInducing

protected theorem denseRange (hp_ne_top : p ‚â† ‚àû) :
    DenseRange ((‚Üë) : Lp.simpleFunc E p Œº ‚Üí Lp E p Œº) :=
  (simpleFunc.denseInducing hp_ne_top).dense
#align measure_theory.Lp.simple_func.dense_range MeasureTheory.Lp.simpleFunc.denseRange

variable [NormedRing ùïú] [Module ùïú E] [BoundedSMul ùïú E]
variable (Œ± E ùïú)

/-- The embedding of Lp simple functions into Lp functions, as a continuous linear map. -/
def coeToLp : Lp.simpleFunc E p Œº ‚ÜíL[ùïú] Lp E p Œº :=
  { AddSubgroup.subtype (Lp.simpleFunc E p Œº) with
    map_smul' := fun _ _ => rfl
    cont := Lp.simpleFunc.uniformContinuous.continuous }
#align measure_theory.Lp.simple_func.coe_to_Lp MeasureTheory.Lp.simpleFunc.coeToLp

variable {Œ± E ùïú}

end CoeToLp

section Order

variable {G : Type*} [NormedLatticeAddCommGroup G]

theorem coeFn_le (f g : Lp.simpleFunc G p Œº) : (f : Œ± ‚Üí G) ‚â§·µê[Œº] g ‚Üî f ‚â§ g := by
  rw [‚Üê Subtype.coe_le_coe, ‚Üê Lp.coeFn_le]
#align measure_theory.Lp.simple_func.coe_fn_le MeasureTheory.Lp.simpleFunc.coeFn_le

instance instCovariantClassLE :
    CovariantClass (Lp.simpleFunc G p Œº) (Lp.simpleFunc G p Œº) (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑) := by
  refine ‚ü®fun f g‚ÇÅ g‚ÇÇ hg‚ÇÅ‚ÇÇ => ?_‚ü©
  rw [‚Üê Lp.simpleFunc.coeFn_le] at hg‚ÇÅ‚ÇÇ ‚ä¢
  have h_add_1 : ((f + g‚ÇÅ : Lp.simpleFunc G p Œº) : Œ± ‚Üí G) =·µê[Œº] (f : Œ± ‚Üí G) + g‚ÇÅ := Lp.coeFn_add _ _
  have h_add_2 : ((f + g‚ÇÇ : Lp.simpleFunc G p Œº) : Œ± ‚Üí G) =·µê[Œº] (f : Œ± ‚Üí G) + g‚ÇÇ := Lp.coeFn_add _ _
  filter_upwards [h_add_1, h_add_2, hg‚ÇÅ‚ÇÇ] with _ h1 h2 h3
  rw [h1, h2, Pi.add_apply, Pi.add_apply]
  exact add_le_add le_rfl h3
#align measure_theory.Lp.simple_func.has_le.le.covariant_class MeasureTheory.Lp.simpleFunc.instCovariantClassLE

variable (p Œº G)

theorem coeFn_zero : (0 : Lp.simpleFunc G p Œº) =·µê[Œº] (0 : Œ± ‚Üí G) :=
  Lp.coeFn_zero _ _ _
#align measure_theory.Lp.simple_func.coe_fn_zero MeasureTheory.Lp.simpleFunc.coeFn_zero

variable {p Œº G}

theorem coeFn_nonneg (f : Lp.simpleFunc G p Œº) : (0 : Œ± ‚Üí G) ‚â§·µê[Œº] f ‚Üî 0 ‚â§ f := by
  rw [‚Üê Subtype.coe_le_coe, Lp.coeFn_nonneg, AddSubmonoid.coe_zero]
#align measure_theory.Lp.simple_func.coe_fn_nonneg MeasureTheory.Lp.simpleFunc.coeFn_nonneg

theorem exists_simpleFunc_nonneg_ae_eq {f : Lp.simpleFunc G p Œº} (hf : 0 ‚â§ f) :
    ‚àÉ f' : Œ± ‚Üí‚Çõ G, 0 ‚â§ f' ‚àß f =·µê[Œº] f' := by
  rcases f with ‚ü®‚ü®f, hp‚ü©, g, (rfl : _ = f)‚ü©
  change 0 ‚â§·µê[Œº] g at hf
  refine ‚ü®g ‚äî 0, le_sup_right, (AEEqFun.coeFn_mk _ _).trans ?_‚ü©
  exact hf.mono fun x hx ‚Ü¶ (sup_of_le_left hx).symm
#align measure_theory.Lp.simple_func.exists_simple_func_nonneg_ae_eq MeasureTheory.Lp.simpleFunc.exists_simpleFunc_nonneg_ae_eq

variable (p Œº G)

/-- Coercion from nonnegative simple functions of Lp to nonnegative functions of Lp. -/
def coeSimpleFuncNonnegToLpNonneg :
    { g : Lp.simpleFunc G p Œº // 0 ‚â§ g } ‚Üí { g : Lp G p Œº // 0 ‚â§ g } := fun g => ‚ü®g, g.2‚ü©
#align measure_theory.Lp.simple_func.coe_simple_func_nonneg_to_Lp_nonneg MeasureTheory.Lp.simpleFunc.coeSimpleFuncNonnegToLpNonneg

theorem denseRange_coeSimpleFuncNonnegToLpNonneg [hp : Fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) :
    DenseRange (coeSimpleFuncNonnegToLpNonneg p Œº G) := fun g ‚Ü¶ by
  borelize G
  rw [mem_closure_iff_seq_limit]
  have hg_mem‚Ñíp : Mem‚Ñíp (g : Œ± ‚Üí G) p Œº := Lp.mem‚Ñíp (g : Lp G p Œº)
  have zero_mem : (0 : G) ‚àà (range (g : Œ± ‚Üí G) ‚à™ {0} : Set G) ‚à© { y | 0 ‚â§ y } := by
    simp only [union_singleton, mem_inter_iff, mem_insert_iff, eq_self_iff_true, true_or_iff,
      mem_setOf_eq, le_refl, and_self_iff]
  have : SeparableSpace ((range (g : Œ± ‚Üí G) ‚à™ {0}) ‚à© { y | 0 ‚â§ y } : Set G) := by
    apply IsSeparable.separableSpace
    apply IsSeparable.mono _ (Set.inter_subset_left _ _)
    exact
      (Lp.stronglyMeasurable (g : Lp G p Œº)).isSeparable_range.union
        (finite_singleton _).isSeparable
  have g_meas : Measurable (g : Œ± ‚Üí G) := (Lp.stronglyMeasurable (g : Lp G p Œº)).measurable
  let x n := SimpleFunc.approxOn g g_meas ((range (g : Œ± ‚Üí G) ‚à™ {0}) ‚à© { y | 0 ‚â§ y }) 0 zero_mem n
  have hx_nonneg : ‚àÄ n, 0 ‚â§ x n := by
    intro n a
    change x n a ‚àà { y : G | 0 ‚â§ y }
    have A : (range (g : Œ± ‚Üí G) ‚à™ {0} : Set G) ‚à© { y | 0 ‚â§ y } ‚äÜ { y | 0 ‚â§ y } :=
      inter_subset_right _ _
    apply A
    exact SimpleFunc.approxOn_mem g_meas _ n a
  have hx_mem‚Ñíp : ‚àÄ n, Mem‚Ñíp (x n) p Œº :=
    SimpleFunc.mem‚Ñíp_approxOn _ hg_mem‚Ñíp _ ‚ü®aestronglyMeasurable_const, by simp‚ü©
  have h_toLp := fun n => Mem‚Ñíp.coeFn_toLp (hx_mem‚Ñíp n)
  have hx_nonneg_Lp : ‚àÄ n, 0 ‚â§ toLp (x n) (hx_mem‚Ñíp n) := by
    intro n
    rw [‚Üê Lp.simpleFunc.coeFn_le, Lp.simpleFunc.toLp_eq_toLp]
    filter_upwards [Lp.simpleFunc.coeFn_zero p Œº G, h_toLp n] with a ha0 ha_toLp
    rw [ha0, ha_toLp]
    exact hx_nonneg n a
  have hx_tendsto :
      Tendsto (fun n : ‚Ñï => snorm ((x n : Œ± ‚Üí G) - (g : Œ± ‚Üí G)) p Œº) atTop (ùìù 0) := by
    apply SimpleFunc.tendsto_approxOn_Lp_snorm g_meas zero_mem hp_ne_top
    ¬∑ have hg_nonneg : (0 : Œ± ‚Üí G) ‚â§·µê[Œº] g := (Lp.coeFn_nonneg _).mpr g.2
      refine hg_nonneg.mono fun a ha => subset_closure ?_
      simpa using ha
    ¬∑ simp_rw [sub_zero]; exact hg_mem‚Ñíp.snorm_lt_top
  refine
    ‚ü®fun n =>
      (coeSimpleFuncNonnegToLpNonneg p Œº G) ‚ü®toLp (x n) (hx_mem‚Ñíp n), hx_nonneg_Lp n‚ü©,
      fun n => mem_range_self _, ?_‚ü©
  suffices Tendsto (fun n : ‚Ñï => (toLp (x n) (hx_mem‚Ñíp n) : Lp G p Œº)) atTop (ùìù (g : Lp G p Œº)) by
    rw [tendsto_iff_dist_tendsto_zero] at this ‚ä¢
    simp_rw [Subtype.dist_eq]
    exact this
  rw [Lp.tendsto_Lp_iff_tendsto_‚Ñíp']
  refine Filter.Tendsto.congr (fun n => snorm_congr_ae (EventuallyEq.sub ?_ ?_)) hx_tendsto
  ¬∑ symm
    rw [Lp.simpleFunc.toLp_eq_toLp]
    exact h_toLp n
  ¬∑ rfl
#align measure_theory.Lp.simple_func.dense_range_coe_simple_func_nonneg_to_Lp_nonneg MeasureTheory.Lp.simpleFunc.denseRange_coeSimpleFuncNonnegToLpNonneg

variable {p Œº G}

end Order

end simpleFunc

end Lp

variable [MeasurableSpace Œ±] [NormedAddCommGroup E] {f : Œ± ‚Üí E} {p : ‚Ñù‚â•0‚àû} {Œº : Measure Œ±}

/-- To prove something for an arbitrary `Lp` function in a second countable Borel normed group, it
suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in `Lp` for which the property holds is closed.
-/
@[elab_as_elim]
theorem Lp.induction [_i : Fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) (P : Lp E p Œº ‚Üí Prop)
    (h_ind : ‚àÄ (c : E) {s : Set Œ±} (hs : MeasurableSet s) (hŒºs : Œº s < ‚àû),
      P (Lp.simpleFunc.indicatorConst p hs hŒºs.ne c))
    (h_add : ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : Mem‚Ñíp f p Œº, ‚àÄ hg : Mem‚Ñíp g p Œº, Disjoint (support f) (support g) ‚Üí
      P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g))
    (h_closed : IsClosed { f : Lp E p Œº | P f }) : ‚àÄ f : Lp E p Œº, P f := by
  refine fun f => (Lp.simpleFunc.denseRange hp_ne_top).induction_on f h_closed ?_
  refine Lp.simpleFunc.induction (Œ± := Œ±) (E := E) (lt_of_lt_of_le zero_lt_one _i.elim).ne'
    hp_ne_top ?_ ?_
  ¬∑ exact fun c s => h_ind c
  ¬∑ exact fun f g hf hg => h_add hf hg
#align measure_theory.Lp.induction MeasureTheory.Lp.induction

/-- To prove something for an arbitrary `Mem‚Ñíp` function in a second countable
Borel normed group, it suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in the `L·µñ` space for which the property holds is closed.
* the property is closed under the almost-everywhere equal relation.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`).
-/
@[elab_as_elim]
theorem Mem‚Ñíp.induction [_i : Fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) (P : (Œ± ‚Üí E) ‚Üí Prop)
    (h_ind : ‚àÄ (c : E) ‚¶És‚¶Ñ, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí P (s.indicator fun _ => c))
    (h_add : ‚àÄ ‚¶Éf g : Œ± ‚Üí E‚¶Ñ, Disjoint (support f) (support g) ‚Üí Mem‚Ñíp f p Œº ‚Üí Mem‚Ñíp g p Œº ‚Üí
      P f ‚Üí P g ‚Üí P (f + g))
    (h_closed : IsClosed { f : Lp E p Œº | P f })
    (h_ae : ‚àÄ ‚¶Éf g‚¶Ñ, f =·µê[Œº] g ‚Üí Mem‚Ñíp f p Œº ‚Üí P f ‚Üí P g) :
    ‚àÄ ‚¶Éf : Œ± ‚Üí E‚¶Ñ, Mem‚Ñíp f p Œº ‚Üí P f := by
  have : ‚àÄ f : SimpleFunc Œ± E, Mem‚Ñíp f p Œº ‚Üí P f := by
    apply SimpleFunc.induction
    ¬∑ intro c s hs h
      by_cases hc : c = 0
      ¬∑ subst hc; convert h_ind 0 MeasurableSet.empty (by simp) using 1; ext; simp [const]
      have hp_pos : p ‚â† 0 := (lt_of_lt_of_le zero_lt_one _i.elim).ne'
      exact h_ind c hs (SimpleFunc.measure_lt_top_of_mem‚Ñíp_indicator hp_pos hp_ne_top hc hs h)
    ¬∑ intro f g hfg hf hg int_fg
      rw [SimpleFunc.coe_add,
        mem‚Ñíp_add_of_disjoint hfg f.stronglyMeasurable g.stronglyMeasurable] at int_fg
      exact h_add hfg int_fg.1 int_fg.2 (hf int_fg.1) (hg int_fg.2)
  have : ‚àÄ f : Lp.simpleFunc E p Œº, P f := by
    intro f
    exact
      h_ae (Lp.simpleFunc.toSimpleFunc_eq_toFun f) (Lp.simpleFunc.mem‚Ñíp f)
        (this (Lp.simpleFunc.toSimpleFunc f) (Lp.simpleFunc.mem‚Ñíp f))
  have : ‚àÄ f : Lp E p Œº, P f := fun f =>
    (Lp.simpleFunc.denseRange hp_ne_top).induction_on f h_closed this
  exact fun f hf => h_ae hf.coeFn_toLp (Lp.mem‚Ñíp _) (this (hf.toLp f))
#align measure_theory.mem_‚Ñíp.induction MeasureTheory.Mem‚Ñíp.induction

/-- If a set of ae strongly measurable functions is stable under addition and approximates
characteristic functions in `‚Ñíp`, then it is dense in `‚Ñíp`. -/
theorem Mem‚Ñíp.induction_dense (hp_ne_top : p ‚â† ‚àû) (P : (Œ± ‚Üí E) ‚Üí Prop)
    (h0P :
      ‚àÄ (c : E) ‚¶És : Set Œ±‚¶Ñ,
        MeasurableSet s ‚Üí
          Œº s < ‚àû ‚Üí
            ‚àÄ {Œµ : ‚Ñù‚â•0‚àû}, Œµ ‚â† 0 ‚Üí ‚àÉ g : Œ± ‚Üí E, snorm (g - s.indicator fun _ => c) p Œº ‚â§ Œµ ‚àß P g)
    (h1P : ‚àÄ f g, P f ‚Üí P g ‚Üí P (f + g)) (h2P : ‚àÄ f, P f ‚Üí AEStronglyMeasurable f Œº) {f : Œ± ‚Üí E}
    (hf : Mem‚Ñíp f p Œº) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) : ‚àÉ g : Œ± ‚Üí E, snorm (f - g) p Œº ‚â§ Œµ ‚àß P g := by
  rcases eq_or_ne p 0 with (rfl | hp_pos)
  ¬∑ rcases h0P (0 : E) MeasurableSet.empty (by simp only [measure_empty, zero_lt_top])
        hŒµ with ‚ü®g, _, Pg‚ü©
    exact ‚ü®g, by simp only [snorm_exponent_zero, zero_le'], Pg‚ü©
  suffices H : ‚àÄ (f' : Œ± ‚Üí‚Çõ E) (Œ¥ : ‚Ñù‚â•0‚àû) (hŒ¥ : Œ¥ ‚â† 0), Mem‚Ñíp f' p Œº ‚Üí
      ‚àÉ g, snorm (‚áëf' - g) p Œº ‚â§ Œ¥ ‚àß P g by
    obtain ‚ü®Œ∑, Œ∑pos, hŒ∑‚ü© := exists_Lp_half E Œº p hŒµ
    rcases hf.exists_simpleFunc_snorm_sub_lt hp_ne_top Œ∑pos.ne' with ‚ü®f', hf', f'_mem‚ü©
    rcases H f' Œ∑ Œ∑pos.ne' f'_mem with ‚ü®g, hg, Pg‚ü©
    refine ‚ü®g, ?_, Pg‚ü©
    convert (hŒ∑ _ _ (hf.aestronglyMeasurable.sub f'.aestronglyMeasurable)
          (f'.aestronglyMeasurable.sub (h2P g Pg)) hf'.le hg).le using 2
    simp only [sub_add_sub_cancel]
  apply SimpleFunc.induction
  ¬∑ intro c s hs Œµ Œµpos Hs
    rcases eq_or_ne c 0 with (rfl | hc)
    ¬∑ rcases h0P (0 : E) MeasurableSet.empty (by simp only [measure_empty, zero_lt_top])
          Œµpos with ‚ü®g, hg, Pg‚ü©
      rw [‚Üê snorm_neg, neg_sub] at hg
      refine ‚ü®g, ?_, Pg‚ü©
      convert hg
      ext x
      simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_zero,
        piecewise_eq_indicator, indicator_zero', Pi.zero_apply, indicator_zero]
    ¬∑ have : Œº s < ‚àû := SimpleFunc.measure_lt_top_of_mem‚Ñíp_indicator hp_pos hp_ne_top hc hs Hs
      rcases h0P c hs this Œµpos with ‚ü®g, hg, Pg‚ü©
      rw [‚Üê snorm_neg, neg_sub] at hg
      exact ‚ü®g, hg, Pg‚ü©
  ¬∑ intro f f' hff' hf hf' Œ¥ Œ¥pos int_ff'
    obtain ‚ü®Œ∑, Œ∑pos, hŒ∑‚ü© := exists_Lp_half E Œº p Œ¥pos
    rw [SimpleFunc.coe_add,
      mem‚Ñíp_add_of_disjoint hff' f.stronglyMeasurable f'.stronglyMeasurable] at int_ff'
    rcases hf Œ∑ Œ∑pos.ne' int_ff'.1 with ‚ü®g, hg, Pg‚ü©
    rcases hf' Œ∑ Œ∑pos.ne' int_ff'.2 with ‚ü®g', hg', Pg'‚ü©
    refine ‚ü®g + g', ?_, h1P g g' Pg Pg'‚ü©
    convert (hŒ∑ _ _ (f.aestronglyMeasurable.sub (h2P g Pg))
          (f'.aestronglyMeasurable.sub (h2P g' Pg')) hg hg').le using 2
    rw [SimpleFunc.coe_add]
    abel
#align measure_theory.mem_‚Ñíp.induction_dense MeasureTheory.Mem‚Ñíp.induction_dense

section Integrable

@[inherit_doc MeasureTheory.Lp.simpleFunc]
notation:25 Œ± " ‚Üí‚ÇÅ‚Çõ[" Œº "] " E => @MeasureTheory.Lp.simpleFunc Œ± E _ _ 1 Œº

theorem L1.SimpleFunc.toLp_one_eq_toL1 (f : Œ± ‚Üí‚Çõ E) (hf : Integrable f Œº) :
    (Lp.simpleFunc.toLp f (mem‚Ñíp_one_iff_integrable.2 hf) : Œ± ‚Üí‚ÇÅ[Œº] E) = hf.toL1 f :=
  rfl
#align measure_theory.L1.simple_func.to_Lp_one_eq_to_L1 MeasureTheory.L1.SimpleFunc.toLp_one_eq_toL1

protected theorem L1.SimpleFunc.integrable (f : Œ± ‚Üí‚ÇÅ‚Çõ[Œº] E) :
    Integrable (Lp.simpleFunc.toSimpleFunc f) Œº := by
  rw [‚Üê mem‚Ñíp_one_iff_integrable]; exact Lp.simpleFunc.mem‚Ñíp f
#align measure_theory.L1.simple_func.integrable MeasureTheory.L1.SimpleFunc.integrable

/-- To prove something for an arbitrary integrable function in a normed group,
it suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in the `L¬π` space for which the property holds is closed.
* the property is closed under the almost-everywhere equal relation.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`).
-/
@[elab_as_elim]
theorem Integrable.induction (P : (Œ± ‚Üí E) ‚Üí Prop)
    (h_ind : ‚àÄ (c : E) ‚¶És‚¶Ñ, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí P (s.indicator fun _ => c))
    (h_add :
      ‚àÄ ‚¶Éf g : Œ± ‚Üí E‚¶Ñ,
        Disjoint (support f) (support g) ‚Üí Integrable f Œº ‚Üí Integrable g Œº ‚Üí P f ‚Üí P g ‚Üí P (f + g))
    (h_closed : IsClosed { f : Œ± ‚Üí‚ÇÅ[Œº] E | P f })
    (h_ae : ‚àÄ ‚¶Éf g‚¶Ñ, f =·µê[Œº] g ‚Üí Integrable f Œº ‚Üí P f ‚Üí P g) :
    ‚àÄ ‚¶Éf : Œ± ‚Üí E‚¶Ñ, Integrable f Œº ‚Üí P f := by
  simp only [‚Üê mem‚Ñíp_one_iff_integrable] at *
  exact Mem‚Ñíp.induction one_ne_top (P := P) h_ind h_add h_closed h_ae
#align measure_theory.integrable.induction MeasureTheory.Integrable.induction

end Integrable

end MeasureTheory
