/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.StronglyMeasurable.ENNReal
import Mathlib.MeasureTheory.Measure.WithDensity

/-! # From equality of integrals to equality of functions

This file provides various statements of the general form "if two functions have the same integral
on all sets, then they are equal almost everywhere".
The different lemmas use various hypotheses on the class of functions, on the target space or on the
possible finiteness of the measure.

This file is about Lebesgue integrals. See the file `AEEqOfIntegral` for Bochner integrals.

## Main statements

The results listed below apply to two functions `f, g`, under the hypothesis that
for all measurable sets `s` with finite measure, `‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in s, g x ‚àÇŒº`.
The conclusion is then `f =·µê[Œº] g`. The main lemmas are:
* `ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite`: case of a sigma-finite measure.
* `AEMeasurable.ae_eq_of_forall_setLIntegral_eq`: for functions which are `AEMeasurable` and
  have finite integral.

-/


open Filter

open scoped ENNReal NNReal MeasureTheory Topology

namespace MeasureTheory

variable {Œ± : Type*} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {p : ‚Ñù‚â•0‚àû}

theorem ae_const_le_iff_forall_lt_measure_zero {Œ≤} [LinearOrder Œ≤] [TopologicalSpace Œ≤]
    [OrderTopology Œ≤] [FirstCountableTopology Œ≤] (f : Œ± ‚Üí Œ≤) (c : Œ≤) :
    (‚àÄ·µê x ‚àÇŒº, c ‚â§ f x) ‚Üî ‚àÄ b < c, Œº {x | f x ‚â§ b} = 0 := by
  rw [ae_iff]
  push_neg
  constructor
  ¬∑ intro h b hb
    exact measure_mono_null (fun y hy => (lt_of_le_of_lt hy hb : _)) h
  intro hc
  by_cases h : ‚àÄ b, c ‚â§ b
  ¬∑ have : {a : Œ± | f a < c} = ‚àÖ := by
      apply Set.eq_empty_iff_forall_not_mem.2 fun x hx => ?_
      exact (lt_irrefl _ (lt_of_lt_of_le hx (h (f x)))).elim
    simp [this]
  by_cases H : ¬¨IsLUB (Set.Iio c) c
  ¬∑ have : c ‚àà upperBounds (Set.Iio c) := fun y hy => le_of_lt hy
    obtain ‚ü®b, b_up, bc‚ü© : ‚àÉ b : Œ≤, b ‚àà upperBounds (Set.Iio c) ‚àß b < c := by
      simpa [IsLUB, IsLeast, this, lowerBounds] using H
    exact measure_mono_null (fun x hx => b_up hx) (hc b bc)
  push_neg at H h
  obtain ‚ü®u, _, u_lt, u_lim, -‚ü© :
    ‚àÉ u : ‚Ñï ‚Üí Œ≤,
      StrictMono u ‚àß (‚àÄ n : ‚Ñï, u n < c) ‚àß Tendsto u atTop (ùìù c) ‚àß ‚àÄ n : ‚Ñï, u n ‚àà Set.Iio c :=
    H.exists_seq_strictMono_tendsto_of_not_mem (lt_irrefl c) h
  have h_Union : {x | f x < c} = ‚ãÉ n : ‚Ñï, {x | f x ‚â§ u n} := by
    ext1 x
    simp_rw [Set.mem_iUnion, Set.mem_setOf_eq]
    constructor <;> intro h
    ¬∑ obtain ‚ü®n, hn‚ü© := ((tendsto_order.1 u_lim).1 _ h).exists; exact ‚ü®n, hn.le‚ü©
    ¬∑ obtain ‚ü®n, hn‚ü© := h; exact hn.trans_lt (u_lt _)
  rw [h_Union, measure_iUnion_null_iff]
  intro n
  exact hc _ (u_lt n)

theorem ae_le_of_forall_setLIntegral_le_of_sigmaFinite‚ÇÄ [SigmaFinite Œº]
    {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº)
    (h : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº ‚â§ ‚à´‚Åª x in s, g x ‚àÇŒº) :
    f ‚â§·µê[Œº] g := by
  have A : ‚àÄ (Œµ N : ‚Ñù‚â•0) (p : ‚Ñï), 0 < Œµ ‚Üí
      Œº ({x | g x + Œµ ‚â§ f x ‚àß g x ‚â§ N} ‚à© spanningSets Œº p) = 0 := by
    intro Œµ N p Œµpos
    let s := {x | g x + Œµ ‚â§ f x ‚àß g x ‚â§ N} ‚à© spanningSets Œº p
    have s_lt_top : Œº s < ‚àû :=
      (measure_mono (Set.inter_subset_right)).trans_lt (measure_spanningSets_lt_top Œº p)
    have A : (‚à´‚Åª x in s, g x ‚àÇŒº) + Œµ * Œº s ‚â§ (‚à´‚Åª x in s, g x ‚àÇŒº) + 0 :=
      calc
        (‚à´‚Åª x in s, g x ‚àÇŒº) + Œµ * Œº s = (‚à´‚Åª x in s, g x ‚àÇŒº) + ‚à´‚Åª _ in s, Œµ ‚àÇŒº := by
          simp only [lintegral_const, Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply]
        _ = ‚à´‚Åª x in s, g x + Œµ ‚àÇŒº := (lintegral_add_right _ measurable_const).symm
        _ ‚â§ ‚à´‚Åª x in s, f x ‚àÇŒº :=
          setLIntegral_mono_ae hf.restrict <| ae_of_all _ fun x hx => hx.1.1
        _ ‚â§ (‚à´‚Åª x in s, g x ‚àÇŒº) + 0 := by
          rw [add_zero, ‚Üê Measure.restrict_toMeasurable s_lt_top.ne]
          refine h _ (measurableSet_toMeasurable ..) ?_
          rwa [measure_toMeasurable]
    have B : (‚à´‚Åª x in s, g x ‚àÇŒº) ‚â† ‚àû :=
      (setLIntegral_lt_top_of_le_nnreal s_lt_top.ne ‚ü®N, fun _ h ‚Ü¶ h.1.2‚ü©).ne
    have : (Œµ : ‚Ñù‚â•0‚àû) * Œº s ‚â§ 0 := ENNReal.le_of_add_le_add_left B A
    simpa only [ENNReal.coe_eq_zero, nonpos_iff_eq_zero, mul_eq_zero, Œµpos.ne', false_or]
  obtain ‚ü®u, _, u_pos, u_lim‚ü© :
    ‚àÉ u : ‚Ñï ‚Üí ‚Ñù‚â•0, StrictAnti u ‚àß (‚àÄ n, 0 < u n) ‚àß Tendsto u atTop (ùìù 0) :=
    exists_seq_strictAnti_tendsto (0 : ‚Ñù‚â•0)
  let s := fun n : ‚Ñï => {x | g x + u n ‚â§ f x ‚àß g x ‚â§ (n : ‚Ñù‚â•0)} ‚à© spanningSets Œº n
  have Œºs : ‚àÄ n, Œº (s n) = 0 := fun n => A _ _ _ (u_pos n)
  have B : {x | f x ‚â§ g x}·∂ú ‚äÜ ‚ãÉ n, s n := by
    intro x hx
    simp only [Set.mem_compl_iff, Set.mem_setOf, not_le] at hx
    have L1 : ‚àÄ·∂† n in atTop, g x + u n ‚â§ f x := by
      have : Tendsto (fun n => g x + u n) atTop (ùìù (g x + (0 : ‚Ñù‚â•0))) :=
        tendsto_const_nhds.add (ENNReal.tendsto_coe.2 u_lim)
      simp only [ENNReal.coe_zero, add_zero] at this
      exact this.eventually_le_const hx
    have L2 : ‚àÄ·∂† n : ‚Ñï in (atTop : Filter ‚Ñï), g x ‚â§ (n : ‚Ñù‚â•0) :=
      have : Tendsto (fun n : ‚Ñï => ((n : ‚Ñù‚â•0) : ‚Ñù‚â•0‚àû)) atTop (ùìù ‚àû) := by
        simp only [ENNReal.coe_natCast]
        exact ENNReal.tendsto_nat_nhds_top
      this.eventually_const_le (hx.trans_le le_top)
    apply Set.mem_iUnion.2
    exact ((L1.and L2).and (eventually_mem_spanningSets Œº x)).exists
  refine le_antisymm ?_ bot_le
  calc
    Œº {x : Œ± | (fun x : Œ± => f x ‚â§ g x) x}·∂ú ‚â§ Œº (‚ãÉ n, s n) := measure_mono B
    _ ‚â§ ‚àë' n, Œº (s n) := measure_iUnion_le _
    _ = 0 := by simp only [Œºs, tsum_zero]

@[deprecated (since := "2024-06-29")]
alias ae_le_of_forall_set_lintegral_le_of_sigmaFinite‚ÇÄ :=
  ae_le_of_forall_setLIntegral_le_of_sigmaFinite‚ÇÄ

theorem ae_le_of_forall_setLIntegral_le_of_sigmaFinite [SigmaFinite Œº] {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû}
    (hf : Measurable f)
    (h : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí (‚à´‚Åª x in s, f x ‚àÇŒº) ‚â§ ‚à´‚Åª x in s, g x ‚àÇŒº) : f ‚â§·µê[Œº] g :=
  ae_le_of_forall_setLIntegral_le_of_sigmaFinite‚ÇÄ hf.aemeasurable h

@[deprecated (since := "2024-06-29")]
alias ae_le_of_forall_set_lintegral_le_of_sigmaFinite :=
  ae_le_of_forall_setLIntegral_le_of_sigmaFinite

theorem ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite‚ÇÄ [SigmaFinite Œº]
    {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº) (hg : AEMeasurable g Œº)
    (h : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in s, g x ‚àÇŒº) : f =·µê[Œº] g := by
  have A : f ‚â§·µê[Œº] g :=
    ae_le_of_forall_setLIntegral_le_of_sigmaFinite‚ÇÄ hf fun s hs h's => le_of_eq (h s hs h's)
  have B : g ‚â§·µê[Œº] f :=
    ae_le_of_forall_setLIntegral_le_of_sigmaFinite‚ÇÄ hg fun s hs h's => ge_of_eq (h s hs h's)
  filter_upwards [A, B] with x using le_antisymm

@[deprecated (since := "2024-06-29")]
alias ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite‚ÇÄ :=
  ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite‚ÇÄ

theorem ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite [SigmaFinite Œº] {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû}
    (hf : Measurable f) (hg : Measurable g)
    (h : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in s, g x ‚àÇŒº) : f =·µê[Œº] g :=
  ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite‚ÇÄ hf.aemeasurable hg.aemeasurable h

@[deprecated (since := "2024-06-29")]
alias ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite :=
  ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite

theorem AEMeasurable.ae_eq_of_forall_setLIntegral_eq {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº)
    (hg : AEMeasurable g Œº) (hfi : ‚à´‚Åª x, f x ‚àÇŒº ‚â† ‚àû) (hgi : ‚à´‚Åª x, g x ‚àÇŒº ‚â† ‚àû)
    (hfg : ‚àÄ ‚¶És‚¶Ñ, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in s, g x ‚àÇŒº) :
    f =·µê[Œº] g := by
  have hf' : AEFinStronglyMeasurable f Œº :=
    ENNReal.aefinStronglyMeasurable_of_aemeasurable hfi hf
  have hg' : AEFinStronglyMeasurable g Œº :=
    ENNReal.aefinStronglyMeasurable_of_aemeasurable hgi hg
  let s := hf'.sigmaFiniteSet
  let t := hg'.sigmaFiniteSet
  suffices f =·µê[Œº.restrict (s ‚à™ t)] g by
    refine ae_of_ae_restrict_of_ae_restrict_compl _ this ?_
    simp only [Set.compl_union]
    have h1 : f =·µê[Œº.restrict s·∂ú] 0 := hf'.ae_eq_zero_compl
    have h2 : g =·µê[Œº.restrict t·∂ú] 0 := hg'.ae_eq_zero_compl
    rw [ae_restrict_iff' (hf'.measurableSet.compl.inter hg'.measurableSet.compl)]
    rw [EventuallyEq, ae_restrict_iff' hf'.measurableSet.compl] at h1
    rw [EventuallyEq, ae_restrict_iff' hg'.measurableSet.compl] at h2
    filter_upwards [h1, h2] with x h1 h2 hx
    rw [h1 (Set.inter_subset_left hx), h2 (Set.inter_subset_right hx)]
  have := hf'.sigmaFinite_restrict
  have := hg'.sigmaFinite_restrict
  refine ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite‚ÇÄ hf.restrict hg.restrict
    fun u hu huŒº ‚Ü¶ ?_
  rw [Measure.restrict_restrict hu]
  rw [Measure.restrict_apply hu] at huŒº
  exact hfg (hu.inter (hf'.measurableSet.union hg'.measurableSet)) huŒº

@[deprecated (since := "2024-06-29")]
alias AEMeasurable.ae_eq_of_forall_set_lintegral_eq := AEMeasurable.ae_eq_of_forall_setLIntegral_eq

section WithDensity

variable {m : MeasurableSpace Œ±} {Œº : Measure Œ±}

theorem withDensity_eq_iff_of_sigmaFinite [SigmaFinite Œº] {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº)
    (hg : AEMeasurable g Œº) : Œº.withDensity f = Œº.withDensity g ‚Üî f =·µê[Œº] g :=
  ‚ü®fun hfg ‚Ü¶ by
    refine ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite‚ÇÄ hf hg fun s hs _ ‚Ü¶ ?_
    rw [‚Üê withDensity_apply f hs, ‚Üê withDensity_apply g hs, ‚Üê hfg], withDensity_congr_ae‚ü©

theorem withDensity_eq_iff {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : AEMeasurable f Œº)
    (hg : AEMeasurable g Œº) (hfi : ‚à´‚Åª x, f x ‚àÇŒº ‚â† ‚àû) :
    Œº.withDensity f = Œº.withDensity g ‚Üî f =·µê[Œº] g :=
  ‚ü®fun hfg ‚Ü¶ by
    refine AEMeasurable.ae_eq_of_forall_setLIntegral_eq hf hg hfi ?_ fun s hs _ ‚Ü¶ ?_
    ¬∑ rwa [‚Üê setLIntegral_univ, ‚Üê withDensity_apply g MeasurableSet.univ, ‚Üê hfg,
        withDensity_apply f MeasurableSet.univ, setLIntegral_univ]
    ¬∑ rw [‚Üê withDensity_apply f hs, ‚Üê withDensity_apply g hs, ‚Üê hfg], withDensity_congr_ae‚ü©

end WithDensity

end MeasureTheory
