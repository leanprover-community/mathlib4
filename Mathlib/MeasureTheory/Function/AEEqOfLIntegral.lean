/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.StronglyMeasurable.ENNReal
import Mathlib.MeasureTheory.Measure.WithDensity

/-! # From equality of integrals to equality of functions

This file provides various statements of the general form "if two functions have the same integral
on all sets, then they are equal almost everywhere".
The different lemmas use various hypotheses on the class of functions, on the target space or on the
possible finiteness of the measure.

This file is about Lebesgue integrals. See the file `AEEqOfIntegral` for Bochner integrals.

## Main statements

The results listed below apply to two functions `f, g`, under the hypothesis that
for all measurable sets `s` with finite measure, `âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼`.
The conclusion is then `f =áµ[Î¼] g`. The main lemmas are:
* `ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite`: case of a sigma-finite measure.
* `AEMeasurable.ae_eq_of_forall_setLIntegral_eq`: for functions which are `AEMeasurable` and
  have finite integral.

-/


open Filter

open scoped ENNReal NNReal MeasureTheory Topology

namespace MeasureTheory

variable {Î± : Type*} {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {p : â„â‰¥0âˆ}

theorem ae_const_le_iff_forall_lt_measure_zero {Î²} [LinearOrder Î²] [TopologicalSpace Î²]
    [OrderTopology Î²] [FirstCountableTopology Î²] (f : Î± â†’ Î²) (c : Î²) :
    (âˆ€áµ x âˆ‚Î¼, c â‰¤ f x) â†” âˆ€ b < c, Î¼ {x | f x â‰¤ b} = 0 := by
  rw [ae_iff]
  push_neg
  constructor
  Â· intro h b hb
    exact measure_mono_null (fun y hy => (lt_of_le_of_lt hy hb : _)) h
  intro hc
  by_cases h : âˆ€ b, c â‰¤ b
  Â· have : {a : Î± | f a < c} = âˆ… := by
      apply Set.eq_empty_iff_forall_notMem.2 fun x hx => ?_
      exact (lt_irrefl _ (lt_of_lt_of_le hx (h (f x)))).elim
    simp [this]
  by_cases H : Â¬IsLUB (Set.Iio c) c
  Â· have : c âˆˆ upperBounds (Set.Iio c) := fun y hy => le_of_lt hy
    obtain âŸ¨b, b_up, bcâŸ© : âˆƒ b : Î², b âˆˆ upperBounds (Set.Iio c) âˆ§ b < c := by
      simpa [IsLUB, IsLeast, this, lowerBounds] using H
    exact measure_mono_null (fun x hx => b_up hx) (hc b bc)
  push_neg at H h
  obtain âŸ¨u, _, u_lt, u_lim, -âŸ© :
    âˆƒ u : â„• â†’ Î²,
      StrictMono u âˆ§ (âˆ€ n : â„•, u n < c) âˆ§ Tendsto u atTop (ğ“ c) âˆ§ âˆ€ n : â„•, u n âˆˆ Set.Iio c :=
    H.exists_seq_strictMono_tendsto_of_notMem (lt_irrefl c) h
  have h_Union : {x | f x < c} = â‹ƒ n : â„•, {x | f x â‰¤ u n} := by
    ext1 x
    simp_rw [Set.mem_iUnion, Set.mem_setOf_eq]
    constructor <;> intro h
    Â· obtain âŸ¨n, hnâŸ© := ((tendsto_order.1 u_lim).1 _ h).exists; exact âŸ¨n, hn.leâŸ©
    Â· obtain âŸ¨n, hnâŸ© := h; exact hn.trans_lt (u_lt _)
  rw [h_Union, measure_iUnion_null_iff]
  intro n
  exact hc _ (u_lt n)

lemma ae_le_const_iff_forall_gt_measure_zero {Î²} [LinearOrder Î²] [TopologicalSpace Î²]
    [OrderTopology Î²] [FirstCountableTopology Î²] {Î¼ : Measure Î±} (f : Î± â†’ Î²) (c : Î²) :
    (âˆ€áµ x âˆ‚Î¼, f x â‰¤ c) â†” âˆ€ b, c < b â†’ Î¼ {x | b â‰¤ f x} = 0 :=
  ae_const_le_iff_forall_lt_measure_zero (Î² := Î²áµ’áµˆ) _ _

theorem ae_le_of_forall_setLIntegral_le_of_sigmaFiniteâ‚€ [SigmaFinite Î¼]
    {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, g x âˆ‚Î¼) :
    f â‰¤áµ[Î¼] g := by
  have A : âˆ€ (Îµ N : â„â‰¥0) (p : â„•), 0 < Îµ â†’
      Î¼ ({x | g x + Îµ â‰¤ f x âˆ§ g x â‰¤ N} âˆ© spanningSets Î¼ p) = 0 := by
    intro Îµ N p Îµpos
    let s := {x | g x + Îµ â‰¤ f x âˆ§ g x â‰¤ N} âˆ© spanningSets Î¼ p
    have s_lt_top : Î¼ s < âˆ :=
      (measure_mono (Set.inter_subset_right)).trans_lt (measure_spanningSets_lt_top Î¼ p)
    have A : (âˆ«â» x in s, g x âˆ‚Î¼) + Îµ * Î¼ s â‰¤ (âˆ«â» x in s, g x âˆ‚Î¼) + 0 :=
      calc
        (âˆ«â» x in s, g x âˆ‚Î¼) + Îµ * Î¼ s = (âˆ«â» x in s, g x âˆ‚Î¼) + âˆ«â» _ in s, Îµ âˆ‚Î¼ := by
          simp only [lintegral_const, Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply]
        _ = âˆ«â» x in s, g x + Îµ âˆ‚Î¼ := (lintegral_add_right _ measurable_const).symm
        _ â‰¤ âˆ«â» x in s, f x âˆ‚Î¼ :=
          setLIntegral_mono_ae hf.restrict <| ae_of_all _ fun x hx => hx.1.1
        _ â‰¤ (âˆ«â» x in s, g x âˆ‚Î¼) + 0 := by
          rw [add_zero, â† Measure.restrict_toMeasurable s_lt_top.ne]
          refine h _ (measurableSet_toMeasurable ..) ?_
          rwa [measure_toMeasurable]
    have B : (âˆ«â» x in s, g x âˆ‚Î¼) â‰  âˆ :=
      (setLIntegral_lt_top_of_le_nnreal s_lt_top.ne âŸ¨N, fun _ h â†¦ h.1.2âŸ©).ne
    have : (Îµ : â„â‰¥0âˆ) * Î¼ s â‰¤ 0 := ENNReal.le_of_add_le_add_left B A
    simpa only [ENNReal.coe_eq_zero, nonpos_iff_eq_zero, mul_eq_zero, Îµpos.ne', false_or]
  obtain âŸ¨u, _, u_pos, u_limâŸ© :
    âˆƒ u : â„• â†’ â„â‰¥0, StrictAnti u âˆ§ (âˆ€ n, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„â‰¥0)
  let s := fun n : â„• => {x | g x + u n â‰¤ f x âˆ§ g x â‰¤ (n : â„â‰¥0)} âˆ© spanningSets Î¼ n
  have Î¼s : âˆ€ n, Î¼ (s n) = 0 := fun n => A _ _ _ (u_pos n)
  have B : {x | f x â‰¤ g x}á¶œ âŠ† â‹ƒ n, s n := by
    intro x hx
    simp only [Set.mem_compl_iff, Set.mem_setOf, not_le] at hx
    have L1 : âˆ€á¶  n in atTop, g x + u n â‰¤ f x := by
      have : Tendsto (fun n => g x + u n) atTop (ğ“ (g x + (0 : â„â‰¥0))) :=
        tendsto_const_nhds.add (ENNReal.tendsto_coe.2 u_lim)
      simp only [ENNReal.coe_zero, add_zero] at this
      exact this.eventually_le_const hx
    have L2 : âˆ€á¶  n : â„• in (atTop : Filter â„•), g x â‰¤ (n : â„â‰¥0) :=
      have : Tendsto (fun n : â„• => ((n : â„â‰¥0) : â„â‰¥0âˆ)) atTop (ğ“ âˆ) := by
        simp only [ENNReal.coe_natCast]
        exact ENNReal.tendsto_nat_nhds_top
      this.eventually_const_le (hx.trans_le le_top)
    apply Set.mem_iUnion.2
    exact ((L1.and L2).and (eventually_mem_spanningSets Î¼ x)).exists
  refine le_antisymm ?_ bot_le
  calc
    Î¼ {x : Î± | (fun x : Î± => f x â‰¤ g x) x}á¶œ â‰¤ Î¼ (â‹ƒ n, s n) := measure_mono B
    _ â‰¤ âˆ‘' n, Î¼ (s n) := measure_iUnion_le _
    _ = 0 := by simp only [Î¼s, tsum_zero]

theorem ae_le_of_forall_setLIntegral_le_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ â„â‰¥0âˆ}
    (hf : Measurable f)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ«â» x in s, f x âˆ‚Î¼) â‰¤ âˆ«â» x in s, g x âˆ‚Î¼) : f â‰¤áµ[Î¼] g :=
  ae_le_of_forall_setLIntegral_le_of_sigmaFiniteâ‚€ hf.aemeasurable h

theorem ae_eq_of_forall_setLIntegral_eq_of_sigmaFiniteâ‚€ [SigmaFinite Î¼]
    {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼) : f =áµ[Î¼] g := by
  have A : f â‰¤áµ[Î¼] g :=
    ae_le_of_forall_setLIntegral_le_of_sigmaFiniteâ‚€ hf fun s hs h's => le_of_eq (h s hs h's)
  have B : g â‰¤áµ[Î¼] f :=
    ae_le_of_forall_setLIntegral_le_of_sigmaFiniteâ‚€ hg fun s hs h's => ge_of_eq (h s hs h's)
  filter_upwards [A, B] with x using le_antisymm

theorem ae_eq_of_forall_setLIntegral_eq_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ â„â‰¥0âˆ}
    (hf : Measurable f) (hg : Measurable g)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼) : f =áµ[Î¼] g :=
  ae_eq_of_forall_setLIntegral_eq_of_sigmaFiniteâ‚€ hf.aemeasurable hg.aemeasurable h

theorem AEMeasurable.ae_eq_of_forall_setLIntegral_eq {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) (hgi : âˆ«â» x, g x âˆ‚Î¼ â‰  âˆ)
    (hfg : âˆ€ â¦ƒsâ¦„, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼) :
    f =áµ[Î¼] g := by
  have hf' : AEFinStronglyMeasurable f Î¼ :=
    ENNReal.aefinStronglyMeasurable_of_aemeasurable hfi hf
  have hg' : AEFinStronglyMeasurable g Î¼ :=
    ENNReal.aefinStronglyMeasurable_of_aemeasurable hgi hg
  let s := hf'.sigmaFiniteSet
  let t := hg'.sigmaFiniteSet
  suffices f =áµ[Î¼.restrict (s âˆª t)] g by
    refine ae_of_ae_restrict_of_ae_restrict_compl _ this ?_
    simp only [Set.compl_union]
    have h1 : f =áµ[Î¼.restrict sá¶œ] 0 := hf'.ae_eq_zero_compl
    have h2 : g =áµ[Î¼.restrict tá¶œ] 0 := hg'.ae_eq_zero_compl
    rw [ae_restrict_iff' (hf'.measurableSet.compl.inter hg'.measurableSet.compl)]
    rw [EventuallyEq, ae_restrict_iff' hf'.measurableSet.compl] at h1
    rw [EventuallyEq, ae_restrict_iff' hg'.measurableSet.compl] at h2
    filter_upwards [h1, h2] with x h1 h2 hx
    rw [h1 (Set.inter_subset_left hx), h2 (Set.inter_subset_right hx)]
  refine ae_eq_of_forall_setLIntegral_eq_of_sigmaFiniteâ‚€ hf.restrict hg.restrict
    fun u hu huÎ¼ â†¦ ?_
  rw [Measure.restrict_restrict hu]
  rw [Measure.restrict_apply hu] at huÎ¼
  exact hfg (hu.inter (hf'.measurableSet.union hg'.measurableSet)) huÎ¼

section PiSystem

variable {s : Set (Set Î±)} {f g : Î± â†’ â„â‰¥0âˆ}

theorem lintegral_eq_lintegral_of_isPiSystem
    (h_eq : m0 = MeasurableSpace.generateFrom s) (h_inter : IsPiSystem s)
    (basic : âˆ€ t âˆˆ s, âˆ«â» x in t, f x âˆ‚Î¼ = âˆ«â» x in t, g x âˆ‚Î¼)
    (h_univ : âˆ«â» x, f x âˆ‚Î¼ = âˆ«â» x, g x âˆ‚Î¼) (hf_int : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) :
    âˆ€ t (_ : MeasurableSet t), âˆ«â» x in t, f x âˆ‚Î¼ = âˆ«â» x in t, g x âˆ‚Î¼ := by
  refine MeasurableSpace.induction_on_inter h_eq h_inter ?_ basic ?_ ?_
  Â· simp
  Â· intro t ht h_eq
    rw [setLIntegral_compl ht, setLIntegral_compl ht, h_eq, h_univ]
    Â· refine ne_of_lt ?_
      calc âˆ«â» x in t, g x âˆ‚Î¼
      _ â‰¤ âˆ«â» x, g x âˆ‚Î¼ := setLIntegral_le_lintegral t _
      _ < âˆ := by rw [â† h_univ]; exact hf_int.lt_top
    Â· refine ne_of_lt ?_
      calc âˆ«â» x in t, f x âˆ‚Î¼
      _ â‰¤ âˆ«â» x, f x âˆ‚Î¼ := setLIntegral_le_lintegral t _
      _ < âˆ := hf_int.lt_top
  Â· intro t htd htm h
    simp_rw [lintegral_iUnion htm htd, h]

lemma lintegral_eq_lintegral_of_isPiSystem_of_univ_mem
    (h_eq : m0 = MeasurableSpace.generateFrom s) (h_inter : IsPiSystem s) (h_univ : Set.univ âˆˆ s)
    (basic : âˆ€ t âˆˆ s, âˆ«â» x in t, f x âˆ‚Î¼ = âˆ«â» x in t, g x âˆ‚Î¼)
    (hf_int : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) {t : Set Î±} (ht : MeasurableSet t) :
    âˆ«â» x in t, f x âˆ‚Î¼ = âˆ«â» x in t, g x âˆ‚Î¼ := by
  refine lintegral_eq_lintegral_of_isPiSystem h_eq h_inter basic ?_ hf_int t ht
  rw [â† setLIntegral_univ, â† setLIntegral_univ g]
  exact basic _ h_univ

/-- If two a.e.-measurable functions `Î± Ã— Î² â†’ â„â‰¥0âˆ` with finite integrals have the same integral
on every rectangle, then they are almost everywhere equal. -/
lemma ae_eq_of_setLIntegral_prod_eq {Î² : Type*} {mÎ² : MeasurableSpace Î²}
    {Î¼ : Measure (Î± Ã— Î²)} {f g : Î± Ã— Î² â†’ â„â‰¥0âˆ}
    (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) (hf_int : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ)
    (h : âˆ€ â¦ƒs : Set Î±â¦„ (_ : MeasurableSet s) â¦ƒt : Set Î²â¦„ (_ : MeasurableSet t),
      âˆ«â» x in s Ã—Ë¢ t, f x âˆ‚Î¼ = âˆ«â» x in s Ã—Ë¢ t, g x âˆ‚Î¼) :
    f =áµ[Î¼] g := by
  have hg_int : âˆ«â» x, g x âˆ‚Î¼ â‰  âˆ := by
    rwa [â† setLIntegral_univ, â† Set.univ_prod_univ, â† h .univ .univ, Set.univ_prod_univ,
      setLIntegral_univ]
  refine AEMeasurable.ae_eq_of_forall_setLIntegral_eq hf hg hf_int hg_int fun s hs _ â†¦ ?_
  refine lintegral_eq_lintegral_of_isPiSystem_of_univ_mem generateFrom_prod.symm isPiSystem_prod
    ?_ ?_ hf_int hs
  Â· exact âŸ¨Set.univ, .univ, Set.univ, .univ, Set.univ_prod_univâŸ©
  Â· rintro _ âŸ¨s, hs, t, ht, rflâŸ©
    exact h hs ht

end PiSystem

section WithDensity

variable {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

theorem withDensity_eq_iff_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) : Î¼.withDensity f = Î¼.withDensity g â†” f =áµ[Î¼] g :=
  âŸ¨fun hfg â†¦ by
    refine ae_eq_of_forall_setLIntegral_eq_of_sigmaFiniteâ‚€ hf hg fun s hs _ â†¦ ?_
    rw [â† withDensity_apply f hs, â† withDensity_apply g hs, â† hfg], withDensity_congr_aeâŸ©

theorem withDensity_eq_iff {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) :
    Î¼.withDensity f = Î¼.withDensity g â†” f =áµ[Î¼] g :=
  âŸ¨fun hfg â†¦ by
    refine AEMeasurable.ae_eq_of_forall_setLIntegral_eq hf hg hfi ?_ fun s hs _ â†¦ ?_
    Â· rwa [â† setLIntegral_univ, â† withDensity_apply g MeasurableSet.univ, â† hfg,
        withDensity_apply f MeasurableSet.univ, setLIntegral_univ]
    Â· rw [â† withDensity_apply f hs, â† withDensity_apply g hs, â† hfg], withDensity_congr_aeâŸ©

end WithDensity

end MeasureTheory
