/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.MeasureTheory.Measure.Regular
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp
import Mathlib.Topology.UrysohnsLemma
import Mathlib.MeasureTheory.Function.LpSpace.ContinuousFunctions
import Mathlib.MeasureTheory.Integral.Bochner.Basic

/-!
# Approximation in Láµ– by continuous functions

This file proves that bounded continuous functions are dense in `Lp E p Î¼`, for `p < âˆž`, if the
domain `Î±` of the functions is a normal topological space and the measure `Î¼` is weakly regular.
It also proves the same results for approximation by continuous functions with compact support
when the space is locally compact and `Î¼` is regular.

The result is presented in several versions. First concrete versions giving an approximation
up to `Îµ` in these various contexts, and then abstract versions stating that the topological
closure of the relevant subgroups of `Lp` are the whole space.

* `MeasureTheory.MemLp.exists_hasCompactSupport_eLpNorm_sub_le` states that, in a locally compact
  space, an `â„’p` function can be approximated by continuous functions with compact support,
  in the sense that `eLpNorm (f - g) p Î¼` is small.
* `MeasureTheory.MemLp.exists_hasCompactSupport_integral_rpow_sub_le`: same result, but expressed in
  terms of `âˆ« â€–f - gâ€–^p`.

Versions with `Integrable` instead of `MemLp` are specialized to the case `p = 1`.
Versions with `boundedContinuous` instead of `HasCompactSupport` drop the locally
compact assumption and give only approximation by a bounded continuous function.

* `MeasureTheory.Lp.boundedContinuousFunction_dense`: The subgroup
  `MeasureTheory.Lp.boundedContinuousFunction` of `Lp E p Î¼`, the additive subgroup of
  `Lp E p Î¼` consisting of equivalence classes containing a continuous representative, is dense in
  `Lp E p Î¼`.
* `BoundedContinuousFunction.toLp_denseRange`: For finite-measure `Î¼`, the continuous linear
  map `BoundedContinuousFunction.toLp p Î¼ ð•œ` from `Î± â†’áµ‡ E` to `Lp E p Î¼` has dense range.
* `ContinuousMap.toLp_denseRange`: For compact `Î±` and finite-measure `Î¼`, the continuous linear
  map `ContinuousMap.toLp p Î¼ ð•œ` from `C(Î±, E)` to `Lp E p Î¼` has dense range.

Note that for `p = âˆž` this result is not true:  the characteristic function of the set `[0, âˆž)` in
`â„` cannot be continuously approximated in `Lâˆž`.

The proof is in three steps.  First, since simple functions are dense in `Lp`, it suffices to prove
the result for a scalar multiple of a characteristic function of a measurable set `s`. Secondly,
since the measure `Î¼` is weakly regular, the set `s` can be approximated above by an open set and
below by a closed set.  Finally, since the domain `Î±` is normal, we use Urysohn's lemma to find a
continuous function interpolating between these two sets.

## Related results

Are you looking for a result on "directional" approximation (above or below with respect to an
order) of functions whose codomain is `â„â‰¥0âˆž` or `â„`, by semicontinuous functions?
See the Vitali-CarathÃ©odory theorem,
in the file `Mathlib/MeasureTheory/Integral/Bochner/VitaliCaratheodory.lean`.
-/

open scoped ENNReal NNReal Topology BoundedContinuousFunction

open MeasureTheory TopologicalSpace ContinuousMap Set Bornology

variable {Î± : Type*} [TopologicalSpace Î±] [NormalSpace Î±]
  [MeasurableSpace Î±] [BorelSpace Î±]
variable {E : Type*} [NormedAddCommGroup E] {Î¼ : Measure Î±} {p : â„â‰¥0âˆž}

namespace MeasureTheory

variable [NormedSpace â„ E]

/-- A variant of Urysohn's lemma, `â„’^p` version, for an outer regular measure `Î¼`:
consider two sets `s âŠ† u` which are respectively closed and open with `Î¼ s < âˆž`, and a vector `c`.
Then one may find a continuous function `f` equal to `c` on `s` and to `0` outside of `u`,
bounded by `â€–câ€–` everywhere, and such that the `â„’^p` norm of `f - s.indicator (fun y â†¦ c)` is
arbitrarily small. Additionally, this function `f` belongs to `â„’^p`. -/
theorem exists_continuous_eLpNorm_sub_le_of_closed [Î¼.OuterRegular] (hp : p â‰  âˆž) {s u : Set Î±}
    (s_closed : IsClosed s) (u_open : IsOpen u) (hsu : s âŠ† u) (hs : Î¼ s â‰  âˆž) (c : E) {Îµ : â„â‰¥0âˆž}
    (hÎµ : Îµ â‰  0) :
    âˆƒ f : Î± â†’ E,
      Continuous f âˆ§
        eLpNorm (fun x â†¦ f x - s.indicator (fun _y â†¦ c) x) p Î¼ â‰¤ Îµ âˆ§
          (âˆ€ x, â€–f xâ€– â‰¤ â€–câ€–) âˆ§ Function.support f âŠ† u âˆ§ MemLp f p Î¼ := by
  obtain âŸ¨Î·, Î·_pos, hÎ·âŸ© :
      âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ âˆ€ s : Set Î±, Î¼ s â‰¤ Î· â†’ eLpNorm (s.indicator fun _x â†¦ c) p Î¼ â‰¤ Îµ :=
    exists_eLpNorm_indicator_le hp c hÎµ
  have Î·pos : (0 : â„â‰¥0âˆž) < Î· := ENNReal.coe_lt_coe.2 Î·_pos
  obtain âŸ¨V, sV, V_open, h'V, hVâŸ© : âˆƒ (V : Set Î±), V âŠ‡ s âˆ§ IsOpen V âˆ§ Î¼ V < âˆž âˆ§ Î¼ (V \ s) < Î· :=
    s_closed.measurableSet.exists_isOpen_diff_lt hs Î·pos.ne'
  let v := u âˆ© V
  have hsv : s âŠ† v := subset_inter hsu sV
  have hÎ¼v : Î¼ v < âˆž := (measure_mono inter_subset_right).trans_lt h'V
  obtain âŸ¨g, hgv, hgs, hg_rangeâŸ© :=
    exists_continuous_zero_one_of_isClosed (u_open.inter V_open).isClosed_compl s_closed
      (disjoint_compl_left_iff.2 hsv)
  -- Multiply this by `c` to get a continuous approximation to the function `f`; the key point is
  -- that this is pointwise bounded by the indicator of the set `v \ s`, which has small measure.
  have g_norm : âˆ€ x, â€–g xâ€– = g x := fun x â†¦ by rw [Real.norm_eq_abs, abs_of_nonneg (hg_range x).1]
  have gc_bd0 : âˆ€ x, â€–g x â€¢ câ€– â‰¤ â€–câ€– := by
    intro x
    simp only [norm_smul, g_norm x]
    apply mul_le_of_le_one_left (norm_nonneg _)
    exact (hg_range x).2
  have gc_bd :
      âˆ€ x, â€–g x â€¢ c - s.indicator (fun _x â†¦ c) xâ€– â‰¤ â€–(v \ s).indicator (fun _x â†¦ c) xâ€– := by
    intro x
    by_cases hv : x âˆˆ v
    Â· rw [â† Set.diff_union_of_subset hsv] at hv
      rcases hv with hsv | hs
      Â· simpa only [hsv.2, Set.indicator_of_notMem, not_false_iff, sub_zero, hsv,
          Set.indicator_of_mem] using gc_bd0 x
      Â· simp [hgs hs, hs]
    Â· simp [hgv hv, show x âˆ‰ s from fun h â†¦ hv (hsv h)]
  have gc_support : (Function.support fun x : Î± â†¦ g x â€¢ c) âŠ† v := by
    refine Function.support_subset_iff'.2 fun x hx â†¦ ?_
    simp only [hgv hx, Pi.zero_apply, zero_smul]
  have gc_mem : MemLp (fun x â†¦ g x â€¢ c) p Î¼ := by
    refine MemLp.smul (memLp_top_const _) ?_ (p := p) (q := âˆž)
    refine âŸ¨g.continuous.aestronglyMeasurable, ?_âŸ©
    have : eLpNorm (v.indicator fun _x â†¦ (1 : â„)) p Î¼ < âŠ¤ :=
      (eLpNorm_indicator_const_le _ _).trans_lt <| by simp [lt_top_iff_ne_top, hÎ¼v.ne]
    refine (eLpNorm_mono fun x â†¦ ?_).trans_lt this
    by_cases hx : x âˆˆ v
    Â· simp only [hx, abs_of_nonneg (hg_range x).1, (hg_range x).2, Real.norm_eq_abs,
        indicator_of_mem, CStarRing.norm_one]
    Â· simp only [hgv hx, Pi.zero_apply, Real.norm_eq_abs, abs_zero, abs_nonneg]
  refine
    âŸ¨fun x â†¦ g x â€¢ c, g.continuous.smul continuous_const, (eLpNorm_mono gc_bd).trans ?_, gc_bd0,
      gc_support.trans inter_subset_left, gc_memâŸ©
  exact hÎ· _ ((measure_mono (diff_subset_diff inter_subset_right Subset.rfl)).trans hV.le)

/-- In a locally compact space, any function in `â„’p` can be approximated by compactly supported
continuous functions when `p < âˆž`, version in terms of `eLpNorm`. -/
theorem MemLp.exists_hasCompactSupport_eLpNorm_sub_le
    [R1Space Î±] [WeaklyLocallyCompactSpace Î±] [Î¼.Regular]
    (hp : p â‰  âˆž) {f : Î± â†’ E} (hf : MemLp f p Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
    âˆƒ g : Î± â†’ E, HasCompactSupport g âˆ§ eLpNorm (f - g) p Î¼ â‰¤ Îµ âˆ§ Continuous g âˆ§ MemLp g p Î¼ := by
  suffices H :
      âˆƒ g : Î± â†’ E, eLpNorm (f - g) p Î¼ â‰¤ Îµ âˆ§ Continuous g âˆ§ MemLp g p Î¼ âˆ§ HasCompactSupport g by
    rcases H with âŸ¨g, hg, g_cont, g_mem, g_supportâŸ©
    exact âŸ¨g, g_support, hg, g_cont, g_memâŸ©
  -- It suffices to check that the set of functions we consider approximates characteristic
  -- functions, is stable under addition and consists of ae strongly measurable functions.
  -- First check the latter easy facts.
  apply hf.induction_dense hp _ _ _ _ hÎµ
  rotate_left
  -- stability under addition
  Â· rintro f g âŸ¨f_cont, f_mem, hfâŸ© âŸ¨g_cont, g_mem, hgâŸ©
    exact âŸ¨f_cont.add g_cont, f_mem.add g_mem, hf.add hgâŸ©
  -- ae strong measurability
  Â· rintro f âŸ¨_f_cont, f_mem, _hfâŸ©
    exact f_mem.aestronglyMeasurable
  -- We are left with approximating characteristic functions.
  -- This follows from `exists_continuous_eLpNorm_sub_le_of_closed`.
  intro c t ht htÎ¼ Îµ hÎµ
  rcases exists_Lp_half E Î¼ p hÎµ with âŸ¨Î´, Î´pos, hÎ´âŸ©
  obtain âŸ¨Î·, Î·pos, hÎ·âŸ© :
      âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ âˆ€ s : Set Î±, Î¼ s â‰¤ Î· â†’ eLpNorm (s.indicator fun _x â†¦ c) p Î¼ â‰¤ Î´ :=
    exists_eLpNorm_indicator_le hp c Î´pos.ne'
  have hÎ·_pos' : (0 : â„â‰¥0âˆž) < Î· := ENNReal.coe_pos.2 Î·pos
  obtain âŸ¨s, st, s_compact, s_closed, Î¼sâŸ© :
      âˆƒ s, s âŠ† t âˆ§ IsCompact s âˆ§ IsClosed s âˆ§ Î¼ (t \ s) < Î· :=
    ht.exists_isCompact_isClosed_diff_lt htÎ¼.ne hÎ·_pos'.ne'
  have hsÎ¼ : Î¼ s < âˆž := (measure_mono st).trans_lt htÎ¼
  have I1 : eLpNorm ((s.indicator fun _y â†¦ c) - t.indicator fun _y â†¦ c) p Î¼ â‰¤ Î´ := by
    rw [â† eLpNorm_neg, neg_sub, â† indicator_diff st]
    exact hÎ· _ Î¼s.le
  obtain âŸ¨k, k_compact, skâŸ© : âˆƒ k : Set Î±, IsCompact k âˆ§ s âŠ† interior k :=
    exists_compact_superset s_compact
  rcases exists_continuous_eLpNorm_sub_le_of_closed hp s_closed isOpen_interior sk hsÎ¼.ne c Î´pos.ne'
    with âŸ¨f, f_cont, I2, _f_bound, f_support, f_memâŸ©
  have I3 : eLpNorm (f - t.indicator fun _y â†¦ c) p Î¼ â‰¤ Îµ := by
    convert
      (hÎ´ _ _
          (f_mem.aestronglyMeasurable.sub
            (aestronglyMeasurable_const.indicator s_closed.measurableSet))
          ((aestronglyMeasurable_const.indicator s_closed.measurableSet).sub
            (aestronglyMeasurable_const.indicator ht))
          I2 I1).le using 2
    simp only [sub_add_sub_cancel]
  refine âŸ¨f, I3, f_cont, f_mem, HasCompactSupport.intro k_compact fun x hx â†¦ ?_âŸ©
  rw [â† Function.notMem_support]
  contrapose! hx
  exact interior_subset (f_support hx)

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.exists_hasCompactSupport_eLpNorm_sub_le := MemLp.exists_hasCompactSupport_eLpNorm_sub_le


/-- In a locally compact space, any function in `â„’p` can be approximated by compactly supported
continuous functions when `0 < p < âˆž`, version in terms of `âˆ«`. -/
theorem MemLp.exists_hasCompactSupport_integral_rpow_sub_le
    [R1Space Î±] [WeaklyLocallyCompactSpace Î±] [Î¼.Regular]
    {p : â„} (hp : 0 < p) {f : Î± â†’ E} (hf : MemLp f (ENNReal.ofReal p) Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ g : Î± â†’ E,
      HasCompactSupport g âˆ§
        (âˆ« x, â€–f x - g xâ€– ^ p âˆ‚Î¼) â‰¤ Îµ âˆ§ Continuous g âˆ§ MemLp g (ENNReal.ofReal p) Î¼ := by
  have I : 0 < Îµ ^ (1 / p) := Real.rpow_pos_of_pos hÎµ _
  have A : ENNReal.ofReal (Îµ ^ (1 / p)) â‰  0 := by
    simp only [Ne, ENNReal.ofReal_eq_zero, not_le, I]
  have B : ENNReal.ofReal p â‰  0 := by simpa only [Ne, ENNReal.ofReal_eq_zero, not_le] using hp
  rcases hf.exists_hasCompactSupport_eLpNorm_sub_le ENNReal.coe_ne_top A with
    âŸ¨g, g_support, hg, g_cont, g_memâŸ©
  change eLpNorm _ (ENNReal.ofReal p) _ â‰¤ _ at hg
  refine âŸ¨g, g_support, ?_, g_cont, g_memâŸ©
  rwa [(hf.sub g_mem).eLpNorm_eq_integral_rpow_norm B ENNReal.coe_ne_top,
    ENNReal.ofReal_le_ofReal_iff I.le, one_div, ENNReal.toReal_ofReal hp.le,
    Real.rpow_le_rpow_iff _ hÎµ.le (inv_pos.2 hp)] at hg
  positivity

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.exists_hasCompactSupport_integral_rpow_sub_le :=
  MemLp.exists_hasCompactSupport_integral_rpow_sub_le


/-- In a locally compact space, any integrable function can be approximated by compactly supported
continuous functions, version in terms of `âˆ«â»`. -/
theorem Integrable.exists_hasCompactSupport_lintegral_sub_le
    [R1Space Î±] [WeaklyLocallyCompactSpace Î±] [Î¼.Regular]
    {f : Î± â†’ E} (hf : Integrable f Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
    âˆƒ g : Î± â†’ E,
      HasCompactSupport g âˆ§ âˆ«â» x, â€–f x - g xâ€–â‚‘ âˆ‚Î¼ â‰¤ Îµ âˆ§ Continuous g âˆ§ Integrable g Î¼ := by
  simp only [â† memLp_one_iff_integrable, â† eLpNorm_one_eq_lintegral_enorm] at hf âŠ¢
  exact hf.exists_hasCompactSupport_eLpNorm_sub_le ENNReal.one_ne_top hÎµ

/-- In a locally compact space, any integrable function can be approximated by compactly supported
continuous functions, version in terms of `âˆ«`. -/
theorem Integrable.exists_hasCompactSupport_integral_sub_le
    [R1Space Î±] [WeaklyLocallyCompactSpace Î±] [Î¼.Regular]
    {f : Î± â†’ E} (hf : Integrable f Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ g : Î± â†’ E, HasCompactSupport g âˆ§ (âˆ« x, â€–f x - g xâ€– âˆ‚Î¼) â‰¤ Îµ âˆ§
      Continuous g âˆ§ Integrable g Î¼ := by
  simp only [â† memLp_one_iff_integrable, â† ENNReal.ofReal_one]
    at hf âŠ¢
  simpa using hf.exists_hasCompactSupport_integral_rpow_sub_le zero_lt_one hÎµ

/-- Any function in `â„’p` can be approximated by bounded continuous functions when `p < âˆž`,
version in terms of `eLpNorm`. -/
theorem MemLp.exists_boundedContinuous_eLpNorm_sub_le [Î¼.WeaklyRegular] (hp : p â‰  âˆž) {f : Î± â†’ E}
    (hf : MemLp f p Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
    âˆƒ g : Î± â†’áµ‡ E, eLpNorm (f - (g : Î± â†’ E)) p Î¼ â‰¤ Îµ âˆ§ MemLp g p Î¼ := by
  suffices H :
      âˆƒ g : Î± â†’ E, eLpNorm (f - g) p Î¼ â‰¤ Îµ âˆ§ Continuous g âˆ§ MemLp g p Î¼ âˆ§ IsBounded (range g) by
    rcases H with âŸ¨g, hg, g_cont, g_mem, g_bdâŸ©
    exact âŸ¨âŸ¨âŸ¨g, g_contâŸ©, Metric.isBounded_range_iff.1 g_bdâŸ©, hg, g_memâŸ©
  -- It suffices to check that the set of functions we consider approximates characteristic
  -- functions, is stable under addition and made of ae strongly measurable functions.
  -- First check the latter easy facts.
  apply hf.induction_dense hp _ _ _ _ hÎµ
  rotate_left
  -- stability under addition
  Â· rintro f g âŸ¨f_cont, f_mem, f_bdâŸ© âŸ¨g_cont, g_mem, g_bdâŸ©
    refine âŸ¨f_cont.add g_cont, f_mem.add g_mem, ?_âŸ©
    let f' : Î± â†’áµ‡ E := âŸ¨âŸ¨f, f_contâŸ©, Metric.isBounded_range_iff.1 f_bdâŸ©
    let g' : Î± â†’áµ‡ E := âŸ¨âŸ¨g, g_contâŸ©, Metric.isBounded_range_iff.1 g_bdâŸ©
    exact (f' + g').isBounded_range
  -- ae strong measurability
  Â· exact fun f âŸ¨_, h, _âŸ© â†¦ h.aestronglyMeasurable
  -- We are left with approximating characteristic functions.
  -- This follows from `exists_continuous_eLpNorm_sub_le_of_closed`.
  intro c t ht htÎ¼ Îµ hÎµ
  rcases exists_Lp_half E Î¼ p hÎµ with âŸ¨Î´, Î´pos, hÎ´âŸ©
  obtain âŸ¨Î·, Î·pos, hÎ·âŸ© :
      âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ âˆ€ s : Set Î±, Î¼ s â‰¤ Î· â†’ eLpNorm (s.indicator fun _x â†¦ c) p Î¼ â‰¤ Î´ :=
    exists_eLpNorm_indicator_le hp c Î´pos.ne'
  have hÎ·_pos' : (0 : â„â‰¥0âˆž) < Î· := ENNReal.coe_pos.2 Î·pos
  obtain âŸ¨s, st, s_closed, Î¼sâŸ© : âˆƒ s, s âŠ† t âˆ§ IsClosed s âˆ§ Î¼ (t \ s) < Î· :=
    ht.exists_isClosed_diff_lt htÎ¼.ne hÎ·_pos'.ne'
  have hsÎ¼ : Î¼ s < âˆž := (measure_mono st).trans_lt htÎ¼
  have I1 : eLpNorm ((s.indicator fun _y â†¦ c) - t.indicator fun _y â†¦ c) p Î¼ â‰¤ Î´ := by
    rw [â† eLpNorm_neg, neg_sub, â† indicator_diff st]
    exact hÎ· _ Î¼s.le
  rcases exists_continuous_eLpNorm_sub_le_of_closed hp s_closed isOpen_univ (subset_univ _) hsÎ¼.ne c
      Î´pos.ne' with
    âŸ¨f, f_cont, I2, f_bound, -, f_memâŸ©
  have I3 : eLpNorm (f - t.indicator fun _y â†¦ c) p Î¼ â‰¤ Îµ := by
    convert
      (hÎ´ _ _
          (f_mem.aestronglyMeasurable.sub
            (aestronglyMeasurable_const.indicator s_closed.measurableSet))
          ((aestronglyMeasurable_const.indicator s_closed.measurableSet).sub
            (aestronglyMeasurable_const.indicator ht))
          I2 I1).le using 2
    simp only [sub_add_sub_cancel]
  refine âŸ¨f, I3, f_cont, f_mem, ?_âŸ©
  exact (BoundedContinuousFunction.ofNormedAddCommGroup f f_cont _ f_bound).isBounded_range

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.exists_boundedContinuous_eLpNorm_sub_le := MemLp.exists_boundedContinuous_eLpNorm_sub_le

/-- Any function in `â„’p` can be approximated by bounded continuous functions when `0 < p < âˆž`,
version in terms of `âˆ«`. -/
theorem MemLp.exists_boundedContinuous_integral_rpow_sub_le [Î¼.WeaklyRegular] {p : â„} (hp : 0 < p)
    {f : Î± â†’ E} (hf : MemLp f (ENNReal.ofReal p) Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ g : Î± â†’áµ‡ E, (âˆ« x, â€–f x - g xâ€– ^ p âˆ‚Î¼) â‰¤ Îµ âˆ§ MemLp g (ENNReal.ofReal p) Î¼ := by
  have I : 0 < Îµ ^ (1 / p) := Real.rpow_pos_of_pos hÎµ _
  have A : ENNReal.ofReal (Îµ ^ (1 / p)) â‰  0 := by
    simp only [Ne, ENNReal.ofReal_eq_zero, not_le, I]
  have B : ENNReal.ofReal p â‰  0 := by simpa only [Ne, ENNReal.ofReal_eq_zero, not_le] using hp
  rcases hf.exists_boundedContinuous_eLpNorm_sub_le ENNReal.coe_ne_top A with âŸ¨g, hg, g_memâŸ©
  change eLpNorm _ (ENNReal.ofReal p) _ â‰¤ _ at hg
  refine âŸ¨g, ?_, g_memâŸ©
  rwa [(hf.sub g_mem).eLpNorm_eq_integral_rpow_norm B ENNReal.coe_ne_top,
    ENNReal.ofReal_le_ofReal_iff I.le, one_div, ENNReal.toReal_ofReal hp.le,
    Real.rpow_le_rpow_iff _ hÎµ.le (inv_pos.2 hp)] at hg
  positivity

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.exists_boundedContinuous_integral_rpow_sub_le :=
  MemLp.exists_boundedContinuous_integral_rpow_sub_le

/-- Any integrable function can be approximated by bounded continuous functions,
version in terms of `âˆ«â»`. -/
theorem Integrable.exists_boundedContinuous_lintegral_sub_le [Î¼.WeaklyRegular] {f : Î± â†’ E}
    (hf : Integrable f Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
    âˆƒ g : Î± â†’áµ‡ E, âˆ«â» x, â€–f x - g xâ€–â‚‘ âˆ‚Î¼ â‰¤ Îµ âˆ§ Integrable g Î¼ := by
  simp only [â† memLp_one_iff_integrable, â† eLpNorm_one_eq_lintegral_enorm] at hf âŠ¢
  exact hf.exists_boundedContinuous_eLpNorm_sub_le ENNReal.one_ne_top hÎµ

/-- Any integrable function can be approximated by bounded continuous functions,
version in terms of `âˆ«`. -/
theorem Integrable.exists_boundedContinuous_integral_sub_le [Î¼.WeaklyRegular] {f : Î± â†’ E}
    (hf : Integrable f Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ g : Î± â†’áµ‡ E, (âˆ« x, â€–f x - g xâ€– âˆ‚Î¼) â‰¤ Îµ âˆ§ Integrable g Î¼ := by
  simp only [â† memLp_one_iff_integrable, â† ENNReal.ofReal_one]
    at hf âŠ¢
  simpa using hf.exists_boundedContinuous_integral_rpow_sub_le zero_lt_one hÎµ

namespace Lp

variable (E Î¼)

/-- A function in `Lp` can be approximated in `Lp` by continuous functions. -/
theorem boundedContinuousFunction_dense [SecondCountableTopologyEither Î± E] [Fact (1 â‰¤ p)]
    (hp : p â‰  âˆž) [Î¼.WeaklyRegular] :
    Dense (boundedContinuousFunction E p Î¼ : Set (Lp E p Î¼)) := by
  intro f
  refine (mem_closure_iff_nhds_basis EMetric.nhds_basis_closed_eball).2 fun Îµ hÎµ â†¦ ?_
  obtain âŸ¨g, hg, g_memâŸ© :
      âˆƒ g : Î± â†’áµ‡ E, eLpNorm ((f : Î± â†’ E) - (g : Î± â†’ E)) p Î¼ â‰¤ Îµ âˆ§ MemLp g p Î¼ :=
    (Lp.memLp f).exists_boundedContinuous_eLpNorm_sub_le hp hÎµ.ne'
  refine âŸ¨g_mem.toLp _, âŸ¨g, rflâŸ©, ?_âŸ©
  rwa [EMetric.mem_closedBall', â† Lp.toLp_coeFn f (Lp.memLp f), Lp.edist_toLp_toLp]

/-- A function in `Lp` can be approximated in `Lp` by continuous functions. -/
theorem boundedContinuousFunction_topologicalClosure [SecondCountableTopologyEither Î± E]
    [Fact (1 â‰¤ p)] (hp : p â‰  âˆž) [Î¼.WeaklyRegular] :
    (boundedContinuousFunction E p Î¼).topologicalClosure = âŠ¤ :=
  SetLike.ext' <| (boundedContinuousFunction_dense E Î¼ hp).closure_eq

end Lp

end MeasureTheory

variable [SecondCountableTopologyEither Î± E] [_i : Fact (1 â‰¤ p)]
variable (ð•œ : Type*) [NormedRing ð•œ] [Module ð•œ E] [IsBoundedSMul ð•œ E] [NormedSpace â„ E]

variable (E) (Î¼)

namespace BoundedContinuousFunction

theorem toLp_denseRange [Î¼.WeaklyRegular] [IsFiniteMeasure Î¼] (hp : p â‰  âˆž) :
    DenseRange (toLp p Î¼ ð•œ : (Î± â†’áµ‡ E) â†’L[ð•œ] Lp E p Î¼) := by
  simpa only [â† range_toLp p Î¼ (ð•œ := ð•œ)]
    using MeasureTheory.Lp.boundedContinuousFunction_dense E Î¼ hp

end BoundedContinuousFunction

namespace ContinuousMap

/-- Continuous functions are dense in `MeasureTheory.Lp`, `1 â‰¤ p < âˆž`. This theorem assumes that
the domain is a compact space because otherwise `ContinuousMap.toLp` is undefined. Use
`BoundedContinuousFunction.toLp_denseRange` if the domain is not a compact space. -/
theorem toLp_denseRange [CompactSpace Î±] [Î¼.WeaklyRegular] [IsFiniteMeasure Î¼] (hp : p â‰  âˆž) :
    DenseRange (toLp p Î¼ ð•œ : C(Î±, E) â†’L[ð•œ] Lp E p Î¼) := by
  refine (BoundedContinuousFunction.toLp_denseRange _ _ ð•œ hp).mono ?_
  refine range_subset_iff.2 fun f â†¦ ?_
  exact âŸ¨f.toContinuousMap, rflâŸ©

end ContinuousMap
