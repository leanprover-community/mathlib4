/-
Copyright (c) 2025 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.MeasureTheory.Function.Jacobian

/-!
# Change of variable formulas for integrals in dimension 1

We record in this file versions of the general change of variables formula in integrals for
functions from `R` to `â„`. This makes it possible to replace the determinant of the FrÃ©chet
derivative with the one-dimensional derivative.

We also give more specific versions of these theorems for monotone and antitone functions: this
makes it possible to drop the injectivity assumption of the general theorems, as the derivative
is zero on the set of non-injectivity, which means that it can be discarded.

See also `Mathlib/MeasureTheory/Integral/IntervalIntegral/IntegrationByParts.lean` for versions of
the change of variables formula in dimension 1 for non-monotone functions, formulated with
the interval integral and with stronger requirements on the integrand.
-/


open MeasureTheory MeasureTheory.Measure Metric Filter Set Module Asymptotics
  TopologicalSpace ContinuousLinearMap

open scoped NNReal ENNReal Topology Pointwise

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] {s : Set â„} {f f' : â„ â†’ â„}
  {g : â„ â†’ F}

namespace MeasureTheory

/-- Integrability in the change of variable formula for differentiable functions (one-variable
version): if a function `f` is injective and differentiable on a measurable set `s âŠ† â„`, then the
Lebesgue integral of a function `g : â„ â†’ â„â‰¥0âˆ` on `f '' s` coincides with the Lebesgue integral
of `|(f' x)| * g âˆ˜ f` on `s`. -/
theorem lintegral_image_eq_lintegral_abs_deriv_mul
    (hs : MeasurableSet s) (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : InjOn f s)
    (g : â„ â†’ â„â‰¥0âˆ) :
    âˆ«â» x in f '' s, g x = âˆ«â» x in s, ENNReal.ofReal (|f' x|) * g (f x) := by
  simpa only [det_one_smulRight] using
    lintegral_image_eq_lintegral_abs_det_fderiv_mul volume hs
      (fun x hx => (hf' x hx).hasFDerivWithinAt) hf g

/-- Integrability in the change of variable formula for differentiable functions (one-variable
version): if a function `f` is injective and differentiable on a measurable set `s âŠ† â„`, then a
function `g : â„ â†’ F` is integrable on `f '' s` if and only if `|(f' x)| â€¢ g âˆ˜ f` is integrable on
`s`. -/
theorem integrableOn_image_iff_integrableOn_abs_deriv_smul
    (hs : MeasurableSet s) (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : InjOn f s)
    (g : â„ â†’ F) : IntegrableOn g (f '' s) â†” IntegrableOn (fun x => |f' x| â€¢ g (f x)) s := by
  simpa only [det_one_smulRight] using
    integrableOn_image_iff_integrableOn_abs_det_fderiv_smul volume hs
      (fun x hx => (hf' x hx).hasFDerivWithinAt) hf g

/-- Change of variable formula for differentiable functions (one-variable version): if a function
`f` is injective and differentiable on a measurable set `s âŠ† â„`, then the Bochner integral of a
function `g : â„ â†’ F` on `f '' s` coincides with the integral of `|(f' x)| â€¢ g âˆ˜ f` on `s`. -/
theorem integral_image_eq_integral_abs_deriv_smul
    (hs : MeasurableSet s) (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x)
    (hf : InjOn f s) (g : â„ â†’ F) : âˆ« x in f '' s, g x = âˆ« x in s, |f' x| â€¢ g (f x) := by
  simpa only [det_one_smulRight] using
    integral_image_eq_integral_abs_det_fderiv_smul volume hs
      (fun x hx => (hf' x hx).hasFDerivWithinAt) hf g

/-- Technical structure theorem for monotone differentiable functions.

If a function `f` is monotone on a measurable set and has a derivative `f'`, one can decompose
the set as a disjoint union `a âˆª b âˆª c` of measurable sets where `a` is countable (the points which
are isolated on the left or on the right, where `f'` is not well controlled),
`f` is locally constant on `b` and `f' = 0` there (the preimages of the countably many points with
several preimages), and `f` is injective on `c` with nonnegative derivative (the other points). -/
theorem exists_decomposition_of_monotoneOn_hasDerivWithinAt (hs : MeasurableSet s)
    (hf : MonotoneOn f s) (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) :
    âˆƒ (a b c : Set â„), a âˆª (b âˆª c) = s âˆ§ MeasurableSet a âˆ§ MeasurableSet b âˆ§ MeasurableSet c âˆ§
    Disjoint a (b âˆª c) âˆ§ Disjoint b c âˆ§ a.Countable âˆ§ (f '' b).Countable âˆ§
    (âˆ€ x âˆˆ b, f' x = 0) âˆ§ (âˆ€ x âˆˆ c, 0 â‰¤ f' x) âˆ§ InjOn f c := by
  let a := {x âˆˆ s | ğ“[s âˆ© Ioi x] x = âŠ¥} âˆª {x âˆˆ s | ğ“[s âˆ© Iio x] x = âŠ¥}
  have a_count : a.Countable :=
    countable_setOf_isolated_right_within.union countable_setOf_isolated_left_within
  let sâ‚ := s \ a
  have hsâ‚ : MeasurableSet sâ‚ := hs.diff a_count.measurableSet
  let u : Set â„ := {c | âˆƒ x y, x âˆˆ sâ‚ âˆ§ y âˆˆ sâ‚ âˆ§ x < y âˆ§ f x = c âˆ§ f y = c}
  have hu : Set.Countable u := MonotoneOn.countable_setOf_two_preimages (hf.mono diff_subset)
  let b := sâ‚ âˆ© f â»Â¹' u
  have hb : MeasurableSet b := by
    have : b = â‹ƒ z âˆˆ u, sâ‚ âˆ© fâ»Â¹' {z} := by ext; simp [b]
    rw [this]
    apply MeasurableSet.biUnion hu (fun z hz â†¦ ?_)
    obtain âŸ¨v, hv, tvâŸ© : âˆƒ v, OrdConnected v âˆ§ (s \ a) âˆ© f â»Â¹' {z} = (s \ a) âˆ© v :=
      ordConnected_singleton.preimage_monotoneOn (hf.mono diff_subset)
    exact tv â–¸ (hs.diff a_count.measurableSet).inter hv.measurableSet
  let c := sâ‚ \ b
  have hc : MeasurableSet c := hsâ‚.diff hb
  refine âŸ¨a, b, c, ?_, a_count.measurableSet, hb, hc, ?_, ?_, a_count, ?_, ?_, ?_, ?_âŸ©
  Â· ext x
    simp only [diff_self_inter, inter_union_diff, union_diff_self, mem_union, mem_setOf_eq,
      or_iff_right_iff_imp, a, b, sâ‚, c]
    tauto
  Â· simpa [b, c, sâ‚] using disjoint_sdiff_right
  Â· simpa [c] using disjoint_sdiff_right
  Â· exact hu.mono (by simp [b])
  Â· /- We have to show that the derivative is `0` at `x âˆˆ b`. For that, we use that there is another
    point `p` with `f p = f x`, by definition of `b`. If `p < x`, then `f` is locally constant to
    the left of `x`. As `x` is not isolated to its left (since we are not in the set `a`), it
    follows that `f' x = 0`. The same argument works if `x < p`, using the right neighborhood
    instead. -/
    intro x hx
    obtain âŸ¨p, psâ‚, px, fpxâŸ© : âˆƒ p âˆˆ sâ‚, p â‰  x âˆ§ f p = f x := by
      rcases hx.2 with âŸ¨p, q, psâ‚, qsâ‚, pq, hp, hqâŸ©
      rcases eq_or_ne p x with h'p | h'p
      Â· exact âŸ¨q, qsâ‚, (h'p.symm.le.trans_lt pq).ne', hqâŸ©
      Â· exact âŸ¨p, psâ‚, h'p, hpâŸ©
    -- we treat separately the cases `p < x` and `x < p` as we couldn't unify their proofs nicely
    rcases lt_or_gt_of_ne px with px | px
    Â· have K : HasDerivWithinAt f 0 (s âˆ© Ioo p x) x := by
        have E (y) (hy : y âˆˆ s âˆ© Ioo p x) : f y = f x := by
          apply le_antisymm (hf hy.1 hx.1.1 hy.2.2.le)
          rw [â† fpx]
          exact hf psâ‚.1 hy.1 hy.2.1.le
        have : HasDerivWithinAt (fun y â†¦ f x) 0 (s âˆ© Ioo p x) x :=
          hasDerivWithinAt_const x (s âˆ© Ioo p x) (f x)
        exact this.congr E rfl
      have K' : HasDerivWithinAt f (f' x) (s âˆ© Ioo p x) x :=
        (hf' x hx.1.1).mono inter_subset_left
      apply UniqueDiffWithinAt.eq_deriv _ _ K' K
      have J1 : (s âˆ© Ioo p x) \ {x} = s âˆ© Ioo p x := by simp
      have J2 : ğ“[s âˆ© Ioo p x] x = ğ“[s âˆ© Iio x] x := by
        simp [nhdsWithin_inter, nhdsWithin_Ioo_eq_nhdsLT px]
      rw [uniqueDiffWithinAt_iff_accPt, accPt_principal_iff_nhdsWithin, J1, J2]
      simp only [mem_inter_iff, mem_diff, hx.1.1, mem_union, mem_setOf_eq, true_and, not_or,
        mem_preimage, b, sâ‚, a] at hx
      exact neBot_iff.2 hx.1.2
    Â· have K : HasDerivWithinAt f 0 (s âˆ© Ioo x p) x := by
        have E (y) (hy : y âˆˆ s âˆ© Ioo x p) : f y = f x := by
          apply le_antisymm  _ (hf hx.1.1 hy.1 hy.2.1.le)
          rw [â† fpx]
          exact hf hy.1 psâ‚.1 hy.2.2.le
        have : HasDerivWithinAt (fun y â†¦ f x) 0 (s âˆ© Ioo x p) x :=
          hasDerivWithinAt_const x (s âˆ© Ioo x p) (f x)
        exact this.congr E rfl
      have K' : HasDerivWithinAt f (f' x) (s âˆ© Ioo x p) x :=
        (hf' x hx.1.1).mono inter_subset_left
      apply UniqueDiffWithinAt.eq_deriv _ _ K' K
      have J1 : (s âˆ© Ioo x p) \ {x} = (s âˆ© Ioo x p) := by simp
      have J2 : ğ“[s âˆ© Ioo x p] x = ğ“[s âˆ© Ioi x] x := by
        simp [nhdsWithin_inter, nhdsWithin_Ioo_eq_nhdsGT px]
      rw [uniqueDiffWithinAt_iff_accPt, accPt_principal_iff_nhdsWithin, J1, J2]
      simp only [mem_inter_iff, mem_diff, hx.1.1, mem_union, mem_setOf_eq, true_and, not_or,
        mem_preimage, b, sâ‚, a] at hx
      exact neBot_iff.2 hx.1.1
  Â· /- We have to show that the derivative is nonnegative at points of `c`. As these points are
    not isolated in `s`, this follows from the fact that `f` is monotone on `s`. -/
    intro x hx
    apply (hf' x hx.1.1).nonneg_of_monotoneOn _ hf
    simp only [mem_diff, hx.1.1, mem_union, mem_setOf_eq, true_and, not_or, c, sâ‚, a, b] at hx
    rw [accPt_principal_iff_nhdsWithin]
    have : (ğ“[s âˆ© Iio x] x).NeBot := neBot_iff.2 hx.1.2
    apply this.mono
    apply nhdsWithin_mono
    rintro y âŸ¨yt, yx : y < xâŸ©
    exact âŸ¨yt, by simpa using yx.neâŸ©
  Â· intro x hx y hy hxy
    contrapose! hxy
    wlog H : x < y generalizing x y with h
    Â· have : y < x := by order
      exact (h hy hx hxy.symm this).symm
    refine fun h â†¦ hx.2 âŸ¨hx.1, ?_âŸ©
    exact âŸ¨x, y, hx.1, hy.1, H, rfl, h.symmâŸ©

/- Change of variable formula for differentiable functions: if a real function `f` is
monotone and differentiable on a measurable set `s`, then the Lebesgue integral of a function
`u : â„ â†’ â„â‰¥0âˆ` on `f '' s` coincides with the integral of `(f' x) * u âˆ˜ f` on `s`.
Note that the measurability of `f '' s` is given by `MeasurableSet.image_of_monotoneOn`. -/
theorem lintegral_image_eq_lintegral_deriv_mul_of_monotoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : MonotoneOn f s) (u : â„ â†’ â„â‰¥0âˆ) :
    âˆ«â» x in f '' s, u x = âˆ«â» x in s, ENNReal.ofReal (f' x) * u (f x) := by
  rcases exists_decomposition_of_monotoneOn_hasDerivWithinAt hs hf hf' with
    âŸ¨a, b, c, h_union, ha, hb, hc, h_disj, h_disj', a_count, fb_count, deriv_b, deriv_c, inj_câŸ©
  have I : âˆ«â» x in s, ENNReal.ofReal (f' x) * u (f x)
      = âˆ«â» x in c, ENNReal.ofReal (f' x) * u (f x) := by
    have : âˆ«â» x in a, ENNReal.ofReal (f' x) * u (f x) = 0 :=
      setLIntegral_measure_zero a _ (a_count.measure_zero volume)
    rw [â† h_union, lintegral_union (hb.union hc) h_disj, this, zero_add]
    have : âˆ«â» x in b, ENNReal.ofReal (f' x) * u (f x) = 0 :=
      setLIntegral_eq_zero hb (fun x hx â†¦ by simp [deriv_b x hx])
    rw [lintegral_union hc h_disj', this, zero_add]
  have J : âˆ«â» x in f '' s, u x = âˆ«â» x in f '' c, u x := by
    apply setLIntegral_congr
    rw [â† h_union, image_union, image_union]
    have A : (f '' a âˆª (f '' b âˆª f '' c) : Set â„) =áµ[volume] (f '' b âˆª f '' c : Set â„) := by
      refine union_ae_eq_right_of_ae_eq_empty (ae_eq_empty.mpr ?_)
      exact (a_count.image _).measure_zero _
    have B : (f '' b âˆª f '' c : Set â„) =áµ[volume] f '' c :=
      union_ae_eq_right_of_ae_eq_empty (ae_eq_empty.mpr (fb_count.measure_zero _))
    exact A.trans B
  rw [I, J]
  have c_s : c âŠ† s := by rw [â† h_union]; exact subset_union_right.trans subset_union_right
  let F' : â„ â†’ (â„ â†’L[â„] â„) := fun x â†¦ ContinuousLinearMap.smulRight (1 : â„ â†’L[â„] â„) (f' x)
  have hf' (x : â„) (hx : x âˆˆ c) : HasFDerivWithinAt f (F' x) c x :=
    (hf' x (c_s hx)).hasFDerivWithinAt.mono c_s
  have : âˆ«â» x in c, ENNReal.ofReal (f' x) * u (f x)
      = âˆ«â» x in c, ENNReal.ofReal (|(F' x).det|) * u (f x) := by
    apply setLIntegral_congr_fun hc (fun x hx â†¦ ?_)
    simp only [LinearMap.det_ring, ContinuousLinearMap.coe_coe, ContinuousLinearMap.smulRight_apply,
      ContinuousLinearMap.one_apply, smul_eq_mul, one_mul, F']
    rw [abs_of_nonneg (deriv_c x hx)]
  rw [this]
  exact lintegral_image_eq_lintegral_abs_det_fderiv_mul _ hc hf' inj_c _

/-- Change of variable formula for differentiable functions, set version: if a real function `f` is
monotone and differentiable on a measurable set `s`, then the measure of `f '' s` is given by the
integral of `f' x` on `s` .
Note that the measurability of `f '' s` is given by `MeasurableSet.image_of_monotoneOn`. -/
theorem lintegral_deriv_eq_volume_image_of_monotoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : MonotoneOn f s) :
    (âˆ«â» x in s, ENNReal.ofReal (f' x)) = volume (f '' s) := by
  simpa using (lintegral_image_eq_lintegral_deriv_mul_of_monotoneOn hs hf' hf 1).symm

/-- Integrability in the change of variable formula for differentiable functions: if a real
function `f` is monotone and differentiable on a measurable set `s`, then a function
`g : â„ â†’ F` is integrable on `f '' s` if and only if `f' x â€¢ g âˆ˜ f` is integrable on `s` . -/
theorem integrableOn_image_iff_integrableOn_deriv_smul_of_monotoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : MonotoneOn f s) (g : â„ â†’ F) :
    IntegrableOn g (f '' s) â†” IntegrableOn (fun x â†¦ (f' x) â€¢ g (f x)) s := by
  rcases exists_decomposition_of_monotoneOn_hasDerivWithinAt hs hf hf' with
    âŸ¨a, b, c, h_union, ha, hb, hc, h_disj, h_disj', a_count, fb_count, deriv_b, deriv_c, inj_câŸ©
  have I : IntegrableOn (fun x => (f' x) â€¢ g (f x)) s
      â†” IntegrableOn (fun x => (f' x) â€¢ g (f x)) c := by
    have A : IntegrableOn (fun x â†¦ f' x â€¢ g (f x)) a :=
      IntegrableOn.of_measure_zero (a_count.measure_zero volume)
    have B : IntegrableOn (fun x â†¦ f' x â€¢ g (f x)) b := by
      have : IntegrableOn (fun x â†¦ (0 : F)) b := by simp
      exact this.congr_fun (fun x hx â†¦ by simp [deriv_b x hx]) hb
    simp only [â† h_union, integrableOn_union, A, B, true_and]
  have J : IntegrableOn g (f '' s) â†” IntegrableOn g (f '' c) := by
    apply integrableOn_congr_set_ae
    rw [â† h_union, image_union, image_union]
    have A : (f '' a âˆª (f '' b âˆª f '' c) : Set â„) =áµ[volume] (f '' b âˆª f '' c : Set â„) := by
      refine union_ae_eq_right_of_ae_eq_empty (ae_eq_empty.mpr ?_)
      exact (a_count.image _).measure_zero _
    have B : (f '' b âˆª f '' c : Set â„) =áµ[volume] f '' c :=
      union_ae_eq_right_of_ae_eq_empty (ae_eq_empty.mpr (fb_count.measure_zero _))
    exact A.trans B
  rw [I, J]
  have c_s : c âŠ† s := by rw [â† h_union]; exact subset_union_right.trans subset_union_right
  let F' : â„ â†’ (â„ â†’L[â„] â„) := fun x â†¦ ContinuousLinearMap.smulRight (1 : â„ â†’L[â„] â„) (f' x)
  have hF' (x : â„) (hx : x âˆˆ c) : HasFDerivWithinAt f (F' x) c x :=
    (hf' x (c_s hx)).hasFDerivWithinAt.mono c_s
  rw [integrableOn_image_iff_integrableOn_abs_det_fderiv_smul _ hc hF' inj_c]
  apply integrableOn_congr_fun (fun x hx â†¦ ?_) hc
  simp only [LinearMap.det_ring, ContinuousLinearMap.coe_coe, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.one_apply, smul_eq_mul, one_mul, F']
  rw [abs_of_nonneg (deriv_c x hx)]

/-- Change of variable formula for differentiable functions: if a real function `f` is
monotone and differentiable on a measurable set `s`, then the Bochner integral of a function
`g : â„ â†’ F` on `f '' s` coincides with the integral of `(f' x) â€¢ g âˆ˜ f` on `s` . -/
theorem integral_image_eq_integral_deriv_smul_of_monotoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : MonotoneOn f s) (g : â„ â†’ F) :
    âˆ« x in f '' s, g x = âˆ« x in s, f' x â€¢ g (f x) := by
  by_cases H : IntegrableOn g (f '' s); swap
  Â· rw [integral_undef H, integral_undef]
    simpa [integrableOn_image_iff_integrableOn_deriv_smul_of_monotoneOn hs hf' hf] using H
  have H' : IntegrableOn (fun x â†¦ (f' x) â€¢ g (f x)) s :=
    (integrableOn_image_iff_integrableOn_deriv_smul_of_monotoneOn hs hf' hf g).1 H
  rcases exists_decomposition_of_monotoneOn_hasDerivWithinAt hs hf hf' with
    âŸ¨a, b, c, h_union, ha, hb, hc, h_disj, h_disj', a_count, fb_count, deriv_b, deriv_c, inj_câŸ©
  have a_s : a âŠ† s := by rw [â† h_union]; exact subset_union_left
  have bc_s : b âˆª c âŠ† s := by rw [â† h_union]; exact subset_union_right
  have b_s : b âŠ† s := by rw [â† h_union]; exact subset_union_left.trans subset_union_right
  have c_s : c âŠ† s := by rw [â† h_union]; exact subset_union_right.trans subset_union_right
  have I : âˆ« x in s, f' x â€¢ g (f x) = âˆ« x in c, f' x â€¢ g (f x) := by
    have : âˆ« x in a, f' x â€¢ g (f x) = 0 :=
      setIntegral_measure_zero _ (a_count.measure_zero volume)
    rw [â† h_union, setIntegral_union h_disj (hb.union hc) (H'.mono_set a_s) (H'.mono_set bc_s),
      this, zero_add]
    have : âˆ« x in b, f' x â€¢ g (f x) = 0 :=
      setIntegral_eq_zero_of_forall_eq_zero (fun x hx â†¦ by simp [deriv_b x hx])
    rw [setIntegral_union h_disj' hc (H'.mono_set b_s) (H'.mono_set c_s), this, zero_add]
  have J : âˆ« x in f '' s, g x = âˆ« x in f '' c, g x := by
    apply setIntegral_congr_set
    rw [â† h_union, image_union, image_union]
    have A : (f '' a âˆª (f '' b âˆª f '' c) : Set â„) =áµ[volume] (f '' b âˆª f '' c : Set â„) := by
      refine union_ae_eq_right_of_ae_eq_empty (ae_eq_empty.mpr ?_)
      exact (a_count.image _).measure_zero _
    have B : (f '' b âˆª f '' c : Set â„) =áµ[volume] f '' c :=
      union_ae_eq_right_of_ae_eq_empty (ae_eq_empty.mpr (fb_count.measure_zero _))
    exact A.trans B
  rw [I, J]
  let F' : â„ â†’ (â„ â†’L[â„] â„) := fun x â†¦ ContinuousLinearMap.smulRight (1 : â„ â†’L[â„] â„) (f' x)
  have hF' (x : â„) (hx : x âˆˆ c) : HasFDerivWithinAt f (F' x) c x :=
    (hf' x (c_s hx)).hasFDerivWithinAt.mono c_s
  have : âˆ« x in c, f' x â€¢ g (f x) = âˆ« x in c, |(F' x).det| â€¢ g (f x) := by
    apply setIntegral_congr_fun hc (fun x hx â†¦ ?_)
    simp only [LinearMap.det_ring, ContinuousLinearMap.coe_coe, ContinuousLinearMap.smulRight_apply,
      ContinuousLinearMap.one_apply, smul_eq_mul, one_mul, F']
    rw [abs_of_nonneg (deriv_c x hx)]
  rw [this]
  exact integral_image_eq_integral_abs_det_fderiv_smul _ hc hF' inj_c _

/- Change of variable formula for differentiable functions: if a real function `f` is
antitone and differentiable on a measurable set `s`, then the Lebesgue integral of a function
`u : â„ â†’ â„â‰¥0âˆ` on `f '' s` coincides with the integral of `(-f' x) * u âˆ˜ f` on `s`.
Note that the measurability of `f '' s` is given by `MeasurableSet.image_of_antitoneOn`. -/
theorem lintegral_image_eq_lintegral_deriv_mul_of_antitoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : AntitoneOn f s) (u : â„ â†’ â„â‰¥0âˆ) :
    âˆ«â» x in f '' s, u x = âˆ«â» x in s, ENNReal.ofReal (-f' x) * u (f x) := by
  let n : â„ â†’ â„ := (fun x â†¦ - x)
  let e := n âˆ˜ f
  have hg' (x) (hx : x âˆˆ s) : HasDerivWithinAt e (-f' x) s x := (hf' x hx).neg
  have A : âˆ«â» x in e '' s, u (n x) = âˆ«â» x in s, ENNReal.ofReal (-f' x) * (u âˆ˜ n) (e x) := by
    rw [â† lintegral_image_eq_lintegral_deriv_mul_of_monotoneOn hs hg' hf.neg (u âˆ˜ n)]; rfl
  have B : âˆ«â» x in n '' (e '' s), u x = âˆ«â» x in e '' s, ENNReal.ofReal (|-1|) * u (n x) :=
    lintegral_image_eq_lintegral_abs_deriv_mul (hs.image_of_monotoneOn hf.neg)
      (fun x hx â†¦ hasDerivWithinAt_neg _ _) neg_injective.injOn _
  simp only [abs_neg, abs_one, ENNReal.ofReal_one, one_mul] at B
  rw [A, â† image_comp] at B
  convert B using 4 with x hx x <;> simp [n, e]

/-- Change of variable formula for differentiable functions, set version: if a real function `f` is
antitone and differentiable on a measurable set `s`, then the measure of `f '' s` is given by the
integral of `-f' x` on `s` .
Note that the measurability of `f '' s` is given by `MeasurableSet.image_of_antitoneOn`. -/
theorem lintegral_deriv_eq_volume_image_of_antitoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : AntitoneOn f s) :
    (âˆ«â» x in s, ENNReal.ofReal (-f' x)) = volume (f '' s) := by
  simpa using (lintegral_image_eq_lintegral_deriv_mul_of_antitoneOn hs hf' hf 1).symm

/-- Integrability in the change of variable formula for differentiable functions: if a real
function `f` is antitone and differentiable on a measurable set `s`, then a function
`g : â„ â†’ F` is integrable on `f '' s` if and only if `-f' x â€¢ g âˆ˜ f` is integrable on `s` . -/
theorem integrableOn_image_iff_integrableOn_deriv_smul_of_antitoneOn (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : AntitoneOn f s) (g : â„ â†’ F) :
    IntegrableOn g (f '' s) â†” IntegrableOn (fun x â†¦ (-f' x) â€¢ g (f x)) s := by
  let n : â„ â†’ â„ := (fun x â†¦ - x)
  let e := n âˆ˜ f
  have hg' (x) (hx : x âˆˆ s) : HasDerivWithinAt e (-f' x) s x := (hf' x hx).neg
  have A : IntegrableOn (fun x â†¦ g (n x)) (e '' s)
      â†” IntegrableOn (fun x â†¦ (-f' x) â€¢ (g âˆ˜ n) (e x)) s := by
    rw [â† integrableOn_image_iff_integrableOn_deriv_smul_of_monotoneOn hs hg' hf.neg (g âˆ˜ n)]; rfl
  have B : IntegrableOn g (n '' (e '' s)) â†” IntegrableOn (fun x â†¦ (|-1| : â„) â€¢ g (n x)) (e '' s) :=
    integrableOn_image_iff_integrableOn_abs_deriv_smul (hs.image_of_monotoneOn hf.neg)
      (fun x hx â†¦ hasDerivWithinAt_neg _ _) neg_injective.injOn _
  simp only [abs_neg, abs_one, one_smul] at B
  rw [A, â† image_comp] at B
  convert B using 3 with x hx x <;> simp [n, e]

/-- Change of variable formula for differentiable functions: if a real function `f` is
antitone and differentiable on a measurable set `s`, then the Bochner integral of a function
`g : â„ â†’ F` on `f '' s` coincides with the integral of `(-f' x) â€¢ g âˆ˜ f` on `s` . -/
theorem integral_image_eq_integral_deriv_smul_of_antitone (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (hf : AntitoneOn f s) (g : â„ â†’ F) :
    âˆ« x in f '' s, g x = âˆ« x in s, (-f' x) â€¢ g (f x) := by
  let n : â„ â†’ â„ := (fun x â†¦ - x)
  let e := n âˆ˜ f
  have hg' (x) (hx : x âˆˆ s) : HasDerivWithinAt e (-f' x) s x := (hf' x hx).neg
  have A : âˆ« x in e '' s, g (n x) = âˆ« x in s, (-f' x) â€¢ (g âˆ˜ n) (e x) := by
    rw [â† integral_image_eq_integral_deriv_smul_of_monotoneOn hs hg' hf.neg (g âˆ˜ n)]; rfl
  have B : âˆ« x in n '' (e '' s), g x = âˆ« x in e '' s, (|-1| : â„) â€¢ g (n x) :=
    integral_image_eq_integral_abs_deriv_smul (hs.image_of_monotoneOn hf.neg)
      (fun x hx â†¦ hasDerivWithinAt_neg _ _) neg_injective.injOn _
  simp only [abs_neg, abs_one, one_smul] at B
  rw [A, â† image_comp] at B
  convert B using 3 with x hx x <;> simp [n, e]

section WithDensity

lemma _root_.MeasurableEmbedding.withDensity_ofReal_comap_apply_eq_integral_abs_deriv_mul
    {f : â„ â†’ â„} (hf : MeasurableEmbedding f) {s : Set â„} (hs : MeasurableSet s)
    {g : â„ â†’ â„} (hg : âˆ€áµ x, x âˆˆ f '' s â†’ 0 â‰¤ g x) (hf_int : IntegrableOn g (f '' s))
    {f' : â„ â†’ â„} (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) :
    (volume.withDensity (fun x â†¦ ENNReal.ofReal (g x))).comap f s
      = ENNReal.ofReal (âˆ« x in s, |f' x| * g (f x)) := by
  rw [hf.withDensity_ofReal_comap_apply_eq_integral_abs_det_fderiv_mul volume hs
    hg hf_int hf']
  simp only [det_one_smulRight]

lemma _root_.MeasurableEquiv.withDensity_ofReal_map_symm_apply_eq_integral_abs_deriv_mul
    (f : â„ â‰ƒáµ â„) {s : Set â„} (hs : MeasurableSet s)
    {g : â„ â†’ â„} (hg : âˆ€áµ x, x âˆˆ f '' s â†’ 0 â‰¤ g x) (hf_int : IntegrableOn g (f '' s))
    {f' : â„ â†’ â„} (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) :
    (volume.withDensity (fun x â†¦ ENNReal.ofReal (g x))).map f.symm s
      = ENNReal.ofReal (âˆ« x in s, |f' x| * g (f x)) := by
  rw [MeasurableEquiv.withDensity_ofReal_map_symm_apply_eq_integral_abs_det_fderiv_mul volume hs
      f hg hf_int hf']
  simp only [det_one_smulRight]

lemma _root_.MeasurableEmbedding.withDensity_ofReal_comap_apply_eq_integral_abs_deriv_mul'
    {f : â„ â†’ â„} (hf : MeasurableEmbedding f) {s : Set â„} (hs : MeasurableSet s)
    {f' : â„ â†’ â„} (hf' : âˆ€ x, HasDerivAt f (f' x) x)
    {g : â„ â†’ â„} (hg : 0 â‰¤áµ[volume] g) (hg_int : Integrable g) :
    (volume.withDensity (fun x â†¦ ENNReal.ofReal (g x))).comap f s
      = ENNReal.ofReal (âˆ« x in s, |f' x| * g (f x)) :=
  hf.withDensity_ofReal_comap_apply_eq_integral_abs_deriv_mul hs
    (by filter_upwards [hg] with x hx using fun _ â†¦ hx) hg_int.integrableOn
    (fun x _ => (hf' x).hasDerivWithinAt)

lemma _root_.MeasurableEquiv.withDensity_ofReal_map_symm_apply_eq_integral_abs_deriv_mul'
    (f : â„ â‰ƒáµ â„) {s : Set â„} (hs : MeasurableSet s)
    {f' : â„ â†’ â„} (hf' : âˆ€ x, HasDerivAt f (f' x) x)
    {g : â„ â†’ â„} (hg : 0 â‰¤áµ[volume] g) (hg_int : Integrable g) :
    (volume.withDensity (fun x â†¦ ENNReal.ofReal (g x))).map f.symm s
      = ENNReal.ofReal (âˆ« x in s, |f' x| * g (f x)) := by
  rw [MeasurableEquiv.withDensity_ofReal_map_symm_apply_eq_integral_abs_det_fderiv_mul volume hs
      f (by filter_upwards [hg] with x hx using fun _ â†¦ hx) hg_int.integrableOn
      (fun x _ => (hf' x).hasDerivWithinAt)]
  simp only [det_one_smulRight]

end WithDensity

end MeasureTheory
