/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.MeasureTheory.Function.SimpleFunc
import Mathlib.MeasureTheory.Constructions.BorelSpace.Metrizable

/-!
# Density of simple functions

Show that each Borel measurable function can be approximated pointwise
by a sequence of simple functions.

## Main definitions

* `MeasureTheory.SimpleFunc.nearestPt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› â„•`: the `SimpleFunc` sending
  each `x : Î±` to the point `e k` which is the nearest to `x` among `e 0`, ..., `e N`.
* `MeasureTheory.SimpleFunc.approxOn (f : Î² â†’ Î±) (hf : Measurable f) (s : Set Î±) (yâ‚€ : Î±)
  (hâ‚€ : yâ‚€ âˆˆ s) [SeparableSpace s] (n : â„•) : Î² â†’â‚› Î±` : a simple function that takes values in `s`
  and approximates `f`.

## Main results

* `tendsto_approxOn` (pointwise convergence): If `f x âˆˆ s`, then the sequence of simple
  approximations `MeasureTheory.SimpleFunc.approxOn f hf s yâ‚€ hâ‚€ n`, evaluated at `x`,
  tends to `f x` as `n` tends to `âˆ`.

## Notations

* `Î± â†’â‚› Î²` (local notation): the type of simple functions `Î± â†’ Î²`.
-/

open Set Function Filter TopologicalSpace EMetric MeasureTheory
open scoped Topology ENNReal

variable {Î± Î² : Type*}

noncomputable section

namespace MeasureTheory

local infixr:25 " â†’â‚› " => SimpleFunc

namespace SimpleFunc

/-! ### Pointwise approximation by simple functions -/


variable [MeasurableSpace Î±] [PseudoEMetricSpace Î±] [OpensMeasurableSpace Î±]

/-- `nearestPtInd e N x` is the index `k` such that `e k` is the nearest point to `x` among the
points `e 0`, ..., `e N`. If more than one point are at the same distance from `x`, then
`nearestPtInd e N x` returns the least of their indexes. -/
noncomputable def nearestPtInd (e : â„• â†’ Î±) : â„• â†’ Î± â†’â‚› â„•
  | 0 => const Î± 0
  | N + 1 =>
    piecewise (â‹‚ k â‰¤ N, { x | edist (e (N + 1)) x < edist (e k) x })
      (MeasurableSet.iInter fun _ =>
        MeasurableSet.iInter fun _ =>
          measurableSet_lt measurable_edist_right measurable_edist_right)
      (const Î± <| N + 1) (nearestPtInd e N)

/-- `nearestPt e N x` is the nearest point to `x` among the points `e 0`, ..., `e N`. If more than
one point are at the same distance from `x`, then `nearestPt e N x` returns the point with the
least possible index. -/
noncomputable def nearestPt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› Î± :=
  (nearestPtInd e N).map e

@[simp]
theorem nearestPtInd_zero (e : â„• â†’ Î±) : nearestPtInd e 0 = const Î± 0 :=
  rfl

@[simp]
theorem nearestPt_zero (e : â„• â†’ Î±) : nearestPt e 0 = const Î± (e 0) :=
  rfl

theorem nearestPtInd_succ (e : â„• â†’ Î±) (N : â„•) (x : Î±) :
    nearestPtInd e (N + 1) x =
      if âˆ€ k â‰¤ N, edist (e (N + 1)) x < edist (e k) x then N + 1 else nearestPtInd e N x := by
  simp only [nearestPtInd, coe_piecewise, Set.piecewise]
  congr
  simp

theorem nearestPtInd_le (e : â„• â†’ Î±) (N : â„•) (x : Î±) : nearestPtInd e N x â‰¤ N := by
  induction N with
  | zero => simp
  | succ N ihN =>
    simp only [nearestPtInd_succ]
    split_ifs
    exacts [le_rfl, ihN.trans N.le_succ]

theorem edist_nearestPt_le (e : â„• â†’ Î±) (x : Î±) {k N : â„•} (hk : k â‰¤ N) :
    edist (nearestPt e N x) x â‰¤ edist (e k) x := by
  induction N generalizing k with
  | zero => simp [nonpos_iff_eq_zero.1 hk, le_refl]
  | succ N ihN =>
    simp only [nearestPt, nearestPtInd_succ, map_apply]
    split_ifs with h
    Â· rcases hk.eq_or_lt with (rfl | hk)
      exacts [le_rfl, (h k (Nat.lt_succ_iff.1 hk)).le]
    Â· push_neg at h
      rcases h with âŸ¨l, hlN, hxlâŸ©
      rcases hk.eq_or_lt with (rfl | hk)
      exacts [(ihN hlN).trans hxl, ihN (Nat.lt_succ_iff.1 hk)]

theorem tendsto_nearestPt {e : â„• â†’ Î±} {x : Î±} (hx : x âˆˆ closure (range e)) :
    Tendsto (fun N => nearestPt e N x) atTop (ğ“ x) := by
  refine (atTop_basis.tendsto_iff nhds_basis_eball).2 fun Îµ hÎµ => ?_
  rcases EMetric.mem_closure_iff.1 hx Îµ hÎµ with âŸ¨_, âŸ¨N, rflâŸ©, hNâŸ©
  rw [edist_comm] at hN
  exact âŸ¨N, trivial, fun n hn => (edist_nearestPt_le e x hn).trans_lt hNâŸ©

variable [MeasurableSpace Î²] {f : Î² â†’ Î±}

/-- Approximate a measurable function by a sequence of simple functions `F n` such that
`F n x âˆˆ s`. -/
noncomputable def approxOn (f : Î² â†’ Î±) (hf : Measurable f) (s : Set Î±) (yâ‚€ : Î±) (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] (n : â„•) : Î² â†’â‚› Î± :=
  haveI : Nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©
  comp (nearestPt (fun k => Nat.casesOn k yâ‚€ ((â†‘) âˆ˜ denseSeq s) : â„• â†’ Î±) n) f hf

@[simp]
theorem approxOn_zero {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] (x : Î²) : approxOn f hf s yâ‚€ hâ‚€ 0 x = yâ‚€ :=
  rfl

theorem approxOn_mem {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] (n : â„•) (x : Î²) : approxOn f hf s yâ‚€ hâ‚€ n x âˆˆ s := by
  haveI : Nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©
  suffices âˆ€ n, (Nat.casesOn n yâ‚€ ((â†‘) âˆ˜ denseSeq s) : Î±) âˆˆ s by apply this
  rintro (_ | n)
  exacts [hâ‚€, Subtype.mem _]

lemma approxOn_range_nonneg [Zero Î±] [Preorder Î±] {f : Î² â†’ Î±}
    (hf : 0 â‰¤ f) {hfm : Measurable f} [SeparableSpace (range f âˆª {0} : Set Î±)] (n : â„•) :
    0 â‰¤ approxOn f hfm (range f âˆª {0}) 0 (by simp) n := by
  have : range f âˆª {0} âŠ† Set.Ici 0 := by
    simp only [Set.union_singleton, Set.insert_subset_iff, Set.mem_Ici, le_refl, true_and]
    rintro - âŸ¨x, rflâŸ©
    exact hf x
  exact fun _ â†¦ this <| approxOn_mem ..

@[simp]
theorem approxOn_comp {Î³ : Type*} [MeasurableSpace Î³] {f : Î² â†’ Î±} (hf : Measurable f) {g : Î³ â†’ Î²}
    (hg : Measurable g) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s) [SeparableSpace s] (n : â„•) :
    approxOn (f âˆ˜ g) (hf.comp hg) s yâ‚€ hâ‚€ n = (approxOn f hf s yâ‚€ hâ‚€ n).comp g hg :=
  rfl

theorem tendsto_approxOn {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] {x : Î²} (hx : f x âˆˆ closure s) :
    Tendsto (fun n => approxOn f hf s yâ‚€ hâ‚€ n x) atTop (ğ“ <| f x) := by
  haveI : Nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©
  rw [â† @Subtype.range_coe _ s, â† image_univ, â† (denseRange_denseSeq s).closure_eq] at hx
  simp -iota only [approxOn, coe_comp]
  refine tendsto_nearestPt (closure_minimal ?_ isClosed_closure hx)
  simp -iota only [Nat.range_casesOn, closure_union, range_comp]
  exact
    Subset.trans (image_closure_subset_closure_image continuous_subtype_val)
      subset_union_right

theorem edist_approxOn_mono {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] (x : Î²) {m n : â„•} (h : m â‰¤ n) :
    edist (approxOn f hf s yâ‚€ hâ‚€ n x) (f x) â‰¤ edist (approxOn f hf s yâ‚€ hâ‚€ m x) (f x) := by
  dsimp only [approxOn, coe_comp, Function.comp_def]
  exact edist_nearestPt_le _ _ ((nearestPtInd_le _ _ _).trans h)

theorem edist_approxOn_le {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] (x : Î²) (n : â„•) : edist (approxOn f hf s yâ‚€ hâ‚€ n x) (f x) â‰¤ edist yâ‚€ (f x) :=
  edist_approxOn_mono hf hâ‚€ x (zero_le n)

theorem edist_approxOn_y0_le {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
    [SeparableSpace s] (x : Î²) (n : â„•) :
    edist yâ‚€ (approxOn f hf s yâ‚€ hâ‚€ n x) â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) :=
  calc
    edist yâ‚€ (approxOn f hf s yâ‚€ hâ‚€ n x) â‰¤
        edist yâ‚€ (f x) + edist (approxOn f hf s yâ‚€ hâ‚€ n x) (f x) :=
      edist_triangle_right _ _ _
    _ â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) := add_le_add_left (edist_approxOn_le hf hâ‚€ x n) _

end SimpleFunc

end MeasureTheory

section CompactSupport

variable {X Y Î± : Type*} [Zero Î±]
    [TopologicalSpace X] [TopologicalSpace Y] [MeasurableSpace X] [MeasurableSpace Y]
    [OpensMeasurableSpace X] [OpensMeasurableSpace Y]

/-- A continuous function with compact support on a product space can be uniformly approximated by
simple functions. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma HasCompactSupport.exists_simpleFunc_approx_of_prod [PseudoMetricSpace Î±]
    {f : X Ã— Y â†’ Î±} (hf : Continuous f) (h'f : HasCompactSupport f)
    {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ (g : SimpleFunc (X Ã— Y) Î±), âˆ€ x, dist (f x) (g x) < Îµ := by
  have M : âˆ€ (K : Set (X Ã— Y)), IsCompact K â†’
      âˆƒ (g : SimpleFunc (X Ã— Y) Î±), âˆƒ (s : Set (X Ã— Y)), MeasurableSet s âˆ§ K âŠ† s âˆ§
      âˆ€ x âˆˆ s, dist (f x) (g x) < Îµ := by
    intro K hK
    apply IsCompact.induction_on
      (p := fun t â†¦ âˆƒ (g : SimpleFunc (X Ã— Y) Î±), âˆƒ (s : Set (X Ã— Y)), MeasurableSet s âˆ§ t âŠ† s âˆ§
        âˆ€ x âˆˆ s, dist (f x) (g x) < Îµ) hK
    Â· exact âŸ¨0, âˆ…, by simpâŸ©
    Â· intro t t' htt' âŸ¨g, s, s_meas, ts, hgâŸ©
      exact âŸ¨g, s, s_meas, htt'.trans ts, hgâŸ©
    Â· intro t t' âŸ¨g, s, s_meas, ts, hgâŸ© âŸ¨g', s', s'_meas, t's', hg'âŸ©
      refine âŸ¨g.piecewise s s_meas g', s âˆª s', s_meas.union s'_meas,
        union_subset_union ts t's', fun p hp â†¦ ?_âŸ©
      by_cases H : p âˆˆ s
      Â· simpa [H, SimpleFunc.piecewise_apply] using hg p H
      Â· simp only [SimpleFunc.piecewise_apply, H, ite_false]
        apply hg'
        simpa [H] using (mem_union _ _ _).1 hp
    Â· rintro âŸ¨x, yâŸ© -
      obtain âŸ¨u, v, hu, xu, hv, yv, huvâŸ© : âˆƒ u v, IsOpen u âˆ§ x âˆˆ u âˆ§ IsOpen v âˆ§ y âˆˆ v âˆ§
        u Ã—Ë¢ v âŠ† {z | dist (f z) (f (x, y)) < Îµ} :=
          mem_nhds_prod_iff'.1 <| Metric.continuousAt_iff'.1 hf.continuousAt Îµ hÎµ
      refine âŸ¨u Ã—Ë¢ v, nhdsWithin_le_nhds <| (hu.prod hv).mem_nhds (mk_mem_prod xu yv), ?_âŸ©
      exact âŸ¨SimpleFunc.const _ (f (x, y)), u Ã—Ë¢ v, hu.measurableSet.prod hv.measurableSet,
        Subset.rfl, fun z hz â†¦ huv hzâŸ©
  obtain âŸ¨g, s, s_meas, fs, hgâŸ© : âˆƒ (g : SimpleFunc (X Ã— Y) Î±) (s : Set (X Ã— Y)),
    MeasurableSet s âˆ§ tsupport f âŠ† s âˆ§ âˆ€ (x : X Ã— Y), x âˆˆ s â†’ dist (f x) (g x) < Îµ := M _ h'f
  refine âŸ¨g.piecewise s s_meas 0, fun p â†¦ ?_âŸ©
  by_cases H : p âˆˆ s
  Â· simpa [H, SimpleFunc.piecewise_apply] using hg p H
  Â· have : f p = 0 := by
      contrapose! H
      rw [â† Function.mem_support] at H
      exact fs (subset_tsupport _ H)
    simp [SimpleFunc.piecewise_apply, H, ite_false, this, hÎµ]

/-- A continuous function with compact support on a product space is measurable for the product
sigma-algebra. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma HasCompactSupport.measurable_of_prod
    [TopologicalSpace Î±] [PseudoMetrizableSpace Î±] [MeasurableSpace Î±] [BorelSpace Î±]
    {f : X Ã— Y â†’ Î±} (hf : Continuous f) (h'f : HasCompactSupport f) :
    Measurable f := by
  letI : PseudoMetricSpace Î± := TopologicalSpace.pseudoMetrizableSpacePseudoMetric Î±
  obtain âŸ¨u, -, u_pos, u_limâŸ© : âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  have : âˆ€ n, âˆƒ (g : SimpleFunc (X Ã— Y) Î±), âˆ€ x, dist (f x) (g x) < u n :=
    fun n â†¦ h'f.exists_simpleFunc_approx_of_prod hf (u_pos n)
  choose g hg using this
  have A : âˆ€ x, Tendsto (fun n â†¦ g n x) atTop (ğ“ (f x)) := by
    intro x
    rw [tendsto_iff_dist_tendsto_zero]
    apply squeeze_zero (fun n â†¦ dist_nonneg) (fun n â†¦ ?_) u_lim
    rw [dist_comm]
    exact (hg n x).le
  apply measurable_of_tendsto_metrizable (fun n â†¦ (g n).measurable) (tendsto_pi_nhds.2 A)

end CompactSupport
