/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.MeasureTheory.Function.SimpleFunc
import Mathlib.MeasureTheory.Constructions.BorelSpace.Metrizable

/-!
# Density of simple functions

Show that each Borel measurable function can be approximated pointwise
by a sequence of simple functions.

## Main definitions

* `MeasureTheory.SimpleFunc.nearestPt (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) : Œ± ‚Üí‚Çõ ‚Ñï`: the `SimpleFunc` sending
  each `x : Œ±` to the point `e k` which is the nearest to `x` among `e 0`, ..., `e N`.
* `MeasureTheory.SimpleFunc.approxOn (f : Œ≤ ‚Üí Œ±) (hf : Measurable f) (s : Set Œ±) (y‚ÇÄ : Œ±)
  (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s] (n : ‚Ñï) : Œ≤ ‚Üí‚Çõ Œ±` : a simple function that takes values in `s`
  and approximates `f`.

## Main results

* `tendsto_approxOn` (pointwise convergence): If `f x ‚àà s`, then the sequence of simple
  approximations `MeasureTheory.SimpleFunc.approxOn f hf s y‚ÇÄ h‚ÇÄ n`, evaluated at `x`,
  tends to `f x` as `n` tends to `‚àû`.

## Notations

* `Œ± ‚Üí‚Çõ Œ≤` (local notation): the type of simple functions `Œ± ‚Üí Œ≤`.
-/


open Set Function Filter TopologicalSpace ENNReal EMetric Finset

open scoped Classical
open Topology ENNReal MeasureTheory

variable {Œ± Œ≤ Œπ E F ùïú : Type*}

noncomputable section

namespace MeasureTheory

local infixr:25 " ‚Üí‚Çõ " => SimpleFunc

namespace SimpleFunc

/-! ### Pointwise approximation by simple functions -/


variable [MeasurableSpace Œ±] [PseudoEMetricSpace Œ±] [OpensMeasurableSpace Œ±]

/-- `nearestPtInd e N x` is the index `k` such that `e k` is the nearest point to `x` among the
points `e 0`, ..., `e N`. If more than one point are at the same distance from `x`, then
`nearestPtInd e N x` returns the least of their indexes. -/
noncomputable def nearestPtInd (e : ‚Ñï ‚Üí Œ±) : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ ‚Ñï
  | 0 => const Œ± 0
  | N + 1 =>
    piecewise (‚ãÇ k ‚â§ N, { x | edist (e (N + 1)) x < edist (e k) x })
      (MeasurableSet.iInter fun _ =>
        MeasurableSet.iInter fun _ =>
          measurableSet_lt measurable_edist_right measurable_edist_right)
      (const Œ± <| N + 1) (nearestPtInd e N)

/-- `nearestPt e N x` is the nearest point to `x` among the points `e 0`, ..., `e N`. If more than
one point are at the same distance from `x`, then `nearestPt e N x` returns the point with the
least possible index. -/
noncomputable def nearestPt (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) : Œ± ‚Üí‚Çõ Œ± :=
  (nearestPtInd e N).map e

@[simp]
theorem nearestPtInd_zero (e : ‚Ñï ‚Üí Œ±) : nearestPtInd e 0 = const Œ± 0 :=
  rfl

@[simp]
theorem nearestPt_zero (e : ‚Ñï ‚Üí Œ±) : nearestPt e 0 = const Œ± (e 0) :=
  rfl

theorem nearestPtInd_succ (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) (x : Œ±) :
    nearestPtInd e (N + 1) x =
      if ‚àÄ k ‚â§ N, edist (e (N + 1)) x < edist (e k) x then N + 1 else nearestPtInd e N x := by
  simp only [nearestPtInd, coe_piecewise, Set.piecewise]
  congr
  simp

theorem nearestPtInd_le (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) (x : Œ±) : nearestPtInd e N x ‚â§ N := by
  induction' N with N ihN; ¬∑ simp
  simp only [nearestPtInd_succ]
  split_ifs
  exacts [le_rfl, ihN.trans N.le_succ]

theorem edist_nearestPt_le (e : ‚Ñï ‚Üí Œ±) (x : Œ±) {k N : ‚Ñï} (hk : k ‚â§ N) :
    edist (nearestPt e N x) x ‚â§ edist (e k) x := by
  induction' N with N ihN generalizing k
  ¬∑ simp [nonpos_iff_eq_zero.1 hk, le_refl]
  ¬∑ simp only [nearestPt, nearestPtInd_succ, map_apply]
    split_ifs with h
    ¬∑ rcases hk.eq_or_lt with (rfl | hk)
      exacts [le_rfl, (h k (Nat.lt_succ_iff.1 hk)).le]
    ¬∑ push_neg at h
      rcases h with ‚ü®l, hlN, hxl‚ü©
      rcases hk.eq_or_lt with (rfl | hk)
      exacts [(ihN hlN).trans hxl, ihN (Nat.lt_succ_iff.1 hk)]

theorem tendsto_nearestPt {e : ‚Ñï ‚Üí Œ±} {x : Œ±} (hx : x ‚àà closure (range e)) :
    Tendsto (fun N => nearestPt e N x) atTop (ùìù x) := by
  refine (atTop_basis.tendsto_iff nhds_basis_eball).2 fun Œµ hŒµ => ?_
  rcases EMetric.mem_closure_iff.1 hx Œµ hŒµ with ‚ü®_, ‚ü®N, rfl‚ü©, hN‚ü©
  rw [edist_comm] at hN
  exact ‚ü®N, trivial, fun n hn => (edist_nearestPt_le e x hn).trans_lt hN‚ü©

variable [MeasurableSpace Œ≤] {f : Œ≤ ‚Üí Œ±}

/-- Approximate a measurable function by a sequence of simple functions `F n` such that
`F n x ‚àà s`. -/
noncomputable def approxOn (f : Œ≤ ‚Üí Œ±) (hf : Measurable f) (s : Set Œ±) (y‚ÇÄ : Œ±) (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] (n : ‚Ñï) : Œ≤ ‚Üí‚Çõ Œ± :=
  haveI : Nonempty s := ‚ü®‚ü®y‚ÇÄ, h‚ÇÄ‚ü©‚ü©
  comp (nearestPt (fun k ‚Ü¶ Nat.casesOn k y‚ÇÄ ((‚Üë) ‚àò denseSeq s) : ‚Ñï ‚Üí Œ±) n) f hf

@[simp]
theorem approxOn_zero {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] (x : Œ≤) : approxOn f hf s y‚ÇÄ h‚ÇÄ 0 x = y‚ÇÄ :=
  rfl

theorem approxOn_mem {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] (n : ‚Ñï) (x : Œ≤) : approxOn f hf s y‚ÇÄ h‚ÇÄ n x ‚àà s := by
  haveI : Nonempty s := ‚ü®‚ü®y‚ÇÄ, h‚ÇÄ‚ü©‚ü©
  suffices ‚àÄ n, (Nat.casesOn n y‚ÇÄ ((‚Üë) ‚àò denseSeq s) : Œ±) ‚àà s by apply this
  rintro (_ | n)
  exacts [h‚ÇÄ, Subtype.mem _]

@[simp, nolint simpNF] -- Porting note: LHS doesn't simplify.
theorem approxOn_comp {Œ≥ : Type*} [MeasurableSpace Œ≥] {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {g : Œ≥ ‚Üí Œ≤}
    (hg : Measurable g) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s) [SeparableSpace s] (n : ‚Ñï) :
    approxOn (f ‚àò g) (hf.comp hg) s y‚ÇÄ h‚ÇÄ n = (approxOn f hf s y‚ÇÄ h‚ÇÄ n).comp g hg :=
  rfl

theorem tendsto_approxOn {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] {x : Œ≤} (hx : f x ‚àà closure s) :
    Tendsto (fun n ‚Ü¶ approxOn f hf s y‚ÇÄ h‚ÇÄ n x) atTop (ùìù <| f x) := by
  haveI : Nonempty s := ‚ü®‚ü®y‚ÇÄ, h‚ÇÄ‚ü©‚ü©
  rw [‚Üê @Subtype.range_coe _ s, ‚Üê image_univ, ‚Üê (denseRange_denseSeq s).closure_eq] at hx
  simp (config := { iota := false }) only [approxOn, coe_comp]
  refine tendsto_nearestPt (closure_minimal ?_ isClosed_closure hx)
  simp (config := { iota := false }) only [Nat.range_casesOn, closure_union, range_comp]
  exact
    Subset.trans (image_closure_subset_closure_image continuous_subtype_val)
      subset_union_right

theorem edist_approxOn_mono {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] (x : Œ≤) {m n : ‚Ñï} (h : m ‚â§ n) :
    edist (approxOn f hf s y‚ÇÄ h‚ÇÄ n x) (f x) ‚â§ edist (approxOn f hf s y‚ÇÄ h‚ÇÄ m x) (f x) := by
  dsimp only [approxOn, coe_comp, Function.comp_def]
  exact edist_nearestPt_le _ _ ((nearestPtInd_le _ _ _).trans h)

theorem edist_approxOn_le {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] (x : Œ≤) (n : ‚Ñï) : edist (approxOn f hf s y‚ÇÄ h‚ÇÄ n x) (f x) ‚â§ edist y‚ÇÄ (f x) :=
  edist_approxOn_mono hf h‚ÇÄ x (zero_le n)

theorem edist_approxOn_y0_le {f : Œ≤ ‚Üí Œ±} (hf : Measurable f) {s : Set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
    [SeparableSpace s] (x : Œ≤) (n : ‚Ñï) :
    edist y‚ÇÄ (approxOn f hf s y‚ÇÄ h‚ÇÄ n x) ‚â§ edist y‚ÇÄ (f x) + edist y‚ÇÄ (f x) :=
  calc
    edist y‚ÇÄ (approxOn f hf s y‚ÇÄ h‚ÇÄ n x) ‚â§
        edist y‚ÇÄ (f x) + edist (approxOn f hf s y‚ÇÄ h‚ÇÄ n x) (f x) :=
      edist_triangle_right _ _ _
    _ ‚â§ edist y‚ÇÄ (f x) + edist y‚ÇÄ (f x) := add_le_add_left (edist_approxOn_le hf h‚ÇÄ x n) _

end SimpleFunc

end MeasureTheory

section CompactSupport

variable {X Y Œ± : Type*} [Zero Œ±]
    [TopologicalSpace X] [TopologicalSpace Y] [MeasurableSpace X] [MeasurableSpace Y]
    [OpensMeasurableSpace X] [OpensMeasurableSpace Y]

/-- A continuous function with compact support on a product space can be uniformly approximated by
simple functions. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma HasCompactSupport.exists_simpleFunc_approx_of_prod [PseudoMetricSpace Œ±]
    {f : X √ó Y ‚Üí Œ±} (hf : Continuous f) (h'f : HasCompactSupport f)
    {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ (g : SimpleFunc (X √ó Y) Œ±), ‚àÄ x, dist (f x) (g x) < Œµ := by
  have M : ‚àÄ (K : Set (X √ó Y)), IsCompact K ‚Üí
      ‚àÉ (g : SimpleFunc (X √ó Y) Œ±), ‚àÉ (s : Set (X √ó Y)), MeasurableSet s ‚àß K ‚äÜ s ‚àß
      ‚àÄ x ‚àà s, dist (f x) (g x) < Œµ := by
    intro K hK
    apply IsCompact.induction_on
      (p := fun t ‚Ü¶ ‚àÉ (g : SimpleFunc (X √ó Y) Œ±), ‚àÉ (s : Set (X √ó Y)), MeasurableSet s ‚àß t ‚äÜ s ‚àß
        ‚àÄ x ‚àà s, dist (f x) (g x) < Œµ) hK
    ¬∑ exact ‚ü®0, ‚àÖ, by simp‚ü©
    ¬∑ intro t t' htt' ‚ü®g, s, s_meas, ts, hg‚ü©
      exact ‚ü®g, s, s_meas, htt'.trans ts, hg‚ü©
    ¬∑ intro t t' ‚ü®g, s, s_meas, ts, hg‚ü© ‚ü®g', s', s'_meas, t's', hg'‚ü©
      refine ‚ü®g.piecewise s s_meas g', s ‚à™ s', s_meas.union s'_meas,
        union_subset_union ts t's', fun p hp ‚Ü¶ ?_‚ü©
      by_cases H : p ‚àà s
      ¬∑ simpa [H, SimpleFunc.piecewise_apply] using hg p H
      ¬∑ simp only [SimpleFunc.piecewise_apply, H, ite_false]
        apply hg'
        simpa [H] using (mem_union _ _ _).1 hp
    ¬∑ rintro ‚ü®x, y‚ü© -
      obtain ‚ü®u, v, hu, xu, hv, yv, huv‚ü© : ‚àÉ u v, IsOpen u ‚àß x ‚àà u ‚àß IsOpen v ‚àß y ‚àà v ‚àß
        u √óÀ¢ v ‚äÜ {z | dist (f z) (f (x, y)) < Œµ} :=
          mem_nhds_prod_iff'.1 <| Metric.continuousAt_iff'.1 hf.continuousAt Œµ hŒµ
      refine ‚ü®u √óÀ¢ v, nhdsWithin_le_nhds <| (hu.prod hv).mem_nhds (mk_mem_prod xu yv), ?_‚ü©
      exact ‚ü®SimpleFunc.const _ (f (x, y)), u √óÀ¢ v, hu.measurableSet.prod hv.measurableSet,
        Subset.rfl, fun z hz ‚Ü¶ huv hz‚ü©
  obtain ‚ü®g, s, s_meas, fs, hg‚ü© : ‚àÉ (g : SimpleFunc (X √ó Y) Œ±) (s : Set (X √ó Y)),
    MeasurableSet s ‚àß tsupport f ‚äÜ s ‚àß ‚àÄ (x : X √ó Y), x ‚àà s ‚Üí dist (f x) (g x) < Œµ := M _ h'f
  refine ‚ü®g.piecewise s s_meas 0, fun p ‚Ü¶ ?_‚ü©
  by_cases H : p ‚àà s
  ¬∑ simpa [H, SimpleFunc.piecewise_apply] using hg p H
  ¬∑ have : f p = 0 := by
      contrapose! H
      rw [‚Üê Function.mem_support] at H
      exact fs (subset_tsupport _ H)
    simp [SimpleFunc.piecewise_apply, H, ite_false, this, hŒµ]

/-- A continuous function with compact support on a product space is measurable for the product
sigma-algebra. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma HasCompactSupport.measurable_of_prod
    [TopologicalSpace Œ±] [PseudoMetrizableSpace Œ±] [MeasurableSpace Œ±] [BorelSpace Œ±]
    {f : X √ó Y ‚Üí Œ±} (hf : Continuous f) (h'f : HasCompactSupport f) :
    Measurable f := by
  letI : PseudoMetricSpace Œ± := TopologicalSpace.pseudoMetrizableSpacePseudoMetric Œ±
  obtain ‚ü®u, -, u_pos, u_lim‚ü© : ‚àÉ u, StrictAnti u ‚àß (‚àÄ (n : ‚Ñï), 0 < u n) ‚àß Tendsto u atTop (ùìù 0) :=
    exists_seq_strictAnti_tendsto (0 : ‚Ñù)
  have : ‚àÄ n, ‚àÉ (g : SimpleFunc (X √ó Y) Œ±), ‚àÄ x, dist (f x) (g x) < u n :=
    fun n ‚Ü¶ h'f.exists_simpleFunc_approx_of_prod hf (u_pos n)
  choose g hg using this
  have A : ‚àÄ x, Tendsto (fun n ‚Ü¶ g n x) atTop (ùìù (f x)) := by
    intro x
    rw [tendsto_iff_dist_tendsto_zero]
    apply squeeze_zero (fun n ‚Ü¶ dist_nonneg) (fun n ‚Ü¶ ?_) u_lim
    rw [dist_comm]
    exact (hg n x).le
  apply measurable_of_tendsto_metrizable (fun n ‚Ü¶ (g n).measurable) (tendsto_pi_nhds.2 A)

end CompactSupport
