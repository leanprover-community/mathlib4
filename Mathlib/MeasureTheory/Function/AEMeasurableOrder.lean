/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic

#align_import measure_theory.function.ae_measurable_order from "leanprover-community/mathlib"@"bf6a01357ff5684b1ebcd0f1a13be314fc82c0bf"

/-!
# Measurability criterion for ennreal-valued functions

Consider a function `f : Î± â†’ â„â‰¥0âˆ`. If the level sets `{f < p}` and `{q < f}` have measurable
supersets which are disjoint up to measure zero when `p` and `q` are finite numbers satisfying
`p < q`, then `f` is almost-everywhere measurable. This is proved in
`ENNReal.aemeasurable_of_exist_almost_disjoint_supersets`, and deduced from an analogous statement
for any target space which is a complete linear dense order, called
`MeasureTheory.aemeasurable_of_exist_almost_disjoint_supersets`.

Note that it should be enough to assume that the space is a conditionally complete linear order,
but the proof would be more painful. Since our only use for now is for `â„â‰¥0âˆ`, we keep it as simple
as possible.
-/


open MeasureTheory Set TopologicalSpace

open Classical ENNReal NNReal

/-- If a function `f : Î± â†’ Î²` is such that the level sets `{f < p}` and `{q < f}` have measurable
supersets which are disjoint up to measure zero when `p < q`, then `f` is almost-everywhere
measurable. It is even enough to have this for `p` and `q` in a countable dense set. -/
theorem MeasureTheory.aemeasurable_of_exist_almost_disjoint_supersets {Î± : Type*}
    {m : MeasurableSpace Î±} (Î¼ : Measure Î±) {Î² : Type*} [CompleteLinearOrder Î²] [DenselyOrdered Î²]
    [TopologicalSpace Î²] [OrderTopology Î²] [SecondCountableTopology Î²] [MeasurableSpace Î²]
    [BorelSpace Î²] (s : Set Î²) (s_count : s.Countable) (s_dense : Dense s) (f : Î± â†’ Î²)
    (h : âˆ€ p âˆˆ s, âˆ€ q âˆˆ s, p < q â†’ âˆƒ u v, MeasurableSet u âˆ§ MeasurableSet v âˆ§
      { x | f x < p } âŠ† u âˆ§ { x | q < f x } âŠ† v âˆ§ Î¼ (u âˆ© v) = 0) :
    AEMeasurable f Î¼ := by
  haveI : Encodable s := s_count.toEncodable
  -- âŠ¢ AEMeasurable f
  have h' : âˆ€ p q, âˆƒ u v, MeasurableSet u âˆ§ MeasurableSet v âˆ§
      { x | f x < p } âŠ† u âˆ§ { x | q < f x } âŠ† v âˆ§ (p âˆˆ s â†’ q âˆˆ s â†’ p < q â†’ Î¼ (u âˆ© v) = 0) := by
    intro p q
    by_cases H : p âˆˆ s âˆ§ q âˆˆ s âˆ§ p < q
    Â· rcases h p H.1 q H.2.1 H.2.2 with âŸ¨u, v, hu, hv, h'u, h'v, hÎ¼âŸ©
      exact âŸ¨u, v, hu, hv, h'u, h'v, fun _ _ _ => hÎ¼âŸ©
    Â· refine'
        âŸ¨univ, univ, MeasurableSet.univ, MeasurableSet.univ, subset_univ _, subset_univ _,
          fun ps qs pq => _âŸ©
      simp only [not_and] at H
      exact (H ps qs pq).elim
  choose! u v huv using h'
  -- âŠ¢ AEMeasurable f
  let u' : Î² â†’ Set Î± := fun p => â‹‚ q âˆˆ s âˆ© Ioi p, u p q
  -- âŠ¢ AEMeasurable f
  have u'_meas : âˆ€ i, MeasurableSet (u' i) := by
    intro i
    exact MeasurableSet.biInter (s_count.mono (inter_subset_left _ _)) fun b _ => (huv i b).1
  let f' : Î± â†’ Î² := fun x => â¨… i : s, piecewise (u' i) (fun _ => (i : Î²)) (fun _ => (âŠ¤ : Î²)) x
  -- âŠ¢ AEMeasurable f
  have f'_meas : Measurable f' := by
    apply measurable_iInf
    exact fun i => Measurable.piecewise (u'_meas i) measurable_const measurable_const
  let t := â‹ƒ (p : s) (q : â†¥(s âˆ© Ioi p)), u' p âˆ© v p q
  -- âŠ¢ AEMeasurable f
  have Î¼t : Î¼ t â‰¤ 0 :=
    calc
      Î¼ t â‰¤ âˆ‘' (p : s) (q : â†¥(s âˆ© Ioi p)), Î¼ (u' p âˆ© v p q) := by
        refine (measure_iUnion_le _).trans ?_
        refine ENNReal.tsum_le_tsum fun p => ?_
        refine @measure_iUnion_le _ _ _ _ ?_ _
        exact (s_count.mono (inter_subset_left _ _)).to_subtype
      _ â‰¤ âˆ‘' (p : s) (q : â†¥(s âˆ© Ioi p)), Î¼ (u p q âˆ© v p q) := by
        refine ENNReal.tsum_le_tsum fun p => ?_
        refine ENNReal.tsum_le_tsum fun q => measure_mono ?_
        exact inter_subset_inter_left _ (biInter_subset_of_mem q.2)
      _ = âˆ‘' (p : s) (_ : â†¥(s âˆ© Ioi p)), (0 : â„â‰¥0âˆ) := by
        congr
        ext1 p
        congr
        ext1 q
        exact (huv p q).2.2.2.2 p.2 q.2.1 q.2.2
      _ = 0 := by simp only [tsum_zero]
  have ff' : âˆ€áµ x âˆ‚Î¼, f x = f' x := by
    have : âˆ€áµ x âˆ‚Î¼, x âˆ‰ t := by
      have : Î¼ t = 0 := le_antisymm Î¼t bot_le
      change Î¼ _ = 0
      convert this
      ext y
      simp only [not_exists, exists_prop, mem_setOf_eq, mem_compl_iff, not_not_mem]
    filter_upwards [this]with x hx
    apply (iInf_eq_of_forall_ge_of_forall_gt_exists_lt _ _).symm
    Â· intro i
      by_cases H : x âˆˆ u' i
      swap
      Â· simp only [H, le_top, not_false_iff, piecewise_eq_of_not_mem]
      simp only [H, piecewise_eq_of_mem]
      contrapose! hx
      obtain âŸ¨r, âŸ¨xr, rqâŸ©, rsâŸ© : âˆƒ r, r âˆˆ Ioo (i : Î²) (f x) âˆ© s :=
        dense_iff_inter_open.1 s_dense (Ioo i (f x)) isOpen_Ioo (nonempty_Ioo.2 hx)
      have A : x âˆˆ v i r := (huv i r).2.2.2.1 rq
      refine mem_iUnion.2 âŸ¨i, ?_âŸ©
      refine mem_iUnion.2 âŸ¨âŸ¨r, âŸ¨rs, xrâŸ©âŸ©, ?_âŸ©
      exact âŸ¨H, AâŸ©
    Â· intro q hq
      obtain âŸ¨r, âŸ¨xr, rqâŸ©, rsâŸ© : âˆƒ r, r âˆˆ Ioo (f x) q âˆ© s :=
        dense_iff_inter_open.1 s_dense (Ioo (f x) q) isOpen_Ioo (nonempty_Ioo.2 hq)
      refine' âŸ¨âŸ¨r, rsâŸ©, _âŸ©
      have A : x âˆˆ u' r := mem_biInter fun i _ => (huv r i).2.2.1 xr
      simp only [A, rq, piecewise_eq_of_mem, Subtype.coe_mk]
  exact âŸ¨f', f'_meas, ff'âŸ©
  -- ğŸ‰ no goals
#align measure_theory.ae_measurable_of_exist_almost_disjoint_supersets MeasureTheory.aemeasurable_of_exist_almost_disjoint_supersets

/-- If a function `f : Î± â†’ â„â‰¥0âˆ` is such that the level sets `{f < p}` and `{q < f}` have measurable
supersets which are disjoint up to measure zero when `p` and `q` are finite numbers satisfying
`p < q`, then `f` is almost-everywhere measurable. -/
theorem ENNReal.aemeasurable_of_exist_almost_disjoint_supersets {Î± : Type*} {m : MeasurableSpace Î±}
    (Î¼ : Measure Î±) (f : Î± â†’ â„â‰¥0âˆ)
    (h : âˆ€ (p : â„â‰¥0) (q : â„â‰¥0), p < q â†’
      âˆƒ u v, MeasurableSet u âˆ§ MeasurableSet v âˆ§
        { x | f x < p } âŠ† u âˆ§ { x | (q : â„â‰¥0âˆ) < f x } âŠ† v âˆ§ Î¼ (u âˆ© v) = 0) :
    AEMeasurable f Î¼ := by
  obtain âŸ¨s, s_count, s_dense, _, s_topâŸ© :
    âˆƒ s : Set â„â‰¥0âˆ, s.Countable âˆ§ Dense s âˆ§ 0 âˆ‰ s âˆ§ âˆ âˆ‰ s :=
    ENNReal.exists_countable_dense_no_zero_top
  have I : âˆ€ x âˆˆ s, x â‰  âˆ := fun x xs hx => s_top (hx â–¸ xs)
  -- âŠ¢ AEMeasurable f
  apply MeasureTheory.aemeasurable_of_exist_almost_disjoint_supersets Î¼ s s_count s_dense _
  -- âŠ¢ âˆ€ (p : â„â‰¥0âˆ), p âˆˆ s â†’ âˆ€ (q : â„â‰¥0âˆ), q âˆˆ s â†’ p < q â†’ âˆƒ u v, MeasurableSet u âˆ§ â€¦
  rintro p hp q hq hpq
  -- âŠ¢ âˆƒ u v, MeasurableSet u âˆ§ MeasurableSet v âˆ§ {x | f x < p} âŠ† u âˆ§ {x | q < f x} â€¦
  lift p to â„â‰¥0 using I p hp
  -- âŠ¢ âˆƒ u v, MeasurableSet u âˆ§ MeasurableSet v âˆ§ {x | f x < â†‘p} âŠ† u âˆ§ {x | q < f x â€¦
  lift q to â„â‰¥0 using I q hq
  -- âŠ¢ âˆƒ u v, MeasurableSet u âˆ§ MeasurableSet v âˆ§ {x | f x < â†‘p} âŠ† u âˆ§ {x | â†‘q < f  â€¦
  exact h p q (ENNReal.coe_lt_coe.1 hpq)
  -- ğŸ‰ no goals
#align ennreal.ae_measurable_of_exist_almost_disjoint_supersets ENNReal.aemeasurable_of_exist_almost_disjoint_supersets
