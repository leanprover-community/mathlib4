/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.MeasureTheory.Function.LpSpace.Basic

/-!
# Order related properties of Lp spaces

## Results

- `Lp E p Œº` is an `OrderedAddCommGroup` when `E` is a `NormedLatticeAddCommGroup`.

## TODO

- move definitions of `Lp.posPart` and `Lp.negPart` to this file, and define them as
  `PosPart.pos` and `NegPart.neg` given by the lattice structure.

-/



open TopologicalSpace MeasureTheory
open scoped ENNReal

variable {Œ± E : Type*} {m : MeasurableSpace Œ±} {Œº : Measure Œ±} {p : ‚Ñù‚â•0‚àû}

namespace MeasureTheory

namespace Lp

section Order

variable [NormedLatticeAddCommGroup E]

theorem coeFn_le (f g : Lp E p Œº) : f ‚â§·µê[Œº] g ‚Üî f ‚â§ g := by
  rw [‚Üê Subtype.coe_le_coe, ‚Üê AEEqFun.coeFn_le]

theorem coeFn_nonneg (f : Lp E p Œº) : 0 ‚â§·µê[Œº] f ‚Üî 0 ‚â§ f := by
  rw [‚Üê coeFn_le]
  have h0 := Lp.coeFn_zero E p Œº
  constructor <;> intro h <;> filter_upwards [h, h0] with _ _ h2
  ¬∑ rwa [h2]
  ¬∑ rwa [‚Üê h2]

instance instAddLeftMono : AddLeftMono (Lp E p Œº) := by
  refine ‚ü®fun f g‚ÇÅ g‚ÇÇ hg‚ÇÅ‚ÇÇ => ?_‚ü©
  rw [‚Üê coeFn_le] at hg‚ÇÅ‚ÇÇ ‚ä¢
  filter_upwards [coeFn_add f g‚ÇÅ, coeFn_add f g‚ÇÇ, hg‚ÇÅ‚ÇÇ] with _ h1 h2 h3
  rw [h1, h2, Pi.add_apply, Pi.add_apply]
  exact add_le_add le_rfl h3

instance instOrderedAddCommGroup : OrderedAddCommGroup (Lp E p Œº) :=
  { Subtype.partialOrder _, AddSubgroup.toAddCommGroup _ with
    add_le_add_left := fun _ _ => add_le_add_left }

theorem _root_.MeasureTheory.MemLp.sup {f g : Œ± ‚Üí E} (hf : MemLp f p Œº) (hg : MemLp g p Œº) :
    MemLp (f ‚äî g) p Œº :=
  MemLp.mono' (hf.norm.add hg.norm) (hf.1.sup hg.1)
    (Filter.Eventually.of_forall fun x => norm_sup_le_add (f x) (g x))

@[deprecated (since := "2025-02-21")]
alias _root_.MeasureTheory.Memùìõp.sup := _root_.MeasureTheory.MemLp.sup

theorem _root_.MeasureTheory.MemLp.inf {f g : Œ± ‚Üí E} (hf : MemLp f p Œº) (hg : MemLp g p Œº) :
    MemLp (f ‚äì g) p Œº :=
  MemLp.mono' (hf.norm.add hg.norm) (hf.1.inf hg.1)
    (Filter.Eventually.of_forall fun x => norm_inf_le_add (f x) (g x))

@[deprecated (since := "2025-02-21")]
alias _root_.MeasureTheory.Memùìõp.inf := _root_.MeasureTheory.MemLp.inf

theorem _root_.MeasureTheory.MemLp.abs {f : Œ± ‚Üí E} (hf : MemLp f p Œº) : MemLp |f| p Œº :=
  hf.sup hf.neg

@[deprecated (since := "2025-02-21")]
alias _root_.MeasureTheory.Memùìõp.abs := _root_.MeasureTheory.MemLp.abs

instance instLattice : Lattice (Lp E p Œº) :=
  Subtype.lattice
    (fun f g hf hg => by
      rw [mem_Lp_iff_memLp] at *
      exact (memLp_congr_ae (AEEqFun.coeFn_sup _ _)).mpr (hf.sup hg))
    fun f g hf hg => by
    rw [mem_Lp_iff_memLp] at *
    exact (memLp_congr_ae (AEEqFun.coeFn_inf _ _)).mpr (hf.inf hg)

theorem coeFn_sup (f g : Lp E p Œº) : ‚áë(f ‚äî g) =·µê[Œº] ‚áëf ‚äî ‚áëg :=
  AEEqFun.coeFn_sup _ _

theorem coeFn_inf (f g : Lp E p Œº) : ‚áë(f ‚äì g) =·µê[Œº] ‚áëf ‚äì ‚áëg :=
  AEEqFun.coeFn_inf _ _

theorem coeFn_abs (f : Lp E p Œº) : ‚áë|f| =·µê[Œº] fun x => |f x| :=
  AEEqFun.coeFn_abs _

noncomputable instance instNormedLatticeAddCommGroup [Fact (1 ‚â§ p)] :
    NormedLatticeAddCommGroup (Lp E p Œº) :=
  { Lp.instLattice, Lp.instNormedAddCommGroup with
    add_le_add_left := fun _ _ => add_le_add_left
    solid := fun f g hfg => by
      rw [‚Üê coeFn_le] at hfg
      simp_rw [Lp.norm_def, ENNReal.toReal_le_toReal (Lp.eLpNorm_ne_top f) (Lp.eLpNorm_ne_top g)]
      refine eLpNorm_mono_ae ?_
      filter_upwards [hfg, Lp.coeFn_abs f, Lp.coeFn_abs g] with x hx hxf hxg
      rw [hxf, hxg] at hx
      exact HasSolidNorm.solid hx }

end Order

end Lp

end MeasureTheory
