/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.InnerProductSpace.LinearMap
import Mathlib.MeasureTheory.Function.LpSpace.ContinuousFunctions
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Inner
import Mathlib.MeasureTheory.Integral.Bochner.ContinuousLinearMap

/-! # `L^2` space

If `E` is an inner product space over `ğ•œ` (`â„` or `â„‚`), then `Lp E 2 Î¼`
(defined in `Mathlib/MeasureTheory/Function/LpSpace.lean`)
is also an inner product space, with inner product defined as `inner f g := âˆ« a, âŸªf a, g aâŸ« âˆ‚Î¼`.

### Main results

* `mem_L1_inner` : for `f` and `g` in `Lp E 2 Î¼`, the pointwise inner product `fun x â†¦ âŸªf x, g xâŸ«`
  belongs to `Lp ğ•œ 1 Î¼`.
* `integrable_inner` : for `f` and `g` in `Lp E 2 Î¼`, the pointwise inner product
  `fun x â†¦ âŸªf x, g xâŸ«` is integrable.
* `L2.innerProductSpace` : `Lp E 2 Î¼` is an inner product space.
-/

noncomputable section

open TopologicalSpace MeasureTheory MeasureTheory.Lp Filter

open scoped NNReal ENNReal MeasureTheory

namespace MeasureTheory

section

variable {Î± F : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedAddCommGroup F]

theorem MemLp.integrable_sq {f : Î± â†’ â„} (h : MemLp f 2 Î¼) : Integrable (fun x => f x ^ 2) Î¼ := by
  simpa [â† memLp_one_iff_integrable] using h.norm_rpow two_ne_zero ENNReal.ofNat_ne_top

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.integrable_sq := MemLp.integrable_sq

theorem memLp_two_iff_integrable_sq_norm {f : Î± â†’ F} (hf : AEStronglyMeasurable f Î¼) :
    MemLp f 2 Î¼ â†” Integrable (fun x => â€–f xâ€– ^ 2) Î¼ := by
  rw [â† memLp_one_iff_integrable]
  convert (memLp_norm_rpow_iff hf two_ne_zero ENNReal.ofNat_ne_top).symm
  Â· simp
  Â· rw [div_eq_mul_inv, ENNReal.mul_inv_cancel two_ne_zero ENNReal.ofNat_ne_top]

@[deprecated (since := "2025-02-21")]
alias memâ„’p_two_iff_integrable_sq_norm := memLp_two_iff_integrable_sq_norm

theorem memLp_two_iff_integrable_sq {f : Î± â†’ â„} (hf : AEStronglyMeasurable f Î¼) :
    MemLp f 2 Î¼ â†” Integrable (fun x => f x ^ 2) Î¼ := by
  convert memLp_two_iff_integrable_sq_norm hf using 3
  simp

@[deprecated (since := "2025-02-21")]
alias memâ„’p_two_iff_integrable_sq := memLp_two_iff_integrable_sq

end

section InnerProductSpace

variable {Î± : Type*} {m : MeasurableSpace Î±} {p : â„â‰¥0âˆ} {Î¼ : Measure Î±}
variable {E ğ•œ : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

theorem MemLp.const_inner (c : E) {f : Î± â†’ E} (hf : MemLp f p Î¼) : MemLp (fun a => âŸªc, f aâŸ«) p Î¼ :=
  hf.of_le_mul (AEStronglyMeasurable.inner aestronglyMeasurable_const hf.1)
    (Eventually.of_forall fun _ => norm_inner_le_norm _ _)

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.const_inner := MemLp.const_inner

theorem MemLp.inner_const {f : Î± â†’ E} (hf : MemLp f p Î¼) (c : E) : MemLp (fun a => âŸªf a, câŸ«) p Î¼ :=
  hf.of_le_mul (c := â€–câ€–) (AEStronglyMeasurable.inner hf.1 aestronglyMeasurable_const)
    (Eventually.of_forall fun x => by rw [mul_comm]; exact norm_inner_le_norm _ _)

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.inner_const := MemLp.inner_const

variable {f : Î± â†’ E}

@[fun_prop]
theorem Integrable.const_inner (c : E) (hf : Integrable f Î¼) :
    Integrable (fun x => âŸªc, f xâŸ«) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢; exact hf.const_inner c

@[fun_prop]
theorem Integrable.inner_const (hf : Integrable f Î¼) (c : E) :
    Integrable (fun x => âŸªf x, câŸ«) Î¼ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢; exact hf.inner_const c

variable [CompleteSpace E] [NormedSpace â„ E]

theorem _root_.integral_inner {f : Î± â†’ E} (hf : Integrable f Î¼) (c : E) :
    âˆ« x, âŸªc, f xâŸ« âˆ‚Î¼ = âŸªc, âˆ« x, f x âˆ‚Î¼âŸ« :=
  ((innerSL ğ•œ c).restrictScalars â„).integral_comp_comm hf

variable (ğ•œ)

theorem _root_.integral_eq_zero_of_forall_integral_inner_eq_zero (f : Î± â†’ E) (hf : Integrable f Î¼)
    (hf_int : âˆ€ c : E, âˆ« x, âŸªc, f xâŸ« âˆ‚Î¼ = 0) : âˆ« x, f x âˆ‚Î¼ = 0 := by
  specialize hf_int (âˆ« x, f x âˆ‚Î¼); rwa [integral_inner hf, inner_self_eq_zero] at hf_int

end InnerProductSpace

namespace L2

variable {Î± E F ğ•œ : Type*} [RCLike ğ•œ] [MeasurableSpace Î±] {Î¼ : Measure Î±} [NormedAddCommGroup E]
  [InnerProductSpace ğ•œ E] [NormedAddCommGroup F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

theorem eLpNorm_rpow_two_norm_lt_top (f : Lp F 2 Î¼) :
    eLpNorm (fun x => â€–f xâ€– ^ (2 : â„)) 1 Î¼ < âˆ := by
  have h_two : ENNReal.ofReal (2 : â„) = 2 := by simp [zero_le_one]
  rw [eLpNorm_norm_rpow f zero_lt_two, one_mul, h_two]
  exact ENNReal.rpow_lt_top_of_nonneg zero_le_two (Lp.eLpNorm_ne_top f)

theorem eLpNorm_inner_lt_top (f g : Î± â†’â‚‚[Î¼] E) : eLpNorm (fun x : Î± => âŸªf x, g xâŸ«) 1 Î¼ < âˆ := by
  have h : âˆ€ x, â€–âŸªf x, g xâŸ«â€– â‰¤ â€–â€–f xâ€– ^ (2 : â„) + â€–g xâ€– ^ (2 : â„)â€– := by
    intro x
    rw [â† @Nat.cast_two â„, Real.rpow_natCast, Real.rpow_natCast]
    calc
      â€–âŸªf x, g xâŸ«â€– â‰¤ â€–f xâ€– * â€–g xâ€– := norm_inner_le_norm _ _
      _ â‰¤ 2 * â€–f xâ€– * â€–g xâ€– :=
        (mul_le_mul_of_nonneg_right (le_mul_of_one_le_left (norm_nonneg _) one_le_two)
          (norm_nonneg _))
      -- TODO(kmill): the type ascription is getting around an elaboration error
      _ â‰¤ â€–(â€–f xâ€– ^ 2 + â€–g xâ€– ^ 2 : â„)â€– := (two_mul_le_add_sq _ _).trans (le_abs_self _)
  refine (eLpNorm_mono_ae (ae_of_all _ h)).trans_lt ((eLpNorm_add_le ?_ ?_ le_rfl).trans_lt ?_)
  Â· exact ((Lp.aestronglyMeasurable f).norm.aemeasurable.pow_const _).aestronglyMeasurable
  Â· exact ((Lp.aestronglyMeasurable g).norm.aemeasurable.pow_const _).aestronglyMeasurable
  rw [ENNReal.add_lt_top]
  exact âŸ¨eLpNorm_rpow_two_norm_lt_top f, eLpNorm_rpow_two_norm_lt_top gâŸ©

section InnerProductSpace

open scoped ComplexConjugate

instance : Inner ğ•œ (Î± â†’â‚‚[Î¼] E) :=
  âŸ¨fun f g => âˆ« a, âŸªf a, g aâŸ« âˆ‚Î¼âŸ©

theorem inner_def (f g : Î± â†’â‚‚[Î¼] E) : âŸªf, gâŸ« = âˆ« a : Î±, âŸªf a, g aâŸ« âˆ‚Î¼ :=
  rfl

theorem integral_inner_eq_sq_eLpNorm (f : Î± â†’â‚‚[Î¼] E) :
    âˆ« a, âŸªf a, f aâŸ« âˆ‚Î¼ = ENNReal.toReal (âˆ«â» a, (â€–f aâ€–â‚Š : â„â‰¥0âˆ) ^ (2 : â„) âˆ‚Î¼) := by
  simp_rw [inner_self_eq_norm_sq_to_K]
  norm_cast
  rw [integral_eq_lintegral_of_nonneg_ae]
  rotate_left
  Â· exact Filter.Eventually.of_forall fun x => sq_nonneg _
  Â· exact ((Lp.aestronglyMeasurable f).norm.aemeasurable.pow_const _).aestronglyMeasurable
  congr
  ext1 x
  have h_two : (2 : â„) = ((2 : â„•) : â„) := by simp
  rw [â† Real.rpow_natCast _ 2, â† h_two, â†
    ENNReal.ofReal_rpow_of_nonneg (norm_nonneg _) zero_le_two, ofReal_norm_eq_enorm]
  norm_cast

private theorem norm_sq_eq_re_inner (f : Î± â†’â‚‚[Î¼] E) : â€–fâ€– ^ 2 = RCLike.re âŸªf, fâŸ« := by
  have h_two : (2 : â„â‰¥0âˆ).toReal = 2 := by simp
  rw [inner_def, integral_inner_eq_sq_eLpNorm, norm_def, â† ENNReal.toReal_pow, RCLike.ofReal_re,
    ENNReal.toReal_eq_toReal (ENNReal.pow_ne_top (Lp.eLpNorm_ne_top f)) _]
  Â· rw [â† ENNReal.rpow_natCast, eLpNorm_eq_eLpNorm' two_ne_zero ENNReal.ofNat_ne_top, eLpNorm', â†
      ENNReal.rpow_mul, one_div, h_two]
    simp [enorm_eq_nnnorm]
  Â· refine (lintegral_rpow_enorm_lt_top_of_eLpNorm'_lt_top zero_lt_two (Îµ := E) ?_).ne
    rw [â† h_two, â† eLpNorm_eq_eLpNorm' two_ne_zero ENNReal.ofNat_ne_top]
    exact Lp.eLpNorm_lt_top f

@[deprecated (since := "2025-04-22")] alias norm_sq_eq_inner' := norm_sq_eq_re_inner

theorem mem_L1_inner (f g : Î± â†’â‚‚[Î¼] E) :
    AEEqFun.mk (fun x => âŸªf x, g xâŸ«)
        ((Lp.aestronglyMeasurable f).inner (Lp.aestronglyMeasurable g)) âˆˆ
      Lp ğ•œ 1 Î¼ := by
  simp_rw [mem_Lp_iff_eLpNorm_lt_top, eLpNorm_aeeqFun]; exact eLpNorm_inner_lt_top f g

theorem integrable_inner (f g : Î± â†’â‚‚[Î¼] E) : Integrable (fun x : Î± => âŸªf x, g xâŸ«) Î¼ :=
  (integrable_congr
        (AEEqFun.coeFn_mk (fun x => âŸªf x, g xâŸ«)
          ((Lp.aestronglyMeasurable f).inner (Lp.aestronglyMeasurable g)))).mp
    (AEEqFun.integrable_iff_mem_L1.mpr (mem_L1_inner f g))

private theorem add_left' (f f' g : Î± â†’â‚‚[Î¼] E) : âŸªf + f', gâŸ« = âŸªf, gâŸ« + âŸªf', gâŸ« := by
  simp_rw [inner_def, â† integral_add (integrable_inner (ğ•œ := ğ•œ) f g) (integrable_inner f' g),
    â† inner_add_left]
  refine integral_congr_ae ((coeFn_add f f').mono fun x hx => ?_)
  simp only [hx, Pi.add_apply]

private theorem smul_left' (f g : Î± â†’â‚‚[Î¼] E) (r : ğ•œ) : âŸªr â€¢ f, gâŸ« = conj r * âŸªf, gâŸ« := by
  rw [inner_def, inner_def, â† smul_eq_mul, â† integral_smul]
  refine integral_congr_ae ((coeFn_smul r f).mono fun x hx => ?_)
  simp only
  rw [smul_eq_mul, â† inner_smul_left, hx, Pi.smul_apply]

instance innerProductSpace : InnerProductSpace ğ•œ (Î± â†’â‚‚[Î¼] E) where
  norm_sq_eq_re_inner := norm_sq_eq_re_inner
  conj_inner_symm _ _ := by simp_rw [inner_def, â† integral_conj, inner_conj_symm]
  add_left := add_left'
  smul_left := smul_left'

end InnerProductSpace

section IndicatorConstLp

variable (ğ•œ) {s : Set Î±}

/-- The inner product in `L2` of the indicator of a set `indicatorConstLp 2 hs hÎ¼s c` and `f` is
equal to the integral of the inner product over `s`: `âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼`. -/
theorem inner_indicatorConstLp_eq_setIntegral_inner (f : Lp E 2 Î¼) (hs : MeasurableSet s) (c : E)
    (hÎ¼s : Î¼ s â‰  âˆ) : (âŸªindicatorConstLp 2 hs hÎ¼s c, fâŸ« : ğ•œ) = âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼ := by
  rw [inner_def, â† integral_add_compl hs (L2.integrable_inner _ f)]
  have h_left : (âˆ« x in s, âŸª(indicatorConstLp 2 hs hÎ¼s c) x, f xâŸ« âˆ‚Î¼) = âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼ := by
    suffices h_ae_eq : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âŸªindicatorConstLp 2 hs hÎ¼s c x, f xâŸ« = âŸªc, f xâŸ« from
      setIntegral_congr_ae hs h_ae_eq
    have h_indicator : âˆ€áµ x : Î± âˆ‚Î¼, x âˆˆ s â†’ indicatorConstLp 2 hs hÎ¼s c x = c :=
      indicatorConstLp_coeFn_mem
    refine h_indicator.mono fun x hx hxs => ?_
    congr
    exact hx hxs
  have h_right : (âˆ« x in sá¶œ, âŸª(indicatorConstLp 2 hs hÎ¼s c) x, f xâŸ« âˆ‚Î¼) = 0 := by
    suffices h_ae_eq : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ âŸªindicatorConstLp 2 hs hÎ¼s c x, f xâŸ« = 0 by
      simp_rw [â† Set.mem_compl_iff] at h_ae_eq
      suffices h_int_zero :
          (âˆ« x in sá¶œ, âŸªindicatorConstLp 2 hs hÎ¼s c x, f xâŸ« âˆ‚Î¼) = âˆ« _ in sá¶œ, 0 âˆ‚Î¼ by
        rw [h_int_zero]
        simp
      exact setIntegral_congr_ae hs.compl h_ae_eq
    have h_indicator : âˆ€áµ x : Î± âˆ‚Î¼, x âˆ‰ s â†’ indicatorConstLp 2 hs hÎ¼s c x = 0 :=
      indicatorConstLp_coeFn_notMem
    refine h_indicator.mono fun x hx hxs => ?_
    rw [hx hxs]
    exact inner_zero_left _
  rw [h_left, h_right, add_zero]

/-- The inner product in `L2` of the indicator of a set `indicatorConstLp 2 hs hÎ¼s c` and `f` is
equal to the inner product of the constant `c` and the integral of `f` over `s`. -/
theorem inner_indicatorConstLp_eq_inner_setIntegral [CompleteSpace E] [NormedSpace â„ E]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) (f : Lp E 2 Î¼) :
    (âŸªindicatorConstLp 2 hs hÎ¼s c, fâŸ« : ğ•œ) = âŸªc, âˆ« x in s, f x âˆ‚Î¼âŸ« := by
  rw [â† integral_inner (integrableOn_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s),
    L2.inner_indicatorConstLp_eq_setIntegral_inner]

variable {ğ•œ}

/-- The inner product in `L2` of the indicator of a set `indicatorConstLp 2 hs hÎ¼s (1 : ğ•œ)` and
a real or complex function `f` is equal to the integral of `f` over `s`. -/
theorem inner_indicatorConstLp_one (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp ğ•œ 2 Î¼) :
    âŸªindicatorConstLp 2 hs hÎ¼s (1 : ğ•œ), fâŸ« = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [L2.inner_indicatorConstLp_eq_inner_setIntegral ğ•œ hs hÎ¼s (1 : ğ•œ) f]; simp

end IndicatorConstLp

end L2

section InnerContinuous

variable {Î± ğ•œ : Type*} [TopologicalSpace Î±] [MeasurableSpace Î±] [BorelSpace Î±] [RCLike ğ•œ]
variable (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]

open scoped BoundedContinuousFunction ComplexConjugate

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-- For bounded continuous functions `f`, `g` on a finite-measure topological space `Î±`, the L^2
inner product is the integral of their pointwise inner product. -/
theorem BoundedContinuousFunction.inner_toLp (f g : Î± â†’áµ‡ ğ•œ) :
    âŸªBoundedContinuousFunction.toLp 2 Î¼ ğ•œ f,
        BoundedContinuousFunction.toLp 2 Î¼ ğ•œ gâŸ« =
      âˆ« x, g x * conj (f x) âˆ‚Î¼ := by
  apply integral_congr_ae
  have hf_ae := f.coeFn_toLp 2 Î¼ ğ•œ
  have hg_ae := g.coeFn_toLp 2 Î¼ ğ•œ
  filter_upwards [hf_ae, hg_ae] with _ hf hg
  rw [hf, hg]
  simp

variable [CompactSpace Î±]

/-- For continuous functions `f`, `g` on a compact, finite-measure topological space `Î±`, the L^2
inner product is the integral of their pointwise inner product. -/
theorem ContinuousMap.inner_toLp (f g : C(Î±, ğ•œ)) :
    âŸªContinuousMap.toLp 2 Î¼ ğ•œ f, ContinuousMap.toLp 2 Î¼ ğ•œ gâŸ« =
      âˆ« x, g x * conj (f x) âˆ‚Î¼ := by
  apply integral_congr_ae
  have hf_ae := f.coeFn_toLp (p := 2) (ğ•œ := ğ•œ) Î¼
  have hg_ae := g.coeFn_toLp (p := 2) (ğ•œ := ğ•œ) Î¼
  filter_upwards [hf_ae, hg_ae] with _ hf hg
  rw [hf, hg]
  simp

end InnerContinuous

end MeasureTheory
