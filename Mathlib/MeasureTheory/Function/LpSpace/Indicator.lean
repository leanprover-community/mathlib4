/-
Copyright (c) 2020 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.MeasureTheory.Function.LpSpace.Basic
import Mathlib.MeasureTheory.Measure.Real
import Mathlib.Order.Filter.IndicatorFunction

/-!
# Indicator of a set as an element of `Lp`

For a set `s` with `(hs : MeasurableSet s)` and `(hÎ¼s : Î¼ s < âˆ)`, we build
`indicatorConstLp p hs hÎ¼s c`, the element of `Lp` corresponding to `s.indicator (fun _ â†¦ c)`.

## Main definitions

* `MeasureTheory.indicatorConstLp`: Indicator of a set as an element of `Lp`.
* `MeasureTheory.Lp.const`: Constant function as an element of `Lp` for a finite measure.
-/

noncomputable section

open MeasureTheory Filter
open scoped NNReal ENNReal Topology symmDiff

variable {Î± E : Type*} {m : MeasurableSpace Î±} {p : â„â‰¥0âˆ} {Î¼ : Measure Î±} [NormedAddCommGroup E]

namespace MeasureTheory

/-- The `eLpNorm` of the indicator of a set is uniformly small if the set itself has small measure,
for any `p < âˆ`. Given here as an existential `âˆ€ Îµ > 0, âˆƒ Î· > 0, ...` to avoid later
management of `â„â‰¥0âˆ`-arithmetic. -/
theorem exists_eLpNorm_indicator_le (hp : p â‰  âˆ) (c : E) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ âˆ€ s : Set Î±, Î¼ s â‰¤ Î· â†’ eLpNorm (s.indicator fun _ â†¦ c) p Î¼ â‰¤ Îµ := by
  rcases eq_or_ne p 0 with (rfl | h'p)
  Â· exact âŸ¨1, zero_lt_one, fun s _ â†¦ by simpâŸ©
  have hpâ‚€ : 0 < p := bot_lt_iff_ne_bot.2 h'p
  have hpâ‚€' : 0 â‰¤ 1 / p.toReal := div_nonneg zero_le_one ENNReal.toReal_nonneg
  have hpâ‚€'' : 0 < p.toReal := ENNReal.toReal_pos hpâ‚€.ne' hp
  obtain âŸ¨Î·, hÎ·_pos, hÎ·_leâŸ© : âˆƒ Î· : â„â‰¥0, 0 < Î· âˆ§ â€–câ€–â‚‘ * (Î· : â„â‰¥0âˆ) ^ (1 / p.toReal) â‰¤ Îµ := by
    have :
      Filter.Tendsto (fun x : â„â‰¥0 â†¦ ((â€–câ€–â‚Š * x ^ (1 / p.toReal) : â„â‰¥0) : â„â‰¥0âˆ)) (ğ“ 0)
        (ğ“ (0 : â„â‰¥0)) := by
      rw [ENNReal.tendsto_coe]
      convert (NNReal.continuousAt_rpow_const (Or.inr hpâ‚€')).tendsto.const_mul _
      simp [hpâ‚€''.ne']
    have hÎµ' : 0 < Îµ := hÎµ.bot_lt
    obtain âŸ¨Î´, hÎ´, hÎ´Îµ'âŸ© := NNReal.nhds_zero_basis.eventually_iff.mp (this.eventually_le_const hÎµ')
    obtain âŸ¨Î·, hÎ·, hÎ·Î´âŸ© := exists_between hÎ´
    refine âŸ¨Î·, hÎ·, ?_âŸ©
    simpa only [â† ENNReal.coe_rpow_of_nonneg _ hpâ‚€', enorm, â† ENNReal.coe_mul] using hÎ´Îµ' hÎ·Î´
  refine âŸ¨Î·, hÎ·_pos, fun s hs â†¦ ?_âŸ©
  refine (eLpNorm_indicator_const_le _ _).trans (le_trans ?_ hÎ·_le)
  exact mul_le_mul_left' (ENNReal.rpow_le_rpow hs hpâ‚€') _

section Topology
variable {X : Type*} [TopologicalSpace X] [MeasurableSpace X]
  {Î¼ : Measure X} [IsFiniteMeasureOnCompacts Î¼]

/-- A bounded measurable function with compact support is in L^p. -/
theorem _root_.HasCompactSupport.memLp_of_bound {f : X â†’ E} (hf : HasCompactSupport f)
    (h2f : AEStronglyMeasurable f Î¼) (C : â„) (hfC : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ C) : MemLp f p Î¼ := by
  have := memLp_top_of_bound h2f C hfC
  exact this.mono_exponent_of_measure_support_ne_top
    (fun x â†¦ image_eq_zero_of_notMem_tsupport) (hf.measure_lt_top.ne) le_top

@[deprecated (since := "2025-02-21")]
alias _root_.HasCompactSupport.memâ„’p_of_bound := _root_.HasCompactSupport.memLp_of_bound

/-- A continuous function with compact support is in L^p. -/
theorem _root_.Continuous.memLp_of_hasCompactSupport [OpensMeasurableSpace X]
    {f : X â†’ E} (hf : Continuous f) (h'f : HasCompactSupport f) : MemLp f p Î¼ := by
  have := hf.memLp_top_of_hasCompactSupport h'f Î¼
  exact this.mono_exponent_of_measure_support_ne_top
    (fun x â†¦ image_eq_zero_of_notMem_tsupport) (h'f.measure_lt_top.ne) le_top

@[deprecated (since := "2025-02-21")]
alias _root_.Continuous.memâ„’p_of_hasCompactSupport := _root_.Continuous.memLp_of_hasCompactSupport

end Topology

section IndicatorConstLp

open Set Function

variable {s : Set Î±} {hs : MeasurableSet s} {hÎ¼s : Î¼ s â‰  âˆ} {c : E}

/-- Indicator of a set as an element of `Lp`. -/
def indicatorConstLp (p : â„â‰¥0âˆ) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) : Lp E p Î¼ :=
  MemLp.toLp (s.indicator fun _ â†¦ c) (memLp_indicator_const p hs c (Or.inr hÎ¼s))

/-- A version of `Set.indicator_add` for `MeasureTheory.indicatorConstLp` -/
theorem indicatorConstLp_add {c' : E} :
    indicatorConstLp p hs hÎ¼s c + indicatorConstLp p hs hÎ¼s c' =
    indicatorConstLp p hs hÎ¼s (c + c') := by
  simp_rw [indicatorConstLp, â† MemLp.toLp_add, indicator_add]
  rfl

/-- A version of `Set.indicator_sub` for `MeasureTheory.indicatorConstLp` -/
theorem indicatorConstLp_sub {c' : E} :
    indicatorConstLp p hs hÎ¼s c - indicatorConstLp p hs hÎ¼s c' =
    indicatorConstLp p hs hÎ¼s (c - c') := by
  simp_rw [indicatorConstLp, â† MemLp.toLp_sub, indicator_sub]
  rfl

theorem indicatorConstLp_coeFn : â‡‘(indicatorConstLp p hs hÎ¼s c) =áµ[Î¼] s.indicator fun _ â†¦ c :=
  MemLp.coeFn_toLp (memLp_indicator_const p hs c (Or.inr hÎ¼s))

theorem indicatorConstLp_coeFn_mem : âˆ€áµ x : Î± âˆ‚Î¼, x âˆˆ s â†’ indicatorConstLp p hs hÎ¼s c x = c :=
  indicatorConstLp_coeFn.mono fun _x hx hxs â†¦ hx.trans (Set.indicator_of_mem hxs _)

theorem indicatorConstLp_coeFn_notMem : âˆ€áµ x : Î± âˆ‚Î¼, x âˆ‰ s â†’ indicatorConstLp p hs hÎ¼s c x = 0 :=
  indicatorConstLp_coeFn.mono fun _x hx hxs â†¦ hx.trans (Set.indicator_of_notMem hxs _)

@[deprecated (since := "2025-05-24")]
alias indicatorConstLp_coeFn_nmem := indicatorConstLp_coeFn_notMem

theorem norm_indicatorConstLp (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ) :
    â€–indicatorConstLp p hs hÎ¼s câ€– = â€–câ€– * Î¼.real s ^ (1 / p.toReal) := by
  rw [Lp.norm_def, eLpNorm_congr_ae indicatorConstLp_coeFn,
    eLpNorm_indicator_const hs hp_ne_zero hp_ne_top, ENNReal.toReal_mul, measureReal_def,
    ENNReal.toReal_rpow, toReal_enorm]

theorem norm_indicatorConstLp_top (hÎ¼s_ne_zero : Î¼ s â‰  0) :
    â€–indicatorConstLp âˆ hs hÎ¼s câ€– = â€–câ€– := by
  rw [Lp.norm_def, eLpNorm_congr_ae indicatorConstLp_coeFn,
    eLpNorm_indicator_const' hs hÎ¼s_ne_zero ENNReal.top_ne_zero, ENNReal.toReal_top,
    _root_.div_zero, ENNReal.rpow_zero, mul_one, toReal_enorm]

theorem norm_indicatorConstLp' (hp_pos : p â‰  0) (hÎ¼s_pos : Î¼ s â‰  0) :
    â€–indicatorConstLp p hs hÎ¼s câ€– = â€–câ€– * Î¼.real s ^ (1 / p.toReal) := by
  by_cases hp_top : p = âˆ
  Â· rw [hp_top, ENNReal.toReal_top, _root_.div_zero, Real.rpow_zero, mul_one]
    exact norm_indicatorConstLp_top hÎ¼s_pos
  Â· exact norm_indicatorConstLp hp_pos hp_top

theorem norm_indicatorConstLp_le :
    â€–indicatorConstLp p hs hÎ¼s câ€– â‰¤ â€–câ€– * Î¼.real s ^ (1 / p.toReal) := by
  rw [indicatorConstLp, Lp.norm_toLp]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) ?_
  refine (eLpNorm_indicator_const_le _ _).trans_eq ?_
  rw [ENNReal.ofReal_mul (norm_nonneg _), ofReal_norm, measureReal_def,
    ENNReal.toReal_rpow, ENNReal.ofReal_toReal]
  finiteness

theorem nnnorm_indicatorConstLp_le :
    â€–indicatorConstLp p hs hÎ¼s câ€–â‚Š â‰¤ â€–câ€–â‚Š * (Î¼ s).toNNReal ^ (1 / p.toReal) :=
  norm_indicatorConstLp_le

theorem enorm_indicatorConstLp_le :
    â€–indicatorConstLp p hs hÎ¼s câ€–â‚‘ â‰¤ â€–câ€–â‚‘ * Î¼ s ^ (1 / p.toReal) := by
  simpa [ENNReal.coe_rpow_of_nonneg, ENNReal.coe_toNNReal hÎ¼s, Lp.enorm_def, â† enorm_eq_nnnorm]
    using ENNReal.coe_le_coe.2 <| nnnorm_indicatorConstLp_le (c := c) (hÎ¼s := hÎ¼s)

theorem edist_indicatorConstLp_eq_enorm {t : Set Î±} {ht : MeasurableSet t} {hÎ¼t : Î¼ t â‰  âˆ} :
    edist (indicatorConstLp p hs hÎ¼s c) (indicatorConstLp p ht hÎ¼t c) =
      â€–indicatorConstLp p (hs.symmDiff ht) (measure_symmDiff_ne_top hÎ¼s hÎ¼t) câ€–â‚‘ := by
  unfold indicatorConstLp
  rw [Lp.edist_toLp_toLp, eLpNorm_indicator_sub_indicator, Lp.enorm_toLp]

theorem dist_indicatorConstLp_eq_norm {t : Set Î±} {ht : MeasurableSet t} {hÎ¼t : Î¼ t â‰  âˆ} :
    dist (indicatorConstLp p hs hÎ¼s c) (indicatorConstLp p ht hÎ¼t c) =
      â€–indicatorConstLp p (hs.symmDiff ht) (measure_symmDiff_ne_top hÎ¼s hÎ¼t) câ€– := by
  -- Squeezed for performance reasons
  simp only [Lp.dist_edist, edist_indicatorConstLp_eq_enorm, enorm, ENNReal.coe_toReal,
    Lp.coe_nnnorm]

/-- A family of `indicatorConstLp` functions tends to an `indicatorConstLp`,
if the underlying sets tend to the set in the sense of the measure of the symmetric difference. -/
theorem tendsto_indicatorConstLp_set [hpâ‚ : Fact (1 â‰¤ p)] {Î² : Type*} {l : Filter Î²} {t : Î² â†’ Set Î±}
    {ht : âˆ€ b, MeasurableSet (t b)} {hÎ¼t : âˆ€ b, Î¼ (t b) â‰  âˆ} (hp : p â‰  âˆ)
    (h : Tendsto (fun b â†¦ Î¼ (t b âˆ† s)) l (ğ“ 0)) :
    Tendsto (fun b â†¦ indicatorConstLp p (ht b) (hÎ¼t b) c) l (ğ“ (indicatorConstLp p hs hÎ¼s c)) := by
  rw [tendsto_iff_dist_tendsto_zero]
  have hpâ‚€ : p â‰  0 := (one_pos.trans_le hpâ‚.out).ne'
  simp only [dist_indicatorConstLp_eq_norm, norm_indicatorConstLp hpâ‚€ hp]
  convert tendsto_const_nhds.mul
    (((ENNReal.tendsto_toReal ENNReal.zero_ne_top).comp h).rpow_const _)
  Â· simp [ENNReal.toReal_eq_zero_iff, hp, hpâ‚€]
  Â· simp

/-- A family of `indicatorConstLp` functions is continuous in the parameter,
if `Î¼ (s y âˆ† s x)` tends to zero as `y` tends to `x` for all `x`. -/
theorem continuous_indicatorConstLp_set [Fact (1 â‰¤ p)] {X : Type*} [TopologicalSpace X]
    {s : X â†’ Set Î±} {hs : âˆ€ x, MeasurableSet (s x)} {hÎ¼s : âˆ€ x, Î¼ (s x) â‰  âˆ} (hp : p â‰  âˆ)
    (h : âˆ€ x, Tendsto (fun y â†¦ Î¼ (s y âˆ† s x)) (ğ“ x) (ğ“ 0)) :
    Continuous fun x â†¦ indicatorConstLp p (hs x) (hÎ¼s x) c :=
  continuous_iff_continuousAt.2 fun x â†¦ tendsto_indicatorConstLp_set hp (h x)

@[simp]
theorem indicatorConstLp_empty :
    indicatorConstLp p MeasurableSet.empty (by simp : Î¼ âˆ… â‰  âˆ) c = 0 := by
  simp only [indicatorConstLp, Set.indicator_empty', MemLp.toLp_zero]

theorem indicatorConstLp_inj {s t : Set Î±} (hs : MeasurableSet s) (hsÎ¼ : Î¼ s â‰  âˆ)
    (ht : MeasurableSet t) (htÎ¼ : Î¼ t â‰  âˆ) {c : E} (hc : c â‰  0) :
    indicatorConstLp p hs hsÎ¼ c = indicatorConstLp p ht htÎ¼ c â†” s =áµ[Î¼] t := by
  simp_rw [â† indicator_const_eventuallyEq hc, indicatorConstLp, MemLp.toLp_eq_toLp_iff]

theorem memLp_add_of_disjoint {f g : Î± â†’ E} (h : Disjoint (support f) (support g))
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    MemLp (f + g) p Î¼ â†” MemLp f p Î¼ âˆ§ MemLp g p Î¼ := by
  borelize E
  refine âŸ¨fun hfg â†¦ âŸ¨?_, ?_âŸ©, fun h â†¦ h.1.add h.2âŸ©
  Â· rw [â† Set.indicator_add_eq_left h]; exact hfg.indicator (measurableSet_support hf.measurable)
  Â· rw [â† Set.indicator_add_eq_right h]; exact hfg.indicator (measurableSet_support hg.measurable)

@[deprecated (since := "2025-02-21")]
alias memâ„’p_add_of_disjoint := memLp_add_of_disjoint

/-- The indicator of a disjoint union of two sets is the sum of the indicators of the sets. -/
theorem indicatorConstLp_disjoint_union {s t : Set Î±} (hs : MeasurableSet s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (hst : Disjoint s t) (c : E) :
    indicatorConstLp p (hs.union ht) (measure_union_ne_top hÎ¼s hÎ¼t) c =
      indicatorConstLp p hs hÎ¼s c + indicatorConstLp p ht hÎ¼t c := by
  ext1
  grw [Lp.coeFn_add, indicatorConstLp_coeFn]
  refine
    EventuallyEq.trans ?_
      (EventuallyEq.fun_add indicatorConstLp_coeFn.symm indicatorConstLp_coeFn.symm)
  rw [Set.indicator_union_of_disjoint hst]

end IndicatorConstLp

section const

variable (Î¼ p)
variable [IsFiniteMeasure Î¼] (c : E)

/-- Constant function as an element of `MeasureTheory.Lp` for a finite measure. -/
protected def Lp.const : E â†’+ Lp E p Î¼ where
  toFun c := âŸ¨AEEqFun.const Î± c, const_mem_Lp Î± Î¼ câŸ©
  map_zero' := rfl
  map_add' _ _ := rfl

lemma Lp.coeFn_const : Lp.const p Î¼ c =áµ[Î¼] Function.const Î± c :=
  AEEqFun.coeFn_const Î± c

@[simp] lemma Lp.const_val : (Lp.const p Î¼ c).1 = AEEqFun.const Î± c := rfl

@[simp]
lemma MemLp.toLp_const : MemLp.toLp _ (memLp_const c) = Lp.const p Î¼ c := rfl

@[deprecated (since := "2025-02-21")]
alias Memâ„’p.toLp_const := MemLp.toLp_const

@[simp]
lemma indicatorConstLp_univ :
    indicatorConstLp p .univ (measure_ne_top Î¼ _) c = Lp.const p Î¼ c := by
  rw [â† MemLp.toLp_const, indicatorConstLp]
  simp only [Set.indicator_univ]

theorem Lp.norm_const [NeZero Î¼] (hp_zero : p â‰  0) :
    â€–Lp.const p Î¼ câ€– = â€–câ€– * Î¼.real Set.univ ^ (1 / p.toReal) := by
  have := NeZero.ne Î¼
  rw [â† MemLp.toLp_const, Lp.norm_toLp, eLpNorm_const] <;> try assumption
  rw [measureReal_def, ENNReal.toReal_mul, toReal_enorm, â† ENNReal.toReal_rpow]

theorem Lp.norm_const' (hp_zero : p â‰  0) (hp_top : p â‰  âˆ) :
    â€–Lp.const p Î¼ câ€– = â€–câ€– * Î¼.real Set.univ ^ (1 / p.toReal) := by
  rw [â† MemLp.toLp_const, Lp.norm_toLp, eLpNorm_const'] <;> try assumption
  rw [measureReal_def, ENNReal.toReal_mul, toReal_enorm, â† ENNReal.toReal_rpow]

theorem Lp.norm_const_le : â€–Lp.const p Î¼ câ€– â‰¤ â€–câ€– * Î¼.real Set.univ ^ (1 / p.toReal) := by
  rw [â† indicatorConstLp_univ]
  exact norm_indicatorConstLp_le

/-- `MeasureTheory.Lp.const` as a `LinearMap`. -/
@[simps] protected def Lp.constâ‚— (ğ•œ : Type*) [NormedRing ğ•œ] [Module ğ•œ E] [IsBoundedSMul ğ•œ E] :
    E â†’â‚—[ğ•œ] Lp E p Î¼ where
  toFun := Lp.const p Î¼
  map_add' := map_add _
  map_smul' _ _ := rfl

/-- `MeasureTheory.Lp.const` as a `ContinuousLinearMap`. -/
@[simps! apply]
protected def Lp.constL (ğ•œ : Type*) [NormedRing ğ•œ] [Module ğ•œ E] [IsBoundedSMul ğ•œ E] [Fact (1 â‰¤ p)] :
    E â†’L[ğ•œ] Lp E p Î¼ :=
  (Lp.constâ‚— p Î¼ ğ•œ).mkContinuous (Î¼.real Set.univ ^ (1 / p.toReal)) fun _ â†¦
    (Lp.norm_const_le _ _ _).trans_eq (mul_comm _ _)

theorem Lp.norm_constL_le (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]
    [Fact (1 â‰¤ p)] :
    â€–(Lp.constL p Î¼ ğ•œ : E â†’L[ğ•œ] Lp E p Î¼)â€– â‰¤ Î¼.real Set.univ ^ (1 / p.toReal) :=
  LinearMap.mkContinuous_norm_le _ (by positivity) _

end const

namespace Lp

variable {Î² : Type*} [MeasurableSpace Î²] {Î¼b : MeasureTheory.Measure Î²} {f : Î± â†’ Î²}

theorem indicatorConstLp_compMeasurePreserving {s : Set Î²} (hs : MeasurableSet s)
    (hÎ¼s : Î¼b s â‰  âˆ) (c : E) (hf : MeasurePreserving f Î¼ Î¼b) :
    Lp.compMeasurePreserving f hf (indicatorConstLp p hs hÎ¼s c) =
      indicatorConstLp p (hs.preimage hf.measurable)
        (by rwa [hf.measure_preimage hs.nullMeasurableSet]) c :=
  rfl

end Lp

theorem indicatorConstLp_eq_toSpanSingleton_compLp {s : Set Î±} [NormedSpace â„ E]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) :
    indicatorConstLp 2 hs hÎ¼s x =
      (ContinuousLinearMap.toSpanSingleton â„ x).compLp (indicatorConstLp 2 hs hÎ¼s (1 : â„)) := by
  ext1
  refine indicatorConstLp_coeFn.trans ?_
  have h_compLp :=
    (ContinuousLinearMap.toSpanSingleton â„ x).coeFn_compLp (indicatorConstLp 2 hs hÎ¼s (1 : â„))
  rw [â† EventuallyEq] at h_compLp
  refine EventuallyEq.trans ?_ h_compLp.symm
  refine (@indicatorConstLp_coeFn _ _ _ 2 Î¼ _ s hs hÎ¼s (1 : â„)).mono fun y hy â†¦ ?_
  dsimp only
  rw [hy]
  simp_rw [ContinuousLinearMap.toSpanSingleton_apply]
  by_cases hy_mem : y âˆˆ s <;> simp [hy_mem]

end MeasureTheory
