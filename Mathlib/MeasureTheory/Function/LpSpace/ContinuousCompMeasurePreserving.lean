/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp
import Mathlib.MeasureTheory.Measure.Regular

/-!
# Continuity of `MeasureTheory.Lp.compMeasurePreserving`

In this file we prove that the composition of an `L^p` function `g`
with a continuous measure-preserving map `f` is continuous in both arguments.

First, we prove it for indicator functions,
in terms of convergence of `Î¼ ((f a â»Â¹' s) âˆ† (g â»Â¹' s))` to zero.

Then we prove the continuity of the function of two arguments
defined on `MeasureTheory.Lp E p Î½ Ã— {f : C(X, Y) // MeasurePreserving f Î¼ Î½}`.

Finally, we provide dot notation convenience lemmas.
-/

open Filter Set MeasureTheory
open scoped ENNReal Topology symmDiff

variable {Î± X Y : Type*}
  [TopologicalSpace X] [MeasurableSpace X] [BorelSpace X] [R1Space X]
  [TopologicalSpace Y] [MeasurableSpace Y] [BorelSpace Y] [R1Space Y]
  {Î¼ : Measure X} {Î½ : Measure Y} [Î¼.InnerRegularCompactLTTop] [IsLocallyFiniteMeasure Î½]

namespace MeasureTheory

/-- Let `X` and `Y` be Râ‚ topological spaces
with Borel Ïƒ-algebras and measures `Î¼` and `Î½`, respectively.
Suppose that `Î¼` is inner regular for finite measure sets with respect to compact sets
and `Î½` is a locally finite measure.
Let `f : Î± â†’ C(X, Y)` be a family of continuous maps
that converges to a continuous map `g : C(X, Y)` in the compact-open topology along a filter `l`.
Suppose that `g` is a measure preserving map
and `f a` is a measure preserving map eventually along `l`.
Then for any finite measure measurable set `s`,
the preimages `f a â»Â¹' s` tend to the preimage `g â»Â¹' s` in measure.
More precisely, the measure of the symmetric difference of these two sets tends to zero. -/
theorem tendsto_measure_symmDiff_preimage_nhds_zero
    {l : Filter Î±} {f : Î± â†’ C(X, Y)} {g : C(X, Y)} {s : Set Y} (hfg : Tendsto f l (ğ“ g))
    (hf : âˆ€á¶  a in l, MeasurePreserving (f a) Î¼ Î½) (hg : MeasurePreserving g Î¼ Î½)
    (hs : MeasurableSet s) (hÎ½s : Î½ s â‰  âˆ) :
    Tendsto (fun a â†¦ Î¼ ((f a â»Â¹' s) âˆ† (g â»Â¹' s))) l (ğ“ 0) := by
  have : Î½.InnerRegularCompactLTTop := by
    rw [â† hg.map_eq]
    exact .map_of_continuous (map_continuous _)
  rw [ENNReal.tendsto_nhds_zero]
  intro Îµ hÎµ
  -- Without loss of generality, `s` is an open set.
  wlog hso : IsOpen s generalizing s Îµ
  Â· have H : 0 < Îµ / 3 := ENNReal.div_pos hÎµ.ne' ENNReal.coe_ne_top
    -- Indeed, we can choose an open set `U` such that `Î½ (U âˆ† s) < Îµ / 3`,
    -- apply the lemma to `U`, then use the triangle inequality for `Î¼ (_ âˆ† _)`.
    rcases hs.exists_isOpen_symmDiff_lt hÎ½s H.ne' with âŸ¨U, hUo, hU, hUsâŸ©
    have hmU : MeasurableSet U := hUo.measurableSet
    replace hUs := hUs.le
    filter_upwards [hf, this hmU hU.ne _ H hUo] with a hfa ha
    calc
      Î¼ ((f a â»Â¹' s) âˆ† (g â»Â¹' s))
        â‰¤ Î¼ ((f a â»Â¹' s) âˆ† (f a â»Â¹' U)) + Î¼ ((f a â»Â¹' U) âˆ† (g â»Â¹' U))
          + Î¼ ((g â»Â¹' U) âˆ† (g â»Â¹' s)) := by
        refine (measure_symmDiff_le _ (g â»Â¹' U) _).trans ?_
        gcongr
        apply measure_symmDiff_le
      _ â‰¤ Îµ / 3 + Îµ / 3 + Îµ / 3 := by
        gcongr
        Â· rwa [â† preimage_symmDiff, hfa.measure_preimage (hs.symmDiff hmU), symmDiff_comm]
        Â· rwa [â† preimage_symmDiff, hg.measure_preimage (hmU.symmDiff hs)]
      _ = Îµ := by simp
  -- Take a compact closed subset `K âŠ† g â»Â¹' s` of almost full measure,
  -- `Î¼ (g â»Â¹' s \ K) < Îµ / 2`.
  have hÎ½s' : Î¼ (g â»Â¹' s) â‰  âˆ := by rwa [hg.measure_preimage hs]
  obtain âŸ¨K, hKg, hKco, hKcl, hKÎ¼âŸ© :
      âˆƒ K, MapsTo g K s âˆ§ IsCompact K âˆ§ IsClosed K âˆ§ Î¼ (g â»Â¹' s \ K) < Îµ / 2 :=
    (hs.preimage hg.measurable).exists_isCompact_isClosed_diff_lt hÎ½s' <| by simp [hÎµ.ne']
  have hKm : MeasurableSet K := hKcl.measurableSet
  -- Take `a` such that `f a` is measure preserving and maps `K` to `s`.
  -- This is possible, because `K` is a compact set and `s` is an open set.
  filter_upwards [hf, ContinuousMap.tendsto_nhds_compactOpen.mp hfg K hKco s hso hKg] with a hfa ha
  -- Then each of the sets `g â»Â¹' s âˆ† K = g â»Â¹' s \ K` and `f a â»Â¹' s âˆ† K = f a â»Â¹' s \ K`
  -- have measure at most `Îµ / 2`, thus `f a â»Â¹' s âˆ† g â»Â¹' s` has measure at  most `Îµ`.
  rw [â† ENNReal.add_halves Îµ]
  refine (measure_symmDiff_le _ K _).trans ?_
  rw [symmDiff_of_ge ha.subset_preimage, symmDiff_of_le hKg.subset_preimage]
  gcongr
  have hK' : Î¼ K â‰  âˆ := ne_top_of_le_ne_top hÎ½s' <| measure_mono hKg.subset_preimage
  rw [measure_diff_le_iff_le_add hKm ha.subset_preimage hK', hfa.measure_preimage hs,
    â† hg.measure_preimage hs, â† measure_diff_le_iff_le_add hKm hKg.subset_preimage hK']
  exact hKÎ¼.le

namespace Lp

variable (Î¼ Î½)
variable (E : Type*) [NormedAddCommGroup E] {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)]

/-- Let `X` and `Y` be Râ‚ topological spaces
with Borel Ïƒ-algebras and measures `Î¼` and `Î½`, respectively.
Suppose that `Î¼` is inner regular for finite measure sets with respect to compact sets
and `Î½` is a locally finite measure.
Let `1 â‰¤ p < âˆ` be an extended nonnegative real number.
Then the composition of a function `g : Lp E p Î½`
and a measure preserving continuous function `f : C(X, Y)`
is continuous in both variables. -/
theorem compMeasurePreserving_continuous (hp : p â‰  âˆ) :
    Continuous fun gf : Lp E p Î½ Ã— {f : C(X, Y) // MeasurePreserving f Î¼ Î½} â†¦
      compMeasurePreserving gf.2.1 gf.2.2 gf.1 := by
  have hpâ‚€ : p â‰  0 := (one_pos.trans_le Fact.out).ne'
  refine continuous_prod_of_dense_continuous_lipschitzWith _ 1
    (MeasureTheory.Lp.simpleFunc.dense hp) ?_ fun f â†¦ (isometry_compMeasurePreserving f.2).lipschitz
  intro f hf
  lift f to Lp.simpleFunc E p Î½ using hf
  induction f using Lp.simpleFunc.induction hpâ‚€ hp with
  | h_add hfp hgp _ ihf ihg => exact ihf.add ihg
  | @h_ind c s hs hÎ½s =>
    dsimp only [Lp.simpleFunc.coe_indicatorConst, Lp.indicatorConstLp_compMeasurePreserving]
    refine continuous_indicatorConstLp_set hp fun f â†¦ ?_
    apply tendsto_measure_symmDiff_preimage_nhds_zero continuousAt_subtype_val _ f.2 hs hÎ½s.ne
    exact eventually_of_forall Subtype.property

end Lp

end MeasureTheory

variable {E : Type*} [NormedAddCommGroup E] {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)]

theorem Filter.Tendsto.compMeasurePreservingLp {Î± : Type*} {l : Filter Î±}
    {f : Î± â†’ Lp E p Î½} {fâ‚€ : Lp E p Î½} {g : Î± â†’ C(X, Y)} {gâ‚€ : C(X, Y)}
    (hf : Tendsto f l (ğ“ fâ‚€)) (hg : Tendsto g l (ğ“ gâ‚€))
    (hgm : âˆ€ a, MeasurePreserving (g a) Î¼ Î½) (hgmâ‚€ : MeasurePreserving gâ‚€ Î¼ Î½) (hp : p â‰  âˆ) :
    Tendsto (fun a â†¦ Lp.compMeasurePreserving (g a) (hgm a) (f a)) l
      (ğ“ (Lp.compMeasurePreserving gâ‚€ hgmâ‚€ fâ‚€)) := by
  have := (Lp.compMeasurePreserving_continuous Î¼ Î½ E hp).tendsto âŸ¨fâ‚€, gâ‚€, hgmâ‚€âŸ©
  replace hg : Tendsto (fun a â†¦ âŸ¨g a, hgm aâŸ© : Î± â†’ {g : C(X, Y) // MeasurePreserving g Î¼ Î½})
      l (ğ“ âŸ¨gâ‚€, hgmâ‚€âŸ©) :=
    tendsto_subtype_rng.2 hg
  convert this.comp (hf.prod_mk_nhds hg)

variable {Z : Type*} [TopologicalSpace Z] {f : Z â†’ Lp E p Î½} {g : Z â†’ C(X, Y)} {s : Set Z} {z : Z}

theorem ContinuousWithinAt.compMeasurePreservingLp (hf : ContinuousWithinAt f s z)
    (hg : ContinuousWithinAt g s z) (hgm : âˆ€ z, MeasurePreserving (g z) Î¼ Î½) (hp : p â‰  âˆ) :
    ContinuousWithinAt (fun z â†¦ Lp.compMeasurePreserving (g z) (hgm z) (f z)) s z :=
  Tendsto.compMeasurePreservingLp hf hg _ _ hp

theorem ContinuousAt.compMeasurePreservingLp (hf : ContinuousAt f z)
    (hg : ContinuousAt g z) (hgm : âˆ€ z, MeasurePreserving (g z) Î¼ Î½) (hp : p â‰  âˆ) :
    ContinuousAt (fun z â†¦ Lp.compMeasurePreserving (g z) (hgm z) (f z)) z :=
  Tendsto.compMeasurePreservingLp hf hg _ _ hp

theorem ContinuousOn.compMeasurePreservingLp (hf : ContinuousOn f s)
    (hg : ContinuousOn g s) (hgm : âˆ€ z, MeasurePreserving (g z) Î¼ Î½) (hp : p â‰  âˆ) :
    ContinuousOn (fun z â†¦ Lp.compMeasurePreserving (g z) (hgm z) (f z)) s := fun z hz â†¦
  (hf z hz).compMeasurePreservingLp (hg z hz) hgm hp

theorem Continuous.compMeasurePreservingLp (hf : Continuous f) (hg : Continuous g)
    (hgm : âˆ€ z, MeasurePreserving (g z) Î¼ Î½) (hp : p â‰  âˆ) :
    Continuous (fun z â†¦ Lp.compMeasurePreserving (g z) (hgm z) (f z)) :=
  continuous_iff_continuousAt.mpr fun _ â†¦
    hf.continuousAt.compMeasurePreservingLp hg.continuousAt hgm hp
