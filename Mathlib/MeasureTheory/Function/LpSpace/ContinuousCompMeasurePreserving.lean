/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp
import Mathlib.MeasureTheory.Measure.ContinuousPreimage

/-!
# Continuity of `MeasureTheory.Lp.compMeasurePreserving`

In this file we prove that the composition of an `L^p` function `g`
with a continuous measure-preserving map `f` is continuous in both arguments.

First, we prove it for indicator functions,
in terms of convergence of `Œº ((f a ‚Åª¬π' s) ‚àÜ (g ‚Åª¬π' s))` to zero.

Then we prove the continuity of the function of two arguments
defined on `MeasureTheory.Lp E p ŒΩ √ó {f : C(X, Y) // MeasurePreserving f Œº ŒΩ}`.

Finally, we provide dot notation convenience lemmas.
-/

open Filter Set MeasureTheory
open scoped ENNReal Topology symmDiff

variable {X Y : Type*}
  [TopologicalSpace X] [MeasurableSpace X] [BorelSpace X] [R1Space X]
  [TopologicalSpace Y] [MeasurableSpace Y] [BorelSpace Y] [R1Space Y]
  {Œº : Measure X} {ŒΩ : Measure Y} [Œº.InnerRegularCompactLTTop] [IsLocallyFiniteMeasure ŒΩ]

namespace MeasureTheory
namespace Lp

variable (Œº ŒΩ)
variable (E : Type*) [NormedAddCommGroup E] {p : ‚Ñù‚â•0‚àû} [Fact (1 ‚â§ p)]

/-- Let `X` and `Y` be R‚ÇÅ topological spaces
with Borel œÉ-algebras and measures `Œº` and `ŒΩ`, respectively.
Suppose that `Œº` is inner regular for finite measure sets with respect to compact sets
and `ŒΩ` is a locally finite measure.
Let `1 ‚â§ p < ‚àû` be an extended nonnegative real number.
Then the composition of a function `g : Lp E p ŒΩ`
and a measure-preserving continuous function `f : C(X, Y)`
is continuous in both variables. -/
theorem compMeasurePreserving_continuous (hp : p ‚â† ‚àû) :
    Continuous fun gf : Lp E p ŒΩ √ó {f : C(X, Y) // MeasurePreserving f Œº ŒΩ} ‚Ü¶
      compMeasurePreserving gf.2.1 gf.2.2 gf.1 := by
  have hp‚ÇÄ : p ‚â† 0 := (one_pos.trans_le Fact.out).ne'
  refine continuous_prod_of_dense_continuous_lipschitzWith _ 1
    (MeasureTheory.Lp.simpleFunc.dense hp) ?_ fun f ‚Ü¶ (isometry_compMeasurePreserving f.2).lipschitz
  intro f hf
  lift f to Lp.simpleFunc E p ŒΩ using hf
  induction f using Lp.simpleFunc.induction hp‚ÇÄ hp with
  | add hfp hgp _ ihf ihg => exact ihf.add ihg
  | @indicatorConst c s hs hŒΩs =>
    dsimp only [Lp.simpleFunc.coe_indicatorConst, Lp.indicatorConstLp_compMeasurePreserving]
    refine continuous_indicatorConstLp_set hp fun f ‚Ü¶ ?_
    apply tendsto_measure_symmDiff_preimage_nhds_zero continuousAt_subtype_val _ f.2
      hs.nullMeasurableSet hŒΩs.ne
    exact .of_forall Subtype.property

end Lp

end MeasureTheory

variable {E : Type*} [NormedAddCommGroup E] {p : ‚Ñù‚â•0‚àû} [Fact (1 ‚â§ p)]

theorem Filter.Tendsto.compMeasurePreservingLp {Œ± : Type*} {l : Filter Œ±}
    {f : Œ± ‚Üí Lp E p ŒΩ} {f‚ÇÄ : Lp E p ŒΩ} {g : Œ± ‚Üí C(X, Y)} {g‚ÇÄ : C(X, Y)}
    (hf : Tendsto f l (ùìù f‚ÇÄ)) (hg : Tendsto g l (ùìù g‚ÇÄ))
    (hgm : ‚àÄ a, MeasurePreserving (g a) Œº ŒΩ) (hgm‚ÇÄ : MeasurePreserving g‚ÇÄ Œº ŒΩ) (hp : p ‚â† ‚àû) :
    Tendsto (fun a ‚Ü¶ Lp.compMeasurePreserving (g a) (hgm a) (f a)) l
      (ùìù (Lp.compMeasurePreserving g‚ÇÄ hgm‚ÇÄ f‚ÇÄ)) := by
  have := (Lp.compMeasurePreserving_continuous Œº ŒΩ E hp).tendsto ‚ü®f‚ÇÄ, g‚ÇÄ, hgm‚ÇÄ‚ü©
  replace hg : Tendsto (fun a ‚Ü¶ ‚ü®g a, hgm a‚ü© : Œ± ‚Üí {g : C(X, Y) // MeasurePreserving g Œº ŒΩ})
      l (ùìù ‚ü®g‚ÇÄ, hgm‚ÇÄ‚ü©) :=
    tendsto_subtype_rng.2 hg
  convert this.comp (hf.prodMk_nhds hg)

variable {Z : Type*} [TopologicalSpace Z] {f : Z ‚Üí Lp E p ŒΩ} {g : Z ‚Üí C(X, Y)} {s : Set Z} {z : Z}

theorem ContinuousWithinAt.compMeasurePreservingLp (hf : ContinuousWithinAt f s z)
    (hg : ContinuousWithinAt g s z) (hgm : ‚àÄ z, MeasurePreserving (g z) Œº ŒΩ) (hp : p ‚â† ‚àû) :
    ContinuousWithinAt (fun z ‚Ü¶ Lp.compMeasurePreserving (g z) (hgm z) (f z)) s z :=
  Tendsto.compMeasurePreservingLp hf hg _ _ hp

theorem ContinuousAt.compMeasurePreservingLp (hf : ContinuousAt f z)
    (hg : ContinuousAt g z) (hgm : ‚àÄ z, MeasurePreserving (g z) Œº ŒΩ) (hp : p ‚â† ‚àû) :
    ContinuousAt (fun z ‚Ü¶ Lp.compMeasurePreserving (g z) (hgm z) (f z)) z :=
  Tendsto.compMeasurePreservingLp hf hg _ _ hp

theorem ContinuousOn.compMeasurePreservingLp (hf : ContinuousOn f s)
    (hg : ContinuousOn g s) (hgm : ‚àÄ z, MeasurePreserving (g z) Œº ŒΩ) (hp : p ‚â† ‚àû) :
    ContinuousOn (fun z ‚Ü¶ Lp.compMeasurePreserving (g z) (hgm z) (f z)) s := fun z hz ‚Ü¶
  (hf z hz).compMeasurePreservingLp (hg z hz) hgm hp

theorem Continuous.compMeasurePreservingLp (hf : Continuous f) (hg : Continuous g)
    (hgm : ‚àÄ z, MeasurePreserving (g z) Œº ŒΩ) (hp : p ‚â† ‚àû) :
    Continuous (fun z ‚Ü¶ Lp.compMeasurePreserving (g z) (hgm z) (f z)) :=
  continuous_iff_continuousAt.mpr fun _ ‚Ü¶
    hf.continuousAt.compMeasurePreservingLp hg.continuousAt hgm hp
