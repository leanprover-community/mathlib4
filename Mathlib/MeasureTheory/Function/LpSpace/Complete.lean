/-
Copyright (c) 2020 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.MeasureTheory.Function.LpSpace.Basic

/-!
# `Lp` is a complete space

In this file we show that `Lp` is a complete space for `1 â‰¤ p`,
in `MeasureTheory.Lp.instCompleteSpace`.
-/

open MeasureTheory Filter
open scoped ENNReal Topology

variable {Î± E : Type*} {m : MeasurableSpace Î±} {p : â„â‰¥0âˆ} {Î¼ : Measure Î±} [NormedAddCommGroup E]

namespace MeasureTheory.Lp

theorem eLpNorm'_lim_eq_lintegral_liminf {Î¹} [Nonempty Î¹] [LinearOrder Î¹] {f : Î¹ â†’ Î± â†’ E} {p : â„}
    {f_lim : Î± â†’ E} (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    eLpNorm' f_lim p Î¼ = (âˆ«â» a, atTop.liminf (â€–f Â· aâ€–â‚‘ ^ p) âˆ‚Î¼) ^ (1 / p) := by
  suffices h_no_pow : (âˆ«â» a, â€–f_lim aâ€–â‚‘ ^ p âˆ‚Î¼) = âˆ«â» a, atTop.liminf fun m => â€–f m aâ€–â‚‘ ^ p âˆ‚Î¼ by
    rw [eLpNorm'_eq_lintegral_enorm, h_no_pow]
  refine lintegral_congr_ae (h_lim.mono fun a ha => ?_)
  dsimp only
  rw [Tendsto.liminf_eq]
  refine (ENNReal.continuous_rpow_const.tendsto â€–f_lim aâ€–â‚Š).comp ?_
  exact (continuous_enorm.tendsto (f_lim a)).comp ha

theorem eLpNorm'_lim_le_liminf_eLpNorm' {f : â„• â†’ Î± â†’ E} {p : â„}
    (hp_pos : 0 < p) (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) {f_lim : Î± â†’ E}
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    eLpNorm' f_lim p Î¼ â‰¤ atTop.liminf fun n => eLpNorm' (f n) p Î¼ := by
  rw [eLpNorm'_lim_eq_lintegral_liminf h_lim]
  rw [one_div, â† ENNReal.le_rpow_inv_iff (by simp [hp_pos] : 0 < pâ»Â¹), inv_inv]
  refine (lintegral_liminf_le' fun m => (hf m).enorm.pow_const _).trans_eq ?_
  have h_pow_liminf :
    atTop.liminf (fun n â†¦ eLpNorm' (f n) p Î¼) ^ p
      = atTop.liminf fun n â†¦ eLpNorm' (f n) p Î¼ ^ p := by
    have h_rpow_mono := ENNReal.strictMono_rpow_of_pos hp_pos
    have h_rpow_surj := (ENNReal.rpow_left_bijective hp_pos.ne.symm).2
    refine (h_rpow_mono.orderIsoOfSurjective _ h_rpow_surj).liminf_apply ?_ ?_ ?_ ?_
    all_goals isBoundedDefault
  rw [h_pow_liminf]
  simp_rw [eLpNorm'_eq_lintegral_enorm, â† ENNReal.rpow_mul, one_div,
    inv_mul_cancelâ‚€ hp_pos.ne.symm, ENNReal.rpow_one]

theorem eLpNorm_exponent_top_lim_eq_essSup_liminf {Î¹} [Nonempty Î¹] [LinearOrder Î¹] {f : Î¹ â†’ Î± â†’ E}
    {f_lim : Î± â†’ E} (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    eLpNorm f_lim âˆ Î¼ = essSup (fun x => atTop.liminf fun m => â€–f m xâ€–â‚‘) Î¼ := by
  rw [eLpNorm_exponent_top, eLpNormEssSup_eq_essSup_enorm]
  refine essSup_congr_ae (h_lim.mono fun x hx => ?_)
  dsimp only
  apply (Tendsto.liminf_eq ..).symm
  exact (continuous_enorm.tendsto (f_lim x)).comp hx

theorem eLpNorm_exponent_top_lim_le_liminf_eLpNorm_exponent_top {Î¹} [Nonempty Î¹] [Countable Î¹]
    [LinearOrder Î¹] {f : Î¹ â†’ Î± â†’ E} {f_lim : Î± â†’ E}
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    eLpNorm f_lim âˆ Î¼ â‰¤ atTop.liminf fun n => eLpNorm (f n) âˆ Î¼ := by
  rw [eLpNorm_exponent_top_lim_eq_essSup_liminf h_lim]
  simp_rw [eLpNorm_exponent_top, eLpNormEssSup]
  exact ENNReal.essSup_liminf_le _

theorem eLpNorm_lim_le_liminf_eLpNorm {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (f_lim : Î± â†’ E)
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    eLpNorm f_lim p Î¼ â‰¤ atTop.liminf fun n => eLpNorm (f n) p Î¼ := by
  obtain rfl | hp0 := eq_or_ne p 0
  Â· simp
  by_cases hp_top : p = âˆ
  Â· simp_rw [hp_top]
    exact eLpNorm_exponent_top_lim_le_liminf_eLpNorm_exponent_top h_lim
  simp_rw [eLpNorm_eq_eLpNorm' hp0 hp_top]
  have hp_pos : 0 < p.toReal := ENNReal.toReal_pos hp0 hp_top
  exact eLpNorm'_lim_le_liminf_eLpNorm' hp_pos hf h_lim

/-! ### `Lp` is complete iff Cauchy sequences of `â„’p` have limits in `â„’p` -/


theorem tendsto_Lp_iff_tendsto_eLpNorm' {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] (f : Î¹ â†’ Lp E p Î¼)
    (f_lim : Lp E p Î¼) :
    fi.Tendsto f (ğ“ f_lim) â†” fi.Tendsto (fun n => eLpNorm (â‡‘(f n) - â‡‘f_lim) p Î¼) (ğ“ 0) := by
  rw [tendsto_iff_dist_tendsto_zero]
  simp_rw [dist_def]
  rw [â† ENNReal.toReal_zero, ENNReal.tendsto_toReal_iff (fun n => ?_) ENNReal.zero_ne_top]
  rw [eLpNorm_congr_ae (Lp.coeFn_sub _ _).symm]
  exact Lp.eLpNorm_ne_top _

theorem tendsto_Lp_iff_tendsto_eLpNorm {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] (f : Î¹ â†’ Lp E p Î¼)
    (f_lim : Î± â†’ E) (f_lim_â„’p : MemLp f_lim p Î¼) :
    fi.Tendsto f (ğ“ (f_lim_â„’p.toLp f_lim)) â†”
      fi.Tendsto (fun n => eLpNorm (â‡‘(f n) - f_lim) p Î¼) (ğ“ 0) := by
  rw [tendsto_Lp_iff_tendsto_eLpNorm']
  suffices h_eq :
      (fun n => eLpNorm (â‡‘(f n) - â‡‘(MemLp.toLp f_lim f_lim_â„’p)) p Î¼) =
        (fun n => eLpNorm (â‡‘(f n) - f_lim) p Î¼) by
    rw [h_eq]
  exact funext fun n => eLpNorm_congr_ae (EventuallyEq.rfl.sub (MemLp.coeFn_toLp f_lim_â„’p))

theorem tendsto_Lp_iff_tendsto_eLpNorm'' {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] (f : Î¹ â†’ Î± â†’ E)
    (f_â„’p : âˆ€ n, MemLp (f n) p Î¼) (f_lim : Î± â†’ E) (f_lim_â„’p : MemLp f_lim p Î¼) :
    fi.Tendsto (fun n => (f_â„’p n).toLp (f n)) (ğ“ (f_lim_â„’p.toLp f_lim)) â†”
      fi.Tendsto (fun n => eLpNorm (f n - f_lim) p Î¼) (ğ“ 0) := by
  rw [Lp.tendsto_Lp_iff_tendsto_eLpNorm' (fun n => (f_â„’p n).toLp (f n)) (f_lim_â„’p.toLp f_lim)]
  refine Filter.tendsto_congr fun n => ?_
  apply eLpNorm_congr_ae
  filter_upwards [((f_â„’p n).sub f_lim_â„’p).coeFn_toLp,
    Lp.coeFn_sub ((f_â„’p n).toLp (f n)) (f_lim_â„’p.toLp f_lim)] with _ hxâ‚ hxâ‚‚
  rw [â† hxâ‚‚]
  exact hxâ‚


theorem tendsto_Lp_of_tendsto_eLpNorm {Î¹} {fi : Filter Î¹} [Fact (1 â‰¤ p)] {f : Î¹ â†’ Lp E p Î¼}
    (f_lim : Î± â†’ E) (f_lim_â„’p : MemLp f_lim p Î¼)
    (h_tendsto : fi.Tendsto (fun n => eLpNorm (â‡‘(f n) - f_lim) p Î¼) (ğ“ 0)) :
    fi.Tendsto f (ğ“ (f_lim_â„’p.toLp f_lim)) :=
  (tendsto_Lp_iff_tendsto_eLpNorm f f_lim f_lim_â„’p).mpr h_tendsto

theorem cauchySeq_Lp_iff_cauchySeq_eLpNorm {Î¹} [Nonempty Î¹] [SemilatticeSup Î¹] [hp : Fact (1 â‰¤ p)]
    (f : Î¹ â†’ Lp E p Î¼) :
    CauchySeq f â†” Tendsto (fun n : Î¹ Ã— Î¹ => eLpNorm (â‡‘(f n.fst) - â‡‘(f n.snd)) p Î¼) atTop (ğ“ 0) := by
  simp_rw [cauchySeq_iff_tendsto_dist_atTop_0, dist_def]
  rw [â† ENNReal.toReal_zero, ENNReal.tendsto_toReal_iff (fun n => ?_) ENNReal.zero_ne_top]
  rw [eLpNorm_congr_ae (Lp.coeFn_sub _ _).symm]
  exact eLpNorm_ne_top _

theorem completeSpace_lp_of_cauchy_complete_eLpNorm [hp : Fact (1 â‰¤ p)]
    (H :
      âˆ€ (f : â„• â†’ Î± â†’ E) (_ : âˆ€ n, MemLp (f n) p Î¼) (B : â„• â†’ â„â‰¥0âˆ) (_ : âˆ‘' i, B i < âˆ)
        (_ : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm (f n - f m) p Î¼ < B N),
        âˆƒ (f_lim : Î± â†’ E), MemLp f_lim p Î¼ âˆ§
          atTop.Tendsto (fun n => eLpNorm (f n - f_lim) p Î¼) (ğ“ 0)) :
    CompleteSpace (Lp E p Î¼) := by
  let B := fun n : â„• => ((1 : â„) / 2) ^ n
  have hB_pos : âˆ€ n, 0 < B n := fun n => pow_pos (div_pos zero_lt_one zero_lt_two) n
  refine Metric.complete_of_convergent_controlled_sequences B hB_pos fun f hf => ?_
  rsuffices âŸ¨f_lim, hf_lim_meas, h_tendstoâŸ© :
    âˆƒ (f_lim : Î± â†’ E), MemLp f_lim p Î¼ âˆ§
      atTop.Tendsto (fun n => eLpNorm (â‡‘(f n) - f_lim) p Î¼) (ğ“ 0)
  Â· exact âŸ¨hf_lim_meas.toLp f_lim, tendsto_Lp_of_tendsto_eLpNorm f_lim hf_lim_meas h_tendstoâŸ©
  obtain âŸ¨M, hBâŸ© : Summable B := summable_geometric_two
  let B1 n := ENNReal.ofReal (B n)
  have hB1_has : HasSum B1 (ENNReal.ofReal M) := by
    have h_tsum_B1 : âˆ‘' i, B1 i = ENNReal.ofReal M := by
      change (âˆ‘' n : â„•, ENNReal.ofReal (B n)) = ENNReal.ofReal M
      rw [â† hB.tsum_eq]
      exact (ENNReal.ofReal_tsum_of_nonneg (fun n => le_of_lt (hB_pos n)) hB.summable).symm
    have h_sum := (@ENNReal.summable _ B1).hasSum
    rwa [h_tsum_B1] at h_sum
  have hB1 : âˆ‘' i, B1 i < âˆ := by
    rw [hB1_has.tsum_eq]
    exact ENNReal.ofReal_lt_top
  let f1 : â„• â†’ Î± â†’ E := fun n => f n
  refine H f1 (fun n => Lp.memLp (f n)) B1 hB1 fun N n m hn hm => ?_
  specialize hf N n m hn hm
  rw [dist_def] at hf
  dsimp only [f1]
  rwa [ENNReal.lt_ofReal_iff_toReal_lt]
  rw [eLpNorm_congr_ae (Lp.coeFn_sub _ _).symm]
  exact Lp.eLpNorm_ne_top _

/-! ### Prove that controlled Cauchy sequences of `â„’p` have limits in `â„’p` -/

private theorem eLpNorm'_sum_norm_sub_le_tsum_of_cauchy_eLpNorm' {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ}
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm' (f n - f m) p Î¼ < B N) (n : â„•) :
    eLpNorm' (fun x => âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) p Î¼ â‰¤ âˆ‘' i, B i := by
  let f_norm_diff i x := â€–f (i + 1) x - f i xâ€–
  have hgf_norm_diff :
    âˆ€ n,
      (fun x => âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) =
        âˆ‘ i âˆˆ Finset.range (n + 1), f_norm_diff i :=
    fun n => funext fun x => by simp [f_norm_diff]
  rw [hgf_norm_diff]
  refine (eLpNorm'_sum_le (fun i _ => ((hf (i + 1)).sub (hf i)).norm) hp1).trans ?_
  simp_rw [eLpNorm'_norm]
  refine (Finset.sum_le_sum ?_).trans <| ENNReal.sum_le_tsum _
  exact fun m _ => (h_cau m (m + 1) m (Nat.le_succ m) (le_refl m)).le

private theorem lintegral_rpow_sum_enorm_sub_le_rpow_tsum
    {f : â„• â†’ Î± â†’ E} {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (n : â„•)
    (hn : eLpNorm' (fun x => âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) p Î¼ â‰¤ âˆ‘' i, B i) :
    (âˆ«â» a, (âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼) â‰¤ (âˆ‘' i, B i) ^ p := by
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1
  rw [â† inv_inv p, @ENNReal.le_rpow_inv_iff _ _ pâ»Â¹ (by simp [hp_pos]), inv_inv p]
  simp_rw [eLpNorm'_eq_lintegral_enorm, one_div] at hn
  have h_nnnorm_nonneg :
    (fun a => â€–âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â€–â‚‘ ^ p) = fun a =>
      (âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚‘) ^ p := by
    ext1 a
    congr
    simp_rw [â† ofReal_norm_eq_enorm]
    rw [â† ENNReal.ofReal_sum_of_nonneg]
    Â· rw [Real.norm_of_nonneg _]
      exact Finset.sum_nonneg fun x _ => norm_nonneg _
    Â· exact fun x _ => norm_nonneg _
  rwa [h_nnnorm_nonneg] at hn

private theorem lintegral_rpow_tsum_coe_enorm_sub_le_tsum {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ}
    (h : âˆ€ n, âˆ«â» a, (âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼ â‰¤ (âˆ‘' i, B i) ^ p) :
    (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼) ^ (1 / p) â‰¤ âˆ‘' i, B i := by
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1
  suffices h_pow : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼) â‰¤ (âˆ‘' i, B i) ^ p by
      rwa [one_div, â† ENNReal.le_rpow_inv_iff (by simp [hp_pos] : 0 < pâ»Â¹), inv_inv]
  have h_tsum_1 :
    âˆ€ g : â„• â†’ â„â‰¥0âˆ, âˆ‘' i, g i = atTop.liminf fun n => âˆ‘ i âˆˆ Finset.range (n + 1), g i := by
    intro g
    rw [ENNReal.tsum_eq_liminf_sum_nat, â† liminf_nat_add _ 1]
  simp_rw [h_tsum_1 _]
  rw [â† h_tsum_1]
  have h_liminf_pow :
    âˆ«â» a, (atTop.liminf fun n => âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼ =
      âˆ«â» a, atTop.liminf fun n => (âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼ := by
    refine lintegral_congr fun x => ?_
    have h_rpow_mono := ENNReal.strictMono_rpow_of_pos (zero_lt_one.trans_le hp1)
    have h_rpow_surj := (ENNReal.rpow_left_bijective hp_pos.ne.symm).2
    refine (h_rpow_mono.orderIsoOfSurjective _ h_rpow_surj).liminf_apply ?_ ?_ ?_ ?_
    all_goals isBoundedDefault
  rw [h_liminf_pow]
  refine (lintegral_liminf_le' fun n â†¦ ?_).trans <| liminf_le_of_frequently_le' <| .of_forall h
  exact ((Finset.range _).aemeasurable_fun_sum fun i _ â†¦ ((hf _).sub (hf i)).enorm).pow_const _

private theorem tsum_enorm_sub_ae_lt_top {f : â„• â†’ Î± â†’ E} (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    {p : â„} (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼) ^ (1 / p) â‰¤ âˆ‘' i, B i) :
    âˆ€áµ x âˆ‚Î¼, âˆ‘' i, â€–f (i + 1) x - f i xâ€–â‚‘ < âˆ := by
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1
  have h_integral : âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼ < âˆ := by
    have h_tsum_lt_top : (âˆ‘' i, B i) ^ p < âˆ := ENNReal.rpow_lt_top_of_nonneg hp_pos.le hB
    refine lt_of_le_of_lt ?_ h_tsum_lt_top
    rwa [one_div, â† ENNReal.le_rpow_inv_iff (by simp [hp_pos] : 0 < pâ»Â¹), inv_inv] at h
  have rpow_ae_lt_top : âˆ€áµ x âˆ‚Î¼, (âˆ‘' i, â€–f (i + 1) x - f i xâ€–â‚‘) ^ p < âˆ := by
    refine ae_lt_top' (AEMeasurable.pow_const ?_ _) h_integral.ne
    exact AEMeasurable.ennreal_tsum fun n => ((hf (n + 1)).sub (hf n)).enorm
  refine rpow_ae_lt_top.mono fun x hx => ?_
  rwa [â† ENNReal.lt_rpow_inv_iff hp_pos,
    ENNReal.top_rpow_of_pos (by simp [hp_pos] : 0 < pâ»Â¹)] at hx

theorem ae_tendsto_of_cauchy_eLpNorm' [CompleteSpace E] {f : â„• â†’ Î± â†’ E} {p : â„}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hp1 : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm' (f n - f m) p Î¼ < B N) :
    âˆ€áµ x âˆ‚Î¼, âˆƒ l : E, atTop.Tendsto (fun n => f n x) (ğ“ l) := by
  have h_summable : âˆ€áµ x âˆ‚Î¼, Summable fun i : â„• => f (i + 1) x - f i x := by
    have h1 :
      âˆ€ n, eLpNorm' (fun x => âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) x - f i xâ€–) p Î¼ â‰¤ âˆ‘' i, B i :=
      eLpNorm'_sum_norm_sub_le_tsum_of_cauchy_eLpNorm' hf hp1 h_cau
    have h2 n :
        âˆ«â» a, (âˆ‘ i âˆˆ Finset.range (n + 1), â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼ â‰¤ (âˆ‘' i, B i) ^ p :=
      lintegral_rpow_sum_enorm_sub_le_rpow_tsum hp1 n (h1 n)
    have h3 : (âˆ«â» a, (âˆ‘' i, â€–f (i + 1) a - f i aâ€–â‚‘) ^ p âˆ‚Î¼) ^ (1 / p) â‰¤ âˆ‘' i, B i :=
      lintegral_rpow_tsum_coe_enorm_sub_le_tsum hf hp1 h2
    have h4 : âˆ€áµ x âˆ‚Î¼, âˆ‘' i, â€–f (i + 1) x - f i xâ€–â‚‘ < âˆ :=
      tsum_enorm_sub_ae_lt_top hf hp1 hB h3
    exact h4.mono fun x hx => .of_nnnorm <| ENNReal.tsum_coe_ne_top_iff_summable.mp hx.ne
  have h :
    âˆ€áµ x âˆ‚Î¼, âˆƒ l : E,
      atTop.Tendsto (fun n => âˆ‘ i âˆˆ Finset.range n, (f (i + 1) x - f i x)) (ğ“ l) := by
    refine h_summable.mono fun x hx => ?_
    let hx_sum := hx.hasSum.tendsto_sum_nat
    exact âŸ¨âˆ‘' i, (f (i + 1) x - f i x), hx_sumâŸ©
  refine h.mono fun x hx => ?_
  obtain âŸ¨l, hxâŸ© := hx
  have h_rw_sum :
      (fun n => âˆ‘ i âˆˆ Finset.range n, (f (i + 1) x - f i x)) = fun n => f n x - f 0 x := by
    ext1 n
    change
      (âˆ‘ i âˆˆ Finset.range n, ((fun m => f m x) (i + 1) - (fun m => f m x) i)) = f n x - f 0 x
    rw [Finset.sum_range_sub (fun m => f m x)]
  rw [h_rw_sum] at hx
  have hf_rw : (fun n => f n x) = fun n => f n x - f 0 x + f 0 x := by
    ext1 n
    abel
  rw [hf_rw]
  exact âŸ¨l + f 0 x, Tendsto.add_const _ hxâŸ©

theorem ae_tendsto_of_cauchy_eLpNorm [CompleteSpace E] {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hp : 1 â‰¤ p) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm (f n - f m) p Î¼ < B N) :
    âˆ€áµ x âˆ‚Î¼, âˆƒ l : E, atTop.Tendsto (fun n => f n x) (ğ“ l) := by
  by_cases hp_top : p = âˆ
  Â· simp_rw [hp_top] at *
    have h_cau_ae : âˆ€áµ x âˆ‚Î¼, âˆ€ N n m, N â‰¤ n â†’ N â‰¤ m â†’ â€–(f n - f m) xâ€–â‚‘ < B N := by
      simp_rw [ae_all_iff]
      exact fun N n m hnN hmN => ae_lt_of_essSup_lt (h_cau N n m hnN hmN)
    simp_rw [eLpNorm_exponent_top, eLpNormEssSup] at h_cau
    refine h_cau_ae.mono fun x hx => cauchySeq_tendsto_of_complete ?_
    refine cauchySeq_of_le_tendsto_0 (fun n => (B n).toReal) ?_ ?_
    Â· intro n m N hnN hmN
      specialize hx N n m hnN hmN
      rw [_root_.dist_eq_norm,
        â† ENNReal.ofReal_le_iff_le_toReal (ENNReal.ne_top_of_tsum_ne_top hB N),
        ofReal_norm_eq_enorm]
      exact hx.le
    Â· rw [â† ENNReal.toReal_zero]
      exact
        Tendsto.comp (g := ENNReal.toReal) (ENNReal.tendsto_toReal ENNReal.zero_ne_top)
          (ENNReal.tendsto_atTop_zero_of_tsum_ne_top hB)
  have hp1 : 1 â‰¤ p.toReal := by
    rw [â† ENNReal.ofReal_le_iff_le_toReal hp_top, ENNReal.ofReal_one]
    exact hp
  have h_cau' : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm' (f n - f m) p.toReal Î¼ < B N := by
    intro N n m hn hm
    specialize h_cau N n m hn hm
    rwa [eLpNorm_eq_eLpNorm' (zero_lt_one.trans_le hp).ne.symm hp_top] at h_cau
  exact ae_tendsto_of_cauchy_eLpNorm' hf hp1 hB h_cau'

theorem cauchy_tendsto_of_tendsto {f : â„• â†’ Î± â†’ E} (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (f_lim : Î± â†’ E) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm (f n - f m) p Î¼ < B N)
    (h_lim : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x))) :
    atTop.Tendsto (fun n => eLpNorm (f n - f_lim) p Î¼) (ğ“ 0) := by
  rw [ENNReal.tendsto_atTop_zero]
  intro Îµ hÎµ
  have h_B : âˆƒ N : â„•, B N â‰¤ Îµ := by
    suffices h_tendsto_zero : âˆƒ N : â„•, âˆ€ n : â„•, N â‰¤ n â†’ B n â‰¤ Îµ from
      âŸ¨h_tendsto_zero.choose, h_tendsto_zero.choose_spec _ le_rflâŸ©
    exact (ENNReal.tendsto_atTop_zero.mp (ENNReal.tendsto_atTop_zero_of_tsum_ne_top hB)) Îµ hÎµ
  obtain âŸ¨N, h_BâŸ© := h_B
  refine âŸ¨N, fun n hn => ?_âŸ©
  have h_sub : eLpNorm (f n - f_lim) p Î¼ â‰¤ atTop.liminf fun m => eLpNorm (f n - f m) p Î¼ := by
    refine eLpNorm_lim_le_liminf_eLpNorm (fun m => (hf n).sub (hf m)) (f n - f_lim) ?_
    refine h_lim.mono fun x hx => ?_
    simp_rw [sub_eq_add_neg]
    exact Tendsto.add tendsto_const_nhds (Tendsto.neg hx)
  refine h_sub.trans ?_
  refine liminf_le_of_frequently_le' (frequently_atTop.mpr ?_)
  refine fun N1 => âŸ¨max N N1, le_max_right _ _, ?_âŸ©
  exact (h_cau N n (max N N1) hn (le_max_left _ _)).le.trans h_B

theorem memLp_of_cauchy_tendsto (hp : 1 â‰¤ p) {f : â„• â†’ Î± â†’ E} (hf : âˆ€ n, MemLp (f n) p Î¼)
    (f_lim : Î± â†’ E) (h_lim_meas : AEStronglyMeasurable f_lim Î¼)
    (h_tendsto : atTop.Tendsto (fun n => eLpNorm (f n - f_lim) p Î¼) (ğ“ 0)) : MemLp f_lim p Î¼ := by
  refine âŸ¨h_lim_meas, ?_âŸ©
  rw [ENNReal.tendsto_atTop_zero] at h_tendsto
  obtain âŸ¨N, h_tendsto_1âŸ© := h_tendsto 1 zero_lt_one
  specialize h_tendsto_1 N (le_refl N)
  have h_add : f_lim = f_lim - f N + f N := by abel
  rw [h_add]
  refine lt_of_le_of_lt (eLpNorm_add_le (h_lim_meas.sub (hf N).1) (hf N).1 hp) ?_
  rw [ENNReal.add_lt_top]
  constructor
  Â· refine lt_of_le_of_lt ?_ ENNReal.one_lt_top
    have h_neg : f_lim - f N = -(f N - f_lim) := by simp
    rwa [h_neg, eLpNorm_neg]
  Â· exact (hf N).2

theorem cauchy_complete_eLpNorm [CompleteSpace E] (hp : 1 â‰¤ p) {f : â„• â†’ Î± â†’ E}
    (hf : âˆ€ n, MemLp (f n) p Î¼) {B : â„• â†’ â„â‰¥0âˆ} (hB : âˆ‘' i, B i â‰  âˆ)
    (h_cau : âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ eLpNorm (f n - f m) p Î¼ < B N) :
    âˆƒ (f_lim : Î± â†’ E), MemLp f_lim p Î¼ âˆ§
      atTop.Tendsto (fun n => eLpNorm (f n - f_lim) p Î¼) (ğ“ 0) := by
  obtain âŸ¨f_lim, h_f_lim_meas, h_limâŸ© :
      âˆƒ f_lim : Î± â†’ E, StronglyMeasurable f_lim âˆ§
        âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (f_lim x)) :=
    exists_stronglyMeasurable_limit_of_tendsto_ae (fun n => (hf n).1)
      (ae_tendsto_of_cauchy_eLpNorm (fun n => (hf n).1) hp hB h_cau)
  have h_tendsto' : atTop.Tendsto (fun n => eLpNorm (f n - f_lim) p Î¼) (ğ“ 0) :=
    cauchy_tendsto_of_tendsto (fun m => (hf m).1) f_lim hB h_cau h_lim
  have h_â„’p_lim : MemLp f_lim p Î¼ :=
    memLp_of_cauchy_tendsto hp hf f_lim h_f_lim_meas.aestronglyMeasurable h_tendsto'
  exact âŸ¨f_lim, h_â„’p_lim, h_tendsto'âŸ©

/-- `Lp` is complete for `1 â‰¤ p`. -/
instance instCompleteSpace [CompleteSpace E] [hp : Fact (1 â‰¤ p)] : CompleteSpace (Lp E p Î¼) :=
  completeSpace_lp_of_cauchy_complete_eLpNorm fun _f hf _B hB h_cau =>
    cauchy_complete_eLpNorm hp.elim hf hB.ne h_cau

end MeasureTheory.Lp
