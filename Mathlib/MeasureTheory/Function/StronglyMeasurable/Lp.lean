/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Lemmas

/-!
# Finitely strongly measurable functions in `Lp`

Functions in `Lp` for `0 < p < ‚àû` are finitely strongly measurable.

## Main statements

* `MemLp.aefinStronglyMeasurable`: if `MemLp f p Œº` with `0 < p < ‚àû`, then
  `AEFinStronglyMeasurable f Œº`.
* `Lp.finStronglyMeasurable`: for `0 < p < ‚àû`, `Lp` functions are finitely strongly measurable.

## References

* [Hyt√∂nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.][Hyt√∂nen_VanNeerven_Veraar_Wies_2016]

-/


open MeasureTheory Filter TopologicalSpace Function

open scoped ENNReal Topology MeasureTheory

namespace MeasureTheory

local infixr:25 " ‚Üí‚Çõ " => SimpleFunc

variable {Œ± G : Type*} {p : ‚Ñù‚â•0‚àû} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} [NormedAddCommGroup G]
  {f : Œ± ‚Üí G}

theorem MemLp.finStronglyMeasurable_of_stronglyMeasurable (hf : MemLp f p Œº)
    (hf_meas : StronglyMeasurable f) (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
    FinStronglyMeasurable f Œº := by
  borelize G
  haveI : SeparableSpace (Set.range f ‚à™ {0} : Set G) :=
    hf_meas.separableSpace_range_union_singleton
  let fs := SimpleFunc.approxOn f hf_meas.measurable (Set.range f ‚à™ {0}) 0 (by simp)
  refine ‚ü®fs, ?_, ?_‚ü©
  ¬∑ have h_fs_Lp : ‚àÄ n, MemLp (fs n) p Œº :=
      SimpleFunc.memLp_approxOn_range hf_meas.measurable hf
    exact fun n => (fs n).measure_support_lt_top_of_memLp (h_fs_Lp n) hp_ne_zero hp_ne_top
  ¬∑ intro x
    apply SimpleFunc.tendsto_approxOn
    apply subset_closure
    simp

@[deprecated (since := "2025-02-21")]
alias Memùìõp.finStronglyMeasurable_of_stronglyMeasurable := MemLp.finStronglyMeasurable_of_stronglyMeasurable

theorem MemLp.aefinStronglyMeasurable (hf : MemLp f p Œº) (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
    AEFinStronglyMeasurable f Œº :=
  ‚ü®hf.aestronglyMeasurable.mk f,
    ((memLp_congr_ae hf.aestronglyMeasurable.ae_eq_mk).mp
          hf).finStronglyMeasurable_of_stronglyMeasurable
      hf.aestronglyMeasurable.stronglyMeasurable_mk hp_ne_zero hp_ne_top,
    hf.aestronglyMeasurable.ae_eq_mk‚ü©

@[deprecated (since := "2025-02-21")]
alias Memùìõp.aefinStronglyMeasurable := MemLp.aefinStronglyMeasurable

theorem Integrable.aefinStronglyMeasurable (hf : Integrable f Œº) : AEFinStronglyMeasurable f Œº :=
  (memLp_one_iff_integrable.mpr hf).aefinStronglyMeasurable one_ne_zero ENNReal.coe_ne_top

theorem Lp.finStronglyMeasurable (f : Lp G p Œº) (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
    FinStronglyMeasurable f Œº :=
  (Lp.memLp f).finStronglyMeasurable_of_stronglyMeasurable (Lp.stronglyMeasurable f) hp_ne_zero
    hp_ne_top

end MeasureTheory
