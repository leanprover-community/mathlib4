/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.MeasureTheory.Function.SimpleFuncDense

/-!
# Strongly measurable and finitely strongly measurable functions

A function `f` is said to be strongly measurable if `f` is the sequential limit of simple functions.
It is said to be finitely strongly measurable with respect to a measure `Î¼` if the supports
of those simple functions have finite measure. We also provide almost everywhere versions of
these notions.

Almost everywhere strongly measurable functions form the largest class of functions that can be
integrated using the Bochner integral.

If the target space has a second countable topology, strongly measurable and measurable are
equivalent.

If the measure is sigma-finite, strongly measurable and finitely strongly measurable are equivalent.

The main property of finitely strongly measurable functions is
`FinStronglyMeasurable.exists_set_sigmaFinite`: there exists a measurable set `t` such that the
function is supported on `t` and `Î¼.restrict t` is sigma-finite. As a consequence, we can prove some
results for those functions as if the measure was sigma-finite.

## Main definitions

* `StronglyMeasurable f`: `f : Î± â†’ Î²` is the limit of a sequence `fs : â„• â†’ SimpleFunc Î± Î²`.
* `FinStronglyMeasurable f Î¼`: `f : Î± â†’ Î²` is the limit of a sequence `fs : â„• â†’ SimpleFunc Î± Î²`
  such that for all `n âˆˆ â„•`, the measure of the support of `fs n` is finite.
* `AEStronglyMeasurable f Î¼`: `f` is almost everywhere equal to a `StronglyMeasurable` function.
* `AEFinStronglyMeasurable f Î¼`: `f` is almost everywhere equal to a `FinStronglyMeasurable`
  function.

* `AEFinStronglyMeasurable.sigmaFiniteSet`: a measurable set `t` such that
  `f =áµ[Î¼.restrict tá¶œ] 0` and `Î¼.restrict t` is sigma-finite.

## Main statements

* `AEFinStronglyMeasurable.exists_set_sigmaFinite`: there exists a measurable set `t` such that
  `f =áµ[Î¼.restrict tá¶œ] 0` and `Î¼.restrict t` is sigma-finite.

We provide a solid API for strongly measurable functions, and for almost everywhere strongly
measurable functions, as a basis for the Bochner integral.

## References

* HytÃ¶nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.

-/

-- Guard against import creep
assert_not_exists InnerProductSpace

open MeasureTheory Filter TopologicalSpace Function Set MeasureTheory.Measure

open ENNReal Topology MeasureTheory NNReal

variable {Î± Î² Î³ Î¹ : Type*} [Countable Î¹]

namespace MeasureTheory

local infixr:25 " â†’â‚› " => SimpleFunc

section Definitions

variable [TopologicalSpace Î²]

/-- A function is `StronglyMeasurable` if it is the limit of simple functions. -/
def StronglyMeasurable [MeasurableSpace Î±] (f : Î± â†’ Î²) : Prop :=
  âˆƒ fs : â„• â†’ Î± â†’â‚› Î², âˆ€ x, Tendsto (fun n â†¦ fs n x) atTop (ğ“ (f x))

/-- The notation for StronglyMeasurable giving the measurable space instance explicitly. -/
scoped notation "StronglyMeasurable[" m "]" => @MeasureTheory.StronglyMeasurable _ _ _ m

/-- A function is `FinStronglyMeasurable` with respect to a measure if it is the limit of simple
  functions with support with finite measure. -/
def FinStronglyMeasurable [Zero Î²]
    {_ : MeasurableSpace Î±} (f : Î± â†’ Î²) (Î¼ : Measure Î± := by volume_tac) : Prop :=
  âˆƒ fs : â„• â†’ Î± â†’â‚› Î², (âˆ€ n, Î¼ (support (fs n)) < âˆ) âˆ§ âˆ€ x, Tendsto (fun n â†¦ fs n x) atTop (ğ“ (f x))

/-- A function is `AEStronglyMeasurable` with respect to a measure `Î¼` if it is almost everywhere
equal to the limit of a sequence of simple functions. -/
@[fun_prop]
def AEStronglyMeasurable
    {_ : MeasurableSpace Î±} (f : Î± â†’ Î²) (Î¼ : Measure Î± := by volume_tac) : Prop :=
  âˆƒ g, StronglyMeasurable g âˆ§ f =áµ[Î¼] g

/-- A function is `AEFinStronglyMeasurable` with respect to a measure if it is almost everywhere
equal to the limit of a sequence of simple functions with support with finite measure. -/
def AEFinStronglyMeasurable
    [Zero Î²] {_ : MeasurableSpace Î±} (f : Î± â†’ Î²) (Î¼ : Measure Î± := by volume_tac) : Prop :=
  âˆƒ g, FinStronglyMeasurable g Î¼ âˆ§ f =áµ[Î¼] g

end Definitions

open MeasureTheory

/-! ## Strongly measurable functions -/

@[aesop 30% apply (rule_sets := [Measurable])]
protected theorem StronglyMeasurable.aestronglyMeasurable {Î± Î²} {_ : MeasurableSpace Î±}
    [TopologicalSpace Î²] {f : Î± â†’ Î²} {Î¼ : Measure Î±} (hf : StronglyMeasurable f) :
    AEStronglyMeasurable f Î¼ :=
  âŸ¨f, hf, EventuallyEq.refl _ _âŸ©

@[simp]
theorem Subsingleton.stronglyMeasurable {Î± Î²} [MeasurableSpace Î±] [TopologicalSpace Î²]
    [Subsingleton Î²] (f : Î± â†’ Î²) : StronglyMeasurable f := by
  let f_sf : Î± â†’â‚› Î² := âŸ¨f, fun x â†¦ ?_, Set.Subsingleton.finite Set.subsingleton_of_subsingletonâŸ©
  Â· exact âŸ¨fun _ => f_sf, fun x â†¦ tendsto_const_nhdsâŸ©
  Â· have h_univ : f â»Â¹' {x} = Set.univ := by
      ext1 y
      simp [eq_iff_true_of_subsingleton]
    rw [h_univ]
    exact MeasurableSet.univ

theorem SimpleFunc.stronglyMeasurable {Î± Î²} {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    (f : Î± â†’â‚› Î²) : StronglyMeasurable f :=
  âŸ¨fun _ => f, fun _ => tendsto_const_nhdsâŸ©

@[nontriviality]
theorem StronglyMeasurable.of_finite [Finite Î±] {_ : MeasurableSpace Î±}
    [MeasurableSingletonClass Î±] [TopologicalSpace Î²]
    {f : Î± â†’ Î²} : StronglyMeasurable f :=
  âŸ¨fun _ => SimpleFunc.ofFinite f, fun _ => tendsto_const_nhdsâŸ©

@[deprecated (since := "2024-02-05")]
alias stronglyMeasurable_of_fintype := StronglyMeasurable.of_finite

@[deprecated StronglyMeasurable.of_finite (since := "2024-02-06")]
theorem stronglyMeasurable_of_isEmpty [IsEmpty Î±] {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    (f : Î± â†’ Î²) : StronglyMeasurable f :=
  .of_finite

theorem stronglyMeasurable_const {Î± Î²} {_ : MeasurableSpace Î±} [TopologicalSpace Î²] {b : Î²} :
    StronglyMeasurable fun _ : Î± => b :=
  âŸ¨fun _ => SimpleFunc.const Î± b, fun _ => tendsto_const_nhdsâŸ©

@[to_additive]
theorem stronglyMeasurable_one {Î± Î²} {_ : MeasurableSpace Î±} [TopologicalSpace Î²] [One Î²] :
    StronglyMeasurable (1 : Î± â†’ Î²) :=
  stronglyMeasurable_const

/-- A version of `stronglyMeasurable_const` that assumes `f x = f y` for all `x, y`.
This version works for functions between empty types. -/
theorem stronglyMeasurable_const' {Î± Î²} {m : MeasurableSpace Î±} [TopologicalSpace Î²] {f : Î± â†’ Î²}
    (hf : âˆ€ x y, f x = f y) : StronglyMeasurable f := by
  nontriviality Î±
  inhabit Î±
  convert stronglyMeasurable_const (Î² := Î²) using 1
  exact funext fun x â†¦ hf x default

-- Porting note: changed binding type of `MeasurableSpace Î±`.
@[simp]
theorem Subsingleton.stronglyMeasurable' {Î± Î²} [MeasurableSpace Î±] [TopologicalSpace Î²]
    [Subsingleton Î±] (f : Î± â†’ Î²) : StronglyMeasurable f :=
  stronglyMeasurable_const' fun x y => by rw [Subsingleton.elim x y]

namespace StronglyMeasurable

variable {f g : Î± â†’ Î²}

section BasicPropertiesInAnyTopologicalSpace

variable [TopologicalSpace Î²]

/-- A sequence of simple functions such that
`âˆ€ x, Tendsto (fun n â†¦ hf.approx n x) atTop (ğ“ (f x))`.
That property is given by `stronglyMeasurable.tendsto_approx`. -/
protected noncomputable def approx {_ : MeasurableSpace Î±} (hf : StronglyMeasurable f) :
    â„• â†’ Î± â†’â‚› Î² :=
  hf.choose

protected theorem tendsto_approx {_ : MeasurableSpace Î±} (hf : StronglyMeasurable f) :
    âˆ€ x, Tendsto (fun n â†¦ hf.approx n x) atTop (ğ“ (f x)) :=
  hf.choose_spec

/-- Similar to `stronglyMeasurable.approx`, but enforces that the norm of every function in the
sequence is less than `c` everywhere. If `â€–f xâ€– â‰¤ c` this sequence of simple functions verifies
`Tendsto (fun n â†¦ hf.approxBounded n x) atTop (ğ“ (f x))`. -/
noncomputable def approxBounded {_ : MeasurableSpace Î±} [Norm Î²] [SMul â„ Î²]
    (hf : StronglyMeasurable f) (c : â„) : â„• â†’ SimpleFunc Î± Î² := fun n =>
  (hf.approx n).map fun x â†¦ min 1 (c / â€–xâ€–) â€¢ x

theorem tendsto_approxBounded_of_norm_le {Î²} {f : Î± â†’ Î²} [NormedAddCommGroup Î²] [NormedSpace â„ Î²]
    {m : MeasurableSpace Î±} (hf : StronglyMeasurable[m] f) {c : â„} {x : Î±} (hfx : â€–f xâ€– â‰¤ c) :
    Tendsto (fun n â†¦ hf.approxBounded c n x) atTop (ğ“ (f x)) := by
  have h_tendsto := hf.tendsto_approx x
  simp only [StronglyMeasurable.approxBounded, SimpleFunc.coe_map, Function.comp_apply]
  by_cases hfx0 : â€–f xâ€– = 0
  Â· rw [norm_eq_zero] at hfx0
    rw [hfx0] at h_tendsto âŠ¢
    have h_tendsto_norm : Tendsto (fun n â†¦ â€–hf.approx n xâ€–) atTop (ğ“ 0) := by
      convert h_tendsto.norm
      rw [norm_zero]
    refine squeeze_zero_norm (fun n â†¦ ?_) h_tendsto_norm
    calc
      â€–min 1 (c / â€–hf.approx n xâ€–) â€¢ hf.approx n xâ€– =
          â€–min 1 (c / â€–hf.approx n xâ€–)â€– * â€–hf.approx n xâ€– :=
        norm_smul _ _
      _ â‰¤ â€–(1 : â„)â€– * â€–hf.approx n xâ€– := by
        refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
        rw [norm_one, Real.norm_of_nonneg]
        Â· exact min_le_left _ _
        Â· exact le_min zero_le_one (div_nonneg ((norm_nonneg _).trans hfx) (norm_nonneg _))
      _ = â€–hf.approx n xâ€– := by rw [norm_one, one_mul]
  rw [â† one_smul â„ (f x)]
  refine Tendsto.smul ?_ h_tendsto
  have : min 1 (c / â€–f xâ€–) = 1 := by
    rw [min_eq_left_iff, one_le_div (lt_of_le_of_ne (norm_nonneg _) (Ne.symm hfx0))]
    exact hfx
  nth_rw 2 [this.symm]
  refine Tendsto.min tendsto_const_nhds ?_
  exact Tendsto.div tendsto_const_nhds h_tendsto.norm hfx0

theorem tendsto_approxBounded_ae {Î²} {f : Î± â†’ Î²} [NormedAddCommGroup Î²] [NormedSpace â„ Î²]
    {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} (hf : StronglyMeasurable[m] f) {c : â„}
    (hf_bound : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ c) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ hf.approxBounded c n x) atTop (ğ“ (f x)) := by
  filter_upwards [hf_bound] with x hfx using tendsto_approxBounded_of_norm_le hf hfx

theorem norm_approxBounded_le {Î²} {f : Î± â†’ Î²} [SeminormedAddCommGroup Î²] [NormedSpace â„ Î²]
    {m : MeasurableSpace Î±} {c : â„} (hf : StronglyMeasurable[m] f) (hc : 0 â‰¤ c) (n : â„•) (x : Î±) :
    â€–hf.approxBounded c n xâ€– â‰¤ c := by
  simp only [StronglyMeasurable.approxBounded, SimpleFunc.coe_map, Function.comp_apply]
  refine (norm_smul_le _ _).trans ?_
  by_cases h0 : â€–hf.approx n xâ€– = 0
  Â· simp only [h0, _root_.div_zero, min_eq_right, zero_le_one, norm_zero, mul_zero]
    exact hc
  rcases le_total â€–hf.approx n xâ€– c with h | h
  Â· rw [min_eq_left _]
    Â· simpa only [norm_one, one_mul] using h
    Â· rwa [one_le_div (lt_of_le_of_ne (norm_nonneg _) (Ne.symm h0))]
  Â· rw [min_eq_right _]
    Â· rw [norm_div, norm_norm, mul_comm, mul_div, div_eq_mul_inv, mul_comm, â† mul_assoc,
        inv_mul_cancelâ‚€ h0, one_mul, Real.norm_of_nonneg hc]
    Â· rwa [div_le_one (lt_of_le_of_ne (norm_nonneg _) (Ne.symm h0))]

theorem _root_.stronglyMeasurable_bot_iff [Nonempty Î²] [T2Space Î²] :
    StronglyMeasurable[âŠ¥] f â†” âˆƒ c, f = fun _ => c := by
  cases' isEmpty_or_nonempty Î± with hÎ± hÎ±
  Â· simp only [@Subsingleton.stronglyMeasurable' _ _ âŠ¥ _ _ f,
      eq_iff_true_of_subsingleton, exists_const]
  refine âŸ¨fun hf => ?_, fun hf_eq => ?_âŸ©
  Â· refine âŸ¨f hÎ±.some, ?_âŸ©
    let fs := hf.approx
    have h_fs_tendsto : âˆ€ x, Tendsto (fun n â†¦ fs n x) atTop (ğ“ (f x)) := hf.tendsto_approx
    have : âˆ€ n, âˆƒ c, âˆ€ x, fs n x = c := fun n â†¦ SimpleFunc.simpleFunc_bot (fs n)
    let cs n := (this n).choose
    have h_cs_eq : âˆ€ n, â‡‘(fs n) = fun _ => cs n := fun n â†¦ funext (this n).choose_spec
    conv at h_fs_tendsto => enter [x, 1, n]; rw [h_cs_eq]
    have h_tendsto : Tendsto cs atTop (ğ“ (f hÎ±.some)) := h_fs_tendsto hÎ±.some
    ext1 x
    exact tendsto_nhds_unique (h_fs_tendsto x) h_tendsto
  Â· obtain âŸ¨c, rflâŸ© := hf_eq
    exact stronglyMeasurable_const

end BasicPropertiesInAnyTopologicalSpace

theorem finStronglyMeasurable_of_set_sigmaFinite [TopologicalSpace Î²] [Zero Î²]
    {m : MeasurableSpace Î±} {Î¼ : Measure Î±} (hf_meas : StronglyMeasurable f) {t : Set Î±}
    (ht : MeasurableSet t) (hft_zero : âˆ€ x âˆˆ tá¶œ, f x = 0) (htÎ¼ : SigmaFinite (Î¼.restrict t)) :
    FinStronglyMeasurable f Î¼ := by
  haveI : SigmaFinite (Î¼.restrict t) := htÎ¼
  let S := spanningSets (Î¼.restrict t)
  have hS_meas : âˆ€ n, MeasurableSet (S n) := measurableSet_spanningSets (Î¼.restrict t)
  let f_approx := hf_meas.approx
  let fs n := SimpleFunc.restrict (f_approx n) (S n âˆ© t)
  have h_fs_t_compl : âˆ€ n, âˆ€ x, x âˆ‰ t â†’ fs n x = 0 := by
    intro n x hxt
    rw [SimpleFunc.restrict_apply _ ((hS_meas n).inter ht)]
    refine Set.indicator_of_not_mem ?_ _
    simp [hxt]
  refine âŸ¨fs, ?_, fun x â†¦ ?_âŸ©
  Â· simp_rw [SimpleFunc.support_eq, â† Finset.mem_coe]
    classical
    refine fun n â†¦ measure_biUnion_lt_top {y âˆˆ (fs n).range | y â‰  0}.finite_toSet fun y hy => ?_
    rw [SimpleFunc.restrict_preimage_singleton _ ((hS_meas n).inter ht)]
    swap
    Â· letI : (y : Î²) â†’ Decidable (y = 0) := fun y â†¦ Classical.propDecidable _
      rw [Finset.mem_coe, Finset.mem_filter] at hy
      exact hy.2
    refine (measure_mono Set.inter_subset_left).trans_lt ?_
    have h_lt_top := measure_spanningSets_lt_top (Î¼.restrict t) n
    rwa [Measure.restrict_apply' ht] at h_lt_top
  Â· by_cases hxt : x âˆˆ t
    swap
    Â· rw [funext fun n â†¦ h_fs_t_compl n x hxt, hft_zero x hxt]
      exact tendsto_const_nhds
    have h : Tendsto (fun n â†¦ (f_approx n) x) atTop (ğ“ (f x)) := hf_meas.tendsto_approx x
    obtain âŸ¨nâ‚, hnâ‚âŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ fs m x = f_approx m x := by
      obtain âŸ¨n, hnâŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ x âˆˆ S m âˆ© t := by
        rsuffices âŸ¨n, hnâŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ x âˆˆ S m
        Â· exact âŸ¨n, fun m hnm => Set.mem_inter (hn m hnm) hxtâŸ©
        rsuffices âŸ¨n, hnâŸ© : âˆƒ n, x âˆˆ S n
        Â· exact âŸ¨n, fun m hnm => monotone_spanningSets (Î¼.restrict t) hnm hnâŸ©
        rw [â† Set.mem_iUnion, iUnion_spanningSets (Î¼.restrict t)]
        trivial
      refine âŸ¨n, fun m hnm => ?_âŸ©
      simp_rw [fs, SimpleFunc.restrict_apply _ ((hS_meas m).inter ht),
        Set.indicator_of_mem (hn m hnm)]
    rw [tendsto_atTop'] at h âŠ¢
    intro s hs
    obtain âŸ¨nâ‚‚, hnâ‚‚âŸ© := h s hs
    refine âŸ¨max nâ‚ nâ‚‚, fun m hm => ?_âŸ©
    rw [hnâ‚ m ((le_max_left _ _).trans hm.le)]
    exact hnâ‚‚ m ((le_max_right _ _).trans hm.le)

/-- If the measure is sigma-finite, all strongly measurable functions are
  `FinStronglyMeasurable`. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem finStronglyMeasurable [TopologicalSpace Î²] [Zero Î²] {m0 : MeasurableSpace Î±}
    (hf : StronglyMeasurable f) (Î¼ : Measure Î±) [SigmaFinite Î¼] : FinStronglyMeasurable f Î¼ :=
  hf.finStronglyMeasurable_of_set_sigmaFinite MeasurableSet.univ (by simp)
    (by rwa [Measure.restrict_univ])

/-- A strongly measurable function is measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem measurable {_ : MeasurableSpace Î±} [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [MeasurableSpace Î²] [BorelSpace Î²] (hf : StronglyMeasurable f) : Measurable f :=
  measurable_of_tendsto_metrizable (fun n â†¦ (hf.approx n).measurable)
    (tendsto_pi_nhds.mpr hf.tendsto_approx)

/-- A strongly measurable function is almost everywhere measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²] {Î¼ : Measure Î±}
    (hf : StronglyMeasurable f) : AEMeasurable f Î¼ :=
  hf.measurable.aemeasurable

theorem _root_.Continuous.comp_stronglyMeasurable {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    [TopologicalSpace Î³] {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Continuous g) (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x â†¦ g (f x) :=
  âŸ¨fun n â†¦ SimpleFunc.map g (hf.approx n), fun x â†¦ (hg.tendsto _).comp (hf.tendsto_approx x)âŸ©

@[to_additive]
nonrec theorem measurableSet_mulSupport {m : MeasurableSpace Î±} [One Î²] [TopologicalSpace Î²]
    [MetrizableSpace Î²] (hf : StronglyMeasurable f) : MeasurableSet (mulSupport f) := by
  borelize Î²
  exact measurableSet_mulSupport hf.measurable

protected theorem mono {m m' : MeasurableSpace Î±} [TopologicalSpace Î²]
    (hf : StronglyMeasurable[m'] f) (h_mono : m' â‰¤ m) : StronglyMeasurable[m] f := by
  let f_approx : â„• â†’ @SimpleFunc Î± m Î² := fun n =>
    @SimpleFunc.mk Î± m Î²
      (hf.approx n)
      (fun x â†¦ h_mono _ (SimpleFunc.measurableSet_fiber' _ x))
      (SimpleFunc.finite_range (hf.approx n))
  exact âŸ¨f_approx, hf.tendsto_approxâŸ©

protected theorem prod_mk {m : MeasurableSpace Î±} [TopologicalSpace Î²] [TopologicalSpace Î³]
    {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x â†¦ (f x, g x) := by
  refine âŸ¨fun n â†¦ SimpleFunc.pair (hf.approx n) (hg.approx n), fun x â†¦ ?_âŸ©
  rw [nhds_prod_eq]
  exact Tendsto.prod_mk (hf.tendsto_approx x) (hg.tendsto_approx x)

theorem comp_measurable [TopologicalSpace Î²] {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î³}
    {f : Î± â†’ Î²} {g : Î³ â†’ Î±} (hf : StronglyMeasurable f) (hg : Measurable g) :
    StronglyMeasurable (f âˆ˜ g) :=
  âŸ¨fun n â†¦ SimpleFunc.comp (hf.approx n) g hg, fun x â†¦ hf.tendsto_approx (g x)âŸ©

theorem of_uncurry_left [TopologicalSpace Î²] {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î³}
    {f : Î± â†’ Î³ â†’ Î²} (hf : StronglyMeasurable (uncurry f)) {x : Î±} : StronglyMeasurable (f x) :=
  hf.comp_measurable measurable_prod_mk_left

theorem of_uncurry_right [TopologicalSpace Î²] {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î³}
    {f : Î± â†’ Î³ â†’ Î²} (hf : StronglyMeasurable (uncurry f)) {y : Î³} :
    StronglyMeasurable fun x â†¦ f x y :=
  hf.comp_measurable measurable_prod_mk_right

section Arithmetic

variable {mÎ± : MeasurableSpace Î±} [TopologicalSpace Î²]

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem mul [Mul Î²] [ContinuousMul Î²] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f * g) :=
  âŸ¨fun n â†¦ hf.approx n * hg.approx n, fun x â†¦ (hf.tendsto_approx x).mul (hg.tendsto_approx x)âŸ©

@[to_additive (attr := measurability)]
theorem mul_const [Mul Î²] [ContinuousMul Î²] (hf : StronglyMeasurable f) (c : Î²) :
    StronglyMeasurable fun x â†¦ f x * c :=
  hf.mul stronglyMeasurable_const

@[to_additive (attr := measurability)]
theorem const_mul [Mul Î²] [ContinuousMul Î²] (hf : StronglyMeasurable f) (c : Î²) :
    StronglyMeasurable fun x â†¦ c * f x :=
  stronglyMeasurable_const.mul hf

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable])) const_nsmul]
protected theorem pow [Monoid Î²] [ContinuousMul Î²] (hf : StronglyMeasurable f) (n : â„•) :
    StronglyMeasurable (f ^ n) :=
  âŸ¨fun k â†¦ hf.approx k ^ n, fun x â†¦ (hf.tendsto_approx x).pow nâŸ©

@[to_additive (attr := measurability)]
protected theorem inv [Inv Î²] [ContinuousInv Î²] (hf : StronglyMeasurable f) :
    StronglyMeasurable fâ»Â¹ :=
  âŸ¨fun n â†¦ (hf.approx n)â»Â¹, fun x â†¦ (hf.tendsto_approx x).invâŸ©

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem div [Div Î²] [ContinuousDiv Î²] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f / g) :=
  âŸ¨fun n â†¦ hf.approx n / hg.approx n, fun x â†¦ (hf.tendsto_approx x).div' (hg.tendsto_approx x)âŸ©

@[to_additive]
theorem mul_iff_right [CommGroup Î²] [TopologicalGroup Î²] (hf : StronglyMeasurable f) :
    StronglyMeasurable (f * g) â†” StronglyMeasurable g :=
  âŸ¨fun h â†¦ show g = f * g * fâ»Â¹ by simp only [mul_inv_cancel_comm] â–¸ h.mul hf.inv,
    fun h â†¦ hf.mul hâŸ©

@[to_additive]
theorem mul_iff_left [CommGroup Î²] [TopologicalGroup Î²] (hf : StronglyMeasurable f) :
    StronglyMeasurable (g * f) â†” StronglyMeasurable g :=
  mul_comm g f â–¸ mul_iff_right hf

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem smul {ğ•œ} [TopologicalSpace ğ•œ] [SMul ğ•œ Î²] [ContinuousSMul ğ•œ Î²] {f : Î± â†’ ğ•œ}
    {g : Î± â†’ Î²} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x â†¦ f x â€¢ g x :=
  continuous_smul.comp_stronglyMeasurable (hf.prod_mk hg)

@[to_additive (attr := measurability)]
protected theorem const_smul {ğ•œ} [SMul ğ•œ Î²] [ContinuousConstSMul ğ•œ Î²] (hf : StronglyMeasurable f)
    (c : ğ•œ) : StronglyMeasurable (c â€¢ f) :=
  âŸ¨fun n â†¦ c â€¢ hf.approx n, fun x â†¦ (hf.tendsto_approx x).const_smul câŸ©

@[to_additive (attr := measurability)]
protected theorem const_smul' {ğ•œ} [SMul ğ•œ Î²] [ContinuousConstSMul ğ•œ Î²] (hf : StronglyMeasurable f)
    (c : ğ•œ) : StronglyMeasurable fun x â†¦ c â€¢ f x :=
  hf.const_smul c

@[to_additive (attr := measurability)]
protected theorem smul_const {ğ•œ} [TopologicalSpace ğ•œ] [SMul ğ•œ Î²] [ContinuousSMul ğ•œ Î²] {f : Î± â†’ ğ•œ}
    (hf : StronglyMeasurable f) (c : Î²) : StronglyMeasurable fun x â†¦ f x â€¢ c :=
  continuous_smul.comp_stronglyMeasurable (hf.prod_mk stronglyMeasurable_const)

/-- In a normed vector space, the addition of a measurable function and a strongly measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the addition of two measurable functions. -/
theorem _root_.Measurable.add_stronglyMeasurable
    {Î± E : Type*} {_ : MeasurableSpace Î±} [AddGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [PseudoMetrizableSpace E]
    {g f : Î± â†’ E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (g + f) := by
  rcases hf with âŸ¨Ï†, hÏ†âŸ©
  have : Tendsto (fun n x â†¦ g x + Ï† n x) atTop (ğ“ (g + f)) :=
    tendsto_pi_nhds.2 (fun x â†¦ tendsto_const_nhds.add (hÏ† x))
  apply measurable_of_tendsto_metrizable (fun n â†¦ ?_) this
  exact hg.add_simpleFunc _

/-- In a normed vector space, the subtraction of a measurable function and a strongly measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the subtraction of two measurable functions. -/
theorem _root_.Measurable.sub_stronglyMeasurable
    {Î± E : Type*} {_ : MeasurableSpace Î±} [AddCommGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [ContinuousNeg E] [PseudoMetrizableSpace E]
    {g f : Î± â†’ E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (g - f) := by
  rw [sub_eq_add_neg]
  exact hg.add_stronglyMeasurable hf.neg

/-- In a normed vector space, the addition of a strongly measurable function and a measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the addition of two measurable functions. -/
theorem _root_.Measurable.stronglyMeasurable_add
    {Î± E : Type*} {_ : MeasurableSpace Î±} [AddGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [PseudoMetrizableSpace E]
    {g f : Î± â†’ E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (f + g) := by
  rcases hf with âŸ¨Ï†, hÏ†âŸ©
  have : Tendsto (fun n x â†¦ Ï† n x + g x) atTop (ğ“ (f + g)) :=
    tendsto_pi_nhds.2 (fun x â†¦ (hÏ† x).add tendsto_const_nhds)
  apply measurable_of_tendsto_metrizable (fun n â†¦ ?_) this
  exact hg.simpleFunc_add _

end Arithmetic

section MulAction

variable {M G Gâ‚€ : Type*}
variable [TopologicalSpace Î²]
variable [Monoid M] [MulAction M Î²] [ContinuousConstSMul M Î²]
variable [Group G] [MulAction G Î²] [ContinuousConstSMul G Î²]
variable [GroupWithZero Gâ‚€] [MulAction Gâ‚€ Î²] [ContinuousConstSMul Gâ‚€ Î²]

theorem _root_.stronglyMeasurable_const_smul_iff {m : MeasurableSpace Î±} (c : G) :
    (StronglyMeasurable fun x â†¦ c â€¢ f x) â†” StronglyMeasurable f :=
  âŸ¨fun h â†¦ by simpa only [inv_smul_smul] using h.const_smul' câ»Â¹, fun h â†¦ h.const_smul câŸ©

nonrec theorem _root_.IsUnit.stronglyMeasurable_const_smul_iff {_ : MeasurableSpace Î±} {c : M}
    (hc : IsUnit c) :
    (StronglyMeasurable fun x â†¦ c â€¢ f x) â†” StronglyMeasurable f :=
  let âŸ¨u, huâŸ© := hc
  hu â–¸ stronglyMeasurable_const_smul_iff u

theorem _root_.stronglyMeasurable_const_smul_iffâ‚€ {_ : MeasurableSpace Î±} {c : Gâ‚€} (hc : c â‰  0) :
    (StronglyMeasurable fun x â†¦ c â€¢ f x) â†” StronglyMeasurable f :=
  (IsUnit.mk0 _ hc).stronglyMeasurable_const_smul_iff

end MulAction

section Order

variable [MeasurableSpace Î±] [TopologicalSpace Î²]

open Filter

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [Max Î²] [ContinuousSup Î²] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f âŠ” g) :=
  âŸ¨fun n â†¦ hf.approx n âŠ” hg.approx n, fun x =>
    (hf.tendsto_approx x).sup_nhds (hg.tendsto_approx x)âŸ©

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [Min Î²] [ContinuousInf Î²] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f âŠ“ g) :=
  âŸ¨fun n â†¦ hf.approx n âŠ“ hg.approx n, fun x =>
    (hf.tendsto_approx x).inf_nhds (hg.tendsto_approx x)âŸ©

end Order

/-!
### Big operators: `âˆ` and `âˆ‘`
-/


section Monoid

variable {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M] {m : MeasurableSpace Î±}

@[to_additive (attr := measurability)]
theorem _root_.List.stronglyMeasurable_prod' (l : List (Î± â†’ M))
    (hl : âˆ€ f âˆˆ l, StronglyMeasurable f) : StronglyMeasurable l.prod := by
  induction' l with f l ihl; Â· exact stronglyMeasurable_one
  rw [List.forall_mem_cons] at hl
  rw [List.prod_cons]
  exact hl.1.mul (ihl hl.2)

@[to_additive (attr := measurability)]
theorem _root_.List.stronglyMeasurable_prod (l : List (Î± â†’ M))
    (hl : âˆ€ f âˆˆ l, StronglyMeasurable f) :
    StronglyMeasurable fun x â†¦ (l.map fun f : Î± â†’ M => f x).prod := by
  simpa only [â† Pi.list_prod_apply] using l.stronglyMeasurable_prod' hl

end Monoid

section CommMonoid

variable {M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M] {m : MeasurableSpace Î±}

@[to_additive (attr := measurability)]
theorem _root_.Multiset.stronglyMeasurable_prod' (l : Multiset (Î± â†’ M))
    (hl : âˆ€ f âˆˆ l, StronglyMeasurable f) : StronglyMeasurable l.prod := by
  rcases l with âŸ¨lâŸ©
  simpa using l.stronglyMeasurable_prod' (by simpa using hl)

@[to_additive (attr := measurability)]
theorem _root_.Multiset.stronglyMeasurable_prod (s : Multiset (Î± â†’ M))
    (hs : âˆ€ f âˆˆ s, StronglyMeasurable f) :
    StronglyMeasurable fun x â†¦ (s.map fun f : Î± â†’ M => f x).prod := by
  simpa only [â† Pi.multiset_prod_apply] using s.stronglyMeasurable_prod' hs

@[to_additive (attr := measurability)]
theorem _root_.Finset.stronglyMeasurable_prod' {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ M} (s : Finset Î¹)
    (hf : âˆ€ i âˆˆ s, StronglyMeasurable (f i)) : StronglyMeasurable (âˆ i âˆˆ s, f i) :=
  Finset.prod_induction _ _ (fun _a _b ha hb => ha.mul hb) (@stronglyMeasurable_one Î± M _ _ _) hf

@[to_additive (attr := measurability)]
theorem _root_.Finset.stronglyMeasurable_prod {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ M} (s : Finset Î¹)
    (hf : âˆ€ i âˆˆ s, StronglyMeasurable (f i)) : StronglyMeasurable fun a â†¦ âˆ i âˆˆ s, f i a := by
  simpa only [â† Finset.prod_apply] using s.stronglyMeasurable_prod' hf

end CommMonoid

/-- The range of a strongly measurable function is separable. -/
protected theorem isSeparable_range {m : MeasurableSpace Î±} [TopologicalSpace Î²]
    (hf : StronglyMeasurable f) : TopologicalSpace.IsSeparable (range f) := by
  have : IsSeparable (closure (â‹ƒ n, range (hf.approx n))) :=
    .closure <| .iUnion fun n â†¦ (hf.approx n).finite_range.isSeparable
  apply this.mono
  rintro _ âŸ¨x, rflâŸ©
  apply mem_closure_of_tendsto (hf.tendsto_approx x)
  filter_upwards with n
  apply mem_iUnion_of_mem n
  exact mem_range_self _

theorem separableSpace_range_union_singleton {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] (hf : StronglyMeasurable f) {b : Î²} :
    SeparableSpace (range f âˆª {b} : Set Î²) :=
  letI := pseudoMetrizableSpacePseudoMetric Î²
  (hf.isSeparable_range.union (finite_singleton _).isSeparable).separableSpace

section SecondCountableStronglyMeasurable

variable {mÎ± : MeasurableSpace Î±} [MeasurableSpace Î²]

/-- In a space with second countable topology, measurable implies strongly measurable. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem _root_.Measurable.stronglyMeasurable [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [SecondCountableTopology Î²] [OpensMeasurableSpace Î²] (hf : Measurable f) :
    StronglyMeasurable f := by
  letI := pseudoMetrizableSpacePseudoMetric Î²
  nontriviality Î²; inhabit Î²
  exact âŸ¨SimpleFunc.approxOn f hf Set.univ default (Set.mem_univ _), fun x â†¦
    SimpleFunc.tendsto_approxOn hf (Set.mem_univ _) (by rw [closure_univ]; simp)âŸ©

/-- In a space with second countable topology, strongly measurable and measurable are equivalent. -/
theorem _root_.stronglyMeasurable_iff_measurable [TopologicalSpace Î²] [MetrizableSpace Î²]
    [BorelSpace Î²] [SecondCountableTopology Î²] : StronglyMeasurable f â†” Measurable f :=
  âŸ¨fun h â†¦ h.measurable, fun h â†¦ Measurable.stronglyMeasurable hâŸ©

@[measurability]
theorem _root_.stronglyMeasurable_id [TopologicalSpace Î±] [PseudoMetrizableSpace Î±]
    [OpensMeasurableSpace Î±] [SecondCountableTopology Î±] : StronglyMeasurable (id : Î± â†’ Î±) :=
  measurable_id.stronglyMeasurable

end SecondCountableStronglyMeasurable

/-- A function is strongly measurable if and only if it is measurable and has separable
range. -/
theorem _root_.stronglyMeasurable_iff_measurable_separable {m : MeasurableSpace Î±}
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²] :
    StronglyMeasurable f â†” Measurable f âˆ§ IsSeparable (range f) := by
  refine âŸ¨fun H â†¦ âŸ¨H.measurable, H.isSeparable_rangeâŸ©, fun âŸ¨Hm, HsepâŸ©  â†¦ ?_âŸ©
  have := Hsep.secondCountableTopology
  have Hm' : StronglyMeasurable (rangeFactorization f) := Hm.subtype_mk.stronglyMeasurable
  exact continuous_subtype_val.comp_stronglyMeasurable Hm'

/-- A continuous function is strongly measurable when either the source space or the target space
is second-countable. -/
theorem _root_.Continuous.stronglyMeasurable [MeasurableSpace Î±] [TopologicalSpace Î±]
    [OpensMeasurableSpace Î±] [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [h : SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²} (hf : Continuous f) :
    StronglyMeasurable f := by
  borelize Î²
  cases h.out
  Â· rw [stronglyMeasurable_iff_measurable_separable]
    refine âŸ¨hf.measurable, ?_âŸ©
    exact isSeparable_range hf
  Â· exact hf.measurable.stronglyMeasurable

/-- A continuous function whose support is contained in a compact set is strongly measurable. -/
@[to_additive]
theorem _root_.Continuous.stronglyMeasurable_of_mulSupport_subset_isCompact
    [MeasurableSpace Î±] [TopologicalSpace Î±] [OpensMeasurableSpace Î±] [MeasurableSpace Î²]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [BorelSpace Î²] [One Î²] {f : Î± â†’ Î²}
    (hf : Continuous f) {k : Set Î±} (hk : IsCompact k)
    (h'f : mulSupport f âŠ† k) : StronglyMeasurable f := by
  letI : PseudoMetricSpace Î² := pseudoMetrizableSpacePseudoMetric Î²
  rw [stronglyMeasurable_iff_measurable_separable]
  exact âŸ¨hf.measurable, (isCompact_range_of_mulSupport_subset_isCompact hf hk h'f).isSeparableâŸ©

/-- A continuous function with compact support is strongly measurable. -/
@[to_additive]
theorem _root_.Continuous.stronglyMeasurable_of_hasCompactMulSupport
    [MeasurableSpace Î±] [TopologicalSpace Î±] [OpensMeasurableSpace Î±] [MeasurableSpace Î²]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [BorelSpace Î²] [One Î²] {f : Î± â†’ Î²}
    (hf : Continuous f) (h'f : HasCompactMulSupport f) : StronglyMeasurable f :=
  hf.stronglyMeasurable_of_mulSupport_subset_isCompact h'f (subset_mulTSupport f)

/-- A continuous function with compact support on a product space is strongly measurable for the
product sigma-algebra. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma _root_.HasCompactSupport.stronglyMeasurable_of_prod {X Y : Type*} [Zero Î±]
    [TopologicalSpace X] [TopologicalSpace Y] [MeasurableSpace X] [MeasurableSpace Y]
    [OpensMeasurableSpace X] [OpensMeasurableSpace Y] [TopologicalSpace Î±] [PseudoMetrizableSpace Î±]
    {f : X Ã— Y â†’ Î±} (hf : Continuous f) (h'f : HasCompactSupport f) :
    StronglyMeasurable f := by
  borelize Î±
  apply stronglyMeasurable_iff_measurable_separable.2 âŸ¨h'f.measurable_of_prod hf, ?_âŸ©
  letI : PseudoMetricSpace Î± := pseudoMetrizableSpacePseudoMetric Î±
  exact IsCompact.isSeparable (s := range f) (h'f.isCompact_range hf)

/-- If `g` is a topological embedding, then `f` is strongly measurable iff `g âˆ˜ f` is. -/
theorem _root_.Embedding.comp_stronglyMeasurable_iff {m : MeasurableSpace Î±} [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] [TopologicalSpace Î³] [PseudoMetrizableSpace Î³] {g : Î² â†’ Î³} {f : Î± â†’ Î²}
    (hg : IsEmbedding g) : (StronglyMeasurable fun x â†¦ g (f x)) â†” StronglyMeasurable f := by
  letI := pseudoMetrizableSpacePseudoMetric Î³
  borelize Î² Î³
  refine
    âŸ¨fun H => stronglyMeasurable_iff_measurable_separable.2 âŸ¨?_, ?_âŸ©, fun H =>
      hg.continuous.comp_stronglyMeasurable HâŸ©
  Â· let G : Î² â†’ range g := rangeFactorization g
    have hG : IsClosedEmbedding G :=
      { hg.codRestrict _ _ with
        isClosed_range := by
          rw [surjective_onto_range.range_eq]
          exact isClosed_univ }
    have : Measurable (G âˆ˜ f) := Measurable.subtype_mk H.measurable
    exact hG.measurableEmbedding.measurable_comp_iff.1 this
  Â· have : IsSeparable (g â»Â¹' range (g âˆ˜ f)) := hg.isSeparable_preimage H.isSeparable_range
    rwa [range_comp, hg.injective.preimage_image] at this

/-- A sequential limit of strongly measurable functions is strongly measurable. -/
theorem _root_.stronglyMeasurable_of_tendsto {Î¹ : Type*} {m : MeasurableSpace Î±}
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] (u : Filter Î¹) [NeBot u] [IsCountablyGenerated u]
    {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (hf : âˆ€ i, StronglyMeasurable (f i)) (lim : Tendsto f u (ğ“ g)) :
    StronglyMeasurable g := by
  borelize Î²
  refine stronglyMeasurable_iff_measurable_separable.2 âŸ¨?_, ?_âŸ©
  Â· exact measurable_of_tendsto_metrizable' u (fun i â†¦ (hf i).measurable) lim
  Â· rcases u.exists_seq_tendsto with âŸ¨v, hvâŸ©
    have : IsSeparable (closure (â‹ƒ i, range (f (v i)))) :=
      .closure <| .iUnion fun i â†¦ (hf (v i)).isSeparable_range
    apply this.mono
    rintro _ âŸ¨x, rflâŸ©
    rw [tendsto_pi_nhds] at lim
    apply mem_closure_of_tendsto ((lim x).comp hv)
    filter_upwards with n
    apply mem_iUnion_of_mem n
    exact mem_range_self _

protected theorem piecewise {m : MeasurableSpace Î±} [TopologicalSpace Î²] {s : Set Î±}
    {_ : DecidablePred (Â· âˆˆ s)} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (Set.piecewise s f g) := by
  refine âŸ¨fun n â†¦ SimpleFunc.piecewise s hs (hf.approx n) (hg.approx n), fun x â†¦ ?_âŸ©
  by_cases hx : x âˆˆ s
  Â· simpa [@Set.piecewise_eq_of_mem _ _ _ _ _ (fun _ => Classical.propDecidable _) _ hx,
      hx] using hf.tendsto_approx x
  Â· simpa [@Set.piecewise_eq_of_not_mem _ _ _ _ _ (fun _ => Classical.propDecidable _) _ hx,
      hx] using hg.tendsto_approx x

/-- this is slightly different from `StronglyMeasurable.piecewise`. It can be used to show
`StronglyMeasurable (ite (x=0) 0 1)` by
`exact StronglyMeasurable.ite (measurableSet_singleton 0) stronglyMeasurable_const
stronglyMeasurable_const`, but replacing `StronglyMeasurable.ite` by
`StronglyMeasurable.piecewise` in that example proof does not work. -/
protected theorem ite {_ : MeasurableSpace Î±} [TopologicalSpace Î²] {p : Î± â†’ Prop}
    {_ : DecidablePred p} (hp : MeasurableSet { a : Î± | p a }) (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable fun x â†¦ ite (p x) (f x) (g x) :=
  StronglyMeasurable.piecewise hp hf hg

@[measurability]
theorem _root_.MeasurableEmbedding.stronglyMeasurable_extend {f : Î± â†’ Î²} {g : Î± â†’ Î³} {g' : Î³ â†’ Î²}
    {mÎ± : MeasurableSpace Î±} {mÎ³ : MeasurableSpace Î³} [TopologicalSpace Î²]
    (hg : MeasurableEmbedding g) (hf : StronglyMeasurable f) (hg' : StronglyMeasurable g') :
    StronglyMeasurable (Function.extend g f g') := by
  refine âŸ¨fun n â†¦ SimpleFunc.extend (hf.approx n) g hg (hg'.approx n), ?_âŸ©
  intro x
  by_cases hx : âˆƒ y, g y = x
  Â· rcases hx with âŸ¨y, rflâŸ©
    simpa only [SimpleFunc.extend_apply, hg.injective, Injective.extend_apply] using
      hf.tendsto_approx y
  Â· simpa only [hx, SimpleFunc.extend_apply', not_false_iff, extend_apply'] using
      hg'.tendsto_approx x

theorem _root_.MeasurableEmbedding.exists_stronglyMeasurable_extend {f : Î± â†’ Î²} {g : Î± â†’ Î³}
    {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î³} [TopologicalSpace Î²]
    (hg : MeasurableEmbedding g) (hf : StronglyMeasurable f) (hne : Î³ â†’ Nonempty Î²) :
    âˆƒ f' : Î³ â†’ Î², StronglyMeasurable f' âˆ§ f' âˆ˜ g = f :=
  âŸ¨Function.extend g f fun x â†¦ Classical.choice (hne x),
    hg.stronglyMeasurable_extend hf (stronglyMeasurable_const' fun _ _ => rfl),
    funext fun _ => hg.injective.extend_apply _ _ _âŸ©

theorem _root_.stronglyMeasurable_of_stronglyMeasurable_union_cover {m : MeasurableSpace Î±}
    [TopologicalSpace Î²] {f : Î± â†’ Î²} (s t : Set Î±) (hs : MeasurableSet s) (ht : MeasurableSet t)
    (h : univ âŠ† s âˆª t) (hc : StronglyMeasurable fun a : s => f a)
    (hd : StronglyMeasurable fun a : t => f a) : StronglyMeasurable f := by
  nontriviality Î²; inhabit Î²
  suffices Function.extend Subtype.val (fun x : s â†¦ f x)
      (Function.extend (â†‘) (fun x : t â†¦ f x) fun _ â†¦ default) = f from
    this â–¸ (MeasurableEmbedding.subtype_coe hs).stronglyMeasurable_extend hc <|
      (MeasurableEmbedding.subtype_coe ht).stronglyMeasurable_extend hd stronglyMeasurable_const
  ext x
  by_cases hxs : x âˆˆ s
  Â· lift x to s using hxs
    simp [Subtype.coe_injective.extend_apply]
  Â· lift x to t using (h trivial).resolve_left hxs
    rw [extend_apply', Subtype.coe_injective.extend_apply]
    exact fun âŸ¨y, hyâŸ© â†¦ hxs <| hy â–¸ y.2

theorem _root_.stronglyMeasurable_of_restrict_of_restrict_compl {_ : MeasurableSpace Î±}
    [TopologicalSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} (hs : MeasurableSet s)
    (hâ‚ : StronglyMeasurable (s.restrict f)) (hâ‚‚ : StronglyMeasurable (sá¶œ.restrict f)) :
    StronglyMeasurable f :=
  stronglyMeasurable_of_stronglyMeasurable_union_cover s sá¶œ hs hs.compl (union_compl_self s).ge hâ‚
    hâ‚‚

@[measurability]
protected theorem indicator {_ : MeasurableSpace Î±} [TopologicalSpace Î²] [Zero Î²]
    (hf : StronglyMeasurable f) {s : Set Î±} (hs : MeasurableSet s) :
    StronglyMeasurable (s.indicator f) :=
  hf.piecewise hs stronglyMeasurable_const

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem dist {_ : MeasurableSpace Î±} {Î² : Type*} [PseudoMetricSpace Î²] {f g : Î± â†’ Î²}
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x â†¦ dist (f x) (g x) :=
  continuous_dist.comp_stronglyMeasurable (hf.prod_mk hg)

@[measurability]
protected theorem norm {_ : MeasurableSpace Î±} {Î² : Type*} [SeminormedAddCommGroup Î²] {f : Î± â†’ Î²}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x â†¦ â€–f xâ€– :=
  continuous_norm.comp_stronglyMeasurable hf

@[measurability]
protected theorem nnnorm {_ : MeasurableSpace Î±} {Î² : Type*} [SeminormedAddCommGroup Î²] {f : Î± â†’ Î²}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x â†¦ â€–f xâ€–â‚Š :=
  continuous_nnnorm.comp_stronglyMeasurable hf

@[measurability]
protected theorem ennnorm {_ : MeasurableSpace Î±} {Î² : Type*} [SeminormedAddCommGroup Î²]
    {f : Î± â†’ Î²} (hf : StronglyMeasurable f) : Measurable fun a â†¦ (â€–f aâ€–â‚Š : â„â‰¥0âˆ) :=
  (ENNReal.continuous_coe.comp_stronglyMeasurable hf.nnnorm).measurable

@[measurability]
protected theorem real_toNNReal {_ : MeasurableSpace Î±} {f : Î± â†’ â„} (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x â†¦ (f x).toNNReal :=
  continuous_real_toNNReal.comp_stronglyMeasurable hf

theorem measurableSet_eq_fun {m : MeasurableSpace Î±} {E} [TopologicalSpace E] [MetrizableSpace E]
    {f g : Î± â†’ E} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    MeasurableSet { x | f x = g x } := by
  borelize (E Ã— E)
  exact (hf.prod_mk hg).measurable isClosed_diagonal.measurableSet

theorem measurableSet_lt {m : MeasurableSpace Î±} [TopologicalSpace Î²] [LinearOrder Î²]
    [OrderClosedTopology Î²] [PseudoMetrizableSpace Î²] {f g : Î± â†’ Î²} (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : MeasurableSet { a | f a < g a } := by
  borelize (Î² Ã— Î²)
  exact (hf.prod_mk hg).measurable isOpen_lt_prod.measurableSet

theorem measurableSet_le {m : MeasurableSpace Î±} [TopologicalSpace Î²] [Preorder Î²]
    [OrderClosedTopology Î²] [PseudoMetrizableSpace Î²] {f g : Î± â†’ Î²} (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : MeasurableSet { a | f a â‰¤ g a } := by
  borelize (Î² Ã— Î²)
  exact (hf.prod_mk hg).measurable isClosed_le_prod.measurableSet

theorem stronglyMeasurable_in_set {m : MeasurableSpace Î±} [TopologicalSpace Î²] [Zero Î²] {s : Set Î±}
    {f : Î± â†’ Î²} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hf_zero : âˆ€ x, x âˆ‰ s â†’ f x = 0) :
    âˆƒ fs : â„• â†’ Î± â†’â‚› Î²,
      (âˆ€ x, Tendsto (fun n â†¦ fs n x) atTop (ğ“ (f x))) âˆ§ âˆ€ x âˆ‰ s, âˆ€ n, fs n x = 0 := by
  let g_seq_s : â„• â†’ @SimpleFunc Î± m Î² := fun n â†¦ (hf.approx n).restrict s
  have hg_eq : âˆ€ x âˆˆ s, âˆ€ n, g_seq_s n x = hf.approx n x := by
    intro x hx n
    rw [SimpleFunc.coe_restrict _ hs, Set.indicator_of_mem hx]
  have hg_zero : âˆ€ x âˆ‰ s, âˆ€ n, g_seq_s n x = 0 := by
    intro x hx n
    rw [SimpleFunc.coe_restrict _ hs, Set.indicator_of_not_mem hx]
  refine âŸ¨g_seq_s, fun x â†¦ ?_, hg_zeroâŸ©
  by_cases hx : x âˆˆ s
  Â· simp_rw [hg_eq x hx]
    exact hf.tendsto_approx x
  Â· simp_rw [hg_zero x hx, hf_zero x hx]
    exact tendsto_const_nhds

/-- If the restriction to a set `s` of a Ïƒ-algebra `m` is included in the restriction to `s` of
another Ïƒ-algebra `mâ‚‚` (hypothesis `hs`), the set `s` is `m` measurable and a function `f` supported
on `s` is `m`-strongly-measurable, then `f` is also `mâ‚‚`-strongly-measurable. -/
theorem stronglyMeasurable_of_measurableSpace_le_on {Î± E} {m mâ‚‚ : MeasurableSpace Î±}
    [TopologicalSpace E] [Zero E] {s : Set Î±} {f : Î± â†’ E} (hs_m : MeasurableSet[m] s)
    (hs : âˆ€ t, MeasurableSet[m] (s âˆ© t) â†’ MeasurableSet[mâ‚‚] (s âˆ© t))
    (hf : StronglyMeasurable[m] f) (hf_zero : âˆ€ x âˆ‰ s, f x = 0) :
    StronglyMeasurable[mâ‚‚] f := by
  have hs_mâ‚‚ : MeasurableSet[mâ‚‚] s := by
    rw [â† Set.inter_univ s]
    refine hs Set.univ ?_
    rwa [Set.inter_univ]
  obtain âŸ¨g_seq_s, hg_seq_tendsto, hg_seq_zeroâŸ© := stronglyMeasurable_in_set hs_m hf hf_zero
  let g_seq_sâ‚‚ : â„• â†’ @SimpleFunc Î± mâ‚‚ E := fun n =>
    { toFun := g_seq_s n
      measurableSet_fiber' := fun x â†¦ by
        rw [â† Set.inter_univ (g_seq_s n â»Â¹' {x}), â† Set.union_compl_self s,
          Set.inter_union_distrib_left, Set.inter_comm (g_seq_s n â»Â¹' {x})]
        refine MeasurableSet.union (hs _ (hs_m.inter ?_)) ?_
        Â· exact @SimpleFunc.measurableSet_fiber _ _ m _ _
        by_cases hx : x = 0
        Â· suffices g_seq_s n â»Â¹' {x} âˆ© sá¶œ = sá¶œ by
            rw [this]
            exact hs_mâ‚‚.compl
          ext1 y
          rw [hx, Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff]
          exact âŸ¨fun h â†¦ h.2, fun h â†¦ âŸ¨hg_seq_zero y h n, hâŸ©âŸ©
        Â· suffices g_seq_s n â»Â¹' {x} âˆ© sá¶œ = âˆ… by
            rw [this]
            exact MeasurableSet.empty
          ext1 y
          simp only [mem_inter_iff, mem_preimage, mem_singleton_iff, mem_compl_iff,
            mem_empty_iff_false, iff_false, not_and, not_not_mem]
          refine Function.mtr fun hys => ?_
          rw [hg_seq_zero y hys n]
          exact Ne.symm hx
      finite_range' := @SimpleFunc.finite_range _ _ m (g_seq_s n) }
  exact âŸ¨g_seq_sâ‚‚, hg_seq_tendstoâŸ©

/-- If a function `f` is strongly measurable w.r.t. a sub-Ïƒ-algebra `m` and the measure is Ïƒ-finite
on `m`, then there exists spanning measurable sets with finite measure on which `f` has bounded
norm. In particular, `f` is integrable on each of those sets. -/
theorem exists_spanning_measurableSet_norm_le [SeminormedAddCommGroup Î²] {m m0 : MeasurableSpace Î±}
    (hm : m â‰¤ m0) (hf : StronglyMeasurable[m] f) (Î¼ : Measure Î±) [SigmaFinite (Î¼.trim hm)] :
    âˆƒ s : â„• â†’ Set Î±,
      (âˆ€ n, MeasurableSet[m] (s n) âˆ§ Î¼ (s n) < âˆ âˆ§ âˆ€ x âˆˆ s n, â€–f xâ€– â‰¤ n) âˆ§
      â‹ƒ i, s i = Set.univ := by
  obtain âŸ¨s, hs, hs_univâŸ© :=
    @exists_spanning_measurableSet_le _ m _ hf.nnnorm.measurable (Î¼.trim hm) _
  refine âŸ¨s, fun n â†¦ âŸ¨(hs n).1, (le_trim hm).trans_lt (hs n).2.1, fun x hx â†¦ ?_âŸ©, hs_univâŸ©
  have hx_nnnorm : â€–f xâ€–â‚Š â‰¤ n := (hs n).2.2 x hx
  rw [â† coe_nnnorm]
  norm_cast

end StronglyMeasurable

/-! ## Finitely strongly measurable functions -/


theorem finStronglyMeasurable_zero {Î± Î²} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [Zero Î²]
    [TopologicalSpace Î²] : FinStronglyMeasurable (0 : Î± â†’ Î²) Î¼ :=
  âŸ¨0, by
    simp only [Pi.zero_apply, SimpleFunc.coe_zero, support_zero', measure_empty,
      zero_lt_top, forall_const],
    fun _ => tendsto_const_nhdsâŸ©

namespace FinStronglyMeasurable

variable {m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {f g : Î± â†’ Î²}

theorem aefinStronglyMeasurable [Zero Î²] [TopologicalSpace Î²] (hf : FinStronglyMeasurable f Î¼) :
    AEFinStronglyMeasurable f Î¼ :=
  âŸ¨f, hf, ae_eq_refl fâŸ©

section sequence

variable [Zero Î²] [TopologicalSpace Î²] (hf : FinStronglyMeasurable f Î¼)

/-- A sequence of simple functions such that `âˆ€ x, Tendsto (fun n â†¦ hf.approx n x) atTop (ğ“ (f x))`
and `âˆ€ n, Î¼ (support (hf.approx n)) < âˆ`. These properties are given by
`FinStronglyMeasurable.tendsto_approx` and `FinStronglyMeasurable.fin_support_approx`. -/
protected noncomputable def approx : â„• â†’ Î± â†’â‚› Î² :=
  hf.choose

protected theorem fin_support_approx : âˆ€ n, Î¼ (support (hf.approx n)) < âˆ :=
  hf.choose_spec.1

protected theorem tendsto_approx : âˆ€ x, Tendsto (fun n â†¦ hf.approx n x) atTop (ğ“ (f x)) :=
  hf.choose_spec.2

end sequence

/-- A finitely strongly measurable function is strongly measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem stronglyMeasurable [Zero Î²] [TopologicalSpace Î²]
    (hf : FinStronglyMeasurable f Î¼) : StronglyMeasurable f :=
  âŸ¨hf.approx, hf.tendsto_approxâŸ©

theorem exists_set_sigmaFinite [Zero Î²] [TopologicalSpace Î²] [T2Space Î²]
    (hf : FinStronglyMeasurable f Î¼) :
    âˆƒ t, MeasurableSet t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ SigmaFinite (Î¼.restrict t) := by
  rcases hf with âŸ¨fs, hT_lt_top, h_approxâŸ©
  let T n := support (fs n)
  have hT_meas : âˆ€ n, MeasurableSet (T n) := fun n â†¦ SimpleFunc.measurableSet_support (fs n)
  let t := â‹ƒ n, T n
  refine âŸ¨t, MeasurableSet.iUnion hT_meas, ?_, ?_âŸ©
  Â· have h_fs_zero : âˆ€ n, âˆ€ x âˆˆ tá¶œ, fs n x = 0 := by
      intro n x hxt
      rw [Set.mem_compl_iff, Set.mem_iUnion, not_exists] at hxt
      simpa [T] using hxt n
    refine fun x hxt => tendsto_nhds_unique (h_approx x) ?_
    rw [funext fun n â†¦ h_fs_zero n x hxt]
    exact tendsto_const_nhds
  Â· refine âŸ¨âŸ¨âŸ¨fun n â†¦ tá¶œ âˆª T n, fun _ => trivial, fun n â†¦ ?_, ?_âŸ©âŸ©âŸ©
    Â· rw [Measure.restrict_apply' (MeasurableSet.iUnion hT_meas), Set.union_inter_distrib_right,
        Set.compl_inter_self t, Set.empty_union]
      exact (measure_mono Set.inter_subset_left).trans_lt (hT_lt_top n)
    Â· rw [â† Set.union_iUnion tá¶œ T]
      exact Set.compl_union_self _

/-- A finitely strongly measurable function is measurable. -/
protected theorem measurable [Zero Î²] [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [MeasurableSpace Î²] [BorelSpace Î²] (hf : FinStronglyMeasurable f Î¼) : Measurable f :=
  hf.stronglyMeasurable.measurable

section Arithmetic

variable [TopologicalSpace Î²]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem mul [MonoidWithZero Î²] [ContinuousMul Î²] (hf : FinStronglyMeasurable f Î¼)
    (hg : FinStronglyMeasurable g Î¼) : FinStronglyMeasurable (f * g) Î¼ := by
  refine
    âŸ¨fun n â†¦ hf.approx n * hg.approx n, ?_, fun x =>
      (hf.tendsto_approx x).mul (hg.tendsto_approx x)âŸ©
  intro n
  exact (measure_mono (support_mul_subset_left _ _)).trans_lt (hf.fin_support_approx n)

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem add [AddMonoid Î²] [ContinuousAdd Î²] (hf : FinStronglyMeasurable f Î¼)
    (hg : FinStronglyMeasurable g Î¼) : FinStronglyMeasurable (f + g) Î¼ :=
  âŸ¨fun n â†¦ hf.approx n + hg.approx n, fun n =>
    (measure_mono (Function.support_add _ _)).trans_lt
      ((measure_union_le _ _).trans_lt
        (ENNReal.add_lt_top.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©)),
    fun x â†¦ (hf.tendsto_approx x).add (hg.tendsto_approx x)âŸ©

@[measurability]
protected theorem neg [AddGroup Î²] [TopologicalAddGroup Î²] (hf : FinStronglyMeasurable f Î¼) :
    FinStronglyMeasurable (-f) Î¼ := by
  refine âŸ¨fun n â†¦ -hf.approx n, fun n â†¦ ?_, fun x â†¦ (hf.tendsto_approx x).negâŸ©
  suffices Î¼ (Function.support fun x â†¦ -(hf.approx n) x) < âˆ by convert this
  rw [Function.support_neg (hf.approx n)]
  exact hf.fin_support_approx n

@[measurability]
protected theorem sub [AddGroup Î²] [ContinuousSub Î²] (hf : FinStronglyMeasurable f Î¼)
    (hg : FinStronglyMeasurable g Î¼) : FinStronglyMeasurable (f - g) Î¼ :=
  âŸ¨fun n â†¦ hf.approx n - hg.approx n, fun n =>
    (measure_mono (Function.support_sub _ _)).trans_lt
      ((measure_union_le _ _).trans_lt
        (ENNReal.add_lt_top.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©)),
    fun x â†¦ (hf.tendsto_approx x).sub (hg.tendsto_approx x)âŸ©

@[measurability]
protected theorem const_smul {ğ•œ} [TopologicalSpace ğ•œ] [AddMonoid Î²] [Monoid ğ•œ]
    [DistribMulAction ğ•œ Î²] [ContinuousSMul ğ•œ Î²] (hf : FinStronglyMeasurable f Î¼) (c : ğ•œ) :
    FinStronglyMeasurable (c â€¢ f) Î¼ := by
  refine âŸ¨fun n â†¦ c â€¢ hf.approx n, fun n â†¦ ?_, fun x â†¦ (hf.tendsto_approx x).const_smul câŸ©
  rw [SimpleFunc.coe_smul]
  exact (measure_mono (support_const_smul_subset c _)).trans_lt (hf.fin_support_approx n)

end Arithmetic

section Order

variable [TopologicalSpace Î²] [Zero Î²]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Î²] [ContinuousSup Î²] (hf : FinStronglyMeasurable f Î¼)
    (hg : FinStronglyMeasurable g Î¼) : FinStronglyMeasurable (f âŠ” g) Î¼ := by
  refine
    âŸ¨fun n â†¦ hf.approx n âŠ” hg.approx n, fun n â†¦ ?_, fun x =>
      (hf.tendsto_approx x).sup_nhds (hg.tendsto_approx x)âŸ©
  refine (measure_mono (support_sup _ _)).trans_lt ?_
  exact measure_union_lt_top_iff.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Î²] [ContinuousInf Î²] (hf : FinStronglyMeasurable f Î¼)
    (hg : FinStronglyMeasurable g Î¼) : FinStronglyMeasurable (f âŠ“ g) Î¼ := by
  refine
    âŸ¨fun n â†¦ hf.approx n âŠ“ hg.approx n, fun n â†¦ ?_, fun x =>
      (hf.tendsto_approx x).inf_nhds (hg.tendsto_approx x)âŸ©
  refine (measure_mono (support_inf _ _)).trans_lt ?_
  exact measure_union_lt_top_iff.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©

end Order

end FinStronglyMeasurable

theorem finStronglyMeasurable_iff_stronglyMeasurable_and_exists_set_sigmaFinite {Î± Î²} {f : Î± â†’ Î²}
    [TopologicalSpace Î²] [T2Space Î²] [Zero Î²] {_ : MeasurableSpace Î±} {Î¼ : Measure Î±} :
    FinStronglyMeasurable f Î¼ â†”
      StronglyMeasurable f âˆ§
        âˆƒ t, MeasurableSet t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ SigmaFinite (Î¼.restrict t) :=
  âŸ¨fun hf => âŸ¨hf.stronglyMeasurable, hf.exists_set_sigmaFiniteâŸ©, fun hf =>
    hf.1.finStronglyMeasurable_of_set_sigmaFinite hf.2.choose_spec.1 hf.2.choose_spec.2.1
      hf.2.choose_spec.2.2âŸ©

theorem aefinStronglyMeasurable_zero {Î± Î²} {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) [Zero Î²]
    [TopologicalSpace Î²] : AEFinStronglyMeasurable (0 : Î± â†’ Î²) Î¼ :=
  âŸ¨0, finStronglyMeasurable_zero, EventuallyEq.rflâŸ©

/-! ## Almost everywhere strongly measurable functions -/

@[measurability]
theorem aestronglyMeasurable_const {Î± Î²} {_ : MeasurableSpace Î±} {Î¼ : Measure Î±}
    [TopologicalSpace Î²] {b : Î²} : AEStronglyMeasurable (fun _ : Î± => b) Î¼ :=
  stronglyMeasurable_const.aestronglyMeasurable

@[to_additive (attr := measurability)]
theorem aestronglyMeasurable_one {Î± Î²} {_ : MeasurableSpace Î±} {Î¼ : Measure Î±} [TopologicalSpace Î²]
    [One Î²] : AEStronglyMeasurable (1 : Î± â†’ Î²) Î¼ :=
  stronglyMeasurable_one.aestronglyMeasurable

@[simp]
theorem Subsingleton.aestronglyMeasurable {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    [Subsingleton Î²] {Î¼ : Measure Î±} (f : Î± â†’ Î²) : AEStronglyMeasurable f Î¼ :=
  (Subsingleton.stronglyMeasurable f).aestronglyMeasurable

@[simp]
theorem Subsingleton.aestronglyMeasurable' {_ : MeasurableSpace Î±} [TopologicalSpace Î²]
    [Subsingleton Î±] {Î¼ : Measure Î±} (f : Î± â†’ Î²) : AEStronglyMeasurable f Î¼ :=
  (Subsingleton.stronglyMeasurable' f).aestronglyMeasurable

@[simp]
theorem aestronglyMeasurable_zero_measure [MeasurableSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) :
    AEStronglyMeasurable f (0 : Measure Î±) := by
  nontriviality Î±
  inhabit Î±
  exact âŸ¨fun _ => f default, stronglyMeasurable_const, rflâŸ©

@[measurability]
theorem SimpleFunc.aestronglyMeasurable {_ : MeasurableSpace Î±} {Î¼ : Measure Î±} [TopologicalSpace Î²]
    (f : Î± â†’â‚› Î²) : AEStronglyMeasurable f Î¼ :=
  f.stronglyMeasurable.aestronglyMeasurable

namespace AEStronglyMeasurable

variable {m : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} [TopologicalSpace Î²] [TopologicalSpace Î³]
  {f g : Î± â†’ Î²}

lemma of_finite [DiscreteMeasurableSpace Î±] [Finite Î±] : AEStronglyMeasurable f Î¼ :=
  âŸ¨_, .of_finite, ae_eq_rflâŸ©

section Mk

/-- A `StronglyMeasurable` function such that `f =áµ[Î¼] hf.mk f`. See lemmas
`stronglyMeasurable_mk` and `ae_eq_mk`. -/
protected noncomputable def mk (f : Î± â†’ Î²) (hf : AEStronglyMeasurable f Î¼) : Î± â†’ Î² :=
  hf.choose

theorem stronglyMeasurable_mk (hf : AEStronglyMeasurable f Î¼) : StronglyMeasurable (hf.mk f) :=
  hf.choose_spec.1

theorem measurable_mk [PseudoMetrizableSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²]
    (hf : AEStronglyMeasurable f Î¼) : Measurable (hf.mk f) :=
  hf.stronglyMeasurable_mk.measurable

theorem ae_eq_mk (hf : AEStronglyMeasurable f Î¼) : f =áµ[Î¼] hf.mk f :=
  hf.choose_spec.2

@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {Î²} [MeasurableSpace Î²] [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] [BorelSpace Î²] {f : Î± â†’ Î²} (hf : AEStronglyMeasurable f Î¼) :
    AEMeasurable f Î¼ :=
  âŸ¨hf.mk f, hf.stronglyMeasurable_mk.measurable, hf.ae_eq_mkâŸ©

end Mk

theorem congr (hf : AEStronglyMeasurable f Î¼) (h : f =áµ[Î¼] g) : AEStronglyMeasurable g Î¼ :=
  âŸ¨hf.mk f, hf.stronglyMeasurable_mk, h.symm.trans hf.ae_eq_mkâŸ©

theorem _root_.aestronglyMeasurable_congr (h : f =áµ[Î¼] g) :
    AEStronglyMeasurable f Î¼ â†” AEStronglyMeasurable g Î¼ :=
  âŸ¨fun hf => hf.congr h, fun hg => hg.congr h.symmâŸ©

theorem mono_measure {Î½ : Measure Î±} (hf : AEStronglyMeasurable f Î¼) (h : Î½ â‰¤ Î¼) :
    AEStronglyMeasurable f Î½ :=
  âŸ¨hf.mk f, hf.stronglyMeasurable_mk, Eventually.filter_mono (ae_mono h) hf.ae_eq_mkâŸ©

protected lemma mono_ac (h : Î½ â‰ª Î¼) (hÎ¼ : AEStronglyMeasurable f Î¼) : AEStronglyMeasurable f Î½ :=
  let âŸ¨g, hg, hg'âŸ© := hÎ¼; âŸ¨g, hg, h.ae_eq hg'âŸ©

@[deprecated (since := "2024-02-15")] protected alias mono' := AEStronglyMeasurable.mono_ac

theorem mono_set {s t} (h : s âŠ† t) (ht : AEStronglyMeasurable f (Î¼.restrict t)) :
    AEStronglyMeasurable f (Î¼.restrict s) :=
  ht.mono_measure (restrict_mono h le_rfl)

protected theorem restrict (hfm : AEStronglyMeasurable f Î¼) {s} :
    AEStronglyMeasurable f (Î¼.restrict s) :=
  hfm.mono_measure Measure.restrict_le_self

theorem ae_mem_imp_eq_mk {s} (h : AEStronglyMeasurable f (Î¼.restrict s)) :
    âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = h.mk f x :=
  ae_imp_of_ae_restrict h.ae_eq_mk

/-- The composition of a continuous function and an ae strongly measurable function is ae strongly
measurable. -/
theorem _root_.Continuous.comp_aestronglyMeasurable {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Continuous g)
    (hf : AEStronglyMeasurable f Î¼) : AEStronglyMeasurable (fun x â†¦ g (f x)) Î¼ :=
  âŸ¨_, hg.comp_stronglyMeasurable hf.stronglyMeasurable_mk, EventuallyEq.fun_comp hf.ae_eq_mk gâŸ©

/-- A continuous function from `Î±` to `Î²` is ae strongly measurable when one of the two spaces is
second countable. -/
theorem _root_.Continuous.aestronglyMeasurable [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [PseudoMetrizableSpace Î²] [SecondCountableTopologyEither Î± Î²] (hf : Continuous f) :
    AEStronglyMeasurable f Î¼ :=
  hf.stronglyMeasurable.aestronglyMeasurable

protected theorem prod_mk {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) : AEStronglyMeasurable (fun x â†¦ (f x, g x)) Î¼ :=
  âŸ¨fun x â†¦ (hf.mk f x, hg.mk g x), hf.stronglyMeasurable_mk.prod_mk hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.prod_mk hg.ae_eq_mkâŸ©

/-- The composition of a continuous function of two variables and two ae strongly measurable
functions is ae strongly measurable. -/
theorem _root_.Continuous.comp_aestronglyMeasurableâ‚‚
    {Î²' : Type*} [TopologicalSpace Î²']
    {g : Î² â†’ Î²' â†’ Î³} {f : Î± â†’ Î²} {f' : Î± â†’ Î²'} (hg : Continuous g.uncurry)
    (hf : AEStronglyMeasurable f Î¼) (h'f : AEStronglyMeasurable f' Î¼) :
    AEStronglyMeasurable (fun x â†¦ g (f x) (f' x)) Î¼ :=
  hg.comp_aestronglyMeasurable (hf.prod_mk h'f)

/-- In a space with second countable topology, measurable implies ae strongly measurable. -/
@[fun_prop, aesop unsafe 30% apply (rule_sets := [Measurable])]
theorem _root_.Measurable.aestronglyMeasurable {_ : MeasurableSpace Î±} {Î¼ : Measure Î±}
    [MeasurableSpace Î²] [PseudoMetrizableSpace Î²] [SecondCountableTopology Î²]
    [OpensMeasurableSpace Î²] (hf : Measurable f) : AEStronglyMeasurable f Î¼ :=
  hf.stronglyMeasurable.aestronglyMeasurable

section Arithmetic

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem mul [Mul Î²] [ContinuousMul Î²] (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) : AEStronglyMeasurable (f * g) Î¼ :=
  âŸ¨hf.mk f * hg.mk g, hf.stronglyMeasurable_mk.mul hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.mul hg.ae_eq_mkâŸ©

@[to_additive (attr := measurability)]
protected theorem mul_const [Mul Î²] [ContinuousMul Î²] (hf : AEStronglyMeasurable f Î¼) (c : Î²) :
    AEStronglyMeasurable (fun x â†¦ f x * c) Î¼ :=
  hf.mul aestronglyMeasurable_const

@[to_additive (attr := measurability)]
protected theorem const_mul [Mul Î²] [ContinuousMul Î²] (hf : AEStronglyMeasurable f Î¼) (c : Î²) :
    AEStronglyMeasurable (fun x â†¦ c * f x) Î¼ :=
  aestronglyMeasurable_const.mul hf

@[to_additive (attr := measurability)]
protected theorem inv [Inv Î²] [ContinuousInv Î²] (hf : AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable fâ»Â¹ Î¼ :=
  âŸ¨(hf.mk f)â»Â¹, hf.stronglyMeasurable_mk.inv, hf.ae_eq_mk.invâŸ©

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem div [Group Î²] [TopologicalGroup Î²] (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) : AEStronglyMeasurable (f / g) Î¼ :=
  âŸ¨hf.mk f / hg.mk g, hf.stronglyMeasurable_mk.div hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.div hg.ae_eq_mkâŸ©

@[to_additive]
theorem mul_iff_right [CommGroup Î²] [TopologicalGroup Î²] (hf : AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable (f * g) Î¼ â†” AEStronglyMeasurable g Î¼ :=
  âŸ¨fun h â†¦ show g = f * g * fâ»Â¹ by simp only [mul_inv_cancel_comm] â–¸ h.mul hf.inv,
    fun h â†¦ hf.mul hâŸ©

@[to_additive]
theorem mul_iff_left [CommGroup Î²] [TopologicalGroup Î²] (hf : AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable (g * f) Î¼ â†” AEStronglyMeasurable g Î¼ :=
  mul_comm g f â–¸ AEStronglyMeasurable.mul_iff_right hf

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem smul {ğ•œ} [TopologicalSpace ğ•œ] [SMul ğ•œ Î²] [ContinuousSMul ğ•œ Î²] {f : Î± â†’ ğ•œ}
    {g : Î± â†’ Î²} (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    AEStronglyMeasurable (fun x â†¦ f x â€¢ g x) Î¼ :=
  continuous_smul.comp_aestronglyMeasurable (hf.prod_mk hg)

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable])) const_nsmul]
protected theorem pow [Monoid Î²] [ContinuousMul Î²] (hf : AEStronglyMeasurable f Î¼) (n : â„•) :
    AEStronglyMeasurable (f ^ n) Î¼ :=
  âŸ¨hf.mk f ^ n, hf.stronglyMeasurable_mk.pow _, hf.ae_eq_mk.pow_const _âŸ©

@[to_additive (attr := measurability)]
protected theorem const_smul {ğ•œ} [SMul ğ•œ Î²] [ContinuousConstSMul ğ•œ Î²]
    (hf : AEStronglyMeasurable f Î¼) (c : ğ•œ) : AEStronglyMeasurable (c â€¢ f) Î¼ :=
  âŸ¨c â€¢ hf.mk f, hf.stronglyMeasurable_mk.const_smul c, hf.ae_eq_mk.const_smul câŸ©

@[to_additive (attr := measurability)]
protected theorem const_smul' {ğ•œ} [SMul ğ•œ Î²] [ContinuousConstSMul ğ•œ Î²]
    (hf : AEStronglyMeasurable f Î¼) (c : ğ•œ) : AEStronglyMeasurable (fun x â†¦ c â€¢ f x) Î¼ :=
  hf.const_smul c

@[to_additive (attr := measurability)]
protected theorem smul_const {ğ•œ} [TopologicalSpace ğ•œ] [SMul ğ•œ Î²] [ContinuousSMul ğ•œ Î²] {f : Î± â†’ ğ•œ}
    (hf : AEStronglyMeasurable f Î¼) (c : Î²) : AEStronglyMeasurable (fun x â†¦ f x â€¢ c) Î¼ :=
  continuous_smul.comp_aestronglyMeasurable (hf.prod_mk aestronglyMeasurable_const)

end Arithmetic

section Order

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Î²] [ContinuousSup Î²] (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) : AEStronglyMeasurable (f âŠ” g) Î¼ :=
  âŸ¨hf.mk f âŠ” hg.mk g, hf.stronglyMeasurable_mk.sup hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.sup hg.ae_eq_mkâŸ©

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Î²] [ContinuousInf Î²] (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) : AEStronglyMeasurable (f âŠ“ g) Î¼ :=
  âŸ¨hf.mk f âŠ“ hg.mk g, hf.stronglyMeasurable_mk.inf hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.inf hg.ae_eq_mkâŸ©

end Order

/-!
### Big operators: `âˆ` and `âˆ‘`
-/


section Monoid

variable {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M]

@[to_additive (attr := measurability)]
theorem _root_.List.aestronglyMeasurable_prod' (l : List (Î± â†’ M))
    (hl : âˆ€ f âˆˆ l, AEStronglyMeasurable f Î¼) : AEStronglyMeasurable l.prod Î¼ := by
  induction' l with f l ihl; Â· exact aestronglyMeasurable_one
  rw [List.forall_mem_cons] at hl
  rw [List.prod_cons]
  exact hl.1.mul (ihl hl.2)

@[to_additive (attr := measurability)]
theorem _root_.List.aestronglyMeasurable_prod
    (l : List (Î± â†’ M)) (hl : âˆ€ f âˆˆ l, AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable (fun x â†¦ (l.map fun f : Î± â†’ M => f x).prod) Î¼ := by
  simpa only [â† Pi.list_prod_apply] using l.aestronglyMeasurable_prod' hl

end Monoid

section CommMonoid

variable {M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M]

@[to_additive (attr := measurability)]
theorem _root_.Multiset.aestronglyMeasurable_prod' (l : Multiset (Î± â†’ M))
    (hl : âˆ€ f âˆˆ l, AEStronglyMeasurable f Î¼) : AEStronglyMeasurable l.prod Î¼ := by
  rcases l with âŸ¨lâŸ©
  simpa using l.aestronglyMeasurable_prod' (by simpa using hl)

@[to_additive (attr := measurability)]
theorem _root_.Multiset.aestronglyMeasurable_prod (s : Multiset (Î± â†’ M))
    (hs : âˆ€ f âˆˆ s, AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable (fun x â†¦ (s.map fun f : Î± â†’ M => f x).prod) Î¼ := by
  simpa only [â† Pi.multiset_prod_apply] using s.aestronglyMeasurable_prod' hs

@[to_additive (attr := measurability)]
theorem _root_.Finset.aestronglyMeasurable_prod' {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ M} (s : Finset Î¹)
    (hf : âˆ€ i âˆˆ s, AEStronglyMeasurable (f i) Î¼) : AEStronglyMeasurable (âˆ i âˆˆ s, f i) Î¼ :=
  Multiset.aestronglyMeasurable_prod' _ fun _g hg =>
    let âŸ¨_i, hi, hgâŸ© := Multiset.mem_map.1 hg
    hg â–¸ hf _ hi

@[to_additive (attr := measurability)]
theorem _root_.Finset.aestronglyMeasurable_prod {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ M} (s : Finset Î¹)
    (hf : âˆ€ i âˆˆ s, AEStronglyMeasurable (f i) Î¼) :
    AEStronglyMeasurable (fun a â†¦ âˆ i âˆˆ s, f i a) Î¼ := by
  simpa only [â† Finset.prod_apply] using s.aestronglyMeasurable_prod' hf

end CommMonoid

section SecondCountableAEStronglyMeasurable

variable [MeasurableSpace Î²]

/-- In a space with second countable topology, measurable implies strongly measurable. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem _root_.AEMeasurable.aestronglyMeasurable [PseudoMetrizableSpace Î²] [OpensMeasurableSpace Î²]
    [SecondCountableTopology Î²] (hf : AEMeasurable f Î¼) : AEStronglyMeasurable f Î¼ :=
  âŸ¨hf.mk f, hf.measurable_mk.stronglyMeasurable, hf.ae_eq_mkâŸ©

@[measurability]
theorem _root_.aestronglyMeasurable_id {Î± : Type*} [TopologicalSpace Î±] [PseudoMetrizableSpace Î±]
    {_ : MeasurableSpace Î±} [OpensMeasurableSpace Î±] [SecondCountableTopology Î±] {Î¼ : Measure Î±} :
    AEStronglyMeasurable (id : Î± â†’ Î±) Î¼ :=
  aemeasurable_id.aestronglyMeasurable

/-- In a space with second countable topology, strongly measurable and measurable are equivalent. -/
theorem _root_.aestronglyMeasurable_iff_aemeasurable [PseudoMetrizableSpace Î²] [BorelSpace Î²]
    [SecondCountableTopology Î²] : AEStronglyMeasurable f Î¼ â†” AEMeasurable f Î¼ :=
  âŸ¨fun h â†¦ h.aemeasurable, fun h â†¦ h.aestronglyMeasurableâŸ©

end SecondCountableAEStronglyMeasurable

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem dist {Î² : Type*} [PseudoMetricSpace Î²] {f g : Î± â†’ Î²}
    (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    AEStronglyMeasurable (fun x â†¦ dist (f x) (g x)) Î¼ :=
  continuous_dist.comp_aestronglyMeasurable (hf.prod_mk hg)

@[measurability]
protected theorem norm {Î² : Type*} [SeminormedAddCommGroup Î²] {f : Î± â†’ Î²}
    (hf : AEStronglyMeasurable f Î¼) : AEStronglyMeasurable (fun x â†¦ â€–f xâ€–) Î¼ :=
  continuous_norm.comp_aestronglyMeasurable hf

@[measurability]
protected theorem nnnorm {Î² : Type*} [SeminormedAddCommGroup Î²] {f : Î± â†’ Î²}
    (hf : AEStronglyMeasurable f Î¼) : AEStronglyMeasurable (fun x â†¦ â€–f xâ€–â‚Š) Î¼ :=
  continuous_nnnorm.comp_aestronglyMeasurable hf

@[measurability]
protected theorem ennnorm {Î² : Type*} [SeminormedAddCommGroup Î²] {f : Î± â†’ Î²}
    (hf : AEStronglyMeasurable f Î¼) : AEMeasurable (fun a â†¦ (â€–f aâ€–â‚Š : â„â‰¥0âˆ)) Î¼ :=
  (ENNReal.continuous_coe.comp_aestronglyMeasurable hf.nnnorm).aemeasurable

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem edist {Î² : Type*} [SeminormedAddCommGroup Î²] {f g : Î± â†’ Î²}
    (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    AEMeasurable (fun a â†¦ edist (f a) (g a)) Î¼ :=
  (continuous_edist.comp_aestronglyMeasurable (hf.prod_mk hg)).aemeasurable

@[measurability]
protected theorem real_toNNReal {f : Î± â†’ â„} (hf : AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable (fun x â†¦ (f x).toNNReal) Î¼ :=
  continuous_real_toNNReal.comp_aestronglyMeasurable hf

theorem _root_.aestronglyMeasurable_indicator_iff [Zero Î²] {s : Set Î±} (hs : MeasurableSet s) :
    AEStronglyMeasurable (indicator s f) Î¼ â†” AEStronglyMeasurable f (Î¼.restrict s) := by
  constructor
  Â· intro h
    exact (h.mono_measure Measure.restrict_le_self).congr (indicator_ae_eq_restrict hs)
  Â· intro h
    refine âŸ¨indicator s (h.mk f), h.stronglyMeasurable_mk.indicator hs, ?_âŸ©
    have A : s.indicator f =áµ[Î¼.restrict s] s.indicator (h.mk f) :=
      (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans <| (indicator_ae_eq_restrict hs).symm)
    have B : s.indicator f =áµ[Î¼.restrict sá¶œ] s.indicator (h.mk f) :=
      (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm
    exact ae_of_ae_restrict_of_ae_restrict_compl _ A B

@[measurability]
protected theorem indicator [Zero Î²] (hfm : AEStronglyMeasurable f Î¼) {s : Set Î±}
    (hs : MeasurableSet s) : AEStronglyMeasurable (s.indicator f) Î¼ :=
  (aestronglyMeasurable_indicator_iff hs).mpr hfm.restrict

theorem nullMeasurableSet_eq_fun {E} [TopologicalSpace E] [MetrizableSpace E] {f g : Î± â†’ E}
    (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    NullMeasurableSet { x | f x = g x } Î¼ := by
  apply
    (hf.stronglyMeasurable_mk.measurableSet_eq_fun
          hg.stronglyMeasurable_mk).nullMeasurableSet.congr
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk] with x hfx hgx
  change (hf.mk f x = hg.mk g x) = (f x = g x)
  simp only [hfx, hgx]

@[to_additive]
lemma nullMeasurableSet_mulSupport {E} [TopologicalSpace E] [MetrizableSpace E] [One E] {f : Î± â†’ E}
    (hf : AEStronglyMeasurable f Î¼) : NullMeasurableSet (mulSupport f) Î¼ :=
  (hf.nullMeasurableSet_eq_fun stronglyMeasurable_const.aestronglyMeasurable).compl

theorem nullMeasurableSet_lt [LinearOrder Î²] [OrderClosedTopology Î²] [PseudoMetrizableSpace Î²]
    {f g : Î± â†’ Î²} (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    NullMeasurableSet { a | f a < g a } Î¼ := by
  apply
    (hf.stronglyMeasurable_mk.measurableSet_lt hg.stronglyMeasurable_mk).nullMeasurableSet.congr
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk] with x hfx hgx
  change (hf.mk f x < hg.mk g x) = (f x < g x)
  simp only [hfx, hgx]

theorem nullMeasurableSet_le [Preorder Î²] [OrderClosedTopology Î²] [PseudoMetrizableSpace Î²]
    {f g : Î± â†’ Î²} (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    NullMeasurableSet { a | f a â‰¤ g a } Î¼ := by
  apply
    (hf.stronglyMeasurable_mk.measurableSet_le hg.stronglyMeasurable_mk).nullMeasurableSet.congr
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk] with x hfx hgx
  change (hf.mk f x â‰¤ hg.mk g x) = (f x â‰¤ g x)
  simp only [hfx, hgx]

theorem _root_.aestronglyMeasurable_of_aestronglyMeasurable_trim {Î±} {m m0 : MeasurableSpace Î±}
    {Î¼ : Measure Î±} (hm : m â‰¤ m0) {f : Î± â†’ Î²} (hf : AEStronglyMeasurable f (Î¼.trim hm)) :
    AEStronglyMeasurable f Î¼ :=
  âŸ¨hf.mk f, StronglyMeasurable.mono hf.stronglyMeasurable_mk hm, ae_eq_of_ae_eq_trim hf.ae_eq_mkâŸ©

theorem comp_aemeasurable {Î³ : Type*} {_ : MeasurableSpace Î³} {_ : MeasurableSpace Î±} {f : Î³ â†’ Î±}
    {Î¼ : Measure Î³} (hg : AEStronglyMeasurable g (Measure.map f Î¼)) (hf : AEMeasurable f Î¼) :
    AEStronglyMeasurable (g âˆ˜ f) Î¼ :=
  âŸ¨hg.mk g âˆ˜ hf.mk f, hg.stronglyMeasurable_mk.comp_measurable hf.measurable_mk,
    (ae_eq_comp hf hg.ae_eq_mk).trans (hf.ae_eq_mk.fun_comp (hg.mk g))âŸ©

theorem comp_measurable {Î³ : Type*} {_ : MeasurableSpace Î³} {_ : MeasurableSpace Î±} {f : Î³ â†’ Î±}
    {Î¼ : Measure Î³} (hg : AEStronglyMeasurable g (Measure.map f Î¼)) (hf : Measurable f) :
    AEStronglyMeasurable (g âˆ˜ f) Î¼ :=
  hg.comp_aemeasurable hf.aemeasurable

theorem comp_quasiMeasurePreserving {Î³ : Type*} {_ : MeasurableSpace Î³} {_ : MeasurableSpace Î±}
    {f : Î³ â†’ Î±} {Î¼ : Measure Î³} {Î½ : Measure Î±} (hg : AEStronglyMeasurable g Î½)
    (hf : QuasiMeasurePreserving f Î¼ Î½) : AEStronglyMeasurable (g âˆ˜ f) Î¼ :=
  (hg.mono_ac hf.absolutelyContinuous).comp_measurable hf.measurable

theorem isSeparable_ae_range (hf : AEStronglyMeasurable f Î¼) :
    âˆƒ t : Set Î², IsSeparable t âˆ§ âˆ€áµ x âˆ‚Î¼, f x âˆˆ t := by
  refine âŸ¨range (hf.mk f), hf.stronglyMeasurable_mk.isSeparable_range, ?_âŸ©
  filter_upwards [hf.ae_eq_mk] with x hx
  simp [hx]

/-- A function is almost everywhere strongly measurable if and only if it is almost everywhere
measurable, and up to a zero measure set its range is contained in a separable set. -/
theorem _root_.aestronglyMeasurable_iff_aemeasurable_separable [PseudoMetrizableSpace Î²]
    [MeasurableSpace Î²] [BorelSpace Î²] :
    AEStronglyMeasurable f Î¼ â†”
      AEMeasurable f Î¼ âˆ§ âˆƒ t : Set Î², IsSeparable t âˆ§ âˆ€áµ x âˆ‚Î¼, f x âˆˆ t := by
  refine âŸ¨fun H => âŸ¨H.aemeasurable, H.isSeparable_ae_rangeâŸ©, ?_âŸ©
  rintro âŸ¨H, âŸ¨t, t_sep, htâŸ©âŸ©
  rcases eq_empty_or_nonempty t with (rfl | hâ‚€)
  Â· simp only [mem_empty_iff_false, eventually_false_iff_eq_bot, ae_eq_bot] at ht
    rw [ht]
    exact aestronglyMeasurable_zero_measure f
  Â· obtain âŸ¨g, g_meas, gt, fgâŸ© : âˆƒ g : Î± â†’ Î², Measurable g âˆ§ range g âŠ† t âˆ§ f =áµ[Î¼] g :=
      H.exists_ae_eq_range_subset ht hâ‚€
    refine âŸ¨g, ?_, fgâŸ©
    exact stronglyMeasurable_iff_measurable_separable.2 âŸ¨g_meas, t_sep.mono gtâŸ©

theorem _root_.aestronglyMeasurable_iff_nullMeasurable_separable [PseudoMetrizableSpace Î²]
    [MeasurableSpace Î²] [BorelSpace Î²] :
    AEStronglyMeasurable f Î¼ â†”
      NullMeasurable f Î¼ âˆ§ âˆƒ t : Set Î², IsSeparable t âˆ§ âˆ€áµ x âˆ‚Î¼, f x âˆˆ t :=
  aestronglyMeasurable_iff_aemeasurable_separable.trans <| and_congr_left fun âŸ¨_, hsep, hâŸ© â†¦
    have := hsep.secondCountableTopology
    âŸ¨AEMeasurable.nullMeasurable, fun hf â†¦ hf.aemeasurable_of_aerange hâŸ©

theorem _root_.MeasurableEmbedding.aestronglyMeasurable_map_iff {Î³ : Type*}
    {mÎ³ : MeasurableSpace Î³} {mÎ± : MeasurableSpace Î±} {f : Î³ â†’ Î±} {Î¼ : Measure Î³}
    (hf : MeasurableEmbedding f) {g : Î± â†’ Î²} :
    AEStronglyMeasurable g (Measure.map f Î¼) â†” AEStronglyMeasurable (g âˆ˜ f) Î¼ := by
  refine âŸ¨fun H => H.comp_measurable hf.measurable, ?_âŸ©
  rintro âŸ¨gâ‚, hgmâ‚, heqâŸ©
  rcases hf.exists_stronglyMeasurable_extend hgmâ‚ fun x â†¦ âŸ¨g xâŸ© with âŸ¨gâ‚‚, hgmâ‚‚, rflâŸ©
  exact âŸ¨gâ‚‚, hgmâ‚‚, hf.ae_map_iff.2 heqâŸ©

theorem _root_.Topology.IsEmbedding.aestronglyMeasurable_comp_iff [PseudoMetrizableSpace Î²]
    [PseudoMetrizableSpace Î³] {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : IsEmbedding g) :
    AEStronglyMeasurable (fun x â†¦ g (f x)) Î¼ â†” AEStronglyMeasurable f Î¼ := by
  letI := pseudoMetrizableSpacePseudoMetric Î³
  borelize Î² Î³
  refine
    âŸ¨fun H => aestronglyMeasurable_iff_aemeasurable_separable.2 âŸ¨?_, ?_âŸ©, fun H =>
      hg.continuous.comp_aestronglyMeasurable HâŸ©
  Â· let G : Î² â†’ range g := rangeFactorization g
    have hG : IsClosedEmbedding G :=
      { hg.codRestrict _ _ with
        isClosed_range := by rw [surjective_onto_range.range_eq]; exact isClosed_univ }
    have : AEMeasurable (G âˆ˜ f) Î¼ := AEMeasurable.subtype_mk H.aemeasurable
    exact hG.measurableEmbedding.aemeasurable_comp_iff.1 this
  Â· rcases (aestronglyMeasurable_iff_aemeasurable_separable.1 H).2 with âŸ¨t, ht, h'tâŸ©
    exact âŸ¨g â»Â¹' t, hg.isSeparable_preimage ht, h'tâŸ©

@[deprecated (since := "2024-10-26")]
alias _root_.Embedding.aestronglyMeasurable_comp_iff := IsEmbedding.aestronglyMeasurable_comp_iff

/-- An almost everywhere sequential limit of almost everywhere strongly measurable functions is
almost everywhere strongly measurable. -/
theorem _root_.aestronglyMeasurable_of_tendsto_ae {Î¹ : Type*} [PseudoMetrizableSpace Î²]
    (u : Filter Î¹) [NeBot u] [IsCountablyGenerated u] {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²}
    (hf : âˆ€ i, AEStronglyMeasurable (f i) Î¼) (lim : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) u (ğ“ (g x))) :
    AEStronglyMeasurable g Î¼ := by
  borelize Î²
  refine aestronglyMeasurable_iff_aemeasurable_separable.2 âŸ¨?_, ?_âŸ©
  Â· exact aemeasurable_of_tendsto_metrizable_ae _ (fun n â†¦ (hf n).aemeasurable) lim
  Â· rcases u.exists_seq_tendsto with âŸ¨v, hvâŸ©
    have : âˆ€ n : â„•, âˆƒ t : Set Î², IsSeparable t âˆ§ f (v n) â»Â¹' t âˆˆ ae Î¼ := fun n =>
      (aestronglyMeasurable_iff_aemeasurable_separable.1 (hf (v n))).2
    choose t t_sep ht using this
    refine âŸ¨closure (â‹ƒ i, t i), .closure <| .iUnion t_sep, ?_âŸ©
    filter_upwards [ae_all_iff.2 ht, lim] with x hx h'x
    apply mem_closure_of_tendsto (h'x.comp hv)
    filter_upwards with n using mem_iUnion_of_mem n (hx n)

/-- If a sequence of almost everywhere strongly measurable functions converges almost everywhere,
one can select a strongly measurable function as the almost everywhere limit. -/
theorem _root_.exists_stronglyMeasurable_limit_of_tendsto_ae [PseudoMetrizableSpace Î²]
    {f : â„• â†’ Î± â†’ Î²} (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (h_ae_tendsto : âˆ€áµ x âˆ‚Î¼, âˆƒ l : Î², Tendsto (fun n â†¦ f n x) atTop (ğ“ l)) :
    âˆƒ f_lim : Î± â†’ Î², StronglyMeasurable f_lim âˆ§
      âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (f_lim x)) := by
  borelize Î²
  obtain âŸ¨g, _, hgâŸ© :
    âˆƒ g : Î± â†’ Î², Measurable g âˆ§ âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (g x)) :=
    measurable_limit_of_tendsto_metrizable_ae (fun n â†¦ (hf n).aemeasurable) h_ae_tendsto
  have Hg : AEStronglyMeasurable g Î¼ := aestronglyMeasurable_of_tendsto_ae _ hf hg
  refine âŸ¨Hg.mk g, Hg.stronglyMeasurable_mk, ?_âŸ©
  filter_upwards [hg, Hg.ae_eq_mk] with x hx h'x
  rwa [h'x] at hx

theorem piecewise {s : Set Î±} [DecidablePred (Â· âˆˆ s)]
    (hs : MeasurableSet s) (hf : AEStronglyMeasurable f (Î¼.restrict s))
    (hg : AEStronglyMeasurable g (Î¼.restrict sá¶œ)) :
    AEStronglyMeasurable (s.piecewise f g) Î¼ := by
  refine âŸ¨s.piecewise (hf.mk f) (hg.mk g),
    StronglyMeasurable.piecewise hs hf.stronglyMeasurable_mk hg.stronglyMeasurable_mk, ?_âŸ©
  refine ae_of_ae_restrict_of_ae_restrict_compl s ?_ ?_
  Â· have h := hf.ae_eq_mk
    rw [Filter.EventuallyEq, ae_restrict_iff' hs] at h
    rw [ae_restrict_iff' hs]
    filter_upwards [h] with x hx
    intro hx_mem
    simp only [hx_mem, Set.piecewise_eq_of_mem, hx hx_mem]
  Â· have h := hg.ae_eq_mk
    rw [Filter.EventuallyEq, ae_restrict_iff' hs.compl] at h
    rw [ae_restrict_iff' hs.compl]
    filter_upwards [h] with x hx
    intro hx_mem
    rw [Set.mem_compl_iff] at hx_mem
    simp only [hx_mem, not_false_eq_true, Set.piecewise_eq_of_not_mem, hx hx_mem]

theorem sum_measure [PseudoMetrizableSpace Î²] {m : MeasurableSpace Î±} {Î¼ : Î¹ â†’ Measure Î±}
    (h : âˆ€ i, AEStronglyMeasurable f (Î¼ i)) : AEStronglyMeasurable f (Measure.sum Î¼) := by
  borelize Î²
  refine
    aestronglyMeasurable_iff_aemeasurable_separable.2
      âŸ¨AEMeasurable.sum_measure fun i â†¦ (h i).aemeasurable, ?_âŸ©
  have A : âˆ€ i : Î¹, âˆƒ t : Set Î², IsSeparable t âˆ§ f â»Â¹' t âˆˆ ae (Î¼ i) := fun i =>
    (aestronglyMeasurable_iff_aemeasurable_separable.1 (h i)).2
  choose t t_sep ht using A
  refine âŸ¨â‹ƒ i, t i, .iUnion t_sep, ?_âŸ©
  simp only [Measure.ae_sum_eq, mem_iUnion, eventually_iSup]
  intro i
  filter_upwards [ht i] with x hx
  exact âŸ¨i, hxâŸ©

@[simp]
theorem _root_.aestronglyMeasurable_sum_measure_iff [PseudoMetrizableSpace Î²]
    {_m : MeasurableSpace Î±} {Î¼ : Î¹ â†’ Measure Î±} :
    AEStronglyMeasurable f (sum Î¼) â†” âˆ€ i, AEStronglyMeasurable f (Î¼ i) :=
  âŸ¨fun h _ => h.mono_measure (Measure.le_sum _ _), sum_measureâŸ©

@[simp]
theorem _root_.aestronglyMeasurable_add_measure_iff [PseudoMetrizableSpace Î²] {Î½ : Measure Î±} :
    AEStronglyMeasurable f (Î¼ + Î½) â†” AEStronglyMeasurable f Î¼ âˆ§ AEStronglyMeasurable f Î½ := by
  rw [â† sum_cond, aestronglyMeasurable_sum_measure_iff, Bool.forall_bool, and_comm]
  rfl

@[measurability]
theorem add_measure [PseudoMetrizableSpace Î²] {Î½ : Measure Î±} {f : Î± â†’ Î²}
    (hÎ¼ : AEStronglyMeasurable f Î¼) (hÎ½ : AEStronglyMeasurable f Î½) :
    AEStronglyMeasurable f (Î¼ + Î½) :=
  aestronglyMeasurable_add_measure_iff.2 âŸ¨hÎ¼, hÎ½âŸ©

@[measurability]
protected theorem iUnion [PseudoMetrizableSpace Î²] {s : Î¹ â†’ Set Î±}
    (h : âˆ€ i, AEStronglyMeasurable f (Î¼.restrict (s i))) :
    AEStronglyMeasurable f (Î¼.restrict (â‹ƒ i, s i)) :=
  (sum_measure h).mono_measure <| restrict_iUnion_le

@[simp]
theorem _root_.aestronglyMeasurable_iUnion_iff [PseudoMetrizableSpace Î²] {s : Î¹ â†’ Set Î±} :
    AEStronglyMeasurable f (Î¼.restrict (â‹ƒ i, s i)) â†”
      âˆ€ i, AEStronglyMeasurable f (Î¼.restrict (s i)) :=
  âŸ¨fun h _ => h.mono_measure <| restrict_mono (subset_iUnion _ _) le_rfl,
    AEStronglyMeasurable.iUnionâŸ©

@[simp]
theorem _root_.aestronglyMeasurable_union_iff [PseudoMetrizableSpace Î²] {s t : Set Î±} :
    AEStronglyMeasurable f (Î¼.restrict (s âˆª t)) â†”
      AEStronglyMeasurable f (Î¼.restrict s) âˆ§ AEStronglyMeasurable f (Î¼.restrict t) := by
  simp only [union_eq_iUnion, aestronglyMeasurable_iUnion_iff, Bool.forall_bool, cond, and_comm]

theorem aestronglyMeasurable_uIoc_iff [LinearOrder Î±] [PseudoMetrizableSpace Î²] {f : Î± â†’ Î²}
    {a b : Î±} :
    AEStronglyMeasurable f (Î¼.restrict <| uIoc a b) â†”
      AEStronglyMeasurable f (Î¼.restrict <| Ioc a b) âˆ§
        AEStronglyMeasurable f (Î¼.restrict <| Ioc b a) := by
  rw [uIoc_eq_union, aestronglyMeasurable_union_iff]

@[measurability]
theorem smul_measure {R : Type*} [Monoid R] [DistribMulAction R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]
    (h : AEStronglyMeasurable f Î¼) (c : R) : AEStronglyMeasurable f (c â€¢ Î¼) :=
  âŸ¨h.mk f, h.stronglyMeasurable_mk, ae_smul_measure h.ae_eq_mk câŸ©

section MulAction

variable {M G Gâ‚€ : Type*}
variable [Monoid M] [MulAction M Î²] [ContinuousConstSMul M Î²]
variable [Group G] [MulAction G Î²] [ContinuousConstSMul G Î²]
variable [GroupWithZero Gâ‚€] [MulAction Gâ‚€ Î²] [ContinuousConstSMul Gâ‚€ Î²]

theorem _root_.aestronglyMeasurable_const_smul_iff (c : G) :
    AEStronglyMeasurable (fun x â†¦ c â€¢ f x) Î¼ â†” AEStronglyMeasurable f Î¼ :=
  âŸ¨fun h â†¦ by simpa only [inv_smul_smul] using h.const_smul' câ»Â¹, fun h â†¦ h.const_smul câŸ©

nonrec theorem _root_.IsUnit.aestronglyMeasurable_const_smul_iff {c : M} (hc : IsUnit c) :
    AEStronglyMeasurable (fun x â†¦ c â€¢ f x) Î¼ â†” AEStronglyMeasurable f Î¼ :=
  let âŸ¨u, huâŸ© := hc
  hu â–¸ aestronglyMeasurable_const_smul_iff u

theorem _root_.aestronglyMeasurable_const_smul_iffâ‚€ {c : Gâ‚€} (hc : c â‰  0) :
    AEStronglyMeasurable (fun x â†¦ c â€¢ f x) Î¼ â†” AEStronglyMeasurable f Î¼ :=
  (IsUnit.mk0 _ hc).aestronglyMeasurable_const_smul_iff

end MulAction

end AEStronglyMeasurable

/-! ## Almost everywhere finitely strongly measurable functions -/


namespace AEFinStronglyMeasurable

variable {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [TopologicalSpace Î²] {f g : Î± â†’ Î²}

section Mk

variable [Zero Î²]

/-- A `fin_strongly_measurable` function such that `f =áµ[Î¼] hf.mk f`. See lemmas
`fin_strongly_measurable_mk` and `ae_eq_mk`. -/
protected noncomputable def mk (f : Î± â†’ Î²) (hf : AEFinStronglyMeasurable f Î¼) : Î± â†’ Î² :=
  hf.choose

theorem finStronglyMeasurable_mk (hf : AEFinStronglyMeasurable f Î¼) :
    FinStronglyMeasurable (hf.mk f) Î¼ :=
  hf.choose_spec.1

theorem ae_eq_mk (hf : AEFinStronglyMeasurable f Î¼) : f =áµ[Î¼] hf.mk f :=
  hf.choose_spec.2

@[aesop 10% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {Î²} [Zero Î²] [MeasurableSpace Î²] [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] [BorelSpace Î²] {f : Î± â†’ Î²} (hf : AEFinStronglyMeasurable f Î¼) :
    AEMeasurable f Î¼ :=
  âŸ¨hf.mk f, hf.finStronglyMeasurable_mk.measurable, hf.ae_eq_mkâŸ©

end Mk

section Arithmetic

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem mul [MonoidWithZero Î²] [ContinuousMul Î²] (hf : AEFinStronglyMeasurable f Î¼)
    (hg : AEFinStronglyMeasurable g Î¼) : AEFinStronglyMeasurable (f * g) Î¼ :=
  âŸ¨hf.mk f * hg.mk g, hf.finStronglyMeasurable_mk.mul hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.mul hg.ae_eq_mkâŸ©

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem add [AddMonoid Î²] [ContinuousAdd Î²] (hf : AEFinStronglyMeasurable f Î¼)
    (hg : AEFinStronglyMeasurable g Î¼) : AEFinStronglyMeasurable (f + g) Î¼ :=
  âŸ¨hf.mk f + hg.mk g, hf.finStronglyMeasurable_mk.add hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.add hg.ae_eq_mkâŸ©

@[measurability]
protected theorem neg [AddGroup Î²] [TopologicalAddGroup Î²] (hf : AEFinStronglyMeasurable f Î¼) :
    AEFinStronglyMeasurable (-f) Î¼ :=
  âŸ¨-hf.mk f, hf.finStronglyMeasurable_mk.neg, hf.ae_eq_mk.negâŸ©

@[measurability]
protected theorem sub [AddGroup Î²] [ContinuousSub Î²] (hf : AEFinStronglyMeasurable f Î¼)
    (hg : AEFinStronglyMeasurable g Î¼) : AEFinStronglyMeasurable (f - g) Î¼ :=
  âŸ¨hf.mk f - hg.mk g, hf.finStronglyMeasurable_mk.sub hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.sub hg.ae_eq_mkâŸ©

@[measurability]
protected theorem const_smul {ğ•œ} [TopologicalSpace ğ•œ] [AddMonoid Î²] [Monoid ğ•œ]
    [DistribMulAction ğ•œ Î²] [ContinuousSMul ğ•œ Î²] (hf : AEFinStronglyMeasurable f Î¼) (c : ğ•œ) :
    AEFinStronglyMeasurable (c â€¢ f) Î¼ :=
  âŸ¨c â€¢ hf.mk f, hf.finStronglyMeasurable_mk.const_smul c, hf.ae_eq_mk.const_smul câŸ©

end Arithmetic

section Order

variable [Zero Î²]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Î²] [ContinuousSup Î²] (hf : AEFinStronglyMeasurable f Î¼)
    (hg : AEFinStronglyMeasurable g Î¼) : AEFinStronglyMeasurable (f âŠ” g) Î¼ :=
  âŸ¨hf.mk f âŠ” hg.mk g, hf.finStronglyMeasurable_mk.sup hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.sup hg.ae_eq_mkâŸ©

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Î²] [ContinuousInf Î²] (hf : AEFinStronglyMeasurable f Î¼)
    (hg : AEFinStronglyMeasurable g Î¼) : AEFinStronglyMeasurable (f âŠ“ g) Î¼ :=
  âŸ¨hf.mk f âŠ“ hg.mk g, hf.finStronglyMeasurable_mk.inf hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.inf hg.ae_eq_mkâŸ©

end Order

variable [Zero Î²] [T2Space Î²]

theorem exists_set_sigmaFinite (hf : AEFinStronglyMeasurable f Î¼) :
    âˆƒ t, MeasurableSet t âˆ§ f =áµ[Î¼.restrict tá¶œ] 0 âˆ§ SigmaFinite (Î¼.restrict t) := by
  rcases hf with âŸ¨g, hg, hfgâŸ©
  obtain âŸ¨t, ht, hgt_zero, htÎ¼âŸ© := hg.exists_set_sigmaFinite
  refine âŸ¨t, ht, ?_, htÎ¼âŸ©
  refine EventuallyEq.trans (ae_restrict_of_ae hfg) ?_
  rw [EventuallyEq, ae_restrict_iff' ht.compl]
  exact Eventually.of_forall hgt_zero

/-- A measurable set `t` such that `f =áµ[Î¼.restrict tá¶œ] 0` and `sigma_finite (Î¼.restrict t)`. -/
def sigmaFiniteSet (hf : AEFinStronglyMeasurable f Î¼) : Set Î± :=
  hf.exists_set_sigmaFinite.choose

protected theorem measurableSet (hf : AEFinStronglyMeasurable f Î¼) :
    MeasurableSet hf.sigmaFiniteSet :=
  hf.exists_set_sigmaFinite.choose_spec.1

theorem ae_eq_zero_compl (hf : AEFinStronglyMeasurable f Î¼) :
    f =áµ[Î¼.restrict hf.sigmaFiniteSetá¶œ] 0 :=
  hf.exists_set_sigmaFinite.choose_spec.2.1

instance sigmaFinite_restrict (hf : AEFinStronglyMeasurable f Î¼) :
    SigmaFinite (Î¼.restrict hf.sigmaFiniteSet) :=
  hf.exists_set_sigmaFinite.choose_spec.2.2

end AEFinStronglyMeasurable

section SecondCountableTopology

variable {G : Type*} [SeminormedAddCommGroup G] [MeasurableSpace G] [BorelSpace G]
  [SecondCountableTopology G] {f : Î± â†’ G}

/-- In a space with second countable topology and a sigma-finite measure, `FinStronglyMeasurable`
  and `Measurable` are equivalent. -/
theorem finStronglyMeasurable_iff_measurable {_m0 : MeasurableSpace Î±} (Î¼ : Measure Î±)
    [SigmaFinite Î¼] : FinStronglyMeasurable f Î¼ â†” Measurable f :=
  âŸ¨fun h â†¦ h.measurable, fun h â†¦ (Measurable.stronglyMeasurable h).finStronglyMeasurable Î¼âŸ©

/-- In a space with second countable topology and a sigma-finite measure, a measurable function
is `FinStronglyMeasurable`. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem finStronglyMeasurable_of_measurable {_m0 : MeasurableSpace Î±} (Î¼ : Measure Î±)
    [SigmaFinite Î¼] (hf : Measurable f) : FinStronglyMeasurable f Î¼ :=
  (finStronglyMeasurable_iff_measurable Î¼).mpr hf

/-- In a space with second countable topology and a sigma-finite measure,
  `AEFinStronglyMeasurable` and `AEMeasurable` are equivalent. -/
theorem aefinStronglyMeasurable_iff_aemeasurable {_m0 : MeasurableSpace Î±} (Î¼ : Measure Î±)
    [SigmaFinite Î¼] : AEFinStronglyMeasurable f Î¼ â†” AEMeasurable f Î¼ := by
  simp_rw [AEFinStronglyMeasurable, AEMeasurable, finStronglyMeasurable_iff_measurable]

/-- In a space with second countable topology and a sigma-finite measure,
  an `AEMeasurable` function is `AEFinStronglyMeasurable`. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem aefinStronglyMeasurable_of_aemeasurable {_m0 : MeasurableSpace Î±} (Î¼ : Measure Î±)
    [SigmaFinite Î¼] (hf : AEMeasurable f Î¼) : AEFinStronglyMeasurable f Î¼ :=
  (aefinStronglyMeasurable_iff_aemeasurable Î¼).mpr hf

end SecondCountableTopology

theorem measurable_uncurry_of_continuous_of_measurable {Î± Î² Î¹ : Type*} [TopologicalSpace Î¹]
    [MetrizableSpace Î¹] [MeasurableSpace Î¹] [SecondCountableTopology Î¹] [OpensMeasurableSpace Î¹]
    {mÎ² : MeasurableSpace Î²} [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [BorelSpace Î²]
    {m : MeasurableSpace Î±} {u : Î¹ â†’ Î± â†’ Î²} (hu_cont : âˆ€ x, Continuous fun i â†¦ u i x)
    (h : âˆ€ i, Measurable (u i)) : Measurable (Function.uncurry u) := by
  obtain âŸ¨t_sf, ht_sfâŸ© :
    âˆƒ t : â„• â†’ SimpleFunc Î¹ Î¹, âˆ€ j x, Tendsto (fun n â†¦ u (t n j) x) atTop (ğ“ <| u j x) := by
    have h_str_meas : StronglyMeasurable (id : Î¹ â†’ Î¹) := stronglyMeasurable_id
    refine âŸ¨h_str_meas.approx, fun j x => ?_âŸ©
    exact ((hu_cont x).tendsto j).comp (h_str_meas.tendsto_approx j)
  let U (n : â„•) (p : Î¹ Ã— Î±) := u (t_sf n p.fst) p.snd
  have h_tendsto : Tendsto U atTop (ğ“ fun p â†¦ u p.fst p.snd) := by
    rw [tendsto_pi_nhds]
    exact fun p â†¦ ht_sf p.fst p.snd
  refine measurable_of_tendsto_metrizable (fun n â†¦ ?_) h_tendsto
  have h_meas : Measurable fun p : (t_sf n).range Ã— Î± => u (â†‘p.fst) p.snd := by
    have :
      (fun p : â†¥(t_sf n).range Ã— Î± => u (â†‘p.fst) p.snd) =
        (fun p : Î± Ã— (t_sf n).range => u (â†‘p.snd) p.fst) âˆ˜ Prod.swap :=
      rfl
    rw [this, @measurable_swap_iff Î± (â†¥(t_sf n).range) Î² m]
    exact measurable_from_prod_countable fun j â†¦ h j
  have :
    (fun p : Î¹ Ã— Î± => u (t_sf n p.fst) p.snd) =
      (fun p : â†¥(t_sf n).range Ã— Î± => u p.fst p.snd) âˆ˜ fun p : Î¹ Ã— Î± =>
        (âŸ¨t_sf n p.fst, SimpleFunc.mem_range_self _ _âŸ©, p.snd) :=
    rfl
  simp_rw [U, this]
  refine h_meas.comp (Measurable.prod_mk ?_ measurable_snd)
  exact ((t_sf n).measurable.comp measurable_fst).subtype_mk

theorem stronglyMeasurable_uncurry_of_continuous_of_stronglyMeasurable {Î± Î² Î¹ : Type*}
    [TopologicalSpace Î¹] [MetrizableSpace Î¹] [MeasurableSpace Î¹] [SecondCountableTopology Î¹]
    [OpensMeasurableSpace Î¹] [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [MeasurableSpace Î±]
    {u : Î¹ â†’ Î± â†’ Î²} (hu_cont : âˆ€ x, Continuous fun i â†¦ u i x) (h : âˆ€ i, StronglyMeasurable (u i)) :
    StronglyMeasurable (Function.uncurry u) := by
  borelize Î²
  obtain âŸ¨t_sf, ht_sfâŸ© :
    âˆƒ t : â„• â†’ SimpleFunc Î¹ Î¹, âˆ€ j x, Tendsto (fun n â†¦ u (t n j) x) atTop (ğ“ <| u j x) := by
    have h_str_meas : StronglyMeasurable (id : Î¹ â†’ Î¹) := stronglyMeasurable_id
    refine âŸ¨h_str_meas.approx, fun j x => ?_âŸ©
    exact ((hu_cont x).tendsto j).comp (h_str_meas.tendsto_approx j)
  let U (n : â„•) (p : Î¹ Ã— Î±) := u (t_sf n p.fst) p.snd
  have h_tendsto : Tendsto U atTop (ğ“ fun p â†¦ u p.fst p.snd) := by
    rw [tendsto_pi_nhds]
    exact fun p â†¦ ht_sf p.fst p.snd
  refine stronglyMeasurable_of_tendsto _ (fun n â†¦ ?_) h_tendsto
  have h_str_meas : StronglyMeasurable fun p : (t_sf n).range Ã— Î± => u (â†‘p.fst) p.snd := by
    refine stronglyMeasurable_iff_measurable_separable.2 âŸ¨?_, ?_âŸ©
    Â· have :
        (fun p : â†¥(t_sf n).range Ã— Î± => u (â†‘p.fst) p.snd) =
          (fun p : Î± Ã— (t_sf n).range => u (â†‘p.snd) p.fst) âˆ˜ Prod.swap :=
        rfl
      rw [this, measurable_swap_iff]
      exact measurable_from_prod_countable fun j â†¦ (h j).measurable
    Â· have : IsSeparable (â‹ƒ i : (t_sf n).range, range (u i)) :=
        .iUnion fun i â†¦ (h i).isSeparable_range
      apply this.mono
      rintro _ âŸ¨âŸ¨i, xâŸ©, rflâŸ©
      simp only [mem_iUnion, mem_range]
      exact âŸ¨i, x, rflâŸ©
  have :
    (fun p : Î¹ Ã— Î± => u (t_sf n p.fst) p.snd) =
      (fun p : â†¥(t_sf n).range Ã— Î± => u p.fst p.snd) âˆ˜ fun p : Î¹ Ã— Î± =>
        (âŸ¨t_sf n p.fst, SimpleFunc.mem_range_self _ _âŸ©, p.snd) :=
    rfl
  simp_rw [U, this]
  refine h_str_meas.comp_measurable (Measurable.prod_mk ?_ measurable_snd)
  exact ((t_sf n).measurable.comp measurable_fst).subtype_mk

end MeasureTheory

set_option linter.style.longFile 2000
