/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, S√©bastien Gou√´zel
-/
import Mathlib.Analysis.NormedSpace.Basic
import Mathlib.MeasureTheory.Function.SimpleFuncDense

#align_import measure_theory.function.strongly_measurable.basic from "leanprover-community/mathlib"@"3b52265189f3fb43aa631edffce5d060fafaf82f"

/-!
# Strongly measurable and finitely strongly measurable functions

A function `f` is said to be strongly measurable if `f` is the sequential limit of simple functions.
It is said to be finitely strongly measurable with respect to a measure `Œº` if the supports
of those simple functions have finite measure. We also provide almost everywhere versions of
these notions.

Almost everywhere strongly measurable functions form the largest class of functions that can be
integrated using the Bochner integral.

If the target space has a second countable topology, strongly measurable and measurable are
equivalent.

If the measure is sigma-finite, strongly measurable and finitely strongly measurable are equivalent.

The main property of finitely strongly measurable functions is
`FinStronglyMeasurable.exists_set_sigmaFinite`: there exists a measurable set `t` such that the
function is supported on `t` and `Œº.restrict t` is sigma-finite. As a consequence, we can prove some
results for those functions as if the measure was sigma-finite.

## Main definitions

* `StronglyMeasurable f`: `f : Œ± ‚Üí Œ≤` is the limit of a sequence `fs : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤`.
* `FinStronglyMeasurable f Œº`: `f : Œ± ‚Üí Œ≤` is the limit of a sequence `fs : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤`
  such that for all `n ‚àà ‚Ñï`, the measure of the support of `fs n` is finite.
* `AEStronglyMeasurable f Œº`: `f` is almost everywhere equal to a `StronglyMeasurable` function.
* `AEFinStronglyMeasurable f Œº`: `f` is almost everywhere equal to a `FinStronglyMeasurable`
  function.

* `AEFinStronglyMeasurable.sigmaFiniteSet`: a measurable set `t` such that
  `f =·µê[Œº.restrict t·∂ú] 0` and `Œº.restrict t` is sigma-finite.

## Main statements

* `AEFinStronglyMeasurable.exists_set_sigmaFinite`: there exists a measurable set `t` such that
  `f =·µê[Œº.restrict t·∂ú] 0` and `Œº.restrict t` is sigma-finite.

We provide a solid API for strongly measurable functions, and for almost everywhere strongly
measurable functions, as a basis for the Bochner integral.

## References

* Hyt√∂nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.

-/


open MeasureTheory Filter TopologicalSpace Function Set MeasureTheory.Measure

open ENNReal Topology MeasureTheory NNReal

variable {Œ± Œ≤ Œ≥ Œπ : Type*} [Countable Œπ]

namespace MeasureTheory

local infixr:25 " ‚Üí‚Çõ " => SimpleFunc

section Definitions

variable [TopologicalSpace Œ≤]

/-- A function is `StronglyMeasurable` if it is the limit of simple functions. -/
def StronglyMeasurable [MeasurableSpace Œ±] (f : Œ± ‚Üí Œ≤) : Prop :=
  ‚àÉ fs : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤, ‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x))
#align measure_theory.strongly_measurable MeasureTheory.StronglyMeasurable

/-- The notation for StronglyMeasurable giving the measurable space instance explicitly. -/
scoped notation "StronglyMeasurable[" m "]" => @MeasureTheory.StronglyMeasurable _ _ _ m

/-- A function is `FinStronglyMeasurable` with respect to a measure if it is the limit of simple
  functions with support with finite measure. -/
def FinStronglyMeasurable [Zero Œ≤]
    {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí Œ≤) (Œº : Measure Œ± := by volume_tac) : Prop :=
  ‚àÉ fs : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤, (‚àÄ n, Œº (support (fs n)) < ‚àû) ‚àß ‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x))
#align measure_theory.fin_strongly_measurable MeasureTheory.FinStronglyMeasurable

/-- A function is `AEStronglyMeasurable` with respect to a measure `Œº` if it is almost everywhere
equal to the limit of a sequence of simple functions. -/
def AEStronglyMeasurable
    {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí Œ≤) (Œº : Measure Œ± := by volume_tac) : Prop :=
  ‚àÉ g, StronglyMeasurable g ‚àß f =·µê[Œº] g
#align measure_theory.ae_strongly_measurable MeasureTheory.AEStronglyMeasurable

/-- A function is `AEFinStronglyMeasurable` with respect to a measure if it is almost everywhere
equal to the limit of a sequence of simple functions with support with finite measure. -/
def AEFinStronglyMeasurable
    [Zero Œ≤] {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí Œ≤) (Œº : Measure Œ± := by volume_tac) : Prop :=
  ‚àÉ g, FinStronglyMeasurable g Œº ‚àß f =·µê[Œº] g
#align measure_theory.ae_fin_strongly_measurable MeasureTheory.AEFinStronglyMeasurable

end Definitions

open MeasureTheory

/-! ## Strongly measurable functions -/

@[aesop 30% apply (rule_sets := [Measurable])]
protected theorem StronglyMeasurable.aestronglyMeasurable {Œ± Œ≤} {_ : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {Œº : Measure Œ±} (hf : StronglyMeasurable f) :
    AEStronglyMeasurable f Œº :=
  ‚ü®f, hf, EventuallyEq.refl _ _‚ü©
#align measure_theory.strongly_measurable.ae_strongly_measurable MeasureTheory.StronglyMeasurable.aestronglyMeasurable

@[simp]
theorem Subsingleton.stronglyMeasurable {Œ± Œ≤} [MeasurableSpace Œ±] [TopologicalSpace Œ≤]
    [Subsingleton Œ≤] (f : Œ± ‚Üí Œ≤) : StronglyMeasurable f := by
  let f_sf : Œ± ‚Üí‚Çõ Œ≤ := ‚ü®f, fun x => ?_, Set.Subsingleton.finite Set.subsingleton_of_subsingleton‚ü©
  ¬∑ exact ‚ü®fun _ => f_sf, fun x => tendsto_const_nhds‚ü©
  ¬∑ have h_univ : f ‚Åª¬π' {x} = Set.univ := by
      ext1 y
      simp [eq_iff_true_of_subsingleton]
    rw [h_univ]
    exact MeasurableSet.univ
#align measure_theory.subsingleton.strongly_measurable MeasureTheory.Subsingleton.stronglyMeasurable

theorem SimpleFunc.stronglyMeasurable {Œ± Œ≤} {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    (f : Œ± ‚Üí‚Çõ Œ≤) : StronglyMeasurable f :=
  ‚ü®fun _ => f, fun _ => tendsto_const_nhds‚ü©
#align measure_theory.simple_func.strongly_measurable MeasureTheory.SimpleFunc.stronglyMeasurable

@[nontriviality]
theorem StronglyMeasurable.of_finite [Finite Œ±] {_ : MeasurableSpace Œ±}
    [MeasurableSingletonClass Œ±] [TopologicalSpace Œ≤]
    (f : Œ± ‚Üí Œ≤) : StronglyMeasurable f :=
  ‚ü®fun _ => SimpleFunc.ofFinite f, fun _ => tendsto_const_nhds‚ü©

@[deprecated (since := "2024-02-05")]
alias stronglyMeasurable_of_fintype := StronglyMeasurable.of_finite

@[deprecated StronglyMeasurable.of_finite (since := "2024-02-06")]
theorem stronglyMeasurable_of_isEmpty [IsEmpty Œ±] {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    (f : Œ± ‚Üí Œ≤) : StronglyMeasurable f :=
  .of_finite f
#align measure_theory.strongly_measurable_of_is_empty MeasureTheory.StronglyMeasurable.of_finite

theorem stronglyMeasurable_const {Œ± Œ≤} {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] {b : Œ≤} :
    StronglyMeasurable fun _ : Œ± => b :=
  ‚ü®fun _ => SimpleFunc.const Œ± b, fun _ => tendsto_const_nhds‚ü©
#align measure_theory.strongly_measurable_const MeasureTheory.stronglyMeasurable_const

@[to_additive]
theorem stronglyMeasurable_one {Œ± Œ≤} {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [One Œ≤] :
    StronglyMeasurable (1 : Œ± ‚Üí Œ≤) :=
  stronglyMeasurable_const
#align measure_theory.strongly_measurable_one MeasureTheory.stronglyMeasurable_one
#align measure_theory.strongly_measurable_zero MeasureTheory.stronglyMeasurable_zero

/-- A version of `stronglyMeasurable_const` that assumes `f x = f y` for all `x, y`.
This version works for functions between empty types. -/
theorem stronglyMeasurable_const' {Œ± Œ≤} {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ‚àÄ x y, f x = f y) : StronglyMeasurable f := by
  nontriviality Œ±
  inhabit Œ±
  convert stronglyMeasurable_const (Œ≤ := Œ≤) using 1
  exact funext fun x => hf x default
#align measure_theory.strongly_measurable_const' MeasureTheory.stronglyMeasurable_const'

-- Porting note: changed binding type of `MeasurableSpace Œ±`.
@[simp]
theorem Subsingleton.stronglyMeasurable' {Œ± Œ≤} [MeasurableSpace Œ±] [TopologicalSpace Œ≤]
    [Subsingleton Œ±] (f : Œ± ‚Üí Œ≤) : StronglyMeasurable f :=
  stronglyMeasurable_const' fun x y => by rw [Subsingleton.elim x y]
#align measure_theory.subsingleton.strongly_measurable' MeasureTheory.Subsingleton.stronglyMeasurable'

namespace StronglyMeasurable

variable {f g : Œ± ‚Üí Œ≤}

section BasicPropertiesInAnyTopologicalSpace

variable [TopologicalSpace Œ≤]

/-- A sequence of simple functions such that
`‚àÄ x, Tendsto (fun n => hf.approx n x) atTop (ùìù (f x))`.
That property is given by `stronglyMeasurable.tendsto_approx`. -/
protected noncomputable def approx {_ : MeasurableSpace Œ±} (hf : StronglyMeasurable f) :
    ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤ :=
  hf.choose
#align measure_theory.strongly_measurable.approx MeasureTheory.StronglyMeasurable.approx

protected theorem tendsto_approx {_ : MeasurableSpace Œ±} (hf : StronglyMeasurable f) :
    ‚àÄ x, Tendsto (fun n => hf.approx n x) atTop (ùìù (f x)) :=
  hf.choose_spec
#align measure_theory.strongly_measurable.tendsto_approx MeasureTheory.StronglyMeasurable.tendsto_approx

/-- Similar to `stronglyMeasurable.approx`, but enforces that the norm of every function in the
sequence is less than `c` everywhere. If `‚Äñf x‚Äñ ‚â§ c` this sequence of simple functions verifies
`Tendsto (fun n => hf.approxBounded n x) atTop (ùìù (f x))`. -/
noncomputable def approxBounded {_ : MeasurableSpace Œ±} [Norm Œ≤] [SMul ‚Ñù Œ≤]
    (hf : StronglyMeasurable f) (c : ‚Ñù) : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤ := fun n =>
  (hf.approx n).map fun x => min 1 (c / ‚Äñx‚Äñ) ‚Ä¢ x
#align measure_theory.strongly_measurable.approx_bounded MeasureTheory.StronglyMeasurable.approxBounded

theorem tendsto_approxBounded_of_norm_le {Œ≤} {f : Œ± ‚Üí Œ≤} [NormedAddCommGroup Œ≤] [NormedSpace ‚Ñù Œ≤]
    {m : MeasurableSpace Œ±} (hf : StronglyMeasurable[m] f) {c : ‚Ñù} {x : Œ±} (hfx : ‚Äñf x‚Äñ ‚â§ c) :
    Tendsto (fun n => hf.approxBounded c n x) atTop (ùìù (f x)) := by
  have h_tendsto := hf.tendsto_approx x
  simp only [StronglyMeasurable.approxBounded, SimpleFunc.coe_map, Function.comp_apply]
  by_cases hfx0 : ‚Äñf x‚Äñ = 0
  ¬∑ rw [norm_eq_zero] at hfx0
    rw [hfx0] at h_tendsto ‚ä¢
    have h_tendsto_norm : Tendsto (fun n => ‚Äñhf.approx n x‚Äñ) atTop (ùìù 0) := by
      convert h_tendsto.norm
      rw [norm_zero]
    refine squeeze_zero_norm (fun n => ?_) h_tendsto_norm
    calc
      ‚Äñmin 1 (c / ‚Äñhf.approx n x‚Äñ) ‚Ä¢ hf.approx n x‚Äñ =
          ‚Äñmin 1 (c / ‚Äñhf.approx n x‚Äñ)‚Äñ * ‚Äñhf.approx n x‚Äñ :=
        norm_smul _ _
      _ ‚â§ ‚Äñ(1 : ‚Ñù)‚Äñ * ‚Äñhf.approx n x‚Äñ := by
        refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
        rw [norm_one, Real.norm_of_nonneg]
        ¬∑ exact min_le_left _ _
        ¬∑ exact le_min zero_le_one (div_nonneg ((norm_nonneg _).trans hfx) (norm_nonneg _))
      _ = ‚Äñhf.approx n x‚Äñ := by rw [norm_one, one_mul]
  rw [‚Üê one_smul ‚Ñù (f x)]
  refine Tendsto.smul ?_ h_tendsto
  have : min 1 (c / ‚Äñf x‚Äñ) = 1 := by
    rw [min_eq_left_iff, one_le_div (lt_of_le_of_ne (norm_nonneg _) (Ne.symm hfx0))]
    exact hfx
  nth_rw 2 [this.symm]
  refine Tendsto.min tendsto_const_nhds ?_
  exact Tendsto.div tendsto_const_nhds h_tendsto.norm hfx0
#align measure_theory.strongly_measurable.tendsto_approx_bounded_of_norm_le MeasureTheory.StronglyMeasurable.tendsto_approxBounded_of_norm_le

theorem tendsto_approxBounded_ae {Œ≤} {f : Œ± ‚Üí Œ≤} [NormedAddCommGroup Œ≤] [NormedSpace ‚Ñù Œ≤]
    {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} (hf : StronglyMeasurable[m] f) {c : ‚Ñù}
    (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => hf.approxBounded c n x) atTop (ùìù (f x)) := by
  filter_upwards [hf_bound] with x hfx using tendsto_approxBounded_of_norm_le hf hfx
#align measure_theory.strongly_measurable.tendsto_approx_bounded_ae MeasureTheory.StronglyMeasurable.tendsto_approxBounded_ae

theorem norm_approxBounded_le {Œ≤} {f : Œ± ‚Üí Œ≤} [SeminormedAddCommGroup Œ≤] [NormedSpace ‚Ñù Œ≤]
    {m : MeasurableSpace Œ±} {c : ‚Ñù} (hf : StronglyMeasurable[m] f) (hc : 0 ‚â§ c) (n : ‚Ñï) (x : Œ±) :
    ‚Äñhf.approxBounded c n x‚Äñ ‚â§ c := by
  simp only [StronglyMeasurable.approxBounded, SimpleFunc.coe_map, Function.comp_apply]
  refine (norm_smul_le _ _).trans ?_
  by_cases h0 : ‚Äñhf.approx n x‚Äñ = 0
  ¬∑ simp only [h0, _root_.div_zero, min_eq_right, zero_le_one, norm_zero, mul_zero]
    exact hc
  rcases le_total ‚Äñhf.approx n x‚Äñ c with h | h
  ¬∑ rw [min_eq_left _]
    ¬∑ simpa only [norm_one, one_mul] using h
    ¬∑ rwa [one_le_div (lt_of_le_of_ne (norm_nonneg _) (Ne.symm h0))]
  ¬∑ rw [min_eq_right _]
    ¬∑ rw [norm_div, norm_norm, mul_comm, mul_div, div_eq_mul_inv, mul_comm, ‚Üê mul_assoc,
        inv_mul_cancel h0, one_mul, Real.norm_of_nonneg hc]
    ¬∑ rwa [div_le_one (lt_of_le_of_ne (norm_nonneg _) (Ne.symm h0))]
#align measure_theory.strongly_measurable.norm_approx_bounded_le MeasureTheory.StronglyMeasurable.norm_approxBounded_le

theorem _root_.stronglyMeasurable_bot_iff [Nonempty Œ≤] [T2Space Œ≤] :
    StronglyMeasurable[‚ä•] f ‚Üî ‚àÉ c, f = fun _ => c := by
  cases' isEmpty_or_nonempty Œ± with hŒ± hŒ±
  ¬∑ simp only [@Subsingleton.stronglyMeasurable' _ _ ‚ä• _ _ f,
      eq_iff_true_of_subsingleton, exists_const]
  refine ‚ü®fun hf => ?_, fun hf_eq => ?_‚ü©
  ¬∑ refine ‚ü®f hŒ±.some, ?_‚ü©
    let fs := hf.approx
    have h_fs_tendsto : ‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x)) := hf.tendsto_approx
    have : ‚àÄ n, ‚àÉ c, ‚àÄ x, fs n x = c := fun n => SimpleFunc.simpleFunc_bot (fs n)
    let cs n := (this n).choose
    have h_cs_eq : ‚àÄ n, ‚áë(fs n) = fun _ => cs n := fun n => funext (this n).choose_spec
    conv at h_fs_tendsto => enter [x, 1, n]; rw [h_cs_eq]
    have h_tendsto : Tendsto cs atTop (ùìù (f hŒ±.some)) := h_fs_tendsto hŒ±.some
    ext1 x
    exact tendsto_nhds_unique (h_fs_tendsto x) h_tendsto
  ¬∑ obtain ‚ü®c, rfl‚ü© := hf_eq
    exact stronglyMeasurable_const
#align strongly_measurable_bot_iff stronglyMeasurable_bot_iff

end BasicPropertiesInAnyTopologicalSpace

theorem finStronglyMeasurable_of_set_sigmaFinite [TopologicalSpace Œ≤] [Zero Œ≤]
    {m : MeasurableSpace Œ±} {Œº : Measure Œ±} (hf_meas : StronglyMeasurable f) {t : Set Œ±}
    (ht : MeasurableSet t) (hft_zero : ‚àÄ x ‚àà t·∂ú, f x = 0) (htŒº : SigmaFinite (Œº.restrict t)) :
    FinStronglyMeasurable f Œº := by
  haveI : SigmaFinite (Œº.restrict t) := htŒº
  let S := spanningSets (Œº.restrict t)
  have hS_meas : ‚àÄ n, MeasurableSet (S n) := measurable_spanningSets (Œº.restrict t)
  let f_approx := hf_meas.approx
  let fs n := SimpleFunc.restrict (f_approx n) (S n ‚à© t)
  have h_fs_t_compl : ‚àÄ n, ‚àÄ x, x ‚àâ t ‚Üí fs n x = 0 := by
    intro n x hxt
    rw [SimpleFunc.restrict_apply _ ((hS_meas n).inter ht)]
    refine Set.indicator_of_not_mem ?_ _
    simp [hxt]
  refine ‚ü®fs, ?_, fun x => ?_‚ü©
  ¬∑ simp_rw [SimpleFunc.support_eq]
    refine fun n => (measure_biUnion_finset_le _ _).trans_lt ?_
    refine ENNReal.sum_lt_top_iff.mpr fun y hy => ?_
    rw [SimpleFunc.restrict_preimage_singleton _ ((hS_meas n).inter ht)]
    swap
    ¬∑ letI : (y : Œ≤) ‚Üí Decidable (y = 0) := fun y => Classical.propDecidable _
      rw [Finset.mem_filter] at hy
      exact hy.2
    refine (measure_mono Set.inter_subset_left).trans_lt ?_
    have h_lt_top := measure_spanningSets_lt_top (Œº.restrict t) n
    rwa [Measure.restrict_apply' ht] at h_lt_top
  ¬∑ by_cases hxt : x ‚àà t
    swap
    ¬∑ rw [funext fun n => h_fs_t_compl n x hxt, hft_zero x hxt]
      exact tendsto_const_nhds
    have h : Tendsto (fun n => (f_approx n) x) atTop (ùìù (f x)) := hf_meas.tendsto_approx x
    obtain ‚ü®n‚ÇÅ, hn‚ÇÅ‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí fs m x = f_approx m x := by
      obtain ‚ü®n, hn‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí x ‚àà S m ‚à© t := by
        rsuffices ‚ü®n, hn‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí x ‚àà S m
        ¬∑ exact ‚ü®n, fun m hnm => Set.mem_inter (hn m hnm) hxt‚ü©
        rsuffices ‚ü®n, hn‚ü© : ‚àÉ n, x ‚àà S n
        ¬∑ exact ‚ü®n, fun m hnm => monotone_spanningSets (Œº.restrict t) hnm hn‚ü©
        rw [‚Üê Set.mem_iUnion, iUnion_spanningSets (Œº.restrict t)]
        trivial
      refine ‚ü®n, fun m hnm => ?_‚ü©
      simp_rw [fs, SimpleFunc.restrict_apply _ ((hS_meas m).inter ht),
        Set.indicator_of_mem (hn m hnm)]
    rw [tendsto_atTop'] at h ‚ä¢
    intro s hs
    obtain ‚ü®n‚ÇÇ, hn‚ÇÇ‚ü© := h s hs
    refine ‚ü®max n‚ÇÅ n‚ÇÇ, fun m hm => ?_‚ü©
    rw [hn‚ÇÅ m ((le_max_left _ _).trans hm.le)]
    exact hn‚ÇÇ m ((le_max_right _ _).trans hm.le)
#align measure_theory.strongly_measurable.fin_strongly_measurable_of_set_sigma_finite MeasureTheory.StronglyMeasurable.finStronglyMeasurable_of_set_sigmaFinite

/-- If the measure is sigma-finite, all strongly measurable functions are
  `FinStronglyMeasurable`. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem finStronglyMeasurable [TopologicalSpace Œ≤] [Zero Œ≤] {m0 : MeasurableSpace Œ±}
    (hf : StronglyMeasurable f) (Œº : Measure Œ±) [SigmaFinite Œº] : FinStronglyMeasurable f Œº :=
  hf.finStronglyMeasurable_of_set_sigmaFinite MeasurableSet.univ (by simp)
    (by rwa [Measure.restrict_univ])
#align measure_theory.strongly_measurable.fin_strongly_measurable MeasureTheory.StronglyMeasurable.finStronglyMeasurable

/-- A strongly measurable function is measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem measurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [MeasurableSpace Œ≤] [BorelSpace Œ≤] (hf : StronglyMeasurable f) : Measurable f :=
  measurable_of_tendsto_metrizable (fun n => (hf.approx n).measurable)
    (tendsto_pi_nhds.mpr hf.tendsto_approx)
#align measure_theory.strongly_measurable.measurable MeasureTheory.StronglyMeasurable.measurable

/-- A strongly measurable function is almost everywhere measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤] {Œº : Measure Œ±}
    (hf : StronglyMeasurable f) : AEMeasurable f Œº :=
  hf.measurable.aemeasurable
#align measure_theory.strongly_measurable.ae_measurable MeasureTheory.StronglyMeasurable.aemeasurable

theorem _root_.Continuous.comp_stronglyMeasurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [TopologicalSpace Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : Continuous g) (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x => g (f x) :=
  ‚ü®fun n => SimpleFunc.map g (hf.approx n), fun x => (hg.tendsto _).comp (hf.tendsto_approx x)‚ü©
#align continuous.comp_strongly_measurable Continuous.comp_stronglyMeasurable

@[to_additive]
nonrec theorem measurableSet_mulSupport {m : MeasurableSpace Œ±} [One Œ≤] [TopologicalSpace Œ≤]
    [MetrizableSpace Œ≤] (hf : StronglyMeasurable f) : MeasurableSet (mulSupport f) := by
  borelize Œ≤
  exact measurableSet_mulSupport hf.measurable
#align measure_theory.strongly_measurable.measurable_set_mul_support MeasureTheory.StronglyMeasurable.measurableSet_mulSupport
#align measure_theory.strongly_measurable.measurable_set_support MeasureTheory.StronglyMeasurable.measurableSet_support

protected theorem mono {m m' : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    (hf : StronglyMeasurable[m'] f) (h_mono : m' ‚â§ m) : StronglyMeasurable[m] f := by
  let f_approx : ‚Ñï ‚Üí @SimpleFunc Œ± m Œ≤ := fun n =>
    @SimpleFunc.mk Œ± m Œ≤
      (hf.approx n)
      (fun x => h_mono _ (SimpleFunc.measurableSet_fiber' _ x))
      (SimpleFunc.finite_range (hf.approx n))
  exact ‚ü®f_approx, hf.tendsto_approx‚ü©
#align measure_theory.strongly_measurable.mono MeasureTheory.StronglyMeasurable.mono

protected theorem prod_mk {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
    {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => (f x, g x) := by
  refine ‚ü®fun n => SimpleFunc.pair (hf.approx n) (hg.approx n), fun x => ?_‚ü©
  rw [nhds_prod_eq]
  exact Tendsto.prod_mk (hf.tendsto_approx x) (hg.tendsto_approx x)
#align measure_theory.strongly_measurable.prod_mk MeasureTheory.StronglyMeasurable.prod_mk

theorem comp_measurable [TopologicalSpace Œ≤] {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥}
    {f : Œ± ‚Üí Œ≤} {g : Œ≥ ‚Üí Œ±} (hf : StronglyMeasurable f) (hg : Measurable g) :
    StronglyMeasurable (f ‚àò g) :=
  ‚ü®fun n => SimpleFunc.comp (hf.approx n) g hg, fun x => hf.tendsto_approx (g x)‚ü©
#align measure_theory.strongly_measurable.comp_measurable MeasureTheory.StronglyMeasurable.comp_measurable

theorem of_uncurry_left [TopologicalSpace Œ≤] {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥}
    {f : Œ± ‚Üí Œ≥ ‚Üí Œ≤} (hf : StronglyMeasurable (uncurry f)) {x : Œ±} : StronglyMeasurable (f x) :=
  hf.comp_measurable measurable_prod_mk_left
#align measure_theory.strongly_measurable.of_uncurry_left MeasureTheory.StronglyMeasurable.of_uncurry_left

theorem of_uncurry_right [TopologicalSpace Œ≤] {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥}
    {f : Œ± ‚Üí Œ≥ ‚Üí Œ≤} (hf : StronglyMeasurable (uncurry f)) {y : Œ≥} :
    StronglyMeasurable fun x => f x y :=
  hf.comp_measurable measurable_prod_mk_right
#align measure_theory.strongly_measurable.of_uncurry_right MeasureTheory.StronglyMeasurable.of_uncurry_right

section Arithmetic

variable {mŒ± : MeasurableSpace Œ±} [TopologicalSpace Œ≤]

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem mul [Mul Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f * g) :=
  ‚ü®fun n => hf.approx n * hg.approx n, fun x => (hf.tendsto_approx x).mul (hg.tendsto_approx x)‚ü©
#align measure_theory.strongly_measurable.mul MeasureTheory.StronglyMeasurable.mul
#align measure_theory.strongly_measurable.add MeasureTheory.StronglyMeasurable.add

@[to_additive (attr := measurability)]
theorem mul_const [Mul Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f) (c : Œ≤) :
    StronglyMeasurable fun x => f x * c :=
  hf.mul stronglyMeasurable_const
#align measure_theory.strongly_measurable.mul_const MeasureTheory.StronglyMeasurable.mul_const
#align measure_theory.strongly_measurable.add_const MeasureTheory.StronglyMeasurable.add_const

@[to_additive (attr := measurability)]
theorem const_mul [Mul Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f) (c : Œ≤) :
    StronglyMeasurable fun x => c * f x :=
  stronglyMeasurable_const.mul hf
#align measure_theory.strongly_measurable.const_mul MeasureTheory.StronglyMeasurable.const_mul
#align measure_theory.strongly_measurable.const_add MeasureTheory.StronglyMeasurable.const_add

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable])) const_nsmul]
protected theorem pow [Monoid Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f) (n : ‚Ñï) :
    StronglyMeasurable (f ^ n) :=
  ‚ü®fun k => hf.approx k ^ n, fun x => (hf.tendsto_approx x).pow n‚ü©

@[to_additive (attr := measurability)]
protected theorem inv [Inv Œ≤] [ContinuousInv Œ≤] (hf : StronglyMeasurable f) :
    StronglyMeasurable f‚Åª¬π :=
  ‚ü®fun n => (hf.approx n)‚Åª¬π, fun x => (hf.tendsto_approx x).inv‚ü©
#align measure_theory.strongly_measurable.inv MeasureTheory.StronglyMeasurable.inv
#align measure_theory.strongly_measurable.neg MeasureTheory.StronglyMeasurable.neg

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem div [Div Œ≤] [ContinuousDiv Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f / g) :=
  ‚ü®fun n => hf.approx n / hg.approx n, fun x => (hf.tendsto_approx x).div' (hg.tendsto_approx x)‚ü©
#align measure_theory.strongly_measurable.div MeasureTheory.StronglyMeasurable.div
#align measure_theory.strongly_measurable.sub MeasureTheory.StronglyMeasurable.sub

@[to_additive]
theorem mul_iff_right [CommGroup Œ≤] [TopologicalGroup Œ≤] (hf : StronglyMeasurable f) :
    StronglyMeasurable (f * g) ‚Üî StronglyMeasurable g :=
  ‚ü®fun h ‚Ü¶ show g = f * g * f‚Åª¬π by simp only [mul_inv_cancel_comm] ‚ñ∏ h.mul hf.inv,
    fun h ‚Ü¶ hf.mul h‚ü©

@[to_additive]
theorem mul_iff_left [CommGroup Œ≤] [TopologicalGroup Œ≤] (hf : StronglyMeasurable f) :
    StronglyMeasurable (g * f) ‚Üî StronglyMeasurable g :=
  mul_comm g f ‚ñ∏ mul_iff_right hf

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem smul {ùïú} [TopologicalSpace ùïú] [SMul ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú}
    {g : Œ± ‚Üí Œ≤} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => f x ‚Ä¢ g x :=
  continuous_smul.comp_stronglyMeasurable (hf.prod_mk hg)
#align measure_theory.strongly_measurable.smul MeasureTheory.StronglyMeasurable.smul
#align measure_theory.strongly_measurable.vadd MeasureTheory.StronglyMeasurable.vadd

@[to_additive (attr := measurability)]
protected theorem const_smul {ùïú} [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] (hf : StronglyMeasurable f)
    (c : ùïú) : StronglyMeasurable (c ‚Ä¢ f) :=
  ‚ü®fun n => c ‚Ä¢ hf.approx n, fun x => (hf.tendsto_approx x).const_smul c‚ü©
#align measure_theory.strongly_measurable.const_smul MeasureTheory.StronglyMeasurable.const_smul

@[to_additive (attr := measurability)]
protected theorem const_smul' {ùïú} [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] (hf : StronglyMeasurable f)
    (c : ùïú) : StronglyMeasurable fun x => c ‚Ä¢ f x :=
  hf.const_smul c
#align measure_theory.strongly_measurable.const_smul' MeasureTheory.StronglyMeasurable.const_smul'

@[to_additive (attr := measurability)]
protected theorem smul_const {ùïú} [TopologicalSpace ùïú] [SMul ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú}
    (hf : StronglyMeasurable f) (c : Œ≤) : StronglyMeasurable fun x => f x ‚Ä¢ c :=
  continuous_smul.comp_stronglyMeasurable (hf.prod_mk stronglyMeasurable_const)
#align measure_theory.strongly_measurable.smul_const MeasureTheory.StronglyMeasurable.smul_const
#align measure_theory.strongly_measurable.vadd_const MeasureTheory.StronglyMeasurable.vadd_const

/-- In a normed vector space, the addition of a measurable function and a strongly measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the addition of two measurable functions. -/
theorem _root_.Measurable.add_stronglyMeasurable
    {Œ± E : Type*} {_ : MeasurableSpace Œ±} [AddGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [PseudoMetrizableSpace E]
    {g f : Œ± ‚Üí E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (g + f) := by
  rcases hf with ‚ü®œÜ, hœÜ‚ü©
  have : Tendsto (fun n x ‚Ü¶ g x + œÜ n x) atTop (ùìù (g + f)) :=
    tendsto_pi_nhds.2 (fun x ‚Ü¶ tendsto_const_nhds.add (hœÜ x))
  apply measurable_of_tendsto_metrizable (fun n ‚Ü¶ ?_) this
  exact hg.add_simpleFunc _

/-- In a normed vector space, the subtraction of a measurable function and a strongly measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the subtraction of two measurable functions. -/
theorem _root_.Measurable.sub_stronglyMeasurable
    {Œ± E : Type*} {_ : MeasurableSpace Œ±} [AddCommGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [ContinuousNeg E] [PseudoMetrizableSpace E]
    {g f : Œ± ‚Üí E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (g - f) := by
  rw [sub_eq_add_neg]
  exact hg.add_stronglyMeasurable hf.neg

/-- In a normed vector space, the addition of a strongly measurable function and a measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the addition of two measurable functions. -/
theorem _root_.Measurable.stronglyMeasurable_add
    {Œ± E : Type*} {_ : MeasurableSpace Œ±} [AddGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [PseudoMetrizableSpace E]
    {g f : Œ± ‚Üí E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (f + g) := by
  rcases hf with ‚ü®œÜ, hœÜ‚ü©
  have : Tendsto (fun n x ‚Ü¶ œÜ n x + g x) atTop (ùìù (f + g)) :=
    tendsto_pi_nhds.2 (fun x ‚Ü¶ (hœÜ x).add tendsto_const_nhds)
  apply measurable_of_tendsto_metrizable (fun n ‚Ü¶ ?_) this
  exact hg.simpleFunc_add _

end Arithmetic

section MulAction

variable {M G G‚ÇÄ : Type*}
variable [TopologicalSpace Œ≤]
variable [Monoid M] [MulAction M Œ≤] [ContinuousConstSMul M Œ≤]
variable [Group G] [MulAction G Œ≤] [ContinuousConstSMul G Œ≤]
variable [GroupWithZero G‚ÇÄ] [MulAction G‚ÇÄ Œ≤] [ContinuousConstSMul G‚ÇÄ Œ≤]

theorem _root_.stronglyMeasurable_const_smul_iff {m : MeasurableSpace Œ±} (c : G) :
    (StronglyMeasurable fun x => c ‚Ä¢ f x) ‚Üî StronglyMeasurable f :=
  ‚ü®fun h => by simpa only [inv_smul_smul] using h.const_smul' c‚Åª¬π, fun h => h.const_smul c‚ü©
#align strongly_measurable_const_smul_iff stronglyMeasurable_const_smul_iff

nonrec theorem _root_.IsUnit.stronglyMeasurable_const_smul_iff {_ : MeasurableSpace Œ±} {c : M}
    (hc : IsUnit c) :
    (StronglyMeasurable fun x => c ‚Ä¢ f x) ‚Üî StronglyMeasurable f :=
  let ‚ü®u, hu‚ü© := hc
  hu ‚ñ∏ stronglyMeasurable_const_smul_iff u
#align is_unit.strongly_measurable_const_smul_iff IsUnit.stronglyMeasurable_const_smul_iff

theorem _root_.stronglyMeasurable_const_smul_iff‚ÇÄ {_ : MeasurableSpace Œ±} {c : G‚ÇÄ} (hc : c ‚â† 0) :
    (StronglyMeasurable fun x => c ‚Ä¢ f x) ‚Üî StronglyMeasurable f :=
  (IsUnit.mk0 _ hc).stronglyMeasurable_const_smul_iff
#align strongly_measurable_const_smul_iff‚ÇÄ stronglyMeasurable_const_smul_iff‚ÇÄ

end MulAction

section Order

variable [MeasurableSpace Œ±] [TopologicalSpace Œ≤]

open Filter

open Filter

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [Sup Œ≤] [ContinuousSup Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f ‚äî g) :=
  ‚ü®fun n => hf.approx n ‚äî hg.approx n, fun x =>
    (hf.tendsto_approx x).sup_nhds (hg.tendsto_approx x)‚ü©
#align measure_theory.strongly_measurable.sup MeasureTheory.StronglyMeasurable.sup

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [Inf Œ≤] [ContinuousInf Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f ‚äì g) :=
  ‚ü®fun n => hf.approx n ‚äì hg.approx n, fun x =>
    (hf.tendsto_approx x).inf_nhds (hg.tendsto_approx x)‚ü©
#align measure_theory.strongly_measurable.inf MeasureTheory.StronglyMeasurable.inf

end Order

/-!
### Big operators: `‚àè` and `‚àë`
-/


section Monoid

variable {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M] {m : MeasurableSpace Œ±}

@[to_additive (attr := measurability)]
theorem _root_.List.stronglyMeasurable_prod' (l : List (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, StronglyMeasurable f) : StronglyMeasurable l.prod := by
  induction' l with f l ihl; ¬∑ exact stronglyMeasurable_one
  rw [List.forall_mem_cons] at hl
  rw [List.prod_cons]
  exact hl.1.mul (ihl hl.2)
#align list.strongly_measurable_prod' List.stronglyMeasurable_prod'
#align list.strongly_measurable_sum' List.stronglyMeasurable_sum'

@[to_additive (attr := measurability)]
theorem _root_.List.stronglyMeasurable_prod (l : List (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, StronglyMeasurable f) :
    StronglyMeasurable fun x => (l.map fun f : Œ± ‚Üí M => f x).prod := by
  simpa only [‚Üê Pi.list_prod_apply] using l.stronglyMeasurable_prod' hl
#align list.strongly_measurable_prod List.stronglyMeasurable_prod
#align list.strongly_measurable_sum List.stronglyMeasurable_sum

end Monoid

section CommMonoid

variable {M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M] {m : MeasurableSpace Œ±}

@[to_additive (attr := measurability)]
theorem _root_.Multiset.stronglyMeasurable_prod' (l : Multiset (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, StronglyMeasurable f) : StronglyMeasurable l.prod := by
  rcases l with ‚ü®l‚ü©
  simpa using l.stronglyMeasurable_prod' (by simpa using hl)
#align multiset.strongly_measurable_prod' Multiset.stronglyMeasurable_prod'
#align multiset.strongly_measurable_sum' Multiset.stronglyMeasurable_sum'

@[to_additive (attr := measurability)]
theorem _root_.Multiset.stronglyMeasurable_prod (s : Multiset (Œ± ‚Üí M))
    (hs : ‚àÄ f ‚àà s, StronglyMeasurable f) :
    StronglyMeasurable fun x => (s.map fun f : Œ± ‚Üí M => f x).prod := by
  simpa only [‚Üê Pi.multiset_prod_apply] using s.stronglyMeasurable_prod' hs
#align multiset.strongly_measurable_prod Multiset.stronglyMeasurable_prod
#align multiset.strongly_measurable_sum Multiset.stronglyMeasurable_sum

@[to_additive (attr := measurability)]
theorem _root_.Finset.stronglyMeasurable_prod' {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí M} (s : Finset Œπ)
    (hf : ‚àÄ i ‚àà s, StronglyMeasurable (f i)) : StronglyMeasurable (‚àè i ‚àà s, f i) :=
  Finset.prod_induction _ _ (fun _a _b ha hb => ha.mul hb) (@stronglyMeasurable_one Œ± M _ _ _) hf
#align finset.strongly_measurable_prod' Finset.stronglyMeasurable_prod'
#align finset.strongly_measurable_sum' Finset.stronglyMeasurable_sum'

@[to_additive (attr := measurability)]
theorem _root_.Finset.stronglyMeasurable_prod {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí M} (s : Finset Œπ)
    (hf : ‚àÄ i ‚àà s, StronglyMeasurable (f i)) : StronglyMeasurable fun a => ‚àè i ‚àà s, f i a := by
  simpa only [‚Üê Finset.prod_apply] using s.stronglyMeasurable_prod' hf
#align finset.strongly_measurable_prod Finset.stronglyMeasurable_prod
#align finset.strongly_measurable_sum Finset.stronglyMeasurable_sum

end CommMonoid

/-- The range of a strongly measurable function is separable. -/
protected theorem isSeparable_range {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    (hf : StronglyMeasurable f) : TopologicalSpace.IsSeparable (range f) := by
  have : IsSeparable (closure (‚ãÉ n, range (hf.approx n))) :=
    .closure <| .iUnion fun n => (hf.approx n).finite_range.isSeparable
  apply this.mono
  rintro _ ‚ü®x, rfl‚ü©
  apply mem_closure_of_tendsto (hf.tendsto_approx x)
  filter_upwards with n
  apply mem_iUnion_of_mem n
  exact mem_range_self _
#align measure_theory.strongly_measurable.is_separable_range MeasureTheory.StronglyMeasurable.isSeparable_range

theorem separableSpace_range_union_singleton {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] (hf : StronglyMeasurable f) {b : Œ≤} :
    SeparableSpace (range f ‚à™ {b} : Set Œ≤) :=
  letI := pseudoMetrizableSpacePseudoMetric Œ≤
  (hf.isSeparable_range.union (finite_singleton _).isSeparable).separableSpace
#align measure_theory.strongly_measurable.separable_space_range_union_singleton MeasureTheory.StronglyMeasurable.separableSpace_range_union_singleton

section SecondCountableStronglyMeasurable

variable {mŒ± : MeasurableSpace Œ±} [MeasurableSpace Œ≤]

/-- In a space with second countable topology, measurable implies strongly measurable. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem _root_.Measurable.stronglyMeasurable [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [SecondCountableTopology Œ≤] [OpensMeasurableSpace Œ≤] (hf : Measurable f) :
    StronglyMeasurable f := by
  letI := pseudoMetrizableSpacePseudoMetric Œ≤
  nontriviality Œ≤; inhabit Œ≤
  exact ‚ü®SimpleFunc.approxOn f hf Set.univ default (Set.mem_univ _), fun x ‚Ü¶
    SimpleFunc.tendsto_approxOn hf (Set.mem_univ _) (by rw [closure_univ]; simp)‚ü©
#align measurable.strongly_measurable Measurable.stronglyMeasurable

/-- In a space with second countable topology, strongly measurable and measurable are equivalent. -/
theorem _root_.stronglyMeasurable_iff_measurable [TopologicalSpace Œ≤] [MetrizableSpace Œ≤]
    [BorelSpace Œ≤] [SecondCountableTopology Œ≤] : StronglyMeasurable f ‚Üî Measurable f :=
  ‚ü®fun h => h.measurable, fun h => Measurable.stronglyMeasurable h‚ü©
#align strongly_measurable_iff_measurable stronglyMeasurable_iff_measurable

@[measurability]
theorem _root_.stronglyMeasurable_id [TopologicalSpace Œ±] [PseudoMetrizableSpace Œ±]
    [OpensMeasurableSpace Œ±] [SecondCountableTopology Œ±] : StronglyMeasurable (id : Œ± ‚Üí Œ±) :=
  measurable_id.stronglyMeasurable
#align strongly_measurable_id stronglyMeasurable_id

end SecondCountableStronglyMeasurable

/-- A function is strongly measurable if and only if it is measurable and has separable
range. -/
theorem _root_.stronglyMeasurable_iff_measurable_separable {m : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤] :
    StronglyMeasurable f ‚Üî Measurable f ‚àß IsSeparable (range f) := by
  refine ‚ü®fun H ‚Ü¶ ‚ü®H.measurable, H.isSeparable_range‚ü©, fun ‚ü®Hm, Hsep‚ü©  ‚Ü¶ ?_‚ü©
  have := Hsep.secondCountableTopology
  have Hm' : StronglyMeasurable (rangeFactorization f) := Hm.subtype_mk.stronglyMeasurable
  exact continuous_subtype_val.comp_stronglyMeasurable Hm'
#align strongly_measurable_iff_measurable_separable stronglyMeasurable_iff_measurable_separable

/-- A continuous function is strongly measurable when either the source space or the target space
is second-countable. -/
theorem _root_.Continuous.stronglyMeasurable [MeasurableSpace Œ±] [TopologicalSpace Œ±]
    [OpensMeasurableSpace Œ±] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [h : SecondCountableTopologyEither Œ± Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Continuous f) :
    StronglyMeasurable f := by
  borelize Œ≤
  cases h.out
  ¬∑ rw [stronglyMeasurable_iff_measurable_separable]
    refine ‚ü®hf.measurable, ?_‚ü©
    exact isSeparable_range hf
  ¬∑ exact hf.measurable.stronglyMeasurable
#align continuous.strongly_measurable Continuous.stronglyMeasurable

/-- A continuous function whose support is contained in a compact set is strongly measurable. -/
@[to_additive]
theorem _root_.Continuous.stronglyMeasurable_of_mulSupport_subset_isCompact
    [MeasurableSpace Œ±] [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±] [MeasurableSpace Œ≤]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] [One Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Continuous f) {k : Set Œ±} (hk : IsCompact k)
    (h'f : mulSupport f ‚äÜ k) : StronglyMeasurable f := by
  letI : PseudoMetricSpace Œ≤ := pseudoMetrizableSpacePseudoMetric Œ≤
  rw [stronglyMeasurable_iff_measurable_separable]
  exact ‚ü®hf.measurable, (isCompact_range_of_mulSupport_subset_isCompact hf hk h'f).isSeparable‚ü©

/-- A continuous function with compact support is strongly measurable. -/
@[to_additive]
theorem _root_.Continuous.stronglyMeasurable_of_hasCompactMulSupport
    [MeasurableSpace Œ±] [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±] [MeasurableSpace Œ≤]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] [One Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Continuous f) (h'f : HasCompactMulSupport f) : StronglyMeasurable f :=
  hf.stronglyMeasurable_of_mulSupport_subset_isCompact h'f (subset_mulTSupport f)

/-- A continuous function with compact support on a product space is strongly measurable for the
product sigma-algebra. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma _root_.HasCompactSupport.stronglyMeasurable_of_prod {X Y : Type*} [Zero Œ±]
    [TopologicalSpace X] [TopologicalSpace Y] [MeasurableSpace X] [MeasurableSpace Y]
    [OpensMeasurableSpace X] [OpensMeasurableSpace Y] [TopologicalSpace Œ±] [PseudoMetrizableSpace Œ±]
    {f : X √ó Y ‚Üí Œ±} (hf : Continuous f) (h'f : HasCompactSupport f) :
    StronglyMeasurable f := by
  borelize Œ±
  apply stronglyMeasurable_iff_measurable_separable.2 ‚ü®h'f.measurable_of_prod hf, ?_‚ü©
  letI : PseudoMetricSpace Œ± := pseudoMetrizableSpacePseudoMetric Œ±
  exact IsCompact.isSeparable (s := range f) (h'f.isCompact_range hf)

/-- If `g` is a topological embedding, then `f` is strongly measurable iff `g ‚àò f` is. -/
theorem _root_.Embedding.comp_stronglyMeasurable_iff {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] [TopologicalSpace Œ≥] [PseudoMetrizableSpace Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤}
    (hg : Embedding g) : (StronglyMeasurable fun x => g (f x)) ‚Üî StronglyMeasurable f := by
  letI := pseudoMetrizableSpacePseudoMetric Œ≥
  borelize Œ≤ Œ≥
  refine
    ‚ü®fun H => stronglyMeasurable_iff_measurable_separable.2 ‚ü®?_, ?_‚ü©, fun H =>
      hg.continuous.comp_stronglyMeasurable H‚ü©
  ¬∑ let G : Œ≤ ‚Üí range g := rangeFactorization g
    have hG : ClosedEmbedding G :=
      { hg.codRestrict _ _ with
        isClosed_range := by
          rw [surjective_onto_range.range_eq]
          exact isClosed_univ }
    have : Measurable (G ‚àò f) := Measurable.subtype_mk H.measurable
    exact hG.measurableEmbedding.measurable_comp_iff.1 this
  ¬∑ have : IsSeparable (g ‚Åª¬π' range (g ‚àò f)) := hg.isSeparable_preimage H.isSeparable_range
    rwa [range_comp, hg.inj.preimage_image] at this
#align embedding.comp_strongly_measurable_iff Embedding.comp_stronglyMeasurable_iff

/-- A sequential limit of strongly measurable functions is strongly measurable. -/
theorem _root_.stronglyMeasurable_of_tendsto {Œπ : Type*} {m : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] (u : Filter Œπ) [NeBot u] [IsCountablyGenerated u]
    {f : Œπ ‚Üí Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≤} (hf : ‚àÄ i, StronglyMeasurable (f i)) (lim : Tendsto f u (ùìù g)) :
    StronglyMeasurable g := by
  borelize Œ≤
  refine stronglyMeasurable_iff_measurable_separable.2 ‚ü®?_, ?_‚ü©
  ¬∑ exact measurable_of_tendsto_metrizable' u (fun i => (hf i).measurable) lim
  ¬∑ rcases u.exists_seq_tendsto with ‚ü®v, hv‚ü©
    have : IsSeparable (closure (‚ãÉ i, range (f (v i)))) :=
      .closure <| .iUnion fun i => (hf (v i)).isSeparable_range
    apply this.mono
    rintro _ ‚ü®x, rfl‚ü©
    rw [tendsto_pi_nhds] at lim
    apply mem_closure_of_tendsto ((lim x).comp hv)
    filter_upwards with n
    apply mem_iUnion_of_mem n
    exact mem_range_self _
#align strongly_measurable_of_tendsto stronglyMeasurable_of_tendsto

protected theorem piecewise {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] {s : Set Œ±}
    {_ : DecidablePred (¬∑ ‚àà s)} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (Set.piecewise s f g) := by
  refine ‚ü®fun n => SimpleFunc.piecewise s hs (hf.approx n) (hg.approx n), fun x => ?_‚ü©
  by_cases hx : x ‚àà s
  ¬∑ simpa [@Set.piecewise_eq_of_mem _ _ _ _ _ (fun _ => Classical.propDecidable _) _ hx,
      hx] using hf.tendsto_approx x
  ¬∑ simpa [@Set.piecewise_eq_of_not_mem _ _ _ _ _ (fun _ => Classical.propDecidable _) _ hx,
      hx] using hg.tendsto_approx x
#align measure_theory.strongly_measurable.piecewise MeasureTheory.StronglyMeasurable.piecewise

/-- this is slightly different from `StronglyMeasurable.piecewise`. It can be used to show
`StronglyMeasurable (ite (x=0) 0 1)` by
`exact StronglyMeasurable.ite (measurableSet_singleton 0) stronglyMeasurable_const
stronglyMeasurable_const`, but replacing `StronglyMeasurable.ite` by
`StronglyMeasurable.piecewise` in that example proof does not work. -/
protected theorem ite {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] {p : Œ± ‚Üí Prop}
    {_ : DecidablePred p} (hp : MeasurableSet { a : Œ± | p a }) (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable fun x => ite (p x) (f x) (g x) :=
  StronglyMeasurable.piecewise hp hf hg
#align measure_theory.strongly_measurable.ite MeasureTheory.StronglyMeasurable.ite

@[measurability]
theorem _root_.MeasurableEmbedding.stronglyMeasurable_extend {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {g' : Œ≥ ‚Üí Œ≤}
    {mŒ± : MeasurableSpace Œ±} {mŒ≥ : MeasurableSpace Œ≥} [TopologicalSpace Œ≤]
    (hg : MeasurableEmbedding g) (hf : StronglyMeasurable f) (hg' : StronglyMeasurable g') :
    StronglyMeasurable (Function.extend g f g') := by
  refine ‚ü®fun n => SimpleFunc.extend (hf.approx n) g hg (hg'.approx n), ?_‚ü©
  intro x
  by_cases hx : ‚àÉ y, g y = x
  ¬∑ rcases hx with ‚ü®y, rfl‚ü©
    simpa only [SimpleFunc.extend_apply, hg.injective, Injective.extend_apply] using
      hf.tendsto_approx y
  ¬∑ simpa only [hx, SimpleFunc.extend_apply', not_false_iff, extend_apply'] using
      hg'.tendsto_approx x
#align measurable_embedding.strongly_measurable_extend MeasurableEmbedding.stronglyMeasurable_extend

theorem _root_.MeasurableEmbedding.exists_stronglyMeasurable_extend {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥}
    {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥} [TopologicalSpace Œ≤]
    (hg : MeasurableEmbedding g) (hf : StronglyMeasurable f) (hne : Œ≥ ‚Üí Nonempty Œ≤) :
    ‚àÉ f' : Œ≥ ‚Üí Œ≤, StronglyMeasurable f' ‚àß f' ‚àò g = f :=
  ‚ü®Function.extend g f fun x => Classical.choice (hne x),
    hg.stronglyMeasurable_extend hf (stronglyMeasurable_const' fun _ _ => rfl),
    funext fun _ => hg.injective.extend_apply _ _ _‚ü©
#align measurable_embedding.exists_strongly_measurable_extend MeasurableEmbedding.exists_stronglyMeasurable_extend

theorem _root_.stronglyMeasurable_of_stronglyMeasurable_union_cover {m : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} (s t : Set Œ±) (hs : MeasurableSet s) (ht : MeasurableSet t)
    (h : univ ‚äÜ s ‚à™ t) (hc : StronglyMeasurable fun a : s => f a)
    (hd : StronglyMeasurable fun a : t => f a) : StronglyMeasurable f := by
  nontriviality Œ≤; inhabit Œ≤
  suffices Function.extend Subtype.val (fun x : s ‚Ü¶ f x)
      (Function.extend (‚Üë) (fun x : t ‚Ü¶ f x) fun _ ‚Ü¶ default) = f from
    this ‚ñ∏ (MeasurableEmbedding.subtype_coe hs).stronglyMeasurable_extend hc <|
      (MeasurableEmbedding.subtype_coe ht).stronglyMeasurable_extend hd stronglyMeasurable_const
  ext x
  by_cases hxs : x ‚àà s
  ¬∑ lift x to s using hxs
    simp [Subtype.coe_injective.extend_apply]
  ¬∑ lift x to t using (h trivial).resolve_left hxs
    rw [extend_apply', Subtype.coe_injective.extend_apply]
    exact fun ‚ü®y, hy‚ü© ‚Ü¶ hxs <| hy ‚ñ∏ y.2
#align strongly_measurable_of_strongly_measurable_union_cover stronglyMeasurable_of_stronglyMeasurable_union_cover

theorem _root_.stronglyMeasurable_of_restrict_of_restrict_compl {_ : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} (hs : MeasurableSet s)
    (h‚ÇÅ : StronglyMeasurable (s.restrict f)) (h‚ÇÇ : StronglyMeasurable (s·∂ú.restrict f)) :
    StronglyMeasurable f :=
  stronglyMeasurable_of_stronglyMeasurable_union_cover s s·∂ú hs hs.compl (union_compl_self s).ge h‚ÇÅ
    h‚ÇÇ
#align strongly_measurable_of_restrict_of_restrict_compl stronglyMeasurable_of_restrict_of_restrict_compl

@[measurability]
protected theorem indicator {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [Zero Œ≤]
    (hf : StronglyMeasurable f) {s : Set Œ±} (hs : MeasurableSet s) :
    StronglyMeasurable (s.indicator f) :=
  hf.piecewise hs stronglyMeasurable_const
#align measure_theory.strongly_measurable.indicator MeasureTheory.StronglyMeasurable.indicator

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem dist {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [PseudoMetricSpace Œ≤] {f g : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => dist (f x) (g x) :=
  continuous_dist.comp_stronglyMeasurable (hf.prod_mk hg)
#align measure_theory.strongly_measurable.dist MeasureTheory.StronglyMeasurable.dist

@[measurability]
protected theorem norm {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x => ‚Äñf x‚Äñ :=
  continuous_norm.comp_stronglyMeasurable hf
#align measure_theory.strongly_measurable.norm MeasureTheory.StronglyMeasurable.norm

@[measurability]
protected theorem nnnorm {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x => ‚Äñf x‚Äñ‚Çä :=
  continuous_nnnorm.comp_stronglyMeasurable hf
#align measure_theory.strongly_measurable.nnnorm MeasureTheory.StronglyMeasurable.nnnorm

@[measurability]
protected theorem ennnorm {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤]
    {f : Œ± ‚Üí Œ≤} (hf : StronglyMeasurable f) : Measurable fun a => (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) :=
  (ENNReal.continuous_coe.comp_stronglyMeasurable hf.nnnorm).measurable
#align measure_theory.strongly_measurable.ennnorm MeasureTheory.StronglyMeasurable.ennnorm

@[measurability]
protected theorem real_toNNReal {_ : MeasurableSpace Œ±} {f : Œ± ‚Üí ‚Ñù} (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x => (f x).toNNReal :=
  continuous_real_toNNReal.comp_stronglyMeasurable hf
#align measure_theory.strongly_measurable.real_to_nnreal MeasureTheory.StronglyMeasurable.real_toNNReal

theorem measurableSet_eq_fun {m : MeasurableSpace Œ±} {E} [TopologicalSpace E] [MetrizableSpace E]
    {f g : Œ± ‚Üí E} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    MeasurableSet { x | f x = g x } := by
  borelize (E √ó E)
  exact (hf.prod_mk hg).measurable isClosed_diagonal.measurableSet
#align measure_theory.strongly_measurable.measurable_set_eq_fun MeasureTheory.StronglyMeasurable.measurableSet_eq_fun

theorem measurableSet_lt {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [LinearOrder Œ≤]
    [OrderClosedTopology Œ≤] [PseudoMetrizableSpace Œ≤] {f g : Œ± ‚Üí Œ≤} (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : MeasurableSet { a | f a < g a } := by
  borelize (Œ≤ √ó Œ≤)
  exact (hf.prod_mk hg).measurable isOpen_lt_prod.measurableSet
#align measure_theory.strongly_measurable.measurable_set_lt MeasureTheory.StronglyMeasurable.measurableSet_lt

theorem measurableSet_le {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [Preorder Œ≤]
    [OrderClosedTopology Œ≤] [PseudoMetrizableSpace Œ≤] {f g : Œ± ‚Üí Œ≤} (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : MeasurableSet { a | f a ‚â§ g a } := by
  borelize (Œ≤ √ó Œ≤)
  exact (hf.prod_mk hg).measurable isClosed_le_prod.measurableSet
#align measure_theory.strongly_measurable.measurable_set_le MeasureTheory.StronglyMeasurable.measurableSet_le

theorem stronglyMeasurable_in_set {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [Zero Œ≤] {s : Set Œ±}
    {f : Œ± ‚Üí Œ≤} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hf_zero : ‚àÄ x, x ‚àâ s ‚Üí f x = 0) :
    ‚àÉ fs : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤,
      (‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x))) ‚àß ‚àÄ x ‚àâ s, ‚àÄ n, fs n x = 0 := by
  let g_seq_s : ‚Ñï ‚Üí @SimpleFunc Œ± m Œ≤ := fun n => (hf.approx n).restrict s
  have hg_eq : ‚àÄ x ‚àà s, ‚àÄ n, g_seq_s n x = hf.approx n x := by
    intro x hx n
    rw [SimpleFunc.coe_restrict _ hs, Set.indicator_of_mem hx]
  have hg_zero : ‚àÄ x ‚àâ s, ‚àÄ n, g_seq_s n x = 0 := by
    intro x hx n
    rw [SimpleFunc.coe_restrict _ hs, Set.indicator_of_not_mem hx]
  refine ‚ü®g_seq_s, fun x => ?_, hg_zero‚ü©
  by_cases hx : x ‚àà s
  ¬∑ simp_rw [hg_eq x hx]
    exact hf.tendsto_approx x
  ¬∑ simp_rw [hg_zero x hx, hf_zero x hx]
    exact tendsto_const_nhds
#align measure_theory.strongly_measurable.strongly_measurable_in_set MeasureTheory.StronglyMeasurable.stronglyMeasurable_in_set

/-- If the restriction to a set `s` of a œÉ-algebra `m` is included in the restriction to `s` of
another œÉ-algebra `m‚ÇÇ` (hypothesis `hs`), the set `s` is `m` measurable and a function `f` supported
on `s` is `m`-strongly-measurable, then `f` is also `m‚ÇÇ`-strongly-measurable. -/
theorem stronglyMeasurable_of_measurableSpace_le_on {Œ± E} {m m‚ÇÇ : MeasurableSpace Œ±}
    [TopologicalSpace E] [Zero E] {s : Set Œ±} {f : Œ± ‚Üí E} (hs_m : MeasurableSet[m] s)
    (hs : ‚àÄ t, MeasurableSet[m] (s ‚à© t) ‚Üí MeasurableSet[m‚ÇÇ] (s ‚à© t))
    (hf : StronglyMeasurable[m] f) (hf_zero : ‚àÄ x ‚àâ s, f x = 0) :
    StronglyMeasurable[m‚ÇÇ] f := by
  have hs_m‚ÇÇ : MeasurableSet[m‚ÇÇ] s := by
    rw [‚Üê Set.inter_univ s]
    refine hs Set.univ ?_
    rwa [Set.inter_univ]
  obtain ‚ü®g_seq_s, hg_seq_tendsto, hg_seq_zero‚ü© := stronglyMeasurable_in_set hs_m hf hf_zero
  let g_seq_s‚ÇÇ : ‚Ñï ‚Üí @SimpleFunc Œ± m‚ÇÇ E := fun n =>
    { toFun := g_seq_s n
      measurableSet_fiber' := fun x => by
        rw [‚Üê Set.inter_univ (g_seq_s n ‚Åª¬π' {x}), ‚Üê Set.union_compl_self s,
          Set.inter_union_distrib_left, Set.inter_comm (g_seq_s n ‚Åª¬π' {x})]
        refine MeasurableSet.union (hs _ (hs_m.inter ?_)) ?_
        ¬∑ exact @SimpleFunc.measurableSet_fiber _ _ m _ _
        by_cases hx : x = 0
        ¬∑ suffices g_seq_s n ‚Åª¬π' {x} ‚à© s·∂ú = s·∂ú by
            rw [this]
            exact hs_m‚ÇÇ.compl
          ext1 y
          rw [hx, Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff]
          exact ‚ü®fun h => h.2, fun h => ‚ü®hg_seq_zero y h n, h‚ü©‚ü©
        ¬∑ suffices g_seq_s n ‚Åª¬π' {x} ‚à© s·∂ú = ‚àÖ by
            rw [this]
            exact MeasurableSet.empty
          ext1 y
          simp only [mem_inter_iff, mem_preimage, mem_singleton_iff, mem_compl_iff,
            mem_empty_iff_false, iff_false_iff, not_and, not_not_mem]
          refine Function.mtr fun hys => ?_
          rw [hg_seq_zero y hys n]
          exact Ne.symm hx
      finite_range' := @SimpleFunc.finite_range _ _ m (g_seq_s n) }
  exact ‚ü®g_seq_s‚ÇÇ, hg_seq_tendsto‚ü©
#align measure_theory.strongly_measurable.strongly_measurable_of_measurable_space_le_on MeasureTheory.StronglyMeasurable.stronglyMeasurable_of_measurableSpace_le_on

/-- If a function `f` is strongly measurable w.r.t. a sub-œÉ-algebra `m` and the measure is œÉ-finite
on `m`, then there exists spanning measurable sets with finite measure on which `f` has bounded
norm. In particular, `f` is integrable on each of those sets. -/
theorem exists_spanning_measurableSet_norm_le [SeminormedAddCommGroup Œ≤] {m m0 : MeasurableSpace Œ±}
    (hm : m ‚â§ m0) (hf : StronglyMeasurable[m] f) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)] :
    ‚àÉ s : ‚Ñï ‚Üí Set Œ±,
      (‚àÄ n, MeasurableSet[m] (s n) ‚àß Œº (s n) < ‚àû ‚àß ‚àÄ x ‚àà s n, ‚Äñf x‚Äñ ‚â§ n) ‚àß
      ‚ãÉ i, s i = Set.univ := by
  obtain ‚ü®s, hs, hs_univ‚ü© := exists_spanning_measurableSet_le hf.nnnorm.measurable (Œº.trim hm)
  refine ‚ü®s, fun n ‚Ü¶ ‚ü®(hs n).1, (le_trim hm).trans_lt (hs n).2.1, fun x hx ‚Ü¶ ?_‚ü©, hs_univ‚ü©
  have hx_nnnorm : ‚Äñf x‚Äñ‚Çä ‚â§ n := (hs n).2.2 x hx
  rw [‚Üê coe_nnnorm]
  norm_cast
#align measure_theory.strongly_measurable.exists_spanning_measurable_set_norm_le MeasureTheory.StronglyMeasurable.exists_spanning_measurableSet_norm_le

end StronglyMeasurable

/-! ## Finitely strongly measurable functions -/


theorem finStronglyMeasurable_zero {Œ± Œ≤} {m : MeasurableSpace Œ±} {Œº : Measure Œ±} [Zero Œ≤]
    [TopologicalSpace Œ≤] : FinStronglyMeasurable (0 : Œ± ‚Üí Œ≤) Œº :=
  ‚ü®0, by
    simp only [Pi.zero_apply, SimpleFunc.coe_zero, support_zero', measure_empty,
      zero_lt_top, forall_const],
    fun _ => tendsto_const_nhds‚ü©
#align measure_theory.fin_strongly_measurable_zero MeasureTheory.finStronglyMeasurable_zero

namespace FinStronglyMeasurable

variable {m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {f g : Œ± ‚Üí Œ≤}

theorem aefinStronglyMeasurable [Zero Œ≤] [TopologicalSpace Œ≤] (hf : FinStronglyMeasurable f Œº) :
    AEFinStronglyMeasurable f Œº :=
  ‚ü®f, hf, ae_eq_refl f‚ü©
#align measure_theory.fin_strongly_measurable.ae_fin_strongly_measurable MeasureTheory.FinStronglyMeasurable.aefinStronglyMeasurable

section sequence

variable [Zero Œ≤] [TopologicalSpace Œ≤] (hf : FinStronglyMeasurable f Œº)

/-- A sequence of simple functions such that `‚àÄ x, Tendsto (fun n ‚Ü¶ hf.approx n x) atTop (ùìù (f x))`
and `‚àÄ n, Œº (support (hf.approx n)) < ‚àû`. These properties are given by
`FinStronglyMeasurable.tendsto_approx` and `FinStronglyMeasurable.fin_support_approx`. -/
protected noncomputable def approx : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤ :=
  hf.choose
#align measure_theory.fin_strongly_measurable.approx MeasureTheory.FinStronglyMeasurable.approx

protected theorem fin_support_approx : ‚àÄ n, Œº (support (hf.approx n)) < ‚àû :=
  hf.choose_spec.1
#align measure_theory.fin_strongly_measurable.fin_support_approx MeasureTheory.FinStronglyMeasurable.fin_support_approx

protected theorem tendsto_approx : ‚àÄ x, Tendsto (fun n => hf.approx n x) atTop (ùìù (f x)) :=
  hf.choose_spec.2
#align measure_theory.fin_strongly_measurable.tendsto_approx MeasureTheory.FinStronglyMeasurable.tendsto_approx

end sequence

/-- A finitely strongly measurable function is strongly measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem stronglyMeasurable [Zero Œ≤] [TopologicalSpace Œ≤]
    (hf : FinStronglyMeasurable f Œº) : StronglyMeasurable f :=
  ‚ü®hf.approx, hf.tendsto_approx‚ü©
#align measure_theory.fin_strongly_measurable.strongly_measurable MeasureTheory.FinStronglyMeasurable.stronglyMeasurable

theorem exists_set_sigmaFinite [Zero Œ≤] [TopologicalSpace Œ≤] [T2Space Œ≤]
    (hf : FinStronglyMeasurable f Œº) :
    ‚àÉ t, MeasurableSet t ‚àß (‚àÄ x ‚àà t·∂ú, f x = 0) ‚àß SigmaFinite (Œº.restrict t) := by
  rcases hf with ‚ü®fs, hT_lt_top, h_approx‚ü©
  let T n := support (fs n)
  have hT_meas : ‚àÄ n, MeasurableSet (T n) := fun n => SimpleFunc.measurableSet_support (fs n)
  let t := ‚ãÉ n, T n
  refine ‚ü®t, MeasurableSet.iUnion hT_meas, ?_, ?_‚ü©
  ¬∑ have h_fs_zero : ‚àÄ n, ‚àÄ x ‚àà t·∂ú, fs n x = 0 := by
      intro n x hxt
      rw [Set.mem_compl_iff, Set.mem_iUnion, not_exists] at hxt
      simpa [T] using hxt n
    refine fun x hxt => tendsto_nhds_unique (h_approx x) ?_
    rw [funext fun n => h_fs_zero n x hxt]
    exact tendsto_const_nhds
  ¬∑ refine ‚ü®‚ü®‚ü®fun n => t·∂ú ‚à™ T n, fun _ => trivial, fun n => ?_, ?_‚ü©‚ü©‚ü©
    ¬∑ rw [Measure.restrict_apply' (MeasurableSet.iUnion hT_meas), Set.union_inter_distrib_right,
        Set.compl_inter_self t, Set.empty_union]
      exact (measure_mono Set.inter_subset_left).trans_lt (hT_lt_top n)
    ¬∑ rw [‚Üê Set.union_iUnion t·∂ú T]
      exact Set.compl_union_self _
#align measure_theory.fin_strongly_measurable.exists_set_sigma_finite MeasureTheory.FinStronglyMeasurable.exists_set_sigmaFinite

/-- A finitely strongly measurable function is measurable. -/
protected theorem measurable [Zero Œ≤] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [MeasurableSpace Œ≤] [BorelSpace Œ≤] (hf : FinStronglyMeasurable f Œº) : Measurable f :=
  hf.stronglyMeasurable.measurable
#align measure_theory.fin_strongly_measurable.measurable MeasureTheory.FinStronglyMeasurable.measurable

section Arithmetic

variable [TopologicalSpace Œ≤]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem mul [MonoidWithZero Œ≤] [ContinuousMul Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f * g) Œº := by
  refine
    ‚ü®fun n => hf.approx n * hg.approx n, ?_, fun x =>
      (hf.tendsto_approx x).mul (hg.tendsto_approx x)‚ü©
  intro n
  exact (measure_mono (support_mul_subset_left _ _)).trans_lt (hf.fin_support_approx n)
#align measure_theory.fin_strongly_measurable.mul MeasureTheory.FinStronglyMeasurable.mul

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem add [AddMonoid Œ≤] [ContinuousAdd Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f + g) Œº :=
  ‚ü®fun n => hf.approx n + hg.approx n, fun n =>
    (measure_mono (Function.support_add _ _)).trans_lt
      ((measure_union_le _ _).trans_lt
        (ENNReal.add_lt_top.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©)),
    fun x => (hf.tendsto_approx x).add (hg.tendsto_approx x)‚ü©
#align measure_theory.fin_strongly_measurable.add MeasureTheory.FinStronglyMeasurable.add

@[measurability]
protected theorem neg [AddGroup Œ≤] [TopologicalAddGroup Œ≤] (hf : FinStronglyMeasurable f Œº) :
    FinStronglyMeasurable (-f) Œº := by
  refine ‚ü®fun n => -hf.approx n, fun n => ?_, fun x => (hf.tendsto_approx x).neg‚ü©
  suffices Œº (Function.support fun x => -(hf.approx n) x) < ‚àû by convert this
  rw [Function.support_neg (hf.approx n)]
  exact hf.fin_support_approx n
#align measure_theory.fin_strongly_measurable.neg MeasureTheory.FinStronglyMeasurable.neg

@[measurability]
protected theorem sub [AddGroup Œ≤] [ContinuousSub Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f - g) Œº :=
  ‚ü®fun n => hf.approx n - hg.approx n, fun n =>
    (measure_mono (Function.support_sub _ _)).trans_lt
      ((measure_union_le _ _).trans_lt
        (ENNReal.add_lt_top.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©)),
    fun x => (hf.tendsto_approx x).sub (hg.tendsto_approx x)‚ü©
#align measure_theory.fin_strongly_measurable.sub MeasureTheory.FinStronglyMeasurable.sub

@[measurability]
protected theorem const_smul {ùïú} [TopologicalSpace ùïú] [AddMonoid Œ≤] [Monoid ùïú]
    [DistribMulAction ùïú Œ≤] [ContinuousSMul ùïú Œ≤] (hf : FinStronglyMeasurable f Œº) (c : ùïú) :
    FinStronglyMeasurable (c ‚Ä¢ f) Œº := by
  refine ‚ü®fun n => c ‚Ä¢ hf.approx n, fun n => ?_, fun x => (hf.tendsto_approx x).const_smul c‚ü©
  rw [SimpleFunc.coe_smul]
  exact (measure_mono (support_const_smul_subset c _)).trans_lt (hf.fin_support_approx n)
#align measure_theory.fin_strongly_measurable.const_smul MeasureTheory.FinStronglyMeasurable.const_smul

end Arithmetic

section Order

variable [TopologicalSpace Œ≤] [Zero Œ≤]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Œ≤] [ContinuousSup Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f ‚äî g) Œº := by
  refine
    ‚ü®fun n => hf.approx n ‚äî hg.approx n, fun n => ?_, fun x =>
      (hf.tendsto_approx x).sup_nhds (hg.tendsto_approx x)‚ü©
  refine (measure_mono (support_sup _ _)).trans_lt ?_
  exact measure_union_lt_top_iff.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©
#align measure_theory.fin_strongly_measurable.sup MeasureTheory.FinStronglyMeasurable.sup

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Œ≤] [ContinuousInf Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f ‚äì g) Œº := by
  refine
    ‚ü®fun n => hf.approx n ‚äì hg.approx n, fun n => ?_, fun x =>
      (hf.tendsto_approx x).inf_nhds (hg.tendsto_approx x)‚ü©
  refine (measure_mono (support_inf _ _)).trans_lt ?_
  exact measure_union_lt_top_iff.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©
#align measure_theory.fin_strongly_measurable.inf MeasureTheory.FinStronglyMeasurable.inf

end Order

end FinStronglyMeasurable

theorem finStronglyMeasurable_iff_stronglyMeasurable_and_exists_set_sigmaFinite {Œ± Œ≤} {f : Œ± ‚Üí Œ≤}
    [TopologicalSpace Œ≤] [T2Space Œ≤] [Zero Œ≤] {_ : MeasurableSpace Œ±} {Œº : Measure Œ±} :
    FinStronglyMeasurable f Œº ‚Üî
      StronglyMeasurable f ‚àß
        ‚àÉ t, MeasurableSet t ‚àß (‚àÄ x ‚àà t·∂ú, f x = 0) ‚àß SigmaFinite (Œº.restrict t) :=
  ‚ü®fun hf => ‚ü®hf.stronglyMeasurable, hf.exists_set_sigmaFinite‚ü©, fun hf =>
    hf.1.finStronglyMeasurable_of_set_sigmaFinite hf.2.choose_spec.1 hf.2.choose_spec.2.1
      hf.2.choose_spec.2.2‚ü©
#align measure_theory.fin_strongly_measurable_iff_strongly_measurable_and_exists_set_sigma_finite MeasureTheory.finStronglyMeasurable_iff_stronglyMeasurable_and_exists_set_sigmaFinite

theorem aefinStronglyMeasurable_zero {Œ± Œ≤} {_ : MeasurableSpace Œ±} (Œº : Measure Œ±) [Zero Œ≤]
    [TopologicalSpace Œ≤] : AEFinStronglyMeasurable (0 : Œ± ‚Üí Œ≤) Œº :=
  ‚ü®0, finStronglyMeasurable_zero, EventuallyEq.rfl‚ü©
#align measure_theory.ae_fin_strongly_measurable_zero MeasureTheory.aefinStronglyMeasurable_zero

/-! ## Almost everywhere strongly measurable functions -/

@[measurability]
theorem aestronglyMeasurable_const {Œ± Œ≤} {_ : MeasurableSpace Œ±} {Œº : Measure Œ±}
    [TopologicalSpace Œ≤] {b : Œ≤} : AEStronglyMeasurable (fun _ : Œ± => b) Œº :=
  stronglyMeasurable_const.aestronglyMeasurable
#align measure_theory.ae_strongly_measurable_const MeasureTheory.aestronglyMeasurable_const

@[to_additive (attr := measurability)]
theorem aestronglyMeasurable_one {Œ± Œ≤} {_ : MeasurableSpace Œ±} {Œº : Measure Œ±} [TopologicalSpace Œ≤]
    [One Œ≤] : AEStronglyMeasurable (1 : Œ± ‚Üí Œ≤) Œº :=
  stronglyMeasurable_one.aestronglyMeasurable
#align measure_theory.ae_strongly_measurable_one MeasureTheory.aestronglyMeasurable_one
#align measure_theory.ae_strongly_measurable_zero MeasureTheory.aestronglyMeasurable_zero

@[simp]
theorem Subsingleton.aestronglyMeasurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [Subsingleton Œ≤] {Œº : Measure Œ±} (f : Œ± ‚Üí Œ≤) : AEStronglyMeasurable f Œº :=
  (Subsingleton.stronglyMeasurable f).aestronglyMeasurable
#align measure_theory.subsingleton.ae_strongly_measurable MeasureTheory.Subsingleton.aestronglyMeasurable

@[simp]
theorem Subsingleton.aestronglyMeasurable' {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [Subsingleton Œ±] {Œº : Measure Œ±} (f : Œ± ‚Üí Œ≤) : AEStronglyMeasurable f Œº :=
  (Subsingleton.stronglyMeasurable' f).aestronglyMeasurable
#align measure_theory.subsingleton.ae_strongly_measurable' MeasureTheory.Subsingleton.aestronglyMeasurable'

@[simp]
theorem aestronglyMeasurable_zero_measure [MeasurableSpace Œ±] [TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) :
    AEStronglyMeasurable f (0 : Measure Œ±) := by
  nontriviality Œ±
  inhabit Œ±
  exact ‚ü®fun _ => f default, stronglyMeasurable_const, rfl‚ü©
#align measure_theory.ae_strongly_measurable_zero_measure MeasureTheory.aestronglyMeasurable_zero_measure

@[measurability]
theorem SimpleFunc.aestronglyMeasurable {_ : MeasurableSpace Œ±} {Œº : Measure Œ±} [TopologicalSpace Œ≤]
    (f : Œ± ‚Üí‚Çõ Œ≤) : AEStronglyMeasurable f Œº :=
  f.stronglyMeasurable.aestronglyMeasurable
#align measure_theory.simple_func.ae_strongly_measurable MeasureTheory.SimpleFunc.aestronglyMeasurable

namespace AEStronglyMeasurable

variable {m : MeasurableSpace Œ±} {Œº ŒΩ : Measure Œ±} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
  {f g : Œ± ‚Üí Œ≤}

section Mk

/-- A `StronglyMeasurable` function such that `f =·µê[Œº] hf.mk f`. See lemmas
`stronglyMeasurable_mk` and `ae_eq_mk`. -/
protected noncomputable def mk (f : Œ± ‚Üí Œ≤) (hf : AEStronglyMeasurable f Œº) : Œ± ‚Üí Œ≤ :=
  hf.choose
#align measure_theory.ae_strongly_measurable.mk MeasureTheory.AEStronglyMeasurable.mk

theorem stronglyMeasurable_mk (hf : AEStronglyMeasurable f Œº) : StronglyMeasurable (hf.mk f) :=
  hf.choose_spec.1
#align measure_theory.ae_strongly_measurable.strongly_measurable_mk MeasureTheory.AEStronglyMeasurable.stronglyMeasurable_mk

theorem measurable_mk [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤]
    (hf : AEStronglyMeasurable f Œº) : Measurable (hf.mk f) :=
  hf.stronglyMeasurable_mk.measurable
#align measure_theory.ae_strongly_measurable.measurable_mk MeasureTheory.AEStronglyMeasurable.measurable_mk

theorem ae_eq_mk (hf : AEStronglyMeasurable f Œº) : f =·µê[Œº] hf.mk f :=
  hf.choose_spec.2
#align measure_theory.ae_strongly_measurable.ae_eq_mk MeasureTheory.AEStronglyMeasurable.ae_eq_mk

@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {Œ≤} [MeasurableSpace Œ≤] [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] {f : Œ± ‚Üí Œ≤} (hf : AEStronglyMeasurable f Œº) :
    AEMeasurable f Œº :=
  ‚ü®hf.mk f, hf.stronglyMeasurable_mk.measurable, hf.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.ae_measurable MeasureTheory.AEStronglyMeasurable.aemeasurable

end Mk

theorem congr (hf : AEStronglyMeasurable f Œº) (h : f =·µê[Œº] g) : AEStronglyMeasurable g Œº :=
  ‚ü®hf.mk f, hf.stronglyMeasurable_mk, h.symm.trans hf.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.congr MeasureTheory.AEStronglyMeasurable.congr

theorem _root_.aestronglyMeasurable_congr (h : f =·µê[Œº] g) :
    AEStronglyMeasurable f Œº ‚Üî AEStronglyMeasurable g Œº :=
  ‚ü®fun hf => hf.congr h, fun hg => hg.congr h.symm‚ü©
#align ae_strongly_measurable_congr aestronglyMeasurable_congr

theorem mono_measure {ŒΩ : Measure Œ±} (hf : AEStronglyMeasurable f Œº) (h : ŒΩ ‚â§ Œº) :
    AEStronglyMeasurable f ŒΩ :=
  ‚ü®hf.mk f, hf.stronglyMeasurable_mk, Eventually.filter_mono (ae_mono h) hf.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.mono_measure MeasureTheory.AEStronglyMeasurable.mono_measure

protected lemma mono_ac (h : ŒΩ ‚â™ Œº) (hŒº : AEStronglyMeasurable f Œº) : AEStronglyMeasurable f ŒΩ :=
  let ‚ü®g, hg, hg'‚ü© := hŒº; ‚ü®g, hg, h.ae_eq hg'‚ü©
#align measure_theory.ae_strongly_measurable.mono' MeasureTheory.AEStronglyMeasurable.mono_ac
#align measure_theory.ae_strongly_measurable_of_absolutely_continuous MeasureTheory.AEStronglyMeasurable.mono_ac

@[deprecated (since := "2024-02-15")] protected alias mono' := AEStronglyMeasurable.mono_ac

theorem mono_set {s t} (h : s ‚äÜ t) (ht : AEStronglyMeasurable f (Œº.restrict t)) :
    AEStronglyMeasurable f (Œº.restrict s) :=
  ht.mono_measure (restrict_mono h le_rfl)
#align measure_theory.ae_strongly_measurable.mono_set MeasureTheory.AEStronglyMeasurable.mono_set

protected theorem restrict (hfm : AEStronglyMeasurable f Œº) {s} :
    AEStronglyMeasurable f (Œº.restrict s) :=
  hfm.mono_measure Measure.restrict_le_self
#align measure_theory.ae_strongly_measurable.restrict MeasureTheory.AEStronglyMeasurable.restrict

theorem ae_mem_imp_eq_mk {s} (h : AEStronglyMeasurable f (Œº.restrict s)) :
    ‚àÄ·µê x ‚àÇŒº, x ‚àà s ‚Üí f x = h.mk f x :=
  ae_imp_of_ae_restrict h.ae_eq_mk
#align measure_theory.ae_strongly_measurable.ae_mem_imp_eq_mk MeasureTheory.AEStronglyMeasurable.ae_mem_imp_eq_mk

/-- The composition of a continuous function and an ae strongly measurable function is ae strongly
measurable. -/
theorem _root_.Continuous.comp_aestronglyMeasurable {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : Continuous g)
    (hf : AEStronglyMeasurable f Œº) : AEStronglyMeasurable (fun x => g (f x)) Œº :=
  ‚ü®_, hg.comp_stronglyMeasurable hf.stronglyMeasurable_mk, EventuallyEq.fun_comp hf.ae_eq_mk g‚ü©
#align continuous.comp_ae_strongly_measurable Continuous.comp_aestronglyMeasurable

/-- A continuous function from `Œ±` to `Œ≤` is ae strongly measurable when one of the two spaces is
second countable. -/
theorem _root_.Continuous.aestronglyMeasurable [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    [PseudoMetrizableSpace Œ≤] [SecondCountableTopologyEither Œ± Œ≤] (hf : Continuous f) :
    AEStronglyMeasurable f Œº :=
  hf.stronglyMeasurable.aestronglyMeasurable
#align continuous.ae_strongly_measurable Continuous.aestronglyMeasurable

protected theorem prod_mk {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) : AEStronglyMeasurable (fun x => (f x, g x)) Œº :=
  ‚ü®fun x => (hf.mk f x, hg.mk g x), hf.stronglyMeasurable_mk.prod_mk hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.prod_mk hg.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.prod_mk MeasureTheory.AEStronglyMeasurable.prod_mk

/-- The composition of a continuous function of two variables and two ae strongly measurable
functions is ae strongly measurable. -/
theorem _root_.Continuous.comp_aestronglyMeasurable‚ÇÇ
    {Œ≤' : Type*} [TopologicalSpace Œ≤']
    {g : Œ≤ ‚Üí Œ≤' ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {f' : Œ± ‚Üí Œ≤'} (hg : Continuous g.uncurry)
    (hf : AEStronglyMeasurable f Œº) (h'f : AEStronglyMeasurable f' Œº) :
    AEStronglyMeasurable (fun x => g (f x) (f' x)) Œº :=
  hg.comp_aestronglyMeasurable (hf.prod_mk h'f)

/-- In a space with second countable topology, measurable implies ae strongly measurable. -/
@[aesop unsafe 30% apply (rule_sets := [Measurable])]
theorem _root_.Measurable.aestronglyMeasurable {_ : MeasurableSpace Œ±} {Œº : Measure Œ±}
    [MeasurableSpace Œ≤] [PseudoMetrizableSpace Œ≤] [SecondCountableTopology Œ≤]
    [OpensMeasurableSpace Œ≤] (hf : Measurable f) : AEStronglyMeasurable f Œº :=
  hf.stronglyMeasurable.aestronglyMeasurable
#align measurable.ae_strongly_measurable Measurable.aestronglyMeasurable

section Arithmetic

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem mul [Mul Œ≤] [ContinuousMul Œ≤] (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) : AEStronglyMeasurable (f * g) Œº :=
  ‚ü®hf.mk f * hg.mk g, hf.stronglyMeasurable_mk.mul hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.mul hg.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.mul MeasureTheory.AEStronglyMeasurable.mul
#align measure_theory.ae_strongly_measurable.add MeasureTheory.AEStronglyMeasurable.add

@[to_additive (attr := measurability)]
protected theorem mul_const [Mul Œ≤] [ContinuousMul Œ≤] (hf : AEStronglyMeasurable f Œº) (c : Œ≤) :
    AEStronglyMeasurable (fun x => f x * c) Œº :=
  hf.mul aestronglyMeasurable_const
#align measure_theory.ae_strongly_measurable.mul_const MeasureTheory.AEStronglyMeasurable.mul_const
#align measure_theory.ae_strongly_measurable.add_const MeasureTheory.AEStronglyMeasurable.add_const

@[to_additive (attr := measurability)]
protected theorem const_mul [Mul Œ≤] [ContinuousMul Œ≤] (hf : AEStronglyMeasurable f Œº) (c : Œ≤) :
    AEStronglyMeasurable (fun x => c * f x) Œº :=
  aestronglyMeasurable_const.mul hf
#align measure_theory.ae_strongly_measurable.const_mul MeasureTheory.AEStronglyMeasurable.const_mul
#align measure_theory.ae_strongly_measurable.const_add MeasureTheory.AEStronglyMeasurable.const_add

@[to_additive (attr := measurability)]
protected theorem inv [Inv Œ≤] [ContinuousInv Œ≤] (hf : AEStronglyMeasurable f Œº) :
    AEStronglyMeasurable f‚Åª¬π Œº :=
  ‚ü®(hf.mk f)‚Åª¬π, hf.stronglyMeasurable_mk.inv, hf.ae_eq_mk.inv‚ü©
#align measure_theory.ae_strongly_measurable.inv MeasureTheory.AEStronglyMeasurable.inv
#align measure_theory.ae_strongly_measurable.neg MeasureTheory.AEStronglyMeasurable.neg

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem div [Group Œ≤] [TopologicalGroup Œ≤] (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) : AEStronglyMeasurable (f / g) Œº :=
  ‚ü®hf.mk f / hg.mk g, hf.stronglyMeasurable_mk.div hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.div hg.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.div MeasureTheory.AEStronglyMeasurable.div
#align measure_theory.ae_strongly_measurable.sub MeasureTheory.AEStronglyMeasurable.sub

@[to_additive]
theorem mul_iff_right [CommGroup Œ≤] [TopologicalGroup Œ≤] (hf : AEStronglyMeasurable f Œº) :
    AEStronglyMeasurable (f * g) Œº ‚Üî AEStronglyMeasurable g Œº :=
  ‚ü®fun h ‚Ü¶ show g = f * g * f‚Åª¬π by simp only [mul_inv_cancel_comm] ‚ñ∏ h.mul hf.inv,
    fun h ‚Ü¶ hf.mul h‚ü©

@[to_additive]
theorem mul_iff_left [CommGroup Œ≤] [TopologicalGroup Œ≤] (hf : AEStronglyMeasurable f Œº) :
    AEStronglyMeasurable (g * f) Œº ‚Üî AEStronglyMeasurable g Œº :=
  mul_comm g f ‚ñ∏ AEStronglyMeasurable.mul_iff_right hf

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem smul {ùïú} [TopologicalSpace ùïú] [SMul ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú}
    {g : Œ± ‚Üí Œ≤} (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    AEStronglyMeasurable (fun x => f x ‚Ä¢ g x) Œº :=
  continuous_smul.comp_aestronglyMeasurable (hf.prod_mk hg)
#align measure_theory.ae_strongly_measurable.smul MeasureTheory.AEStronglyMeasurable.smul
#align measure_theory.ae_strongly_measurable.vadd MeasureTheory.AEStronglyMeasurable.vadd

@[to_additive (attr := aesop safe 20 apply (rule_sets := [Measurable])) const_nsmul]
protected theorem pow [Monoid Œ≤] [ContinuousMul Œ≤] (hf : AEStronglyMeasurable f Œº) (n : ‚Ñï) :
    AEStronglyMeasurable (f ^ n) Œº :=
  ‚ü®hf.mk f ^ n, hf.stronglyMeasurable_mk.pow _, hf.ae_eq_mk.pow_const _‚ü©

@[to_additive (attr := measurability)]
protected theorem const_smul {ùïú} [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤]
    (hf : AEStronglyMeasurable f Œº) (c : ùïú) : AEStronglyMeasurable (c ‚Ä¢ f) Œº :=
  ‚ü®c ‚Ä¢ hf.mk f, hf.stronglyMeasurable_mk.const_smul c, hf.ae_eq_mk.const_smul c‚ü©
#align measure_theory.ae_strongly_measurable.const_smul MeasureTheory.AEStronglyMeasurable.const_smul

@[to_additive (attr := measurability)]
protected theorem const_smul' {ùïú} [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤]
    (hf : AEStronglyMeasurable f Œº) (c : ùïú) : AEStronglyMeasurable (fun x => c ‚Ä¢ f x) Œº :=
  hf.const_smul c
#align measure_theory.ae_strongly_measurable.const_smul' MeasureTheory.AEStronglyMeasurable.const_smul'

@[to_additive (attr := measurability)]
protected theorem smul_const {ùïú} [TopologicalSpace ùïú] [SMul ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú}
    (hf : AEStronglyMeasurable f Œº) (c : Œ≤) : AEStronglyMeasurable (fun x => f x ‚Ä¢ c) Œº :=
  continuous_smul.comp_aestronglyMeasurable (hf.prod_mk aestronglyMeasurable_const)
#align measure_theory.ae_strongly_measurable.smul_const MeasureTheory.AEStronglyMeasurable.smul_const
#align measure_theory.ae_strongly_measurable.vadd_const MeasureTheory.AEStronglyMeasurable.vadd_const

end Arithmetic

section Order

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Œ≤] [ContinuousSup Œ≤] (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) : AEStronglyMeasurable (f ‚äî g) Œº :=
  ‚ü®hf.mk f ‚äî hg.mk g, hf.stronglyMeasurable_mk.sup hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.sup hg.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.sup MeasureTheory.AEStronglyMeasurable.sup

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Œ≤] [ContinuousInf Œ≤] (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) : AEStronglyMeasurable (f ‚äì g) Œº :=
  ‚ü®hf.mk f ‚äì hg.mk g, hf.stronglyMeasurable_mk.inf hg.stronglyMeasurable_mk,
    hf.ae_eq_mk.inf hg.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.inf MeasureTheory.AEStronglyMeasurable.inf

end Order

/-!
### Big operators: `‚àè` and `‚àë`
-/


section Monoid

variable {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M]

@[to_additive (attr := measurability)]
theorem _root_.List.aestronglyMeasurable_prod' (l : List (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, AEStronglyMeasurable f Œº) : AEStronglyMeasurable l.prod Œº := by
  induction' l with f l ihl; ¬∑ exact aestronglyMeasurable_one
  rw [List.forall_mem_cons] at hl
  rw [List.prod_cons]
  exact hl.1.mul (ihl hl.2)
#align list.ae_strongly_measurable_prod' List.aestronglyMeasurable_prod'
#align list.ae_strongly_measurable_sum' List.aestronglyMeasurable_sum'

@[to_additive (attr := measurability)]
theorem _root_.List.aestronglyMeasurable_prod
    (l : List (Œ± ‚Üí M)) (hl : ‚àÄ f ‚àà l, AEStronglyMeasurable f Œº) :
    AEStronglyMeasurable (fun x => (l.map fun f : Œ± ‚Üí M => f x).prod) Œº := by
  simpa only [‚Üê Pi.list_prod_apply] using l.aestronglyMeasurable_prod' hl
#align list.ae_strongly_measurable_prod List.aestronglyMeasurable_prod
#align list.ae_strongly_measurable_sum List.aestronglyMeasurable_sum

end Monoid

section CommMonoid

variable {M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M]

@[to_additive (attr := measurability)]
theorem _root_.Multiset.aestronglyMeasurable_prod' (l : Multiset (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, AEStronglyMeasurable f Œº) : AEStronglyMeasurable l.prod Œº := by
  rcases l with ‚ü®l‚ü©
  simpa using l.aestronglyMeasurable_prod' (by simpa using hl)
#align multiset.ae_strongly_measurable_prod' Multiset.aestronglyMeasurable_prod'
#align multiset.ae_strongly_measurable_sum' Multiset.aestronglyMeasurable_sum'

@[to_additive (attr := measurability)]
theorem _root_.Multiset.aestronglyMeasurable_prod (s : Multiset (Œ± ‚Üí M))
    (hs : ‚àÄ f ‚àà s, AEStronglyMeasurable f Œº) :
    AEStronglyMeasurable (fun x => (s.map fun f : Œ± ‚Üí M => f x).prod) Œº := by
  simpa only [‚Üê Pi.multiset_prod_apply] using s.aestronglyMeasurable_prod' hs
#align multiset.ae_strongly_measurable_prod Multiset.aestronglyMeasurable_prod
#align multiset.ae_strongly_measurable_sum Multiset.aestronglyMeasurable_sum

@[to_additive (attr := measurability)]
theorem _root_.Finset.aestronglyMeasurable_prod' {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí M} (s : Finset Œπ)
    (hf : ‚àÄ i ‚àà s, AEStronglyMeasurable (f i) Œº) : AEStronglyMeasurable (‚àè i ‚àà s, f i) Œº :=
  Multiset.aestronglyMeasurable_prod' _ fun _g hg =>
    let ‚ü®_i, hi, hg‚ü© := Multiset.mem_map.1 hg
    hg ‚ñ∏ hf _ hi
#align finset.ae_strongly_measurable_prod' Finset.aestronglyMeasurable_prod'
#align finset.ae_strongly_measurable_sum' Finset.aestronglyMeasurable_sum'

@[to_additive (attr := measurability)]
theorem _root_.Finset.aestronglyMeasurable_prod {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí M} (s : Finset Œπ)
    (hf : ‚àÄ i ‚àà s, AEStronglyMeasurable (f i) Œº) :
    AEStronglyMeasurable (fun a => ‚àè i ‚àà s, f i a) Œº := by
  simpa only [‚Üê Finset.prod_apply] using s.aestronglyMeasurable_prod' hf
#align finset.ae_strongly_measurable_prod Finset.aestronglyMeasurable_prod
#align finset.ae_strongly_measurable_sum Finset.aestronglyMeasurable_sum

end CommMonoid

section SecondCountableAEStronglyMeasurable

variable [MeasurableSpace Œ≤]

/-- In a space with second countable topology, measurable implies strongly measurable. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem _root_.AEMeasurable.aestronglyMeasurable [PseudoMetrizableSpace Œ≤] [OpensMeasurableSpace Œ≤]
    [SecondCountableTopology Œ≤] (hf : AEMeasurable f Œº) : AEStronglyMeasurable f Œº :=
  ‚ü®hf.mk f, hf.measurable_mk.stronglyMeasurable, hf.ae_eq_mk‚ü©
#align ae_measurable.ae_strongly_measurable AEMeasurable.aestronglyMeasurable

@[measurability]
theorem _root_.aestronglyMeasurable_id {Œ± : Type*} [TopologicalSpace Œ±] [PseudoMetrizableSpace Œ±]
    {_ : MeasurableSpace Œ±} [OpensMeasurableSpace Œ±] [SecondCountableTopology Œ±] {Œº : Measure Œ±} :
    AEStronglyMeasurable (id : Œ± ‚Üí Œ±) Œº :=
  aemeasurable_id.aestronglyMeasurable
#align ae_strongly_measurable_id aestronglyMeasurable_id

/-- In a space with second countable topology, strongly measurable and measurable are equivalent. -/
theorem _root_.aestronglyMeasurable_iff_aemeasurable [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤]
    [SecondCountableTopology Œ≤] : AEStronglyMeasurable f Œº ‚Üî AEMeasurable f Œº :=
  ‚ü®fun h => h.aemeasurable, fun h => h.aestronglyMeasurable‚ü©
#align ae_strongly_measurable_iff_ae_measurable aestronglyMeasurable_iff_aemeasurable

end SecondCountableAEStronglyMeasurable

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem dist {Œ≤ : Type*} [PseudoMetricSpace Œ≤] {f g : Œ± ‚Üí Œ≤}
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    AEStronglyMeasurable (fun x => dist (f x) (g x)) Œº :=
  continuous_dist.comp_aestronglyMeasurable (hf.prod_mk hg)
#align measure_theory.ae_strongly_measurable.dist MeasureTheory.AEStronglyMeasurable.dist

@[measurability]
protected theorem norm {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : AEStronglyMeasurable f Œº) : AEStronglyMeasurable (fun x => ‚Äñf x‚Äñ) Œº :=
  continuous_norm.comp_aestronglyMeasurable hf
#align measure_theory.ae_strongly_measurable.norm MeasureTheory.AEStronglyMeasurable.norm

@[measurability]
protected theorem nnnorm {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : AEStronglyMeasurable f Œº) : AEStronglyMeasurable (fun x => ‚Äñf x‚Äñ‚Çä) Œº :=
  continuous_nnnorm.comp_aestronglyMeasurable hf
#align measure_theory.ae_strongly_measurable.nnnorm MeasureTheory.AEStronglyMeasurable.nnnorm

@[measurability]
protected theorem ennnorm {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : AEStronglyMeasurable f Œº) : AEMeasurable (fun a => (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) Œº :=
  (ENNReal.continuous_coe.comp_aestronglyMeasurable hf.nnnorm).aemeasurable
#align measure_theory.ae_strongly_measurable.ennnorm MeasureTheory.AEStronglyMeasurable.ennnorm

@[aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem edist {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f g : Œ± ‚Üí Œ≤}
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    AEMeasurable (fun a => edist (f a) (g a)) Œº :=
  (continuous_edist.comp_aestronglyMeasurable (hf.prod_mk hg)).aemeasurable
#align measure_theory.ae_strongly_measurable.edist MeasureTheory.AEStronglyMeasurable.edist

@[measurability]
protected theorem real_toNNReal {f : Œ± ‚Üí ‚Ñù} (hf : AEStronglyMeasurable f Œº) :
    AEStronglyMeasurable (fun x => (f x).toNNReal) Œº :=
  continuous_real_toNNReal.comp_aestronglyMeasurable hf
#align measure_theory.ae_strongly_measurable.real_to_nnreal MeasureTheory.AEStronglyMeasurable.real_toNNReal

theorem _root_.aestronglyMeasurable_indicator_iff [Zero Œ≤] {s : Set Œ±} (hs : MeasurableSet s) :
    AEStronglyMeasurable (indicator s f) Œº ‚Üî AEStronglyMeasurable f (Œº.restrict s) := by
  constructor
  ¬∑ intro h
    exact (h.mono_measure Measure.restrict_le_self).congr (indicator_ae_eq_restrict hs)
  ¬∑ intro h
    refine ‚ü®indicator s (h.mk f), h.stronglyMeasurable_mk.indicator hs, ?_‚ü©
    have A : s.indicator f =·µê[Œº.restrict s] s.indicator (h.mk f) :=
      (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans <| (indicator_ae_eq_restrict hs).symm)
    have B : s.indicator f =·µê[Œº.restrict s·∂ú] s.indicator (h.mk f) :=
      (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm
    exact ae_of_ae_restrict_of_ae_restrict_compl _ A B
#align ae_strongly_measurable_indicator_iff aestronglyMeasurable_indicator_iff

@[measurability]
protected theorem indicator [Zero Œ≤] (hfm : AEStronglyMeasurable f Œº) {s : Set Œ±}
    (hs : MeasurableSet s) : AEStronglyMeasurable (s.indicator f) Œº :=
  (aestronglyMeasurable_indicator_iff hs).mpr hfm.restrict
#align measure_theory.ae_strongly_measurable.indicator MeasureTheory.AEStronglyMeasurable.indicator

theorem nullMeasurableSet_eq_fun {E} [TopologicalSpace E] [MetrizableSpace E] {f g : Œ± ‚Üí E}
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    NullMeasurableSet { x | f x = g x } Œº := by
  apply
    (hf.stronglyMeasurable_mk.measurableSet_eq_fun
          hg.stronglyMeasurable_mk).nullMeasurableSet.congr
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk] with x hfx hgx
  change (hf.mk f x = hg.mk g x) = (f x = g x)
  simp only [hfx, hgx]
#align measure_theory.ae_strongly_measurable.null_measurable_set_eq_fun MeasureTheory.AEStronglyMeasurable.nullMeasurableSet_eq_fun

@[to_additive]
lemma nullMeasurableSet_mulSupport {E} [TopologicalSpace E] [MetrizableSpace E] [One E] {f : Œ± ‚Üí E}
    (hf : AEStronglyMeasurable f Œº) : NullMeasurableSet (mulSupport f) Œº :=
  (hf.nullMeasurableSet_eq_fun stronglyMeasurable_const.aestronglyMeasurable).compl

theorem nullMeasurableSet_lt [LinearOrder Œ≤] [OrderClosedTopology Œ≤] [PseudoMetrizableSpace Œ≤]
    {f g : Œ± ‚Üí Œ≤} (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    NullMeasurableSet { a | f a < g a } Œº := by
  apply
    (hf.stronglyMeasurable_mk.measurableSet_lt hg.stronglyMeasurable_mk).nullMeasurableSet.congr
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk] with x hfx hgx
  change (hf.mk f x < hg.mk g x) = (f x < g x)
  simp only [hfx, hgx]
#align measure_theory.ae_strongly_measurable.null_measurable_set_lt MeasureTheory.AEStronglyMeasurable.nullMeasurableSet_lt

theorem nullMeasurableSet_le [Preorder Œ≤] [OrderClosedTopology Œ≤] [PseudoMetrizableSpace Œ≤]
    {f g : Œ± ‚Üí Œ≤} (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    NullMeasurableSet { a | f a ‚â§ g a } Œº := by
  apply
    (hf.stronglyMeasurable_mk.measurableSet_le hg.stronglyMeasurable_mk).nullMeasurableSet.congr
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk] with x hfx hgx
  change (hf.mk f x ‚â§ hg.mk g x) = (f x ‚â§ g x)
  simp only [hfx, hgx]
#align measure_theory.ae_strongly_measurable.null_measurable_set_le MeasureTheory.AEStronglyMeasurable.nullMeasurableSet_le

theorem _root_.aestronglyMeasurable_of_aestronglyMeasurable_trim {Œ±} {m m0 : MeasurableSpace Œ±}
    {Œº : Measure Œ±} (hm : m ‚â§ m0) {f : Œ± ‚Üí Œ≤} (hf : AEStronglyMeasurable f (Œº.trim hm)) :
    AEStronglyMeasurable f Œº :=
  ‚ü®hf.mk f, StronglyMeasurable.mono hf.stronglyMeasurable_mk hm, ae_eq_of_ae_eq_trim hf.ae_eq_mk‚ü©
#align ae_strongly_measurable_of_ae_strongly_measurable_trim aestronglyMeasurable_of_aestronglyMeasurable_trim

theorem comp_aemeasurable {Œ≥ : Type*} {_ : MeasurableSpace Œ≥} {_ : MeasurableSpace Œ±} {f : Œ≥ ‚Üí Œ±}
    {Œº : Measure Œ≥} (hg : AEStronglyMeasurable g (Measure.map f Œº)) (hf : AEMeasurable f Œº) :
    AEStronglyMeasurable (g ‚àò f) Œº :=
  ‚ü®hg.mk g ‚àò hf.mk f, hg.stronglyMeasurable_mk.comp_measurable hf.measurable_mk,
    (ae_eq_comp hf hg.ae_eq_mk).trans (hf.ae_eq_mk.fun_comp (hg.mk g))‚ü©
#align measure_theory.ae_strongly_measurable.comp_ae_measurable MeasureTheory.AEStronglyMeasurable.comp_aemeasurable

theorem comp_measurable {Œ≥ : Type*} {_ : MeasurableSpace Œ≥} {_ : MeasurableSpace Œ±} {f : Œ≥ ‚Üí Œ±}
    {Œº : Measure Œ≥} (hg : AEStronglyMeasurable g (Measure.map f Œº)) (hf : Measurable f) :
    AEStronglyMeasurable (g ‚àò f) Œº :=
  hg.comp_aemeasurable hf.aemeasurable
#align measure_theory.ae_strongly_measurable.comp_measurable MeasureTheory.AEStronglyMeasurable.comp_measurable

theorem comp_quasiMeasurePreserving {Œ≥ : Type*} {_ : MeasurableSpace Œ≥} {_ : MeasurableSpace Œ±}
    {f : Œ≥ ‚Üí Œ±} {Œº : Measure Œ≥} {ŒΩ : Measure Œ±} (hg : AEStronglyMeasurable g ŒΩ)
    (hf : QuasiMeasurePreserving f Œº ŒΩ) : AEStronglyMeasurable (g ‚àò f) Œº :=
  (hg.mono_ac hf.absolutelyContinuous).comp_measurable hf.measurable
#align measure_theory.ae_strongly_measurable.comp_quasi_measure_preserving MeasureTheory.AEStronglyMeasurable.comp_quasiMeasurePreserving

theorem isSeparable_ae_range (hf : AEStronglyMeasurable f Œº) :
    ‚àÉ t : Set Œ≤, IsSeparable t ‚àß ‚àÄ·µê x ‚àÇŒº, f x ‚àà t := by
  refine ‚ü®range (hf.mk f), hf.stronglyMeasurable_mk.isSeparable_range, ?_‚ü©
  filter_upwards [hf.ae_eq_mk] with x hx
  simp [hx]
#align measure_theory.ae_strongly_measurable.is_separable_ae_range MeasureTheory.AEStronglyMeasurable.isSeparable_ae_range

/-- A function is almost everywhere strongly measurable if and only if it is almost everywhere
measurable, and up to a zero measure set its range is contained in a separable set. -/
theorem _root_.aestronglyMeasurable_iff_aemeasurable_separable [PseudoMetrizableSpace Œ≤]
    [MeasurableSpace Œ≤] [BorelSpace Œ≤] :
    AEStronglyMeasurable f Œº ‚Üî
      AEMeasurable f Œº ‚àß ‚àÉ t : Set Œ≤, IsSeparable t ‚àß ‚àÄ·µê x ‚àÇŒº, f x ‚àà t := by
  refine ‚ü®fun H => ‚ü®H.aemeasurable, H.isSeparable_ae_range‚ü©, ?_‚ü©
  rintro ‚ü®H, ‚ü®t, t_sep, ht‚ü©‚ü©
  rcases eq_empty_or_nonempty t with (rfl | h‚ÇÄ)
  ¬∑ simp only [mem_empty_iff_false, eventually_false_iff_eq_bot, ae_eq_bot] at ht
    rw [ht]
    exact aestronglyMeasurable_zero_measure f
  ¬∑ obtain ‚ü®g, g_meas, gt, fg‚ü© : ‚àÉ g : Œ± ‚Üí Œ≤, Measurable g ‚àß range g ‚äÜ t ‚àß f =·µê[Œº] g :=
      H.exists_ae_eq_range_subset ht h‚ÇÄ
    refine ‚ü®g, ?_, fg‚ü©
    exact stronglyMeasurable_iff_measurable_separable.2 ‚ü®g_meas, t_sep.mono gt‚ü©
#align ae_strongly_measurable_iff_ae_measurable_separable aestronglyMeasurable_iff_aemeasurable_separable

theorem _root_.aestronglyMeasurable_iff_nullMeasurable_separable [PseudoMetrizableSpace Œ≤]
    [MeasurableSpace Œ≤] [BorelSpace Œ≤] :
    AEStronglyMeasurable f Œº ‚Üî
      NullMeasurable f Œº ‚àß ‚àÉ t : Set Œ≤, IsSeparable t ‚àß ‚àÄ·µê x ‚àÇŒº, f x ‚àà t :=
  aestronglyMeasurable_iff_aemeasurable_separable.trans <| and_congr_left fun ‚ü®_, hsep, h‚ü© ‚Ü¶
    have := hsep.secondCountableTopology
    ‚ü®AEMeasurable.nullMeasurable, fun hf ‚Ü¶ hf.aemeasurable_of_aerange h‚ü©

theorem _root_.MeasurableEmbedding.aestronglyMeasurable_map_iff {Œ≥ : Type*}
    {mŒ≥ : MeasurableSpace Œ≥} {mŒ± : MeasurableSpace Œ±} {f : Œ≥ ‚Üí Œ±} {Œº : Measure Œ≥}
    (hf : MeasurableEmbedding f) {g : Œ± ‚Üí Œ≤} :
    AEStronglyMeasurable g (Measure.map f Œº) ‚Üî AEStronglyMeasurable (g ‚àò f) Œº := by
  refine ‚ü®fun H => H.comp_measurable hf.measurable, ?_‚ü©
  rintro ‚ü®g‚ÇÅ, hgm‚ÇÅ, heq‚ü©
  rcases hf.exists_stronglyMeasurable_extend hgm‚ÇÅ fun x => ‚ü®g x‚ü© with ‚ü®g‚ÇÇ, hgm‚ÇÇ, rfl‚ü©
  exact ‚ü®g‚ÇÇ, hgm‚ÇÇ, hf.ae_map_iff.2 heq‚ü©
#align measurable_embedding.ae_strongly_measurable_map_iff MeasurableEmbedding.aestronglyMeasurable_map_iff

theorem _root_.Embedding.aestronglyMeasurable_comp_iff [PseudoMetrizableSpace Œ≤]
    [PseudoMetrizableSpace Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : Embedding g) :
    AEStronglyMeasurable (fun x => g (f x)) Œº ‚Üî AEStronglyMeasurable f Œº := by
  letI := pseudoMetrizableSpacePseudoMetric Œ≥
  borelize Œ≤ Œ≥
  refine
    ‚ü®fun H => aestronglyMeasurable_iff_aemeasurable_separable.2 ‚ü®?_, ?_‚ü©, fun H =>
      hg.continuous.comp_aestronglyMeasurable H‚ü©
  ¬∑ let G : Œ≤ ‚Üí range g := rangeFactorization g
    have hG : ClosedEmbedding G :=
      { hg.codRestrict _ _ with
        isClosed_range := by rw [surjective_onto_range.range_eq]; exact isClosed_univ }
    have : AEMeasurable (G ‚àò f) Œº := AEMeasurable.subtype_mk H.aemeasurable
    exact hG.measurableEmbedding.aemeasurable_comp_iff.1 this
  ¬∑ rcases (aestronglyMeasurable_iff_aemeasurable_separable.1 H).2 with ‚ü®t, ht, h't‚ü©
    exact ‚ü®g ‚Åª¬π' t, hg.isSeparable_preimage ht, h't‚ü©
#align embedding.ae_strongly_measurable_comp_iff Embedding.aestronglyMeasurable_comp_iff

/-- An almost everywhere sequential limit of almost everywhere strongly measurable functions is
almost everywhere strongly measurable. -/
theorem _root_.aestronglyMeasurable_of_tendsto_ae {Œπ : Type*} [PseudoMetrizableSpace Œ≤]
    (u : Filter Œπ) [NeBot u] [IsCountablyGenerated u] {f : Œπ ‚Üí Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≤}
    (hf : ‚àÄ i, AEStronglyMeasurable (f i) Œº) (lim : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f n x) u (ùìù (g x))) :
    AEStronglyMeasurable g Œº := by
  borelize Œ≤
  refine aestronglyMeasurable_iff_aemeasurable_separable.2 ‚ü®?_, ?_‚ü©
  ¬∑ exact aemeasurable_of_tendsto_metrizable_ae _ (fun n => (hf n).aemeasurable) lim
  ¬∑ rcases u.exists_seq_tendsto with ‚ü®v, hv‚ü©
    have : ‚àÄ n : ‚Ñï, ‚àÉ t : Set Œ≤, IsSeparable t ‚àß f (v n) ‚Åª¬π' t ‚àà ae Œº := fun n =>
      (aestronglyMeasurable_iff_aemeasurable_separable.1 (hf (v n))).2
    choose t t_sep ht using this
    refine ‚ü®closure (‚ãÉ i, t i), .closure <| .iUnion t_sep, ?_‚ü©
    filter_upwards [ae_all_iff.2 ht, lim] with x hx h'x
    apply mem_closure_of_tendsto (h'x.comp hv)
    filter_upwards with n using mem_iUnion_of_mem n (hx n)
#align ae_strongly_measurable_of_tendsto_ae aestronglyMeasurable_of_tendsto_ae

/-- If a sequence of almost everywhere strongly measurable functions converges almost everywhere,
one can select a strongly measurable function as the almost everywhere limit. -/
theorem _root_.exists_stronglyMeasurable_limit_of_tendsto_ae [PseudoMetrizableSpace Œ≤]
    {f : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} (hf : ‚àÄ n, AEStronglyMeasurable (f n) Œº)
    (h_ae_tendsto : ‚àÄ·µê x ‚àÇŒº, ‚àÉ l : Œ≤, Tendsto (fun n => f n x) atTop (ùìù l)) :
    ‚àÉ f_lim : Œ± ‚Üí Œ≤, StronglyMeasurable f_lim ‚àß
      ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f n x) atTop (ùìù (f_lim x)) := by
  borelize Œ≤
  obtain ‚ü®g, _, hg‚ü© :
    ‚àÉ g : Œ± ‚Üí Œ≤, Measurable g ‚àß ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f n x) atTop (ùìù (g x)) :=
    measurable_limit_of_tendsto_metrizable_ae (fun n => (hf n).aemeasurable) h_ae_tendsto
  have Hg : AEStronglyMeasurable g Œº := aestronglyMeasurable_of_tendsto_ae _ hf hg
  refine ‚ü®Hg.mk g, Hg.stronglyMeasurable_mk, ?_‚ü©
  filter_upwards [hg, Hg.ae_eq_mk] with x hx h'x
  rwa [h'x] at hx
#align exists_strongly_measurable_limit_of_tendsto_ae exists_stronglyMeasurable_limit_of_tendsto_ae

theorem piecewise {s : Set Œ±} [DecidablePred (¬∑ ‚àà s)]
    (hs : MeasurableSet s) (hf : AEStronglyMeasurable f (Œº.restrict s))
    (hg : AEStronglyMeasurable g (Œº.restrict s·∂ú)) :
    AEStronglyMeasurable (s.piecewise f g) Œº := by
  refine ‚ü®s.piecewise (hf.mk f) (hg.mk g),
    StronglyMeasurable.piecewise hs hf.stronglyMeasurable_mk hg.stronglyMeasurable_mk, ?_‚ü©
  refine ae_of_ae_restrict_of_ae_restrict_compl s ?_ ?_
  ¬∑ have h := hf.ae_eq_mk
    rw [Filter.EventuallyEq, ae_restrict_iff' hs] at h
    rw [ae_restrict_iff' hs]
    filter_upwards [h] with x hx
    intro hx_mem
    simp only [hx_mem, Set.piecewise_eq_of_mem, hx hx_mem]
  ¬∑ have h := hg.ae_eq_mk
    rw [Filter.EventuallyEq, ae_restrict_iff' hs.compl] at h
    rw [ae_restrict_iff' hs.compl]
    filter_upwards [h] with x hx
    intro hx_mem
    rw [Set.mem_compl_iff] at hx_mem
    simp only [hx_mem, not_false_eq_true, Set.piecewise_eq_of_not_mem, hx hx_mem]

theorem sum_measure [PseudoMetrizableSpace Œ≤] {m : MeasurableSpace Œ±} {Œº : Œπ ‚Üí Measure Œ±}
    (h : ‚àÄ i, AEStronglyMeasurable f (Œº i)) : AEStronglyMeasurable f (Measure.sum Œº) := by
  borelize Œ≤
  refine
    aestronglyMeasurable_iff_aemeasurable_separable.2
      ‚ü®AEMeasurable.sum_measure fun i => (h i).aemeasurable, ?_‚ü©
  have A : ‚àÄ i : Œπ, ‚àÉ t : Set Œ≤, IsSeparable t ‚àß f ‚Åª¬π' t ‚àà ae (Œº i) := fun i =>
    (aestronglyMeasurable_iff_aemeasurable_separable.1 (h i)).2
  choose t t_sep ht using A
  refine ‚ü®‚ãÉ i, t i, .iUnion t_sep, ?_‚ü©
  simp only [Measure.ae_sum_eq, mem_iUnion, eventually_iSup]
  intro i
  filter_upwards [ht i] with x hx
  exact ‚ü®i, hx‚ü©
#align measure_theory.ae_strongly_measurable.sum_measure MeasureTheory.AEStronglyMeasurable.sum_measure

@[simp]
theorem _root_.aestronglyMeasurable_sum_measure_iff [PseudoMetrizableSpace Œ≤]
    {_m : MeasurableSpace Œ±} {Œº : Œπ ‚Üí Measure Œ±} :
    AEStronglyMeasurable f (sum Œº) ‚Üî ‚àÄ i, AEStronglyMeasurable f (Œº i) :=
  ‚ü®fun h _ => h.mono_measure (Measure.le_sum _ _), sum_measure‚ü©
#align ae_strongly_measurable_sum_measure_iff aestronglyMeasurable_sum_measure_iff

@[simp]
theorem _root_.aestronglyMeasurable_add_measure_iff [PseudoMetrizableSpace Œ≤] {ŒΩ : Measure Œ±} :
    AEStronglyMeasurable f (Œº + ŒΩ) ‚Üî AEStronglyMeasurable f Œº ‚àß AEStronglyMeasurable f ŒΩ := by
  rw [‚Üê sum_cond, aestronglyMeasurable_sum_measure_iff, Bool.forall_bool, and_comm]
  rfl
#align ae_strongly_measurable_add_measure_iff aestronglyMeasurable_add_measure_iff

@[measurability]
theorem add_measure [PseudoMetrizableSpace Œ≤] {ŒΩ : Measure Œ±} {f : Œ± ‚Üí Œ≤}
    (hŒº : AEStronglyMeasurable f Œº) (hŒΩ : AEStronglyMeasurable f ŒΩ) :
    AEStronglyMeasurable f (Œº + ŒΩ) :=
  aestronglyMeasurable_add_measure_iff.2 ‚ü®hŒº, hŒΩ‚ü©
#align measure_theory.ae_strongly_measurable.add_measure MeasureTheory.AEStronglyMeasurable.add_measure

@[measurability]
protected theorem iUnion [PseudoMetrizableSpace Œ≤] {s : Œπ ‚Üí Set Œ±}
    (h : ‚àÄ i, AEStronglyMeasurable f (Œº.restrict (s i))) :
    AEStronglyMeasurable f (Œº.restrict (‚ãÉ i, s i)) :=
  (sum_measure h).mono_measure <| restrict_iUnion_le
#align measure_theory.ae_strongly_measurable.Union MeasureTheory.AEStronglyMeasurable.iUnion

@[simp]
theorem _root_.aestronglyMeasurable_iUnion_iff [PseudoMetrizableSpace Œ≤] {s : Œπ ‚Üí Set Œ±} :
    AEStronglyMeasurable f (Œº.restrict (‚ãÉ i, s i)) ‚Üî
      ‚àÄ i, AEStronglyMeasurable f (Œº.restrict (s i)) :=
  ‚ü®fun h _ => h.mono_measure <| restrict_mono (subset_iUnion _ _) le_rfl,
    AEStronglyMeasurable.iUnion‚ü©
#align ae_strongly_measurable_Union_iff aestronglyMeasurable_iUnion_iff

@[simp]
theorem _root_.aestronglyMeasurable_union_iff [PseudoMetrizableSpace Œ≤] {s t : Set Œ±} :
    AEStronglyMeasurable f (Œº.restrict (s ‚à™ t)) ‚Üî
      AEStronglyMeasurable f (Œº.restrict s) ‚àß AEStronglyMeasurable f (Œº.restrict t) := by
  simp only [union_eq_iUnion, aestronglyMeasurable_iUnion_iff, Bool.forall_bool, cond, and_comm]
#align ae_strongly_measurable_union_iff aestronglyMeasurable_union_iff

theorem aestronglyMeasurable_uIoc_iff [LinearOrder Œ±] [PseudoMetrizableSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    {a b : Œ±} :
    AEStronglyMeasurable f (Œº.restrict <| uIoc a b) ‚Üî
      AEStronglyMeasurable f (Œº.restrict <| Ioc a b) ‚àß
        AEStronglyMeasurable f (Œº.restrict <| Ioc b a) := by
  rw [uIoc_eq_union, aestronglyMeasurable_union_iff]
#align measure_theory.ae_strongly_measurable.ae_strongly_measurable_uIoc_iff MeasureTheory.AEStronglyMeasurable.aestronglyMeasurable_uIoc_iff

@[measurability]
theorem smul_measure {R : Type*} [Monoid R] [DistribMulAction R ‚Ñù‚â•0‚àû] [IsScalarTower R ‚Ñù‚â•0‚àû ‚Ñù‚â•0‚àû]
    (h : AEStronglyMeasurable f Œº) (c : R) : AEStronglyMeasurable f (c ‚Ä¢ Œº) :=
  ‚ü®h.mk f, h.stronglyMeasurable_mk, ae_smul_measure h.ae_eq_mk c‚ü©
#align measure_theory.ae_strongly_measurable.smul_measure MeasureTheory.AEStronglyMeasurable.smul_measure

section MulAction

variable {M G G‚ÇÄ : Type*}
variable [Monoid M] [MulAction M Œ≤] [ContinuousConstSMul M Œ≤]
variable [Group G] [MulAction G Œ≤] [ContinuousConstSMul G Œ≤]
variable [GroupWithZero G‚ÇÄ] [MulAction G‚ÇÄ Œ≤] [ContinuousConstSMul G‚ÇÄ Œ≤]

theorem _root_.aestronglyMeasurable_const_smul_iff (c : G) :
    AEStronglyMeasurable (fun x => c ‚Ä¢ f x) Œº ‚Üî AEStronglyMeasurable f Œº :=
  ‚ü®fun h => by simpa only [inv_smul_smul] using h.const_smul' c‚Åª¬π, fun h => h.const_smul c‚ü©
#align ae_strongly_measurable_const_smul_iff aestronglyMeasurable_const_smul_iff

nonrec theorem _root_.IsUnit.aestronglyMeasurable_const_smul_iff {c : M} (hc : IsUnit c) :
    AEStronglyMeasurable (fun x => c ‚Ä¢ f x) Œº ‚Üî AEStronglyMeasurable f Œº :=
  let ‚ü®u, hu‚ü© := hc
  hu ‚ñ∏ aestronglyMeasurable_const_smul_iff u
#align is_unit.ae_strongly_measurable_const_smul_iff IsUnit.aestronglyMeasurable_const_smul_iff

theorem _root_.aestronglyMeasurable_const_smul_iff‚ÇÄ {c : G‚ÇÄ} (hc : c ‚â† 0) :
    AEStronglyMeasurable (fun x => c ‚Ä¢ f x) Œº ‚Üî AEStronglyMeasurable f Œº :=
  (IsUnit.mk0 _ hc).aestronglyMeasurable_const_smul_iff
#align ae_strongly_measurable_const_smul_iff‚ÇÄ aestronglyMeasurable_const_smul_iff‚ÇÄ

end MulAction

end AEStronglyMeasurable

/-! ## Almost everywhere finitely strongly measurable functions -/


namespace AEFinStronglyMeasurable

variable {m : MeasurableSpace Œ±} {Œº : Measure Œ±} [TopologicalSpace Œ≤] {f g : Œ± ‚Üí Œ≤}

section Mk

variable [Zero Œ≤]

/-- A `fin_strongly_measurable` function such that `f =·µê[Œº] hf.mk f`. See lemmas
`fin_strongly_measurable_mk` and `ae_eq_mk`. -/
protected noncomputable def mk (f : Œ± ‚Üí Œ≤) (hf : AEFinStronglyMeasurable f Œº) : Œ± ‚Üí Œ≤ :=
  hf.choose
#align measure_theory.ae_fin_strongly_measurable.mk MeasureTheory.AEFinStronglyMeasurable.mk

theorem finStronglyMeasurable_mk (hf : AEFinStronglyMeasurable f Œº) :
    FinStronglyMeasurable (hf.mk f) Œº :=
  hf.choose_spec.1
#align measure_theory.ae_fin_strongly_measurable.fin_strongly_measurable_mk MeasureTheory.AEFinStronglyMeasurable.finStronglyMeasurable_mk

theorem ae_eq_mk (hf : AEFinStronglyMeasurable f Œº) : f =·µê[Œº] hf.mk f :=
  hf.choose_spec.2
#align measure_theory.ae_fin_strongly_measurable.ae_eq_mk MeasureTheory.AEFinStronglyMeasurable.ae_eq_mk

@[aesop 10% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {Œ≤} [Zero Œ≤] [MeasurableSpace Œ≤] [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] {f : Œ± ‚Üí Œ≤} (hf : AEFinStronglyMeasurable f Œº) :
    AEMeasurable f Œº :=
  ‚ü®hf.mk f, hf.finStronglyMeasurable_mk.measurable, hf.ae_eq_mk‚ü©
#align measure_theory.ae_fin_strongly_measurable.ae_measurable MeasureTheory.AEFinStronglyMeasurable.aemeasurable

end Mk

section Arithmetic

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem mul [MonoidWithZero Œ≤] [ContinuousMul Œ≤] (hf : AEFinStronglyMeasurable f Œº)
    (hg : AEFinStronglyMeasurable g Œº) : AEFinStronglyMeasurable (f * g) Œº :=
  ‚ü®hf.mk f * hg.mk g, hf.finStronglyMeasurable_mk.mul hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.mul hg.ae_eq_mk‚ü©
#align measure_theory.ae_fin_strongly_measurable.mul MeasureTheory.AEFinStronglyMeasurable.mul

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem add [AddMonoid Œ≤] [ContinuousAdd Œ≤] (hf : AEFinStronglyMeasurable f Œº)
    (hg : AEFinStronglyMeasurable g Œº) : AEFinStronglyMeasurable (f + g) Œº :=
  ‚ü®hf.mk f + hg.mk g, hf.finStronglyMeasurable_mk.add hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.add hg.ae_eq_mk‚ü©
#align measure_theory.ae_fin_strongly_measurable.add MeasureTheory.AEFinStronglyMeasurable.add

@[measurability]
protected theorem neg [AddGroup Œ≤] [TopologicalAddGroup Œ≤] (hf : AEFinStronglyMeasurable f Œº) :
    AEFinStronglyMeasurable (-f) Œº :=
  ‚ü®-hf.mk f, hf.finStronglyMeasurable_mk.neg, hf.ae_eq_mk.neg‚ü©
#align measure_theory.ae_fin_strongly_measurable.neg MeasureTheory.AEFinStronglyMeasurable.neg

@[measurability]
protected theorem sub [AddGroup Œ≤] [ContinuousSub Œ≤] (hf : AEFinStronglyMeasurable f Œº)
    (hg : AEFinStronglyMeasurable g Œº) : AEFinStronglyMeasurable (f - g) Œº :=
  ‚ü®hf.mk f - hg.mk g, hf.finStronglyMeasurable_mk.sub hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.sub hg.ae_eq_mk‚ü©
#align measure_theory.ae_fin_strongly_measurable.sub MeasureTheory.AEFinStronglyMeasurable.sub

@[measurability]
protected theorem const_smul {ùïú} [TopologicalSpace ùïú] [AddMonoid Œ≤] [Monoid ùïú]
    [DistribMulAction ùïú Œ≤] [ContinuousSMul ùïú Œ≤] (hf : AEFinStronglyMeasurable f Œº) (c : ùïú) :
    AEFinStronglyMeasurable (c ‚Ä¢ f) Œº :=
  ‚ü®c ‚Ä¢ hf.mk f, hf.finStronglyMeasurable_mk.const_smul c, hf.ae_eq_mk.const_smul c‚ü©
#align measure_theory.ae_fin_strongly_measurable.const_smul MeasureTheory.AEFinStronglyMeasurable.const_smul

end Arithmetic

section Order

variable [Zero Œ≤]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Œ≤] [ContinuousSup Œ≤] (hf : AEFinStronglyMeasurable f Œº)
    (hg : AEFinStronglyMeasurable g Œº) : AEFinStronglyMeasurable (f ‚äî g) Œº :=
  ‚ü®hf.mk f ‚äî hg.mk g, hf.finStronglyMeasurable_mk.sup hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.sup hg.ae_eq_mk‚ü©
#align measure_theory.ae_fin_strongly_measurable.sup MeasureTheory.AEFinStronglyMeasurable.sup

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Œ≤] [ContinuousInf Œ≤] (hf : AEFinStronglyMeasurable f Œº)
    (hg : AEFinStronglyMeasurable g Œº) : AEFinStronglyMeasurable (f ‚äì g) Œº :=
  ‚ü®hf.mk f ‚äì hg.mk g, hf.finStronglyMeasurable_mk.inf hg.finStronglyMeasurable_mk,
    hf.ae_eq_mk.inf hg.ae_eq_mk‚ü©
#align measure_theory.ae_fin_strongly_measurable.inf MeasureTheory.AEFinStronglyMeasurable.inf

end Order

variable [Zero Œ≤] [T2Space Œ≤]

theorem exists_set_sigmaFinite (hf : AEFinStronglyMeasurable f Œº) :
    ‚àÉ t, MeasurableSet t ‚àß f =·µê[Œº.restrict t·∂ú] 0 ‚àß SigmaFinite (Œº.restrict t) := by
  rcases hf with ‚ü®g, hg, hfg‚ü©
  obtain ‚ü®t, ht, hgt_zero, htŒº‚ü© := hg.exists_set_sigmaFinite
  refine ‚ü®t, ht, ?_, htŒº‚ü©
  refine EventuallyEq.trans (ae_restrict_of_ae hfg) ?_
  rw [EventuallyEq, ae_restrict_iff' ht.compl]
  exact eventually_of_forall hgt_zero
#align measure_theory.ae_fin_strongly_measurable.exists_set_sigma_finite MeasureTheory.AEFinStronglyMeasurable.exists_set_sigmaFinite

/-- A measurable set `t` such that `f =·µê[Œº.restrict t·∂ú] 0` and `sigma_finite (Œº.restrict t)`. -/
def sigmaFiniteSet (hf : AEFinStronglyMeasurable f Œº) : Set Œ± :=
  hf.exists_set_sigmaFinite.choose
#align measure_theory.ae_fin_strongly_measurable.sigma_finite_set MeasureTheory.AEFinStronglyMeasurable.sigmaFiniteSet

protected theorem measurableSet (hf : AEFinStronglyMeasurable f Œº) :
    MeasurableSet hf.sigmaFiniteSet :=
  hf.exists_set_sigmaFinite.choose_spec.1
#align measure_theory.ae_fin_strongly_measurable.measurable_set MeasureTheory.AEFinStronglyMeasurable.measurableSet

theorem ae_eq_zero_compl (hf : AEFinStronglyMeasurable f Œº) :
    f =·µê[Œº.restrict hf.sigmaFiniteSet·∂ú] 0 :=
  hf.exists_set_sigmaFinite.choose_spec.2.1
#align measure_theory.ae_fin_strongly_measurable.ae_eq_zero_compl MeasureTheory.AEFinStronglyMeasurable.ae_eq_zero_compl

instance sigmaFinite_restrict (hf : AEFinStronglyMeasurable f Œº) :
    SigmaFinite (Œº.restrict hf.sigmaFiniteSet) :=
  hf.exists_set_sigmaFinite.choose_spec.2.2
#align measure_theory.ae_fin_strongly_measurable.sigma_finite_restrict MeasureTheory.AEFinStronglyMeasurable.sigmaFinite_restrict

end AEFinStronglyMeasurable

section SecondCountableTopology

variable {G : Type*} {p : ‚Ñù‚â•0‚àû} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
  [SeminormedAddCommGroup G] [MeasurableSpace G] [BorelSpace G] [SecondCountableTopology G]
  {f : Œ± ‚Üí G}

/-- In a space with second countable topology and a sigma-finite measure, `FinStronglyMeasurable`
  and `Measurable` are equivalent. -/
theorem finStronglyMeasurable_iff_measurable {_m0 : MeasurableSpace Œ±} (Œº : Measure Œ±)
    [SigmaFinite Œº] : FinStronglyMeasurable f Œº ‚Üî Measurable f :=
  ‚ü®fun h => h.measurable, fun h => (Measurable.stronglyMeasurable h).finStronglyMeasurable Œº‚ü©
#align measure_theory.fin_strongly_measurable_iff_measurable MeasureTheory.finStronglyMeasurable_iff_measurable

/-- In a space with second countable topology and a sigma-finite measure, a measurable function
is `FinStronglyMeasurable`. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem finStronglyMeasurable_of_measurable {_m0 : MeasurableSpace Œ±} (Œº : Measure Œ±)
    [SigmaFinite Œº] (hf : Measurable f) : FinStronglyMeasurable f Œº :=
  (finStronglyMeasurable_iff_measurable Œº).mpr hf

/-- In a space with second countable topology and a sigma-finite measure,
  `AEFinStronglyMeasurable` and `AEMeasurable` are equivalent. -/
theorem aefinStronglyMeasurable_iff_aemeasurable {_m0 : MeasurableSpace Œ±} (Œº : Measure Œ±)
    [SigmaFinite Œº] : AEFinStronglyMeasurable f Œº ‚Üî AEMeasurable f Œº := by
  simp_rw [AEFinStronglyMeasurable, AEMeasurable, finStronglyMeasurable_iff_measurable]
#align measure_theory.ae_fin_strongly_measurable_iff_ae_measurable MeasureTheory.aefinStronglyMeasurable_iff_aemeasurable

/-- In a space with second countable topology and a sigma-finite measure,
  an `AEMeasurable` function is `AEFinStronglyMeasurable`. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem aefinStronglyMeasurable_of_aemeasurable {_m0 : MeasurableSpace Œ±} (Œº : Measure Œ±)
    [SigmaFinite Œº] (hf : AEMeasurable f Œº) : AEFinStronglyMeasurable f Œº :=
  (aefinStronglyMeasurable_iff_aemeasurable Œº).mpr hf

end SecondCountableTopology

theorem measurable_uncurry_of_continuous_of_measurable {Œ± Œ≤ Œπ : Type*} [TopologicalSpace Œπ]
    [MetrizableSpace Œπ] [MeasurableSpace Œπ] [SecondCountableTopology Œπ] [OpensMeasurableSpace Œπ]
    {mŒ≤ : MeasurableSpace Œ≤} [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤]
    {m : MeasurableSpace Œ±} {u : Œπ ‚Üí Œ± ‚Üí Œ≤} (hu_cont : ‚àÄ x, Continuous fun i => u i x)
    (h : ‚àÄ i, Measurable (u i)) : Measurable (Function.uncurry u) := by
  obtain ‚ü®t_sf, ht_sf‚ü© :
    ‚àÉ t : ‚Ñï ‚Üí SimpleFunc Œπ Œπ, ‚àÄ j x, Tendsto (fun n => u (t n j) x) atTop (ùìù <| u j x) := by
    have h_str_meas : StronglyMeasurable (id : Œπ ‚Üí Œπ) := stronglyMeasurable_id
    refine ‚ü®h_str_meas.approx, fun j x => ?_‚ü©
    exact ((hu_cont x).tendsto j).comp (h_str_meas.tendsto_approx j)
  let U (n : ‚Ñï) (p : Œπ √ó Œ±) := u (t_sf n p.fst) p.snd
  have h_tendsto : Tendsto U atTop (ùìù fun p => u p.fst p.snd) := by
    rw [tendsto_pi_nhds]
    exact fun p => ht_sf p.fst p.snd
  refine measurable_of_tendsto_metrizable (fun n => ?_) h_tendsto
  have h_meas : Measurable fun p : (t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd := by
    have :
      (fun p : ‚Ü•(t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd) =
        (fun p : Œ± √ó (t_sf n).range => u (‚Üëp.snd) p.fst) ‚àò Prod.swap :=
      rfl
    rw [this, @measurable_swap_iff Œ± (‚Ü•(t_sf n).range) Œ≤ m]
    exact measurable_from_prod_countable fun j => h j
  have :
    (fun p : Œπ √ó Œ± => u (t_sf n p.fst) p.snd) =
      (fun p : ‚Ü•(t_sf n).range √ó Œ± => u p.fst p.snd) ‚àò fun p : Œπ √ó Œ± =>
        (‚ü®t_sf n p.fst, SimpleFunc.mem_range_self _ _‚ü©, p.snd) :=
    rfl
  simp_rw [U, this]
  refine h_meas.comp (Measurable.prod_mk ?_ measurable_snd)
  exact ((t_sf n).measurable.comp measurable_fst).subtype_mk
#align measure_theory.measurable_uncurry_of_continuous_of_measurable MeasureTheory.measurable_uncurry_of_continuous_of_measurable

theorem stronglyMeasurable_uncurry_of_continuous_of_stronglyMeasurable {Œ± Œ≤ Œπ : Type*}
    [TopologicalSpace Œπ] [MetrizableSpace Œπ] [MeasurableSpace Œπ] [SecondCountableTopology Œπ]
    [OpensMeasurableSpace Œπ] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ±]
    {u : Œπ ‚Üí Œ± ‚Üí Œ≤} (hu_cont : ‚àÄ x, Continuous fun i => u i x) (h : ‚àÄ i, StronglyMeasurable (u i)) :
    StronglyMeasurable (Function.uncurry u) := by
  borelize Œ≤
  obtain ‚ü®t_sf, ht_sf‚ü© :
    ‚àÉ t : ‚Ñï ‚Üí SimpleFunc Œπ Œπ, ‚àÄ j x, Tendsto (fun n => u (t n j) x) atTop (ùìù <| u j x) := by
    have h_str_meas : StronglyMeasurable (id : Œπ ‚Üí Œπ) := stronglyMeasurable_id
    refine ‚ü®h_str_meas.approx, fun j x => ?_‚ü©
    exact ((hu_cont x).tendsto j).comp (h_str_meas.tendsto_approx j)
  let U (n : ‚Ñï) (p : Œπ √ó Œ±) := u (t_sf n p.fst) p.snd
  have h_tendsto : Tendsto U atTop (ùìù fun p => u p.fst p.snd) := by
    rw [tendsto_pi_nhds]
    exact fun p => ht_sf p.fst p.snd
  refine stronglyMeasurable_of_tendsto _ (fun n => ?_) h_tendsto
  have h_str_meas : StronglyMeasurable fun p : (t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd := by
    refine stronglyMeasurable_iff_measurable_separable.2 ‚ü®?_, ?_‚ü©
    ¬∑ have :
        (fun p : ‚Ü•(t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd) =
          (fun p : Œ± √ó (t_sf n).range => u (‚Üëp.snd) p.fst) ‚àò Prod.swap :=
        rfl
      rw [this, measurable_swap_iff]
      exact measurable_from_prod_countable fun j => (h j).measurable
    ¬∑ have : IsSeparable (‚ãÉ i : (t_sf n).range, range (u i)) :=
        .iUnion fun i => (h i).isSeparable_range
      apply this.mono
      rintro _ ‚ü®‚ü®i, x‚ü©, rfl‚ü©
      simp only [mem_iUnion, mem_range]
      exact ‚ü®i, x, rfl‚ü©
  have :
    (fun p : Œπ √ó Œ± => u (t_sf n p.fst) p.snd) =
      (fun p : ‚Ü•(t_sf n).range √ó Œ± => u p.fst p.snd) ‚àò fun p : Œπ √ó Œ± =>
        (‚ü®t_sf n p.fst, SimpleFunc.mem_range_self _ _‚ü©, p.snd) :=
    rfl
  simp_rw [U, this]
  refine h_str_meas.comp_measurable (Measurable.prod_mk ?_ measurable_snd)
  exact ((t_sf n).measurable.comp measurable_fst).subtype_mk
#align measure_theory.strongly_measurable_uncurry_of_continuous_of_strongly_measurable MeasureTheory.stronglyMeasurable_uncurry_of_continuous_of_stronglyMeasurable

end MeasureTheory

-- Guard against import creep
assert_not_exists InnerProductSpace
