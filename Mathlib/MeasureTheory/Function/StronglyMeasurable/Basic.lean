/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.MeasureTheory.Function.SimpleFuncDense

/-!
# Strongly measurable and finitely strongly measurable functions

A function `f` is said to be strongly measurable if `f` is the sequential limit of simple functions.
It is said to be finitely strongly measurable with respect to a measure `Œº` if the supports
of those simple functions have finite measure.

If the target space has a second countable topology, strongly measurable and measurable are
equivalent.

If the measure is sigma-finite, strongly measurable and finitely strongly measurable are equivalent.

The main property of finitely strongly measurable functions is
`FinStronglyMeasurable.exists_set_sigmaFinite`: there exists a measurable set `t` such that the
function is supported on `t` and `Œº.restrict t` is sigma-finite. As a consequence, we can prove some
results for those functions as if the measure was sigma-finite.

We provide a solid API for strongly measurable functions, as a basis for the Bochner integral.

## Main definitions

* `StronglyMeasurable f`: `f : Œ± ‚Üí Œ≤` is the limit of a sequence `fs : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤`.
* `FinStronglyMeasurable f Œº`: `f : Œ± ‚Üí Œ≤` is the limit of a sequence `fs : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤`
  such that for all `n ‚àà ‚Ñï`, the measure of the support of `fs n` is finite.

## References

* [Hyt√∂nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.][Hytonen_VanNeerven_Veraar_Wies_2016]

-/

-- Guard against import creep
assert_not_exists InnerProductSpace

open MeasureTheory Filter TopologicalSpace Function Set MeasureTheory.Measure

open ENNReal Topology MeasureTheory NNReal

variable {Œ± Œ≤ Œ≥ Œπ : Type*} [Countable Œπ]

namespace MeasureTheory

local infixr:25 " ‚Üí‚Çõ " => SimpleFunc

section Definitions

variable [TopologicalSpace Œ≤]

/-- A function is `StronglyMeasurable` if it is the limit of simple functions. -/
@[fun_prop]
def StronglyMeasurable [MeasurableSpace Œ±] (f : Œ± ‚Üí Œ≤) : Prop :=
  ‚àÉ fs : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤, ‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x))

/-- The notation for StronglyMeasurable giving the measurable space instance explicitly. -/
scoped notation "StronglyMeasurable[" m "]" => @MeasureTheory.StronglyMeasurable _ _ _ m

/-- A function is `FinStronglyMeasurable` with respect to a measure if it is the limit of simple
  functions with support with finite measure. -/
def FinStronglyMeasurable [Zero Œ≤]
    {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí Œ≤) (Œº : Measure Œ± := by volume_tac) : Prop :=
  ‚àÉ fs : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤, (‚àÄ n, Œº (support (fs n)) < ‚àû) ‚àß ‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x))

end Definitions

open MeasureTheory

/-! ## Strongly measurable functions -/

section StronglyMeasurable
variable {_ : MeasurableSpace Œ±} {Œº : Measure Œ±} {f : Œ± ‚Üí Œ≤} {g : ‚Ñï ‚Üí Œ±} {m : ‚Ñï}

variable [TopologicalSpace Œ≤]

@[fun_prop]
theorem SimpleFunc.stronglyMeasurable (f : Œ± ‚Üí‚Çõ Œ≤) : StronglyMeasurable f :=
  ‚ü®fun _ => f, fun _ => tendsto_const_nhds‚ü©

@[simp, nontriviality]
lemma StronglyMeasurable.of_subsingleton_dom [Subsingleton Œ±] : StronglyMeasurable f :=
  ‚ü®fun _ => SimpleFunc.ofFinite f, fun _ => tendsto_const_nhds‚ü©

@[simp, nontriviality]
lemma StronglyMeasurable.of_subsingleton_cod [Subsingleton Œ≤] : StronglyMeasurable f := by
  let f_sf : Œ± ‚Üí‚Çõ Œ≤ := ‚ü®f, fun x => ?_, Set.Subsingleton.finite Set.subsingleton_of_subsingleton‚ü©
  ¬∑ exact ‚ü®fun _ => f_sf, fun x => tendsto_const_nhds‚ü©
  ¬∑ simp [Set.preimage, eq_iff_true_of_subsingleton]

@[deprecated StronglyMeasurable.of_subsingleton_cod (since := "2025-04-09")]
lemma Subsingleton.stronglyMeasurable [Subsingleton Œ≤] (f : Œ± ‚Üí Œ≤) : StronglyMeasurable f :=
  .of_subsingleton_cod

@[deprecated StronglyMeasurable.of_subsingleton_dom (since := "2025-04-09")]
lemma Subsingleton.stronglyMeasurable' [Subsingleton Œ±] (f : Œ± ‚Üí Œ≤) : StronglyMeasurable f :=
  .of_subsingleton_dom

@[fun_prop]
theorem stronglyMeasurable_const {b : Œ≤} : StronglyMeasurable fun _ : Œ± => b :=
  ‚ü®fun _ => SimpleFunc.const Œ± b, fun _ => tendsto_const_nhds‚ü©

@[to_additive]
theorem stronglyMeasurable_one [One Œ≤] : StronglyMeasurable (1 : Œ± ‚Üí Œ≤) := stronglyMeasurable_const

/-- A version of `stronglyMeasurable_const` that assumes `f x = f y` for all `x, y`.
This version works for functions between empty types. -/
theorem stronglyMeasurable_const' (hf : ‚àÄ x y, f x = f y) : StronglyMeasurable f := by
  nontriviality Œ±
  inhabit Œ±
  convert stronglyMeasurable_const (Œ≤ := Œ≤) using 1
  exact funext fun x => hf x default

variable [MeasurableSingletonClass Œ±]

section aux
omit [TopologicalSpace Œ≤]

/-- Auxiliary definition for `StronglyMeasurable.of_discrete`. -/
private noncomputable def simpleFuncAux (f : Œ± ‚Üí Œ≤) (g : ‚Ñï ‚Üí Œ±) : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤
  | 0 => .const _ (f (g 0))
  | n + 1 => .piecewise {g n} (.singleton _) (.const _ <| f (g n)) (simpleFuncAux f g n)

private lemma simpleFuncAux_eq_of_lt : ‚àÄ n > m, simpleFuncAux f g n (g m) = f (g m)
  | _, .refl => by simp [simpleFuncAux]
  | _, Nat.le.step (m := n) hmn => by
    obtain hnm | hnm := eq_or_ne (g n) (g m) <;>
      simp [simpleFuncAux, Set.piecewise_eq_of_notMem, hnm.symm, simpleFuncAux_eq_of_lt _ hmn]

private lemma simpleFuncAux_eventuallyEq : ‚àÄ·∂† n in atTop, simpleFuncAux f g n (g m) = f (g m) :=
  eventually_atTop.2 ‚ü®_, simpleFuncAux_eq_of_lt‚ü©

end aux

@[fun_prop]
lemma StronglyMeasurable.of_discrete [Countable Œ±] : StronglyMeasurable f := by
  nontriviality Œ±
  nontriviality Œ≤
  obtain ‚ü®g, hg‚ü© := exists_surjective_nat Œ±
  exact ‚ü®simpleFuncAux f g, hg.forall.2 fun m ‚Ü¶
    tendsto_nhds_of_eventually_eq simpleFuncAux_eventuallyEq‚ü©

@[deprecated StronglyMeasurable.of_discrete (since := "2025-04-09")]
theorem StronglyMeasurable.of_finite [Finite Œ±] : StronglyMeasurable f := .of_discrete

end StronglyMeasurable

namespace StronglyMeasurable

variable {f g : Œ± ‚Üí Œ≤}

section BasicPropertiesInAnyTopologicalSpace

variable [TopologicalSpace Œ≤]

/-- A sequence of simple functions such that
`‚àÄ x, Tendsto (fun n => hf.approx n x) atTop (ùìù (f x))`.
That property is given by `stronglyMeasurable.tendsto_approx`. -/
protected noncomputable def approx {_ : MeasurableSpace Œ±} (hf : StronglyMeasurable f) :
    ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤ :=
  hf.choose

protected theorem tendsto_approx {_ : MeasurableSpace Œ±} (hf : StronglyMeasurable f) :
    ‚àÄ x, Tendsto (fun n => hf.approx n x) atTop (ùìù (f x)) :=
  hf.choose_spec

/-- Similar to `stronglyMeasurable.approx`, but enforces that the norm of every function in the
sequence is less than `c` everywhere. If `‚Äñf x‚Äñ ‚â§ c` this sequence of simple functions verifies
`Tendsto (fun n => hf.approxBounded n x) atTop (ùìù (f x))`. -/
noncomputable def approxBounded {_ : MeasurableSpace Œ±} [Norm Œ≤] [SMul ‚Ñù Œ≤]
    (hf : StronglyMeasurable f) (c : ‚Ñù) : ‚Ñï ‚Üí SimpleFunc Œ± Œ≤ := fun n =>
  (hf.approx n).map fun x => min 1 (c / ‚Äñx‚Äñ) ‚Ä¢ x

theorem tendsto_approxBounded_of_norm_le {Œ≤} {f : Œ± ‚Üí Œ≤} [NormedAddCommGroup Œ≤] [NormedSpace ‚Ñù Œ≤]
    {m : MeasurableSpace Œ±} (hf : StronglyMeasurable[m] f) {c : ‚Ñù} {x : Œ±} (hfx : ‚Äñf x‚Äñ ‚â§ c) :
    Tendsto (fun n => hf.approxBounded c n x) atTop (ùìù (f x)) := by
  have h_tendsto := hf.tendsto_approx x
  simp only [StronglyMeasurable.approxBounded, SimpleFunc.coe_map, Function.comp_apply]
  by_cases hfx0 : ‚Äñf x‚Äñ = 0
  ¬∑ rw [norm_eq_zero] at hfx0
    rw [hfx0] at h_tendsto ‚ä¢
    have h_tendsto_norm : Tendsto (fun n => ‚Äñhf.approx n x‚Äñ) atTop (ùìù 0) := by
      convert h_tendsto.norm
      rw [norm_zero]
    refine squeeze_zero_norm (fun n => ?_) h_tendsto_norm
    calc
      ‚Äñmin 1 (c / ‚Äñhf.approx n x‚Äñ) ‚Ä¢ hf.approx n x‚Äñ =
          ‚Äñmin 1 (c / ‚Äñhf.approx n x‚Äñ)‚Äñ * ‚Äñhf.approx n x‚Äñ :=
        norm_smul _ _
      _ ‚â§ ‚Äñ(1 : ‚Ñù)‚Äñ * ‚Äñhf.approx n x‚Äñ := by
        refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
        rw [norm_one, Real.norm_of_nonneg]
        ¬∑ exact min_le_left _ _
        ¬∑ exact le_min zero_le_one (div_nonneg ((norm_nonneg _).trans hfx) (norm_nonneg _))
      _ = ‚Äñhf.approx n x‚Äñ := by rw [norm_one, one_mul]
  rw [‚Üê one_smul ‚Ñù (f x)]
  refine Tendsto.smul ?_ h_tendsto
  have : min 1 (c / ‚Äñf x‚Äñ) = 1 := by
    rw [min_eq_left_iff, one_le_div (lt_of_le_of_ne (norm_nonneg _) (Ne.symm hfx0))]
    exact hfx
  nth_rw 2 [this.symm]
  refine Tendsto.min tendsto_const_nhds ?_
  exact Tendsto.div tendsto_const_nhds h_tendsto.norm hfx0

theorem tendsto_approxBounded_ae {Œ≤} {f : Œ± ‚Üí Œ≤} [NormedAddCommGroup Œ≤] [NormedSpace ‚Ñù Œ≤]
    {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} (hf : StronglyMeasurable[m] f) {c : ‚Ñù}
    (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => hf.approxBounded c n x) atTop (ùìù (f x)) := by
  filter_upwards [hf_bound] with x hfx using tendsto_approxBounded_of_norm_le hf hfx

theorem norm_approxBounded_le {Œ≤} {f : Œ± ‚Üí Œ≤} [SeminormedAddCommGroup Œ≤] [NormedSpace ‚Ñù Œ≤]
    {m : MeasurableSpace Œ±} {c : ‚Ñù} (hf : StronglyMeasurable[m] f) (hc : 0 ‚â§ c) (n : ‚Ñï) (x : Œ±) :
    ‚Äñhf.approxBounded c n x‚Äñ ‚â§ c := by
  simp only [StronglyMeasurable.approxBounded, SimpleFunc.coe_map, Function.comp_apply]
  refine (norm_smul_le _ _).trans ?_
  by_cases h0 : ‚Äñhf.approx n x‚Äñ = 0
  ¬∑ simp only [h0, _root_.div_zero, min_eq_right, zero_le_one, norm_zero, mul_zero]
    exact hc
  rcases le_total ‚Äñhf.approx n x‚Äñ c with h | h
  ¬∑ rw [min_eq_left _]
    ¬∑ simpa only [norm_one, one_mul] using h
    ¬∑ rwa [one_le_div (lt_of_le_of_ne (norm_nonneg _) (Ne.symm h0))]
  ¬∑ rw [min_eq_right _]
    ¬∑ rw [norm_div, norm_norm, mul_comm, mul_div, div_eq_mul_inv, mul_comm, ‚Üê mul_assoc,
        inv_mul_cancel‚ÇÄ h0, one_mul, Real.norm_of_nonneg hc]
    ¬∑ rwa [div_le_one (lt_of_le_of_ne (norm_nonneg _) (Ne.symm h0))]

theorem _root_.stronglyMeasurable_bot_iff [Nonempty Œ≤] [T2Space Œ≤] :
    StronglyMeasurable[‚ä•] f ‚Üî ‚àÉ c, f = fun _ => c := by
  rcases isEmpty_or_nonempty Œ± with hŒ± | hŒ±
  ¬∑ simp [eq_iff_true_of_subsingleton]
  refine ‚ü®fun hf => ?_, fun hf_eq => ?_‚ü©
  ¬∑ refine ‚ü®f hŒ±.some, ?_‚ü©
    let fs := hf.approx
    have h_fs_tendsto : ‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x)) := hf.tendsto_approx
    have : ‚àÄ n, ‚àÉ c, ‚àÄ x, fs n x = c := fun n => SimpleFunc.simpleFunc_bot (fs n)
    let cs n := (this n).choose
    have h_cs_eq : ‚àÄ n, ‚áë(fs n) = fun _ => cs n := fun n => funext (this n).choose_spec
    conv at h_fs_tendsto => enter [x, 1, n]; rw [h_cs_eq]
    have h_tendsto : Tendsto cs atTop (ùìù (f hŒ±.some)) := h_fs_tendsto hŒ±.some
    ext1 x
    exact tendsto_nhds_unique (h_fs_tendsto x) h_tendsto
  ¬∑ obtain ‚ü®c, rfl‚ü© := hf_eq
    exact stronglyMeasurable_const

end BasicPropertiesInAnyTopologicalSpace

theorem finStronglyMeasurable_of_set_sigmaFinite [TopologicalSpace Œ≤] [Zero Œ≤]
    {m : MeasurableSpace Œ±} {Œº : Measure Œ±} (hf_meas : StronglyMeasurable f) {t : Set Œ±}
    (ht : MeasurableSet t) (hft_zero : ‚àÄ x ‚àà t·∂ú, f x = 0) (htŒº : SigmaFinite (Œº.restrict t)) :
    FinStronglyMeasurable f Œº := by
  haveI : SigmaFinite (Œº.restrict t) := htŒº
  let S := spanningSets (Œº.restrict t)
  have hS_meas : ‚àÄ n, MeasurableSet (S n) := measurableSet_spanningSets (Œº.restrict t)
  let f_approx := hf_meas.approx
  let fs n := SimpleFunc.restrict (f_approx n) (S n ‚à© t)
  have h_fs_t_compl : ‚àÄ n, ‚àÄ x, x ‚àâ t ‚Üí fs n x = 0 := by
    intro n x hxt
    rw [SimpleFunc.restrict_apply _ ((hS_meas n).inter ht)]
    refine Set.indicator_of_notMem ?_ _
    simp [hxt]
  refine ‚ü®fs, ?_, fun x => ?_‚ü©
  ¬∑ simp_rw [SimpleFunc.support_eq, ‚Üê Finset.mem_coe]
    classical
    refine fun n => measure_biUnion_lt_top {y ‚àà (fs n).range | y ‚â† 0}.finite_toSet fun y hy => ?_
    rw [SimpleFunc.restrict_preimage_singleton _ ((hS_meas n).inter ht)]
    swap
    ¬∑ letI : (y : Œ≤) ‚Üí Decidable (y = 0) := fun y => Classical.propDecidable _
      rw [Finset.mem_coe, Finset.mem_filter] at hy
      exact hy.2
    refine (measure_mono Set.inter_subset_left).trans_lt ?_
    have h_lt_top := measure_spanningSets_lt_top (Œº.restrict t) n
    rwa [Measure.restrict_apply' ht] at h_lt_top
  ¬∑ by_cases hxt : x ‚àà t
    swap
    ¬∑ rw [funext fun n => h_fs_t_compl n x hxt, hft_zero x hxt]
      exact tendsto_const_nhds
    have h : Tendsto (fun n => (f_approx n) x) atTop (ùìù (f x)) := hf_meas.tendsto_approx x
    obtain ‚ü®n‚ÇÅ, hn‚ÇÅ‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí fs m x = f_approx m x := by
      obtain ‚ü®n, hn‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí x ‚àà S m ‚à© t := by
        rsuffices ‚ü®n, hn‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí x ‚àà S m
        ¬∑ exact ‚ü®n, fun m hnm => Set.mem_inter (hn m hnm) hxt‚ü©
        rsuffices ‚ü®n, hn‚ü© : ‚àÉ n, x ‚àà S n
        ¬∑ exact ‚ü®n, fun m hnm => monotone_spanningSets (Œº.restrict t) hnm hn‚ü©
        rw [‚Üê Set.mem_iUnion, iUnion_spanningSets (Œº.restrict t)]
        trivial
      refine ‚ü®n, fun m hnm => ?_‚ü©
      simp_rw [fs, SimpleFunc.restrict_apply _ ((hS_meas m).inter ht),
        Set.indicator_of_mem (hn m hnm)]
    rw [tendsto_atTop'] at h ‚ä¢
    intro s hs
    obtain ‚ü®n‚ÇÇ, hn‚ÇÇ‚ü© := h s hs
    refine ‚ü®max n‚ÇÅ n‚ÇÇ, fun m hm => ?_‚ü©
    rw [hn‚ÇÅ m ((le_max_left _ _).trans hm.le)]
    exact hn‚ÇÇ m ((le_max_right _ _).trans hm.le)

/-- If the measure is sigma-finite, all strongly measurable functions are
  `FinStronglyMeasurable`. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem finStronglyMeasurable [TopologicalSpace Œ≤] [Zero Œ≤] {m0 : MeasurableSpace Œ±}
    (hf : StronglyMeasurable f) (Œº : Measure Œ±) [SigmaFinite Œº] : FinStronglyMeasurable f Œº :=
  hf.finStronglyMeasurable_of_set_sigmaFinite MeasurableSet.univ (by simp)
    (by rwa [Measure.restrict_univ])

/-- A strongly measurable function is measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem measurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [MeasurableSpace Œ≤] [BorelSpace Œ≤] (hf : StronglyMeasurable f) : Measurable f :=
  measurable_of_tendsto_metrizable (fun n => (hf.approx n).measurable)
    (tendsto_pi_nhds.mpr hf.tendsto_approx)

/-- A strongly measurable function is almost everywhere measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem aemeasurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤] {Œº : Measure Œ±}
    (hf : StronglyMeasurable f) : AEMeasurable f Œº :=
  hf.measurable.aemeasurable

theorem _root_.Continuous.comp_stronglyMeasurable {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [TopologicalSpace Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : Continuous g) (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x => g (f x) :=
  ‚ü®fun n => SimpleFunc.map g (hf.approx n), fun x => (hg.tendsto _).comp (hf.tendsto_approx x)‚ü©

@[to_additive]
nonrec theorem measurableSet_mulSupport {m : MeasurableSpace Œ±} [One Œ≤] [TopologicalSpace Œ≤]
    [MetrizableSpace Œ≤] (hf : StronglyMeasurable f) : MeasurableSet (mulSupport f) := by
  borelize Œ≤
  exact measurableSet_mulSupport hf.measurable

protected theorem mono {m m' : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    (hf : StronglyMeasurable[m'] f) (h_mono : m' ‚â§ m) : StronglyMeasurable[m] f := by
  let f_approx : ‚Ñï ‚Üí @SimpleFunc Œ± m Œ≤ := fun n =>
    @SimpleFunc.mk Œ± m Œ≤
      (hf.approx n)
      (fun x => h_mono _ (SimpleFunc.measurableSet_fiber' _ x))
      (SimpleFunc.finite_range (hf.approx n))
  exact ‚ü®f_approx, hf.tendsto_approx‚ü©

@[fun_prop]
protected theorem fst {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
    {f : Œ± ‚Üí Œ≤ √ó Œ≥} (hf : StronglyMeasurable f) : StronglyMeasurable fun x ‚Ü¶ (f x).1 :=
  continuous_fst.comp_stronglyMeasurable hf

@[fun_prop]
protected theorem snd {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
    {f : Œ± ‚Üí Œ≤ √ó Œ≥} (hf : StronglyMeasurable f) : StronglyMeasurable fun x ‚Ü¶ (f x).2 :=
  continuous_snd.comp_stronglyMeasurable hf

@[fun_prop]
protected theorem prodMk {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
    {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => (f x, g x) := by
  refine ‚ü®fun n => SimpleFunc.pair (hf.approx n) (hg.approx n), fun x => ?_‚ü©
  rw [nhds_prod_eq]
  exact Tendsto.prodMk (hf.tendsto_approx x) (hg.tendsto_approx x)

@[deprecated (since := "2025-03-05")] protected alias prod_mk := StronglyMeasurable.prodMk

@[fun_prop]
theorem comp_measurable [TopologicalSpace Œ≤] {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥}
    {f : Œ± ‚Üí Œ≤} {g : Œ≥ ‚Üí Œ±} (hf : StronglyMeasurable f) (hg : Measurable g) :
    StronglyMeasurable (f ‚àò g) :=
  ‚ü®fun n => SimpleFunc.comp (hf.approx n) g hg, fun x => hf.tendsto_approx (g x)‚ü©

theorem of_uncurry_left [TopologicalSpace Œ≤] {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥}
    {f : Œ± ‚Üí Œ≥ ‚Üí Œ≤} (hf : StronglyMeasurable (uncurry f)) {x : Œ±} : StronglyMeasurable (f x) :=
  hf.comp_measurable measurable_prodMk_left

theorem of_uncurry_right [TopologicalSpace Œ≤] {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥}
    {f : Œ± ‚Üí Œ≥ ‚Üí Œ≤} (hf : StronglyMeasurable (uncurry f)) {y : Œ≥} :
    StronglyMeasurable fun x => f x y :=
  hf.comp_measurable measurable_prodMk_right

protected theorem prod_swap {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≤} [TopologicalSpace Œ≥]
    {f : Œ≤ √ó Œ± ‚Üí Œ≥} (hf : StronglyMeasurable f) :
    StronglyMeasurable (fun z : Œ± √ó Œ≤ => f z.swap) :=
  hf.comp_measurable measurable_swap

protected theorem comp_fst {_ : MeasurableSpace Œ±} [mŒ≤ : MeasurableSpace Œ≤] [TopologicalSpace Œ≥]
    {f : Œ± ‚Üí Œ≥} (hf : StronglyMeasurable f) :
    StronglyMeasurable (fun z : Œ± √ó Œ≤ => f z.1) :=
  hf.comp_measurable measurable_fst

protected theorem comp_snd [mŒ± : MeasurableSpace Œ±] {_ : MeasurableSpace Œ≤} [TopologicalSpace Œ≥]
    {f : Œ≤ ‚Üí Œ≥} (hf : StronglyMeasurable f) :
    StronglyMeasurable (fun z : Œ± √ó Œ≤ => f z.2) :=
  hf.comp_measurable measurable_snd

section Arithmetic

variable {mŒ± : MeasurableSpace Œ±} [TopologicalSpace Œ≤]

@[to_additive (attr := fun_prop, aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem mul [Mul Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f * g) :=
  ‚ü®fun n => hf.approx n * hg.approx n, fun x => (hf.tendsto_approx x).mul (hg.tendsto_approx x)‚ü©

@[to_additive (attr := fun_prop, measurability)]
theorem mul_const [Mul Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f) (c : Œ≤) :
    StronglyMeasurable fun x => f x * c :=
  hf.mul stronglyMeasurable_const

@[to_additive (attr := fun_prop, measurability)]
theorem const_mul [Mul Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f) (c : Œ≤) :
    StronglyMeasurable fun x => c * f x :=
  stronglyMeasurable_const.mul hf

@[to_additive (attr := fun_prop, aesop safe 20 apply (rule_sets := [Measurable])) const_nsmul]
protected theorem pow [Monoid Œ≤] [ContinuousMul Œ≤] (hf : StronglyMeasurable f) (n : ‚Ñï) :
    StronglyMeasurable (f ^ n) :=
  ‚ü®fun k => hf.approx k ^ n, fun x => (hf.tendsto_approx x).pow n‚ü©

@[to_additive (attr := fun_prop, measurability)]
protected theorem inv [Inv Œ≤] [ContinuousInv Œ≤] (hf : StronglyMeasurable f) :
    StronglyMeasurable f‚Åª¬π :=
  ‚ü®fun n => (hf.approx n)‚Åª¬π, fun x => (hf.tendsto_approx x).inv‚ü©

@[to_additive (attr := fun_prop, aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem div [Div Œ≤] [ContinuousDiv Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f / g) :=
  ‚ü®fun n => hf.approx n / hg.approx n, fun x => (hf.tendsto_approx x).div' (hg.tendsto_approx x)‚ü©

@[to_additive]
theorem mul_iff_right [CommGroup Œ≤] [IsTopologicalGroup Œ≤] (hf : StronglyMeasurable f) :
    StronglyMeasurable (f * g) ‚Üî StronglyMeasurable g :=
  ‚ü®fun h ‚Ü¶ show g = f * g * f‚Åª¬π by simp only [mul_inv_cancel_comm] ‚ñ∏ h.mul hf.inv,
    fun h ‚Ü¶ hf.mul h‚ü©

@[to_additive]
theorem mul_iff_left [CommGroup Œ≤] [IsTopologicalGroup Œ≤] (hf : StronglyMeasurable f) :
    StronglyMeasurable (g * f) ‚Üî StronglyMeasurable g :=
  mul_comm g f ‚ñ∏ mul_iff_right hf

@[to_additive (attr := fun_prop, aesop safe 20 apply (rule_sets := [Measurable]))]
protected theorem smul {ùïú} [TopologicalSpace ùïú] [SMul ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú}
    {g : Œ± ‚Üí Œ≤} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => f x ‚Ä¢ g x :=
  continuous_smul.comp_stronglyMeasurable (hf.prodMk hg)

@[to_additive (attr := fun_prop, measurability)]
protected theorem const_smul {ùïú} [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] (hf : StronglyMeasurable f)
    (c : ùïú) : StronglyMeasurable (c ‚Ä¢ f) :=
  ‚ü®fun n => c ‚Ä¢ hf.approx n, fun x => (hf.tendsto_approx x).const_smul c‚ü©

@[to_additive (attr := fun_prop, measurability)]
protected theorem const_smul' {ùïú} [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] (hf : StronglyMeasurable f)
    (c : ùïú) : StronglyMeasurable fun x => c ‚Ä¢ f x :=
  hf.const_smul c

@[to_additive (attr := fun_prop, measurability)]
protected theorem smul_const {ùïú} [TopologicalSpace ùïú] [SMul ùïú Œ≤] [ContinuousSMul ùïú Œ≤] {f : Œ± ‚Üí ùïú}
    (hf : StronglyMeasurable f) (c : Œ≤) : StronglyMeasurable fun x => f x ‚Ä¢ c :=
  continuous_smul.comp_stronglyMeasurable (hf.prodMk stronglyMeasurable_const)

/-- Pointwise star on functions induced from continuous star preserves strong measurability. -/
@[measurability]
protected theorem star {R : Type*} [MeasurableSpace Œ±] [Star R] [TopologicalSpace R]
    [ContinuousStar R] (f : Œ± ‚Üí R) (hf : StronglyMeasurable f) : StronglyMeasurable (star f) :=
  ‚ü®fun n => star (hf.approx n), fun x => (hf.tendsto_approx x).star‚ü©

/-- In a normed vector space, the addition of a measurable function and a strongly measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the addition of two measurable functions. -/
theorem _root_.Measurable.add_stronglyMeasurable
    {Œ± E : Type*} {_ : MeasurableSpace Œ±} [AddCancelMonoid E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [PseudoMetrizableSpace E]
    {g f : Œ± ‚Üí E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (g + f) := by
  rcases hf with ‚ü®œÜ, hœÜ‚ü©
  have : Tendsto (fun n x ‚Ü¶ g x + œÜ n x) atTop (ùìù (g + f)) :=
    tendsto_pi_nhds.2 (fun x ‚Ü¶ tendsto_const_nhds.add (hœÜ x))
  apply measurable_of_tendsto_metrizable (fun n ‚Ü¶ ?_) this
  exact hg.add_simpleFunc _

/-- In a normed vector space, the subtraction of a measurable function and a strongly measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the subtraction of two measurable functions. -/
theorem _root_.Measurable.sub_stronglyMeasurable
    {Œ± E : Type*} {_ : MeasurableSpace Œ±} [AddGroup E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [ContinuousNeg E] [PseudoMetrizableSpace E]
    {g f : Œ± ‚Üí E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (g - f) := by
  rw [sub_eq_add_neg]
  exact hg.add_stronglyMeasurable hf.neg

/-- In a normed vector space, the addition of a strongly measurable function and a measurable
function is measurable. Note that this is not true without further second-countability assumptions
for the addition of two measurable functions. -/
theorem _root_.Measurable.stronglyMeasurable_add
    {Œ± E : Type*} {_ : MeasurableSpace Œ±} [AddCancelMonoid E] [TopologicalSpace E]
    [MeasurableSpace E] [BorelSpace E] [ContinuousAdd E] [PseudoMetrizableSpace E]
    {g f : Œ± ‚Üí E} (hg : Measurable g) (hf : StronglyMeasurable f) :
    Measurable (f + g) := by
  rcases hf with ‚ü®œÜ, hœÜ‚ü©
  have : Tendsto (fun n x ‚Ü¶ œÜ n x + g x) atTop (ùìù (f + g)) :=
    tendsto_pi_nhds.2 (fun x ‚Ü¶ (hœÜ x).add tendsto_const_nhds)
  apply measurable_of_tendsto_metrizable (fun n ‚Ü¶ ?_) this
  exact hg.simpleFunc_add _

end Arithmetic

section MulAction

variable {M G G‚ÇÄ : Type*}
variable [TopologicalSpace Œ≤]
variable [Monoid M] [MulAction M Œ≤] [ContinuousConstSMul M Œ≤]
variable [Group G] [MulAction G Œ≤] [ContinuousConstSMul G Œ≤]
variable [GroupWithZero G‚ÇÄ] [MulAction G‚ÇÄ Œ≤] [ContinuousConstSMul G‚ÇÄ Œ≤]

theorem _root_.stronglyMeasurable_const_smul_iff {m : MeasurableSpace Œ±} (c : G) :
    (StronglyMeasurable fun x => c ‚Ä¢ f x) ‚Üî StronglyMeasurable f :=
  ‚ü®fun h => by simpa only [inv_smul_smul] using h.const_smul' c‚Åª¬π, fun h => h.const_smul c‚ü©

nonrec theorem _root_.IsUnit.stronglyMeasurable_const_smul_iff {_ : MeasurableSpace Œ±} {c : M}
    (hc : IsUnit c) :
    (StronglyMeasurable fun x => c ‚Ä¢ f x) ‚Üî StronglyMeasurable f :=
  let ‚ü®u, hu‚ü© := hc
  hu ‚ñ∏ stronglyMeasurable_const_smul_iff u

theorem _root_.stronglyMeasurable_const_smul_iff‚ÇÄ {_ : MeasurableSpace Œ±} {c : G‚ÇÄ} (hc : c ‚â† 0) :
    (StronglyMeasurable fun x => c ‚Ä¢ f x) ‚Üî StronglyMeasurable f :=
  (IsUnit.mk0 _ hc).stronglyMeasurable_const_smul_iff

end MulAction

section Order

variable [MeasurableSpace Œ±] [TopologicalSpace Œ≤]

open Filter

@[fun_prop, aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [Max Œ≤] [ContinuousSup Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f ‚äî g) :=
  ‚ü®fun n => hf.approx n ‚äî hg.approx n, fun x =>
    (hf.tendsto_approx x).sup_nhds (hg.tendsto_approx x)‚ü©

@[fun_prop, aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [Min Œ≤] [ContinuousInf Œ≤] (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (f ‚äì g) :=
  ‚ü®fun n => hf.approx n ‚äì hg.approx n, fun x =>
    (hf.tendsto_approx x).inf_nhds (hg.tendsto_approx x)‚ü©

end Order

/-!
### Big operators: `‚àè` and `‚àë`
-/


section Monoid

variable {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M] {m : MeasurableSpace Œ±}

@[to_additive (attr := fun_prop, measurability)]
theorem _root_.List.stronglyMeasurable_prod (l : List (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, StronglyMeasurable f) : StronglyMeasurable l.prod := by
  induction l with
  | nil => exact stronglyMeasurable_one
  | cons f l ihl =>
    rw [List.forall_mem_cons] at hl
    rw [List.prod_cons]
    exact hl.1.mul (ihl hl.2)

@[deprecated (since := "2025-05-30")]
alias _root_.List.stronglyMeasurable_sum' := List.stronglyMeasurable_sum
@[to_additive existing, deprecated (since := "2025-05-30")]
alias _root_.List.stronglyMeasurable_prod' := List.stronglyMeasurable_prod

@[to_additive (attr := fun_prop, measurability)]
theorem _root_.List.stronglyMeasurable_fun_prod (l : List (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, StronglyMeasurable f) :
    StronglyMeasurable fun x => (l.map fun f : Œ± ‚Üí M => f x).prod := by
  simpa only [‚Üê Pi.list_prod_apply] using l.stronglyMeasurable_prod hl

end Monoid

section CommMonoid

variable {M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M] {m : MeasurableSpace Œ±}


@[to_additive (attr := fun_prop, measurability)]
theorem _root_.Multiset.stronglyMeasurable_prod (l : Multiset (Œ± ‚Üí M))
    (hl : ‚àÄ f ‚àà l, StronglyMeasurable f) : StronglyMeasurable l.prod := by
  rcases l with ‚ü®l‚ü©
  simpa using l.stronglyMeasurable_prod (by simpa using hl)

@[deprecated (since := "2025-05-30")]
alias _root_.Multiset.stronglyMeasurable_sum' := Multiset.stronglyMeasurable_sum
@[to_additive existing, deprecated (since := "2025-05-30")]
alias _root_.Multiset.stronglyMeasurable_prod' := Multiset.stronglyMeasurable_prod

@[to_additive (attr := fun_prop, measurability)]
theorem _root_.Multiset.stronglyMeasurable_fun_prod (s : Multiset (Œ± ‚Üí M))
    (hs : ‚àÄ f ‚àà s, StronglyMeasurable f) :
    StronglyMeasurable fun x => (s.map fun f : Œ± ‚Üí M => f x).prod := by
  simpa only [‚Üê Pi.multiset_prod_apply] using s.stronglyMeasurable_prod hs

@[to_additive (attr := measurability, fun_prop)]
theorem _root_.Finset.stronglyMeasurable_prod {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí M} (s : Finset Œπ)
    (hf : ‚àÄ i ‚àà s, StronglyMeasurable (f i)) : StronglyMeasurable (‚àè i ‚àà s, f i) :=
  Finset.prod_induction _ _ (fun _a _b ha hb => ha.mul hb) (@stronglyMeasurable_one Œ± M _ _ _) hf

@[deprecated (since := "2025-05-30")]
alias _root_.Finset.stronglyMeasurable_sum' := Finset.stronglyMeasurable_sum
@[to_additive existing, deprecated (since := "2025-05-30")]
alias _root_.Finset.stronglyMeasurable_prod' := Finset.stronglyMeasurable_prod

@[to_additive (attr := measurability, fun_prop)]
theorem _root_.Finset.stronglyMeasurable_fun_prod {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí M} (s : Finset Œπ)
    (hf : ‚àÄ i ‚àà s, StronglyMeasurable (f i)) : StronglyMeasurable fun a => ‚àè i ‚àà s, f i a := by
  simpa only [‚Üê Finset.prod_apply] using s.stronglyMeasurable_prod hf

variable {n : MeasurableSpace Œ≤} in
/-- Compositional version of `Finset.stronglyMeasurable_prod` for use by `fun_prop`. -/
@[to_additive (attr := measurability, fun_prop)
/-- Compositional version of `Finset.stronglyMeasurable_sum` for use by `fun_prop`. -/]
lemma Finset.stronglyMeasurable_prod_apply {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí Œ≤ ‚Üí M} {g : Œ± ‚Üí Œ≤}
    {s : Finset Œπ} (hf : ‚àÄ i ‚àà s, StronglyMeasurable ‚Üø(f i)) (hg : Measurable g) :
    StronglyMeasurable fun a ‚Ü¶ (‚àè i ‚àà s, f i a) (g a) := by
  simp only [Finset.prod_apply]; fun_prop (discharger := assumption)

end CommMonoid

/-- The range of a strongly measurable function is separable. -/
protected theorem isSeparable_range {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    (hf : StronglyMeasurable f) : TopologicalSpace.IsSeparable (range f) := by
  have : IsSeparable (closure (‚ãÉ n, range (hf.approx n))) :=
    .closure <| .iUnion fun n => (hf.approx n).finite_range.isSeparable
  apply this.mono
  rintro _ ‚ü®x, rfl‚ü©
  apply mem_closure_of_tendsto (hf.tendsto_approx x)
  filter_upwards with n
  apply mem_iUnion_of_mem n
  exact mem_range_self _

theorem separableSpace_range_union_singleton {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] (hf : StronglyMeasurable f) {b : Œ≤} :
    SeparableSpace (range f ‚à™ {b} : Set Œ≤) :=
  letI := pseudoMetrizableSpacePseudoMetric Œ≤
  (hf.isSeparable_range.union (finite_singleton _).isSeparable).separableSpace

section SecondCountableStronglyMeasurable

variable {mŒ± : MeasurableSpace Œ±} [MeasurableSpace Œ≤]

/-- In a space with second countable topology, measurable implies strongly measurable. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem _root_.Measurable.stronglyMeasurable [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [SecondCountableTopology Œ≤] [OpensMeasurableSpace Œ≤] (hf : Measurable f) :
    StronglyMeasurable f := by
  letI := pseudoMetrizableSpacePseudoMetric Œ≤
  nontriviality Œ≤; inhabit Œ≤
  exact ‚ü®SimpleFunc.approxOn f hf Set.univ default (Set.mem_univ _), fun x ‚Ü¶
    SimpleFunc.tendsto_approxOn hf (Set.mem_univ _) (by simp)‚ü©

/-- In a space with second countable topology, strongly measurable and measurable are equivalent. -/
theorem _root_.stronglyMeasurable_iff_measurable [TopologicalSpace Œ≤] [MetrizableSpace Œ≤]
    [BorelSpace Œ≤] [SecondCountableTopology Œ≤] : StronglyMeasurable f ‚Üî Measurable f :=
  ‚ü®fun h => h.measurable, fun h => Measurable.stronglyMeasurable h‚ü©

@[fun_prop, measurability]
theorem _root_.stronglyMeasurable_id [TopologicalSpace Œ±] [PseudoMetrizableSpace Œ±]
    [OpensMeasurableSpace Œ±] [SecondCountableTopology Œ±] : StronglyMeasurable (id : Œ± ‚Üí Œ±) :=
  measurable_id.stronglyMeasurable

end SecondCountableStronglyMeasurable

/-- A function is strongly measurable if and only if it is measurable and has separable
range. -/
theorem _root_.stronglyMeasurable_iff_measurable_separable {m : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤] :
    StronglyMeasurable f ‚Üî Measurable f ‚àß IsSeparable (range f) := by
  refine ‚ü®fun H ‚Ü¶ ‚ü®H.measurable, H.isSeparable_range‚ü©, fun ‚ü®Hm, Hsep‚ü©  ‚Ü¶ ?_‚ü©
  have := Hsep.secondCountableTopology
  have Hm' : StronglyMeasurable (rangeFactorization f) := Hm.subtype_mk.stronglyMeasurable
  exact continuous_subtype_val.comp_stronglyMeasurable Hm'

/-- A continuous function is strongly measurable when either the source space or the target space
is second-countable. -/
theorem _root_.Continuous.stronglyMeasurable [MeasurableSpace Œ±] [TopologicalSpace Œ±]
    [OpensMeasurableSpace Œ±] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [h : SecondCountableTopologyEither Œ± Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Continuous f) :
    StronglyMeasurable f := by
  borelize Œ≤
  cases h.out
  ¬∑ rw [stronglyMeasurable_iff_measurable_separable]
    refine ‚ü®hf.measurable, ?_‚ü©
    exact isSeparable_range hf
  ¬∑ exact hf.measurable.stronglyMeasurable

/-- A continuous function whose support is contained in a compact set is strongly measurable. -/
@[to_additive]
theorem _root_.Continuous.stronglyMeasurable_of_mulSupport_subset_isCompact
    [MeasurableSpace Œ±] [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±] [MeasurableSpace Œ≤]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] [One Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Continuous f) {k : Set Œ±} (hk : IsCompact k)
    (h'f : mulSupport f ‚äÜ k) : StronglyMeasurable f := by
  letI : PseudoMetricSpace Œ≤ := pseudoMetrizableSpacePseudoMetric Œ≤
  rw [stronglyMeasurable_iff_measurable_separable]
  exact ‚ü®hf.measurable, (isCompact_range_of_mulSupport_subset_isCompact hf hk h'f).isSeparable‚ü©

/-- A continuous function with compact support is strongly measurable. -/
@[to_additive]
theorem _root_.Continuous.stronglyMeasurable_of_hasCompactMulSupport
    [MeasurableSpace Œ±] [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±] [MeasurableSpace Œ≤]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] [One Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Continuous f) (h'f : HasCompactMulSupport f) : StronglyMeasurable f :=
  hf.stronglyMeasurable_of_mulSupport_subset_isCompact h'f (subset_mulTSupport f)

/-- A continuous function with compact support on a product space is strongly measurable for the
product sigma-algebra. The subtlety is that we do not assume that the spaces are separable, so the
product of the Borel sigma algebras might not contain all open sets, but still it contains enough
of them to approximate compactly supported continuous functions. -/
lemma _root_.HasCompactSupport.stronglyMeasurable_of_prod {X Y : Type*} [Zero Œ±]
    [TopologicalSpace X] [TopologicalSpace Y] [MeasurableSpace X] [MeasurableSpace Y]
    [OpensMeasurableSpace X] [OpensMeasurableSpace Y] [TopologicalSpace Œ±] [PseudoMetrizableSpace Œ±]
    {f : X √ó Y ‚Üí Œ±} (hf : Continuous f) (h'f : HasCompactSupport f) :
    StronglyMeasurable f := by
  borelize Œ±
  apply stronglyMeasurable_iff_measurable_separable.2 ‚ü®h'f.measurable_of_prod hf, ?_‚ü©
  letI : PseudoMetricSpace Œ± := pseudoMetrizableSpacePseudoMetric Œ±
  exact IsCompact.isSeparable (s := range f) (h'f.isCompact_range hf)

/-- If `g` is a topological embedding, then `f` is strongly measurable iff `g ‚àò f` is. -/
theorem _root_.Embedding.comp_stronglyMeasurable_iff {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] [TopologicalSpace Œ≥] [PseudoMetrizableSpace Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤}
    (hg : IsEmbedding g) : (StronglyMeasurable fun x => g (f x)) ‚Üî StronglyMeasurable f := by
  letI := pseudoMetrizableSpacePseudoMetric Œ≥
  borelize Œ≤ Œ≥
  refine
    ‚ü®fun H => stronglyMeasurable_iff_measurable_separable.2 ‚ü®?_, ?_‚ü©, fun H =>
      hg.continuous.comp_stronglyMeasurable H‚ü©
  ¬∑ let G : Œ≤ ‚Üí range g := rangeFactorization g
    have hG : IsClosedEmbedding G :=
      { hg.codRestrict _ _ with
        isClosed_range := by
          rw [rangeFactorization_surjective.range_eq]
          exact isClosed_univ }
    have : Measurable (G ‚àò f) := Measurable.subtype_mk H.measurable
    exact hG.measurableEmbedding.measurable_comp_iff.1 this
  ¬∑ have : IsSeparable (g ‚Åª¬π' range (g ‚àò f)) := hg.isSeparable_preimage H.isSeparable_range
    rwa [range_comp, hg.injective.preimage_image] at this

/-- A sequential limit of strongly measurable functions is strongly measurable. -/
theorem _root_.stronglyMeasurable_of_tendsto {Œπ : Type*} {m : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] (u : Filter Œπ) [NeBot u] [IsCountablyGenerated u]
    {f : Œπ ‚Üí Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≤} (hf : ‚àÄ i, StronglyMeasurable (f i)) (lim : Tendsto f u (ùìù g)) :
    StronglyMeasurable g := by
  borelize Œ≤
  refine stronglyMeasurable_iff_measurable_separable.2 ‚ü®?_, ?_‚ü©
  ¬∑ exact measurable_of_tendsto_metrizable' u (fun i => (hf i).measurable) lim
  ¬∑ rcases u.exists_seq_tendsto with ‚ü®v, hv‚ü©
    have : IsSeparable (closure (‚ãÉ i, range (f (v i)))) :=
      .closure <| .iUnion fun i => (hf (v i)).isSeparable_range
    apply this.mono
    rintro _ ‚ü®x, rfl‚ü©
    rw [tendsto_pi_nhds] at lim
    apply mem_closure_of_tendsto ((lim x).comp hv)
    filter_upwards with n
    apply mem_iUnion_of_mem n
    exact mem_range_self _

protected theorem piecewise {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] {s : Set Œ±}
    {_ : DecidablePred (¬∑ ‚àà s)} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable (Set.piecewise s f g) := by
  refine ‚ü®fun n => SimpleFunc.piecewise s hs (hf.approx n) (hg.approx n), fun x => ?_‚ü©
  by_cases hx : x ‚àà s
  ¬∑ simpa [@Set.piecewise_eq_of_mem _ _ _ _ _ (fun _ => Classical.propDecidable _) _ hx,
      hx] using hf.tendsto_approx x
  ¬∑ simpa [@Set.piecewise_eq_of_notMem _ _ _ _ _ (fun _ => Classical.propDecidable _) _ hx,
      hx] using hg.tendsto_approx x

/-- this is slightly different from `StronglyMeasurable.piecewise`. It can be used to show
`StronglyMeasurable (ite (x=0) 0 1)` by
`exact StronglyMeasurable.ite (measurableSet_singleton 0) stronglyMeasurable_const
stronglyMeasurable_const`, but replacing `StronglyMeasurable.ite` by
`StronglyMeasurable.piecewise` in that example proof does not work. -/
protected theorem ite {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] {p : Œ± ‚Üí Prop}
    {_ : DecidablePred p} (hp : MeasurableSet { a : Œ± | p a }) (hf : StronglyMeasurable f)
    (hg : StronglyMeasurable g) : StronglyMeasurable fun x => ite (p x) (f x) (g x) :=
  StronglyMeasurable.piecewise hp hf hg

@[measurability]
theorem _root_.MeasurableEmbedding.stronglyMeasurable_extend {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {g' : Œ≥ ‚Üí Œ≤}
    {mŒ± : MeasurableSpace Œ±} {mŒ≥ : MeasurableSpace Œ≥} [TopologicalSpace Œ≤]
    (hg : MeasurableEmbedding g) (hf : StronglyMeasurable f) (hg' : StronglyMeasurable g') :
    StronglyMeasurable (Function.extend g f g') := by
  refine ‚ü®fun n => SimpleFunc.extend (hf.approx n) g hg (hg'.approx n), ?_‚ü©
  intro x
  by_cases hx : ‚àÉ y, g y = x
  ¬∑ rcases hx with ‚ü®y, rfl‚ü©
    simpa only [SimpleFunc.extend_apply, hg.injective, Injective.extend_apply] using
      hf.tendsto_approx y
  ¬∑ simpa only [hx, SimpleFunc.extend_apply', not_false_iff, extend_apply'] using
      hg'.tendsto_approx x

theorem _root_.MeasurableEmbedding.exists_stronglyMeasurable_extend {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥}
    {_ : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥} [TopologicalSpace Œ≤]
    (hg : MeasurableEmbedding g) (hf : StronglyMeasurable f) (hne : Œ≥ ‚Üí Nonempty Œ≤) :
    ‚àÉ f' : Œ≥ ‚Üí Œ≤, StronglyMeasurable f' ‚àß f' ‚àò g = f :=
  ‚ü®Function.extend g f fun x => Classical.choice (hne x),
    hg.stronglyMeasurable_extend hf (stronglyMeasurable_const' fun _ _ => rfl),
    funext fun _ => hg.injective.extend_apply _ _ _‚ü©

theorem _root_.stronglyMeasurable_of_stronglyMeasurable_union_cover {m : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} (s t : Set Œ±) (hs : MeasurableSet s) (ht : MeasurableSet t)
    (h : univ ‚äÜ s ‚à™ t) (hc : StronglyMeasurable fun a : s => f a)
    (hd : StronglyMeasurable fun a : t => f a) : StronglyMeasurable f := by
  nontriviality Œ≤; inhabit Œ≤
  suffices Function.extend Subtype.val (fun x : s ‚Ü¶ f x)
      (Function.extend (‚Üë) (fun x : t ‚Ü¶ f x) fun _ ‚Ü¶ default) = f from
    this ‚ñ∏ (MeasurableEmbedding.subtype_coe hs).stronglyMeasurable_extend hc <|
      (MeasurableEmbedding.subtype_coe ht).stronglyMeasurable_extend hd stronglyMeasurable_const
  ext x
  by_cases hxs : x ‚àà s
  ¬∑ lift x to s using hxs
    simp
  ¬∑ lift x to t using (h trivial).resolve_left hxs
    rw [extend_apply', Subtype.coe_injective.extend_apply]
    exact fun ‚ü®y, hy‚ü© ‚Ü¶ hxs <| hy ‚ñ∏ y.2

theorem _root_.stronglyMeasurable_of_restrict_of_restrict_compl {_ : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} (hs : MeasurableSet s)
    (h‚ÇÅ : StronglyMeasurable (s.restrict f)) (h‚ÇÇ : StronglyMeasurable (s·∂ú.restrict f)) :
    StronglyMeasurable f :=
  stronglyMeasurable_of_stronglyMeasurable_union_cover s s·∂ú hs hs.compl (union_compl_self s).ge h‚ÇÅ
    h‚ÇÇ

@[measurability]
protected theorem indicator {_ : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [Zero Œ≤]
    (hf : StronglyMeasurable f) {s : Set Œ±} (hs : MeasurableSet s) :
    StronglyMeasurable (s.indicator f) :=
  hf.piecewise hs stronglyMeasurable_const

/-- To prove that a property holds for any strongly measurable function, it is enough to show
that it holds for constant indicator functions of measurable sets and that it is closed under
addition and pointwise limit.

To use in an induction proof, the syntax is
`induction f, hf using StronglyMeasurable.induction with`. -/
theorem induction [MeasurableSpace Œ±] [AddZeroClass Œ≤] [TopologicalSpace Œ≤]
    {P : (f : Œ± ‚Üí Œ≤) ‚Üí StronglyMeasurable f ‚Üí Prop}
    (ind : ‚àÄ c ‚¶És : Set Œ±‚¶Ñ (hs : MeasurableSet s),
      P (s.indicator fun _ ‚Ü¶ c) (stronglyMeasurable_const.indicator hs))
    (add : ‚àÄ ‚¶Éf g : Œ± ‚Üí Œ≤‚¶Ñ (hf : StronglyMeasurable f) (hg : StronglyMeasurable g)
      (hfg : StronglyMeasurable (f + g)), Disjoint f.support g.support ‚Üí
      P f hf ‚Üí P g hg ‚Üí P (f + g) hfg)
    (lim : ‚àÄ ‚¶Éf : ‚Ñï ‚Üí Œ± ‚Üí Œ≤‚¶Ñ ‚¶Ég : Œ± ‚Üí Œ≤‚¶Ñ (hf : ‚àÄ n, StronglyMeasurable (f n))
      (hg : StronglyMeasurable g), (‚àÄ n, P (f n) (hf n)) ‚Üí
      (‚àÄ x, Tendsto (f ¬∑ x) atTop (ùìù (g x))) ‚Üí P g hg)
    (f : Œ± ‚Üí Œ≤) (hf : StronglyMeasurable f) : P f hf := by
  let s := hf.approx
  refine lim (fun n ‚Ü¶ (s n).stronglyMeasurable) hf (fun n ‚Ü¶ ?_) hf.tendsto_approx
  change P (s n) (s n).stronglyMeasurable
  induction s n using SimpleFunc.induction with
  | const c hs => exact ind c hs
  | @add f g h_supp hf hg =>
    exact add f.stronglyMeasurable g.stronglyMeasurable (f + g).stronglyMeasurable h_supp hf hg

open scoped Classical in
/-- To prove that a property holds for any strongly measurable function, it is enough to show
that it holds for constant functions and that it is closed under piecewise combination of functions
and pointwise limits.

To use in an induction proof, the syntax is
`induction f, hf using StronglyMeasurable.induction' with`. -/
theorem induction' [MeasurableSpace Œ±] [Nonempty Œ≤] [TopologicalSpace Œ≤]
    {P : (f : Œ± ‚Üí Œ≤) ‚Üí StronglyMeasurable f ‚Üí Prop}
    (const : ‚àÄ (c), P (fun _ ‚Ü¶ c) stronglyMeasurable_const)
    (pcw : ‚àÄ ‚¶Éf g : Œ± ‚Üí Œ≤‚¶Ñ {s} (hf : StronglyMeasurable f) (hg : StronglyMeasurable g)
      (hs : MeasurableSet s), P f hf ‚Üí P g hg ‚Üí P (s.piecewise f g) (hf.piecewise hs hg))
    (lim : ‚àÄ ‚¶Éf : ‚Ñï ‚Üí Œ± ‚Üí Œ≤‚¶Ñ ‚¶Ég : Œ± ‚Üí Œ≤‚¶Ñ (hf : ‚àÄ n, StronglyMeasurable (f n))
      (hg : StronglyMeasurable g), (‚àÄ n, P (f n) (hf n)) ‚Üí
      (‚àÄ x, Tendsto (f ¬∑ x) atTop (ùìù (g x))) ‚Üí P g hg)
    (f : Œ± ‚Üí Œ≤) (hf : StronglyMeasurable f) : P f hf := by
  let s := hf.approx
  refine lim (fun n ‚Ü¶ (s n).stronglyMeasurable) hf (fun n ‚Ü¶ ?_) hf.tendsto_approx
  change P (s n) (s n).stronglyMeasurable
  induction s n with
  | const c => exact const c
  | @pcw f g s hs Pf Pg =>
    simp_rw [SimpleFunc.coe_piecewise]
    exact pcw f.stronglyMeasurable g.stronglyMeasurable hs Pf Pg

@[fun_prop, aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem dist {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [PseudoMetricSpace Œ≤] {f g : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => dist (f x) (g x) :=
  continuous_dist.comp_stronglyMeasurable (hf.prodMk hg)

@[fun_prop, aesop safe 20 apply (rule_sets := [Measurable])]
protected theorem edist {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [PseudoEMetricSpace Œ≤] {f g : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    StronglyMeasurable fun x => edist (f x) (g x) :=
  continuous_edist.comp_stronglyMeasurable (hf.prodMk hg)

@[fun_prop, measurability]
protected theorem norm {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x => ‚Äñf x‚Äñ :=
  continuous_norm.comp_stronglyMeasurable hf

@[fun_prop, measurability]
protected theorem nnnorm {_ : MeasurableSpace Œ±} {Œ≤ : Type*} [SeminormedAddCommGroup Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x => ‚Äñf x‚Äñ‚Çä :=
  continuous_nnnorm.comp_stronglyMeasurable hf

/-- The `enorm` of a strongly measurable function is measurable.

Unlike `StrongMeasurable.norm` and `StronglyMeasurable.nnnorm`, this lemma proves measurability,
**not** strong measurability. This is an intentional decision: for functions taking values in
‚Ñù‚â•0‚àû, measurability is much more useful than strong measurability. -/
@[fun_prop, measurability]
protected theorem enorm {_ : MeasurableSpace Œ±} {Œµ : Type*} [TopologicalSpace Œµ] [ContinuousENorm Œµ]
    {f : Œ± ‚Üí Œµ} (hf : StronglyMeasurable f) : Measurable (‚Äñf ¬∑‚Äñ‚Çë) :=
  (continuous_enorm.comp_stronglyMeasurable hf).measurable

@[fun_prop, measurability]
protected theorem real_toNNReal {_ : MeasurableSpace Œ±} {f : Œ± ‚Üí ‚Ñù} (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x => (f x).toNNReal :=
  continuous_real_toNNReal.comp_stronglyMeasurable hf

section PseudoMetrizableSpace
variable {E : Type*} {m m‚ÇÄ : MeasurableSpace Œ±} {Œº : Measure[m‚ÇÄ] Œ±} {f g : Œ± ‚Üí E}
  [TopologicalSpace E] [Preorder E] [OrderClosedTopology E] [PseudoMetrizableSpace E]

lemma measurableSet_le (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) :
    MeasurableSet[m] {a | f a ‚â§ g a} := by
  borelize (E √ó E)
  exact (hf.prodMk hg).measurable isClosed_le_prod.measurableSet

lemma measurableSet_lt (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) :
    MeasurableSet[m] {a | f a < g a} := by
  simpa only [lt_iff_le_not_ge] using (hf.measurableSet_le hg).inter (hg.measurableSet_le hf).compl

lemma ae_le_trim_of_stronglyMeasurable (hm : m ‚â§ m‚ÇÄ) (hf : StronglyMeasurable[m] f)
    (hg : StronglyMeasurable[m] g) (hfg : f ‚â§·µê[Œº] g) : f ‚â§·µê[Œº.trim hm] g := by
  rwa [EventuallyLE, ae_iff, trim_measurableSet_eq hm]
  exact (hf.measurableSet_le hg).compl

lemma ae_le_trim_iff (hm : m ‚â§ m‚ÇÄ) (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) :
    f ‚â§·µê[Œº.trim hm] g ‚Üî f ‚â§·µê[Œº] g :=
  ‚ü®ae_le_of_ae_le_trim, ae_le_trim_of_stronglyMeasurable hm hf hg‚ü©

end PseudoMetrizableSpace

section MetrizableSpace
variable {E : Type*} {m m‚ÇÄ : MeasurableSpace Œ±} {Œº : Measure[m‚ÇÄ] Œ±} {f g : Œ± ‚Üí E}
  [TopologicalSpace E] [MetrizableSpace E]

lemma measurableSet_eq_fun (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) :
    MeasurableSet[m] {a | f a = g a} := by
  borelize (E √ó E)
  exact (hf.prodMk hg).measurable isClosed_diagonal.measurableSet

lemma ae_eq_trim_of_stronglyMeasurable (hm : m ‚â§ m‚ÇÄ) (hf : StronglyMeasurable[m] f)
    (hg : StronglyMeasurable[m] g) (hfg : f =·µê[Œº] g) : f =·µê[Œº.trim hm] g := by
  rwa [EventuallyEq, ae_iff, trim_measurableSet_eq hm]
  exact (hf.measurableSet_eq_fun hg).compl

lemma ae_eq_trim_iff (hm : m ‚â§ m‚ÇÄ) (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) :
    f =·µê[Œº.trim hm] g ‚Üî f =·µê[Œº] g :=
  ‚ü®ae_eq_of_ae_eq_trim, ae_eq_trim_of_stronglyMeasurable hm hf hg‚ü©

end MetrizableSpace

theorem stronglyMeasurable_in_set {m : MeasurableSpace Œ±} [TopologicalSpace Œ≤] [Zero Œ≤] {s : Set Œ±}
    {f : Œ± ‚Üí Œ≤} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hf_zero : ‚àÄ x, x ‚àâ s ‚Üí f x = 0) :
    ‚àÉ fs : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤,
      (‚àÄ x, Tendsto (fun n => fs n x) atTop (ùìù (f x))) ‚àß ‚àÄ x ‚àâ s, ‚àÄ n, fs n x = 0 := by
  let g_seq_s : ‚Ñï ‚Üí @SimpleFunc Œ± m Œ≤ := fun n => (hf.approx n).restrict s
  have hg_eq : ‚àÄ x ‚àà s, ‚àÄ n, g_seq_s n x = hf.approx n x := by
    intro x hx n
    rw [SimpleFunc.coe_restrict _ hs, Set.indicator_of_mem hx]
  have hg_zero : ‚àÄ x ‚àâ s, ‚àÄ n, g_seq_s n x = 0 := by
    intro x hx n
    rw [SimpleFunc.coe_restrict _ hs, Set.indicator_of_notMem hx]
  refine ‚ü®g_seq_s, fun x => ?_, hg_zero‚ü©
  by_cases hx : x ‚àà s
  ¬∑ simp_rw [hg_eq x hx]
    exact hf.tendsto_approx x
  ¬∑ simp_rw [hg_zero x hx, hf_zero x hx]
    exact tendsto_const_nhds

/-- If the restriction to a set `s` of a œÉ-algebra `m` is included in the restriction to `s` of
another œÉ-algebra `m‚ÇÇ` (hypothesis `hs`), the set `s` is `m` measurable and a function `f` supported
on `s` is `m`-strongly-measurable, then `f` is also `m‚ÇÇ`-strongly-measurable. -/
theorem stronglyMeasurable_of_measurableSpace_le_on {Œ± E} {m m‚ÇÇ : MeasurableSpace Œ±}
    [TopologicalSpace E] [Zero E] {s : Set Œ±} {f : Œ± ‚Üí E} (hs_m : MeasurableSet[m] s)
    (hs : ‚àÄ t, MeasurableSet[m] (s ‚à© t) ‚Üí MeasurableSet[m‚ÇÇ] (s ‚à© t))
    (hf : StronglyMeasurable[m] f) (hf_zero : ‚àÄ x ‚àâ s, f x = 0) :
    StronglyMeasurable[m‚ÇÇ] f := by
  have hs_m‚ÇÇ : MeasurableSet[m‚ÇÇ] s := by
    rw [‚Üê Set.inter_univ s]
    refine hs Set.univ ?_
    rwa [Set.inter_univ]
  obtain ‚ü®g_seq_s, hg_seq_tendsto, hg_seq_zero‚ü© := stronglyMeasurable_in_set hs_m hf hf_zero
  let g_seq_s‚ÇÇ : ‚Ñï ‚Üí @SimpleFunc Œ± m‚ÇÇ E := fun n =>
    { toFun := g_seq_s n
      measurableSet_fiber' := fun x => by
        rw [‚Üê Set.inter_univ (g_seq_s n ‚Åª¬π' {x}), ‚Üê Set.union_compl_self s,
          Set.inter_union_distrib_left, Set.inter_comm (g_seq_s n ‚Åª¬π' {x})]
        refine MeasurableSet.union (hs _ (hs_m.inter ?_)) ?_
        ¬∑ exact @SimpleFunc.measurableSet_fiber _ _ m _ _
        by_cases hx : x = 0
        ¬∑ suffices g_seq_s n ‚Åª¬π' {x} ‚à© s·∂ú = s·∂ú by
            rw [this]
            exact hs_m‚ÇÇ.compl
          ext1 y
          rw [hx, Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff]
          exact ‚ü®fun h => h.2, fun h => ‚ü®hg_seq_zero y h n, h‚ü©‚ü©
        ¬∑ suffices g_seq_s n ‚Åª¬π' {x} ‚à© s·∂ú = ‚àÖ by
            rw [this]
            exact MeasurableSet.empty
          ext1 y
          simp only [mem_inter_iff, mem_preimage, mem_singleton_iff, mem_compl_iff,
            mem_empty_iff_false, iff_false, not_and, not_notMem]
          refine Function.mtr fun hys => ?_
          rw [hg_seq_zero y hys n]
          exact Ne.symm hx
      finite_range' := @SimpleFunc.finite_range _ _ m (g_seq_s n) }
  exact ‚ü®g_seq_s‚ÇÇ, hg_seq_tendsto‚ü©

/-- If a function `f` is strongly measurable w.r.t. a sub-œÉ-algebra `m` and the measure is œÉ-finite
on `m`, then there exists spanning measurable sets with finite measure on which `f` has bounded
norm. In particular, `f` is integrable on each of those sets. -/
theorem exists_spanning_measurableSet_norm_le [SeminormedAddCommGroup Œ≤] {m m0 : MeasurableSpace Œ±}
    (hm : m ‚â§ m0) (hf : StronglyMeasurable[m] f) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)] :
    ‚àÉ s : ‚Ñï ‚Üí Set Œ±,
      (‚àÄ n, MeasurableSet[m] (s n) ‚àß Œº (s n) < ‚àû ‚àß ‚àÄ x ‚àà s n, ‚Äñf x‚Äñ ‚â§ n) ‚àß
      ‚ãÉ i, s i = Set.univ := by
  obtain ‚ü®s, hs, hs_univ‚ü© :=
    @exists_spanning_measurableSet_le _ m _ hf.nnnorm.measurable (Œº.trim hm) _
  refine ‚ü®s, fun n ‚Ü¶ ‚ü®(hs n).1, (le_trim hm).trans_lt (hs n).2.1, fun x hx ‚Ü¶ ?_‚ü©, hs_univ‚ü©
  have hx_nnnorm : ‚Äñf x‚Äñ‚Çä ‚â§ n := (hs n).2.2 x hx
  rw [‚Üê coe_nnnorm]
  norm_cast

end StronglyMeasurable

/-! ## Finitely strongly measurable functions -/


theorem finStronglyMeasurable_zero {Œ± Œ≤} {m : MeasurableSpace Œ±} {Œº : Measure Œ±} [Zero Œ≤]
    [TopologicalSpace Œ≤] : FinStronglyMeasurable (0 : Œ± ‚Üí Œ≤) Œº :=
  ‚ü®0, by
    simp only [Pi.zero_apply, SimpleFunc.coe_zero, support_zero, measure_empty,
      zero_lt_top, forall_const],
    fun _ => tendsto_const_nhds‚ü©

namespace FinStronglyMeasurable

variable {m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {f g : Œ± ‚Üí Œ≤}

section sequence

variable [Zero Œ≤] [TopologicalSpace Œ≤] (hf : FinStronglyMeasurable f Œº)

/-- A sequence of simple functions such that `‚àÄ x, Tendsto (fun n ‚Ü¶ hf.approx n x) atTop (ùìù (f x))`
and `‚àÄ n, Œº (support (hf.approx n)) < ‚àû`. These properties are given by
`FinStronglyMeasurable.tendsto_approx` and `FinStronglyMeasurable.fin_support_approx`. -/
protected noncomputable def approx : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ Œ≤ :=
  hf.choose

protected theorem fin_support_approx : ‚àÄ n, Œº (support (hf.approx n)) < ‚àû :=
  hf.choose_spec.1

protected theorem tendsto_approx : ‚àÄ x, Tendsto (fun n => hf.approx n x) atTop (ùìù (f x)) :=
  hf.choose_spec.2

end sequence

/-- A finitely strongly measurable function is strongly measurable. -/
@[aesop 5% apply (rule_sets := [Measurable])]
protected theorem stronglyMeasurable [Zero Œ≤] [TopologicalSpace Œ≤]
    (hf : FinStronglyMeasurable f Œº) : StronglyMeasurable f :=
  ‚ü®hf.approx, hf.tendsto_approx‚ü©

theorem exists_set_sigmaFinite [Zero Œ≤] [TopologicalSpace Œ≤] [T2Space Œ≤]
    (hf : FinStronglyMeasurable f Œº) :
    ‚àÉ t, MeasurableSet t ‚àß (‚àÄ x ‚àà t·∂ú, f x = 0) ‚àß SigmaFinite (Œº.restrict t) := by
  rcases hf with ‚ü®fs, hT_lt_top, h_approx‚ü©
  let T n := support (fs n)
  have hT_meas : ‚àÄ n, MeasurableSet (T n) := fun n => SimpleFunc.measurableSet_support (fs n)
  let t := ‚ãÉ n, T n
  refine ‚ü®t, MeasurableSet.iUnion hT_meas, ?_, ?_‚ü©
  ¬∑ have h_fs_zero : ‚àÄ n, ‚àÄ x ‚àà t·∂ú, fs n x = 0 := by
      intro n x hxt
      rw [Set.mem_compl_iff, Set.mem_iUnion, not_exists] at hxt
      simpa [T] using hxt n
    refine fun x hxt => tendsto_nhds_unique (h_approx x) ?_
    rw [funext fun n => h_fs_zero n x hxt]
    exact tendsto_const_nhds
  ¬∑ refine ‚ü®‚ü®‚ü®fun n => t·∂ú ‚à™ T n, fun _ => trivial, fun n => ?_, ?_‚ü©‚ü©‚ü©
    ¬∑ rw [Measure.restrict_apply' (MeasurableSet.iUnion hT_meas), Set.union_inter_distrib_right,
        Set.compl_inter_self t, Set.empty_union]
      exact (measure_mono Set.inter_subset_left).trans_lt (hT_lt_top n)
    ¬∑ rw [‚Üê Set.union_iUnion t·∂ú T]
      exact Set.compl_union_self _

/-- A finitely strongly measurable function is measurable. -/
protected theorem measurable [Zero Œ≤] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [MeasurableSpace Œ≤] [BorelSpace Œ≤] (hf : FinStronglyMeasurable f Œº) : Measurable f :=
  hf.stronglyMeasurable.measurable

section Arithmetic

variable [TopologicalSpace Œ≤]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem mul [MulZeroClass Œ≤] [ContinuousMul Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f * g) Œº := by
  refine
    ‚ü®fun n => hf.approx n * hg.approx n, ?_, fun x =>
      (hf.tendsto_approx x).mul (hg.tendsto_approx x)‚ü©
  intro n
  exact (measure_mono (support_mul_subset_left _ _)).trans_lt (hf.fin_support_approx n)

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem add [AddZeroClass Œ≤] [ContinuousAdd Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f + g) Œº :=
  ‚ü®fun n => hf.approx n + hg.approx n, fun n =>
    (measure_mono (Function.support_add _ _)).trans_lt
      ((measure_union_le _ _).trans_lt
        (ENNReal.add_lt_top.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©)),
    fun x => (hf.tendsto_approx x).add (hg.tendsto_approx x)‚ü©

@[measurability]
protected theorem neg [SubtractionMonoid Œ≤] [ContinuousNeg Œ≤] (hf : FinStronglyMeasurable f Œº) :
    FinStronglyMeasurable (-f) Œº := by
  refine ‚ü®fun n ‚Ü¶ -hf.approx n, fun n ‚Ü¶ ?_, fun x ‚Ü¶ (hf.tendsto_approx x).neg‚ü©
  suffices Œº (Function.support fun x ‚Ü¶ -(hf.approx n) x) < ‚àû by convert this
  rw [Function.support_fun_neg (hf.approx n)]
  exact hf.fin_support_approx n

@[measurability]
protected theorem sub [SubtractionMonoid Œ≤] [ContinuousSub Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f - g) Œº :=
  ‚ü®fun n => hf.approx n - hg.approx n, fun n =>
    (measure_mono (Function.support_sub _ _)).trans_lt
      ((measure_union_le _ _).trans_lt
        (ENNReal.add_lt_top.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©)),
    fun x => (hf.tendsto_approx x).sub (hg.tendsto_approx x)‚ü©

@[measurability]
protected theorem const_smul {ùïú} [TopologicalSpace ùïú] [Zero Œ≤]
    [SMulZeroClass ùïú Œ≤] [ContinuousSMul ùïú Œ≤] (hf : FinStronglyMeasurable f Œº) (c : ùïú) :
    FinStronglyMeasurable (c ‚Ä¢ f) Œº := by
  refine ‚ü®fun n => c ‚Ä¢ hf.approx n, fun n => ?_, fun x => (hf.tendsto_approx x).const_smul c‚ü©
  rw [SimpleFunc.coe_smul]
  exact (measure_mono (support_const_smul_subset c _)).trans_lt (hf.fin_support_approx n)

end Arithmetic

section Order

variable [TopologicalSpace Œ≤] [Zero Œ≤]

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem sup [SemilatticeSup Œ≤] [ContinuousSup Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f ‚äî g) Œº := by
  refine
    ‚ü®fun n => hf.approx n ‚äî hg.approx n, fun n => ?_, fun x =>
      (hf.tendsto_approx x).sup_nhds (hg.tendsto_approx x)‚ü©
  refine (measure_mono (support_sup _ _)).trans_lt ?_
  exact measure_union_lt_top_iff.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©

@[aesop safe 20 (rule_sets := [Measurable])]
protected theorem inf [SemilatticeInf Œ≤] [ContinuousInf Œ≤] (hf : FinStronglyMeasurable f Œº)
    (hg : FinStronglyMeasurable g Œº) : FinStronglyMeasurable (f ‚äì g) Œº := by
  refine
    ‚ü®fun n => hf.approx n ‚äì hg.approx n, fun n => ?_, fun x =>
      (hf.tendsto_approx x).inf_nhds (hg.tendsto_approx x)‚ü©
  refine (measure_mono (support_inf _ _)).trans_lt ?_
  exact measure_union_lt_top_iff.mpr ‚ü®hf.fin_support_approx n, hg.fin_support_approx n‚ü©

end Order

end FinStronglyMeasurable

theorem finStronglyMeasurable_iff_stronglyMeasurable_and_exists_set_sigmaFinite {Œ± Œ≤} {f : Œ± ‚Üí Œ≤}
    [TopologicalSpace Œ≤] [T2Space Œ≤] [Zero Œ≤] {_ : MeasurableSpace Œ±} {Œº : Measure Œ±} :
    FinStronglyMeasurable f Œº ‚Üî
      StronglyMeasurable f ‚àß
        ‚àÉ t, MeasurableSet t ‚àß (‚àÄ x ‚àà t·∂ú, f x = 0) ‚àß SigmaFinite (Œº.restrict t) :=
  ‚ü®fun hf => ‚ü®hf.stronglyMeasurable, hf.exists_set_sigmaFinite‚ü©, fun hf =>
    hf.1.finStronglyMeasurable_of_set_sigmaFinite hf.2.choose_spec.1 hf.2.choose_spec.2.1
      hf.2.choose_spec.2.2‚ü©

section SecondCountableTopology

variable {G : Type*} [SeminormedAddCommGroup G] [MeasurableSpace G] [BorelSpace G]
  [SecondCountableTopology G] {f : Œ± ‚Üí G}

/-- In a space with second countable topology and a sigma-finite measure, `FinStronglyMeasurable`
  and `Measurable` are equivalent. -/
theorem finStronglyMeasurable_iff_measurable {_m0 : MeasurableSpace Œ±} (Œº : Measure Œ±)
    [SigmaFinite Œº] : FinStronglyMeasurable f Œº ‚Üî Measurable f :=
  ‚ü®fun h => h.measurable, fun h => (Measurable.stronglyMeasurable h).finStronglyMeasurable Œº‚ü©

/-- In a space with second countable topology and a sigma-finite measure, a measurable function
is `FinStronglyMeasurable`. -/
@[aesop 90% apply (rule_sets := [Measurable])]
theorem finStronglyMeasurable_of_measurable {_m0 : MeasurableSpace Œ±} (Œº : Measure Œ±)
    [SigmaFinite Œº] (hf : Measurable f) : FinStronglyMeasurable f Œº :=
  (finStronglyMeasurable_iff_measurable Œº).mpr hf

end SecondCountableTopology

theorem measurable_uncurry_of_continuous_of_measurable {Œ± Œ≤ Œπ : Type*} [TopologicalSpace Œπ]
    [MetrizableSpace Œπ] [MeasurableSpace Œπ] [SecondCountableTopology Œπ] [OpensMeasurableSpace Œπ]
    {mŒ≤ : MeasurableSpace Œ≤} [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤]
    {m : MeasurableSpace Œ±} {u : Œπ ‚Üí Œ± ‚Üí Œ≤} (hu_cont : ‚àÄ x, Continuous fun i => u i x)
    (h : ‚àÄ i, Measurable (u i)) : Measurable (Function.uncurry u) := by
  obtain ‚ü®t_sf, ht_sf‚ü© :
    ‚àÉ t : ‚Ñï ‚Üí SimpleFunc Œπ Œπ, ‚àÄ j x, Tendsto (fun n => u (t n j) x) atTop (ùìù <| u j x) := by
    have h_str_meas : StronglyMeasurable (id : Œπ ‚Üí Œπ) := stronglyMeasurable_id
    refine ‚ü®h_str_meas.approx, fun j x => ?_‚ü©
    exact ((hu_cont x).tendsto j).comp (h_str_meas.tendsto_approx j)
  let U (n : ‚Ñï) (p : Œπ √ó Œ±) := u (t_sf n p.fst) p.snd
  have h_tendsto : Tendsto U atTop (ùìù fun p => u p.fst p.snd) := by
    rw [tendsto_pi_nhds]
    exact fun p => ht_sf p.fst p.snd
  refine measurable_of_tendsto_metrizable (fun n => ?_) h_tendsto
  have h_meas : Measurable fun p : (t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd := by
    have :
      (fun p : ‚Ü•(t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd) =
        (fun p : Œ± √ó (t_sf n).range => u (‚Üëp.snd) p.fst) ‚àò Prod.swap :=
      rfl
    rw [this, @measurable_swap_iff Œ± (‚Ü•(t_sf n).range) Œ≤ m]
    exact measurable_from_prod_countable_left fun j => h j
  have :
    (fun p : Œπ √ó Œ± => u (t_sf n p.fst) p.snd) =
      (fun p : ‚Ü•(t_sf n).range √ó Œ± => u p.fst p.snd) ‚àò fun p : Œπ √ó Œ± =>
        (‚ü®t_sf n p.fst, SimpleFunc.mem_range_self _ _‚ü©, p.snd) :=
    rfl
  simp_rw [U, this]
  refine h_meas.comp (Measurable.prodMk ?_ measurable_snd)
  exact ((t_sf n).measurable.comp measurable_fst).subtype_mk

theorem stronglyMeasurable_uncurry_of_continuous_of_stronglyMeasurable {Œ± Œ≤ Œπ : Type*}
    [TopologicalSpace Œπ] [MetrizableSpace Œπ] [MeasurableSpace Œπ] [SecondCountableTopology Œπ]
    [OpensMeasurableSpace Œπ] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ±]
    {u : Œπ ‚Üí Œ± ‚Üí Œ≤} (hu_cont : ‚àÄ x, Continuous fun i => u i x) (h : ‚àÄ i, StronglyMeasurable (u i)) :
    StronglyMeasurable (Function.uncurry u) := by
  borelize Œ≤
  obtain ‚ü®t_sf, ht_sf‚ü© :
    ‚àÉ t : ‚Ñï ‚Üí SimpleFunc Œπ Œπ, ‚àÄ j x, Tendsto (fun n => u (t n j) x) atTop (ùìù <| u j x) := by
    have h_str_meas : StronglyMeasurable (id : Œπ ‚Üí Œπ) := stronglyMeasurable_id
    refine ‚ü®h_str_meas.approx, fun j x => ?_‚ü©
    exact ((hu_cont x).tendsto j).comp (h_str_meas.tendsto_approx j)
  let U (n : ‚Ñï) (p : Œπ √ó Œ±) := u (t_sf n p.fst) p.snd
  have h_tendsto : Tendsto U atTop (ùìù fun p => u p.fst p.snd) := by
    rw [tendsto_pi_nhds]
    exact fun p => ht_sf p.fst p.snd
  refine stronglyMeasurable_of_tendsto _ (fun n => ?_) h_tendsto
  have h_str_meas : StronglyMeasurable fun p : (t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd := by
    refine stronglyMeasurable_iff_measurable_separable.2 ‚ü®?_, ?_‚ü©
    ¬∑ have :
        (fun p : ‚Ü•(t_sf n).range √ó Œ± => u (‚Üëp.fst) p.snd) =
          (fun p : Œ± √ó (t_sf n).range => u (‚Üëp.snd) p.fst) ‚àò Prod.swap :=
        rfl
      rw [this, measurable_swap_iff]
      exact measurable_from_prod_countable_left fun j => (h j).measurable
    ¬∑ have : IsSeparable (‚ãÉ i : (t_sf n).range, range (u i)) :=
        .iUnion fun i => (h i).isSeparable_range
      apply this.mono
      rintro _ ‚ü®‚ü®i, x‚ü©, rfl‚ü©
      simp only [mem_iUnion, mem_range]
      exact ‚ü®i, x, rfl‚ü©
  have :
    (fun p : Œπ √ó Œ± => u (t_sf n p.fst) p.snd) =
      (fun p : ‚Ü•(t_sf n).range √ó Œ± => u p.fst p.snd) ‚àò fun p : Œπ √ó Œ± =>
        (‚ü®t_sf n p.fst, SimpleFunc.mem_range_self _ _‚ü©, p.snd) :=
    rfl
  simp_rw [U, this]
  refine h_str_meas.comp_measurable (Measurable.prodMk ?_ measurable_snd)
  exact ((t_sf n).measurable.comp measurable_fst).subtype_mk

end MeasureTheory
