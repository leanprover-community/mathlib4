/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Normed.Operator.BoundedLinearMaps
import Mathlib.Dynamics.Ergodic.MeasurePreserving
import Mathlib.MeasureTheory.Function.StronglyMeasurable.AEStronglyMeasurable
import Mathlib.MeasureTheory.Measure.WithDensity
import Mathlib.Topology.Algebra.Module.FiniteDimension

/-!
# Strongly measurable and finitely strongly measurable functions

This file contains some further development of strongly measurable and finitely strongly measurable
functions, started in `Mathlib/MeasureTheory/Function/StronglyMeasurable/Basic.lean`.

## References

* [HytÃ¶nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.][Hytonen_VanNeerven_Veraar_Wies_2016]

-/

open MeasureTheory Filter Set ENNReal NNReal

variable {Î± Î² Î³ : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [TopologicalSpace Î²]
  [TopologicalSpace Î³] {f g : Î± â†’ Î²}

@[fun_prop]
lemma aestronglyMeasurable_dirac [MeasurableSingletonClass Î±] {a : Î±} {f : Î± â†’ Î²} :
    AEStronglyMeasurable f (Measure.dirac a) :=
  âŸ¨fun _ â†¦ f a, stronglyMeasurable_const, ae_eq_dirac fâŸ©

theorem MeasureTheory.AEStronglyMeasurable.comp_measurePreserving
    {Î³ : Type*} {_ : MeasurableSpace Î³} {_ : MeasurableSpace Î±} {f : Î³ â†’ Î±} {Î¼ : Measure Î³}
    {Î½ : Measure Î±} (hg : AEStronglyMeasurable g Î½) (hf : MeasurePreserving f Î¼ Î½) :
    AEStronglyMeasurable (g âˆ˜ f) Î¼ :=
  hg.comp_quasiMeasurePreserving hf.quasiMeasurePreserving

theorem MeasureTheory.MeasurePreserving.aestronglyMeasurable_comp_iff {Î² : Type*}
    {f : Î± â†’ Î²} {mÎ± : MeasurableSpace Î±} {Î¼a : Measure Î±} {mÎ² : MeasurableSpace Î²} {Î¼b : Measure Î²}
    (hf : MeasurePreserving f Î¼a Î¼b) (hâ‚‚ : MeasurableEmbedding f) {g : Î² â†’ Î³} :
    AEStronglyMeasurable (g âˆ˜ f) Î¼a â†” AEStronglyMeasurable g Î¼b := by
  rw [â† hf.map_eq, hâ‚‚.aestronglyMeasurable_map_iff]

section NormedSpace

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem aestronglyMeasurable_smul_const_iff {f : Î± â†’ ğ•œ} {c : E} (hc : c â‰  0) :
    AEStronglyMeasurable (fun x â†¦ f x â€¢ c) Î¼ â†” AEStronglyMeasurable f Î¼ :=
  (isClosedEmbedding_smul_left hc).isEmbedding.aestronglyMeasurable_comp_iff

end NormedSpace

section ContinuousLinearMapNontriviallyNormedField

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]

theorem StronglyMeasurable.apply_continuousLinearMap
    {_m : MeasurableSpace Î±} {Ï† : Î± â†’ F â†’L[ğ•œ] E} (hÏ† : StronglyMeasurable Ï†) (v : F) :
    StronglyMeasurable fun a â†¦ Ï† a v :=
  (ContinuousLinearMap.apply ğ•œ E v).continuous.comp_stronglyMeasurable hÏ†

@[measurability]
theorem MeasureTheory.AEStronglyMeasurable.apply_continuousLinearMap {Ï† : Î± â†’ F â†’L[ğ•œ] E}
    (hÏ† : AEStronglyMeasurable Ï† Î¼) (v : F) :
    AEStronglyMeasurable (fun a â†¦ Ï† a v) Î¼ :=
  (ContinuousLinearMap.apply ğ•œ E v).continuous.comp_aestronglyMeasurable hÏ†

theorem ContinuousLinearMap.aestronglyMeasurable_compâ‚‚ (L : E â†’L[ğ•œ] F â†’L[ğ•œ] G) {f : Î± â†’ E}
    {g : Î± â†’ F} (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    AEStronglyMeasurable (fun x â†¦ L (f x) (g x)) Î¼ :=
  L.continuousâ‚‚.comp_aestronglyMeasurableâ‚‚ hf hg

end ContinuousLinearMapNontriviallyNormedField

theorem aestronglyMeasurable_withDensity_iff {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {f : Î± â†’ â„â‰¥0} (hf : Measurable f) {g : Î± â†’ E} :
    AEStronglyMeasurable g (Î¼.withDensity fun x â†¦ (f x : â„â‰¥0âˆ)) â†”
      AEStronglyMeasurable (fun x â†¦ (f x : â„) â€¢ g x) Î¼ := by
  constructor
  Â· rintro âŸ¨g', g'meas, hg'âŸ©
    have A : MeasurableSet { x : Î± | f x â‰  0 } := (hf (measurableSet_singleton 0)).compl
    refine âŸ¨fun x â†¦ (f x : â„) â€¢ g' x, hf.coe_nnreal_real.stronglyMeasurable.smul g'meas, ?_âŸ©
    apply @ae_of_ae_restrict_of_ae_restrict_compl _ _ _ { x | f x â‰  0 }
    Â· rw [EventuallyEq, ae_withDensity_iff hf.coe_nnreal_ennreal] at hg'
      rw [ae_restrict_iff' A]
      filter_upwards [hg'] with a ha h'a
      have : (f a : â„â‰¥0âˆ) â‰  0 := by simpa only [Ne, ENNReal.coe_eq_zero] using h'a
      rw [ha this]
    Â· filter_upwards [ae_restrict_mem A.compl] with x hx
      simp only [Classical.not_not, mem_setOf_eq, mem_compl_iff] at hx
      simp [hx]
  Â· rintro âŸ¨g', g'meas, hg'âŸ©
    refine âŸ¨fun x â†¦ (f x : â„)â»Â¹ â€¢ g' x, hf.coe_nnreal_real.inv.stronglyMeasurable.smul g'meas, ?_âŸ©
    rw [EventuallyEq, ae_withDensity_iff hf.coe_nnreal_ennreal]
    filter_upwards [hg'] with x hx h'x
    rw [â† hx, smul_smul, inv_mul_cancelâ‚€, one_smul]
    simp only [Ne, ENNReal.coe_eq_zero] at h'x
    simpa only [NNReal.coe_eq_zero, Ne] using h'x
