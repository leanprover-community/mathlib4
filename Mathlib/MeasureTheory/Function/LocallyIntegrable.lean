/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Integral.IntegrableOn

#align_import measure_theory.function.locally_integrable from "leanprover-community/mathlib"@"08a4542bec7242a5c60f179e4e49de8c0d677b1b"

/-!
# Locally integrable functions

A function is called *locally integrable* (`MeasureTheory.LocallyIntegrable`) if it is integrable
on a neighborhood of every point. More generally, it is *locally integrable on `s`* if it is
locally integrable on a neighbourhood within `s` of any point of `s`.

This file contains properties of locally integrable functions, and integrability results
on compact sets.

## Main statements

* `Continuous.locallyIntegrable`: A continuous function is locally integrable.
* `ContinuousOn.locallyIntegrableOn`: A function which is continuous on `s` is locally
  integrable on `s`.
-/

open MeasureTheory MeasureTheory.Measure Set Function TopologicalSpace Bornology

open scoped Topology Interval ENNReal

variable {X Y E F R : Type*} [MeasurableSpace X] [TopologicalSpace X]
variable [MeasurableSpace Y] [TopologicalSpace Y]
variable [NormedAddCommGroup E] [NormedAddCommGroup F] {f g : X ‚Üí E} {Œº : Measure X} {s : Set X}

namespace MeasureTheory

section LocallyIntegrableOn

/-- A function `f : X ‚Üí E` is *locally integrable on s*, for `s ‚äÜ X`, if for every `x ‚àà s` there is
a neighbourhood of `x` within `s` on which `f` is integrable. (Note this is, in general, strictly
weaker than local integrability with respect to `Œº.restrict s`.) -/
def LocallyIntegrableOn (f : X ‚Üí E) (s : Set X) (Œº : Measure X := by volume_tac) : Prop :=
  ‚àÄ x : X, x ‚àà s ‚Üí IntegrableAtFilter f (ùìù[s] x) Œº
#align measure_theory.locally_integrable_on MeasureTheory.LocallyIntegrableOn

theorem LocallyIntegrableOn.mono_set (hf : LocallyIntegrableOn f s Œº) {t : Set X}
    (hst : t ‚äÜ s) : LocallyIntegrableOn f t Œº := fun x hx =>
  (hf x <| hst hx).filter_mono (nhdsWithin_mono x hst)
#align measure_theory.locally_integrable_on.mono MeasureTheory.LocallyIntegrableOn.mono_set

theorem LocallyIntegrableOn.norm (hf : LocallyIntegrableOn f s Œº) :
    LocallyIntegrableOn (fun x => ‚Äñf x‚Äñ) s Œº := fun t ht =>
  let ‚ü®U, hU_nhd, hU_int‚ü© := hf t ht
  ‚ü®U, hU_nhd, hU_int.norm‚ü©
#align measure_theory.locally_integrable_on.norm MeasureTheory.LocallyIntegrableOn.norm

theorem LocallyIntegrableOn.mono (hf : LocallyIntegrableOn f s Œº) {g : X ‚Üí F}
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñg x‚Äñ ‚â§ ‚Äñf x‚Äñ) :
    LocallyIntegrableOn g s Œº := by
  intro x hx
  rcases hf x hx with ‚ü®t, t_mem, ht‚ü©
  exact ‚ü®t, t_mem, Integrable.mono ht hg.restrict (ae_restrict_of_ae h)‚ü©

theorem IntegrableOn.locallyIntegrableOn (hf : IntegrableOn f s Œº) : LocallyIntegrableOn f s Œº :=
  fun _ _ => ‚ü®s, self_mem_nhdsWithin, hf‚ü©
#align measure_theory.integrable_on.locally_integrable_on MeasureTheory.IntegrableOn.locallyIntegrableOn

/-- If a function is locally integrable on a compact set, then it is integrable on that set. -/
theorem LocallyIntegrableOn.integrableOn_isCompact (hf : LocallyIntegrableOn f s Œº)
    (hs : IsCompact s) : IntegrableOn f s Œº :=
  IsCompact.induction_on hs integrableOn_empty (fun _u _v huv hv => hv.mono_set huv)
    (fun _u _v hu hv => integrableOn_union.mpr ‚ü®hu, hv‚ü©) hf
#align measure_theory.locally_integrable_on.integrable_on_is_compact MeasureTheory.LocallyIntegrableOn.integrableOn_isCompact

theorem LocallyIntegrableOn.integrableOn_compact_subset (hf : LocallyIntegrableOn f s Œº) {t : Set X}
    (hst : t ‚äÜ s) (ht : IsCompact t) : IntegrableOn f t Œº :=
  (hf.mono_set hst).integrableOn_isCompact ht
#align measure_theory.locally_integrable_on.integrable_on_compact_subset MeasureTheory.LocallyIntegrableOn.integrableOn_compact_subset

/-- If a function `f` is locally integrable on a set `s` in a second countable topological space,
then there exist countably many open sets `u` covering `s` such that `f` is integrable on each
set `u ‚à© s`. -/
theorem LocallyIntegrableOn.exists_countable_integrableOn [SecondCountableTopology X]
    (hf : LocallyIntegrableOn f s Œº) : ‚àÉ T : Set (Set X), T.Countable ‚àß
    (‚àÄ u ‚àà T, IsOpen u) ‚àß (s ‚äÜ ‚ãÉ u ‚àà T, u) ‚àß (‚àÄ u ‚àà T, IntegrableOn f (u ‚à© s) Œº) := by
  have : ‚àÄ x : s, ‚àÉ u, IsOpen u ‚àß x.1 ‚àà u ‚àß IntegrableOn f (u ‚à© s) Œº := by
    rintro ‚ü®x, hx‚ü©
    rcases hf x hx with ‚ü®t, ht, h't‚ü©
    rcases mem_nhdsWithin.1 ht with ‚ü®u, u_open, x_mem, u_sub‚ü©
    exact ‚ü®u, u_open, x_mem, h't.mono_set u_sub‚ü©
  choose u u_open xu hu using this
  obtain ‚ü®T, T_count, hT‚ü© : ‚àÉ T : Set s, T.Countable ‚àß s ‚äÜ ‚ãÉ i ‚àà T, u i := by
    have : s ‚äÜ ‚ãÉ x : s, u x := fun y hy => mem_iUnion_of_mem ‚ü®y, hy‚ü© (xu ‚ü®y, hy‚ü©)
    obtain ‚ü®T, hT_count, hT_un‚ü© := isOpen_iUnion_countable u u_open
    exact ‚ü®T, hT_count, by rwa [hT_un]‚ü©
  refine ‚ü®u '' T, T_count.image _, ?_, by rwa [biUnion_image], ?_‚ü©
  ¬∑ rintro v ‚ü®w, -, rfl‚ü©
    exact u_open _
  ¬∑ rintro v ‚ü®w, -, rfl‚ü©
    exact hu _

/-- If a function `f` is locally integrable on a set `s` in a second countable topological space,
then there exists a sequence of open sets `u n` covering `s` such that `f` is integrable on each
set `u n ‚à© s`. -/
theorem LocallyIntegrableOn.exists_nat_integrableOn [SecondCountableTopology X]
    (hf : LocallyIntegrableOn f s Œº) : ‚àÉ u : ‚Ñï ‚Üí Set X,
    (‚àÄ n, IsOpen (u n)) ‚àß (s ‚äÜ ‚ãÉ n, u n) ‚àß (‚àÄ n, IntegrableOn f (u n ‚à© s) Œº) := by
  rcases hf.exists_countable_integrableOn with ‚ü®T, T_count, T_open, sT, hT‚ü©
  let T' : Set (Set X) := insert ‚àÖ T
  have T'_count : T'.Countable := Countable.insert ‚àÖ T_count
  have T'_ne : T'.Nonempty := by simp only [T', insert_nonempty]
  rcases T'_count.exists_eq_range T'_ne with ‚ü®u, hu‚ü©
  refine ‚ü®u, ?_, ?_, ?_‚ü©
  ¬∑ intro n
    have : u n ‚àà T' := by rw [hu]; exact mem_range_self n
    rcases mem_insert_iff.1 this with h|h
    ¬∑ rw [h]
      exact isOpen_empty
    ¬∑ exact T_open _ h
  ¬∑ intro x hx
    obtain ‚ü®v, hv, h'v‚ü© : ‚àÉ v, v ‚àà T ‚àß x ‚àà v := by simpa only [mem_iUnion, exists_prop] using sT hx
    have : v ‚àà range u := by rw [‚Üê hu]; exact subset_insert ‚àÖ T hv
    obtain ‚ü®n, rfl‚ü© : ‚àÉ n, u n = v := by simpa only [mem_range] using this
    exact mem_iUnion_of_mem _ h'v
  ¬∑ intro n
    have : u n ‚àà T' := by rw [hu]; exact mem_range_self n
    rcases mem_insert_iff.1 this with h|h
    ¬∑ simp only [h, empty_inter, integrableOn_empty]
    ¬∑ exact hT _ h

theorem LocallyIntegrableOn.aestronglyMeasurable [SecondCountableTopology X]
    (hf : LocallyIntegrableOn f s Œº) : AEStronglyMeasurable f (Œº.restrict s) := by
  rcases hf.exists_nat_integrableOn with ‚ü®u, -, su, hu‚ü©
  have : s = ‚ãÉ n, u n ‚à© s := by rw [‚Üê iUnion_inter]; exact (inter_eq_right.mpr su).symm
  rw [this, aestronglyMeasurable_iUnion_iff]
  exact fun i : ‚Ñï => (hu i).aestronglyMeasurable
#align measure_theory.locally_integrable_on.ae_strongly_measurable MeasureTheory.LocallyIntegrableOn.aestronglyMeasurable

/-- If `s` is either open, or closed, then `f` is locally integrable on `s` iff it is integrable on
every compact subset contained in `s`. -/
theorem locallyIntegrableOn_iff [LocallyCompactSpace X] [T2Space X] (hs : IsClosed s ‚à® IsOpen s) :
    LocallyIntegrableOn f s Œº ‚Üî ‚àÄ (k : Set X), k ‚äÜ s ‚Üí (IsCompact k ‚Üí IntegrableOn f k Œº) := by
  -- The correct condition is that `s` be *locally closed*, i.e. for every `x ‚àà s` there is some
  -- `U ‚àà ùìù x` such that `U ‚à© s` is closed. But mathlib doesn't have locally closed sets yet.
  refine ‚ü®fun hf k hk => hf.integrableOn_compact_subset hk, fun hf x hx => ?_‚ü©
  cases hs with
  | inl hs =>
    exact
      let ‚ü®K, hK, h2K‚ü© := exists_compact_mem_nhds x
      ‚ü®_, inter_mem_nhdsWithin s h2K,
        hf _ (inter_subset_left _ _)
          (hK.of_isClosed_subset (hs.inter hK.isClosed) (inter_subset_right _ _))‚ü©
  | inr hs =>
    obtain ‚ü®K, hK, h2K, h3K‚ü© := exists_compact_subset hs hx
    refine ‚ü®K, ?_, hf K h3K hK‚ü©
    simpa only [IsOpen.nhdsWithin_eq hs hx, interior_eq_nhds'] using h2K
#align measure_theory.locally_integrable_on_iff MeasureTheory.locallyIntegrableOn_iff

protected theorem LocallyIntegrableOn.add
    (hf : LocallyIntegrableOn f s Œº) (hg : LocallyIntegrableOn g s Œº) :
    LocallyIntegrableOn (f + g) s Œº := fun x hx ‚Ü¶ (hf x hx).add (hg x hx)

protected theorem LocallyIntegrableOn.sub
    (hf : LocallyIntegrableOn f s Œº) (hg : LocallyIntegrableOn g s Œº) :
    LocallyIntegrableOn (f - g) s Œº := fun x hx ‚Ü¶ (hf x hx).sub (hg x hx)

protected theorem LocallyIntegrableOn.neg (hf : LocallyIntegrableOn f s Œº) :
    LocallyIntegrableOn (-f) s Œº := fun x hx ‚Ü¶ (hf x hx).neg

end LocallyIntegrableOn

/-- A function `f : X ‚Üí E` is *locally integrable* if it is integrable on a neighborhood of every
point. In particular, it is integrable on all compact sets,
see `LocallyIntegrable.integrableOn_isCompact`. -/
def LocallyIntegrable (f : X ‚Üí E) (Œº : Measure X := by volume_tac) : Prop :=
  ‚àÄ x : X, IntegrableAtFilter f (ùìù x) Œº
#align measure_theory.locally_integrable MeasureTheory.LocallyIntegrable

theorem locallyIntegrable_comap (hs : MeasurableSet s) :
    LocallyIntegrable (fun x : s ‚Ü¶ f x) (Œº.comap Subtype.val) ‚Üî LocallyIntegrableOn f s Œº := by
  simp_rw [LocallyIntegrableOn, Subtype.forall', ‚Üê map_nhds_subtype_val]
  exact forall_congr' fun _ ‚Ü¶ (MeasurableEmbedding.subtype_coe hs).integrableAtFilter_iff_comap.symm

theorem locallyIntegrableOn_univ : LocallyIntegrableOn f univ Œº ‚Üî LocallyIntegrable f Œº := by
  simp only [LocallyIntegrableOn, nhdsWithin_univ, mem_univ, true_imp_iff]; rfl
#align measure_theory.locally_integrable_on_univ MeasureTheory.locallyIntegrableOn_univ

theorem LocallyIntegrable.locallyIntegrableOn (hf : LocallyIntegrable f Œº) (s : Set X) :
    LocallyIntegrableOn f s Œº := fun x _ => (hf x).filter_mono nhdsWithin_le_nhds
#align measure_theory.locally_integrable.locally_integrable_on MeasureTheory.LocallyIntegrable.locallyIntegrableOn

theorem Integrable.locallyIntegrable (hf : Integrable f Œº) : LocallyIntegrable f Œº := fun _ =>
  hf.integrableAtFilter _
#align measure_theory.integrable.locally_integrable MeasureTheory.Integrable.locallyIntegrable

theorem LocallyIntegrable.mono (hf : LocallyIntegrable f Œº) {g : X ‚Üí F}
    (hg : AEStronglyMeasurable g Œº) (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñg x‚Äñ ‚â§ ‚Äñf x‚Äñ) :
    LocallyIntegrable g Œº := by
  rw [‚Üê locallyIntegrableOn_univ] at hf ‚ä¢
  exact hf.mono hg h

/-- If `f` is locally integrable with respect to `Œº.restrict s`, it is locally integrable on `s`.
(See `locallyIntegrableOn_iff_locallyIntegrable_restrict` for an iff statement when `s` is
closed.) -/
theorem locallyIntegrableOn_of_locallyIntegrable_restrict [OpensMeasurableSpace X]
    (hf : LocallyIntegrable f (Œº.restrict s)) : LocallyIntegrableOn f s Œº := by
  intro x _
  obtain ‚ü®t, ht_mem, ht_int‚ü© := hf x
  obtain ‚ü®u, hu_sub, hu_o, hu_mem‚ü© := mem_nhds_iff.mp ht_mem
  refine ‚ü®_, inter_mem_nhdsWithin s (hu_o.mem_nhds hu_mem), ?_‚ü©
  simpa only [IntegrableOn, Measure.restrict_restrict hu_o.measurableSet, inter_comm] using
    ht_int.mono_set hu_sub
#align measure_theory.locally_integrable_on_of_locally_integrable_restrict MeasureTheory.locallyIntegrableOn_of_locallyIntegrable_restrict

/-- If `s` is closed, being locally integrable on `s` wrt `Œº` is equivalent to being locally
integrable with respect to `Œº.restrict s`. For the one-way implication without assuming `s` closed,
see `locallyIntegrableOn_of_locallyIntegrable_restrict`. -/
theorem locallyIntegrableOn_iff_locallyIntegrable_restrict [OpensMeasurableSpace X]
    (hs : IsClosed s) : LocallyIntegrableOn f s Œº ‚Üî LocallyIntegrable f (Œº.restrict s) := by
  refine ‚ü®fun hf x => ?_, locallyIntegrableOn_of_locallyIntegrable_restrict‚ü©
  by_cases h : x ‚àà s
  ¬∑ obtain ‚ü®t, ht_nhds, ht_int‚ü© := hf x h
    obtain ‚ü®u, hu_o, hu_x, hu_sub‚ü© := mem_nhdsWithin.mp ht_nhds
    refine ‚ü®u, hu_o.mem_nhds hu_x, ?_‚ü©
    rw [IntegrableOn, restrict_restrict hu_o.measurableSet]
    exact ht_int.mono_set hu_sub
  ¬∑ rw [‚Üê isOpen_compl_iff] at hs
    refine ‚ü®s·∂ú, hs.mem_nhds h, ?_‚ü©
    rw [IntegrableOn, restrict_restrict, inter_comm, inter_compl_self, ‚Üê IntegrableOn]
    exacts [integrableOn_empty, hs.measurableSet]
#align measure_theory.locally_integrable_on_iff_locally_integrable_restrict MeasureTheory.locallyIntegrableOn_iff_locallyIntegrable_restrict

/-- If a function is locally integrable, then it is integrable on any compact set. -/
theorem LocallyIntegrable.integrableOn_isCompact {k : Set X} (hf : LocallyIntegrable f Œº)
    (hk : IsCompact k) : IntegrableOn f k Œº :=
  (hf.locallyIntegrableOn k).integrableOn_isCompact hk
#align measure_theory.locally_integrable.integrable_on_is_compact MeasureTheory.LocallyIntegrable.integrableOn_isCompact

/-- If a function is locally integrable, then it is integrable on an open neighborhood of any
compact set. -/
theorem LocallyIntegrable.integrableOn_nhds_isCompact (hf : LocallyIntegrable f Œº) {k : Set X}
    (hk : IsCompact k) : ‚àÉ u, IsOpen u ‚àß k ‚äÜ u ‚àß IntegrableOn f u Œº := by
  refine IsCompact.induction_on hk ?_ ?_ ?_ ?_
  ¬∑ refine ‚ü®‚àÖ, isOpen_empty, Subset.rfl, integrableOn_empty‚ü©
  ¬∑ rintro s t hst ‚ü®u, u_open, tu, hu‚ü©
    exact ‚ü®u, u_open, hst.trans tu, hu‚ü©
  ¬∑ rintro s t ‚ü®u, u_open, su, hu‚ü© ‚ü®v, v_open, tv, hv‚ü©
    exact ‚ü®u ‚à™ v, u_open.union v_open, union_subset_union su tv, hu.union hv‚ü©
  ¬∑ intro x _
    rcases hf x with ‚ü®u, ux, hu‚ü©
    rcases mem_nhds_iff.1 ux with ‚ü®v, vu, v_open, xv‚ü©
    exact ‚ü®v, nhdsWithin_le_nhds (v_open.mem_nhds xv), v, v_open, Subset.rfl, hu.mono_set vu‚ü©
#align measure_theory.locally_integrable.integrable_on_nhds_is_compact MeasureTheory.LocallyIntegrable.integrableOn_nhds_isCompact

theorem locallyIntegrable_iff [LocallyCompactSpace X] :
    LocallyIntegrable f Œº ‚Üî ‚àÄ k : Set X, IsCompact k ‚Üí IntegrableOn f k Œº :=
  ‚ü®fun hf _k hk => hf.integrableOn_isCompact hk, fun hf x =>
    let ‚ü®K, hK, h2K‚ü© := exists_compact_mem_nhds x
    ‚ü®K, h2K, hf K hK‚ü©‚ü©
#align measure_theory.locally_integrable_iff MeasureTheory.locallyIntegrable_iff

theorem LocallyIntegrable.aestronglyMeasurable [SecondCountableTopology X]
    (hf : LocallyIntegrable f Œº) : AEStronglyMeasurable f Œº := by
  simpa only [restrict_univ] using (locallyIntegrableOn_univ.mpr hf).aestronglyMeasurable
#align measure_theory.locally_integrable.ae_strongly_measurable MeasureTheory.LocallyIntegrable.aestronglyMeasurable

/-- If a function is locally integrable in a second countable topological space,
then there exists a sequence of open sets covering the space on which it is integrable. -/
theorem LocallyIntegrable.exists_nat_integrableOn [SecondCountableTopology X]
    (hf : LocallyIntegrable f Œº) : ‚àÉ u : ‚Ñï ‚Üí Set X,
    (‚àÄ n, IsOpen (u n)) ‚àß ((‚ãÉ n, u n) = univ) ‚àß (‚àÄ n, IntegrableOn f (u n) Œº) := by
  rcases (hf.locallyIntegrableOn univ).exists_nat_integrableOn with ‚ü®u, u_open, u_union, hu‚ü©
  refine ‚ü®u, u_open, eq_univ_of_univ_subset u_union, fun n ‚Ü¶ ?_‚ü©
  simpa only [inter_univ] using hu n

theorem Mem‚Ñíp.locallyIntegrable [IsLocallyFiniteMeasure Œº] {f : X ‚Üí E} {p : ‚Ñù‚â•0‚àû}
    (hf : Mem‚Ñíp f p Œº) (hp : 1 ‚â§ p) : LocallyIntegrable f Œº := by
  intro x
  rcases Œº.finiteAt_nhds x with ‚ü®U, hU, h'U‚ü©
  have : Fact (Œº U < ‚ä§) := ‚ü®h'U‚ü©
  refine ‚ü®U, hU, ?_‚ü©
  rw [IntegrableOn, ‚Üê mem‚Ñíp_one_iff_integrable]
  apply (hf.restrict U).mem‚Ñíp_of_exponent_le hp

theorem locallyIntegrable_const [IsLocallyFiniteMeasure Œº] (c : E) :
    LocallyIntegrable (fun _ => c) Œº :=
  (mem‚Ñíp_top_const c).locallyIntegrable le_top
#align measure_theory.locally_integrable_const MeasureTheory.locallyIntegrable_const

theorem locallyIntegrableOn_const [IsLocallyFiniteMeasure Œº] (c : E) :
    LocallyIntegrableOn (fun _ => c) s Œº :=
  (locallyIntegrable_const c).locallyIntegrableOn s
#align measure_theory.locally_integrable_on_const MeasureTheory.locallyIntegrableOn_const

theorem locallyIntegrable_zero : LocallyIntegrable (fun _ ‚Ü¶ (0 : E)) Œº :=
  (integrable_zero X E Œº).locallyIntegrable

theorem locallyIntegrableOn_zero : LocallyIntegrableOn (fun _ ‚Ü¶ (0 : E)) s Œº :=
  locallyIntegrable_zero.locallyIntegrableOn s

theorem LocallyIntegrable.indicator (hf : LocallyIntegrable f Œº) {s : Set X}
    (hs : MeasurableSet s) : LocallyIntegrable (s.indicator f) Œº := by
  intro x
  rcases hf x with ‚ü®U, hU, h'U‚ü©
  exact ‚ü®U, hU, h'U.indicator hs‚ü©
#align measure_theory.locally_integrable.indicator MeasureTheory.LocallyIntegrable.indicator

theorem locallyIntegrable_map_homeomorph [BorelSpace X] [BorelSpace Y] (e : X ‚âÉ‚Çú Y) {f : Y ‚Üí E}
    {Œº : Measure X} : LocallyIntegrable f (Measure.map e Œº) ‚Üî LocallyIntegrable (f ‚àò e) Œº := by
  refine ‚ü®fun h x => ?_, fun h x => ?_‚ü©
  ¬∑ rcases h (e x) with ‚ü®U, hU, h'U‚ü©
    refine ‚ü®e ‚Åª¬π' U, e.continuous.continuousAt.preimage_mem_nhds hU, ?_‚ü©
    exact (integrableOn_map_equiv e.toMeasurableEquiv).1 h'U
  ¬∑ rcases h (e.symm x) with ‚ü®U, hU, h'U‚ü©
    refine ‚ü®e.symm ‚Åª¬π' U, e.symm.continuous.continuousAt.preimage_mem_nhds hU, ?_‚ü©
    apply (integrableOn_map_equiv e.toMeasurableEquiv).2
    simp only [Homeomorph.toMeasurableEquiv_coe]
    convert h'U
    ext x
    simp only [mem_preimage, Homeomorph.symm_apply_apply]
#align measure_theory.locally_integrable_map_homeomorph MeasureTheory.locallyIntegrable_map_homeomorph

protected theorem LocallyIntegrable.add (hf : LocallyIntegrable f Œº) (hg : LocallyIntegrable g Œº) :
    LocallyIntegrable (f + g) Œº := fun x ‚Ü¶ (hf x).add (hg x)

protected theorem LocallyIntegrable.sub (hf : LocallyIntegrable f Œº) (hg : LocallyIntegrable g Œº) :
    LocallyIntegrable (f - g) Œº := fun x ‚Ü¶ (hf x).sub (hg x)

protected theorem LocallyIntegrable.neg (hf : LocallyIntegrable f Œº) :
    LocallyIntegrable (-f) Œº := fun x ‚Ü¶ (hf x).neg

protected theorem LocallyIntegrable.smul {ùïú : Type*} [NormedAddCommGroup ùïú] [SMulZeroClass ùïú E]
    [BoundedSMul ùïú E] (hf : LocallyIntegrable f Œº) (c : ùïú) :
    LocallyIntegrable (c ‚Ä¢ f) Œº := fun x ‚Ü¶ (hf x).smul c

theorem locallyIntegrable_finset_sum' {Œπ} (s : Finset Œπ) {f : Œπ ‚Üí X ‚Üí E}
    (hf : ‚àÄ i ‚àà s, LocallyIntegrable (f i) Œº) : LocallyIntegrable (‚àë i ‚àà s, f i) Œº :=
  Finset.sum_induction f (fun g => LocallyIntegrable g Œº) (fun _ _ => LocallyIntegrable.add)
    locallyIntegrable_zero hf

theorem locallyIntegrable_finset_sum {Œπ} (s : Finset Œπ) {f : Œπ ‚Üí X ‚Üí E}
    (hf : ‚àÄ i ‚àà s, LocallyIntegrable (f i) Œº) : LocallyIntegrable (fun a ‚Ü¶ ‚àë i ‚àà s, f i a) Œº := by
  simpa only [‚Üê Finset.sum_apply] using locallyIntegrable_finset_sum' s hf

/-- If `f` is locally integrable and `g` is continuous with compact support,
then `g ‚Ä¢ f` is integrable. -/
theorem LocallyIntegrable.integrable_smul_left_of_hasCompactSupport
    [NormedSpace ‚Ñù E] [OpensMeasurableSpace X] [T2Space X]
    (hf : LocallyIntegrable f Œº) {g : X ‚Üí ‚Ñù} (hg : Continuous g) (h'g : HasCompactSupport g) :
    Integrable (fun x ‚Ü¶ g x ‚Ä¢ f x) Œº := by
  let K := tsupport g
  have hK : IsCompact K := h'g
  have : K.indicator (fun x ‚Ü¶ g x ‚Ä¢ f x) = (fun x ‚Ü¶ g x ‚Ä¢ f x) := by
    apply indicator_eq_self.2
    apply support_subset_iff'.2
    intros x hx
    simp [image_eq_zero_of_nmem_tsupport hx]
  rw [‚Üê this, indicator_smul]
  apply Integrable.smul_of_top_right
  ¬∑ rw [integrable_indicator_iff hK.measurableSet]
    exact hf.integrableOn_isCompact hK
  ¬∑ exact hg.mem‚Ñíp_top_of_hasCompactSupport h'g Œº

/-- If `f` is locally integrable and `g` is continuous with compact support,
then `f ‚Ä¢ g` is integrable. -/
theorem LocallyIntegrable.integrable_smul_right_of_hasCompactSupport
    [NormedSpace ‚Ñù E] [OpensMeasurableSpace X] [T2Space X] {f : X ‚Üí ‚Ñù} (hf : LocallyIntegrable f Œº)
    {g : X ‚Üí E} (hg : Continuous g) (h'g : HasCompactSupport g) :
    Integrable (fun x ‚Ü¶ f x ‚Ä¢ g x) Œº := by
  let K := tsupport g
  have hK : IsCompact K := h'g
  have : K.indicator (fun x ‚Ü¶ f x ‚Ä¢ g x) = (fun x ‚Ü¶ f x ‚Ä¢ g x) := by
    apply indicator_eq_self.2
    apply support_subset_iff'.2
    intros x hx
    simp [image_eq_zero_of_nmem_tsupport hx]
  rw [‚Üê this, indicator_smul_left]
  apply Integrable.smul_of_top_left
  ¬∑ rw [integrable_indicator_iff hK.measurableSet]
    exact hf.integrableOn_isCompact hK
  ¬∑ exact hg.mem‚Ñíp_top_of_hasCompactSupport h'g Œº

open Filter

theorem integrable_iff_integrableAtFilter_cocompact :
    Integrable f Œº ‚Üî (IntegrableAtFilter f (cocompact X) Œº ‚àß LocallyIntegrable f Œº) := by
  refine ‚ü®fun hf ‚Ü¶ ‚ü®hf.integrableAtFilter _, hf.locallyIntegrable‚ü©, fun ‚ü®‚ü®s, hsc, hs‚ü©, hloc‚ü© ‚Ü¶ ?_‚ü©
  obtain ‚ü®t, htc, ht‚ü© := mem_cocompact'.mp hsc
  rewrite [‚Üê integrableOn_univ, ‚Üê compl_union_self s, integrableOn_union]
  exact ‚ü®(hloc.integrableOn_isCompact htc).mono ht le_rfl, hs‚ü©

theorem integrable_iff_integrableAtFilter_atBot_atTop [LinearOrder X] [CompactIccSpace X] :
    Integrable f Œº ‚Üî
    (IntegrableAtFilter f atBot Œº ‚àß IntegrableAtFilter f atTop Œº) ‚àß LocallyIntegrable f Œº := by
  constructor
  ¬∑ exact fun hf ‚Ü¶ ‚ü®‚ü®hf.integrableAtFilter _, hf.integrableAtFilter _‚ü©, hf.locallyIntegrable‚ü©
  ¬∑ refine fun h ‚Ü¶ integrable_iff_integrableAtFilter_cocompact.mpr ‚ü®?_, h.2‚ü©
    exact (IntegrableAtFilter.sup_iff.mpr h.1).filter_mono cocompact_le_atBot_atTop

theorem integrable_iff_integrableAtFilter_atBot [LinearOrder X] [OrderTop X] [CompactIccSpace X] :
    Integrable f Œº ‚Üî IntegrableAtFilter f atBot Œº ‚àß LocallyIntegrable f Œº := by
  constructor
  ¬∑ exact fun hf ‚Ü¶ ‚ü®hf.integrableAtFilter _, hf.locallyIntegrable‚ü©
  ¬∑ refine fun h ‚Ü¶ integrable_iff_integrableAtFilter_cocompact.mpr ‚ü®?_, h.2‚ü©
    exact h.1.filter_mono cocompact_le_atBot

theorem integrable_iff_integrableAtFilter_atTop [LinearOrder X] [OrderBot X] [CompactIccSpace X] :
    Integrable f Œº ‚Üî IntegrableAtFilter f atTop Œº ‚àß LocallyIntegrable f Œº :=
  integrable_iff_integrableAtFilter_atBot (X := X·µí·µà)

variable {a : X}

theorem integrableOn_Iic_iff_integrableAtFilter_atBot [LinearOrder X] [CompactIccSpace X] :
    IntegrableOn f (Iic a) Œº ‚Üî IntegrableAtFilter f atBot Œº ‚àß LocallyIntegrableOn f (Iic a) Œº := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®‚ü®Iic a, Iic_mem_atBot a, h‚ü©, h.locallyIntegrableOn‚ü©, fun ‚ü®‚ü®s, hsl, hs‚ü©, h‚ü© ‚Ü¶ ?_‚ü©
  haveI : Nonempty X := Nonempty.intro a
  obtain ‚ü®a', ha'‚ü© := mem_atBot_sets.mp hsl
  refine (integrableOn_union.mpr ‚ü®hs.mono ha' le_rfl, ?_‚ü©).mono Iic_subset_Iic_union_Icc le_rfl
  exact h.integrableOn_compact_subset Icc_subset_Iic_self isCompact_Icc

theorem integrableOn_Ici_iff_integrableAtFilter_atTop [LinearOrder X] [CompactIccSpace X] :
    IntegrableOn f (Ici a) Œº ‚Üî IntegrableAtFilter f atTop Œº ‚àß LocallyIntegrableOn f (Ici a) Œº :=
  integrableOn_Iic_iff_integrableAtFilter_atBot (X := X·µí·µà)

theorem integrableOn_Iio_iff_integrableAtFilter_atBot_nhdsWithin
    [LinearOrder X] [CompactIccSpace X] [NoMinOrder X] [OrderTopology X] :
    IntegrableOn f (Iio a) Œº ‚Üî IntegrableAtFilter f atBot Œº ‚àß
    IntegrableAtFilter f (ùìù[<] a) Œº ‚àß LocallyIntegrableOn f (Iio a) Œº := by
  constructor
  ¬∑ intro h
    exact ‚ü®‚ü®Iio a, Iio_mem_atBot a, h‚ü©, ‚ü®Iio a, self_mem_nhdsWithin, h‚ü©, h.locallyIntegrableOn‚ü©
  ¬∑ intro ‚ü®hbot, ‚ü®s, hsl, hs‚ü©, hlocal‚ü©
    obtain ‚ü®s', ‚ü®hs'_mono, hs'‚ü©‚ü© := mem_nhdsWithin_Iio_iff_exists_Ioo_subset.mp hsl
    refine (integrableOn_union.mpr ‚ü®?_, hs.mono hs' le_rfl‚ü©).mono Iio_subset_Iic_union_Ioo le_rfl
    exact integrableOn_Iic_iff_integrableAtFilter_atBot.mpr
      ‚ü®hbot, hlocal.mono_set (Iic_subset_Iio.mpr hs'_mono)‚ü©

theorem integrableOn_Ioi_iff_integrableAtFilter_atTop_nhdsWithin
    [LinearOrder X] [CompactIccSpace X] [NoMaxOrder X] [OrderTopology X] :
    IntegrableOn f (Ioi a) Œº ‚Üî IntegrableAtFilter f atTop Œº ‚àß
    IntegrableAtFilter f (ùìù[>] a) Œº ‚àß LocallyIntegrableOn f (Ioi a) Œº :=
  integrableOn_Iio_iff_integrableAtFilter_atBot_nhdsWithin (X := X·µí·µà)

end MeasureTheory

open MeasureTheory

section borel

variable [OpensMeasurableSpace X]
variable {K : Set X} {a b : X}

/-- A continuous function `f` is locally integrable with respect to any locally finite measure. -/
theorem Continuous.locallyIntegrable [IsLocallyFiniteMeasure Œº] [SecondCountableTopologyEither X E]
    (hf : Continuous f) : LocallyIntegrable f Œº :=
  hf.integrableAt_nhds
#align continuous.locally_integrable Continuous.locallyIntegrable

/-- A function `f` continuous on a set `K` is locally integrable on this set with respect
to any locally finite measure. -/
theorem ContinuousOn.locallyIntegrableOn [IsLocallyFiniteMeasure Œº]
    [SecondCountableTopologyEither X E] (hf : ContinuousOn f K)
    (hK : MeasurableSet K) : LocallyIntegrableOn f K Œº := fun _x hx =>
  hf.integrableAt_nhdsWithin hK hx
#align continuous_on.locally_integrable_on ContinuousOn.locallyIntegrableOn

variable [IsFiniteMeasureOnCompacts Œº]

/-- A function `f` continuous on a compact set `K` is integrable on this set with respect to any
locally finite measure. -/
theorem ContinuousOn.integrableOn_compact'
    (hK : IsCompact K) (h'K : MeasurableSet K) (hf : ContinuousOn f K) :
    IntegrableOn f K Œº := by
  refine ‚ü®ContinuousOn.aestronglyMeasurable_of_isCompact hf hK h'K, ?_‚ü©
  have : Fact (Œº K < ‚àû) := ‚ü®hK.measure_lt_top‚ü©
  obtain ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ x ‚àà f '' K, ‚Äñx‚Äñ ‚â§ C :=
    IsBounded.exists_norm_le (hK.image_of_continuousOn hf).isBounded
  apply hasFiniteIntegral_of_bounded (C := C)
  filter_upwards [ae_restrict_mem h'K] with x hx using hC _ (mem_image_of_mem f hx)

theorem ContinuousOn.integrableOn_compact [T2Space X]
    (hK : IsCompact K) (hf : ContinuousOn f K) : IntegrableOn f K Œº :=
  hf.integrableOn_compact' hK hK.measurableSet
#align continuous_on.integrable_on_compact ContinuousOn.integrableOn_compact

theorem ContinuousOn.integrableOn_Icc [Preorder X] [CompactIccSpace X] [T2Space X]
    (hf : ContinuousOn f (Icc a b)) : IntegrableOn f (Icc a b) Œº :=
  hf.integrableOn_compact isCompact_Icc
#align continuous_on.integrable_on_Icc ContinuousOn.integrableOn_Icc

theorem Continuous.integrableOn_Icc [Preorder X] [CompactIccSpace X] [T2Space X]
    (hf : Continuous f) : IntegrableOn f (Icc a b) Œº :=
  hf.continuousOn.integrableOn_Icc
#align continuous.integrable_on_Icc Continuous.integrableOn_Icc

theorem Continuous.integrableOn_Ioc [Preorder X] [CompactIccSpace X] [T2Space X]
    (hf : Continuous f) : IntegrableOn f (Ioc a b) Œº :=
  hf.integrableOn_Icc.mono_set Ioc_subset_Icc_self
#align continuous.integrable_on_Ioc Continuous.integrableOn_Ioc

theorem ContinuousOn.integrableOn_uIcc [LinearOrder X] [CompactIccSpace X] [T2Space X]
    (hf : ContinuousOn f [[a, b]]) : IntegrableOn f [[a, b]] Œº :=
  hf.integrableOn_Icc
#align continuous_on.integrable_on_uIcc ContinuousOn.integrableOn_uIcc

theorem Continuous.integrableOn_uIcc [LinearOrder X] [CompactIccSpace X] [T2Space X]
    (hf : Continuous f) : IntegrableOn f [[a, b]] Œº :=
  hf.integrableOn_Icc
#align continuous.integrable_on_uIcc Continuous.integrableOn_uIcc

theorem Continuous.integrableOn_uIoc [LinearOrder X] [CompactIccSpace X] [T2Space X]
    (hf : Continuous f) : IntegrableOn f (Œô a b) Œº :=
  hf.integrableOn_Ioc
#align continuous.integrable_on_uIoc Continuous.integrableOn_uIoc

/-- A continuous function with compact support is integrable on the whole space. -/
theorem Continuous.integrable_of_hasCompactSupport (hf : Continuous f) (hcf : HasCompactSupport f) :
    Integrable f Œº :=
  (integrableOn_iff_integrable_of_support_subset (subset_tsupport f)).mp <|
    hf.continuousOn.integrableOn_compact' hcf (isClosed_tsupport _).measurableSet
#align continuous.integrable_of_has_compact_support Continuous.integrable_of_hasCompactSupport

end borel

open scoped ENNReal

section Monotone

variable [BorelSpace X] [ConditionallyCompleteLinearOrder X] [ConditionallyCompleteLinearOrder E]
  [OrderTopology X] [OrderTopology E] [SecondCountableTopology E]

theorem MonotoneOn.integrableOn_of_measure_ne_top (hmono : MonotoneOn f s) {a b : X}
    (ha : IsLeast s a) (hb : IsGreatest s b) (hs : Œº s ‚â† ‚àû) (h's : MeasurableSet s) :
    IntegrableOn f s Œº := by
  borelize E
  obtain rfl | _ := s.eq_empty_or_nonempty
  ¬∑ exact integrableOn_empty
  have hbelow : BddBelow (f '' s) := ‚ü®f a, fun x ‚ü®y, hy, hyx‚ü© => hyx ‚ñ∏ hmono ha.1 hy (ha.2 hy)‚ü©
  have habove : BddAbove (f '' s) := ‚ü®f b, fun x ‚ü®y, hy, hyx‚ü© => hyx ‚ñ∏ hmono hy hb.1 (hb.2 hy)‚ü©
  have : IsBounded (f '' s) := Metric.isBounded_of_bddAbove_of_bddBelow habove hbelow
  rcases isBounded_iff_forall_norm_le.mp this with ‚ü®C, hC‚ü©
  have A : IntegrableOn (fun _ => C) s Œº := by
    simp only [hs.lt_top, integrableOn_const, or_true_iff]
  exact
    Integrable.mono' A (aemeasurable_restrict_of_monotoneOn h's hmono).aestronglyMeasurable
      ((ae_restrict_iff' h's).mpr <| ae_of_all _ fun y hy => hC (f y) (mem_image_of_mem f hy))
#align monotone_on.integrable_on_of_measure_ne_top MonotoneOn.integrableOn_of_measure_ne_top

theorem MonotoneOn.integrableOn_isCompact [IsFiniteMeasureOnCompacts Œº] (hs : IsCompact s)
    (hmono : MonotoneOn f s) : IntegrableOn f s Œº := by
  obtain rfl | h := s.eq_empty_or_nonempty
  ¬∑ exact integrableOn_empty
  ¬∑ exact
      hmono.integrableOn_of_measure_ne_top (hs.isLeast_sInf h) (hs.isGreatest_sSup h)
        hs.measure_lt_top.ne hs.measurableSet
#align monotone_on.integrable_on_is_compact MonotoneOn.integrableOn_isCompact

theorem AntitoneOn.integrableOn_of_measure_ne_top (hanti : AntitoneOn f s) {a b : X}
    (ha : IsLeast s a) (hb : IsGreatest s b) (hs : Œº s ‚â† ‚àû) (h's : MeasurableSet s) :
    IntegrableOn f s Œº :=
  hanti.dual_right.integrableOn_of_measure_ne_top ha hb hs h's
#align antitone_on.integrable_on_of_measure_ne_top AntitoneOn.integrableOn_of_measure_ne_top

theorem AntioneOn.integrableOn_isCompact [IsFiniteMeasureOnCompacts Œº] (hs : IsCompact s)
    (hanti : AntitoneOn f s) : IntegrableOn f s Œº :=
  hanti.dual_right.integrableOn_isCompact (E := E·µí·µà) hs
#align antione_on.integrable_on_is_compact AntioneOn.integrableOn_isCompact

theorem Monotone.locallyIntegrable [IsLocallyFiniteMeasure Œº] (hmono : Monotone f) :
    LocallyIntegrable f Œº := by
  intro x
  rcases Œº.finiteAt_nhds x with ‚ü®U, hU, h'U‚ü©
  obtain ‚ü®a, b, xab, hab, abU‚ü© : ‚àÉ a b : X, x ‚àà Icc a b ‚àß Icc a b ‚àà ùìù x ‚àß Icc a b ‚äÜ U :=
    exists_Icc_mem_subset_of_mem_nhds hU
  have ab : a ‚â§ b := xab.1.trans xab.2
  refine ‚ü®Icc a b, hab, ?_‚ü©
  exact
    (hmono.monotoneOn _).integrableOn_of_measure_ne_top (isLeast_Icc ab) (isGreatest_Icc ab)
      ((measure_mono abU).trans_lt h'U).ne measurableSet_Icc
#align monotone.locally_integrable Monotone.locallyIntegrable

theorem Antitone.locallyIntegrable [IsLocallyFiniteMeasure Œº] (hanti : Antitone f) :
    LocallyIntegrable f Œº :=
  hanti.dual_right.locallyIntegrable
#align antitone.locally_integrable Antitone.locallyIntegrable

end Monotone

namespace MeasureTheory

variable [OpensMeasurableSpace X] {A K : Set X}

section Mul

variable [NormedRing R] [SecondCountableTopologyEither X R] {g g' : X ‚Üí R}

theorem IntegrableOn.mul_continuousOn_of_subset (hg : IntegrableOn g A Œº) (hg' : ContinuousOn g' K)
    (hA : MeasurableSet A) (hK : IsCompact K) (hAK : A ‚äÜ K) :
    IntegrableOn (fun x => g x * g' x) A Œº := by
  rcases IsCompact.exists_bound_of_continuousOn hK hg' with ‚ü®C, hC‚ü©
  rw [IntegrableOn, ‚Üê mem‚Ñíp_one_iff_integrable] at hg ‚ä¢
  have : ‚àÄ·µê x ‚àÇŒº.restrict A, ‚Äñg x * g' x‚Äñ ‚â§ C * ‚Äñg x‚Äñ := by
    filter_upwards [ae_restrict_mem hA] with x hx
    refine (norm_mul_le _ _).trans ?_
    rw [mul_comm]
    gcongr
    exact hC x (hAK hx)
  exact
    Mem‚Ñíp.of_le_mul hg (hg.aestronglyMeasurable.mul <| (hg'.mono hAK).aestronglyMeasurable hA) this
#align measure_theory.integrable_on.mul_continuous_on_of_subset MeasureTheory.IntegrableOn.mul_continuousOn_of_subset

theorem IntegrableOn.mul_continuousOn [T2Space X] (hg : IntegrableOn g K Œº)
    (hg' : ContinuousOn g' K) (hK : IsCompact K) : IntegrableOn (fun x => g x * g' x) K Œº :=
  hg.mul_continuousOn_of_subset hg' hK.measurableSet hK (Subset.refl _)
#align measure_theory.integrable_on.mul_continuous_on MeasureTheory.IntegrableOn.mul_continuousOn

theorem IntegrableOn.continuousOn_mul_of_subset (hg : ContinuousOn g K) (hg' : IntegrableOn g' A Œº)
    (hK : IsCompact K) (hA : MeasurableSet A) (hAK : A ‚äÜ K) :
    IntegrableOn (fun x => g x * g' x) A Œº := by
  rcases IsCompact.exists_bound_of_continuousOn hK hg with ‚ü®C, hC‚ü©
  rw [IntegrableOn, ‚Üê mem‚Ñíp_one_iff_integrable] at hg' ‚ä¢
  have : ‚àÄ·µê x ‚àÇŒº.restrict A, ‚Äñg x * g' x‚Äñ ‚â§ C * ‚Äñg' x‚Äñ := by
    filter_upwards [ae_restrict_mem hA] with x hx
    refine (norm_mul_le _ _).trans ?_
    gcongr
    exact hC x (hAK hx)
  exact
    Mem‚Ñíp.of_le_mul hg' (((hg.mono hAK).aestronglyMeasurable hA).mul hg'.aestronglyMeasurable) this
#align measure_theory.integrable_on.continuous_on_mul_of_subset MeasureTheory.IntegrableOn.continuousOn_mul_of_subset

theorem IntegrableOn.continuousOn_mul [T2Space X] (hg : ContinuousOn g K)
    (hg' : IntegrableOn g' K Œº) (hK : IsCompact K) : IntegrableOn (fun x => g x * g' x) K Œº :=
  hg'.continuousOn_mul_of_subset hg hK hK.measurableSet Subset.rfl
#align measure_theory.integrable_on.continuous_on_mul MeasureTheory.IntegrableOn.continuousOn_mul

end Mul

section SMul

variable {ùïú : Type*} [NormedField ùïú] [NormedSpace ùïú E]

theorem IntegrableOn.continuousOn_smul [T2Space X] [SecondCountableTopologyEither X ùïú] {g : X ‚Üí E}
    (hg : IntegrableOn g K Œº) {f : X ‚Üí ùïú} (hf : ContinuousOn f K) (hK : IsCompact K) :
    IntegrableOn (fun x => f x ‚Ä¢ g x) K Œº := by
  rw [IntegrableOn, ‚Üê integrable_norm_iff]
  ¬∑ simp_rw [norm_smul]
    refine IntegrableOn.continuousOn_mul ?_ hg.norm hK
    exact continuous_norm.comp_continuousOn hf
  ¬∑ exact (hf.aestronglyMeasurable hK.measurableSet).smul hg.1
#align measure_theory.integrable_on.continuous_on_smul MeasureTheory.IntegrableOn.continuousOn_smul

theorem IntegrableOn.smul_continuousOn [T2Space X] [SecondCountableTopologyEither X E] {f : X ‚Üí ùïú}
    (hf : IntegrableOn f K Œº) {g : X ‚Üí E} (hg : ContinuousOn g K) (hK : IsCompact K) :
    IntegrableOn (fun x => f x ‚Ä¢ g x) K Œº := by
  rw [IntegrableOn, ‚Üê integrable_norm_iff]
  ¬∑ simp_rw [norm_smul]
    refine IntegrableOn.mul_continuousOn hf.norm ?_ hK
    exact continuous_norm.comp_continuousOn hg
  ¬∑ exact hf.1.smul (hg.aestronglyMeasurable hK.measurableSet)
#align measure_theory.integrable_on.smul_continuous_on MeasureTheory.IntegrableOn.smul_continuousOn

end SMul

namespace LocallyIntegrableOn

theorem continuousOn_mul [LocallyCompactSpace X] [T2Space X] [NormedRing R]
    [SecondCountableTopologyEither X R] {f g : X ‚Üí R} {s : Set X} (hf : LocallyIntegrableOn f s Œº)
    (hg : ContinuousOn g s) (hs : IsOpen s) : LocallyIntegrableOn (fun x => g x * f x) s Œº := by
  rw [MeasureTheory.locallyIntegrableOn_iff (Or.inr hs)] at hf ‚ä¢
  exact fun k hk_sub hk_c => (hf k hk_sub hk_c).continuousOn_mul (hg.mono hk_sub) hk_c
#align measure_theory.locally_integrable_on.continuous_on_mul MeasureTheory.LocallyIntegrableOn.continuousOn_mul

theorem mul_continuousOn [LocallyCompactSpace X] [T2Space X] [NormedRing R]
    [SecondCountableTopologyEither X R] {f g : X ‚Üí R} {s : Set X} (hf : LocallyIntegrableOn f s Œº)
    (hg : ContinuousOn g s) (hs : IsOpen s) : LocallyIntegrableOn (fun x => f x * g x) s Œº := by
  rw [MeasureTheory.locallyIntegrableOn_iff (Or.inr hs)] at hf ‚ä¢
  exact fun k hk_sub hk_c => (hf k hk_sub hk_c).mul_continuousOn (hg.mono hk_sub) hk_c
#align measure_theory.locally_integrable_on.mul_continuous_on MeasureTheory.LocallyIntegrableOn.mul_continuousOn

theorem continuousOn_smul [LocallyCompactSpace X] [T2Space X] {ùïú : Type*} [NormedField ùïú]
    [SecondCountableTopologyEither X ùïú] [NormedSpace ùïú E] {f : X ‚Üí E} {g : X ‚Üí ùïú} {s : Set X}
    (hs : IsOpen s) (hf : LocallyIntegrableOn f s Œº) (hg : ContinuousOn g s) :
    LocallyIntegrableOn (fun x => g x ‚Ä¢ f x) s Œº := by
  rw [MeasureTheory.locallyIntegrableOn_iff (Or.inr hs)] at hf ‚ä¢
  exact fun k hk_sub hk_c => (hf k hk_sub hk_c).continuousOn_smul (hg.mono hk_sub) hk_c
#align measure_theory.locally_integrable_on.continuous_on_smul MeasureTheory.LocallyIntegrableOn.continuousOn_smul

theorem smul_continuousOn [LocallyCompactSpace X] [T2Space X] {ùïú : Type*} [NormedField ùïú]
    [SecondCountableTopologyEither X E] [NormedSpace ùïú E] {f : X ‚Üí ùïú} {g : X ‚Üí E} {s : Set X}
    (hs : IsOpen s) (hf : LocallyIntegrableOn f s Œº) (hg : ContinuousOn g s) :
    LocallyIntegrableOn (fun x => f x ‚Ä¢ g x) s Œº := by
  rw [MeasureTheory.locallyIntegrableOn_iff (Or.inr hs)] at hf ‚ä¢
  exact fun k hk_sub hk_c => (hf k hk_sub hk_c).smul_continuousOn (hg.mono hk_sub) hk_c
#align measure_theory.locally_integrable_on.smul_continuous_on MeasureTheory.LocallyIntegrableOn.smul_continuousOn

end LocallyIntegrableOn

end MeasureTheory
