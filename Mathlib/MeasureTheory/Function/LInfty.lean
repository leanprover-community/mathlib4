/-
Copyright (c) 2025 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.MeasureTheory.Function.LpSpace.Basic
import Mathlib.MeasureTheory.Function.Holder

/-!
# The API for multiplicative structure on `Lâˆ`

This file develops the basic results specific to `Lp R âˆ Î¼` when `R` is a
`NormedRing`.  The main goal is to equip `Lâˆ` with its natural pointwise multiplicative
structure (defined a.e.) and to register the constant embedding.  This is a small,
self-contained layer intended to be imported later by files that build richer structure
(e.g. the commutative Câ‹†-algebra structure when `R = ğ•œ`, for `RCLike ğ•œ`).

## Main definitions

* `instance : Mul (Lp R âŠ¤ Î¼)` â€“ pointwise (a.e.) multiplication on `Lâˆ`.
* `Linfty.const : R â†’+ Lp R âŠ¤ Î¼` â€“ additive monoid hom sending a scalar to the corresponding
  constant `Lâˆ` function.

## Tags

Lp, Lâˆ

-/

namespace MeasureTheory

open ENNReal

variable {Î± : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±}
variable {R : Type*} [NormedRing R]

namespace Linfty
section Mul

/-- We use the existing `HSMul (Lp R âˆ Î¼) (Lp R âˆ Î¼) (Lp R âˆ Î¼)` instance to define
   `Mul (Lp R âˆ Î¼)`, to ensure compatibility of the former with the latter. -/
noncomputable instance : Mul (Lp R âˆ Î¼) where
  mul f g := f â€¢ g

/-- Check for diamonds, to guard against future refactors that try to change the def of `Mul`. -/
example : (Â· * Â·) = (Â· â€¢ Â· : Lp R âˆ Î¼ â†’ Lp R âˆ Î¼ â†’ Lp R âˆ Î¼) := by with_reducible_and_instances rfl

lemma coeFn_mul (f g : Lp R âˆ Î¼) : f * g =áµ[Î¼] â‡‘f * g :=
  Lp.coeFn_lpSMul f g

end Mul

section Const

/-- Note: Unlike for general Lp, this does not require `IsFiniteMeasure` instance. -/
theorem memLp_const (c : R) : MemLp (fun _ : Î± => c) âˆ Î¼ := by
  refine âŸ¨aestronglyMeasurable_const, ?_âŸ©
  by_cases hÎ¼ : Î¼ = 0
  Â· simp [hÎ¼]
  Â· rw [eLpNorm_const c (ENNReal.top_ne_zero) hÎ¼]
    simp

theorem const_mem_Linfty (c : R) :
    @AEEqFun.const Î± _ _ Î¼ _ c âˆˆ Lp R âˆ Î¼ :=
  (memLp_const c).eLpNorm_mk_lt_top

/-- The constant Lâˆ function. -/
def const : R â†’+ Lp R âˆ Î¼ where
  toFun c := âŸ¨AEEqFun.const Î± c, const_mem_Linfty câŸ©
  map_zero' := rfl
  map_add' _ _ := rfl

@[simp]
lemma const_val (c : R) : (Linfty.const c).1 = AEEqFun.const (Î² := R) (Î¼ := Î¼) Î± c := rfl

lemma coeFn_const (c : R) : Linfty.const (Î¼ := Î¼) c =áµ[Î¼] Function.const Î± c :=
  AEEqFun.coeFn_const Î± c

end Const

end Linfty

end MeasureTheory
