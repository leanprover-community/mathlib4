/-
Copyright (c) 2023 Igor Khavkine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Igor Khavkine
-/
import Mathlib.Order.Filter.IndicatorFunction
import Mathlib.MeasureTheory.Function.ConvergenceInMeasure
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.MeasureTheory.Function.UniformIntegrable

/-!
# Uniform tightness

This file contains the definitions for uniform tightness for a family of Lp functions.
It is used as a hypothesis to the version of Vitali's convergence theorem for Lp spaces
that works also for spaces of infinite measure. This version of Vitali's theorem
is also proved later in the file.

## Main definitions

* `MeasureTheory.UnifTight`:
  A sequence of functions `f` is uniformly tight if for all `Îµ > 0`, there
  exists some measurable set `s` with finite measure such that the Lp-norm of
  `f i` restricted to `sá¶œ` is smaller than `Îµ` for all `i`.

# Main results

* `MeasureTheory.unifTight_finite`: a finite sequence of Lp functions is uniformly
  tight.
* `MeasureTheory.tendsto_Lp_notFinite_of_tendsto_ae`: a sequence of Lp functions which is uniformly
  integrable and uniformly tight converges in Lp if they converge almost everywhere.
* `MeasureTheory.tendstoInMeasure_notFinite_iff_tendsto_Lp`: Vitali convergence theorem:
  a sequence of Lp functions converges in Lp if and only if it is uniformly integrable,
  uniformly tight and converges in measure.

## Tags
uniform integrable, uniformly absolutely continuous integral, Vitali convergence theorem
-/


namespace MeasureTheory

open Classical Set Filter Topology MeasureTheory NNReal ENNReal

variable {Î± Î² Î¹ : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedAddCommGroup Î²]


/- Counterpart of `tendsto_indicator_ge` from `MeasureTheory.Function.UniformIntegrable`.
   It is used in `lintegral_indicator_compl_le`, so it is more convenient
   to formulate it for `f` valued in `ENNReal`. Could be wrapped with `nnnorm` to make it
   more general. -/
theorem tendsto_ENNReal_indicator_lt (f : Î± â†’ â„â‰¥0âˆ) (x : Î±) :
    Tendsto (fun M : â„• => { x | f x < 1 / (â†‘M + 1) }.indicator f x) atTop (ğ“ 0) := by
  by_cases hfx : f x â‰  0
  Â· refine' tendsto_atTop_of_eventually_const (iâ‚€ := Nat.ceil (1 / f x).toReal) fun n hn => _
    rw [Set.indicator_of_not_mem]
    simp only [not_lt, Set.mem_setOf_eq, one_div, inv_le_iff_inv_le]
    simp only [one_div, ge_iff_le, Nat.ceil_le] at hn
    calc
      (f x)â»Â¹ = .ofReal (f x)â»Â¹.toReal := (ofReal_toReal (inv_ne_top.mpr hfx)).symm
      _       â‰¤ .ofReal n              := ENNReal.ofReal_le_ofReal hn
      _       = â†‘n                     := by norm_cast
      _       â‰¤ â†‘n + 1                 := by norm_num
  Â· refine' tendsto_atTop_of_eventually_const (iâ‚€ := 0) fun n _ => _
    simp only [ne_eq, not_not] at hfx
    simp only [mem_setOf_eq, not_lt, indicator_apply_eq_zero]
    intro; assumption


section UnifTight

/- This follows closely the `UnifIntegrable` section
   from `MeasureTheory.Functions.UniformIntegrable`.-/

variable {f g : Î¹ â†’ Î± â†’ Î²} {p : â„â‰¥0âˆ}


/-- Definition of being Uniformly Tight. -/
def UnifTight {_ : MeasurableSpace Î±} (f : Î¹ â†’ Î± â†’ Î²) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) : Prop :=
  âˆ€ â¦ƒÎµ : â„â‰¥0âˆâ¦„, 0 < Îµ â†’ âˆƒ s : Set Î±, Î¼ s â‰  âˆ âˆ§ âˆ€ i, snorm (sá¶œ.indicator (f i)) p Î¼ â‰¤ Îµ

namespace UnifTight

theorem eventually_cofinite_indicator (hf : UnifTight f p Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆ€á¶  s in Î¼.cofinite.smallSets, âˆ€ i, snorm (s.indicator (f i)) p Î¼ â‰¤ Îµ := by
  rcases hf (pos_iff_ne_zero.2 hÎµ) with âŸ¨s, hÎ¼s, hfsâŸ©
  refine (eventually_smallSets' ?_).2 âŸ¨sá¶œ, ?_, fun i â†¦ hfs iâŸ©
  Â· intro s t hst ht i
    exact (snorm_mono <| norm_indicator_le_of_subset hst _).trans (ht i)
  Â· rwa [Measure.compl_mem_cofinite, lt_top_iff_ne_top]

protected theorem exists_measurableSet_indicator (hf : UnifTight f p Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ s, MeasurableSet s âˆ§ Î¼ s < âˆ âˆ§ âˆ€ i, snorm (sá¶œ.indicator (f i)) p Î¼ â‰¤ Îµ :=
  let âŸ¨s, hÎ¼s, hsm, hfsâŸ© := (hf.eventually_cofinite_indicator hÎµ).exists_measurable_mem_of_smallSets
  âŸ¨sá¶œ, hsm.compl, hÎ¼s, by rwa [compl_compl s]âŸ©

protected theorem add (hf : UnifTight f p Î¼) (hg : UnifTight g p Î¼)
    (hf_meas : âˆ€ i, AEStronglyMeasurable (f i) Î¼) (hg_meas : âˆ€ i, AEStronglyMeasurable (g i) Î¼) :
    UnifTight (f + g) p Î¼ := fun Îµ hÎµ â†¦ by
  rcases exists_Lp_half Î² Î¼ p hÎµ.ne' with âŸ¨Î·, hÎ·_pos, hÎ·âŸ©
  obtain âŸ¨s, hÎ¼s, hsm, hfs, hgsâŸ© :
      âˆƒ s âˆˆ Î¼.cofinite, MeasurableSet s âˆ§
        (âˆ€ i, snorm (s.indicator (f i)) p Î¼ â‰¤ Î·) âˆ§ (âˆ€ i, snorm (s.indicator (g i)) p Î¼ â‰¤ Î·) :=
    ((hf.eventually_cofinite_indicator hÎ·_pos.ne').and
      (hg.eventually_cofinite_indicator hÎ·_pos.ne')).exists_measurable_mem_of_smallSets
  refine âŸ¨sá¶œ, ne_of_lt hÎ¼s, fun i â†¦ ?_âŸ©
  calc
    snorm (indicator sá¶œá¶œ (f i + g i)) p Î¼ = snorm (indicator s (f i) + indicator s (g i)) p Î¼ := by
      rw [compl_compl, indicator_add']
    _ â‰¤ Îµ := le_of_lt <|
      hÎ· _ _ ((hf_meas i).indicator hsm) ((hg_meas i).indicator hsm)
        (hfs i) (hgs i)

protected theorem neg (hf : UnifTight f p Î¼) : UnifTight (-f) p Î¼ := by
  simp_rw [UnifTight, Pi.neg_apply, Set.indicator_neg', snorm_neg]
  exact hf

protected theorem sub (hf : UnifTight f p Î¼) (hg : UnifTight g p Î¼)
    (hf_meas : âˆ€ i, AEStronglyMeasurable (f i) Î¼) (hg_meas : âˆ€ i, AEStronglyMeasurable (g i) Î¼) :
    UnifTight (f - g) p Î¼ := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg hf_meas fun i => (hg_meas i).neg


protected theorem ae_eq (hf : UnifTight f p Î¼) (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
    UnifTight g p Î¼ := by
  intro Îµ hÎµ
  obtain âŸ¨s, hÎ¼s, hfÎµâŸ© := hf hÎµ
  refine' âŸ¨s, hÎ¼s, fun n => (le_of_eq <| snorm_congr_ae _).trans (hfÎµ n)âŸ©
  filter_upwards [hfg n] with x hx
  simp only [indicator, mem_compl_iff, ite_not, hx]

end UnifTight


/-- Core lemma to be used in `MeasureTheory.Memâ„’p.snorm_indicator_compl_le`. -/
theorem lintegral_indicator_compl_le
    {g : Î± â†’ â„â‰¥0âˆ} (haemg : AEMeasurable g Î¼) (hg : âˆ«â» a, g a âˆ‚Î¼ < âˆ)
    {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ (s : Set Î±) (_ : MeasurableSet s) (_ : Î¼ s < âˆ),
      âˆ«â» a, (sá¶œ.indicator g) a âˆ‚Î¼ â‰¤ ENNReal.ofReal Îµ := by
  -- come up with an a.e. equal measurable replacement `f` for `g`
  have hmf := haemg.measurable_mk
  have hgf := haemg.ae_eq_mk
  set f := haemg.mk
  have hf := calc
    âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, g a âˆ‚Î¼ := (lintegral_congr_ae hgf).symm
    _            < âˆ := hg
  simp (config := { zeta := false } /- prevent let expansion -/)
    only [lintegral_congr_ae hgf.indicator]
  have hmeas_lt : âˆ€ M : â„•, MeasurableSet { x | f x < 1 / (â†‘M + 1) } := by
    intro M
    apply measurableSet_lt hmf measurable_const
  have hmeas : âˆ€ M : â„•, Measurable ({ x | f x < 1 / (â†‘M + 1) }.indicator f) := by
    intro M
    apply hmf.indicator
    apply hmeas_lt M
  -- show that the sequence a.e. converges to 0
  have htendsto :
      âˆ€áµ x âˆ‚Î¼, Tendsto (fun M : â„• => { x | f x < 1 / (â†‘M + 1) }.indicator f x) atTop (ğ“ 0) :=
    univ_mem' (id fun x => tendsto_ENNReal_indicator_lt f x)
  -- use Lebesgue dominated convergence to show that the integrals eventually go to zero
  have : Tendsto (fun n : â„• â†¦ âˆ«â» a, { x | f x < 1 / (â†‘n + 1) }.indicator f a âˆ‚Î¼)
      atTop (ğ“ (âˆ«â» (_ : Î±), 0 âˆ‚Î¼)) := by
    refine' tendsto_lintegral_of_dominated_convergence _ hmeas _ hf.ne htendsto
    -- show that the sequence is bounded by f (which is integrable)
    refine' fun n => univ_mem' (id fun x => _)
    by_cases hx : f x < 1 / (â†‘n + 1)
    Â· dsimp
      rwa [Set.indicator_of_mem]
    Â· dsimp
      rw [Set.indicator_of_not_mem]
      Â· exact zero_le _
      Â· assumption
  -- rewrite limit to be more usable and get the sufficiently large M, so the integral is < Îµ
  rw [lintegral_zero, ENNReal.tendsto_atTop_zero] at this
  obtain âŸ¨M, hMâŸ© := this (ENNReal.ofReal Îµ) (ENNReal.ofReal_pos.2 hÎµ)
  simp only [true_and_iff, ge_iff_le, zero_tsub, zero_le, sub_zero, zero_add, coe_nnnorm,
    Set.mem_Icc] at hM
  -- the target estimate is now in hM
  have hM := hM M le_rfl
  -- let s be the complement of the integration domain in hM,
  -- prove its measurability and finite measure
  have : { x | f x < 1 / (â†‘M + 1) } = { x | 1 / (â†‘M + 1) â‰¤ f x }á¶œ := by
    apply Set.ext; intro x
    simp only [mem_compl_iff, mem_setOf_eq, not_le]
  have hms := (hmeas_lt M).compl
  rw [this] at hM hms
  rw [compl_compl] at hms
  have hÎ¼s := calc
    Î¼ { x | 1 / (â†‘M + 1) â‰¤ f x }
      â‰¤ (âˆ«â» a, f a âˆ‚Î¼) / (1 / (â†‘M + 1)) :=
        meas_ge_le_lintegral_div hmf.aemeasurable (by norm_num) (by norm_num)
    _ < âˆ := by apply div_lt_top hf.ne (by norm_num)
  set s := { x | 1 / (â†‘M + 1) â‰¤ f x }
  -- fulfill the goal
  use s, hms, hÎ¼s, hM

/-- A single function that is `Memâ„’p f p Î¼` is tight wrt to `Î¼`. -/
theorem Memâ„’p.snorm_indicator_compl_le (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
    {f : Î± â†’ Î²} (hf : Memâ„’p f p Î¼)
    {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ (s : Set Î±) (_ : MeasurableSet s) (_ : Î¼ s < âˆ),
      snorm (sá¶œ.indicator f) p Î¼ â‰¤ ENNReal.ofReal Îµ := by
  -- The proof unwraps `Memâ„’p f p Î¼` and applies the analogous result for `lintegral`.
  -- do some arithmetic that will come in useful
  have hp_pos := zero_lt_one.trans_le hp_one
  have hp_nz := hp_pos.ne'
  have hrp_pos : 0 < p.toReal := ENNReal.toReal_pos hp_nz hp_top
  have hÎµp : 0 < Îµ ^ p.toReal := by simp only [Real.rpow_pos_of_pos, hÎµ]
  -- decode Memâ„’p into a.e. strong measurability and finite snorm
  obtain âŸ¨haesmf, hsnfâŸ© := hf
  -- transform snorm to lintegral
  rw [snorm_eq_snorm' (by assumption) (by assumption)] at hsnf
  have hinpf := calc
    âˆ«â» a, â€–f aâ€–â‚Š ^ p.toReal âˆ‚Î¼
      = (snorm' f p.toReal Î¼) ^ p.toReal := lintegral_rpow_nnnorm_eq_rpow_snorm' hrp_pos
    _ < âˆ                                := (rpow_lt_top_iff_of_pos hrp_pos).mpr hsnf
  -- get a.e. measurability for the integrand
  -- XXX: Why does `AEStronglyMeasurable.ennnorm` only give the weaker AEMeasurable?
  --      It would make sense to me to use `haesmf.ennnorm.aemeasurable` below.
  have haemnf := haesmf.ennnorm
  have haemnpf := haemnf.pow_const p.toReal
  -- use core result for lintegral (needs only AEMeasurable), the target estimate will be in `hsfÎµ`
  obtain âŸ¨s, hms, hÎ¼s, hsfÎµâŸ© := lintegral_indicator_compl_le haemnpf hinpf hÎµp
  use s, hms, hÎ¼s
  -- move indicator through function compositions, XXX: is this simp-able?
  rw [â† Function.comp_def (fun x : â„â‰¥0âˆ => x ^ p.toReal)] at hsfÎµ
  rw [â† Function.comp_def ENNReal.ofNNReal] at hsfÎµ
  rw [â† Function.comp_def nnnorm] at hsfÎµ
  rw [sá¶œ.indicator_comp_of_zero (@ENNReal.zero_rpow_of_pos p.toReal hrp_pos)] at hsfÎµ
  rw [sá¶œ.indicator_comp_of_zero ENNReal.coe_zero] at hsfÎµ
  rw [sá¶œ.indicator_comp_of_zero nnnorm_zero] at hsfÎµ
  rw [Function.comp_def nnnorm] at hsfÎµ
  rw [Function.comp_def ENNReal.ofNNReal] at hsfÎµ
  rw [Function.comp_def (fun x : â„â‰¥0âˆ => x ^ p.toReal)] at hsfÎµ
  -- convert lintegral to snorm
  rw [lintegral_rpow_nnnorm_eq_rpow_snorm' hrp_pos] at hsfÎµ
  rw [â† snorm_eq_snorm' (by assumption) (by assumption)] at hsfÎµ
  -- commute ENNReal coersion with rpow, use rpow monotonicity
  rw [â† ofReal_rpow_of_pos (by assumption)] at hsfÎµ
  rw [ENNReal.rpow_le_rpow_iff hrp_pos] at hsfÎµ
  exact hsfÎµ

/-- A constant function is tight. -/
theorem unifTight_const {g : Î± â†’ Î²} (hp : 1 â‰¤ p) (hp_ne_top : p â‰  âˆ) (hg : Memâ„’p g p Î¼) :
    UnifTight (fun _ : Î¹ => g) p Î¼ := fun Îµ hÎµ â†¦ by
  by_cases hÎµ_top : Îµ = âˆ
  Â· exact âŸ¨âˆ…, (by measurability), fun _ => hÎµ_top.symm â–¸ le_topâŸ©
  have hrÎµ : 0 < Îµ.toReal := ENNReal.toReal_pos hÎµ.ne' hÎµ_top
  obtain âŸ¨s, _, hÎ¼s, hgÎµâŸ© := hg.snorm_indicator_compl_le hp hp_ne_top hrÎµ
  exact âŸ¨s, ne_of_lt hÎ¼s, fun _ => hgÎµ.trans_eq (ENNReal.ofReal_toReal hÎµ_top)âŸ©

/-- A single function is tight. -/
theorem unifTight_subsingleton [Subsingleton Î¹] (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
    {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, Memâ„’p (f i) p Î¼) : UnifTight f p Î¼ := fun Îµ hÎµ â†¦ by
  by_cases hÎµ_top : Îµ = âˆ
  Â· exact âŸ¨âˆ…, (by measurability), fun _ => hÎµ_top.symm â–¸ le_topâŸ©
  have hrÎµ : 0 < Îµ.toReal := ENNReal.toReal_pos hÎµ.ne' hÎµ_top
  by_cases hÎ¹ : Nonempty Î¹
  case neg => exact âŸ¨âˆ…, (by measurability), fun i => False.elim <| hÎ¹ <| Nonempty.intro iâŸ©
  cases' hÎ¹ with i
  obtain âŸ¨s, _, hÎ¼s, hfÎµâŸ© := (hf i).snorm_indicator_compl_le hp_one hp_top hrÎµ
  refine' âŸ¨s, ne_of_lt hÎ¼s, fun j => _âŸ©
  convert hfÎµ
  exact (ENNReal.ofReal_toReal hÎµ_top).symm


/-- This lemma is less general than `MeasureTheory.unifIntegrable_finite` which applies to
all sequences indexed by a finite type. -/
theorem unifTight_fin (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ) {n : â„•} {f : Fin n â†’ Î± â†’ Î²}
    (hf : âˆ€ i, Memâ„’p (f i) p Î¼) : UnifTight f p Î¼ := by
  revert f
  induction' n with n h
  Â· intro f hf
    have : Subsingleton (Fin Nat.zero) := subsingleton_fin_zero -- Porting note: Added this instance
    exact unifTight_subsingleton hp_one hp_top hf
  intro f hfLp Îµ hÎµ
  by_cases hÎµ_top : Îµ = âˆ
  Â· exact âŸ¨âˆ…, (by measurability), fun _ => hÎµ_top.symm â–¸ le_topâŸ©
  have hrÎµ : 0 < Îµ.toReal := ENNReal.toReal_pos hÎµ.ne' hÎµ_top
  let g : Fin n â†’ Î± â†’ Î² := fun k => f k
  have hgLp : âˆ€ i, Memâ„’p (g i) p Î¼ := fun i => hfLp i
  obtain âŸ¨S, hÎ¼S, hFÎµâŸ© := h hgLp hÎµ
  obtain âŸ¨s, _, hÎ¼s, hfÎµâŸ© := (hfLp n).snorm_indicator_compl_le hp_one hp_top hrÎµ
  refine' âŸ¨s âˆª S, (by measurability), fun i => _âŸ©
  by_cases hi : i.val < n
  Â· rw [(_ : f i = g âŸ¨i.val, hiâŸ©)]
    Â· rw [compl_union, â† indicator_indicator]
      apply (snorm_indicator_le _).trans
      exact hFÎµ (Fin.castLT i hi)
    Â· simp only [Fin.coe_eq_castSucc, Fin.castSucc_mk, Fin.eta]
  Â· rw [(_ : i = n)]
    Â· rw [compl_union, inter_comm, â† indicator_indicator]
      apply (snorm_indicator_le _).trans
      convert hfÎµ
      exact (ENNReal.ofReal_toReal hÎµ_top).symm
    Â· have hi' := Fin.is_lt i
      rw [Nat.lt_succ_iff] at hi'
      rw [not_lt] at hi
      -- Porting note: Original proof was `simp [â† le_antisymm hi' hi]`
      ext; symm; rw [Fin.coe_ofNat_eq_mod, le_antisymm hi' hi, Nat.mod_succ_eq_iff_lt, Nat.lt_succ]

/-- A finite sequence of Lp functions is uniformly integrable. -/
theorem unifTight_finite [Finite Î¹] (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ) {f : Î¹ â†’ Î± â†’ Î²}
    (hf : âˆ€ i, Memâ„’p (f i) p Î¼) : UnifTight f p Î¼ := fun Îµ hÎµ â†¦ by
  obtain âŸ¨n, hnâŸ© := Finite.exists_equiv_fin Î¹
  set g : Fin n â†’ Î± â†’ Î² := f âˆ˜ hn.some.symm
  have hg : âˆ€ i, Memâ„’p (g i) p Î¼ := fun _ => hf _
  obtain âŸ¨s, hÎ¼s, hfÎµâŸ© := unifTight_fin hp_one hp_top hg hÎµ
  refine' âŸ¨s, hÎ¼s, fun i => _âŸ©
  specialize hfÎµ (hn.some i)
  unfold_let g at hfÎµ
  simp_rw [Function.comp_apply, Equiv.symm_apply_apply] at hfÎµ
  assumption

end UnifTight


section VitaliConvergence

/- XXX: In the analogous place in `MeasureTheory.Function.UniformIntegrable`, the measure variable
   is declared `(Î¼)` non-implicit. I don't see why, as in all relevant cases it can be
   deduced from other arguments. -/
variable {Î¼ : Measure Î±} {p : â„â‰¥0âˆ}

variable {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²}

/- Both directions and an iff version of Vitali's convergence theorem on measure spaces
   of not necesserily finite volume. See `Thm III.6.15` of Dunford & Schwartz, Part I (1958). -/

/- We start with the reverse direction. We only need to show that uniform tightness follows
   from convergence in Lp. Mathlib already has the analogous `unifIntegrable_of_tendsto_Lp`
   and `tendstoInMeasure_of_tendsto_snorm`. -/

/-- Intermediate lemma for `unifTight_of_tendsto_Lp`. -/
theorem unifTight_of_tendsto_Lp_zero (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) (hf : âˆ€ n, Memâ„’p (f n) p Î¼)
    (hf_tendsto : Tendsto (fun n => snorm (f n) p Î¼) atTop (ğ“ 0)) : UnifTight f p Î¼ := fun Îµ hÎµ â†¦by
  rw [ENNReal.tendsto_atTop_zero] at hf_tendsto
  obtain âŸ¨N, hNÎµâŸ© := hf_tendsto Îµ (by simpa only [gt_iff_lt, ofReal_pos])
  let F : Fin N â†’ Î± â†’ Î² := fun n => f n
  have hF : âˆ€ n, Memâ„’p (F n) p Î¼ := fun n => hf n
  obtain âŸ¨s, hÎ¼s, hFÎµâŸ© := unifTight_fin hp hp' hF hÎµ
  refine' âŸ¨s, hÎ¼s, fun n => _âŸ©
  by_cases hn : n < N
  Â· exact hFÎµ âŸ¨n, hnâŸ©
  Â· exact (snorm_indicator_le _).trans (hNÎµ n (not_lt.mp hn))

/-- Convergence in Lp implies uniform tightness. -/
theorem unifTight_of_tendsto_Lp (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) (hf : âˆ€ n, Memâ„’p (f n) p Î¼)
    (hg : Memâ„’p g p Î¼) (hfg : Tendsto (fun n => snorm (f n - g) p Î¼) atTop (ğ“ 0)) :
    UnifTight f p Î¼ := by
  have : f = (fun _ => g) + fun n => f n - g := by ext1 n; simp
  rw [this]
  refine' UnifTight.add _ _ (fun _ => hg.aestronglyMeasurable)
      fun n => (hf n).1.sub hg.aestronglyMeasurable
  Â· exact unifTight_const hp hp' hg
  Â· exact unifTight_of_tendsto_Lp_zero hp hp' (fun n => (hf n).sub hg) hfg


/- Next we deal with the forward direction. The `Memâ„’p` and `TendstoInMeasure` hypotheses
   are unwrapped and strengthened to by known lemmas to have in addition `StronglyMeasurable`
   and a.e. convergence. The bulk of the proof is done under these stronger hyptheses. -/

theorem tendsto_Lp_notFinite_of_tendsto_ae_of_meas (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    (hg' : Memâ„’p g p Î¼) (hui : UnifIntegrable f p Î¼) (hut : UnifTight f p Î¼)
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    Tendsto (fun n => snorm (f n - g) p Î¼) atTop (ğ“ 0) := by
  rw [ENNReal.tendsto_atTop_zero]
  intro Îµ hÎµ
  by_cases hfinÎµ : Îµ â‰  âˆ; swap
  Â· rw [not_ne_iff.mp hfinÎµ]; exact âŸ¨0, fun n _ => le_topâŸ©
  by_cases hÎ¼ : Î¼ = 0
  Â· rw [hÎ¼]; use 0; intro n _; rw [snorm_measure_zero]; exact zero_le Îµ
  have hÎµ'' : Îµ / 3 â‰  âˆ := (ENNReal.div_lt_top (by assumption) (by norm_num)).ne
  have hÎµ' : 0 < Îµ / 3 := ENNReal.div_pos hÎµ.ne' (coe_ne_top)
  have hrÎµ' : 0 < (Îµ / 3).toReal := ENNReal.toReal_pos hÎµ'.ne' (by assumption)
  -- use tightness to divide the domain into interior and exterior
  obtain âŸ¨Eg, hmEg, hÎ¼Eg, hgÎµâŸ© := Memâ„’p.snorm_indicator_compl_le hp hp' hg' hrÎµ'
  obtain âŸ¨Ef, hmEf, hÎ¼Ef, hfÎµâŸ© := hut.exists_measurableSet_indicator hÎµ'.ne'
  have hmE := hmEf.union hmEg
  have hfmE := (measure_union_le Ef Eg).trans_lt (add_lt_top.mpr âŸ¨hÎ¼Ef, hÎ¼EgâŸ©)
  set E : Set Î± := Ef âˆª Eg
  -- use uniform integrability to get control on the limit over E
  have hgE' := Memâ„’p.restrict E hg'
  have huiE := hui.restrict  E
  have hfgE : (âˆ€áµ x âˆ‚(Î¼.restrict E), Tendsto (fun n => f n x) atTop (ğ“ (g x))) :=
    ae_restrict_of_ae hfg
  -- `tendsto_Lp_of_tendsto_ae_of_meas` needs to
  -- synthesize an argument `[IsFiniteMeasure (Î¼.restrict E)]`.
  -- It is enough to have in the context a term of `Fact (Î¼ E < âˆ)`, which is our `ffmE` below,
  -- which is automatically fed into `Restrict.isFiniteInstance`.
  have ffmE : Fact _ := { out := hfmE }
  have hInner := tendsto_Lp_of_tendsto_ae_of_meas (Î¼.restrict E) hp hp' hf hg hgE' huiE hfgE
  rw [ENNReal.tendsto_atTop_zero] at hInner
  -- get a sufficiently large N for given Îµ, and consider any n â‰¥ N
  obtain âŸ¨N, hfngÎµâŸ© := hInner (Îµ / 3) hÎµ'
  use N; intro n hn
  -- get interior estimates
  have hmfngE : AEStronglyMeasurable _ Î¼ := (((hf n).sub hg).indicator hmE).aestronglyMeasurable
  have hfngEÎµ := calc
    snorm (E.indicator (f n - g)) p Î¼
      = snorm (f n - g) p (Î¼.restrict E) := snorm_indicator_eq_snorm_restrict hmE
    _ â‰¤ Îµ / 3                            := hfngÎµ n hn
  -- get exterior estimates
  have hmgEc : AEStronglyMeasurable _ Î¼ := (hg.indicator hmE.compl).aestronglyMeasurable
  have hgEcÎµ := calc
    snorm (Eá¶œ.indicator g) p Î¼
      â‰¤ snorm (Efá¶œ.indicator (Egá¶œ.indicator g)) p Î¼ := by
        unfold_let E; rw [compl_union, â† indicator_indicator]
    _ â‰¤ snorm (Egá¶œ.indicator g) p Î¼ := snorm_indicator_le _
    _ â‰¤ .ofReal (Îµ / 3).toReal := hgÎµ
    _ = Îµ / 3 := ENNReal.ofReal_toReal hÎµ''
  have hmfnEc : AEStronglyMeasurable _ Î¼ := ((hf n).indicator hmE.compl).aestronglyMeasurable
  have hfnEcÎµ : snorm (Eá¶œ.indicator (f n)) p Î¼ â‰¤ Îµ / 3 := calc
    snorm (Eá¶œ.indicator (f n)) p Î¼
      â‰¤ snorm (Egá¶œ.indicator (Efá¶œ.indicator (f n))) p Î¼ := by
        unfold_let E; rw [compl_union, inter_comm, â† indicator_indicator]
    _ â‰¤ snorm (Efá¶œ.indicator (f n)) p Î¼ := snorm_indicator_le _
    _ â‰¤ Îµ / 3 := hfÎµ n
  have hmfngEc : AEStronglyMeasurable _ Î¼ :=
    (((hf n).sub hg).indicator hmE.compl).aestronglyMeasurable
  have hfngEcÎµ := calc
    snorm (Eá¶œ.indicator (f n - g)) p Î¼
      = snorm (Eá¶œ.indicator (f n) - Eá¶œ.indicator g) p Î¼ := by
        rw [(Eá¶œ.indicator_sub' _ _)]
    _ â‰¤ snorm (Eá¶œ.indicator (f n)) p Î¼ + snorm (Eá¶œ.indicator g) p Î¼ := by
        apply snorm_sub_le (by assumption) (by assumption) hp
    _ â‰¤ Îµ / 3 + Îµ / 3 := add_le_add hfnEcÎµ hgEcÎµ
  -- finally, combine interior and exterior estimates
  calc
    snorm (f n - g) p Î¼
      = snorm (Eá¶œ.indicator (f n - g) + E.indicator (f n - g)) p Î¼ := by
        congr; exact (E.indicator_compl_add_self _).symm
    _ â‰¤ snorm (indicator Eá¶œ (f n - g)) p Î¼ + snorm (indicator E (f n - g)) p Î¼ := by
        apply snorm_add_le (by assumption) (by assumption) hp
    _ â‰¤ (Îµ / 3 + Îµ / 3) + Îµ / 3 := add_le_add hfngEcÎµ hfngEÎµ
    _ = Îµ := by simp only [ENNReal.add_thirds] --ENNReal.add_thirds Îµ

/- Lemma used in `tendsto_Lp_notFinite_of_tendsto_ae`.
   XXX: Alternative name: `ae_tendsto_ae_congr`? -/
theorem tendsto_ae_congr_ae {f f' : â„• â†’ Î± â†’ Î²} {g g' : Î± â†’ Î²}
    (hff' : âˆ€ (n : â„•), f n =áµ[Î¼] f' n) (hgg' : g =áµ[Î¼] g')
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f' n x) atTop (ğ“ (g' x)) := by
  have hff'' := eventually_countable_forall.mpr hff'
  filter_upwards [hff'', hgg', hfg] with x hff'x hgg'x hfgx
  apply Tendsto.congr hff'x
  rw [â† hgg'x]; exact hfgx

theorem tendsto_Lp_notFinite_of_tendsto_ae (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (haef : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (hg' : Memâ„’p g p Î¼) (hui : UnifIntegrable f p Î¼) (hut : UnifTight f p Î¼)
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    Tendsto (fun n => snorm (f n - g) p Î¼) atTop (ğ“ 0) := by
  -- come up with an a.e. equal strongly measurable replacement `f` for `g`
  have hf := fun n => (haef n).stronglyMeasurable_mk
  have hff' := fun n => (haef n).ae_eq_mk (Î¼ := Î¼)
  have hui' := hui.ae_eq hff'
  have hut' := hut.ae_eq hff'
  have hg := hg'.aestronglyMeasurable.stronglyMeasurable_mk
  have hgg' := hg'.aestronglyMeasurable.ae_eq_mk (Î¼ := Î¼)
  have hg'' := hg'.ae_eq hgg'
  have haefg' := tendsto_ae_congr_ae hff' hgg' hfg
  set f' := fun n => (haef n).mk (Î¼ := Î¼)
  set g' := hg'.aestronglyMeasurable.mk (Î¼ := Î¼)
  have haefg (n : â„•) : f n - g =áµ[Î¼] f' n - g' := (hff' n).sub hgg'
  have hsnfg (n : â„•) := snorm_congr_ae (p := p) (haefg n)
  apply Filter.Tendsto.congr (fun n => (hsnfg n).symm)
  exact tendsto_Lp_notFinite_of_tendsto_ae_of_meas hp hp' hf hg hg'' hui' hut' haefg'


/-- Forward direction of Vitali's convergence theorem (non-finite version):
    if `f` is a sequence of uniformly integrable, uniformly tight functions that converge in
    measure to some function `g` in a finite measure space, then `f` converge in Lp to `g`. -/
theorem tendsto_Lp_notFinite_of_tendstoInMeasure (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hg : Memâ„’p g p Î¼)
    (hui : UnifIntegrable f p Î¼) (hut : UnifTight f p Î¼)
    (hfg : TendstoInMeasure Î¼ f atTop g) : Tendsto (fun n => snorm (f n - g) p Î¼) atTop (ğ“ 0) := by
  refine' tendsto_of_subseq_tendsto fun ns hns => _
  obtain âŸ¨ms, _, hms'âŸ© := TendstoInMeasure.exists_seq_tendsto_ae fun Îµ hÎµ => (hfg Îµ hÎµ).comp hns
  exact âŸ¨ms,
    tendsto_Lp_notFinite_of_tendsto_ae hp hp' (fun _ => hf _) hg
      (fun Îµ hÎµ => -- `UnifIntegrable` on a subsequence
        let âŸ¨Î´, hÎ´, hÎ´'âŸ© := hui hÎµ
        âŸ¨Î´, hÎ´, fun i s hs hÎ¼s => hÎ´' _ s hs hÎ¼sâŸ©)
      (fun Îµ hÎµ => -- `UnifTight` on a subsequence
        let âŸ¨s, hÎ¼s, hfÎµâŸ© := hut hÎµ
        âŸ¨s, hÎ¼s, fun i => hfÎµ _âŸ©)
      hms'âŸ©


/-- **Vitali's convergence theorem** (non-finite measure version):
    A sequence of functions `f` converges to `g` in Lp
    if and only if it is uniformly integrable, uniformly tight and to `g` in measure. -/
-- XXX: logically, this should be renamed to `tendstoInMeasure_iff_tendsto_Lp`, while
--  the current version of that could be renamed to `tendstoInMeasure_iff_tendsto_Lp_of_isFinite`.
theorem tendstoInMeasure_notFinite_iff_tendsto_Lp (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    (hf : âˆ€ n, Memâ„’p (f n) p Î¼) (hg : Memâ„’p g p Î¼) :
    TendstoInMeasure Î¼ f atTop g âˆ§ UnifIntegrable f p Î¼ âˆ§ UnifTight f p Î¼
      â†” Tendsto (fun n => snorm (f n - g) p Î¼) atTop (ğ“ 0) :=
  âŸ¨fun h => tendsto_Lp_notFinite_of_tendstoInMeasure hp hp' (fun n => (hf n).1) hg h.2.1 h.2.2 h.1,
    fun h =>
    âŸ¨tendstoInMeasure_of_tendsto_snorm (lt_of_lt_of_le zero_lt_one hp).ne'
        (fun n => (hf n).aestronglyMeasurable) hg.aestronglyMeasurable h,
      unifIntegrable_of_tendsto_Lp Î¼ hp hp' hf hg h,
      unifTight_of_tendsto_Lp hp hp' hf hg hâŸ©âŸ©


end VitaliConvergence


end MeasureTheory
