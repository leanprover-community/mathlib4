/-
Copyright (c) 2023 Igor Khavkine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Igor Khavkine
-/
import Mathlib.MeasureTheory.Function.ConvergenceInMeasure
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.MeasureTheory.Function.UniformIntegrable

/-!
# Uniform tightness

This file contains the definitions for uniform tightness for a family of Lp functions.
It is used as a hypothesis to the version of Vitali's convergence theorem for Lp spaces
that works also for spaces of infinite measure. This version of Vitali's theorem
is also proved later in the file.

## Main definitions

* `MeasureTheory.UnifTight`:
  A sequence of functions `f` is uniformly tight in `L^p` if for all `Œµ > 0`, there
  exists some measurable set `s` with finite measure such that the Lp-norm of
  `f i` restricted to `s·∂ú` is smaller than `Œµ` for all `i`.

# Main results

* `MeasureTheory.unifTight_finite`: a finite sequence of Lp functions is uniformly
  tight.
* `MeasureTheory.tendsto_Lp_notFinite_of_tendsto_ae`: a sequence of Lp functions which is uniformly
  integrable and uniformly tight converges in Lp if they converge almost everywhere.
* `MeasureTheory.tendstoInMeasure_notFinite_iff_tendsto_Lp`: Vitali convergence theorem:
  a sequence of Lp functions converges in Lp if and only if it is uniformly integrable,
  uniformly tight and converges in measure.

## Tags
uniform integrable, uniformly absolutely continuous integral, Vitali convergence theorem
-/


namespace MeasureTheory

open Classical Set Filter Topology MeasureTheory NNReal ENNReal

variable {Œ± Œ≤ Œπ : Type*} {m : MeasurableSpace Œ±} {Œº : Measure Œ±} [NormedAddCommGroup Œ≤]



section UnifTight

/- This follows closely the `UnifIntegrable` section
   from `MeasureTheory.Functions.UniformIntegrable`.-/

variable {f g : Œπ ‚Üí Œ± ‚Üí Œ≤} {p : ‚Ñù‚â•0‚àû}


/-- Definition of being Uniformly Tight. -/
def UnifTight {_ : MeasurableSpace Œ±} (f : Œπ ‚Üí Œ± ‚Üí Œ≤) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : Prop :=
  ‚àÄ ‚¶ÉŒµ : ‚Ñù‚â•0‚àû‚¶Ñ, 0 < Œµ ‚Üí ‚àÉ s : Set Œ±, Œº s ‚â† ‚àû ‚àß ‚àÄ i, snorm (s·∂ú.indicator (f i)) p Œº ‚â§ Œµ

namespace UnifTight

theorem eventually_cofinite_indicator (hf : UnifTight f p Œº) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
    ‚àÄ·∂† s in Œº.cofinite.smallSets, ‚àÄ i, snorm (s.indicator (f i)) p Œº ‚â§ Œµ := by
  rcases hf (pos_iff_ne_zero.2 hŒµ) with ‚ü®s, hŒºs, hfs‚ü©
  refine (eventually_smallSets' ?_).2 ‚ü®s·∂ú, ?_, fun i ‚Ü¶ hfs i‚ü©
  ¬∑ intro s t hst ht i
    exact (snorm_mono <| norm_indicator_le_of_subset hst _).trans (ht i)
  ¬∑ rwa [Measure.compl_mem_cofinite, lt_top_iff_ne_top]

protected theorem exists_measurableSet_indicator (hf : UnifTight f p Œº) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
    ‚àÉ s, MeasurableSet s ‚àß Œº s < ‚àû ‚àß ‚àÄ i, snorm (s·∂ú.indicator (f i)) p Œº ‚â§ Œµ :=
  let ‚ü®s, hŒºs, hsm, hfs‚ü© := (hf.eventually_cofinite_indicator hŒµ).exists_measurable_mem_of_smallSets
  ‚ü®s·∂ú, hsm.compl, hŒºs, by rwa [compl_compl s]‚ü©

protected theorem add (hf : UnifTight f p Œº) (hg : UnifTight g p Œº)
    (hf_meas : ‚àÄ i, AEStronglyMeasurable (f i) Œº) (hg_meas : ‚àÄ i, AEStronglyMeasurable (g i) Œº) :
    UnifTight (f + g) p Œº := fun Œµ hŒµ ‚Ü¶ by
  rcases exists_Lp_half Œ≤ Œº p hŒµ.ne' with ‚ü®Œ∑, hŒ∑_pos, hŒ∑‚ü©
  obtain ‚ü®s, hŒºs, hsm, hfs, hgs‚ü© :
      ‚àÉ s ‚àà Œº.cofinite, MeasurableSet s ‚àß
        (‚àÄ i, snorm (s.indicator (f i)) p Œº ‚â§ Œ∑) ‚àß (‚àÄ i, snorm (s.indicator (g i)) p Œº ‚â§ Œ∑) :=
    ((hf.eventually_cofinite_indicator hŒ∑_pos.ne').and
      (hg.eventually_cofinite_indicator hŒ∑_pos.ne')).exists_measurable_mem_of_smallSets
  refine ‚ü®s·∂ú, ne_of_lt hŒºs, fun i ‚Ü¶ ?_‚ü©
  calc
    snorm (indicator s·∂ú·∂ú (f i + g i)) p Œº = snorm (indicator s (f i) + indicator s (g i)) p Œº := by
      rw [compl_compl, indicator_add']
    _ ‚â§ Œµ := le_of_lt <|
      hŒ∑ _ _ ((hf_meas i).indicator hsm) ((hg_meas i).indicator hsm)
        (hfs i) (hgs i)

protected theorem neg (hf : UnifTight f p Œº) : UnifTight (-f) p Œº := by
  simp_rw [UnifTight, Pi.neg_apply, Set.indicator_neg', snorm_neg]
  exact hf

protected theorem sub (hf : UnifTight f p Œº) (hg : UnifTight g p Œº)
    (hf_meas : ‚àÄ i, AEStronglyMeasurable (f i) Œº) (hg_meas : ‚àÄ i, AEStronglyMeasurable (g i) Œº) :
    UnifTight (f - g) p Œº := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg hf_meas fun i => (hg_meas i).neg


protected theorem ae_eq (hf : UnifTight f p Œº) (hfg : ‚àÄ n, f n =·µê[Œº] g n) :
    UnifTight g p Œº := by
  intro Œµ hŒµ
  obtain ‚ü®s, hŒºs, hfŒµ‚ü© := hf hŒµ
  refine ‚ü®s, hŒºs, fun n => (le_of_eq <| snorm_congr_ae ?_).trans (hfŒµ n)‚ü©
  filter_upwards [hfg n] with x hx
  simp only [indicator, mem_compl_iff, ite_not, hx]

end UnifTight

/-- A constant function is tight. -/
theorem unifTight_const {g : Œ± ‚Üí Œ≤} (hp_ne_top : p ‚â† ‚àû) (hg : Mem‚Ñíp g p Œº) :
    UnifTight (fun _ : Œπ => g) p Œº := by
  intro Œµ hŒµ
  by_cases hŒµ_top : Œµ = ‚àû
  ¬∑ exact ‚ü®‚àÖ, (by measurability), fun _ => hŒµ_top.symm ‚ñ∏ le_top‚ü©
  obtain ‚ü®s, _, hŒºs, hgŒµ‚ü© := hg.exists_snorm_indicator_compl_lt hp_ne_top hŒµ.ne'
  exact ‚ü®s, ne_of_lt hŒºs, fun _ => hgŒµ.le‚ü©

/-- A single function is tight. -/
theorem unifTight_subsingleton [Subsingleton Œπ] (hp_top : p ‚â† ‚àû)
    {f : Œπ ‚Üí Œ± ‚Üí Œ≤} (hf : ‚àÄ i, Mem‚Ñíp (f i) p Œº) : UnifTight f p Œº := fun Œµ hŒµ ‚Ü¶ by
  by_cases hŒµ_top : Œµ = ‚àû
  ¬∑ exact ‚ü®‚àÖ, by measurability, fun _ => hŒµ_top.symm ‚ñ∏ le_top‚ü©
  by_cases hŒπ : Nonempty Œπ
  case neg => exact ‚ü®‚àÖ, (by measurability), fun i => False.elim <| hŒπ <| Nonempty.intro i‚ü©
  cases' hŒπ with i
  obtain ‚ü®s, _, hŒºs, hfŒµ‚ü© := (hf i).exists_snorm_indicator_compl_lt hp_top hŒµ.ne'
  refine ‚ü®s, ne_of_lt hŒºs, fun j => ?_‚ü©
  convert hfŒµ.le


/-- This lemma is less general than `MeasureTheory.unifTight_finite` which applies to
all sequences indexed by a finite type. -/
theorem unifTight_fin (hp_top : p ‚â† ‚àû) {n : ‚Ñï} {f : Fin n ‚Üí Œ± ‚Üí Œ≤}
    (hf : ‚àÄ i, Mem‚Ñíp (f i) p Œº) : UnifTight f p Œº := by
  revert f
  induction' n with n h
  ¬∑ intro f hf
    have : Subsingleton (Fin Nat.zero) := subsingleton_fin_zero -- Porting note: Added this instance
    exact unifTight_subsingleton hp_top hf
  intro f hfLp Œµ hŒµ
  by_cases hŒµ_top : Œµ = ‚àû
  ¬∑ exact ‚ü®‚àÖ, (by measurability), fun _ => hŒµ_top.symm ‚ñ∏ le_top‚ü©
  let g : Fin n ‚Üí Œ± ‚Üí Œ≤ := fun k => f k
  have hgLp : ‚àÄ i, Mem‚Ñíp (g i) p Œº := fun i => hfLp i
  obtain ‚ü®S, hŒºS, hFŒµ‚ü© := h hgLp hŒµ
  obtain ‚ü®s, _, hŒºs, hfŒµ‚ü© := (hfLp n).exists_snorm_indicator_compl_lt hp_top hŒµ.ne'
  refine ‚ü®s ‚à™ S, (by measurability), fun i => ?_‚ü©
  by_cases hi : i.val < n
  ¬∑ rw [(_ : f i = g ‚ü®i.val, hi‚ü©)]
    ¬∑ rw [compl_union, ‚Üê indicator_indicator]
      apply (snorm_indicator_le _).trans
      exact hFŒµ (Fin.castLT i hi)
    ¬∑ simp only [Fin.coe_eq_castSucc, Fin.castSucc_mk, g]
  ¬∑ rw [(_ : i = n)]
    ¬∑ rw [compl_union, inter_comm, ‚Üê indicator_indicator]
      apply (snorm_indicator_le _).trans
      convert hfŒµ.le
    ¬∑ have hi' := Fin.is_lt i
      rw [Nat.lt_succ_iff] at hi'
      rw [not_lt] at hi
      -- Porting note: Original proof was `simp [‚Üê le_antisymm hi' hi]`
      ext; symm; rw [Fin.coe_ofNat_eq_mod, le_antisymm hi' hi, Nat.mod_succ_eq_iff_lt, Nat.lt_succ]

/-- A finite sequence of Lp functions is uniformly tight. -/
theorem unifTight_finite [Finite Œπ] (hp_top : p ‚â† ‚àû) {f : Œπ ‚Üí Œ± ‚Üí Œ≤}
    (hf : ‚àÄ i, Mem‚Ñíp (f i) p Œº) : UnifTight f p Œº := fun Œµ hŒµ ‚Ü¶ by
  obtain ‚ü®n, hn‚ü© := Finite.exists_equiv_fin Œπ
  set g : Fin n ‚Üí Œ± ‚Üí Œ≤ := f ‚àò hn.some.symm
  have hg : ‚àÄ i, Mem‚Ñíp (g i) p Œº := fun _ => hf _
  obtain ‚ü®s, hŒºs, hfŒµ‚ü© := unifTight_fin hp_top hg hŒµ
  refine ‚ü®s, hŒºs, fun i => ?_‚ü©
  specialize hfŒµ (hn.some i)
  unfold_let g at hfŒµ
  simp_rw [Function.comp_apply, Equiv.symm_apply_apply] at hfŒµ
  assumption

end UnifTight


section VitaliConvergence

variable {Œº : Measure Œ±} {p : ‚Ñù‚â•0‚àû}

variable {f : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≤}

/- Both directions and an iff version of Vitali's convergence theorem on measure spaces
   of not necesserily finite volume. See `Thm III.6.15` of Dunford & Schwartz, Part I (1958). -/

/- We start with the reverse direction. We only need to show that uniform tightness follows
   from convergence in Lp. Mathlib already has the analogous `unifIntegrable_of_tendsto_Lp`
   and `tendstoInMeasure_of_tendsto_snorm`. -/

/-- Intermediate lemma for `unifTight_of_tendsto_Lp`. -/
theorem unifTight_of_tendsto_Lp_zero (hp' : p ‚â† ‚àû) (hf : ‚àÄ n, Mem‚Ñíp (f n) p Œº)
    (hf_tendsto : Tendsto (fun n => snorm (f n) p Œº) atTop (ùìù 0)) : UnifTight f p Œº := fun Œµ hŒµ ‚Ü¶by
  rw [ENNReal.tendsto_atTop_zero] at hf_tendsto
  obtain ‚ü®N, hNŒµ‚ü© := hf_tendsto Œµ (by simpa only [gt_iff_lt, ofReal_pos])
  let F : Fin N ‚Üí Œ± ‚Üí Œ≤ := fun n => f n
  have hF : ‚àÄ n, Mem‚Ñíp (F n) p Œº := fun n => hf n
  obtain ‚ü®s, hŒºs, hFŒµ‚ü© := unifTight_fin hp' hF hŒµ
  refine ‚ü®s, hŒºs, fun n => ?_‚ü©
  by_cases hn : n < N
  ¬∑ exact hFŒµ ‚ü®n, hn‚ü©
  ¬∑ exact (snorm_indicator_le _).trans (hNŒµ n (not_lt.mp hn))

/-- Convergence in Lp implies uniform tightness. -/
theorem unifTight_of_tendsto_Lp (hp' : p ‚â† ‚àû) (hf : ‚àÄ n, Mem‚Ñíp (f n) p Œº)
    (hg : Mem‚Ñíp g p Œº) (hfg : Tendsto (fun n => snorm (f n - g) p Œº) atTop (ùìù 0)) :
    UnifTight f p Œº := by
  have : f = (fun _ => g) + fun n => f n - g := by ext1 n; simp
  rw [this]
  refine UnifTight.add ?_ ?_ (fun _ => hg.aestronglyMeasurable)
      fun n => (hf n).1.sub hg.aestronglyMeasurable
  ¬∑ exact unifTight_const hp' hg
  ¬∑ exact unifTight_of_tendsto_Lp_zero hp' (fun n => (hf n).sub hg) hfg


/- Next we deal with the forward direction. The `Mem‚Ñíp` and `TendstoInMeasure` hypotheses
   are unwrapped and strengthened to by known lemmas to have in addition `StronglyMeasurable`
   and a.e. convergence. The bulk of the proof is done under these stronger hyptheses. -/

theorem tendsto_Lp_notFinite_of_tendsto_ae_of_meas (hp : 1 ‚â§ p) (hp' : p ‚â† ‚àû)
    {f : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≤} (hf : ‚àÄ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    (hg' : Mem‚Ñíp g p Œº) (hui : UnifIntegrable f p Œº) (hut : UnifTight f p Œº)
    (hfg : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f n x) atTop (ùìù (g x))) :
    Tendsto (fun n => snorm (f n - g) p Œº) atTop (ùìù 0) := by
  rw [ENNReal.tendsto_atTop_zero]
  intro Œµ hŒµ
  by_cases hfinŒµ : Œµ ‚â† ‚àû; swap
  ¬∑ rw [not_ne_iff.mp hfinŒµ]; exact ‚ü®0, fun n _ => le_top‚ü©
  by_cases hŒº : Œº = 0
  ¬∑ rw [hŒº]; use 0; intro n _; rw [snorm_measure_zero]; exact zero_le Œµ
  have hŒµ' : 0 < Œµ / 3 := ENNReal.div_pos hŒµ.ne' (coe_ne_top)
  -- use tightness to divide the domain into interior and exterior
  obtain ‚ü®Eg, hmEg, hŒºEg, hgŒµ‚ü© := Mem‚Ñíp.exists_snorm_indicator_compl_lt hp' hg' hŒµ'.ne' --hrŒµ'
  obtain ‚ü®Ef, hmEf, hŒºEf, hfŒµ‚ü© := hut.exists_measurableSet_indicator hŒµ'.ne'
  have hmE := hmEf.union hmEg
  have hfmE := (measure_union_le Ef Eg).trans_lt (add_lt_top.mpr ‚ü®hŒºEf, hŒºEg‚ü©)
  set E : Set Œ± := Ef ‚à™ Eg
  -- use uniform integrability to get control on the limit over E
  have hgE' := Mem‚Ñíp.restrict E hg'
  have huiE := hui.restrict  E
  have hfgE : (‚àÄ·µê x ‚àÇ(Œº.restrict E), Tendsto (fun n => f n x) atTop (ùìù (g x))) :=
    ae_restrict_of_ae hfg
  -- `tendsto_Lp_of_tendsto_ae_of_meas` needs to
  -- synthesize an argument `[IsFiniteMeasure (Œº.restrict E)]`.
  -- It is enough to have in the context a term of `Fact (Œº E < ‚àû)`, which is our `ffmE` below,
  -- which is automatically fed into `Restrict.isFiniteInstance`.
  have ffmE : Fact _ := { out := hfmE }
  have hInner := tendsto_Lp_of_tendsto_ae_of_meas hp hp' hf hg hgE' huiE hfgE
  rw [ENNReal.tendsto_atTop_zero] at hInner
  -- get a sufficiently large N for given Œµ, and consider any n ‚â• N
  obtain ‚ü®N, hfngŒµ‚ü© := hInner (Œµ / 3) hŒµ'
  use N; intro n hn
  -- get interior estimates
  have hmfngE : AEStronglyMeasurable _ Œº := (((hf n).sub hg).indicator hmE).aestronglyMeasurable
  have hfngEŒµ := calc
    snorm (E.indicator (f n - g)) p Œº
      = snorm (f n - g) p (Œº.restrict E) := snorm_indicator_eq_snorm_restrict hmE
    _ ‚â§ Œµ / 3                            := hfngŒµ n hn
  -- get exterior estimates
  have hmgEc : AEStronglyMeasurable _ Œº := (hg.indicator hmE.compl).aestronglyMeasurable
  have hgEcŒµ := calc
    snorm (E·∂ú.indicator g) p Œº
      ‚â§ snorm (Ef·∂ú.indicator (Eg·∂ú.indicator g)) p Œº := by
        unfold_let E; rw [compl_union, ‚Üê indicator_indicator]
    _ ‚â§ snorm (Eg·∂ú.indicator g) p Œº := snorm_indicator_le _
    _ ‚â§ Œµ / 3 := hgŒµ.le
  have hmfnEc : AEStronglyMeasurable _ Œº := ((hf n).indicator hmE.compl).aestronglyMeasurable
  have hfnEcŒµ : snorm (E·∂ú.indicator (f n)) p Œº ‚â§ Œµ / 3 := calc
    snorm (E·∂ú.indicator (f n)) p Œº
      ‚â§ snorm (Eg·∂ú.indicator (Ef·∂ú.indicator (f n))) p Œº := by
        unfold_let E; rw [compl_union, inter_comm, ‚Üê indicator_indicator]
    _ ‚â§ snorm (Ef·∂ú.indicator (f n)) p Œº := snorm_indicator_le _
    _ ‚â§ Œµ / 3 := hfŒµ n
  have hmfngEc : AEStronglyMeasurable _ Œº :=
    (((hf n).sub hg).indicator hmE.compl).aestronglyMeasurable
  have hfngEcŒµ := calc
    snorm (E·∂ú.indicator (f n - g)) p Œº
      = snorm (E·∂ú.indicator (f n) - E·∂ú.indicator g) p Œº := by
        rw [(E·∂ú.indicator_sub' _ _)]
    _ ‚â§ snorm (E·∂ú.indicator (f n)) p Œº + snorm (E·∂ú.indicator g) p Œº := by
        apply snorm_sub_le (by assumption) (by assumption) hp
    _ ‚â§ Œµ / 3 + Œµ / 3 := add_le_add hfnEcŒµ hgEcŒµ
  -- finally, combine interior and exterior estimates
  calc
    snorm (f n - g) p Œº
      = snorm (E·∂ú.indicator (f n - g) + E.indicator (f n - g)) p Œº := by
        congr; exact (E.indicator_compl_add_self _).symm
    _ ‚â§ snorm (indicator E·∂ú (f n - g)) p Œº + snorm (indicator E (f n - g)) p Œº := by
        apply snorm_add_le (by assumption) (by assumption) hp
    _ ‚â§ (Œµ / 3 + Œµ / 3) + Œµ / 3 := add_le_add hfngEcŒµ hfngEŒµ
    _ = Œµ := by simp only [ENNReal.add_thirds] --ENNReal.add_thirds Œµ

/- Lemma used in `tendsto_Lp_notFinite_of_tendsto_ae`.
   XXX: Alternative name: `ae_tendsto_ae_congr`? -/
theorem tendsto_ae_congr_ae {f f' : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {g g' : Œ± ‚Üí Œ≤}
    (hff' : ‚àÄ (n : ‚Ñï), f n =·µê[Œº] f' n) (hgg' : g =·µê[Œº] g')
    (hfg : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f n x) atTop (ùìù (g x))) :
    ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f' n x) atTop (ùìù (g' x)) := by
  have hff'' := eventually_countable_forall.mpr hff'
  filter_upwards [hff'', hgg', hfg] with x hff'x hgg'x hfgx
  apply Tendsto.congr hff'x
  rw [‚Üê hgg'x]; exact hfgx

theorem tendsto_Lp_notFinite_of_tendsto_ae (hp : 1 ‚â§ p) (hp' : p ‚â† ‚àû)
    {f : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≤} (haef : ‚àÄ n, AEStronglyMeasurable (f n) Œº)
    (hg' : Mem‚Ñíp g p Œº) (hui : UnifIntegrable f p Œº) (hut : UnifTight f p Œº)
    (hfg : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => f n x) atTop (ùìù (g x))) :
    Tendsto (fun n => snorm (f n - g) p Œº) atTop (ùìù 0) := by
  -- come up with an a.e. equal strongly measurable replacement `f` for `g`
  have hf := fun n => (haef n).stronglyMeasurable_mk
  have hff' := fun n => (haef n).ae_eq_mk (Œº := Œº)
  have hui' := hui.ae_eq hff'
  have hut' := hut.ae_eq hff'
  have hg := hg'.aestronglyMeasurable.stronglyMeasurable_mk
  have hgg' := hg'.aestronglyMeasurable.ae_eq_mk (Œº := Œº)
  have hg'' := hg'.ae_eq hgg'
  have haefg' := tendsto_ae_congr_ae hff' hgg' hfg
  set f' := fun n => (haef n).mk (Œº := Œº)
  set g' := hg'.aestronglyMeasurable.mk (Œº := Œº)
  have haefg (n : ‚Ñï) : f n - g =·µê[Œº] f' n - g' := (hff' n).sub hgg'
  have hsnfg (n : ‚Ñï) := snorm_congr_ae (p := p) (haefg n)
  apply Filter.Tendsto.congr (fun n => (hsnfg n).symm)
  exact tendsto_Lp_notFinite_of_tendsto_ae_of_meas hp hp' hf hg hg'' hui' hut' haefg'


/-- Forward direction of Vitali's convergence theorem (non-finite version):
    if `f` is a sequence of uniformly integrable, uniformly tight functions that converge in
    measure to some function `g` in a finite measure space, then `f` converge in Lp to `g`. -/
theorem tendsto_Lp_notFinite_of_tendstoInMeasure (hp : 1 ‚â§ p) (hp' : p ‚â† ‚àû)
    (hf : ‚àÄ n, AEStronglyMeasurable (f n) Œº) (hg : Mem‚Ñíp g p Œº)
    (hui : UnifIntegrable f p Œº) (hut : UnifTight f p Œº)
    (hfg : TendstoInMeasure Œº f atTop g) : Tendsto (fun n => snorm (f n - g) p Œº) atTop (ùìù 0) := by
  refine tendsto_of_subseq_tendsto fun ns hns => ?_
  obtain ‚ü®ms, _, hms'‚ü© := TendstoInMeasure.exists_seq_tendsto_ae fun Œµ hŒµ => (hfg Œµ hŒµ).comp hns
  exact ‚ü®ms,
    tendsto_Lp_notFinite_of_tendsto_ae hp hp' (fun _ => hf _) hg
      (fun Œµ hŒµ => -- `UnifIntegrable` on a subsequence
        let ‚ü®Œ¥, hŒ¥, hŒ¥'‚ü© := hui hŒµ
        ‚ü®Œ¥, hŒ¥, fun i s hs hŒºs => hŒ¥' _ s hs hŒºs‚ü©)
      (fun Œµ hŒµ => -- `UnifTight` on a subsequence
        let ‚ü®s, hŒºs, hfŒµ‚ü© := hut hŒµ
        ‚ü®s, hŒºs, fun i => hfŒµ _‚ü©)
      hms'‚ü©


/-- **Vitali's convergence theorem** (non-finite measure version):
    A sequence of functions `f` converges to `g` in Lp
    if and only if it is uniformly integrable, uniformly tight and to `g` in measure. -/
-- XXX: logically, this should be renamed to `tendstoInMeasure_iff_tendsto_Lp`, while
--  the current version of that could be renamed to `tendstoInMeasure_iff_tendsto_Lp_of_isFinite`.
theorem tendstoInMeasure_notFinite_iff_tendsto_Lp (hp : 1 ‚â§ p) (hp' : p ‚â† ‚àû)
    (hf : ‚àÄ n, Mem‚Ñíp (f n) p Œº) (hg : Mem‚Ñíp g p Œº) :
    TendstoInMeasure Œº f atTop g ‚àß UnifIntegrable f p Œº ‚àß UnifTight f p Œº
      ‚Üî Tendsto (fun n => snorm (f n - g) p Œº) atTop (ùìù 0) :=
  ‚ü®fun h => tendsto_Lp_notFinite_of_tendstoInMeasure hp hp' (fun n => (hf n).1) hg h.2.1 h.2.2 h.1,
    fun h =>
    ‚ü®tendstoInMeasure_of_tendsto_snorm (lt_of_lt_of_le zero_lt_one hp).ne'
        (fun n => (hf n).aestronglyMeasurable) hg.aestronglyMeasurable h,
      unifIntegrable_of_tendsto_Lp hp hp' hf hg h,
      unifTight_of_tendsto_Lp hp' hf hg h‚ü©‚ü©


end VitaliConvergence


end MeasureTheory
