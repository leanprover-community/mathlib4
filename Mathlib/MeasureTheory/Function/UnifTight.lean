/-
Copyright (c) 2023 Igor Khavkine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Igor Khavkine
-/
import Mathlib.MeasureTheory.Function.ConvergenceInMeasure
import Mathlib.MeasureTheory.Function.UniformIntegrable

/-!
# Uniform tightness

This file contains the definitions for uniform tightness for a family of Lp functions.
It is used as a hypothesis to the version of Vitali's convergence theorem for Lp spaces
that works also for spaces of infinite measure. This version of Vitali's theorem
is also proved later in the file.

## Main definitions

* `MeasureTheory.UnifTight`:
  A sequence of functions `f` is uniformly tight in `L^p` if for all `Îµ > 0`, there
  exists some measurable set `s` with finite measure such that the Lp-norm of
  `f i` restricted to `sá¶œ` is smaller than `Îµ` for all `i`.

# Main results

* `MeasureTheory.unifTight_finite`: a finite sequence of Lp functions is uniformly
  tight.
* `MeasureTheory.tendsto_Lp_of_tendsto_ae`: a sequence of Lp functions which is uniformly
  integrable and uniformly tight converges in Lp if it converges almost everywhere.
* `MeasureTheory.tendstoInMeasure_iff_tendsto_Lp`: Vitali convergence theorem:
  a sequence of Lp functions converges in Lp if and only if it is uniformly integrable,
  uniformly tight and converges in measure.

## Tags

uniform integrable, uniformly tight, Vitali convergence theorem
-/

namespace MeasureTheory

open Set Filter Topology MeasureTheory NNReal ENNReal

variable {Î± Î² Î¹ : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedAddCommGroup Î²]

section UnifTight

/- This follows closely the `UnifIntegrable` section
from `Mathlib/MeasureTheory/Functions/UniformIntegrable.lean`. -/

variable {f g : Î¹ â†’ Î± â†’ Î²} {p : â„â‰¥0âˆ}

/-- A sequence of functions `f` is uniformly tight in `L^p` if for all `Îµ > 0`, there
exists some measurable set `s` with finite measure such that the Lp-norm of
`f i` restricted to `sá¶œ` is smaller than `Îµ` for all `i`. -/
def UnifTight {_ : MeasurableSpace Î±} (f : Î¹ â†’ Î± â†’ Î²) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) : Prop :=
  âˆ€ â¦ƒÎµ : â„â‰¥0â¦„, 0 < Îµ â†’ âˆƒ s : Set Î±, Î¼ s â‰  âˆ âˆ§ âˆ€ i, eLpNorm (sá¶œ.indicator (f i)) p Î¼ â‰¤ Îµ

theorem unifTight_iff_ennreal {_ : MeasurableSpace Î±} (f : Î¹ â†’ Î± â†’ Î²) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) :
    UnifTight f p Î¼ â†” âˆ€ â¦ƒÎµ : â„â‰¥0âˆâ¦„, 0 < Îµ â†’ âˆƒ s : Set Î±,
      Î¼ s â‰  âˆ âˆ§ âˆ€ i, eLpNorm (sá¶œ.indicator (f i)) p Î¼ â‰¤ Îµ := by
  simp only [ENNReal.forall_ennreal, ENNReal.coe_pos]
  refine (and_iff_left ?_).symm
  simp only [zero_lt_top, le_top, implies_true, and_true, true_implies]
  use âˆ…; simpa only [measure_empty] using zero_ne_top

theorem unifTight_iff_real {_ : MeasurableSpace Î±} (f : Î¹ â†’ Î± â†’ Î²) (p : â„â‰¥0âˆ) (Î¼ : Measure Î±) :
    UnifTight f p Î¼ â†” âˆ€ â¦ƒÎµ : â„â¦„, 0 < Îµ â†’ âˆƒ s : Set Î±,
      Î¼ s â‰  âˆ âˆ§ âˆ€ i, eLpNorm (sá¶œ.indicator (f i)) p Î¼ â‰¤ .ofReal Îµ := by
  refine âŸ¨fun hut rÎµ hrÎµ â†¦ hut (Real.toNNReal_pos.mpr hrÎµ), fun hut Îµ hÎµ â†¦ ?_âŸ©
  obtain âŸ¨s, hÎ¼s, hfÎµâŸ© := hut hÎµ
  use s, hÎ¼s; intro i
  exact (hfÎµ i).trans_eq (ofReal_coe_nnreal (p := Îµ))

namespace UnifTight

theorem eventually_cofinite_indicator (hf : UnifTight f p Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆ€á¶  s in Î¼.cofinite.smallSets, âˆ€ i, eLpNorm (s.indicator (f i)) p Î¼ â‰¤ Îµ := by
  by_cases hÎµ_top : Îµ = âˆ
  Â· subst hÎµ_top; simp
  rcases hf (pos_iff_ne_zero.2 (toNNReal_ne_zero.mpr âŸ¨hÎµ,hÎµ_topâŸ©)) with âŸ¨s, hÎ¼s, hfsâŸ©
  refine (eventually_smallSets' ?_).2 âŸ¨sá¶œ, ?_, fun i â†¦ (coe_toNNReal hÎµ_top) â–¸ hfs iâŸ©
  Â· intro s t hst ht i
    exact (eLpNorm_mono <| norm_indicator_le_of_subset hst _).trans (ht i)
  Â· rwa [Measure.compl_mem_cofinite, lt_top_iff_ne_top]

protected theorem exists_measurableSet_indicator (hf : UnifTight f p Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ s, MeasurableSet s âˆ§ Î¼ s < âˆ âˆ§ âˆ€ i, eLpNorm (sá¶œ.indicator (f i)) p Î¼ â‰¤ Îµ :=
  let âŸ¨s, hÎ¼s, hsm, hfsâŸ© := (hf.eventually_cofinite_indicator hÎµ).exists_measurable_mem_of_smallSets
  âŸ¨sá¶œ, hsm.compl, hÎ¼s, by rwa [compl_compl s]âŸ©

protected theorem add (hf : UnifTight f p Î¼) (hg : UnifTight g p Î¼)
    (hf_meas : âˆ€ i, AEStronglyMeasurable (f i) Î¼) (hg_meas : âˆ€ i, AEStronglyMeasurable (g i) Î¼) :
    UnifTight (f + g) p Î¼ := fun Îµ hÎµ â†¦ by
  rcases exists_Lp_half Î² Î¼ p (coe_ne_zero.mpr hÎµ.ne') with âŸ¨Î·, hÎ·_pos, hÎ·âŸ©
  by_cases hÎ·_top : Î· = âˆ
  Â· replace hÎ· := hÎ·_top â–¸ hÎ·
    refine âŸ¨âˆ…, (by simp), fun i â†¦ ?_âŸ©
    simp only [compl_empty, indicator_univ, Pi.add_apply]
    exact (hÎ· (f i) (g i) (hf_meas i) (hg_meas i) le_top le_top).le
  obtain âŸ¨s, hÎ¼s, hsm, hfs, hgsâŸ© :
      âˆƒ s âˆˆ Î¼.cofinite, MeasurableSet s âˆ§
        (âˆ€ i, eLpNorm (s.indicator (f i)) p Î¼ â‰¤ Î·) âˆ§
        (âˆ€ i, eLpNorm (s.indicator (g i)) p Î¼ â‰¤ Î·) :=
    ((hf.eventually_cofinite_indicator hÎ·_pos.ne').and
      (hg.eventually_cofinite_indicator hÎ·_pos.ne')).exists_measurable_mem_of_smallSets
  refine âŸ¨sá¶œ, ne_of_lt hÎ¼s, fun i â†¦ ?_âŸ©
  have Î·_cast : â†‘Î·.toNNReal = Î· := coe_toNNReal hÎ·_top
  calc
    eLpNorm (indicator sá¶œá¶œ (f i + g i)) p Î¼
      = eLpNorm (indicator s (f i) + indicator s (g i)) p Î¼ := by rw [compl_compl, indicator_add']
    _ â‰¤ Îµ := le_of_lt <|
      hÎ· _ _ ((hf_meas i).indicator hsm) ((hg_meas i).indicator hsm)
        (Î·_cast â–¸ hfs i) (Î·_cast â–¸ hgs i)

protected theorem neg (hf : UnifTight f p Î¼) : UnifTight (-f) p Î¼ := by
  simp_rw [UnifTight, Pi.neg_apply, Set.indicator_neg', eLpNorm_neg]
  exact hf

protected theorem sub (hf : UnifTight f p Î¼) (hg : UnifTight g p Î¼)
    (hf_meas : âˆ€ i, AEStronglyMeasurable (f i) Î¼) (hg_meas : âˆ€ i, AEStronglyMeasurable (g i) Î¼) :
    UnifTight (f - g) p Î¼ := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg hf_meas fun i => (hg_meas i).neg

protected theorem aeeq (hf : UnifTight f p Î¼) (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
    UnifTight g p Î¼ := by
  intro Îµ hÎµ
  obtain âŸ¨s, hÎ¼s, hfÎµâŸ© := hf hÎµ
  refine âŸ¨s, hÎ¼s, fun n => (le_of_eq <| eLpNorm_congr_ae ?_).trans (hfÎµ n)âŸ©
  filter_upwards [hfg n] with x hx
  simp only [indicator, mem_compl_iff, hx]

end UnifTight

/-- If two functions agree a.e., then one is tight iff the other is tight. -/
theorem unifTight_congr_ae {g : Î¹ â†’ Î± â†’ Î²} (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
    UnifTight f p Î¼ â†” UnifTight g p Î¼ :=
  âŸ¨fun h => h.aeeq hfg, fun h => h.aeeq fun i => (hfg i).symmâŸ©

/-- A constant sequence is tight. -/
theorem unifTight_const {g : Î± â†’ Î²} (hp_ne_top : p â‰  âˆ) (hg : MemLp g p Î¼) :
    UnifTight (fun _ : Î¹ => g) p Î¼ := by
  intro Îµ hÎµ
  by_cases hÎµ_top : Îµ = âˆ
  Â· exact âŸ¨âˆ…, (by simp), fun _ => hÎµ_top.symm â–¸ le_topâŸ©
  obtain âŸ¨s, _, hÎ¼s, hgÎµâŸ© := hg.exists_eLpNorm_indicator_compl_lt hp_ne_top (coe_ne_zero.mpr hÎµ.ne')
  exact âŸ¨s, ne_of_lt hÎ¼s, fun _ => hgÎµ.leâŸ©

/-- A single function is tight. -/
theorem unifTight_of_subsingleton [Subsingleton Î¹] (hp_top : p â‰  âˆ)
    {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, MemLp (f i) p Î¼) : UnifTight f p Î¼ := fun Îµ hÎµ â†¦ by
  by_cases hÎµ_top : Îµ = âˆ
  Â· exact âŸ¨âˆ…, by simp, fun _ => hÎµ_top.symm â–¸ le_topâŸ©
  by_cases hÎ¹ : Nonempty Î¹
  case neg => exact âŸ¨âˆ…, (by simp), fun i => False.elim <| hÎ¹ <| Nonempty.intro iâŸ©
  obtain âŸ¨iâŸ© := hÎ¹
  obtain âŸ¨s, _, hÎ¼s, hfÎµâŸ© := (hf i).exists_eLpNorm_indicator_compl_lt hp_top (coe_ne_zero.2 hÎµ.ne')
  refine âŸ¨s, ne_of_lt hÎ¼s, fun j => ?_âŸ©
  convert hfÎµ.le

open Fin.NatCast in -- TODO: should this be refactored to avoid needing the coercion?
/-- This lemma is less general than `MeasureTheory.unifTight_finite` which applies to
all sequences indexed by a finite type. -/
private theorem unifTight_fin (hp_top : p â‰  âˆ) {n : â„•} {f : Fin n â†’ Î± â†’ Î²}
    (hf : âˆ€ i, MemLp (f i) p Î¼) : UnifTight f p Î¼ := by
  revert f
  induction n with
  | zero => exact fun {f} hf â†¦ unifTight_of_subsingleton hp_top hf
  | succ n h =>
    intro f hfLp Îµ hÎµ
    by_cases hÎµ_top : Îµ = âˆ
    Â· exact âŸ¨âˆ…, (by simp), fun _ => hÎµ_top.symm â–¸ le_topâŸ©
    let g : Fin n â†’ Î± â†’ Î² := fun k => f k
    have hgLp : âˆ€ i, MemLp (g i) p Î¼ := fun i => hfLp i
    obtain âŸ¨S, hÎ¼S, hFÎµâŸ© := h hgLp hÎµ
    obtain âŸ¨s, _, hÎ¼s, hfÎµâŸ© :=
      (hfLp n).exists_eLpNorm_indicator_compl_lt hp_top (coe_ne_zero.2 hÎµ.ne')
    refine âŸ¨s âˆª S, (by measurability), fun i => ?_âŸ©
    by_cases hi : i.val < n
    Â· rw [(_ : f i = g âŸ¨i.val, hiâŸ©)]
      Â· rw [compl_union, â† indicator_indicator]
        apply (eLpNorm_indicator_le _).trans
        exact hFÎµ (Fin.castLT i hi)
      Â· simp only [Fin.coe_eq_castSucc, Fin.castSucc_mk, g]
    Â· rw [(_ : i = n)]
      Â· rw [compl_union, inter_comm, â† indicator_indicator]
        exact (eLpNorm_indicator_le _).trans hfÎµ.le
      Â· have hi' := Fin.is_lt i
        rw [Nat.lt_succ_iff] at hi'
        rw [not_lt] at hi
        simp [â† le_antisymm hi' hi]

/-- A finite sequence of Lp functions is uniformly tight. -/
theorem unifTight_finite [Finite Î¹] (hp_top : p â‰  âˆ) {f : Î¹ â†’ Î± â†’ Î²}
    (hf : âˆ€ i, MemLp (f i) p Î¼) : UnifTight f p Î¼ := fun Îµ hÎµ â†¦ by
  obtain âŸ¨n, hnâŸ© := Finite.exists_equiv_fin Î¹
  set g : Fin n â†’ Î± â†’ Î² := f âˆ˜ hn.some.symm
  have hg : âˆ€ i, MemLp (g i) p Î¼ := fun _ => hf _
  obtain âŸ¨s, hÎ¼s, hfÎµâŸ© := unifTight_fin hp_top hg hÎµ
  refine âŸ¨s, hÎ¼s, fun i => ?_âŸ©
  simpa only [g, Function.comp_apply, Equiv.symm_apply_apply] using hfÎµ (hn.some i)

end UnifTight

section VitaliConvergence

variable {Î¼ : Measure Î±} {p : â„â‰¥0âˆ} {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²}

/-! Both directions and an iff version of Vitali's convergence theorem on measure spaces
of not necessarily finite volume. See `Thm III.6.15` of Dunford & Schwartz, Part I (1958). -/

/- We start with the reverse direction. We only need to show that uniform tightness follows
from convergence in Lp. Mathlib already has the analogous `unifIntegrable_of_tendsto_Lp`
and `tendstoInMeasure_of_tendsto_eLpNorm`. -/

/-- Intermediate lemma for `unifTight_of_tendsto_Lp`. -/
private theorem unifTight_of_tendsto_Lp_zero (hp' : p â‰  âˆ) (hf : âˆ€ n, MemLp (f n) p Î¼)
    (hf_tendsto : Tendsto (fun n â†¦ eLpNorm (f n) p Î¼) atTop (ğ“ 0)) : UnifTight f p Î¼ := by
  intro Îµ hÎµ
  rw [ENNReal.tendsto_atTop_zero] at hf_tendsto
  obtain âŸ¨N, hNÎµâŸ© := hf_tendsto Îµ (by simpa only [gt_iff_lt, ENNReal.coe_pos])
  let F : Fin N â†’ Î± â†’ Î² := fun n => f n
  have hF : âˆ€ n, MemLp (F n) p Î¼ := fun n => hf n
  obtain âŸ¨s, hÎ¼s, hFÎµâŸ© := unifTight_fin hp' hF hÎµ
  refine âŸ¨s, hÎ¼s, fun n => ?_âŸ©
  by_cases hn : n < N
  Â· exact hFÎµ âŸ¨n, hnâŸ©
  Â· exact (eLpNorm_indicator_le _).trans (hNÎµ n (not_lt.mp hn))

/-- Convergence in Lp implies uniform tightness. -/
private theorem unifTight_of_tendsto_Lp (hp' : p â‰  âˆ) (hf : âˆ€ n, MemLp (f n) p Î¼)
    (hg : MemLp g p Î¼) (hfg : Tendsto (fun n => eLpNorm (f n - g) p Î¼) atTop (ğ“ 0)) :
    UnifTight f p Î¼ := by
  have : f = (fun _ => g) + fun n => f n - g := by ext1 n; simp
  rw [this]
  refine UnifTight.add ?_ ?_ (fun _ => hg.aestronglyMeasurable)
      fun n => (hf n).1.sub hg.aestronglyMeasurable
  Â· exact unifTight_const hp' hg
  Â· exact unifTight_of_tendsto_Lp_zero hp' (fun n => (hf n).sub hg) hfg

/- Next we deal with the forward direction. The `MemLp` and `TendstoInMeasure` hypotheses
are unwrapped and strengthened (by known lemmas) to also have the `StronglyMeasurable`
and a.e. convergence hypotheses. The bulk of the proof is done under these stronger hypotheses. -/

/-- Bulk of the proof under strengthened hypotheses. Invoked from `tendsto_Lp_of_tendsto_ae`. -/
private theorem tendsto_Lp_of_tendsto_ae_of_meas (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    (hg' : MemLp g p Î¼) (hui : UnifIntegrable f p Î¼) (hut : UnifTight f p Î¼)
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    Tendsto (fun n => eLpNorm (f n - g) p Î¼) atTop (ğ“ 0) := by
  rw [ENNReal.tendsto_atTop_zero]
  intro Îµ hÎµ
  by_cases hfinÎµ : Îµ â‰  âˆ; swap
  Â· rw [not_ne_iff.mp hfinÎµ]; exact âŸ¨0, fun n _ => le_topâŸ©
  by_cases hÎ¼ : Î¼ = 0
  Â· rw [hÎ¼]; use 0; intro n _; rw [eLpNorm_measure_zero]; exact zero_le Îµ
  have hÎµ' : 0 < Îµ / 3 := ENNReal.div_pos hÎµ.ne' (ofNat_ne_top)
  -- use tightness to divide the domain into interior and exterior
  obtain âŸ¨Eg, hmEg, hÎ¼Eg, hgÎµâŸ© := MemLp.exists_eLpNorm_indicator_compl_lt hp' hg' hÎµ'.ne'
  obtain âŸ¨Ef, hmEf, hÎ¼Ef, hfÎµâŸ© := hut.exists_measurableSet_indicator hÎµ'.ne'
  have hmE := hmEf.union hmEg
  have hfmE := (measure_union_le Ef Eg).trans_lt (add_lt_top.mpr âŸ¨hÎ¼Ef, hÎ¼EgâŸ©)
  set E : Set Î± := Ef âˆª Eg
  -- use uniform integrability to get control on the limit over E
  have hgE' := MemLp.restrict E hg'
  have huiE := hui.restrict  E
  have hfgE : (âˆ€áµ x âˆ‚(Î¼.restrict E), Tendsto (fun n => f n x) atTop (ğ“ (g x))) :=
    ae_restrict_of_ae hfg
  -- `tendsto_Lp_of_tendsto_ae_of_meas` needs to
  -- synthesize an argument `[IsFiniteMeasure (Î¼.restrict E)]`.
  -- It is enough to have in the context a term of `Fact (Î¼ E < âˆ)`, which is our `ffmE` below,
  -- which is automatically fed into `Restrict.isFiniteInstance`.
  have ffmE := Fact.mk hfmE
  have hInner := tendsto_Lp_finite_of_tendsto_ae_of_meas hp hp' hf hg hgE' huiE hfgE
  rw [ENNReal.tendsto_atTop_zero] at hInner
  -- get a sufficiently large N for given Îµ, and consider any n â‰¥ N
  obtain âŸ¨N, hfngÎµâŸ© := hInner (Îµ / 3) hÎµ'
  use N; intro n hn
  -- get interior estimates
  have hmfngE : AEStronglyMeasurable _ Î¼ := (((hf n).sub hg).indicator hmE).aestronglyMeasurable
  have hfngEÎµ := calc
    eLpNorm (E.indicator (f n - g)) p Î¼
      = eLpNorm (f n - g) p (Î¼.restrict E) := eLpNorm_indicator_eq_eLpNorm_restrict hmE
    _ â‰¤ Îµ / 3                            := hfngÎµ n hn
  -- get exterior estimates
  have hmgEc : AEStronglyMeasurable _ Î¼ := (hg.indicator hmE.compl).aestronglyMeasurable
  have hgEcÎµ := calc
    eLpNorm (Eá¶œ.indicator g) p Î¼
      â‰¤ eLpNorm (Efá¶œ.indicator (Egá¶œ.indicator g)) p Î¼ := by
        unfold E; rw [compl_union, â† indicator_indicator]
    _ â‰¤ eLpNorm (Egá¶œ.indicator g) p Î¼ := eLpNorm_indicator_le _
    _ â‰¤ Îµ / 3 := hgÎµ.le
  have hmfnEc : AEStronglyMeasurable _ Î¼ := ((hf n).indicator hmE.compl).aestronglyMeasurable
  have hfnEcÎµ : eLpNorm (Eá¶œ.indicator (f n)) p Î¼ â‰¤ Îµ / 3 := calc
    eLpNorm (Eá¶œ.indicator (f n)) p Î¼
      â‰¤ eLpNorm (Egá¶œ.indicator (Efá¶œ.indicator (f n))) p Î¼ := by
        unfold E; rw [compl_union, inter_comm, â† indicator_indicator]
    _ â‰¤ eLpNorm (Efá¶œ.indicator (f n)) p Î¼ := eLpNorm_indicator_le _
    _ â‰¤ Îµ / 3 := hfÎµ n
  have hmfngEc : AEStronglyMeasurable _ Î¼ :=
    (((hf n).sub hg).indicator hmE.compl).aestronglyMeasurable
  have hfngEcÎµ := calc
    eLpNorm (Eá¶œ.indicator (f n - g)) p Î¼
      = eLpNorm (Eá¶œ.indicator (f n) - Eá¶œ.indicator g) p Î¼ := by
        rw [(Eá¶œ.indicator_sub' _ _)]
    _ â‰¤ eLpNorm (Eá¶œ.indicator (f n)) p Î¼ + eLpNorm (Eá¶œ.indicator g) p Î¼ := by
        apply eLpNorm_sub_le (by assumption) (by assumption) hp
    _ â‰¤ Îµ / 3 + Îµ / 3 := add_le_add hfnEcÎµ hgEcÎµ
  -- finally, combine interior and exterior estimates
  calc
    eLpNorm (f n - g) p Î¼
      = eLpNorm (Eá¶œ.indicator (f n - g) + E.indicator (f n - g)) p Î¼ := by
        congr; exact (E.indicator_compl_add_self _).symm
    _ â‰¤ eLpNorm (indicator Eá¶œ (f n - g)) p Î¼ + eLpNorm (indicator E (f n - g)) p Î¼ := by
        apply eLpNorm_add_le (by assumption) (by assumption) hp
    _ â‰¤ (Îµ / 3 + Îµ / 3) + Îµ / 3 := add_le_add hfngEcÎµ hfngEÎµ
    _ = Îµ := by simp only [ENNReal.add_thirds]

/-- Lemma used in `tendsto_Lp_of_tendsto_ae`. -/
private theorem ae_tendsto_ae_congr {f f' : â„• â†’ Î± â†’ Î²} {g g' : Î± â†’ Î²}
    (hff' : âˆ€ (n : â„•), f n =áµ[Î¼] f' n) (hgg' : g =áµ[Î¼] g')
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f' n x) atTop (ğ“ (g' x)) := by
  have hff'' := eventually_countable_forall.mpr hff'
  filter_upwards [hff'', hgg', hfg] with x hff'x hgg'x hfgx
  apply Tendsto.congr hff'x
  rw [â† hgg'x]; exact hfgx

/-- Forward direction of Vitali's convergence theorem, with a.e. instead of `InMeasure`
convergence -/
theorem tendsto_Lp_of_tendsto_ae (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (haef : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (hg' : MemLp g p Î¼) (hui : UnifIntegrable f p Î¼) (hut : UnifTight f p Î¼)
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) :
    Tendsto (fun n => eLpNorm (f n - g) p Î¼) atTop (ğ“ 0) := by
  -- come up with an a.e. equal strongly measurable replacement `f` for `g`
  have hf := fun n => (haef n).stronglyMeasurable_mk
  have hff' := fun n => (haef n).ae_eq_mk (Î¼ := Î¼)
  have hui' := hui.ae_eq hff'
  have hut' := hut.aeeq hff'
  have hg := hg'.aestronglyMeasurable.stronglyMeasurable_mk
  have hgg' := hg'.aestronglyMeasurable.ae_eq_mk (Î¼ := Î¼)
  have hg'' := hg'.ae_eq hgg'
  have haefg' := ae_tendsto_ae_congr hff' hgg' hfg
  set f' := fun n => (haef n).mk (Î¼ := Î¼)
  set g' := hg'.aestronglyMeasurable.mk (Î¼ := Î¼)
  have haefg (n : â„•) : f n - g =áµ[Î¼] f' n - g' := (hff' n).sub hgg'
  have hsnfg (n : â„•) := eLpNorm_congr_ae (p := p) (haefg n)
  apply Filter.Tendsto.congr (fun n => (hsnfg n).symm)
  exact tendsto_Lp_of_tendsto_ae_of_meas hp hp' hf hg hg'' hui' hut' haefg'

/-- Forward direction of Vitali's convergence theorem:
if `f` is a sequence of uniformly integrable, uniformly tight functions that converge in
measure to some function `g` in a finite measure space, then `f` converge in Lp to `g`. -/
theorem tendsto_Lp_of_tendstoInMeasure (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hg : MemLp g p Î¼)
    (hui : UnifIntegrable f p Î¼) (hut : UnifTight f p Î¼)
    (hfg : TendstoInMeasure Î¼ f atTop g) : Tendsto (fun n â†¦ eLpNorm (f n - g) p Î¼) atTop (ğ“ 0) := by
  refine tendsto_of_subseq_tendsto fun ns hns => ?_
  obtain âŸ¨ms, _, hms'âŸ© := TendstoInMeasure.exists_seq_tendsto_ae fun Îµ hÎµ => (hfg Îµ hÎµ).comp hns
  exact âŸ¨ms,
    tendsto_Lp_of_tendsto_ae hp hp' (fun _ => hf _) hg
      (fun Îµ hÎµ => -- `UnifIntegrable` on a subsequence
        let âŸ¨Î´, hÎ´, hÎ´'âŸ© := hui hÎµ
        âŸ¨Î´, hÎ´, fun i s hs hÎ¼s => hÎ´' _ s hs hÎ¼sâŸ©)
      (fun Îµ hÎµ => -- `UnifTight` on a subsequence
        let âŸ¨s, hÎ¼s, hfÎµâŸ© := hut hÎµ
        âŸ¨s, hÎ¼s, fun i => hfÎµ _âŸ©)
      hms'âŸ©

/-- **Vitali's convergence theorem** (non-finite measure version).

A sequence of functions `f` converges to `g` in Lp
if and only if it is uniformly integrable, uniformly tight and converges to `g` in measure. -/
theorem tendstoInMeasure_iff_tendsto_Lp (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
    (hf : âˆ€ n, MemLp (f n) p Î¼) (hg : MemLp g p Î¼) :
    TendstoInMeasure Î¼ f atTop g âˆ§ UnifIntegrable f p Î¼ âˆ§ UnifTight f p Î¼
      â†” Tendsto (fun n => eLpNorm (f n - g) p Î¼) atTop (ğ“ 0) where
  mp h := tendsto_Lp_of_tendstoInMeasure hp hp' (fun n => (hf n).1) hg h.2.1 h.2.2 h.1
  mpr h := âŸ¨tendstoInMeasure_of_tendsto_eLpNorm (lt_of_lt_of_le zero_lt_one hp).ne'
        (fun n => (hf n).aestronglyMeasurable) hg.aestronglyMeasurable h,
      unifIntegrable_of_tendsto_Lp hp hp' hf hg h,
      unifTight_of_tendsto_Lp hp' hf hg hâŸ©

end VitaliConvergence
end MeasureTheory
