/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Johannes HÃ¶lzl
-/
import Mathlib.Algebra.Order.Pi
import Mathlib.Algebra.Algebra.Pi
import Mathlib.MeasureTheory.Constructions.BorelSpace.Order

/-!
# Simple functions

A function `f` from a measurable space to any type is called *simple*, if every preimage `f â»Â¹' {x}`
is measurable, and the range is finite. In this file, we define simple functions and establish their
basic properties; and we construct a sequence of simple functions approximating an arbitrary Borel
measurable function `f : Î± â†’ â„â‰¥0âˆ`.

The theorem `Measurable.ennreal_induction` shows that in order to prove something for an arbitrary
measurable function into `â„â‰¥0âˆ`, it is sufficient to show that the property holds for (multiples of)
characteristic functions and is closed under addition and supremum of increasing sequences of
functions.
-/


noncomputable section

open Set hiding restrict restrict_apply

open Filter ENNReal

open Function (support)

open Topology NNReal ENNReal MeasureTheory

namespace MeasureTheory

variable {Î± Î² Î³ Î´ : Type*}

/-- A function `f` from a measurable space to any type is called *simple*,
if every preimage `f â»Â¹' {x}` is measurable, and the range is finite. This structure bundles
a function with these properties. -/
structure SimpleFunc.{u, v} (Î± : Type u) [MeasurableSpace Î±] (Î² : Type v) where
  /-- The underlying function -/
  toFun : Î± â†’ Î²
  measurableSet_fiber' : âˆ€ x, MeasurableSet (toFun â»Â¹' {x})
  finite_range' : (Set.range toFun).Finite

local infixr:25 " â†’â‚› " => SimpleFunc

namespace SimpleFunc

section Measurable

variable [MeasurableSpace Î±]

instance instFunLike : FunLike (Î± â†’â‚› Î²) Î± Î² where
  coe := toFun
  coe_injective' | âŸ¨_, _, _âŸ©, âŸ¨_, _, _âŸ©, rfl => rfl

theorem coe_injective â¦ƒf g : Î± â†’â‚› Î²â¦„ (H : (f : Î± â†’ Î²) = g) : f = g := DFunLike.ext' H

@[ext]
theorem ext {f g : Î± â†’â‚› Î²} (H : âˆ€ a, f a = g a) : f = g := DFunLike.ext _ _ H

theorem finite_range (f : Î± â†’â‚› Î²) : (Set.range f).Finite :=
  f.finite_range'

theorem measurableSet_fiber (f : Î± â†’â‚› Î²) (x : Î²) : MeasurableSet (f â»Â¹' {x}) :=
  f.measurableSet_fiber' x

@[simp] theorem coe_mk (f : Î± â†’ Î²) (h h') : â‡‘(mk f h h') = f := rfl

theorem apply_mk (f : Î± â†’ Î²) (h h') (x : Î±) : SimpleFunc.mk f h h' x = f x :=
  rfl

/-- Simple function defined on a finite type. -/
def ofFinite [Finite Î±] [MeasurableSingletonClass Î±] (f : Î± â†’ Î²) : Î± â†’â‚› Î² where
  toFun := f
  measurableSet_fiber' x := (toFinite (f â»Â¹' {x})).measurableSet
  finite_range' := Set.finite_range f


/-- Simple function defined on the empty type. -/
def ofIsEmpty [IsEmpty Î±] : Î± â†’â‚› Î² := ofFinite isEmptyElim

/-- Range of a simple function `Î± â†’â‚› Î²` as a `Finset Î²`. -/
protected def range (f : Î± â†’â‚› Î²) : Finset Î² :=
  f.finite_range.toFinset

@[simp]
theorem mem_range {f : Î± â†’â‚› Î²} {b} : b âˆˆ f.range â†” b âˆˆ range f :=
  Finite.mem_toFinset _

theorem mem_range_self (f : Î± â†’â‚› Î²) (x : Î±) : f x âˆˆ f.range :=
  mem_range.2 âŸ¨x, rflâŸ©

@[simp]
theorem coe_range (f : Î± â†’â‚› Î²) : (â†‘f.range : Set Î²) = Set.range f :=
  f.finite_range.coe_toFinset

theorem mem_range_of_measure_ne_zero {f : Î± â†’â‚› Î²} {x : Î²} {Î¼ : Measure Î±} (H : Î¼ (f â»Â¹' {x}) â‰  0) :
    x âˆˆ f.range :=
  let âŸ¨a, haâŸ© := nonempty_of_measure_ne_zero H
  mem_range.2 âŸ¨a, haâŸ©

theorem forall_mem_range {f : Î± â†’â‚› Î²} {p : Î² â†’ Prop} : (âˆ€ y âˆˆ f.range, p y) â†” âˆ€ x, p (f x) := by
  simp only [mem_range, Set.forall_mem_range]

theorem exists_range_iff {f : Î± â†’â‚› Î²} {p : Î² â†’ Prop} : (âˆƒ y âˆˆ f.range, p y) â†” âˆƒ x, p (f x) := by
  simpa only [mem_range, exists_prop] using Set.exists_range_iff

theorem preimage_eq_empty_iff (f : Î± â†’â‚› Î²) (b : Î²) : f â»Â¹' {b} = âˆ… â†” b âˆ‰ f.range :=
  preimage_singleton_eq_empty.trans <| not_congr mem_range.symm

theorem exists_forall_le [Nonempty Î²] [Preorder Î²] [IsDirected Î² (Â· â‰¤ Â·)] (f : Î± â†’â‚› Î²) :
    âˆƒ C, âˆ€ x, f x â‰¤ C :=
  f.range.exists_le.imp fun _ => forall_mem_range.1

/-- Constant function as a `SimpleFunc`. -/
def const (Î±) {Î²} [MeasurableSpace Î±] (b : Î²) : Î± â†’â‚› Î² :=
  âŸ¨fun _ => b, fun _ => MeasurableSet.const _, finite_range_constâŸ©

instance instInhabited [Inhabited Î²] : Inhabited (Î± â†’â‚› Î²) :=
  âŸ¨const _ defaultâŸ©

theorem const_apply (a : Î±) (b : Î²) : (const Î± b) a = b :=
  rfl

@[simp]
theorem coe_const (b : Î²) : â‡‘(const Î± b) = Function.const Î± b :=
  rfl

@[simp]
theorem range_const (Î±) [MeasurableSpace Î±] [Nonempty Î±] (b : Î²) : (const Î± b).range = {b} :=
  Finset.coe_injective <| by simp +unfoldPartialApp [Function.const]

theorem range_const_subset (Î±) [MeasurableSpace Î±] (b : Î²) : (const Î± b).range âŠ† {b} :=
  Finset.coe_subset.1 <| by simp

theorem simpleFunc_bot {Î±} (f : @SimpleFunc Î± âŠ¥ Î²) [Nonempty Î²] : âˆƒ c, âˆ€ x, f x = c := by
  have hf_meas := @SimpleFunc.measurableSet_fiber Î± _ âŠ¥ f
  simp_rw [MeasurableSpace.measurableSet_bot_iff] at hf_meas
  exact (exists_eq_const_of_preimage_singleton hf_meas).imp fun c hc â†¦ congr_fun hc

theorem simpleFunc_bot' {Î±} [Nonempty Î²] (f : @SimpleFunc Î± âŠ¥ Î²) :
    âˆƒ c, f = @SimpleFunc.const Î± _ âŠ¥ c :=
  letI : MeasurableSpace Î± := âŠ¥; (simpleFunc_bot f).imp fun _ â†¦ ext

theorem measurableSet_cut (r : Î± â†’ Î² â†’ Prop) (f : Î± â†’â‚› Î²) (h : âˆ€ b, MeasurableSet { a | r a b }) :
    MeasurableSet { a | r a (f a) } := by
  have : { a | r a (f a) } = â‹ƒ b âˆˆ range f, { a | r a b } âˆ© f â»Â¹' {b} := by
    ext a
    suffices r a (f a) â†” âˆƒ i, r a (f i) âˆ§ f a = f i by simpa
    exact âŸ¨fun h => âŸ¨a, âŸ¨h, rflâŸ©âŸ©, fun âŸ¨a', âŸ¨h', eâŸ©âŸ© => e.symm â–¸ h'âŸ©
  rw [this]
  exact
    MeasurableSet.biUnion f.finite_range.countable fun b _ =>
      MeasurableSet.inter (h b) (f.measurableSet_fiber _)

@[measurability]
theorem measurableSet_preimage (f : Î± â†’â‚› Î²) (s) : MeasurableSet (f â»Â¹' s) :=
  measurableSet_cut (fun _ b => b âˆˆ s) f fun b => MeasurableSet.const (b âˆˆ s)

/-- A simple function is measurable -/
@[measurability, fun_prop]
protected theorem measurable [MeasurableSpace Î²] (f : Î± â†’â‚› Î²) : Measurable f := fun s _ =>
  measurableSet_preimage f s

@[measurability]
protected theorem aemeasurable [MeasurableSpace Î²] {Î¼ : Measure Î±} (f : Î± â†’â‚› Î²) :
    AEMeasurable f Î¼ :=
  f.measurable.aemeasurable

protected theorem sum_measure_preimage_singleton (f : Î± â†’â‚› Î²) {Î¼ : Measure Î±} (s : Finset Î²) :
    (âˆ‘ y âˆˆ s, Î¼ (f â»Â¹' {y})) = Î¼ (f â»Â¹' â†‘s) :=
  sum_measure_preimage_singleton _ fun _ _ => f.measurableSet_fiber _

theorem sum_range_measure_preimage_singleton (f : Î± â†’â‚› Î²) (Î¼ : Measure Î±) :
    (âˆ‘ y âˆˆ f.range, Î¼ (f â»Â¹' {y})) = Î¼ univ := by
  rw [f.sum_measure_preimage_singleton, coe_range, preimage_range]

open scoped Classical in
/-- If-then-else as a `SimpleFunc`. -/
def piecewise (s : Set Î±) (hs : MeasurableSet s) (f g : Î± â†’â‚› Î²) : Î± â†’â‚› Î² :=
  âŸ¨s.piecewise f g, fun _ =>
    letI : MeasurableSpace Î² := âŠ¤
    f.measurable.piecewise hs g.measurable trivial,
    (f.finite_range.union g.finite_range).subset range_ite_subsetâŸ©

open scoped Classical in
@[simp]
theorem coe_piecewise {s : Set Î±} (hs : MeasurableSet s) (f g : Î± â†’â‚› Î²) :
    â‡‘(piecewise s hs f g) = s.piecewise f g :=
  rfl

open scoped Classical in
theorem piecewise_apply {s : Set Î±} (hs : MeasurableSet s) (f g : Î± â†’â‚› Î²) (a) :
    piecewise s hs f g a = if a âˆˆ s then f a else g a :=
  rfl

open scoped Classical in
@[simp]
theorem piecewise_compl {s : Set Î±} (hs : MeasurableSet sá¶œ) (f g : Î± â†’â‚› Î²) :
    piecewise sá¶œ hs f g = piecewise s hs.of_compl g f :=
  coe_injective <| by simp

@[simp]
theorem piecewise_univ (f g : Î± â†’â‚› Î²) : piecewise univ MeasurableSet.univ f g = f :=
  coe_injective <| by simp

@[simp]
theorem piecewise_empty (f g : Î± â†’â‚› Î²) : piecewise âˆ… MeasurableSet.empty f g = g :=
  coe_injective <| by simp

open scoped Classical in
@[simp]
theorem piecewise_same (f : Î± â†’â‚› Î²) {s : Set Î±} (hs : MeasurableSet s) :
    piecewise s hs f f = f :=
  coe_injective <| Set.piecewise_same _ _

theorem support_indicator [Zero Î²] {s : Set Î±} (hs : MeasurableSet s) (f : Î± â†’â‚› Î²) :
    Function.support (f.piecewise s hs (SimpleFunc.const Î± 0)) = s âˆ© Function.support f :=
  Set.support_indicator

open scoped Classical in
theorem range_indicator {s : Set Î±} (hs : MeasurableSet s) (hs_nonempty : s.Nonempty)
    (hs_ne_univ : s â‰  univ) (x y : Î²) :
    (piecewise s hs (const Î± x) (const Î± y)).range = {x, y} := by
  simp only [â† Finset.coe_inj, coe_range, coe_piecewise, range_piecewise, coe_const,
    Finset.coe_insert, Finset.coe_singleton, hs_nonempty.image_const,
    (nonempty_compl.2 hs_ne_univ).image_const, singleton_union, Function.const]

theorem measurable_bind [MeasurableSpace Î³] (f : Î± â†’â‚› Î²) (g : Î² â†’ Î± â†’ Î³)
    (hg : âˆ€ b, Measurable (g b)) : Measurable fun a => g (f a) a := fun s hs =>
  f.measurableSet_cut (fun a b => g b a âˆˆ s) fun b => hg b hs

/-- If `f : Î± â†’â‚› Î²` is a simple function and `g : Î² â†’ Î± â†’â‚› Î³` is a family of simple functions,
then `f.bind g` binds the first argument of `g` to `f`. In other words, `f.bind g a = g (f a) a`. -/
def bind (f : Î± â†’â‚› Î²) (g : Î² â†’ Î± â†’â‚› Î³) : Î± â†’â‚› Î³ :=
  âŸ¨fun a => g (f a) a, fun c =>
    f.measurableSet_cut (fun a b => g b a = c) fun b => (g b).measurableSet_preimage {c},
    (f.finite_range.biUnion fun b _ => (g b).finite_range).subset <| by
      rintro _ âŸ¨a, rflâŸ©; simpâŸ©

@[simp]
theorem bind_apply (f : Î± â†’â‚› Î²) (g : Î² â†’ Î± â†’â‚› Î³) (a) : f.bind g a = g (f a) a :=
  rfl

/-- Given a function `g : Î² â†’ Î³` and a simple function `f : Î± â†’â‚› Î²`, `f.map g` return the simple
function `g âˆ˜ f : Î± â†’â‚› Î³` -/
def map (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) : Î± â†’â‚› Î³ :=
  bind f (const Î± âˆ˜ g)

theorem map_apply (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) (a) : f.map g a = g (f a) :=
  rfl

theorem map_map (g : Î² â†’ Î³) (h : Î³ â†’ Î´) (f : Î± â†’â‚› Î²) : (f.map g).map h = f.map (h âˆ˜ g) :=
  rfl

@[simp]
theorem coe_map (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) : (f.map g : Î± â†’ Î³) = g âˆ˜ f :=
  rfl

@[simp]
theorem range_map [DecidableEq Î³] (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) : (f.map g).range = f.range.image g :=
  Finset.coe_injective <| by simp only [coe_range, coe_map, Finset.coe_image, range_comp]

@[simp]
theorem map_const (g : Î² â†’ Î³) (b : Î²) : (const Î± b).map g = const Î± (g b) :=
  rfl

open scoped Classical in
theorem map_preimage (f : Î± â†’â‚› Î²) (g : Î² â†’ Î³) (s : Set Î³) :
    f.map g â»Â¹' s = f â»Â¹' â†‘{b âˆˆ f.range | g b âˆˆ s} := by
  simp only [coe_range, sep_mem_eq, coe_map, Finset.coe_filter,
    â† mem_preimage, inter_comm, preimage_inter_range, â† Finset.mem_coe]
  exact preimage_comp

open scoped Classical in
theorem map_preimage_singleton (f : Î± â†’â‚› Î²) (g : Î² â†’ Î³) (c : Î³) :
    f.map g â»Â¹' {c} = f â»Â¹' â†‘{b âˆˆ f.range | g b = c} :=
  map_preimage _ _ _

/-- Composition of a `SimpleFun` and a measurable function is a `SimpleFunc`. -/
def comp [MeasurableSpace Î²] (f : Î² â†’â‚› Î³) (g : Î± â†’ Î²) (hgm : Measurable g) : Î± â†’â‚› Î³ where
  toFun := f âˆ˜ g
  finite_range' := f.finite_range.subset <| Set.range_comp_subset_range _ _
  measurableSet_fiber' z := hgm (f.measurableSet_fiber z)

@[simp]
theorem coe_comp [MeasurableSpace Î²] (f : Î² â†’â‚› Î³) {g : Î± â†’ Î²} (hgm : Measurable g) :
    â‡‘(f.comp g hgm) = f âˆ˜ g :=
  rfl

theorem range_comp_subset_range [MeasurableSpace Î²] (f : Î² â†’â‚› Î³) {g : Î± â†’ Î²} (hgm : Measurable g) :
    (f.comp g hgm).range âŠ† f.range :=
  Finset.coe_subset.1 <| by simp only [coe_range, coe_comp, Set.range_comp_subset_range]

/-- Extend a `SimpleFunc` along a measurable embedding: `fâ‚.extend g hg fâ‚‚` is the function
`F : Î² â†’â‚› Î³` such that `F âˆ˜ g = fâ‚` and `F y = fâ‚‚ y` whenever `y âˆ‰ range g`. -/
def extend [MeasurableSpace Î²] (fâ‚ : Î± â†’â‚› Î³) (g : Î± â†’ Î²) (hg : MeasurableEmbedding g)
    (fâ‚‚ : Î² â†’â‚› Î³) : Î² â†’â‚› Î³ where
  toFun := Function.extend g fâ‚ fâ‚‚
  finite_range' :=
    (fâ‚.finite_range.union <| fâ‚‚.finite_range.subset (image_subset_range _ _)).subset
      (range_extend_subset _ _ _)
  measurableSet_fiber' := by
    letI : MeasurableSpace Î³ := âŠ¤; haveI : MeasurableSingletonClass Î³ := âŸ¨fun _ => trivialâŸ©
    exact fun x => hg.measurable_extend fâ‚.measurable fâ‚‚.measurable (measurableSet_singleton _)

@[simp]
theorem extend_apply [MeasurableSpace Î²] (fâ‚ : Î± â†’â‚› Î³) {g : Î± â†’ Î²} (hg : MeasurableEmbedding g)
    (fâ‚‚ : Î² â†’â‚› Î³) (x : Î±) : (fâ‚.extend g hg fâ‚‚) (g x) = fâ‚ x :=
  hg.injective.extend_apply _ _ _

@[simp]
theorem extend_apply' [MeasurableSpace Î²] (fâ‚ : Î± â†’â‚› Î³) {g : Î± â†’ Î²} (hg : MeasurableEmbedding g)
    (fâ‚‚ : Î² â†’â‚› Î³) {y : Î²} (h : Â¬âˆƒ x, g x = y) : (fâ‚.extend g hg fâ‚‚) y = fâ‚‚ y :=
  Function.extend_apply' _ _ _ h

@[simp]
theorem extend_comp_eq' [MeasurableSpace Î²] (fâ‚ : Î± â†’â‚› Î³) {g : Î± â†’ Î²} (hg : MeasurableEmbedding g)
    (fâ‚‚ : Î² â†’â‚› Î³) : fâ‚.extend g hg fâ‚‚ âˆ˜ g = fâ‚ :=
  funext fun _ => extend_apply _ _ _ _

@[simp]
theorem extend_comp_eq [MeasurableSpace Î²] (fâ‚ : Î± â†’â‚› Î³) {g : Î± â†’ Î²} (hg : MeasurableEmbedding g)
    (fâ‚‚ : Î² â†’â‚› Î³) : (fâ‚.extend g hg fâ‚‚).comp g hg.measurable = fâ‚ :=
  coe_injective <| extend_comp_eq' _ hg _

/-- If `f` is a simple function taking values in `Î² â†’ Î³` and `g` is another simple function
with the same domain and codomain `Î²`, then `f.seq g = f a (g a)`. -/
def seq (f : Î± â†’â‚› Î² â†’ Î³) (g : Î± â†’â‚› Î²) : Î± â†’â‚› Î³ :=
  f.bind fun f => g.map f

@[simp]
theorem seq_apply (f : Î± â†’â‚› Î² â†’ Î³) (g : Î± â†’â‚› Î²) (a : Î±) : f.seq g a = f a (g a) :=
  rfl

/-- Combine two simple functions `f : Î± â†’â‚› Î²` and `g : Î± â†’â‚› Î²`
into `fun a => (f a, g a)`. -/
def pair (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) : Î± â†’â‚› Î² Ã— Î³ :=
  (f.map Prod.mk).seq g

@[simp]
theorem pair_apply (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) (a) : pair f g a = (f a, g a) :=
  rfl

theorem pair_preimage (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) (s : Set Î²) (t : Set Î³) :
    pair f g â»Â¹' s Ã—Ë¢ t = f â»Â¹' s âˆ© g â»Â¹' t :=
  rfl

-- A special form of `pair_preimage`
theorem pair_preimage_singleton (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) (b : Î²) (c : Î³) :
    pair f g â»Â¹' {(b, c)} = f â»Â¹' {b} âˆ© g â»Â¹' {c} := by
  rw [â† singleton_prod_singleton]
  exact pair_preimage _ _ _ _

@[simp] theorem map_fst_pair (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) : (f.pair g).map Prod.fst = f := rfl
@[simp] theorem map_snd_pair (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) : (f.pair g).map Prod.snd = g := rfl

@[simp]
theorem bind_const (f : Î± â†’â‚› Î²) : f.bind (const Î±) = f := by ext; simp

@[to_additive]
instance instOne [One Î²] : One (Î± â†’â‚› Î²) :=
  âŸ¨const Î± 1âŸ©

@[to_additive]
instance instMul [Mul Î²] : Mul (Î± â†’â‚› Î²) :=
  âŸ¨fun f g => (f.map (Â· * Â·)).seq gâŸ©

@[to_additive]
instance instDiv [Div Î²] : Div (Î± â†’â‚› Î²) :=
  âŸ¨fun f g => (f.map (Â· / Â·)).seq gâŸ©

@[to_additive]
instance instInv [Inv Î²] : Inv (Î± â†’â‚› Î²) :=
  âŸ¨fun f => f.map Inv.invâŸ©

instance instSup [Max Î²] : Max (Î± â†’â‚› Î²) :=
  âŸ¨fun f g => (f.map (Â· âŠ” Â·)).seq gâŸ©

instance instInf [Min Î²] : Min (Î± â†’â‚› Î²) :=
  âŸ¨fun f g => (f.map (Â· âŠ“ Â·)).seq gâŸ©

instance instLE [LE Î²] : LE (Î± â†’â‚› Î²) :=
  âŸ¨fun f g => âˆ€ a, f a â‰¤ g aâŸ©

@[to_additive (attr := simp)]
theorem const_one [One Î²] : const Î± (1 : Î²) = 1 :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem coe_one [One Î²] : â‡‘(1 : Î± â†’â‚› Î²) = 1 :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem coe_mul [Mul Î²] (f g : Î± â†’â‚› Î²) : â‡‘(f * g) = â‡‘f * â‡‘g :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem coe_inv [Inv Î²] (f : Î± â†’â‚› Î²) : â‡‘(fâ»Â¹) = (â‡‘f)â»Â¹ :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem coe_div [Div Î²] (f g : Î± â†’â‚› Î²) : â‡‘(f / g) = â‡‘f / â‡‘g :=
  rfl

@[simp, norm_cast]
theorem coe_le [LE Î²] {f g : Î± â†’â‚› Î²} : (f : Î± â†’ Î²) â‰¤ g â†” f â‰¤ g :=
  Iff.rfl

@[simp, norm_cast]
theorem coe_sup [Max Î²] (f g : Î± â†’â‚› Î²) : â‡‘(f âŠ” g) = â‡‘f âŠ” â‡‘g :=
  rfl

@[simp, norm_cast]
theorem coe_inf [Min Î²] (f g : Î± â†’â‚› Î²) : â‡‘(f âŠ“ g) = â‡‘f âŠ“ â‡‘g :=
  rfl

@[to_additive]
theorem mul_apply [Mul Î²] (f g : Î± â†’â‚› Î²) (a : Î±) : (f * g) a = f a * g a :=
  rfl

@[to_additive]
theorem div_apply [Div Î²] (f g : Î± â†’â‚› Î²) (x : Î±) : (f / g) x = f x / g x :=
  rfl

@[to_additive]
theorem inv_apply [Inv Î²] (f : Î± â†’â‚› Î²) (x : Î±) : fâ»Â¹ x = (f x)â»Â¹ :=
  rfl

theorem sup_apply [Max Î²] (f g : Î± â†’â‚› Î²) (a : Î±) : (f âŠ” g) a = f a âŠ” g a :=
  rfl

theorem inf_apply [Min Î²] (f g : Î± â†’â‚› Î²) (a : Î±) : (f âŠ“ g) a = f a âŠ“ g a :=
  rfl

@[to_additive (attr := simp)]
theorem range_one [Nonempty Î±] [One Î²] : (1 : Î± â†’â‚› Î²).range = {1} :=
  Finset.ext fun x => by simp

@[simp]
theorem range_eq_empty_of_isEmpty {Î²} [hÎ± : IsEmpty Î±] (f : Î± â†’â‚› Î²) : f.range = âˆ… := by
  rw [â† Finset.not_nonempty_iff_eq_empty]
  by_contra h
  obtain âŸ¨y, hy_memâŸ© := h
  rw [SimpleFunc.mem_range, Set.mem_range] at hy_mem
  obtain âŸ¨x, hxyâŸ© := hy_mem
  rw [isEmpty_iff] at hÎ±
  exact hÎ± x

theorem eq_zero_of_mem_range_zero [Zero Î²] : âˆ€ {y : Î²}, y âˆˆ (0 : Î± â†’â‚› Î²).range â†’ y = 0 :=
  @(forall_mem_range.2 fun _ => rfl)

@[to_additive]
theorem mul_eq_mapâ‚‚ [Mul Î²] (f g : Î± â†’â‚› Î²) : f * g = (pair f g).map fun p : Î² Ã— Î² => p.1 * p.2 :=
  rfl

theorem sup_eq_mapâ‚‚ [Max Î²] (f g : Î± â†’â‚› Î²) : f âŠ” g = (pair f g).map fun p : Î² Ã— Î² => p.1 âŠ” p.2 :=
  rfl

@[to_additive]
theorem const_mul_eq_map [Mul Î²] (f : Î± â†’â‚› Î²) (b : Î²) : const Î± b * f = f.map fun a => b * a :=
  rfl

@[to_additive]
theorem map_mul [Mul Î²] [Mul Î³] {g : Î² â†’ Î³} (hg : âˆ€ x y, g (x * y) = g x * g y) (fâ‚ fâ‚‚ : Î± â†’â‚› Î²) :
    (fâ‚ * fâ‚‚).map g = fâ‚.map g * fâ‚‚.map g :=
  ext fun _ => hg _ _

variable {K : Type*}

@[to_additive]
instance instSMul [SMul K Î²] : SMul K (Î± â†’â‚› Î²) :=
  âŸ¨fun k f => f.map (k â€¢ Â·)âŸ©

@[to_additive (attr := simp)]
theorem coe_smul [SMul K Î²] (c : K) (f : Î± â†’â‚› Î²) : â‡‘(c â€¢ f) = c â€¢ â‡‘f :=
  rfl

@[to_additive (attr := simp)]
theorem smul_apply [SMul K Î²] (k : K) (f : Î± â†’â‚› Î²) (a : Î±) : (k â€¢ f) a = k â€¢ f a :=
  rfl

instance hasNatSMul [AddMonoid Î²] : SMul â„• (Î± â†’â‚› Î²) := inferInstance

@[to_additive existing hasNatSMul]
instance hasNatPow [Monoid Î²] : Pow (Î± â†’â‚› Î²) â„• :=
  âŸ¨fun f n => f.map (Â· ^ n)âŸ©

@[simp]
theorem coe_pow [Monoid Î²] (f : Î± â†’â‚› Î²) (n : â„•) : â‡‘(f ^ n) = (â‡‘f) ^ n :=
  rfl

theorem pow_apply [Monoid Î²] (n : â„•) (f : Î± â†’â‚› Î²) (a : Î±) : (f ^ n) a = f a ^ n :=
  rfl

instance hasIntPow [DivInvMonoid Î²] : Pow (Î± â†’â‚› Î²) â„¤ :=
  âŸ¨fun f n => f.map (Â· ^ n)âŸ©

@[simp]
theorem coe_zpow [DivInvMonoid Î²] (f : Î± â†’â‚› Î²) (z : â„¤) : â‡‘(f ^ z) = (â‡‘f) ^ z :=
  rfl

theorem zpow_apply [DivInvMonoid Î²] (z : â„¤) (f : Î± â†’â‚› Î²) (a : Î±) : (f ^ z) a = f a ^ z :=
  rfl

-- TODO: work out how to generate these instances with `to_additive`, which gets confused by the
-- argument order swap between `coe_smul` and `coe_pow`.
section Additive

instance instAddMonoid [AddMonoid Î²] : AddMonoid (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.addMonoid (fun f => show Î± â†’ Î² from f) coe_injective coe_zero
    coe_add fun _ _ => coe_smul _ _

instance instAddCommMonoid [AddCommMonoid Î²] : AddCommMonoid (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.addCommMonoid (fun f => show Î± â†’ Î² from f)
    coe_injective coe_zero coe_add fun _ _ => coe_smul _ _

instance instAddGroup [AddGroup Î²] : AddGroup (Î± â†’â‚› Î²) :=
  Function.Injective.addGroup (fun f => show Î± â†’ Î² from f) coe_injective coe_zero coe_add coe_neg
    coe_sub (fun _ _ => coe_smul _ _) fun _ _ => coe_smul _ _

instance instAddCommGroup [AddCommGroup Î²] : AddCommGroup (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.addCommGroup (fun f => show Î± â†’ Î² from f) coe_injective
    coe_zero coe_add coe_neg coe_sub (fun _ _ => coe_smul _ _) fun _ _ => coe_smul _ _

end Additive

@[to_additive existing]
instance instMonoid [Monoid Î²] : Monoid (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.monoid (fun f => show Î± â†’ Î² from f) coe_injective coe_one
    coe_mul coe_pow

@[to_additive existing]
instance instCommMonoid [CommMonoid Î²] : CommMonoid (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.commMonoid (fun f => show Î± â†’ Î² from f) coe_injective coe_one
    coe_mul coe_pow

@[to_additive existing]
instance instGroup [Group Î²] : Group (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.group (fun f => show Î± â†’ Î² from f) coe_injective coe_one
    coe_mul coe_inv coe_div coe_pow coe_zpow

@[to_additive existing]
instance instCommGroup [CommGroup Î²] : CommGroup (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.commGroup (fun f => show Î± â†’ Î² from f) coe_injective coe_one
    coe_mul coe_inv coe_div coe_pow coe_zpow

instance [Monoid K] [MulAction K Î²] : MulAction K (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.mulAction (fun f => show Î± â†’ Î² from f) coe_injective coe_smul

instance instModule [Semiring K] [AddCommMonoid Î²] [Module K Î²] : Module K (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.module K âŸ¨âŸ¨fun f => show Î± â†’ Î² from f, coe_zeroâŸ©, coe_addâŸ©
    coe_injective coe_smul

theorem smul_eq_map [SMul K Î²] (k : K) (f : Î± â†’â‚› Î²) : k â€¢ f = f.map (k â€¢ Â·) :=
  rfl

lemma smul_const [SMul K Î²] (k : K) (b : Î²) :
    (k â€¢ const Î± b : Î± â†’â‚› Î²) = const Î± (k â€¢ b) := ext fun _ â†¦ rfl

instance [NonUnitalNonAssocSemiring Î²] : NonUnitalNonAssocSemiring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonUnitalNonAssocSemiring (fun f => show Î± â†’ Î² from f)
    coe_injective coe_zero coe_add coe_mul coe_smul

instance [NonUnitalSemiring Î²] : NonUnitalSemiring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonUnitalSemiring (fun f => show Î± â†’ Î² from f)
    SimpleFunc.coe_injective coe_zero coe_add coe_mul coe_smul

instance [NatCast Î²] : NatCast (Î± â†’â‚› Î²) where
  natCast n := const _ (NatCast.natCast n)

@[simp, norm_cast]
lemma coe_natCast [NatCast Î²] (n : â„•) :
    â‡‘(â†‘n : Î± â†’â‚› Î²) = fun _ â†¦ â†‘n := rfl

instance [NonAssocSemiring Î²] : NonAssocSemiring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonAssocSemiring (fun f => show Î± â†’ Î² from f)
    coe_injective coe_zero coe_one coe_add coe_mul coe_smul coe_natCast

instance [IntCast Î²] : IntCast (Î± â†’â‚› Î²) where
  intCast n := const _ (IntCast.intCast n)

@[simp, norm_cast]
lemma coe_intCast [IntCast Î²] (n : â„¤) :
    â‡‘(â†‘n : Î± â†’â‚› Î²) = fun _ â†¦ â†‘n := rfl

instance [NonAssocRing Î²] : NonAssocRing (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonAssocRing (fun f => show Î± â†’ Î² from f) coe_injective
    coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_smul coe_smul coe_natCast coe_intCast

instance [NonUnitalCommSemiring Î²] : NonUnitalCommSemiring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonUnitalCommSemiring (fun f => show Î± â†’ Î² from f)
    coe_injective coe_zero coe_add coe_mul coe_smul

instance [CommSemiring Î²] : CommSemiring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.commSemiring (fun f => show Î± â†’ Î² from f)
    coe_injective coe_zero coe_one coe_add coe_mul coe_smul coe_pow coe_natCast

instance [NonUnitalCommRing Î²] : NonUnitalCommRing (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonUnitalCommRing (fun f => show Î± â†’ Î² from f)
    coe_injective coe_zero coe_add coe_mul coe_neg coe_sub coe_smul coe_smul

instance [CommRing Î²] : CommRing (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.commRing (fun f => show Î± â†’ Î² from f) coe_injective coe_zero
    coe_one coe_add coe_mul coe_neg coe_sub coe_smul coe_smul coe_pow coe_natCast coe_intCast

instance [Semiring Î²] : Semiring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.semiring (fun f => show Î± â†’ Î² from f) coe_injective coe_zero
    coe_one coe_add coe_mul coe_smul coe_pow coe_natCast

instance [NonUnitalRing Î²] : NonUnitalRing (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.nonUnitalRing (fun f => show Î± â†’ Î² from f) coe_injective
    coe_zero coe_add coe_mul coe_neg coe_sub coe_smul coe_smul

instance [Ring Î²] : Ring (Î± â†’â‚› Î²) :=
  fast_instance% Function.Injective.ring (fun f => show Î± â†’ Î² from f) coe_injective coe_zero
    coe_one coe_add coe_mul coe_neg coe_sub coe_smul coe_smul coe_pow coe_natCast coe_intCast

instance [SMul K Î³] [SMul Î³ Î²] [SMul K Î²] [IsScalarTower K Î³ Î²] : IsScalarTower K Î³ (Î± â†’â‚› Î²) where
  smul_assoc _ _ _ := ext fun _ â†¦ smul_assoc ..

instance [SMul Î³ Î²] [SMul K Î²] [SMulCommClass K Î³ Î²] : SMulCommClass K Î³ (Î± â†’â‚› Î²) where
  smul_comm _ _ _ := ext fun _ â†¦  smul_comm ..

instance [CommSemiring K] [Semiring Î²] [Algebra K Î²] : Algebra K (Î± â†’â‚› Î²) where
  algebraMap :={
    toFun r := const Î± <| algebraMap K Î² r
    map_one' := ext fun _ â†¦ algebraMap K Î² |>.map_one â–¸ rfl
    map_mul' _ _ := ext fun _ â†¦ algebraMap K Î² |>.map_mul ..
    map_zero' := ext fun _ â†¦ algebraMap K Î² |>.map_zero â–¸ rfl
    map_add' _ _ := ext fun _ â†¦ algebraMap K Î² |>.map_add ..}
  commutes' _ _ := ext fun _ â†¦ Algebra.commutes ..
  smul_def' _ _ := ext fun _ â†¦ Algebra.smul_def ..

@[simp]
lemma const_algebraMap [CommSemiring K] [Semiring Î²] [Algebra K Î²] (k : K) :
    const Î± (algebraMap K Î² k) = algebraMap K (Î± â†’â‚› Î²) k := rfl

@[simp]
lemma coe_algebraMap [CommSemiring K] [Semiring Î²] [Algebra K Î²] (k : K) (x : Î±) :
    â‡‘(algebraMap K (Î± â†’â‚› Î²)) k x = algebraMap K (Î± â†’ Î²) k x := rfl

section Star

instance [Star Î²] : Star (Î± â†’â‚› Î²) where
  star f := f.map Star.star

@[simp]
lemma coe_star [Star Î²] {f : Î± â†’â‚› Î²} : â‡‘(star f) = star â‡‘f := rfl

instance [InvolutiveStar Î²] : InvolutiveStar (Î± â†’â‚› Î²) where
  star_involutive _ := ext fun _ â†¦ star_star _

instance [AddMonoid Î²] [StarAddMonoid Î²] : StarAddMonoid (Î± â†’â‚› Î²) where
  star_add _ _ := ext fun _ â†¦ star_add ..

instance [Mul Î²] [StarMul Î²] : StarMul (Î± â†’â‚› Î²) where
  star_mul _ _ := ext fun _ â†¦ star_mul ..

instance [NonUnitalNonAssocSemiring Î²] [StarRing Î²] : StarRing (Î± â†’â‚› Î²) where
  star_add _ _ := ext fun _ â†¦ star_add ..

end Star

section Preorder
variable [Preorder Î²] {s : Set Î±} {f fâ‚ fâ‚‚ g gâ‚ gâ‚‚ : Î± â†’â‚› Î²} {hs : MeasurableSet s}

instance instPreorder : Preorder (Î± â†’â‚› Î²) := Preorder.lift (â‡‘)

@[norm_cast] lemma coe_le_coe : â‡‘f â‰¤ g â†” f â‰¤ g := .rfl
@[simp, norm_cast] lemma coe_lt_coe : â‡‘f < g â†” f < g := .rfl

@[simp] lemma mk_le_mk {f g : Î± â†’ Î²} {hf hg hf' hg'} : mk f hf hf' â‰¤ mk g hg hg' â†” f â‰¤ g := Iff.rfl
@[simp] lemma mk_lt_mk {f g : Î± â†’ Î²} {hf hg hf' hg'} : mk f hf hf' < mk g hg hg' â†” f < g := Iff.rfl

@[gcongr] protected alias âŸ¨_, GCongr.mk_le_mkâŸ© := mk_le_mk
@[gcongr] protected alias âŸ¨_, GCongr.mk_lt_mkâŸ© := mk_lt_mk
@[gcongr] protected alias âŸ¨_, GCongr.coe_le_coeâŸ© := coe_le_coe
@[gcongr] protected alias âŸ¨_, GCongr.coe_lt_coeâŸ© := coe_lt_coe

open scoped Classical in
@[gcongr]
lemma piecewise_mono (hf : âˆ€ a âˆˆ s, fâ‚ a â‰¤ fâ‚‚ a) (hg : âˆ€ a âˆ‰ s, gâ‚ a â‰¤ gâ‚‚ a) :
    piecewise s hs fâ‚ gâ‚ â‰¤ piecewise s hs fâ‚‚ gâ‚‚ := Set.piecewise_mono hf hg

end Preorder

instance instPartialOrder [PartialOrder Î²] : PartialOrder (Î± â†’â‚› Î²) :=
  { SimpleFunc.instPreorder with
    le_antisymm := fun _f _g hfg hgf => ext fun a => le_antisymm (hfg a) (hgf a) }

instance instOrderBot [LE Î²] [OrderBot Î²] : OrderBot (Î± â†’â‚› Î²) where
  bot := const Î± âŠ¥
  bot_le _ _ := bot_le

instance instOrderTop [LE Î²] [OrderTop Î²] : OrderTop (Î± â†’â‚› Î²) where
  top := const Î± âŠ¤
  le_top _ _ := le_top

@[to_additive]
instance [CommMonoid Î²] [PartialOrder Î²] [IsOrderedMonoid Î²] :
    IsOrderedMonoid (Î± â†’â‚› Î²) where
  mul_le_mul_left _ _ h _ _ := mul_le_mul_left' (h _) _

instance instSemilatticeInf [SemilatticeInf Î²] : SemilatticeInf (Î± â†’â‚› Î²) :=
  { SimpleFunc.instPartialOrder with
    inf := (Â· âŠ“ Â·)
    inf_le_left := fun _ _ _ => inf_le_left
    inf_le_right := fun _ _ _ => inf_le_right
    le_inf := fun _f _g _h hfh hgh a => le_inf (hfh a) (hgh a) }

instance instSemilatticeSup [SemilatticeSup Î²] : SemilatticeSup (Î± â†’â‚› Î²) :=
  { SimpleFunc.instPartialOrder with
    sup := (Â· âŠ” Â·)
    le_sup_left := fun _ _ _ => le_sup_left
    le_sup_right := fun _ _ _ => le_sup_right
    sup_le := fun _f _g _h hfh hgh a => sup_le (hfh a) (hgh a) }

instance instLattice [Lattice Î²] : Lattice (Î± â†’â‚› Î²) :=
  { SimpleFunc.instSemilatticeSup, SimpleFunc.instSemilatticeInf with }

instance instBoundedOrder [LE Î²] [BoundedOrder Î²] : BoundedOrder (Î± â†’â‚› Î²) :=
  { SimpleFunc.instOrderBot, SimpleFunc.instOrderTop with }

theorem finset_sup_apply [SemilatticeSup Î²] [OrderBot Î²] {f : Î³ â†’ Î± â†’â‚› Î²} (s : Finset Î³) (a : Î±) :
    s.sup f a = s.sup fun c => f c a := by
  classical
  refine Finset.induction_on s rfl ?_
  intro a s _ ih
  rw [Finset.sup_insert, Finset.sup_insert, sup_apply, ih]

section Restrict

variable [Zero Î²]

open scoped Classical in
/-- Restrict a simple function `f : Î± â†’â‚› Î²` to a set `s`. If `s` is measurable,
then `f.restrict s a = if a âˆˆ s then f a else 0`, otherwise `f.restrict s = const Î± 0`. -/
def restrict (f : Î± â†’â‚› Î²) (s : Set Î±) : Î± â†’â‚› Î² :=
  if hs : MeasurableSet s then piecewise s hs f 0 else 0

theorem restrict_of_not_measurable {f : Î± â†’â‚› Î²} {s : Set Î±} (hs : Â¬MeasurableSet s) :
    restrict f s = 0 :=
  dif_neg hs

@[simp]
theorem coe_restrict (f : Î± â†’â‚› Î²) {s : Set Î±} (hs : MeasurableSet s) :
    â‡‘(restrict f s) = indicator s f := by
  classical
  rw [restrict, dif_pos hs, coe_piecewise, coe_zero, piecewise_eq_indicator]

@[simp]
theorem restrict_univ (f : Î± â†’â‚› Î²) : restrict f univ = f := by simp [restrict]

@[simp]
theorem restrict_empty (f : Î± â†’â‚› Î²) : restrict f âˆ… = 0 := by simp [restrict]

open scoped Classical in
theorem map_restrict_of_zero [Zero Î³] {g : Î² â†’ Î³} (hg : g 0 = 0) (f : Î± â†’â‚› Î²) (s : Set Î±) :
    (f.restrict s).map g = (f.map g).restrict s :=
  ext fun x =>
    if hs : MeasurableSet s then by simp [hs, Set.indicator_comp_of_zero hg]
    else by simp [restrict_of_not_measurable hs, hg]

theorem map_coe_ennreal_restrict (f : Î± â†’â‚› â„â‰¥0) (s : Set Î±) :
    (f.restrict s).map ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) = (f.map (â†‘)).restrict s :=
  map_restrict_of_zero ENNReal.coe_zero _ _

theorem map_coe_nnreal_restrict (f : Î± â†’â‚› â„â‰¥0) (s : Set Î±) :
    (f.restrict s).map ((â†‘) : â„â‰¥0 â†’ â„) = (f.map (â†‘)).restrict s :=
  map_restrict_of_zero NNReal.coe_zero _ _

theorem restrict_apply (f : Î± â†’â‚› Î²) {s : Set Î±} (hs : MeasurableSet s) (a) :
    restrict f s a = indicator s f a := by simp only [f.coe_restrict hs]

theorem restrict_preimage (f : Î± â†’â‚› Î²) {s : Set Î±} (hs : MeasurableSet s) {t : Set Î²}
    (ht : (0 : Î²) âˆ‰ t) : restrict f s â»Â¹' t = s âˆ© f â»Â¹' t := by
  simp [hs, indicator_preimage_of_notMem _ _ ht, inter_comm]

theorem restrict_preimage_singleton (f : Î± â†’â‚› Î²) {s : Set Î±} (hs : MeasurableSet s) {r : Î²}
    (hr : r â‰  0) : restrict f s â»Â¹' {r} = s âˆ© f â»Â¹' {r} :=
  f.restrict_preimage hs hr.symm

theorem mem_restrict_range {r : Î²} {s : Set Î±} {f : Î± â†’â‚› Î²} (hs : MeasurableSet s) :
    r âˆˆ (restrict f s).range â†” r = 0 âˆ§ s â‰  univ âˆ¨ r âˆˆ f '' s := by
  rw [â† Finset.mem_coe, coe_range, coe_restrict _ hs, mem_range_indicator]

open scoped Classical in
theorem mem_image_of_mem_range_restrict {r : Î²} {s : Set Î±} {f : Î± â†’â‚› Î²}
    (hr : r âˆˆ (restrict f s).range) (h0 : r â‰  0) : r âˆˆ f '' s :=
  if hs : MeasurableSet s then by simpa [mem_restrict_range hs, h0, -mem_range] using hr
  else by
    rw [restrict_of_not_measurable hs] at hr
    exact (h0 <| eq_zero_of_mem_range_zero hr).elim

open scoped Classical in
@[gcongr, mono]
theorem restrict_mono [Preorder Î²] (s : Set Î±) {f g : Î± â†’â‚› Î²} (H : f â‰¤ g) :
    f.restrict s â‰¤ g.restrict s :=
  if hs : MeasurableSet s then fun x => by
    simp only [coe_restrict _ hs, indicator_le_indicator (H x)]
  else by simp only [restrict_of_not_measurable hs, le_refl]

end Restrict

section Approx

section

variable [SemilatticeSup Î²] [OrderBot Î²] [Zero Î²]

/-- Fix a sequence `i : â„• â†’ Î²`. Given a function `Î± â†’ Î²`, its `n`-th approximation
by simple functions is defined so that in case `Î² = â„â‰¥0âˆ` it sends each `a` to the supremum
of the set `{i k | k â‰¤ n âˆ§ i k â‰¤ f a}`, see `approx_apply` and `iSup_approx_apply` for details. -/
def approx (i : â„• â†’ Î²) (f : Î± â†’ Î²) (n : â„•) : Î± â†’â‚› Î² :=
  (Finset.range n).sup fun k => restrict (const Î± (i k)) { a : Î± | i k â‰¤ f a }

open scoped Classical in
theorem approx_apply [TopologicalSpace Î²] [OrderClosedTopology Î²] [MeasurableSpace Î²]
    [OpensMeasurableSpace Î²] {i : â„• â†’ Î²} {f : Î± â†’ Î²} {n : â„•} (a : Î±) (hf : Measurable f) :
    (approx i f n : Î± â†’â‚› Î²) a = (Finset.range n).sup fun k => if i k â‰¤ f a then i k else 0 := by
  dsimp only [approx]
  rw [finset_sup_apply]
  congr
  funext k
  rw [restrict_apply]
  Â· simp only [coe_const, mem_setOf_eq, indicator_apply, Function.const_apply]
  Â· exact hf measurableSet_Ici

theorem monotone_approx (i : â„• â†’ Î²) (f : Î± â†’ Î²) : Monotone (approx i f) := fun _ _ h =>
  Finset.sup_mono <| Finset.range_subset.2 h

theorem approx_comp [TopologicalSpace Î²] [OrderClosedTopology Î²] [MeasurableSpace Î²]
    [OpensMeasurableSpace Î²] [MeasurableSpace Î³] {i : â„• â†’ Î²} {f : Î³ â†’ Î²} {g : Î± â†’ Î³} {n : â„•} (a : Î±)
    (hf : Measurable f) (hg : Measurable g) :
    (approx i (f âˆ˜ g) n : Î± â†’â‚› Î²) a = (approx i f n : Î³ â†’â‚› Î²) (g a) := by
  rw [approx_apply _ hf, approx_apply _ (hf.comp hg), Function.comp_apply]

end

theorem iSup_approx_apply [TopologicalSpace Î²] [CompleteLattice Î²] [OrderClosedTopology Î²] [Zero Î²]
    [MeasurableSpace Î²] [OpensMeasurableSpace Î²] (i : â„• â†’ Î²) (f : Î± â†’ Î²) (a : Î±) (hf : Measurable f)
    (h_zero : (0 : Î²) = âŠ¥) : â¨† n, (approx i f n : Î± â†’â‚› Î²) a = â¨† (k) (_ : i k â‰¤ f a), i k := by
  refine le_antisymm (iSup_le fun n => ?_) (iSup_le fun k => iSup_le fun hk => ?_)
  Â· rw [approx_apply a hf, h_zero]
    refine Finset.sup_le fun k _ => ?_
    split_ifs with h
    Â· exact le_iSup_of_le k (le_iSup (fun _ : i k â‰¤ f a => i k) h)
    Â· exact bot_le
  Â· refine le_iSup_of_le (k + 1) ?_
    rw [approx_apply a hf]
    have : k âˆˆ Finset.range (k + 1) := Finset.mem_range.2 (Nat.lt_succ_self _)
    refine le_trans (le_of_eq ?_) (Finset.le_sup this)
    rw [if_pos hk]

end Approx

section EApprox
variable {f : Î± â†’ â„â‰¥0âˆ}

/-- A sequence of `â„â‰¥0âˆ`s such that its range is the set of non-negative rational numbers. -/
def ennrealRatEmbed (n : â„•) : â„â‰¥0âˆ :=
  ENNReal.ofReal ((Encodable.decode (Î± := â„š) n).getD (0 : â„š))

theorem ennrealRatEmbed_encode (q : â„š) :
    ennrealRatEmbed (Encodable.encode q) = Real.toNNReal q := by
  rw [ennrealRatEmbed, Encodable.encodek]; rfl

/-- Approximate a function `Î± â†’ â„â‰¥0âˆ` by a sequence of simple functions. -/
def eapprox : (Î± â†’ â„â‰¥0âˆ) â†’ â„• â†’ Î± â†’â‚› â„â‰¥0âˆ :=
  approx ennrealRatEmbed

theorem eapprox_lt_top (f : Î± â†’ â„â‰¥0âˆ) (n : â„•) (a : Î±) : eapprox f n a < âˆ := by
  simp only [eapprox, approx, finset_sup_apply, restrict]
  rw [Finset.sup_lt_iff (Î± := â„â‰¥0âˆ) WithTop.top_pos]
  intro b _
  split_ifs
  Â· simp only [coe_zero, coe_piecewise, piecewise_eq_indicator, coe_const]
    calc
      { a : Î± | ennrealRatEmbed b â‰¤ f a }.indicator (fun _ => ennrealRatEmbed b) a â‰¤
          ennrealRatEmbed b :=
        indicator_le_self _ _ a
      _ < âŠ¤ := ENNReal.coe_lt_top
  Â· exact WithTop.top_pos

@[mono]
theorem monotone_eapprox (f : Î± â†’ â„â‰¥0âˆ) : Monotone (eapprox f) :=
  monotone_approx _ f

@[gcongr]
lemma eapprox_mono {m n : â„•} (hmn : m â‰¤ n) : eapprox f m â‰¤ eapprox f n := monotone_eapprox _ hmn

lemma iSup_eapprox_apply (hf : Measurable f) (a : Î±) : â¨† n, (eapprox f n : Î± â†’â‚› â„â‰¥0âˆ) a = f a := by
  rw [eapprox, iSup_approx_apply ennrealRatEmbed f a hf rfl]
  refine le_antisymm (iSup_le fun i => iSup_le fun hi => hi) (le_of_not_gt ?_)
  intro h
  rcases ENNReal.lt_iff_exists_rat_btwn.1 h with âŸ¨q, _, lt_q, q_ltâŸ©
  have :
    (Real.toNNReal q : â„â‰¥0âˆ) â‰¤ â¨† (k : â„•) (_ : ennrealRatEmbed k â‰¤ f a), ennrealRatEmbed k := by
    refine le_iSup_of_le (Encodable.encode q) ?_
    rw [ennrealRatEmbed_encode q]
    exact le_iSup_of_le (le_of_lt q_lt) le_rfl
  exact lt_irrefl _ (lt_of_le_of_lt this lt_q)

lemma iSup_coe_eapprox (hf : Measurable f) : â¨† n, â‡‘(eapprox f n) = f := by
  simpa [funext_iff] using iSup_eapprox_apply hf

theorem eapprox_comp [MeasurableSpace Î³] {f : Î³ â†’ â„â‰¥0âˆ} {g : Î± â†’ Î³} {n : â„•} (hf : Measurable f)
    (hg : Measurable g) : (eapprox (f âˆ˜ g) n : Î± â†’ â„â‰¥0âˆ) = (eapprox f n : Î³ â†’â‚› â„â‰¥0âˆ) âˆ˜ g :=
  funext fun a => approx_comp a hf hg

lemma tendsto_eapprox {f : Î± â†’ â„â‰¥0âˆ} (hf_meas : Measurable f) (a : Î±) :
    Tendsto (fun n â†¦ eapprox f n a) atTop (ğ“ (f a)) := by
  nth_rw 2 [â† iSup_coe_eapprox hf_meas]
  rw [iSup_apply]
  exact tendsto_atTop_iSup fun _ _ hnm â†¦ monotone_eapprox f hnm a

/-- Approximate a function `Î± â†’ â„â‰¥0âˆ` by a series of simple functions taking their values
in `â„â‰¥0`. -/
def eapproxDiff (f : Î± â†’ â„â‰¥0âˆ) : â„• â†’ Î± â†’â‚› â„â‰¥0
  | 0 => (eapprox f 0).map ENNReal.toNNReal
  | n + 1 => (eapprox f (n + 1) - eapprox f n).map ENNReal.toNNReal

theorem sum_eapproxDiff (f : Î± â†’ â„â‰¥0âˆ) (n : â„•) (a : Î±) :
    (âˆ‘ k âˆˆ Finset.range (n + 1), (eapproxDiff f k a : â„â‰¥0âˆ)) = eapprox f n a := by
  induction n with
  | zero =>
    simp [eapproxDiff, (eapprox_lt_top f 0 a).ne]
  | succ n IH =>
    rw [Finset.sum_range_succ, IH, eapproxDiff, coe_map, Function.comp_apply,
      coe_sub, Pi.sub_apply, ENNReal.coe_toNNReal,
      add_tsub_cancel_of_le (monotone_eapprox f (Nat.le_succ _) _)]
    apply (lt_of_le_of_lt _ (eapprox_lt_top f (n + 1) a)).ne
    rw [tsub_le_iff_right]
    exact le_self_add

theorem tsum_eapproxDiff (f : Î± â†’ â„â‰¥0âˆ) (hf : Measurable f) (a : Î±) :
    (âˆ‘' n, (eapproxDiff f n a : â„â‰¥0âˆ)) = f a := by
  simp_rw [ENNReal.tsum_eq_iSup_nat' (tendsto_add_atTop_nat 1), sum_eapproxDiff,
    iSup_eapprox_apply hf a]

end EApprox

end Measurable

section Measure

variable {m : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±}

/-- Integral of a simple function whose codomain is `â„â‰¥0âˆ`. -/
def lintegral {_m : MeasurableSpace Î±} (f : Î± â†’â‚› â„â‰¥0âˆ) (Î¼ : Measure Î±) : â„â‰¥0âˆ :=
  âˆ‘ x âˆˆ f.range, x * Î¼ (f â»Â¹' {x})

theorem lintegral_eq_of_subset (f : Î± â†’â‚› â„â‰¥0âˆ) {s : Finset â„â‰¥0âˆ}
    (hs : âˆ€ x, f x â‰  0 â†’ Î¼ (f â»Â¹' {f x}) â‰  0 â†’ f x âˆˆ s) :
    f.lintegral Î¼ = âˆ‘ x âˆˆ s, x * Î¼ (f â»Â¹' {x}) := by
  refine Finset.sum_bij_ne_zero (fun r _ _ => r) ?_ ?_ ?_ ?_
  Â· simpa only [forall_mem_range, mul_ne_zero_iff, and_imp]
  Â· intros
    assumption
  Â· intro b _ hb
    refine âŸ¨b, ?_, hb, rflâŸ©
    rw [mem_range, â† preimage_singleton_nonempty]
    exact nonempty_of_measure_ne_zero (mul_ne_zero_iff.1 hb).2
  Â· intros
    rfl

theorem lintegral_eq_of_subset' (f : Î± â†’â‚› â„â‰¥0âˆ) {s : Finset â„â‰¥0âˆ} (hs : f.range \ {0} âŠ† s) :
    f.lintegral Î¼ = âˆ‘ x âˆˆ s, x * Î¼ (f â»Â¹' {x}) :=
  f.lintegral_eq_of_subset fun x hfx _ =>
    hs <| Finset.mem_sdiff.2 âŸ¨f.mem_range_self x, mt Finset.mem_singleton.1 hfxâŸ©

/-- Calculate the integral of `(g âˆ˜ f)`, where `g : Î² â†’ â„â‰¥0âˆ` and `f : Î± â†’â‚› Î²`. -/
theorem map_lintegral (g : Î² â†’ â„â‰¥0âˆ) (f : Î± â†’â‚› Î²) :
    (f.map g).lintegral Î¼ = âˆ‘ x âˆˆ f.range, g x * Î¼ (f â»Â¹' {x}) := by
  simp only [lintegral, range_map]
  refine Finset.sum_image' _ fun b hb => ?_
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©
  rw [map_preimage_singleton, â† f.sum_measure_preimage_singleton, Finset.mul_sum]
  refine Finset.sum_congr ?_ ?_
  Â· congr
  Â· grind [Finset.mem_filter]

theorem add_lintegral (f g : Î± â†’â‚› â„â‰¥0âˆ) : (f + g).lintegral Î¼ = f.lintegral Î¼ + g.lintegral Î¼ :=
  calc
    (f + g).lintegral Î¼ =
        âˆ‘ x âˆˆ (pair f g).range, (x.1 * Î¼ (pair f g â»Â¹' {x}) + x.2 * Î¼ (pair f g â»Â¹' {x})) := by
      rw [add_eq_mapâ‚‚, map_lintegral]; exact Finset.sum_congr rfl fun a _ => add_mul _ _ _
    _ = (âˆ‘ x âˆˆ (pair f g).range, x.1 * Î¼ (pair f g â»Â¹' {x})) +
          âˆ‘ x âˆˆ (pair f g).range, x.2 * Î¼ (pair f g â»Â¹' {x}) := by
      rw [Finset.sum_add_distrib]
    _ = ((pair f g).map Prod.fst).lintegral Î¼ + ((pair f g).map Prod.snd).lintegral Î¼ := by
      rw [map_lintegral, map_lintegral]
    _ = lintegral f Î¼ + lintegral g Î¼ := rfl

theorem const_mul_lintegral (f : Î± â†’â‚› â„â‰¥0âˆ) (x : â„â‰¥0âˆ) :
    (const Î± x * f).lintegral Î¼ = x * f.lintegral Î¼ :=
  calc
    (f.map fun a => x * a).lintegral Î¼ = âˆ‘ r âˆˆ f.range, x * r * Î¼ (f â»Â¹' {r}) := map_lintegral _ _
    _ = x * âˆ‘ r âˆˆ f.range, r * Î¼ (f â»Â¹' {r}) := by simp_rw [Finset.mul_sum, mul_assoc]

/-- Integral of a simple function `Î± â†’â‚› â„â‰¥0âˆ` as a bilinear map. -/
def lintegralâ‚— {m : MeasurableSpace Î±} : (Î± â†’â‚› â„â‰¥0âˆ) â†’â‚—[â„â‰¥0âˆ] Measure Î± â†’â‚—[â„â‰¥0âˆ] â„â‰¥0âˆ where
  toFun f :=
    { toFun := lintegral f
      map_add' := by simp [lintegral, mul_add, Finset.sum_add_distrib]
      map_smul' := fun c Î¼ => by
        simp [lintegral, mul_left_comm _ c, Finset.mul_sum, Measure.smul_apply c] }
  map_add' f g := LinearMap.ext fun _ => add_lintegral f g
  map_smul' c f := LinearMap.ext fun _ => const_mul_lintegral f c

@[simp]
theorem zero_lintegral : (0 : Î± â†’â‚› â„â‰¥0âˆ).lintegral Î¼ = 0 :=
  LinearMap.ext_iff.1 lintegralâ‚—.map_zero Î¼

theorem lintegral_add {Î½} (f : Î± â†’â‚› â„â‰¥0âˆ) : f.lintegral (Î¼ + Î½) = f.lintegral Î¼ + f.lintegral Î½ :=
  (lintegralâ‚— f).map_add Î¼ Î½

theorem lintegral_smul {R : Type*} [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]
    (f : Î± â†’â‚› â„â‰¥0âˆ) (c : R) : f.lintegral (c â€¢ Î¼) = c â€¢ f.lintegral Î¼ := by
  simpa only [smul_one_smul] using (lintegralâ‚— f).map_smul (c â€¢ 1) Î¼

@[simp]
theorem lintegral_zero [MeasurableSpace Î±] (f : Î± â†’â‚› â„â‰¥0âˆ) : f.lintegral 0 = 0 :=
  (lintegralâ‚— f).map_zero

theorem lintegral_finset_sum {Î¹} (f : Î± â†’â‚› â„â‰¥0âˆ) (Î¼ : Î¹ â†’ Measure Î±) (s : Finset Î¹) :
    f.lintegral (âˆ‘ i âˆˆ s, Î¼ i) = âˆ‘ i âˆˆ s, f.lintegral (Î¼ i) :=
  map_sum (lintegralâ‚— f) ..

theorem lintegral_sum {m : MeasurableSpace Î±} {Î¹} (f : Î± â†’â‚› â„â‰¥0âˆ) (Î¼ : Î¹ â†’ Measure Î±) :
    f.lintegral (Measure.sum Î¼) = âˆ‘' i, f.lintegral (Î¼ i) := by
  simp only [lintegral, Measure.sum_apply, f.measurableSet_preimage, â† Finset.tsum_subtype, â†
    ENNReal.tsum_mul_left]
  apply ENNReal.tsum_comm

open scoped Classical in
theorem restrict_lintegral (f : Î± â†’â‚› â„â‰¥0âˆ) {s : Set Î±} (hs : MeasurableSet s) :
    (restrict f s).lintegral Î¼ = âˆ‘ r âˆˆ f.range, r * Î¼ (f â»Â¹' {r} âˆ© s) :=
  calc
    (restrict f s).lintegral Î¼ = âˆ‘ r âˆˆ f.range, r * Î¼ (restrict f s â»Â¹' {r}) :=
      lintegral_eq_of_subset _ fun x hx =>
        if hxs : x âˆˆ s then fun _ => by
          simp only [f.restrict_apply hs, indicator_of_mem hxs, mem_range_self]
        else False.elim <| hx <| by simp [*]
    _ = âˆ‘ r âˆˆ f.range, r * Î¼ (f â»Â¹' {r} âˆ© s) :=
      Finset.sum_congr rfl <|
        forall_mem_range.2 fun b =>
          if hb : f b = 0 then by simp only [hb, zero_mul]
          else by rw [restrict_preimage_singleton _ hs hb, inter_comm]

theorem lintegral_restrict {m : MeasurableSpace Î±} (f : Î± â†’â‚› â„â‰¥0âˆ) (s : Set Î±) (Î¼ : Measure Î±) :
    f.lintegral (Î¼.restrict s) = âˆ‘ y âˆˆ f.range, y * Î¼ (f â»Â¹' {y} âˆ© s) := by
  simp only [lintegral, Measure.restrict_apply, f.measurableSet_preimage]

theorem restrict_lintegral_eq_lintegral_restrict (f : Î± â†’â‚› â„â‰¥0âˆ) {s : Set Î±}
    (hs : MeasurableSet s) : (restrict f s).lintegral Î¼ = f.lintegral (Î¼.restrict s) := by
  rw [f.restrict_lintegral hs, lintegral_restrict]

theorem lintegral_restrict_iUnion_of_directed {Î¹ : Type*} [Countable Î¹]
    (f : Î± â†’â‚› â„â‰¥0âˆ) {s : Î¹ â†’ Set Î±} (hd : Directed (Â· âŠ† Â·) s) (Î¼ : Measure Î±) :
    f.lintegral (Î¼.restrict (â‹ƒ i, s i)) = â¨† i, f.lintegral (Î¼.restrict (s i)) := by
  simp only [lintegral, Measure.restrict_iUnion_apply_eq_iSup hd (measurableSet_preimage ..),
    ENNReal.mul_iSup]
  refine finsetSum_iSup fun i j â†¦ (hd i j).imp fun k âŸ¨hik, hjkâŸ© â†¦ fun a â†¦ ?_
  -- TODO https://github.com/leanprover-community/mathlib4/pull/14739 make `gcongr` close this goal
  constructor <;> Â· gcongr; refine Measure.restrict_mono ?_ le_rfl _; assumption

theorem const_lintegral (c : â„â‰¥0âˆ) : (const Î± c).lintegral Î¼ = c * Î¼ univ := by
  rw [lintegral]
  cases isEmpty_or_nonempty Î±
  Â· simp [Î¼.eq_zero_of_isEmpty]
  Â· simp only [range_const, coe_const, Finset.sum_singleton]
    unfold Function.const; rw [preimage_const_of_mem (mem_singleton c)]

theorem const_lintegral_restrict (c : â„â‰¥0âˆ) (s : Set Î±) :
    (const Î± c).lintegral (Î¼.restrict s) = c * Î¼ s := by
  rw [const_lintegral, Measure.restrict_apply MeasurableSet.univ, univ_inter]

theorem restrict_const_lintegral (c : â„â‰¥0âˆ) {s : Set Î±} (hs : MeasurableSet s) :
    ((const Î± c).restrict s).lintegral Î¼ = c * Î¼ s := by
  rw [restrict_lintegral_eq_lintegral_restrict _ hs, const_lintegral_restrict]

theorem lintegral_mono_fun {f g : Î± â†’â‚› â„â‰¥0âˆ} (h : f â‰¤ g) : f.lintegral Î¼ â‰¤ g.lintegral Î¼ := by
  refine Monotone.of_left_le_map_sup (f := (lintegral Â· Î¼)) (fun f g â†¦ ?_) h
  calc
    f.lintegral Î¼ = ((pair f g).map Prod.fst).lintegral Î¼ := by rw [map_fst_pair]
    _ â‰¤ ((pair f g).map fun p â†¦ p.1 âŠ” p.2).lintegral Î¼ := by
      simp only [map_lintegral]
      gcongr
      exact le_sup_left

theorem le_sup_lintegral (f g : Î± â†’â‚› â„â‰¥0âˆ) : f.lintegral Î¼ âŠ” g.lintegral Î¼ â‰¤ (f âŠ” g).lintegral Î¼ :=
  Monotone.le_map_sup (fun _ _ â†¦ lintegral_mono_fun) f g

theorem lintegral_mono_measure {f : Î± â†’â‚› â„â‰¥0âˆ} (h : Î¼ â‰¤ Î½) : f.lintegral Î¼ â‰¤ f.lintegral Î½ := by
  simp only [lintegral]
  gcongr
  apply h

/-- `SimpleFunc.lintegral` is monotone both in function and in measure. -/
@[mono, gcongr]
theorem lintegral_mono {f g : Î± â†’â‚› â„â‰¥0âˆ} (hfg : f â‰¤ g) (hÎ¼Î½ : Î¼ â‰¤ Î½) :
    f.lintegral Î¼ â‰¤ g.lintegral Î½ :=
  (lintegral_mono_fun hfg).trans (lintegral_mono_measure hÎ¼Î½)

/-- `SimpleFunc.lintegral` depends only on the measures of `f â»Â¹' {y}`. -/
theorem lintegral_eq_of_measure_preimage [MeasurableSpace Î²] {f : Î± â†’â‚› â„â‰¥0âˆ} {g : Î² â†’â‚› â„â‰¥0âˆ}
    {Î½ : Measure Î²} (H : âˆ€ y, Î¼ (f â»Â¹' {y}) = Î½ (g â»Â¹' {y})) : f.lintegral Î¼ = g.lintegral Î½ := by
  simp only [lintegral, â† H]
  apply lintegral_eq_of_subset
  simp only [H]
  intros
  exact mem_range_of_measure_ne_zero â€¹_â€º

/-- If two simple functions are equal a.e., then their `lintegral`s are equal. -/
theorem lintegral_congr {f g : Î± â†’â‚› â„â‰¥0âˆ} (h : f =áµ[Î¼] g) : f.lintegral Î¼ = g.lintegral Î¼ :=
  lintegral_eq_of_measure_preimage fun y =>
    measure_congr <| Eventually.set_eq <| h.mono fun x hx => by simp [hx]

theorem lintegral_map' {Î²} [MeasurableSpace Î²] {Î¼' : Measure Î²} (f : Î± â†’â‚› â„â‰¥0âˆ) (g : Î² â†’â‚› â„â‰¥0âˆ)
    (m' : Î± â†’ Î²) (eq : âˆ€ a, f a = g (m' a)) (h : âˆ€ s, MeasurableSet s â†’ Î¼' s = Î¼ (m' â»Â¹' s)) :
    f.lintegral Î¼ = g.lintegral Î¼' :=
  lintegral_eq_of_measure_preimage fun y => by
    simp only [preimage, eq]
    exact (h (g â»Â¹' {y}) (g.measurableSet_preimage _)).symm

theorem lintegral_map {Î²} [MeasurableSpace Î²] (g : Î² â†’â‚› â„â‰¥0âˆ) {f : Î± â†’ Î²} (hf : Measurable f) :
    g.lintegral (Measure.map f Î¼) = (g.comp f hf).lintegral Î¼ :=
  Eq.symm <| lintegral_map' _ _ f (fun _ => rfl) fun _s hs => Measure.map_apply hf hs

end Measure

section FinMeasSupp

open Finset Function

open scoped Classical in
theorem support_eq [MeasurableSpace Î±] [Zero Î²] (f : Î± â†’â‚› Î²) :
    support f = â‹ƒ y âˆˆ {y âˆˆ f.range | y â‰  0}, f â»Â¹' {y} :=
  Set.ext fun x => by
    simp only [mem_support, Set.mem_preimage, mem_filter, mem_range_self, true_and, exists_prop,
      mem_iUnion, mem_singleton_iff, exists_eq_right']

variable {m : MeasurableSpace Î±} [Zero Î²] [Zero Î³] {Î¼ : Measure Î±} {f : Î± â†’â‚› Î²}

theorem measurableSet_support [MeasurableSpace Î±] (f : Î± â†’â‚› Î²) : MeasurableSet (support f) := by
  rw [f.support_eq]
  exact Finset.measurableSet_biUnion _ fun y _ => measurableSet_fiber _ _

lemma measure_support_lt_top (f : Î± â†’â‚› Î²) (hf : âˆ€ y, y â‰  0 â†’ Î¼ (f â»Â¹' {y}) < âˆ) :
    Î¼ (support f) < âˆ := by
  classical
  rw [support_eq]
  refine (measure_biUnion_finset_le _ _).trans_lt (ENNReal.sum_lt_top.mpr fun y hy => ?_)
  rw [Finset.mem_filter] at hy
  exact hf y hy.2

/-- A `SimpleFunc` has finite measure support if it is equal to `0` outside of a set of finite
measure. -/
protected def FinMeasSupp {_m : MeasurableSpace Î±} (f : Î± â†’â‚› Î²) (Î¼ : Measure Î±) : Prop :=
  f =á¶ [Î¼.cofinite] 0

theorem finMeasSupp_iff_support : f.FinMeasSupp Î¼ â†” Î¼ (support f) < âˆ :=
  Iff.rfl

theorem finMeasSupp_iff : f.FinMeasSupp Î¼ â†” âˆ€ y, y â‰  0 â†’ Î¼ (f â»Â¹' {y}) < âˆ := by
  classical
  constructor
  Â· refine fun h y hy => lt_of_le_of_lt (measure_mono ?_) h
    exact fun x hx (H : f x = 0) => hy <| H â–¸ Eq.symm hx
  Â· intro H
    rw [finMeasSupp_iff_support, support_eq]
    exact measure_biUnion_lt_top (finite_toSet _) fun y hy â†¦ H y (mem_filter.1 hy).2

namespace FinMeasSupp

theorem meas_preimage_singleton_ne_zero (h : f.FinMeasSupp Î¼) {y : Î²} (hy : y â‰  0) :
    Î¼ (f â»Â¹' {y}) < âˆ :=
  finMeasSupp_iff.1 h y hy

protected theorem map {g : Î² â†’ Î³} (hf : f.FinMeasSupp Î¼) (hg : g 0 = 0) : (f.map g).FinMeasSupp Î¼ :=
  flip lt_of_le_of_lt hf (measure_mono <| support_comp_subset hg f)

theorem of_map {g : Î² â†’ Î³} (h : (f.map g).FinMeasSupp Î¼) (hg : âˆ€ b, g b = 0 â†’ b = 0) :
    f.FinMeasSupp Î¼ :=
  flip lt_of_le_of_lt h <| measure_mono <| support_subset_comp @(hg) _

theorem map_iff {g : Î² â†’ Î³} (hg : âˆ€ {b}, g b = 0 â†” b = 0) :
    (f.map g).FinMeasSupp Î¼ â†” f.FinMeasSupp Î¼ :=
  âŸ¨fun h => h.of_map fun _ => hg.1, fun h => h.map <| hg.2 rflâŸ©

protected theorem pair {g : Î± â†’â‚› Î³} (hf : f.FinMeasSupp Î¼) (hg : g.FinMeasSupp Î¼) :
    (pair f g).FinMeasSupp Î¼ :=
  calc
    Î¼ (support <| pair f g) = Î¼ (support f âˆª support g) := congr_arg Î¼ <| support_prodMk f g
    _ â‰¤ Î¼ (support f) + Î¼ (support g) := measure_union_le _ _
    _ < _ := add_lt_top.2 âŸ¨hf, hgâŸ©

protected theorem mapâ‚‚ [Zero Î´] (hf : f.FinMeasSupp Î¼) {g : Î± â†’â‚› Î³} (hg : g.FinMeasSupp Î¼)
    {op : Î² â†’ Î³ â†’ Î´} (H : op 0 0 = 0) : ((pair f g).map (Function.uncurry op)).FinMeasSupp Î¼ :=
  (hf.pair hg).map H

protected theorem add {Î²} [AddZeroClass Î²] {f g : Î± â†’â‚› Î²} (hf : f.FinMeasSupp Î¼)
    (hg : g.FinMeasSupp Î¼) : (f + g).FinMeasSupp Î¼ := by
  rw [add_eq_mapâ‚‚]
  exact hf.mapâ‚‚ hg (zero_add 0)

protected theorem mul {Î²} [MulZeroClass Î²] {f g : Î± â†’â‚› Î²} (hf : f.FinMeasSupp Î¼)
    (hg : g.FinMeasSupp Î¼) : (f * g).FinMeasSupp Î¼ := by
  rw [mul_eq_mapâ‚‚]
  exact hf.mapâ‚‚ hg (zero_mul 0)

theorem lintegral_lt_top {f : Î± â†’â‚› â„â‰¥0âˆ} (hm : f.FinMeasSupp Î¼) (hf : âˆ€áµ a âˆ‚Î¼, f a â‰  âˆ) :
    f.lintegral Î¼ < âˆ := by
  refine sum_lt_top.2 fun a ha => ?_
  rcases eq_or_ne a âˆ with (rfl | ha)
  Â· simp only [ae_iff, Ne, Classical.not_not] at hf
    simp [Set.preimage, hf]
  Â· by_cases ha0 : a = 0
    Â· subst a
      simp
    Â· exact mul_lt_top ha.lt_top (finMeasSupp_iff.1 hm _ ha0)

theorem of_lintegral_ne_top {f : Î± â†’â‚› â„â‰¥0âˆ} (h : f.lintegral Î¼ â‰  âˆ) : f.FinMeasSupp Î¼ := by
  refine finMeasSupp_iff.2 fun b hb => ?_
  rw [f.lintegral_eq_of_subset' (Finset.subset_insert b _)] at h
  refine ENNReal.lt_top_of_mul_ne_top_right ?_ hb
  exact (lt_top_of_sum_ne_top h (Finset.mem_insert_self _ _)).ne

theorem iff_lintegral_lt_top {f : Î± â†’â‚› â„â‰¥0âˆ} (hf : âˆ€áµ a âˆ‚Î¼, f a â‰  âˆ) :
    f.FinMeasSupp Î¼ â†” f.lintegral Î¼ < âˆ :=
  âŸ¨fun h => h.lintegral_lt_top hf, fun h => of_lintegral_ne_top h.neâŸ©

end FinMeasSupp

lemma measure_support_lt_top_of_lintegral_ne_top {f : Î± â†’â‚› â„â‰¥0âˆ} (hf : f.lintegral Î¼ â‰  âˆ) :
    Î¼ (support f) < âˆ := by
  refine measure_support_lt_top f ?_
  rw [â† finMeasSupp_iff]
  exact FinMeasSupp.of_lintegral_ne_top hf

end FinMeasSupp

/-- To prove something for an arbitrary simple function, it suffices to show
that the property holds for (multiples of) characteristic functions and is closed under
addition (of functions with disjoint support).

It is possible to make the hypotheses in `h_add` a bit stronger, and such conditions can be added
once we need them (for example it is only necessary to consider the case where `g` is a multiple
of a characteristic function, and that this multiple doesn't appear in the image of `f`).

To use in an induction proof, the syntax is `induction f using SimpleFunc.induction with`. -/
@[elab_as_elim]
protected theorem induction {Î± Î³} [MeasurableSpace Î±] [AddZeroClass Î³]
    {motive : SimpleFunc Î± Î³ â†’ Prop}
    (const : âˆ€ (c) {s} (hs : MeasurableSet s),
      motive (SimpleFunc.piecewise s hs (SimpleFunc.const _ c) (SimpleFunc.const _ 0)))
    (add : âˆ€ â¦ƒf g : SimpleFunc Î± Î³â¦„,
      Disjoint (support f) (support g) â†’ motive f â†’ motive g â†’ motive (f + g))
    (f : SimpleFunc Î± Î³) : motive f := by
  classical
  generalize h : f.range \ {0} = s
  rw [â† Finset.coe_inj, Finset.coe_sdiff, Finset.coe_singleton, SimpleFunc.coe_range] at h
  induction s using Finset.induction generalizing f with
  | empty =>
    rw [Finset.coe_empty, diff_eq_empty, range_subset_singleton] at h
    convert const 0 MeasurableSet.univ
    ext x
    simp [h]
  | insert x s hxs ih =>
    have mx := f.measurableSet_preimage {x}
    let g := SimpleFunc.piecewise (f â»Â¹' {x}) mx 0 f
    have Pg : motive g := by
      apply ih
      simp only [g, SimpleFunc.coe_piecewise, range_piecewise]
      rw [image_compl_preimage, union_diff_distrib, diff_diff_comm, h, Finset.coe_insert,
        insert_diff_self_of_notMem, diff_eq_empty.mpr, Set.empty_union]
      Â· rw [Set.image_subset_iff]
        convert Set.subset_univ _
        exact preimage_const_of_mem (mem_singleton _)
      Â· rwa [Finset.mem_coe]
    convert add _ Pg (const x mx)
    Â· ext1 y
      by_cases hy : y âˆˆ f â»Â¹' {x}
      Â· simpa [g, hy]
      Â· simp [g, hy]
    rw [disjoint_iff_inf_le]
    rintro y
    by_cases hy : y âˆˆ f â»Â¹' {x} <;> simp [g, hy]

/-- To prove something for an arbitrary simple function, it suffices to show
that the property holds for constant functions and that it is closed under piecewise combinations
of functions.

To use in an induction proof, the syntax is `induction f with`. -/
@[induction_eliminator]
protected theorem induction' {Î± Î³} [MeasurableSpace Î±] [Nonempty Î³] {P : SimpleFunc Î± Î³ â†’ Prop}
    (const : âˆ€ (c), P (SimpleFunc.const _ c))
    (pcw : âˆ€ â¦ƒf g : SimpleFunc Î± Î³â¦„ {s} (hs : MeasurableSet s), P f â†’ P g â†’
      P (f.piecewise s hs g))
    (f : SimpleFunc Î± Î³) : P f := by
  let c : Î³ := Classical.ofNonempty
  classical
  generalize h : f.range \ {c} = s
  rw [â† Finset.coe_inj, Finset.coe_sdiff, Finset.coe_singleton, SimpleFunc.coe_range] at h
  induction s using Finset.induction generalizing f with
  | empty =>
    rw [Finset.coe_empty, diff_eq_empty, range_subset_singleton] at h
    convert const c
    ext x
    simp [h]
  | insert x s hxs ih =>
    have mx := f.measurableSet_preimage {x}
    let g := SimpleFunc.piecewise (f â»Â¹' {x}) mx (SimpleFunc.const Î± c) f
    have Pg : P g := by
      apply ih
      simp only [g, SimpleFunc.coe_piecewise, range_piecewise]
      rw [image_compl_preimage, union_diff_distrib, diff_diff_comm, h, Finset.coe_insert,
        insert_diff_self_of_notMem, diff_eq_empty.mpr, Set.empty_union]
      Â· rw [Set.image_subset_iff]
        convert Set.subset_univ _
        exact preimage_const_of_mem (mem_singleton _)
      Â· rwa [Finset.mem_coe]
    convert pcw mx.compl Pg (const x)
    Â· ext1 y
      by_cases hy : y âˆˆ f â»Â¹' {x}
      Â· simpa [g, hy]
      Â· simp [g, hy]

/-- In a topological vector space, the addition of a measurable function and a simple function is
measurable. -/
theorem _root_.Measurable.add_simpleFunc
    {E : Type*} {_ : MeasurableSpace Î±} [MeasurableSpace E] [AddCancelMonoid E] [MeasurableAdd E]
    {g : Î± â†’ E} (hg : Measurable g) (f : SimpleFunc Î± E) :
    Measurable (g + (f : Î± â†’ E)) := by
  classical
  induction f using SimpleFunc.induction with
  | @const c s hs =>
    simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
      SimpleFunc.coe_zero]
    rw [â† s.piecewise_same g, â† piecewise_add]
    exact Measurable.piecewise hs (hg.add_const _) (hg.add_const _)
  | @add f f' hff' hf hf' =>
    have : (g + â†‘(f + f')) = (Function.support f).piecewise (g + (f : Î± â†’ E)) (g + f') := by
      ext x
      by_cases hx : x âˆˆ Function.support f
      Â· simpa only [SimpleFunc.coe_add, Pi.add_apply, Function.mem_support, ne_eq, not_not,
          Set.piecewise_eq_of_mem _ _ _ hx, _root_.add_right_inj, add_eq_left]
          using Set.disjoint_left.1 hff' hx
      Â· simpa only [SimpleFunc.coe_add, Pi.add_apply, Function.mem_support, ne_eq, not_not,
          Set.piecewise_eq_of_notMem _ _ _ hx, _root_.add_right_inj, add_eq_right] using hx
    rw [this]
    exact Measurable.piecewise f.measurableSet_support hf hf'

/-- In a topological vector space, the addition of a simple function and a measurable function is
measurable. -/
theorem _root_.Measurable.simpleFunc_add
    {E : Type*} {_ : MeasurableSpace Î±} [MeasurableSpace E] [AddCancelMonoid E] [MeasurableAdd E]
    {g : Î± â†’ E} (hg : Measurable g) (f : SimpleFunc Î± E) :
    Measurable ((f : Î± â†’ E) + g) := by
  classical
  induction f using SimpleFunc.induction with
  | @const c s hs =>
    simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
      SimpleFunc.coe_zero]
    rw [â† s.piecewise_same g, â† piecewise_add]
    exact Measurable.piecewise hs (hg.const_add _) (hg.const_add _)
  | @add f f' hff' hf hf' =>
    have : (â†‘(f + f') + g) = (Function.support f).piecewise ((f : Î± â†’ E) + g) (f' + g) := by
      ext x
      by_cases hx : x âˆˆ Function.support f
      Â· simpa only [coe_add, Pi.add_apply, Function.mem_support, ne_eq, not_not,
          Set.piecewise_eq_of_mem _ _ _ hx, _root_.add_left_inj, add_eq_left]
          using Set.disjoint_left.1 hff' hx
      Â· simpa only [SimpleFunc.coe_add, Pi.add_apply, Function.mem_support, ne_eq, not_not,
          Set.piecewise_eq_of_notMem _ _ _ hx, _root_.add_left_inj, add_eq_right] using hx
    rw [this]
    exact Measurable.piecewise f.measurableSet_support hf hf'

end SimpleFunc

end MeasureTheory

open MeasureTheory MeasureTheory.SimpleFunc

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- To prove something for an arbitrary measurable function into `â„â‰¥0âˆ`, it suffices to show
that the property holds for (multiples of) characteristic functions and is closed under addition
and supremum of increasing sequences of functions.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`. -/
@[elab_as_elim]
theorem Measurable.ennreal_induction {motive : (Î± â†’ â„â‰¥0âˆ) â†’ Prop}
    (indicator : âˆ€ (c : â„â‰¥0âˆ) â¦ƒsâ¦„, MeasurableSet s â†’ motive (Set.indicator s fun _ => c))
    (add : âˆ€ â¦ƒf g : Î± â†’ â„â‰¥0âˆâ¦„, Disjoint (support f) (support g) â†’
      Measurable f â†’ Measurable g â†’ motive f â†’ motive g â†’ motive (f + g))
    (iSup : âˆ€ â¦ƒf : â„• â†’ Î± â†’ â„â‰¥0âˆâ¦„, (âˆ€ n, Measurable (f n)) â†’ Monotone f â†’
      (âˆ€ n, motive (f n)) â†’ motive fun x => â¨† n, f n x)
    â¦ƒf : Î± â†’ â„â‰¥0âˆâ¦„ (hf : Measurable f) : motive f := by
  convert iSup (fun n => (eapprox f n).measurable) (monotone_eapprox f) _ using 2
  Â· rw [iSup_eapprox_apply hf]
  Â· exact fun n =>
      SimpleFunc.induction (fun c s hs => indicator c hs)
        (fun f g hfg hf hg => add hfg f.measurable g.measurable hf hg) (eapprox f n)

/-- To prove something for an arbitrary measurable function into `â„â‰¥0âˆ`, it suffices to show
that the property holds for (multiples of) characteristic functions with finite mass according to
some sigma-finite measure and is closed under addition and supremum of increasing sequences of
functions.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`. -/
@[elab_as_elim]
lemma Measurable.ennreal_sigmaFinite_induction [SigmaFinite Î¼] {motive : (Î± â†’ â„â‰¥0âˆ) â†’ Prop}
    (indicator : âˆ€ (c : â„â‰¥0âˆ) â¦ƒsâ¦„, MeasurableSet s â†’ Î¼ s < âˆ â†’ motive (Set.indicator s fun _ â†¦ c))
    (add : âˆ€ â¦ƒf g : Î± â†’ â„â‰¥0âˆâ¦„, Disjoint (support f) (support g) â†’
      Measurable f â†’ Measurable g â†’ motive f â†’ motive g â†’ motive (f + g))
    (iSup : âˆ€ â¦ƒf : â„• â†’ Î± â†’ â„â‰¥0âˆâ¦„, (âˆ€ n, Measurable (f n)) â†’ Monotone f â†’
      (âˆ€ n, motive (f n)) â†’ motive fun x => â¨† n, f n x)
    â¦ƒf : Î± â†’ â„â‰¥0âˆâ¦„ (hf : Measurable f) : motive f := by
  refine Measurable.ennreal_induction (fun c s hs â†¦ ?_) add iSup hf
  convert iSup (f := fun n â†¦ (s âˆ© spanningSets Î¼ n).indicator fun _ â†¦ c)
    (fun n â†¦ measurable_const.indicator (hs.inter (measurableSet_spanningSets ..)))
    (fun m n hmn a â†¦ Set.indicator_le_indicator_of_subset (by gcongr) (by simp) _)
    (fun n â†¦ indicator _ (hs.inter (measurableSet_spanningSets ..))
      (measure_inter_lt_top_of_right_ne_top (measure_spanningSets_lt_top ..).ne)) with a
  simp [â† Set.indicator_iUnion_apply (M := â„â‰¥0âˆ) rfl, â† Set.inter_iUnion]
