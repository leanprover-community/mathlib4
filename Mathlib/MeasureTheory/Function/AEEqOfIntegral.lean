/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.NormedSpace.Dual
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Lp
import Mathlib.MeasureTheory.Integral.SetIntegral

#align_import measure_theory.function.ae_eq_of_integral from "leanprover-community/mathlib"@"915591b2bb3ea303648db07284a161a7f2a9e3d4"

/-! # From equality of integrals to equality of functions

This file provides various statements of the general form "if two functions have the same integral
on all sets, then they are equal almost everywhere".
The different lemmas use various hypotheses on the class of functions, on the target space or on the
possible finiteness of the measure.

## Main statements

All results listed below apply to two functions `f, g`, together with two main hypotheses,
* `f` and `g` are integrable on all measurable sets with finite measure,
* for all measurable sets `s` with finite measure, `âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼`.
The conclusion is then `f =áµ[Î¼] g`. The main lemmas are:
* `ae_eq_of_forall_setIntegral_eq_of_sigmaFinite`: case of a sigma-finite measure.
* `AEFinStronglyMeasurable.ae_eq_of_forall_setIntegral_eq`: for functions which are
  `AEFinStronglyMeasurable`.
* `Lp.ae_eq_of_forall_setIntegral_eq`: for elements of `Lp`, for `0 < p < âˆ`.
* `Integrable.ae_eq_of_forall_setIntegral_eq`: for integrable functions.

For each of these results, we also provide a lemma about the equality of one function and 0. For
example, `Lp.ae_eq_zero_of_forall_setIntegral_eq_zero`.

We also register the corresponding lemma for integrals of `â„â‰¥0âˆ`-valued functions, in
`ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite`.

Generally useful lemmas which are not related to integrals:
* `ae_eq_zero_of_forall_inner`: if for all constants `c`, `fun x => inner c (f x) =áµ[Î¼] 0` then
  `f =áµ[Î¼] 0`.
* `ae_eq_zero_of_forall_dual`: if for all constants `c` in the dual space,
  `fun x => c (f x) =áµ[Î¼] 0` then `f =áµ[Î¼] 0`.

-/


open MeasureTheory TopologicalSpace NormedSpace Filter

open scoped ENNReal NNReal MeasureTheory Topology

namespace MeasureTheory

section AeEqOfForall

variable {Î± E ğ•œ : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [RCLike ğ•œ]

theorem ae_eq_zero_of_forall_inner [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    [SecondCountableTopology E] {f : Î± â†’ E} (hf : âˆ€ c : E, (fun x => (inner c (f x) : ğ•œ)) =áµ[Î¼] 0) :
    f =áµ[Î¼] 0 := by
  let s := denseSeq E
  have hs : DenseRange s := denseRange_denseSeq E
  have hf' : âˆ€áµ x âˆ‚Î¼, âˆ€ n : â„•, inner (s n) (f x) = (0 : ğ•œ) := ae_all_iff.mpr fun n => hf (s n)
  refine hf'.mono fun x hx => ?_
  rw [Pi.zero_apply, â† @inner_self_eq_zero ğ•œ]
  have h_closed : IsClosed {c : E | inner c (f x) = (0 : ğ•œ)} :=
    isClosed_eq (continuous_id.inner continuous_const) continuous_const
  exact @isClosed_property â„• E _ s (fun c => inner c (f x) = (0 : ğ•œ)) hs h_closed (fun n => hx n) _
#align measure_theory.ae_eq_zero_of_forall_inner MeasureTheory.ae_eq_zero_of_forall_inner

local notation "âŸª" x ", " y "âŸ«" => y x

variable (ğ•œ)

theorem ae_eq_zero_of_forall_dual_of_isSeparable [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {t : Set E} (ht : TopologicalSpace.IsSeparable t) {f : Î± â†’ E}
    (hf : âˆ€ c : Dual ğ•œ E, (fun x => âŸªf x, câŸ«) =áµ[Î¼] 0) (h't : âˆ€áµ x âˆ‚Î¼, f x âˆˆ t) : f =áµ[Î¼] 0 := by
  rcases ht with âŸ¨d, d_count, hdâŸ©
  haveI : Encodable d := d_count.toEncodable
  have : âˆ€ x : d, âˆƒ g : E â†’L[ğ•œ] ğ•œ, â€–gâ€– â‰¤ 1 âˆ§ g x = â€–(x : E)â€– :=
    fun x => exists_dual_vector'' ğ•œ (x : E)
  choose s hs using this
  have A : âˆ€ a : E, a âˆˆ t â†’ (âˆ€ x, âŸªa, s xâŸ« = (0 : ğ•œ)) â†’ a = 0 := by
    intro a hat ha
    contrapose! ha
    have a_pos : 0 < â€–aâ€– := by simp only [ha, norm_pos_iff, Ne, not_false_iff]
    have a_mem : a âˆˆ closure d := hd hat
    obtain âŸ¨x, hxâŸ© : âˆƒ x : d, dist a x < â€–aâ€– / 2 := by
      rcases Metric.mem_closure_iff.1 a_mem (â€–aâ€– / 2) (half_pos a_pos) with âŸ¨x, h'x, hxâŸ©
      exact âŸ¨âŸ¨x, h'xâŸ©, hxâŸ©
    use x
    have I : â€–aâ€– / 2 < â€–(x : E)â€– := by
      have : â€–aâ€– â‰¤ â€–(x : E)â€– + â€–a - xâ€– := norm_le_insert' _ _
      have : â€–a - xâ€– < â€–aâ€– / 2 := by rwa [dist_eq_norm] at hx
      linarith
    intro h
    apply lt_irrefl â€–s x xâ€–
    calc
      â€–s x xâ€– = â€–s x (x - a)â€– := by simp only [h, sub_zero, ContinuousLinearMap.map_sub]
      _ â‰¤ 1 * â€–(x : E) - aâ€– := ContinuousLinearMap.le_of_opNorm_le _ (hs x).1 _
      _ < â€–aâ€– / 2 := by rw [one_mul]; rwa [dist_eq_norm'] at hx
      _ < â€–(x : E)â€– := I
      _ = â€–s x xâ€– := by rw [(hs x).2, RCLike.norm_coe_norm]
  have hfs : âˆ€ y : d, âˆ€áµ x âˆ‚Î¼, âŸªf x, s yâŸ« = (0 : ğ•œ) := fun y => hf (s y)
  have hf' : âˆ€áµ x âˆ‚Î¼, âˆ€ y : d, âŸªf x, s yâŸ« = (0 : ğ•œ) := by rwa [ae_all_iff]
  filter_upwards [hf', h't] with x hx h'x
  exact A (f x) h'x hx
#align measure_theory.ae_eq_zero_of_forall_dual_of_is_separable MeasureTheory.ae_eq_zero_of_forall_dual_of_isSeparable

theorem ae_eq_zero_of_forall_dual [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [SecondCountableTopology E] {f : Î± â†’ E} (hf : âˆ€ c : Dual ğ•œ E, (fun x => âŸªf x, câŸ«) =áµ[Î¼] 0) :
    f =áµ[Î¼] 0 :=
  ae_eq_zero_of_forall_dual_of_isSeparable ğ•œ (.of_separableSpace Set.univ) hf
    (eventually_of_forall fun _ => Set.mem_univ _)
#align measure_theory.ae_eq_zero_of_forall_dual MeasureTheory.ae_eq_zero_of_forall_dual

variable {ğ•œ}

end AeEqOfForall

variable {Î± E : Type*} {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {s t : Set Î±}
  [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] {p : â„â‰¥0âˆ}

section AeEqOfForallSetIntegralEq

theorem ae_const_le_iff_forall_lt_measure_zero {Î²} [LinearOrder Î²] [TopologicalSpace Î²]
    [OrderTopology Î²] [FirstCountableTopology Î²] (f : Î± â†’ Î²) (c : Î²) :
    (âˆ€áµ x âˆ‚Î¼, c â‰¤ f x) â†” âˆ€ b < c, Î¼ {x | f x â‰¤ b} = 0 := by
  rw [ae_iff]
  push_neg
  constructor
  Â· intro h b hb
    exact measure_mono_null (fun y hy => (lt_of_le_of_lt hy hb : _)) h
  intro hc
  by_cases h : âˆ€ b, c â‰¤ b
  Â· have : {a : Î± | f a < c} = âˆ… := by
      apply Set.eq_empty_iff_forall_not_mem.2 fun x hx => ?_
      exact (lt_irrefl _ (lt_of_lt_of_le hx (h (f x)))).elim
    simp [this]
  by_cases H : Â¬IsLUB (Set.Iio c) c
  Â· have : c âˆˆ upperBounds (Set.Iio c) := fun y hy => le_of_lt hy
    obtain âŸ¨b, b_up, bcâŸ© : âˆƒ b : Î², b âˆˆ upperBounds (Set.Iio c) âˆ§ b < c := by
      simpa [IsLUB, IsLeast, this, lowerBounds] using H
    exact measure_mono_null (fun x hx => b_up hx) (hc b bc)
  push_neg at H h
  obtain âŸ¨u, _, u_lt, u_lim, -âŸ© :
    âˆƒ u : â„• â†’ Î²,
      StrictMono u âˆ§ (âˆ€ n : â„•, u n < c) âˆ§ Tendsto u atTop (ğ“ c) âˆ§ âˆ€ n : â„•, u n âˆˆ Set.Iio c :=
    H.exists_seq_strictMono_tendsto_of_not_mem (lt_irrefl c) h
  have h_Union : {x | f x < c} = â‹ƒ n : â„•, {x | f x â‰¤ u n} := by
    ext1 x
    simp_rw [Set.mem_iUnion, Set.mem_setOf_eq]
    constructor <;> intro h
    Â· obtain âŸ¨n, hnâŸ© := ((tendsto_order.1 u_lim).1 _ h).exists; exact âŸ¨n, hn.leâŸ©
    Â· obtain âŸ¨n, hnâŸ© := h; exact hn.trans_lt (u_lt _)
  rw [h_Union, measure_iUnion_null_iff]
  intro n
  exact hc _ (u_lt n)
#align measure_theory.ae_const_le_iff_forall_lt_measure_zero MeasureTheory.ae_const_le_iff_forall_lt_measure_zero

section ENNReal

open scoped Topology

theorem ae_le_of_forall_set_lintegral_le_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ â„â‰¥0âˆ}
    (hf : Measurable f) (hg : Measurable g)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ«â» x in s, f x âˆ‚Î¼) â‰¤ âˆ«â» x in s, g x âˆ‚Î¼) : f â‰¤áµ[Î¼] g := by
  have A :
    âˆ€ (Îµ N : â„â‰¥0) (p : â„•), 0 < Îµ â†’ Î¼ ({x | g x + Îµ â‰¤ f x âˆ§ g x â‰¤ N} âˆ© spanningSets Î¼ p) = 0 := by
    intro Îµ N p Îµpos
    let s := {x | g x + Îµ â‰¤ f x âˆ§ g x â‰¤ N} âˆ© spanningSets Î¼ p
    have s_meas : MeasurableSet s := by
      have A : MeasurableSet {x | g x + Îµ â‰¤ f x} := measurableSet_le (hg.add measurable_const) hf
      have B : MeasurableSet {x | g x â‰¤ N} := measurableSet_le hg measurable_const
      exact (A.inter B).inter (measurable_spanningSets Î¼ p)
    have s_lt_top : Î¼ s < âˆ :=
      (measure_mono (Set.inter_subset_right _ _)).trans_lt (measure_spanningSets_lt_top Î¼ p)
    have A : (âˆ«â» x in s, g x âˆ‚Î¼) + Îµ * Î¼ s â‰¤ (âˆ«â» x in s, g x âˆ‚Î¼) + 0 :=
      calc
        (âˆ«â» x in s, g x âˆ‚Î¼) + Îµ * Î¼ s = (âˆ«â» x in s, g x âˆ‚Î¼) + âˆ«â» _ in s, Îµ âˆ‚Î¼ := by
          simp only [lintegral_const, Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply]
        _ = âˆ«â» x in s, g x + Îµ âˆ‚Î¼ := (lintegral_add_right _ measurable_const).symm
        _ â‰¤ âˆ«â» x in s, f x âˆ‚Î¼ :=
          (set_lintegral_mono (hg.add measurable_const) hf fun x hx => hx.1.1)
        _ â‰¤ (âˆ«â» x in s, g x âˆ‚Î¼) + 0 := by rw [add_zero]; exact h s s_meas s_lt_top
    have B : (âˆ«â» x in s, g x âˆ‚Î¼) â‰  âˆ := by
      apply ne_of_lt
      calc
        (âˆ«â» x in s, g x âˆ‚Î¼) â‰¤ âˆ«â» _ in s, N âˆ‚Î¼ :=
          set_lintegral_mono hg measurable_const fun x hx => hx.1.2
        _ = N * Î¼ s := by
          simp only [lintegral_const, Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply]
        _ < âˆ := by
          simp only [lt_top_iff_ne_top, s_lt_top.ne, and_false_iff, ENNReal.coe_ne_top,
            ENNReal.mul_eq_top, Ne, not_false_iff, false_and_iff, or_self_iff]
    have : (Îµ : â„â‰¥0âˆ) * Î¼ s â‰¤ 0 := ENNReal.le_of_add_le_add_left B A
    simpa only [ENNReal.coe_eq_zero, nonpos_iff_eq_zero, mul_eq_zero, Îµpos.ne', false_or_iff]
  obtain âŸ¨u, _, u_pos, u_limâŸ© :
    âˆƒ u : â„• â†’ â„â‰¥0, StrictAnti u âˆ§ (âˆ€ n, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„â‰¥0)
  let s := fun n : â„• => {x | g x + u n â‰¤ f x âˆ§ g x â‰¤ (n : â„â‰¥0)} âˆ© spanningSets Î¼ n
  have Î¼s : âˆ€ n, Î¼ (s n) = 0 := fun n => A _ _ _ (u_pos n)
  have B : {x | f x â‰¤ g x}á¶œ âŠ† â‹ƒ n, s n := by
    intro x hx
    simp only [Set.mem_compl_iff, Set.mem_setOf, not_le] at hx
    have L1 : âˆ€á¶  n in atTop, g x + u n â‰¤ f x := by
      have : Tendsto (fun n => g x + u n) atTop (ğ“ (g x + (0 : â„â‰¥0))) :=
        tendsto_const_nhds.add (ENNReal.tendsto_coe.2 u_lim)
      simp only [ENNReal.coe_zero, add_zero] at this
      exact eventually_le_of_tendsto_lt hx this
    have L2 : âˆ€á¶  n : â„• in (atTop : Filter â„•), g x â‰¤ (n : â„â‰¥0) :=
      haveI : Tendsto (fun n : â„• => ((n : â„â‰¥0) : â„â‰¥0âˆ)) atTop (ğ“ âˆ) := by
        simp only [ENNReal.coe_natCast]
        exact ENNReal.tendsto_nat_nhds_top
      eventually_ge_of_tendsto_gt (hx.trans_le le_top) this
    apply Set.mem_iUnion.2
    exact ((L1.and L2).and (eventually_mem_spanningSets Î¼ x)).exists
  refine le_antisymm ?_ bot_le
  calc
    Î¼ {x : Î± | (fun x : Î± => f x â‰¤ g x) x}á¶œ â‰¤ Î¼ (â‹ƒ n, s n) := measure_mono B
    _ â‰¤ âˆ‘' n, Î¼ (s n) := measure_iUnion_le _
    _ = 0 := by simp only [Î¼s, tsum_zero]
#align measure_theory.ae_le_of_forall_set_lintegral_le_of_sigma_finite MeasureTheory.ae_le_of_forall_set_lintegral_le_of_sigmaFinite

theorem ae_le_of_forall_set_lintegral_le_of_sigmaFiniteâ‚€ [SigmaFinite Î¼]
    {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, g x âˆ‚Î¼) :
    f â‰¤áµ[Î¼] g := by
  have h' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, hf.mk f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, hg.mk g x âˆ‚Î¼ := by
    refine fun s hs hÎ¼s â†¦ (set_lintegral_congr_fun hs ?_).trans_le
      ((h s hs hÎ¼s).trans_eq (set_lintegral_congr_fun hs ?_))
    Â· filter_upwards [hf.ae_eq_mk] with a ha using fun _ â†¦ ha.symm
    Â· filter_upwards [hg.ae_eq_mk] with a ha using fun _ â†¦ ha
  filter_upwards [hf.ae_eq_mk, hg.ae_eq_mk,
    ae_le_of_forall_set_lintegral_le_of_sigmaFinite hf.measurable_mk hg.measurable_mk h']
    with a haf hag ha
  rwa [haf, hag]

theorem ae_eq_of_forall_set_lintegral_eq_of_sigmaFiniteâ‚€ [SigmaFinite Î¼]
    {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼) : f =áµ[Î¼] g := by
  have A : f â‰¤áµ[Î¼] g :=
    ae_le_of_forall_set_lintegral_le_of_sigmaFiniteâ‚€ hf hg fun s hs h's => le_of_eq (h s hs h's)
  have B : g â‰¤áµ[Î¼] f :=
    ae_le_of_forall_set_lintegral_le_of_sigmaFiniteâ‚€ hg hf fun s hs h's => ge_of_eq (h s hs h's)
  filter_upwards [A, B] with x using le_antisymm

theorem ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ â„â‰¥0âˆ}
    (hf : Measurable f) (hg : Measurable g)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼) : f =áµ[Î¼] g :=
  ae_eq_of_forall_set_lintegral_eq_of_sigmaFiniteâ‚€ hf.aemeasurable hg.aemeasurable h
#align measure_theory.ae_eq_of_forall_set_lintegral_eq_of_sigma_finite MeasureTheory.ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite

end ENNReal

section Real

variable {f : Î± â†’ â„}

/-- Don't use this lemma. Use `ae_nonneg_of_forall_setIntegral_nonneg`. -/
theorem ae_nonneg_of_forall_setIntegral_nonneg_of_stronglyMeasurable (hfm : StronglyMeasurable f)
    (hf : Integrable f Î¼) (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼) :
    0 â‰¤áµ[Î¼] f := by
  simp_rw [EventuallyLE, Pi.zero_apply]
  rw [ae_const_le_iff_forall_lt_measure_zero]
  intro b hb_neg
  let s := {x | f x â‰¤ b}
  have hs : MeasurableSet s := hfm.measurableSet_le stronglyMeasurable_const
  have mus : Î¼ s < âˆ := Integrable.measure_le_lt_top hf hb_neg
  have h_int_gt : (âˆ« x in s, f x âˆ‚Î¼) â‰¤ b * (Î¼ s).toReal := by
    have h_const_le : (âˆ« x in s, f x âˆ‚Î¼) â‰¤ âˆ« _ in s, b âˆ‚Î¼ := by
      refine
        setIntegral_mono_ae_restrict hf.integrableOn (integrableOn_const.mpr (Or.inr mus)) ?_
      rw [EventuallyLE, ae_restrict_iff hs]
      exact eventually_of_forall fun x hxs => hxs
    rwa [setIntegral_const, smul_eq_mul, mul_comm] at h_const_le
  by_contra h
  refine (lt_self_iff_false (âˆ« x in s, f x âˆ‚Î¼)).mp (h_int_gt.trans_lt ?_)
  refine (mul_neg_iff.mpr (Or.inr âŸ¨hb_neg, ?_âŸ©)).trans_le ?_
  swap
  Â· exact hf_zero s hs mus
  refine ENNReal.toReal_nonneg.lt_of_ne fun h_eq => h ?_
  cases' (ENNReal.toReal_eq_zero_iff _).mp h_eq.symm with hÎ¼s_eq_zero hÎ¼s_eq_top
  Â· exact hÎ¼s_eq_zero
  Â· exact absurd hÎ¼s_eq_top mus.ne
#align measure_theory.ae_nonneg_of_forall_set_integral_nonneg_of_strongly_measurable MeasureTheory.ae_nonneg_of_forall_setIntegral_nonneg_of_stronglyMeasurable

@[deprecated (since := "2024-04-17")]
alias ae_nonneg_of_forall_set_integral_nonneg_of_stronglyMeasurable :=
  ae_nonneg_of_forall_setIntegral_nonneg_of_stronglyMeasurable

theorem ae_nonneg_of_forall_setIntegral_nonneg (hf : Integrable f Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼) : 0 â‰¤áµ[Î¼] f := by
  rcases hf.1 with âŸ¨f', hf'_meas, hf_aeâŸ©
  have hf'_integrable : Integrable f' Î¼ := Integrable.congr hf hf_ae
  have hf'_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ 0 â‰¤ âˆ« x in s, f' x âˆ‚Î¼ := by
    intro s hs h's
    rw [setIntegral_congr_ae hs (hf_ae.mono fun x hx _ => hx.symm)]
    exact hf_zero s hs h's
  exact
    (ae_nonneg_of_forall_setIntegral_nonneg_of_stronglyMeasurable hf'_meas hf'_integrable
          hf'_zero).trans
      hf_ae.symm.le
#align measure_theory.ae_nonneg_of_forall_set_integral_nonneg MeasureTheory.ae_nonneg_of_forall_setIntegral_nonneg

@[deprecated (since := "2024-04-17")]
alias ae_nonneg_of_forall_set_integral_nonneg :=
  ae_nonneg_of_forall_setIntegral_nonneg

theorem ae_le_of_forall_setIntegral_le {f g : Î± â†’ â„} (hf : Integrable f Î¼) (hg : Integrable g Î¼)
    (hf_le : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ« x in s, f x âˆ‚Î¼) â‰¤ âˆ« x in s, g x âˆ‚Î¼) :
    f â‰¤áµ[Î¼] g := by
  rw [â† eventually_sub_nonneg]
  refine ae_nonneg_of_forall_setIntegral_nonneg (hg.sub hf) fun s hs => ?_
  rw [integral_sub' hg.integrableOn hf.integrableOn, sub_nonneg]
  exact hf_le s hs
#align measure_theory.ae_le_of_forall_set_integral_le MeasureTheory.ae_le_of_forall_setIntegral_le

@[deprecated (since := "2024-04-17")]
alias ae_le_of_forall_set_integral_le := ae_le_of_forall_setIntegral_le

theorem ae_nonneg_restrict_of_forall_setIntegral_nonneg_inter {f : Î± â†’ â„} {t : Set Î±}
    (hf : IntegrableOn f t Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ (s âˆ© t) < âˆ â†’ 0 â‰¤ âˆ« x in s âˆ© t, f x âˆ‚Î¼) :
    0 â‰¤áµ[Î¼.restrict t] f := by
  refine ae_nonneg_of_forall_setIntegral_nonneg hf fun s hs h's => ?_
  simp_rw [Measure.restrict_restrict hs]
  apply hf_zero s hs
  rwa [Measure.restrict_apply hs] at h's
#align measure_theory.ae_nonneg_restrict_of_forall_set_integral_nonneg_inter MeasureTheory.ae_nonneg_restrict_of_forall_setIntegral_nonneg_inter

@[deprecated (since := "2024-04-17")]
alias ae_nonneg_restrict_of_forall_set_integral_nonneg_inter :=
  ae_nonneg_restrict_of_forall_setIntegral_nonneg_inter

theorem ae_nonneg_of_forall_setIntegral_nonneg_of_sigmaFinite [SigmaFinite Î¼] {f : Î± â†’ â„}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼) : 0 â‰¤áµ[Î¼] f := by
  apply ae_of_forall_measure_lt_top_ae_restrict
  intro t t_meas t_lt_top
  apply ae_nonneg_restrict_of_forall_setIntegral_nonneg_inter (hf_int_finite t t_meas t_lt_top)
  intro s s_meas _
  exact
    hf_zero _ (s_meas.inter t_meas)
      (lt_of_le_of_lt (measure_mono (Set.inter_subset_right _ _)) t_lt_top)
#align measure_theory.ae_nonneg_of_forall_set_integral_nonneg_of_sigma_finite MeasureTheory.ae_nonneg_of_forall_setIntegral_nonneg_of_sigmaFinite

@[deprecated (since := "2024-04-17")]
alias ae_nonneg_of_forall_set_integral_nonneg_of_sigmaFinite :=
  ae_nonneg_of_forall_setIntegral_nonneg_of_sigmaFinite

theorem AEFinStronglyMeasurable.ae_nonneg_of_forall_setIntegral_nonneg {f : Î± â†’ â„}
    (hf : AEFinStronglyMeasurable f Î¼)
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼) : 0 â‰¤áµ[Î¼] f := by
  let t := hf.sigmaFiniteSet
  suffices 0 â‰¤áµ[Î¼.restrict t] f from
    ae_of_ae_restrict_of_ae_restrict_compl _ this hf.ae_eq_zero_compl.symm.le
  haveI : SigmaFinite (Î¼.restrict t) := hf.sigmaFinite_restrict
  refine
    ae_nonneg_of_forall_setIntegral_nonneg_of_sigmaFinite (fun s hs hÎ¼ts => ?_) fun s hs hÎ¼ts => ?_
  Â· rw [IntegrableOn, Measure.restrict_restrict hs]
    rw [Measure.restrict_apply hs] at hÎ¼ts
    exact hf_int_finite (s âˆ© t) (hs.inter hf.measurableSet) hÎ¼ts
  Â· rw [Measure.restrict_restrict hs]
    rw [Measure.restrict_apply hs] at hÎ¼ts
    exact hf_zero (s âˆ© t) (hs.inter hf.measurableSet) hÎ¼ts
#align measure_theory.ae_fin_strongly_measurable.ae_nonneg_of_forall_set_integral_nonneg MeasureTheory.AEFinStronglyMeasurable.ae_nonneg_of_forall_setIntegral_nonneg

@[deprecated (since := "2024-04-17")]
alias AEFinStronglyMeasurable.ae_nonneg_of_forall_set_integral_nonneg :=
  AEFinStronglyMeasurable.ae_nonneg_of_forall_setIntegral_nonneg

theorem ae_nonneg_restrict_of_forall_setIntegral_nonneg {f : Î± â†’ â„}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼) {t : Set Î±}
    (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) : 0 â‰¤áµ[Î¼.restrict t] f := by
  refine
    ae_nonneg_restrict_of_forall_setIntegral_nonneg_inter
      (hf_int_finite t ht (lt_top_iff_ne_top.mpr hÎ¼t)) fun s hs _ => ?_
  refine hf_zero (s âˆ© t) (hs.inter ht) ?_
  exact (measure_mono (Set.inter_subset_right s t)).trans_lt (lt_top_iff_ne_top.mpr hÎ¼t)
#align measure_theory.ae_nonneg_restrict_of_forall_set_integral_nonneg MeasureTheory.ae_nonneg_restrict_of_forall_setIntegral_nonneg

@[deprecated (since := "2024-04-17")]
alias ae_nonneg_restrict_of_forall_set_integral_nonneg :=
  ae_nonneg_restrict_of_forall_setIntegral_nonneg

theorem ae_eq_zero_restrict_of_forall_setIntegral_eq_zero_real {f : Î± â†’ â„}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) {t : Set Î±}
    (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) : f =áµ[Î¼.restrict t] 0 := by
  suffices h_and : f â‰¤áµ[Î¼.restrict t] 0 âˆ§ 0 â‰¤áµ[Î¼.restrict t] f from
    h_and.1.mp (h_and.2.mono fun x hx1 hx2 => le_antisymm hx2 hx1)
  refine
    âŸ¨?_,
      ae_nonneg_restrict_of_forall_setIntegral_nonneg hf_int_finite
        (fun s hs hÎ¼s => (hf_zero s hs hÎ¼s).symm.le) ht hÎ¼tâŸ©
  suffices h_neg : 0 â‰¤áµ[Î¼.restrict t] -f by
    refine h_neg.mono fun x hx => ?_
    rw [Pi.neg_apply] at hx
    simpa using hx
  refine
    ae_nonneg_restrict_of_forall_setIntegral_nonneg (fun s hs hÎ¼s => (hf_int_finite s hs hÎ¼s).neg)
      (fun s hs hÎ¼s => ?_) ht hÎ¼t
  simp_rw [Pi.neg_apply]
  rw [integral_neg, neg_nonneg]
  exact (hf_zero s hs hÎ¼s).le
#align measure_theory.ae_eq_zero_restrict_of_forall_set_integral_eq_zero_real MeasureTheory.ae_eq_zero_restrict_of_forall_setIntegral_eq_zero_real

@[deprecated (since := "2024-04-17")]
alias ae_eq_zero_restrict_of_forall_set_integral_eq_zero_real :=
  ae_eq_zero_restrict_of_forall_setIntegral_eq_zero_real

end Real

theorem ae_eq_zero_restrict_of_forall_setIntegral_eq_zero {f : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) {t : Set Î±}
    (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) : f =áµ[Î¼.restrict t] 0 := by
  rcases (hf_int_finite t ht hÎ¼t.lt_top).aestronglyMeasurable.isSeparable_ae_range with
    âŸ¨u, u_sep, huâŸ©
  refine ae_eq_zero_of_forall_dual_of_isSeparable â„ u_sep (fun c => ?_) hu
  refine ae_eq_zero_restrict_of_forall_setIntegral_eq_zero_real ?_ ?_ ht hÎ¼t
  Â· intro s hs hÎ¼s
    exact ContinuousLinearMap.integrable_comp c (hf_int_finite s hs hÎ¼s)
  Â· intro s hs hÎ¼s
    rw [ContinuousLinearMap.integral_comp_comm c (hf_int_finite s hs hÎ¼s), hf_zero s hs hÎ¼s]
    exact ContinuousLinearMap.map_zero _
#align measure_theory.ae_eq_zero_restrict_of_forall_set_integral_eq_zero MeasureTheory.ae_eq_zero_restrict_of_forall_setIntegral_eq_zero

@[deprecated (since := "2024-04-17")]
alias ae_eq_zero_restrict_of_forall_set_integral_eq_zero :=
  ae_eq_zero_restrict_of_forall_setIntegral_eq_zero

theorem ae_eq_restrict_of_forall_setIntegral_eq {f g : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hfg_zero : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼)
    {t : Set Î±} (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) : f =áµ[Î¼.restrict t] g := by
  rw [â† sub_ae_eq_zero]
  have hfg' : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ« x in s, (f - g) x âˆ‚Î¼) = 0 := by
    intro s hs hÎ¼s
    rw [integral_sub' (hf_int_finite s hs hÎ¼s) (hg_int_finite s hs hÎ¼s)]
    exact sub_eq_zero.mpr (hfg_zero s hs hÎ¼s)
  have hfg_int : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn (f - g) s Î¼ := fun s hs hÎ¼s =>
    (hf_int_finite s hs hÎ¼s).sub (hg_int_finite s hs hÎ¼s)
  exact ae_eq_zero_restrict_of_forall_setIntegral_eq_zero hfg_int hfg' ht hÎ¼t
#align measure_theory.ae_eq_restrict_of_forall_set_integral_eq MeasureTheory.ae_eq_restrict_of_forall_setIntegral_eq

@[deprecated (since := "2024-04-17")]
alias ae_eq_restrict_of_forall_set_integral_eq :=
  ae_eq_restrict_of_forall_setIntegral_eq

theorem ae_eq_zero_of_forall_setIntegral_eq_of_sigmaFinite [SigmaFinite Î¼] {f : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) : f =áµ[Î¼] 0 := by
  let S := spanningSets Î¼
  rw [â† @Measure.restrict_univ _ _ Î¼, â† iUnion_spanningSets Î¼, EventuallyEq, ae_iff,
    Measure.restrict_apply' (MeasurableSet.iUnion (measurable_spanningSets Î¼))]
  rw [Set.inter_iUnion, measure_iUnion_null_iff]
  intro n
  have h_meas_n : MeasurableSet (S n) := measurable_spanningSets Î¼ n
  have hÎ¼n : Î¼ (S n) < âˆ := measure_spanningSets_lt_top Î¼ n
  rw [â† Measure.restrict_apply' h_meas_n]
  exact ae_eq_zero_restrict_of_forall_setIntegral_eq_zero hf_int_finite hf_zero h_meas_n hÎ¼n.ne
#align measure_theory.ae_eq_zero_of_forall_set_integral_eq_of_sigma_finite MeasureTheory.ae_eq_zero_of_forall_setIntegral_eq_of_sigmaFinite

@[deprecated (since := "2024-04-17")]
alias ae_eq_zero_of_forall_set_integral_eq_of_sigmaFinite :=
  ae_eq_zero_of_forall_setIntegral_eq_of_sigmaFinite

theorem ae_eq_of_forall_setIntegral_eq_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hfg_eq : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼) :
    f =áµ[Î¼] g := by
  rw [â† sub_ae_eq_zero]
  have hfg : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ« x in s, (f - g) x âˆ‚Î¼) = 0 := by
    intro s hs hÎ¼s
    rw [integral_sub' (hf_int_finite s hs hÎ¼s) (hg_int_finite s hs hÎ¼s),
      sub_eq_zero.mpr (hfg_eq s hs hÎ¼s)]
  have hfg_int : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn (f - g) s Î¼ := fun s hs hÎ¼s =>
    (hf_int_finite s hs hÎ¼s).sub (hg_int_finite s hs hÎ¼s)
  exact ae_eq_zero_of_forall_setIntegral_eq_of_sigmaFinite hfg_int hfg
#align measure_theory.ae_eq_of_forall_set_integral_eq_of_sigma_finite MeasureTheory.ae_eq_of_forall_setIntegral_eq_of_sigmaFinite

@[deprecated (since := "2024-04-17")]
alias ae_eq_of_forall_set_integral_eq_of_sigmaFinite :=
  ae_eq_of_forall_setIntegral_eq_of_sigmaFinite

theorem AEFinStronglyMeasurable.ae_eq_zero_of_forall_setIntegral_eq_zero {f : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0)
    (hf : AEFinStronglyMeasurable f Î¼) : f =áµ[Î¼] 0 := by
  let t := hf.sigmaFiniteSet
  suffices f =áµ[Î¼.restrict t] 0 from
    ae_of_ae_restrict_of_ae_restrict_compl _ this hf.ae_eq_zero_compl
  haveI : SigmaFinite (Î¼.restrict t) := hf.sigmaFinite_restrict
  refine ae_eq_zero_of_forall_setIntegral_eq_of_sigmaFinite ?_ ?_
  Â· intro s hs hÎ¼s
    rw [IntegrableOn, Measure.restrict_restrict hs]
    rw [Measure.restrict_apply hs] at hÎ¼s
    exact hf_int_finite _ (hs.inter hf.measurableSet) hÎ¼s
  Â· intro s hs hÎ¼s
    rw [Measure.restrict_restrict hs]
    rw [Measure.restrict_apply hs] at hÎ¼s
    exact hf_zero _ (hs.inter hf.measurableSet) hÎ¼s
#align measure_theory.ae_fin_strongly_measurable.ae_eq_zero_of_forall_set_integral_eq_zero MeasureTheory.AEFinStronglyMeasurable.ae_eq_zero_of_forall_setIntegral_eq_zero

@[deprecated (since := "2024-04-17")]
alias AEFinStronglyMeasurable.ae_eq_zero_of_forall_set_integral_eq_zero :=
  AEFinStronglyMeasurable.ae_eq_zero_of_forall_setIntegral_eq_zero

theorem AEFinStronglyMeasurable.ae_eq_of_forall_setIntegral_eq {f g : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hfg_eq : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼)
    (hf : AEFinStronglyMeasurable f Î¼) (hg : AEFinStronglyMeasurable g Î¼) : f =áµ[Î¼] g := by
  rw [â† sub_ae_eq_zero]
  have hfg : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ« x in s, (f - g) x âˆ‚Î¼) = 0 := by
    intro s hs hÎ¼s
    rw [integral_sub' (hf_int_finite s hs hÎ¼s) (hg_int_finite s hs hÎ¼s),
      sub_eq_zero.mpr (hfg_eq s hs hÎ¼s)]
  have hfg_int : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn (f - g) s Î¼ := fun s hs hÎ¼s =>
    (hf_int_finite s hs hÎ¼s).sub (hg_int_finite s hs hÎ¼s)
  exact (hf.sub hg).ae_eq_zero_of_forall_setIntegral_eq_zero hfg_int hfg
#align measure_theory.ae_fin_strongly_measurable.ae_eq_of_forall_set_integral_eq MeasureTheory.AEFinStronglyMeasurable.ae_eq_of_forall_setIntegral_eq

@[deprecated (since := "2024-04-17")]
alias AEFinStronglyMeasurable.ae_eq_of_forall_set_integral_eq :=
  AEFinStronglyMeasurable.ae_eq_of_forall_setIntegral_eq

theorem Lp.ae_eq_zero_of_forall_setIntegral_eq_zero (f : Lp E p Î¼) (hp_ne_zero : p â‰  0)
    (hp_ne_top : p â‰  âˆ) (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) : f =áµ[Î¼] 0 :=
  AEFinStronglyMeasurable.ae_eq_zero_of_forall_setIntegral_eq_zero hf_int_finite hf_zero
    (Lp.finStronglyMeasurable _ hp_ne_zero hp_ne_top).aefinStronglyMeasurable
set_option linter.uppercaseLean3 false in
#align measure_theory.Lp.ae_eq_zero_of_forall_set_integral_eq_zero MeasureTheory.Lp.ae_eq_zero_of_forall_setIntegral_eq_zero

@[deprecated (since := "2024-04-17")]
alias Lp.ae_eq_zero_of_forall_set_integral_eq_zero :=
  Lp.ae_eq_zero_of_forall_setIntegral_eq_zero

theorem Lp.ae_eq_of_forall_setIntegral_eq (f g : Lp E p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
    (hf_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hfg : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼) :
    f =áµ[Î¼] g :=
  AEFinStronglyMeasurable.ae_eq_of_forall_setIntegral_eq hf_int_finite hg_int_finite hfg
    (Lp.finStronglyMeasurable _ hp_ne_zero hp_ne_top).aefinStronglyMeasurable
    (Lp.finStronglyMeasurable _ hp_ne_zero hp_ne_top).aefinStronglyMeasurable
set_option linter.uppercaseLean3 false in
#align measure_theory.Lp.ae_eq_of_forall_set_integral_eq MeasureTheory.Lp.ae_eq_of_forall_setIntegral_eq

@[deprecated (since := "2024-04-17")]
alias Lp.ae_eq_of_forall_set_integral_eq := Lp.ae_eq_of_forall_setIntegral_eq

theorem ae_eq_zero_of_forall_setIntegral_eq_of_finStronglyMeasurable_trim (hm : m â‰¤ m0) {f : Î± â†’ E}
    (hf_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0)
    (hf : FinStronglyMeasurable f (Î¼.trim hm)) : f =áµ[Î¼] 0 := by
  obtain âŸ¨t, ht_meas, htf_zero, htÎ¼âŸ© := hf.exists_set_sigmaFinite
  haveI : SigmaFinite ((Î¼.restrict t).trim hm) := by rwa [restrict_trim hm Î¼ ht_meas] at htÎ¼
  have htf_zero : f =áµ[Î¼.restrict tá¶œ] 0 := by
    rw [EventuallyEq, ae_restrict_iff' (MeasurableSet.compl (hm _ ht_meas))]
    exact eventually_of_forall htf_zero
  have hf_meas_m : StronglyMeasurable[m] f := hf.stronglyMeasurable
  suffices f =áµ[Î¼.restrict t] 0 from
    ae_of_ae_restrict_of_ae_restrict_compl _ this htf_zero
  refine measure_eq_zero_of_trim_eq_zero hm ?_
  refine ae_eq_zero_of_forall_setIntegral_eq_of_sigmaFinite ?_ ?_
  Â· intro s hs hÎ¼s
    unfold IntegrableOn
    rw [restrict_trim hm (Î¼.restrict t) hs, Measure.restrict_restrict (hm s hs)]
    rw [â† restrict_trim hm Î¼ ht_meas, Measure.restrict_apply hs,
      trim_measurableSet_eq hm (hs.inter ht_meas)] at hÎ¼s
    refine Integrable.trim hm ?_ hf_meas_m
    exact hf_int_finite _ (hs.inter ht_meas) hÎ¼s
  Â· intro s hs hÎ¼s
    rw [restrict_trim hm (Î¼.restrict t) hs, Measure.restrict_restrict (hm s hs)]
    rw [â† restrict_trim hm Î¼ ht_meas, Measure.restrict_apply hs,
      trim_measurableSet_eq hm (hs.inter ht_meas)] at hÎ¼s
    rw [â† integral_trim hm hf_meas_m]
    exact hf_zero _ (hs.inter ht_meas) hÎ¼s
#align measure_theory.ae_eq_zero_of_forall_set_integral_eq_of_fin_strongly_measurable_trim MeasureTheory.ae_eq_zero_of_forall_setIntegral_eq_of_finStronglyMeasurable_trim

@[deprecated (since := "2024-04-17")]
alias ae_eq_zero_of_forall_set_integral_eq_of_finStronglyMeasurable_trim :=
  ae_eq_zero_of_forall_setIntegral_eq_of_finStronglyMeasurable_trim

theorem Integrable.ae_eq_zero_of_forall_setIntegral_eq_zero {f : Î± â†’ E} (hf : Integrable f Î¼)
    (hf_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) : f =áµ[Î¼] 0 := by
  have hf_Lp : Memâ„’p f 1 Î¼ := memâ„’p_one_iff_integrable.mpr hf
  let f_Lp := hf_Lp.toLp f
  have hf_f_Lp : f =áµ[Î¼] f_Lp := (Memâ„’p.coeFn_toLp hf_Lp).symm
  refine hf_f_Lp.trans ?_
  refine Lp.ae_eq_zero_of_forall_setIntegral_eq_zero f_Lp one_ne_zero ENNReal.coe_ne_top ?_ ?_
  Â· exact fun s _ _ => Integrable.integrableOn (L1.integrable_coeFn _)
  Â· intro s hs hÎ¼s
    rw [integral_congr_ae (ae_restrict_of_ae hf_f_Lp.symm)]
    exact hf_zero s hs hÎ¼s
#align measure_theory.integrable.ae_eq_zero_of_forall_set_integral_eq_zero MeasureTheory.Integrable.ae_eq_zero_of_forall_setIntegral_eq_zero

@[deprecated (since := "2024-04-17")]
alias Integrable.ae_eq_zero_of_forall_set_integral_eq_zero :=
  Integrable.ae_eq_zero_of_forall_setIntegral_eq_zero

theorem Integrable.ae_eq_of_forall_setIntegral_eq (f g : Î± â†’ E) (hf : Integrable f Î¼)
    (hg : Integrable g Î¼)
    (hfg : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼) :
    f =áµ[Î¼] g := by
  rw [â† sub_ae_eq_zero]
  have hfg' : âˆ€ s : Set Î±, MeasurableSet s â†’ Î¼ s < âˆ â†’ (âˆ« x in s, (f - g) x âˆ‚Î¼) = 0 := by
    intro s hs hÎ¼s
    rw [integral_sub' hf.integrableOn hg.integrableOn]
    exact sub_eq_zero.mpr (hfg s hs hÎ¼s)
  exact Integrable.ae_eq_zero_of_forall_setIntegral_eq_zero (hf.sub hg) hfg'
#align measure_theory.integrable.ae_eq_of_forall_set_integral_eq MeasureTheory.Integrable.ae_eq_of_forall_setIntegral_eq

@[deprecated (since := "2024-04-17")]
alias Integrable.ae_eq_of_forall_set_integral_eq :=
  Integrable.ae_eq_of_forall_setIntegral_eq

variable {Î² : Type*} [TopologicalSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²]

/-- If an integrable function has zero integral on all closed sets, then it is zero
almost everwhere. -/
lemma ae_eq_zero_of_forall_setIntegral_isClosed_eq_zero {Î¼ : Measure Î²} {f : Î² â†’ E}
    (hf : Integrable f Î¼) (h'f : âˆ€ (s : Set Î²), IsClosed s â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) :
    f =áµ[Î¼] 0 := by
  suffices âˆ€ s, MeasurableSet s â†’ âˆ« x in s, f x âˆ‚Î¼ = 0 from
    hf.ae_eq_zero_of_forall_setIntegral_eq_zero (fun s hs _ â†¦ this s hs)
  have A : âˆ€ (t : Set Î²), MeasurableSet t â†’ âˆ« (x : Î²) in t, f x âˆ‚Î¼ = 0
      â†’ âˆ« (x : Î²) in tá¶œ, f x âˆ‚Î¼ = 0 := by
    intro t t_meas ht
    have I : âˆ« x, f x âˆ‚Î¼ = 0 := by rw [â† integral_univ]; exact h'f _ isClosed_univ
    simpa [ht, I] using integral_add_compl t_meas hf
  intro s hs
  refine MeasurableSet.induction_on_open (fun U hU â†¦ ?_) A (fun g g_disj g_meas hg â†¦ ?_) hs
  Â· rw [â† compl_compl U]
    exact A _ hU.measurableSet.compl (h'f _ hU.isClosed_compl)
  Â· rw [integral_iUnion g_meas g_disj hf.integrableOn]
    simp [hg]

@[deprecated (since := "2024-04-17")]
alias ae_eq_zero_of_forall_set_integral_isClosed_eq_zero :=
  ae_eq_zero_of_forall_setIntegral_isClosed_eq_zero

/-- If an integrable function has zero integral on all compact sets in a sigma-compact space, then
it is zero almost everwhere. -/
lemma ae_eq_zero_of_forall_setIntegral_isCompact_eq_zero
    [SigmaCompactSpace Î²] [T2Space Î²] {Î¼ : Measure Î²} {f : Î² â†’ E} (hf : Integrable f Î¼)
    (h'f : âˆ€ (s : Set Î²), IsCompact s â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) :
    f =áµ[Î¼] 0 := by
  apply ae_eq_zero_of_forall_setIntegral_isClosed_eq_zero hf (fun s hs â†¦ ?_)
  let t : â„• â†’ Set Î² := fun n â†¦ compactCovering Î² n âˆ© s
  suffices H : Tendsto (fun n â†¦ âˆ« x in t n, f x âˆ‚Î¼) atTop (ğ“ (âˆ« x in s, f x âˆ‚Î¼)) by
    have A : âˆ€ n, âˆ« x in t n, f x âˆ‚Î¼ = 0 :=
      fun n â†¦ h'f _ (IsCompact.inter_right (isCompact_compactCovering Î² n) hs)
    simp_rw [A, tendsto_const_nhds_iff] at H
    exact H.symm
  have B : s = â‹ƒ n, t n := by rw [â† Set.iUnion_inter, iUnion_compactCovering, Set.univ_inter]
  rw [B]
  apply tendsto_setIntegral_of_monotone
  Â· intros n
    exact ((isCompact_compactCovering Î² n).inter_right hs).isClosed.measurableSet
  Â· intros m n hmn
    exact Set.inter_subset_inter_left _ (compactCovering_subset Î² hmn)
  Â· exact hf.integrableOn

/-- If a locally integrable function has zero integral on all compact sets in a sigma-compact space,
then it is zero almost everwhere. -/
lemma ae_eq_zero_of_forall_setIntegral_isCompact_eq_zero'
    [SigmaCompactSpace Î²] [T2Space Î²] {Î¼ : Measure Î²} {f : Î² â†’ E} (hf : LocallyIntegrable f Î¼)
    (h'f : âˆ€ (s : Set Î²), IsCompact s â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) :
    f =áµ[Î¼] 0 := by
  rw [â† Measure.restrict_univ (Î¼ := Î¼), â† iUnion_compactCovering]
  apply (ae_restrict_iUnion_iff _ _).2 (fun n â†¦ ?_)
  apply ae_eq_zero_of_forall_setIntegral_isCompact_eq_zero
  Â· exact hf.integrableOn_isCompact (isCompact_compactCovering Î² n)
  Â· intro s hs
    rw [Measure.restrict_restrict hs.measurableSet]
    exact h'f _ (hs.inter (isCompact_compactCovering Î² n))

end AeEqOfForallSetIntegralEq

section Lintegral

theorem AEMeasurable.ae_eq_of_forall_set_lintegral_eq {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) (hgi : âˆ«â» x, g x âˆ‚Î¼ â‰  âˆ)
    (hfg : âˆ€ â¦ƒsâ¦„, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼) :
    f =áµ[Î¼] g := by
  refine
    ENNReal.eventuallyEq_of_toReal_eventuallyEq (ae_lt_top' hf hfi).ne_of_lt
      (ae_lt_top' hg hgi).ne_of_lt
      (Integrable.ae_eq_of_forall_setIntegral_eq _ _
        (integrable_toReal_of_lintegral_ne_top hf hfi)
        (integrable_toReal_of_lintegral_ne_top hg hgi) fun s hs hs' => ?_)
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae]
  Â· congr 1
    rw [lintegral_congr_ae (ofReal_toReal_ae_eq _), lintegral_congr_ae (ofReal_toReal_ae_eq _)]
    Â· exact hfg hs hs'
    Â· refine ae_lt_top' hg.restrict (ne_of_lt (lt_of_le_of_lt ?_ hgi.lt_top))
      exact @set_lintegral_univ Î± _ Î¼ g â–¸ lintegral_mono_set (Set.subset_univ _)
    Â· refine ae_lt_top' hf.restrict (ne_of_lt (lt_of_le_of_lt ?_ hfi.lt_top))
      exact @set_lintegral_univ Î± _ Î¼ f â–¸ lintegral_mono_set (Set.subset_univ _)
  -- putting the proofs where they are used is extremely slow
  exacts [ae_of_all _ fun x => ENNReal.toReal_nonneg,
    hg.ennreal_toReal.restrict.aestronglyMeasurable, ae_of_all _ fun x => ENNReal.toReal_nonneg,
    hf.ennreal_toReal.restrict.aestronglyMeasurable]
#align measure_theory.ae_measurable.ae_eq_of_forall_set_lintegral_eq MeasureTheory.AEMeasurable.ae_eq_of_forall_set_lintegral_eq

end Lintegral

section WithDensity

variable {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

theorem withDensity_eq_iff_of_sigmaFinite [SigmaFinite Î¼] {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) : Î¼.withDensity f = Î¼.withDensity g â†” f =áµ[Î¼] g :=
  âŸ¨fun hfg â†¦ by
    refine ae_eq_of_forall_set_lintegral_eq_of_sigmaFiniteâ‚€ hf hg fun s hs _ â†¦ ?_
    rw [â† withDensity_apply f hs, â† withDensity_apply g hs, â† hfg], withDensity_congr_aeâŸ©

theorem withDensity_eq_iff {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) :
    Î¼.withDensity f = Î¼.withDensity g â†” f =áµ[Î¼] g :=
  âŸ¨fun hfg â†¦ by
    refine AEMeasurable.ae_eq_of_forall_set_lintegral_eq hf hg hfi ?_ fun s hs _ â†¦ ?_
    Â· rwa [â† set_lintegral_univ, â† withDensity_apply g MeasurableSet.univ, â† hfg,
        withDensity_apply f MeasurableSet.univ, set_lintegral_univ]
    Â· rw [â† withDensity_apply f hs, â† withDensity_apply g hs, â† hfg], withDensity_congr_aeâŸ©

end WithDensity

end MeasureTheory
