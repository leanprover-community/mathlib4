/-
Copyright (c) 2022 RÃ©my Degenne, Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Topology.MetricSpace.Pseudo.Defs
import Mathlib.MeasureTheory.Function.Egorov
import Mathlib.MeasureTheory.Function.LpSpace

/-!
# Convergence in measure

We define convergence in measure which is one of the many notions of convergence in probability.
A sequence of functions `f` is said to converge in measure to some function `g`
if for all `Îµ > 0`, the measure of the set `{x | Îµ â‰¤ dist (f i x) (g x)}` tends to 0 as `i`
converges along some given filter `l`.

Convergence in measure is most notably used in the formulation of the weak law of large numbers
and is also useful in theorems such as the Vitali convergence theorem. This file provides some
basic lemmas for working with convergence in measure and establishes some relations between
convergence in measure and other notions of convergence.

## Main definitions

* `MeasureTheory.TendstoInMeasure (Î¼ : Measure Î±) (f : Î¹ â†’ Î± â†’ E) (g : Î± â†’ E)`: `f` converges
  in `Î¼`-measure to `g`.

## Main results

* `MeasureTheory.tendstoInMeasure_of_tendsto_ae`: convergence almost everywhere in a finite
  measure space implies convergence in measure.
* `MeasureTheory.TendstoInMeasure.exists_seq_tendsto_ae`: if `f` is a sequence of functions
  which converges in measure to `g`, then `f` has a subsequence which convergence almost
  everywhere to `g`.
* `MeasureTheory.exists_seq_tendstoInMeasure_atTop_iff`: for a sequence of functions `f`,
  convergence in measure is equivalent to the fact that every subsequence has another subsequence
  that converges almost surely.
* `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm`: convergence in Lp implies convergence
  in measure.
-/


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory Topology

namespace MeasureTheory

variable {Î± Î¹ Îº E : Type*} {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- A sequence of functions `f` is said to converge in measure to some function `g` if for all
`Îµ > 0`, the measure of the set `{x | Îµ â‰¤ dist (f i x) (g x)}` tends to 0 as `i` converges along
some given filter `l`. -/
def TendstoInMeasure [Dist E] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î¹ â†’ Î± â†’ E) (l : Filter Î¹)
    (g : Î± â†’ E) : Prop :=
  âˆ€ Îµ, 0 < Îµ â†’ Tendsto (fun i => Î¼ { x | Îµ â‰¤ dist (f i x) (g x) }) l (ğ“ 0)

theorem tendstoInMeasure_iff_norm [SeminormedAddCommGroup E] {l : Filter Î¹} {f : Î¹ â†’ Î± â†’ E}
    {g : Î± â†’ E} :
    TendstoInMeasure Î¼ f l g â†”
      âˆ€ Îµ, 0 < Îµ â†’ Tendsto (fun i => Î¼ { x | Îµ â‰¤ â€–f i x - g xâ€– }) l (ğ“ 0) := by
  simp_rw [TendstoInMeasure, dist_eq_norm]

theorem tendstoInMeasure_iff_tendsto_toNNReal [Dist E] {_ : MeasurableSpace Î±} {Î¼ : Measure Î±}
    [hfin: MeasureTheory.IsFiniteMeasure Î¼] {f : Î¹ â†’ Î± â†’ E} {l : Filter Î¹} {g : Î± â†’ E} :
    TendstoInMeasure Î¼ f l g â†” âˆ€ Îµ, 0 < Îµ â†’ Tendsto (fun i =>
    (Î¼ { x | Îµ â‰¤ dist (f i x) (g x) }).toNNReal) l (ğ“ 0) := by
  have hfin : âˆ€ Îµ, âˆ€ i, Î¼ { x | Îµ â‰¤ dist (f i x) (g x) } â‰  âŠ¤ := by
    exact fun Îµ i â†¦ (measure_ne_top Î¼ {x | Îµ â‰¤ dist (f i x) (g x)})
  constructor
  Â· intros h Îµ hÎµ
    have hf : (fun i =>
    (Î¼ { x | Îµ â‰¤ dist (f i x) (g x) }).toNNReal) = ENNReal.toNNReal âˆ˜ (fun i =>
    (Î¼ { x | Îµ â‰¤ dist (f i x) (g x) })) := by
      exact rfl
    rw [hf, â† ENNReal.tendsto_toNNReal_iff' (hfin Îµ)]
    exact h Îµ hÎµ
  Â· intros h Îµ hÎµ
    rw [ENNReal.tendsto_toNNReal_iff ENNReal.zero_ne_top (hfin Îµ) ]
    exact h Îµ hÎµ

-- Convergence in measure is stable under taking subsequences.
lemma TendstoInMeasure.mono [Dist E] {f : Î¹ â†’ Î± â†’ E}  {g : Î± â†’ E} {u v : Filter Î¹} (huv : v â‰¤ u)
    (hg :  TendstoInMeasure Î¼ f u g) : TendstoInMeasure Î¼ f v g :=
  fun Îµ hÎµ => (hg Îµ hÎµ).mono_left huv

lemma TendstoInMeasure.subseq [Dist E] {f : â„• â†’ Î± â†’ E}  {g : Î± â†’ E} {ns : â„• â†’ â„•}
    (hns : StrictMono ns) (hg :  TendstoInMeasure Î¼ f atTop g) :
    TendstoInMeasure Î¼ (f âˆ˜ ns) atTop g := by
  intro Îµ hÎµ
  apply Filter.Tendsto.comp (hg Îµ hÎµ) (StrictMono.tendsto_atTop hns)

lemma TendstoInMeasure.subseq' [Dist E] {f : Î¹ â†’ Î± â†’ E}  {g : Î± â†’ E} {u : Filter Î¹}
    {v : Filter Îº} {ns : Îº â†’ Î¹} (hns : Tendsto ns v u) (hg :  TendstoInMeasure Î¼ f u g) :
    TendstoInMeasure Î¼ (f âˆ˜ ns) v g := by
  intro Îµ hÎµ
  apply Filter.Tendsto.comp (hg Îµ hÎµ) hns

namespace TendstoInMeasure

variable [Dist E] {l : Filter Î¹} {f f' : Î¹ â†’ Î± â†’ E} {g g' : Î± â†’ E}

protected theorem congr' (h_left : âˆ€á¶  i in l, f i =áµ[Î¼] f' i) (h_right : g =áµ[Î¼] g')
    (h_tendsto : TendstoInMeasure Î¼ f l g) : TendstoInMeasure Î¼ f' l g' := by
  intro Îµ hÎµ
  suffices
    (fun i => Î¼ { x | Îµ â‰¤ dist (f' i x) (g' x) }) =á¶ [l] fun i => Î¼ { x | Îµ â‰¤ dist (f i x) (g x) } by
    rw [tendsto_congr' this]
    exact h_tendsto Îµ hÎµ
  filter_upwards [h_left] with i h_ae_eq
  refine measure_congr ?_
  filter_upwards [h_ae_eq, h_right] with x hxf hxg
  rw [eq_iff_iff]
  change Îµ â‰¤ dist (f' i x) (g' x) â†” Îµ â‰¤ dist (f i x) (g x)
  rw [hxg, hxf]

protected theorem congr (h_left : âˆ€ i, f i =áµ[Î¼] f' i) (h_right : g =áµ[Î¼] g')
    (h_tendsto : TendstoInMeasure Î¼ f l g) : TendstoInMeasure Î¼ f' l g' :=
  TendstoInMeasure.congr' (Eventually.of_forall h_left) h_right h_tendsto

theorem congr_left (h : âˆ€ i, f i =áµ[Î¼] f' i) (h_tendsto : TendstoInMeasure Î¼ f l g) :
    TendstoInMeasure Î¼ f' l g :=
  h_tendsto.congr h EventuallyEq.rfl

theorem congr_right (h : g =áµ[Î¼] g') (h_tendsto : TendstoInMeasure Î¼ f l g) :
    TendstoInMeasure Î¼ f l g' :=
  h_tendsto.congr (fun _ => EventuallyEq.rfl) h

end TendstoInMeasure

section ExistsSeqTendstoAe

variable [MetricSpace E]
variable {f : â„• â†’ Î± â†’ E} {g : Î± â†’ E}

/-- Auxiliary lemma for `tendstoInMeasure_of_tendsto_ae`. -/
theorem tendstoInMeasure_of_tendsto_ae_of_stronglyMeasurable [IsFiniteMeasure Î¼]
    (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) : TendstoInMeasure Î¼ f atTop g := by
  refine fun Îµ hÎµ => ENNReal.tendsto_atTop_zero.mpr fun Î´ hÎ´ => ?_
  by_cases hÎ´i : Î´ = âˆ
  Â· simp only [hÎ´i, imp_true_iff, le_top, exists_const]
  lift Î´ to â„â‰¥0 using hÎ´i
  rw [gt_iff_lt, ENNReal.coe_pos, â† NNReal.coe_pos] at hÎ´
  obtain âŸ¨t, _, ht, hunifâŸ© := tendstoUniformlyOn_of_ae_tendsto' hf hg hfg hÎ´
  rw [ENNReal.ofReal_coe_nnreal] at ht
  rw [Metric.tendstoUniformlyOn_iff] at hunif
  obtain âŸ¨N, hNâŸ© := eventually_atTop.1 (hunif Îµ hÎµ)
  refine âŸ¨N, fun n hn => ?_âŸ©
  suffices { x : Î± | Îµ â‰¤ dist (f n x) (g x) } âŠ† t from (measure_mono this).trans ht
  rw [â† Set.compl_subset_compl]
  intro x hx
  rw [Set.mem_compl_iff, Set.nmem_setOf_iff, dist_comm, not_le]
  exact hN n hn x hx

/-- Convergence a.e. implies convergence in measure in a finite measure space. -/
theorem tendstoInMeasure_of_tendsto_ae [IsFiniteMeasure Î¼] (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (hfg : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) : TendstoInMeasure Î¼ f atTop g := by
  have hg : AEStronglyMeasurable g Î¼ := aestronglyMeasurable_of_tendsto_ae _ hf hfg
  refine TendstoInMeasure.congr (fun i => (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm ?_
  refine tendstoInMeasure_of_tendsto_ae_of_stronglyMeasurable
    (fun i => (hf i).stronglyMeasurable_mk) hg.stronglyMeasurable_mk ?_
  have hf_eq_ae : âˆ€áµ x âˆ‚Î¼, âˆ€ n, (hf n).mk (f n) x = f n x :=
    ae_all_iff.mpr fun n => (hf n).ae_eq_mk.symm
  filter_upwards [hf_eq_ae, hg.ae_eq_mk, hfg] with x hxf hxg hxfg
  rw [â† hxg, funext fun n => hxf n]
  exact hxfg

namespace ExistsSeqTendstoAe

theorem exists_nat_measure_lt_two_inv (hfg : TendstoInMeasure Î¼ f atTop g) (n : â„•) :
    âˆƒ N, âˆ€ m â‰¥ N, Î¼ { x | (2 : â„)â»Â¹ ^ n â‰¤ dist (f m x) (g x) } â‰¤ (2â»Â¹ : â„â‰¥0âˆ) ^ n := by
  specialize hfg ((2â»Â¹ : â„) ^ n) (by simp only [Real.rpow_natCast, inv_pos, zero_lt_two, pow_pos])
  rw [ENNReal.tendsto_atTop_zero] at hfg
  exact hfg ((2 : â„â‰¥0âˆ)â»Â¹ ^ n) (pos_iff_ne_zero.mpr fun h_zero => by simpa using pow_eq_zero h_zero)

/-- Given a sequence of functions `f` which converges in measure to `g`,
`seqTendstoAeSeqAux` is a sequence such that
`âˆ€ m â‰¥ seqTendstoAeSeqAux n, Î¼ {x | 2â»Â¹ ^ n â‰¤ dist (f m x) (g x)} â‰¤ 2â»Â¹ ^ n`. -/
noncomputable def seqTendstoAeSeqAux (hfg : TendstoInMeasure Î¼ f atTop g) (n : â„•) :=
  Classical.choose (exists_nat_measure_lt_two_inv hfg n)

/-- Transformation of `seqTendstoAeSeqAux` to makes sure it is strictly monotone. -/
noncomputable def seqTendstoAeSeq (hfg : TendstoInMeasure Î¼ f atTop g) : â„• â†’ â„•
  | 0 => seqTendstoAeSeqAux hfg 0
  | n + 1 => max (seqTendstoAeSeqAux hfg (n + 1)) (seqTendstoAeSeq hfg n + 1)

theorem seqTendstoAeSeq_succ (hfg : TendstoInMeasure Î¼ f atTop g) {n : â„•} :
    seqTendstoAeSeq hfg (n + 1) =
      max (seqTendstoAeSeqAux hfg (n + 1)) (seqTendstoAeSeq hfg n + 1) := by
  rw [seqTendstoAeSeq]

theorem seqTendstoAeSeq_spec (hfg : TendstoInMeasure Î¼ f atTop g) (n k : â„•)
    (hn : seqTendstoAeSeq hfg n â‰¤ k) :
    Î¼ { x | (2 : â„)â»Â¹ ^ n â‰¤ dist (f k x) (g x) } â‰¤ (2 : â„â‰¥0âˆ)â»Â¹ ^ n := by
  cases n
  Â· exact Classical.choose_spec (exists_nat_measure_lt_two_inv hfg 0) k hn
  Â· exact Classical.choose_spec
      (exists_nat_measure_lt_two_inv hfg _) _ (le_trans (le_max_left _ _) hn)

theorem seqTendstoAeSeq_strictMono (hfg : TendstoInMeasure Î¼ f atTop g) :
    StrictMono (seqTendstoAeSeq hfg) := by
  refine strictMono_nat_of_lt_succ fun n => ?_
  rw [seqTendstoAeSeq_succ]
  exact lt_of_lt_of_le (lt_add_one <| seqTendstoAeSeq hfg n) (le_max_right _ _)

end ExistsSeqTendstoAe

/-- If `f` is a sequence of functions which converges in measure to `g`, then there exists a
subsequence of `f` which converges a.e. to `g`. -/
theorem TendstoInMeasure.exists_seq_tendsto_ae (hfg : TendstoInMeasure Î¼ f atTop g) :
    âˆƒ ns : â„• â†’ â„•, StrictMono ns âˆ§ âˆ€áµ x âˆ‚Î¼, Tendsto (fun i => f (ns i) x) atTop (ğ“ (g x)) := by
  /- Since `f` tends to `g` in measure, it has a subsequence `k â†¦ f (ns k)` such that
    `Î¼ {|f (ns k) - g| â‰¥ 2â»áµ} â‰¤ 2â»áµ` for all `k`. Defining
    `s := â‹‚ k, â‹ƒ i â‰¥ k, {|f (ns k) - g| â‰¥ 2â»áµ}`, we see that `Î¼ s = 0` by the
    first Borel-Cantelli lemma.

    On the other hand, as `s` is precisely the set for which `f (ns k)`
    doesn't converge to `g`, `f (ns k)` converges almost everywhere to `g` as required. -/
  have h_lt_Îµ_real : âˆ€ (Îµ : â„) (_ : 0 < Îµ), âˆƒ k : â„•, 2 * (2 : â„)â»Â¹ ^ k < Îµ := by
    intro Îµ hÎµ
    obtain âŸ¨k, h_kâŸ© : âˆƒ k : â„•, (2 : â„)â»Â¹ ^ k < Îµ := exists_pow_lt_of_lt_one hÎµ (by norm_num)
    refine âŸ¨k + 1, (le_of_eq ?_).trans_lt h_kâŸ©
    rw [pow_add]; ring
  set ns := ExistsSeqTendstoAe.seqTendstoAeSeq hfg
  use ns
  let S := fun k => { x | (2 : â„)â»Â¹ ^ k â‰¤ dist (f (ns k) x) (g x) }
  have hÎ¼S_le : âˆ€ k, Î¼ (S k) â‰¤ (2 : â„â‰¥0âˆ)â»Â¹ ^ k :=
    fun k => ExistsSeqTendstoAe.seqTendstoAeSeq_spec hfg k (ns k) le_rfl
  set s := Filter.atTop.limsup S with hs
  have hÎ¼s : Î¼ s = 0 := by
    refine measure_limsup_eq_zero (ne_of_lt <| lt_of_le_of_lt (ENNReal.tsum_le_tsum hÎ¼S_le) ?_)
    simp only [ENNReal.tsum_geometric, ENNReal.one_sub_inv_two, ENNReal.two_lt_top, inv_inv]
  have h_tendsto : âˆ€ x âˆˆ sá¶œ, Tendsto (fun i => f (ns i) x) atTop (ğ“ (g x)) := by
    refine fun x hx => Metric.tendsto_atTop.mpr fun Îµ hÎµ => ?_
    rw [hs, limsup_eq_iInf_iSup_of_nat] at hx
    simp only [S, Set.iSup_eq_iUnion, Set.iInf_eq_iInter, Set.compl_iInter, Set.compl_iUnion,
      Set.mem_iUnion, Set.mem_iInter, Set.mem_compl_iff, Set.mem_setOf_eq, not_le] at hx
    obtain âŸ¨N, hNxâŸ© := hx
    obtain âŸ¨k, hk_lt_ÎµâŸ© := h_lt_Îµ_real Îµ hÎµ
    refine âŸ¨max N (k - 1), fun n hn_ge => lt_of_le_of_lt ?_ hk_lt_ÎµâŸ©
    specialize hNx n ((le_max_left _ _).trans hn_ge)
    have h_inv_n_le_k : (2 : â„)â»Â¹ ^ n â‰¤ 2 * (2 : â„)â»Â¹ ^ k := by
      rw [mul_comm, â† inv_mul_le_iff' (zero_lt_two' â„)]
      conv_lhs =>
        congr
        rw [â† pow_one (2 : â„)â»Â¹]
      rw [â† pow_add, add_comm]
      exact pow_le_pow_of_le_one (one_div (2 : â„) â–¸ one_half_pos.le) (inv_le_one one_le_two)
        ((le_tsub_add.trans (add_le_add_right (le_max_right _ _) 1)).trans
          (add_le_add_right hn_ge 1))
    exact le_trans hNx.le h_inv_n_le_k
  rw [ae_iff]
  refine âŸ¨ExistsSeqTendstoAe.seqTendstoAeSeq_strictMono hfg, measure_mono_null (fun x => ?_) hÎ¼sâŸ©
  rw [Set.mem_setOf_eq, â† @Classical.not_not (x âˆˆ s), not_imp_not]
  exact h_tendsto x

theorem TendstoInMeasure.exists_seq_tendstoInMeasure_atTop {u : Filter Î¹} [NeBot u]
    [IsCountablyGenerated u] {f : Î¹ â†’ Î± â†’ E} {g : Î± â†’ E} (hfg : TendstoInMeasure Î¼ f u g) :
    âˆƒ (ns : â„• â†’ Î¹) (_ : Tendsto ns atTop u),  TendstoInMeasure Î¼ (fun n => f (ns n)) atTop g := by
  obtain âŸ¨ns, h_tendsto_nsâŸ© : âˆƒ ns : â„• â†’ Î¹, Tendsto ns atTop u := exists_seq_tendsto u
  exact âŸ¨ns, h_tendsto_ns, fun Îµ hÎµ => (hfg Îµ hÎµ).comp h_tendsto_nsâŸ©

theorem TendstoInMeasure.exists_seq_tendsto_ae' {u : Filter Î¹} [NeBot u] [IsCountablyGenerated u]
    {f : Î¹ â†’ Î± â†’ E} {g : Î± â†’ E} (hfg : TendstoInMeasure Î¼ f u g) :
    âˆƒ (ns : â„• â†’ Î¹) (_ : Tendsto ns atTop u),
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun i => f (ns i) x) atTop (ğ“ (g x)) := by
  obtain âŸ¨ms, hms1, hms2âŸ© := hfg.exists_seq_tendstoInMeasure_atTop
  obtain âŸ¨ns, hns1, hns2âŸ© := hms2.exists_seq_tendsto_ae
  exact âŸ¨ms âˆ˜ ns, Tendsto.comp hms1 (StrictMono.tendsto_atTop hns1), hns2âŸ©

section

/- An auxiliary lemma for a proof by contradiction in exists_seq_tendstoInMeasure_atTop_iff -/
lemma false_of_tendsto_of_bddBelow_aux (f : â„• â†’ â„â‰¥0) (Î´ : â„) (hÎ´: (0 : â„) < Î´)
    (hf1 : Tendsto f atTop (ğ“ 0)) (hf2 : âˆ€ n, Î´ â‰¤ (f n) ) : False := by
  have h : âˆ€ x : â„â‰¥0, x.toReal = dist x 0 := by
    intro x
    rw [NNReal.dist_eq x 0, NNReal.coe_zero, sub_zero, NNReal.abs_eq]
  simp_rw [h] at hf2
  apply Metric.false_of_Tendsto_of_bddBelow
    hÎ´ (Tendsto.comp (NNReal.tendsto_coe'.mpr âŸ¨Preorder.le_refl 0, fun â¦ƒ_â¦„ a â†¦ a âŸ©) hf1) _
  refine frequently_atTop'.mpr ?_
  intro n
  use n+1
  simp only [gt_iff_lt, lt_add_iff_pos_right, zero_lt_one, Function.comp_apply, dist_zero_right,
    Real.norm_eq_abs, NNReal.abs_eq, true_and]
  have h : âˆ€ n, dist (f n) 0 = f n := by
    exact fun n â†¦ Eq.symm (Real.ext_cauchy (congrArg Real.cauchy (h (f n))))
  rw [â† h]
  exact hf2 (n+1)

end

lemma TendstoInMeasure.comp_of_tendsto {u : Filter Î¹} {v : Filter Îº} {f : Î¹ â†’ Î± â†’ E}
    {g : Î± â†’ E} {ns : Îº â†’ Î¹} (hfg : TendstoInMeasure Î¼ f u g) (hns : Tendsto ns v u) :
    TendstoInMeasure Î¼ (f âˆ˜ ns) v g :=
  fun Îµ hÎµ => (hfg Îµ hÎµ).comp hns

lemma subseq_of_not_tendsto {f : â„• â†’ NNReal} (h : Â¬Tendsto f atTop (ğ“ (0 : â„â‰¥0))) :
    âˆƒ Îµ > 0, âˆƒ (ns : â„• â†’ â„•) (_ : StrictMono ns), âˆ€ n, Îµ â‰¤ (f (ns n)).toReal := by
  rw [Filter.not_tendsto_iff_exists_frequently_nmem] at h
  rcases h with âŸ¨A, âŸ¨hA1, hA2âŸ©âŸ©
  rw [Metric.mem_nhds_iff] at hA1
  rcases (Filter.extraction_of_frequently_atTop hA2) with âŸ¨ns, hns, h4âŸ©
  rcases hA1 with âŸ¨Îµ, hÎµ, h3âŸ©
  use Îµ, hÎµ, ns, hns
  intro n
  rw [â† NNReal.abs_eq, â† Real.norm_eq_abs, â† not_lt, â† mem_ball_zero_iff]
  exact fun a => (h4 n) (h3 a)

/- TendstoInMeasure is equivalent to  every subsequence has another subsequence
which converges almost surely. -/
theorem exists_seq_tendstoInMeasure_atTop_iff (hfin : MeasureTheory.IsFiniteMeasure Î¼)
    {f : â„• â†’ Î± â†’ E} (hf : âˆ€ (n : â„•), AEStronglyMeasurable (f n) Î¼) {g : Î± â†’ E} :
    (TendstoInMeasure Î¼ f atTop g) â†”
    âˆ€ (ns : â„• â†’ â„•) (_ : StrictMono ns), âˆƒ (ns' : â„• â†’ â„•) (_ : StrictMono ns'), âˆ€áµ (Ï‰ : Î±) âˆ‚Î¼,
    Tendsto (fun i â†¦ f (ns (ns' i)) Ï‰) atTop (ğ“ (g Ï‰)) := by
  rw [tendstoInMeasure_iff_tendsto_toNNReal]
  constructor
  Â· intros hfg ns hns
    have h1 : TendstoInMeasure Î¼ (f âˆ˜ ns)
      atTop g := (TendstoInMeasure.subseq hns
      (tendstoInMeasure_iff_tendsto_toNNReal.mpr hfg))
    have âŸ¨ns', hns1, hns2âŸ© :=
    TendstoInMeasure.exists_seq_tendsto_ae h1
    use ns', hns1
    filter_upwards [hns2] with x hns3
    exact hns3
  Â· rw [â† not_imp_not]
    intros h1
    push_neg
    obtain h2 : âˆƒ (Îµ : â„) (_ : 0 < Îµ),
      Â¬(Tendsto (fun n => (Î¼ { x | Îµ â‰¤ dist (f n x) (g x) }).toNNReal) atTop (ğ“ 0)) := by
      Â· by_contra h3
        apply h1
        push_neg at h3
        exact h3
    rcases h2 with âŸ¨Îµ, hÎµ, h4âŸ©
    obtain h5 := @subseq_of_not_tendsto (fun n â†¦ (Î¼ {x | Îµ â‰¤ dist (f n x) (g x)}).toNNReal) h4
    rcases h5 with âŸ¨Î´, hÎ´, ns, hns, h5âŸ©
    use ns, hns
    intros ns' _
    by_contra h6
    apply h4
    have h4 : âˆ€ (n : â„•), AEStronglyMeasurable ((f âˆ˜ ns âˆ˜ ns') n) Î¼ := by exact fun n â†¦
      (hf ((ns âˆ˜ ns') n))
    have h8 := (MeasureTheory.tendstoInMeasure_of_tendsto_ae (f := f âˆ˜ ns âˆ˜ ns') (h4) h6)
    obtain h7 := fun n => h5 (ns' n)
    rw [tendstoInMeasure_iff_tendsto_toNNReal] at h8
    exfalso
    revert h7
    apply false_of_tendsto_of_bddBelow_aux
      (fun n => (Î¼ {x | Îµ â‰¤ dist (f (ns (ns' n)) x) (g x)}).toNNReal) Î´ hÎ´ (h8 Îµ hÎµ)

section

end

end ExistsSeqTendstoAe

section TendstoInMeasureUnique

variable [MetricSpace E]
variable {f : â„• â†’ Î± â†’ E} {g h : Î± â†’ E}

-- The TendstoInMeasure is ae unique
theorem ae_unique_of_tendstoInMeasure' (hg : TendstoInMeasure Î¼ f atTop g)
    (hh : TendstoInMeasure Î¼ f atTop h) : g =áµ[Î¼] h := by
  obtain âŸ¨ns,h1,h1'âŸ© := TendstoInMeasure.exists_seq_tendsto_ae hg
  obtain âŸ¨ns', h2, h2'âŸ© :=
    TendstoInMeasure.exists_seq_tendsto_ae (TendstoInMeasure.subseq h1 hh)
  obtain h4 : âˆ€áµ (x : Î±) âˆ‚Î¼, Tendsto (fun i â†¦ f (ns (ns' i)) x) atTop (ğ“ (g x)) := by
    filter_upwards [h1'] with Ï‰ h
    apply Filter.Tendsto.comp h (StrictMono.tendsto_atTop h2)
  filter_upwards [h4, h2'] with Ï‰ hg1 hh1
  refine tendsto_nhds_unique hg1 hh1

-- Same as above but with a more general filter on Î¹
theorem ae_unique_of_tendstoInMeasure {g h : Î± â†’ E} {f : Î¹ â†’ Î± â†’ E}  {u : Filter Î¹} [NeBot u]
    [IsCountablyGenerated u] (hg : TendstoInMeasure Î¼ f u g) (hh : TendstoInMeasure Î¼ f u h) :
    g =áµ[Î¼] h := by
  obtain âŸ¨ns,h1,h1'âŸ© := TendstoInMeasure.exists_seq_tendstoInMeasure_atTop hg
  exact ae_unique_of_tendstoInMeasure' (f := f âˆ˜ ns) h1' (TendstoInMeasure.subseq' h1 hh)

end TendstoInMeasureUnique

section AEMeasurableOf

variable [MeasurableSpace E] [NormedAddCommGroup E] [BorelSpace E]

theorem TendstoInMeasure.aemeasurable {u : Filter Î¹} [NeBot u] [IsCountablyGenerated u]
    {f : Î¹ â†’ Î± â†’ E} {g : Î± â†’ E} (hf : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_tendsto : TendstoInMeasure Î¼ f u g) : AEMeasurable g Î¼ := by
  obtain âŸ¨ns, -, hnsâŸ© := h_tendsto.exists_seq_tendsto_ae'
  exact aemeasurable_of_tendsto_metrizable_ae atTop (fun n => hf (ns n)) hns

end AEMeasurableOf

section TendstoInMeasureOf

variable [NormedAddCommGroup E] {p : â„â‰¥0âˆ}
variable {f : Î¹ â†’ Î± â†’ E} {g : Î± â†’ E}

/-- This lemma is superceded by `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm` where we
allow `p = âˆ` and only require `AEStronglyMeasurable`. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm_of_stronglyMeasurable (hp_ne_zero : p â‰  0)
    (hp_ne_top : p â‰  âˆ) (hf : âˆ€ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    {l : Filter Î¹} (hfg : Tendsto (fun n => eLpNorm (f n - g) p Î¼) l (ğ“ 0)) :
    TendstoInMeasure Î¼ f l g := by
  intro Îµ hÎµ
  replace hfg := ENNReal.Tendsto.const_mul
    (Tendsto.ennrpow_const p.toReal hfg) (Or.inr <| @ENNReal.ofReal_ne_top (1 / Îµ ^ p.toReal))
  simp only [mul_zero,
    ENNReal.zero_rpow_of_pos (ENNReal.toReal_pos hp_ne_zero hp_ne_top)] at hfg
  rw [ENNReal.tendsto_nhds_zero] at hfg âŠ¢
  intro Î´ hÎ´
  refine (hfg Î´ hÎ´).mono fun n hn => ?_
  refine le_trans ?_ hn
  rw [ENNReal.ofReal_div_of_pos (Real.rpow_pos_of_pos hÎµ _), ENNReal.ofReal_one, mul_comm,
    mul_one_div, ENNReal.le_div_iff_mul_le _ (Or.inl ENNReal.ofReal_ne_top), mul_comm]
  Â· rw [â† ENNReal.ofReal_rpow_of_pos hÎµ]
    convert mul_meas_ge_le_pow_eLpNorm' Î¼ hp_ne_zero hp_ne_top ((hf n).sub hg).aestronglyMeasurable
        (ENNReal.ofReal Îµ)
    rw [dist_eq_norm, â† ENNReal.ofReal_le_ofReal_iff (norm_nonneg _), ofReal_norm_eq_coe_nnnorm]
    exact Iff.rfl
  Â· rw [Ne, ENNReal.ofReal_eq_zero, not_le]
    exact Or.inl (Real.rpow_pos_of_pos hÎµ _)

@[deprecated (since := "2024-07-27")]
alias tendstoInMeasure_of_tendsto_snorm_of_stronglyMeasurable :=
  tendstoInMeasure_of_tendsto_eLpNorm_of_stronglyMeasurable

/-- This lemma is superceded by `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm` where we
allow `p = âˆ`. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm_of_ne_top (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hg : AEStronglyMeasurable g Î¼) {l : Filter Î¹}
    (hfg : Tendsto (fun n => eLpNorm (f n - g) p Î¼) l (ğ“ 0)) : TendstoInMeasure Î¼ f l g := by
  refine TendstoInMeasure.congr (fun i => (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm ?_
  refine tendstoInMeasure_of_tendsto_eLpNorm_of_stronglyMeasurable
    hp_ne_zero hp_ne_top (fun i => (hf i).stronglyMeasurable_mk) hg.stronglyMeasurable_mk ?_
  have : (fun n => eLpNorm ((hf n).mk (f n) - hg.mk g) p Î¼) = fun n => eLpNorm (f n - g) p Î¼ := by
    ext1 n; refine eLpNorm_congr_ae (EventuallyEq.sub (hf n).ae_eq_mk.symm hg.ae_eq_mk.symm)
  rw [this]
  exact hfg

@[deprecated (since := "2024-07-27")]
alias tendstoInMeasure_of_tendsto_snorm_of_ne_top := tendstoInMeasure_of_tendsto_eLpNorm_of_ne_top

/-- See also `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm` which work for general
Lp-convergence for all `p â‰  0`. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm_top {E} [NormedAddCommGroup E] {f : Î¹ â†’ Î± â†’ E}
    {g : Î± â†’ E} {l : Filter Î¹} (hfg : Tendsto (fun n => eLpNorm (f n - g) âˆ Î¼) l (ğ“ 0)) :
    TendstoInMeasure Î¼ f l g := by
  intro Î´ hÎ´
  simp only [eLpNorm_exponent_top, eLpNormEssSup] at hfg
  rw [ENNReal.tendsto_nhds_zero] at hfg âŠ¢
  intro Îµ hÎµ
  specialize hfg (ENNReal.ofReal Î´ / 2)
      (ENNReal.div_pos_iff.2 âŸ¨(ENNReal.ofReal_pos.2 hÎ´).ne.symm, ENNReal.two_ne_topâŸ©)
  refine hfg.mono fun n hn => ?_
  simp only [true_and_iff, gt_iff_lt, zero_tsub, zero_le, zero_add, Set.mem_Icc,
    Pi.sub_apply] at *
  have : essSup (fun x : Î± => (â€–f n x - g xâ€–â‚Š : â„â‰¥0âˆ)) Î¼ < ENNReal.ofReal Î´ :=
    lt_of_le_of_lt hn
      (ENNReal.half_lt_self (ENNReal.ofReal_pos.2 hÎ´).ne.symm ENNReal.ofReal_lt_top.ne)
  refine ((le_of_eq ?_).trans (ae_lt_of_essSup_lt this).le).trans hÎµ.le
  congr with x
  simp only [ENNReal.ofReal_le_iff_le_toReal ENNReal.coe_lt_top.ne, ENNReal.coe_toReal, not_lt,
    coe_nnnorm, Set.mem_setOf_eq, Set.mem_compl_iff]
  rw [â† dist_eq_norm (f n x) (g x)]

@[deprecated (since := "2024-07-27")]
alias tendstoInMeasure_of_tendsto_snorm_top := tendstoInMeasure_of_tendsto_eLpNorm_top

/-- Convergence in Lp implies convergence in measure. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm {l : Filter Î¹} (hp_ne_zero : p â‰  0)
    (hf : âˆ€ n, AEStronglyMeasurable (f n) Î¼) (hg : AEStronglyMeasurable g Î¼)
    (hfg : Tendsto (fun n => eLpNorm (f n - g) p Î¼) l (ğ“ 0)) : TendstoInMeasure Î¼ f l g := by
  by_cases hp_ne_top : p = âˆ
  Â· subst hp_ne_top
    exact tendstoInMeasure_of_tendsto_eLpNorm_top hfg
  Â· exact tendstoInMeasure_of_tendsto_eLpNorm_of_ne_top hp_ne_zero hp_ne_top hf hg hfg

@[deprecated (since := "2024-07-27")]
alias tendstoInMeasure_of_tendsto_snorm := tendstoInMeasure_of_tendsto_eLpNorm

/-- Convergence in Lp implies convergence in measure. -/
theorem tendstoInMeasure_of_tendsto_Lp [hp : Fact (1 â‰¤ p)] {f : Î¹ â†’ Lp E p Î¼} {g : Lp E p Î¼}
    {l : Filter Î¹} (hfg : Tendsto f l (ğ“ g)) : TendstoInMeasure Î¼ (fun n => f n) l g :=
  tendstoInMeasure_of_tendsto_eLpNorm (zero_lt_one.trans_le hp.elim).ne.symm
    (fun _ => Lp.aestronglyMeasurable _) (Lp.aestronglyMeasurable _)
    ((Lp.tendsto_Lp_iff_tendsto_â„’p' _ _).mp hfg)

end TendstoInMeasureOf

end MeasureTheory
