/-
Copyright (c) 2022 R√©my Degenne, Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Kexing Ying
-/
import Mathlib.MeasureTheory.Function.Egorov
import Mathlib.MeasureTheory.Function.LpSpace.Complete

/-!
# Convergence in measure

We define convergence in measure which is one of the many notions of convergence in probability.
A sequence of functions `f` is said to converge in measure to some function `g`
if for all `Œµ > 0`, the measure of the set `{x | Œµ ‚â§ edist (f i x) (g x)}` tends to 0 as `i`
converges along some given filter `l`.

Convergence in measure is most notably used in the formulation of the weak law of large numbers
and is also useful in theorems such as the Vitali convergence theorem. This file provides some
basic lemmas for working with convergence in measure and establishes some relations between
convergence in measure and other notions of convergence.

## Main definitions

* `MeasureTheory.TendstoInMeasure (Œº : Measure Œ±) (f : Œπ ‚Üí Œ± ‚Üí E) (g : Œ± ‚Üí E)`: `f` converges
  in `Œº`-measure to `g`.

## Main results

* `MeasureTheory.tendstoInMeasure_of_tendsto_ae`: convergence almost everywhere in a finite
  measure space implies convergence in measure.
* `MeasureTheory.TendstoInMeasure.exists_seq_tendsto_ae`: if `f` is a sequence of functions
  which converges in measure to `g`, then `f` has a subsequence which convergence almost
  everywhere to `g`.
* `MeasureTheory.exists_seq_tendstoInMeasure_atTop_iff`: for a sequence of functions `f`,
  convergence in measure is equivalent to the fact that every subsequence has another subsequence
  that converges almost surely.
* `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm`: convergence in Lp implies convergence
  in measure.
-/


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory Topology

namespace MeasureTheory

variable {Œ± Œπ Œ∫ E : Type*} {m : MeasurableSpace Œ±} {Œº : Measure Œ±}

/-- A sequence of functions `f` is said to converge in measure to some function `g` if for all
`Œµ > 0`, the measure of the set `{x | Œµ ‚â§ dist (f i x) (g x)}` tends to 0 as `i` converges along
some given filter `l`. -/
def TendstoInMeasure [EDist E] {_ : MeasurableSpace Œ±} (Œº : Measure Œ±) (f : Œπ ‚Üí Œ± ‚Üí E)
    (l : Filter Œπ) (g : Œ± ‚Üí E) : Prop :=
  ‚àÄ Œµ, 0 < Œµ ‚Üí Tendsto (fun i ‚Ü¶ Œº { x | Œµ ‚â§ edist (f i x) (g x) }) l (ùìù 0)

lemma tendstoInMeasure_of_ne_top [EDist E] {f : Œπ ‚Üí Œ± ‚Üí E} {l : Filter Œπ} {g : Œ± ‚Üí E}
    (h : ‚àÄ Œµ, 0 < Œµ ‚Üí Œµ ‚â† ‚àû ‚Üí Tendsto (fun i ‚Ü¶ Œº { x | Œµ ‚â§ edist (f i x) (g x) }) l (ùìù 0)) :
    TendstoInMeasure Œº f l g := by
  intro Œµ hŒµ
  by_cases hŒµ_top : Œµ = ‚àû
  ¬∑ have h1 : Tendsto (fun n ‚Ü¶ Œº {œâ | 1 ‚â§ edist (f n œâ) (g œâ)}) l (ùìù 0) := h 1 (by simp) (by simp)
    refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h1 (fun _ ‚Ü¶ zero_le') ?_
    intro n
    simp only [hŒµ_top]
    gcongr
    simp
  ¬∑ exact h Œµ hŒµ hŒµ_top

theorem tendstoInMeasure_iff_enorm [SeminormedAddCommGroup E] {l : Filter Œπ} {f : Œπ ‚Üí Œ± ‚Üí E}
    {g : Œ± ‚Üí E} :
    TendstoInMeasure Œº f l g ‚Üî
      ‚àÄ Œµ, 0 < Œµ ‚Üí Œµ ‚â† ‚àû ‚Üí Tendsto (fun i ‚Ü¶ Œº { x | Œµ ‚â§ ‚Äñf i x - g x‚Äñ‚Çë }) l (ùìù 0) := by
  simp_rw [‚Üê edist_eq_enorm_sub]
  exact ‚ü®fun h Œµ hŒµ hŒµ_top ‚Ü¶ h Œµ hŒµ, tendstoInMeasure_of_ne_top‚ü©

lemma tendstoInMeasure_iff_dist [PseudoMetricSpace E] {f : Œπ ‚Üí Œ± ‚Üí E} {l : Filter Œπ} {g : Œ± ‚Üí E} :
    TendstoInMeasure Œº f l g
      ‚Üî ‚àÄ Œµ, 0 < Œµ ‚Üí Tendsto (fun i ‚Ü¶ Œº { x | Œµ ‚â§ dist (f i x) (g x) }) l (ùìù 0) := by
  refine ‚ü®fun h Œµ hŒµ ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ convert h (ENNReal.ofReal Œµ) (ENNReal.ofReal_pos.mpr hŒµ) with i a
    rw [edist_dist, ENNReal.ofReal_le_ofReal_iff (by positivity)]
  ¬∑ refine tendstoInMeasure_of_ne_top fun Œµ hŒµ hŒµ_top ‚Ü¶ ?_
    convert h Œµ.toReal (ENNReal.toReal_pos hŒµ.ne' hŒµ_top) with i a
    rw [edist_dist, ENNReal.le_ofReal_iff_toReal_le hŒµ_top (by positivity)]

theorem tendstoInMeasure_iff_norm [SeminormedAddCommGroup E] {l : Filter Œπ} {f : Œπ ‚Üí Œ± ‚Üí E}
    {g : Œ± ‚Üí E} :
    TendstoInMeasure Œº f l g ‚Üî
      ‚àÄ Œµ, 0 < Œµ ‚Üí Tendsto (fun i ‚Ü¶ Œº { x | Œµ ‚â§ ‚Äñf i x - g x‚Äñ }) l (ùìù 0) := by
  simp_rw [tendstoInMeasure_iff_dist, dist_eq_norm_sub]

theorem tendstoInMeasure_iff_tendsto_toNNReal [EDist E] [IsFiniteMeasure Œº]
    {f : Œπ ‚Üí Œ± ‚Üí E} {l : Filter Œπ} {g : Œ± ‚Üí E} :
    TendstoInMeasure Œº f l g ‚Üî
      ‚àÄ Œµ, 0 < Œµ ‚Üí Tendsto (fun i ‚Ü¶ (Œº { x | Œµ ‚â§ edist (f i x) (g x) }).toNNReal) l (ùìù 0) := by
  have hfin Œµ i : Œº { x | Œµ ‚â§ edist (f i x) (g x) } ‚â† ‚ä§ :=
    measure_ne_top Œº {x | Œµ ‚â§ edist (f i x) (g x)}
  refine ‚ü®fun h Œµ hŒµ ‚Ü¶ ?_, fun h Œµ hŒµ ‚Ü¶ ?_‚ü©
  ¬∑ have hf : (fun i ‚Ü¶ (Œº { x | Œµ ‚â§ edist (f i x) (g x) }).toNNReal) =
        ENNReal.toNNReal ‚àò (fun i ‚Ü¶ (Œº { x | Œµ ‚â§ edist (f i x) (g x) })) := rfl
    rw [hf, ENNReal.tendsto_toNNReal_iff' (hfin Œµ)]
    exact h Œµ hŒµ
  ¬∑ rw [‚Üê ENNReal.tendsto_toNNReal_iff ENNReal.zero_ne_top (hfin Œµ)]
    exact h Œµ hŒµ

lemma TendstoInMeasure.mono [EDist E] {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} {u v : Filter Œπ} (huv : v ‚â§ u)
    (hg : TendstoInMeasure Œº f u g) : TendstoInMeasure Œº f v g :=
  fun Œµ hŒµ ‚Ü¶ (hg Œµ hŒµ).mono_left huv

lemma TendstoInMeasure.comp [EDist E] {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} {u : Filter Œπ}
    {v : Filter Œ∫} {ns : Œ∫ ‚Üí Œπ} (hg : TendstoInMeasure Œº f u g) (hns : Tendsto ns v u) :
    TendstoInMeasure Œº (f ‚àò ns) v g := fun Œµ hŒµ ‚Ü¶ (hg Œµ hŒµ).comp hns

namespace TendstoInMeasure

variable [EDist E] {l : Filter Œπ} {f f' : Œπ ‚Üí Œ± ‚Üí E} {g g' : Œ± ‚Üí E}

protected theorem congr' (h_left : ‚àÄ·∂† i in l, f i =·µê[Œº] f' i) (h_right : g =·µê[Œº] g')
    (h_tendsto : TendstoInMeasure Œº f l g) : TendstoInMeasure Œº f' l g' := by
  intro Œµ hŒµ
  suffices
    (fun i ‚Ü¶ Œº { x | Œµ ‚â§ edist (f' i x) (g' x) }) =·∂†[l] fun i ‚Ü¶ Œº { x | Œµ ‚â§ edist (f i x) (g x) } by
    rw [tendsto_congr' this]
    exact h_tendsto Œµ hŒµ
  filter_upwards [h_left] with i h_ae_eq
  refine measure_congr ?_
  filter_upwards [h_ae_eq, h_right] with x hxf hxg
  rw [eq_iff_iff]
  change Œµ ‚â§ edist (f' i x) (g' x) ‚Üî Œµ ‚â§ edist (f i x) (g x)
  rw [hxg, hxf]

protected theorem congr (h_left : ‚àÄ i, f i =·µê[Œº] f' i) (h_right : g =·µê[Œº] g')
    (h_tendsto : TendstoInMeasure Œº f l g) : TendstoInMeasure Œº f' l g' :=
  TendstoInMeasure.congr' (Eventually.of_forall h_left) h_right h_tendsto

theorem congr_left (h : ‚àÄ i, f i =·µê[Œº] f' i) (h_tendsto : TendstoInMeasure Œº f l g) :
    TendstoInMeasure Œº f' l g :=
  h_tendsto.congr h EventuallyEq.rfl

theorem congr_right (h : g =·µê[Œº] g') (h_tendsto : TendstoInMeasure Œº f l g) :
    TendstoInMeasure Œº f l g' :=
  h_tendsto.congr (fun _ ‚Ü¶ EventuallyEq.rfl) h

end TendstoInMeasure

section ExistsSeqTendstoAe

variable [PseudoEMetricSpace E]
variable {f : ‚Ñï ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E}

/-- Auxiliary lemma for `tendstoInMeasure_of_tendsto_ae`. -/
theorem tendstoInMeasure_of_tendsto_ae_of_stronglyMeasurable [IsFiniteMeasure Œº]
    (hf : ‚àÄ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    (hfg : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n ‚Ü¶ f n x) atTop (ùìù (g x))) : TendstoInMeasure Œº f atTop g := by
  refine fun Œµ hŒµ ‚Ü¶ ENNReal.tendsto_atTop_zero.mpr fun Œ¥ hŒ¥ ‚Ü¶ ?_
  by_cases hŒ¥i : Œ¥ = ‚àû
  ¬∑ simp only [hŒ¥i, imp_true_iff, le_top, exists_const]
  lift Œ¥ to ‚Ñù‚â•0 using hŒ¥i
  rw [gt_iff_lt, ENNReal.coe_pos, ‚Üê NNReal.coe_pos] at hŒ¥
  obtain ‚ü®t, _, ht, hunif‚ü© := tendstoUniformlyOn_of_ae_tendsto' hf hg hfg hŒ¥
  rw [ENNReal.ofReal_coe_nnreal] at ht
  rw [EMetric.tendstoUniformlyOn_iff] at hunif
  obtain ‚ü®N, hN‚ü© := eventually_atTop.1 (hunif Œµ hŒµ)
  refine ‚ü®N, fun n hn ‚Ü¶ ?_‚ü©
  suffices { x : Œ± | Œµ ‚â§ edist (f n x) (g x) } ‚äÜ t from (measure_mono this).trans ht
  rw [‚Üê Set.compl_subset_compl]
  intro x hx
  rw [Set.mem_compl_iff, Set.notMem_setOf_iff, edist_comm, not_le]
  exact hN n hn x hx

/-- Convergence a.e. implies convergence in measure in a finite measure space. -/
theorem tendstoInMeasure_of_tendsto_ae [IsFiniteMeasure Œº] (hf : ‚àÄ n, AEStronglyMeasurable (f n) Œº)
    (hfg : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n ‚Ü¶ f n x) atTop (ùìù (g x))) : TendstoInMeasure Œº f atTop g := by
  have hg : AEStronglyMeasurable g Œº := aestronglyMeasurable_of_tendsto_ae _ hf hfg
  refine TendstoInMeasure.congr (fun i ‚Ü¶ (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm ?_
  refine tendstoInMeasure_of_tendsto_ae_of_stronglyMeasurable
    (fun i ‚Ü¶ (hf i).stronglyMeasurable_mk) hg.stronglyMeasurable_mk ?_
  have hf_eq_ae : ‚àÄ·µê x ‚àÇŒº, ‚àÄ n, (hf n).mk (f n) x = f n x :=
    ae_all_iff.mpr fun n ‚Ü¶ (hf n).ae_eq_mk.symm
  filter_upwards [hf_eq_ae, hg.ae_eq_mk, hfg] with x hxf hxg hxfg
  rw [‚Üê hxg, funext fun n ‚Ü¶ hxf n]
  exact hxfg

namespace ExistsSeqTendstoAe

theorem exists_nat_measure_lt_two_inv (hfg : TendstoInMeasure Œº f atTop g) (n : ‚Ñï) :
    ‚àÉ N, ‚àÄ m ‚â• N, Œº { x | (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ n ‚â§ edist (f m x) (g x) } ‚â§ (2‚Åª¬π : ‚Ñù‚â•0‚àû) ^ n := by
  specialize hfg ((2‚Åª¬π : ‚Ñù‚â•0‚àû) ^ n) (ENNReal.pow_pos (by simp) _)
  rw [ENNReal.tendsto_atTop_zero] at hfg
  exact hfg ((2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ n) (pos_iff_ne_zero.mpr fun h_zero ‚Ü¶ by simpa using pow_eq_zero h_zero)

/-- Given a sequence of functions `f` which converges in measure to `g`,
`seqTendstoAeSeqAux` is a sequence such that
`‚àÄ m ‚â• seqTendstoAeSeqAux n, Œº {x | 2‚Åª¬π ^ n ‚â§ dist (f m x) (g x)} ‚â§ 2‚Åª¬π ^ n`. -/
noncomputable def seqTendstoAeSeqAux (hfg : TendstoInMeasure Œº f atTop g) (n : ‚Ñï) :=
  Classical.choose (exists_nat_measure_lt_two_inv hfg n)

/-- Transformation of `seqTendstoAeSeqAux` to makes sure it is strictly monotone. -/
noncomputable def seqTendstoAeSeq (hfg : TendstoInMeasure Œº f atTop g) : ‚Ñï ‚Üí ‚Ñï
  | 0 => seqTendstoAeSeqAux hfg 0
  | n + 1 => max (seqTendstoAeSeqAux hfg (n + 1)) (seqTendstoAeSeq hfg n + 1)

theorem seqTendstoAeSeq_succ (hfg : TendstoInMeasure Œº f atTop g) {n : ‚Ñï} :
    seqTendstoAeSeq hfg (n + 1) =
      max (seqTendstoAeSeqAux hfg (n + 1)) (seqTendstoAeSeq hfg n + 1) := by
  rw [seqTendstoAeSeq]

theorem seqTendstoAeSeq_spec (hfg : TendstoInMeasure Œº f atTop g) (n k : ‚Ñï)
    (hn : seqTendstoAeSeq hfg n ‚â§ k) :
    Œº { x | (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ n ‚â§ edist (f k x) (g x) } ‚â§ (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ n := by
  cases n
  ¬∑ exact Classical.choose_spec (exists_nat_measure_lt_two_inv hfg 0) k hn
  ¬∑ exact Classical.choose_spec
      (exists_nat_measure_lt_two_inv hfg _) _ (le_trans (le_max_left _ _) hn)

theorem seqTendstoAeSeq_strictMono (hfg : TendstoInMeasure Œº f atTop g) :
    StrictMono (seqTendstoAeSeq hfg) := by
  refine strictMono_nat_of_lt_succ fun n ‚Ü¶ ?_
  rw [seqTendstoAeSeq_succ]
  exact lt_of_lt_of_le (lt_add_one <| seqTendstoAeSeq hfg n) (le_max_right _ _)

end ExistsSeqTendstoAe

/-- If `f` is a sequence of functions which converges in measure to `g`, then there exists a
subsequence of `f` which converges a.e. to `g`. -/
theorem TendstoInMeasure.exists_seq_tendsto_ae (hfg : TendstoInMeasure Œº f atTop g) :
    ‚àÉ ns : ‚Ñï ‚Üí ‚Ñï, StrictMono ns ‚àß ‚àÄ·µê x ‚àÇŒº, Tendsto (fun i ‚Ü¶ f (ns i) x) atTop (ùìù (g x)) := by
  /- Since `f` tends to `g` in measure, it has a subsequence `k ‚Ü¶ f (ns k)` such that
    `Œº {|f (ns k) - g| ‚â• 2‚Åª·µè} ‚â§ 2‚Åª·µè` for all `k`. Defining
    `s := ‚ãÇ k, ‚ãÉ i ‚â• k, {|f (ns k) - g| ‚â• 2‚Åª·µè}`, we see that `Œº s = 0` by the
    first Borel-Cantelli lemma.

    On the other hand, as `s` is precisely the set for which `f (ns k)`
    doesn't converge to `g`, `f (ns k)` converges almost everywhere to `g` as required. -/
  have h_lt_Œµ_real (Œµ : ‚Ñù‚â•0‚àû) (hŒµ : 0 < Œµ) : ‚àÉ k : ‚Ñï, 2 * (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ k < Œµ := by
    obtain ‚ü®k, h_k‚ü© : ‚àÉ k : ‚Ñï, (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ k < Œµ := ENNReal.exists_inv_two_pow_lt hŒµ.ne'
    refine ‚ü®k + 1, lt_of_eq_of_lt ?_ h_k‚ü©
    rw [pow_succ', ‚Üê mul_assoc, ENNReal.mul_inv_cancel, one_mul]
    ¬∑ positivity
    ¬∑ simp
  set ns := ExistsSeqTendstoAe.seqTendstoAeSeq hfg
  use ns
  let S := fun k ‚Ü¶ { x | (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ k ‚â§ edist (f (ns k) x) (g x) }
  have hŒºS_le : ‚àÄ k, Œº (S k) ‚â§ (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ k :=
    fun k ‚Ü¶ ExistsSeqTendstoAe.seqTendstoAeSeq_spec hfg k (ns k) le_rfl
  set s := Filter.atTop.limsup S with hs
  have hŒºs : Œº s = 0 := by
    refine measure_limsup_atTop_eq_zero (ne_top_of_le_ne_top ?_ (ENNReal.tsum_le_tsum hŒºS_le))
    simpa only [ENNReal.tsum_geometric, ENNReal.one_sub_inv_two, inv_inv] using ENNReal.ofNat_ne_top
  have h_tendsto : ‚àÄ x ‚àà s·∂ú, Tendsto (fun i ‚Ü¶ f (ns i) x) atTop (ùìù (g x)) := by
    refine fun x hx ‚Ü¶ EMetric.tendsto_atTop.mpr fun Œµ hŒµ ‚Ü¶ ?_
    rw [hs, limsup_eq_iInf_iSup_of_nat] at hx
    simp only [S, Set.iSup_eq_iUnion, Set.iInf_eq_iInter, Set.compl_iInter, Set.compl_iUnion,
      Set.mem_iUnion, Set.mem_iInter, Set.mem_compl_iff, Set.mem_setOf_eq, not_le] at hx
    obtain ‚ü®N, hNx‚ü© := hx
    obtain ‚ü®k, hk_lt_Œµ‚ü© := h_lt_Œµ_real Œµ hŒµ
    refine ‚ü®max N (k - 1), fun n hn_ge ‚Ü¶ lt_of_le_of_lt ?_ hk_lt_Œµ‚ü©
    specialize hNx n ((le_max_left _ _).trans hn_ge)
    have h_inv_n_le_k : (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ n ‚â§ 2 * (2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ k := by
      nth_rw 2 [‚Üê pow_one (2 : ‚Ñù‚â•0‚àû)]
      rw [mul_comm, ‚Üê ENNReal.inv_pow, ‚Üê ENNReal.inv_pow, ENNReal.inv_le_iff_le_mul, ‚Üê mul_assoc,
        mul_comm (_ ^ n), mul_assoc, ‚Üê ENNReal.inv_le_iff_le_mul, inv_inv, ‚Üê pow_add]
      ¬∑ gcongr
        ¬∑ norm_num
        ¬∑ omega
      all_goals simp
    exact le_trans hNx.le h_inv_n_le_k
  rw [ae_iff]
  refine ‚ü®ExistsSeqTendstoAe.seqTendstoAeSeq_strictMono hfg, measure_mono_null (fun x ‚Ü¶ ?_) hŒºs‚ü©
  rw [Set.mem_setOf_eq, ‚Üê @Classical.not_not (x ‚àà s), not_imp_not]
  exact h_tendsto x

theorem TendstoInMeasure.exists_seq_tendstoInMeasure_atTop {u : Filter Œπ} [NeBot u]
    [IsCountablyGenerated u] {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} (hfg : TendstoInMeasure Œº f u g) :
    ‚àÉ ns : ‚Ñï ‚Üí Œπ, Tendsto ns atTop u ‚àß TendstoInMeasure Œº (fun n ‚Ü¶ f (ns n)) atTop g := by
  obtain ‚ü®ns, h_tendsto_ns‚ü© : ‚àÉ ns : ‚Ñï ‚Üí Œπ, Tendsto ns atTop u := exists_seq_tendsto u
  exact ‚ü®ns, h_tendsto_ns, fun Œµ hŒµ ‚Ü¶ (hfg Œµ hŒµ).comp h_tendsto_ns‚ü©

theorem TendstoInMeasure.exists_seq_tendsto_ae' {u : Filter Œπ} [NeBot u] [IsCountablyGenerated u]
    {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} (hfg : TendstoInMeasure Œº f u g) :
    ‚àÉ ns : ‚Ñï ‚Üí Œπ, Tendsto ns atTop u ‚àß ‚àÄ·µê x ‚àÇŒº, Tendsto (fun i ‚Ü¶ f (ns i) x) atTop (ùìù (g x)) := by
  obtain ‚ü®ms, hms1, hms2‚ü© := hfg.exists_seq_tendstoInMeasure_atTop
  obtain ‚ü®ns, hns1, hns2‚ü© := hms2.exists_seq_tendsto_ae
  exact ‚ü®ms ‚àò ns, hms1.comp hns1.tendsto_atTop, hns2‚ü©

/-- `TendstoInMeasure` is equivalent to every subsequence having another subsequence
 which converges almost surely. -/
theorem exists_seq_tendstoInMeasure_atTop_iff [IsFiniteMeasure Œº]
    {f : ‚Ñï ‚Üí Œ± ‚Üí E} (hf : ‚àÄ (n : ‚Ñï), AEStronglyMeasurable (f n) Œº) {g : Œ± ‚Üí E} :
    TendstoInMeasure Œº f atTop g ‚Üî
      ‚àÄ ns : ‚Ñï ‚Üí ‚Ñï, StrictMono ns ‚Üí ‚àÉ ns' : ‚Ñï ‚Üí ‚Ñï, StrictMono ns' ‚àß
        ‚àÄ·µê (œâ : Œ±) ‚àÇŒº, Tendsto (fun i ‚Ü¶ f (ns (ns' i)) œâ) atTop (ùìù (g œâ)) := by
  refine ‚ü®fun hfg _ hns ‚Ü¶ (hfg.comp hns.tendsto_atTop).exists_seq_tendsto_ae,
    not_imp_not.mp (fun h1 ‚Ü¶ ?_)‚ü©
  rw [tendstoInMeasure_iff_tendsto_toNNReal] at h1
  push_neg at *
  obtain ‚ü®Œµ, hŒµ, h2‚ü© := h1
  obtain ‚ü®Œ¥, ns, hŒ¥, hns, h3‚ü© : ‚àÉ (Œ¥ : ‚Ñù‚â•0) (ns : ‚Ñï ‚Üí ‚Ñï), 0 < Œ¥ ‚àß StrictMono ns ‚àß
      ‚àÄ n, Œ¥ ‚â§ (Œº {x | Œµ ‚â§ edist (f (ns n) x) (g x)}).toNNReal := by
    obtain ‚ü®s, hs, h4‚ü© := not_tendsto_iff_exists_frequently_notMem.1 h2
    obtain ‚ü®Œ¥, hŒ¥, h5‚ü© := NNReal.nhds_zero_basis.mem_iff.1 hs
    obtain ‚ü®ns, hns, h6‚ü© := extraction_of_frequently_atTop h4
    exact ‚ü®Œ¥, ns, hŒ¥, hns, fun n ‚Ü¶ Set.notMem_Iio.1 (Set.notMem_subset h5 (h6 n))‚ü©
  refine ‚ü®ns, hns, fun ns' _ ‚Ü¶ ?_‚ü©
  by_contra h6
  have h7 := tendstoInMeasure_iff_tendsto_toNNReal.mp <|
    tendstoInMeasure_of_tendsto_ae (fun n ‚Ü¶ hf _) h6
  exact lt_irrefl _ (lt_of_le_of_lt (ge_of_tendsto' (h7 Œµ hŒµ) (fun n ‚Ü¶ h3 _)) hŒ¥)

end ExistsSeqTendstoAe

section TendstoInMeasureUnique

/-- The limit in measure is ae unique. -/
theorem tendstoInMeasure_ae_unique [EMetricSpace E] {g h : Œ± ‚Üí E} {f : Œπ ‚Üí Œ± ‚Üí E} {u : Filter Œπ}
    [NeBot u] [IsCountablyGenerated u] (hg : TendstoInMeasure Œº f u g)
    (hh : TendstoInMeasure Œº f u h) : g =·µê[Œº] h := by
  obtain ‚ü®ns, h1, h1'‚ü© := hg.exists_seq_tendsto_ae'
  obtain ‚ü®ns', h2, h2'‚ü© := (hh.comp h1).exists_seq_tendsto_ae'
  filter_upwards [h1', h2'] with œâ hg1 hh1
  exact tendsto_nhds_unique (hg1.comp h2) hh1

end TendstoInMeasureUnique

section AEMeasurableOf

variable [MeasurableSpace E] [NormedAddCommGroup E] [BorelSpace E]

theorem TendstoInMeasure.aemeasurable {u : Filter Œπ} [NeBot u] [IsCountablyGenerated u]
    {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} (hf : ‚àÄ n, AEMeasurable (f n) Œº)
    (h_tendsto : TendstoInMeasure Œº f u g) : AEMeasurable g Œº := by
  obtain ‚ü®ns, -, hns‚ü© := h_tendsto.exists_seq_tendsto_ae'
  exact aemeasurable_of_tendsto_metrizable_ae atTop (fun n ‚Ü¶ hf (ns n)) hns

end AEMeasurableOf

section TendstoInMeasureOf

variable [NormedAddCommGroup E] {p : ‚Ñù‚â•0‚àû}
variable {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E}

/-- This lemma is superseded by `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm` where we
allow `p = ‚àû` and only require `AEStronglyMeasurable`. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm_of_stronglyMeasurable (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) (hf : ‚àÄ n, StronglyMeasurable (f n)) (hg : StronglyMeasurable g)
    {l : Filter Œπ} (hfg : Tendsto (fun n ‚Ü¶ eLpNorm (f n - g) p Œº) l (ùìù 0)) :
    TendstoInMeasure Œº f l g := by
  refine tendstoInMeasure_of_ne_top fun Œµ hŒµ hŒµ_top ‚Ü¶ ?_
  replace hfg := ENNReal.Tendsto.const_mul (a := 1 / Œµ ^ p.toReal)
    (Tendsto.ennrpow_const p.toReal hfg) (Or.inr <| by simp [hŒµ.ne'])
  simp only [mul_zero,
    ENNReal.zero_rpow_of_pos (ENNReal.toReal_pos hp_ne_zero hp_ne_top)] at hfg
  rw [ENNReal.tendsto_nhds_zero] at hfg ‚ä¢
  intro Œ¥ hŒ¥
  refine (hfg Œ¥ hŒ¥).mono fun n hn ‚Ü¶ ?_
  refine le_trans ?_ hn
  rw [one_div, ‚Üê ENNReal.inv_mul_le_iff, inv_inv]
  ¬∑ convert mul_meas_ge_le_pow_eLpNorm' Œº hp_ne_zero hp_ne_top
      ((hf n).sub hg).aestronglyMeasurable Œµ using 6
    simp [edist_eq_enorm_sub]
  ¬∑ simp [hŒµ_top]
  ¬∑ simp [hŒµ.ne']

/-- This lemma is superseded by `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm` where we
allow `p = ‚àû`. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm_of_ne_top (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû)
    (hf : ‚àÄ n, AEStronglyMeasurable (f n) Œº) (hg : AEStronglyMeasurable g Œº) {l : Filter Œπ}
    (hfg : Tendsto (fun n ‚Ü¶ eLpNorm (f n - g) p Œº) l (ùìù 0)) : TendstoInMeasure Œº f l g := by
  refine TendstoInMeasure.congr (fun i ‚Ü¶ (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm ?_
  refine tendstoInMeasure_of_tendsto_eLpNorm_of_stronglyMeasurable
    hp_ne_zero hp_ne_top (fun i ‚Ü¶ (hf i).stronglyMeasurable_mk) hg.stronglyMeasurable_mk ?_
  have : (fun n ‚Ü¶ eLpNorm ((hf n).mk (f n) - hg.mk g) p Œº) = fun n ‚Ü¶ eLpNorm (f n - g) p Œº := by
    ext1 n; refine eLpNorm_congr_ae (EventuallyEq.sub (hf n).ae_eq_mk.symm hg.ae_eq_mk.symm)
  rw [this]
  exact hfg

/-- See also `MeasureTheory.tendstoInMeasure_of_tendsto_eLpNorm` which work for general
Lp-convergence for all `p ‚â† 0`. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm_top {E} [NormedAddCommGroup E] {f : Œπ ‚Üí Œ± ‚Üí E}
    {g : Œ± ‚Üí E} {l : Filter Œπ} (hfg : Tendsto (fun n ‚Ü¶ eLpNorm (f n - g) ‚àû Œº) l (ùìù 0)) :
    TendstoInMeasure Œº f l g := by
  refine tendstoInMeasure_of_ne_top fun Œ¥ hŒ¥ hŒ¥_top ‚Ü¶ ?_
  simp only [eLpNorm_exponent_top, eLpNormEssSup] at hfg
  rw [ENNReal.tendsto_nhds_zero] at hfg ‚ä¢
  intro Œµ hŒµ
  specialize hfg (Œ¥ / 2) (ENNReal.div_pos_iff.2 ‚ü®hŒ¥.ne', ENNReal.ofNat_ne_top‚ü©)
  refine hfg.mono fun n hn ‚Ü¶ ?_
  simp only [Pi.sub_apply] at *
  have : essSup (fun x : Œ± ‚Ü¶ ‚Äñf n x - g x‚Äñ‚Çë) Œº < Œ¥ :=
    hn.trans_lt (ENNReal.half_lt_self hŒ¥.ne' hŒ¥_top)
  refine ((le_of_eq ?_).trans (ae_lt_of_essSup_lt this).le).trans hŒµ.le
  congr with x
  simp [edist_eq_enorm_sub]

/-- Convergence in Lp implies convergence in measure. -/
theorem tendstoInMeasure_of_tendsto_eLpNorm {l : Filter Œπ} (hp_ne_zero : p ‚â† 0)
    (hf : ‚àÄ n, AEStronglyMeasurable (f n) Œº) (hg : AEStronglyMeasurable g Œº)
    (hfg : Tendsto (fun n ‚Ü¶ eLpNorm (f n - g) p Œº) l (ùìù 0)) : TendstoInMeasure Œº f l g := by
  by_cases hp_ne_top : p = ‚àû
  ¬∑ subst hp_ne_top
    exact tendstoInMeasure_of_tendsto_eLpNorm_top hfg
  ¬∑ exact tendstoInMeasure_of_tendsto_eLpNorm_of_ne_top hp_ne_zero hp_ne_top hf hg hfg

/-- Convergence in Lp implies convergence in measure. -/
theorem tendstoInMeasure_of_tendsto_Lp [hp : Fact (1 ‚â§ p)] {f : Œπ ‚Üí Lp E p Œº} {g : Lp E p Œº}
    {l : Filter Œπ} (hfg : Tendsto f l (ùìù g)) : TendstoInMeasure Œº (fun n ‚Ü¶ f n) l g :=
  tendstoInMeasure_of_tendsto_eLpNorm (zero_lt_one.trans_le hp.elim).ne.symm
    (fun _ ‚Ü¶ Lp.aestronglyMeasurable _) (Lp.aestronglyMeasurable _)
    ((Lp.tendsto_Lp_iff_tendsto_eLpNorm' _ _).mp hfg)

end TendstoInMeasureOf

end MeasureTheory
