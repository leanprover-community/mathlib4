/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.CondexpL1

/-! # Conditional expectation

We build the conditional expectation of an integrable function `f` with value in a Banach space
with respect to a measure `Œº` (defined on a measurable space structure `m‚ÇÄ`) and a measurable space
structure `m` with `hm : m ‚â§ m‚ÇÄ` (a sub-sigma-algebra). This is an `m`-strongly measurable
function `Œº[f|hm]` which is integrable and verifies `‚à´ x in s, Œº[f|hm] x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº`
for all `m`-measurable sets `s`. It is unique as an element of `L¬π`.

The construction is done in four steps:
* Define the conditional expectation of an `L¬≤` function, as an element of `L¬≤`. This is the
  orthogonal projection on the subspace of almost everywhere `m`-measurable functions.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `Set Œ± ‚Üí (E ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E))` which to a set associates a linear
  map. That linear map sends `x ‚àà E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condExpL1CLM : (Œ± ‚Üí‚ÇÅ[Œº] E) ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E)`. This is done using the same
  construction as the Bochner integral (see the file `MeasureTheory/Integral/SetToL1`).
* Define the conditional expectation of a function `f : Œ± ‚Üí E`, which is an integrable function
  `Œ± ‚Üí E` equal to 0 if `f` is not integrable, and equal to an `m`-measurable representative of
  `condExpL1CLM` applied to `[f]`, the equivalence class of `f` in `L¬π`.

The first step is done in `MeasureTheory.Function.ConditionalExpectation.CondexpL2`, the two
next steps in `MeasureTheory.Function.ConditionalExpectation.CondexpL1` and the final step is
performed in this file.

## Main results

The conditional expectation and its properties

* `condExp (m : MeasurableSpace Œ±) (Œº : Measure Œ±) (f : Œ± ‚Üí E)`: conditional expectation of `f`
  with respect to `m`.
* `integrable_condExp` : `condExp` is integrable.
* `stronglyMeasurable_condExp` : `condExp` is `m`-strongly-measurable.
* `setIntegral_condExp (hf : Integrable f Œº) (hs : MeasurableSet[m] s)` : if `m ‚â§ m‚ÇÄ` (the
  œÉ-algebra over which the measure is defined), then the conditional expectation verifies
  `‚à´ x in s, condExp m Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº` for any `m`-measurable set `s`.

While `condExp` is function-valued, we also define `condExpL1` with value in `L1` and a continuous
linear map `condExpL1CLM` from `L1` to `L1`. `condExp` should be used in most cases.

Uniqueness of the conditional expectation

* `ae_eq_condExp_of_forall_setIntegral_eq`: an a.e. `m`-measurable function which verifies the
  equality of integrals is a.e. equal to `condExp`.

## Notations

For a measure `Œº` defined on a measurable space structure `m‚ÇÄ`, another measurable space structure
`m` with `hm : m ‚â§ m‚ÇÄ` (a sub-œÉ-algebra) and a function `f`, we define the notation
* `Œº[f|m] = condExp m Œº f`.

## TODO

See https://leanprover.zulipchat.com/#narrow/channel/217875-Is-there-code-for-X.3F/topic/Conditional.20expectation.20of.20product
for how to prove that we can pull `m`-measurable continuous linear maps out of the `m`-conditional
expectation. This would generalise `MeasureTheory.condExp_mul_of_stronglyMeasurable_left`.

## Tags

conditional expectation, conditional expected value

-/

open TopologicalSpace MeasureTheory.Lp Filter
open scoped ENNReal Topology MeasureTheory

namespace MeasureTheory
  -- ùïú for ‚Ñù or ‚ÑÇ
  -- E for integrals on a Lp submodule
variable {Œ± Œ≤ E ùïú : Type*} [RCLike ùïú] {m m‚ÇÄ : MeasurableSpace Œ±} {Œº : Measure Œ±} {f g : Œ± ‚Üí E}
  {s : Set Œ±}

section NormedAddCommGroup
variable [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]

open scoped Classical in
variable (m) in
/-- Conditional expectation of a function, with notation `Œº[f|m]`.

It is defined as 0 if any one of the following conditions is true:
- `m` is not a sub-œÉ-algebra of `m‚ÇÄ`,
- `Œº` is not œÉ-finite with respect to `m`,
- `f` is not integrable. -/
noncomputable irreducible_def condExp (Œº : Measure[m‚ÇÄ] Œ±) (f : Œ± ‚Üí E) : Œ± ‚Üí E :=
  if hm : m ‚â§ m‚ÇÄ then
    if h : SigmaFinite (Œº.trim hm) ‚àß Integrable f Œº then
      if StronglyMeasurable[m] f then f
      else have := h.1; aestronglyMeasurable_condExpL1.mk (condExpL1 hm Œº f)
    else 0
  else 0

@[inherit_doc MeasureTheory.condExp]
scoped macro:max Œº:term noWs "[" f:term "|" m:term "]" : term =>
  `(MeasureTheory.condExp $m $Œº $f)

/-- Unexpander for `Œº[f|m]` notation. -/
@[app_unexpander MeasureTheory.condExp]
def condExpUnexpander : Lean.PrettyPrinter.Unexpander
  | `($_ $m $Œº $f) => `($Œº[$f|$m])
  | _ => throw ()

/-- info: Œº[f|m] : Œ± ‚Üí E -/
#guard_msgs in
#check Œº[f|m]
/-- info: Œº[f|m] sorry : E -/
#guard_msgs in
#check Œº[f|m] (sorry : Œ±)

theorem condExp_of_not_le (hm_not : ¬¨m ‚â§ m‚ÇÄ) : Œº[f|m] = 0 := by rw [condExp, dif_neg hm_not]

theorem condExp_of_not_sigmaFinite (hm : m ‚â§ m‚ÇÄ) (hŒºm_not : ¬¨SigmaFinite (Œº.trim hm)) :
    Œº[f|m] = 0 := by rw [condExp, dif_pos hm, dif_neg]; push_neg; exact fun h => absurd h hŒºm_not

open scoped Classical in
theorem condExp_of_sigmaFinite (hm : m ‚â§ m‚ÇÄ) [hŒºm : SigmaFinite (Œº.trim hm)] :
    Œº[f|m] =
      if Integrable f Œº then
        if StronglyMeasurable[m] f then f
        else aestronglyMeasurable_condExpL1.mk (condExpL1 hm Œº f)
      else 0 := by
  rw [condExp, dif_pos hm]
  grind

theorem condExp_of_stronglyMeasurable (hm : m ‚â§ m‚ÇÄ) [hŒºm : SigmaFinite (Œº.trim hm)] {f : Œ± ‚Üí E}
    (hf : StronglyMeasurable[m] f) (hfi : Integrable f Œº) : Œº[f|m] = f := by
  rw [condExp_of_sigmaFinite hm, if_pos hfi, if_pos hf]

@[simp]
theorem condExp_const (hm : m ‚â§ m‚ÇÄ) (c : E) [IsFiniteMeasure Œº] : Œº[fun _ : Œ± ‚Ü¶ c|m] = fun _ ‚Ü¶ c :=
  condExp_of_stronglyMeasurable hm stronglyMeasurable_const (integrable_const c)

theorem condExp_ae_eq_condExpL1 (hm : m ‚â§ m‚ÇÄ) [hŒºm : SigmaFinite (Œº.trim hm)] (f : Œ± ‚Üí E) :
    Œº[f|m] =·µê[Œº] condExpL1 hm Œº f := by
  rw [condExp_of_sigmaFinite hm]
  by_cases hfi : Integrable f Œº
  ¬∑ rw [if_pos hfi]
    by_cases hfm : StronglyMeasurable[m] f
    ¬∑ rw [if_pos hfm]
      exact (condExpL1_of_aestronglyMeasurable' hfm.aestronglyMeasurable hfi).symm
    ¬∑ rw [if_neg hfm]
      exact aestronglyMeasurable_condExpL1.ae_eq_mk.symm
  rw [if_neg hfi, condExpL1_undef hfi]
  exact (coeFn_zero _ _ _).symm

theorem condExp_ae_eq_condExpL1CLM (hm : m ‚â§ m‚ÇÄ) [SigmaFinite (Œº.trim hm)] (hf : Integrable f Œº) :
    Œº[f|m] =·µê[Œº] condExpL1CLM E hm Œº (hf.toL1 f) := by
  refine (condExp_ae_eq_condExpL1 hm f).trans (Eventually.of_forall fun x => ?_)
  rw [condExpL1_eq hf]

theorem condExp_of_not_integrable (hf : ¬¨Integrable f Œº) : Œº[f|m] = 0 := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ rw [condExp_of_not_le hm]
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ rw [condExp_of_not_sigmaFinite hm hŒºm]
  rw [condExp_of_sigmaFinite, if_neg hf]

@[simp]
theorem condExp_zero : Œº[(0 : Œ± ‚Üí E)|m] = 0 := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ rw [condExp_of_not_le hm]
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ rw [condExp_of_not_sigmaFinite hm hŒºm]
  exact condExp_of_stronglyMeasurable hm stronglyMeasurable_zero (integrable_zero _ _ _)

theorem stronglyMeasurable_condExp : StronglyMeasurable[m] (Œº[f|m]) := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ rw [condExp_of_not_le hm]; exact stronglyMeasurable_zero
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ rw [condExp_of_not_sigmaFinite hm hŒºm]; exact stronglyMeasurable_zero
  rw [condExp_of_sigmaFinite hm]
  split_ifs with hfi hfm
  ¬∑ exact hfm
  ¬∑ exact aestronglyMeasurable_condExpL1.stronglyMeasurable_mk
  ¬∑ exact stronglyMeasurable_zero

theorem condExp_congr_ae (h : f =·µê[Œº] g) : Œº[f|m] =·µê[Œº] Œº[g|m] := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ simp_rw [condExp_of_not_le hm]; rfl
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm]; rfl
  exact (condExp_ae_eq_condExpL1 hm f).trans
    (Filter.EventuallyEq.trans (by rw [condExpL1_congr_ae hm h])
      (condExp_ae_eq_condExpL1 hm g).symm)

lemma condExp_congr_ae_trim (hm : m ‚â§ m‚ÇÄ) (hfg : f =·µê[Œº] g) :
    Œº[f|m] =·µê[Œº.trim hm] Œº[g|m] :=
  StronglyMeasurable.ae_eq_trim_of_stronglyMeasurable hm
    stronglyMeasurable_condExp stronglyMeasurable_condExp (condExp_congr_ae hfg)

theorem condExp_of_aestronglyMeasurable' (hm : m ‚â§ m‚ÇÄ) [hŒºm : SigmaFinite (Œº.trim hm)] {f : Œ± ‚Üí E}
    (hf : AEStronglyMeasurable[m] f Œº) (hfi : Integrable f Œº) : Œº[f|m] =·µê[Œº] f := by
  refine ((condExp_congr_ae hf.ae_eq_mk).trans ?_).trans hf.ae_eq_mk.symm
  rw [condExp_of_stronglyMeasurable hm hf.stronglyMeasurable_mk
    ((integrable_congr hf.ae_eq_mk).mp hfi)]

@[fun_prop]
theorem integrable_condExp : Integrable (Œº[f|m]) Œº := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ rw [condExp_of_not_le hm]; exact integrable_zero _ _ _
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ rw [condExp_of_not_sigmaFinite hm hŒºm]; exact integrable_zero _ _ _
  exact (integrable_condExpL1 f).congr (condExp_ae_eq_condExpL1 hm f).symm

/-- The integral of the conditional expectation `Œº[f|hm]` over an `m`-measurable set is equal to
the integral of `f` on that set. -/
theorem setIntegral_condExp (hm : m ‚â§ m‚ÇÄ) [SigmaFinite (Œº.trim hm)] (hf : Integrable f Œº)
    (hs : MeasurableSet[m] s) : ‚à´ x in s, (Œº[f|m]) x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  rw [setIntegral_congr_ae (hm s hs) ((condExp_ae_eq_condExpL1 hm f).mono fun x hx _ => hx)]
  exact setIntegral_condExpL1 hf hs

theorem integral_condExp (hm : m ‚â§ m‚ÇÄ) [hŒºm : SigmaFinite (Œº.trim hm)] :
    ‚à´ x, (Œº[f|m]) x ‚àÇŒº = ‚à´ x, f x ‚àÇŒº := by
  by_cases hf : Integrable f Œº
  ¬∑ suffices ‚à´ x in Set.univ, (Œº[f|m]) x ‚àÇŒº = ‚à´ x in Set.univ, f x ‚àÇŒº by
      simp_rw [setIntegral_univ] at this; exact this
    exact setIntegral_condExp hm hf .univ
  simp only [condExp_of_not_integrable hf, Pi.zero_apply, integral_zero, integral_undef hf]

/-- **Law of total probability** using `condExp` as conditional probability. -/
theorem integral_condExp_indicator [mŒ≤ : MeasurableSpace Œ≤] {Y : Œ± ‚Üí Œ≤} (hY : Measurable Y)
    [SigmaFinite (Œº.trim hY.comap_le)] {A : Set Œ±} (hA : MeasurableSet A) :
    ‚à´ x, (Œº[(A.indicator fun _ ‚Ü¶ (1 : ‚Ñù))|mŒ≤.comap Y]) x ‚àÇŒº = Œº.real A := by
  rw [integral_condExp, integral_indicator hA, setIntegral_const, smul_eq_mul, mul_one]

/-- **Uniqueness of the conditional expectation**
If a function is a.e. `m`-measurable, verifies an integrability condition and has same integral
as `f` on all `m`-measurable sets, then it is a.e. equal to `Œº[f|hm]`. -/
theorem ae_eq_condExp_of_forall_setIntegral_eq (hm : m ‚â§ m‚ÇÄ) [SigmaFinite (Œº.trim hm)]
    {f g : Œ± ‚Üí E} (hf : Integrable f Œº)
    (hg_int_finite : ‚àÄ s, MeasurableSet[m] s ‚Üí Œº s < ‚àû ‚Üí IntegrableOn g s Œº)
    (hg_eq : ‚àÄ s : Set Œ±, MeasurableSet[m] s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, g x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº)
    (hgm : AEStronglyMeasurable[m] g Œº) : g =·µê[Œº] Œº[f|m] := by
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hm hg_int_finite
    (fun s _ _ => integrable_condExp.integrableOn) (fun s hs hŒºs => ?_) hgm
    (StronglyMeasurable.aestronglyMeasurable stronglyMeasurable_condExp)
  rw [hg_eq s hs hŒºs, setIntegral_condExp hm hf hs]

theorem condExp_bot' [hŒº : NeZero Œº] (f : Œ± ‚Üí E) :
    Œº[f|‚ä•] = fun _ => (Œº.real Set.univ)‚Åª¬π ‚Ä¢ ‚à´ x, f x ‚àÇŒº := by
  by_cases hŒº_finite : IsFiniteMeasure Œº
  swap
  ¬∑ have h : ¬¨SigmaFinite (Œº.trim bot_le) := by rwa [sigmaFinite_trim_bot_iff]
    rw [not_isFiniteMeasure_iff] at hŒº_finite
    rw [condExp_of_not_sigmaFinite bot_le h]
    simp only [hŒº_finite, ENNReal.toReal_top, inv_zero, zero_smul, measureReal_def]
    rfl
  have h_meas : StronglyMeasurable[‚ä•] (Œº[f|‚ä•]) := stronglyMeasurable_condExp
  obtain ‚ü®c, h_eq‚ü© := stronglyMeasurable_bot_iff.mp h_meas
  rw [h_eq]
  have h_integral : ‚à´ x, (Œº[f|‚ä•]) x ‚àÇŒº = ‚à´ x, f x ‚àÇŒº := integral_condExp bot_le
  simp_rw [h_eq, integral_const] at h_integral
  rw [‚Üê h_integral, ‚Üê smul_assoc, smul_eq_mul, inv_mul_cancel‚ÇÄ, one_smul]
  rw [Ne, measureReal_def, ENNReal.toReal_eq_zero_iff, not_or]
  exact ‚ü®NeZero.ne _, measure_ne_top Œº Set.univ‚ü©

theorem condExp_bot_ae_eq (f : Œ± ‚Üí E) :
    Œº[f|‚ä•] =·µê[Œº] fun _ => (Œº.real Set.univ)‚Åª¬π ‚Ä¢ ‚à´ x, f x ‚àÇŒº := by
  rcases eq_zero_or_neZero Œº with rfl | hŒº
  ¬∑ rw [ae_zero]; exact eventually_bot
  ¬∑ exact Eventually.of_forall <| congr_fun (condExp_bot' f)

theorem condExp_bot [IsProbabilityMeasure Œº] (f : Œ± ‚Üí E) : Œº[f|‚ä•] = fun _ => ‚à´ x, f x ‚àÇŒº := by
  refine (condExp_bot' f).trans ?_
  rw [measureReal_univ_eq_one, inv_one, one_smul]

theorem condExp_add (hf : Integrable f Œº) (hg : Integrable g Œº) (m : MeasurableSpace Œ±) :
    Œº[f + g|m] =·µê[Œº] Œº[f|m] + Œº[g|m] := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ simp_rw [condExp_of_not_le hm]; simp
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm]; simp
  refine (condExp_ae_eq_condExpL1 hm _).trans ?_
  rw [condExpL1_add hf hg]
  exact (coeFn_add _ _).trans
    ((condExp_ae_eq_condExpL1 hm _).symm.add (condExp_ae_eq_condExpL1 hm _).symm)

theorem condExp_finset_sum {Œπ : Type*} {s : Finset Œπ} {f : Œπ ‚Üí Œ± ‚Üí E}
    (hf : ‚àÄ i ‚àà s, Integrable (f i) Œº) (m : MeasurableSpace Œ±) :
    Œº[‚àë i ‚àà s, f i|m] =·µê[Œº] ‚àë i ‚àà s, Œº[f i|m] := by
  classical
  induction s using Finset.induction_on with
  | empty => rw [Finset.sum_empty, Finset.sum_empty, condExp_zero]
  | insert i s his heq =>
    rw [Finset.sum_insert his, Finset.sum_insert his]
    exact (condExp_add (hf i <| Finset.mem_insert_self i s)
      (integrable_finset_sum' _ <| Finset.forall_of_forall_insert hf) _).trans
        ((EventuallyEq.refl _ _).add <| heq <| Finset.forall_of_forall_insert hf)

theorem condExp_smul [NormedSpace ùïú E] (c : ùïú) (f : Œ± ‚Üí E) (m : MeasurableSpace Œ±) :
    Œº[c ‚Ä¢ f|m] =·µê[Œº] c ‚Ä¢ Œº[f|m] := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ simp_rw [condExp_of_not_le hm]; simp
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm]; simp
  refine (condExp_ae_eq_condExpL1 hm _).trans ?_
  rw [condExpL1_smul c f]
  refine (condExp_ae_eq_condExpL1 hm f).mp ?_
  refine (coeFn_smul c (condExpL1 hm Œº f)).mono fun x hx1 hx2 => ?_
  simp only [hx1, hx2, Pi.smul_apply]

theorem condExp_neg (f : Œ± ‚Üí E) (m : MeasurableSpace Œ±) : Œº[-f|m] =·µê[Œº] -Œº[f|m] := by
  calc
    Œº[-f|m] = Œº[(-1 : ‚Ñù) ‚Ä¢ f|m] := by rw [neg_one_smul ‚Ñù f]
    _ =·µê[Œº] (-1 : ‚Ñù) ‚Ä¢ Œº[f|m] := condExp_smul ..
    _ = -Œº[f|m] := neg_one_smul ‚Ñù (Œº[f|m])

theorem condExp_sub (hf : Integrable f Œº) (hg : Integrable g Œº) (m : MeasurableSpace Œ±) :
    Œº[f - g|m] =·µê[Œº] Œº[f|m] - Œº[g|m] := by
  simp_rw [sub_eq_add_neg]
  exact (condExp_add hf hg.neg _).trans (EventuallyEq.rfl.add (condExp_neg ..))

/-- **Tower property of the conditional expectation**.

Taking the `m‚ÇÇ`-conditional expectation then the `m‚ÇÅ`-conditional expectation, where `m‚ÇÅ` is a
smaller œÉ-algebra, is the same as taking the `m‚ÇÅ`-conditional expectation directly. -/
theorem condExp_condExp_of_le {m‚ÇÅ m‚ÇÇ m‚ÇÄ : MeasurableSpace Œ±} {Œº : Measure Œ±} (hm‚ÇÅ‚ÇÇ : m‚ÇÅ ‚â§ m‚ÇÇ)
    (hm‚ÇÇ : m‚ÇÇ ‚â§ m‚ÇÄ) [SigmaFinite (Œº.trim hm‚ÇÇ)] : Œº[Œº[f|m‚ÇÇ]|m‚ÇÅ] =·µê[Œº] Œº[f|m‚ÇÅ] := by
  by_cases hŒºm‚ÇÅ : SigmaFinite (Œº.trim (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ))
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ) hŒºm‚ÇÅ]; rfl
  by_cases hf : Integrable f Œº
  swap; ¬∑ simp_rw [condExp_of_not_integrable hf, condExp_zero]; rfl
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ)
    (fun s _ _ => integrable_condExp.integrableOn) (fun s _ _ => integrable_condExp.integrableOn) ?_
    stronglyMeasurable_condExp.aestronglyMeasurable
    stronglyMeasurable_condExp.aestronglyMeasurable
  intro s hs _
  rw [setIntegral_condExp (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ) integrable_condExp hs]
  rw [setIntegral_condExp (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ) hf hs, setIntegral_condExp hm‚ÇÇ hf (hm‚ÇÅ‚ÇÇ s hs)]

section RCLike
variable [InnerProductSpace ùïú E]

lemma MemLp.condExpL2_ae_eq_condExp' (hm : m ‚â§ m‚ÇÄ) (hf1 : Integrable f Œº) (hf2 : MemLp f 2 Œº)
    [SigmaFinite (Œº.trim hm)] : condExpL2 E ùïú hm hf2.toLp =·µê[Œº] Œº[f|m] := by
  refine ae_eq_condExp_of_forall_setIntegral_eq hm hf1
    (fun s hs htop ‚Ü¶ integrableOn_condExpL2_of_measure_ne_top hm htop.ne _) (fun s hs htop ‚Ü¶ ?_)
    (aestronglyMeasurable_condExpL2 hm _)
  rw [integral_condExpL2_eq hm (hf2.toLp _) hs htop.ne]
  refine setIntegral_congr_ae (hm _ hs) ?_
  filter_upwards [hf2.coeFn_toLp] with œâ hœâ _ using hœâ

@[deprecated (since := "2025-02-21")]
alias Mem‚Ñíp.condExpL2_ae_eq_condExp' := MemLp.condExpL2_ae_eq_condExp'

lemma MemLp.condExpL2_ae_eq_condExp (hm : m ‚â§ m‚ÇÄ) (hf : MemLp f 2 Œº) [IsFiniteMeasure Œº] :
    condExpL2 E ùïú hm hf.toLp =·µê[Œº] Œº[f|m] :=
  hf.condExpL2_ae_eq_condExp' hm (memLp_one_iff_integrable.1 <| hf.mono_exponent one_le_two)

@[deprecated (since := "2025-02-21")]
alias Mem‚Ñíp.condExpL2_ae_eq_condExp := MemLp.condExpL2_ae_eq_condExp

end RCLike

section Real
variable [InnerProductSpace ‚Ñù E]

-- TODO: Generalize via the conditional Jensen inequality
lemma eLpNorm_condExp_le : eLpNorm (Œº[f|m]) 2 Œº ‚â§ eLpNorm f 2 Œº := by
  by_cases hm : m ‚â§ m‚ÇÄ; swap
  ¬∑ simp [condExp_of_not_le hm]
  by_cases hfŒº : SigmaFinite (Œº.trim hm); swap
  ¬∑ rw [condExp_of_not_sigmaFinite hm hfŒº]
    simp
  by_cases hfi : Integrable f Œº; swap
  ¬∑ rw [condExp_of_not_integrable hfi]
    simp
  obtain hf | hf := eq_or_ne (eLpNorm f 2 Œº) ‚àû
  ¬∑ simp [hf]
  replace hf : MemLp f 2 Œº := ‚ü®hfi.1, Ne.lt_top' fun a ‚Ü¶ hf a.symm‚ü©
  rw [‚Üê eLpNorm_congr_ae (hf.condExpL2_ae_eq_condExp' (ùïú := ‚Ñù) hm hfi)]
  refine le_trans (eLpNorm_condExpL2_le hm _) ?_
  rw [eLpNorm_congr_ae hf.coeFn_toLp]

protected lemma MemLp.condExp (hf : MemLp f 2 Œº) : MemLp (Œº[f|m]) 2 Œº := by
  by_cases hm : m ‚â§ m‚ÇÄ
  ¬∑ exact ‚ü®(stronglyMeasurable_condExp.mono hm).aestronglyMeasurable,
      eLpNorm_condExp_le.trans_lt hf.eLpNorm_lt_top‚ü©
  ¬∑ simp [condExp_of_not_le hm]

@[deprecated (since := "2025-02-21")]
alias Mem‚Ñíp.condExp := MemLp.condExp

end Real
end NormedAddCommGroup

section NormedRing
variable {R : Type*} [NormedRing R] [NormedSpace ‚Ñù R] [CompleteSpace R]

@[simp]
lemma condExp_ofNat (n : ‚Ñï) [n.AtLeastTwo] (f : Œ± ‚Üí R) :
    Œº[ofNat(n) * f|m] =·µê[Œº] ofNat(n) * Œº[f|m] := by
  simpa [Nat.cast_smul_eq_nsmul] using condExp_smul (Œº := Œº) (m := m) (n : ‚Ñù) f

end NormedRing

section NormedLatticeAddCommGroup
variable [NormedAddCommGroup E] [CompleteSpace E] [NormedSpace ‚Ñù E]

/-- **Lebesgue dominated convergence theorem**: sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their image by
  `condExpL1`. -/
theorem tendsto_condExpL1_of_dominated_convergence (hm : m ‚â§ m‚ÇÄ) [SigmaFinite (Œº.trim hm)]
    {fs : ‚Ñï ‚Üí Œ± ‚Üí E} {f : Œ± ‚Üí E} (bound_fs : Œ± ‚Üí ‚Ñù)
    (hfs_meas : ‚àÄ n, AEStronglyMeasurable (fs n) Œº) (h_int_bound_fs : Integrable bound_fs Œº)
    (hfs_bound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚Äñfs n x‚Äñ ‚â§ bound_fs x)
    (hfs : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => fs n x) atTop (ùìù (f x))) :
    Tendsto (fun n => condExpL1 hm Œº (fs n)) atTop (ùìù (condExpL1 hm Œº f)) :=
  tendsto_setToFun_of_dominated_convergence _ bound_fs hfs_meas h_int_bound_fs hfs_bound hfs

/-- If two sequences of functions have a.e. equal conditional expectations at each step, converge
and verify dominated convergence hypotheses, then the conditional expectations of their limits are
a.e. equal. -/
theorem tendsto_condExp_unique (fs gs : ‚Ñï ‚Üí Œ± ‚Üí E) (f g : Œ± ‚Üí E)
    (hfs_int : ‚àÄ n, Integrable (fs n) Œº) (hgs_int : ‚àÄ n, Integrable (gs n) Œº)
    (hfs : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => fs n x) atTop (ùìù (f x)))
    (hgs : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => gs n x) atTop (ùìù (g x))) (bound_fs : Œ± ‚Üí ‚Ñù)
    (h_int_bound_fs : Integrable bound_fs Œº) (bound_gs : Œ± ‚Üí ‚Ñù)
    (h_int_bound_gs : Integrable bound_gs Œº) (hfs_bound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚Äñfs n x‚Äñ ‚â§ bound_fs x)
    (hgs_bound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚Äñgs n x‚Äñ ‚â§ bound_gs x) (hfg : ‚àÄ n, Œº[fs n|m] =·µê[Œº] Œº[gs n|m]) :
    Œº[f|m] =·µê[Œº] Œº[g|m] := by
  by_cases hm : m ‚â§ m‚ÇÄ; swap; ¬∑ simp_rw [condExp_of_not_le hm]; rfl
  by_cases hŒºm : SigmaFinite (Œº.trim hm); swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm]; rfl
  refine (condExp_ae_eq_condExpL1 hm f).trans ((condExp_ae_eq_condExpL1 hm g).trans ?_).symm
  rw [‚Üê Lp.ext_iff]
  have hn_eq : ‚àÄ n, condExpL1 hm Œº (gs n) = condExpL1 hm Œº (fs n) := by
    intro n
    ext1
    refine (condExp_ae_eq_condExpL1 hm (gs n)).symm.trans ((hfg n).symm.trans ?_)
    exact condExp_ae_eq_condExpL1 hm (fs n)
  have hcond_fs : Tendsto (fun n => condExpL1 hm Œº (fs n)) atTop (ùìù (condExpL1 hm Œº f)) :=
    tendsto_condExpL1_of_dominated_convergence hm _ (fun n => (hfs_int n).1) h_int_bound_fs
      hfs_bound hfs
  have hcond_gs : Tendsto (fun n => condExpL1 hm Œº (gs n)) atTop (ùìù (condExpL1 hm Œº g)) :=
    tendsto_condExpL1_of_dominated_convergence hm _ (fun n => (hgs_int n).1) h_int_bound_gs
      hgs_bound hgs
  exact tendsto_nhds_unique_of_eventuallyEq hcond_gs hcond_fs (Eventually.of_forall hn_eq)

variable [PartialOrder E] [OrderClosedTopology E] [IsOrderedAddMonoid E] [OrderedSMul ‚Ñù E]

lemma condExp_mono (hf : Integrable f Œº) (hg : Integrable g Œº) (hfg : f ‚â§·µê[Œº] g) :
    Œº[f|m] ‚â§·µê[Œº] Œº[g|m] := by
  by_cases hm : m ‚â§ m‚ÇÄ
  swap; ¬∑ simp_rw [condExp_of_not_le hm]; rfl
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm]; rfl
  exact (condExp_ae_eq_condExpL1 hm _).trans_le
    ((condExpL1_mono hf hg hfg).trans_eq (condExp_ae_eq_condExpL1 hm _).symm)

lemma condExp_nonneg (hf : 0 ‚â§·µê[Œº] f) : 0 ‚â§·µê[Œº] Œº[f|m] := by
  by_cases hfint : Integrable f Œº
  ¬∑ rw [(condExp_zero.symm : (0 : Œ± ‚Üí E) = Œº[0|m])]
    exact condExp_mono (integrable_zero _ _ _) hfint hf
  ¬∑ rw [condExp_of_not_integrable hfint]

lemma condExp_nonpos (hf : f ‚â§·µê[Œº] 0) : Œº[f|m] ‚â§·µê[Œº] 0 := by
  by_cases hfint : Integrable f Œº
  ¬∑ rw [(condExp_zero.symm : (0 : Œ± ‚Üí E) = Œº[0|m])]
    exact condExp_mono hfint (integrable_zero _ _ _) hf
  ¬∑ rw [condExp_of_not_integrable hfint]

end NormedLatticeAddCommGroup
end MeasureTheory
