/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.CondexpL1

/-! # Conditional expectation

We build the conditional expectation of an integrable function `f` with value in a Banach space
with respect to a measure `Î¼` (defined on a measurable space structure `m0`) and a measurable space
structure `m` with `hm : m â‰¤ m0` (a sub-sigma-algebra). This is an `m`-strongly measurable
function `Î¼[f|hm]` which is integrable and verifies `âˆ« x in s, Î¼[f|hm] x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼`
for all `m`-measurable sets `s`. It is unique as an element of `LÂ¹`.

The construction is done in four steps:
* Define the conditional expectation of an `LÂ²` function, as an element of `LÂ²`. This is the
  orthogonal projection on the subspace of almost everywhere `m`-measurable functions.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `Set Î± â†’ (E â†’L[â„] (Î± â†’â‚[Î¼] E))` which to a set associates a linear
  map. That linear map sends `x âˆˆ E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condExpL1CLM : (Î± â†’â‚[Î¼] E) â†’L[â„] (Î± â†’â‚[Î¼] E)`. This is done using the same
  construction as the Bochner integral (see the file `MeasureTheory/Integral/SetToL1`).
* Define the conditional expectation of a function `f : Î± â†’ E`, which is an integrable function
  `Î± â†’ E` equal to 0 if `f` is not integrable, and equal to an `m`-measurable representative of
  `condExpL1CLM` applied to `[f]`, the equivalence class of `f` in `LÂ¹`.

The first step is done in `MeasureTheory.Function.ConditionalExpectation.CondexpL2`, the two
next steps in `MeasureTheory.Function.ConditionalExpectation.CondexpL1` and the final step is
performed in this file.

## Main results

The conditional expectation and its properties

* `condExp (m : MeasurableSpace Î±) (Î¼ : Measure Î±) (f : Î± â†’ E)`: conditional expectation of `f`
  with respect to `m`.
* `integrable_condExp` : `condExp` is integrable.
* `stronglyMeasurable_condExp` : `condExp` is `m`-strongly-measurable.
* `setIntegral_condExp (hf : Integrable f Î¼) (hs : MeasurableSet[m] s)` : if `m â‰¤ m0` (the
  Ïƒ-algebra over which the measure is defined), then the conditional expectation verifies
  `âˆ« x in s, condExp m Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼` for any `m`-measurable set `s`.

While `condExp` is function-valued, we also define `condExpL1` with value in `L1` and a continuous
linear map `condExpL1CLM` from `L1` to `L1`. `condExp` should be used in most cases.

Uniqueness of the conditional expectation

* `ae_eq_condExp_of_forall_setIntegral_eq`: an a.e. `m`-measurable function which verifies the
  equality of integrals is a.e. equal to `condExp`.

## Notations

For a measure `Î¼` defined on a measurable space structure `m0`, another measurable space structure
`m` with `hm : m â‰¤ m0` (a sub-Ïƒ-algebra) and a function `f`, we define the notation
* `Î¼[f|m] = condExp m Î¼ f`.

## Tags

conditional expectation, conditional expected value

-/


open TopologicalSpace MeasureTheory.Lp Filter

open scoped ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± F F' ğ•œ : Type*} [RCLike ğ•œ]
  -- ğ•œ for â„ or â„‚
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace ğ•œ F'] [NormedSpace â„ F'] [CompleteSpace F']

open scoped Classical

variable {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {f g : Î± â†’ F'} {s : Set Î±}

/-- Conditional expectation of a function. It is defined as 0 if any one of the following conditions
is true:
- `m` is not a sub-Ïƒ-algebra of `m0`,
- `Î¼` is not Ïƒ-finite with respect to `m`,
- `f` is not integrable. -/
noncomputable irreducible_def condExp (m : MeasurableSpace Î±) {m0 : MeasurableSpace Î±}
    (Î¼ : Measure Î±) (f : Î± â†’ F') : Î± â†’ F' :=
  if hm : m â‰¤ m0 then
    if h : SigmaFinite (Î¼.trim hm) âˆ§ Integrable f Î¼ then
      if StronglyMeasurable[m] f then f
      else (@aestronglyMeasurable'_condExpL1 _ _ _ _ _ m m0 Î¼ hm h.1 _).mk
        (@condExpL1 _ _ _ _ _ _ _ hm Î¼ h.1 f)
    else 0
  else 0

@[deprecated (since := "2025-01-21")] alias condexp := condExp

-- We define notation `Î¼[f|m]` for the conditional expectation of `f` with respect to `m`.
scoped notation Î¼ "[" f "|" m "]" => MeasureTheory.condExp m Î¼ f

theorem condExp_of_not_le (hm_not : Â¬m â‰¤ m0) : Î¼[f|m] = 0 := by rw [condExp, dif_neg hm_not]

@[deprecated (since := "2025-01-21")] alias condexp_of_not_le := condExp_of_not_le

theorem condExp_of_not_sigmaFinite (hm : m â‰¤ m0) (hÎ¼m_not : Â¬SigmaFinite (Î¼.trim hm)) :
    Î¼[f|m] = 0 := by rw [condExp, dif_pos hm, dif_neg]; push_neg; exact fun h => absurd h hÎ¼m_not

@[deprecated (since := "2025-01-21")] alias condexp_of_not_sigmaFinite := condExp_of_not_sigmaFinite

theorem condExp_of_sigmaFinite (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] :
    Î¼[f|m] =
      if Integrable f Î¼ then
        if StronglyMeasurable[m] f then f
        else aestronglyMeasurable'_condExpL1.mk (condExpL1 hm Î¼ f)
      else 0 := by
  rw [condExp, dif_pos hm]
  simp only [hÎ¼m, Ne, true_and]
  by_cases hf : Integrable f Î¼
  Â· rw [dif_pos hf, if_pos hf]
  Â· rw [dif_neg hf, if_neg hf]

@[deprecated (since := "2025-01-21")] alias condexp_of_sigmaFinite := condExp_of_sigmaFinite

theorem condExp_of_stronglyMeasurable (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] {f : Î± â†’ F'}
    (hf : StronglyMeasurable[m] f) (hfi : Integrable f Î¼) : Î¼[f|m] = f := by
  rw [condExp_of_sigmaFinite hm, if_pos hfi, if_pos hf]

@[deprecated (since := "2025-01-21")]
alias condexp_of_stronglyMeasurable := condExp_of_stronglyMeasurable

theorem condExp_const (hm : m â‰¤ m0) (c : F') [IsFiniteMeasure Î¼] :
    Î¼[fun _ : Î± => c|m] = fun _ => c :=
  condExp_of_stronglyMeasurable hm (@stronglyMeasurable_const _ _ m _ _) (integrable_const c)

@[deprecated (since := "2025-01-21")] alias condexp_const := condExp_const

theorem condExp_ae_eq_condExpL1 (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] (f : Î± â†’ F') :
    Î¼[f|m] =áµ[Î¼] condExpL1 hm Î¼ f := by
  rw [condExp_of_sigmaFinite hm]
  by_cases hfi : Integrable f Î¼
  Â· rw [if_pos hfi]
    by_cases hfm : StronglyMeasurable[m] f
    Â· rw [if_pos hfm]
      exact (condExpL1_of_aestronglyMeasurable' (StronglyMeasurable.aeStronglyMeasurable' hfm)
        hfi).symm
    Â· rw [if_neg hfm]
      exact (AEStronglyMeasurable'.ae_eq_mk aestronglyMeasurable'_condExpL1).symm
  rw [if_neg hfi, condExpL1_undef hfi]
  exact (coeFn_zero _ _ _).symm

@[deprecated (since := "2025-01-21")] alias condexp_ae_eq_condexpL1 := condExp_ae_eq_condExpL1

theorem condExp_ae_eq_condExpL1CLM (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hf : Integrable f Î¼) :
    Î¼[f|m] =áµ[Î¼] condExpL1CLM F' hm Î¼ (hf.toL1 f) := by
  refine (condExp_ae_eq_condExpL1 hm f).trans (Eventually.of_forall fun x => ?_)
  rw [condExpL1_eq hf]

@[deprecated (since := "2025-01-21")] alias condexp_ae_eq_condexpL1CLM := condExp_ae_eq_condExpL1CLM

theorem condExp_undef (hf : Â¬Integrable f Î¼) : Î¼[f|m] = 0 := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condExp_of_not_le hm]
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condExp_of_not_sigmaFinite hm hÎ¼m]
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  rw [condExp_of_sigmaFinite, if_neg hf]

@[deprecated (since := "2025-01-21")] alias condexp_undef := condExp_undef

@[simp]
theorem condExp_zero : Î¼[(0 : Î± â†’ F')|m] = 0 := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condExp_of_not_le hm]
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condExp_of_not_sigmaFinite hm hÎ¼m]
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact
    condExp_of_stronglyMeasurable hm (@stronglyMeasurable_zero _ _ m _ _) (integrable_zero _ _ _)

@[deprecated (since := "2025-01-21")] alias condexp_zero := condExp_zero

theorem stronglyMeasurable_condExp : StronglyMeasurable[m] (Î¼[f|m]) := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condExp_of_not_le hm]; exact stronglyMeasurable_zero
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condExp_of_not_sigmaFinite hm hÎ¼m]; exact stronglyMeasurable_zero
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  rw [condExp_of_sigmaFinite hm]
  split_ifs with hfi hfm
  Â· exact hfm
  Â· exact AEStronglyMeasurable'.stronglyMeasurable_mk _
  Â· exact stronglyMeasurable_zero

@[deprecated (since := "2025-01-21")] alias stronglyMeasurable_condexp := stronglyMeasurable_condExp

theorem condExp_congr_ae (h : f =áµ[Î¼] g) : Î¼[f|m] =áµ[Î¼] Î¼[g|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact (condExp_ae_eq_condExpL1 hm f).trans
    (Filter.EventuallyEq.trans (by rw [condExpL1_congr_ae hm h])
      (condExp_ae_eq_condExpL1 hm g).symm)

@[deprecated (since := "2025-01-21")] alias condexp_congr_ae := condExp_congr_ae

theorem condExp_of_aestronglyMeasurable' (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] {f : Î± â†’ F'}
    (hf : AEStronglyMeasurable' m f Î¼) (hfi : Integrable f Î¼) : Î¼[f|m] =áµ[Î¼] f := by
  refine ((condExp_congr_ae hf.ae_eq_mk).trans ?_).trans hf.ae_eq_mk.symm
  rw [condExp_of_stronglyMeasurable hm hf.stronglyMeasurable_mk
    ((integrable_congr hf.ae_eq_mk).mp hfi)]

@[deprecated (since := "2025-01-21")]
alias condexp_of_aestronglyMeasurable' := condExp_of_aestronglyMeasurable'

theorem integrable_condExp : Integrable (Î¼[f|m]) Î¼ := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condExp_of_not_le hm]; exact integrable_zero _ _ _
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condExp_of_not_sigmaFinite hm hÎ¼m]; exact integrable_zero _ _ _
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact (integrable_condExpL1 f).congr (condExp_ae_eq_condExpL1 hm f).symm

@[deprecated (since := "2025-01-21")] alias integrable_condexp := integrable_condExp

/-- The integral of the conditional expectation `Î¼[f|hm]` over an `m`-measurable set is equal to
the integral of `f` on that set. -/
theorem setIntegral_condExp (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hf : Integrable f Î¼)
    (hs : MeasurableSet[m] s) : âˆ« x in s, (Î¼[f|m]) x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [setIntegral_congr_ae (hm s hs) ((condExp_ae_eq_condExpL1 hm f).mono fun x hx _ => hx)]
  exact setIntegral_condExpL1 hf hs

@[deprecated (since := "2025-01-21")] alias setIntegral_condexp := setIntegral_condExp

@[deprecated (since := "2024-04-17")] alias set_integral_condexp := setIntegral_condExp

theorem integral_condExp (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] :
    âˆ« x, (Î¼[f|m]) x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  by_cases hf : Integrable f Î¼
  Â· suffices âˆ« x in Set.univ, (Î¼[f|m]) x âˆ‚Î¼ = âˆ« x in Set.univ, f x âˆ‚Î¼ by
      simp_rw [setIntegral_univ] at this; exact this
    exact setIntegral_condExp hm hf (@MeasurableSet.univ _ m)
  simp only [condExp_undef hf, Pi.zero_apply, integral_zero, integral_undef hf]

@[deprecated (since := "2025-01-21")] alias integral_condexp := integral_condExp

/-- Total probability law using `condExp` as conditional probability. -/
theorem integral_condExp_indicator [mF : MeasurableSpace F] {Y : Î± â†’ F} (hY : Measurable Y)
    [SigmaFinite (Î¼.trim hY.comap_le)] {A : Set Î±} (hA : MeasurableSet A) :
    âˆ« x, (Î¼[(A.indicator fun _ â†¦ (1 : â„)) | mF.comap Y]) x âˆ‚Î¼ = (Î¼ A).toReal := by
  rw [integral_condExp, integral_indicator hA, setIntegral_const, smul_eq_mul, mul_one]

@[deprecated (since := "2025-01-21")] alias integral_condexp_indicator := integral_condExp_indicator

/-- **Uniqueness of the conditional expectation**
If a function is a.e. `m`-measurable, verifies an integrability condition and has same integral
as `f` on all `m`-measurable sets, then it is a.e. equal to `Î¼[f|hm]`. -/
theorem ae_eq_condExp_of_forall_setIntegral_eq (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    {f g : Î± â†’ F'} (hf : Integrable f Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hg_eq : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, g x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼)
    (hgm : AEStronglyMeasurable' m g Î¼) : g =áµ[Î¼] Î¼[f|m] := by
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hm hg_int_finite
    (fun s _ _ => integrable_condExp.integrableOn) (fun s hs hÎ¼s => ?_) hgm
    (StronglyMeasurable.aeStronglyMeasurable' stronglyMeasurable_condExp)
  rw [hg_eq s hs hÎ¼s, setIntegral_condExp hm hf hs]

@[deprecated (since := "2025-01-21")]
alias ae_eq_condexp_of_forall_setIntegral_eq := ae_eq_condExp_of_forall_setIntegral_eq

@[deprecated (since := "2024-04-17")]
alias ae_eq_condExp_of_forall_set_integral_eq := ae_eq_condExp_of_forall_setIntegral_eq

section MemL2

variable [IsFiniteMeasure Î¼]

lemma Memâ„’p.condExpL2_ae_eq_condExp {ğ•œ : Type*} [RCLike ğ•œ] [InnerProductSpace ğ•œ F']
    (hm : m â‰¤ m0) (hf : Memâ„’p f 2 Î¼) :
    condExpL2 F' ğ•œ hm hf.toLp =áµ[Î¼] Î¼[f | m] := by
  refine ae_eq_condExp_of_forall_setIntegral_eq hm
    (memâ„’p_one_iff_integrable.1 <| hf.mono_exponent one_le_two)
    (fun s hs htop â†¦ integrableOn_condExpL2_of_measure_ne_top hm htop.ne _) (fun s hs htop â†¦ ?_)
    (aeStronglyMeasurable'_condExpL2 hm _)
  rw [integral_condExpL2_eq hm (hf.toLp _) hs htop.ne]
  refine setIntegral_congr_ae (hm _ hs) ?_
  filter_upwards [hf.coeFn_toLp] with Ï‰ hÏ‰ _ using hÏ‰

-- TODO: Generalize via the conditional Jensen inequality
lemma eLpNorm_condExp_le {ğ•œ : Type*} [RCLike ğ•œ] [InnerProductSpace ğ•œ F'] :
    eLpNorm (Î¼[f | m]) 2 Î¼ â‰¤ eLpNorm f 2 Î¼ := by
  by_cases hm : m â‰¤ m0; swap
  Â· simp [condExp_of_not_le hm]
  by_cases hfm : AEStronglyMeasurable f Î¼; swap
  Â· rw [condExp_undef (by simp [Integrable, not_and_of_not_left _ hfm])]
    simp
  obtain hf | hf := eq_or_ne (eLpNorm f 2 Î¼) âˆ
  Â· simp [hf]
  replace hf : Memâ„’p f 2 Î¼ := âŸ¨hfm, Ne.lt_top' fun a â†¦ hf (id (Eq.symm a))âŸ©
  rw [â† eLpNorm_congr_ae (hf.condExpL2_ae_eq_condExp (ğ•œ := ğ•œ) hm)]
  refine le_trans (eLpNorm_condExpL2_le hm _) ?_
  rw [eLpNorm_congr_ae hf.coeFn_toLp]

protected lemma Memâ„’p.condExp {ğ•œ : Type*} [RCLike ğ•œ] [InnerProductSpace ğ•œ F']
    (hf : Memâ„’p f 2 Î¼) : Memâ„’p (Î¼[f | m]) 2 Î¼ := by
  by_cases hm : m â‰¤ m0
  Â· exact âŸ¨(stronglyMeasurable_condExp.mono hm).aestronglyMeasurable,
      eLpNorm_condExp_le (ğ•œ := ğ•œ).trans_lt hf.eLpNorm_lt_topâŸ©
  Â· simp [condExp_of_not_le hm]

end MemL2

theorem condExp_bot' [hÎ¼ : NeZero Î¼] (f : Î± â†’ F') :
    Î¼[f|âŠ¥] = fun _ => (Î¼ Set.univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  by_cases hÎ¼_finite : IsFiniteMeasure Î¼
  swap
  Â· have h : Â¬SigmaFinite (Î¼.trim bot_le) := by rwa [sigmaFinite_trim_bot_iff]
    rw [not_isFiniteMeasure_iff] at hÎ¼_finite
    rw [condExp_of_not_sigmaFinite bot_le h]
    simp only [hÎ¼_finite, ENNReal.top_toReal, inv_zero, zero_smul]
    rfl
  have h_meas : StronglyMeasurable[âŠ¥] (Î¼[f|âŠ¥]) := stronglyMeasurable_condExp
  obtain âŸ¨c, h_eqâŸ© := stronglyMeasurable_bot_iff.mp h_meas
  rw [h_eq]
  have h_integral : âˆ« x, (Î¼[f|âŠ¥]) x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := integral_condExp bot_le
  simp_rw [h_eq, integral_const] at h_integral
  rw [â† h_integral, â† smul_assoc, smul_eq_mul, inv_mul_cancelâ‚€, one_smul]
  rw [Ne, ENNReal.toReal_eq_zero_iff, not_or]
  exact âŸ¨NeZero.ne _, measure_ne_top Î¼ Set.univâŸ©

@[deprecated (since := "2025-01-21")] alias condexp_bot' := condExp_bot'

theorem condExp_bot_ae_eq (f : Î± â†’ F') :
    Î¼[f|âŠ¥] =áµ[Î¼] fun _ => (Î¼ Set.univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rcases eq_zero_or_neZero Î¼ with rfl | hÎ¼
  Â· rw [ae_zero]; exact eventually_bot
  Â· exact Eventually.of_forall <| congr_fun (condExp_bot' f)

@[deprecated (since := "2025-01-21")] alias condexp_bot_ae_eq := condExp_bot_ae_eq

theorem condExp_bot [IsProbabilityMeasure Î¼] (f : Î± â†’ F') : Î¼[f|âŠ¥] = fun _ => âˆ« x, f x âˆ‚Î¼ := by
  refine (condExp_bot' f).trans ?_; rw [measure_univ, ENNReal.one_toReal, inv_one, one_smul]

@[deprecated (since := "2025-01-21")] alias condexp_bot := condExp_bot

theorem condExp_add (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼[f + g|m] =áµ[Î¼] Î¼[f|m] + Î¼[g|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm]; simp
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m]; simp
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  refine (condExp_ae_eq_condExpL1 hm _).trans ?_
  rw [condExpL1_add hf hg]
  exact (coeFn_add _ _).trans
    ((condExp_ae_eq_condExpL1 hm _).symm.add (condExp_ae_eq_condExpL1 hm _).symm)

@[deprecated (since := "2025-01-21")] alias condexp_add := condExp_add

theorem condExp_finset_sum {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ F'}
    (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) : Î¼[âˆ‘ i âˆˆ s, f i|m] =áµ[Î¼] âˆ‘ i âˆˆ s, Î¼[f i|m] := by
  induction' s using Finset.induction_on with i s his heq hf
  Â· rw [Finset.sum_empty, Finset.sum_empty, condExp_zero]
  Â· rw [Finset.sum_insert his, Finset.sum_insert his]
    exact (condExp_add (hf i <| Finset.mem_insert_self i s) <|
      integrable_finset_sum' _ fun j hmem => hf j <| Finset.mem_insert_of_mem hmem).trans
        ((EventuallyEq.refl _ _).add (heq fun j hmem => hf j <| Finset.mem_insert_of_mem hmem))

@[deprecated (since := "2025-01-21")] alias condexp_finset_sum := condExp_finset_sum

theorem condExp_smul (c : ğ•œ) (f : Î± â†’ F') : Î¼[c â€¢ f|m] =áµ[Î¼] c â€¢ Î¼[f|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm]; simp
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m]; simp
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  refine (condExp_ae_eq_condExpL1 hm _).trans ?_
  rw [condExpL1_smul c f]
  refine (@condExp_ae_eq_condExpL1 _ _ _ _ _ m _ _ hm _ f).mp ?_
  refine (coeFn_smul c (condExpL1 hm Î¼ f)).mono fun x hx1 hx2 => ?_
  simp only [hx1, hx2, Pi.smul_apply]

@[deprecated (since := "2025-01-21")] alias condexp_smul := condExp_smul

theorem condExp_neg (f : Î± â†’ F') : Î¼[-f|m] =áµ[Î¼] -Î¼[f|m] := by
  letI : Module â„ (Î± â†’ F') := @Pi.module Î± (fun _ => F') â„ _ _ fun _ => inferInstance
  calc
    Î¼[-f|m] = Î¼[(-1 : â„) â€¢ f|m] := by rw [neg_one_smul â„ f]
    _ =áµ[Î¼] (-1 : â„) â€¢ Î¼[f|m] := condExp_smul (-1) f
    _ = -Î¼[f|m] := neg_one_smul â„ (Î¼[f|m])

@[deprecated (since := "2025-01-21")] alias condexp_neg := condExp_neg

theorem condExp_sub (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼[f - g|m] =áµ[Î¼] Î¼[f|m] - Î¼[g|m] := by
  simp_rw [sub_eq_add_neg]
  exact (condExp_add hf hg.neg).trans (EventuallyEq.rfl.add (condExp_neg g))

@[deprecated (since := "2025-01-21")] alias condexp_sub := condExp_sub

theorem condExp_condExp_of_le {mâ‚ mâ‚‚ m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} (hmâ‚â‚‚ : mâ‚ â‰¤ mâ‚‚)
    (hmâ‚‚ : mâ‚‚ â‰¤ m0) [SigmaFinite (Î¼.trim hmâ‚‚)] : Î¼[Î¼[f|mâ‚‚]|mâ‚] =áµ[Î¼] Î¼[f|mâ‚] := by
  by_cases hÎ¼mâ‚ : SigmaFinite (Î¼.trim (hmâ‚â‚‚.trans hmâ‚‚))
  swap; Â· simp_rw [condExp_of_not_sigmaFinite (hmâ‚â‚‚.trans hmâ‚‚) hÎ¼mâ‚]; rfl
  haveI : SigmaFinite (Î¼.trim (hmâ‚â‚‚.trans hmâ‚‚)) := hÎ¼mâ‚
  by_cases hf : Integrable f Î¼
  swap; Â· simp_rw [condExp_undef hf, condExp_zero]; rfl
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' (hmâ‚â‚‚.trans hmâ‚‚)
    (fun s _ _ => integrable_condExp.integrableOn)
    (fun s _ _ => integrable_condExp.integrableOn) ?_
    (StronglyMeasurable.aeStronglyMeasurable' stronglyMeasurable_condExp)
    (StronglyMeasurable.aeStronglyMeasurable' stronglyMeasurable_condExp)
  intro s hs _
  rw [setIntegral_condExp (hmâ‚â‚‚.trans hmâ‚‚) integrable_condExp hs]
  rw [setIntegral_condExp (hmâ‚â‚‚.trans hmâ‚‚) hf hs, setIntegral_condExp hmâ‚‚ hf (hmâ‚â‚‚ s hs)]

@[deprecated (since := "2025-01-21")] alias condexp_condexp_of_le := condExp_condExp_of_le

theorem condExp_mono {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f g : Î± â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) (hfg : f â‰¤áµ[Î¼] g) :
    Î¼[f|m] â‰¤áµ[Î¼] Î¼[g|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact (condExp_ae_eq_condExpL1 hm _).trans_le
    ((condExpL1_mono hf hg hfg).trans_eq (condExp_ae_eq_condExpL1 hm _).symm)

@[deprecated (since := "2025-01-21")] alias condexp_mono := condExp_mono

theorem condExp_nonneg {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f : Î± â†’ E} (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤áµ[Î¼] Î¼[f|m] := by
  by_cases hfint : Integrable f Î¼
  Â· rw [(condExp_zero.symm : (0 : Î± â†’ E) = Î¼[0|m])]
    exact condExp_mono (integrable_zero _ _ _) hfint hf
  Â· rw [condExp_undef hfint]

@[deprecated (since := "2025-01-21")] alias condexp_nonneg := condExp_nonneg

theorem condExp_nonpos {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f : Î± â†’ E} (hf : f â‰¤áµ[Î¼] 0) : Î¼[f|m] â‰¤áµ[Î¼] 0 := by
  by_cases hfint : Integrable f Î¼
  Â· rw [(condExp_zero.symm : (0 : Î± â†’ E) = Î¼[0|m])]
    exact condExp_mono hfint (integrable_zero _ _ _) hf
  Â· rw [condExp_undef hfint]

@[deprecated (since := "2025-01-21")] alias condexp_nonpos := condExp_nonpos

/-- **Lebesgue dominated convergence theorem**: sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their image by
  `condExpL1`. -/
theorem tendsto_condExpL1_of_dominated_convergence (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    {fs : â„• â†’ Î± â†’ F'} {f : Î± â†’ F'} (bound_fs : Î± â†’ â„)
    (hfs_meas : âˆ€ n, AEStronglyMeasurable (fs n) Î¼) (h_int_bound_fs : Integrable bound_fs Î¼)
    (hfs_bound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–fs n xâ€– â‰¤ bound_fs x)
    (hfs : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => fs n x) atTop (ğ“ (f x))) :
    Tendsto (fun n => condExpL1 hm Î¼ (fs n)) atTop (ğ“ (condExpL1 hm Î¼ f)) :=
  tendsto_setToFun_of_dominated_convergence _ bound_fs hfs_meas h_int_bound_fs hfs_bound hfs

@[deprecated (since := "2025-01-21")]
alias tendsto_condexpL1_of_dominated_convergence := tendsto_condExpL1_of_dominated_convergence

/-- If two sequences of functions have a.e. equal conditional expectations at each step, converge
and verify dominated convergence hypotheses, then the conditional expectations of their limits are
a.e. equal. -/
theorem tendsto_condExp_unique (fs gs : â„• â†’ Î± â†’ F') (f g : Î± â†’ F')
    (hfs_int : âˆ€ n, Integrable (fs n) Î¼) (hgs_int : âˆ€ n, Integrable (gs n) Î¼)
    (hfs : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => fs n x) atTop (ğ“ (f x)))
    (hgs : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => gs n x) atTop (ğ“ (g x))) (bound_fs : Î± â†’ â„)
    (h_int_bound_fs : Integrable bound_fs Î¼) (bound_gs : Î± â†’ â„)
    (h_int_bound_gs : Integrable bound_gs Î¼) (hfs_bound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–fs n xâ€– â‰¤ bound_fs x)
    (hgs_bound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–gs n xâ€– â‰¤ bound_gs x) (hfg : âˆ€ n, Î¼[fs n|m] =áµ[Î¼] Î¼[gs n|m]) :
    Î¼[f|m] =áµ[Î¼] Î¼[g|m] := by
  by_cases hm : m â‰¤ m0; swap; Â· simp_rw [condExp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm); swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  refine (condExp_ae_eq_condExpL1 hm f).trans ((condExp_ae_eq_condExpL1 hm g).trans ?_).symm
  rw [â† Lp.ext_iff]
  have hn_eq : âˆ€ n, condExpL1 hm Î¼ (gs n) = condExpL1 hm Î¼ (fs n) := by
    intro n
    ext1
    refine (condExp_ae_eq_condExpL1 hm (gs n)).symm.trans ((hfg n).symm.trans ?_)
    exact condExp_ae_eq_condExpL1 hm (fs n)
  have hcond_fs : Tendsto (fun n => condExpL1 hm Î¼ (fs n)) atTop (ğ“ (condExpL1 hm Î¼ f)) :=
    tendsto_condExpL1_of_dominated_convergence hm _ (fun n => (hfs_int n).1) h_int_bound_fs
      hfs_bound hfs
  have hcond_gs : Tendsto (fun n => condExpL1 hm Î¼ (gs n)) atTop (ğ“ (condExpL1 hm Î¼ g)) :=
    tendsto_condExpL1_of_dominated_convergence hm _ (fun n => (hgs_int n).1) h_int_bound_gs
      hgs_bound hgs
  exact tendsto_nhds_unique_of_eventuallyEq hcond_gs hcond_fs (Eventually.of_forall hn_eq)

@[deprecated (since := "2025-01-21")] alias tendsto_condexp_unique := tendsto_condExp_unique

end MeasureTheory
