/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.CondexpL1

#align_import measure_theory.function.conditional_expectation.basic from "leanprover-community/mathlib"@"d8bbb04e2d2a44596798a9207ceefc0fb236e41e"

/-! # Conditional expectation

We build the conditional expectation of an integrable function `f` with value in a Banach space
with respect to a measure `Î¼` (defined on a measurable space structure `m0`) and a measurable space
structure `m` with `hm : m â‰¤ m0` (a sub-sigma-algebra). This is an `m`-strongly measurable
function `Î¼[f|hm]` which is integrable and verifies `âˆ« x in s, Î¼[f|hm] x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼`
for all `m`-measurable sets `s`. It is unique as an element of `LÂ¹`.

The construction is done in four steps:
* Define the conditional expectation of an `LÂ²` function, as an element of `LÂ²`. This is the
  orthogonal projection on the subspace of almost everywhere `m`-measurable functions.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `Set Î± â†’ (E â†’L[â„] (Î± â†’â‚[Î¼] E))` which to a set associates a linear
  map. That linear map sends `x âˆˆ E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condexpL1CLM : (Î± â†’â‚[Î¼] E) â†’L[â„] (Î± â†’â‚[Î¼] E)`. This is done using the same
  construction as the Bochner integral (see the file `MeasureTheory/Integral/SetToL1`).
* Define the conditional expectation of a function `f : Î± â†’ E`, which is an integrable function
  `Î± â†’ E` equal to 0 if `f` is not integrable, and equal to an `m`-measurable representative of
  `condexpL1CLM` applied to `[f]`, the equivalence class of `f` in `LÂ¹`.

The first step is done in `MeasureTheory.Function.ConditionalExpectation.CondexpL2`, the two
next steps in `MeasureTheory.Function.ConditionalExpectation.CondexpL1` and the final step is
performed in this file.

## Main results

The conditional expectation and its properties

* `condexp (m : MeasurableSpace Î±) (Î¼ : Measure Î±) (f : Î± â†’ E)`: conditional expectation of `f`
  with respect to `m`.
* `integrable_condexp` : `condexp` is integrable.
* `stronglyMeasurable_condexp` : `condexp` is `m`-strongly-measurable.
* `setIntegral_condexp (hf : Integrable f Î¼) (hs : MeasurableSet[m] s)` : if `m â‰¤ m0` (the
  Ïƒ-algebra over which the measure is defined), then the conditional expectation verifies
  `âˆ« x in s, condexp m Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼` for any `m`-measurable set `s`.

While `condexp` is function-valued, we also define `condexpL1` with value in `L1` and a continuous
linear map `condexpL1CLM` from `L1` to `L1`. `condexp` should be used in most cases.

Uniqueness of the conditional expectation

* `ae_eq_condexp_of_forall_setIntegral_eq`: an a.e. `m`-measurable function which verifies the
  equality of integrals is a.e. equal to `condexp`.

## Notations

For a measure `Î¼` defined on a measurable space structure `m0`, another measurable space structure
`m` with `hm : m â‰¤ m0` (a sub-Ïƒ-algebra) and a function `f`, we define the notation
* `Î¼[f|m] = condexp m Î¼ f`.

## Tags

conditional expectation, conditional expected value

-/


open TopologicalSpace MeasureTheory.Lp Filter

open scoped ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± F F' ğ•œ : Type*} {p : â„â‰¥0âˆ} [RCLike ğ•œ]
  -- ğ•œ for â„ or â„‚
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ğ•œ F]
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace ğ•œ F'] [NormedSpace â„ F'] [CompleteSpace F']

open scoped Classical

variable {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {f g : Î± â†’ F'} {s : Set Î±}

/-- Conditional expectation of a function. It is defined as 0 if any one of the following conditions
is true:
- `m` is not a sub-Ïƒ-algebra of `m0`,
- `Î¼` is not Ïƒ-finite with respect to `m`,
- `f` is not integrable. -/
noncomputable irreducible_def condexp (m : MeasurableSpace Î±) {m0 : MeasurableSpace Î±}
    (Î¼ : Measure Î±) (f : Î± â†’ F') : Î± â†’ F' :=
  if hm : m â‰¤ m0 then
    if h : SigmaFinite (Î¼.trim hm) âˆ§ Integrable f Î¼ then
      if StronglyMeasurable[m] f then f
      else (@aestronglyMeasurable'_condexpL1 _ _ _ _ _ m m0 Î¼ hm h.1 _).mk
        (@condexpL1 _ _ _ _ _ _ _ hm Î¼ h.1 f)
    else 0
  else 0
#align measure_theory.condexp MeasureTheory.condexp

-- We define notation `Î¼[f|m]` for the conditional expectation of `f` with respect to `m`.
scoped notation Î¼ "[" f "|" m "]" => MeasureTheory.condexp m Î¼ f

theorem condexp_of_not_le (hm_not : Â¬m â‰¤ m0) : Î¼[f|m] = 0 := by rw [condexp, dif_neg hm_not]
#align measure_theory.condexp_of_not_le MeasureTheory.condexp_of_not_le

theorem condexp_of_not_sigmaFinite (hm : m â‰¤ m0) (hÎ¼m_not : Â¬SigmaFinite (Î¼.trim hm)) :
    Î¼[f|m] = 0 := by rw [condexp, dif_pos hm, dif_neg]; push_neg; exact fun h => absurd h hÎ¼m_not
#align measure_theory.condexp_of_not_sigma_finite MeasureTheory.condexp_of_not_sigmaFinite

theorem condexp_of_sigmaFinite (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] :
    Î¼[f|m] =
      if Integrable f Î¼ then
        if StronglyMeasurable[m] f then f
        else aestronglyMeasurable'_condexpL1.mk (condexpL1 hm Î¼ f)
      else 0 := by
  rw [condexp, dif_pos hm]
  simp only [hÎ¼m, Ne, true_and_iff]
  by_cases hf : Integrable f Î¼
  Â· rw [dif_pos hf, if_pos hf]
  Â· rw [dif_neg hf, if_neg hf]
#align measure_theory.condexp_of_sigma_finite MeasureTheory.condexp_of_sigmaFinite

theorem condexp_of_stronglyMeasurable (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] {f : Î± â†’ F'}
    (hf : StronglyMeasurable[m] f) (hfi : Integrable f Î¼) : Î¼[f|m] = f := by
  rw [condexp_of_sigmaFinite hm, if_pos hfi, if_pos hf]
#align measure_theory.condexp_of_strongly_measurable MeasureTheory.condexp_of_stronglyMeasurable

theorem condexp_const (hm : m â‰¤ m0) (c : F') [IsFiniteMeasure Î¼] :
    Î¼[fun _ : Î± => c|m] = fun _ => c :=
  condexp_of_stronglyMeasurable hm (@stronglyMeasurable_const _ _ m _ _) (integrable_const c)
#align measure_theory.condexp_const MeasureTheory.condexp_const

theorem condexp_ae_eq_condexpL1 (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] (f : Î± â†’ F') :
    Î¼[f|m] =áµ[Î¼] condexpL1 hm Î¼ f := by
  rw [condexp_of_sigmaFinite hm]
  by_cases hfi : Integrable f Î¼
  Â· rw [if_pos hfi]
    by_cases hfm : StronglyMeasurable[m] f
    Â· rw [if_pos hfm]
      exact (condexpL1_of_aestronglyMeasurable' (StronglyMeasurable.aeStronglyMeasurable' hfm)
        hfi).symm
    Â· rw [if_neg hfm]
      exact (AEStronglyMeasurable'.ae_eq_mk aestronglyMeasurable'_condexpL1).symm
  rw [if_neg hfi, condexpL1_undef hfi]
  exact (coeFn_zero _ _ _).symm
set_option linter.uppercaseLean3 false in
#align measure_theory.condexp_ae_eq_condexp_L1 MeasureTheory.condexp_ae_eq_condexpL1

theorem condexp_ae_eq_condexpL1CLM (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hf : Integrable f Î¼) :
    Î¼[f|m] =áµ[Î¼] condexpL1CLM F' hm Î¼ (hf.toL1 f) := by
  refine (condexp_ae_eq_condexpL1 hm f).trans (eventually_of_forall fun x => ?_)
  rw [condexpL1_eq hf]
set_option linter.uppercaseLean3 false in
#align measure_theory.condexp_ae_eq_condexp_L1_clm MeasureTheory.condexp_ae_eq_condexpL1CLM

theorem condexp_undef (hf : Â¬Integrable f Î¼) : Î¼[f|m] = 0 := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condexp_of_not_le hm]
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condexp_of_not_sigmaFinite hm hÎ¼m]
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  rw [condexp_of_sigmaFinite, if_neg hf]
#align measure_theory.condexp_undef MeasureTheory.condexp_undef

@[simp]
theorem condexp_zero : Î¼[(0 : Î± â†’ F')|m] = 0 := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condexp_of_not_le hm]
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condexp_of_not_sigmaFinite hm hÎ¼m]
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact
    condexp_of_stronglyMeasurable hm (@stronglyMeasurable_zero _ _ m _ _) (integrable_zero _ _ _)
#align measure_theory.condexp_zero MeasureTheory.condexp_zero

theorem stronglyMeasurable_condexp : StronglyMeasurable[m] (Î¼[f|m]) := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condexp_of_not_le hm]; exact stronglyMeasurable_zero
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condexp_of_not_sigmaFinite hm hÎ¼m]; exact stronglyMeasurable_zero
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  rw [condexp_of_sigmaFinite hm]
  split_ifs with hfi hfm
  Â· exact hfm
  Â· exact AEStronglyMeasurable'.stronglyMeasurable_mk _
  Â· exact stronglyMeasurable_zero
#align measure_theory.strongly_measurable_condexp MeasureTheory.stronglyMeasurable_condexp

theorem condexp_congr_ae (h : f =áµ[Î¼] g) : Î¼[f|m] =áµ[Î¼] Î¼[g|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condexp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condexp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact (condexp_ae_eq_condexpL1 hm f).trans
    (Filter.EventuallyEq.trans (by rw [condexpL1_congr_ae hm h])
      (condexp_ae_eq_condexpL1 hm g).symm)
#align measure_theory.condexp_congr_ae MeasureTheory.condexp_congr_ae

theorem condexp_of_aestronglyMeasurable' (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] {f : Î± â†’ F'}
    (hf : AEStronglyMeasurable' m f Î¼) (hfi : Integrable f Î¼) : Î¼[f|m] =áµ[Î¼] f := by
  refine ((condexp_congr_ae hf.ae_eq_mk).trans ?_).trans hf.ae_eq_mk.symm
  rw [condexp_of_stronglyMeasurable hm hf.stronglyMeasurable_mk
    ((integrable_congr hf.ae_eq_mk).mp hfi)]
#align measure_theory.condexp_of_ae_strongly_measurable' MeasureTheory.condexp_of_aestronglyMeasurable'

theorem integrable_condexp : Integrable (Î¼[f|m]) Î¼ := by
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condexp_of_not_le hm]; exact integrable_zero _ _ _
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condexp_of_not_sigmaFinite hm hÎ¼m]; exact integrable_zero _ _ _
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact (integrable_condexpL1 f).congr (condexp_ae_eq_condexpL1 hm f).symm
#align measure_theory.integrable_condexp MeasureTheory.integrable_condexp

/-- The integral of the conditional expectation `Î¼[f|hm]` over an `m`-measurable set is equal to
the integral of `f` on that set. -/
theorem setIntegral_condexp (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hf : Integrable f Î¼)
    (hs : MeasurableSet[m] s) : âˆ« x in s, (Î¼[f|m]) x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [setIntegral_congr_ae (hm s hs) ((condexp_ae_eq_condexpL1 hm f).mono fun x hx _ => hx)]
  exact setIntegral_condexpL1 hf hs
#align measure_theory.set_integral_condexp MeasureTheory.setIntegral_condexp

@[deprecated (since := "2024-04-17")] alias set_integral_condexp := setIntegral_condexp

theorem integral_condexp (hm : m â‰¤ m0) [hÎ¼m : SigmaFinite (Î¼.trim hm)] (hf : Integrable f Î¼) :
    âˆ« x, (Î¼[f|m]) x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  suffices âˆ« x in Set.univ, (Î¼[f|m]) x âˆ‚Î¼ = âˆ« x in Set.univ, f x âˆ‚Î¼ by
    simp_rw [integral_univ] at this; exact this
  exact setIntegral_condexp hm hf (@MeasurableSet.univ _ m)
#align measure_theory.integral_condexp MeasureTheory.integral_condexp

/-- **Uniqueness of the conditional expectation**
If a function is a.e. `m`-measurable, verifies an integrability condition and has same integral
as `f` on all `m`-measurable sets, then it is a.e. equal to `Î¼[f|hm]`. -/
theorem ae_eq_condexp_of_forall_setIntegral_eq (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    {f g : Î± â†’ F'} (hf : Integrable f Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hg_eq : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, g x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼)
    (hgm : AEStronglyMeasurable' m g Î¼) : g =áµ[Î¼] Î¼[f|m] := by
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hm hg_int_finite
    (fun s _ _ => integrable_condexp.integrableOn) (fun s hs hÎ¼s => ?_) hgm
    (StronglyMeasurable.aeStronglyMeasurable' stronglyMeasurable_condexp)
  rw [hg_eq s hs hÎ¼s, setIntegral_condexp hm hf hs]
#align measure_theory.ae_eq_condexp_of_forall_set_integral_eq MeasureTheory.ae_eq_condexp_of_forall_setIntegral_eq

@[deprecated (since := "2024-04-17")]
alias ae_eq_condexp_of_forall_set_integral_eq := ae_eq_condexp_of_forall_setIntegral_eq

theorem condexp_bot' [hÎ¼ : NeZero Î¼] (f : Î± â†’ F') :
    Î¼[f|âŠ¥] = fun _ => (Î¼ Set.univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  by_cases hÎ¼_finite : IsFiniteMeasure Î¼
  swap
  Â· have h : Â¬SigmaFinite (Î¼.trim bot_le) := by rwa [sigmaFinite_trim_bot_iff]
    rw [not_isFiniteMeasure_iff] at hÎ¼_finite
    rw [condexp_of_not_sigmaFinite bot_le h]
    simp only [hÎ¼_finite, ENNReal.top_toReal, inv_zero, zero_smul]
    rfl
  by_cases hf : Integrable f Î¼
  swap; Â· rw [integral_undef hf, smul_zero, condexp_undef hf]; rfl
  have h_meas : StronglyMeasurable[âŠ¥] (Î¼[f|âŠ¥]) := stronglyMeasurable_condexp
  obtain âŸ¨c, h_eqâŸ© := stronglyMeasurable_bot_iff.mp h_meas
  rw [h_eq]
  have h_integral : âˆ« x, (Î¼[f|âŠ¥]) x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := integral_condexp bot_le hf
  simp_rw [h_eq, integral_const] at h_integral
  rw [â† h_integral, â† smul_assoc, smul_eq_mul, inv_mul_cancel, one_smul]
  rw [Ne, ENNReal.toReal_eq_zero_iff, not_or]
  exact âŸ¨NeZero.ne _, measure_ne_top Î¼ Set.univâŸ©
#align measure_theory.condexp_bot' MeasureTheory.condexp_bot'

theorem condexp_bot_ae_eq (f : Î± â†’ F') :
    Î¼[f|âŠ¥] =áµ[Î¼] fun _ => (Î¼ Set.univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rcases eq_zero_or_neZero Î¼ with rfl | hÎ¼
  Â· rw [ae_zero]; exact eventually_bot
  Â· exact eventually_of_forall <| congr_fun (condexp_bot' f)
#align measure_theory.condexp_bot_ae_eq MeasureTheory.condexp_bot_ae_eq

theorem condexp_bot [IsProbabilityMeasure Î¼] (f : Î± â†’ F') : Î¼[f|âŠ¥] = fun _ => âˆ« x, f x âˆ‚Î¼ := by
  refine (condexp_bot' f).trans ?_; rw [measure_univ, ENNReal.one_toReal, inv_one, one_smul]
#align measure_theory.condexp_bot MeasureTheory.condexp_bot

theorem condexp_add (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼[f + g|m] =áµ[Î¼] Î¼[f|m] + Î¼[g|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condexp_of_not_le hm]; simp
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condexp_of_not_sigmaFinite hm hÎ¼m]; simp
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  refine (condexp_ae_eq_condexpL1 hm _).trans ?_
  rw [condexpL1_add hf hg]
  exact (coeFn_add _ _).trans
    ((condexp_ae_eq_condexpL1 hm _).symm.add (condexp_ae_eq_condexpL1 hm _).symm)
#align measure_theory.condexp_add MeasureTheory.condexp_add

theorem condexp_finset_sum {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ F'}
    (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) : Î¼[âˆ‘ i âˆˆ s, f i|m] =áµ[Î¼] âˆ‘ i âˆˆ s, Î¼[f i|m] := by
  induction' s using Finset.induction_on with i s his heq hf
  Â· rw [Finset.sum_empty, Finset.sum_empty, condexp_zero]
  Â· rw [Finset.sum_insert his, Finset.sum_insert his]
    exact (condexp_add (hf i <| Finset.mem_insert_self i s) <|
      integrable_finset_sum' _ fun j hmem => hf j <| Finset.mem_insert_of_mem hmem).trans
        ((EventuallyEq.refl _ _).add (heq fun j hmem => hf j <| Finset.mem_insert_of_mem hmem))
#align measure_theory.condexp_finset_sum MeasureTheory.condexp_finset_sum

theorem condexp_smul (c : ğ•œ) (f : Î± â†’ F') : Î¼[c â€¢ f|m] =áµ[Î¼] c â€¢ Î¼[f|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condexp_of_not_le hm]; simp
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condexp_of_not_sigmaFinite hm hÎ¼m]; simp
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  refine (condexp_ae_eq_condexpL1 hm _).trans ?_
  rw [condexpL1_smul c f]
  refine (@condexp_ae_eq_condexpL1 _ _ _ _ _ m _ _ hm _ f).mp ?_
  refine (coeFn_smul c (condexpL1 hm Î¼ f)).mono fun x hx1 hx2 => ?_
  simp only [hx1, hx2, Pi.smul_apply]
#align measure_theory.condexp_smul MeasureTheory.condexp_smul

theorem condexp_neg (f : Î± â†’ F') : Î¼[-f|m] =áµ[Î¼] -Î¼[f|m] := by
  letI : Module â„ (Î± â†’ F') := @Pi.module Î± (fun _ => F') â„ _ _ fun _ => inferInstance
  calc
    Î¼[-f|m] = Î¼[(-1 : â„) â€¢ f|m] := by rw [neg_one_smul â„ f]
    _ =áµ[Î¼] (-1 : â„) â€¢ Î¼[f|m] := condexp_smul (-1) f
    _ = -Î¼[f|m] := neg_one_smul â„ (Î¼[f|m])
#align measure_theory.condexp_neg MeasureTheory.condexp_neg

theorem condexp_sub (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼[f - g|m] =áµ[Î¼] Î¼[f|m] - Î¼[g|m] := by
  simp_rw [sub_eq_add_neg]
  exact (condexp_add hf hg.neg).trans (EventuallyEq.rfl.add (condexp_neg g))
#align measure_theory.condexp_sub MeasureTheory.condexp_sub

theorem condexp_condexp_of_le {mâ‚ mâ‚‚ m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} (hmâ‚â‚‚ : mâ‚ â‰¤ mâ‚‚)
    (hmâ‚‚ : mâ‚‚ â‰¤ m0) [SigmaFinite (Î¼.trim hmâ‚‚)] : Î¼[Î¼[f|mâ‚‚]|mâ‚] =áµ[Î¼] Î¼[f|mâ‚] := by
  by_cases hÎ¼mâ‚ : SigmaFinite (Î¼.trim (hmâ‚â‚‚.trans hmâ‚‚))
  swap; Â· simp_rw [condexp_of_not_sigmaFinite (hmâ‚â‚‚.trans hmâ‚‚) hÎ¼mâ‚]; rfl
  haveI : SigmaFinite (Î¼.trim (hmâ‚â‚‚.trans hmâ‚‚)) := hÎ¼mâ‚
  by_cases hf : Integrable f Î¼
  swap; Â· simp_rw [condexp_undef hf, condexp_zero]; rfl
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' (hmâ‚â‚‚.trans hmâ‚‚)
    (fun s _ _ => integrable_condexp.integrableOn)
    (fun s _ _ => integrable_condexp.integrableOn) ?_
    (StronglyMeasurable.aeStronglyMeasurable' stronglyMeasurable_condexp)
    (StronglyMeasurable.aeStronglyMeasurable' stronglyMeasurable_condexp)
  intro s hs _
  rw [setIntegral_condexp (hmâ‚â‚‚.trans hmâ‚‚) integrable_condexp hs]
  rw [setIntegral_condexp (hmâ‚â‚‚.trans hmâ‚‚) hf hs, setIntegral_condexp hmâ‚‚ hf (hmâ‚â‚‚ s hs)]
#align measure_theory.condexp_condexp_of_le MeasureTheory.condexp_condexp_of_le

theorem condexp_mono {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f g : Î± â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) (hfg : f â‰¤áµ[Î¼] g) :
    Î¼[f|m] â‰¤áµ[Î¼] Î¼[g|m] := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condexp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condexp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  exact (condexp_ae_eq_condexpL1 hm _).trans_le
    ((condexpL1_mono hf hg hfg).trans_eq (condexp_ae_eq_condexpL1 hm _).symm)
#align measure_theory.condexp_mono MeasureTheory.condexp_mono

theorem condexp_nonneg {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f : Î± â†’ E} (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤áµ[Î¼] Î¼[f|m] := by
  by_cases hfint : Integrable f Î¼
  Â· rw [(condexp_zero.symm : (0 : Î± â†’ E) = Î¼[0|m])]
    exact condexp_mono (integrable_zero _ _ _) hfint hf
  Â· rw [condexp_undef hfint]
#align measure_theory.condexp_nonneg MeasureTheory.condexp_nonneg

theorem condexp_nonpos {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f : Î± â†’ E} (hf : f â‰¤áµ[Î¼] 0) : Î¼[f|m] â‰¤áµ[Î¼] 0 := by
  by_cases hfint : Integrable f Î¼
  Â· rw [(condexp_zero.symm : (0 : Î± â†’ E) = Î¼[0|m])]
    exact condexp_mono hfint (integrable_zero _ _ _) hf
  Â· rw [condexp_undef hfint]
#align measure_theory.condexp_nonpos MeasureTheory.condexp_nonpos

/-- **Lebesgue dominated convergence theorem**: sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their image by
  `condexpL1`. -/
theorem tendsto_condexpL1_of_dominated_convergence (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    {fs : â„• â†’ Î± â†’ F'} {f : Î± â†’ F'} (bound_fs : Î± â†’ â„)
    (hfs_meas : âˆ€ n, AEStronglyMeasurable (fs n) Î¼) (h_int_bound_fs : Integrable bound_fs Î¼)
    (hfs_bound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–fs n xâ€– â‰¤ bound_fs x)
    (hfs : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => fs n x) atTop (ğ“ (f x))) :
    Tendsto (fun n => condexpL1 hm Î¼ (fs n)) atTop (ğ“ (condexpL1 hm Î¼ f)) :=
  tendsto_setToFun_of_dominated_convergence _ bound_fs hfs_meas h_int_bound_fs hfs_bound hfs
set_option linter.uppercaseLean3 false in
#align measure_theory.tendsto_condexp_L1_of_dominated_convergence MeasureTheory.tendsto_condexpL1_of_dominated_convergence

/-- If two sequences of functions have a.e. equal conditional expectations at each step, converge
and verify dominated convergence hypotheses, then the conditional expectations of their limits are
a.e. equal. -/
theorem tendsto_condexp_unique (fs gs : â„• â†’ Î± â†’ F') (f g : Î± â†’ F')
    (hfs_int : âˆ€ n, Integrable (fs n) Î¼) (hgs_int : âˆ€ n, Integrable (gs n) Î¼)
    (hfs : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => fs n x) atTop (ğ“ (f x)))
    (hgs : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => gs n x) atTop (ğ“ (g x))) (bound_fs : Î± â†’ â„)
    (h_int_bound_fs : Integrable bound_fs Î¼) (bound_gs : Î± â†’ â„)
    (h_int_bound_gs : Integrable bound_gs Î¼) (hfs_bound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–fs n xâ€– â‰¤ bound_fs x)
    (hgs_bound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–gs n xâ€– â‰¤ bound_gs x) (hfg : âˆ€ n, Î¼[fs n|m] =áµ[Î¼] Î¼[gs n|m]) :
    Î¼[f|m] =áµ[Î¼] Î¼[g|m] := by
  by_cases hm : m â‰¤ m0; swap; Â· simp_rw [condexp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm); swap; Â· simp_rw [condexp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  refine (condexp_ae_eq_condexpL1 hm f).trans ((condexp_ae_eq_condexpL1 hm g).trans ?_).symm
  rw [â† Lp.ext_iff]
  have hn_eq : âˆ€ n, condexpL1 hm Î¼ (gs n) = condexpL1 hm Î¼ (fs n) := by
    intro n
    ext1
    refine (condexp_ae_eq_condexpL1 hm (gs n)).symm.trans ((hfg n).symm.trans ?_)
    exact condexp_ae_eq_condexpL1 hm (fs n)
  have hcond_fs : Tendsto (fun n => condexpL1 hm Î¼ (fs n)) atTop (ğ“ (condexpL1 hm Î¼ f)) :=
    tendsto_condexpL1_of_dominated_convergence hm _ (fun n => (hfs_int n).1) h_int_bound_fs
      hfs_bound hfs
  have hcond_gs : Tendsto (fun n => condexpL1 hm Î¼ (gs n)) atTop (ğ“ (condexpL1 hm Î¼ g)) :=
    tendsto_condexpL1_of_dominated_convergence hm _ (fun n => (hgs_int n).1) h_int_bound_gs
      hgs_bound hgs
  exact tendsto_nhds_unique_of_eventuallyEq hcond_gs hcond_fs (eventually_of_forall hn_eq)
#align measure_theory.tendsto_condexp_unique MeasureTheory.tendsto_condexp_unique

end MeasureTheory
