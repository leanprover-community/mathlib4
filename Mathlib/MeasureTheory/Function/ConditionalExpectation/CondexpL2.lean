/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Unique
import Mathlib.MeasureTheory.Function.L2Space

#align_import measure_theory.function.conditional_expectation.condexp_L2 from "leanprover-community/mathlib"@"d8bbb04e2d2a44596798a9207ceefc0fb236e41e"

/-! # Conditional expectation in L2

This file contains one step of the construction of the conditional expectation, which is completed
in `MeasureTheory.Function.ConditionalExpectation.Basic`. See that file for a description of the
full process.

We build the conditional expectation of an `LÂ²` function, as an element of `LÂ²`. This is the
orthogonal projection on the subspace of almost everywhere `m`-measurable functions.

## Main definitions

* `condexpL2`: Conditional expectation of a function in L2 with respect to a sigma-algebra: it is
the orthogonal projection on the subspace `lpMeas`.

## Implementation notes

Most of the results in this file are valid for a complete real normed space `F`.
However, some lemmas also use `ğ•œ : RCLike`:
* `condexpL2` is defined only for an `InnerProductSpace` for now, and we use `ğ•œ` for its field.
* results about scalar multiplication are stated not only for `â„` but also for `ğ•œ` if we happen to
  have `NormedSpace ğ•œ F`.

-/

set_option linter.uppercaseLean3 false

open TopologicalSpace Filter ContinuousLinearMap

open scoped ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± E E' F G G' ğ•œ : Type*} {p : â„â‰¥0âˆ} [RCLike ğ•œ]
  -- ğ•œ for â„ or â„‚
  -- E for an inner product space
  [NormedAddCommGroup E]
  [InnerProductSpace ğ•œ E] [CompleteSpace E]
  -- E' for an inner product space on which we compute integrals
  [NormedAddCommGroup E']
  [InnerProductSpace ğ•œ E'] [CompleteSpace E'] [NormedSpace â„ E']
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ğ•œ F]
  -- G for a Lp add_subgroup
  [NormedAddCommGroup G]
  -- G' for integrals on a Lp add_subgroup
  [NormedAddCommGroup G']
  [NormedSpace â„ G'] [CompleteSpace G']

variable {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {s t : Set Î±}

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ E _ x y

local notation "âŸª" x ", " y "âŸ«â‚‚" => @inner ğ•œ (Î± â†’â‚‚[Î¼] E) _ x y

-- Porting note: the argument `E` of `condexpL2` is not automatically filled in Lean 4.
-- To avoid typing `(E := _)` every time it is made explicit.
variable (E ğ•œ)

/-- Conditional expectation of a function in L2 with respect to a sigma-algebra -/
noncomputable def condexpL2 (hm : m â‰¤ m0) : (Î± â†’â‚‚[Î¼] E) â†’L[ğ•œ] lpMeas E ğ•œ m 2 Î¼ :=
  @orthogonalProjection ğ•œ (Î± â†’â‚‚[Î¼] E) _ _ _ (lpMeas E ğ•œ m 2 Î¼)
    haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
    inferInstance
#align measure_theory.condexp_L2 MeasureTheory.condexpL2

variable {E ğ•œ}

theorem aeStronglyMeasurable'_condexpL2 (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
    AEStronglyMeasurable' (Î² := E) m (condexpL2 E ğ•œ hm f) Î¼ :=
  lpMeas.aeStronglyMeasurable' _
#align measure_theory.ae_strongly_measurable'_condexp_L2 MeasureTheory.aeStronglyMeasurable'_condexpL2

theorem integrableOn_condexpL2_of_measure_ne_top (hm : m â‰¤ m0) (hÎ¼s : Î¼ s â‰  âˆ) (f : Î± â†’â‚‚[Î¼] E) :
    IntegrableOn (E := E) (condexpL2 E ğ•œ hm f) s Î¼ :=
  integrableOn_Lp_of_measure_ne_top (condexpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E) fact_one_le_two_ennreal.elim
    hÎ¼s
#align measure_theory.integrable_on_condexp_L2_of_measure_ne_top MeasureTheory.integrableOn_condexpL2_of_measure_ne_top

theorem integrable_condexpL2_of_isFiniteMeasure (hm : m â‰¤ m0) [IsFiniteMeasure Î¼] {f : Î± â†’â‚‚[Î¼] E} :
    Integrable (Î² := E) (condexpL2 E ğ•œ hm f) Î¼ :=
  integrableOn_univ.mp <| integrableOn_condexpL2_of_measure_ne_top hm (measure_ne_top _ _) f
#align measure_theory.integrable_condexp_L2_of_is_finite_measure MeasureTheory.integrable_condexpL2_of_isFiniteMeasure

theorem norm_condexpL2_le_one (hm : m â‰¤ m0) : â€–@condexpL2 Î± E ğ•œ _ _ _ _ _ _ Î¼ hmâ€– â‰¤ 1 :=
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  orthogonalProjection_norm_le _
#align measure_theory.norm_condexp_L2_le_one MeasureTheory.norm_condexpL2_le_one

theorem norm_condexpL2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) : â€–condexpL2 E ğ•œ hm fâ€– â‰¤ â€–fâ€– :=
  ((@condexpL2 _ E ğ•œ _ _ _ _ _ _ Î¼ hm).le_opNorm f).trans
    (mul_le_of_le_one_left (norm_nonneg _) (norm_condexpL2_le_one hm))
#align measure_theory.norm_condexp_L2_le MeasureTheory.norm_condexpL2_le

theorem snorm_condexpL2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
    snorm (F := E) (condexpL2 E ğ•œ hm f) 2 Î¼ â‰¤ snorm f 2 Î¼ := by
  rw [lpMeas_coe, â† ENNReal.toReal_le_toReal (Lp.snorm_ne_top _) (Lp.snorm_ne_top _), â†
    Lp.norm_def, â† Lp.norm_def, Submodule.norm_coe]
  exact norm_condexpL2_le hm f
#align measure_theory.snorm_condexp_L2_le MeasureTheory.snorm_condexpL2_le

theorem norm_condexpL2_coe_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
    â€–(condexpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E)â€– â‰¤ â€–fâ€– := by
  rw [Lp.norm_def, Lp.norm_def, â† lpMeas_coe]
  refine (ENNReal.toReal_le_toReal ?_ (Lp.snorm_ne_top _)).mpr (snorm_condexpL2_le hm f)
  exact Lp.snorm_ne_top _
#align measure_theory.norm_condexp_L2_coe_le MeasureTheory.norm_condexpL2_coe_le

theorem inner_condexpL2_left_eq_right (hm : m â‰¤ m0) {f g : Î± â†’â‚‚[Î¼] E} :
    âŸª(condexpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ«â‚‚ = âŸªf, (condexpL2 E ğ•œ hm g : Î± â†’â‚‚[Î¼] E)âŸ«â‚‚ :=
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  inner_orthogonalProjection_left_eq_right _ f g
#align measure_theory.inner_condexp_L2_left_eq_right MeasureTheory.inner_condexpL2_left_eq_right

theorem condexpL2_indicator_of_measurable (hm : m â‰¤ m0) (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ)
    (c : E) :
    (condexpL2 E ğ•œ hm (indicatorConstLp 2 (hm s hs) hÎ¼s c) : Î± â†’â‚‚[Î¼] E) =
      indicatorConstLp 2 (hm s hs) hÎ¼s c := by
  rw [condexpL2]
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  have h_mem : indicatorConstLp 2 (hm s hs) hÎ¼s c âˆˆ lpMeas E ğ•œ m 2 Î¼ :=
    mem_lpMeas_indicatorConstLp hm hs hÎ¼s
  let ind := (âŸ¨indicatorConstLp 2 (hm s hs) hÎ¼s c, h_memâŸ© : lpMeas E ğ•œ m 2 Î¼)
  have h_coe_ind : (ind : Î± â†’â‚‚[Î¼] E) = indicatorConstLp 2 (hm s hs) hÎ¼s c := rfl
  have h_orth_mem := orthogonalProjection_mem_subspace_eq_self ind
  rw [â† h_coe_ind, h_orth_mem]
#align measure_theory.condexp_L2_indicator_of_measurable MeasureTheory.condexpL2_indicator_of_measurable

theorem inner_condexpL2_eq_inner_fun (hm : m â‰¤ m0) (f g : Î± â†’â‚‚[Î¼] E)
    (hg : AEStronglyMeasurable' m g Î¼) :
    âŸª(condexpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ«â‚‚ = âŸªf, gâŸ«â‚‚ := by
  symm
  rw [â† sub_eq_zero, â† inner_sub_left, condexpL2]
  simp only [mem_lpMeas_iff_aeStronglyMeasurable'.mpr hg, orthogonalProjection_inner_eq_zero f g]
#align measure_theory.inner_condexp_L2_eq_inner_fun MeasureTheory.inner_condexpL2_eq_inner_fun

section Real

variable {hm : m â‰¤ m0}

theorem integral_condexpL2_eq_of_fin_meas_real (f : Lp ğ•œ 2 Î¼) (hs : MeasurableSet[m] s)
    (hÎ¼s : Î¼ s â‰  âˆ) : âˆ« x in s, (condexpL2 ğ•œ ğ•œ hm f : Î± â†’ ğ•œ) x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† L2.inner_indicatorConstLp_one (ğ•œ := ğ•œ) (hm s hs) hÎ¼s f]
  have h_eq_inner : âˆ« x in s, (condexpL2 ğ•œ ğ•œ hm f : Î± â†’ ğ•œ) x âˆ‚Î¼ =
      inner (indicatorConstLp 2 (hm s hs) hÎ¼s (1 : ğ•œ)) (condexpL2 ğ•œ ğ•œ hm f) := by
    rw [L2.inner_indicatorConstLp_one (hm s hs) hÎ¼s]
  rw [h_eq_inner, â† inner_condexpL2_left_eq_right, condexpL2_indicator_of_measurable hm hs hÎ¼s]
#align measure_theory.integral_condexp_L2_eq_of_fin_meas_real MeasureTheory.integral_condexpL2_eq_of_fin_meas_real

theorem lintegral_nnnorm_condexpL2_le (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp â„ 2 Î¼) :
    âˆ«â» x in s, â€–(condexpL2 â„ â„ hm f : Î± â†’ â„) xâ€–â‚Š âˆ‚Î¼ â‰¤ âˆ«â» x in s, â€–f xâ€–â‚Š âˆ‚Î¼ := by
  let h_meas := lpMeas.aeStronglyMeasurable' (condexpL2 â„ â„ hm f)
  let g := h_meas.choose
  have hg_meas : StronglyMeasurable[m] g := h_meas.choose_spec.1
  have hg_eq : g =áµ[Î¼] condexpL2 â„ â„ hm f := h_meas.choose_spec.2.symm
  have hg_eq_restrict : g =áµ[Î¼.restrict s] condexpL2 â„ â„ hm f := ae_restrict_of_ae hg_eq
  have hg_nnnorm_eq : (fun x => (â€–g xâ€–â‚Š : â„â‰¥0âˆ)) =áµ[Î¼.restrict s] fun x =>
      (â€–(condexpL2 â„ â„ hm f : Î± â†’ â„) xâ€–â‚Š : â„â‰¥0âˆ) := by
    refine hg_eq_restrict.mono fun x hx => ?_
    dsimp only
    simp_rw [hx]
  rw [lintegral_congr_ae hg_nnnorm_eq.symm]
  refine lintegral_nnnorm_le_of_forall_fin_meas_integral_eq
    hm (Lp.stronglyMeasurable f) ?_ ?_ ?_ ?_ hs hÎ¼s
  Â· exact integrableOn_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s
  Â· exact hg_meas
  Â· rw [IntegrableOn, integrable_congr hg_eq_restrict]
    exact integrableOn_condexpL2_of_measure_ne_top hm hÎ¼s f
  Â· intro t ht hÎ¼t
    rw [â† integral_condexpL2_eq_of_fin_meas_real f ht hÎ¼t.ne]
    exact setIntegral_congr_ae (hm t ht) (hg_eq.mono fun x hx _ => hx)
#align measure_theory.lintegral_nnnorm_condexp_L2_le MeasureTheory.lintegral_nnnorm_condexpL2_le

theorem condexpL2_ae_eq_zero_of_ae_eq_zero (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) {f : Lp â„ 2 Î¼}
    (hf : f =áµ[Î¼.restrict s] 0) : condexpL2 â„ â„ hm f =áµ[Î¼.restrict s] (0 : Î± â†’ â„) := by
  suffices h_nnnorm_eq_zero : âˆ«â» x in s, â€–(condexpL2 â„ â„ hm f : Î± â†’ â„) xâ€–â‚Š âˆ‚Î¼ = 0 by
    rw [lintegral_eq_zero_iff] at h_nnnorm_eq_zero
    Â· refine h_nnnorm_eq_zero.mono fun x hx => ?_
      dsimp only at hx
      rw [Pi.zero_apply] at hx âŠ¢
      Â· rwa [ENNReal.coe_eq_zero, nnnorm_eq_zero] at hx
    Â· refine Measurable.coe_nnreal_ennreal (Measurable.nnnorm ?_)
      rw [lpMeas_coe]
      exact (Lp.stronglyMeasurable _).measurable
  refine le_antisymm ?_ (zero_le _)
  refine (lintegral_nnnorm_condexpL2_le hs hÎ¼s f).trans (le_of_eq ?_)
  rw [lintegral_eq_zero_iff]
  Â· refine hf.mono fun x hx => ?_
    dsimp only
    rw [hx]
    simp
  Â· exact (Lp.stronglyMeasurable _).ennnorm
#align measure_theory.condexp_L2_ae_eq_zero_of_ae_eq_zero MeasureTheory.condexpL2_ae_eq_zero_of_ae_eq_zero

theorem lintegral_nnnorm_condexpL2_indicator_le_real (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (ht : MeasurableSet[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
    âˆ«â» a in t, â€–(condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ (s âˆ© t) := by
  refine (lintegral_nnnorm_condexpL2_le ht hÎ¼t _).trans (le_of_eq ?_)
  have h_eq :
    âˆ«â» x in t, â€–(indicatorConstLp 2 hs hÎ¼s (1 : â„)) xâ€–â‚Š âˆ‚Î¼ =
      âˆ«â» x in t, s.indicator (fun _ => (1 : â„â‰¥0âˆ)) x âˆ‚Î¼ := by
    refine lintegral_congr_ae (ae_restrict_of_ae ?_)
    refine (@indicatorConstLp_coeFn _ _ _ 2 _ _ _ hs hÎ¼s (1 : â„)).mono fun x hx => ?_
    dsimp only
    rw [hx]
    classical
    simp_rw [Set.indicator_apply]
    split_ifs <;> simp
  rw [h_eq, lintegral_indicator _ hs, lintegral_const, Measure.restrict_restrict hs]
  simp only [one_mul, Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply]
#align measure_theory.lintegral_nnnorm_condexp_L2_indicator_le_real MeasureTheory.lintegral_nnnorm_condexpL2_indicator_le_real

end Real

/-- `condexpL2` commutes with taking inner products with constants. See the lemma
`condexpL2_comp_continuousLinearMap` for a more general result about commuting with continuous
linear maps. -/
theorem condexpL2_const_inner (hm : m â‰¤ m0) (f : Lp E 2 Î¼) (c : E) :
    condexpL2 ğ•œ ğ•œ hm (((Lp.memâ„’p f).const_inner c).toLp fun a => âŸªc, f aâŸ«) =áµ[Î¼]
    fun a => âŸªc, (condexpL2 E ğ•œ hm f : Î± â†’ E) aâŸ« := by
  rw [lpMeas_coe]
  have h_mem_Lp : Memâ„’p (fun a => âŸªc, (condexpL2 E ğ•œ hm f : Î± â†’ E) aâŸ«) 2 Î¼ := by
    refine Memâ„’p.const_inner _ ?_; rw [lpMeas_coe]; exact Lp.memâ„’p _
  have h_eq : h_mem_Lp.toLp _ =áµ[Î¼] fun a => âŸªc, (condexpL2 E ğ•œ hm f : Î± â†’ E) aâŸ« :=
    h_mem_Lp.coeFn_toLp
  refine EventuallyEq.trans ?_ h_eq
  refine Lp.ae_eq_of_forall_setIntegral_eq' ğ•œ hm _ _ two_ne_zero ENNReal.coe_ne_top
    (fun s _ hÎ¼s => integrableOn_condexpL2_of_measure_ne_top hm hÎ¼s.ne _) ?_ ?_ ?_ ?_
  Â· intro s _ hÎ¼s
    rw [IntegrableOn, integrable_congr (ae_restrict_of_ae h_eq)]
    exact (integrableOn_condexpL2_of_measure_ne_top hm hÎ¼s.ne _).const_inner _
  Â· intro s hs hÎ¼s
    rw [â† lpMeas_coe, integral_condexpL2_eq_of_fin_meas_real _ hs hÎ¼s.ne,
      integral_congr_ae (ae_restrict_of_ae h_eq), lpMeas_coe, â†
      L2.inner_indicatorConstLp_eq_setIntegral_inner ğ•œ (â†‘(condexpL2 E ğ•œ hm f)) (hm s hs) c hÎ¼s.ne,
      â† inner_condexpL2_left_eq_right, condexpL2_indicator_of_measurable _ hs,
      L2.inner_indicatorConstLp_eq_setIntegral_inner ğ•œ f (hm s hs) c hÎ¼s.ne,
      setIntegral_congr_ae (hm s hs)
        ((Memâ„’p.coeFn_toLp ((Lp.memâ„’p f).const_inner c)).mono fun x hx _ => hx)]
  Â· rw [â† lpMeas_coe]; exact lpMeas.aeStronglyMeasurable' _
  Â· refine AEStronglyMeasurable'.congr ?_ h_eq.symm
    exact (lpMeas.aeStronglyMeasurable' _).const_inner _
#align measure_theory.condexp_L2_const_inner MeasureTheory.condexpL2_const_inner

/-- `condexpL2` verifies the equality of integrals defining the conditional expectation. -/
theorem integral_condexpL2_eq (hm : m â‰¤ m0) (f : Lp E' 2 Î¼) (hs : MeasurableSet[m] s)
    (hÎ¼s : Î¼ s â‰  âˆ) : âˆ« x in s, (condexpL2 E' ğ•œ hm f : Î± â†’ E') x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† sub_eq_zero, lpMeas_coe, â†
    integral_sub' (integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s)
      (integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s)]
  refine integral_eq_zero_of_forall_integral_inner_eq_zero ğ•œ _ ?_ ?_
  Â· rw [integrable_congr (ae_restrict_of_ae (Lp.coeFn_sub (â†‘(condexpL2 E' ğ•œ hm f)) f).symm)]
    exact integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s
  intro c
  simp_rw [Pi.sub_apply, inner_sub_right]
  rw [integral_sub
      ((integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s).const_inner c)
      ((integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s).const_inner c)]
  have h_ae_eq_f := Memâ„’p.coeFn_toLp (E := ğ•œ) ((Lp.memâ„’p f).const_inner c)
  rw [â† lpMeas_coe, sub_eq_zero, â†
    setIntegral_congr_ae (hm s hs) ((condexpL2_const_inner hm f c).mono fun x hx _ => hx), â†
    setIntegral_congr_ae (hm s hs) (h_ae_eq_f.mono fun x hx _ => hx)]
  exact integral_condexpL2_eq_of_fin_meas_real _ hs hÎ¼s
#align measure_theory.integral_condexp_L2_eq MeasureTheory.integral_condexpL2_eq

variable {E'' ğ•œ' : Type*} [RCLike ğ•œ'] [NormedAddCommGroup E''] [InnerProductSpace ğ•œ' E'']
  [CompleteSpace E''] [NormedSpace â„ E'']

variable (ğ•œ ğ•œ')

theorem condexpL2_comp_continuousLinearMap (hm : m â‰¤ m0) (T : E' â†’L[â„] E'') (f : Î± â†’â‚‚[Î¼] E') :
    (condexpL2 E'' ğ•œ' hm (T.compLp f) : Î± â†’â‚‚[Î¼] E'') =áµ[Î¼]
    T.compLp (condexpL2 E' ğ•œ hm f : Î± â†’â‚‚[Î¼] E') := by
  refine Lp.ae_eq_of_forall_setIntegral_eq' ğ•œ' hm _ _ two_ne_zero ENNReal.coe_ne_top
    (fun s _ hÎ¼s => integrableOn_condexpL2_of_measure_ne_top hm hÎ¼s.ne _) (fun s _ hÎ¼s =>
      integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s.ne) ?_ ?_ ?_
  Â· intro s hs hÎ¼s
    rw [T.setIntegral_compLp _ (hm s hs),
      T.integral_comp_comm
        (integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s.ne),
      â† lpMeas_coe, â† lpMeas_coe, integral_condexpL2_eq hm f hs hÎ¼s.ne,
      integral_condexpL2_eq hm (T.compLp f) hs hÎ¼s.ne, T.setIntegral_compLp _ (hm s hs),
      T.integral_comp_comm
        (integrableOn_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s.ne)]
  Â· rw [â† lpMeas_coe]; exact lpMeas.aeStronglyMeasurable' _
  Â· have h_coe := T.coeFn_compLp (condexpL2 E' ğ•œ hm f : Î± â†’â‚‚[Î¼] E')
    rw [â† EventuallyEq] at h_coe
    refine AEStronglyMeasurable'.congr ?_ h_coe.symm
    exact (lpMeas.aeStronglyMeasurable' (condexpL2 E' ğ•œ hm f)).continuous_comp T.continuous
#align measure_theory.condexp_L2_comp_continuous_linear_map MeasureTheory.condexpL2_comp_continuousLinearMap

variable {ğ•œ ğ•œ'}

section CondexpL2Indicator

variable (ğ•œ)

theorem condexpL2_indicator_ae_eq_smul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : E') :
    condexpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) =áµ[Î¼] fun a =>
      (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s (1 : â„)) : Î± â†’ â„) a â€¢ x := by
  rw [indicatorConstLp_eq_toSpanSingleton_compLp hs hÎ¼s x]
  have h_comp :=
    condexpL2_comp_continuousLinearMap â„ ğ•œ hm (toSpanSingleton â„ x)
      (indicatorConstLp 2 hs hÎ¼s (1 : â„))
  rw [â† lpMeas_coe] at h_comp
  refine h_comp.trans ?_
  exact (toSpanSingleton â„ x).coeFn_compLp _
#align measure_theory.condexp_L2_indicator_ae_eq_smul MeasureTheory.condexpL2_indicator_ae_eq_smul

theorem condexpL2_indicator_eq_toSpanSingleton_comp (hm : m â‰¤ m0) (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (x : E') : (condexpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’â‚‚[Î¼] E') =
    (toSpanSingleton â„ x).compLp (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1)) := by
  ext1
  rw [â† lpMeas_coe]
  refine (condexpL2_indicator_ae_eq_smul ğ•œ hm hs hÎ¼s x).trans ?_
  have h_comp := (toSpanSingleton â„ x).coeFn_compLp
    (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’â‚‚[Î¼] â„)
  rw [â† EventuallyEq] at h_comp
  refine EventuallyEq.trans ?_ h_comp.symm
  filter_upwards with y using rfl
#align measure_theory.condexp_L2_indicator_eq_to_span_singleton_comp MeasureTheory.condexpL2_indicator_eq_toSpanSingleton_comp

variable {ğ•œ}

theorem set_lintegral_nnnorm_condexpL2_indicator_le (hm : m â‰¤ m0) (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (x : E') {t : Set Î±} (ht : MeasurableSet[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
    âˆ«â» a in t, â€–(condexpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’ E') aâ€–â‚Š âˆ‚Î¼ â‰¤
    Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
  calc
    âˆ«â» a in t, â€–(condexpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’ E') aâ€–â‚Š âˆ‚Î¼ =
        âˆ«â» a in t, â€–(condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) a â€¢ xâ€–â‚Š âˆ‚Î¼ :=
      set_lintegral_congr_fun (hm t ht)
        ((condexpL2_indicator_ae_eq_smul ğ•œ hm hs hÎ¼s x).mono fun a ha _ => by rw [ha])
    _ = (âˆ«â» a in t, â€–(condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) aâ€–â‚Š âˆ‚Î¼) * â€–xâ€–â‚Š := by
      simp_rw [nnnorm_smul, ENNReal.coe_mul]
      rw [lintegral_mul_const, lpMeas_coe]
      exact (Lp.stronglyMeasurable _).ennnorm
    _ â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
      mul_le_mul_right' (lintegral_nnnorm_condexpL2_indicator_le_real hs hÎ¼s ht hÎ¼t) _
#align measure_theory.set_lintegral_nnnorm_condexp_L2_indicator_le MeasureTheory.set_lintegral_nnnorm_condexpL2_indicator_le

theorem lintegral_nnnorm_condexpL2_indicator_le (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : E') [SigmaFinite (Î¼.trim hm)] :
    âˆ«â» a, â€–(condexpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’ E') aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ s * â€–xâ€–â‚Š := by
  refine lintegral_le_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š) ?_ fun t ht hÎ¼t => ?_
  Â· rw [lpMeas_coe]
    exact (Lp.aestronglyMeasurable _).ennnorm
  refine (set_lintegral_nnnorm_condexpL2_indicator_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
  gcongr
  apply Set.inter_subset_left
#align measure_theory.lintegral_nnnorm_condexp_L2_indicator_le MeasureTheory.lintegral_nnnorm_condexpL2_indicator_le

/-- If the measure `Î¼.trim hm` is sigma-finite, then the conditional expectation of a measurable set
with finite measure is integrable. -/
theorem integrable_condexpL2_indicator (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E') :
    Integrable (Î² := E') (condexpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x)) Î¼ := by
  refine integrable_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š)
    (ENNReal.mul_lt_top hÎ¼s ENNReal.coe_ne_top) ?_ ?_
  Â· rw [lpMeas_coe]; exact Lp.aestronglyMeasurable _
  Â· refine fun t ht hÎ¼t =>
      (set_lintegral_nnnorm_condexpL2_indicator_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
    gcongr
    apply Set.inter_subset_left
#align measure_theory.integrable_condexp_L2_indicator MeasureTheory.integrable_condexpL2_indicator

end CondexpL2Indicator

section CondexpIndSMul

variable [NormedSpace â„ G] {hm : m â‰¤ m0}

/-- Conditional expectation of the indicator of a measurable set with finite measure, in L2. -/
noncomputable def condexpIndSMul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    Lp G 2 Î¼ :=
  (toSpanSingleton â„ x).compLpL 2 Î¼ (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s (1 : â„)))
#align measure_theory.condexp_ind_smul MeasureTheory.condexpIndSMul

theorem aeStronglyMeasurable'_condexpIndSMul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) : AEStronglyMeasurable' m (condexpIndSMul hm hs hÎ¼s x) Î¼ := by
  have h : AEStronglyMeasurable' m (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) Î¼ :=
    aeStronglyMeasurable'_condexpL2 _ _
  rw [condexpIndSMul]
  suffices AEStronglyMeasurable' m
      (toSpanSingleton â„ x âˆ˜ condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1)) Î¼ by
    refine AEStronglyMeasurable'.congr this ?_
    refine EventuallyEq.trans ?_ (coeFn_compLpL _ _).symm
    rfl
  exact AEStronglyMeasurable'.continuous_comp (toSpanSingleton â„ x).continuous h
#align measure_theory.ae_strongly_measurable'_condexp_ind_smul MeasureTheory.aeStronglyMeasurable'_condexpIndSMul

theorem condexpIndSMul_add (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x y : G) :
    condexpIndSMul hm hs hÎ¼s (x + y) = condexpIndSMul hm hs hÎ¼s x + condexpIndSMul hm hs hÎ¼s y := by
  simp_rw [condexpIndSMul]; rw [toSpanSingleton_add, add_compLpL, add_apply]
#align measure_theory.condexp_ind_smul_add MeasureTheory.condexpIndSMul_add

theorem condexpIndSMul_smul (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : â„) (x : G) :
    condexpIndSMul hm hs hÎ¼s (c â€¢ x) = c â€¢ condexpIndSMul hm hs hÎ¼s x := by
  simp_rw [condexpIndSMul]; rw [toSpanSingleton_smul, smul_compLpL, smul_apply]
#align measure_theory.condexp_ind_smul_smul MeasureTheory.condexpIndSMul_smul

theorem condexpIndSMul_smul' [NormedSpace â„ F] [SMulCommClass â„ ğ•œ F] (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (c : ğ•œ) (x : F) :
    condexpIndSMul hm hs hÎ¼s (c â€¢ x) = c â€¢ condexpIndSMul hm hs hÎ¼s x := by
  rw [condexpIndSMul, condexpIndSMul, toSpanSingleton_smul',
    (toSpanSingleton â„ x).smul_compLpL c, smul_apply]
#align measure_theory.condexp_ind_smul_smul' MeasureTheory.condexpIndSMul_smul'

theorem condexpIndSMul_ae_eq_smul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    condexpIndSMul hm hs hÎ¼s x =áµ[Î¼] fun a =>
      (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) a â€¢ x :=
  (toSpanSingleton â„ x).coeFn_compLpL _
#align measure_theory.condexp_ind_smul_ae_eq_smul MeasureTheory.condexpIndSMul_ae_eq_smul

theorem set_lintegral_nnnorm_condexpIndSMul_le (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) {t : Set Î±} (ht : MeasurableSet[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
    (âˆ«â» a in t, â€–condexpIndSMul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼) â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
  calc
    âˆ«â» a in t, â€–condexpIndSMul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼ =
        âˆ«â» a in t, â€–(condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) a â€¢ xâ€–â‚Š âˆ‚Î¼ :=
      set_lintegral_congr_fun (hm t ht)
        ((condexpIndSMul_ae_eq_smul hm hs hÎ¼s x).mono fun a ha _ => by rw [ha])
    _ = (âˆ«â» a in t, â€–(condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) aâ€–â‚Š âˆ‚Î¼) * â€–xâ€–â‚Š := by
      simp_rw [nnnorm_smul, ENNReal.coe_mul]
      rw [lintegral_mul_const, lpMeas_coe]
      exact (Lp.stronglyMeasurable _).ennnorm
    _ â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
      mul_le_mul_right' (lintegral_nnnorm_condexpL2_indicator_le_real hs hÎ¼s ht hÎ¼t) _
#align measure_theory.set_lintegral_nnnorm_condexp_ind_smul_le MeasureTheory.set_lintegral_nnnorm_condexpIndSMul_le

theorem lintegral_nnnorm_condexpIndSMul_le (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) [SigmaFinite (Î¼.trim hm)] : âˆ«â» a, â€–condexpIndSMul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ s * â€–xâ€–â‚Š := by
  refine lintegral_le_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š) ?_ fun t ht hÎ¼t => ?_
  Â· exact (Lp.aestronglyMeasurable _).ennnorm
  refine (set_lintegral_nnnorm_condexpIndSMul_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
  gcongr
  apply Set.inter_subset_left
#align measure_theory.lintegral_nnnorm_condexp_ind_smul_le MeasureTheory.lintegral_nnnorm_condexpIndSMul_le

/-- If the measure `Î¼.trim hm` is sigma-finite, then the conditional expectation of a measurable set
with finite measure is integrable. -/
theorem integrable_condexpIndSMul (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (x : G) : Integrable (condexpIndSMul hm hs hÎ¼s x) Î¼ := by
  refine
    integrable_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š) (ENNReal.mul_lt_top hÎ¼s ENNReal.coe_ne_top) ?_
      ?_
  Â· exact Lp.aestronglyMeasurable _
  Â· refine fun t ht hÎ¼t => (set_lintegral_nnnorm_condexpIndSMul_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
    gcongr
    apply Set.inter_subset_left
#align measure_theory.integrable_condexp_ind_smul MeasureTheory.integrable_condexpIndSMul

theorem condexpIndSMul_empty {x : G} : condexpIndSMul hm MeasurableSet.empty
    ((measure_empty (Î¼ := Î¼)).le.trans_lt ENNReal.coe_lt_top).ne x = 0 := by
  rw [condexpIndSMul, indicatorConstLp_empty]
  simp only [Submodule.coe_zero, ContinuousLinearMap.map_zero]
#align measure_theory.condexp_ind_smul_empty MeasureTheory.condexpIndSMul_empty

theorem setIntegral_condexpL2_indicator (hs : MeasurableSet[m] s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) :
    âˆ« x in s, (condexpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) x âˆ‚Î¼ = (Î¼ (t âˆ© s)).toReal :=
  calc
    âˆ« x in s, (condexpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) x âˆ‚Î¼ =
        âˆ« x in s, indicatorConstLp 2 ht hÎ¼t (1 : â„) x âˆ‚Î¼ :=
      @integral_condexpL2_eq Î± _ â„ _ _ _ _ _ _ _ _ _ hm (indicatorConstLp 2 ht hÎ¼t (1 : â„)) hs hÎ¼s
    _ = (Î¼ (t âˆ© s)).toReal â€¢ (1 : â„) := setIntegral_indicatorConstLp (hm s hs) ht hÎ¼t 1
    _ = (Î¼ (t âˆ© s)).toReal := by rw [smul_eq_mul, mul_one]
#align measure_theory.set_integral_condexp_L2_indicator MeasureTheory.setIntegral_condexpL2_indicator

@[deprecated (since := "2024-04-17")]
alias set_integral_condexpL2_indicator := setIntegral_condexpL2_indicator

theorem setIntegral_condexpIndSMul (hs : MeasurableSet[m] s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (x : G') :
    âˆ« a in s, (condexpIndSMul hm ht hÎ¼t x) a âˆ‚Î¼ = (Î¼ (t âˆ© s)).toReal â€¢ x :=
  calc
    âˆ« a in s, (condexpIndSMul hm ht hÎ¼t x) a âˆ‚Î¼ =
        âˆ« a in s, (condexpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) a â€¢ x âˆ‚Î¼ :=
      setIntegral_congr_ae (hm s hs)
        ((condexpIndSMul_ae_eq_smul hm ht hÎ¼t x).mono fun x hx _ => hx)
    _ = (âˆ« a in s, (condexpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) a âˆ‚Î¼) â€¢ x :=
      (integral_smul_const _ x)
    _ = (Î¼ (t âˆ© s)).toReal â€¢ x := by rw [setIntegral_condexpL2_indicator hs ht hÎ¼s hÎ¼t]
#align measure_theory.set_integral_condexp_ind_smul MeasureTheory.setIntegral_condexpIndSMul

@[deprecated (since := "2024-04-17")]
alias set_integral_condexpIndSMul := setIntegral_condexpIndSMul

theorem condexpL2_indicator_nonneg (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    [SigmaFinite (Î¼.trim hm)] : (0 : Î± â†’ â„) â‰¤áµ[Î¼]
    condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) := by
  have h : AEStronglyMeasurable' m (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) Î¼ :=
    aeStronglyMeasurable'_condexpL2 _ _
  refine EventuallyLE.trans_eq ?_ h.ae_eq_mk.symm
  refine @ae_le_of_ae_le_trim _ _ _ _ _ _ hm (0 : Î± â†’ â„) _ ?_
  refine ae_nonneg_of_forall_setIntegral_nonneg_of_sigmaFinite ?_ ?_
  Â· rintro t - -
    refine @Integrable.integrableOn _ _ m _ _ _ _ ?_
    refine Integrable.trim hm ?_ ?_
    Â· rw [integrable_congr h.ae_eq_mk.symm]
      exact integrable_condexpL2_indicator hm hs hÎ¼s _
    Â· exact h.stronglyMeasurable_mk
  Â· intro t ht hÎ¼t
    rw [â† setIntegral_trim hm h.stronglyMeasurable_mk ht]
    have h_ae :
      âˆ€áµ x âˆ‚Î¼, x âˆˆ t â†’ h.mk _ x = (condexpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) x := by
      filter_upwards [h.ae_eq_mk] with x hx
      exact fun _ => hx.symm
    rw [setIntegral_congr_ae (hm t ht) h_ae,
      setIntegral_condexpL2_indicator ht hs ((le_trim hm).trans_lt hÎ¼t).ne hÎ¼s]
    exact ENNReal.toReal_nonneg
#align measure_theory.condexp_L2_indicator_nonneg MeasureTheory.condexpL2_indicator_nonneg

theorem condexpIndSMul_nonneg {E} [NormedLatticeAddCommGroup E] [NormedSpace â„ E] [OrderedSMul â„ E]
    [SigmaFinite (Î¼.trim hm)] (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) (hx : 0 â‰¤ x) :
    (0 : Î± â†’ E) â‰¤áµ[Î¼] condexpIndSMul hm hs hÎ¼s x := by
  refine EventuallyLE.trans_eq ?_ (condexpIndSMul_ae_eq_smul hm hs hÎ¼s x).symm
  filter_upwards [condexpL2_indicator_nonneg hm hs hÎ¼s] with a ha
  exact smul_nonneg ha hx
#align measure_theory.condexp_ind_smul_nonneg MeasureTheory.condexpIndSMul_nonneg

end CondexpIndSMul

end MeasureTheory
