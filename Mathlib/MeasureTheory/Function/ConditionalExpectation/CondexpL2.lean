/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Unique
import Mathlib.MeasureTheory.Function.L2Space

/-! # Conditional expectation in L2

This file contains one step of the construction of the conditional expectation, which is completed
in `MeasureTheory.Function.ConditionalExpectation.Basic`. See that file for a description of the
full process.

We build the conditional expectation of an `LÂ²` function, as an element of `LÂ²`. This is the
orthogonal projection on the subspace of almost everywhere `m`-measurable functions.

## Main definitions

* `condExpL2`: Conditional expectation of a function in L2 with respect to a sigma-algebra: it is
  the orthogonal projection on the subspace `lpMeas`.

## Implementation notes

Most of the results in this file are valid for a complete real normed space `F`.
However, some lemmas also use `ğ•œ : RCLike`:
* `condExpL2` is defined only for an `InnerProductSpace` for now, and we use `ğ•œ` for its field.
* results about scalar multiplication are stated not only for `â„` but also for `ğ•œ` if we happen to
  have `NormedSpace ğ•œ F`.

-/


open TopologicalSpace Filter ContinuousLinearMap

open scoped ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± E E' F G G' ğ•œ : Type*} [RCLike ğ•œ]
  -- ğ•œ for â„ or â„‚
  -- E for an inner product space
  [NormedAddCommGroup E]
  [InnerProductSpace ğ•œ E] [CompleteSpace E]
  -- E' for an inner product space on which we compute integrals
  [NormedAddCommGroup E']
  [InnerProductSpace ğ•œ E'] [CompleteSpace E'] [NormedSpace â„ E']
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ğ•œ F]
  -- G for a Lp add_subgroup
  [NormedAddCommGroup G]
  -- G' for integrals on a Lp add_subgroup
  [NormedAddCommGroup G']
  [NormedSpace â„ G'] [CompleteSpace G']

variable {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {s t : Set Î±}

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable (E ğ•œ)

/-- Conditional expectation of a function in L2 with respect to a sigma-algebra -/
noncomputable def condExpL2 (hm : m â‰¤ m0) : (Î± â†’â‚‚[Î¼] E) â†’L[ğ•œ] lpMeas E ğ•œ m 2 Î¼ :=
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  (lpMeas E ğ•œ m 2 Î¼).orthogonalProjection

@[deprecated (since := "2025-01-21")] alias condexpL2 := condExpL2

variable {E ğ•œ}

theorem aestronglyMeasurable_condExpL2 (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
    AEStronglyMeasurable[m] (condExpL2 E ğ•œ hm f : Î± â†’ E) Î¼ :=
  lpMeas.aestronglyMeasurable _

@[deprecated (since := "2025-01-24")]
alias aeStronglyMeasurable'_condExpL2 := aestronglyMeasurable_condExpL2

@[deprecated (since := "2025-01-24")]
alias aeStronglyMeasurable'_condexpL2 := aestronglyMeasurable_condExpL2

theorem integrableOn_condExpL2_of_measure_ne_top (hm : m â‰¤ m0) (hÎ¼s : Î¼ s â‰  âˆ) (f : Î± â†’â‚‚[Î¼] E) :
    IntegrableOn (Îµ := E) (condExpL2 E ğ•œ hm f) s Î¼ :=
  integrableOn_Lp_of_measure_ne_top (condExpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E) fact_one_le_two_ennreal.elim
    hÎ¼s

@[deprecated (since := "2025-01-21")]
alias integrableOn_condexpL2_of_measure_ne_top := integrableOn_condExpL2_of_measure_ne_top

theorem integrable_condExpL2_of_isFiniteMeasure (hm : m â‰¤ m0) [IsFiniteMeasure Î¼] {f : Î± â†’â‚‚[Î¼] E} :
    Integrable (Îµ := E) (condExpL2 E ğ•œ hm f) Î¼ :=
  integrableOn_univ.mp <| integrableOn_condExpL2_of_measure_ne_top hm (measure_ne_top _ _) f

@[deprecated (since := "2025-01-21")]
alias integrable_condexpL2_of_isFiniteMeasure := integrable_condExpL2_of_isFiniteMeasure

theorem norm_condExpL2_le_one (hm : m â‰¤ m0) : â€–@condExpL2 Î± E ğ•œ _ _ _ _ _ _ Î¼ hmâ€– â‰¤ 1 :=
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  Submodule.orthogonalProjection_norm_le _

@[deprecated (since := "2025-01-21")] alias norm_condexpL2_le_one := norm_condExpL2_le_one

theorem norm_condExpL2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) : â€–condExpL2 E ğ•œ hm fâ€– â‰¤ â€–fâ€– :=
  ((@condExpL2 _ E ğ•œ _ _ _ _ _ _ Î¼ hm).le_opNorm f).trans
    (mul_le_of_le_one_left (norm_nonneg _) (norm_condExpL2_le_one hm))

@[deprecated (since := "2025-01-21")] alias norm_condexpL2_le := norm_condExpL2_le

theorem eLpNorm_condExpL2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
    eLpNorm (Îµ := E) (condExpL2 E ğ•œ hm f) 2 Î¼ â‰¤ eLpNorm f 2 Î¼ := by
  rw [â† ENNReal.toReal_le_toReal (Lp.eLpNorm_ne_top _) (Lp.eLpNorm_ne_top _), â†
    Lp.norm_def, â† Lp.norm_def, Submodule.norm_coe]
  exact norm_condExpL2_le hm f

@[deprecated (since := "2025-01-21")] alias eLpNorm_condexpL2_le := eLpNorm_condExpL2_le

theorem norm_condExpL2_coe_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
    â€–(condExpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E)â€– â‰¤ â€–fâ€– := by
  rw [Lp.norm_def, Lp.norm_def]
  exact ENNReal.toReal_mono (Lp.eLpNorm_ne_top _) (eLpNorm_condExpL2_le hm f)

@[deprecated (since := "2025-01-21")] alias norm_condexpL2_coe_le := norm_condExpL2_coe_le

theorem inner_condExpL2_left_eq_right (hm : m â‰¤ m0) {f g : Î± â†’â‚‚[Î¼] E} :
    âŸª(condExpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ« = âŸªf, (condExpL2 E ğ•œ hm g : Î± â†’â‚‚[Î¼] E)âŸ« :=
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  Submodule.inner_orthogonalProjection_left_eq_right _ f g

@[deprecated (since := "2025-01-21")]
alias inner_condexpL2_left_eq_right := inner_condExpL2_left_eq_right

theorem condExpL2_indicator_of_measurable (hm : m â‰¤ m0) (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ)
    (c : E) :
    (condExpL2 E ğ•œ hm (indicatorConstLp 2 (hm s hs) hÎ¼s c) : Î± â†’â‚‚[Î¼] E) =
      indicatorConstLp 2 (hm s hs) hÎ¼s c := by
  rw [condExpL2]
  haveI : Fact (m â‰¤ m0) := âŸ¨hmâŸ©
  have h_mem : indicatorConstLp 2 (hm s hs) hÎ¼s c âˆˆ lpMeas E ğ•œ m 2 Î¼ :=
    mem_lpMeas_indicatorConstLp hm hs hÎ¼s
  let ind := (âŸ¨indicatorConstLp 2 (hm s hs) hÎ¼s c, h_memâŸ© : lpMeas E ğ•œ m 2 Î¼)
  have h_coe_ind : (ind : Î± â†’â‚‚[Î¼] E) = indicatorConstLp 2 (hm s hs) hÎ¼s c := rfl
  have h_orth_mem := Submodule.orthogonalProjection_mem_subspace_eq_self ind
  rw [â† h_coe_ind, h_orth_mem]

@[deprecated (since := "2025-01-21")]
alias condexpL2_indicator_of_measurable := condExpL2_indicator_of_measurable

theorem inner_condExpL2_eq_inner_fun (hm : m â‰¤ m0) (f g : Î± â†’â‚‚[Î¼] E)
    (hg : AEStronglyMeasurable[m] g Î¼) :
    âŸª(condExpL2 E ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ« = âŸªf, gâŸ« := by
  symm
  rw [â† sub_eq_zero, â† inner_sub_left, condExpL2]
  simp only [mem_lpMeas_iff_aestronglyMeasurable.mpr hg,
    Submodule.orthogonalProjection_inner_eq_zero f g]

@[deprecated (since := "2025-01-21")]
alias inner_condexpL2_eq_inner_fun := inner_condExpL2_eq_inner_fun

section Real

variable {hm : m â‰¤ m0}

theorem integral_condExpL2_eq_of_fin_meas_real (f : Lp ğ•œ 2 Î¼) (hs : MeasurableSet[m] s)
    (hÎ¼s : Î¼ s â‰  âˆ) : âˆ« x in s, (condExpL2 ğ•œ ğ•œ hm f : Î± â†’ ğ•œ) x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† L2.inner_indicatorConstLp_one (ğ•œ := ğ•œ) (hm s hs) hÎ¼s f]
  have h_eq_inner : âˆ« x in s, (condExpL2 ğ•œ ğ•œ hm f : Î± â†’ ğ•œ) x âˆ‚Î¼ =
      âŸªindicatorConstLp 2 (hm s hs) hÎ¼s (1 : ğ•œ), condExpL2 ğ•œ ğ•œ hm fâŸ« := by
    rw [L2.inner_indicatorConstLp_one (hm s hs) hÎ¼s]
  rw [h_eq_inner, â† inner_condExpL2_left_eq_right, condExpL2_indicator_of_measurable hm hs hÎ¼s]

@[deprecated (since := "2025-01-21")]
alias integral_condexpL2_eq_of_fin_meas_real := integral_condExpL2_eq_of_fin_meas_real

theorem lintegral_nnnorm_condExpL2_le (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp â„ 2 Î¼) :
    âˆ«â» x in s, â€–(condExpL2 â„ â„ hm f : Î± â†’ â„) xâ€–â‚Š âˆ‚Î¼ â‰¤ âˆ«â» x in s, â€–f xâ€–â‚Š âˆ‚Î¼ := by
  let h_meas := lpMeas.aestronglyMeasurable (condExpL2 â„ â„ hm f)
  let g := h_meas.choose
  have hg_meas : StronglyMeasurable[m] g := h_meas.choose_spec.1
  have hg_eq : g =áµ[Î¼] condExpL2 â„ â„ hm f := h_meas.choose_spec.2.symm
  have hg_eq_restrict : g =áµ[Î¼.restrict s] condExpL2 â„ â„ hm f := ae_restrict_of_ae hg_eq
  have hg_nnnorm_eq : (fun x => (â€–g xâ€–â‚Š : â„â‰¥0âˆ)) =áµ[Î¼.restrict s] fun x =>
      (â€–(condExpL2 â„ â„ hm f : Î± â†’ â„) xâ€–â‚Š : â„â‰¥0âˆ) := by
    refine hg_eq_restrict.mono fun x hx => ?_
    dsimp only
    simp_rw [hx]
  rw [lintegral_congr_ae hg_nnnorm_eq.symm]
  refine lintegral_enorm_le_of_forall_fin_meas_integral_eq
    hm (Lp.stronglyMeasurable f) ?_ ?_ ?_ ?_ hs hÎ¼s
  Â· exact integrableOn_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s
  Â· exact hg_meas
  Â· rw [IntegrableOn, integrable_congr hg_eq_restrict]
    exact integrableOn_condExpL2_of_measure_ne_top hm hÎ¼s f
  Â· intro t ht hÎ¼t
    rw [â† integral_condExpL2_eq_of_fin_meas_real f ht hÎ¼t.ne]
    exact setIntegral_congr_ae (hm t ht) (hg_eq.mono fun x hx _ => hx)

@[deprecated (since := "2025-01-21")]
alias lintegral_nnnorm_condexpL2_le := lintegral_nnnorm_condExpL2_le

theorem condExpL2_ae_eq_zero_of_ae_eq_zero (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) {f : Lp â„ 2 Î¼}
    (hf : f =áµ[Î¼.restrict s] 0) : condExpL2 â„ â„ hm f =áµ[Î¼.restrict s] (0 : Î± â†’ â„) := by
  suffices h_nnnorm_eq_zero : âˆ«â» x in s, â€–(condExpL2 â„ â„ hm f : Î± â†’ â„) xâ€–â‚Š âˆ‚Î¼ = 0 by
    rw [lintegral_eq_zero_iff] at h_nnnorm_eq_zero
    Â· refine h_nnnorm_eq_zero.mono fun x hx => ?_
      dsimp only at hx
      rw [Pi.zero_apply] at hx âŠ¢
      Â· rwa [ENNReal.coe_eq_zero, nnnorm_eq_zero] at hx
    Â· refine Measurable.coe_nnreal_ennreal (Measurable.nnnorm ?_)
      exact (Lp.stronglyMeasurable _).measurable
  refine le_antisymm ?_ (zero_le _)
  refine (lintegral_nnnorm_condExpL2_le hs hÎ¼s f).trans (le_of_eq ?_)
  rw [lintegral_eq_zero_iff]
  Â· refine hf.mono fun x hx => ?_
    dsimp only
    rw [hx]
    simp
  Â· exact (Lp.stronglyMeasurable _).enorm (Îµ := â„)

@[deprecated (since := "2025-01-21")]
alias condexpL2_ae_eq_zero_of_ae_eq_zero := condExpL2_ae_eq_zero_of_ae_eq_zero

theorem lintegral_nnnorm_condExpL2_indicator_le_real (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (ht : MeasurableSet[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
    âˆ«â» a in t, â€–(condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ (s âˆ© t) := by
  refine (lintegral_nnnorm_condExpL2_le ht hÎ¼t _).trans (le_of_eq ?_)
  have h_eq :
    âˆ«â» x in t, â€–(indicatorConstLp 2 hs hÎ¼s (1 : â„)) xâ€–â‚Š âˆ‚Î¼ =
      âˆ«â» x in t, s.indicator (fun _ => (1 : â„â‰¥0âˆ)) x âˆ‚Î¼ := by
    refine lintegral_congr_ae (ae_restrict_of_ae ?_)
    refine (@indicatorConstLp_coeFn _ _ _ 2 _ _ _ hs hÎ¼s (1 : â„)).mono fun x hx => ?_
    dsimp only
    rw [hx]
    classical
    simp_rw [Set.indicator_apply]
    split_ifs <;> simp
  rw [h_eq, lintegral_indicator hs, lintegral_const, Measure.restrict_restrict hs]
  simp only [one_mul, Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply]

@[deprecated (since := "2025-01-21")]
alias lintegral_nnnorm_condexpL2_indicator_le_real := lintegral_nnnorm_condExpL2_indicator_le_real

end Real

/-- `condExpL2` commutes with taking inner products with constants. See the lemma
`condExpL2_comp_continuousLinearMap` for a more general result about commuting with continuous
linear maps. -/
theorem condExpL2_const_inner (hm : m â‰¤ m0) (f : Lp E 2 Î¼) (c : E) :
    condExpL2 ğ•œ ğ•œ hm (((Lp.memLp f).const_inner c).toLp fun a => âŸªc, f aâŸ«) =áµ[Î¼]
    fun a => âŸªc, (condExpL2 E ğ•œ hm f : Î± â†’ E) aâŸ« := by
  have h_mem_Lp : MemLp (fun a => âŸªc, (condExpL2 E ğ•œ hm f : Î± â†’ E) aâŸ«) 2 Î¼ := by
    refine MemLp.const_inner _ ?_; exact Lp.memLp _
  have h_eq : h_mem_Lp.toLp _ =áµ[Î¼] fun a => âŸªc, (condExpL2 E ğ•œ hm f : Î± â†’ E) aâŸ« :=
    h_mem_Lp.coeFn_toLp
  refine EventuallyEq.trans ?_ h_eq
  refine Lp.ae_eq_of_forall_setIntegral_eq' ğ•œ hm _ _ two_ne_zero ENNReal.coe_ne_top
    (fun s _ hÎ¼s => integrableOn_condExpL2_of_measure_ne_top hm hÎ¼s.ne _) ?_ ?_ ?_ ?_
  Â· intro s _ hÎ¼s
    rw [IntegrableOn, integrable_congr (ae_restrict_of_ae h_eq)]
    exact (integrableOn_condExpL2_of_measure_ne_top hm hÎ¼s.ne _).const_inner _
  Â· intro s hs hÎ¼s
    rw [integral_condExpL2_eq_of_fin_meas_real _ hs hÎ¼s.ne,
      integral_congr_ae (ae_restrict_of_ae h_eq), â†
      L2.inner_indicatorConstLp_eq_setIntegral_inner ğ•œ (â†‘(condExpL2 E ğ•œ hm f)) (hm s hs) c hÎ¼s.ne,
      â† inner_condExpL2_left_eq_right, condExpL2_indicator_of_measurable _ hs,
      L2.inner_indicatorConstLp_eq_setIntegral_inner ğ•œ f (hm s hs) c hÎ¼s.ne,
      setIntegral_congr_ae (hm s hs)
        ((MemLp.coeFn_toLp ((Lp.memLp f).const_inner c)).mono fun x hx _ => hx)]
  Â· exact lpMeas.aestronglyMeasurable _
  Â· refine AEStronglyMeasurable.congr ?_ h_eq.symm
    exact (lpMeas.aestronglyMeasurable _).const_inner

@[deprecated (since := "2025-01-21")] alias condexpL2_const_inner := condExpL2_const_inner

/-- `condExpL2` verifies the equality of integrals defining the conditional expectation. -/
theorem integral_condExpL2_eq (hm : m â‰¤ m0) (f : Lp E' 2 Î¼) (hs : MeasurableSet[m] s)
    (hÎ¼s : Î¼ s â‰  âˆ) : âˆ« x in s, (condExpL2 E' ğ•œ hm f : Î± â†’ E') x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† sub_eq_zero, â†
    integral_sub' (integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s)
      (integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s)]
  refine integral_eq_zero_of_forall_integral_inner_eq_zero ğ•œ _ ?_ ?_
  Â· rw [integrable_congr (ae_restrict_of_ae (Lp.coeFn_sub (â†‘(condExpL2 E' ğ•œ hm f)) f).symm)]
    exact integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s
  intro c
  simp_rw [Pi.sub_apply, inner_sub_right]
  rw [integral_sub
      ((integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s).const_inner c)
      ((integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s).const_inner c)]
  have h_ae_eq_f := MemLp.coeFn_toLp (E := ğ•œ) ((Lp.memLp f).const_inner c)
  rw [sub_eq_zero, â†
    setIntegral_congr_ae (hm s hs) ((condExpL2_const_inner hm f c).mono fun x hx _ => hx), â†
    setIntegral_congr_ae (hm s hs) (h_ae_eq_f.mono fun x hx _ => hx)]
  exact integral_condExpL2_eq_of_fin_meas_real _ hs hÎ¼s

@[deprecated (since := "2025-01-21")] alias integral_condexpL2_eq := integral_condExpL2_eq

variable {E'' ğ•œ' : Type*} [RCLike ğ•œ'] [NormedAddCommGroup E''] [InnerProductSpace ğ•œ' E'']
  [CompleteSpace E''] [NormedSpace â„ E'']

variable (ğ•œ ğ•œ')

theorem condExpL2_comp_continuousLinearMap (hm : m â‰¤ m0) (T : E' â†’L[â„] E'') (f : Î± â†’â‚‚[Î¼] E') :
    (condExpL2 E'' ğ•œ' hm (T.compLp f) : Î± â†’â‚‚[Î¼] E'') =áµ[Î¼]
    T.compLp (condExpL2 E' ğ•œ hm f : Î± â†’â‚‚[Î¼] E') := by
  refine Lp.ae_eq_of_forall_setIntegral_eq' ğ•œ' hm _ _ two_ne_zero ENNReal.coe_ne_top
    (fun s _ hÎ¼s => integrableOn_condExpL2_of_measure_ne_top hm hÎ¼s.ne _) (fun s _ hÎ¼s =>
      integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s.ne) ?_ ?_ ?_
  Â· intro s hs hÎ¼s
    rw [T.setIntegral_compLp _ (hm s hs),
      T.integral_comp_comm
        (integrableOn_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s.ne),
      integral_condExpL2_eq hm f hs hÎ¼s.ne,
      integral_condExpL2_eq hm (T.compLp f) hs hÎ¼s.ne, T.setIntegral_compLp _ (hm s hs),
      T.integral_comp_comm
        (integrableOn_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s.ne)]
  Â· exact lpMeas.aestronglyMeasurable _
  Â· have h_coe := T.coeFn_compLp (condExpL2 E' ğ•œ hm f : Î± â†’â‚‚[Î¼] E')
    rw [â† EventuallyEq] at h_coe
    refine AEStronglyMeasurable.congr ?_ h_coe.symm
    exact T.continuous.comp_aestronglyMeasurable (lpMeas.aestronglyMeasurable (condExpL2 E' ğ•œ hm f))

@[deprecated (since := "2025-01-21")]
alias condexpL2_comp_continuousLinearMap := condExpL2_comp_continuousLinearMap

variable {ğ•œ ğ•œ'}

section CondexpL2Indicator

variable (ğ•œ)

theorem condExpL2_indicator_ae_eq_smul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : E') :
    condExpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) =áµ[Î¼] fun a =>
      (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s (1 : â„)) : Î± â†’ â„) a â€¢ x := by
  rw [indicatorConstLp_eq_toSpanSingleton_compLp hs hÎ¼s x]
  have h_comp :=
    condExpL2_comp_continuousLinearMap â„ ğ•œ hm (toSpanSingleton â„ x)
      (indicatorConstLp 2 hs hÎ¼s (1 : â„))
  refine h_comp.trans ?_
  exact (toSpanSingleton â„ x).coeFn_compLp _

@[deprecated (since := "2025-01-21")]
alias condexpL2_indicator_ae_eq_smul := condExpL2_indicator_ae_eq_smul

theorem condExpL2_indicator_eq_toSpanSingleton_comp (hm : m â‰¤ m0) (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (x : E') : (condExpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’â‚‚[Î¼] E') =
    (toSpanSingleton â„ x).compLp (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1)) := by
  ext1
  refine (condExpL2_indicator_ae_eq_smul ğ•œ hm hs hÎ¼s x).trans ?_
  have h_comp := (toSpanSingleton â„ x).coeFn_compLp
    (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’â‚‚[Î¼] â„)
  rw [â† EventuallyEq] at h_comp
  refine EventuallyEq.trans ?_ h_comp.symm
  filter_upwards with y using rfl

@[deprecated (since := "2025-01-21")]
alias condexpL2_indicator_eq_toSpanSingleton_comp := condExpL2_indicator_eq_toSpanSingleton_comp

variable {ğ•œ}

theorem setLIntegral_nnnorm_condExpL2_indicator_le (hm : m â‰¤ m0) (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (x : E') {t : Set Î±} (ht : MeasurableSet[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
    âˆ«â» a in t, â€–(condExpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’ E') aâ€–â‚Š âˆ‚Î¼ â‰¤
    Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
  calc
    âˆ«â» a in t, â€–(condExpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’ E') aâ€–â‚Š âˆ‚Î¼ =
        âˆ«â» a in t, â€–(condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) a â€¢ xâ€–â‚Š âˆ‚Î¼ :=
      setLIntegral_congr_fun_ae (hm t ht)
        ((condExpL2_indicator_ae_eq_smul ğ•œ hm hs hÎ¼s x).mono fun a ha _ => by rw [ha])
    _ = (âˆ«â» a in t, â€–(condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) aâ€–â‚Š âˆ‚Î¼) * â€–xâ€–â‚Š := by
      simp_rw [nnnorm_smul, ENNReal.coe_mul]
      rw [lintegral_mul_const]
      exact (Lp.stronglyMeasurable _).enorm (Îµ := â„)
    _ â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
      mul_le_mul_right' (lintegral_nnnorm_condExpL2_indicator_le_real hs hÎ¼s ht hÎ¼t) _

@[deprecated (since := "2025-01-21")]
alias setLIntegral_nnnorm_condexpL2_indicator_le := setLIntegral_nnnorm_condExpL2_indicator_le

theorem lintegral_nnnorm_condExpL2_indicator_le (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : E') [SigmaFinite (Î¼.trim hm)] :
    âˆ«â» a, â€–(condExpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x) : Î± â†’ E') aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ s * â€–xâ€–â‚Š := by
  refine lintegral_le_of_forall_fin_meas_trim_le hm (Î¼ s * â€–xâ€–â‚Š) fun t ht hÎ¼t => ?_
  refine (setLIntegral_nnnorm_condExpL2_indicator_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
  gcongr
  apply Set.inter_subset_left

@[deprecated (since := "2025-01-21")]
alias lintegral_nnnorm_condexpL2_indicator_le := lintegral_nnnorm_condExpL2_indicator_le

/-- If the measure `Î¼.trim hm` is sigma-finite, then the conditional expectation of a measurable set
with finite measure is integrable. -/
theorem integrable_condExpL2_indicator (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E') :
    Integrable (Îµ := E') (condExpL2 E' ğ•œ hm (indicatorConstLp 2 hs hÎ¼s x)) Î¼ := by
  refine integrable_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š)
    (ENNReal.mul_lt_top hÎ¼s.lt_top ENNReal.coe_lt_top) ?_ ?_
  Â· exact Lp.aestronglyMeasurable _
  Â· refine fun t ht hÎ¼t =>
      (setLIntegral_nnnorm_condExpL2_indicator_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
    gcongr
    apply Set.inter_subset_left

@[deprecated (since := "2025-01-21")]
alias integrable_condexpL2_indicator := integrable_condExpL2_indicator

end CondexpL2Indicator

section CondexpIndSMul

variable [NormedSpace â„ G] {hm : m â‰¤ m0}

/-- Conditional expectation of the indicator of a measurable set with finite measure, in L2. -/
noncomputable def condExpIndSMul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    Lp G 2 Î¼ :=
  (toSpanSingleton â„ x).compLpL 2 Î¼ (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s (1 : â„)))

@[deprecated (since := "2025-01-21")] alias condexpIndSMul := condExpIndSMul

theorem aestronglyMeasurable_condExpIndSMul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) : AEStronglyMeasurable[m] (condExpIndSMul hm hs hÎ¼s x) Î¼ := by
  have h : AEStronglyMeasurable[m] (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) Î¼ :=
    aestronglyMeasurable_condExpL2 _ _
  rw [condExpIndSMul]
  exact ((toSpanSingleton â„ x).continuous.comp_aestronglyMeasurable h).congr
    (coeFn_compLpL _ _).symm

@[deprecated (since := "2025-01-24")]
alias aeStronglyMeasurable'_condExpIndSMul := aestronglyMeasurable_condExpIndSMul

@[deprecated (since := "2025-01-21")]
alias aestronglyMeasurable'_condexpIndSMul := aestronglyMeasurable_condExpIndSMul

theorem condExpIndSMul_add (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x y : G) :
    condExpIndSMul hm hs hÎ¼s (x + y) = condExpIndSMul hm hs hÎ¼s x + condExpIndSMul hm hs hÎ¼s y := by
  simp_rw [condExpIndSMul]; rw [toSpanSingleton_add, add_compLpL, add_apply]

@[deprecated (since := "2025-01-21")] alias condexpIndSMul_add := condExpIndSMul_add

theorem condExpIndSMul_smul (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : â„) (x : G) :
    condExpIndSMul hm hs hÎ¼s (c â€¢ x) = c â€¢ condExpIndSMul hm hs hÎ¼s x := by
  simp_rw [condExpIndSMul]; rw [toSpanSingleton_smul, smul_compLpL, smul_apply]

@[deprecated (since := "2025-01-21")] alias condexpIndSMul_smul := condExpIndSMul_smul

theorem condExpIndSMul_smul' [NormedSpace â„ F] [SMulCommClass â„ ğ•œ F] (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (c : ğ•œ) (x : F) :
    condExpIndSMul hm hs hÎ¼s (c â€¢ x) = c â€¢ condExpIndSMul hm hs hÎ¼s x := by
  rw [condExpIndSMul, condExpIndSMul, toSpanSingleton_smul',
    (toSpanSingleton â„ x).smul_compLpL c, smul_apply]

@[deprecated (since := "2025-01-21")] alias condexpIndSMul_smul' := condExpIndSMul_smul'

theorem condExpIndSMul_ae_eq_smul (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    condExpIndSMul hm hs hÎ¼s x =áµ[Î¼] fun a =>
      (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) a â€¢ x :=
  (toSpanSingleton â„ x).coeFn_compLpL _

@[deprecated (since := "2025-01-21")] alias condexpIndSMul_ae_eq_smul := condExpIndSMul_ae_eq_smul

theorem setLIntegral_nnnorm_condExpIndSMul_le (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) {t : Set Î±} (ht : MeasurableSet[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
    (âˆ«â» a in t, â€–condExpIndSMul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼) â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
  calc
    âˆ«â» a in t, â€–condExpIndSMul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼ =
        âˆ«â» a in t, â€–(condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) a â€¢ xâ€–â‚Š âˆ‚Î¼ :=
      setLIntegral_congr_fun_ae (hm t ht)
        ((condExpIndSMul_ae_eq_smul hm hs hÎ¼s x).mono fun a ha _ => by rw [ha])
    _ = (âˆ«â» a in t, â€–(condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) aâ€–â‚Š âˆ‚Î¼) * â€–xâ€–â‚Š := by
      simp_rw [nnnorm_smul, ENNReal.coe_mul]
      rw [lintegral_mul_const]
      exact (Lp.stronglyMeasurable _).enorm (Îµ := â„)
    _ â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
      mul_le_mul_right' (lintegral_nnnorm_condExpL2_indicator_le_real hs hÎ¼s ht hÎ¼t) _

@[deprecated (since := "2025-01-21")]
alias setLIntegral_nnnorm_condexpIndSMul_le := setLIntegral_nnnorm_condExpIndSMul_le

theorem lintegral_nnnorm_condExpIndSMul_le (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) [SigmaFinite (Î¼.trim hm)] : âˆ«â» a, â€–condExpIndSMul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ s * â€–xâ€–â‚Š := by
  refine lintegral_le_of_forall_fin_meas_trim_le hm (Î¼ s * â€–xâ€–â‚Š) fun t ht hÎ¼t => ?_
  refine (setLIntegral_nnnorm_condExpIndSMul_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
  gcongr
  apply Set.inter_subset_left

@[deprecated (since := "2025-01-21")]
alias lintegral_nnnorm_condexpIndSMul_le := lintegral_nnnorm_condExpIndSMul_le

/-- If the measure `Î¼.trim hm` is sigma-finite, then the conditional expectation of a measurable set
with finite measure is integrable. -/
theorem integrable_condExpIndSMul (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (x : G) : Integrable (condExpIndSMul hm hs hÎ¼s x) Î¼ := by
  refine integrable_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š)
    (ENNReal.mul_lt_top hÎ¼s.lt_top ENNReal.coe_lt_top) ?_ ?_
  Â· exact Lp.aestronglyMeasurable _
  Â· refine fun t ht hÎ¼t => (setLIntegral_nnnorm_condExpIndSMul_le hm hs hÎ¼s x ht hÎ¼t).trans ?_
    gcongr
    apply Set.inter_subset_left

@[deprecated (since := "2025-01-21")] alias integrable_condexpIndSMul := integrable_condExpIndSMul

theorem condExpIndSMul_empty {x : G} : condExpIndSMul hm MeasurableSet.empty
    ((measure_empty (Î¼ := Î¼)).le.trans_lt ENNReal.coe_lt_top).ne x = 0 := by
  rw [condExpIndSMul, indicatorConstLp_empty]
  simp only [Submodule.coe_zero, ContinuousLinearMap.map_zero]

@[deprecated (since := "2025-01-21")] alias condexpIndSMul_empty := condExpIndSMul_empty

theorem setIntegral_condExpL2_indicator (hs : MeasurableSet[m] s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) :
    âˆ« x in s, (condExpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) x âˆ‚Î¼ = Î¼.real (t âˆ© s) :=
  calc
    âˆ« x in s, (condExpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) x âˆ‚Î¼ =
        âˆ« x in s, indicatorConstLp 2 ht hÎ¼t (1 : â„) x âˆ‚Î¼ :=
      @integral_condExpL2_eq Î± _ â„ _ _ _ _ _ _ _ _ _ hm (indicatorConstLp 2 ht hÎ¼t (1 : â„)) hs hÎ¼s
    _ = Î¼.real (t âˆ© s) â€¢ (1 : â„) := setIntegral_indicatorConstLp (hm s hs) ht hÎ¼t 1
    _ = Î¼.real (t âˆ© s) := by rw [smul_eq_mul, mul_one]

@[deprecated (since := "2025-01-21")]
alias setIntegral_condexpL2_indicator := setIntegral_condExpL2_indicator

theorem setIntegral_condExpIndSMul (hs : MeasurableSet[m] s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (x : G') :
    âˆ« a in s, (condExpIndSMul hm ht hÎ¼t x) a âˆ‚Î¼ = Î¼.real (t âˆ© s) â€¢ x :=
  calc
    âˆ« a in s, (condExpIndSMul hm ht hÎ¼t x) a âˆ‚Î¼ =
        âˆ« a in s, (condExpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) a â€¢ x âˆ‚Î¼ :=
      setIntegral_congr_ae (hm s hs)
        ((condExpIndSMul_ae_eq_smul hm ht hÎ¼t x).mono fun _ hx _ => hx)
    _ = (âˆ« a in s, (condExpL2 â„ â„ hm (indicatorConstLp 2 ht hÎ¼t 1) : Î± â†’ â„) a âˆ‚Î¼) â€¢ x :=
      (integral_smul_const _ x)
    _ = Î¼.real (t âˆ© s) â€¢ x := by rw [setIntegral_condExpL2_indicator hs ht hÎ¼s hÎ¼t]

@[deprecated (since := "2025-01-21")] alias setIntegral_condexpIndSMul := setIntegral_condExpIndSMul

theorem condExpL2_indicator_nonneg (hm : m â‰¤ m0) (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    [SigmaFinite (Î¼.trim hm)] : (0 : Î± â†’ â„) â‰¤áµ[Î¼]
    condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) := by
  have h : AEStronglyMeasurable[m] (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) Î¼ :=
    aestronglyMeasurable_condExpL2 _ _
  refine EventuallyLE.trans_eq ?_ h.ae_eq_mk.symm
  refine @ae_le_of_ae_le_trim _ _ _ _ _ _ hm (0 : Î± â†’ â„) _ ?_
  refine ae_nonneg_of_forall_setIntegral_nonneg_of_sigmaFinite ?_ ?_
  Â· rintro t - -
    refine @Integrable.integrableOn _ _ m _ _ _ _ _ ?_
    refine Integrable.trim hm ?_ h.stronglyMeasurable_mk
    rw [integrable_congr h.ae_eq_mk.symm]
    exact integrable_condExpL2_indicator hm hs hÎ¼s _
  Â· intro t ht hÎ¼t
    rw [â† setIntegral_trim hm h.stronglyMeasurable_mk ht]
    have h_ae :
        âˆ€áµ x âˆ‚Î¼, x âˆˆ t â†’ h.mk _ x = (condExpL2 â„ â„ hm (indicatorConstLp 2 hs hÎ¼s 1) : Î± â†’ â„) x := by
      filter_upwards [h.ae_eq_mk] with x hx using fun _ => hx.symm
    rw [setIntegral_congr_ae (hm t ht) h_ae,
      setIntegral_condExpL2_indicator ht hs ((le_trim hm).trans_lt hÎ¼t).ne hÎ¼s]
    exact ENNReal.toReal_nonneg

@[deprecated (since := "2025-01-21")] alias condexpL2_indicator_nonneg := condExpL2_indicator_nonneg

theorem condExpIndSMul_nonneg {E}
    [NormedAddCommGroup E] [PartialOrder E] [NormedSpace â„ E] [OrderedSMul â„ E]
    [SigmaFinite (Î¼.trim hm)] (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) (hx : 0 â‰¤ x) :
    (0 : Î± â†’ E) â‰¤áµ[Î¼] condExpIndSMul hm hs hÎ¼s x := by
  refine EventuallyLE.trans_eq ?_ (condExpIndSMul_ae_eq_smul hm hs hÎ¼s x).symm
  filter_upwards [condExpL2_indicator_nonneg hm hs hÎ¼s] with a ha
  exact smul_nonneg ha hx

@[deprecated (since := "2025-01-21")] alias condexpIndSMul_nonneg := condExpIndSMul_nonneg

end CondexpIndSMul

end MeasureTheory
