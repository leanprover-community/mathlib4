/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.CondexpL2
import Mathlib.MeasureTheory.Measure.Real

/-! # Conditional expectation in L1

This file contains two more steps of the construction of the conditional expectation, which is
completed in `MeasureTheory.Function.ConditionalExpectation.Basic`. See that file for a
description of the full process.

The conditional expectation of an `LÂ²` function is defined in
`MeasureTheory.Function.ConditionalExpectation.CondexpL2`. In this file, we perform two steps.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `Set Î± â†’ (E â†’L[â„] (Î± â†’â‚[Î¼] E))` which to a set associates a linear
  map. That linear map sends `x âˆˆ E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condExpL1CLM : (Î± â†’â‚[Î¼] E) â†’L[â„] (Î± â†’â‚[Î¼] E)`. This is done using the same
  construction as the Bochner integral (see the file `MeasureTheory/Integral/SetToL1`).

## Main definitions

* `condExpL1`: Conditional expectation of a function as a linear map from `L1` to itself.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± F F' G G' ğ•œ : Type*} [RCLike ğ•œ]
  -- ğ•œ for â„ or â„‚
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ğ•œ F]
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace ğ•œ F'] [NormedSpace â„ F'] [CompleteSpace F']
  -- G for a Lp add_subgroup
  [NormedAddCommGroup G]
  -- G' for integrals on a Lp add_subgroup
  [NormedAddCommGroup G']
  [NormedSpace â„ G'] [CompleteSpace G']

section CondexpInd

/-! ## Conditional expectation of an indicator as a continuous linear map.

The goal of this section is to build
`condExpInd (hm : m â‰¤ m0) (Î¼ : Measure Î±) (s : Set s) : G â†’L[â„] Î± â†’â‚[Î¼] G`, which
takes `x : G` to the conditional expectation of the indicator of the set `s` with value `x`,
seen as an element of `Î± â†’â‚[Î¼] G`.
-/


variable {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {s t : Set Î±} [NormedSpace â„ G]

section CondexpIndL1Fin


/-- Conditional expectation of the indicator of a measurable set with finite measure,
as a function in L1. -/
def condExpIndL1Fin (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) : Î± â†’â‚[Î¼] G :=
  (integrable_condExpIndSMul hm hs hÎ¼s x).toL1 _

theorem condExpIndL1Fin_ae_eq_condExpIndSMul (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    condExpIndL1Fin hm hs hÎ¼s x =áµ[Î¼] condExpIndSMul hm hs hÎ¼s x :=
  (integrable_condExpIndSMul hm hs hÎ¼s x).coeFn_toL1

variable {hm : m â‰¤ m0} [SigmaFinite (Î¼.trim hm)]

-- Porting note: this lemma fills the hole in `refine' (MemLp.coeFn_toLp _) ...`
-- which is not automatically filled in Lean 4
private theorem q {hs : MeasurableSet s} {hÎ¼s : Î¼ s â‰  âˆ} {x : G} :
    MemLp (condExpIndSMul hm hs hÎ¼s x) 1 Î¼ := by
  rw [memLp_one_iff_integrable]; apply integrable_condExpIndSMul

theorem condExpIndL1Fin_add (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x y : G) :
    condExpIndL1Fin hm hs hÎ¼s (x + y) =
    condExpIndL1Fin hm hs hÎ¼s x + condExpIndL1Fin hm hs hÎ¼s y := by
  ext1
  refine (MemLp.coeFn_toLp q).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_add _ _).symm
  refine EventuallyEq.trans ?_
    (EventuallyEq.add (MemLp.coeFn_toLp q).symm (MemLp.coeFn_toLp q).symm)
  rw [condExpIndSMul_add]
  refine (Lp.coeFn_add _ _).trans (Eventually.of_forall fun a â†¦ ?_)
  rfl

theorem condExpIndL1Fin_smul (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : â„) (x : G) :
    condExpIndL1Fin hm hs hÎ¼s (c â€¢ x) = c â€¢ condExpIndL1Fin hm hs hÎ¼s x := by
  ext1
  refine (MemLp.coeFn_toLp q).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_smul _ _).symm
  rw [condExpIndSMul_smul hs hÎ¼s c x]
  refine (Lp.coeFn_smul _ _).trans ?_
  refine (condExpIndL1Fin_ae_eq_condExpIndSMul hm hs hÎ¼s x).mono fun y hy â†¦ ?_
  simp only [Pi.smul_apply, hy]

theorem condExpIndL1Fin_smul' [NormedSpace â„ F] [SMulCommClass â„ ğ•œ F] (hs : MeasurableSet s)
    (hÎ¼s : Î¼ s â‰  âˆ) (c : ğ•œ) (x : F) :
    condExpIndL1Fin hm hs hÎ¼s (c â€¢ x) = c â€¢ condExpIndL1Fin hm hs hÎ¼s x := by
  ext1
  refine (MemLp.coeFn_toLp q).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_smul _ _).symm
  rw [condExpIndSMul_smul' hs hÎ¼s c x]
  refine (Lp.coeFn_smul _ _).trans ?_
  refine (condExpIndL1Fin_ae_eq_condExpIndSMul hm hs hÎ¼s x).mono fun y hy â†¦ ?_
  simp only [Pi.smul_apply, hy]

theorem norm_condExpIndL1Fin_le (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    â€–condExpIndL1Fin hm hs hÎ¼s xâ€– â‰¤ Î¼.real s * â€–xâ€– := by
  rw [L1.norm_eq_integral_norm, â† ENNReal.toReal_ofReal (norm_nonneg x), measureReal_def,
    â† ENNReal.toReal_mul,
    â† ENNReal.ofReal_le_iff_le_toReal (ENNReal.mul_ne_top hÎ¼s ENNReal.ofReal_ne_top),
    ofReal_integral_norm_eq_lintegral_enorm]
  swap; Â· rw [â† memLp_one_iff_integrable]; exact Lp.memLp _
  have h_eq :
    âˆ«â» a, â€–condExpIndL1Fin hm hs hÎ¼s x aâ€–â‚‘ âˆ‚Î¼ = âˆ«â» a, â€–condExpIndSMul hm hs hÎ¼s x aâ€–â‚‘ âˆ‚Î¼ := by
    refine lintegral_congr_ae ?_
    refine (condExpIndL1Fin_ae_eq_condExpIndSMul hm hs hÎ¼s x).mono fun z hz â†¦ ?_
    dsimp only
    rw [hz]
  rw [h_eq, ofReal_norm_eq_enorm]
  exact lintegral_nnnorm_condExpIndSMul_le hm hs hÎ¼s x

theorem condExpIndL1Fin_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hÎ¼s : Î¼ s â‰  âˆ)
    (hÎ¼t : Î¼ t â‰  âˆ) (hst : Disjoint s t) (x : G) :
    condExpIndL1Fin hm (hs.union ht) ((measure_union_le s t).trans_lt
      (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr âŸ¨hÎ¼s, hÎ¼tâŸ©))).ne x =
    condExpIndL1Fin hm hs hÎ¼s x + condExpIndL1Fin hm ht hÎ¼t x := by
  ext1
  have hÎ¼st := measure_union_ne_top hÎ¼s hÎ¼t
  refine (condExpIndL1Fin_ae_eq_condExpIndSMul hm (hs.union ht) hÎ¼st x).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_add _ _).symm
  have hs_eq := condExpIndL1Fin_ae_eq_condExpIndSMul hm hs hÎ¼s x
  have ht_eq := condExpIndL1Fin_ae_eq_condExpIndSMul hm ht hÎ¼t x
  refine EventuallyEq.trans ?_ (EventuallyEq.add hs_eq.symm ht_eq.symm)
  rw [condExpIndSMul]
  rw [indicatorConstLp_disjoint_union hs ht hÎ¼s hÎ¼t hst (1 : â„)]
  rw [(condExpL2 â„ â„ hm).map_add]
  push_cast
  rw [((toSpanSingleton â„ x).compLpL 2 Î¼).map_add]
  refine (Lp.coeFn_add _ _).trans ?_
  filter_upwards with y using rfl

end CondexpIndL1Fin

section CondexpIndL1


open scoped Classical in
/-- Conditional expectation of the indicator of a set, as a function in L1. Its value for sets
which are not both measurable and of finite measure is not used: we set it to 0. -/
def condExpIndL1 {m m0 : MeasurableSpace Î±} (hm : m â‰¤ m0) (Î¼ : Measure Î±) (s : Set Î±)
    [SigmaFinite (Î¼.trim hm)] (x : G) : Î± â†’â‚[Î¼] G :=
  if hs : MeasurableSet s âˆ§ Î¼ s â‰  âˆ then condExpIndL1Fin hm hs.1 hs.2 x else 0

variable {hm : m â‰¤ m0} [SigmaFinite (Î¼.trim hm)]

theorem condExpIndL1_of_measurableSet_of_measure_ne_top (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (x : G) : condExpIndL1 hm Î¼ s x = condExpIndL1Fin hm hs hÎ¼s x := by
  simp only [condExpIndL1, And.intro hs hÎ¼s, dif_pos, Ne, not_false_iff, and_self_iff]

theorem condExpIndL1_of_measure_eq_top (hÎ¼s : Î¼ s = âˆ) (x : G) : condExpIndL1 hm Î¼ s x = 0 := by
  simp only [condExpIndL1, hÎ¼s, not_true, Ne, dif_neg, not_false_iff,
    and_false]

theorem condExpIndL1_of_not_measurableSet (hs : Â¬MeasurableSet s) (x : G) :
    condExpIndL1 hm Î¼ s x = 0 := by
  simp only [condExpIndL1, hs, dif_neg, not_false_iff, false_and]

theorem condExpIndL1_add (x y : G) :
    condExpIndL1 hm Î¼ s (x + y) = condExpIndL1 hm Î¼ s x + condExpIndL1 hm Î¼ s y := by
  by_cases hs : MeasurableSet s
  swap; Â· simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [zero_add]
  by_cases hÎ¼s : Î¼ s = âˆ
  Â· simp_rw [condExpIndL1_of_measure_eq_top hÎ¼s]; rw [zero_add]
  Â· simp_rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hÎ¼s]
    exact condExpIndL1Fin_add hs hÎ¼s x y

theorem condExpIndL1_smul (c : â„) (x : G) :
    condExpIndL1 hm Î¼ s (c â€¢ x) = c â€¢ condExpIndL1 hm Î¼ s x := by
  by_cases hs : MeasurableSet s
  swap; Â· simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [smul_zero]
  by_cases hÎ¼s : Î¼ s = âˆ
  Â· simp_rw [condExpIndL1_of_measure_eq_top hÎ¼s]; rw [smul_zero]
  Â· simp_rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hÎ¼s]
    exact condExpIndL1Fin_smul hs hÎ¼s c x

theorem condExpIndL1_smul' [NormedSpace â„ F] [SMulCommClass â„ ğ•œ F] (c : ğ•œ) (x : F) :
    condExpIndL1 hm Î¼ s (c â€¢ x) = c â€¢ condExpIndL1 hm Î¼ s x := by
  by_cases hs : MeasurableSet s
  swap; Â· simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [smul_zero]
  by_cases hÎ¼s : Î¼ s = âˆ
  Â· simp_rw [condExpIndL1_of_measure_eq_top hÎ¼s]; rw [smul_zero]
  Â· simp_rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hÎ¼s]
    exact condExpIndL1Fin_smul' hs hÎ¼s c x

theorem norm_condExpIndL1_le (x : G) : â€–condExpIndL1 hm Î¼ s xâ€– â‰¤ Î¼.real s * â€–xâ€– := by
  by_cases hs : MeasurableSet s
  swap
  Â· simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [Lp.norm_zero]
    exact mul_nonneg ENNReal.toReal_nonneg (norm_nonneg _)
  by_cases hÎ¼s : Î¼ s = âˆ
  Â· rw [condExpIndL1_of_measure_eq_top hÎ¼s x, Lp.norm_zero]
    exact mul_nonneg ENNReal.toReal_nonneg (norm_nonneg _)
  Â· rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hÎ¼s x]
    exact norm_condExpIndL1Fin_le hs hÎ¼s x

theorem continuous_condExpIndL1 : Continuous fun x : G â†¦ condExpIndL1 hm Î¼ s x :=
  continuous_of_linear_of_bound condExpIndL1_add condExpIndL1_smul norm_condExpIndL1_le

theorem condExpIndL1_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hÎ¼s : Î¼ s â‰  âˆ)
    (hÎ¼t : Î¼ t â‰  âˆ) (hst : Disjoint s t) (x : G) :
    condExpIndL1 hm Î¼ (s âˆª t) x = condExpIndL1 hm Î¼ s x + condExpIndL1 hm Î¼ t x := by
  have hÎ¼st : Î¼ (s âˆª t) â‰  âˆ :=
    ((measure_union_le s t).trans_lt (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr âŸ¨hÎ¼s, hÎ¼tâŸ©))).ne
  rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hÎ¼s x,
    condExpIndL1_of_measurableSet_of_measure_ne_top ht hÎ¼t x,
    condExpIndL1_of_measurableSet_of_measure_ne_top (hs.union ht) hÎ¼st x]
  exact condExpIndL1Fin_disjoint_union hs ht hÎ¼s hÎ¼t hst x

end CondexpIndL1

variable (G)

/-- Conditional expectation of the indicator of a set, as a linear map from `G` to L1. -/
def condExpInd {m m0 : MeasurableSpace Î±} (hm : m â‰¤ m0) (Î¼ : Measure Î±) [SigmaFinite (Î¼.trim hm)]
    (s : Set Î±) : G â†’L[â„] Î± â†’â‚[Î¼] G where
  toFun := condExpIndL1 hm Î¼ s
  map_add' := condExpIndL1_add
  map_smul' := condExpIndL1_smul
  cont := continuous_condExpIndL1

variable {G}

theorem condExpInd_ae_eq_condExpIndSMul (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    condExpInd G hm Î¼ s x =áµ[Î¼] condExpIndSMul hm hs hÎ¼s x := by
  refine EventuallyEq.trans ?_ (condExpIndL1Fin_ae_eq_condExpIndSMul hm hs hÎ¼s x)
  simp [condExpInd, condExpIndL1, hs, hÎ¼s]

variable {hm : m â‰¤ m0} [SigmaFinite (Î¼.trim hm)]

theorem aestronglyMeasurable_condExpInd (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
    AEStronglyMeasurable[m] (condExpInd G hm Î¼ s x) Î¼ :=
  (aestronglyMeasurable_condExpIndSMul hm hs hÎ¼s x).congr
    (condExpInd_ae_eq_condExpIndSMul hm hs hÎ¼s x).symm

@[simp]
theorem condExpInd_empty : condExpInd G hm Î¼ âˆ… = (0 : G â†’L[â„] Î± â†’â‚[Î¼] G) := by
  ext1 x
  ext1
  refine (condExpInd_ae_eq_condExpIndSMul hm MeasurableSet.empty (by simp) x).trans ?_
  rw [condExpIndSMul_empty]
  refine (Lp.coeFn_zero G 2 Î¼).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_zero G 1 Î¼).symm
  rfl

theorem condExpInd_smul' [NormedSpace â„ F] [SMulCommClass â„ ğ•œ F] (c : ğ•œ) (x : F) :
    condExpInd F hm Î¼ s (c â€¢ x) = c â€¢ condExpInd F hm Î¼ s x :=
  condExpIndL1_smul' c x

theorem norm_condExpInd_apply_le (x : G) : â€–condExpInd G hm Î¼ s xâ€– â‰¤ Î¼.real s * â€–xâ€– :=
  norm_condExpIndL1_le x

theorem norm_condExpInd_le : â€–(condExpInd G hm Î¼ s : G â†’L[â„] Î± â†’â‚[Î¼] G)â€– â‰¤ Î¼.real s :=
  ContinuousLinearMap.opNorm_le_bound _ ENNReal.toReal_nonneg norm_condExpInd_apply_le

theorem condExpInd_disjoint_union_apply (hs : MeasurableSet s) (ht : MeasurableSet t)
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (hst : Disjoint s t) (x : G) :
    condExpInd G hm Î¼ (s âˆª t) x = condExpInd G hm Î¼ s x + condExpInd G hm Î¼ t x :=
  condExpIndL1_disjoint_union hs ht hÎ¼s hÎ¼t hst x

theorem condExpInd_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hÎ¼s : Î¼ s â‰  âˆ)
    (hÎ¼t : Î¼ t â‰  âˆ) (hst : Disjoint s t) : (condExpInd G hm Î¼ (s âˆª t) : G â†’L[â„] Î± â†’â‚[Î¼] G) =
    condExpInd G hm Î¼ s + condExpInd G hm Î¼ t := by
  ext1 x; push_cast; exact condExpInd_disjoint_union_apply hs ht hÎ¼s hÎ¼t hst x

variable (G)

theorem dominatedFinMeasAdditive_condExpInd (hm : m â‰¤ m0) (Î¼ : Measure Î±)
    [SigmaFinite (Î¼.trim hm)] :
    DominatedFinMeasAdditive Î¼ (condExpInd G hm Î¼ : Set Î± â†’ G â†’L[â„] Î± â†’â‚[Î¼] G) 1 :=
  âŸ¨fun _ _ â†¦ condExpInd_disjoint_union, fun _ _ _ â†¦ norm_condExpInd_le.trans (one_mul _).symm.leâŸ©

variable {G}

theorem setIntegral_condExpInd (hs : MeasurableSet[m] s) (ht : MeasurableSet t) (hÎ¼s : Î¼ s â‰  âˆ)
    (hÎ¼t : Î¼ t â‰  âˆ) (x : G') : âˆ« a in s, condExpInd G' hm Î¼ t x a âˆ‚Î¼ = Î¼.real (t âˆ© s) â€¢ x :=
  calc
    âˆ« a in s, condExpInd G' hm Î¼ t x a âˆ‚Î¼ = âˆ« a in s, condExpIndSMul hm ht hÎ¼t x a âˆ‚Î¼ :=
      setIntegral_congr_ae (hm s hs)
        ((condExpInd_ae_eq_condExpIndSMul hm ht hÎ¼t x).mono fun _ hx _ â†¦ hx)
    _ = Î¼.real (t âˆ© s) â€¢ x := setIntegral_condExpIndSMul hs ht hÎ¼s hÎ¼t x

theorem condExpInd_of_measurable (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (c : G) :
    condExpInd G hm Î¼ s c = indicatorConstLp 1 (hm s hs) hÎ¼s c := by
  ext1
  refine EventuallyEq.trans ?_ indicatorConstLp_coeFn.symm
  refine (condExpInd_ae_eq_condExpIndSMul hm (hm s hs) hÎ¼s c).trans ?_
  refine (condExpIndSMul_ae_eq_smul hm (hm s hs) hÎ¼s c).trans ?_
  rw [condExpL2_indicator_of_measurable hm hs hÎ¼s (1 : â„)]
  refine (@indicatorConstLp_coeFn Î± _ _ 2 Î¼ _ s (hm s hs) hÎ¼s (1 : â„)).mono fun x hx â†¦ ?_
  dsimp only
  rw [hx]
  by_cases hx_mem : x âˆˆ s <;> simp [hx_mem]

theorem condExpInd_nonneg {E} [NormedAddCommGroup E] [PartialOrder E] [NormedSpace â„ E]
    [OrderedSMul â„ E] (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) (hx : 0 â‰¤ x) :
    0 â‰¤ condExpInd E hm Î¼ s x := by
  rw [â† coeFn_le]
  refine EventuallyLE.trans_eq ?_ (condExpInd_ae_eq_condExpIndSMul hm hs hÎ¼s x).symm
  exact (coeFn_zero E 1 Î¼).trans_le (condExpIndSMul_nonneg hs hÎ¼s x hx)

end CondexpInd

section CondexpL1


variable {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} {hm : m â‰¤ m0} [SigmaFinite (Î¼.trim hm)]
  {f g : Î± â†’ F'} {s : Set Î±}

variable (F')

/-- Conditional expectation of a function as a linear map from `Î± â†’â‚[Î¼] F'` to itself. -/
def condExpL1CLM (hm : m â‰¤ m0) (Î¼ : Measure Î±) [SigmaFinite (Î¼.trim hm)] :
    (Î± â†’â‚[Î¼] F') â†’L[â„] Î± â†’â‚[Î¼] F' :=
  L1.setToL1 (dominatedFinMeasAdditive_condExpInd F' hm Î¼)

variable {F'}

theorem condExpL1CLM_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] F') :
    condExpL1CLM F' hm Î¼ (c â€¢ f) = c â€¢ condExpL1CLM F' hm Î¼ f := by
  refine L1.setToL1_smul (dominatedFinMeasAdditive_condExpInd F' hm Î¼) ?_ c f
  exact fun c s x â†¦ condExpInd_smul' c x

theorem condExpL1CLM_indicatorConstLp (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : F') :
    (condExpL1CLM F' hm Î¼) (indicatorConstLp 1 hs hÎ¼s x) = condExpInd F' hm Î¼ s x :=
  L1.setToL1_indicatorConstLp (dominatedFinMeasAdditive_condExpInd F' hm Î¼) hs hÎ¼s x

theorem condExpL1CLM_indicatorConst (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : F') :
    (condExpL1CLM F' hm Î¼) â†‘(simpleFunc.indicatorConst 1 hs hÎ¼s x) = condExpInd F' hm Î¼ s x := by
  rw [Lp.simpleFunc.coe_indicatorConst]; exact condExpL1CLM_indicatorConstLp hs hÎ¼s x

/-- Auxiliary lemma used in the proof of `setIntegral_condExpL1CLM`. -/
theorem setIntegral_condExpL1CLM_of_measure_ne_top (f : Î± â†’â‚[Î¼] F') (hs : MeasurableSet[m] s)
    (hÎ¼s : Î¼ s â‰  âˆ) : âˆ« x in s, condExpL1CLM F' hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  refine @Lp.induction _ _ _ _ _ _ _ ENNReal.one_ne_top
    (fun f : Î± â†’â‚[Î¼] F' â†¦ âˆ« x in s, condExpL1CLM F' hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼) ?_ ?_
    (isClosed_eq ?_ ?_) f
  Â· intro x t ht hÎ¼t
    simp_rw [condExpL1CLM_indicatorConst ht hÎ¼t.ne x]
    rw [Lp.simpleFunc.coe_indicatorConst, setIntegral_indicatorConstLp (hm _ hs)]
    exact setIntegral_condExpInd hs ht hÎ¼s hÎ¼t.ne x
  Â· intro f g hf_Lp hg_Lp _ hf hg
    simp_rw [(condExpL1CLM F' hm Î¼).map_add]
    rw [setIntegral_congr_ae (hm s hs) ((Lp.coeFn_add (condExpL1CLM F' hm Î¼ (hf_Lp.toLp f))
      (condExpL1CLM F' hm Î¼ (hg_Lp.toLp g))).mono fun x hx _ â†¦ hx)]
    rw [setIntegral_congr_ae (hm s hs)
      ((Lp.coeFn_add (hf_Lp.toLp f) (hg_Lp.toLp g)).mono fun x hx _ â†¦ hx)]
    simp_rw [Pi.add_apply]
    rw [integral_add (L1.integrable_coeFn _).integrableOn (L1.integrable_coeFn _).integrableOn,
      integral_add (L1.integrable_coeFn _).integrableOn (L1.integrable_coeFn _).integrableOn, hf,
      hg]
  Â· exact (continuous_setIntegral s).comp (condExpL1CLM F' hm Î¼).continuous
  Â· exact continuous_setIntegral s

/-- The integral of the conditional expectation `condExpL1CLM` over an `m`-measurable set is equal
to the integral of `f` on that set. See also `setIntegral_condExp`, the similar statement for
`condExp`. -/
theorem setIntegral_condExpL1CLM (f : Î± â†’â‚[Î¼] F') (hs : MeasurableSet[m] s) :
    âˆ« x in s, condExpL1CLM F' hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  let S := spanningSets (Î¼.trim hm)
  have hS_meas : âˆ€ i, MeasurableSet[m] (S i) := measurableSet_spanningSets (Î¼.trim hm)
  have hS_meas0 : âˆ€ i, MeasurableSet (S i) := fun i â†¦ hm _ (hS_meas i)
  have hs_eq : s = â‹ƒ i, S i âˆ© s := by
    simp_rw [Set.inter_comm]
    rw [â† Set.inter_iUnion, iUnion_spanningSets (Î¼.trim hm), Set.inter_univ]
  have hS_finite : âˆ€ i, Î¼ (S i âˆ© s) < âˆ := by
    refine fun i â†¦ (measure_mono Set.inter_subset_left).trans_lt ?_
    have hS_finite_trim := measure_spanningSets_lt_top (Î¼.trim hm) i
    rwa [trim_measurableSet_eq hm (hS_meas i)] at hS_finite_trim
  have h_mono : Monotone fun i â†¦ S i âˆ© s := by
    intro i j hij x
    simp_rw [Set.mem_inter_iff]
    exact fun h â†¦ âŸ¨monotone_spanningSets (Î¼.trim hm) hij h.1, h.2âŸ©
  have h_eq_forall :
    (fun i â†¦ âˆ« x in S i âˆ© s, condExpL1CLM F' hm Î¼ f x âˆ‚Î¼) = fun i â†¦ âˆ« x in S i âˆ© s, f x âˆ‚Î¼ :=
    funext fun i â†¦
      setIntegral_condExpL1CLM_of_measure_ne_top f (@MeasurableSet.inter Î± m _ _ (hS_meas i) hs)
        (hS_finite i).ne
  have h_right : Tendsto (fun i â†¦ âˆ« x in S i âˆ© s, f x âˆ‚Î¼) atTop (ğ“ (âˆ« x in s, f x âˆ‚Î¼)) := by
    have h :=
      tendsto_setIntegral_of_monotone (fun i â†¦ (hS_meas0 i).inter (hm s hs)) h_mono
        (L1.integrable_coeFn f).integrableOn
    rwa [â† hs_eq] at h
  have h_left : Tendsto (fun i â†¦ âˆ« x in S i âˆ© s, condExpL1CLM F' hm Î¼ f x âˆ‚Î¼) atTop
      (ğ“ (âˆ« x in s, condExpL1CLM F' hm Î¼ f x âˆ‚Î¼)) := by
    have h := tendsto_setIntegral_of_monotone (fun i â†¦ (hS_meas0 i).inter (hm s hs)) h_mono
      (L1.integrable_coeFn (condExpL1CLM F' hm Î¼ f)).integrableOn
    rwa [â† hs_eq] at h
  rw [h_eq_forall] at h_left
  exact tendsto_nhds_unique h_left h_right

theorem aestronglyMeasurable_condExpL1CLM (f : Î± â†’â‚[Î¼] F') :
    AEStronglyMeasurable[m] (condExpL1CLM F' hm Î¼ f) Î¼ := by
  refine @Lp.induction _ _ _ _ _ _ _ ENNReal.one_ne_top
    (fun f : Î± â†’â‚[Î¼] F' â†¦ AEStronglyMeasurable[m] (condExpL1CLM F' hm Î¼ f) Î¼) ?_ ?_ ?_ f
  Â· intro c s hs hÎ¼s
    rw [condExpL1CLM_indicatorConst hs hÎ¼s.ne c]
    exact aestronglyMeasurable_condExpInd hs hÎ¼s.ne c
  Â· intro f g hf hg _ hfm hgm
    rw [(condExpL1CLM F' hm Î¼).map_add]
    exact (hfm.add hgm).congr (coeFn_add ..).symm
  Â· have : {f : Lp F' 1 Î¼ | AEStronglyMeasurable[m] (condExpL1CLM F' hm Î¼ f) Î¼} =
        condExpL1CLM F' hm Î¼ â»Â¹' {f | AEStronglyMeasurable[m] f Î¼} := rfl
    rw [this]
    refine IsClosed.preimage (condExpL1CLM F' hm Î¼).continuous ?_
    exact isClosed_aestronglyMeasurable hm

theorem condExpL1CLM_lpMeas (f : lpMeas F' â„ m 1 Î¼) :
    condExpL1CLM F' hm Î¼ (f : Î± â†’â‚[Î¼] F') = â†‘f := by
  let g := lpMeasToLpTrimLie F' â„ 1 Î¼ hm f
  have hfg : f = (lpMeasToLpTrimLie F' â„ 1 Î¼ hm).symm g := by
    simp only [g, LinearIsometryEquiv.symm_apply_apply]
  rw [hfg]
  refine @Lp.induction Î± F' m _ 1 (Î¼.trim hm) _ ENNReal.coe_ne_top (fun g : Î± â†’â‚[Î¼.trim hm] F' â†¦
    condExpL1CLM F' hm Î¼ ((lpMeasToLpTrimLie F' â„ 1 Î¼ hm).symm g : Î± â†’â‚[Î¼] F') =
    â†‘((lpMeasToLpTrimLie F' â„ 1 Î¼ hm).symm g)) ?_ ?_ ?_ g
  Â· intro c s hs hÎ¼s
    rw [@Lp.simpleFunc.coe_indicatorConst _ _ m, lpMeasToLpTrimLie_symm_indicator hs hÎ¼s.ne c,
      condExpL1CLM_indicatorConstLp]
    exact condExpInd_of_measurable hs ((le_trim hm).trans_lt hÎ¼s).ne c
  Â· intro f g hf hg _ hf_eq hg_eq
    rw [LinearIsometryEquiv.map_add]
    push_cast
    rw [map_add, hf_eq, hg_eq]
  Â· refine isClosed_eq ?_ ?_
    Â· refine (condExpL1CLM F' hm Î¼).continuous.comp (continuous_induced_dom.comp ?_)
      exact LinearIsometryEquiv.continuous _
    Â· refine continuous_induced_dom.comp ?_
      exact LinearIsometryEquiv.continuous _

theorem condExpL1CLM_of_aestronglyMeasurable' (f : Î± â†’â‚[Î¼] F') (hfm : AEStronglyMeasurable[m] f Î¼) :
    condExpL1CLM F' hm Î¼ f = f :=
  condExpL1CLM_lpMeas (âŸ¨f, hfmâŸ© : lpMeas F' â„ m 1 Î¼)

/-- Conditional expectation of a function, in L1. Its value is 0 if the function is not
integrable. The function-valued `condExp` should be used instead in most cases. -/
def condExpL1 (hm : m â‰¤ m0) (Î¼ : Measure Î±) [SigmaFinite (Î¼.trim hm)] (f : Î± â†’ F') : Î± â†’â‚[Î¼] F' :=
  setToFun Î¼ (condExpInd F' hm Î¼) (dominatedFinMeasAdditive_condExpInd F' hm Î¼) f

theorem condExpL1_undef (hf : Â¬Integrable f Î¼) : condExpL1 hm Î¼ f = 0 :=
  setToFun_undef (dominatedFinMeasAdditive_condExpInd F' hm Î¼) hf

theorem condExpL1_eq (hf : Integrable f Î¼) : condExpL1 hm Î¼ f = condExpL1CLM F' hm Î¼ (hf.toL1 f) :=
  setToFun_eq (dominatedFinMeasAdditive_condExpInd F' hm Î¼) hf

@[simp]
theorem condExpL1_zero : condExpL1 hm Î¼ (0 : Î± â†’ F') = 0 :=
  setToFun_zero _

@[simp]
theorem condExpL1_measure_zero (hm : m â‰¤ m0) : condExpL1 hm (0 : Measure Î±) f = 0 :=
  setToFun_measure_zero _ rfl

theorem aestronglyMeasurable_condExpL1 {f : Î± â†’ F'} :
    AEStronglyMeasurable[m] (condExpL1 hm Î¼ f) Î¼ := by
  by_cases hf : Integrable f Î¼
  Â· rw [condExpL1_eq hf]
    exact aestronglyMeasurable_condExpL1CLM _
  Â· rw [condExpL1_undef hf]
    exact stronglyMeasurable_zero.aestronglyMeasurable.congr (coeFn_zero ..).symm

theorem condExpL1_congr_ae (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)] (h : f =áµ[Î¼] g) :
    condExpL1 hm Î¼ f = condExpL1 hm Î¼ g :=
  setToFun_congr_ae _ h

theorem integrable_condExpL1 (f : Î± â†’ F') : Integrable (condExpL1 hm Î¼ f) Î¼ :=
  L1.integrable_coeFn _

/-- The integral of the conditional expectation `condExpL1` over an `m`-measurable set is equal to
the integral of `f` on that set. See also `setIntegral_condExp`, the similar statement for
`condExp`. -/
theorem setIntegral_condExpL1 (hf : Integrable f Î¼) (hs : MeasurableSet[m] s) :
    âˆ« x in s, condExpL1 hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  simp_rw [condExpL1_eq hf]
  rw [setIntegral_condExpL1CLM (hf.toL1 f) hs]
  exact setIntegral_congr_ae (hm s hs) (hf.coeFn_toL1.mono fun x hx _ â†¦ hx)

theorem condExpL1_add (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    condExpL1 hm Î¼ (f + g) = condExpL1 hm Î¼ f + condExpL1 hm Î¼ g :=
  setToFun_add _ hf hg

theorem condExpL1_neg (f : Î± â†’ F') : condExpL1 hm Î¼ (-f) = -condExpL1 hm Î¼ f :=
  setToFun_neg _ f

theorem condExpL1_smul (c : ğ•œ) (f : Î± â†’ F') : condExpL1 hm Î¼ (c â€¢ f) = c â€¢ condExpL1 hm Î¼ f := by
  refine setToFun_smul _ ?_ c f
  exact fun c _ x â†¦ condExpInd_smul' c x

theorem condExpL1_sub (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    condExpL1 hm Î¼ (f - g) = condExpL1 hm Î¼ f - condExpL1 hm Î¼ g :=
  setToFun_sub _ hf hg

theorem condExpL1_of_aestronglyMeasurable' (hfm : AEStronglyMeasurable[m] f Î¼)
    (hfi : Integrable f Î¼) : condExpL1 hm Î¼ f =áµ[Î¼] f := by
  rw [condExpL1_eq hfi]
  refine EventuallyEq.trans ?_ (Integrable.coeFn_toL1 hfi)
  rw [condExpL1CLM_of_aestronglyMeasurable']
  exact hfm.congr hfi.coeFn_toL1.symm

theorem condExpL1_mono {E}
    [NormedAddCommGroup E] [PartialOrder E] [OrderClosedTopology E] [IsOrderedAddMonoid E]
    [CompleteSpace E] [NormedSpace â„ E]
    [OrderedSMul â„ E] {f g : Î± â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) (hfg : f â‰¤áµ[Î¼] g) :
    condExpL1 hm Î¼ f â‰¤áµ[Î¼] condExpL1 hm Î¼ g := by
  rw [coeFn_le]
  have h_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x : E, 0 â‰¤ x â†’ 0 â‰¤ condExpInd E hm Î¼ s x :=
    fun s hs hÎ¼s x hx â†¦ condExpInd_nonneg hs hÎ¼s.ne x hx
  exact setToFun_mono (dominatedFinMeasAdditive_condExpInd E hm Î¼) h_nonneg hf hg hfg

end CondexpL1

end MeasureTheory
