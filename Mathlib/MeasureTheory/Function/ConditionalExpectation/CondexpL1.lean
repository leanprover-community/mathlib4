/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.CondexpL2

#align_import measure_theory.function.conditional_expectation.condexp_L1 from "leanprover-community/mathlib"@"d8bbb04e2d2a44596798a9207ceefc0fb236e41e"

/-! # Conditional expectation in L1

This file contains two more steps of the construction of the conditional expectation, which is
completed in `MeasureTheory.Function.ConditionalExpectation.Basic`. See that file for a
description of the full process.

The contitional expectation of an `L¬≤` function is defined in
`MeasureTheory.Function.ConditionalExpectation.CondexpL2`. In this file, we perform two steps.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `Set Œ± ‚Üí (E ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E))` which to a set associates a linear
  map. That linear map sends `x ‚àà E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condexpL1CLM : (Œ± ‚Üí‚ÇÅ[Œº] E) ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E)`. This is done using the same
  construction as the Bochner integral (see the file `MeasureTheory/Integral/SetToL1`).

## Main definitions

* `condexpL1`: Conditional expectation of a function as a linear map from `L1` to itself.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Œ± Œ≤ F F' G G' ùïú : Type*} {p : ‚Ñù‚â•0‚àû} [RCLike ùïú]
  -- ùïú for ‚Ñù or ‚ÑÇ
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ùïú F]
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace ùïú F'] [NormedSpace ‚Ñù F'] [CompleteSpace F']
  -- G for a Lp add_subgroup
  [NormedAddCommGroup G]
  -- G' for integrals on a Lp add_subgroup
  [NormedAddCommGroup G']
  [NormedSpace ‚Ñù G'] [CompleteSpace G']

section CondexpInd

/-! ## Conditional expectation of an indicator as a continuous linear map.

The goal of this section is to build
`condexpInd (hm : m ‚â§ m0) (Œº : Measure Œ±) (s : Set s) : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G`, which
takes `x : G` to the conditional expectation of the indicator of the set `s` with value `x`,
seen as an element of `Œ± ‚Üí‚ÇÅ[Œº] G`.
-/


variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {s t : Set Œ±} [NormedSpace ‚Ñù G]

section CondexpIndL1Fin

set_option linter.uppercaseLean3 false

/-- Conditional expectation of the indicator of a measurable set with finite measure,
as a function in L1. -/
def condexpIndL1Fin (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)] (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû)
    (x : G) : Œ± ‚Üí‚ÇÅ[Œº] G :=
  (integrable_condexpIndSMul hm hs hŒºs x).toL1 _
#align measure_theory.condexp_ind_L1_fin MeasureTheory.condexpIndL1Fin

theorem condexpIndL1Fin_ae_eq_condexpIndSMul (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    condexpIndL1Fin hm hs hŒºs x =·µê[Œº] condexpIndSMul hm hs hŒºs x :=
  (integrable_condexpIndSMul hm hs hŒºs x).coeFn_toL1
#align measure_theory.condexp_ind_L1_fin_ae_eq_condexp_ind_smul MeasureTheory.condexpIndL1Fin_ae_eq_condexpIndSMul

variable {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]

-- Porting note: this lemma fills the hole in `refine' (Mem‚Ñíp.coeFn_toLp _) ...`
-- which is not automatically filled in Lean 4
private theorem q {hs : MeasurableSet s} {hŒºs : Œº s ‚â† ‚àû} {x : G} :
    Mem‚Ñíp (condexpIndSMul hm hs hŒºs x) 1 Œº := by
  rw [mem‚Ñíp_one_iff_integrable]; apply integrable_condexpIndSMul

theorem condexpIndL1Fin_add (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x y : G) :
    condexpIndL1Fin hm hs hŒºs (x + y) =
    condexpIndL1Fin hm hs hŒºs x + condexpIndL1Fin hm hs hŒºs y := by
  ext1
  refine (Mem‚Ñíp.coeFn_toLp q).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_add _ _).symm
  refine EventuallyEq.trans ?_
    (EventuallyEq.add (Mem‚Ñíp.coeFn_toLp q).symm (Mem‚Ñíp.coeFn_toLp q).symm)
  rw [condexpIndSMul_add]
  refine (Lp.coeFn_add _ _).trans (eventually_of_forall fun a => ?_)
  rfl
#align measure_theory.condexp_ind_L1_fin_add MeasureTheory.condexpIndL1Fin_add

theorem condexpIndL1Fin_smul (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (c : ‚Ñù) (x : G) :
    condexpIndL1Fin hm hs hŒºs (c ‚Ä¢ x) = c ‚Ä¢ condexpIndL1Fin hm hs hŒºs x := by
  ext1
  refine (Mem‚Ñíp.coeFn_toLp q).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_smul _ _).symm
  rw [condexpIndSMul_smul hs hŒºs c x]
  refine (Lp.coeFn_smul _ _).trans ?_
  refine (condexpIndL1Fin_ae_eq_condexpIndSMul hm hs hŒºs x).mono fun y hy => ?_
  simp only [Pi.smul_apply, hy]
#align measure_theory.condexp_ind_L1_fin_smul MeasureTheory.condexpIndL1Fin_smul

theorem condexpIndL1Fin_smul' [NormedSpace ‚Ñù F] [SMulCommClass ‚Ñù ùïú F] (hs : MeasurableSet s)
    (hŒºs : Œº s ‚â† ‚àû) (c : ùïú) (x : F) :
    condexpIndL1Fin hm hs hŒºs (c ‚Ä¢ x) = c ‚Ä¢ condexpIndL1Fin hm hs hŒºs x := by
  ext1
  refine (Mem‚Ñíp.coeFn_toLp q).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_smul _ _).symm
  rw [condexpIndSMul_smul' hs hŒºs c x]
  refine (Lp.coeFn_smul _ _).trans ?_
  refine (condexpIndL1Fin_ae_eq_condexpIndSMul hm hs hŒºs x).mono fun y hy => ?_
  simp only [Pi.smul_apply, hy]
#align measure_theory.condexp_ind_L1_fin_smul' MeasureTheory.condexpIndL1Fin_smul'

theorem norm_condexpIndL1Fin_le (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    ‚ÄñcondexpIndL1Fin hm hs hŒºs x‚Äñ ‚â§ (Œº s).toReal * ‚Äñx‚Äñ := by
  have : 0 ‚â§ ‚à´ a : Œ±, ‚ÄñcondexpIndL1Fin hm hs hŒºs x a‚Äñ ‚àÇŒº := by positivity
  rw [L1.norm_eq_integral_norm, ‚Üê ENNReal.toReal_ofReal (norm_nonneg x), ‚Üê ENNReal.toReal_mul, ‚Üê
    ENNReal.toReal_ofReal this,
    ENNReal.toReal_le_toReal ENNReal.ofReal_ne_top (ENNReal.mul_ne_top hŒºs ENNReal.ofReal_ne_top),
    ofReal_integral_norm_eq_lintegral_nnnorm]
  swap; ¬∑ rw [‚Üê mem‚Ñíp_one_iff_integrable]; exact Lp.mem‚Ñíp _
  have h_eq :
    ‚à´‚Åª a, ‚ÄñcondexpIndL1Fin hm hs hŒºs x a‚Äñ‚Çä ‚àÇŒº = ‚à´‚Åª a, ‚ÄñcondexpIndSMul hm hs hŒºs x a‚Äñ‚Çä ‚àÇŒº := by
    refine lintegral_congr_ae ?_
    refine (condexpIndL1Fin_ae_eq_condexpIndSMul hm hs hŒºs x).mono fun z hz => ?_
    dsimp only
    rw [hz]
  rw [h_eq, ofReal_norm_eq_coe_nnnorm]
  exact lintegral_nnnorm_condexpIndSMul_le hm hs hŒºs x
#align measure_theory.norm_condexp_ind_L1_fin_le MeasureTheory.norm_condexpIndL1Fin_le

theorem condexpIndL1Fin_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (hst : s ‚à© t = ‚àÖ) (x : G) :
    condexpIndL1Fin hm (hs.union ht) ((measure_union_le s t).trans_lt
      (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr ‚ü®hŒºs, hŒºt‚ü©))).ne x =
    condexpIndL1Fin hm hs hŒºs x + condexpIndL1Fin hm ht hŒºt x := by
  ext1
  have hŒºst :=
    ((measure_union_le s t).trans_lt (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr ‚ü®hŒºs, hŒºt‚ü©))).ne
  refine (condexpIndL1Fin_ae_eq_condexpIndSMul hm (hs.union ht) hŒºst x).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_add _ _).symm
  have hs_eq := condexpIndL1Fin_ae_eq_condexpIndSMul hm hs hŒºs x
  have ht_eq := condexpIndL1Fin_ae_eq_condexpIndSMul hm ht hŒºt x
  refine EventuallyEq.trans ?_ (EventuallyEq.add hs_eq.symm ht_eq.symm)
  rw [condexpIndSMul]
  rw [indicatorConstLp_disjoint_union hs ht hŒºs hŒºt hst (1 : ‚Ñù)]
  rw [(condexpL2 ‚Ñù ‚Ñù hm).map_add]
  push_cast
  rw [((toSpanSingleton ‚Ñù x).compLpL 2 Œº).map_add]
  refine (Lp.coeFn_add _ _).trans ?_
  filter_upwards with y using rfl
#align measure_theory.condexp_ind_L1_fin_disjoint_union MeasureTheory.condexpIndL1Fin_disjoint_union

end CondexpIndL1Fin

open scoped Classical

section CondexpIndL1

set_option linter.uppercaseLean3 false

/-- Conditional expectation of the indicator of a set, as a function in L1. Its value for sets
which are not both measurable and of finite measure is not used: we set it to 0. -/
def condexpIndL1 {m m0 : MeasurableSpace Œ±} (hm : m ‚â§ m0) (Œº : Measure Œ±) (s : Set Œ±)
    [SigmaFinite (Œº.trim hm)] (x : G) : Œ± ‚Üí‚ÇÅ[Œº] G :=
  if hs : MeasurableSet s ‚àß Œº s ‚â† ‚àû then condexpIndL1Fin hm hs.1 hs.2 x else 0
#align measure_theory.condexp_ind_L1 MeasureTheory.condexpIndL1

variable {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]

theorem condexpIndL1_of_measurableSet_of_measure_ne_top (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû)
    (x : G) : condexpIndL1 hm Œº s x = condexpIndL1Fin hm hs hŒºs x := by
  simp only [condexpIndL1, And.intro hs hŒºs, dif_pos, Ne, not_false_iff, and_self_iff]
#align measure_theory.condexp_ind_L1_of_measurable_set_of_measure_ne_top MeasureTheory.condexpIndL1_of_measurableSet_of_measure_ne_top

theorem condexpIndL1_of_measure_eq_top (hŒºs : Œº s = ‚àû) (x : G) : condexpIndL1 hm Œº s x = 0 := by
  simp only [condexpIndL1, hŒºs, eq_self_iff_true, not_true, Ne, dif_neg, not_false_iff,
    and_false_iff]
#align measure_theory.condexp_ind_L1_of_measure_eq_top MeasureTheory.condexpIndL1_of_measure_eq_top

theorem condexpIndL1_of_not_measurableSet (hs : ¬¨MeasurableSet s) (x : G) :
    condexpIndL1 hm Œº s x = 0 := by
  simp only [condexpIndL1, hs, dif_neg, not_false_iff, false_and_iff]
#align measure_theory.condexp_ind_L1_of_not_measurable_set MeasureTheory.condexpIndL1_of_not_measurableSet

theorem condexpIndL1_add (x y : G) :
    condexpIndL1 hm Œº s (x + y) = condexpIndL1 hm Œº s x + condexpIndL1 hm Œº s y := by
  by_cases hs : MeasurableSet s
  swap; ¬∑ simp_rw [condexpIndL1_of_not_measurableSet hs]; rw [zero_add]
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ simp_rw [condexpIndL1_of_measure_eq_top hŒºs]; rw [zero_add]
  ¬∑ simp_rw [condexpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs]
    exact condexpIndL1Fin_add hs hŒºs x y
#align measure_theory.condexp_ind_L1_add MeasureTheory.condexpIndL1_add

theorem condexpIndL1_smul (c : ‚Ñù) (x : G) :
    condexpIndL1 hm Œº s (c ‚Ä¢ x) = c ‚Ä¢ condexpIndL1 hm Œº s x := by
  by_cases hs : MeasurableSet s
  swap; ¬∑ simp_rw [condexpIndL1_of_not_measurableSet hs]; rw [smul_zero]
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ simp_rw [condexpIndL1_of_measure_eq_top hŒºs]; rw [smul_zero]
  ¬∑ simp_rw [condexpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs]
    exact condexpIndL1Fin_smul hs hŒºs c x
#align measure_theory.condexp_ind_L1_smul MeasureTheory.condexpIndL1_smul

theorem condexpIndL1_smul' [NormedSpace ‚Ñù F] [SMulCommClass ‚Ñù ùïú F] (c : ùïú) (x : F) :
    condexpIndL1 hm Œº s (c ‚Ä¢ x) = c ‚Ä¢ condexpIndL1 hm Œº s x := by
  by_cases hs : MeasurableSet s
  swap; ¬∑ simp_rw [condexpIndL1_of_not_measurableSet hs]; rw [smul_zero]
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ simp_rw [condexpIndL1_of_measure_eq_top hŒºs]; rw [smul_zero]
  ¬∑ simp_rw [condexpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs]
    exact condexpIndL1Fin_smul' hs hŒºs c x
#align measure_theory.condexp_ind_L1_smul' MeasureTheory.condexpIndL1_smul'

theorem norm_condexpIndL1_le (x : G) : ‚ÄñcondexpIndL1 hm Œº s x‚Äñ ‚â§ (Œº s).toReal * ‚Äñx‚Äñ := by
  by_cases hs : MeasurableSet s
  swap
  ¬∑ simp_rw [condexpIndL1_of_not_measurableSet hs]; rw [Lp.norm_zero]
    exact mul_nonneg ENNReal.toReal_nonneg (norm_nonneg _)
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ rw [condexpIndL1_of_measure_eq_top hŒºs x, Lp.norm_zero]
    exact mul_nonneg ENNReal.toReal_nonneg (norm_nonneg _)
  ¬∑ rw [condexpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs x]
    exact norm_condexpIndL1Fin_le hs hŒºs x
#align measure_theory.norm_condexp_ind_L1_le MeasureTheory.norm_condexpIndL1_le

theorem continuous_condexpIndL1 : Continuous fun x : G => condexpIndL1 hm Œº s x :=
  continuous_of_linear_of_bound condexpIndL1_add condexpIndL1_smul norm_condexpIndL1_le
#align measure_theory.continuous_condexp_ind_L1 MeasureTheory.continuous_condexpIndL1

theorem condexpIndL1_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (hst : s ‚à© t = ‚àÖ) (x : G) :
    condexpIndL1 hm Œº (s ‚à™ t) x = condexpIndL1 hm Œº s x + condexpIndL1 hm Œº t x := by
  have hŒºst : Œº (s ‚à™ t) ‚â† ‚àû :=
    ((measure_union_le s t).trans_lt (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr ‚ü®hŒºs, hŒºt‚ü©))).ne
  rw [condexpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs x,
    condexpIndL1_of_measurableSet_of_measure_ne_top ht hŒºt x,
    condexpIndL1_of_measurableSet_of_measure_ne_top (hs.union ht) hŒºst x]
  exact condexpIndL1Fin_disjoint_union hs ht hŒºs hŒºt hst x
#align measure_theory.condexp_ind_L1_disjoint_union MeasureTheory.condexpIndL1_disjoint_union

end CondexpIndL1

-- Porting note: `G` is not automatically inferred in `condexpInd` in Lean 4;
-- to avoid repeatedly typing `(G := ...)` it is made explicit.
variable (G)

/-- Conditional expectation of the indicator of a set, as a linear map from `G` to L1. -/
def condexpInd {m m0 : MeasurableSpace Œ±} (hm : m ‚â§ m0) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)]
    (s : Set Œ±) : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G where
  toFun := condexpIndL1 hm Œº s
  map_add' := condexpIndL1_add
  map_smul' := condexpIndL1_smul
  cont := continuous_condexpIndL1
#align measure_theory.condexp_ind MeasureTheory.condexpInd

variable {G}

theorem condexpInd_ae_eq_condexpIndSMul (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    condexpInd G hm Œº s x =·µê[Œº] condexpIndSMul hm hs hŒºs x := by
  refine EventuallyEq.trans ?_ (condexpIndL1Fin_ae_eq_condexpIndSMul hm hs hŒºs x)
  simp [condexpInd, condexpIndL1, hs, hŒºs]
#align measure_theory.condexp_ind_ae_eq_condexp_ind_smul MeasureTheory.condexpInd_ae_eq_condexpIndSMul

variable {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]

theorem aestronglyMeasurable'_condexpInd (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    AEStronglyMeasurable' m (condexpInd G hm Œº s x) Œº :=
  AEStronglyMeasurable'.congr (aeStronglyMeasurable'_condexpIndSMul hm hs hŒºs x)
    (condexpInd_ae_eq_condexpIndSMul hm hs hŒºs x).symm
#align measure_theory.ae_strongly_measurable'_condexp_ind MeasureTheory.aestronglyMeasurable'_condexpInd

@[simp]
theorem condexpInd_empty : condexpInd G hm Œº ‚àÖ = (0 : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G) := by
  ext1 x
  ext1
  refine (condexpInd_ae_eq_condexpIndSMul hm MeasurableSet.empty (by simp) x).trans ?_
  rw [condexpIndSMul_empty]
  refine (Lp.coeFn_zero G 2 Œº).trans ?_
  refine EventuallyEq.trans ?_ (Lp.coeFn_zero G 1 Œº).symm
  rfl
#align measure_theory.condexp_ind_empty MeasureTheory.condexpInd_empty

theorem condexpInd_smul' [NormedSpace ‚Ñù F] [SMulCommClass ‚Ñù ùïú F] (c : ùïú) (x : F) :
    condexpInd F hm Œº s (c ‚Ä¢ x) = c ‚Ä¢ condexpInd F hm Œº s x :=
  condexpIndL1_smul' c x
#align measure_theory.condexp_ind_smul' MeasureTheory.condexpInd_smul'

theorem norm_condexpInd_apply_le (x : G) : ‚ÄñcondexpInd G hm Œº s x‚Äñ ‚â§ (Œº s).toReal * ‚Äñx‚Äñ :=
  norm_condexpIndL1_le x
#align measure_theory.norm_condexp_ind_apply_le MeasureTheory.norm_condexpInd_apply_le

theorem norm_condexpInd_le : ‚Äñ(condexpInd G hm Œº s : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G)‚Äñ ‚â§ (Œº s).toReal :=
  ContinuousLinearMap.opNorm_le_bound _ ENNReal.toReal_nonneg norm_condexpInd_apply_le
#align measure_theory.norm_condexp_ind_le MeasureTheory.norm_condexpInd_le

theorem condexpInd_disjoint_union_apply (hs : MeasurableSet s) (ht : MeasurableSet t)
    (hŒºs : Œº s ‚â† ‚àû) (hŒºt : Œº t ‚â† ‚àû) (hst : s ‚à© t = ‚àÖ) (x : G) :
    condexpInd G hm Œº (s ‚à™ t) x = condexpInd G hm Œº s x + condexpInd G hm Œº t x :=
  condexpIndL1_disjoint_union hs ht hŒºs hŒºt hst x
#align measure_theory.condexp_ind_disjoint_union_apply MeasureTheory.condexpInd_disjoint_union_apply

theorem condexpInd_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (hst : s ‚à© t = ‚àÖ) : (condexpInd G hm Œº (s ‚à™ t) : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G) =
    condexpInd G hm Œº s + condexpInd G hm Œº t := by
  ext1 x; push_cast; exact condexpInd_disjoint_union_apply hs ht hŒºs hŒºt hst x
#align measure_theory.condexp_ind_disjoint_union MeasureTheory.condexpInd_disjoint_union

variable (G)

theorem dominatedFinMeasAdditive_condexpInd (hm : m ‚â§ m0) (Œº : Measure Œ±)
    [SigmaFinite (Œº.trim hm)] :
    DominatedFinMeasAdditive Œº (condexpInd G hm Œº : Set Œ± ‚Üí G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G) 1 :=
  ‚ü®fun _ _ => condexpInd_disjoint_union, fun _ _ _ => norm_condexpInd_le.trans (one_mul _).symm.le‚ü©
#align measure_theory.dominated_fin_meas_additive_condexp_ind MeasureTheory.dominatedFinMeasAdditive_condexpInd

variable {G}

theorem setIntegral_condexpInd (hs : MeasurableSet[m] s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (x : G') : ‚à´ a in s, condexpInd G' hm Œº t x a ‚àÇŒº = (Œº (t ‚à© s)).toReal ‚Ä¢ x :=
  calc
    ‚à´ a in s, condexpInd G' hm Œº t x a ‚àÇŒº = ‚à´ a in s, condexpIndSMul hm ht hŒºt x a ‚àÇŒº :=
      setIntegral_congr_ae (hm s hs)
        ((condexpInd_ae_eq_condexpIndSMul hm ht hŒºt x).mono fun _ hx _ => hx)
    _ = (Œº (t ‚à© s)).toReal ‚Ä¢ x := setIntegral_condexpIndSMul hs ht hŒºs hŒºt x
#align measure_theory.set_integral_condexp_ind MeasureTheory.setIntegral_condexpInd

@[deprecated (since := "2024-04-17")]
alias set_integral_condexpInd := setIntegral_condexpInd

theorem condexpInd_of_measurable (hs : MeasurableSet[m] s) (hŒºs : Œº s ‚â† ‚àû) (c : G) :
    condexpInd G hm Œº s c = indicatorConstLp 1 (hm s hs) hŒºs c := by
  ext1
  refine EventuallyEq.trans ?_ indicatorConstLp_coeFn.symm
  refine (condexpInd_ae_eq_condexpIndSMul hm (hm s hs) hŒºs c).trans ?_
  refine (condexpIndSMul_ae_eq_smul hm (hm s hs) hŒºs c).trans ?_
  rw [lpMeas_coe, condexpL2_indicator_of_measurable hm hs hŒºs (1 : ‚Ñù)]
  refine (@indicatorConstLp_coeFn Œ± _ _ 2 Œº _ s (hm s hs) hŒºs (1 : ‚Ñù)).mono fun x hx => ?_
  dsimp only
  rw [hx]
  by_cases hx_mem : x ‚àà s <;> simp [hx_mem]
#align measure_theory.condexp_ind_of_measurable MeasureTheory.condexpInd_of_measurable

theorem condexpInd_nonneg {E} [NormedLatticeAddCommGroup E] [NormedSpace ‚Ñù E] [OrderedSMul ‚Ñù E]
    (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : E) (hx : 0 ‚â§ x) : 0 ‚â§ condexpInd E hm Œº s x := by
  rw [‚Üê coeFn_le]
  refine EventuallyLE.trans_eq ?_ (condexpInd_ae_eq_condexpIndSMul hm hs hŒºs x).symm
  exact (coeFn_zero E 1 Œº).trans_le (condexpIndSMul_nonneg hs hŒºs x hx)
#align measure_theory.condexp_ind_nonneg MeasureTheory.condexpInd_nonneg

end CondexpInd

section CondexpL1

set_option linter.uppercaseLean3 false

variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]
  {f g : Œ± ‚Üí F'} {s : Set Œ±}

-- Porting note: `F'` is not automatically inferred in `condexpL1CLM` in Lean 4;
-- to avoid repeatedly typing `(F' := ...)` it is made explicit.
variable (F')

/-- Conditional expectation of a function as a linear map from `Œ± ‚Üí‚ÇÅ[Œº] F'` to itself. -/
def condexpL1CLM (hm : m ‚â§ m0) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)] :
    (Œ± ‚Üí‚ÇÅ[Œº] F') ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] F' :=
  L1.setToL1 (dominatedFinMeasAdditive_condexpInd F' hm Œº)
#align measure_theory.condexp_L1_clm MeasureTheory.condexpL1CLM

variable {F'}

theorem condexpL1CLM_smul (c : ùïú) (f : Œ± ‚Üí‚ÇÅ[Œº] F') :
    condexpL1CLM F' hm Œº (c ‚Ä¢ f) = c ‚Ä¢ condexpL1CLM F' hm Œº f := by
  refine L1.setToL1_smul (dominatedFinMeasAdditive_condexpInd F' hm Œº) ?_ c f
  exact fun c s x => condexpInd_smul' c x
#align measure_theory.condexp_L1_clm_smul MeasureTheory.condexpL1CLM_smul

theorem condexpL1CLM_indicatorConstLp (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : F') :
    (condexpL1CLM F' hm Œº) (indicatorConstLp 1 hs hŒºs x) = condexpInd F' hm Œº s x :=
  L1.setToL1_indicatorConstLp (dominatedFinMeasAdditive_condexpInd F' hm Œº) hs hŒºs x
#align measure_theory.condexp_L1_clm_indicator_const_Lp MeasureTheory.condexpL1CLM_indicatorConstLp

theorem condexpL1CLM_indicatorConst (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : F') :
    (condexpL1CLM F' hm Œº) ‚Üë(simpleFunc.indicatorConst 1 hs hŒºs x) = condexpInd F' hm Œº s x := by
  rw [Lp.simpleFunc.coe_indicatorConst]; exact condexpL1CLM_indicatorConstLp hs hŒºs x
#align measure_theory.condexp_L1_clm_indicator_const MeasureTheory.condexpL1CLM_indicatorConst

/-- Auxiliary lemma used in the proof of `setIntegral_condexpL1CLM`. -/
theorem setIntegral_condexpL1CLM_of_measure_ne_top (f : Œ± ‚Üí‚ÇÅ[Œº] F') (hs : MeasurableSet[m] s)
    (hŒºs : Œº s ‚â† ‚àû) : ‚à´ x in s, condexpL1CLM F' hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  refine @Lp.induction _ _ _ _ _ _ _ ENNReal.one_ne_top
    (fun f : Œ± ‚Üí‚ÇÅ[Œº] F' => ‚à´ x in s, condexpL1CLM F' hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº) ?_ ?_
    (isClosed_eq ?_ ?_) f
  ¬∑ intro x t ht hŒºt
    simp_rw [condexpL1CLM_indicatorConst ht hŒºt.ne x]
    rw [Lp.simpleFunc.coe_indicatorConst, setIntegral_indicatorConstLp (hm _ hs)]
    exact setIntegral_condexpInd hs ht hŒºs hŒºt.ne x
  ¬∑ intro f g hf_Lp hg_Lp _ hf hg
    simp_rw [(condexpL1CLM F' hm Œº).map_add]
    rw [setIntegral_congr_ae (hm s hs) ((Lp.coeFn_add (condexpL1CLM F' hm Œº (hf_Lp.toLp f))
      (condexpL1CLM F' hm Œº (hg_Lp.toLp g))).mono fun x hx _ => hx)]
    rw [setIntegral_congr_ae (hm s hs)
      ((Lp.coeFn_add (hf_Lp.toLp f) (hg_Lp.toLp g)).mono fun x hx _ => hx)]
    simp_rw [Pi.add_apply]
    rw [integral_add (L1.integrable_coeFn _).integrableOn (L1.integrable_coeFn _).integrableOn,
      integral_add (L1.integrable_coeFn _).integrableOn (L1.integrable_coeFn _).integrableOn, hf,
      hg]
  ¬∑ exact (continuous_setIntegral s).comp (condexpL1CLM F' hm Œº).continuous
  ¬∑ exact continuous_setIntegral s
#align measure_theory.set_integral_condexp_L1_clm_of_measure_ne_top MeasureTheory.setIntegral_condexpL1CLM_of_measure_ne_top

@[deprecated (since := "2024-04-17")]
alias set_integral_condexpL1CLM_of_measure_ne_top :=
  setIntegral_condexpL1CLM_of_measure_ne_top

/-- The integral of the conditional expectation `condexpL1CLM` over an `m`-measurable set is equal
to the integral of `f` on that set. See also `setIntegral_condexp`, the similar statement for
`condexp`. -/
theorem setIntegral_condexpL1CLM (f : Œ± ‚Üí‚ÇÅ[Œº] F') (hs : MeasurableSet[m] s) :
    ‚à´ x in s, condexpL1CLM F' hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  let S := spanningSets (Œº.trim hm)
  have hS_meas : ‚àÄ i, MeasurableSet[m] (S i) := measurable_spanningSets (Œº.trim hm)
  have hS_meas0 : ‚àÄ i, MeasurableSet (S i) := fun i => hm _ (hS_meas i)
  have hs_eq : s = ‚ãÉ i, S i ‚à© s := by
    simp_rw [Set.inter_comm]
    rw [‚Üê Set.inter_iUnion, iUnion_spanningSets (Œº.trim hm), Set.inter_univ]
  have hS_finite : ‚àÄ i, Œº (S i ‚à© s) < ‚àû := by
    refine fun i => (measure_mono (Set.inter_subset_left _ _)).trans_lt ?_
    have hS_finite_trim := measure_spanningSets_lt_top (Œº.trim hm) i
    rwa [trim_measurableSet_eq hm (hS_meas i)] at hS_finite_trim
  have h_mono : Monotone fun i => S i ‚à© s := by
    intro i j hij x
    simp_rw [Set.mem_inter_iff]
    exact fun h => ‚ü®monotone_spanningSets (Œº.trim hm) hij h.1, h.2‚ü©
  have h_eq_forall :
    (fun i => ‚à´ x in S i ‚à© s, condexpL1CLM F' hm Œº f x ‚àÇŒº) = fun i => ‚à´ x in S i ‚à© s, f x ‚àÇŒº :=
    funext fun i =>
      setIntegral_condexpL1CLM_of_measure_ne_top f (@MeasurableSet.inter Œ± m _ _ (hS_meas i) hs)
        (hS_finite i).ne
  have h_right : Tendsto (fun i => ‚à´ x in S i ‚à© s, f x ‚àÇŒº) atTop (ùìù (‚à´ x in s, f x ‚àÇŒº)) := by
    have h :=
      tendsto_setIntegral_of_monotone (fun i => (hS_meas0 i).inter (hm s hs)) h_mono
        (L1.integrable_coeFn f).integrableOn
    rwa [‚Üê hs_eq] at h
  have h_left : Tendsto (fun i => ‚à´ x in S i ‚à© s, condexpL1CLM F' hm Œº f x ‚àÇŒº) atTop
      (ùìù (‚à´ x in s, condexpL1CLM F' hm Œº f x ‚àÇŒº)) := by
    have h := tendsto_setIntegral_of_monotone (fun i => (hS_meas0 i).inter (hm s hs)) h_mono
      (L1.integrable_coeFn (condexpL1CLM F' hm Œº f)).integrableOn
    rwa [‚Üê hs_eq] at h
  rw [h_eq_forall] at h_left
  exact tendsto_nhds_unique h_left h_right
#align measure_theory.set_integral_condexp_L1_clm MeasureTheory.setIntegral_condexpL1CLM

@[deprecated (since := "2024-04-17")]
alias set_integral_condexpL1CLM := setIntegral_condexpL1CLM

theorem aestronglyMeasurable'_condexpL1CLM (f : Œ± ‚Üí‚ÇÅ[Œº] F') :
    AEStronglyMeasurable' m (condexpL1CLM F' hm Œº f) Œº := by
  refine @Lp.induction _ _ _ _ _ _ _ ENNReal.one_ne_top
    (fun f : Œ± ‚Üí‚ÇÅ[Œº] F' => AEStronglyMeasurable' m (condexpL1CLM F' hm Œº f) Œº) ?_ ?_ ?_ f
  ¬∑ intro c s hs hŒºs
    rw [condexpL1CLM_indicatorConst hs hŒºs.ne c]
    exact aestronglyMeasurable'_condexpInd hs hŒºs.ne c
  ¬∑ intro f g hf hg _ hfm hgm
    rw [(condexpL1CLM F' hm Œº).map_add]
    refine AEStronglyMeasurable'.congr ?_ (coeFn_add _ _).symm
    exact AEStronglyMeasurable'.add hfm hgm
  ¬∑ have : {f : Lp F' 1 Œº | AEStronglyMeasurable' m (condexpL1CLM F' hm Œº f) Œº} =
        condexpL1CLM F' hm Œº ‚Åª¬π' {f | AEStronglyMeasurable' m f Œº} := rfl
    rw [this]
    refine IsClosed.preimage (condexpL1CLM F' hm Œº).continuous ?_
    exact isClosed_aeStronglyMeasurable' hm
#align measure_theory.ae_strongly_measurable'_condexp_L1_clm MeasureTheory.aestronglyMeasurable'_condexpL1CLM

theorem condexpL1CLM_lpMeas (f : lpMeas F' ‚Ñù m 1 Œº) :
    condexpL1CLM F' hm Œº (f : Œ± ‚Üí‚ÇÅ[Œº] F') = ‚Üëf := by
  let g := lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm f
  have hfg : f = (lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm).symm g := by
    simp only [g, LinearIsometryEquiv.symm_apply_apply]
  rw [hfg]
  refine @Lp.induction Œ± F' m _ 1 (Œº.trim hm) _ ENNReal.coe_ne_top (fun g : Œ± ‚Üí‚ÇÅ[Œº.trim hm] F' =>
    condexpL1CLM F' hm Œº ((lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm).symm g : Œ± ‚Üí‚ÇÅ[Œº] F') =
    ‚Üë((lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm).symm g)) ?_ ?_ ?_ g
  ¬∑ intro c s hs hŒºs
    rw [@Lp.simpleFunc.coe_indicatorConst _ _ m, lpMeasToLpTrimLie_symm_indicator hs hŒºs.ne c,
      condexpL1CLM_indicatorConstLp]
    exact condexpInd_of_measurable hs ((le_trim hm).trans_lt hŒºs).ne c
  ¬∑ intro f g hf hg _ hf_eq hg_eq
    rw [LinearIsometryEquiv.map_add]
    push_cast
    rw [map_add, hf_eq, hg_eq]
  ¬∑ refine isClosed_eq ?_ ?_
    ¬∑ refine (condexpL1CLM F' hm Œº).continuous.comp (continuous_induced_dom.comp ?_)
      exact LinearIsometryEquiv.continuous _
    ¬∑ refine continuous_induced_dom.comp ?_
      exact LinearIsometryEquiv.continuous _
#align measure_theory.condexp_L1_clm_Lp_meas MeasureTheory.condexpL1CLM_lpMeas

theorem condexpL1CLM_of_aestronglyMeasurable' (f : Œ± ‚Üí‚ÇÅ[Œº] F') (hfm : AEStronglyMeasurable' m f Œº) :
    condexpL1CLM F' hm Œº f = f :=
  condexpL1CLM_lpMeas (‚ü®f, hfm‚ü© : lpMeas F' ‚Ñù m 1 Œº)
#align measure_theory.condexp_L1_clm_of_ae_strongly_measurable' MeasureTheory.condexpL1CLM_of_aestronglyMeasurable'

/-- Conditional expectation of a function, in L1. Its value is 0 if the function is not
integrable. The function-valued `condexp` should be used instead in most cases. -/
def condexpL1 (hm : m ‚â§ m0) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)] (f : Œ± ‚Üí F') : Œ± ‚Üí‚ÇÅ[Œº] F' :=
  setToFun Œº (condexpInd F' hm Œº) (dominatedFinMeasAdditive_condexpInd F' hm Œº) f
#align measure_theory.condexp_L1 MeasureTheory.condexpL1

theorem condexpL1_undef (hf : ¬¨Integrable f Œº) : condexpL1 hm Œº f = 0 :=
  setToFun_undef (dominatedFinMeasAdditive_condexpInd F' hm Œº) hf
#align measure_theory.condexp_L1_undef MeasureTheory.condexpL1_undef

theorem condexpL1_eq (hf : Integrable f Œº) : condexpL1 hm Œº f = condexpL1CLM F' hm Œº (hf.toL1 f) :=
  setToFun_eq (dominatedFinMeasAdditive_condexpInd F' hm Œº) hf
#align measure_theory.condexp_L1_eq MeasureTheory.condexpL1_eq

@[simp]
theorem condexpL1_zero : condexpL1 hm Œº (0 : Œ± ‚Üí F') = 0 :=
  setToFun_zero _
#align measure_theory.condexp_L1_zero MeasureTheory.condexpL1_zero

@[simp]
theorem condexpL1_measure_zero (hm : m ‚â§ m0) : condexpL1 hm (0 : Measure Œ±) f = 0 :=
  setToFun_measure_zero _ rfl
#align measure_theory.condexp_L1_measure_zero MeasureTheory.condexpL1_measure_zero

theorem aestronglyMeasurable'_condexpL1 {f : Œ± ‚Üí F'} :
    AEStronglyMeasurable' m (condexpL1 hm Œº f) Œº := by
  by_cases hf : Integrable f Œº
  ¬∑ rw [condexpL1_eq hf]
    exact aestronglyMeasurable'_condexpL1CLM _
  ¬∑ rw [condexpL1_undef hf]
    refine AEStronglyMeasurable'.congr ?_ (coeFn_zero _ _ _).symm
    exact StronglyMeasurable.aeStronglyMeasurable' (@stronglyMeasurable_zero _ _ m _ _)
#align measure_theory.ae_strongly_measurable'_condexp_L1 MeasureTheory.aestronglyMeasurable'_condexpL1

theorem condexpL1_congr_ae (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)] (h : f =·µê[Œº] g) :
    condexpL1 hm Œº f = condexpL1 hm Œº g :=
  setToFun_congr_ae _ h
#align measure_theory.condexp_L1_congr_ae MeasureTheory.condexpL1_congr_ae

theorem integrable_condexpL1 (f : Œ± ‚Üí F') : Integrable (condexpL1 hm Œº f) Œº :=
  L1.integrable_coeFn _
#align measure_theory.integrable_condexp_L1 MeasureTheory.integrable_condexpL1

/-- The integral of the conditional expectation `condexpL1` over an `m`-measurable set is equal to
the integral of `f` on that set. See also `setIntegral_condexp`, the similar statement for
`condexp`. -/
theorem setIntegral_condexpL1 (hf : Integrable f Œº) (hs : MeasurableSet[m] s) :
    ‚à´ x in s, condexpL1 hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  simp_rw [condexpL1_eq hf]
  rw [setIntegral_condexpL1CLM (hf.toL1 f) hs]
  exact setIntegral_congr_ae (hm s hs) (hf.coeFn_toL1.mono fun x hx _ => hx)
#align measure_theory.set_integral_condexp_L1 MeasureTheory.setIntegral_condexpL1

@[deprecated (since := "2024-04-17")]
alias set_integral_condexpL1 := setIntegral_condexpL1

theorem condexpL1_add (hf : Integrable f Œº) (hg : Integrable g Œº) :
    condexpL1 hm Œº (f + g) = condexpL1 hm Œº f + condexpL1 hm Œº g :=
  setToFun_add _ hf hg
#align measure_theory.condexp_L1_add MeasureTheory.condexpL1_add

theorem condexpL1_neg (f : Œ± ‚Üí F') : condexpL1 hm Œº (-f) = -condexpL1 hm Œº f :=
  setToFun_neg _ f
#align measure_theory.condexp_L1_neg MeasureTheory.condexpL1_neg

theorem condexpL1_smul (c : ùïú) (f : Œ± ‚Üí F') : condexpL1 hm Œº (c ‚Ä¢ f) = c ‚Ä¢ condexpL1 hm Œº f := by
  refine setToFun_smul _ ?_ c f
  exact fun c _ x => condexpInd_smul' c x
#align measure_theory.condexp_L1_smul MeasureTheory.condexpL1_smul

theorem condexpL1_sub (hf : Integrable f Œº) (hg : Integrable g Œº) :
    condexpL1 hm Œº (f - g) = condexpL1 hm Œº f - condexpL1 hm Œº g :=
  setToFun_sub _ hf hg
#align measure_theory.condexp_L1_sub MeasureTheory.condexpL1_sub

theorem condexpL1_of_aestronglyMeasurable' (hfm : AEStronglyMeasurable' m f Œº)
    (hfi : Integrable f Œº) : condexpL1 hm Œº f =·µê[Œº] f := by
  rw [condexpL1_eq hfi]
  refine EventuallyEq.trans ?_ (Integrable.coeFn_toL1 hfi)
  rw [condexpL1CLM_of_aestronglyMeasurable']
  exact AEStronglyMeasurable'.congr hfm (Integrable.coeFn_toL1 hfi).symm
#align measure_theory.condexp_L1_of_ae_strongly_measurable' MeasureTheory.condexpL1_of_aestronglyMeasurable'

theorem condexpL1_mono {E} [NormedLatticeAddCommGroup E] [CompleteSpace E] [NormedSpace ‚Ñù E]
    [OrderedSMul ‚Ñù E] {f g : Œ± ‚Üí E} (hf : Integrable f Œº) (hg : Integrable g Œº) (hfg : f ‚â§·µê[Œº] g) :
    condexpL1 hm Œº f ‚â§·µê[Œº] condexpL1 hm Œº g := by
  rw [coeFn_le]
  have h_nonneg : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí ‚àÄ x : E, 0 ‚â§ x ‚Üí 0 ‚â§ condexpInd E hm Œº s x :=
    fun s hs hŒºs x hx => condexpInd_nonneg hs hŒºs.ne x hx
  exact setToFun_mono (dominatedFinMeasAdditive_condexpInd E hm Œº) h_nonneg hf hg hfg
#align measure_theory.condexp_L1_mono MeasureTheory.condexpL1_mono

end CondexpL1

end MeasureTheory
