/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.CondexpL2
import Mathlib.MeasureTheory.Measure.Real

/-! # Conditional expectation in L1

This file contains two more steps of the construction of the conditional expectation, which is
completed in `MeasureTheory.Function.ConditionalExpectation.Basic`. See that file for a
description of the full process.

The conditional expectation of an `L¬≤` function is defined in
`MeasureTheory.Function.ConditionalExpectation.CondexpL2`. In this file, we perform two steps.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `Set Œ± ‚Üí (E ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E))` which to a set associates a linear
  map. That linear map sends `x ‚àà E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condExpL1CLM : (Œ± ‚Üí‚ÇÅ[Œº] E) ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E)`. This is done using the same
  construction as the Bochner integral (see the file `MeasureTheory/Integral/SetToL1`).

## Main definitions

* `condExpL1`: Conditional expectation of a function as a linear map from `L1` to itself.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Œ± F F' G G' ùïú : Type*} [RCLike ùïú]
  -- ùïú for ‚Ñù or ‚ÑÇ
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ùïú F]
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace ùïú F'] [NormedSpace ‚Ñù F'] [CompleteSpace F']
  -- G for a Lp add_subgroup
  [NormedAddCommGroup G]
  -- G' for integrals on a Lp add_subgroup
  [NormedAddCommGroup G']
  [NormedSpace ‚Ñù G'] [CompleteSpace G']

section CondexpInd

/-! ## Conditional expectation of an indicator as a continuous linear map.

The goal of this section is to build
`condExpInd (hm : m ‚â§ m0) (Œº : Measure Œ±) (s : Set s) : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G`, which
takes `x : G` to the conditional expectation of the indicator of the set `s` with value `x`,
seen as an element of `Œ± ‚Üí‚ÇÅ[Œº] G`.
-/


variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {s t : Set Œ±} [NormedSpace ‚Ñù G]

section CondexpIndL1Fin


/-- Conditional expectation of the indicator of a measurable set with finite measure,
as a function in L1. -/
def condExpIndL1Fin (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)] (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû)
    (x : G) : Œ± ‚Üí‚ÇÅ[Œº] G :=
  (integrable_condExpIndSMul hm hs hŒºs x).toL1 _

theorem condExpIndL1Fin_ae_eq_condExpIndSMul (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    condExpIndL1Fin hm hs hŒºs x =·µê[Œº] condExpIndSMul hm hs hŒºs x :=
  (integrable_condExpIndSMul hm hs hŒºs x).coeFn_toL1

variable {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]

-- Porting note: this lemma fills the hole in `refine' (MemLp.coeFn_toLp _) ...`
-- which is not automatically filled in Lean 4
private theorem q {hs : MeasurableSet s} {hŒºs : Œº s ‚â† ‚àû} {x : G} :
    MemLp (condExpIndSMul hm hs hŒºs x) 1 Œº := by
  rw [memLp_one_iff_integrable]; apply integrable_condExpIndSMul

theorem condExpIndL1Fin_add (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x y : G) :
    condExpIndL1Fin hm hs hŒºs (x + y) =
    condExpIndL1Fin hm hs hŒºs x + condExpIndL1Fin hm hs hŒºs y := by
  ext1
  unfold condExpIndL1Fin Integrable.toL1
  grw [Lp.coeFn_add, MemLp.coeFn_toLp, MemLp.coeFn_toLp, MemLp.coeFn_toLp, condExpIndSMul_add,
    Lp.coeFn_add]

theorem condExpIndL1Fin_smul (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (c : ‚Ñù) (x : G) :
    condExpIndL1Fin hm hs hŒºs (c ‚Ä¢ x) = c ‚Ä¢ condExpIndL1Fin hm hs hŒºs x := by
  ext1
  grw [Lp.coeFn_smul, condExpIndL1Fin_ae_eq_condExpIndSMul, condExpIndL1Fin_ae_eq_condExpIndSMul,
    condExpIndSMul_smul, Lp.coeFn_smul]

theorem condExpIndL1Fin_smul' [NormedSpace ‚Ñù F] [SMulCommClass ‚Ñù ùïú F] (hs : MeasurableSet s)
    (hŒºs : Œº s ‚â† ‚àû) (c : ùïú) (x : F) :
    condExpIndL1Fin hm hs hŒºs (c ‚Ä¢ x) = c ‚Ä¢ condExpIndL1Fin hm hs hŒºs x := by
  ext1
  grw [Lp.coeFn_smul, condExpIndL1Fin_ae_eq_condExpIndSMul, condExpIndL1Fin_ae_eq_condExpIndSMul,
    condExpIndSMul_smul, Lp.coeFn_smul]

theorem norm_condExpIndL1Fin_le (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    ‚ÄñcondExpIndL1Fin hm hs hŒºs x‚Äñ ‚â§ Œº.real s * ‚Äñx‚Äñ := by
  rw [L1.norm_eq_integral_norm, ‚Üê ENNReal.toReal_ofReal (norm_nonneg x), measureReal_def,
    ‚Üê ENNReal.toReal_mul,
    ‚Üê ENNReal.ofReal_le_iff_le_toReal (ENNReal.mul_ne_top hŒºs ENNReal.ofReal_ne_top),
    ofReal_integral_norm_eq_lintegral_enorm]
  swap; ¬∑ rw [‚Üê memLp_one_iff_integrable]; exact Lp.memLp _
  have h_eq :
    ‚à´‚Åª a, ‚ÄñcondExpIndL1Fin hm hs hŒºs x a‚Äñ‚Çë ‚àÇŒº = ‚à´‚Åª a, ‚ÄñcondExpIndSMul hm hs hŒºs x a‚Äñ‚Çë ‚àÇŒº := by
    refine lintegral_congr_ae ?_
    filter_upwards [condExpIndL1Fin_ae_eq_condExpIndSMul hm hs hŒºs x] with z hz
    rw [hz]
  rw [h_eq, ofReal_norm_eq_enorm]
  exact lintegral_nnnorm_condExpIndSMul_le hm hs hŒºs x

theorem condExpIndL1Fin_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (hst : Disjoint s t) (x : G) :
    condExpIndL1Fin hm (hs.union ht) ((measure_union_le s t).trans_lt
      (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr ‚ü®hŒºs, hŒºt‚ü©))).ne x =
    condExpIndL1Fin hm hs hŒºs x + condExpIndL1Fin hm ht hŒºt x := by
  ext1
  grw [Lp.coeFn_add, condExpIndL1Fin_ae_eq_condExpIndSMul, condExpIndL1Fin_ae_eq_condExpIndSMul,
    condExpIndL1Fin_ae_eq_condExpIndSMul]
  rw [condExpIndSMul]
  rw [indicatorConstLp_disjoint_union hs ht hŒºs hŒºt hst (1 : ‚Ñù)]
  rw [map_add]
  push_cast
  rw [map_add]
  grw [Lp.coeFn_add]
  rfl

end CondexpIndL1Fin

section CondexpIndL1


open scoped Classical in
/-- Conditional expectation of the indicator of a set, as a function in L1. Its value for sets
which are not both measurable and of finite measure is not used: we set it to 0. -/
def condExpIndL1 {m m0 : MeasurableSpace Œ±} (hm : m ‚â§ m0) (Œº : Measure Œ±) (s : Set Œ±)
    [SigmaFinite (Œº.trim hm)] (x : G) : Œ± ‚Üí‚ÇÅ[Œº] G :=
  if hs : MeasurableSet s ‚àß Œº s ‚â† ‚àû then condExpIndL1Fin hm hs.1 hs.2 x else 0

variable {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]

theorem condExpIndL1_of_measurableSet_of_measure_ne_top (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû)
    (x : G) : condExpIndL1 hm Œº s x = condExpIndL1Fin hm hs hŒºs x := by
  simp only [condExpIndL1, And.intro hs hŒºs, dif_pos, Ne, not_false_iff, and_self_iff]

theorem condExpIndL1_of_measure_eq_top (hŒºs : Œº s = ‚àû) (x : G) : condExpIndL1 hm Œº s x = 0 := by
  simp only [condExpIndL1, hŒºs, not_true, Ne, dif_neg, not_false_iff,
    and_false]

theorem condExpIndL1_of_not_measurableSet (hs : ¬¨MeasurableSet s) (x : G) :
    condExpIndL1 hm Œº s x = 0 := by
  simp only [condExpIndL1, hs, dif_neg, not_false_iff, false_and]

theorem condExpIndL1_add (x y : G) :
    condExpIndL1 hm Œº s (x + y) = condExpIndL1 hm Œº s x + condExpIndL1 hm Œº s y := by
  by_cases hs : MeasurableSet s
  swap; ¬∑ simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [zero_add]
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ simp_rw [condExpIndL1_of_measure_eq_top hŒºs]; rw [zero_add]
  ¬∑ simp_rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs]
    exact condExpIndL1Fin_add hs hŒºs x y

theorem condExpIndL1_smul (c : ‚Ñù) (x : G) :
    condExpIndL1 hm Œº s (c ‚Ä¢ x) = c ‚Ä¢ condExpIndL1 hm Œº s x := by
  by_cases hs : MeasurableSet s
  swap; ¬∑ simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [smul_zero]
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ simp_rw [condExpIndL1_of_measure_eq_top hŒºs]; rw [smul_zero]
  ¬∑ simp_rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs]
    exact condExpIndL1Fin_smul hs hŒºs c x

theorem condExpIndL1_smul' [NormedSpace ‚Ñù F] [SMulCommClass ‚Ñù ùïú F] (c : ùïú) (x : F) :
    condExpIndL1 hm Œº s (c ‚Ä¢ x) = c ‚Ä¢ condExpIndL1 hm Œº s x := by
  by_cases hs : MeasurableSet s
  swap; ¬∑ simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [smul_zero]
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ simp_rw [condExpIndL1_of_measure_eq_top hŒºs]; rw [smul_zero]
  ¬∑ simp_rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs]
    exact condExpIndL1Fin_smul' hs hŒºs c x

theorem norm_condExpIndL1_le (x : G) : ‚ÄñcondExpIndL1 hm Œº s x‚Äñ ‚â§ Œº.real s * ‚Äñx‚Äñ := by
  by_cases hs : MeasurableSet s
  swap
  ¬∑ simp_rw [condExpIndL1_of_not_measurableSet hs]; rw [Lp.norm_zero]
    exact mul_nonneg ENNReal.toReal_nonneg (norm_nonneg _)
  by_cases hŒºs : Œº s = ‚àû
  ¬∑ rw [condExpIndL1_of_measure_eq_top hŒºs x, Lp.norm_zero]
    exact mul_nonneg ENNReal.toReal_nonneg (norm_nonneg _)
  ¬∑ rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs x]
    exact norm_condExpIndL1Fin_le hs hŒºs x

theorem continuous_condExpIndL1 : Continuous fun x : G => condExpIndL1 hm Œº s x :=
  continuous_of_linear_of_bound condExpIndL1_add condExpIndL1_smul norm_condExpIndL1_le

theorem condExpIndL1_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (hst : Disjoint s t) (x : G) :
    condExpIndL1 hm Œº (s ‚à™ t) x = condExpIndL1 hm Œº s x + condExpIndL1 hm Œº t x := by
  have hŒºst : Œº (s ‚à™ t) ‚â† ‚àû :=
    ((measure_union_le s t).trans_lt (lt_top_iff_ne_top.mpr (ENNReal.add_ne_top.mpr ‚ü®hŒºs, hŒºt‚ü©))).ne
  rw [condExpIndL1_of_measurableSet_of_measure_ne_top hs hŒºs x,
    condExpIndL1_of_measurableSet_of_measure_ne_top ht hŒºt x,
    condExpIndL1_of_measurableSet_of_measure_ne_top (hs.union ht) hŒºst x]
  exact condExpIndL1Fin_disjoint_union hs ht hŒºs hŒºt hst x

end CondexpIndL1

variable (G)

/-- Conditional expectation of the indicator of a set, as a linear map from `G` to L1. -/
def condExpInd {m m0 : MeasurableSpace Œ±} (hm : m ‚â§ m0) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)]
    (s : Set Œ±) : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G where
  toFun := condExpIndL1 hm Œº s
  map_add' := condExpIndL1_add
  map_smul' := condExpIndL1_smul
  cont := continuous_condExpIndL1

variable {G}

theorem condExpInd_ae_eq_condExpIndSMul (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    condExpInd G hm Œº s x =·µê[Œº] condExpIndSMul hm hs hŒºs x := by
  grw [‚Üê condExpIndL1Fin_ae_eq_condExpIndSMul]
  simp [condExpInd, condExpIndL1, hs, hŒºs]

variable {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]

theorem aestronglyMeasurable_condExpInd (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : G) :
    AEStronglyMeasurable[m] (condExpInd G hm Œº s x) Œº :=
  (aestronglyMeasurable_condExpIndSMul hm hs hŒºs x).congr
    (condExpInd_ae_eq_condExpIndSMul hm hs hŒºs x).symm

@[simp]
theorem condExpInd_empty : condExpInd G hm Œº ‚àÖ = (0 : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G) := by
  ext x
  grw [condExpInd_ae_eq_condExpIndSMul, condExpIndSMul_empty, zero_apply, Lp.coeFn_zero,
    Lp.coeFn_zero]

theorem condExpInd_smul' [NormedSpace ‚Ñù F] [SMulCommClass ‚Ñù ùïú F] (c : ùïú) (x : F) :
    condExpInd F hm Œº s (c ‚Ä¢ x) = c ‚Ä¢ condExpInd F hm Œº s x :=
  condExpIndL1_smul' c x

theorem norm_condExpInd_apply_le (x : G) : ‚ÄñcondExpInd G hm Œº s x‚Äñ ‚â§ Œº.real s * ‚Äñx‚Äñ :=
  norm_condExpIndL1_le x

theorem norm_condExpInd_le : ‚Äñ(condExpInd G hm Œº s : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G)‚Äñ ‚â§ Œº.real s :=
  ContinuousLinearMap.opNorm_le_bound _ ENNReal.toReal_nonneg norm_condExpInd_apply_le

theorem condExpInd_disjoint_union_apply (hs : MeasurableSet s) (ht : MeasurableSet t)
    (hŒºs : Œº s ‚â† ‚àû) (hŒºt : Œº t ‚â† ‚àû) (hst : Disjoint s t) (x : G) :
    condExpInd G hm Œº (s ‚à™ t) x = condExpInd G hm Œº s x + condExpInd G hm Œº t x :=
  condExpIndL1_disjoint_union hs ht hŒºs hŒºt hst x

theorem condExpInd_disjoint_union (hs : MeasurableSet s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (hst : Disjoint s t) : (condExpInd G hm Œº (s ‚à™ t) : G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G) =
    condExpInd G hm Œº s + condExpInd G hm Œº t := by
  ext1 x; push_cast; exact condExpInd_disjoint_union_apply hs ht hŒºs hŒºt hst x

variable (G)

theorem dominatedFinMeasAdditive_condExpInd (hm : m ‚â§ m0) (Œº : Measure Œ±)
    [SigmaFinite (Œº.trim hm)] :
    DominatedFinMeasAdditive Œº (condExpInd G hm Œº : Set Œ± ‚Üí G ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] G) 1 :=
  ‚ü®fun _ _ => condExpInd_disjoint_union, fun _ _ _ => norm_condExpInd_le.trans (one_mul _).symm.le‚ü©

variable {G}

theorem setIntegral_condExpInd (hs : MeasurableSet[m] s) (ht : MeasurableSet t) (hŒºs : Œº s ‚â† ‚àû)
    (hŒºt : Œº t ‚â† ‚àû) (x : G') : ‚à´ a in s, condExpInd G' hm Œº t x a ‚àÇŒº = Œº.real (t ‚à© s) ‚Ä¢ x :=
  calc
    ‚à´ a in s, condExpInd G' hm Œº t x a ‚àÇŒº = ‚à´ a in s, condExpIndSMul hm ht hŒºt x a ‚àÇŒº :=
      setIntegral_congr_ae (hm s hs)
        ((condExpInd_ae_eq_condExpIndSMul hm ht hŒºt x).mono fun _ hx _ => hx)
    _ = Œº.real (t ‚à© s) ‚Ä¢ x := setIntegral_condExpIndSMul hs ht hŒºs hŒºt x

theorem condExpInd_of_measurable (hs : MeasurableSet[m] s) (hŒºs : Œº s ‚â† ‚àû) (c : G) :
    condExpInd G hm Œº s c = indicatorConstLp 1 (hm s hs) hŒºs c := by
  ext1
  grw [indicatorConstLp_coeFn, condExpInd_ae_eq_condExpIndSMul, condExpIndSMul_ae_eq_smul]
  rw [condExpL2_indicator_of_measurable hm hs hŒºs (1 : ‚Ñù)]
  filter_upwards [@indicatorConstLp_coeFn Œ± _ _ 2 Œº _ s (hm s hs) hŒºs (1 : ‚Ñù)] with x hx
  rw [hx]
  by_cases hx_mem : x ‚àà s <;> simp [hx_mem]

theorem condExpInd_nonneg {E} [NormedAddCommGroup E] [PartialOrder E] [NormedSpace ‚Ñù E]
    [IsOrderedModule ‚Ñù E] (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : E) (hx : 0 ‚â§ x) :
    0 ‚â§ condExpInd E hm Œº s x := by
  rw [‚Üê coeFn_le]
  refine EventuallyLE.trans_eq ?_ (condExpInd_ae_eq_condExpIndSMul hm hs hŒºs x).symm
  exact (coeFn_zero E 1 Œº).trans_le (condExpIndSMul_nonneg hs hŒºs x hx)

end CondexpInd

section CondexpL1


variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {hm : m ‚â§ m0} [SigmaFinite (Œº.trim hm)]
  {f g : Œ± ‚Üí F'} {s : Set Œ±}

variable (F')

/-- Conditional expectation of a function as a linear map from `Œ± ‚Üí‚ÇÅ[Œº] F'` to itself. -/
def condExpL1CLM (hm : m ‚â§ m0) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)] :
    (Œ± ‚Üí‚ÇÅ[Œº] F') ‚ÜíL[‚Ñù] Œ± ‚Üí‚ÇÅ[Œº] F' :=
  L1.setToL1 (dominatedFinMeasAdditive_condExpInd F' hm Œº)

variable {F'}

theorem condExpL1CLM_smul (c : ùïú) (f : Œ± ‚Üí‚ÇÅ[Œº] F') :
    condExpL1CLM F' hm Œº (c ‚Ä¢ f) = c ‚Ä¢ condExpL1CLM F' hm Œº f := by
  refine L1.setToL1_smul (dominatedFinMeasAdditive_condExpInd F' hm Œº) ?_ c f
  exact fun c s x => condExpInd_smul' c x

theorem condExpL1CLM_indicatorConstLp (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : F') :
    (condExpL1CLM F' hm Œº) (indicatorConstLp 1 hs hŒºs x) = condExpInd F' hm Œº s x :=
  L1.setToL1_indicatorConstLp (dominatedFinMeasAdditive_condExpInd F' hm Œº) hs hŒºs x

theorem condExpL1CLM_indicatorConst (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (x : F') :
    (condExpL1CLM F' hm Œº) ‚Üë(simpleFunc.indicatorConst 1 hs hŒºs x) = condExpInd F' hm Œº s x := by
  rw [Lp.simpleFunc.coe_indicatorConst]; exact condExpL1CLM_indicatorConstLp hs hŒºs x

/-- Auxiliary lemma used in the proof of `setIntegral_condExpL1CLM`. -/
theorem setIntegral_condExpL1CLM_of_measure_ne_top (f : Œ± ‚Üí‚ÇÅ[Œº] F') (hs : MeasurableSet[m] s)
    (hŒºs : Œº s ‚â† ‚àû) : ‚à´ x in s, condExpL1CLM F' hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  refine @Lp.induction _ _ _ _ _ _ _ ENNReal.one_ne_top
    (fun f : Œ± ‚Üí‚ÇÅ[Œº] F' => ‚à´ x in s, condExpL1CLM F' hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº) ?_ ?_
    (isClosed_eq ?_ ?_) f
  ¬∑ intro x t ht hŒºt
    simp_rw [condExpL1CLM_indicatorConst ht hŒºt.ne x]
    rw [Lp.simpleFunc.coe_indicatorConst, setIntegral_indicatorConstLp (hm _ hs)]
    exact setIntegral_condExpInd hs ht hŒºs hŒºt.ne x
  ¬∑ intro f g hf_Lp hg_Lp _ hf hg
    simp_rw [(condExpL1CLM F' hm Œº).map_add]
    rw [setIntegral_congr_ae (hm s hs) ((Lp.coeFn_add (condExpL1CLM F' hm Œº (hf_Lp.toLp f))
      (condExpL1CLM F' hm Œº (hg_Lp.toLp g))).mono fun x hx _ => hx)]
    rw [setIntegral_congr_ae (hm s hs)
      ((Lp.coeFn_add (hf_Lp.toLp f) (hg_Lp.toLp g)).mono fun x hx _ => hx)]
    simp_rw [Pi.add_apply]
    rw [integral_add (L1.integrable_coeFn _).integrableOn (L1.integrable_coeFn _).integrableOn,
      integral_add (L1.integrable_coeFn _).integrableOn (L1.integrable_coeFn _).integrableOn, hf,
      hg]
  ¬∑ exact (continuous_setIntegral s).comp (condExpL1CLM F' hm Œº).continuous
  ¬∑ exact continuous_setIntegral s

/-- The integral of the conditional expectation `condExpL1CLM` over an `m`-measurable set is equal
to the integral of `f` on that set. See also `setIntegral_condExp`, the similar statement for
`condExp`. -/
theorem setIntegral_condExpL1CLM (f : Œ± ‚Üí‚ÇÅ[Œº] F') (hs : MeasurableSet[m] s) :
    ‚à´ x in s, condExpL1CLM F' hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  let S := spanningSets (Œº.trim hm)
  have hS_meas : ‚àÄ i, MeasurableSet[m] (S i) := measurableSet_spanningSets (Œº.trim hm)
  have hS_meas0 : ‚àÄ i, MeasurableSet (S i) := fun i => hm _ (hS_meas i)
  have hs_eq : s = ‚ãÉ i, S i ‚à© s := by
    simp_rw [Set.inter_comm]
    rw [‚Üê Set.inter_iUnion, iUnion_spanningSets (Œº.trim hm), Set.inter_univ]
  have hS_finite : ‚àÄ i, Œº (S i ‚à© s) < ‚àû := by
    refine fun i => (measure_mono Set.inter_subset_left).trans_lt ?_
    have hS_finite_trim := measure_spanningSets_lt_top (Œº.trim hm) i
    rwa [trim_measurableSet_eq hm (hS_meas i)] at hS_finite_trim
  have h_mono : Monotone fun i => S i ‚à© s := by
    intro i j hij x
    simp_rw [Set.mem_inter_iff]
    exact fun h => ‚ü®monotone_spanningSets (Œº.trim hm) hij h.1, h.2‚ü©
  have h_eq_forall :
    (fun i => ‚à´ x in S i ‚à© s, condExpL1CLM F' hm Œº f x ‚àÇŒº) = fun i => ‚à´ x in S i ‚à© s, f x ‚àÇŒº :=
    funext fun i =>
      setIntegral_condExpL1CLM_of_measure_ne_top f (@MeasurableSet.inter Œ± m _ _ (hS_meas i) hs)
        (hS_finite i).ne
  have h_right : Tendsto (fun i => ‚à´ x in S i ‚à© s, f x ‚àÇŒº) atTop (ùìù (‚à´ x in s, f x ‚àÇŒº)) := by
    have h :=
      tendsto_setIntegral_of_monotone (fun i => (hS_meas0 i).inter (hm s hs)) h_mono
        (L1.integrable_coeFn f).integrableOn
    rwa [‚Üê hs_eq] at h
  have h_left : Tendsto (fun i => ‚à´ x in S i ‚à© s, condExpL1CLM F' hm Œº f x ‚àÇŒº) atTop
      (ùìù (‚à´ x in s, condExpL1CLM F' hm Œº f x ‚àÇŒº)) := by
    have h := tendsto_setIntegral_of_monotone (fun i => (hS_meas0 i).inter (hm s hs)) h_mono
      (L1.integrable_coeFn (condExpL1CLM F' hm Œº f)).integrableOn
    rwa [‚Üê hs_eq] at h
  rw [h_eq_forall] at h_left
  exact tendsto_nhds_unique h_left h_right

theorem aestronglyMeasurable_condExpL1CLM (f : Œ± ‚Üí‚ÇÅ[Œº] F') :
    AEStronglyMeasurable[m] (condExpL1CLM F' hm Œº f) Œº := by
  refine @Lp.induction _ _ _ _ _ _ _ ENNReal.one_ne_top
    (fun f : Œ± ‚Üí‚ÇÅ[Œº] F' => AEStronglyMeasurable[m] (condExpL1CLM F' hm Œº f) Œº) ?_ ?_ ?_ f
  ¬∑ intro c s hs hŒºs
    rw [condExpL1CLM_indicatorConst hs hŒºs.ne c]
    exact aestronglyMeasurable_condExpInd hs hŒºs.ne c
  ¬∑ intro f g hf hg _ hfm hgm
    rw [(condExpL1CLM F' hm Œº).map_add]
    exact (hfm.add hgm).congr (coeFn_add ..).symm
  ¬∑ have : {f : Lp F' 1 Œº | AEStronglyMeasurable[m] (condExpL1CLM F' hm Œº f) Œº} =
        condExpL1CLM F' hm Œº ‚Åª¬π' {f | AEStronglyMeasurable[m] f Œº} := rfl
    rw [this]
    refine IsClosed.preimage (condExpL1CLM F' hm Œº).continuous ?_
    exact isClosed_aestronglyMeasurable hm

theorem condExpL1CLM_lpMeas (f : lpMeas F' ‚Ñù m 1 Œº) :
    condExpL1CLM F' hm Œº (f : Œ± ‚Üí‚ÇÅ[Œº] F') = ‚Üëf := by
  let g := lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm f
  have hfg : f = (lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm).symm g := by
    simp only [g, LinearIsometryEquiv.symm_apply_apply]
  rw [hfg]
  refine @Lp.induction Œ± F' m _ 1 (Œº.trim hm) _ ENNReal.coe_ne_top (fun g : Œ± ‚Üí‚ÇÅ[Œº.trim hm] F' =>
    condExpL1CLM F' hm Œº ((lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm).symm g : Œ± ‚Üí‚ÇÅ[Œº] F') =
    ‚Üë((lpMeasToLpTrimLie F' ‚Ñù 1 Œº hm).symm g)) ?_ ?_ ?_ g
  ¬∑ intro c s hs hŒºs
    rw [@Lp.simpleFunc.coe_indicatorConst _ _ m, lpMeasToLpTrimLie_symm_indicator hs hŒºs.ne c,
      condExpL1CLM_indicatorConstLp]
    exact condExpInd_of_measurable hs ((le_trim hm).trans_lt hŒºs).ne c
  ¬∑ intro f g hf hg _ hf_eq hg_eq
    rw [LinearIsometryEquiv.map_add]
    push_cast
    rw [map_add, hf_eq, hg_eq]
  ¬∑ refine isClosed_eq ?_ ?_
    ¬∑ refine (condExpL1CLM F' hm Œº).continuous.comp (continuous_induced_dom.comp ?_)
      exact LinearIsometryEquiv.continuous _
    ¬∑ refine continuous_induced_dom.comp ?_
      exact LinearIsometryEquiv.continuous _

theorem condExpL1CLM_of_aestronglyMeasurable' (f : Œ± ‚Üí‚ÇÅ[Œº] F') (hfm : AEStronglyMeasurable[m] f Œº) :
    condExpL1CLM F' hm Œº f = f :=
  condExpL1CLM_lpMeas (‚ü®f, hfm‚ü© : lpMeas F' ‚Ñù m 1 Œº)

/-- Conditional expectation of a function, in L1. Its value is 0 if the function is not
integrable. The function-valued `condExp` should be used instead in most cases. -/
def condExpL1 (hm : m ‚â§ m0) (Œº : Measure Œ±) [SigmaFinite (Œº.trim hm)] (f : Œ± ‚Üí F') : Œ± ‚Üí‚ÇÅ[Œº] F' :=
  setToFun Œº (condExpInd F' hm Œº) (dominatedFinMeasAdditive_condExpInd F' hm Œº) f

theorem condExpL1_undef (hf : ¬¨Integrable f Œº) : condExpL1 hm Œº f = 0 :=
  setToFun_undef (dominatedFinMeasAdditive_condExpInd F' hm Œº) hf

theorem condExpL1_eq (hf : Integrable f Œº) : condExpL1 hm Œº f = condExpL1CLM F' hm Œº (hf.toL1 f) :=
  setToFun_eq (dominatedFinMeasAdditive_condExpInd F' hm Œº) hf

@[simp]
theorem condExpL1_zero : condExpL1 hm Œº (0 : Œ± ‚Üí F') = 0 :=
  setToFun_zero _

@[simp]
theorem condExpL1_measure_zero (hm : m ‚â§ m0) : condExpL1 hm (0 : Measure Œ±) f = 0 :=
  setToFun_measure_zero _ rfl

theorem aestronglyMeasurable_condExpL1 {f : Œ± ‚Üí F'} :
    AEStronglyMeasurable[m] (condExpL1 hm Œº f) Œº := by
  by_cases hf : Integrable f Œº
  ¬∑ rw [condExpL1_eq hf]
    exact aestronglyMeasurable_condExpL1CLM _
  ¬∑ rw [condExpL1_undef hf]
    exact stronglyMeasurable_zero.aestronglyMeasurable.congr (coeFn_zero ..).symm

theorem condExpL1_congr_ae (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)] (h : f =·µê[Œº] g) :
    condExpL1 hm Œº f = condExpL1 hm Œº g :=
  setToFun_congr_ae _ h

theorem integrable_condExpL1 (f : Œ± ‚Üí F') : Integrable (condExpL1 hm Œº f) Œº :=
  L1.integrable_coeFn _

/-- The integral of the conditional expectation `condExpL1` over an `m`-measurable set is equal to
the integral of `f` on that set. See also `setIntegral_condExp`, the similar statement for
`condExp`. -/
theorem setIntegral_condExpL1 (hf : Integrable f Œº) (hs : MeasurableSet[m] s) :
    ‚à´ x in s, condExpL1 hm Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº := by
  simp_rw [condExpL1_eq hf]
  rw [setIntegral_condExpL1CLM (hf.toL1 f) hs]
  exact setIntegral_congr_ae (hm s hs) (hf.coeFn_toL1.mono fun x hx _ => hx)

theorem condExpL1_add (hf : Integrable f Œº) (hg : Integrable g Œº) :
    condExpL1 hm Œº (f + g) = condExpL1 hm Œº f + condExpL1 hm Œº g :=
  setToFun_add _ hf hg

theorem condExpL1_neg (f : Œ± ‚Üí F') : condExpL1 hm Œº (-f) = -condExpL1 hm Œº f :=
  setToFun_neg _ f

theorem condExpL1_smul (c : ùïú) (f : Œ± ‚Üí F') : condExpL1 hm Œº (c ‚Ä¢ f) = c ‚Ä¢ condExpL1 hm Œº f := by
  refine setToFun_smul _ ?_ c f
  exact fun c _ x => condExpInd_smul' c x

theorem condExpL1_sub (hf : Integrable f Œº) (hg : Integrable g Œº) :
    condExpL1 hm Œº (f - g) = condExpL1 hm Œº f - condExpL1 hm Œº g :=
  setToFun_sub _ hf hg

theorem condExpL1_of_aestronglyMeasurable' (hfm : AEStronglyMeasurable[m] f Œº)
    (hfi : Integrable f Œº) : condExpL1 hm Œº f =·µê[Œº] f := by
  rw [condExpL1_eq hfi]
  refine EventuallyEq.trans ?_ (Integrable.coeFn_toL1 hfi)
  rw [condExpL1CLM_of_aestronglyMeasurable']
  exact hfm.congr hfi.coeFn_toL1.symm

theorem condExpL1_mono {E}
    [NormedAddCommGroup E] [PartialOrder E] [OrderClosedTopology E] [IsOrderedAddMonoid E]
    [CompleteSpace E] [NormedSpace ‚Ñù E] [IsOrderedModule ‚Ñù E] {f g : Œ± ‚Üí E} (hf : Integrable f Œº)
    (hg : Integrable g Œº) (hfg : f ‚â§·µê[Œº] g) :
    condExpL1 hm Œº f ‚â§·µê[Œº] condExpL1 hm Œº g := by
  rw [coeFn_le]
  have h_nonneg : ‚àÄ s, MeasurableSet s ‚Üí Œº s < ‚àû ‚Üí ‚àÄ x : E, 0 ‚â§ x ‚Üí 0 ‚â§ condExpInd E hm Œº s x :=
    fun s hs hŒºs x hx => condExpInd_nonneg hs hŒºs.ne x hx
  exact setToFun_mono (dominatedFinMeasAdditive_condExpInd E hm Œº) h_nonneg hf hg hfg

end CondexpL1

end MeasureTheory
