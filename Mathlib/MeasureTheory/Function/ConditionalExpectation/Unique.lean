/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.AEEqOfIntegral
import Mathlib.MeasureTheory.Function.ConditionalExpectation.AEMeasurable

/-!
# Uniqueness of the conditional expectation

Two Lp functions `f, g` which are almost everywhere strongly measurable with respect to a Ïƒ-algebra
`m` and verify `âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼` for all `m`-measurable sets `s` are equal
almost everywhere. This proves the uniqueness of the conditional expectation, which is not yet
defined in this file but is introduced in
`Mathlib/MeasureTheory/Function/ConditionalExpectation/Basic.lean`.

## Main statements

* `Lp.ae_eq_of_forall_setIntegral_eq'`: two `Lp` functions verifying the equality of integrals
  defining the conditional expectation are equal.
* `ae_eq_of_forall_setIntegral_eq_of_sigma_finite'`: two functions verifying the equality of
  integrals defining the conditional expectation are equal almost everywhere.
  Requires `[SigmaFinite (Î¼.trim hm)]`.

-/


open scoped ENNReal MeasureTheory

namespace MeasureTheory

variable {Î± E' F' ğ•œ : Type*} {p : â„â‰¥0âˆ} {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±} [RCLike ğ•œ]
  -- ğ•œ for â„ or â„‚
  -- E' for an inner product space on which we compute integrals
  [NormedAddCommGroup E']
  [InnerProductSpace ğ•œ E'] [CompleteSpace E'] [NormedSpace â„ E']
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace â„ F'] [CompleteSpace F']

section UniquenessOfConditionalExpectation

/-! ## Uniqueness of the conditional expectation -/

theorem lpMeas.ae_eq_zero_of_forall_setIntegral_eq_zero (hm : m â‰¤ m0) (f : lpMeas E' ğ•œ m p Î¼)
    (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
    (hf_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn (f : Lp E' p Î¼) s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, (f : Lp E' p Î¼) x âˆ‚Î¼ = 0) :
    f =áµ[Î¼] (0 : Î± â†’ E') := by
  obtain âŸ¨g, hg_sm, hfgâŸ© := lpMeas.ae_fin_strongly_measurable' hm f hp_ne_zero hp_ne_top
  refine hfg.trans ?_
  refine ae_eq_zero_of_forall_setIntegral_eq_of_finStronglyMeasurable_trim hm ?_ ?_ hg_sm
  Â· intro s hs hÎ¼s
    have hfg_restrict : f =áµ[Î¼.restrict s] g := ae_restrict_of_ae hfg
    rw [IntegrableOn, integrable_congr hfg_restrict.symm]
    exact hf_int_finite s hs hÎ¼s
  Â· intro s hs hÎ¼s
    have hfg_restrict : f =áµ[Î¼.restrict s] g := ae_restrict_of_ae hfg
    rw [integral_congr_ae hfg_restrict.symm]
    exact hf_zero s hs hÎ¼s

variable (ğ•œ)

include ğ•œ in
theorem Lp.ae_eq_zero_of_forall_setIntegral_eq_zero' (hm : m â‰¤ m0) (f : Lp E' p Î¼)
    (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
    (hf_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hf_zero : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0)
    (hf_meas : AEStronglyMeasurable[m] f Î¼) : f =áµ[Î¼] 0 := by
  let f_meas : lpMeas E' ğ•œ m p Î¼ := âŸ¨f, hf_measâŸ©
  have hf_f_meas : f =áµ[Î¼] f_meas := by simp [f_meas]
  refine hf_f_meas.trans ?_
  exact lpMeas.ae_eq_zero_of_forall_setIntegral_eq_zero
    hm f_meas hp_ne_zero hp_ne_top hf_int_finite hf_zero

include ğ•œ in
/-- **Uniqueness of the conditional expectation** -/
theorem Lp.ae_eq_of_forall_setIntegral_eq' (hm : m â‰¤ m0) (f g : Lp E' p Î¼) (hp_ne_zero : p â‰  0)
    (hp_ne_top : p â‰  âˆ) (hf_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hfg : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼)
    (hf_meas : AEStronglyMeasurable[m] f Î¼) (hg_meas : AEStronglyMeasurable[m] g Î¼) :
    f =áµ[Î¼] g := by
  suffices h_sub : â‡‘(f - g) =áµ[Î¼] 0 by
    rw [â† sub_ae_eq_zero]; exact (Lp.coeFn_sub f g).symm.trans h_sub
  have hfg' : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ (âˆ« x in s, (f - g) x âˆ‚Î¼) = 0 := by
    intro s hs hÎ¼s
    rw [integral_congr_ae (ae_restrict_of_ae (Lp.coeFn_sub f g))]
    rw [integral_sub' (hf_int_finite s hs hÎ¼s) (hg_int_finite s hs hÎ¼s)]
    exact sub_eq_zero.mpr (hfg s hs hÎ¼s)
  have hfg_int : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn (â‡‘(f - g)) s Î¼ := by
    intro s hs hÎ¼s
    rw [IntegrableOn, integrable_congr (ae_restrict_of_ae (Lp.coeFn_sub f g))]
    exact (hf_int_finite s hs hÎ¼s).sub (hg_int_finite s hs hÎ¼s)
  exact Lp.ae_eq_zero_of_forall_setIntegral_eq_zero' ğ•œ hm (f - g) hp_ne_zero hp_ne_top hfg_int hfg'
    <| (hf_meas.sub hg_meas).congr (Lp.coeFn_sub f g).symm

variable {ğ•œ}

theorem ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    {f g : Î± â†’ F'} (hf_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn f s Î¼)
    (hg_int_finite : âˆ€ s, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ IntegrableOn g s Î¼)
    (hfg_eq : âˆ€ s : Set Î±, MeasurableSet[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼)
    (hfm : AEStronglyMeasurable[m] f Î¼) (hgm : AEStronglyMeasurable[m] g Î¼) : f =áµ[Î¼] g := by
  rw [â† ae_eq_trim_iff_of_aestronglyMeasurable hm hfm hgm]
  have hf_mk_int_finite (s) :
      MeasurableSet[m] s â†’ Î¼.trim hm s < âˆ â†’ @IntegrableOn _ _ m _ _ (hfm.mk f) s (Î¼.trim hm) := by
    intro hs hÎ¼s
    rw [trim_measurableSet_eq hm hs] at hÎ¼s
    -- Porting note: `rw [IntegrableOn]` fails with
    -- synthesized type class instance is not definitionally equal to expression inferred by typing
    -- rules, synthesized m0 inferred m
    unfold IntegrableOn
    rw [restrict_trim hm _ hs]
    refine Integrable.trim hm ?_ hfm.stronglyMeasurable_mk
    exact Integrable.congr (hf_int_finite s hs hÎ¼s) (ae_restrict_of_ae hfm.ae_eq_mk)
  have hg_mk_int_finite (s) :
      MeasurableSet[m] s â†’ Î¼.trim hm s < âˆ â†’ @IntegrableOn _ _ m _ _ (hgm.mk g) s (Î¼.trim hm) := by
    intro hs hÎ¼s
    rw [trim_measurableSet_eq hm hs] at hÎ¼s
    -- Porting note: `rw [IntegrableOn]` fails with
    -- synthesized type class instance is not definitionally equal to expression inferred by typing
    -- rules, synthesized m0 inferred m
    unfold IntegrableOn
    rw [restrict_trim hm _ hs]
    refine Integrable.trim hm ?_ hgm.stronglyMeasurable_mk
    exact Integrable.congr (hg_int_finite s hs hÎ¼s) (ae_restrict_of_ae hgm.ae_eq_mk)
  have hfg_mk_eq :
    âˆ€ s : Set Î±,
      MeasurableSet[m] s â†’
        Î¼.trim hm s < âˆ â†’ âˆ« x in s, hfm.mk f x âˆ‚Î¼.trim hm = âˆ« x in s, hgm.mk g x âˆ‚Î¼.trim hm := by
    intro s hs hÎ¼s
    rw [trim_measurableSet_eq hm hs] at hÎ¼s
    rw [restrict_trim hm _ hs, â† integral_trim hm hfm.stronglyMeasurable_mk, â†
      integral_trim hm hgm.stronglyMeasurable_mk,
      integral_congr_ae (ae_restrict_of_ae hfm.ae_eq_mk.symm),
      integral_congr_ae (ae_restrict_of_ae hgm.ae_eq_mk.symm)]
    exact hfg_eq s hs hÎ¼s
  exact ae_eq_of_forall_setIntegral_eq_of_sigmaFinite hf_mk_int_finite hg_mk_int_finite hfg_mk_eq

end UniquenessOfConditionalExpectation

section IntegralNormLE

variable {s : Set Î±}

/-- Let `m` be a sub-Ïƒ-algebra of `m0`, `f` an `m0`-measurable function and `g` an `m`-measurable
function, such that their integrals coincide on `m`-measurable sets with finite measure.
Then `âˆ« x in s, â€–g xâ€– âˆ‚Î¼ â‰¤ âˆ« x in s, â€–f xâ€– âˆ‚Î¼` on all `m`-measurable sets with finite measure. -/
theorem integral_norm_le_of_forall_fin_meas_integral_eq (hm : m â‰¤ m0) {f g : Î± â†’ â„}
    (hf : StronglyMeasurable f) (hfi : IntegrableOn f s Î¼) (hg : StronglyMeasurable[m] g)
    (hgi : IntegrableOn g s Î¼)
    (hgf : âˆ€ t, MeasurableSet[m] t â†’ Î¼ t < âˆ â†’ âˆ« x in t, g x âˆ‚Î¼ = âˆ« x in t, f x âˆ‚Î¼)
    (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) : (âˆ« x in s, â€–g xâ€– âˆ‚Î¼) â‰¤ âˆ« x in s, â€–f xâ€– âˆ‚Î¼ := by
  rw [integral_norm_eq_pos_sub_neg hgi, integral_norm_eq_pos_sub_neg hfi]
  have h_meas_nonneg_g : MeasurableSet[m] {x | 0 â‰¤ g x} :=
    (@stronglyMeasurable_const _ _ m _ _).measurableSet_le hg
  have h_meas_nonneg_f : MeasurableSet {x | 0 â‰¤ f x} :=
    stronglyMeasurable_const.measurableSet_le hf
  have h_meas_nonpos_g : MeasurableSet[m] {x | g x â‰¤ 0} :=
    hg.measurableSet_le (@stronglyMeasurable_const _ _ m _ _)
  have h_meas_nonpos_f : MeasurableSet {x | f x â‰¤ 0} :=
    hf.measurableSet_le stronglyMeasurable_const
  refine sub_le_sub ?_ ?_
  Â· rw [Measure.restrict_restrict (hm _ h_meas_nonneg_g), Measure.restrict_restrict h_meas_nonneg_f,
      hgf _ (@MeasurableSet.inter Î± m _ _ h_meas_nonneg_g hs)
        ((measure_mono Set.inter_subset_right).trans_lt (lt_top_iff_ne_top.mpr hÎ¼s)),
      â† Measure.restrict_restrict (hm _ h_meas_nonneg_g), â†
      Measure.restrict_restrict h_meas_nonneg_f]
    exact setIntegral_le_nonneg (hm _ h_meas_nonneg_g) hf hfi
  Â· rw [Measure.restrict_restrict (hm _ h_meas_nonpos_g), Measure.restrict_restrict h_meas_nonpos_f,
      hgf _ (@MeasurableSet.inter Î± m _ _ h_meas_nonpos_g hs)
        ((measure_mono Set.inter_subset_right).trans_lt (lt_top_iff_ne_top.mpr hÎ¼s)),
      â† Measure.restrict_restrict (hm _ h_meas_nonpos_g), â†
      Measure.restrict_restrict h_meas_nonpos_f]
    exact setIntegral_nonpos_le (hm _ h_meas_nonpos_g) hf hfi

/-- Let `m` be a sub-Ïƒ-algebra of `m0`, `f` an `m0`-measurable function and `g` an `m`-measurable
function, such that their integrals coincide on `m`-measurable sets with finite measure.
Then `âˆ«â» x in s, â€–g xâ€–â‚‘ âˆ‚Î¼ â‰¤ âˆ«â» x in s, â€–f xâ€–â‚‘ âˆ‚Î¼` on all `m`-measurable sets with finite
measure. -/
theorem lintegral_enorm_le_of_forall_fin_meas_integral_eq (hm : m â‰¤ m0) {f g : Î± â†’ â„}
    (hf : StronglyMeasurable f) (hfi : IntegrableOn f s Î¼) (hg : StronglyMeasurable[m] g)
    (hgi : IntegrableOn g s Î¼)
    (hgf : âˆ€ t, MeasurableSet[m] t â†’ Î¼ t < âˆ â†’ âˆ« x in t, g x âˆ‚Î¼ = âˆ« x in t, f x âˆ‚Î¼)
    (hs : MeasurableSet[m] s) (hÎ¼s : Î¼ s â‰  âˆ) : (âˆ«â» x in s, â€–g xâ€–â‚‘ âˆ‚Î¼) â‰¤ âˆ«â» x in s, â€–f xâ€–â‚‘ âˆ‚Î¼ := by
  rw [â† ofReal_integral_norm_eq_lintegral_enorm hfi, â†
    ofReal_integral_norm_eq_lintegral_enorm hgi, ENNReal.ofReal_le_ofReal_iff]
  Â· exact integral_norm_le_of_forall_fin_meas_integral_eq hm hf hfi hg hgi hgf hs hÎ¼s
  Â· positivity

end IntegralNormLE

end MeasureTheory
