/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.LpSeminorm.Trim
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Lp

/-! # Functions a.e. measurable with respect to a sub-œÉ-algebra

A function `f` verifies `AEStronglyMeasurable[m] f Œº` if it is `Œº`-a.e. equal to
an `m`-strongly measurable function. This is similar to `AEStronglyMeasurable`, but the
`MeasurableSpace` structures used for the measurability statement and for the measure are
different.

We define `lpMeas F ùïú m p Œº`, the subspace of `Lp F p Œº` containing functions `f` verifying
`AEStronglyMeasurable[m] f Œº`, i.e. functions which are `Œº`-a.e. equal to an `m`-strongly
measurable function.

## Main statements

We define an `IsometryEquiv` between `lpMeasSubgroup` and the `Lp` space corresponding to the
measure `Œº.trim hm`. As a consequence, the completeness of `Lp` implies completeness of `lpMeas`.

`Lp.induction_stronglyMeasurable` (see also `MemLp.induction_stronglyMeasurable`):
To prove something for an `Lp` function a.e. strongly measurable with respect to a
sub-œÉ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in `Lp` strongly measurable w.r.t. `m` for which the property holds is
  closed.

-/


open TopologicalSpace Filter

open scoped ENNReal MeasureTheory

namespace MeasureTheory

theorem ae_eq_trim_iff_of_aestronglyMeasurable {Œ± Œ≤} [TopologicalSpace Œ≤] [MetrizableSpace Œ≤]
    {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {f g : Œ± ‚Üí Œ≤} (hm : m ‚â§ m0)
    (hfm : AEStronglyMeasurable[m] f Œº) (hgm : AEStronglyMeasurable[m] g Œº) :
    hfm.mk f =·µê[Œº.trim hm] hgm.mk g ‚Üî f =·µê[Œº] g :=
  (hfm.stronglyMeasurable_mk.ae_eq_trim_iff hm  hgm.stronglyMeasurable_mk).trans
    ‚ü®fun h => hfm.ae_eq_mk.trans (h.trans hgm.ae_eq_mk.symm), fun h =>
      hfm.ae_eq_mk.symm.trans (h.trans hgm.ae_eq_mk)‚ü©

theorem AEStronglyMeasurable.comp_ae_measurable' {Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ≤]
    {mŒ± : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥} {f : Œ± ‚Üí Œ≤} {Œº : Measure Œ≥} {g : Œ≥ ‚Üí Œ±}
    (hf : AEStronglyMeasurable f (Œº.map g)) (hg : AEMeasurable g Œº) :
    AEStronglyMeasurable[mŒ±.comap g] (f ‚àò g) Œº :=
  ‚ü®hf.mk f ‚àò g, hf.stronglyMeasurable_mk.comp_measurable (measurable_iff_comap_le.mpr le_rfl),
    ae_eq_comp hg hf.ae_eq_mk‚ü©

variable {Œ± F ùïú : Type*} {p : ‚Ñù‚â•0‚àû} [RCLike ùïú]
  -- ùïú for ‚Ñù or ‚ÑÇ
  -- F for a Lp submodule
  [NormedAddCommGroup F] [NormedSpace ùïú F]

section LpMeas

/-! ## The subset `lpMeas` of `Lp` functions a.e. measurable with respect to a sub-sigma-algebra -/


variable (F)

/-- `lpMeasSubgroup F m p Œº` is the subspace of `Lp F p Œº` containing functions `f` verifying
`AEStronglyMeasurable[m] f Œº`, i.e. functions which are `Œº`-a.e. equal to
an `m`-strongly measurable function. -/
def lpMeasSubgroup (m : MeasurableSpace Œ±) [MeasurableSpace Œ±] (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    AddSubgroup (Lp F p Œº) where
  carrier := {f : Lp F p Œº | AEStronglyMeasurable[m] f Œº}
  zero_mem' := ‚ü®(0 : Œ± ‚Üí F), @stronglyMeasurable_zero _ _ m _ _, Lp.coeFn_zero _ _ _‚ü©
  add_mem' {f g} hf hg := (hf.add hg).congr (Lp.coeFn_add f g).symm
  neg_mem' {f} hf := AEStronglyMeasurable.congr hf.neg (Lp.coeFn_neg f).symm

variable (ùïú)

/-- `lpMeas F ùïú m p Œº` is the subspace of `Lp F p Œº` containing functions `f` verifying
`AEStronglyMeasurable[m] f Œº`, i.e. functions which are `Œº`-a.e. equal to
an `m`-strongly measurable function. -/
def lpMeas (m : MeasurableSpace Œ±) [MeasurableSpace Œ±] (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    Submodule ùïú (Lp F p Œº) where
  carrier := {f : Lp F p Œº | AEStronglyMeasurable[m] f Œº}
  zero_mem' := ‚ü®(0 : Œ± ‚Üí F), @stronglyMeasurable_zero _ _ m _ _, Lp.coeFn_zero _ _ _‚ü©
  add_mem' {f g} hf hg := (hf.add hg).congr (Lp.coeFn_add f g).symm
  smul_mem' c f hf := (hf.const_smul c).congr (Lp.coeFn_smul c f).symm

variable {F ùïú}

theorem mem_lpMeasSubgroup_iff_aestronglyMeasurable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
    {f : Lp F p Œº} : f ‚àà lpMeasSubgroup F m p Œº ‚Üî AEStronglyMeasurable[m] f Œº := by
  rw [‚Üê AddSubgroup.mem_carrier, lpMeasSubgroup, Set.mem_setOf_eq]

theorem mem_lpMeas_iff_aestronglyMeasurable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
    {f : Lp F p Œº} : f ‚àà lpMeas F ùïú m p Œº ‚Üî AEStronglyMeasurable[m] f Œº := by
  rw [‚Üê SetLike.mem_coe, ‚Üê Submodule.mem_carrier, lpMeas, Set.mem_setOf_eq]

theorem lpMeas.aestronglyMeasurable {m _ : MeasurableSpace Œ±} {Œº : Measure Œ±}
    (f : lpMeas F ùïú m p Œº) : AEStronglyMeasurable[m] (f : Œ± ‚Üí F) Œº :=
  mem_lpMeas_iff_aestronglyMeasurable.mp f.mem

theorem mem_lpMeas_self {m0 : MeasurableSpace Œ±} (Œº : Measure Œ±) (f : Lp F p Œº) :
    f ‚àà lpMeas F ùïú m0 p Œº :=
  mem_lpMeas_iff_aestronglyMeasurable.mpr (Lp.aestronglyMeasurable f)

theorem mem_lpMeas_indicatorConstLp {m m0 : MeasurableSpace Œ±} (hm : m ‚â§ m0) {Œº : Measure Œ±}
    {s : Set Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº s ‚â† ‚àû) {c : F} :
    indicatorConstLp p (hm s hs) hŒºs c ‚àà lpMeas F ùïú m p Œº :=
  ‚ü®s.indicator fun _ : Œ± => c, (@stronglyMeasurable_const _ _ m _ _).indicator hs,
    indicatorConstLp_coeFn‚ü©

section CompleteSubspace

/-! ## The subspace `lpMeas` is complete.

We define an `IsometryEquiv` between `lpMeasSubgroup` and the `Lp` space corresponding to the
measure `Œº.trim hm`. As a consequence, the completeness of `Lp` implies completeness of
`lpMeasSubgroup` (and `lpMeas`). -/


variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

/-- If `f` belongs to `lpMeasSubgroup F m p Œº`, then the measurable function it is almost
everywhere equal to (given by `AEMeasurable.mk`) belongs to `‚Ñíp` for the measure `Œº.trim hm`. -/
theorem memLp_trim_of_mem_lpMeasSubgroup (hm : m ‚â§ m0) (f : Lp F p Œº)
    (hf_meas : f ‚àà lpMeasSubgroup F m p Œº) :
    MemLp (mem_lpMeasSubgroup_iff_aestronglyMeasurable.mp hf_meas).choose p (Œº.trim hm) := by
  have hf : AEStronglyMeasurable[m] f Œº :=
    mem_lpMeasSubgroup_iff_aestronglyMeasurable.mp hf_meas
  let g := hf.choose
  obtain ‚ü®hg, hfg‚ü© := hf.choose_spec
  change MemLp g p (Œº.trim hm)
  refine ‚ü®hg.aestronglyMeasurable, ?_‚ü©
  have h_eLpNorm_fg : eLpNorm g p (Œº.trim hm) = eLpNorm f p Œº := by
    rw [eLpNorm_trim hm hg]
    exact eLpNorm_congr_ae hfg.symm
  rw [h_eLpNorm_fg]
  exact Lp.eLpNorm_lt_top f

/-- If `f` belongs to `Lp` for the measure `Œº.trim hm`, then it belongs to the subgroup
`lpMeasSubgroup F m p Œº`. -/
theorem mem_lpMeasSubgroup_toLp_of_trim (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    (memLp_of_memLp_trim hm (Lp.memLp f)).toLp f ‚àà lpMeasSubgroup F m p Œº := by
  let hf_mem_‚Ñíp := memLp_of_memLp_trim hm (Lp.memLp f)
  rw [mem_lpMeasSubgroup_iff_aestronglyMeasurable]
  refine AEStronglyMeasurable.congr ?_ (MemLp.coeFn_toLp hf_mem_‚Ñíp).symm
  exact (Lp.aestronglyMeasurable f).of_trim hm

variable (F p Œº)

/-- Map from `lpMeasSubgroup` to `Lp F p (Œº.trim hm)`. -/
noncomputable def lpMeasSubgroupToLpTrim (hm : m ‚â§ m0) (f : lpMeasSubgroup F m p Œº) :
    Lp F p (Œº.trim hm) :=
  MemLp.toLp (mem_lpMeasSubgroup_iff_aestronglyMeasurable.mp f.mem).choose
    (memLp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem)

variable (ùïú) in
/-- Map from `lpMeas` to `Lp F p (Œº.trim hm)`. -/
noncomputable def lpMeasToLpTrim (hm : m ‚â§ m0) (f : lpMeas F ùïú m p Œº) : Lp F p (Œº.trim hm) :=
  MemLp.toLp (mem_lpMeas_iff_aestronglyMeasurable.mp f.mem).choose
    (memLp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem)

/-- Map from `Lp F p (Œº.trim hm)` to `lpMeasSubgroup`, inverse of
`lpMeasSubgroupToLpTrim`. -/
noncomputable def lpTrimToLpMeasSubgroup (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    lpMeasSubgroup F m p Œº :=
  ‚ü®(memLp_of_memLp_trim hm (Lp.memLp f)).toLp f, mem_lpMeasSubgroup_toLp_of_trim hm f‚ü©

variable (ùïú) in
/-- Map from `Lp F p (Œº.trim hm)` to `lpMeas`, inverse of `Lp_meas_to_Lp_trim`. -/
noncomputable def lpTrimToLpMeas (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) : lpMeas F ùïú m p Œº :=
  ‚ü®(memLp_of_memLp_trim hm (Lp.memLp f)).toLp f, mem_lpMeasSubgroup_toLp_of_trim hm f‚ü©

variable {F p Œº}

theorem lpMeasSubgroupToLpTrim_ae_eq (hm : m ‚â§ m0) (f : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm f =·µê[Œº] f :=
  (ae_eq_of_ae_eq_trim (MemLp.coeFn_toLp (memLp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem))).trans
    (mem_lpMeasSubgroup_iff_aestronglyMeasurable.mp f.mem).choose_spec.2.symm

theorem lpTrimToLpMeasSubgroup_ae_eq (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    lpTrimToLpMeasSubgroup F p Œº hm f =·µê[Œº] f :=
  MemLp.coeFn_toLp (memLp_of_memLp_trim hm (Lp.memLp f))

theorem lpMeasToLpTrim_ae_eq (hm : m ‚â§ m0) (f : lpMeas F ùïú m p Œº) :
    lpMeasToLpTrim F ùïú p Œº hm f =·µê[Œº] f :=
  (ae_eq_of_ae_eq_trim (MemLp.coeFn_toLp (memLp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem))).trans
    (mem_lpMeasSubgroup_iff_aestronglyMeasurable.mp f.mem).choose_spec.2.symm

theorem lpTrimToLpMeas_ae_eq (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    lpTrimToLpMeas F ùïú p Œº hm f =·µê[Œº] f :=
  MemLp.coeFn_toLp (memLp_of_memLp_trim hm (Lp.memLp f))

/-- `lpTrimToLpMeasSubgroup` is a right inverse of `lpMeasSubgroupToLpTrim`. -/
theorem lpMeasSubgroupToLpTrim_right_inv (hm : m ‚â§ m0) :
    Function.RightInverse (lpTrimToLpMeasSubgroup F p Œº hm) (lpMeasSubgroupToLpTrim F p Œº hm) := by
  intro f
  ext1
  refine
    (Lp.stronglyMeasurable _).ae_eq_trim_of_stronglyMeasurable hm (Lp.stronglyMeasurable _) ?_
  exact (lpMeasSubgroupToLpTrim_ae_eq hm _).trans (lpTrimToLpMeasSubgroup_ae_eq hm _)

/-- `lpTrimToLpMeasSubgroup` is a left inverse of `lpMeasSubgroupToLpTrim`. -/
theorem lpMeasSubgroupToLpTrim_left_inv (hm : m ‚â§ m0) :
    Function.LeftInverse (lpTrimToLpMeasSubgroup F p Œº hm) (lpMeasSubgroupToLpTrim F p Œº hm) := by
  intro f
  ext1
  ext1
  exact (lpTrimToLpMeasSubgroup_ae_eq hm _).trans (lpMeasSubgroupToLpTrim_ae_eq hm _)

theorem lpMeasSubgroupToLpTrim_add (hm : m ‚â§ m0) (f g : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm (f + g) =
      lpMeasSubgroupToLpTrim F p Œº hm f + lpMeasSubgroupToLpTrim F p Œº hm g := by
  ext1
  grw [Lp.coeFn_add]
  refine (Lp.stronglyMeasurable _).ae_eq_trim_of_stronglyMeasurable hm ?_ ?_
  ¬∑ exact (Lp.stronglyMeasurable _).add (Lp.stronglyMeasurable _)
  grw [lpMeasSubgroupToLpTrim_ae_eq, lpMeasSubgroupToLpTrim_ae_eq, lpMeasSubgroupToLpTrim_ae_eq,
    ‚Üê Lp.coeFn_add]
  rfl

theorem lpMeasSubgroupToLpTrim_neg (hm : m ‚â§ m0) (f : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm (-f) = -lpMeasSubgroupToLpTrim F p Œº hm f := by
  ext1
  grw [Lp.coeFn_neg]
  refine (Lp.stronglyMeasurable _).ae_eq_trim_of_stronglyMeasurable hm (Lp.stronglyMeasurable _).neg
    ?_
  grw [lpMeasSubgroupToLpTrim_ae_eq, lpMeasSubgroupToLpTrim_ae_eq, ‚Üê Lp.coeFn_neg]
  rfl

theorem lpMeasSubgroupToLpTrim_sub (hm : m ‚â§ m0) (f g : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm (f - g) =
      lpMeasSubgroupToLpTrim F p Œº hm f - lpMeasSubgroupToLpTrim F p Œº hm g := by
  rw [sub_eq_add_neg, sub_eq_add_neg, lpMeasSubgroupToLpTrim_add,
    lpMeasSubgroupToLpTrim_neg]

theorem lpMeasToLpTrim_smul (hm : m ‚â§ m0) (c : ùïú) (f : lpMeas F ùïú m p Œº) :
    lpMeasToLpTrim F ùïú p Œº hm (c ‚Ä¢ f) = c ‚Ä¢ lpMeasToLpTrim F ùïú p Œº hm f := by
  ext1
  grw [Lp.coeFn_smul]
  refine (Lp.stronglyMeasurable _).ae_eq_trim_of_stronglyMeasurable hm ?_ ?_
  ¬∑ exact (Lp.stronglyMeasurable _).const_smul c
  grw [lpMeasToLpTrim_ae_eq]
  push_cast
  grw [Lp.coeFn_smul, lpMeasToLpTrim_ae_eq]

/-- `lpMeasSubgroupToLpTrim` preserves the norm. -/
theorem lpMeasSubgroupToLpTrim_norm_map [hp : Fact (1 ‚â§ p)] (hm : m ‚â§ m0)
    (f : lpMeasSubgroup F m p Œº) : ‚ÄñlpMeasSubgroupToLpTrim F p Œº hm f‚Äñ = ‚Äñf‚Äñ := by
  rw [Lp.norm_def, eLpNorm_trim hm (Lp.stronglyMeasurable _),
    eLpNorm_congr_ae (lpMeasSubgroupToLpTrim_ae_eq hm _), ‚Üê Lp.norm_def]
  congr

theorem isometry_lpMeasSubgroupToLpTrim [hp : Fact (1 ‚â§ p)] (hm : m ‚â§ m0) :
    Isometry (lpMeasSubgroupToLpTrim F p Œº hm) :=
  Isometry.of_dist_eq fun f g => by
    rw [dist_eq_norm, ‚Üê lpMeasSubgroupToLpTrim_sub, lpMeasSubgroupToLpTrim_norm_map,
      dist_eq_norm]

variable (F p Œº)

/-- `lpMeasSubgroup` and `Lp F p (Œº.trim hm)` are isometric. -/
noncomputable def lpMeasSubgroupToLpTrimIso [Fact (1 ‚â§ p)] (hm : m ‚â§ m0) :
    lpMeasSubgroup F m p Œº ‚âÉ·µ¢ Lp F p (Œº.trim hm) where
  toFun := lpMeasSubgroupToLpTrim F p Œº hm
  invFun := lpTrimToLpMeasSubgroup F p Œº hm
  left_inv := lpMeasSubgroupToLpTrim_left_inv hm
  right_inv := lpMeasSubgroupToLpTrim_right_inv hm
  isometry_toFun := isometry_lpMeasSubgroupToLpTrim hm

variable (ùïú)

/-- `lpMeasSubgroup` and `lpMeas` are isometric. -/
noncomputable def lpMeasSubgroupToLpMeasIso [Fact (1 ‚â§ p)] :
    lpMeasSubgroup F m p Œº ‚âÉ·µ¢ lpMeas F ùïú m p Œº :=
  IsometryEquiv.refl (lpMeasSubgroup F m p Œº)

/-- `lpMeas` and `Lp F p (Œº.trim hm)` are isometric, with a linear equivalence. -/
noncomputable def lpMeasToLpTrimLie [Fact (1 ‚â§ p)] (hm : m ‚â§ m0) :
    lpMeas F ùïú m p Œº ‚âÉ‚Çó·µ¢[ùïú] Lp F p (Œº.trim hm) where
  toFun := lpMeasToLpTrim F ùïú p Œº hm
  invFun := lpTrimToLpMeas F ùïú p Œº hm
  left_inv := lpMeasSubgroupToLpTrim_left_inv hm
  right_inv := lpMeasSubgroupToLpTrim_right_inv hm
  map_add' := lpMeasSubgroupToLpTrim_add hm
  map_smul' := lpMeasToLpTrim_smul hm
  norm_map' := lpMeasSubgroupToLpTrim_norm_map hm

variable {F ùïú p Œº}

instance [hm : Fact (m ‚â§ m0)] [CompleteSpace F] [hp : Fact (1 ‚â§ p)] :
    CompleteSpace (lpMeasSubgroup F m p Œº) := by
  rw [(lpMeasSubgroupToLpTrimIso F p Œº hm.elim).completeSpace_iff]; infer_instance

instance [hm : Fact (m ‚â§ m0)] [CompleteSpace F] [hp : Fact (1 ‚â§ p)] :
    CompleteSpace (lpMeas F ùïú m p Œº) := by
  rw [(lpMeasSubgroupToLpMeasIso F ùïú p Œº).symm.completeSpace_iff]; infer_instance

theorem isComplete_aestronglyMeasurable [hp : Fact (1 ‚â§ p)] [CompleteSpace F] (hm : m ‚â§ m0) :
    IsComplete {f : Lp F p Œº | AEStronglyMeasurable[m] f Œº} := by
  rw [‚Üê completeSpace_coe_iff_isComplete]
  haveI : Fact (m ‚â§ m0) := ‚ü®hm‚ü©
  change CompleteSpace (lpMeasSubgroup F m p Œº)
  infer_instance

theorem isClosed_aestronglyMeasurable [Fact (1 ‚â§ p)] [CompleteSpace F] (hm : m ‚â§ m0) :
    IsClosed {f : Lp F p Œº | AEStronglyMeasurable[m] f Œº} :=
  IsComplete.isClosed (isComplete_aestronglyMeasurable hm)

end CompleteSubspace

section StronglyMeasurable

variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

/-- We do not get `ae_fin_strongly_measurable f (Œº.trim hm)`, since we don't have
`f =·µê[Œº.trim hm] Lp_meas_to_Lp_trim F ùïú p Œº hm f` but only the weaker
`f =·µê[Œº] Lp_meas_to_Lp_trim F ùïú p Œº hm f`. -/
theorem lpMeas.ae_fin_strongly_measurable' (hm : m ‚â§ m0) (f : lpMeas F ùïú m p Œº) (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) :
    ‚àÉ g, FinStronglyMeasurable g (Œº.trim hm) ‚àß f.1 =·µê[Œº] g :=
  ‚ü®lpMeasSubgroupToLpTrim F p Œº hm f, Lp.finStronglyMeasurable _ hp_ne_zero hp_ne_top,
    (lpMeasSubgroupToLpTrim_ae_eq hm f).symm‚ü©

/-- When applying the inverse of `lpMeasToLpTrimLie` (which takes a function in the Lp space of
the sub-sigma algebra and returns its version in the larger Lp space) to an indicator of the
sub-sigma-algebra, we obtain an indicator in the Lp space of the larger sigma-algebra. -/
theorem lpMeasToLpTrimLie_symm_indicator [one_le_p : Fact (1 ‚â§ p)] [NormedSpace ‚Ñù F] {hm : m ‚â§ m0}
    {s : Set Œ±} {Œº : Measure Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº.trim hm s ‚â† ‚àû) (c : F) :
    ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm (indicatorConstLp p hs hŒºs c) : Lp F p Œº) =
      indicatorConstLp p (hm s hs) ((le_trim hm).trans_lt hŒºs.lt_top).ne c := by
  ext1
  change
    lpTrimToLpMeas F ‚Ñù p Œº hm (indicatorConstLp p hs hŒºs c) =·µê[Œº]
      (indicatorConstLp p _ _ c : Œ± ‚Üí F)
  grw [lpTrimToLpMeas_ae_eq, ae_eq_of_ae_eq_trim indicatorConstLp_coeFn, indicatorConstLp_coeFn]

theorem lpMeasToLpTrimLie_symm_toLp [one_le_p : Fact (1 ‚â§ p)] [NormedSpace ‚Ñù F] (hm : m ‚â§ m0)
    (f : Œ± ‚Üí F) (hf : MemLp f p (Œº.trim hm)) :
    ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm (hf.toLp f) : Lp F p Œº) =
      (memLp_of_memLp_trim hm hf).toLp f := by
  ext1
  change lpTrimToLpMeas F ‚Ñù p Œº hm (MemLp.toLp f hf) =·µê[Œº] (MemLp.toLp f _ : Œ± ‚Üí F)
  grw [lpTrimToLpMeas_ae_eq, ae_eq_of_ae_eq_trim (MemLp.coeFn_toLp hf), MemLp.coeFn_toLp]

end StronglyMeasurable

end LpMeas

section Induction

variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} [Fact (1 ‚â§ p)] [NormedSpace ‚Ñù F]

/-- Auxiliary lemma for `Lp.induction_stronglyMeasurable`. -/
@[elab_as_elim]
theorem Lp.induction_stronglyMeasurable_aux (hm : m ‚â§ m0) (hp_ne_top : p ‚â† ‚àû) (P : Lp F p Œº ‚Üí Prop)
    (h_ind : ‚àÄ (c : F) {s : Set Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº s < ‚àû),
      P (Lp.simpleFunc.indicatorConst p (hm s hs) hŒºs.ne c))
    (h_add : ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : MemLp f p Œº, ‚àÄ hg : MemLp g p Œº, AEStronglyMeasurable[m] f Œº ‚Üí
      AEStronglyMeasurable[m] g Œº ‚Üí Disjoint (Function.support f) (Function.support g) ‚Üí
        P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g))
    (h_closed : IsClosed {f : lpMeas F ‚Ñù m p Œº | P f}) :
    ‚àÄ f : Lp F p Œº, AEStronglyMeasurable[m] f Œº ‚Üí P f := by
  intro f hf
  let f' := (‚ü®f, hf‚ü© : lpMeas F ‚Ñù m p Œº)
  let g := lpMeasToLpTrimLie F ‚Ñù p Œº hm f'
  have hfg : f' = (lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm g := by
    simp only [f', g, LinearIsometryEquiv.symm_apply_apply]
  change P ‚Üëf'
  rw [hfg]
  refine
    @Lp.induction Œ± F m _ p (Œº.trim hm) _ hp_ne_top
      (fun g => P ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm g)) ?_ ?_ ?_ g
  ¬∑ intro b t ht hŒºt
    rw [@Lp.simpleFunc.coe_indicatorConst _ _ m, lpMeasToLpTrimLie_symm_indicator ht hŒºt.ne b]
    have hŒºt' : Œº t < ‚àû := (le_trim hm).trans_lt hŒºt
    specialize h_ind b ht hŒºt'
    rwa [Lp.simpleFunc.coe_indicatorConst] at h_ind
  ¬∑ intro f g hf hg h_disj hfP hgP
    rw [LinearIsometryEquiv.map_add]
    push_cast
    have h_eq :
      ‚àÄ (f : Œ± ‚Üí F) (hf : MemLp f p (Œº.trim hm)),
        ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm (MemLp.toLp f hf) : Lp F p Œº) =
          (memLp_of_memLp_trim hm hf).toLp f :=
      lpMeasToLpTrimLie_symm_toLp hm
    rw [h_eq f hf] at hfP ‚ä¢
    rw [h_eq g hg] at hgP ‚ä¢
    exact h_add (memLp_of_memLp_trim hm hf) (memLp_of_memLp_trim hm hg)
      (hf.aestronglyMeasurable.of_trim hm) (hg.aestronglyMeasurable.of_trim hm) h_disj hfP hgP
  ¬∑ change IsClosed ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm ‚Åª¬π' {g : lpMeas F ‚Ñù m p Œº | P ‚Üëg})
    exact IsClosed.preimage (LinearIsometryEquiv.continuous _) h_closed

/-- To prove something for an `Lp` function a.e. strongly measurable with respect to a
sub-œÉ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in `Lp` strongly measurable w.r.t. `m` for which the property holds is
  closed.
-/
@[elab_as_elim]
theorem Lp.induction_stronglyMeasurable (hm : m ‚â§ m0) (hp_ne_top : p ‚â† ‚àû) (P : Lp F p Œº ‚Üí Prop)
    (h_ind : ‚àÄ (c : F) {s : Set Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº s < ‚àû),
      P (Lp.simpleFunc.indicatorConst p (hm s hs) hŒºs.ne c))
    (h_add : ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : MemLp f p Œº, ‚àÄ hg : MemLp g p Œº, StronglyMeasurable[m] f ‚Üí
      StronglyMeasurable[m] g ‚Üí Disjoint (Function.support f) (Function.support g) ‚Üí
        P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g))
    (h_closed : IsClosed {f : lpMeas F ‚Ñù m p Œº | P f}) :
    ‚àÄ f : Lp F p Œº, AEStronglyMeasurable[m] f Œº ‚Üí P f := by
  intro f hf
  suffices h_add_ae :
    ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : MemLp f p Œº, ‚àÄ hg : MemLp g p Œº, AEStronglyMeasurable[m] f Œº ‚Üí
      AEStronglyMeasurable[m] g Œº ‚Üí Disjoint (Function.support f) (Function.support g) ‚Üí
        P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g) from
    Lp.induction_stronglyMeasurable_aux hm hp_ne_top _ h_ind h_add_ae h_closed f hf
  intro f g hf hg hfm hgm h_disj hPf hPg
  let s_f : Set Œ± := Function.support (hfm.mk f)
  have hs_f : MeasurableSet[m] s_f := hfm.stronglyMeasurable_mk.measurableSet_support
  have hs_f_eq : s_f =·µê[Œº] Function.support f := hfm.ae_eq_mk.symm.support
  let s_g : Set Œ± := Function.support (hgm.mk g)
  have hs_g : MeasurableSet[m] s_g := hgm.stronglyMeasurable_mk.measurableSet_support
  have hs_g_eq : s_g =·µê[Œº] Function.support g := hgm.ae_eq_mk.symm.support
  have h_inter_empty : (s_f ‚à© s_g : Set Œ±) =·µê[Œº] (‚àÖ : Set Œ±) := by
    refine (hs_f_eq.inter hs_g_eq).trans ?_
    suffices Function.support f ‚à© Function.support g = ‚àÖ by rw [this]
    exact Set.disjoint_iff_inter_eq_empty.mp h_disj
  let f' := (s_f \ s_g).indicator (hfm.mk f)
  have hff' : f =·µê[Œº] f' := by
    have : s_f \ s_g =·µê[Œº] s_f := by
      rw [‚Üê Set.diff_inter_self_eq_diff, Set.inter_comm]
      refine ((ae_eq_refl s_f).diff h_inter_empty).trans ?_
      rw [Set.diff_empty]
    refine ((indicator_ae_eq_of_ae_eq_set this).trans ?_).symm
    rw [Set.indicator_support]
    exact hfm.ae_eq_mk.symm
  have hf'_meas : StronglyMeasurable[m] f' := hfm.stronglyMeasurable_mk.indicator (hs_f.diff hs_g)
  have hf'_Lp : MemLp f' p Œº := hf.ae_eq hff'
  let g' := (s_g \ s_f).indicator (hgm.mk g)
  have hgg' : g =·µê[Œº] g' := by
    have : s_g \ s_f =·µê[Œº] s_g := by
      rw [‚Üê Set.diff_inter_self_eq_diff]
      refine ((ae_eq_refl s_g).diff h_inter_empty).trans ?_
      rw [Set.diff_empty]
    refine ((indicator_ae_eq_of_ae_eq_set this).trans ?_).symm
    rw [Set.indicator_support]
    exact hgm.ae_eq_mk.symm
  have hg'_meas : StronglyMeasurable[m] g' := hgm.stronglyMeasurable_mk.indicator (hs_g.diff hs_f)
  have hg'_Lp : MemLp g' p Œº := hg.ae_eq hgg'
  have h_disj : Disjoint (Function.support f') (Function.support g') :=
    haveI : Disjoint (s_f \ s_g) (s_g \ s_f) := disjoint_sdiff_sdiff
    this.mono Set.support_indicator_subset Set.support_indicator_subset
  rw [‚Üê MemLp.toLp_congr hf'_Lp hf hff'.symm] at hPf ‚ä¢
  rw [‚Üê MemLp.toLp_congr hg'_Lp hg hgg'.symm] at hPg ‚ä¢
  exact h_add hf'_Lp hg'_Lp hf'_meas hg'_meas h_disj hPf hPg

/-- To prove something for an arbitrary `MemLp` function a.e. strongly measurable with respect
to a sub-œÉ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in the `L·µñ` space strongly measurable w.r.t. `m` for which the property
  holds is closed.
* the property is closed under the almost-everywhere equal relation.
-/
@[elab_as_elim]
theorem MemLp.induction_stronglyMeasurable (hm : m ‚â§ m0) (hp_ne_top : p ‚â† ‚àû) (P : (Œ± ‚Üí F) ‚Üí Prop)
    (h_ind : ‚àÄ (c : F) ‚¶És‚¶Ñ, MeasurableSet[m] s ‚Üí Œº s < ‚àû ‚Üí P (s.indicator fun _ => c))
    (h_add : ‚àÄ ‚¶Éf g : Œ± ‚Üí F‚¶Ñ, Disjoint (Function.support f) (Function.support g) ‚Üí
      MemLp f p Œº ‚Üí MemLp g p Œº ‚Üí StronglyMeasurable[m] f ‚Üí StronglyMeasurable[m] g ‚Üí
        P f ‚Üí P g ‚Üí P (f + g))
    (h_closed : IsClosed {f : lpMeas F ‚Ñù m p Œº | P f})
    (h_ae : ‚àÄ ‚¶Éf g‚¶Ñ, f =·µê[Œº] g ‚Üí MemLp f p Œº ‚Üí P f ‚Üí P g) :
    ‚àÄ ‚¶Éf : Œ± ‚Üí F‚¶Ñ, MemLp f p Œº ‚Üí AEStronglyMeasurable[m] f Œº ‚Üí P f := by
  intro f hf hfm
  let f_Lp := hf.toLp f
  have hfm_Lp : AEStronglyMeasurable[m] f_Lp Œº := hfm.congr hf.coeFn_toLp.symm
  refine h_ae hf.coeFn_toLp (Lp.memLp _) ?_
  change P f_Lp
  refine Lp.induction_stronglyMeasurable hm hp_ne_top (fun f => P f) ?_ ?_ h_closed f_Lp hfm_Lp
  ¬∑ intro c s hs hŒºs
    rw [Lp.simpleFunc.coe_indicatorConst]
    refine h_ae indicatorConstLp_coeFn.symm ?_ (h_ind c hs hŒºs)
    exact memLp_indicator_const p (hm s hs) c (Or.inr hŒºs.ne)
  ¬∑ intro f g hf_mem hg_mem hfm hgm h_disj hfP hgP
    have hfP' : P f := h_ae hf_mem.coeFn_toLp (Lp.memLp _) hfP
    have hgP' : P g := h_ae hg_mem.coeFn_toLp (Lp.memLp _) hgP
    specialize h_add h_disj hf_mem hg_mem hfm hgm hfP' hgP'
    refine h_ae ?_ (hf_mem.add hg_mem) h_add
    exact (hf_mem.coeFn_toLp.symm.add hg_mem.coeFn_toLp.symm).trans (Lp.coeFn_add _ _).symm

end Induction

end MeasureTheory
