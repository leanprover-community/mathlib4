/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Lp
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.Order.Filter.IndicatorFunction
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Inner
import Mathlib.MeasureTheory.Function.LpSeminorm.Trim

#align_import measure_theory.function.conditional_expectation.ae_measurable from "leanprover-community/mathlib"@"d8bbb04e2d2a44596798a9207ceefc0fb236e41e"

/-! # Functions a.e. measurable with respect to a sub-œÉ-algebra

A function `f` verifies `AEStronglyMeasurable' m f Œº` if it is `Œº`-a.e. equal to
an `m`-strongly measurable function. This is similar to `AEStronglyMeasurable`, but the
`MeasurableSpace` structures used for the measurability statement and for the measure are
different.

We define `lpMeas F ùïú m p Œº`, the subspace of `Lp F p Œº` containing functions `f` verifying
`AEStronglyMeasurable' m f Œº`, i.e. functions which are `Œº`-a.e. equal to an `m`-strongly
measurable function.

## Main statements

We define an `IsometryEquiv` between `lpMeasSubgroup` and the `Lp` space corresponding to the
measure `Œº.trim hm`. As a consequence, the completeness of `Lp` implies completeness of `lpMeas`.

`Lp.induction_stronglyMeasurable` (see also `Mem‚Ñíp.induction_stronglyMeasurable`):
To prove something for an `Lp` function a.e. strongly measurable with respect to a
sub-œÉ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in `Lp` strongly measurable w.r.t. `m` for which the property holds is
  closed.

-/

set_option linter.uppercaseLean3 false

open TopologicalSpace Filter

open scoped ENNReal MeasureTheory

namespace MeasureTheory

/-- A function `f` verifies `AEStronglyMeasurable' m f Œº` if it is `Œº`-a.e. equal to
an `m`-strongly measurable function. This is similar to `AEStronglyMeasurable`, but the
`MeasurableSpace` structures used for the measurability statement and for the measure are
different. -/
def AEStronglyMeasurable' {Œ± Œ≤} [TopologicalSpace Œ≤] (m : MeasurableSpace Œ±)
    {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí Œ≤) (Œº : Measure Œ±) : Prop :=
  ‚àÉ g : Œ± ‚Üí Œ≤, StronglyMeasurable[m] g ‚àß f =·µê[Œº] g
#align measure_theory.ae_strongly_measurable' MeasureTheory.AEStronglyMeasurable'

namespace AEStronglyMeasurable'

variable {Œ± Œ≤ ùïú : Type*} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} [TopologicalSpace Œ≤]
  {f g : Œ± ‚Üí Œ≤}

theorem congr (hf : AEStronglyMeasurable' m f Œº) (hfg : f =·µê[Œº] g) :
    AEStronglyMeasurable' m g Œº := by
  obtain ‚ü®f', hf'_meas, hff'‚ü© := hf; exact ‚ü®f', hf'_meas, hfg.symm.trans hff'‚ü©
#align measure_theory.ae_strongly_measurable'.congr MeasureTheory.AEStronglyMeasurable'.congr

theorem mono {m'} (hf : AEStronglyMeasurable' m f Œº) (hm : m ‚â§ m') :
    AEStronglyMeasurable' m' f Œº :=
  let ‚ü®f', hf'_meas, hff'‚ü© := hf; ‚ü®f', hf'_meas.mono hm, hff'‚ü©

theorem add [Add Œ≤] [ContinuousAdd Œ≤] (hf : AEStronglyMeasurable' m f Œº)
    (hg : AEStronglyMeasurable' m g Œº) : AEStronglyMeasurable' m (f + g) Œº := by
  rcases hf with ‚ü®f', h_f'_meas, hff'‚ü©
  rcases hg with ‚ü®g', h_g'_meas, hgg'‚ü©
  exact ‚ü®f' + g', h_f'_meas.add h_g'_meas, hff'.add hgg'‚ü©
#align measure_theory.ae_strongly_measurable'.add MeasureTheory.AEStronglyMeasurable'.add

theorem neg [AddGroup Œ≤] [TopologicalAddGroup Œ≤] {f : Œ± ‚Üí Œ≤} (hfm : AEStronglyMeasurable' m f Œº) :
    AEStronglyMeasurable' m (-f) Œº := by
  rcases hfm with ‚ü®f', hf'_meas, hf_ae‚ü©
  refine ‚ü®-f', hf'_meas.neg, hf_ae.mono fun x hx => ?_‚ü©
  simp_rw [Pi.neg_apply]
  rw [hx]
#align measure_theory.ae_strongly_measurable'.neg MeasureTheory.AEStronglyMeasurable'.neg

theorem sub [AddGroup Œ≤] [TopologicalAddGroup Œ≤] {f g : Œ± ‚Üí Œ≤} (hfm : AEStronglyMeasurable' m f Œº)
    (hgm : AEStronglyMeasurable' m g Œº) : AEStronglyMeasurable' m (f - g) Œº := by
  rcases hfm with ‚ü®f', hf'_meas, hf_ae‚ü©
  rcases hgm with ‚ü®g', hg'_meas, hg_ae‚ü©
  refine ‚ü®f' - g', hf'_meas.sub hg'_meas, hf_ae.mp (hg_ae.mono fun x hx1 hx2 => ?_)‚ü©
  simp_rw [Pi.sub_apply]
  rw [hx1, hx2]
#align measure_theory.ae_strongly_measurable'.sub MeasureTheory.AEStronglyMeasurable'.sub

theorem const_smul [SMul ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] (c : ùïú) (hf : AEStronglyMeasurable' m f Œº) :
    AEStronglyMeasurable' m (c ‚Ä¢ f) Œº := by
  rcases hf with ‚ü®f', h_f'_meas, hff'‚ü©
  refine ‚ü®c ‚Ä¢ f', h_f'_meas.const_smul c, ?_‚ü©
  exact EventuallyEq.fun_comp hff' fun x => c ‚Ä¢ x
#align measure_theory.ae_strongly_measurable'.const_smul MeasureTheory.AEStronglyMeasurable'.const_smul

theorem const_inner {ùïú Œ≤} [RCLike ùïú] [NormedAddCommGroup Œ≤] [InnerProductSpace ùïú Œ≤] {f : Œ± ‚Üí Œ≤}
    (hfm : AEStronglyMeasurable' m f Œº) (c : Œ≤) :
    AEStronglyMeasurable' m (fun x => (inner c (f x) : ùïú)) Œº := by
  rcases hfm with ‚ü®f', hf'_meas, hf_ae‚ü©
  refine
    ‚ü®fun x => (inner c (f' x) : ùïú), (@stronglyMeasurable_const _ _ m _ c).inner hf'_meas,
      hf_ae.mono fun x hx => ?_‚ü©
  dsimp only
  rw [hx]
#align measure_theory.ae_strongly_measurable'.const_inner MeasureTheory.AEStronglyMeasurable'.const_inner

/-- An `m`-strongly measurable function almost everywhere equal to `f`. -/
noncomputable def mk (f : Œ± ‚Üí Œ≤) (hfm : AEStronglyMeasurable' m f Œº) : Œ± ‚Üí Œ≤ :=
  hfm.choose
#align measure_theory.ae_strongly_measurable'.mk MeasureTheory.AEStronglyMeasurable'.mk

theorem stronglyMeasurable_mk {f : Œ± ‚Üí Œ≤} (hfm : AEStronglyMeasurable' m f Œº) :
    StronglyMeasurable[m] (hfm.mk f) :=
  hfm.choose_spec.1
#align measure_theory.ae_strongly_measurable'.stronglyMeasurable_mk MeasureTheory.AEStronglyMeasurable'.stronglyMeasurable_mk

theorem ae_eq_mk {f : Œ± ‚Üí Œ≤} (hfm : AEStronglyMeasurable' m f Œº) : f =·µê[Œº] hfm.mk f :=
  hfm.choose_spec.2
#align measure_theory.ae_strongly_measurable'.ae_eq_mk MeasureTheory.AEStronglyMeasurable'.ae_eq_mk

theorem continuous_comp {Œ≥} [TopologicalSpace Œ≥] {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hg : Continuous g)
    (hf : AEStronglyMeasurable' m f Œº) : AEStronglyMeasurable' m (g ‚àò f) Œº :=
  ‚ü®fun x => g (hf.mk _ x),
    @Continuous.comp_stronglyMeasurable _ _ _ m _ _ _ _ hg hf.stronglyMeasurable_mk,
    hf.ae_eq_mk.mono fun x hx => by rw [Function.comp_apply, hx]‚ü©
#align measure_theory.ae_strongly_measurable'.continuous_comp MeasureTheory.AEStronglyMeasurable'.continuous_comp

end AEStronglyMeasurable'

theorem aeStronglyMeasurable'_of_aeStronglyMeasurable'_trim {Œ± Œ≤} {m m0 m0' : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] (hm0 : m0 ‚â§ m0') {Œº : Measure Œ±} {f : Œ± ‚Üí Œ≤}
    (hf : AEStronglyMeasurable' m f (Œº.trim hm0)) : AEStronglyMeasurable' m f Œº := by
  obtain ‚ü®g, hg_meas, hfg‚ü© := hf; exact ‚ü®g, hg_meas, ae_eq_of_ae_eq_trim hfg‚ü©
#align measure_theory.ae_strongly_measurable'_of_ae_strongly_measurable'_trim MeasureTheory.aeStronglyMeasurable'_of_aeStronglyMeasurable'_trim

theorem StronglyMeasurable.aeStronglyMeasurable' {Œ± Œ≤} {m _ : MeasurableSpace Œ±}
    [TopologicalSpace Œ≤] {Œº : Measure Œ±} {f : Œ± ‚Üí Œ≤} (hf : StronglyMeasurable[m] f) :
    AEStronglyMeasurable' m f Œº :=
  ‚ü®f, hf, ae_eq_refl _‚ü©
#align measure_theory.strongly_measurable.ae_strongly_measurable' MeasureTheory.StronglyMeasurable.aeStronglyMeasurable'

theorem ae_eq_trim_iff_of_aeStronglyMeasurable' {Œ± Œ≤} [TopologicalSpace Œ≤] [MetrizableSpace Œ≤]
    {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} {f g : Œ± ‚Üí Œ≤} (hm : m ‚â§ m0)
    (hfm : AEStronglyMeasurable' m f Œº) (hgm : AEStronglyMeasurable' m g Œº) :
    hfm.mk f =·µê[Œº.trim hm] hgm.mk g ‚Üî f =·µê[Œº] g :=
  (ae_eq_trim_iff hm hfm.stronglyMeasurable_mk hgm.stronglyMeasurable_mk).trans
    ‚ü®fun h => hfm.ae_eq_mk.trans (h.trans hgm.ae_eq_mk.symm), fun h =>
      hfm.ae_eq_mk.symm.trans (h.trans hgm.ae_eq_mk)‚ü©
#align measure_theory.ae_eq_trim_iff_of_ae_strongly_measurable' MeasureTheory.ae_eq_trim_iff_of_aeStronglyMeasurable'

theorem AEStronglyMeasurable.comp_ae_measurable' {Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ≤]
    {mŒ± : MeasurableSpace Œ±} {_ : MeasurableSpace Œ≥} {f : Œ± ‚Üí Œ≤} {Œº : Measure Œ≥} {g : Œ≥ ‚Üí Œ±}
    (hf : AEStronglyMeasurable f (Œº.map g)) (hg : AEMeasurable g Œº) :
    AEStronglyMeasurable' (mŒ±.comap g) (f ‚àò g) Œº :=
  ‚ü®hf.mk f ‚àò g, hf.stronglyMeasurable_mk.comp_measurable (measurable_iff_comap_le.mpr le_rfl),
    ae_eq_comp hg hf.ae_eq_mk‚ü©
#align measure_theory.ae_strongly_measurable.comp_ae_measurable' MeasureTheory.AEStronglyMeasurable.comp_ae_measurable'

/-- If the restriction to a set `s` of a œÉ-algebra `m` is included in the restriction to `s` of
another œÉ-algebra `m‚ÇÇ` (hypothesis `hs`), the set `s` is `m` measurable and a function `f` almost
everywhere supported on `s` is `m`-ae-strongly-measurable, then `f` is also
`m‚ÇÇ`-ae-strongly-measurable. -/
theorem AEStronglyMeasurable'.aeStronglyMeasurable'_of_measurableSpace_le_on {Œ± E}
    {m m‚ÇÇ m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} [TopologicalSpace E] [Zero E] (hm : m ‚â§ m0)
    {s : Set Œ±} {f : Œ± ‚Üí E} (hs_m : MeasurableSet[m] s)
    (hs : ‚àÄ t, MeasurableSet[m] (s ‚à© t) ‚Üí MeasurableSet[m‚ÇÇ] (s ‚à© t))
    (hf : AEStronglyMeasurable' m f Œº) (hf_zero : f =·µê[Œº.restrict s·∂ú] 0) :
    AEStronglyMeasurable' m‚ÇÇ f Œº := by
  have h_ind_eq : s.indicator (hf.mk f) =·µê[Œº] f := by
    refine Filter.EventuallyEq.trans ?_ <|
      indicator_ae_eq_of_restrict_compl_ae_eq_zero (hm _ hs_m) hf_zero
    filter_upwards [hf.ae_eq_mk] with x hx
    by_cases hxs : x ‚àà s
    ¬∑ simp [hxs, hx]
    ¬∑ simp [hxs]
  suffices StronglyMeasurable[m‚ÇÇ] (s.indicator (hf.mk f)) from
    AEStronglyMeasurable'.congr this.aeStronglyMeasurable' h_ind_eq
  have hf_ind : StronglyMeasurable[m] (s.indicator (hf.mk f)) :=
    hf.stronglyMeasurable_mk.indicator hs_m
  exact
    hf_ind.stronglyMeasurable_of_measurableSpace_le_on hs_m hs fun x hxs =>
      Set.indicator_of_not_mem hxs _
#align measure_theory.ae_strongly_measurable'.ae_strongly_measurable'_of_measurable_space_le_on MeasureTheory.AEStronglyMeasurable'.aeStronglyMeasurable'_of_measurableSpace_le_on

variable {Œ± E' F F' ùïú : Type*} {p : ‚Ñù‚â•0‚àû} [RCLike ùïú]
  -- ùïú for ‚Ñù or ‚ÑÇ
  -- E' for an inner product space on which we compute integrals
  [NormedAddCommGroup E']
  [InnerProductSpace ùïú E'] [CompleteSpace E'] [NormedSpace ‚Ñù E']
  -- F for a Lp submodule
  [NormedAddCommGroup F]
  [NormedSpace ùïú F]
  -- F' for integrals on a Lp submodule
  [NormedAddCommGroup F']
  [NormedSpace ùïú F'] [NormedSpace ‚Ñù F'] [CompleteSpace F']

section LpMeas

/-! ## The subset `lpMeas` of `Lp` functions a.e. measurable with respect to a sub-sigma-algebra -/


variable (F)

/-- `lpMeasSubgroup F m p Œº` is the subspace of `Lp F p Œº` containing functions `f` verifying
`AEStronglyMeasurable' m f Œº`, i.e. functions which are `Œº`-a.e. equal to
an `m`-strongly measurable function. -/
def lpMeasSubgroup (m : MeasurableSpace Œ±) [MeasurableSpace Œ±] (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    AddSubgroup (Lp F p Œº) where
  carrier := {f : Lp F p Œº | AEStronglyMeasurable' m f Œº}
  zero_mem' := ‚ü®(0 : Œ± ‚Üí F), @stronglyMeasurable_zero _ _ m _ _, Lp.coeFn_zero _ _ _‚ü©
  add_mem' {f g} hf hg := (hf.add hg).congr (Lp.coeFn_add f g).symm
  neg_mem' {f} hf := AEStronglyMeasurable'.congr hf.neg (Lp.coeFn_neg f).symm
#align measure_theory.Lp_meas_subgroup MeasureTheory.lpMeasSubgroup

variable (ùïú)

/-- `lpMeas F ùïú m p Œº` is the subspace of `Lp F p Œº` containing functions `f` verifying
`AEStronglyMeasurable' m f Œº`, i.e. functions which are `Œº`-a.e. equal to
an `m`-strongly measurable function. -/
def lpMeas (m : MeasurableSpace Œ±) [MeasurableSpace Œ±] (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) :
    Submodule ùïú (Lp F p Œº) where
  carrier := {f : Lp F p Œº | AEStronglyMeasurable' m f Œº}
  zero_mem' := ‚ü®(0 : Œ± ‚Üí F), @stronglyMeasurable_zero _ _ m _ _, Lp.coeFn_zero _ _ _‚ü©
  add_mem' {f g} hf hg := (hf.add hg).congr (Lp.coeFn_add f g).symm
  smul_mem' c f hf := (hf.const_smul c).congr (Lp.coeFn_smul c f).symm
#align measure_theory.Lp_meas MeasureTheory.lpMeas

variable {F ùïú}

theorem mem_lpMeasSubgroup_iff_aeStronglyMeasurable' {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
    {f : Lp F p Œº} : f ‚àà lpMeasSubgroup F m p Œº ‚Üî AEStronglyMeasurable' m f Œº := by
  rw [‚Üê AddSubgroup.mem_carrier, lpMeasSubgroup, Set.mem_setOf_eq]
#align measure_theory.mem_Lp_meas_subgroup_iff_ae_strongly_measurable' MeasureTheory.mem_lpMeasSubgroup_iff_aeStronglyMeasurable'

theorem mem_lpMeas_iff_aeStronglyMeasurable' {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}
    {f : Lp F p Œº} : f ‚àà lpMeas F ùïú m p Œº ‚Üî AEStronglyMeasurable' m f Œº := by
  rw [‚Üê SetLike.mem_coe, ‚Üê Submodule.mem_carrier, lpMeas, Set.mem_setOf_eq]
#align measure_theory.mem_Lp_meas_iff_ae_strongly_measurable' MeasureTheory.mem_lpMeas_iff_aeStronglyMeasurable'

theorem lpMeas.aeStronglyMeasurable' {m _ : MeasurableSpace Œ±} {Œº : Measure Œ±}
    (f : lpMeas F ùïú m p Œº) : AEStronglyMeasurable' (Œ≤ := F) m f Œº :=
  mem_lpMeas_iff_aeStronglyMeasurable'.mp f.mem
#align measure_theory.Lp_meas.ae_strongly_measurable' MeasureTheory.lpMeas.aeStronglyMeasurable'

theorem mem_lpMeas_self {m0 : MeasurableSpace Œ±} (Œº : Measure Œ±) (f : Lp F p Œº) :
    f ‚àà lpMeas F ùïú m0 p Œº :=
  mem_lpMeas_iff_aeStronglyMeasurable'.mpr (Lp.aestronglyMeasurable f)
#align measure_theory.mem_Lp_meas_self MeasureTheory.mem_lpMeas_self

theorem lpMeasSubgroup_coe {m _ : MeasurableSpace Œ±} {Œº : Measure Œ±} {f : lpMeasSubgroup F m p Œº} :
    (f : _ ‚Üí _) = (f : Lp F p Œº) :=
  rfl
#align measure_theory.Lp_meas_subgroup_coe MeasureTheory.lpMeasSubgroup_coe

theorem lpMeas_coe {m _ : MeasurableSpace Œ±} {Œº : Measure Œ±} {f : lpMeas F ùïú m p Œº} :
    (f : _ ‚Üí _) = (f : Lp F p Œº) :=
  rfl
#align measure_theory.Lp_meas_coe MeasureTheory.lpMeas_coe

theorem mem_lpMeas_indicatorConstLp {m m0 : MeasurableSpace Œ±} (hm : m ‚â§ m0) {Œº : Measure Œ±}
    {s : Set Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº s ‚â† ‚àû) {c : F} :
    indicatorConstLp p (hm s hs) hŒºs c ‚àà lpMeas F ùïú m p Œº :=
  ‚ü®s.indicator fun _ : Œ± => c, (@stronglyMeasurable_const _ _ m _ _).indicator hs,
    indicatorConstLp_coeFn‚ü©
#align measure_theory.mem_Lp_meas_indicator_const_Lp MeasureTheory.mem_lpMeas_indicatorConstLp

section CompleteSubspace

/-! ## The subspace `lpMeas` is complete.

We define an `IsometryEquiv` between `lpMeasSubgroup` and the `Lp` space corresponding to the
measure `Œº.trim hm`. As a consequence, the completeness of `Lp` implies completeness of
`lpMeasSubgroup` (and `lpMeas`). -/


variable {Œπ : Type*} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

/-- If `f` belongs to `lpMeasSubgroup F m p Œº`, then the measurable function it is almost
everywhere equal to (given by `AEMeasurable.mk`) belongs to `‚Ñíp` for the measure `Œº.trim hm`. -/
theorem mem‚Ñíp_trim_of_mem_lpMeasSubgroup (hm : m ‚â§ m0) (f : Lp F p Œº)
    (hf_meas : f ‚àà lpMeasSubgroup F m p Œº) :
    Mem‚Ñíp (mem_lpMeasSubgroup_iff_aeStronglyMeasurable'.mp hf_meas).choose p (Œº.trim hm) := by
  have hf : AEStronglyMeasurable' m f Œº :=
    mem_lpMeasSubgroup_iff_aeStronglyMeasurable'.mp hf_meas
  let g := hf.choose
  obtain ‚ü®hg, hfg‚ü© := hf.choose_spec
  change Mem‚Ñíp g p (Œº.trim hm)
  refine ‚ü®hg.aestronglyMeasurable, ?_‚ü©
  have h_snorm_fg : snorm g p (Œº.trim hm) = snorm f p Œº := by
    rw [snorm_trim hm hg]
    exact snorm_congr_ae hfg.symm
  rw [h_snorm_fg]
  exact Lp.snorm_lt_top f
#align measure_theory.mem_‚Ñíp_trim_of_mem_Lp_meas_subgroup MeasureTheory.mem‚Ñíp_trim_of_mem_lpMeasSubgroup

/-- If `f` belongs to `Lp` for the measure `Œº.trim hm`, then it belongs to the subgroup
`lpMeasSubgroup F m p Œº`. -/
theorem mem_lpMeasSubgroup_toLp_of_trim (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    (mem‚Ñíp_of_mem‚Ñíp_trim hm (Lp.mem‚Ñíp f)).toLp f ‚àà lpMeasSubgroup F m p Œº := by
  let hf_mem_‚Ñíp := mem‚Ñíp_of_mem‚Ñíp_trim hm (Lp.mem‚Ñíp f)
  rw [mem_lpMeasSubgroup_iff_aeStronglyMeasurable']
  refine AEStronglyMeasurable'.congr ?_ (Mem‚Ñíp.coeFn_toLp hf_mem_‚Ñíp).symm
  refine aeStronglyMeasurable'_of_aeStronglyMeasurable'_trim hm ?_
  exact Lp.aestronglyMeasurable f
#align measure_theory.mem_Lp_meas_subgroup_to_Lp_of_trim MeasureTheory.mem_lpMeasSubgroup_toLp_of_trim

variable (F p Œº)

/-- Map from `lpMeasSubgroup` to `Lp F p (Œº.trim hm)`. -/
noncomputable def lpMeasSubgroupToLpTrim (hm : m ‚â§ m0) (f : lpMeasSubgroup F m p Œº) :
    Lp F p (Œº.trim hm) :=
  Mem‚Ñíp.toLp (mem_lpMeasSubgroup_iff_aeStronglyMeasurable'.mp f.mem).choose
    -- Porting note: had to replace `f` with `f.1` here.
    (mem‚Ñíp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem)
#align measure_theory.Lp_meas_subgroup_to_Lp_trim MeasureTheory.lpMeasSubgroupToLpTrim

variable (ùïú)

/-- Map from `lpMeas` to `Lp F p (Œº.trim hm)`. -/
noncomputable def lpMeasToLpTrim (hm : m ‚â§ m0) (f : lpMeas F ùïú m p Œº) : Lp F p (Œº.trim hm) :=
  Mem‚Ñíp.toLp (mem_lpMeas_iff_aeStronglyMeasurable'.mp f.mem).choose
    -- Porting note: had to replace `f` with `f.1` here.
    (mem‚Ñíp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem)
#align measure_theory.Lp_meas_to_Lp_trim MeasureTheory.lpMeasToLpTrim

variable {ùïú}

/-- Map from `Lp F p (Œº.trim hm)` to `lpMeasSubgroup`, inverse of
`lpMeasSubgroupToLpTrim`. -/
noncomputable def lpTrimToLpMeasSubgroup (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    lpMeasSubgroup F m p Œº :=
  ‚ü®(mem‚Ñíp_of_mem‚Ñíp_trim hm (Lp.mem‚Ñíp f)).toLp f, mem_lpMeasSubgroup_toLp_of_trim hm f‚ü©
#align measure_theory.Lp_trim_to_Lp_meas_subgroup MeasureTheory.lpTrimToLpMeasSubgroup

variable (ùïú)

/-- Map from `Lp F p (Œº.trim hm)` to `lpMeas`, inverse of `Lp_meas_to_Lp_trim`. -/
noncomputable def lpTrimToLpMeas (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) : lpMeas F ùïú m p Œº :=
  ‚ü®(mem‚Ñíp_of_mem‚Ñíp_trim hm (Lp.mem‚Ñíp f)).toLp f, mem_lpMeasSubgroup_toLp_of_trim hm f‚ü©
#align measure_theory.Lp_trim_to_Lp_meas MeasureTheory.lpTrimToLpMeas

variable {F ùïú p Œº}

theorem lpMeasSubgroupToLpTrim_ae_eq (hm : m ‚â§ m0) (f : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm f =·µê[Œº] f :=
  -- Porting note: replaced `(‚Üëf)` with `f.1` here.
  (ae_eq_of_ae_eq_trim (Mem‚Ñíp.coeFn_toLp (mem‚Ñíp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem))).trans
    (mem_lpMeasSubgroup_iff_aeStronglyMeasurable'.mp f.mem).choose_spec.2.symm
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_ae_eq MeasureTheory.lpMeasSubgroupToLpTrim_ae_eq

theorem lpTrimToLpMeasSubgroup_ae_eq (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    lpTrimToLpMeasSubgroup F p Œº hm f =·µê[Œº] f :=
  -- Porting note: filled in the argument
  Mem‚Ñíp.coeFn_toLp (mem‚Ñíp_of_mem‚Ñíp_trim hm (Lp.mem‚Ñíp f))
#align measure_theory.Lp_trim_to_Lp_meas_subgroup_ae_eq MeasureTheory.lpTrimToLpMeasSubgroup_ae_eq

theorem lpMeasToLpTrim_ae_eq (hm : m ‚â§ m0) (f : lpMeas F ùïú m p Œº) :
    lpMeasToLpTrim F ùïú p Œº hm f =·µê[Œº] f :=
  -- Porting note: replaced `(‚Üëf)` with `f.1` here.
  (ae_eq_of_ae_eq_trim (Mem‚Ñíp.coeFn_toLp (mem‚Ñíp_trim_of_mem_lpMeasSubgroup hm f.1 f.mem))).trans
    (mem_lpMeasSubgroup_iff_aeStronglyMeasurable'.mp f.mem).choose_spec.2.symm
#align measure_theory.Lp_meas_to_Lp_trim_ae_eq MeasureTheory.lpMeasToLpTrim_ae_eq

theorem lpTrimToLpMeas_ae_eq (hm : m ‚â§ m0) (f : Lp F p (Œº.trim hm)) :
    lpTrimToLpMeas F ùïú p Œº hm f =·µê[Œº] f :=
  -- Porting note: filled in the argument
  Mem‚Ñíp.coeFn_toLp (mem‚Ñíp_of_mem‚Ñíp_trim hm (Lp.mem‚Ñíp f))
#align measure_theory.Lp_trim_to_Lp_meas_ae_eq MeasureTheory.lpTrimToLpMeas_ae_eq

/-- `lpTrimToLpMeasSubgroup` is a right inverse of `lpMeasSubgroupToLpTrim`. -/
theorem lpMeasSubgroupToLpTrim_right_inv (hm : m ‚â§ m0) :
    Function.RightInverse (lpTrimToLpMeasSubgroup F p Œº hm) (lpMeasSubgroupToLpTrim F p Œº hm) := by
  intro f
  ext1
  refine
    ae_eq_trim_of_stronglyMeasurable hm (Lp.stronglyMeasurable _) (Lp.stronglyMeasurable _) ?_
  exact (lpMeasSubgroupToLpTrim_ae_eq hm _).trans (lpTrimToLpMeasSubgroup_ae_eq hm _)
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_right_inv MeasureTheory.lpMeasSubgroupToLpTrim_right_inv

/-- `lpTrimToLpMeasSubgroup` is a left inverse of `lpMeasSubgroupToLpTrim`. -/
theorem lpMeasSubgroupToLpTrim_left_inv (hm : m ‚â§ m0) :
    Function.LeftInverse (lpTrimToLpMeasSubgroup F p Œº hm) (lpMeasSubgroupToLpTrim F p Œº hm) := by
  intro f
  ext1
  ext1
  rw [‚Üê lpMeasSubgroup_coe]
  exact (lpTrimToLpMeasSubgroup_ae_eq hm _).trans (lpMeasSubgroupToLpTrim_ae_eq hm _)
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_left_inv MeasureTheory.lpMeasSubgroupToLpTrim_left_inv

theorem lpMeasSubgroupToLpTrim_add (hm : m ‚â§ m0) (f g : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm (f + g) =
      lpMeasSubgroupToLpTrim F p Œº hm f + lpMeasSubgroupToLpTrim F p Œº hm g := by
  ext1
  refine EventuallyEq.trans ?_ (Lp.coeFn_add _ _).symm
  refine ae_eq_trim_of_stronglyMeasurable hm (Lp.stronglyMeasurable _) ?_ ?_
  ¬∑ exact (Lp.stronglyMeasurable _).add (Lp.stronglyMeasurable _)
  refine (lpMeasSubgroupToLpTrim_ae_eq hm _).trans ?_
  refine
    EventuallyEq.trans ?_
      (EventuallyEq.add (lpMeasSubgroupToLpTrim_ae_eq hm f).symm
        (lpMeasSubgroupToLpTrim_ae_eq hm g).symm)
  refine (Lp.coeFn_add _ _).trans ?_
  simp_rw [lpMeasSubgroup_coe]
  filter_upwards with x using rfl
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_add MeasureTheory.lpMeasSubgroupToLpTrim_add

theorem lpMeasSubgroupToLpTrim_neg (hm : m ‚â§ m0) (f : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm (-f) = -lpMeasSubgroupToLpTrim F p Œº hm f := by
  ext1
  refine EventuallyEq.trans ?_ (Lp.coeFn_neg _).symm
  refine ae_eq_trim_of_stronglyMeasurable hm (Lp.stronglyMeasurable _) ?_ ?_
  ¬∑ exact @StronglyMeasurable.neg _ _ _ m _ _ _ (Lp.stronglyMeasurable _)
  refine (lpMeasSubgroupToLpTrim_ae_eq hm _).trans ?_
  refine EventuallyEq.trans ?_ (EventuallyEq.neg (lpMeasSubgroupToLpTrim_ae_eq hm f).symm)
  refine (Lp.coeFn_neg _).trans ?_
  simp_rw [lpMeasSubgroup_coe]
  exact eventually_of_forall fun x => by rfl
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_neg MeasureTheory.lpMeasSubgroupToLpTrim_neg

theorem lpMeasSubgroupToLpTrim_sub (hm : m ‚â§ m0) (f g : lpMeasSubgroup F m p Œº) :
    lpMeasSubgroupToLpTrim F p Œº hm (f - g) =
      lpMeasSubgroupToLpTrim F p Œº hm f - lpMeasSubgroupToLpTrim F p Œº hm g := by
  rw [sub_eq_add_neg, sub_eq_add_neg, lpMeasSubgroupToLpTrim_add,
    lpMeasSubgroupToLpTrim_neg]
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_sub MeasureTheory.lpMeasSubgroupToLpTrim_sub

theorem lpMeasToLpTrim_smul (hm : m ‚â§ m0) (c : ùïú) (f : lpMeas F ùïú m p Œº) :
    lpMeasToLpTrim F ùïú p Œº hm (c ‚Ä¢ f) = c ‚Ä¢ lpMeasToLpTrim F ùïú p Œº hm f := by
  ext1
  refine EventuallyEq.trans ?_ (Lp.coeFn_smul _ _).symm
  refine ae_eq_trim_of_stronglyMeasurable hm (Lp.stronglyMeasurable _) ?_ ?_
  ¬∑ exact (Lp.stronglyMeasurable _).const_smul c
  refine (lpMeasToLpTrim_ae_eq hm _).trans ?_
  refine (Lp.coeFn_smul _ _).trans ?_
  refine (lpMeasToLpTrim_ae_eq hm f).mono fun x hx => ?_
  simp only [Pi.smul_apply, hx]
#align measure_theory.Lp_meas_to_Lp_trim_smul MeasureTheory.lpMeasToLpTrim_smul

/-- `lpMeasSubgroupToLpTrim` preserves the norm. -/
theorem lpMeasSubgroupToLpTrim_norm_map [hp : Fact (1 ‚â§ p)] (hm : m ‚â§ m0)
    (f : lpMeasSubgroup F m p Œº) : ‚ÄñlpMeasSubgroupToLpTrim F p Œº hm f‚Äñ = ‚Äñf‚Äñ := by
  rw [Lp.norm_def, snorm_trim hm (Lp.stronglyMeasurable _),
    snorm_congr_ae (lpMeasSubgroupToLpTrim_ae_eq hm _), lpMeasSubgroup_coe, ‚Üê Lp.norm_def]
  congr
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_norm_map MeasureTheory.lpMeasSubgroupToLpTrim_norm_map

theorem isometry_lpMeasSubgroupToLpTrim [hp : Fact (1 ‚â§ p)] (hm : m ‚â§ m0) :
    Isometry (lpMeasSubgroupToLpTrim F p Œº hm) :=
  Isometry.of_dist_eq fun f g => by
    rw [dist_eq_norm, ‚Üê lpMeasSubgroupToLpTrim_sub, lpMeasSubgroupToLpTrim_norm_map,
      dist_eq_norm]
#align measure_theory.isometry_Lp_meas_subgroup_to_Lp_trim MeasureTheory.isometry_lpMeasSubgroupToLpTrim

variable (F p Œº)

/-- `lpMeasSubgroup` and `Lp F p (Œº.trim hm)` are isometric. -/
noncomputable def lpMeasSubgroupToLpTrimIso [Fact (1 ‚â§ p)] (hm : m ‚â§ m0) :
    lpMeasSubgroup F m p Œº ‚âÉ·µ¢ Lp F p (Œº.trim hm) where
  toFun := lpMeasSubgroupToLpTrim F p Œº hm
  invFun := lpTrimToLpMeasSubgroup F p Œº hm
  left_inv := lpMeasSubgroupToLpTrim_left_inv hm
  right_inv := lpMeasSubgroupToLpTrim_right_inv hm
  isometry_toFun := isometry_lpMeasSubgroupToLpTrim hm
#align measure_theory.Lp_meas_subgroup_to_Lp_trim_iso MeasureTheory.lpMeasSubgroupToLpTrimIso

variable (ùïú)

/-- `lpMeasSubgroup` and `lpMeas` are isometric. -/
noncomputable def lpMeasSubgroupToLpMeasIso [Fact (1 ‚â§ p)] :
    lpMeasSubgroup F m p Œº ‚âÉ·µ¢ lpMeas F ùïú m p Œº :=
  IsometryEquiv.refl (lpMeasSubgroup F m p Œº)
#align measure_theory.Lp_meas_subgroup_to_Lp_meas_iso MeasureTheory.lpMeasSubgroupToLpMeasIso

/-- `lpMeas` and `Lp F p (Œº.trim hm)` are isometric, with a linear equivalence. -/
noncomputable def lpMeasToLpTrimLie [Fact (1 ‚â§ p)] (hm : m ‚â§ m0) :
    lpMeas F ùïú m p Œº ‚âÉ‚Çó·µ¢[ùïú] Lp F p (Œº.trim hm) where
  toFun := lpMeasToLpTrim F ùïú p Œº hm
  invFun := lpTrimToLpMeas F ùïú p Œº hm
  left_inv := lpMeasSubgroupToLpTrim_left_inv hm
  right_inv := lpMeasSubgroupToLpTrim_right_inv hm
  map_add' := lpMeasSubgroupToLpTrim_add hm
  map_smul' := lpMeasToLpTrim_smul hm
  norm_map' := lpMeasSubgroupToLpTrim_norm_map hm
#align measure_theory.Lp_meas_to_Lp_trim_lie MeasureTheory.lpMeasToLpTrimLie

variable {F ùïú p Œº}

instance [hm : Fact (m ‚â§ m0)] [CompleteSpace F] [hp : Fact (1 ‚â§ p)] :
    CompleteSpace (lpMeasSubgroup F m p Œº) := by
  rw [(lpMeasSubgroupToLpTrimIso F p Œº hm.elim).completeSpace_iff]; infer_instance

-- For now just no-lint this; lean4's tree-based logging will make this easier to debug.
-- One possible change might be to generalize `ùïú` from `RCLike` to `NormedField`, as this
-- result may well hold there.
-- Porting note: removed @[nolint fails_quickly]
instance [hm : Fact (m ‚â§ m0)] [CompleteSpace F] [hp : Fact (1 ‚â§ p)] :
    CompleteSpace (lpMeas F ùïú m p Œº) := by
  rw [(lpMeasSubgroupToLpMeasIso F ùïú p Œº).symm.completeSpace_iff]; infer_instance

theorem isComplete_aeStronglyMeasurable' [hp : Fact (1 ‚â§ p)] [CompleteSpace F] (hm : m ‚â§ m0) :
    IsComplete {f : Lp F p Œº | AEStronglyMeasurable' m f Œº} := by
  rw [‚Üê completeSpace_coe_iff_isComplete]
  haveI : Fact (m ‚â§ m0) := ‚ü®hm‚ü©
  change CompleteSpace (lpMeasSubgroup F m p Œº)
  infer_instance
#align measure_theory.is_complete_ae_strongly_measurable' MeasureTheory.isComplete_aeStronglyMeasurable'

theorem isClosed_aeStronglyMeasurable' [Fact (1 ‚â§ p)] [CompleteSpace F] (hm : m ‚â§ m0) :
    IsClosed {f : Lp F p Œº | AEStronglyMeasurable' m f Œº} :=
  IsComplete.isClosed (isComplete_aeStronglyMeasurable' hm)
#align measure_theory.is_closed_ae_strongly_measurable' MeasureTheory.isClosed_aeStronglyMeasurable'

end CompleteSubspace

section StronglyMeasurable

variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

/-- We do not get `ae_fin_strongly_measurable f (Œº.trim hm)`, since we don't have
`f =·µê[Œº.trim hm] Lp_meas_to_Lp_trim F ùïú p Œº hm f` but only the weaker
`f =·µê[Œº] Lp_meas_to_Lp_trim F ùïú p Œº hm f`. -/
theorem lpMeas.ae_fin_strongly_measurable' (hm : m ‚â§ m0) (f : lpMeas F ùïú m p Œº) (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) :
    -- Porting note: changed `f` to `f.1` in the next line. Not certain this is okay.
    ‚àÉ g, FinStronglyMeasurable g (Œº.trim hm) ‚àß f.1 =·µê[Œº] g :=
  ‚ü®lpMeasSubgroupToLpTrim F p Œº hm f, Lp.finStronglyMeasurable _ hp_ne_zero hp_ne_top,
    (lpMeasSubgroupToLpTrim_ae_eq hm f).symm‚ü©
#align measure_theory.Lp_meas.ae_fin_strongly_measurable' MeasureTheory.lpMeas.ae_fin_strongly_measurable'

/-- When applying the inverse of `lpMeasToLpTrimLie` (which takes a function in the Lp space of
the sub-sigma algebra and returns its version in the larger Lp space) to an indicator of the
sub-sigma-algebra, we obtain an indicator in the Lp space of the larger sigma-algebra. -/
theorem lpMeasToLpTrimLie_symm_indicator [one_le_p : Fact (1 ‚â§ p)] [NormedSpace ‚Ñù F] {hm : m ‚â§ m0}
    {s : Set Œ±} {Œº : Measure Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº.trim hm s ‚â† ‚àû) (c : F) :
    ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm (indicatorConstLp p hs hŒºs c) : Lp F p Œº) =
      indicatorConstLp p (hm s hs) ((le_trim hm).trans_lt hŒºs.lt_top).ne c := by
  ext1
  rw [‚Üê lpMeas_coe]
  change
    lpTrimToLpMeas F ‚Ñù p Œº hm (indicatorConstLp p hs hŒºs c) =·µê[Œº]
      (indicatorConstLp p _ _ c : Œ± ‚Üí F)
  refine (lpTrimToLpMeas_ae_eq hm _).trans ?_
  exact (ae_eq_of_ae_eq_trim indicatorConstLp_coeFn).trans indicatorConstLp_coeFn.symm
#align measure_theory.Lp_meas_to_Lp_trim_lie_symm_indicator MeasureTheory.lpMeasToLpTrimLie_symm_indicator

theorem lpMeasToLpTrimLie_symm_toLp [one_le_p : Fact (1 ‚â§ p)] [NormedSpace ‚Ñù F] (hm : m ‚â§ m0)
    (f : Œ± ‚Üí F) (hf : Mem‚Ñíp f p (Œº.trim hm)) :
    ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm (hf.toLp f) : Lp F p Œº) =
      (mem‚Ñíp_of_mem‚Ñíp_trim hm hf).toLp f := by
  ext1
  rw [‚Üê lpMeas_coe]
  refine (lpTrimToLpMeas_ae_eq hm _).trans ?_
  exact (ae_eq_of_ae_eq_trim (Mem‚Ñíp.coeFn_toLp hf)).trans (Mem‚Ñíp.coeFn_toLp _).symm
#align measure_theory.Lp_meas_to_Lp_trim_lie_symm_to_Lp MeasureTheory.lpMeasToLpTrimLie_symm_toLp

end StronglyMeasurable

end LpMeas

section Induction

variable {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±} [Fact (1 ‚â§ p)] [NormedSpace ‚Ñù F]

/-- Auxiliary lemma for `Lp.induction_stronglyMeasurable`. -/
@[elab_as_elim]
theorem Lp.induction_stronglyMeasurable_aux (hm : m ‚â§ m0) (hp_ne_top : p ‚â† ‚àû) (P : Lp F p Œº ‚Üí Prop)
    (h_ind : ‚àÄ (c : F) {s : Set Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº s < ‚àû),
      P (Lp.simpleFunc.indicatorConst p (hm s hs) hŒºs.ne c))
    (h_add : ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : Mem‚Ñíp f p Œº, ‚àÄ hg : Mem‚Ñíp g p Œº, AEStronglyMeasurable' m f Œº ‚Üí
      AEStronglyMeasurable' m g Œº ‚Üí Disjoint (Function.support f) (Function.support g) ‚Üí
        P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g))
    (h_closed : IsClosed {f : lpMeas F ‚Ñù m p Œº | P f}) :
    ‚àÄ f : Lp F p Œº, AEStronglyMeasurable' m f Œº ‚Üí P f := by
  intro f hf
  let f' := (‚ü®f, hf‚ü© : lpMeas F ‚Ñù m p Œº)
  let g := lpMeasToLpTrimLie F ‚Ñù p Œº hm f'
  have hfg : f' = (lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm g := by
    simp only [f', g, LinearIsometryEquiv.symm_apply_apply]
  change P ‚Üëf'
  rw [hfg]
  refine
    @Lp.induction Œ± F m _ p (Œº.trim hm) _ hp_ne_top
      (fun g => P ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm g)) ?_ ?_ ?_ g
  ¬∑ intro b t ht hŒºt
    -- Porting note: needed to pass `m` to `Lp.simpleFunc.coe_indicatorConst` to avoid
    -- synthesized type class instance is not definitionally equal to expression inferred by typing
    -- rules, synthesized m0 inferred m
    rw [@Lp.simpleFunc.coe_indicatorConst _ _ m, lpMeasToLpTrimLie_symm_indicator ht hŒºt.ne b]
    have hŒºt' : Œº t < ‚àû := (le_trim hm).trans_lt hŒºt
    specialize h_ind b ht hŒºt'
    rwa [Lp.simpleFunc.coe_indicatorConst] at h_ind
  ¬∑ intro f g hf hg h_disj hfP hgP
    rw [LinearIsometryEquiv.map_add]
    push_cast
    have h_eq :
      ‚àÄ (f : Œ± ‚Üí F) (hf : Mem‚Ñíp f p (Œº.trim hm)),
        ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm (Mem‚Ñíp.toLp f hf) : Lp F p Œº) =
          (mem‚Ñíp_of_mem‚Ñíp_trim hm hf).toLp f :=
      lpMeasToLpTrimLie_symm_toLp hm
    rw [h_eq f hf] at hfP ‚ä¢
    rw [h_eq g hg] at hgP ‚ä¢
    exact
      h_add (mem‚Ñíp_of_mem‚Ñíp_trim hm hf) (mem‚Ñíp_of_mem‚Ñíp_trim hm hg)
        (aeStronglyMeasurable'_of_aeStronglyMeasurable'_trim hm hf.aestronglyMeasurable)
        (aeStronglyMeasurable'_of_aeStronglyMeasurable'_trim hm hg.aestronglyMeasurable)
        h_disj hfP hgP
  ¬∑ change IsClosed ((lpMeasToLpTrimLie F ‚Ñù p Œº hm).symm ‚Åª¬π' {g : lpMeas F ‚Ñù m p Œº | P ‚Üëg})
    exact IsClosed.preimage (LinearIsometryEquiv.continuous _) h_closed
#align measure_theory.Lp.induction_strongly_measurable_aux MeasureTheory.Lp.induction_stronglyMeasurable_aux

/-- To prove something for an `Lp` function a.e. strongly measurable with respect to a
sub-œÉ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in `Lp` strongly measurable w.r.t. `m` for which the property holds is
  closed.
-/
@[elab_as_elim]
theorem Lp.induction_stronglyMeasurable (hm : m ‚â§ m0) (hp_ne_top : p ‚â† ‚àû) (P : Lp F p Œº ‚Üí Prop)
    (h_ind : ‚àÄ (c : F) {s : Set Œ±} (hs : MeasurableSet[m] s) (hŒºs : Œº s < ‚àû),
      P (Lp.simpleFunc.indicatorConst p (hm s hs) hŒºs.ne c))
    (h_add : ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : Mem‚Ñíp f p Œº, ‚àÄ hg : Mem‚Ñíp g p Œº, StronglyMeasurable[m] f ‚Üí
      StronglyMeasurable[m] g ‚Üí Disjoint (Function.support f) (Function.support g) ‚Üí
        P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g))
    (h_closed : IsClosed {f : lpMeas F ‚Ñù m p Œº | P f}) :
    ‚àÄ f : Lp F p Œº, AEStronglyMeasurable' m f Œº ‚Üí P f := by
  intro f hf
  suffices h_add_ae :
    ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : Mem‚Ñíp f p Œº, ‚àÄ hg : Mem‚Ñíp g p Œº, AEStronglyMeasurable' m f Œº ‚Üí
      AEStronglyMeasurable' m g Œº ‚Üí Disjoint (Function.support f) (Function.support g) ‚Üí
        P (hf.toLp f) ‚Üí P (hg.toLp g) ‚Üí P (hf.toLp f + hg.toLp g) from
  -- Porting note: `P` should be an explicit argument to `Lp.induction_stronglyMeasurable_aux`, but
  -- it isn't?
    Lp.induction_stronglyMeasurable_aux hm hp_ne_top h_ind h_add_ae h_closed f hf
  intro f g hf hg hfm hgm h_disj hPf hPg
  let s_f : Set Œ± := Function.support (hfm.mk f)
  have hs_f : MeasurableSet[m] s_f := hfm.stronglyMeasurable_mk.measurableSet_support
  have hs_f_eq : s_f =·µê[Œº] Function.support f := hfm.ae_eq_mk.symm.support
  let s_g : Set Œ± := Function.support (hgm.mk g)
  have hs_g : MeasurableSet[m] s_g := hgm.stronglyMeasurable_mk.measurableSet_support
  have hs_g_eq : s_g =·µê[Œº] Function.support g := hgm.ae_eq_mk.symm.support
  have h_inter_empty : (s_f ‚à© s_g : Set Œ±) =·µê[Œº] (‚àÖ : Set Œ±) := by
    refine (hs_f_eq.inter hs_g_eq).trans ?_
    suffices Function.support f ‚à© Function.support g = ‚àÖ by rw [this]
    exact Set.disjoint_iff_inter_eq_empty.mp h_disj
  let f' := (s_f \ s_g).indicator (hfm.mk f)
  have hff' : f =·µê[Œº] f' := by
    have : s_f \ s_g =·µê[Œº] s_f := by
      rw [‚Üê Set.diff_inter_self_eq_diff, Set.inter_comm]
      refine ((ae_eq_refl s_f).diff h_inter_empty).trans ?_
      rw [Set.diff_empty]
    refine ((indicator_ae_eq_of_ae_eq_set this).trans ?_).symm
    rw [Set.indicator_support]
    exact hfm.ae_eq_mk.symm
  have hf'_meas : StronglyMeasurable[m] f' := hfm.stronglyMeasurable_mk.indicator (hs_f.diff hs_g)
  have hf'_Lp : Mem‚Ñíp f' p Œº := hf.ae_eq hff'
  let g' := (s_g \ s_f).indicator (hgm.mk g)
  have hgg' : g =·µê[Œº] g' := by
    have : s_g \ s_f =·µê[Œº] s_g := by
      rw [‚Üê Set.diff_inter_self_eq_diff]
      refine ((ae_eq_refl s_g).diff h_inter_empty).trans ?_
      rw [Set.diff_empty]
    refine ((indicator_ae_eq_of_ae_eq_set this).trans ?_).symm
    rw [Set.indicator_support]
    exact hgm.ae_eq_mk.symm
  have hg'_meas : StronglyMeasurable[m] g' := hgm.stronglyMeasurable_mk.indicator (hs_g.diff hs_f)
  have hg'_Lp : Mem‚Ñíp g' p Œº := hg.ae_eq hgg'
  have h_disj : Disjoint (Function.support f') (Function.support g') :=
    haveI : Disjoint (s_f \ s_g) (s_g \ s_f) := disjoint_sdiff_sdiff
    this.mono Set.support_indicator_subset Set.support_indicator_subset
  rw [‚Üê Mem‚Ñíp.toLp_congr hf'_Lp hf hff'.symm] at hPf ‚ä¢
  rw [‚Üê Mem‚Ñíp.toLp_congr hg'_Lp hg hgg'.symm] at hPg ‚ä¢
  exact h_add hf'_Lp hg'_Lp hf'_meas hg'_meas h_disj hPf hPg
#align measure_theory.Lp.induction_strongly_measurable MeasureTheory.Lp.induction_stronglyMeasurable

/-- To prove something for an arbitrary `Mem‚Ñíp` function a.e. strongly measurable with respect
to a sub-œÉ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in the `L·µñ` space strongly measurable w.r.t. `m` for which the property
  holds is closed.
* the property is closed under the almost-everywhere equal relation.
-/
@[elab_as_elim]
theorem Mem‚Ñíp.induction_stronglyMeasurable (hm : m ‚â§ m0) (hp_ne_top : p ‚â† ‚àû) (P : (Œ± ‚Üí F) ‚Üí Prop)
    (h_ind : ‚àÄ (c : F) ‚¶És‚¶Ñ, MeasurableSet[m] s ‚Üí Œº s < ‚àû ‚Üí P (s.indicator fun _ => c))
    (h_add : ‚àÄ ‚¶Éf g : Œ± ‚Üí F‚¶Ñ, Disjoint (Function.support f) (Function.support g) ‚Üí
      Mem‚Ñíp f p Œº ‚Üí Mem‚Ñíp g p Œº ‚Üí StronglyMeasurable[m] f ‚Üí StronglyMeasurable[m] g ‚Üí
        P f ‚Üí P g ‚Üí P (f + g))
    (h_closed : IsClosed {f : lpMeas F ‚Ñù m p Œº | P f})
    (h_ae : ‚àÄ ‚¶Éf g‚¶Ñ, f =·µê[Œº] g ‚Üí Mem‚Ñíp f p Œº ‚Üí P f ‚Üí P g) :
    ‚àÄ ‚¶Éf : Œ± ‚Üí F‚¶Ñ, Mem‚Ñíp f p Œº ‚Üí AEStronglyMeasurable' m f Œº ‚Üí P f := by
  intro f hf hfm
  let f_Lp := hf.toLp f
  have hfm_Lp : AEStronglyMeasurable' m f_Lp Œº := hfm.congr hf.coeFn_toLp.symm
  refine h_ae hf.coeFn_toLp (Lp.mem‚Ñíp _) ?_
  change P f_Lp
  -- Porting note: `P` should be an explicit argument to `Lp.induction_stronglyMeasurable`, but
  -- it isn't?
  refine Lp.induction_stronglyMeasurable hm hp_ne_top (P := fun f => P f) ?_ ?_ h_closed f_Lp hfm_Lp
  ¬∑ intro c s hs hŒºs
    rw [Lp.simpleFunc.coe_indicatorConst]
    refine h_ae indicatorConstLp_coeFn.symm ?_ (h_ind c hs hŒºs)
    exact mem‚Ñíp_indicator_const p (hm s hs) c (Or.inr hŒºs.ne)
  ¬∑ intro f g hf_mem hg_mem hfm hgm h_disj hfP hgP
    have hfP' : P f := h_ae hf_mem.coeFn_toLp (Lp.mem‚Ñíp _) hfP
    have hgP' : P g := h_ae hg_mem.coeFn_toLp (Lp.mem‚Ñíp _) hgP
    specialize h_add h_disj hf_mem hg_mem hfm hgm hfP' hgP'
    refine h_ae ?_ (hf_mem.add hg_mem) h_add
    exact (hf_mem.coeFn_toLp.symm.add hg_mem.coeFn_toLp.symm).trans (Lp.coeFn_add _ _).symm
#align measure_theory.mem_‚Ñíp.induction_strongly_measurable MeasureTheory.Mem‚Ñíp.induction_stronglyMeasurable

end Induction

end MeasureTheory
