/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic

/-!

# Conditional expectation of indicator functions

This file proves some results about the conditional expectation of an indicator function and
as a corollary, also proves several results about the behaviour of the conditional expectation on
a restricted measure.

## Main result

* `MeasureTheory.condExp_indicator`: If `s` is an `m`-measurable set, then the conditional
  expectation of the indicator function of `s` is almost everywhere equal to the indicator
  of `s` of the conditional expectation. Namely, `ùîº[s.indicator f | m] = s.indicator ùîº[f | m]` a.e.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Œ± E : Type*} {m m0 : MeasurableSpace Œ±} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [CompleteSpace E] {Œº : Measure Œ±} {f : Œ± ‚Üí E} {s : Set Œ±}

theorem condExp_ae_eq_restrict_zero (hs : MeasurableSet[m] s) (hf : f =·µê[Œº.restrict s] 0) :
    Œº[f|m] =·µê[Œº.restrict s] 0 := by
  by_cases hm : m ‚â§ m0
  swap; ¬∑ simp_rw [condExp_of_not_le hm]; rfl
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm]; rfl
  haveI : SigmaFinite (Œº.trim hm) := hŒºm
  have : SigmaFinite ((Œº.restrict s).trim hm) := by
    rw [‚Üê restrict_trim hm _ hs]
    exact Restrict.sigmaFinite _ s
  by_cases hf_int : Integrable f Œº
  swap; ¬∑ rw [condExp_of_not_integrable hf_int]
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hm ?_ ?_ ?_ ?_ ?_
  ¬∑ exact fun t _ _ ‚Ü¶ integrable_condExp.integrableOn.integrableOn
  ¬∑ exact fun t _ _ ‚Ü¶ (integrable_zero _ _ _).integrableOn
  ¬∑ intro t ht _
    rw [Measure.restrict_restrict (hm _ ht), setIntegral_condExp hm hf_int (ht.inter hs), ‚Üê
      Measure.restrict_restrict (hm _ ht)]
    refine setIntegral_congr_ae (hm _ ht) ?_
    filter_upwards [hf] with x hx _ using hx
  ¬∑ exact stronglyMeasurable_condExp.aestronglyMeasurable
  ¬∑ exact stronglyMeasurable_zero.aestronglyMeasurable

/-- Auxiliary lemma for `condExp_indicator`. -/
theorem condExp_indicator_aux (hs : MeasurableSet[m] s) (hf : f =·µê[Œº.restrict s·∂ú] 0) :
    Œº[s.indicator f|m] =·µê[Œº] s.indicator (Œº[f|m]) := by
  by_cases hm : m ‚â§ m0
  swap; ¬∑ simp_rw [condExp_of_not_le hm, Set.indicator_zero']; rfl
  have hsf_zero : ‚àÄ g : Œ± ‚Üí E, g =·µê[Œº.restrict s·∂ú] 0 ‚Üí s.indicator g =·µê[Œº] g := fun g ‚Ü¶
    indicator_ae_eq_of_restrict_compl_ae_eq_zero (hm _ hs)
  refine ((hsf_zero (Œº[f|m]) (condExp_ae_eq_restrict_zero hs.compl hf)).trans ?_).symm
  exact condExp_congr_ae (hsf_zero f hf).symm

/-- The conditional expectation of the indicator of a function over an `m`-measurable set with
respect to the œÉ-algebra `m` is a.e. equal to the indicator of the conditional expectation. -/
theorem condExp_indicator (hf_int : Integrable f Œº) (hs : MeasurableSet[m] s) :
    Œº[s.indicator f|m] =·µê[Œº] s.indicator (Œº[f|m]) := by
  by_cases hm : m ‚â§ m0
  swap; ¬∑ simp_rw [condExp_of_not_le hm, Set.indicator_zero']; rfl
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condExp_of_not_sigmaFinite hm hŒºm, Set.indicator_zero']; rfl
  haveI : SigmaFinite (Œº.trim hm) := hŒºm
  -- use `have` to perform what should be the first calc step because of an error I don't
  -- understand
  have : s.indicator (Œº[f|m]) =·µê[Œº] s.indicator (Œº[s.indicator f + s·∂ú.indicator f|m]) := by
    rw [Set.indicator_self_add_compl s f]
  refine (this.trans ?_).symm
  calc
    s.indicator (Œº[s.indicator f + s·∂ú.indicator f|m]) =·µê[Œº]
        s.indicator (Œº[s.indicator f|m] + Œº[s·∂ú.indicator f|m]) := by
      filter_upwards [condExp_add (hf_int.indicator (hm _ hs)) (hf_int.indicator (hm _ hs.compl)) m]
        with x hx
      classical rw [Set.indicator_apply, Set.indicator_apply, hx]
    _ = s.indicator (Œº[s.indicator f|m]) + s.indicator (Œº[s·∂ú.indicator f|m]) :=
      (s.indicator_add' _ _)
    _ =·µê[Œº] s.indicator (Œº[s.indicator f|m]) +
        s.indicator (s·∂ú.indicator (Œº[s·∂ú.indicator f|m])) := by
      refine Filter.EventuallyEq.rfl.add ?_
      have : s·∂ú.indicator (Œº[s·∂ú.indicator f|m]) =·µê[Œº] Œº[s·∂ú.indicator f|m] := by
        refine (condExp_indicator_aux hs.compl ?_).symm.trans ?_
        ¬∑ exact indicator_ae_eq_restrict_compl (hm _ hs.compl)
        ¬∑ rw [Set.indicator_indicator, Set.inter_self]
      filter_upwards [this] with x hx
      by_cases hxs : x ‚àà s
      ¬∑ simp only [hx, hxs, Set.indicator_of_mem]
      ¬∑ simp only [hxs, Set.indicator_of_notMem, not_false_iff]
    _ =·µê[Œº] s.indicator (Œº[s.indicator f|m]) := by
      rw [Set.indicator_indicator, Set.inter_compl_self, Set.indicator_empty', add_zero]
    _ =·µê[Œº] Œº[s.indicator f|m] := by
      refine (condExp_indicator_aux hs ?_).symm.trans ?_
      ¬∑ exact indicator_ae_eq_restrict_compl (hm _ hs)
      ¬∑ rw [Set.indicator_indicator, Set.inter_self]

theorem condExp_restrict_ae_eq_restrict (hm : m ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    (hs_m : MeasurableSet[m] s) (hf_int : Integrable f Œº) :
    (Œº.restrict s)[f|m] =·µê[Œº.restrict s] Œº[f|m] := by
  have : SigmaFinite ((Œº.restrict s).trim hm) := by rw [‚Üê restrict_trim hm _ hs_m]; infer_instance
  rw [ae_eq_restrict_iff_indicator_ae_eq (hm _ hs_m)]
  refine EventuallyEq.trans ?_ (condExp_indicator hf_int hs_m)
  refine ae_eq_condExp_of_forall_setIntegral_eq hm (hf_int.indicator (hm _ hs_m)) ?_ ?_ ?_
  ¬∑ intro t ht _
    rw [‚Üê integrable_indicator_iff (hm _ ht), Set.indicator_indicator, Set.inter_comm, ‚Üê
      Set.indicator_indicator]
    suffices h_int_restrict : Integrable (t.indicator ((Œº.restrict s)[f|m])) (Œº.restrict s) by
      rw [integrable_indicator_iff (hm _ hs_m), IntegrableOn]
      exact h_int_restrict
    exact integrable_condExp.indicator (hm _ ht)
  ¬∑ intro t ht _
    calc
      ‚à´ x in t, s.indicator ((Œº.restrict s)[f|m]) x ‚àÇŒº =
          ‚à´ x in t, ((Œº.restrict s)[f|m]) x ‚àÇŒº.restrict s := by
        rw [integral_indicator (hm _ hs_m), Measure.restrict_restrict (hm _ hs_m),
          Measure.restrict_restrict (hm _ ht), Set.inter_comm]
      _ = ‚à´ x in t, f x ‚àÇŒº.restrict s := setIntegral_condExp hm hf_int.integrableOn ht
      _ = ‚à´ x in t, s.indicator f x ‚àÇŒº := by
        rw [integral_indicator (hm _ hs_m), Measure.restrict_restrict (hm _ hs_m),
          Measure.restrict_restrict (hm _ ht), Set.inter_comm]
  ¬∑ exact (stronglyMeasurable_condExp.indicator hs_m).aestronglyMeasurable

/-- If the restriction to an `m`-measurable set `s` of a œÉ-algebra `m` is equal to the restriction
to `s` of another œÉ-algebra `m‚ÇÇ` (hypothesis `hs`), then `Œº[f | m] =·µê[Œº.restrict s] Œº[f | m‚ÇÇ]`. -/
theorem condExp_ae_eq_restrict_of_measurableSpace_eq_on {m m‚ÇÇ m0 : MeasurableSpace Œ±}
    {Œº : Measure Œ±} (hm : m ‚â§ m0) (hm‚ÇÇ : m‚ÇÇ ‚â§ m0) [SigmaFinite (Œº.trim hm)]
    [SigmaFinite (Œº.trim hm‚ÇÇ)] (hs_m : MeasurableSet[m] s)
    (hs : ‚àÄ t, MeasurableSet[m] (s ‚à© t) ‚Üî MeasurableSet[m‚ÇÇ] (s ‚à© t)) :
    Œº[f|m] =·µê[Œº.restrict s] Œº[f|m‚ÇÇ] := by
  rw [ae_eq_restrict_iff_indicator_ae_eq (hm _ hs_m)]
  have hs_m‚ÇÇ : MeasurableSet[m‚ÇÇ] s := by rwa [‚Üê Set.inter_univ s, ‚Üê hs Set.univ, Set.inter_univ]
  by_cases hf_int : Integrable f Œº
  swap; ¬∑ simp_rw [condExp_of_not_integrable hf_int]; rfl
  refine ((condExp_indicator hf_int hs_m).symm.trans ?_).trans (condExp_indicator hf_int hs_m‚ÇÇ)
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hm‚ÇÇ
    (fun s _ _ ‚Ü¶ integrable_condExp.integrableOn)
    (fun s _ _ ‚Ü¶ integrable_condExp.integrableOn) ?_ ?_
    stronglyMeasurable_condExp.aestronglyMeasurable
  swap
  ¬∑ have : StronglyMeasurable[m] (Œº[s.indicator f|m]) := stronglyMeasurable_condExp
    refine this.aestronglyMeasurable.of_measurableSpace_le_on hm hs_m (fun t ‚Ü¶ (hs t).mp) ?_
    exact condExp_ae_eq_restrict_zero hs_m.compl (indicator_ae_eq_restrict_compl (hm _ hs_m))
  intro t ht _
  have : ‚à´ x in t, (Œº[s.indicator f|m]) x ‚àÇŒº = ‚à´ x in s ‚à© t, (Œº[s.indicator f|m]) x ‚àÇŒº := by
    rw [‚Üê integral_add_compl (hm _ hs_m) integrable_condExp.integrableOn]
    suffices ‚à´ x in s·∂ú, (Œº[s.indicator f|m]) x ‚àÇŒº.restrict t = 0 by
      rw [this, add_zero, Measure.restrict_restrict (hm _ hs_m)]
    rw [Measure.restrict_restrict (MeasurableSet.compl (hm _ hs_m))]
    suffices Œº[s.indicator f|m] =·µê[Œº.restrict s·∂ú] 0 by
      rw [Set.inter_comm, ‚Üê Measure.restrict_restrict (hm‚ÇÇ _ ht)]
      calc
        ‚à´ x : Œ± in t, (Œº[s.indicator f|m]) x ‚àÇŒº.restrict s·∂ú =
            ‚à´ x : Œ± in t, 0 ‚àÇŒº.restrict s·∂ú := by
          refine setIntegral_congr_ae (hm‚ÇÇ _ ht) ?_
          filter_upwards [this] with x hx _ using hx
        _ = 0 := integral_zero _ _
    refine condExp_ae_eq_restrict_zero hs_m.compl ?_
    exact indicator_ae_eq_restrict_compl (hm _ hs_m)
  have hst_m : MeasurableSet[m] (s ‚à© t) := (hs _).mpr (hs_m‚ÇÇ.inter ht)
  simp_rw [this, setIntegral_condExp hm‚ÇÇ (hf_int.indicator (hm _ hs_m)) ht,
    setIntegral_condExp hm (hf_int.indicator (hm _ hs_m)) hst_m, integral_indicator (hm _ hs_m),
    Measure.restrict_restrict (hm _ hs_m), ‚Üê Set.inter_assoc, Set.inter_self]

end MeasureTheory
