/-
Copyright (c) 2022 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic

/-!

# Conditional expectation of indicator functions

This file proves some results about the conditional expectation of an indicator function and
as a corollary, also proves several results about the behaviour of the conditional expectation on
a restricted measure.

## Main result

* `MeasureTheory.condExp_indicator`: If `s` is an `m`-measurable set, then the conditional
  expectation of the indicator function of `s` is almost everywhere equal to the indicator
  of `s` of the conditional expectation. Namely, `ğ”¼[s.indicator f | m] = s.indicator ğ”¼[f | m]` a.e.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± E : Type*} {m m0 : MeasurableSpace Î±} [NormedAddCommGroup E] [NormedSpace â„ E]
  [CompleteSpace E] {Î¼ : Measure Î±} {f : Î± â†’ E} {s : Set Î±}

theorem condExp_ae_eq_restrict_zero (hs : MeasurableSet[m] s) (hf : f =áµ[Î¼.restrict s] 0) :
    Î¼[f|m] =áµ[Î¼.restrict s] 0 := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm]; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m]; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  have : SigmaFinite ((Î¼.restrict s).trim hm) := by
    rw [â† restrict_trim hm _ hs]
    exact Restrict.sigmaFinite _ s
  by_cases hf_int : Integrable f Î¼
  swap; Â· rw [condExp_of_not_integrable hf_int]
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hm ?_ ?_ ?_ ?_ ?_
  Â· exact fun t _ _ => integrable_condExp.integrableOn.integrableOn
  Â· exact fun t _ _ => (integrable_zero _ _ _).integrableOn
  Â· intro t ht _
    rw [Measure.restrict_restrict (hm _ ht), setIntegral_condExp hm hf_int (ht.inter hs), â†
      Measure.restrict_restrict (hm _ ht)]
    refine setIntegral_congr_ae (hm _ ht) ?_
    filter_upwards [hf] with x hx _ using hx
  Â· exact stronglyMeasurable_condExp.aestronglyMeasurable
  Â· exact stronglyMeasurable_zero.aestronglyMeasurable

/-- Auxiliary lemma for `condExp_indicator`. -/
theorem condExp_indicator_aux (hs : MeasurableSet[m] s) (hf : f =áµ[Î¼.restrict sá¶œ] 0) :
    Î¼[s.indicator f|m] =áµ[Î¼] s.indicator (Î¼[f|m]) := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm, Set.indicator_zero']; rfl
  have hsf_zero : âˆ€ g : Î± â†’ E, g =áµ[Î¼.restrict sá¶œ] 0 â†’ s.indicator g =áµ[Î¼] g := fun g =>
    indicator_ae_eq_of_restrict_compl_ae_eq_zero (hm _ hs)
  refine ((hsf_zero (Î¼[f|m]) (condExp_ae_eq_restrict_zero hs.compl hf)).trans ?_).symm
  exact condExp_congr_ae (hsf_zero f hf).symm

/-- The conditional expectation of the indicator of a function over an `m`-measurable set with
respect to the Ïƒ-algebra `m` is a.e. equal to the indicator of the conditional expectation. -/
theorem condExp_indicator (hf_int : Integrable f Î¼) (hs : MeasurableSet[m] s) :
    Î¼[s.indicator f|m] =áµ[Î¼] s.indicator (Î¼[f|m]) := by
  by_cases hm : m â‰¤ m0
  swap; Â· simp_rw [condExp_of_not_le hm, Set.indicator_zero']; rfl
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condExp_of_not_sigmaFinite hm hÎ¼m, Set.indicator_zero']; rfl
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  -- use `have` to perform what should be the first calc step because of an error I don't
  -- understand
  have : s.indicator (Î¼[f|m]) =áµ[Î¼] s.indicator (Î¼[s.indicator f + sá¶œ.indicator f|m]) := by
    rw [Set.indicator_self_add_compl s f]
  refine (this.trans ?_).symm
  calc
    s.indicator (Î¼[s.indicator f + sá¶œ.indicator f|m]) =áµ[Î¼]
        s.indicator (Î¼[s.indicator f|m] + Î¼[sá¶œ.indicator f|m]) := by
      filter_upwards [condExp_add (hf_int.indicator (hm _ hs)) (hf_int.indicator (hm _ hs.compl)) m]
        with x hx
      classical rw [Set.indicator_apply, Set.indicator_apply, hx]
    _ = s.indicator (Î¼[s.indicator f|m]) + s.indicator (Î¼[sá¶œ.indicator f|m]) :=
      (s.indicator_add' _ _)
    _ =áµ[Î¼] s.indicator (Î¼[s.indicator f|m]) +
        s.indicator (sá¶œ.indicator (Î¼[sá¶œ.indicator f|m])) := by
      refine Filter.EventuallyEq.rfl.add ?_
      have : sá¶œ.indicator (Î¼[sá¶œ.indicator f|m]) =áµ[Î¼] Î¼[sá¶œ.indicator f|m] := by
        refine (condExp_indicator_aux hs.compl ?_).symm.trans ?_
        Â· exact indicator_ae_eq_restrict_compl (hm _ hs.compl)
        Â· rw [Set.indicator_indicator, Set.inter_self]
      filter_upwards [this] with x hx
      by_cases hxs : x âˆˆ s
      Â· simp only [hx, hxs, Set.indicator_of_mem]
      Â· simp only [hxs, Set.indicator_of_notMem, not_false_iff]
    _ =áµ[Î¼] s.indicator (Î¼[s.indicator f|m]) := by
      rw [Set.indicator_indicator, Set.inter_compl_self, Set.indicator_empty', add_zero]
    _ =áµ[Î¼] Î¼[s.indicator f|m] := by
      refine (condExp_indicator_aux hs ?_).symm.trans ?_
      Â· exact indicator_ae_eq_restrict_compl (hm _ hs)
      Â· rw [Set.indicator_indicator, Set.inter_self]

theorem condExp_restrict_ae_eq_restrict (hm : m â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    (hs_m : MeasurableSet[m] s) (hf_int : Integrable f Î¼) :
    (Î¼.restrict s)[f|m] =áµ[Î¼.restrict s] Î¼[f|m] := by
  have : SigmaFinite ((Î¼.restrict s).trim hm) := by rw [â† restrict_trim hm _ hs_m]; infer_instance
  rw [ae_eq_restrict_iff_indicator_ae_eq (hm _ hs_m)]
  refine EventuallyEq.trans ?_ (condExp_indicator hf_int hs_m)
  refine ae_eq_condExp_of_forall_setIntegral_eq hm (hf_int.indicator (hm _ hs_m)) ?_ ?_ ?_
  Â· intro t ht _
    rw [â† integrable_indicator_iff (hm _ ht), Set.indicator_indicator, Set.inter_comm, â†
      Set.indicator_indicator]
    suffices h_int_restrict : Integrable (t.indicator ((Î¼.restrict s)[f|m])) (Î¼.restrict s) by
      rw [integrable_indicator_iff (hm _ hs_m), IntegrableOn]
      exact h_int_restrict
    exact integrable_condExp.indicator (hm _ ht)
  Â· intro t ht _
    calc
      âˆ« x in t, s.indicator ((Î¼.restrict s)[f|m]) x âˆ‚Î¼ =
          âˆ« x in t, ((Î¼.restrict s)[f|m]) x âˆ‚Î¼.restrict s := by
        rw [integral_indicator (hm _ hs_m), Measure.restrict_restrict (hm _ hs_m),
          Measure.restrict_restrict (hm _ ht), Set.inter_comm]
      _ = âˆ« x in t, f x âˆ‚Î¼.restrict s := setIntegral_condExp hm hf_int.integrableOn ht
      _ = âˆ« x in t, s.indicator f x âˆ‚Î¼ := by
        rw [integral_indicator (hm _ hs_m), Measure.restrict_restrict (hm _ hs_m),
          Measure.restrict_restrict (hm _ ht), Set.inter_comm]
  Â· exact (stronglyMeasurable_condExp.indicator hs_m).aestronglyMeasurable

/-- If the restriction to an `m`-measurable set `s` of a Ïƒ-algebra `m` is equal to the restriction
to `s` of another Ïƒ-algebra `mâ‚‚` (hypothesis `hs`), then `Î¼[f | m] =áµ[Î¼.restrict s] Î¼[f | mâ‚‚]`. -/
theorem condExp_ae_eq_restrict_of_measurableSpace_eq_on {m mâ‚‚ m0 : MeasurableSpace Î±}
    {Î¼ : Measure Î±} (hm : m â‰¤ m0) (hmâ‚‚ : mâ‚‚ â‰¤ m0) [SigmaFinite (Î¼.trim hm)]
    [SigmaFinite (Î¼.trim hmâ‚‚)] (hs_m : MeasurableSet[m] s)
    (hs : âˆ€ t, MeasurableSet[m] (s âˆ© t) â†” MeasurableSet[mâ‚‚] (s âˆ© t)) :
    Î¼[f|m] =áµ[Î¼.restrict s] Î¼[f|mâ‚‚] := by
  rw [ae_eq_restrict_iff_indicator_ae_eq (hm _ hs_m)]
  have hs_mâ‚‚ : MeasurableSet[mâ‚‚] s := by rwa [â† Set.inter_univ s, â† hs Set.univ, Set.inter_univ]
  by_cases hf_int : Integrable f Î¼
  swap; Â· simp_rw [condExp_of_not_integrable hf_int]; rfl
  refine ((condExp_indicator hf_int hs_m).symm.trans ?_).trans (condExp_indicator hf_int hs_mâ‚‚)
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hmâ‚‚
    (fun s _ _ => integrable_condExp.integrableOn)
    (fun s _ _ => integrable_condExp.integrableOn) ?_ ?_
    stronglyMeasurable_condExp.aestronglyMeasurable
  swap
  Â· have : StronglyMeasurable[m] (Î¼[s.indicator f|m]) := stronglyMeasurable_condExp
    refine this.aestronglyMeasurable.of_measurableSpace_le_on hm hs_m (fun t => (hs t).mp) ?_
    exact condExp_ae_eq_restrict_zero hs_m.compl (indicator_ae_eq_restrict_compl (hm _ hs_m))
  intro t ht _
  have : âˆ« x in t, (Î¼[s.indicator f|m]) x âˆ‚Î¼ = âˆ« x in s âˆ© t, (Î¼[s.indicator f|m]) x âˆ‚Î¼ := by
    rw [â† integral_add_compl (hm _ hs_m) integrable_condExp.integrableOn]
    suffices âˆ« x in sá¶œ, (Î¼[s.indicator f|m]) x âˆ‚Î¼.restrict t = 0 by
      rw [this, add_zero, Measure.restrict_restrict (hm _ hs_m)]
    rw [Measure.restrict_restrict (MeasurableSet.compl (hm _ hs_m))]
    suffices Î¼[s.indicator f|m] =áµ[Î¼.restrict sá¶œ] 0 by
      rw [Set.inter_comm, â† Measure.restrict_restrict (hmâ‚‚ _ ht)]
      calc
        âˆ« x : Î± in t, (Î¼[s.indicator f|m]) x âˆ‚Î¼.restrict sá¶œ =
            âˆ« x : Î± in t, 0 âˆ‚Î¼.restrict sá¶œ := by
          refine setIntegral_congr_ae (hmâ‚‚ _ ht) ?_
          filter_upwards [this] with x hx _ using hx
        _ = 0 := integral_zero _ _
    refine condExp_ae_eq_restrict_zero hs_m.compl ?_
    exact indicator_ae_eq_restrict_compl (hm _ hs_m)
  have hst_m : MeasurableSet[m] (s âˆ© t) := (hs _).mpr (hs_mâ‚‚.inter ht)
  simp_rw [this, setIntegral_condExp hmâ‚‚ (hf_int.indicator (hm _ hs_m)) ht,
    setIntegral_condExp hm (hf_int.indicator (hm _ hs_m)) hst_m, integral_indicator (hm _ hs_m),
    Measure.restrict_restrict (hm _ hs_m), â† Set.inter_assoc, Set.inter_self]

end MeasureTheory
