/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion, RÃ©my Degenne, Kexing Ying
-/
module

public import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Indicator
import Mathlib.MeasureTheory.Function.Holder

/-!
# Pull-out property of the conditional expectation

Let `Î©` be endowed with a measurable space structure `mÎ©`, and let `m : MeasurableSpace Î©` such that
`m â‰¤ mÎ©`. Let `Î¼` be a measure over `Î©`. Let `B : F â†’L[â„] E â†’L[â„] G` a continuous bilinear map,
`f : Î© â†’ F` and `g : Î© â†’ E` such that `fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)` is integrable, `g` is integrable
and `f` is `AEStronglyMeasurable` with respect to `m`. The **pull-out** property of the conditional
expectation states that almost surely, `Î¼[B f g|m] = B f Î¼[g|m]`.

We specialize this statement to the cases where `B` is scalar multiplication and multiplication.

# Main statements

* `condExp_bilin_of_aestronglyMeasurable_left`: The pull-out property of the conditional
  expectation: almost surely, `Î¼[B f g|m] = B f Î¼[g|m]`.
* `condExp_smul_of_aestronglyMeasurable_left`: The pull-out property of the conditional
  expectation: almost surely, `Î¼[f â€¢ g|m] = f â€¢ Î¼[g|m]`.
* `condExp_mul_of_aestronglyMeasurable_left`: The pull-out property of the conditional
  expectation: almost surely, `Î¼[f * g|m] = f * Î¼[g|m]`.

# Tags

conditional expectation, pull-out, bilinear map
-/

public section


open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î© : Type*} {m mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©}
  {E F G : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [NormedAddCommGroup G] [NormedSpace â„ G]
  [CompleteSpace G] (B : F â†’L[â„] E â†’L[â„] G)

/-- Auxiliary lemma for `condExp_bilin_of_stronglyMeasurable_left`. -/
theorem condExp_stronglyMeasurable_simpleFunc_bilin [CompleteSpace E]
    (hm : m â‰¤ mÎ©) (f : @SimpleFunc Î© m F) {g : Î© â†’ E} (hg : Integrable g Î¼) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m] =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := by
  have : âˆ€ (s c) (f : Î© â†’ E),
      (fun Ï‰ â†¦ B (Set.indicator s (Function.const Î© c) Ï‰) (f Ï‰)) =
        s.indicator (fun Ï‰ â†¦ B c (f Ï‰)) := by
    intro s c f
    ext Ï‰
    by_cases hÏ‰ : Ï‰ âˆˆ s <;> simp [hÏ‰]
  apply @SimpleFunc.induction _ _ m _ (fun f â†¦ _)
    (fun c s hs â†¦ ?_) (fun gâ‚ gâ‚‚ _ h_eqâ‚ h_eqâ‚‚ â†¦ ?_) f
  Â· simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
      SimpleFunc.coe_zero, Set.piecewise_eq_indicator]
    rw [this, this]
    refine (condExp_indicator ((B c).integrable_comp hg) hs).trans ?_
    filter_upwards [(B c).comp_condExp_comm hg (m := m)] with Ï‰ hÏ‰
    simp only [Function.comp_apply] at hÏ‰
    simp only [Set.indicator, hÏ‰, Function.comp_def]
  Â· have h_add := @SimpleFunc.coe_add _ _ m _ gâ‚ gâ‚‚
    calc
      Î¼[fun Ï‰ â†¦ B (gâ‚ Ï‰ + gâ‚‚ Ï‰) (g Ï‰)|m] =áµ[Î¼]
          Î¼[fun Ï‰ â†¦ B (gâ‚ Ï‰) (g Ï‰)|m] + Î¼[fun Ï‰ â†¦ B (gâ‚‚ Ï‰) (g Ï‰)|m] := by
        simp_rw [B.map_add]
        obtain âŸ¨Câ‚, hCâ‚âŸ© := @SimpleFunc.exists_forall_norm_le _ _ m _ gâ‚
        obtain âŸ¨Câ‚‚, hCâ‚‚âŸ© := @SimpleFunc.exists_forall_norm_le _ _ m _ gâ‚‚
        exact condExp_add
          (B.integrable_of_bilin_of_bdd_left Câ‚ (gâ‚.stronglyMeasurable.mono hm).aestronglyMeasurable
            (ae_of_all _ hCâ‚) hg)
          (B.integrable_of_bilin_of_bdd_left Câ‚‚ (gâ‚‚.stronglyMeasurable.mono hm).aestronglyMeasurable
            (ae_of_all _ hCâ‚‚) hg) m
      _ =áµ[Î¼] fun Ï‰ â†¦ B (gâ‚ Ï‰) (Î¼[g|m] Ï‰) + B (gâ‚‚ Ï‰) (Î¼[g|m] Ï‰) := EventuallyEq.add h_eqâ‚ h_eqâ‚‚
      _ =áµ[Î¼] fun Ï‰ â†¦ B ((gâ‚ + gâ‚‚) Ï‰) (Î¼[g|m] Ï‰) := by simp

theorem condExp_stronglyMeasurable_bilin_of_bound [CompleteSpace E]
    (hm : m â‰¤ mÎ©) [IsFiniteMeasure Î¼] {f : Î© â†’ F} {g : Î© â†’ E} (hf : StronglyMeasurable[m] f)
    (hg : Integrable g Î¼) (c : â„) (hf_bound : âˆ€áµ Ï‰ âˆ‚Î¼, â€–f Ï‰â€– â‰¤ c) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m] =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := by
  let fs := hf.approxBounded c
  have hfs_tendsto : âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fs Â· Ï‰) atTop (ğ“ (f Ï‰)) :=
    hf.tendsto_approxBounded_ae hf_bound
  by_cases hÎ¼ : Î¼ = 0
  Â· simp only [hÎ¼, ae_zero]; norm_cast
  have : (ae Î¼).NeBot := ae_neBot.2 hÎ¼
  have hc : 0 â‰¤ c := by
    rcases hf_bound.exists with âŸ¨_, hâŸ©
    exact (norm_nonneg _).trans h
  have hfs_bound : âˆ€ n Ï‰, â€–fs n Ï‰â€– â‰¤ c := hf.norm_approxBounded_le hc
  have : Î¼[fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰)|m] = fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := by
    refine condExp_of_stronglyMeasurable hm ?_ ?_
    Â· exact Continuous.comp_stronglyMeasurable (g := (fun z : F Ã— E â†¦ B z.1 z.2)) (by fun_prop)
        (hf.prodMk stronglyMeasurable_condExp)
    Â· exact B.integrable_of_bilin_of_bdd_left c (hf.mono hm).aestronglyMeasurable hf_bound
        integrable_condExp
  rw [â† this]
  refine tendsto_condExp_unique (fun n Ï‰ â†¦ B (fs n Ï‰) (g Ï‰))
    (fun n Ï‰ â†¦ B (fs n Ï‰) (Î¼[g|m] Ï‰)) (fun Ï‰ â†¦ B (f Ï‰) (g Ï‰))
    (fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰)) ?_ ?_ ?_ ?_ (â€–Bâ€– * c * â€–g Â·â€–) ?_ (â€–Bâ€– * c * â€–(Î¼[g|m]) Â·â€–)
    ?_ ?_ ?_ ?_
  Â· exact fun n â†¦ B.integrable_of_bilin_of_bdd_left c
      ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable (ae_of_all _ <| hfs_bound n) hg
  Â· exact fun n â†¦ B.integrable_of_bilin_of_bdd_left c
      ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable (ae_of_all _ <| hfs_bound n)
      integrable_condExp
  Â· filter_upwards [hfs_tendsto] with Ï‰ hÏ‰
    exact ((by fun_prop : Continuous (fun x â†¦ B x (g Ï‰))).tendsto (f Ï‰)).comp hÏ‰
  Â· filter_upwards [hfs_tendsto] with Ï‰ hÏ‰
    exact ((by fun_prop : Continuous (fun x â†¦ B x (Î¼[g|m] Ï‰))).tendsto (f Ï‰)).comp hÏ‰
  Â· exact hg.norm.const_mul _
  Â· fun_prop
  Â· refine fun n â†¦ Eventually.of_forall fun _ â†¦ ?_
    grw [B.le_opNormâ‚‚, hfs_bound]
  Â· refine fun n â†¦ Eventually.of_forall fun _ â†¦ ?_
    grw [B.le_opNormâ‚‚, hfs_bound]
  Â· intro n
    refine (condExp_stronglyMeasurable_simpleFunc_bilin B hm _ hg).trans ?_
    nth_rw 2 [condExp_of_stronglyMeasurable hm]
    Â· exact Continuous.comp_stronglyMeasurable (g := (fun z : F Ã— E â†¦ B z.1 z.2)) (by fun_prop)
        ((fs n).stronglyMeasurable.prodMk stronglyMeasurable_condExp)
    exact B.integrable_of_bilin_of_bdd_left c
      ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable (ae_of_all _ <| hfs_bound n)
      integrable_condExp

theorem condExp_aestronglyMeasurable_bilin_of_bound [CompleteSpace E]
    (hm : m â‰¤ mÎ©) [IsFiniteMeasure Î¼] {f : Î© â†’ F} {g : Î© â†’ E} (hf : AEStronglyMeasurable[m] f Î¼)
    (hg : Integrable g Î¼) (c : â„) (hf_bound : âˆ€áµ Ï‰ âˆ‚Î¼, â€–f Ï‰â€– â‰¤ c) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m] =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := calc
  Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m]
  _ =áµ[Î¼] Î¼[fun Ï‰ â†¦ B (hf.mk f Ï‰) (g Ï‰)|m] := by
    apply condExp_congr_ae
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]
  _ =áµ[Î¼] fun Ï‰ â†¦ B (hf.mk f Ï‰) (Î¼[g|m] Ï‰) := by
    refine condExp_stronglyMeasurable_bilin_of_bound B hm hf.stronglyMeasurable_mk
      hg c ?_
    filter_upwards [hf_bound, hf.ae_eq_mk] with Ï‰ hÏ‰1 hÏ‰2
    rwa [â† hÏ‰2]
  _ =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := by
    filter_upwards [hf.ae_eq_mk] with Ï‰ hÏ‰ using by rw [hÏ‰]

/-- Pull-out property of the conditional expectation. -/
theorem condExp_bilin_of_stronglyMeasurable_left [CompleteSpace E] {f : Î© â†’ F} {g : Î© â†’ E}
    (hf : StronglyMeasurable[m] f) (hfg : Integrable (fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)) Î¼)
    (hg : Integrable g Î¼) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m] =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := by
  by_cases hm : m â‰¤ mÎ©; swap; Â· exact ae_of_all _ <| by simp [condExp_of_not_le hm]
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· exact ae_of_all _ <| by simp [condExp_of_not_sigmaFinite hm hÎ¼m]
  obtain âŸ¨sets, sets_prop, h_univâŸ© := hf.exists_spanning_measurableSet_norm_le hm Î¼
  simp_rw [forall_and] at sets_prop
  obtain âŸ¨h_meas, h_finite, h_normâŸ© := sets_prop
  suffices âˆ€ n, âˆ€áµ Ï‰ âˆ‚Î¼, Ï‰ âˆˆ sets n â†’ (Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m]) Ï‰ = B (f Ï‰) (Î¼[g|m] Ï‰) by
    rw [â† ae_all_iff] at this
    filter_upwards [this] with Ï‰ hÏ‰
    obtain âŸ¨i, hiâŸ© : âˆƒ i, Ï‰ âˆˆ sets i := by
      have h_mem : Ï‰ âˆˆ â‹ƒ i, sets i := by rw [h_univ]; exact Set.mem_univ _
      simpa using h_mem
    exact hÏ‰ i hi
  refine fun n â†¦ ae_imp_of_ae_restrict ?_
  suffices (Î¼.restrict (sets n))[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m] =áµ[Î¼.restrict (sets n)]
      fun Ï‰ â†¦ B (f Ï‰) ((Î¼.restrict (sets n))[g|m] Ï‰) by
    refine (condExp_restrict_ae_eq_restrict hm (h_meas n) hfg).symm.trans ?_
    filter_upwards [this, (condExp_restrict_ae_eq_restrict hm (h_meas n) hg)] with Ï‰ hÏ‰1 hÏ‰2
    rw [hÏ‰1, hÏ‰2]
  suffices (Î¼.restrict (sets n))[fun Ï‰ â†¦ B ((sets n).indicator f Ï‰) (g Ï‰)|m] =áµ[Î¼.restrict (sets n)]
      fun Ï‰ â†¦ B ((sets n).indicator f Ï‰) ((Î¼.restrict (sets n))[g|m] Ï‰) by
    refine EventuallyEq.trans (condExp_congr_ae ?_) (this.trans ?_)
    Â· filter_upwards [indicator_ae_eq_restrict (f := f) <| hm _ <| h_meas n] with Ï‰ hÏ‰
      rw [hÏ‰]
    Â· filter_upwards [indicator_ae_eq_restrict (f := f) <| hm _ <| h_meas n] with Ï‰ hÏ‰
      rw [hÏ‰]
  have : IsFiniteMeasure (Î¼.restrict (sets n)) := by
    constructor
    rw [Measure.restrict_apply_univ]
    exact h_finite n
  refine condExp_stronglyMeasurable_bilin_of_bound B hm (hf.indicator (h_meas n))
    hg.integrableOn n ?_
  filter_upwards with Ï‰
  by_cases hÏ‰s : Ï‰ âˆˆ sets n <;> simp [hÏ‰s, h_norm]

/-- Pull-out property of the conditional expectation. -/
theorem condExp_bilin_of_stronglyMeasurable_right [CompleteSpace F] {f : Î© â†’ F} {g : Î© â†’ E}
    (hg : StronglyMeasurable[m] g)
    (hfg : Integrable (fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)) Î¼) (hf : Integrable f Î¼) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰) | m] =áµ[Î¼] fun Ï‰ â†¦ B (Î¼[f | m] Ï‰) (g Ï‰) := by
  simp_rw [â† B.flip_apply] at hfg âŠ¢
  exact condExp_bilin_of_stronglyMeasurable_left B.flip hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_bilin_of_aestronglyMeasurable_left [CompleteSpace E]
    {f : Î© â†’ F} {g : Î© â†’ E} (hf : AEStronglyMeasurable[m] f Î¼)
    (hfg : Integrable (fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)) Î¼) (hg : Integrable g Î¼) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m] =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := calc
  Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)|m]
  _ =áµ[Î¼] Î¼[fun Ï‰ â†¦ B (hf.mk f Ï‰) (g Ï‰)|m] := by
    apply condExp_congr_ae
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]
  _ =áµ[Î¼] fun Ï‰ â†¦ B (hf.mk f Ï‰) (Î¼[g|m] Ï‰) := by
    refine condExp_bilin_of_stronglyMeasurable_left B hf.stronglyMeasurable_mk
      ((integrable_congr ?_).mp hfg) hg
    filter_upwards [hf.ae_eq_mk] with Ï‰ hÏ‰ using by rw [hÏ‰]
  _ =áµ[Î¼] fun Ï‰ â†¦ B (f Ï‰) (Î¼[g|m] Ï‰) := by
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]

/-- Pull-out property of the conditional expectation. -/
theorem condExp_bilin_of_aestronglyMeasurable_right [CompleteSpace F] {f : Î© â†’ F} {g : Î© â†’ E}
    (hg : AEStronglyMeasurable[m] g Î¼)
    (hfg : Integrable (fun Ï‰ â†¦ B (f Ï‰) (g Ï‰)) Î¼) (hf : Integrable f Î¼) :
    Î¼[fun Ï‰ â†¦ B (f Ï‰) (g Ï‰) | m] =áµ[Î¼] fun Ï‰ â†¦ B (Î¼[f | m] Ï‰) (g Ï‰) := by
  simp_rw [â† B.flip_apply] at hfg âŠ¢
  exact condExp_bilin_of_aestronglyMeasurable_left B.flip hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_smul_of_aestronglyMeasurable_left [CompleteSpace E] {f : Î© â†’ â„} {g : Î© â†’ E}
    (hf : AEStronglyMeasurable[m] f Î¼) (hfg : Integrable (f â€¢ g) Î¼) (hg : Integrable g Î¼) :
    Î¼[f â€¢ g|m] =áµ[Î¼] f â€¢ Î¼[g|m] :=
  condExp_bilin_of_aestronglyMeasurable_left (.lsmul â„ â„) hf hfg hg

/-- Pull-out property of the conditional expectation. -/
theorem condExp_smul_of_aestronglyMeasurable_right [CompleteSpace E] {f : Î© â†’ â„} {g : Î© â†’ E}
    (hf : Integrable f Î¼) (hfg : Integrable (f â€¢ g) Î¼) (hg : AEStronglyMeasurable[m] g Î¼) :
    Î¼[f â€¢ g|m] =áµ[Î¼] Î¼[f|m] â€¢ g :=
  condExp_bilin_of_aestronglyMeasurable_left (ContinuousLinearMap.lsmul â„ â„).flip hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_aestronglyMeasurable_left {f g : Î© â†’ â„} (hf : AEStronglyMeasurable[m] f Î¼)
    (hfg : Integrable (f * g) Î¼) (hg : Integrable g Î¼) : Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] :=
  condExp_bilin_of_aestronglyMeasurable_left (.mul â„ â„) hf hfg hg

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_aestronglyMeasurable_right {f g : Î© â†’ â„} (hg : AEStronglyMeasurable[m] g Î¼)
    (hfg : Integrable (f * g) Î¼) (hf : Integrable f Î¼) : Î¼[f * g | m] =áµ[Î¼] Î¼[f | m] * g :=
  condExp_bilin_of_aestronglyMeasurable_right (.mul â„ â„) hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_stronglyMeasurable_left {f g : Î© â†’ â„} (hf : StronglyMeasurable[m] f)
    (hfg : Integrable (f * g) Î¼) (hg : Integrable g Î¼) : Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] :=
  condExp_bilin_of_aestronglyMeasurable_left (.mul â„ â„)
    hf.aestronglyMeasurable hfg hg

/-- Pull-out property of the conditional expectation. -/
lemma condExp_mul_of_stronglyMeasurable_right {f g : Î© â†’ â„} (hg : StronglyMeasurable[m] g)
    (hfg : Integrable (f * g) Î¼) (hf : Integrable f Î¼) : Î¼[f * g | m] =áµ[Î¼] Î¼[f | m] * g :=
  condExp_bilin_of_aestronglyMeasurable_right (.mul â„ â„)
    hg.aestronglyMeasurable hfg hf

theorem condExp_stronglyMeasurable_simpleFunc_mul (hm : m â‰¤ mÎ©) (f : @SimpleFunc Î© m â„) {g : Î© â†’ â„}
    (hg : Integrable g Î¼) : Î¼[(f * g : Î© â†’ â„)|m] =áµ[Î¼] f * Î¼[g|m] :=
  condExp_stronglyMeasurable_simpleFunc_bilin (.mul â„ â„) hm f hg

theorem condExp_stronglyMeasurable_mul_of_bound (hm : m â‰¤ mÎ©) [IsFiniteMeasure Î¼] {f g : Î© â†’ â„}
    (hf : StronglyMeasurable[m] f) (hg : Integrable g Î¼) (c : â„) (hf_bound : âˆ€áµ Ï‰ âˆ‚Î¼, â€–f Ï‰â€– â‰¤ c) :
    Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] :=
  condExp_stronglyMeasurable_bilin_of_bound (.mul â„ â„) hm hf hg c hf_bound

theorem condExp_stronglyMeasurable_mul_of_boundâ‚€ (hm : m â‰¤ mÎ©) [IsFiniteMeasure Î¼] {f g : Î© â†’ â„}
    (hf : AEStronglyMeasurable[m] f Î¼) (hg : Integrable g Î¼) (c : â„)
    (hf_bound : âˆ€áµ Ï‰ âˆ‚Î¼, â€–f Ï‰â€– â‰¤ c) : Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] :=
  condExp_aestronglyMeasurable_bilin_of_bound (.mul â„ â„) hm hf hg c hf_bound

end MeasureTheory
