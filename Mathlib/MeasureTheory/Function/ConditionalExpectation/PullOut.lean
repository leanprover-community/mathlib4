/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion, R√©my Degenne, Kexing Ying
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Indicator
import Mathlib.MeasureTheory.Function.Holder

/-!
# Pull-out property of the conditional expectation

Let `Œ©` be endowed with a measurable space structure `mŒ©`, and let `m : MeasurableSpace Œ©` such that
`m ‚â§ mŒ©`. Let `Œº` be a measure over `Œ©`. Let `B : F ‚ÜíL[‚Ñù] E ‚ÜíL[‚Ñù] G` a continuous bilinear map,
`f : Œ© ‚Üí F` and `g : Œ© ‚Üí E` such that `fun œâ ‚Ü¶ B (f œâ) (g œâ)` is integrable, `g` is integrable
and `f` is `AEStronglyMeasurable` with respect to `m`. The **pull-out** property of the conditional
expectation states that almost surely, `Œº[B f g|m] = B f Œº[g|m]`.

We specialize this statement to the cases where `B` is scalar multiplication and multiplication.

# Main statements

* `condExp_bilin_of_aestronglyMeasurable_left`: The pull-out property of the conditional
  expectation: almost surely, `Œº[B f g|m] = B f Œº[g|m]`.
* `condExp_smul_of_aestronglyMeasurable_left`: The pull-out property of the conditional
  expectation: almost surely, `Œº[f ‚Ä¢ g|m] = f ‚Ä¢ Œº[g|m]`.
* `condExp_mul_of_aestronglyMeasurable_left`: The pull-out property of the conditional
  expectation: almost surely, `Œº[f * g|m] = f * Œº[g|m]`.

# Tags

conditional expectation, pull-out, bilinear map
-/

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Œ© : Type*} {m mŒ© : MeasurableSpace Œ©} {Œº : Measure Œ©}
  {E F G : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  [CompleteSpace G] (B : F ‚ÜíL[‚Ñù] E ‚ÜíL[‚Ñù] G)

/-- Auxiliary lemma for `condExp_bilin_of_stronglyMeasurable_left`. -/
theorem condExp_stronglyMeasurable_simpleFunc_bilin [CompleteSpace E]
    (hm : m ‚â§ mŒ©) (f : @SimpleFunc Œ© m F) {g : Œ© ‚Üí E} (hg : Integrable g Œº) :
    Œº[fun a ‚Ü¶ B (f a) (g a)|m] =·µê[Œº] fun a ‚Ü¶ B (f a) (Œº[g|m] a) := by
  have : ‚àÄ (s c) (f : Œ© ‚Üí E),
      (fun x ‚Ü¶ B (Set.indicator s (Function.const Œ© c) x) (f x)) =
        s.indicator (fun a ‚Ü¶ B c (f a)) := by
    intro s c f
    ext x
    by_cases hx : x ‚àà s <;> simp [hx]
  apply @SimpleFunc.induction _ _ m _ (fun f => _)
    (fun c s hs => ?_) (fun g‚ÇÅ g‚ÇÇ _ h_eq‚ÇÅ h_eq‚ÇÇ => ?_) f
  ¬∑ simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
    SimpleFunc.coe_zero, Set.piecewise_eq_indicator]
    rw [this, this]
    refine (condExp_indicator ((B c).integrable_comp hg) hs).trans ?_
    filter_upwards [(B c).comp_condExp_comm hg (m := m)] with x hx
    simp only [Function.comp_apply] at hx
    classical simp_rw [Set.indicator_apply, hx]
    rfl
  ¬∑ have h_add := @SimpleFunc.coe_add _ _ m _ g‚ÇÅ g‚ÇÇ
    calc
      Œº[fun a ‚Ü¶ B (g‚ÇÅ a + g‚ÇÇ a) (g a)|m] =·µê[Œº]
          Œº[fun a ‚Ü¶ B (g‚ÇÅ a) (g a)|m] + Œº[fun a ‚Ü¶ B (g‚ÇÇ a) (g a)|m] := by
        simp_rw [B.map_add]
        exact condExp_add (hg.simpleFunc_bilinearMap' B hm g‚ÇÅ)
          (hg.simpleFunc_bilinearMap' B hm g‚ÇÇ) m
      _ =·µê[Œº] fun a ‚Ü¶ B (g‚ÇÅ a) (Œº[g|m] a) + B (g‚ÇÇ a) (Œº[g|m] a) := EventuallyEq.add h_eq‚ÇÅ h_eq‚ÇÇ
      _ =·µê[Œº] fun a ‚Ü¶ B ((g‚ÇÅ + g‚ÇÇ) a) (Œº[g|m] a) := by simp

theorem condExp_stronglyMeasurable_bilin_of_bound [CompleteSpace E]
    (hm : m ‚â§ mŒ©) [IsFiniteMeasure Œº] {f : Œ© ‚Üí F} {g : Œ© ‚Üí E} (hf : StronglyMeasurable[m] f)
    (hg : Integrable g Œº) (c : ‚Ñù) (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    Œº[fun a ‚Ü¶ B (f a) (g a)|m] =·µê[Œº] fun a ‚Ü¶ B (f a) (Œº[g|m] a) := by
  let fs := hf.approxBounded c
  have hfs_tendsto : ‚àÄ·µê x ‚àÇŒº, Tendsto (fs ¬∑ x) atTop (ùìù (f x)) :=
    hf.tendsto_approxBounded_ae hf_bound
  by_cases hŒº : Œº = 0
  ¬∑ simp only [hŒº, ae_zero]; norm_cast
  have : (ae Œº).NeBot := ae_neBot.2 hŒº
  have hc : 0 ‚â§ c := by
    rcases hf_bound.exists with ‚ü®_x, hx‚ü©
    exact (norm_nonneg _).trans hx
  have hfs_bound : ‚àÄ n x, ‚Äñfs n x‚Äñ ‚â§ c := hf.norm_approxBounded_le hc
  have : Œº[fun a ‚Ü¶ B (f a) (Œº[g|m] a)|m] = fun a ‚Ü¶ B (f a) (Œº[g|m] a) := by
    refine condExp_of_stronglyMeasurable hm ?_ ?_
    ¬∑ exact Continuous.comp_stronglyMeasurable (g := (fun z : F √ó E ‚Ü¶ B z.1 z.2)) (by fun_prop)
        (hf.prodMk stronglyMeasurable_condExp)
    ¬∑ exact B.integrable_of_bilin_of_bdd_left c (hf.mono hm).aestronglyMeasurable hf_bound
        integrable_condExp
  rw [‚Üê this]
  refine tendsto_condExp_unique (fun n x => B (fs n x) (g x))
    (fun n x => B (fs n x) (Œº[g|m] x)) (fun x ‚Ü¶ B (f x) (g x))
    (fun x ‚Ü¶ B (f x) (Œº[g|m] x)) ?_ ?_ ?_ ?_ (‚ÄñB‚Äñ * c * ‚Äñg ¬∑‚Äñ) ?_ (‚ÄñB‚Äñ * c * ‚Äñ(Œº[g|m]) ¬∑‚Äñ)
    ?_ ?_ ?_ ?_
  ¬∑ exact fun n ‚Ü¶ B.integrable_of_bilin_of_bdd_left c
      ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable (ae_of_all _ <| hfs_bound n) hg
  ¬∑ exact fun n ‚Ü¶ B.integrable_of_bilin_of_bdd_left c
      ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable (ae_of_all _ <| hfs_bound n)
      integrable_condExp
  ¬∑ filter_upwards [hfs_tendsto] with x hx
    exact ((by fun_prop : Continuous (fun y ‚Ü¶ B y (g x))).tendsto (f x)).comp hx
  ¬∑ filter_upwards [hfs_tendsto] with x hx
    exact ((by fun_prop : Continuous (fun y ‚Ü¶ B y (Œº[g|m] x))).tendsto (f x)).comp hx
  ¬∑ exact hg.norm.const_mul _
  ¬∑ fun_prop
  ¬∑ refine fun n => Eventually.of_forall fun x => ?_
    grw [B.le_opNorm‚ÇÇ, hfs_bound]
  ¬∑ refine fun n => Eventually.of_forall fun x => ?_
    grw [B.le_opNorm‚ÇÇ, hfs_bound]
  ¬∑ intro n
    refine (condExp_stronglyMeasurable_simpleFunc_bilin B hm _ hg).trans ?_
    nth_rw 2 [condExp_of_stronglyMeasurable hm]
    ¬∑ exact Continuous.comp_stronglyMeasurable (g := (fun z : F √ó E ‚Ü¶ B z.1 z.2)) (by fun_prop)
        ((fs n).stronglyMeasurable.prodMk stronglyMeasurable_condExp)
    exact B.integrable_of_bilin_of_bdd_left c
      ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable (ae_of_all _ <| hfs_bound n)
      integrable_condExp

/-- Pull-out property of the conditional expectation. -/
theorem condExp_bilin_of_stronglyMeasurable_left [CompleteSpace E] {f : Œ© ‚Üí F} {g : Œ© ‚Üí E}
    (hf : StronglyMeasurable[m] f) (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº)
    (hg : Integrable g Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := by
  by_cases hm : m ‚â§ mŒ©; swap; ¬∑ exact ae_of_all _ <| by simp [condExp_of_not_le hm]
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ exact ae_of_all _ <| by simp [condExp_of_not_sigmaFinite hm hŒºm]
  obtain ‚ü®sets, sets_prop, h_univ‚ü© := hf.exists_spanning_measurableSet_norm_le hm Œº
  simp_rw [forall_and] at sets_prop
  obtain ‚ü®h_meas, h_finite, h_norm‚ü© := sets_prop
  suffices ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, x ‚àà sets n ‚Üí (Œº[fun x ‚Ü¶ B (f x) (g x)|m]) x = B (f x) (Œº[g|m] x) by
    rw [‚Üê ae_all_iff] at this
    filter_upwards [this] with x hx
    obtain ‚ü®i, hi‚ü© : ‚àÉ i, x ‚àà sets i := by
      have h_mem : x ‚àà ‚ãÉ i, sets i := by rw [h_univ]; exact Set.mem_univ _
      simpa using h_mem
    exact hx i hi
  refine fun n => ae_imp_of_ae_restrict ?_
  suffices (Œº.restrict (sets n))[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº.restrict (sets n)]
      fun x ‚Ü¶ B (f x) ((Œº.restrict (sets n))[g|m] x) by
    refine (condExp_restrict_ae_eq_restrict hm (h_meas n) hfg).symm.trans ?_
    filter_upwards [this, (condExp_restrict_ae_eq_restrict hm (h_meas n) hg)] with x hx1 hx2
    rw [hx1, hx2]
  suffices (Œº.restrict (sets n))[fun x ‚Ü¶ B ((sets n).indicator f x) (g x)|m] =·µê[Œº.restrict (sets n)]
      fun x ‚Ü¶ B ((sets n).indicator f x) ((Œº.restrict (sets n))[g|m] x) by
    refine EventuallyEq.trans (condExp_congr_ae ?_) (this.trans ?_)
    ¬∑ filter_upwards [indicator_ae_eq_restrict (f := f) <| hm _ <| h_meas n] with x hx
      rw [hx]
    ¬∑ filter_upwards [indicator_ae_eq_restrict (f := f) <| hm _ <| h_meas n] with x hx
      rw [hx]
  have : IsFiniteMeasure (Œº.restrict (sets n)) := by
    constructor
    rw [Measure.restrict_apply_univ]
    exact h_finite n
  refine condExp_stronglyMeasurable_bilin_of_bound B hm (hf.indicator (h_meas n))
    hg.integrableOn n ?_
  filter_upwards with x
  by_cases hxs : x ‚àà sets n <;> simp [hxs, h_norm]

/-- Pull-out property of the conditional expectation. -/
lemma condExp_bilin_of_stronglyMeasurable_right [CompleteSpace F] {f : Œ© ‚Üí F} {g : Œ© ‚Üí E}
    (hg : StronglyMeasurable[m] g)
    (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº) (hf : Integrable f Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x) | m] =·µê[Œº] fun x ‚Ü¶ B (Œº[f | m] x) (g x) := by
  simp_rw [‚Üê B.flip_apply] at hfg ‚ä¢
  exact condExp_bilin_of_stronglyMeasurable_left B.flip hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_bilin_of_aestronglyMeasurable_left [CompleteSpace E]
    {f : Œ© ‚Üí F} {g : Œ© ‚Üí E} (hf : AEStronglyMeasurable[m] f Œº)
    (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº) (hg : Integrable g Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := calc
  Œº[fun x ‚Ü¶ B (f x) (g x)|m]
  _ =·µê[Œº] Œº[fun x ‚Ü¶ B (hf.mk f x) (g x)|m] := by
    apply condExp_congr_ae
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]
  _ =·µê[Œº] fun x ‚Ü¶ B (hf.mk f x) (Œº[g|m] x) := by
    refine condExp_bilin_of_stronglyMeasurable_left B hf.stronglyMeasurable_mk
      ((integrable_congr ?_).mp hfg) hg
    filter_upwards [hf.ae_eq_mk] with x hx using by rw [hx]
  _ =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := by
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]

/-- Pull-out property of the conditional expectation. -/
lemma condExp_bilin_of_aestronglyMeasurable_right [CompleteSpace F] {f : Œ© ‚Üí F} {g : Œ© ‚Üí E}
    (hg : AEStronglyMeasurable[m] g Œº)
    (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº) (hf : Integrable f Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x) | m] =·µê[Œº] fun x ‚Ü¶ B (Œº[f | m] x) (g x) := by
  simp_rw [‚Üê B.flip_apply] at hfg ‚ä¢
  exact condExp_bilin_of_aestronglyMeasurable_left B.flip hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_smul_of_aestronglyMeasurable_left [CompleteSpace E] {f : Œ© ‚Üí ‚Ñù} {g : Œ© ‚Üí E}
    (hf : AEStronglyMeasurable[m] f Œº) (hfg : Integrable (f ‚Ä¢ g) Œº) (hg : Integrable g Œº) :
    Œº[f ‚Ä¢ g|m] =·µê[Œº] f ‚Ä¢ Œº[g|m] :=
  condExp_bilin_of_aestronglyMeasurable_left
    (ContinuousLinearMap.smulRightL ‚Ñù ‚Ñù E (ContinuousLinearMap.id ‚Ñù ‚Ñù)).flip hf hfg hg

/-- Pull-out property of the conditional expectation. -/
theorem condExp_smul_of_aestronglyMeasurable_right [CompleteSpace E] {f : Œ© ‚Üí ‚Ñù} {g : Œ© ‚Üí E}
    (hf : Integrable f Œº) (hfg : Integrable (f ‚Ä¢ g) Œº) (hg : AEStronglyMeasurable[m] g Œº) :
    Œº[f ‚Ä¢ g|m] =·µê[Œº] Œº[f|m] ‚Ä¢ g :=
  condExp_bilin_of_aestronglyMeasurable_left
    (ContinuousLinearMap.smulRightL ‚Ñù ‚Ñù E (ContinuousLinearMap.id ‚Ñù ‚Ñù)) hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_aestronglyMeasurable_left {f g : Œ© ‚Üí ‚Ñù} (hf : AEStronglyMeasurable[m] f Œº)
    (hfg : Integrable (f * g) Œº) (hg : Integrable g Œº) : Œº[f * g|m] =·µê[Œº] f * Œº[g|m] :=
  condExp_bilin_of_aestronglyMeasurable_left (ContinuousLinearMap.mul ‚Ñù ‚Ñù) hf hfg hg

/-- Pull-out property of the conditional expectation. -/
lemma condExp_mul_of_aestronglyMeasurable_right {f g : Œ© ‚Üí ‚Ñù} (hg : AEStronglyMeasurable[m] g Œº)
    (hfg : Integrable (f * g) Œº) (hf : Integrable f Œº) : Œº[f * g | m] =·µê[Œº] Œº[f | m] * g :=
  condExp_bilin_of_aestronglyMeasurable_right (ContinuousLinearMap.mul ‚Ñù ‚Ñù) hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_stronglyMeasurable_left {f g : Œ© ‚Üí ‚Ñù} (hf : StronglyMeasurable[m] f)
    (hfg : Integrable (f * g) Œº) (hg : Integrable g Œº) : Œº[f * g|m] =·µê[Œº] f * Œº[g|m] :=
  condExp_bilin_of_aestronglyMeasurable_left (ContinuousLinearMap.mul ‚Ñù ‚Ñù)
    hf.aestronglyMeasurable hfg hg

/-- Pull-out property of the conditional expectation. -/
lemma condExp_mul_of_stronglyMeasurable_right {f g : Œ© ‚Üí ‚Ñù} (hg : StronglyMeasurable[m] g)
    (hfg : Integrable (f * g) Œº) (hf : Integrable f Œº) : Œº[f * g | m] =·µê[Œº] Œº[f | m] * g :=
  condExp_bilin_of_aestronglyMeasurable_right (ContinuousLinearMap.mul ‚Ñù ‚Ñù)
    hg.aestronglyMeasurable hfg hf

end MeasureTheory
