/-
Copyright (c) 2022 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Indicator
import Mathlib.MeasureTheory.Function.UniformIntegrable
import Mathlib.MeasureTheory.Decomposition.RadonNikodym

#align_import measure_theory.function.conditional_expectation.real from "leanprover-community/mathlib"@"b2ff9a3d7a15fd5b0f060b135421d6a89a999c2f"

/-!

# Conditional expectation of real-valued functions

This file proves some results regarding the conditional expectation of real-valued functions.

## Main results

* `MeasureTheory.rnDeriv_ae_eq_condexp`: the conditional expectation `Î¼[f | m]` is equal to the
  Radon-Nikodym derivative of `fÎ¼` restricted on `m` with respect to `Î¼` restricted on `m`.
* `MeasureTheory.Integrable.uniformIntegrable_condexp`: the conditional expectation of a function
  form a uniformly integrable class.
* `MeasureTheory.condexp_stronglyMeasurable_mul`: the pull-out property of the conditional
  expectation.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Î± : Type*} {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}

theorem rnDeriv_ae_eq_condexp {hm : m â‰¤ m0} [hÎ¼m : SigmaFinite (Î¼.trim hm)] {f : Î± â†’ â„}
    (hf : Integrable f Î¼) :
    SignedMeasure.rnDeriv ((Î¼.withDensityáµ¥ f).trim hm) (Î¼.trim hm) =áµ[Î¼] Î¼[f|m] := by
  refine ae_eq_condexp_of_forall_setIntegral_eq hm hf ?_ ?_ ?_
  Â· exact fun _ _ _ => (integrable_of_integrable_trim hm
      (SignedMeasure.integrable_rnDeriv ((Î¼.withDensityáµ¥ f).trim hm) (Î¼.trim hm))).integrableOn
  Â· intro s hs _
    conv_rhs => rw [â† hf.withDensityáµ¥_trim_eq_integral hm hs,
      â† SignedMeasure.withDensityáµ¥_rnDeriv_eq ((Î¼.withDensityáµ¥ f).trim hm) (Î¼.trim hm)
        (hf.withDensityáµ¥_trim_absolutelyContinuous hm)]
    rw [withDensityáµ¥_apply
      (SignedMeasure.integrable_rnDeriv ((Î¼.withDensityáµ¥ f).trim hm) (Î¼.trim hm)) hs,
      â† setIntegral_trim hm _ hs]
    exact (SignedMeasure.measurable_rnDeriv _ _).stronglyMeasurable
  Â· exact (SignedMeasure.measurable_rnDeriv _ _).stronglyMeasurable.aeStronglyMeasurable'
#align measure_theory.rn_deriv_ae_eq_condexp MeasureTheory.rnDeriv_ae_eq_condexp

-- TODO: the following couple of lemmas should be generalized and proved using Jensen's inequality
-- for the conditional expectation (not in mathlib yet) .
theorem snorm_one_condexp_le_snorm (f : Î± â†’ â„) : snorm (Î¼[f|m]) 1 Î¼ â‰¤ snorm f 1 Î¼ := by
  by_cases hf : Integrable f Î¼
  swap; Â· rw [condexp_undef hf, snorm_zero]; exact zero_le _
  by_cases hm : m â‰¤ m0
  swap; Â· rw [condexp_of_not_le hm, snorm_zero]; exact zero_le _
  by_cases hsig : SigmaFinite (Î¼.trim hm)
  swap; Â· rw [condexp_of_not_sigmaFinite hm hsig, snorm_zero]; exact zero_le _
  calc
    snorm (Î¼[f|m]) 1 Î¼ â‰¤ snorm (Î¼[(|f|)|m]) 1 Î¼ := by
      refine snorm_mono_ae ?_
      filter_upwards [condexp_mono hf hf.abs
        (ae_of_all Î¼ (fun x => le_abs_self (f x) : âˆ€ x, f x â‰¤ |f x|)),
        EventuallyLE.trans (condexp_neg f).symm.le
          (condexp_mono hf.neg hf.abs
          (ae_of_all Î¼ (fun x => neg_le_abs (f x): âˆ€ x, -f x â‰¤ |f x|)))] with x hxâ‚ hxâ‚‚
      exact abs_le_abs hxâ‚ hxâ‚‚
    _ = snorm f 1 Î¼ := by
      rw [snorm_one_eq_lintegral_nnnorm, snorm_one_eq_lintegral_nnnorm, â†
        ENNReal.toReal_eq_toReal (ne_of_lt integrable_condexp.2) (ne_of_lt hf.2), â†
        integral_norm_eq_lintegral_nnnorm
          (stronglyMeasurable_condexp.mono hm).aestronglyMeasurable,
        â† integral_norm_eq_lintegral_nnnorm hf.1]
      simp_rw [Real.norm_eq_abs]
      rw [â† integral_condexp hm hf.abs]
      refine integral_congr_ae ?_
      have : 0 â‰¤áµ[Î¼] Î¼[(|f|)|m] := by
        rw [â† condexp_zero]
        exact condexp_mono (integrable_zero _ _ _) hf.abs
          (ae_of_all Î¼ (fun x => abs_nonneg (f x) : âˆ€ x, 0 â‰¤ |f x|))
      filter_upwards [this] with x hx
      exact abs_eq_self.2 hx
#align measure_theory.snorm_one_condexp_le_snorm MeasureTheory.snorm_one_condexp_le_snorm

theorem integral_abs_condexp_le (f : Î± â†’ â„) : âˆ« x, |(Î¼[f|m]) x| âˆ‚Î¼ â‰¤ âˆ« x, |f x| âˆ‚Î¼ := by
  by_cases hm : m â‰¤ m0
  swap
  Â· simp_rw [condexp_of_not_le hm, Pi.zero_apply, abs_zero, integral_zero]
    positivity
  by_cases hfint : Integrable f Î¼
  swap
  Â· simp only [condexp_undef hfint, Pi.zero_apply, abs_zero, integral_const, Algebra.id.smul_eq_mul,
      mul_zero]
    positivity
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae]
  Â· rw [ENNReal.toReal_le_toReal] <;> simp_rw [â† Real.norm_eq_abs, ofReal_norm_eq_coe_nnnorm]
    Â· rw [â† snorm_one_eq_lintegral_nnnorm, â† snorm_one_eq_lintegral_nnnorm]
      exact snorm_one_condexp_le_snorm _
    Â· exact integrable_condexp.2.ne
    Â· exact hfint.2.ne
  Â· filter_upwards with x using abs_nonneg _
  Â· simp_rw [â† Real.norm_eq_abs]
    exact hfint.1.norm
  Â· filter_upwards with x using abs_nonneg _
  Â· simp_rw [â† Real.norm_eq_abs]
    exact (stronglyMeasurable_condexp.mono hm).aestronglyMeasurable.norm
#align measure_theory.integral_abs_condexp_le MeasureTheory.integral_abs_condexp_le

theorem setIntegral_abs_condexp_le {s : Set Î±} (hs : MeasurableSet[m] s) (f : Î± â†’ â„) :
    âˆ« x in s, |(Î¼[f|m]) x| âˆ‚Î¼ â‰¤ âˆ« x in s, |f x| âˆ‚Î¼ := by
  by_cases hnm : m â‰¤ m0
  swap
  Â· simp_rw [condexp_of_not_le hnm, Pi.zero_apply, abs_zero, integral_zero]
    positivity
  by_cases hfint : Integrable f Î¼
  swap
  Â· simp only [condexp_undef hfint, Pi.zero_apply, abs_zero, integral_const, Algebra.id.smul_eq_mul,
      mul_zero]
    positivity
  have : âˆ« x in s, |(Î¼[f|m]) x| âˆ‚Î¼ = âˆ« x, |(Î¼[s.indicator f|m]) x| âˆ‚Î¼ := by
    rw [â† integral_indicator (hnm _ hs)]
    refine integral_congr_ae ?_
    have : (fun x => |(Î¼[s.indicator f|m]) x|) =áµ[Î¼] fun x => |s.indicator (Î¼[f|m]) x| :=
      (condexp_indicator hfint hs).fun_comp abs
    refine EventuallyEq.trans (eventually_of_forall fun x => ?_) this.symm
    rw [â† Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
    simp only [Real.norm_eq_abs]
  rw [this, â† integral_indicator (hnm _ hs)]
  refine (integral_abs_condexp_le _).trans
    (le_of_eq <| integral_congr_ae <| eventually_of_forall fun x => ?_)
  simp_rw [â† Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
#align measure_theory.set_integral_abs_condexp_le MeasureTheory.setIntegral_abs_condexp_le

@[deprecated (since := "2024-04-17")]
alias set_integral_abs_condexp_le := setIntegral_abs_condexp_le

/-- If the real valued function `f` is bounded almost everywhere by `R`, then so is its conditional
expectation. -/
theorem ae_bdd_condexp_of_ae_bdd {R : â„â‰¥0} {f : Î± â†’ â„} (hbdd : âˆ€áµ x âˆ‚Î¼, |f x| â‰¤ R) :
    âˆ€áµ x âˆ‚Î¼, |(Î¼[f|m]) x| â‰¤ R := by
  by_cases hnm : m â‰¤ m0
  swap
  Â· simp_rw [condexp_of_not_le hnm, Pi.zero_apply, abs_zero]
    exact eventually_of_forall fun _ => R.coe_nonneg
  by_cases hfint : Integrable f Î¼
  swap
  Â· simp_rw [condexp_undef hfint]
    filter_upwards [hbdd] with x hx
    rw [Pi.zero_apply, abs_zero]
    exact (abs_nonneg _).trans hx
  by_contra h
  change Î¼ _ â‰  0 at h
  simp only [â† zero_lt_iff, Set.compl_def, Set.mem_setOf_eq, not_le] at h
  suffices (Î¼ {x | â†‘R < |(Î¼[f|m]) x|}).toReal * â†‘R < (Î¼ {x | â†‘R < |(Î¼[f|m]) x|}).toReal * â†‘R by
    exact this.ne rfl
  refine lt_of_lt_of_le (setIntegral_gt_gt R.coe_nonneg ?_ ?_ h.ne.symm) ?_
  Â· simp_rw [â† Real.norm_eq_abs]
    exact (stronglyMeasurable_condexp.mono hnm).measurable.norm
  Â· exact integrable_condexp.abs.integrableOn
  refine (setIntegral_abs_condexp_le ?_ _).trans ?_
  Â· simp_rw [â† Real.norm_eq_abs]
    exact @measurableSet_lt _ _ _ _ _ m _ _ _ _ _ measurable_const
      stronglyMeasurable_condexp.norm.measurable
  simp only [â† smul_eq_mul, â† setIntegral_const, NNReal.val_eq_coe, RCLike.ofReal_real_eq_id,
    _root_.id]
  refine setIntegral_mono_ae hfint.abs.integrableOn ?_ hbdd
  refine âŸ¨aestronglyMeasurable_const, lt_of_le_of_lt ?_
    (integrable_condexp.integrableOn : IntegrableOn (Î¼[f|m]) {x | â†‘R < |(Î¼[f|m]) x|} Î¼).2âŸ©
  refine set_lintegral_mono measurable_const.nnnorm.coe_nnreal_ennreal
    (stronglyMeasurable_condexp.mono hnm).measurable.nnnorm.coe_nnreal_ennreal fun x hx => ?_
  rw [ENNReal.coe_le_coe, Real.nnnorm_of_nonneg R.coe_nonneg]
  exact Subtype.mk_le_mk.2 (le_of_lt hx)
#align measure_theory.ae_bdd_condexp_of_ae_bdd MeasureTheory.ae_bdd_condexp_of_ae_bdd

/-- Given an integrable function `g`, the conditional expectations of `g` with respect to
a sequence of sub-Ïƒ-algebras is uniformly integrable. -/
theorem Integrable.uniformIntegrable_condexp {Î¹ : Type*} [IsFiniteMeasure Î¼] {g : Î± â†’ â„}
    (hint : Integrable g Î¼) {â„± : Î¹ â†’ MeasurableSpace Î±} (hâ„± : âˆ€ i, â„± i â‰¤ m0) :
    UniformIntegrable (fun i => Î¼[g|â„± i]) 1 Î¼ := by
  let A : MeasurableSpace Î± := m0
  have hmeas : âˆ€ n, âˆ€ C, MeasurableSet {x | C â‰¤ â€–(Î¼[g|â„± n]) xâ€–â‚Š} := fun n C =>
    measurableSet_le measurable_const (stronglyMeasurable_condexp.mono (hâ„± n)).measurable.nnnorm
  have hg : Memâ„’p g 1 Î¼ := memâ„’p_one_iff_integrable.2 hint
  refine uniformIntegrable_of le_rfl ENNReal.one_ne_top
    (fun n => (stronglyMeasurable_condexp.mono (hâ„± n)).aestronglyMeasurable) fun Îµ hÎµ => ?_
  by_cases hne : snorm g 1 Î¼ = 0
  Â· rw [snorm_eq_zero_iff hg.1 one_ne_zero] at hne
    refine âŸ¨0, fun n => (le_of_eq <|
      (snorm_eq_zero_iff ((stronglyMeasurable_condexp.mono (hâ„± n)).aestronglyMeasurable.indicator
        (hmeas n 0)) one_ne_zero).2 ?_).trans (zero_le _)âŸ©
    filter_upwards [condexp_congr_ae (m := â„± n) hne] with x hx
    simp only [zero_le', Set.setOf_true, Set.indicator_univ, Pi.zero_apply, hx, condexp_zero]
  obtain âŸ¨Î´, hÎ´, hâŸ© := hg.snorm_indicator_le le_rfl ENNReal.one_ne_top hÎµ
  set C : â„â‰¥0 := âŸ¨Î´, hÎ´.leâŸ©â»Â¹ * (snorm g 1 Î¼).toNNReal with hC
  have hCpos : 0 < C := mul_pos (inv_pos.2 hÎ´) (ENNReal.toNNReal_pos hne hg.snorm_lt_top.ne)
  have : âˆ€ n, Î¼ {x : Î± | C â‰¤ â€–(Î¼[g|â„± n]) xâ€–â‚Š} â‰¤ ENNReal.ofReal Î´ := by
    intro n
    have := mul_meas_ge_le_pow_snorm' Î¼ one_ne_zero ENNReal.one_ne_top
      ((stronglyMeasurable_condexp (m := â„± n) (Î¼ := Î¼) (f := g)).mono (hâ„± n)).aestronglyMeasurable C
    rw [ENNReal.one_toReal, ENNReal.rpow_one, ENNReal.rpow_one, mul_comm, â†
      ENNReal.le_div_iff_mul_le (Or.inl (ENNReal.coe_ne_zero.2 hCpos.ne.symm))
        (Or.inl ENNReal.coe_lt_top.ne)] at this
    simp_rw [ENNReal.coe_le_coe] at this
    refine this.trans ?_
    rw [ENNReal.div_le_iff_le_mul (Or.inl (ENNReal.coe_ne_zero.2 hCpos.ne.symm))
        (Or.inl ENNReal.coe_lt_top.ne),
      hC, Nonneg.inv_mk, ENNReal.coe_mul, ENNReal.coe_toNNReal hg.snorm_lt_top.ne, â† mul_assoc, â†
      ENNReal.ofReal_eq_coe_nnreal, â† ENNReal.ofReal_mul hÎ´.le, mul_inv_cancel hÎ´.ne.symm,
      ENNReal.ofReal_one, one_mul]
    exact snorm_one_condexp_le_snorm _
  refine âŸ¨C, fun n => le_trans ?_ (h {x : Î± | C â‰¤ â€–(Î¼[g|â„± n]) xâ€–â‚Š} (hmeas n C) (this n))âŸ©
  have hmeasâ„± : MeasurableSet[â„± n] {x : Î± | C â‰¤ â€–(Î¼[g|â„± n]) xâ€–â‚Š} :=
    @measurableSet_le _ _ _ _ _ (â„± n) _ _ _ _ _ measurable_const
      (@Measurable.nnnorm _ _ _ _ _ (â„± n) _ stronglyMeasurable_condexp.measurable)
  rw [â† snorm_congr_ae (condexp_indicator hint hmeasâ„±)]
  exact snorm_one_condexp_le_snorm _
#align measure_theory.integrable.uniform_integrable_condexp MeasureTheory.Integrable.uniformIntegrable_condexp

section PullOut

-- TODO: this section could be generalized beyond multiplication, to any bounded bilinear map.
/-- Auxiliary lemma for `condexp_stronglyMeasurable_mul`. -/
theorem condexp_stronglyMeasurable_simpleFunc_mul (hm : m â‰¤ m0) (f : @SimpleFunc Î± m â„) {g : Î± â†’ â„}
    (hg : Integrable g Î¼) : Î¼[(f * g : Î± â†’ â„)|m] =áµ[Î¼] f * Î¼[g|m] := by
  have : âˆ€ (s c) (f : Î± â†’ â„), Set.indicator s (Function.const Î± c) * f = s.indicator (c â€¢ f) := by
    intro s c f
    ext1 x
    by_cases hx : x âˆˆ s
    Â· simp only [hx, Pi.mul_apply, Set.indicator_of_mem, Pi.smul_apply, Algebra.id.smul_eq_mul,
        Function.const_apply]
    Â· simp only [hx, Pi.mul_apply, Set.indicator_of_not_mem, not_false_iff, zero_mul]
  apply @SimpleFunc.induction _ _ m _ (fun f => _)
    (fun c s hs => ?_) (fun gâ‚ gâ‚‚ _ h_eqâ‚ h_eqâ‚‚ => ?_) f
  Â· -- Porting note: if not classical, `DecidablePred fun x â†¦ x âˆˆ s` cannot be synthesised
    -- for `Set.piecewise_eq_indicator`
    classical simp only [@SimpleFunc.const_zero _ _ m, @SimpleFunc.coe_piecewise _ _ m,
      @SimpleFunc.coe_const _ _ m, @SimpleFunc.coe_zero _ _ m, Set.piecewise_eq_indicator]
    rw [this, this]
    refine (condexp_indicator (hg.smul c) hs).trans ?_
    filter_upwards [condexp_smul (m := m) (m0 := m0) c g] with x hx
    classical simp_rw [Set.indicator_apply, hx]
  Â· have h_add := @SimpleFunc.coe_add _ _ m _ gâ‚ gâ‚‚
    calc
      Î¼[â‡‘(gâ‚ + gâ‚‚) * g|m] =áµ[Î¼] Î¼[(â‡‘gâ‚ + â‡‘gâ‚‚) * g|m] := by
        refine condexp_congr_ae (EventuallyEq.mul ?_ EventuallyEq.rfl); rw [h_add]
      _ =áµ[Î¼] Î¼[â‡‘gâ‚ * g|m] + Î¼[â‡‘gâ‚‚ * g|m] := by
        rw [add_mul]; exact condexp_add (hg.simpleFunc_mul' hm _) (hg.simpleFunc_mul' hm _)
      _ =áµ[Î¼] â‡‘gâ‚ * Î¼[g|m] + â‡‘gâ‚‚ * Î¼[g|m] := EventuallyEq.add h_eqâ‚ h_eqâ‚‚
      _ =áµ[Î¼] â‡‘(gâ‚ + gâ‚‚) * Î¼[g|m] := by rw [h_add, add_mul]
#align measure_theory.condexp_strongly_measurable_simple_func_mul MeasureTheory.condexp_stronglyMeasurable_simpleFunc_mul

theorem condexp_stronglyMeasurable_mul_of_bound (hm : m â‰¤ m0) [IsFiniteMeasure Î¼] {f g : Î± â†’ â„}
    (hf : StronglyMeasurable[m] f) (hg : Integrable g Î¼) (c : â„) (hf_bound : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ c) :
    Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] := by
  let fs := hf.approxBounded c
  have hfs_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fs Â· x) atTop (ğ“ (f x)) :=
    hf.tendsto_approxBounded_ae hf_bound
  by_cases hÎ¼ : Î¼ = 0
  Â· simp only [hÎ¼, ae_zero]; norm_cast
  have : (ae Î¼).NeBot := ae_neBot.2 hÎ¼
  have hc : 0 â‰¤ c := by
    rcases hf_bound.exists with âŸ¨_x, hxâŸ©
    exact (norm_nonneg _).trans hx
  have hfs_bound : âˆ€ n x, â€–fs n xâ€– â‰¤ c := hf.norm_approxBounded_le hc
  have : Î¼[f * Î¼[g|m]|m] = f * Î¼[g|m] := by
    refine condexp_of_stronglyMeasurable hm (hf.mul stronglyMeasurable_condexp) ?_
    exact integrable_condexp.bdd_mul' (hf.mono hm).aestronglyMeasurable hf_bound
  rw [â† this]
  refine tendsto_condexp_unique (fun n x => fs n x * g x) (fun n x => fs n x * (Î¼[g|m]) x) (f * g)
    (f * Î¼[g|m]) ?_ ?_ ?_ ?_ (c * â€–g Â·â€–) ?_ (c * â€–(Î¼[g|m]) Â·â€–) ?_ ?_ ?_ ?_
  Â· exact fun n => hg.bdd_mul' ((SimpleFunc.stronglyMeasurable (fs n)).mono hm).aestronglyMeasurable
      (eventually_of_forall (hfs_bound n))
  Â· exact fun n => integrable_condexp.bdd_mul'
      ((SimpleFunc.stronglyMeasurable (fs n)).mono hm).aestronglyMeasurable
      (eventually_of_forall (hfs_bound n))
  Â· filter_upwards [hfs_tendsto] with x hx
    exact hx.mul tendsto_const_nhds
  Â· filter_upwards [hfs_tendsto] with x hx
    exact hx.mul tendsto_const_nhds
  Â· exact hg.norm.const_mul c
  Â· exact integrable_condexp.norm.const_mul c
  Â· refine fun n => eventually_of_forall fun x => ?_
    exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right (hfs_bound n x) (norm_nonneg _))
  Â· refine fun n => eventually_of_forall fun x => ?_
    exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right (hfs_bound n x) (norm_nonneg _))
  Â· intro n
    simp_rw [â† Pi.mul_apply]
    refine (condexp_stronglyMeasurable_simpleFunc_mul hm _ hg).trans ?_
    rw [condexp_of_stronglyMeasurable hm
      ((SimpleFunc.stronglyMeasurable _).mul stronglyMeasurable_condexp) _]
    exact integrable_condexp.bdd_mul'
      ((SimpleFunc.stronglyMeasurable (fs n)).mono hm).aestronglyMeasurable
      (eventually_of_forall (hfs_bound n))
#align measure_theory.condexp_strongly_measurable_mul_of_bound MeasureTheory.condexp_stronglyMeasurable_mul_of_bound

theorem condexp_stronglyMeasurable_mul_of_boundâ‚€ (hm : m â‰¤ m0) [IsFiniteMeasure Î¼] {f g : Î± â†’ â„}
    (hf : AEStronglyMeasurable' m f Î¼) (hg : Integrable g Î¼) (c : â„)
    (hf_bound : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ c) : Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] := by
  have : Î¼[f * g|m] =áµ[Î¼] Î¼[hf.mk f * g|m] :=
    condexp_congr_ae (EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl)
  refine this.trans ?_
  have : f * Î¼[g|m] =áµ[Î¼] hf.mk f * Î¼[g|m] := EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl
  refine EventuallyEq.trans ?_ this.symm
  refine condexp_stronglyMeasurable_mul_of_bound hm hf.stronglyMeasurable_mk hg c ?_
  filter_upwards [hf_bound, hf.ae_eq_mk] with x hxc hx_eq
  rwa [â† hx_eq]
#align measure_theory.condexp_strongly_measurable_mul_of_boundâ‚€ MeasureTheory.condexp_stronglyMeasurable_mul_of_boundâ‚€

/-- Pull-out property of the conditional expectation. -/
theorem condexp_stronglyMeasurable_mul {f g : Î± â†’ â„} (hf : StronglyMeasurable[m] f)
    (hfg : Integrable (f * g) Î¼) (hg : Integrable g Î¼) : Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] := by
  by_cases hm : m â‰¤ m0; swap; Â· simp_rw [condexp_of_not_le hm]; rw [mul_zero]
  by_cases hÎ¼m : SigmaFinite (Î¼.trim hm)
  swap; Â· simp_rw [condexp_of_not_sigmaFinite hm hÎ¼m]; rw [mul_zero]
  haveI : SigmaFinite (Î¼.trim hm) := hÎ¼m
  obtain âŸ¨sets, sets_prop, h_univâŸ© := hf.exists_spanning_measurableSet_norm_le hm Î¼
  simp_rw [forall_and] at sets_prop
  obtain âŸ¨h_meas, h_finite, h_normâŸ© := sets_prop
  suffices âˆ€ n, âˆ€áµ x âˆ‚Î¼, x âˆˆ sets n â†’ (Î¼[f * g|m]) x = f x * (Î¼[g|m]) x by
    rw [â† ae_all_iff] at this
    filter_upwards [this] with x hx
    obtain âŸ¨i, hiâŸ© : âˆƒ i, x âˆˆ sets i := by
      have h_mem : x âˆˆ â‹ƒ i, sets i := by rw [h_univ]; exact Set.mem_univ _
      simpa using h_mem
    exact hx i hi
  refine fun n => ae_imp_of_ae_restrict ?_
  suffices (Î¼.restrict (sets n))[f * g|m] =áµ[Î¼.restrict (sets n)] f * (Î¼.restrict (sets n))[g|m] by
    refine (condexp_restrict_ae_eq_restrict hm (h_meas n) hfg).symm.trans ?_
    exact this.trans (EventuallyEq.rfl.mul (condexp_restrict_ae_eq_restrict hm (h_meas n) hg))
  suffices (Î¼.restrict (sets n))[(sets n).indicator f * g|m] =áµ[Î¼.restrict (sets n)]
      (sets n).indicator f * (Î¼.restrict (sets n))[g|m] by
    refine EventuallyEq.trans ?_ (this.trans ?_)
    Â· exact
        condexp_congr_ae ((indicator_ae_eq_restrict <| hm _ <| h_meas n).symm.mul EventuallyEq.rfl)
    Â· exact (indicator_ae_eq_restrict <| hm _ <| h_meas n).mul EventuallyEq.rfl
  have : IsFiniteMeasure (Î¼.restrict (sets n)) := by
    constructor
    rw [Measure.restrict_apply_univ]
    exact h_finite n
  refine condexp_stronglyMeasurable_mul_of_bound hm (hf.indicator (h_meas n)) hg.integrableOn n ?_
  filter_upwards with x
  by_cases hxs : x âˆˆ sets n
  Â· simpa only [hxs, Set.indicator_of_mem] using h_norm n x hxs
  Â· simp only [hxs, Set.indicator_of_not_mem, not_false_iff, _root_.norm_zero, Nat.cast_nonneg]
#align measure_theory.condexp_strongly_measurable_mul MeasureTheory.condexp_stronglyMeasurable_mul

/-- Pull-out property of the conditional expectation. -/
theorem condexp_stronglyMeasurable_mulâ‚€ {f g : Î± â†’ â„} (hf : AEStronglyMeasurable' m f Î¼)
    (hfg : Integrable (f * g) Î¼) (hg : Integrable g Î¼) : Î¼[f * g|m] =áµ[Î¼] f * Î¼[g|m] := by
  have : Î¼[f * g|m] =áµ[Î¼] Î¼[hf.mk f * g|m] :=
    condexp_congr_ae (hf.ae_eq_mk.mul EventuallyEq.rfl)
  refine this.trans ?_
  have : f * Î¼[g|m] =áµ[Î¼] hf.mk f * Î¼[g|m] := hf.ae_eq_mk.mul EventuallyEq.rfl
  refine (condexp_stronglyMeasurable_mul hf.stronglyMeasurable_mk ?_ hg).trans this.symm
  refine (integrable_congr ?_).mp hfg
  exact hf.ae_eq_mk.mul EventuallyEq.rfl
#align measure_theory.condexp_strongly_measurable_mulâ‚€ MeasureTheory.condexp_stronglyMeasurable_mulâ‚€

end PullOut

end MeasureTheory
