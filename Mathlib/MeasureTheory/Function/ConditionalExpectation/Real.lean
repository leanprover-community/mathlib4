/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Kexing Ying
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Indicator
import Mathlib.MeasureTheory.Function.UniformIntegrable
import Mathlib.MeasureTheory.Decomposition.RadonNikodym

#align_import measure_theory.function.conditional_expectation.real from "leanprover-community/mathlib"@"b2ff9a3d7a15fd5b0f060b135421d6a89a999c2f"

/-!

# Conditional expectation of real-valued functions

This file proves some results regarding the conditional expectation of real-valued functions.

## Main results

* `MeasureTheory.rnDeriv_ae_eq_condexp`: the conditional expectation `Œº[f | m]` is equal to the
  Radon-Nikodym derivative of `fŒº` restricted on `m` with respect to `Œº` restricted on `m`.
* `MeasureTheory.Integrable.uniformIntegrable_condexp`: the conditional expectation of a function
  form a uniformly integrable class.
* `MeasureTheory.condexp_stronglyMeasurable_mul`: the pull-out property of the conditional
  expectation.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology BigOperators MeasureTheory

namespace MeasureTheory

variable {Œ± : Type*} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

theorem rnDeriv_ae_eq_condexp {hm : m ‚â§ m0} [hŒºm : SigmaFinite (Œº.trim hm)] {f : Œ± ‚Üí ‚Ñù}
    (hf : Integrable f Œº) :
    SignedMeasure.rnDeriv ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm) =·µê[Œº] Œº[f|m] := by
  refine' ae_eq_condexp_of_forall_set_integral_eq hm hf _ _ _
  ¬∑ exact fun _ _ _ => (integrable_of_integrable_trim hm
      (SignedMeasure.integrable_rnDeriv ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm))).integrableOn
  ¬∑ intro s hs _
    conv_rhs => rw [‚Üê hf.withDensity·µ•_trim_eq_integral hm hs,
      ‚Üê SignedMeasure.withDensity·µ•_rnDeriv_eq ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm)
        (hf.withDensity·µ•_trim_absolutelyContinuous hm)]
    rw [withDensity·µ•_apply
      (SignedMeasure.integrable_rnDeriv ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm)) hs,
      ‚Üê set_integral_trim hm _ hs]
    exact (SignedMeasure.measurable_rnDeriv _ _).stronglyMeasurable
  ¬∑ exact StronglyMeasurable.aeStronglyMeasurable'
      (SignedMeasure.measurable_rnDeriv _ _).stronglyMeasurable
#align measure_theory.rn_deriv_ae_eq_condexp MeasureTheory.rnDeriv_ae_eq_condexp

-- TODO: the following couple of lemmas should be generalized and proved using Jensen's inequality
-- for the conditional expectation (not in mathlib yet) .
theorem snorm_one_condexp_le_snorm (f : Œ± ‚Üí ‚Ñù) : snorm (Œº[f|m]) 1 Œº ‚â§ snorm f 1 Œº := by
  by_cases hf : Integrable f Œº
  swap; ¬∑ rw [condexp_undef hf, snorm_zero]; exact zero_le _
  by_cases hm : m ‚â§ m0
  swap; ¬∑ rw [condexp_of_not_le hm, snorm_zero]; exact zero_le _
  by_cases hsig : SigmaFinite (Œº.trim hm)
  swap; ¬∑ rw [condexp_of_not_sigmaFinite hm hsig, snorm_zero]; exact zero_le _
  calc
    snorm (Œº[f|m]) 1 Œº ‚â§ snorm (Œº[(|f|)|m]) 1 Œº := by
      refine' snorm_mono_ae _
      filter_upwards [@condexp_mono _ m m0 _ _ _ _ _ _ _ _ hf hf.abs
        (@ae_of_all _ m0 _ Œº (fun x => le_abs_self (f x) : ‚àÄ x, f x ‚â§ |f x|)),
        EventuallyLE.trans (condexp_neg f).symm.le
          (@condexp_mono _ m m0 _ _ _ _ _ _ _ _ hf.neg hf.abs
          (@ae_of_all _ m0 _ Œº (fun x => neg_le_abs_self (f x): ‚àÄ x, -f x ‚â§ |f x|)))] with x hx‚ÇÅ hx‚ÇÇ
      exact abs_le_abs hx‚ÇÅ hx‚ÇÇ
    _ = snorm f 1 Œº := by
      rw [snorm_one_eq_lintegral_nnnorm, snorm_one_eq_lintegral_nnnorm, ‚Üê
        ENNReal.toReal_eq_toReal (ne_of_lt integrable_condexp.2) (ne_of_lt hf.2), ‚Üê
        integral_norm_eq_lintegral_nnnorm
          (stronglyMeasurable_condexp.mono hm).aestronglyMeasurable,
        ‚Üê integral_norm_eq_lintegral_nnnorm hf.1]
      simp_rw [Real.norm_eq_abs]
      rw [‚Üê @integral_condexp _ _ _ _ _ m m0 Œº _ hm hsig hf.abs]
      refine' integral_congr_ae _
      have : 0 ‚â§·µê[Œº] Œº[(|f|)|m] := by
        rw [‚Üê @condexp_zero Œ± ‚Ñù _ _ _ m m0 Œº]
        exact condexp_mono (integrable_zero _ _ _) hf.abs
          (@ae_of_all _ m0 _ Œº (fun x => abs_nonneg (f x) : ‚àÄ x, 0 ‚â§ |f x|))
      filter_upwards [this] with x hx
      exact abs_eq_self.2 hx
#align measure_theory.snorm_one_condexp_le_snorm MeasureTheory.snorm_one_condexp_le_snorm

theorem integral_abs_condexp_le (f : Œ± ‚Üí ‚Ñù) : ‚à´ x, |(Œº[f|m]) x| ‚àÇŒº ‚â§ ‚à´ x, |f x| ‚àÇŒº := by
  by_cases hm : m ‚â§ m0
  swap
  ¬∑ simp_rw [condexp_of_not_le hm, Pi.zero_apply, abs_zero, integral_zero]
    exact integral_nonneg fun x => abs_nonneg _
  by_cases hfint : Integrable f Œº
  swap
  ¬∑ simp only [condexp_undef hfint, Pi.zero_apply, abs_zero, integral_const, Algebra.id.smul_eq_mul,
      mul_zero]
    exact integral_nonneg fun x => abs_nonneg _
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae]
  ¬∑ rw [ENNReal.toReal_le_toReal] <;> simp_rw [‚Üê Real.norm_eq_abs, ofReal_norm_eq_coe_nnnorm]
    ¬∑ rw [‚Üê snorm_one_eq_lintegral_nnnorm, ‚Üê snorm_one_eq_lintegral_nnnorm]
      exact snorm_one_condexp_le_snorm _
    ¬∑ exact ne_of_lt integrable_condexp.2
    ¬∑ exact ne_of_lt hfint.2
  ¬∑ exact eventually_of_forall fun x => abs_nonneg _
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact hfint.1.norm
  ¬∑ exact eventually_of_forall fun x => abs_nonneg _
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact (stronglyMeasurable_condexp.mono hm).aestronglyMeasurable.norm
#align measure_theory.integral_abs_condexp_le MeasureTheory.integral_abs_condexp_le

theorem set_integral_abs_condexp_le {s : Set Œ±} (hs : MeasurableSet[m] s) (f : Œ± ‚Üí ‚Ñù) :
    ‚à´ x in s, |(Œº[f|m]) x| ‚àÇŒº ‚â§ ‚à´ x in s, |f x| ‚àÇŒº := by
  by_cases hnm : m ‚â§ m0
  swap
  ¬∑ simp_rw [condexp_of_not_le hnm, Pi.zero_apply, abs_zero, integral_zero]
    exact integral_nonneg fun x => abs_nonneg _
  by_cases hfint : Integrable f Œº
  swap
  ¬∑ simp only [condexp_undef hfint, Pi.zero_apply, abs_zero, integral_const, Algebra.id.smul_eq_mul,
      mul_zero]
    exact integral_nonneg fun x => abs_nonneg _
  have : ‚à´ x in s, |(Œº[f|m]) x| ‚àÇŒº = ‚à´ x, |(Œº[s.indicator f|m]) x| ‚àÇŒº := by
    rw [‚Üê integral_indicator]
    swap; ¬∑ exact hnm _ hs
    refine' integral_congr_ae _
    have : (fun x => |(Œº[s.indicator f|m]) x|) =·µê[Œº] fun x => |s.indicator (Œº[f|m]) x| :=
      EventuallyEq.fun_comp (condexp_indicator hfint hs) _
    refine' EventuallyEq.trans (eventually_of_forall fun x => _) this.symm
    rw [‚Üê Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
    rfl
  rw [this, ‚Üê integral_indicator]
  swap; ¬∑ exact hnm _ hs
  refine' (integral_abs_condexp_le _).trans
    (le_of_eq <| integral_congr_ae <| eventually_of_forall fun x => _)
  simp_rw [‚Üê Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
#align measure_theory.set_integral_abs_condexp_le MeasureTheory.set_integral_abs_condexp_le

/-- If the real valued function `f` is bounded almost everywhere by `R`, then so is its conditional
expectation. -/
theorem ae_bdd_condexp_of_ae_bdd {R : ‚Ñù‚â•0} {f : Œ± ‚Üí ‚Ñù} (hbdd : ‚àÄ·µê x ‚àÇŒº, |f x| ‚â§ R) :
    ‚àÄ·µê x ‚àÇŒº, |(Œº[f|m]) x| ‚â§ R := by
  by_cases hnm : m ‚â§ m0
  swap
  ¬∑ simp_rw [condexp_of_not_le hnm, Pi.zero_apply, abs_zero]
    refine' eventually_of_forall fun _ => R.coe_nonneg
  by_cases hfint : Integrable f Œº
  swap
  ¬∑ simp_rw [condexp_undef hfint]
    filter_upwards [hbdd] with x hx
    rw [Pi.zero_apply, abs_zero]
    exact (abs_nonneg _).trans hx
  by_contra h
  change Œº _ ‚â† 0 at h
  simp only [‚Üê zero_lt_iff, Set.compl_def, Set.mem_setOf_eq, not_le] at h
  suffices (Œº {x | ‚ÜëR < |(Œº[f|m]) x|}).toReal * ‚ÜëR < (Œº {x | ‚ÜëR < |(Œº[f|m]) x|}).toReal * ‚ÜëR by
    exact this.ne rfl
  refine' lt_of_lt_of_le (set_integral_gt_gt R.coe_nonneg _ _ h.ne.symm) _
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact (stronglyMeasurable_condexp.mono hnm).measurable.norm
  ¬∑ exact integrable_condexp.abs.integrableOn
  refine' (set_integral_abs_condexp_le _ _).trans _
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact @measurableSet_lt _ _ _ _ _ m _ _ _ _ _ measurable_const
      stronglyMeasurable_condexp.norm.measurable
  simp only [‚Üê smul_eq_mul, ‚Üê set_integral_const, NNReal.val_eq_coe, IsROrC.ofReal_real_eq_id,
    id.def]
  refine' set_integral_mono_ae hfint.abs.integrableOn _ _
  ¬∑ refine' ‚ü®aestronglyMeasurable_const, lt_of_le_of_lt _
      (integrable_condexp.integrableOn : IntegrableOn (Œº[f|m]) {x | ‚ÜëR < |(Œº[f|m]) x|} Œº).2‚ü©
    refine' set_lintegral_mono (Measurable.nnnorm _).coe_nnreal_ennreal
      (stronglyMeasurable_condexp.mono hnm).measurable.nnnorm.coe_nnreal_ennreal fun x hx => _
    ¬∑ exact measurable_const
    ¬∑ rw [ENNReal.coe_le_coe, Real.nnnorm_of_nonneg R.coe_nonneg]
      exact Subtype.mk_le_mk.2 (le_of_lt hx)
  ¬∑ exact hbdd
#align measure_theory.ae_bdd_condexp_of_ae_bdd MeasureTheory.ae_bdd_condexp_of_ae_bdd

/-- Given an integrable function `g`, the conditional expectations of `g` with respect to
a sequence of sub-œÉ-algebras is uniformly integrable. -/
theorem Integrable.uniformIntegrable_condexp {Œπ : Type*} [IsFiniteMeasure Œº] {g : Œ± ‚Üí ‚Ñù}
    (hint : Integrable g Œº) {‚Ñ± : Œπ ‚Üí MeasurableSpace Œ±} (h‚Ñ± : ‚àÄ i, ‚Ñ± i ‚â§ m0) :
    UniformIntegrable (fun i => Œº[g|‚Ñ± i]) 1 Œº := by
  have hmeas : ‚àÄ n, ‚àÄ C, MeasurableSet {x | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} := fun n C =>
    measurableSet_le measurable_const (stronglyMeasurable_condexp.mono (h‚Ñ± n)).measurable.nnnorm
  have hg : Mem‚Ñíp g 1 Œº := mem‚Ñíp_one_iff_integrable.2 hint
  refine' uniformIntegrable_of le_rfl ENNReal.one_ne_top
    (fun n => (stronglyMeasurable_condexp.mono (h‚Ñ± n)).aestronglyMeasurable) fun Œµ hŒµ => _
  by_cases hne : snorm g 1 Œº = 0
  ¬∑ rw [snorm_eq_zero_iff hg.1 one_ne_zero] at hne
    refine' ‚ü®0, fun n => (le_of_eq <|
      (snorm_eq_zero_iff ((stronglyMeasurable_condexp.mono (h‚Ñ± n)).aestronglyMeasurable.indicator
        (hmeas n 0)) one_ne_zero).2 _).trans (zero_le _)‚ü©
    filter_upwards [@condexp_congr_ae _ _ _ _ _ (‚Ñ± n) m0 Œº _ _ hne] with x hx
    simp only [zero_le', Set.setOf_true, Set.indicator_univ, Pi.zero_apply, hx, condexp_zero]
  obtain ‚ü®Œ¥, hŒ¥, h‚ü© := hg.snorm_indicator_le Œº le_rfl ENNReal.one_ne_top hŒµ
  set C : ‚Ñù‚â•0 := ‚ü®Œ¥, hŒ¥.le‚ü©‚Åª¬π * (snorm g 1 Œº).toNNReal with hC
  have hCpos : 0 < C := mul_pos (inv_pos.2 hŒ¥) (ENNReal.toNNReal_pos hne hg.snorm_lt_top.ne)
  have : ‚àÄ n, Œº {x : Œ± | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} ‚â§ ENNReal.ofReal Œ¥ := by
    intro n
    have := mul_meas_ge_le_pow_snorm' Œº one_ne_zero ENNReal.one_ne_top
      ((@stronglyMeasurable_condexp _ _ _ _ _ (‚Ñ± n) _ Œº g).mono (h‚Ñ± n)).aestronglyMeasurable C
    rw [ENNReal.one_toReal, ENNReal.rpow_one, ENNReal.rpow_one, mul_comm, ‚Üê
      ENNReal.le_div_iff_mul_le (Or.inl (ENNReal.coe_ne_zero.2 hCpos.ne.symm))
        (Or.inl ENNReal.coe_lt_top.ne)] at this
    simp_rw [ENNReal.coe_le_coe] at this
    refine' this.trans _
    rw [ENNReal.div_le_iff_le_mul (Or.inl (ENNReal.coe_ne_zero.2 hCpos.ne.symm))
        (Or.inl ENNReal.coe_lt_top.ne),
      hC, Nonneg.inv_mk, ENNReal.coe_mul, ENNReal.coe_toNNReal hg.snorm_lt_top.ne, ‚Üê mul_assoc, ‚Üê
      ENNReal.ofReal_eq_coe_nnreal, ‚Üê ENNReal.ofReal_mul hŒ¥.le, mul_inv_cancel hŒ¥.ne.symm,
      ENNReal.ofReal_one, one_mul]
    exact snorm_one_condexp_le_snorm _
  refine' ‚ü®C, fun n => le_trans _ (h {x : Œ± | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} (hmeas n C) (this n))‚ü©
  have hmeas‚Ñ± : MeasurableSet[‚Ñ± n] {x : Œ± | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} :=
    @measurableSet_le _ _ _ _ _ (‚Ñ± n) _ _ _ _ _ measurable_const
      (@Measurable.nnnorm _ _ _ _ _ (‚Ñ± n) _ stronglyMeasurable_condexp.measurable)
  rw [‚Üê snorm_congr_ae (condexp_indicator hint hmeas‚Ñ±)]
  exact snorm_one_condexp_le_snorm _
#align measure_theory.integrable.uniform_integrable_condexp MeasureTheory.Integrable.uniformIntegrable_condexp

section PullOut

-- TODO: this section could be generalized beyond multiplication, to any bounded bilinear map.
/-- Auxiliary lemma for `condexp_stronglyMeasurable_mul`. -/
theorem condexp_stronglyMeasurable_simpleFunc_mul (hm : m ‚â§ m0) (f : @SimpleFunc Œ± m ‚Ñù) {g : Œ± ‚Üí ‚Ñù}
    (hg : Integrable g Œº) : Œº[(f * g : Œ± ‚Üí ‚Ñù)|m] =·µê[Œº] f * Œº[g|m] := by
  have : ‚àÄ (s c) (f : Œ± ‚Üí ‚Ñù), Set.indicator s (Function.const Œ± c) * f = s.indicator (c ‚Ä¢ f) := by
    intro s c f
    ext1 x
    by_cases hx : x ‚àà s
    ¬∑ simp only [hx, Pi.mul_apply, Set.indicator_of_mem, Pi.smul_apply, Algebra.id.smul_eq_mul]; rfl
    ¬∑ simp only [hx, Pi.mul_apply, Set.indicator_of_not_mem, not_false_iff, zero_mul]
  apply @SimpleFunc.induction _ _ m _ (fun f => _)
    (fun c s hs => ?_) (fun g‚ÇÅ g‚ÇÇ _ h_eq‚ÇÅ h_eq‚ÇÇ => ?_) f
  ¬∑ -- Porting note: if not classical, `DecidablePred fun x ‚Ü¶ x ‚àà s` cannot be synthesised
    -- for `Set.piecewise_eq_indicator`
    classical simp only [@SimpleFunc.const_zero _ _ m, @SimpleFunc.coe_piecewise _ _ m,
      @SimpleFunc.coe_const _ _ m, @SimpleFunc.coe_zero _ _ m, Set.piecewise_eq_indicator]
    rw [this, this]
    refine' (condexp_indicator (hg.smul c) hs).trans _
    filter_upwards [@condexp_smul Œ± ‚Ñù ‚Ñù _ _ _ _ _ m m0 Œº c g] with x hx
    classical simp_rw [Set.indicator_apply, hx]
  ¬∑ have h_add := @SimpleFunc.coe_add _ _ m _ g‚ÇÅ g‚ÇÇ
    calc
      Œº[‚áë(g‚ÇÅ + g‚ÇÇ) * g|m] =·µê[Œº] Œº[(‚áëg‚ÇÅ + ‚áëg‚ÇÇ) * g|m] := by
        refine' condexp_congr_ae (EventuallyEq.mul _ EventuallyEq.rfl); rw [h_add]
      _ =·µê[Œº] Œº[‚áëg‚ÇÅ * g|m] + Œº[‚áëg‚ÇÇ * g|m] := by
        rw [add_mul]; exact condexp_add (hg.simpleFunc_mul' hm _) (hg.simpleFunc_mul' hm _)
      _ =·µê[Œº] ‚áëg‚ÇÅ * Œº[g|m] + ‚áëg‚ÇÇ * Œº[g|m] := (EventuallyEq.add h_eq‚ÇÅ h_eq‚ÇÇ)
      _ =·µê[Œº] ‚áë(g‚ÇÅ + g‚ÇÇ) * Œº[g|m] := by rw [h_add, add_mul]
#align measure_theory.condexp_strongly_measurable_simple_func_mul MeasureTheory.condexp_stronglyMeasurable_simpleFunc_mul

theorem condexp_stronglyMeasurable_mul_of_bound (hm : m ‚â§ m0) [IsFiniteMeasure Œº] {f g : Œ± ‚Üí ‚Ñù}
    (hf : StronglyMeasurable[m] f) (hg : Integrable g Œº) (c : ‚Ñù) (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    Œº[f * g|m] =·µê[Œº] f * Œº[g|m] := by
  let fs := hf.approxBounded c
  have hfs_tendsto : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => fs n x) atTop (ùìù (f x)) :=
    hf.tendsto_approxBounded_ae hf_bound
  by_cases hŒº : Œº = 0
  ¬∑ simp only [hŒº, ae_zero]; norm_cast
  have : Œº.ae.NeBot := by simp only [hŒº, ae_neBot, Ne.def, not_false_iff]
  have hc : 0 ‚â§ c :=
    haveI h_exists : ‚àÉ x, ‚Äñf x‚Äñ ‚â§ c := Eventually.exists hf_bound
    (norm_nonneg _).trans h_exists.choose_spec
  have hfs_bound : ‚àÄ n x, ‚Äñfs n x‚Äñ ‚â§ c := hf.norm_approxBounded_le hc
  have : Œº[f * Œº[g|m]|m] = f * Œº[g|m] := by
    refine' condexp_of_stronglyMeasurable hm (hf.mul stronglyMeasurable_condexp) _
    exact integrable_condexp.bdd_mul' (hf.mono hm).aestronglyMeasurable hf_bound
  rw [‚Üê this]
  refine' tendsto_condexp_unique (fun n x => fs n x * g x) (fun n x => fs n x * (Œº[g|m]) x) (f * g)
    (f * Œº[g|m]) _ _ _ _ (fun x => c * ‚Äñg x‚Äñ) _ (fun x => c * ‚Äñ(Œº[g|m]) x‚Äñ) _ _ _ _
  ¬∑ exact fun n => hg.bdd_mul' ((SimpleFunc.stronglyMeasurable (fs n)).mono hm).aestronglyMeasurable
      (eventually_of_forall (hfs_bound n))
  ¬∑ exact fun n => integrable_condexp.bdd_mul'
      ((SimpleFunc.stronglyMeasurable (fs n)).mono hm).aestronglyMeasurable
      (eventually_of_forall (hfs_bound n))
  ¬∑ filter_upwards [hfs_tendsto] with x hx
    rw [Pi.mul_apply]
    exact Tendsto.mul hx tendsto_const_nhds
  ¬∑ filter_upwards [hfs_tendsto] with x hx
    rw [Pi.mul_apply]
    exact Tendsto.mul hx tendsto_const_nhds
  ¬∑ exact hg.norm.const_mul c
  ¬∑ exact integrable_condexp.norm.const_mul c
  ¬∑ refine' fun n => eventually_of_forall fun x => _
    exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right (hfs_bound n x) (norm_nonneg _))
  ¬∑ refine' fun n => eventually_of_forall fun x => _
    exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right (hfs_bound n x) (norm_nonneg _))
  ¬∑ intro n
    simp_rw [‚Üê Pi.mul_apply]
    refine' (condexp_stronglyMeasurable_simpleFunc_mul hm _ hg).trans _
    rw [condexp_of_stronglyMeasurable hm
      ((SimpleFunc.stronglyMeasurable _).mul stronglyMeasurable_condexp) _]
    exact integrable_condexp.bdd_mul'
      ((SimpleFunc.stronglyMeasurable (fs n)).mono hm).aestronglyMeasurable
      (eventually_of_forall (hfs_bound n))
#align measure_theory.condexp_strongly_measurable_mul_of_bound MeasureTheory.condexp_stronglyMeasurable_mul_of_bound

theorem condexp_stronglyMeasurable_mul_of_bound‚ÇÄ (hm : m ‚â§ m0) [IsFiniteMeasure Œº] {f g : Œ± ‚Üí ‚Ñù}
    (hf : AEStronglyMeasurable' m f Œº) (hg : Integrable g Œº) (c : ‚Ñù)
    (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) : Œº[f * g|m] =·µê[Œº] f * Œº[g|m] := by
  have : Œº[f * g|m] =·µê[Œº] Œº[hf.mk f * g|m] :=
    condexp_congr_ae (EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl)
  refine' this.trans _
  have : f * Œº[g|m] =·µê[Œº] hf.mk f * Œº[g|m] := EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl
  refine' EventuallyEq.trans _ this.symm
  refine' condexp_stronglyMeasurable_mul_of_bound hm hf.stronglyMeasurable_mk hg c _
  filter_upwards [hf_bound, hf.ae_eq_mk] with x hxc hx_eq
  rw [‚Üê hx_eq]
  exact hxc
#align measure_theory.condexp_strongly_measurable_mul_of_bound‚ÇÄ MeasureTheory.condexp_stronglyMeasurable_mul_of_bound‚ÇÄ

/-- Pull-out property of the conditional expectation. -/
theorem condexp_stronglyMeasurable_mul {f g : Œ± ‚Üí ‚Ñù} (hf : StronglyMeasurable[m] f)
    (hfg : Integrable (f * g) Œº) (hg : Integrable g Œº) : Œº[f * g|m] =·µê[Œº] f * Œº[g|m] := by
  by_cases hm : m ‚â§ m0; swap; ¬∑ simp_rw [condexp_of_not_le hm]; rw [mul_zero]
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ simp_rw [condexp_of_not_sigmaFinite hm hŒºm]; rw [mul_zero]
  haveI : SigmaFinite (Œº.trim hm) := hŒºm
  obtain ‚ü®sets, sets_prop, h_univ‚ü© := hf.exists_spanning_measurableSet_norm_le hm Œº
  simp_rw [forall_and] at sets_prop
  obtain ‚ü®h_meas, h_finite, h_norm‚ü© := sets_prop
  suffices ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, x ‚àà sets n ‚Üí (Œº[f * g|m]) x = f x * (Œº[g|m]) x by
    rw [‚Üê ae_all_iff] at this
    filter_upwards [this] with x hx
    rw [Pi.mul_apply]
    obtain ‚ü®i, hi‚ü© : ‚àÉ i, x ‚àà sets i := by
      have h_mem : x ‚àà ‚ãÉ i, sets i := by rw [h_univ]; exact Set.mem_univ _
      simpa using h_mem
    exact hx i hi
  refine' fun n => ae_imp_of_ae_restrict _
  suffices (Œº.restrict (sets n))[f * g|m] =·µê[Œº.restrict (sets n)] f * (Œº.restrict (sets n))[g|m] by
    simp_rw [‚Üê Pi.mul_apply]
    refine' (condexp_restrict_ae_eq_restrict hm (h_meas n) hfg).symm.trans _
    exact this.trans (EventuallyEq.rfl.mul (condexp_restrict_ae_eq_restrict hm (h_meas n) hg))
  suffices (Œº.restrict (sets n))[(sets n).indicator f * g|m] =·µê[Œº.restrict (sets n)]
      (sets n).indicator f * (Œº.restrict (sets n))[g|m] by
    refine' EventuallyEq.trans _ (this.trans _)
    ¬∑ exact
        condexp_congr_ae ((indicator_ae_eq_restrict (hm _ (h_meas n))).symm.mul EventuallyEq.rfl)
    ¬∑ exact (indicator_ae_eq_restrict (hm _ (h_meas n))).mul EventuallyEq.rfl
  have : IsFiniteMeasure (Œº.restrict (sets n)) := by
    constructor
    rw [Measure.restrict_apply_univ]
    exact h_finite n
  refine' condexp_stronglyMeasurable_mul_of_bound hm (hf.indicator (h_meas n)) hg.integrableOn n _
  refine' eventually_of_forall fun x => _
  by_cases hxs : x ‚àà sets n
  ¬∑ simp only [hxs, Set.indicator_of_mem]
    exact h_norm n x hxs
  ¬∑ simp only [hxs, Set.indicator_of_not_mem, not_false_iff, _root_.norm_zero, Nat.cast_nonneg]
#align measure_theory.condexp_strongly_measurable_mul MeasureTheory.condexp_stronglyMeasurable_mul

/-- Pull-out property of the conditional expectation. -/
theorem condexp_stronglyMeasurable_mul‚ÇÄ {f g : Œ± ‚Üí ‚Ñù} (hf : AEStronglyMeasurable' m f Œº)
    (hfg : Integrable (f * g) Œº) (hg : Integrable g Œº) : Œº[f * g|m] =·µê[Œº] f * Œº[g|m] := by
  have : Œº[f * g|m] =·µê[Œº] Œº[hf.mk f * g|m] :=
    condexp_congr_ae (EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl)
  refine' this.trans _
  have : f * Œº[g|m] =·µê[Œº] hf.mk f * Œº[g|m] := EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl
  refine' EventuallyEq.trans _ this.symm
  refine' condexp_stronglyMeasurable_mul hf.stronglyMeasurable_mk _ hg
  refine' (integrable_congr _).mp hfg
  exact EventuallyEq.mul hf.ae_eq_mk EventuallyEq.rfl
#align measure_theory.condexp_strongly_measurable_mul‚ÇÄ MeasureTheory.condexp_stronglyMeasurable_mul‚ÇÄ

end PullOut

end MeasureTheory
