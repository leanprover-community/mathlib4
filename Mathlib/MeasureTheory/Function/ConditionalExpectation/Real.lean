/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Kexing Ying
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Indicator
import Mathlib.MeasureTheory.Function.Holder
import Mathlib.MeasureTheory.Function.UniformIntegrable
import Mathlib.MeasureTheory.VectorMeasure.Decomposition.RadonNikodym

/-!

# Conditional expectation of real-valued functions

This file proves some results regarding the conditional expectation of real-valued functions.

## Main results

* `MeasureTheory.rnDeriv_ae_eq_condExp`: the conditional expectation `Œº[f | m]` is equal to the
  Radon-Nikodym derivative of `fŒº` restricted on `m` with respect to `Œº` restricted on `m`.
* `MeasureTheory.Integrable.uniformIntegrable_condExp`: the conditional expectation of a function
  form a uniformly integrable class.
* `MeasureTheory.condExp_mul_of_stronglyMeasurable_left`: the pull-out property of the conditional
  expectation.

-/


noncomputable section

open TopologicalSpace MeasureTheory.Lp Filter ContinuousLinearMap

open scoped NNReal ENNReal Topology MeasureTheory

namespace MeasureTheory

variable {Œ± : Type*} {m m0 : MeasurableSpace Œ±} {Œº : Measure Œ±}

theorem rnDeriv_ae_eq_condExp {hm : m ‚â§ m0} [hŒºm : SigmaFinite (Œº.trim hm)] {f : Œ± ‚Üí ‚Ñù}
    (hf : Integrable f Œº) :
    SignedMeasure.rnDeriv ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm) =·µê[Œº] Œº[f|m] := by
  refine ae_eq_condExp_of_forall_setIntegral_eq hm hf ?_ ?_ ?_
  ¬∑ exact fun _ _ _ => (integrable_of_integrable_trim hm
      (SignedMeasure.integrable_rnDeriv ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm))).integrableOn
  ¬∑ intro s hs _
    conv_rhs => rw [‚Üê hf.withDensity·µ•_trim_eq_integral hm hs,
      ‚Üê SignedMeasure.withDensity·µ•_rnDeriv_eq ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm)
        (hf.withDensity·µ•_trim_absolutelyContinuous hm)]
    rw [withDensity·µ•_apply
      (SignedMeasure.integrable_rnDeriv ((Œº.withDensity·µ• f).trim hm) (Œº.trim hm)) hs,
      ‚Üê setIntegral_trim hm _ hs]
    exact (SignedMeasure.measurable_rnDeriv _ _).stronglyMeasurable
  ¬∑ exact (SignedMeasure.measurable_rnDeriv _ _).stronglyMeasurable.aestronglyMeasurable

-- TODO: the following couple of lemmas should be generalized and proved using Jensen's inequality
-- for the conditional expectation (not in mathlib yet) .
theorem eLpNorm_one_condExp_le_eLpNorm (f : Œ± ‚Üí ‚Ñù) : eLpNorm (Œº[f|m]) 1 Œº ‚â§ eLpNorm f 1 Œº := by
  by_cases hf : Integrable f Œº
  swap; ¬∑ rw [condExp_of_not_integrable hf, eLpNorm_zero]; exact zero_le _
  by_cases hm : m ‚â§ m0
  swap; ¬∑ rw [condExp_of_not_le hm, eLpNorm_zero]; exact zero_le _
  by_cases hsig : SigmaFinite (Œº.trim hm)
  swap; ¬∑ rw [condExp_of_not_sigmaFinite hm hsig, eLpNorm_zero]; exact zero_le _
  calc
    eLpNorm (Œº[f|m]) 1 Œº ‚â§ eLpNorm (Œº[(|f|)|m]) 1 Œº := by
      refine eLpNorm_mono_ae ?_
      filter_upwards [condExp_mono hf hf.abs
        (ae_of_all Œº (fun x => le_abs_self (f x) : ‚àÄ x, f x ‚â§ |f x|)),
        (condExp_neg ..).symm.le.trans (condExp_mono hf.neg hf.abs
          (ae_of_all Œº (fun x => neg_le_abs (f x) : ‚àÄ x, -f x ‚â§ |f x|)))] with x hx‚ÇÅ hx‚ÇÇ
      exact abs_le_abs hx‚ÇÅ hx‚ÇÇ
    _ = eLpNorm f 1 Œº := by
      rw [eLpNorm_one_eq_lintegral_enorm, eLpNorm_one_eq_lintegral_enorm,
        ‚Üê ENNReal.toReal_eq_toReal_iff' (hasFiniteIntegral_iff_enorm.mp integrable_condExp.2).ne
          (hasFiniteIntegral_iff_enorm.mp hf.2).ne,
        ‚Üê integral_norm_eq_lintegral_enorm
          (stronglyMeasurable_condExp.mono hm).aestronglyMeasurable,
        ‚Üê integral_norm_eq_lintegral_enorm hf.1]
      simp_rw [Real.norm_eq_abs]
      rw (config := {occs := .pos [2]}) [‚Üê integral_condExp hm]
      refine integral_congr_ae ?_
      have : 0 ‚â§·µê[Œº] Œº[(|f|)|m] := by
        rw [‚Üê condExp_zero]
        exact condExp_mono (integrable_zero _ _ _) hf.abs
          (ae_of_all Œº (fun x => abs_nonneg (f x) : ‚àÄ x, 0 ‚â§ |f x|))
      filter_upwards [this] with x hx
      exact abs_eq_self.2 hx

theorem integral_abs_condExp_le (f : Œ± ‚Üí ‚Ñù) : ‚à´ x, |(Œº[f|m]) x| ‚àÇŒº ‚â§ ‚à´ x, |f x| ‚àÇŒº := by
  by_cases hm : m ‚â§ m0
  swap
  ¬∑ simp_rw [condExp_of_not_le hm, Pi.zero_apply, abs_zero, integral_zero]
    positivity
  by_cases hfint : Integrable f Œº
  swap
  ¬∑ simp only [condExp_of_not_integrable hfint, Pi.zero_apply, abs_zero, integral_const,
      Algebra.id.smul_eq_mul, mul_zero]
    positivity
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae]
  ¬∑ apply ENNReal.toReal_mono <;> simp_rw [‚Üê Real.norm_eq_abs, ofReal_norm_eq_enorm]
    ¬∑ exact hfint.2.ne
    ¬∑ rw [‚Üê eLpNorm_one_eq_lintegral_enorm, ‚Üê eLpNorm_one_eq_lintegral_enorm]
      exact eLpNorm_one_condExp_le_eLpNorm _
  ¬∑ filter_upwards with x using abs_nonneg _
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact hfint.1.norm
  ¬∑ filter_upwards with x using abs_nonneg _
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact (stronglyMeasurable_condExp.mono hm).aestronglyMeasurable.norm

theorem setIntegral_abs_condExp_le {s : Set Œ±} (hs : MeasurableSet[m] s) (f : Œ± ‚Üí ‚Ñù) :
    ‚à´ x in s, |(Œº[f|m]) x| ‚àÇŒº ‚â§ ‚à´ x in s, |f x| ‚àÇŒº := by
  by_cases hnm : m ‚â§ m0
  swap
  ¬∑ simp_rw [condExp_of_not_le hnm, Pi.zero_apply, abs_zero, integral_zero]
    positivity
  by_cases hfint : Integrable f Œº
  swap
  ¬∑ simp only [condExp_of_not_integrable hfint, Pi.zero_apply, abs_zero, integral_const,
      Algebra.id.smul_eq_mul, mul_zero]
    positivity
  have : ‚à´ x in s, |(Œº[f|m]) x| ‚àÇŒº = ‚à´ x, |(Œº[s.indicator f|m]) x| ‚àÇŒº := by
    rw [‚Üê integral_indicator (hnm _ hs)]
    refine integral_congr_ae ?_
    have : (fun x => |(Œº[s.indicator f|m]) x|) =·µê[Œº] fun x => |s.indicator (Œº[f|m]) x| :=
      (condExp_indicator hfint hs).fun_comp abs
    refine EventuallyEq.trans (Eventually.of_forall fun x => ?_) this.symm
    rw [‚Üê Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
    simp only [Real.norm_eq_abs]
  rw [this, ‚Üê integral_indicator (hnm _ hs)]
  refine (integral_abs_condExp_le _).trans
    (le_of_eq <| integral_congr_ae <| Eventually.of_forall fun x => ?_)
  simp_rw [‚Üê Real.norm_eq_abs, norm_indicator_eq_indicator_norm]

/-- If the real-valued function `f` is bounded almost everywhere by `R`, then so is its conditional
expectation. -/
theorem ae_bdd_condExp_of_ae_bdd {R : ‚Ñù‚â•0} {f : Œ± ‚Üí ‚Ñù} (hbdd : ‚àÄ·µê x ‚àÇŒº, |f x| ‚â§ R) :
    ‚àÄ·µê x ‚àÇŒº, |(Œº[f|m]) x| ‚â§ R := by
  by_cases hnm : m ‚â§ m0
  swap
  ¬∑ simp_rw [condExp_of_not_le hnm, Pi.zero_apply, abs_zero]
    exact Eventually.of_forall fun _ => R.coe_nonneg
  by_cases hfint : Integrable f Œº
  swap
  ¬∑ simp_rw [condExp_of_not_integrable hfint]
    filter_upwards [hbdd] with x hx
    rw [Pi.zero_apply, abs_zero]
    exact (abs_nonneg _).trans hx
  by_contra h
  change Œº _ ‚â† 0 at h
  simp only [‚Üê zero_lt_iff, Set.compl_def, Set.mem_setOf_eq, not_le] at h
  suffices Œº.real {x | ‚ÜëR < |(Œº[f|m]) x|} * ‚ÜëR < Œº.real {x | ‚ÜëR < |(Œº[f|m]) x|} * ‚ÜëR by
    exact this.ne rfl
  refine lt_of_lt_of_le (setIntegral_gt_gt R.coe_nonneg ?_ h.ne') ?_
  ¬∑ exact integrable_condExp.abs.integrableOn
  refine (setIntegral_abs_condExp_le ?_ _).trans ?_
  ¬∑ simp_rw [‚Üê Real.norm_eq_abs]
    exact @measurableSet_lt _ _ _ _ _ m _ _ _ _ _ measurable_const
      stronglyMeasurable_condExp.norm.measurable
  simp only [‚Üê smul_eq_mul, ‚Üê setIntegral_const]
  refine setIntegral_mono_ae hfint.abs.integrableOn ?_ hbdd
  refine ‚ü®aestronglyMeasurable_const, lt_of_le_of_lt ?_
    (integrable_condExp.integrableOn : IntegrableOn (Œº[f|m]) {x | ‚ÜëR < |(Œº[f|m]) x|} Œº).2‚ü©
  refine setLIntegral_mono
    (stronglyMeasurable_condExp.mono hnm).measurable.nnnorm.coe_nnreal_ennreal fun x hx => ?_
  rw [enorm_eq_nnnorm, enorm_eq_nnnorm, ENNReal.coe_le_coe, Real.nnnorm_of_nonneg R.coe_nonneg]
  exact Subtype.mk_le_mk.2 (le_of_lt hx)

/-- Given an integrable function `g`, the conditional expectations of `g` with respect to
a sequence of sub-œÉ-algebras is uniformly integrable. -/
theorem Integrable.uniformIntegrable_condExp {Œπ : Type*} [IsFiniteMeasure Œº] {g : Œ± ‚Üí ‚Ñù}
    (hint : Integrable g Œº) {‚Ñ± : Œπ ‚Üí MeasurableSpace Œ±} (h‚Ñ± : ‚àÄ i, ‚Ñ± i ‚â§ m0) :
    UniformIntegrable (fun i => Œº[g|‚Ñ± i]) 1 Œº := by
  let A : MeasurableSpace Œ± := m0
  have hmeas : ‚àÄ n, ‚àÄ C, MeasurableSet {x | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} := fun n C =>
    measurableSet_le measurable_const (stronglyMeasurable_condExp.mono (h‚Ñ± n)).measurable.nnnorm
  have hg : MemLp g 1 Œº := memLp_one_iff_integrable.2 hint
  refine uniformIntegrable_of le_rfl ENNReal.one_ne_top
    (fun n => (stronglyMeasurable_condExp.mono (h‚Ñ± n)).aestronglyMeasurable) fun Œµ hŒµ => ?_
  by_cases hne : eLpNorm g 1 Œº = 0
  ¬∑ rw [eLpNorm_eq_zero_iff hg.1 one_ne_zero] at hne
    refine ‚ü®0, fun n => (le_of_eq <|
      (eLpNorm_eq_zero_iff ((stronglyMeasurable_condExp.mono (h‚Ñ± n)).aestronglyMeasurable.indicator
        (hmeas n 0)) one_ne_zero).2 ?_).trans (zero_le _)‚ü©
    filter_upwards [condExp_congr_ae (m := ‚Ñ± n) hne] with x hx
    simp only [zero_le', Set.setOf_true, Set.indicator_univ, Pi.zero_apply, hx, condExp_zero]
  obtain ‚ü®Œ¥, hŒ¥, h‚ü© := hg.eLpNorm_indicator_le le_rfl ENNReal.one_ne_top hŒµ
  set C : ‚Ñù‚â•0 := ‚ü®Œ¥, hŒ¥.le‚ü©‚Åª¬π * (eLpNorm g 1 Œº).toNNReal with hC
  have hCpos : 0 < C := mul_pos (inv_pos.2 hŒ¥) (ENNReal.toNNReal_pos hne hg.eLpNorm_lt_top.ne)
  have : ‚àÄ n, Œº {x : Œ± | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} ‚â§ ENNReal.ofReal Œ¥ := by
    intro n
    have : C ^ ENNReal.toReal 1 * Œº {x | ENNReal.ofNNReal C ‚â§ ‚ÄñŒº[g|‚Ñ± n] x‚Äñ‚Çä} ‚â§
        eLpNorm Œº[g|‚Ñ± n] 1 Œº ^ ENNReal.toReal 1 := by
      rw [ENNReal.toReal_one, ENNReal.rpow_one]
      convert mul_meas_ge_le_pow_eLpNorm Œº one_ne_zero ENNReal.one_ne_top
        (stronglyMeasurable_condExp.mono (h‚Ñ± n)).aestronglyMeasurable C
      ¬∑ rw [ENNReal.toReal_one, ENNReal.rpow_one, enorm_eq_nnnorm]
    rw [ENNReal.toReal_one, ENNReal.rpow_one, mul_comm, ‚Üê
      ENNReal.le_div_iff_mul_le (Or.inl (ENNReal.coe_ne_zero.2 hCpos.ne'))
        (Or.inl ENNReal.coe_lt_top.ne)] at this
    simp_rw [ENNReal.coe_le_coe] at this
    refine this.trans ?_
    rw [ENNReal.div_le_iff_le_mul (Or.inl (ENNReal.coe_ne_zero.2 hCpos.ne'))
        (Or.inl ENNReal.coe_lt_top.ne),
      hC, Nonneg.inv_mk, ENNReal.coe_mul, ENNReal.coe_toNNReal hg.eLpNorm_lt_top.ne, ‚Üê mul_assoc, ‚Üê
      ENNReal.ofReal_eq_coe_nnreal, ‚Üê ENNReal.ofReal_mul hŒ¥.le, mul_inv_cancel‚ÇÄ hŒ¥.ne',
      ENNReal.ofReal_one, one_mul, ENNReal.rpow_one]
    exact eLpNorm_one_condExp_le_eLpNorm _
  refine ‚ü®C, fun n => le_trans ?_ (h {x : Œ± | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} (hmeas n C) (this n))‚ü©
  have hmeas‚Ñ± : MeasurableSet[‚Ñ± n] {x : Œ± | C ‚â§ ‚Äñ(Œº[g|‚Ñ± n]) x‚Äñ‚Çä} :=
    @measurableSet_le _ _ _ _ _ (‚Ñ± n) _ _ _ _ _ measurable_const
      (@Measurable.nnnorm _ _ _ _ _ (‚Ñ± n) _ stronglyMeasurable_condExp.measurable)
  rw [‚Üê eLpNorm_congr_ae (condExp_indicator hint hmeas‚Ñ±)]
  exact eLpNorm_one_condExp_le_eLpNorm _

section PullOut

variable {E F G : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  [CompleteSpace G] (B : F ‚ÜíL[‚Ñù] E ‚ÜíL[‚Ñù] G)

-- TODO: this section could be generalized beyond multiplication, to any bounded bilinear map.
/-- Auxiliary lemma for `condExp_mul_of_stronglyMeasurable_left`. -/
theorem condExp_stronglyMeasurable_simpleFunc_mul (hm : m ‚â§ m0) (f : @SimpleFunc Œ± m F) {g : Œ± ‚Üí E}
    (hg : Integrable g Œº) : Œº[fun a ‚Ü¶ B (f a) (g a)|m] =·µê[Œº] fun a ‚Ü¶ B (f a) (Œº[g|m] a) := by
  have : ‚àÄ (s c) (f : Œ± ‚Üí E),
      (fun x ‚Ü¶ B (Set.indicator s (Function.const Œ± c) x) (f x)) =
        s.indicator (fun a ‚Ü¶ B c (f a)) := by
    intro s c f
    ext x
    by_cases hx : x ‚àà s <;> simp [hx]
  apply @SimpleFunc.induction _ _ m _ (fun f => _)
    (fun c s hs => ?_) (fun g‚ÇÅ g‚ÇÇ _ h_eq‚ÇÅ h_eq‚ÇÇ => ?_) f
  ¬∑ simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
    SimpleFunc.coe_zero, Set.piecewise_eq_indicator]
    rw [this, this]
    refine (condExp_indicator ((B c).integrable_comp hg) hs).trans ?_
    filter_upwards [(B c).comp_condExp_comm hg (m := m)] with x hx
    simp only [Function.comp_apply] at hx
    classical simp_rw [Set.indicator_apply, hx]
    rfl
  ¬∑ have h_add := @SimpleFunc.coe_add _ _ m _ g‚ÇÅ g‚ÇÇ
    calc
      Œº[fun a ‚Ü¶ B (g‚ÇÅ a + g‚ÇÇ a) (g a)|m] =·µê[Œº]
          Œº[fun a ‚Ü¶ B (g‚ÇÅ a) (g a)|m] + Œº[fun a ‚Ü¶ B (g‚ÇÇ a) (g a)|m] := by
        simp_rw [B.map_add]
        exact condExp_add (hg.simpleFunc_bilinearMap' B hm g‚ÇÅ)
          (hg.simpleFunc_bilinearMap' B hm g‚ÇÇ) m
      _ =·µê[Œº] fun a ‚Ü¶ B (g‚ÇÅ a) (Œº[g|m] a) + B (g‚ÇÇ a) (Œº[g|m] a) := EventuallyEq.add h_eq‚ÇÅ h_eq‚ÇÇ
      _ =·µê[Œº] fun a ‚Ü¶ B ((g‚ÇÅ + g‚ÇÇ) a) (Œº[g|m] a) := by simp

theorem condExp_stronglyMeasurable_mul_of_bound (hm : m ‚â§ m0) [IsFiniteMeasure Œº] {f : Œ± ‚Üí F}
    {g : Œ± ‚Üí E}
    (hf : StronglyMeasurable[m] f) (hg : Integrable g Œº) (c : ‚Ñù) (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    Œº[fun a ‚Ü¶ B (f a) (g a)|m] =·µê[Œº] fun a ‚Ü¶ B (f a) (Œº[g|m] a) := by
  let fs := hf.approxBounded c
  have hfs_tendsto : ‚àÄ·µê x ‚àÇŒº, Tendsto (fs ¬∑ x) atTop (ùìù (f x)) :=
    hf.tendsto_approxBounded_ae hf_bound
  by_cases hŒº : Œº = 0
  ¬∑ simp only [hŒº, ae_zero]; norm_cast
  have : (ae Œº).NeBot := ae_neBot.2 hŒº
  have hc : 0 ‚â§ c := by
    rcases hf_bound.exists with ‚ü®_x, hx‚ü©
    exact (norm_nonneg _).trans hx
  have hfs_bound : ‚àÄ n x, ‚Äñfs n x‚Äñ ‚â§ c := hf.norm_approxBounded_le hc
  have : Œº[fun a ‚Ü¶ B (f a) (Œº[g|m] a)|m] = fun a ‚Ü¶ B (f a) (Œº[g|m] a) := by
    refine condExp_of_stronglyMeasurable hm ?_ ?_
    ¬∑ exact Continuous.comp_stronglyMeasurable (g := (fun z : F √ó E ‚Ü¶ B z.1 z.2)) (by fun_prop)
        (hf.prodMk stronglyMeasurable_condExp)
    ¬∑ exact memLp_one_iff_integrable.1 <| B.memLp_of_bilin 1
        (memLp_top_of_bound (hf.aestronglyMeasurable.mono hm) c hf_bound)
        (memLp_one_iff_integrable.2 integrable_condExp)
  rw [‚Üê this]
  refine tendsto_condExp_unique (fun n x => B (fs n x) (g x))
    (fun n x => B (fs n x) (Œº[g|m] x)) (fun x ‚Ü¶ B (f x) (g x))
    (fun x ‚Ü¶ B (f x) (Œº[g|m] x)) ?_ ?_ ?_ ?_ (‚ÄñB‚Äñ * c * ‚Äñg ¬∑‚Äñ) ?_ (‚ÄñB‚Äñ * c * ‚Äñ(Œº[g|m]) ¬∑‚Äñ)
    ?_ ?_ ?_ ?_
  ¬∑ exact fun n ‚Ü¶ memLp_one_iff_integrable.1 <| B.memLp_of_bilin 1
      (memLp_top_of_bound ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable
        c (ae_of_all _ (hfs_bound n))) (memLp_one_iff_integrable.2 hg)
  ¬∑ exact fun n ‚Ü¶ memLp_one_iff_integrable.1 <| B.memLp_of_bilin 1
      (memLp_top_of_bound ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable
        c (ae_of_all _ (hfs_bound n))) (memLp_one_iff_integrable.2 integrable_condExp)
  ¬∑ filter_upwards [hfs_tendsto] with x hx
    exact ((by fun_prop : Continuous (fun y ‚Ü¶ B y (g x))).tendsto (f x)).comp hx
  ¬∑ filter_upwards [hfs_tendsto] with x hx
    exact ((by fun_prop : Continuous (fun y ‚Ü¶ B y (Œº[g|m] x))).tendsto (f x)).comp hx
  ¬∑ exact hg.norm.const_mul _
  ¬∑ fun_prop
  ¬∑ refine fun n => Eventually.of_forall fun x => ?_
    grw [B.le_opNorm‚ÇÇ, hfs_bound]
  ¬∑ refine fun n => Eventually.of_forall fun x => ?_
    grw [B.le_opNorm‚ÇÇ, hfs_bound]
  ¬∑ intro n
    refine (condExp_stronglyMeasurable_simpleFunc_mul B hm _ hg).trans ?_
    nth_rw 2 [condExp_of_stronglyMeasurable hm]
    ¬∑ exact Continuous.comp_stronglyMeasurable (g := (fun z : F √ó E ‚Ü¶ B z.1 z.2)) (by fun_prop)
        ((fs n).stronglyMeasurable.prodMk stronglyMeasurable_condExp)
    exact memLp_one_iff_integrable.1 <| B.memLp_of_bilin 1
      (memLp_top_of_bound ((fs n).stronglyMeasurable.mono hm).aestronglyMeasurable
        c (ae_of_all _ (hfs_bound n)))
      (memLp_one_iff_integrable.2 integrable_condExp)

theorem condExp_stronglyMeasurable_mul_of_bound‚ÇÄ (hm : m ‚â§ m0) [IsFiniteMeasure Œº] {f : Œ± ‚Üí F}
    {g : Œ± ‚Üí E} (hf : AEStronglyMeasurable[m] f Œº) (hg : Integrable g Œº) (c : ‚Ñù)
    (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
    Œº[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := calc
  Œº[fun x ‚Ü¶ B (f x) (g x)|m]
  _ =·µê[Œº] Œº[fun x ‚Ü¶ B (hf.mk f x) (g x)|m] := by
    apply condExp_congr_ae
    filter_upwards [hf.ae_eq_mk] with a ha using by simp [ha]
  _ =·µê[Œº] fun x ‚Ü¶ B (hf.mk f x) (Œº[g|m] x) := by
    refine condExp_stronglyMeasurable_mul_of_bound B hm hf.stronglyMeasurable_mk hg c ?_
    filter_upwards [hf_bound, hf.ae_eq_mk] with x hxc hx_eq
    rwa [‚Üê hx_eq]
  _ =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := by
    filter_upwards [hf.ae_eq_mk] with a ha using by simp [ha]

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_stronglyMeasurable_left {f : Œ± ‚Üí F} {g : Œ± ‚Üí E}
    (hf : StronglyMeasurable[m] f) (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº)
    (hg : Integrable g Œº) : Œº[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := by
  by_cases hm : m ‚â§ m0; swap; ¬∑ exact ae_of_all _ <| by simp [condExp_of_not_le hm]
  by_cases hŒºm : SigmaFinite (Œº.trim hm)
  swap; ¬∑ exact ae_of_all _ <| by simp [condExp_of_not_sigmaFinite hm hŒºm]
  obtain ‚ü®sets, sets_prop, h_univ‚ü© := hf.exists_spanning_measurableSet_norm_le hm Œº
  simp_rw [forall_and] at sets_prop
  obtain ‚ü®h_meas, h_finite, h_norm‚ü© := sets_prop
  suffices ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, x ‚àà sets n ‚Üí (Œº[fun x ‚Ü¶ B (f x) (g x)|m]) x = B (f x) (Œº[g|m] x) by
    rw [‚Üê ae_all_iff] at this
    filter_upwards [this] with x hx
    obtain ‚ü®i, hi‚ü© : ‚àÉ i, x ‚àà sets i := by
      have h_mem : x ‚àà ‚ãÉ i, sets i := by rw [h_univ]; exact Set.mem_univ _
      simpa using h_mem
    exact hx i hi
  refine fun n => ae_imp_of_ae_restrict ?_
  suffices (Œº.restrict (sets n))[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº.restrict (sets n)]
      fun x ‚Ü¶ B (f x) ((Œº.restrict (sets n))[g|m] x) by
    refine (condExp_restrict_ae_eq_restrict hm (h_meas n) hfg).symm.trans ?_
    filter_upwards [this, (condExp_restrict_ae_eq_restrict hm (h_meas n) hg)] with x hx1 hx2
    rw [hx1, hx2]
  suffices (Œº.restrict (sets n))[fun x ‚Ü¶ B ((sets n).indicator f x) (g x)|m] =·µê[Œº.restrict (sets n)]
      fun x ‚Ü¶ B ((sets n).indicator f x) ((Œº.restrict (sets n))[g|m] x) by
    refine EventuallyEq.trans (condExp_congr_ae ?_) (this.trans ?_)
    ¬∑ filter_upwards [indicator_ae_eq_restrict (f := f) <| hm _ <| h_meas n] with x hx
      rw [hx]
    ¬∑ filter_upwards [indicator_ae_eq_restrict (f := f) <| hm _ <| h_meas n] with x hx
      rw [hx]
  have : IsFiniteMeasure (Œº.restrict (sets n)) := by
    constructor
    rw [Measure.restrict_apply_univ]
    exact h_finite n
  refine condExp_stronglyMeasurable_mul_of_bound B hm (hf.indicator (h_meas n)) hg.integrableOn n ?_
  filter_upwards with x
  by_cases hxs : x ‚àà sets n
  ¬∑ simpa only [hxs, Set.indicator_of_mem] using h_norm n x hxs
  ¬∑ simp only [hxs, Set.indicator_of_notMem, not_false_iff, _root_.norm_zero, Nat.cast_nonneg]

omit [CompleteSpace E] in
/-- Pull-out property of the conditional expectation. -/
lemma condExp_mul_of_stronglyMeasurable_right [CompleteSpace F] {f : Œ± ‚Üí F} {g : Œ± ‚Üí E}
    (hg : StronglyMeasurable[m] g)
    (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº) (hf : Integrable f Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x) | m] =·µê[Œº] fun x ‚Ü¶ B (Œº[f | m] x) (g x) := by
  simp_rw [‚Üê B.flip_apply] at hfg ‚ä¢
  exact condExp_mul_of_stronglyMeasurable_left B.flip hg hfg hf

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_aestronglyMeasurable_left {f : Œ± ‚Üí F} {g : Œ± ‚Üí E}
    (hf : AEStronglyMeasurable[m] f Œº)
    (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº) (hg : Integrable g Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x)|m] =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := calc
  Œº[fun x ‚Ü¶ B (f x) (g x)|m]
  _ =·µê[Œº] Œº[fun x ‚Ü¶ B (hf.mk f x) (g x)|m] := by
    apply condExp_congr_ae
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]
  _ =·µê[Œº] fun x ‚Ü¶ B (hf.mk f x) (Œº[g|m] x) := by
    refine condExp_mul_of_stronglyMeasurable_left B hf.stronglyMeasurable_mk
      ((integrable_congr ?_).mp hfg) hg
    filter_upwards [hf.ae_eq_mk] with x hx using by rw [hx]
  _ =·µê[Œº] fun x ‚Ü¶ B (f x) (Œº[g|m] x) := by
    filter_upwards [hf.ae_eq_mk] with a ha using by rw [ha]

omit [CompleteSpace E] in
/-- Pull-out property of the conditional expectation. -/
lemma condExp_mul_of_aestronglyMeasurable_right [CompleteSpace F] {f : Œ± ‚Üí F} {g : Œ± ‚Üí E}
    (hg : AEStronglyMeasurable[m] g Œº)
    (hfg : Integrable (fun x ‚Ü¶ B (f x) (g x)) Œº) (hf : Integrable f Œº) :
    Œº[fun x ‚Ü¶ B (f x) (g x) | m] =·µê[Œº] fun x ‚Ü¶ B (Œº[f | m] x) (g x) := by
  simp_rw [‚Üê B.flip_apply] at hfg ‚ä¢
  exact condExp_mul_of_aestronglyMeasurable_left B.flip hg hfg hf

end PullOut

section PullOut

/-- Pull-out property of the conditional expectation. -/
theorem condExp_mul_of_aestronglyMeasurable_left' {f g : Œ± ‚Üí ‚Ñù} (hf : AEStronglyMeasurable[m] f Œº)
    (hfg : Integrable (f * g) Œº) (hg : Integrable g Œº) : Œº[f * g|m] =·µê[Œº] f * Œº[g|m] :=
  condExp_mul_of_aestronglyMeasurable_left (ContinuousLinearMap.mul ‚Ñù ‚Ñù) hf hfg hg

/-- Pull-out property of the conditional expectation. -/
lemma condExp_mul_of_aestronglyMeasurable_right' {f g : Œ± ‚Üí ‚Ñù} (hg : AEStronglyMeasurable[m] g Œº)
    (hfg : Integrable (f * g) Œº) (hf : Integrable f Œº) : Œº[f * g | m] =·µê[Œº] Œº[f | m] * g :=
  condExp_mul_of_aestronglyMeasurable_right (ContinuousLinearMap.mul ‚Ñù ‚Ñù) hg hfg hf

end PullOut

end MeasureTheory
