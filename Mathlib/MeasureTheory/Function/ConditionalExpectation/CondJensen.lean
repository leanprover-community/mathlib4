/-
Copyright (c) 2025 Yongxi Lin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yongxi Lin, Thomas Zhu
-/
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Data.Int.Star
import Mathlib.Data.Real.StarOrdered
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic
import Mathlib.Topology.Compactness.PseudometrizableLindelof

/-!
# Conditional Jensen's Inequality

This file contains the conditional Jensen's inequality.

## Main Statement

* `conditional_jensen`: the conditional Jensen's inequality: in a Banach space `X` with finite
  measure `Î¼`, if `Ï† : X â†’ â„` is a convex lower-semicontinuous function, then for any `f : Î± â†’ X`
  such that `f` and `Ï† âˆ˜ f` are integrable, we have `Ï† (ğ”¼[f | m]) â‰¤ ğ”¼[Ï† âˆ˜ f | m]`.

## References

* [HytÃ¶nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.][Hytonen_VanNeerven_Veraar_Wies_2016]
-/

open MeasureTheory ProbabilityTheory TopologicalSpace Set Metric ContinuousLinearMap RCLike
open scoped ENNReal

variable {K E : Type*} [RCLike K] {s : Set E} {Ï† : E â†’ â„}

theorem LowerSemicontinuous.isClosed_re_epigraph [TopologicalSpace E]
    (hÏ†_cont : LowerSemicontinuous Ï†) :
    IsClosed  { p : E Ã— K | Ï† p.1 â‰¤ re p.2 } := by
  let A := {(x, (s : EReal)) | Ï† x â‰¤ s}
  have hC : { p : E Ã— K | Ï† p.1 â‰¤ re p.2 }
    = (Prod.map (id: E â†’ E) ((Real.toEReal âˆ˜ re) : K â†’ EReal))â»Â¹' A := by simp [A]
  rw [hC]
  apply IsClosed.preimage
  Â· refine (Continuous.prodMap continuous_id ?_)
    exact continuous_coe_real_ereal.comp reCLM.cont
  Â· have M : Monotone Real.toEReal := by
      intro a b hab
      rw [EReal.coe_le_coe_iff]
      exact hab
    have hÏ† : LowerSemicontinuous (Real.toEReal âˆ˜ Ï†) := Continuous.comp_lowerSemicontinuous
      continuous_coe_real_ereal hÏ†_cont M
    exact LowerSemicontinuous.isClosed_epigraph hÏ†

theorem ConvexOn.convex_re_epigraph [AddCommMonoid E] [Module â„ E] (hÏ†_cvx : ConvexOn â„ s Ï†) :
    Convex â„ { p : E Ã— K | p.1 âˆˆ s âˆ§ Ï† p.1 â‰¤ re p.2 } := by
  have lem : { p : E Ã— K | p.1 âˆˆ s âˆ§ Ï† p.1 â‰¤ re p.2 } = (LinearMap.prodMap
    (LinearMap.id : E â†’â‚—[â„] E) reLm)â»Â¹' { p : E Ã— â„ | p.1 âˆˆ s âˆ§ Ï† p.1 â‰¤ p.2 } := by simp
  rw [lem]
  apply Convex.linear_preimage
  exact ConvexOn.convex_epigraph hÏ†_cvx

variable [NormedAddCommGroup E] [NormedSpace â„ E]

/-- A convex lower-semicontinuous function is the supremum of a sequence of affine functions
in a separable space. -/
theorem ConvexOn.iSup_affine_eq_of_separableSpace (hÏ†_cont : LowerSemicontinuous Ï†) [Module K E]
    [SecondCountableTopology E] [ContinuousSMul K E] (hÏ†_cvx : ConvexOn â„ Set.univ Ï†) :
    âˆƒ (L : â„• â†’ E â†’L[K] K) (c : â„• â†’ â„),
    âˆ€ x, BddAbove (Set.range (fun i â†¦ (re ((L i) x) + c i)))
    âˆ§ (â¨† (i : â„•), re ((L i) x) + c i = Ï† x) := by
  let C :=  {(x, (s : K)) | Ï† x â‰¤ re s}
  have hCâ‚ : Convex â„ C := by simpa using hÏ†_cvx.convex_re_epigraph
  have hCâ‚‚ : IsClosed C := by simpa using hÏ†_cont.isClosed_re_epigraph
  have hCâ‚ƒ : C.Nonempty := by refine (nonempty_of_mem (x := (0, â†‘ (Ï† 0))) ?_); simp [C]
  rcases iInter_nat_halfSpaces_eq_of_prod (ğ•œ := K) hCâ‚ hCâ‚‚ (.of_separableSpace _)
    with âŸ¨L, T, c, hLTc1, hLTc2âŸ©
  have lem1 (i : â„•) (y : K) : T i y = (T i 1) * y := by
    rw [mul_comm, â† smul_eq_mul, â† map_smul (T i) y 1, smul_eq_mul, mul_one]
  have lem2 (x : E) (y : K) (hy : Ï† x â‰¤ re y) (i : â„•) :
    c i â‰¤ re (L i x) + re (T i 1) * (re y) - im (T i 1) * im (y) := by
    have hy2 : (x, y) âˆˆ C := by simpa [C] using hy
    rw [add_sub_assoc, â† mul_re, â† lem1 i]
    simp only [â† hLTc1, mem_iInter, mem_setOf_eq, C] at hy2
    exact (hy2 i)
  have lem3 (i : â„•) : 0 = im (T i 1) := by
    cases @I_eq_zero_or_im_I_eq_one K (by infer_instance) with
    | inl hI0 => rw [â† I_im', hI0]; simp [map_zero, zero_mul]
    | inr hI1 =>
      by_contra ht
      let z : K := (Ï† 0) + I * â†‘((c i - re (T i 1) * (Ï† 0) - 1) / -im (T i 1))
      have rez : re z = Ï† 0 := by
        rw [map_add, ofReal_re, mul_re, I_re, zero_mul, ofReal_im, mul_zero, sub_self, add_zero]
      have imz : im z = (c i - re ((T i) 1) * (Ï† 0) - 1) / -im ((T i) 1) := by
        rw [map_add, ofReal_im, mul_im, I_re, ofReal_re, zero_add]; simp [hI1]
      have lem31 : c i â‰¤ c i - 1 :=
        calc
          c i â‰¤ re (L i 0) + re (T i 1) * (re z) - im (T i 1) * im (z) := by grind
            _ = re (T i 1) * (Ï† 0) - im (T i 1) * ((c i -
                  re (T i 1) * (Ï† 0) - 1) / -im (T i 1)) := by simp [rez, imz]
            _ = c i - 1 := by grind
      exact not_lt_of_ge lem31 (by linarith)
  have lem4 (i : â„•) : 0 < re ((T i) 1) := by
    by_contra! h
    rw [le_iff_eq_or_lt] at h
    cases h with
    | inl h1 =>
      -- Our goal is to show that in this case, the half spaces in hLTc1 are trivial. That is,
      -- re ((L i) x) + re ((T i) y) = 0, which contradicts with hLTc2.
      have lem411 (x : E) : c i â‰¤ re (L i x) := by
        simpa [h1] using (lem2 x (Ï† x) (by simp [ofReal_re]) i)
      have lem412 (y : K) : re (T i y) = 0 := by
        rw [lem1 i, mul_re, h1, â† lem3 i, zero_mul, zero_mul, sub_zero]
      have hCâ‚„ : C â‰  univ := by rw [ne_univ_iff_exists_notMem]; use (0, (Ï† 0 - 1)); simp [C]
      have P1 := hLTc2 hCâ‚ƒ hCâ‚„ i
      simp only [â† not_forall] at P1
      have P2 (x : E) (y : K) : (re âˆ˜ L i) x + (re âˆ˜ T i) y = 0 := by
        -- (re âˆ˜ T i) y = 0 follows from lem412.  Notice that lem411 holds for (n â€¢ x) for any
        -- integer n. Thus, we can prove (re âˆ˜ L i) x = 0 by using a scaling argument.
        have P21 (x : E) : re ((L i) x) = 0 := by
          have ge1 : {n | 1 â‰¤ n} âˆˆ Filter.atTop := by
            simp only [Filter.mem_atTop_sets]; use 1; exact fun _ hb => hb
          apply le_antisymm
          Â· have : âˆ€á¶  (n : â„•) in Filter.atTop, re (L i x) â‰¤ - c i / n := by
              filter_upwards [ge1] with n hn
              calc
                re (L i x)
                  = re ((L i) (((-((n : â„) : K))â»Â¹ * -((n : â„) : K)) â€¢ x)) := by
                  rw (config := {occs := .pos [1]}) [â† (one_smul K x), inv_mul_cancelâ‚€]
                  simp [ne_eq, neg_eq_zero, Nat.cast_eq_zero]; linarith
                _ = (-(n : â„))â»Â¹ * re ((L i) ((-(n : K)) â€¢ x)) := by
                  rw (config := {occs := .pos [1]}) [â† smul_smul, map_smul, smul_eq_mul,
                    â† ofReal_neg, â† ofReal_inv, re_ofReal_mul, ofReal_natCast]
                _ â‰¤ c i / -n := by
                  rw [inv_mul_eq_div, div_le_div_right_of_neg (by simp; linarith)]
                  exact lem411 ((-(n : K) â€¢ x))
                _ = - c i / n := by rw [div_neg, neg_div]
            exact ge_of_tendsto (tendsto_const_div_atTop_nhds_zero_nat (- c i)) this
          Â· have : âˆ€á¶  (n : â„•) in Filter.atTop, c i / n â‰¤ re (L i x) := by
              filter_upwards [ge1] with n hn
              calc
                c i / n
                  â‰¤ re ((L i) ((n : K) â€¢ x)) / n := by
                  rw [div_le_div_iff_of_pos_right (by simp; linarith)]; exact lem411 ((n : K) â€¢ x)
                _ = re ((n : â„) * (L i x)) / n := by rw [map_smul, smul_eq_mul, â† ofReal_natCast]
                _ = n * re (L i x) / n := by rw [re_ofReal_mul]
                _ = re (L i x) := by field
            exact le_of_tendsto (tendsto_const_div_atTop_nhds_zero_nat (c i)) this
        simp [P21, lem412]
      exact P1 P2
    | inr h2 =>
      let m := max ((c i) / re (T i 1) + 1) (Ï† 0)
      have lem421 : Ï† 0 â‰¤ re (@ofReal K (by infer_instance) m) := by simp [m]
      have lem422 : c i â‰¤ re (T i 1) * m := by simpa using (lem2 0 m lem421 i)
      have lem423 : c i < c i := by
        refine lt_of_le_of_lt lem422 ((div_lt_iff_of_neg' h2).mp ?_)
        have : (c i) / re (T i 1) < ((c i) / re (T i 1) + 1) := by linarith
        exact lt_of_lt_of_le this (by simp [m])
      exact lt_irrefl (c i) lem423
  have lem5 (i : â„•) : T i 1 = re (T i 1) := by
    apply Eq.trans (re_add_im ((T i) 1)).symm; simp [â† lem3 i]
  let f := fun (y : E) â†¦ (fun i â†¦ re (( -(T i 1)â»Â¹ â€¢ L i) y) + c i / re (T i 1))
  have hf (y : E) : BddAbove (Set.range (f y)) := by
    have (i : â„•) : f y i â‰¤ Ï† y := by
      have : Ï† y â‰¤ re (@ofReal K (by infer_instance) (Ï† y)) := by simp [ofReal_re]
      have := (lem2 y (Ï† y)) this
      simp only [ofReal_re, ofReal_im, mul_zero, sub_zero] at this
      calc
        f y i = re (( -(T i 1)â»Â¹ â€¢ L i) y) + c i / re (T i 1) := by simp [f]
            _ â‰¤ re (( -(T i 1)â»Â¹ â€¢ L i) y) + (re (L i y) + re (T i 1) * Ï† y) / re (T i 1) := by
              apply add_le_add_left
              rw [div_eq_mul_inv, div_eq_mul_inv]
              apply mul_le_mul_of_nonneg_right (this i) (le_of_lt (inv_pos.mpr (lem4 i)))
            _ = re (( -(T i 1)â»Â¹ â€¢ L i) y) + re (L i y) / re (T i 1)
                + re (T i 1) * Ï† y / re (T i 1) := by rw [add_div, add_assoc]
            _ = re (-(T i 1)â»Â¹ * L i y) + re (L i y) / re (T i 1)
                + re (T i 1) / re (T i 1) * Ï† y := by
              simp [coe_smul', Pi.smul_apply, smul_eq_mul, mul_div_right_comm]
            _ = - (re (L i y) / re (T i 1))  + re (L i y) / re (T i 1) + 1 * Ï† y := by
              rw (config := {occs := .pos [1]}) [lem5 i, â† ofReal_inv, â† ofReal_neg, re_ofReal_mul,
                mul_comm, â† inv_neg, â† div_eq_mul_inv, div_neg, div_self (ne_of_gt (lem4 i))]
            _ = Ï† y := by rw [neg_add_cancel, zero_add, one_mul]
    use Ï† y; intro z hz; rcases mem_range.mp hz with âŸ¨i, hfiâŸ©; rw [â† hfi]; exact this i
  refine âŸ¨fun i â†¦ -(T i 1)â»Â¹ â€¢ (L i), fun i â†¦ c i / re (T i 1), fun x => âŸ¨hf x, ?_ âŸ©âŸ©
  have lem6 (x : E) (s : K) : iSup (f x) â‰¤ re s â†” Ï† x â‰¤ re s := by
    constructor
    Â· intro hxs
      have : (x,s) âˆˆ C := by
        rw [â† hLTc1]
        simp only [mem_iInter, mem_setOf_eq]
        intro i
        have hi (i : â„•) : (f x) i â‰¤ re s := by apply (ciSup_le_iff (hf x)).mp; use hxs
        calc
          c i = c i / re ((T i) 1) * re ((T i) 1) := by field_simp [ne_of_gt (lem4 i)]
            _ = - re ( -(T i 1)â»Â¹ â€¢ L i x) * re (T i 1) + (re ( -(T i 1)â»Â¹ â€¢ L i x)
              + c i / re (T i 1)) * re (T i 1) := by linarith
            _ = re (L i x) +  (re ( -(T i 1)â»Â¹ â€¢ L i x)
              + c i / re (T i 1)) * re (T i 1) := by
              rw (config := {occs := .pos [1]}) [lem5 i]
              rw [smul_eq_mul, â† ofReal_inv, â† ofReal_neg, re_ofReal_mul]
              rw (config := {occs := .pos [2]}) [neg_mul]
              rw [neg_neg, mul_comm (re ((T i) 1))â»Â¹, inv_mul_cancel_rightâ‚€ (ne_of_gt (lem4 i))]
            _ â‰¤ re (L i x) +  re s * re (T i 1) := by
              exact add_le_add_left (mul_le_mul_of_nonneg_right (hi i) (le_of_lt (lem4 i)))
                (re ((L i) x))
            _ = re (L i x) + re (T i s) := by
              rw [lem1 i s]
              rw (config := { occs := .neg [1]}) [lem5 i]
              rw [re_ofReal_mul, mul_comm]
      use this
    Â· refine fun hxs => ciSup_le fun i => ?_
      have : (x,s) âˆˆ C := hxs
      rw [â† hLTc1, mem_iInter] at this
      have := this i
      simp only [mem_setOf_eq] at this
      calc
        re (-(T i 1)â»Â¹ â€¢ L i  x) + c i / re (T i 1)
          = (- re (L i x) + c i) / re (T i 1) := by
          rw (config := {occs := .pos [1]}) [lem5 i]
          simp [-map_invâ‚€, -map_neg, smul_eq_mul, â† ofReal_inv, â† ofReal_neg,
            neg_mul, â† div_eq_inv_mul, â† neg_div, add_div]
        _ â‰¤ re (T i s) / re (T i 1) := by
          apply (div_le_div_iff_of_pos_right (lem4 i)).mpr; linarith
        _ = re s := by
          rw (config := {occs := .pos [1]}) [lem1 i s, lem5 i, re_ofReal_mul, mul_div_right_comm,
            div_eq_mul_inv, mul_inv_cancelâ‚€ (ne_of_gt (lem4 i)), one_mul]
  apply le_antisymm
  Â· rw [â† @ofReal_re K (by infer_instance) (Ï† x)]; apply (lem6 x (Ï† x)).mpr; simp
  Â· rw [â† @ofReal_re K (by infer_instance) (â¨† i, re ((-(T i 1)â»Â¹ â€¢ L i) x) + c i / re (T i 1))]
    apply (lem6 x (ofReal (â¨† i, re ((-(T i 1)â»Â¹ â€¢ L i) x) + c i / re (T i 1)))).mp; simp [f]

variable [CompleteSpace E] {Î± : Type*} {f : Î± â†’ E}
variable {m mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±} [IsFiniteMeasure Î¼]

/-- Conditional Jensen for separable spaces. -/
private lemma conditional_jensen_of_separableSpace [SecondCountableTopology E]
    (hm : m â‰¤ mÎ±) (hÏ†_cvx : ConvexOn â„ Set.univ Ï†) (hÏ†_cont : LowerSemicontinuous Ï†)
    (hf_int : Integrable f Î¼) (hÏ†_int : Integrable (Ï† âˆ˜ f) Î¼) :
    âˆ€áµ a âˆ‚Î¼, Ï† (Î¼[f | m] a) â‰¤ Î¼[Ï† âˆ˜ f | m] a := by
  rcases hÏ†_cvx.iSup_affine_eq_of_separableSpace (K := â„) hÏ†_cont with âŸ¨L, c, hpâŸ©
  have py : âˆ€áµ a âˆ‚Î¼, âˆ€ i : â„•, re (L i (Î¼[f | m] a)) + c i
    = Î¼[re âˆ˜ (L i) âˆ˜ f + (fun (b : Î±) â†¦ (c i)) | m] a := by
    rw [ae_all_iff]; intro i; apply condExp_comm_affine hm hf_int (L i) (c i)
  have pz : âˆ€áµ a âˆ‚Î¼, âˆ€ i : â„•, (re âˆ˜ (L i) âˆ˜ f + (fun (b : Î±) â†¦ (c i))) a â‰¤ (Ï† âˆ˜ f) a := by
    rw [ae_all_iff]; intro i; filter_upwards with a
    rw [Function.comp_apply, â† (hp (f a)).2, Pi.add_apply, Function.comp_apply, Function.comp_apply]
    apply le_ciSup (hp (f a)).1 i
  have pw : âˆ€áµ a âˆ‚Î¼, âˆ€ i : â„•, Î¼[(re âˆ˜ (L i) âˆ˜ f + (fun (b : Î±) â†¦ (c i))) | m] a
    â‰¤ Î¼[Ï† âˆ˜ f | m] a := by
    rw [ae_all_iff]; intro i; apply condExp_mono
    Â· let g := @reCLM â„ (by infer_instance)
      have reLif_int : Integrable (fun (a : Î±) â†¦ (re âˆ˜ (L i)) (f a)) Î¼
        := integrable_comp (comp g (L i)) hf_int
      exact Integrable.add reLif_int (integrable_const (c i))
    Â· exact hÏ†_int
    Â· exact ae_all_iff.mp pz i
  filter_upwards [py, pw] with a
  intro hy hw
  rw [â† (hp (Î¼[f | m] a)).right]
  apply ciSup_le
  intro i
  rw [hy i]
  apply hw i

/-- Conditional Jensen's inequality.
# TODO

Generalize this theorem to Ïƒ-finite measures.
-/
theorem conditional_jensen (hm : m â‰¤ mÎ±)
    (hÏ†_cvx : ConvexOn â„ Set.univ Ï†) (hÏ†_cont : LowerSemicontinuous Ï†)
    (hf_int : Integrable f Î¼) (hÏ†_int : Integrable (Ï† âˆ˜ f) Î¼) :
    Ï† âˆ˜ Î¼[f | m] â‰¤áµ[Î¼] Î¼[Ï† âˆ˜ f | m] := by
  classical
  borelize E
  have sep := hf_int.aestronglyMeasurable.isSeparable_ae_range
  rcases sep with âŸ¨t, ht, httâŸ©
  let Y := (Submodule.span â„ t).topologicalClosure
  have : CompleteSpace Y := (Submodule.isClosed_topologicalClosure _).completeSpace_coe
  have : SecondCountableTopology Y := ht.span.closure.secondCountableTopology
  let Ï†Y := Ï† âˆ˜ Y.subtypeL
  have hÏ†Y_cvx : ConvexOn â„ Set.univ Ï†Y := hÏ†_cvx.comp_linearMap Y.subtype
  have hÏ†Y_cont : LowerSemicontinuous Ï†Y := hÏ†_cont.comp_continuous Y.subtypeL.cont
  have tsubY : t âŠ† Y := subset_trans Submodule.subset_span subset_closure
  have aeinY : âˆ€áµ (x : Î±) âˆ‚Î¼, f x âˆˆ Y := by filter_upwards [htt] with a ha; exact tsubY ha
  let fY : Î± â†’ Y := fun a => if h : f a âˆˆ Y then âŸ¨f a, hâŸ© else 0
  let fX : Î± â†’ E := Y.subtypeL âˆ˜ fY
  have lem1 : f =áµ[Î¼] fX := by filter_upwards [aeinY] with a ha; simp [fX, fY, ha, reduceDIte]
  have hfX_int : Integrable fX Î¼ := Integrable.congr hf_int lem1
  have hfY_int : Integrable fY Î¼ := by
    constructor
    Â· have hs : MeasurableSet (Y : Set E) :=
        (Submodule.isClosed_topologicalClosure _).measurableSet
      have h_nonempty : (Y : Set E).Nonempty := Set.Nonempty.of_subtype
      obtain âŸ¨g, hg1, hg2 : âˆ€ x, g x âˆˆ Y, hg3âŸ© :=
        hf_int.1.exists_stronglyMeasurable_range_subset hs h_nonempty aeinY
      use codRestrict g Y hg2
      constructor
      Â· rw [stronglyMeasurable_iff_measurable]
        exact hg1.measurable.codRestrict hg2
      Â· filter_upwards [hg3] with a ha1
        simp [fY, ha1, Set.codRestrict, dif_pos (hg2 a)]
    Â· apply hfX_int.2.mono
      simp [fX, Function.comp_apply, le_refl, Filter.eventually_true]
  have lem3 : Î¼[f | m] =áµ[Î¼] Y.subtypeL âˆ˜ Î¼[fY | m] :=
    calc
      Î¼[f | m] =áµ[Î¼] Î¼[fX | m] := condExp_congr_ae lem1
      _        =áµ[Î¼] Y.subtypeL âˆ˜ Î¼[fY | m] :=
        (condExp_comm_continuousLinearMap hm hfY_int Y.subtypeL).symm
  have lem2 : Ï† âˆ˜ f =áµ[Î¼] Ï†Y âˆ˜ fY := by filter_upwards [lem1] with a ha; simp [Ï†Y, ha, fX]
  have hÏ†YfY_int : Integrable (Ï†Y âˆ˜ fY) Î¼ := hÏ†_int.congr lem2
  calc
    Ï† âˆ˜ Î¼[f | m]
      =áµ[Î¼] Ï†Y âˆ˜ Î¼[fY | m] := by filter_upwards [lem3] with a ha; simp [Ï†Y, ha]
    _ â‰¤áµ[Î¼] Î¼[Ï†Y âˆ˜ fY | m] :=
      conditional_jensen_of_separableSpace hm hÏ†Y_cvx hÏ†Y_cont hfY_int hÏ†YfY_int
    _ =áµ[Î¼] Î¼[Ï† âˆ˜ f | m] := condExp_congr_ae lem2.symm
