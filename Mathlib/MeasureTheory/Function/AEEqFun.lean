/-
Copyright (c) 2019 Johannes H√∂lzl, Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Zhouhang Zhou
-/
import Mathlib.MeasureTheory.Integral.Lebesgue
import Mathlib.Order.Filter.Germ
import Mathlib.Topology.ContinuousFunction.Algebra
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic

#align_import measure_theory.function.ae_eq_fun from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!

# Almost everywhere equal functions

We build a space of equivalence classes of functions, where two functions are treated as identical
if they are almost everywhere equal. We form the set of equivalence classes under the relation of
being almost everywhere equal, which is sometimes known as the `L‚Å∞` space.
To use this space as a basis for the `L^p` spaces and for the Bochner integral, we consider
equivalence classes of strongly measurable functions (or, equivalently, of almost everywhere
strongly measurable functions.)

See `L1Space.lean` for `L¬π` space.

## Notation

* `Œ± ‚Üí‚Çò[Œº] Œ≤` is the type of `L‚Å∞` space, where `Œ±` is a measurable space, `Œ≤` is a topological
  space, and `Œº` is a measure on `Œ±`. `f : Œ± ‚Üí‚Çò Œ≤` is a "function" in `L‚Å∞`.
  In comments, `[f]` is also used to denote an `L‚Å∞` function.

  `‚Çò` can be typed as `\_m`. Sometimes it is shown as a box if font is missing.

## Main statements

* The linear structure of `L‚Å∞` :
    Addition and scalar multiplication are defined on `L‚Å∞` in the natural way, i.e.,
    `[f] + [g] := [f + g]`, `c ‚Ä¢ [f] := [c ‚Ä¢ f]`. So defined, `Œ± ‚Üí‚Çò Œ≤` inherits the linear structure
    of `Œ≤`. For example, if `Œ≤` is a module, then `Œ± ‚Üí‚Çò Œ≤` is a module over the same ring.

    See `mk_add_mk`,  `neg_mk`,     `mk_sub_mk`,  `smul_mk`,
        `add_toFun`, `neg_toFun`, `sub_toFun`, `smul_toFun`

* The order structure of `L‚Å∞` :
    `‚â§` can be defined in a similar way: `[f] ‚â§ [g]` if `f a ‚â§ g a` for almost all `a` in domain.
    And `Œ± ‚Üí‚Çò Œ≤` inherits the preorder and partial order of `Œ≤`.

    TODO: Define `sup` and `inf` on `L‚Å∞` so that it forms a lattice. It seems that `Œ≤` must be a
    linear order, since otherwise `f ‚äî g` may not be a measurable function.

## Implementation notes

* `f.toFun`      : To find a representative of `f : Œ± ‚Üí‚Çò Œ≤`, use the coercion `(f : Œ± ‚Üí Œ≤)`, which
                 is implemented as `f.toFun`.
                 For each operation `op` in `L‚Å∞`, there is a lemma called `coe_fn_op`,
                 characterizing, say, `(f op g : Œ± ‚Üí Œ≤)`.
* `ae_eq_fun.mk` : To constructs an `L‚Å∞` function `Œ± ‚Üí‚Çò Œ≤` from an almost everywhere strongly
                 measurable function `f : Œ± ‚Üí Œ≤`, use `ae_eq_fun.mk`
* `comp`         : Use `comp g f` to get `[g ‚àò f]` from `g : Œ≤ ‚Üí Œ≥` and `[f] : Œ± ‚Üí‚Çò Œ≥` when `g` is
                 continuous. Use `comp_measurable` if `g` is only measurable (this requires the
                 target space to be second countable).
* `comp‚ÇÇ`        : Use `comp‚ÇÇ g f‚ÇÅ f‚ÇÇ` to get `[fun a ‚Ü¶ g (f‚ÇÅ a) (f‚ÇÇ a)]`.
                 For example, `[f + g]` is `comp‚ÇÇ (+)`


## Tags

function space, almost everywhere equal, `L‚Å∞`, ae_eq_fun

-/

noncomputable section

open scoped Classical
open ENNReal Topology

open Set Filter TopologicalSpace ENNReal EMetric MeasureTheory Function

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*} [MeasurableSpace Œ±] {Œº ŒΩ : Measure Œ±}

namespace MeasureTheory

section MeasurableSpace

variable [TopologicalSpace Œ≤]
variable (Œ≤)

/-- The equivalence relation of being almost everywhere equal for almost everywhere strongly
measurable functions. -/
def Measure.aeEqSetoid (Œº : Measure Œ±) : Setoid { f : Œ± ‚Üí Œ≤ // AEStronglyMeasurable f Œº } :=
  ‚ü®fun f g => (f : Œ± ‚Üí Œ≤) =·µê[Œº] g, fun {f} => ae_eq_refl f.val, fun {_ _} => ae_eq_symm,
    fun {_ _ _} => ae_eq_trans‚ü©
#align measure_theory.measure.ae_eq_setoid MeasureTheory.Measure.aeEqSetoid

variable (Œ±)

/-- The space of equivalence classes of almost everywhere strongly measurable functions, where two
    strongly measurable functions are equivalent if they agree almost everywhere, i.e.,
    they differ on a set of measure `0`.  -/
def AEEqFun (Œº : Measure Œ±) : Type _ :=
  Quotient (Œº.aeEqSetoid Œ≤)
#align measure_theory.ae_eq_fun MeasureTheory.AEEqFun

variable {Œ± Œ≤}

@[inherit_doc MeasureTheory.AEEqFun]
notation:25 Œ± " ‚Üí‚Çò[" Œº "] " Œ≤ => AEEqFun Œ± Œ≤ Œº

end MeasurableSpace

namespace AEEqFun

variable [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [TopologicalSpace Œ¥]

/-- Construct the equivalence class `[f]` of an almost everywhere measurable function `f`, based
    on the equivalence relation of being almost everywhere equal. -/
def mk {Œ≤ : Type*} [TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) (hf : AEStronglyMeasurable f Œº) : Œ± ‚Üí‚Çò[Œº] Œ≤ :=
  Quotient.mk'' ‚ü®f, hf‚ü©
#align measure_theory.ae_eq_fun.mk MeasureTheory.AEEqFun.mk

/-- Coercion from a space of equivalence classes of almost everywhere strongly measurable
functions to functions. -/
@[coe]
def cast (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Œ± ‚Üí Œ≤ :=
  AEStronglyMeasurable.mk _ (Quotient.out' f : { f : Œ± ‚Üí Œ≤ // AEStronglyMeasurable f Œº }).2

/-- A measurable representative of an `AEEqFun` [f] -/
instance instCoeFun : CoeFun (Œ± ‚Üí‚Çò[Œº] Œ≤) fun _ => Œ± ‚Üí Œ≤ := ‚ü®cast‚ü©
#align measure_theory.ae_eq_fun.has_coe_to_fun MeasureTheory.AEEqFun.instCoeFun

protected theorem stronglyMeasurable (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : StronglyMeasurable f :=
  AEStronglyMeasurable.stronglyMeasurable_mk _
#align measure_theory.ae_eq_fun.strongly_measurable MeasureTheory.AEEqFun.stronglyMeasurable

protected theorem aestronglyMeasurable (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : AEStronglyMeasurable f Œº :=
  f.stronglyMeasurable.aestronglyMeasurable
#align measure_theory.ae_eq_fun.ae_strongly_measurable MeasureTheory.AEEqFun.aestronglyMeasurable

protected theorem measurable [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤]
    (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Measurable f :=
  AEStronglyMeasurable.measurable_mk _
#align measure_theory.ae_eq_fun.measurable MeasureTheory.AEEqFun.measurable

protected theorem aemeasurable [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤]
    (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : AEMeasurable f Œº :=
  f.measurable.aemeasurable
#align measure_theory.ae_eq_fun.ae_measurable MeasureTheory.AEEqFun.aemeasurable

@[simp]
theorem quot_mk_eq_mk (f : Œ± ‚Üí Œ≤) (hf) :
    (Quot.mk (@Setoid.r _ <| Œº.aeEqSetoid Œ≤) ‚ü®f, hf‚ü© : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk f hf :=
  rfl
#align measure_theory.ae_eq_fun.quot_mk_eq_mk MeasureTheory.AEEqFun.quot_mk_eq_mk

@[simp]
theorem mk_eq_mk {f g : Œ± ‚Üí Œ≤} {hf hg} : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk g hg ‚Üî f =·µê[Œº] g :=
  Quotient.eq''
#align measure_theory.ae_eq_fun.mk_eq_mk MeasureTheory.AEEqFun.mk_eq_mk

@[simp]
theorem mk_coeFn (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : mk f f.aestronglyMeasurable = f := by
  conv_rhs => rw [‚Üê Quotient.out_eq' f]
  set g : { f : Œ± ‚Üí Œ≤ // AEStronglyMeasurable f Œº } := Quotient.out' f
  have : g = ‚ü®g.1, g.2‚ü© := Subtype.eq rfl
  rw [this, ‚Üê mk, mk_eq_mk]
  exact (AEStronglyMeasurable.ae_eq_mk _).symm
#align measure_theory.ae_eq_fun.mk_coe_fn MeasureTheory.AEEqFun.mk_coeFn

@[ext]
theorem ext {f g : Œ± ‚Üí‚Çò[Œº] Œ≤} (h : f =·µê[Œº] g) : f = g := by
  rwa [‚Üê f.mk_coeFn, ‚Üê g.mk_coeFn, mk_eq_mk]
#align measure_theory.ae_eq_fun.ext MeasureTheory.AEEqFun.ext

theorem ext_iff {f g : Œ± ‚Üí‚Çò[Œº] Œ≤} : f = g ‚Üî f =·µê[Œº] g :=
  ‚ü®fun h => by rw [h], fun h => ext h‚ü©
#align measure_theory.ae_eq_fun.ext_iff MeasureTheory.AEEqFun.ext_iff

theorem coeFn_mk (f : Œ± ‚Üí Œ≤) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) =·µê[Œº] f := by
  apply (AEStronglyMeasurable.ae_eq_mk _).symm.trans
  exact @Quotient.mk_out' _ (Œº.aeEqSetoid Œ≤) (‚ü®f, hf‚ü© : { f // AEStronglyMeasurable f Œº })
#align measure_theory.ae_eq_fun.coe_fn_mk MeasureTheory.AEEqFun.coeFn_mk

@[elab_as_elim]
theorem induction_on (f : Œ± ‚Üí‚Çò[Œº] Œ≤) {p : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí Prop} (H : ‚àÄ f hf, p (mk f hf)) : p f :=
  Quotient.inductionOn' f <| Subtype.forall.2 H
#align measure_theory.ae_eq_fun.induction_on MeasureTheory.AEEqFun.induction_on

@[elab_as_elim]
theorem induction_on‚ÇÇ {Œ±' Œ≤' : Type*} [MeasurableSpace Œ±'] [TopologicalSpace Œ≤'] {Œº' : Measure Œ±'}
    (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (f' : Œ±' ‚Üí‚Çò[Œº'] Œ≤') {p : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí (Œ±' ‚Üí‚Çò[Œº'] Œ≤') ‚Üí Prop}
    (H : ‚àÄ f hf f' hf', p (mk f hf) (mk f' hf')) : p f f' :=
  induction_on f fun f hf => induction_on f' <| H f hf
#align measure_theory.ae_eq_fun.induction_on‚ÇÇ MeasureTheory.AEEqFun.induction_on‚ÇÇ

@[elab_as_elim]
theorem induction_on‚ÇÉ {Œ±' Œ≤' : Type*} [MeasurableSpace Œ±'] [TopologicalSpace Œ≤'] {Œº' : Measure Œ±'}
    {Œ±'' Œ≤'' : Type*} [MeasurableSpace Œ±''] [TopologicalSpace Œ≤''] {Œº'' : Measure Œ±''}
    (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (f' : Œ±' ‚Üí‚Çò[Œº'] Œ≤') (f'' : Œ±'' ‚Üí‚Çò[Œº''] Œ≤'')
    {p : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí (Œ±' ‚Üí‚Çò[Œº'] Œ≤') ‚Üí (Œ±'' ‚Üí‚Çò[Œº''] Œ≤'') ‚Üí Prop}
    (H : ‚àÄ f hf f' hf' f'' hf'', p (mk f hf) (mk f' hf') (mk f'' hf'')) : p f f' f'' :=
  induction_on f fun f hf => induction_on‚ÇÇ f' f'' <| H f hf
#align measure_theory.ae_eq_fun.induction_on‚ÇÉ MeasureTheory.AEEqFun.induction_on‚ÇÉ

/-!
### Composition of an a.e. equal function with a (quasi) measure preserving function
-/

section compQuasiMeasurePreserving

variable [MeasurableSpace Œ≤] {ŒΩ : MeasureTheory.Measure Œ≤} {f : Œ± ‚Üí Œ≤}

open MeasureTheory.Measure (QuasiMeasurePreserving)

/-- Composition of an almost everywhere equal function and a quasi measure preserving function.

See also `AEEqFun.compMeasurePreserving`. -/
def compQuasiMeasurePreserving (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (f : Œ± ‚Üí Œ≤) (hf : QuasiMeasurePreserving f Œº ŒΩ) :
    Œ± ‚Üí‚Çò[Œº] Œ≥ :=
  Quotient.liftOn' g (fun g ‚Ü¶ mk (g ‚àò f) <| g.2.comp_quasiMeasurePreserving hf) fun _ _ h ‚Ü¶
    mk_eq_mk.2 <| h.comp_tendsto hf.tendsto_ae

@[simp]
theorem compQuasiMeasurePreserving_mk {g : Œ≤ ‚Üí Œ≥} (hg : AEStronglyMeasurable g ŒΩ)
    (hf : QuasiMeasurePreserving f Œº ŒΩ) :
    (mk g hg).compQuasiMeasurePreserving f hf = mk (g ‚àò f) (hg.comp_quasiMeasurePreserving hf) :=
  rfl

theorem compQuasiMeasurePreserving_eq_mk (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (hf : QuasiMeasurePreserving f Œº ŒΩ) :
    g.compQuasiMeasurePreserving f hf =
      mk (g ‚àò f) (g.aestronglyMeasurable.comp_quasiMeasurePreserving hf) := by
  rw [‚Üê compQuasiMeasurePreserving_mk g.aestronglyMeasurable hf, mk_coeFn]

theorem coeFn_compQuasiMeasurePreserving (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (hf : QuasiMeasurePreserving f Œº ŒΩ) :
    g.compQuasiMeasurePreserving f hf =·µê[Œº] g ‚àò f := by
  rw [compQuasiMeasurePreserving_eq_mk]
  apply coeFn_mk

end compQuasiMeasurePreserving

section compMeasurePreserving

variable [MeasurableSpace Œ≤] {ŒΩ : MeasureTheory.Measure Œ≤} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥}

/-- Composition of an almost everywhere equal function and a quasi measure preserving function.

This is an important special case of `AEEqFun.compQuasiMeasurePreserving`. We use a separate
definition so that lemmas that need `f` to be measure preserving can be `@[simp]` lemmas.  -/
def compMeasurePreserving (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (f : Œ± ‚Üí Œ≤) (hf : MeasurePreserving f Œº ŒΩ) : Œ± ‚Üí‚Çò[Œº] Œ≥ :=
  g.compQuasiMeasurePreserving f hf.quasiMeasurePreserving

@[simp]
theorem compMeasurePreserving_mk (hg : AEStronglyMeasurable g ŒΩ) (hf : MeasurePreserving f Œº ŒΩ) :
    (mk g hg).compMeasurePreserving f hf =
      mk (g ‚àò f) (hg.comp_quasiMeasurePreserving hf.quasiMeasurePreserving) :=
  rfl

theorem compMeasurePreserving_eq_mk (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (hf : MeasurePreserving f Œº ŒΩ) :
    g.compMeasurePreserving f hf =
      mk (g ‚àò f) (g.aestronglyMeasurable.comp_quasiMeasurePreserving hf.quasiMeasurePreserving) :=
  g.compQuasiMeasurePreserving_eq_mk _

theorem coeFn_compMeasurePreserving (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (hf : MeasurePreserving f Œº ŒΩ) :
    g.compMeasurePreserving f hf =·µê[Œº] g ‚àò f :=
  g.coeFn_compQuasiMeasurePreserving _

end compMeasurePreserving

/-- Given a continuous function `g : Œ≤ ‚Üí Œ≥`, and an almost everywhere equal function `[f] : Œ± ‚Üí‚Çò Œ≤`,
    return the equivalence class of `g ‚àò f`, i.e., the almost everywhere equal function
    `[g ‚àò f] : Œ± ‚Üí‚Çò Œ≥`. -/
def comp (g : Œ≤ ‚Üí Œ≥) (hg : Continuous g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Œ± ‚Üí‚Çò[Œº] Œ≥ :=
  Quotient.liftOn' f (fun f => mk (g ‚àò (f : Œ± ‚Üí Œ≤)) (hg.comp_aestronglyMeasurable f.2))
    fun _ _ H => mk_eq_mk.2 <| H.fun_comp g
#align measure_theory.ae_eq_fun.comp MeasureTheory.AEEqFun.comp

@[simp]
theorem comp_mk (g : Œ≤ ‚Üí Œ≥) (hg : Continuous g) (f : Œ± ‚Üí Œ≤) (hf) :
    comp g hg (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk (g ‚àò f) (hg.comp_aestronglyMeasurable hf) :=
  rfl
#align measure_theory.ae_eq_fun.comp_mk MeasureTheory.AEEqFun.comp_mk

theorem comp_eq_mk (g : Œ≤ ‚Üí Œ≥) (hg : Continuous g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
    comp g hg f = mk (g ‚àò f) (hg.comp_aestronglyMeasurable f.aestronglyMeasurable) := by
  rw [‚Üê comp_mk g hg f f.aestronglyMeasurable, mk_coeFn]
#align measure_theory.ae_eq_fun.comp_eq_mk MeasureTheory.AEEqFun.comp_eq_mk

theorem coeFn_comp (g : Œ≤ ‚Üí Œ≥) (hg : Continuous g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : comp g hg f =·µê[Œº] g ‚àò f := by
  rw [comp_eq_mk]
  apply coeFn_mk
#align measure_theory.ae_eq_fun.coe_fn_comp MeasureTheory.AEEqFun.coeFn_comp

theorem comp_compQuasiMeasurePreserving [MeasurableSpace Œ≤] {ŒΩ} (g : Œ≥ ‚Üí Œ¥) (hg : Continuous g)
    (f : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) {œÜ : Œ± ‚Üí Œ≤} (hœÜ : Measure.QuasiMeasurePreserving œÜ Œº ŒΩ) :
    (comp g hg f).compQuasiMeasurePreserving œÜ hœÜ =
      comp g hg (f.compQuasiMeasurePreserving œÜ hœÜ) := by
  rcases f; rfl

section CompMeasurable

variable [MeasurableSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤] [MeasurableSpace Œ≥]
  [PseudoMetrizableSpace Œ≥] [OpensMeasurableSpace Œ≥] [SecondCountableTopology Œ≥]

/-- Given a measurable function `g : Œ≤ ‚Üí Œ≥`, and an almost everywhere equal function `[f] : Œ± ‚Üí‚Çò Œ≤`,
    return the equivalence class of `g ‚àò f`, i.e., the almost everywhere equal function
    `[g ‚àò f] : Œ± ‚Üí‚Çò Œ≥`. This requires that `Œ≥` has a second countable topology. -/
def compMeasurable (g : Œ≤ ‚Üí Œ≥) (hg : Measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Œ± ‚Üí‚Çò[Œº] Œ≥ :=
  Quotient.liftOn' f
    (fun f' => mk (g ‚àò (f' : Œ± ‚Üí Œ≤)) (hg.comp_aemeasurable f'.2.aemeasurable).aestronglyMeasurable)
    fun _ _ H => mk_eq_mk.2 <| H.fun_comp g
#align measure_theory.ae_eq_fun.comp_measurable MeasureTheory.AEEqFun.compMeasurable

@[simp]
theorem compMeasurable_mk (g : Œ≤ ‚Üí Œ≥) (hg : Measurable g) (f : Œ± ‚Üí Œ≤)
    (hf : AEStronglyMeasurable f Œº) :
    compMeasurable g hg (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) =
      mk (g ‚àò f) (hg.comp_aemeasurable hf.aemeasurable).aestronglyMeasurable :=
  rfl
#align measure_theory.ae_eq_fun.comp_measurable_mk MeasureTheory.AEEqFun.compMeasurable_mk

theorem compMeasurable_eq_mk (g : Œ≤ ‚Üí Œ≥) (hg : Measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
    compMeasurable g hg f =
    mk (g ‚àò f) (hg.comp_aemeasurable f.aemeasurable).aestronglyMeasurable := by
  rw [‚Üê compMeasurable_mk g hg f f.aestronglyMeasurable, mk_coeFn]
#align measure_theory.ae_eq_fun.comp_measurable_eq_mk MeasureTheory.AEEqFun.compMeasurable_eq_mk

theorem coeFn_compMeasurable (g : Œ≤ ‚Üí Œ≥) (hg : Measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
    compMeasurable g hg f =·µê[Œº] g ‚àò f := by
  rw [compMeasurable_eq_mk]
  apply coeFn_mk
#align measure_theory.ae_eq_fun.coe_fn_comp_measurable MeasureTheory.AEEqFun.coeFn_compMeasurable

end CompMeasurable

/-- The class of `x ‚Ü¶ (f x, g x)`. -/
def pair (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] Œ≤ √ó Œ≥ :=
  Quotient.liftOn‚ÇÇ' f g (fun f g => mk (fun x => (f.1 x, g.1 x)) (f.2.prod_mk g.2))
    fun _f _g _f' _g' Hf Hg => mk_eq_mk.2 <| Hf.prod_mk Hg
#align measure_theory.ae_eq_fun.pair MeasureTheory.AEEqFun.pair

@[simp]
theorem pair_mk_mk (f : Œ± ‚Üí Œ≤) (hf) (g : Œ± ‚Üí Œ≥) (hg) :
    (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤).pair (mk g hg) = mk (fun x => (f x, g x)) (hf.prod_mk hg) :=
  rfl
#align measure_theory.ae_eq_fun.pair_mk_mk MeasureTheory.AEEqFun.pair_mk_mk

theorem pair_eq_mk (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) :
    f.pair g =
      mk (fun x => (f x, g x)) (f.aestronglyMeasurable.prod_mk g.aestronglyMeasurable) := by
  simp only [‚Üê pair_mk_mk, mk_coeFn, f.aestronglyMeasurable, g.aestronglyMeasurable]
#align measure_theory.ae_eq_fun.pair_eq_mk MeasureTheory.AEEqFun.pair_eq_mk

theorem coeFn_pair (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) : f.pair g =·µê[Œº] fun x => (f x, g x) := by
  rw [pair_eq_mk]
  apply coeFn_mk
#align measure_theory.ae_eq_fun.coe_fn_pair MeasureTheory.AEEqFun.coeFn_pair

/-- Given a continuous function `g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥`, and almost everywhere equal functions
    `[f‚ÇÅ] : Œ± ‚Üí‚Çò Œ≤` and `[f‚ÇÇ] : Œ± ‚Üí‚Çò Œ≥`, return the equivalence class of the function
    `fun a => g (f‚ÇÅ a) (f‚ÇÇ a)`, i.e., the almost everywhere equal function
    `[fun a => g (f‚ÇÅ a) (f‚ÇÇ a)] : Œ± ‚Üí‚Çò Œ≥` -/
def comp‚ÇÇ (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Continuous (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
    Œ± ‚Üí‚Çò[Œº] Œ¥ :=
  comp _ hg (f‚ÇÅ.pair f‚ÇÇ)
#align measure_theory.ae_eq_fun.comp‚ÇÇ MeasureTheory.AEEqFun.comp‚ÇÇ

@[simp]
theorem comp‚ÇÇ_mk_mk (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Continuous (uncurry g)) (f‚ÇÅ : Œ± ‚Üí Œ≤) (f‚ÇÇ : Œ± ‚Üí Œ≥)
    (hf‚ÇÅ hf‚ÇÇ) :
    comp‚ÇÇ g hg (mk f‚ÇÅ hf‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (mk f‚ÇÇ hf‚ÇÇ) =
      mk (fun a => g (f‚ÇÅ a) (f‚ÇÇ a)) (hg.comp_aestronglyMeasurable (hf‚ÇÅ.prod_mk hf‚ÇÇ)) :=
  rfl
#align measure_theory.ae_eq_fun.comp‚ÇÇ_mk_mk MeasureTheory.AEEqFun.comp‚ÇÇ_mk_mk

theorem comp‚ÇÇ_eq_pair (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Continuous (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ = comp _ hg (f‚ÇÅ.pair f‚ÇÇ) :=
  rfl
#align measure_theory.ae_eq_fun.comp‚ÇÇ_eq_pair MeasureTheory.AEEqFun.comp‚ÇÇ_eq_pair

theorem comp‚ÇÇ_eq_mk (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Continuous (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ = mk (fun a => g (f‚ÇÅ a) (f‚ÇÇ a))
      (hg.comp_aestronglyMeasurable (f‚ÇÅ.aestronglyMeasurable.prod_mk f‚ÇÇ.aestronglyMeasurable)) := by
  rw [comp‚ÇÇ_eq_pair, pair_eq_mk, comp_mk]; rfl
#align measure_theory.ae_eq_fun.comp‚ÇÇ_eq_mk MeasureTheory.AEEqFun.comp‚ÇÇ_eq_mk

theorem coeFn_comp‚ÇÇ (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Continuous (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ =·µê[Œº] fun a => g (f‚ÇÅ a) (f‚ÇÇ a) := by
  rw [comp‚ÇÇ_eq_mk]
  apply coeFn_mk
#align measure_theory.ae_eq_fun.coe_fn_comp‚ÇÇ MeasureTheory.AEEqFun.coeFn_comp‚ÇÇ

section

variable [MeasurableSpace Œ≤] [PseudoMetrizableSpace Œ≤] [BorelSpace Œ≤]
  [MeasurableSpace Œ≥] [PseudoMetrizableSpace Œ≥] [BorelSpace Œ≥] [SecondCountableTopologyEither Œ≤ Œ≥]
  [MeasurableSpace Œ¥] [PseudoMetrizableSpace Œ¥] [OpensMeasurableSpace Œ¥] [SecondCountableTopology Œ¥]

/-- Given a measurable function `g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥`, and almost everywhere equal functions
    `[f‚ÇÅ] : Œ± ‚Üí‚Çò Œ≤` and `[f‚ÇÇ] : Œ± ‚Üí‚Çò Œ≥`, return the equivalence class of the function
    `fun a => g (f‚ÇÅ a) (f‚ÇÇ a)`, i.e., the almost everywhere equal function
    `[fun a => g (f‚ÇÅ a) (f‚ÇÇ a)] : Œ± ‚Üí‚Çò Œ≥`. This requires `Œ¥` to have second-countable topology. -/
def comp‚ÇÇMeasurable (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] Œ¥ :=
  compMeasurable _ hg (f‚ÇÅ.pair f‚ÇÇ)
#align measure_theory.ae_eq_fun.comp‚ÇÇ_measurable MeasureTheory.AEEqFun.comp‚ÇÇMeasurable

@[simp]
theorem comp‚ÇÇMeasurable_mk_mk (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí Œ≤)
    (f‚ÇÇ : Œ± ‚Üí Œ≥) (hf‚ÇÅ hf‚ÇÇ) :
    comp‚ÇÇMeasurable g hg (mk f‚ÇÅ hf‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (mk f‚ÇÇ hf‚ÇÇ) =
      mk (fun a => g (f‚ÇÅ a) (f‚ÇÇ a))
        (hg.comp_aemeasurable (hf‚ÇÅ.aemeasurable.prod_mk hf‚ÇÇ.aemeasurable)).aestronglyMeasurable :=
  rfl
#align measure_theory.ae_eq_fun.comp‚ÇÇ_measurable_mk_mk MeasureTheory.AEEqFun.comp‚ÇÇMeasurable_mk_mk

theorem comp‚ÇÇMeasurable_eq_pair (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : comp‚ÇÇMeasurable g hg f‚ÇÅ f‚ÇÇ = compMeasurable _ hg (f‚ÇÅ.pair f‚ÇÇ) :=
  rfl
#align measure_theory.ae_eq_fun.comp‚ÇÇ_measurable_eq_pair MeasureTheory.AEEqFun.comp‚ÇÇMeasurable_eq_pair

theorem comp‚ÇÇMeasurable_eq_mk (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
    comp‚ÇÇMeasurable g hg f‚ÇÅ f‚ÇÇ =
      mk (fun a => g (f‚ÇÅ a) (f‚ÇÇ a))
        (hg.comp_aemeasurable (f‚ÇÅ.aemeasurable.prod_mk f‚ÇÇ.aemeasurable)).aestronglyMeasurable := by
  rw [comp‚ÇÇMeasurable_eq_pair, pair_eq_mk, compMeasurable_mk]; rfl
#align measure_theory.ae_eq_fun.comp‚ÇÇ_measurable_eq_mk MeasureTheory.AEEqFun.comp‚ÇÇMeasurable_eq_mk

theorem coeFn_comp‚ÇÇMeasurable (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : comp‚ÇÇMeasurable g hg f‚ÇÅ f‚ÇÇ =·µê[Œº] fun a => g (f‚ÇÅ a) (f‚ÇÇ a) := by
  rw [comp‚ÇÇMeasurable_eq_mk]
  apply coeFn_mk
#align measure_theory.ae_eq_fun.coe_fn_comp‚ÇÇ_measurable MeasureTheory.AEEqFun.coeFn_comp‚ÇÇMeasurable

end

/-- Interpret `f : Œ± ‚Üí‚Çò[Œº] Œ≤` as a germ at `ae Œº` forgetting that `f` is almost everywhere
    strongly measurable. -/
def toGerm (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Germ (ae Œº) Œ≤ :=
  Quotient.liftOn' f (fun f => ((f : Œ± ‚Üí Œ≤) : Germ (ae Œº) Œ≤)) fun _ _ H => Germ.coe_eq.2 H
#align measure_theory.ae_eq_fun.to_germ MeasureTheory.AEEqFun.toGerm

@[simp]
theorem mk_toGerm (f : Œ± ‚Üí Œ≤) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤).toGerm = f :=
  rfl
#align measure_theory.ae_eq_fun.mk_to_germ MeasureTheory.AEEqFun.mk_toGerm

theorem toGerm_eq (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : f.toGerm = (f : Œ± ‚Üí Œ≤) := by rw [‚Üê mk_toGerm, mk_coeFn]
#align measure_theory.ae_eq_fun.to_germ_eq MeasureTheory.AEEqFun.toGerm_eq

theorem toGerm_injective : Injective (toGerm : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí Germ (ae Œº) Œ≤) := fun f g H =>
  ext <| Germ.coe_eq.1 <| by rwa [‚Üê toGerm_eq, ‚Üê toGerm_eq]
#align measure_theory.ae_eq_fun.to_germ_injective MeasureTheory.AEEqFun.toGerm_injective

@[simp]
theorem compQuasiMeasurePreserving_toGerm [MeasurableSpace Œ≤] {f : Œ± ‚Üí Œ≤} {ŒΩ}
    (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (hf : Measure.QuasiMeasurePreserving f Œº ŒΩ) :
    (g.compQuasiMeasurePreserving f hf).toGerm = g.toGerm.compTendsto f hf.tendsto_ae := by
  rcases g; rfl

@[simp]
theorem compMeasurePreserving_toGerm [MeasurableSpace Œ≤] {f : Œ± ‚Üí Œ≤} {ŒΩ}
    (g : Œ≤ ‚Üí‚Çò[ŒΩ] Œ≥) (hf : MeasurePreserving f Œº ŒΩ) :
    (g.compMeasurePreserving f hf).toGerm =
      g.toGerm.compTendsto f hf.quasiMeasurePreserving.tendsto_ae :=
  compQuasiMeasurePreserving_toGerm _ _

theorem comp_toGerm (g : Œ≤ ‚Üí Œ≥) (hg : Continuous g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
    (comp g hg f).toGerm = f.toGerm.map g :=
  induction_on f fun f _ => by simp
#align measure_theory.ae_eq_fun.comp_to_germ MeasureTheory.AEEqFun.comp_toGerm

theorem compMeasurable_toGerm [MeasurableSpace Œ≤] [BorelSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [PseudoMetrizableSpace Œ≥] [SecondCountableTopology Œ≥] [MeasurableSpace Œ≥]
    [OpensMeasurableSpace Œ≥] (g : Œ≤ ‚Üí Œ≥) (hg : Measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
    (compMeasurable g hg f).toGerm = f.toGerm.map g :=
  induction_on f fun f _ => by simp
#align measure_theory.ae_eq_fun.comp_measurable_to_germ MeasureTheory.AEEqFun.compMeasurable_toGerm

theorem comp‚ÇÇ_toGerm (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Continuous (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤)
    (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : (comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ).toGerm = f‚ÇÅ.toGerm.map‚ÇÇ g f‚ÇÇ.toGerm :=
  induction_on‚ÇÇ f‚ÇÅ f‚ÇÇ fun f‚ÇÅ _ f‚ÇÇ _ => by simp
#align measure_theory.ae_eq_fun.comp‚ÇÇ_to_germ MeasureTheory.AEEqFun.comp‚ÇÇ_toGerm

theorem comp‚ÇÇMeasurable_toGerm [PseudoMetrizableSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤]
    [PseudoMetrizableSpace Œ≥] [SecondCountableTopologyEither Œ≤ Œ≥]
    [MeasurableSpace Œ≥] [BorelSpace Œ≥] [PseudoMetrizableSpace Œ¥] [SecondCountableTopology Œ¥]
    [MeasurableSpace Œ¥] [OpensMeasurableSpace Œ¥] (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : Measurable (uncurry g))
    (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
    (comp‚ÇÇMeasurable g hg f‚ÇÅ f‚ÇÇ).toGerm = f‚ÇÅ.toGerm.map‚ÇÇ g f‚ÇÇ.toGerm :=
  induction_on‚ÇÇ f‚ÇÅ f‚ÇÇ fun f‚ÇÅ _ f‚ÇÇ _ => by simp
#align measure_theory.ae_eq_fun.comp‚ÇÇ_measurable_to_germ MeasureTheory.AEEqFun.comp‚ÇÇMeasurable_toGerm

/-- Given a predicate `p` and an equivalence class `[f]`, return true if `p` holds of `f a`
    for almost all `a` -/
def LiftPred (p : Œ≤ ‚Üí Prop) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Prop :=
  f.toGerm.LiftPred p
#align measure_theory.ae_eq_fun.lift_pred MeasureTheory.AEEqFun.LiftPred

/-- Given a relation `r` and equivalence class `[f]` and `[g]`, return true if `r` holds of
    `(f a, g a)` for almost all `a` -/
def LiftRel (r : Œ≤ ‚Üí Œ≥ ‚Üí Prop) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) : Prop :=
  f.toGerm.LiftRel r g.toGerm
#align measure_theory.ae_eq_fun.lift_rel MeasureTheory.AEEqFun.LiftRel

theorem liftRel_mk_mk {r : Œ≤ ‚Üí Œ≥ ‚Üí Prop} {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {hf hg} :
    LiftRel r (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) (mk g hg) ‚Üî ‚àÄ·µê a ‚àÇŒº, r (f a) (g a) :=
  Iff.rfl
#align measure_theory.ae_eq_fun.lift_rel_mk_mk MeasureTheory.AEEqFun.liftRel_mk_mk

theorem liftRel_iff_coeFn {r : Œ≤ ‚Üí Œ≥ ‚Üí Prop} {f : Œ± ‚Üí‚Çò[Œº] Œ≤} {g : Œ± ‚Üí‚Çò[Œº] Œ≥} :
    LiftRel r f g ‚Üî ‚àÄ·µê a ‚àÇŒº, r (f a) (g a) := by rw [‚Üê liftRel_mk_mk, mk_coeFn, mk_coeFn]
#align measure_theory.ae_eq_fun.lift_rel_iff_coe_fn MeasureTheory.AEEqFun.liftRel_iff_coeFn

section Order

instance instPreorder [Preorder Œ≤] : Preorder (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
  Preorder.lift toGerm
#align measure_theory.ae_eq_fun.preorder MeasureTheory.AEEqFun.instPreorder

@[simp]
theorem mk_le_mk [Preorder Œ≤] {f g : Œ± ‚Üí Œ≤} (hf hg) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) ‚â§ mk g hg ‚Üî f ‚â§·µê[Œº] g :=
  Iff.rfl
#align measure_theory.ae_eq_fun.mk_le_mk MeasureTheory.AEEqFun.mk_le_mk

@[simp, norm_cast]
theorem coeFn_le [Preorder Œ≤] {f g : Œ± ‚Üí‚Çò[Œº] Œ≤} : (f : Œ± ‚Üí Œ≤) ‚â§·µê[Œº] g ‚Üî f ‚â§ g :=
  liftRel_iff_coeFn.symm
#align measure_theory.ae_eq_fun.coe_fn_le MeasureTheory.AEEqFun.coeFn_le

instance instPartialOrder [PartialOrder Œ≤] : PartialOrder (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
  PartialOrder.lift toGerm toGerm_injective
#align measure_theory.ae_eq_fun.partial_order MeasureTheory.AEEqFun.instPartialOrder

section Lattice

section Sup

variable [SemilatticeSup Œ≤] [ContinuousSup Œ≤]

instance instSup : Sup (Œ± ‚Üí‚Çò[Œº] Œ≤) where sup f g := AEEqFun.comp‚ÇÇ (¬∑ ‚äî ¬∑) continuous_sup f g
#align measure_theory.ae_eq_fun.has_sup MeasureTheory.AEEqFun.instSup

theorem coeFn_sup (f g : Œ± ‚Üí‚Çò[Œº] Œ≤) : ‚áë(f ‚äî g) =·µê[Œº] fun x => f x ‚äî g x :=
  coeFn_comp‚ÇÇ _ _ _ _
#align measure_theory.ae_eq_fun.coe_fn_sup MeasureTheory.AEEqFun.coeFn_sup

protected theorem le_sup_left (f g : Œ± ‚Üí‚Çò[Œº] Œ≤) : f ‚â§ f ‚äî g := by
  rw [‚Üê coeFn_le]
  filter_upwards [coeFn_sup f g] with _ ha
  rw [ha]
  exact le_sup_left
#align measure_theory.ae_eq_fun.le_sup_left MeasureTheory.AEEqFun.le_sup_left

protected theorem le_sup_right (f g : Œ± ‚Üí‚Çò[Œº] Œ≤) : g ‚â§ f ‚äî g := by
  rw [‚Üê coeFn_le]
  filter_upwards [coeFn_sup f g] with _ ha
  rw [ha]
  exact le_sup_right
#align measure_theory.ae_eq_fun.le_sup_right MeasureTheory.AEEqFun.le_sup_right

protected theorem sup_le (f g f' : Œ± ‚Üí‚Çò[Œº] Œ≤) (hf : f ‚â§ f') (hg : g ‚â§ f') : f ‚äî g ‚â§ f' := by
  rw [‚Üê coeFn_le] at hf hg ‚ä¢
  filter_upwards [hf, hg, coeFn_sup f g] with _ haf hag ha_sup
  rw [ha_sup]
  exact sup_le haf hag
#align measure_theory.ae_eq_fun.sup_le MeasureTheory.AEEqFun.sup_le

end Sup

section Inf

variable [SemilatticeInf Œ≤] [ContinuousInf Œ≤]

instance instInf : Inf (Œ± ‚Üí‚Çò[Œº] Œ≤) where inf f g := AEEqFun.comp‚ÇÇ (¬∑ ‚äì ¬∑) continuous_inf f g
#align measure_theory.ae_eq_fun.has_inf MeasureTheory.AEEqFun.instInf

theorem coeFn_inf (f g : Œ± ‚Üí‚Çò[Œº] Œ≤) : ‚áë(f ‚äì g) =·µê[Œº] fun x => f x ‚äì g x :=
  coeFn_comp‚ÇÇ _ _ _ _
#align measure_theory.ae_eq_fun.coe_fn_inf MeasureTheory.AEEqFun.coeFn_inf

protected theorem inf_le_left (f g : Œ± ‚Üí‚Çò[Œº] Œ≤) : f ‚äì g ‚â§ f := by
  rw [‚Üê coeFn_le]
  filter_upwards [coeFn_inf f g] with _ ha
  rw [ha]
  exact inf_le_left
#align measure_theory.ae_eq_fun.inf_le_left MeasureTheory.AEEqFun.inf_le_left

protected theorem inf_le_right (f g : Œ± ‚Üí‚Çò[Œº] Œ≤) : f ‚äì g ‚â§ g := by
  rw [‚Üê coeFn_le]
  filter_upwards [coeFn_inf f g] with _ ha
  rw [ha]
  exact inf_le_right
#align measure_theory.ae_eq_fun.inf_le_right MeasureTheory.AEEqFun.inf_le_right

protected theorem le_inf (f' f g : Œ± ‚Üí‚Çò[Œº] Œ≤) (hf : f' ‚â§ f) (hg : f' ‚â§ g) : f' ‚â§ f ‚äì g := by
  rw [‚Üê coeFn_le] at hf hg ‚ä¢
  filter_upwards [hf, hg, coeFn_inf f g] with _ haf hag ha_inf
  rw [ha_inf]
  exact le_inf haf hag
#align measure_theory.ae_eq_fun.le_inf MeasureTheory.AEEqFun.le_inf

end Inf

instance instLattice [Lattice Œ≤] [TopologicalLattice Œ≤] : Lattice (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
  { AEEqFun.instPartialOrder with
    sup := Sup.sup
    le_sup_left := AEEqFun.le_sup_left
    le_sup_right := AEEqFun.le_sup_right
    sup_le := AEEqFun.sup_le
    inf := Inf.inf
    inf_le_left := AEEqFun.inf_le_left
    inf_le_right := AEEqFun.inf_le_right
    le_inf := AEEqFun.le_inf }
#align measure_theory.ae_eq_fun.lattice MeasureTheory.AEEqFun.instLattice

end Lattice

end Order

variable (Œ±)

/-- The equivalence class of a constant function: `[fun _ : Œ± => b]`, based on the equivalence
relation of being almost everywhere equal -/
def const (b : Œ≤) : Œ± ‚Üí‚Çò[Œº] Œ≤ :=
  mk (fun _ : Œ± => b) aestronglyMeasurable_const
#align measure_theory.ae_eq_fun.const MeasureTheory.AEEqFun.const

theorem coeFn_const (b : Œ≤) : (const Œ± b : Œ± ‚Üí‚Çò[Œº] Œ≤) =·µê[Œº] Function.const Œ± b :=
  coeFn_mk _ _
#align measure_theory.ae_eq_fun.coe_fn_const MeasureTheory.AEEqFun.coeFn_const

variable {Œ±}

instance instInhabited [Inhabited Œ≤] : Inhabited (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
  ‚ü®const Œ± default‚ü©
#align measure_theory.ae_eq_fun.inhabited MeasureTheory.AEEqFun.instInhabited

@[to_additive]
instance instOne [One Œ≤] : One (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
  ‚ü®const Œ± 1‚ü©
#align measure_theory.ae_eq_fun.has_one MeasureTheory.AEEqFun.instOne
#align measure_theory.ae_eq_fun.has_zero MeasureTheory.AEEqFun.instZero

@[to_additive]
theorem one_def [One Œ≤] : (1 : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk (fun _ : Œ± => 1) aestronglyMeasurable_const :=
  rfl
#align measure_theory.ae_eq_fun.one_def MeasureTheory.AEEqFun.one_def
#align measure_theory.ae_eq_fun.zero_def MeasureTheory.AEEqFun.zero_def

@[to_additive]
theorem coeFn_one [One Œ≤] : ‚áë(1 : Œ± ‚Üí‚Çò[Œº] Œ≤) =·µê[Œº] 1 :=
  coeFn_const _ _
#align measure_theory.ae_eq_fun.coe_fn_one MeasureTheory.AEEqFun.coeFn_one
#align measure_theory.ae_eq_fun.coe_fn_zero MeasureTheory.AEEqFun.coeFn_zero

@[to_additive (attr := simp)]
theorem one_toGerm [One Œ≤] : (1 : Œ± ‚Üí‚Çò[Œº] Œ≤).toGerm = 1 :=
  rfl
#align measure_theory.ae_eq_fun.one_to_germ MeasureTheory.AEEqFun.one_toGerm
#align measure_theory.ae_eq_fun.zero_to_germ MeasureTheory.AEEqFun.zero_toGerm

-- Note we set up the scalar actions before the `Monoid` structures in case we want to
-- try to override the `nsmul` or `zsmul` fields in future.
section SMul

variable {ùïú ùïú' : Type*}
variable [SMul ùïú Œ≥] [ContinuousConstSMul ùïú Œ≥]
variable [SMul ùïú' Œ≥] [ContinuousConstSMul ùïú' Œ≥]

instance instSMul : SMul ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®fun c f => comp (c ‚Ä¢ ¬∑) (continuous_id.const_smul c) f‚ü©
#align measure_theory.ae_eq_fun.has_smul MeasureTheory.AEEqFun.instSMul

@[simp]
theorem smul_mk (c : ùïú) (f : Œ± ‚Üí Œ≥) (hf : AEStronglyMeasurable f Œº) :
    c ‚Ä¢ (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) = mk (c ‚Ä¢ f) (hf.const_smul _) :=
  rfl
#align measure_theory.ae_eq_fun.smul_mk MeasureTheory.AEEqFun.smul_mk

theorem coeFn_smul (c : ùïú) (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(c ‚Ä¢ f) =·µê[Œº] c ‚Ä¢ ‚áëf :=
  coeFn_comp _ _ _
#align measure_theory.ae_eq_fun.coe_fn_smul MeasureTheory.AEEqFun.coeFn_smul

theorem smul_toGerm (c : ùïú) (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : (c ‚Ä¢ f).toGerm = c ‚Ä¢ f.toGerm :=
  comp_toGerm _ _ _
#align measure_theory.ae_eq_fun.smul_to_germ MeasureTheory.AEEqFun.smul_toGerm

instance instSMulCommClass [SMulCommClass ùïú ùïú' Œ≥] : SMulCommClass ùïú ùïú' (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®fun a b f => induction_on f fun f hf => by simp_rw [smul_mk, smul_comm]‚ü©
#align measure_theory.ae_eq_fun.smul_comm_class MeasureTheory.AEEqFun.instSMulCommClass

instance instIsScalarTower [SMul ùïú ùïú'] [IsScalarTower ùïú ùïú' Œ≥] : IsScalarTower ùïú ùïú' (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®fun a b f => induction_on f fun f hf => by simp_rw [smul_mk, smul_assoc]‚ü©
#align measure_theory.ae_eq_fun.is_scalar_tower MeasureTheory.AEEqFun.instIsScalarTower

instance instIsCentralScalar [SMul ùïú·µê·µí·µñ Œ≥] [IsCentralScalar ùïú Œ≥] : IsCentralScalar ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®fun a f => induction_on f fun f hf => by simp_rw [smul_mk, op_smul_eq_smul]‚ü©
#align measure_theory.ae_eq_fun.is_central_scalar MeasureTheory.AEEqFun.instIsCentralScalar

end SMul

section Mul

variable [Mul Œ≥] [ContinuousMul Œ≥]

@[to_additive]
instance instMul : Mul (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®comp‚ÇÇ (¬∑ * ¬∑) continuous_mul‚ü©
#align measure_theory.ae_eq_fun.has_mul MeasureTheory.AEEqFun.instMul
#align measure_theory.ae_eq_fun.has_add MeasureTheory.AEEqFun.instAdd

@[to_additive (attr := simp)]
theorem mk_mul_mk (f g : Œ± ‚Üí Œ≥) (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) * mk g hg = mk (f * g) (hf.mul hg) :=
  rfl
#align measure_theory.ae_eq_fun.mk_mul_mk MeasureTheory.AEEqFun.mk_mul_mk
#align measure_theory.ae_eq_fun.mk_add_mk MeasureTheory.AEEqFun.mk_add_mk

@[to_additive]
theorem coeFn_mul (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(f * g) =·µê[Œº] f * g :=
  coeFn_comp‚ÇÇ _ _ _ _
#align measure_theory.ae_eq_fun.coe_fn_mul MeasureTheory.AEEqFun.coeFn_mul
#align measure_theory.ae_eq_fun.coe_fn_add MeasureTheory.AEEqFun.coeFn_add

@[to_additive (attr := simp)]
theorem mul_toGerm (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : (f * g).toGerm = f.toGerm * g.toGerm :=
  comp‚ÇÇ_toGerm _ _ _ _
#align measure_theory.ae_eq_fun.mul_to_germ MeasureTheory.AEEqFun.mul_toGerm
#align measure_theory.ae_eq_fun.add_to_germ MeasureTheory.AEEqFun.add_toGerm

end Mul

instance instAddMonoid [AddMonoid Œ≥] [ContinuousAdd Œ≥] : AddMonoid (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.addMonoid toGerm zero_toGerm add_toGerm fun _ _ => smul_toGerm _ _
#align measure_theory.ae_eq_fun.add_monoid MeasureTheory.AEEqFun.instAddMonoid

instance instAddCommMonoid [AddCommMonoid Œ≥] [ContinuousAdd Œ≥] : AddCommMonoid (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.addCommMonoid toGerm zero_toGerm add_toGerm fun _ _ => smul_toGerm _ _
#align measure_theory.ae_eq_fun.add_comm_monoid MeasureTheory.AEEqFun.instAddCommMonoid

section Monoid

variable [Monoid Œ≥] [ContinuousMul Œ≥]

instance instPowNat : Pow (Œ± ‚Üí‚Çò[Œº] Œ≥) ‚Ñï :=
  ‚ü®fun f n => comp _ (continuous_pow n) f‚ü©
#align measure_theory.ae_eq_fun.nat.has_pow MeasureTheory.AEEqFun.instPowNat

@[simp]
theorem mk_pow (f : Œ± ‚Üí Œ≥) (hf) (n : ‚Ñï) :
    (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) ^ n =
      mk (f ^ n) ((_root_.continuous_pow n).comp_aestronglyMeasurable hf) :=
  rfl
#align measure_theory.ae_eq_fun.mk_pow MeasureTheory.AEEqFun.mk_pow

theorem coeFn_pow (f : Œ± ‚Üí‚Çò[Œº] Œ≥) (n : ‚Ñï) : ‚áë(f ^ n) =·µê[Œº] (‚áëf) ^ n :=
  coeFn_comp _ _ _
#align measure_theory.ae_eq_fun.coe_fn_pow MeasureTheory.AEEqFun.coeFn_pow

@[simp]
theorem pow_toGerm (f : Œ± ‚Üí‚Çò[Œº] Œ≥) (n : ‚Ñï) : (f ^ n).toGerm = f.toGerm ^ n :=
  comp_toGerm _ _ _
#align measure_theory.ae_eq_fun.pow_to_germ MeasureTheory.AEEqFun.pow_toGerm

@[to_additive existing]
instance instMonoid : Monoid (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.monoid toGerm one_toGerm mul_toGerm pow_toGerm
#align measure_theory.ae_eq_fun.monoid MeasureTheory.AEEqFun.instMonoid

/-- `AEEqFun.toGerm` as a `MonoidHom`. -/
@[to_additive (attr := simps) "`AEEqFun.toGerm` as an `AddMonoidHom`."]
def toGermMonoidHom : (Œ± ‚Üí‚Çò[Œº] Œ≥) ‚Üí* (ae Œº).Germ Œ≥ where
  toFun := toGerm
  map_one' := one_toGerm
  map_mul' := mul_toGerm
#align measure_theory.ae_eq_fun.to_germ_monoid_hom MeasureTheory.AEEqFun.toGermMonoidHom
#align measure_theory.ae_eq_fun.to_germ_monoid_hom_apply MeasureTheory.AEEqFun.toGermMonoidHom_apply
#align measure_theory.ae_eq_fun.to_germ_add_monoid_hom MeasureTheory.AEEqFun.toGermAddMonoidHom
#align measure_theory.ae_eq_fun.to_germ_add_monoid_hom_apply MeasureTheory.AEEqFun.toGermAddMonoidHom_apply

end Monoid

@[to_additive existing]
instance instCommMonoid [CommMonoid Œ≥] [ContinuousMul Œ≥] : CommMonoid (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.commMonoid toGerm one_toGerm mul_toGerm pow_toGerm
#align measure_theory.ae_eq_fun.comm_monoid MeasureTheory.AEEqFun.instCommMonoid

section Group

variable [Group Œ≥] [TopologicalGroup Œ≥]

section Inv

@[to_additive]
instance instInv : Inv (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®comp Inv.inv continuous_inv‚ü©
#align measure_theory.ae_eq_fun.has_inv MeasureTheory.AEEqFun.instInv
#align measure_theory.ae_eq_fun.has_neg MeasureTheory.AEEqFun.instNeg

@[to_additive (attr := simp)]
theorem inv_mk (f : Œ± ‚Üí Œ≥) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥)‚Åª¬π = mk f‚Åª¬π hf.inv :=
  rfl
#align measure_theory.ae_eq_fun.inv_mk MeasureTheory.AEEqFun.inv_mk
#align measure_theory.ae_eq_fun.neg_mk MeasureTheory.AEEqFun.neg_mk

@[to_additive]
theorem coeFn_inv (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áëf‚Åª¬π =·µê[Œº] f‚Åª¬π :=
  coeFn_comp _ _ _
#align measure_theory.ae_eq_fun.coe_fn_inv MeasureTheory.AEEqFun.coeFn_inv
#align measure_theory.ae_eq_fun.coe_fn_neg MeasureTheory.AEEqFun.coeFn_neg

@[to_additive]
theorem inv_toGerm (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : f‚Åª¬π.toGerm = f.toGerm‚Åª¬π :=
  comp_toGerm _ _ _
#align measure_theory.ae_eq_fun.inv_to_germ MeasureTheory.AEEqFun.inv_toGerm
#align measure_theory.ae_eq_fun.neg_to_germ MeasureTheory.AEEqFun.neg_toGerm

end Inv

section Div

@[to_additive]
instance instDiv : Div (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  ‚ü®comp‚ÇÇ Div.div continuous_div'‚ü©
#align measure_theory.ae_eq_fun.has_div MeasureTheory.AEEqFun.instDiv
#align measure_theory.ae_eq_fun.has_sub MeasureTheory.AEEqFun.instSub

@[to_additive (attr := simp, nolint simpNF)] -- Porting note: LHS does not simplify.
theorem mk_div (f g : Œ± ‚Üí Œ≥) (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) :
    mk (f / g) (hf.div hg) = (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) / mk g hg :=
  rfl
#align measure_theory.ae_eq_fun.mk_div MeasureTheory.AEEqFun.mk_div
#align measure_theory.ae_eq_fun.mk_sub MeasureTheory.AEEqFun.mk_sub

@[to_additive]
theorem coeFn_div (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(f / g) =·µê[Œº] f / g :=
  coeFn_comp‚ÇÇ _ _ _ _
#align measure_theory.ae_eq_fun.coe_fn_div MeasureTheory.AEEqFun.coeFn_div
#align measure_theory.ae_eq_fun.coe_fn_sub MeasureTheory.AEEqFun.coeFn_sub

@[to_additive]
theorem div_toGerm (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : (f / g).toGerm = f.toGerm / g.toGerm :=
  comp‚ÇÇ_toGerm _ _ _ _
#align measure_theory.ae_eq_fun.div_to_germ MeasureTheory.AEEqFun.div_toGerm
#align measure_theory.ae_eq_fun.sub_to_germ MeasureTheory.AEEqFun.sub_toGerm

end Div

section ZPow

instance instPowInt : Pow (Œ± ‚Üí‚Çò[Œº] Œ≥) ‚Ñ§ :=
  ‚ü®fun f n => comp _ (continuous_zpow n) f‚ü©
#align measure_theory.ae_eq_fun.has_int_pow MeasureTheory.AEEqFun.instPowInt

@[simp]
theorem mk_zpow (f : Œ± ‚Üí Œ≥) (hf) (n : ‚Ñ§) :
    (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) ^ n = mk (f ^ n) ((continuous_zpow n).comp_aestronglyMeasurable hf) :=
  rfl
#align measure_theory.ae_eq_fun.mk_zpow MeasureTheory.AEEqFun.mk_zpow

theorem coeFn_zpow (f : Œ± ‚Üí‚Çò[Œº] Œ≥) (n : ‚Ñ§) : ‚áë(f ^ n) =·µê[Œº] (‚áëf) ^ n :=
  coeFn_comp _ _ _
#align measure_theory.ae_eq_fun.coe_fn_zpow MeasureTheory.AEEqFun.coeFn_zpow

@[simp]
theorem zpow_toGerm (f : Œ± ‚Üí‚Çò[Œº] Œ≥) (n : ‚Ñ§) : (f ^ n).toGerm = f.toGerm ^ n :=
  comp_toGerm _ _ _
#align measure_theory.ae_eq_fun.zpow_to_germ MeasureTheory.AEEqFun.zpow_toGerm

end ZPow

end Group

instance instAddGroup [AddGroup Œ≥] [TopologicalAddGroup Œ≥] : AddGroup (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.addGroup toGerm zero_toGerm add_toGerm neg_toGerm sub_toGerm
    (fun _ _ => smul_toGerm _ _) fun _ _ => smul_toGerm _ _
#align measure_theory.ae_eq_fun.add_group MeasureTheory.AEEqFun.instAddGroup

instance instAddCommGroup [AddCommGroup Œ≥] [TopologicalAddGroup Œ≥] : AddCommGroup (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  { add_comm := add_comm }
#align measure_theory.ae_eq_fun.add_comm_group MeasureTheory.AEEqFun.instAddCommGroup

@[to_additive existing]
instance instGroup [Group Œ≥] [TopologicalGroup Œ≥] : Group (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.group _ one_toGerm mul_toGerm inv_toGerm div_toGerm pow_toGerm zpow_toGerm
#align measure_theory.ae_eq_fun.group MeasureTheory.AEEqFun.instGroup

@[to_additive existing]
instance instCommGroup [CommGroup Œ≥] [TopologicalGroup Œ≥] : CommGroup (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  { mul_comm := mul_comm }
#align measure_theory.ae_eq_fun.comm_group MeasureTheory.AEEqFun.instCommGroup

section Module

variable {ùïú : Type*}

instance instMulAction [Monoid ùïú] [MulAction ùïú Œ≥] [ContinuousConstSMul ùïú Œ≥] :
    MulAction ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.mulAction toGerm smul_toGerm
#align measure_theory.ae_eq_fun.mul_action MeasureTheory.AEEqFun.instMulAction

instance instDistribMulAction [Monoid ùïú] [AddMonoid Œ≥] [ContinuousAdd Œ≥] [DistribMulAction ùïú Œ≥]
    [ContinuousConstSMul ùïú Œ≥] : DistribMulAction ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.distribMulAction (toGermAddMonoidHom : (Œ± ‚Üí‚Çò[Œº] Œ≥) ‚Üí+ _) fun c : ùïú =>
    smul_toGerm c
#align measure_theory.ae_eq_fun.distrib_mul_action MeasureTheory.AEEqFun.instDistribMulAction

instance instModule [Semiring ùïú] [AddCommMonoid Œ≥] [ContinuousAdd Œ≥] [Module ùïú Œ≥]
    [ContinuousConstSMul ùïú Œ≥] : Module ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
  toGerm_injective.module ùïú (toGermAddMonoidHom : (Œ± ‚Üí‚Çò[Œº] Œ≥) ‚Üí+ _) smul_toGerm
#align measure_theory.ae_eq_fun.module MeasureTheory.AEEqFun.instModule

end Module

open ENNReal

/-- For `f : Œ± ‚Üí ‚Ñù‚â•0‚àû`, define `‚à´ [f]` to be `‚à´ f` -/
def lintegral (f : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû) : ‚Ñù‚â•0‚àû :=
  Quotient.liftOn' f (fun f => ‚à´‚Åª a, (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) a ‚àÇŒº) fun _ _ => lintegral_congr_ae
#align measure_theory.ae_eq_fun.lintegral MeasureTheory.AEEqFun.lintegral

@[simp]
theorem lintegral_mk (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû).lintegral = ‚à´‚Åª a, f a ‚àÇŒº :=
  rfl
#align measure_theory.ae_eq_fun.lintegral_mk MeasureTheory.AEEqFun.lintegral_mk

theorem lintegral_coeFn (f : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû) : ‚à´‚Åª a, f a ‚àÇŒº = f.lintegral := by
  rw [‚Üê lintegral_mk, mk_coeFn]
#align measure_theory.ae_eq_fun.lintegral_coe_fn MeasureTheory.AEEqFun.lintegral_coeFn

@[simp]
nonrec theorem lintegral_zero : lintegral (0 : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû) = 0 :=
  lintegral_zero
#align measure_theory.ae_eq_fun.lintegral_zero MeasureTheory.AEEqFun.lintegral_zero

@[simp]
theorem lintegral_eq_zero_iff {f : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû} : lintegral f = 0 ‚Üî f = 0 :=
  induction_on f fun _f hf => (lintegral_eq_zero_iff' hf.aemeasurable).trans mk_eq_mk.symm
#align measure_theory.ae_eq_fun.lintegral_eq_zero_iff MeasureTheory.AEEqFun.lintegral_eq_zero_iff

theorem lintegral_add (f g : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû) : lintegral (f + g) = lintegral f + lintegral g :=
  induction_on‚ÇÇ f g fun f hf g _ => by simp [lintegral_add_left' hf.aemeasurable]
#align measure_theory.ae_eq_fun.lintegral_add MeasureTheory.AEEqFun.lintegral_add

theorem lintegral_mono {f g : Œ± ‚Üí‚Çò[Œº] ‚Ñù‚â•0‚àû} : f ‚â§ g ‚Üí lintegral f ‚â§ lintegral g :=
  induction_on‚ÇÇ f g fun _f _ _g _ hfg => lintegral_mono_ae hfg
#align measure_theory.ae_eq_fun.lintegral_mono MeasureTheory.AEEqFun.lintegral_mono

section Abs

theorem coeFn_abs {Œ≤} [TopologicalSpace Œ≤] [Lattice Œ≤] [TopologicalLattice Œ≤] [AddGroup Œ≤]
    [TopologicalAddGroup Œ≤] (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : ‚áë|f| =·µê[Œº] fun x => |f x| := by
  simp_rw [abs]
  filter_upwards [AEEqFun.coeFn_sup f (-f), AEEqFun.coeFn_neg f] with x hx_sup hx_neg
  rw [hx_sup, hx_neg, Pi.neg_apply]
#align measure_theory.ae_eq_fun.coe_fn_abs MeasureTheory.AEEqFun.coeFn_abs

end Abs

section PosPart

variable [LinearOrder Œ≥] [OrderClosedTopology Œ≥] [Zero Œ≥]

/-- Positive part of an `AEEqFun`. -/
def posPart (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] Œ≥ :=
  comp (fun x => max x 0) (continuous_id.max continuous_const) f
#align measure_theory.ae_eq_fun.pos_part MeasureTheory.AEEqFun.posPart

@[simp]
theorem posPart_mk (f : Œ± ‚Üí Œ≥) (hf) :
    posPart (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) =
      mk (fun x => max (f x) 0)
        ((continuous_id.max continuous_const).comp_aestronglyMeasurable hf) :=
  rfl
#align measure_theory.ae_eq_fun.pos_part_mk MeasureTheory.AEEqFun.posPart_mk

theorem coeFn_posPart (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(posPart f) =·µê[Œº] fun a => max (f a) 0 :=
  coeFn_comp _ _ _
#align measure_theory.ae_eq_fun.coe_fn_pos_part MeasureTheory.AEEqFun.coeFn_posPart

end PosPart

end AEEqFun

end MeasureTheory

namespace ContinuousMap

open MeasureTheory

variable [TopologicalSpace Œ±] [BorelSpace Œ±] (Œº)
variable [TopologicalSpace Œ≤] [SecondCountableTopologyEither Œ± Œ≤] [PseudoMetrizableSpace Œ≤]

/-- The equivalence class of `Œº`-almost-everywhere measurable functions associated to a continuous
map. -/
def toAEEqFun (f : C(Œ±, Œ≤)) : Œ± ‚Üí‚Çò[Œº] Œ≤ :=
  AEEqFun.mk f f.continuous.aestronglyMeasurable
#align continuous_map.to_ae_eq_fun ContinuousMap.toAEEqFun

theorem coeFn_toAEEqFun (f : C(Œ±, Œ≤)) : f.toAEEqFun Œº =·µê[Œº] f :=
  AEEqFun.coeFn_mk f _
#align continuous_map.coe_fn_to_ae_eq_fun ContinuousMap.coeFn_toAEEqFun

variable [Group Œ≤] [TopologicalGroup Œ≤]

/-- The `MulHom` from the group of continuous maps from `Œ±` to `Œ≤` to the group of equivalence
classes of `Œº`-almost-everywhere measurable functions. -/
@[to_additive "The `AddHom` from the group of continuous maps from `Œ±` to `Œ≤` to the group of
equivalence classes of `Œº`-almost-everywhere measurable functions."]
def toAEEqFunMulHom : C(Œ±, Œ≤) ‚Üí* Œ± ‚Üí‚Çò[Œº] Œ≤ where
  toFun := ContinuousMap.toAEEqFun Œº
  map_one' := rfl
  map_mul' f g :=
    AEEqFun.mk_mul_mk _ _ f.continuous.aestronglyMeasurable g.continuous.aestronglyMeasurable
#align continuous_map.to_ae_eq_fun_mul_hom ContinuousMap.toAEEqFunMulHom
#align continuous_map.to_ae_eq_fun_add_hom ContinuousMap.toAEEqFunAddHom

variable {ùïú : Type*} [Semiring ùïú]
variable [TopologicalSpace Œ≥] [PseudoMetrizableSpace Œ≥] [AddCommGroup Œ≥] [Module ùïú Œ≥]
  [TopologicalAddGroup Œ≥] [ContinuousConstSMul ùïú Œ≥] [SecondCountableTopologyEither Œ± Œ≥]

/-- The linear map from the group of continuous maps from `Œ±` to `Œ≤` to the group of equivalence
classes of `Œº`-almost-everywhere measurable functions. -/
def toAEEqFunLinearMap : C(Œ±, Œ≥) ‚Üí‚Çó[ùïú] Œ± ‚Üí‚Çò[Œº] Œ≥ :=
  { toAEEqFunAddHom Œº with
    map_smul' := fun c f => AEEqFun.smul_mk c f f.continuous.aestronglyMeasurable }
#align continuous_map.to_ae_eq_fun_linear_map ContinuousMap.toAEEqFunLinearMap

end ContinuousMap

-- Guard against import creep
assert_not_exists InnerProductSpace
