/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.LpSeminorm.Basic

/-!
# Lp seminorm with respect to trimmed measure

In this file we prove basic properties of the Lp-seminorm of a function
with respect to the restriction of a measure to a sub-œÉ-algebra.
-/

namespace MeasureTheory

open Filter
open scoped ENNReal

variable {Œ± E : Type*} {m m0 : MeasurableSpace Œ±} {p : ‚Ñù‚â•0‚àû} {q : ‚Ñù} {Œº : Measure Œ±}
  [NormedAddCommGroup E]

theorem eLpNorm'_trim (hm : m ‚â§ m0) {f : Œ± ‚Üí E} (hf : StronglyMeasurable[m] f) :
    eLpNorm' f q (Œº.trim hm) = eLpNorm' f q Œº := by
  simp_rw [eLpNorm']
  congr 1
  refine lintegral_trim hm ?_
  refine @Measurable.pow_const _ _ _ _ _ _ _ m _ (@Measurable.coe_nnreal_ennreal _ m _ ?_) q
  apply @StronglyMeasurable.measurable
  exact @StronglyMeasurable.nnnorm Œ± m _ _ _ hf

theorem limsup_trim (hm : m ‚â§ m0) {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : Measurable[m] f) :
    limsup f (ae (Œº.trim hm)) = limsup f (ae Œº) := by
  simp_rw [limsup_eq]
  suffices h_set_eq : { a : ‚Ñù‚â•0‚àû | ‚àÄ·µê n ‚àÇŒº.trim hm, f n ‚â§ a } = { a : ‚Ñù‚â•0‚àû | ‚àÄ·µê n ‚àÇŒº, f n ‚â§ a } by
    rw [h_set_eq]
  ext1 a
  suffices h_meas_eq : Œº { x | ¬¨f x ‚â§ a } = Œº.trim hm { x | ¬¨f x ‚â§ a } by
    simp_rw [Set.mem_setOf_eq, ae_iff, h_meas_eq]
  refine (trim_measurableSet_eq hm ?_).symm
  refine @MeasurableSet.compl _ _ m (@measurableSet_le ‚Ñù‚â•0‚àû _ _ _ _ m _ _ _ _ _ hf ?_)
  exact @measurable_const _ _ _ m _

theorem essSup_trim (hm : m ‚â§ m0) {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : Measurable[m] f) :
    essSup f (Œº.trim hm) = essSup f Œº := by
  simp_rw [essSup]
  exact limsup_trim hm hf

theorem eLpNormEssSup_trim (hm : m ‚â§ m0) {f : Œ± ‚Üí E} (hf : StronglyMeasurable[m] f) :
    eLpNormEssSup f (Œº.trim hm) = eLpNormEssSup f Œº :=
  essSup_trim _ (@StronglyMeasurable.enorm _ m _ _ _ hf)

theorem eLpNorm_trim (hm : m ‚â§ m0) {f : Œ± ‚Üí E} (hf : StronglyMeasurable[m] f) :
    eLpNorm f p (Œº.trim hm) = eLpNorm f p Œº := by
  by_cases h0 : p = 0
  ¬∑ simp [h0]
  by_cases h_top : p = ‚àû
  ¬∑ simpa only [h_top, eLpNorm_exponent_top] using eLpNormEssSup_trim hm hf
  simpa only [eLpNorm_eq_eLpNorm' h0 h_top] using eLpNorm'_trim hm hf

theorem eLpNorm_trim_ae (hm : m ‚â§ m0) {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable[m] f (Œº.trim hm)) :
    eLpNorm f p (Œº.trim hm) = eLpNorm f p Œº := by
  rw [eLpNorm_congr_ae hf.ae_eq_mk, eLpNorm_congr_ae (ae_eq_of_ae_eq_trim hf.ae_eq_mk)]
  exact eLpNorm_trim hm hf.stronglyMeasurable_mk

theorem memLp_of_memLp_trim (hm : m ‚â§ m0) {f : Œ± ‚Üí E} (hf : MemLp f p (Œº.trim hm)) : MemLp f p Œº :=
  ‚ü®aestronglyMeasurable_of_aestronglyMeasurable_trim hm hf.1,
    (le_of_eq (eLpNorm_trim_ae hm hf.1).symm).trans_lt hf.2‚ü©

@[deprecated (since := "2025-02-21")]
alias memLp_of_memùìõp_trim := memLp_of_memLp_trim

end MeasureTheory
