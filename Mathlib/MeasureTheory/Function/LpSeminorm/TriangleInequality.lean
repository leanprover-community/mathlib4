/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.MeasureTheory.Function.LpSeminorm.Basic
import Mathlib.MeasureTheory.Integral.MeanInequalities

/-!
# Triangle inequality for `Lp`-seminorm

In this file we prove several versions of the triangle inequality for the `Lp` seminorm,
as well as simple corollaries.
-/

open Filter
open scoped ENNReal Topology

namespace MeasureTheory

variable {Œ± E Œµ Œµ' : Type*} {m : MeasurableSpace Œ±} [NormedAddCommGroup E]
  [TopologicalSpace Œµ] [ENormedAddMonoid Œµ] [TopologicalSpace Œµ'] [ENormedAddCommMonoid Œµ']
  {p : ‚Ñù‚â•0‚àû} {q : ‚Ñù} {Œº : Measure Œ±} {f g : Œ± ‚Üí Œµ}

theorem eLpNorm'_add_le (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº)
    (hq1 : 1 ‚â§ q) : eLpNorm' (f + g) q Œº ‚â§ eLpNorm' f q Œº + eLpNorm' g q Œº :=
  calc
    (‚à´‚Åª a, ‚Äñ(f + g) a‚Äñ‚Çë ^ q ‚àÇŒº) ^ (1 / q) ‚â§ (‚à´‚Åª a, ((‚Äñf ¬∑‚Äñ‚Çë) + (‚Äñg ¬∑‚Äñ‚Çë)) a ^ q ‚àÇŒº) ^ (1 / q) := by
      gcongr with a
      simp only [Pi.add_apply, ‚Üê ENNReal.coe_add, ENNReal.coe_le_coe, enorm_add_le]
    _ ‚â§ eLpNorm' f q Œº + eLpNorm' g q Œº := ENNReal.lintegral_Lp_add_le hf.enorm hg.enorm hq1

theorem eLpNorm'_add_le_of_le_one (hf : AEStronglyMeasurable f Œº) (hq0 : 0 ‚â§ q) (hq1 : q ‚â§ 1) :
    eLpNorm' (f + g) q Œº ‚â§ 2 ^ (1 / q - 1) * (eLpNorm' f q Œº + eLpNorm' g q Œº) :=
  calc
    (‚à´‚Åª a, ‚Äñ(f + g) a‚Äñ‚Çë ^ q ‚àÇŒº) ^ (1 / q) ‚â§ (‚à´‚Åª a, (((‚Äñf ¬∑‚Äñ‚Çë)) + (‚Äñg ¬∑‚Äñ‚Çë)) a ^ q ‚àÇŒº) ^ (1 / q) := by
      gcongr with a
      simp only [Pi.add_apply, ‚Üê ENNReal.coe_add, ENNReal.coe_le_coe, enorm_add_le]
    _ ‚â§ (2 : ‚Ñù‚â•0‚àû) ^ (1 / q - 1) * (eLpNorm' f q Œº + eLpNorm' g q Œº) :=
      ENNReal.lintegral_Lp_add_le_of_le_one hf.enorm hq0 hq1

theorem eLpNormEssSup_add_le :
    eLpNormEssSup (f + g) Œº ‚â§ eLpNormEssSup f Œº + eLpNormEssSup g Œº := by
  refine le_trans (essSup_mono_ae (Eventually.of_forall fun x => ?_)) (ENNReal.essSup_add_le _ _)
  simp_rw [Pi.add_apply]
  exact enorm_add_le _ _

theorem eLpNorm_add_le (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº)
    (hp1 : 1 ‚â§ p) : eLpNorm (f + g) p Œº ‚â§ eLpNorm f p Œº + eLpNorm g p Œº := by
  by_cases hp0 : p = 0
  ¬∑ simp [hp0]
  by_cases hp_top : p = ‚àû
  ¬∑ simp [hp_top, eLpNormEssSup_add_le]
  have hp1_real : 1 ‚â§ p.toReal := by
    rwa [‚Üê ENNReal.toReal_one, ENNReal.toReal_le_toReal ENNReal.one_ne_top hp_top]
  repeat rw [eLpNorm_eq_eLpNorm' hp0 hp_top]
  exact eLpNorm'_add_le hf hg hp1_real

/-- A constant for the inequality `‚Äñf + g‚Äñ_{L^p} ‚â§ C * (‚Äñf‚Äñ_{L^p} + ‚Äñg‚Äñ_{L^p})`. It is equal to `1`
for `p ‚â• 1` or `p = 0`, and `2^(1/p-1)` in the more tricky interval `(0, 1)`. -/
noncomputable def LpAddConst (p : ‚Ñù‚â•0‚àû) : ‚Ñù‚â•0‚àû :=
  if p ‚àà Set.Ioo (0 : ‚Ñù‚â•0‚àû) 1 then (2 : ‚Ñù‚â•0‚àû) ^ (1 / p.toReal - 1) else 1

theorem LpAddConst_of_one_le {p : ‚Ñù‚â•0‚àû} (hp : 1 ‚â§ p) : LpAddConst p = 1 := by
  rw [LpAddConst, if_neg]
  intro h
  exact lt_irrefl _ (h.2.trans_le hp)

theorem LpAddConst_zero : LpAddConst 0 = 1 := by
  rw [LpAddConst, if_neg]
  intro h
  exact lt_irrefl _ h.1

theorem LpAddConst_lt_top (p : ‚Ñù‚â•0‚àû) : LpAddConst p < ‚àû := by
  rw [LpAddConst]
  split_ifs with h
  ¬∑ apply ENNReal.rpow_lt_top_of_nonneg _ ENNReal.ofNat_ne_top
    rw [one_div, sub_nonneg, ‚Üê ENNReal.toReal_inv, ‚Üê ENNReal.toReal_one]
    exact ENNReal.toReal_mono (by simpa using h.1.ne') (ENNReal.one_le_inv.2 h.2.le)
  ¬∑ exact ENNReal.one_lt_top

theorem eLpNorm_add_le' (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº)
    (p : ‚Ñù‚â•0‚àû) : eLpNorm (f + g) p Œº ‚â§ LpAddConst p * (eLpNorm f p Œº + eLpNorm g p Œº) := by
  rcases eq_or_ne p 0 with (rfl | hp)
  ¬∑ simp only [eLpNorm_exponent_zero, add_zero, mul_zero, le_zero_iff]
  rcases lt_or_ge p 1 with (h'p | h'p)
  ¬∑ simp only [eLpNorm_eq_eLpNorm' hp (h'p.trans ENNReal.one_lt_top).ne]
    convert eLpNorm'_add_le_of_le_one hf ENNReal.toReal_nonneg _
    ¬∑ have : p ‚àà Set.Ioo (0 : ‚Ñù‚â•0‚àû) 1 := ‚ü®hp.bot_lt, h'p‚ü©
      simp only [LpAddConst, if_pos this]
    ¬∑ simpa using ENNReal.toReal_mono ENNReal.one_ne_top h'p.le
  ¬∑ simpa [LpAddConst_of_one_le h'p] using eLpNorm_add_le hf hg h'p

variable (Œº Œµ) in
/-- Technical lemma to control the addition of functions in `L^p` even for `p < 1`: Given `Œ¥ > 0`,
there exists `Œ∑` such that two functions bounded by `Œ∑` in `L^p` have a sum bounded by `Œ¥`. One
could take `Œ∑ = Œ¥ / 2` for `p ‚â• 1`, but the point of the lemma is that it works also for `p < 1`.
-/
theorem exists_Lp_half (p : ‚Ñù‚â•0‚àû) {Œ¥ : ‚Ñù‚â•0‚àû} (hŒ¥ : Œ¥ ‚â† 0) :
    ‚àÉ Œ∑ : ‚Ñù‚â•0‚àû,
      0 < Œ∑ ‚àß
        ‚àÄ (f g : Œ± ‚Üí Œµ), AEStronglyMeasurable f Œº ‚Üí AEStronglyMeasurable g Œº ‚Üí
          eLpNorm f p Œº ‚â§ Œ∑ ‚Üí eLpNorm g p Œº ‚â§ Œ∑ ‚Üí eLpNorm (f + g) p Œº < Œ¥ := by
  have :
    Tendsto (fun Œ∑ : ‚Ñù‚â•0‚àû => LpAddConst p * (Œ∑ + Œ∑)) (ùìù[>] 0) (ùìù (LpAddConst p * (0 + 0))) :=
    (ENNReal.Tendsto.const_mul (tendsto_id.add tendsto_id)
          (Or.inr (LpAddConst_lt_top p).ne)).mono_left
      nhdsWithin_le_nhds
  simp only [add_zero, mul_zero] at this
  rcases (((tendsto_order.1 this).2 Œ¥ hŒ¥.bot_lt).and self_mem_nhdsWithin).exists with ‚ü®Œ∑, hŒ∑, Œ∑pos‚ü©
  refine ‚ü®Œ∑, Œ∑pos, fun f g hf hg Hf Hg => ?_‚ü©
  calc
    eLpNorm (f + g) p Œº ‚â§ LpAddConst p * (eLpNorm f p Œº + eLpNorm g p Œº) := eLpNorm_add_le' hf hg p
    _ ‚â§ LpAddConst p * (Œ∑ + Œ∑) := by gcongr
    _ < Œ¥ := hŒ∑

theorem eLpNorm_sub_le' {f g : Œ± ‚Üí E}
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº)
    (p : ‚Ñù‚â•0‚àû) : eLpNorm (f - g) p Œº ‚â§ LpAddConst p * (eLpNorm f p Œº + eLpNorm g p Œº) := by
  simpa only [sub_eq_add_neg, eLpNorm_neg] using eLpNorm_add_le' hf hg.neg p

theorem eLpNorm_sub_le {f g : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº)
    (hp : 1 ‚â§ p) : eLpNorm (f - g) p Œº ‚â§ eLpNorm f p Œº + eLpNorm g p Œº := by
  simpa [LpAddConst_of_one_le hp] using eLpNorm_sub_le' hf hg p

theorem eLpNorm_add_lt_top (hf : MemLp f p Œº) (hg : MemLp g p Œº) :
    eLpNorm (f + g) p Œº < ‚àû :=
  calc
    eLpNorm (f + g) p Œº ‚â§ LpAddConst p * (eLpNorm f p Œº + eLpNorm g p Œº) :=
      eLpNorm_add_le' hf.aestronglyMeasurable hg.aestronglyMeasurable p
    _ < ‚àû := by
      apply ENNReal.mul_lt_top (LpAddConst_lt_top p)
      exact ENNReal.add_lt_top.2 ‚ü®hf.2, hg.2‚ü©

theorem eLpNorm'_sum_le [ContinuousAdd Œµ'] {Œπ} {f : Œπ ‚Üí Œ± ‚Üí Œµ'} {s : Finset Œπ}
    (hfs : ‚àÄ i, i ‚àà s ‚Üí AEStronglyMeasurable (f i) Œº) (hq1 : 1 ‚â§ q) :
    eLpNorm' (‚àë i ‚àà s, f i) q Œº ‚â§ ‚àë i ‚àà s, eLpNorm' (f i) q Œº :=
  Finset.le_sum_of_subadditive_on_pred (fun f : Œ± ‚Üí Œµ' => eLpNorm' f q Œº)
    (fun f => AEStronglyMeasurable f Œº) (eLpNorm'_zero (zero_lt_one.trans_le hq1))
    (fun _f _g hf hg => eLpNorm'_add_le hf hg hq1) (fun _f _g hf hg => hf.add hg) _ hfs

theorem eLpNorm_sum_le [ContinuousAdd Œµ'] {Œπ} {f : Œπ ‚Üí Œ± ‚Üí Œµ'} {s : Finset Œπ}
    (hfs : ‚àÄ i, i ‚àà s ‚Üí AEStronglyMeasurable (f i) Œº) (hp1 : 1 ‚â§ p) :
    eLpNorm (‚àë i ‚àà s, f i) p Œº ‚â§ ‚àë i ‚àà s, eLpNorm (f i) p Œº :=
  Finset.le_sum_of_subadditive_on_pred (fun f : Œ± ‚Üí Œµ' => eLpNorm f p Œº)
    (fun f => AEStronglyMeasurable f Œº) eLpNorm_zero (fun _f _g hf hg => eLpNorm_add_le hf hg hp1)
    (fun _f _g hf hg => hf.add hg) _ hfs

theorem MemLp.add [ContinuousAdd Œµ] (hf : MemLp f p Œº) (hg : MemLp g p Œº) : MemLp (f + g) p Œº :=
  ‚ü®AEStronglyMeasurable.add hf.1 hg.1, eLpNorm_add_lt_top hf hg‚ü©

theorem MemLp.sub {f g : Œ± ‚Üí E} (hf : MemLp f p Œº) (hg : MemLp g p Œº) : MemLp (f - g) p Œº := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg

theorem memLp_finset_sum [ContinuousAdd Œµ']
    {Œπ} (s : Finset Œπ) {f : Œπ ‚Üí Œ± ‚Üí Œµ'} (hf : ‚àÄ i ‚àà s, MemLp (f i) p Œº) :
    MemLp (fun a => ‚àë i ‚àà s, f i a) p Œº := by
  haveI : DecidableEq Œπ := Classical.decEq _
  revert hf
  refine Finset.induction_on s ?_ ?_
  ¬∑ simp only [MemLp.zero', Finset.sum_empty, imp_true_iff]
  ¬∑ intro i s his ih hf
    simp only [his, Finset.sum_insert, not_false_iff]
    exact (hf i (s.mem_insert_self i)).add (ih fun j hj => hf j (Finset.mem_insert_of_mem hj))

theorem memLp_finset_sum' [ContinuousAdd Œµ']
    {Œπ} (s : Finset Œπ) {f : Œπ ‚Üí Œ± ‚Üí Œµ'} (hf : ‚àÄ i ‚àà s, MemLp (f i) p Œº) :
    MemLp (‚àë i ‚àà s, f i) p Œº := by
  convert memLp_finset_sum s hf using 1
  ext x
  simp

end MeasureTheory
