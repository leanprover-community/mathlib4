/-
Copyright (c) 2020 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Integral.Lebesgue
import Mathlib.MeasureTheory.Function.EssSup
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic
import Mathlib.MeasureTheory.Function.SpecialFunctions.Basic

#align_import measure_theory.function.lp_seminorm from "leanprover-community/mathlib"@"c4015acc0a223449d44061e27ddac1835a3852b9"
/-!
# ‚Ñíp seminorm

In this file we define `‚Ñí·µñ` seminorm, denoted by `MeasureTheory.snorm f p Œº`.

- For `p = ‚àû`, it is the essential supemum of `(‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû)`.
- For `1 ‚â§ p < ‚àû`, it is the `p`-th root of the integral of `‚Äñf x‚Äñ‚Çä ^ p`.
- For `0 < p ‚â§ 1`, it is the integral of `‚Äñf x‚Äñ‚Çä ^ p`.
- For `p = 0`, it is the measure of the support of `f`.

We also define a predicate `MeasureTheory.Mem‚Ñíp f p Œº`,
requesting that a function is almost everywhere strongly measurable and has finite `‚Ñí·µñ` seminorm.

This file contains definitions and a few trivial lemmas about these definitions.
More properties can be found in other files in this folder.

## Implementation notes

While different sources agree on the the choice of a seminorm on `‚Ñí·µñ` for `p ‚â• 1`,
there are two competing formulas for `0 < p < 1`:
the integral of `‚Äñf x‚Äñ‚Çä ^ p` and the `p`-th root of this integral.

The former seminorm satisfies the triangle inequality for all `p`
but is not homogeneous in `f`:
we have `snorm (c ‚Ä¢ f) p Œº = ‚Äñc‚Äñ‚Çä ^ p * snorm f p Œº`
instead of `snorm (c ‚Ä¢ f) p Œº = ‚Äñc‚Äñ‚Çä * snorm f p Œº`
(both formulas omit some type conversions).

The latter formula satisfies `snorm (c ‚Ä¢ f) p Œº = ‚Äñc‚Äñ‚Çä * snorm f p Œº`
but needs an extra multiplicative constant in the triangle inequality:
`snorm (f + g) p Œº ‚â§ C p * (snorm f p Œº + snorm g p Œº)`.

We choose the former formula so that the `L·µñ` space is a normed group for all `p`.
-/

noncomputable section

open TopologicalSpace MeasureTheory Filter Function ENNReal
open scoped NNReal BigOperators Topology MeasureTheory

variable {Œ± E F G : Type*} {m m0 : MeasurableSpace Œ±} {p : ‚Ñù‚â•0‚àû} {q : ‚Ñù} {Œº ŒΩ : Measure Œ±}
  [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]

namespace MeasureTheory

section Definitions

/-- `‚Ñíp` seminorm, equal to `0` for `p=0`, to `(‚à´ ‚Äñf a‚Äñ^p ‚àÇŒº) ^ (1/p)` for `0 < p < ‚àû` and to
`essSup ‚Äñf‚Äñ Œº` for `p = ‚àû`. -/
def snorm (f : Œ± ‚Üí F) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) : ‚Ñù‚â•0‚àû :=
  if p = 0 then Œº f.support
  else if p = ‚àû then essSup (‚Äñf ¬∑‚Äñ‚Çä : Œ± ‚Üí ‚Ñù‚â•0‚àû) Œº
  else if p < 1 then ‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº
  else (‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº) ^ (1 / p.toReal)
#align measure_theory.snorm MeasureTheory.snorm

#noalign measure_theory.snorm_eq_snorm'

@[simp] lemma snorm_exponent_zero (f : Œ± ‚Üí F) : snorm f 0 Œº = Œº f.support := if_pos rfl
#align measure_theory.snorm_exponent_zero MeasureTheory.snorm_exponent_zero‚Çì

lemma snorm_exponent_top (f : Œ± ‚Üí F) : snorm f ‚àû Œº = essSup (‚Äñf ¬∑‚Äñ‚Çä : Œ± ‚Üí ‚Ñù‚â•0‚àû) Œº := rfl
#align measure_theory.snorm_exponent_top MeasureTheory.snorm_exponent_top

lemma snorm_of_one_le_ne_top (f : Œ± ‚Üí F) (hp_one_le : 1 ‚â§ p) (hp_ne_top : p ‚â† ‚àû) (Œº : Measure Œ±) :
    snorm f p Œº = (‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº) ^ (1 / p.toReal) := by
  simp [snorm, hp_ne_top, hp_one_le.not_lt, (one_pos.trans_le hp_one_le).ne']
#align measure_theory.snorm_eq_lintegral_rpow_nnnorm MeasureTheory.snorm_of_one_le_ne_top

lemma snorm_of_ne_zero_le_one (f : Œ± ‚Üí F) (hp_ne_zero : p ‚â† 0) (hp_le_one : p ‚â§ 1)
    (Œº : Measure Œ±) : snorm f p Œº = ‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº := by
  rcases hp_le_one.eq_or_lt with rfl | _ <;>
    simp [*, snorm, ne_top_of_le_ne_top coe_ne_top hp_le_one]

lemma snorm_coe_of_one_le {p : ‚Ñù‚â•0} (f : Œ± ‚Üí F) (hp : 1 ‚â§ p) (Œº : Measure Œ±) :
    snorm f p Œº = (‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ (p : ‚Ñù) ‚àÇŒº) ^ (1 / (p : ‚Ñù)) :=
  snorm_of_one_le_ne_top _ (coe_le_coe.2 hp) coe_ne_top _

lemma snorm_coe_of_ne_zero_le_one {p : ‚Ñù‚â•0} (f : Œ± ‚Üí F) (hp0 : p ‚â† 0) (hp1 : p ‚â§ 1)
    (Œº : Measure Œ±) : snorm f p Œº = ‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ (p : ‚Ñù) ‚àÇŒº :=
  snorm_of_ne_zero_le_one _ (coe_ne_zero.2 hp0) (coe_le_coe.2 hp1) _

@[simp]
lemma snorm_exponent_one (f : Œ± ‚Üí F) : snorm f 1 Œº = ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çä ‚àÇŒº := by
  simp [snorm_of_one_le_ne_top]
#align measure_theory.snorm_one_eq_lintegral_nnnorm MeasureTheory.snorm_exponent_one

/-- The property that `f:Œ±‚ÜíE` is ae strongly measurable and `(‚à´ ‚Äñf a‚Äñ^p ‚àÇŒº)^(1/p)` is finite
if `p < ‚àû`, or `essSup f < ‚àû` if `p = ‚àû`. -/
def Mem‚Ñíp {Œ±} {_ : MeasurableSpace Œ±} (f : Œ± ‚Üí E) (p : ‚Ñù‚â•0‚àû)
    (Œº : Measure Œ± := by volume_tac) : Prop :=
  AEStronglyMeasurable f Œº ‚àß snorm f p Œº < ‚àû
#align measure_theory.mem_‚Ñíp MeasureTheory.Mem‚Ñíp

theorem Mem‚Ñíp.aestronglyMeasurable {f : Œ± ‚Üí E} {p : ‚Ñù‚â•0‚àû} (h : Mem‚Ñíp f p Œº) :
    AEStronglyMeasurable f Œº :=
  h.1
#align measure_theory.mem_‚Ñíp.ae_strongly_measurable MeasureTheory.Mem‚Ñíp.aestronglyMeasurable

theorem lintegral_rpow_nnnorm_eq_rpow_snorm {f : Œ± ‚Üí F} (hq1_lt : 1 ‚â§ q) :
    (‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ q ‚àÇŒº) = snorm f (.ofReal q) Œº ^ q := by
  rw [snorm_of_one_le_ne_top, toReal_ofReal, ‚Üê ENNReal.rpow_mul, one_div_mul_cancel,
    ENNReal.rpow_one] <;> try { linarith } <;> simp [*]
#align measure_theory.lintegral_rpow_nnnorm_eq_rpow_snorm' MeasureTheory.lintegral_rpow_nnnorm_eq_rpow_snorm

theorem lintegral_rpow_nnnorm_eq_snorm {f : Œ± ‚Üí F} (hq_pos : 0 < q) (hq_le_1 : q ‚â§ 1) :
    (‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ q ‚àÇŒº) = snorm f (.ofReal q) Œº := by
  rw [snorm_of_ne_zero_le_one, ENNReal.toReal_ofReal] <;> try { linarith } <;> simp [*]

theorem Mem‚Ñíp.snorm_lt_top {f : Œ± ‚Üí E} (hfp : Mem‚Ñíp f p Œº) : snorm f p Œº < ‚àû := hfp.2
#align measure_theory.mem_‚Ñíp.snorm_lt_top MeasureTheory.Mem‚Ñíp.snorm_lt_top

theorem Mem‚Ñíp.snorm_ne_top {f : Œ± ‚Üí E} (hfp : Mem‚Ñíp f p Œº) : snorm f p Œº ‚â† ‚àû := hfp.2.ne
#align measure_theory.mem_‚Ñíp.snorm_ne_top MeasureTheory.Mem‚Ñíp.snorm_ne_top

theorem snorm_lt_top_iff_lintegral {f : Œ± ‚Üí F} (hp_ne_zero : p ‚â† 0)
    (hp_ne_top : p ‚â† ‚àû) : snorm f p Œº < ‚àû ‚Üî (‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº) < ‚àû := by
  cases le_total p 1 with
  | inl hp1 => rw [snorm_of_ne_zero_le_one _ hp_ne_zero hp1]
  | inr hp1 =>
    rw [snorm_of_one_le_ne_top _ hp1 hp_ne_top, ENNReal.rpow_lt_top_iff_of_pos]
    exact div_pos one_pos <| toReal_pos hp_ne_zero hp_ne_top
#align measure_theory.snorm_lt_top_iff_lintegral_rpow_nnnorm_lt_top MeasureTheory.snorm_lt_top_iff_lintegral

lemma snorm_ofReal_lt_top_iff_lintegral {f : Œ± ‚Üí F} (hq_pos : 0 < q) :
    snorm f (.ofReal q) Œº < ‚àû ‚Üî ‚à´‚Åª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ q ‚àÇŒº < ‚àû := by
  rw [snorm_lt_top_iff_lintegral, toReal_ofReal hq_pos.le] <;> simp [*]

#noalign measure_theory.lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top
#noalign measure_theory.lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top

end Definitions

section ExponentTop

theorem ae_le_snorm_top {f : Œ± ‚Üí F} : ‚àÄ·µê y ‚àÇŒº, ‚Äñf y‚Äñ‚Çä ‚â§ snorm f ‚àû Œº := ae_le_essSup
#align measure_theory.ae_le_snorm_ess_sup MeasureTheory.ae_le_snorm_top

theorem meas_snorm_top_lt {f : Œ± ‚Üí F} : Œº { y | snorm f ‚àû Œº < ‚Äñf y‚Äñ‚Çä } = 0 := meas_essSup_lt
#align measure_theory.meas_snorm_ess_sup_lt MeasureTheory.meas_snorm_top_lt

lemma snorm_top_le_iff {f : Œ± ‚Üí E} {C : ‚Ñù‚â•0‚àû} : snorm f ‚àû Œº ‚â§ C ‚Üî ‚àÄ·µê x ‚àÇŒº, (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ‚â§ C := by
  rw [snorm_exponent_top]
  exact ‚ü®fun h ‚Ü¶ (ENNReal.ae_le_essSup (‚Äñf ¬∑‚Äñ‚Çä)).mono fun x hx ‚Ü¶ hx.trans h, essSup_le_of_ae_le C‚ü©

@[mono]
theorem snorm_top_mono_measure (f : Œ± ‚Üí F) (hŒºŒΩ : ŒΩ ‚â™ Œº) : snorm f ‚àû ŒΩ ‚â§ snorm f ‚àû Œº := by
  simp_rw [snorm_exponent_top]
  exact essSup_mono_measure hŒºŒΩ
#align measure_theory.snorm_ess_sup_mono_measure MeasureTheory.snorm_top_mono_measure
#noalign measure_theory.snorm'_mono_measure

@[simp]
theorem snorm_top_eq_zero_iff {f : Œ± ‚Üí F} : snorm f ‚àû Œº = 0 ‚Üî f =·µê[Œº] 0 := by
  simp [EventuallyEq, snorm]
#align measure_theory.snorm_ess_sup_eq_zero_iff MeasureTheory.snorm_top_eq_zero_iff

theorem snorm_top_le_coe {f : Œ± ‚Üí F} {C : ‚Ñù‚â•0} : snorm f ‚àû Œº ‚â§ C ‚Üî ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ C :=
  snorm_top_le_iff.trans <| by simp only [coe_le_coe]
#align measure_theory.snorm_ess_sup_le_of_ae_nnnorm_bound MeasureTheory.snorm_top_le_coe

theorem coe_nnnorm_ae_le_snorm_top (f : Œ± ‚Üí F) (Œº : Measure Œ±) :
    ‚àÄ·µê x ‚àÇŒº, (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ‚â§ snorm f ‚àû Œº :=
  snorm_top_le_iff.1 le_rfl
#align measure_theory.coe_nnnorm_ae_le_snorm_ess_sup MeasureTheory.coe_nnnorm_ae_le_snorm_top

theorem snorm_top_le_ofReal_of_ae_bound {f : Œ± ‚Üí F} {C : ‚Ñù} (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) :
    snorm f ‚àû Œº ‚â§ ENNReal.ofReal C :=
  snorm_top_le_coe.2 <| hfC.mono fun _x hx => hx.trans C.le_coe_toNNReal
#align measure_theory.snorm_ess_sup_le_of_ae_bound MeasureTheory.snorm_top_le_ofReal_of_ae_bound

theorem snorm_top_lt_top_of_ae_nnnorm_bound {f : Œ± ‚Üí F} {C : ‚Ñù‚â•0} (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ C) :
    snorm f ‚àû Œº < ‚àû :=
  (snorm_top_le_coe.2 hfC).trans_lt coe_lt_top
#align measure_theory.snorm_ess_sup_lt_top_of_ae_nnnorm_bound MeasureTheory.snorm_top_lt_top_of_ae_nnnorm_bound

theorem snorm_top_lt_top_of_ae_bound {f : Œ± ‚Üí F} {C : ‚Ñù} (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) :
    snorm f ‚àû Œº < ‚àû :=
  (snorm_top_le_ofReal_of_ae_bound hfC).trans_lt ENNReal.ofReal_lt_top
#align measure_theory.snorm_ess_sup_lt_top_of_ae_bound MeasureTheory.snorm_top_lt_top_of_ae_bound

theorem mem‚Ñíp_top_of_bound {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº) (C : ‚Ñù)
    (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) : Mem‚Ñíp f ‚àû Œº :=
  ‚ü®hf, snorm_top_lt_top_of_ae_bound hfC‚ü©
#align measure_theory.mem_‚Ñíp_top_of_bound MeasureTheory.mem‚Ñíp_top_of_bound

lemma snorm_top_piecewise {s : Set Œ±} (f g : Œ± ‚Üí E) [DecidablePred (¬∑ ‚àà s)]
    (hs : MeasurableSet s) :
    snorm (Set.piecewise s f g) ‚àû Œº =
      max (snorm f ‚àû (Œº.restrict s)) (snorm g ‚àû (Œº.restrict s·∂ú)) := by
  simp only [snorm_exponent_top, ‚Üê essSup_piecewise hs]
  congr with x
  by_cases hx : x ‚àà s <;> simp [hx]

end ExponentTop

section Mono

#noalign measure_theory.snorm'_mono_nnnorm_ae
#noalign measure_theory.snorm'_mono_ae
#noalign measure_theory.snorm'_congr_nnnorm_ae
#noalign measure_theory.snorm'_congr_norm_ae
#noalign measure_theory.snorm'_congr_ae
#noalign measure_theory.snorm_ess_sup_congr_ae
#noalign measure_theory.snorm_ess_sup_mono_nnnorm_ae

theorem snorm_mono_nnnorm_ae {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} (h : (‚Äñf ¬∑‚Äñ‚Çä) ‚â§·µê[Œº] (‚Äñg ¬∑‚Äñ‚Çä)) :
    snorm f p Œº ‚â§ snorm g p Œº := by
  have : ‚à´‚Åª x, (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº ‚â§ ‚à´‚Åª x, (‚Äñg x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº :=
    lintegral_mono_ae <| h.mono fun x hx ‚Ü¶ ENNReal.rpow_le_rpow (coe_le_coe.2 hx) toReal_nonneg
  simp only [snorm]
  split_ifs
  ¬∑ refine measure_mono_ae (h.mono fun x hx ‚Ü¶ mt fun hg ‚Ü¶ ?_)
    simpa [hg] using hx
  ¬∑ exact essSup_mono_ae (h.mono fun x hx => ENNReal.coe_le_coe.mpr hx)
  ¬∑ exact this
  ¬∑ exact ENNReal.rpow_le_rpow this (by positivity)
#align measure_theory.snorm_mono_nnnorm_ae MeasureTheory.snorm_mono_nnnorm_ae

theorem snorm_mono_ae {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ ‚Äñg x‚Äñ) :
    snorm f p Œº ‚â§ snorm g p Œº :=
  snorm_mono_nnnorm_ae h
#align measure_theory.snorm_mono_ae MeasureTheory.snorm_mono_ae

theorem snorm_mono_ae_real {f : Œ± ‚Üí F} {g : Œ± ‚Üí ‚Ñù} (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ g x) :
    snorm f p Œº ‚â§ snorm g p Œº :=
  snorm_mono_ae <| h.mono fun _x hx => hx.trans (le_abs_self _)
#align measure_theory.snorm_mono_ae_real MeasureTheory.snorm_mono_ae_real

theorem snorm_mono_nnnorm {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} (h : ‚àÄ x, ‚Äñf x‚Äñ‚Çä ‚â§ ‚Äñg x‚Äñ‚Çä) :
    snorm f p Œº ‚â§ snorm g p Œº :=
  snorm_mono_nnnorm_ae (eventually_of_forall fun x => h x)
#align measure_theory.snorm_mono_nnnorm MeasureTheory.snorm_mono_nnnorm

theorem snorm_mono {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ ‚Äñg x‚Äñ) : snorm f p Œº ‚â§ snorm g p Œº :=
  snorm_mono_ae (eventually_of_forall fun x => h x)
#align measure_theory.snorm_mono MeasureTheory.snorm_mono

theorem snorm_mono_real {f : Œ± ‚Üí F} {g : Œ± ‚Üí ‚Ñù} (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ g x) :
    snorm f p Œº ‚â§ snorm g p Œº :=
  snorm_mono_ae_real (eventually_of_forall fun x => h x)
#align measure_theory.snorm_mono_real MeasureTheory.snorm_mono_real

theorem Mem‚Ñíp.of_le {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} (hg : Mem‚Ñíp g p Œº) (hf : AEStronglyMeasurable f Œº)
    (hfg : (‚Äñf ¬∑‚Äñ) ‚â§·µê[Œº] (‚Äñg ¬∑‚Äñ)) : Mem‚Ñíp f p Œº :=
  ‚ü®hf, (snorm_mono_ae hfg).trans_lt hg.snorm_lt_top‚ü©
#align measure_theory.mem_‚Ñíp.of_le MeasureTheory.Mem‚Ñíp.of_le

alias Mem‚Ñíp.mono := Mem‚Ñíp.of_le
#align measure_theory.mem_‚Ñíp.mono MeasureTheory.Mem‚Ñíp.mono

theorem Mem‚Ñíp.mono_real {f : Œ± ‚Üí E} {g : Œ± ‚Üí ‚Ñù} (hg : Mem‚Ñíp g p Œº) (hf : AEStronglyMeasurable f Œº)
    (h : ‚àÄ·µê a ‚àÇŒº, ‚Äñf a‚Äñ ‚â§ g a) : Mem‚Ñíp f p Œº :=
  hg.mono hf <| h.mono fun _x hx => le_trans hx (le_abs_self _)
#align measure_theory.mem_‚Ñíp.mono' MeasureTheory.Mem‚Ñíp.mono_real

@[mono]
theorem snorm_mono_measure (f : Œ± ‚Üí F) (hŒºŒΩ : ŒΩ ‚â§ Œº) : snorm f p ŒΩ ‚â§ snorm f p Œº := by
  rcases eq_or_ne p ‚àû with rfl | hp_ne_top
  ¬∑ exact snorm_top_le_iff.2 <| ae_mono hŒºŒΩ ae_le_snorm_top
  rcases eq_or_ne p 0 with rfl | hp0
  ¬∑ simpa only [snorm_exponent_zero] using Measure.le_iff'.1 hŒºŒΩ _
  have : ‚à´‚Åª x, (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒΩ ‚â§ ‚à´‚Åª x, (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal ‚àÇŒº :=
    lintegral_mono' hŒºŒΩ le_rfl
  cases' le_total p 1 with hp1 hp1
  ¬∑ simpa only [snorm_of_ne_zero_le_one _ hp0 hp1]
  ¬∑ simp only [snorm_of_one_le_ne_top _ hp1 hp_ne_top]; gcongr
#align measure_theory.snorm_mono_measure MeasureTheory.snorm_mono_measure

theorem Mem‚Ñíp.mono_measure {f : Œ± ‚Üí E} (hŒºŒΩ : ŒΩ ‚â§ Œº) (hf : Mem‚Ñíp f p Œº) : Mem‚Ñíp f p ŒΩ :=
  ‚ü®hf.1.mono_measure hŒºŒΩ, (snorm_mono_measure f hŒºŒΩ).trans_lt hf.2‚ü©
#align measure_theory.mem_‚Ñíp.mono_measure MeasureTheory.Mem‚Ñíp.mono_measure

lemma snorm_restrict_le (f : Œ± ‚Üí F) (p : ‚Ñù‚â•0‚àû) (Œº : Measure Œ±) (s : Set Œ±) :
    snorm f p (Œº.restrict s) ‚â§ snorm f p Œº :=
  snorm_mono_measure f Measure.restrict_le_self

theorem Mem‚Ñíp.restrict (s : Set Œ±) {f : Œ± ‚Üí E} (hf : Mem‚Ñíp f p Œº) : Mem‚Ñíp f p (Œº.restrict s) :=
  hf.mono_measure Measure.restrict_le_self
#align measure_theory.mem_‚Ñíp.restrict MeasureTheory.Mem‚Ñíp.restrict

end Mono

section Congr

theorem snorm_congr_nnnorm_ae {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} (hfg : (‚Äñf ¬∑‚Äñ‚Çä) =·µê[Œº] (‚Äñg ¬∑‚Äñ‚Çä)) :
    snorm f p Œº = snorm g p Œº :=
  (snorm_mono_nnnorm_ae hfg.le).antisymm (snorm_mono_nnnorm_ae hfg.symm.le)
#align measure_theory.snorm_congr_nnnorm_ae MeasureTheory.snorm_congr_nnnorm_ae

theorem snorm_congr_norm_ae {f : Œ± ‚Üí F} {g : Œ± ‚Üí G} (hfg : (‚Äñf ¬∑‚Äñ) =·µê[Œº] (‚Äñg ¬∑‚Äñ)) :
    snorm f p Œº = snorm g p Œº :=
  snorm_congr_nnnorm_ae <| hfg.mono fun _x hx => NNReal.eq hx
#align measure_theory.snorm_congr_norm_ae MeasureTheory.snorm_congr_norm_ae

@[simp]
theorem snorm_norm (f : Œ± ‚Üí F) : snorm (fun x => ‚Äñf x‚Äñ) p Œº = snorm f p Œº :=
  snorm_congr_norm_ae <| eventually_of_forall fun _ => norm_norm _
#align measure_theory.snorm_norm MeasureTheory.snorm_norm
#noalign measure_theory.snorm'_norm

theorem snorm_congr_ae {f g : Œ± ‚Üí F} (hfg : f =·µê[Œº] g) : snorm f p Œº = snorm g p Œº :=
  snorm_congr_norm_ae <| hfg.fun_comp _
#align measure_theory.snorm_congr_ae MeasureTheory.snorm_congr_ae

theorem mem‚Ñíp_congr_ae {f g : Œ± ‚Üí E} (hfg : f =·µê[Œº] g) : Mem‚Ñíp f p Œº ‚Üî Mem‚Ñíp g p Œº := by
  simp only [Mem‚Ñíp, snorm_congr_ae hfg, aestronglyMeasurable_congr hfg]
#align measure_theory.mem_‚Ñíp_congr_ae MeasureTheory.mem‚Ñíp_congr_ae

theorem Mem‚Ñíp.ae_eq {f g : Œ± ‚Üí E} (hf_Lp : Mem‚Ñíp f p Œº) (hfg : f =·µê[Œº] g) : Mem‚Ñíp g p Œº :=
  (mem‚Ñíp_congr_ae hfg).1 hf_Lp
#align measure_theory.mem_‚Ñíp.ae_eq MeasureTheory.Mem‚Ñíp.ae_eq

theorem Mem‚Ñíp.congr_norm {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} (hf : Mem‚Ñíp f p Œº) (hg : AEStronglyMeasurable g Œº)
    (h : (‚Äñf ¬∑‚Äñ) =·µê[Œº] (‚Äñg ¬∑‚Äñ)) : Mem‚Ñíp g p Œº :=
  hf.mono hg <| EventuallyEq.le <| EventuallyEq.symm h
#align measure_theory.mem_‚Ñíp.congr_norm MeasureTheory.Mem‚Ñíp.congr_norm

theorem mem‚Ñíp_congr_norm {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) (h : (‚Äñf ¬∑‚Äñ) =·µê[Œº] (‚Äñg ¬∑‚Äñ)) : Mem‚Ñíp f p Œº ‚Üî Mem‚Ñíp g p Œº :=
  ‚ü®fun h2f => h2f.congr_norm hg h, fun h2g => h2g.congr_norm hf <| EventuallyEq.symm h‚ü©
#align measure_theory.mem_‚Ñíp_congr_norm MeasureTheory.mem‚Ñíp_congr_norm

theorem snorm_indicator_sub_indicator (s t : Set Œ±) (f : Œ± ‚Üí E) :
    snorm (s.indicator f - t.indicator f) p Œº = snorm ((s ‚àÜ t).indicator f) p Œº :=
  snorm_congr_norm_ae <| ae_of_all _ fun x ‚Ü¶ by
    simp only [Pi.sub_apply, Set.apply_indicator_symmDiff norm_neg]

theorem Mem‚Ñíp.norm {f : Œ± ‚Üí E} (h : Mem‚Ñíp f p Œº) : Mem‚Ñíp (fun x => ‚Äñf x‚Äñ) p Œº :=
  h.of_le h.aestronglyMeasurable.norm (eventually_of_forall fun x => by simp)
#align measure_theory.mem_‚Ñíp.norm MeasureTheory.Mem‚Ñíp.norm

theorem mem‚Ñíp_norm_iff {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº) :
    Mem‚Ñíp (fun x => ‚Äñf x‚Äñ) p Œº ‚Üî Mem‚Ñíp f p Œº :=
  ‚ü®fun h => ‚ü®hf, by rw [‚Üê snorm_norm]; exact h.2‚ü©, fun h => h.norm‚ü©
#align measure_theory.mem_‚Ñíp_norm_iff MeasureTheory.mem‚Ñíp_norm_iff

end Congr

section Zero

theorem mem‚Ñíp_zero_iff_aestronglyMeasurable {f : Œ± ‚Üí E} :
    Mem‚Ñíp f 0 Œº ‚Üî AEStronglyMeasurable f Œº ‚àß Œº f.support < ‚àû :=
  by rw [Mem‚Ñíp, snorm_exponent_zero]
#align measure_theory.mem_‚Ñíp_zero_iff_ae_strongly_measurable MeasureTheory.mem‚Ñíp_zero_iff_aestronglyMeasurable

#noalign measure_theory.snorm'_zero
#noalign measure_theory.snorm'_zero'
#noalign measure_theory.snorm_ess_sup_zero

@[simp]
theorem snorm_zero' : snorm (fun _ : Œ± => (0 : F)) p Œº = 0 := by
  rcases eq_or_ne p 0 with rfl | hp0; ¬∑ simp
  rcases eq_or_ne p ‚àû with rfl | hp_top; ¬∑ simp [snorm_exponent_top, Pi.zero_def, EventuallyEq.rfl]
  by_cases hlt : p < 1 <;> simp [snorm, toReal_pos, *]
#align measure_theory.snorm_zero' MeasureTheory.snorm_zero'

@[simp]
theorem snorm_zero : snorm (0 : Œ± ‚Üí F) p Œº = 0 := snorm_zero'
#align measure_theory.snorm_zero MeasureTheory.snorm_zero

@[simp]
theorem snorm_measure_zero {f : Œ± ‚Üí F} : snorm f p (0 : Measure Œ±) = 0 := by
  simp (config := {contextual := true}) [snorm, ENNReal.toReal_pos]
#align measure_theory.snorm_measure_zero MeasureTheory.snorm_measure_zero

theorem zero_mem‚Ñíp : Mem‚Ñíp (0 : Œ± ‚Üí E) p Œº :=
  ‚ü®aestronglyMeasurable_zero, by rw [snorm_zero]; exact ENNReal.coe_lt_top‚ü©
#align measure_theory.zero_mem_‚Ñíp MeasureTheory.zero_mem‚Ñíp

theorem zero_mem_‚Ñíp' : Mem‚Ñíp (fun _ : Œ± => (0 : E)) p Œº := zero_mem‚Ñíp (E := E)
#align measure_theory.zero_mem_‚Ñíp' MeasureTheory.zero_mem_‚Ñíp'

theorem snorm_eq_zero_iff {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº) :
    snorm f p Œº = 0 ‚Üî f =·µê[Œº] 0 := by
  unfold snorm; split_ifs
  ¬∑ rfl
  ¬∑ simp [EventuallyEq]
  ¬∑ simp [hf.ennnorm.pow_const p.toReal, EventuallyEq, ENNReal.toReal_pos, *]
  ¬∑ simp [hf.ennnorm.pow_const p.toReal, EventuallyEq, ENNReal.toReal_pos,
      ENNReal.toReal_nonneg.not_lt, *]
#align measure_theory.snorm_eq_zero_iff MeasureTheory.snorm_eq_zero_iff

#noalign measure_theory.snorm'_measure_zero_of_pos
#noalign measure_theory.snorm'_measure_zero_of_exponent_zero
#noalign measure_theory.snorm'_measure_zero_of_neg
#noalign measure_theory.snorm_ess_sup_measure_zero

end Zero

section Neg

#noalign measure_theory.snorm'_neg

@[simp]
theorem snorm_neg {f : Œ± ‚Üí F} : snorm (-f) p Œº = snorm f p Œº := by simp [snorm]
#align measure_theory.snorm_neg MeasureTheory.snorm_neg

theorem Mem‚Ñíp.neg {f : Œ± ‚Üí E} (hf : Mem‚Ñíp f p Œº) : Mem‚Ñíp (-f) p Œº :=
  ‚ü®AEStronglyMeasurable.neg hf.1, by simp [hf.right]‚ü©
#align measure_theory.mem_‚Ñíp.neg MeasureTheory.Mem‚Ñíp.neg

theorem mem‚Ñíp_neg_iff {f : Œ± ‚Üí E} : Mem‚Ñíp (-f) p Œº ‚Üî Mem‚Ñíp f p Œº :=
  ‚ü®fun h => neg_neg f ‚ñ∏ h.neg, Mem‚Ñíp.neg‚ü©  
#align measure_theory.mem_‚Ñíp_neg_iff MeasureTheory.mem‚Ñíp_neg_iff

end Neg

section Const

@[simp]
lemma snorm_const_top [NeZero Œº] (c : F) : snorm (fun _ ‚Ü¶ c) ‚àû Œº = ‚Äñc‚Äñ‚Çä := by
  simp [snorm_exponent_top]

lemma snorm_const_coe_of_one_le {p : ‚Ñù‚â•0} (c : F) (hp : 1 ‚â§ p) :
    snorm (fun _ ‚Ü¶ c) p Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) * Œº Set.univ ^ (1 / (p : ‚Ñù)) := by
  rw [snorm_coe_of_one_le _ hp, lintegral_const, mul_rpow_of_nonneg _ _ (by positivity),
    ‚Üê ENNReal.rpow_mul, mul_one_div, div_self, ENNReal.rpow_one]
  exact NNReal.coe_ne_zero.2 (one_pos.trans_le hp).ne'

lemma snorm_const_ofReal_of_one_le (c : F) (one_le_q : 1 ‚â§ q) :
    snorm (fun _ ‚Ü¶ c) (.ofReal q) Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) * Œº Set.univ ^ (1 / q) := by
  lift q to ‚Ñù‚â•0 using zero_le_one.trans one_le_q
  rw [ENNReal.ofReal_coe_nnreal, snorm_const_coe_of_one_le c one_le_q]

lemma snorm_const_of_one_le_ne_top (c : F) (hp : 1 ‚â§ p) (hp_ne_top : p ‚â† ‚àû) :
    snorm (fun _ ‚Ü¶ c) p Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) * Œº Set.univ ^ (1 / p.toReal) := by
  lift p to ‚Ñù‚â•0 using hp_ne_top
  exact snorm_const_coe_of_one_le c (coe_le_coe.1 hp)
#align measure_theory.snorm_const' MeasureTheory.snorm_const_of_one_le_ne_top

lemma snorm_const_of_one_le [NeZero Œº] (c : F) (hp : 1 ‚â§ p) :
    snorm (fun _ ‚Ü¶ c) p Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) * Œº Set.univ ^ (1 / p.toReal) := by
  rcases eq_or_ne p ‚àû with rfl | hp'
  ¬∑ simp
  ¬∑ exact snorm_const_of_one_le_ne_top c hp hp'
#align measure_theory.snorm_const MeasureTheory.snorm_const_of_one_le

lemma snorm_const_eq_ennorm [IsProbabilityMeasure Œº] (c : F) (hp : 1 ‚â§ p) :
    snorm (fun _ ‚Ü¶ c) p Œº = ‚Äñc‚Äñ‚Çä := by
  simp [snorm_const_of_one_le c hp]

lemma snorm_const_coe_of_ne_zero_le_one {p : ‚Ñù‚â•0} {c : F} (h0 : c ‚â† 0 ‚à® p ‚â† 0) (hp1 : p ‚â§ 1) :
    snorm (fun _ ‚Ü¶ c) p Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ (p : ‚Ñù) * Œº Set.univ := by
  rcases eq_or_ne p 0 with rfl | hp0
  ¬∑ have hc0 : c ‚â† 0 := h0.resolve_right fun h ‚Ü¶ h rfl
    simp [hc0, Function.support_const]
  ¬∑ rw [snorm_coe_of_ne_zero_le_one _ hp0 hp1, lintegral_const]

lemma snorm_const_ofReal_of_pos_le_one (c : F) (hq0 : 0 < q) (hq1 : q ‚â§ 1) :
    snorm (fun _ ‚Ü¶ c) (.ofReal q) Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ q * Œº Set.univ := by
  lift q to ‚Ñù‚â•0 using hq0.le
  rw [NNReal.coe_pos] at hq0
  rw [ENNReal.ofReal_coe_nnreal, snorm_const_coe_of_ne_zero_le_one (.inr hq0.ne') hq1]

lemma snorm_const_of_ne_zero_le_one {c : F} (h0 : c ‚â† 0 ‚à® p ‚â† 0) (hp1 : p ‚â§ 1) :
    snorm (fun _ ‚Ü¶ c) p Œº = (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.toReal * Œº Set.univ := by
  lift p to ‚Ñù‚â•0 using ne_top_of_le_ne_top one_ne_top hp1
  apply snorm_const_coe_of_ne_zero_le_one <;> assumption_mod_cast

#noalign measure_theory.snorm'_const
#noalign measure_theory.snorm'_const'
#noalign measure_theory.snorm_ess_sup_const
#noalign measure_theory.snorm'_const_of_is_probability_measure

theorem snorm_const_lt_top_iff {p : ‚Ñù‚â•0‚àû} {c : F} :
    snorm (fun _ : Œ± => c) p Œº < ‚àû ‚Üî c = 0 ‚à® p = ‚àû ‚à® Œº Set.univ < ‚àû := by
  rcases eq_zero_or_neZero Œº with rfl | hŒº; ¬∑ simp
  rcases eq_or_ne c 0 with rfl | hc; ¬∑ simp
  rcases eq_or_ne p ‚àû with rfl | hp; ¬∑ simp
  simp only [*, false_or]
  lift p to ‚Ñù‚â•0 using hp
  cases' le_total p 1 with hp1 hp1
  ¬∑ simp [snorm_const_coe_of_ne_zero_le_one (.inl hc) hp1, lt_top_iff_ne_top, mul_eq_top, *]
  ¬∑ have : p ‚â† 0 := (one_pos.trans_le hp1).ne'
    simp [snorm_const_coe_of_one_le c hp1, lt_top_iff_ne_top, mul_eq_top, NeZero.ne, *]
#align measure_theory.snorm_const_lt_top_iff MeasureTheory.snorm_const_lt_top_iff

theorem mem‚Ñíp_const_iff {p : ‚Ñù‚â•0‚àû} {c : E} :
    Mem‚Ñíp (fun _ : Œ± => c) p Œº ‚Üî c = 0 ‚à® p = ‚àû ‚à® Œº Set.univ < ‚àû := by
  simp only [Mem‚Ñíp, aestronglyMeasurable_const, true_and, snorm_const_lt_top_iff]
#align measure_theory.mem_‚Ñíp_const_iff MeasureTheory.mem‚Ñíp_const_iff

theorem mem‚Ñíp_const (c : E) [IsFiniteMeasure Œº] : Mem‚Ñíp (fun _ : Œ± => c) p Œº :=
  mem‚Ñíp_const_iff.2 <| .inr <| .inr <| measure_lt_top _ _
#align measure_theory.mem_‚Ñíp_const MeasureTheory.mem‚Ñíp_const

theorem mem‚Ñíp_top_const (c : E) : Mem‚Ñíp (fun _ : Œ± => c) ‚àû Œº :=
  mem‚Ñíp_const_iff.2 <| .inr <| .inl rfl
#align measure_theory.mem_‚Ñíp_top_const MeasureTheory.mem‚Ñíp_top_const

theorem Mem‚Ñíp.of_bound [IsFiniteMeasure Œº] {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº) (C : ‚Ñù)
    (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) : Mem‚Ñíp f p Œº :=
  (mem‚Ñíp_const C).of_le hf (hfC.mono fun _x hx => le_trans hx (le_abs_self _))
#align measure_theory.mem_‚Ñíp.of_bound MeasureTheory.Mem‚Ñíp.of_bound

-- theorem snorm_le_of_ae_nnnorm_bound {f : Œ± ‚Üí F} {C : ‚Ñù‚â•0} (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ C) :
--     snorm f p Œº ‚â§ C ‚Ä¢ Œº Set.univ ^ p.toReal‚Åª¬π := by
--   rcases eq_zero_or_neZero Œº with rfl | hŒº
--   ¬∑ simp
--   by_cases hp : p = 0
--   ¬∑ simp [hp]
--   have : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ ‚Äñ(C : ‚Ñù)‚Äñ‚Çä := hfC.mono fun x hx => hx.trans_eq C.nnnorm_eq.symm
--   refine' (snorm_mono_ae this).trans_eq _
--   rw [snorm_const _ hp (NeZero.ne Œº), C.nnnorm_eq, one_div, ENNReal.smul_def, smul_eq_mul]
-- #align measure_theory.snorm_le_of_ae_nnnorm_bound MeasureTheory.snorm_le_of_ae_nnnorm_bound

-- theorem snorm_le_of_ae_bound {f : Œ± ‚Üí F} {C : ‚Ñù} (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) :
--     snorm f p Œº ‚â§ Œº Set.univ ^ p.toReal‚Åª¬π * ENNReal.ofReal C := by
--   rw [‚Üê mul_comm]
--   exact snorm_le_of_ae_nnnorm_bound (hfC.mono fun x hx => hx.trans C.le_coe_toNNReal)
-- #align measure_theory.snorm_le_of_ae_bound MeasureTheory.snorm_le_of_ae_bound

end Const

section RCLike

variable {ùïú : Type*} [RCLike ùïú] {f : Œ± ‚Üí ùïú}

protected lemma Mem‚Ñíp.re (hf : Mem‚Ñíp f p Œº) : Mem‚Ñíp (fun x => RCLike.re (f x)) p Œº :=
  hf.of_le (RCLike.continuous_re.comp_aestronglyMeasurable hf.1) <| ae_of_all _ fun _ ‚Ü¶
    RCLike.norm_re_le_norm _
#align measure_theory.mem_‚Ñíp.re MeasureTheory.Mem‚Ñíp.re

protected lemma Mem‚Ñíp.im (hf : Mem‚Ñíp f p Œº) : Mem‚Ñíp (fun x => RCLike.im (f x)) p Œº :=
  hf.of_le (RCLike.continuous_im.comp_aestronglyMeasurable hf.1) <| ae_of_all _ fun _ ‚Ü¶
    RCLike.norm_im_le_norm _
#align measure_theory.mem_‚Ñíp.im MeasureTheory.Mem‚Ñíp.im

end RCLike
