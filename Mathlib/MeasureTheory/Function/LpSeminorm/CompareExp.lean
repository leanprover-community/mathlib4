/-
Copyright (c) 2020 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Eric Wieser
-/
import Mathlib.Data.ENNReal.Holder
import Mathlib.MeasureTheory.Function.LpSeminorm.Basic
import Mathlib.MeasureTheory.Integral.MeanInequalities

/-!
# Compare Lp seminorms for different values of `p`

In this file we compare `MeasureTheory.eLpNorm'` and `MeasureTheory.eLpNorm` for different
exponents.
-/

open Filter ENNReal
open scoped Topology

namespace MeasureTheory

section SameSpace

variable {Œ± Œµ Œµ' : Type*} {m : MeasurableSpace Œ±} {Œº : Measure Œ±} {f : Œ± ‚Üí Œµ}
  [TopologicalSpace Œµ] [ContinuousENorm Œµ]
  [TopologicalSpace Œµ'] [ESeminormedAddMonoid Œµ']

theorem eLpNorm'_le_eLpNorm'_mul_rpow_measure_univ {p q : ‚Ñù} (hp0_lt : 0 < p) (hpq : p ‚â§ q)
    (hf : AEStronglyMeasurable f Œº) :
    eLpNorm' f p Œº ‚â§ eLpNorm' f q Œº * Œº Set.univ ^ (1 / p - 1 / q) := by
  have hq0_lt : 0 < q := lt_of_lt_of_le hp0_lt hpq
  by_cases hpq_eq : p = q
  ¬∑ rw [hpq_eq, sub_self, ENNReal.rpow_zero, mul_one]
  have hpq : p < q := lt_of_le_of_ne hpq hpq_eq
  let g := fun _ : Œ± => (1 : ‚Ñù‚â•0‚àû)
  have h_rw : (‚à´‚Åª a, ‚Äñf a‚Äñ‚Çë ^ p ‚àÇŒº) = ‚à´‚Åª a, (‚Äñf a‚Äñ‚Çë * g a) ^ p ‚àÇŒº :=
    lintegral_congr fun a => by simp [g]
  repeat' rw [eLpNorm'_eq_lintegral_enorm]
  rw [h_rw]
  let r := p * q / (q - p)
  have hpqr : 1 / p = 1 / q + 1 / r := by simp [field]
  calc
    (‚à´‚Åª a : Œ±, (‚Äñf a‚Äñ‚Çë * g a) ^ p ‚àÇŒº) ^ (1 / p) ‚â§
        (‚à´‚Åª a : Œ±, ‚Äñf a‚Äñ‚Çë ^ q ‚àÇŒº) ^ (1 / q) * (‚à´‚Åª a : Œ±, g a ^ r ‚àÇŒº) ^ (1 / r) :=
      ENNReal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Œº hf.enorm aemeasurable_const
    _ = (‚à´‚Åª a : Œ±, ‚Äñf a‚Äñ‚Çë ^ q ‚àÇŒº) ^ (1 / q) * Œº Set.univ ^ (1 / p - 1 / q) := by
      rw [hpqr]; simp [r, g]

theorem eLpNorm'_le_eLpNormEssSup_mul_rpow_measure_univ {q : ‚Ñù} (hq_pos : 0 < q) :
    eLpNorm' f q Œº ‚â§ eLpNormEssSup f Œº * Œº Set.univ ^ (1 / q) := by
  have h_le : (‚à´‚Åª a : Œ±, ‚Äñf a‚Äñ‚Çë ^ q ‚àÇŒº) ‚â§ ‚à´‚Åª _ : Œ±, eLpNormEssSup f Œº ^ q ‚àÇŒº := by
    refine lintegral_mono_ae ?_
    have h_nnnorm_le_eLpNorm_ess_sup := enorm_ae_le_eLpNormEssSup f Œº
    exact h_nnnorm_le_eLpNorm_ess_sup.mono fun x hx => by gcongr
  rw [eLpNorm', ‚Üê ENNReal.rpow_one (eLpNormEssSup f Œº)]
  nth_rw 2 [‚Üê mul_inv_cancel‚ÇÄ (ne_of_lt hq_pos).symm]
  rw [ENNReal.rpow_mul, one_div, ‚Üê ENNReal.mul_rpow_of_nonneg _ _ (by simp [hq_pos.le] : 0 ‚â§ q‚Åª¬π)]
  gcongr
  rwa [lintegral_const] at h_le

theorem eLpNorm_le_eLpNorm_mul_rpow_measure_univ {p q : ‚Ñù‚â•0‚àû} (hpq : p ‚â§ q)
    (hf : AEStronglyMeasurable f Œº) :
    eLpNorm f p Œº ‚â§ eLpNorm f q Œº * Œº Set.univ ^ (1 / p.toReal - 1 / q.toReal) := by
  by_cases hp0 : p = 0
  ¬∑ simp [hp0, zero_le]
  rw [‚Üê Ne] at hp0
  have hp0_lt : 0 < p := lt_of_le_of_ne (zero_le _) hp0.symm
  have hq0_lt : 0 < q := lt_of_lt_of_le hp0_lt hpq
  by_cases hq_top : q = ‚àû
  ¬∑ simp only [hq_top, _root_.div_zero, one_div, ENNReal.toReal_top, sub_zero, eLpNorm_exponent_top]
    by_cases hp_top : p = ‚àû
    ¬∑ simp [hp_top]
    rw [eLpNorm_eq_eLpNorm' hp0 hp_top]
    have hp_pos : 0 < p.toReal := ENNReal.toReal_pos hp0_lt.ne' hp_top
    refine (eLpNorm'_le_eLpNormEssSup_mul_rpow_measure_univ hp_pos).trans (le_of_eq ?_)
    congr
    exact one_div _
  have hp_lt_top : p < ‚àû := hpq.trans_lt (lt_top_iff_ne_top.mpr hq_top)
  have hp_pos : 0 < p.toReal := ENNReal.toReal_pos hp0_lt.ne' hp_lt_top.ne
  rw [eLpNorm_eq_eLpNorm' hp0_lt.ne.symm hp_lt_top.ne, eLpNorm_eq_eLpNorm' hq0_lt.ne.symm hq_top]
  have hpq_real : p.toReal ‚â§ q.toReal := ENNReal.toReal_mono hq_top hpq
  exact eLpNorm'_le_eLpNorm'_mul_rpow_measure_univ hp_pos hpq_real hf

theorem eLpNorm'_le_eLpNorm'_of_exponent_le {p q : ‚Ñù} (hp0_lt : 0 < p)
    (hpq : p ‚â§ q) (Œº : Measure Œ±) [IsProbabilityMeasure Œº] (hf : AEStronglyMeasurable f Œº) :
    eLpNorm' f p Œº ‚â§ eLpNorm' f q Œº := by
  have h_le_Œº := eLpNorm'_le_eLpNorm'_mul_rpow_measure_univ hp0_lt hpq hf
  rwa [measure_univ, ENNReal.one_rpow, mul_one] at h_le_Œº

theorem eLpNorm'_le_eLpNormEssSup {q : ‚Ñù} (hq_pos : 0 < q) [IsProbabilityMeasure Œº] :
    eLpNorm' f q Œº ‚â§ eLpNormEssSup f Œº :=
  (eLpNorm'_le_eLpNormEssSup_mul_rpow_measure_univ hq_pos).trans_eq (by simp [measure_univ])

theorem eLpNorm_le_eLpNorm_of_exponent_le {p q : ‚Ñù‚â•0‚àû} (hpq : p ‚â§ q) [IsProbabilityMeasure Œº]
    (hf : AEStronglyMeasurable f Œº) : eLpNorm f p Œº ‚â§ eLpNorm f q Œº :=
  (eLpNorm_le_eLpNorm_mul_rpow_measure_univ hpq hf).trans (le_of_eq (by simp [measure_univ]))

theorem eLpNorm'_lt_top_of_eLpNorm'_lt_top_of_exponent_le {p q : ‚Ñù} [IsFiniteMeasure Œº]
    (hf : AEStronglyMeasurable f Œº) (hfq_lt_top : eLpNorm' f q Œº < ‚àû) (hp_nonneg : 0 ‚â§ p)
    (hpq : p ‚â§ q) : eLpNorm' f p Œº < ‚àû := by
  rcases le_or_gt p 0 with hp_nonpos | hp_pos
  ¬∑ rw [le_antisymm hp_nonpos hp_nonneg]
    simp
  have hq_pos : 0 < q := lt_of_lt_of_le hp_pos hpq
  calc
    eLpNorm' f p Œº ‚â§ eLpNorm' f q Œº * Œº Set.univ ^ (1 / p - 1 / q) :=
      eLpNorm'_le_eLpNorm'_mul_rpow_measure_univ hp_pos hpq hf
    _ < ‚àû := by
      rw [ENNReal.mul_lt_top_iff]
      refine Or.inl ‚ü®hfq_lt_top, ENNReal.rpow_lt_top_of_nonneg ?_ (measure_ne_top Œº Set.univ)‚ü©
      rwa [le_sub_comm, sub_zero, one_div, one_div, inv_le_inv‚ÇÄ hq_pos hp_pos]

theorem MemLp.mono_exponent {p q : ‚Ñù‚â•0‚àû} [IsFiniteMeasure Œº] (hfq : MemLp f q Œº)
    (hpq : p ‚â§ q) : MemLp f p Œº := by
  obtain ‚ü®hfq_m, hfq_lt_top‚ü© := hfq
  by_cases hp0 : p = 0
  ¬∑ rwa [hp0, memLp_zero_iff_aestronglyMeasurable]
  rw [‚Üê Ne] at hp0
  refine ‚ü®hfq_m, ?_‚ü©
  by_cases hp_top : p = ‚àû
  ¬∑ have hq_top : q = ‚àû := by rwa [hp_top, top_le_iff] at hpq
    rw [hp_top]
    rwa [hq_top] at hfq_lt_top
  have hp_pos : 0 < p.toReal := ENNReal.toReal_pos hp0 hp_top
  by_cases hq_top : q = ‚àû
  ¬∑ rw [eLpNorm_eq_eLpNorm' hp0 hp_top]
    rw [hq_top, eLpNorm_exponent_top] at hfq_lt_top
    refine lt_of_le_of_lt (eLpNorm'_le_eLpNormEssSup_mul_rpow_measure_univ hp_pos) ?_
    refine ENNReal.mul_lt_top hfq_lt_top ?_
    exact ENNReal.rpow_lt_top_of_nonneg (by simp [hp_pos.le]) (measure_ne_top Œº Set.univ)
  have hq0 : q ‚â† 0 := by
    by_contra hq_eq_zero
    have hp_eq_zero : p = 0 := le_antisymm (by rwa [hq_eq_zero] at hpq) (zero_le _)
    rw [hp_eq_zero, ENNReal.toReal_zero] at hp_pos
    exact (lt_irrefl _) hp_pos
  have hpq_real : p.toReal ‚â§ q.toReal := ENNReal.toReal_mono hq_top hpq
  rw [eLpNorm_eq_eLpNorm' hp0 hp_top]
  rw [eLpNorm_eq_eLpNorm' hq0 hq_top] at hfq_lt_top
  exact eLpNorm'_lt_top_of_eLpNorm'_lt_top_of_exponent_le hfq_m hfq_lt_top hp_pos.le hpq_real

/-- If a function is supported on a finite-measure set and belongs to `‚Ñí^p`, then it belongs to
`‚Ñí^q` for any `q ‚â§ p`. -/
lemma MemLp.mono_exponent_of_measure_support_ne_top {p q : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí Œµ'} (hfq : MemLp f q Œº)
    {s : Set Œ±} (hf : ‚àÄ x, x ‚àâ s ‚Üí f x = 0) (hs : Œº s ‚â† ‚àû) (hpq : p ‚â§ q) : MemLp f p Œº := by
  have : (toMeasurable Œº s).indicator f = f := by
    apply Set.indicator_eq_self.2
    apply Function.support_subset_iff'.2 fun x hx ‚Ü¶ hf x ?_
    contrapose! hx
    exact subset_toMeasurable Œº s hx
  rw [‚Üê this, memLp_indicator_iff_restrict (measurableSet_toMeasurable Œº s)] at hfq ‚ä¢
  have : Fact (Œº (toMeasurable Œº s) < ‚àû) := ‚ü®by simpa [lt_top_iff_ne_top] using hs‚ü©
  exact hfq.mono_exponent hpq

end SameSpace

section Bilinear

variable {Œ± E F G : Type*} {m : MeasurableSpace Œ±}
  [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G] {Œº : Measure Œ±}
  {f : Œ± ‚Üí E} {g : Œ± ‚Üí F}

open NNReal

theorem eLpNorm_le_eLpNorm_top_mul_eLpNorm (p : ‚Ñù‚â•0‚àû) (f : Œ± ‚Üí E) {g : Œ± ‚Üí F}
    (hg : AEStronglyMeasurable g Œº) (b : E ‚Üí F ‚Üí G) (c : ‚Ñù‚â•0)
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñb (f x) (g x)‚Äñ‚Çä ‚â§ c * ‚Äñf x‚Äñ‚Çä * ‚Äñg x‚Äñ‚Çä) :
    eLpNorm (fun x => b (f x) (g x)) p Œº ‚â§ c * eLpNorm f ‚àû Œº * eLpNorm g p Œº := by
  calc
    eLpNorm (fun x => b (f x) (g x)) p Œº ‚â§ eLpNorm (fun x => (c : ‚Ñù) ‚Ä¢ ‚Äñf x‚Äñ * ‚Äñg x‚Äñ) p Œº :=
      eLpNorm_mono_ae_real h
    _ ‚â§ c * eLpNorm f ‚àû Œº * eLpNorm g p Œº := ?_
  simp only [smul_mul_assoc, ‚Üê Pi.smul_def, eLpNorm_const_smul]
  rw [Real.enorm_eq_ofReal c.coe_nonneg, ENNReal.ofReal_coe_nnreal, mul_assoc]
  gcongr
  obtain (rfl | rfl | hp) := ENNReal.trichotomy p
  ¬∑ simp
  ¬∑ rw [‚Üê eLpNorm_norm f, ‚Üê eLpNorm_norm g]
    simp_rw [eLpNorm_exponent_top, eLpNormEssSup_eq_essSup_enorm, enorm_mul, enorm_norm]
    exact ENNReal.essSup_mul_le (‚Äñf ¬∑‚Äñ‚Çë) (‚Äñg ¬∑‚Äñ‚Çë)
  obtain ‚ü®hp‚ÇÅ, hp‚ÇÇ‚ü© := ENNReal.toReal_pos_iff.mp hp
  simp_rw [eLpNorm_eq_lintegral_rpow_enorm hp‚ÇÅ.ne' hp‚ÇÇ.ne, eLpNorm_exponent_top, eLpNormEssSup,
    one_div, ENNReal.rpow_inv_le_iff hp, enorm_mul, enorm_norm]
  rw [ENNReal.mul_rpow_of_nonneg (hz := hp.le), ENNReal.rpow_inv_rpow hp.ne',
    ‚Üê lintegral_const_mul'' _ (by fun_prop)]
  simp only [‚Üê ENNReal.mul_rpow_of_nonneg (hz := hp.le)]
  apply lintegral_mono_ae
  filter_upwards [h, enorm_ae_le_eLpNormEssSup f Œº] with x hb hf
  gcongr
  exact hf

theorem eLpNorm_le_eLpNorm_mul_eLpNorm_top (p : ‚Ñù‚â•0‚àû) {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº)
    (g : Œ± ‚Üí F) (b : E ‚Üí F ‚Üí G) (c : ‚Ñù‚â•0)
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñb (f x) (g x)‚Äñ‚Çä ‚â§ c * ‚Äñf x‚Äñ‚Çä * ‚Äñg x‚Äñ‚Çä) :
    eLpNorm (fun x => b (f x) (g x)) p Œº ‚â§ c * eLpNorm f p Œº * eLpNorm g ‚àû Œº :=
  calc
    eLpNorm (fun x ‚Ü¶ b (f x) (g x)) p Œº ‚â§ c * eLpNorm g ‚àû Œº * eLpNorm f p Œº :=
      eLpNorm_le_eLpNorm_top_mul_eLpNorm p g hf (flip b) c <| by
        convert h using 3 with x
        simp only [mul_assoc, mul_comm ‚Äñf x‚Äñ‚Çä]
    _ = c *  eLpNorm f p Œº * eLpNorm g ‚àû Œº := by
      simp only [mul_assoc]; rw [mul_comm (eLpNorm _ _ _)]

theorem eLpNorm'_le_eLpNorm'_mul_eLpNorm' {p q r : ‚Ñù} (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) (b : E ‚Üí F ‚Üí G) (c : ‚Ñù‚â•0)
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñb (f x) (g x)‚Äñ‚Çä ‚â§ c * ‚Äñf x‚Äñ‚Çä * ‚Äñg x‚Äñ‚Çä) (hro_lt : 0 < r) (hrp : r < p)
    (hpqr : 1 / r = 1 / p + 1 / q) :
    eLpNorm' (fun x => b (f x) (g x)) r Œº ‚â§ c * eLpNorm' f p Œº * eLpNorm' g q Œº := by
  calc
    eLpNorm' (fun x => b (f x) (g x)) r Œº
      ‚â§ eLpNorm' (fun x ‚Ü¶ (c : ‚Ñù) ‚Ä¢ ‚Äñf x‚Äñ * ‚Äñg x‚Äñ) r Œº := by
      simp only [eLpNorm']
      gcongr ?_ ^ _
      refine lintegral_mono_ae <| h.mono fun a ha ‚Ü¶ ?_
      gcongr
      simp only [enorm_eq_nnnorm, ENNReal.coe_le_coe]
      simpa using ha
    _ ‚â§ c * eLpNorm' f p Œº * eLpNorm' g q Œº := by
      simp only [smul_mul_assoc, ‚Üê Pi.smul_def, eLpNorm'_const_smul _ hro_lt]
      rw [Real.enorm_eq_ofReal c.coe_nonneg, ENNReal.ofReal_coe_nnreal, mul_assoc]
      gcongr
      simpa only [eLpNorm', enorm_mul, enorm_norm] using
        ENNReal.lintegral_Lp_mul_le_Lq_mul_Lr hro_lt hrp hpqr Œº hf.enorm hg.enorm

/-- H√∂lder's inequality, as an inequality on the `‚Ñíp` seminorm of an elementwise operation
`fun x => b (f x) (g x)`. -/
theorem eLpNorm_le_eLpNorm_mul_eLpNorm_of_nnnorm {p q r : ‚Ñù‚â•0‚àû}
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) (b : E ‚Üí F ‚Üí G) (c : ‚Ñù‚â•0)
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñb (f x) (g x)‚Äñ‚Çä ‚â§ c * ‚Äñf x‚Äñ‚Çä * ‚Äñg x‚Äñ‚Çä) [hpqr : HolderTriple p q r] :
    eLpNorm (fun x => b (f x) (g x)) r Œº ‚â§ c * eLpNorm f p Œº * eLpNorm g q Œº := by
  have hpqr := hpqr.one_div_eq
  obtain (rfl | rfl | hp) := ENNReal.trichotomy p
  ¬∑ simp_all
  ¬∑ have : r = q := by simpa using hpqr
    exact this ‚ñ∏ eLpNorm_le_eLpNorm_top_mul_eLpNorm r f hg b c h
  obtain (rfl | rfl | hq) := ENNReal.trichotomy q
  ¬∑ simp_all
  ¬∑ have : r = p := by simpa using hpqr
    exact this ‚ñ∏ eLpNorm_le_eLpNorm_mul_eLpNorm_top p hf g b c h
  obtain ‚ü®hp‚ÇÅ, hp‚ÇÇ‚ü© := ENNReal.toReal_pos_iff.mp hp
  obtain ‚ü®hq‚ÇÅ, hq‚ÇÇ‚ü© := ENNReal.toReal_pos_iff.mp hq
  have hpqr' : 1 / r.toReal = 1 / p.toReal + 1 / q.toReal := by
    have := congr(ENNReal.toReal $(hpqr))
    rw [ENNReal.toReal_add (by simpa using hp‚ÇÅ.ne') (by simpa using hq‚ÇÅ.ne')] at this
    simpa
  have hr : 0 < r.toReal := one_div_pos.mp <| by rw [hpqr']; positivity
  obtain ‚ü®hr‚ÇÅ, hr‚ÇÇ‚ü© := ENNReal.toReal_pos_iff.mp hr
  have hrp : r.toReal < p.toReal := lt_of_one_div_lt_one_div hp <|
    hpqr' ‚ñ∏ lt_add_of_pos_right _ (by positivity)
  rw [eLpNorm_eq_eLpNorm', eLpNorm_eq_eLpNorm', eLpNorm_eq_eLpNorm']
  ¬∑ exact eLpNorm'_le_eLpNorm'_mul_eLpNorm' hf hg b c h hr hrp hpqr'
  all_goals first | positivity | finiteness

/-- H√∂lder's inequality, as an inequality on the `‚Ñíp` seminorm of an elementwise operation
`fun x => b (f x) (g x)`. -/
theorem eLpNorm_le_eLpNorm_mul_eLpNorm'_of_norm {p q r : ‚Ñù‚â•0‚àû} (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) (b : E ‚Üí F ‚Üí G) (c : ‚Ñù‚â•0)
    (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñb (f x) (g x)‚Äñ ‚â§ c * ‚Äñf x‚Äñ * ‚Äñg x‚Äñ) [hpqr : HolderTriple p q r] :
    eLpNorm (fun x => b (f x) (g x)) r Œº ‚â§ c * eLpNorm f p Œº * eLpNorm g q Œº :=
  eLpNorm_le_eLpNorm_mul_eLpNorm_of_nnnorm hf hg b c h

open NNReal in
theorem MemLp.of_bilin {p q r : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} (b : E ‚Üí F ‚Üí G) (c : ‚Ñù‚â•0)
    (hf : MemLp f p Œº) (hg : MemLp g q Œº)
    (h : AEStronglyMeasurable (fun x ‚Ü¶ b (f x) (g x)) Œº)
    (hb : ‚àÄ·µê (x : Œ±) ‚àÇŒº, ‚Äñb (f x) (g x)‚Äñ‚Çä ‚â§ c * ‚Äñf x‚Äñ‚Çä * ‚Äñg x‚Äñ‚Çä)
    [hpqr : HolderTriple p q r] :
    MemLp (fun x ‚Ü¶ b (f x) (g x)) r Œº := by
  refine ‚ü®h, ?_‚ü©
  apply (eLpNorm_le_eLpNorm_mul_eLpNorm_of_nnnorm hf.1 hg.1 b c hb (hpqr := hpqr)).trans_lt
  have := hf.2
  have := hg.2
  finiteness

end Bilinear

section IsBoundedSMul

variable {ùïú Œ± E F : Type*} {m : MeasurableSpace Œ±} {Œº : Measure Œ±} [NormedRing ùïú]
  [NormedAddCommGroup E] [MulActionWithZero ùïú E] [IsBoundedSMul ùïú E]
  [NormedAddCommGroup F] [MulActionWithZero ùïú F] [IsBoundedSMul ùïú F] {f : Œ± ‚Üí E}

theorem eLpNorm_smul_le_eLpNorm_top_mul_eLpNorm (p : ‚Ñù‚â•0‚àû) (hf : AEStronglyMeasurable f Œº)
    (œÜ : Œ± ‚Üí ùïú) : eLpNorm (œÜ ‚Ä¢ f) p Œº ‚â§ eLpNorm œÜ ‚àû Œº * eLpNorm f p Œº := by
  simpa using (eLpNorm_le_eLpNorm_top_mul_eLpNorm p œÜ hf (¬∑ ‚Ä¢ ¬∑) 1
    (.of_forall fun _ => by simpa using nnnorm_smul_le _ _) :)

theorem eLpNorm_smul_le_eLpNorm_mul_eLpNorm_top (p : ‚Ñù‚â•0‚àû) (f : Œ± ‚Üí E) {œÜ : Œ± ‚Üí ùïú}
    (hœÜ : AEStronglyMeasurable œÜ Œº) : eLpNorm (œÜ ‚Ä¢ f) p Œº ‚â§ eLpNorm œÜ p Œº * eLpNorm f ‚àû Œº := by
  simpa using (eLpNorm_le_eLpNorm_mul_eLpNorm_top p hœÜ f (¬∑ ‚Ä¢ ¬∑) 1
    (.of_forall fun _ => by simpa using nnnorm_smul_le _ _) :)

theorem eLpNorm'_smul_le_mul_eLpNorm' {p q r : ‚Ñù} {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº)
    {œÜ : Œ± ‚Üí ùïú} (hœÜ : AEStronglyMeasurable œÜ Œº) (hp0_lt : 0 < p) (hpq : p < q)
    (hpqr : 1 / p = 1 / q + 1 / r) : eLpNorm' (œÜ ‚Ä¢ f) p Œº ‚â§ eLpNorm' œÜ q Œº * eLpNorm' f r Œº := by
  simpa using eLpNorm'_le_eLpNorm'_mul_eLpNorm' hœÜ hf (¬∑ ‚Ä¢ ¬∑) 1
    (.of_forall fun _ => by simpa using nnnorm_smul_le _ _)
    hp0_lt hpq hpqr

/-- H√∂lder's inequality, as an inequality on the `‚Ñíp` seminorm of a scalar product `œÜ ‚Ä¢ f`. -/
theorem eLpNorm_smul_le_mul_eLpNorm {p q r : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí E} (hf : AEStronglyMeasurable f Œº)
    {œÜ : Œ± ‚Üí ùïú} (hœÜ : AEStronglyMeasurable œÜ Œº) [hpqr : HolderTriple p q r] :
    eLpNorm (œÜ ‚Ä¢ f) r Œº ‚â§ eLpNorm œÜ p Œº * eLpNorm f q Œº := by
  simpa using (eLpNorm_le_eLpNorm_mul_eLpNorm_of_nnnorm hœÜ hf (¬∑ ‚Ä¢ ¬∑) 1
      (.of_forall fun _ => by simpa using nnnorm_smul_le _ _) : _)

theorem MemLp.smul {p q r : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí E} {œÜ : Œ± ‚Üí ùïú} (hf : MemLp f q Œº) (hœÜ : MemLp œÜ p Œº)
    [hpqr : HolderTriple p q r] : MemLp (œÜ ‚Ä¢ f) r Œº :=
  ‚ü®hœÜ.1.smul hf.1,
    eLpNorm_smul_le_mul_eLpNorm hf.1 hœÜ.1 |>.trans_lt <|
      ENNReal.mul_lt_top hœÜ.eLpNorm_lt_top hf.eLpNorm_lt_top‚ü©

end IsBoundedSMul

section Mul

variable {Œ± : Type*} {_ : MeasurableSpace Œ±} {ùïú : Type*} [NormedRing ùïú] {Œº : Measure Œ±}
  {p q r : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí ùïú} {œÜ : Œ± ‚Üí ùïú}

theorem MemLp.mul (hf : MemLp f q Œº) (hœÜ : MemLp œÜ p Œº) [hpqr : HolderTriple p q r] :
    MemLp (œÜ * f) r Œº :=
  MemLp.smul hf hœÜ

/-- Variant of `MemLp.mul` where the function is written as `fun x ‚Ü¶ œÜ x * f x`
instead of `œÜ * f`. -/
theorem MemLp.mul' (hf : MemLp f q Œº) (hœÜ : MemLp œÜ p Œº) [hpqr : HolderTriple p q r] :
    MemLp (fun x ‚Ü¶ œÜ x * f x) r Œº :=
  MemLp.smul hf hœÜ

end Mul

section Prod
variable {Œπ Œ± ùïú : Type*} {_ : MeasurableSpace Œ±} [NormedCommRing ùïú] {Œº : Measure Œ±} {f : Œπ ‚Üí Œ± ‚Üí ùïú}
  {p : Œπ ‚Üí ‚Ñù‚â•0‚àû} {s : Finset Œπ}

open Finset in
/-- See `MemLp.prod'` for the applied version. -/
protected lemma MemLp.prod (hf : ‚àÄ i ‚àà s, MemLp (f i) (p i) Œº) :
    MemLp (‚àè i ‚àà s, f i) (‚àë i ‚àà s, (p i)‚Åª¬π)‚Åª¬π Œº := by
  induction s using cons_induction with
  | empty =>
    by_cases hŒº : Œº = 0 <;>
      simp [MemLp, eLpNormEssSup_const, hŒº, aestronglyMeasurable_const, Pi.one_def]
  | cons i s hi ih =>
    rw [prod_cons]
    exact (ih <| forall_of_forall_cons hf).mul (hf i <| mem_cons_self ..) (hpqr := ‚ü®by simp‚ü©)

/-- See `MemLp.prod` for the unapplied version. -/
protected lemma MemLp.prod' (hf : ‚àÄ i ‚àà s, MemLp (f i) (p i) Œº) :
    MemLp (fun œâ ‚Ü¶ ‚àè i ‚àà s, f i œâ) (‚àë i ‚àà s, (p i)‚Åª¬π)‚Åª¬π Œº := by
  simpa [Finset.prod_fn] using MemLp.prod hf

end Prod
end MeasureTheory
