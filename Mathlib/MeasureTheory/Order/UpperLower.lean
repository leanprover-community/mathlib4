/-
Copyright (c) 2022 Ya√´l Dillies, Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Kexing Ying
-/
import Mathlib.Analysis.Normed.Order.UpperLower
import Mathlib.MeasureTheory.Covering.BesicovitchVectorSpace
import Mathlib.Topology.Order.DenselyOrdered

/-!
# Order-connected sets are null-measurable

This file proves that order-connected sets in `‚Ñù‚Åø` under the pointwise order are null-measurable.
Recall that `x ‚â§ y` iff `‚àÄ i, x i ‚â§ y i`, and `s` is order-connected iff
`‚àÄ x y ‚àà s, ‚àÄ z, x ‚â§ z ‚Üí z ‚â§ y ‚Üí z ‚àà s`.

## Main declarations

* `Set.OrdConnected.null_frontier`: The frontier of an order-connected set in `‚Ñù‚Åø` has measure `0`.

## Notes

We prove null-measurability in `‚Ñù‚Åø` with the `‚àû`-metric, but this transfers directly to `‚Ñù‚Åø` with
the Euclidean metric because they have the same measurable sets.

Null-measurability can't be strengthened to measurability because any antichain (and in particular
any subset of the antidiagonal `{(x, y) | x + y = 0}`) is order-connected.

## Sketch proof

1. To show an order-connected set is null-measurable, it is enough to show it has null frontier.
2. Since an order-connected set is the intersection of its upper and lower closure, it's enough to
  show that upper and lower sets have null frontier.
3. WLOG let's prove it for an upper set `s`.
4. By the Lebesgue density theorem, it is enough to show that any frontier point `x` of `s` is not a
  Lebesgue point, namely we want the density of `s` over small balls centered at `x` to not tend to
  either `0` or `1`.
5. This is true, since by the upper setness of `s` we can intercalate a ball of radius `Œ¥ / 4` in
  `s` intersected with the upper quadrant of the ball of radius `Œ¥` centered at `x` (recall that the
  balls are taken in the ‚àû-norm, so they are cubes), and another ball of radius `Œ¥ / 4` in `s·∂ú` and
  the lower quadrant of the ball of radius `Œ¥` centered at `x`.

## TODO

Generalize so that it also applies to `‚Ñù √ó ‚Ñù`, for example.
-/

open Filter MeasureTheory Metric Set
open scoped Topology

variable {Œπ : Type*} [Fintype Œπ] {s : Set (Œπ ‚Üí ‚Ñù)} {x : Œπ ‚Üí ‚Ñù}

/-- If we can fit a small ball inside a set `s` intersected with any neighborhood of `x`, then the
density of `s` near `x` is not `0`.

Along with `aux‚ÇÅ`, this proves that `x` is a Lebesgue point of `s`. This will be used to prove that
the frontier of an order-connected set is null. -/
private lemma aux‚ÇÄ
    (h : ‚àÄ Œ¥, 0 < Œ¥ ‚Üí
      ‚àÉ y, closedBall y (Œ¥ / 4) ‚äÜ closedBall x Œ¥ ‚àß closedBall y (Œ¥ / 4) ‚äÜ interior s) :
    ¬¨Tendsto (fun r ‚Ü¶ volume (closure s ‚à© closedBall x r) / volume (closedBall x r)) (ùìù[>] 0)
        (ùìù 0) := by
  choose f hf‚ÇÄ hf‚ÇÅ using h
  intro H
  obtain ‚ü®Œµ, -, hŒµ', hŒµ‚ÇÄ‚ü© := exists_seq_strictAnti_tendsto_nhdsWithin (0 : ‚Ñù)
  refine not_eventually.2
    (Frequently.of_forall fun _ ‚Ü¶ lt_irrefl <| ENNReal.ofReal <| 4‚Åª¬π ^ Fintype.card Œπ)
    ((Filter.Tendsto.eventually_lt (H.comp hŒµ‚ÇÄ) tendsto_const_nhds ?_).mono fun n ‚Ü¶
      lt_of_le_of_lt ?_)
  on_goal 2 =>
    calc
      ENNReal.ofReal (4‚Åª¬π ^ Fintype.card Œπ)
        = volume (closedBall (f (Œµ n) (hŒµ' n)) (Œµ n / 4)) / volume (closedBall x (Œµ n)) := ?_
      _ ‚â§ volume (closure s ‚à© closedBall x (Œµ n)) / volume (closedBall x (Œµ n)) := by
        gcongr
        exact subset_inter ((hf‚ÇÅ _ <| hŒµ' n).trans interior_subset_closure) <| hf‚ÇÄ _ <| hŒµ' n
    have := hŒµ' n
    rw [Real.volume_pi_closedBall, Real.volume_pi_closedBall, ‚Üê ENNReal.ofReal_div_of_pos,
      ‚Üê div_pow, mul_div_mul_left _ _ (two_ne_zero' ‚Ñù), div_right_comm, div_self, one_div]
  all_goals positivity

/-- If we can fit a small ball inside a set `s·∂ú` intersected with any neighborhood of `x`, then the
density of `s` near `x` is not `1`.

Along with `aux‚ÇÄ`, this proves that `x` is a Lebesgue point of `s`. This will be used to prove that
the frontier of an order-connected set is null. -/
private lemma aux‚ÇÅ
    (h : ‚àÄ Œ¥, 0 < Œ¥ ‚Üí
      ‚àÉ y, closedBall y (Œ¥ / 4) ‚äÜ closedBall x Œ¥ ‚àß closedBall y (Œ¥ / 4) ‚äÜ interior s·∂ú) :
    ¬¨Tendsto (fun r ‚Ü¶ volume (closure s ‚à© closedBall x r) / volume (closedBall x r)) (ùìù[>] 0)
        (ùìù 1) := by
  choose f hf‚ÇÄ hf‚ÇÅ using h
  intro H
  obtain ‚ü®Œµ, -, hŒµ', hŒµ‚ÇÄ‚ü© := exists_seq_strictAnti_tendsto_nhdsWithin (0 : ‚Ñù)
  refine not_eventually.2
      (Frequently.of_forall fun _ ‚Ü¶ lt_irrefl <| 1 - ENNReal.ofReal (4‚Åª¬π ^ Fintype.card Œπ))
      ((Filter.Tendsto.eventually_lt tendsto_const_nhds (H.comp hŒµ‚ÇÄ) <|
            ENNReal.sub_lt_self ENNReal.one_ne_top one_ne_zero ?_).mono
        fun n ‚Ü¶ lt_of_le_of_lt' ?_)
  on_goal 2 =>
    calc
      volume (closure s ‚à© closedBall x (Œµ n)) / volume (closedBall x (Œµ n))
        ‚â§ volume (closedBall x (Œµ n) \ closedBall (f (Œµ n) <| hŒµ' n) (Œµ n / 4)) /
          volume (closedBall x (Œµ n)) := by
        gcongr
        rw [diff_eq_compl_inter]
        refine inter_subset_inter_left _ ?_
        rw [subset_compl_comm, ‚Üê interior_compl]
        exact hf‚ÇÅ _ _
      _ = 1 - ENNReal.ofReal (4‚Åª¬π ^ Fintype.card Œπ) := ?_
    dsimp only
    have := hŒµ' n
    rw [measure_diff (hf‚ÇÄ _ _) _ ((Real.volume_pi_closedBall _ _).trans_ne ENNReal.ofReal_ne_top),
      Real.volume_pi_closedBall, Real.volume_pi_closedBall, ENNReal.sub_div fun _ _ ‚Ü¶ _,
      ENNReal.div_self _ ENNReal.ofReal_ne_top, ‚Üê ENNReal.ofReal_div_of_pos, ‚Üê div_pow,
      mul_div_mul_left _ _ (two_ne_zero' ‚Ñù), div_right_comm, div_self, one_div]
  all_goals try positivity
  ¬∑ simp_all
  ¬∑ exact measurableSet_closedBall.nullMeasurableSet

theorem IsUpperSet.null_frontier (hs : IsUpperSet s) : volume (frontier s) = 0 := by
  refine measure_mono_null (fun x hx ‚Ü¶ ?_)
    (Besicovitch.ae_tendsto_measure_inter_div_of_measurableSet _
      (isClosed_closure (s := s)).measurableSet)
  by_cases h : x ‚àà closure s <;>
    simp only [mem_compl_iff, mem_setOf, h, not_false_eq_true, indicator_of_notMem,
      indicator_of_mem, Pi.one_apply]
  ¬∑ refine aux‚ÇÅ fun _ ‚Ü¶ hs.compl.exists_subset_ball <| frontier_subset_closure ?_
    rwa [frontier_compl]
  ¬∑ exact aux‚ÇÄ fun _ ‚Ü¶ hs.exists_subset_ball <| frontier_subset_closure hx

theorem IsLowerSet.null_frontier (hs : IsLowerSet s) : volume (frontier s) = 0 := by
  refine measure_mono_null (fun x hx ‚Ü¶ ?_)
    (Besicovitch.ae_tendsto_measure_inter_div_of_measurableSet _
      (isClosed_closure (s := s)).measurableSet)
  by_cases h : x ‚àà closure s <;>
    simp only [mem_compl_iff, mem_setOf, h, not_false_eq_true, indicator_of_notMem,
      indicator_of_mem, Pi.one_apply]
  ¬∑ refine aux‚ÇÅ fun _ ‚Ü¶ hs.compl.exists_subset_ball <| frontier_subset_closure ?_
    rwa [frontier_compl]
  ¬∑ exact aux‚ÇÄ fun _ ‚Ü¶ hs.exists_subset_ball <| frontier_subset_closure hx

theorem Set.OrdConnected.null_frontier (hs : s.OrdConnected) : volume (frontier s) = 0 := by
  rw [‚Üê hs.upperClosure_inter_lowerClosure]
  exact measure_mono_null (frontier_inter_subset _ _) <| measure_union_null
    (measure_inter_null_of_null_left _ (UpperSet.upper _).null_frontier)
    (measure_inter_null_of_null_right _ (LowerSet.lower _).null_frontier)

protected theorem Set.OrdConnected.nullMeasurableSet (hs : s.OrdConnected) : NullMeasurableSet s :=
  nullMeasurableSet_of_null_frontier hs.null_frontier

theorem IsAntichain.volume_eq_zero [Nonempty Œπ] (hs : IsAntichain (¬∑ ‚â§ ¬∑) s) : volume s = 0 := by
  refine measure_mono_null ?_ hs.ordConnected.null_frontier
  rw [‚Üê closure_diff_interior, hs.interior_eq_empty, diff_empty]
  exact subset_closure
