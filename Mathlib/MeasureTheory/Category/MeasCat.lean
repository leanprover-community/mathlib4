/-
Copyright (c) 2018 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.MeasureTheory.Measure.GiryMonad
import Mathlib.CategoryTheory.ConcreteCategory.UnbundledHom
import Mathlib.CategoryTheory.Monad.Algebra
import Mathlib.Topology.Category.TopCat.Basic

#align_import measure_theory.category.Meas from "leanprover-community/mathlib"@"d6814c584384ddf2825ff038e868451a7c956f31"

/-!
# The category of measurable spaces

Measurable spaces and measurable functions form a (concrete) category `MeasCat`.

## Main definitions

* `Measure : MeasCat ‚•§ MeasCat`: the functor which sends a measurable space `X`
to the space of measures on `X`; it is a monad (the "Giry monad").

* `Borel : TopCat ‚•§ MeasCat`: sends a topological space `X` to `X` equipped with the
`œÉ`-algebra of Borel sets (the `œÉ`-algebra generated by the open subsets of `X`).

## Tags

measurable space, giry monad, borel
-/


noncomputable section

open CategoryTheory MeasureTheory

open scoped ENNReal

universe u v

set_option linter.uppercaseLean3 false -- `Meas` `Top` `Borel`

/-- The category of measurable spaces and measurable functions. -/
def MeasCat : Type (u + 1) :=
  Bundled MeasurableSpace
#align Meas MeasCat

namespace MeasCat

instance : CoeSort MeasCat (Type*) :=
  Bundled.coeSort

instance (X : MeasCat) : MeasurableSpace X :=
  X.str

/-- Construct a bundled `MeasCat` from the underlying type and the typeclass. -/
def of (Œ± : Type u) [ms : MeasurableSpace Œ±] : MeasCat :=
  ‚ü®Œ±, ms‚ü©
#align Meas.of MeasCat.of

@[simp]
theorem coe_of (X : Type u) [MeasurableSpace X] : (of X : Type u) = X :=
  rfl
#align Meas.coe_of MeasCat.coe_of

instance unbundledHom : UnbundledHom @Measurable :=
  ‚ü®@measurable_id, @Measurable.comp‚ü©
#align Meas.unbundled_hom MeasCat.unbundledHom

deriving instance LargeCategory for MeasCat

-- Porting note: `deriving instance ConcreteCategory for MeasCat` didn't work. Define it manually.
-- see https://github.com/leanprover-community/mathlib4/issues/5020
instance : ConcreteCategory MeasCat := by
  unfold MeasCat
  -- ‚ä¢ ConcreteCategory (Bundled MeasurableSpace)
  infer_instance
  -- üéâ no goals

instance : Inhabited MeasCat :=
  ‚ü®MeasCat.of Empty‚ü©

/-- `Measure X` is the measurable space of measures over the measurable space `X`. It is the
weakest measurable space, s.t. `fun Œº ‚Ü¶ Œº s` is measurable for all measurable sets `s` in `X`. An
important purpose is to assign a monadic structure on it, the Giry monad. In the Giry monad,
the pure values are the Dirac measure, and the bind operation maps to the integral:
`(Œº >>= ŒΩ) s = ‚à´ x. (ŒΩ x) s dŒº`.

In probability theory, the `MeasCat`-morphisms `X ‚Üí Prob X` are (sub-)Markov kernels (here `Prob` is
the restriction of `Measure` to (sub-)probability space.)
-/
def Measure : MeasCat ‚•§ MeasCat where
  obj X := ‚ü®@MeasureTheory.Measure X.1 X.2, inferInstance‚ü©
  map f := ‚ü®Measure.map (‚áëf), Measure.measurable_map f.1 f.2‚ü©
  map_id := fun ‚ü®Œ±, I‚ü© => Subtype.eq <| funext fun Œº => @Measure.map_id Œ± I Œº
  map_comp := fun ‚ü®_, hf‚ü© ‚ü®_, hg‚ü© => Subtype.eq <| funext fun _ => (Measure.map_map hg hf).symm
#align Meas.Measure MeasCat.Measure

/-- The Giry monad, i.e. the monadic structure associated with `Measure`. -/
def Giry : CategoryTheory.Monad MeasCat where
  toFunctor := Measure
  Œ∑' :=
    { app := fun X => ‚ü®@Measure.dirac X.1 X.2, Measure.measurable_dirac‚ü©
      naturality := fun _ _ ‚ü®_, hf‚ü© => Subtype.eq <| funext fun a => (Measure.map_dirac hf a).symm }
  Œº' :=
    { app := fun X => ‚ü®@Measure.join X.1 X.2, Measure.measurable_join‚ü©
      naturality := fun _ _ ‚ü®_, hf‚ü© => Subtype.eq <| funext fun Œº => Measure.join_map_map hf Œº }
  assoc' _ := Subtype.eq <| funext fun _ => Measure.join_map_join _
  left_unit' _ := Subtype.eq <| funext fun _ => Measure.join_dirac _
  right_unit' _ := Subtype.eq <| funext fun _ => Measure.join_map_dirac _
#align Meas.Giry MeasCat.Giry

/-- An example for an algebra on `Measure`: the nonnegative Lebesgue integral is a hom, behaving
nicely under the monad operations. -/
def Integral : Giry.Algebra where
  A := MeasCat.of ‚Ñù‚â•0‚àû
  a := ‚ü®fun m : MeasureTheory.Measure ‚Ñù‚â•0‚àû ‚Ü¶ ‚à´‚Åª x, x ‚àÇm, Measure.measurable_lintegral measurable_id‚ü©
  unit := Subtype.eq <| funext fun r : ‚Ñù‚â•0‚àû => lintegral_dirac' _ measurable_id
  assoc := Subtype.eq <| funext fun Œº : MeasureTheory.Measure (MeasureTheory.Measure ‚Ñù‚â•0‚àû) =>
    show ‚à´‚Åª x, x ‚àÇŒº.join = ‚à´‚Åª x, x ‚àÇMeasure.map (fun m => ‚à´‚Åª x, x ‚àÇm) Œº by
      rw [Measure.lintegral_join, lintegral_map] <;>
        apply_rules [measurable_id, Measure.measurable_lintegral]
        -- üéâ no goals
        -- üéâ no goals
        -- üéâ no goals
#align Meas.Integral MeasCat.Integral

end MeasCat

instance TopCat.hasForgetToMeasCat : HasForget‚ÇÇ TopCat.{u} MeasCat.{u} :=
  BundledHom.mkHasForget‚ÇÇ borel (fun f => ‚ü®f.1, f.2.borel_measurable‚ü©) (fun _ => rfl)
#align Top.has_forget_to_Meas TopCat.hasForgetToMeasCat

/-- The Borel functor, the canonical embedding of topological spaces into measurable spaces. -/
@[reducible]
def Borel : TopCat.{u} ‚•§ MeasCat.{u} :=
  forget‚ÇÇ TopCat.{u} MeasCat.{u}
#align Borel Borel
