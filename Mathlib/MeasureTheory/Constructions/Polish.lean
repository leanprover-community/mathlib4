/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Felix Weilacher
-/
import Mathlib.Data.Real.Cardinality
import Mathlib.Topology.Perfect
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic

#align_import measure_theory.constructions.polish from "leanprover-community/mathlib"@"9f55d0d4363ae59948c33864cbc52e0b12e0e8ce"

/-!
# The Borel sigma-algebra on Polish spaces

We discuss several results pertaining to the relationship between the topology and the Borel
structure on Polish spaces.

## Main definitions and results

First, we define standard Borel spaces.

* A `StandardBorelSpace Î±` is a typeclass for measurable spaces which arise as the Borel sets
  of some Polish topology.

Next, we define the class of analytic sets and establish its basic properties.

* `MeasureTheory.AnalyticSet s`: a set in a topological space is analytic if it is the continuous
  image of a Polish space. Equivalently, it is empty, or the image of `â„• â†’ â„•`.
* `MeasureTheory.AnalyticSet.image_of_continuous`: a continuous image of an analytic set is
  analytic.
* `MeasurableSet.analyticSet`: in a Polish space, any Borel-measurable set is analytic.

Then, we show Lusin's theorem that two disjoint analytic sets can be separated by Borel sets.

* `MeasurablySeparable s t` states that there exists a measurable set containing `s` and disjoint
  from `t`.
* `AnalyticSet.measurablySeparable` shows that two disjoint analytic sets are separated by a
  Borel set.

We then prove the Lusin-Souslin theorem that a continuous injective image of a Borel subset of
a Polish space is Borel. The proof of this nontrivial result relies on the above results on
analytic sets.

* `MeasurableSet.image_of_continuousOn_injOn` asserts that, if `s` is a Borel measurable set in
  a Polish space, then the image of `s` under a continuous injective map is still Borel measurable.
* `Continuous.measurableEmbedding` states that a continuous injective map on a Polish space
  is a measurable embedding for the Borel sigma-algebra.
* `ContinuousOn.measurableEmbedding` is the same result for a map restricted to a measurable set
  on which it is continuous.
* `Measurable.measurableEmbedding` states that a measurable injective map from
  a standard Borel space to a second-countable topological space is a measurable embedding.
* `isClopenable_iff_measurableSet`: in a Polish space, a set is clopenable (i.e., it can be made
  open and closed by using a finer Polish topology) if and only if it is Borel-measurable.

We use this to prove several versions of the Borel isomorphism theorem.

* `PolishSpace.measurableEquivOfNotCountable` : Any two uncountable standard Borel spaces
  are Borel isomorphic.
* `PolishSpace.Equiv.measurableEquiv` : Any two standard Borel spaces of the same cardinality
  are Borel isomorphic.
-/


open Set Function PolishSpace PiNat TopologicalSpace Bornology Metric Filter Topology MeasureTheory

/-! ### Standard Borel Spaces -/

variable (Î± : Type*)

/-- A standard Borel space is a measurable space arising as the Borel sets of some Polish topology.
This is useful in situations where a space has no natural topology or
the natural topology in a space is non-Polish.

To endow a standard Borel space `Î±` with a compatible Polish topology, use
`letI := upgradeStandardBorel Î±`. One can then use `eq_borel_upgradeStandardBorel Î±` to
rewrite the `MeasurableSpace Î±` instance to `borel Î± t`, where `t` is the new topology.-/
class StandardBorelSpace [MeasurableSpace Î±] : Prop where
  /-- There exists a compatible Polish topology. -/
  polish : âˆƒ _ : TopologicalSpace Î±, BorelSpace Î± âˆ§ PolishSpace Î±

/-- A convenience class similar to `UpgradedPolishSpace`. No instance should be registered.
Instead one should use `letI := upgradeStandardBorel Î±`. -/
class UpgradedStandardBorel extends MeasurableSpace Î±, TopologicalSpace Î±,
  BorelSpace Î±, PolishSpace Î±

/-- Use as `letI := upgradeStandardBorel Î±` to endow a standard Borel space `Î±` with
a compatible Polish topology.

Warning: following this with `borelize Î±` will cause an error. Instead, one can
rewrite with `eq_borel_upgradeStandardBorel Î±`.
TODO: fix the corresponding bug in `borelize`. -/
noncomputable
def upgradeStandardBorel [MeasurableSpace Î±] [h : StandardBorelSpace Î±] :
    UpgradedStandardBorel Î± := by
  choose Ï„ hb hp using h.polish
  constructor

/-- The `MeasurableSpace Î±` instance on a `StandardBorelSpace` `Î±` is equal to
the borel sets of `upgradeStandardBorel Î±`.-/
theorem eq_borel_upgradeStandardBorel [MeasurableSpace Î±] [StandardBorelSpace Î±] :
    â€¹MeasurableSpace Î±â€º = @borel _ (upgradeStandardBorel Î±).toTopologicalSpace :=
  @BorelSpace.measurable_eq _ (upgradeStandardBorel Î±).toTopologicalSpace _
    (upgradeStandardBorel Î±).toBorelSpace

variable {Î±}

section

variable [MeasurableSpace Î±]

instance standardBorel_of_polish [Ï„ : TopologicalSpace Î±]
    [BorelSpace Î±] [PolishSpace Î±] : StandardBorelSpace Î± := by exists Ï„

instance countablyGenerated_of_standardBorel [StandardBorelSpace Î±] :
    MeasurableSpace.CountablyGenerated Î± :=
  letI := upgradeStandardBorel Î±
  inferInstance

instance measurableSingleton_of_standardBorel [StandardBorelSpace Î±] : MeasurableSingletonClass Î± :=
  letI := upgradeStandardBorel Î±
  inferInstance

namespace StandardBorelSpace

variable {Î² : Type*} [MeasurableSpace Î²]

section instances

/-- A product of two standard Borel spaces is standard Borel. -/
instance prod [StandardBorelSpace Î±] [StandardBorelSpace Î²] : StandardBorelSpace (Î± Ã— Î²) :=
  letI := upgradeStandardBorel Î±
  letI := upgradeStandardBorel Î²
  inferInstance

/-- A product of countably many standard Borel spaces is standard Borel. -/
instance pi_countable {Î¹ : Type*} [Countable Î¹] {Î± : Î¹ â†’ Type*} [âˆ€ n, MeasurableSpace (Î± n)]
    [âˆ€ n, StandardBorelSpace (Î± n)] : StandardBorelSpace (âˆ€ n, Î± n) :=
  letI := fun n => upgradeStandardBorel (Î± n)
  inferInstance

end instances

end StandardBorelSpace

end section

variable {Î¹ : Type*}

namespace MeasureTheory

variable [TopologicalSpace Î±]

/-! ### Analytic sets -/

/-- An analytic set is a set which is the continuous image of some Polish space. There are several
equivalent characterizations of this definition. For the definition, we pick one that avoids
universe issues: a set is analytic if and only if it is a continuous image of `â„• â†’ â„•` (or if it
is empty). The above more usual characterization is given
in `analyticSet_iff_exists_polishSpace_range`.

Warning: these are analytic sets in the context of descriptive set theory (which is why they are
registered in the namespace `MeasureTheory`). They have nothing to do with analytic sets in the
context of complex analysis. -/
irreducible_def AnalyticSet (s : Set Î±) : Prop :=
  s = âˆ… âˆ¨ âˆƒ f : (â„• â†’ â„•) â†’ Î±, Continuous f âˆ§ range f = s
#align measure_theory.analytic_set MeasureTheory.AnalyticSet

theorem analyticSet_empty : AnalyticSet (âˆ… : Set Î±) := by
  rw [AnalyticSet]
  exact Or.inl rfl
#align measure_theory.analytic_set_empty MeasureTheory.analyticSet_empty

theorem analyticSet_range_of_polishSpace {Î² : Type*} [TopologicalSpace Î²] [PolishSpace Î²]
    {f : Î² â†’ Î±} (f_cont : Continuous f) : AnalyticSet (range f) := by
  cases isEmpty_or_nonempty Î²
  Â· rw [range_eq_empty]
    exact analyticSet_empty
  Â· rw [AnalyticSet]
    obtain âŸ¨g, g_cont, hgâŸ© : âˆƒ g : (â„• â†’ â„•) â†’ Î², Continuous g âˆ§ Surjective g :=
      exists_nat_nat_continuous_surjective Î²
    refine' Or.inr âŸ¨f âˆ˜ g, f_cont.comp g_cont, _âŸ©
    rw [hg.range_comp]
#align measure_theory.analytic_set_range_of_polish_space MeasureTheory.analyticSet_range_of_polishSpace

/-- The image of an open set under a continuous map is analytic. -/
theorem _root_.IsOpen.analyticSet_image {Î² : Type*} [TopologicalSpace Î²] [PolishSpace Î²]
    {s : Set Î²} (hs : IsOpen s) {f : Î² â†’ Î±} (f_cont : Continuous f) : AnalyticSet (f '' s) := by
  rw [image_eq_range]
  haveI : PolishSpace s := hs.polishSpace
  exact analyticSet_range_of_polishSpace (f_cont.comp continuous_subtype_val)
#align is_open.analytic_set_image IsOpen.analyticSet_image

/-- A set is analytic if and only if it is the continuous image of some Polish space. -/
theorem analyticSet_iff_exists_polishSpace_range {s : Set Î±} :
    AnalyticSet s â†”
      âˆƒ (Î² : Type) (h : TopologicalSpace Î²) (_ : @PolishSpace Î² h) (f : Î² â†’ Î±),
        @Continuous _ _ h _ f âˆ§ range f = s := by
  constructor
  Â· intro h
    rw [AnalyticSet] at h
    cases' h with h h
    Â· refine' âŸ¨Empty, inferInstance, inferInstance, Empty.elim, continuous_bot, _âŸ©
      rw [h]
      exact range_eq_empty _
    Â· exact âŸ¨â„• â†’ â„•, inferInstance, inferInstance, hâŸ©
  Â· rintro âŸ¨Î², h, h', f, f_cont, f_rangeâŸ©
    skip
    rw [â† f_range]
    exact analyticSet_range_of_polishSpace f_cont
#align measure_theory.analytic_set_iff_exists_polish_space_range MeasureTheory.analyticSet_iff_exists_polishSpace_range

/-- The continuous image of an analytic set is analytic -/
theorem AnalyticSet.image_of_continuousOn {Î² : Type*} [TopologicalSpace Î²] {s : Set Î±}
    (hs : AnalyticSet s) {f : Î± â†’ Î²} (hf : ContinuousOn f s) : AnalyticSet (f '' s) := by
  rcases analyticSet_iff_exists_polishSpace_range.1 hs with âŸ¨Î³, Î³top, Î³polish, g, g_cont, gsâŸ©
  skip
  have : f '' s = range (f âˆ˜ g) := by rw [range_comp, gs]
  rw [this]
  apply analyticSet_range_of_polishSpace
  apply hf.comp_continuous g_cont fun x => _
  rw [â† gs]
  exact mem_range_self
#align measure_theory.analytic_set.image_of_continuous_on MeasureTheory.AnalyticSet.image_of_continuousOn

theorem AnalyticSet.image_of_continuous {Î² : Type*} [TopologicalSpace Î²] {s : Set Î±}
    (hs : AnalyticSet s) {f : Î± â†’ Î²} (hf : Continuous f) : AnalyticSet (f '' s) :=
  hs.image_of_continuousOn hf.continuousOn
#align measure_theory.analytic_set.image_of_continuous MeasureTheory.AnalyticSet.image_of_continuous

/-- A countable intersection of analytic sets is analytic. -/
theorem AnalyticSet.iInter [hÎ¹ : Nonempty Î¹] [Countable Î¹] [T2Space Î±] {s : Î¹ â†’ Set Î±}
    (hs : âˆ€ n, AnalyticSet (s n)) : AnalyticSet (â‹‚ n, s n) := by
  rcases hÎ¹ with âŸ¨iâ‚€âŸ©
  /- For the proof, write each `s n` as the continuous image under a map `f n` of a
    Polish space `Î² n`. The product space `Î³ = Î  n, Î² n` is also Polish, and so is the subset
    `t` of sequences `x n` for which `f n (x n)` is independent of `n`. The set `t` is Polish, and
    the range of `x â†¦ f 0 (x 0)` on `t` is exactly `â‹‚ n, s n`, so this set is analytic. -/
  choose Î² hÎ² h'Î² f f_cont f_range using fun n =>
    analyticSet_iff_exists_polishSpace_range.1 (hs n)
  skip
  let Î³ := âˆ€ n, Î² n
  let t : Set Î³ := â‹‚ n, { x | f n (x n) = f iâ‚€ (x iâ‚€) }
  have t_closed : IsClosed t := by
    apply isClosed_iInter
    intro n
    exact
      isClosed_eq ((f_cont n).comp (continuous_apply n)) ((f_cont iâ‚€).comp (continuous_apply iâ‚€))
  haveI : PolishSpace t := t_closed.polishSpace
  let F : t â†’ Î± := fun x => f iâ‚€ ((x : Î³) iâ‚€)
  have F_cont : Continuous F := (f_cont iâ‚€).comp ((continuous_apply iâ‚€).comp continuous_subtype_val)
  have F_range : range F = â‹‚ n : Î¹, s n := by
    apply Subset.antisymm
    Â· rintro y âŸ¨x, rflâŸ©
      refine mem_iInter.2 fun n => ?_
      have : f n ((x : Î³) n) = F x := (mem_iInter.1 x.2 n : _)
      rw [â† this, â† f_range n]
      exact mem_range_self _
    Â· intro y hy
      have A : âˆ€ n, âˆƒ x : Î² n, f n x = y := by
        intro n
        rw [â† mem_range, f_range n]
        exact mem_iInter.1 hy n
      choose x hx using A
      have xt : x âˆˆ t := by
        refine mem_iInter.2 fun n => ?_
        simp [hx]
      refine' âŸ¨âŸ¨x, xtâŸ©, _âŸ©
      exact hx iâ‚€
  rw [â† F_range]
  exact analyticSet_range_of_polishSpace F_cont
#align measure_theory.analytic_set.Inter MeasureTheory.AnalyticSet.iInter

/-- A countable union of analytic sets is analytic. -/
theorem AnalyticSet.iUnion [Countable Î¹] {s : Î¹ â†’ Set Î±} (hs : âˆ€ n, AnalyticSet (s n)) :
    AnalyticSet (â‹ƒ n, s n) := by
  /- For the proof, write each `s n` as the continuous image under a map `f n` of a
    Polish space `Î² n`. The union space `Î³ = Î£ n, Î² n` is also Polish, and the map `F : Î³ â†’ Î±` which
    coincides with `f n` on `Î² n` sends it to `â‹ƒ n, s n`. -/
  choose Î² hÎ² h'Î² f f_cont f_range using fun n =>
    analyticSet_iff_exists_polishSpace_range.1 (hs n)
  let Î³ := Î£n, Î² n
  let F : Î³ â†’ Î± := fun âŸ¨n, xâŸ© â†¦ f n x
  have F_cont : Continuous F := continuous_sigma f_cont
  have F_range : range F = â‹ƒ n, s n := by
    simp only [range_sigma_eq_iUnion_range, f_range]
  rw [â† F_range]
  exact analyticSet_range_of_polishSpace F_cont
#align measure_theory.analytic_set.Union MeasureTheory.AnalyticSet.iUnion

theorem _root_.IsClosed.analyticSet [PolishSpace Î±] {s : Set Î±} (hs : IsClosed s) :
    AnalyticSet s := by
  haveI : PolishSpace s := hs.polishSpace
  rw [â† @Subtype.range_val Î± s]
  exact analyticSet_range_of_polishSpace continuous_subtype_val
#align is_closed.analytic_set IsClosed.analyticSet

/-- Given a Borel-measurable set in a Polish space, there exists a finer Polish topology making
it clopen. This is in fact an equivalence, see `isClopenable_iff_measurableSet`. -/
theorem _root_.MeasurableSet.isClopenable [PolishSpace Î±] [MeasurableSpace Î±] [BorelSpace Î±]
    {s : Set Î±} (hs : MeasurableSet s) : IsClopenable s := by
  revert s
  apply MeasurableSet.induction_on_open
  Â· exact fun u hu => hu.isClopenable
  Â· exact fun u _ h'u => h'u.compl
  Â· exact fun f _ _ hf => IsClopenable.iUnion hf
#align measurable_set.is_clopenable MeasurableSet.isClopenable

/-- A Borel-measurable set in a Polish space is analytic. -/
theorem _root_.MeasurableSet.analyticSet {Î± : Type*} [t : TopologicalSpace Î±] [PolishSpace Î±]
    [MeasurableSpace Î±] [BorelSpace Î±] {s : Set Î±} (hs : MeasurableSet s) : AnalyticSet s := by
  /- For a short proof (avoiding measurable induction), one sees `s` as a closed set for a finer
    topology `t'`. It is analytic for this topology. As the identity from `t'` to `t` is continuous
    and the image of an analytic set is analytic, it follows that `s` is also analytic for `t`. -/
  obtain âŸ¨t', t't, t'_polish, s_closed, _âŸ© :
    âˆƒ t' : TopologicalSpace Î±, t' â‰¤ t âˆ§ @PolishSpace Î± t' âˆ§ IsClosed[t'] s âˆ§ IsOpen[t'] s :=
    hs.isClopenable
  have A := @IsClosed.analyticSet Î± t' t'_polish s s_closed
  convert @AnalyticSet.image_of_continuous Î± t' Î± t s A id (continuous_id_of_le t't)
  simp only [id.def, image_id']
#align measurable_set.analytic_set MeasurableSet.analyticSet

/-- Given a Borel-measurable function from a Polish space to a second-countable space, there exists
a finer Polish topology on the source space for which the function is continuous. -/
theorem _root_.Measurable.exists_continuous {Î± Î² : Type*} [t : TopologicalSpace Î±] [PolishSpace Î±]
    [MeasurableSpace Î±] [BorelSpace Î±] [tÎ² : TopologicalSpace Î²] [MeasurableSpace Î²]
    [OpensMeasurableSpace Î²] {f : Î± â†’ Î²} [SecondCountableTopology (range f)] (hf : Measurable f) :
    âˆƒ t' : TopologicalSpace Î±, t' â‰¤ t âˆ§ @Continuous Î± Î² t' tÎ² f âˆ§ @PolishSpace Î± t' := by
  obtain âŸ¨b, b_count, -, hbâŸ© :
    âˆƒ b : Set (Set (range f)), b.Countable âˆ§ âˆ… âˆ‰ b âˆ§ IsTopologicalBasis b :=
    exists_countable_basis (range f)
  haveI : Countable b := b_count.to_subtype
  have : âˆ€ s : b, IsClopenable (rangeFactorization f â»Â¹' s) := fun s â†¦ by
    apply MeasurableSet.isClopenable
    exact hf.subtype_mk (hb.isOpen s.2).measurableSet
  choose T Tt Tpolish _ Topen using this
  obtain âŸ¨t', t'T, t't, t'_polishâŸ© :
    âˆƒ t' : TopologicalSpace Î±, (âˆ€ i, t' â‰¤ T i) âˆ§ t' â‰¤ t âˆ§ @PolishSpace Î± t' :=
    exists_polishSpace_forall_le T Tt Tpolish
  refine' âŸ¨t', t't, _, t'_polishâŸ©
  have : Continuous[t', _] (rangeFactorization f) :=
    hb.continuous_iff.2 fun s hs => t'T âŸ¨s, hsâŸ© _ (Topen âŸ¨s, hsâŸ©)
  exact continuous_subtype_val.comp this
#align measurable.exists_continuous Measurable.exists_continuous

/-- The image of a measurable set in a standard Borel space under a measurable map
is an analytic set. -/
theorem _root_.MeasurableSet.analyticSet_image {X Y : Type*} [MeasurableSpace X]
    [StandardBorelSpace X] [TopologicalSpace Y] [MeasurableSpace Y]
    [OpensMeasurableSpace Y] {f : X â†’ Y} [SecondCountableTopology (range f)] {s : Set X}
    (hs : MeasurableSet s) (hf : Measurable f) : AnalyticSet (f '' s) := by
  letI := upgradeStandardBorel X
  rw [eq_borel_upgradeStandardBorel X] at hs
  rcases hf.exists_continuous with âŸ¨Ï„', hle, hfc, hÏ„'âŸ©
  letI m' : MeasurableSpace X := @borel _ Ï„'
  haveI b' : BorelSpace X := âŸ¨rflâŸ©
  have hle := borel_anti hle
  exact (hle _ hs).analyticSet.image_of_continuous hfc
#align measurable_set.analytic_set_image MeasurableSet.analyticSet_image

/-- Preimage of an analytic set is an analytic set. -/
protected lemma AnalyticSet.preimage {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [PolishSpace X] [T2Space Y] {s : Set Y} (hs : AnalyticSet s) {f : X â†’ Y} (hf : Continuous f) :
    AnalyticSet (f â»Â¹' s) := by
  rcases analyticSet_iff_exists_polishSpace_range.1 hs with âŸ¨Z, _, _, g, hg, rflâŸ©
  have : IsClosed {x : X Ã— Z | f x.1 = g x.2} := isClosed_diagonal.preimage (hf.prod_map hg)
  convert this.analyticSet.image_of_continuous continuous_fst
  ext x
  simp [eq_comm]

/-! ### Separating sets with measurable sets -/

/-- Two sets `u` and `v` in a measurable space are measurably separable if there
exists a measurable set containing `u` and disjoint from `v`.
This is mostly interesting for Borel-separable sets. -/
def MeasurablySeparable {Î± : Type*} [MeasurableSpace Î±] (s t : Set Î±) : Prop :=
  âˆƒ u, s âŠ† u âˆ§ Disjoint t u âˆ§ MeasurableSet u
#align measure_theory.measurably_separable MeasureTheory.MeasurablySeparable

theorem MeasurablySeparable.iUnion [Countable Î¹] {Î± : Type*} [MeasurableSpace Î±] {s t : Î¹ â†’ Set Î±}
    (h : âˆ€ m n, MeasurablySeparable (s m) (t n)) : MeasurablySeparable (â‹ƒ n, s n) (â‹ƒ m, t m) := by
  choose u hsu htu hu using h
  refine' âŸ¨â‹ƒ m, â‹‚ n, u m n, _, _, _âŸ©
  Â· refine' iUnion_subset fun m => subset_iUnion_of_subset m _
    exact subset_iInter fun n => hsu m n
  Â· simp_rw [disjoint_iUnion_left, disjoint_iUnion_right]
    intro n m
    apply Disjoint.mono_right _ (htu m n)
    apply iInter_subset
  Â· refine' MeasurableSet.iUnion fun m => _
    exact MeasurableSet.iInter fun n => hu m n
#align measure_theory.measurably_separable.Union MeasureTheory.MeasurablySeparable.iUnion

/-- The hard part of the Lusin separation theorem saying that two disjoint analytic sets are
contained in disjoint Borel sets (see the full statement in `AnalyticSet.measurablySeparable`).
Here, we prove this when our analytic sets are the ranges of functions from `â„• â†’ â„•`.
-/
theorem measurablySeparable_range_of_disjoint [T2Space Î±] [MeasurableSpace Î±]
    [OpensMeasurableSpace Î±] {f g : (â„• â†’ â„•) â†’ Î±} (hf : Continuous f) (hg : Continuous g)
    (h : Disjoint (range f) (range g)) : MeasurablySeparable (range f) (range g) := by
  /- We follow [Kechris, *Classical Descriptive Set Theory* (Theorem 14.7)][kechris1995].
    If the ranges are not Borel-separated, then one can find two cylinders of length one whose
    images are not Borel-separated, and then two smaller cylinders of length two whose images are
    not Borel-separated, and so on. One thus gets two sequences of cylinders, that decrease to two
    points `x` and `y`. Their images are different by the disjointness assumption, hence contained
    in two disjoint open sets by the T2 property. By continuity, long enough cylinders around `x`
    and `y` have images which are separated by these two disjoint open sets, a contradiction.
    -/
  by_contra hfg
  have I : âˆ€ n x y, Â¬MeasurablySeparable (f '' cylinder x n) (g '' cylinder y n) â†’
      âˆƒ x' y', x' âˆˆ cylinder x n âˆ§ y' âˆˆ cylinder y n âˆ§
      Â¬MeasurablySeparable (f '' cylinder x' (n + 1)) (g '' cylinder y' (n + 1)) := by
    intro n x y
    contrapose!
    intro H
    rw [â† iUnion_cylinder_update x n, â† iUnion_cylinder_update y n, image_iUnion, image_iUnion]
    refine' MeasurablySeparable.iUnion fun i j => _
    exact H _ _ (update_mem_cylinder _ _ _) (update_mem_cylinder _ _ _)
  -- consider the set of pairs of cylinders of some length whose images are not Borel-separated
  let A :=
    { p : â„• Ã— (â„• â†’ â„•) Ã— (â„• â†’ â„•) //
      Â¬MeasurablySeparable (f '' cylinder p.2.1 p.1) (g '' cylinder p.2.2 p.1) }
  -- for each such pair, one can find longer cylinders whose images are not Borel-separated either
  have : âˆ€ p : A, âˆƒ q : A,
      q.1.1 = p.1.1 + 1 âˆ§ q.1.2.1 âˆˆ cylinder p.1.2.1 p.1.1 âˆ§ q.1.2.2 âˆˆ cylinder p.1.2.2 p.1.1 := by
    rintro âŸ¨âŸ¨n, x, yâŸ©, hpâŸ©
    rcases I n x y hp with âŸ¨x', y', hx', hy', h'âŸ©
    exact âŸ¨âŸ¨âŸ¨n + 1, x', y'âŸ©, h'âŸ©, rfl, hx', hy'âŸ©
  choose F hFn hFx hFy using this
  let p0 : A := âŸ¨âŸ¨0, fun _ => 0, fun _ => 0âŸ©, by simp [hfg]âŸ©
  -- construct inductively decreasing sequences of cylinders whose images are not separated
  let p : â„• â†’ A := fun n => F^[n] p0
  have prec : âˆ€ n, p (n + 1) = F (p n) := fun n => by simp only [iterate_succ', Function.comp]
  -- check that at the `n`-th step we deal with cylinders of length `n`
  have pn_fst : âˆ€ n, (p n).1.1 = n := by
    intro n
    induction' n with n IH
    Â· rfl
    Â· simp only [prec, hFn, IH]
  -- check that the cylinders we construct are indeed decreasing, by checking that the coordinates
  -- are stationary.
  have Ix : âˆ€ m n, m + 1 â‰¤ n â†’ (p n).1.2.1 m = (p (m + 1)).1.2.1 m := by
    intro m
    apply Nat.le_induction
    Â· rfl
    intro n hmn IH
    have I : (F (p n)).val.snd.fst m = (p n).val.snd.fst m := by
      apply hFx (p n) m
      rw [pn_fst]
      exact hmn
    rw [prec, I, IH]
  have Iy : âˆ€ m n, m + 1 â‰¤ n â†’ (p n).1.2.2 m = (p (m + 1)).1.2.2 m := by
    intro m
    apply Nat.le_induction
    Â· rfl
    intro n hmn IH
    have I : (F (p n)).val.snd.snd m = (p n).val.snd.snd m := by
      apply hFy (p n) m
      rw [pn_fst]
      exact hmn
    rw [prec, I, IH]
  -- denote by `x` and `y` the limit points of these two sequences of cylinders.
  set x : â„• â†’ â„• := fun n => (p (n + 1)).1.2.1 n with hx
  set y : â„• â†’ â„• := fun n => (p (n + 1)).1.2.2 n with hy
  -- by design, the cylinders around these points have images which are not Borel-separable.
  have M : âˆ€ n, Â¬MeasurablySeparable (f '' cylinder x n) (g '' cylinder y n) := by
    intro n
    convert (p n).2 using 3
    Â· rw [pn_fst, â† mem_cylinder_iff_eq, mem_cylinder_iff]
      intro i hi
      rw [hx]
      exact (Ix i n hi).symm
    Â· rw [pn_fst, â† mem_cylinder_iff_eq, mem_cylinder_iff]
      intro i hi
      rw [hy]
      exact (Iy i n hi).symm
  -- consider two open sets separating `f x` and `g y`.
  obtain âŸ¨u, v, u_open, v_open, xu, yv, huvâŸ© :
    âˆƒ u v : Set Î±, IsOpen u âˆ§ IsOpen v âˆ§ f x âˆˆ u âˆ§ g y âˆˆ v âˆ§ Disjoint u v := by
    apply t2_separation
    exact disjoint_iff_forall_ne.1 h (mem_range_self _) (mem_range_self _)
  letI : MetricSpace (â„• â†’ â„•) := metricSpaceNatNat
  obtain âŸ¨Îµx, Îµxpos, hÎµxâŸ© : âˆƒ (Îµx : â„), Îµx > 0 âˆ§ Metric.ball x Îµx âŠ† f â»Â¹' u := by
    apply Metric.mem_nhds_iff.1
    exact hf.continuousAt.preimage_mem_nhds (u_open.mem_nhds xu)
  obtain âŸ¨Îµy, Îµypos, hÎµyâŸ© : âˆƒ (Îµy : â„), Îµy > 0 âˆ§ Metric.ball y Îµy âŠ† g â»Â¹' v := by
    apply Metric.mem_nhds_iff.1
    exact hg.continuousAt.preimage_mem_nhds (v_open.mem_nhds yv)
  obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, (1 / 2 : â„) ^ n < min Îµx Îµy :=
    exists_pow_lt_of_lt_one (lt_min Îµxpos Îµypos) (by norm_num)
  -- for large enough `n`, these open sets separate the images of long cylinders around `x` and `y`
  have B : MeasurablySeparable (f '' cylinder x n) (g '' cylinder y n) := by
    refine' âŸ¨u, _, _, u_open.measurableSetâŸ©
    Â· rw [image_subset_iff]
      apply Subset.trans _ hÎµx
      intro z hz
      rw [mem_cylinder_iff_dist_le] at hz
      exact hz.trans_lt (hn.trans_le (min_le_left _ _))
    Â· refine' Disjoint.mono_left _ huv.symm
      change g '' cylinder y n âŠ† v
      rw [image_subset_iff]
      apply Subset.trans _ hÎµy
      intro z hz
      rw [mem_cylinder_iff_dist_le] at hz
      exact hz.trans_lt (hn.trans_le (min_le_right _ _))
  -- this is a contradiction.
  exact M n B
#align measure_theory.measurably_separable_range_of_disjoint MeasureTheory.measurablySeparable_range_of_disjoint

/-- The **Lusin separation theorem**: if two analytic sets are disjoint, then they are contained in
disjoint Borel sets. -/
theorem AnalyticSet.measurablySeparable [T2Space Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±]
    {s t : Set Î±} (hs : AnalyticSet s) (ht : AnalyticSet t) (h : Disjoint s t) :
    MeasurablySeparable s t := by
  rw [AnalyticSet] at hs ht
  rcases hs with (rfl | âŸ¨f, f_cont, rflâŸ©)
  Â· refine' âŸ¨âˆ…, Subset.refl _, by simp, MeasurableSet.emptyâŸ©
  rcases ht with (rfl | âŸ¨g, g_cont, rflâŸ©)
  Â· exact âŸ¨univ, subset_univ _, by simp, MeasurableSet.univâŸ©
  exact measurablySeparable_range_of_disjoint f_cont g_cont h
#align measure_theory.analytic_set.measurably_separable MeasureTheory.AnalyticSet.measurablySeparable

/-- **Suslin's Theorem**: in a Hausdorff topological space, an analytic set with an analytic
complement is measurable. -/
theorem AnalyticSet.measurableSet_of_compl [T2Space Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±]
    {s : Set Î±} (hs : AnalyticSet s) (hsc : AnalyticSet sá¶œ) : MeasurableSet s := by
  rcases hs.measurablySeparable hsc disjoint_compl_right with âŸ¨u, hsu, hdu, hmuâŸ©
  obtain rfl : s = u := hsu.antisymm (disjoint_compl_left_iff_subset.1 hdu)
  exact hmu
#align measure_theory.analytic_set.measurable_set_of_compl MeasureTheory.AnalyticSet.measurableSet_of_compl

end MeasureTheory

/-!
### Measurability of preimages under measurable maps
-/

namespace Measurable

variable {X Y Î² : Type*} [MeasurableSpace X] [StandardBorelSpace X]
  [TopologicalSpace Y] [T2Space Y] [MeasurableSpace Y] [OpensMeasurableSpace Y] [MeasurableSpace Î²]

/-- If `f : X â†’ Y` is a surjective Borel measurable map from a standard Borel space
to a topological space with second countable topology, then the preimage of a set `s`
is measurable if and only if the set is measurable.
One implication is the definition of measurability, the other one heavily relies on `X` being a
standard Borel space. -/
theorem measurableSet_preimage_iff_of_surjective [SecondCountableTopology Y] {f : X â†’ Y}
    (hf : Measurable f) (hsurj : Surjective f) {s : Set Y} :
    MeasurableSet (f â»Â¹' s) â†” MeasurableSet s := by
  refine âŸ¨fun h => ?_, fun h => hf hâŸ©
  apply AnalyticSet.measurableSet_of_compl
  Â· rw [â† image_preimage_eq s hsurj]
    exact h.analyticSet_image hf
  Â· rw [â† image_preimage_eq sá¶œ hsurj]
    exact h.compl.analyticSet_image hf
#align measurable.measurable_set_preimage_iff_of_surjective Measurable.measurableSet_preimage_iff_of_surjective

theorem map_measurableSpace_eq [SecondCountableTopology Y] {f : X â†’ Y} (hf : Measurable f)
    (hsurj : Surjective f) : MeasurableSpace.map f â€¹MeasurableSpace Xâ€º = â€¹MeasurableSpace Yâ€º :=
  MeasurableSpace.ext fun _ => hf.measurableSet_preimage_iff_of_surjective hsurj
#align measurable.map_measurable_space_eq Measurable.map_measurableSpace_eq

theorem map_measurableSpace_eq_borel [SecondCountableTopology Y] {f : X â†’ Y} (hf : Measurable f)
    (hsurj : Surjective f) : MeasurableSpace.map f â€¹MeasurableSpace Xâ€º = borel Y := by
  have d := hf.mono le_rfl OpensMeasurableSpace.borel_le
  letI := borel Y; haveI : BorelSpace Y := âŸ¨rflâŸ©
  exact d.map_measurableSpace_eq hsurj
#align measurable.map_measurable_space_eq_borel Measurable.map_measurableSpace_eq_borel

theorem borelSpace_codomain [SecondCountableTopology Y] {f : X â†’ Y} (hf : Measurable f)
    (hsurj : Surjective f) : BorelSpace Y :=
  âŸ¨(hf.map_measurableSpace_eq hsurj).symm.trans <| hf.map_measurableSpace_eq_borel hsurjâŸ©
#align measurable.borel_space_codomain Measurable.borelSpace_codomain

/-- If `f : X â†’ Y` is a Borel measurable map from a standard Borel space to a topological space
with second countable topology, then the preimage of a set `s` is measurable if and only if
the set is measurable in `Set.range f`. -/
theorem measurableSet_preimage_iff_preimage_val {f : X â†’ Y} [SecondCountableTopology (range f)]
    (hf : Measurable f) {s : Set Y} :
    MeasurableSet (f â»Â¹' s) â†” MeasurableSet ((â†‘) â»Â¹' s : Set (range f)) :=
  have hf' : Measurable (rangeFactorization f) := hf.subtype_mk
  hf'.measurableSet_preimage_iff_of_surjective (s := Subtype.val â»Â¹' s) surjective_onto_range
#align measurable.measurable_set_preimage_iff_preimage_coe Measurable.measurableSet_preimage_iff_preimage_val

/-- If `f : X â†’ Y` is a Borel measurable map from a standard Borel space to a
topological space with second countable topology and the range of `f` is measurable,
then the preimage of a set `s` is measurable
if and only if the intesection with `Set.range f` is measurable. -/
theorem measurableSet_preimage_iff_inter_range {f : X â†’ Y} [SecondCountableTopology (range f)]
    (hf : Measurable f) (hr : MeasurableSet (range f)) {s : Set Y} :
    MeasurableSet (f â»Â¹' s) â†” MeasurableSet (s âˆ© range f) := by
  rw [hf.measurableSet_preimage_iff_preimage_val,
    â† (MeasurableEmbedding.subtype_coe hr).measurableSet_image, Subtype.image_preimage_coe]
#align measurable.measurable_set_preimage_iff_inter_range Measurable.measurableSet_preimage_iff_inter_range

/-- If `f : X â†’ Y` is a Borel measurable map from a standard Borel space
to a topological space with second countable topology,
then for any measurable space `Î²` and `g : Y â†’ Î²`, the composition `g âˆ˜ f` is
measurable if and only if the restriction of `g` to the range of `f` is measurable. -/
theorem measurable_comp_iff_restrict {f : X â†’ Y} [SecondCountableTopology (range f)]
    (hf : Measurable f) {g : Y â†’ Î²} : Measurable (g âˆ˜ f) â†” Measurable (restrict (range f) g) :=
  forallâ‚‚_congr fun s _ => measurableSet_preimage_iff_preimage_val hf (s := g â»Â¹' s)
#align measurable.measurable_comp_iff_restrict Measurable.measurable_comp_iff_restrict

/-- If `f : X â†’ Y` is a surjective Borel measurable map from a standard Borel space
to a topological space with second countable topology,
then for any measurable space `Î±` and `g : Y â†’ Î±`, the composition
`g âˆ˜ f` is measurable if and only if `g` is measurable. -/
theorem measurable_comp_iff_of_surjective [SecondCountableTopology Y] {f : X â†’ Y}
    (hf : Measurable f) (hsurj : Surjective f) {g : Y â†’ Î²} : Measurable (g âˆ˜ f) â†” Measurable g :=
  forallâ‚‚_congr fun s _ => measurableSet_preimage_iff_of_surjective hf hsurj (s := g â»Â¹' s)
#align measurable.measurable_comp_iff_of_surjective Measurable.measurable_comp_iff_of_surjective

end Measurable

theorem Continuous.map_eq_borel {X Y : Type*} [TopologicalSpace X] [PolishSpace X]
    [MeasurableSpace X] [BorelSpace X] [TopologicalSpace Y] [T2Space Y] [SecondCountableTopology Y]
    {f : X â†’ Y} (hf : Continuous f) (hsurj : Surjective f) :
    MeasurableSpace.map f â€¹MeasurableSpace Xâ€º = borel Y := by
  borelize Y
  exact hf.measurable.map_measurableSpace_eq hsurj
#align continuous.map_eq_borel Continuous.map_eq_borel

theorem Continuous.map_borel_eq {X Y : Type*} [TopologicalSpace X] [PolishSpace X]
    [TopologicalSpace Y] [T2Space Y] [SecondCountableTopology Y] {f : X â†’ Y} (hf : Continuous f)
    (hsurj : Surjective f) : MeasurableSpace.map f (borel X) = borel Y := by
  borelize X
  exact hf.map_eq_borel hsurj
#align continuous.map_borel_eq Continuous.map_borel_eq

instance Quotient.borelSpace {X : Type*} [TopologicalSpace X] [PolishSpace X] [MeasurableSpace X]
    [BorelSpace X] {s : Setoid X} [T2Space (Quotient s)] [SecondCountableTopology (Quotient s)] :
    BorelSpace (Quotient s) :=
  âŸ¨continuous_quotient_mk'.map_eq_borel (surjective_quotient_mk' _)âŸ©
#align quotient.borel_space Quotient.borelSpace

@[to_additive]
instance QuotientGroup.borelSpace {G : Type*} [TopologicalSpace G] [PolishSpace G] [Group G]
    [TopologicalGroup G] [MeasurableSpace G] [BorelSpace G] {N : Subgroup G} [N.Normal]
    [IsClosed (N : Set G)] : BorelSpace (G â§¸ N) :=
  -- porting note: 1st and 3rd `haveI`s were not needed in Lean 3
  haveI := Subgroup.t3_quotient_of_isClosed N
  haveI := QuotientGroup.secondCountableTopology (Î“ := N)
  Quotient.borelSpace
#align quotient_group.borel_space QuotientGroup.borelSpace
#align quotient_add_group.borel_space QuotientAddGroup.borelSpace

namespace MeasureTheory

/-! ### Injective images of Borel sets -/

variable {Î³ : Type*}

/-- The **Lusin-Souslin theorem**: the range of a continuous injective function defined on a Polish
space is Borel-measurable. -/
theorem measurableSet_range_of_continuous_injective {Î² : Type*} [TopologicalSpace Î³]
    [PolishSpace Î³] [TopologicalSpace Î²] [T2Space Î²] [MeasurableSpace Î²] [OpensMeasurableSpace Î²]
    {f : Î³ â†’ Î²} (f_cont : Continuous f) (f_inj : Injective f) :
    MeasurableSet (range f) := by
  /- We follow [Fremlin, *Measure Theory* (volume 4, 423I)][fremlin_vol4].
    Let `b = {s i}` be a countable basis for `Î±`. When `s i` and `s j` are disjoint, their images
    are disjoint analytic sets, hence by the separation theorem one can find a Borel-measurable set
    `q i j` separating them.
    Let `E i = closure (f '' s i) âˆ© â‹‚ j, q i j \ q j i`. It contains `f '' (s i)` and it is
    measurable. Let `F n = â‹ƒ E i`, where the union is taken over those `i` for which `diam (s i)`
    is bounded by some number `u n` tending to `0` with `n`.
    We claim that `range f = â‹‚ F n`, from which the measurability is obvious. The inclusion `âŠ†` is
    straightforward. To show `âŠ‡`, consider a point `x` in the intersection. For each `n`, it belongs
    to some `E i` with `diam (s i) â‰¤ u n`. Pick a point `y i âˆˆ s i`. We claim that for such `i`
    and `j`, the intersection `s i âˆ© s j` is nonempty: if it were empty, then thanks to the
    separating set `q i j` in the definition of `E i` one could not have `x âˆˆ E i âˆ© E j`.
    Since these two sets have small diameter, it follows that `y i` and `y j` are close.
    Thus, `y` is a Cauchy sequence, converging to a limit `z`. We claim that `f z = x`, completing
    the proof.
    Otherwise, one could find open sets `v` and `w` separating `f z` from `x`. Then, for large `n`,
    the image `f '' (s i)` would be included in `v` by continuity of `f`, so its closure would be
    contained in the closure of `v`, and therefore it would be disjoint from `w`. This is a
    contradiction since `x` belongs both to this closure and to `w`. -/
  letI := upgradePolishSpace Î³
  obtain âŸ¨b, b_count, b_nonempty, hbâŸ© :
    âˆƒ b : Set (Set Î³), b.Countable âˆ§ âˆ… âˆ‰ b âˆ§ IsTopologicalBasis b := exists_countable_basis Î³
  haveI : Encodable b := b_count.toEncodable
  let A := { p : b Ã— b // Disjoint (p.1 : Set Î³) p.2 }
  -- for each pair of disjoint sets in the topological basis `b`, consider Borel sets separating
  -- their images, by injectivity of `f` and the Lusin separation theorem.
  have : âˆ€ p : A, âˆƒ q : Set Î²,
      f '' (p.1.1 : Set Î³) âŠ† q âˆ§ Disjoint (f '' (p.1.2 : Set Î³)) q âˆ§ MeasurableSet q := by
    intro p
    apply
      AnalyticSet.measurablySeparable ((hb.isOpen p.1.1.2).analyticSet_image f_cont)
        ((hb.isOpen p.1.2.2).analyticSet_image f_cont)
    exact Disjoint.image p.2 (f_inj.injOn univ) (subset_univ _) (subset_univ _)
  choose q hq1 hq2 q_meas using this
  -- define sets `E i` and `F n` as in the proof sketch above
  let E : b â†’ Set Î² := fun s =>
    closure (f '' s) âˆ© â‹‚ (t : b) (ht : Disjoint s.1 t.1), q âŸ¨(s, t), htâŸ© \ q âŸ¨(t, s), ht.symmâŸ©
  obtain âŸ¨u, u_anti, u_pos, u_limâŸ© :
    âˆƒ u : â„• â†’ â„, StrictAnti u âˆ§ (âˆ€ n : â„•, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  let F : â„• â†’ Set Î² := fun n => â‹ƒ (s : b) (_ : IsBounded s.1 âˆ§ diam s.1 â‰¤ u n), E s
  -- it is enough to show that `range f = â‹‚ F n`, as the latter set is obviously measurable.
  suffices range f = â‹‚ n, F n by
    have E_meas : âˆ€ s : b, MeasurableSet (E s) := by
      intro b
      refine' isClosed_closure.measurableSet.inter _
      refine' MeasurableSet.iInter fun s => _
      exact MeasurableSet.iInter fun hs => (q_meas _).diff (q_meas _)
    have F_meas : âˆ€ n, MeasurableSet (F n) := by
      intro n
      refine' MeasurableSet.iUnion fun s => _
      exact MeasurableSet.iUnion fun _ => E_meas _
    rw [this]
    exact MeasurableSet.iInter fun n => F_meas n
  -- we check both inclusions.
  apply Subset.antisymm
  -- we start with the easy inclusion `range f âŠ† â‹‚ F n`. One just needs to unfold the definitions.
  Â· rintro x âŸ¨y, rflâŸ©
    refine mem_iInter.2 fun n => ?_
    obtain âŸ¨s, sb, ys, hsâŸ© : âˆƒ (s : Set Î³), s âˆˆ b âˆ§ y âˆˆ s âˆ§ s âŠ† ball y (u n / 2) := by
      apply hb.mem_nhds_iff.1
      exact ball_mem_nhds _ (half_pos (u_pos n))
    have diam_s : diam s â‰¤ u n := by
      apply (diam_mono hs isBounded_ball).trans
      convert diam_ball (x := y) (half_pos (u_pos n)).le
      ring
    refine' mem_iUnion.2 âŸ¨âŸ¨s, sbâŸ©, _âŸ©
    refine' mem_iUnion.2 âŸ¨âŸ¨isBounded_ball.subset hs, diam_sâŸ©, _âŸ©
    apply mem_inter (subset_closure (mem_image_of_mem _ ys))
    refine' mem_iInter.2 fun t => mem_iInter.2 fun ht => âŸ¨_, _âŸ©
    Â· apply hq1
      exact mem_image_of_mem _ ys
    Â· apply disjoint_left.1 (hq2 âŸ¨(t, âŸ¨s, sbâŸ©), ht.symmâŸ©)
      exact mem_image_of_mem _ ys
  -- Now, let us prove the harder inclusion `â‹‚ F n âŠ† range f`.
  Â· intro x hx
    -- pick for each `n` a good set `s n` of small diameter for which `x âˆˆ E (s n)`.
    have C1 : âˆ€ n, âˆƒ (s : b) (_ : IsBounded s.1 âˆ§ diam s.1 â‰¤ u n), x âˆˆ E s := fun n => by
      simpa only [mem_iUnion] using mem_iInter.1 hx n
    choose s hs hxs using C1
    have C2 : âˆ€ n, (s n).1.Nonempty := by
      intro n
      rw [nonempty_iff_ne_empty]
      intro hn
      have := (s n).2
      rw [hn] at this
      exact b_nonempty this
    -- choose a point `y n âˆˆ s n`.
    choose y hy using C2
    have I : âˆ€ m n, ((s m).1 âˆ© (s n).1).Nonempty := by
      intro m n
      rw [â† not_disjoint_iff_nonempty_inter]
      by_contra! h
      have A : x âˆˆ q âŸ¨(s m, s n), hâŸ© \ q âŸ¨(s n, s m), h.symmâŸ© :=
        haveI := mem_iInter.1 (hxs m).2 (s n)
        (mem_iInter.1 this h : _)
      have B : x âˆˆ q âŸ¨(s n, s m), h.symmâŸ© \ q âŸ¨(s m, s n), hâŸ© :=
        haveI := mem_iInter.1 (hxs n).2 (s m)
        (mem_iInter.1 this h.symm : _)
      exact A.2 B.1
    -- the points `y n` are nearby, and therefore they form a Cauchy sequence.
    have cauchy_y : CauchySeq y := by
      have : Tendsto (fun n => 2 * u n) atTop (ğ“ 0) := by
        simpa only [mul_zero] using u_lim.const_mul 2
      refine cauchySeq_of_le_tendsto_0' (fun n => 2 * u n) (fun m n hmn => ?_) this
      rcases I m n with âŸ¨z, zsm, zsnâŸ©
      calc
        dist (y m) (y n) â‰¤ dist (y m) z + dist z (y n) := dist_triangle _ _ _
        _ â‰¤ u m + u n :=
          (add_le_add ((dist_le_diam_of_mem (hs m).1 (hy m) zsm).trans (hs m).2)
            ((dist_le_diam_of_mem (hs n).1 zsn (hy n)).trans (hs n).2))
        _ â‰¤ 2 * u m := by linarith [u_anti.antitone hmn]
    haveI : Nonempty Î³ := âŸ¨y 0âŸ©
    -- let `z` be its limit.
    let z := limUnder atTop y
    have y_lim : Tendsto y atTop (ğ“ z) := cauchy_y.tendsto_limUnder
    suffices f z = x by
      rw [â† this]
      exact mem_range_self _
    -- assume for a contradiction that `f z â‰  x`.
    by_contra! hne
    -- introduce disjoint open sets `v` and `w` separating `f z` from `x`.
    obtain âŸ¨v, w, v_open, w_open, fzv, xw, hvwâŸ© := t2_separation hne
    obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ Î´ > (0 : â„), ball z Î´ âŠ† f â»Â¹' v := by
      apply Metric.mem_nhds_iff.1
      exact f_cont.continuousAt.preimage_mem_nhds (v_open.mem_nhds fzv)
    obtain âŸ¨n, hnâŸ© : âˆƒ n, u n + dist (y n) z < Î´ :=
      haveI : Tendsto (fun n => u n + dist (y n) z) atTop (ğ“ 0) := by
        simpa only [add_zero] using u_lim.add (tendsto_iff_dist_tendsto_zero.1 y_lim)
      ((tendsto_order.1 this).2 _ Î´pos).exists
    -- for large enough `n`, the image of `s n` is contained in `v`, by continuity of `f`.
    have fsnv : f '' s n âŠ† v := by
      rw [image_subset_iff]
      apply Subset.trans _ hÎ´
      intro a ha
      calc
        dist a z â‰¤ dist a (y n) + dist (y n) z := dist_triangle _ _ _
        _ â‰¤ u n + dist (y n) z :=
          (add_le_add_right ((dist_le_diam_of_mem (hs n).1 ha (hy n)).trans (hs n).2) _)
        _ < Î´ := hn
    -- as `x` belongs to the closure of `f '' (s n)`, it belongs to the closure of `v`.
    have : x âˆˆ closure v := closure_mono fsnv (hxs n).1
    -- this is a contradiction, as `x` is supposed to belong to `w`, which is disjoint from
    -- the closure of `v`.
    exact disjoint_left.1 (hvw.closure_left w_open) this xw
#align measure_theory.measurable_set_range_of_continuous_injective MeasureTheory.measurableSet_range_of_continuous_injective

theorem _root_.IsClosed.measurableSet_image_of_continuousOn_injOn
    [TopologicalSpace Î³] [PolishSpace Î³] {Î² : Type*} [TopologicalSpace Î²] [T2Space Î²]
    [MeasurableSpace Î²] [OpensMeasurableSpace Î²] {s : Set Î³} (hs : IsClosed s) {f : Î³ â†’ Î²}
    (f_cont : ContinuousOn f s) (f_inj : InjOn f s) : MeasurableSet (f '' s) := by
  rw [image_eq_range]
  haveI : PolishSpace s := IsClosed.polishSpace hs
  apply measurableSet_range_of_continuous_injective
  Â· rwa [continuousOn_iff_continuous_restrict] at f_cont
  Â· rwa [injOn_iff_injective] at f_inj
#align is_closed.measurable_set_image_of_continuous_on_inj_on IsClosed.measurableSet_image_of_continuousOn_injOn

variable {Î² : Type*} [tÎ² : TopologicalSpace Î²] [T2Space Î²] [MeasurableSpace Î²]
  {s : Set Î³} {f : Î³ â†’ Î²}

/-- The Lusin-Souslin theorem: if `s` is Borel-measurable in a Polish space, then its image under
a continuous injective map is also Borel-measurable. -/
theorem _root_.MeasurableSet.image_of_continuousOn_injOn [OpensMeasurableSpace Î²]
    [tÎ³ : TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³] [BorelSpace Î³]
    (hs : MeasurableSet s)
    (f_cont : ContinuousOn f s) (f_inj : InjOn f s) : MeasurableSet (f '' s) := by
  obtain âŸ¨t', t't, t'_polish, s_closed, _âŸ© :
    âˆƒ t' : TopologicalSpace Î³, t' â‰¤ tÎ³ âˆ§ @PolishSpace Î³ t' âˆ§ IsClosed[t'] s âˆ§ IsOpen[t'] s :=
    hs.isClopenable
  exact
    @IsClosed.measurableSet_image_of_continuousOn_injOn Î³ t' t'_polish Î² _ _ _ _ s s_closed f
      (f_cont.mono_dom t't) f_inj
#align measurable_set.image_of_continuous_on_inj_on MeasurableSet.image_of_continuousOn_injOn

/-- The Lusin-Souslin theorem: if `s` is Borel-measurable in a standard Borel space,
then its image under a measurable injective map taking values in a
second-countable topological space is also Borel-measurable. -/
theorem _root_.MeasurableSet.image_of_measurable_injOn [OpensMeasurableSpace Î²]
    [MeasurableSpace Î³] [StandardBorelSpace Î³] [SecondCountableTopology Î²]
    (hs : MeasurableSet s) (f_meas : Measurable f) (f_inj : InjOn f s) :
    MeasurableSet (f '' s) := by
  letI := upgradeStandardBorel Î³
  let tÎ³ : TopologicalSpace Î³ := inferInstance
  -- for a finer Polish topology, `f` is continuous. Therefore, one may apply the corresponding
  -- result for continuous maps.
  obtain âŸ¨t', t't, f_cont, t'_polishâŸ© :
    âˆƒ t' : TopologicalSpace Î³, t' â‰¤ tÎ³ âˆ§ @Continuous Î³ Î² t' tÎ² f âˆ§ @PolishSpace Î³ t' :=
    f_meas.exists_continuous
  have M : MeasurableSet[@borel Î³ t'] s :=
    @Continuous.measurable Î³ Î³ t' (@borel Î³ t')
      (@BorelSpace.opensMeasurable Î³ t' (@borel Î³ t') (@BorelSpace.mk _ _ (borel Î³) rfl))
      tÎ³ _ _ _ (continuous_id_of_le t't) s hs
  exact
    @MeasurableSet.image_of_continuousOn_injOn Î³
      Î² _ _ _  s f _ t' t'_polish (@borel Î³ t') (@BorelSpace.mk _ _ (borel Î³) rfl)
      M (@Continuous.continuousOn Î³ Î² t' tÎ² f s f_cont) f_inj
#align measurable_set.image_of_measurable_inj_on MeasurableSet.image_of_measurable_injOn

/-- An injective continuous function on a Polish space is a measurable embedding. -/
theorem _root_.Continuous.measurableEmbedding [BorelSpace Î²]
    [TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³] [BorelSpace Î³]
    (f_cont : Continuous f) (f_inj : Injective f) :
    MeasurableEmbedding f :=
  { injective := f_inj
    measurable := f_cont.measurable
    measurableSet_image' := fun _u hu =>
      hu.image_of_continuousOn_injOn f_cont.continuousOn (f_inj.injOn _) }
#align continuous.measurable_embedding Continuous.measurableEmbedding

/-- If `s` is Borel-measurable in a Polish space and `f` is continuous injective on `s`, then
the restriction of `f` to `s` is a measurable embedding. -/
theorem _root_.ContinuousOn.measurableEmbedding [BorelSpace Î²]
    [TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³] [BorelSpace Î³]
    (hs : MeasurableSet s) (f_cont : ContinuousOn f s)
    (f_inj : InjOn f s) : MeasurableEmbedding (s.restrict f) :=
  { injective := injOn_iff_injective.1 f_inj
    measurable := (continuousOn_iff_continuous_restrict.1 f_cont).measurable
    measurableSet_image' := by
      intro u hu
      have A : MeasurableSet (((â†‘) : s â†’ Î³) '' u) :=
        (MeasurableEmbedding.subtype_coe hs).measurableSet_image.2 hu
      have B : MeasurableSet (f '' (((â†‘) : s â†’ Î³) '' u)) :=
        A.image_of_continuousOn_injOn (f_cont.mono (Subtype.coe_image_subset s u))
          (f_inj.mono (Subtype.coe_image_subset s u))
      rwa [â† image_comp] at B }
#align continuous_on.measurable_embedding ContinuousOn.measurableEmbedding

/-- An injective measurable function from a standard Borel space to a
second-countable topological space is a measurable embedding. -/
theorem _root_.Measurable.measurableEmbedding [OpensMeasurableSpace Î²]
    [MeasurableSpace Î³] [StandardBorelSpace Î³] [SecondCountableTopology Î²]
    (f_meas : Measurable f) (f_inj : Injective f) : MeasurableEmbedding f :=
  { injective := f_inj
    measurable := f_meas
    measurableSet_image' := fun _u hu => hu.image_of_measurable_injOn f_meas (f_inj.injOn _) }
#align measurable.measurable_embedding Measurable.measurableEmbedding

/-- If one Polish topology on a type refines another, they have the same Borel sets.-/
theorem borel_eq_borel_of_le {t t' : TopologicalSpace Î³}
    (ht : PolishSpace (h := t)) (ht' : PolishSpace (h := t')) (hle : t â‰¤ t') :
    @borel _ t = @borel _ t' := by
  refine' le_antisymm _ (borel_anti hle)
  intro s hs
  have e := @Continuous.measurableEmbedding
    _ _ t' _ (@borel _ t') _ (@BorelSpace.mk _ _ (borel Î³) rfl)
    t _ (@borel _ t) (@BorelSpace.mk _ t (@borel _ t) rfl) (continuous_id_of_le hle) injective_id
  convert e.measurableSet_image.2 hs
  simp only [id_eq, image_id']

/-- In a Polish space, a set is clopenable if and only if it is Borel-measurable. -/
theorem isClopenable_iff_measurableSet
    [tÎ³ : TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³] [BorelSpace Î³] :
    IsClopenable s â†” MeasurableSet s := by
  -- we already know that a measurable set is clopenable. Conversely, assume that `s` is clopenable.
  refine' âŸ¨fun hs => _, fun hs => hs.isClopenableâŸ©
  borelize Î³
  -- consider a finer topology `t'` in which `s` is open and closed.
  obtain âŸ¨t', t't, t'_polish, _, s_openâŸ© :
    âˆƒ t' : TopologicalSpace Î³, t' â‰¤ tÎ³ âˆ§ @PolishSpace Î³ t' âˆ§ IsClosed[t'] s âˆ§ IsOpen[t'] s := hs
  rw [â† borel_eq_borel_of_le t'_polish _ t't]
  Â· exact MeasurableSpace.measurableSet_generateFrom s_open
  infer_instance

/-- The set of points for which a measurable sequence of functions converges to a given value
is measurable. -/
@[measurability]
lemma measurableSet_tendsto_nhds [TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³]
    [hÎ³ : OpensMeasurableSpace Î³] [Countable Î¹] {l : Filter Î¹}
    [l.IsCountablyGenerated] {f : Î¹ â†’ Î² â†’ Î³} (hf : âˆ€ i, Measurable (f i)) (c : Î³) :
    MeasurableSet { x | Tendsto (fun n â†¦ f n x) l (ğ“ c) } := by
  letI := upgradePolishSpace Î³
  rcases l.exists_antitone_basis with âŸ¨u, huâŸ©
  have h : âˆ€ x, HasAntitoneBasis (l.map (fun n â†¦ f n x)) (fun n â†¦ (fun n â†¦ f n x) '' u n) :=
    fun x â†¦ hu.map (m := fun n â†¦ f n x)
  change MeasurableSet { x | l.map (fun n â†¦ f n x) â‰¤ ğ“ c }
  simp_rw [Filter.HasBasis.le_basis_iff (h _).toHasBasis Metric.nhds_basis_ball_inv_nat_succ,
    Set.setOf_forall]
  refine MeasurableSet.biInter Set.countable_univ fun K _ â†¦ ?_
  simp_rw [Set.setOf_exists, true_and]
  refine MeasurableSet.iUnion fun N â†¦ ?_
  simp_rw [image_subset_iff]
  change MeasurableSet {x | âˆ€ i âˆˆ u N, i âˆˆ (fun n â†¦ f n x) â»Â¹' Metric.ball c (1 / (K + 1))}
  simp_rw [Set.setOf_forall]
  refine MeasurableSet.biInter (to_countable (u N)) fun i _ â†¦ ?_
  simp only [one_div, mem_preimage, Metric.mem_ball]
  exact measurableSet_lt (Measurable.dist (hf i) measurable_const) measurable_const

/-- The set of points for which a measurable sequence of functions converges is measurable. -/
@[measurability]
theorem measurableSet_exists_tendsto [TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³]
    [hÎ³ : OpensMeasurableSpace Î³] [Countable Î¹] {l : Filter Î¹}
    [l.IsCountablyGenerated] {f : Î¹ â†’ Î² â†’ Î³} (hf : âˆ€ i, Measurable (f i)) :
    MeasurableSet { x | âˆƒ c, Tendsto (fun n => f n x) l (ğ“ c) } := by
  rcases l.eq_or_neBot with rfl | hl
  Â· simp
  letI := upgradePolishSpace Î³
  rcases l.exists_antitone_basis with âŸ¨u, huâŸ©
  simp_rw [â† cauchy_map_iff_exists_tendsto]
  change MeasurableSet { x | _ âˆ§ _ }
  have :
    âˆ€ x,
      (map (fun i => f i x) l Ã—Ë¢ map (fun i => f i x) l).HasAntitoneBasis fun n =>
        ((fun i => f i x) '' u n) Ã—Ë¢ ((fun i => f i x) '' u n) :=
    fun x => hu.map.prod hu.map
  simp_rw [and_iff_right (hl.map _),
    Filter.HasBasis.le_basis_iff (this _).toHasBasis Metric.uniformity_basis_dist_inv_nat_succ,
    Set.setOf_forall]
  refine' MeasurableSet.biInter Set.countable_univ fun K _ => _
  simp_rw [Set.setOf_exists, true_and]
  refine' MeasurableSet.iUnion fun N => _
  simp_rw [prod_image_image_eq, image_subset_iff, prod_subset_iff, Set.setOf_forall]
  exact
    MeasurableSet.biInter (to_countable (u N)) fun i _ =>
      MeasurableSet.biInter (to_countable (u N)) fun j _ =>
        measurableSet_lt (Measurable.dist (hf i) (hf j)) measurable_const
#align measure_theory.measurable_set_exists_tendsto MeasureTheory.measurableSet_exists_tendsto

end MeasureTheory

namespace StandardBorelSpace

variable [MeasurableSpace Î±] [StandardBorelSpace Î±]

/-- If `s` is a measurable set in a standard Borel space, there is a compatible Polish topology
making `s` clopen. -/
theorem _root_.MeasurableSet.isClopenable' {s : Set Î±} (hs : MeasurableSet s) :
    âˆƒ _ : TopologicalSpace Î±, BorelSpace Î± âˆ§ PolishSpace Î± âˆ§ IsClosed s âˆ§ IsOpen s := by
  letI := upgradeStandardBorel Î±
  obtain âŸ¨t, hle, ht, s_clopenâŸ© := hs.isClopenable
  refine' âŸ¨t, _, ht, s_clopenâŸ©
  constructor
  rw [eq_borel_upgradeStandardBorel Î±, borel_eq_borel_of_le ht _ hle]
  infer_instance

/-- A measurable subspace of a standard Borel space is standard Borel. -/
theorem _root_.MeasurableSet.standardBorel {s : Set Î±} (hs : MeasurableSet s) :
    StandardBorelSpace s := by
  obtain âŸ¨_, _, _, s_closed, _âŸ© := hs.isClopenable'
  haveI := s_closed.polishSpace
  infer_instance

end StandardBorelSpace

/-! ### The Borel Isomorphism Theorem -/

namespace PolishSpace

variable {Î² : Type*}

variable [MeasurableSpace Î±] [MeasurableSpace Î²] [StandardBorelSpace Î±] [StandardBorelSpace Î²]

/-- If two standard Borel spaces admit Borel measurable injections to one another,
then they are Borel isomorphic.-/
noncomputable def borelSchroederBernstein {f : Î± â†’ Î²} {g : Î² â†’ Î±} (fmeas : Measurable f)
    (finj : Function.Injective f) (gmeas : Measurable g) (ginj : Function.Injective g) : Î± â‰ƒáµ Î² :=
  letI := upgradeStandardBorel Î±
  letI := upgradeStandardBorel Î²
  (fmeas.measurableEmbedding finj).schroederBernstein (gmeas.measurableEmbedding ginj)
#align polish_space.borel_schroeder_bernstein PolishSpace.borelSchroederBernstein

/-- Any uncountable standard Borel space is Borel isomorphic to the Cantor space `â„• â†’ Bool`.-/
noncomputable def measurableEquivNatBoolOfNotCountable (h : Â¬Countable Î±) : Î± â‰ƒáµ (â„• â†’ Bool) := by
  apply Nonempty.some
  letI := upgradeStandardBorel Î±
  obtain âŸ¨f, -, fcts, finjâŸ© :=
    isClosed_univ.exists_nat_bool_injection_of_not_countable
      (by rwa [â† countable_coe_iff, (Equiv.Set.univ _).countable_iff])
  obtain âŸ¨g, gmeas, ginjâŸ© := MeasurableSpace.measurable_injection_nat_bool_of_countablyGenerated Î±
  exact âŸ¨borelSchroederBernstein gmeas ginj fcts.measurable finjâŸ©
#align polish_space.measurable_equiv_nat_bool_of_not_countable PolishSpace.measurableEquivNatBoolOfNotCountable

/-- The **Borel Isomorphism Theorem**: Any two uncountable standard Borel spaces are
Borel isomorphic.-/
noncomputable def measurableEquivOfNotCountable (hÎ± : Â¬Countable Î±) (hÎ² : Â¬Countable Î²) : Î± â‰ƒáµ Î² :=
  (measurableEquivNatBoolOfNotCountable hÎ±).trans (measurableEquivNatBoolOfNotCountable hÎ²).symm
#align polish_space.measurable_equiv_of_not_countable PolishSpace.measurableEquivOfNotCountable

/-- The **Borel Isomorphism Theorem**: If two standard Borel spaces have the same cardinality,
they are Borel isomorphic.-/
noncomputable def Equiv.measurableEquiv (e : Î± â‰ƒ Î²) : Î± â‰ƒáµ Î² := by
  by_cases h : Countable Î±
  Â· letI := Countable.of_equiv Î± e
    refine âŸ¨e, ?_, ?_âŸ© <;> apply measurable_of_countable
  refine' measurableEquivOfNotCountable h _
  rwa [e.countable_iff] at h
#align polish_space.equiv.measurable_equiv PolishSpace.Equiv.measurableEquiv

end PolishSpace

namespace MeasureTheory

variable (Î±)
variable [MeasurableSpace Î±] [StandardBorelSpace Î±]

theorem exists_nat_measurableEquiv_range_coe_fin_of_finite [Finite Î±] :
    âˆƒ n : â„•, Nonempty (Î± â‰ƒáµ range ((â†‘) : Fin n â†’ â„)) := by
  obtain âŸ¨n, âŸ¨n_equivâŸ©âŸ© := Finite.exists_equiv_fin Î±
  refine' âŸ¨n, âŸ¨PolishSpace.Equiv.measurableEquiv (n_equiv.trans _)âŸ©âŸ©
  exact Equiv.ofInjective _ (Nat.cast_injective.comp Fin.val_injective)
#align measure_theory.exists_nat_measurable_equiv_range_coe_fin_of_finite MeasureTheory.exists_nat_measurableEquiv_range_coe_fin_of_finite

theorem measurableEquiv_range_coe_nat_of_infinite_of_countable [Infinite Î±] [Countable Î±] :
    Nonempty (Î± â‰ƒáµ range ((â†‘) : â„• â†’ â„)) := by
  have : PolishSpace (range ((â†‘) : â„• â†’ â„)) :=
    Nat.closedEmbedding_coe_real.isClosedMap.closed_range.polishSpace
  refine' âŸ¨PolishSpace.Equiv.measurableEquiv _âŸ©
  refine' (nonempty_equiv_of_countable.some : Î± â‰ƒ â„•).trans _
  exact Equiv.ofInjective ((â†‘) : â„• â†’ â„) Nat.cast_injective
#align measure_theory.measurable_equiv_range_coe_nat_of_infinite_of_countable MeasureTheory.measurableEquiv_range_coe_nat_of_infinite_of_countable

/-- Any standard Borel space is measurably equivalent to a subset of the reals. -/
theorem exists_subset_real_measurableEquiv : âˆƒ s : Set â„, MeasurableSet s âˆ§ Nonempty (Î± â‰ƒáµ s) := by
  by_cases hÎ± : Countable Î±
  Â· cases finite_or_infinite Î±
    Â· obtain âŸ¨n, h_nonempty_equivâŸ© := exists_nat_measurableEquiv_range_coe_fin_of_finite Î±
      refine' âŸ¨_, _, h_nonempty_equivâŸ©
      letI : MeasurableSpace (Fin n) := borel (Fin n)
      haveI : BorelSpace (Fin n) := âŸ¨rflâŸ©
      refine' MeasurableEmbedding.measurableSet_range _
      Â· infer_instance
      Â· exact
          continuous_of_discreteTopology.measurableEmbedding
            (Nat.cast_injective.comp Fin.val_injective)
    Â· refine' âŸ¨_, _, measurableEquiv_range_coe_nat_of_infinite_of_countable Î±âŸ©
      refine' MeasurableEmbedding.measurableSet_range _
      Â· infer_instance
      Â· exact continuous_of_discreteTopology.measurableEmbedding Nat.cast_injective
  Â· refine'
      âŸ¨univ, MeasurableSet.univ,
        âŸ¨(PolishSpace.measurableEquivOfNotCountable hÎ± _ : Î± â‰ƒáµ (univ : Set â„))âŸ©âŸ©
    rw [countable_coe_iff]
    exact Cardinal.not_countable_real
#align measure_theory.exists_subset_real_measurable_equiv MeasureTheory.exists_subset_real_measurableEquiv

/-- Any standard Borel space embeds measurably into the reals. -/
theorem exists_measurableEmbedding_real : âˆƒ f : Î± â†’ â„, MeasurableEmbedding f := by
  obtain âŸ¨s, hs, âŸ¨eâŸ©âŸ© := exists_subset_real_measurableEquiv Î±
  exact âŸ¨(â†‘) âˆ˜ e, (MeasurableEmbedding.subtype_coe hs).comp e.measurableEmbeddingâŸ©
#align measure_theory.exists_measurable_embedding_real MeasureTheory.exists_measurableEmbedding_real

end MeasureTheory
