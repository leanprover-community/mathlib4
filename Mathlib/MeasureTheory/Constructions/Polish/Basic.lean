/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Felix Weilacher
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Metric
import Mathlib.MeasureTheory.Constructions.BorelSpace.Order
import Mathlib.Topology.MetricSpace.Perfect
import Mathlib.Topology.Separation.CountableSeparatingOn

/-!
# The Borel sigma-algebra on Polish spaces

We discuss several results pertaining to the relationship between the topology and the Borel
structure on Polish spaces.

## Main definitions and results

First, we define standard Borel spaces.

* A `StandardBorelSpace Œ±` is a typeclass for measurable spaces which arise as the Borel sets
  of some Polish topology.

Next, we define the class of analytic sets and establish its basic properties.

* `MeasureTheory.AnalyticSet s`: a set in a topological space is analytic if it is the continuous
  image of a Polish space. Equivalently, it is empty, or the image of `‚Ñï ‚Üí ‚Ñï`.
* `MeasureTheory.AnalyticSet.image_of_continuous`: a continuous image of an analytic set is
  analytic.
* `MeasurableSet.analyticSet`: in a Polish space, any Borel-measurable set is analytic.

Then, we show Lusin's theorem that two disjoint analytic sets can be separated by Borel sets.

* `MeasurablySeparable s t` states that there exists a measurable set containing `s` and disjoint
  from `t`.
* `AnalyticSet.measurablySeparable` shows that two disjoint analytic sets are separated by a
  Borel set.

We then prove the Lusin-Souslin theorem that a continuous injective image of a Borel subset of
a Polish space is Borel. The proof of this nontrivial result relies on the above results on
analytic sets.

* `MeasurableSet.image_of_continuousOn_injOn` asserts that, if `s` is a Borel measurable set in
  a Polish space, then the image of `s` under a continuous injective map is still Borel measurable.
* `Continuous.measurableEmbedding` states that a continuous injective map on a Polish space
  is a measurable embedding for the Borel sigma-algebra.
* `ContinuousOn.measurableEmbedding` is the same result for a map restricted to a measurable set
  on which it is continuous.
* `Measurable.measurableEmbedding` states that a measurable injective map from
  a standard Borel space to a second-countable topological space is a measurable embedding.
* `isClopenable_iff_measurableSet`: in a Polish space, a set is clopenable (i.e., it can be made
  open and closed by using a finer Polish topology) if and only if it is Borel-measurable.

We use this to prove several versions of the Borel isomorphism theorem.

* `PolishSpace.measurableEquivOfNotCountable` : Any two uncountable standard Borel spaces
  are Borel isomorphic.
* `PolishSpace.Equiv.measurableEquiv` : Any two standard Borel spaces of the same cardinality
  are Borel isomorphic.
-/


open Set Function PolishSpace PiNat TopologicalSpace Bornology Metric Filter Topology MeasureTheory

/-! ### Standard Borel Spaces -/

variable (Œ± : Type*)

/-- A standard Borel space is a measurable space arising as the Borel sets of some Polish topology.
This is useful in situations where a space has no natural topology or
the natural topology in a space is non-Polish.

To endow a standard Borel space `Œ±` with a compatible Polish topology, use
`letI := upgradeStandardBorel Œ±`. One can then use `eq_borel_upgradeStandardBorel Œ±` to
rewrite the `MeasurableSpace Œ±` instance to `borel Œ± t`, where `t` is the new topology. -/
class StandardBorelSpace [MeasurableSpace Œ±] : Prop where
  /-- There exists a compatible Polish topology. -/
  polish : ‚àÉ _ : TopologicalSpace Œ±, BorelSpace Œ± ‚àß PolishSpace Œ±

/-- A convenience class similar to `UpgradedPolishSpace`. No instance should be registered.
Instead one should use `letI := upgradeStandardBorel Œ±`. -/
class UpgradedStandardBorel extends MeasurableSpace Œ±, TopologicalSpace Œ±,
  BorelSpace Œ±, PolishSpace Œ±

/-- Use as `letI := upgradeStandardBorel Œ±` to endow a standard Borel space `Œ±` with
a compatible Polish topology.

Warning: following this with `borelize Œ±` will cause an error. Instead, one can
rewrite with `eq_borel_upgradeStandardBorel Œ±`.
TODO: fix the corresponding bug in `borelize`. -/
noncomputable
def upgradeStandardBorel [MeasurableSpace Œ±] [h : StandardBorelSpace Œ±] :
    UpgradedStandardBorel Œ± := by
  choose œÑ hb hp using h.polish
  constructor

/-- The `MeasurableSpace Œ±` instance on a `StandardBorelSpace` `Œ±` is equal to
the Borel sets of `upgradeStandardBorel Œ±`. -/
theorem eq_borel_upgradeStandardBorel [MeasurableSpace Œ±] [StandardBorelSpace Œ±] :
    ‚ÄπMeasurableSpace Œ±‚Ä∫ = @borel _ (upgradeStandardBorel Œ±).toTopologicalSpace :=
  @BorelSpace.measurable_eq _ (upgradeStandardBorel Œ±).toTopologicalSpace _
    (upgradeStandardBorel Œ±).toBorelSpace

variable {Œ±}

section

variable [MeasurableSpace Œ±]

-- See note [lower instance priority]
instance (priority := 100) standardBorel_of_polish [œÑ : TopologicalSpace Œ±]
    [BorelSpace Œ±] [PolishSpace Œ±] : StandardBorelSpace Œ± := by exists œÑ

-- See note [lower instance priority]
instance (priority := 100) standardBorelSpace_of_discreteMeasurableSpace [DiscreteMeasurableSpace Œ±]
    [Countable Œ±] : StandardBorelSpace Œ± :=
  let _ : TopologicalSpace Œ± := ‚ä•
  have : DiscreteTopology Œ± := ‚ü®rfl‚ü©
  inferInstance

-- See note [lower instance priority]
instance (priority := 100) countablyGenerated_of_standardBorel [StandardBorelSpace Œ±] :
    MeasurableSpace.CountablyGenerated Œ± :=
  letI := upgradeStandardBorel Œ±
  inferInstance

-- See note [lower instance priority]
instance (priority := 100) measurableSingleton_of_standardBorel [StandardBorelSpace Œ±] :
    MeasurableSingletonClass Œ± :=
  letI := upgradeStandardBorel Œ±
  inferInstance

namespace StandardBorelSpace

variable {Œ≤ : Type*} [MeasurableSpace Œ≤]

section instances

/-- A product of two standard Borel spaces is standard Borel. -/
instance prod [StandardBorelSpace Œ±] [StandardBorelSpace Œ≤] : StandardBorelSpace (Œ± √ó Œ≤) :=
  letI := upgradeStandardBorel Œ±
  letI := upgradeStandardBorel Œ≤
  inferInstance

/-- A product of countably many standard Borel spaces is standard Borel. -/
instance pi_countable {Œπ : Type*} [Countable Œπ] {Œ± : Œπ ‚Üí Type*} [‚àÄ n, MeasurableSpace (Œ± n)]
    [‚àÄ n, StandardBorelSpace (Œ± n)] : StandardBorelSpace (‚àÄ n, Œ± n) :=
  letI := fun n => upgradeStandardBorel (Œ± n)
  inferInstance

end instances

end StandardBorelSpace

end

variable {Œπ : Type*}

namespace MeasureTheory

variable [TopologicalSpace Œ±]

/-! ### Analytic sets -/

/-- An analytic set is a set which is the continuous image of some Polish space. There are several
equivalent characterizations of this definition. For the definition, we pick one that avoids
universe issues: a set is analytic if and only if it is a continuous image of `‚Ñï ‚Üí ‚Ñï` (or if it
is empty). The above more usual characterization is given
in `analyticSet_iff_exists_polishSpace_range`.

Warning: these are analytic sets in the context of descriptive set theory (which is why they are
registered in the namespace `MeasureTheory`). They have nothing to do with analytic sets in the
context of complex analysis. -/
irreducible_def AnalyticSet (s : Set Œ±) : Prop :=
  s = ‚àÖ ‚à® ‚àÉ f : (‚Ñï ‚Üí ‚Ñï) ‚Üí Œ±, Continuous f ‚àß range f = s

theorem analyticSet_empty : AnalyticSet (‚àÖ : Set Œ±) := by
  rw [AnalyticSet]
  exact Or.inl rfl

theorem analyticSet_range_of_polishSpace {Œ≤ : Type*} [TopologicalSpace Œ≤] [PolishSpace Œ≤]
    {f : Œ≤ ‚Üí Œ±} (f_cont : Continuous f) : AnalyticSet (range f) := by
  cases isEmpty_or_nonempty Œ≤
  ¬∑ rw [range_eq_empty]
    exact analyticSet_empty
  ¬∑ rw [AnalyticSet]
    obtain ‚ü®g, g_cont, hg‚ü© : ‚àÉ g : (‚Ñï ‚Üí ‚Ñï) ‚Üí Œ≤, Continuous g ‚àß Surjective g :=
      exists_nat_nat_continuous_surjective Œ≤
    refine Or.inr ‚ü®f ‚àò g, f_cont.comp g_cont, ?_‚ü©
    rw [hg.range_comp]

/-- The image of an open set under a continuous map is analytic. -/
theorem _root_.IsOpen.analyticSet_image {Œ≤ : Type*} [TopologicalSpace Œ≤] [PolishSpace Œ≤]
    {s : Set Œ≤} (hs : IsOpen s) {f : Œ≤ ‚Üí Œ±} (f_cont : Continuous f) : AnalyticSet (f '' s) := by
  rw [image_eq_range]
  haveI : PolishSpace s := hs.polishSpace
  exact analyticSet_range_of_polishSpace (f_cont.comp continuous_subtype_val)

/-- A set is analytic if and only if it is the continuous image of some Polish space. -/
theorem analyticSet_iff_exists_polishSpace_range {s : Set Œ±} :
    AnalyticSet s ‚Üî
      ‚àÉ (Œ≤ : Type) (h : TopologicalSpace Œ≤) (_ : @PolishSpace Œ≤ h) (f : Œ≤ ‚Üí Œ±),
        @Continuous _ _ h _ f ‚àß range f = s := by
  constructor
  ¬∑ intro h
    rw [AnalyticSet] at h
    rcases h with h | h
    ¬∑ refine ‚ü®Empty, inferInstance, inferInstance, Empty.elim, continuous_bot, ?_‚ü©
      rw [h]
      exact range_eq_empty _
    ¬∑ exact ‚ü®‚Ñï ‚Üí ‚Ñï, inferInstance, inferInstance, h‚ü©
  ¬∑ rintro ‚ü®Œ≤, h, h', f, f_cont, f_range‚ü©
    rw [‚Üê f_range]
    exact analyticSet_range_of_polishSpace f_cont

/-- The continuous image of an analytic set is analytic -/
theorem AnalyticSet.image_of_continuousOn {Œ≤ : Type*} [TopologicalSpace Œ≤] {s : Set Œ±}
    (hs : AnalyticSet s) {f : Œ± ‚Üí Œ≤} (hf : ContinuousOn f s) : AnalyticSet (f '' s) := by
  rcases analyticSet_iff_exists_polishSpace_range.1 hs with ‚ü®Œ≥, Œ≥top, Œ≥polish, g, g_cont, gs‚ü©
  have : f '' s = range (f ‚àò g) := by rw [range_comp, gs]
  rw [this]
  apply analyticSet_range_of_polishSpace
  apply hf.comp_continuous g_cont fun x => _
  rw [‚Üê gs]
  exact mem_range_self

theorem AnalyticSet.image_of_continuous {Œ≤ : Type*} [TopologicalSpace Œ≤] {s : Set Œ±}
    (hs : AnalyticSet s) {f : Œ± ‚Üí Œ≤} (hf : Continuous f) : AnalyticSet (f '' s) :=
  hs.image_of_continuousOn hf.continuousOn

/-- A countable intersection of analytic sets is analytic. -/
theorem AnalyticSet.iInter [hŒπ : Nonempty Œπ] [Countable Œπ] [T2Space Œ±] {s : Œπ ‚Üí Set Œ±}
    (hs : ‚àÄ n, AnalyticSet (s n)) : AnalyticSet (‚ãÇ n, s n) := by
  rcases hŒπ with ‚ü®i‚ÇÄ‚ü©
  /- For the proof, write each `s n` as the continuous image under a map `f n` of a
    Polish space `Œ≤ n`. The product space `Œ≥ = Œ† n, Œ≤ n` is also Polish, and so is the subset
    `t` of sequences `x n` for which `f n (x n)` is independent of `n`. The set `t` is Polish, and
    the range of `x ‚Ü¶ f 0 (x 0)` on `t` is exactly `‚ãÇ n, s n`, so this set is analytic. -/
  choose Œ≤ hŒ≤ h'Œ≤ f f_cont f_range using fun n =>
    analyticSet_iff_exists_polishSpace_range.1 (hs n)
  let Œ≥ := ‚àÄ n, Œ≤ n
  let t : Set Œ≥ := ‚ãÇ n, { x | f n (x n) = f i‚ÇÄ (x i‚ÇÄ) }
  have t_closed : IsClosed t := by
    apply isClosed_iInter
    intro n
    exact
      isClosed_eq ((f_cont n).comp (continuous_apply n)) ((f_cont i‚ÇÄ).comp (continuous_apply i‚ÇÄ))
  haveI : PolishSpace t := t_closed.polishSpace
  let F : t ‚Üí Œ± := fun x => f i‚ÇÄ ((x : Œ≥) i‚ÇÄ)
  have F_cont : Continuous F := (f_cont i‚ÇÄ).comp ((continuous_apply i‚ÇÄ).comp continuous_subtype_val)
  have F_range : range F = ‚ãÇ n : Œπ, s n := by
    apply Subset.antisymm
    ¬∑ rintro y ‚ü®x, rfl‚ü©
      refine mem_iInter.2 fun n => ?_
      have : f n ((x : Œ≥) n) = F x := (mem_iInter.1 x.2 n :)
      rw [‚Üê this, ‚Üê f_range n]
      exact mem_range_self _
    ¬∑ intro y hy
      have A : ‚àÄ n, ‚àÉ x : Œ≤ n, f n x = y := by
        intro n
        rw [‚Üê mem_range, f_range n]
        exact mem_iInter.1 hy n
      choose x hx using A
      have xt : x ‚àà t := by
        refine mem_iInter.2 fun n => ?_
        simp [Œ≥, hx]
      refine ‚ü®‚ü®x, xt‚ü©, ?_‚ü©
      exact hx i‚ÇÄ
  rw [‚Üê F_range]
  exact analyticSet_range_of_polishSpace F_cont

/-- A countable union of analytic sets is analytic. -/
theorem AnalyticSet.iUnion [Countable Œπ] {s : Œπ ‚Üí Set Œ±} (hs : ‚àÄ n, AnalyticSet (s n)) :
    AnalyticSet (‚ãÉ n, s n) := by
  /- For the proof, write each `s n` as the continuous image under a map `f n` of a
    Polish space `Œ≤ n`. The union space `Œ≥ = Œ£ n, Œ≤ n` is also Polish, and the map `F : Œ≥ ‚Üí Œ±` which
    coincides with `f n` on `Œ≤ n` sends it to `‚ãÉ n, s n`. -/
  choose Œ≤ hŒ≤ h'Œ≤ f f_cont f_range using fun n =>
    analyticSet_iff_exists_polishSpace_range.1 (hs n)
  let Œ≥ := Œ£ n, Œ≤ n
  let F : Œ≥ ‚Üí Œ± := fun ‚ü®n, x‚ü© ‚Ü¶ f n x
  have F_cont : Continuous F := continuous_sigma f_cont
  have F_range : range F = ‚ãÉ n, s n := by
    simp only [Œ≥, F, range_sigma_eq_iUnion_range, f_range]
  rw [‚Üê F_range]
  exact analyticSet_range_of_polishSpace F_cont

theorem _root_.IsClosed.analyticSet [PolishSpace Œ±] {s : Set Œ±} (hs : IsClosed s) :
    AnalyticSet s := by
  haveI : PolishSpace s := hs.polishSpace
  rw [‚Üê @Subtype.range_val Œ± s]
  exact analyticSet_range_of_polishSpace continuous_subtype_val

/-- Given a Borel-measurable set in a Polish space, there exists a finer Polish topology making
it clopen. This is in fact an equivalence, see `isClopenable_iff_measurableSet`. -/
theorem _root_.MeasurableSet.isClopenable [PolishSpace Œ±] [MeasurableSpace Œ±] [BorelSpace Œ±]
    {s : Set Œ±} (hs : MeasurableSet s) : IsClopenable s := by
  revert s
  apply MeasurableSet.induction_on_open
  ¬∑ exact fun u hu => hu.isClopenable
  ¬∑ exact fun u _ h'u => h'u.compl
  ¬∑ exact fun f _ _ hf => IsClopenable.iUnion hf

/-- A Borel-measurable set in a Polish space is analytic. -/
theorem _root_.MeasurableSet.analyticSet {Œ± : Type*} [t : TopologicalSpace Œ±] [PolishSpace Œ±]
    [MeasurableSpace Œ±] [BorelSpace Œ±] {s : Set Œ±} (hs : MeasurableSet s) : AnalyticSet s := by
  /- For a short proof (avoiding measurable induction), one sees `s` as a closed set for a finer
    topology `t'`. It is analytic for this topology. As the identity from `t'` to `t` is continuous
    and the image of an analytic set is analytic, it follows that `s` is also analytic for `t`. -/
  obtain ‚ü®t', t't, t'_polish, s_closed, _‚ü© :
      ‚àÉ t' : TopologicalSpace Œ±, t' ‚â§ t ‚àß @PolishSpace Œ± t' ‚àß IsClosed[t'] s ‚àß IsOpen[t'] s :=
    hs.isClopenable
  have A := @IsClosed.analyticSet Œ± t' t'_polish s s_closed
  convert @AnalyticSet.image_of_continuous Œ± t' Œ± t s A id (continuous_id_of_le t't)
  simp only [id, image_id']

/-- Given a Borel-measurable function from a Polish space to a second-countable space, there exists
a finer Polish topology on the source space for which the function is continuous. -/
theorem _root_.Measurable.exists_continuous {Œ± Œ≤ : Type*} [t : TopologicalSpace Œ±] [PolishSpace Œ±]
    [MeasurableSpace Œ±] [BorelSpace Œ±] [tŒ≤ : TopologicalSpace Œ≤] [MeasurableSpace Œ≤]
    [OpensMeasurableSpace Œ≤] {f : Œ± ‚Üí Œ≤} [SecondCountableTopology (range f)] (hf : Measurable f) :
    ‚àÉ t' : TopologicalSpace Œ±, t' ‚â§ t ‚àß @Continuous Œ± Œ≤ t' tŒ≤ f ‚àß @PolishSpace Œ± t' := by
  obtain ‚ü®b, b_count, -, hb‚ü© :
      ‚àÉ b : Set (Set (range f)), b.Countable ‚àß ‚àÖ ‚àâ b ‚àß IsTopologicalBasis b :=
    exists_countable_basis (range f)
  haveI : Countable b := b_count.to_subtype
  have : ‚àÄ s : b, IsClopenable (rangeFactorization f ‚Åª¬π' s) := fun s ‚Ü¶ by
    apply MeasurableSet.isClopenable
    exact hf.subtype_mk (hb.isOpen s.2).measurableSet
  choose T Tt Tpolish _ Topen using this
  obtain ‚ü®t', t'T, t't, t'_polish‚ü© :
      ‚àÉ t' : TopologicalSpace Œ±, (‚àÄ i, t' ‚â§ T i) ‚àß t' ‚â§ t ‚àß @PolishSpace Œ± t' :=
    exists_polishSpace_forall_le (t := t) T Tt Tpolish
  refine ‚ü®t', t't, ?_, t'_polish‚ü©
  have : Continuous[t', _] (rangeFactorization f) :=
    hb.continuous_iff.2 fun s hs => t'T ‚ü®s, hs‚ü© _ (Topen ‚ü®s, hs‚ü©)
  exact continuous_subtype_val.comp this

/-- The image of a measurable set in a standard Borel space under a measurable map
is an analytic set. -/
theorem _root_.MeasurableSet.analyticSet_image {X Y : Type*} [MeasurableSpace X]
    [StandardBorelSpace X] [TopologicalSpace Y] [MeasurableSpace Y]
    [OpensMeasurableSpace Y] {f : X ‚Üí Y} [SecondCountableTopology (range f)] {s : Set X}
    (hs : MeasurableSet s) (hf : Measurable f) : AnalyticSet (f '' s) := by
  letI := upgradeStandardBorel X
  rw [eq_borel_upgradeStandardBorel X] at hs
  rcases hf.exists_continuous with ‚ü®œÑ', hle, hfc, hœÑ'‚ü©
  letI m' : MeasurableSpace X := @borel _ œÑ'
  haveI b' : BorelSpace X := ‚ü®rfl‚ü©
  have hle := borel_anti hle
  exact (hle _ hs).analyticSet.image_of_continuous hfc

/-- Preimage of an analytic set is an analytic set. -/
protected lemma AnalyticSet.preimage {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [PolishSpace X] [T2Space Y] {s : Set Y} (hs : AnalyticSet s) {f : X ‚Üí Y} (hf : Continuous f) :
    AnalyticSet (f ‚Åª¬π' s) := by
  rcases analyticSet_iff_exists_polishSpace_range.1 hs with ‚ü®Z, _, _, g, hg, rfl‚ü©
  have : IsClosed {x : X √ó Z | f x.1 = g x.2} := isClosed_eq hf.fst' hg.snd'
  convert this.analyticSet.image_of_continuous continuous_fst
  ext x
  simp [eq_comm]

/-! ### Separating sets with measurable sets -/

/-- Two sets `u` and `v` in a measurable space are measurably separable if there
exists a measurable set containing `u` and disjoint from `v`.
This is mostly interesting for Borel-separable sets. -/
def MeasurablySeparable {Œ± : Type*} [MeasurableSpace Œ±] (s t : Set Œ±) : Prop :=
  ‚àÉ u, s ‚äÜ u ‚àß Disjoint t u ‚àß MeasurableSet u

theorem MeasurablySeparable.iUnion [Countable Œπ] {Œ± : Type*} [MeasurableSpace Œ±] {s t : Œπ ‚Üí Set Œ±}
    (h : ‚àÄ m n, MeasurablySeparable (s m) (t n)) : MeasurablySeparable (‚ãÉ n, s n) (‚ãÉ m, t m) := by
  choose u hsu htu hu using h
  refine ‚ü®‚ãÉ m, ‚ãÇ n, u m n, ?_, ?_, ?_‚ü©
  ¬∑ refine iUnion_subset fun m => subset_iUnion_of_subset m ?_
    exact subset_iInter fun n => hsu m n
  ¬∑ simp_rw [disjoint_iUnion_left, disjoint_iUnion_right]
    intro n m
    apply Disjoint.mono_right _ (htu m n)
    apply iInter_subset
  ¬∑ refine MeasurableSet.iUnion fun m => ?_
    exact MeasurableSet.iInter fun n => hu m n

/-- The hard part of the Lusin separation theorem saying that two disjoint analytic sets are
contained in disjoint Borel sets (see the full statement in `AnalyticSet.measurablySeparable`).
Here, we prove this when our analytic sets are the ranges of functions from `‚Ñï ‚Üí ‚Ñï`.
-/
theorem measurablySeparable_range_of_disjoint [T2Space Œ±] [MeasurableSpace Œ±]
    [OpensMeasurableSpace Œ±] {f g : (‚Ñï ‚Üí ‚Ñï) ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g)
    (h : Disjoint (range f) (range g)) : MeasurablySeparable (range f) (range g) := by
  /- We follow [Kechris, *Classical Descriptive Set Theory* (Theorem 14.7)][kechris1995].
    If the ranges are not Borel-separated, then one can find two cylinders of length one whose
    images are not Borel-separated, and then two smaller cylinders of length two whose images are
    not Borel-separated, and so on. One thus gets two sequences of cylinders, that decrease to two
    points `x` and `y`. Their images are different by the disjointness assumption, hence contained
    in two disjoint open sets by the T2 property. By continuity, long enough cylinders around `x`
    and `y` have images which are separated by these two disjoint open sets, a contradiction.
    -/
  by_contra hfg
  have I : ‚àÄ n x y, ¬¨MeasurablySeparable (f '' cylinder x n) (g '' cylinder y n) ‚Üí
      ‚àÉ x' y', x' ‚àà cylinder x n ‚àß y' ‚àà cylinder y n ‚àß
      ¬¨MeasurablySeparable (f '' cylinder x' (n + 1)) (g '' cylinder y' (n + 1)) := by
    intro n x y
    contrapose!
    intro H
    rw [‚Üê iUnion_cylinder_update x n, ‚Üê iUnion_cylinder_update y n, image_iUnion, image_iUnion]
    refine MeasurablySeparable.iUnion fun i j => ?_
    exact H _ _ (update_mem_cylinder _ _ _) (update_mem_cylinder _ _ _)
  -- consider the set of pairs of cylinders of some length whose images are not Borel-separated
  let A :=
    { p : ‚Ñï √ó (‚Ñï ‚Üí ‚Ñï) √ó (‚Ñï ‚Üí ‚Ñï) //
      ¬¨MeasurablySeparable (f '' cylinder p.2.1 p.1) (g '' cylinder p.2.2 p.1) }
  -- for each such pair, one can find longer cylinders whose images are not Borel-separated either
  have : ‚àÄ p : A, ‚àÉ q : A,
      q.1.1 = p.1.1 + 1 ‚àß q.1.2.1 ‚àà cylinder p.1.2.1 p.1.1 ‚àß q.1.2.2 ‚àà cylinder p.1.2.2 p.1.1 := by
    rintro ‚ü®‚ü®n, x, y‚ü©, hp‚ü©
    rcases I n x y hp with ‚ü®x', y', hx', hy', h'‚ü©
    exact ‚ü®‚ü®‚ü®n + 1, x', y'‚ü©, h'‚ü©, rfl, hx', hy'‚ü©
  choose F hFn hFx hFy using this
  let p0 : A := ‚ü®‚ü®0, fun _ => 0, fun _ => 0‚ü©, by simp [hfg]‚ü©
  -- construct inductively decreasing sequences of cylinders whose images are not separated
  let p : ‚Ñï ‚Üí A := fun n => F^[n] p0
  have prec : ‚àÄ n, p (n + 1) = F (p n) := fun n => by simp only [p, iterate_succ', Function.comp]
  -- check that at the `n`-th step we deal with cylinders of length `n`
  have pn_fst : ‚àÄ n, (p n).1.1 = n := fun n ‚Ü¶ by
    induction n with
    | zero => rfl
    | succ n IH => simp only [prec, hFn, IH]
  -- check that the cylinders we construct are indeed decreasing, by checking that the coordinates
  -- are stationary.
  have Ix : ‚àÄ m n, m + 1 ‚â§ n ‚Üí (p n).1.2.1 m = (p (m + 1)).1.2.1 m := by
    intro m
    apply Nat.le_induction
    ¬∑ rfl
    intro n hmn IH
    have I : (F (p n)).val.snd.fst m = (p n).val.snd.fst m := by
      apply hFx (p n) m
      rw [pn_fst]
      exact hmn
    rw [prec, I, IH]
  have Iy : ‚àÄ m n, m + 1 ‚â§ n ‚Üí (p n).1.2.2 m = (p (m + 1)).1.2.2 m := by
    intro m
    apply Nat.le_induction
    ¬∑ rfl
    intro n hmn IH
    have I : (F (p n)).val.snd.snd m = (p n).val.snd.snd m := by
      apply hFy (p n) m
      rw [pn_fst]
      exact hmn
    rw [prec, I, IH]
  -- denote by `x` and `y` the limit points of these two sequences of cylinders.
  set x : ‚Ñï ‚Üí ‚Ñï := fun n => (p (n + 1)).1.2.1 n with hx
  set y : ‚Ñï ‚Üí ‚Ñï := fun n => (p (n + 1)).1.2.2 n with hy
  -- by design, the cylinders around these points have images which are not Borel-separable.
  have M : ‚àÄ n, ¬¨MeasurablySeparable (f '' cylinder x n) (g '' cylinder y n) := by
    intro n
    convert (p n).2 using 3
    ¬∑ rw [pn_fst, ‚Üê mem_cylinder_iff_eq, mem_cylinder_iff]
      intro i hi
      rw [hx]
      exact (Ix i n hi).symm
    ¬∑ rw [pn_fst, ‚Üê mem_cylinder_iff_eq, mem_cylinder_iff]
      intro i hi
      rw [hy]
      exact (Iy i n hi).symm
  -- consider two open sets separating `f x` and `g y`.
  obtain ‚ü®u, v, u_open, v_open, xu, yv, huv‚ü© :
      ‚àÉ u v : Set Œ±, IsOpen u ‚àß IsOpen v ‚àß f x ‚àà u ‚àß g y ‚àà v ‚àß Disjoint u v := by
    apply t2_separation
    exact disjoint_iff_forall_ne.1 h (mem_range_self _) (mem_range_self _)
  letI : MetricSpace (‚Ñï ‚Üí ‚Ñï) := metricSpaceNatNat
  obtain ‚ü®Œµx, Œµxpos, hŒµx‚ü© : ‚àÉ (Œµx : ‚Ñù), Œµx > 0 ‚àß Metric.ball x Œµx ‚äÜ f ‚Åª¬π' u := by
    apply Metric.mem_nhds_iff.1
    exact hf.continuousAt.preimage_mem_nhds (u_open.mem_nhds xu)
  obtain ‚ü®Œµy, Œµypos, hŒµy‚ü© : ‚àÉ (Œµy : ‚Ñù), Œµy > 0 ‚àß Metric.ball y Œµy ‚äÜ g ‚Åª¬π' v := by
    apply Metric.mem_nhds_iff.1
    exact hg.continuousAt.preimage_mem_nhds (v_open.mem_nhds yv)
  obtain ‚ü®n, hn‚ü© : ‚àÉ n : ‚Ñï, (1 / 2 : ‚Ñù) ^ n < min Œµx Œµy :=
    exists_pow_lt_of_lt_one (lt_min Œµxpos Œµypos) (by norm_num)
  -- for large enough `n`, these open sets separate the images of long cylinders around `x` and `y`
  have B : MeasurablySeparable (f '' cylinder x n) (g '' cylinder y n) := by
    refine ‚ü®u, ?_, ?_, u_open.measurableSet‚ü©
    ¬∑ rw [image_subset_iff]
      apply Subset.trans _ hŒµx
      intro z hz
      rw [mem_cylinder_iff_dist_le] at hz
      exact hz.trans_lt (hn.trans_le (min_le_left _ _))
    ¬∑ refine Disjoint.mono_left ?_ huv.symm
      change g '' cylinder y n ‚äÜ v
      rw [image_subset_iff]
      apply Subset.trans _ hŒµy
      intro z hz
      rw [mem_cylinder_iff_dist_le] at hz
      exact hz.trans_lt (hn.trans_le (min_le_right _ _))
  -- this is a contradiction.
  exact M n B

/-- The **Lusin separation theorem**: if two analytic sets are disjoint, then they are contained in
disjoint Borel sets. -/
theorem AnalyticSet.measurablySeparable [T2Space Œ±] [MeasurableSpace Œ±] [OpensMeasurableSpace Œ±]
    {s t : Set Œ±} (hs : AnalyticSet s) (ht : AnalyticSet t) (h : Disjoint s t) :
    MeasurablySeparable s t := by
  rw [AnalyticSet] at hs ht
  rcases hs with (rfl | ‚ü®f, f_cont, rfl‚ü©)
  ¬∑ refine ‚ü®‚àÖ, Subset.refl _, by simp, MeasurableSet.empty‚ü©
  rcases ht with (rfl | ‚ü®g, g_cont, rfl‚ü©)
  ¬∑ exact ‚ü®univ, subset_univ _, by simp, MeasurableSet.univ‚ü©
  exact measurablySeparable_range_of_disjoint f_cont g_cont h

/-- **Suslin's Theorem**: in a Hausdorff topological space, an analytic set with an analytic
complement is measurable. -/
theorem AnalyticSet.measurableSet_of_compl [T2Space Œ±] [MeasurableSpace Œ±] [OpensMeasurableSpace Œ±]
    {s : Set Œ±} (hs : AnalyticSet s) (hsc : AnalyticSet s·∂ú) : MeasurableSet s := by
  rcases hs.measurablySeparable hsc disjoint_compl_right with ‚ü®u, hsu, hdu, hmu‚ü©
  obtain rfl : s = u := hsu.antisymm (disjoint_compl_left_iff_subset.1 hdu)
  exact hmu

end MeasureTheory

/-!
### Measurability of preimages under measurable maps
-/

namespace Measurable

open MeasurableSpace

variable {X Y Z Œ≤ : Type*} [MeasurableSpace X] [StandardBorelSpace X]
  [TopologicalSpace Y] [T0Space Y] [MeasurableSpace Y] [OpensMeasurableSpace Y] [MeasurableSpace Œ≤]
  [MeasurableSpace Z]

/-- If `f : X ‚Üí Z` is a surjective Borel measurable map from a standard Borel space
to a countably separated measurable space, then the preimage of a set `s`
is measurable if and only if the set is measurable.
One implication is the definition of measurability, the other one heavily relies on `X` being a
standard Borel space. -/
theorem measurableSet_preimage_iff_of_surjective [CountablySeparated Z]
    {f : X ‚Üí Z} (hf : Measurable f) (hsurj : Surjective f) {s : Set Z} :
    MeasurableSet (f ‚Åª¬π' s) ‚Üî MeasurableSet s := by
  refine ‚ü®fun h => ?_, fun h => hf h‚ü©
  rcases exists_opensMeasurableSpace_of_countablySeparated Z with ‚ü®œÑ, _, _, _‚ü©
  apply AnalyticSet.measurableSet_of_compl
  ¬∑ rw [‚Üê image_preimage_eq s hsurj]
    exact h.analyticSet_image hf
  ¬∑ rw [‚Üê image_preimage_eq s·∂ú hsurj]
    exact h.compl.analyticSet_image hf

theorem map_measurableSpace_eq [CountablySeparated Z]
    {f : X ‚Üí Z} (hf : Measurable f)
    (hsurj : Surjective f) : MeasurableSpace.map f ‚ÄπMeasurableSpace X‚Ä∫ = ‚ÄπMeasurableSpace Z‚Ä∫ :=
  MeasurableSpace.ext fun _ => hf.measurableSet_preimage_iff_of_surjective hsurj

theorem map_measurableSpace_eq_borel [SecondCountableTopology Y] {f : X ‚Üí Y} (hf : Measurable f)
    (hsurj : Surjective f) : MeasurableSpace.map f ‚ÄπMeasurableSpace X‚Ä∫ = borel Y := by
  have d := hf.mono le_rfl OpensMeasurableSpace.borel_le
  letI := borel Y; haveI : BorelSpace Y := ‚ü®rfl‚ü©
  exact d.map_measurableSpace_eq hsurj

theorem borelSpace_codomain [SecondCountableTopology Y] {f : X ‚Üí Y} (hf : Measurable f)
    (hsurj : Surjective f) : BorelSpace Y :=
  ‚ü®(hf.map_measurableSpace_eq hsurj).symm.trans <| hf.map_measurableSpace_eq_borel hsurj‚ü©

/-- If `f : X ‚Üí Z` is a Borel measurable map from a standard Borel space to a
countably separated measurable space then the preimage of a set `s` is measurable
if and only if the set is measurable in `Set.range f`. -/
theorem measurableSet_preimage_iff_preimage_val {f : X ‚Üí Z} [CountablySeparated (range f)]
    (hf : Measurable f) {s : Set Z} :
    MeasurableSet (f ‚Åª¬π' s) ‚Üî MeasurableSet ((‚Üë) ‚Åª¬π' s : Set (range f)) :=
  have hf' : Measurable (rangeFactorization f) := hf.subtype_mk
  hf'.measurableSet_preimage_iff_of_surjective (s := Subtype.val ‚Åª¬π' s)
    rangeFactorization_surjective

/-- If `f : X ‚Üí Z` is a Borel measurable map from a standard Borel space to a
countably separated measurable space and the range of `f` is measurable,
then the preimage of a set `s` is measurable
if and only if the intersection with `Set.range f` is measurable. -/
theorem measurableSet_preimage_iff_inter_range {f : X ‚Üí Z} [CountablySeparated (range f)]
    (hf : Measurable f) (hr : MeasurableSet (range f)) {s : Set Z} :
    MeasurableSet (f ‚Åª¬π' s) ‚Üî MeasurableSet (s ‚à© range f) := by
  rw [hf.measurableSet_preimage_iff_preimage_val, inter_comm,
    ‚Üê (MeasurableEmbedding.subtype_coe hr).measurableSet_image, Subtype.image_preimage_coe]

/-- If `f : X ‚Üí Z` is a Borel measurable map from a standard Borel space
to a countably separated measurable space,
then for any measurable space `Œ≤` and `g : Z ‚Üí Œ≤`, the composition `g ‚àò f` is
measurable if and only if the restriction of `g` to the range of `f` is measurable. -/
theorem measurable_comp_iff_restrict {f : X ‚Üí Z}
    [CountablySeparated (range f)]
    (hf : Measurable f) {g : Z ‚Üí Œ≤} : Measurable (g ‚àò f) ‚Üî Measurable (restrict (range f) g) :=
  forall‚ÇÇ_congr fun s _ => measurableSet_preimage_iff_preimage_val hf (s := g ‚Åª¬π' s)

/-- If `f : X ‚Üí Z` is a surjective Borel measurable map from a standard Borel space
to a countably separated measurable space,
then for any measurable space `Œ±` and `g : Z ‚Üí Œ±`, the composition
`g ‚àò f` is measurable if and only if `g` is measurable. -/
theorem measurable_comp_iff_of_surjective [CountablySeparated Z]
    {f : X ‚Üí Z} (hf : Measurable f) (hsurj : Surjective f)
    {g : Z ‚Üí Œ≤} : Measurable (g ‚àò f) ‚Üî Measurable g :=
  forall‚ÇÇ_congr fun s _ => measurableSet_preimage_iff_of_surjective hf hsurj (s := g ‚Åª¬π' s)

end Measurable

theorem Continuous.map_eq_borel {X Y : Type*} [TopologicalSpace X] [PolishSpace X]
    [MeasurableSpace X] [BorelSpace X] [TopologicalSpace Y] [T0Space Y] [SecondCountableTopology Y]
    {f : X ‚Üí Y} (hf : Continuous f) (hsurj : Surjective f) :
    MeasurableSpace.map f ‚ÄπMeasurableSpace X‚Ä∫ = borel Y := by
  borelize Y
  exact hf.measurable.map_measurableSpace_eq hsurj

theorem Continuous.map_borel_eq {X Y : Type*} [TopologicalSpace X] [PolishSpace X]
    [TopologicalSpace Y] [T0Space Y] [SecondCountableTopology Y] {f : X ‚Üí Y} (hf : Continuous f)
    (hsurj : Surjective f) : MeasurableSpace.map f (borel X) = borel Y := by
  borelize X
  exact hf.map_eq_borel hsurj

instance Quotient.borelSpace {X : Type*} [TopologicalSpace X] [PolishSpace X] [MeasurableSpace X]
    [BorelSpace X] {s : Setoid X} [T0Space (Quotient s)] [SecondCountableTopology (Quotient s)] :
    BorelSpace (Quotient s) :=
  ‚ü®continuous_quotient_mk'.map_eq_borel Quotient.mk'_surjective‚ü©

/-- When the subgroup `N < G` is not necessarily `Normal`, we have a `CosetSpace` as opposed
to `QuotientGroup` (the next `instance`).
TODO: typeclass inference should normally find this, but currently doesn't.
E.g., `MeasurableSMul G (G ‚ß∏ Œì)` fails to synthesize, even though `G ‚ß∏ Œì` is the quotient
of `G` by the action of `Œì`; it seems unable to pick up the `BorelSpace` instance. -/
@[to_additive AddCosetSpace.borelSpace]
instance CosetSpace.borelSpace {G : Type*} [TopologicalSpace G] [PolishSpace G] [Group G]
    [MeasurableSpace G] [BorelSpace G] {N : Subgroup G} [T2Space (G ‚ß∏ N)]
    [SecondCountableTopology (G ‚ß∏ N)] : BorelSpace (G ‚ß∏ N) := Quotient.borelSpace

@[to_additive]
instance QuotientGroup.borelSpace {G : Type*} [TopologicalSpace G] [PolishSpace G] [Group G]
    [IsTopologicalGroup G] [MeasurableSpace G] [BorelSpace G] {N : Subgroup G} [N.Normal]
    [IsClosed (N : Set G)] : BorelSpace (G ‚ß∏ N) :=
  ‚ü®continuous_mk.map_eq_borel mk_surjective‚ü©

/-! ### Injective images of Borel sets -/

variable {Œ≥ : Type*}

/-- The **Lusin-Souslin theorem**: the range of a continuous injective function defined on a Polish
space is Borel-measurable. -/
theorem MeasureTheory.measurableSet_range_of_continuous_injective {Œ≤ : Type*} [TopologicalSpace Œ≥]
    [PolishSpace Œ≥] [TopologicalSpace Œ≤] [T2Space Œ≤] [MeasurableSpace Œ≤] [OpensMeasurableSpace Œ≤]
    {f : Œ≥ ‚Üí Œ≤} (f_cont : Continuous f) (f_inj : Injective f) :
    MeasurableSet (range f) := by
  /- We follow [Fremlin, *Measure Theory* (volume 4, 423I)][fremlin_vol4].
    Let `b = {s i}` be a countable basis for `Œ±`. When `s i` and `s j` are disjoint, their images
    are disjoint analytic sets, hence by the separation theorem one can find a Borel-measurable set
    `q i j` separating them.
    Let `E i = closure (f '' s i) ‚à© ‚ãÇ j, q i j \ q j i`. It contains `f '' (s i)` and it is
    measurable. Let `F n = ‚ãÉ E i`, where the union is taken over those `i` for which `diam (s i)`
    is bounded by some number `u n` tending to `0` with `n`.
    We claim that `range f = ‚ãÇ F n`, from which the measurability is obvious. The inclusion `‚äÜ` is
    straightforward. To show `‚äá`, consider a point `x` in the intersection. For each `n`, it belongs
    to some `E i` with `diam (s i) ‚â§ u n`. Pick a point `y i ‚àà s i`. We claim that for such `i`
    and `j`, the intersection `s i ‚à© s j` is nonempty: if it were empty, then thanks to the
    separating set `q i j` in the definition of `E i` one could not have `x ‚àà E i ‚à© E j`.
    Since these two sets have small diameter, it follows that `y i` and `y j` are close.
    Thus, `y` is a Cauchy sequence, converging to a limit `z`. We claim that `f z = x`, completing
    the proof.
    Otherwise, one could find open sets `v` and `w` separating `f z` from `x`. Then, for large `n`,
    the image `f '' (s i)` would be included in `v` by continuity of `f`, so its closure would be
    contained in the closure of `v`, and therefore it would be disjoint from `w`. This is a
    contradiction since `x` belongs both to this closure and to `w`. -/
  letI := TopologicalSpace.upgradeIsCompletelyMetrizable Œ≥
  obtain ‚ü®b, b_count, b_nonempty, hb‚ü© :
    ‚àÉ b : Set (Set Œ≥), b.Countable ‚àß ‚àÖ ‚àâ b ‚àß IsTopologicalBasis b := exists_countable_basis Œ≥
  haveI : Encodable b := b_count.toEncodable
  let A := { p : b √ó b // Disjoint (p.1 : Set Œ≥) p.2 }
  -- for each pair of disjoint sets in the topological basis `b`, consider Borel sets separating
  -- their images, by injectivity of `f` and the Lusin separation theorem.
  have : ‚àÄ p : A, ‚àÉ q : Set Œ≤,
      f '' (p.1.1 : Set Œ≥) ‚äÜ q ‚àß Disjoint (f '' (p.1.2 : Set Œ≥)) q ‚àß MeasurableSet q := by
    intro p
    apply
      AnalyticSet.measurablySeparable ((hb.isOpen p.1.1.2).analyticSet_image f_cont)
        ((hb.isOpen p.1.2.2).analyticSet_image f_cont)
    exact Disjoint.image p.2 f_inj.injOn (subset_univ _) (subset_univ _)
  choose q hq1 hq2 q_meas using this
  -- define sets `E i` and `F n` as in the proof sketch above
  let E : b ‚Üí Set Œ≤ := fun s =>
    closure (f '' s) ‚à© ‚ãÇ (t : b) (ht : Disjoint s.1 t.1), q ‚ü®(s, t), ht‚ü© \ q ‚ü®(t, s), ht.symm‚ü©
  obtain ‚ü®u, u_anti, u_pos, u_lim‚ü© :
      ‚àÉ u : ‚Ñï ‚Üí ‚Ñù, StrictAnti u ‚àß (‚àÄ n : ‚Ñï, 0 < u n) ‚àß Tendsto u atTop (ùìù 0) :=
    exists_seq_strictAnti_tendsto (0 : ‚Ñù)
  let F : ‚Ñï ‚Üí Set Œ≤ := fun n => ‚ãÉ (s : b) (_ : IsBounded s.1 ‚àß diam s.1 ‚â§ u n), E s
  -- it is enough to show that `range f = ‚ãÇ F n`, as the latter set is obviously measurable.
  suffices range f = ‚ãÇ n, F n by
    have E_meas : ‚àÄ s : b, MeasurableSet (E s) := by
      intro b
      refine isClosed_closure.measurableSet.inter ?_
      refine MeasurableSet.iInter fun s => ?_
      exact MeasurableSet.iInter fun hs => (q_meas _).diff (q_meas _)
    have F_meas : ‚àÄ n, MeasurableSet (F n) := by
      intro n
      refine MeasurableSet.iUnion fun s => ?_
      exact MeasurableSet.iUnion fun _ => E_meas _
    rw [this]
    exact MeasurableSet.iInter fun n => F_meas n
  -- we check both inclusions.
  apply Subset.antisymm
  -- we start with the easy inclusion `range f ‚äÜ ‚ãÇ F n`. One just needs to unfold the definitions.
  ¬∑ rintro x ‚ü®y, rfl‚ü©
    refine mem_iInter.2 fun n => ?_
    obtain ‚ü®s, sb, ys, hs‚ü© : ‚àÉ (s : Set Œ≥), s ‚àà b ‚àß y ‚àà s ‚àß s ‚äÜ ball y (u n / 2) := by
      apply hb.mem_nhds_iff.1
      exact ball_mem_nhds _ (half_pos (u_pos n))
    have diam_s : diam s ‚â§ u n := by
      apply (diam_mono hs isBounded_ball).trans
      convert diam_ball (x := y) (half_pos (u_pos n)).le
      ring
    refine mem_iUnion.2 ‚ü®‚ü®s, sb‚ü©, ?_‚ü©
    refine mem_iUnion.2 ‚ü®‚ü®isBounded_ball.subset hs, diam_s‚ü©, ?_‚ü©
    apply mem_inter (subset_closure (mem_image_of_mem _ ys))
    refine mem_iInter.2 fun t => mem_iInter.2 fun ht => ‚ü®?_, ?_‚ü©
    ¬∑ apply hq1
      exact mem_image_of_mem _ ys
    ¬∑ apply disjoint_left.1 (hq2 ‚ü®(t, ‚ü®s, sb‚ü©), ht.symm‚ü©)
      exact mem_image_of_mem _ ys
  -- Now, let us prove the harder inclusion `‚ãÇ F n ‚äÜ range f`.
  ¬∑ intro x hx
    -- pick for each `n` a good set `s n` of small diameter for which `x ‚àà E (s n)`.
    have C1 : ‚àÄ n, ‚àÉ (s : b) (_ : IsBounded s.1 ‚àß diam s.1 ‚â§ u n), x ‚àà E s := fun n => by
      simpa only [F, mem_iUnion] using mem_iInter.1 hx n
    choose s hs hxs using C1
    have C2 : ‚àÄ n, (s n).1.Nonempty := by
      intro n
      rw [nonempty_iff_ne_empty]
      grind
    -- choose a point `y n ‚àà s n`.
    choose y hy using C2
    have I : ‚àÄ m n, ((s m).1 ‚à© (s n).1).Nonempty := by
      intro m n
      rw [‚Üê not_disjoint_iff_nonempty_inter]
      by_contra! h
      have A : x ‚àà q ‚ü®(s m, s n), h‚ü© \ q ‚ü®(s n, s m), h.symm‚ü© :=
        haveI := mem_iInter.1 (hxs m).2 (s n)
        (mem_iInter.1 this h :)
      have B : x ‚àà q ‚ü®(s n, s m), h.symm‚ü© \ q ‚ü®(s m, s n), h‚ü© :=
        haveI := mem_iInter.1 (hxs n).2 (s m)
        (mem_iInter.1 this h.symm :)
      exact A.2 B.1
    -- the points `y n` are nearby, and therefore they form a Cauchy sequence.
    have cauchy_y : CauchySeq y := by
      have : Tendsto (fun n => 2 * u n) atTop (ùìù 0) := by
        simpa only [mul_zero] using u_lim.const_mul 2
      refine cauchySeq_of_le_tendsto_0' (fun n => 2 * u n) (fun m n hmn => ?_) this
      rcases I m n with ‚ü®z, zsm, zsn‚ü©
      calc
        dist (y m) (y n) ‚â§ dist (y m) z + dist z (y n) := dist_triangle _ _ _
        _ ‚â§ u m + u n :=
          (add_le_add ((dist_le_diam_of_mem (hs m).1 (hy m) zsm).trans (hs m).2)
            ((dist_le_diam_of_mem (hs n).1 zsn (hy n)).trans (hs n).2))
        _ ‚â§ 2 * u m := by linarith [u_anti.antitone hmn]
    haveI : Nonempty Œ≥ := ‚ü®y 0‚ü©
    -- let `z` be its limit.
    let z := limUnder atTop y
    have y_lim : Tendsto y atTop (ùìù z) := cauchy_y.tendsto_limUnder
    suffices f z = x by
      rw [‚Üê this]
      exact mem_range_self _
    -- assume for a contradiction that `f z ‚â† x`.
    by_contra! hne
    -- introduce disjoint open sets `v` and `w` separating `f z` from `x`.
    obtain ‚ü®v, w, v_open, w_open, fzv, xw, hvw‚ü© := t2_separation hne
    obtain ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü© : ‚àÉ Œ¥ > (0 : ‚Ñù), ball z Œ¥ ‚äÜ f ‚Åª¬π' v := by
      apply Metric.mem_nhds_iff.1
      exact f_cont.continuousAt.preimage_mem_nhds (v_open.mem_nhds fzv)
    obtain ‚ü®n, hn‚ü© : ‚àÉ n, u n + dist (y n) z < Œ¥ :=
      haveI : Tendsto (fun n => u n + dist (y n) z) atTop (ùìù 0) := by
        simpa only [add_zero] using u_lim.add (tendsto_iff_dist_tendsto_zero.1 y_lim)
      ((tendsto_order.1 this).2 _ Œ¥pos).exists
    -- for large enough `n`, the image of `s n` is contained in `v`, by continuity of `f`.
    have fsnv : f '' s n ‚äÜ v := by
      rw [image_subset_iff]
      apply Subset.trans _ hŒ¥
      intro a ha
      calc
        dist a z ‚â§ dist a (y n) + dist (y n) z := dist_triangle _ _ _
        _ ‚â§ u n + dist (y n) z := by grw [dist_le_diam_of_mem (hs n).1 ha (hy n), (hs n).2]
        _ < Œ¥ := hn
    -- as `x` belongs to the closure of `f '' (s n)`, it belongs to the closure of `v`.
    have : x ‚àà closure v := closure_mono fsnv (hxs n).1
    -- this is a contradiction, as `x` is supposed to belong to `w`, which is disjoint from
    -- the closure of `v`.
    exact disjoint_left.1 (hvw.closure_left w_open) this xw

theorem IsClosed.measurableSet_image_of_continuousOn_injOn
    [TopologicalSpace Œ≥] [PolishSpace Œ≥] {Œ≤ : Type*} [TopologicalSpace Œ≤] [T2Space Œ≤]
    [MeasurableSpace Œ≤] [OpensMeasurableSpace Œ≤] {s : Set Œ≥} (hs : IsClosed s) {f : Œ≥ ‚Üí Œ≤}
    (f_cont : ContinuousOn f s) (f_inj : InjOn f s) : MeasurableSet (f '' s) := by
  rw [image_eq_range]
  haveI : PolishSpace s := IsClosed.polishSpace hs
  apply measurableSet_range_of_continuous_injective
  ¬∑ rwa [continuousOn_iff_continuous_restrict] at f_cont
  ¬∑ rwa [injOn_iff_injective] at f_inj

variable {Œ± Œ≤ : Type*} [MeasurableSpace Œ≤]
section
variable [tŒ≤ : TopologicalSpace Œ≤] [T2Space Œ≤] [MeasurableSpace Œ±] {s : Set Œ≥} {f : Œ≥ ‚Üí Œ≤}

/-- The Lusin-Souslin theorem: if `s` is Borel-measurable in a Polish space, then its image under
a continuous injective map is also Borel-measurable. -/
theorem MeasurableSet.image_of_continuousOn_injOn [OpensMeasurableSpace Œ≤]
    [tŒ≥ : TopologicalSpace Œ≥] [PolishSpace Œ≥] [MeasurableSpace Œ≥] [BorelSpace Œ≥]
    (hs : MeasurableSet s)
    (f_cont : ContinuousOn f s) (f_inj : InjOn f s) : MeasurableSet (f '' s) := by
  obtain ‚ü®t', t't, t'_polish, s_closed, _‚ü© :
      ‚àÉ t' : TopologicalSpace Œ≥, t' ‚â§ tŒ≥ ‚àß @PolishSpace Œ≥ t' ‚àß IsClosed[t'] s ‚àß IsOpen[t'] s :=
    hs.isClopenable
  exact
    @IsClosed.measurableSet_image_of_continuousOn_injOn Œ≥ t' t'_polish Œ≤ _ _ _ _ s s_closed f
      (f_cont.mono_dom t't) f_inj

/-- The Lusin-Souslin theorem: if `s` is Borel-measurable in a standard Borel space,
then its image under a measurable injective map taking values in a
countably separate measurable space is also Borel-measurable. -/
theorem MeasurableSet.image_of_measurable_injOn {f : Œ≥ ‚Üí Œ±}
    [MeasurableSpace.CountablySeparated Œ±]
    [MeasurableSpace Œ≥] [StandardBorelSpace Œ≥]
    (hs : MeasurableSet s) (f_meas : Measurable f) (f_inj : InjOn f s) :
    MeasurableSet (f '' s) := by
  letI := upgradeStandardBorel Œ≥
  let tŒ≥ : TopologicalSpace Œ≥ := inferInstance
  rcases exists_opensMeasurableSpace_of_countablySeparated Œ± with ‚ü®œÑ, _, _, _‚ü©
  -- for a finer Polish topology, `f` is continuous. Therefore, one may apply the corresponding
  -- result for continuous maps.
  obtain ‚ü®t', t't, f_cont, t'_polish‚ü© :
      ‚àÉ t' : TopologicalSpace Œ≥, t' ‚â§ tŒ≥ ‚àß @Continuous Œ≥ _ t' _ f ‚àß @PolishSpace Œ≥ t' :=
    f_meas.exists_continuous
  have M : MeasurableSet[@borel Œ≥ t'] s :=
    @Continuous.measurable Œ≥ Œ≥ t' (@borel Œ≥ t')
      (@BorelSpace.opensMeasurable Œ≥ t' (@borel Œ≥ t') (@BorelSpace.mk _ _ (borel Œ≥) rfl))
      tŒ≥ _ _ _ (continuous_id_of_le t't) s hs
  exact
    @MeasurableSet.image_of_continuousOn_injOn Œ≥
      _ _ _ _  s f _ t' t'_polish (@borel Œ≥ t') (@BorelSpace.mk _ _ (borel Œ≥) rfl)
      M (@Continuous.continuousOn Œ≥ _ t' _ f s f_cont) f_inj

/-- An injective continuous function on a Polish space is a measurable embedding. -/
theorem Continuous.measurableEmbedding [BorelSpace Œ≤]
    [TopologicalSpace Œ≥] [PolishSpace Œ≥] [MeasurableSpace Œ≥] [BorelSpace Œ≥]
    (f_cont : Continuous f) (f_inj : Injective f) :
    MeasurableEmbedding f :=
  { injective := f_inj
    measurable := f_cont.measurable
    measurableSet_image' := fun _u hu =>
      hu.image_of_continuousOn_injOn f_cont.continuousOn f_inj.injOn }

/-- If `s` is Borel-measurable in a Polish space and `f` is continuous injective on `s`, then
the restriction of `f` to `s` is a measurable embedding. -/
theorem ContinuousOn.measurableEmbedding [BorelSpace Œ≤]
    [TopologicalSpace Œ≥] [PolishSpace Œ≥] [MeasurableSpace Œ≥] [BorelSpace Œ≥]
    (hs : MeasurableSet s) (f_cont : ContinuousOn f s)
    (f_inj : InjOn f s) : MeasurableEmbedding (s.restrict f) :=
  { injective := injOn_iff_injective.1 f_inj
    measurable := (continuousOn_iff_continuous_restrict.1 f_cont).measurable
    measurableSet_image' := by
      intro u hu
      have A : MeasurableSet (((‚Üë) : s ‚Üí Œ≥) '' u) :=
        (MeasurableEmbedding.subtype_coe hs).measurableSet_image.2 hu
      have B : MeasurableSet (f '' (((‚Üë) : s ‚Üí Œ≥) '' u)) :=
        A.image_of_continuousOn_injOn (f_cont.mono (Subtype.coe_image_subset s u))
          (f_inj.mono (Subtype.coe_image_subset s u))
      rwa [‚Üê image_comp] at B }

/-- An injective measurable function from a standard Borel space to a
countably separated measurable space is a measurable embedding. -/
theorem Measurable.measurableEmbedding {f : Œ≥ ‚Üí Œ±}
    [MeasurableSpace.CountablySeparated Œ±]
    [MeasurableSpace Œ≥] [StandardBorelSpace Œ≥]
    (f_meas : Measurable f) (f_inj : Injective f) : MeasurableEmbedding f :=
  { injective := f_inj
    measurable := f_meas
    measurableSet_image' := fun _u hu => hu.image_of_measurable_injOn f_meas f_inj.injOn }

/-- If one Polish topology on a type refines another, they have the same Borel sets. -/
theorem MeasureTheory.borel_eq_borel_of_le {t t' : TopologicalSpace Œ≥}
    (ht : PolishSpace (h := t)) (ht' : PolishSpace (h := t')) (hle : t ‚â§ t') :
    @borel _ t = @borel _ t' := by
  refine le_antisymm ?_ (borel_anti hle)
  intro s hs
  have e := @Continuous.measurableEmbedding
    _ _ (@borel _ t') t' _ _ (@BorelSpace.mk _ _ (borel Œ≥) rfl)
    t _ (@borel _ t) (@BorelSpace.mk _ t (@borel _ t) rfl) (continuous_id_of_le hle) injective_id
  convert e.measurableSet_image.2 hs
  simp only [id_eq, image_id']

/-- In a Polish space, a set is clopenable if and only if it is Borel-measurable. -/
theorem MeasureTheory.isClopenable_iff_measurableSet
    [tŒ≥ : TopologicalSpace Œ≥] [PolishSpace Œ≥] [MeasurableSpace Œ≥] [BorelSpace Œ≥] :
    IsClopenable s ‚Üî MeasurableSet s := by
  -- we already know that a measurable set is clopenable. Conversely, assume that `s` is clopenable.
  refine ‚ü®fun hs => ?_, fun hs => hs.isClopenable‚ü©
  borelize Œ≥
  -- consider a finer topology `t'` in which `s` is open and closed.
  obtain ‚ü®t', t't, t'_polish, _, s_open‚ü© :
    ‚àÉ t' : TopologicalSpace Œ≥, t' ‚â§ tŒ≥ ‚àß @PolishSpace Œ≥ t' ‚àß IsClosed[t'] s ‚àß IsOpen[t'] s := hs
  rw [‚Üê borel_eq_borel_of_le t'_polish _ t't]
  ¬∑ exact MeasurableSpace.measurableSet_generateFrom s_open
  infer_instance

end

section LinearOrder

variable {Œ± Œ≤ : Type*} {t : Set Œ±} {g : Œ± ‚Üí Œ≤}
  [TopologicalSpace Œ±] [MeasurableSpace Œ±] [BorelSpace Œ±] [LinearOrder Œ±] [OrderTopology Œ±]
  [PolishSpace Œ±]
  [TopologicalSpace Œ≤] [MeasurableSpace Œ≤] [BorelSpace Œ≤] [LinearOrder Œ≤] [OrderTopology Œ≤]

theorem MeasurableSet.image_of_monotoneOn_of_continuousOn
    (ht : MeasurableSet t) (hg : MonotoneOn g t) (h'g : ContinuousOn g t) :
    MeasurableSet (g '' t) := by
  /- We use that the image of a measurable set by a continuous injective map is measurable.
  Therefore, we need to remove the points where the map is not injective. There are only countably
  many points that have several preimages, so this set is also measurable. -/
  let u : Set Œ≤ := {c | ‚àÉ x, ‚àÉ y, x ‚àà t ‚àß y ‚àà t ‚àß x < y ‚àß g x = c ‚àß g y = c}
  have hu : Set.Countable u := MonotoneOn.countable_setOf_two_preimages hg
  let t' := t ‚à© g ‚Åª¬π' u
  have ht' : MeasurableSet t' := by
    have : t' = ‚ãÉ c ‚àà u, t ‚à© g‚Åª¬π' {c} := by ext; simp [t']
    rw [this]
    apply MeasurableSet.biUnion hu (fun c hc ‚Ü¶ ?_)
    obtain ‚ü®v, hv, tv‚ü© : ‚àÉ v, OrdConnected v ‚àß t ‚à© g ‚Åª¬π' {c} = t ‚à© v :=
      ordConnected_singleton.preimage_monotoneOn hg
    exact tv ‚ñ∏ ht.inter hv.measurableSet
  have : g '' t = g '' (t \ t') ‚à™ g '' t' := by simp [‚Üê image_union, t']
  rw [this]
  apply MeasurableSet.union
  ¬∑ apply (ht.diff ht').image_of_continuousOn_injOn (h'g.mono diff_subset)
    intro x hx y hy hxy
    contrapose! hxy
    wlog H : x < y generalizing x y with h
    ¬∑ have : y < x := lt_of_le_of_ne (not_lt.1 H) hxy.symm
      exact (h hy hx hxy.symm this).symm
    intro h
    exact hx.2 ‚ü®hx.1, x, y, hx.1, hy.1, H, rfl, h.symm‚ü©
  ¬∑ exact hu.mono (by simp [t']) |>.measurableSet

/-- The image of a measurable set under a monotone map is measurable. -/
theorem MeasurableSet.image_of_monotoneOn [SecondCountableTopology Œ≤]
    (ht : MeasurableSet t) (hg : MonotoneOn g t) : MeasurableSet (g '' t) := by
  /- Since there are only countably many discontinuity points, the result follows by reduction to
  the continuous case, which we have already proved. -/
  let t' := {x ‚àà t | ¬¨ ContinuousWithinAt g t x}
  have ht' : Set.Countable t' := hg.countable_not_continuousWithinAt
  have : g '' t = g '' (t \ t') ‚à™ g '' t' := by
    rw [‚Üê image_union]
    congr!
    ext
    simp only [sdiff_sep_self, not_not, mem_union, mem_setOf_eq, t']
    tauto
  rw [this]
  apply MeasurableSet.union _ (ht'.image g).measurableSet
  apply MeasurableSet.image_of_monotoneOn_of_continuousOn (ht.diff ht'.measurableSet)
    (hg.mono diff_subset)
  intro x hx
  simp only [sdiff_sep_self, not_not, mem_setOf_eq, t'] at hx
  exact hx.2.mono diff_subset

/-- The image of a measurable set under an antitone map is measurable. -/
theorem MeasurableSet.image_of_antitoneOn [SecondCountableTopology Œ≤]
    (ht : MeasurableSet t) (hg : AntitoneOn g t) : MeasurableSet (g '' t) :=
  (ht.image_of_monotoneOn hg.dual_right :)

end LinearOrder

/-- The set of points for which a sequence of measurable functions converges to a given function
is measurable. -/
@[measurability]
lemma MeasureTheory.measurableSet_tendsto_fun [MeasurableSpace Œ≥] [Countable Œπ]
    {l : Filter Œπ} [l.IsCountablyGenerated]
    [TopologicalSpace Œ≥] [SecondCountableTopology Œ≥] [PseudoMetrizableSpace Œ≥]
    [OpensMeasurableSpace Œ≥]
    {f : Œπ ‚Üí Œ≤ ‚Üí Œ≥} (hf : ‚àÄ i, Measurable (f i)) {g : Œ≤ ‚Üí Œ≥} (hg : Measurable g) :
    MeasurableSet { x | Tendsto (fun n ‚Ü¶ f n x) l (ùìù (g x)) } := by
  letI := TopologicalSpace.pseudoMetrizableSpacePseudoMetric Œ≥
  simp_rw [tendsto_iff_dist_tendsto_zero (f := fun n ‚Ü¶ f n _)]
  exact measurableSet_tendsto (ùìù 0) (fun n ‚Ü¶ (hf n).dist hg)

/-- The set of points for which a measurable sequence of functions converges is measurable. -/
@[measurability]
theorem MeasureTheory.measurableSet_exists_tendsto
    [TopologicalSpace Œ≥] [PolishSpace Œ≥] [MeasurableSpace Œ≥]
    [hŒ≥ : OpensMeasurableSpace Œ≥] [Countable Œπ] {l : Filter Œπ}
    [l.IsCountablyGenerated] {f : Œπ ‚Üí Œ≤ ‚Üí Œ≥} (hf : ‚àÄ i, Measurable (f i)) :
    MeasurableSet { x | ‚àÉ c, Tendsto (fun n => f n x) l (ùìù c) } := by
  rcases l.eq_or_neBot with rfl | hl
  ¬∑ simp
  letI := TopologicalSpace.upgradeIsCompletelyMetrizable Œ≥
  rcases l.exists_antitone_basis with ‚ü®u, hu‚ü©
  simp_rw [‚Üê cauchy_map_iff_exists_tendsto]
  change MeasurableSet { x | _ ‚àß _ }
  have : ‚àÄ x, (map (f ¬∑ x) l √óÀ¢ map (f ¬∑ x) l).HasAntitoneBasis fun n =>
      ((f ¬∑ x) '' u n) √óÀ¢ ((f ¬∑ x) '' u n) := fun x => (hu.map _).prod (hu.map _)
  simp_rw [and_iff_right (hl.map _),
    Filter.HasBasis.le_basis_iff (this _).toHasBasis Metric.uniformity_basis_dist_inv_nat_succ,
    Set.setOf_forall]
  refine MeasurableSet.biInter Set.countable_univ fun K _ => ?_
  simp_rw [Set.setOf_exists, true_and]
  refine MeasurableSet.iUnion fun N => ?_
  simp_rw [prod_image_image_eq, image_subset_iff, prod_subset_iff, Set.setOf_forall]
  exact
    MeasurableSet.biInter (to_countable (u N)) fun i _ =>
      MeasurableSet.biInter (to_countable (u N)) fun j _ =>
        measurableSet_lt (Measurable.dist (hf i) (hf j)) measurable_const

section StandardBorelSpace

variable [MeasurableSpace Œ±] [StandardBorelSpace Œ±]

/-- If `s` is a measurable set in a standard Borel space, there is a compatible Polish topology
making `s` clopen. -/
theorem MeasurableSet.isClopenable' {s : Set Œ±} (hs : MeasurableSet s) :
    ‚àÉ _ : TopologicalSpace Œ±, BorelSpace Œ± ‚àß PolishSpace Œ± ‚àß IsClosed s ‚àß IsOpen s := by
  letI := upgradeStandardBorel Œ±
  obtain ‚ü®t, hle, ht, s_clopen‚ü© := hs.isClopenable
  refine ‚ü®t, ?_, ht, s_clopen‚ü©
  constructor
  rw [eq_borel_upgradeStandardBorel Œ±, borel_eq_borel_of_le ht _ hle]
  infer_instance

/-- A measurable subspace of a standard Borel space is standard Borel. -/
theorem MeasurableSet.standardBorel {s : Set Œ±} (hs : MeasurableSet s) :
    StandardBorelSpace s := by
  obtain ‚ü®_, _, _, s_closed, _‚ü© := hs.isClopenable'
  haveI := s_closed.polishSpace
  infer_instance

end StandardBorelSpace

/-! ### The Borel Isomorphism Theorem -/

namespace PolishSpace

variable {Œ≤ : Type*}
variable [MeasurableSpace Œ±] [MeasurableSpace Œ≤] [StandardBorelSpace Œ±] [StandardBorelSpace Œ≤]

/-- If two standard Borel spaces admit Borel measurable injections to one another,
then they are Borel isomorphic. -/
noncomputable def borelSchroederBernstein {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±} (fmeas : Measurable f)
    (finj : Function.Injective f) (gmeas : Measurable g) (ginj : Function.Injective g) : Œ± ‚âÉ·µê Œ≤ :=
  letI := upgradeStandardBorel Œ±
  letI := upgradeStandardBorel Œ≤
  (fmeas.measurableEmbedding finj).schroederBernstein (gmeas.measurableEmbedding ginj)

/-- Any uncountable standard Borel space is Borel isomorphic to the Cantor space `‚Ñï ‚Üí Bool`. -/
noncomputable def measurableEquivNatBoolOfNotCountable (h : ¬¨Countable Œ±) : Œ± ‚âÉ·µê (‚Ñï ‚Üí Bool) := by
  apply Nonempty.some
  letI := upgradeStandardBorel Œ±
  obtain ‚ü®f, -, fcts, finj‚ü© :=
    isClosed_univ.exists_nat_bool_injection_of_not_countable (Œ± := Œ±)
      (by rwa [‚Üê countable_coe_iff, (Equiv.Set.univ _).countable_iff])
  obtain ‚ü®g, gmeas, ginj‚ü© :=
    MeasurableSpace.measurable_injection_nat_bool_of_countablySeparated Œ±
  exact ‚ü®borelSchroederBernstein gmeas ginj fcts.measurable finj‚ü©

/-- The **Borel Isomorphism Theorem**: Any two uncountable standard Borel spaces are
Borel isomorphic. -/
noncomputable def measurableEquivOfNotCountable (hŒ± : ¬¨Countable Œ±) (hŒ≤ : ¬¨Countable Œ≤) : Œ± ‚âÉ·µê Œ≤ :=
  (measurableEquivNatBoolOfNotCountable hŒ±).trans (measurableEquivNatBoolOfNotCountable hŒ≤).symm

/-- The **Borel Isomorphism Theorem**: If two standard Borel spaces have the same cardinality,
they are Borel isomorphic. -/
noncomputable def Equiv.measurableEquiv (e : Œ± ‚âÉ Œ≤) : Œ± ‚âÉ·µê Œ≤ := by
  by_cases h : Countable Œ±
  ¬∑ letI := Countable.of_equiv Œ± e
    refine ‚ü®e, ?_, ?_‚ü© <;> apply measurable_of_countable
  refine measurableEquivOfNotCountable h ?_
  rwa [e.countable_iff] at h

end PolishSpace
