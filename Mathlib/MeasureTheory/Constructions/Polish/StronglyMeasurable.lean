/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.MeasureTheory.Constructions.Polish.Basic
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic

/-!
# Results about strongly measurable functions

In measure theory it is often assumed that some space is a `PolishSpace`, i.e. a separable and
completely metrizable topological space, because it ensures a nice interaction between the topology
and the measurable space structure. Moreover a strongly measurable function whose codomain is a
metric space is measurable and has a separable range
(see `stronglyMeasurable_iff_measurable_separable`). Therefore if the codomain is also complete,
by corestricting the function to the closure of its range, some results about measurable functions
can be extended to strongly measurable functions without assuming separability on the codomain.
The purpose of this file is to collect those results.
-/

open Filter MeasureTheory Set TopologicalSpace

open scoped Topology

variable {Œπ X E : Type*} [MeasurableSpace X] [TopologicalSpace E] [IsCompletelyMetrizableSpace E]
  [Countable Œπ] {l : Filter Œπ} [l.IsCountablyGenerated] {f : Œπ ‚Üí X ‚Üí E}

namespace MeasureTheory.StronglyMeasurable

theorem measurableSet_exists_tendsto (hf : ‚àÄ i, StronglyMeasurable (f i)) :
    MeasurableSet {x | ‚àÉ c, Tendsto (f ¬∑ x) l (ùìù c)} := by
  obtain rfl | hl := eq_or_neBot l
  ¬∑ simp_all
  borelize E
  letI := upgradeIsCompletelyMetrizable E
  let s := closure (‚ãÉ i, range (f i))
  have : PolishSpace s :=
    { toSecondCountableTopology := @UniformSpace.secondCountable_of_separable s _ _
        (IsSeparable.iUnion (fun i ‚Ü¶ (hf i).isSeparable_range)).closure.separableSpace
      toIsCompletelyMetrizableSpace := isClosed_closure.isCompletelyMetrizableSpace }
  let g i x : s := ‚ü®f i x, subset_closure <| mem_iUnion.2 ‚ü®i, ‚ü®x, rfl‚ü©‚ü©‚ü©
  have mg i : Measurable (g i) := (hf i).measurable.subtype_mk
  convert MeasureTheory.measurableSet_exists_tendsto (l := l) mg with x
  refine ‚ü®fun ‚ü®c, hc‚ü© ‚Ü¶ ‚ü®‚ü®c, ?_‚ü©, tendsto_subtype_rng.2 hc‚ü©,
    fun ‚ü®c, hc‚ü© ‚Ü¶ ‚ü®c, tendsto_subtype_rng.1 hc‚ü©‚ü©
  exact mem_closure_of_tendsto hc (Eventually.of_forall fun i ‚Ü¶ mem_iUnion.2 ‚ü®i, ‚ü®x, rfl‚ü©‚ü©)

theorem limUnder [hE : Nonempty E] (hf : ‚àÄ i, StronglyMeasurable (f i)) :
    StronglyMeasurable (fun x ‚Ü¶ limUnder l (f ¬∑ x)) := by
  obtain rfl | hl := eq_or_neBot l
  ¬∑ simpa [limUnder, Filter.map_bot] using stronglyMeasurable_const
  borelize E
  let e := Classical.choice hE
  rw [stronglyMeasurable_iff_measurable_separable]; constructor
  ¬∑ let conv := {x | ‚àÉ c, Tendsto (f ¬∑ x) l (ùìù c)}
    have mconv : MeasurableSet conv := StronglyMeasurable.measurableSet_exists_tendsto hf
    have : (fun x ‚Ü¶ _root_.limUnder l (f ¬∑ x)) = ((‚Üë) : conv ‚Üí X).extend
        (fun x ‚Ü¶ _root_.limUnder l (f ¬∑ x)) (fun _ ‚Ü¶ e) := by
      ext x
      by_cases hx : x ‚àà conv
      ¬∑ rw [Function.extend_val_apply hx]
      ¬∑ rw [Function.extend_val_apply' hx, limUnder_of_not_tendsto hx]
    rw [this]
    refine (MeasurableEmbedding.subtype_coe mconv).measurable_extend
      (measurable_of_tendsto_metrizable' l
        (fun i ‚Ü¶ (hf i).measurable.comp measurable_subtype_coe)
        (tendsto_pi_nhds.2 fun ‚ü®x, ‚ü®c, hc‚ü©‚ü© ‚Ü¶ ?_)) measurable_const
    rwa [hc.limUnder_eq]
  ¬∑ let s := closure (‚ãÉ i, range (f i)) ‚à™ {e}
    have hs : IsSeparable s := (IsSeparable.iUnion (fun i ‚Ü¶ (hf i).isSeparable_range)).closure.union
      (finite_singleton e).isSeparable
    refine hs.mono ?_
    rintro - ‚ü®x, rfl‚ü©
    by_cases hx : ‚àÉ c, Tendsto (f ¬∑ x) l (ùìù c)
    ¬∑ obtain ‚ü®c, hc‚ü© := hx
      simp_rw [hc.limUnder_eq]
      exact subset_union_left <| mem_closure_of_tendsto hc
        (Eventually.of_forall fun i ‚Ü¶ mem_iUnion.2 ‚ü®i, ‚ü®x, rfl‚ü©‚ü©)
    ¬∑ simp_rw [limUnder_of_not_tendsto hx]
      exact subset_union_right (mem_singleton e)

end MeasureTheory.StronglyMeasurable
