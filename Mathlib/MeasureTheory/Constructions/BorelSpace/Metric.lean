/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Yury Kudryashov
-/
import Mathlib.Analysis.Normed.Group.Continuity
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic
import Mathlib.Topology.MetricSpace.Thickening

/-!
# Borel sigma algebras on (pseudo-)metric spaces

## Main statements

* `measurable_dist`, `measurable_infEdist`, `measurable_norm`, `measurable_enorm`,
  `Measurable.dist`, `Measurable.infEdist`, `Measurable.norm`, `Measurable.enorm`:
  measurability of various metric-related notions;
* `tendsto_measure_thickening_of_isClosed`:
  the measure of a closed set is the limit of the measure of its Îµ-thickenings as Îµ â†’ 0.
* `exists_borelSpace_of_countablyGenerated_of_separatesPoints`:
  if a measurable space is countably generated and separates points, it arises as the Borel sets
  of some second countable separable metrizable topology.

-/

open Set Filter MeasureTheory MeasurableSpace TopologicalSpace

open scoped Topology NNReal ENNReal MeasureTheory

universe u v w x y

variable {Î± Î² Î³ Î´ : Type*} {Î¹ : Sort y} {s t u : Set Î±}

section PseudoMetricSpace

variable [PseudoMetricSpace Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±]
variable [MeasurableSpace Î²] {x : Î±} {Îµ : â„}

open Metric

@[measurability]
theorem measurableSet_ball : MeasurableSet (Metric.ball x Îµ) :=
  Metric.isOpen_ball.measurableSet

@[measurability]
theorem measurableSet_closedBall : MeasurableSet (Metric.closedBall x Îµ) :=
  Metric.isClosed_closedBall.measurableSet

@[measurability]
theorem measurable_infDist {s : Set Î±} : Measurable fun x => infDist x s :=
  (continuous_infDist_pt s).measurable

@[measurability, fun_prop]
theorem Measurable.infDist {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} :
    Measurable fun x => infDist (f x) s :=
  measurable_infDist.comp hf

@[measurability]
theorem measurable_infNndist {s : Set Î±} : Measurable fun x => infNndist x s :=
  (continuous_infNndist_pt s).measurable

@[measurability, fun_prop]
theorem Measurable.infNndist {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} :
    Measurable fun x => infNndist (f x) s :=
  measurable_infNndist.comp hf

section

variable [SecondCountableTopology Î±]

@[measurability]
theorem measurable_dist : Measurable fun p : Î± Ã— Î± => dist p.1 p.2 :=
  continuous_dist.measurable

@[measurability, fun_prop]
theorem Measurable.dist {f g : Î² â†’ Î±} (hf : Measurable f) (hg : Measurable g) :
    Measurable fun b => dist (f b) (g b) :=
  (@continuous_dist Î± _).measurable2 hf hg

@[measurability]
theorem measurable_nndist : Measurable fun p : Î± Ã— Î± => nndist p.1 p.2 :=
  continuous_nndist.measurable

@[measurability, fun_prop]
theorem Measurable.nndist {f g : Î² â†’ Î±} (hf : Measurable f) (hg : Measurable g) :
    Measurable fun b => nndist (f b) (g b) :=
  (@continuous_nndist Î± _).measurable2 hf hg

end

end PseudoMetricSpace

section PseudoEMetricSpace

variable [PseudoEMetricSpace Î±] [MeasurableSpace Î±] [OpensMeasurableSpace Î±]
variable [MeasurableSpace Î²] {x : Î±} {Îµ : â„â‰¥0âˆ}

open EMetric

@[measurability]
theorem measurableSet_eball : MeasurableSet (EMetric.ball x Îµ) :=
  EMetric.isOpen_ball.measurableSet

@[measurability, fun_prop]
theorem measurable_edist_right : Measurable (edist x) :=
  (continuous_const.edist continuous_id).measurable

@[measurability, fun_prop]
theorem measurable_edist_left : Measurable fun y => edist y x :=
  (continuous_id.edist continuous_const).measurable

@[measurability]
theorem measurable_infEdist {s : Set Î±} : Measurable fun x => infEdist x s :=
  continuous_infEdist.measurable

@[measurability, fun_prop]
theorem Measurable.infEdist {f : Î² â†’ Î±} (hf : Measurable f) {s : Set Î±} :
    Measurable fun x => infEdist (f x) s :=
  measurable_infEdist.comp hf

open Metric EMetric

/-- If a set has a closed thickening with finite measure, then the measure of its `r`-closed
thickenings converges to the measure of its closure as `r` tends to `0`. -/
theorem tendsto_measure_cthickening {Î¼ : Measure Î±} {s : Set Î±}
    (hs : âˆƒ R > 0, Î¼ (cthickening R s) â‰  âˆ) :
    Tendsto (fun r => Î¼ (cthickening r s)) (ğ“ 0) (ğ“ (Î¼ (closure s))) := by
  have A : Tendsto (fun r => Î¼ (cthickening r s)) (ğ“[Ioi 0] 0) (ğ“ (Î¼ (closure s))) := by
    rw [closure_eq_iInter_cthickening]
    exact
      tendsto_measure_biInter_gt (fun r _ => isClosed_cthickening.nullMeasurableSet)
        (fun i j _ ij => cthickening_mono ij _) hs
  have B : Tendsto (fun r => Î¼ (cthickening r s)) (ğ“[Iic 0] 0) (ğ“ (Î¼ (closure s))) := by
    apply Tendsto.congr' _ tendsto_const_nhds
    filter_upwards [self_mem_nhdsWithin (Î± := â„)] with _ hr
    rw [cthickening_of_nonpos hr]
  convert B.sup A
  exact (nhdsLE_sup_nhdsGT 0).symm

/-- If a closed set has a closed thickening with finite measure, then the measure of its closed
`r`-thickenings converge to its measure as `r` tends to `0`. -/
theorem tendsto_measure_cthickening_of_isClosed {Î¼ : Measure Î±} {s : Set Î±}
    (hs : âˆƒ R > 0, Î¼ (cthickening R s) â‰  âˆ) (h's : IsClosed s) :
    Tendsto (fun r => Î¼ (cthickening r s)) (ğ“ 0) (ğ“ (Î¼ s)) := by
  convert tendsto_measure_cthickening hs
  exact h's.closure_eq.symm

/-- If a set has a thickening with finite measure, then the measures of its `r`-thickenings
converge to the measure of its closure as `r > 0` tends to `0`. -/
theorem tendsto_measure_thickening {Î¼ : Measure Î±} {s : Set Î±}
    (hs : âˆƒ R > 0, Î¼ (thickening R s) â‰  âˆ) :
    Tendsto (fun r => Î¼ (thickening r s)) (ğ“[>] 0) (ğ“ (Î¼ (closure s))) := by
  rw [closure_eq_iInter_thickening]
  exact tendsto_measure_biInter_gt (fun r _ => isOpen_thickening.nullMeasurableSet)
      (fun i j _ ij => thickening_mono ij _) hs

/-- If a closed set has a thickening with finite measure, then the measure of its
`r`-thickenings converge to its measure as `r > 0` tends to `0`. -/
theorem tendsto_measure_thickening_of_isClosed {Î¼ : Measure Î±} {s : Set Î±}
    (hs : âˆƒ R > 0, Î¼ (thickening R s) â‰  âˆ) (h's : IsClosed s) :
    Tendsto (fun r => Î¼ (thickening r s)) (ğ“[>] 0) (ğ“ (Î¼ s)) := by
  convert tendsto_measure_thickening hs
  exact h's.closure_eq.symm

variable [SecondCountableTopology Î±]

@[measurability]
theorem measurable_edist : Measurable fun p : Î± Ã— Î± => edist p.1 p.2 :=
  continuous_edist.measurable

@[measurability, fun_prop]
theorem Measurable.edist {f g : Î² â†’ Î±} (hf : Measurable f) (hg : Measurable g) :
    Measurable fun b => edist (f b) (g b) :=
  (@continuous_edist Î± _).measurable2 hf hg

@[measurability, fun_prop]
theorem AEMeasurable.edist {f g : Î² â†’ Î±} {Î¼ : Measure Î²} (hf : AEMeasurable f Î¼)
    (hg : AEMeasurable g Î¼) : AEMeasurable (fun a => edist (f a) (g a)) Î¼ :=
  (@continuous_edist Î± _).aemeasurable2 hf hg

end PseudoEMetricSpace

/-- Given a compact set in a proper space, the measure of its `r`-closed thickenings converges to
its measure as `r` tends to `0`. -/
theorem tendsto_measure_cthickening_of_isCompact [MetricSpace Î±] [MeasurableSpace Î±]
    [OpensMeasurableSpace Î±] [ProperSpace Î±] {Î¼ : Measure Î±} [IsFiniteMeasureOnCompacts Î¼]
    {s : Set Î±} (hs : IsCompact s) :
    Tendsto (fun r => Î¼ (Metric.cthickening r s)) (ğ“ 0) (ğ“ (Î¼ s)) :=
  tendsto_measure_cthickening_of_isClosed
    âŸ¨1, zero_lt_one, hs.isBounded.cthickening.measure_lt_top.neâŸ© hs.isClosed

/-- If a measurable space is countably generated and separates points, it arises as
the borel sets of some second countable t4 topology (i.e. a separable metrizable one). -/
theorem exists_borelSpace_of_countablyGenerated_of_separatesPoints (Î± : Type*)
    [m : MeasurableSpace Î±] [CountablyGenerated Î±] [SeparatesPoints Î±] :
    âˆƒ _ : TopologicalSpace Î±, SecondCountableTopology Î± âˆ§ T4Space Î± âˆ§ BorelSpace Î± := by
  rcases measurableEquiv_nat_bool_of_countablyGenerated Î± with âŸ¨s, âŸ¨fâŸ©âŸ©
  letI := induced f inferInstance
  let F := f.toEquiv.toHomeomorphOfIsInducing <| .induced _
  exact âŸ¨inferInstance, F.secondCountableTopology, F.symm.t4Space,
    f.measurableEmbedding.borelSpace F.isInducingâŸ©

/-- If a measurable space on `Î±` is countably generated and separates points, there is some
second countable t4 topology on `Î±` (i.e. a separable metrizable one) for which every
open set is measurable. -/
theorem exists_opensMeasurableSpace_of_countablySeparated (Î± : Type*)
    [m : MeasurableSpace Î±] [CountablySeparated Î±] :
    âˆƒ _ : TopologicalSpace Î±, SecondCountableTopology Î± âˆ§ T4Space Î± âˆ§ OpensMeasurableSpace Î± := by
  rcases exists_countablyGenerated_le_of_countablySeparated Î± with âŸ¨m', _, _, m'leâŸ©
  rcases exists_borelSpace_of_countablyGenerated_of_separatesPoints (m := m') with âŸ¨Ï„, _, _, Ï„m'âŸ©
  exact âŸ¨Ï„, â€¹_â€º, â€¹_â€º, @OpensMeasurableSpace.mk _ _ m (Ï„m'.measurable_eq.symm.le.trans m'le)âŸ©


section ContinuousENorm

variable {Îµ : Type*} [MeasurableSpace Îµ] [TopologicalSpace Îµ] [ContinuousENorm Îµ]
  [OpensMeasurableSpace Îµ] [MeasurableSpace Î²]

@[measurability, fun_prop]
lemma measurable_enorm : Measurable (enorm : Îµ â†’ â„â‰¥0âˆ) := continuous_enorm.measurable

@[measurability, fun_prop]
protected lemma Measurable.enorm {f : Î² â†’ Îµ} (hf : Measurable f) : Measurable (â€–f Â·â€–â‚‘) :=
  measurable_enorm.comp hf

@[measurability, fun_prop]
protected lemma AEMeasurable.enorm {f : Î² â†’ Îµ} {Î¼ : Measure Î²} (hf : AEMeasurable f Î¼) :
    AEMeasurable (â€–f Â·â€–â‚‘) Î¼ :=
  measurable_enorm.comp_aemeasurable hf

@[deprecated (since := "2025-01-21")] alias measurable_ennnorm := measurable_enorm
@[deprecated (since := "2025-01-21")] alias Measurable.ennnorm := Measurable.enorm
@[deprecated (since := "2025-01-21")] alias AEMeasurable.ennnorm := AEMeasurable.enorm

end ContinuousENorm

section NormedAddCommGroup

variable [MeasurableSpace Î±] [NormedAddCommGroup Î±] [OpensMeasurableSpace Î±] [MeasurableSpace Î²]

@[fun_prop, measurability]
theorem measurable_norm : Measurable (norm : Î± â†’ â„) :=
  continuous_norm.measurable

@[measurability, fun_prop]
theorem Measurable.norm {f : Î² â†’ Î±} (hf : Measurable f) : Measurable fun a => norm (f a) :=
  measurable_norm.comp hf

@[measurability, fun_prop]
theorem AEMeasurable.norm {f : Î² â†’ Î±} {Î¼ : Measure Î²} (hf : AEMeasurable f Î¼) :
    AEMeasurable (fun a => norm (f a)) Î¼ :=
  measurable_norm.comp_aemeasurable hf

@[measurability]
theorem measurable_nnnorm : Measurable (nnnorm : Î± â†’ â„â‰¥0) :=
  continuous_nnnorm.measurable

@[measurability, fun_prop]
protected theorem Measurable.nnnorm {f : Î² â†’ Î±} (hf : Measurable f) : Measurable fun a => â€–f aâ€–â‚Š :=
  measurable_nnnorm.comp hf

@[measurability, fun_prop]
protected lemma AEMeasurable.nnnorm {f : Î² â†’ Î±} {Î¼ : Measure Î²} (hf : AEMeasurable f Î¼) :
    AEMeasurable (fun a => â€–f aâ€–â‚Š) Î¼ :=
  measurable_nnnorm.comp_aemeasurable hf

end NormedAddCommGroup
