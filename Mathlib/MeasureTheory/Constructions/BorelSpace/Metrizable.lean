/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic
import Mathlib.Topology.Metrizable.Basic

#align_import measure_theory.constructions.borel_space.metrizable from "leanprover-community/mathlib"@"bf6a01357ff5684b1ebcd0f1a13be314fc82c0bf"

/-!
# Measurable functions in (pseudo-)metrizable Borel spaces
-/


open Filter MeasureTheory TopologicalSpace

open Classical Topology NNReal ENNReal MeasureTheory

variable {Î± Î² : Type*} [MeasurableSpace Î±]

section Limits

variable [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²]

open Metric

/-- A limit (over a general filter) of measurable `â„â‰¥0âˆ` valued functions is measurable. -/
theorem measurable_of_tendsto_ennreal' {Î¹} {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} {g : Î± â†’ â„â‰¥0âˆ} (u : Filter Î¹)
    [NeBot u] [IsCountablyGenerated u] (hf : âˆ€ i, Measurable (f i)) (lim : Tendsto f u (ğ“ g)) :
    Measurable g := by
  rcases u.exists_seq_tendsto with âŸ¨x, hxâŸ©
  rw [tendsto_pi_nhds] at lim
  have : (fun y => liminf (fun n => (f (x n) y : â„â‰¥0âˆ)) atTop) = g := by
    ext1 y
    exact ((lim y).comp hx).liminf_eq
  rw [â† this]
  show Measurable fun y => liminf (fun n => (f (x n) y : â„â‰¥0âˆ)) atTop
  exact measurable_liminf fun n => hf (x n)
#align measurable_of_tendsto_ennreal' measurable_of_tendsto_ennreal'

/-- A sequential limit of measurable `â„â‰¥0âˆ` valued functions is measurable. -/
theorem measurable_of_tendsto_ennreal {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {g : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ i, Measurable (f i))
    (lim : Tendsto f atTop (ğ“ g)) : Measurable g :=
  measurable_of_tendsto_ennreal' atTop hf lim
#align measurable_of_tendsto_ennreal measurable_of_tendsto_ennreal

/-- A limit (over a general filter) of measurable `â„â‰¥0` valued functions is measurable. -/
theorem measurable_of_tendsto_nnreal' {Î¹} {f : Î¹ â†’ Î± â†’ â„â‰¥0} {g : Î± â†’ â„â‰¥0} (u : Filter Î¹) [NeBot u]
    [IsCountablyGenerated u] (hf : âˆ€ i, Measurable (f i)) (lim : Tendsto f u (ğ“ g)) :
    Measurable g := by
  simp_rw [â† measurable_coe_nnreal_ennreal_iff] at hf âŠ¢
  refine' measurable_of_tendsto_ennreal' u hf _
  rw [tendsto_pi_nhds] at lim âŠ¢
  exact fun x => (ENNReal.continuous_coe.tendsto (g x)).comp (lim x)
#align measurable_of_tendsto_nnreal' measurable_of_tendsto_nnreal'

/-- A sequential limit of measurable `â„â‰¥0` valued functions is measurable. -/
theorem measurable_of_tendsto_nnreal {f : â„• â†’ Î± â†’ â„â‰¥0} {g : Î± â†’ â„â‰¥0} (hf : âˆ€ i, Measurable (f i))
    (lim : Tendsto f atTop (ğ“ g)) : Measurable g :=
  measurable_of_tendsto_nnreal' atTop hf lim
#align measurable_of_tendsto_nnreal measurable_of_tendsto_nnreal

/-- A limit (over a general filter) of measurable functions valued in a (pseudo) metrizable space is
measurable. -/
theorem measurable_of_tendsto_metrizable' {Î¹} {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (u : Filter Î¹) [NeBot u]
    [IsCountablyGenerated u] (hf : âˆ€ i, Measurable (f i)) (lim : Tendsto f u (ğ“ g)) :
    Measurable g := by
  letI : PseudoMetricSpace Î² := pseudoMetrizableSpacePseudoMetric Î²
  apply measurable_of_isClosed'
  intro s h1s h2s h3s
  have : Measurable fun x => infNndist (g x) s := by
    suffices : Tendsto (fun i x => infNndist (f i x) s) u (ğ“ fun x => infNndist (g x) s)
    exact measurable_of_tendsto_nnreal' u (fun i => (hf i).infNndist) this
    rw [tendsto_pi_nhds] at lim âŠ¢
    intro x
    exact ((continuous_infNndist_pt s).tendsto (g x)).comp (lim x)
  have h4s : g â»Â¹' s = (fun x => infNndist (g x) s) â»Â¹' {0} := by
    ext x
    simp [h1s, â† h1s.mem_iff_infDist_zero h2s, â† NNReal.coe_eq_zero]
  rw [h4s]
  exact this (measurableSet_singleton 0)
#align measurable_of_tendsto_metrizable' measurable_of_tendsto_metrizable'

/-- A sequential limit of measurable functions valued in a (pseudo) metrizable space is
measurable. -/
theorem measurable_of_tendsto_metrizable {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²} (hf : âˆ€ i, Measurable (f i))
    (lim : Tendsto f atTop (ğ“ g)) : Measurable g :=
  measurable_of_tendsto_metrizable' atTop hf lim
#align measurable_of_tendsto_metrizable measurable_of_tendsto_metrizable

theorem aemeasurable_of_tendsto_metrizable_ae {Î¹} {Î¼ : Measure Î±} {f : Î¹ â†’ Î± â†’ Î²} {g : Î± â†’ Î²}
    (u : Filter Î¹) [hu : NeBot u] [IsCountablyGenerated u] (hf : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) u (ğ“ (g x))) : AEMeasurable g Î¼ := by
  rcases u.exists_seq_tendsto with âŸ¨v, hvâŸ©
  have h'f : âˆ€ n, AEMeasurable (f (v n)) Î¼ := fun n => hf (v n)
  set p : Î± â†’ (â„• â†’ Î²) â†’ Prop := fun x f' => Tendsto (fun n => f' n) atTop (ğ“ (g x))
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun n => f (v n) x := by
    filter_upwards [h_tendsto] with x hx using hx.comp hv
  set aeSeqLim := fun x => ite (x âˆˆ aeSeqSet h'f p) (g x) (âŸ¨f (v 0) xâŸ© : Nonempty Î²).some
  refine'
    âŸ¨aeSeqLim,
      measurable_of_tendsto_metrizable' atTop (aeSeq.measurable h'f p)
        (tendsto_pi_nhds.mpr fun x => _),
      _âŸ©
  Â· simp_rw [aeSeq]
    split_ifs with hx
    Â· simp_rw [aeSeq.mk_eq_fun_of_mem_aeSeqSet h'f hx]
      exact @aeSeq.fun_prop_of_mem_aeSeqSet _ Î± Î² _ _ _ _ _ h'f x hx
    Â· exact tendsto_const_nhds
  Â· exact
      (ite_ae_eq_of_measure_compl_zero g (fun x => (âŸ¨f (v 0) xâŸ© : Nonempty Î²).some) (aeSeqSet h'f p)
          (aeSeq.measure_compl_aeSeqSet_eq_zero h'f hp)).symm
#align ae_measurable_of_tendsto_metrizable_ae aemeasurable_of_tendsto_metrizable_ae

theorem aemeasurable_of_tendsto_metrizable_ae' {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²}
    (hf : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_ae_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) : AEMeasurable g Î¼ :=
  aemeasurable_of_tendsto_metrizable_ae atTop hf h_ae_tendsto
#align ae_measurable_of_tendsto_metrizable_ae' aemeasurable_of_tendsto_metrizable_ae'

theorem aemeasurable_of_unif_approx {Î²} [MeasurableSpace Î²] [PseudoMetricSpace Î²] [BorelSpace Î²]
    {Î¼ : Measure Î±} {g : Î± â†’ Î²}
    (hf : âˆ€ Îµ > (0 : â„), âˆƒ f : Î± â†’ Î², AEMeasurable f Î¼ âˆ§ âˆ€áµ x âˆ‚Î¼, dist (f x) (g x) â‰¤ Îµ) :
    AEMeasurable g Î¼ := by
  obtain âŸ¨u, -, u_pos, u_limâŸ© :
    âˆƒ u : â„• â†’ â„, StrictAnti u âˆ§ (âˆ€ n : â„•, 0 < u n) âˆ§ Tendsto u atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  choose f Hf using fun n : â„• => hf (u n) (u_pos n)
  have : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x)) := by
    have : âˆ€áµ x âˆ‚Î¼, âˆ€ n, dist (f n x) (g x) â‰¤ u n := ae_all_iff.2 fun n => (Hf n).2
    filter_upwards [this]
    intro x hx
    rw [tendsto_iff_dist_tendsto_zero]
    exact squeeze_zero (fun n => dist_nonneg) hx u_lim
  exact aemeasurable_of_tendsto_metrizable_ae' (fun n => (Hf n).1) this
#align ae_measurable_of_unif_approx aemeasurable_of_unif_approx

theorem measurable_of_tendsto_metrizable_ae {Î¼ : Measure Î±} [Î¼.IsComplete] {f : â„• â†’ Î± â†’ Î²}
    {g : Î± â†’ Î²} (hf : âˆ€ n, Measurable (f n))
    (h_ae_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ (g x))) : Measurable g :=
  aemeasurable_iff_measurable.mp
    (aemeasurable_of_tendsto_metrizable_ae' (fun i => (hf i).aemeasurable) h_ae_tendsto)
#align measurable_of_tendsto_metrizable_ae measurable_of_tendsto_metrizable_ae

theorem measurable_limit_of_tendsto_metrizable_ae {Î¹} [Countable Î¹] [Nonempty Î¹] {Î¼ : Measure Î±}
    {f : Î¹ â†’ Î± â†’ Î²} {L : Filter Î¹} [L.IsCountablyGenerated] (hf : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_ae_tendsto : âˆ€áµ x âˆ‚Î¼, âˆƒ l : Î², Tendsto (fun n => f n x) L (ğ“ l)) :
    âˆƒ (f_lim : Î± â†’ Î²) (hf_lim_meas : Measurable f_lim),
      âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) L (ğ“ (f_lim x)) := by
  inhabit Î¹
  rcases eq_or_neBot L with (rfl | hL)
  Â· exact âŸ¨(hf default).mk _, (hf default).measurable_mk, eventually_of_forall fun x => tendsto_botâŸ©
  let p : Î± â†’ (Î¹ â†’ Î²) â†’ Prop := fun x f' => âˆƒ l : Î², Tendsto (fun n => f' n) L (ğ“ l)
  have hp_mem : âˆ€ x âˆˆ aeSeqSet hf p, p x fun n => f n x := fun x hx =>
    aeSeq.fun_prop_of_mem_aeSeqSet hf hx
  have h_ae_eq : âˆ€áµ x âˆ‚Î¼, âˆ€ n, aeSeq hf p n x = f n x := aeSeq.aeSeq_eq_fun_ae hf h_ae_tendsto
  set f_lim : Î± â†’ Î² := fun x => dite (x âˆˆ aeSeqSet hf p) (fun h => (hp_mem x h).choose)
    fun _ => (âŸ¨f default xâŸ© : Nonempty Î²).some
  have hf_lim : âˆ€ x, Tendsto (fun n => aeSeq hf p n x) L (ğ“ (f_lim x)) := by
    intro x
    simp only [aeSeq]
    split_ifs with h
    Â· refine' (hp_mem x h).choose_spec.congr fun n => _
      exact (aeSeq.mk_eq_fun_of_mem_aeSeqSet hf h n).symm
    Â· exact tendsto_const_nhds
  have h_ae_tendsto_f_lim : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) L (ğ“ (f_lim x)) :=
    h_ae_eq.mono fun x hx => (hf_lim x).congr hx
  have h_f_lim_meas : Measurable f_lim :=
    measurable_of_tendsto_metrizable' L (aeSeq.measurable hf p)
      (tendsto_pi_nhds.mpr fun x => hf_lim x)
  exact âŸ¨f_lim, h_f_lim_meas, h_ae_tendsto_f_limâŸ©
#align measurable_limit_of_tendsto_metrizable_ae measurable_limit_of_tendsto_metrizable_ae

end Limits

section TendstoIndicator

variable {Î± : Type*} [MeasurableSpace Î±] {A : Set Î±}
variable {Î¹ : Type*} (L : Filter Î¹) [IsCountablyGenerated L] {As : Î¹ â†’ Set Î±}

/-- If the indicator functions of measurable sets `Aáµ¢` converge to the indicator function of
a set `A` along a nontrivial countably generated filter, then `A` is also measurable. -/
lemma measurableSet_of_tendsto_indicator [NeBot L] (As_mble : âˆ€ i, MeasurableSet (As i))
    (h_lim : Tendsto (fun i â†¦ (As i).indicator (1 : Î± â†’ â„â‰¥0âˆ)) L (ğ“ (A.indicator 1))) :
    MeasurableSet A := by
  simp_rw [â† measurable_indicator_const_iff (1 : â„â‰¥0âˆ)] at As_mble âŠ¢
  exact measurable_of_tendsto_ennreal' L As_mble h_lim

/-- If the indicator functions of a.e.-measurable sets `Aáµ¢` converge a.e. to the indicator function
of a set `A` along a nontrivial countably generated filter, then `A` is also a.e.-measurable. -/
lemma nullMeasurableSet_of_tendsto_indicator [NeBot L] {Î¼ : Measure Î±}
    (As_mble : âˆ€ i, NullMeasurableSet (As i) Î¼)
    (h_lim : âˆ€áµ x âˆ‚Î¼, Tendsto (fun i â†¦ (As i).indicator (1 : Î± â†’ â„â‰¥0âˆ) x)
      L (ğ“ (A.indicator 1 x))) :
    NullMeasurableSet A Î¼ := by
  simp_rw [â† aemeasurable_indicator_const_iff (1 : â„â‰¥0âˆ)] at As_mble âŠ¢
  exact aemeasurable_of_tendsto_metrizable_ae L As_mble h_lim

end TendstoIndicator
