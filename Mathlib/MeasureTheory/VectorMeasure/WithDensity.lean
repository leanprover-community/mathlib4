/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.VectorMeasure.Basic
import Mathlib.MeasureTheory.Function.AEEqOfIntegral

/-!

# Vector measure defined by an integral

Given a measure `Î¼` and an integrable function `f : Î± â†’ E`, we can define a vector measure `v` such
that for all measurable set `s`, `v i = âˆ« x in s, f x âˆ‚Î¼`. This definition is useful for
the Radon-Nikodym theorem for signed measures.

## Main definitions

* `MeasureTheory.Measure.withDensityáµ¥`: the vector measure formed by integrating a function `f`
  with respect to a measure `Î¼` on some set if `f` is integrable, and `0` otherwise.

-/


noncomputable section

open scoped MeasureTheory NNReal ENNReal

variable {Î± : Type*} {m : MeasurableSpace Î±}

namespace MeasureTheory

open TopologicalSpace

variable {Î¼ : Measure Î±}
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

open Classical in
/-- Given a measure `Î¼` and an integrable function `f`, `Î¼.withDensityáµ¥ f` is
the vector measure which maps the set `s` to `âˆ«â‚› f âˆ‚Î¼`. -/
def Measure.withDensityáµ¥ {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’ E) : VectorMeasure Î± E :=
  if hf : Integrable f Î¼ then
    { measureOf' := fun s â†¦ if MeasurableSet s then âˆ« x in s, f x âˆ‚Î¼ else 0
      empty' := by simp
      not_measurable' := fun _ hs â†¦ if_neg hs
      m_iUnion' := fun s hsâ‚ hsâ‚‚ â†¦ by
        convert hasSum_integral_iUnion hsâ‚ hsâ‚‚ hf.integrableOn with n
        Â· rw [if_pos (hsâ‚ n)]
        Â· rw [if_pos (MeasurableSet.iUnion hsâ‚)] }
  else 0

open Measure

variable {f g : Î± â†’ E}

theorem withDensityáµ¥_apply (hf : Integrable f Î¼) {s : Set Î±} (hs : MeasurableSet s) :
    Î¼.withDensityáµ¥ f s = âˆ« x in s, f x âˆ‚Î¼ := by rw [withDensityáµ¥, dif_pos hf]; exact dif_pos hs

@[simp]
theorem withDensityáµ¥_zero : Î¼.withDensityáµ¥ (0 : Î± â†’ E) = 0 := by
  ext1 s hs
  rw [Pi.zero_def, withDensityáµ¥_apply (integrable_zero Î± E Î¼) hs]
  simp

@[simp]
theorem withDensityáµ¥_neg : Î¼.withDensityáµ¥ (-f) = -Î¼.withDensityáµ¥ f := by
  by_cases hf : Integrable f Î¼
  Â· ext1 i hi
    rw [VectorMeasure.neg_apply, withDensityáµ¥_apply hf hi, â† integral_neg,
      withDensityáµ¥_apply hf.neg hi]
    simp only [Pi.neg_apply]
  Â· rw [withDensityáµ¥, withDensityáµ¥, dif_neg hf, dif_neg, neg_zero]
    rwa [integrable_neg_iff]

theorem withDensityáµ¥_neg' : (Î¼.withDensityáµ¥ fun x â†¦ -f x) = -Î¼.withDensityáµ¥ f :=
  withDensityáµ¥_neg

@[simp]
theorem withDensityáµ¥_add (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼.withDensityáµ¥ (f + g) = Î¼.withDensityáµ¥ f + Î¼.withDensityáµ¥ g := by
  ext1 i hi
  rw [withDensityáµ¥_apply (hf.add hg) hi, VectorMeasure.add_apply, withDensityáµ¥_apply hf hi,
    withDensityáµ¥_apply hg hi]
  simp_rw [Pi.add_apply]
  rw [integral_add]
  Â· exact hf.integrableOn
  Â· exact hg.integrableOn

theorem withDensityáµ¥_add' (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    (Î¼.withDensityáµ¥ fun x â†¦ f x + g x) = Î¼.withDensityáµ¥ f + Î¼.withDensityáµ¥ g :=
  withDensityáµ¥_add hf hg

@[simp]
theorem withDensityáµ¥_sub (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼.withDensityáµ¥ (f - g) = Î¼.withDensityáµ¥ f - Î¼.withDensityáµ¥ g := by
  rw [sub_eq_add_neg, sub_eq_add_neg, withDensityáµ¥_add hf hg.neg, withDensityáµ¥_neg]

theorem withDensityáµ¥_sub' (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    (Î¼.withDensityáµ¥ fun x â†¦ f x - g x) = Î¼.withDensityáµ¥ f - Î¼.withDensityáµ¥ g :=
  withDensityáµ¥_sub hf hg

@[simp]
theorem withDensityáµ¥_smul {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]
    [SMulCommClass â„ ğ•œ E] (f : Î± â†’ E) (r : ğ•œ) : Î¼.withDensityáµ¥ (r â€¢ f) = r â€¢ Î¼.withDensityáµ¥ f := by
  by_cases hf : Integrable f Î¼
  Â· ext1 i hi
    rw [withDensityáµ¥_apply (hf.smul r) hi, VectorMeasure.smul_apply, withDensityáµ¥_apply hf hi, â†
      integral_smul r f]
    simp only [Pi.smul_apply]
  Â· by_cases hr : r = 0
    Â· rw [hr, zero_smul, zero_smul, withDensityáµ¥_zero]
    Â· rw [withDensityáµ¥, withDensityáµ¥, dif_neg hf, dif_neg, smul_zero]
      rwa [integrable_smul_iff hr f]

theorem withDensityáµ¥_smul' {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]
    [SMulCommClass â„ ğ•œ E] (f : Î± â†’ E) (r : ğ•œ) :
    (Î¼.withDensityáµ¥ fun x â†¦ r â€¢ f x) = r â€¢ Î¼.withDensityáµ¥ f :=
  withDensityáµ¥_smul f r

theorem withDensityáµ¥_smul_eq_withDensityáµ¥_withDensity {f : Î± â†’ â„â‰¥0} {g : Î± â†’ E}
    (hf : AEMeasurable f Î¼) (hfg : Integrable (f â€¢ g) Î¼) :
    Î¼.withDensityáµ¥ (f â€¢ g) = (Î¼.withDensity (fun x â†¦ f x)).withDensityáµ¥ g := by
  ext s hs
  rw [withDensityáµ¥_apply hfg hs,
    withDensityáµ¥_apply ((integrable_withDensity_iff_integrable_smulâ‚€ hf).mpr hfg) hs,
    setIntegral_withDensity_eq_setIntegral_smulâ‚€ hf.restrict _ hs]
  simp only [Pi.smul_apply']

theorem withDensityáµ¥_smul_eq_withDensityáµ¥_withDensity' {f : Î± â†’ â„â‰¥0âˆ} {g : Î± â†’ E}
    (hf : AEMeasurable f Î¼) (hflt : âˆ€áµ x âˆ‚Î¼, f x < âˆ)
    (hfg : Integrable (fun x â†¦ (f x).toReal â€¢ g x) Î¼) :
    Î¼.withDensityáµ¥ (fun x â†¦ (f x).toReal â€¢ g x) = (Î¼.withDensity f).withDensityáµ¥ g := by
  rw [â† withDensity_congr_ae (coe_toNNReal_ae_eq hflt),
    â† withDensityáµ¥_smul_eq_withDensityáµ¥_withDensity hf.ennreal_toNNReal hfg]
  apply congr_arg
  ext
  simp [NNReal.smul_def, ENNReal.coe_toNNReal_eq_toReal]

theorem Measure.withDensityáµ¥_absolutelyContinuous (Î¼ : Measure Î±) (f : Î± â†’ â„) :
    Î¼.withDensityáµ¥ f â‰ªáµ¥ Î¼.toENNRealVectorMeasure := by
  by_cases hf : Integrable f Î¼
  Â· refine VectorMeasure.AbsolutelyContinuous.mk fun i hiâ‚ hiâ‚‚ â†¦ ?_
    rw [toENNRealVectorMeasure_apply_measurable hiâ‚] at hiâ‚‚
    rw [withDensityáµ¥_apply hf hiâ‚, Measure.restrict_zero_set hiâ‚‚, integral_zero_measure]
  Â· rw [withDensityáµ¥, dif_neg hf]
    exact VectorMeasure.AbsolutelyContinuous.zero _

/-- Having the same density implies the underlying functions are equal almost everywhere. -/
theorem Integrable.ae_eq_of_withDensityáµ¥_eq [CompleteSpace E] {f g : Î± â†’ E} (hf : Integrable f Î¼)
    (hg : Integrable g Î¼) (hfg : Î¼.withDensityáµ¥ f = Î¼.withDensityáµ¥ g) : f =áµ[Î¼] g := by
  refine hf.ae_eq_of_forall_setIntegral_eq f g hg fun i hi _ â†¦ ?_
  rw [â† withDensityáµ¥_apply hf hi, hfg, withDensityáµ¥_apply hg hi]

theorem WithDensityáµ¥Eq.congr_ae {f g : Î± â†’ E} (h : f =áµ[Î¼] g) :
    Î¼.withDensityáµ¥ f = Î¼.withDensityáµ¥ g := by
  by_cases hf : Integrable f Î¼
  Â· ext i hi
    rw [withDensityáµ¥_apply hf hi, withDensityáµ¥_apply (hf.congr h) hi]
    exact integral_congr_ae (ae_restrict_of_ae h)
  Â· have hg : Â¬Integrable g Î¼ := by intro hg; exact hf (hg.congr h.symm)
    rw [withDensityáµ¥, withDensityáµ¥, dif_neg hf, dif_neg hg]

theorem Integrable.withDensityáµ¥_eq_iff [CompleteSpace E]
    {f g : Î± â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    Î¼.withDensityáµ¥ f = Î¼.withDensityáµ¥ g â†” f =áµ[Î¼] g :=
  âŸ¨fun hfg â†¦ hf.ae_eq_of_withDensityáµ¥_eq hg hfg, fun h â†¦ WithDensityáµ¥Eq.congr_ae hâŸ©

section SignedMeasure

theorem withDensityáµ¥_toReal {f : Î± â†’ â„â‰¥0âˆ} (hfm : AEMeasurable f Î¼) (hf : (âˆ«â» x, f x âˆ‚Î¼) â‰  âˆ) :
    (Î¼.withDensityáµ¥ fun x â†¦ (f x).toReal) =
      @toSignedMeasure Î± _ (Î¼.withDensity f) (isFiniteMeasure_withDensity hf) := by
  have hfi := integrable_toReal_of_lintegral_ne_top hfm hf
  haveI := isFiniteMeasure_withDensity hf
  ext i hi
  rw [withDensityáµ¥_apply hfi hi, toSignedMeasure_apply_measurable hi, measureReal_def,
    withDensity_apply _ hi, integral_toReal hfm.restrict]
  refine ae_lt_top' hfm.restrict (ne_top_of_le_ne_top hf ?_)
  conv_rhs => rw [â† setLIntegral_univ]
  exact lintegral_mono_set (Set.subset_univ _)

theorem withDensityáµ¥_eq_withDensity_pos_part_sub_withDensity_neg_part {f : Î± â†’ â„}
    (hfi : Integrable f Î¼) :
    Î¼.withDensityáµ¥ f =
      @toSignedMeasure Î± _ (Î¼.withDensity fun x â†¦ ENNReal.ofReal <| f x)
          (isFiniteMeasure_withDensity_ofReal hfi.2) -
        @toSignedMeasure Î± _ (Î¼.withDensity fun x â†¦ ENNReal.ofReal <| -f x)
          (isFiniteMeasure_withDensity_ofReal hfi.neg.2) := by
  haveI := isFiniteMeasure_withDensity_ofReal hfi.2
  haveI := isFiniteMeasure_withDensity_ofReal hfi.neg.2
  ext i hi
  rw [withDensityáµ¥_apply hfi hi,
    integral_eq_lintegral_pos_part_sub_lintegral_neg_part hfi.integrableOn,
    VectorMeasure.sub_apply, toSignedMeasure_apply_measurable hi,
    toSignedMeasure_apply_measurable hi, measureReal_def, measureReal_def,
    withDensity_apply _ hi, withDensity_apply _ hi]

theorem Integrable.withDensityáµ¥_trim_eq_integral {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}
    (hm : m â‰¤ m0) {f : Î± â†’ â„} (hf : Integrable f Î¼) {i : Set Î±} (hi : MeasurableSet[m] i) :
    (Î¼.withDensityáµ¥ f).trim hm i = âˆ« x in i, f x âˆ‚Î¼ := by
  rw [VectorMeasure.trim_measurableSet_eq hm hi, withDensityáµ¥_apply hf (hm _ hi)]

theorem Integrable.withDensityáµ¥_trim_absolutelyContinuous {m m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}
    (hm : m â‰¤ m0) (hfi : Integrable f Î¼) :
    (Î¼.withDensityáµ¥ f).trim hm â‰ªáµ¥ (Î¼.trim hm).toENNRealVectorMeasure := by
  refine VectorMeasure.AbsolutelyContinuous.mk fun j hjâ‚ hjâ‚‚ â†¦ ?_
  rw [Measure.toENNRealVectorMeasure_apply_measurable hjâ‚, trim_measurableSet_eq hm hjâ‚] at hjâ‚‚
  rw [VectorMeasure.trim_measurableSet_eq hm hjâ‚, withDensityáµ¥_apply hfi (hm _ hjâ‚)]
  simp only [Measure.restrict_eq_zero.mpr hjâ‚‚, integral_zero_measure]

end SignedMeasure

end MeasureTheory
