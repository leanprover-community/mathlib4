/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
module

public import Mathlib.MeasureTheory.Measure.Real
public import Mathlib.MeasureTheory.Measure.Typeclasses.Finite
public import Mathlib.Topology.Algebra.InfiniteSum.Module

/-!

# Vector-valued measures

This file defines vector-valued measures, which are Ïƒ-additive functions from a set to an
additive monoid `M` such that it maps the empty set and non-measurable sets to zero. In the case
that `M = â„`, we called the vector measure a signed measure and write `SignedMeasure Î±`.
Similarly, when `M = â„‚`, we call the measure a complex measure and write `ComplexMeasure Î±`
(defined in `MeasureTheory/Measure/Complex`).

## Main definitions

* `MeasureTheory.VectorMeasure` is a vector-valued, Ïƒ-additive function that maps the empty
  and non-measurable sets to zero.
* `MeasureTheory.VectorMeasure.map` is the pushforward of a vector measure along a function.
* `MeasureTheory.VectorMeasure.restrict` is the restriction of a vector measure on some set.

## Notation

* `v â‰¤[i] w` means that the vector measure `v` restricted on the set `i` is less than or equal
  to the vector measure `w` restricted on `i`, i.e. `v.restrict i â‰¤ w.restrict i`.

## Implementation notes

We require all non-measurable sets to be mapped to zero in order for the extensionality lemma
to only compare the underlying functions for measurable sets.

We use `HasSum` instead of `tsum` in the definition of vector measures in comparison to `Measure`
since this provides summability.

## Tags

vector measure, signed measure, complex measure
-/

@[expose] public section


noncomputable section

open NNReal ENNReal Filter

open scoped Topology Function -- required for scoped `on` notation
namespace MeasureTheory

variable {Î± Î² : Type*} {m : MeasurableSpace Î±}

/-- A vector measure on a measurable space `Î±` is a Ïƒ-additive `M`-valued function (for some `M`
an additive monoid) such that the empty set and non-measurable sets are mapped to zero. -/
structure VectorMeasure (Î± : Type*) [MeasurableSpace Î±] (M : Type*) [AddCommMonoid M]
    [TopologicalSpace M] where
  /-- The measure of sets -/
  measureOf' : Set Î± â†’ M
  /-- The empty set has measure zero -/
  empty' : measureOf' âˆ… = 0
  /-- Non-measurable sets have measure zero -/
  not_measurable' â¦ƒi : Set Î±â¦„ : Â¬MeasurableSet i â†’ measureOf' i = 0
  /-- The measure is Ïƒ-additive -/
  m_iUnion' â¦ƒf : â„• â†’ Set Î±â¦„ : (âˆ€ i, MeasurableSet (f i)) â†’ Pairwise (Disjoint on f) â†’
    HasSum (fun i => measureOf' (f i)) (measureOf' (â‹ƒ i, f i))

/-- A `SignedMeasure` is an `â„`-vector measure. -/
abbrev SignedMeasure (Î± : Type*) [MeasurableSpace Î±] :=
  VectorMeasure Î± â„

open Set

namespace VectorMeasure

section

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]

attribute [coe] VectorMeasure.measureOf'

instance instCoeFun : CoeFun (VectorMeasure Î± M) fun _ => Set Î± â†’ M :=
  âŸ¨VectorMeasure.measureOf'âŸ©

initialize_simps_projections VectorMeasure (measureOf' â†’ apply)

@[simp]
theorem empty (v : VectorMeasure Î± M) : v âˆ… = 0 :=
  v.empty'

theorem not_measurable (v : VectorMeasure Î± M) {i : Set Î±} (hi : Â¬MeasurableSet i) : v i = 0 :=
  v.not_measurable' hi

theorem m_iUnion (v : VectorMeasure Î± M) {f : â„• â†’ Set Î±} (hfâ‚ : âˆ€ i, MeasurableSet (f i))
    (hfâ‚‚ : Pairwise (Disjoint on f)) : HasSum (fun i => v (f i)) (v (â‹ƒ i, f i)) :=
  v.m_iUnion' hfâ‚ hfâ‚‚

theorem coe_injective : @Function.Injective (VectorMeasure Î± M) (Set Î± â†’ M) (â‡‘) := fun v w h => by
  cases v
  cases w
  congr

theorem ext_iff' (v w : VectorMeasure Î± M) : v = w â†” âˆ€ i : Set Î±, v i = w i := by
  rw [â† coe_injective.eq_iff, funext_iff]

theorem ext_iff (v w : VectorMeasure Î± M) : v = w â†” âˆ€ i : Set Î±, MeasurableSet i â†’ v i = w i := by
  constructor
  Â· rintro rfl _ _
    rfl
  Â· rw [ext_iff']
    intro h i
    by_cases hi : MeasurableSet i
    Â· exact h i hi
    Â· simp_rw [not_measurable _ hi]

@[ext]
theorem ext {s t : VectorMeasure Î± M} (h : âˆ€ i : Set Î±, MeasurableSet i â†’ s i = t i) : s = t :=
  (ext_iff s t).2 h

variable [Countable Î²] {v : VectorMeasure Î± M} {f : Î² â†’ Set Î±}

theorem hasSum_of_disjoint_iUnion (hm : âˆ€ i, MeasurableSet (f i)) (hd : Pairwise (Disjoint on f)) :
    HasSum (fun i => v (f i)) (v (â‹ƒ i, f i)) := by
  rcases Countable.exists_injective_nat Î² with âŸ¨e, heâŸ©
  rw [â† hasSum_extend_zero he]
  convert m_iUnion v (f := Function.extend e f fun _ â†¦ âˆ…) _ _
  Â· simp only [Pi.zero_def, Function.apply_extend v, Function.comp_def, empty]
  Â· exact (iSup_extend_bot he _).symm
  Â· simp [Function.apply_extend MeasurableSet, Function.comp_def, hm]
  Â· exact hd.disjoint_extend_bot (he.factorsThrough _)

variable [T2Space M]

theorem of_disjoint_iUnion (hm : âˆ€ i, MeasurableSet (f i)) (hd : Pairwise (Disjoint on f)) :
    v (â‹ƒ i, f i) = âˆ‘' i, v (f i) :=
  (hasSum_of_disjoint_iUnion hm hd).tsum_eq.symm

theorem of_union {A B : Set Î±} (h : Disjoint A B) (hA : MeasurableSet A) (hB : MeasurableSet B) :
    v (A âˆª B) = v A + v B := by
  rw [Set.union_eq_iUnion, of_disjoint_iUnion, tsum_fintype, Fintype.sum_bool, cond, cond]
  exacts [fun b => Bool.casesOn b hB hA, pairwise_disjoint_on_bool.2 h]

theorem of_add_of_diff {A B : Set Î±} (hA : MeasurableSet A) (hB : MeasurableSet B) (h : A âŠ† B) :
    v A + v (B \ A) = v B := by
  rw [â† of_union (@Set.disjoint_sdiff_right _ A B) hA (hB.diff hA), Set.union_diff_cancel h]

theorem of_diff {M : Type*} [AddCommGroup M] [TopologicalSpace M] [T2Space M]
    {v : VectorMeasure Î± M} {A B : Set Î±} (hA : MeasurableSet A) (hB : MeasurableSet B)
    (h : A âŠ† B) : v (B \ A) = v B - v A := by
  rw [â† of_add_of_diff hA hB h, add_sub_cancel_left]

theorem of_compl {M : Type*} [AddCommGroup M] [TopologicalSpace M] [T2Space M]
    {v : VectorMeasure Î± M} {A : Set Î±} (hA : MeasurableSet A) :
    v Aá¶œ = v univ - v A := by
  simpa [compl_eq_univ_diff] using of_diff hA .univ (v := v) (subset_univ _)

theorem of_diff_of_diff_eq_zero {A B : Set Î±} (hA : MeasurableSet A) (hB : MeasurableSet B)
    (h' : v (B \ A) = 0) : v (A \ B) + v B = v A := by
  symm
  calc
    v A = v (A \ B âˆª A âˆ© B) := by simp only [Set.diff_union_inter]
    _ = v (A \ B) + v (A âˆ© B) := by
      rw [of_union]
      Â· rw [disjoint_comm]
        exact Set.disjoint_of_subset_left A.inter_subset_right disjoint_sdiff_self_right
      Â· exact hA.diff hB
      Â· exact hA.inter hB
    _ = v (A \ B) + v (A âˆ© B âˆª B \ A) := by
      rw [of_union, h', add_zero]
      Â· exact Set.disjoint_of_subset_left A.inter_subset_left disjoint_sdiff_self_right
      Â· exact hA.inter hB
      Â· exact hB.diff hA
    _ = v (A \ B) + v B := by rw [Set.union_comm, Set.inter_comm, Set.diff_union_inter]

theorem of_iUnion_nonneg {M : Type*} [TopologicalSpace M]
    [AddCommMonoid M] [PartialOrder M] [IsOrderedAddMonoid M]
    [OrderClosedTopology M] {v : VectorMeasure Î± M} (hfâ‚ : âˆ€ i, MeasurableSet (f i))
    (hfâ‚‚ : Pairwise (Disjoint on f)) (hfâ‚ƒ : âˆ€ i, 0 â‰¤ v (f i)) : 0 â‰¤ v (â‹ƒ i, f i) :=
  (v.of_disjoint_iUnion hfâ‚ hfâ‚‚).symm â–¸ tsum_nonneg hfâ‚ƒ

theorem of_iUnion_nonpos {M : Type*} [TopologicalSpace M]
    [AddCommMonoid M] [PartialOrder M] [IsOrderedAddMonoid M]
    [OrderClosedTopology M] {v : VectorMeasure Î± M} (hfâ‚ : âˆ€ i, MeasurableSet (f i))
    (hfâ‚‚ : Pairwise (Disjoint on f)) (hfâ‚ƒ : âˆ€ i, v (f i) â‰¤ 0) : v (â‹ƒ i, f i) â‰¤ 0 :=
  (v.of_disjoint_iUnion hfâ‚ hfâ‚‚).symm â–¸ tsum_nonpos hfâ‚ƒ

theorem of_nonneg_disjoint_union_eq_zero {s : SignedMeasure Î±} {A B : Set Î±} (h : Disjoint A B)
    (hAâ‚ : MeasurableSet A) (hBâ‚ : MeasurableSet B) (hAâ‚‚ : 0 â‰¤ s A) (hBâ‚‚ : 0 â‰¤ s B)
    (hAB : s (A âˆª B) = 0) : s A = 0 := by
  rw [of_union h hAâ‚ hBâ‚] at hAB
  linarith

theorem of_nonpos_disjoint_union_eq_zero {s : SignedMeasure Î±} {A B : Set Î±} (h : Disjoint A B)
    (hAâ‚ : MeasurableSet A) (hBâ‚ : MeasurableSet B) (hAâ‚‚ : s A â‰¤ 0) (hBâ‚‚ : s B â‰¤ 0)
    (hAB : s (A âˆª B) = 0) : s A = 0 := by
  rw [of_union h hAâ‚ hBâ‚] at hAB
  linarith

lemma of_biUnion_finset {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ Set Î±} (hd : PairwiseDisjoint (â†‘s) f)
    (hm : âˆ€ b âˆˆ s, MeasurableSet (f b)) : v (â‹ƒ b âˆˆ s, f b) = âˆ‘ p âˆˆ s, v (f p) := by
  classical
  induction s using Finset.induction with
  | empty => simp
  | insert a s has ih =>
    simp only [Finset.mem_insert, iUnion_iUnion_eq_or_left, has, not_false_eq_true,
      Finset.sum_insert]
    rw [of_union, ih]
    Â· exact hd.subset (by simp)
    Â· grind
    Â· simp only [disjoint_iUnion_right]
      exact fun i hi â†¦  hd (by simp) (by simp [hi]) (by grind)
    Â· apply hm _ (by simp)
    Â· apply Finset.measurableSet_biUnion _ (by grind)

theorem tendsto_vectorMeasure_iUnion_atTop_nat
    {s : â„• â†’ Set Î±} (hm : Monotone s) (hs : âˆ€ i, MeasurableSet (s i)) :
    Tendsto (fun n â†¦ v (s n)) atTop (ğ“ (v (â‹ƒ n, s n))) := by
  set t : â„• â†’ Set Î± := disjointed s
  have ht n : MeasurableSet (t n) := .disjointed (fun n â†¦ hs n) n
  have : HasSum (fun n â†¦ v (t n)) (v (â‹ƒ n, s n)) := by
    rw [â† iUnion_disjointed]
    apply m_iUnion _ ht (disjoint_disjointed _)
  convert (HasSum.tendsto_sum_nat this).comp (tendsto_add_atTop_nat 1) with n
  dsimp
  rw [â† of_biUnion_finset]
  Â· rw [biUnion_range_succ_disjointed, Monotone.partialSups_eq hm]
  Â· exact fun i hi j hj hij â†¦ disjoint_disjointed _ hij
  Â· exact fun b hb â†¦ ht _

theorem tendsto_vectorMeasure_iInter_atTop_nat
    {M : Type*} [AddCommGroup M] [TopologicalSpace M] [T2Space M] [ContinuousSub M]
    {v : VectorMeasure Î± M} {s : â„• â†’ Set Î±} (hm : Antitone s) (hs : âˆ€ i, MeasurableSet (s i)) :
    Tendsto (fun n â†¦ v (s n)) atTop (ğ“ (v (â‹‚ n, s n))) := by
  have I n : v (s n) = v univ - v (s n)á¶œ := by simp [of_compl (hs n)]
  have J : v (â‹‚ n, s n) = v univ - v (â‹ƒ n, (s n)á¶œ) := by
    rw [â† of_compl (MeasurableSet.iUnion (fun n â†¦ (hs n).compl))]
    simp
  simp_rw [I, J]
  apply tendsto_const_nhds.sub
  exact tendsto_vectorMeasure_iUnion_atTop_nat (fun i j hij â†¦ by simpa using hm hij)
    (fun i â†¦ (hs i).compl)

end

section SMul

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]
variable {R : Type*} [Semiring R] [DistribMulAction R M] [ContinuousConstSMul R M]

/-- Given a scalar `r` and a vector measure `v`, `smul r v` is the vector measure corresponding to
the set function `s : Set Î± => r â€¢ (v s)`. -/
def smul (r : R) (v : VectorMeasure Î± M) : VectorMeasure Î± M where
  measureOf' := r â€¢ â‡‘v
  empty' := by rw [Pi.smul_apply, empty, smul_zero]
  not_measurable' _ hi := by rw [Pi.smul_apply, v.not_measurable hi, smul_zero]
  m_iUnion' _ hfâ‚ hfâ‚‚ := by exact HasSum.const_smul _ (v.m_iUnion hfâ‚ hfâ‚‚)

instance instSMul : SMul R (VectorMeasure Î± M) :=
  âŸ¨smulâŸ©

@[simp]
theorem coe_smul (r : R) (v : VectorMeasure Î± M) : â‡‘(r â€¢ v) = r â€¢ â‡‘v := rfl

theorem smul_apply (r : R) (v : VectorMeasure Î± M) (i : Set Î±) : (r â€¢ v) i = r â€¢ v i := rfl

end SMul

section AddCommMonoid

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]

instance instZero : Zero (VectorMeasure Î± M) :=
  âŸ¨âŸ¨0, rfl, fun _ _ => rfl, fun _ _ _ => hasSum_zeroâŸ©âŸ©

instance instInhabited : Inhabited (VectorMeasure Î± M) :=
  âŸ¨0âŸ©

@[simp]
theorem coe_zero : â‡‘(0 : VectorMeasure Î± M) = 0 := rfl

theorem zero_apply (i : Set Î±) : (0 : VectorMeasure Î± M) i = 0 := rfl

variable [ContinuousAdd M]

/-- The sum of two vector measure is a vector measure. -/
def add (v w : VectorMeasure Î± M) : VectorMeasure Î± M where
  measureOf' := v + w
  empty' := by simp
  not_measurable' _ hi := by rw [Pi.add_apply, v.not_measurable hi, w.not_measurable hi, add_zero]
  m_iUnion' _ hfâ‚ hfâ‚‚ := HasSum.add (v.m_iUnion hfâ‚ hfâ‚‚) (w.m_iUnion hfâ‚ hfâ‚‚)

instance instAdd : Add (VectorMeasure Î± M) :=
  âŸ¨addâŸ©

@[simp]
theorem coe_add (v w : VectorMeasure Î± M) : â‡‘(v + w) = v + w := rfl

theorem add_apply (v w : VectorMeasure Î± M) (i : Set Î±) : (v + w) i = v i + w i := rfl

instance instAddCommMonoid : AddCommMonoid (VectorMeasure Î± M) :=
  Function.Injective.addCommMonoid _ coe_injective coe_zero coe_add fun _ _ => coe_smul _ _

/-- `(â‡‘)` is an `AddMonoidHom`. -/
@[simps]
def coeFnAddMonoidHom : VectorMeasure Î± M â†’+ Set Î± â†’ M where
  toFun := (â‡‘)
  map_zero' := coe_zero
  map_add' := coe_add

end AddCommMonoid

section AddCommGroup

variable {M : Type*} [AddCommGroup M] [TopologicalSpace M] [IsTopologicalAddGroup M]

/-- The negative of a vector measure is a vector measure. -/
def neg (v : VectorMeasure Î± M) : VectorMeasure Î± M where
  measureOf' := -v
  empty' := by simp
  not_measurable' _ hi := by rw [Pi.neg_apply, neg_eq_zero, v.not_measurable hi]
  m_iUnion' _ hfâ‚ hfâ‚‚ := HasSum.neg <| v.m_iUnion hfâ‚ hfâ‚‚

instance instNeg : Neg (VectorMeasure Î± M) :=
  âŸ¨negâŸ©

@[simp]
theorem coe_neg (v : VectorMeasure Î± M) : â‡‘(-v) = -v := rfl

theorem neg_apply (v : VectorMeasure Î± M) (i : Set Î±) : (-v) i = -v i := rfl

/-- The difference of two vector measure is a vector measure. -/
def sub (v w : VectorMeasure Î± M) : VectorMeasure Î± M where
  measureOf' := v - w
  empty' := by simp
  not_measurable' _ hi := by rw [Pi.sub_apply, v.not_measurable hi, w.not_measurable hi, sub_zero]
  m_iUnion' _ hfâ‚ hfâ‚‚ := HasSum.sub (v.m_iUnion hfâ‚ hfâ‚‚) (w.m_iUnion hfâ‚ hfâ‚‚)

instance instSub : Sub (VectorMeasure Î± M) :=
  âŸ¨subâŸ©

@[simp]
theorem coe_sub (v w : VectorMeasure Î± M) : â‡‘(v - w) = v - w := rfl

theorem sub_apply (v w : VectorMeasure Î± M) (i : Set Î±) : (v - w) i = v i - w i := rfl

instance instAddCommGroup : AddCommGroup (VectorMeasure Î± M) :=
  Function.Injective.addCommGroup _ coe_injective coe_zero coe_add coe_neg coe_sub
    (fun _ _ => coe_smul _ _) fun _ _ => coe_smul _ _

end AddCommGroup

section DistribMulAction

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]
variable {R : Type*} [Semiring R] [DistribMulAction R M] [ContinuousConstSMul R M]

instance instDistribMulAction [ContinuousAdd M] : DistribMulAction R (VectorMeasure Î± M) :=
  Function.Injective.distribMulAction coeFnAddMonoidHom coe_injective coe_smul

end DistribMulAction

section Module

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]
variable {R : Type*} [Semiring R] [Module R M] [ContinuousConstSMul R M]

instance instModule [ContinuousAdd M] : Module R (VectorMeasure Î± M) :=
  Function.Injective.module R coeFnAddMonoidHom coe_injective coe_smul

end Module

section Dirac

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] [MeasurableSpace Î²]
  {x : Î²} {v : M} {s : Set Î²}

open scoped Classical in
/-- The Dirac vector measure with mass `v` at a point `x`. It gives mass `v` to measurable sets
containing `x`, and `0` otherwise. -/
def dirac (x : Î²) (v : M) : VectorMeasure Î² M where
  measureOf' s := if MeasurableSet s âˆ§ x âˆˆ s then v else 0
  empty' := by simp
  not_measurable' := by simp +contextual
  m_iUnion' f f_meas f_disj := by
    by_cases hx : x âˆˆ â‹ƒ i, f i; swap
    Â· simp only [mem_iUnion, not_exists] at hx
      simp [hx, hasSum_zero]
    have : MeasurableSet (â‹ƒ i, f i) := by
      apply MeasurableSet.iUnion f_meas
    simp only [f_meas, true_and, MeasurableSet.iUnion f_meas, hx, and_self, â†“reduceIte]
    obtain âŸ¨j, hjâŸ© : âˆƒ j, x âˆˆ f j := by simpa using hx
    nth_rewrite 2 [show v = if x âˆˆ f j then v else 0 by simp [hj]]
    apply hasSum_single
    intro i hi
    have : Disjoint (f i) (f j) := f_disj hi
    grind

@[simp] lemma dirac_apply_of_mem (hs : MeasurableSet s) (hx : x âˆˆ s) : dirac x v s = v := by
  simp [dirac, hs, hx]

@[simp] lemma dirac_apply_of_notMem (hx : x âˆ‰ s) : dirac x v s = 0 := by
  simp [dirac, hx]

end Dirac

end VectorMeasure

namespace Measure

open Classical in
/-- A finite measure coerced into a real function is a signed measure. -/
@[simps]
def toSignedMeasure (Î¼ : Measure Î±) [hÎ¼ : IsFiniteMeasure Î¼] : SignedMeasure Î± where
  measureOf' := fun s : Set Î± => if MeasurableSet s then Î¼.real s else 0
  empty' := by simp
  not_measurable' _ hi := if_neg hi
  m_iUnion' f hfâ‚ hfâ‚‚ := by
    simp only [*, MeasurableSet.iUnion hfâ‚, if_true, measure_iUnion hfâ‚‚ hfâ‚, measureReal_def]
    rw [ENNReal.tsum_toReal_eq]
    exacts [(summable_measure_toReal hfâ‚ hfâ‚‚).hasSum, fun _ â†¦ measure_ne_top _ _]

theorem toSignedMeasure_apply_measurable {Î¼ : Measure Î±} [IsFiniteMeasure Î¼] {i : Set Î±}
    (hi : MeasurableSet i) : Î¼.toSignedMeasure i = Î¼.real i :=
  if_pos hi

-- Without this lemma, `singularPart_neg` in
-- `Mathlib/MeasureTheory/Measure/Decomposition/Lebesgue.lean` is extremely slow
theorem toSignedMeasure_congr {Î¼ Î½ : Measure Î±} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (h : Î¼ = Î½) : Î¼.toSignedMeasure = Î½.toSignedMeasure := by
  congr

theorem toSignedMeasure_eq_toSignedMeasure_iff {Î¼ Î½ : Measure Î±} [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] : Î¼.toSignedMeasure = Î½.toSignedMeasure â†” Î¼ = Î½ := by
  refine âŸ¨fun h => ?_, fun h => ?_âŸ©
  Â· ext1 i hi
    have : Î¼.toSignedMeasure i = Î½.toSignedMeasure i := by rw [h]
    rwa [toSignedMeasure_apply_measurable hi, toSignedMeasure_apply_measurable hi,
        measureReal_eq_measureReal_iff] at this
  Â· congr

@[simp]
theorem toSignedMeasure_zero : (0 : Measure Î±).toSignedMeasure = 0 := by
  ext i
  simp

@[simp]
theorem toSignedMeasure_add (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    (Î¼ + Î½).toSignedMeasure = Î¼.toSignedMeasure + Î½.toSignedMeasure := by
  ext i hi
  rw [toSignedMeasure_apply_measurable hi, measureReal_add_apply,
    VectorMeasure.add_apply, toSignedMeasure_apply_measurable hi,
    toSignedMeasure_apply_measurable hi]

@[simp]
theorem toSignedMeasure_smul (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (r : â„â‰¥0) :
    (r â€¢ Î¼).toSignedMeasure = r â€¢ Î¼.toSignedMeasure := by
  ext i hi
  rw [toSignedMeasure_apply_measurable hi, VectorMeasure.smul_apply,
    toSignedMeasure_apply_measurable hi, measureReal_nnreal_smul_apply]
  rfl

open Classical in
/-- A measure is a vector measure over `â„â‰¥0âˆ`. -/
@[simps]
def toENNRealVectorMeasure (Î¼ : Measure Î±) : VectorMeasure Î± â„â‰¥0âˆ where
  measureOf' := fun i : Set Î± => if MeasurableSet i then Î¼ i else 0
  empty' := by simp
  not_measurable' _ hi := if_neg hi
  m_iUnion' _ hfâ‚ hfâ‚‚ := by
    rw [Summable.hasSum_iff ENNReal.summable, if_pos (MeasurableSet.iUnion hfâ‚),
      MeasureTheory.measure_iUnion hfâ‚‚ hfâ‚]
    exact tsum_congr fun n => if_pos (hfâ‚ n)

theorem toENNRealVectorMeasure_apply_measurable {Î¼ : Measure Î±} {i : Set Î±} (hi : MeasurableSet i) :
    Î¼.toENNRealVectorMeasure i = Î¼ i :=
  if_pos hi

@[simp]
theorem toENNRealVectorMeasure_zero : (0 : Measure Î±).toENNRealVectorMeasure = 0 := by
  ext i
  simp

@[simp]
theorem toENNRealVectorMeasure_add (Î¼ Î½ : Measure Î±) :
    (Î¼ + Î½).toENNRealVectorMeasure = Î¼.toENNRealVectorMeasure + Î½.toENNRealVectorMeasure := by
  refine MeasureTheory.VectorMeasure.ext fun i hi => ?_
  rw [toENNRealVectorMeasure_apply_measurable hi, add_apply, VectorMeasure.add_apply,
    toENNRealVectorMeasure_apply_measurable hi, toENNRealVectorMeasure_apply_measurable hi]

theorem toSignedMeasure_sub_apply {Î¼ Î½ : Measure Î±} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    {i : Set Î±} (hi : MeasurableSet i) :
    (Î¼.toSignedMeasure - Î½.toSignedMeasure) i = Î¼.real i - Î½.real i := by
  rw [VectorMeasure.sub_apply, toSignedMeasure_apply_measurable hi,
    Measure.toSignedMeasure_apply_measurable hi]

end Measure

namespace VectorMeasure

open Measure

section

/-- A vector measure over `â„â‰¥0âˆ` is a measure. -/
def ennrealToMeasure {_ : MeasurableSpace Î±} (v : VectorMeasure Î± â„â‰¥0âˆ) : Measure Î± :=
  ofMeasurable (fun s _ => v s) v.empty fun _ hfâ‚ hfâ‚‚ => v.of_disjoint_iUnion hfâ‚ hfâ‚‚

theorem ennrealToMeasure_apply {m : MeasurableSpace Î±} {v : VectorMeasure Î± â„â‰¥0âˆ} {s : Set Î±}
    (hs : MeasurableSet s) : ennrealToMeasure v s = v s := by
  rw [ennrealToMeasure, ofMeasurable_apply _ hs]

@[simp]
theorem _root_.MeasureTheory.Measure.toENNRealVectorMeasure_ennrealToMeasure
    (Î¼ : VectorMeasure Î± â„â‰¥0âˆ) :
    toENNRealVectorMeasure (ennrealToMeasure Î¼) = Î¼ := ext fun s hs => by
  rw [toENNRealVectorMeasure_apply_measurable hs, ennrealToMeasure_apply hs]

@[simp]
theorem ennrealToMeasure_toENNRealVectorMeasure (Î¼ : Measure Î±) :
    ennrealToMeasure (toENNRealVectorMeasure Î¼) = Î¼ := Measure.ext fun s hs => by
  rw [ennrealToMeasure_apply hs, toENNRealVectorMeasure_apply_measurable hs]

/-- The equiv between `VectorMeasure Î± â„â‰¥0âˆ` and `Measure Î±` formed by
`MeasureTheory.VectorMeasure.ennrealToMeasure` and
`MeasureTheory.Measure.toENNRealVectorMeasure`. -/
@[simps]
def equivMeasure [MeasurableSpace Î±] : VectorMeasure Î± â„â‰¥0âˆ â‰ƒ Measure Î± where
  toFun := ennrealToMeasure
  invFun := toENNRealVectorMeasure
  left_inv := toENNRealVectorMeasure_ennrealToMeasure
  right_inv := ennrealToMeasure_toENNRealVectorMeasure

end

section

variable [MeasurableSpace Î±] [MeasurableSpace Î²]
variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]
variable (v : VectorMeasure Î± M)

open Classical in
/-- The pushforward of a vector measure along a function. -/
def map (v : VectorMeasure Î± M) (f : Î± â†’ Î²) : VectorMeasure Î² M :=
  if hf : Measurable f then
    { measureOf' := fun s => if MeasurableSet s then v (f â»Â¹' s) else 0
      empty' := by simp
      not_measurable' := fun _ hi => if_neg hi
      m_iUnion' := by
        intro g hgâ‚ hgâ‚‚
        convert v.m_iUnion (fun i => hf (hgâ‚ i)) fun i j hij => (hgâ‚‚ hij).preimage _
        Â· rw [if_pos (hgâ‚ _)]
        Â· rw [Set.preimage_iUnion, if_pos (MeasurableSet.iUnion hgâ‚)] }
  else 0

theorem map_not_measurable {f : Î± â†’ Î²} (hf : Â¬Measurable f) : v.map f = 0 :=
  dif_neg hf

theorem map_apply {f : Î± â†’ Î²} (hf : Measurable f) {s : Set Î²} (hs : MeasurableSet s) :
    v.map f s = v (f â»Â¹' s) := by
  rw [map, dif_pos hf]
  exact if_pos hs

@[simp]
theorem map_id : v.map id = v :=
  ext fun i hi => by rw [map_apply v measurable_id hi, Set.preimage_id]

@[simp]
theorem map_zero (f : Î± â†’ Î²) : (0 : VectorMeasure Î± M).map f = 0 := by
  by_cases hf : Measurable f
  Â· ext i hi
    rw [map_apply _ hf hi, zero_apply, zero_apply]
  Â· exact dif_neg hf

section

variable {N : Type*} [AddCommMonoid N] [TopologicalSpace N]

/-- Given a vector measure `v` on `M` and a continuous `AddMonoidHom` `f : M â†’ N`, `f âˆ˜ v` is a
vector measure on `N`. -/
def mapRange (v : VectorMeasure Î± M) (f : M â†’+ N) (hf : Continuous f) : VectorMeasure Î± N where
  measureOf' s := f (v s)
  empty' := by rw [empty, AddMonoidHom.map_zero]
  not_measurable' i hi := by rw [not_measurable v hi, AddMonoidHom.map_zero]
  m_iUnion' _ hgâ‚ hgâ‚‚ := HasSum.map (v.m_iUnion hgâ‚ hgâ‚‚) f hf

@[simp]
theorem mapRange_apply {f : M â†’+ N} (hf : Continuous f) {s : Set Î±} : v.mapRange f hf s = f (v s) :=
  rfl

@[simp]
theorem mapRange_id : v.mapRange (AddMonoidHom.id M) continuous_id = v := by
  ext
  rfl

@[simp]
theorem mapRange_zero {f : M â†’+ N} (hf : Continuous f) :
    mapRange (0 : VectorMeasure Î± M) f hf = 0 := by
  ext
  simp

section ContinuousAdd

variable [ContinuousAdd M] [ContinuousAdd N]

@[simp]
theorem mapRange_add {v w : VectorMeasure Î± M} {f : M â†’+ N} (hf : Continuous f) :
    (v + w).mapRange f hf = v.mapRange f hf + w.mapRange f hf := by
  ext
  simp

/-- Given a continuous `AddMonoidHom` `f : M â†’ N`, `mapRangeHom` is the `AddMonoidHom` mapping the
vector measure `v` on `M` to the vector measure `f âˆ˜ v` on `N`. -/
def mapRangeHom (f : M â†’+ N) (hf : Continuous f) : VectorMeasure Î± M â†’+ VectorMeasure Î± N where
  toFun v := v.mapRange f hf
  map_zero' := mapRange_zero hf
  map_add' _ _ := mapRange_add hf

end ContinuousAdd

section Module

variable {R : Type*} [Semiring R] [Module R M] [Module R N]
variable [ContinuousAdd M] [ContinuousAdd N] [ContinuousConstSMul R M] [ContinuousConstSMul R N]

/-- Given a continuous linear map `f : M â†’ N`, `mapRangeâ‚—` is the linear map mapping the
vector measure `v` on `M` to the vector measure `f âˆ˜ v` on `N`. -/
def mapRangeâ‚— (f : M â†’â‚—[R] N) (hf : Continuous f) : VectorMeasure Î± M â†’â‚—[R] VectorMeasure Î± N where
  toFun v := v.mapRange f.toAddMonoidHom hf
  map_add' _ _ := mapRange_add hf
  map_smul' := by
    intros
    ext
    simp

end Module

end

open Classical in
/-- The restriction of a vector measure on some set. -/
def restrict (v : VectorMeasure Î± M) (i : Set Î±) : VectorMeasure Î± M :=
  if hi : MeasurableSet i then
    { measureOf' := fun s => if MeasurableSet s then v (s âˆ© i) else 0
      empty' := by simp
      not_measurable' := fun _ hi => if_neg hi
      m_iUnion' := by
        intro f hfâ‚ hfâ‚‚
        convert v.m_iUnion (fun n => (hfâ‚ n).inter hi)
            (hfâ‚‚.mono fun i j => Disjoint.mono inf_le_left inf_le_left)
        Â· rw [if_pos (hfâ‚ _)]
        Â· rw [Set.iUnion_inter, if_pos (MeasurableSet.iUnion hfâ‚)] }
  else 0

theorem restrict_not_measurable {i : Set Î±} (hi : Â¬MeasurableSet i) : v.restrict i = 0 :=
  dif_neg hi

theorem restrict_apply {i : Set Î±} (hi : MeasurableSet i) {j : Set Î±} (hj : MeasurableSet j) :
    v.restrict i j = v (j âˆ© i) := by
  rw [restrict, dif_pos hi]
  exact if_pos hj

theorem restrict_eq_self {i : Set Î±} (hi : MeasurableSet i) {j : Set Î±} (hj : MeasurableSet j)
    (hij : j âŠ† i) : v.restrict i j = v j := by
  rw [restrict_apply v hi hj, Set.inter_eq_left.2 hij]

@[simp]
theorem restrict_empty : v.restrict âˆ… = 0 :=
  ext fun i hi => by
    rw [restrict_apply v MeasurableSet.empty hi, Set.inter_empty, v.empty, zero_apply]

@[simp]
theorem restrict_univ : v.restrict Set.univ = v :=
  ext fun i hi => by rw [restrict_apply v MeasurableSet.univ hi, Set.inter_univ]

@[simp]
theorem restrict_zero {i : Set Î±} : (0 : VectorMeasure Î± M).restrict i = 0 := by
  by_cases hi : MeasurableSet i
  Â· ext j hj
    rw [restrict_apply 0 hi hj, zero_apply, zero_apply]
  Â· exact dif_neg hi

section ContinuousAdd

variable [ContinuousAdd M]

theorem map_add (v w : VectorMeasure Î± M) (f : Î± â†’ Î²) : (v + w).map f = v.map f + w.map f := by
  by_cases hf : Measurable f
  Â· ext i hi
    simp [map_apply _ hf hi]
  Â· simp [map, dif_neg hf]

/-- `VectorMeasure.map` as an additive monoid homomorphism. -/
@[simps]
def mapGm (f : Î± â†’ Î²) : VectorMeasure Î± M â†’+ VectorMeasure Î² M where
  toFun v := v.map f
  map_zero' := map_zero f
  map_add' _ _ := map_add _ _ f

@[simp]
theorem restrict_add (v w : VectorMeasure Î± M) (i : Set Î±) :
    (v + w).restrict i = v.restrict i + w.restrict i := by
  by_cases hi : MeasurableSet i
  Â· ext j hj
    simp [restrict_apply _ hi hj]
  Â· simp [restrict_not_measurable _ hi]

/-- `VectorMeasure.restrict` as an additive monoid homomorphism. -/
@[simps]
def restrictGm (i : Set Î±) : VectorMeasure Î± M â†’+ VectorMeasure Î± M where
  toFun v := v.restrict i
  map_zero' := restrict_zero
  map_add' _ _ := restrict_add _ _ i

end ContinuousAdd

section Partition

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [T2Space M] [ContinuousAdd M]
variable (v : VectorMeasure Î± M) {i : Set Î±}

@[simp]
theorem restrict_add_restrict_compl (hi : MeasurableSet i) :
    v.restrict i + v.restrict iá¶œ = v := by
  ext A hA
  rw [add_apply, restrict_apply _ hi hA, restrict_apply _ hi.compl hA,
    â† of_union _ (hA.inter hi) (hA.inter hi.compl)]
  Â· simp
  Â· exact disjoint_compl_right.inter_right' A |>.inter_left' A

end Partition

section Sub

variable {M : Type*} [AddCommGroup M] [TopologicalSpace M] [IsTopologicalAddGroup M]

@[simp]
theorem restrict_neg (v : VectorMeasure Î± M) (i : Set Î±) :
    (-v).restrict i = -(v.restrict i) := by
  by_cases hi : MeasurableSet i
  Â· ext j hj; simp [restrict_apply _ hi hj]
  Â· simp [restrict_not_measurable _ hi]

@[simp]
theorem restrict_sub (v w : VectorMeasure Î± M) (i : Set Î±) :
    (v - w).restrict i = v.restrict i - w.restrict i := by
  simp [sub_eq_add_neg, restrict_add, restrict_neg]

end Sub

end

section

variable [MeasurableSpace Î²]
variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]
variable {R : Type*} [Semiring R] [DistribMulAction R M] [ContinuousConstSMul R M]

@[simp]
theorem map_smul {v : VectorMeasure Î± M} {f : Î± â†’ Î²} (c : R) : (c â€¢ v).map f = c â€¢ v.map f := by
  by_cases hf : Measurable f
  Â· ext i hi
    simp [map_apply _ hf hi]
  Â· simp only [map, dif_neg hf]
    -- `smul_zero` does not work since we do not require `ContinuousAdd`
    ext i
    simp

@[simp]
theorem restrict_smul {v : VectorMeasure Î± M} {i : Set Î±} (c : R) :
    (c â€¢ v).restrict i = c â€¢ v.restrict i := by
  by_cases hi : MeasurableSet i
  Â· ext j hj
    simp [restrict_apply _ hi hj]
  Â· simp only [restrict_not_measurable _ hi]
    -- `smul_zero` does not work since we do not require `ContinuousAdd`
    ext j
    simp

end

section

variable [MeasurableSpace Î²]
variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M]
variable {R : Type*} [Semiring R] [Module R M] [ContinuousConstSMul R M] [ContinuousAdd M]

/-- `VectorMeasure.map` as a linear map. -/
@[simps]
def mapâ‚— (f : Î± â†’ Î²) : VectorMeasure Î± M â†’â‚—[R] VectorMeasure Î² M where
  toFun v := v.map f
  map_add' _ _ := map_add _ _ f
  map_smul' _ _ := map_smul _

/-- `VectorMeasure.restrict` as an additive monoid homomorphism. -/
@[simps]
def restrictâ‚— (i : Set Î±) : VectorMeasure Î± M â†’â‚—[R] VectorMeasure Î± M where
  toFun v := v.restrict i
  map_add' _ _ := restrict_add _ _ i
  map_smul' _ _ := restrict_smul _

end

section

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [PartialOrder M]

/-- Vector measures over a partially ordered monoid is partially ordered.

This definition is consistent with `Measure.instPartialOrder`. -/
instance instPartialOrder : PartialOrder (VectorMeasure Î± M) where
  le v w := âˆ€ i, MeasurableSet i â†’ v i â‰¤ w i
  le_refl _ _ _ := le_rfl
  le_trans _ _ _ hâ‚ hâ‚‚ i hi := le_trans (hâ‚ i hi) (hâ‚‚ i hi)
  le_antisymm _ _ hâ‚ hâ‚‚ := ext fun i hi => le_antisymm (hâ‚ i hi) (hâ‚‚ i hi)

variable {v w : VectorMeasure Î± M}

theorem le_iff : v â‰¤ w â†” âˆ€ i, MeasurableSet i â†’ v i â‰¤ w i := Iff.rfl

theorem le_iff' : v â‰¤ w â†” âˆ€ i, v i â‰¤ w i := by
  refine âŸ¨fun h i => ?_, fun h i _ => h iâŸ©
  by_cases hi : MeasurableSet i
  Â· exact h i hi
  Â· rw [v.not_measurable hi, w.not_measurable hi]

end

/-- `v â‰¤[i] w` is notation for `v.restrict i â‰¤ w.restrict i`. -/
scoped[MeasureTheory]
  notation3:50 v " â‰¤[" i:50 "] " w:50 =>
    MeasureTheory.VectorMeasure.restrict v i â‰¤ MeasureTheory.VectorMeasure.restrict w i

section

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [PartialOrder M]
variable (v w : VectorMeasure Î± M)

theorem restrict_le_restrict_iff {i : Set Î±} (hi : MeasurableSet i) :
    v â‰¤[i] w â†” âˆ€ â¦ƒjâ¦„, MeasurableSet j â†’ j âŠ† i â†’ v j â‰¤ w j :=
  âŸ¨fun h j hjâ‚ hjâ‚‚ => restrict_eq_self v hi hjâ‚ hjâ‚‚ â–¸ restrict_eq_self w hi hjâ‚ hjâ‚‚ â–¸ h j hjâ‚,
    fun h => le_iff.1 fun _ hj =>
      (restrict_apply v hi hj).symm â–¸ (restrict_apply w hi hj).symm â–¸
      h (hj.inter hi) Set.inter_subset_rightâŸ©

theorem subset_le_of_restrict_le_restrict {i : Set Î±} (hi : MeasurableSet i) (hiâ‚‚ : v â‰¤[i] w)
    {j : Set Î±} (hj : j âŠ† i) : v j â‰¤ w j := by
  by_cases hjâ‚ : MeasurableSet j
  Â· exact (restrict_le_restrict_iff _ _ hi).1 hiâ‚‚ hjâ‚ hj
  Â· rw [v.not_measurable hjâ‚, w.not_measurable hjâ‚]

theorem restrict_le_restrict_of_subset_le {i : Set Î±}
    (h : âˆ€ â¦ƒjâ¦„, MeasurableSet j â†’ j âŠ† i â†’ v j â‰¤ w j) : v â‰¤[i] w := by
  by_cases hi : MeasurableSet i
  Â· exact (restrict_le_restrict_iff _ _ hi).2 h
  Â· rw [restrict_not_measurable v hi, restrict_not_measurable w hi]

theorem restrict_le_restrict_subset {i j : Set Î±} (hiâ‚ : MeasurableSet i) (hiâ‚‚ : v â‰¤[i] w)
    (hij : j âŠ† i) : v â‰¤[j] w :=
  restrict_le_restrict_of_subset_le v w fun _ _ hkâ‚‚ =>
    subset_le_of_restrict_le_restrict v w hiâ‚ hiâ‚‚ (Set.Subset.trans hkâ‚‚ hij)

theorem le_restrict_empty : v â‰¤[âˆ…] w := by
  simp

theorem le_restrict_univ_iff_le : v â‰¤[Set.univ] w â†” v â‰¤ w := by
  simp

end

section

variable {M : Type*} [TopologicalSpace M]
  [AddCommGroup M] [PartialOrder M] [IsOrderedAddMonoid M] [IsTopologicalAddGroup M]
variable (v w : VectorMeasure Î± M)

nonrec theorem neg_le_neg {i : Set Î±} (hi : MeasurableSet i) (h : v â‰¤[i] w) : -w â‰¤[i] -v := by
  intro j hjâ‚
  rw [restrict_apply _ hi hjâ‚, restrict_apply _ hi hjâ‚, neg_apply, neg_apply]
  refine neg_le_neg ?_
  rw [â† restrict_apply _ hi hjâ‚, â† restrict_apply _ hi hjâ‚]
  exact h j hjâ‚

theorem neg_le_neg_iff {i : Set Î±} (hi : MeasurableSet i) : -w â‰¤[i] -v â†” v â‰¤[i] w :=
  âŸ¨fun h => neg_neg v â–¸ neg_neg w â–¸ neg_le_neg _ _ hi h, fun h => neg_le_neg _ _ hi hâŸ©

end

section

variable {M : Type*} [TopologicalSpace M]
  [AddCommMonoid M] [PartialOrder M] [IsOrderedAddMonoid M] [OrderClosedTopology M]
variable (v w : VectorMeasure Î± M) {i j : Set Î±}

theorem restrict_le_restrict_iUnion {f : â„• â†’ Set Î±} (hfâ‚ : âˆ€ n, MeasurableSet (f n))
    (hfâ‚‚ : âˆ€ n, v â‰¤[f n] w) : v â‰¤[â‹ƒ n, f n] w := by
  refine restrict_le_restrict_of_subset_le v w fun a haâ‚ haâ‚‚ => ?_
  have haâ‚ƒ : â‹ƒ n, a âˆ© disjointed f n = a := by
    rwa [â† Set.inter_iUnion, iUnion_disjointed, Set.inter_eq_left]
  have haâ‚„ : Pairwise (Disjoint on fun n => a âˆ© disjointed f n) :=
    (disjoint_disjointed _).mono fun i j => Disjoint.mono inf_le_right inf_le_right
  rw [â† haâ‚ƒ, v.of_disjoint_iUnion _ haâ‚„, w.of_disjoint_iUnion _ haâ‚„]
  Â· refine Summable.tsum_le_tsum (fun n => (restrict_le_restrict_iff v w (hfâ‚ n)).1 (hfâ‚‚ n) ?_ ?_)
      ?_ ?_
    Â· exact haâ‚.inter (MeasurableSet.disjointed hfâ‚ n)
    Â· exact Set.Subset.trans Set.inter_subset_right (disjointed_subset _ _)
    Â· refine (v.m_iUnion (fun n => ?_) ?_).summable
      Â· exact haâ‚.inter (MeasurableSet.disjointed hfâ‚ n)
      Â· exact (disjoint_disjointed _).mono fun i j => Disjoint.mono inf_le_right inf_le_right
    Â· refine (w.m_iUnion (fun n => ?_) ?_).summable
      Â· exact haâ‚.inter (MeasurableSet.disjointed hfâ‚ n)
      Â· exact (disjoint_disjointed _).mono fun i j => Disjoint.mono inf_le_right inf_le_right
  Â· intro n
    exact haâ‚.inter (MeasurableSet.disjointed hfâ‚ n)
  Â· exact fun n => haâ‚.inter (MeasurableSet.disjointed hfâ‚ n)

theorem restrict_le_restrict_countable_iUnion [Countable Î²] {f : Î² â†’ Set Î±}
    (hfâ‚ : âˆ€ b, MeasurableSet (f b)) (hfâ‚‚ : âˆ€ b, v â‰¤[f b] w) : v â‰¤[â‹ƒ b, f b] w := by
  cases nonempty_encodable Î²
  rw [â† Encodable.iUnion_decodeâ‚‚]
  refine restrict_le_restrict_iUnion v w ?_ ?_
  Â· intro n
    measurability
  Â· intro n
    rcases Encodable.decodeâ‚‚ Î² n with - | b
    Â· simp
    Â· simp [hfâ‚‚ b]

theorem restrict_le_restrict_union (hiâ‚ : MeasurableSet i) (hiâ‚‚ : v â‰¤[i] w) (hjâ‚ : MeasurableSet j)
    (hjâ‚‚ : v â‰¤[j] w) : v â‰¤[i âˆª j] w := by
  rw [Set.union_eq_iUnion]
  refine restrict_le_restrict_countable_iUnion v w ?_ ?_
  Â· measurability
  Â· rintro (_ | _) <;> simpa

end

section

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [PartialOrder M]
variable (v w : VectorMeasure Î± M) {i j : Set Î±}

theorem nonneg_of_zero_le_restrict (hiâ‚‚ : 0 â‰¤[i] v) : 0 â‰¤ v i := by
  by_cases hiâ‚ : MeasurableSet i
  Â· exact (restrict_le_restrict_iff _ _ hiâ‚).1 hiâ‚‚ hiâ‚ Set.Subset.rfl
  Â· rw [v.not_measurable hiâ‚]

theorem nonpos_of_restrict_le_zero (hiâ‚‚ : v â‰¤[i] 0) : v i â‰¤ 0 := by
  by_cases hiâ‚ : MeasurableSet i
  Â· exact (restrict_le_restrict_iff _ _ hiâ‚).1 hiâ‚‚ hiâ‚ Set.Subset.rfl
  Â· rw [v.not_measurable hiâ‚]

theorem zero_le_restrict_not_measurable (hi : Â¬MeasurableSet i) : 0 â‰¤[i] v := by
  rw [restrict_zero, restrict_not_measurable _ hi]

theorem restrict_le_zero_of_not_measurable (hi : Â¬MeasurableSet i) : v â‰¤[i] 0 := by
  rw [restrict_zero, restrict_not_measurable _ hi]

theorem measurable_of_not_zero_le_restrict (hi : Â¬0 â‰¤[i] v) : MeasurableSet i :=
  Not.imp_symm (zero_le_restrict_not_measurable _) hi

theorem measurable_of_not_restrict_le_zero (hi : Â¬v â‰¤[i] 0) : MeasurableSet i :=
  Not.imp_symm (restrict_le_zero_of_not_measurable _) hi

theorem zero_le_restrict_subset (hiâ‚ : MeasurableSet i) (hij : j âŠ† i) (hiâ‚‚ : 0 â‰¤[i] v) : 0 â‰¤[j] v :=
  restrict_le_restrict_of_subset_le _ _ fun _ hkâ‚ hkâ‚‚ =>
    (restrict_le_restrict_iff _ _ hiâ‚).1 hiâ‚‚ hkâ‚ (Set.Subset.trans hkâ‚‚ hij)

theorem restrict_le_zero_subset (hiâ‚ : MeasurableSet i) (hij : j âŠ† i) (hiâ‚‚ : v â‰¤[i] 0) : v â‰¤[j] 0 :=
  restrict_le_restrict_of_subset_le _ _ fun _ hkâ‚ hkâ‚‚ =>
    (restrict_le_restrict_iff _ _ hiâ‚).1 hiâ‚‚ hkâ‚ (Set.Subset.trans hkâ‚‚ hij)

end

section

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [LinearOrder M]
variable (v w : VectorMeasure Î± M) {i j : Set Î±}

theorem exists_pos_measure_of_not_restrict_le_zero (hi : Â¬v â‰¤[i] 0) :
    âˆƒ j : Set Î±, MeasurableSet j âˆ§ j âŠ† i âˆ§ 0 < v j := by
  have hiâ‚ : MeasurableSet i := measurable_of_not_restrict_le_zero _ hi
  rw [restrict_le_restrict_iff _ _ hiâ‚] at hi
  push_neg at hi
  exact hi

end

section

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [PartialOrder M]
  [AddLeftMono M] [ContinuousAdd M]

instance instAddLeftMono : AddLeftMono (VectorMeasure Î± M) :=
  âŸ¨fun _ _ _ h i hi => by dsimp; grw [h i hi]âŸ©

end

section

variable {L M N : Type*}
variable [AddCommMonoid L] [TopologicalSpace L] [AddCommMonoid M] [TopologicalSpace M]
  [AddCommMonoid N] [TopologicalSpace N]

/-- A vector measure `v` is absolutely continuous with respect to a measure `Î¼` if for all sets
`s`, `Î¼ s = 0`, we have `v s = 0`. -/
def AbsolutelyContinuous (v : VectorMeasure Î± M) (w : VectorMeasure Î± N) :=
  âˆ€ â¦ƒs : Set Î±â¦„, w s = 0 â†’ v s = 0

@[inherit_doc VectorMeasure.AbsolutelyContinuous]
scoped[MeasureTheory] infixl:50 " â‰ªáµ¥ " => MeasureTheory.VectorMeasure.AbsolutelyContinuous

open MeasureTheory

namespace AbsolutelyContinuous

variable {v : VectorMeasure Î± M} {w : VectorMeasure Î± N}

theorem mk (h : âˆ€ â¦ƒs : Set Î±â¦„, MeasurableSet s â†’ w s = 0 â†’ v s = 0) : v â‰ªáµ¥ w := by
  intro s hs
  by_cases hmeas : MeasurableSet s
  Â· exact h hmeas hs
  Â· exact not_measurable v hmeas

theorem eq {w : VectorMeasure Î± M} (h : v = w) : v â‰ªáµ¥ w :=
  fun _ hs => h.symm â–¸ hs

@[refl]
theorem refl (v : VectorMeasure Î± M) : v â‰ªáµ¥ v :=
  eq rfl

@[trans]
theorem trans {u : VectorMeasure Î± L} {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} (huv : u â‰ªáµ¥ v)
    (hvw : v â‰ªáµ¥ w) : u â‰ªáµ¥ w :=
  fun _ hs => huv <| hvw hs

theorem zero (v : VectorMeasure Î± N) : (0 : VectorMeasure Î± M) â‰ªáµ¥ v :=
  fun s _ => VectorMeasure.zero_apply s

theorem neg_left {M : Type*} [AddCommGroup M] [TopologicalSpace M] [IsTopologicalAddGroup M]
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} (h : v â‰ªáµ¥ w) : -v â‰ªáµ¥ w := by
  intro s hs
  rw [neg_apply, h hs, neg_zero]

theorem neg_right {N : Type*} [AddCommGroup N] [TopologicalSpace N] [IsTopologicalAddGroup N]
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} (h : v â‰ªáµ¥ w) : v â‰ªáµ¥ -w := by
  intro s hs
  rw [neg_apply, neg_eq_zero] at hs
  exact h hs

theorem add [ContinuousAdd M] {vâ‚ vâ‚‚ : VectorMeasure Î± M} {w : VectorMeasure Î± N} (hvâ‚ : vâ‚ â‰ªáµ¥ w)
    (hvâ‚‚ : vâ‚‚ â‰ªáµ¥ w) : vâ‚ + vâ‚‚ â‰ªáµ¥ w := by
  intro s hs
  rw [add_apply, hvâ‚ hs, hvâ‚‚ hs, zero_add]

theorem sub {M : Type*} [AddCommGroup M] [TopologicalSpace M] [IsTopologicalAddGroup M]
    {vâ‚ vâ‚‚ : VectorMeasure Î± M} {w : VectorMeasure Î± N} (hvâ‚ : vâ‚ â‰ªáµ¥ w) (hvâ‚‚ : vâ‚‚ â‰ªáµ¥ w) :
    vâ‚ - vâ‚‚ â‰ªáµ¥ w := by
  intro s hs
  rw [sub_apply, hvâ‚ hs, hvâ‚‚ hs, zero_sub, neg_zero]

theorem smul {R : Type*} [Semiring R] [DistribMulAction R M] [ContinuousConstSMul R M] {r : R}
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} (h : v â‰ªáµ¥ w) : r â€¢ v â‰ªáµ¥ w := by
  intro s hs
  rw [smul_apply, h hs, smul_zero]

theorem map [MeasureSpace Î²] (h : v â‰ªáµ¥ w) (f : Î± â†’ Î²) : v.map f â‰ªáµ¥ w.map f := by
  by_cases hf : Measurable f
  Â· refine mk fun s hs hws => ?_
    rw [map_apply _ hf hs] at hws âŠ¢
    exact h hws
  Â· intro s _
    rw [map_not_measurable v hf, zero_apply]

theorem ennrealToMeasure {Î¼ : VectorMeasure Î± â„â‰¥0âˆ} :
    (âˆ€ â¦ƒs : Set Î±â¦„, Î¼.ennrealToMeasure s = 0 â†’ v s = 0) â†” v â‰ªáµ¥ Î¼ := by
  constructor <;> intro h
  Â· refine mk fun s hmeas hs => h ?_
    rw [â† hs, ennrealToMeasure_apply hmeas]
  Â· intro s hs
    by_cases hmeas : MeasurableSet s
    Â· rw [ennrealToMeasure_apply hmeas] at hs
      exact h hs
    Â· exact not_measurable v hmeas

end AbsolutelyContinuous

/-- Two vector measures `v` and `w` are said to be mutually singular if there exists a measurable
set `s`, such that for all `t âŠ† s`, `v t = 0` and for all `t âŠ† sá¶œ`, `w t = 0`.

We note that we do not require the measurability of `t` in the definition since this makes it easier
to use. This is equivalent to the definition which requires measurability. To prove
`MutuallySingular` with the measurability condition, use
`MeasureTheory.VectorMeasure.MutuallySingular.mk`. -/
def MutuallySingular (v : VectorMeasure Î± M) (w : VectorMeasure Î± N) : Prop :=
  âˆƒ s : Set Î±, MeasurableSet s âˆ§ (âˆ€ t âŠ† s, v t = 0) âˆ§ âˆ€ t âŠ† sá¶œ, w t = 0

@[inherit_doc VectorMeasure.MutuallySingular]
scoped[MeasureTheory] infixl:60 " âŸ‚áµ¥ " => MeasureTheory.VectorMeasure.MutuallySingular

namespace MutuallySingular

variable {v vâ‚ vâ‚‚ : VectorMeasure Î± M} {w wâ‚ wâ‚‚ : VectorMeasure Î± N}

theorem mk (s : Set Î±) (hs : MeasurableSet s) (hâ‚ : âˆ€ t âŠ† s, MeasurableSet t â†’ v t = 0)
    (hâ‚‚ : âˆ€ t âŠ† sá¶œ, MeasurableSet t â†’ w t = 0) : v âŸ‚áµ¥ w := by
  refine âŸ¨s, hs, fun t hst => ?_, fun t hst => ?_âŸ© <;> by_cases ht : MeasurableSet t
  Â· exact hâ‚ t hst ht
  Â· exact not_measurable v ht
  Â· exact hâ‚‚ t hst ht
  Â· exact not_measurable w ht

theorem symm (h : v âŸ‚áµ¥ w) : w âŸ‚áµ¥ v :=
  let âŸ¨s, hmeas, hsâ‚, hsâ‚‚âŸ© := h
  âŸ¨sá¶œ, hmeas.compl, hsâ‚‚, fun t ht => hsâ‚ _ (compl_compl s â–¸ ht : t âŠ† s)âŸ©

theorem zero_right : v âŸ‚áµ¥ (0 : VectorMeasure Î± N) :=
  âŸ¨âˆ…, MeasurableSet.empty, fun _ ht => (Set.subset_empty_iff.1 ht).symm â–¸ v.empty,
    fun _ _ => zero_apply _âŸ©

theorem zero_left : (0 : VectorMeasure Î± M) âŸ‚áµ¥ w :=
  zero_right.symm

theorem add_left [T2Space N] [ContinuousAdd M] (hâ‚ : vâ‚ âŸ‚áµ¥ w) (hâ‚‚ : vâ‚‚ âŸ‚áµ¥ w) : vâ‚ + vâ‚‚ âŸ‚áµ¥ w := by
  obtain âŸ¨u, hmu, huâ‚, huâ‚‚âŸ© := hâ‚
  obtain âŸ¨v, hmv, hvâ‚, hvâ‚‚âŸ© := hâ‚‚
  refine mk (u âˆ© v) (hmu.inter hmv) (fun t ht _ => ?_) fun t ht hmt => ?_
  Â· rw [add_apply, huâ‚ _ (Set.subset_inter_iff.1 ht).1, hvâ‚ _ (Set.subset_inter_iff.1 ht).2,
      zero_add]
  Â· rw [Set.compl_inter] at ht
    rw [(_ : t = uá¶œ âˆ© t âˆª vá¶œ \ uá¶œ âˆ© t),
      of_union _ (hmu.compl.inter hmt) ((hmv.compl.diff hmu.compl).inter hmt), huâ‚‚, hvâ‚‚, add_zero]
    Â· exact Set.Subset.trans Set.inter_subset_left diff_subset
    Â· exact Set.inter_subset_left
    Â· exact disjoint_sdiff_self_right.mono Set.inter_subset_left Set.inter_subset_left
    Â· apply Set.Subset.antisymm <;> intro x hx
      Â· by_cases hxu' : x âˆˆ uá¶œ
        Â· exact Or.inl âŸ¨hxu', hxâŸ©
        rcases ht hx with (hxu | hxv)
        exacts [False.elim (hxu' hxu), Or.inr âŸ¨âŸ¨hxv, hxu'âŸ©, hxâŸ©]
      Â· rcases hx with hx | hx <;> exact hx.2

theorem add_right [T2Space M] [ContinuousAdd N] (hâ‚ : v âŸ‚áµ¥ wâ‚) (hâ‚‚ : v âŸ‚áµ¥ wâ‚‚) : v âŸ‚áµ¥ wâ‚ + wâ‚‚ :=
  (add_left hâ‚.symm hâ‚‚.symm).symm

theorem smul_right {R : Type*} [Semiring R] [DistribMulAction R N] [ContinuousConstSMul R N]
    (r : R) (h : v âŸ‚áµ¥ w) : v âŸ‚áµ¥ r â€¢ w :=
  let âŸ¨s, hmeas, hsâ‚, hsâ‚‚âŸ© := h
  âŸ¨s, hmeas, hsâ‚, fun t ht => by simp only [coe_smul, Pi.smul_apply, hsâ‚‚ t ht, smul_zero]âŸ©

theorem smul_left {R : Type*} [Semiring R] [DistribMulAction R M] [ContinuousConstSMul R M] (r : R)
    (h : v âŸ‚áµ¥ w) : r â€¢ v âŸ‚áµ¥ w :=
  (smul_right r h.symm).symm

theorem neg_left {M : Type*} [AddCommGroup M] [TopologicalSpace M] [IsTopologicalAddGroup M]
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} (h : v âŸ‚áµ¥ w) : -v âŸ‚áµ¥ w := by
  obtain âŸ¨u, hmu, huâ‚, huâ‚‚âŸ© := h
  refine âŸ¨u, hmu, fun s hs => ?_, huâ‚‚âŸ©
  rw [neg_apply v s, neg_eq_zero]
  exact huâ‚ s hs

theorem neg_right {N : Type*} [AddCommGroup N] [TopologicalSpace N] [IsTopologicalAddGroup N]
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} (h : v âŸ‚áµ¥ w) : v âŸ‚áµ¥ -w :=
  h.symm.neg_left.symm

@[simp]
theorem neg_left_iff {M : Type*} [AddCommGroup M] [TopologicalSpace M] [IsTopologicalAddGroup M]
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} : -v âŸ‚áµ¥ w â†” v âŸ‚áµ¥ w :=
  âŸ¨fun h => neg_neg v â–¸ h.neg_left, neg_leftâŸ©

@[simp]
theorem neg_right_iff {N : Type*} [AddCommGroup N] [TopologicalSpace N] [IsTopologicalAddGroup N]
    {v : VectorMeasure Î± M} {w : VectorMeasure Î± N} : v âŸ‚áµ¥ -w â†” v âŸ‚áµ¥ w :=
  âŸ¨fun h => neg_neg w â–¸ h.neg_right, neg_rightâŸ©

end MutuallySingular

section Trim

open Classical in
/-- Restriction of a vector measure onto a sub-Ïƒ-algebra. -/
@[simps]
def trim {m n : MeasurableSpace Î±} (v : VectorMeasure Î± M) (hle : m â‰¤ n) :
    @VectorMeasure Î± m M _ _ :=
  @VectorMeasure.mk Î± m M _ _
    (fun i => if MeasurableSet[m] i then v i else 0)
    (by dsimp only; rw [if_pos (@MeasurableSet.empty _ m), v.empty])
    (fun i hi => by dsimp only; rw [if_neg hi])
    (fun f hfâ‚ hfâ‚‚ => by
      dsimp only
      have hfâ‚' : âˆ€ k, MeasurableSet[n] (f k) := fun k => hle _ (hfâ‚ k)
      convert v.m_iUnion hfâ‚' hfâ‚‚ using 1
      Â· ext n
        rw [if_pos (hfâ‚ n)]
      Â· rw [if_pos (@MeasurableSet.iUnion _ _ m _ _ hfâ‚)])

variable {n : MeasurableSpace Î±} {v : VectorMeasure Î± M}

theorem trim_eq_self : v.trim le_rfl = v := by
  ext i hi
  exact if_pos hi

@[simp]
theorem zero_trim (hle : m â‰¤ n) : (0 : VectorMeasure Î± M).trim hle = 0 := by
  ext i hi
  exact if_pos hi

theorem trim_measurableSet_eq (hle : m â‰¤ n) {i : Set Î±} (hi : MeasurableSet[m] i) :
    v.trim hle i = v i :=
  if_pos hi

theorem restrict_trim (hle : m â‰¤ n) {i : Set Î±} (hi : MeasurableSet[m] i) :
    @VectorMeasure.restrict Î± m M _ _ (v.trim hle) i = (v.restrict i).trim hle := by
  ext j hj
  rw [@restrict_apply _ m, trim_measurableSet_eq hle hj, restrict_apply, trim_measurableSet_eq]
  all_goals measurability

end Trim

end

end VectorMeasure

namespace SignedMeasure

open VectorMeasure

open MeasureTheory

/-- The underlying function for `SignedMeasure.toMeasureOfZeroLE`. -/
def toMeasureOfZeroLE' (s : SignedMeasure Î±) (i : Set Î±) (hi : 0 â‰¤[i] s) (j : Set Î±)
    (hj : MeasurableSet j) : â„â‰¥0âˆ :=
  ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) âŸ¨s.restrict i j, le_trans (by simp) (hi j hj)âŸ©

/-- Given a signed measure `s` and a positive measurable set `i`, `toMeasureOfZeroLE`
provides the measure, mapping measurable sets `j` to `s (i âˆ© j)`. -/
def toMeasureOfZeroLE (s : SignedMeasure Î±) (i : Set Î±) (hiâ‚ : MeasurableSet i) (hiâ‚‚ : 0 â‰¤[i] s) :
    Measure Î± := by
  refine Measure.ofMeasurable (s.toMeasureOfZeroLE' i hiâ‚‚) ?_ ?_
  Â· simp_rw [toMeasureOfZeroLE', s.restrict_apply hiâ‚ MeasurableSet.empty, Set.empty_inter i,
      s.empty]
    rfl
  Â· intro f hfâ‚ hfâ‚‚
    have hâ‚ : âˆ€ n, MeasurableSet (i âˆ© f n) := fun n => hiâ‚.inter (hfâ‚ n)
    have hâ‚‚ : Pairwise (Disjoint on fun n : â„• => i âˆ© f n) := by
      intro n m hnm
      exact ((hfâ‚‚ hnm).inf_left' i).inf_right' i
    simp only [toMeasureOfZeroLE', s.restrict_apply hiâ‚ (MeasurableSet.iUnion hfâ‚), Set.inter_comm,
      Set.inter_iUnion, s.of_disjoint_iUnion hâ‚ hâ‚‚]
    have h : âˆ€ n, 0 â‰¤ s (i âˆ© f n) := fun n =>
      s.nonneg_of_zero_le_restrict (s.zero_le_restrict_subset hiâ‚ Set.inter_subset_left hiâ‚‚)
    rw [NNReal.coe_tsum_of_nonneg h, ENNReal.coe_tsum]
    Â· refine tsum_congr fun n => ?_
      simp_rw [s.restrict_apply hiâ‚ (hfâ‚ n), Set.inter_comm]
    Â· exact (NNReal.summable_mk h).2 (s.m_iUnion hâ‚ hâ‚‚).summable

variable (s : SignedMeasure Î±) {i j : Set Î±}

theorem toMeasureOfZeroLE_apply (hi : 0 â‰¤[i] s) (hiâ‚ : MeasurableSet i) (hjâ‚ : MeasurableSet j) :
    s.toMeasureOfZeroLE i hiâ‚ hi j = ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) âŸ¨s (i âˆ© j), nonneg_of_zero_le_restrict
      s (zero_le_restrict_subset s hiâ‚ Set.inter_subset_left hi)âŸ© := by
  simp_rw [toMeasureOfZeroLE, Measure.ofMeasurable_apply _ hjâ‚, toMeasureOfZeroLE',
    s.restrict_apply hiâ‚ hjâ‚, Set.inter_comm]

theorem toMeasureOfZeroLE_real_apply (hi : 0 â‰¤[i] s) (hiâ‚ : MeasurableSet i)
    (hjâ‚ : MeasurableSet j) :
    (s.toMeasureOfZeroLE i hiâ‚ hi).real j = s (i âˆ© j) := by
  simp [measureReal_def, toMeasureOfZeroLE_apply, hjâ‚]

/-- Given a signed measure `s` and a negative measurable set `i`, `toMeasureOfLEZero`
provides the measure, mapping measurable sets `j` to `-s (i âˆ© j)`. -/
def toMeasureOfLEZero (s : SignedMeasure Î±) (i : Set Î±) (hiâ‚ : MeasurableSet i) (hiâ‚‚ : s â‰¤[i] 0) :
    Measure Î± :=
  toMeasureOfZeroLE (-s) i hiâ‚ <| @neg_zero (VectorMeasure Î± â„) _ â–¸ neg_le_neg _ _ hiâ‚ hiâ‚‚

theorem toMeasureOfLEZero_apply (hi : s â‰¤[i] 0) (hiâ‚ : MeasurableSet i) (hjâ‚ : MeasurableSet j) :
    s.toMeasureOfLEZero i hiâ‚ hi j = ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) âŸ¨-s (i âˆ© j), neg_apply s (i âˆ© j) â–¸
      nonneg_of_zero_le_restrict _ (zero_le_restrict_subset _ hiâ‚ Set.inter_subset_left
      (@neg_zero (VectorMeasure Î± â„) _ â–¸ neg_le_neg _ _ hiâ‚ hi))âŸ© := by
  simp [toMeasureOfLEZero, toMeasureOfZeroLE_apply _ _ _ hjâ‚]

theorem toMeasureOfLEZero_real_apply (hi : s â‰¤[i] 0) (hiâ‚ : MeasurableSet i)
    (hjâ‚ : MeasurableSet j) :
    (s.toMeasureOfLEZero i hiâ‚ hi).real j = -s (i âˆ© j) := by
  simp [measureReal_def, toMeasureOfLEZero_apply _ hi hiâ‚ hjâ‚]

/-- `SignedMeasure.toMeasureOfZeroLE` is a finite measure. -/
instance toMeasureOfZeroLE_finite (hi : 0 â‰¤[i] s) (hiâ‚ : MeasurableSet i) :
    IsFiniteMeasure (s.toMeasureOfZeroLE i hiâ‚ hi) where
  measure_univ_lt_top := by
    rw [toMeasureOfZeroLE_apply s hi hiâ‚ MeasurableSet.univ]
    exact ENNReal.coe_lt_top

/-- `SignedMeasure.toMeasureOfLEZero` is a finite measure. -/
instance toMeasureOfLEZero_finite (hi : s â‰¤[i] 0) (hiâ‚ : MeasurableSet i) :
    IsFiniteMeasure (s.toMeasureOfLEZero i hiâ‚ hi) where
  measure_univ_lt_top := by
    rw [toMeasureOfLEZero_apply s hi hiâ‚ MeasurableSet.univ]
    exact ENNReal.coe_lt_top

theorem toMeasureOfZeroLE_toSignedMeasure (hs : 0 â‰¤[Set.univ] s) :
    (s.toMeasureOfZeroLE Set.univ MeasurableSet.univ hs).toSignedMeasure = s := by
  ext i hi
  simp [hi, toMeasureOfZeroLE_apply _ _ _ hi, measureReal_def]

theorem toMeasureOfLEZero_toSignedMeasure (hs : s â‰¤[Set.univ] 0) :
    (s.toMeasureOfLEZero Set.univ MeasurableSet.univ hs).toSignedMeasure = -s := by
  ext i hi
  simp [hi, toMeasureOfLEZero_apply _ _ _ hi, measureReal_def]

end SignedMeasure

namespace Measure

open VectorMeasure

variable (Î¼ Î½ : Measure Î±) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] (s : Set Î±)

theorem zero_le_toSignedMeasure : 0 â‰¤ Î¼.toSignedMeasure := by
  rw [â† le_restrict_univ_iff_le]
  refine restrict_le_restrict_of_subset_le _ _ fun j hjâ‚ _ => ?_
  simp only [VectorMeasure.coe_zero, Pi.zero_apply, Measure.toSignedMeasure_apply_measurable hjâ‚,
    measureReal_nonneg]

theorem toSignedMeasure_toMeasureOfZeroLE :
    Î¼.toSignedMeasure.toMeasureOfZeroLE Set.univ MeasurableSet.univ
      ((le_restrict_univ_iff_le _ _).2 (zero_le_toSignedMeasure Î¼)) = Î¼ := by
  refine Measure.ext fun i hi => ?_
  lift Î¼ i to â„â‰¥0 using (measure_lt_top _ _).ne with m hm
  rw [SignedMeasure.toMeasureOfZeroLE_apply _ _ _ hi, ENNReal.coe_inj]
  congr
  simp [hi, â† hm, measureReal_def]

theorem toSignedMeasure_restrict_eq_restrict_toSignedMeasure (hs : MeasurableSet s) :
    Î¼.toSignedMeasure.restrict s = (Î¼.restrict s).toSignedMeasure := by
  ext A hA
  simp [VectorMeasure.restrict_apply, toSignedMeasure_apply, hA, hs]

theorem toSignedMeasure_le_toSignedMeasure_iff :
    Î¼.toSignedMeasure â‰¤ Î½.toSignedMeasure â†” Î¼ â‰¤ Î½ := by
  rw [Measure.le_iff, VectorMeasure.le_iff]
  congrm âˆ€ s, (hs : MeasurableSet s) â†’ ?_
  simp_rw [toSignedMeasure_apply_measurable hs, real_def]
  apply ENNReal.toReal_le_toReal <;> finiteness

end Measure

end MeasureTheory
