/-
Copyright (c) 2026 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
module

public import Mathlib.MeasureTheory.Measure.Stieltjes
public import Mathlib.MeasureTheory.VectorMeasure.AddContent
public import Mathlib.Topology.EMetricSpace.BoundedVariation

/-!
# Vector valued Stieltjes measure associated to a bounded variation function

Let `Œ±` be a dense linear order with compact segments (e.g. `‚Ñù` or `‚Ñù‚â•0`), and `f : Œ± ‚Üí E` a
bounded variation function to a normed group.
We associate to `f` a vector measure, called `BoundedVariationOn.vectorMeasure`. It gives
mass `f.rightLim b - f.leftLim a` to the interval `[a, b]` (with similar formulas for
other types of intervals).

For the construction, we define first a content on the set of open-closed intervals `(a, b]`,
mapping this interval to `f.rightLim b - f.rightLim a`. To extend this content to the whole
sigma-algebra, we show that it is dominated by a finite measure, the Stieltjes measure associated
to the variation of `f.rightLim`. This extension is not exactly the desired limit, as we need to
tweak things if there is a bot element `a`: the previous measure gives to `{a}` the mass `0` instead
of the desired `f.rightLim a - f a`, so we add a Dirac mass to correct this defect.
-/

@[expose] public section

open Filter Set MeasureTheory MeasurableSpace MeasureTheory
open scoped symmDiff Topology NNReal ENNReal

variable {Œ± : Type*} [LinearOrder Œ±] [DenselyOrdered Œ±] [TopologicalSpace Œ±] [OrderTopology Œ±]
  [SecondCountableTopology Œ±] [CompactIccSpace Œ±] [hŒ± : MeasurableSpace Œ±] [BorelSpace Œ±]
  {E : Type*} [NormedAddCommGroup E] [CompleteSpace E]
  {f : Œ± ‚Üí E} {a b : Œ±}

namespace BoundedVariationOn

/-- The Stieltjes function associated to a bounded variation function. It is given by
the variation of the function `f.rightLim` from a fixed base point.
Using right limits ensures the right continuity, which is used to construct Sieltjes measures. -/
@[simps] noncomputable def stieltjesFunctionRightLim
    (hf : BoundedVariationOn f univ) (x‚ÇÄ : Œ±) : StieltjesFunction Œ± where
  toFun x := variationOnFromTo f.rightLim univ x‚ÇÄ x
  mono' := by
    rw [‚Üê monotoneOn_univ]
    exact variationOnFromTo.monotoneOn hf.rightLim.locallyBoundedVariationOn (mem_univ _)
  right_continuous' x := hf.continuousWithinAt_variationOnFromTo_rightLim_Ici

open scoped Classical in
/-- Auxiliary measure used to construct the vector measure associated to a bounded variation
function. This is *not* the total variation of this measure in general, as we need to adjust things
when there is a bot element by adding a Dirac mass there. -/
noncomputable def measureAux (hf : BoundedVariationOn f univ) : Measure Œ± :=
  if h : Nonempty Œ± then (hf.stieltjesFunctionRightLim h.some).measure else 0

instance (hf : BoundedVariationOn f univ) : IsFiniteMeasure hf.measureAux := by
  by_cases h : Nonempty Œ±; swap
  ¬∑ simp only [BoundedVariationOn.measureAux, h, ‚ÜìreduceDIte]
    infer_instance
  simp only [BoundedVariationOn.measureAux, h, ‚ÜìreduceDIte]
  apply StieltjesFunction.isFiniteMeasure_of_forall_abs_le
    (C := (eVariationOn f.rightLim univ).toReal) _ (fun x ‚Ü¶ ?_)
  exact variationOnFromTo.abs_le_eVariationOn hf.rightLim

/-- Given a bounded variation function `f`, we can construct a vector measure giving
mass `f.rightLim v - f.rightLim a` to each open-closed interval `(a, b]`. This is *not* the
measure associated to `f` in general, as we may need to adjust things at the bot element if
there is one. -/
lemma exists_vectorMeasure_le_measureAux (hf : BoundedVariationOn f univ) :
    ‚àÉ m : VectorMeasure Œ± E, (‚àÄ u v, u ‚â§ v ‚Üí m (Set.Ioc u v) = f.rightLim v - f.rightLim u) ‚àß
      m botSet = 0 ‚àß ‚àÄ s, ‚Äñm s‚Äñ‚Çë ‚â§ hf.measureAux s := by
  rcases isEmpty_or_nonempty Œ± with h'Œ± | h'Œ±
  ¬∑ exact ‚ü®0, by simp‚ü©
  let m := AddContent.onIoc f.rightLim
  have A : ‚àÄ s ‚àà {s | ‚àÉ u v, u ‚â§ v ‚àß s = Ioc u v}, ‚Äñm s‚Äñ‚Çë ‚â§ hf.measureAux s := by
    rintro s ‚ü®u, v, huv, rfl‚ü©
    rw [AddContent.onIoc_apply huv]
    simp only [BoundedVariationOn.measureAux, h'Œ±, ‚ÜìreduceDIte, StieltjesFunction.measure_Ioc,
      BoundedVariationOn.stieltjesFunctionRightLim_apply]
    rw [‚Üê variationOnFromTo.add hf.rightLim.locallyBoundedVariationOn
      (mem_univ h'Œ±.some) (mem_univ u) (mem_univ v)]
    simp only [add_sub_cancel_left, variationOnFromTo, huv, ‚ÜìreduceIte, univ_inter]
    rw [ENNReal.ofReal_toReal]; swap
    ¬∑ exact ((eVariationOn.mono _ (subset_univ _)).trans_lt hf.rightLim.lt_top).ne
    rw [‚Üê edist_eq_enorm_sub]
    exact eVariationOn.edist_le _ (by grind) (by grind)
  have B : hŒ± = generateFrom {s | ‚àÉ u v, u ‚â§ v ‚àß s = Ioc u v} := by
    borelize Œ±
    convert borel_eq_generateFrom_Ioc_le Œ± using 2
    grind only
  have C : ‚àÉ D : Set (Set Œ±), D.Countable ‚àß D ‚äÜ {s | ‚àÉ u v, u ‚â§ v ‚àß s = Ioc u v}
      ‚àß hf.measureAux (‚ãÉ‚ÇÄ D)·∂ú = 0 := by
    obtain ‚ü®s, s_count, s_dense, s_bot, s_top‚ü© :
        ‚àÉ s, s.Countable ‚àß Dense s ‚àß (‚àÄ (x : Œ±), IsBot x ‚Üí x ‚àà s) ‚àß ‚àÄ (x : Œ±), IsTop x ‚Üí x ‚àà s :=
      exists_countable_dense_bot_top Œ±
    let D := {t : Set Œ± | ‚àÉ u v, u ‚â§ v ‚àß t = Ioc u v ‚àß u ‚àà s ‚àß v ‚àà s}
    refine ‚ü®D, ?_, by grind, ?_‚ü©
    ¬∑ have : D ‚äÜ (fun (p : Œ± √ó Œ±) ‚Ü¶ Ioc p.1 p.2) '' (s √óÀ¢ s) := by
        rintro - ‚ü®u, v, -, rfl, us, vs‚ü©
        exact mem_image_of_mem (x := (u, v)) _ (by simp [us, vs])
      exact Countable.mono this ((s_count.prod s_count).image _)
    have : (‚ãÉ‚ÇÄ D)·∂ú ‚äÜ botSet := by
      rw [compl_subset_comm, botSet]
      intro x hx
      simp only [mem_sUnion]
      obtain ‚ü®y, ys, hy‚ü© : ‚àÉ y ‚àà s, y < x := by
        have : (Iio x).Nonempty := by simpa [IsBot] using hx
        exact s_dense.exists_mem_open isOpen_Iio this
      by_cases h'x : IsTop x
      ¬∑ exact ‚ü®Ioc y x, ‚ü®y, x, hy.le, rfl, ys, s_top _ h'x‚ü©, ‚ü®hy, le_rfl‚ü©‚ü©
      obtain ‚ü®z, zs, hz‚ü© : ‚àÉ z ‚àà s, x < z := by
        have : (Ioi x).Nonempty := by simpa [IsTop] using h'x
        exact s_dense.exists_mem_open isOpen_Ioi this
      exact ‚ü®Ioc y z, ‚ü®y, z, (hy.trans hz).le, rfl, ys, zs‚ü©, ‚ü®hy, hz.le‚ü©‚ü©
    exact measure_mono_null this (by simp [measureAux, h'Œ±])
  rcases VectorMeasure.exists_extension_of_isSetSemiring_of_le_measure_of_generateFrom
    IsSetSemiring.Ioc A B C with ‚ü®m', hm', h'm'‚ü©
  refine ‚ü®m', fun u v huv ‚Ü¶ ?_, ?_, h'm'‚ü©
  ¬∑ rw [hm']
    ¬∑ exact AddContent.onIoc_apply huv
    ¬∑ exact ‚ü®u, v, huv, rfl‚ü©
  ¬∑ apply enorm_eq_zero.1
    apply le_bot_iff.1
    exact (h'm' _).trans (by simp [measureAux, h'Œ±])

open scoped Classical in
/-- The vector measure associated to a bounded variation function `f`, giving mass
`f.rightLim b - f.leftLim a` to closed intervals `[a, b]`, and similarly for other intervals. -/
noncomputable irreducible_def vectorMeasure (hf : BoundedVariationOn f univ) : VectorMeasure Œ± E :=
  hf.exists_vectorMeasure_le_measureAux.choose +
  (if h : ‚àÉ x, IsBot x then VectorMeasure.dirac h.choose (f.rightLim h.choose - f h.choose) else 0)

lemma vectorMeasure_Ioc (hf : BoundedVariationOn f univ) (h : a ‚â§ b) :
    hf.vectorMeasure (Ioc a b) = f.rightLim b - f.rightLim a := by
  classical
  have A : hf.exists_vectorMeasure_le_measureAux.choose (Ioc a b) =
      f.rightLim b - f.rightLim a :=
    hf.exists_vectorMeasure_le_measureAux.choose_spec.1 a b h
  have B : (if hx : ‚àÉ (x : Œ±), IsBot x then VectorMeasure.dirac hx.choose
      (f.rightLim hx.choose - f hx.choose) else 0) (Ioc a b) = 0 := by
    by_cases hx : ‚àÉ (x : Œ±), IsBot x
    ¬∑ simp only [hx, ‚ÜìreduceDIte]
      rw [VectorMeasure.dirac_apply_of_notMem]
      simp only [mem_Ioc, not_and_or, not_lt, not_le]
      exact Or.inl (hx.choose_spec _)
    ¬∑ simp [hx]
  simp [vectorMeasure, A, B]

lemma vectorMeasure_singleton (hf : BoundedVariationOn f univ) :
    hf.vectorMeasure {a} = f.rightLim a - f.leftLim a := by
  by_cases ha : IsBot a
  ¬∑ have h : ‚àÉ x, IsBot x := ‚ü®a, ha‚ü©
    have heqa : h.choose = a := by
      have := h.choose_spec
      apply subsingleton_isBot _ h.choose_spec ha
    have A : hf.exists_vectorMeasure_le_measureAux.choose {a} = 0 := by
      rw [‚Üê botSet_eq_singleton_of_isBot ha]
      exact hf.exists_vectorMeasure_le_measureAux.choose_spec.2.1
    simp only [vectorMeasure, h, ‚ÜìreduceDIte, VectorMeasure.coe_add, Pi.add_apply, A,
      zero_add]
    rw [VectorMeasure.dirac_apply_of_mem (MeasurableSet.singleton a)]
    ¬∑ simpa only [heqa, sub_right_inj] using (leftLim_eq_of_isBot ha).symm
    ¬∑ simp [heqa]
  obtain ‚ü®b, hb‚ü© : ‚àÉ b, b < a := by simpa only [IsBot, not_forall, not_le] using ha
  obtain ‚ü®u, u_mono, u_lt_a, u_lim‚ü© :
    ‚àÉ u : ‚Ñï ‚Üí Œ±, StrictMono u ‚àß (‚àÄ n : ‚Ñï, u n ‚àà Ioo b a) ‚àß Tendsto u atTop (ùìù a) :=
    exists_seq_strictMono_tendsto' hb
  replace u_lt_a n : u n < a := (u_lt_a n).2
  have A : {a} = ‚ãÇ n, Ioc (u n) a := by
    refine Subset.antisymm (fun x hx => by simp [mem_singleton_iff.1 hx, u_lt_a]) fun x hx => ?_
    replace hx : ‚àÄ (i : ‚Ñï), u i < x ‚àß x ‚â§ a := by simpa using hx
    have : a ‚â§ x := le_of_tendsto' u_lim fun n => (hx n).1.le
    simp [le_antisymm this (hx 0).2]
  have L1 : Tendsto (fun n ‚Ü¶ hf.vectorMeasure (Ioc (u n) a)) atTop (ùìù (hf.vectorMeasure {a})) := by
    rw [A]
    apply VectorMeasure.tendsto_vectorMeasure_iInter_atTop_nat ?_ (fun n ‚Ü¶ measurableSet_Ioc)
    exact fun m n hmn ‚Ü¶ Ioc_subset_Ioc_left (u_mono.monotone hmn)
  have L2 : Tendsto (fun n ‚Ü¶ hf.vectorMeasure (Ioc (u n) a)) atTop
      (ùìù (f.rightLim a - f.leftLim a)) := by
    simp_rw [hf.vectorMeasure_Ioc (u_lt_a _).le]
    apply tendsto_const_nhds.sub
    have : Tendsto u atTop (ùìù[<] a) := tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      u_lim (Eventually.of_forall u_lt_a)
    convert (hf.rightLim.tendsto_leftLim a).comp this using 2
    have : (ùìù[<] a).NeBot := by
      rw [‚Üê mem_closure_iff_nhdsWithin_neBot, closure_Iio' ‚ü®b, hb‚ü©]
      exact self_mem_Iic
    exact (leftLim_rightLim (hf.tendsto_leftLim _)).symm
  exact tendsto_nhds_unique L1 L2

lemma vectorMeasure_Icc (hf : BoundedVariationOn f univ) (h : a ‚â§ b) :
    hf.vectorMeasure (Icc a b) = f.rightLim b - f.leftLim a := by
  rw [‚Üê Icc_union_Ioc_eq_Icc le_rfl h, VectorMeasure.of_union (by simp)
    measurableSet_Icc measurableSet_Ioc, Icc_self, hf.vectorMeasure_singleton,
    hf.vectorMeasure_Ioc h]
  simp

theorem vectorMeasure_Ioo (hf : BoundedVariationOn f univ) (h : a < b) :
    hf.vectorMeasure (Ioo a b) = f.leftLim b - f.rightLim a := by
  have := hf.vectorMeasure_Ioc h.le
  rw [‚Üê Ioo_union_Icc_eq_Ioc h le_rfl, VectorMeasure.of_union (by simp) measurableSet_Ioo
    measurableSet_Icc, hf.vectorMeasure_Icc le_rfl] at this
  grind

theorem vectorMeasure_Ico (hf : BoundedVariationOn f univ) (h : a ‚â§ b) :
    hf.vectorMeasure (Ico a b) = f.leftLim b - f.leftLim a := by
  rcases h.eq_or_lt with rfl | h'
  ¬∑ simp
  rw [‚Üê Icc_union_Ioo_eq_Ico le_rfl h', VectorMeasure.of_union (by simp) measurableSet_Icc
    measurableSet_Ioo, hf.vectorMeasure_Icc le_rfl, hf.vectorMeasure_Ioo h']
  abel

end BoundedVariationOn
