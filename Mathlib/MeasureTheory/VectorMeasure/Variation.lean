/-
Copyright (c) 2025 Oliver Butterley. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Butterley, Yoh Tanimoto
-/
import Mathlib.MeasureTheory.VectorMeasure.Basic
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Normed.Group.InfiniteSum
import Mathlib.MeasureTheory.VectorMeasure.Decomposition.Jordan

/-!
# Variation and total variation for vector-valued measures

This file contains the definition of variation for `VectorMeasure` and proofs of some basic
properities of variation.

Given a vector-valued measure Œº we consider the problem of finding a function f such that, for any
set E, ‚ÄñŒº(E)‚Äñ ‚â§ f(E). This suggests defining f(E) as the supremum over partitions {E·µ¢} of E, of the
quantity ‚àë·µ¢, ‚ÄñŒº(E·µ¢)‚Äñ. Indeed any solution of the problem must be not less than this function. It
turns out that this function actually is a measure.

## Main definitions & statements

* `VectorMeasure.variation` is the definition of the (total) variation measure.

## Implementation notes

Variation is defined as an `‚Ñù‚â•0‚àû`-valued `VectorMeasure` rather than as a `Measure`, this is
somewhat natural since we start with `VectorMeasure`.

Variation is defined for signed measures in `MeasureTheory.SignedMeasure.totalVariation`. This
definition uses the Hahn‚ÄìJordan decomposition of a signed measure. However this construction doesn't
generalize to other vector-valued measures, in particular doesn't apply to the case of complex
measures.

## References

* [Walter Rudin, Real and Complex Analysis.][Rud87]

## To do

* Total variation is a enorm on the space of vector-valued measures.
* If `variation Œº univ < ‚àû` for each `Œº` then variation is a norm on the space of vector-valued
  measures.
* If `Œº` is a `SignedMeasure`, i.e., a `‚Ñù‚â•0‚àû`-valued `VectorMeasure`, then `variation Œº = Œº`.
* Variation is equivalent to that defined via the Hahn‚ÄìJordan decomposition for signed measures.
* If `Œº` is a complex measure then `variation Œº univ < ‚àû`.
* Suppose that `Œº` is a measure, that `g ‚àà L¬π(Œº)` and `Œª(E) = ‚à´_E g dŒº` for each measureable `E`.
  Then `variation Œº E = ‚à´_E |g| dŒº` (Rudin Theorem 6.13).
-/

open MeasureTheory BigOperators NNReal ENNReal Function Filter

namespace VectorMeasure

/-!
## Inner partitions

Instead of working with partitions of a set `s`, we work with finite sets of disjoints sets
contained within `s` since the same value will be achieved in the supremum. The empty set is
forbidden so that partitions of disjoint sets are disjoint sets of sets.
-/

section IsInnerPart

variable {X : Type*} [MeasurableSpace X]

/-- An inner partition is a finite collection of pairwise disjoint sets which are all contained
within a given set. Different to `Setoid.IsPartition` there is no requirement for the union to be
the entire set and the the number of partition elements is required to be finite. -/
def IsInnerPart (s : Set X) (P : Finset (Set X)) : Prop :=
    (‚àÄ t ‚àà P, t ‚äÜ s) ‚àß (‚àÄ t ‚àà P, MeasurableSet t) ‚àß (P.toSet.PairwiseDisjoint id) ‚àß (‚àÄ p ‚àà P, p ‚â† ‚àÖ)

lemma isInnerPart_of_empty {P : Finset (Set X)} (hP : IsInnerPart ‚àÖ P) : P = ‚àÖ := by
  obtain ‚ü®h, _, _, h'‚ü© := hP
  refine Finset.eq_empty_of_forall_notMem ?_
  by_contra! hc
  obtain ‚ü®p, hp‚ü© := hc
  exact h' p hp <| Set.subset_eq_empty (h p hp) rfl

lemma isInnerPart_self (s : Set X) (hs : MeasurableSet s) (hs' : s ‚â† ‚àÖ) : IsInnerPart s {s} := by
  simpa [IsInnerPart] using ‚ü®hs, hs'‚ü©

lemma isInnerPart_monotone  {s‚ÇÅ s‚ÇÇ : Set X} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) (P : Finset (Set X))
    (hP :  IsInnerPart s‚ÇÅ P) : IsInnerPart s‚ÇÇ P := by
  obtain ‚ü®h1, h2, h3, _‚ü© := hP
  exact ‚ü®fun p hp ‚Ü¶ subset_trans (h1 p hp) h, h2, h3, by simp_all‚ü©

open Classical in
/-- If the `s i` are pairwise disjoint sets and each `P i` is a partition of `s i` then the union of
the `P i` is a partition of `‚ãÉ i, s i`. -/
lemma isInnerPart_iUnion {s : ‚Ñï ‚Üí Set X} (hs : Pairwise (Disjoint on s))
    {P : ‚Ñï ‚Üí Finset (Set X)} (hP : ‚àÄ i, IsInnerPart (s i) (P i)) (n : ‚Ñï) :
    IsInnerPart (‚ãÉ i, s i) (Finset.biUnion (Finset.range n) P) := by
  simp [IsInnerPart]
  refine ‚ü®fun p i _ hp ‚Ü¶ ?_, fun p i _ hp ‚Ü¶ ?_, fun p hp q hq hpq _ hrp hrq ‚Ü¶ ?_, fun _ i _ h' ‚Ü¶ ?_‚ü©
  ¬∑ exact Set.subset_iUnion_of_subset i ((hP i).1 p hp)
  ¬∑ exact (hP i).2.1 p hp
  ¬∑ obtain ‚ü®i, hi, hp‚ü© : ‚àÉ i < n, p ‚àà P i := by simp_all
    obtain ‚ü®j, hj, hq‚ü© : ‚àÉ i < n, q ‚àà P i := by simp_all
    obtain hc | hc : i = j ‚à® i ‚â† j := by omega
    ¬∑ rw [hc] at hp
      simpa using Set.subset_eq_empty ((hP j).2.2.1 hp hq hpq hrp hrq) rfl
    ¬∑ have hp' := (hP i).1 p hp
      have hq' := (hP j).1 q hq
      simpa using Set.subset_eq_empty (hs hc (subset_trans hrp hp') (subset_trans hrq hq')) rfl
  ¬∑ exact ne_of_mem_of_not_mem h' <| fun a ‚Ü¶ ((hP i).2.2.2 ‚àÖ) a rfl

/-- If P, Q are partitions of two disjoint sets then P and Q are disjoint. -/
lemma isInnerPart_of_disjoint {s t : Set X} (hst : Disjoint s t) {P Q : Finset (Set X)}
    (hP : IsInnerPart s P) (hQ : IsInnerPart t Q) : Disjoint P Q := by
  intro R hRP hRQ
  simp only [Finset.bot_eq_empty, Finset.le_eq_subset, Finset.subset_empty]
  by_contra! hc
  obtain ‚ü®r, hr‚ü© := Finset.Nonempty.exists_mem <| Finset.nonempty_iff_ne_empty.mpr hc
  have := hst (hP.1 r <| hRP hr) (hQ.1 r <| hRQ hr)
  have := hP.2.2.2 r (hRP hr)
  simp_all

open Classical in
/-- The restriction of a partition `P` to the set `t`. -/
noncomputable def restriction (t : Set X) (P : Finset (Set X)) : Finset (Set X) :=
  (P.image (fun p ‚Ü¶ p ‚à© t)).filter (¬∑ ‚â† ‚àÖ)

open Classical in
/-- If `P` is a partition then the restriction of `P` to a set `s` is a partition of `s`. -/
lemma restriction_isInnerPart {s t : Set X} {P : Finset (Set X)} (hs : IsInnerPart s P)
    (ht : MeasurableSet t) : IsInnerPart t (restriction t P) := by
  refine ‚ü®fun _ h ‚Ü¶ ?_, fun r hr ‚Ü¶ ?_, fun _ hr _ hr' ‚Ü¶ ?_, fun _ hp ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®_, _, hp‚ü© := Finset.mem_image.mp (Finset.mem_filter.mp h).1
    simp [‚Üê hp]
  ¬∑ obtain ‚ü®p, hp, hp'‚ü© := Finset.mem_image.mp (Finset.mem_filter.mp hr).1
    simpa [‚Üê hp'] using MeasurableSet.inter (hs.2.1 p hp) ht
  ¬∑ obtain ‚ü®p, hp, hp'‚ü© := Finset.mem_image.mp (Finset.mem_filter.mp hr).1
    obtain ‚ü®q, hq, hq'‚ü© := Finset.mem_image.mp (Finset.mem_filter.mp hr').1
    rw [‚Üê hp', ‚Üê hq']
    intro hpqt _ h h'
    have hpq : p ‚â† q := fun h ‚Ü¶ hpqt (congrFun (congrArg Inter.inter h) t)
    exact hs.2.2.1 hp hq hpq (Set.subset_inter_iff.mp h).1 (Set.subset_inter_iff.mp h').1
  ¬∑ exact (Finset.mem_filter.mp hp).2

end IsInnerPart

/-!
## Variation of a subadditive `‚Ñù‚â•0‚àû`-valued function

Given a set function `f : Set X ‚Üí ‚Ñù‚â•0‚àû` we can define another set function by taking the supremum
over all partitions `E i` of the sum of `‚àë i, f (E i)`. If `f` is sub-additive then the function
defined is an `‚Ñù‚â•0‚àû`-valued measure.
-/

section var_aux

variable {X : Type*} [MeasurableSpace X] (f : Set X ‚Üí ‚Ñù‚â•0‚àû)

open Classical in
/-- If `s` is measurable then `var_aux s f` is the supremum over partitions `P` of `s` of the
quantity `‚àë p ‚àà P, f p`. If `s` is not measurable then it is set to `0`. -/
noncomputable def var_aux (s : Set X) :=
    if (MeasurableSet s) then ‚®Ü (P : Finset (Set X)) (_ : IsInnerPart s P), ‚àë p ‚àà P, f p else 0

/-- `var_aux` of the empty set is equal to zero. -/
lemma var_aux_empty' : var_aux f ‚àÖ = 0 := by
  simp only [var_aux, MeasurableSet.empty, reduceIte, ENNReal.iSup_eq_zero]
  intro _ hP
  simp_all [isInnerPart_of_empty hP]

/-- `var_aux` of a non-measurable set is equal to zero. -/
lemma var_aux_of_not_measurable (s : Set X) (hs : ¬¨MeasurableSet s) : var_aux f s = 0 := by
  simp [var_aux, hs]

/-- `var_aux` is monotone in terms of the set. -/
lemma var_aux_monotone {s‚ÇÅ s‚ÇÇ : Set X} (h : s‚ÇÅ ‚äÜ s‚ÇÇ)
    (hs‚ÇÇ : MeasurableSet s‚ÇÇ) : var_aux f s‚ÇÅ ‚â§ var_aux f s‚ÇÇ := by
  by_cases hs‚ÇÅ : MeasurableSet s‚ÇÅ
  ¬∑ simp only [var_aux, hs‚ÇÅ, reduceIte, hs‚ÇÇ]
    exact iSup_le_iSup_of_subset (isInnerPart_monotone h)
  ¬∑ simp [var_aux, hs‚ÇÅ]

lemma var_aux_lt {s : Set X} (hs : MeasurableSet s) {a : ‚Ñù‚â•0‚àû} (ha : a < var_aux f s) :
    ‚àÉ P, IsInnerPart s P ‚àß a < ‚àë p ‚àà P, f p := by
  obtain ‚ü®P, hP, hP'‚ü© : ‚àÉ P, IsInnerPart s P ‚àß a < ‚àë p ‚àà P, f p := by
    simp_all [var_aux, hs, lt_iSup_iff]
  exact ‚ü®P, hP, by gcongr‚ü©

lemma var_aux_le {s : Set X} (hs : MeasurableSet s) {Œµ : NNReal} (hŒµ: 0 < Œµ)
    (h : var_aux f s ‚â† ‚ä§) :
    ‚àÉ P, IsInnerPart s P ‚àß var_aux f s ‚â§ ‚àë p ‚àà P, f p + Œµ := by
  let Œµ' := min Œµ (var_aux f s).toNNReal
  have hŒµ1 : Œµ' ‚â§ var_aux f s := by simp_all [Œµ']
  have : Œµ' ‚â§ Œµ := by simp_all [Œµ']
  obtain hw | hw : var_aux f s ‚â† 0 ‚à® var_aux f s = 0 := ne_or_eq _ _
  ¬∑ have : 0 < Œµ' := by
      simp only [lt_inf_iff, Œµ']
      exact ‚ü®hŒµ, toNNReal_pos hw h‚ü©
    let a := var_aux f s - Œµ'
    have ha : a < var_aux f s := by exact ENNReal.sub_lt_self h hw (by positivity)
    obtain ‚ü®P, hP, hP'‚ü© := var_aux_lt f hs ha
    refine ‚ü®P, hP, ?_‚ü©
    calc var_aux f s
      _ = a + Œµ' := (tsub_add_cancel_of_le hŒµ1).symm
      _ ‚â§  ‚àë p ‚àà P, f p + Œµ' := by
        exact (ENNReal.add_le_add_iff_right coe_ne_top).mpr (le_of_lt hP')
      _ ‚â§  ‚àë p ‚àà P, f p + Œµ := by gcongr
  ¬∑ simp_rw [hw, zero_le, and_true]
    exact ‚ü®{}, by simp, by simp [hs], by simp, by simp‚ü©

lemma le_var_aux {s : Set X} (hs : MeasurableSet s) {P : Finset (Set X)}
    (hP : IsInnerPart s P) : ‚àë p ‚àà P, f p ‚â§ var_aux f s := by
  simpa [var_aux, hs] using le_biSup (fun P ‚Ü¶ ‚àë p ‚àà P, f p) hP

-- TO DO: better to rephrase as `HasSum`?
/-- A set function is subadditive if the value assigned to the union of disjoint sets is bounded
above by the sum of the values assigned to the individual sets. -/
def IsSubadditive (f : Set X ‚Üí ‚Ñù‚â•0‚àû) := ‚àÄ (s : ‚Ñï ‚Üí Set X), (‚àÄ i, MeasurableSet (s i)) ‚Üí
  Pairwise (Disjoint on s) ‚Üí f (‚ãÉ (i : ‚Ñï), s i) ‚â§ ‚àë' (i : ‚Ñï), f (s i)

open Classical in
/-- Given a partition `Q`, `varOfPart Œº Q` is bounded by the sum of the `varOfPart Œº (P i)` where
the `P i` are the partitions formed by restricting to a disjoint set of sets `s i`. -/
lemma sum_part_le_tsum_sum_part (hf : IsSubadditive f) (hf' : f ‚àÖ = 0) {s : ‚Ñï ‚Üí Set X}
    (hs : ‚àÄ i, MeasurableSet (s i)) (hs' : Pairwise (Disjoint on s)) {Q : Finset (Set X)}
    (hQ : IsInnerPart (‚ãÉ i, s i) Q) : ‚àë q ‚àà Q, f q ‚â§ ‚àë' i, ‚àë p ‚àà (restriction (s i) Q), f p := by
  let P (i : ‚Ñï) := restriction (s i) Q
  calc ‚àë q ‚àà Q, f q
    _ = ‚àë q ‚àà Q, f (‚ãÉ i, q ‚à© s i) := ?_
    _ ‚â§ ‚àë q ‚àà Q, ‚àë' i, f (q ‚à© s i) := ?_
    _ = ‚àë' i, ‚àë q ‚àà Q, f (q ‚à© s i) := ?_
    _ ‚â§ ‚àë' i, ‚àë p ‚àà (P i), f p := ?_
  ¬∑ -- Each `q` is equal to the union of `q ‚à© s i`.
    -- TO DO: This only needs one direction of the argument.
    suffices h : ‚àÄ q ‚àà Q, q = ‚ãÉ i, q ‚à© s i by
      exact Finset.sum_congr rfl (fun q hq ‚Ü¶ (by simp [‚Üê h q hq]))
    intro q hq
    ext x
    refine ‚ü®fun hx ‚Ü¶ ?_, by simp_all‚ü©
    obtain ‚ü®_, hs‚ü© := (hQ.1 q hq) hx
    obtain ‚ü®i, _‚ü© := Set.mem_range.mp hs.1
    simp_all [Set.mem_iUnion_of_mem i]
  ¬∑ -- Subadditivity of `f` since the `s i` are pairwise disjoint.
    suffices h : ‚àÄ p ‚àà Q, f (‚ãÉ i, p ‚à© s i) ‚â§ ‚àë' (i : ‚Ñï), f (p ‚à© s i) by exact Finset.sum_le_sum h
    intro p hp
    refine hf (fun i ‚Ü¶ p ‚à© s i) (fun i ‚Ü¶ ?_) ?_
    ¬∑ exact MeasurableSet.inter (hQ.2.1 p hp) (hs i)
    ¬∑ refine (Symmetric.pairwise_on (fun ‚¶Éx y‚¶Ñ a ‚Ü¶ Disjoint.symm a) fun i ‚Ü¶ p ‚à© s i).mpr ?_
      intro _ _ _
      exact Disjoint.inter_left' p (Disjoint.inter_right' p (hs' (by omega)))
  ¬∑ -- Swapping the order of the sum.
    refine Eq.symm (Summable.tsum_finsetSum (fun _ _ ‚Ü¶ ENNReal.summable))
  ¬∑ -- By defintion of the restricted partition
    refine ENNReal.tsum_le_tsum (fun i ‚Ü¶ ?_)
    calc ‚àë q ‚àà Q, f (q ‚à© s i)
      _ = ‚àë p ‚àà (Finset.image (fun q ‚Ü¶ q ‚à© s i) Q), f p := by
        refine Eq.symm (Finset.sum_image_of_disjoint (by simp [hf']) ?_)
        intro _ hp _ hq hpq
        exact Disjoint.inter_left (s i) (Disjoint.inter_right (s i) (hQ.2.2.1 hp hq hpq))
      _ ‚â§  ‚àë p ‚àà P i, f p := by
        refine Finset.sum_le_sum_of_ne_zero (fun p hp hp' ‚Ü¶ ?_)
        obtain hc | hc : p = ‚àÖ ‚à® ¬¨p = ‚àÖ := eq_or_ne p ‚àÖ
        ¬∑ simp [hc, hf'] at hp'
        ¬∑ simp only [P, restriction, Finset.mem_filter, Finset.mem_image]
          obtain ‚ü®q, hq, hq'‚ü© := Finset.mem_image.mp hp
          exact ‚ü®‚ü®q, hq, hq'‚ü©, hc‚ü©

open Classical in
lemma le_var_aux_iUnion' {s : ‚Ñï ‚Üí Set X} (hs : ‚àÄ i, MeasurableSet (s i))
    (hs' : Pairwise (Disjoint on s)) (P : ‚Ñï ‚Üí Finset (Set X))
    (hP : ‚àÄ (i : ‚Ñï), IsInnerPart (s i) (P i)) (n : ‚Ñï) :
    ‚àë i ‚àà Finset.range n, ‚àë p ‚àà (P i), f p ‚â§ var_aux f (‚ãÉ i, s i) := by
  let Q := Finset.biUnion (Finset.range n) P
  have hQ : IsInnerPart (‚ãÉ i, s i) Q := by exact isInnerPart_iUnion hs' hP n
  calc
    _ = ‚àë i ‚àà Finset.range n, ‚àë p ‚àà P i, f p := by simp
    _ = ‚àë q ‚àà Q, f q := by
      refine Eq.symm (Finset.sum_biUnion fun l _ m _ hlm ‚Ü¶ ?_)
      exact isInnerPart_of_disjoint (hs' hlm) (hP l) (hP m)
    _ ‚â§ var_aux f (‚ãÉ i, s i) := by
      simpa using le_var_aux f (MeasurableSet.iUnion hs) hQ

open Classical in
lemma le_var_aux_iUnion (s : ‚Ñï ‚Üí Set X) (hs : ‚àÄ i, MeasurableSet (s i))
    (hs' : Pairwise (Disjoint on s)) :
    ‚àë' i, var_aux f (s i) ‚â§ var_aux f (‚ãÉ i, s i) := by
  refine ENNReal.tsum_le_of_sum_range_le fun n ‚Ü¶ ?_
  wlog hn : n ‚â† 0
  ¬∑ simp [show n = 0 by omega]
  refine ENNReal.le_of_forall_pos_le_add fun Œµ' hŒµ' hsnetop ‚Ü¶ ?_
  let Œµ := Œµ' / n
  have hŒµ : 0 < Œµ := by positivity
  have hs'' i : var_aux f (s i) ‚â† ‚ä§ := by
    refine lt_top_iff_ne_top.mp <| lt_of_le_of_lt ?_ hsnetop
    exact var_aux_monotone f (Set.subset_iUnion_of_subset i fun ‚¶Éa‚¶Ñ a ‚Ü¶ a) (MeasurableSet.iUnion hs)
  -- For each set `s i` we choose a partition `P i` such that, for each `i`,
  -- `var_aux f (s i) ‚â§ ‚àë p ‚àà (P i), f p + Œµ`.
  choose P hP using fun i ‚Ü¶ var_aux_le f (hs i) (hŒµ) (hs'' i)
  calc ‚àë i ‚àà Finset.range n, var_aux f (s i)
    _ ‚â§ ‚àë i ‚àà Finset.range n, (‚àë p ‚àà (P i), f p + Œµ) := by
      gcongr with i _
      exact (hP i).2
    _ = ‚àë i ‚àà Finset.range n, ‚àë p ‚àà (P i), f p + Œµ' := by
      rw [Finset.sum_add_distrib]
      norm_cast
      simp [show n * Œµ = Œµ' by rw [mul_div_cancel‚ÇÄ _ (by positivity)]]
    _ ‚â§ var_aux f (‚ãÉ i, s i) + Œµ' := by
      have := le_var_aux_iUnion' f hs hs' P (fun i ‚Ü¶ (hP i).1) n
      gcongr

lemma ENNReal.sum_le_tsum' {f : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} {a : ‚Ñù‚â•0‚àû}
    (h : ‚àÄ b < a, ‚àÉ n, b < ‚àë i ‚àà Finset.range n, f i) : a ‚â§ ‚àë' i, f i := by
  obtain ha | ha | ha := a.trichotomy
  ¬∑ simp [ha]
  ¬∑ rw [ha] at h
    simp only [ha, top_le_iff]
    by_contra! hc
    obtain ‚ü®n, hn‚ü© := h (‚àë' (i : ‚Ñï), f i) hc.symm.lt_top'
    exact not_le_of_lt hn <| ENNReal.sum_le_tsum _
  ¬∑ obtain ‚ü®ha, ha'‚ü© := (a.toReal_pos_iff).mp ha
    suffices hs : ‚àÄ Œµ > 0, a ‚â§ ‚àë' i, f i + Œµ by
      exact le_of_forall_pos_le_add hs
    intro Œµ hŒµ
    have hŒµ' := (ENNReal.sub_lt_self_iff (LT.lt.ne_top ha')).mpr ‚ü®ha, hŒµ‚ü©
    obtain ‚ü®n, hn‚ü© := h (a - Œµ) hŒµ'
    calc
      a ‚â§ a - Œµ + Œµ := by exact le_tsub_add
      _ ‚â§ ‚àë i ‚àà Finset.range n, f i + Œµ := add_le_add_right (le_of_lt hn) Œµ
      _ ‚â§ ‚àë' i, f i + Œµ := by
        gcongr
        exact ENNReal.sum_le_tsum (Finset.range n)

open Classical in
lemma var_aux_iUnion_le (s : ‚Ñï ‚Üí Set X) (hs : ‚àÄ i, MeasurableSet (s i))
    (hs' : Pairwise (Disjoint on s)) (hf : IsSubadditive f) (hf' : f ‚àÖ = 0) :
    var_aux f (‚ãÉ i, s i) ‚â§ ‚àë' i, var_aux f (s i) := by
  refine ENNReal.sum_le_tsum' fun b hb ‚Ü¶ ?_
  simp only [var_aux, MeasurableSet.iUnion hs, reduceIte, lt_iSup_iff] at hb
  obtain ‚ü®Q, hQ, hbQ‚ü© := hb
  -- Take the partitions defined as intersection of `Q` and `s i`.
  let P (i : ‚Ñï) := (Q.image (fun q ‚Ü¶ q ‚à© (s i))).filter (¬∑ ‚â† ‚àÖ)
  have hP (i : ‚Ñï) : IsInnerPart (s i) (P i) := restriction_isInnerPart hQ (hs i)
  have hP' := calc
    b < ‚àë q ‚àà Q, f q := hbQ
    _ ‚â§ ‚àë' i, ‚àë p ‚àà (P i), f p := by exact sum_part_le_tsum_sum_part f hf hf' hs hs' hQ
  have := tendsto_nat_tsum fun i ‚Ü¶ ‚àë p ‚àà (P i), f p
  obtain ‚ü®n, hn, _‚ü© := (((tendsto_order.mp this).1 b hP').and (Ici_mem_atTop 1)).exists
  use n
  calc
    b < ‚àë i ‚àà Finset.range n, ‚àë p ‚àà (P i), f p := hn
    _ ‚â§ ‚àë i ‚àà Finset.range n, var_aux f (s i) := by
      gcongr with i hi
      exact le_var_aux f (hs i) (hP i)

/-- Aditivity of `variation_aux` for disjoint measurable sets. -/
lemma var_aux_iUnion (hf : IsSubadditive f) (hf' : f ‚àÖ = 0) (s : ‚Ñï ‚Üí Set X)
    (hs : ‚àÄ i, MeasurableSet (s i)) (hs' : Pairwise (Disjoint on s)) :
    HasSum (fun i ‚Ü¶ var_aux f (s i)) (var_aux f (‚ãÉ i, s i)) := by
  refine (Summable.hasSum_iff ENNReal.summable).mpr (eq_of_le_of_le ?_ ?_)
  ¬∑ exact le_var_aux_iUnion f s hs hs'
  ¬∑ exact var_aux_iUnion_le f s hs hs' hf hf'

end var_aux

/-!
## Definition of variation
-/

section variation

variable  {X : Type*} [MeasurableSpace X]

/-- The variation of a subadditive function as an `‚Ñù‚â•0‚àû`-valued `VectorMeasure`. -/
noncomputable def variation' {f : Set X ‚Üí ‚Ñù‚â•0‚àû} (hf : IsSubadditive f) (hf' : f ‚àÖ = 0) :
    VectorMeasure X ‚Ñù‚â•0‚àû where
  measureOf'          := var_aux f
  empty'              := var_aux_empty' f
  not_measurable' _ h := if_neg h
  m_iUnion'           := var_aux_iUnion f hf hf'

variable {V : Type*} [TopologicalSpace V] [ENormedAddCommMonoid V]

lemma isSubadditive_enorm_vectorMeasure (Œº : VectorMeasure X V) [T2Space V] :
    IsSubadditive fun s ‚Ü¶ ‚ÄñŒº s‚Äñ‚Çë := by
  intro _ hs hs'
  simpa [VectorMeasure.of_disjoint_iUnion hs hs'] using enorm_tsum_le_tsum_enorm

-- TO DO: define using `variation'` or directly?
/-- The variation of a `VectorMeasure` as an `‚Ñù‚â•0‚àû`-valued `VectorMeasure`. -/
noncomputable def variation [T2Space V] (Œº : VectorMeasure X V) : VectorMeasure X ‚Ñù‚â•0‚àû :=
  variation' (isSubadditive_enorm_vectorMeasure Œº) (by simp)

end variation

end VectorMeasure

/-!
## Properties of variation
-/

namespace VectorMeasure
variable {X V ùïú : Type*} [MeasurableSpace X] [NormedAddCommGroup V] [NormedField ùïú]
  [NormedSpace ùïú V]

theorem norm_measure_le_variation (Œº : VectorMeasure X V) (E : Set X) : ‚ÄñŒº E‚Äñ‚Çë ‚â§ variation Œº E := by
  wlog hE' : E ‚â† ‚àÖ
  ¬∑ push_neg at hE'
    simp [hE']
  wlog hE : MeasurableSet E
  ¬∑ simp [hE, Œº.not_measurable' hE]
  -- have h : {E} ‚àà partitions E := ‚ü®by simp, by simpa, by simp, by simpa‚ü©
  have h := isInnerPart_self E hE (by simpa)
  -- have := le_biSup (fun P ‚Ü¶ ‚àë p ‚àà P, ‚ÄñŒº p‚Äñ‚Çë) h
  simp_all [variation, variation', var_aux]
  sorry

-- lemma variation_of_ENNReal  (Œº : VectorMeasure X ‚Ñù‚â•0‚àû) : variation Œº = Œº := by
--   ext s hs
--   simp only [variation, variation_aux, hs, reduceIte]
--   apply eq_of_le_of_le
--   ¬∑ simp only [varOfPart, enorm_eq_self, iSup_le_iff]
--     intro P hP
--     have : ‚àë x ‚àà P, Œº x  =  Œº (‚ãÉ p ‚àà P, p) := by
--       have := Œº.m_iUnion'
--       -- need to move from m_iUnion' to union over a Finset
--       sorry
--     rw [this]
--     have : ‚ãÉ p ‚àà P, p ‚äÜ s := Set.iUnion‚ÇÇ_subset hP.1
--     -- ENNReal valued measure is monotone
--     sorry
--   ¬∑ by_cases hc : s ‚â† ‚àÖ
--     ¬∑ have : {s} ‚àà partitions s := by -- Extract as separate lemma
--         refine ‚ü®by simp, by simp [hs], by simp, by simp [hc]‚ü©
--       have := le_biSup (fun P ‚Ü¶ ‚àë x ‚àà P, Œº x) this
--       simp_all [varOfPart]
--     ¬∑ push_neg at hc
--       simp [hc]

open VectorMeasure SignedMeasure in
/-- For signed measures, variation defined by the Hahn‚ÄìJordan decomposition coincides with variation
defined as a sup. -/
lemma variation_SignedMeasure (Œº : SignedMeasure X) :
    totalVariation Œº = ennrealToMeasure (variation Œº) := by
  sorry

end VectorMeasure
