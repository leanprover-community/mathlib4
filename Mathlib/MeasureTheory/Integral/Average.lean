/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, YaÃ«l Dillies
-/
import Mathlib.MeasureTheory.Integral.SetIntegral

#align_import measure_theory.integral.average from "leanprover-community/mathlib"@"c14c8fcde993801fca8946b0d80131a1a81d1520"

/-!
# Integral average of a function

In this file we define `MeasureTheory.average Î¼ f` (notation: `â¨ x, f x âˆ‚Î¼`) to be the average
value of `f` with respect to measure `Î¼`. It is defined as `âˆ« x, f x âˆ‚((Î¼ univ)â»Â¹ â€¢ Î¼)`, so it
is equal to zero if `f` is not integrable or if `Î¼` is an infinite measure. If `Î¼` is a probability
measure, then the average of any function is equal to its integral.

For the average on a set, we use `â¨ x in s, f x âˆ‚Î¼` (notation for `â¨ x, f x âˆ‚(Î¼.restrict s)`). For
average w.r.t. the volume, one can omit `âˆ‚volume`.

Both have a version for the Lebesgue integral rather than Bochner.

We prove several version of the first moment method: An integrable function is below/above its
average on a set of positive measure.

## Implementation notes

The average is defined as an integral over `(Î¼ univ)â»Â¹ â€¢ Î¼` so that all theorems about Bochner
integrals work for the average without modifications. For theorems that require integrability of a
function, we provide a convenience lemma `MeasureTheory.Integrable.to_average`.

## TODO

Provide the first moment method for the Lebesgue integral as well. A draft is available on branch
`first_moment_lintegral` in mathlib3 repository.

## Tags

integral, center mass, average value
-/


open ENNReal MeasureTheory MeasureTheory.Measure Metric Set Filter TopologicalSpace Function

open scoped Topology ENNReal Convex

variable {Î± E F : Type*} {m0 : MeasurableSpace Î±} [NormedAddCommGroup E] [NormedSpace â„ E]
  [CompleteSpace E] [NormedAddCommGroup F] [NormedSpace â„ F] [CompleteSpace F] {Î¼ Î½ : Measure Î±}
  {s t : Set Î±}

/-!
### Average value of a function w.r.t. a measure

The (Bochner, Lebesgue) average value of a function `f` w.r.t. a measure `Î¼` (notation:
`â¨ x, f x âˆ‚Î¼`, `â¨â» x, f x âˆ‚Î¼`) is defined as the (Bochner, Lebesgue) integral divided by the total
measure, so it is equal to zero if `Î¼` is an infinite measure, and (typically) equal to infinity if
`f` is not integrable. If `Î¼` is a probability measure, then the average of any function is equal to
its integral.
-/

namespace MeasureTheory
section ENNReal
variable (Î¼) {f g : Î± â†’ â„â‰¥0âˆ}

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. a measure `Î¼`, denoted `â¨â» x, f x âˆ‚Î¼`.

It is equal to `(Î¼ univ)â»Â¹ * âˆ«â» x, f x âˆ‚Î¼`, so it takes value zero if `Î¼` is an infinite measure. If
`Î¼` is a probability measure, then the average of any function is equal to its integral.

For the average on a set, use `â¨â» x in s, f x âˆ‚Î¼`, defined as `â¨â» x, f x âˆ‚(Î¼.restrict s)`. For the
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
noncomputable def laverage (f : Î± â†’ â„â‰¥0âˆ) := âˆ«â» x, f x âˆ‚(Î¼ univ)â»Â¹ â€¢ Î¼
#align measure_theory.laverage MeasureTheory.laverage

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. a measure `Î¼`.

It is equal to `(Î¼ univ)â»Â¹ * âˆ«â» x, f x âˆ‚Î¼`, so it takes value zero if `Î¼` is an infinite measure. If
`Î¼` is a probability measure, then the average of any function is equal to its integral.

For the average on a set, use `â¨â» x in s, f x âˆ‚Î¼`, defined as `â¨â» x, f x âˆ‚(Î¼.restrict s)`. For the
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
notation3 "â¨â» "(...)", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => laverage Î¼ r

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. to the standard measure.

It is equal to `(volume univ)â»Â¹ * âˆ«â» x, f x`, so it takes value zero if the space has infinite
measure. In a probability space, the average of any function is equal to its integral.

For the average on a set, use `â¨â» x in s, f x`, defined as `â¨â» x, f x âˆ‚(volume.restrict s)`. -/
notation3 "â¨â» "(...)", "r:60:(scoped f => laverage volume f) => r

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. a measure `Î¼` on a set `s`.

It is equal to `(Î¼ s)â»Â¹ * âˆ«â» x, f x âˆ‚Î¼`, so it takes value zero if `s` has infinite measure. If `s`
has measure `1`, then the average of any function is equal to its integral.

For the average w.r.t. the volume, one can omit `âˆ‚volume`. -/
notation3 "â¨â» "(...)" in "s", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => laverage (Measure.restrict Î¼ s) r

/-- Average value of an `â„â‰¥0âˆ`-valued function `f` w.r.t. to the standard measure on a set `s`.

It is equal to `(volume s)â»Â¹ * âˆ«â» x, f x`, so it takes value zero if `s` has infinite measure. If
`s` has measure `1`, then the average of any function is equal to its integral. -/
notation3 (prettyPrint := false)
  "â¨â» "(...)" in "s", "r:60:(scoped f => laverage Measure.restrict volume s f) => r

@[simp]
theorem laverage_zero : â¨â» _x, (0 : â„â‰¥0âˆ) âˆ‚Î¼ = 0 := by rw [laverage, lintegral_zero]
#align measure_theory.laverage_zero MeasureTheory.laverage_zero

@[simp]
theorem laverage_zero_measure (f : Î± â†’ â„â‰¥0âˆ) : â¨â» x, f x âˆ‚(0 : Measure Î±) = 0 := by simp [laverage]
#align measure_theory.laverage_zero_measure MeasureTheory.laverage_zero_measure

theorem laverage_eq' (f : Î± â†’ â„â‰¥0âˆ) : â¨â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚(Î¼ univ)â»Â¹ â€¢ Î¼ := rfl
#align measure_theory.laverage_eq' MeasureTheory.laverage_eq'

theorem laverage_eq (f : Î± â†’ â„â‰¥0âˆ) : â¨â» x, f x âˆ‚Î¼ = (âˆ«â» x, f x âˆ‚Î¼) / Î¼ univ := by
  rw [laverage_eq', lintegral_smul_measure, ENNReal.div_eq_inv_mul]
#align measure_theory.laverage_eq MeasureTheory.laverage_eq

theorem laverage_eq_lintegral [IsProbabilityMeasure Î¼] (f : Î± â†’ â„â‰¥0âˆ) :
    â¨â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by rw [laverage, measure_univ, inv_one, one_smul]
#align measure_theory.laverage_eq_lintegral MeasureTheory.laverage_eq_lintegral

@[simp]
theorem measure_mul_laverage [IsFiniteMeasure Î¼] (f : Î± â†’ â„â‰¥0âˆ) :
    Î¼ univ * â¨â» x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by
  rcases eq_or_ne Î¼ 0 with hÎ¼ | hÎ¼
  Â· rw [hÎ¼, lintegral_zero_measure, laverage_zero_measure, mul_zero]
  Â· rw [laverage_eq, ENNReal.mul_div_cancel' (measure_univ_ne_zero.2 hÎ¼) (measure_ne_top _ _)]
#align measure_theory.measure_mul_laverage MeasureTheory.measure_mul_laverage

theorem setLaverage_eq (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î±) :
    â¨â» x in s, f x âˆ‚Î¼ = (âˆ«â» x in s, f x âˆ‚Î¼) / Î¼ s := by rw [laverage_eq, restrict_apply_univ]
#align measure_theory.set_laverage_eq MeasureTheory.setLaverage_eq

theorem setLaverage_eq' (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î±) :
    â¨â» x in s, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚(Î¼ s)â»Â¹ â€¢ Î¼.restrict s := by
  simp only [laverage_eq', restrict_apply_univ]
#align measure_theory.set_laverage_eq' MeasureTheory.setLaverage_eq'

variable {Î¼}

theorem laverage_congr {f g : Î± â†’ â„â‰¥0âˆ} (h : f =áµ[Î¼] g) : â¨â» x, f x âˆ‚Î¼ = â¨â» x, g x âˆ‚Î¼ := by
  simp only [laverage_eq, lintegral_congr_ae h]
#align measure_theory.laverage_congr MeasureTheory.laverage_congr

theorem setLaverage_congr (h : s =áµ[Î¼] t) : â¨â» x in s, f x âˆ‚Î¼ = â¨â» x in t, f x âˆ‚Î¼ := by
  simp only [setLaverage_eq, set_lintegral_congr h, measure_congr h]
#align measure_theory.set_laverage_congr MeasureTheory.setLaverage_congr

theorem setLaverage_congr_fun (hs : MeasurableSet s) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = g x) :
    â¨â» x in s, f x âˆ‚Î¼ = â¨â» x in s, g x âˆ‚Î¼ := by
  simp only [laverage_eq, set_lintegral_congr_fun hs h]
#align measure_theory.set_laverage_congr_fun MeasureTheory.setLaverage_congr_fun

theorem laverage_lt_top (hf : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) : â¨â» x, f x âˆ‚Î¼ < âˆ := by
  obtain rfl | hÎ¼ := eq_or_ne Î¼ 0
  Â· simp
  Â· rw [laverage_eq]
    exact div_lt_top hf (measure_univ_ne_zero.2 hÎ¼)
#align measure_theory.laverage_lt_top MeasureTheory.laverage_lt_top

theorem setLaverage_lt_top : âˆ«â» x in s, f x âˆ‚Î¼ â‰  âˆ â†’ â¨â» x in s, f x âˆ‚Î¼ < âˆ :=
  laverage_lt_top
#align measure_theory.set_laverage_lt_top MeasureTheory.setLaverage_lt_top

theorem laverage_add_measure :
    â¨â» x, f x âˆ‚(Î¼ + Î½) =
      Î¼ univ / (Î¼ univ + Î½ univ) * â¨â» x, f x âˆ‚Î¼ + Î½ univ / (Î¼ univ + Î½ univ) * â¨â» x, f x âˆ‚Î½ := by
  by_cases hÎ¼ : IsFiniteMeasure Î¼; swap
  Â· rw [not_isFiniteMeasure_iff] at hÎ¼
    simp [laverage_eq, hÎ¼]
  by_cases hÎ½ : IsFiniteMeasure Î½; swap
  Â· rw [not_isFiniteMeasure_iff] at hÎ½
    simp [laverage_eq, hÎ½]
  haveI := hÎ¼; haveI := hÎ½
  simp only [â† ENNReal.mul_div_right_comm, measure_mul_laverage, â† ENNReal.add_div,
    â† lintegral_add_measure, â† Measure.add_apply, â† laverage_eq]
#align measure_theory.laverage_add_measure MeasureTheory.laverage_add_measure

theorem measure_mul_setLaverage (f : Î± â†’ â„â‰¥0âˆ) (h : Î¼ s â‰  âˆ) :
    Î¼ s * â¨â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, f x âˆ‚Î¼ := by
  have := Fact.mk h.lt_top
  rw [â† measure_mul_laverage, restrict_apply_univ]
#align measure_theory.measure_mul_set_laverage MeasureTheory.measure_mul_setLaverage

theorem laverage_union (hd : AEDisjoint Î¼ s t) (ht : NullMeasurableSet t Î¼) :
    â¨â» x in s âˆª t, f x âˆ‚Î¼ =
      Î¼ s / (Î¼ s + Î¼ t) * â¨â» x in s, f x âˆ‚Î¼ + Î¼ t / (Î¼ s + Î¼ t) * â¨â» x in t, f x âˆ‚Î¼ := by
  rw [restrict_unionâ‚€ hd ht, laverage_add_measure, restrict_apply_univ, restrict_apply_univ]
#align measure_theory.laverage_union MeasureTheory.laverage_union

theorem laverage_union_mem_openSegment (hd : AEDisjoint Î¼ s t) (ht : NullMeasurableSet t Î¼)
    (hsâ‚€ : Î¼ s â‰  0) (htâ‚€ : Î¼ t â‰  0) (hsÎ¼ : Î¼ s â‰  âˆ) (htÎ¼ : Î¼ t â‰  âˆ) :
    â¨â» x in s âˆª t, f x âˆ‚Î¼ âˆˆ openSegment â„â‰¥0âˆ (â¨â» x in s, f x âˆ‚Î¼) (â¨â» x in t, f x âˆ‚Î¼) := by
  refine
    âŸ¨Î¼ s / (Î¼ s + Î¼ t), Î¼ t / (Î¼ s + Î¼ t), ENNReal.div_pos hsâ‚€ <| add_ne_top.2 âŸ¨hsÎ¼, htÎ¼âŸ©,
      ENNReal.div_pos htâ‚€ <| add_ne_top.2 âŸ¨hsÎ¼, htÎ¼âŸ©, ?_, (laverage_union hd ht).symmâŸ©
  rw [â† ENNReal.add_div,
    ENNReal.div_self (add_eq_zero.not.2 fun h => hsâ‚€ h.1) (add_ne_top.2 âŸ¨hsÎ¼, htÎ¼âŸ©)]
#align measure_theory.laverage_union_mem_open_segment MeasureTheory.laverage_union_mem_openSegment

theorem laverage_union_mem_segment (hd : AEDisjoint Î¼ s t) (ht : NullMeasurableSet t Î¼)
    (hsÎ¼ : Î¼ s â‰  âˆ) (htÎ¼ : Î¼ t â‰  âˆ) :
    â¨â» x in s âˆª t, f x âˆ‚Î¼ âˆˆ [â¨â» x in s, f x âˆ‚Î¼ -[â„â‰¥0âˆ] â¨â» x in t, f x âˆ‚Î¼] := by
  by_cases hsâ‚€ : Î¼ s = 0
  Â· rw [â† ae_eq_empty] at hsâ‚€
    rw [restrict_congr_set (hsâ‚€.union EventuallyEq.rfl), empty_union]
    exact right_mem_segment _ _ _
  Â· refine
      âŸ¨Î¼ s / (Î¼ s + Î¼ t), Î¼ t / (Î¼ s + Î¼ t), zero_le _, zero_le _, ?_, (laverage_union hd ht).symmâŸ©
    rw [â† ENNReal.add_div,
      ENNReal.div_self (add_eq_zero.not.2 fun h => hsâ‚€ h.1) (add_ne_top.2 âŸ¨hsÎ¼, htÎ¼âŸ©)]
#align measure_theory.laverage_union_mem_segment MeasureTheory.laverage_union_mem_segment

theorem laverage_mem_openSegment_compl_self [IsFiniteMeasure Î¼] (hs : NullMeasurableSet s Î¼)
    (hsâ‚€ : Î¼ s â‰  0) (hscâ‚€ : Î¼ sá¶œ â‰  0) :
    â¨â» x, f x âˆ‚Î¼ âˆˆ openSegment â„â‰¥0âˆ (â¨â» x in s, f x âˆ‚Î¼) (â¨â» x in sá¶œ, f x âˆ‚Î¼) := by
  simpa only [union_compl_self, restrict_univ] using
    laverage_union_mem_openSegment aedisjoint_compl_right hs.compl hsâ‚€ hscâ‚€ (measure_ne_top _ _)
      (measure_ne_top _ _)
#align measure_theory.laverage_mem_open_segment_compl_self MeasureTheory.laverage_mem_openSegment_compl_self

@[simp]
theorem laverage_const (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] [h : NeZero Î¼] (c : â„â‰¥0âˆ) :
    â¨â» _x, c âˆ‚Î¼ = c := by
  simp only [laverage, lintegral_const, measure_univ, mul_one]
#align measure_theory.laverage_const MeasureTheory.laverage_const

theorem setLaverage_const (hsâ‚€ : Î¼ s â‰  0) (hs : Î¼ s â‰  âˆ) (c : â„â‰¥0âˆ) : â¨â» _x in s, c âˆ‚Î¼ = c := by
  simp only [setLaverage_eq, lintegral_const, Measure.restrict_apply, MeasurableSet.univ,
    univ_inter, div_eq_mul_inv, mul_assoc, ENNReal.mul_inv_cancel hsâ‚€ hs, mul_one]
#align measure_theory.set_laverage_const MeasureTheory.setLaverage_const

theorem laverage_one [IsFiniteMeasure Î¼] [NeZero Î¼] : â¨â» _x, (1 : â„â‰¥0âˆ) âˆ‚Î¼ = 1 :=
  laverage_const _ _
#align measure_theory.laverage_one MeasureTheory.laverage_one

theorem setLaverage_one (hsâ‚€ : Î¼ s â‰  0) (hs : Î¼ s â‰  âˆ) : â¨â» _x in s, (1 : â„â‰¥0âˆ) âˆ‚Î¼ = 1 :=
  setLaverage_const hsâ‚€ hs _
#align measure_theory.set_laverage_one MeasureTheory.setLaverage_one

-- Porting note: Dropped `simp` because of `simp` seeing through `1 : Î± â†’ â„â‰¥0âˆ` and applying
-- `lintegral_const`. This is suboptimal.
theorem lintegral_laverage (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» _x, â¨â» a, f a âˆ‚Î¼ âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by
  obtain rfl | hÎ¼ := eq_or_ne Î¼ 0
  Â· simp
  Â· rw [lintegral_const, laverage_eq,
      ENNReal.div_mul_cancel (measure_univ_ne_zero.2 hÎ¼) (measure_ne_top _ _)]
#align measure_theory.lintegral_laverage MeasureTheory.lintegral_laverage

theorem setLintegral_setLaverage (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î±) :
    âˆ«â» _x in s, â¨â» a in s, f a âˆ‚Î¼ âˆ‚Î¼ = âˆ«â» x in s, f x âˆ‚Î¼ :=
  lintegral_laverage _ _
#align measure_theory.set_lintegral_set_laverage MeasureTheory.setLintegral_setLaverage

end ENNReal

section NormedAddCommGroup

variable (Î¼)
variable {f g : Î± â†’ E}

/-- Average value of a function `f` w.r.t. a measure `Î¼`, denoted `â¨ x, f x âˆ‚Î¼`.

It is equal to `(Î¼ univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼`, so it takes value zero if `f` is not integrable or
if `Î¼` is an infinite measure. If `Î¼` is a probability measure, then the average of any function is
equal to its integral.

For the average on a set, use `â¨ x in s, f x âˆ‚Î¼`, defined as `â¨ x, f x âˆ‚(Î¼.restrict s)`. For the
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
noncomputable def average (f : Î± â†’ E) :=
  âˆ« x, f x âˆ‚(Î¼ univ)â»Â¹ â€¢ Î¼
#align measure_theory.average MeasureTheory.average

/-- Average value of a function `f` w.r.t. a measure `Î¼`.

It is equal to `(Î¼ univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼`, so it takes value zero if `f` is not integrable or
if `Î¼` is an infinite measure. If `Î¼` is a probability measure, then the average of any function is
equal to its integral.

For the average on a set, use `â¨ x in s, f x âˆ‚Î¼`, defined as `â¨ x, f x âˆ‚(Î¼.restrict s)`. For the
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
notation3 "â¨ "(...)", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => average Î¼ r

/-- Average value of a function `f` w.r.t. to the standard measure.

It is equal to `(volume univ).toRealâ»Â¹ * âˆ« x, f x`, so it takes value zero if `f` is not integrable
or if the space has infinite measure. In a probability space, the average of any function is equal
to its integral.

For the average on a set, use `â¨ x in s, f x`, defined as `â¨ x, f x âˆ‚(volume.restrict s)`. -/
notation3 "â¨ "(...)", "r:60:(scoped f => average volume f) => r

/-- Average value of a function `f` w.r.t. a measure `Î¼` on a set `s`.

It is equal to `(Î¼ s).toRealâ»Â¹ * âˆ« x, f x âˆ‚Î¼`, so it takes value zero if `f` is not integrable on
`s` or if `s` has infinite measure. If `s` has measure `1`, then the average of any function is
equal to its integral.

For the average w.r.t. the volume, one can omit `âˆ‚volume`. -/
notation3 "â¨ "(...)" in "s", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => average (Measure.restrict Î¼ s) r

/-- Average value of a function `f` w.r.t. to the standard measure on a set `s`.

It is equal to `(volume s).toRealâ»Â¹ * âˆ« x, f x`, so it takes value zero `f` is not integrable on `s`
or if `s` has infinite measure. If `s` has measure `1`, then the average of any function is equal to
its integral. -/
notation3 "â¨ "(...)" in "s", "r:60:(scoped f => average (Measure.restrict volume s) f) => r

@[simp]
theorem average_zero : â¨ _, (0 : E) âˆ‚Î¼ = 0 := by rw [average, integral_zero]
#align measure_theory.average_zero MeasureTheory.average_zero

@[simp]
theorem average_zero_measure (f : Î± â†’ E) : â¨ x, f x âˆ‚(0 : Measure Î±) = 0 := by
  rw [average, smul_zero, integral_zero_measure]
#align measure_theory.average_zero_measure MeasureTheory.average_zero_measure

@[simp]
theorem average_neg (f : Î± â†’ E) : â¨ x, -f x âˆ‚Î¼ = -â¨ x, f x âˆ‚Î¼ :=
  integral_neg f
#align measure_theory.average_neg MeasureTheory.average_neg

theorem average_eq' (f : Î± â†’ E) : â¨ x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚(Î¼ univ)â»Â¹ â€¢ Î¼ :=
  rfl
#align measure_theory.average_eq' MeasureTheory.average_eq'

theorem average_eq (f : Î± â†’ E) : â¨ x, f x âˆ‚Î¼ = (Î¼ univ).toRealâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rw [average_eq', integral_smul_measure, ENNReal.toReal_inv]
#align measure_theory.average_eq MeasureTheory.average_eq

theorem average_eq_integral [IsProbabilityMeasure Î¼] (f : Î± â†’ E) : â¨ x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  rw [average, measure_univ, inv_one, one_smul]
#align measure_theory.average_eq_integral MeasureTheory.average_eq_integral

@[simp]
theorem measure_smul_average [IsFiniteMeasure Î¼] (f : Î± â†’ E) :
    (Î¼ univ).toReal â€¢ â¨ x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  rcases eq_or_ne Î¼ 0 with hÎ¼ | hÎ¼
  Â· rw [hÎ¼, integral_zero_measure, average_zero_measure, smul_zero]
  Â· rw [average_eq, smul_inv_smulâ‚€]
    refine (ENNReal.toReal_pos ?_ <| measure_ne_top _ _).ne'
    rwa [Ne, measure_univ_eq_zero]
#align measure_theory.measure_smul_average MeasureTheory.measure_smul_average

theorem setAverage_eq (f : Î± â†’ E) (s : Set Î±) :
    â¨ x in s, f x âˆ‚Î¼ = (Î¼ s).toRealâ»Â¹ â€¢ âˆ« x in s, f x âˆ‚Î¼ := by rw [average_eq, restrict_apply_univ]
#align measure_theory.set_average_eq MeasureTheory.setAverage_eq

theorem setAverage_eq' (f : Î± â†’ E) (s : Set Î±) :
    â¨ x in s, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚(Î¼ s)â»Â¹ â€¢ Î¼.restrict s := by
  simp only [average_eq', restrict_apply_univ]
#align measure_theory.set_average_eq' MeasureTheory.setAverage_eq'

variable {Î¼}

theorem average_congr {f g : Î± â†’ E} (h : f =áµ[Î¼] g) : â¨ x, f x âˆ‚Î¼ = â¨ x, g x âˆ‚Î¼ := by
  simp only [average_eq, integral_congr_ae h]
#align measure_theory.average_congr MeasureTheory.average_congr

theorem setAverage_congr (h : s =áµ[Î¼] t) : â¨ x in s, f x âˆ‚Î¼ = â¨ x in t, f x âˆ‚Î¼ := by
  simp only [setAverage_eq, setIntegral_congr_set_ae h, measure_congr h]
#align measure_theory.set_average_congr MeasureTheory.setAverage_congr

theorem setAverage_congr_fun (hs : MeasurableSet s) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = g x) :
    â¨ x in s, f x âˆ‚Î¼ = â¨ x in s, g x âˆ‚Î¼ := by simp only [average_eq, setIntegral_congr_ae hs h]
#align measure_theory.set_average_congr_fun MeasureTheory.setAverage_congr_fun

theorem average_add_measure [IsFiniteMeasure Î¼] {Î½ : Measure Î±} [IsFiniteMeasure Î½] {f : Î± â†’ E}
    (hÎ¼ : Integrable f Î¼) (hÎ½ : Integrable f Î½) :
    â¨ x, f x âˆ‚(Î¼ + Î½) =
      ((Î¼ univ).toReal / ((Î¼ univ).toReal + (Î½ univ).toReal)) â€¢ â¨ x, f x âˆ‚Î¼ +
        ((Î½ univ).toReal / ((Î¼ univ).toReal + (Î½ univ).toReal)) â€¢ â¨ x, f x âˆ‚Î½ := by
  simp only [div_eq_inv_mul, mul_smul, measure_smul_average, â† smul_add,
    â† integral_add_measure hÎ¼ hÎ½, â† ENNReal.toReal_add (measure_ne_top Î¼ _) (measure_ne_top Î½ _)]
  rw [average_eq, Measure.add_apply]
#align measure_theory.average_add_measure MeasureTheory.average_add_measure

theorem average_pair {f : Î± â†’ E} {g : Î± â†’ F} (hfi : Integrable f Î¼) (hgi : Integrable g Î¼) :
    â¨ x, (f x, g x) âˆ‚Î¼ = (â¨ x, f x âˆ‚Î¼, â¨ x, g x âˆ‚Î¼) :=
  integral_pair hfi.to_average hgi.to_average
#align measure_theory.average_pair MeasureTheory.average_pair

theorem measure_smul_setAverage (f : Î± â†’ E) {s : Set Î±} (h : Î¼ s â‰  âˆ) :
    (Î¼ s).toReal â€¢ â¨ x in s, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  haveI := Fact.mk h.lt_top
  rw [â† measure_smul_average, restrict_apply_univ]
#align measure_theory.measure_smul_set_average MeasureTheory.measure_smul_setAverage

theorem average_union {f : Î± â†’ E} {s t : Set Î±} (hd : AEDisjoint Î¼ s t) (ht : NullMeasurableSet t Î¼)
    (hsÎ¼ : Î¼ s â‰  âˆ) (htÎ¼ : Î¼ t â‰  âˆ) (hfs : IntegrableOn f s Î¼) (hft : IntegrableOn f t Î¼) :
    â¨ x in s âˆª t, f x âˆ‚Î¼ =
      ((Î¼ s).toReal / ((Î¼ s).toReal + (Î¼ t).toReal)) â€¢ â¨ x in s, f x âˆ‚Î¼ +
        ((Î¼ t).toReal / ((Î¼ s).toReal + (Î¼ t).toReal)) â€¢ â¨ x in t, f x âˆ‚Î¼ := by
  haveI := Fact.mk hsÎ¼.lt_top; haveI := Fact.mk htÎ¼.lt_top
  rw [restrict_unionâ‚€ hd ht, average_add_measure hfs hft, restrict_apply_univ, restrict_apply_univ]
#align measure_theory.average_union MeasureTheory.average_union

theorem average_union_mem_openSegment {f : Î± â†’ E} {s t : Set Î±} (hd : AEDisjoint Î¼ s t)
    (ht : NullMeasurableSet t Î¼) (hsâ‚€ : Î¼ s â‰  0) (htâ‚€ : Î¼ t â‰  0) (hsÎ¼ : Î¼ s â‰  âˆ) (htÎ¼ : Î¼ t â‰  âˆ)
    (hfs : IntegrableOn f s Î¼) (hft : IntegrableOn f t Î¼) :
    â¨ x in s âˆª t, f x âˆ‚Î¼ âˆˆ openSegment â„ (â¨ x in s, f x âˆ‚Î¼) (â¨ x in t, f x âˆ‚Î¼) := by
  replace hsâ‚€ : 0 < (Î¼ s).toReal := ENNReal.toReal_pos hsâ‚€ hsÎ¼
  replace htâ‚€ : 0 < (Î¼ t).toReal := ENNReal.toReal_pos htâ‚€ htÎ¼
  exact mem_openSegment_iff_div.mpr
    âŸ¨(Î¼ s).toReal, (Î¼ t).toReal, hsâ‚€, htâ‚€, (average_union hd ht hsÎ¼ htÎ¼ hfs hft).symmâŸ©
#align measure_theory.average_union_mem_open_segment MeasureTheory.average_union_mem_openSegment

theorem average_union_mem_segment {f : Î± â†’ E} {s t : Set Î±} (hd : AEDisjoint Î¼ s t)
    (ht : NullMeasurableSet t Î¼) (hsÎ¼ : Î¼ s â‰  âˆ) (htÎ¼ : Î¼ t â‰  âˆ) (hfs : IntegrableOn f s Î¼)
    (hft : IntegrableOn f t Î¼) :
    â¨ x in s âˆª t, f x âˆ‚Î¼ âˆˆ [â¨ x in s, f x âˆ‚Î¼ -[â„] â¨ x in t, f x âˆ‚Î¼] := by
  by_cases hse : Î¼ s = 0
  Â· rw [â† ae_eq_empty] at hse
    rw [restrict_congr_set (hse.union EventuallyEq.rfl), empty_union]
    exact right_mem_segment _ _ _
  Â· refine
      mem_segment_iff_div.mpr
        âŸ¨(Î¼ s).toReal, (Î¼ t).toReal, ENNReal.toReal_nonneg, ENNReal.toReal_nonneg, ?_,
          (average_union hd ht hsÎ¼ htÎ¼ hfs hft).symmâŸ©
    calc
      0 < (Î¼ s).toReal := ENNReal.toReal_pos hse hsÎ¼
      _ â‰¤ _ := le_add_of_nonneg_right ENNReal.toReal_nonneg
#align measure_theory.average_union_mem_segment MeasureTheory.average_union_mem_segment

theorem average_mem_openSegment_compl_self [IsFiniteMeasure Î¼] {f : Î± â†’ E} {s : Set Î±}
    (hs : NullMeasurableSet s Î¼) (hsâ‚€ : Î¼ s â‰  0) (hscâ‚€ : Î¼ sá¶œ â‰  0) (hfi : Integrable f Î¼) :
    â¨ x, f x âˆ‚Î¼ âˆˆ openSegment â„ (â¨ x in s, f x âˆ‚Î¼) (â¨ x in sá¶œ, f x âˆ‚Î¼) := by
  simpa only [union_compl_self, restrict_univ] using
    average_union_mem_openSegment aedisjoint_compl_right hs.compl hsâ‚€ hscâ‚€ (measure_ne_top _ _)
      (measure_ne_top _ _) hfi.integrableOn hfi.integrableOn
#align measure_theory.average_mem_open_segment_compl_self MeasureTheory.average_mem_openSegment_compl_self

@[simp]
theorem average_const (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] [h : NeZero Î¼] (c : E) :
    â¨ _x, c âˆ‚Î¼ = c := by
  rw [average, integral_const, measure_univ, ENNReal.one_toReal, one_smul]
#align measure_theory.average_const MeasureTheory.average_const

theorem setAverage_const {s : Set Î±} (hsâ‚€ : Î¼ s â‰  0) (hs : Î¼ s â‰  âˆ) (c : E) :
    â¨ _ in s, c âˆ‚Î¼ = c :=
  have := NeZero.mk hsâ‚€; have := Fact.mk hs.lt_top; average_const _ _
#align measure_theory.set_average_const MeasureTheory.setAverage_const

-- Porting note (#10618): was `@[simp]` but `simp` can prove it
theorem integral_average (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (f : Î± â†’ E) :
    âˆ« _, â¨ a, f a âˆ‚Î¼ âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by simp
#align measure_theory.integral_average MeasureTheory.integral_average

theorem setIntegral_setAverage (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (f : Î± â†’ E) (s : Set Î±) :
    âˆ« _ in s, â¨ a in s, f a âˆ‚Î¼ âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
  integral_average _ _
#align measure_theory.set_integral_set_average MeasureTheory.setIntegral_setAverage

theorem integral_sub_average (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (f : Î± â†’ E) :
    âˆ« x, f x - â¨ a, f a âˆ‚Î¼ âˆ‚Î¼ = 0 := by
  by_cases hf : Integrable f Î¼
  Â· rw [integral_sub hf (integrable_const _), integral_average, sub_self]
  refine integral_undef fun h => hf ?_
  convert h.add (integrable_const (â¨ a, f a âˆ‚Î¼))
  exact (sub_add_cancel _ _).symm
#align measure_theory.integral_sub_average MeasureTheory.integral_sub_average

theorem setAverage_sub_setAverage (hs : Î¼ s â‰  âˆ) (f : Î± â†’ E) :
    âˆ« x in s, f x - â¨ a in s, f a âˆ‚Î¼ âˆ‚Î¼ = 0 :=
  haveI : Fact (Î¼ s < âˆ) := âŸ¨lt_top_iff_ne_top.2 hsâŸ©
  integral_sub_average _ _
#align measure_theory.set_integral_sub_set_average MeasureTheory.setAverage_sub_setAverage

theorem integral_average_sub [IsFiniteMeasure Î¼] (hf : Integrable f Î¼) :
    âˆ« x, â¨ a, f a âˆ‚Î¼ - f x âˆ‚Î¼ = 0 := by
  rw [integral_sub (integrable_const _) hf, integral_average, sub_self]
#align measure_theory.integral_average_sub MeasureTheory.integral_average_sub

theorem setIntegral_setAverage_sub (hs : Î¼ s â‰  âˆ) (hf : IntegrableOn f s Î¼) :
    âˆ« x in s, â¨ a in s, f a âˆ‚Î¼ - f x âˆ‚Î¼ = 0 :=
  haveI : Fact (Î¼ s < âˆ) := âŸ¨lt_top_iff_ne_top.2 hsâŸ©
  integral_average_sub hf
#align measure_theory.set_integral_set_average_sub MeasureTheory.setIntegral_setAverage_sub

end NormedAddCommGroup

theorem ofReal_average {f : Î± â†’ â„} (hf : Integrable f Î¼) (hfâ‚€ : 0 â‰¤áµ[Î¼] f) :
    ENNReal.ofReal (â¨ x, f x âˆ‚Î¼) = (âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼) / Î¼ univ := by
  obtain rfl | hÎ¼ := eq_or_ne Î¼ 0
  Â· simp
  Â· rw [average_eq, smul_eq_mul, â† toReal_inv, ofReal_mul toReal_nonneg,
      ofReal_toReal (inv_ne_top.2 <| measure_univ_ne_zero.2 hÎ¼),
      ofReal_integral_eq_lintegral_ofReal hf hfâ‚€, ENNReal.div_eq_inv_mul]
#align measure_theory.of_real_average MeasureTheory.ofReal_average

theorem ofReal_setAverage {f : Î± â†’ â„} (hf : IntegrableOn f s Î¼) (hfâ‚€ : 0 â‰¤áµ[Î¼.restrict s] f) :
    ENNReal.ofReal (â¨ x in s, f x âˆ‚Î¼) = (âˆ«â» x in s, ENNReal.ofReal (f x) âˆ‚Î¼) / Î¼ s := by
  simpa using ofReal_average hf hfâ‚€
#align measure_theory.of_real_set_average MeasureTheory.ofReal_setAverage

theorem toReal_laverage {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hf' : âˆ€áµ x âˆ‚Î¼, f x â‰  âˆ) :
    (â¨â» x, f x âˆ‚Î¼).toReal = â¨ x, (f x).toReal âˆ‚Î¼ := by
    rw [average_eq, laverage_eq, smul_eq_mul, toReal_div, div_eq_inv_mul, â†
      integral_toReal hf (hf'.mono fun _ => lt_top_iff_ne_top.2)]
#align measure_theory.to_real_laverage MeasureTheory.toReal_laverage

theorem toReal_setLaverage {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f (Î¼.restrict s))
    (hf' : âˆ€áµ x âˆ‚Î¼.restrict s, f x â‰  âˆ) :
    (â¨â» x in s, f x âˆ‚Î¼).toReal = â¨ x in s, (f x).toReal âˆ‚Î¼ := by
  simpa [laverage_eq] using toReal_laverage hf hf'
#align measure_theory.to_real_set_laverage MeasureTheory.toReal_setLaverage

/-! ### First moment method -/

section FirstMomentReal
variable {N : Set Î±} {f : Î± â†’ â„}

/-- **First moment method**. An integrable function is smaller than its mean on a set of positive
measure. -/
theorem measure_le_setAverage_pos (hÎ¼ : Î¼ s â‰  0) (hÎ¼â‚ : Î¼ s â‰  âˆ) (hf : IntegrableOn f s Î¼) :
    0 < Î¼ ({x âˆˆ s | f x â‰¤ â¨ a in s, f a âˆ‚Î¼}) := by
  refine pos_iff_ne_zero.2 fun H => ?_
  replace H : (Î¼.restrict s) {x | f x â‰¤ â¨ a in s, f a âˆ‚Î¼} = 0 := by
    rwa [restrict_applyâ‚€, inter_comm]
    exact AEStronglyMeasurable.nullMeasurableSet_le hf.1 aestronglyMeasurable_const
  haveI := Fact.mk hÎ¼â‚.lt_top
  refine (integral_sub_average (Î¼.restrict s) f).not_gt ?_
  refine (setIntegral_pos_iff_support_of_nonneg_ae ?_ ?_).2 ?_
  Â· refine measure_mono_null (fun x hx â†¦ ?_) H
    simp only [Pi.zero_apply, sub_nonneg, mem_compl_iff, mem_setOf_eq, not_le] at hx
    exact hx.le
  Â· exact hf.sub (integrableOn_const.2 <| Or.inr <| lt_top_iff_ne_top.2 hÎ¼â‚)
  Â· rwa [pos_iff_ne_zero, inter_comm, â† diff_compl, â† diff_inter_self_eq_diff, measure_diff_null]
    refine measure_mono_null ?_ (measure_inter_eq_zero_of_restrict H)
    exact inter_subset_inter_left _ fun a ha => (sub_eq_zero.1 <| of_not_not ha).le
#align measure_theory.measure_le_set_average_pos MeasureTheory.measure_le_setAverage_pos

/-- **First moment method**. An integrable function is greater than its mean on a set of positive
measure. -/
theorem measure_setAverage_le_pos (hÎ¼ : Î¼ s â‰  0) (hÎ¼â‚ : Î¼ s â‰  âˆ) (hf : IntegrableOn f s Î¼) :
    0 < Î¼ ({x âˆˆ s | â¨ a in s, f a âˆ‚Î¼ â‰¤ f x}) := by
  simpa [integral_neg, neg_div] using measure_le_setAverage_pos hÎ¼ hÎ¼â‚ hf.neg
#align measure_theory.measure_set_average_le_pos MeasureTheory.measure_setAverage_le_pos

/-- **First moment method**. The minimum of an integrable function is smaller than its mean. -/
theorem exists_le_setAverage (hÎ¼ : Î¼ s â‰  0) (hÎ¼â‚ : Î¼ s â‰  âˆ) (hf : IntegrableOn f s Î¼) :
    âˆƒ x âˆˆ s, f x â‰¤ â¨ a in s, f a âˆ‚Î¼ :=
  let âŸ¨x, hx, hâŸ© := nonempty_of_measure_ne_zero (measure_le_setAverage_pos hÎ¼ hÎ¼â‚ hf).ne'
  âŸ¨x, hx, hâŸ©
#align measure_theory.exists_le_set_average MeasureTheory.exists_le_setAverage

/-- **First moment method**. The maximum of an integrable function is greater than its mean. -/
theorem exists_setAverage_le (hÎ¼ : Î¼ s â‰  0) (hÎ¼â‚ : Î¼ s â‰  âˆ) (hf : IntegrableOn f s Î¼) :
    âˆƒ x âˆˆ s, â¨ a in s, f a âˆ‚Î¼ â‰¤ f x :=
  let âŸ¨x, hx, hâŸ© := nonempty_of_measure_ne_zero (measure_setAverage_le_pos hÎ¼ hÎ¼â‚ hf).ne'
  âŸ¨x, hx, hâŸ©
#align measure_theory.exists_set_average_le MeasureTheory.exists_setAverage_le

section FiniteMeasure

variable [IsFiniteMeasure Î¼]

/-- **First moment method**. An integrable function is smaller than its mean on a set of positive
measure. -/
theorem measure_le_average_pos (hÎ¼ : Î¼ â‰  0) (hf : Integrable f Î¼) :
    0 < Î¼ {x | f x â‰¤ â¨ a, f a âˆ‚Î¼} := by
  simpa using measure_le_setAverage_pos (Measure.measure_univ_ne_zero.2 hÎ¼) (measure_ne_top _ _)
    hf.integrableOn
#align measure_theory.measure_le_average_pos MeasureTheory.measure_le_average_pos

/-- **First moment method**. An integrable function is greater than its mean on a set of positive
measure. -/
theorem measure_average_le_pos (hÎ¼ : Î¼ â‰  0) (hf : Integrable f Î¼) :
    0 < Î¼ {x | â¨ a, f a âˆ‚Î¼ â‰¤ f x} := by
  simpa using measure_setAverage_le_pos (Measure.measure_univ_ne_zero.2 hÎ¼) (measure_ne_top _ _)
    hf.integrableOn
#align measure_theory.measure_average_le_pos MeasureTheory.measure_average_le_pos

/-- **First moment method**. The minimum of an integrable function is smaller than its mean. -/
theorem exists_le_average (hÎ¼ : Î¼ â‰  0) (hf : Integrable f Î¼) : âˆƒ x, f x â‰¤ â¨ a, f a âˆ‚Î¼ :=
  let âŸ¨x, hxâŸ© := nonempty_of_measure_ne_zero (measure_le_average_pos hÎ¼ hf).ne'
  âŸ¨x, hxâŸ©
#align measure_theory.exists_le_average MeasureTheory.exists_le_average

/-- **First moment method**. The maximum of an integrable function is greater than its mean. -/
theorem exists_average_le (hÎ¼ : Î¼ â‰  0) (hf : Integrable f Î¼) : âˆƒ x, â¨ a, f a âˆ‚Î¼ â‰¤ f x :=
  let âŸ¨x, hxâŸ© := nonempty_of_measure_ne_zero (measure_average_le_pos hÎ¼ hf).ne'
  âŸ¨x, hxâŸ©
#align measure_theory.exists_average_le MeasureTheory.exists_average_le

/-- **First moment method**. The minimum of an integrable function is smaller than its mean, while
avoiding a null set. -/
theorem exists_not_mem_null_le_average (hÎ¼ : Î¼ â‰  0) (hf : Integrable f Î¼) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ f x â‰¤ â¨ a, f a âˆ‚Î¼ := by
  have := measure_le_average_pos hÎ¼ hf
  rw [â† measure_diff_null hN] at this
  obtain âŸ¨x, hx, hxNâŸ© := nonempty_of_measure_ne_zero this.ne'
  exact âŸ¨x, hxN, hxâŸ©
#align measure_theory.exists_not_mem_null_le_average MeasureTheory.exists_not_mem_null_le_average

/-- **First moment method**. The maximum of an integrable function is greater than its mean, while
avoiding a null set. -/
theorem exists_not_mem_null_average_le (hÎ¼ : Î¼ â‰  0) (hf : Integrable f Î¼) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ â¨ a, f a âˆ‚Î¼ â‰¤ f x := by
  simpa [integral_neg, neg_div] using exists_not_mem_null_le_average hÎ¼ hf.neg hN
#align measure_theory.exists_not_mem_null_average_le MeasureTheory.exists_not_mem_null_average_le

end FiniteMeasure

section ProbabilityMeasure

variable [IsProbabilityMeasure Î¼]

/-- **First moment method**. An integrable function is smaller than its integral on a set of
positive measure. -/
theorem measure_le_integral_pos (hf : Integrable f Î¼) : 0 < Î¼ {x | f x â‰¤ âˆ« a, f a âˆ‚Î¼} := by
  simpa only [average_eq_integral] using
    measure_le_average_pos (IsProbabilityMeasure.ne_zero Î¼) hf
#align measure_theory.measure_le_integral_pos MeasureTheory.measure_le_integral_pos

/-- **First moment method**. An integrable function is greater than its integral on a set of
positive measure. -/
theorem measure_integral_le_pos (hf : Integrable f Î¼) : 0 < Î¼ {x | âˆ« a, f a âˆ‚Î¼ â‰¤ f x} := by
  simpa only [average_eq_integral] using
    measure_average_le_pos (IsProbabilityMeasure.ne_zero Î¼) hf
#align measure_theory.measure_integral_le_pos MeasureTheory.measure_integral_le_pos

/-- **First moment method**. The minimum of an integrable function is smaller than its integral. -/
theorem exists_le_integral (hf : Integrable f Î¼) : âˆƒ x, f x â‰¤ âˆ« a, f a âˆ‚Î¼ := by
  simpa only [average_eq_integral] using exists_le_average (IsProbabilityMeasure.ne_zero Î¼) hf
#align measure_theory.exists_le_integral MeasureTheory.exists_le_integral

/-- **First moment method**. The maximum of an integrable function is greater than its integral. -/
theorem exists_integral_le (hf : Integrable f Î¼) : âˆƒ x, âˆ« a, f a âˆ‚Î¼ â‰¤ f x := by
  simpa only [average_eq_integral] using exists_average_le (IsProbabilityMeasure.ne_zero Î¼) hf
#align measure_theory.exists_integral_le MeasureTheory.exists_integral_le

/-- **First moment method**. The minimum of an integrable function is smaller than its integral,
while avoiding a null set. -/
theorem exists_not_mem_null_le_integral (hf : Integrable f Î¼) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ f x â‰¤ âˆ« a, f a âˆ‚Î¼ := by
  simpa only [average_eq_integral] using
    exists_not_mem_null_le_average (IsProbabilityMeasure.ne_zero Î¼) hf hN
#align measure_theory.exists_not_mem_null_le_integral MeasureTheory.exists_not_mem_null_le_integral

/-- **First moment method**. The maximum of an integrable function is greater than its integral,
while avoiding a null set. -/
theorem exists_not_mem_null_integral_le (hf : Integrable f Î¼) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ âˆ« a, f a âˆ‚Î¼ â‰¤ f x := by
  simpa only [average_eq_integral] using
    exists_not_mem_null_average_le (IsProbabilityMeasure.ne_zero Î¼) hf hN
#align measure_theory.exists_not_mem_null_integral_le MeasureTheory.exists_not_mem_null_integral_le

end ProbabilityMeasure
end FirstMomentReal

section FirstMomentENNReal
variable {N : Set Î±} {f : Î± â†’ â„â‰¥0âˆ}

/-- **First moment method**. A measurable function is smaller than its mean on a set of positive
measure. -/
theorem measure_le_setLaverage_pos (hÎ¼ : Î¼ s â‰  0) (hÎ¼â‚ : Î¼ s â‰  âˆ)
    (hf : AEMeasurable f (Î¼.restrict s)) : 0 < Î¼ {x âˆˆ s | f x â‰¤ â¨â» a in s, f a âˆ‚Î¼} := by
  obtain h | h := eq_or_ne (âˆ«â» a in s, f a âˆ‚Î¼) âˆ
  Â· simpa [mul_top, hÎ¼â‚, laverage, h, top_div_of_ne_top hÎ¼â‚, pos_iff_ne_zero] using hÎ¼
  have := measure_le_setAverage_pos hÎ¼ hÎ¼â‚ (integrable_toReal_of_lintegral_ne_top hf h)
  rw [â† setOf_inter_eq_sep, â† Measure.restrict_applyâ‚€
    (hf.aestronglyMeasurable.nullMeasurableSet_le aestronglyMeasurable_const)]
  rw [â† setOf_inter_eq_sep, â† Measure.restrict_applyâ‚€
    (hf.ennreal_toReal.aestronglyMeasurable.nullMeasurableSet_le aestronglyMeasurable_const),
    â† measure_diff_null (measure_eq_top_of_lintegral_ne_top hf h)] at this
  refine this.trans_le (measure_mono ?_)
  rintro x âŸ¨hfx, hxâŸ©
  dsimp at hfx
  rwa [â† toReal_laverage hf, toReal_le_toReal hx (setLaverage_lt_top h).ne] at hfx
  simp_rw [ae_iff, not_ne_iff]
  exact measure_eq_top_of_lintegral_ne_top hf h
#align measure_theory.measure_le_set_laverage_pos MeasureTheory.measure_le_setLaverage_pos

/-- **First moment method**. A measurable function is greater than its mean on a set of positive
measure. -/
theorem measure_setLaverage_le_pos (hÎ¼ : Î¼ s â‰  0) (hs : NullMeasurableSet s Î¼)
    (hint : âˆ«â» a in s, f a âˆ‚Î¼ â‰  âˆ) : 0 < Î¼ {x âˆˆ s | â¨â» a in s, f a âˆ‚Î¼ â‰¤ f x} := by
  obtain hÎ¼â‚ | hÎ¼â‚ := eq_or_ne (Î¼ s) âˆ
  Â· simp [setLaverage_eq, hÎ¼â‚]
  obtain âŸ¨g, hg, hgf, hfgâŸ© := exists_measurable_le_lintegral_eq (Î¼.restrict s) f
  have hfg' : â¨â» a in s, f a âˆ‚Î¼ = â¨â» a in s, g a âˆ‚Î¼ := by simp_rw [laverage_eq, hfg]
  rw [hfg] at hint
  have :=
    measure_setAverage_le_pos hÎ¼ hÎ¼â‚ (integrable_toReal_of_lintegral_ne_top hg.aemeasurable hint)
  simp_rw [â† setOf_inter_eq_sep, â† Measure.restrict_applyâ‚€' hs, hfg']
  rw [â† setOf_inter_eq_sep, â† Measure.restrict_applyâ‚€' hs, â†
    measure_diff_null (measure_eq_top_of_lintegral_ne_top hg.aemeasurable hint)] at this
  refine this.trans_le (measure_mono ?_)
  rintro x âŸ¨hfx, hxâŸ©
  dsimp at hfx
  rw [â† toReal_laverage hg.aemeasurable, toReal_le_toReal (setLaverage_lt_top hint).ne hx] at hfx
  Â· exact hfx.trans (hgf _)
  Â· simp_rw [ae_iff, not_ne_iff]
    exact measure_eq_top_of_lintegral_ne_top hg.aemeasurable hint
#align measure_theory.measure_set_laverage_le_pos MeasureTheory.measure_setLaverage_le_pos

/-- **First moment method**. The minimum of a measurable function is smaller than its mean. -/
theorem exists_le_setLaverage (hÎ¼ : Î¼ s â‰  0) (hÎ¼â‚ : Î¼ s â‰  âˆ) (hf : AEMeasurable f (Î¼.restrict s)) :
    âˆƒ x âˆˆ s, f x â‰¤ â¨â» a in s, f a âˆ‚Î¼ :=
  let âŸ¨x, hx, hâŸ© := nonempty_of_measure_ne_zero (measure_le_setLaverage_pos hÎ¼ hÎ¼â‚ hf).ne'
  âŸ¨x, hx, hâŸ©
#align measure_theory.exists_le_set_laverage MeasureTheory.exists_le_setLaverage

/-- **First moment method**. The maximum of a measurable function is greater than its mean. -/
theorem exists_setLaverage_le (hÎ¼ : Î¼ s â‰  0) (hs : NullMeasurableSet s Î¼)
    (hint : âˆ«â» a in s, f a âˆ‚Î¼ â‰  âˆ) : âˆƒ x âˆˆ s, â¨â» a in s, f a âˆ‚Î¼ â‰¤ f x :=
  let âŸ¨x, hx, hâŸ© := nonempty_of_measure_ne_zero (measure_setLaverage_le_pos hÎ¼ hs hint).ne'
  âŸ¨x, hx, hâŸ©
#align measure_theory.exists_set_laverage_le MeasureTheory.exists_setLaverage_le

/-- **First moment method**. A measurable function is greater than its mean on a set of positive
measure. -/
theorem measure_laverage_le_pos (hÎ¼ : Î¼ â‰  0) (hint : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ) :
    0 < Î¼ {x | â¨â» a, f a âˆ‚Î¼ â‰¤ f x} := by
  simpa [hint] using
    @measure_setLaverage_le_pos _ _ _ _ f (measure_univ_ne_zero.2 hÎ¼) nullMeasurableSet_univ
#align measure_theory.measure_laverage_le_pos MeasureTheory.measure_laverage_le_pos

/-- **First moment method**. The maximum of a measurable function is greater than its mean. -/
theorem exists_laverage_le (hÎ¼ : Î¼ â‰  0) (hint : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ) : âˆƒ x, â¨â» a, f a âˆ‚Î¼ â‰¤ f x :=
  let âŸ¨x, hxâŸ© := nonempty_of_measure_ne_zero (measure_laverage_le_pos hÎ¼ hint).ne'
  âŸ¨x, hxâŸ©
#align measure_theory.exists_laverage_le MeasureTheory.exists_laverage_le

/-- **First moment method**. The maximum of a measurable function is greater than its mean, while
avoiding a null set. -/
theorem exists_not_mem_null_laverage_le (hÎ¼ : Î¼ â‰  0) (hint : âˆ«â» a : Î±, f a âˆ‚Î¼ â‰  âˆ) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ â¨â» a, f a âˆ‚Î¼ â‰¤ f x := by
  have := measure_laverage_le_pos hÎ¼ hint
  rw [â† measure_diff_null hN] at this
  obtain âŸ¨x, hx, hxNâŸ© := nonempty_of_measure_ne_zero this.ne'
  exact âŸ¨x, hxN, hxâŸ©
#align measure_theory.exists_not_mem_null_laverage_le MeasureTheory.exists_not_mem_null_laverage_le

section FiniteMeasure
variable [IsFiniteMeasure Î¼]

/-- **First moment method**. A measurable function is smaller than its mean on a set of positive
measure. -/
theorem measure_le_laverage_pos (hÎ¼ : Î¼ â‰  0) (hf : AEMeasurable f Î¼) :
    0 < Î¼ {x | f x â‰¤ â¨â» a, f a âˆ‚Î¼} := by
  simpa using
    measure_le_setLaverage_pos (measure_univ_ne_zero.2 hÎ¼) (measure_ne_top _ _) hf.restrict
#align measure_theory.measure_le_laverage_pos MeasureTheory.measure_le_laverage_pos

/-- **First moment method**. The minimum of a measurable function is smaller than its mean. -/
theorem exists_le_laverage (hÎ¼ : Î¼ â‰  0) (hf : AEMeasurable f Î¼) : âˆƒ x, f x â‰¤ â¨â» a, f a âˆ‚Î¼ :=
  let âŸ¨x, hxâŸ© := nonempty_of_measure_ne_zero (measure_le_laverage_pos hÎ¼ hf).ne'
  âŸ¨x, hxâŸ©
#align measure_theory.exists_le_laverage MeasureTheory.exists_le_laverage

/-- **First moment method**. The minimum of a measurable function is smaller than its mean, while
avoiding a null set. -/
theorem exists_not_mem_null_le_laverage (hÎ¼ : Î¼ â‰  0) (hf : AEMeasurable f Î¼) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ f x â‰¤ â¨â» a, f a âˆ‚Î¼ := by
  have := measure_le_laverage_pos hÎ¼ hf
  rw [â† measure_diff_null hN] at this
  obtain âŸ¨x, hx, hxNâŸ© := nonempty_of_measure_ne_zero this.ne'
  exact âŸ¨x, hxN, hxâŸ©
#align measure_theory.exists_not_mem_null_le_laverage MeasureTheory.exists_not_mem_null_le_laverage

end FiniteMeasure

section ProbabilityMeasure

variable [IsProbabilityMeasure Î¼]

/-- **First moment method**. A measurable function is smaller than its integral on a set f
positive measure. -/
theorem measure_le_lintegral_pos (hf : AEMeasurable f Î¼) : 0 < Î¼ {x | f x â‰¤ âˆ«â» a, f a âˆ‚Î¼} := by
  simpa only [laverage_eq_lintegral] using
    measure_le_laverage_pos (IsProbabilityMeasure.ne_zero Î¼) hf
#align measure_theory.measure_le_lintegral_pos MeasureTheory.measure_le_lintegral_pos

/-- **First moment method**. A measurable function is greater than its integral on a set f
positive measure. -/
theorem measure_lintegral_le_pos (hint : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ) : 0 < Î¼ {x | âˆ«â» a, f a âˆ‚Î¼ â‰¤ f x} := by
  simpa only [laverage_eq_lintegral] using
    measure_laverage_le_pos (IsProbabilityMeasure.ne_zero Î¼) hint
#align measure_theory.measure_lintegral_le_pos MeasureTheory.measure_lintegral_le_pos

/-- **First moment method**. The minimum of a measurable function is smaller than its integral. -/
theorem exists_le_lintegral (hf : AEMeasurable f Î¼) : âˆƒ x, f x â‰¤ âˆ«â» a, f a âˆ‚Î¼ := by
  simpa only [laverage_eq_lintegral] using exists_le_laverage (IsProbabilityMeasure.ne_zero Î¼) hf
#align measure_theory.exists_le_lintegral MeasureTheory.exists_le_lintegral

/-- **First moment method**. The maximum of a measurable function is greater than its integral. -/
theorem exists_lintegral_le (hint : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ) : âˆƒ x, âˆ«â» a, f a âˆ‚Î¼ â‰¤ f x := by
  simpa only [laverage_eq_lintegral] using
    exists_laverage_le (IsProbabilityMeasure.ne_zero Î¼) hint
#align measure_theory.exists_lintegral_le MeasureTheory.exists_lintegral_le

/-- **First moment method**. The minimum of a measurable function is smaller than its integral,
while avoiding a null set. -/
theorem exists_not_mem_null_le_lintegral (hf : AEMeasurable f Î¼) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ f x â‰¤ âˆ«â» a, f a âˆ‚Î¼ := by
  simpa only [laverage_eq_lintegral] using
    exists_not_mem_null_le_laverage (IsProbabilityMeasure.ne_zero Î¼) hf hN
#align measure_theory.exists_not_mem_null_le_lintegral MeasureTheory.exists_not_mem_null_le_lintegral

/-- **First moment method**. The maximum of a measurable function is greater than its integral,
while avoiding a null set. -/
theorem exists_not_mem_null_lintegral_le (hint : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ) (hN : Î¼ N = 0) :
    âˆƒ x, x âˆ‰ N âˆ§ âˆ«â» a, f a âˆ‚Î¼ â‰¤ f x := by
  simpa only [laverage_eq_lintegral] using
    exists_not_mem_null_laverage_le (IsProbabilityMeasure.ne_zero Î¼) hint hN
#align measure_theory.exists_not_mem_null_lintegral_le MeasureTheory.exists_not_mem_null_lintegral_le

end ProbabilityMeasure
end FirstMomentENNReal

/-- If the average of a function `f` along a sequence of sets `aâ‚™` converges to `c` (more precisely,
we require that `â¨ y in a i, â€–f y - câ€– âˆ‚Î¼` tends to `0`), then the integral of `gâ‚™ â€¢ f` also tends
to `c` if `gâ‚™` is supported in `aâ‚™`, has integral converging to one and supremum at most `K / Î¼ aâ‚™`.
-/
theorem tendsto_integral_smul_of_tendsto_average_norm_sub
    {Î¹ : Type*} {a : Î¹ â†’ Set Î±} {l : Filter Î¹} {f : Î± â†’ E} {c : E} {g : Î¹ â†’ Î± â†’ â„} (K : â„)
    (hf : Tendsto (fun i â†¦ â¨ y in a i, â€–f y - câ€– âˆ‚Î¼) l (ğ“ 0))
    (f_int : âˆ€á¶  i in l, IntegrableOn f (a i) Î¼)
    (hg : Tendsto (fun i â†¦ âˆ« y, g i y âˆ‚Î¼) l (ğ“ 1))
    (g_supp : âˆ€á¶  i in l, Function.support (g i) âŠ† a i)
    (g_bound : âˆ€á¶  i in l, âˆ€ x, |g i x| â‰¤ K / (Î¼ (a i)).toReal) :
    Tendsto (fun i â†¦ âˆ« y, g i y â€¢ f y âˆ‚Î¼) l (ğ“ c) := by
  have g_int : âˆ€á¶  i in l, Integrable (g i) Î¼ := by
    filter_upwards [(tendsto_order.1 hg).1 _ zero_lt_one] with i hi
    contrapose hi
    simp only [integral_undef hi, lt_self_iff_false, not_false_eq_true]
  have I : âˆ€á¶  i in l, âˆ« y, g i y â€¢ (f y - c) âˆ‚Î¼ + (âˆ« y, g i y âˆ‚Î¼) â€¢ c = âˆ« y, g i y â€¢ f y âˆ‚Î¼ := by
    filter_upwards [f_int, g_int, g_supp, g_bound] with i hif hig hisupp hibound
    rw [â† integral_smul_const, â† integral_add]
    Â· simp only [smul_sub, sub_add_cancel]
    Â· simp_rw [smul_sub]
      apply Integrable.sub _ (hig.smul_const _)
      have A : Function.support (fun y â†¦ g i y â€¢ f y) âŠ† a i := by
        apply Subset.trans _ hisupp
        exact Function.support_smul_subset_left _ _
      rw [â† integrableOn_iff_integrable_of_support_subset A]
      apply Integrable.smul_of_top_right hif
      exact memâ„’p_top_of_bound hig.aestronglyMeasurable.restrict
        (K / (Î¼ (a i)).toReal) (eventually_of_forall hibound)
    Â· exact hig.smul_const _
  have L0 : Tendsto (fun i â†¦ âˆ« y, g i y â€¢ (f y - c) âˆ‚Î¼) l (ğ“ 0) := by
    have := hf.const_mul K
    simp only [mul_zero] at this
    refine squeeze_zero_norm' ?_ this
    filter_upwards [g_supp, g_bound, f_int, (tendsto_order.1 hg).1 _ zero_lt_one]
      with i hi h'i h''i hi_int
    have mu_ai : Î¼ (a i) < âˆ := by
      rw [lt_top_iff_ne_top]
      intro h
      simp only [h, ENNReal.top_toReal, _root_.div_zero, abs_nonpos_iff] at h'i
      have : âˆ« (y : Î±), g i y âˆ‚Î¼ = âˆ« (y : Î±), 0 âˆ‚Î¼ := by congr; ext y; exact h'i y
      simp [this] at hi_int
    apply (norm_integral_le_integral_norm _).trans
    simp_rw [average_eq, smul_eq_mul, â† integral_mul_left, norm_smul, â† mul_assoc, â† div_eq_mul_inv]
    have : âˆ€ x, x âˆ‰ a i â†’ â€–g i xâ€– * â€–(f x - c)â€– = 0 := by
      intro x hx
      have : g i x = 0 := by rw [â† Function.nmem_support]; exact fun h â†¦ hx (hi h)
      simp [this]
    rw [â† setIntegral_eq_integral_of_forall_compl_eq_zero this (Î¼ := Î¼)]
    refine integral_mono_of_nonneg (eventually_of_forall (fun x â†¦ by positivity)) ?_
      (eventually_of_forall (fun x â†¦ ?_))
    Â· apply (Integrable.sub h''i _).norm.const_mul
      change IntegrableOn (fun _ â†¦ c) (a i) Î¼
      simp [integrableOn_const, mu_ai]
    Â· dsimp; gcongr; simpa using h'i x
  have := L0.add (hg.smul_const c)
  simp only [one_smul, zero_add] at this
  exact Tendsto.congr' I this

end MeasureTheory
