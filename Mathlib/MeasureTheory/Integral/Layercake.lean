/-
Copyright (c) 2022 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# The layer cake formula / Cavalieri's principle / tail probability formula

In this file we prove the following layer cake formula.

Consider a non-negative measurable function `f` on a measure space. Apply pointwise
to it an increasing absolutely continuous function `G : â„â‰¥0 â†’ â„â‰¥0` vanishing at the origin, with
derivative `G' = g` on the positive real line (in other words, `G` a primitive of a non-negative
locally integrable function `g` on the positive real line). Then the integral of the result,
`âˆ« G âˆ˜ f`, can be written as the integral over the positive real line of the "tail measures" of `f`
(i.e., a function giving the measures of the sets on which `f` exceeds different positive real
values) weighted by `g`. In probability theory contexts, the "tail measures" could be referred to
as "tail probabilities" of the random variable `f`, or as values of the "complementary cumulative
distribution function" of the random variable `f`. The terminology "tail probability formula" is
therefore occasionally used for the layer cake formula (or a standard application of it).

The essence of the (mathematical) proof is Fubini's theorem.

We also give the most common application of the layer cake formula -
a representation of the integral of a nonnegative function f:
âˆ« f(Ï‰) âˆ‚Î¼(Ï‰) = âˆ« Î¼ {Ï‰ | f(Ï‰) â‰¥ t} dt

Variants of the formulas with measures of sets of the form {Ï‰ | f(Ï‰) > t} instead of {Ï‰ | f(Ï‰) â‰¥ t}
are also included.

## Main results

* `MeasureTheory.lintegral_comp_eq_lintegral_meas_le_mul`
  and `MeasureTheory.lintegral_comp_eq_lintegral_meas_lt_mul`:
  The general layer cake formulas with Lebesgue integrals, written in terms of measures of
  sets of the forms {Ï‰ | t â‰¤ f(Ï‰)} and {Ï‰ | t < f(Ï‰)}, respectively.
* `MeasureTheory.lintegral_eq_lintegral_meas_le` and
  `MeasureTheory.lintegral_eq_lintegral_meas_lt`:
  The most common special cases of the layer cake formulas, stating that for a nonnegative
  function f we have âˆ« f(Ï‰) âˆ‚Î¼(Ï‰) = âˆ« Î¼ {Ï‰ | f(Ï‰) â‰¥ t} dt and
  âˆ« f(Ï‰) âˆ‚Î¼(Ï‰) = âˆ« Î¼ {Ï‰ | f(Ï‰) > t} dt, respectively.
* `Integrable.integral_eq_integral_meas_lt`:
  A Bochner integral version of the most common special case of the layer cake formulas, stating
  that for an integrable and a.e.-nonnegative function f we have
  âˆ« f(Ï‰) âˆ‚Î¼(Ï‰) = âˆ« Î¼ {Ï‰ | f(Ï‰) > t} dt.

## See also

Another common application, a representation of the integral of a real power of a nonnegative
function, is given in `Mathlib/Analysis/SpecialFunctions/Pow/Integral.lean`.

## Tags

layer cake representation, Cavalieri's principle, tail probability formula
-/

noncomputable section

open scoped ENNReal MeasureTheory Topology

open Set MeasureTheory Filter Measure

namespace MeasureTheory

section

variable {Î± R : Type*} [MeasurableSpace Î±] (Î¼ : Measure Î±) [LinearOrder R]

theorem countable_meas_le_ne_meas_lt (g : Î± â†’ R) :
    {t : R | Î¼ {a : Î± | t â‰¤ g a} â‰  Î¼ {a : Î± | t < g a}}.Countable := by
  -- the target set is contained in the set of points where the function `t â†¦ Î¼ {a : Î± | t â‰¤ g a}`
  -- jumps down on the right of `t`. This jump set is countable for any function.
  let F : R â†’ â„â‰¥0âˆ := fun t â†¦ Î¼ {a : Î± | t â‰¤ g a}
  apply (countable_image_gt_image_Ioi F).mono
  intro t ht
  have : Î¼ {a | t < g a} < Î¼ {a | t â‰¤ g a} :=
    lt_of_le_of_ne (measure_mono (fun a ha â†¦ le_of_lt ha)) (Ne.symm ht)
  exact âŸ¨Î¼ {a | t < g a}, this, fun s hs â†¦ measure_mono (fun a ha â†¦ hs.trans_le ha)âŸ©

theorem meas_le_ae_eq_meas_lt {R : Type*} [LinearOrder R] [MeasurableSpace R]
    (Î½ : Measure R) [NoAtoms Î½] (g : Î± â†’ R) :
    (fun t => Î¼ {a : Î± | t â‰¤ g a}) =áµ[Î½] fun t => Î¼ {a : Î± | t < g a} :=
  Set.Countable.measure_zero (countable_meas_le_ne_meas_lt Î¼ g) _

end

/-! ### Layercake formula -/


section Layercake

variable {Î± : Type*} [MeasurableSpace Î±] {f : Î± â†’ â„} {g : â„ â†’ â„}

/-- An auxiliary version of the layer cake formula (Cavalieri's principle, tail probability
formula), with a measurability assumption that would also essentially follow from the
integrability assumptions, and a sigma-finiteness assumption.

See `MeasureTheory.lintegral_comp_eq_lintegral_meas_le_mul` and
`MeasureTheory.lintegral_comp_eq_lintegral_meas_lt_mul` for the main formulations of the layer
cake formula. -/
theorem lintegral_comp_eq_lintegral_meas_le_mul_of_measurable_of_sigmaFinite
    (Î¼ : Measure Î±) [SFinite Î¼]
    (f_nn : 0 â‰¤ f) (f_mble : Measurable f)
    (g_intble : âˆ€ t > 0, IntervalIntegrable g volume 0 t) (g_mble : Measurable g)
    (g_nn : âˆ€ t > 0, 0 â‰¤ g t) :
    âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ =
      âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
  have g_intble' : âˆ€ t : â„, 0 â‰¤ t â†’ IntervalIntegrable g volume 0 t := by
    intro t ht
    rcases eq_or_lt_of_le ht with h | h
    Â· simp [â† h]
    Â· exact g_intble t h
  have integrand_eq : âˆ€ Ï‰,
      ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) = âˆ«â» t in Ioc 0 (f Ï‰), ENNReal.ofReal (g t) := by
    intro Ï‰
    have g_ae_nn : 0 â‰¤áµ[volume.restrict (Ioc 0 (f Ï‰))] g := by
      filter_upwards [self_mem_ae_restrict (measurableSet_Ioc : MeasurableSet (Ioc 0 (f Ï‰)))]
        with x hx using g_nn x hx.1
    rw [â† ofReal_integral_eq_lintegral_ofReal (g_intble' (f Ï‰) (f_nn Ï‰)).1 g_ae_nn]
    congr
    exact intervalIntegral.integral_of_le (f_nn Ï‰)
  rw [lintegral_congr integrand_eq]
  simp_rw [â† lintegral_indicator measurableSet_Ioc]
  rw [â† lintegral_indicator measurableSet_Ioi, lintegral_lintegral_swap]
  Â· apply congr_arg
    funext s
    have auxâ‚ :
      (fun x => (Ioc 0 (f x)).indicator (fun t : â„ => ENNReal.ofReal (g t)) s) = fun x =>
        ENNReal.ofReal (g s) * (Ioi (0 : â„)).indicator (fun _ => 1) s *
          (Ici s).indicator (fun _ : â„ => (1 : â„â‰¥0âˆ)) (f x) := by
      funext a
      by_cases h : s âˆˆ Ioc (0 : â„) (f a)
      Â· simp only [h, show s âˆˆ Ioi (0 : â„) from h.1, show f a âˆˆ Ici s from h.2, indicator_of_mem,
          mul_one]
      Â· have h_copy := h
        simp only [mem_Ioc, not_and, not_le] at h
        by_cases h' : 0 < s
        Â· simp only [h_copy, h h', indicator_of_notMem, not_false_iff, mem_Ici, not_le, mul_zero]
        Â· have : s âˆ‰ Ioi (0 : â„) := h'
          simp only [this, h', indicator_of_notMem, not_false_iff, mul_zero,
            zero_mul, mem_Ioc, false_and]
    simp_rw [auxâ‚]
    rw [lintegral_const_mul']
    swap
    Â· apply ENNReal.mul_ne_top ENNReal.ofReal_ne_top
      by_cases h : (0 : â„) < s <;> Â· simp [h]
    simp_rw [show
        (fun a => (Ici s).indicator (fun _ : â„ => (1 : â„â‰¥0âˆ)) (f a)) = fun a =>
          {a : Î± | s â‰¤ f a}.indicator (fun _ => 1) a
        by funext a; by_cases h : s â‰¤ f a <;> simp [h]]
    rw [lintegral_indicatorâ‚€]
    swap; Â· exact f_mble.nullMeasurable measurableSet_Ici
    rw [lintegral_one, Measure.restrict_apply MeasurableSet.univ, univ_inter, indicator_mul_left,
      mul_assoc,
      show
        (Ioi 0).indicator (fun _x : â„ => (1 : â„â‰¥0âˆ)) s * Î¼ {a : Î± | s â‰¤ f a} =
          (Ioi 0).indicator (fun _x : â„ => 1 * Î¼ {a : Î± | s â‰¤ f a}) s
        by by_cases h : 0 < s <;> simp [h]]
    simp_rw [mul_comm _ (ENNReal.ofReal _), one_mul]
    rfl
  have auxâ‚‚ :
    (Function.uncurry fun (x : Î±) (y : â„) =>
        (Ioc 0 (f x)).indicator (fun t : â„ => ENNReal.ofReal (g t)) y) =
      {p : Î± Ã— â„ | p.2 âˆˆ Ioc 0 (f p.1)}.indicator fun p => ENNReal.ofReal (g p.2) := by
    funext p
    cases p with | mk p_fst p_snd => ?_
    rw [Function.uncurry_apply_pair]
    by_cases h : p_snd âˆˆ Ioc 0 (f p_fst)
    Â· have h' : (p_fst, p_snd) âˆˆ {p : Î± Ã— â„ | p.snd âˆˆ Ioc 0 (f p.fst)} := h
      rw [Set.indicator_of_mem h', Set.indicator_of_mem h]
    Â· have h' : (p_fst, p_snd) âˆ‰ {p : Î± Ã— â„ | p.snd âˆˆ Ioc 0 (f p.fst)} := h
      rw [Set.indicator_of_notMem h', Set.indicator_of_notMem h]
  rw [auxâ‚‚]
  have mbleâ‚€ : MeasurableSet {p : Î± Ã— â„ | p.snd âˆˆ Ioc 0 (f p.fst)} := by
    simpa only [mem_univ, Pi.zero_apply, true_and] using
      measurableSet_region_between_oc measurable_zero f_mble MeasurableSet.univ
  exact (ENNReal.measurable_ofReal.comp (g_mble.comp measurable_snd)).aemeasurable.indicatorâ‚€
    mbleâ‚€.nullMeasurableSet

/-- An auxiliary version of the layer cake formula (Cavalieri's principle, tail probability
formula), with a measurability assumption that would also essentially follow from the
integrability assumptions.
Compared to `lintegral_comp_eq_lintegral_meas_le_mul_of_measurable_of_sigmaFinite`, we remove
the sigma-finite assumption.

See `MeasureTheory.lintegral_comp_eq_lintegral_meas_le_mul` and
`MeasureTheory.lintegral_comp_eq_lintegral_meas_lt_mul` for the main formulations of the layer
cake formula. -/
theorem lintegral_comp_eq_lintegral_meas_le_mul_of_measurable (Î¼ : Measure Î±)
    (f_nn : 0 â‰¤ f) (f_mble : Measurable f)
    (g_intble : âˆ€ t > 0, IntervalIntegrable g volume 0 t) (g_mble : Measurable g)
    (g_nn : âˆ€ t > 0, 0 â‰¤ g t) :
    âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ =
      âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
  /- We will reduce to the sigma-finite case, after excluding two easy cases where the result
  is more or less obvious. -/
  have f_nonneg : âˆ€ Ï‰, 0 â‰¤ f Ï‰ := fun Ï‰ â†¦ f_nn Ï‰
  -- trivial case where `g` is ae zero. Then both integrals vanish.
  by_cases H1 : g =áµ[volume.restrict (Ioi (0 : â„))] 0
  Â· have A : âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ = 0 := by
      have : âˆ€ Ï‰, âˆ« t in (0)..f Ï‰, g t = âˆ« t in (0)..f Ï‰, 0 := by
        intro Ï‰
        simp_rw [intervalIntegral.integral_of_le (f_nonneg Ï‰)]
        apply integral_congr_ae
        exact ae_restrict_of_ae_restrict_of_subset Ioc_subset_Ioi_self H1
      simp [this]
    have B : âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) = 0 := by
      have : (fun t â†¦ Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t))
        =áµ[volume.restrict (Ioi (0 : â„))] 0 := by
          filter_upwards [H1] with t ht using by simp [ht]
      simp [lintegral_congr_ae this]
    rw [A, B]
  -- easy case where both sides are obviously infinite: for some `s`, one has
  -- `Î¼ {a : Î± | s < f a} = âˆ` and moreover `g` is not ae zero on `[0, s]`.
  by_cases H2 : âˆƒ s > 0, 0 < âˆ« t in (0)..s, g t âˆ§ Î¼ {a : Î± | s < f a} = âˆ
  Â· rcases H2 with âŸ¨s, s_pos, hs, h'sâŸ©
    rw [intervalIntegral.integral_of_le s_pos.le] at hs
    /- The first integral is infinite, as for `t âˆˆ [0, s]` one has `Î¼ {a : Î± | t â‰¤ f a} = âˆ`,
    and moreover the additional integral `g` is not uniformly zero. -/
    have A : âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) = âˆ := by
      rw [eq_top_iff]
      calc
      âˆ = âˆ«â» t in Ioc 0 s, âˆ * ENNReal.ofReal (g t) := by
          have I_pos : âˆ«â» (a : â„) in Ioc 0 s, ENNReal.ofReal (g a) â‰  0 := by
            rw [â† ofReal_integral_eq_lintegral_ofReal (g_intble s s_pos).1]
            Â· simpa only [not_lt, ne_eq, ENNReal.ofReal_eq_zero, not_le] using hs
            Â· filter_upwards [ae_restrict_mem measurableSet_Ioc] with t ht using g_nn _ ht.1
          rw [lintegral_const_mul, ENNReal.top_mul I_pos]
          exact ENNReal.measurable_ofReal.comp g_mble
      _ â‰¤ âˆ«â» t in Ioc 0 s, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
          apply setLIntegral_mono' measurableSet_Ioc (fun x hx â†¦ ?_)
          rw [â† h's]
          gcongr
          exact fun ha â†¦ hx.2.trans (le_of_lt ha)
      _ â‰¤ âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) :=
          lintegral_mono_set Ioc_subset_Ioi_self
    /- The second integral is infinite, as one integrates among other things on those `Ï‰` where
    `f Ï‰ > s`: this is an infinite measure set, and on it the integrand is bounded below
    by `âˆ« t in 0..s, g t` which is positive. -/
    have B : âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ = âˆ := by
      rw [eq_top_iff]
      calc
      âˆ = âˆ«â» _ in {a | s < f a}, ENNReal.ofReal (âˆ« t in (0)..s, g t) âˆ‚Î¼ := by
          simp only [lintegral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter,
            h's]
          rw [ENNReal.mul_top]
          simpa [intervalIntegral.integral_of_le s_pos.le] using hs
      _ â‰¤ âˆ«â» Ï‰ in {a | s < f a}, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ := by
          apply setLIntegral_mono' (measurableSet_lt measurable_const f_mble) (fun a ha â†¦ ?_)
          apply ENNReal.ofReal_le_ofReal
          apply intervalIntegral.integral_mono_interval le_rfl s_pos.le (le_of_lt ha)
          Â· filter_upwards [ae_restrict_mem measurableSet_Ioc] with t ht using g_nn _ ht.1
          Â· exact g_intble _ (s_pos.trans ha)
      _ â‰¤ âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ := setLIntegral_le_lintegral _ _
    rw [A, B]
  /- It remains to handle the interesting case, where `g` is not zero, but both integrals are
  not obviously infinite. Let `M` be the largest number such that `g = 0` on `[0, M]`. Then we
  may restrict `Î¼` to the points where `f Ï‰ > M` (as the other ones do not contribute to the
  integral). The restricted measure `Î½` is sigma-finite, as `Î¼` gives finite measure to
  `{Ï‰ | f Ï‰ > a}` for any `a > M` (otherwise, we would be in the easy case above), so that
  one can write (a full measure subset of) the space as the countable union of the finite measure
  sets `{Ï‰ | f Ï‰ > uâ‚™}` for `uâ‚™` a sequence decreasing to `M`. Therefore,
  this case follows from the case where the measure is sigma-finite, applied to `Î½`. -/
  push_neg at H2
  have M_bdd : BddAbove {s : â„ | g =áµ[volume.restrict (Ioc (0 : â„) s)] 0} := by
    contrapose! H1
    have : âˆ€ (n : â„•), g =áµ[volume.restrict (Ioc (0 : â„) n)] 0 := by
      intro n
      rcases not_bddAbove_iff.1 H1 n with âŸ¨s, hs, nsâŸ©
      exact ae_restrict_of_ae_restrict_of_subset (Ioc_subset_Ioc_right ns.le) hs
    have Hg : g =áµ[volume.restrict (â‹ƒ (n : â„•), (Ioc (0 : â„) n))] 0 :=
      (ae_restrict_iUnion_iff _ _).2 this
    have : (â‹ƒ (n : â„•), (Ioc (0 : â„) n)) = Ioi 0 :=
      iUnion_Ioc_eq_Ioi_self_iff.2 (fun x _ â†¦ exists_nat_ge x)
    rwa [this] at Hg
  -- let `M` be the largest number such that `g` vanishes ae on `(0, M]`.
  let M : â„ := sSup {s : â„ | g =áµ[volume.restrict (Ioc (0 : â„) s)] 0}
  have zero_mem : 0 âˆˆ {s : â„ | g =áµ[volume.restrict (Ioc (0 : â„) s)] 0} := by simpa using trivial
  have M_nonneg : 0 â‰¤ M := le_csSup M_bdd zero_mem
  -- Then the function `g` indeed vanishes ae on `(0, M]`.
  have hgM : g =áµ[volume.restrict (Ioc (0 : â„) M)] 0 := by
    rw [â† restrict_Ioo_eq_restrict_Ioc]
    obtain âŸ¨u, -, uM, ulimâŸ© : âˆƒ u, StrictMono u âˆ§ (âˆ€ (n : â„•), u n < M) âˆ§ Tendsto u atTop (ğ“ M) :=
      exists_seq_strictMono_tendsto M
    have I : âˆ€ n, g =áµ[volume.restrict (Ioc (0 : â„) (u n))] 0 := by
      intro n
      obtain âŸ¨s, hs, unsâŸ© : âˆƒ s, g =á¶ [ae (Measure.restrict volume (Ioc 0 s))] 0 âˆ§ u n < s :=
        exists_lt_of_lt_csSup (Set.nonempty_of_mem zero_mem) (uM n)
      exact ae_restrict_of_ae_restrict_of_subset (Ioc_subset_Ioc_right uns.le) hs
    have : g =áµ[volume.restrict (â‹ƒ n, Ioc (0 : â„) (u n))] 0 := (ae_restrict_iUnion_iff _ _).2 I
    apply ae_restrict_of_ae_restrict_of_subset _ this
    rintro x âŸ¨x_pos, xMâŸ©
    obtain âŸ¨n, hnâŸ© : âˆƒ n, x < u n := ((tendsto_order.1 ulim).1 _ xM).exists
    exact mem_iUnion.2 âŸ¨n, âŸ¨x_pos, hn.leâŸ©âŸ©
  -- Let `Î½` be the restriction of `Î¼` to those points where `f a > M`.
  let Î½ := Î¼.restrict {a : Î± | M < f a}
  -- This measure is sigma-finite (this is the whole point of the argument).
  have : SigmaFinite Î½ := by
    obtain âŸ¨u, -, uM, ulimâŸ© : âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), M < u n) âˆ§ Tendsto u atTop (ğ“ M) :=
      exists_seq_strictAnti_tendsto M
    let s : Î½.FiniteSpanningSetsIn univ :=
    { set := fun n â†¦ {a | f a â‰¤ M} âˆª {a | u n < f a}
      set_mem := fun _ â†¦ trivial
      finite := by
        intro n
        have I : Î½ {a | f a â‰¤ M} = 0 := by
          rw [Measure.restrict_apply (measurableSet_le f_mble measurable_const)]
          convert measure_empty (Î¼ := Î¼)
          rw [â† disjoint_iff_inter_eq_empty]
          exact disjoint_left.mpr (fun a ha â†¦ by simpa using ha)
        have J : Î¼ {a | u n < f a} < âˆ := by
          rw [lt_top_iff_ne_top]
          apply H2 _ (M_nonneg.trans_lt (uM n))
          by_contra H3
          rw [not_lt, intervalIntegral.integral_of_le (M_nonneg.trans (uM n).le)] at H3
          have g_nn_ae : âˆ€áµ t âˆ‚(volume.restrict (Ioc 0 (u n))), 0 â‰¤ g t := by
            filter_upwards [ae_restrict_mem measurableSet_Ioc] with s hs using g_nn _ hs.1
          have Ig : âˆ« (t : â„) in Ioc 0 (u n), g t = 0 :=
            le_antisymm H3 (integral_nonneg_of_ae g_nn_ae)
          have J : âˆ€áµ t âˆ‚(volume.restrict (Ioc 0 (u n))), g t = 0 :=
            (integral_eq_zero_iff_of_nonneg_ae g_nn_ae
              (g_intble (u n) (M_nonneg.trans_lt (uM n))).1).1 Ig
          have : u n â‰¤ M := le_csSup M_bdd J
          exact lt_irrefl _ (this.trans_lt (uM n))
        refine lt_of_le_of_lt (measure_union_le _ _) ?_
        rw [I, zero_add]
        apply lt_of_le_of_lt _ J
        exact restrict_le_self _
      spanning := by
        apply eq_univ_iff_forall.2 (fun a â†¦ ?_)
        rcases le_or_gt (f a) M with ha|ha
        Â· exact mem_iUnion.2 âŸ¨0, Or.inl haâŸ©
        Â· obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < f a := ((tendsto_order.1 ulim).2 _ ha).exists
          exact mem_iUnion.2 âŸ¨n, Or.inr hnâŸ© }
    exact âŸ¨âŸ¨sâŸ©âŸ©
  -- the first integrals with respect to `Î¼` and to `Î½` coincide, as points with `f a â‰¤ M` are
  -- weighted by zero as `g` vanishes there.
  have A : âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼
         = âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î½ := by
    have meas : MeasurableSet {a | M < f a} := measurableSet_lt measurable_const f_mble
    have I : âˆ«â» Ï‰ in {a | M < f a}á¶œ, ENNReal.ofReal (âˆ« t in (0).. f Ï‰, g t) âˆ‚Î¼
             = âˆ«â» _ in {a | M < f a}á¶œ, 0 âˆ‚Î¼ := by
      apply setLIntegral_congr_fun meas.compl (fun s hs â†¦ ?_)
      have : âˆ« (t : â„) in (0)..f s, g t = âˆ« (t : â„) in (0)..f s, 0 := by
        simp_rw [intervalIntegral.integral_of_le (f_nonneg s)]
        apply integral_congr_ae
        apply ae_mono (restrict_mono ?_ le_rfl) hgM
        apply Ioc_subset_Ioc_right
        simpa using hs
      simp [this]
    simp only [lintegral_const, zero_mul] at I
    rw [â† lintegral_add_compl _ meas, I, add_zero]
  -- the second integrals with respect to `Î¼` and to `Î½` coincide, as points with `f a â‰¤ M` do not
  -- contribute to either integral since the weight `g` vanishes.
  have B : âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t)
           = âˆ«â» t in Ioi 0, Î½ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
    have B1 : âˆ«â» t in Ioc 0 M, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t)
         = âˆ«â» t in Ioc 0 M, Î½ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
      apply lintegral_congr_ae
      filter_upwards [hgM] with t ht
      simp [ht]
    have B2 : âˆ«â» t in Ioi M, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t)
              = âˆ«â» t in Ioi M, Î½ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
      apply setLIntegral_congr_fun measurableSet_Ioi (fun t ht â†¦ ?_)
      rw [Measure.restrict_apply (measurableSet_le measurable_const f_mble)]
      congr 3
      exact (inter_eq_left.2 (fun a ha â†¦ (mem_Ioi.1 ht).trans_le ha)).symm
    have I : Ioi (0 : â„) = Ioc (0 : â„) M âˆª Ioi M := (Ioc_union_Ioi_eq_Ioi M_nonneg).symm
    have J : Disjoint (Ioc 0 M) (Ioi M) := Ioc_disjoint_Ioi le_rfl
    rw [I, lintegral_union measurableSet_Ioi J, lintegral_union measurableSet_Ioi J, B1, B2]
  -- therefore, we may replace the integrals wrt `Î¼` with integrals wrt `Î½`, and apply the
  -- result for sigma-finite measures.
  rw [A, B]
  exact lintegral_comp_eq_lintegral_meas_le_mul_of_measurable_of_sigmaFinite
    Î½ f_nn f_mble g_intble g_mble g_nn

/-- The layer cake formula / **Cavalieri's principle** / tail probability formula:

Let `f` be a non-negative measurable function on a measure space. Let `G` be an
increasing absolutely continuous function on the positive real line, vanishing at the origin,
with derivative `G' = g`. Then the integral of the composition `G âˆ˜ f` can be written as
the integral over the positive real line of the "tail measures" `Î¼ {Ï‰ | f(Ï‰) â‰¥ t}` of `f`
weighted by `g`.

Roughly speaking, the statement is: `âˆ«â» (G âˆ˜ f) âˆ‚Î¼ = âˆ«â» t in 0..âˆ, g(t) * Î¼ {Ï‰ | f(Ï‰) â‰¥ t}`.

See `MeasureTheory.lintegral_comp_eq_lintegral_meas_lt_mul` for a version with sets of the form
`{Ï‰ | f(Ï‰) > t}` instead. -/
theorem lintegral_comp_eq_lintegral_meas_le_mul (Î¼ : Measure Î±) (f_nn : 0 â‰¤áµ[Î¼] f)
    (f_mble : AEMeasurable f Î¼) (g_intble : âˆ€ t > 0, IntervalIntegrable g volume 0 t)
    (g_nn : âˆ€áµ t âˆ‚volume.restrict (Ioi 0), 0 â‰¤ g t) :
    âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ =
      âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) := by
  obtain âŸ¨G, G_mble, G_nn, g_eq_GâŸ© : âˆƒ G : â„ â†’ â„, Measurable G âˆ§ 0 â‰¤ G
      âˆ§ g =áµ[volume.restrict (Ioi 0)] G := by
    refine AEMeasurable.exists_measurable_nonneg ?_ g_nn
    exact aemeasurable_Ioi_of_forall_Ioc fun t ht => (g_intble t ht).1.1.aemeasurable
  have g_eq_G_on : âˆ€ t, g =áµ[volume.restrict (Ioc 0 t)] G := fun t =>
    ae_mono (Measure.restrict_mono Ioc_subset_Ioi_self le_rfl) g_eq_G
  have G_intble : âˆ€ t > 0, IntervalIntegrable G volume 0 t := by
    refine fun t t_pos => âŸ¨(g_intble t t_pos).1.congr_fun_ae (g_eq_G_on t), ?_âŸ©
    rw [Ioc_eq_empty_of_le t_pos.lt.le]
    exact integrableOn_empty
  obtain âŸ¨F, F_mble, F_nn, f_eq_FâŸ© : âˆƒ F : Î± â†’ â„, Measurable F âˆ§ 0 â‰¤ F âˆ§ f =áµ[Î¼] F := by
    refine âŸ¨fun Ï‰ â†¦ max (f_mble.mk f Ï‰) 0, f_mble.measurable_mk.max measurable_const,
        fun Ï‰ â†¦ le_max_right _ _, ?_âŸ©
    filter_upwards [f_mble.ae_eq_mk, f_nn] with Ï‰ hÏ‰ h'Ï‰
    rw [â† hÏ‰]
    exact (max_eq_left h'Ï‰).symm
  have eqâ‚ :
    âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} * ENNReal.ofReal (g t) =
      âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ F a} * ENNReal.ofReal (G t) := by
    apply lintegral_congr_ae
    filter_upwards [g_eq_G] with t ht
    rw [ht]
    congr 1
    apply measure_congr
    filter_upwards [f_eq_F] with a ha using by simp [setOf, ha]
  have eqâ‚‚ : âˆ€áµ Ï‰ âˆ‚Î¼,
      ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) = ENNReal.ofReal (âˆ« t in (0)..F Ï‰, G t) := by
    filter_upwards [f_eq_F] with Ï‰ fÏ‰_nn
    rw [fÏ‰_nn]
    congr 1
    refine intervalIntegral.integral_congr_ae ?_
    have fÏ‰_nn : 0 â‰¤ F Ï‰ := F_nn Ï‰
    rw [uIoc_of_le fÏ‰_nn, â†
      ae_restrict_iff' (measurableSet_Ioc : MeasurableSet (Ioc (0 : â„) (F Ï‰)))]
    exact g_eq_G_on (F Ï‰)
  simp_rw [lintegral_congr_ae eqâ‚‚, eqâ‚]
  exact lintegral_comp_eq_lintegral_meas_le_mul_of_measurable Î¼ F_nn F_mble
          G_intble G_mble (fun t _ => G_nn t)

/-- The standard case of the layer cake formula / Cavalieri's principle / tail probability formula:

For a nonnegative function `f` on a measure space, the Lebesgue integral of `f` can
be written (roughly speaking) as: `âˆ«â» f âˆ‚Î¼ = âˆ«â» t in 0..âˆ, Î¼ {Ï‰ | f(Ï‰) â‰¥ t}`.

See `MeasureTheory.lintegral_eq_lintegral_meas_lt` for a version with sets of the form
`{Ï‰ | f(Ï‰) > t}` instead. -/
theorem lintegral_eq_lintegral_meas_le (Î¼ : Measure Î±) (f_nn : 0 â‰¤áµ[Î¼] f)
    (f_mble : AEMeasurable f Î¼) :
    âˆ«â» Ï‰, ENNReal.ofReal (f Ï‰) âˆ‚Î¼ = âˆ«â» t in Ioi 0, Î¼ {a : Î± | t â‰¤ f a} := by
  set cst := fun _ : â„ => (1 : â„)
  have cst_intble : âˆ€ t > 0, IntervalIntegrable cst volume 0 t := fun _ _ =>
    intervalIntegrable_const
  have key :=
    lintegral_comp_eq_lintegral_meas_le_mul Î¼ f_nn f_mble cst_intble
      (Eventually.of_forall fun _ => zero_le_one)
  simp_rw [cst, ENNReal.ofReal_one, mul_one] at key
  rw [â† key]
  congr with Ï‰
  simp only [intervalIntegral.integral_const, sub_zero, Algebra.id.smul_eq_mul, mul_one]

end Layercake

section LayercakeLT

variable {Î± : Type*} [MeasurableSpace Î±]
variable {f : Î± â†’ â„} {g : â„ â†’ â„}

/-- The layer cake formula / Cavalieri's principle / tail probability formula:

Let `f` be a non-negative measurable function on a measure space. Let `G` be an
increasing absolutely continuous function on the positive real line, vanishing at the origin,
with derivative `G' = g`. Then the integral of the composition `G âˆ˜ f` can be written as
the integral over the positive real line of the "tail measures" `Î¼ {Ï‰ | f(Ï‰) > t}` of `f`
weighted by `g`.

Roughly speaking, the statement is: `âˆ«â» (G âˆ˜ f) âˆ‚Î¼ = âˆ«â» t in 0..âˆ, g(t) * Î¼ {Ï‰ | f(Ï‰) > t}`.

See `lintegral_comp_eq_lintegral_meas_le_mul` for a version with sets of the form `{Ï‰ | f(Ï‰) â‰¥ t}`
instead. -/
theorem lintegral_comp_eq_lintegral_meas_lt_mul (Î¼ : Measure Î±) (f_nn : 0 â‰¤áµ[Î¼] f)
    (f_mble : AEMeasurable f Î¼) (g_intble : âˆ€ t > 0, IntervalIntegrable g volume 0 t)
    (g_nn : âˆ€áµ t âˆ‚volume.restrict (Ioi 0), 0 â‰¤ g t) :
    âˆ«â» Ï‰, ENNReal.ofReal (âˆ« t in (0)..f Ï‰, g t) âˆ‚Î¼ =
      âˆ«â» t in Ioi 0, Î¼ {a : Î± | t < f a} * ENNReal.ofReal (g t) := by
  rw [lintegral_comp_eq_lintegral_meas_le_mul Î¼ f_nn f_mble g_intble g_nn]
  apply lintegral_congr_ae
  filter_upwards [meas_le_ae_eq_meas_lt Î¼ (volume.restrict (Ioi 0)) f]
    with t ht
  rw [ht]

/-- The standard case of the layer cake formula / Cavalieri's principle / tail probability formula:

For a nonnegative function `f` on a measure space, the Lebesgue integral of `f` can
be written (roughly speaking) as: `âˆ«â» f âˆ‚Î¼ = âˆ«â» t in 0..âˆ, Î¼ {Ï‰ | f(Ï‰) > t}`.

See `lintegral_eq_lintegral_meas_le` for a version with sets of the form `{Ï‰ | f(Ï‰) â‰¥ t}`
instead. -/
theorem lintegral_eq_lintegral_meas_lt (Î¼ : Measure Î±)
    (f_nn : 0 â‰¤áµ[Î¼] f) (f_mble : AEMeasurable f Î¼) :
    âˆ«â» Ï‰, ENNReal.ofReal (f Ï‰) âˆ‚Î¼ = âˆ«â» t in Ioi 0, Î¼ {a : Î± | t < f a} := by
  rw [lintegral_eq_lintegral_meas_le Î¼ f_nn f_mble]
  apply lintegral_congr_ae
  filter_upwards [meas_le_ae_eq_meas_lt Î¼ (volume.restrict (Ioi 0)) f]
    with t ht
  rw [ht]

end LayercakeLT

section LayercakeIntegral

variable {Î± : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±} {f : Î± â†’ â„}

/-- The standard case of the layer cake formula / Cavalieri's principle / tail probability formula:

For an integrable a.e.-nonnegative real-valued function `f`, the Bochner integral of `f` can be
written (roughly speaking) as: `âˆ« f âˆ‚Î¼ = âˆ« t in 0..âˆ, Î¼ {Ï‰ | f(Ï‰) > t}`.

See `MeasureTheory.lintegral_eq_lintegral_meas_lt` for a version with Lebesgue integral `âˆ«â»`
instead. -/
theorem Integrable.integral_eq_integral_meas_lt
    (f_intble : Integrable f Î¼) (f_nn : 0 â‰¤áµ[Î¼] f) :
    âˆ« Ï‰, f Ï‰ âˆ‚Î¼ = âˆ« t in Set.Ioi 0, Î¼.real {a : Î± | t < f a} := by
  have key := lintegral_eq_lintegral_meas_lt Î¼ f_nn f_intble.aemeasurable
  have lhs_finite : âˆ«â» (Ï‰ : Î±), ENNReal.ofReal (f Ï‰) âˆ‚Î¼ < âˆ := Integrable.lintegral_lt_top f_intble
  have rhs_finite : âˆ«â» (t : â„) in Set.Ioi 0, Î¼ {a | t < f a} < âˆ := by simp only [â† key, lhs_finite]
  have rhs_integrand_finite : âˆ€ (t : â„), t > 0 â†’ Î¼ {a | t < f a} < âˆ :=
    fun t ht â†¦ measure_gt_lt_top f_intble ht
  convert (ENNReal.toReal_eq_toReal lhs_finite.ne rhs_finite.ne).mpr key
  Â· exact integral_eq_lintegral_of_nonneg_ae f_nn f_intble.aestronglyMeasurable
  Â· have aux := @integral_eq_lintegral_of_nonneg_ae _ _ ((volume : Measure â„).restrict (Set.Ioi 0))
      (fun t â†¦ Î¼.real {a : Î± | t < f a}) ?_ ?_
    Â· rw [aux]
      congr 1
      apply setLIntegral_congr_fun measurableSet_Ioi
      exact fun t t_pos â†¦ ENNReal.ofReal_toReal (rhs_integrand_finite t t_pos).ne
    Â· exact Eventually.of_forall (fun x â†¦ by positivity)
    Â· apply Measurable.aestronglyMeasurable
      refine Measurable.ennreal_toReal ?_
      exact Antitone.measurable (fun _ _ hst â†¦ measure_mono (fun _ h â†¦ lt_of_le_of_lt hst h))

theorem Integrable.integral_eq_integral_meas_le
    (f_intble : Integrable f Î¼) (f_nn : 0 â‰¤áµ[Î¼] f) :
    âˆ« Ï‰, f Ï‰ âˆ‚Î¼ = âˆ« t in Set.Ioi 0, Î¼.real {a : Î± | t â‰¤ f a} := by
  rw [Integrable.integral_eq_integral_meas_lt f_intble f_nn]
  apply integral_congr_ae
  filter_upwards [meas_le_ae_eq_meas_lt Î¼ (volume.restrict (Ioi 0)) f] with t ht
  exact congrArg ENNReal.toReal ht.symm

lemma Integrable.integral_eq_integral_Ioc_meas_le {f : Î± â†’ â„} {M : â„}
    (f_intble : Integrable f Î¼) (f_nn : 0 â‰¤áµ[Î¼] f) (f_bdd : f â‰¤áµ[Î¼] (fun _ â†¦ M)) :
    âˆ« Ï‰, f Ï‰ âˆ‚Î¼ = âˆ« t in Ioc 0 M, Î¼.real {a : Î± | t â‰¤ f a} := by
  rw [f_intble.integral_eq_integral_meas_le f_nn]
  rw [setIntegral_eq_of_subset_of_ae_diff_eq_zero
      nullMeasurableSet_Ioi Ioc_subset_Ioi_self ?_]
  apply Eventually.of_forall (fun t ht â†¦ ?_)
  have htM : M < t := by simp_all only [mem_diff, mem_Ioi, mem_Ioc, not_and, not_le]
  have obs : Î¼ {a | M < f a} = 0 := by
    rw [measure_zero_iff_ae_notMem]
    filter_upwards [f_bdd] with a ha using not_lt.mpr ha
  rw [measureReal_def, ENNReal.toReal_eq_zero_iff]
  exact Or.inl <| measure_mono_null (fun a ha â†¦ lt_of_lt_of_le htM ha) obs

end LayercakeIntegral

end MeasureTheory
