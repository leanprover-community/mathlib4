/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
import Mathlib.Analysis.Calculus.Deriv.CompMul
import Mathlib.Analysis.Calculus.Deriv.Shift
import Mathlib.Analysis.Calculus.ContDiff.Basic

/-!
# Integral of a 1-form along a path

In this file we define integral of a 1-form along a path
and prove basic properties of this operation.
-/

open MeasureTheory unitInterval Topology Set Interval

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] {a b : E}

/-- The function `t â†¦ Ï‰ (Î³ t) (Î³' t)` which appears in the definition of a path integral.

This definition is used to factor out common parts of lemmas about `Pa -/
noncomputable def pathIntegralFun (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) (t : â„) : F :=
  Ï‰ (Î³.extend t) (derivWithin Î³.extend I t)

/-- A 1-form `Ï‰` is *path integrable* along a path `Î³`,
if the function `pathIntegralFun Ï‰ Î³ t = Ï‰ (Î³ t) (Î³' t)` is integrable on `[0, 1]`.

The actual definition uses `Path.extend Î³`,
because both interval integrals and derivatives expect globally defined functions.
-/
def PathIntegrable (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) : Prop :=
  IntervalIntegrable (pathIntegralFun Ï‰ Î³) volume 0 1

/-- Integral of a 1-form `Ï‰ : E â†’ E â†’L[â„] F` along a path `Î³`,
defined as $\int_0^1 \omega(\gamma(t))(\gamma'(t))$.

The actual definition uses `pathIntegralFun` which uses `Path.extend Î³`
and `derivWithin (Path.extend Î³) (Set.Icc 0 1) t`,
because calculus-related definitions in Mathlib expect globally defined functions as arguments. -/
noncomputable def pathIntegral (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) : F :=
  âˆ« t in (0)..1, pathIntegralFun Ï‰ Î³ t

-- TODO: use `âˆˆ`
-- TODO: fix priorities
@[inherit_doc pathIntegral]
notation3 "âˆ«áµ– "(...)" in " Î³ ", "r:60:(scoped Ï‰ => pathIntegral Ï‰ Î³) => r

/-- Path integral is defined using Bochner integral,
thus it is defined as zero whenever the codomain is not a complete space. -/
theorem pathIntegral_of_not_completeSpace (h : Â¬CompleteSpace F) (Ï‰ : E â†’ E â†’L[â„] F)
    (Î³ : Path a b) : âˆ«áµ– x in Î³, Ï‰ x = 0 := by
  simp [pathIntegral, intervalIntegral, integral, h]

/-!
### Operations on paths
-/

section PathOperations

variable {c d : E} {Ï‰ : E â†’ E â†’L[â„] F} {Î³ Î³ab : Path a b} {Î³bc : Path b c} {t : â„}

@[simp]
theorem pathIntegralFun_refl (Ï‰ : E â†’ E â†’L[â„] F) (a : E) : pathIntegralFun Ï‰ (.refl a) = 0 := by
  ext
  simp [pathIntegralFun]

@[simp]
theorem pathIntegral_refl (Ï‰ : E â†’ E â†’L[â„] F) (a : E) : âˆ«áµ– x in .refl a, Ï‰ x = 0 := by
  simp [pathIntegral]

@[simp]
theorem PathIntegrable.refl (Ï‰ : E â†’ E â†’L[â„] F) (a : E) : PathIntegrable Ï‰ (.refl a) := by
  simp [PathIntegrable, Pi.zero_def]

@[simp]
theorem pathIntegralFun_cast (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) (hc : c = a) (hd : d = b) :
    pathIntegralFun Ï‰ (Î³.cast hc hd) = pathIntegralFun Ï‰ Î³ :=
  rfl

@[simp]
theorem pathIntegral_cast (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) (hc : c = a) (hd : d = b) :
    âˆ«áµ– x in Î³.cast hc hd, Ï‰ x = âˆ«áµ– x in Î³, Ï‰ x :=
  rfl

@[simp]
theorem PathIntegrable.cast_iff (hc : c = a) (hd : d = b) :
    PathIntegrable Ï‰ (Î³.cast hc hd) â†” PathIntegrable Ï‰ Î³ := .rfl

protected alias âŸ¨_, PathIntegrable.castâŸ© := PathIntegrable.cast_iff

theorem pathIntegralFun_symm_apply (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) (t : â„) :
    pathIntegralFun Ï‰ Î³.symm t = -pathIntegralFun Ï‰ Î³ (1 - t) := by
  simp [pathIntegralFun, Î³.extend_symm, derivWithin_comp_const_sub]

@[simp]
theorem pathIntegralFun_symm (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b):
    pathIntegralFun Ï‰ Î³.symm = (-pathIntegralFun Ï‰ Î³ <| 1 - Â·) :=
  funext <| pathIntegralFun_symm_apply Ï‰ Î³

protected theorem PathIntegrable.symm (h : PathIntegrable Ï‰ Î³) : PathIntegrable Ï‰ Î³.symm := by
  simpa [PathIntegrable] using (h.comp_sub_left 1).neg.symm

@[simp]
theorem pathIntegrable_symm : PathIntegrable Ï‰ Î³.symm â†” PathIntegrable Ï‰ Î³ :=
  âŸ¨fun h â†¦ by simpa using h.symm, .symmâŸ©

@[simp]
theorem pathIntegral_symm (Ï‰ : E â†’ E â†’L[â„] F) (Î³ : Path a b) :
    âˆ«áµ– x in Î³.symm, Ï‰ x = -âˆ«áµ– x in Î³, Ï‰ x := by
  simp [pathIntegral, pathIntegralFun_symm]

theorem pathIntegralFun_trans_of_lt_half (Ï‰ : E â†’ E â†’L[â„] F) (Î³ab : Path a b) (Î³bc : Path b c)
    (htâ‚€ : 0 < t) (ht : t < 1 / 2) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) t = (2 : â„) â€¢ pathIntegralFun Ï‰ Î³ab (2 * t) := by
  have : (Î³ab.trans Î³bc).extend =á¶ [ğ“ t] (fun s â†¦ Î³ab.extend (2 * s)) :=
    (eventually_le_nhds ht).mono fun _ â†¦ Path.extend_trans_of_le_half _ _
  rw [pathIntegralFun, this.self_of_nhds, derivWithin_of_mem_nhds, this.deriv_eq, pathIntegralFun,
    derivWithin_of_mem_nhds, deriv_comp_mul_left, map_smul] <;>
    apply Icc_mem_nhds <;> linarith

theorem pathIntegralFun_trans_aeeq_left (Ï‰ : E â†’ E â†’L[â„] F) (Î³ab : Path a b) (Î³bc : Path b c) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) =áµ[volume.restrict (Î™ (0 : â„) (1 / 2))]
      fun t â†¦ (2 : â„) â€¢ pathIntegralFun Ï‰ Î³ab (2 * t) := by
  rw [uIoc_of_le (by positivity), â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem measurableSet_Ioo] with t âŸ¨htâ‚€, htâŸ©
  exact pathIntegralFun_trans_of_lt_half Ï‰ Î³ab Î³bc htâ‚€ ht

theorem pathIntegralFun_trans_of_half_lt (Ï‰ : E â†’ E â†’L[â„] F) (Î³ab : Path a b) (Î³bc : Path b c)
    (htâ‚€ : 1 / 2 < t) (ht : t < 1) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) t = (2 : â„) â€¢ pathIntegralFun Ï‰ Î³bc (2 * t - 1) := by
  have : (Î³ab.trans Î³bc).extend =á¶ [ğ“ t] (fun s â†¦ Î³bc.extend (2 * s - 1)) :=
    (eventually_ge_nhds htâ‚€).mono fun _ â†¦ Path.extend_trans_of_half_le _ _
  rw [pathIntegralFun, this.self_of_nhds, derivWithin_of_mem_nhds, this.deriv_eq, pathIntegralFun,
    derivWithin_of_mem_nhds, deriv_comp_mul_left _ (Î³bc.extend <| Â· - 1), deriv_comp_sub_const,
    map_smul] <;> apply Icc_mem_nhds <;> linarith

theorem pathIntegralFun_trans_aeeq_right (Ï‰ : E â†’ E â†’L[â„] F) (Î³ab : Path a b) (Î³bc : Path b c) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) =áµ[volume.restrict (Î™ (1 / 2 : â„) 1)]
      fun t â†¦ (2 : â„) â€¢ pathIntegralFun Ï‰ Î³bc (2 * t - 1) := by
  rw [uIoc_of_le (by linarith), â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem measurableSet_Ioo] with t âŸ¨htâ‚, htâ‚‚âŸ©
  exact pathIntegralFun_trans_of_half_lt Ï‰ Î³ab Î³bc htâ‚ htâ‚‚

theorem PathIntegrable.intervalIntegrable_pathIntegralFun_trans_left
    (h : PathIntegrable Ï‰ Î³ab) (Î³bc : Path b c) :
    IntervalIntegrable (pathIntegralFun Ï‰ (Î³ab.trans Î³bc)) volume 0 (1 / 2) :=
  .congr (by simpa using (h.comp_mul_left 2).smul (2 : â„))
    (pathIntegralFun_trans_aeeq_left _ _ _).symm

theorem PathIntegrable.intervalIntegrable_pathIntegralFun_trans_right
    (Î³ab : Path a b) (h : PathIntegrable Ï‰ Î³bc) :
    IntervalIntegrable (pathIntegralFun Ï‰ (Î³ab.trans Î³bc)) volume (1 / 2) 1 :=
  .congr (by simpa using ((h.comp_sub_right 1).comp_mul_left 2).smul (2 : â„))
    (pathIntegralFun_trans_aeeq_right _ _ _).symm

protected theorem PathIntegrable.trans (hâ‚ : PathIntegrable Ï‰ Î³ab) (hâ‚‚ : PathIntegrable Ï‰ Î³bc) :
    PathIntegrable Ï‰ (Î³ab.trans Î³bc) :=
  (hâ‚.intervalIntegrable_pathIntegralFun_trans_left Î³bc).trans
    (hâ‚‚.intervalIntegrable_pathIntegralFun_trans_right Î³ab)

theorem pathIntegral_trans (hâ‚ : PathIntegrable Ï‰ Î³ab) (hâ‚‚ : PathIntegrable Ï‰ Î³bc) :
    âˆ«áµ– x in Î³ab.trans Î³bc, Ï‰ x = pathIntegral Ï‰ Î³ab + pathIntegral Ï‰ Î³bc := by
  rw [pathIntegral, â† intervalIntegral.integral_add_adjacent_intervals
    (hâ‚.intervalIntegrable_pathIntegralFun_trans_left Î³bc)
    (hâ‚‚.intervalIntegrable_pathIntegralFun_trans_right Î³ab),
    intervalIntegral.integral_congr_ae_restrict (pathIntegralFun_trans_aeeq_left _ _ _),
    intervalIntegral.integral_congr_ae_restrict (pathIntegralFun_trans_aeeq_right _ _ _),
    intervalIntegral.integral_smul, intervalIntegral.smul_integral_comp_mul_left,
    intervalIntegral.integral_smul,
    intervalIntegral.smul_integral_comp_mul_left (f := (pathIntegralFun Ï‰ Î³bc <| Â· - 1)),
    intervalIntegral.integral_comp_sub_right]
  norm_num [pathIntegral]

/-- If a 1-form `Ï‰` is continuous on a set `s`,
then it is path integrable along any $C^1$ path in this set. -/
theorem ContinuousOn.pathIntegrable_of_contDiffOn {s : Set E} (hÏ‰ : ContinuousOn Ï‰ s)
    (hÎ³ : ContDiffOn â„ 1 Î³.extend I) (hÎ³s : âˆ€ t, Î³ t âˆˆ s) : PathIntegrable Ï‰ Î³ := by
  apply ContinuousOn.intervalIntegrable_of_Icc zero_le_one
  unfold pathIntegralFun
  apply ContinuousOn.clm_apply
  Â· exact hÏ‰.comp (by fun_prop) fun _ _ â†¦ hÎ³s _
  Â· exact hÎ³.continuousOn_derivWithin uniqueDiffOn_Icc_zero_one le_rfl

end PathOperations

/-!
### Algebraic operations on the 1-form
-/

variable {Ï‰ Ï‰â‚ Ï‰â‚‚ : E â†’ E â†’L[â„] F} {Î³ : Path a b} {t : â„}

@[simp]
theorem pathIntegralFun_add :
    pathIntegralFun (Ï‰â‚ + Ï‰â‚‚) Î³ = pathIntegralFun Ï‰â‚ Î³ + pathIntegralFun Ï‰â‚‚ Î³ :=
  rfl

protected nonrec theorem PathIntegrable.add (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    PathIntegrable (Ï‰â‚ + Ï‰â‚‚) Î³ :=
  hâ‚.add hâ‚‚

theorem pathIntegral_add (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    pathIntegral (Ï‰â‚ + Ï‰â‚‚) Î³ = (âˆ«áµ– x in Î³, Ï‰â‚ x) + âˆ«áµ– x in Î³, Ï‰â‚‚ x :=
  intervalIntegral.integral_add hâ‚ hâ‚‚

theorem pathIntegral_fun_add (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    âˆ«áµ– x in Î³, Ï‰â‚ x + Ï‰â‚‚ x = (âˆ«áµ– x in Î³, Ï‰â‚ x) + âˆ«áµ– x in Î³, Ï‰â‚‚ x :=
  pathIntegral_add hâ‚ hâ‚‚

@[simp]
theorem pathIntegralFun_zero : pathIntegralFun (0 : E â†’ E â†’L[â„] F) Î³ = 0 := rfl

@[simp]
theorem pathIntegralFun_fun_zero : pathIntegralFun (fun _ â†¦ 0 : E â†’ E â†’L[â„] F) Î³ = 0 := rfl

-- TODO: add `intervalIntegrable_zero`
theorem PathIntegrable.zero : PathIntegrable (0 : E â†’ E â†’L[â„] F) Î³ := by
  simp [PathIntegrable, intervalIntegrable_const, Pi.zero_def]

theorem PathIntegrable.fun_zero : PathIntegrable (fun _ â†¦ 0 : E â†’ E â†’L[â„] F) Î³ := .zero

@[simp]
theorem pathIntegral_zero : pathIntegral (0 : E â†’ E â†’L[â„] F) Î³ = 0 := by simp [pathIntegral]

@[simp]
theorem pathIntegral_fun_zero : âˆ«áµ– _ in Î³, (0 : E â†’L[â„] F) = 0 := pathIntegral_zero

@[simp]
theorem pathIntegralFun_neg : pathIntegralFun (-Ï‰) Î³ = -pathIntegralFun Ï‰ Î³ := rfl

nonrec theorem PathIntegrable.neg (h : PathIntegrable Ï‰ Î³) : PathIntegrable (-Ï‰) Î³ :=
  h.neg

theorem PathIntegrable.fun_neg (h : PathIntegrable Ï‰ Î³) : PathIntegrable (-Ï‰ Â·) Î³ :=
  h.neg

@[simp]
theorem PathIntegrable.neg_iff : PathIntegrable (-Ï‰) Î³ â†” PathIntegrable Ï‰ Î³ :=
  âŸ¨fun h â†¦ by simpa using h.neg, .negâŸ©

@[simp]
theorem PathIntegrable.fun_neg_iff : PathIntegrable (-Ï‰ Â·) Î³ â†” PathIntegrable Ï‰ Î³ :=
  PathIntegrable.neg_iff

@[simp]
theorem pathIntegral_neg : pathIntegral (-Ï‰) Î³ = -âˆ«áµ– x in Î³, Ï‰ x := by
  simp [pathIntegral]

@[simp]
theorem pathIntegral_fun_neg : âˆ«áµ– x in Î³, -Ï‰ x = -âˆ«áµ– x in Î³, Ï‰ x := pathIntegral_neg

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F] {c : ğ•œ}

@[simp]
theorem pathIntegralFun_smul : pathIntegralFun (c â€¢ Ï‰) Î³ = c â€¢ pathIntegralFun Ï‰ Î³ := rfl

nonrec theorem PathIntegrable.smul (h : PathIntegrable Ï‰ Î³) : PathIntegrable (c â€¢ Ï‰) Î³ :=
  h.smul c

@[simp]
theorem PathIntegrable.smul_iff : PathIntegrable (c â€¢ Ï‰) Î³ â†” c = 0 âˆ¨ PathIntegrable Ï‰ Î³ := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp [PathIntegrable.zero]
  Â· simp only [hc, false_or]
    refine âŸ¨fun h â†¦ ?_, .smulâŸ©
    simpa [hc] using h.smul (c := câ»Â¹)

@[simp]
theorem pathIntegral_smul : pathIntegral (c â€¢ Ï‰) Î³ = c â€¢ pathIntegral Ï‰ Î³ :=
  intervalIntegral.integral_smul _ _

@[simp]
theorem pathIntegral_fun_smul : âˆ«áµ– x in Î³, c â€¢ Ï‰ x = c â€¢ âˆ«áµ– x in Î³, Ï‰ x := pathIntegral_smul
