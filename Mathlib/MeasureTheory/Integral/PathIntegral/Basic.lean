/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Order.Field.Pointwise
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.AffineMap
import Mathlib.Analysis.Calculus.Deriv.Shift
import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# Integral of a 1-form along a path

In this file we define integral of a 1-form along a path
and prove basic properties of this operation.

The integral `âˆ«áµ– x in Î³, Ï‰ x` is defined as $\int_0^1 \omega(\gamma(t))(\gamma'(t))$.
More precisely, we use

- `Path.extend Î³ t` instead of `Î³ t`, because both derivatives and `intervalIntegral`
  expect globally defined functions;
- `derivWithin Î³.extend (Set.Icc 0 1) t`, not `deriv Î³.extend t`, for the derivative,
  so that it takes meaningful values at `t = 0` and `t = 1`,
  even though this does not affect the integral.

The argument `Ï‰ : E â†’ E â†’L[ğ•œ] F` is a `ğ•œ`-linear 1-form on `E` taking values in `F`,
where `ğ•œ` is `â„` or `â„‚`.
The definition does not depend on `ğ•œ`, see `pathIntegral_restrictScalars` and nearby lemmas.
However, the fact that `ğ•œ = â„` is not hardcoded
allows us to avoid inserting `ContinuousLinearMap.restrictScalars` here and there.

## Main definitions

- `pathIntegral Ï‰ Î³`, notation `âˆ«áµ– x in Î³, Ï‰ x`, is the integral of a 1-form `Ï‰` along a path `Î³`.
- `PathIntegrable Ï‰ Î³` is the predicate saying that the above integral makes sense.

## Main results

We prove that `pathIntegral` well behaves with respect to

- operations on `Path`s, see `pathIntegral_refl`, `pathIntegral_symm`, `pathIntegral_trans` etc;
- algebraic operations on 1-forms, see `pathIntegral_add` etc.

We also show that the derivative of `fun b â†¦ âˆ«áµ– x in Path.segment a b, Ï‰ x`
has derivative `Ï‰ a` at `b = a`.
We provide 2 versions of this result: one for derivative (`HasFDerivWithinAt`) within a convex set
and one for `HasFDerivAt`.

## Implementation notes

### Usage of `ContinuousLinearMap`s for 1-forms

Similarly to the way `fderiv` uses continuous linear maps
while higher order derivatives use continuous multilinear maps,
this file uses `E â†’ E â†’L[ğ•œ] F` instead of continuous alternating maps for 1-forms.

### Differentiability assumptions

The definitions in this file make sense if the path is a piecewise $C^1$ curve.
PoincarÃ© lemma (formalization WIP, see #24019) implies that for a closed 1-form on an open set `U`,
the integral depends on the homotopy class of the path only,
thus we can define the integral along a continuous path
or an element of the fundamental groupoid of `U`.

### Usage of an extra field

The definitions in this file deal with `ğ•œ`-linear 1-forms.
This allows us to avoid using `ContinuousLinearMap.restrictScalars`
in `HasFDerivWithinAt.pathIntegral_segment_source`
and a future formalization of PoincarÃ© lemma.
-/

open Metric MeasureTheory Topology Set Interval AffineMap Convex Filter
open scoped Pointwise unitInterval

section Defs

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {a b : E}

/-- The function `t â†¦ Ï‰ (Î³ t) (Î³' t)` which appears in the definition of a path integral.

This definition is used to factor out common parts of lemmas about `Pa -/
@[irreducible]
noncomputable def pathIntegralFun (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (t : â„) : F :=
  letI : NormedSpace â„ E := .restrictScalars â„ ğ•œ E
  Ï‰ (Î³.extend t) (derivWithin Î³.extend I t)

/-- A 1-form `Ï‰` is *path integrable* along a path `Î³`,
if the function `pathIntegralFun Ï‰ Î³ t = Ï‰ (Î³ t) (Î³' t)` is integrable on `[0, 1]`.

The actual definition uses `Path.extend Î³`,
because both interval integrals and derivatives expect globally defined functions.
-/
def PathIntegrable (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) : Prop :=
  IntervalIntegrable (pathIntegralFun Ï‰ Î³) volume 0 1

/-- Integral of a 1-form `Ï‰ : E â†’ E â†’L[ğ•œ] F` along a path `Î³`,
defined as $\int_0^1 \omega(\gamma(t))(\gamma'(t))$.

The actual definition uses `pathIntegralFun` which uses `Path.extend Î³`
and `derivWithin (Path.extend Î³) (Set.Icc 0 1) t`,
because calculus-related definitions in Mathlib expect globally defined functions as arguments. -/
@[irreducible]
noncomputable def pathIntegral (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) : F :=
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  âˆ« t in (0)..1, pathIntegralFun Ï‰ Î³ t

-- TODO: use `âˆˆ`
-- TODO: fix priorities
@[inherit_doc pathIntegral]
notation3 "âˆ«áµ– "(...)" in " Î³ ", "r:60:(scoped Ï‰ => pathIntegral Ï‰ Î³) => r

/-- Path integral is defined using Bochner integral,
thus it is defined as zero whenever the codomain is not a complete space. -/
theorem pathIntegral_of_not_completeSpace (h : Â¬CompleteSpace F) (Ï‰ : E â†’ E â†’L[ğ•œ] F)
    (Î³ : Path a b) : âˆ«áµ– x in Î³, Ï‰ x = 0 := by
  simp [pathIntegral, intervalIntegral, integral, h]

theorem pathIntegralFun_def [NormedSpace â„ E] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (t : â„) :
    pathIntegralFun Ï‰ Î³ t = Ï‰ (Î³.extend t) (derivWithin Î³.extend I t) := by
  simp only [pathIntegralFun, NormedSpace.restrictScalars_eq]

theorem pathIntegral_def [NormedSpace â„ F] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    pathIntegral Ï‰ Î³ = âˆ« t in (0)..1, pathIntegralFun Ï‰ Î³ t := by
  simp only [pathIntegral, NormedSpace.restrictScalars_eq]

theorem pathIntegral_def' [NormedSpace â„ E] [NormedSpace â„ F] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    pathIntegral Ï‰ Î³ = âˆ« t in (0)..1, Ï‰ (Î³.extend t) (derivWithin Î³.extend I t) := by
  simp only [pathIntegral_def, pathIntegralFun_def]

end Defs

/-!
### Operations on paths
-/

section PathOperations

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {a b c d : E} {Ï‰ : E â†’ E â†’L[ğ•œ] F}
  {Î³ Î³ab : Path a b} {Î³bc : Path b c} {t : â„}

@[simp]
theorem pathIntegralFun_refl (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a : E) : pathIntegralFun Ï‰ (.refl a) = 0 := by
  ext
  simp [pathIntegralFun, â† Function.const_def]

@[simp]
theorem pathIntegral_refl (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a : E) : âˆ«áµ– x in .refl a, Ï‰ x = 0 := by
  simp [pathIntegral]

@[simp]
theorem PathIntegrable.refl (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a : E) : PathIntegrable Ï‰ (.refl a) := by
  simp [PathIntegrable, Pi.zero_def]

@[simp]
theorem pathIntegralFun_cast (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (hc : c = a) (hd : d = b) :
    pathIntegralFun Ï‰ (Î³.cast hc hd) = pathIntegralFun Ï‰ Î³ := by
  unfold pathIntegralFun
  rw [Path.extend_cast]

@[simp]
theorem pathIntegral_cast (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (hc : c = a) (hd : d = b) :
    âˆ«áµ– x in Î³.cast hc hd, Ï‰ x = âˆ«áµ– x in Î³, Ï‰ x := by
  simp [pathIntegral]

@[simp]
theorem PathIntegrable.cast_iff (hc : c = a) (hd : d = b) :
    PathIntegrable Ï‰ (Î³.cast hc hd) â†” PathIntegrable Ï‰ Î³ := by
  simp [PathIntegrable]

protected alias âŸ¨_, PathIntegrable.castâŸ© := PathIntegrable.cast_iff

theorem pathIntegralFun_symm_apply (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (t : â„) :
    pathIntegralFun Ï‰ Î³.symm t = -pathIntegralFun Ï‰ Î³ (1 - t) := by
  simp [pathIntegralFun, Î³.extend_symm, derivWithin_comp_const_sub]

@[simp]
theorem pathIntegralFun_symm (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b):
    pathIntegralFun Ï‰ Î³.symm = (-pathIntegralFun Ï‰ Î³ <| 1 - Â·) :=
  funext <| pathIntegralFun_symm_apply Ï‰ Î³

protected theorem PathIntegrable.symm (h : PathIntegrable Ï‰ Î³) : PathIntegrable Ï‰ Î³.symm := by
  simpa [PathIntegrable] using (h.comp_sub_left 1).neg.symm

@[simp]
theorem pathIntegrable_symm : PathIntegrable Ï‰ Î³.symm â†” PathIntegrable Ï‰ Î³ :=
  âŸ¨fun h â†¦ by simpa using h.symm, .symmâŸ©

@[simp]
theorem pathIntegral_symm (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    âˆ«áµ– x in Î³.symm, Ï‰ x = -âˆ«áµ– x in Î³, Ï‰ x := by
  simp [pathIntegral, pathIntegralFun_symm]

theorem pathIntegralFun_trans_of_lt_half (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c)
    (ht : t < 1 / 2) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) t = (2 : â„•) â€¢ pathIntegralFun Ï‰ Î³ab (2 * t) := by
  let instE := NormedSpace.restrictScalars â„ ğ•œ E
  have Hâ‚ : (Î³ab.trans Î³bc).extend =á¶ [ğ“ t] (fun s â†¦ Î³ab.extend (2 * s)) :=
    (eventually_le_nhds ht).mono fun _ â†¦ Path.extend_trans_of_le_half _ _
  have Hâ‚‚ : (2 : â„) â€¢ I =á¶ [ğ“ (2 * t)] I := by
    rw [LinearOrderedField.smul_Icc two_pos, mul_zero, mul_one, â† nhdsWithin_eq_iff_eventuallyEq]
    rcases lt_trichotomy t 0 with htâ‚€ | rfl | htâ‚€
    Â· rw [not_mem_closure_iff_nhdsWithin_eq_bot.mp, not_mem_closure_iff_nhdsWithin_eq_bot.mp] <;>
        simp_intro h <;> linarith
    Â· simp
    Â· rw [nhdsWithin_eq_nhds.2, nhdsWithin_eq_nhds.2] <;> simp [*] <;> linarith
  rw [pathIntegralFun_def, Hâ‚.self_of_nhds, Hâ‚.derivWithin_eq_of_nhds, pathIntegralFun_def,
    derivWithin_comp_mul_left, ofNat_smul_eq_nsmul, map_nsmul, derivWithin_congr_set Hâ‚‚]

theorem pathIntegralFun_trans_aeeq_left (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) =áµ[volume.restrict (Î™ (0 : â„) (1 / 2))]
      fun t â†¦ (2 : â„•) â€¢ pathIntegralFun Ï‰ Î³ab (2 * t) := by
  rw [uIoc_of_le (by positivity), â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem measurableSet_Ioo] with t âŸ¨htâ‚€, htâŸ©
  exact pathIntegralFun_trans_of_lt_half Ï‰ Î³ab Î³bc ht

theorem pathIntegralFun_trans_of_half_lt (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c)
    (htâ‚€ : 1 / 2 < t) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) t = (2 : â„•) â€¢ pathIntegralFun Ï‰ Î³bc (2 * t - 1) := by
  rw [â† (Î³ab.trans Î³bc).symm_symm, pathIntegralFun_symm_apply, Path.trans_symm,
    pathIntegralFun_trans_of_lt_half (ht := by linarith), pathIntegralFun_symm_apply, smul_neg,
    neg_neg]
  congr 2
  ring

theorem pathIntegralFun_trans_aeeq_right (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c) :
    pathIntegralFun Ï‰ (Î³ab.trans Î³bc) =áµ[volume.restrict (Î™ (1 / 2 : â„) 1)]
      fun t â†¦ (2 : â„•) â€¢ pathIntegralFun Ï‰ Î³bc (2 * t - 1) := by
  rw [uIoc_of_le (by linarith), â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem measurableSet_Ioo] with t âŸ¨htâ‚, htâ‚‚âŸ©
  exact pathIntegralFun_trans_of_half_lt Ï‰ Î³ab Î³bc htâ‚

theorem PathIntegrable.intervalIntegrable_pathIntegralFun_trans_left
    (h : PathIntegrable Ï‰ Î³ab) (Î³bc : Path b c) :
    IntervalIntegrable (pathIntegralFun Ï‰ (Î³ab.trans Î³bc)) volume 0 (1 / 2) := by
  refine .congr_ae ?_ (pathIntegralFun_trans_aeeq_left _ _ _).symm
  simpa [ofNat_smul_eq_nsmul] using h.comp_mul_left 2 |>.smul (2 : ğ•œ)

theorem PathIntegrable.intervalIntegrable_pathIntegralFun_trans_right
    (Î³ab : Path a b) (h : PathIntegrable Ï‰ Î³bc) :
    IntervalIntegrable (pathIntegralFun Ï‰ (Î³ab.trans Î³bc)) volume (1 / 2) 1 := by
  refine .congr_ae ?_ (pathIntegralFun_trans_aeeq_right _ _ _).symm
  simpa [ofNat_smul_eq_nsmul] using h.comp_sub_right 1 |>.comp_mul_left 2 |>.smul (2 : ğ•œ)

protected theorem PathIntegrable.trans (hâ‚ : PathIntegrable Ï‰ Î³ab) (hâ‚‚ : PathIntegrable Ï‰ Î³bc) :
    PathIntegrable Ï‰ (Î³ab.trans Î³bc) :=
  (hâ‚.intervalIntegrable_pathIntegralFun_trans_left Î³bc).trans
    (hâ‚‚.intervalIntegrable_pathIntegralFun_trans_right Î³ab)

theorem pathIntegral_trans (hâ‚ : PathIntegrable Ï‰ Î³ab) (hâ‚‚ : PathIntegrable Ï‰ Î³bc) :
    âˆ«áµ– x in Î³ab.trans Î³bc, Ï‰ x = pathIntegral Ï‰ Î³ab + pathIntegral Ï‰ Î³bc := by
  let instF := NormedSpace.restrictScalars â„ ğ•œ F
  rw [pathIntegral_def, â† intervalIntegral.integral_add_adjacent_intervals
    (hâ‚.intervalIntegrable_pathIntegralFun_trans_left Î³bc)
    (hâ‚‚.intervalIntegrable_pathIntegralFun_trans_right Î³ab),
    intervalIntegral.integral_congr_ae_restrict (pathIntegralFun_trans_aeeq_left _ _ _),
    intervalIntegral.integral_congr_ae_restrict (pathIntegralFun_trans_aeeq_right _ _ _)]
  simp only [â† ofNat_smul_eq_nsmul (R := â„)]
  rw [intervalIntegral.integral_smul, intervalIntegral.smul_integral_comp_mul_left,
    intervalIntegral.integral_smul,
    intervalIntegral.smul_integral_comp_mul_left (f := (pathIntegralFun Ï‰ Î³bc <| Â· - 1)),
    intervalIntegral.integral_comp_sub_right]
  norm_num [pathIntegral_def]

theorem pathIntegralFun_segment [NormedSpace â„ E] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a b : E)
    {t : â„} (ht : t âˆˆ I) : pathIntegralFun Ï‰ (.segment a b) t = Ï‰ (lineMap a b t) (b - a) := by
  have := Path.eqOn_extend_segment a b
  simp only [pathIntegralFun_def, this ht, derivWithin_congr this (this ht),
    (hasDerivWithinAt_lineMap ..).derivWithin (uniqueDiffOn_Icc_zero_one t ht)]

theorem pathIntegrable_segment [NormedSpace â„ E] :
    PathIntegrable Ï‰ (.segment a b) â†”
      IntervalIntegrable (fun t â†¦ Ï‰ (lineMap a b t) (b - a)) volume 0 1 := by
  rw [PathIntegrable, intervalIntegrable_congr]
  rw [uIoc_of_le zero_le_one]
  exact .mono Ioc_subset_Icc_self fun _t â†¦ pathIntegralFun_segment Ï‰ a b

theorem pathIntegral_segment [NormedSpace â„ E] [NormedSpace â„ F] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a b : E) :
    âˆ«áµ– x in .segment a b, Ï‰ x = âˆ« t in (0)..1, Ï‰ (lineMap a b t) (b - a) := by
  rw [pathIntegral_def]
  refine intervalIntegral.integral_congr fun t ht â†¦ ?_
  rw [uIcc_of_le zero_le_one] at ht
  exact pathIntegralFun_segment Ï‰ a b ht

@[simp]
theorem pathIntegral_segment_const [NormedSpace â„ E] [CompleteSpace F] (Ï‰ : E â†’L[ğ•œ] F) (a b : E) :
    âˆ«áµ– _ in .segment a b, Ï‰ = Ï‰ (b - a) := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp [pathIntegral_segment]

/-- If `â€–Ï‰ zâ€– â‰¤ C` at all points of the segment `[a -[â„] b]`,
then the path integral `âˆ«áµ– x in .segment a b, Ï‰ x` has norm at most `C * â€–b - aâ€–`. -/
theorem norm_pathIntegral_segment_le [NormedSpace â„ E] {C : â„} (h : âˆ€ z âˆˆ [a -[â„] b], â€–Ï‰ zâ€– â‰¤ C) :
    â€–âˆ«áµ– x in .segment a b, Ï‰ xâ€– â‰¤ C * â€–b - aâ€– := calc
  â€–âˆ«áµ– x in .segment a b, Ï‰ xâ€– â‰¤ C * â€–b - aâ€– * |1 - 0| := by
    letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
    rw [pathIntegral_segment]
    refine intervalIntegral.norm_integral_le_of_norm_le_const fun t ht â†¦ ?_
    rw [segment_eq_image_lineMap] at h
    rw [uIoc_of_le zero_le_one] at ht
    apply_rules [(Ï‰ _).le_of_opNorm_le, mem_image_of_mem, Ioc_subset_Icc_self]
  _ = C * â€–b - aâ€– := by simp

/-- If a 1-form `Ï‰` is continuous on a set `s`,
then it is path integrable along any $C^1$ path in this set. -/
theorem ContinuousOn.pathIntegrable_of_contDiffOn [NormedSpace â„ E] {s : Set E}
    (hÏ‰ : ContinuousOn Ï‰ s) (hÎ³ : ContDiffOn â„ 1 Î³.extend I) (hÎ³s : âˆ€ t, Î³ t âˆˆ s) :
    PathIntegrable Ï‰ Î³ := by
  apply ContinuousOn.intervalIntegrable_of_Icc zero_le_one
  simp only [funext (pathIntegralFun_def Ï‰ Î³)]
  apply ContinuousOn.clm_apply
  Â· exact hÏ‰.comp (by fun_prop) fun _ _ â†¦ hÎ³s _
  Â· exact hÎ³.continuousOn_derivWithin uniqueDiffOn_Icc_zero_one le_rfl

end PathOperations

/-!
### Algebraic operations on the 1-form
-/

section Algebra

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {a b : E}
  {Ï‰ Ï‰â‚ Ï‰â‚‚ : E â†’ E â†’L[ğ•œ] F} {Î³ : Path a b} {t : â„}

@[simp]
theorem pathIntegralFun_add :
    pathIntegralFun (Ï‰â‚ + Ï‰â‚‚) Î³ = pathIntegralFun Ï‰â‚ Î³ + pathIntegralFun Ï‰â‚‚ Î³ := by
  ext; simp [pathIntegralFun]

protected nonrec theorem PathIntegrable.add (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    PathIntegrable (Ï‰â‚ + Ï‰â‚‚) Î³ := by
  simpa [PathIntegrable] using hâ‚.add hâ‚‚

theorem pathIntegral_add (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    pathIntegral (Ï‰â‚ + Ï‰â‚‚) Î³ = (âˆ«áµ– x in Î³, Ï‰â‚ x) + âˆ«áµ– x in Î³, Ï‰â‚‚ x := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp only [pathIntegral, pathIntegralFun_add]
  exact intervalIntegral.integral_add hâ‚ hâ‚‚

theorem pathIntegral_fun_add (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    âˆ«áµ– x in Î³, Ï‰â‚ x + Ï‰â‚‚ x = (âˆ«áµ– x in Î³, Ï‰â‚ x) + âˆ«áµ– x in Î³, Ï‰â‚‚ x :=
  pathIntegral_add hâ‚ hâ‚‚

@[simp]
theorem pathIntegralFun_zero : pathIntegralFun (0 : E â†’ E â†’L[ğ•œ] F) Î³ = 0 := by
  ext; simp [pathIntegralFun]

@[simp]
theorem pathIntegralFun_fun_zero : pathIntegralFun (fun _ â†¦ 0 : E â†’ E â†’L[ğ•œ] F) Î³ = 0 :=
  pathIntegralFun_zero

theorem PathIntegrable.zero : PathIntegrable (0 : E â†’ E â†’L[ğ•œ] F) Î³ := by
  simp [PathIntegrable, IntervalIntegrable.zero]

theorem PathIntegrable.fun_zero : PathIntegrable (fun _ â†¦ 0 : E â†’ E â†’L[ğ•œ] F) Î³ := .zero

@[simp]
theorem pathIntegral_zero : pathIntegral (0 : E â†’ E â†’L[ğ•œ] F) Î³ = 0 := by simp [pathIntegral]

@[simp]
theorem pathIntegral_fun_zero : âˆ«áµ– _ in Î³, (0 : E â†’L[ğ•œ] F) = 0 := pathIntegral_zero

@[simp]
theorem pathIntegralFun_neg : pathIntegralFun (-Ï‰) Î³ = -pathIntegralFun Ï‰ Î³ := by
  ext; simp [pathIntegralFun]

nonrec theorem PathIntegrable.neg (h : PathIntegrable Ï‰ Î³) : PathIntegrable (-Ï‰) Î³ := by
  simpa [PathIntegrable] using h.neg

theorem PathIntegrable.fun_neg (h : PathIntegrable Ï‰ Î³) : PathIntegrable (-Ï‰ Â·) Î³ :=
  h.neg

@[simp]
theorem PathIntegrable.neg_iff : PathIntegrable (-Ï‰) Î³ â†” PathIntegrable Ï‰ Î³ :=
  âŸ¨fun h â†¦ by simpa using h.neg, .negâŸ©

@[simp]
theorem PathIntegrable.fun_neg_iff : PathIntegrable (-Ï‰ Â·) Î³ â†” PathIntegrable Ï‰ Î³ :=
  PathIntegrable.neg_iff

@[simp]
theorem pathIntegral_neg : pathIntegral (-Ï‰) Î³ = -âˆ«áµ– x in Î³, Ï‰ x := by
  simp [pathIntegral]

@[simp]
theorem pathIntegral_fun_neg : âˆ«áµ– x in Î³, -Ï‰ x = -âˆ«áµ– x in Î³, Ï‰ x := pathIntegral_neg

@[simp]
theorem pathIntegralFun_sub :
    pathIntegralFun (Ï‰â‚ - Ï‰â‚‚) Î³ = pathIntegralFun Ï‰â‚ Î³ - pathIntegralFun Ï‰â‚‚ Î³ := by
  simp [sub_eq_add_neg]

protected nonrec theorem PathIntegrable.sub (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    PathIntegrable (Ï‰â‚ - Ï‰â‚‚) Î³ :=
  sub_eq_add_neg Ï‰â‚ Ï‰â‚‚ â–¸ hâ‚.add hâ‚‚.neg

theorem pathIntegral_sub (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    pathIntegral (Ï‰â‚ - Ï‰â‚‚) Î³ = (âˆ«áµ– x in Î³, Ï‰â‚ x) - âˆ«áµ– x in Î³, Ï‰â‚‚ x := by
  rw [sub_eq_add_neg, sub_eq_add_neg, pathIntegral_add hâ‚ hâ‚‚.neg, pathIntegral_neg]

theorem pathIntegral_fun_sub (hâ‚ : PathIntegrable Ï‰â‚ Î³) (hâ‚‚ : PathIntegrable Ï‰â‚‚ Î³) :
    âˆ«áµ– x in Î³, Ï‰â‚ x - Ï‰â‚‚ x = (âˆ«áµ– x in Î³, Ï‰â‚ x) - âˆ«áµ– x in Î³, Ï‰â‚‚ x :=
  pathIntegral_sub hâ‚ hâ‚‚


section RestrictScalars

variable {ğ• : Type*} [RCLike ğ•] [NormedSpace ğ• F] [NormedSpace ğ• E]
  [LinearMap.CompatibleSMul E F ğ• ğ•œ]

@[simp]
theorem pathIntegralFun_restrictScalars :
    pathIntegralFun (fun t â†¦ (Ï‰ t).restrictScalars ğ•) Î³ = pathIntegralFun Ï‰ Î³ := by
  ext
  letI : NormedSpace â„ E := .restrictScalars â„ ğ•œ E
  simp [pathIntegralFun_def]

@[simp]
theorem PathIntegrable.restrictScalars_iff :
    PathIntegrable (fun t â†¦ (Ï‰ t).restrictScalars ğ•) Î³ â†” PathIntegrable Ï‰ Î³ := by
  simp [PathIntegrable]

@[simp]
theorem pathIntegral_restrictScalars :
    âˆ«áµ– x in Î³, (Ï‰ x).restrictScalars ğ• = âˆ«áµ– x in Î³, Ï‰ x := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp [pathIntegral_def]

end RestrictScalars

variable {ğ• : Type*} [RCLike ğ•] [NormedSpace ğ• F] [SMulCommClass ğ•œ ğ• F] {c : ğ•}

@[simp]
theorem pathIntegralFun_smul : pathIntegralFun (c â€¢ Ï‰) Î³ = c â€¢ pathIntegralFun Ï‰ Î³ := by
  ext
  simp [pathIntegralFun]

nonrec theorem PathIntegrable.smul (h : PathIntegrable Ï‰ Î³) :
    PathIntegrable (c â€¢ Ï‰) Î³ := by
  simpa [PathIntegrable] using h.smul c

@[simp]
theorem PathIntegrable.smul_iff : PathIntegrable (c â€¢ Ï‰) Î³ â†” c = 0 âˆ¨ PathIntegrable Ï‰ Î³ := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp [PathIntegrable.zero]
  Â· simp only [hc, false_or]
    refine âŸ¨fun h â†¦ ?_, .smulâŸ©
    simpa [hc] using h.smul (c := câ»Â¹)

@[simp]
theorem pathIntegral_smul : pathIntegral (c â€¢ Ï‰) Î³ = c â€¢ pathIntegral Ï‰ Î³ := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp [pathIntegral_def, intervalIntegral.integral_smul]

@[simp]
theorem pathIntegral_fun_smul : âˆ«áµ– x in Î³, c â€¢ Ï‰ x = c â€¢ âˆ«áµ– x in Î³, Ï‰ x := pathIntegral_smul

end Algebra

section FDeriv

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
  {a b : E} {s : Set E} {Ï‰ : E â†’ E â†’L[ğ•œ] F}

/-- The integral of `Ï‰` along `[a -[â„] b]` has derivative `Ï‰ a` in `b` at `b = a`.
This is a `HasFDerivWithinAt` version assuming that `Ï‰` is continuous within `s`
in a neighborhood of `a` within `s`. -/
theorem HasFDerivWithinAt.pathIntegral_segment_source' (hs : Convex â„ s)
    (hÏ‰ : âˆ€á¶  x in ğ“[s] a, ContinuousWithinAt Ï‰ s x) (ha : a âˆˆ s) :
    HasFDerivWithinAt (âˆ«áµ– x in .segment a Â·, Ï‰ x) (Ï‰ a) s a := by
  simp only [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO, Path.segment_same,
    pathIntegral_refl, sub_zero, Asymptotics.isLittleO_iff]
  intro Îµ hÎµ
  obtain âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© : âˆƒ Î´ > 0,
      ball a Î´ âˆ© s âŠ† {z | ContinuousWithinAt Ï‰ s z âˆ§ dist (Ï‰ z) (Ï‰ a) â‰¤ Îµ} := by
    rw [â† Metric.mem_nhdsWithin_iff, setOf_and, inter_mem_iff]
    exact âŸ¨hÏ‰, (hÏ‰.self_of_nhdsWithin ha).eventually <| closedBall_mem_nhds _ hÎµâŸ©
  rw [eventually_nhdsWithin_iff]
  filter_upwards [Metric.ball_mem_nhds _ hÎ´â‚€] with b hb hbs
  have hsub : [a -[â„] b] âŠ† ball a Î´ âˆ© s :=
    ((convex_ball _ _).inter hs).segment_subset (by simp [*]) (by simp [*])
  rw [â† pathIntegral_segment_const, â† pathIntegral_fun_sub]
  Â· refine norm_pathIntegral_segment_le fun z hz â†¦ (hÎ´ (hsub hz)).2
  Â· rw [pathIntegrable_segment]
    refine ContinuousOn.intervalIntegrable_of_Icc zero_le_one fun t ht â†¦ ?_
    refine ((hÎ´ ?_).1.eval_const _).comp AffineMap.lineMap_continuous.continuousWithinAt ?_
    Â· refine hsub <| segment_eq_image_lineMap â„ a b â–¸ mem_image_of_mem _ ht
    Â· rw [mapsTo', â† segment_eq_image_lineMap]
      exact hs.segment_subset ha hbs
  Â· rw [pathIntegrable_segment]
    exact intervalIntegrable_const

/-- The integral of `Ï‰` along `[a -[â„] b]` has derivative `Ï‰ a` in `b` at `b = a`.
This is a `HasFDerivWithinAt` version assuming that `Ï‰` is continuous on `s`. -/
theorem HasFDerivWithinAt.pathIntegral_segment_source (hs : Convex â„ s) (hÏ‰ : ContinuousOn Ï‰ s)
    (ha : a âˆˆ s) : HasFDerivWithinAt (âˆ«áµ– x in .segment a Â·, Ï‰ x) (Ï‰ a) s a :=
  .pathIntegral_segment_source' hs (mem_of_superset self_mem_nhdsWithin hÏ‰) ha

/-- The integral of `Ï‰` along `[a -[â„] b]` has derivative `Ï‰ a` in `b` at `b = a`.
This is a `HasFDerivAt` version assuming that `Ï‰` is continuous in a neighborhood of `a`. -/
theorem HasFDerivAt.pathIntegral_segment_source' (hÏ‰ : âˆ€á¶  z in ğ“ a, ContinuousAt Ï‰ z) :
    HasFDerivAt (âˆ«áµ– x in .segment a Â·, Ï‰ x) (Ï‰ a) a :=
  HasFDerivWithinAt.pathIntegral_segment_source' convex_univ
    (by simpa only [nhdsWithin_univ, continuousWithinAt_univ]) (mem_univ _) |>.hasFDerivAt_of_univ

/-- The integral of `Ï‰` along `[a -[â„] b]` has derivative `Ï‰ a` in `b` at `b = a`.
This is a `HasFDerivAt` version assuming that `Ï‰` is continuous on the whole space. -/
theorem HasFDerivAt.pathIntegral_segment_source (hÏ‰ : Continuous Ï‰) :
    HasFDerivAt (âˆ«áµ– x in .segment a Â·, Ï‰ x) (Ï‰ a) a :=
  .pathIntegral_segment_source' <| .of_forall fun _ â†¦ hÏ‰.continuousAt

end FDeriv
