/-
Copyright (c) 2020 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Integral.IntegrableOn
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.MeasureTheory.Function.LocallyIntegrable
import Mathlib.Topology.MetricSpace.ThickenedIndicator
import Mathlib.Topology.ContinuousFunction.Compact
import Mathlib.Analysis.NormedSpace.HahnBanach.SeparatingDual

#align_import measure_theory.integral.setIntegral from "leanprover-community/mathlib"@"24e0c85412ff6adbeca08022c25ba4876eedf37a"

/-!
# Set integral

In this file we prove some properties of `âˆ« x in s, f x âˆ‚Î¼`. Recall that this notation
is defined as `âˆ« x, f x âˆ‚(Î¼.restrict s)`. In `integral_indicator` we prove that for a measurable
function `f` and a measurable set `s` this definition coincides with another natural definition:
`âˆ« x, indicator s f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼`, where `indicator s f x` is equal to `f x` for `x âˆˆ s`
and is zero otherwise.

Since `âˆ« x in s, f x âˆ‚Î¼` is a notation, one can rewrite or apply any theorem about `âˆ« x, f x âˆ‚Î¼`
directly. In this file we prove some theorems about dependence of `âˆ« x in s, f x âˆ‚Î¼` on `s`, e.g.
`integral_union`, `integral_empty`, `integral_univ`.

We use the property `IntegrableOn f s Î¼ := Integrable f (Î¼.restrict s)`, defined in
`MeasureTheory.IntegrableOn`. We also defined in that same file a predicate
`IntegrableAtFilter (f : X â†’ E) (l : Filter X) (Î¼ : Measure X)` saying that `f` is integrable at
some set `s âˆˆ l`.

Finally, we prove a version of the
[Fundamental theorem of calculus](https://en.wikipedia.org/wiki/Fundamental_theorem_of_calculus)
for set integral, see `Filter.Tendsto.integral_sub_linear_isLittleO_ae` and its corollaries.
Namely, consider a measurably generated filter `l`, a measure `Î¼` finite at this filter, and
a function `f` that has a finite limit `c` at `l âŠ“ ae Î¼`. Then `âˆ« x in s, f x âˆ‚Î¼ = Î¼ s â€¢ c + o(Î¼ s)`
as `s` tends to `l.smallSets`, i.e. for any `Îµ>0` there exists `t âˆˆ l` such that
`â€–âˆ« x in s, f x âˆ‚Î¼ - Î¼ s â€¢ câ€– â‰¤ Îµ * Î¼ s` whenever `s âŠ† t`. We also formulate a version of this
theorem for a locally finite measure `Î¼` and a function `f` continuous at a point `a`.

## Notation

We provide the following notations for expressing the integral of a function on a set :
* `âˆ« x in s, f x âˆ‚Î¼` is `MeasureTheory.integral (Î¼.restrict s) f`
* `âˆ« x in s, f x` is `âˆ« x in s, f x âˆ‚volume`

Note that the set notations are defined in the file `Mathlib/MeasureTheory/Integral/Bochner.lean`,
but we reference them here because all theorems about set integrals are in this file.

-/


assert_not_exists InnerProductSpace

noncomputable section

open Set Filter TopologicalSpace MeasureTheory Function RCLike

open scoped Classical Topology ENNReal NNReal

variable {X Y E F : Type*} [MeasurableSpace X]

namespace MeasureTheory

section NormedAddCommGroup

variable [NormedAddCommGroup E] [NormedSpace â„ E]
  {f g : X â†’ E} {s t : Set X} {Î¼ Î½ : Measure X} {l l' : Filter X}

theorem setIntegral_congr_aeâ‚€ (hs : NullMeasurableSet s Î¼) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = g x) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼ :=
  integral_congr_ae ((ae_restrict_iff'â‚€ hs).2 h)
#align measure_theory.set_integral_congr_aeâ‚€ MeasureTheory.setIntegral_congr_aeâ‚€

@[deprecated]
alias set_integral_congr_aeâ‚€ :=
  setIntegral_congr_aeâ‚€ -- deprecated on 2024-04-17

theorem setIntegral_congr_ae (hs : MeasurableSet s) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = g x) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼ :=
  integral_congr_ae ((ae_restrict_iff' hs).2 h)
#align measure_theory.set_integral_congr_ae MeasureTheory.setIntegral_congr_ae

@[deprecated]
alias set_integral_congr_ae :=
  setIntegral_congr_ae -- deprecated on 2024-04-17

theorem setIntegral_congrâ‚€ (hs : NullMeasurableSet s Î¼) (h : EqOn f g s) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼ :=
  setIntegral_congr_aeâ‚€ hs <| eventually_of_forall h
#align measure_theory.set_integral_congrâ‚€ MeasureTheory.setIntegral_congrâ‚€

@[deprecated]
alias set_integral_congrâ‚€ :=
  setIntegral_congrâ‚€ -- deprecated on 2024-04-17

theorem setIntegral_congr (hs : MeasurableSet s) (h : EqOn f g s) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼ :=
  setIntegral_congr_ae hs <| eventually_of_forall h
#align measure_theory.set_integral_congr MeasureTheory.setIntegral_congr

@[deprecated]
alias set_integral_congr :=
  setIntegral_congr -- deprecated on 2024-04-17

theorem setIntegral_congr_set_ae (hst : s =áµ[Î¼] t) : âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in t, f x âˆ‚Î¼ := by
  rw [Measure.restrict_congr_set hst]
#align measure_theory.set_integral_congr_set_ae MeasureTheory.setIntegral_congr_set_ae

@[deprecated]
alias set_integral_congr_set_ae :=
  setIntegral_congr_set_ae -- deprecated on 2024-04-17

theorem integral_union_ae (hst : AEDisjoint Î¼ s t) (ht : NullMeasurableSet t Î¼)
    (hfs : IntegrableOn f s Î¼) (hft : IntegrableOn f t Î¼) :
    âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in t, f x âˆ‚Î¼ := by
  simp only [IntegrableOn, Measure.restrict_unionâ‚€ hst ht, integral_add_measure hfs hft]
#align measure_theory.integral_union_ae MeasureTheory.integral_union_ae

theorem integral_union (hst : Disjoint s t) (ht : MeasurableSet t) (hfs : IntegrableOn f s Î¼)
    (hft : IntegrableOn f t Î¼) : âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in t, f x âˆ‚Î¼ :=
  integral_union_ae hst.aedisjoint ht.nullMeasurableSet hfs hft
#align measure_theory.integral_union MeasureTheory.integral_union

theorem integral_diff (ht : MeasurableSet t) (hfs : IntegrableOn f s Î¼) (hts : t âŠ† s) :
    âˆ« x in s \ t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ - âˆ« x in t, f x âˆ‚Î¼ := by
  rw [eq_sub_iff_add_eq, â† integral_union, diff_union_of_subset hts]
  exacts [disjoint_sdiff_self_left, ht, hfs.mono_set (diff_subset _ _), hfs.mono_set hts]
#align measure_theory.integral_diff MeasureTheory.integral_diff

theorem integral_inter_add_diffâ‚€ (ht : NullMeasurableSet t Î¼) (hfs : IntegrableOn f s Î¼) :
    âˆ« x in s âˆ© t, f x âˆ‚Î¼ + âˆ« x in s \ t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† Measure.restrict_inter_add_diffâ‚€ s ht, integral_add_measure]
  Â· exact Integrable.mono_measure hfs (Measure.restrict_mono (inter_subset_left _ _) le_rfl)
  Â· exact Integrable.mono_measure hfs (Measure.restrict_mono (diff_subset _ _) le_rfl)
#align measure_theory.integral_inter_add_diffâ‚€ MeasureTheory.integral_inter_add_diffâ‚€

theorem integral_inter_add_diff (ht : MeasurableSet t) (hfs : IntegrableOn f s Î¼) :
    âˆ« x in s âˆ© t, f x âˆ‚Î¼ + âˆ« x in s \ t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
  integral_inter_add_diffâ‚€ ht.nullMeasurableSet hfs
#align measure_theory.integral_inter_add_diff MeasureTheory.integral_inter_add_diff

theorem integral_finset_biUnion {Î¹ : Type*} (t : Finset Î¹) {s : Î¹ â†’ Set X}
    (hs : âˆ€ i âˆˆ t, MeasurableSet (s i)) (h's : Set.Pairwise (â†‘t) (Disjoint on s))
    (hf : âˆ€ i âˆˆ t, IntegrableOn f (s i) Î¼) :
    âˆ« x in â‹ƒ i âˆˆ t, s i, f x âˆ‚Î¼ = âˆ‘ i âˆˆ t, âˆ« x in s i, f x âˆ‚Î¼ := by
  induction' t using Finset.induction_on with a t hat IH hs h's
  Â· simp
  Â· simp only [Finset.coe_insert, Finset.forall_mem_insert, Set.pairwise_insert,
      Finset.set_biUnion_insert] at hs hf h's âŠ¢
    rw [integral_union _ _ hf.1 (integrableOn_finset_iUnion.2 hf.2)]
    Â· rw [Finset.sum_insert hat, IH hs.2 h's.1 hf.2]
    Â· simp only [disjoint_iUnion_right]
      exact fun i hi => (h's.2 i hi (ne_of_mem_of_not_mem hi hat).symm).1
    Â· exact Finset.measurableSet_biUnion _ hs.2
#align measure_theory.integral_finset_bUnion MeasureTheory.integral_finset_biUnion

theorem integral_fintype_iUnion {Î¹ : Type*} [Fintype Î¹] {s : Î¹ â†’ Set X}
    (hs : âˆ€ i, MeasurableSet (s i)) (h's : Pairwise (Disjoint on s))
    (hf : âˆ€ i, IntegrableOn f (s i) Î¼) : âˆ« x in â‹ƒ i, s i, f x âˆ‚Î¼ = âˆ‘ i, âˆ« x in s i, f x âˆ‚Î¼ := by
  convert integral_finset_biUnion Finset.univ (fun i _ => hs i) _ fun i _ => hf i
  Â· simp
  Â· simp [pairwise_univ, h's]
#align measure_theory.integral_fintype_Union MeasureTheory.integral_fintype_iUnion

theorem integral_empty : âˆ« x in âˆ…, f x âˆ‚Î¼ = 0 := by
  rw [Measure.restrict_empty, integral_zero_measure]
#align measure_theory.integral_empty MeasureTheory.integral_empty

theorem integral_univ : âˆ« x in univ, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by rw [Measure.restrict_univ]
#align measure_theory.integral_univ MeasureTheory.integral_univ

theorem integral_add_complâ‚€ (hs : NullMeasurableSet s Î¼) (hfi : Integrable f Î¼) :
    âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  rw [
    â† integral_union_ae disjoint_compl_right.aedisjoint hs.compl hfi.integrableOn hfi.integrableOn,
    union_compl_self, integral_univ]
#align measure_theory.integral_add_complâ‚€ MeasureTheory.integral_add_complâ‚€

theorem integral_add_compl (hs : MeasurableSet s) (hfi : Integrable f Î¼) :
    âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
  integral_add_complâ‚€ hs.nullMeasurableSet hfi
#align measure_theory.integral_add_compl MeasureTheory.integral_add_compl

/-- For a function `f` and a measurable set `s`, the integral of `indicator s f`
over the whole space is equal to `âˆ« x in s, f x âˆ‚Î¼` defined as `âˆ« x, f x âˆ‚(Î¼.restrict s)`. -/
theorem integral_indicator (hs : MeasurableSet s) :
    âˆ« x, indicator s f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  by_cases hfi : IntegrableOn f s Î¼; swap
  Â· rw [integral_undef hfi, integral_undef]
    rwa [integrable_indicator_iff hs]
  calc
    âˆ« x, indicator s f x âˆ‚Î¼ = âˆ« x in s, indicator s f x âˆ‚Î¼ + âˆ« x in sá¶œ, indicator s f x âˆ‚Î¼ :=
      (integral_add_compl hs (hfi.integrable_indicator hs)).symm
    _ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, 0 âˆ‚Î¼ :=
      (congr_argâ‚‚ (Â· + Â·) (integral_congr_ae (indicator_ae_eq_restrict hs))
        (integral_congr_ae (indicator_ae_eq_restrict_compl hs)))
    _ = âˆ« x in s, f x âˆ‚Î¼ := by simp
#align measure_theory.integral_indicator MeasureTheory.integral_indicator

theorem setIntegral_indicator (ht : MeasurableSet t) :
    âˆ« x in s, t.indicator f x âˆ‚Î¼ = âˆ« x in s âˆ© t, f x âˆ‚Î¼ := by
  rw [integral_indicator ht, Measure.restrict_restrict ht, Set.inter_comm]
#align measure_theory.set_integral_indicator MeasureTheory.setIntegral_indicator

@[deprecated]
alias set_integral_indicator :=
  setIntegral_indicator -- deprecated on 2024-04-17

theorem ofReal_setIntegral_one_of_measure_ne_top {X : Type*} {m : MeasurableSpace X}
    {Î¼ : Measure X} {s : Set X} (hs : Î¼ s â‰  âˆ) : ENNReal.ofReal (âˆ« _ in s, (1 : â„) âˆ‚Î¼) = Î¼ s :=
  calc
    ENNReal.ofReal (âˆ« _ in s, (1 : â„) âˆ‚Î¼) = ENNReal.ofReal (âˆ« _ in s, â€–(1 : â„)â€– âˆ‚Î¼) := by
      simp only [norm_one]
    _ = âˆ«â» _ in s, 1 âˆ‚Î¼ := by
      rw [ofReal_integral_norm_eq_lintegral_nnnorm (integrableOn_const.2 (Or.inr hs.lt_top))]
      simp only [nnnorm_one, ENNReal.coe_one]
    _ = Î¼ s := set_lintegral_one _
#align measure_theory.of_real_set_integral_one_of_measure_ne_top MeasureTheory.ofReal_setIntegral_one_of_measure_ne_top

@[deprecated]
alias ofReal_set_integral_one_of_measure_ne_top :=
  ofReal_setIntegral_one_of_measure_ne_top -- deprecated on 2024-04-17

theorem ofReal_setIntegral_one {X : Type*} {_ : MeasurableSpace X} (Î¼ : Measure X)
    [IsFiniteMeasure Î¼] (s : Set X) : ENNReal.ofReal (âˆ« _ in s, (1 : â„) âˆ‚Î¼) = Î¼ s :=
  ofReal_setIntegral_one_of_measure_ne_top (measure_ne_top Î¼ s)
#align measure_theory.of_real_set_integral_one MeasureTheory.ofReal_setIntegral_one

@[deprecated]
alias ofReal_set_integral_one :=
  ofReal_setIntegral_one -- deprecated on 2024-04-17

theorem integral_piecewise [DecidablePred (Â· âˆˆ s)] (hs : MeasurableSet s) (hf : IntegrableOn f s Î¼)
    (hg : IntegrableOn g sá¶œ Î¼) :
    âˆ« x, s.piecewise f g x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, g x âˆ‚Î¼ := by
  rw [â† Set.indicator_add_compl_eq_piecewise,
    integral_add' (hf.integrable_indicator hs) (hg.integrable_indicator hs.compl),
    integral_indicator hs, integral_indicator hs.compl]
#align measure_theory.integral_piecewise MeasureTheory.integral_piecewise

theorem tendsto_setIntegral_of_monotone {Î¹ : Type*} [Countable Î¹] [SemilatticeSup Î¹]
    {s : Î¹ â†’ Set X} (hsm : âˆ€ i, MeasurableSet (s i)) (h_mono : Monotone s)
    (hfi : IntegrableOn f (â‹ƒ n, s n) Î¼) :
    Tendsto (fun i => âˆ« x in s i, f x âˆ‚Î¼) atTop (ğ“ (âˆ« x in â‹ƒ n, s n, f x âˆ‚Î¼)) := by
  have hfi' : âˆ«â» x in â‹ƒ n, s n, â€–f xâ€–â‚Š âˆ‚Î¼ < âˆ := hfi.2
  set S := â‹ƒ i, s i
  have hSm : MeasurableSet S := MeasurableSet.iUnion hsm
  have hsub : âˆ€ {i}, s i âŠ† S := @(subset_iUnion s)
  rw [â† withDensity_apply _ hSm] at hfi'
  set Î½ := Î¼.withDensity fun x => â€–f xâ€–â‚Š with hÎ½
  refine Metric.nhds_basis_closedBall.tendsto_right_iff.2 fun Îµ Îµ0 => ?_
  lift Îµ to â„â‰¥0 using Îµ0.le
  have : âˆ€á¶  i in atTop, Î½ (s i) âˆˆ Icc (Î½ S - Îµ) (Î½ S + Îµ) :=
    tendsto_measure_iUnion h_mono (ENNReal.Icc_mem_nhds hfi'.ne (ENNReal.coe_pos.2 Îµ0).ne')
  filter_upwards [this] with i hi
  rw [mem_closedBall_iff_norm', â† integral_diff (hsm i) hfi hsub, â† coe_nnnorm, NNReal.coe_le_coe, â†
    ENNReal.coe_le_coe]
  refine (ennnorm_integral_le_lintegral_ennnorm _).trans ?_
  rw [â† withDensity_apply _ (hSm.diff (hsm _)), â† hÎ½, measure_diff hsub (hsm _)]
  exacts [tsub_le_iff_tsub_le.mp hi.1,
    (hi.2.trans_lt <| ENNReal.add_lt_top.2 âŸ¨hfi', ENNReal.coe_lt_topâŸ©).ne]
#align measure_theory.tendsto_set_integral_of_monotone MeasureTheory.tendsto_setIntegral_of_monotone

@[deprecated]
alias tendsto_set_integral_of_monotone :=
  tendsto_setIntegral_of_monotone -- deprecated on 2024-04-17

theorem tendsto_setIntegral_of_antitone {Î¹ : Type*} [Countable Î¹] [SemilatticeSup Î¹]
    {s : Î¹ â†’ Set X} (hsm : âˆ€ i, MeasurableSet (s i)) (h_anti : Antitone s)
    (hfi : âˆƒ i, IntegrableOn f (s i) Î¼) :
    Tendsto (fun i â†¦ âˆ« x in s i, f x âˆ‚Î¼) atTop (ğ“ (âˆ« x in â‹‚ n, s n, f x âˆ‚Î¼)) := by
  set S := â‹‚ i, s i
  have hSm : MeasurableSet S := MeasurableSet.iInter hsm
  have hsub i : S âŠ† s i := iInter_subset _ _
  set Î½ := Î¼.withDensity fun x => â€–f xâ€–â‚Š with hÎ½
  refine Metric.nhds_basis_closedBall.tendsto_right_iff.2 fun Îµ Îµ0 => ?_
  lift Îµ to â„â‰¥0 using Îµ0.le
  rcases hfi with âŸ¨iâ‚€, hiâ‚€âŸ©
  have Î½iâ‚€ : Î½ (s iâ‚€) â‰  âˆ := by
    simpa [hsm iâ‚€, Î½, ENNReal.ofReal, norm_toNNReal] using hiâ‚€.norm.lintegral_lt_top.ne
  have Î½S : Î½ S â‰  âˆ := ((measure_mono (hsub iâ‚€)).trans_lt Î½iâ‚€.lt_top).ne
  have : âˆ€á¶  i in atTop, Î½ (s i) âˆˆ Icc (Î½ S - Îµ) (Î½ S + Îµ) := by
    apply tendsto_measure_iInter hsm h_anti âŸ¨iâ‚€, Î½iâ‚€âŸ©
    apply ENNReal.Icc_mem_nhds Î½S (ENNReal.coe_pos.2 Îµ0).ne'
  filter_upwards [this, Ici_mem_atTop iâ‚€] with i hi h'i
  rw [mem_closedBall_iff_norm, â† integral_diff hSm (hiâ‚€.mono_set (h_anti h'i)) (hsub i),
    â† coe_nnnorm, NNReal.coe_le_coe, â† ENNReal.coe_le_coe]
  refine (ennnorm_integral_le_lintegral_ennnorm _).trans ?_
  rw [â† withDensity_apply _ ((hsm _).diff hSm), â† hÎ½, measure_diff (hsub i) hSm Î½S]
  exact tsub_le_iff_left.2 hi.2

@[deprecated]
alias tendsto_set_integral_of_antitone :=
  tendsto_setIntegral_of_antitone -- deprecated on 2024-04-17

theorem hasSum_integral_iUnion_ae {Î¹ : Type*} [Countable Î¹] {s : Î¹ â†’ Set X}
    (hm : âˆ€ i, NullMeasurableSet (s i) Î¼) (hd : Pairwise (AEDisjoint Î¼ on s))
    (hfi : IntegrableOn f (â‹ƒ i, s i) Î¼) :
    HasSum (fun n => âˆ« x in s n, f x âˆ‚Î¼) (âˆ« x in â‹ƒ n, s n, f x âˆ‚Î¼) := by
  simp only [IntegrableOn, Measure.restrict_iUnion_ae hd hm] at hfi âŠ¢
  exact hasSum_integral_measure hfi
#align measure_theory.has_sum_integral_Union_ae MeasureTheory.hasSum_integral_iUnion_ae

theorem hasSum_integral_iUnion {Î¹ : Type*} [Countable Î¹] {s : Î¹ â†’ Set X}
    (hm : âˆ€ i, MeasurableSet (s i)) (hd : Pairwise (Disjoint on s))
    (hfi : IntegrableOn f (â‹ƒ i, s i) Î¼) :
    HasSum (fun n => âˆ« x in s n, f x âˆ‚Î¼) (âˆ« x in â‹ƒ n, s n, f x âˆ‚Î¼) :=
  hasSum_integral_iUnion_ae (fun i => (hm i).nullMeasurableSet) (hd.mono fun _ _ h => h.aedisjoint)
    hfi
#align measure_theory.has_sum_integral_Union MeasureTheory.hasSum_integral_iUnion

theorem integral_iUnion {Î¹ : Type*} [Countable Î¹] {s : Î¹ â†’ Set X} (hm : âˆ€ i, MeasurableSet (s i))
    (hd : Pairwise (Disjoint on s)) (hfi : IntegrableOn f (â‹ƒ i, s i) Î¼) :
    âˆ« x in â‹ƒ n, s n, f x âˆ‚Î¼ = âˆ‘' n, âˆ« x in s n, f x âˆ‚Î¼ :=
  (HasSum.tsum_eq (hasSum_integral_iUnion hm hd hfi)).symm
#align measure_theory.integral_Union MeasureTheory.integral_iUnion

theorem integral_iUnion_ae {Î¹ : Type*} [Countable Î¹] {s : Î¹ â†’ Set X}
    (hm : âˆ€ i, NullMeasurableSet (s i) Î¼) (hd : Pairwise (AEDisjoint Î¼ on s))
    (hfi : IntegrableOn f (â‹ƒ i, s i) Î¼) : âˆ« x in â‹ƒ n, s n, f x âˆ‚Î¼ = âˆ‘' n, âˆ« x in s n, f x âˆ‚Î¼ :=
  (HasSum.tsum_eq (hasSum_integral_iUnion_ae hm hd hfi)).symm
#align measure_theory.integral_Union_ae MeasureTheory.integral_iUnion_ae

theorem setIntegral_eq_zero_of_ae_eq_zero (ht_eq : âˆ€áµ x âˆ‚Î¼, x âˆˆ t â†’ f x = 0) :
    âˆ« x in t, f x âˆ‚Î¼ = 0 := by
  by_cases hf : AEStronglyMeasurable f (Î¼.restrict t); swap
  Â· rw [integral_undef]
    contrapose! hf
    exact hf.1
  have : âˆ« x in t, hf.mk f x âˆ‚Î¼ = 0 := by
    refine integral_eq_zero_of_ae ?_
    rw [EventuallyEq,
      ae_restrict_iff (hf.stronglyMeasurable_mk.measurableSet_eq_fun stronglyMeasurable_zero)]
    filter_upwards [ae_imp_of_ae_restrict hf.ae_eq_mk, ht_eq] with x hx h'x h''x
    rw [â† hx h''x]
    exact h'x h''x
  rw [â† this]
  exact integral_congr_ae hf.ae_eq_mk
#align measure_theory.set_integral_eq_zero_of_ae_eq_zero MeasureTheory.setIntegral_eq_zero_of_ae_eq_zero

@[deprecated]
alias set_integral_eq_zero_of_ae_eq_zero :=
  setIntegral_eq_zero_of_ae_eq_zero -- deprecated on 2024-04-17

theorem setIntegral_eq_zero_of_forall_eq_zero (ht_eq : âˆ€ x âˆˆ t, f x = 0) :
    âˆ« x in t, f x âˆ‚Î¼ = 0 :=
  setIntegral_eq_zero_of_ae_eq_zero (eventually_of_forall ht_eq)
#align measure_theory.set_integral_eq_zero_of_forall_eq_zero MeasureTheory.setIntegral_eq_zero_of_forall_eq_zero

@[deprecated]
alias set_integral_eq_zero_of_forall_eq_zero :=
  setIntegral_eq_zero_of_forall_eq_zero -- deprecated on 2024-04-17

theorem integral_union_eq_left_of_ae_aux (ht_eq : âˆ€áµ x âˆ‚Î¼.restrict t, f x = 0)
    (haux : StronglyMeasurable f) (H : IntegrableOn f (s âˆª t) Î¼) :
    âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  let k := f â»Â¹' {0}
  have hk : MeasurableSet k := by borelize E; exact haux.measurable (measurableSet_singleton _)
  have h's : IntegrableOn f s Î¼ := H.mono (subset_union_left _ _) le_rfl
  have A : âˆ€ u : Set X, âˆ« x in u âˆ© k, f x âˆ‚Î¼ = 0 := fun u =>
    setIntegral_eq_zero_of_forall_eq_zero fun x hx => hx.2
  rw [â† integral_inter_add_diff hk h's, â† integral_inter_add_diff hk H, A, A, zero_add, zero_add,
    union_diff_distrib, union_comm]
  apply setIntegral_congr_set_ae
  rw [union_ae_eq_right]
  apply measure_mono_null (diff_subset _ _)
  rw [measure_zero_iff_ae_nmem]
  filter_upwards [ae_imp_of_ae_restrict ht_eq] with x hx h'x using h'x.2 (hx h'x.1)
#align measure_theory.integral_union_eq_left_of_ae_aux MeasureTheory.integral_union_eq_left_of_ae_aux

theorem integral_union_eq_left_of_ae (ht_eq : âˆ€áµ x âˆ‚Î¼.restrict t, f x = 0) :
    âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  have ht : IntegrableOn f t Î¼ := by apply integrableOn_zero.congr_fun_ae; symm; exact ht_eq
  by_cases H : IntegrableOn f (s âˆª t) Î¼; swap
  Â· rw [integral_undef H, integral_undef]; simpa [integrableOn_union, ht] using H
  let f' := H.1.mk f
  calc
    âˆ« x : X in s âˆª t, f x âˆ‚Î¼ = âˆ« x : X in s âˆª t, f' x âˆ‚Î¼ := integral_congr_ae H.1.ae_eq_mk
    _ = âˆ« x in s, f' x âˆ‚Î¼ := by
      apply
        integral_union_eq_left_of_ae_aux _ H.1.stronglyMeasurable_mk (H.congr_fun_ae H.1.ae_eq_mk)
      filter_upwards [ht_eq,
        ae_mono (Measure.restrict_mono (subset_union_right s t) le_rfl) H.1.ae_eq_mk] with x hx h'x
      rw [â† h'x, hx]
    _ = âˆ« x in s, f x âˆ‚Î¼ :=
      integral_congr_ae
        (ae_mono (Measure.restrict_mono (subset_union_left s t) le_rfl) H.1.ae_eq_mk.symm)
#align measure_theory.integral_union_eq_left_of_ae MeasureTheory.integral_union_eq_left_of_ae

theorem integral_union_eq_left_of_forallâ‚€ {f : X â†’ E} (ht : NullMeasurableSet t Î¼)
    (ht_eq : âˆ€ x âˆˆ t, f x = 0) : âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
  integral_union_eq_left_of_ae ((ae_restrict_iff'â‚€ ht).2 (eventually_of_forall ht_eq))
#align measure_theory.integral_union_eq_left_of_forallâ‚€ MeasureTheory.integral_union_eq_left_of_forallâ‚€

theorem integral_union_eq_left_of_forall {f : X â†’ E} (ht : MeasurableSet t)
    (ht_eq : âˆ€ x âˆˆ t, f x = 0) : âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
  integral_union_eq_left_of_forallâ‚€ ht.nullMeasurableSet ht_eq
#align measure_theory.integral_union_eq_left_of_forall MeasureTheory.integral_union_eq_left_of_forall

theorem setIntegral_eq_of_subset_of_ae_diff_eq_zero_aux (hts : s âŠ† t)
    (h't : âˆ€áµ x âˆ‚Î¼, x âˆˆ t \ s â†’ f x = 0) (haux : StronglyMeasurable f)
    (h'aux : IntegrableOn f t Î¼) : âˆ« x in t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  let k := f â»Â¹' {0}
  have hk : MeasurableSet k := by borelize E; exact haux.measurable (measurableSet_singleton _)
  calc
    âˆ« x in t, f x âˆ‚Î¼ = âˆ« x in t âˆ© k, f x âˆ‚Î¼ + âˆ« x in t \ k, f x âˆ‚Î¼ := by
      rw [integral_inter_add_diff hk h'aux]
    _ = âˆ« x in t \ k, f x âˆ‚Î¼ := by
      rw [setIntegral_eq_zero_of_forall_eq_zero fun x hx => ?_, zero_add]; exact hx.2
    _ = âˆ« x in s \ k, f x âˆ‚Î¼ := by
      apply setIntegral_congr_set_ae
      filter_upwards [h't] with x hx
      change (x âˆˆ t \ k) = (x âˆˆ s \ k)
      simp only [mem_preimage, mem_singleton_iff, eq_iff_iff, and_congr_left_iff, mem_diff]
      intro h'x
      by_cases xs : x âˆˆ s
      Â· simp only [xs, hts xs]
      Â· simp only [xs, iff_false_iff]
        intro xt
        exact h'x (hx âŸ¨xt, xsâŸ©)
    _ = âˆ« x in s âˆ© k, f x âˆ‚Î¼ + âˆ« x in s \ k, f x âˆ‚Î¼ := by
      have : âˆ€ x âˆˆ s âˆ© k, f x = 0 := fun x hx => hx.2
      rw [setIntegral_eq_zero_of_forall_eq_zero this, zero_add]
    _ = âˆ« x in s, f x âˆ‚Î¼ := by rw [integral_inter_add_diff hk (h'aux.mono hts le_rfl)]
#align measure_theory.set_integral_eq_of_subset_of_ae_diff_eq_zero_aux MeasureTheory.setIntegral_eq_of_subset_of_ae_diff_eq_zero_aux

@[deprecated]
alias set_integral_eq_of_subset_of_ae_diff_eq_zero_aux :=
  setIntegral_eq_of_subset_of_ae_diff_eq_zero_aux -- deprecated on 2024-04-17

/-- If a function vanishes almost everywhere on `t \ s` with `s âŠ† t`, then its integrals on `s`
and `t` coincide if `t` is null-measurable. -/
theorem setIntegral_eq_of_subset_of_ae_diff_eq_zero (ht : NullMeasurableSet t Î¼) (hts : s âŠ† t)
    (h't : âˆ€áµ x âˆ‚Î¼, x âˆˆ t \ s â†’ f x = 0) : âˆ« x in t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ := by
  by_cases h : IntegrableOn f t Î¼; swap
  Â· have : Â¬IntegrableOn f s Î¼ := fun H => h (H.of_ae_diff_eq_zero ht h't)
    rw [integral_undef h, integral_undef this]
  let f' := h.1.mk f
  calc
    âˆ« x in t, f x âˆ‚Î¼ = âˆ« x in t, f' x âˆ‚Î¼ := integral_congr_ae h.1.ae_eq_mk
    _ = âˆ« x in s, f' x âˆ‚Î¼ := by
      apply
        setIntegral_eq_of_subset_of_ae_diff_eq_zero_aux hts _ h.1.stronglyMeasurable_mk
          (h.congr h.1.ae_eq_mk)
      filter_upwards [h't, ae_imp_of_ae_restrict h.1.ae_eq_mk] with x hx h'x h''x
      rw [â† h'x h''x.1, hx h''x]
    _ = âˆ« x in s, f x âˆ‚Î¼ := by
      apply integral_congr_ae
      apply ae_restrict_of_ae_restrict_of_subset hts
      exact h.1.ae_eq_mk.symm
#align measure_theory.set_integral_eq_of_subset_of_ae_diff_eq_zero MeasureTheory.setIntegral_eq_of_subset_of_ae_diff_eq_zero

@[deprecated]
alias set_integral_eq_of_subset_of_ae_diff_eq_zero :=
  setIntegral_eq_of_subset_of_ae_diff_eq_zero -- deprecated on 2024-04-17

/-- If a function vanishes on `t \ s` with `s âŠ† t`, then its integrals on `s`
and `t` coincide if `t` is measurable. -/
theorem setIntegral_eq_of_subset_of_forall_diff_eq_zero (ht : MeasurableSet t) (hts : s âŠ† t)
    (h't : âˆ€ x âˆˆ t \ s, f x = 0) : âˆ« x in t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
  setIntegral_eq_of_subset_of_ae_diff_eq_zero ht.nullMeasurableSet hts
    (eventually_of_forall fun x hx => h't x hx)
#align measure_theory.set_integral_eq_of_subset_of_forall_diff_eq_zero MeasureTheory.setIntegral_eq_of_subset_of_forall_diff_eq_zero

@[deprecated]
alias set_integral_eq_of_subset_of_forall_diff_eq_zero :=
  setIntegral_eq_of_subset_of_forall_diff_eq_zero -- deprecated on 2024-04-17

/-- If a function vanishes almost everywhere on `sá¶œ`, then its integral on `s`
coincides with its integral on the whole space. -/
theorem setIntegral_eq_integral_of_ae_compl_eq_zero (h : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ f x = 0) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  symm
  nth_rw 1 [â† integral_univ]
  apply setIntegral_eq_of_subset_of_ae_diff_eq_zero nullMeasurableSet_univ (subset_univ _)
  filter_upwards [h] with x hx h'x using hx h'x.2
#align measure_theory.set_integral_eq_integral_of_ae_compl_eq_zero MeasureTheory.setIntegral_eq_integral_of_ae_compl_eq_zero

@[deprecated]
alias set_integral_eq_integral_of_ae_compl_eq_zero :=
  setIntegral_eq_integral_of_ae_compl_eq_zero -- deprecated on 2024-04-17

/-- If a function vanishes on `sá¶œ`, then its integral on `s` coincides with its integral on the
whole space. -/
theorem setIntegral_eq_integral_of_forall_compl_eq_zero (h : âˆ€ x, x âˆ‰ s â†’ f x = 0) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
  setIntegral_eq_integral_of_ae_compl_eq_zero (eventually_of_forall h)
#align measure_theory.set_integral_eq_integral_of_forall_compl_eq_zero MeasureTheory.setIntegral_eq_integral_of_forall_compl_eq_zero

@[deprecated]
alias set_integral_eq_integral_of_forall_compl_eq_zero :=
  setIntegral_eq_integral_of_forall_compl_eq_zero -- deprecated on 2024-04-17

theorem setIntegral_neg_eq_setIntegral_nonpos [LinearOrder E] {f : X â†’ E}
    (hf : AEStronglyMeasurable f Î¼) :
    âˆ« x in {x | f x < 0}, f x âˆ‚Î¼ = âˆ« x in {x | f x â‰¤ 0}, f x âˆ‚Î¼ := by
  have h_union : {x | f x â‰¤ 0} = {x | f x < 0} âˆª {x | f x = 0} := by
    simp_rw [le_iff_lt_or_eq, setOf_or]
  rw [h_union]
  have B : NullMeasurableSet {x | f x = 0} Î¼ :=
    hf.nullMeasurableSet_eq_fun aestronglyMeasurable_zero
  symm
  refine integral_union_eq_left_of_ae ?_
  filter_upwards [ae_restrict_memâ‚€ B] with x hx using hx
#align measure_theory.set_integral_neg_eq_set_integral_nonpos MeasureTheory.setIntegral_neg_eq_setIntegral_nonpos

@[deprecated]
alias set_integral_neg_eq_set_integral_nonpos :=
  setIntegral_neg_eq_setIntegral_nonpos -- deprecated on 2024-04-17

theorem integral_norm_eq_pos_sub_neg {f : X â†’ â„} (hfi : Integrable f Î¼) :
    âˆ« x, â€–f xâ€– âˆ‚Î¼ = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ - âˆ« x in {x | f x â‰¤ 0}, f x âˆ‚Î¼ :=
  have h_meas : NullMeasurableSet {x | 0 â‰¤ f x} Î¼ :=
    aestronglyMeasurable_const.nullMeasurableSet_le hfi.1
  calc
    âˆ« x, â€–f xâ€– âˆ‚Î¼ = âˆ« x in {x | 0 â‰¤ f x}, â€–f xâ€– âˆ‚Î¼ + âˆ« x in {x | 0 â‰¤ f x}á¶œ, â€–f xâ€– âˆ‚Î¼ := by
      rw [â† integral_add_complâ‚€ h_meas hfi.norm]
    _ = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ + âˆ« x in {x | 0 â‰¤ f x}á¶œ, â€–f xâ€– âˆ‚Î¼ := by
      congr 1
      refine setIntegral_congrâ‚€ h_meas fun x hx => ?_
      dsimp only
      rw [Real.norm_eq_abs, abs_eq_self.mpr _]
      exact hx
    _ = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ - âˆ« x in {x | 0 â‰¤ f x}á¶œ, f x âˆ‚Î¼ := by
      congr 1
      rw [â† integral_neg]
      refine setIntegral_congrâ‚€ h_meas.compl fun x hx => ?_
      dsimp only
      rw [Real.norm_eq_abs, abs_eq_neg_self.mpr _]
      rw [Set.mem_compl_iff, Set.nmem_setOf_iff] at hx
      linarith
    _ = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ - âˆ« x in {x | f x â‰¤ 0}, f x âˆ‚Î¼ := by
      rw [â† setIntegral_neg_eq_setIntegral_nonpos hfi.1, compl_setOf]; simp only [not_le]
#align measure_theory.integral_norm_eq_pos_sub_neg MeasureTheory.integral_norm_eq_pos_sub_neg

theorem setIntegral_const [CompleteSpace E] (c : E) : âˆ« _ in s, c âˆ‚Î¼ = (Î¼ s).toReal â€¢ c := by
  rw [integral_const, Measure.restrict_apply_univ]
#align measure_theory.set_integral_const MeasureTheory.setIntegral_const

@[deprecated]
alias set_integral_const :=
  setIntegral_const -- deprecated on 2024-04-17

@[simp]
theorem integral_indicator_const [CompleteSpace E] (e : E) â¦ƒs : Set Xâ¦„ (s_meas : MeasurableSet s) :
    âˆ« x : X, s.indicator (fun _ : X => e) x âˆ‚Î¼ = (Î¼ s).toReal â€¢ e := by
  rw [integral_indicator s_meas, â† setIntegral_const]
#align measure_theory.integral_indicator_const MeasureTheory.integral_indicator_const

@[simp]
theorem integral_indicator_one â¦ƒs : Set Xâ¦„ (hs : MeasurableSet s) :
    âˆ« x, s.indicator 1 x âˆ‚Î¼ = (Î¼ s).toReal :=
  (integral_indicator_const 1 hs).trans ((smul_eq_mul _).trans (mul_one _))
#align measure_theory.integral_indicator_one MeasureTheory.integral_indicator_one

theorem setIntegral_indicatorConstLp [CompleteSpace E]
    {p : â„â‰¥0âˆ} (hs : MeasurableSet s) (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) (e : E) :
    âˆ« x in s, indicatorConstLp p ht hÎ¼t e x âˆ‚Î¼ = (Î¼ (t âˆ© s)).toReal â€¢ e :=
  calc
    âˆ« x in s, indicatorConstLp p ht hÎ¼t e x âˆ‚Î¼ = âˆ« x in s, t.indicator (fun _ => e) x âˆ‚Î¼ := by
      rw [setIntegral_congr_ae hs (indicatorConstLp_coeFn.mono fun x hx _ => hx)]
    _ = (Î¼ (t âˆ© s)).toReal â€¢ e := by rw [integral_indicator_const _ ht, Measure.restrict_apply ht]
set_option linter.uppercaseLean3 false in
#align measure_theory.set_integral_indicator_const_Lp MeasureTheory.setIntegral_indicatorConstLp

@[deprecated]
alias set_integral_indicatorConstLp :=
  setIntegral_indicatorConstLp -- deprecated on 2024-04-17

theorem integral_indicatorConstLp [CompleteSpace E]
    {p : â„â‰¥0âˆ} (ht : MeasurableSet t) (hÎ¼t : Î¼ t â‰  âˆ) (e : E) :
    âˆ« x, indicatorConstLp p ht hÎ¼t e x âˆ‚Î¼ = (Î¼ t).toReal â€¢ e :=
  calc
    âˆ« x, indicatorConstLp p ht hÎ¼t e x âˆ‚Î¼ = âˆ« x in univ, indicatorConstLp p ht hÎ¼t e x âˆ‚Î¼ := by
      rw [integral_univ]
    _ = (Î¼ (t âˆ© univ)).toReal â€¢ e := setIntegral_indicatorConstLp MeasurableSet.univ ht hÎ¼t e
    _ = (Î¼ t).toReal â€¢ e := by rw [inter_univ]
set_option linter.uppercaseLean3 false in
#align measure_theory.integral_indicator_const_Lp MeasureTheory.integral_indicatorConstLp

theorem setIntegral_map {Y} [MeasurableSpace Y] {g : X â†’ Y} {f : Y â†’ E} {s : Set Y}
    (hs : MeasurableSet s) (hf : AEStronglyMeasurable f (Measure.map g Î¼)) (hg : AEMeasurable g Î¼) :
    âˆ« y in s, f y âˆ‚Measure.map g Î¼ = âˆ« x in g â»Â¹' s, f (g x) âˆ‚Î¼ := by
  rw [Measure.restrict_map_of_aemeasurable hg hs,
    integral_map (hg.mono_measure Measure.restrict_le_self) (hf.mono_measure _)]
  exact Measure.map_mono_of_aemeasurable Measure.restrict_le_self hg
#align measure_theory.set_integral_map MeasureTheory.setIntegral_map

@[deprecated]
alias set_integral_map :=
  setIntegral_map -- deprecated on 2024-04-17

theorem _root_.MeasurableEmbedding.setIntegral_map {Y} {_ : MeasurableSpace Y} {f : X â†’ Y}
    (hf : MeasurableEmbedding f) (g : Y â†’ E) (s : Set Y) :
    âˆ« y in s, g y âˆ‚Measure.map f Î¼ = âˆ« x in f â»Â¹' s, g (f x) âˆ‚Î¼ := by
  rw [hf.restrict_map, hf.integral_map]
#align measurable_embedding.set_integral_map MeasurableEmbedding.setIntegral_map

@[deprecated]
alias _root_.MeasurableEmbedding.set_integral_map :=
  _root_.MeasurableEmbedding.setIntegral_map -- deprecated on 2024-04-17

theorem _root_.ClosedEmbedding.setIntegral_map [TopologicalSpace X] [BorelSpace X] {Y}
    [MeasurableSpace Y] [TopologicalSpace Y] [BorelSpace Y] {g : X â†’ Y} {f : Y â†’ E} (s : Set Y)
    (hg : ClosedEmbedding g) : âˆ« y in s, f y âˆ‚Measure.map g Î¼ = âˆ« x in g â»Â¹' s, f (g x) âˆ‚Î¼ :=
  hg.measurableEmbedding.setIntegral_map _ _
#align closed_embedding.set_integral_map ClosedEmbedding.setIntegral_map

@[deprecated]
alias _root_.ClosedEmbedding.set_integral_map :=
  _root_.ClosedEmbedding.setIntegral_map -- deprecated on 2024-04-17

theorem MeasurePreserving.setIntegral_preimage_emb {Y} {_ : MeasurableSpace Y} {f : X â†’ Y} {Î½}
    (hâ‚ : MeasurePreserving f Î¼ Î½) (hâ‚‚ : MeasurableEmbedding f) (g : Y â†’ E) (s : Set Y) :
    âˆ« x in f â»Â¹' s, g (f x) âˆ‚Î¼ = âˆ« y in s, g y âˆ‚Î½ :=
  (hâ‚.restrict_preimage_emb hâ‚‚ s).integral_comp hâ‚‚ _
#align measure_theory.measure_preserving.set_integral_preimage_emb MeasureTheory.MeasurePreserving.setIntegral_preimage_emb

@[deprecated]
alias MeasurePreserving.set_integral_preimage_emb :=
  MeasurePreserving.setIntegral_preimage_emb -- deprecated on 2024-04-17

theorem MeasurePreserving.setIntegral_image_emb {Y} {_ : MeasurableSpace Y} {f : X â†’ Y} {Î½}
    (hâ‚ : MeasurePreserving f Î¼ Î½) (hâ‚‚ : MeasurableEmbedding f) (g : Y â†’ E) (s : Set X) :
    âˆ« y in f '' s, g y âˆ‚Î½ = âˆ« x in s, g (f x) âˆ‚Î¼ :=
  Eq.symm <| (hâ‚.restrict_image_emb hâ‚‚ s).integral_comp hâ‚‚ _
#align measure_theory.measure_preserving.set_integral_image_emb MeasureTheory.MeasurePreserving.setIntegral_image_emb

@[deprecated]
alias MeasurePreserving.set_integral_image_emb :=
  MeasurePreserving.setIntegral_image_emb -- deprecated on 2024-04-17

theorem setIntegral_map_equiv {Y} [MeasurableSpace Y] (e : X â‰ƒáµ Y) (f : Y â†’ E) (s : Set Y) :
    âˆ« y in s, f y âˆ‚Measure.map e Î¼ = âˆ« x in e â»Â¹' s, f (e x) âˆ‚Î¼ :=
  e.measurableEmbedding.setIntegral_map f s
#align measure_theory.set_integral_map_equiv MeasureTheory.setIntegral_map_equiv

@[deprecated]
alias set_integral_map_equiv :=
  setIntegral_map_equiv -- deprecated on 2024-04-17

theorem norm_setIntegral_le_of_norm_le_const_ae {C : â„} (hs : Î¼ s < âˆ)
    (hC : âˆ€áµ x âˆ‚Î¼.restrict s, â€–f xâ€– â‰¤ C) : â€–âˆ« x in s, f x âˆ‚Î¼â€– â‰¤ C * (Î¼ s).toReal := by
  rw [â† Measure.restrict_apply_univ] at *
  haveI : IsFiniteMeasure (Î¼.restrict s) := âŸ¨hsâŸ©
  exact norm_integral_le_of_norm_le_const hC
#align measure_theory.norm_set_integral_le_of_norm_le_const_ae MeasureTheory.norm_setIntegral_le_of_norm_le_const_ae

@[deprecated]
alias norm_set_integral_le_of_norm_le_const_ae :=
  norm_setIntegral_le_of_norm_le_const_ae -- deprecated on 2024-04-17

theorem norm_setIntegral_le_of_norm_le_const_ae' {C : â„} (hs : Î¼ s < âˆ)
    (hC : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ â€–f xâ€– â‰¤ C) (hfm : AEStronglyMeasurable f (Î¼.restrict s)) :
    â€–âˆ« x in s, f x âˆ‚Î¼â€– â‰¤ C * (Î¼ s).toReal := by
  apply norm_setIntegral_le_of_norm_le_const_ae hs
  have A : âˆ€áµ x : X âˆ‚Î¼, x âˆˆ s â†’ â€–AEStronglyMeasurable.mk f hfm xâ€– â‰¤ C := by
    filter_upwards [hC, hfm.ae_mem_imp_eq_mk] with _ h1 h2 h3
    rw [â† h2 h3]
    exact h1 h3
  have B : MeasurableSet {x | â€–hfm.mk f xâ€– â‰¤ C} :=
    hfm.stronglyMeasurable_mk.norm.measurable measurableSet_Iic
  filter_upwards [hfm.ae_eq_mk, (ae_restrict_iff B).2 A] with _ h1 _
  rwa [h1]
#align measure_theory.norm_set_integral_le_of_norm_le_const_ae' MeasureTheory.norm_setIntegral_le_of_norm_le_const_ae'

@[deprecated]
alias norm_set_integral_le_of_norm_le_const_ae' :=
  norm_setIntegral_le_of_norm_le_const_ae' -- deprecated on 2024-04-17

theorem norm_setIntegral_le_of_norm_le_const_ae'' {C : â„} (hs : Î¼ s < âˆ) (hsm : MeasurableSet s)
    (hC : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ â€–f xâ€– â‰¤ C) : â€–âˆ« x in s, f x âˆ‚Î¼â€– â‰¤ C * (Î¼ s).toReal :=
  norm_setIntegral_le_of_norm_le_const_ae hs <| by
    rwa [ae_restrict_eq hsm, eventually_inf_principal]
#align measure_theory.norm_set_integral_le_of_norm_le_const_ae'' MeasureTheory.norm_setIntegral_le_of_norm_le_const_ae''

@[deprecated]
alias norm_set_integral_le_of_norm_le_const_ae'' :=
  norm_setIntegral_le_of_norm_le_const_ae'' -- deprecated on 2024-04-17

theorem norm_setIntegral_le_of_norm_le_const {C : â„} (hs : Î¼ s < âˆ) (hC : âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ C)
    (hfm : AEStronglyMeasurable f (Î¼.restrict s)) : â€–âˆ« x in s, f x âˆ‚Î¼â€– â‰¤ C * (Î¼ s).toReal :=
  norm_setIntegral_le_of_norm_le_const_ae' hs (eventually_of_forall hC) hfm
#align measure_theory.norm_set_integral_le_of_norm_le_const MeasureTheory.norm_setIntegral_le_of_norm_le_const

@[deprecated]
alias norm_set_integral_le_of_norm_le_const :=
  norm_setIntegral_le_of_norm_le_const -- deprecated on 2024-04-17

theorem norm_setIntegral_le_of_norm_le_const' {C : â„} (hs : Î¼ s < âˆ) (hsm : MeasurableSet s)
    (hC : âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ C) : â€–âˆ« x in s, f x âˆ‚Î¼â€– â‰¤ C * (Î¼ s).toReal :=
  norm_setIntegral_le_of_norm_le_const_ae'' hs hsm <| eventually_of_forall hC
#align measure_theory.norm_set_integral_le_of_norm_le_const' MeasureTheory.norm_setIntegral_le_of_norm_le_const'

@[deprecated]
alias norm_set_integral_le_of_norm_le_const' :=
  norm_setIntegral_le_of_norm_le_const' -- deprecated on 2024-04-17

theorem setIntegral_eq_zero_iff_of_nonneg_ae {f : X â†’ â„} (hf : 0 â‰¤áµ[Î¼.restrict s] f)
    (hfi : IntegrableOn f s Î¼) : âˆ« x in s, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼.restrict s] 0 :=
  integral_eq_zero_iff_of_nonneg_ae hf hfi
#align measure_theory.set_integral_eq_zero_iff_of_nonneg_ae MeasureTheory.setIntegral_eq_zero_iff_of_nonneg_ae

@[deprecated]
alias set_integral_eq_zero_iff_of_nonneg_ae :=
  setIntegral_eq_zero_iff_of_nonneg_ae -- deprecated on 2024-04-17

theorem setIntegral_pos_iff_support_of_nonneg_ae {f : X â†’ â„} (hf : 0 â‰¤áµ[Î¼.restrict s] f)
    (hfi : IntegrableOn f s Î¼) : (0 < âˆ« x in s, f x âˆ‚Î¼) â†” 0 < Î¼ (support f âˆ© s) := by
  rw [integral_pos_iff_support_of_nonneg_ae hf hfi, Measure.restrict_applyâ‚€]
  rw [support_eq_preimage]
  exact hfi.aestronglyMeasurable.aemeasurable.nullMeasurable (measurableSet_singleton 0).compl
#align measure_theory.set_integral_pos_iff_support_of_nonneg_ae MeasureTheory.setIntegral_pos_iff_support_of_nonneg_ae

@[deprecated]
alias set_integral_pos_iff_support_of_nonneg_ae :=
  setIntegral_pos_iff_support_of_nonneg_ae -- deprecated on 2024-04-17

theorem setIntegral_gt_gt {R : â„} {f : X â†’ â„} (hR : 0 â‰¤ R) (hfm : Measurable f)
    (hfint : IntegrableOn f {x | â†‘R < f x} Î¼) (hÎ¼ : Î¼ {x | â†‘R < f x} â‰  0) :
    (Î¼ {x | â†‘R < f x}).toReal * R < âˆ« x in {x | â†‘R < f x}, f x âˆ‚Î¼ := by
  have : IntegrableOn (fun _ => R) {x | â†‘R < f x} Î¼ := by
    refine âŸ¨aestronglyMeasurable_const, lt_of_le_of_lt ?_ hfint.2âŸ©
    refine
      set_lintegral_mono (Measurable.nnnorm ?_).coe_nnreal_ennreal hfm.nnnorm.coe_nnreal_ennreal
        fun x hx => ?_
    Â· exact measurable_const
    Â· simp only [ENNReal.coe_le_coe, Real.nnnorm_of_nonneg hR,
        Real.nnnorm_of_nonneg (hR.trans <| le_of_lt hx), Subtype.mk_le_mk]
      exact le_of_lt hx
  rw [â† sub_pos, â† smul_eq_mul, â† setIntegral_const, â† integral_sub hfint this,
    setIntegral_pos_iff_support_of_nonneg_ae]
  Â· rw [â† zero_lt_iff] at hÎ¼
    rwa [Set.inter_eq_self_of_subset_right]
    exact fun x hx => Ne.symm (ne_of_lt <| sub_pos.2 hx)
  Â· rw [Pi.zero_def, EventuallyLE, ae_restrict_iff]
    Â· exact eventually_of_forall fun x hx => sub_nonneg.2 <| le_of_lt hx
    Â· exact measurableSet_le measurable_zero (hfm.sub measurable_const)
  Â· exact Integrable.sub hfint this
#align measure_theory.set_integral_gt_gt MeasureTheory.setIntegral_gt_gt

@[deprecated]
alias set_integral_gt_gt :=
  setIntegral_gt_gt -- deprecated on 2024-04-17

theorem setIntegral_trim {X} {m m0 : MeasurableSpace X} {Î¼ : Measure X} (hm : m â‰¤ m0) {f : X â†’ E}
    (hf_meas : StronglyMeasurable[m] f) {s : Set X} (hs : MeasurableSet[m] s) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼.trim hm := by
  rwa [integral_trim hm hf_meas, restrict_trim hm Î¼]
#align measure_theory.set_integral_trim MeasureTheory.setIntegral_trim

@[deprecated]
alias set_integral_trim :=
  setIntegral_trim -- deprecated on 2024-04-17

/-! ### Lemmas about adding and removing interval boundaries

The primed lemmas take explicit arguments about the endpoint having zero measure, while the
unprimed ones use `[NoAtoms Î¼]`.
-/


section PartialOrder

variable [PartialOrder X] {x y : X}

theorem integral_Icc_eq_integral_Ioc' (hx : Î¼ {x} = 0) :
    âˆ« t in Icc x y, f t âˆ‚Î¼ = âˆ« t in Ioc x y, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Ioc_ae_eq_Icc' hx).symm
#align measure_theory.integral_Icc_eq_integral_Ioc' MeasureTheory.integral_Icc_eq_integral_Ioc'

theorem integral_Icc_eq_integral_Ico' (hy : Î¼ {y} = 0) :
    âˆ« t in Icc x y, f t âˆ‚Î¼ = âˆ« t in Ico x y, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Ico_ae_eq_Icc' hy).symm
#align measure_theory.integral_Icc_eq_integral_Ico' MeasureTheory.integral_Icc_eq_integral_Ico'

theorem integral_Ioc_eq_integral_Ioo' (hy : Î¼ {y} = 0) :
    âˆ« t in Ioc x y, f t âˆ‚Î¼ = âˆ« t in Ioo x y, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Ioo_ae_eq_Ioc' hy).symm
#align measure_theory.integral_Ioc_eq_integral_Ioo' MeasureTheory.integral_Ioc_eq_integral_Ioo'

theorem integral_Ico_eq_integral_Ioo' (hx : Î¼ {x} = 0) :
    âˆ« t in Ico x y, f t âˆ‚Î¼ = âˆ« t in Ioo x y, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Ioo_ae_eq_Ico' hx).symm
#align measure_theory.integral_Ico_eq_integral_Ioo' MeasureTheory.integral_Ico_eq_integral_Ioo'

theorem integral_Icc_eq_integral_Ioo' (hx : Î¼ {x} = 0) (hy : Î¼ {y} = 0) :
    âˆ« t in Icc x y, f t âˆ‚Î¼ = âˆ« t in Ioo x y, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Ioo_ae_eq_Icc' hx hy).symm
#align measure_theory.integral_Icc_eq_integral_Ioo' MeasureTheory.integral_Icc_eq_integral_Ioo'

theorem integral_Iic_eq_integral_Iio' (hx : Î¼ {x} = 0) :
    âˆ« t in Iic x, f t âˆ‚Î¼ = âˆ« t in Iio x, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Iio_ae_eq_Iic' hx).symm
#align measure_theory.integral_Iic_eq_integral_Iio' MeasureTheory.integral_Iic_eq_integral_Iio'

theorem integral_Ici_eq_integral_Ioi' (hx : Î¼ {x} = 0) :
    âˆ« t in Ici x, f t âˆ‚Î¼ = âˆ« t in Ioi x, f t âˆ‚Î¼ :=
  setIntegral_congr_set_ae (Ioi_ae_eq_Ici' hx).symm
#align measure_theory.integral_Ici_eq_integral_Ioi' MeasureTheory.integral_Ici_eq_integral_Ioi'

variable [NoAtoms Î¼]

theorem integral_Icc_eq_integral_Ioc : âˆ« t in Icc x y, f t âˆ‚Î¼ = âˆ« t in Ioc x y, f t âˆ‚Î¼ :=
  integral_Icc_eq_integral_Ioc' <| measure_singleton x
#align measure_theory.integral_Icc_eq_integral_Ioc MeasureTheory.integral_Icc_eq_integral_Ioc

theorem integral_Icc_eq_integral_Ico : âˆ« t in Icc x y, f t âˆ‚Î¼ = âˆ« t in Ico x y, f t âˆ‚Î¼ :=
  integral_Icc_eq_integral_Ico' <| measure_singleton y
#align measure_theory.integral_Icc_eq_integral_Ico MeasureTheory.integral_Icc_eq_integral_Ico

theorem integral_Ioc_eq_integral_Ioo : âˆ« t in Ioc x y, f t âˆ‚Î¼ = âˆ« t in Ioo x y, f t âˆ‚Î¼ :=
  integral_Ioc_eq_integral_Ioo' <| measure_singleton y
#align measure_theory.integral_Ioc_eq_integral_Ioo MeasureTheory.integral_Ioc_eq_integral_Ioo

theorem integral_Ico_eq_integral_Ioo : âˆ« t in Ico x y, f t âˆ‚Î¼ = âˆ« t in Ioo x y, f t âˆ‚Î¼ :=
  integral_Ico_eq_integral_Ioo' <| measure_singleton x
#align measure_theory.integral_Ico_eq_integral_Ioo MeasureTheory.integral_Ico_eq_integral_Ioo

theorem integral_Icc_eq_integral_Ioo : âˆ« t in Icc x y, f t âˆ‚Î¼ = âˆ« t in Ioo x y, f t âˆ‚Î¼ := by
  rw [integral_Icc_eq_integral_Ico, integral_Ico_eq_integral_Ioo]
#align measure_theory.integral_Icc_eq_integral_Ioo MeasureTheory.integral_Icc_eq_integral_Ioo

theorem integral_Iic_eq_integral_Iio : âˆ« t in Iic x, f t âˆ‚Î¼ = âˆ« t in Iio x, f t âˆ‚Î¼ :=
  integral_Iic_eq_integral_Iio' <| measure_singleton x
#align measure_theory.integral_Iic_eq_integral_Iio MeasureTheory.integral_Iic_eq_integral_Iio

theorem integral_Ici_eq_integral_Ioi : âˆ« t in Ici x, f t âˆ‚Î¼ = âˆ« t in Ioi x, f t âˆ‚Î¼ :=
  integral_Ici_eq_integral_Ioi' <| measure_singleton x
#align measure_theory.integral_Ici_eq_integral_Ioi MeasureTheory.integral_Ici_eq_integral_Ioi

end PartialOrder

end NormedAddCommGroup

section Mono

variable {Î¼ : Measure X} {f g : X â†’ â„} {s t : Set X} (hf : IntegrableOn f s Î¼)
  (hg : IntegrableOn g s Î¼)

theorem setIntegral_mono_ae_restrict (h : f â‰¤áµ[Î¼.restrict s] g) :
    âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in s, g x âˆ‚Î¼ :=
  integral_mono_ae hf hg h
#align measure_theory.set_integral_mono_ae_restrict MeasureTheory.setIntegral_mono_ae_restrict

@[deprecated]
alias set_integral_mono_ae_restrict :=
  setIntegral_mono_ae_restrict -- deprecated on 2024-04-17

theorem setIntegral_mono_ae (h : f â‰¤áµ[Î¼] g) : âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in s, g x âˆ‚Î¼ :=
  setIntegral_mono_ae_restrict hf hg (ae_restrict_of_ae h)
#align measure_theory.set_integral_mono_ae MeasureTheory.setIntegral_mono_ae

@[deprecated]
alias set_integral_mono_ae :=
  setIntegral_mono_ae -- deprecated on 2024-04-17

theorem setIntegral_mono_on (hs : MeasurableSet s) (h : âˆ€ x âˆˆ s, f x â‰¤ g x) :
    âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in s, g x âˆ‚Î¼ :=
  setIntegral_mono_ae_restrict hf hg
    (by simp [hs, EventuallyLE, eventually_inf_principal, ae_of_all _ h])
#align measure_theory.set_integral_mono_on MeasureTheory.setIntegral_mono_on

@[deprecated]
alias set_integral_mono_on :=
  setIntegral_mono_on -- deprecated on 2024-04-17

theorem setIntegral_mono_on_ae (hs : MeasurableSet s) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x â‰¤ g x) :
    âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in s, g x âˆ‚Î¼ := by
  refine setIntegral_mono_ae_restrict hf hg ?_; rwa [EventuallyLE, ae_restrict_iff' hs]
#align measure_theory.set_integral_mono_on_ae MeasureTheory.setIntegral_mono_on_ae

@[deprecated]
alias set_integral_mono_on_ae :=
  setIntegral_mono_on_ae -- deprecated on 2024-04-17

theorem setIntegral_mono (h : f â‰¤ g) : âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in s, g x âˆ‚Î¼ :=
  integral_mono hf hg h
#align measure_theory.set_integral_mono MeasureTheory.setIntegral_mono

@[deprecated]
alias set_integral_mono :=
  setIntegral_mono -- deprecated on 2024-04-17

theorem setIntegral_mono_set (hfi : IntegrableOn f t Î¼) (hf : 0 â‰¤áµ[Î¼.restrict t] f)
    (hst : s â‰¤áµ[Î¼] t) : âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in t, f x âˆ‚Î¼ :=
  integral_mono_measure (Measure.restrict_mono_ae hst) hf hfi
#align measure_theory.set_integral_mono_set MeasureTheory.setIntegral_mono_set

@[deprecated]
alias set_integral_mono_set :=
  setIntegral_mono_set -- deprecated on 2024-04-17

theorem setIntegral_le_integral (hfi : Integrable f Î¼) (hf : 0 â‰¤áµ[Î¼] f) :
    âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x, f x âˆ‚Î¼ :=
  integral_mono_measure (Measure.restrict_le_self) hf hfi

@[deprecated]
alias set_integral_le_integral :=
  setIntegral_le_integral -- deprecated on 2024-04-17

theorem setIntegral_ge_of_const_le {c : â„} (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ)
    (hf : âˆ€ x âˆˆ s, c â‰¤ f x) (hfint : IntegrableOn (fun x : X => f x) s Î¼) :
    c * (Î¼ s).toReal â‰¤ âˆ« x in s, f x âˆ‚Î¼ := by
  rw [mul_comm, â† smul_eq_mul, â† setIntegral_const c]
  exact setIntegral_mono_on (integrableOn_const.2 (Or.inr hÎ¼s.lt_top)) hfint hs hf
#align measure_theory.set_integral_ge_of_const_le MeasureTheory.setIntegral_ge_of_const_le

@[deprecated]
alias set_integral_ge_of_const_le :=
  setIntegral_ge_of_const_le -- deprecated on 2024-04-17

end Mono

section Nonneg

variable {Î¼ : Measure X} {f : X â†’ â„} {s : Set X}

theorem setIntegral_nonneg_of_ae_restrict (hf : 0 â‰¤áµ[Î¼.restrict s] f) : 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼ :=
  integral_nonneg_of_ae hf
#align measure_theory.set_integral_nonneg_of_ae_restrict MeasureTheory.setIntegral_nonneg_of_ae_restrict

@[deprecated]
alias set_integral_nonneg_of_ae_restrict :=
  setIntegral_nonneg_of_ae_restrict -- deprecated on 2024-04-17

theorem setIntegral_nonneg_of_ae (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ âˆ« x in s, f x âˆ‚Î¼ :=
  setIntegral_nonneg_of_ae_restrict (ae_restrict_of_ae hf)
#align measure_theory.set_integral_nonneg_of_ae MeasureTheory.setIntegral_nonneg_of_ae

@[deprecated]
alias set_integral_nonneg_of_ae :=
  setIntegral_nonneg_of_ae -- deprecated on 2024-04-17

theorem setIntegral_nonneg (hs : MeasurableSet s) (hf : âˆ€ x, x âˆˆ s â†’ 0 â‰¤ f x) :
    0 â‰¤ âˆ« x in s, f x âˆ‚Î¼ :=
  setIntegral_nonneg_of_ae_restrict ((ae_restrict_iff' hs).mpr (ae_of_all Î¼ hf))
#align measure_theory.set_integral_nonneg MeasureTheory.setIntegral_nonneg

@[deprecated]
alias set_integral_nonneg :=
  setIntegral_nonneg -- deprecated on 2024-04-17

theorem setIntegral_nonneg_ae (hs : MeasurableSet s) (hf : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ 0 â‰¤ f x) :
    0 â‰¤ âˆ« x in s, f x âˆ‚Î¼ :=
  setIntegral_nonneg_of_ae_restrict <| by rwa [EventuallyLE, ae_restrict_iff' hs]
#align measure_theory.set_integral_nonneg_ae MeasureTheory.setIntegral_nonneg_ae

@[deprecated]
alias set_integral_nonneg_ae :=
  setIntegral_nonneg_ae -- deprecated on 2024-04-17

theorem setIntegral_le_nonneg {s : Set X} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hfi : Integrable f Î¼) : âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in {y | 0 â‰¤ f y}, f x âˆ‚Î¼ := by
  rw [â† integral_indicator hs, â†
    integral_indicator (stronglyMeasurable_const.measurableSet_le hf)]
  exact
    integral_mono (hfi.indicator hs)
      (hfi.indicator (stronglyMeasurable_const.measurableSet_le hf))
      (indicator_le_indicator_nonneg s f)
#align measure_theory.set_integral_le_nonneg MeasureTheory.setIntegral_le_nonneg

@[deprecated]
alias set_integral_le_nonneg :=
  setIntegral_le_nonneg -- deprecated on 2024-04-17

theorem setIntegral_nonpos_of_ae_restrict (hf : f â‰¤áµ[Î¼.restrict s] 0) : âˆ« x in s, f x âˆ‚Î¼ â‰¤ 0 :=
  integral_nonpos_of_ae hf
#align measure_theory.set_integral_nonpos_of_ae_restrict MeasureTheory.setIntegral_nonpos_of_ae_restrict

@[deprecated]
alias set_integral_nonpos_of_ae_restrict :=
  setIntegral_nonpos_of_ae_restrict -- deprecated on 2024-04-17

theorem setIntegral_nonpos_of_ae (hf : f â‰¤áµ[Î¼] 0) : âˆ« x in s, f x âˆ‚Î¼ â‰¤ 0 :=
  setIntegral_nonpos_of_ae_restrict (ae_restrict_of_ae hf)
#align measure_theory.set_integral_nonpos_of_ae MeasureTheory.setIntegral_nonpos_of_ae

@[deprecated]
alias set_integral_nonpos_of_ae :=
  setIntegral_nonpos_of_ae -- deprecated on 2024-04-17

theorem setIntegral_nonpos_ae (hs : MeasurableSet s) (hf : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x â‰¤ 0) :
    âˆ« x in s, f x âˆ‚Î¼ â‰¤ 0 :=
  setIntegral_nonpos_of_ae_restrict <| by rwa [EventuallyLE, ae_restrict_iff' hs]
#align measure_theory.set_integral_nonpos_ae MeasureTheory.setIntegral_nonpos_ae

@[deprecated]
alias set_integral_nonpos_ae :=
  setIntegral_nonpos_ae -- deprecated on 2024-04-17

theorem setIntegral_nonpos (hs : MeasurableSet s) (hf : âˆ€ x, x âˆˆ s â†’ f x â‰¤ 0) :
    âˆ« x in s, f x âˆ‚Î¼ â‰¤ 0 :=
  setIntegral_nonpos_ae hs <| ae_of_all Î¼ hf
#align measure_theory.set_integral_nonpos MeasureTheory.setIntegral_nonpos

@[deprecated]
alias set_integral_nonpos :=
  setIntegral_nonpos -- deprecated on 2024-04-17

theorem setIntegral_nonpos_le {s : Set X} (hs : MeasurableSet s) (hf : StronglyMeasurable f)
    (hfi : Integrable f Î¼) : âˆ« x in {y | f y â‰¤ 0}, f x âˆ‚Î¼ â‰¤ âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† integral_indicator hs, â†
    integral_indicator (hf.measurableSet_le stronglyMeasurable_const)]
  exact
    integral_mono (hfi.indicator (hf.measurableSet_le stronglyMeasurable_const))
      (hfi.indicator hs) (indicator_nonpos_le_indicator s f)
#align measure_theory.set_integral_nonpos_le MeasureTheory.setIntegral_nonpos_le

@[deprecated]
alias set_integral_nonpos_le :=
  setIntegral_nonpos_le -- deprecated on 2024-04-17

lemma Integrable.measure_le_integral {f : X â†’ â„} (f_int : Integrable f Î¼) (f_nonneg : 0 â‰¤áµ[Î¼] f)
    {s : Set X} (hs : âˆ€ x âˆˆ s, 1 â‰¤ f x) :
    Î¼ s â‰¤ ENNReal.ofReal (âˆ« x, f x âˆ‚Î¼) := by
  rw [ofReal_integral_eq_lintegral_ofReal f_int f_nonneg]
  apply meas_le_lintegralâ‚€
  Â· exact ENNReal.continuous_ofReal.measurable.comp_aemeasurable f_int.1.aemeasurable
  Â· intro x hx
    simpa using ENNReal.ofReal_le_ofReal (hs x hx)

lemma integral_le_measure {f : X â†’ â„} {s : Set X}
    (hs : âˆ€ x âˆˆ s, f x â‰¤ 1) (h's : âˆ€ x âˆˆ sá¶œ, f x â‰¤ 0) :
    ENNReal.ofReal (âˆ« x, f x âˆ‚Î¼) â‰¤ Î¼ s := by
  by_cases H : Integrable f Î¼; swap
  Â· simp [integral_undef H]
  let g x := max (f x) 0
  have g_int : Integrable g Î¼ := H.pos_part
  have : ENNReal.ofReal (âˆ« x, f x âˆ‚Î¼) â‰¤ ENNReal.ofReal (âˆ« x, g x âˆ‚Î¼) := by
    apply ENNReal.ofReal_le_ofReal
    exact integral_mono H g_int (fun x â†¦ le_max_left _ _)
  apply this.trans
  rw [ofReal_integral_eq_lintegral_ofReal g_int (eventually_of_forall (fun x â†¦ le_max_right _ _))]
  apply lintegral_le_meas
  Â· intro x
    apply ENNReal.ofReal_le_of_le_toReal
    by_cases H : x âˆˆ s
    Â· simpa [g] using hs x H
    Â· apply le_trans _ zero_le_one
      simpa [g] using h's x H
  Â· intro x hx
    simpa [g] using h's x hx

end Nonneg

section IntegrableUnion

variable {Î¹ : Type*} [Countable Î¹] {Î¼ : Measure X} [NormedAddCommGroup E]

theorem integrableOn_iUnion_of_summable_integral_norm {f : X â†’ E} {s : Î¹ â†’ Set X}
    (hs : âˆ€ i : Î¹, MeasurableSet (s i)) (hi : âˆ€ i : Î¹, IntegrableOn f (s i) Î¼)
    (h : Summable fun i : Î¹ => âˆ« x : X in s i, â€–f xâ€– âˆ‚Î¼) : IntegrableOn f (iUnion s) Î¼ := by
  refine âŸ¨AEStronglyMeasurable.iUnion fun i => (hi i).1, (lintegral_iUnion_le _ _).trans_lt ?_âŸ©
  have B := fun i => lintegral_coe_eq_integral (fun x : X => â€–f xâ€–â‚Š) (hi i).norm
  rw [tsum_congr B]
  have S' :
    Summable fun i : Î¹ =>
      (âŸ¨âˆ« x : X in s i, â€–f xâ€–â‚Š âˆ‚Î¼, setIntegral_nonneg (hs i) fun x _ => NNReal.coe_nonneg _âŸ© :
        NNReal) := by
    rw [â† NNReal.summable_coe]; exact h
  have S'' := ENNReal.tsum_coe_eq S'.hasSum
  simp_rw [ENNReal.coe_nnreal_eq, NNReal.coe_mk, coe_nnnorm] at S''
  convert ENNReal.ofReal_lt_top
#align measure_theory.integrable_on_Union_of_summable_integral_norm MeasureTheory.integrableOn_iUnion_of_summable_integral_norm

variable [TopologicalSpace X] [BorelSpace X] [MetrizableSpace X] [IsLocallyFiniteMeasure Î¼]

/-- If `s` is a countable family of compact sets, `f` is a continuous function, and the sequence
`â€–f.restrict (s i)â€– * Î¼ (s i)` is summable, then `f` is integrable on the union of the `s i`. -/
theorem integrableOn_iUnion_of_summable_norm_restrict {f : C(X, E)} {s : Î¹ â†’ Compacts X}
    (hf : Summable fun i : Î¹ => â€–f.restrict (s i)â€– * ENNReal.toReal (Î¼ <| s i)) :
    IntegrableOn f (â‹ƒ i : Î¹, s i) Î¼ := by
  refine
    integrableOn_iUnion_of_summable_integral_norm (fun i => (s i).isCompact.isClosed.measurableSet)
      (fun i => (map_continuous f).continuousOn.integrableOn_compact (s i).isCompact)
      (.of_nonneg_of_le (fun Î¹ => integral_nonneg fun x => norm_nonneg _) (fun i => ?_) hf)
  rw [â† (Real.norm_of_nonneg (integral_nonneg fun x => norm_nonneg _) : â€–_â€– = âˆ« x in s i, â€–f xâ€– âˆ‚Î¼)]
  exact
    norm_setIntegral_le_of_norm_le_const' (s i).isCompact.measure_lt_top
      (s i).isCompact.isClosed.measurableSet fun x hx =>
      (norm_norm (f x)).symm â–¸ (f.restrict (s i : Set X)).norm_coe_le_norm âŸ¨x, hxâŸ©
#align measure_theory.integrable_on_Union_of_summable_norm_restrict MeasureTheory.integrableOn_iUnion_of_summable_norm_restrict

/-- If `s` is a countable family of compact sets covering `X`, `f` is a continuous function, and
the sequence `â€–f.restrict (s i)â€– * Î¼ (s i)` is summable, then `f` is integrable. -/
theorem integrable_of_summable_norm_restrict {f : C(X, E)} {s : Î¹ â†’ Compacts X}
    (hf : Summable fun i : Î¹ => â€–f.restrict (s i)â€– * ENNReal.toReal (Î¼ <| s i))
    (hs : â‹ƒ i : Î¹, â†‘(s i) = (univ : Set X)) : Integrable f Î¼ := by
  simpa only [hs, integrableOn_univ] using integrableOn_iUnion_of_summable_norm_restrict hf
#align measure_theory.integrable_of_summable_norm_restrict MeasureTheory.integrable_of_summable_norm_restrict

end IntegrableUnion

/-! ### Continuity of the set integral

We prove that for any set `s`, the function
`fun f : X â†’â‚[Î¼] E => âˆ« x in s, f x âˆ‚Î¼` is continuous. -/


section ContinuousSetIntegral

variable [NormedAddCommGroup E]
  {ğ•œ : Type*} [NormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F] {p : â„â‰¥0âˆ} {Î¼ : Measure X}

/-- For `f : Lp E p Î¼`, we can define an element of `Lp E p (Î¼.restrict s)` by
`(Lp.memâ„’p f).restrict s).toLp f`. This map is additive. -/
theorem Lp_toLp_restrict_add (f g : Lp E p Î¼) (s : Set X) :
    ((Lp.memâ„’p (f + g)).restrict s).toLp (â‡‘(f + g)) =
      ((Lp.memâ„’p f).restrict s).toLp f + ((Lp.memâ„’p g).restrict s).toLp g := by
  ext1
  refine (ae_restrict_of_ae (Lp.coeFn_add f g)).mp ?_
  refine
    (Lp.coeFn_add (Memâ„’p.toLp f ((Lp.memâ„’p f).restrict s))
          (Memâ„’p.toLp g ((Lp.memâ„’p g).restrict s))).mp ?_
  refine (Memâ„’p.coeFn_toLp ((Lp.memâ„’p f).restrict s)).mp ?_
  refine (Memâ„’p.coeFn_toLp ((Lp.memâ„’p g).restrict s)).mp ?_
  refine (Memâ„’p.coeFn_toLp ((Lp.memâ„’p (f + g)).restrict s)).mono fun x hx1 hx2 hx3 hx4 hx5 => ?_
  rw [hx4, hx1, Pi.add_apply, hx2, hx3, hx5, Pi.add_apply]
set_option linter.uppercaseLean3 false in
#align measure_theory.Lp_to_Lp_restrict_add MeasureTheory.Lp_toLp_restrict_add

/-- For `f : Lp E p Î¼`, we can define an element of `Lp E p (Î¼.restrict s)` by
`(Lp.memâ„’p f).restrict s).toLp f`. This map commutes with scalar multiplication. -/
theorem Lp_toLp_restrict_smul (c : ğ•œ) (f : Lp F p Î¼) (s : Set X) :
    ((Lp.memâ„’p (c â€¢ f)).restrict s).toLp (â‡‘(c â€¢ f)) = c â€¢ ((Lp.memâ„’p f).restrict s).toLp f := by
  ext1
  refine (ae_restrict_of_ae (Lp.coeFn_smul c f)).mp ?_
  refine (Memâ„’p.coeFn_toLp ((Lp.memâ„’p f).restrict s)).mp ?_
  refine (Memâ„’p.coeFn_toLp ((Lp.memâ„’p (c â€¢ f)).restrict s)).mp ?_
  refine
    (Lp.coeFn_smul c (Memâ„’p.toLp f ((Lp.memâ„’p f).restrict s))).mono fun x hx1 hx2 hx3 hx4 => ?_
  simp only [hx2, hx1, hx3, hx4, Pi.smul_apply]
set_option linter.uppercaseLean3 false in
#align measure_theory.Lp_to_Lp_restrict_smul MeasureTheory.Lp_toLp_restrict_smul

/-- For `f : Lp E p Î¼`, we can define an element of `Lp E p (Î¼.restrict s)` by
`(Lp.memâ„’p f).restrict s).toLp f`. This map is non-expansive. -/
theorem norm_Lp_toLp_restrict_le (s : Set X) (f : Lp E p Î¼) :
    â€–((Lp.memâ„’p f).restrict s).toLp fâ€– â‰¤ â€–fâ€– := by
  rw [Lp.norm_def, Lp.norm_def, ENNReal.toReal_le_toReal (Lp.snorm_ne_top _) (Lp.snorm_ne_top _)]
  apply (le_of_eq _).trans (snorm_mono_measure _ (Measure.restrict_le_self (s := s)))
  exact snorm_congr_ae (Memâ„’p.coeFn_toLp _)
set_option linter.uppercaseLean3 false in
#align measure_theory.norm_Lp_to_Lp_restrict_le MeasureTheory.norm_Lp_toLp_restrict_le

variable (X F ğ•œ) in
/-- Continuous linear map sending a function of `Lp F p Î¼` to the same function in
`Lp F p (Î¼.restrict s)`. -/
def LpToLpRestrictCLM (Î¼ : Measure X) (p : â„â‰¥0âˆ) [hp : Fact (1 â‰¤ p)] (s : Set X) :
    Lp F p Î¼ â†’L[ğ•œ] Lp F p (Î¼.restrict s) :=
  @LinearMap.mkContinuous ğ•œ ğ•œ (Lp F p Î¼) (Lp F p (Î¼.restrict s)) _ _ _ _ _ _ (RingHom.id ğ•œ)
    âŸ¨âŸ¨fun f => Memâ„’p.toLp f ((Lp.memâ„’p f).restrict s), fun f g => Lp_toLp_restrict_add f g sâŸ©,
      fun c f => Lp_toLp_restrict_smul c f sâŸ©
    1 (by intro f; rw [one_mul]; exact norm_Lp_toLp_restrict_le s f)
set_option linter.uppercaseLean3 false in
#align measure_theory.Lp_to_Lp_restrict_clm MeasureTheory.LpToLpRestrictCLM

variable (ğ•œ) in
theorem LpToLpRestrictCLM_coeFn [Fact (1 â‰¤ p)] (s : Set X) (f : Lp F p Î¼) :
    LpToLpRestrictCLM X F ğ•œ Î¼ p s f =áµ[Î¼.restrict s] f :=
  Memâ„’p.coeFn_toLp ((Lp.memâ„’p f).restrict s)
set_option linter.uppercaseLean3 false in
#align measure_theory.Lp_to_Lp_restrict_clm_coe_fn MeasureTheory.LpToLpRestrictCLM_coeFn

@[continuity]
theorem continuous_setIntegral [NormedSpace â„ E] (s : Set X) :
    Continuous fun f : X â†’â‚[Î¼] E => âˆ« x in s, f x âˆ‚Î¼ := by
  haveI : Fact ((1 : â„â‰¥0âˆ) â‰¤ 1) := âŸ¨le_rflâŸ©
  have h_comp :
    (fun f : X â†’â‚[Î¼] E => âˆ« x in s, f x âˆ‚Î¼) =
      integral (Î¼.restrict s) âˆ˜ fun f => LpToLpRestrictCLM X E â„ Î¼ 1 s f := by
    ext1 f
    rw [Function.comp_apply, integral_congr_ae (LpToLpRestrictCLM_coeFn â„ s f)]
  rw [h_comp]
  exact continuous_integral.comp (LpToLpRestrictCLM X E â„ Î¼ 1 s).continuous
#align measure_theory.continuous_set_integral MeasureTheory.continuous_setIntegral

@[deprecated]
alias continuous_set_integral :=
  continuous_setIntegral -- deprecated on 2024-04-17

end ContinuousSetIntegral

end MeasureTheory

section OpenPos

open Measure

variable [TopologicalSpace X] [OpensMeasurableSpace X] {Î¼ : Measure X} [IsOpenPosMeasure Î¼]

theorem Continuous.integral_pos_of_hasCompactSupport_nonneg_nonzero [IsFiniteMeasureOnCompacts Î¼]
    {f : X â†’ â„} {x : X} (f_cont : Continuous f) (f_comp : HasCompactSupport f) (f_nonneg : 0 â‰¤ f)
    (f_x : f x â‰  0) : 0 < âˆ« x, f x âˆ‚Î¼ :=
  integral_pos_of_integrable_nonneg_nonzero f_cont (f_cont.integrable_of_hasCompactSupport f_comp)
    f_nonneg f_x

end OpenPos

/-! Fundamental theorem of calculus for set integrals -/
section FTC

open MeasureTheory Asymptotics Metric

variable {Î¹ : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]

/-- Fundamental theorem of calculus for set integrals:
if `Î¼` is a measure that is finite at a filter `l` and
`f` is a measurable function that has a finite limit `b` at `l âŠ“ ae Î¼`, then
`âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ b + o(Î¼ (s i))` at a filter `li` provided that
`s i` tends to `l.smallSets` along `li`.
Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `(Î¼ (s i)).toReal` in the actual statement.

Often there is a good formula for `(Î¼ (s i)).toReal`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => (Î¼ (s i)).toReal) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).toReal` is used in the output. -/
theorem Filter.Tendsto.integral_sub_linear_isLittleO_ae
    {Î¼ : Measure X} {l : Filter X} [l.IsMeasurablyGenerated] {f : X â†’ E} {b : E}
    (h : Tendsto f (l âŠ“ ae Î¼) (ğ“ b)) (hfm : StronglyMeasurableAtFilter f l Î¼)
    (hÎ¼ : Î¼.FiniteAtFilter l) {s : Î¹ â†’ Set X} {li : Filter Î¹} (hs : Tendsto s li l.smallSets)
    (m : Î¹ â†’ â„ := fun i => (Î¼ (s i)).toReal)
    (hsÎ¼ : (fun i => (Î¼ (s i)).toReal) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ b) =o[li] m := by
  suffices
      (fun s => (âˆ« x in s, f x âˆ‚Î¼) - (Î¼ s).toReal â€¢ b) =o[l.smallSets] fun s => (Î¼ s).toReal from
    (this.comp_tendsto hs).congr'
      (hsÎ¼.mono fun a ha => by dsimp only [Function.comp_apply] at ha âŠ¢; rw [ha]) hsÎ¼
  refine isLittleO_iff.2 fun Îµ Îµâ‚€ => ?_
  have : âˆ€á¶  s in l.smallSets, âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x âˆˆ closedBall b Îµ :=
    eventually_smallSets_eventually.2 (h.eventually <| closedBall_mem_nhds _ Îµâ‚€)
  filter_upwards [hÎ¼.eventually, (hÎ¼.integrableAtFilter_of_tendsto_ae hfm h).eventually,
    hfm.eventually, this]
  simp only [mem_closedBall, dist_eq_norm]
  intro s hÎ¼s h_integrable hfm h_norm
  rw [â† setIntegral_const, â† integral_sub h_integrable (integrableOn_const.2 <| Or.inr hÎ¼s),
    Real.norm_eq_abs, abs_of_nonneg ENNReal.toReal_nonneg]
  exact norm_setIntegral_le_of_norm_le_const_ae' hÎ¼s h_norm (hfm.sub aestronglyMeasurable_const)
#align filter.tendsto.integral_sub_linear_is_o_ae Filter.Tendsto.integral_sub_linear_isLittleO_ae

/-- Fundamental theorem of calculus for set integrals, `nhdsWithin` version: if `Î¼` is a locally
finite measure and `f` is an almost everywhere measurable function that is continuous at a point `a`
within a measurable set `t`, then `âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at a filter `li`
provided that `s i` tends to `(ğ“[t] a).smallSets` along `li`.  Since `Î¼ (s i)` is an `â„â‰¥0âˆ`
number, we use `(Î¼ (s i)).toReal` in the actual statement.

Often there is a good formula for `(Î¼ (s i)).toReal`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => (Î¼ (s i)).toReal) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).toReal` is used in the output. -/
theorem ContinuousWithinAt.integral_sub_linear_isLittleO_ae [TopologicalSpace X]
    [OpensMeasurableSpace X] {Î¼ : Measure X}
    [IsLocallyFiniteMeasure Î¼] {x : X} {t : Set X} {f : X â†’ E} (hx : ContinuousWithinAt f t x)
    (ht : MeasurableSet t) (hfm : StronglyMeasurableAtFilter f (ğ“[t] x) Î¼) {s : Î¹ â†’ Set X}
    {li : Filter Î¹} (hs : Tendsto s li (ğ“[t] x).smallSets) (m : Î¹ â†’ â„ := fun i => (Î¼ (s i)).toReal)
    (hsÎ¼ : (fun i => (Î¼ (s i)).toReal) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ f x) =o[li] m :=
  haveI : (ğ“[t] x).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hx.mono_left inf_le_left).integral_sub_linear_isLittleO_ae hfm (Î¼.finiteAt_nhdsWithin x t) hs m
    hsÎ¼
#align continuous_within_at.integral_sub_linear_is_o_ae ContinuousWithinAt.integral_sub_linear_isLittleO_ae

/-- Fundamental theorem of calculus for set integrals, `nhds` version: if `Î¼` is a locally finite
measure and `f` is an almost everywhere measurable function that is continuous at a point `a`, then
`âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at `li` provided that `s` tends to
`(ğ“ a).smallSets` along `li`. Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `(Î¼ (s i)).toReal` in
the actual statement.

Often there is a good formula for `(Î¼ (s i)).toReal`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => (Î¼ (s i)).toReal) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).toReal` is used in the output. -/
theorem ContinuousAt.integral_sub_linear_isLittleO_ae [TopologicalSpace X] [OpensMeasurableSpace X]
    {Î¼ : Measure X} [IsLocallyFiniteMeasure Î¼] {x : X}
    {f : X â†’ E} (hx : ContinuousAt f x) (hfm : StronglyMeasurableAtFilter f (ğ“ x) Î¼) {s : Î¹ â†’ Set X}
    {li : Filter Î¹} (hs : Tendsto s li (ğ“ x).smallSets) (m : Î¹ â†’ â„ := fun i => (Î¼ (s i)).toReal)
    (hsÎ¼ : (fun i => (Î¼ (s i)).toReal) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ f x) =o[li] m :=
  (hx.mono_left inf_le_left).integral_sub_linear_isLittleO_ae hfm (Î¼.finiteAt_nhds x) hs m hsÎ¼
#align continuous_at.integral_sub_linear_is_o_ae ContinuousAt.integral_sub_linear_isLittleO_ae

/-- Fundamental theorem of calculus for set integrals, `nhdsWithin` version: if `Î¼` is a locally
finite measure, `f` is continuous on a measurable set `t`, and `a âˆˆ t`, then `âˆ« x in (s i), f x âˆ‚Î¼ =
Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at `li` provided that `s i` tends to `(ğ“[t] a).smallSets` along `li`.
Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `(Î¼ (s i)).toReal` in the actual statement.

Often there is a good formula for `(Î¼ (s i)).toReal`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => (Î¼ (s i)).toReal) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).toReal` is used in the output. -/
theorem ContinuousOn.integral_sub_linear_isLittleO_ae [TopologicalSpace X] [OpensMeasurableSpace X]
    [SecondCountableTopologyEither X E] {Î¼ : Measure X}
    [IsLocallyFiniteMeasure Î¼] {x : X} {t : Set X} {f : X â†’ E} (hft : ContinuousOn f t) (hx : x âˆˆ t)
    (ht : MeasurableSet t) {s : Î¹ â†’ Set X} {li : Filter Î¹} (hs : Tendsto s li (ğ“[t] x).smallSets)
    (m : Î¹ â†’ â„ := fun i => (Î¼ (s i)).toReal)
    (hsÎ¼ : (fun i => (Î¼ (s i)).toReal) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ f x) =o[li] m :=
  (hft x hx).integral_sub_linear_isLittleO_ae ht
    âŸ¨t, self_mem_nhdsWithin, hft.aestronglyMeasurable htâŸ© hs m hsÎ¼
#align continuous_on.integral_sub_linear_is_o_ae ContinuousOn.integral_sub_linear_isLittleO_ae

end FTC

section

/-! ### Continuous linear maps composed with integration

The goal of this section is to prove that integration commutes with continuous linear maps.
This holds for simple functions. The general result follows from the continuity of all involved
operations on the space `LÂ¹`. Note that composition by a continuous linear map on `LÂ¹` is not just
the composition, as we are dealing with classes of functions, but it has already been defined
as `ContinuousLinearMap.compLp`. We take advantage of this construction here.
-/

open scoped ComplexConjugate

variable {Î¼ : Measure X} {ğ•œ : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {p : ENNReal}

namespace ContinuousLinearMap

variable [NormedSpace â„ F]

theorem integral_compLp (L : E â†’L[ğ•œ] F) (Ï† : Lp E p Î¼) :
    âˆ« x, (L.compLp Ï†) x âˆ‚Î¼ = âˆ« x, L (Ï† x) âˆ‚Î¼ :=
  integral_congr_ae <| coeFn_compLp _ _
set_option linter.uppercaseLean3 false in
#align continuous_linear_map.integral_comp_Lp ContinuousLinearMap.integral_compLp

theorem setIntegral_compLp (L : E â†’L[ğ•œ] F) (Ï† : Lp E p Î¼) {s : Set X} (hs : MeasurableSet s) :
    âˆ« x in s, (L.compLp Ï†) x âˆ‚Î¼ = âˆ« x in s, L (Ï† x) âˆ‚Î¼ :=
  setIntegral_congr_ae hs ((L.coeFn_compLp Ï†).mono fun _x hx _ => hx)
set_option linter.uppercaseLean3 false in
#align continuous_linear_map.set_integral_comp_Lp ContinuousLinearMap.setIntegral_compLp

@[deprecated]
alias set_integral_compLp :=
  setIntegral_compLp -- deprecated on 2024-04-17

theorem continuous_integral_comp_L1 (L : E â†’L[ğ•œ] F) :
    Continuous fun Ï† : X â†’â‚[Î¼] E => âˆ« x : X, L (Ï† x) âˆ‚Î¼ := by
  rw [â† funext L.integral_compLp]; exact continuous_integral.comp (L.compLpL 1 Î¼).continuous
set_option linter.uppercaseLean3 false in
#align continuous_linear_map.continuous_integral_comp_L1 ContinuousLinearMap.continuous_integral_comp_L1

variable [CompleteSpace F] [NormedSpace â„ E]

theorem integral_comp_comm [CompleteSpace E] (L : E â†’L[ğ•œ] F) {Ï† : X â†’ E} (Ï†_int : Integrable Ï† Î¼) :
    âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := by
  apply Ï†_int.induction (P := fun Ï† => âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼))
  Â· intro e s s_meas _
    rw [integral_indicator_const e s_meas, â† @smul_one_smul E â„ ğ•œ _ _ _ _ _ (Î¼ s).toReal e,
      ContinuousLinearMap.map_smul, @smul_one_smul F â„ ğ•œ _ _ _ _ _ (Î¼ s).toReal (L e), â†
      integral_indicator_const (L e) s_meas]
    congr 1 with a
    rw [â† Function.comp_def L, Set.indicator_comp_of_zero L.map_zero, Function.comp_apply]
  Â· intro f g _ f_int g_int hf hg
    simp [L.map_add, integral_add (Î¼ := Î¼) f_int g_int,
      integral_add (Î¼ := Î¼) (L.integrable_comp f_int) (L.integrable_comp g_int), hf, hg]
  Â· exact isClosed_eq L.continuous_integral_comp_L1 (L.continuous.comp continuous_integral)
  Â· intro f g hfg _ hf
    convert hf using 1 <;> clear hf
    Â· exact integral_congr_ae (hfg.fun_comp L).symm
    Â· rw [integral_congr_ae hfg.symm]
#align continuous_linear_map.integral_comp_comm ContinuousLinearMap.integral_comp_comm

theorem integral_apply {H : Type*} [NormedAddCommGroup H] [NormedSpace ğ•œ H] {Ï† : X â†’ H â†’L[ğ•œ] E}
    (Ï†_int : Integrable Ï† Î¼) (v : H) : (âˆ« x, Ï† x âˆ‚Î¼) v = âˆ« x, Ï† x v âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E
  Â· exact ((ContinuousLinearMap.apply ğ•œ E v).integral_comp_comm Ï†_int).symm
  Â· rcases subsingleton_or_nontrivial H with hH|hH
    Â· simp [Subsingleton.eq_zero v]
    Â· have : Â¬(CompleteSpace (H â†’L[ğ•œ] E)) := by
        rwa [SeparatingDual.completeSpace_continuousLinearMap_iff]
      simp [integral, hE, this]
#align continuous_linear_map.integral_apply ContinuousLinearMap.integral_apply

theorem _root_.ContinuousMultilinearMap.integral_apply {Î¹ : Type*} [Fintype Î¹] {M : Î¹ â†’ Type*}
    [âˆ€ i, NormedAddCommGroup (M i)] [âˆ€ i, NormedSpace ğ•œ (M i)]
    {Ï† : X â†’ ContinuousMultilinearMap ğ•œ M E} (Ï†_int : Integrable Ï† Î¼) (m : âˆ€ i, M i) :
    (âˆ« x, Ï† x âˆ‚Î¼) m = âˆ« x, Ï† x m âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E
  Â· exact ((ContinuousMultilinearMap.apply ğ•œ M E m).integral_comp_comm Ï†_int).symm
  Â· by_cases hm : âˆ€ i, m i â‰  0
    Â· have : Â¬ CompleteSpace (ContinuousMultilinearMap ğ•œ M E) := by
        rwa [SeparatingDual.completeSpace_continuousMultilinearMap_iff _ _ hm]
      simp [integral, hE, this]
    Â· push_neg at hm
      rcases hm with âŸ¨i, hiâŸ©
      simp [ContinuousMultilinearMap.map_coord_zero _ i hi]

variable [CompleteSpace E]

theorem integral_comp_comm' (L : E â†’L[ğ•œ] F) {K} (hL : AntilipschitzWith K L) (Ï† : X â†’ E) :
    âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := by
  by_cases h : Integrable Ï† Î¼
  Â· exact integral_comp_comm L h
  have : Â¬Integrable (fun x => L (Ï† x)) Î¼ := by
    rwa [â† Function.comp_def,
      LipschitzWith.integrable_comp_iff_of_antilipschitz L.lipschitz hL L.map_zero]
  simp [integral_undef, h, this]
#align continuous_linear_map.integral_comp_comm' ContinuousLinearMap.integral_comp_comm'

theorem integral_comp_L1_comm (L : E â†’L[ğ•œ] F) (Ï† : X â†’â‚[Î¼] E) :
    âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) :=
  L.integral_comp_comm (L1.integrable_coeFn Ï†)
set_option linter.uppercaseLean3 false in
#align continuous_linear_map.integral_comp_L1_comm ContinuousLinearMap.integral_comp_L1_comm

end ContinuousLinearMap

namespace LinearIsometry

variable [CompleteSpace F] [NormedSpace â„ F] [CompleteSpace E] [NormedSpace â„ E]

theorem integral_comp_comm (L : E â†’â‚—áµ¢[ğ•œ] F) (Ï† : X â†’ E) : âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) :=
  L.toContinuousLinearMap.integral_comp_comm' L.antilipschitz _
#align linear_isometry.integral_comp_comm LinearIsometry.integral_comp_comm

end LinearIsometry

namespace ContinuousLinearEquiv

variable [NormedSpace â„ F] [NormedSpace â„ E]

theorem integral_comp_comm (L : E â‰ƒL[ğ•œ] F) (Ï† : X â†’ E) : âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := by
  have : CompleteSpace E â†” CompleteSpace F :=
    completeSpace_congr (e := L.toEquiv) L.uniformEmbedding
  obtain âŸ¨_, _âŸ©|âŸ¨_, _âŸ© := iff_iff_and_or_not_and_not.mp this
  Â· exact L.toContinuousLinearMap.integral_comp_comm' L.antilipschitz _
  Â· simp [integral, *]
#align continuous_linear_equiv.integral_comp_comm ContinuousLinearEquiv.integral_comp_comm

end ContinuousLinearEquiv

@[norm_cast]
theorem integral_ofReal {f : X â†’ â„} : âˆ« x, (f x : ğ•œ) âˆ‚Î¼ = â†‘(âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.ofRealLI ğ•œ _).integral_comp_comm f
#align integral_of_real integral_ofReal

theorem integral_re {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    âˆ« x, RCLike.re (f x) âˆ‚Î¼ = RCLike.re (âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.reCLM ğ•œ _).integral_comp_comm hf
#align integral_re integral_re

theorem integral_im {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    âˆ« x, RCLike.im (f x) âˆ‚Î¼ = RCLike.im (âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.imCLM ğ•œ _).integral_comp_comm hf
#align integral_im integral_im

theorem integral_conj {f : X â†’ ğ•œ} : âˆ« x, conj (f x) âˆ‚Î¼ = conj (âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.conjLIE ğ•œ _).toLinearIsometry.integral_comp_comm f
#align integral_conj integral_conj

theorem integral_coe_re_add_coe_im {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    âˆ« x, (re (f x) : ğ•œ) âˆ‚Î¼ + (âˆ« x, (im (f x) : ğ•œ) âˆ‚Î¼) * RCLike.I = âˆ« x, f x âˆ‚Î¼ := by
  rw [mul_comm, â† smul_eq_mul, â† integral_smul, â† integral_add]
  Â· congr
    ext1 x
    rw [smul_eq_mul, mul_comm, RCLike.re_add_im]
  Â· exact hf.re.ofReal
  Â· exact hf.im.ofReal.smul (ğ•œ := ğ•œ) (Î² := ğ•œ) RCLike.I
#align integral_coe_re_add_coe_im integral_coe_re_add_coe_im

theorem integral_re_add_im {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    ((âˆ« x, RCLike.re (f x) âˆ‚Î¼ : â„) : ğ•œ) + (âˆ« x, RCLike.im (f x) âˆ‚Î¼ : â„) * RCLike.I =
      âˆ« x, f x âˆ‚Î¼ := by
  rw [â† integral_ofReal, â† integral_ofReal, integral_coe_re_add_coe_im hf]
#align integral_re_add_im integral_re_add_im

theorem setIntegral_re_add_im {f : X â†’ ğ•œ} {i : Set X} (hf : IntegrableOn f i Î¼) :
    ((âˆ« x in i, RCLike.re (f x) âˆ‚Î¼ : â„) : ğ•œ) + (âˆ« x in i, RCLike.im (f x) âˆ‚Î¼ : â„) * RCLike.I =
      âˆ« x in i, f x âˆ‚Î¼ :=
  integral_re_add_im hf
#align set_integral_re_add_im setIntegral_re_add_im

@[deprecated]
alias set_integral_re_add_im :=
  setIntegral_re_add_im -- deprecated on 2024-04-17

variable [NormedSpace â„ E] [NormedSpace â„ F]

lemma swap_integral (f : X â†’ E Ã— F) : (âˆ« x, f x âˆ‚Î¼).swap = âˆ« x, (f x).swap âˆ‚Î¼ :=
  .symm <| (ContinuousLinearEquiv.prodComm â„ E F).integral_comp_comm f

theorem fst_integral [CompleteSpace F] {f : X â†’ E Ã— F} (hf : Integrable f Î¼) :
    (âˆ« x, f x âˆ‚Î¼).1 = âˆ« x, (f x).1 âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E
  Â· exact ((ContinuousLinearMap.fst â„ E F).integral_comp_comm hf).symm
  Â· have : Â¬(CompleteSpace (E Ã— F)) := fun h â†¦ hE <| .fst_of_prod (Î² := F)
    simp [integral, *]
#align fst_integral fst_integral

theorem snd_integral [CompleteSpace E] {f : X â†’ E Ã— F} (hf : Integrable f Î¼) :
    (âˆ« x, f x âˆ‚Î¼).2 = âˆ« x, (f x).2 âˆ‚Î¼ := by
  rw [â† Prod.fst_swap, swap_integral]
  exact fst_integral <| hf.snd.prod_mk hf.fst
#align snd_integral snd_integral

theorem integral_pair [CompleteSpace E] [CompleteSpace F] {f : X â†’ E} {g : X â†’ F}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« x, (f x, g x) âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼, âˆ« x, g x âˆ‚Î¼) :=
  have := hf.prod_mk hg
  Prod.ext (fst_integral this) (snd_integral this)
#align integral_pair integral_pair

theorem integral_smul_const {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [CompleteSpace E]
    (f : X â†’ ğ•œ) (c : E) :
    âˆ« x, f x â€¢ c âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼) â€¢ c := by
  by_cases hf : Integrable f Î¼
  Â· exact ((1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight c).integral_comp_comm hf
  Â· by_cases hc : c = 0
    Â· simp [hc, integral_zero, smul_zero]
    rw [integral_undef hf, integral_undef, zero_smul]
    rw [integrable_smul_const hc]
    simp_rw [hf, not_false_eq_true]
#align integral_smul_const integral_smul_const

theorem integral_withDensity_eq_integral_smul {f : X â†’ â„â‰¥0} (f_meas : Measurable f) (g : X â†’ E) :
    âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, f x â€¢ g x âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E; swap; Â· simp [integral, hE]
  by_cases hg : Integrable g (Î¼.withDensity fun x => f x); swap
  Â· rw [integral_undef hg, integral_undef]
    rwa [â† integrable_withDensity_iff_integrable_smul f_meas]
  refine Integrable.induction
    (P := fun g => âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, f x â€¢ g x âˆ‚Î¼) ?_ ?_ ?_ ?_ hg
  Â· intro c s s_meas hs
    rw [integral_indicator s_meas]
    simp_rw [â† indicator_smul_apply, integral_indicator s_meas]
    simp only [s_meas, integral_const, Measure.restrict_apply', univ_inter, withDensity_apply]
    rw [lintegral_coe_eq_integral, ENNReal.toReal_ofReal, â† integral_smul_const]
    Â· rfl
    Â· exact integral_nonneg fun x => NNReal.coe_nonneg _
    Â· refine âŸ¨f_meas.coe_nnreal_real.aemeasurable.aestronglyMeasurable, ?_âŸ©
      rw [withDensity_apply _ s_meas] at hs
      rw [HasFiniteIntegral]
      convert hs with x
      simp only [NNReal.nnnorm_eq]
  Â· intro u u' _ u_int u'_int h h'
    change
      (âˆ« x : X, u x + u' x âˆ‚Î¼.withDensity fun x : X => â†‘(f x)) = âˆ« x : X, f x â€¢ (u x + u' x) âˆ‚Î¼
    simp_rw [smul_add]
    rw [integral_add u_int u'_int, h, h', integral_add]
    Â· exact (integrable_withDensity_iff_integrable_smul f_meas).1 u_int
    Â· exact (integrable_withDensity_iff_integrable_smul f_meas).1 u'_int
  Â· have C1 :
      Continuous fun u : Lp E 1 (Î¼.withDensity fun x => f x) =>
        âˆ« x, u x âˆ‚Î¼.withDensity fun x => f x :=
      continuous_integral
    have C2 : Continuous fun u : Lp E 1 (Î¼.withDensity fun x => f x) => âˆ« x, f x â€¢ u x âˆ‚Î¼ := by
      have : Continuous ((fun u : Lp E 1 Î¼ => âˆ« x, u x âˆ‚Î¼) âˆ˜ withDensitySMulLI (E := E) Î¼ f_meas) :=
        continuous_integral.comp (withDensitySMulLI (E := E) Î¼ f_meas).continuous
      convert this with u
      simp only [Function.comp_apply, withDensitySMulLI_apply]
      exact integral_congr_ae (memâ„’1_smul_of_L1_withDensity f_meas u).coeFn_toLp.symm
    exact isClosed_eq C1 C2
  Â· intro u v huv _ hu
    rw [â† integral_congr_ae huv, hu]
    apply integral_congr_ae
    filter_upwards [(ae_withDensity_iff f_meas.coe_nnreal_ennreal).1 huv] with x hx
    rcases eq_or_ne (f x) 0 with (h'x | h'x)
    Â· simp only [h'x, zero_smul]
    Â· rw [hx _]
      simpa only [Ne, ENNReal.coe_eq_zero] using h'x
#align integral_with_density_eq_integral_smul integral_withDensity_eq_integral_smul

theorem integral_withDensity_eq_integral_smulâ‚€ {f : X â†’ â„â‰¥0} (hf : AEMeasurable f Î¼) (g : X â†’ E) :
    âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, f x â€¢ g x âˆ‚Î¼ := by
  let f' := hf.mk _
  calc
    âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, g x âˆ‚Î¼.withDensity fun x => f' x := by
      congr 1
      apply withDensity_congr_ae
      filter_upwards [hf.ae_eq_mk] with x hx
      rw [hx]
    _ = âˆ« x, f' x â€¢ g x âˆ‚Î¼ := integral_withDensity_eq_integral_smul hf.measurable_mk _
    _ = âˆ« x, f x â€¢ g x âˆ‚Î¼ := by
      apply integral_congr_ae
      filter_upwards [hf.ae_eq_mk] with x hx
      rw [hx]
#align integral_with_density_eq_integral_smulâ‚€ integral_withDensity_eq_integral_smulâ‚€

theorem setIntegral_withDensity_eq_setIntegral_smul {f : X â†’ â„â‰¥0} (f_meas : Measurable f)
    (g : X â†’ E) {s : Set X} (hs : MeasurableSet s) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, f x â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity hs, integral_withDensity_eq_integral_smul f_meas]
#align set_integral_with_density_eq_set_integral_smul setIntegral_withDensity_eq_setIntegral_smul

@[deprecated]
alias set_integral_withDensity_eq_set_integral_smul :=
  setIntegral_withDensity_eq_setIntegral_smul -- deprecated on 2024-04-17

theorem setIntegral_withDensity_eq_setIntegral_smulâ‚€ {f : X â†’ â„â‰¥0} {s : Set X}
    (hf : AEMeasurable f (Î¼.restrict s)) (g : X â†’ E) (hs : MeasurableSet s) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, f x â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity hs, integral_withDensity_eq_integral_smulâ‚€ hf]
#align set_integral_with_density_eq_set_integral_smulâ‚€ setIntegral_withDensity_eq_setIntegral_smulâ‚€

@[deprecated]
alias set_integral_withDensity_eq_set_integral_smulâ‚€ :=
  setIntegral_withDensity_eq_setIntegral_smulâ‚€ -- deprecated on 2024-04-17

theorem setIntegral_withDensity_eq_setIntegral_smulâ‚€' [SFinite Î¼] {f : X â†’ â„â‰¥0} (s : Set X)
    (hf : AEMeasurable f (Î¼.restrict s)) (g : X â†’ E)  :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, f x â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity' s, integral_withDensity_eq_integral_smulâ‚€ hf]

@[deprecated]
alias set_integral_withDensity_eq_set_integral_smulâ‚€' :=
  setIntegral_withDensity_eq_setIntegral_smulâ‚€' -- deprecated on 2024-04-17

end

section thickenedIndicator

variable [PseudoEMetricSpace X]

theorem measure_le_lintegral_thickenedIndicatorAux (Î¼ : Measure X) {E : Set X}
    (E_mble : MeasurableSet E) (Î´ : â„) : Î¼ E â‰¤ âˆ«â» x, (thickenedIndicatorAux Î´ E x : â„â‰¥0âˆ) âˆ‚Î¼ := by
  convert_to lintegral Î¼ (E.indicator fun _ => (1 : â„â‰¥0âˆ)) â‰¤ lintegral Î¼ (thickenedIndicatorAux Î´ E)
  Â· rw [lintegral_indicator _ E_mble]
    simp only [lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
  Â· apply lintegral_mono
    apply indicator_le_thickenedIndicatorAux
#align measure_le_lintegral_thickened_indicator_aux measure_le_lintegral_thickenedIndicatorAux

theorem measure_le_lintegral_thickenedIndicator (Î¼ : Measure X) {E : Set X}
    (E_mble : MeasurableSet E) {Î´ : â„} (Î´_pos : 0 < Î´) :
    Î¼ E â‰¤ âˆ«â» x, (thickenedIndicator Î´_pos E x : â„â‰¥0âˆ) âˆ‚Î¼ := by
  convert measure_le_lintegral_thickenedIndicatorAux Î¼ E_mble Î´
  dsimp
  simp only [thickenedIndicatorAux_lt_top.ne, ENNReal.coe_toNNReal, Ne, not_false_iff]
#align measure_le_lintegral_thickened_indicator measure_le_lintegral_thickenedIndicator

end thickenedIndicator

section BilinearMap

namespace MeasureTheory

variable {f : X â†’ â„} {m m0 : MeasurableSpace X} {Î¼ : Measure X}

theorem Integrable.simpleFunc_mul (g : SimpleFunc X â„) (hf : Integrable f Î¼) :
    Integrable (â‡‘g * f) Î¼ := by
  refine
    SimpleFunc.induction (fun c s hs => ?_)
      (fun gâ‚ gâ‚‚ _ h_intâ‚ h_intâ‚‚ =>
        (h_intâ‚.add h_intâ‚‚).congr (by rw [SimpleFunc.coe_add, add_mul]))
      g
  simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
    SimpleFunc.coe_zero, Set.piecewise_eq_indicator]
  have : Set.indicator s (Function.const X c) * f = s.indicator (c â€¢ f) := by
    ext1 x
    by_cases hx : x âˆˆ s
    Â· simp only [hx, Pi.mul_apply, Set.indicator_of_mem, Pi.smul_apply, Algebra.id.smul_eq_mul,
        â† Function.const_def]
    Â· simp only [hx, Pi.mul_apply, Set.indicator_of_not_mem, not_false_iff, zero_mul]
  rw [this, integrable_indicator_iff hs]
  exact (hf.smul c).integrableOn
#align measure_theory.integrable.simple_func_mul MeasureTheory.Integrable.simpleFunc_mul

theorem Integrable.simpleFunc_mul' (hm : m â‰¤ m0) (g : @SimpleFunc X m â„) (hf : Integrable f Î¼) :
    Integrable (â‡‘g * f) Î¼ := by
  rw [â† SimpleFunc.coe_toLargerSpace_eq hm g]; exact hf.simpleFunc_mul (g.toLargerSpace hm)
#align measure_theory.integrable.simple_func_mul' MeasureTheory.Integrable.simpleFunc_mul'

end MeasureTheory

end BilinearMap

section ParametricIntegral

variable {G ğ•œ : Type*} [TopologicalSpace X]
  [TopologicalSpace Y] [MeasurableSpace Y] [OpensMeasurableSpace Y] {Î¼ : Measure Y}
  [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

open Metric ContinuousLinearMap

/-- The parametric integral over a continuous function on a compact set is continuous,
  under mild assumptions on the topologies involved. -/
theorem continuous_parametric_integral_of_continuous
    [FirstCountableTopology X] [LocallyCompactSpace X]
    [OpensMeasurableSpace Y] [SecondCountableTopologyEither Y E] [IsLocallyFiniteMeasure Î¼]
    {f : X â†’ Y â†’ E} (hf : Continuous f.uncurry) {s : Set Y} (hs : IsCompact s) :
    Continuous (âˆ« y in s, f Â· y âˆ‚Î¼) := by
  rw [continuous_iff_continuousAt]
  intro xâ‚€
  rcases exists_compact_mem_nhds xâ‚€ with âŸ¨U, U_cpct, U_nhdsâŸ©
  rcases (U_cpct.prod hs).bddAbove_image hf.norm.continuousOn with âŸ¨M, hMâŸ©
  apply continuousAt_of_dominated
  Â· filter_upwards with x using Continuous.aestronglyMeasurable (by fun_prop)
  Â· filter_upwards [U_nhds] with x x_in
    rw [ae_restrict_iff]
    Â· filter_upwards with t t_in using hM (mem_image_of_mem _ <| mk_mem_prod x_in t_in)
    Â· exact (isClosed_le (by fun_prop) (by fun_prop)).measurableSet
  Â· exact integrableOn_const.mpr (Or.inr hs.measure_lt_top)
  Â· filter_upwards using (by fun_prop)

/-- Consider a parameterized integral `x â†¦ âˆ« y, L (g y) (f x y)` where `L` is bilinear,
`g` is locally integrable and `f` is continuous and uniformly compactly supported. Then the
integral depends continuously on `x`. -/
lemma continuousOn_integral_bilinear_of_locally_integrable_of_compact_support
    [NormedSpace ğ•œ E] (L : F â†’L[ğ•œ] G â†’L[ğ•œ] E)
    {f : X â†’ Y â†’ G} {s : Set X} {k : Set Y} {g : Y â†’ F}
    (hk : IsCompact k) (hf : ContinuousOn f.uncurry (s Ã—Ë¢ univ))
    (hfs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ f p x = 0) (hg : IntegrableOn g k Î¼) :
    ContinuousOn (fun x â†¦ âˆ« y, L (g y) (f x y) âˆ‚Î¼) s := by
  have A : âˆ€ p âˆˆ s, Continuous (f p) := fun p hp â†¦ by
    refine hf.comp_continuous (continuous_const.prod_mk continuous_id') fun y => ?_
    simpa only [prod_mk_mem_set_prod_eq, mem_univ, and_true] using hp
  intro q hq
  apply Metric.continuousWithinAt_iff'.2 (fun Îµ Îµpos â†¦ ?_)
  obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ (Î´ : â„), 0 < Î´ âˆ§ âˆ« x in k, â€–Lâ€– * â€–g xâ€– * Î´ âˆ‚Î¼ < Îµ := by
    simpa [integral_mul_right] using exists_pos_mul_lt Îµpos _
  obtain âŸ¨v, v_mem, hvâŸ© : âˆƒ v âˆˆ ğ“[s] q, âˆ€ p âˆˆ v, âˆ€ x âˆˆ k, dist (f p x) (f q x) < Î´ :=
    hk.mem_uniformity_of_prod
      (hf.mono (Set.prod_mono_right (subset_univ k))) hq (dist_mem_uniformity Î´pos)
  simp_rw [dist_eq_norm] at hv âŠ¢
  have I : âˆ€ p âˆˆ s, IntegrableOn (fun y â†¦ L (g y) (f p y)) k Î¼ := by
    intro p hp
    obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€ y, â€–f p yâ€– â‰¤ C := by
      have : ContinuousOn (f p) k := by
        have : ContinuousOn (fun y â†¦ (p, y)) k := by fun_prop
        exact hf.comp this (by simp [MapsTo, hp])
      rcases IsCompact.exists_bound_of_continuousOn hk this with âŸ¨C, hCâŸ©
      refine âŸ¨max C 0, fun y â†¦ ?_âŸ©
      by_cases hx : y âˆˆ k
      Â· exact (hC y hx).trans (le_max_left _ _)
      Â· simp [hfs p y hp hx]
    have : IntegrableOn (fun y â†¦ â€–Lâ€– * â€–g yâ€– * C) k Î¼ :=
      (hg.norm.const_mul _).mul_const _
    apply Integrable.mono' this ?_ ?_
    Â· borelize G
      apply L.aestronglyMeasurable_compâ‚‚ hg.aestronglyMeasurable
      apply StronglyMeasurable.aestronglyMeasurable
      apply Continuous.stronglyMeasurable_of_support_subset_isCompact (A p hp) hk
      apply support_subset_iff'.2 (fun y hy â†¦ hfs p y hp hy)
    Â· apply eventually_of_forall (fun y â†¦ (le_opNormâ‚‚ L (g y) (f p y)).trans ?_)
      gcongr
      apply hC
  filter_upwards [v_mem, self_mem_nhdsWithin] with p hp h'p
  calc
  â€–âˆ« x, L (g x) (f p x) âˆ‚Î¼ - âˆ« x, L (g x) (f q x) âˆ‚Î¼â€–
    = â€–âˆ« x in k, L (g x) (f p x) âˆ‚Î¼ - âˆ« x in k, L (g x) (f q x) âˆ‚Î¼â€– := by
      congr 2
      Â· refine (setIntegral_eq_integral_of_forall_compl_eq_zero (fun y hy â†¦ ?_)).symm
        simp [hfs p y h'p hy]
      Â· refine (setIntegral_eq_integral_of_forall_compl_eq_zero (fun y hy â†¦ ?_)).symm
        simp [hfs q y hq hy]
  _ = â€–âˆ« x in k, L (g x) (f p x) - L (g x) (f q x) âˆ‚Î¼â€– := by rw [integral_sub (I p h'p) (I q hq)]
  _ â‰¤ âˆ« x in k, â€–L (g x) (f p x) - L (g x) (f q x)â€– âˆ‚Î¼ := norm_integral_le_integral_norm _
  _ â‰¤ âˆ« x in k, â€–Lâ€– * â€–g xâ€– * Î´ âˆ‚Î¼ := by
      apply integral_mono_of_nonneg (eventually_of_forall (fun y â†¦ by positivity))
      Â· exact (hg.norm.const_mul _).mul_const _
      Â· filter_upwards with y
        by_cases hy : y âˆˆ k
        Â· dsimp only
          specialize hv p hp y hy
          calc
          â€–L (g y) (f p y) - L (g y) (f q y)â€–
            = â€–L (g y) (f p y - f q y)â€– := by simp only [map_sub]
          _ â‰¤ â€–Lâ€– * â€–g yâ€– * â€–f p y - f q yâ€– := le_opNormâ‚‚ _ _ _
          _ â‰¤ â€–Lâ€– * â€–g yâ€– * Î´ := by gcongr
        Â· simp only [hfs p y h'p hy, hfs q y hq hy, sub_self, norm_zero, mul_zero]
          positivity
  _ < Îµ := hÎ´

/-- Consider a parameterized integral `x â†¦ âˆ« y, f x y` where `f` is continuous and uniformly
compactly supported. Then the integral depends continuously on `x`. -/
lemma continuousOn_integral_of_compact_support
    {f : X â†’ Y â†’ E} {s : Set X} {k : Set Y} [IsFiniteMeasureOnCompacts Î¼]
    (hk : IsCompact k) (hf : ContinuousOn f.uncurry (s Ã—Ë¢ univ))
    (hfs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ f p x = 0) :
    ContinuousOn (fun x â†¦ âˆ« y, f x y âˆ‚Î¼) s := by
  simpa using continuousOn_integral_bilinear_of_locally_integrable_of_compact_support (lsmul â„ â„)
    hk hf hfs (integrableOn_const.2 (Or.inr hk.measure_lt_top)) (Î¼ := Î¼) (g := fun _ â†¦ 1)

end ParametricIntegral
