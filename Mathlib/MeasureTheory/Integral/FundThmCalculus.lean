/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Patrick Massot, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Measurable
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.NormedSpace.Dual
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Integral.VitaliCaratheodory

#align_import measure_theory.integral.fund_thm_calculus from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Fundamental Theorem of Calculus

We prove various versions of the
[fundamental theorem of calculus](https://en.wikipedia.org/wiki/Fundamental_theorem_of_calculus)
for interval integrals in `â„`.

Recall that its first version states that the function `(u, v) â†¦ âˆ« x in u..v, f x` has derivative
`(Î´u, Î´v) â†¦ Î´v â€¢ f b - Î´u â€¢ f a` at `(a, b)` provided that `f` is continuous at `a` and `b`,
and its second version states that, if `f` has an integrable derivative on `[a, b]`, then
`âˆ« x in a..b, f' x = f b - f a`.

## Main statements

### FTC-1 for Lebesgue measure

We prove several versions of FTC-1, all in the `intervalIntegral` namespace. Many of them follow
the naming scheme `integral_has(Strict?)(F?)Deriv(Within?)At(_of_tendsto_ae?)(_right|_left?)`.
They formulate FTC in terms of `Has(Strict?)(F?)Deriv(Within?)At`.
Let us explain the meaning of each part of the name:

* `Strict` means that the theorem is about strict differentiability, see `HasStrictDerivAt` and
  `HasStrictFDerivAt`;
* `F` means that the theorem is about differentiability in both endpoints; incompatible with
  `_right|_left`;
* `Within` means that the theorem is about one-sided derivatives, see below for details;
* `_of_tendsto_ae` means that instead of continuity the theorem assumes that `f` has a finite limit
  almost surely as `x` tends to `a` and/or `b`;
* `_right` or `_left` mean that the theorem is about differentiability in the right (resp., left)
  endpoint.

We also reformulate these theorems in terms of `(f?)deriv(Within?)`. These theorems are named
`(f?)deriv(Within?)_integral(_of_tendsto_ae?)(_right|_left?)` with the same meaning of parts of the
name.

### One-sided derivatives

Theorem `intervalIntegral.integral_hasFDerivWithinAt_of_tendsto_ae` states that
`(u, v) â†¦ âˆ« x in u..v, f x` has a derivative `(Î´u, Î´v) â†¦ Î´v â€¢ cb - Î´u â€¢ ca` within the set `s Ã— t`
at `(a, b)` provided that `f` tends to `ca` (resp., `cb`) almost surely at `la` (resp., `lb`), where
possible values of `s`, `t`, and corresponding filters `la`, `lb` are given in the following table.

| `s`     | `la`     | `t`     | `lb`     |
| ------- | ----     | ---     | ----     |
| `Iic a` | `ğ“[â‰¤] a` | `Iic b` | `ğ“[â‰¤] b` |
| `Ici a` | `ğ“[>] a` | `Ici b` | `ğ“[>] b` |
| `{a}`   | `âŠ¥`      | `{b}`   | `âŠ¥`      |
| `univ`  | `ğ“ a`    | `univ`  | `ğ“ b`    |

We use a typeclass `intervalIntegral.FTCFilter` to make Lean automatically find `la`/`lb` based on
`s`/`t`. This way we can formulate one theorem instead of `16` (or `8` if we leave only non-trivial
ones not covered by `integral_hasDerivWithinAt_of_tendsto_ae_(left|right)` and
`integral_hasFDerivAt_of_tendsto_ae`). Similarly, `integral_hasDerivWithinAt_of_tendsto_ae_right`
works for both one-sided derivatives using the same typeclass to find an appropriate filter.

### FTC for a locally finite measure

Before proving FTC for the Lebesgue measure, we prove a few statements that can be seen as FTC for
any measure. The most general of them,
`measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae`, states the following.
Let `(la, la')` be an `intervalIntegral.FTCFilter` pair of filters around `a` (i.e.,
`intervalIntegral.FTCFilter a la la'`) and let `(lb, lb')` be an `intervalIntegral.FTCFilter` pair
of filters around `b`. If `f` has finite limits `ca` and `cb` almost surely at `la'` and `lb'`,
respectively, then
$$
  \int_{va}^{vb} f âˆ‚Î¼ - \int_{ua}^{ub} f âˆ‚Î¼ =
  \int_{ub}^{vb} cb âˆ‚Î¼ - \int_{ua}^{va} ca âˆ‚Î¼ + o(â€–âˆ«_{ua}^{va} 1 âˆ‚Î¼â€– + â€–âˆ«_{ub}^{vb} (1:â„) âˆ‚Î¼â€–)
$$
as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.

### FTC-2 and corollaries

We use FTC-1 to prove several versions of FTC-2 for the Lebesgue measure, using a similar naming
scheme as for the versions of FTC-1. They include:
* `intervalIntegral.integral_eq_sub_of_hasDeriv_right_of_le` - most general version, for functions
  with a right derivative
* `intervalIntegral.integral_eq_sub_of_hasDerivAt` - version for functions with a derivative on
  an open set
* `intervalIntegral.integral_deriv_eq_sub'` - version that is easiest to use when computing the
  integral of a specific function

We then derive additional integration techniques from FTC-2:
* `intervalIntegral.integral_mul_deriv_eq_deriv_mul` - integration by parts
* `intervalIntegral.integral_comp_mul_deriv''` - integration by substitution

Many applications of these theorems can be found in the file
`Mathlib/Analysis/SpecialFunctions/Integrals.lean`.

Note that the assumptions of FTC-2 are formulated in the form that `f'` is integrable. To use it in
a context with the stronger assumption that `f'` is continuous, one can use
`ContinuousOn.intervalIntegrable` or `ContinuousOn.integrableOn_Icc` or
`ContinuousOn.integrableOn_uIcc`.

### `intervalIntegral.FTCFilter` class

As explained above, many theorems in this file rely on the typeclass
`intervalIntegral.FTCFilter (a : â„) (l l' : Filter â„)` to avoid code duplication. This typeclass
combines four assumptions:

- `pure a â‰¤ l`;
- `l' â‰¤ ğ“ a`;
- `l'` has a basis of measurable sets;
- if `u n` and `v n` tend to `l`, then for any `s âˆˆ l'`, `Ioc (u n) (v n)` is eventually included
  in `s`.

This typeclass has the following â€œrealâ€ instances: `(a, pure a, âŠ¥)`, `(a, ğ“[â‰¥] a, ğ“[>] a)`,
`(a, ğ“[â‰¤] a, ğ“[â‰¤] a)`, `(a, ğ“ a, ğ“ a)`.
Furthermore, we have the following instances that are equal to the previously mentioned instances:
`(a, ğ“[{a}] a, âŠ¥)` and `(a, ğ“[univ] a, ğ“[univ] a)`.
While the difference between `Ici a` and `Ioi a` doesn't matter for theorems about Lebesgue measure,
it becomes important in the versions of FTC about any locally finite measure if this measure has an
atom at one of the endpoints.

### Combining one-sided and two-sided derivatives

There are some `intervalIntegral.FTCFilter` instances where the fact that it is one-sided or
two-sided depends on the point, namely `(x, ğ“[Set.Icc a b] x, ğ“[Set.Icc a b] x)` (resp.
`(x, ğ“[Set.uIcc a b] x, ğ“[Set.uIcc a b] x)`, with `x âˆˆ Icc a b` (resp. `x âˆˆ uIcc a b`). This results
in a two-sided derivatives for `x âˆˆ Set.Ioo a b` and one-sided derivatives for `x âˆˆ {a, b}`. Other
instances could be added when needed (in that case, one also needs to add instances for
`Filter.IsMeasurablyGenerated` and `Filter.TendstoIxxClass`).

## Tags

integral, fundamental theorem of calculus, FTC-1, FTC-2, change of variables in integrals
-/

set_option autoImplicit true

noncomputable section

open scoped Classical
open MeasureTheory Set Filter Function

open scoped Classical Topology Filter ENNReal Interval NNReal

variable {Î¹ ğ•œ E F A : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

namespace intervalIntegral

section FTC1

/-!
### Fundamental theorem of calculus, part 1, for any measure

In this section we prove a few lemmas that can be seen as versions of FTC-1 for interval integrals
w.r.t. any measure. Many theorems are formulated for one or two pairs of filters related by
`intervalIntegral.FTCFilter a l l'`. This typeclass has exactly four â€œrealâ€ instances:
`(a, pure a, âŠ¥)`, `(a, ğ“[â‰¥] a, ğ“[>] a)`, `(a, ğ“[â‰¤] a, ğ“[â‰¤] a)`, `(a, ğ“ a, ğ“ a)`, and two instances
that are equal to the first and last â€œrealâ€ instances: `(a, ğ“[{a}] a, âŠ¥)` and
`(a, ğ“[univ] a, ğ“[univ] a)`.  We use this approach to avoid repeating arguments in many very similar
cases.  Lean can automatically find both `a` and `l'` based on `l`.

The most general theorem `measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae` can be
seen as a generalization of lemma `integral_hasStrictFDerivAt` below which states strict
differentiability of `âˆ« x in u..v, f x` in `(u, v)` at `(a, b)` for a measurable function `f` that
is integrable on `a..b` and is continuous at `a` and `b`. The lemma is generalized in three
directions: first, `measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae` deals with any
locally finite measure `Î¼`; second, it works for one-sided limits/derivatives; third, it assumes
only that `f` has finite limits almost surely at `a` and `b`.

Namely, let `f` be a measurable function integrable on `a..b`. Let `(la, la')` be a pair of
`intervalIntegral.FTCFilter`s around `a`; let `(lb, lb')` be a pair of `intervalIntegral.FTCFilter`s
around `b`. Suppose that `f` has finite limits `ca` and `cb` at `la' âŠ“ ae Î¼` and `lb' âŠ“ ae Î¼`,
respectively.  Then
`âˆ« x in va..vb, f x âˆ‚Î¼ - âˆ« x in ua..ub, f x âˆ‚Î¼ = âˆ« x in ub..vb, cb âˆ‚Î¼ - âˆ« x in ua..va, ca âˆ‚Î¼ +
  o(â€–âˆ« x in ua..va, (1:â„) âˆ‚Î¼â€– + â€–âˆ« x in ub..vb, (1:â„) âˆ‚Î¼â€–)`
as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.

This theorem is formulated with integral of constants instead of measures in the right hand sides
for two reasons: first, this way we avoid `min`/`max` in the statements; second, often it is
possible to write better `simp` lemmas for these integrals, see `integral_const` and
`integral_const_of_cdf`.

In the next subsection we apply this theorem to prove various theorems about differentiability
of the integral w.r.t. Lebesgue measure. -/

/-- An auxiliary typeclass for the Fundamental theorem of calculus, part 1. It is used to formulate
theorems that work simultaneously for left and right one-sided derivatives of `âˆ« x in u..v, f x`. -/
class FTCFilter (a : outParam â„) (outer : Filter â„) (inner : outParam <| Filter â„) extends
    TendstoIxxClass Ioc outer inner : Prop where
  pure_le : pure a â‰¤ outer
  le_nhds : inner â‰¤ ğ“ a
  [meas_gen : IsMeasurablyGenerated inner]
set_option linter.uppercaseLean3 false in
#align interval_integral.FTC_filter intervalIntegral.FTCFilter

namespace FTCFilter

set_option linter.uppercaseLean3 false -- `FTC` in every name

instance pure (a : â„) : FTCFilter a (pure a) âŠ¥ where
  pure_le := le_rfl
  le_nhds := bot_le
#align interval_integral.FTC_filter.pure intervalIntegral.FTCFilter.pure

instance nhdsWithinSingleton (a : â„) : FTCFilter a (ğ“[{a}] a) âŠ¥ := by
  rw [nhdsWithin, principal_singleton, inf_eq_right.2 (pure_le_nhds a)]; infer_instance
#align interval_integral.FTC_filter.nhds_within_singleton intervalIntegral.FTCFilter.nhdsWithinSingleton

theorem finiteAt_inner {a : â„} (l : Filter â„) {l'} [h : FTCFilter a l l'] {Î¼ : Measure â„}
    [IsLocallyFiniteMeasure Î¼] : Î¼.FiniteAtFilter l' :=
  (Î¼.finiteAt_nhds a).filter_mono h.le_nhds
#align interval_integral.FTC_filter.finite_at_inner intervalIntegral.FTCFilter.finiteAt_inner

instance nhds (a : â„) : FTCFilter a (ğ“ a) (ğ“ a) where
  pure_le := pure_le_nhds a
  le_nhds := le_rfl
#align interval_integral.FTC_filter.nhds intervalIntegral.FTCFilter.nhds

instance nhdsUniv (a : â„) : FTCFilter a (ğ“[univ] a) (ğ“ a) := by rw [nhdsWithin_univ]; infer_instance
#align interval_integral.FTC_filter.nhds_univ intervalIntegral.FTCFilter.nhdsUniv

instance nhdsLeft (a : â„) : FTCFilter a (ğ“[â‰¤] a) (ğ“[â‰¤] a) where
  pure_le := pure_le_nhdsWithin right_mem_Iic
  le_nhds := inf_le_left
#align interval_integral.FTC_filter.nhds_left intervalIntegral.FTCFilter.nhdsLeft

instance nhdsRight (a : â„) : FTCFilter a (ğ“[â‰¥] a) (ğ“[>] a) where
  pure_le := pure_le_nhdsWithin left_mem_Ici
  le_nhds := inf_le_left
#align interval_integral.FTC_filter.nhds_right intervalIntegral.FTCFilter.nhdsRight

instance nhdsIcc {x a b : â„} [h : Fact (x âˆˆ Icc a b)] :
    FTCFilter x (ğ“[Icc a b] x) (ğ“[Icc a b] x) where
  pure_le := pure_le_nhdsWithin h.out
  le_nhds := inf_le_left
#align interval_integral.FTC_filter.nhds_Icc intervalIntegral.FTCFilter.nhdsIcc

instance nhdsUIcc {x a b : â„} [h : Fact (x âˆˆ [[a, b]])] :
    FTCFilter x (ğ“[[[a, b]]] x) (ğ“[[[a, b]]] x) :=
  .nhdsIcc (h := h)
#align interval_integral.FTC_filter.nhds_uIcc intervalIntegral.FTCFilter.nhdsUIcc

end FTCFilter

open Asymptotics

section

variable {f : â„ â†’ E} {a b : â„} {c ca cb : E} {l l' la la' lb lb' : Filter â„} {lt : Filter Î¹}
  {Î¼ : Measure â„} {u v ua va ub vb : Î¹ â†’ â„}

/-- **Fundamental theorem of calculus-1**, local version for any measure.
Let filters `l` and `l'` be related by `TendstoIxxClass Ioc`.
If `f` has a finite limit `c` at `l' âŠ“ ae Î¼`, where `Î¼` is a measure
finite at `l'`, then `âˆ« x in u..v, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)` as both
`u` and `v` tend to `l`.

See also `measure_integral_sub_linear_isLittleO_of_tendsto_ae` for a version assuming
`[intervalIntegral.FTCFilter a l l']` and `[MeasureTheory.IsLocallyFiniteMeasure Î¼]`. If `l` is one
of `ğ“[â‰¥] a`, `ğ“[â‰¤] a`, `ğ“ a`, then it's easier to apply the non-primed version.  The primed version
also works, e.g., for `l = l' = atTop`.

We use integrals of constants instead of measures because this way it is easier to formulate
a statement that works in both cases `u â‰¤ v` and `v â‰¤ u`. -/
theorem measure_integral_sub_linear_isLittleO_of_tendsto_ae' [IsMeasurablyGenerated l']
    [TendstoIxxClass Ioc l l'] (hfm : StronglyMeasurableAtFilter f l' Î¼)
    (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c)) (hl : Î¼.FiniteAtFilter l') (hu : Tendsto u lt l)
    (hv : Tendsto v lt l) :
    (fun t => (âˆ« x in u t..v t, f x âˆ‚Î¼) - âˆ« _ in u t..v t, c âˆ‚Î¼) =o[lt] fun t =>
      âˆ« _ in u t..v t, (1 : â„) âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [intervalIntegral, integral, hE]
  have A := hf.integral_sub_linear_isLittleO_ae hfm hl (hu.Ioc hv)
  have B := hf.integral_sub_linear_isLittleO_ae hfm hl (hv.Ioc hu)
  simp_rw [integral_const', sub_smul]
  refine ((A.trans_le fun t â†¦ ?_).sub (B.trans_le fun t â†¦ ?_)).congr_left fun t â†¦ ?_
  Â· cases le_total (u t) (v t) <;> simp [*]
  Â· cases le_total (u t) (v t) <;> simp [*]
  Â· simp_rw [intervalIntegral]
    abel
#align interval_integral.measure_integral_sub_linear_is_o_of_tendsto_ae' intervalIntegral.measure_integral_sub_linear_isLittleO_of_tendsto_ae'

variable [CompleteSpace E]

/-- **Fundamental theorem of calculus-1**, local version for any measure.
Let filters `l` and `l'` be related by `TendstoIxxClass Ioc`.
If `f` has a finite limit `c` at `l âŠ“ ae Î¼`, where `Î¼` is a measure
finite at `l`, then `âˆ« x in u..v, f x âˆ‚Î¼ = Î¼ (Ioc u v) â€¢ c + o(Î¼(Ioc u v))` as both
`u` and `v` tend to `l` so that `u â‰¤ v`.

See also `measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le` for a version assuming
`[intervalIntegral.FTCFilter a l l']` and `[MeasureTheory.IsLocallyFiniteMeasure Î¼]`. If `l` is one
of `ğ“[â‰¥] a`, `ğ“[â‰¤] a`, `ğ“ a`, then it's easier to apply the non-primed version.  The primed version
also works, e.g., for `l = l' = Filter.atTop`. -/
theorem measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le' [IsMeasurablyGenerated l']
    [TendstoIxxClass Ioc l l'] (hfm : StronglyMeasurableAtFilter f l' Î¼)
    (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c)) (hl : Î¼.FiniteAtFilter l') (hu : Tendsto u lt l)
    (hv : Tendsto v lt l) (huv : u â‰¤á¶ [lt] v) :
    (fun t => (âˆ« x in u t..v t, f x âˆ‚Î¼) - (Î¼ (Ioc (u t) (v t))).toReal â€¢ c) =o[lt] fun t =>
      (Î¼ <| Ioc (u t) (v t)).toReal :=
  (measure_integral_sub_linear_isLittleO_of_tendsto_ae' hfm hf hl hu hv).congr'
    (huv.mono fun x hx => by simp [integral_const', hx])
    (huv.mono fun x hx => by simp [integral_const', hx])
#align interval_integral.measure_integral_sub_linear_is_o_of_tendsto_ae_of_le' intervalIntegral.measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le'

/-- **Fundamental theorem of calculus-1**, local version for any measure.
Let filters `l` and `l'` be related by `TendstoIxxClass Ioc`.
If `f` has a finite limit `c` at `l âŠ“ ae Î¼`, where `Î¼` is a measure
finite at `l`, then `âˆ« x in u..v, f x âˆ‚Î¼ = -Î¼ (Ioc v u) â€¢ c + o(Î¼(Ioc v u))` as both
`u` and `v` tend to `l` so that `v â‰¤ u`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge` for a version assuming
`[intervalIntegral.FTCFilter a l l']` and `[MeasureTheory.IsLocallyFiniteMeasure Î¼]`. If `l` is one
of `ğ“[â‰¥] a`, `ğ“[â‰¤] a`, `ğ“ a`, then it's easier to apply the non-primed version. The primed version
also works, e.g., for `l = l' = Filter.atTop`. -/
theorem measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_ge' [IsMeasurablyGenerated l']
    [TendstoIxxClass Ioc l l'] (hfm : StronglyMeasurableAtFilter f l' Î¼)
    (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c)) (hl : Î¼.FiniteAtFilter l') (hu : Tendsto u lt l)
    (hv : Tendsto v lt l) (huv : v â‰¤á¶ [lt] u) :
    (fun t => (âˆ« x in u t..v t, f x âˆ‚Î¼) + (Î¼ (Ioc (v t) (u t))).toReal â€¢ c) =o[lt] fun t =>
      (Î¼ <| Ioc (v t) (u t)).toReal :=
  (measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le' hfm hf hl hv hu
          huv).neg_left.congr_left
    fun t => by simp [integral_symm (u t), add_comm]
#align interval_integral.measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge' intervalIntegral.measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_ge'

section

variable [IsLocallyFiniteMeasure Î¼] [FTCFilter a l l']

/-- **Fundamental theorem of calculus-1**, local version for any measure.

Let filters `l` and `l'` be related by `[intervalIntegral.FTCFilter a l l']`; let `Î¼` be a locally
finite measure.  If `f` has a finite limit `c` at `l' âŠ“ ae Î¼`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)` as both `u` and `v` tend to `l`.

See also `measure_integral_sub_linear_isLittleO_of_tendsto_ae'` for a version that also works, e.g.,
for `l = l' = Filter.atTop`.

We use integrals of constants instead of measures because this way it is easier to formulate
a statement that works in both cases `u â‰¤ v` and `v â‰¤ u`. -/
theorem measure_integral_sub_linear_isLittleO_of_tendsto_ae
    (hfm : StronglyMeasurableAtFilter f l' Î¼) (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c))
    (hu : Tendsto u lt l) (hv : Tendsto v lt l) :
    (fun t => (âˆ« x in u t..v t, f x âˆ‚Î¼) - âˆ« _ in u t..v t, c âˆ‚Î¼) =o[lt] fun t =>
      âˆ« _ in u t..v t, (1 : â„) âˆ‚Î¼ :=
  haveI := FTCFilter.meas_gen l
  measure_integral_sub_linear_isLittleO_of_tendsto_ae' hfm hf (FTCFilter.finiteAt_inner l) hu hv
#align interval_integral.measure_integral_sub_linear_is_o_of_tendsto_ae intervalIntegral.measure_integral_sub_linear_isLittleO_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**, local version for any measure.

Let filters `l` and `l'` be related by `[intervalIntegral.FTCFilter a l l']`; let `Î¼` be a locally
finite measure.  If `f` has a finite limit `c` at `l' âŠ“ ae Î¼`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = Î¼ (Ioc u v) â€¢ c + o(Î¼(Ioc u v))` as both `u` and `v` tend to `l`.

See also `measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le'` for a version that also works,
e.g., for `l = l' = Filter.atTop`. -/
theorem measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le
    (hfm : StronglyMeasurableAtFilter f l' Î¼) (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c))
    (hu : Tendsto u lt l) (hv : Tendsto v lt l) (huv : u â‰¤á¶ [lt] v) :
    (fun t => (âˆ« x in u t..v t, f x âˆ‚Î¼) - (Î¼ (Ioc (u t) (v t))).toReal â€¢ c) =o[lt] fun t =>
      (Î¼ <| Ioc (u t) (v t)).toReal :=
  haveI := FTCFilter.meas_gen l
  measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le' hfm hf (FTCFilter.finiteAt_inner l) hu
    hv huv
#align interval_integral.measure_integral_sub_linear_is_o_of_tendsto_ae_of_le intervalIntegral.measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_le

/-- **Fundamental theorem of calculus-1**, local version for any measure.

Let filters `l` and `l'` be related by `[intervalIntegral.FTCFilter a l l']`; let `Î¼` be a locally
finite measure.  If `f` has a finite limit `c` at `l' âŠ“ ae Î¼`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = -Î¼ (Set.Ioc v u) â€¢ c + o(Î¼(Set.Ioc v u))` as both `u` and `v` tend to `l`.

See also `measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_ge'` for a version that also works,
e.g., for `l = l' = Filter.atTop`. -/
theorem measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_ge
    (hfm : StronglyMeasurableAtFilter f l' Î¼) (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c))
    (hu : Tendsto u lt l) (hv : Tendsto v lt l) (huv : v â‰¤á¶ [lt] u) :
    (fun t => (âˆ« x in u t..v t, f x âˆ‚Î¼) + (Î¼ (Ioc (v t) (u t))).toReal â€¢ c) =o[lt] fun t =>
      (Î¼ <| Ioc (v t) (u t)).toReal :=
  haveI := FTCFilter.meas_gen l
  measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_ge' hfm hf (FTCFilter.finiteAt_inner l) hu
    hv huv
#align interval_integral.measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge intervalIntegral.measure_integral_sub_linear_isLittleO_of_tendsto_ae_of_ge

end

variable [FTCFilter a la la'] [FTCFilter b lb lb'] [IsLocallyFiniteMeasure Î¼]

/-- **Fundamental theorem of calculus-1**, strict derivative in both limits for a locally finite
measure.

Let `f` be a measurable function integrable on `a..b`. Let `(la, la')` be a pair of
`intervalIntegral.FTCFilter`s around `a`; let `(lb, lb')` be a pair of `intervalIntegral.FTCFilter`s
around `b`. Suppose that `f` has finite limits `ca` and `cb` at `la' âŠ“ ae Î¼` and `lb' âŠ“ ae Î¼`,
respectively.
Then `âˆ« x in va..vb, f x âˆ‚Î¼ - âˆ« x in ua..ub, f x âˆ‚Î¼ =
  âˆ« x in ub..vb, cb âˆ‚Î¼ - âˆ« x in ua..va, ca âˆ‚Î¼ +
    o(â€–âˆ« x in ua..va, (1:â„) âˆ‚Î¼â€– + â€–âˆ« x in ub..vb, (1:â„) âˆ‚Î¼â€–)`
as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.
-/
theorem measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae
    (hab : IntervalIntegrable f Î¼ a b) (hmeas_a : StronglyMeasurableAtFilter f la' Î¼)
    (hmeas_b : StronglyMeasurableAtFilter f lb' Î¼) (ha_lim : Tendsto f (la' âŠ“ ae Î¼) (ğ“ ca))
    (hb_lim : Tendsto f (lb' âŠ“ ae Î¼) (ğ“ cb)) (hua : Tendsto ua lt la) (hva : Tendsto va lt la)
    (hub : Tendsto ub lt lb) (hvb : Tendsto vb lt lb) :
    (fun t =>
        ((âˆ« x in va t..vb t, f x âˆ‚Î¼) - âˆ« x in ua t..ub t, f x âˆ‚Î¼) -
          ((âˆ« _ in ub t..vb t, cb âˆ‚Î¼) - âˆ« _ in ua t..va t, ca âˆ‚Î¼)) =o[lt]
      fun t => â€–âˆ« _ in ua t..va t, (1 : â„) âˆ‚Î¼â€– + â€–âˆ« _ in ub t..vb t, (1 : â„) âˆ‚Î¼â€– := by
  haveI := FTCFilter.meas_gen la; haveI := FTCFilter.meas_gen lb
  refine
    ((measure_integral_sub_linear_isLittleO_of_tendsto_ae hmeas_a ha_lim hua hva).neg_left.add_add
          (measure_integral_sub_linear_isLittleO_of_tendsto_ae hmeas_b hb_lim hub hvb)).congr'
      ?_ EventuallyEq.rfl
  have A : âˆ€á¶  t in lt, IntervalIntegrable f Î¼ (ua t) (va t) :=
    ha_lim.eventually_intervalIntegrable_ae hmeas_a (FTCFilter.finiteAt_inner la) hua hva
  have A' : âˆ€á¶  t in lt, IntervalIntegrable f Î¼ a (ua t) :=
    ha_lim.eventually_intervalIntegrable_ae hmeas_a (FTCFilter.finiteAt_inner la)
      (tendsto_const_pure.mono_right FTCFilter.pure_le) hua
  have B : âˆ€á¶  t in lt, IntervalIntegrable f Î¼ (ub t) (vb t) :=
    hb_lim.eventually_intervalIntegrable_ae hmeas_b (FTCFilter.finiteAt_inner lb) hub hvb
  have B' : âˆ€á¶  t in lt, IntervalIntegrable f Î¼ b (ub t) :=
    hb_lim.eventually_intervalIntegrable_ae hmeas_b (FTCFilter.finiteAt_inner lb)
      (tendsto_const_pure.mono_right FTCFilter.pure_le) hub
  filter_upwards [A, A', B, B'] with _ ua_va a_ua ub_vb b_ub
  rw [â† integral_interval_sub_interval_comm']
  Â· abel
  exacts [ub_vb, ua_va, b_ub.symm.trans <| hab.symm.trans a_ua]
#align interval_integral.measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae intervalIntegral.measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**, strict derivative in right endpoint for a locally finite
measure.

Let `f` be a measurable function integrable on `a..b`. Let `(lb, lb')` be a pair of
`intervalIntegral.FTCFilter`s around `b`. Suppose that `f` has a finite limit `c` at `lb' âŠ“ ae Î¼`.

Then `âˆ« x in a..v, f x âˆ‚Î¼ - âˆ« x in a..u, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, (1:â„) âˆ‚Î¼)` as
`u` and `v` tend to `lb`.
-/
theorem measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right
    (hab : IntervalIntegrable f Î¼ a b) (hmeas : StronglyMeasurableAtFilter f lb' Î¼)
    (hf : Tendsto f (lb' âŠ“ ae Î¼) (ğ“ c)) (hu : Tendsto u lt lb) (hv : Tendsto v lt lb) :
    (fun t => ((âˆ« x in a..v t, f x âˆ‚Î¼) - âˆ« x in a..u t, f x âˆ‚Î¼) - âˆ« _ in u t..v t, c âˆ‚Î¼) =o[lt]
      fun t => âˆ« _ in u t..v t, (1 : â„) âˆ‚Î¼ := by
  simpa using
    measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae hab stronglyMeasurableAt_bot
      hmeas ((tendsto_bot : Tendsto _ âŠ¥ (ğ“ (0 : E))).mono_left inf_le_left) hf
      (tendsto_const_pure : Tendsto _ _ (pure a)) tendsto_const_pure hu hv
#align interval_integral.measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right intervalIntegral.measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right

/-- **Fundamental theorem of calculus-1**, strict derivative in left endpoint for a locally finite
measure.

Let `f` be a measurable function integrable on `a..b`. Let `(la, la')` be a pair of
`intervalIntegral.FTCFilter`s around `a`. Suppose that `f` has a finite limit `c` at `la' âŠ“ ae Î¼`.

Then `âˆ« x in v..b, f x âˆ‚Î¼ - âˆ« x in u..b, f x âˆ‚Î¼ = -âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, (1:â„) âˆ‚Î¼)`
as `u` and `v` tend to `la`.
-/
theorem measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_left
    (hab : IntervalIntegrable f Î¼ a b) (hmeas : StronglyMeasurableAtFilter f la' Î¼)
    (hf : Tendsto f (la' âŠ“ ae Î¼) (ğ“ c)) (hu : Tendsto u lt la) (hv : Tendsto v lt la) :
    (fun t => ((âˆ« x in v t..b, f x âˆ‚Î¼) - âˆ« x in u t..b, f x âˆ‚Î¼) + âˆ« _ in u t..v t, c âˆ‚Î¼) =o[lt]
      fun t => âˆ« _ in u t..v t, (1 : â„) âˆ‚Î¼ := by
  simpa using
    measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae hab hmeas
      stronglyMeasurableAt_bot hf ((tendsto_bot : Tendsto _ âŠ¥ (ğ“ (0 : E))).mono_left inf_le_left) hu
      hv (tendsto_const_pure : Tendsto _ _ (pure b)) tendsto_const_pure
#align interval_integral.measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_left intervalIntegral.measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_left

end

/-!
### Fundamental theorem of calculus-1 for Lebesgue measure

In this section we restate theorems from the previous section for Lebesgue measure.
In particular, we prove that `âˆ« x in u..v, f x` is strictly differentiable in `(u, v)`
at `(a, b)` provided that `f` is integrable on `a..b` and is continuous at `a` and `b`.
-/


variable [CompleteSpace E]
  {f : â„ â†’ E} {c ca cb : E} {l l' la la' lb lb' : Filter â„} {lt : Filter Î¹} {a b z : â„}
  {u v ua ub va vb : Î¹ â†’ â„} [FTCFilter a la la'] [FTCFilter b lb lb']

/-!
#### Auxiliary `Asymptotics.IsLittleO` statements

In this section we prove several lemmas that can be interpreted as strict differentiability of
`(u, v) â†¦ âˆ« x in u..v, f x âˆ‚Î¼` in `u` and/or `v` at a filter. The statements use
`Asymptotics.isLittleO` because we have no definition of `HasStrict(F)DerivAtFilter` in the library.
-/


/-- **Fundamental theorem of calculus-1**, local version.

If `f` has a finite limit `c` almost surely at `l'`, where `(l, l')` is an
`intervalIntegral.FTCFilter` pair around `a`, then `âˆ« x in u..v, f x âˆ‚Î¼ = (v - u) â€¢ c + o (v - u)`
as both `u` and `v` tend to `l`. -/
theorem integral_sub_linear_isLittleO_of_tendsto_ae [FTCFilter a l l']
    (hfm : StronglyMeasurableAtFilter f l') (hf : Tendsto f (l' âŠ“ ae volume) (ğ“ c)) {u v : Î¹ â†’ â„}
    (hu : Tendsto u lt l) (hv : Tendsto v lt l) :
    (fun t => (âˆ« x in u t..v t, f x) - (v t - u t) â€¢ c) =o[lt] (v - u) := by
  simpa [integral_const] using measure_integral_sub_linear_isLittleO_of_tendsto_ae hfm hf hu hv
#align interval_integral.integral_sub_linear_is_o_of_tendsto_ae intervalIntegral.integral_sub_linear_isLittleO_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**, strict differentiability at filter in both endpoints.

If `f` is a measurable function integrable on `a..b`, `(la, la')` is an `intervalIntegral.FTCFilter`
pair around `a`, and `(lb, lb')` is an `intervalIntegral.FTCFilter` pair around `b`, and `f` has
finite limits `ca` and `cb` almost surely at `la'` and `lb'`, respectively, then
`(âˆ« x in va..vb, f x) - âˆ« x in ua..ub, f x = (vb - ub) â€¢ cb - (va - ua) â€¢ ca +
  o(â€–va - uaâ€– + â€–vb - ubâ€–)` as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.

This lemma could've been formulated using `HasStrictFDerivAtFilter` if we had this
definition. -/
theorem integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae
    (hab : IntervalIntegrable f volume a b) (hmeas_a : StronglyMeasurableAtFilter f la')
    (hmeas_b : StronglyMeasurableAtFilter f lb') (ha_lim : Tendsto f (la' âŠ“ ae volume) (ğ“ ca))
    (hb_lim : Tendsto f (lb' âŠ“ ae volume) (ğ“ cb)) (hua : Tendsto ua lt la) (hva : Tendsto va lt la)
    (hub : Tendsto ub lt lb) (hvb : Tendsto vb lt lb) :
    (fun t =>
        ((âˆ« x in va t..vb t, f x) - âˆ« x in ua t..ub t, f x) -
          ((vb t - ub t) â€¢ cb - (va t - ua t) â€¢ ca)) =o[lt]
      fun t => â€–va t - ua tâ€– + â€–vb t - ub tâ€– := by
  simpa [integral_const]
    using measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae hab hmeas_a hmeas_b
      ha_lim hb_lim hua hva hub hvb
#align interval_integral.integral_sub_integral_sub_linear_is_o_of_tendsto_ae intervalIntegral.integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**, strict differentiability at filter in both endpoints.

If `f` is a measurable function integrable on `a..b`, `(lb, lb')` is an `intervalIntegral.FTCFilter`
pair around `b`, and `f` has a finite limit `c` almost surely at `lb'`, then
`(âˆ« x in a..v, f x) - âˆ« x in a..u, f x = (v - u) â€¢ c + o(â€–v - uâ€–)` as `u` and `v` tend to `lb`.

This lemma could've been formulated using `HasStrictDerivAtFilter` if we had this definition. -/
theorem integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right
    (hab : IntervalIntegrable f volume a b) (hmeas : StronglyMeasurableAtFilter f lb')
    (hf : Tendsto f (lb' âŠ“ ae volume) (ğ“ c)) (hu : Tendsto u lt lb) (hv : Tendsto v lt lb) :
    (fun t => ((âˆ« x in a..v t, f x) - âˆ« x in a..u t, f x) - (v t - u t) â€¢ c) =o[lt] (v - u) := by
  simpa only [integral_const, smul_eq_mul, mul_one] using
    measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right hab hmeas hf hu hv
#align interval_integral.integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right intervalIntegral.integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right

/-- **Fundamental theorem of calculus-1**, strict differentiability at filter in both endpoints.

If `f` is a measurable function integrable on `a..b`, `(la, la')` is an `intervalIntegral.FTCFilter`
pair around `a`, and `f` has a finite limit `c` almost surely at `la'`, then
`(âˆ« x in v..b, f x) - âˆ« x in u..b, f x = -(v - u) â€¢ c + o(â€–v - uâ€–)` as `u` and `v` tend to `la`.

This lemma could've been formulated using `HasStrictDerivAtFilter` if we had this definition. -/
theorem integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_left
    (hab : IntervalIntegrable f volume a b) (hmeas : StronglyMeasurableAtFilter f la')
    (hf : Tendsto f (la' âŠ“ ae volume) (ğ“ c)) (hu : Tendsto u lt la) (hv : Tendsto v lt la) :
    (fun t => ((âˆ« x in v t..b, f x) - âˆ« x in u t..b, f x) + (v t - u t) â€¢ c) =o[lt] (v - u) := by
  simpa only [integral_const, smul_eq_mul, mul_one] using
    measure_integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_left hab hmeas hf hu hv
#align interval_integral.integral_sub_integral_sub_linear_is_o_of_tendsto_ae_left intervalIntegral.integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_left

open ContinuousLinearMap (fst snd smulRight sub_apply smulRight_apply coe_fst' coe_snd' map_sub)

/-!
#### Strict differentiability

In this section we prove that for a measurable function `f` integrable on `a..b`,

* `integral_hasStrictFDerivAt_of_tendsto_ae`: the function `(u, v) â†¦ âˆ« x in u..v, f x` has
  derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)` in the sense of strict differentiability
  provided that `f` tends to `ca` and `cb` almost surely as `x` tendsto to `a` and `b`,
  respectively;

* `integral_hasStrictFDerivAt`: the function `(u, v) â†¦ âˆ« x in u..v, f x` has
  derivative `(u, v) â†¦ v â€¢ f b - u â€¢ f a` at `(a, b)` in the sense of strict differentiability
  provided that `f` is continuous at `a` and `b`;

* `integral_hasStrictDerivAt_of_tendsto_ae_right`: the function `u â†¦ âˆ« x in a..u, f x` has
  derivative `c` at `b` in the sense of strict differentiability provided that `f` tends to `c`
  almost surely as `x` tends to `b`;

* `integral_hasStrictDerivAt_right`: the function `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at
  `b` in the sense of strict differentiability provided that `f` is continuous at `b`;

* `integral_hasStrictDerivAt_of_tendsto_ae_left`: the function `u â†¦ âˆ« x in u..b, f x` has
  derivative `-c` at `a` in the sense of strict differentiability provided that `f` tends to `c`
  almost surely as `x` tends to `a`;

* `integral_hasStrictDerivAt_left`: the function `u â†¦ âˆ« x in u..b, f x` has derivative `-f a` at
  `a` in the sense of strict differentiability provided that `f` is continuous at `a`.
-/


/-- **Fundamental theorem of calculus-1**, strict differentiability in both endpoints.

If `f : â„ â†’ E` is integrable on `a..b` and `f x` has finite limits `ca` and `cb` almost surely as
`x` tends to `a` and `b`, respectively, then
`(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)`
in the sense of strict differentiability. -/
theorem integral_hasStrictFDerivAt_of_tendsto_ae (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f (ğ“ a)) (hmeas_b : StronglyMeasurableAtFilter f (ğ“ b))
    (ha : Tendsto f (ğ“ a âŠ“ ae volume) (ğ“ ca)) (hb : Tendsto f (ğ“ b âŠ“ ae volume) (ğ“ cb)) :
    HasStrictFDerivAt (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x)
      ((snd â„ â„ â„).smulRight cb - (fst â„ â„ â„).smulRight ca) (a, b) := by
  have :=
    integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae hf hmeas_a hmeas_b ha hb
      (continuous_snd.fst.tendsto ((a, b), (a, b)))
      (continuous_fst.fst.tendsto ((a, b), (a, b)))
      (continuous_snd.snd.tendsto ((a, b), (a, b)))
      (continuous_fst.snd.tendsto ((a, b), (a, b)))
  refine (this.congr_left ?_).trans_isBigO ?_
  Â· intro x; simp [sub_smul]; abel
  Â· exact isBigO_fst_prod.norm_left.add isBigO_snd_prod.norm_left
#align interval_integral.integral_has_strict_fderiv_at_of_tendsto_ae intervalIntegral.integral_hasStrictFDerivAt_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**, strict differentiability in both endpoints.

If `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous at `a` and `b`, then
`(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)` in the sense of
strict differentiability. -/
theorem integral_hasStrictFDerivAt (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f (ğ“ a)) (hmeas_b : StronglyMeasurableAtFilter f (ğ“ b))
    (ha : ContinuousAt f a) (hb : ContinuousAt f b) :
    HasStrictFDerivAt (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x)
      ((snd â„ â„ â„).smulRight (f b) - (fst â„ â„ â„).smulRight (f a)) (a, b) :=
  integral_hasStrictFDerivAt_of_tendsto_ae hf hmeas_a hmeas_b (ha.mono_left inf_le_left)
    (hb.mono_left inf_le_left)
#align interval_integral.integral_has_strict_fderiv_at intervalIntegral.integral_hasStrictFDerivAt

/-- **Fundamental theorem of calculus-1**, strict differentiability in the right endpoint.

If `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite limit `c` almost surely at `b`, then
`u â†¦ âˆ« x in a..u, f x` has derivative `c` at `b` in the sense of strict differentiability. -/
theorem integral_hasStrictDerivAt_of_tendsto_ae_right (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ b)) (hb : Tendsto f (ğ“ b âŠ“ ae volume) (ğ“ c)) :
    HasStrictDerivAt (fun u => âˆ« x in a..u, f x) c b :=
  integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right hf hmeas hb continuousAt_snd
    continuousAt_fst
#align interval_integral.integral_has_strict_deriv_at_of_tendsto_ae_right intervalIntegral.integral_hasStrictDerivAt_of_tendsto_ae_right

/-- **Fundamental theorem of calculus-1**, strict differentiability in the right endpoint.

If `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous at `b`, then `u â†¦ âˆ« x in a..u, f x` has
derivative `f b` at `b` in the sense of strict differentiability. -/
theorem integral_hasStrictDerivAt_right (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ b)) (hb : ContinuousAt f b) :
    HasStrictDerivAt (fun u => âˆ« x in a..u, f x) (f b) b :=
  integral_hasStrictDerivAt_of_tendsto_ae_right hf hmeas (hb.mono_left inf_le_left)
#align interval_integral.integral_has_strict_deriv_at_right intervalIntegral.integral_hasStrictDerivAt_right

/-- **Fundamental theorem of calculus-1**, strict differentiability in the left endpoint.

If `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite limit `c` almost surely at `a`, then
`u â†¦ âˆ« x in u..b, f x` has derivative `-c` at `a` in the sense of strict differentiability. -/
theorem integral_hasStrictDerivAt_of_tendsto_ae_left (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ a)) (ha : Tendsto f (ğ“ a âŠ“ ae volume) (ğ“ c)) :
    HasStrictDerivAt (fun u => âˆ« x in u..b, f x) (-c) a := by
  simpa only [â† integral_symm] using
    (integral_hasStrictDerivAt_of_tendsto_ae_right hf.symm hmeas ha).neg
#align interval_integral.integral_has_strict_deriv_at_of_tendsto_ae_left intervalIntegral.integral_hasStrictDerivAt_of_tendsto_ae_left

/-- **Fundamental theorem of calculus-1**, strict differentiability in the left endpoint.

If `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous at `a`, then `u â†¦ âˆ« x in u..b, f x` has
derivative `-f a` at `a` in the sense of strict differentiability. -/
theorem integral_hasStrictDerivAt_left (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ a)) (ha : ContinuousAt f a) :
    HasStrictDerivAt (fun u => âˆ« x in u..b, f x) (-f a) a := by
  simpa only [â† integral_symm] using (integral_hasStrictDerivAt_right hf.symm hmeas ha).neg
#align interval_integral.integral_has_strict_deriv_at_left intervalIntegral.integral_hasStrictDerivAt_left

/-- **Fundamental theorem of calculus-1**, strict differentiability in the right endpoint.

If `f : â„ â†’ E` is continuous, then `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at `b` in the sense
of strict differentiability. -/
theorem _root_.Continuous.integral_hasStrictDerivAt {f : â„ â†’ E} (hf : Continuous f) (a b : â„) :
    HasStrictDerivAt (fun u => âˆ« x : â„ in a..u, f x) (f b) b :=
  integral_hasStrictDerivAt_right (hf.intervalIntegrable _ _) (hf.stronglyMeasurableAtFilter _ _)
    hf.continuousAt
#align continuous.integral_has_strict_deriv_at Continuous.integral_hasStrictDerivAt

/-- **Fundamental theorem of calculus-1**, derivative in the right endpoint.

If `f : â„ â†’ E` is continuous, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b` is `f b`. -/
theorem _root_.Continuous.deriv_integral (f : â„ â†’ E) (hf : Continuous f) (a b : â„) :
    deriv (fun u => âˆ« x : â„ in a..u, f x) b = f b :=
  (hf.integral_hasStrictDerivAt a b).hasDerivAt.deriv
#align continuous.deriv_integral Continuous.deriv_integral

/-!
#### FrÃ©chet differentiability

In this subsection we restate results from the previous subsection in terms of `HasFDerivAt`,
`HasDerivAt`, `fderiv`, and `deriv`.
-/


/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has
finite limits `ca` and `cb` almost surely as `x` tends to `a` and `b`, respectively, then
`(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)`. -/
theorem integral_hasFDerivAt_of_tendsto_ae (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f (ğ“ a)) (hmeas_b : StronglyMeasurableAtFilter f (ğ“ b))
    (ha : Tendsto f (ğ“ a âŠ“ ae volume) (ğ“ ca)) (hb : Tendsto f (ğ“ b âŠ“ ae volume) (ğ“ cb)) :
    HasFDerivAt (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x)
      ((snd â„ â„ â„).smulRight cb - (fst â„ â„ â„).smulRight ca) (a, b) :=
  (integral_hasStrictFDerivAt_of_tendsto_ae hf hmeas_a hmeas_b ha hb).hasFDerivAt
#align interval_integral.integral_has_fderiv_at_of_tendsto_ae intervalIntegral.integral_hasFDerivAt_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f` is
continuous at `a` and `b`, then `(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢
ca` at `(a, b)`. -/
theorem integral_hasFDerivAt (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f (ğ“ a)) (hmeas_b : StronglyMeasurableAtFilter f (ğ“ b))
    (ha : ContinuousAt f a) (hb : ContinuousAt f b) :
    HasFDerivAt (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x)
      ((snd â„ â„ â„).smulRight (f b) - (fst â„ â„ â„).smulRight (f a)) (a, b) :=
  (integral_hasStrictFDerivAt hf hmeas_a hmeas_b ha hb).hasFDerivAt
#align interval_integral.integral_has_fderiv_at intervalIntegral.integral_hasFDerivAt

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has
finite limits `ca` and `cb` almost surely as `x` tends to `a` and `b`, respectively, then `fderiv`
derivative of `(u, v) â†¦ âˆ« x in u..v, f x` at `(a, b)` equals `(u, v) â†¦ v â€¢ cb - u â€¢ ca`. -/
theorem fderiv_integral_of_tendsto_ae (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f (ğ“ a)) (hmeas_b : StronglyMeasurableAtFilter f (ğ“ b))
    (ha : Tendsto f (ğ“ a âŠ“ ae volume) (ğ“ ca)) (hb : Tendsto f (ğ“ b âŠ“ ae volume) (ğ“ cb)) :
    fderiv â„ (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x) (a, b) =
      (snd â„ â„ â„).smulRight cb - (fst â„ â„ â„).smulRight ca :=
  (integral_hasFDerivAt_of_tendsto_ae hf hmeas_a hmeas_b ha hb).fderiv
#align interval_integral.fderiv_integral_of_tendsto_ae intervalIntegral.fderiv_integral_of_tendsto_ae

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f` is
continuous at `a` and `b`, then `fderiv` derivative of `(u, v) â†¦ âˆ« x in u..v, f x` at `(a, b)`
equals `(u, v) â†¦ v â€¢ cb - u â€¢ ca`. -/
theorem fderiv_integral (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f (ğ“ a)) (hmeas_b : StronglyMeasurableAtFilter f (ğ“ b))
    (ha : ContinuousAt f a) (hb : ContinuousAt f b) :
    fderiv â„ (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x) (a, b) =
      (snd â„ â„ â„).smulRight (f b) - (fst â„ â„ â„).smulRight (f a) :=
  (integral_hasFDerivAt hf hmeas_a hmeas_b ha hb).fderiv
#align interval_integral.fderiv_integral intervalIntegral.fderiv_integral

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a
finite limit `c` almost surely at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `c` at `b`. -/
theorem integral_hasDerivAt_of_tendsto_ae_right (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ b)) (hb : Tendsto f (ğ“ b âŠ“ ae volume) (ğ“ c)) :
    HasDerivAt (fun u => âˆ« x in a..u, f x) c b :=
  (integral_hasStrictDerivAt_of_tendsto_ae_right hf hmeas hb).hasDerivAt
#align interval_integral.integral_has_deriv_at_of_tendsto_ae_right intervalIntegral.integral_hasDerivAt_of_tendsto_ae_right

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f` is
continuous at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at `b`. -/
theorem integral_hasDerivAt_right (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ b)) (hb : ContinuousAt f b) :
    HasDerivAt (fun u => âˆ« x in a..u, f x) (f b) b :=
  (integral_hasStrictDerivAt_right hf hmeas hb).hasDerivAt
#align interval_integral.integral_has_deriv_at_right intervalIntegral.integral_hasDerivAt_right

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` has a finite
limit `c` almost surely at `b`, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
theorem deriv_integral_of_tendsto_ae_right (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ b)) (hb : Tendsto f (ğ“ b âŠ“ ae volume) (ğ“ c)) :
    deriv (fun u => âˆ« x in a..u, f x) b = c :=
  (integral_hasDerivAt_of_tendsto_ae_right hf hmeas hb).deriv
#align interval_integral.deriv_integral_of_tendsto_ae_right intervalIntegral.deriv_integral_of_tendsto_ae_right

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `b`, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `f b`. -/
theorem deriv_integral_right (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ b)) (hb : ContinuousAt f b) :
    deriv (fun u => âˆ« x in a..u, f x) b = f b :=
  (integral_hasDerivAt_right hf hmeas hb).deriv
#align interval_integral.deriv_integral_right intervalIntegral.deriv_integral_right

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a
finite limit `c` almost surely at `a`, then `u â†¦ âˆ« x in u..b, f x` has derivative `-c` at `a`. -/
theorem integral_hasDerivAt_of_tendsto_ae_left (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ a)) (ha : Tendsto f (ğ“ a âŠ“ ae volume) (ğ“ c)) :
    HasDerivAt (fun u => âˆ« x in u..b, f x) (-c) a :=
  (integral_hasStrictDerivAt_of_tendsto_ae_left hf hmeas ha).hasDerivAt
#align interval_integral.integral_has_deriv_at_of_tendsto_ae_left intervalIntegral.integral_hasDerivAt_of_tendsto_ae_left

/-- **Fundamental theorem of calculus-1**: if `f : â„ â†’ E` is integrable on `a..b` and `f` is
continuous at `a`, then `u â†¦ âˆ« x in u..b, f x` has derivative `-f a` at `a`. -/
theorem integral_hasDerivAt_left (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ a)) (ha : ContinuousAt f a) :
    HasDerivAt (fun u => âˆ« x in u..b, f x) (-f a) a :=
  (integral_hasStrictDerivAt_left hf hmeas ha).hasDerivAt
#align interval_integral.integral_has_deriv_at_left intervalIntegral.integral_hasDerivAt_left

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` has a finite
limit `c` almost surely at `a`, then the derivative of `u â†¦ âˆ« x in u..b, f x` at `a` equals `-c`. -/
theorem deriv_integral_of_tendsto_ae_left (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ a)) (hb : Tendsto f (ğ“ a âŠ“ ae volume) (ğ“ c)) :
    deriv (fun u => âˆ« x in u..b, f x) a = -c :=
  (integral_hasDerivAt_of_tendsto_ae_left hf hmeas hb).deriv
#align interval_integral.deriv_integral_of_tendsto_ae_left intervalIntegral.deriv_integral_of_tendsto_ae_left

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a`, then the derivative of `u â†¦ âˆ« x in u..b, f x` at `a` equals `-f a`. -/
theorem deriv_integral_left (hf : IntervalIntegrable f volume a b)
    (hmeas : StronglyMeasurableAtFilter f (ğ“ a)) (hb : ContinuousAt f a) :
    deriv (fun u => âˆ« x in u..b, f x) a = -f a :=
  (integral_hasDerivAt_left hf hmeas hb).deriv
#align interval_integral.deriv_integral_left intervalIntegral.deriv_integral_left

/-!
#### One-sided derivatives
-/


/-- Let `f` be a measurable function integrable on `a..b`. The function `(u, v) â†¦ âˆ« x in u..v, f x`
has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` within `s Ã— t` at `(a, b)`, where
`s âˆˆ {Iic a, {a}, Ici a, univ}` and `t âˆˆ {Iic b, {b}, Ici b, univ}` provided that `f` tends to `ca`
and `cb` almost surely at the filters `la` and `lb` from the following table.

| `s`     | `la`     | `t`     | `lb`     |
| ------- | ----     | ---     | ----     |
| `Iic a` | `ğ“[â‰¤] a` | `Iic b` | `ğ“[â‰¤] b` |
| `Ici a` | `ğ“[>] a` | `Ici b` | `ğ“[>] b` |
| `{a}`   | `âŠ¥`      | `{b}`   | `âŠ¥`      |
| `univ`  | `ğ“ a`    | `univ`  | `ğ“ b`    |
-/
theorem integral_hasFDerivWithinAt_of_tendsto_ae (hf : IntervalIntegrable f volume a b)
    {s t : Set â„} [FTCFilter a (ğ“[s] a) la] [FTCFilter b (ğ“[t] b) lb]
    (hmeas_a : StronglyMeasurableAtFilter f la) (hmeas_b : StronglyMeasurableAtFilter f lb)
    (ha : Tendsto f (la âŠ“ ae volume) (ğ“ ca)) (hb : Tendsto f (lb âŠ“ ae volume) (ğ“ cb)) :
    HasFDerivWithinAt (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x)
      ((snd â„ â„ â„).smulRight cb - (fst â„ â„ â„).smulRight ca) (s Ã—Ë¢ t) (a, b) := by
  rw [HasFDerivWithinAt, nhdsWithin_prod_eq]
  have :=
    integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae hf hmeas_a hmeas_b ha hb
      (tendsto_const_pure.mono_right FTCFilter.pure_le : Tendsto _ _ (ğ“[s] a)) tendsto_fst
      (tendsto_const_pure.mono_right FTCFilter.pure_le : Tendsto _ _ (ğ“[t] b)) tendsto_snd
  refine .of_isLittleO <| (this.congr_left ?_).trans_isBigO ?_
  Â· intro x; simp [sub_smul]; abel
  Â· exact isBigO_fst_prod.norm_left.add isBigO_snd_prod.norm_left
#align interval_integral.integral_has_fderiv_within_at_of_tendsto_ae intervalIntegral.integral_hasFDerivWithinAt_of_tendsto_ae

/-- Let `f` be a measurable function integrable on `a..b`. The function `(u, v) â†¦ âˆ« x in u..v, f x`
has derivative `(u, v) â†¦ v â€¢ f b - u â€¢ f a` within `s Ã— t` at `(a, b)`, where
`s âˆˆ {Iic a, {a}, Ici a, univ}` and `t âˆˆ {Iic b, {b}, Ici b, univ}` provided that `f` tends to
`f a` and `f b` at the filters `la` and `lb` from the following table. In most cases this assumption
is definitionally equal `ContinuousAt f _` or `ContinuousWithinAt f _ _`.

| `s`     | `la`     | `t`     | `lb`     |
| ------- | ----     | ---     | ----     |
| `Iic a` | `ğ“[â‰¤] a` | `Iic b` | `ğ“[â‰¤] b` |
| `Ici a` | `ğ“[>] a` | `Ici b` | `ğ“[>] b` |
| `{a}`   | `âŠ¥`      | `{b}`   | `âŠ¥`      |
| `univ`  | `ğ“ a`    | `univ`  | `ğ“ b`    |
-/
theorem integral_hasFDerivWithinAt (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f la) (hmeas_b : StronglyMeasurableAtFilter f lb)
    {s t : Set â„} [FTCFilter a (ğ“[s] a) la] [FTCFilter b (ğ“[t] b) lb] (ha : Tendsto f la (ğ“ <| f a))
    (hb : Tendsto f lb (ğ“ <| f b)) :
    HasFDerivWithinAt (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x)
      ((snd â„ â„ â„).smulRight (f b) - (fst â„ â„ â„).smulRight (f a)) (s Ã—Ë¢ t) (a, b) :=
  integral_hasFDerivWithinAt_of_tendsto_ae hf hmeas_a hmeas_b (ha.mono_left inf_le_left)
    (hb.mono_left inf_le_left)
#align interval_integral.integral_has_fderiv_within_at intervalIntegral.integral_hasFDerivWithinAt

/-- An auxiliary tactic closing goals `UniqueDiffWithinAt â„ s a` where
`s âˆˆ {Iic a, Ici a, univ}`. -/
macro "uniqueDiffWithinAt_Ici_Iic_univ" : tactic =>
  `(tactic| (first | exact uniqueDiffOn_Ici _ _ left_mem_Ici |
    exact uniqueDiffOn_Iic _ _ right_mem_Iic | exact uniqueDiffWithinAt_univ (ğ•œ := â„) (E := â„)))
#noalign interval_integral.unique_diff_within_at_Ici_Iic_univ

/-- Let `f` be a measurable function integrable on `a..b`. Choose `s âˆˆ {Iic a, Ici a, univ}`
and `t âˆˆ {Iic b, Ici b, univ}`. Suppose that `f` tends to `ca` and `cb` almost surely at the filters
`la` and `lb` from the table below. Then `fderivWithin â„ (fun p â†¦ âˆ« x in p.1..p.2, f x) (s Ã—Ë¢ t)`
is equal to `(u, v) â†¦ u â€¢ cb - v â€¢ ca`.

| `s`     | `la`     | `t`     | `lb`     |
| ------- | ----     | ---     | ----     |
| `Iic a` | `ğ“[â‰¤] a` | `Iic b` | `ğ“[â‰¤] b` |
| `Ici a` | `ğ“[>] a` | `Ici b` | `ğ“[>] b` |
| `{a}`   | `âŠ¥`      | `{b}`   | `âŠ¥`      |
| `univ`  | `ğ“ a`    | `univ`  | `ğ“ b`    |
-/
theorem fderivWithin_integral_of_tendsto_ae (hf : IntervalIntegrable f volume a b)
    (hmeas_a : StronglyMeasurableAtFilter f la) (hmeas_b : StronglyMeasurableAtFilter f lb)
    {s t : Set â„} [FTCFilter a (ğ“[s] a) la] [FTCFilter b (ğ“[t] b) lb]
    (ha : Tendsto f (la âŠ“ ae volume) (ğ“ ca)) (hb : Tendsto f (lb âŠ“ ae volume) (ğ“ cb))
    (hs : UniqueDiffWithinAt â„ s a := by uniqueDiffWithinAt_Ici_Iic_univ)
    (ht : UniqueDiffWithinAt â„ t b := by uniqueDiffWithinAt_Ici_Iic_univ) :
    fderivWithin â„ (fun p : â„ Ã— â„ => âˆ« x in p.1..p.2, f x) (s Ã—Ë¢ t) (a, b) =
      (snd â„ â„ â„).smulRight cb - (fst â„ â„ â„).smulRight ca :=
  (integral_hasFDerivWithinAt_of_tendsto_ae hf hmeas_a hmeas_b ha hb).fderivWithin <| hs.prod ht
#align interval_integral.fderiv_within_integral_of_tendsto_ae intervalIntegral.fderivWithin_integral_of_tendsto_ae

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the right or from the left,
then `u â†¦ âˆ« x in a..u, f x` has right (resp., left) derivative `c` at `b`. -/
theorem integral_hasDerivWithinAt_of_tendsto_ae_right (hf : IntervalIntegrable f volume a b)
    {s t : Set â„} [FTCFilter b (ğ“[s] b) (ğ“[t] b)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] b))
    (hb : Tendsto f (ğ“[t] b âŠ“ ae volume) (ğ“ c)) :
    HasDerivWithinAt (fun u => âˆ« x in a..u, f x) c s b :=
  .of_isLittleO <| integral_sub_integral_sub_linear_isLittleO_of_tendsto_ae_right hf hmeas hb
    (tendsto_const_pure.mono_right FTCFilter.pure_le) tendsto_id
#align interval_integral.integral_has_deriv_within_at_of_tendsto_ae_right intervalIntegral.integral_hasDerivWithinAt_of_tendsto_ae_right

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the left or from the right at `b`, then `u â†¦ âˆ« x in a..u, f x` has left (resp., right)
derivative `f b` at `b`. -/
theorem integral_hasDerivWithinAt_right (hf : IntervalIntegrable f volume a b) {s t : Set â„}
    [FTCFilter b (ğ“[s] b) (ğ“[t] b)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] b))
    (hb : ContinuousWithinAt f t b) : HasDerivWithinAt (fun u => âˆ« x in a..u, f x) (f b) s b :=
  integral_hasDerivWithinAt_of_tendsto_ae_right hf hmeas (hb.mono_left inf_le_left)
#align interval_integral.integral_has_deriv_within_at_right intervalIntegral.integral_hasDerivWithinAt_right

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the right or from the left, then the right
(resp., left) derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
theorem derivWithin_integral_of_tendsto_ae_right (hf : IntervalIntegrable f volume a b)
    {s t : Set â„} [FTCFilter b (ğ“[s] b) (ğ“[t] b)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] b))
    (hb : Tendsto f (ğ“[t] b âŠ“ ae volume) (ğ“ c))
    (hs : UniqueDiffWithinAt â„ s b := by uniqueDiffWithinAt_Ici_Iic_univ) :
    derivWithin (fun u => âˆ« x in a..u, f x) s b = c :=
  (integral_hasDerivWithinAt_of_tendsto_ae_right hf hmeas hb).derivWithin hs
#align interval_integral.deriv_within_integral_of_tendsto_ae_right intervalIntegral.derivWithin_integral_of_tendsto_ae_right

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
on the right or on the left at `b`, then the right (resp., left) derivative of
`u â†¦ âˆ« x in a..u, f x` at `b` equals `f b`. -/
theorem derivWithin_integral_right (hf : IntervalIntegrable f volume a b) {s t : Set â„}
    [FTCFilter b (ğ“[s] b) (ğ“[t] b)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] b))
    (hb : ContinuousWithinAt f t b)
    (hs : UniqueDiffWithinAt â„ s b := by uniqueDiffWithinAt_Ici_Iic_univ) :
    derivWithin (fun u => âˆ« x in a..u, f x) s b = f b :=
  (integral_hasDerivWithinAt_right hf hmeas hb).derivWithin hs
#align interval_integral.deriv_within_integral_right intervalIntegral.derivWithin_integral_right

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `a` from the right or from the left,
then `u â†¦ âˆ« x in u..b, f x` has right (resp., left) derivative `-c` at `a`. -/
theorem integral_hasDerivWithinAt_of_tendsto_ae_left (hf : IntervalIntegrable f volume a b)
    {s t : Set â„} [FTCFilter a (ğ“[s] a) (ğ“[t] a)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] a))
    (ha : Tendsto f (ğ“[t] a âŠ“ ae volume) (ğ“ c)) :
    HasDerivWithinAt (fun u => âˆ« x in u..b, f x) (-c) s a := by
  simp only [integral_symm b]
  exact (integral_hasDerivWithinAt_of_tendsto_ae_right hf.symm hmeas ha).neg
#align interval_integral.integral_has_deriv_within_at_of_tendsto_ae_left intervalIntegral.integral_hasDerivWithinAt_of_tendsto_ae_left

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the left or from the right at `a`, then `u â†¦ âˆ« x in u..b, f x` has left (resp., right)
derivative `-f a` at `a`. -/
theorem integral_hasDerivWithinAt_left (hf : IntervalIntegrable f volume a b) {s t : Set â„}
    [FTCFilter a (ğ“[s] a) (ğ“[t] a)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] a))
    (ha : ContinuousWithinAt f t a) : HasDerivWithinAt (fun u => âˆ« x in u..b, f x) (-f a) s a :=
  integral_hasDerivWithinAt_of_tendsto_ae_left hf hmeas (ha.mono_left inf_le_left)
#align interval_integral.integral_has_deriv_within_at_left intervalIntegral.integral_hasDerivWithinAt_left

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `a` from the right or from the left, then the right
(resp., left) derivative of `u â†¦ âˆ« x in u..b, f x` at `a` equals `-c`. -/
theorem derivWithin_integral_of_tendsto_ae_left (hf : IntervalIntegrable f volume a b) {s t : Set â„}
    [FTCFilter a (ğ“[s] a) (ğ“[t] a)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] a))
    (ha : Tendsto f (ğ“[t] a âŠ“ ae volume) (ğ“ c))
    (hs : UniqueDiffWithinAt â„ s a := by uniqueDiffWithinAt_Ici_Iic_univ) :
    derivWithin (fun u => âˆ« x in u..b, f x) s a = -c :=
  (integral_hasDerivWithinAt_of_tendsto_ae_left hf hmeas ha).derivWithin hs
#align interval_integral.deriv_within_integral_of_tendsto_ae_left intervalIntegral.derivWithin_integral_of_tendsto_ae_left

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
on the right or on the left at `a`, then the right (resp., left) derivative of
`u â†¦ âˆ« x in u..b, f x` at `a` equals `-f a`. -/
theorem derivWithin_integral_left (hf : IntervalIntegrable f volume a b) {s t : Set â„}
    [FTCFilter a (ğ“[s] a) (ğ“[t] a)] (hmeas : StronglyMeasurableAtFilter f (ğ“[t] a))
    (ha : ContinuousWithinAt f t a)
    (hs : UniqueDiffWithinAt â„ s a := by uniqueDiffWithinAt_Ici_Iic_univ) :
    derivWithin (fun u => âˆ« x in u..b, f x) s a = -f a :=
  (integral_hasDerivWithinAt_left hf hmeas ha).derivWithin hs
#align interval_integral.deriv_within_integral_left intervalIntegral.derivWithin_integral_left

/-- The integral of a continuous function is differentiable on a real set `s`. -/
theorem differentiableOn_integral_of_continuous {s : Set â„}
    (hintg : âˆ€ x âˆˆ s, IntervalIntegrable f volume a x) (hcont : Continuous f) :
    DifferentiableOn â„ (fun u => âˆ« x in a..u, f x) s := fun y hy =>
  (integral_hasDerivAt_right (hintg y hy) hcont.aestronglyMeasurable.stronglyMeasurableAtFilter
        hcont.continuousAt).differentiableAt.differentiableWithinAt
#align interval_integral.differentiable_on_integral_of_continuous intervalIntegral.differentiableOn_integral_of_continuous

end FTC1

/-!
### Fundamental theorem of calculus, part 2

This section contains theorems pertaining to FTC-2 for interval integrals, i.e., the assertion
that `âˆ« x in a..b, f' x = f b - f a` under suitable assumptions.

The most classical version of this theorem assumes that `f'` is continuous. However, this is
unnecessarily strong: the result holds if `f'` is just integrable. We prove the strong version,
following [Rudin, *Real and Complex Analysis* (Theorem 7.21)][rudin2006real]. The proof is first
given for real-valued functions, and then deduced for functions with a general target space. For
a real-valued function `g`, it suffices to show that `g b - g a â‰¤ (âˆ« x in a..b, g' x) + Îµ` for all
positive `Îµ`. To prove this, choose a lower-semicontinuous function `G'` with `g' < G'` and with
integral close to that of `g'` (its existence is guaranteed by the Vitali-CarathÃ©odory theorem).
It satisfies `g t - g a â‰¤ âˆ« x in a..t, G' x` for all `t âˆˆ [a, b]`: this inequality holds at `a`,
and if it holds at `t` then it holds for `u` close to `t` on its right, as the left hand side
increases by `g u - g t âˆ¼ (u -t) g' t`, while the right hand side increases by
`âˆ« x in t..u, G' x` which is roughly at least `âˆ« x in t..u, G' t = (u - t) G' t`, by lower
semicontinuity. As  `g' t < G' t`, this gives the conclusion. One can therefore push progressively
this inequality to the right until the point `b`, where it gives the desired conclusion.
-/

variable {f : â„ â†’ E} {g' g Ï† : â„ â†’ â„}

/-- Hard part of FTC-2 for integrable derivatives, real-valued functions: one has
`g b - g a â‰¤ âˆ« y in a..b, g' y` when `g'` is integrable.
Auxiliary lemma in the proof of `integral_eq_sub_of_hasDeriv_right_of_le`.
We give the slightly more general version that `g b - g a â‰¤ âˆ« y in a..b, Ï† y` when `g' â‰¤ Ï†` and
`Ï†` is integrable (even if `g'` is not known to be integrable).
Version assuming that `g` is differentiable on `[a, b)`. -/
theorem sub_le_integral_of_hasDeriv_right_of_le_Ico (hab : a â‰¤ b)
    (hcont : ContinuousOn g (Icc a b)) (hderiv : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt g (g' x) (Ioi x) x)
    (Ï†int : IntegrableOn Ï† (Icc a b)) (hÏ†g : âˆ€ x âˆˆ Ico a b, g' x â‰¤ Ï† x) :
    g b - g a â‰¤ âˆ« y in a..b, Ï† y := by
  refine le_of_forall_pos_le_add fun Îµ Îµpos => ?_
  -- Bound from above `g'` by a lower-semicontinuous function `G'`.
  rcases exists_lt_lowerSemicontinuous_integral_lt Ï† Ï†int Îµpos with
    âŸ¨G', f_lt_G', G'cont, G'int, G'lt_top, hG'âŸ©
  -- we will show by "induction" that `g t - g a â‰¤ âˆ« u in a..t, G' u` for all `t âˆˆ [a, b]`.
  set s := {t | g t - g a â‰¤ âˆ« u in a..t, (G' u).toReal} âˆ© Icc a b
  -- the set `s` of points where this property holds is closed.
  have s_closed : IsClosed s := by
    have : ContinuousOn (fun t => (g t - g a, âˆ« u in a..t, (G' u).toReal)) (Icc a b) := by
      rw [â† uIcc_of_le hab] at G'int hcont âŠ¢
      exact (hcont.sub continuousOn_const).prod (continuousOn_primitive_interval G'int)
    simp only [s, inter_comm]
    exact this.preimage_isClosed_of_isClosed isClosed_Icc OrderClosedTopology.isClosed_le'
  have main : Icc a b âŠ† {t | g t - g a â‰¤ âˆ« u in a..t, (G' u).toReal} := by
    -- to show that the set `s` is all `[a, b]`, it suffices to show that any point `t` in `s`
    -- with `t < b` admits another point in `s` slightly to its right
    -- (this is a sort of real induction).
    refine s_closed.Icc_subset_of_forall_exists_gt
      (by simp only [integral_same, mem_setOf_eq, sub_self, le_rfl]) fun t ht v t_lt_v => ?_
    obtain âŸ¨y, g'_lt_y', y_lt_G'âŸ© : âˆƒ y : â„, (g' t : EReal) < y âˆ§ (y : EReal) < G' t :=
      EReal.lt_iff_exists_real_btwn.1 ((EReal.coe_le_coe_iff.2 (hÏ†g t ht.2)).trans_lt (f_lt_G' t))
    -- bound from below the increase of `âˆ« x in a..u, G' x` on the right of `t`, using the lower
    -- semicontinuity of `G'`.
    have I1 : âˆ€á¶  u in ğ“[>] t, (u - t) * y â‰¤ âˆ« w in t..u, (G' w).toReal := by
      have B : âˆ€á¶  u in ğ“ t, (y : EReal) < G' u := G'cont.lowerSemicontinuousAt _ _ y_lt_G'
      rcases mem_nhds_iff_exists_Ioo_subset.1 B with âŸ¨m, M, âŸ¨hm, hMâŸ©, HâŸ©
      have : Ioo t (min M b) âˆˆ ğ“[>] t := Ioo_mem_nhdsWithin_Ioi' (lt_min hM ht.right.right)
      filter_upwards [this] with u hu
      have I : Icc t u âŠ† Icc a b := Icc_subset_Icc ht.2.1 (hu.2.le.trans (min_le_right _ _))
      calc
        (u - t) * y = âˆ« _ in Icc t u, y := by
          simp only [hu.left.le, MeasureTheory.integral_const, Algebra.id.smul_eq_mul, sub_nonneg,
            MeasurableSet.univ, Real.volume_Icc, Measure.restrict_apply, univ_inter,
            ENNReal.toReal_ofReal]
        _ â‰¤ âˆ« w in t..u, (G' w).toReal := by
          rw [intervalIntegral.integral_of_le hu.1.le, â† integral_Icc_eq_integral_Ioc]
          apply setIntegral_mono_ae_restrict
          Â· simp only [integrableOn_const, Real.volume_Icc, ENNReal.ofReal_lt_top, or_true_iff]
          Â· exact IntegrableOn.mono_set G'int I
          Â· have C1 : âˆ€áµ x : â„ âˆ‚volume.restrict (Icc t u), G' x < âˆ :=
              ae_mono (Measure.restrict_mono I le_rfl) G'lt_top
            have C2 : âˆ€áµ x : â„ âˆ‚volume.restrict (Icc t u), x âˆˆ Icc t u :=
              ae_restrict_mem measurableSet_Icc
            filter_upwards [C1, C2] with x G'x hx
            apply EReal.coe_le_coe_iff.1
            have : x âˆˆ Ioo m M := by
              simp only [hm.trans_le hx.left,
                (hx.right.trans_lt hu.right).trans_le (min_le_left M b), mem_Ioo, and_self_iff]
            refine (H this).out.le.trans_eq ?_
            exact (EReal.coe_toReal G'x.ne (f_lt_G' x).ne_bot).symm
    -- bound from above the increase of `g u - g a` on the right of `t`, using the derivative at `t`
    have I2 : âˆ€á¶  u in ğ“[>] t, g u - g t â‰¤ (u - t) * y := by
      have g'_lt_y : g' t < y := EReal.coe_lt_coe_iff.1 g'_lt_y'
      filter_upwards [(hderiv t âŸ¨ht.2.1, ht.2.2âŸ©).limsup_slope_le' (not_mem_Ioi.2 le_rfl) g'_lt_y,
        self_mem_nhdsWithin] with u hu t_lt_u
      have := mul_le_mul_of_nonneg_left hu.le (sub_pos.2 t_lt_u.out).le
      rwa [â† smul_eq_mul, sub_smul_slope] at this
    -- combine the previous two bounds to show that `g u - g a` increases less quickly than
    -- `âˆ« x in a..u, G' x`.
    have I3 : âˆ€á¶  u in ğ“[>] t, g u - g t â‰¤ âˆ« w in t..u, (G' w).toReal := by
      filter_upwards [I1, I2] with u hu1 hu2 using hu2.trans hu1
    have I4 : âˆ€á¶  u in ğ“[>] t, u âˆˆ Ioc t (min v b) := by
      refine mem_nhdsWithin_Ioi_iff_exists_Ioc_subset.2 âŸ¨min v b, ?_, Subset.rflâŸ©
      simp only [lt_min_iff, mem_Ioi]
      exact âŸ¨t_lt_v, ht.2.2âŸ©
    -- choose a point `x` slightly to the right of `t` which satisfies the above bound
    rcases (I3.and I4).exists with âŸ¨x, hx, h'xâŸ©
    -- we check that it belongs to `s`, essentially by construction
    refine âŸ¨x, ?_, Ioc_subset_Ioc le_rfl (min_le_left _ _) h'xâŸ©
    calc
      g x - g a = g t - g a + (g x - g t) := by abel
      _ â‰¤ (âˆ« w in a..t, (G' w).toReal) + âˆ« w in t..x, (G' w).toReal := add_le_add ht.1 hx
      _ = âˆ« w in a..x, (G' w).toReal := by
        apply integral_add_adjacent_intervals
        Â· rw [intervalIntegrable_iff_integrableOn_Ioc_of_le ht.2.1]
          exact IntegrableOn.mono_set G'int
            (Ioc_subset_Icc_self.trans (Icc_subset_Icc le_rfl ht.2.2.le))
        Â· rw [intervalIntegrable_iff_integrableOn_Ioc_of_le h'x.1.le]
          apply IntegrableOn.mono_set G'int
          exact Ioc_subset_Icc_self.trans (Icc_subset_Icc ht.2.1 (h'x.2.trans (min_le_right _ _)))
  -- now that we know that `s` contains `[a, b]`, we get the desired result by applying this to `b`.
  calc
    g b - g a â‰¤ âˆ« y in a..b, (G' y).toReal := main (right_mem_Icc.2 hab)
    _ â‰¤ (âˆ« y in a..b, Ï† y) + Îµ := by
      convert hG'.le <;>
        Â· rw [intervalIntegral.integral_of_le hab]
          simp only [integral_Icc_eq_integral_Ioc', Real.volume_singleton]
#align interval_integral.sub_le_integral_of_has_deriv_right_of_le_Ico intervalIntegral.sub_le_integral_of_hasDeriv_right_of_le_Ico

-- Porting note: Lean was adding `lb`/`lb'` to the arguments of this theorem, so I enclosed FTC-1
-- into a `section`
/-- Hard part of FTC-2 for integrable derivatives, real-valued functions: one has
`g b - g a â‰¤ âˆ« y in a..b, g' y` when `g'` is integrable.
Auxiliary lemma in the proof of `integral_eq_sub_of_hasDeriv_right_of_le`.
We give the slightly more general version that `g b - g a â‰¤ âˆ« y in a..b, Ï† y` when `g' â‰¤ Ï†` and
`Ï†` is integrable (even if `g'` is not known to be integrable).
Version assuming that `g` is differentiable on `(a, b)`. -/
theorem sub_le_integral_of_hasDeriv_right_of_le (hab : a â‰¤ b) (hcont : ContinuousOn g (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivWithinAt g (g' x) (Ioi x) x) (Ï†int : IntegrableOn Ï† (Icc a b))
    (hÏ†g : âˆ€ x âˆˆ Ioo a b, g' x â‰¤ Ï† x) : g b - g a â‰¤ âˆ« y in a..b, Ï† y := by
  -- This follows from the version on a closed-open interval (applied to `[t, b)` for `t` close to
  -- `a`) and a continuity argument.
  obtain rfl | a_lt_b := hab.eq_or_lt
  Â· simp
  set s := {t | g b - g t â‰¤ âˆ« u in t..b, Ï† u} âˆ© Icc a b
  have s_closed : IsClosed s := by
    have : ContinuousOn (fun t => (g b - g t, âˆ« u in t..b, Ï† u)) (Icc a b) := by
      rw [â† uIcc_of_le hab] at hcont Ï†int âŠ¢
      exact (continuousOn_const.sub hcont).prod (continuousOn_primitive_interval_left Ï†int)
    simp only [s, inter_comm]
    exact this.preimage_isClosed_of_isClosed isClosed_Icc isClosed_le_prod
  have A : closure (Ioc a b) âŠ† s := by
    apply s_closed.closure_subset_iff.2
    intro t ht
    refine âŸ¨?_, âŸ¨ht.1.le, ht.2âŸ©âŸ©
    exact
      sub_le_integral_of_hasDeriv_right_of_le_Ico ht.2 (hcont.mono (Icc_subset_Icc ht.1.le le_rfl))
        (fun x hx => hderiv x âŸ¨ht.1.trans_le hx.1, hx.2âŸ©)
        (Ï†int.mono_set (Icc_subset_Icc ht.1.le le_rfl)) fun x hx => hÏ†g x âŸ¨ht.1.trans_le hx.1, hx.2âŸ©
  rw [closure_Ioc a_lt_b.ne] at A
  exact (A (left_mem_Icc.2 hab)).1
#align interval_integral.sub_le_integral_of_has_deriv_right_of_le intervalIntegral.sub_le_integral_of_hasDeriv_right_of_le

/-- Auxiliary lemma in the proof of `integral_eq_sub_of_hasDeriv_right_of_le`. -/
theorem integral_le_sub_of_hasDeriv_right_of_le (hab : a â‰¤ b) (hcont : ContinuousOn g (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivWithinAt g (g' x) (Ioi x) x) (Ï†int : IntegrableOn Ï† (Icc a b))
    (hÏ†g : âˆ€ x âˆˆ Ioo a b, Ï† x â‰¤ g' x) : (âˆ« y in a..b, Ï† y) â‰¤ g b - g a := by
  rw [â† neg_le_neg_iff]
  convert sub_le_integral_of_hasDeriv_right_of_le hab hcont.neg (fun x hx => (hderiv x hx).neg)
    Ï†int.neg fun x hx => neg_le_neg (hÏ†g x hx) using 1
  Â· abel
  Â· simp only [â† integral_neg]; rfl
#align interval_integral.integral_le_sub_of_has_deriv_right_of_le intervalIntegral.integral_le_sub_of_hasDeriv_right_of_le

/-- Auxiliary lemma in the proof of `integral_eq_sub_of_hasDeriv_right_of_le`: real version -/
theorem integral_eq_sub_of_hasDeriv_right_of_le_real (hab : a â‰¤ b)
    (hcont : ContinuousOn g (Icc a b)) (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivWithinAt g (g' x) (Ioi x) x)
    (g'int : IntegrableOn g' (Icc a b)) : âˆ« y in a..b, g' y = g b - g a :=
  le_antisymm (integral_le_sub_of_hasDeriv_right_of_le hab hcont hderiv g'int fun _ _ => le_rfl)
    (sub_le_integral_of_hasDeriv_right_of_le hab hcont hderiv g'int fun _ _ => le_rfl)
#align interval_integral.integral_eq_sub_of_has_deriv_right_of_le_real intervalIntegral.integral_eq_sub_of_hasDeriv_right_of_le_real

variable [CompleteSpace E] {f f' : â„ â†’ E}

/-- **Fundamental theorem of calculus-2**: If `f : â„ â†’ E` is continuous on `[a, b]` (where `a â‰¤ b`)
  and has a right derivative at `f' x` for all `x` in `(a, b)`, and `f'` is integrable on `[a, b]`,
  then `âˆ« y in a..b, f' y` equals `f b - f a`. -/
theorem integral_eq_sub_of_hasDeriv_right_of_le (hab : a â‰¤ b) (hcont : ContinuousOn f (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivWithinAt f (f' x) (Ioi x) x)
    (f'int : IntervalIntegrable f' volume a b) : âˆ« y in a..b, f' y = f b - f a := by
  refine (NormedSpace.eq_iff_forall_dual_eq â„).2 fun g => ?_
  rw [â† g.intervalIntegral_comp_comm f'int, g.map_sub]
  exact integral_eq_sub_of_hasDeriv_right_of_le_real hab (g.continuous.comp_continuousOn hcont)
    (fun x hx => g.hasFDerivAt.comp_hasDerivWithinAt x (hderiv x hx))
    (g.integrable_comp ((intervalIntegrable_iff_integrableOn_Icc_of_le hab).1 f'int))
#align interval_integral.integral_eq_sub_of_has_deriv_right_of_le intervalIntegral.integral_eq_sub_of_hasDeriv_right_of_le

/-- Fundamental theorem of calculus-2: If `f : â„ â†’ E` is continuous on `[a, b]` and
  has a right derivative at `f' x` for all `x` in `[a, b)`, and `f'` is integrable on `[a, b]` then
  `âˆ« y in a..b, f' y` equals `f b - f a`. -/
theorem integral_eq_sub_of_hasDeriv_right (hcont : ContinuousOn f (uIcc a b))
    (hderiv : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hint : IntervalIntegrable f' volume a b) : âˆ« y in a..b, f' y = f b - f a := by
  rcases le_total a b with hab | hab
  Â· simp only [uIcc_of_le, min_eq_left, max_eq_right, hab] at hcont hderiv hint
    apply integral_eq_sub_of_hasDeriv_right_of_le hab hcont hderiv hint
  Â· simp only [uIcc_of_ge, min_eq_right, max_eq_left, hab] at hcont hderiv
    rw [integral_symm, integral_eq_sub_of_hasDeriv_right_of_le hab hcont hderiv hint.symm, neg_sub]
#align interval_integral.integral_eq_sub_of_has_deriv_right intervalIntegral.integral_eq_sub_of_hasDeriv_right

/-- Fundamental theorem of calculus-2: If `f : â„ â†’ E` is continuous on `[a, b]` (where `a â‰¤ b`) and
  has a derivative at `f' x` for all `x` in `(a, b)`, and `f'` is integrable on `[a, b]`, then
  `âˆ« y in a..b, f' y` equals `f b - f a`. -/
theorem integral_eq_sub_of_hasDerivAt_of_le (hab : a â‰¤ b) (hcont : ContinuousOn f (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x) (hint : IntervalIntegrable f' volume a b) :
    âˆ« y in a..b, f' y = f b - f a :=
  integral_eq_sub_of_hasDeriv_right_of_le hab hcont (fun x hx => (hderiv x hx).hasDerivWithinAt)
    hint
#align interval_integral.integral_eq_sub_of_has_deriv_at_of_le intervalIntegral.integral_eq_sub_of_hasDerivAt_of_le

/-- Fundamental theorem of calculus-2: If `f : â„ â†’ E` has a derivative at `f' x` for all `x` in
  `[a, b]` and `f'` is integrable on `[a, b]`, then `âˆ« y in a..b, f' y` equals `f b - f a`. -/
theorem integral_eq_sub_of_hasDerivAt (hderiv : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x)
    (hint : IntervalIntegrable f' volume a b) : âˆ« y in a..b, f' y = f b - f a :=
  integral_eq_sub_of_hasDeriv_right (HasDerivAt.continuousOn hderiv)
    (fun _x hx => (hderiv _ (mem_Icc_of_Ioo hx)).hasDerivWithinAt) hint
#align interval_integral.integral_eq_sub_of_has_deriv_at intervalIntegral.integral_eq_sub_of_hasDerivAt

theorem integral_eq_sub_of_hasDerivAt_of_tendsto (hab : a < b) {fa fb}
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x) (hint : IntervalIntegrable f' volume a b)
    (ha : Tendsto f (ğ“[>] a) (ğ“ fa)) (hb : Tendsto f (ğ“[<] b) (ğ“ fb)) :
    âˆ« y in a..b, f' y = fb - fa := by
  set F : â„ â†’ E := update (update f a fa) b fb
  have Fderiv : âˆ€ x âˆˆ Ioo a b, HasDerivAt F (f' x) x := by
    refine fun x hx => (hderiv x hx).congr_of_eventuallyEq ?_
    filter_upwards [Ioo_mem_nhds hx.1 hx.2] with _ hy
    unfold_let F
    rw [update_noteq hy.2.ne, update_noteq hy.1.ne']
  have hcont : ContinuousOn F (Icc a b) := by
    rw [continuousOn_update_iff, continuousOn_update_iff, Icc_diff_right, Ico_diff_left]
    refine âŸ¨âŸ¨fun z hz => (hderiv z hz).continuousAt.continuousWithinAt, ?_âŸ©, ?_âŸ©
    Â· exact fun _ => ha.mono_left (nhdsWithin_mono _ Ioo_subset_Ioi_self)
    Â· rintro -
      refine (hb.congr' ?_).mono_left (nhdsWithin_mono _ Ico_subset_Iio_self)
      filter_upwards [Ioo_mem_nhdsWithin_Iio (right_mem_Ioc.2 hab)] with _ hz using
        (update_noteq hz.1.ne' _ _).symm
  simpa [F, hab.ne, hab.ne'] using integral_eq_sub_of_hasDerivAt_of_le hab.le hcont Fderiv hint
#align interval_integral.integral_eq_sub_of_has_deriv_at_of_tendsto intervalIntegral.integral_eq_sub_of_hasDerivAt_of_tendsto

/-- Fundamental theorem of calculus-2: If `f : â„ â†’ E` is differentiable at every `x` in `[a, b]` and
  its derivative is integrable on `[a, b]`, then `âˆ« y in a..b, deriv f y` equals `f b - f a`. -/
theorem integral_deriv_eq_sub (hderiv : âˆ€ x âˆˆ [[a, b]], DifferentiableAt â„ f x)
    (hint : IntervalIntegrable (deriv f) volume a b) : âˆ« y in a..b, deriv f y = f b - f a :=
  integral_eq_sub_of_hasDerivAt (fun x hx => (hderiv x hx).hasDerivAt) hint
#align interval_integral.integral_deriv_eq_sub intervalIntegral.integral_deriv_eq_sub

theorem integral_deriv_eq_sub' (f) (hderiv : deriv f = f')
    (hdiff : âˆ€ x âˆˆ uIcc a b, DifferentiableAt â„ f x) (hcont : ContinuousOn f' (uIcc a b)) :
    âˆ« y in a..b, f' y = f b - f a := by
  rw [â† hderiv, integral_deriv_eq_sub hdiff]
  rw [hderiv]
  exact hcont.intervalIntegrable
#align interval_integral.integral_deriv_eq_sub' intervalIntegral.integral_deriv_eq_sub'

/-- A variant of `intervalIntegral.integral_deriv_eq_sub`, the Fundamental theorem
of calculus, involving integrating over the unit interval. -/
lemma integral_unitInterval_deriv_eq_sub [RCLike ğ•œ] [NormedSpace ğ•œ E] [IsScalarTower â„ ğ•œ E]
    {f f' : ğ•œ â†’ E} {zâ‚€ zâ‚ : ğ•œ}
    (hcont : ContinuousOn (fun t : â„ â†¦ f' (zâ‚€ + t â€¢ zâ‚)) (Set.Icc 0 1))
    (hderiv : âˆ€ t âˆˆ Set.Icc (0 : â„) 1, HasDerivAt f (f' (zâ‚€ + t â€¢ zâ‚)) (zâ‚€ + t â€¢ zâ‚)) :
    zâ‚ â€¢ âˆ« t in (0 : â„)..1, f' (zâ‚€ + t â€¢ zâ‚) = f (zâ‚€ + zâ‚) - f zâ‚€ := by
  let Î³ (t : â„) : ğ•œ := zâ‚€ + t â€¢ zâ‚
  have hint : IntervalIntegrable (zâ‚ â€¢ (f' âˆ˜ Î³)) MeasureTheory.volume 0 1 :=
    (ContinuousOn.const_smul hcont zâ‚).intervalIntegrable_of_Icc zero_le_one
  have hderiv' : âˆ€ t âˆˆ Set.uIcc (0 : â„) 1, HasDerivAt (f âˆ˜ Î³) (zâ‚ â€¢ (f' âˆ˜ Î³) t) t := by
    intro t ht
    refine (hderiv t <| (Set.uIcc_of_le (Î± := â„) zero_le_one).symm â–¸ ht).scomp t ?_
    have : HasDerivAt (fun t : â„ â†¦ t â€¢ zâ‚) zâ‚ t := by
      convert (hasDerivAt_id t).smul_const (F := ğ•œ) _ using 1
      simp only [one_smul]
    exact this.const_add zâ‚€
  convert (integral_eq_sub_of_hasDerivAt hderiv' hint) using 1
  Â· simp_rw [â† integral_smul, Function.comp_apply]
  Â· simp only [Î³, Function.comp_apply, one_smul, zero_smul, add_zero]

/-!
### Automatic integrability for nonnegative derivatives
-/


/-- When the right derivative of a function is nonnegative, then it is automatically integrable. -/
theorem integrableOn_deriv_right_of_nonneg (hcont : ContinuousOn g (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivWithinAt g (g' x) (Ioi x) x)
    (g'pos : âˆ€ x âˆˆ Ioo a b, 0 â‰¤ g' x) : IntegrableOn g' (Ioc a b) := by
  by_cases hab : a < b; swap
  Â· simp [Ioc_eq_empty hab]
  rw [integrableOn_Ioc_iff_integrableOn_Ioo]
  have meas_g' : AEMeasurable g' (volume.restrict (Ioo a b)) := by
    apply (aemeasurable_derivWithin_Ioi g _).congr
    refine (ae_restrict_mem measurableSet_Ioo).mono fun x hx => ?_
    exact (hderiv x hx).derivWithin (uniqueDiffWithinAt_Ioi _)
  suffices H : (âˆ«â» x in Ioo a b, â€–g' xâ€–â‚Š) â‰¤ ENNReal.ofReal (g b - g a) from
    âŸ¨meas_g'.aestronglyMeasurable, H.trans_lt ENNReal.ofReal_lt_topâŸ©
  by_contra! H
  obtain âŸ¨f, fle, fint, hfâŸ© :
    âˆƒ f : SimpleFunc â„ â„â‰¥0,
      (âˆ€ x, f x â‰¤ â€–g' xâ€–â‚Š) âˆ§
        (âˆ«â» x : â„ in Ioo a b, f x) < âˆ âˆ§ ENNReal.ofReal (g b - g a) < âˆ«â» x : â„ in Ioo a b, f x :=
    exists_lt_lintegral_simpleFunc_of_lt_lintegral H
  let F : â„ â†’ â„ := (â†‘) âˆ˜ f
  have intF : IntegrableOn F (Ioo a b) := by
    refine âŸ¨f.measurable.coe_nnreal_real.aestronglyMeasurable, ?_âŸ©
    simpa only [F, HasFiniteIntegral, comp_apply, NNReal.nnnorm_eq] using fint
  have A : âˆ«â» x : â„ in Ioo a b, f x = ENNReal.ofReal (âˆ« x in Ioo a b, F x) :=
    lintegral_coe_eq_integral _ intF
  rw [A] at hf
  have B : (âˆ« x : â„ in Ioo a b, F x) â‰¤ g b - g a := by
    rw [â† integral_Ioc_eq_integral_Ioo, â† intervalIntegral.integral_of_le hab.le]
    refine integral_le_sub_of_hasDeriv_right_of_le hab.le hcont hderiv ?_ fun x hx => ?_
    Â· rwa [integrableOn_Icc_iff_integrableOn_Ioo]
    Â· convert NNReal.coe_le_coe.2 (fle x)
      simp only [Real.norm_of_nonneg (g'pos x hx), coe_nnnorm]
  exact lt_irrefl _ (hf.trans_le (ENNReal.ofReal_le_ofReal B))
#align interval_integral.integrable_on_deriv_right_of_nonneg intervalIntegral.integrableOn_deriv_right_of_nonneg

/-- When the derivative of a function is nonnegative, then it is automatically integrable,
Ioc version. -/
theorem integrableOn_deriv_of_nonneg (hcont : ContinuousOn g (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x) (g'pos : âˆ€ x âˆˆ Ioo a b, 0 â‰¤ g' x) :
    IntegrableOn g' (Ioc a b) :=
  integrableOn_deriv_right_of_nonneg hcont (fun x hx => (hderiv x hx).hasDerivWithinAt) g'pos
#align interval_integral.integrable_on_deriv_of_nonneg intervalIntegral.integrableOn_deriv_of_nonneg

/-- When the derivative of a function is nonnegative, then it is automatically integrable,
interval version. -/
theorem intervalIntegrable_deriv_of_nonneg (hcont : ContinuousOn g (uIcc a b))
    (hderiv : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivAt g (g' x) x)
    (hpos : âˆ€ x âˆˆ Ioo (min a b) (max a b), 0 â‰¤ g' x) : IntervalIntegrable g' volume a b := by
  rcases le_total a b with hab | hab
  Â· simp only [uIcc_of_le, min_eq_left, max_eq_right, hab, IntervalIntegrable, hab,
      Ioc_eq_empty_of_le, integrableOn_empty, and_true_iff] at hcont hderiv hpos âŠ¢
    exact integrableOn_deriv_of_nonneg hcont hderiv hpos
  Â· simp only [uIcc_of_ge, min_eq_right, max_eq_left, hab, IntervalIntegrable, Ioc_eq_empty_of_le,
      integrableOn_empty, true_and_iff] at hcont hderiv hpos âŠ¢
    exact integrableOn_deriv_of_nonneg hcont hderiv hpos
#align interval_integral.interval_integrable_deriv_of_nonneg intervalIntegral.intervalIntegrable_deriv_of_nonneg

/-!
### Integration by parts
-/


section Parts

variable [NormedRing A] [NormedAlgebra â„ A] [CompleteSpace A]

/-- The integral of the derivative of a product of two maps.
For improper integrals, see `MeasureTheory.integral_deriv_mul_eq_sub`,
`MeasureTheory.integral_Ioi_deriv_mul_eq_sub`, and `MeasureTheory.integral_Iic_deriv_mul_eq_sub`. -/
theorem integral_deriv_mul_eq_sub_of_hasDeriv_right {u v u' v' : â„ â†’ A}
    (hu : ContinuousOn u [[a, b]])
    (hv : ContinuousOn v [[a, b]])
    (huu' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt u (u' x) (Ioi x) x)
    (hvv' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt v (v' x) (Ioi x) x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u' x * v x + u x * v' x = u b * v b - u a * v a := by
  apply integral_eq_sub_of_hasDeriv_right (hu.mul hv) fun x hx â†¦ (huu' x hx).mul (hvv' x hx)
  exact (hu'.mul_continuousOn hv).add (hv'.continuousOn_mul hu)

/-- The integral of the derivative of a product of two maps.
Special case of `integral_deriv_mul_eq_sub_of_hasDeriv_right` where the functions have a
two-sided derivative in the interior of the interval. -/
theorem integral_deriv_mul_eq_sub_of_hasDerivAt {u v u' v' : â„ â†’ A}
    (hu : ContinuousOn u [[a, b]])
    (hv : ContinuousOn v [[a, b]])
    (huu' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivAt u (u' x) x)
    (hvv' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivAt v (v' x) x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u' x * v x + u x * v' x = u b * v b - u a * v a :=
  integral_deriv_mul_eq_sub_of_hasDeriv_right hu hv
    (fun x hx â†¦ huu' x hx |>.hasDerivWithinAt) (fun x hx â†¦ hvv' x hx |>.hasDerivWithinAt) hu' hv'

/-- The integral of the derivative of a product of two maps.
Special case of `integral_deriv_mul_eq_sub_of_hasDeriv_right` where the functions have a
  one-sided derivative at the endpoints. -/
theorem integral_deriv_mul_eq_sub_of_hasDerivWithinAt {u v u' v' : â„ â†’ A}
    (hu : âˆ€ x âˆˆ [[a, b]], HasDerivWithinAt u (u' x) [[a, b]] x)
    (hv : âˆ€ x âˆˆ [[a, b]], HasDerivWithinAt v (v' x) [[a, b]] x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u' x * v x + u x * v' x = u b * v b - u a * v a :=
  integral_deriv_mul_eq_sub_of_hasDerivAt
    (fun x hx â†¦ (hu x hx).continuousWithinAt)
    (fun x hx â†¦ (hv x hx).continuousWithinAt)
    (fun x hx â†¦ hu x (mem_Icc_of_Ioo hx) |>.hasDerivAt (Icc_mem_nhds hx.1 hx.2))
    (fun x hx â†¦ hv x (mem_Icc_of_Ioo hx) |>.hasDerivAt (Icc_mem_nhds hx.1 hx.2))
    hu' hv'

/-- Special case of `integral_deriv_mul_eq_sub_of_hasDeriv_right` where the functions have a
  derivative at the endpoints. -/
theorem integral_deriv_mul_eq_sub {u v u' v' : â„ â†’ A}
    (hu : âˆ€ x âˆˆ [[a, b]], HasDerivAt u (u' x) x)
    (hv : âˆ€ x âˆˆ [[a, b]], HasDerivAt v (v' x) x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u' x * v x + u x * v' x = u b * v b - u a * v a :=
  integral_deriv_mul_eq_sub_of_hasDerivWithinAt
    (fun x hx â†¦ hu x hx |>.hasDerivWithinAt) (fun x hx â†¦ hv x hx |>.hasDerivWithinAt) hu' hv'
#align interval_integral.integral_deriv_mul_eq_sub intervalIntegral.integral_deriv_mul_eq_sub

/-- **Integration by parts**. For improper integrals, see
`MeasureTheory.integral_mul_deriv_eq_deriv_mul`,
`MeasureTheory.integral_Ioi_mul_deriv_eq_deriv_mul`,
and `MeasureTheory.integral_Iic_mul_deriv_eq_deriv_mul`. -/
theorem integral_mul_deriv_eq_deriv_mul_of_hasDeriv_right {u v u' v' : â„ â†’ A}
    (hu : ContinuousOn u [[a, b]])
    (hv : ContinuousOn v [[a, b]])
    (huu' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt u (u' x) (Ioi x) x)
    (hvv' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt v (v' x) (Ioi x) x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u x * v' x = u b * v b - u a * v a - âˆ« x in a..b, u' x * v x := by
  rw [â† integral_deriv_mul_eq_sub_of_hasDeriv_right hu hv huu' hvv' hu' hv', â† integral_sub]
  Â· simp_rw [add_sub_cancel_left]
  Â· exact (hu'.mul_continuousOn hv).add (hv'.continuousOn_mul hu)
  Â· exact hu'.mul_continuousOn hv

/-- **Integration by parts**. Special case of `integral_mul_deriv_eq_deriv_mul_of_hasDeriv_right`
where the functions have a two-sided derivative in the interior of the interval. -/
theorem integral_mul_deriv_eq_deriv_mul_of_hasDerivAt {u v u' v' : â„ â†’ A}
    (hu : ContinuousOn u [[a, b]])
    (hv : ContinuousOn v [[a, b]])
    (huu' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivAt u (u' x) x)
    (hvv' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivAt v (v' x) x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u x * v' x = u b * v b - u a * v a - âˆ« x in a..b, u' x * v x :=
  integral_mul_deriv_eq_deriv_mul_of_hasDeriv_right hu hv
        (fun x hx â†¦ (huu' x hx).hasDerivWithinAt) (fun x hx â†¦ (hvv' x hx).hasDerivWithinAt) hu' hv'

/-- **Integration by parts**. Special case of
`intervalIntegrable.integral_mul_deriv_eq_deriv_mul_of_hasDeriv_right`
where the functions have a one-sided derivative at the endpoints. -/
theorem integral_mul_deriv_eq_deriv_mul_of_hasDerivWithinAt {u v u' v' : â„ â†’ A}
    (hu : âˆ€ x âˆˆ [[a, b]], HasDerivWithinAt u (u' x) [[a, b]] x)
    (hv : âˆ€ x âˆˆ [[a, b]], HasDerivWithinAt v (v' x) [[a, b]] x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u x * v' x = u b * v b - u a * v a - âˆ« x in a..b, u' x * v x :=
  integral_mul_deriv_eq_deriv_mul_of_hasDerivAt
    (fun x hx â†¦ (hu x hx).continuousWithinAt)
    (fun x hx â†¦ (hv x hx).continuousWithinAt)
    (fun x hx â†¦ hu x (mem_Icc_of_Ioo hx) |>.hasDerivAt (Icc_mem_nhds hx.1 hx.2))
    (fun x hx â†¦ hv x (mem_Icc_of_Ioo hx) |>.hasDerivAt (Icc_mem_nhds hx.1 hx.2))
    hu' hv'

/-- **Integration by parts**. Special case of
`intervalIntegrable.integral_mul_deriv_eq_deriv_mul_of_hasDeriv_right`
where the functions have a derivative also at the endpoints.
For improper integrals, see
`MeasureTheory.integral_mul_deriv_eq_deriv_mul`,
`MeasureTheory.integral_Ioi_mul_deriv_eq_deriv_mul`,
and `MeasureTheory.integral_Iic_mul_deriv_eq_deriv_mul`. -/
theorem integral_mul_deriv_eq_deriv_mul {u v u' v' : â„ â†’ A}
    (hu : âˆ€ x âˆˆ [[a, b]], HasDerivAt u (u' x) x)
    (hv : âˆ€ x âˆˆ [[a, b]], HasDerivAt v (v' x) x)
    (hu' : IntervalIntegrable u' volume a b)
    (hv' : IntervalIntegrable v' volume a b) :
    âˆ« x in a..b, u x * v' x = u b * v b - u a * v a - âˆ« x in a..b, u' x * v x :=
  integral_mul_deriv_eq_deriv_mul_of_hasDerivWithinAt
    (fun x hx â†¦ (hu x hx).hasDerivWithinAt) (fun x hx â†¦ (hv x hx).hasDerivWithinAt) hu' hv'
#align interval_integral.integral_mul_deriv_eq_deriv_mul intervalIntegral.integral_mul_deriv_eq_deriv_mul

end Parts

/-!
### Integration by substitution / Change of variables
-/

section SMul

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G]

/-- Change of variables, general form. If `f` is continuous on `[a, b]` and has
right-derivative `f'` in `(a, b)`, `g` is continuous on `f '' (a, b)` and integrable on
`f '' [a, b]`, and `f' x â€¢ (g âˆ˜ f) x` is integrable on `[a, b]`,
then we can substitute `u = f x` to get `âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x = âˆ« u in f a..f b, g u`.
-/
theorem integral_comp_smul_deriv''' {f f' : â„ â†’ â„} {g : â„ â†’ G} (hf : ContinuousOn f [[a, b]])
    (hff' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hg_cont : ContinuousOn g (f '' Ioo (min a b) (max a b))) (hg1 : IntegrableOn g (f '' [[a, b]]))
    (hg2 : IntegrableOn (fun x => f' x â€¢ (g âˆ˜ f) x) [[a, b]]) :
    (âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x) = âˆ« u in f a..f b, g u := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [intervalIntegral, integral, hG]
  rw [hf.image_uIcc, â† intervalIntegrable_iff'] at hg1
  have h_cont : ContinuousOn (fun u => âˆ« t in f a..f u, g t) [[a, b]] := by
    refine (continuousOn_primitive_interval' hg1 ?_).comp hf ?_
    Â· rw [â† hf.image_uIcc]; exact mem_image_of_mem f left_mem_uIcc
    Â· rw [â† hf.image_uIcc]; exact mapsTo_image _ _
  have h_der :
    âˆ€ x âˆˆ Ioo (min a b) (max a b),
      HasDerivWithinAt (fun u => âˆ« t in f a..f u, g t) (f' x â€¢ (g âˆ˜ f) x) (Ioi x) x := by
    intro x hx
    obtain âŸ¨c, hcâŸ© := nonempty_Ioo.mpr hx.1
    obtain âŸ¨d, hdâŸ© := nonempty_Ioo.mpr hx.2
    have cdsub : [[c, d]] âŠ† Ioo (min a b) (max a b) := by
      rw [uIcc_of_le (hc.2.trans hd.1).le]
      exact Icc_subset_Ioo hc.1 hd.2
    replace hg_cont := hg_cont.mono (image_subset f cdsub)
    let J := [[sInf (f '' [[c, d]]), sSup (f '' [[c, d]])]]
    have hJ : f '' [[c, d]] = J := (hf.mono (cdsub.trans Ioo_subset_Icc_self)).image_uIcc
    rw [hJ] at hg_cont
    have h2x : f x âˆˆ J := by rw [â† hJ]; exact mem_image_of_mem _ (mem_uIcc_of_le hc.2.le hd.1.le)
    have h2g : IntervalIntegrable g volume (f a) (f x) := by
      refine hg1.mono_set ?_
      rw [â† hf.image_uIcc]
      exact hf.surjOn_uIcc left_mem_uIcc (Ioo_subset_Icc_self hx)
    have h3g : StronglyMeasurableAtFilter g (ğ“[J] f x) :=
      hg_cont.stronglyMeasurableAtFilter_nhdsWithin measurableSet_Icc (f x)
    haveI : Fact (f x âˆˆ J) := âŸ¨h2xâŸ©
    have : HasDerivWithinAt (fun u => âˆ« x in f a..u, g x) (g (f x)) J (f x) :=
      intervalIntegral.integral_hasDerivWithinAt_right h2g h3g (hg_cont (f x) h2x)
    refine (this.scomp x ((hff' x hx).Ioo_of_Ioi hd.1) ?_).Ioi_of_Ioo hd.1
    rw [â† hJ]
    refine (mapsTo_image _ _).mono ?_ Subset.rfl
    exact Ioo_subset_Icc_self.trans ((Icc_subset_Icc_left hc.2.le).trans Icc_subset_uIcc)
  rw [â† intervalIntegrable_iff'] at hg2
  simp_rw [integral_eq_sub_of_hasDeriv_right h_cont h_der hg2, integral_same, sub_zero]
#align interval_integral.integral_comp_smul_deriv''' intervalIntegral.integral_comp_smul_deriv'''

/-- Change of variables for continuous integrands. If `f` is continuous on `[a, b]` and has
continuous right-derivative `f'` in `(a, b)`, and `g` is continuous on `f '' [a, b]` then we can
substitute `u = f x` to get `âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x = âˆ« u in f a..f b, g u`.
-/
theorem integral_comp_smul_deriv'' {f f' : â„ â†’ â„} {g : â„ â†’ G} (hf : ContinuousOn f [[a, b]])
    (hff' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hf' : ContinuousOn f' [[a, b]]) (hg : ContinuousOn g (f '' [[a, b]])) :
    (âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x) = âˆ« u in f a..f b, g u := by
  refine
    integral_comp_smul_deriv''' hf hff' (hg.mono <| image_subset _ Ioo_subset_Icc_self) ?_
      (hf'.smul (hg.comp hf <| subset_preimage_image f _)).integrableOn_Icc
  rw [hf.image_uIcc] at hg âŠ¢
  exact hg.integrableOn_Icc
#align interval_integral.integral_comp_smul_deriv'' intervalIntegral.integral_comp_smul_deriv''

/-- Change of variables. If `f` has continuous derivative `f'` on `[a, b]`,
and `g` is continuous on `f '' [a, b]`, then we can substitute `u = f x` to get
`âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x = âˆ« u in f a..f b, g u`.
Compared to `intervalIntegral.integral_comp_smul_deriv` we only require that `g` is continuous on
`f '' [a, b]`.
-/
theorem integral_comp_smul_deriv' {f f' : â„ â†’ â„} {g : â„ â†’ G}
    (h : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x) (h' : ContinuousOn f' (uIcc a b))
    (hg : ContinuousOn g (f '' [[a, b]])) :
    (âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x) = âˆ« x in f a..f b, g x :=
  integral_comp_smul_deriv'' (fun x hx => (h x hx).continuousAt.continuousWithinAt)
    (fun x hx => (h x <| Ioo_subset_Icc_self hx).hasDerivWithinAt) h' hg
#align interval_integral.integral_comp_smul_deriv' intervalIntegral.integral_comp_smul_deriv'

/-- Change of variables, most common version. If `f` has continuous derivative `f'` on `[a, b]`,
and `g` is continuous, then we can substitute `u = f x` to get
`âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x = âˆ« u in f a..f b, g u`.
-/
theorem integral_comp_smul_deriv {f f' : â„ â†’ â„} {g : â„ â†’ G}
    (h : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x) (h' : ContinuousOn f' (uIcc a b))
    (hg : Continuous g) : (âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x) = âˆ« x in f a..f b, g x :=
  integral_comp_smul_deriv' h h' hg.continuousOn
#align interval_integral.integral_comp_smul_deriv intervalIntegral.integral_comp_smul_deriv

theorem integral_deriv_comp_smul_deriv' {f f' : â„ â†’ â„} {g g' : â„ â†’ E} (hf : ContinuousOn f [[a, b]])
    (hff' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hf' : ContinuousOn f' [[a, b]]) (hg : ContinuousOn g [[f a, f b]])
    (hgg' : âˆ€ x âˆˆ Ioo (min (f a) (f b)) (max (f a) (f b)), HasDerivWithinAt g (g' x) (Ioi x) x)
    (hg' : ContinuousOn g' (f '' [[a, b]])) :
    (âˆ« x in a..b, f' x â€¢ (g' âˆ˜ f) x) = (g âˆ˜ f) b - (g âˆ˜ f) a := by
  rw [integral_comp_smul_deriv'' hf hff' hf' hg',
    integral_eq_sub_of_hasDeriv_right hg hgg' (hg'.mono _).intervalIntegrable]
  exacts [rfl, intermediate_value_uIcc hf]
#align interval_integral.integral_deriv_comp_smul_deriv' intervalIntegral.integral_deriv_comp_smul_deriv'

theorem integral_deriv_comp_smul_deriv {f f' : â„ â†’ â„} {g g' : â„ â†’ E}
    (hf : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x)
    (hg : âˆ€ x âˆˆ uIcc a b, HasDerivAt g (g' (f x)) (f x)) (hf' : ContinuousOn f' (uIcc a b))
    (hg' : Continuous g') : (âˆ« x in a..b, f' x â€¢ (g' âˆ˜ f) x) = (g âˆ˜ f) b - (g âˆ˜ f) a :=
  integral_eq_sub_of_hasDerivAt (fun x hx => (hg x hx).scomp x <| hf x hx)
    (hf'.smul (hg'.comp_continuousOn <| HasDerivAt.continuousOn hf)).intervalIntegrable
#align interval_integral.integral_deriv_comp_smul_deriv intervalIntegral.integral_deriv_comp_smul_deriv

end SMul

section Mul

/-- Change of variables, general form for scalar functions. If `f` is continuous on `[a, b]` and has
continuous right-derivative `f'` in `(a, b)`, `g` is continuous on `f '' (a, b)` and integrable on
`f '' [a, b]`, and `(g âˆ˜ f) x * f' x` is integrable on `[a, b]`, then we can substitute `u = f x`
to get `âˆ« x in a..b, (g âˆ˜ f) x * f' x = âˆ« u in f a..f b, g u`.
-/
theorem integral_comp_mul_deriv''' {a b : â„} {f f' : â„ â†’ â„} {g : â„ â†’ â„}
    (hf : ContinuousOn f [[a, b]])
    (hff' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hg_cont : ContinuousOn g (f '' Ioo (min a b) (max a b))) (hg1 : IntegrableOn g (f '' [[a, b]]))
    (hg2 : IntegrableOn (fun x => (g âˆ˜ f) x * f' x) [[a, b]]) :
    (âˆ« x in a..b, (g âˆ˜ f) x * f' x) = âˆ« u in f a..f b, g u := by
  have hg2' : IntegrableOn (fun x => f' x â€¢ (g âˆ˜ f) x) [[a, b]] := by simpa [mul_comm] using hg2
  simpa [mul_comm] using integral_comp_smul_deriv''' hf hff' hg_cont hg1 hg2'
#align interval_integral.integral_comp_mul_deriv''' intervalIntegral.integral_comp_mul_deriv'''

/-- Change of variables for continuous integrands. If `f` is continuous on `[a, b]` and has
continuous right-derivative `f'` in `(a, b)`, and `g` is continuous on `f '' [a, b]` then we can
substitute `u = f x` to get `âˆ« x in a..b, (g âˆ˜ f) x * f' x = âˆ« u in f a..f b, g u`.
-/
theorem integral_comp_mul_deriv'' {f f' g : â„ â†’ â„} (hf : ContinuousOn f [[a, b]])
    (hff' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hf' : ContinuousOn f' [[a, b]]) (hg : ContinuousOn g (f '' [[a, b]])) :
    (âˆ« x in a..b, (g âˆ˜ f) x * f' x) = âˆ« u in f a..f b, g u := by
  simpa [mul_comm] using integral_comp_smul_deriv'' hf hff' hf' hg
#align interval_integral.integral_comp_mul_deriv'' intervalIntegral.integral_comp_mul_deriv''

/-- Change of variables. If `f` has continuous derivative `f'` on `[a, b]`,
and `g` is continuous on `f '' [a, b]`, then we can substitute `u = f x` to get
`âˆ« x in a..b, (g âˆ˜ f) x * f' x = âˆ« u in f a..f b, g u`.
Compared to `intervalIntegral.integral_comp_mul_deriv` we only require that `g` is continuous on
`f '' [a, b]`.
-/
theorem integral_comp_mul_deriv' {f f' g : â„ â†’ â„} (h : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x)
    (h' : ContinuousOn f' (uIcc a b)) (hg : ContinuousOn g (f '' [[a, b]])) :
    (âˆ« x in a..b, (g âˆ˜ f) x * f' x) = âˆ« x in f a..f b, g x := by
  simpa [mul_comm] using integral_comp_smul_deriv' h h' hg
#align interval_integral.integral_comp_mul_deriv' intervalIntegral.integral_comp_mul_deriv'

/-- Change of variables, most common version. If `f` has continuous derivative `f'` on `[a, b]`,
and `g` is continuous, then we can substitute `u = f x` to get
`âˆ« x in a..b, (g âˆ˜ f) x * f' x = âˆ« u in f a..f b, g u`.
-/
theorem integral_comp_mul_deriv {f f' g : â„ â†’ â„} (h : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x)
    (h' : ContinuousOn f' (uIcc a b)) (hg : Continuous g) :
    (âˆ« x in a..b, (g âˆ˜ f) x * f' x) = âˆ« x in f a..f b, g x :=
  integral_comp_mul_deriv' h h' hg.continuousOn
#align interval_integral.integral_comp_mul_deriv intervalIntegral.integral_comp_mul_deriv

theorem integral_deriv_comp_mul_deriv' {f f' g g' : â„ â†’ â„} (hf : ContinuousOn f [[a, b]])
    (hff' : âˆ€ x âˆˆ Ioo (min a b) (max a b), HasDerivWithinAt f (f' x) (Ioi x) x)
    (hf' : ContinuousOn f' [[a, b]]) (hg : ContinuousOn g [[f a, f b]])
    (hgg' : âˆ€ x âˆˆ Ioo (min (f a) (f b)) (max (f a) (f b)), HasDerivWithinAt g (g' x) (Ioi x) x)
    (hg' : ContinuousOn g' (f '' [[a, b]])) :
    (âˆ« x in a..b, (g' âˆ˜ f) x * f' x) = (g âˆ˜ f) b - (g âˆ˜ f) a := by
  simpa [mul_comm] using integral_deriv_comp_smul_deriv' hf hff' hf' hg hgg' hg'
#align interval_integral.integral_deriv_comp_mul_deriv' intervalIntegral.integral_deriv_comp_mul_deriv'

theorem integral_deriv_comp_mul_deriv {f f' g g' : â„ â†’ â„}
    (hf : âˆ€ x âˆˆ uIcc a b, HasDerivAt f (f' x) x)
    (hg : âˆ€ x âˆˆ uIcc a b, HasDerivAt g (g' (f x)) (f x)) (hf' : ContinuousOn f' (uIcc a b))
    (hg' : Continuous g') : (âˆ« x in a..b, (g' âˆ˜ f) x * f' x) = (g âˆ˜ f) b - (g âˆ˜ f) a := by
  simpa [mul_comm] using integral_deriv_comp_smul_deriv hf hg hf' hg'
#align interval_integral.integral_deriv_comp_mul_deriv intervalIntegral.integral_deriv_comp_mul_deriv

end Mul

end intervalIntegral
