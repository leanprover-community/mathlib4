/-
Copyright (c) 2025 Yizheng Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yizheng Zhu
-/
import Mathlib.Analysis.BoundedVariation
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.MeasureTheory.Covering.Vitali
import Mathlib.MeasureTheory.Function.AbsolutelyContinuous
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
import Mathlib.MeasureTheory.Integral.IntervalIntegral.LebesgueDifferetiationThm
import Mathlib.MeasureTheory.Integral.Lebesgue.Basic
import Mathlib.MeasureTheory.Measure.MeasureSpace

/-!
# Fundamental Theorem of Calculus and Integration by Parts for Absolutely Continuous Functions

This file proves that:
* If `f` is absolutely continuous on `uIcc a b`, then `f` is a.e. differentiable on `uIcc a b`,
`f'` is interval integrable on `a..b`, and that *Fundamental Theorem of Calculus* holds for `f'` on
`a..b`, i.e. `âˆ« (x : â„) in a..b, deriv f x = f b - f a`.
* *Integration by Parts* holds for absolutely continuous functions.

## Implementation notes

We need to prove that `f'` is interval integrable on `a..b` for any monotone function `f`. The
proof uses Fatou's lemma. From this we get `f'` is interval integrable on `a..b` for BV functions
and finally for absolutely continuous functions.

## Tags
absolutely continuous, fundamental theorem of calculus, integration by parts
-/

open MeasureTheory Set Filter Function

open scoped Topology ENNReal Interval NNReal

/-- If `f z` tends to `y` as `z` tends to `x`, then `f (x + (n : â„)â»Â¹)` tends to `y` as `n` tends
to infinity. -/
theorem Filter.Tendsto.inverse_atTop {f : â„ â†’ â„} {x y : â„}
    (h : Filter.Tendsto f (ğ“[â‰ ] x) (ğ“ y)) :
    Filter.Tendsto (fun n : â„• â†¦ f (x + (n : â„)â»Â¹)) Filter.atTop (ğ“ y) := by
  apply Filter.Tendsto.comp h
  apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within
  Â· nth_rw 2 [show x = x + 0 by simp]
    apply tendsto_const_nhds.add
    exact tendsto_inverse_atTop_nhds_zero_nat
  Â· have : âˆ€á¶  (n : â„•) in atTop, n â‰  0 := by
      simp only [Filter.Eventually, mem_atTop_sets, mem_setOf_eq]
      use 1; intro n hn; omega
    filter_upwards [this] with n hn
    simp only [mem_compl_iff, mem_singleton_iff, add_eq_left, inv_eq_zero]
    norm_cast

/-- If `f` is monotone on `uIcc a b`, then `f'` is interval integrable on `a..b`. -/
theorem MonotoneOn.deriv_intervalIntegrable {f : â„ â†’ â„} {a b : â„} (hf : MonotoneOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b := by
  wlog hab : a â‰¤ b
  Â· have S : uIcc a b = uIcc b a := by exact uIcc_comm a b
    exact @this f b a (S â–¸ hf) (by linarith) |>.symm
  simp_rw [uIcc, show min a b = a by simp [hab], show max a b = b by simp [hab] ] at hf
  let g (x : â„) : â„ := if x <= a then f a else if x < b then f x else f b
  have hg : Monotone g := by
    intro x y hxy
    dsimp only [g]
    split_ifs <;> try linarith
    all_goals apply hf
    all_goals try rw [mem_Icc]
    all_goals try constructor
    all_goals try linarith
  have hgc (c : â„) : Monotone (fun x â†¦ g (x + c)) := by
    intro x y hxy; beta_reduce; apply hg; linarith
  have h1 : âˆ€áµ x, x â‰  a := by simp [ae_iff, measure_singleton]
  have h2 : âˆ€áµ x, x â‰  b := by simp [ae_iff, measure_singleton]
  have hg1 := hg.ae_differentiableAt
  have hg2 : âˆ€áµ (x : â„), HasDerivAt g (deriv g x) x âˆ§ 0 â‰¤ deriv g x := by
    filter_upwards [hg1, h1, h2] with x hx1 hx2 hx3
    exact âŸ¨hx1.hasDerivAt, hg.deriv_nonnegâŸ©
  have hfg : âˆ€ x âˆˆ Ioo a b, deriv f x = deriv g x := by
    intro x hx
    apply Filter.EventuallyEq.deriv_eq
    have : Ioo a b âˆˆ ğ“ x := by
      rw [Metric.mem_nhds_iff]
      obtain âŸ¨Îµ, hÎµ1, hÎµ2âŸ© := Metric.isOpen_iff.mp isOpen_Ioo x hx
      use Îµ, hÎµ1
    filter_upwards [this] with y hy
    rw [mem_Ioo] at hy
    simp [g, hy]
  have hg3 (a0 b0 : â„) := hg.intervalIntegrable (Î¼ := volume) (a := a0) (b := b0)
  have hg4 (z a0 b0 : â„) : IntervalIntegrable (fun x â†¦ g (x + z)) volume a0 b0 := by
      convert hg3 (a0 + z) (b0 + z) |>.comp_add_right z <;> abel
  have hg5 (x : â„) (hx : b â‰¤ x) : g x = g b := by
    simp only [lt_self_iff_false, â†“reduceIte, g]
    split_ifs <;> (congr; try linarith)
  let G (c x : â„) := slope g x (x + c)
  have G_nonneg (c x : â„) (hc : 0 â‰¤ c) : 0 â‰¤ G c x := by
    have : g x â‰¤ g (x + c) := by apply hg; linarith
    have : 0 â‰¤ g (x + c) - g x := by linarith
    simp only [slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul, ge_iff_le, G]
    exact mul_nonneg (inv_nonneg.mpr hc) this
  have G_integrable (c : â„) : LocallyIntegrable (G c) volume := by
    simp only [G, slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul]
    exact (hgc c).locallyIntegrable.sub (hg.locallyIntegrable) |>.smul (c := câ»Â¹)
  have G_measurable (n : â„•) : AEMeasurable (G (n : â„)â»Â¹) volume := by
    exact G_integrable (n : â„)â»Â¹ |>.aestronglyMeasurable |>.aemeasurable
  have G_measurable_ab (n : â„•) : AEMeasurable ((Ioc a b).indicator (G (n : â„)â»Â¹)) volume := by
    apply (G_measurable n).indicator; simp
  have G_lim : âˆ€áµ (x : â„), Filter.Tendsto (fun (n : â„•) â†¦ G (n : â„)â»Â¹ x) Filter.atTop
      (nhds (deriv g x)) := by
    filter_upwards [hg2] with x âŸ¨hx1, hx2âŸ©
    rw [hasDerivAt_iff_tendsto_slope] at hx1
    dsimp [G]
    exact hx1.inverse_atTop
  have G_liminf' : âˆ€áµ (x : â„),
      Filter.liminf (fun (n : â„•) â†¦ â€–G (n : â„)â»Â¹ xâ€–â‚‘) Filter.atTop =
        â€–deriv g xâ€–â‚‘:= by
    filter_upwards [G_lim] with x hx
    exact hx.enorm.liminf_eq
  have G_liminf'_ab : âˆ€áµ (x : â„),
      Filter.liminf (fun (n : â„•) â†¦ â€–(Ioc a b).indicator (G (n : â„)â»Â¹) xâ€–â‚‘) Filter.atTop =
      â€–((Ioc a b).indicator (deriv g)) xâ€–â‚‘ := by
    filter_upwards [G_liminf'] with x hx
    by_cases hx1 : x âˆˆ Ioc a b <;> simp only [hx1, Set.indicator, â†“reduceIte]
    Â· exact hx
    Â· simp
  have G_fatou := MeasureTheory.lintegral_liminf_le' (fun n â†¦ ((G_measurable_ab n).enorm))
  have G_bound (n : â„•) (hn : n â‰¥ 1) :
      n * (âˆ« (x : â„) in a..b, g (x + (n : â„)â»Â¹) - g x) â‰¤ g b - g a := by
    calc
    _ = n * ((âˆ« (x : â„) in a..b, g (x + (â†‘n)â»Â¹)) - âˆ« (x : â„) in a..b, g x) := by
      rw [intervalIntegral.integral_sub]
      Â· exact hg4 _ _ _
      Â· exact hg3 _ _
    _ = n * ((âˆ« (x : â„) in (a + (â†‘n)â»Â¹)..(b + (â†‘n)â»Â¹), g x) - âˆ« (x : â„) in a..b, g x) := by simp
    _ = n * ((âˆ« (x : â„) in b..(b + (â†‘n)â»Â¹), g x) - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g x) := by
      rw [intervalIntegral.integral_interval_sub_interval_comm'] <;> exact hg3 _ _
    _ = n * ((âˆ« (x : â„) in b..(b + (â†‘n)â»Â¹), g b) - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g x) := by
      congr 2
      apply intervalIntegral.integral_congr
      simp only [EqOn, le_add_iff_nonneg_right, inv_nonneg, Nat.cast_nonneg, uIcc_of_le, mem_Icc,
        and_imp]
      intro x hx1 _
      exact hg5 x hx1
    _ = n * ((â†‘n)â»Â¹ * g b - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g x) := by simp
    _ â‰¤ n * ((â†‘n)â»Â¹ * g b - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g a) := by
      gcongr
      apply intervalIntegral.integral_mono_on <;> try simp
      Â· exact hg3 _ _
      Â· intros; apply hg; assumption
    _ = n * ((â†‘n)â»Â¹ * g b - (â†‘n)â»Â¹ * g a) := by simp
    _ = g b - g a := by
      ring_nf
      rw [show (n : â„) * (n : â„)â»Â¹ = 1 by refine mul_inv_cancelâ‚€ ?_; norm_cast; omega]
      ring
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hab]
  constructor
  Â· suffices AEStronglyMeasurable (deriv g) (volume.restrict (Ioc a b)) by
      apply this.congr
      have h3 : âˆ€áµ x âˆ‚(volume.restrict (Ioc a b)), x âˆˆ Ioc a b := by
        apply MeasureTheory.ae_restrict_mem; simp
      have h4 : âˆ€áµ x âˆ‚(volume.restrict (Ioc a b)), x â‰  b := by
        rw [MeasureTheory.ae_restrict_iff']
        Â· filter_upwards [h2] with x hx1 hx2; exact hx1
        Â· simp
      filter_upwards [h3, h4] with x hx1 hx2
      symm; apply hfg
      rw [â† Ioc_diff_right, mem_diff]
      simp [hx1, hx2]
    suffices AEStronglyMeasurable (deriv g) from this.restrict
    apply aestronglyMeasurable_of_tendsto_ae (lim := G_lim)
    intro n; exact (G_integrable (n : â„)â»Â¹).aestronglyMeasurable
  Â· unfold HasFiniteIntegral
    calc âˆ«â» x in Ioc a b, â€–deriv f xâ€–â‚‘
    _ = âˆ«â» x, (Ioc a b).indicator (fun t â†¦ â€–deriv f tâ€–â‚‘) x := by simp
    _ = âˆ«â» x, (Ioc a b).indicator (fun t â†¦ â€–deriv g tâ€–â‚‘) x := by
      apply MeasureTheory.lintegral_congr_ae
      filter_upwards [h2] with x hxb
      by_cases hx : x âˆˆ Ioc a b <;> simp only [indicator, hx, â†“reduceIte]
      congr 1
      apply hfg
      rw [â† Ioc_diff_right, mem_diff]
      simp [hx, hxb]
    _ = âˆ«â» x, â€–(Ioc a b).indicator (deriv g) xâ€–â‚‘ := by
      apply MeasureTheory.lintegral_congr
      intro x
      dsimp only [Set.indicator]
      by_cases hx : x âˆˆ Ioc a b <;> simp [hx]
    _ â‰¤ liminf (fun (n : â„•) â†¦ âˆ«â» (a_1 : â„), â€–(Ioc a b).indicator (G (n : â„)â»Â¹) a_1â€–â‚‘) atTop :=
        by
      convert G_fatou using 1
      apply MeasureTheory.lintegral_congr_ae
      filter_upwards [G_liminf'_ab] with x hx
      rw [hx]
    _ = liminf (fun (n : â„•) â†¦ ENNReal.ofReal (âˆ« (a_1 : â„), (Ioc a b).indicator (G (n : â„)â»Â¹) a_1))
        atTop := by
      apply Filter.liminf_congr
      filter_upwards with n
      rw [â† MeasureTheory.ofReal_integral_norm_eq_lintegral_enorm]
      Â· congr with y
        apply abs_eq_self.mpr
        dsimp only [Set.indicator]
        by_cases hy : y âˆˆ Ioc a b
        Â· simp only [hy, â†“reduceIte]
          apply G_nonneg; simp
        Â· simp only [hy, â†“reduceIte]; simp
      Â· have := (G_integrable (n : â„)â»Â¹).integrableOn_isCompact (k := Icc a b)
            (hk := isCompact_Icc)
        have := this.indicator (t := Ioc a b) (ht := by simp)
        have := this.integrable_indicator (hs := by simp)
        convert this using 1
        ext x
        by_cases hx : x âˆˆ Ioc a b <;> simp only [indicator, hx, â†“reduceIte]
        Â· simp [Ioc_subset_Icc_self hx]
        Â· simp
    _ = liminf (fun (n : â„•) â†¦ ENNReal.ofReal (âˆ« a_1 in a..b, (G (n : â„)â»Â¹) a_1)) atTop :=
        by
      apply Filter.liminf_congr
      filter_upwards with n
      congr 1
      rw [intervalIntegral.integral_of_le hab, integral_indicator]
      simp
    _ â‰¤ ENNReal.ofReal (g b - g a) :=
        by
      apply Filter.liminf_le_of_frequently_le'
      refine Filter.Eventually.frequently ?_
      simp only [Filter.Eventually, mem_atTop_sets, mem_setOf_eq]
      use 1
      intro n hn
      apply ENNReal.ofReal_le_ofReal
      simp only [slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul, inv_inv,
        intervalIntegral.integral_const_mul, G]
      exact G_bound n hn
    _ < âˆ := by
      exact ENNReal.ofReal_lt_top

/-- If `f` is absolute continuous on `uIcc a b`, then `f` is a.e. differentiable on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_differentiableWithinAt {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    âˆ€áµ (x : â„), x âˆˆ Set.uIcc a b â†’ DifferentiableWithinAt â„ f (Set.uIcc a b) x := by
  exact hf.boundedVariationOn.locallyBoundedVariationOn.ae_differentiableWithinAt_of_mem

/-- If `f` differentiable at `x âˆˆ uIoo a b` within `uIcc a b`, then `f'` exists at `x`. -/
theorem DifferentiableWithinAt.hasDerivAt_interval {f : â„ â†’ â„} {a b x : â„}
    (hf : DifferentiableWithinAt â„ f (Set.uIcc a b) x) (hx : x âˆˆ Set.uIoo a b) :
    HasDerivAt f (deriv f x) x := by
  have : uIcc a b âˆˆ ğ“ x := by
    rw [Metric.mem_nhds_iff]
    obtain âŸ¨Îµ, hÎµ1, hÎµ2âŸ© := Metric.isOpen_iff.mp isOpen_Ioo x hx
    use Îµ, hÎµ1
    trans uIoo a b; assumption; exact Ioo_subset_Icc_self
  have hx1 := hf.hasDerivWithinAt.hasDerivAt this
  rwa [hx1.deriv]

/-- If `f` is absolute continuous on `uIcc a b`, then `f` exists a.e. on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_hasDerivAt {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    âˆ€áµ (x : â„), x âˆˆ Set.uIcc a b â†’ HasDerivAt f (deriv f x) x := by
  have h1 : âˆ€áµ x, x â‰  min a b := by simp [ae_iff, measure_singleton]
  have h2 : âˆ€áµ x, x â‰  max a b := by simp [ae_iff, measure_singleton]
  filter_upwards [hf.ae_differentiableWithinAt, h1, h2] with x hx1 hx2 hx3 hx4
  have : x âˆˆ uIoo a b := by
    rw [uIoo, â† Icc_diff_both, mem_diff, â† uIcc]; simp [hx2, hx3, hx4]
  exact (hx1 hx4).hasDerivAt_interval this

/-- If `f` has locally bounded variation on `uIcc a b`, then `f'` is interval integrable on
`a..b`. -/
theorem LocallyBoundedVariationOn.deriv_intervalIntegrable {f : â„ â†’ â„} {a b : â„}
  (hf : LocallyBoundedVariationOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b := by
  obtain âŸ¨p, q, hp, hq, rflâŸ© := hf.exists_monotoneOn_sub_monotoneOn
  have h1 : âˆ€áµ x, x â‰  min a b := by simp [ae_iff, measure_singleton]
  have h2 : âˆ€áµ x, x â‰  max a b := by simp [ae_iff, measure_singleton]
  have hp1 := hp.deriv_intervalIntegrable
  have hq1 := hq.deriv_intervalIntegrable
  have hp2 := hp.ae_differentiableWithinAt_of_mem
  have hq2 := hq.ae_differentiableWithinAt_of_mem
  apply (hp1.sub hq1).congr
  rw [Filter.EventuallyEq, MeasureTheory.ae_restrict_iff']
  Â· filter_upwards [hp2, hq2, h1, h2] with x hx1 hx2 hx3 hx4 hx5
    have hx6 : x âˆˆ uIcc a b := by rw [uIcc]; rw [uIoc] at hx5; exact Ioc_subset_Icc_self hx5
    have hx7 : x âˆˆ uIoo a b := by
      rw [uIoo, â† Icc_diff_both, mem_diff, â† uIcc]; simp [hx3, hx4, hx6]
    replace hx1 := (hx1 hx6).hasDerivAt_interval hx7
    replace hx2 := (hx2 hx6).hasDerivAt_interval hx7
    rw [(hx1.sub hx2).deriv]
  Â· simp [uIoc]

/-- If `f` is absolute continuous on `uIcc a b`, then `f'` is interval integrable on `a..b`. -/
theorem AbsolutelyContinuousOnInterval.deriv_intervalIntegrable {f : â„ â†’ â„} {a b : â„}
  (hf : AbsolutelyContinuousOnInterval f a b) :
    IntervalIntegrable (deriv f) volume a b := by
  exact hf.boundedVariationOn.locallyBoundedVariationOn.deriv_intervalIntegrable

/-- If `f` is interval integrable on `a..b` and `c âˆˆ uIcc a b`, then `fun x â†¦ âˆ« v in c..x, f v` is
absolute continuous on `uIcc a b`. -/
theorem IntervalIntegrable.integral_absolutelyContinuousOnInterval {f : â„ â†’ â„} {a b : â„}
    (h : IntervalIntegrable f volume a b) (c : â„) (hc : c âˆˆ uIcc a b) :
    AbsolutelyContinuousOnInterval (fun x â†¦ âˆ« v in c..x, f v) a b := by
  wlog hab : a â‰¤ b
  Â· have S : uIcc a b = uIcc b a := by exact uIcc_comm a b
    exact @this f b a h.symm c (S â–¸ hc) (by linarith) |>.symm
  have subinterval_integrable {x y : â„} (hx : a â‰¤ x âˆ§ x â‰¤ b) (hy : a â‰¤ y âˆ§ y â‰¤ b) :
      IntervalIntegrable f volume x y := by
    apply IntervalIntegrable.mono_set' (a := a) (b := b)
    Â· assumption
    Â· dsimp [uIoc]
      simp only [hab, inf_of_le_left, sup_of_le_right]
      gcongr <;> simp [hx, hy]
  have hf := h.1.hasFiniteIntegral
  unfold HasFiniteIntegral at hf
  replace hf := ne_of_lt hf
  unfold AbsolutelyContinuousOnInterval
  rw [uIcc] at hc
  simp_rw [show min a b = a by simp [hab], show max a b = b by simp [hab] ] at hc âŠ¢
  rw [mem_Icc] at hc
  intro Îµ hÎµ
  have hÎµ' := ne_of_gt ((ENNReal.ofReal_pos).mpr hÎµ)
  obtain âŸ¨Î´', hÎ´'1, hÎ´'2âŸ© := exists_pos_setLIntegral_lt_of_measure_lt hf hÎµ'
  let Î´'' := min 1 Î´'
  have hÎ´''1 : Î´'' â‰  0 := by positivity
  have hÎ´''2 : Î´'' â‰  âˆ := by simp [Î´'']
  use Î´''.toReal
  have hÎ´ : Î´''.toReal > 0 := by apply ENNReal.toReal_pos <;> assumption
  constructor
  Â· exact hÎ´
  Â· intro n I hI1 hI2 hI3
    let s := â‹ƒ i âˆˆ Finset.range n, Set.Ioc (I i).1 (I i).2
    have hs : s âŠ† Set.Ioc a b := by
      dsimp only [s]
      intro x hx
      simp only [mem_iUnion, exists_prop] at hx
      obtain âŸ¨i, hi1, hi2âŸ© := hx
      suffices Ioc (I i).1 (I i).2 âŠ† Ioc a b by exact this hi2
      gcongr <;> linarith [hI1 i hi1]
    have : volume.restrict (Ioc a b) s < Î´' := by
      dsimp only [s]
      rw [MeasureTheory.measure_biUnion_finset]
      Â· have (i : â„•) (hi : i âˆˆ Finset.range n) :
            volume.restrict (Ioc a b) (Ioc (I i).1 (I i).2) =
            ENNReal.ofReal (dist (I i).1 (I i).2) := by
          have := hI1 i hi
          have : (I i).1 - (I i).2 â‰¤ 0 := by simp [this]
          have : Ioc (I i).1 (I i).2 âŠ† Ioc a b := by gcongr <;> tauto
          have : Ioc (I i).1 (I i).2 âˆ© Ioc a b = Ioc (I i).1 (I i).2 := by simp [this]
          simp [measurableSet_Ioc, Measure.restrict_apply, this]
          congr
          rw [Real.dist_eq, abs_eq_neg_self.mpr]
          Â· ring
          Â· assumption
        calc
        _ = âˆ‘ i âˆˆ Finset.range n, ENNReal.ofReal ((dist (I i).1 (I i).2)) := by
              apply Finset.sum_congr rfl
              exact this
        _ = ENNReal.ofReal (âˆ‘ i âˆˆ Finset.range n, (dist (I i).1 (I i).2)) := by
              rw [ENNReal.ofReal_sum_of_nonneg]
              simp
        _ < ENNReal.ofReal Î´''.toReal := by rw [ENNReal.ofReal_lt_ofReal_iff] <;> assumption
        _ = Î´'' := by simp [hÎ´''2]
        _ â‰¤ Î´' := by simp [Î´'']
      Â· exact hI2
      Â· simp
    specialize hÎ´'2 s this
    simp only [Real.dist_eq]
    calc
    _ = âˆ‘ i âˆˆ Finset.range n, |(âˆ« (v : â„) in (I i).1..(I i).2, f v)| := by
          apply Finset.sum_congr rfl
          intro i hi
          trans |(âˆ« (v : â„) in (I i).2..(I i).1, f v)|
          Â· congr 1
            rw [â† intervalIntegral.integral_add_adjacent_intervals (b := (I i).2)]
            Â· abel
            all_goals apply subinterval_integrable <;> (constructor <;> linarith [hI1 i hi])
          Â· rw [intervalIntegral.integral_symm]
            simp
    _ â‰¤ âˆ‘ i âˆˆ Finset.range n, (âˆ« (v : â„) in (I i).1..(I i).2, |f v|) := by
          gcongr with i hi
          exact intervalIntegral.abs_integral_le_integral_abs (hI1 i hi).right.left
    _ = âˆ‘ i âˆˆ Finset.range n, (âˆ« (v : â„) in Ioc (I i).1 (I i).2, |f v|) := by
          apply Finset.sum_congr rfl
          intro i hi
          exact intervalIntegral.integral_of_le (hI1 i hi).right.left
    _ = âˆ« (v : â„) in s, |f v| := by
          dsimp [s]
          symm
          apply MeasureTheory.integral_finset_biUnion
          Â· simp
          Â· exact hI2
          Â· intro i hi
            have : Ioc (I i).1 (I i).2 âŠ† Ioc a b := by gcongr <;> linarith [hI1 i hi]
            replace h := IntegrableOn.mono_set h.1 this
            dsimp only [IntegrableOn] at h âŠ¢
            fun_prop
    _ = âˆ« (v : â„) in s, â€–f vâ€– := by congr
    _ = (âˆ«â» (v : â„) in s, â€–f vâ€–â‚‘).toReal := by
          apply MeasureTheory.integral_norm_eq_lintegral_enorm
          exact AEStronglyMeasurable.mono_set hs h.1.left
    _ = (âˆ«â» (x : â„) in s, â€–f xâ€–â‚‘ âˆ‚volume.restrict (Ioc a b)).toReal := by
          congr 2
          rw [MeasureTheory.Measure.restrict_restrictâ‚€]
          Â· congr 1; simp [hs]
          Â· apply MeasurableSet.nullMeasurableSet
            dsimp only [s]
            measurability
    _ < Îµ := by
          convert ENNReal.toReal_strict_mono _ hÎ´'2
          Â· symm; exact ENNReal.toReal_ofReal (by linarith)
          Â· simp

/-- If `f` has derivative 0 a.e. on `[d, b]`, then there is a coultable Vitali cover of `[d, b]`
a.e., consisting of closed intervals, where each has small variations wrt `f`. -/
lemma ae_deriv_zero_ctb_cover {f : â„ â†’ â„} {d b Î· : â„}
    (hf : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x) (hÎ· : 0 < Î·) :
    let t := {(x, h) : â„ Ã— â„ | d < x âˆ§ 0 < h âˆ§ x + h < b âˆ§ |f (x + h) - f x| < h * Î·};
    let B : â„ Ã— â„ â†’ Set â„ := fun (x, h) â†¦ Set.Icc x (x + h);
    âˆƒ u âŠ† t, u.Countable âˆ§ u.PairwiseDisjoint B âˆ§ volume (Ioo d b \ â‹ƒ a âˆˆ u, B a) = 0 := by
  intro t B
  replace hf : âˆ€áµ x, x âˆˆ Ioo d b â†’ HasDerivAt f 0 x := by
    filter_upwards [hf] with x hx1 hx2
    exact hx1 (Ioo_subset_Icc_self hx2)
  let s := {x : â„ | x âˆˆ Ioo d b âˆ§ HasDerivAt f 0 x}
  have hs0: NullMeasurableSet s := by
    have : s = Ioo d b \ {x : â„ | x âˆˆ Ioo d b âˆ§ Â¬HasDerivAt f 0 x} := by
      simp only [s]; ext x; constructor
        <;> simp only [mem_setOf_eq, mem_diff, mem_Ioo, not_and, not_not, and_imp]
        <;> tauto
    have : NullMeasurableSet (Ioo d b) := by measurability
    have : NullMeasurableSet {x : â„ | x âˆˆ Ioo d b âˆ§ Â¬HasDerivAt f 0 x} := by
      rw [ae_iff] at hf
      push_neg at hf
      exact NullMeasurableSet.of_null hf
    measurability
  have : âˆƒ u âŠ† t, u.Countable âˆ§ u.PairwiseDisjoint B âˆ§ volume (s \ â‹ƒ a âˆˆ u, B a) = 0 := by
    apply Vitali.exists_disjoint_covering_ae volume s t 6 (Prod.snd) (Prod.fst) B
    Â· simp only [Icc, Metric.closedBall, Real.dist_eq, abs_le', tsub_le_iff_right, neg_sub,
      setOf_subset_setOf, and_imp, Prod.forall, B]
      intros; constructor <;> linarith
    Â· intro A hA
      simp only [Real.volume_closedBall, ENNReal.coe_ofNat, Real.volume_Icc, add_sub_cancel_left, B]
      rw [show 6 = ENNReal.ofReal 6 by norm_num, â† ENNReal.ofReal_mul] <;> try norm_num
      rw [ENNReal.ofReal_le_ofReal_iff]
      Â· linarith
      Â· simp only [mem_setOf_eq, t] at hA; linarith
    Â· simp +contextual [B, t]
    Â· simp [B, isClosed_Icc]
    Â· intro x hx Îµ hÎµ
      simp only [mem_Ioo, mem_setOf_eq, s] at hx
      obtain âŸ¨Î´, hÎ´1, hÎ´2âŸ© := (Metric.tendsto_nhds_nhds).mp (hasDerivAt_iff_tendsto.mp hx.2) Î· hÎ·
      set Î´' := min (min (Î´ / 2) Îµ) ((b - x) / 2)
      use (x, Î´')
      have h1 : Î´' > 0 := by simp [Î´', hÎµ, hÎ´1, hx.left.right]
      have h2 : Î´' â‰¥ 0 := by exact le_of_lt h1
      have h3 : |Î´'| < Î´ := by rw [abs_eq_self.mpr h2]; simp [Î´', hÎ´1]
      simp only [mem_setOf_eq, h1, true_and, and_true, t]
      simp only [Real.dist_eq, Real.norm_eq_abs, smul_eq_mul, mul_zero, sub_zero, dist_zero_right,
        norm_mul, norm_inv, abs_abs] at hÎ´2
      specialize hÎ´2 (show |x + Î´' - x| < Î´ by simp [h3])
      simp only [add_sub_cancel_left] at hÎ´2
      rw [abs_eq_self.mpr h2, â† mul_lt_mul_left h1] at hÎ´2
      repeat' constructor
      Â· exact hx.left.left
      Â· have : Î´' â‰¤ (b - x) / 2 := by simp [Î´']
        linarith
      Â· convert hÎ´2 using 1; field_simp
      Â· simp [Î´']
  obtain âŸ¨u, huâŸ© := this
  use u
  simp only [hu, true_and]
  have hv : Ioo d b \ â‹ƒ a âˆˆ u, B a âŠ† (Ioo d b \ s) âˆª (s \ â‹ƒ a âˆˆ u, B a) := by tauto_set
  suffices volume ((Ioo d b \ s) âˆª (s \ â‹ƒ a âˆˆ u, B a)) = 0 by exact Measure.mono_null hv this
  simp only [measure_union_null_iff]; constructor
  Â· convert ae_iff.mp hf using 2
    ext x
    simp only [mem_Ioo, mem_diff, mem_setOf_eq, s]
    tauto
  Â· tauto

/-- If `f` has derivative 0 a.e. on `[d, b]`, then there is a finite Vitali cover of `[d, b]`
except for measure at most `Î´`, consisting of closed intervals, where each has small variations
wrt `f`. -/
lemma ae_deriv_zero_fin_cover {f : â„ â†’ â„} {d b Î· Î´ : â„}
    (hf : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x)
    (hÎ· : 0 < Î·) (hÎ´ : 0 < Î´) :
    let t := {(x, h) : â„ Ã— â„ | d < x âˆ§ 0 < h âˆ§ x + h < b âˆ§ |f (x + h) - f x| < h * Î·};
    let B : â„ Ã— â„ â†’ Set â„ := fun (x, h) â†¦ Set.Icc x (x + h);
    âˆƒ n : â„•, âˆƒ v : â„• â†’ â„ Ã— â„,
      Set.image v (Finset.range n) âŠ† t âˆ§
      Set.PairwiseDisjoint (Finset.range n) (fun i â†¦ B (v i)) âˆ§
      volume (Ioo d b \ â‹ƒ i âˆˆ Finset.range n, B (v i)) < ENNReal.ofReal Î´ := by
  intro t B
  obtain âŸ¨u, hu1, hu2, hu3, hu4âŸ© := ae_deriv_zero_ctb_cover hf hÎ·
  obtain âŸ¨e, heâŸ© := Set.countable_iff_exists_injOn.mp hu2
  have : Ioo d b \ â‹ƒ a âˆˆ u, B a = â‹‚ (i : â„•), (Ioo d b \ â‹ƒ a âˆˆ {x âˆˆ u | e x < i}, B a) := by
    ext x; simp only [mem_diff, mem_iUnion, exists_prop, not_exists, not_and,
      mem_setOf_eq, mem_iInter, and_imp]
    constructor
    Â· intro âŸ¨h1, h2âŸ© i
      constructor <;> simp +contextual [h1, h2]
    Â· intro h
      constructor
      Â· exact (h 0).left
      Â· intro x hx; exact (h (e x + 1)).right x hx (by omega)
  rw [this] at hu4
  rw [MeasureTheory.measure_iInter_eq_iInf_measure_iInter_le] at hu4
  Â· clear this
    replace hu4 := hu4.symm â–¸
      (show @OfNat.ofNat â„â‰¥0âˆ 0 Zero.toOfNat0 < ENNReal.ofReal Î´ by simp [hÎ´])
    rw [iInf_lt_iff] at hu4
    obtain âŸ¨n, hnâŸ© := hu4
    classical
    let enum := (Finset.equivFin {j âˆˆ Finset.range n | âˆƒ x âˆˆ u, e x = j}).symm
    set n' := Finset.card ({j âˆˆ Finset.range n | âˆƒ x âˆˆ u, e x = j})
    have hvi (i : â„•) (hi : i < n') : âˆƒ x âˆˆ u, e x = enum âŸ¨i, hiâŸ© := by
      have := (enum âŸ¨i, hiâŸ©).property
      simp only [Finset.mem_filter, Finset.mem_range] at this
      tauto
    let v (i : â„•) : â„ Ã— â„ :=
      if hi : i < n' then Classical.choose (hvi i hi) else (0, 0)
    have v_prop (i : â„•) (hi : i < n') : v i âˆˆ u âˆ§ e (v i) = enum âŸ¨i, hiâŸ© := by
      simp only [hi, â†“reduceDIte, v]
      exact Classical.choose_spec (hvi i hi)
    use n', v
    constructor
    Â· intro z hz
      simp only [Finset.coe_range, mem_image, mem_Iio] at hz
      obtain âŸ¨i, hi1, hi2âŸ© := hz
      exact hi2 â–¸ hu1 (v_prop i hi1).left
    Â· constructor
      Â· intro i hi j hj hij
        have hi1 := v_prop i (Finset.mem_range.mp hi)
        have hj1 := v_prop j (Finset.mem_range.mp hj)
        apply hu3 hi1.left hj1.left
        intro hC
        have := Fin.mk.inj_iff.mp <| Equiv.injective enum <| Subtype.eq <|
          hj1.right â–¸ hC â–¸ hi1.right
        tauto
      Â· convert hn
        ext x
        simp only [Finset.mem_range, mem_diff, mem_iUnion, exists_prop, not_exists, not_and,
          mem_setOf_eq, mem_iInter, and_imp]
        constructor
        Â· intro âŸ¨hx1, hx2âŸ© j hj
          constructor; Â· assumption
          intro y hy1 hy2
          have hy : e y âˆˆ {j âˆˆ Finset.range n | âˆƒ x âˆˆ u, e x = j} := by
            simp only [Finset.mem_filter, Finset.mem_range]
            constructor; omega; use y
          let i := enum.symm âŸ¨e y, hyâŸ©
          have hi : i < n' := by exact i.isLt
          have : y = v i := by
            have : e y = enum i := by simp [i]
            exact he hy1 (v_prop i hi).left (this â–¸ (v_prop i hi).right.symm)
          exact this.symm â–¸ hx2 i hi
        Â· intro hx
          constructor
          Â· exact hx 0 (by omega) |>.left
          Â· intro i hi
            have := v_prop i hi
            apply hx n (by omega) |>.right (v i)
            Â· tauto
            Â· rw [this.right]
              set j := enum âŸ¨i, hiâŸ©
              have := j.property
              simp only [Finset.mem_filter, Finset.mem_range] at this
              exact this.left
  Â· intro i
    dsimp only [B]
    apply NullMeasurableSet.diff; Â· measurability
    apply NullMeasurableSet.biUnion
    Â· exact hu2.mono (by simp)
    Â· measurability
  Â· use 0
    have : volume (Ioo d b) â‰  âˆ := by simp
    intro hC
    apply measure_mono_top (sâ‚‚ := Ioo d b) at hC
    Â· tauto
    Â· simp

/-- If `f` has derivative 0 a.e. on `[d, b]`, then there is a finite Vitali cover of `[d, b]`
except for measure at most `Î´`, consisting of closed intervals, where each has small variations
wrt `f`. Additionally, The finite cover is already ordered. -/
lemma ae_deriv_zero_fin_ordered_cover {f : â„ â†’ â„} {d b Î· Î´ : â„}
    (hf : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x)
    (hÎ· : 0 < Î·) (hÎ´1 : 0 < Î´) :
    âˆƒ n : â„•, âˆƒ v : â„• â†’ â„ Ã— â„,
      (âˆ€ i âˆˆ Finset.range n, d < (v i).1 âˆ§ 0 < (v i).2 âˆ§ (v i).1 + (v i).2 < b âˆ§
        |f ((v i).1 + (v i).2) - f (v i).1| < (v i).2 * Î·) âˆ§
      (âˆ€ i âˆˆ Finset.range n, âˆ€ j âˆˆ Finset.range n, i < j â†’ (v i).1 + (v i).2 < (v j).1) âˆ§
      (b - d) - (âˆ‘ i âˆˆ Finset.range n, (v i).2) < Î´ := by
  obtain âŸ¨n, v, hv1, hv2, hv3âŸ© := ae_deriv_zero_fin_cover hf hÎ· hÎ´1
  replace hv1 : âˆ€ i âˆˆ Finset.range n, d < (v i).1 âˆ§ 0 < (v i).2 âˆ§ (v i).1 + (v i).2 < b âˆ§
      |f ((v i).1 + (v i).2) - f (v i).1| < (v i).2 * Î· := by
    intro i hi
    have : v i âˆˆ {(x, h) : â„ Ã— â„ | d < x âˆ§ h > 0 âˆ§ x + h < b âˆ§ |f (x + h) - f x| < h * Î·} := by
      apply @hv1 (v i)
      simp only [Finset.coe_range, mem_image, mem_Iio]
      use i; exact âŸ¨List.mem_range.mp hi, rflâŸ©
    simpa using this
  let r_list := @Finset.sort (Finset.range n) (fun (i j) â†¦ (v i).1 â‰¤ (v j).1) _
    { trans := by intros; linarith }
    { antisymm := by
        intro i j h1 h2
        have hij: (v i).1 = (v j).1 := by linarith
        have := hv2 i.property j.property
        contrapose this
        push_neg
        constructor
        Â· exact Subtype.coe_ne_coe.mpr this
        Â· simp only [Disjoint, le_eq_subset, bot_eq_empty, subset_empty_iff, not_forall]
          use {(v i).1}
          simp only [singleton_ne_empty, not_false_eq_true, singleton_subset_iff, exists_prop,
            and_true]
          constructor <;> simp only [mem_Icc, le_refl, le_add_iff_nonneg_right, true_and]
          Â· linarith [hv1 i.val i.property]
          Â· simp only [hij, le_refl, le_add_iff_nonneg_right, true_and]
            linarith [hv1 j.val j.property] }
    { total := by intros; exact LinearOrder.le_total _ _ }
    Finset.univ
  have r_list_len : r_list.length = n := by simp [r_list]
  let r (i : â„•) : â„• :=
    if hi : i âˆˆ Finset.range n then r_list.get âŸ¨i, r_list_len.symm â–¸ Finset.mem_range.mp hiâŸ©
    else i
  have r_mem {i : â„•} (hi : i âˆˆ Finset.range n) : r i âˆˆ Finset.range n := by
    simp [r, Finset.mem_range.mp hi]
  have r_mono {i j : â„•} (hi : i âˆˆ Finset.range n) (hj : j âˆˆ Finset.range n) (hij : i â‰¤ j) :
      (v (r i)).1 â‰¤ (v (r j)).1 := by
    have : List.Sorted (fun (i j : Finset.range n) â†¦ (v i).1 â‰¤ (v j).1) r_list := by simp [r_list]
    simp only [hi, hj, r, â†“reduceDIte]
    apply @List.Sorted.rel_get_of_le _ _ {refl := by simp +contextual} _ this
    simpa
  have r_inj {i j : â„•} (hi : i âˆˆ Finset.range n) (hj : j âˆˆ Finset.range n) (hij : i â‰  j) :
      r i â‰  r j := by
    have nodup : r_list.Nodup := by simp [r_list]
    have := List.Nodup.getElem_inj_iff (h := nodup)
      (hi := r_list_len.symm â–¸ (List.mem_range.mp hi))
      (hj := r_list_len.symm â–¸ (List.mem_range.mp hj))
    simp only [hi, hj, r, â†“reduceDIte]
    intro hC
    have := this.mp (Subtype.eq hC)
    contradiction
  have r_surj {k : â„•} (hk : k âˆˆ Finset.range n) : âˆƒ i âˆˆ Finset.range n, r i = k := by
    have : âŸ¨k, hkâŸ© âˆˆ r_list := by simp [r_list]
    obtain âŸ¨i, hi, hâŸ© := List.mem_iff_getElem.mp this
    rw [r_list_len] at hi
    use i; constructor
    Â· rwa [Finset.mem_range]
    Â· simp [r, hi, h]
  let v' (i : â„•) : (â„ Ã— â„) := v (r i)
  use n, v'
  repeat' constructor
  Â· intro i hi
    simp only [v']
    exact hv1 _ (r_mem hi)
  Â· intro i hi j hj hij
    have hi1 : i + 1 âˆˆ Finset.range n := by
      have := List.mem_range.mp hj;
      rw [Finset.mem_range]; omega
    simp only [v']
    suffices (v (r i)).1 + (v (r i)).2 < (v (r (i + 1))).1 by
      have : (v (r (i + 1))).1 â‰¤ (v (r j)).1 := by apply r_mono <;> assumption
      refine lt_of_lt_of_le ?_ this; assumption
    have hL: (v (r i)).1 â‰¤ (v (r (i + 1))).1 := by apply r_mono <;> (first | assumption | omega)
    have disj := hv2 (r_mem hi) (r_mem hi1) (by apply r_inj <;> (first | assumption | omega))
    simp only [Disjoint, le_eq_subset, bot_eq_empty, subset_empty_iff] at disj
    specialize @disj {(v (r (i + 1))).1}
    by_contra hC
    have : (v (r (i + 1))).1 â‰¤ (v (r i)).1 + (v (r i)).2 := by linarith
    simp only [singleton_subset_iff, mem_Icc, hL, this, and_self, le_refl,
      le_add_iff_nonneg_right, true_and, singleton_ne_empty, imp_false, not_le,
      forall_const] at disj
    linarith [hv1 _ (r_mem hi1)]
  rw [MeasureTheory.measure_diff, MeasureTheory.measure_biUnion_finset] at hv3
  Â· simp only [Real.volume_Icc, Real.volume_Ioo, add_sub_cancel_left] at hv3
    have : âˆ‘ i âˆˆ Finset.range n, (v' i).2 = âˆ‘ x âˆˆ Finset.range n, (v x).2 := by
      dsimp only [v']
      symm
      have : Finset.range n = Finset.image r (Finset.range n) := by
        ext k; constructor
        Â· intro hk
          simp only [Finset.mem_image]
          obtain âŸ¨i, hi1, hi2âŸ© := r_surj hk; use i, hi1
        Â· intro hk
          simp only [Finset.mem_image] at hk
          obtain âŸ¨i, hi1, hi2âŸ© := hk
          exact hi2 â–¸ r_mem hi1
      nth_rw 1 [this]
      apply @Finset.sum_image (g := r)
      dsimp only [InjOn]
      intro i hi j hj; contrapose!; exact r_inj hi hj
    rw [this]
    have : âˆ€ i âˆˆ Finset.range n, 0 â‰¤ (v i).2 := by intro i hi; linarith [hv1 i hi]
    rw [â† ENNReal.ofReal_sum_of_nonneg, â† ENNReal.ofReal_sub] at hv3
    Â· exact (ENNReal.ofReal_lt_ofReal_iff hÎ´1).mp hv3
    Â· exact Finset.sum_nonneg this
    Â· exact this
  Â· assumption
  Â· simp +contextual
  Â· intro x hx
    simp only [Finset.mem_range, mem_iUnion, exists_prop] at hx
    obtain âŸ¨i, hi1, hi2âŸ© := hx
    simp only [mem_Icc] at hi2
    rw [mem_Ioo]
    constructor <;> linarith [hv1 i (List.mem_range.mpr hi1)]
  Â· measurability
  Â· have : âˆ‘ i âˆˆ Finset.range n, volume (Icc (v i).1 ((v i).1 + (v i).2)) â‰  âŠ¤ := by simp
    exact ne_top_of_le_ne_top this <| measure_biUnion_finset_le (Finset.range n)
      fun i â†¦ Icc (v i).1 ((v i).1 + (v i).2)

lemma split_sum_even_odd (n : â„•) (f : â„• â†’ â„) : âˆ‘ i âˆˆ Finset.range (2 * n + 1), f i =
    âˆ‘ i âˆˆ Finset.range (n + 1), f (2 * i) + âˆ‘ i âˆˆ Finset.range n, f (2 * i + 1) := by
  let A : Finset â„• := {i âˆˆ Finset.range (2 * n + 1) | Even i}
  let B : Finset â„• := {i âˆˆ Finset.range (2 * n + 1) | Odd i}
  have disj : Disjoint A B := by
    suffices A âˆ© B = âˆ… by exact Finset.disjoint_iff_inter_eq_empty.mpr this
    suffices Â¬âˆƒ x, x âˆˆ A âˆ© B by exact Finset.not_nonempty_iff_eq_empty.mp this
    by_contra hC
    obtain âŸ¨x, hxâŸ© := hC
    simp [A, B] at hx
    have : Â¬(Even x âˆ§ Odd x) := by simp
    tauto
  have union : Finset.range (2 * n + 1) = A.disjUnion B disj := by
    ext a; simp only [Finset.mem_range, Finset.disjUnion_eq_union, Finset.mem_union,
      Finset.mem_filter, A, B]
    constructor
    Â· intro ha
      have : Odd a â†” Â¬Even a := by simp
      tauto
    Â· tauto
  rw [union, Finset.sum_disjUnion]
  congr 1
  Â· let r (i : â„•) := 2 * i
    have : A = Finset.image r (Finset.range (n + 1)) := by
      ext a; simp only [Finset.mem_filter, Finset.mem_range, Finset.mem_image, A, r]
      constructor
      Â· intro âŸ¨h1, âŸ¨b, hbâŸ©âŸ©; use b; omega
      Â· intro âŸ¨b, h1, h2âŸ©; constructor; Â· omega
        use b; omega
    rw [this]
    apply @Finset.sum_image (g := r)
    dsimp only [InjOn, r]
    intros; omega
  Â· let r (i : â„•) := 2 * i + 1
    have : B = Finset.image r (Finset.range n) := by
      ext a; simp only [Finset.mem_filter, Finset.mem_range, Finset.mem_image, B, r]
      constructor
      Â· intro âŸ¨h1, âŸ¨b, hbâŸ©âŸ©; use b; omega
      Â· intro âŸ¨b, h1, h2âŸ©; constructor; Â· omega
        use b; omega
    rw [this]
    apply @Finset.sum_image (g := r)
    dsimp only [InjOn, r]
    intros; omega

/-- If `f` is absolutely continuous on `uIcc a b` and `f' x = 0` for a.e. `x âˆˆ uIcc a b`, then `f`
is constant on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_deriv_zero_const {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b)
    (hf1 : âˆ€áµ x, x âˆˆ uIcc a b â†’ HasDerivAt f 0 x) :
    âˆƒ C, âˆ€ x âˆˆ uIcc a b, f x = C := by
  wlog hab : a â‰¤ b
  Â· have S : uIcc a b = uIcc b a := by exact uIcc_comm a b
    exact S.symm â–¸ @this f b a hf.symm (S â–¸ hf1) (by linarith)
  suffices âˆ€ x âˆˆ uIcc a b, f x = f b by use f b
  by_contra hC
  push_neg at hC
  obtain âŸ¨d, hd1, hd2âŸ© := hC
  simp only [Set.uIcc, mem_Icc,
    show min a b = a by simp [hab], show max a b = b by simp [hab] ] at hd1
  have had : a â‰¤ d := by linarith
  have hdb : d < b := lt_of_le_of_ne hd1.right fun hC â†¦ hd2 (congrArg f hC)
  dsimp only [AbsolutelyContinuousOnInterval] at hf
  have hfdb: 0 < |f d - f b| / 2 := by
    simp only [Nat.ofNat_pos, div_pos_iff_of_pos_right, abs_pos, ne_eq]
    rwa [sub_eq_zero]
  obtain âŸ¨Î´, hÎ´1, hÎ´2âŸ© := hf (|f d - f b| / 2) hfdb
  simp_rw [uIcc, show min a b = a by simp [hab], show max a b = b by simp [hab] ] at hf1 hÎ´2 âŠ¢
  replace hf1 : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x := by
    filter_upwards [hf1] with x hx1 hx2
    apply hx1
    suffices Icc d b âŠ† Icc a b by exact this hx2
    gcongr
  have hfdb': 0 < |f d - f b| / (2 * (b - d)) := by
    apply div_pos <;> linarith
  obtain âŸ¨n, v, hv1, hv2, hv3âŸ© := ae_deriv_zero_fin_ordered_cover hf1 hfdb' hÎ´1
  let I (i : â„•) :=
    if i < n then
      if i = 0 then (d, (v i).1) else ((v (i - 1)).1 + (v (i - 1)).2, (v i).1)
    else
      if i = n âˆ§ 0 < n then ((v (i - 1)).1 + (v (i - 1)).2, b) else (d, b)
  have hI1 : (I 0).1 = d := by
    dsimp only [I]
    split_ifs
    any_goals omega
    all_goals simp
  have hI2 : (I n).2 = b := by
    dsimp only [I]
    split_ifs
    any_goals omega
    all_goals simp
  have hI3 (i : â„•) (hi : i âˆˆ Finset.range n) : (I (i + 1)).1 = (v i).1 + (v i).2 := by
    have := Finset.mem_range.mp hi
    dsimp only [I]
    split_ifs
    any_goals omega
    any_goals contradiction
    all_goals simp
  have hI4 (i : â„•) (hi : i âˆˆ Finset.range n) : (I i).2 = (v i).1 := by
    have := Finset.mem_range.mp hi
    dsimp only [I]
    split_ifs
    all_goals simp
  have hI : âˆ€ i âˆˆ Finset.range (n + 1), a â‰¤ (I i).1 âˆ§ (I i).1 â‰¤ (I i).2 âˆ§ (I i).2 â‰¤ b := by
    Â· intro i hi
      by_cases hi1 : i < n
      Â· simp only [hi1, â†“reduceIte, I]
        Â· by_cases hi0 : i = 0
          Â· simp only [hi0, â†“reduceIte, true_and, had]
            constructor <;> linarith [hv1 0 (by rw [Finset.mem_range]; omega)]
          Â· simp only [hi0, â†“reduceIte]
            have := hv1 (i - 1) (by rw [Finset.mem_range]; omega)
            have := hv1 i (by rw [Finset.mem_range]; omega)
            have := hv2 (i - 1) (by rw [Finset.mem_range]; omega) i
              (by rw [Finset.mem_range]; omega) (by omega)
            constructor
            Â· linarith
            Â· constructor <;> linarith
      Â· simp only [hi1, â†“reduceIte, I]
        Â· by_cases hn : i = n âˆ§ 0 < n
          Â· simp only [hn, and_self, â†“reduceIte, le_refl, and_true]
            constructor <;> linarith [hv1 (n - 1) (by rw [Finset.mem_range]; omega)]
          Â· simp only [hn, â†“reduceIte, le_refl, and_true, true_and, had]
            exact hd1.right
  let r (i : â„•) : â„ :=
    if Even i then (I (i / 2)).1 else (I (i / 2)).2
  have hr1 (i : â„•) : r (2 * i) = (I i).1 := by simp [r]
  have hr2 (i : â„•) : r (2 * i + 1) = (I i).2 := by
    simp only [Nat.not_even_bit1, â†“reduceIte, r]
    congr; omega
  have hrd : d = r 0 := by rw [show 0 = 2 * 0 by rfl, hr1, hI1]
  have hrb : b = r (2 * n + 1) := by rw [hr2, hI2]
  have h_dist_sum : âˆ‘ i âˆˆ Finset.range (n + 1), dist (I i).1 (I i).2 =
      b - d - âˆ‘ i âˆˆ Finset.range n, (v i).2 := by
    rw [fun a b c â†¦ show a = b - c â†” b = a + c by constructor <;> (intro h; rw [h]; abel) ]
    calc
    _ = r (2 * n + 1) - r 0 := by rw [hrd, hrb]
    _ = âˆ‘ k âˆˆ Finset.range (2 * n + 1), (r (k + 1) - r k) := by rw [â† Finset.sum_range_sub]
    _ = âˆ‘ i âˆˆ Finset.range (n + 1), (r (2 * i + 1) - r (2 * i)) +
        âˆ‘ i âˆˆ Finset.range n, (r (2 * i + 1 + 1) - r (2 * i + 1)) := by
      rw [split_sum_even_odd]
    _ = âˆ‘ i âˆˆ Finset.range (n + 1), dist (I i).1 (I i).2 + âˆ‘ i âˆˆ Finset.range n, (v i).2 := by
      congr 1 <;> apply Finset.sum_congr rfl
      Â· intro i hi
        rw [hr1, hr2, Real.dist_eq, abs_eq_neg_self.mpr]
        Â· abel
        Â· linarith [hI i hi]
      Â· intro i hi
        rw [show 2 * i + 1 + 1 = 2 * (i + 1) by ring, hr1, hr2, hI3, hI4] <;> try assumption
        abel
  have : âˆ‘ i âˆˆ Finset.range (n + 1), dist (f (I i).1) (f (I i).2) < |f d - f b| / 2 := by
    apply hÎ´2 (n + 1) I
    Â· exact hI
    Â· intro i hi j hj hij
      wlog hij1 : i < j
      Â· exact (this hf hab d hd2 hd1 had hdb hfdb Î´ hÎ´1 hÎ´2 hf1 hfdb' n v hv1 hv2 hv3 hI1 hI2
          hI3 hI4 hI hr1 hr2 hrd hrb h_dist_sum hj hi hij.symm (by omega)).symm
      Â· simp only [Ioc_disjoint_Ioc, le_sup_iff, inf_le_iff]
        have hv2' {i j : â„•} (hi : i âˆˆ Finset.range n) (hj : j âˆˆ Finset.range n) (hij : i â‰¤ j) :
            (v i).1 â‰¤ (v j).1 := by
          by_cases hij0 : i = j
          Â· rw [hij0]
          Â· linarith [hv2 i hi j hj (by omega), hv1 i hi]
        right; left
        have hjn : j < n + 1 := by exact Finset.mem_range.mp hj
        have hin : i < n + 1 := by exact Finset.mem_range.mp hi
        replace hin : i < n := by omega
        simp only [hin, â†“reduceIte, I]
        have (a : â„) (ha : 0 < a) : 0 â‰¤ a := by exact le_of_lt ha
        split_ifs <;> (simp only; try omega)
        all_goals apply le_add_of_le_of_nonneg
        all_goals try refine le_of_lt (hv1 _ ?_).right.left
        all_goals try refine hv2' ?_ ?_ ?_
        all_goals try rw [Finset.mem_range]
        all_goals omega
    Â· convert hv3 using 1
  suffices |f d - f b| < |f d - f b| by linarith
  calc
  _ = |f (r 0) - f (r (2 * n + 1))| := by rw [hrd, hrb]
  _ = |(f âˆ˜ r) 0 - (f âˆ˜ r) (2 * n + 1)| := by simp
  _ = |âˆ‘ k âˆˆ Finset.range (2 * n + 1), ((f âˆ˜ r) k - (f âˆ˜ r) (k + 1))| := by
    rw [â† Finset.sum_range_sub']
  _ = |âˆ‘ k âˆˆ Finset.range (2 * n + 1), (f (r k) - f (r (k + 1)))| := by simp
  _ â‰¤ âˆ‘ k âˆˆ Finset.range (2 * n + 1), |f (r k) - f (r (k + 1))| := by
    apply Finset.abs_sum_le_sum_abs
  _ = âˆ‘ i âˆˆ Finset.range (n + 1), |f (r (2 * i)) - f (r (2 * i + 1))| +
      âˆ‘ i âˆˆ Finset.range n, |f (r (2 * i + 1)) - f (r (2 * i + 1 + 1))| := by
    rw [split_sum_even_odd]
  _ = âˆ‘ i âˆˆ Finset.range (n + 1), dist (f (I i).1) (f (I i).2) +
      âˆ‘ i âˆˆ Finset.range n, |f ((v i).1 + (v i).2) - f ((v i).1)| := by
    congr 1 <;> apply Finset.sum_congr rfl
    Â· intro i hi
      rw [hr1, hr2, Real.dist_eq]
    Â· intro i hi
      rw [show 2 * i + 1 + 1 = 2 * (i + 1) by ring, hr1, hr2, hI3, hI4] <;> try assumption
      nth_rw 1 [â† abs_neg]; congr 1; abel
  _ < |f d - f b| / 2 + âˆ‘ i âˆˆ Finset.range n, |f ((v i).1 + (v i).2) - f ((v i).1)| := by
    gcongr 1
  _ â‰¤ |f d - f b| / 2 + âˆ‘ i âˆˆ Finset.range n, (v i).2 * (|f d - f b| / (2 * (b - d))) := by
    gcongr with i hi
    specialize hv1 i hi
    linarith
  _ = |f d - f b| / 2 + (âˆ‘ i âˆˆ Finset.range n, (v i).2) * (|f d - f b| / (2 * (b - d))) := by
    rw [Finset.sum_mul]
  _ â‰¤ |f d - f b| / 2 + (b - d) * (|f d - f b| / (2 * (b - d))) := by
    gcongr
    suffices 0 â‰¤ (b - d) - âˆ‘ i âˆˆ Finset.range n, (v i).2 by linarith
    rw [â† h_dist_sum]
    apply Finset.sum_nonneg; simp
  _ = |f d - f b| := by field_simp [show b - d â‰  0 by linarith]; ring

/-- *Fundamental Theorem of Calculus* for absolutely continuous functions: if `f` is absolutely
continuous on `uIcc a b`, then `âˆ« (x : â„) in a..b, deriv f x = f b - f a`. -/
theorem AbsolutelyContinuousOnInterval.integral_deriv_eq_sub {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    âˆ« (x : â„) in a..b, deriv f x = f b - f a := by
  have f_deriv := hf.ae_hasDerivAt
  have f_deriv_integrable := hf.deriv_intervalIntegrable
  have f_deriv_integral_ac := hf.deriv_intervalIntegrable.integral_absolutelyContinuousOnInterval
  have f_deriv_integral_deriv := f_deriv_integrable.ae_eq_deriv_integral (c := a) (hc := by simp)
  let g (x : â„) := f x - âˆ« (t : â„) in a..x, deriv f t
  have g_ac : AbsolutelyContinuousOnInterval g a b := hf.sub (f_deriv_integral_ac a (by simp))
  have g_ae_deriv_zero : âˆ€áµ x, x âˆˆ uIcc a b â†’ HasDerivAt g 0 x := by
    filter_upwards [f_deriv, f_deriv_integral_deriv] with x hx1 hx2 hx3
    convert (hx1 hx3).sub (hx2 hx3)
    abel
  have g_const := g_ac.ae_deriv_zero_const g_ae_deriv_zero
  obtain âŸ¨C, hCâŸ© := g_const
  have C_val : C = f a := by
    specialize hC a (by simp)
    simp only [intervalIntegral.integral_same, sub_zero, g] at hC
    exact hC.symm
  specialize hC b (by simp)
  simp only [C_val, g] at hC
  rw [â† hC]; abel

/-- The integral of the derivative of a product of two absolutely continuous functions. -/
theorem AbsolutelyContinuousOnInterval.integral_deriv_mul_eq_sub
    {f g : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) (hg : AbsolutelyContinuousOnInterval g a b) :
    âˆ« x in a..b, deriv f x * g x + f x * deriv g x = f b * g b - f a * g a := by
  have fg_ac := hf.mul hg
  have fg_ac_FTC := fg_ac.integral_deriv_eq_sub
  simp only [Pi.mul_apply] at fg_ac_FTC
  rw [â† fg_ac_FTC]
  apply intervalIntegral.integral_congr_ae
  have f_deriv := hf.ae_hasDerivAt
  have g_deriv := hg.ae_hasDerivAt
  filter_upwards [f_deriv, g_deriv] with x hx1 hx2 hx3
  have hx4 : x âˆˆ uIcc a b := by rw [uIcc]; rw [uIoc] at hx3; exact Ioc_subset_Icc_self hx3
  have hx5 := (hx1 hx4).mul (hx2 hx4)
  exact hx5.deriv.symm

/-- *Integration by parts* for absolutely continuous functions. -/
theorem AbsolutelyContinuousOnInterval.integral_mul_deriv_eq_deriv_mul
    {f g : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) (hg : AbsolutelyContinuousOnInterval g a b) :
    âˆ« x in a..b, f x * deriv g x = f b * g b - f a * g a - âˆ« x in a..b, deriv f x * g x := by
  rw [â† AbsolutelyContinuousOnInterval.integral_deriv_mul_eq_sub hf hg,
      â† intervalIntegral.integral_sub]
  Â· simp_rw [add_sub_cancel_left]
  Â· exact (hf.deriv_intervalIntegrable.mul_continuousOn hg.continuousOn).add
      (hg.deriv_intervalIntegrable.continuousOn_mul hf.continuousOn)
  Â· exact hf.deriv_intervalIntegrable.mul_continuousOn hg.continuousOn
