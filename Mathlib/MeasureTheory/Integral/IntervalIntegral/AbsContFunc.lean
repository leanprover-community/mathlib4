/-
Copyright (c) 2025 Yizheng Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yizheng Zhu
-/
module

public import Mathlib.Analysis.BoundedVariation
public import Mathlib.Analysis.Calculus.Deriv.Basic
public import Mathlib.Analysis.Calculus.Deriv.Mul
public import Mathlib.Analysis.Calculus.Deriv.Slope
public import Mathlib.MeasureTheory.Covering.Vitali
public import Mathlib.MeasureTheory.Function.AbsolutelyContinuous
public import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
public import Mathlib.MeasureTheory.Integral.IntervalIntegral.DerivIntegrable
public import Mathlib.MeasureTheory.Integral.IntervalIntegral.LebesgueDifferentiationThm
public import Mathlib.MeasureTheory.Integral.Lebesgue.Basic
public import Mathlib.MeasureTheory.Measure.MeasureSpace
public import Mathlib.Order.Interval.Lex

-- import Mathlib.Order.Monotone.Nat
/-!
# Fundamental Theorem of Calculus and Integration by Parts for Absolutely Continuous Functions

This file proves that:
* If `f` is absolutely continuous on `uIcc a b`, then *Fundamental Theorem of Calculus* holds for
`f'` on `a..b`, i.e. `‚à´ (x : ‚Ñù) in a..b, deriv f x = f b - f a`.
* *Integration by Parts* holds for absolutely continuous functions.

## Implementation notes

We need to prove that `f'` is interval integrable on `a..b` for any monotone function `f`. The
proof uses Fatou's lemma and is proved in `MonotoneOn.deriv_intervalIntegrable`. From this we get
`f'` is interval integrable on `a..b` for BV functions, proved in
`LocallyBoundedVariationOn.deriv_intervalIntegrable`,
and finally for absolutely continuous functions, proved in
`AbsolutelyContinuousOnInterval.deriv_intervalIntegrable`.

## Tags
absolutely continuous, fundamental theorem of calculus, integration by parts
-/

@[expose] public section

open MeasureTheory Set Filter Function AbsolutelyContinuousOnInterval

open scoped Topology ENNReal Interval NNReal

/-- If `f` is interval integrable on `a..b` and `c ‚àà uIcc a b`, then `fun x ‚Ü¶ ‚à´ v in c..x, f v` is
absolute continuous on `uIcc a b`. -/
theorem IntervalIntegrable.absolutelyContinuousOnInterval_intervalIntegral {f : ‚Ñù ‚Üí ‚Ñù} {a b c : ‚Ñù}
    (h : IntervalIntegrable f volume a b) (hc : c ‚àà uIcc a b) :
    AbsolutelyContinuousOnInterval (fun x ‚Ü¶ ‚à´ v in c..x, f v) a b := by
  let s := fun E : ‚Ñï √ó (‚Ñï ‚Üí ‚Ñù √ó ‚Ñù) ‚Ü¶ ‚ãÉ i ‚àà Finset.range E.1, uIoc (E.2 i).1 (E.2 i).2
  have : Tendsto (‚áë(volume.restrict (uIoc a b)) ‚àò s) (totalLengthFilter ‚äì ùìü (disjWithin a b))
      (ùìù 0) := by
    rw [(hasBasis_totalLengthFilter.inf_principal _).tendsto_iff ENNReal.nhds_zero_basis_Iic]
    intro Œµ hŒµ
    by_cases hŒµ_top : Œµ = ‚ä§
    ¬∑ exact ‚ü®1, by simp, by simp [hŒµ_top]‚ü©
    replace hŒµ := ENNReal.toReal_pos (hŒµ.ne.symm) hŒµ_top
    refine ‚ü®Œµ.toReal, hŒµ, fun (n, I) hnI ‚Ü¶ ?_‚ü©
    rw [mem_inter_iff] at hnI
    simp only [comp_apply, mem_Iic, s]
    rw [Measure.restrict_eq_self (h := union_subset_of_disjWithin hnI.right)]
    simp only [disjWithin, mem_setOf_eq] at hnI
    obtain ‚ü®hnI‚ÇÅ, hnI‚ÇÇ, hnI‚ÇÉ‚ü© := hnI
    rw [MeasureTheory.measure_biUnion_finset hnI‚ÇÉ (by simp [uIoc])]
    calc ‚àë i ‚àà Finset.range n, volume (uIoc (I i).1 (I i).2)
      _ = ‚àë i ‚àà Finset.range n, ENNReal.ofReal ((dist (I i).1 (I i).2)) := by
        apply Finset.sum_congr rfl
        simp [uIoc, Real.dist_eq, max_sub_min_eq_abs']
      _ = ENNReal.ofReal (‚àë i ‚àà Finset.range n, (dist (I i).1 (I i).2)) := by
        simp [ENNReal.ofReal_sum_of_nonneg]
      _ ‚â§ ENNReal.ofReal Œµ.toReal :=
        ENNReal.ofReal_lt_ofReal_iff hŒµ |>.mpr hnI‚ÇÅ |>.le
      _ ‚â§ Œµ := ENNReal.ofReal_toReal_le
  have := MeasureTheory.tendsto_setLIntegral_zero
    (ne_of_lt <| intervalIntegrable_iff.mp h |>.hasFiniteIntegral)
    (s := s)
    (l := totalLengthFilter ‚äì ùìü (disjWithin a b))
    this
  have := ENNReal.toReal_zero ‚ñ∏ (ENNReal.continuousAt_toReal (by simp)).tendsto.comp this
  refine squeeze_zero' ?_ ?_ this
  ¬∑ filter_upwards with (n, I)
    exact Finset.sum_nonneg (fun _ _ ‚Ü¶ dist_nonneg)
  simp only [comp_apply, s]
  have : ‚àÄ·∂† (E : ‚Ñï √ó (‚Ñï ‚Üí ‚Ñù √ó ‚Ñù)) in totalLengthFilter ‚äì ùìü (disjWithin a b),
      E ‚àà disjWithin a b :=
    eventually_inf_principal.mpr (by simp)
  filter_upwards [this] with (n, I) hnI
  obtain ‚ü®hnI1, hnI2‚ü© := mem_setOf_eq ‚ñ∏ hnI
  simp only
  rw [‚Üê MeasureTheory.integral_norm_eq_lintegral_enorm (h.aestronglyMeasurable_uIoc.restrict),
      MeasureTheory.integral_biUnion_finset _ (by simp +contextual [uIoc]) hnI2]
  ¬∑ refine Finset.sum_le_sum (fun i hi ‚Ü¶ ?_)
    rw [Real.dist_eq,
        intervalIntegral.integral_interval_sub_left
          (by apply IntervalIntegrable.mono_set' h; grind [uIoc, uIcc])
          (by apply IntervalIntegrable.mono_set' h; grind [uIoc, uIcc]),
        MeasureTheory.Measure.restrict_restrict_of_subset
          (subset_of_disjWithin hnI (Finset.mem_range.mp hi)),
        intervalIntegral.integral_symm, abs_neg,
        intervalIntegral.abs_intervalIntegral_eq]
    exact abs_integral_le_integral_abs
  ¬∑ intro i hi
    unfold IntegrableOn
    have h_subset := subset_of_disjWithin hnI (Finset.mem_range.mp hi)
    rw [MeasureTheory.Measure.restrict_restrict_of_subset h_subset]
    exact MeasureTheory.IntegrableOn.mono_set h.def'.norm h_subset |>.integrable

/-- If `f` has derivative `f'` a.e. on `[d, b]` and `Œ∑` is positive, then there is a countable
collection of pairwise disjoint closed subinterval of `[a, b]` of total length `b - a` where the
slope of `f` on each subinterval `[x, y]` differs from `f' x` by at most `Œ∑`. -/
lemma ae_hasDerivAt_exists_countable_pairwiseDisjoint_tsum_sub_eq_sub {f f' : ‚Ñù ‚Üí ‚Ñù} {d b Œ∑ : ‚Ñù}
    (hdb : d ‚â§ b)
    (hf : ‚àÄ·µê x, x ‚àà Icc d b ‚Üí HasDerivAt f (f' x) x) (hŒ∑ : 0 < Œ∑) :
    ‚àÉ u : Set (‚Ñù √ó ‚Ñù),
      (‚àÄ z ‚àà u, (d < z.1 ‚àß z.1 < z.2 ‚àß z.2 < b) ‚àß dist (slope f z.1 z.2) (f' z.1) < Œ∑) ‚àß
      u.PairwiseDisjoint (fun z ‚Ü¶ Icc z.1 z.2) ‚àß
      HasSum (fun (z : u) ‚Ü¶ z.val.2 - z.val.1) (b - d) := by
  by_cases hdb : d = b
  ¬∑ use ‚àÖ
    simp [hdb]
  replace hdb : d < b := by grind
  replace hf : ‚àÄ·µê x, x ‚àà Ioo d b ‚Üí HasDerivAt f (f' x) x := by
    filter_upwards [hf] with x hx1 hx2
    exact hx1 (Ioo_subset_Icc_self hx2)
  let t := {z : ‚Ñù √ó ‚Ñù | (d < z.1 ‚àß z.1 < z.2 ‚àß z.2 < b) ‚àß dist (slope f z.1 z.2) (f' z.1) < Œ∑}
  let s := {x : ‚Ñù | x ‚àà Ioo d b ‚àß HasDerivAt f (f' x) x}
  have : ‚àÉ u ‚äÜ t, u.Countable ‚àß u.PairwiseDisjoint (fun z ‚Ü¶ Icc z.1 z.2) ‚àß
      volume (s \ ‚ãÉ z ‚àà u, Icc z.1 z.2) = 0 := by
    apply Vitali.exists_disjoint_covering_ae' volume s t 6 (Prod.snd - Prod.fst) Prod.fst
      (fun z ‚Ü¶ Icc z.1 z.2)
    ¬∑ simp only [Icc, Metric.closedBall, Real.dist_eq, Pi.sub_apply, abs_le', tsub_le_iff_right,
      sub_add_cancel, neg_sub, setOf_subset_setOf, and_imp, Prod.forall]
      intros; constructor <;> linarith
    ¬∑ intro A hA
      simp only [Pi.sub_apply, Real.volume_closedBall, ENNReal.coe_ofNat, Real.volume_Icc]
      rw [show 6 = ENNReal.ofReal 6 by norm_num, ‚Üê ENNReal.ofReal_mul (by norm_num),
          ENNReal.ofReal_le_ofReal_iff (by simp only [mem_setOf_eq, t] at hA; linarith)]
      linarith
    ¬∑ simp +contextual [t]
    ¬∑ simp [isClosed_Icc]
    ¬∑ intro x hx
      apply Filter.Eventually.frequently
      have := hasDerivAt_iff_tendsto_slope.mp hx.right
      simp only at this
      obtain ‚ü®Œ¥, hŒ¥‚ÇÅ, hŒ¥‚ÇÇ‚ü© := (Metric.tendsto_nhdsWithin_nhds).mp
        (hasDerivAt_iff_tendsto_slope.mp hx.right) Œ∑ hŒ∑
      have evn_bound {Œ± : ‚Ñù} (hŒ± : 0 < Œ±) : ‚àÄ·∂† (Œµ : ‚Ñù) in ùìù[>] 0, Œµ < Œ± := by
        rw [eventually_nhdsWithin_iff, eventually_nhds_iff]
        refine ‚ü®Ioo (-Œ±) Œ±, by grind, isOpen_Ioo, by grind‚ü©
      have evn_pos : ‚àÄ·∂† (Œµ : ‚Ñù) in ùìù[>] 0, 0 < Œµ :=
        eventually_mem_of_tendsto_nhdsWithin (fun _ a ‚Ü¶ a)
      filter_upwards [evn_pos, evn_bound hŒ∑, evn_bound hŒ¥‚ÇÅ,
                      @evn_bound ((b - x) / 2) (by simp [hx.left.right])]
        with Œµ hŒµ‚ÇÅ hŒµ‚ÇÇ hŒµ‚ÇÉ hŒµ‚ÇÑ
      use (x, x + Œµ)
      repeat' constructor
      ¬∑ exact hx.left.left
      ¬∑ linarith
      ¬∑ linarith
      ¬∑ apply hŒ¥‚ÇÇ
        ¬∑ grind
        ¬∑ simp [abs_eq_self.mpr hŒµ‚ÇÅ.le, hŒµ‚ÇÉ]
      ¬∑ simp
  obtain ‚ü®u, ‚ü®hu‚ÇÅ, hu‚ÇÇ, hu‚ÇÉ, hu‚ÇÑ‚ü©‚ü© := this
  simp only [t, Set.subset_def, mem_setOf_eq] at hu‚ÇÅ
  refine ‚ü®u, ‚ü®hu‚ÇÅ, hu‚ÇÉ, ?_‚ü©‚ü©
  have : Countable u := by simp [hu‚ÇÇ]
  have : Pairwise (Disjoint on fun (z : u) ‚Ü¶ Icc z.val.1 z.val.2) :=
    fun z‚ÇÅ z‚ÇÇ hz‚ÇÅz‚ÇÇ ‚Ü¶ hu‚ÇÉ z‚ÇÅ.prop z‚ÇÇ.prop (Subtype.coe_ne_coe.mpr hz‚ÇÅz‚ÇÇ)
  replace hu‚ÇÑ : volume (Ioo d b \ ‚ãÉ z ‚àà u, Icc z.1 z.2) = 0 := by
    rw [measure_eq_zero_iff_ae_notMem] at hu‚ÇÑ ‚ä¢
    filter_upwards [hf, hu‚ÇÑ] with x hx‚ÇÅ hx‚ÇÇ
    grind
  have vol_sum : volume (‚ãÉ z : u, Icc z.val.1 z.val.2) = ENNReal.ofReal (b - d) := by
    convert Real.volume_Ioo ‚ñ∏
      measure_eq_measure_of_null_diff (by simp only [iUnion_subset_iff]; grind) hu‚ÇÑ
      using 2
    simp
  rw [measure_iUnion this (by simp)] at vol_sum
  simp_rw [Real.volume_Icc] at vol_sum
  apply_fun fun x ‚Ü¶ x.toReal at vol_sum
  rw [ENNReal.tsum_toReal_eq (by simp), ENNReal.toReal_ofReal (by linarith)] at vol_sum
  rw [‚Üê Summable.hasSum_iff (by rw [tsum_def] at vol_sum; grind)] at vol_sum
  convert vol_sum with z
  rw [ENNReal.toReal_ofReal]
  linarith [hu‚ÇÅ z.val z.prop]


section IntervalGapsWithin

namespace Finset

variable (F : Finset (‚Ñù √ó ‚Ñù)) (a b : ‚Ñù) {i : ‚Ñï}

noncomputable def intervalGapsWithin (i : ‚Ñï) : ‚Ñù √ó ‚Ñù := (fst, snd) where
  fst := match i with
    | 0 => a
    | i + 1 => if hi : i < F.card then F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl ‚ü®i, hi‚ü© |>.2 else a
  snd := if hi : i < F.card then F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl ‚ü®i, hi‚ü© |>.1 else b

@[simp]
theorem intervalGapsWithin_zero_fst : (F.intervalGapsWithin a b 0).1 = a := by
  simp [intervalGapsWithin, intervalGapsWithin.fst]

@[simp]
theorem intervalGapsWithin_fst_of_card_lt (hi : F.card < i) :
    (F.intervalGapsWithin a b i).1 = a := by
  simp only [intervalGapsWithin, intervalGapsWithin.fst]
  grind

@[simp]
theorem intervalGapsWithin_card_snd : (F.intervalGapsWithin a b F.card).2 = b := by
  simp [intervalGapsWithin, intervalGapsWithin.snd]

@[simp]
theorem intervalGapsWithin_snd_of_card_le (hi : F.card ‚â§ i) :
    (F.intervalGapsWithin a b i).2 = b := by
  simp only [intervalGapsWithin, intervalGapsWithin.snd]
  grind

@[simp]
theorem intervalGapsWithin_snd_of_card_eq (hi : F.card = i) :
    (F.intervalGapsWithin a b i).2 = b :=
  intervalGapsWithin_snd_of_card_le F a b hi.le

theorem intervalGapsWithin_succ_fst_of_lt_card (hi : i < F.card) :
    (F.intervalGapsWithin a b (i + 1)).1 = (F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl ‚ü®i, hi‚ü©).2 := by
  simp [intervalGapsWithin, intervalGapsWithin.fst, hi]

theorem intervalGapsWithin_fst_of_zero_lt_le_card (hi‚ÇÄ : 0 < i) (hi : i ‚â§ F.card) :
    (F.intervalGapsWithin a b i).1 =
      (F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl ‚ü®i - 1, Nat.sub_one_lt_of_le hi‚ÇÄ hi‚ü©).2 := by
  convert F.intervalGapsWithin_succ_fst_of_lt_card a b (i := i - 1) (by omega)
  omega

theorem intervalGapsWithin_snd_of_lt_card (hi : i < F.card) :
    (F.intervalGapsWithin a b i).2 = (F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl ‚ü®i, hi‚ü©).1 := by
  simp [intervalGapsWithin, intervalGapsWithin.snd, hi]

theorem intervalGapsWithin_mapsTo :
    (Set.Iio F.card).MapsTo
      (fun i ‚Ü¶ ((F.intervalGapsWithin a b i).2, (F.intervalGapsWithin a b (i + 1)).1)) F := by
  intro i hi
  rw [Set.mem_Iio] at hi
  simp only [hi, intervalGapsWithin_snd_of_lt_card, intervalGapsWithin_succ_fst_of_lt_card]
  convert F.orderEmbOfFin_mem rfl ‚ü®i, hi‚ü© using 1

theorem intervalGapsWithin_injOn :
    (Set.Iio F.card).InjOn
      (fun i ‚Ü¶ ((F.intervalGapsWithin a b i).2, (F.intervalGapsWithin a b (i + 1)).1)) := by
  intro i hi j hj
  rw [Set.mem_Iio] at hi hj
  simp only [hi, hj, intervalGapsWithin_snd_of_lt_card, intervalGapsWithin_succ_fst_of_lt_card]
  exact fun hij ‚Ü¶ Fin.ext_iff.mp (F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl |>.injective hij)

theorem intervalGapsWithin_surjOn :
    (Set.Iio F.card).SurjOn
      (fun i ‚Ü¶ ((F.intervalGapsWithin a b i).2, (F.intervalGapsWithin a b (i + 1)).1)) F := by
  intro z hz
  rw [‚Üê F.range_orderEmbOfFin rfl (Œ± := ‚Ñù √ó‚Çó ‚Ñù)] at hz
  obtain ‚ü®i, hi‚ü© := hz
  use i.val, i.prop
  simp [i.prop, intervalGapsWithin_snd_of_lt_card, intervalGapsWithin_succ_fst_of_lt_card, hi]

theorem intervalGapsWithin_le_fst {a b : ‚Ñù} (hFab : ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà F ‚Üí a ‚â§ z.1 ‚àß z.1 ‚â§ z.2 ‚àß z.2 ‚â§ b)
    (i : ‚Ñï) :
    a ‚â§ (F.intervalGapsWithin a b i).1 := by
  by_cases hi : i = 0 ‚à® F.card < i
  ¬∑ rcases hi with hi | hi <;> simp [hi]
  ¬∑ have := hFab (F.intervalGapsWithin_mapsTo a b (x := i - 1) (by grind))
    grind

theorem intervalGapsWithin_snd_le {a b : ‚Ñù} (hFab : ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà F ‚Üí a ‚â§ z.1 ‚àß z.1 ‚â§ z.2 ‚àß z.2 ‚â§ b)
    (i : ‚Ñï) :
    (F.intervalGapsWithin a b i).2 ‚â§ b := by
  by_cases hi : F.card ‚â§ i
  ¬∑ simp [hi]
  ¬∑ have := hFab (F.intervalGapsWithin_mapsTo a b (x := i) (by grind))
    grind

theorem intervalGapsWithin_fst_le_snd {a b : ‚Ñù} (hab : a ‚â§ b)
    (hFab : ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà F ‚Üí a ‚â§ z.1 ‚àß z.1 ‚â§ z.2 ‚àß z.2 ‚â§ b)
    (hF : (SetLike.coe F).PairwiseDisjoint (fun z ‚Ü¶ Set.Icc z.1 z.2)) (i : ‚Ñï) :
    (F.intervalGapsWithin a b i).1 ‚â§ (F.intervalGapsWithin a b i).2 := by
  by_cases hi : i ‚â§ F.card
  swap
  ¬∑ rwa [intervalGapsWithin_fst_of_card_lt _ _ _ (by omega),
      intervalGapsWithin_snd_of_card_le _ _ _ (by omega)]
  by_cases hi‚ÇÅ : i = 0
  ¬∑ simp only [hi‚ÇÅ, intervalGapsWithin_zero_fst]
    by_cases hi‚ÇÇ : F.card = 0
    ¬∑ simp [hi‚ÇÇ, hab]
    ¬∑ exact hFab (F.intervalGapsWithin_mapsTo a b (by grind)) |>.left
  ¬∑ by_cases hi‚ÇÇ : F.card = i
    ¬∑ simp only [hi‚ÇÇ.le, intervalGapsWithin_snd_of_card_le]
      convert hFab (F.intervalGapsWithin_mapsTo a b (x := i - 1) (by grind)) |>.right.right using 1
      simp only
      congr
      omega
    ¬∑ replace hi‚ÇÇ : i < F.card := by omega
      replace hi‚ÇÅ : 0 < i := Nat.zero_lt_of_ne_zero hi‚ÇÅ
      simp only [hi‚ÇÇ, hi‚ÇÅ, hi, intervalGapsWithin_snd_of_lt_card,
        intervalGapsWithin_fst_of_zero_lt_le_card]
      set G := F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl
      have hi' : (‚ü®i - 1, by omega‚ü© : Fin F.card) < ‚ü®i, hi‚ÇÇ‚ü© := Fin.mk_lt_mk.mpr (by omega)
      have hG : (G ‚ü®i - 1, by omega‚ü©).1 ‚â§ (G ‚ü®i, hi‚ÇÇ‚ü©).1 :=
        Prod.Lex.le_iff'.mp (G.monotone hi'.le) |>.left
      have := hF (by simp [G, F.orderEmbOfFin_mem (Œ± := ‚Ñù √ó‚Çó ‚Ñù)])
        (by simp [G, F.orderEmbOfFin_mem (Œ± := ‚Ñù √ó‚Çó ‚Ñù)]) (G.injective.ne hi'.ne)
      contrapose! this
      simp only [Set.not_disjoint_iff, Set.mem_Icc]
      use (G ‚ü®i, hi‚ÇÇ‚ü©).1
      have hFabi := hFab (z := G ‚ü®i, hi‚ÇÇ‚ü©) (by simp [G, F.orderEmbOfFin_mem (Œ± := ‚Ñù √ó‚Çó ‚Ñù)])
      simp [hFabi, this.le, hG]

theorem intervalGapsWithin_pairwiseDisjoint_Ioc {a b : ‚Ñù}
    (hFab : ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà F ‚Üí a ‚â§ z.1 ‚àß z.1 ‚â§ z.2 ‚àß z.2 ‚â§ b) :
    (Set.Iio (F.card + 1)).PairwiseDisjoint
      (fun i ‚Ü¶ Set.Ioc (F.intervalGapsWithin a b i).1 (F.intervalGapsWithin a b i).2) := by
  intro i hi j hj hij
  wlog hij' : i < j generalizing i j
  ¬∑ exact (this hj hi hij.symm (by omega)).symm
  ¬∑ rw [onFun, Set.disjoint_iff_inter_eq_empty]
    suffices (F.intervalGapsWithin a b i).2 ‚â§ (F.intervalGapsWithin a b j).1 by grind
    have hi : i < F.card := by grind
    have hj : j - 1 < F.card := by grind
    have hij'' : (‚ü®i, hi‚ü© : Fin F.card) ‚â§ ‚ü®j - 1, hj‚ü© := Fin.mk_le_mk.mpr (by omega)
    trans (F.intervalGapsWithin a b (j - 1)).2
    ¬∑ simp only [hi, hj, intervalGapsWithin_snd_of_lt_card]
      exact Prod.Lex.le_iff'.mp (F.orderEmbOfFin (Œ± := ‚Ñù √ó‚Çó ‚Ñù) rfl |>.monotone hij'') |>.left
    ¬∑ have := hFab (intervalGapsWithin_mapsTo F a b (x := j - 1) (by grind))
      grind

end Finset

end IntervalGapsWithin


theorem Finset.sum_intervalGapsWithin_add_sum_eq_sub (F : Finset (‚Ñù √ó ‚Ñù)) {a b : ‚Ñù} (g : ‚Ñù ‚Üí ‚Ñù) :
    ‚àë i ‚àà Finset.range (F.card + 1),
      (g (F.intervalGapsWithin a b i).2 - g (F.intervalGapsWithin a b i).1) +
    ‚àë z ‚àà F, (g z.2 - g z.1) = g b - g a := by
  let p := F.intervalGapsWithin a b
  have := Finset.sum_bij (s := Finset.range F.card) (t := F) (g := fun z ‚Ü¶ g z.2 - g z.1)
    (f := fun i ‚Ü¶ (g (p (i + 1)).1 - g (p i).2))
    (fun i hi ‚Ü¶ ((p i).2, (p (i + 1)).1))
    (fun i hi ‚Ü¶ F.intervalGapsWithin_mapsTo a b (x := i) (by grind))
    (fun i hi j hj hij ‚Ü¶ F.intervalGapsWithin_injOn a b (by grind) (by grind) hij)
    (fun z hz ‚Ü¶ by
      obtain ‚ü®i, hi‚ÇÅ, hi‚ÇÇ‚ü© := F.intervalGapsWithin_surjOn a b hz
      exact ‚ü®i, by grind, hi‚ÇÇ‚ü©)
    (by simp)
  rw [‚Üê this, add_comm, Finset.sum_range_succ, ‚Üê add_assoc,
      ‚Üê Finset.sum_add_distrib,
      Finset.sum_congr rfl (fun _ _ ‚Ü¶ sub_add_sub_cancel _ _ _),
      Finset.sum_range_sub (fun i ‚Ü¶ g (F.intervalGapsWithin a b i).1)]
  simp

theorem Finset.sum_intervalGapsWithin_eq_sub_sub_sum (F : Finset (‚Ñù √ó ‚Ñù)) {a b : ‚Ñù} (g : ‚Ñù ‚Üí ‚Ñù) :
    ‚àë i ‚àà Finset.range (F.card + 1),
      (g (F.intervalGapsWithin a b i).2 - g (F.intervalGapsWithin a b i).1) =
    g b - g a - ‚àë z ‚àà F, (g z.2 - g z.1) :=
  eq_sub_iff_add_eq.mpr (F.sum_intervalGapsWithin_add_sum_eq_sub g)

lemma AbsolutelyContinuousOnInterval.dist_le_of_pairwiseDisjoint_hasSum {f : ‚Ñù ‚Üí ‚Ñù}
    {d b y : ‚Ñù}
    (hdb : d ‚â§ b) (hf : AbsolutelyContinuousOnInterval f d b)
    {u : Set (‚Ñù √ó ‚Ñù)}
    (hu‚ÇÅ : ‚àÄ z ‚àà u, d < z.1 ‚àß z.1 < z.2 ‚àß z.2 < b)
    (hu‚ÇÇ : u.PairwiseDisjoint (fun z ‚Ü¶ Icc z.1 z.2))
    (hu‚ÇÉ : HasSum (fun (z : u) ‚Ü¶ z.val.2 - z.val.1) (b - d))
    (hu‚ÇÑ : HasSum (fun (z : u) ‚Ü¶ dist (f z.val.1) (f z.val.2)) y) :
    dist (f d) (f b) ‚â§ y := by
  let u_coe (s : Finset u) : Finset (‚Ñù √ó ‚Ñù) := s.image Subtype.val
  replace hu‚ÇÅ (s : Finset u) : ‚àÄ ‚¶Éz : ‚Ñù √ó ‚Ñù‚¶Ñ, z ‚àà u_coe s ‚Üí d ‚â§ z.1 ‚àß z.1 ‚â§ z.2 ‚àß z.2 ‚â§ b := by
    intro z hz
    have := hu‚ÇÅ z (by grind)
    grind
  replace hu‚ÇÇ (s : Finset u) : (SetLike.coe (u_coe s)).PairwiseDisjoint fun z ‚Ü¶ Icc z.1 z.2 :=
    hu‚ÇÇ.subset (by grind)
  let T (s : Finset u) := ((u_coe s).card + 1, (u_coe s).intervalGapsWithin d b)
  have hT‚ÇÅ (s : Finset u) (i : ‚Ñï) := (u_coe s).intervalGapsWithin_le_fst (hu‚ÇÅ s) i
  have hT‚ÇÇ (s : Finset u) (i : ‚Ñï) :=
    (u_coe s).intervalGapsWithin_fst_le_snd hdb (hu‚ÇÅ s) (hu‚ÇÇ s) i
  have hT‚ÇÉ (s : Finset u) (i : ‚Ñï) := (u_coe s).intervalGapsWithin_snd_le (hu‚ÇÅ s) i
  have hT‚ÇÑ (s : Finset u) := (u_coe s).intervalGapsWithin_pairwiseDisjoint_Ioc (hu‚ÇÅ s)
  have hT : univ.MapsTo T (disjWithin d b) := by
    intro s _
    simp only [disjWithin, Finset.mem_range, Finset.coe_range, mem_setOf_eq, T]
    constructor
    ¬∑ simp only [uIcc_of_le hdb, mem_Icc]
      grind
    ¬∑ convert hT‚ÇÑ s using 2 with i
      exact uIoc_of_le (hT‚ÇÇ s i)
  have u_coe_sum (s : Finset u) (g : ‚Ñù ‚Üí ‚Ñù ‚Üí ‚Ñù) :
      ‚àë b ‚àà s, (g b.val.1 b.val.2) = ‚àë z ‚àà u_coe s, (g z.1 z.2) :=
    Finset.sum_nbij Subtype.val (by simp [u_coe]) (by simp)
      (by simp only [Finset.coe_image, u_coe]; tauto) (by simp)
  replace hu‚ÇÉ : Tendsto T atTop (totalLengthFilter ‚äì ùìü (disjWithin d b)) := by
    refine tendsto_inf.mpr ‚ü®?_, hT.tendsto.mono_left (by simp)‚ü©
    simp only [totalLengthFilter, tendsto_comap_iff]
    convert hu‚ÇÉ.const_sub (b - d) with s
    ¬∑ simp only [comp_apply]
      rw [Finset.sum_congr rfl (g := fun i ‚Ü¶ ((T s).2 i).2 - ((T s).2 i).1)
            (fun i hi ‚Ü¶ by rw [dist_comm, Real.dist_eq, abs_of_nonneg (by grind)])]
      convert (u_coe s).sum_intervalGapsWithin_eq_sub_sub_sum id
      exact u_coe_sum s fun x y ‚Ü¶ y - x
    ¬∑ abel
  rw [HasSum] at hu‚ÇÑ
  simp_rw [u_coe_sum _ fun x y ‚Ü¶ dist (f x) (f y)] at hu‚ÇÑ
  have sum_tendsto := hf.comp hu‚ÇÉ |>.add hu‚ÇÑ
  simp only [comp_apply, zero_add] at sum_tendsto
  have dist_le_sum (s : Finset u) :
      dist (f d) (f b) ‚â§
      ‚àë i ‚àà Finset.range (T s).1, dist (f ((T s).2 i).1) (f ((T s).2 i).2) +
      (‚àë b ‚àà u_coe s, dist (f b.1) (f b.2)) := by
    rw [dist_comm, Finset.sum_congr rfl fun i hi ‚Ü¶ dist_comm (f ((T s).2 i).1) _,
        Finset.sum_congr rfl fun (b : ‚Ñù √ó ‚Ñù) hb ‚Ü¶ dist_comm (f b.1) _]
    simp_rw [Real.dist_eq]
    rw [‚Üê (u_coe s).sum_intervalGapsWithin_add_sum_eq_sub]
    grw [abs_add_le, Finset.abs_sum_le_sum_abs, Finset.abs_sum_le_sum_abs]
  exact le_of_tendsto_of_tendsto' (by simp) sum_tendsto dist_le_sum

theorem Real.tsum_le_of_sum_le {Œπ : Type*} {f : Œπ ‚Üí ‚Ñù} {c : ‚Ñù} (hf : 0 ‚â§ f)
    (h : ‚àÄ u : Finset Œπ, ‚àë x ‚àà u, f x ‚â§ c) : ‚àë' x, f x ‚â§ c :=
  (summable_of_sum_le hf h).tsum_le_of_sum_le h

/-- If `f` is absolutely continuous on `uIcc a b` and `f' x = 0` for a.e. `x ‚àà uIcc a b`, then `f`
-- is constant on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_deriv_zero_const {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : AbsolutelyContinuousOnInterval f a b)
    (hf‚ÇÄ : ‚àÄ·µê x, x ‚àà uIcc a b ‚Üí HasDerivAt f 0 x) :
    ‚àÉ C, ‚àÄ x ‚àà uIcc a b, f x = C := by
  wlog hab : a ‚â§ b
  ¬∑ exact uIcc_comm b a ‚ñ∏ @this f b a hf.symm (uIcc_comm a b ‚ñ∏ hf‚ÇÄ) (by linarith)
  suffices ‚àÄ x ‚àà uIcc a b, f x = f b by use f b
  rw [uIcc_of_le hab] at hf‚ÇÄ ‚ä¢
  intro d hd
  suffices ‚àÄ r > 0, dist (f d) (f b) ‚â§ r by
    contrapose! this
    exact exists_between (dist_pos.mpr this)
  intro r hr
  rw [mem_Icc] at hd
  have had : a ‚â§ d := by linarith
  by_cases hdb‚ÇÄ : d = b
  ¬∑ simp [hdb‚ÇÄ, hr.le]
  have hdb : d < b := by grind
  replace hf‚ÇÄ : ‚àÄ·µê x, x ‚àà Icc d b ‚Üí HasDerivAt f 0 x := by
    filter_upwards [hf‚ÇÄ] with x hx1 hx2
    apply hx1
    suffices Icc d b ‚äÜ Icc a b from this hx2
    gcongr
  have hfdb': 0 < r / (b - d) := by apply div_pos <;> linarith
  have ‚ü®u, hu‚ÇÅ, hu‚ÇÇ, hu‚ÇÉ‚ü© :=
    ae_hasDerivAt_exists_countable_pairwiseDisjoint_tsum_sub_eq_sub hd.right hf‚ÇÄ hfdb'
  let g := fun (z : u) ‚Ü¶ dist (f z.val.1) (f z.val.2)
  have g_nonneg : 0 ‚â§ g := by intro; simp [g]
  have g_finsum_bound (s : Finset u) : ‚àë z ‚àà s, g z ‚â§ r := by
    have (z : u) (hz : z ‚àà s) : g z ‚â§ r / (b - d) * (z.val.2 - z.val.1) := by
      have slope_bound := hu‚ÇÅ z (by simp) |>.right |>.le
      have : 0 < z.val.2 - z.val.1 := by linarith [hu‚ÇÅ z (by simp)]
      simp only [Real.dist_eq, slope, vsub_eq_sub, smul_eq_mul, sub_zero, abs_mul,
        abs_inv] at slope_bound
      rwa [inv_mul_le_iff‚ÇÄ' (abs_pos_of_pos this), abs_of_pos this, abs_sub_comm] at slope_bound
    grw [Finset.sum_le_sum this]
    rw [‚Üê Finset.mul_sum]
    have : ‚àë z ‚àà s, (z.val.2 - z.val.1) ‚â§ b - d :=
      hu‚ÇÉ.tsum_eq ‚ñ∏ Summable.sum_le_tsum _ (by grind) hu‚ÇÉ.summable
    grw [this]
    field_simp
    grind
  have hu‚ÇÑ := summable_of_sum_le g_nonneg g_finsum_bound |>.hasSum
  have g_sum_bound := Real.tsum_le_of_sum_le g_nonneg g_finsum_bound
  have := (hf.mono (by grind [uIcc_of_le])).dist_le_of_pairwiseDisjoint_hasSum hd.right
    (fun s hs ‚Ü¶ hu‚ÇÅ s hs |>.left) hu‚ÇÇ hu‚ÇÉ hu‚ÇÑ
  grind

/-- *Fundamental Theorem of Calculus* for absolutely continuous functions: if `f` is absolutely
continuous on `uIcc a b`, then `‚à´ (x : ‚Ñù) in a..b, deriv f x = f b - f a`. -/
theorem AbsolutelyContinuousOnInterval.integral_deriv_eq_sub {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    ‚à´ (x : ‚Ñù) in a..b, deriv f x = f b - f a := by
  have f_deriv_integral_ac :=
    hf.intervalIntegrable_deriv.absolutelyContinuousOnInterval_intervalIntegral
    (c := a) (by simp)
  let g (x : ‚Ñù) := f x - ‚à´ (t : ‚Ñù) in a..x, deriv f t
  have g_ac : AbsolutelyContinuousOnInterval g a b := hf.sub (f_deriv_integral_ac)
  have g_ae_deriv_zero : ‚àÄ·µê x, x ‚àà uIcc a b ‚Üí HasDerivAt g 0 x := by
    filter_upwards [hf.ae_differentiableAt, hf.intervalIntegrable_deriv.ae_hasDerivAt_integral]
      with x hx1 hx2 hx3
    convert (hx1 hx3).hasDerivAt.sub (hx2 hx3 a (by simp))
    abel
  obtain ‚ü®C, hC‚ü© := g_ac.ae_deriv_zero_const g_ae_deriv_zero
  have : f a = g a := by simp [g]
  have := hC a (by simp)
  have := hC b (by simp)
  grind

/-- The integral of the derivative of a product of two absolutely continuous functions. -/
theorem AbsolutelyContinuousOnInterval.integral_deriv_mul_eq_sub
    {f g : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : AbsolutelyContinuousOnInterval f a b) (hg : AbsolutelyContinuousOnInterval g a b) :
    ‚à´ x in a..b, deriv f x * g x + f x * deriv g x = f b * g b - f a * g a := by
  rw [‚Üê (hf.fun_mul hg).integral_deriv_eq_sub]
  apply intervalIntegral.integral_congr_ae
  filter_upwards [hf.ae_differentiableAt, hg.ae_differentiableAt] with x hx‚ÇÅ hx‚ÇÇ hx‚ÇÉ
  have hx‚ÇÑ : x ‚àà uIcc a b := by grind [uIcc, uIoc]
  have hx‚ÇÖ := (hx‚ÇÅ hx‚ÇÑ).hasDerivAt.mul (hx‚ÇÇ hx‚ÇÑ).hasDerivAt
  exact hx‚ÇÖ.deriv.symm

/-- *Integration by parts* for absolutely continuous functions. -/
theorem AbsolutelyContinuousOnInterval.integral_mul_deriv_eq_deriv_mul
    {f g : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : AbsolutelyContinuousOnInterval f a b) (hg : AbsolutelyContinuousOnInterval g a b) :
    ‚à´ x in a..b, f x * deriv g x = f b * g b - f a * g a - ‚à´ x in a..b, deriv f x * g x := by
  rw [‚Üê AbsolutelyContinuousOnInterval.integral_deriv_mul_eq_sub hf hg,
      ‚Üê intervalIntegral.integral_sub]
  ¬∑ simp_rw [add_sub_cancel_left]
  ¬∑ exact (hf.intervalIntegrable_deriv.mul_continuousOn hg.continuousOn).add
      (hg.intervalIntegrable_deriv.continuousOn_mul hf.continuousOn)
  ¬∑ exact hf.intervalIntegrable_deriv.mul_continuousOn hg.continuousOn
