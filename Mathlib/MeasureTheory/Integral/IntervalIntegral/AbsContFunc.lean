/-
Copyright (c) 2025 Yizheng Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yizheng Zhu
-/
import Mathlib.Analysis.BoundedVariation
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.MeasureTheory.Covering.Vitali
import Mathlib.MeasureTheory.Function.AbsolutelyContinuous
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
import Mathlib.MeasureTheory.Integral.IntervalIntegral.DerivIntegrable
import Mathlib.MeasureTheory.Integral.IntervalIntegral.LebesgueDifferentiationThm
import Mathlib.MeasureTheory.Integral.Lebesgue.Basic
import Mathlib.MeasureTheory.Measure.MeasureSpace

/-!
# Fundamental Theorem of Calculus and Integration by Parts for Absolutely Continuous Functions

This file proves that:
* If `f` is absolutely continuous on `uIcc a b`, then *Fundamental Theorem of Calculus* holds for
`f'` on `a..b`, i.e. `âˆ« (x : â„) in a..b, deriv f x = f b - f a`.
* *Integration by Parts* holds for absolutely continuous functions.

## Implementation notes

We need to prove that `f'` is interval integrable on `a..b` for any monotone function `f`. The
proof uses Fatou's lemma and is proved in `MonotoneOn.deriv_intervalIntegrable`. From this we get
`f'` is interval integrable on `a..b` for BV functions, proved in
`LocallyBoundedVariationOn.deriv_intervalIntegrable`,
and finally for absolutely continuous functions, proved in
`AbsolutelyContinuousOnInterval.deriv_intervalIntegrable`.

## Tags
absolutely continuous, fundamental theorem of calculus, integration by parts
-/

open MeasureTheory Set Filter Function AbsolutelyContinuousOnInterval

open scoped Topology ENNReal Interval NNReal

/-- If `f` is interval integrable on `a..b` and `c âˆˆ uIcc a b`, then `fun x â†¦ âˆ« v in c..x, f v` is
absolute continuous on `uIcc a b`. -/
theorem IntervalIntegrable.integral_absolutelyContinuousOnInterval {f : â„ â†’ â„} {a b c : â„}
    (h : IntervalIntegrable f volume a b) (hc : c âˆˆ uIcc a b) :
    AbsolutelyContinuousOnInterval (fun x â†¦ âˆ« v in c..x, f v) a b := by
  let s := fun E : â„• Ã— (â„• â†’ â„ Ã— â„) â†¦ â‹ƒ i âˆˆ Finset.range E.1, uIoc (E.2 i).1 (E.2 i).2
  have : Tendsto (â‡‘(volume.restrict (uIoc a b)) âˆ˜ s) (totalLengthFilter âŠ“ ğ“Ÿ (disjWithin a b))
      (ğ“ 0) := by
    rw [(hasBasis_totalLengthFilter.inf_principal _).tendsto_iff ENNReal.nhds_zero_basis_Iic]
    intro Îµ hÎµ
    by_cases hÎµ_top : Îµ = âŠ¤
    Â· exact âŸ¨1, by simp, by simp[hÎµ_top]âŸ©
    replace hÎµ := ENNReal.toReal_pos (hÎµ.ne.symm) hÎµ_top
    refine âŸ¨Îµ.toReal, hÎµ, fun (n, I) hnI â†¦ ?_âŸ©
    rw [mem_inter_iff] at hnI
    simp only [comp_apply, mem_Iic, s]
    rw [Measure.restrict_eq_self (h := union_subset_of_disjWithin hnI.right)]
    simp only [disjWithin, mem_setOf_eq] at hnI
    obtain âŸ¨hnIâ‚, hnIâ‚‚, hnIâ‚ƒâŸ© := hnI
    rw [MeasureTheory.measure_biUnion_finset hnIâ‚ƒ (by simp [uIoc])]
    calc âˆ‘ i âˆˆ Finset.range n, volume (uIoc (I i).1 (I i).2)
      _ = âˆ‘ i âˆˆ Finset.range n, ENNReal.ofReal ((dist (I i).1 (I i).2)) := by
        apply Finset.sum_congr rfl
        simp [uIoc, Real.dist_eq, max_sub_min_eq_abs']
      _ = ENNReal.ofReal (âˆ‘ i âˆˆ Finset.range n, (dist (I i).1 (I i).2)) := by
        simp [ENNReal.ofReal_sum_of_nonneg]
      _ â‰¤ ENNReal.ofReal Îµ.toReal :=
        ENNReal.ofReal_lt_ofReal_iff hÎµ |>.mpr hnIâ‚ |>.le
      _ â‰¤ Îµ := ENNReal.ofReal_toReal_le
  have := MeasureTheory.tendsto_setLIntegral_zero
    (ne_of_lt <| intervalIntegrable_iff.mp h |>.hasFiniteIntegral)
    (s := s)
    (l := totalLengthFilter âŠ“ ğ“Ÿ (disjWithin a b))
    this
  have := ENNReal.toReal_zero â–¸ (ENNReal.continuousAt_toReal (by simp)).tendsto.comp this
  refine squeeze_zero' ?_ ?_ this
  Â· filter_upwards with (n, I)
    exact Finset.sum_nonneg (fun _ _ â†¦ dist_nonneg)
  simp only [comp_apply, s]
  have : âˆ€á¶  (E : â„• Ã— (â„• â†’ â„ Ã— â„)) in totalLengthFilter âŠ“ ğ“Ÿ (disjWithin a b),
      E âˆˆ disjWithin a b :=
    eventually_inf_principal.mpr (by simp)
  filter_upwards [this] with (n, I) hnI
  obtain âŸ¨hnI1, hnI2âŸ© := mem_setOf_eq â–¸ hnI
  simp only
  rw [â† MeasureTheory.integral_norm_eq_lintegral_enorm (h.aestronglyMeasurable_uIoc.restrict),
      MeasureTheory.integral_biUnion_finset _ (by simp +contextual [uIoc]) hnI2]
  Â· refine Finset.sum_le_sum (fun i hi â†¦ ?_)
    rw [Real.dist_eq,
        intervalIntegral.integral_interval_sub_left
          (by apply IntervalIntegrable.mono_set' h; grind [uIoc, uIcc])
          (by apply IntervalIntegrable.mono_set' h; grind [uIoc, uIcc]),
        MeasureTheory.Measure.restrict_restrict_of_subset
          (subset_of_disjWithin hnI (Finset.mem_range.mp hi)),
        intervalIntegral.integral_symm, abs_neg,
        intervalIntegral.abs_intervalIntegral_eq]
    exact abs_integral_le_integral_abs
  Â· intro i hi
    unfold IntegrableOn
    have h_subset := subset_of_disjWithin hnI (Finset.mem_range.mp hi)
    rw [MeasureTheory.Measure.restrict_restrict_of_subset h_subset]
    exact MeasureTheory.IntegrableOn.mono_set h.def'.norm h_subset |>.integrable

/-- If `f` has derivative 0 a.e. on `[d, b]`, then there is a coultable Vitali cover of `[d, b]`
a.e., consisting of closed intervals, where each has small variations wrt `f`. -/
lemma ae_deriv_zero_ctb_cover {f : â„ â†’ â„} {d b Î· : â„}
    (hf : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x) (hÎ· : 0 < Î·) :
    let t := {(x, h) : â„ Ã— â„ | d < x âˆ§ 0 < h âˆ§ x + h < b âˆ§ |f (x + h) - f x| < h * Î·};
    let B : â„ Ã— â„ â†’ Set â„ := fun (x, h) â†¦ Icc x (x + h);
    âˆƒ u âŠ† t, u.Countable âˆ§ u.PairwiseDisjoint B âˆ§ volume (Ioo d b \ â‹ƒ a âˆˆ u, B a) = 0 := by
  intro t B
  replace hf : âˆ€áµ x, x âˆˆ Ioo d b â†’ HasDerivAt f 0 x := by
    filter_upwards [hf] with x hx1 hx2
    exact hx1 (Ioo_subset_Icc_self hx2)
  let s := {x : â„ | x âˆˆ Ioo d b âˆ§ HasDerivAt f 0 x}
  have : âˆƒ u âŠ† t, u.Countable âˆ§ u.PairwiseDisjoint B âˆ§ volume (s \ â‹ƒ a âˆˆ u, B a) = 0 := by
    apply Vitali.exists_disjoint_covering_ae' volume s t 6 (Prod.snd) (Prod.fst) B
    Â· simp only [Icc, Metric.closedBall, Real.dist_eq, abs_le', tsub_le_iff_right, neg_sub,
      setOf_subset_setOf, and_imp, Prod.forall, B]
      intros; constructor <;> linarith
    Â· intro A hA
      simp only [Real.volume_closedBall, ENNReal.coe_ofNat, Real.volume_Icc, add_sub_cancel_left, B]
      rw [show 6 = ENNReal.ofReal 6 by norm_num, â† ENNReal.ofReal_mul (by norm_num),
          ENNReal.ofReal_le_ofReal_iff (by simp only [mem_setOf_eq, t] at hA; linarith)]
      linarith
    Â· simp +contextual [B, t]
    Â· simp [B, isClosed_Icc]
    Â· intro x hx
      apply Filter.Eventually.frequently
      have := hasDerivAt_iff_tendsto.mp hx.right
      simp only [Real.norm_eq_abs, smul_eq_mul, mul_zero, sub_zero] at this
      obtain âŸ¨Î´, hÎ´â‚, hÎ´â‚‚âŸ© := (Metric.tendsto_nhds_nhds).mp (hasDerivAt_iff_tendsto.mp hx.2) Î· hÎ·
      have evn_bound {Î± : â„} (hÎ± : 0 < Î±) : âˆ€á¶  (Îµ : â„) in ğ“[>] 0, Îµ < Î± := by
        rw [eventually_nhdsWithin_iff, eventually_nhds_iff]
        refine âŸ¨Ioo (-Î±) Î±, by grind, isOpen_Ioo, by grindâŸ©
      have evn_pos : âˆ€á¶  (Îµ : â„) in ğ“[>] 0, 0 < Îµ :=
        eventually_mem_of_tendsto_nhdsWithin (fun _ a â†¦ a)
      filter_upwards [evn_pos, evn_bound hÎ·, evn_bound hÎ´â‚,
                      @evn_bound ((b - x) / 2) (by simp [hx.left.right])]
        with Îµ hÎµâ‚ hÎµâ‚‚ hÎµâ‚ƒ hÎµâ‚„
      use (x, Îµ)
      repeat' constructor
      Â· exact hx.left.left
      Â· exact hÎµâ‚
      Â· linarith
      Â· specialize @hÎ´â‚‚ (x := x + Îµ) (by simp [abs_eq_self.mpr hÎµâ‚.le, hÎµâ‚ƒ])
        simp only [add_sub_cancel_left, Real.norm_eq_abs, smul_eq_mul, mul_zero, sub_zero,
          dist_zero_right, norm_mul, norm_inv, abs_abs] at hÎ´â‚‚
        rw [abs_eq_self.mpr hÎµâ‚.le, â† mul_lt_mul_iff_rightâ‚€ hÎµâ‚] at hÎ´â‚‚
        convert hÎ´â‚‚ using 1
        field_simp
  obtain âŸ¨u, âŸ¨huâ‚, huâ‚‚, huâ‚ƒ, huâ‚„âŸ©âŸ© := this
  refine âŸ¨u, âŸ¨huâ‚, huâ‚‚, huâ‚ƒ, ?_âŸ©âŸ©
  rw [MeasureTheory.measure_eq_zero_iff_ae_notMem] at huâ‚„ âŠ¢
  filter_upwards [hf, huâ‚„] with x hxâ‚ hxâ‚‚
  grind

/-- If `f` has derivative 0 a.e. on `[d, b]`, then there is a finite Vitali cover of `[d, b]`
except for measure at most `Î´`, consisting of closed intervals, where each has small variations
wrt `f`. -/
lemma ae_deriv_zero_fin_cover {f : â„ â†’ â„} {d b Î· Î´ : â„}
    (hf : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x)
    (hÎ· : 0 < Î·) (hÎ´ : 0 < Î´) :
    let t := {(x, h) : â„ Ã— â„ | d < x âˆ§ 0 < h âˆ§ x + h < b âˆ§ |f (x + h) - f x| < h * Î·};
    let B : â„ Ã— â„ â†’ Set â„ := fun (x, h) â†¦ Icc x (x + h);
    âˆƒ n : â„•, âˆƒ v : â„• â†’ â„ Ã— â„,
      Set.image v (Finset.range n) âŠ† t âˆ§
      Set.PairwiseDisjoint (Finset.range n) (fun i â†¦ B (v i)) âˆ§
      volume (Ioo d b \ â‹ƒ i âˆˆ Finset.range n, B (v i)) < ENNReal.ofReal Î´ := by
  intro t B
  obtain âŸ¨u, hu1, hu2, hu3, hu4âŸ© := ae_deriv_zero_ctb_cover hf hÎ·
  obtain âŸ¨e, heâŸ© := Set.countable_iff_exists_injOn.mp hu2
  have : Ioo d b \ â‹ƒ a âˆˆ u, B a = â‹‚ (i : â„•), (Ioo d b \ â‹ƒ a âˆˆ {x âˆˆ u | e x < i}, B a) := by
    ext x; simp only [mem_diff, mem_iUnion, exists_prop, not_exists, not_and,
      mem_setOf_eq, mem_iInter, and_imp]
    exact âŸ¨fun âŸ¨h1, h2âŸ© i â†¦ by constructor <;> simp +contextual [h1, h2],
           fun h â†¦ âŸ¨(h 0).left, fun x hx â†¦ (h (e x + 1)).right x hx (by omega)âŸ©âŸ©
  rw [this] at hu4
  rw [MeasureTheory.measure_iInter_eq_iInf_measure_iInter_le] at hu4
  Â· clear this
    replace hu4 := hu4.symm â–¸
      (show @OfNat.ofNat â„â‰¥0âˆ 0 Zero.toOfNat0 < ENNReal.ofReal Î´ by simp [hÎ´])
    obtain âŸ¨n, hnâŸ© := iInf_lt_iff.mp hu4
    classical
    let enum := (Finset.equivFin {j âˆˆ Finset.range n | âˆƒ x âˆˆ u, e x = j}).symm
    set n' := Finset.card ({j âˆˆ Finset.range n | âˆƒ x âˆˆ u, e x = j})
    have hvi {i : â„•} (hi : i < n') : âˆƒ x âˆˆ u, e x = enum âŸ¨i, hiâŸ© := by
      have := (enum âŸ¨i, hiâŸ©).property
      simp only [Finset.mem_filter, Finset.mem_range] at this
      tauto
    let v (i : â„•) : â„ Ã— â„ := if hi : i < n' then Classical.choose (hvi hi) else (0, 0)
    have v_prop {i : â„•} (hi : i < n') : v i âˆˆ u âˆ§ e (v i) = enum âŸ¨i, hiâŸ© := by
      simp only [hi, â†“reduceDIte, v]
      exact Classical.choose_spec (hvi hi)
    refine âŸ¨n', v, ?_, ?_, ?_âŸ©
    Â· intro z hz
      simp only [Finset.coe_range, mem_image, mem_Iio] at hz
      obtain âŸ¨i, hi1, hi2âŸ© := hz
      exact hi2 â–¸ hu1 (v_prop hi1).left
    Â· intro i hi j hj hij
      have hi1 := v_prop (Finset.mem_range.mp hi)
      have hj1 := v_prop (Finset.mem_range.mp hj)
      apply hu3 hi1.left hj1.left
      intro hC
      have := Fin.mk.inj_iff.mp <| Equiv.injective enum <| Subtype.eq <| hj1.right â–¸ hC â–¸ hi1.right
      tauto
    Â· convert hn
      ext x
      simp only [Finset.mem_range, mem_diff, mem_iUnion, exists_prop, not_exists, not_and,
        mem_setOf_eq, mem_iInter, and_imp]
      constructor
      Â· intro âŸ¨hx1, hx2âŸ© j hj
        refine âŸ¨by assumption, fun y hy1 hy2 â†¦ ?_âŸ©
        have hy : e y âˆˆ {j âˆˆ Finset.range n | âˆƒ x âˆˆ u, e x = j} := by
          simp only [Finset.mem_filter, Finset.mem_range]
          exact âŸ¨by omega, by use yâŸ©
        let i := enum.symm âŸ¨e y, hyâŸ©
        have hi : i < n' := i.isLt
        have : y = v i := by
          have : e y = enum i := by simp [i]
          exact he hy1 (v_prop hi).left (this â–¸ (v_prop hi).right.symm)
        exact this.symm â–¸ hx2 i hi
      Â· intro hx
        refine âŸ¨hx 0 (by omega) |>.left, fun i hi â†¦ ?_âŸ©
        have := v_prop hi
        apply hx n (by omega) |>.right (v i)
        Â· tauto
        Â· rw [this.right]
          set j := enum âŸ¨i, hiâŸ©
          have := j.property
          simp only [Finset.mem_filter, Finset.mem_range] at this
          exact this.left
  Â· intro i
    dsimp only [B]
    apply NullMeasurableSet.diff (by measurability)
    exact NullMeasurableSet.biUnion (hu2.mono (by simp)) (by measurability)
  Â· use 0
    have : volume (Ioo d b) â‰  âˆ := by simp
    intro hC
    apply measure_mono_top (sâ‚‚ := Ioo d b) (by simp) at hC
    tauto

/-- If `f` has derivative 0 a.e. on `[d, b]`, then there is a finite Vitali cover of `[d, b]`
except for measure at most `Î´`, consisting of closed intervals, where each has small variations
wrt `f`. Additionally, The finite cover is already ordered. -/
lemma ae_deriv_zero_fin_ordered_cover {f : â„ â†’ â„} {d b Î· Î´ : â„}
    (hf : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x)
    (hÎ· : 0 < Î·) (hÎ´1 : 0 < Î´) :
    âˆƒ n : â„•, âˆƒ v : â„• â†’ â„ Ã— â„,
      (âˆ€ i âˆˆ Finset.range n, d < (v i).1 âˆ§ 0 < (v i).2 âˆ§ (v i).1 + (v i).2 < b âˆ§
        |f ((v i).1 + (v i).2) - f (v i).1| < (v i).2 * Î·) âˆ§
      (âˆ€ i âˆˆ Finset.range n, âˆ€ j âˆˆ Finset.range n, i < j â†’ (v i).1 + (v i).2 < (v j).1) âˆ§
      (b - d) - (âˆ‘ i âˆˆ Finset.range n, (v i).2) < Î´ := by
  obtain âŸ¨n, v, hv1, hv2, hv3âŸ© := ae_deriv_zero_fin_cover hf hÎ· hÎ´1
  replace hv1 : âˆ€ i âˆˆ Finset.range n, d < (v i).1 âˆ§ 0 < (v i).2 âˆ§ (v i).1 + (v i).2 < b âˆ§
      |f ((v i).1 + (v i).2) - f (v i).1| < (v i).2 * Î· := by
    intro i hi
    have : v i âˆˆ {(x, h) : â„ Ã— â„ | d < x âˆ§ h > 0 âˆ§ x + h < b âˆ§ |f (x + h) - f x| < h * Î·} := by
      apply @hv1 (v i)
      simp only [Finset.coe_range, mem_image, mem_Iio]
      exact âŸ¨i, List.mem_range.mp hi, rflâŸ©
    simpa using this
  let r_list := @Finset.sort (Finset.range n) (fun (i j) â†¦ (v i).1 â‰¤ (v j).1) _
    { trans := by intros; linarith }
    { antisymm := by
        intro i j h1 h2
        have hij: (v i).1 = (v j).1 := by linarith
        have := hv2 i.property j.property
        contrapose this
        push_neg
        refine âŸ¨Subtype.coe_ne_coe.mpr this, ?_âŸ©
        simp only [not_disjoint_iff, mem_Icc]
        exact âŸ¨(v i).1, âŸ¨by simp, by linarith [hv1 i.val i.property]âŸ©,
               âŸ¨by assumption, by linarith [hv1 j.val j.property]âŸ©âŸ© }
    { total := by intros; exact LinearOrder.le_total _ _ }
    Finset.univ
  have r_list_len : r_list.length = n := by simp [r_list]
  let r (i : â„•) : â„• :=
    if hi : i âˆˆ Finset.range n then r_list.get âŸ¨i, r_list_len.symm â–¸ Finset.mem_range.mp hiâŸ©
    else i
  have r_mem {i : â„•} (hi : i âˆˆ Finset.range n) : r i âˆˆ Finset.range n := by
    simp [r, Finset.mem_range.mp hi]
  have r_mono {i j : â„•} (hi : i âˆˆ Finset.range n) (hj : j âˆˆ Finset.range n) (hij : i â‰¤ j) :
      (v (r i)).1 â‰¤ (v (r j)).1 := by
    have : List.Sorted (fun (i j : Finset.range n) â†¦ (v i).1 â‰¤ (v j).1) r_list := by simp [r_list]
    simp only [hi, hj, r, â†“reduceDIte]
    apply @List.Sorted.rel_get_of_le _ _ {refl := by simp +contextual} _ this
    simpa
  have r_inj {i j : â„•} (hi : i âˆˆ Finset.range n) (hj : j âˆˆ Finset.range n) (hij : i â‰  j) :
      r i â‰  r j := by
    have nodup : r_list.Nodup := by simp [r_list]
    have := List.Nodup.getElem_inj_iff (h := nodup)
      (hi := r_list_len.symm â–¸ (List.mem_range.mp hi))
      (hj := r_list_len.symm â–¸ (List.mem_range.mp hj))
    simp only [hi, hj, r, â†“reduceDIte]
    intro hC
    have := this.mp (Subtype.eq hC)
    contradiction
  have r_surj {k : â„•} (hk : k âˆˆ Finset.range n) : âˆƒ i âˆˆ Finset.range n, r i = k := by
    have : âŸ¨k, hkâŸ© âˆˆ r_list := by simp [r_list]
    obtain âŸ¨i, hi, hâŸ© := List.mem_iff_getElem.mp this
    rw [r_list_len] at hi
    exact âŸ¨i, by rwa [Finset.mem_range], by simp [r, hi, h]âŸ©
  let v' (i : â„•) : (â„ Ã— â„) := v (r i)
  refine âŸ¨n, v', ?_, ?_, ?_âŸ©
  Â· intro i hi
    simp only [v']
    exact hv1 _ (r_mem hi)
  Â· intro i hi j hj hij
    have hi1 : i + 1 âˆˆ Finset.range n := by rw [Finset.mem_range] at hj âŠ¢; omega
    simp only [v']
    suffices (v (r i)).1 + (v (r i)).2 < (v (r (i + 1))).1 by
      have : (v (r (i + 1))).1 â‰¤ (v (r j)).1 := by apply r_mono <;> assumption
      exact lt_of_lt_of_le (by assumption) this
    have hL: (v (r i)).1 â‰¤ (v (r (i + 1))).1 := by apply r_mono <;> (first | assumption | omega)
    have disj := hv2 (r_mem hi) (r_mem hi1) (by apply r_inj <;> (first | assumption | omega))
    simp only [Disjoint, le_eq_subset, bot_eq_empty, subset_empty_iff] at disj
    specialize @disj {(v (r (i + 1))).1}
    by_contra hC
    have : (v (r (i + 1))).1 â‰¤ (v (r i)).1 + (v (r i)).2 := by linarith
    simp only [singleton_subset_iff, mem_Icc, hL, this, and_self, le_refl,
      le_add_iff_nonneg_right, true_and, singleton_ne_empty, imp_false, not_le,
      forall_const] at disj
    linarith [hv1 _ (r_mem hi1)]
  Â· rw [MeasureTheory.measure_diff, MeasureTheory.measure_biUnion_finset] at hv3
    Â· simp only [Real.volume_Icc, Real.volume_Ioo, add_sub_cancel_left] at hv3
      have : âˆ‘ i âˆˆ Finset.range n, (v' i).2 = âˆ‘ x âˆˆ Finset.range n, (v x).2 := by
        dsimp only [v']
        symm
        have : Finset.range n = Finset.image r (Finset.range n) := by
          ext k; simp only [Finset.mem_image]
          exact âŸ¨fun hk â†¦ r_surj hk, fun âŸ¨_, hi1, hi2âŸ© â†¦ hi2 â–¸ r_mem hi1âŸ©
        nth_rw 1 [this]
        apply Finset.sum_image (g := r)
        dsimp only [InjOn]
        intro i hi j hj; contrapose!; exact r_inj hi hj
      rw [this]
      have : âˆ€ i âˆˆ Finset.range n, 0 â‰¤ (v i).2 := by intro i hi; linarith [hv1 i hi]
      rw [â† ENNReal.ofReal_sum_of_nonneg this,
          â† ENNReal.ofReal_sub (hq := Finset.sum_nonneg this)] at hv3
      exact (ENNReal.ofReal_lt_ofReal_iff hÎ´1).mp hv3
    Â· assumption
    Â· simp +contextual
    Â· intro x hx
      simp only [Finset.mem_range, mem_iUnion, exists_prop] at hx
      obtain âŸ¨i, hi1, hi2âŸ© := hx
      simp only [mem_Icc] at hi2
      rw [mem_Ioo]
      constructor <;> linarith [hv1 i (List.mem_range.mpr hi1)]
    Â· measurability
    Â· have : âˆ‘ i âˆˆ Finset.range n, volume (Icc (v i).1 ((v i).1 + (v i).2)) â‰  âŠ¤ := by simp
      exact ne_top_of_le_ne_top this <| measure_biUnion_finset_le (Finset.range n)
        fun i â†¦ Icc (v i).1 ((v i).1 + (v i).2)

lemma split_sum_even_odd (n : â„•) (f : â„• â†’ â„) : âˆ‘ i âˆˆ Finset.range (2 * n + 1), f i =
    âˆ‘ i âˆˆ Finset.range (n + 1), f (2 * i) + âˆ‘ i âˆˆ Finset.range n, f (2 * i + 1) := by
  induction n with
  | zero => simp
  | succ n ih =>
    have : 2 * (n + 1) = 2 * n + 1 + 1 := by ring
    rw [this, Finset.sum_range_succ, Finset.sum_range_succ, ih]
    nth_rw 2 [Finset.sum_range_succ]
    nth_rw 3 [Finset.sum_range_succ]
    rw [this]
    abel

/-- If `f` is absolutely continuous on `uIcc a b` and `f' x = 0` for a.e. `x âˆˆ uIcc a b`, then `f`
is constant on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_deriv_zero_const {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b)
    (hf1 : âˆ€áµ x, x âˆˆ uIcc a b â†’ HasDerivAt f 0 x) :
    âˆƒ C, âˆ€ x âˆˆ uIcc a b, f x = C := by
  wlog hab : a â‰¤ b
  Â· exact uIcc_comm b a â–¸ @this f b a hf.symm (uIcc_comm a b â–¸ hf1) (by linarith)
  suffices âˆ€ x âˆˆ uIcc a b, f x = f b by use f b
  by_contra hC
  push_neg at hC
  obtain âŸ¨d, hd1, hd2âŸ© := hC
  rw [uIcc_of_le hab, mem_Icc] at hd1
  have had : a â‰¤ d := by linarith
  have hdb : d < b := lt_of_le_of_ne hd1.right fun hC â†¦ hd2 (congrArg f hC)
  rw [absolutelyContinuousOnInterval_iff] at hf
  have hfdb: 0 < |f d - f b| / 2 := by
    simp only [Nat.ofNat_pos, div_pos_iff_of_pos_right, abs_pos, ne_eq]
    rwa [sub_eq_zero]
  obtain âŸ¨Î´, hÎ´1, hÎ´2âŸ© := hf (|f d - f b| / 2) hfdb
  simp only [AbsolutelyContinuousOnInterval.disjWithin, mem_setOf_eq] at hÎ´2
  simp_rw [uIcc_of_le hab] at hf1 hÎ´2 âŠ¢
  replace hf1 : âˆ€áµ x, x âˆˆ Icc d b â†’ HasDerivAt f 0 x := by
    filter_upwards [hf1] with x hx1 hx2
    apply hx1
    suffices Icc d b âŠ† Icc a b from this hx2
    gcongr
  have hfdb': 0 < |f d - f b| / (2 * (b - d)) := by apply div_pos <;> linarith
  obtain âŸ¨n, v, hv1, hv2, hv3âŸ© := ae_deriv_zero_fin_ordered_cover hf1 hfdb' hÎ´1
  let I (i : â„•) :=
    if i < n then
      if i = 0 then (d, (v i).1) else ((v (i - 1)).1 + (v (i - 1)).2, (v i).1)
    else
      if i = n âˆ§ 0 < n then ((v (i - 1)).1 + (v (i - 1)).2, b) else (d, b)
  have hI1 : (I 0).1 = d := by
    dsimp only [I]
    split_ifs
    any_goals omega
    all_goals simp
  have hI2 : (I n).2 = b := by
    dsimp only [I]
    split_ifs
    any_goals omega
    all_goals simp
  have hI3 {i : â„•} (hi : i âˆˆ Finset.range n) : (I (i + 1)).1 = (v i).1 + (v i).2 := by
    have := Finset.mem_range.mp hi
    dsimp only [I]
    split_ifs
    any_goals omega
    any_goals contradiction
    all_goals simp
  have hI4 {i : â„•} (hi : i âˆˆ Finset.range n) : (I i).2 = (v i).1 := by
    have := Finset.mem_range.mp hi
    dsimp only [I]
    split_ifs
    all_goals simp
  have hI5 {i : â„•} (hi : i âˆˆ Finset.range (n + 1)) : a â‰¤ (I i).1 âˆ§ (I i).1 â‰¤ (I i).2 âˆ§ (I i).2 â‰¤ b
      := by
    by_cases hi1 : i < n
    Â· simp only [hi1, â†“reduceIte, I]
      Â· by_cases hi0 : i = 0
        Â· simp only [hi0, â†“reduceIte, true_and, had]
          constructor <;> linarith [hv1 0 (by rw [Finset.mem_range]; omega)]
        Â· simp only [hi0, â†“reduceIte]
          have := hv1 (i - 1) (by rw [Finset.mem_range]; omega)
          have := hv1 i (by rw [Finset.mem_range]; omega)
          have := hv2 (i - 1) (by rw [Finset.mem_range]; omega) i
            (by rw [Finset.mem_range]; omega) (by omega)
          exact âŸ¨by linarith, by linarith, by linarithâŸ©
    Â· simp only [hi1, â†“reduceIte, I]
      Â· by_cases hn : i = n âˆ§ 0 < n
        Â· simp only [hn, and_self, â†“reduceIte, le_refl, and_true]
          constructor <;> linarith [hv1 (n - 1) (by rw [Finset.mem_range]; omega)]
        Â· simp only [hn, â†“reduceIte, le_refl, and_true, had, hd1.right]
  have hI6 {i : â„•} (hi : i âˆˆ Finset.range (n + 1)) : (I i).1 âˆˆ Icc a b âˆ§ (I i).2 âˆˆ Icc a b := by
    simp only [mem_Icc]
    repeat' constructor
    all_goals linarith [hI5 hi]
  have hI7 {i j : â„•} (hi : i âˆˆ Finset.range (n + 1)) (hj : j âˆˆ Finset.range (n + 1))
      (hij : i < j) : (I i).2 â‰¤ (I j).1:= by
    have hv2' {i j : â„•} (hi : i âˆˆ Finset.range n) (hj : j âˆˆ Finset.range n) (hij : i â‰¤ j) :
      (v i).1 â‰¤ (v j).1 := by
      by_cases hij0 : i = j
      Â· rw [hij0]
      Â· linarith [hv2 i hi j hj (by omega), hv1 i hi]
    have hjn : j < n + 1 := Finset.mem_range.mp hj
    have hin : i < n + 1 := Finset.mem_range.mp hi
    replace hin : i < n := by omega
    simp only [hin, â†“reduceIte, I]
    have (a : â„) (ha : 0 < a) : 0 â‰¤ a := le_of_lt ha
    split_ifs <;> (simp only; try omega)
    all_goals try apply le_add_of_le_of_nonneg
    all_goals try refine le_of_lt (hv1 _ ?_).right.left
    all_goals try refine hv2' ?_ ?_ ?_
    all_goals try rw [Finset.mem_range]
    all_goals omega
  let r (i : â„•) : â„ := if Even i then (I (i / 2)).1 else (I (i / 2)).2
  have hr1 (i : â„•) : r (2 * i) = (I i).1 := by simp [r]
  have hr2 (i : â„•) : r (2 * i + 1) = (I i).2 := by
    simp only [Nat.not_even_bit1, â†“reduceIte, r]
    congr; omega
  have hrd : d = r 0 := by rw [show 0 = 2 * 0 by rfl, hr1, hI1]
  have hrb : b = r (2 * n + 1) := by rw [hr2, hI2]
  have h_dist_sum : âˆ‘ i âˆˆ Finset.range (n + 1), dist (I i).1 (I i).2 =
      b - d - âˆ‘ i âˆˆ Finset.range n, (v i).2 := by
    rw [fun a b c â†¦ show a = b - c â†” b = a + c by grind]
    calc
    _ = r (2 * n + 1) - r 0 := by rw [hrd, hrb]
    _ = âˆ‘ k âˆˆ Finset.range (2 * n + 1), (r (k + 1) - r k) := by rw [â† Finset.sum_range_sub]
    _ = âˆ‘ i âˆˆ Finset.range (n + 1), (r (2 * i + 1) - r (2 * i)) +
        âˆ‘ i âˆˆ Finset.range n, (r (2 * i + 1 + 1) - r (2 * i + 1)) := by rw [split_sum_even_odd]
    _ = âˆ‘ i âˆˆ Finset.range (n + 1), dist (I i).1 (I i).2 + âˆ‘ i âˆˆ Finset.range n, (v i).2 := by
      congr 1 <;> apply Finset.sum_congr rfl
      Â· intro i hi
        rw [hr1, hr2, Real.dist_eq, abs_eq_neg_self.mpr]
        Â· abel
        Â· linarith [hI5 hi]
      Â· intro i hi
        rw [show 2 * i + 1 + 1 = 2 * (i + 1) by ring, hr1, hr2, hI3, hI4] <;> try assumption
        abel
  have : âˆ‘ i âˆˆ Finset.range (n + 1), dist (f (I i).1) (f (I i).2) < |f d - f b| / 2 := by
    refine hÎ´2 ((n + 1), I) âŸ¨@hI6, ?_âŸ© (by convert hv3 using 1)
    intro i hi j hj hij
    simp only [onFun, uIoc_of_le (hI5 hi).right.left, uIoc_of_le (hI5 hj).right.left]
    by_cases hij1 : i < j
    Â· exact Ioc_disjoint_Ioc_of_le (hI7 hi hj hij1)
    Â· exact Ioc_disjoint_Ioc_of_le (hI7 hj hi (by omega)) |>.symm
  suffices |f d - f b| < |f d - f b| by linarith
  calc
  _ = |f (r 0) - f (r (2 * n + 1))| := by rw [hrd, hrb]
  _ = |(f âˆ˜ r) 0 - (f âˆ˜ r) (2 * n + 1)| := by simp
  _ = |âˆ‘ k âˆˆ Finset.range (2 * n + 1), ((f âˆ˜ r) k - (f âˆ˜ r) (k + 1))| := by
    rw [â† Finset.sum_range_sub']
  _ = |âˆ‘ k âˆˆ Finset.range (2 * n + 1), (f (r k) - f (r (k + 1)))| := by simp
  _ â‰¤ âˆ‘ k âˆˆ Finset.range (2 * n + 1), |f (r k) - f (r (k + 1))| := by
    apply Finset.abs_sum_le_sum_abs
  _ = âˆ‘ i âˆˆ Finset.range (n + 1), |f (r (2 * i)) - f (r (2 * i + 1))| +
      âˆ‘ i âˆˆ Finset.range n, |f (r (2 * i + 1)) - f (r (2 * i + 1 + 1))| := by
    rw [split_sum_even_odd]
  _ = âˆ‘ i âˆˆ Finset.range (n + 1), dist (f (I i).1) (f (I i).2) +
      âˆ‘ i âˆˆ Finset.range n, |f ((v i).1 + (v i).2) - f ((v i).1)| := by
    congr 1 <;> apply Finset.sum_congr rfl
    Â· intro i hi
      rw [hr1, hr2, Real.dist_eq]
    Â· intro i hi
      rw [show 2 * i + 1 + 1 = 2 * (i + 1) by ring, hr1, hr2, hI3, hI4] <;> try assumption
      nth_rw 1 [â† abs_neg]; congr 1; abel
  _ < |f d - f b| / 2 + âˆ‘ i âˆˆ Finset.range n, |f ((v i).1 + (v i).2) - f ((v i).1)| := by
    gcongr 1
  _ â‰¤ |f d - f b| / 2 + âˆ‘ i âˆˆ Finset.range n, (v i).2 * (|f d - f b| / (2 * (b - d))) := by
    gcongr with i hi
    linarith [hv1 i hi]
  _ = |f d - f b| / 2 + (âˆ‘ i âˆˆ Finset.range n, (v i).2) * (|f d - f b| / (2 * (b - d))) := by
    rw [Finset.sum_mul]
  _ â‰¤ |f d - f b| / 2 + (b - d) * (|f d - f b| / (2 * (b - d))) := by
    gcongr
    suffices 0 â‰¤ (b - d) - âˆ‘ i âˆˆ Finset.range n, (v i).2 by linarith
    rw [â† h_dist_sum]
    apply Finset.sum_nonneg; simp
  _ = |f d - f b| := by field_simp [show b - d â‰  0 by linarith]; ring

/-- *Fundamental Theorem of Calculus* for absolutely continuous functions: if `f` is absolutely
continuous on `uIcc a b`, then `âˆ« (x : â„) in a..b, deriv f x = f b - f a`. -/
theorem AbsolutelyContinuousOnInterval.integral_deriv_eq_sub {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    âˆ« (x : â„) in a..b, deriv f x = f b - f a := by
  have f_deriv_integral_ac := hf.deriv_intervalIntegrable.integral_absolutelyContinuousOnInterval
    (c := a) (by simp)
  let g (x : â„) := f x - âˆ« (t : â„) in a..x, deriv f t
  have g_ac : AbsolutelyContinuousOnInterval g a b := hf.sub (f_deriv_integral_ac)
  have g_ae_deriv_zero : âˆ€áµ x, x âˆˆ uIcc a b â†’ HasDerivAt g 0 x := by
    filter_upwards [hf.ae_hasDerivAt, hf.deriv_intervalIntegrable.ae_hasDerivAt_integral]
      with x hx1 hx2 hx3
    convert (hx1 hx3).sub (hx2 hx3 a (by simp))
    abel
  obtain âŸ¨C, hCâŸ© := g_ac.ae_deriv_zero_const g_ae_deriv_zero
  have : f a = g a := by simp [g]
  have := hC a (by simp)
  have := hC b (by simp)
  grind

/-- The integral of the derivative of a product of two absolutely continuous functions. -/
theorem AbsolutelyContinuousOnInterval.integral_deriv_mul_eq_sub
    {f g : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) (hg : AbsolutelyContinuousOnInterval g a b) :
    âˆ« x in a..b, deriv f x * g x + f x * deriv g x = f b * g b - f a * g a := by
  rw [â† (hf.mul hg).integral_deriv_eq_sub]
  apply intervalIntegral.integral_congr_ae
  filter_upwards [hf.ae_hasDerivAt, hg.ae_hasDerivAt] with x hxâ‚ hxâ‚‚ hxâ‚ƒ
  have hxâ‚„ : x âˆˆ uIcc a b := by grind [uIcc, uIoc]
  have hxâ‚… := (hxâ‚ hxâ‚„).mul (hxâ‚‚ hxâ‚„)
  exact hxâ‚….deriv.symm

/-- *Integration by parts* for absolutely continuous functions. -/
theorem AbsolutelyContinuousOnInterval.integral_mul_deriv_eq_deriv_mul
    {f g : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) (hg : AbsolutelyContinuousOnInterval g a b) :
    âˆ« x in a..b, f x * deriv g x = f b * g b - f a * g a - âˆ« x in a..b, deriv f x * g x := by
  rw [â† AbsolutelyContinuousOnInterval.integral_deriv_mul_eq_sub hf hg,
      â† intervalIntegral.integral_sub]
  Â· simp_rw [add_sub_cancel_left]
  Â· exact (hf.deriv_intervalIntegrable.mul_continuousOn hg.continuousOn).add
      (hg.deriv_intervalIntegrable.continuousOn_mul hf.continuousOn)
  Â· exact hf.deriv_intervalIntegrable.mul_continuousOn hg.continuousOn
