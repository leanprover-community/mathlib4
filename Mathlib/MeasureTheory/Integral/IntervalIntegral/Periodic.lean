/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Alex Kontorovich, Heather Macbeth
-/
import Mathlib.MeasureTheory.Measure.Lebesgue.EqHaar
import Mathlib.MeasureTheory.Measure.Haar.Quotient
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
import Mathlib.Topology.Algebra.Order.Floor
import Mathlib.Topology.Instances.AddCircle.Real

/-!
# Integrals of periodic functions

In this file we prove that the half-open interval `Ioc t (t + T)` in `‚Ñù` is a fundamental domain of
the action of the subgroup `‚Ñ§ ‚àô T` on `‚Ñù`.

A consequence is `AddCircle.measurePreserving_mk`: the covering map from `‚Ñù` to the "additive
circle" `‚Ñù ‚ß∏ (‚Ñ§ ‚àô T)` is measure-preserving, with respect to the restriction of Lebesgue measure to
`Ioc t (t + T)` (upstairs) and with respect to Haar measure (downstairs).

Another consequence (`Function.Periodic.intervalIntegral_add_eq` and related declarations) is that
`‚à´ x in t..t + T, f x = ‚à´ x in s..s + T, f x` for any (not necessarily measurable) function with
period `T`.
-/

open Set Function MeasureTheory MeasureTheory.Measure TopologicalSpace AddSubgroup intervalIntegral

open scoped MeasureTheory NNReal ENNReal

/-!
## Measures and integrability on ‚Ñù and on the circle
-/

@[measurability]
protected theorem AddCircle.measurable_mk' {a : ‚Ñù} :
    Measurable (Œ≤ := AddCircle a) ((‚Üë) : ‚Ñù ‚Üí AddCircle a) :=
  Continuous.measurable <| AddCircle.continuous_mk' a

theorem isAddFundamentalDomain_Ioc {T : ‚Ñù} (hT : 0 < T) (t : ‚Ñù)
    (Œº : Measure ‚Ñù := by volume_tac) :
    IsAddFundamentalDomain (AddSubgroup.zmultiples T) (Ioc t (t + T)) Œº := by
  refine IsAddFundamentalDomain.mk' nullMeasurableSet_Ioc fun x => ?_
  have : Bijective (codRestrict (fun n : ‚Ñ§ => n ‚Ä¢ T) (AddSubgroup.zmultiples T) _) :=
    (Equiv.ofInjective (fun n : ‚Ñ§ => n ‚Ä¢ T) (zsmul_left_strictMono hT).injective).bijective
  refine this.existsUnique_iff.2 ?_
  simpa only [add_comm x] using existsUnique_add_zsmul_mem_Ioc hT x t

theorem isAddFundamentalDomain_Ioc' {T : ‚Ñù} (hT : 0 < T) (t : ‚Ñù) (Œº : Measure ‚Ñù := by volume_tac) :
    IsAddFundamentalDomain (AddSubgroup.op <| .zmultiples T) (Ioc t (t + T)) Œº := by
  refine IsAddFundamentalDomain.mk' nullMeasurableSet_Ioc fun x => ?_
  have : Bijective (codRestrict (fun n : ‚Ñ§ => n ‚Ä¢ T) (AddSubgroup.zmultiples T) _) :=
    (Equiv.ofInjective (fun n : ‚Ñ§ => n ‚Ä¢ T) (zsmul_left_strictMono hT).injective).bijective
  refine (AddSubgroup.equivOp _).bijective.comp this |>.existsUnique_iff.2 ?_
  simpa using existsUnique_add_zsmul_mem_Ioc hT x t

namespace AddCircle

variable (T : ‚Ñù) [hT : Fact (0 < T)]

/-- Equip the "additive circle" `‚Ñù ‚ß∏ (‚Ñ§ ‚àô T)` with, as a standard measure, the Haar measure of total
mass `T` -/
noncomputable instance measureSpace : MeasureSpace (AddCircle T) :=
  { QuotientAddGroup.measurableSpace _ with volume := ENNReal.ofReal T ‚Ä¢ addHaarMeasure ‚ä§ }

@[simp]
protected theorem measure_univ : volume (Set.univ : Set (AddCircle T)) = ENNReal.ofReal T := by
  dsimp [volume]
  rw [‚Üê PositiveCompacts.coe_top]
  simp [addHaarMeasure_self (G := AddCircle T), -PositiveCompacts.coe_top]

instance : IsAddHaarMeasure (volume : Measure (AddCircle T)) :=
  IsAddHaarMeasure.smul _ (by simp [hT.out]) ENNReal.ofReal_ne_top

instance isFiniteMeasure : IsFiniteMeasure (volume : Measure (AddCircle T)) where
  measure_univ_lt_top := by simp

instance : HasAddFundamentalDomain (AddSubgroup.op <| .zmultiples T) ‚Ñù where
  ExistsIsAddFundamentalDomain := ‚ü®Ioc 0 (0 + T), isAddFundamentalDomain_Ioc' Fact.out 0‚ü©

instance : AddQuotientMeasureEqMeasurePreimage volume (volume : Measure (AddCircle T)) := by
  apply MeasureTheory.leftInvariantIsAddQuotientMeasureEqMeasurePreimage
  simp [(isAddFundamentalDomain_Ioc' hT.out 0).covolume_eq_volume, AddCircle.measure_univ]

/-- The covering map from `‚Ñù` to the "additive circle" `‚Ñù ‚ß∏ (‚Ñ§ ‚àô T)` is measure-preserving,
considered with respect to the standard measure (defined to be the Haar measure of total mass `T`)
on the additive circle, and with respect to the restriction of Lebsegue measure on `‚Ñù` to an
interval (t, t + T]. -/
protected theorem measurePreserving_mk (t : ‚Ñù) :
    MeasurePreserving (Œ≤ := AddCircle T) ((‚Üë) : ‚Ñù ‚Üí AddCircle T)
      (volume.restrict (Ioc t (t + T))) :=
  measurePreserving_quotientAddGroup_mk_of_AddQuotientMeasureEqMeasurePreimage
    volume (ùìï := Ioc t (t+T)) (isAddFundamentalDomain_Ioc' hT.out _) _

lemma add_projection_respects_measure (t : ‚Ñù) {U : Set (AddCircle T)} (meas_U : MeasurableSet U) :
    volume U = volume (QuotientAddGroup.mk ‚Åª¬π' U ‚à© (Ioc t (t + T))) :=
  (isAddFundamentalDomain_Ioc' hT.out _).addProjection_respects_measure_apply
    (volume : Measure (AddCircle T)) meas_U

theorem volume_closedBall {x : AddCircle T} (Œµ : ‚Ñù) :
    volume (Metric.closedBall x Œµ) = ENNReal.ofReal (min T (2 * Œµ)) := by
  have hT' : |T| = T := abs_eq_self.mpr hT.out.le
  let I := Ioc (-(T / 2)) (T / 2)
  have h‚ÇÅ : Œµ < T / 2 ‚Üí Metric.closedBall (0 : ‚Ñù) Œµ ‚à© I = Metric.closedBall (0 : ‚Ñù) Œµ := by
    intro hŒµ
    rw [inter_eq_left, Real.closedBall_eq_Icc, zero_sub, zero_add]
    rintro y ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©; constructor <;> linarith
  have h‚ÇÇ : (‚Üë) ‚Åª¬π' Metric.closedBall (0 : AddCircle T) Œµ ‚à© I =
      if Œµ < T / 2 then Metric.closedBall (0 : ‚Ñù) Œµ else I := by
    conv_rhs => rw [‚Üê if_ctx_congr (Iff.rfl : Œµ < T / 2 ‚Üî Œµ < T / 2) h‚ÇÅ fun _ => rfl, ‚Üê hT']
    apply coe_real_preimage_closedBall_inter_eq
    simpa only [hT', Real.closedBall_eq_Icc, zero_add, zero_sub] using Ioc_subset_Icc_self
  rw [addHaar_closedBall_center, add_projection_respects_measure T (-(T/2))
    measurableSet_closedBall, (by linarith : -(T / 2) + T = T / 2), h‚ÇÇ]
  by_cases hŒµ : Œµ < T / 2
  ¬∑ simp [hŒµ, min_eq_right (by linarith : 2 * Œµ ‚â§ T)]
  ¬∑ simp [I, hŒµ, min_eq_left (by linarith : T ‚â§ 2 * Œµ)]

instance : IsUnifLocDoublingMeasure (volume : Measure (AddCircle T)) := by
  refine ‚ü®‚ü®Real.toNNReal 2, Filter.Eventually.of_forall fun Œµ x => ?_‚ü©‚ü©
  simp only [volume_closedBall]
  erw [‚Üê ENNReal.ofReal_mul zero_le_two]
  apply ENNReal.ofReal_le_ofReal
  rw [mul_min_of_nonneg _ _ (zero_le_two : (0 : ‚Ñù) ‚â§ 2)]
  exact min_le_min (by linarith [hT.out]) (le_refl _)

/-- The isomorphism `AddCircle T ‚âÉ Ioc a (a + T)` whose inverse is the natural quotient map,
  as an equivalence of measurable spaces. -/
noncomputable def measurableEquivIoc (a : ‚Ñù) : AddCircle T ‚âÉ·µê Ioc a (a + T) where
  toEquiv := equivIoc T a
  measurable_toFun := measurable_of_measurable_on_compl_singleton _
    (continuousOn_iff_continuous_restrict.mp <| continuousOn_of_forall_continuousAt fun _x hx =>
      continuousAt_equivIoc T a hx).measurable
  measurable_invFun := AddCircle.measurable_mk'.comp measurable_subtype_coe

/-- The isomorphism `AddCircle T ‚âÉ Ico a (a + T)` whose inverse is the natural quotient map,
  as an equivalence of measurable spaces. -/
noncomputable def measurableEquivIco (a : ‚Ñù) : AddCircle T ‚âÉ·µê Ico a (a + T) where
  toEquiv := equivIco T a
  measurable_toFun := measurable_of_measurable_on_compl_singleton _
    (continuousOn_iff_continuous_restrict.mp <| continuousOn_of_forall_continuousAt fun _x hx =>
      continuousAt_equivIco T a hx).measurable
  measurable_invFun := AddCircle.measurable_mk'.comp measurable_subtype_coe

attribute [local instance] Subtype.measureSpace in
/-- The lower integral of a function over `AddCircle T` is equal to the lower integral over an
interval (t, t + T] in `‚Ñù` of its lift to `‚Ñù`. -/
protected theorem lintegral_preimage (t : ‚Ñù) (f : AddCircle T ‚Üí ‚Ñù‚â•0‚àû) :
    (‚à´‚Åª a in Ioc t (t + T), f a) = ‚à´‚Åª b : AddCircle T, f b := by
  have m : MeasurableSet (Ioc t (t + T)) := measurableSet_Ioc
  have := lintegral_map_equiv (Œº := volume) f (measurableEquivIoc T t).symm
  simp only [measurableEquivIoc, equivIoc, QuotientAddGroup.equivIocMod, MeasurableEquiv.symm_mk,
    MeasurableEquiv.coe_mk, Equiv.coe_fn_symm_mk] at this
  rw [‚Üê (AddCircle.measurePreserving_mk T t).map_eq]
  convert this.symm using 1
  ¬∑ rw [‚Üê map_comap_subtype_coe m _]
    exact MeasurableEmbedding.lintegral_map (MeasurableEmbedding.subtype_coe m) _
  ¬∑ congr 1
    have : ((‚Üë) : Ioc t (t + T) ‚Üí AddCircle T) = ((‚Üë) : ‚Ñù ‚Üí AddCircle T) ‚àò ((‚Üë) : _ ‚Üí ‚Ñù) := by
      ext1 x; rfl
    simp_rw [this]
    rw [‚Üê map_map AddCircle.measurable_mk' measurable_subtype_coe, ‚Üê map_comap_subtype_coe m]
    rfl

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

attribute [local instance] Subtype.measureSpace in
/-- The integral of an almost-everywhere strongly measurable function over `AddCircle T` is equal
to the integral over an interval (t, t + T] in `‚Ñù` of its lift to `‚Ñù`. -/
protected theorem integral_preimage (t : ‚Ñù) (f : AddCircle T ‚Üí E) :
    (‚à´ a in Ioc t (t + T), f a) = ‚à´ b : AddCircle T, f b := by
  have m : MeasurableSet (Ioc t (t + T)) := measurableSet_Ioc
  have := integral_map_equiv (Œº := volume) (measurableEquivIoc T t).symm f
  simp only [measurableEquivIoc, equivIoc, QuotientAddGroup.equivIocMod, MeasurableEquiv.symm_mk,
    MeasurableEquiv.coe_mk, Equiv.coe_fn_symm_mk] at this
  rw [‚Üê (AddCircle.measurePreserving_mk T t).map_eq, ‚Üê integral_subtype m, ‚Üê this]
  have : ((‚Üë) : Ioc t (t + T) ‚Üí AddCircle T) = ((‚Üë) : ‚Ñù ‚Üí AddCircle T) ‚àò ((‚Üë) : _ ‚Üí ‚Ñù) := by
    ext1 x; rfl
  simp_rw [this]
  rw [‚Üê map_map AddCircle.measurable_mk' measurable_subtype_coe, ‚Üê map_comap_subtype_coe m]
  rfl

/-- The integral of an almost-everywhere strongly measurable function over `AddCircle T` is equal
to the integral over an interval (t, t + T] in `‚Ñù` of its lift to `‚Ñù`. -/
protected theorem intervalIntegral_preimage (t : ‚Ñù) (f : AddCircle T ‚Üí E) :
    ‚à´ a in t..t + T, f a = ‚à´ b : AddCircle T, f b := by
  rw [integral_of_le, AddCircle.integral_preimage T t f]
  linarith [hT.out]

end AddCircle

namespace UnitAddCircle

attribute [local instance] Real.fact_zero_lt_one

protected theorem measure_univ : volume (Set.univ : Set UnitAddCircle) = 1 := by simp

/-- The covering map from `‚Ñù` to the "unit additive circle" `‚Ñù ‚ß∏ ‚Ñ§` is measure-preserving,
considered with respect to the standard measure (defined to be the Haar measure of total mass 1)
on the additive circle, and with respect to the restriction of Lebsegue measure on `‚Ñù` to an
interval (t, t + 1]. -/
protected theorem measurePreserving_mk (t : ‚Ñù) :
    MeasurePreserving (Œ≤ := UnitAddCircle) ((‚Üë) : ‚Ñù ‚Üí UnitAddCircle)
      (volume.restrict (Ioc t (t + 1))) :=
  AddCircle.measurePreserving_mk 1 t

/-- The integral of a measurable function over `UnitAddCircle` is equal to the integral over an
interval (t, t + 1] in `‚Ñù` of its lift to `‚Ñù`. -/
protected theorem lintegral_preimage (t : ‚Ñù) (f : UnitAddCircle ‚Üí ‚Ñù‚â•0‚àû) :
    (‚à´‚Åª a in Ioc t (t + 1), f a) = ‚à´‚Åª b : UnitAddCircle, f b :=
  AddCircle.lintegral_preimage 1 t f

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-- The integral of an almost-everywhere strongly measurable function over `UnitAddCircle` is
equal to the integral over an interval (t, t + 1] in `‚Ñù` of its lift to `‚Ñù`. -/
protected theorem integral_preimage (t : ‚Ñù) (f : UnitAddCircle ‚Üí E) :
    (‚à´ a in Ioc t (t + 1), f a) = ‚à´ b : UnitAddCircle, f b :=
  AddCircle.integral_preimage 1 t f

/-- The integral of an almost-everywhere strongly measurable function over `UnitAddCircle` is
equal to the integral over an interval (t, t + 1] in `‚Ñù` of its lift to `‚Ñù`. -/
protected theorem intervalIntegral_preimage (t : ‚Ñù) (f : UnitAddCircle ‚Üí E) :
    ‚à´ a in t..t + 1, f a = ‚à´ b : UnitAddCircle, f b :=
  AddCircle.intervalIntegral_preimage 1 t f

end UnitAddCircle

/-!
## Interval integrability of periodic functions
-/
namespace Function

namespace Periodic

variable {E : Type*} [NormedAddCommGroup E]

variable {f : ‚Ñù ‚Üí E} {T : ‚Ñù}

/-- A periodic function is interval integrable over every interval if it is interval integrable
over one period. -/
theorem intervalIntegrable {t : ‚Ñù} (h‚ÇÅf : Function.Periodic f T) (hT : 0 < T)
    (h‚ÇÇf : IntervalIntegrable f MeasureTheory.volume t (t + T)) (a‚ÇÅ a‚ÇÇ : ‚Ñù) :
    IntervalIntegrable f MeasureTheory.volume a‚ÇÅ a‚ÇÇ := by
  -- Replace [a‚ÇÅ, a‚ÇÇ] by [t - n‚ÇÅ * T, t + n‚ÇÇ * T], where n‚ÇÅ and n‚ÇÇ are natural numbers
  obtain ‚ü®n‚ÇÅ, hn‚ÇÅ‚ü© := exists_nat_ge ((t - min a‚ÇÅ a‚ÇÇ) / T)
  obtain ‚ü®n‚ÇÇ, hn‚ÇÇ‚ü© := exists_nat_ge ((max a‚ÇÅ a‚ÇÇ - t) / T)
  have : Set.uIcc a‚ÇÅ a‚ÇÇ ‚äÜ Set.uIcc (t - n‚ÇÅ * T) (t + n‚ÇÇ * T) := by
    rw [Set.uIcc_subset_uIcc_iff_le]
    constructor
    ¬∑ calc min (t - n‚ÇÅ * T) (t + n‚ÇÇ * T)
      _ ‚â§ (t - n‚ÇÅ * T) := by apply min_le_left
      _ ‚â§ min a‚ÇÅ a‚ÇÇ := by linarith [(div_le_iff‚ÇÄ hT).1 hn‚ÇÅ]
    ¬∑ calc max a‚ÇÅ a‚ÇÇ
      _ ‚â§ t + n‚ÇÇ * T := by linarith [(div_le_iff‚ÇÄ hT).1 hn‚ÇÇ]
      _ ‚â§ max (t - n‚ÇÅ * T) (t + n‚ÇÇ * T) := by apply le_max_right
  apply IntervalIntegrable.mono_set _ this
  -- Suffices to show integrability over shifted periods
  let a : ‚Ñï ‚Üí ‚Ñù := fun n ‚Ü¶ t + (n - n‚ÇÅ) * T
  rw [(by ring : t - n‚ÇÅ * T = a 0), (by simp [a] : t + n‚ÇÇ * T = a (n‚ÇÅ + n‚ÇÇ))]
  apply IntervalIntegrable.trans_iterate
  -- Show integrability over a shifted period
  intro k hk
  convert (IntervalIntegrable.comp_sub_right h‚ÇÇf ((k - n‚ÇÅ) * T)) using 1
  ¬∑ funext x
    simpa using (h‚ÇÅf.sub_int_mul_eq (k - n‚ÇÅ)).symm
  ¬∑ simp [a, Nat.cast_add]
    ring

/-- Special case of Function.Periodic.intervalIntegrable: A periodic function is interval integrable
over every interval if it is interval integrable over the period starting from zero. -/
theorem intervalIntegrable‚ÇÄ (h‚ÇÅf : Function.Periodic f T) (hT : 0 < T)
    (h‚ÇÇf : IntervalIntegrable f MeasureTheory.volume 0 T) (a‚ÇÅ a‚ÇÇ : ‚Ñù) :
    IntervalIntegrable f MeasureTheory.volume a‚ÇÅ a‚ÇÇ := by
  apply h‚ÇÅf.intervalIntegrable hT (t := 0)
  simpa

/-!
## Interval integrals of periodic functions
-/

variable [NormedSpace ‚Ñù E]

/-- An auxiliary lemma for a more general `Function.Periodic.intervalIntegral_add_eq`. -/
theorem intervalIntegral_add_eq_of_pos (hf : Periodic f T) (hT : 0 < T) (t s : ‚Ñù) :
    ‚à´ x in t..t + T, f x = ‚à´ x in s..s + T, f x := by
  simp only [integral_of_le, hT.le, le_add_iff_nonneg_right]
  haveI : VAddInvariantMeasure (AddSubgroup.zmultiples T) ‚Ñù volume :=
    ‚ü®fun c s _ => measure_preimage_add _ _ _‚ü©
  apply IsAddFundamentalDomain.setIntegral_eq (G := AddSubgroup.zmultiples T)
  exacts [isAddFundamentalDomain_Ioc hT t, isAddFundamentalDomain_Ioc hT s, hf.map_vadd_zmultiples]

/-- If `f` is a periodic function with period `T`, then its integral over `[t, t + T]` does not
depend on `t`. -/
theorem intervalIntegral_add_eq (hf : Periodic f T) (t s : ‚Ñù) :
    ‚à´ x in t..t + T, f x = ‚à´ x in s..s + T, f x := by
  rcases lt_trichotomy (0 : ‚Ñù) T with (hT | rfl | hT)
  ¬∑ exact hf.intervalIntegral_add_eq_of_pos hT t s
  ¬∑ simp
  ¬∑ rw [‚Üê neg_inj, ‚Üê integral_symm, ‚Üê integral_symm]
    simpa only [‚Üê sub_eq_add_neg, add_sub_cancel_right] using
      hf.neg.intervalIntegral_add_eq_of_pos (neg_pos.2 hT) (t + T) (s + T)

/-- If `f` is an integrable periodic function with period `T`, then its integral over `[t, s + T]`
is the sum of its integrals over the intervals `[t, s]` and `[t, t + T]`. -/
theorem intervalIntegral_add_eq_add (hf : Periodic f T) (t s : ‚Ñù)
    (h_int : ‚àÄ t‚ÇÅ t‚ÇÇ, IntervalIntegrable f MeasureSpace.volume t‚ÇÅ t‚ÇÇ) :
    ‚à´ x in t..s + T, f x = (‚à´ x in t..s, f x) + ‚à´ x in t..t + T, f x := by
  rw [hf.intervalIntegral_add_eq t s, integral_add_adjacent_intervals (h_int t s) (h_int s _)]

/-- If `f` is an integrable periodic function with period `T`, and `n` is an integer, then its
integral over `[t, t + n ‚Ä¢ T]` is `n` times its integral over `[t, t + T]`. -/
theorem intervalIntegral_add_zsmul_eq (hf : Periodic f T) (n : ‚Ñ§) (t : ‚Ñù)
    (h_int : ‚àÄ t‚ÇÅ t‚ÇÇ, IntervalIntegrable f MeasureSpace.volume t‚ÇÅ t‚ÇÇ) :
    ‚à´ x in t..t + n ‚Ä¢ T, f x = n ‚Ä¢ ‚à´ x in t..t + T, f x := by
  -- Reduce to the case `b = 0`
  suffices (‚à´ x in (0)..(n ‚Ä¢ T), f x) = n ‚Ä¢ ‚à´ x in (0)..T, f x by
    simp only [hf.intervalIntegral_add_eq t 0, (hf.zsmul n).intervalIntegral_add_eq t 0, zero_add,
      this]
  -- First prove it for natural numbers
  have : ‚àÄ m : ‚Ñï, (‚à´ x in (0)..m ‚Ä¢ T, f x) = m ‚Ä¢ ‚à´ x in (0)..T, f x := fun m ‚Ü¶ by
    induction m with
    | zero => simp
    | succ m ih =>
      simp only [succ_nsmul, hf.intervalIntegral_add_eq_add 0 (m ‚Ä¢ T) h_int, ih, zero_add]
  -- Then prove it for all integers
  rcases n with n | n
  ¬∑ simp [‚Üê this n]
  ¬∑ conv_rhs => rw [negSucc_zsmul]
    have h‚ÇÄ : Int.negSucc n ‚Ä¢ T + (n + 1) ‚Ä¢ T = 0 := by simp; linarith
    rw [integral_symm, ‚Üê (hf.nsmul (n + 1)).funext, neg_inj]
    simp_rw [integral_comp_add_right, h‚ÇÄ, zero_add, this (n + 1), add_comm T,
      hf.intervalIntegral_add_eq ((n + 1) ‚Ä¢ T) 0, zero_add]

section RealValued

open Filter

variable {g : ‚Ñù ‚Üí ‚Ñù}
variable (hg : Periodic g T)
include hg

/-- If `g : ‚Ñù ‚Üí ‚Ñù` is periodic with period `T > 0`, then for any `t : ‚Ñù`, the function
`t ‚Ü¶ ‚à´ x in 0..t, g x` is bounded below by `t ‚Ü¶ X + ‚åät/T‚åã ‚Ä¢ Y` for appropriate constants `X` and
`Y`. -/
theorem sInf_add_zsmul_le_integral_of_pos (h_int : IntervalIntegrable g MeasureSpace.volume 0 T)
    (hT : 0 < T) (t : ‚Ñù) :
    (sInf ((fun t => ‚à´ x in (0)..t, g x) '' Icc 0 T) + ‚åät / T‚åã ‚Ä¢ ‚à´ x in (0)..T, g x) ‚â§
      ‚à´ x in (0)..t, g x := by
  let h'_int := hg.intervalIntegrable‚ÇÄ hT h_int
  let Œµ := Int.fract (t / T) * T
  conv_rhs =>
    rw [‚Üê Int.fract_div_mul_self_add_zsmul_eq T t (by linarith),
      ‚Üê integral_add_adjacent_intervals (h'_int 0 Œµ) (h'_int _ _)]
  rw [hg.intervalIntegral_add_zsmul_eq ‚åät / T‚åã Œµ (hg.intervalIntegrable‚ÇÄ hT h_int),
    hg.intervalIntegral_add_eq Œµ 0, zero_add, add_le_add_iff_right]
  exact (continuous_primitive h'_int 0).continuousOn.sInf_image_Icc_le <|
    mem_Icc_of_Ico (Int.fract_div_mul_self_mem_Ico T t hT)

/-- If `g : ‚Ñù ‚Üí ‚Ñù` is periodic with period `T > 0`, then for any `t : ‚Ñù`, the function
`t ‚Ü¶ ‚à´ x in 0..t, g x` is bounded above by `t ‚Ü¶ X + ‚åät/T‚åã ‚Ä¢ Y` for appropriate constants `X` and
`Y`. -/
theorem integral_le_sSup_add_zsmul_of_pos (h_int : IntervalIntegrable g MeasureSpace.volume 0 T)
    (hT : 0 < T) (t : ‚Ñù) :
    (‚à´ x in (0)..t, g x) ‚â§
      sSup ((fun t => ‚à´ x in (0)..t, g x) '' Icc 0 T) + ‚åät / T‚åã ‚Ä¢ ‚à´ x in (0)..T, g x := by
  let h'_int := hg.intervalIntegrable‚ÇÄ hT h_int
  let Œµ := Int.fract (t / T) * T
  conv_lhs =>
    rw [‚Üê Int.fract_div_mul_self_add_zsmul_eq T t (by linarith), ‚Üê
      integral_add_adjacent_intervals (h'_int 0 Œµ) (h'_int _ _)]
  rw [hg.intervalIntegral_add_zsmul_eq ‚åät / T‚åã Œµ h'_int, hg.intervalIntegral_add_eq Œµ 0, zero_add,
    add_le_add_iff_right]
  exact (continuous_primitive h'_int 0).continuousOn.le_sSup_image_Icc
    (mem_Icc_of_Ico (Int.fract_div_mul_self_mem_Ico T t hT))

/-- If `g : ‚Ñù ‚Üí ‚Ñù` is periodic with period `T > 0` and `0 < ‚à´ x in 0..T, g x`, then
`t ‚Ü¶ ‚à´ x in 0..t, g x` tends to `‚àû` as `t` tends to `‚àû`. -/
theorem tendsto_atTop_intervalIntegral_of_pos (h‚ÇÄ : 0 < ‚à´ x in (0)..T, g x) (hT : 0 < T) :
    Tendsto (fun t => ‚à´ x in (0)..t, g x) atTop atTop := by
  have h_int := intervalIntegrable_of_integral_ne_zero h‚ÇÄ.ne'
  apply tendsto_atTop_mono (hg.sInf_add_zsmul_le_integral_of_pos h_int hT)
  apply atTop.tendsto_atTop_add_const_left (sInf <| (fun t => ‚à´ x in (0)..t, g x) '' Icc 0 T)
  apply Tendsto.atTop_zsmul_const h‚ÇÄ
  exact tendsto_floor_atTop.comp (tendsto_id.atTop_mul_const (inv_pos.mpr hT))

/-- If `g : ‚Ñù ‚Üí ‚Ñù` is periodic with period `T > 0` and `0 < ‚à´ x in 0..T, g x`, then
`t ‚Ü¶ ‚à´ x in 0..t, g x` tends to `-‚àû` as `t` tends to `-‚àû`. -/
theorem tendsto_atBot_intervalIntegral_of_pos (h‚ÇÄ : 0 < ‚à´ x in (0)..T, g x) (hT : 0 < T) :
    Tendsto (fun t => ‚à´ x in (0)..t, g x) atBot atBot := by
  have h_int := intervalIntegrable_of_integral_ne_zero h‚ÇÄ.ne'
  apply tendsto_atBot_mono (hg.integral_le_sSup_add_zsmul_of_pos h_int hT)
  apply atBot.tendsto_atBot_add_const_left (sSup <| (fun t => ‚à´ x in (0)..t, g x) '' Icc 0 T)
  apply Tendsto.atBot_zsmul_const h‚ÇÄ
  exact tendsto_floor_atBot.comp (tendsto_id.atBot_mul_const (inv_pos.mpr hT))

/-- If `g : ‚Ñù ‚Üí ‚Ñù` is periodic with period `T > 0` and `‚àÄ x, 0 < g x`, then `t ‚Ü¶ ‚à´ x in 0..t, g x`
tends to `‚àû` as `t` tends to `‚àû`. -/
theorem tendsto_atTop_intervalIntegral_of_pos'
    (h_int : IntervalIntegrable g MeasureSpace.volume 0 T) (h‚ÇÄ : ‚àÄ x, 0 < g x) (hT : 0 < T) :
    Tendsto (fun t => ‚à´ x in (0)..t, g x) atTop atTop :=
  hg.tendsto_atTop_intervalIntegral_of_pos (intervalIntegral_pos_of_pos h_int h‚ÇÄ hT) hT

/-- If `g : ‚Ñù ‚Üí ‚Ñù` is periodic with period `T > 0` and `‚àÄ x, 0 < g x`, then `t ‚Ü¶ ‚à´ x in 0..t, g x`
tends to `-‚àû` as `t` tends to `-‚àû`. -/
theorem tendsto_atBot_intervalIntegral_of_pos'
    (h_int : IntervalIntegrable g MeasureSpace.volume 0 T) (h‚ÇÄ : ‚àÄ x, 0 < g x) (hT : 0 < T) :
    Tendsto (fun t => ‚à´ x in (0)..t, g x) atBot atBot := by
  exact hg.tendsto_atBot_intervalIntegral_of_pos (intervalIntegral_pos_of_pos h_int h‚ÇÄ hT) hT

end RealValued

end Periodic

end Function
