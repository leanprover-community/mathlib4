/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Patrick Massot, SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Integral.Bochner.ContinuousLinearMap
import Mathlib.MeasureTheory.Measure.Lebesgue.Basic
import Mathlib.MeasureTheory.Topology

/-!
# Integral over an interval

In this file we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼` if `a â‰¤ b` and
`-âˆ« x in Ioc b a, f x âˆ‚Î¼` if `b â‰¤ a`.

## Implementation notes

### Avoiding `if`, `min`, and `max`

In order to avoid `if`s in the definition, we define `IntervalIntegrable f Î¼ a b` as
`IntegrableOn f (Ioc a b) Î¼ âˆ§ IntegrableOn f (Ioc b a) Î¼`. For any `a`, `b` one of these
intervals is empty and the other coincides with `Set.uIoc a b = Set.Ioc (min a b) (max a b)`.

Similarly, we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`.
Again, for any `a`, `b` one of these integrals is zero, and the other gives the expected result.

This way some properties can be translated from integrals over sets without dealing with
the cases `a â‰¤ b` and `b â‰¤ a` separately.

### Choice of the interval

We use integral over `Set.uIoc a b = Set.Ioc (min a b) (max a b)` instead of one of the other
three possible intervals with the same endpoints for two reasons:

* this way `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼` holds whenever
  `f` is integrable on each interval; in particular, it works even if the measure `Î¼` has an atom
  at `b`; this rules out `Set.Ioo` and `Set.Icc` intervals;
* with this definition for a probability measure `Î¼`, the integral `âˆ« x in a..b, 1 âˆ‚Î¼` equals
  the difference $F_Î¼(b)-F_Î¼(a)$, where $F_Î¼(a)=Î¼(-âˆ, a]$ is the
  [cumulative distribution function](https://en.wikipedia.org/wiki/Cumulative_distribution_function)
  of `Î¼`.

## Tags

integral
-/


noncomputable section

open MeasureTheory Set Filter Function TopologicalSpace

open scoped Topology Filter ENNReal Interval NNReal

variable {Î¹ ğ•œ Îµ Îµ' E F A : Type*} [NormedAddCommGroup E]
  [TopologicalSpace Îµ] [ENormedAddMonoid Îµ] [TopologicalSpace Îµ'] [ENormedAddMonoid Îµ']

/-!
### Integrability on an interval
-/


/-- A function `f` is called *interval integrable* with respect to a measure `Î¼` on an unordered
interval `a..b` if it is integrable on both intervals `(a, b]` and `(b, a]`. One of these
intervals is always empty, so this property is equivalent to `f` being integrable on
`(min a b, max a b]`. -/
def IntervalIntegrable (f : â„ â†’ Îµ) (Î¼ : Measure â„) (a b : â„) : Prop :=
  IntegrableOn f (Ioc a b) Î¼ âˆ§ IntegrableOn f (Ioc b a) Î¼

/-!
## Basic iff's for `IntervalIntegrable`
-/
section

variable [PseudoMetrizableSpace Îµ] {f : â„ â†’ Îµ} {a b : â„} {Î¼ : Measure â„}

/-- A function is interval integrable with respect to a given measure `Î¼` on `a..b` if and
  only if it is integrable on `uIoc a b` with respect to `Î¼`. This is an equivalent
  definition of `IntervalIntegrable`. -/
theorem intervalIntegrable_iff : IntervalIntegrable f Î¼ a b â†” IntegrableOn f (Î™ a b) Î¼ := by
  rw [uIoc_eq_union, integrableOn_union, IntervalIntegrable]

/-- If a function is interval integrable with respect to a given measure `Î¼` on `a..b` then
  it is integrable on `uIoc a b` with respect to `Î¼`. -/
theorem IntervalIntegrable.def' (h : IntervalIntegrable f Î¼ a b) : IntegrableOn f (Î™ a b) Î¼ :=
  intervalIntegrable_iff.mp h

theorem intervalIntegrable_congr_ae {g : â„ â†’ Îµ} (h : f =áµ[Î¼.restrict (Î™ a b)] g) :
    IntervalIntegrable f Î¼ a b â†” IntervalIntegrable g Î¼ a b := by
  rw [intervalIntegrable_iff, integrableOn_congr_fun_ae h, intervalIntegrable_iff]

theorem IntervalIntegrable.congr_ae {g : â„ â†’ Îµ} (hf : IntervalIntegrable f Î¼ a b)
    (h : f =áµ[Î¼.restrict (Î™ a b)] g) :
    IntervalIntegrable g Î¼ a b := by
  rwa [â† intervalIntegrable_congr_ae h]

theorem intervalIntegrable_congr {g : â„ â†’ Îµ} (h : EqOn f g (Î™ a b)) :
    IntervalIntegrable f Î¼ a b â†” IntervalIntegrable g Î¼ a b :=
  intervalIntegrable_congr_ae <| (ae_restrict_mem measurableSet_uIoc).mono h

alias âŸ¨IntervalIntegrable.congr, _âŸ© := intervalIntegrable_congr

theorem IntervalIntegrable.congr' {g : â„ â†’ Îµ} (hf : IntervalIntegrable f Î¼ a b)
    (h : Set.EqOn f g (Set.uIoc a b)) :
    IntervalIntegrable g Î¼ a b :=
  hf.congr (eventuallyEq_of_mem (self_mem_ae_restrict measurableSet_uIoc) h)

/-- Interval integrability is invariant when functions change along discrete sets. -/
theorem IntervalIntegrable.congr_codiscreteWithin {g : â„ â†’ Îµ} [NoAtoms Î¼]
    (h : f =á¶ [codiscreteWithin (Î™ a b)] g) (hf : IntervalIntegrable f Î¼ a b) :
    IntervalIntegrable g Î¼ a b :=
  hf.congr_ae (ae_restrict_le_codiscreteWithin measurableSet_Ioc h)

/-- Interval integrability is invariant when functions change along discrete sets. -/
theorem intervalIntegrable_congr_codiscreteWithin {g : â„ â†’ Îµ} [NoAtoms Î¼]
    (h : f =á¶ [codiscreteWithin (Î™ a b)] g) :
    IntervalIntegrable f Î¼ a b â†” IntervalIntegrable g Î¼ a b :=
  âŸ¨(IntervalIntegrable.congr_codiscreteWithin h Â·),
    (IntervalIntegrable.congr_codiscreteWithin h.symm Â·)âŸ©

theorem intervalIntegrable_iff_integrableOn_Ioc_of_le (hab : a â‰¤ b) :
    IntervalIntegrable f Î¼ a b â†” IntegrableOn f (Ioc a b) Î¼ := by
  rw [intervalIntegrable_iff, uIoc_of_le hab]

theorem intervalIntegrable_iff' [NoAtoms Î¼] (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f Î¼ a b â†” IntegrableOn f (uIcc a b) Î¼ := by
  rw [intervalIntegrable_iff, â† Icc_min_max, uIoc, integrableOn_Icc_iff_integrableOn_Ioc h]

theorem intervalIntegrable_iff_integrableOn_Icc_of_le [NoAtoms Î¼]
    (hab : a â‰¤ b) (ha : â€–f aâ€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f Î¼ a b â†” IntegrableOn f (Icc a b) Î¼ := by
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hab, integrableOn_Icc_iff_integrableOn_Ioc ha]

theorem intervalIntegrable_iff_integrableOn_Ico_of_le [NoAtoms Î¼]
    (hab : a â‰¤ b) (ha : â€–f aâ€–â‚‘ â‰  âˆ := by finiteness) (hb : â€–f bâ€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f Î¼ a b â†” IntegrableOn f (Ico a b) Î¼ := by
  rw [intervalIntegrable_iff_integrableOn_Icc_of_le hab ha,
    integrableOn_Icc_iff_integrableOn_Ico hb]

theorem intervalIntegrable_iff_integrableOn_Ioo_of_le [NoAtoms Î¼]
    (hab : a â‰¤ b) (ha : â€–f aâ€–â‚‘ â‰  âˆ := by finiteness) (hb : â€–f bâ€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f Î¼ a b â†” IntegrableOn f (Ioo a b) Î¼ := by
  rw [intervalIntegrable_iff_integrableOn_Icc_of_le hab ha,
    integrableOn_Icc_iff_integrableOn_Ioo ha hb]

omit [PseudoMetrizableSpace Îµ] in
/-- If a function is integrable with respect to a given measure `Î¼` then it is interval integrable
  with respect to `Î¼` on `uIcc a b`. -/
theorem MeasureTheory.Integrable.intervalIntegrable (hf : Integrable f Î¼) :
    IntervalIntegrable f Î¼ a b :=
  âŸ¨hf.integrableOn, hf.integrableOnâŸ©

omit [PseudoMetrizableSpace Îµ] in
theorem MeasureTheory.IntegrableOn.intervalIntegrable (hf : IntegrableOn f [[a, b]] Î¼) :
    IntervalIntegrable f Î¼ a b :=
  âŸ¨MeasureTheory.IntegrableOn.mono_set hf (Ioc_subset_Icc_self.trans Icc_subset_uIcc),
    MeasureTheory.IntegrableOn.mono_set hf (Ioc_subset_Icc_self.trans Icc_subset_uIcc')âŸ©

theorem intervalIntegrable_const_iff {c : Îµ} (hc : â€–câ€–â‚‘ â‰  âŠ¤ := by finiteness) :
    IntervalIntegrable (fun _ => c) Î¼ a b â†” c = 0 âˆ¨ Î¼ (Î™ a b) < âˆ := by
  simp [intervalIntegrable_iff, integrableOn_const_iff hc]

@[simp]
theorem intervalIntegrable_const [IsLocallyFiniteMeasure Î¼]
    {c : E} (hc : â€–câ€–â‚‘ â‰  âŠ¤ := by finiteness) :
    IntervalIntegrable (fun _ => c) Î¼ a b :=
  intervalIntegrable_const_iff hc |>.2 <| Or.inr measure_Ioc_lt_top

protected theorem IntervalIntegrable.zero : IntervalIntegrable (0 : â„ â†’ E) Î¼ a b :=
  (intervalIntegrable_const_iff <| by finiteness).mpr <| .inl rfl

end

/-!
## Basic properties of interval integrability
- interval integrability is symmetric, reflexive, transitive
- monotonicity and strong measurability of the interval integral
- if `f` is interval integrable, so are its absolute value and norm
- arithmetic properties
-/
namespace IntervalIntegrable

section

variable {f : â„ â†’ Îµ} {a b c d : â„} {Î¼ Î½ : Measure â„}

@[symm]
nonrec theorem symm (h : IntervalIntegrable f Î¼ a b) : IntervalIntegrable f Î¼ b a :=
  h.symm

theorem symm_iff : IntervalIntegrable f Î¼ a b â†” IntervalIntegrable f Î¼ b a := âŸ¨.symm, .symmâŸ©

@[refl, simp]
theorem refl : IntervalIntegrable f Î¼ a a := by constructor <;> simp

variable [PseudoMetrizableSpace Îµ]

@[trans]
theorem trans {a b c : â„} (hab : IntervalIntegrable f Î¼ a b) (hbc : IntervalIntegrable f Î¼ b c) :
    IntervalIntegrable f Î¼ a c :=
  âŸ¨(hab.1.union hbc.1).mono_set Ioc_subset_Ioc_union_Ioc,
    (hbc.2.union hab.2).mono_set Ioc_subset_Ioc_union_IocâŸ©

theorem trans_iff (h : b âˆˆ [[a, c]]) :
    IntervalIntegrable f Î¼ a c â†” IntervalIntegrable f Î¼ a b âˆ§ IntervalIntegrable f Î¼ b c := by
  simp only [intervalIntegrable_iff, â† integrableOn_union, uIoc_union_uIoc h]

theorem trans_iterate_Ico {a : â„• â†’ â„} {m n : â„•} (hmn : m â‰¤ n)
    (hint : âˆ€ k âˆˆ Ico m n, IntervalIntegrable f Î¼ (a k) (a <| k + 1)) :
    IntervalIntegrable f Î¼ (a m) (a n) := by
  revert hint
  refine Nat.le_induction ?_ ?_ n hmn
  Â· simp
  Â· intro p hp IH h
    exact (IH fun k hk => h k (Ico_subset_Ico_right p.le_succ hk)).trans (h p (by simp [hp]))

theorem trans_iterate {a : â„• â†’ â„} {n : â„•}
    (hint : âˆ€ k < n, IntervalIntegrable f Î¼ (a k) (a <| k + 1)) :
    IntervalIntegrable f Î¼ (a 0) (a n) :=
  trans_iterate_Ico bot_le fun k hk => hint k hk.2

theorem neg {f : â„ â†’ E} (h : IntervalIntegrable f Î¼ a b) : IntervalIntegrable (-f) Î¼ a b :=
  âŸ¨h.1.neg, h.2.negâŸ©

omit [PseudoMetrizableSpace Îµ] in
theorem enorm (h : IntervalIntegrable f Î¼ a b) : IntervalIntegrable (â€–f Â·â€–â‚‘) Î¼ a b :=
  âŸ¨h.1.enorm, h.2.enormâŸ©

theorem norm {f : â„ â†’ E} (h : IntervalIntegrable f Î¼ a b) : IntervalIntegrable (â€–f Â·â€–) Î¼ a b :=
  âŸ¨h.1.norm, h.2.normâŸ©

theorem intervalIntegrable_enorm_iff {Î¼ : Measure â„} {a b : â„}
    (hf : AEStronglyMeasurable f (Î¼.restrict (Î™ a b))) :
    IntervalIntegrable (fun t => â€–f tâ€–â‚‘) Î¼ a b â†” IntervalIntegrable f Î¼ a b := by
  simp_rw [intervalIntegrable_iff, IntegrableOn, integrable_enorm_iff hf]

theorem intervalIntegrable_norm_iff {f : â„ â†’ E} {Î¼ : Measure â„} {a b : â„}
    (hf : AEStronglyMeasurable f (Î¼.restrict (Î™ a b))) :
    IntervalIntegrable (fun t => â€–f tâ€–) Î¼ a b â†” IntervalIntegrable f Î¼ a b := by
  simp_rw [intervalIntegrable_iff, IntegrableOn, integrable_norm_iff hf]

theorem abs {f : â„ â†’ â„} (h : IntervalIntegrable f Î¼ a b) :
    IntervalIntegrable (fun x => |f x|) Î¼ a b :=
  h.norm

theorem mono (hf : IntervalIntegrable f Î½ a b) (h1 : [[c, d]] âŠ† [[a, b]]) (h2 : Î¼ â‰¤ Î½) :
    IntervalIntegrable f Î¼ c d :=
  intervalIntegrable_iff.mpr <| hf.def'.mono (uIoc_subset_uIoc_of_uIcc_subset_uIcc h1) h2

theorem mono_measure (hf : IntervalIntegrable f Î½ a b) (h : Î¼ â‰¤ Î½) : IntervalIntegrable f Î¼ a b :=
  hf.mono Subset.rfl h

theorem mono_set (hf : IntervalIntegrable f Î¼ a b) (h : [[c, d]] âŠ† [[a, b]]) :
    IntervalIntegrable f Î¼ c d :=
  hf.mono h le_rfl

theorem mono_set_ae (hf : IntervalIntegrable f Î¼ a b) (h : Î™ c d â‰¤áµ[Î¼] Î™ a b) :
    IntervalIntegrable f Î¼ c d :=
  intervalIntegrable_iff.mpr <| hf.def'.mono_set_ae h

theorem mono_set' (hf : IntervalIntegrable f Î¼ a b) (hsub : Î™ c d âŠ† Î™ a b) :
    IntervalIntegrable f Î¼ c d :=
  hf.mono_set_ae <| Eventually.of_forall hsub

theorem mono_fun_enorm [PseudoMetrizableSpace Îµ'] {g : â„ â†’ Îµ'}
    (hf : IntervalIntegrable f Î¼ a b) (hgm : AEStronglyMeasurable g (Î¼.restrict (Î™ a b)))
    (hle : (â€–g Â·â€–â‚‘) â‰¤áµ[Î¼.restrict (Î™ a b)] (â€–f Â·â€–â‚‘)) : IntervalIntegrable g Î¼ a b :=
  intervalIntegrable_iff.2 <| hf.def'.integrable.mono_enorm hgm hle

theorem mono_fun {f : â„ â†’ E} [NormedAddCommGroup F] {g : â„ â†’ F} (hf : IntervalIntegrable f Î¼ a b)
    (hgm : AEStronglyMeasurable g (Î¼.restrict (Î™ a b)))
    (hle : (fun x => â€–g xâ€–) â‰¤áµ[Î¼.restrict (Î™ a b)] fun x => â€–f xâ€–) : IntervalIntegrable g Î¼ a b :=
  intervalIntegrable_iff.2 <| hf.def'.integrable.mono hgm hle

-- XXX: the best spelling of this lemma may look slightly different (e.gl, with different domain)
theorem mono_fun_enorm' {f : â„ â†’ Îµ} {g : â„ â†’ â„â‰¥0âˆ} (hg : IntervalIntegrable g Î¼ a b)
    (hfm : AEStronglyMeasurable f (Î¼.restrict (Î™ a b)))
    (hle : (fun x => â€–f xâ€–â‚‘) â‰¤áµ[Î¼.restrict (Î™ a b)] g) : IntervalIntegrable f Î¼ a b :=
  intervalIntegrable_iff.2 <| hg.def'.integrable.mono_enorm hfm hle

theorem mono_fun' {f : â„ â†’ E} {g : â„ â†’ â„} (hg : IntervalIntegrable g Î¼ a b)
    (hfm : AEStronglyMeasurable f (Î¼.restrict (Î™ a b)))
    (hle : (fun x => â€–f xâ€–) â‰¤áµ[Î¼.restrict (Î™ a b)] g) : IntervalIntegrable f Î¼ a b :=
  intervalIntegrable_iff.2 <| hg.def'.integrable.mono' hfm hle

omit [PseudoMetrizableSpace Îµ] in
protected theorem aestronglyMeasurable (h : IntervalIntegrable f Î¼ a b) :
    AEStronglyMeasurable f (Î¼.restrict (Ioc a b)) :=
  h.1.aestronglyMeasurable

omit [PseudoMetrizableSpace Îµ] in
protected theorem aestronglyMeasurable' (h : IntervalIntegrable f Î¼ a b) :
    AEStronglyMeasurable f (Î¼.restrict (Ioc b a)) :=
  h.2.aestronglyMeasurable

end

variable [NormedRing A] {f g : â„ â†’ Îµ} {a b : â„} {Î¼ : Measure â„}

theorem smul {R : Type*} [NormedAddCommGroup R] [SMulZeroClass R E] [IsBoundedSMul R E] {f : â„ â†’ E}
    (h : IntervalIntegrable f Î¼ a b) (r : R) :
    IntervalIntegrable (r â€¢ f) Î¼ a b :=
  âŸ¨h.1.smul r, h.2.smul râŸ©

@[simp]
theorem add [ContinuousAdd Îµ] (hf : IntervalIntegrable f Î¼ a b) (hg : IntervalIntegrable g Î¼ a b) :
    IntervalIntegrable (fun x => f x + g x) Î¼ a b :=
  âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

@[simp]
theorem sub {f g : â„ â†’ E} (hf : IntervalIntegrable f Î¼ a b) (hg : IntervalIntegrable g Î¼ a b) :
    IntervalIntegrable (fun x => f x - g x) Î¼ a b :=
  âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©

theorem sum {Îµ} [TopologicalSpace Îµ] [ENormedAddCommMonoid Îµ] [ContinuousAdd Îµ]
    (s : Finset Î¹) {f : Î¹ â†’ â„ â†’ Îµ} (h : âˆ€ i âˆˆ s, IntervalIntegrable (f i) Î¼ a b) :
    IntervalIntegrable (âˆ‘ i âˆˆ s, f i) Î¼ a b :=
  âŸ¨integrable_finset_sum' s fun i hi => (h i hi).1, integrable_finset_sum' s fun i hi => (h i hi).2âŸ©

/-- Finite sums of interval integrable functions are interval integrable. -/
@[simp]
protected theorem finsum
    {Îµ} [TopologicalSpace Îµ] [ENormedAddCommMonoid Îµ] [ContinuousAdd Îµ] [PseudoMetrizableSpace Îµ]
    {f : Î¹ â†’ â„ â†’ Îµ} (h : âˆ€ i, IntervalIntegrable (f i) Î¼ a b) :
    IntervalIntegrable (âˆ‘á¶  i, f i) Î¼ a b := by
  by_cases hâ‚ : f.support.Finite
  Â· simp [finsum_eq_sum _ hâ‚, IntervalIntegrable.sum hâ‚.toFinset (fun i _ â†¦ h i)]
  Â· rw [finsum_of_infinite_support hâ‚]
    apply intervalIntegrable_const_iff (c := 0) (by simp) |>.2
    tauto

section Mul

theorem mul_continuousOn {f g : â„ â†’ A} (hf : IntervalIntegrable f Î¼ a b)
    (hg : ContinuousOn g [[a, b]]) : IntervalIntegrable (fun x => f x * g x) Î¼ a b := by
  rw [intervalIntegrable_iff] at hf âŠ¢
  exact hf.mul_continuousOn_of_subset hg measurableSet_Ioc isCompact_uIcc Ioc_subset_Icc_self

theorem continuousOn_mul {f g : â„ â†’ A} (hf : IntervalIntegrable f Î¼ a b)
    (hg : ContinuousOn g [[a, b]]) : IntervalIntegrable (fun x => g x * f x) Î¼ a b := by
  rw [intervalIntegrable_iff] at hf âŠ¢
  exact hf.continuousOn_mul_of_subset hg isCompact_uIcc measurableSet_Ioc Ioc_subset_Icc_self

@[simp]
theorem const_mul {f : â„ â†’ A} (hf : IntervalIntegrable f Î¼ a b) (c : A) :
    IntervalIntegrable (fun x => c * f x) Î¼ a b :=
  hf.continuousOn_mul continuousOn_const

@[simp]
theorem mul_const {f : â„ â†’ A} (hf : IntervalIntegrable f Î¼ a b) (c : A) :
    IntervalIntegrable (fun x => f x * c) Î¼ a b :=
  hf.mul_continuousOn continuousOn_const

end Mul

section SMul

variable {f : â„ â†’ ğ•œ} {g : â„ â†’ E} [NormedRing ğ•œ] [Module ğ•œ E] [NormSMulClass ğ•œ E]

theorem smul_continuousOn (hf : IntervalIntegrable f Î¼ a b)
    (hg : ContinuousOn g [[a, b]]) : IntervalIntegrable (fun x => f x â€¢ g x) Î¼ a b := by
  rw [intervalIntegrable_iff] at hf âŠ¢
  exact hf.smul_continuousOn_of_subset hg measurableSet_Ioc isCompact_uIcc Ioc_subset_Icc_self

theorem continuousOn_smul (hg : IntervalIntegrable g Î¼ a b)
    (hf : ContinuousOn f [[a, b]]) : IntervalIntegrable (fun x => f x â€¢ g x) Î¼ a b := by
  rw [intervalIntegrable_iff] at hg âŠ¢
  exact hg.continuousOn_smul_of_subset hf isCompact_uIcc measurableSet_Ioc Ioc_subset_Icc_self

end SMul

@[simp]
theorem div_const {ğ•œ : Type*} {f : â„ â†’ ğ•œ} [NormedDivisionRing ğ•œ] (h : IntervalIntegrable f Î¼ a b)
    (c : ğ•œ) : IntervalIntegrable (fun x => f x / c) Î¼ a b := by
  simpa only [div_eq_mul_inv] using mul_const h câ»Â¹

variable [PseudoMetrizableSpace Îµ]

theorem comp_mul_left (hf : IntervalIntegrable f volume a b) {c : â„}
    (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness)
    (h' : â€–f (c * min (a / c) (b / c))â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x => f (c * x)) volume (a / c) (b / c) := by
  rcases eq_or_ne c 0 with (hc | hc); Â· rw [hc]; simp
  rw [intervalIntegrable_iff' h] at hf
  rw [intervalIntegrable_iff' h'] at âŠ¢
  have A : MeasurableEmbedding fun x => x * câ»Â¹ :=
    (Homeomorph.mulRightâ‚€ _ (inv_ne_zero hc)).isClosedEmbedding.measurableEmbedding
  rw [â† Real.smul_map_volume_mul_right (inv_ne_zero hc), IntegrableOn, Measure.restrict_smul,
    integrable_smul_measure (by simpa : ENNReal.ofReal |câ»Â¹| â‰  0) ENNReal.ofReal_ne_top,
    â† IntegrableOn, MeasurableEmbedding.integrableOn_map_iff A]
  convert hf using 1
  Â· ext; simp only [comp_apply]; congr 1; field_simp
  Â· rw [preimage_mul_const_uIcc (inv_ne_zero hc)]; field_simp

-- Note that `h'` is **not** implied by `h` if `c` is negative.
-- TODO: generalise this lemma to enorms!
theorem comp_mul_left_iff {f : â„ â†’ E} {c : â„} (hc : c â‰  0) (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness)
    (h' : â€–f (c * min (a / c) (b / c))â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x â†¦ f (c * x)) volume (a / c) (b / c) â†”
      IntervalIntegrable f volume a b := by
  exact âŸ¨fun h â†¦ by simpa [hc] using h.comp_mul_left (c := câ»Â¹) h' (by simp),
    (comp_mul_left Â· h h')âŸ©

theorem comp_mul_right (hf : IntervalIntegrable f volume a b) {c : â„}
    (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness)
    (h' : â€–f (c * min (a / c) (b / c))â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x => f (x * c)) volume (a / c) (b / c) := by
  simpa only [mul_comm] using comp_mul_left hf h h'

theorem comp_add_right (hf : IntervalIntegrable f volume a b) (c : â„)
    (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x â†¦ f (x + c)) volume (a - c) (b - c) := by
  have h' : â€–f (min (a - c) (b - c) + c)â€–â‚‘ â‰  âŠ¤ := by
    rw [min_sub_sub_right, sub_add, sub_self, sub_zero]
    exact h
  wlog hab : a â‰¤ b generalizing a b
  Â· apply IntervalIntegrable.symm (this hf.symm ?_ ?_ (le_of_not_ge hab))
    Â· rw [min_comm]; exact h
    Â· rw [min_comm]; exact h'
  rw [intervalIntegrable_iff' h] at hf
  rw [intervalIntegrable_iff' h'] at âŠ¢
  have A : MeasurableEmbedding fun x => x + c :=
    (Homeomorph.addRight c).isClosedEmbedding.measurableEmbedding
  rw [â† map_add_right_eq_self volume c] at hf
  convert (MeasurableEmbedding.integrableOn_map_iff A).mp hf using 1
  rw [preimage_add_const_uIcc]

theorem comp_add_left (hf : IntervalIntegrable f volume a b) (c : â„)
    (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x â†¦ f (c + x)) volume (a - c) (b - c) := by
  simpa [add_comm] using IntervalIntegrable.comp_add_right hf c h

theorem comp_sub_right (hf : IntervalIntegrable f volume a b) (c : â„)
    (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x â†¦ f (x - c)) volume (a + c) (b + c) := by
  simpa only [sub_neg_eq_add] using IntervalIntegrable.comp_add_right hf (-c) h

-- TODO: generalise this lemma to enorms!
theorem iff_comp_neg {f : â„ â†’ E} (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f volume a b â†” IntervalIntegrable (fun x â†¦ f (-x)) volume (-a) (-b) := by
  rw [â† comp_mul_left_iff (neg_ne_zero.2 one_ne_zero) h (by simp)]; simp [div_neg]

-- TODO: generalise this lemma to enorms!
theorem comp_sub_left {f : â„ â†’ E} (hf : IntervalIntegrable f volume a b) (c : â„)
    (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x â†¦ f (c - x)) volume (c - a) (c - b) := by
  simpa only [neg_sub, â† sub_eq_add_neg] using (iff_comp_neg (by simp)).mp (hf.comp_add_left c h)

-- TODO: generalise this lemma to enorms!
theorem comp_sub_left_iff {f : â„ â†’ E} (c : â„) (h : â€–f (min a b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable (fun x => f (c - x)) volume (c - a) (c - b) â†”
      IntervalIntegrable f volume a b :=
  âŸ¨fun h â†¦ by simpa using h.comp_sub_left c, (.comp_sub_left Â· c h)âŸ©

end IntervalIntegrable

/-!
## Continuous functions are interval integrable
-/
section

variable {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼]

theorem ContinuousOn.intervalIntegrable {u : â„ â†’ E} {a b : â„} (hu : ContinuousOn u (uIcc a b)) :
    IntervalIntegrable u Î¼ a b :=
  (ContinuousOn.integrableOn_Icc hu).intervalIntegrable

theorem ContinuousOn.intervalIntegrable_of_Icc {u : â„ â†’ E} {a b : â„} (h : a â‰¤ b)
    (hu : ContinuousOn u (Icc a b)) : IntervalIntegrable u Î¼ a b :=
  ContinuousOn.intervalIntegrable ((uIcc_of_le h).symm â–¸ hu)

/-- A continuous function on `â„` is `IntervalIntegrable` with respect to any locally finite measure
`Î½` on â„. -/
theorem Continuous.intervalIntegrable {u : â„ â†’ E} (hu : Continuous u) (a b : â„) :
    IntervalIntegrable u Î¼ a b :=
  hu.continuousOn.intervalIntegrable

end

/-!
## Monotone and antitone functions are integral integrable
-/
section

variable {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼] [ConditionallyCompleteLinearOrder E]
  [OrderTopology E] [SecondCountableTopology E]

theorem MonotoneOn.intervalIntegrable {u : â„ â†’ E} {a b : â„} (hu : MonotoneOn u (uIcc a b)) :
    IntervalIntegrable u Î¼ a b := by
  rw [intervalIntegrable_iff]
  exact (hu.integrableOn_isCompact isCompact_uIcc).mono_set Ioc_subset_Icc_self

theorem AntitoneOn.intervalIntegrable {u : â„ â†’ E} {a b : â„} (hu : AntitoneOn u (uIcc a b)) :
    IntervalIntegrable u Î¼ a b :=
  hu.dual_right.intervalIntegrable

theorem Monotone.intervalIntegrable {u : â„ â†’ E} {a b : â„} (hu : Monotone u) :
    IntervalIntegrable u Î¼ a b :=
  (hu.monotoneOn _).intervalIntegrable

theorem Antitone.intervalIntegrable {u : â„ â†’ E} {a b : â„} (hu : Antitone u) :
    IntervalIntegrable u Î¼ a b :=
  (hu.antitoneOn _).intervalIntegrable

end

/-!
## Interval integrability of functions with even or odd parity
-/
section

variable {f : â„ â†’ E}

/-- An even function is interval integrable (with respect to the volume measure) on every interval
of the form `0..x` if it is interval integrable (with respect to the volume measure) on every
interval of the form `0..x`, for positive `x`.

See `intervalIntegrable_of_even` for a stronger result. -/
lemma intervalIntegrable_of_evenâ‚€ (hâ‚f : âˆ€ x, f x = f (-x))
    (hâ‚‚f : âˆ€ x, 0 < x â†’ IntervalIntegrable f volume 0 x)
    {t : â„} (ht : â€–f (min 0 t)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f volume 0 t := by
  rcases lt_trichotomy t 0 with h | h | h
  Â· rw [IntervalIntegrable.iff_comp_neg ht]
    conv => arg 1; intro t; rw [â† hâ‚f]
    simp [hâ‚‚f (-t) (by simp [h])]
  Â· rw [h]
  Â· exact hâ‚‚f t h

/-- An even function is interval integrable (with respect to the volume measure) on every interval
if it is interval integrable (with respect to the volume measure) on every interval of the form
`0..x`, for positive `x`. -/
theorem intervalIntegrable_of_even
    (hâ‚f : âˆ€ x, f x = f (-x)) (hâ‚‚f : âˆ€ x, 0 < x â†’ IntervalIntegrable f volume 0 x) {a b : â„}
    (ha : â€–f (min 0 a)â€–â‚‘ â‰  âˆ := by finiteness) (hb : â€–f (min 0 b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f volume a b :=
  -- Split integral and apply lemma
  (intervalIntegrable_of_evenâ‚€ hâ‚f hâ‚‚f ha).symm.trans (b := 0)
    (intervalIntegrable_of_evenâ‚€ hâ‚f hâ‚‚f hb)

/-- An odd function is interval integrable (with respect to the volume measure) on every interval
of the form `0..x` if it is interval integrable (with respect to the volume measure) on every
interval of the form `0..x`, for positive `x`.

See `intervalIntegrable_of_odd` for a stronger result. -/
lemma intervalIntegrable_of_oddâ‚€ (hâ‚f : âˆ€ x, -f x = f (-x))
    (hâ‚‚f : âˆ€ x, 0 < x â†’ IntervalIntegrable f volume 0 x) {t : â„}
    (ht : â€–f (min 0 t)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f volume 0 t := by
  rcases lt_trichotomy t 0 with h | h | h
  Â· rw [IntervalIntegrable.iff_comp_neg]
    conv => arg 1; intro t; rw [â† hâ‚f]
    apply IntervalIntegrable.neg
    simp [hâ‚‚f (-t) (by simp [h])]
  Â· rw [h]
  Â· exact hâ‚‚f t h

/-- An odd function is interval integrable (with respect to the volume measure) on every interval
iff it is interval integrable (with respect to the volume measure) on every interval of the form
`0..x`, for positive `x`. -/
theorem intervalIntegrable_of_odd
    (hâ‚f : âˆ€ x, -f x = f (-x)) (hâ‚‚f : âˆ€ x, 0 < x â†’ IntervalIntegrable f volume 0 x) {a b : â„}
    (ha : â€–f (min 0 a)â€–â‚‘ â‰  âˆ := by finiteness) (hb : â€–f (min 0 b)â€–â‚‘ â‰  âˆ := by finiteness) :
    IntervalIntegrable f volume a b :=
  -- Split integral and apply lemma
  (intervalIntegrable_of_oddâ‚€ hâ‚f hâ‚‚f ha).symm.trans (intervalIntegrable_of_oddâ‚€ hâ‚f hâ‚‚f hb)

end

/-!
## Limits of intervals
-/

/-- Let `l'` be a measurably generated filter; let `l` be a of filter such that each `s âˆˆ l'`
eventually includes `Ioc u v` as both `u` and `v` tend to `l`. Let `Î¼` be a measure finite at `l'`.

Suppose that `f : â„ â†’ E` has a finite limit at `l' âŠ“ ae Î¼`. Then `f` is interval integrable on
`u..v` provided that both `u` and `v` tend to `l`.

Typeclass instances allow Lean to find `l'` based on `l` but not vice versa, so
`apply Tendsto.eventually_intervalIntegrable_ae` will generate goals `Filter â„` and
`TendstoIxxClass Ioc ?m_1 l'`. -/
theorem Filter.Tendsto.eventually_intervalIntegrable_ae {f : â„ â†’ E} {Î¼ : Measure â„}
    {l l' : Filter â„} (hfm : StronglyMeasurableAtFilter f l' Î¼) [TendstoIxxClass Ioc l l']
    [IsMeasurablyGenerated l'] (hÎ¼ : Î¼.FiniteAtFilter l') {c : E} (hf : Tendsto f (l' âŠ“ ae Î¼) (ğ“ c))
    {u v : Î¹ â†’ â„} {lt : Filter Î¹} (hu : Tendsto u lt l) (hv : Tendsto v lt l) :
    âˆ€á¶  t in lt, IntervalIntegrable f Î¼ (u t) (v t) :=
  have := (hf.integrableAtFilter_ae hfm hÎ¼).eventually
  ((hu.Ioc hv).eventually this).and <| (hv.Ioc hu).eventually this

/-- Let `l'` be a measurably generated filter; let `l` be a of filter such that each `s âˆˆ l'`
eventually includes `Ioc u v` as both `u` and `v` tend to `l`. Let `Î¼` be a measure finite at `l'`.

Suppose that `f : â„ â†’ E` has a finite limit at `l`. Then `f` is interval integrable on `u..v`
provided that both `u` and `v` tend to `l`.

Typeclass instances allow Lean to find `l'` based on `l` but not vice versa, so
`apply Tendsto.eventually_intervalIntegrable` will generate goals `Filter â„` and
`TendstoIxxClass Ioc ?m_1 l'`. -/
theorem Filter.Tendsto.eventually_intervalIntegrable {f : â„ â†’ E} {Î¼ : Measure â„} {l l' : Filter â„}
    (hfm : StronglyMeasurableAtFilter f l' Î¼) [TendstoIxxClass Ioc l l'] [IsMeasurablyGenerated l']
    (hÎ¼ : Î¼.FiniteAtFilter l') {c : E} (hf : Tendsto f l' (ğ“ c)) {u v : Î¹ â†’ â„} {lt : Filter Î¹}
    (hu : Tendsto u lt l) (hv : Tendsto v lt l) : âˆ€á¶  t in lt, IntervalIntegrable f Î¼ (u t) (v t) :=
  (hf.mono_left inf_le_left).eventually_intervalIntegrable_ae hfm hÎ¼ hu hv

/-!
### Interval integral: definition and basic properties

In this section we define `âˆ« x in a..b, f x âˆ‚Î¼` as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`
and prove some basic properties.
-/

variable [NormedSpace â„ E]

/-- The interval integral `âˆ« x in a..b, f x âˆ‚Î¼` is defined
as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`. If `a â‰¤ b`, then it equals
`âˆ« x in Ioc a b, f x âˆ‚Î¼`, otherwise it equals `-âˆ« x in Ioc b a, f x âˆ‚Î¼`. -/
def intervalIntegral (f : â„ â†’ E) (a b : â„) (Î¼ : Measure â„) : E :=
  (âˆ« x in Ioc a b, f x âˆ‚Î¼) - âˆ« x in Ioc b a, f x âˆ‚Î¼

@[inherit_doc intervalIntegral]
notation3"âˆ« "(...)" in "a".."b", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => intervalIntegral r a b Î¼

/-- The interval integral `âˆ« x in a..b, f x` is defined
as `âˆ« x in Ioc a b, f x - âˆ« x in Ioc b a, f x`. If `a â‰¤ b`, then it equals
`âˆ« x in Ioc a b, f x`, otherwise it equals `-âˆ« x in Ioc b a, f x`. -/
notation3"âˆ« "(...)" in "a".."b", "r:60:(scoped f => intervalIntegral f a b volume) => r

namespace intervalIntegral

section Basic

variable {a b : â„} {f g : â„ â†’ E} {Î¼ : Measure â„}

@[simp]
theorem integral_zero : (âˆ« _ in a..b, (0 : E) âˆ‚Î¼) = 0 := by simp [intervalIntegral]

theorem integral_of_le (h : a â‰¤ b) : âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in Ioc a b, f x âˆ‚Î¼ := by
  simp [intervalIntegral, h]

@[simp]
theorem integral_same : âˆ« x in a..a, f x âˆ‚Î¼ = 0 :=
  sub_self _

theorem integral_symm (a b) : âˆ« x in b..a, f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ := by
  simp only [intervalIntegral, neg_sub]

theorem integral_of_ge (h : b â‰¤ a) : âˆ« x in a..b, f x âˆ‚Î¼ = -âˆ« x in Ioc b a, f x âˆ‚Î¼ := by
  simp only [integral_symm b, integral_of_le h]

theorem intervalIntegral_eq_integral_uIoc (f : â„ â†’ E) (a b : â„) (Î¼ : Measure â„) :
    âˆ« x in a..b, f x âˆ‚Î¼ = (if a â‰¤ b then 1 else -1 : â„) â€¢ âˆ« x in Î™ a b, f x âˆ‚Î¼ := by
  split_ifs with h
  Â· simp only [integral_of_le h, uIoc_of_le h, one_smul]
  Â· simp only [integral_of_ge (not_le.1 h).le, uIoc_of_ge (not_le.1 h).le, neg_one_smul]

theorem norm_intervalIntegral_eq (f : â„ â†’ E) (a b : â„) (Î¼ : Measure â„) :
    â€–âˆ« x in a..b, f x âˆ‚Î¼â€– = â€–âˆ« x in Î™ a b, f x âˆ‚Î¼â€– := by
  simp_rw [intervalIntegral_eq_integral_uIoc, norm_smul]
  split_ifs <;> simp only [norm_neg, norm_one, one_mul]

theorem abs_intervalIntegral_eq (f : â„ â†’ â„) (a b : â„) (Î¼ : Measure â„) :
    |âˆ« x in a..b, f x âˆ‚Î¼| = |âˆ« x in Î™ a b, f x âˆ‚Î¼| :=
  norm_intervalIntegral_eq f a b Î¼

theorem integral_cases (f : â„ â†’ E) (a b) :
    (âˆ« x in a..b, f x âˆ‚Î¼) âˆˆ ({âˆ« x in Î™ a b, f x âˆ‚Î¼, -âˆ« x in Î™ a b, f x âˆ‚Î¼} : Set E) := by
  rw [intervalIntegral_eq_integral_uIoc]; split_ifs <;> simp

nonrec theorem integral_undef (h : Â¬IntervalIntegrable f Î¼ a b) : âˆ« x in a..b, f x âˆ‚Î¼ = 0 := by
  rw [intervalIntegrable_iff] at h
  rw [intervalIntegral_eq_integral_uIoc, integral_undef h, smul_zero]

theorem intervalIntegrable_of_integral_ne_zero {a b : â„} {f : â„ â†’ E} {Î¼ : Measure â„}
    (h : (âˆ« x in a..b, f x âˆ‚Î¼) â‰  0) : IntervalIntegrable f Î¼ a b :=
  not_imp_comm.1 integral_undef h

nonrec theorem integral_non_aestronglyMeasurable
    (hf : Â¬AEStronglyMeasurable f (Î¼.restrict (Î™ a b))) :
    âˆ« x in a..b, f x âˆ‚Î¼ = 0 := by
  rw [intervalIntegral_eq_integral_uIoc, integral_non_aestronglyMeasurable hf, smul_zero]

theorem integral_non_aestronglyMeasurable_of_le (h : a â‰¤ b)
    (hf : Â¬AEStronglyMeasurable f (Î¼.restrict (Ioc a b))) : âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
  integral_non_aestronglyMeasurable <| by rwa [uIoc_of_le h]

theorem norm_integral_min_max (f : â„ â†’ E) :
    â€–âˆ« x in min a b..max a b, f x âˆ‚Î¼â€– = â€–âˆ« x in a..b, f x âˆ‚Î¼â€– := by
  cases le_total a b <;> simp [*, integral_symm a b]

theorem norm_integral_eq_norm_integral_uIoc (f : â„ â†’ E) :
    â€–âˆ« x in a..b, f x âˆ‚Î¼â€– = â€–âˆ« x in Î™ a b, f x âˆ‚Î¼â€– := by
  rw [â† norm_integral_min_max, integral_of_le min_le_max, uIoc]

@[deprecated (since := "2025-04-19")]
alias norm_integral_eq_norm_integral_Ioc := norm_integral_eq_norm_integral_uIoc

theorem abs_integral_eq_abs_integral_uIoc (f : â„ â†’ â„) :
    |âˆ« x in a..b, f x âˆ‚Î¼| = |âˆ« x in Î™ a b, f x âˆ‚Î¼| :=
  norm_integral_eq_norm_integral_uIoc f

theorem norm_integral_le_integral_norm_uIoc : â€–âˆ« x in a..b, f x âˆ‚Î¼â€– â‰¤ âˆ« x in Î™ a b, â€–f xâ€– âˆ‚Î¼ :=
  calc
    â€–âˆ« x in a..b, f x âˆ‚Î¼â€– = â€–âˆ« x in Î™ a b, f x âˆ‚Î¼â€– := norm_integral_eq_norm_integral_uIoc f
    _ â‰¤ âˆ« x in Î™ a b, â€–f xâ€– âˆ‚Î¼ := norm_integral_le_integral_norm f

@[deprecated (since := "2025-04-19")]
alias norm_integral_le_integral_norm_Ioc := norm_integral_le_integral_norm_uIoc

theorem norm_integral_le_abs_integral_norm : â€–âˆ« x in a..b, f x âˆ‚Î¼â€– â‰¤ |âˆ« x in a..b, â€–f xâ€– âˆ‚Î¼| := by
  simp only [â† Real.norm_eq_abs, norm_integral_eq_norm_integral_uIoc]
  exact le_trans (norm_integral_le_integral_norm _) (le_abs_self _)

theorem norm_integral_le_integral_norm (h : a â‰¤ b) :
    â€–âˆ« x in a..b, f x âˆ‚Î¼â€– â‰¤ âˆ« x in a..b, â€–f xâ€– âˆ‚Î¼ :=
  norm_integral_le_integral_norm_uIoc.trans_eq <| by rw [uIoc_of_le h, integral_of_le h]

theorem norm_integral_le_abs_of_norm_le {g : â„ â†’ â„} (h : âˆ€áµ t âˆ‚Î¼.restrict <| Î™ a b, â€–f tâ€– â‰¤ g t)
    (hbound : IntervalIntegrable g Î¼ a b) : â€–âˆ« t in a..b, f t âˆ‚Î¼â€– â‰¤ |âˆ« t in a..b, g t âˆ‚Î¼| := by
  rw [norm_intervalIntegral_eq, abs_intervalIntegral_eq]
  exact (norm_integral_le_of_norm_le hbound.def' h).trans (le_abs_self _)

theorem norm_integral_le_of_norm_le {g : â„ â†’ â„} (hab : a â‰¤ b)
    (h : âˆ€áµ t âˆ‚Î¼, t âˆˆ Set.Ioc a b â†’ â€–f tâ€– â‰¤ g t) (hbound : IntervalIntegrable g Î¼ a b) :
    â€–âˆ« t in a..b, f t âˆ‚Î¼â€– â‰¤ âˆ« t in a..b, g t âˆ‚Î¼ := by
  simp only [integral_of_le hab, â† ae_restrict_iff' measurableSet_Ioc] at *
  exact MeasureTheory.norm_integral_le_of_norm_le hbound.1 h

theorem norm_integral_le_of_norm_le_const_ae {a b C : â„} {f : â„ â†’ E}
    (h : âˆ€áµ x, x âˆˆ Î™ a b â†’ â€–f xâ€– â‰¤ C) : â€–âˆ« x in a..b, f xâ€– â‰¤ C * |b - a| := by
  rw [norm_integral_eq_norm_integral_uIoc]
  convert norm_setIntegral_le_of_norm_le_const_ae' _ h using 1
  Â· rw [uIoc, Real.volume_real_Ioc_of_le inf_le_sup, max_sub_min_eq_abs]
  Â· simp [uIoc, Real.volume_Ioc]

theorem norm_integral_le_of_norm_le_const {a b C : â„} {f : â„ â†’ E} (h : âˆ€ x âˆˆ Î™ a b, â€–f xâ€– â‰¤ C) :
    â€–âˆ« x in a..b, f xâ€– â‰¤ C * |b - a| :=
  norm_integral_le_of_norm_le_const_ae <| Eventually.of_forall h

@[simp]
nonrec theorem integral_add (hf : IntervalIntegrable f Î¼ a b) (hg : IntervalIntegrable g Î¼ a b) :
    âˆ« x in a..b, f x + g x âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) + âˆ« x in a..b, g x âˆ‚Î¼ := by
  simp only [intervalIntegral_eq_integral_uIoc, integral_add hf.def' hg.def', smul_add]

nonrec theorem integral_finset_sum {Î¹} {s : Finset Î¹} {f : Î¹ â†’ â„ â†’ E}
    (h : âˆ€ i âˆˆ s, IntervalIntegrable (f i) Î¼ a b) :
    âˆ« x in a..b, âˆ‘ i âˆˆ s, f i x âˆ‚Î¼ = âˆ‘ i âˆˆ s, âˆ« x in a..b, f i x âˆ‚Î¼ := by
  simp only [intervalIntegral_eq_integral_uIoc, integral_finset_sum s fun i hi => (h i hi).def',
    Finset.smul_sum]

@[simp]
nonrec theorem integral_neg : âˆ« x in a..b, -f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ := by
  simp only [intervalIntegral, integral_neg]; abel

@[simp]
theorem integral_sub (hf : IntervalIntegrable f Î¼ a b) (hg : IntervalIntegrable g Î¼ a b) :
    âˆ« x in a..b, f x - g x âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) - âˆ« x in a..b, g x âˆ‚Î¼ := by
  simpa only [sub_eq_add_neg] using (integral_add hf hg.neg).trans (congr_arg _ integral_neg)

/-- Compatibility with scalar multiplication. Note this assumes `ğ•œ` is a division ring in order to
ensure that for `c â‰  0`, `c â€¢ f` is integrable iff `f` is. For scalar multiplication by more
general rings assuming integrability, see `IntervalIntegrable.integral_smul`. -/
@[simp]
nonrec theorem integral_smul [NormedDivisionRing ğ•œ] [Module ğ•œ E] [NormSMulClass ğ•œ E]
    [SMulCommClass â„ ğ•œ E] (r : ğ•œ) (f : â„ â†’ E) :
    âˆ« x in a..b, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x in a..b, f x âˆ‚Î¼ := by
  simp only [intervalIntegral, integral_smul, smul_sub]

theorem _root_.IntervalIntegrable.integral_smul
    {R : Type*} [NormedRing R] [Module R E] [IsBoundedSMul R E] [SMulCommClass â„ R E]
    {f : â„ â†’ E} (r : R) (hf : IntervalIntegrable f Î¼ a b) :
    âˆ« x in a..b, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x in a..b, f x âˆ‚Î¼ := by
  simp only [intervalIntegral, smul_sub, hf.1.integral_smul, hf.2.integral_smul]

@[simp]
nonrec theorem integral_smul_const [CompleteSpace E]
    {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] (f : â„ â†’ ğ•œ) (c : E) :
    âˆ« x in a..b, f x â€¢ c âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) â€¢ c := by
  simp only [intervalIntegral_eq_integral_uIoc, integral_smul_const, smul_assoc]

@[simp]
theorem integral_const_mul [NormedDivisionRing ğ•œ] [NormedAlgebra â„ ğ•œ] (r : ğ•œ) (f : â„ â†’ ğ•œ) :
    âˆ« x in a..b, r * f x âˆ‚Î¼ = r * âˆ« x in a..b, f x âˆ‚Î¼ :=
  integral_smul r f

@[simp]
theorem integral_mul_const {ğ•œ : Type*} [RCLike ğ•œ] (r : ğ•œ) (f : â„ â†’ ğ•œ) :
    âˆ« x in a..b, f x * r âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) * r := by
  simpa only [mul_comm r] using integral_const_mul r f

@[simp]
theorem integral_div {ğ•œ : Type*} [RCLike ğ•œ] (r : ğ•œ) (f : â„ â†’ ğ•œ) :
    âˆ« x in a..b, f x / r âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) / r := by
  simpa only [div_eq_mul_inv] using integral_mul_const râ»Â¹ f

theorem integral_const' [CompleteSpace E] (c : E) :
    âˆ« _ in a..b, c âˆ‚Î¼ = (Î¼.real (Ioc a b) - Î¼.real (Ioc b a)) â€¢ c := by
  simp only [measureReal_def, intervalIntegral, setIntegral_const, sub_smul]

@[simp]
theorem integral_const [CompleteSpace E] (c : E) : âˆ« _ in a..b, c = (b - a) â€¢ c := by
  simp only [integral_const', Real.volume_Ioc, ENNReal.toReal_ofReal', â† neg_sub b,
    max_zero_sub_eq_self, measureReal_def]

nonrec theorem integral_smul_measure (c : â„â‰¥0âˆ) :
    âˆ« x in a..b, f x âˆ‚c â€¢ Î¼ = c.toReal â€¢ âˆ« x in a..b, f x âˆ‚Î¼ := by
  simp only [intervalIntegral, Measure.restrict_smul, integral_smul_measure, smul_sub]

end Basic

-- TODO: add `Complex.ofReal` version of `_root_.integral_ofReal`

nonrec theorem _root_.RCLike.intervalIntegral_ofReal {ğ•œ : Type*} [RCLike ğ•œ] {a b : â„}
    {Î¼ : Measure â„} {f : â„ â†’ â„} : (âˆ« x in a..b, (f x : ğ•œ) âˆ‚Î¼) = â†‘(âˆ« x in a..b, f x âˆ‚Î¼) := by
  simp only [intervalIntegral, integral_ofReal, RCLike.ofReal_sub]

nonrec theorem integral_ofReal {a b : â„} {Î¼ : Measure â„} {f : â„ â†’ â„} :
    (âˆ« x in a..b, (f x : â„‚) âˆ‚Î¼) = â†‘(âˆ« x in a..b, f x âˆ‚Î¼) :=
  RCLike.intervalIntegral_ofReal

section ContinuousLinearMap

variable {a b : â„} {Î¼ : Measure â„} {f : â„ â†’ E}
variable [RCLike ğ•œ] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

open ContinuousLinearMap

theorem _root_.ContinuousLinearMap.intervalIntegral_apply {a b : â„} {Ï† : â„ â†’ F â†’L[ğ•œ] E}
    (hÏ† : IntervalIntegrable Ï† Î¼ a b) (v : F) :
    (âˆ« x in a..b, Ï† x âˆ‚Î¼) v = âˆ« x in a..b, Ï† x v âˆ‚Î¼ := by
  simp_rw [intervalIntegral_eq_integral_uIoc, â† integral_apply hÏ†.def' v, coe_smul', Pi.smul_apply]

variable [NormedSpace â„ F] [CompleteSpace F]

theorem _root_.ContinuousLinearMap.intervalIntegral_comp_comm [CompleteSpace E] (L : E â†’L[ğ•œ] F)
    (hf : IntervalIntegrable f Î¼ a b) : (âˆ« x in a..b, L (f x) âˆ‚Î¼) = L (âˆ« x in a..b, f x âˆ‚Î¼) := by
  simp_rw [intervalIntegral, L.integral_comp_comm hf.1, L.integral_comp_comm hf.2, L.map_sub]

end ContinuousLinearMap

/-!
## Basic arithmetic
Includes addition, scalar multiplication and affine transformations.
-/
section Comp

variable {a b c d : â„} (f : â„ â†’ E)

@[simp]
theorem integral_comp_mul_right (hc : c â‰  0) :
    (âˆ« x in a..b, f (x * c)) = câ»Â¹ â€¢ âˆ« x in a * c..b * c, f x := by
  have A : MeasurableEmbedding fun x => x * c :=
    (Homeomorph.mulRightâ‚€ c hc).isClosedEmbedding.measurableEmbedding
  conv_rhs => rw [â† Real.smul_map_volume_mul_right hc]
  simp_rw [integral_smul_measure, intervalIntegral, A.setIntegral_map,
    ENNReal.toReal_ofReal (abs_nonneg c)]
  rcases hc.lt_or_gt with h | h
  Â· simp [h, mul_div_cancel_rightâ‚€, hc, abs_of_neg,
      Measure.restrict_congr_set (Î± := â„) (Î¼ := volume) Ico_ae_eq_Ioc]
  Â· simp [h, mul_div_cancel_rightâ‚€, hc, abs_of_pos]

@[simp]
theorem smul_integral_comp_mul_right (c) :
    (c â€¢ âˆ« x in a..b, f (x * c)) = âˆ« x in a * c..b * c, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_mul_right]

@[simp]
theorem integral_comp_mul_left (hc : c â‰  0) :
    (âˆ« x in a..b, f (c * x)) = câ»Â¹ â€¢ âˆ« x in c * a..c * b, f x := by
  simpa only [mul_comm c] using integral_comp_mul_right f hc

@[simp]
theorem smul_integral_comp_mul_left (c) :
    (c â€¢ âˆ« x in a..b, f (c * x)) = âˆ« x in c * a..c * b, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_mul_left]

@[simp]
theorem integral_comp_div (hc : c â‰  0) :
    (âˆ« x in a..b, f (x / c)) = c â€¢ âˆ« x in a / c..b / c, f x := by
  simpa only [inv_inv] using integral_comp_mul_right f (inv_ne_zero hc)

@[simp]
theorem inv_smul_integral_comp_div (c) :
    (câ»Â¹ â€¢ âˆ« x in a..b, f (x / c)) = âˆ« x in a / c..b / c, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_div]

@[simp]
theorem integral_comp_add_right (d) : (âˆ« x in a..b, f (x + d)) = âˆ« x in a + d..b + d, f x :=
  have A : MeasurableEmbedding fun x => x + d :=
    (Homeomorph.addRight d).isClosedEmbedding.measurableEmbedding
  calc
    (âˆ« x in a..b, f (x + d)) = âˆ« x in a + d..b + d, f x âˆ‚Measure.map (fun x => x + d) volume := by
      simp [intervalIntegral, A.setIntegral_map]
    _ = âˆ« x in a + d..b + d, f x := by rw [map_add_right_eq_self]

@[simp]
nonrec theorem integral_comp_add_left (d) :
    (âˆ« x in a..b, f (d + x)) = âˆ« x in d + a..d + b, f x := by
  simpa only [add_comm d] using integral_comp_add_right f d

@[simp]
theorem integral_comp_mul_add (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (c * x + d)) = câ»Â¹ â€¢ âˆ« x in c * a + d..c * b + d, f x := by
  rw [â† integral_comp_add_right, â† integral_comp_mul_left _ hc]

@[simp]
theorem smul_integral_comp_mul_add (c d) :
    (c â€¢ âˆ« x in a..b, f (c * x + d)) = âˆ« x in c * a + d..c * b + d, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_mul_add]

@[simp]
theorem integral_comp_add_mul (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (d + c * x)) = câ»Â¹ â€¢ âˆ« x in d + c * a..d + c * b, f x := by
  rw [â† integral_comp_add_left, â† integral_comp_mul_left _ hc]

@[simp]
theorem smul_integral_comp_add_mul (c d) :
    (c â€¢ âˆ« x in a..b, f (d + c * x)) = âˆ« x in d + c * a..d + c * b, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_add_mul]

@[simp]
theorem integral_comp_div_add (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (x / c + d)) = c â€¢ âˆ« x in a / c + d..b / c + d, f x := by
  simpa only [div_eq_inv_mul, inv_inv] using integral_comp_mul_add f (inv_ne_zero hc) d

@[simp]
theorem inv_smul_integral_comp_div_add (c d) :
    (câ»Â¹ â€¢ âˆ« x in a..b, f (x / c + d)) = âˆ« x in a / c + d..b / c + d, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_div_add]

@[simp]
theorem integral_comp_add_div (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (d + x / c)) = c â€¢ âˆ« x in d + a / c..d + b / c, f x := by
  simpa only [div_eq_inv_mul, inv_inv] using integral_comp_add_mul f (inv_ne_zero hc) d

@[simp]
theorem inv_smul_integral_comp_add_div (c d) :
    (câ»Â¹ â€¢ âˆ« x in a..b, f (d + x / c)) = âˆ« x in d + a / c..d + b / c, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_add_div]

@[simp]
theorem integral_comp_mul_sub (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (c * x - d)) = câ»Â¹ â€¢ âˆ« x in c * a - d..c * b - d, f x := by
  simpa only [sub_eq_add_neg] using integral_comp_mul_add f hc (-d)

@[simp]
theorem smul_integral_comp_mul_sub (c d) :
    (c â€¢ âˆ« x in a..b, f (c * x - d)) = âˆ« x in c * a - d..c * b - d, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_mul_sub]

@[simp]
theorem integral_comp_sub_mul (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (d - c * x)) = câ»Â¹ â€¢ âˆ« x in d - c * b..d - c * a, f x := by
  simp only [sub_eq_add_neg, neg_mul_eq_neg_mul]
  rw [integral_comp_add_mul f (neg_ne_zero.mpr hc) d, integral_symm]
  simp only [inv_neg, smul_neg, neg_neg, neg_smul]

@[simp]
theorem smul_integral_comp_sub_mul (c d) :
    (c â€¢ âˆ« x in a..b, f (d - c * x)) = âˆ« x in d - c * b..d - c * a, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_sub_mul]

@[simp]
theorem integral_comp_div_sub (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (x / c - d)) = c â€¢ âˆ« x in a / c - d..b / c - d, f x := by
  simpa only [div_eq_inv_mul, inv_inv] using integral_comp_mul_sub f (inv_ne_zero hc) d

@[simp]
theorem inv_smul_integral_comp_div_sub (c d) :
    (câ»Â¹ â€¢ âˆ« x in a..b, f (x / c - d)) = âˆ« x in a / c - d..b / c - d, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_div_sub]

@[simp]
theorem integral_comp_sub_div (hc : c â‰  0) (d) :
    (âˆ« x in a..b, f (d - x / c)) = c â€¢ âˆ« x in d - b / c..d - a / c, f x := by
  simpa only [div_eq_inv_mul, inv_inv] using integral_comp_sub_mul f (inv_ne_zero hc) d

@[simp]
theorem inv_smul_integral_comp_sub_div (c d) :
    (câ»Â¹ â€¢ âˆ« x in a..b, f (d - x / c)) = âˆ« x in d - b / c..d - a / c, f x := by
  by_cases hc : c = 0 <;> simp [hc, integral_comp_sub_div]

@[simp]
theorem integral_comp_sub_right (d) : (âˆ« x in a..b, f (x - d)) = âˆ« x in a - d..b - d, f x := by
  simpa only [sub_eq_add_neg] using integral_comp_add_right f (-d)

@[simp]
theorem integral_comp_sub_left (d) : (âˆ« x in a..b, f (d - x)) = âˆ« x in d - b..d - a, f x := by
  simpa only [one_mul, one_smul, inv_one] using integral_comp_sub_mul f one_ne_zero d

@[simp]
theorem integral_comp_neg : (âˆ« x in a..b, f (-x)) = âˆ« x in -b..-a, f x := by
  simpa only [zero_sub] using integral_comp_sub_left f 0

end Comp

/-!
### Integral is an additive function of the interval

In this section we prove that `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼`
as well as a few other identities trivially equivalent to this one. We also prove that
`âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼` provided that `support f âŠ† Ioc a b`.

-/

section OrderClosedTopology

variable {a b c d : â„} {f g : â„ â†’ E} {Î¼ : Measure â„}

/-- If two functions are equal in the relevant interval, their interval integrals are also equal. -/
theorem integral_congr {a b : â„} (h : EqOn f g [[a, b]]) :
    âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ := by
  rcases le_total a b with hab | hab <;>
    simpa [hab, integral_of_le, integral_of_ge] using
      setIntegral_congr_fun measurableSet_Ioc (h.mono Ioc_subset_Icc_self)

theorem integral_add_adjacent_intervals_cancel (hab : IntervalIntegrable f Î¼ a b)
    (hbc : IntervalIntegrable f Î¼ b c) :
    (((âˆ« x in a..b, f x âˆ‚Î¼) + âˆ« x in b..c, f x âˆ‚Î¼) + âˆ« x in c..a, f x âˆ‚Î¼) = 0 := by
  have hac := hab.trans hbc
  simp only [intervalIntegral, sub_add_sub_comm, sub_eq_zero]
  iterate 4 rw [â† setIntegral_union]
  Â· suffices Ioc a b âˆª Ioc b c âˆª Ioc c a = Ioc b a âˆª Ioc c b âˆª Ioc a c by rw [this]
    rw [Ioc_union_Ioc_union_Ioc_cycle, union_right_comm, Ioc_union_Ioc_union_Ioc_cycle,
      min_left_comm, max_left_comm]
  all_goals
    simp [*, hab.1, hab.2, hbc.1, hbc.2, hac.1, hac.2]

theorem integral_add_adjacent_intervals (hab : IntervalIntegrable f Î¼ a b)
    (hbc : IntervalIntegrable f Î¼ b c) :
    ((âˆ« x in a..b, f x âˆ‚Î¼) + âˆ« x in b..c, f x âˆ‚Î¼) = âˆ« x in a..c, f x âˆ‚Î¼ := by
  rw [â† add_neg_eq_zero, â† integral_symm, integral_add_adjacent_intervals_cancel hab hbc]

theorem sum_integral_adjacent_intervals_Ico {a : â„• â†’ â„} {m n : â„•} (hmn : m â‰¤ n)
    (hint : âˆ€ k âˆˆ Ico m n, IntervalIntegrable f Î¼ (a k) (a <| k + 1)) :
    âˆ‘ k âˆˆ Finset.Ico m n, âˆ« x in a k..a <| k + 1, f x âˆ‚Î¼ = âˆ« x in a m..a n, f x âˆ‚Î¼ := by
  revert hint
  refine Nat.le_induction ?_ ?_ n hmn
  Â· simp
  Â· intro p hmp IH h
    rw [Finset.sum_Ico_succ_top hmp, IH, integral_add_adjacent_intervals]
    Â· refine IntervalIntegrable.trans_iterate_Ico hmp fun k hk => h k ?_
      exact (Ico_subset_Ico le_rfl (Nat.le_succ _)) hk
    Â· apply h
      simp [hmp]
    Â· intro k hk
      exact h _ (Ico_subset_Ico_right p.le_succ hk)

theorem sum_integral_adjacent_intervals {a : â„• â†’ â„} {n : â„•}
    (hint : âˆ€ k < n, IntervalIntegrable f Î¼ (a k) (a <| k + 1)) :
    âˆ‘ k âˆˆ Finset.range n, âˆ« x in a k..a <| k + 1, f x âˆ‚Î¼ = âˆ« x in (a 0)..(a n), f x âˆ‚Î¼ := by
  rw [â† Nat.Ico_zero_eq_range]
  exact sum_integral_adjacent_intervals_Ico (zero_le n) fun k hk => hint k hk.2

theorem integral_interval_sub_left (hab : IntervalIntegrable f Î¼ a b)
    (hac : IntervalIntegrable f Î¼ a c) :
    ((âˆ« x in a..b, f x âˆ‚Î¼) - âˆ« x in a..c, f x âˆ‚Î¼) = âˆ« x in c..b, f x âˆ‚Î¼ :=
  sub_eq_of_eq_add' <| Eq.symm <| integral_add_adjacent_intervals hac (hac.symm.trans hab)

theorem integral_interval_add_interval_comm (hab : IntervalIntegrable f Î¼ a b)
    (hcd : IntervalIntegrable f Î¼ c d) (hac : IntervalIntegrable f Î¼ a c) :
    ((âˆ« x in a..b, f x âˆ‚Î¼) + âˆ« x in c..d, f x âˆ‚Î¼) =
      (âˆ« x in a..d, f x âˆ‚Î¼) + âˆ« x in c..b, f x âˆ‚Î¼ := by
  rw [â† integral_add_adjacent_intervals hac hcd, add_assoc, add_left_comm,
    integral_add_adjacent_intervals hac (hac.symm.trans hab), add_comm]

theorem integral_interval_sub_interval_comm (hab : IntervalIntegrable f Î¼ a b)
    (hcd : IntervalIntegrable f Î¼ c d) (hac : IntervalIntegrable f Î¼ a c) :
    ((âˆ« x in a..b, f x âˆ‚Î¼) - âˆ« x in c..d, f x âˆ‚Î¼) =
      (âˆ« x in a..c, f x âˆ‚Î¼) - âˆ« x in b..d, f x âˆ‚Î¼ := by
  simp only [sub_eq_add_neg, â† integral_symm,
    integral_interval_add_interval_comm hab hcd.symm (hac.trans hcd)]

theorem integral_interval_sub_interval_comm' (hab : IntervalIntegrable f Î¼ a b)
    (hcd : IntervalIntegrable f Î¼ c d) (hac : IntervalIntegrable f Î¼ a c) :
    ((âˆ« x in a..b, f x âˆ‚Î¼) - âˆ« x in c..d, f x âˆ‚Î¼) =
      (âˆ« x in d..b, f x âˆ‚Î¼) - âˆ« x in c..a, f x âˆ‚Î¼ := by
  rw [integral_interval_sub_interval_comm hab hcd hac, integral_symm b d, integral_symm a c,
    sub_neg_eq_add, sub_eq_neg_add]

theorem integral_Iic_sub_Iic (ha : IntegrableOn f (Iic a) Î¼) (hb : IntegrableOn f (Iic b) Î¼) :
    ((âˆ« x in Iic b, f x âˆ‚Î¼) - âˆ« x in Iic a, f x âˆ‚Î¼) = âˆ« x in a..b, f x âˆ‚Î¼ := by
  wlog hab : a â‰¤ b generalizing a b
  Â· rw [integral_symm, â† this hb ha (le_of_not_ge hab), neg_sub]
  rw [sub_eq_iff_eq_add', integral_of_le hab, â† setIntegral_union (Iic_disjoint_Ioc le_rfl),
    Iic_union_Ioc_eq_Iic hab]
  exacts [measurableSet_Ioc, ha, hb.mono_set fun _ => And.right]

theorem integral_Iic_add_Ioi (h_left : IntegrableOn f (Iic b) Î¼)
    (h_right : IntegrableOn f (Ioi b) Î¼) :
    (âˆ« x in Iic b, f x âˆ‚Î¼) + (âˆ« x in Ioi b, f x âˆ‚Î¼) = âˆ« (x : â„), f x âˆ‚Î¼ := by
  convert (setIntegral_union (Iic_disjoint_Ioi <| Eq.le rfl) measurableSet_Ioi h_left h_right).symm
  rw [Iic_union_Ioi, Measure.restrict_univ]

theorem integral_Iio_add_Ici (h_left : IntegrableOn f (Iio b) Î¼)
    (h_right : IntegrableOn f (Ici b) Î¼) :
    (âˆ« x in Iio b, f x âˆ‚Î¼) + (âˆ« x in Ici b, f x âˆ‚Î¼) = âˆ« (x : â„), f x âˆ‚Î¼ := by
  convert (setIntegral_union (Iio_disjoint_Ici <| Eq.le rfl) measurableSet_Ici h_left h_right).symm
  rw [Iio_union_Ici, Measure.restrict_univ]

/-- If `Î¼` is a finite measure then `âˆ« x in a..b, c âˆ‚Î¼ = (Î¼ (Iic b) - Î¼ (Iic a)) â€¢ c`. -/
theorem integral_const_of_cdf [CompleteSpace E] [IsFiniteMeasure Î¼] (c : E) :
    âˆ« _ in a..b, c âˆ‚Î¼ = (Î¼.real (Iic b) - Î¼.real (Iic a)) â€¢ c := by
  simp only [sub_smul, â† setIntegral_const]
  refine (integral_Iic_sub_Iic ?_ ?_).symm <;> simp

theorem integral_eq_integral_of_support_subset {a b} (h : support f âŠ† Ioc a b) :
    âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  rcases le_total a b with hab | hab
  Â· rw [integral_of_le hab, â† integral_indicator measurableSet_Ioc, indicator_eq_self.2 h]
  Â· rw [Ioc_eq_empty hab.not_gt, subset_empty_iff, support_eq_empty_iff] at h
    simp [h]

theorem integral_congr_ae' (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ Ioc a b â†’ f x = g x)
    (h' : âˆ€áµ x âˆ‚Î¼, x âˆˆ Ioc b a â†’ f x = g x) : âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ := by
  simp only [intervalIntegral, setIntegral_congr_ae measurableSet_Ioc h,
    setIntegral_congr_ae measurableSet_Ioc h']

theorem integral_congr_ae (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ f x = g x) :
    âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ :=
  integral_congr_ae' (ae_uIoc_iff.mp h).1 (ae_uIoc_iff.mp h).2

/-- Integrals are equal for functions that agree almost everywhere for the restricted measure. -/
theorem integral_congr_ae_restrict {a b : â„} {f g : â„ â†’ E} {Î¼ : Measure â„}
    (h : f =áµ[Î¼.restrict (Î™ a b)] g) :
    âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ :=
  integral_congr_ae (ae_imp_of_ae_restrict h)

/-- Integrals are invariant when functions change along discrete sets. -/
theorem integral_congr_codiscreteWithin {a b : â„} {fâ‚ fâ‚‚ : â„ â†’ â„}
    (hf : fâ‚ =á¶ [codiscreteWithin (Î™ a b)] fâ‚‚) :
    âˆ« (x : â„) in a..b, fâ‚ x = âˆ« (x : â„) in a..b, fâ‚‚ x :=
  integral_congr_ae_restrict (ae_restrict_le_codiscreteWithin measurableSet_uIoc hf)

theorem integral_zero_ae (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ f x = 0) : âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
  calc
    âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« _ in a..b, 0 âˆ‚Î¼ := integral_congr_ae h
    _ = 0 := integral_zero

nonrec theorem integral_indicator {aâ‚ aâ‚‚ aâ‚ƒ : â„} (h : aâ‚‚ âˆˆ Icc aâ‚ aâ‚ƒ) :
    âˆ« x in aâ‚..aâ‚ƒ, indicator {x | x â‰¤ aâ‚‚} f x âˆ‚Î¼ = âˆ« x in aâ‚..aâ‚‚, f x âˆ‚Î¼ := by
  have : {x | x â‰¤ aâ‚‚} âˆ© Ioc aâ‚ aâ‚ƒ = Ioc aâ‚ aâ‚‚ := Iic_inter_Ioc_of_le h.2
  rw [integral_of_le h.1, integral_of_le (h.1.trans h.2), integral_indicator,
    Measure.restrict_restrict, this]
  Â· exact measurableSet_Iic
  all_goals apply measurableSet_Iic

end OrderClosedTopology

section

variable {f g : â„ â†’ â„} {a b : â„} {Î¼ : Measure â„}

theorem integral_eq_zero_iff_of_le_of_nonneg_ae (hab : a â‰¤ b) (hf : 0 â‰¤áµ[Î¼.restrict (Ioc a b)] f)
    (hfi : IntervalIntegrable f Î¼ a b) :
    âˆ« x in a..b, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼.restrict (Ioc a b)] 0 := by
  rw [integral_of_le hab, integral_eq_zero_iff_of_nonneg_ae hf hfi.1]

theorem integral_eq_zero_iff_of_nonneg_ae (hf : 0 â‰¤áµ[Î¼.restrict (Ioc a b âˆª Ioc b a)] f)
    (hfi : IntervalIntegrable f Î¼ a b) :
    âˆ« x in a..b, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼.restrict (Ioc a b âˆª Ioc b a)] 0 := by
  rcases le_total a b with hab | hab <;>
    simp only [Ioc_eq_empty hab.not_gt, empty_union, union_empty] at hf âŠ¢
  Â· exact integral_eq_zero_iff_of_le_of_nonneg_ae hab hf hfi
  Â· rw [integral_symm, neg_eq_zero, integral_eq_zero_iff_of_le_of_nonneg_ae hab hf hfi.symm]

/-- If `f` is nonnegative and integrable on the unordered interval `Set.uIoc a b`, then its
integral over `a..b` is positive if and only if `a < b` and the measure of
`Function.support f âˆ© Set.Ioc a b` is positive. -/
theorem integral_pos_iff_support_of_nonneg_ae' (hf : 0 â‰¤áµ[Î¼.restrict (Î™ a b)] f)
    (hfi : IntervalIntegrable f Î¼ a b) :
    (0 < âˆ« x in a..b, f x âˆ‚Î¼) â†” a < b âˆ§ 0 < Î¼ (support f âˆ© Ioc a b) := by
  rcases lt_or_ge a b with hab | hba
  Â· rw [uIoc_of_le hab.le] at hf
    simp only [hab, true_and, integral_of_le hab.le,
      setIntegral_pos_iff_support_of_nonneg_ae hf hfi.1]
  Â· suffices (âˆ« x in a..b, f x âˆ‚Î¼) â‰¤ 0 by simp only [this.not_gt, hba.not_gt, false_and]
    rw [integral_of_ge hba, neg_nonpos]
    rw [uIoc_comm, uIoc_of_le hba] at hf
    exact integral_nonneg_of_ae hf

/-- If `f` is nonnegative a.e.-everywhere and it is integrable on the unordered interval
`Set.uIoc a b`, then its integral over `a..b` is positive if and only if `a < b` and the
measure of `Function.support f âˆ© Set.Ioc a b` is positive. -/
theorem integral_pos_iff_support_of_nonneg_ae (hf : 0 â‰¤áµ[Î¼] f) (hfi : IntervalIntegrable f Î¼ a b) :
    (0 < âˆ« x in a..b, f x âˆ‚Î¼) â†” a < b âˆ§ 0 < Î¼ (support f âˆ© Ioc a b) :=
  integral_pos_iff_support_of_nonneg_ae' (ae_mono Measure.restrict_le_self hf) hfi

/-- If `f : â„ â†’ â„` is integrable on `(a, b]` for real numbers `a < b`, and positive on the interior
of the interval, then its integral over `a..b` is strictly positive. -/
theorem intervalIntegral_pos_of_pos_on {f : â„ â†’ â„} {a b : â„} (hfi : IntervalIntegrable f volume a b)
    (hpos : âˆ€ x : â„, x âˆˆ Ioo a b â†’ 0 < f x) (hab : a < b) : 0 < âˆ« x : â„ in a..b, f x := by
  have hsupp : Ioo a b âŠ† support f âˆ© Ioc a b := fun x hx =>
    âŸ¨mem_support.mpr (hpos x hx).ne', Ioo_subset_Ioc_self hxâŸ©
  have hâ‚€ : 0 â‰¤áµ[volume.restrict (uIoc a b)] f := by
    rw [EventuallyLE, uIoc_of_le hab.le]
    refine ae_restrict_of_ae_eq_of_ae_restrict Ioo_ae_eq_Ioc ?_
    rw [ae_restrict_iff' measurableSet_Ioo]
    filter_upwards with x hx using (hpos x hx).le
  rw [integral_pos_iff_support_of_nonneg_ae' hâ‚€ hfi]
  exact âŸ¨hab, ((Measure.measure_Ioo_pos _).mpr hab).trans_le (measure_mono hsupp)âŸ©

/-- If `f : â„ â†’ â„` is strictly positive everywhere, and integrable on `(a, b]` for real numbers
`a < b`, then its integral over `a..b` is strictly positive. (See `intervalIntegral_pos_of_pos_on`
for a version only assuming positivity of `f` on `(a, b)` rather than everywhere.) -/
theorem intervalIntegral_pos_of_pos {f : â„ â†’ â„} {a b : â„}
    (hfi : IntervalIntegrable f MeasureSpace.volume a b) (hpos : âˆ€ x, 0 < f x) (hab : a < b) :
    0 < âˆ« x in a..b, f x :=
  intervalIntegral_pos_of_pos_on hfi (fun x _ => hpos x) hab

/-- If `f` and `g` are two functions that are interval integrable on `a..b`, `a â‰¤ b`,
`f x â‰¤ g x` for a.e. `x âˆˆ Set.Ioc a b`, and `f x < g x` on a subset of `Set.Ioc a b`
of nonzero measure, then `âˆ« x in a..b, f x âˆ‚Î¼ < âˆ« x in a..b, g x âˆ‚Î¼`. -/
theorem integral_lt_integral_of_ae_le_of_measure_setOf_lt_ne_zero (hab : a â‰¤ b)
    (hfi : IntervalIntegrable f Î¼ a b) (hgi : IntervalIntegrable g Î¼ a b)
    (hle : f â‰¤áµ[Î¼.restrict (Ioc a b)] g) (hlt : Î¼.restrict (Ioc a b) {x | f x < g x} â‰  0) :
    (âˆ« x in a..b, f x âˆ‚Î¼) < âˆ« x in a..b, g x âˆ‚Î¼ := by
  rw [â† sub_pos, â† integral_sub hgi hfi, integral_of_le hab,
    MeasureTheory.integral_pos_iff_support_of_nonneg_ae]
  Â· refine pos_iff_ne_zero.2 (mt (measure_mono_null ?_) hlt)
    exact fun x hx => (sub_pos.2 hx.out).ne'
  exacts [hle.mono fun x => sub_nonneg.2, hgi.1.sub hfi.1]

/-- If `f` and `g` are continuous on `[a, b]`, `a < b`, `f x â‰¤ g x` on this interval, and
`f c < g c` at some point `c âˆˆ [a, b]`, then `âˆ« x in a..b, f x < âˆ« x in a..b, g x`. -/
theorem integral_lt_integral_of_continuousOn_of_le_of_exists_lt {f g : â„ â†’ â„} {a b : â„}
    (hab : a < b) (hfc : ContinuousOn f (Icc a b)) (hgc : ContinuousOn g (Icc a b))
    (hle : âˆ€ x âˆˆ Ioc a b, f x â‰¤ g x) (hlt : âˆƒ c âˆˆ Icc a b, f c < g c) :
    (âˆ« x in a..b, f x) < âˆ« x in a..b, g x := by
  apply integral_lt_integral_of_ae_le_of_measure_setOf_lt_ne_zero hab.le
    (hfc.intervalIntegrable_of_Icc hab.le) (hgc.intervalIntegrable_of_Icc hab.le)
  Â· simpa only [measurableSet_Ioc, ae_restrict_eq]
      using (ae_restrict_mem measurableSet_Ioc).mono hle
  contrapose! hlt
  have h_eq : f =áµ[volume.restrict (Ioc a b)] g := by
    simp only [â† not_le, â† ae_iff] at hlt
    exact EventuallyLE.antisymm ((ae_restrict_iff' measurableSet_Ioc).2 <|
      Eventually.of_forall hle) hlt
  rw [Measure.restrict_congr_set Ioc_ae_eq_Icc] at h_eq
  exact fun c hc â†¦ (Measure.eqOn_Icc_of_ae_eq volume hab.ne h_eq hfc hgc hc).ge

theorem integral_nonneg_of_ae_restrict (hab : a â‰¤ b) (hf : 0 â‰¤áµ[Î¼.restrict (Icc a b)] f) :
    0 â‰¤ âˆ« u in a..b, f u âˆ‚Î¼ := by
  let H := ae_restrict_of_ae_restrict_of_subset Ioc_subset_Icc_self hf
  simpa only [integral_of_le hab] using setIntegral_nonneg_of_ae_restrict H

theorem integral_nonneg_of_ae (hab : a â‰¤ b) (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ âˆ« u in a..b, f u âˆ‚Î¼ :=
  integral_nonneg_of_ae_restrict hab <| ae_restrict_of_ae hf

theorem integral_nonneg_of_forall (hab : a â‰¤ b) (hf : âˆ€ u, 0 â‰¤ f u) : 0 â‰¤ âˆ« u in a..b, f u âˆ‚Î¼ :=
  integral_nonneg_of_ae hab <| Eventually.of_forall hf

theorem integral_nonneg (hab : a â‰¤ b) (hf : âˆ€ u, u âˆˆ Icc a b â†’ 0 â‰¤ f u) : 0 â‰¤ âˆ« u in a..b, f u âˆ‚Î¼ :=
  integral_nonneg_of_ae_restrict hab <| (ae_restrict_iff' measurableSet_Icc).mpr <| ae_of_all Î¼ hf

theorem abs_integral_le_integral_abs (hab : a â‰¤ b) :
    |âˆ« x in a..b, f x âˆ‚Î¼| â‰¤ âˆ« x in a..b, |f x| âˆ‚Î¼ := by
  simpa only [â† Real.norm_eq_abs] using norm_integral_le_integral_norm hab

lemma integral_pos (hab : a < b)
    (hfc : ContinuousOn f (Icc a b)) (hle : âˆ€ x âˆˆ Ioc a b, 0 â‰¤ f x) (hlt : âˆƒ c âˆˆ Icc a b, 0 < f c) :
    0 < âˆ« x in a..b, f x :=
  (integral_lt_integral_of_continuousOn_of_le_of_exists_lt hab
    continuousOn_const hfc hle hlt).trans_eq' (by simp)

section Mono

theorem integral_mono_interval {c d} (hca : c â‰¤ a) (hab : a â‰¤ b) (hbd : b â‰¤ d)
    (hf : 0 â‰¤áµ[Î¼.restrict (Ioc c d)] f) (hfi : IntervalIntegrable f Î¼ c d) :
    (âˆ« x in a..b, f x âˆ‚Î¼) â‰¤ âˆ« x in c..d, f x âˆ‚Î¼ := by
  rw [integral_of_le hab, integral_of_le (hca.trans (hab.trans hbd))]
  exact setIntegral_mono_set hfi.1 hf (Ioc_subset_Ioc hca hbd).eventuallyLE

theorem abs_integral_mono_interval {c d} (h : Î™ a b âŠ† Î™ c d) (hf : 0 â‰¤áµ[Î¼.restrict (Î™ c d)] f)
    (hfi : IntervalIntegrable f Î¼ c d) : |âˆ« x in a..b, f x âˆ‚Î¼| â‰¤ |âˆ« x in c..d, f x âˆ‚Î¼| :=
  have hf' : 0 â‰¤áµ[Î¼.restrict (Î™ a b)] f := ae_mono (Measure.restrict_mono h le_rfl) hf
  calc
    |âˆ« x in a..b, f x âˆ‚Î¼| = |âˆ« x in Î™ a b, f x âˆ‚Î¼| := abs_integral_eq_abs_integral_uIoc f
    _ = âˆ« x in Î™ a b, f x âˆ‚Î¼ := abs_of_nonneg (MeasureTheory.integral_nonneg_of_ae hf')
    _ â‰¤ âˆ« x in Î™ c d, f x âˆ‚Î¼ := setIntegral_mono_set hfi.def' hf h.eventuallyLE
    _ â‰¤ |âˆ« x in Î™ c d, f x âˆ‚Î¼| := le_abs_self _
    _ = |âˆ« x in c..d, f x âˆ‚Î¼| := (abs_integral_eq_abs_integral_uIoc f).symm

variable (hab : a â‰¤ b) (hf : IntervalIntegrable f Î¼ a b) (hg : IntervalIntegrable g Î¼ a b)
include hab hf hg

theorem integral_mono_ae_restrict (h : f â‰¤áµ[Î¼.restrict (Icc a b)] g) :
    (âˆ« u in a..b, f u âˆ‚Î¼) â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ := by
  let H := h.filter_mono <| ae_mono <| Measure.restrict_mono Ioc_subset_Icc_self <| le_refl Î¼
  simpa only [integral_of_le hab] using setIntegral_mono_ae_restrict hf.1 hg.1 H

theorem integral_mono_ae (h : f â‰¤áµ[Î¼] g) : (âˆ« u in a..b, f u âˆ‚Î¼) â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ := by
  simpa only [integral_of_le hab] using setIntegral_mono_ae hf.1 hg.1 h

theorem integral_mono_on (h : âˆ€ x âˆˆ Icc a b, f x â‰¤ g x) :
    (âˆ« u in a..b, f u âˆ‚Î¼) â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ := by
  let H x hx := h x <| Ioc_subset_Icc_self hx
  simpa only [integral_of_le hab] using setIntegral_mono_on hf.1 hg.1 measurableSet_Ioc H

theorem integral_mono_on_of_le_Ioo [NoAtoms Î¼] (h : âˆ€ x âˆˆ Ioo a b, f x â‰¤ g x) :
    (âˆ« u in a..b, f u âˆ‚Î¼) â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ := by
  simp only [integral_of_le hab, integral_Ioc_eq_integral_Ioo]
  apply setIntegral_mono_on
  Â· apply hf.1.mono Ioo_subset_Ioc_self le_rfl
  Â· apply hg.1.mono Ioo_subset_Ioc_self le_rfl
  Â· exact measurableSet_Ioo
  Â· exact h

theorem integral_mono (h : f â‰¤ g) : (âˆ« u in a..b, f u âˆ‚Î¼) â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
  integral_mono_ae hab hf hg <| ae_of_all _ h

end Mono

end

section HasSum

variable {Î¼ : Measure â„} {f : â„ â†’ E}

theorem _root_.MeasureTheory.Integrable.hasSum_intervalIntegral (hfi : Integrable f Î¼) (y : â„) :
    HasSum (fun n : â„¤ => âˆ« x in y + n..y + n + 1, f x âˆ‚Î¼) (âˆ« x, f x âˆ‚Î¼) := by
  simp_rw [integral_of_le (le_add_of_nonneg_right zero_le_one)]
  rw [â† setIntegral_univ, â† iUnion_Ioc_add_intCast y]
  exact
    hasSum_integral_iUnion (fun i => measurableSet_Ioc) (pairwise_disjoint_Ioc_add_intCast y)
      hfi.integrableOn

theorem _root_.MeasureTheory.Integrable.hasSum_intervalIntegral_comp_add_int (hfi : Integrable f) :
    HasSum (fun n : â„¤ => âˆ« x in (0 : â„)..(1 : â„), f (x + n)) (âˆ« x, f x) := by
  simpa only [integral_comp_add_right, zero_add, add_comm (1 : â„)] using
    hfi.hasSum_intervalIntegral 0

end HasSum

end intervalIntegral
