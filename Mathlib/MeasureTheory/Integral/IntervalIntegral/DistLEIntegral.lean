/-
Copyright (c) 2025 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.Deriv.Basic
public import Mathlib.Analysis.Calculus.DiffContOnCl
public import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
public import Mathlib.Analysis.Calculus.LineDeriv.Basic

import Mathlib.Analysis.Calculus.MeanValue
import Mathlib.MeasureTheory.Integral.IntervalIntegral.FundThmCalculus
import Mathlib.Analysis.Normed.Module.Completion

/-!
# Displacement is at most the integral of the speed

In this file we prove several version of the following fact:
the displacement (`dist (f a) (f b)`) is at most the integral of `‚Äñderiv f‚Äñ` over `[a, b]`.
-/


public section

open Filter Set MeasureTheory Measure Metric
open scoped Topology

variable {E F : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedAddCommGroup F] [NormedSpace ‚Ñù F]

section Line

variable {f : ‚Ñù ‚Üí E} {a b : ‚Ñù}

/-- Displacement is at most the integral of an upper estimate on the speed.

Let `f : ‚Ñù ‚Üí E` be a function which is continuous on a closed interval `[a, b]`
and is differentiable on the open interval `(a, b)`.
If `B t` is an integrable upper estimate on `‚Äñf' t‚Äñ`, `a < t < b`,
then `‚Äñf b - f a‚Äñ ‚â§ ‚à´ t in a..b, B t`.
-/
lemma norm_sub_le_integral_of_norm_deriv_le_of_le {B : ‚Ñù ‚Üí ‚Ñù} (hab : a ‚â§ b)
    (hfc : ContinuousOn f (Icc a b)) (hfd : DifferentiableOn ‚Ñù f (Ioo a b))
    (hfB : ‚àÄ·µê t, t ‚àà Ioo a b ‚Üí ‚Äñderiv f t‚Äñ ‚â§ B t)
    (hBi : IntervalIntegrable B volume a b) : ‚Äñf b - f a‚Äñ ‚â§ ‚à´ t in a..b, B t := by
  -- WLOG, the codomain is a complete space.
  wlog hE : CompleteSpace E generalizing E
  ¬∑ set g : ‚Ñù ‚Üí UniformSpace.Completion E := (‚Üë) ‚àò f with hg
    have hgc : ContinuousOn g (Icc a b) :=
      (UniformSpace.Completion.continuous_coe E).comp_continuousOn hfc
    have hgd : DifferentiableOn ‚Ñù g (Ioo a b) :=
      UniformSpace.Completion.toComplL.differentiable.comp_differentiableOn hfd
    have hdg : ‚àÄ t ‚àà Ioo a b, deriv g t = deriv f t := by
      intro t ht
      have : HasFDerivAt (ùïú := ‚Ñù) (‚Üë) UniformSpace.Completion.toComplL (f t) := by
        rw [‚Üê UniformSpace.Completion.coe_toComplL (ùïú := ‚Ñù)]
        exact (UniformSpace.Completion.toComplL (E := E) (ùïú := ‚Ñù)).hasFDerivAt
      have hdft : HasDerivAt f (deriv f t) t := hfd.hasDerivAt <| Ioo_mem_nhds ht.1 ht.2
      rw [hg, (this.comp_hasDerivAt t hdft).deriv, UniformSpace.Completion.coe_toComplL]
    have hgn : ‚àÄ·µê t, t ‚àà Ioo a b ‚Üí ‚Äñderiv g t‚Äñ ‚â§ B t :=
      hfB.mono fun t htB ht ‚Ü¶ by
        simpa only [hdg t ht, UniformSpace.Completion.norm_coe] using htB ht
    simpa [g, ‚Üê dist_eq_norm_sub] using this hgc hgd hgn inferInstance
  -- In a complete space, we have
  -- `‚Äñf b - f a‚Äñ = ‚Äñ‚à´ t in a..b, deriv f t‚Äñ ‚â§ ‚à´ t in a..b, ‚Äñderiv f t‚Äñ`
  have hfB' : (‚Äñderiv f ¬∑‚Äñ) ‚â§·µê[volume.restrict (uIoc a b)] B := by
    rwa [uIoc_of_le hab, ‚Üê Measure.restrict_congr_set Ioo_ae_eq_Ioc, EventuallyLE,
        ae_restrict_iff' measurableSet_Ioo]
  rw [‚Üê intervalIntegral.integral_eq_sub_of_hasDeriv_right (f' := deriv f)]
  ¬∑ apply intervalIntegral.norm_integral_le_of_norm_le hab _ hBi
    rwa [‚Üê ae_restrict_iff' measurableSet_Ioc, ‚Üê uIoc_of_le hab]
  ¬∑ rwa [uIcc_of_le hab]
  ¬∑ rw [min_eq_left hab, max_eq_right hab]
    intro t ht
    exact hfd.hasDerivAt (isOpen_Ioo.mem_nhds ht) |>.hasDerivWithinAt
  ¬∑ apply hBi.mono_fun (aestronglyMeasurable_deriv _ _)
    exact hfB'.trans <| .of_forall fun _ ‚Ü¶ le_abs_self _

/-- Let `f : ‚Ñù ‚Üí E` be a function which is continuous on `[a, b]` and is differentiable on `(a, b)`.
Suppose that `‚Äñf' t‚Äñ ‚â§ C` for a.e. `t ‚àà (a, b)`.
Then the distance between `f a` and `f b`
is at most `C` times the measure of `x ‚àà (a, b)` such that `f' x ‚â† 0`.

This lemma is useful, if `f` is known to have zero derivative at most points of `[a, b]`. -/
lemma norm_sub_le_mul_volume_of_norm_deriv_le_of_le {C : ‚Ñù} (hab : a ‚â§ b)
    (hfc : ContinuousOn f (Icc a b)) (hfd : DifferentiableOn ‚Ñù f (Ioo a b))
    (hnorm : ‚àÄ·µê t, t ‚àà Ioo a b ‚Üí ‚Äñderiv f t‚Äñ ‚â§ C) :
    ‚Äñf b - f a‚Äñ ‚â§ C * volume.real {x ‚àà Ioo a b | deriv f x ‚â† 0} := by
  set s := toMeasurable volume {x | deriv f x ‚â† 0}
  have hsm : MeasurableSet s := by measurability
  calc
    ‚Äñf b - f a‚Äñ ‚â§ ‚à´ t in a..b, indicator s (fun _ ‚Ü¶ C) t := by
      apply norm_sub_le_integral_of_norm_deriv_le_of_le hab hfc hfd
      ¬∑ refine hnorm.mono fun t ht ht_mem ‚Ü¶ ?_
        apply le_indicator_apply
        ¬∑ exact fun ht' ‚Ü¶ ht ht_mem
        ¬∑ simp only [s, norm_le_zero_iff]
          exact not_imp_comm.2 fun h ‚Ü¶ subset_toMeasurable _ _ h
      ¬∑ rw [intervalIntegrable_iff_integrableOn_Ioo_of_le hab]
        refine (integrableOn_const ?_ ?_).indicator hsm <;> simp
    _ = C * volume.real {x ‚àà Ioo a b | deriv f x ‚â† 0} := by
      rw [intervalIntegral.integral_of_le hab, Measure.restrict_congr_set Ioo_ae_eq_Ioc.symm,
        integral_indicator hsm, Measure.restrict_restrict hsm,
        setIntegral_const, smul_eq_mul, mul_comm]
      simp only [s, Measure.real,
        Measure.measure_toMeasurable_inter_of_sFinite measurableSet_Ioo]
      simp only [inter_def, mem_setOf_eq, and_comm]

end Line

section NormedSpace

open AffineMap
variable {f : E ‚Üí F} {a b : E} {C r : ‚Ñù} {s : Set E}

/-- Consider a function `f : E ‚Üí F` continuous on a segment `[a, b]`
and line differentiable in the direction `b - a` at all points of the open segment `(a, b)`.

If `‚Äñ‚àÇ_{b - a} f‚Äñ ‚â§ C` at a.e. all points of the open segment,
then `‚Äñf b - f a‚Äñ ‚â§ C * volume s`, where `s` is the set of points `t ‚àà Ioo 0 1`
such that `f` has nonzero line derivative in the direction `b - a` at `lineMap a b t`. -/
lemma norm_sub_le_mul_volume_of_norm_lineDeriv_le
    (hfc : ContinuousOn f (segment ‚Ñù a b))
    (hfd : ‚àÄ t ‚àà Ioo (0 : ‚Ñù) 1, LineDifferentiableAt ‚Ñù f (lineMap a b t) (b - a))
    (hf' : ‚àÄ·µê t : ‚Ñù, t ‚àà Ioo (0 : ‚Ñù) 1 ‚Üí ‚ÄñlineDeriv ‚Ñù f (lineMap a b t) (b - a)‚Äñ ‚â§ C) :
    ‚Äñf b - f a‚Äñ ‚â§
      C * volume.real {t ‚àà Ioo (0 : ‚Ñù) 1 | lineDeriv ‚Ñù f (lineMap a b t) (b - a) ‚â† 0} := by
  set g : ‚Ñù ‚Üí F := fun t ‚Ü¶ f (lineMap a b t)
  have hgc : ContinuousOn g (Icc 0 1) := by
    refine hfc.comp ?_ ?_
    ¬∑ exact AffineMap.lineMap_continuous.continuousOn
    ¬∑ simp [segment_eq_image_lineMap, mapsTo_image]
  have hdg (t : ‚Ñù) (ht : t ‚àà Ioo 0 1) : HasDerivAt g (lineDeriv ‚Ñù f (lineMap a b t) (b - a)) t := by
    have := (hfd t ht).hasLineDerivAt.scomp_of_eq (ùïú := ‚Ñù) t ((hasDerivAt_id t).sub_const t)
    simpa [g, lineMap_apply_module', Function.comp_def, sub_smul, add_comm _ a] using this
  suffices ‚Äñg 1 - g 0‚Äñ ‚â§ C * volume.real {t ‚àà Ioo 0 1 | deriv g t ‚â† 0} by
    convert this using 1
    ¬∑ simp [g]
    ¬∑ congr 2 with t
      simp +contextual [(hdg _ _).deriv]
  apply norm_sub_le_mul_volume_of_norm_deriv_le_of_le zero_le_one hgc
  ¬∑ exact fun t ht ‚Ü¶ (hdg t ht).differentiableAt.differentiableWithinAt
  ¬∑ exact hf'.mono fun t ht ht_mem ‚Ü¶ by simpa only [(hdg t ht_mem).deriv] using ht ht_mem

/-- Let `f : E ‚Üí F` be a function differentiable on a set `s` and continuous on its closure.
Let `a`, `b` be two points such that the open segment connecting `a` to `b` is a subset of `s`.

If `‚ÄñDf‚Äñ ‚â§ C` everywhere on `s` then `‚Äñf b - f a‚Äñ ‚â§ C * volume u`,
where `u` is the set of points `t ‚àà Ioo 0 1`
such that `f` has nonzero derivative at `lineMap a b t`. -/
lemma norm_sub_le_mul_volume_of_norm_fderiv_le (hs : IsOpen s) (hf : DiffContOnCl ‚Ñù f s)
    (hab : openSegment ‚Ñù a b ‚äÜ s) (hC : ‚àÄ x ‚àà s, ‚Äñfderiv ‚Ñù f x‚Äñ ‚â§ C) :
    ‚Äñf b - f a‚Äñ ‚â§
      C * ‚Äñb - a‚Äñ * volume.real {t ‚àà Ioo (0 : ‚Ñù) 1 | fderiv ‚Ñù f (lineMap a b t) ‚â† 0} := by
  have hmem_s : ‚àÄ t ‚àà Ioo (0 : ‚Ñù) 1, lineMap a b t ‚àà s := fun t ht ‚Ü¶
    hab <| lineMap_mem_openSegment _ a b ht
  have hC‚ÇÄ : 0 ‚â§ C := (norm_nonneg _).trans <| hC _ <| hmem_s (1 / 2) (by norm_num)
  have hfc : ContinuousOn f (segment ‚Ñù a b) :=
    hf.continuousOn.mono <| segment_subset_closure_openSegment.trans <| closure_mono hab
  have hfd : ‚àÄ t ‚àà Ioo (0 : ‚Ñù) 1, LineDifferentiableAt ‚Ñù f (lineMap a b t) (b - a) := fun t ht ‚Ü¶
    (hf.differentiableAt hs <| hmem_s t ht).lineDifferentiableAt
  have hfC : ‚àÄ t ‚àà Ioo (0 : ‚Ñù) 1, ‚ÄñlineDeriv ‚Ñù f (lineMap a b t) (b - a)‚Äñ ‚â§ C * ‚Äñb - a‚Äñ := by
    intro t ht
    rw [DifferentiableAt.lineDeriv_eq_fderiv]
    ¬∑ exact ContinuousLinearMap.le_of_opNorm_le _ (hC _ <| hmem_s t ht) _
    ¬∑ exact hf.differentiableAt hs <| hmem_s t ht
  refine norm_sub_le_mul_volume_of_norm_lineDeriv_le hfc hfd (.of_forall hfC) |>.trans ?_
  gcongr
  ¬∑ refine ne_top_of_le_ne_top ?_ (measure_mono inter_subset_left)
    simp
  ¬∑ simp +contextual [(hf.differentiableAt hs <| hmem_s _ ‚Äπ_‚Ä∫).lineDeriv_eq_fderiv]

/-- Let `f : E ‚Üí F` be a function differentiable in a neighborhood of `a`.
If $Df(x) = O(‚Äñx - a‚Äñ ^ r)$ as `x ‚Üí a`, where `r ‚â• 0`,
then $f(x) - f(a) = O(‚Äñx - a‚Äñ ^ {r + 1})$ as `x ‚Üí a`. -/
theorem sub_isBigO_norm_rpow_add_one_of_fderiv (hr : 0 ‚â§ r)
    (hdf : ‚àÄ·∂† x in ùìù a, DifferentiableAt ‚Ñù f x) (hderiv : fderiv ‚Ñù f =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ r)) :
    (f ¬∑ - f a) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (r + 1)) := by
  rcases hderiv.exists_pos with ‚ü®C, hC‚ÇÄ, hC‚ü©
  rw [Asymptotics.IsBigOWith_def] at hC
  rcases eventually_nhds_iff_ball.mp (hdf.and hC) with ‚ü®Œµ, hŒµ‚ÇÄ, hŒµ‚ü©
  refine .of_bound C ?_
  rw [eventually_nhds_iff_ball]
  refine ‚ü®Œµ, hŒµ‚ÇÄ, fun y hy ‚Ü¶ ?_‚ü©
  rw [Real.norm_of_nonneg (by positivity), Real.rpow_add_one' (by positivity) (by positivity),
    ‚Üê mul_assoc]
  have hsub : closedBall a ‚Äñy - a‚Äñ ‚äÜ ball a Œµ :=
    closedBall_subset_ball (mem_ball_iff_norm.mp hy)
  apply (convex_closedBall a ‚Äñy - a‚Äñ).norm_image_sub_le_of_norm_fderiv_le (ùïú := ‚Ñù)
  ¬∑ exact fun z hz ‚Ü¶ (hŒµ z <| hsub hz).1
  ¬∑ intro z hz
    grw [(hŒµ z <| hsub hz).2, Real.norm_of_nonneg (by positivity), mem_closedBall_iff_norm.mp hz]
  ¬∑ simp
  ¬∑ simp [dist_eq_norm_sub]

/-- Let `f : E ‚Üí F` be a function differentiable in a neighborhood of `a`.
If $Df(x) = O(‚Äñx - a‚Äñ ^ r)$ as `x ‚Üí a`, where `r ‚â• 0`, and `f a = 0`,
then $f(x) = O(‚Äñx - a‚Äñ ^ {r + 1})$ as `x ‚Üí a`. -/
theorem isBigO_norm_rpow_add_one_of_fderiv_of_apply_eq_zero (hr : 0 ‚â§ r)
    (hdf : ‚àÄ·∂† x in ùìù a, DifferentiableAt ‚Ñù f x) (hderiv : fderiv ‚Ñù f =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ r))
    (hf‚ÇÄ : f a = 0) : f =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (r + 1)) := by
  simpa [hf‚ÇÄ] using sub_isBigO_norm_rpow_add_one_of_fderiv hr hdf hderiv

end NormedSpace
