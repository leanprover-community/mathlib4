/-
Copyright (c) 2025 Yizheng Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yizheng Zhu
-/
import Mathlib.Analysis.BoundedVariation
import Mathlib.MeasureTheory.Function.AbsolutelyContinuous
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# `f'` is interval integrable for certain classes of functions `f`

This file proves that:
* `MonotoneOn.intervalIntegrable_deriv` - If `f` is monotone on `a..b`, then `f'` is interval
integrable on `a..b`.
* `MonotoneOn.intervalIntegral_bound` - If `f` is monotone on `a..b`, then `‚à´ x in a..b, f' x` is
in `uIcc 0 (f b - f a)`.
* `BoundedVariationOn.intervalIntegrable_deriv` - If `f` has bounded variation on `a..b`,
then `f'` is interval integrable on `a..b`.
* `AbsolutelyContinuousOnInterval.intervalIntegrable_deriv` - If `f` is absolutely continuous on
`a..b`, then `f'` is interval integrable on `a..b`.

## Tags
interval integrable, monotone, bounded variation, absolutely continuous
-/

open MeasureTheory Set Filter Function

open scoped Topology ENNReal Interval NNReal

theorem IntervalIntegrable.intervalIntegrable_slope {f : ‚Ñù ‚Üí ‚Ñù} {a b c : ‚Ñù}
    (hf : IntervalIntegrable f volume a (b + c)) (hab : a ‚â§ b) (hc : 0 ‚â§ c) :
    IntervalIntegrable (fun x ‚Ü¶ slope f x (x + c)) volume a b := by
  simp only [slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul]
  exact hf.comp_add_right c |>.mono_set (by grind [uIcc]) |>.sub (hf.mono_set (by grind [uIcc]))
    |>.const_mul (c := c‚Åª¬π)

theorem MonotoneOn.intervalIntegrable_slope {f : ‚Ñù ‚Üí ‚Ñù} {a b c : ‚Ñù}
    (hf : MonotoneOn f (Icc a (b + c))) (hab : a ‚â§ b) (hc : 0 ‚â§ c) :
    IntervalIntegrable (fun x ‚Ü¶ slope f x (x + c)) volume a b :=
  uIcc_of_le (show a ‚â§ b + c by linarith) ‚ñ∏ hf |>.intervalIntegrable.intervalIntegrable_slope hab hc

theorem MonotoneOn.intervalIntegral_slope_bound {f : ‚Ñù ‚Üí ‚Ñù} {a b c : ‚Ñù}
    (hf : MonotoneOn f (Icc a (b + c))) (hab : a ‚â§ b) (hc : 0 ‚â§ c) :
    ‚à´ x in a..b, slope f x (x + c) ‚â§ f (b + c) - f a := by
  rcases eq_or_lt_of_le hc with hc | hc
  ¬∑ simp only [‚Üê hc, add_zero, slope_same, intervalIntegral.integral_zero, sub_nonneg]
    apply hf <;> grind
  rw [‚Üê uIcc_of_le (by linarith)] at hf
  have hf' := hf.intervalIntegrable (Œº := volume)
  simp only [slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul,
    intervalIntegral.integral_const_mul]
  rw [intervalIntegral.integral_sub
        (hf'.comp_add_right c |>.mono_set (by grind [uIcc]))
        (hf'.mono_set (by grind [uIcc])),
      intervalIntegral.integral_comp_add_right,
      intervalIntegral.integral_interval_sub_interval_comm'
        (hf'.mono_set (by grind [uIcc]))
        (hf'.mono_set (by grind [uIcc]))
        (hf'.mono_set (by grind [uIcc]))]
  have fU : ‚à´ (x : ‚Ñù) in b..b + c, f x ‚â§ c * f (b + c) := by
    grw [intervalIntegral.integral_mono_on (g := fun _ ‚Ü¶ f (b + c))
          (by linarith)
          (hf'.mono_set (by grind [uIcc]))
          (by simp)
          (by intros; apply hf <;> grind [uIcc])]
    simp
  have fL : c * f a ‚â§ ‚à´ (x : ‚Ñù) in a..a + c, f x := by
    grw [‚Üê intervalIntegral.integral_mono_on (f := fun _ ‚Ü¶ f a)
            (by linarith)
            (by simp)
            (hf'.mono_set (by grind [uIcc]))
            (by intros; apply hf <;> grind [uIcc])]
    simp
  grw [fU, ‚Üê fL]
  field_simp; rfl

theorem lintegral_bound_of_tendsto_atTop_aemeasurable_enorm
    {G : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {Œº : Measure ‚Ñù}
    (hGf : ‚àÄ·µê x ‚àÇŒº, Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ G n x) Filter.atTop (ùìù (f x)))
    (hG : ‚àÄ (n : ‚Ñï), AEMeasurable (fun x ‚Ü¶ ‚ÄñG n x‚Äñ‚Çë) Œº) :
    ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çë ‚àÇŒº ‚â§ liminf (fun n ‚Ü¶ ‚à´‚Åª x, ‚ÄñG n x‚Äñ‚Çë ‚àÇŒº) atTop :=
  lintegral_congr_ae (by filter_upwards [hGf] with x hx using hx.enorm.liminf_eq) ‚ñ∏
    (MeasureTheory.lintegral_liminf_le' hG)

theorem lintegral_bound_of_tendsto_atTop_aestronglyMeasurable
    {G : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {Œº : Measure ‚Ñù}
    (hGf : ‚àÄ·µê x ‚àÇŒº, Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ G n x) Filter.atTop (ùìù (f x)))
    (hG : ‚àÄ (n : ‚Ñï), AEStronglyMeasurable (G n) Œº) :
    ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çë ‚àÇŒº ‚â§ liminf (fun n ‚Ü¶ ‚à´‚Åª x, ‚ÄñG n x‚Äñ‚Çë ‚àÇŒº) atTop :=
  lintegral_bound_of_tendsto_atTop_aemeasurable_enorm hGf
    (fun n ‚Ü¶ (hG n).aemeasurable.enorm)

theorem lintegral_interval_bound_of_tendsto_atTop_aestronglyMeasurable
    {G : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hGf : ‚àÄ·µê x, x ‚àà uIcc a b ‚Üí Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ G n x) Filter.atTop (ùìù (f x)))
    (hG : ‚àÄ (n : ‚Ñï), AEStronglyMeasurable (G n) (volume.restrict (uIcc a b))) :
    ‚à´‚Åª x in uIcc a b, ‚Äñf x‚Äñ‚Çë ‚â§ liminf (fun n ‚Ü¶ ‚à´‚Åª x in uIcc a b, ‚ÄñG n x‚Äñ‚Çë) atTop :=
  lintegral_bound_of_tendsto_atTop_aestronglyMeasurable
    ((MeasureTheory.ae_restrict_iff' (by measurability) |>.mpr hGf)) hG

theorem integrable_of_tendsto_atTop_aestronglyMeasurable_liminf_ne_top
    {G : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {Œº : Measure ‚Ñù}
    (hGf : ‚àÄ·µê x ‚àÇŒº, Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ G n x) Filter.atTop (ùìù (f x)))
    (hG : ‚àÄ (n : ‚Ñï), AEStronglyMeasurable (G n) Œº)
    (hG' : liminf (fun n ‚Ü¶ ‚à´‚Åª x, ‚ÄñG n x‚Äñ‚Çë ‚àÇŒº) atTop ‚â† ‚ä§) :
    Integrable f Œº :=
  ‚ü®aestronglyMeasurable_of_tendsto_ae _ hG hGf,
   lt_of_le_of_lt (lintegral_bound_of_tendsto_atTop_aestronglyMeasurable hGf hG)
    hG'.lt_top‚ü©

theorem intervalIntegrable_of_tendsto_atTop_aestronglyMeasurable_liminf_ne_top
    {G : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hGf : ‚àÄ·µê x, x ‚àà uIcc a b ‚Üí Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ G n x) Filter.atTop (ùìù (f x)))
    (hG : ‚àÄ (n : ‚Ñï), AEStronglyMeasurable (G n) (volume.restrict (uIcc a b)))
    (hG' : liminf (fun n ‚Ü¶ ‚à´‚Åª x in uIcc a b, ‚ÄñG n x‚Äñ‚Çë) atTop ‚â† ‚ä§) :
    IntervalIntegrable f volume a b := by
  rw [intervalIntegrable_iff']
  exact integrable_of_tendsto_atTop_aestronglyMeasurable_liminf_ne_top
    (MeasureTheory.ae_restrict_iff' (by measurability) |>.mpr hGf) hG hG'

theorem MonotoneOn.intervalIntegrable_deriv_intervalIntegral_bound {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : MonotoneOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b ‚àß ‚à´ x in a..b, deriv f x ‚àà uIcc 0 (f b - f a) := by
  wlog hab : a ‚â§ b generalizing a b with h
  ¬∑ specialize h (uIcc_comm a b ‚ñ∏ hf) (by linarith)
    refine ‚ü®h.left.symm, ?_‚ü©
    ¬∑ have : f b ‚â§ f a := hf (by simp) (by simp) (by linarith)
      rw [intervalIntegral.integral_symm, uIcc_of_ge (by linarith)]
      refine neg_mem_Icc_iff.mpr ?_
      simp only [neg_zero, neg_sub]
      rw [uIcc_of_le (by linarith)] at h
      exact h.right
  rw [uIcc_of_le hab] at hf
  let g (x : ‚Ñù) : ‚Ñù := f (max a (min x b))
  have hg : Monotone g := monotoneOn_univ.mp <| hf.comp (by grind [MonotoneOn]) (by grind [MapsTo])
  have hfg : EqOn f g (Ioo a b) := by grind [EqOn]
  replace hfg := hfg.deriv isOpen_Ioo
  have h‚ÇÅ : ‚àÄ·µê x, x ‚â† a := by simp [ae_iff, measure_singleton]
  have h‚ÇÇ : ‚àÄ·µê x, x ‚â† b := by simp [ae_iff, measure_singleton]
  let G (c x : ‚Ñù) := slope g x (x + c)
  have hGf : ‚àÄ·µê x ‚àÇvolume.restrict (Icc a b),
      Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ G (n : ‚Ñù)‚Åª¬π x) Filter.atTop (ùìù (deriv f x)) := by
    rw [MeasureTheory.ae_restrict_iff' (by measurability)]
    filter_upwards [hg.ae_differentiableAt, h‚ÇÅ, h‚ÇÇ] with x hx‚ÇÅ hx‚ÇÇ hx‚ÇÉ hx‚ÇÑ
    rw [hfg (by grind)]
    exact hx‚ÇÅ.hasDerivAt.tendsto_slope.comp <|
      tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      (by convert tendsto_const_nhds.add tendsto_inverse_atTop_nhds_zero_nat; simp)
      (by simp [eventually_ne_atTop 0])
  have G_integrable (n : ‚Ñï) : Integrable (G (‚Üën)‚Åª¬π) (volume.restrict (Icc a b)) := by
    have := hg.monotoneOn (Icc a (b + (n : ‚Ñù)‚Åª¬π)) |>.intervalIntegrable_slope hab (by simp)
    exact intervalIntegrable_iff_integrableOn_Icc_of_le hab |>.mp this
  have hG (n : ‚Ñï) : AEStronglyMeasurable (G (n : ‚Ñù)‚Åª¬π) (volume.restrict (Icc a b)) :=
    G_integrable n |>.aestronglyMeasurable
  have hG' : liminf (fun (n : ‚Ñï) ‚Ü¶ ‚à´‚Åª (x : ‚Ñù) in Icc a b, ‚ÄñG (n : ‚Ñù)‚Åª¬π x‚Äñ‚Çë) atTop ‚â§
      ENNReal.ofReal (f b - f a) := by
    calc
      _ = liminf (fun (n : ‚Ñï) ‚Ü¶ ENNReal.ofReal (‚à´ (x : ‚Ñù) in Icc a b, (G (n : ‚Ñù)‚Åª¬π) x)) atTop := by
        apply Filter.liminf_congr
        filter_upwards with n
        rw [‚Üê MeasureTheory.ofReal_integral_norm_eq_lintegral_enorm (G_integrable n)]
        congr with y
        exact abs_eq_self.mpr (hg.monotoneOn univ |>.slope_nonneg trivial trivial)
      _ ‚â§ ENNReal.ofReal (g b - g a) := by
        refine Filter.liminf_le_of_frequently_le'
          (Filter.Frequently.of_forall fun n ‚Ü¶ ENNReal.ofReal_le_ofReal ?_)
        rw [integral_Icc_eq_integral_Ioc, ‚Üê intervalIntegral.integral_of_le hab]
        convert hg.monotoneOn (Icc a (b + (n : ‚Ñù)‚Åª¬π)) |>.intervalIntegral_slope_bound hab (by simp)
          using 2
        simp [g]
      _ = ENNReal.ofReal (f b - f a) := by grind
  have hG'‚ÇÄ : liminf (fun (n : ‚Ñï) ‚Ü¶ ‚à´‚Åª (x : ‚Ñù) in Icc a b, ‚ÄñG (n : ‚Ñù)‚Åª¬π x‚Äñ‚Çë) atTop ‚â† ‚ä§ :=
    lt_of_le_of_lt hG' ENNReal.ofReal_lt_top |>.ne_top
  rw [intervalIntegrable_iff_integrableOn_Icc_of_le hab]
  have integrable_f_deriv := integrable_of_tendsto_atTop_aestronglyMeasurable_liminf_ne_top
    hGf hG hG'‚ÇÄ
  refine ‚ü®integrable_f_deriv, ?_‚ü©
  rw [MeasureTheory.ae_restrict_iff' (by simp)] at hGf
  rw [‚Üê uIcc_of_le hab] at hGf hG hG'
  have ebound := lintegral_interval_bound_of_tendsto_atTop_aestronglyMeasurable hGf hG
  grw [hG'] at ebound
  have : f a ‚â§ f b := hf (by simp [hab]) (by simp [hab]) hab
  rw [uIcc_of_le (by linarith), mem_Icc]
  constructor
  ¬∑ apply intervalIntegral.integral_nonneg_of_ae_restrict hab
    rw [Filter.EventuallyLE, MeasureTheory.ae_restrict_iff' (by simp)]
    filter_upwards [h‚ÇÅ, h‚ÇÇ] with x _ _ _
    rw [hfg (by grind)]
    exact hg.deriv_nonneg
  ¬∑ rw [uIcc_of_le hab,
        ‚Üê MeasureTheory.ofReal_integral_norm_eq_lintegral_enorm integrable_f_deriv,
        ENNReal.ofReal_le_ofReal_iff (by linarith),
        integral_Icc_eq_integral_Ioc,
        ‚Üê intervalIntegral.integral_of_le hab] at ebound
    convert ebound using 1
    refine intervalIntegral.integral_congr_ae ?_
    filter_upwards [h‚ÇÇ] with x _ _
    rw [hfg (by grind [uIoc])]
    exact abs_eq_self.mpr hg.deriv_nonneg |>.symm

theorem MonotoneOn.intervalIntegrable_deriv {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : MonotoneOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b :=
  hf.intervalIntegrable_deriv_intervalIntegral_bound.left

theorem MonotoneOn.intervalIntegral_bound {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : MonotoneOn f (uIcc a b)) :
    ‚à´ x in a..b, deriv f x ‚àà uIcc 0 (f b - f a) :=
  hf.intervalIntegrable_deriv_intervalIntegral_bound.right

/-- If `f` has bounded variation on `uIcc a b`, then `f'` is interval integrable on `a..b`. -/
theorem BoundedVariationOn.intervalIntegrable_deriv {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : BoundedVariationOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b := by
  obtain ‚ü®p, q, hp, hq, rfl‚ü© := hf.locallyBoundedVariationOn.exists_monotoneOn_sub_monotoneOn
  have h‚ÇÇ : ‚àÄ·µê x, x ‚â† max a b := by simp [ae_iff, measure_singleton]
  apply (hp.intervalIntegrable_deriv.sub hq.intervalIntegrable_deriv).congr
  rw [Filter.EventuallyEq, MeasureTheory.ae_restrict_iff' (by simp [uIoc])]
  filter_upwards [hp.ae_differentiableWithinAt_of_mem, hq.ae_differentiableWithinAt_of_mem, h‚ÇÇ]
    with x hx‚ÇÅ hx‚ÇÇ hx‚ÇÉ hx‚ÇÑ
  have hx‚ÇÖ : x ‚àà uIcc a b := Ioc_subset_Icc_self hx‚ÇÑ
  rw [uIoc, mem_Ioc] at hx‚ÇÑ
  replace hx‚ÇÅ := (hx‚ÇÅ hx‚ÇÖ).differentiableAt (Icc_mem_nhds (by grind) (by grind)) |>.hasDerivAt
  replace hx‚ÇÇ := (hx‚ÇÇ hx‚ÇÖ).differentiableAt (Icc_mem_nhds (by grind) (by grind)) |>.hasDerivAt
  exact (hx‚ÇÅ.sub hx‚ÇÇ).deriv.symm

/-- If `f` is absolute continuous on `uIcc a b`, then `f'` is interval integrable on `a..b`. -/
theorem AbsolutelyContinuousOnInterval.intervalIntegrable_deriv {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    IntervalIntegrable (deriv f) volume a b :=
  hf.boundedVariationOn.intervalIntegrable_deriv
