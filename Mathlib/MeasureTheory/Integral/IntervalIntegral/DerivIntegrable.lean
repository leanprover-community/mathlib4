/-
Copyright (c) 2025 Yizheng Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yizheng Zhu
-/
import Mathlib.Analysis.BoundedVariation
import Mathlib.MeasureTheory.Function.AbsolutelyContinuous
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# `f'` is interval integrable for certain classes of functions `f`

This file proves that:
* If `f` is monotone on `a..b`, then `f'` is interval integrable on `a..b`.
* If `f` has bounded variation on `a..b`, then `f'` is interval integrable on `a..b`.
* If `f` is absolutely continuous on `a..b`, then `f'` exists a.e. on `a..b` and is interval
integrable on `a..b`.

## Tags
interval integrable, monotone, bounded variation, absolutely continuous
-/

open MeasureTheory Set Filter Function

open scoped Topology ENNReal Interval NNReal

/-- If `f` differentiable at `x âˆˆ uIoo a b` within `uIcc a b`, then `f'` exists at `x`. -/
theorem DifferentiableWithinAt.hasDerivAt_interval {f : â„ â†’ â„} {a b x : â„}
    (hf : DifferentiableWithinAt â„ f (uIcc a b) x) (hx : x âˆˆ uIoo a b) :
    HasDerivAt f (deriv f x) x := by
  rw [uIoo, mem_Ioo] at hx
  have : uIoo a b âˆˆ ğ“ x := Ioo_mem_nhds hx.left hx.right
  have hxâ‚ := hf.hasDerivWithinAt.hasDerivAt (mem_of_superset this Ioo_subset_Icc_self)
  rwa [hxâ‚.deriv]

/-- If `f` is monotone on `uIcc a b`, then `f'` is interval integrable on `a..b`. -/
theorem MonotoneOn.deriv_intervalIntegrable {f : â„ â†’ â„} {a b : â„} (hf : MonotoneOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b := by
  wlog hab : a â‰¤ b generalizing a b
  Â· exact @this b a (uIcc_comm a b â–¸ hf) (by linarith) |>.symm
  rw [uIcc_of_le hab] at hf
  let g (x : â„) : â„ := if x <= a then f a else if x < b then f x else f b
  have hg : Monotone g := by
    intro x y hxy
    dsimp only [g]
    split_ifs <;> try linarith
    all_goals apply hf
    all_goals grind
  have hgc (c : â„) : Monotone (fun x â†¦ g (x + c)) := Monotone.covariant_of_const' hg c
  have hâ‚ : âˆ€áµ x, x â‰  a := by simp [ae_iff, measure_singleton]
  have hâ‚‚ : âˆ€áµ x, x â‰  b := by simp [ae_iff, measure_singleton]
  have hgâ‚‚ : âˆ€áµ (x : â„), HasDerivAt g (deriv g x) x âˆ§ 0 â‰¤ deriv g x := by
    filter_upwards [hg.ae_differentiableAt] with x hxâ‚
    exact âŸ¨hxâ‚.hasDerivAt, hg.deriv_nonnegâŸ©
  have hfg : âˆ€ x âˆˆ Ioo a b, deriv f x = deriv g x := by
    intro x hx
    rw [mem_Ioo] at hx
    apply Filter.EventuallyEq.deriv_eq
    filter_upwards [Ioo_mem_nhds hx.left hx.right] with y hy
    simp [g, mem_Ioo.mp hy]
  have hgâ‚ƒ (a0 b0 : â„) := hg.intervalIntegrable (Î¼ := volume) (a := a0) (b := b0)
  have hgâ‚„ (z a0 b0 : â„) : IntervalIntegrable (fun x â†¦ g (x + z)) volume a0 b0 := by
      convert hgâ‚ƒ (a0 + z) (b0 + z) |>.comp_add_right z <;> abel
  have hgâ‚… {x : â„} (hx : b â‰¤ x) : g x = g b := by grind
  let G (c x : â„) := slope g x (x + c)
  have G_nonneg (c x : â„) (hc : 0 â‰¤ c) : 0 â‰¤ G c x := by
    simp only [slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul, G]
    exact mul_nonneg (inv_nonneg.mpr hc) (by linarith [hg (show x â‰¤ x + c by linarith)])
  have G_integrable (c : â„) : LocallyIntegrable (G c) volume := by
    simp only [G, slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul]
    exact (hgc c).locallyIntegrable.sub (hg.locallyIntegrable) |>.smul (c := câ»Â¹)
  have G_measurable (n : â„•) : AEMeasurable (G (n : â„)â»Â¹) volume :=
    G_integrable (n : â„)â»Â¹ |>.aestronglyMeasurable |>.aemeasurable
  have G_measurable_ab (n : â„•) : AEMeasurable ((Ioc a b).indicator (G (n : â„)â»Â¹)) volume := by
    apply (G_measurable n).indicator; simp
  have G_lim : âˆ€áµ (x : â„), Filter.Tendsto (fun (n : â„•) â†¦ G (n : â„)â»Â¹ x) Filter.atTop
      (nhds (deriv g x)) := by
    filter_upwards [hgâ‚‚] with x âŸ¨hxâ‚, hxâ‚‚âŸ©
    rw [hasDerivAt_iff_tendsto_slope] at hxâ‚
    dsimp only [G]
    refine hxâ‚.comp <| tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ ?_ ?_
    Â· convert tendsto_const_nhds.add tendsto_inverse_atTop_nhds_zero_nat
      simp
    Â· have : âˆ€á¶  (n : â„•) in atTop, n â‰  0 := by
        simp only [Filter.Eventually, mem_atTop_sets, mem_setOf_eq]
        use 1; intros; omega
      filter_upwards [this] with n hn
      simp only [mem_compl_iff, mem_singleton_iff, add_eq_left, inv_eq_zero]
      norm_cast
  have G_liminf' : âˆ€áµ (x : â„),
      Filter.liminf (fun (n : â„•) â†¦ â€–G (n : â„)â»Â¹ xâ€–â‚‘) Filter.atTop = â€–deriv g xâ€–â‚‘:= by
    filter_upwards [G_lim] with x hx
    exact hx.enorm.liminf_eq
  have G_liminf'_ab : âˆ€áµ (x : â„),
      Filter.liminf (fun (n : â„•) â†¦ â€–(Ioc a b).indicator (G (n : â„)â»Â¹) xâ€–â‚‘) Filter.atTop =
      â€–((Ioc a b).indicator (deriv g)) xâ€–â‚‘ := by
    filter_upwards [G_liminf'] with x hx
    by_cases hxâ‚ : x âˆˆ Ioc a b <;> simp only [hxâ‚, Set.indicator, â†“reduceIte]
    Â· exact hx
    Â· simp
  have G_fatou := MeasureTheory.lintegral_liminf_le' (fun n â†¦ ((G_measurable_ab n).enorm))
  have G_bound {n : â„•} (hn : n â‰¥ 1) :
      n * (âˆ« (x : â„) in a..b, g (x + (n : â„)â»Â¹) - g x) â‰¤ g b - g a := by
    calc
      _ = n * ((âˆ« (x : â„) in a..b, g (x + (â†‘n)â»Â¹)) - âˆ« (x : â„) in a..b, g x) := by
        rw [intervalIntegral.integral_sub (hgâ‚„ _ _ _) (hgâ‚ƒ _ _)]
      _ = n * ((âˆ« (x : â„) in (a + (â†‘n)â»Â¹)..(b + (â†‘n)â»Â¹), g x) - âˆ« (x : â„) in a..b, g x) := by simp
      _ = n * ((âˆ« (x : â„) in b..(b + (â†‘n)â»Â¹), g x) - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g x) := by
        rw [intervalIntegral.integral_interval_sub_interval_comm'] <;> exact hgâ‚ƒ _ _
      _ = n * ((âˆ« (x : â„) in b..(b + (â†‘n)â»Â¹), g b) - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g x) := by
        congr 2
        apply intervalIntegral.integral_congr
        simp only [EqOn, le_add_iff_nonneg_right, inv_nonneg, Nat.cast_nonneg, uIcc_of_le, mem_Icc,
          and_imp]
        exact fun x hxâ‚ _ â†¦ hgâ‚… hxâ‚
      _ = n * ((â†‘n)â»Â¹ * g b - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g x) := by simp
      _ â‰¤ n * ((â†‘n)â»Â¹ * g b - âˆ« (x : â„) in a..(a + (â†‘n)â»Â¹), g a) := by
        gcongr
        apply intervalIntegral.integral_mono_on <;> try simp
        Â· exact hgâ‚ƒ _ _
        Â· intros; apply hg; assumption
      _ = n * ((â†‘n)â»Â¹ * g b - (â†‘n)â»Â¹ * g a) := by simp
      _ = g b - g a := by
        ring_nf
        rw [show (n : â„) * (n : â„)â»Â¹ = 1 by refine mul_inv_cancelâ‚€ ?_; norm_cast; omega]
        ring
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hab]
  constructor
  Â· suffices AEStronglyMeasurable (deriv g) (volume.restrict (Ioc a b)) by
      apply this.congr
      have hâ‚ƒ : âˆ€áµ x âˆ‚(volume.restrict (Ioc a b)), x âˆˆ Ioc a b := by
        apply MeasureTheory.ae_restrict_mem; simp
      have hâ‚„ : âˆ€áµ x âˆ‚(volume.restrict (Ioc a b)), x â‰  b := by
        rw [MeasureTheory.ae_restrict_iff' (by simp)]
        filter_upwards [hâ‚‚] with x hxâ‚ hxâ‚‚
        exact hxâ‚
      filter_upwards [hâ‚ƒ, hâ‚„] with x hxâ‚ hxâ‚‚
      symm; apply hfg
      simp [â† Ioc_diff_right, mem_diff, hxâ‚, hxâ‚‚]
    suffices AEStronglyMeasurable (deriv g) from this.restrict
    apply aestronglyMeasurable_of_tendsto_ae (lim := G_lim)
    exact fun n â†¦ (G_integrable (n : â„)â»Â¹).aestronglyMeasurable
  Â· calc âˆ«â» x in Ioc a b, â€–deriv f xâ€–â‚‘
      _ = âˆ«â» x, (Ioc a b).indicator (fun t â†¦ â€–deriv f tâ€–â‚‘) x := by simp
      _ = âˆ«â» x, (Ioc a b).indicator (fun t â†¦ â€–deriv g tâ€–â‚‘) x := by
        apply MeasureTheory.lintegral_congr_ae
        filter_upwards [hâ‚‚] with x hxb
        by_cases hx : x âˆˆ Ioc a b <;> simp only [indicator, hx, â†“reduceIte]
        congr 1
        apply hfg
        simp [â† Ioc_diff_right, mem_diff, hx, hxb]
      _ = âˆ«â» x, â€–(Ioc a b).indicator (deriv g) xâ€–â‚‘ := by
        apply MeasureTheory.lintegral_congr
        intro x
        dsimp only [Set.indicator]
        by_cases hx : x âˆˆ Ioc a b <;> simp [hx]
      _ â‰¤ liminf (fun (n : â„•) â†¦ âˆ«â» (a_1 : â„), â€–(Ioc a b).indicator (G (n : â„)â»Â¹) a_1â€–â‚‘) atTop := by
        convert G_fatou using 1
        apply MeasureTheory.lintegral_congr_ae
        filter_upwards [G_liminf'_ab] with x hx
        rw [hx]
      _ = liminf (fun (n : â„•) â†¦ ENNReal.ofReal (âˆ« (a_1 : â„), (Ioc a b).indicator (G (n : â„)â»Â¹) a_1))
          atTop := by
        apply Filter.liminf_congr
        filter_upwards with n
        rw [â† MeasureTheory.ofReal_integral_norm_eq_lintegral_enorm]
        Â· congr with y
          apply abs_eq_self.mpr
          dsimp only [Set.indicator]
          by_cases hy : y âˆˆ Ioc a b
          Â· simp only [hy, â†“reduceIte]
            apply G_nonneg; simp
          Â· simp [hy, â†“reduceIte]
        Â· have := (G_integrable (n : â„)â»Â¹).integrableOn_isCompact (k := Icc a b)
              (hk := isCompact_Icc)
          have := this.indicator (t := Ioc a b) (ht := by simp)
          have := this.integrable_indicator (hs := by simp)
          convert this using 1
          ext x
          by_cases hx : x âˆˆ Ioc a b
          Â· simp [indicator, hx, â†“reduceIte, Ioc_subset_Icc_self hx]
          Â· simp [indicator, hx, â†“reduceIte]
      _ = liminf (fun (n : â„•) â†¦ ENNReal.ofReal (âˆ« a_1 in a..b, (G (n : â„)â»Â¹) a_1)) atTop := by
        apply Filter.liminf_congr
        filter_upwards with n
        congr 1
        simp [intervalIntegral.integral_of_le hab, integral_indicator]
      _ â‰¤ ENNReal.ofReal (g b - g a) := by
        apply Filter.liminf_le_of_frequently_le'
        refine Filter.Eventually.frequently ?_
        simp only [Filter.Eventually, mem_atTop_sets, mem_setOf_eq]
        use 1
        intro n hn
        apply ENNReal.ofReal_le_ofReal
        simp only [slope, add_sub_cancel_left, vsub_eq_sub, smul_eq_mul, inv_inv,
          intervalIntegral.integral_const_mul, G]
        exact G_bound hn
      _ < âˆ := ENNReal.ofReal_lt_top

/-- If `f` has locally bounded variation on `uIcc a b`, then `f'` is interval integrable on
`a..b`. -/
theorem LocallyBoundedVariationOn.deriv_intervalIntegrable {f : â„ â†’ â„} {a b : â„}
  (hf : LocallyBoundedVariationOn f (uIcc a b)) :
    IntervalIntegrable (deriv f) volume a b := by
  obtain âŸ¨p, q, hp, hq, rflâŸ© := hf.exists_monotoneOn_sub_monotoneOn
  have hâ‚ : âˆ€áµ x, x â‰  min a b := by simp [ae_iff, measure_singleton]
  have hâ‚‚ : âˆ€áµ x, x â‰  max a b := by simp [ae_iff, measure_singleton]
  have hpâ‚ := hp.deriv_intervalIntegrable
  have hqâ‚ := hq.deriv_intervalIntegrable
  have hpâ‚‚ := hp.ae_differentiableWithinAt_of_mem
  have hqâ‚‚ := hq.ae_differentiableWithinAt_of_mem
  apply (hpâ‚.sub hqâ‚).congr
  rw [Filter.EventuallyEq, MeasureTheory.ae_restrict_iff' (by simp [uIoc])]
  filter_upwards [hpâ‚‚, hqâ‚‚, hâ‚, hâ‚‚] with x hxâ‚ hxâ‚‚ hxâ‚ƒ hxâ‚„ hxâ‚…
  have hxâ‚† : x âˆˆ uIcc a b := Ioc_subset_Icc_self hxâ‚…
  have hxâ‚‡ : x âˆˆ uIoo a b := by
    rw [uIoo, â† Icc_diff_both, mem_diff, â† uIcc]; simp [hxâ‚ƒ, hxâ‚„, hxâ‚†]
  replace hxâ‚ := (hxâ‚ hxâ‚†).hasDerivAt_interval hxâ‚‡
  replace hxâ‚‚ := (hxâ‚‚ hxâ‚†).hasDerivAt_interval hxâ‚‡
  rw [(hxâ‚.sub hxâ‚‚).deriv]

/-- If `f` is absolute continuous on `uIcc a b`, then `f` is a.e. differentiable on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_differentiableWithinAt {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    âˆ€áµ (x : â„), x âˆˆ Set.uIcc a b â†’ DifferentiableWithinAt â„ f (Set.uIcc a b) x :=
  hf.boundedVariationOn.locallyBoundedVariationOn.ae_differentiableWithinAt_of_mem

/-- If `f` is absolute continuous on `uIcc a b`, then `f` exists a.e. on `uIcc a b`. -/
theorem AbsolutelyContinuousOnInterval.ae_hasDerivAt {f : â„ â†’ â„} {a b : â„}
    (hf : AbsolutelyContinuousOnInterval f a b) :
    âˆ€áµ (x : â„), x âˆˆ Set.uIcc a b â†’ HasDerivAt f (deriv f x) x := by
  have hâ‚ : âˆ€áµ x, x â‰  min a b := by simp [ae_iff, measure_singleton]
  have hâ‚‚ : âˆ€áµ x, x â‰  max a b := by simp [ae_iff, measure_singleton]
  filter_upwards [hf.ae_differentiableWithinAt, hâ‚, hâ‚‚] with x hxâ‚ hxâ‚‚ hxâ‚ƒ hxâ‚„
  have : x âˆˆ uIoo a b := by rw [uIoo, â† Icc_diff_both, mem_diff, â† uIcc]; simp [hxâ‚‚, hxâ‚ƒ, hxâ‚„]
  exact (hxâ‚ hxâ‚„).hasDerivAt_interval this

/-- If `f` is absolute continuous on `uIcc a b`, then `f'` is interval integrable on `a..b`. -/
theorem AbsolutelyContinuousOnInterval.deriv_intervalIntegrable {f : â„ â†’ â„} {a b : â„}
  (hf : AbsolutelyContinuousOnInterval f a b) :
    IntervalIntegrable (deriv f) volume a b :=
  hf.boundedVariationOn.locallyBoundedVariationOn.deriv_intervalIntegrable
