/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Integral.IntegralEqImproper

/-!
# Integrals against peak functions

A sequence of peak functions is a sequence of functions with average one concentrating around
a point `xâ‚€`. Given such a sequence `Ï†â‚™`, then `âˆ« Ï†â‚™ g` tends to `g xâ‚€` in many situations, with
a whole zoo of possible assumptions on `Ï†â‚™` and `g`. This file is devoted to such results. Such
functions are also called approximations of unity, or approximations of identity.

## Main results

* `tendsto_setIntegral_peak_smul_of_integrableOn_of_tendsto`: If a sequence of peak
  functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€`, and
  `g` is integrable and continuous at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `g xâ‚€`.
* `tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_continuousOn`:
  If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
  then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
  concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€`
  if `g` is continuous on `s`.
* `tendsto_integral_comp_smul_smul_of_integrable`:
  If a nonnegative function `Ï†` has integral one and decays quickly enough at infinity,
  then its renormalizations `x â†¦ c ^ d * Ï† (c â€¢ x)` form a sequence of peak functions as `c â†’ âˆ`.
  Therefore, `âˆ« (c ^ d * Ï† (c â€¢ x)) â€¢ g x` converges to `g 0` as `c â†’ âˆ` if `g` is continuous
  at `0` and integrable.

Note that there are related results about convolution with respect to peak functions in the file
`Mathlib/Analysis/Convolution.lean`, such as `MeasureTheory.convolution_tendsto_right` there.
-/

open Set Filter MeasureTheory MeasureTheory.Measure TopologicalSpace Metric

open scoped Topology ENNReal

/-!
### General convergent result for integrals against a sequence of peak functions
-/

open Set

variable {Î± E Î¹ : Type*} {hm : MeasurableSpace Î±} {Î¼ : Measure Î±} [TopologicalSpace Î±]
  [BorelSpace Î±] [NormedAddCommGroup E] [NormedSpace â„ E] {g : Î± â†’ E} {l : Filter Î¹} {xâ‚€ : Î±}
  {s t : Set Î±} {Ï† : Î¹ â†’ Î± â†’ â„} {a : E}

/-- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€`, and
`g` is integrable and has a limit at `xâ‚€`, then `Ï†áµ¢ â€¢ g` is eventually integrable. -/
theorem integrableOn_peak_smul_of_integrableOn_of_tendsto
    (hs : MeasurableSet s) (h'st : t âˆˆ ğ“[s] xâ‚€)
    (hlÏ† : âˆ€ u : Set Î±, IsOpen u â†’ xâ‚€ âˆˆ u â†’ TendstoUniformlyOn Ï† 0 l (s \ u))
    (hiÏ† : Tendsto (fun i â†¦ âˆ« x in t, Ï† i x âˆ‚Î¼) l (ğ“ 1))
    (h'iÏ† : âˆ€á¶  i in l, AEStronglyMeasurable (Ï† i) (Î¼.restrict s))
    (hmg : IntegrableOn g s Î¼) (hcg : Tendsto g (ğ“[s] xâ‚€) (ğ“ a)) :
    âˆ€á¶  i in l, IntegrableOn (fun x => Ï† i x â€¢ g x) s Î¼ := by
  obtain âŸ¨u, u_open, xâ‚€u, ut, huâŸ© :
      âˆƒ u, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ s âˆ© u âŠ† t âˆ§ âˆ€ x âˆˆ u âˆ© s, g x âˆˆ ball a 1 := by
    rcases mem_nhdsWithin.1 (Filter.inter_mem h'st (hcg (ball_mem_nhds _ zero_lt_one)))
      with âŸ¨u, u_open, xâ‚€u, huâŸ©
    refine âŸ¨u, u_open, xâ‚€u, ?_, hu.trans inter_subset_rightâŸ©
    rw [inter_comm]
    exact hu.trans inter_subset_left
  rw [tendsto_iff_norm_sub_tendsto_zero] at hiÏ†
  filter_upwards [tendstoUniformlyOn_iff.1 (hlÏ† u u_open xâ‚€u) 1 zero_lt_one,
    (tendsto_order.1 hiÏ†).2 1 zero_lt_one, h'iÏ†] with i hi h'i h''i
  have I : IntegrableOn (Ï† i) t Î¼ := .of_integral_ne_zero (fun h â†¦ by simp [h] at h'i)
  have A : IntegrableOn (fun x => Ï† i x â€¢ g x) (s \ u) Î¼ := by
    refine Integrable.smul_of_top_right (hmg.mono diff_subset le_rfl) ?_
    apply memLp_top_of_bound (h''i.mono_set diff_subset) 1
    filter_upwards [self_mem_ae_restrict (hs.diff u_open.measurableSet)] with x hx
    simpa only [Pi.zero_apply, dist_zero_left] using (hi x hx).le
  have B : IntegrableOn (fun x => Ï† i x â€¢ g x) (s âˆ© u) Î¼ := by
    apply Integrable.smul_of_top_left
    Â· exact IntegrableOn.mono_set I ut
    Â· apply
        memLp_top_of_bound (hmg.mono_set inter_subset_left).aestronglyMeasurable (â€–aâ€– + 1)
      filter_upwards [self_mem_ae_restrict (hs.inter u_open.measurableSet)] with x hx
      rw [inter_comm] at hx
      exact (norm_lt_of_mem_ball (hu x hx)).le
  convert A.union B
  simp only [diff_union_inter]

/-- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€` and its
integral on some finite-measure neighborhood of `xâ‚€` converges to `1`, and `g` is integrable and
has a limit `a` at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `a`.
Auxiliary lemma where one assumes additionally `a = 0`. -/
theorem tendsto_setIntegral_peak_smul_of_integrableOn_of_tendsto_aux
    (hs : MeasurableSet s) (ht : MeasurableSet t) (hts : t âŠ† s) (h'ts : t âˆˆ ğ“[s] xâ‚€)
    (hnÏ† : âˆ€á¶  i in l, âˆ€ x âˆˆ s, 0 â‰¤ Ï† i x)
    (hlÏ† : âˆ€ u : Set Î±, IsOpen u â†’ xâ‚€ âˆˆ u â†’ TendstoUniformlyOn Ï† 0 l (s \ u))
    (hiÏ† : Tendsto (fun i â†¦ âˆ« x in t, Ï† i x âˆ‚Î¼) l (ğ“ 1))
    (h'iÏ† : âˆ€á¶  i in l, AEStronglyMeasurable (Ï† i) (Î¼.restrict s))
    (hmg : IntegrableOn g s Î¼) (hcg : Tendsto g (ğ“[s] xâ‚€) (ğ“ 0)) :
    Tendsto (fun i : Î¹ => âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼) l (ğ“ 0) := by
  refine Metric.tendsto_nhds.2 fun Îµ Îµpos => ?_
  obtain âŸ¨Î´, hÎ´, Î´pos, Î´oneâŸ© : âˆƒ Î´, (Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼) + 2 * Î´ < Îµ âˆ§ 0 < Î´ âˆ§ Î´ < 1 := by
    have A :
      Tendsto (fun Î´ => (Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼) + 2 * Î´) (ğ“[>] 0)
        (ğ“ ((0 * âˆ« x in s, â€–g xâ€– âˆ‚Î¼) + 2 * 0)) := by
      apply Tendsto.mono_left _ nhdsWithin_le_nhds
      exact (tendsto_id.mul tendsto_const_nhds).add (tendsto_id.const_mul _)
    rw [zero_mul, zero_add, mul_zero] at A
    have : Ioo (0 : â„) 1 âˆˆ ğ“[>] 0 := Ioo_mem_nhdsGT zero_lt_one
    rcases (((tendsto_order.1 A).2 Îµ Îµpos).and this).exists with âŸ¨Î´, hÎ´, h'Î´âŸ©
    exact âŸ¨Î´, hÎ´, h'Î´.1, h'Î´.2âŸ©
  suffices âˆ€á¶  i in l, â€–âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ (Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼) + 2 * Î´ by
    filter_upwards [this] with i hi
    simp only [dist_zero_right]
    exact hi.trans_lt hÎ´
  obtain âŸ¨u, u_open, xâ‚€u, ut, huâŸ© :
      âˆƒ u, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ s âˆ© u âŠ† t âˆ§ âˆ€ x âˆˆ u âˆ© s, g x âˆˆ ball 0 Î´ := by
    rcases mem_nhdsWithin.1 (Filter.inter_mem h'ts (hcg (ball_mem_nhds _ Î´pos)))
      with âŸ¨u, u_open, xâ‚€u, huâŸ©
    refine âŸ¨u, u_open, xâ‚€u, ?_, hu.trans inter_subset_rightâŸ©
    rw [inter_comm]
    exact hu.trans inter_subset_left
  filter_upwards [tendstoUniformlyOn_iff.1 (hlÏ† u u_open xâ‚€u) Î´ Î´pos,
    (tendsto_order.1 (tendsto_iff_norm_sub_tendsto_zero.1 hiÏ†)).2 Î´ Î´pos, hnÏ†,
    integrableOn_peak_smul_of_integrableOn_of_tendsto hs h'ts hlÏ† hiÏ† h'iÏ† hmg hcg]
    with i hi h'i hÏ†pos h''i
  have I : IntegrableOn (Ï† i) t Î¼ := by
    apply Integrable.of_integral_ne_zero (fun h â†¦ ?_)
    simp [h] at h'i
    linarith
  have B : â€–âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ 2 * Î´ :=
    calc
      â€–âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ âˆ« x in s âˆ© u, â€–Ï† i x â€¢ g xâ€– âˆ‚Î¼ :=
        norm_integral_le_integral_norm _
      _ â‰¤ âˆ« x in s âˆ© u, â€–Ï† i xâ€– * Î´ âˆ‚Î¼ := by
        refine setIntegral_mono_on ?_ ?_ (hs.inter u_open.measurableSet) fun x hx => ?_
        Â· exact IntegrableOn.mono_set h''i.norm inter_subset_left
        Â· exact IntegrableOn.mono_set (I.norm.mul_const _) ut
        rw [norm_smul]
        gcongr
        rw [inter_comm] at hu
        exact (mem_ball_zero_iff.1 (hu x hx)).le
      _ â‰¤ âˆ« x in t, â€–Ï† i xâ€– * Î´ âˆ‚Î¼ := by
        apply setIntegral_mono_set
        Â· exact I.norm.mul_const _
        Â· exact Eventually.of_forall fun x => mul_nonneg (norm_nonneg _) Î´pos.le
        Â· exact Eventually.of_forall ut
      _ = âˆ« x in t, Ï† i x * Î´ âˆ‚Î¼ := by
        apply setIntegral_congr_fun ht fun x hx => ?_
        rw [Real.norm_of_nonneg (hÏ†pos _ (hts hx))]
      _ = (âˆ« x in t, Ï† i x âˆ‚Î¼) * Î´ := by rw [integral_mul_const]
      _ â‰¤ 2 * Î´ := by gcongr; linarith [(le_abs_self _).trans h'i.le]
  have C : â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ :=
    calc
      â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ âˆ« x in s \ u, â€–Ï† i x â€¢ g xâ€– âˆ‚Î¼ :=
        norm_integral_le_integral_norm _
      _ â‰¤ âˆ« x in s \ u, Î´ * â€–g xâ€– âˆ‚Î¼ := by
        refine setIntegral_mono_on ?_ ?_ (hs.diff u_open.measurableSet) fun x hx => ?_
        Â· exact IntegrableOn.mono_set h''i.norm diff_subset
        Â· exact IntegrableOn.mono_set (hmg.norm.const_mul _) diff_subset
        rw [norm_smul]
        gcongr
        simpa only [Pi.zero_apply, dist_zero_left] using (hi x hx).le
      _ â‰¤ Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ := by
        rw [integral_const_mul]
        apply mul_le_mul_of_nonneg_left (setIntegral_mono_set hmg.norm _ _) Î´pos.le
        Â· filter_upwards with x using norm_nonneg _
        Â· filter_upwards using diff_subset (s := s) (t := u)
  calc
    â€–âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼â€– =
      â€–(âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼) + âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– := by
      conv_lhs => rw [â† diff_union_inter s u]
      rw [setIntegral_union disjoint_sdiff_inter (hs.inter u_open.measurableSet)
          (h''i.mono_set diff_subset) (h''i.mono_set inter_subset_left)]
    _ â‰¤ â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼â€– + â€–âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– := norm_add_le _ _
    _ â‰¤ (Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼) + 2 * Î´ := add_le_add C B

variable [CompleteSpace E]

/-- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€` and its
integral on some finite-measure neighborhood of `xâ‚€` converges to `1`, and `g` is integrable and
has a limit `a` at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `a`. Version localized to a subset. -/
theorem tendsto_setIntegral_peak_smul_of_integrableOn_of_tendsto
    (hs : MeasurableSet s) {t : Set Î±} (ht : MeasurableSet t) (hts : t âŠ† s) (h'ts : t âˆˆ ğ“[s] xâ‚€)
    (h't : Î¼ t â‰  âˆ) (hnÏ† : âˆ€á¶  i in l, âˆ€ x âˆˆ s, 0 â‰¤ Ï† i x)
    (hlÏ† : âˆ€ u : Set Î±, IsOpen u â†’ xâ‚€ âˆˆ u â†’ TendstoUniformlyOn Ï† 0 l (s \ u))
    (hiÏ† : Tendsto (fun i â†¦ âˆ« x in t, Ï† i x âˆ‚Î¼) l (ğ“ 1))
    (h'iÏ† : âˆ€á¶  i in l, AEStronglyMeasurable (Ï† i) (Î¼.restrict s))
    (hmg : IntegrableOn g s Î¼) (hcg : Tendsto g (ğ“[s] xâ‚€) (ğ“ a)) :
    Tendsto (fun i : Î¹ â†¦ âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼) l (ğ“ a) := by
  let h := g - t.indicator (fun _ â†¦ a)
  have A : Tendsto (fun i : Î¹ => (âˆ« x in s, Ï† i x â€¢ h x âˆ‚Î¼) + (âˆ« x in t, Ï† i x âˆ‚Î¼) â€¢ a) l
      (ğ“ (0 + (1 : â„) â€¢ a)) := by
    refine Tendsto.add ?_ (Tendsto.smul hiÏ† tendsto_const_nhds)
    apply tendsto_setIntegral_peak_smul_of_integrableOn_of_tendsto_aux hs ht hts h'ts
        hnÏ† hlÏ† hiÏ† h'iÏ†
    Â· apply hmg.sub
      simp only [integrable_indicator_iff ht, integrableOn_const_iff (C := a), ht,
        Measure.restrict_apply]
      right
      exact lt_of_le_of_lt (measure_mono inter_subset_left) (h't.lt_top)
    Â· rw [â† sub_self a]
      apply Tendsto.sub hcg
      apply tendsto_const_nhds.congr'
      filter_upwards [h'ts] with x hx using by simp [hx]
  simp only [one_smul, zero_add] at A
  refine Tendsto.congr' ?_ A
  filter_upwards [integrableOn_peak_smul_of_integrableOn_of_tendsto hs h'ts
    hlÏ† hiÏ† h'iÏ† hmg hcg,
    (tendsto_order.1 (tendsto_iff_norm_sub_tendsto_zero.1 hiÏ†)).2 1 zero_lt_one] with i hi h'i
  simp only [h, Pi.sub_apply, smul_sub, â† indicator_smul_apply]
  rw [integral_sub hi, setIntegral_indicator ht, inter_eq_right.mpr hts,
    integral_smul_const, sub_add_cancel]
  rw [integrable_indicator_iff ht]
  apply Integrable.smul_const
  rw [restrict_restrict ht, inter_eq_left.mpr hts]
  exact .of_integral_ne_zero (fun h â†¦ by simp [h] at h'i)

/-- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€` and its
integral on some finite-measure neighborhood of `xâ‚€` converges to `1`, and `g` is integrable and
has a limit `a` at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `a`. -/
theorem tendsto_integral_peak_smul_of_integrable_of_tendsto
    {t : Set Î±} (ht : MeasurableSet t) (h'ts : t âˆˆ ğ“ xâ‚€)
    (h't : Î¼ t â‰  âˆ) (hnÏ† : âˆ€á¶  i in l, âˆ€ x, 0 â‰¤ Ï† i x)
    (hlÏ† : âˆ€ u : Set Î±, IsOpen u â†’ xâ‚€ âˆˆ u â†’ TendstoUniformlyOn Ï† 0 l uá¶œ)
    (hiÏ† : Tendsto (fun i â†¦ âˆ« x in t, Ï† i x âˆ‚Î¼) l (ğ“ 1))
    (h'iÏ† : âˆ€á¶  i in l, AEStronglyMeasurable (Ï† i) Î¼)
    (hmg : Integrable g Î¼) (hcg : Tendsto g (ğ“ xâ‚€) (ğ“ a)) :
    Tendsto (fun i : Î¹ â†¦ âˆ« x, Ï† i x â€¢ g x âˆ‚Î¼) l (ğ“ a) := by
  suffices Tendsto (fun i : Î¹ â†¦ âˆ« x in univ, Ï† i x â€¢ g x âˆ‚Î¼) l (ğ“ a) by simpa
  exact tendsto_setIntegral_peak_smul_of_integrableOn_of_tendsto MeasurableSet.univ ht (xâ‚€ := xâ‚€)
    (subset_univ _) (by simpa [nhdsWithin_univ]) h't (by simpa)
    (by simpa [â† compl_eq_univ_diff] using hlÏ†) hiÏ†
    (by simpa) (by simpa) (by simpa [nhdsWithin_univ])

/-!
### Peak functions of the form `x â†¦ (c x) ^ n / âˆ« (c y) ^ n`
-/

/-- If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€` if `g` is
integrable on `s` and continuous at `xâ‚€`.

Version assuming that `Î¼` gives positive mass to all neighborhoods of `xâ‚€` within `s`.
For a less precise but more usable version, see
`tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_continuousOn`.
-/
theorem tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_measure_nhdsWithin_pos
    [MetrizableSpace Î±] [IsLocallyFiniteMeasure Î¼] (hs : IsCompact s)
    (hÎ¼ : âˆ€ u, IsOpen u â†’ xâ‚€ âˆˆ u â†’ 0 < Î¼ (u âˆ© s)) {c : Î± â†’ â„} (hc : ContinuousOn c s)
    (h'c : âˆ€ y âˆˆ s, y â‰  xâ‚€ â†’ c y < c xâ‚€) (hnc : âˆ€ x âˆˆ s, 0 â‰¤ c x) (hncâ‚€ : 0 < c xâ‚€) (hâ‚€ : xâ‚€ âˆˆ s)
    (hmg : IntegrableOn g s Î¼) (hcg : ContinuousWithinAt g s xâ‚€) :
    Tendsto (fun n : â„• => (âˆ« x in s, c x ^ n âˆ‚Î¼)â»Â¹ â€¢ âˆ« x in s, c x ^ n â€¢ g x âˆ‚Î¼)
      atTop (ğ“ (g xâ‚€)) := by
  /- We apply the general result
    `tendsto_setIntegral_peak_smul_of_integrableOn_of_continuousWithinAt` to the sequence of
    peak functions `Ï†â‚™ = (c x) ^ n / âˆ« (c x) ^ n`. The only nontrivial bit is to check that this
    sequence converges uniformly to zero on any set `s \ u` away from `xâ‚€`. By compactness, the
    function `c` is bounded by `t < c xâ‚€` there. Consider `t' âˆˆ (t, c xâ‚€)`, and a neighborhood `v`
    of `xâ‚€` where `c x â‰¥ t'`, by continuity. Then `âˆ« (c x) ^ n` is bounded below by `t' ^ n Î¼ v`.
    It follows that, on `s \ u`, then `Ï†â‚™ x â‰¤ t ^ n / (t' ^ n Î¼ v)`,
    which tends (exponentially fast) to zero with `n`. -/
  let Ï† : â„• â†’ Î± â†’ â„ := fun n x => (âˆ« x in s, c x ^ n âˆ‚Î¼)â»Â¹ * c x ^ n
  have hnÏ† : âˆ€ n, âˆ€ x âˆˆ s, 0 â‰¤ Ï† n x := by
    intro n x hx
    apply mul_nonneg (inv_nonneg.2 _) (pow_nonneg (hnc x hx) _)
    exact setIntegral_nonneg hs.measurableSet fun x hx => pow_nonneg (hnc x hx) _
  have I : âˆ€ n, IntegrableOn (fun x => c x ^ n) s Î¼ := fun n =>
    ContinuousOn.integrableOn_compact hs (hc.pow n)
  have J : âˆ€ n, 0 â‰¤áµ[Î¼.restrict s] fun x : Î± => c x ^ n := by
    intro n
    filter_upwards [ae_restrict_mem hs.measurableSet] with x hx
    exact pow_nonneg (hnc x hx) n
  have P : âˆ€ n, (0 : â„) < âˆ« x in s, c x ^ n âˆ‚Î¼ := by
    intro n
    refine (setIntegral_pos_iff_support_of_nonneg_ae (J n) (I n)).2 ?_
    obtain âŸ¨u, u_open, xâ‚€_u, huâŸ© : âˆƒ u : Set Î±, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ u âˆ© s âŠ† c â»Â¹' Ioi 0 :=
      _root_.continuousOn_iff.1 hc xâ‚€ hâ‚€ (Ioi (0 : â„)) isOpen_Ioi hncâ‚€
    apply (hÎ¼ u u_open xâ‚€_u).trans_le
    exact measure_mono fun x hx => âŸ¨ne_of_gt (pow_pos (a := c x) (hu hx) _), hx.2âŸ©
  have hiÏ† : âˆ€ n, âˆ« x in s, Ï† n x âˆ‚Î¼ = 1 := fun n => by
    rw [integral_const_mul, inv_mul_cancelâ‚€ (P n).ne']
  have A : âˆ€ u : Set Î±, IsOpen u â†’ xâ‚€ âˆˆ u â†’ TendstoUniformlyOn Ï† 0 atTop (s \ u) := by
    intro u u_open xâ‚€u
    obtain âŸ¨t, t_pos, txâ‚€, htâŸ© : âˆƒ t, 0 â‰¤ t âˆ§ t < c xâ‚€ âˆ§ âˆ€ x âˆˆ s \ u, c x â‰¤ t := by
      rcases eq_empty_or_nonempty (s \ u) with (h | h)
      Â· exact
          âŸ¨0, le_rfl, hncâ‚€, by simp only [h, mem_empty_iff_false, IsEmpty.forall_iff, imp_true_iff]âŸ©
      obtain âŸ¨x, hx, h'xâŸ© : âˆƒ x âˆˆ s \ u, âˆ€ y âˆˆ s \ u, c y â‰¤ c x :=
        IsCompact.exists_isMaxOn (hs.diff u_open) h (hc.mono diff_subset)
      refine âŸ¨c x, hnc x hx.1, h'c x hx.1 ?_, h'xâŸ©
      rintro rfl
      exact hx.2 xâ‚€u
    obtain âŸ¨t', tt', t'xâ‚€âŸ© : âˆƒ t', t < t' âˆ§ t' < c xâ‚€ := exists_between txâ‚€
    have t'_pos : 0 < t' := t_pos.trans_lt tt'
    obtain âŸ¨v, v_open, xâ‚€_v, hvâŸ© : âˆƒ v : Set Î±, IsOpen v âˆ§ xâ‚€ âˆˆ v âˆ§ v âˆ© s âŠ† c â»Â¹' Ioi t' :=
      _root_.continuousOn_iff.1 hc xâ‚€ hâ‚€ (Ioi t') isOpen_Ioi t'xâ‚€
    have M : âˆ€ n, âˆ€ x âˆˆ s \ u, Ï† n x â‰¤ (Î¼.real (v âˆ© s))â»Â¹ * (t / t') ^ n := by
      intro n x hx
      have B : t' ^ n * Î¼.real (v âˆ© s) â‰¤ âˆ« y in s, c y ^ n âˆ‚Î¼ :=
        calc
          t' ^ n * Î¼.real (v âˆ© s) = âˆ« _ in v âˆ© s, t' ^ n âˆ‚Î¼ := by simp [mul_comm]
          _ â‰¤ âˆ« y in v âˆ© s, c y ^ n âˆ‚Î¼ := by
            apply setIntegral_mono_on _ _ (v_open.measurableSet.inter hs.measurableSet) _
            Â· refine integrableOn_const (C := t' ^ n) ?_
              exact (lt_of_le_of_lt (measure_mono inter_subset_right) hs.measure_lt_top).ne
            Â· exact (I n).mono inter_subset_right le_rfl
            Â· intro x hx
              exact pow_le_pow_leftâ‚€ t'_pos.le (hv hx).le _
          _ â‰¤ âˆ« y in s, c y ^ n âˆ‚Î¼ :=
            setIntegral_mono_set (I n) (J n) (Eventually.of_forall inter_subset_right)
      simp_rw [Ï†, â† div_eq_inv_mul, div_pow, div_div]
      have := ENNReal.toReal_pos (hÎ¼ v v_open xâ‚€_v).ne'
        ((measure_mono inter_subset_right).trans_lt hs.measure_lt_top).ne
      gcongr
      Â· exact hnc _ hx.1
      Â· exact ht x hx
    have N :
      Tendsto (fun n => (Î¼.real (v âˆ© s))â»Â¹ * (t / t') ^ n) atTop
        (ğ“ ((Î¼.real (v âˆ© s))â»Â¹ * 0)) := by
      apply Tendsto.mul tendsto_const_nhds _
      apply tendsto_pow_atTop_nhds_zero_of_lt_one (div_nonneg t_pos t'_pos.le)
      exact (div_lt_one t'_pos).2 tt'
    rw [mul_zero] at N
    refine tendstoUniformlyOn_iff.2 fun Îµ Îµpos => ?_
    filter_upwards [(tendsto_order.1 N).2 Îµ Îµpos] with n hn x hx
    simp only [Pi.zero_apply, dist_zero_left, Real.norm_of_nonneg (hnÏ† n x hx.1)]
    exact (M n x hx).trans_lt hn
  have : Tendsto (fun i : â„• => âˆ« x : Î± in s, Ï† i x â€¢ g x âˆ‚Î¼) atTop (ğ“ (g xâ‚€)) := by
    have B : Tendsto (fun i â†¦ âˆ« (x : Î±) in s, Ï† i x âˆ‚Î¼) atTop (ğ“ 1) :=
      tendsto_const_nhds.congr (fun n â†¦ (hiÏ† n).symm)
    have C : âˆ€á¶  (i : â„•) in atTop, AEStronglyMeasurable (fun x â†¦ Ï† i x) (Î¼.restrict s) := by
      apply Eventually.of_forall (fun n â†¦ ((I n).const_mul _).aestronglyMeasurable)
    exact tendsto_setIntegral_peak_smul_of_integrableOn_of_tendsto hs.measurableSet
      hs.measurableSet (Subset.rfl) (self_mem_nhdsWithin)
      hs.measure_lt_top.ne (Eventually.of_forall hnÏ†) A B C hmg hcg
  convert this
  simp_rw [Ï†, â† smul_smul, integral_smul]

/-- If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€` if `g` is
integrable on `s` and continuous at `xâ‚€`.

Version assuming that `Î¼` gives positive mass to all open sets.
For a less precise but more usable version, see
`tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_continuousOn`.
-/
theorem tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_integrableOn
    [MetrizableSpace Î±] [IsLocallyFiniteMeasure Î¼] [IsOpenPosMeasure Î¼] (hs : IsCompact s)
    {c : Î± â†’ â„} (hc : ContinuousOn c s) (h'c : âˆ€ y âˆˆ s, y â‰  xâ‚€ â†’ c y < c xâ‚€)
    (hnc : âˆ€ x âˆˆ s, 0 â‰¤ c x) (hncâ‚€ : 0 < c xâ‚€) (hâ‚€ : xâ‚€ âˆˆ closure (interior s))
    (hmg : IntegrableOn g s Î¼) (hcg : ContinuousWithinAt g s xâ‚€) :
    Tendsto (fun n : â„• => (âˆ« x in s, c x ^ n âˆ‚Î¼)â»Â¹ â€¢ âˆ« x in s, c x ^ n â€¢ g x âˆ‚Î¼) atTop
      (ğ“ (g xâ‚€)) := by
  have : xâ‚€ âˆˆ s := by rw [â† hs.isClosed.closure_eq]; exact closure_mono interior_subset hâ‚€
  apply
    tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_measure_nhdsWithin_pos hs _ hc
      h'c hnc hncâ‚€ this hmg hcg
  intro u u_open xâ‚€_u
  calc
    0 < Î¼ (u âˆ© interior s) :=
      (u_open.inter isOpen_interior).measure_pos Î¼ (_root_.mem_closure_iff.1 hâ‚€ u u_open xâ‚€_u)
    _ â‰¤ Î¼ (u âˆ© s) := by gcongr; apply interior_subset

/-- If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€` if `g` is
continuous on `s`. -/
theorem tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_continuousOn
    [MetrizableSpace Î±] [IsLocallyFiniteMeasure Î¼] [IsOpenPosMeasure Î¼] (hs : IsCompact s)
    {c : Î± â†’ â„} (hc : ContinuousOn c s) (h'c : âˆ€ y âˆˆ s, y â‰  xâ‚€ â†’ c y < c xâ‚€)
    (hnc : âˆ€ x âˆˆ s, 0 â‰¤ c x) (hncâ‚€ : 0 < c xâ‚€) (hâ‚€ : xâ‚€ âˆˆ closure (interior s))
    (hmg : ContinuousOn g s) :
    Tendsto (fun n : â„• => (âˆ« x in s, c x ^ n âˆ‚Î¼)â»Â¹ â€¢ âˆ« x in s, c x ^ n â€¢ g x âˆ‚Î¼) atTop (ğ“ (g xâ‚€)) :=
  haveI : xâ‚€ âˆˆ s := by rw [â† hs.isClosed.closure_eq]; exact closure_mono interior_subset hâ‚€
  tendsto_setIntegral_pow_smul_of_unique_maximum_of_isCompact_of_integrableOn hs hc h'c hnc hncâ‚€ hâ‚€
    (hmg.integrableOn_compact hs) (hmg xâ‚€ this)

/-!
### Peak functions of the form `x â†¦ c ^ dim * Ï† (c x)`
-/

open Module Bornology

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F]
  [MeasurableSpace F] [BorelSpace F] {Î¼ : Measure F} [IsAddHaarMeasure Î¼]

/-- Consider a nonnegative function `Ï†` with integral one, decaying quickly enough at infinity.
Then suitable renormalizations of `Ï†` form a sequence of peak functions around the origin:
`âˆ« (c ^ d * Ï† (c â€¢ x)) â€¢ g x` converges to `g 0` as `c â†’ âˆ` if `g` is continuous at `0`
and integrable. -/
theorem tendsto_integral_comp_smul_smul_of_integrable
    {Ï† : F â†’ â„} (hÏ† : âˆ€ x, 0 â‰¤ Ï† x) (h'Ï† : âˆ« x, Ï† x âˆ‚Î¼ = 1)
    (h : Tendsto (fun x â†¦ â€–xâ€– ^ finrank â„ F * Ï† x) (cobounded F) (ğ“ 0))
    {g : F â†’ E} (hg : Integrable g Î¼) (h'g : ContinuousAt g 0) :
    Tendsto (fun (c : â„) â†¦ âˆ« x, (c ^ (finrank â„ F) * Ï† (c â€¢ x)) â€¢ g x âˆ‚Î¼) atTop (ğ“ (g 0)) := by
  have I : Integrable Ï† Î¼ := integrable_of_integral_eq_one h'Ï†
  apply tendsto_integral_peak_smul_of_integrable_of_tendsto (t := closedBall 0 1) (xâ‚€ := 0)
  Â· exact isClosed_closedBall.measurableSet
  Â· exact closedBall_mem_nhds _ zero_lt_one
  Â· exact (isCompact_closedBall 0 1).measure_ne_top
  Â· filter_upwards [Ici_mem_atTop 0] with c (hc : 0 â‰¤ c) x using mul_nonneg (by positivity) (hÏ† _)
  Â· intro u u_open hu
    apply tendstoUniformlyOn_iff.2 (fun Îµ Îµpos â†¦ ?_)
    obtain âŸ¨Î´, Î´pos, h'uâŸ© : âˆƒ Î´ > 0, ball 0 Î´ âŠ† u := Metric.isOpen_iff.1 u_open _ hu
    obtain âŸ¨M, Mpos, hMâŸ© : âˆƒ M > 0, âˆ€ â¦ƒx : Fâ¦„, x âˆˆ (closedBall 0 M)á¶œ â†’
        â€–xâ€– ^ finrank â„ F * Ï† x < Î´ ^ finrank â„ F * Îµ := by
      rcases (hasBasis_cobounded_compl_closedBall (0 : F)).eventually_iff.1
        ((tendsto_order.1 h).2 (Î´ ^ finrank â„ F * Îµ) (by positivity)) with âŸ¨M, -, hMâŸ©
      refine âŸ¨max M 1, zero_lt_one.trans_le (le_max_right _ _), fun x hx â†¦ hM ?_âŸ©
      simp only [mem_compl_iff, mem_closedBall, dist_zero_right, le_max_iff, not_or, not_le] at hx
      simpa using hx.1
    filter_upwards [Ioi_mem_atTop (M / Î´)] with c (hc : M / Î´ < c) x hx
    have cpos : 0 < c := lt_trans (by positivity) hc
    suffices c ^ finrank â„ F * Ï† (c â€¢ x) < Îµ by simpa [abs_of_nonneg (hÏ† _), abs_of_nonneg cpos.le]
    have hÎ´x : Î´ â‰¤ â€–xâ€– := by
      have : x âˆˆ (ball 0 Î´)á¶œ := fun h â†¦ hx (h'u h)
      simpa only [mem_compl_iff, mem_ball, dist_zero_right, not_lt]
    suffices Î´ ^ finrank â„ F * (c ^ finrank â„ F * Ï† (c â€¢ x)) < Î´ ^ finrank â„ F * Îµ by
      rwa [mul_lt_mul_iff_of_pos_left (by positivity)] at this
    calc
      Î´ ^ finrank â„ F * (c ^ finrank â„ F * Ï† (c â€¢ x))
      _ â‰¤ â€–xâ€– ^ finrank â„ F * (c ^ finrank â„ F * Ï† (c â€¢ x)) := by
        gcongr; exact mul_nonneg (by positivity) (hÏ† _)
      _ = â€–c â€¢ xâ€– ^ finrank â„ F * Ï† (c â€¢ x) := by
        simp [norm_smul, abs_of_pos cpos, mul_pow]; ring
      _ < Î´ ^ finrank â„ F * Îµ := by
        apply hM
        rw [div_lt_iffâ‚€ Î´pos] at hc
        simp only [mem_compl_iff, mem_closedBall, dist_zero_right, norm_smul, Real.norm_eq_abs,
          abs_of_nonneg cpos.le, not_le, gt_iff_lt]
        exact hc.trans_le (by gcongr)
  Â· have : Tendsto (fun c â†¦ âˆ« (x : F) in closedBall 0 c, Ï† x âˆ‚Î¼) atTop (ğ“ 1) := by
      rw [â† h'Ï†]
      exact (aecover_closedBall tendsto_id).integral_tendsto_of_countably_generated I
    apply this.congr'
    filter_upwards [Ioi_mem_atTop 0] with c (hc : 0 < c)
    rw [integral_const_mul, setIntegral_comp_smul_of_pos _ _ _ hc, smul_eq_mul, â† mul_assoc,
      mul_inv_cancelâ‚€ (by positivity), _root_.smul_closedBall _ _ zero_le_one]
    simp [abs_of_nonneg hc.le]
  Â· filter_upwards [Ioi_mem_atTop 0] with c (hc : 0 < c)
    exact (I.comp_smul hc.ne').aestronglyMeasurable.const_mul _
  Â· exact hg
  Â· exact h'g

/-- Consider a nonnegative function `Ï†` with integral one, decaying quickly enough at infinity.
Then suitable renormalizations of `Ï†` form a sequence of peak functions around any point:
`âˆ« (c ^ d * Ï† (c â€¢ (xâ‚€ - x)) â€¢ g x` converges to `g xâ‚€` as `c â†’ âˆ` if `g` is continuous at `xâ‚€`
and integrable. -/
theorem tendsto_integral_comp_smul_smul_of_integrable'
    {Ï† : F â†’ â„} (hÏ† : âˆ€ x, 0 â‰¤ Ï† x) (h'Ï† : âˆ« x, Ï† x âˆ‚Î¼ = 1)
    (h : Tendsto (fun x â†¦ â€–xâ€– ^ finrank â„ F * Ï† x) (cobounded F) (ğ“ 0))
    {g : F â†’ E} {xâ‚€ : F} (hg : Integrable g Î¼) (h'g : ContinuousAt g xâ‚€) :
    Tendsto (fun (c : â„) â†¦ âˆ« x, (c ^ (finrank â„ F) * Ï† (c â€¢ (xâ‚€ - x))) â€¢ g x âˆ‚Î¼)
      atTop (ğ“ (g xâ‚€)) := by
  let f := fun x â†¦ g (xâ‚€ - x)
  have If : Integrable f Î¼ := by simpa [f, sub_eq_add_neg] using (hg.comp_add_left xâ‚€).comp_neg
  have : Tendsto (fun (c : â„) â†¦ âˆ« x, (c ^ (finrank â„ F) * Ï† (c â€¢ x)) â€¢ f x âˆ‚Î¼)
      atTop (ğ“ (f 0)) := by
    apply tendsto_integral_comp_smul_smul_of_integrable hÏ† h'Ï† h If
    have A : ContinuousAt g (xâ‚€ - 0) := by simpa using h'g
    exact A.comp <| by fun_prop
  simp only [f, sub_zero] at this
  convert this using 2 with c
  conv_rhs => rw [â† integral_add_left_eq_self xâ‚€ (Î¼ := Î¼)
    (f := fun x â†¦ (c ^ finrank â„ F * Ï† (c â€¢ x)) â€¢ g (xâ‚€ - x)), â† integral_neg_eq_self]
  simp [smul_sub, sub_eq_add_neg]
