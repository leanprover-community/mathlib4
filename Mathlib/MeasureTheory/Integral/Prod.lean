/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Integral.Bochner.Set
import Mathlib.MeasureTheory.Measure.Prod

/-!
# Integration with respect to the product measure

In this file we prove Fubini's theorem.

## Main results

* `MeasureTheory.integrable_prod_iff` states that a binary function is integrable iff both
  * `y â†¦ f (x, y)` is integrable for almost every `x`, and
  * the function `x â†¦ âˆ« â€–f (x, y)â€– dy` is integrable.
* `MeasureTheory.integral_prod`: Fubini's theorem. It states that for an integrable function
  `Î± Ã— Î² â†’ E` (where `E` is a second countable Banach space) we have
  `âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼`. This theorem has the same variants as
  Tonelli's theorem (see `MeasureTheory.lintegral_prod`). The lemma
  `MeasureTheory.Integrable.integral_prod_right` states that the inner integral of the right-hand
  side is integrable.
* `MeasureTheory.integral_integral_swap_of_hasCompactSupport`: a version of Fubini theorem for
  continuous functions with compact support, which does not assume that the measures are Ïƒ-finite
  contrary to all the usual versions of Fubini.

## Tags

product measure, Fubini's theorem, Fubini-Tonelli theorem
-/


noncomputable section

open scoped Topology ENNReal MeasureTheory

open Set Function Real ENNReal

open MeasureTheory MeasurableSpace MeasureTheory.Measure

open TopologicalSpace

open Filter hiding prod_eq map

variable {Î± Î² E : Type*} [MeasurableSpace Î±] [MeasurableSpace Î²] {Î¼ : Measure Î±} {Î½ : Measure Î²}
variable [NormedAddCommGroup E]

/-! ### Measurability

Before we define the product measure, we can talk about the measurability of operations on binary
functions. We show that if `f` is a binary measurable function, then the function that integrates
along one of the variables (using either the Lebesgue or Bochner integral) is measurable.
-/


theorem measurableSet_integrable [SFinite Î½] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
    (hf : StronglyMeasurable (uncurry f)) : MeasurableSet {x | Integrable (f x) Î½} := by
  simp_rw [Integrable, hf.of_uncurry_left.aestronglyMeasurable, true_and]
  exact measurableSet_lt (Measurable.lintegral_prod_right hf.enorm) measurable_const

section

variable [NormedSpace â„ E]

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable.
  This version has `f` in curried form. -/
theorem MeasureTheory.StronglyMeasurable.integral_prod_right [SFinite Î½] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
    (hf : StronglyMeasurable (uncurry f)) : StronglyMeasurable fun x => âˆ« y, f x y âˆ‚Î½ := by
  classical
  by_cases hE : CompleteSpace E; swap; Â· simp [integral, hE, stronglyMeasurable_const]
  borelize E
  haveI : SeparableSpace (range (uncurry f) âˆª {0} : Set E) :=
    hf.separableSpace_range_union_singleton
  let s : â„• â†’ SimpleFunc (Î± Ã— Î²) E :=
    SimpleFunc.approxOn _ hf.measurable (range (uncurry f) âˆª {0}) 0 (by simp)
  let s' : â„• â†’ Î± â†’ SimpleFunc Î² E := fun n x => (s n).comp (Prod.mk x) measurable_prodMk_left
  let f' : â„• â†’ Î± â†’ E := fun n => {x | Integrable (f x) Î½}.indicator fun x => (s' n x).integral Î½
  have hf' : âˆ€ n, StronglyMeasurable (f' n) := by
    intro n; refine StronglyMeasurable.indicator ?_ (measurableSet_integrable hf)
    have : âˆ€ x, ((s' n x).range.filter fun x => x â‰  0) âŠ† (s n).range := by
      intro x; refine Finset.Subset.trans (Finset.filter_subset _ _) ?_; intro y
      simp_rw [SimpleFunc.mem_range]; rintro âŸ¨z, rflâŸ©; exact âŸ¨(x, z), rflâŸ©
    simp only [SimpleFunc.integral_eq_sum_of_subset (this _)]
    refine Finset.stronglyMeasurable_sum _ fun x _ => ?_
    refine (Measurable.ennreal_toReal ?_).stronglyMeasurable.smul_const _
    simp only [s', SimpleFunc.coe_comp, preimage_comp]
    apply measurable_measure_prodMk_left
    exact (s n).measurableSet_fiber x
  have h2f' : Tendsto f' atTop (ğ“ fun x : Î± => âˆ« y : Î², f x y âˆ‚Î½) := by
    rw [tendsto_pi_nhds]; intro x
    by_cases hfx : Integrable (f x) Î½
    Â· have (n : _) : Integrable (s' n x) Î½ := by
        apply (hfx.norm.add hfx.norm).mono' (s' n x).aestronglyMeasurable
        filter_upwards with y
        simp_rw [s', SimpleFunc.coe_comp]; exact SimpleFunc.norm_approxOn_zero_le _ _ (x, y) n
      simp only [f', hfx, SimpleFunc.integral_eq_integral _ (this _), indicator_of_mem,
        mem_setOf_eq]
      refine
        tendsto_integral_of_dominated_convergence (fun y => â€–f x yâ€– + â€–f x yâ€–)
          (fun n => (s' n x).aestronglyMeasurable) (hfx.norm.add hfx.norm) ?_ ?_
      Â· refine fun n => Eventually.of_forall fun y =>
          SimpleFunc.norm_approxOn_zero_le ?_ ?_ (x, y) n
        Â· exact hf.measurable
        Â· simp
      Â· refine Eventually.of_forall fun y => SimpleFunc.tendsto_approxOn ?_ ?_ ?_
        Â· exact hf.measurable.of_uncurry_left
        Â· simp
        apply subset_closure
        simp [-uncurry_apply_pair]
    Â· simp [f', hfx, integral_undef]
  exact stronglyMeasurable_of_tendsto _ hf' h2f'

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable. -/
theorem MeasureTheory.StronglyMeasurable.integral_prod_right' [SFinite Î½] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x => âˆ« y, f (x, y) âˆ‚Î½ := by
  rw [â† uncurry_curry f] at hf; exact hf.integral_prod_right

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable.
  This version has `f` in curried form. -/
theorem MeasureTheory.StronglyMeasurable.integral_prod_left [SFinite Î¼] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
    (hf : StronglyMeasurable (uncurry f)) : StronglyMeasurable fun y => âˆ« x, f x y âˆ‚Î¼ :=
  (hf.comp_measurable measurable_swap).integral_prod_right'

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable. -/
theorem MeasureTheory.StronglyMeasurable.integral_prod_left' [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
    (hf : StronglyMeasurable f) : StronglyMeasurable fun y => âˆ« x, f (x, y) âˆ‚Î¼ :=
  (hf.comp_measurable measurable_swap).integral_prod_right'

end

/-! ### The product measure -/


namespace MeasureTheory

namespace Measure

variable [SFinite Î½]

theorem integrable_measure_prodMk_left {s : Set (Î± Ã— Î²)} (hs : MeasurableSet s)
    (h2s : (Î¼.prod Î½) s â‰  âˆ) : Integrable (fun x => Î½.real (Prod.mk x â»Â¹' s)) Î¼ := by
  refine âŸ¨(measurable_measure_prodMk_left hs).ennreal_toReal.aemeasurable.aestronglyMeasurable, ?_âŸ©
  simp_rw [hasFiniteIntegral_iff_enorm, measureReal_def, enorm_eq_ofReal toReal_nonneg]
  convert h2s.lt_top using 1
  rw [prod_apply hs]
  apply lintegral_congr_ae
  filter_upwards [ae_measure_lt_top hs h2s] with x hx
  rw [lt_top_iff_ne_top] at hx
  simp [ofReal_toReal, hx]

@[deprecated (since := "2025-03-05")]
alias MeasureTheory.Measure.integrable_measure_prod_mk_left := integrable_measure_prodMk_left

end Measure

open Measure

end MeasureTheory

open MeasureTheory.Measure

section

nonrec theorem MeasureTheory.AEStronglyMeasurable.prod_swap {Î³ : Type*} [TopologicalSpace Î³]
    [SFinite Î¼] [SFinite Î½] {f : Î² Ã— Î± â†’ Î³} (hf : AEStronglyMeasurable f (Î½.prod Î¼)) :
    AEStronglyMeasurable (fun z : Î± Ã— Î² => f z.swap) (Î¼.prod Î½) := by
  rw [â† prod_swap] at hf
  exact hf.comp_measurable measurable_swap

theorem MeasureTheory.AEStronglyMeasurable.comp_fst {Î³} [TopologicalSpace Î³] {f : Î± â†’ Î³}
    (hf : AEStronglyMeasurable f Î¼) : AEStronglyMeasurable (fun z : Î± Ã— Î² => f z.1) (Î¼.prod Î½) :=
  hf.comp_quasiMeasurePreserving quasiMeasurePreserving_fst

theorem MeasureTheory.AEStronglyMeasurable.comp_snd {Î³} [TopologicalSpace Î³] {f : Î² â†’ Î³}
    (hf : AEStronglyMeasurable f Î½) : AEStronglyMeasurable (fun z : Î± Ã— Î² => f z.2) (Î¼.prod Î½) :=
  hf.comp_quasiMeasurePreserving quasiMeasurePreserving_snd

/-- The Bochner integral is a.e.-measurable.
  This shows that the integrand of (the right-hand-side of) Fubini's theorem is a.e.-measurable. -/
theorem MeasureTheory.AEStronglyMeasurable.integral_prod_right' [SFinite Î½] [NormedSpace â„ E]
    â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : AEStronglyMeasurable f (Î¼.prod Î½)) :
    AEStronglyMeasurable (fun x => âˆ« y, f (x, y) âˆ‚Î½) Î¼ :=
  âŸ¨fun x => âˆ« y, hf.mk f (x, y) âˆ‚Î½, hf.stronglyMeasurable_mk.integral_prod_right', by
    filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk] with _ hx using integral_congr_ae hxâŸ©

theorem MeasureTheory.AEStronglyMeasurable.prodMk_left {Î³ : Type*} [SFinite Î½] [TopologicalSpace Î³]
    {f : Î± Ã— Î² â†’ Î³} (hf : AEStronglyMeasurable f (Î¼.prod Î½)) :
    âˆ€áµ x âˆ‚Î¼, AEStronglyMeasurable (fun y => f (x, y)) Î½ := by
  filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk] with x hx
  exact âŸ¨fun y â†¦ hf.mk f (x, y),
    hf.stronglyMeasurable_mk.comp_measurable measurable_prodMk_left, hxâŸ©

@[deprecated (since := "2025-03-05")]
alias MeasureTheory.AEStronglyMeasurable.prod_mk_left :=
  MeasureTheory.AEStronglyMeasurable.prodMk_left

end

namespace MeasureTheory

variable [SFinite Î½]

/-! ### Integrability on a product -/

section

theorem integrable_swap_iff [SFinite Î¼] {f : Î± Ã— Î² â†’ E} :
    Integrable (f âˆ˜ Prod.swap) (Î½.prod Î¼) â†” Integrable f (Î¼.prod Î½) :=
  measurePreserving_swap.integrable_comp_emb MeasurableEquiv.prodComm.measurableEmbedding

theorem Integrable.swap [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½)) :
    Integrable (f âˆ˜ Prod.swap) (Î½.prod Î¼) :=
  integrable_swap_iff.2 hf

theorem hasFiniteIntegral_prod_iff â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : StronglyMeasurable f) :
    HasFiniteIntegral f (Î¼.prod Î½) â†”
      (âˆ€áµ x âˆ‚Î¼, HasFiniteIntegral (fun y => f (x, y)) Î½) âˆ§
        HasFiniteIntegral (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ := by
  simp only [hasFiniteIntegral_iff_enorm, lintegral_prod _ h1f.enorm.aemeasurable]
  have (x : _) : âˆ€áµ y âˆ‚Î½, 0 â‰¤ â€–f (x, y)â€– := by filter_upwards with y using norm_nonneg _
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
      (h1f.norm.comp_measurable measurable_prodMk_left).aestronglyMeasurable,
    enorm_eq_ofReal toReal_nonneg, ofReal_norm_eq_enorm]
  -- this fact is probably too specialized to be its own lemma
  have : âˆ€ {p q r : Prop} (_ : r â†’ p), (r â†” p âˆ§ q) â†” p â†’ (r â†” q) := fun {p q r} h1 => by
    rw [â† and_congr_right_iff, and_iff_right_of_imp h1]
  rw [this]
  Â· intro h2f; rw [lintegral_congr_ae]
    filter_upwards [h2f] with x hx
    rw [ofReal_toReal]; rw [â† lt_top_iff_ne_top]; exact hx
  Â· intro h2f; refine ae_lt_top ?_ h2f.ne; exact h1f.enorm.lintegral_prod_right'

theorem hasFiniteIntegral_prod_iff' â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : AEStronglyMeasurable f (Î¼.prod Î½)) :
    HasFiniteIntegral f (Î¼.prod Î½) â†”
      (âˆ€áµ x âˆ‚Î¼, HasFiniteIntegral (fun y => f (x, y)) Î½) âˆ§
        HasFiniteIntegral (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ := by
  rw [hasFiniteIntegral_congr h1f.ae_eq_mk,
    hasFiniteIntegral_prod_iff h1f.stronglyMeasurable_mk]
  apply and_congr
  Â· apply eventually_congr
    filter_upwards [ae_ae_of_ae_prod h1f.ae_eq_mk.symm]
    intro x hx
    exact hasFiniteIntegral_congr hx
  Â· apply hasFiniteIntegral_congr
    filter_upwards [ae_ae_of_ae_prod h1f.ae_eq_mk.symm] with _ hx using
      integral_congr_ae (EventuallyEq.fun_comp hx _)

/-- A binary function is integrable if the function `y â†¦ f (x, y)` is integrable for almost every
  `x` and the function `x â†¦ âˆ« â€–f (x, y)â€– dy` is integrable. -/
theorem integrable_prod_iff â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : AEStronglyMeasurable f (Î¼.prod Î½)) :
    Integrable f (Î¼.prod Î½) â†”
      (âˆ€áµ x âˆ‚Î¼, Integrable (fun y => f (x, y)) Î½) âˆ§ Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ := by
  simp [Integrable, h1f, hasFiniteIntegral_prod_iff', h1f.norm.integral_prod_right',
    h1f.prodMk_left]

/-- A binary function is integrable if the function `x â†¦ f (x, y)` is integrable for almost every
  `y` and the function `y â†¦ âˆ« â€–f (x, y)â€– dx` is integrable. -/
theorem integrable_prod_iff' [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
    (h1f : AEStronglyMeasurable f (Î¼.prod Î½)) :
    Integrable f (Î¼.prod Î½) â†”
      (âˆ€áµ y âˆ‚Î½, Integrable (fun x => f (x, y)) Î¼) âˆ§ Integrable (fun y => âˆ« x, â€–f (x, y)â€– âˆ‚Î¼) Î½ := by
  convert integrable_prod_iff h1f.prod_swap using 1
  rw [funext fun _ => Function.comp_apply.symm, integrable_swap_iff]

theorem Integrable.prod_left_ae [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½)) :
    âˆ€áµ y âˆ‚Î½, Integrable (fun x => f (x, y)) Î¼ :=
  ((integrable_prod_iff' hf.aestronglyMeasurable).mp hf).1

theorem Integrable.prod_right_ae [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½)) :
    âˆ€áµ x âˆ‚Î¼, Integrable (fun y => f (x, y)) Î½ :=
  hf.swap.prod_left_ae

theorem Integrable.integral_norm_prod_left â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½)) :
    Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ :=
  ((integrable_prod_iff hf.aestronglyMeasurable).mp hf).2

theorem Integrable.integral_norm_prod_right [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
    (hf : Integrable f (Î¼.prod Î½)) : Integrable (fun y => âˆ« x, â€–f (x, y)â€– âˆ‚Î¼) Î½ :=
  hf.swap.integral_norm_prod_left

omit [SFinite Î½] in
theorem Integrable.op_fst_snd {F G : Type*} [NormedAddCommGroup F] [NormedAddCommGroup G]
    {op : E â†’ F â†’ G} (hop : Continuous op.uncurry) (hop_norm : âˆƒ C, âˆ€ x y, â€–op x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–)
    {f : Î± â†’ E} {g : Î² â†’ F} (hf : Integrable f Î¼) (hg : Integrable g Î½) :
    Integrable (fun z â†¦ op (f z.1) (g z.2)) (Î¼.prod Î½) := by
  use hop.comp_aestronglyMeasurableâ‚‚ hf.1.comp_fst hg.1.comp_snd
  rcases hop_norm with âŸ¨C, hCâŸ©
  calc
    âˆ«â» z, â€–op (f z.1) (g z.2)â€–â‚‘ âˆ‚Î¼.prod Î½ â‰¤ âˆ«â» z, .ofReal C * â€–f z.1â€–â‚‘ * â€–g z.2â€–â‚‘ âˆ‚Î¼.prod Î½ := by
      gcongr with z
      simp only [enorm_eq_nnnorm, ENNReal.ofReal, â† ENNReal.coe_mul, ENNReal.coe_le_coe,
        â† NNReal.coe_le_coe, NNReal.coe_mul, coe_nnnorm]
      refine (hC _ _).trans ?_
      gcongr
      apply le_coe_toNNReal
    _ â‰¤ âˆ«â» x, âˆ«â» y, .ofReal C * â€–f xâ€–â‚‘ * â€–g yâ€–â‚‘ âˆ‚Î½ âˆ‚Î¼ := lintegral_prod_le _
    _ â‰¤ .ofReal C * (âˆ«â» x, â€–f xâ€–â‚‘ âˆ‚Î¼) * âˆ«â» y, â€–g yâ€–â‚‘ âˆ‚Î½ := by
      simp [lintegral_const_mul', lintegral_mul_const', hg.2.ne, mul_assoc]
    _ < âˆ := by apply_rules [ENNReal.mul_lt_top, hf.2, hg.2, ENNReal.ofReal_lt_top]

omit [SFinite Î½] in
theorem Integrable.smul_prod {R : Type*} [NormedRing R] [Module R E] [IsBoundedSMul R E]
    {f : Î± â†’ R} {g : Î² â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î½) :
    Integrable (fun z : Î± Ã— Î² => f z.1 â€¢ g z.2) (Î¼.prod Î½) :=
  hf.op_fst_snd continuous_smul âŸ¨1, by simpa using norm_smul_leâŸ© hg

@[deprecated (since := "2025-04-06")]
alias Integrable.prod_smul := Integrable.smul_prod

omit [SFinite Î½] in
theorem Integrable.mul_prod {L : Type*} [NormedRing L] {f : Î± â†’ L} {g : Î² â†’ L} (hf : Integrable f Î¼)
    (hg : Integrable g Î½) : Integrable (fun z : Î± Ã— Î² => f z.1 * g z.2) (Î¼.prod Î½) :=
  hf.smul_prod hg

@[deprecated (since := "2025-04-06")]
alias Integrable.prod_mul := Integrable.mul_prod

theorem IntegrableOn.swap [SFinite Î¼] {f : Î± Ã— Î² â†’ E} {s : Set Î±} {t : Set Î²}
    (hf : IntegrableOn f (s Ã—Ë¢ t) (Î¼.prod Î½)) :
    IntegrableOn (f âˆ˜ Prod.swap) (t Ã—Ë¢ s) (Î½.prod Î¼) := by
  rw [IntegrableOn, â† Measure.prod_restrict] at hf âŠ¢
  exact hf.swap

end

variable [NormedSpace â„ E]

theorem Integrable.integral_prod_left â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½)) :
    Integrable (fun x => âˆ« y, f (x, y) âˆ‚Î½) Î¼ :=
  Integrable.mono hf.integral_norm_prod_left hf.aestronglyMeasurable.integral_prod_right' <|
    Eventually.of_forall fun x =>
      (norm_integral_le_integral_norm _).trans_eq <|
        (norm_of_nonneg <|
            integral_nonneg_of_ae <|
              Eventually.of_forall fun y => (norm_nonneg (f (x, y)) :)).symm

theorem Integrable.integral_prod_right [SFinite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
    (hf : Integrable f (Î¼.prod Î½)) : Integrable (fun y => âˆ« x, f (x, y) âˆ‚Î¼) Î½ :=
  hf.swap.integral_prod_left

/-! ### The Bochner integral on a product -/

variable [SFinite Î¼]

theorem integral_prod_swap (f : Î± Ã— Î² â†’ E) :
    âˆ« z, f z.swap âˆ‚Î½.prod Î¼ = âˆ« z, f z âˆ‚Î¼.prod Î½ :=
  measurePreserving_swap.integral_comp MeasurableEquiv.prodComm.measurableEmbedding _

theorem setIntegral_prod_swap (s : Set Î±) (t : Set Î²) (f : Î± Ã— Î² â†’ E) :
    âˆ« (z : Î² Ã— Î±) in t Ã—Ë¢ s, f z.swap âˆ‚Î½.prod Î¼ = âˆ« (z : Î± Ã— Î²) in s Ã—Ë¢ t, f z âˆ‚Î¼.prod Î½ := by
  rw [â† Measure.prod_restrict, â† Measure.prod_restrict, integral_prod_swap]

variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']

/-! Some rules about the sum/difference of double integrals. They follow from `integral_add`, but
  we separate them out as separate lemmas, because they involve quite some steps. -/


/-- Integrals commute with addition inside another integral. `F` can be any function. -/
theorem integral_fn_integral_add â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (F : E â†’ E') (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ« x, F (âˆ« y, f (x, y) + g (x, y) âˆ‚Î½) âˆ‚Î¼) =
      âˆ« x, F ((âˆ« y, f (x, y) âˆ‚Î½) + âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ := by
  refine integral_congr_ae ?_
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae] with _ h2f h2g
  simp [integral_add h2f h2g]

/-- Integrals commute with subtraction inside another integral.
  `F` can be any measurable function. -/
theorem integral_fn_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (F : E â†’ E') (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ« x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î½) âˆ‚Î¼) =
      âˆ« x, F ((âˆ« y, f (x, y) âˆ‚Î½) - âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ := by
  refine integral_congr_ae ?_
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae] with _ h2f h2g
  simp [integral_sub h2f h2g]

/-- Integrals commute with subtraction inside a lower Lebesgue integral.
  `F` can be any function. -/
theorem lintegral_fn_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (F : E â†’ â„â‰¥0âˆ) (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ«â» x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î½) âˆ‚Î¼) =
      âˆ«â» x, F ((âˆ« y, f (x, y) âˆ‚Î½) - âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ := by
  refine lintegral_congr_ae ?_
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae] with _ h2f h2g
  simp [integral_sub h2f h2g]

/-- Double integrals commute with addition. -/
theorem integral_integral_add â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ« x, âˆ« y, f (x, y) + g (x, y) âˆ‚Î½ âˆ‚Î¼) = (âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼) + âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
  (integral_fn_integral_add id hf hg).trans <|
    integral_add hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with addition. This is the version with `(f + g) (x, y)`
  (instead of `f (x, y) + g (x, y)`) in the LHS. -/
theorem integral_integral_add' â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ« x, âˆ« y, (f + g) (x, y) âˆ‚Î½ âˆ‚Î¼) = (âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼) + âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
  integral_integral_add hf hg

/-- Double integrals commute with subtraction. -/
theorem integral_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ« x, âˆ« y, f (x, y) - g (x, y) âˆ‚Î½ âˆ‚Î¼) = (âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼) - âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
  (integral_fn_integral_sub id hf hg).trans <|
    integral_sub hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with subtraction. This is the version with `(f - g) (x, y)`
  (instead of `f (x, y) - g (x, y)`) in the LHS. -/
theorem integral_integral_sub' â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (hf : Integrable f (Î¼.prod Î½))
    (hg : Integrable g (Î¼.prod Î½)) :
    (âˆ« x, âˆ« y, (f - g) (x, y) âˆ‚Î½ âˆ‚Î¼) = (âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼) - âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
  integral_integral_sub hf hg

/-- The map that sends an LÂ¹-function `f : Î± Ã— Î² â†’ E` to `âˆ«âˆ«f` is continuous. -/
theorem continuous_integral_integral :
    Continuous fun f : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E => âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ := by
  rw [continuous_iff_continuousAt]; intro g
  refine
    tendsto_integral_of_L1 _ (L1.integrable_coeFn g).integral_prod_left
      (Eventually.of_forall fun h => (L1.integrable_coeFn h).integral_prod_left) ?_
  simp_rw [â† lintegral_fn_integral_sub _ (L1.integrable_coeFn _) (L1.integrable_coeFn g)]
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (fun i => zero_le _) _
  Â· exact fun i => âˆ«â» x, âˆ«â» y, â€–i (x, y) - g (x, y)â€–â‚‘ âˆ‚Î½ âˆ‚Î¼
  swap; Â· exact fun i => lintegral_mono fun x => enorm_integral_le_lintegral_enorm _
  change
    Tendsto (fun i : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E => âˆ«â» x, âˆ«â» y : Î², â€–i (x, y) - g (x, y)â€–â‚‘ âˆ‚Î½ âˆ‚Î¼) (ğ“ g)
      (ğ“ 0)
  have this (i : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E) : Measurable fun z => â€–i z - g zâ€–â‚‘ :=
    ((Lp.stronglyMeasurable i).sub (Lp.stronglyMeasurable g)).enorm
  simp_rw [â† lintegral_prod _ (this _).aemeasurable, â† L1.ofReal_norm_sub_eq_lintegral,
    â† ofReal_zero]
  refine (continuous_ofReal.tendsto 0).comp ?_
  rw [â† tendsto_iff_norm_sub_tendsto_zero]; exact tendsto_id

/-- **Fubini's Theorem**: For integrable functions on `Î± Ã— Î²`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  `integrable_prod_iff` can be useful to show that the function in question in integrable.
  `MeasureTheory.Integrable.integral_prod_right` is useful to show that the inner integral
  of the right-hand side is integrable. -/
theorem integral_prod (f : Î± Ã— Î² â†’ E) (hf : Integrable f (Î¼.prod Î½)) :
    âˆ« z, f z âˆ‚Î¼.prod Î½ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E; swap; Â· simp only [integral, dif_neg hE]
  revert f
  apply Integrable.induction
  Â· intro c s hs h2s
    simp_rw [integral_indicator hs, â† indicator_comp_right, Function.comp_def,
      integral_indicator (measurable_prodMk_left hs), setIntegral_const, integral_smul_const,
      measureReal_def,
      integral_toReal (measurable_measure_prodMk_left hs).aemeasurable
        (ae_measure_lt_top hs h2s.ne)]
    rw [prod_apply hs]
  Â· rintro f g - i_f i_g hf hg
    simp_rw [integral_add' i_f i_g, integral_integral_add' i_f i_g, hf, hg]
  Â· exact isClosed_eq continuous_integral continuous_integral_integral
  Â· rintro f g hfg - hf; convert hf using 1
    Â· exact integral_congr_ae hfg.symm
    Â· apply integral_congr_ae
      filter_upwards [ae_ae_of_ae_prod hfg] with x hfgx using integral_congr_ae (ae_eq_symm hfgx)

/-- Symmetric version of **Fubini's Theorem**: For integrable functions on `Î± Ã— Î²`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  This version has the integrals on the right-hand side in the other order. -/
theorem integral_prod_symm (f : Î± Ã— Î² â†’ E) (hf : Integrable f (Î¼.prod Î½)) :
    âˆ« z, f z âˆ‚Î¼.prod Î½ = âˆ« y, âˆ« x, f (x, y) âˆ‚Î¼ âˆ‚Î½ := by
  rw [â† integral_prod_swap f]; exact integral_prod _ hf.swap

/-- Reversed version of **Fubini's Theorem**. -/
theorem integral_integral {f : Î± â†’ Î² â†’ E} (hf : Integrable (uncurry f) (Î¼.prod Î½)) :
    âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« z, f z.1 z.2 âˆ‚Î¼.prod Î½ :=
  (integral_prod _ hf).symm

/-- Reversed version of **Fubini's Theorem** (symmetric version). -/
theorem integral_integral_symm {f : Î± â†’ Î² â†’ E} (hf : Integrable (uncurry f) (Î¼.prod Î½)) :
    âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« z, f z.2 z.1 âˆ‚Î½.prod Î¼ :=
  (integral_prod_symm _ hf.swap).symm

/-- Change the order of Bochner integration. -/
theorem integral_integral_swap â¦ƒf : Î± â†’ Î² â†’ Eâ¦„ (hf : Integrable (uncurry f) (Î¼.prod Î½)) :
    âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« y, âˆ« x, f x y âˆ‚Î¼ âˆ‚Î½ :=
  (integral_integral hf).trans (integral_prod_symm _ hf)

/-- Change the order of integration, when one of the integrals is an interval integral. -/
lemma intervalIntegral_integral_swap {a b : â„} {f : â„ â†’ Î± â†’ E}
    (h_int : Integrable (uncurry f) ((volume.restrict (Set.uIoc a b)).prod Î¼)) :
    âˆ« x in a..b, âˆ« y, f x y âˆ‚Î¼ = âˆ« y, (âˆ« x in a..b, f x y) âˆ‚Î¼ := by
  rcases le_total a b with (hab | hab)
  Â· simp_rw [intervalIntegral.integral_of_le hab]
    simp only [hab, Set.uIoc_of_le] at h_int
    exact integral_integral_swap h_int
  Â· simp_rw [intervalIntegral.integral_of_ge hab]
    simp only [hab, Set.uIoc_of_ge] at h_int
    rw [integral_integral_swap h_int, integral_neg]

/-- **Fubini's Theorem** for set integrals. -/
theorem setIntegral_prod (f : Î± Ã— Î² â†’ E) {s : Set Î±} {t : Set Î²}
    (hf : IntegrableOn f (s Ã—Ë¢ t) (Î¼.prod Î½)) :
    âˆ« z in s Ã—Ë¢ t, f z âˆ‚Î¼.prod Î½ = âˆ« x in s, âˆ« y in t, f (x, y) âˆ‚Î½ âˆ‚Î¼ := by
  simp only [â† Measure.prod_restrict s t, IntegrableOn] at hf âŠ¢
  exact integral_prod f hf

theorem integral_prod_smul {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] (f : Î± â†’ ğ•œ) (g : Î² â†’ E) :
    âˆ« z, f z.1 â€¢ g z.2 âˆ‚Î¼.prod Î½ = (âˆ« x, f x âˆ‚Î¼) â€¢ âˆ« y, g y âˆ‚Î½ := by
  by_cases hE : CompleteSpace E; swap; Â· simp [integral, hE]
  by_cases h : Integrable (fun z : Î± Ã— Î² => f z.1 â€¢ g z.2) (Î¼.prod Î½)
  Â· rw [integral_prod _ h]
    simp_rw [integral_smul, integral_smul_const]
  have H : Â¬Integrable f Î¼ âˆ¨ Â¬Integrable g Î½ := by
    contrapose! h
    exact h.1.smul_prod h.2
  rcases H with H | H <;> simp [integral_undef h, integral_undef H]

theorem integral_prod_mul {L : Type*} [RCLike L] (f : Î± â†’ L) (g : Î² â†’ L) :
    âˆ« z, f z.1 * g z.2 âˆ‚Î¼.prod Î½ = (âˆ« x, f x âˆ‚Î¼) * âˆ« y, g y âˆ‚Î½ :=
  integral_prod_smul f g

theorem setIntegral_prod_mul {L : Type*} [RCLike L] (f : Î± â†’ L) (g : Î² â†’ L) (s : Set Î±)
    (t : Set Î²) :
    âˆ« z in s Ã—Ë¢ t, f z.1 * g z.2 âˆ‚Î¼.prod Î½ = (âˆ« x in s, f x âˆ‚Î¼) * âˆ« y in t, g y âˆ‚Î½ := by
  rw [â† Measure.prod_restrict s t]
  apply integral_prod_mul

theorem integral_fun_snd (f : Î² â†’ E) : âˆ« z, f z.2 âˆ‚Î¼.prod Î½ = Î¼.real univ â€¢ âˆ« y, f y âˆ‚Î½ := by
  simpa using integral_prod_smul (1 : Î± â†’ â„) f

theorem integral_fun_fst (f : Î± â†’ E) : âˆ« z, f z.1 âˆ‚Î¼.prod Î½ = Î½.real univ â€¢ âˆ« x, f x âˆ‚Î¼ := by
  rw [â† integral_prod_swap]
  apply integral_fun_snd

section

variable {X Y : Type*}
    [TopologicalSpace X] [TopologicalSpace Y] [MeasurableSpace X] [MeasurableSpace Y]
    [OpensMeasurableSpace X] [OpensMeasurableSpace Y]

/-- A version of *Fubini theorem* for continuous functions with compact support: one may swap
the order of integration with respect to locally finite measures. One does not assume that the
measures are Ïƒ-finite, contrary to the usual Fubini theorem. -/
lemma integral_integral_swap_of_hasCompactSupport
    {f : X â†’ Y â†’ E} (hf : Continuous f.uncurry) (h'f : HasCompactSupport f.uncurry)
    {Î¼ : Measure X} {Î½ : Measure Y} [IsFiniteMeasureOnCompacts Î¼] [IsFiniteMeasureOnCompacts Î½] :
    âˆ« x, (âˆ« y, f x y âˆ‚Î½) âˆ‚Î¼ = âˆ« y, (âˆ« x, f x y âˆ‚Î¼) âˆ‚Î½ := by
  let U := Prod.fst '' (tsupport f.uncurry)
  have : Fact (Î¼ U < âˆ) := âŸ¨(IsCompact.image h'f continuous_fst).measure_lt_topâŸ©
  let V := Prod.snd '' (tsupport f.uncurry)
  have : Fact (Î½ V < âˆ) := âŸ¨(IsCompact.image h'f continuous_snd).measure_lt_topâŸ©
  calc
  âˆ« x, (âˆ« y, f x y âˆ‚Î½) âˆ‚Î¼ = âˆ« x, (âˆ« y in V, f x y âˆ‚Î½) âˆ‚Î¼ := by
    congr 1 with x
    apply (setIntegral_eq_integral_of_forall_compl_eq_zero (fun y hy â†¦ ?_)).symm
    contrapose! hy
    have : (x, y) âˆˆ Function.support f.uncurry := hy
    exact mem_image_of_mem _ (subset_tsupport _ this)
  _ = âˆ« x in U, (âˆ« y in V, f x y âˆ‚Î½) âˆ‚Î¼ := by
    apply (setIntegral_eq_integral_of_forall_compl_eq_zero (fun x hx â†¦ ?_)).symm
    have : âˆ€ y, f x y = 0 := by
      intro y
      contrapose! hx
      have : (x, y) âˆˆ Function.support f.uncurry := hx
      exact mem_image_of_mem _ (subset_tsupport _ this)
    simp [this]
  _ = âˆ« y in V, (âˆ« x in U, f x y âˆ‚Î¼) âˆ‚Î½ := by
    apply integral_integral_swap
    apply (integrableOn_iff_integrable_of_support_subset (subset_tsupport f.uncurry)).mp
    refine âŸ¨(h'f.stronglyMeasurable_of_prod hf).aestronglyMeasurable, ?_âŸ©
    obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€ p, â€–f.uncurry pâ€– â‰¤ C := hf.bounded_above_of_compact_support h'f
    exact .of_bounded (C := C) (.of_forall hC)
  _ = âˆ« y, (âˆ« x in U, f x y âˆ‚Î¼) âˆ‚Î½ := by
    apply setIntegral_eq_integral_of_forall_compl_eq_zero (fun y hy â†¦ ?_)
    have : âˆ€ x, f x y = 0 := by
      intro x
      contrapose! hy
      have : (x, y) âˆˆ Function.support f.uncurry := hy
      exact mem_image_of_mem _ (subset_tsupport _ this)
    simp [this]
  _ = âˆ« y, (âˆ« x, f x y âˆ‚Î¼) âˆ‚Î½ := by
    congr 1 with y
    apply setIntegral_eq_integral_of_forall_compl_eq_zero (fun x hx â†¦ ?_)
    contrapose! hx
    have : (x, y) âˆˆ Function.support f.uncurry := hx
    exact mem_image_of_mem _ (subset_tsupport _ this)

end

end MeasureTheory
