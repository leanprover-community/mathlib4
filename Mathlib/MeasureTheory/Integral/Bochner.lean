/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, SÃ©bastien GouÃ«zel, RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Integral.SetToL1

#align_import measure_theory.integral.bochner from "leanprover-community/mathlib"@"48fb5b5280e7c81672afc9524185ae994553ebf4"

/-!
# Bochner integral

The Bochner integral extends the definition of the Lebesgue integral to functions that map from a
measure space into a Banach space (complete normed vector space). It is constructed here by
extending the integral on simple functions.

## Main definitions

The Bochner integral is defined through the extension process described in the file `SetToL1`,
which follows these steps:

1. Define the integral of the indicator of a set. This is `weightedSMul Î¼ s x = (Î¼ s).toReal * x`.
  `weightedSMul Î¼` is shown to be linear in the value `x` and `DominatedFinMeasAdditive`
  (defined in the file `SetToL1`) with respect to the set `s`.

2. Define the integral on simple functions of the type `SimpleFunc Î± E` (notation : `Î± â†’â‚› E`)
  where `E` is a real normed space. (See `SimpleFunc.integral` for details.)

3. Transfer this definition to define the integral on `L1.simpleFunc Î± E` (notation :
  `Î± â†’â‚â‚›[Î¼] E`), see `L1.simpleFunc.integral`. Show that this integral is a continuous linear
  map from `Î± â†’â‚â‚›[Î¼] E` to `E`.

4. Define the Bochner integral on L1 functions by extending the integral on integrable simple
  functions `Î± â†’â‚â‚›[Î¼] E` using `ContinuousLinearMap.extend` and the fact that the embedding of
  `Î± â†’â‚â‚›[Î¼] E` into `Î± â†’â‚[Î¼] E` is dense.

5. Define the Bochner integral on functions as the Bochner integral of its equivalence class in L1
  space, if it is in L1, and 0 otherwise.

The result of that construction is `âˆ« a, f a âˆ‚Î¼`, which is definitionally equal to
`setToFun (dominatedFinMeasAdditive_weightedSMul Î¼) f`. Some basic properties of the integral
(like linearity) are particular cases of the properties of `setToFun` (which are described in the
file `SetToL1`).

## Main statements

1. Basic properties of the Bochner integral on functions of type `Î± â†’ E`, where `Î±` is a measure
   space and `E` is a real normed space.

  * `integral_zero`                  : `âˆ« 0 âˆ‚Î¼ = 0`
  * `integral_add`                   : `âˆ« x, f x + g x âˆ‚Î¼ = âˆ« x, f âˆ‚Î¼ + âˆ« x, g x âˆ‚Î¼`
  * `integral_neg`                   : `âˆ« x, - f x âˆ‚Î¼ = - âˆ« x, f x âˆ‚Î¼`
  * `integral_sub`                   : `âˆ« x, f x - g x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ - âˆ« x, g x âˆ‚Î¼`
  * `integral_smul`                  : `âˆ« x, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x, f x âˆ‚Î¼`
  * `integral_congr_ae`              : `f =áµ[Î¼] g â†’ âˆ« x, f x âˆ‚Î¼ = âˆ« x, g x âˆ‚Î¼`
  * `norm_integral_le_integral_norm` : `â€–âˆ« x, f x âˆ‚Î¼â€– â‰¤ âˆ« x, â€–f xâ€– âˆ‚Î¼`

2. Basic properties of the Bochner integral on functions of type `Î± â†’ â„`, where `Î±` is a measure
  space.

  * `integral_nonneg_of_ae` : `0 â‰¤áµ[Î¼] f â†’ 0 â‰¤ âˆ« x, f x âˆ‚Î¼`
  * `integral_nonpos_of_ae` : `f â‰¤áµ[Î¼] 0 â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ 0`
  * `integral_mono_ae`      : `f â‰¤áµ[Î¼] g â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ âˆ« x, g x âˆ‚Î¼`
  * `integral_nonneg`       : `0 â‰¤ f â†’ 0 â‰¤ âˆ« x, f x âˆ‚Î¼`
  * `integral_nonpos`       : `f â‰¤ 0 â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ 0`
  * `integral_mono`         : `f â‰¤áµ[Î¼] g â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ âˆ« x, g x âˆ‚Î¼`

3. Propositions connecting the Bochner integral with the integral on `â„â‰¥0âˆ`-valued functions,
   which is called `lintegral` and has the notation `âˆ«â»`.

  * `integral_eq_lintegral_pos_part_sub_lintegral_neg_part` :
    `âˆ« x, f x âˆ‚Î¼ = âˆ«â» x, fâº x âˆ‚Î¼ - âˆ«â» x, fâ» x âˆ‚Î¼`,
    where `fâº` is the positive part of `f` and `fâ»` is the negative part of `f`.
  * `integral_eq_lintegral_of_nonneg_ae`          : `0 â‰¤áµ[Î¼] f â†’ âˆ« x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼`

4. (In the file `DominatedConvergence`)
  `tendsto_integral_of_dominated_convergence` : the Lebesgue dominated convergence theorem

5. (In the file `SetIntegral`) integration commutes with continuous linear maps.

  * `ContinuousLinearMap.integral_comp_comm`
  * `LinearIsometry.integral_comp_comm`


## Notes

Some tips on how to prove a proposition if the API for the Bochner integral is not enough so that
you need to unfold the definition of the Bochner integral and go back to simple functions.

One method is to use the theorem `Integrable.induction` in the file `SimpleFuncDenseLp` (or one
of the related results, like `Lp.induction` for functions in `Lp`), which allows you to prove
something for an arbitrary integrable function.

Another method is using the following steps.
See `integral_eq_lintegral_pos_part_sub_lintegral_neg_part` for a complicated example, which proves
that `âˆ« f = âˆ«â» fâº - âˆ«â» fâ»`, with the first integral sign being the Bochner integral of a real-valued
function `f : Î± â†’ â„`, and second and third integral sign being the integral on `â„â‰¥0âˆ`-valued
functions (called `lintegral`). The proof of `integral_eq_lintegral_pos_part_sub_lintegral_neg_part`
is scattered in sections with the name `posPart`.

Here are the usual steps of proving that a property `p`, say `âˆ« f = âˆ«â» fâº - âˆ«â» fâ»`, holds for all
functions :

1. First go to the `LÂ¹` space.

   For example, if you see `ENNReal.toReal (âˆ«â» a, ENNReal.ofReal <| â€–f aâ€–)`, that is the norm of
   `f` in `LÂ¹` space. Rewrite using `L1.norm_of_fun_eq_lintegral_norm`.

2. Show that the set `{f âˆˆ LÂ¹ | âˆ« f = âˆ«â» fâº - âˆ«â» fâ»}` is closed in `LÂ¹` using `isClosed_eq`.

3. Show that the property holds for all simple functions `s` in `LÂ¹` space.

   Typically, you need to convert various notions to their `SimpleFunc` counterpart, using lemmas
   like `L1.integral_coe_eq_integral`.

4. Since simple functions are dense in `LÂ¹`,
```
univ = closure {s simple}
     = closure {s simple | âˆ« s = âˆ«â» sâº - âˆ«â» sâ»} : the property holds for all simple functions
     âŠ† closure {f | âˆ« f = âˆ«â» fâº - âˆ«â» fâ»}
     = {f | âˆ« f = âˆ«â» fâº - âˆ«â» fâ»} : closure of a closed set is itself
```
Use `isClosed_property` or `DenseRange.induction_on` for this argument.

## Notations

* `Î± â†’â‚› E`  : simple functions (defined in `MeasureTheory/Integration`)
* `Î± â†’â‚[Î¼] E` : functions in L1 space, i.e., equivalence classes of integrable functions (defined in
                `MeasureTheory/LpSpace`)
* `Î± â†’â‚â‚›[Î¼] E` : simple functions in L1 space, i.e., equivalence classes of integrable simple
                 functions (defined in `MeasureTheory/SimpleFuncDense`)
* `âˆ« a, f a âˆ‚Î¼` : integral of `f` with respect to a measure `Î¼`
* `âˆ« a, f a` : integral of `f` with respect to `volume`, the default measure on the ambient type

We also define notations for integral on a set, which are described in the file
`MeasureTheory/SetIntegral`.

Note : `â‚›` is typed using `\_s`. Sometimes it shows as a box if the font is missing.

## Tags

Bochner integral, simple function, function space, Lebesgue dominated convergence theorem

-/


assert_not_exists Differentiable

noncomputable section

open scoped Topology NNReal ENNReal MeasureTheory

open Set Filter TopologicalSpace ENNReal EMetric

namespace MeasureTheory

variable {Î± E F ğ•œ : Type*}

section WeightedSMul

open ContinuousLinearMap

variable [NormedAddCommGroup F] [NormedSpace â„ F] {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- Given a set `s`, return the continuous linear map `fun x => (Î¼ s).toReal â€¢ x`. The extension
of that set function through `setToL1` gives the Bochner integral of L1 functions. -/
def weightedSMul {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (s : Set Î±) : F â†’L[â„] F :=
  (Î¼ s).toReal â€¢ ContinuousLinearMap.id â„ F
#align measure_theory.weighted_smul MeasureTheory.weightedSMul

theorem weightedSMul_apply {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (s : Set Î±) (x : F) :
    weightedSMul Î¼ s x = (Î¼ s).toReal â€¢ x := by simp [weightedSMul]
#align measure_theory.weighted_smul_apply MeasureTheory.weightedSMul_apply

@[simp]
theorem weightedSMul_zero_measure {m : MeasurableSpace Î±} :
    weightedSMul (0 : Measure Î±) = (0 : Set Î± â†’ F â†’L[â„] F) := by ext1; simp [weightedSMul]
#align measure_theory.weighted_smul_zero_measure MeasureTheory.weightedSMul_zero_measure

@[simp]
theorem weightedSMul_empty {m : MeasurableSpace Î±} (Î¼ : Measure Î±) :
    weightedSMul Î¼ âˆ… = (0 : F â†’L[â„] F) := by ext1 x; rw [weightedSMul_apply]; simp
#align measure_theory.weighted_smul_empty MeasureTheory.weightedSMul_empty

theorem weightedSMul_add_measure {m : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±) {s : Set Î±}
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ½s : Î½ s â‰  âˆ) :
    (weightedSMul (Î¼ + Î½) s : F â†’L[â„] F) = weightedSMul Î¼ s + weightedSMul Î½ s := by
  ext1 x
  push_cast
  simp_rw [Pi.add_apply, weightedSMul_apply]
  push_cast
  rw [Pi.add_apply, ENNReal.toReal_add hÎ¼s hÎ½s, add_smul]
#align measure_theory.weighted_smul_add_measure MeasureTheory.weightedSMul_add_measure

theorem weightedSMul_smul_measure {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (c : â„â‰¥0âˆ) {s : Set Î±} :
    (weightedSMul (c â€¢ Î¼) s : F â†’L[â„] F) = c.toReal â€¢ weightedSMul Î¼ s := by
  ext1 x
  push_cast
  simp_rw [Pi.smul_apply, weightedSMul_apply]
  push_cast
  simp_rw [Pi.smul_apply, smul_eq_mul, toReal_mul, smul_smul]
#align measure_theory.weighted_smul_smul_measure MeasureTheory.weightedSMul_smul_measure

theorem weightedSMul_congr (s t : Set Î±) (hst : Î¼ s = Î¼ t) :
    (weightedSMul Î¼ s : F â†’L[â„] F) = weightedSMul Î¼ t := by
  ext1 x; simp_rw [weightedSMul_apply]; congr 2
#align measure_theory.weighted_smul_congr MeasureTheory.weightedSMul_congr

theorem weightedSMul_null {s : Set Î±} (h_zero : Î¼ s = 0) : (weightedSMul Î¼ s : F â†’L[â„] F) = 0 := by
  ext1 x; rw [weightedSMul_apply, h_zero]; simp
#align measure_theory.weighted_smul_null MeasureTheory.weightedSMul_null

theorem weightedSMul_union' (s t : Set Î±) (ht : MeasurableSet t) (hs_finite : Î¼ s â‰  âˆ)
    (ht_finite : Î¼ t â‰  âˆ) (h_inter : s âˆ© t = âˆ…) :
    (weightedSMul Î¼ (s âˆª t) : F â†’L[â„] F) = weightedSMul Î¼ s + weightedSMul Î¼ t := by
  ext1 x
  simp_rw [add_apply, weightedSMul_apply,
    measure_union (Set.disjoint_iff_inter_eq_empty.mpr h_inter) ht,
    ENNReal.toReal_add hs_finite ht_finite, add_smul]
#align measure_theory.weighted_smul_union' MeasureTheory.weightedSMul_union'

@[nolint unusedArguments]
theorem weightedSMul_union (s t : Set Î±) (_hs : MeasurableSet s) (ht : MeasurableSet t)
    (hs_finite : Î¼ s â‰  âˆ) (ht_finite : Î¼ t â‰  âˆ) (h_inter : s âˆ© t = âˆ…) :
    (weightedSMul Î¼ (s âˆª t) : F â†’L[â„] F) = weightedSMul Î¼ s + weightedSMul Î¼ t :=
  weightedSMul_union' s t ht hs_finite ht_finite h_inter
#align measure_theory.weighted_smul_union MeasureTheory.weightedSMul_union

theorem weightedSMul_smul [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F] (c : ğ•œ)
    (s : Set Î±) (x : F) : weightedSMul Î¼ s (c â€¢ x) = c â€¢ weightedSMul Î¼ s x := by
  simp_rw [weightedSMul_apply, smul_comm]
#align measure_theory.weighted_smul_smul MeasureTheory.weightedSMul_smul

theorem norm_weightedSMul_le (s : Set Î±) : â€–(weightedSMul Î¼ s : F â†’L[â„] F)â€– â‰¤ (Î¼ s).toReal :=
  calc
    â€–(weightedSMul Î¼ s : F â†’L[â„] F)â€– = â€–(Î¼ s).toRealâ€– * â€–ContinuousLinearMap.id â„ Fâ€– :=
      norm_smul (Î¼ s).toReal (ContinuousLinearMap.id â„ F)
    _ â‰¤ â€–(Î¼ s).toRealâ€– :=
      ((mul_le_mul_of_nonneg_left norm_id_le (norm_nonneg _)).trans (mul_one _).le)
    _ = abs (Î¼ s).toReal := Real.norm_eq_abs _
    _ = (Î¼ s).toReal := abs_eq_self.mpr ENNReal.toReal_nonneg
#align measure_theory.norm_weighted_smul_le MeasureTheory.norm_weightedSMul_le

theorem dominatedFinMeasAdditive_weightedSMul {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) :
    DominatedFinMeasAdditive Î¼ (weightedSMul Î¼ : Set Î± â†’ F â†’L[â„] F) 1 :=
  âŸ¨weightedSMul_union, fun s _ _ => (norm_weightedSMul_le s).trans (one_mul _).symm.leâŸ©
#align measure_theory.dominated_fin_meas_additive_weighted_smul MeasureTheory.dominatedFinMeasAdditive_weightedSMul

theorem weightedSMul_nonneg (s : Set Î±) (x : â„) (hx : 0 â‰¤ x) : 0 â‰¤ weightedSMul Î¼ s x := by
  simp only [weightedSMul, Algebra.id.smul_eq_mul, coe_smul', _root_.id, coe_id', Pi.smul_apply]
  exact mul_nonneg toReal_nonneg hx
#align measure_theory.weighted_smul_nonneg MeasureTheory.weightedSMul_nonneg

end WeightedSMul

local infixr:25 " â†’â‚› " => SimpleFunc

namespace SimpleFunc

section PosPart

variable [LinearOrder E] [Zero E] [MeasurableSpace Î±]

/-- Positive part of a simple function. -/
def posPart (f : Î± â†’â‚› E) : Î± â†’â‚› E :=
  f.map fun b => max b 0
#align measure_theory.simple_func.pos_part MeasureTheory.SimpleFunc.posPart

/-- Negative part of a simple function. -/
def negPart [Neg E] (f : Î± â†’â‚› E) : Î± â†’â‚› E :=
  posPart (-f)
#align measure_theory.simple_func.neg_part MeasureTheory.SimpleFunc.negPart

theorem posPart_map_norm (f : Î± â†’â‚› â„) : (posPart f).map norm = posPart f := by
  ext; rw [map_apply, Real.norm_eq_abs, abs_of_nonneg]; exact le_max_right _ _
#align measure_theory.simple_func.pos_part_map_norm MeasureTheory.SimpleFunc.posPart_map_norm

theorem negPart_map_norm (f : Î± â†’â‚› â„) : (negPart f).map norm = negPart f := by
  rw [negPart]; exact posPart_map_norm _
#align measure_theory.simple_func.neg_part_map_norm MeasureTheory.SimpleFunc.negPart_map_norm

theorem posPart_sub_negPart (f : Î± â†’â‚› â„) : f.posPart - f.negPart = f := by
  simp only [posPart, negPart]
  ext a
  rw [coe_sub]
  exact max_zero_sub_eq_self (f a)
#align measure_theory.simple_func.pos_part_sub_neg_part MeasureTheory.SimpleFunc.posPart_sub_negPart

end PosPart

section Integral

/-!
### The Bochner integral of simple functions

Define the Bochner integral of simple functions of the type `Î± â†’â‚› Î²` where `Î²` is a normed group,
and prove basic property of this integral.
-/


open Finset

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ F] {p : â„â‰¥0âˆ} {G F' : Type*}
  [NormedAddCommGroup G] [NormedAddCommGroup F'] [NormedSpace â„ F'] {m : MeasurableSpace Î±}
  {Î¼ : Measure Î±}

/-- Bochner integral of simple functions whose codomain is a real `NormedSpace`.
This is equal to `âˆ‘ x âˆˆ f.range, (Î¼ (f â»Â¹' {x})).toReal â€¢ x` (see `integral_eq`). -/
def integral {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’â‚› F) : F :=
  f.setToSimpleFunc (weightedSMul Î¼)
#align measure_theory.simple_func.integral MeasureTheory.SimpleFunc.integral

theorem integral_def {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’â‚› F) :
    f.integral Î¼ = f.setToSimpleFunc (weightedSMul Î¼) := rfl
#align measure_theory.simple_func.integral_def MeasureTheory.SimpleFunc.integral_def

theorem integral_eq {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’â‚› F) :
    f.integral Î¼ = âˆ‘ x âˆˆ f.range, (Î¼ (f â»Â¹' {x})).toReal â€¢ x := by
  simp [integral, setToSimpleFunc, weightedSMul_apply]
#align measure_theory.simple_func.integral_eq MeasureTheory.SimpleFunc.integral_eq

theorem integral_eq_sum_filter [DecidablePred fun x : F => x â‰  0] {m : MeasurableSpace Î±}
    (f : Î± â†’â‚› F) (Î¼ : Measure Î±) :
    f.integral Î¼ = âˆ‘ x âˆˆ f.range.filter fun x => x â‰  0, (Î¼ (f â»Â¹' {x})).toReal â€¢ x := by
  rw [integral_def, setToSimpleFunc_eq_sum_filter]; simp_rw [weightedSMul_apply]; congr
#align measure_theory.simple_func.integral_eq_sum_filter MeasureTheory.SimpleFunc.integral_eq_sum_filter

/-- The Bochner integral is equal to a sum over any set that includes `f.range` (except `0`). -/
theorem integral_eq_sum_of_subset [DecidablePred fun x : F => x â‰  0] {f : Î± â†’â‚› F} {s : Finset F}
    (hs : (f.range.filter fun x => x â‰  0) âŠ† s) :
    f.integral Î¼ = âˆ‘ x âˆˆ s, (Î¼ (f â»Â¹' {x})).toReal â€¢ x := by
  rw [SimpleFunc.integral_eq_sum_filter, Finset.sum_subset hs]
  rintro x - hx; rw [Finset.mem_filter, not_and_or, Ne, Classical.not_not] at hx
  -- Porting note: reordered for clarity
  rcases hx.symm with (rfl | hx)
  Â· simp
  rw [SimpleFunc.mem_range] at hx
  -- Porting note: added
  simp only [Set.mem_range, not_exists] at hx
  rw [preimage_eq_empty] <;> simp [Set.disjoint_singleton_left, hx]
#align measure_theory.simple_func.integral_eq_sum_of_subset MeasureTheory.SimpleFunc.integral_eq_sum_of_subset

@[simp]
theorem integral_const {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (y : F) :
    (const Î± y).integral Î¼ = (Î¼ univ).toReal â€¢ y := by
  classical
  calc
    (const Î± y).integral Î¼ = âˆ‘ z âˆˆ {y}, (Î¼ (const Î± y â»Â¹' {z})).toReal â€¢ z :=
      integral_eq_sum_of_subset <| (filter_subset _ _).trans (range_const_subset _ _)
    _ = (Î¼ univ).toReal â€¢ y := by simp [Set.preimage] -- Porting note: added `Set.preimage`
#align measure_theory.simple_func.integral_const MeasureTheory.SimpleFunc.integral_const

@[simp]
theorem integral_piecewise_zero {m : MeasurableSpace Î±} (f : Î± â†’â‚› F) (Î¼ : Measure Î±) {s : Set Î±}
    (hs : MeasurableSet s) : (piecewise s hs f 0).integral Î¼ = f.integral (Î¼.restrict s) := by
  classical
  refine (integral_eq_sum_of_subset ?_).trans
      ((sum_congr rfl fun y hy => ?_).trans (integral_eq_sum_filter _ _).symm)
  Â· intro y hy
    simp only [mem_filter, mem_range, coe_piecewise, coe_zero, piecewise_eq_indicator,
      mem_range_indicator] at *
    rcases hy with âŸ¨âŸ¨rfl, -âŸ© | âŸ¨x, -, rflâŸ©, hâ‚€âŸ©
    exacts [(hâ‚€ rfl).elim, âŸ¨Set.mem_range_self _, hâ‚€âŸ©]
  Â· dsimp
    rw [Set.piecewise_eq_indicator, indicator_preimage_of_not_mem,
      Measure.restrict_apply (f.measurableSet_preimage _)]
    exact fun hâ‚€ => (mem_filter.1 hy).2 (Eq.symm hâ‚€)
#align measure_theory.simple_func.integral_piecewise_zero MeasureTheory.SimpleFunc.integral_piecewise_zero

/-- Calculate the integral of `g âˆ˜ f : Î± â†’â‚› F`, where `f` is an integrable function from `Î±` to `E`
    and `g` is a function from `E` to `F`. We require `g 0 = 0` so that `g âˆ˜ f` is integrable. -/
theorem map_integral (f : Î± â†’â‚› E) (g : E â†’ F) (hf : Integrable f Î¼) (hg : g 0 = 0) :
    (f.map g).integral Î¼ = âˆ‘ x âˆˆ f.range, ENNReal.toReal (Î¼ (f â»Â¹' {x})) â€¢ g x :=
  map_setToSimpleFunc _ weightedSMul_union hf hg
#align measure_theory.simple_func.map_integral MeasureTheory.SimpleFunc.map_integral

/-- `SimpleFunc.integral` and `SimpleFunc.lintegral` agree when the integrand has type
    `Î± â†’â‚› â„â‰¥0âˆ`. But since `â„â‰¥0âˆ` is not a `NormedSpace`, we need some form of coercion.
    See `integral_eq_lintegral` for a simpler version. -/
theorem integral_eq_lintegral' {f : Î± â†’â‚› E} {g : E â†’ â„â‰¥0âˆ} (hf : Integrable f Î¼) (hg0 : g 0 = 0)
    (ht : âˆ€ b, g b â‰  âˆ) :
    (f.map (ENNReal.toReal âˆ˜ g)).integral Î¼ = ENNReal.toReal (âˆ«â» a, g (f a) âˆ‚Î¼) := by
  have hf' : f.FinMeasSupp Î¼ := integrable_iff_finMeasSupp.1 hf
  simp only [â† map_apply g f, lintegral_eq_lintegral]
  rw [map_integral f _ hf, map_lintegral, ENNReal.toReal_sum]
  Â· refine Finset.sum_congr rfl fun b _ => ?_
    -- Porting note: added `Function.comp_apply`
    rw [smul_eq_mul, toReal_mul, mul_comm, Function.comp_apply]
  Â· rintro a -
    by_cases a0 : a = 0
    Â· rw [a0, hg0, zero_mul]; exact WithTop.zero_ne_top
    Â· apply mul_ne_top (ht a) (hf'.meas_preimage_singleton_ne_zero a0).ne
  Â· simp [hg0]
#align measure_theory.simple_func.integral_eq_lintegral' MeasureTheory.SimpleFunc.integral_eq_lintegral'

variable [NormedField ğ•œ] [NormedSpace ğ•œ E] [NormedSpace â„ E] [SMulCommClass â„ ğ•œ E]

theorem integral_congr {f g : Î± â†’â‚› E} (hf : Integrable f Î¼) (h : f =áµ[Î¼] g) :
    f.integral Î¼ = g.integral Î¼ :=
  setToSimpleFunc_congr (weightedSMul Î¼) (fun _ _ => weightedSMul_null) weightedSMul_union hf h
#align measure_theory.simple_func.integral_congr MeasureTheory.SimpleFunc.integral_congr

/-- `SimpleFunc.bintegral` and `SimpleFunc.integral` agree when the integrand has type
    `Î± â†’â‚› â„â‰¥0âˆ`. But since `â„â‰¥0âˆ` is not a `NormedSpace`, we need some form of coercion. -/
theorem integral_eq_lintegral {f : Î± â†’â‚› â„} (hf : Integrable f Î¼) (h_pos : 0 â‰¤áµ[Î¼] f) :
    f.integral Î¼ = ENNReal.toReal (âˆ«â» a, ENNReal.ofReal (f a) âˆ‚Î¼) := by
  have : f =áµ[Î¼] f.map (ENNReal.toReal âˆ˜ ENNReal.ofReal) :=
    h_pos.mono fun a h => (ENNReal.toReal_ofReal h).symm
  rw [â† integral_eq_lintegral' hf]
  exacts [integral_congr hf this, ENNReal.ofReal_zero, fun b => ENNReal.ofReal_ne_top]
#align measure_theory.simple_func.integral_eq_lintegral MeasureTheory.SimpleFunc.integral_eq_lintegral

theorem integral_add {f g : Î± â†’â‚› E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    integral Î¼ (f + g) = integral Î¼ f + integral Î¼ g :=
  setToSimpleFunc_add _ weightedSMul_union hf hg
#align measure_theory.simple_func.integral_add MeasureTheory.SimpleFunc.integral_add

theorem integral_neg {f : Î± â†’â‚› E} (hf : Integrable f Î¼) : integral Î¼ (-f) = -integral Î¼ f :=
  setToSimpleFunc_neg _ weightedSMul_union hf
#align measure_theory.simple_func.integral_neg MeasureTheory.SimpleFunc.integral_neg

theorem integral_sub {f g : Î± â†’â‚› E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    integral Î¼ (f - g) = integral Î¼ f - integral Î¼ g :=
  setToSimpleFunc_sub _ weightedSMul_union hf hg
#align measure_theory.simple_func.integral_sub MeasureTheory.SimpleFunc.integral_sub

theorem integral_smul (c : ğ•œ) {f : Î± â†’â‚› E} (hf : Integrable f Î¼) :
    integral Î¼ (c â€¢ f) = c â€¢ integral Î¼ f :=
  setToSimpleFunc_smul _ weightedSMul_union weightedSMul_smul c hf
#align measure_theory.simple_func.integral_smul MeasureTheory.SimpleFunc.integral_smul

theorem norm_setToSimpleFunc_le_integral_norm (T : Set Î± â†’ E â†’L[â„] F) {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * (Î¼ s).toReal) {f : Î± â†’â‚› E}
    (hf : Integrable f Î¼) : â€–f.setToSimpleFunc Tâ€– â‰¤ C * (f.map norm).integral Î¼ :=
  calc
    â€–f.setToSimpleFunc Tâ€– â‰¤ C * âˆ‘ x âˆˆ f.range, ENNReal.toReal (Î¼ (f â»Â¹' {x})) * â€–xâ€– :=
      norm_setToSimpleFunc_le_sum_mul_norm_of_integrable T hT_norm f hf
    _ = C * (f.map norm).integral Î¼ := by
      rw [map_integral f norm hf norm_zero]; simp_rw [smul_eq_mul]
#align measure_theory.simple_func.norm_set_to_simple_func_le_integral_norm MeasureTheory.SimpleFunc.norm_setToSimpleFunc_le_integral_norm

theorem norm_integral_le_integral_norm (f : Î± â†’â‚› E) (hf : Integrable f Î¼) :
    â€–f.integral Î¼â€– â‰¤ (f.map norm).integral Î¼ := by
  refine (norm_setToSimpleFunc_le_integral_norm _ (fun s _ _ => ?_) hf).trans (one_mul _).le
  exact (norm_weightedSMul_le s).trans (one_mul _).symm.le
#align measure_theory.simple_func.norm_integral_le_integral_norm MeasureTheory.SimpleFunc.norm_integral_le_integral_norm

theorem integral_add_measure {Î½} (f : Î± â†’â‚› E) (hf : Integrable f (Î¼ + Î½)) :
    f.integral (Î¼ + Î½) = f.integral Î¼ + f.integral Î½ := by
  simp_rw [integral_def]
  refine setToSimpleFunc_add_left'
    (weightedSMul Î¼) (weightedSMul Î½) (weightedSMul (Î¼ + Î½)) (fun s _ hÎ¼Î½s => ?_) hf
  rw [lt_top_iff_ne_top, Measure.coe_add, Pi.add_apply, ENNReal.add_ne_top] at hÎ¼Î½s
  rw [weightedSMul_add_measure _ _ hÎ¼Î½s.1 hÎ¼Î½s.2]
#align measure_theory.simple_func.integral_add_measure MeasureTheory.SimpleFunc.integral_add_measure

end Integral

end SimpleFunc

namespace L1
set_option linter.uppercaseLean3 false -- `L1`

open AEEqFun Lp.simpleFunc Lp

variable [NormedAddCommGroup E] [NormedAddCommGroup F] {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

namespace SimpleFunc

theorem norm_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) : â€–fâ€– = ((toSimpleFunc f).map norm).integral Î¼ := by
  rw [norm_eq_sum_mul f, (toSimpleFunc f).map_integral norm (SimpleFunc.integrable f) norm_zero]
  simp_rw [smul_eq_mul]
#align measure_theory.L1.simple_func.norm_eq_integral MeasureTheory.L1.SimpleFunc.norm_eq_integral

section PosPart

/-- Positive part of a simple function in L1 space.  -/
nonrec def posPart (f : Î± â†’â‚â‚›[Î¼] â„) : Î± â†’â‚â‚›[Î¼] â„ :=
  âŸ¨Lp.posPart (f : Î± â†’â‚[Î¼] â„), by
    rcases f with âŸ¨f, s, hsfâŸ©
    use s.posPart
    simp only [Subtype.coe_mk, Lp.coe_posPart, â† hsf, AEEqFun.posPart_mk,
      SimpleFunc.coe_map, mk_eq_mk]
    -- Porting note: added
    simp [SimpleFunc.posPart, Function.comp, EventuallyEq.rfl] âŸ©
#align measure_theory.L1.simple_func.pos_part MeasureTheory.L1.SimpleFunc.posPart

/-- Negative part of a simple function in L1 space. -/
def negPart (f : Î± â†’â‚â‚›[Î¼] â„) : Î± â†’â‚â‚›[Î¼] â„ :=
  posPart (-f)
#align measure_theory.L1.simple_func.neg_part MeasureTheory.L1.SimpleFunc.negPart

@[norm_cast]
theorem coe_posPart (f : Î± â†’â‚â‚›[Î¼] â„) : (posPart f : Î± â†’â‚[Î¼] â„) = Lp.posPart (f : Î± â†’â‚[Î¼] â„) := rfl
#align measure_theory.L1.simple_func.coe_pos_part MeasureTheory.L1.SimpleFunc.coe_posPart

@[norm_cast]
theorem coe_negPart (f : Î± â†’â‚â‚›[Î¼] â„) : (negPart f : Î± â†’â‚[Î¼] â„) = Lp.negPart (f : Î± â†’â‚[Î¼] â„) := rfl
#align measure_theory.L1.simple_func.coe_neg_part MeasureTheory.L1.SimpleFunc.coe_negPart

end PosPart

section SimpleFuncIntegral

/-!
### The Bochner integral of `L1`

Define the Bochner integral on `Î± â†’â‚â‚›[Î¼] E` by extension from the simple functions `Î± â†’â‚â‚›[Î¼] E`,
and prove basic properties of this integral. -/


variable [NormedField ğ•œ] [NormedSpace ğ•œ E] [NormedSpace â„ E] [SMulCommClass â„ ğ•œ E] {F' : Type*}
  [NormedAddCommGroup F'] [NormedSpace â„ F']

attribute [local instance] simpleFunc.normedSpace

/-- The Bochner integral over simple functions in L1 space. -/
def integral (f : Î± â†’â‚â‚›[Î¼] E) : E :=
  (toSimpleFunc f).integral Î¼
#align measure_theory.L1.simple_func.integral MeasureTheory.L1.SimpleFunc.integral

theorem integral_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) : integral f = (toSimpleFunc f).integral Î¼ := rfl
#align measure_theory.L1.simple_func.integral_eq_integral MeasureTheory.L1.SimpleFunc.integral_eq_integral

nonrec theorem integral_eq_lintegral {f : Î± â†’â‚â‚›[Î¼] â„} (h_pos : 0 â‰¤áµ[Î¼] toSimpleFunc f) :
    integral f = ENNReal.toReal (âˆ«â» a, ENNReal.ofReal ((toSimpleFunc f) a) âˆ‚Î¼) := by
  rw [integral, SimpleFunc.integral_eq_lintegral (SimpleFunc.integrable f) h_pos]
#align measure_theory.L1.simple_func.integral_eq_lintegral MeasureTheory.L1.SimpleFunc.integral_eq_lintegral

theorem integral_eq_setToL1S (f : Î± â†’â‚â‚›[Î¼] E) : integral f = setToL1S (weightedSMul Î¼) f := rfl
#align measure_theory.L1.simple_func.integral_eq_set_to_L1s MeasureTheory.L1.SimpleFunc.integral_eq_setToL1S

nonrec theorem integral_congr {f g : Î± â†’â‚â‚›[Î¼] E} (h : toSimpleFunc f =áµ[Î¼] toSimpleFunc g) :
    integral f = integral g :=
  SimpleFunc.integral_congr (SimpleFunc.integrable f) h
#align measure_theory.L1.simple_func.integral_congr MeasureTheory.L1.SimpleFunc.integral_congr

theorem integral_add (f g : Î± â†’â‚â‚›[Î¼] E) : integral (f + g) = integral f + integral g :=
  setToL1S_add _ (fun _ _ => weightedSMul_null) weightedSMul_union _ _
#align measure_theory.L1.simple_func.integral_add MeasureTheory.L1.SimpleFunc.integral_add

theorem integral_smul (c : ğ•œ) (f : Î± â†’â‚â‚›[Î¼] E) : integral (c â€¢ f) = c â€¢ integral f :=
  setToL1S_smul _ (fun _ _ => weightedSMul_null) weightedSMul_union weightedSMul_smul c f
#align measure_theory.L1.simple_func.integral_smul MeasureTheory.L1.SimpleFunc.integral_smul

theorem norm_integral_le_norm (f : Î± â†’â‚â‚›[Î¼] E) : â€–integral fâ€– â‰¤ â€–fâ€– := by
  rw [integral, norm_eq_integral]
  exact (toSimpleFunc f).norm_integral_le_integral_norm (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.norm_integral_le_norm MeasureTheory.L1.SimpleFunc.norm_integral_le_norm

variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E'] [NormedSpace ğ•œ E']
variable (Î± E Î¼ ğ•œ)

/-- The Bochner integral over simple functions in L1 space as a continuous linear map. -/
def integralCLM' : (Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] E :=
  LinearMap.mkContinuous âŸ¨âŸ¨integral, integral_addâŸ©, integral_smulâŸ© 1 fun f =>
    le_trans (norm_integral_le_norm _) <| by rw [one_mul]
#align measure_theory.L1.simple_func.integral_clm' MeasureTheory.L1.SimpleFunc.integralCLM'

/-- The Bochner integral over simple functions in L1 space as a continuous linear map over â„. -/
def integralCLM : (Î± â†’â‚â‚›[Î¼] E) â†’L[â„] E :=
  integralCLM' Î± E â„ Î¼
#align measure_theory.L1.simple_func.integral_clm MeasureTheory.L1.SimpleFunc.integralCLM

variable {Î± E Î¼ ğ•œ}

local notation "Integral" => integralCLM Î± E Î¼

open ContinuousLinearMap

theorem norm_Integral_le_one : â€–Integralâ€– â‰¤ 1 :=
  -- Porting note: Old proof was `LinearMap.mkContinuous_norm_le _ zero_le_one _`
  LinearMap.mkContinuous_norm_le _ zero_le_one (fun f => by
    rw [one_mul]
    exact norm_integral_le_norm f)
#align measure_theory.L1.simple_func.norm_Integral_le_one MeasureTheory.L1.SimpleFunc.norm_Integral_le_one

section PosPart

theorem posPart_toSimpleFunc (f : Î± â†’â‚â‚›[Î¼] â„) :
    toSimpleFunc (posPart f) =áµ[Î¼] (toSimpleFunc f).posPart := by
  have eq : âˆ€ a, (toSimpleFunc f).posPart a = max ((toSimpleFunc f) a) 0 := fun a => rfl
  have ae_eq : âˆ€áµ a âˆ‚Î¼, toSimpleFunc (posPart f) a = max ((toSimpleFunc f) a) 0 := by
    filter_upwards [toSimpleFunc_eq_toFun (posPart f), Lp.coeFn_posPart (f : Î± â†’â‚[Î¼] â„),
      toSimpleFunc_eq_toFun f] with _ _ hâ‚‚ hâ‚ƒ
    convert hâ‚‚ using 1
    -- Porting note: added
    rw [hâ‚ƒ]
  refine ae_eq.mono fun a h => ?_
  rw [h, eq]
#align measure_theory.L1.simple_func.pos_part_to_simple_func MeasureTheory.L1.SimpleFunc.posPart_toSimpleFunc

theorem negPart_toSimpleFunc (f : Î± â†’â‚â‚›[Î¼] â„) :
    toSimpleFunc (negPart f) =áµ[Î¼] (toSimpleFunc f).negPart := by
  rw [SimpleFunc.negPart, MeasureTheory.SimpleFunc.negPart]
  filter_upwards [posPart_toSimpleFunc (-f), neg_toSimpleFunc f]
  intro a hâ‚ hâ‚‚
  rw [hâ‚]
  show max _ _ = max _ _
  rw [hâ‚‚]
  rfl
#align measure_theory.L1.simple_func.neg_part_to_simple_func MeasureTheory.L1.SimpleFunc.negPart_toSimpleFunc

theorem integral_eq_norm_posPart_sub (f : Î± â†’â‚â‚›[Î¼] â„) : integral f = â€–posPart fâ€– - â€–negPart fâ€– := by
  -- Convert things in `LÂ¹` to their `SimpleFunc` counterpart
  have ae_eqâ‚ : (toSimpleFunc f).posPart =áµ[Î¼] (toSimpleFunc (posPart f)).map norm := by
    filter_upwards [posPart_toSimpleFunc f] with _ h
    rw [SimpleFunc.map_apply, h]
    conv_lhs => rw [â† SimpleFunc.posPart_map_norm, SimpleFunc.map_apply]
  -- Convert things in `LÂ¹` to their `SimpleFunc` counterpart
  have ae_eqâ‚‚ : (toSimpleFunc f).negPart =áµ[Î¼] (toSimpleFunc (negPart f)).map norm := by
    filter_upwards [negPart_toSimpleFunc f] with _ h
    rw [SimpleFunc.map_apply, h]
    conv_lhs => rw [â† SimpleFunc.negPart_map_norm, SimpleFunc.map_apply]
  rw [integral, norm_eq_integral, norm_eq_integral, â† SimpleFunc.integral_sub]
  Â· show (toSimpleFunc f).integral Î¼ =
      ((toSimpleFunc (posPart f)).map norm - (toSimpleFunc (negPart f)).map norm).integral Î¼
    apply MeasureTheory.SimpleFunc.integral_congr (SimpleFunc.integrable f)
    filter_upwards [ae_eqâ‚, ae_eqâ‚‚] with _ hâ‚ hâ‚‚
    show _ = _ - _
    rw [â† hâ‚, â† hâ‚‚]
    have := (toSimpleFunc f).posPart_sub_negPart
    conv_lhs => rw [â† this]
    rfl
  Â· exact (SimpleFunc.integrable f).pos_part.congr ae_eqâ‚
  Â· exact (SimpleFunc.integrable f).neg_part.congr ae_eqâ‚‚
#align measure_theory.L1.simple_func.integral_eq_norm_pos_part_sub MeasureTheory.L1.SimpleFunc.integral_eq_norm_posPart_sub

end PosPart

end SimpleFuncIntegral

end SimpleFunc

open SimpleFunc

local notation "Integral" => @integralCLM Î± E _ _ _ _ _ Î¼ _

variable [NormedSpace â„ E] [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E] [SMulCommClass â„ ğ•œ E]
  [NormedSpace â„ F] [CompleteSpace E]

section IntegrationInL1

attribute [local instance] simpleFunc.normedSpace

open ContinuousLinearMap

variable (ğ•œ)

/-- The Bochner integral in L1 space as a continuous linear map. -/
nonrec def integralCLM' : (Î± â†’â‚[Î¼] E) â†’L[ğ•œ] E :=
  (integralCLM' Î± E ğ•œ Î¼).extend (coeToLp Î± E ğ•œ) (simpleFunc.denseRange one_ne_top)
    simpleFunc.uniformInducing
#align measure_theory.L1.integral_clm' MeasureTheory.L1.integralCLM'

variable {ğ•œ}

/-- The Bochner integral in L1 space as a continuous linear map over â„. -/
def integralCLM : (Î± â†’â‚[Î¼] E) â†’L[â„] E :=
  integralCLM' â„
#align measure_theory.L1.integral_clm MeasureTheory.L1.integralCLM

-- Porting note: added `(E := E)` in several places below.
/-- The Bochner integral in L1 space -/
irreducible_def integral (f : Î± â†’â‚[Î¼] E) : E :=
  integralCLM (E := E) f
#align measure_theory.L1.integral MeasureTheory.L1.integral

theorem integral_eq (f : Î± â†’â‚[Î¼] E) : integral f = integralCLM (E := E) f := by
  simp only [integral]
#align measure_theory.L1.integral_eq MeasureTheory.L1.integral_eq

theorem integral_eq_setToL1 (f : Î± â†’â‚[Î¼] E) :
    integral f = setToL1 (E := E) (dominatedFinMeasAdditive_weightedSMul Î¼) f := by
  simp only [integral]; rfl
#align measure_theory.L1.integral_eq_set_to_L1 MeasureTheory.L1.integral_eq_setToL1

@[norm_cast]
theorem SimpleFunc.integral_L1_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) :
    L1.integral (f : Î± â†’â‚[Î¼] E) = SimpleFunc.integral f := by
  simp only [integral, L1.integral]
  exact setToL1_eq_setToL1SCLM (dominatedFinMeasAdditive_weightedSMul Î¼) f
#align measure_theory.L1.simple_func.integral_L1_eq_integral MeasureTheory.L1.SimpleFunc.integral_L1_eq_integral

variable (Î± E)

@[simp]
theorem integral_zero : integral (0 : Î± â†’â‚[Î¼] E) = 0 := by
  simp only [integral]
  exact map_zero integralCLM
#align measure_theory.L1.integral_zero MeasureTheory.L1.integral_zero

variable {Î± E}

@[integral_simps]
theorem integral_add (f g : Î± â†’â‚[Î¼] E) : integral (f + g) = integral f + integral g := by
  simp only [integral]
  exact map_add integralCLM f g
#align measure_theory.L1.integral_add MeasureTheory.L1.integral_add

@[integral_simps]
theorem integral_neg (f : Î± â†’â‚[Î¼] E) : integral (-f) = -integral f := by
  simp only [integral]
  exact map_neg integralCLM f
#align measure_theory.L1.integral_neg MeasureTheory.L1.integral_neg

@[integral_simps]
theorem integral_sub (f g : Î± â†’â‚[Î¼] E) : integral (f - g) = integral f - integral g := by
  simp only [integral]
  exact map_sub integralCLM f g
#align measure_theory.L1.integral_sub MeasureTheory.L1.integral_sub

@[integral_simps]
theorem integral_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] E) : integral (c â€¢ f) = c â€¢ integral f := by
  simp only [integral]
  show (integralCLM' (E := E) ğ•œ) (c â€¢ f) = c â€¢ (integralCLM' (E := E) ğ•œ) f
  exact map_smul (integralCLM' (E := E) ğ•œ) c f
#align measure_theory.L1.integral_smul MeasureTheory.L1.integral_smul

local notation "Integral" => @integralCLM Î± E _ _ Î¼ _ _

local notation "sIntegral" => @SimpleFunc.integralCLM Î± E _ _ Î¼ _

theorem norm_Integral_le_one : â€–integralCLM (Î± := Î±) (E := E) (Î¼ := Î¼)â€– â‰¤ 1 :=
  norm_setToL1_le (dominatedFinMeasAdditive_weightedSMul Î¼) zero_le_one
#align measure_theory.L1.norm_Integral_le_one MeasureTheory.L1.norm_Integral_le_one

theorem nnnorm_Integral_le_one : â€–integralCLM (Î± := Î±) (E := E) (Î¼ := Î¼)â€–â‚Š â‰¤ 1 :=
  norm_Integral_le_one

theorem norm_integral_le (f : Î± â†’â‚[Î¼] E) : â€–integral fâ€– â‰¤ â€–fâ€– :=
  calc
    â€–integral fâ€– = â€–integralCLM (E := E) fâ€– := by simp only [integral]
    _ â‰¤ â€–integralCLM (Î± := Î±) (E := E) (Î¼ := Î¼)â€– * â€–fâ€– := le_opNorm _ _
    _ â‰¤ 1 * â€–fâ€– := mul_le_mul_of_nonneg_right norm_Integral_le_one <| norm_nonneg _
    _ = â€–fâ€– := one_mul _
#align measure_theory.L1.norm_integral_le MeasureTheory.L1.norm_integral_le

theorem nnnorm_integral_le (f : Î± â†’â‚[Î¼] E) : â€–integral fâ€–â‚Š â‰¤ â€–fâ€–â‚Š :=
  norm_integral_le f

@[continuity]
theorem continuous_integral : Continuous fun f : Î± â†’â‚[Î¼] E => integral f := by
  simp only [integral]
  exact L1.integralCLM.continuous
#align measure_theory.L1.continuous_integral MeasureTheory.L1.continuous_integral

section PosPart

theorem integral_eq_norm_posPart_sub (f : Î± â†’â‚[Î¼] â„) :
    integral f = â€–Lp.posPart fâ€– - â€–Lp.negPart fâ€– := by
  -- Use `isClosed_property` and `isClosed_eq`
  refine @isClosed_property _ _ _ ((â†‘) : (Î± â†’â‚â‚›[Î¼] â„) â†’ Î± â†’â‚[Î¼] â„)
      (fun f : Î± â†’â‚[Î¼] â„ => integral f = â€–Lp.posPart fâ€– - â€–Lp.negPart fâ€–)
      (simpleFunc.denseRange one_ne_top) (isClosed_eq ?_ ?_) ?_ f
  Â· simp only [integral]
    exact cont _
  Â· refine Continuous.sub (continuous_norm.comp Lp.continuous_posPart)
      (continuous_norm.comp Lp.continuous_negPart)
  -- Show that the property holds for all simple functions in the `LÂ¹` space.
  Â· intro s
    norm_cast
    exact SimpleFunc.integral_eq_norm_posPart_sub _
#align measure_theory.L1.integral_eq_norm_pos_part_sub MeasureTheory.L1.integral_eq_norm_posPart_sub

end PosPart

end IntegrationInL1

end L1

/-!
## The Bochner integral on functions

Define the Bochner integral on functions generally to be the `L1` Bochner integral, for integrable
functions, and 0 otherwise; prove its basic properties.
-/

variable [NormedAddCommGroup E] [NormedSpace â„ E] [hE : CompleteSpace E] [NontriviallyNormedField ğ•œ]
  [NormedSpace ğ•œ E] [SMulCommClass â„ ğ•œ E] [NormedAddCommGroup F] [NormedSpace â„ F] [CompleteSpace F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G]

section

open scoped Classical

/-- The Bochner integral -/
irreducible_def integral {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’ G) : G :=
  if _ : CompleteSpace G then
    if hf : Integrable f Î¼ then L1.integral (hf.toL1 f) else 0
  else 0
#align measure_theory.integral MeasureTheory.integral

end

/-! In the notation for integrals, an expression like `âˆ« x, g â€–xâ€– âˆ‚Î¼` will not be parsed correctly,
  and needs parentheses. We do not set the binding power of `r` to `0`, because then
  `âˆ« x, f x = 0` will be parsed incorrectly. -/

@[inherit_doc MeasureTheory.integral]
notation3 "âˆ« "(...)", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => integral Î¼ r

@[inherit_doc MeasureTheory.integral]
notation3 "âˆ« "(...)", "r:60:(scoped f => integral volume f) => r

@[inherit_doc MeasureTheory.integral]
notation3 "âˆ« "(...)" in "s", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => integral (Measure.restrict Î¼ s) r

@[inherit_doc MeasureTheory.integral]
notation3 "âˆ« "(...)" in "s", "r:60:(scoped f => integral (Measure.restrict volume s) f) => r

section Properties

open ContinuousLinearMap MeasureTheory.SimpleFunc

variable {f g : Î± â†’ E} {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

theorem integral_eq (f : Î± â†’ E) (hf : Integrable f Î¼) : âˆ« a, f a âˆ‚Î¼ = L1.integral (hf.toL1 f) := by
  simp [integral, hE, hf]
#align measure_theory.integral_eq MeasureTheory.integral_eq

theorem integral_eq_setToFun (f : Î± â†’ E) :
    âˆ« a, f a âˆ‚Î¼ = setToFun Î¼ (weightedSMul Î¼) (dominatedFinMeasAdditive_weightedSMul Î¼) f := by
  simp only [integral, hE, L1.integral]; rfl
#align measure_theory.integral_eq_set_to_fun MeasureTheory.integral_eq_setToFun

theorem L1.integral_eq_integral (f : Î± â†’â‚[Î¼] E) : L1.integral f = âˆ« a, f a âˆ‚Î¼ := by
  simp only [integral, L1.integral, integral_eq_setToFun]
  exact (L1.setToFun_eq_setToL1 (dominatedFinMeasAdditive_weightedSMul Î¼) f).symm
set_option linter.uppercaseLean3 false in
#align measure_theory.L1.integral_eq_integral MeasureTheory.L1.integral_eq_integral

theorem integral_undef {f : Î± â†’ G} (h : Â¬Integrable f Î¼) : âˆ« a, f a âˆ‚Î¼ = 0 := by
  by_cases hG : CompleteSpace G
  Â· simp [integral, hG, h]
  Â· simp [integral, hG]
#align measure_theory.integral_undef MeasureTheory.integral_undef

theorem Integrable.of_integral_ne_zero {f : Î± â†’ G} (h : âˆ« a, f a âˆ‚Î¼ â‰  0) : Integrable f Î¼ :=
  Not.imp_symm integral_undef h

theorem integral_non_aestronglyMeasurable {f : Î± â†’ G} (h : Â¬AEStronglyMeasurable f Î¼) :
    âˆ« a, f a âˆ‚Î¼ = 0 :=
  integral_undef <| not_and_of_not_left _ h
#align measure_theory.integral_non_ae_strongly_measurable MeasureTheory.integral_non_aestronglyMeasurable

variable (Î± G)

@[simp]
theorem integral_zero : âˆ« _ : Î±, (0 : G) âˆ‚Î¼ = 0 := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_zero (dominatedFinMeasAdditive_weightedSMul Î¼)
  Â· simp [integral, hG]
#align measure_theory.integral_zero MeasureTheory.integral_zero

@[simp]
theorem integral_zero' : integral Î¼ (0 : Î± â†’ G) = 0 :=
  integral_zero Î± G
#align measure_theory.integral_zero' MeasureTheory.integral_zero'

variable {Î± G}

theorem integrable_of_integral_eq_one {f : Î± â†’ â„} (h : âˆ« x, f x âˆ‚Î¼ = 1) : Integrable f Î¼ :=
  .of_integral_ne_zero <| h â–¸ one_ne_zero
#align measure_theory.integrable_of_integral_eq_one MeasureTheory.integrable_of_integral_eq_one

theorem integral_add {f g : Î± â†’ G} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« a, f a + g a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ + âˆ« a, g a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_add (dominatedFinMeasAdditive_weightedSMul Î¼) hf hg
  Â· simp [integral, hG]
#align measure_theory.integral_add MeasureTheory.integral_add

theorem integral_add' {f g : Î± â†’ G} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« a, (f + g) a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ + âˆ« a, g a âˆ‚Î¼ :=
  integral_add hf hg
#align measure_theory.integral_add' MeasureTheory.integral_add'

theorem integral_finset_sum {Î¹} (s : Finset Î¹) {f : Î¹ â†’ Î± â†’ G} (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) :
    âˆ« a, âˆ‘ i âˆˆ s, f i a âˆ‚Î¼ = âˆ‘ i âˆˆ s, âˆ« a, f i a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_finset_sum (dominatedFinMeasAdditive_weightedSMul _) s hf
  Â· simp [integral, hG]
#align measure_theory.integral_finset_sum MeasureTheory.integral_finset_sum

@[integral_simps]
theorem integral_neg (f : Î± â†’ G) : âˆ« a, -f a âˆ‚Î¼ = -âˆ« a, f a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_neg (dominatedFinMeasAdditive_weightedSMul Î¼) f
  Â· simp [integral, hG]
#align measure_theory.integral_neg MeasureTheory.integral_neg

theorem integral_neg' (f : Î± â†’ G) : âˆ« a, (-f) a âˆ‚Î¼ = -âˆ« a, f a âˆ‚Î¼ :=
  integral_neg f
#align measure_theory.integral_neg' MeasureTheory.integral_neg'

theorem integral_sub {f g : Î± â†’ G} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« a, f a - g a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ - âˆ« a, g a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_sub (dominatedFinMeasAdditive_weightedSMul Î¼) hf hg
  Â· simp [integral, hG]
#align measure_theory.integral_sub MeasureTheory.integral_sub

theorem integral_sub' {f g : Î± â†’ G} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« a, (f - g) a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ - âˆ« a, g a âˆ‚Î¼ :=
  integral_sub hf hg
#align measure_theory.integral_sub' MeasureTheory.integral_sub'

@[integral_simps]
theorem integral_smul [NormedSpace ğ•œ G] [SMulCommClass â„ ğ•œ G] (c : ğ•œ) (f : Î± â†’ G) :
    âˆ« a, c â€¢ f a âˆ‚Î¼ = c â€¢ âˆ« a, f a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_smul (dominatedFinMeasAdditive_weightedSMul Î¼) weightedSMul_smul c f
  Â· simp [integral, hG]
#align measure_theory.integral_smul MeasureTheory.integral_smul

theorem integral_mul_left {L : Type*} [RCLike L] (r : L) (f : Î± â†’ L) :
    âˆ« a, r * f a âˆ‚Î¼ = r * âˆ« a, f a âˆ‚Î¼ :=
  integral_smul r f
#align measure_theory.integral_mul_left MeasureTheory.integral_mul_left

theorem integral_mul_right {L : Type*} [RCLike L] (r : L) (f : Î± â†’ L) :
    âˆ« a, f a * r âˆ‚Î¼ = (âˆ« a, f a âˆ‚Î¼) * r := by
  simp only [mul_comm]; exact integral_mul_left r f
#align measure_theory.integral_mul_right MeasureTheory.integral_mul_right

theorem integral_div {L : Type*} [RCLike L] (r : L) (f : Î± â†’ L) :
    âˆ« a, f a / r âˆ‚Î¼ = (âˆ« a, f a âˆ‚Î¼) / r := by
  simpa only [â† div_eq_mul_inv] using integral_mul_right râ»Â¹ f
#align measure_theory.integral_div MeasureTheory.integral_div

theorem integral_congr_ae {f g : Î± â†’ G} (h : f =áµ[Î¼] g) : âˆ« a, f a âˆ‚Î¼ = âˆ« a, g a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_congr_ae (dominatedFinMeasAdditive_weightedSMul Î¼) h
  Â· simp [integral, hG]
#align measure_theory.integral_congr_ae MeasureTheory.integral_congr_ae

-- Porting note: `nolint simpNF` added because simplify fails on left-hand side
@[simp, nolint simpNF]
theorem L1.integral_of_fun_eq_integral {f : Î± â†’ G} (hf : Integrable f Î¼) :
    âˆ« a, (hf.toL1 f) a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [MeasureTheory.integral, hG, L1.integral]
    exact setToFun_toL1 (dominatedFinMeasAdditive_weightedSMul Î¼) hf
  Â· simp [MeasureTheory.integral, hG]
set_option linter.uppercaseLean3 false in
#align measure_theory.L1.integral_of_fun_eq_integral MeasureTheory.L1.integral_of_fun_eq_integral

@[continuity]
theorem continuous_integral : Continuous fun f : Î± â†’â‚[Î¼] G => âˆ« a, f a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact continuous_setToFun (dominatedFinMeasAdditive_weightedSMul Î¼)
  Â· simp [integral, hG, continuous_const]
#align measure_theory.continuous_integral MeasureTheory.continuous_integral

theorem norm_integral_le_lintegral_norm (f : Î± â†’ G) :
    â€–âˆ« a, f a âˆ‚Î¼â€– â‰¤ ENNReal.toReal (âˆ«â» a, ENNReal.ofReal â€–f aâ€– âˆ‚Î¼) := by
  by_cases hG : CompleteSpace G
  Â· by_cases hf : Integrable f Î¼
    Â· rw [integral_eq f hf, â† Integrable.norm_toL1_eq_lintegral_norm f hf]
      exact L1.norm_integral_le _
    Â· rw [integral_undef hf, norm_zero]; exact toReal_nonneg
  Â· simp [integral, hG]
#align measure_theory.norm_integral_le_lintegral_norm MeasureTheory.norm_integral_le_lintegral_norm

theorem ennnorm_integral_le_lintegral_ennnorm (f : Î± â†’ G) :
    (â€–âˆ« a, f a âˆ‚Î¼â€–â‚Š : â„â‰¥0âˆ) â‰¤ âˆ«â» a, â€–f aâ€–â‚Š âˆ‚Î¼ := by
  simp_rw [â† ofReal_norm_eq_coe_nnnorm]
  apply ENNReal.ofReal_le_of_le_toReal
  exact norm_integral_le_lintegral_norm f
#align measure_theory.ennnorm_integral_le_lintegral_ennnorm MeasureTheory.ennnorm_integral_le_lintegral_ennnorm

theorem integral_eq_zero_of_ae {f : Î± â†’ G} (hf : f =áµ[Î¼] 0) : âˆ« a, f a âˆ‚Î¼ = 0 := by
  simp [integral_congr_ae hf, integral_zero]
#align measure_theory.integral_eq_zero_of_ae MeasureTheory.integral_eq_zero_of_ae

/-- If `f` has finite integral, then `âˆ« x in s, f x âˆ‚Î¼` is absolutely continuous in `s`: it tends
to zero as `Î¼ s` tends to zero. -/
theorem HasFiniteIntegral.tendsto_setIntegral_nhds_zero {Î¹} {f : Î± â†’ G}
    (hf : HasFiniteIntegral f Î¼) {l : Filter Î¹} {s : Î¹ â†’ Set Î±} (hs : Tendsto (Î¼ âˆ˜ s) l (ğ“ 0)) :
    Tendsto (fun i => âˆ« x in s i, f x âˆ‚Î¼) l (ğ“ 0) := by
  rw [tendsto_zero_iff_norm_tendsto_zero]
  simp_rw [â† coe_nnnorm, â† NNReal.coe_zero, NNReal.tendsto_coe, â† ENNReal.tendsto_coe,
    ENNReal.coe_zero]
  exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
    (tendsto_set_lintegral_zero (ne_of_lt hf) hs) (fun i => zero_le _)
    fun i => ennnorm_integral_le_lintegral_ennnorm _
#align measure_theory.has_finite_integral.tendsto_set_integral_nhds_zero MeasureTheory.HasFiniteIntegral.tendsto_setIntegral_nhds_zero

@[deprecated (since := "2024-04-17")]
alias HasFiniteIntegral.tendsto_set_integral_nhds_zero :=
  HasFiniteIntegral.tendsto_setIntegral_nhds_zero

/-- If `f` is integrable, then `âˆ« x in s, f x âˆ‚Î¼` is absolutely continuous in `s`: it tends
to zero as `Î¼ s` tends to zero. -/
theorem Integrable.tendsto_setIntegral_nhds_zero {Î¹} {f : Î± â†’ G} (hf : Integrable f Î¼)
    {l : Filter Î¹} {s : Î¹ â†’ Set Î±} (hs : Tendsto (Î¼ âˆ˜ s) l (ğ“ 0)) :
    Tendsto (fun i => âˆ« x in s i, f x âˆ‚Î¼) l (ğ“ 0) :=
  hf.2.tendsto_setIntegral_nhds_zero hs
#align measure_theory.integrable.tendsto_set_integral_nhds_zero MeasureTheory.Integrable.tendsto_setIntegral_nhds_zero

@[deprecated (since := "2024-04-17")]
alias Integrable.tendsto_set_integral_nhds_zero :=
  Integrable.tendsto_setIntegral_nhds_zero

/-- If `F i â†’ f` in `L1`, then `âˆ« x, F i x âˆ‚Î¼ â†’ âˆ« x, f x âˆ‚Î¼`. -/
theorem tendsto_integral_of_L1 {Î¹} (f : Î± â†’ G) (hfi : Integrable f Î¼) {F : Î¹ â†’ Î± â†’ G} {l : Filter Î¹}
    (hFi : âˆ€á¶  i in l, Integrable (F i) Î¼)
    (hF : Tendsto (fun i => âˆ«â» x, â€–F i x - f xâ€–â‚Š âˆ‚Î¼) l (ğ“ 0)) :
    Tendsto (fun i => âˆ« x, F i x âˆ‚Î¼) l (ğ“ <| âˆ« x, f x âˆ‚Î¼) := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact tendsto_setToFun_of_L1 (dominatedFinMeasAdditive_weightedSMul Î¼) f hfi hFi hF
  Â· simp [integral, hG, tendsto_const_nhds]
set_option linter.uppercaseLean3 false in
#align measure_theory.tendsto_integral_of_L1 MeasureTheory.tendsto_integral_of_L1

/-- If `F i â†’ f` in `L1`, then `âˆ« x, F i x âˆ‚Î¼ â†’ âˆ« x, f x âˆ‚Î¼`. -/
lemma tendsto_integral_of_L1' {Î¹} (f : Î± â†’ G) (hfi : Integrable f Î¼) {F : Î¹ â†’ Î± â†’ G} {l : Filter Î¹}
    (hFi : âˆ€á¶  i in l, Integrable (F i) Î¼) (hF : Tendsto (fun i â†¦ snorm (F i - f) 1 Î¼) l (ğ“ 0)) :
    Tendsto (fun i â†¦ âˆ« x, F i x âˆ‚Î¼) l (ğ“ (âˆ« x, f x âˆ‚Î¼)) := by
  refine tendsto_integral_of_L1 f hfi hFi ?_
  simp_rw [snorm_one_eq_lintegral_nnnorm, Pi.sub_apply] at hF
  exact hF

/-- If `F i â†’ f` in `L1`, then `âˆ« x in s, F i x âˆ‚Î¼ â†’ âˆ« x in s, f x âˆ‚Î¼`. -/
lemma tendsto_setIntegral_of_L1 {Î¹} (f : Î± â†’ G) (hfi : Integrable f Î¼) {F : Î¹ â†’ Î± â†’ G}
    {l : Filter Î¹}
    (hFi : âˆ€á¶  i in l, Integrable (F i) Î¼) (hF : Tendsto (fun i â†¦ âˆ«â» x, â€–F i x - f xâ€–â‚Š âˆ‚Î¼) l (ğ“ 0))
    (s : Set Î±) :
    Tendsto (fun i â†¦ âˆ« x in s, F i x âˆ‚Î¼) l (ğ“ (âˆ« x in s, f x âˆ‚Î¼)) := by
  refine tendsto_integral_of_L1 f hfi.restrict ?_ ?_
  Â· filter_upwards [hFi] with i hi using hi.restrict
  Â· simp_rw [â† snorm_one_eq_lintegral_nnnorm] at hF âŠ¢
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hF (fun _ â†¦ zero_le')
      (fun _ â†¦ snorm_mono_measure _ Measure.restrict_le_self)

@[deprecated (since := "2024-04-17")]
alias tendsto_set_integral_of_L1 := tendsto_setIntegral_of_L1

/-- If `F i â†’ f` in `L1`, then `âˆ« x in s, F i x âˆ‚Î¼ â†’ âˆ« x in s, f x âˆ‚Î¼`. -/
lemma tendsto_setIntegral_of_L1' {Î¹} (f : Î± â†’ G) (hfi : Integrable f Î¼) {F : Î¹ â†’ Î± â†’ G}
    {l : Filter Î¹}
    (hFi : âˆ€á¶  i in l, Integrable (F i) Î¼) (hF : Tendsto (fun i â†¦ snorm (F i - f) 1 Î¼) l (ğ“ 0))
    (s : Set Î±) :
    Tendsto (fun i â†¦ âˆ« x in s, F i x âˆ‚Î¼) l (ğ“ (âˆ« x in s, f x âˆ‚Î¼)) := by
  refine tendsto_setIntegral_of_L1 f hfi hFi ?_ s
  simp_rw [snorm_one_eq_lintegral_nnnorm, Pi.sub_apply] at hF
  exact hF

@[deprecated (since := "2024-04-17")]
alias tendsto_set_integral_of_L1' := tendsto_setIntegral_of_L1'

variable {X : Type*} [TopologicalSpace X] [FirstCountableTopology X]

theorem continuousWithinAt_of_dominated {F : X â†’ Î± â†’ G} {xâ‚€ : X} {bound : Î± â†’ â„} {s : Set X}
    (hF_meas : âˆ€á¶  x in ğ“[s] xâ‚€, AEStronglyMeasurable (F x) Î¼)
    (h_bound : âˆ€á¶  x in ğ“[s] xâ‚€, âˆ€áµ a âˆ‚Î¼, â€–F x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, ContinuousWithinAt (fun x => F x a) s xâ‚€) :
    ContinuousWithinAt (fun x => âˆ« a, F x a âˆ‚Î¼) s xâ‚€ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact continuousWithinAt_setToFun_of_dominated (dominatedFinMeasAdditive_weightedSMul Î¼)
      hF_meas h_bound bound_integrable h_cont
  Â· simp [integral, hG, continuousWithinAt_const]
#align measure_theory.continuous_within_at_of_dominated MeasureTheory.continuousWithinAt_of_dominated

theorem continuousAt_of_dominated {F : X â†’ Î± â†’ G} {xâ‚€ : X} {bound : Î± â†’ â„}
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) Î¼)
    (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ a âˆ‚Î¼, â€–F x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, ContinuousAt (fun x => F x a) xâ‚€) :
    ContinuousAt (fun x => âˆ« a, F x a âˆ‚Î¼) xâ‚€ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact continuousAt_setToFun_of_dominated (dominatedFinMeasAdditive_weightedSMul Î¼)
      hF_meas h_bound bound_integrable h_cont
  Â· simp [integral, hG, continuousAt_const]
#align measure_theory.continuous_at_of_dominated MeasureTheory.continuousAt_of_dominated

theorem continuousOn_of_dominated {F : X â†’ Î± â†’ G} {bound : Î± â†’ â„} {s : Set X}
    (hF_meas : âˆ€ x âˆˆ s, AEStronglyMeasurable (F x) Î¼)
    (h_bound : âˆ€ x âˆˆ s, âˆ€áµ a âˆ‚Î¼, â€–F x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, ContinuousOn (fun x => F x a) s) :
    ContinuousOn (fun x => âˆ« a, F x a âˆ‚Î¼) s := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact continuousOn_setToFun_of_dominated (dominatedFinMeasAdditive_weightedSMul Î¼)
      hF_meas h_bound bound_integrable h_cont
  Â· simp [integral, hG, continuousOn_const]
#align measure_theory.continuous_on_of_dominated MeasureTheory.continuousOn_of_dominated

theorem continuous_of_dominated {F : X â†’ Î± â†’ G} {bound : Î± â†’ â„}
    (hF_meas : âˆ€ x, AEStronglyMeasurable (F x) Î¼) (h_bound : âˆ€ x, âˆ€áµ a âˆ‚Î¼, â€–F x aâ€– â‰¤ bound a)
    (bound_integrable : Integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, Continuous fun x => F x a) :
    Continuous fun x => âˆ« a, F x a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact continuous_setToFun_of_dominated (dominatedFinMeasAdditive_weightedSMul Î¼)
      hF_meas h_bound bound_integrable h_cont
  Â· simp [integral, hG, continuous_const]
#align measure_theory.continuous_of_dominated MeasureTheory.continuous_of_dominated

/-- The Bochner integral of a real-valued function `f : Î± â†’ â„` is the difference between the
  integral of the positive part of `f` and the integral of the negative part of `f`.  -/
theorem integral_eq_lintegral_pos_part_sub_lintegral_neg_part {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    âˆ« a, f a âˆ‚Î¼ =
      ENNReal.toReal (âˆ«â» a, .ofReal (f a) âˆ‚Î¼) - ENNReal.toReal (âˆ«â» a, .ofReal (-f a) âˆ‚Î¼) := by
  let fâ‚ := hf.toL1 f
  -- Go to the `LÂ¹` space
  have eqâ‚ : ENNReal.toReal (âˆ«â» a, ENNReal.ofReal (f a) âˆ‚Î¼) = â€–Lp.posPart fâ‚â€– := by
    rw [L1.norm_def]
    congr 1
    apply lintegral_congr_ae
    filter_upwards [Lp.coeFn_posPart fâ‚, hf.coeFn_toL1] with _ hâ‚ hâ‚‚
    rw [hâ‚, hâ‚‚, ENNReal.ofReal]
    congr 1
    apply NNReal.eq
    rw [Real.nnnorm_of_nonneg (le_max_right _ _)]
    rw [Real.coe_toNNReal', NNReal.coe_mk]
  -- Go to the `LÂ¹` space
  have eqâ‚‚ : ENNReal.toReal (âˆ«â» a, ENNReal.ofReal (-f a) âˆ‚Î¼) = â€–Lp.negPart fâ‚â€– := by
    rw [L1.norm_def]
    congr 1
    apply lintegral_congr_ae
    filter_upwards [Lp.coeFn_negPart fâ‚, hf.coeFn_toL1] with _ hâ‚ hâ‚‚
    rw [hâ‚, hâ‚‚, ENNReal.ofReal]
    congr 1
    apply NNReal.eq
    simp only [Real.coe_toNNReal', coe_nnnorm, nnnorm_neg]
    rw [Real.norm_of_nonpos (min_le_right _ _), â† max_neg_neg, neg_zero]
  rw [eqâ‚, eqâ‚‚, integral, dif_pos, dif_pos]
  exact L1.integral_eq_norm_posPart_sub _
#align measure_theory.integral_eq_lintegral_pos_part_sub_lintegral_neg_part MeasureTheory.integral_eq_lintegral_pos_part_sub_lintegral_neg_part

theorem integral_eq_lintegral_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f)
    (hfm : AEStronglyMeasurable f Î¼) :
    âˆ« a, f a âˆ‚Î¼ = ENNReal.toReal (âˆ«â» a, ENNReal.ofReal (f a) âˆ‚Î¼) := by
  by_cases hfi : Integrable f Î¼
  Â· rw [integral_eq_lintegral_pos_part_sub_lintegral_neg_part hfi]
    have h_min : âˆ«â» a, ENNReal.ofReal (-f a) âˆ‚Î¼ = 0 := by
      rw [lintegral_eq_zero_iff']
      Â· refine hf.mono ?_
        simp only [Pi.zero_apply]
        intro a h
        simp only [h, neg_nonpos, ofReal_eq_zero]
      Â· exact measurable_ofReal.comp_aemeasurable hfm.aemeasurable.neg
    rw [h_min, zero_toReal, _root_.sub_zero]
  Â· rw [integral_undef hfi]
    simp_rw [Integrable, hfm, hasFiniteIntegral_iff_norm, lt_top_iff_ne_top, Ne, true_and_iff,
      Classical.not_not] at hfi
    have : âˆ«â» a : Î±, ENNReal.ofReal (f a) âˆ‚Î¼ = âˆ«â» a, ENNReal.ofReal â€–f aâ€– âˆ‚Î¼ := by
      refine lintegral_congr_ae (hf.mono fun a h => ?_)
      dsimp only
      rw [Real.norm_eq_abs, abs_of_nonneg h]
    rw [this, hfi]; rfl
#align measure_theory.integral_eq_lintegral_of_nonneg_ae MeasureTheory.integral_eq_lintegral_of_nonneg_ae

theorem integral_norm_eq_lintegral_nnnorm {P : Type*} [NormedAddCommGroup P] {f : Î± â†’ P}
    (hf : AEStronglyMeasurable f Î¼) : âˆ« x, â€–f xâ€– âˆ‚Î¼ = ENNReal.toReal (âˆ«â» x, â€–f xâ€–â‚Š âˆ‚Î¼) := by
  rw [integral_eq_lintegral_of_nonneg_ae _ hf.norm]
  Â· simp_rw [ofReal_norm_eq_coe_nnnorm]
  Â· filter_upwards; simp_rw [Pi.zero_apply, norm_nonneg, imp_true_iff]
#align measure_theory.integral_norm_eq_lintegral_nnnorm MeasureTheory.integral_norm_eq_lintegral_nnnorm

theorem ofReal_integral_norm_eq_lintegral_nnnorm {P : Type*} [NormedAddCommGroup P] {f : Î± â†’ P}
    (hf : Integrable f Î¼) : ENNReal.ofReal (âˆ« x, â€–f xâ€– âˆ‚Î¼) = âˆ«â» x, â€–f xâ€–â‚Š âˆ‚Î¼ := by
  rw [integral_norm_eq_lintegral_nnnorm hf.aestronglyMeasurable,
    ENNReal.ofReal_toReal (lt_top_iff_ne_top.mp hf.2)]
#align measure_theory.of_real_integral_norm_eq_lintegral_nnnorm MeasureTheory.ofReal_integral_norm_eq_lintegral_nnnorm

theorem integral_eq_integral_pos_part_sub_integral_neg_part {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    âˆ« a, f a âˆ‚Î¼ = âˆ« a, (Real.toNNReal (f a) : â„) âˆ‚Î¼ - âˆ« a, (Real.toNNReal (-f a) : â„) âˆ‚Î¼ := by
  rw [â† integral_sub hf.real_toNNReal]
  Â· simp
  Â· exact hf.neg.real_toNNReal
#align measure_theory.integral_eq_integral_pos_part_sub_integral_neg_part MeasureTheory.integral_eq_integral_pos_part_sub_integral_neg_part

theorem integral_nonneg_of_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ âˆ« a, f a âˆ‚Î¼ := by
  have A : CompleteSpace â„ := by infer_instance
  simp only [integral_def, A, L1.integral_def, dite_true, ge_iff_le]
  exact setToFun_nonneg (dominatedFinMeasAdditive_weightedSMul Î¼)
    (fun s _ _ => weightedSMul_nonneg s) hf
#align measure_theory.integral_nonneg_of_ae MeasureTheory.integral_nonneg_of_ae

theorem lintegral_coe_eq_integral (f : Î± â†’ â„â‰¥0) (hfi : Integrable (fun x => (f x : â„)) Î¼) :
    âˆ«â» a, f a âˆ‚Î¼ = ENNReal.ofReal (âˆ« a, f a âˆ‚Î¼) := by
  simp_rw [integral_eq_lintegral_of_nonneg_ae (eventually_of_forall fun x => (f x).coe_nonneg)
      hfi.aestronglyMeasurable, â† ENNReal.coe_nnreal_eq]
  rw [ENNReal.ofReal_toReal]
  rw [â† lt_top_iff_ne_top]
  convert hfi.hasFiniteIntegral
  -- Porting note: `convert` no longer unfolds `HasFiniteIntegral`
  simp_rw [HasFiniteIntegral, NNReal.nnnorm_eq]
#align measure_theory.lintegral_coe_eq_integral MeasureTheory.lintegral_coe_eq_integral

theorem ofReal_integral_eq_lintegral_ofReal {f : Î± â†’ â„} (hfi : Integrable f Î¼) (f_nn : 0 â‰¤áµ[Î¼] f) :
    ENNReal.ofReal (âˆ« x, f x âˆ‚Î¼) = âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼ := by
  have : f =áµ[Î¼] (â€–f Â·â€–) := f_nn.mono fun _x hx â†¦ (abs_of_nonneg hx).symm
  simp_rw [integral_congr_ae this, ofReal_integral_norm_eq_lintegral_nnnorm hfi,
    â† ofReal_norm_eq_coe_nnnorm]
  exact lintegral_congr_ae (this.symm.fun_comp ENNReal.ofReal)
#align measure_theory.of_real_integral_eq_lintegral_of_real MeasureTheory.ofReal_integral_eq_lintegral_ofReal

theorem integral_toReal {f : Î± â†’ â„â‰¥0âˆ} (hfm : AEMeasurable f Î¼) (hf : âˆ€áµ x âˆ‚Î¼, f x < âˆ) :
    âˆ« a, (f a).toReal âˆ‚Î¼ = (âˆ«â» a, f a âˆ‚Î¼).toReal := by
  rw [integral_eq_lintegral_of_nonneg_ae _ hfm.ennreal_toReal.aestronglyMeasurable,
    lintegral_congr_ae (ofReal_toReal_ae_eq hf)]
  exact eventually_of_forall fun x => ENNReal.toReal_nonneg
#align measure_theory.integral_to_real MeasureTheory.integral_toReal

theorem lintegral_coe_le_coe_iff_integral_le {f : Î± â†’ â„â‰¥0} (hfi : Integrable (fun x => (f x : â„)) Î¼)
    {b : â„â‰¥0} : âˆ«â» a, f a âˆ‚Î¼ â‰¤ b â†” âˆ« a, (f a : â„) âˆ‚Î¼ â‰¤ b := by
  rw [lintegral_coe_eq_integral f hfi, ENNReal.ofReal, ENNReal.coe_le_coe,
    Real.toNNReal_le_iff_le_coe]
#align measure_theory.lintegral_coe_le_coe_iff_integral_le MeasureTheory.lintegral_coe_le_coe_iff_integral_le

theorem integral_coe_le_of_lintegral_coe_le {f : Î± â†’ â„â‰¥0} {b : â„â‰¥0} (h : âˆ«â» a, f a âˆ‚Î¼ â‰¤ b) :
    âˆ« a, (f a : â„) âˆ‚Î¼ â‰¤ b := by
  by_cases hf : Integrable (fun a => (f a : â„)) Î¼
  Â· exact (lintegral_coe_le_coe_iff_integral_le hf).1 h
  Â· rw [integral_undef hf]; exact b.2
#align measure_theory.integral_coe_le_of_lintegral_coe_le MeasureTheory.integral_coe_le_of_lintegral_coe_le

theorem integral_nonneg {f : Î± â†’ â„} (hf : 0 â‰¤ f) : 0 â‰¤ âˆ« a, f a âˆ‚Î¼ :=
  integral_nonneg_of_ae <| eventually_of_forall hf
#align measure_theory.integral_nonneg MeasureTheory.integral_nonneg

theorem integral_nonpos_of_ae {f : Î± â†’ â„} (hf : f â‰¤áµ[Î¼] 0) : âˆ« a, f a âˆ‚Î¼ â‰¤ 0 := by
  have hf : 0 â‰¤áµ[Î¼] -f := hf.mono fun a h => by rwa [Pi.neg_apply, Pi.zero_apply, neg_nonneg]
  have : 0 â‰¤ âˆ« a, -f a âˆ‚Î¼ := integral_nonneg_of_ae hf
  rwa [integral_neg, neg_nonneg] at this
#align measure_theory.integral_nonpos_of_ae MeasureTheory.integral_nonpos_of_ae

theorem integral_nonpos {f : Î± â†’ â„} (hf : f â‰¤ 0) : âˆ« a, f a âˆ‚Î¼ â‰¤ 0 :=
  integral_nonpos_of_ae <| eventually_of_forall hf
#align measure_theory.integral_nonpos MeasureTheory.integral_nonpos

theorem integral_eq_zero_iff_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hfi : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼] 0 := by
  simp_rw [integral_eq_lintegral_of_nonneg_ae hf hfi.1, ENNReal.toReal_eq_zero_iff,
    â† ENNReal.not_lt_top, â† hasFiniteIntegral_iff_ofReal hf, hfi.2, not_true_eq_false, or_false_iff]
  -- Porting note: split into parts, to make `rw` and `simp` work
  rw [lintegral_eq_zero_iff']
  Â· rw [â† hf.le_iff_eq, Filter.EventuallyEq, Filter.EventuallyLE]
    simp only [Pi.zero_apply, ofReal_eq_zero]
  Â· exact (ENNReal.measurable_ofReal.comp_aemeasurable hfi.1.aemeasurable)
#align measure_theory.integral_eq_zero_iff_of_nonneg_ae MeasureTheory.integral_eq_zero_iff_of_nonneg_ae

theorem integral_eq_zero_iff_of_nonneg {f : Î± â†’ â„} (hf : 0 â‰¤ f) (hfi : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼] 0 :=
  integral_eq_zero_iff_of_nonneg_ae (eventually_of_forall hf) hfi
#align measure_theory.integral_eq_zero_iff_of_nonneg MeasureTheory.integral_eq_zero_iff_of_nonneg

lemma integral_eq_iff_of_ae_le {f g : Î± â†’ â„}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) (hfg : f â‰¤áµ[Î¼] g) :
    âˆ« a, f a âˆ‚Î¼ = âˆ« a, g a âˆ‚Î¼ â†” f =áµ[Î¼] g := by
  refine âŸ¨fun h_le â†¦ EventuallyEq.symm ?_, fun h â†¦ integral_congr_ae hâŸ©
  rw [â† sub_ae_eq_zero,
    â† integral_eq_zero_iff_of_nonneg_ae ((sub_nonneg_ae _ _).mpr hfg) (hg.sub hf)]
  simpa [Pi.sub_apply, integral_sub hg hf, sub_eq_zero, eq_comm]

theorem integral_pos_iff_support_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hfi : Integrable f Î¼) :
    (0 < âˆ« x, f x âˆ‚Î¼) â†” 0 < Î¼ (Function.support f) := by
  simp_rw [(integral_nonneg_of_ae hf).lt_iff_ne, pos_iff_ne_zero, Ne, @eq_comm â„ 0,
    integral_eq_zero_iff_of_nonneg_ae hf hfi, Filter.EventuallyEq, ae_iff, Pi.zero_apply,
    Function.support]
#align measure_theory.integral_pos_iff_support_of_nonneg_ae MeasureTheory.integral_pos_iff_support_of_nonneg_ae

theorem integral_pos_iff_support_of_nonneg {f : Î± â†’ â„} (hf : 0 â‰¤ f) (hfi : Integrable f Î¼) :
    (0 < âˆ« x, f x âˆ‚Î¼) â†” 0 < Î¼ (Function.support f) :=
  integral_pos_iff_support_of_nonneg_ae (eventually_of_forall hf) hfi
#align measure_theory.integral_pos_iff_support_of_nonneg MeasureTheory.integral_pos_iff_support_of_nonneg

lemma integral_exp_pos {Î¼ : Measure Î±} {f : Î± â†’ â„} [hÎ¼ : NeZero Î¼]
    (hf : Integrable (fun x â†¦ Real.exp (f x)) Î¼) :
    0 < âˆ« x, Real.exp (f x) âˆ‚Î¼ := by
  rw [integral_pos_iff_support_of_nonneg (fun x â†¦ (Real.exp_pos _).le) hf]
  suffices (Function.support fun x â†¦ Real.exp (f x)) = Set.univ by simp [this, hÎ¼.out]
  ext1 x
  simp only [Function.mem_support, ne_eq, (Real.exp_pos _).ne', not_false_eq_true, Set.mem_univ]

/-- Monotone convergence theorem for real-valued functions and Bochner integrals -/
lemma integral_tendsto_of_tendsto_of_monotone {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ â„} {F : Î± â†’ â„}
    (hf : âˆ€ n, Integrable (f n) Î¼) (hF : Integrable F Î¼) (h_mono : âˆ€áµ x âˆ‚Î¼, Monotone fun n â†¦ f n x)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (F x))) :
    Tendsto (fun n â†¦ âˆ« x, f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, F x âˆ‚Î¼)) := by
  -- switch from the Bochner to the Lebesgue integral
  let f' := fun n x â†¦ f n x - f 0 x
  have hf'_nonneg : âˆ€áµ x âˆ‚Î¼, âˆ€ n, 0 â‰¤ f' n x := by
    filter_upwards [h_mono] with a ha n
    simp [f', ha (zero_le n)]
  have hf'_meas : âˆ€ n, Integrable (f' n) Î¼ := fun n â†¦ (hf n).sub (hf 0)
  suffices Tendsto (fun n â†¦ âˆ« x, f' n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, (F - f 0) x âˆ‚Î¼)) by
    simp_rw [integral_sub (hf _) (hf _), integral_sub' hF (hf 0), tendsto_sub_const_iff] at this
    exact this
  have hF_ge : 0 â‰¤áµ[Î¼] fun x â†¦ (F - f 0) x := by
    filter_upwards [h_tendsto, h_mono] with x hx_tendsto hx_mono
    simp only [Pi.zero_apply, Pi.sub_apply, sub_nonneg]
    exact ge_of_tendsto' hx_tendsto (fun n â†¦ hx_mono (zero_le _))
  rw [ae_all_iff] at hf'_nonneg
  simp_rw [integral_eq_lintegral_of_nonneg_ae (hf'_nonneg _) (hf'_meas _).1]
  rw [integral_eq_lintegral_of_nonneg_ae hF_ge (hF.1.sub (hf 0).1)]
  have h_cont := ENNReal.continuousAt_toReal (x := âˆ«â» a, ENNReal.ofReal ((F - f 0) a) âˆ‚Î¼) ?_
  swap
  Â· rw [â† ofReal_integral_eq_lintegral_ofReal (hF.sub (hf 0)) hF_ge]
    exact ENNReal.ofReal_ne_top
  refine h_cont.tendsto.comp ?_
  -- use the result for the Lebesgue integral
  refine lintegral_tendsto_of_tendsto_of_monotone ?_ ?_ ?_
  Â· exact fun n â†¦ ((hf n).sub (hf 0)).aemeasurable.ennreal_ofReal
  Â· filter_upwards [h_mono] with x hx n m hnm
    refine ENNReal.ofReal_le_ofReal ?_
    simp only [f', tsub_le_iff_right, sub_add_cancel]
    exact hx hnm
  Â· filter_upwards [h_tendsto] with x hx
    refine (ENNReal.continuous_ofReal.tendsto _).comp ?_
    simp only [Pi.sub_apply]
    exact Tendsto.sub hx tendsto_const_nhds

/-- Monotone convergence theorem for real-valued functions and Bochner integrals -/
lemma integral_tendsto_of_tendsto_of_antitone {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ â„} {F : Î± â†’ â„}
    (hf : âˆ€ n, Integrable (f n) Î¼) (hF : Integrable F Î¼) (h_mono : âˆ€áµ x âˆ‚Î¼, Antitone fun n â†¦ f n x)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (F x))) :
    Tendsto (fun n â†¦ âˆ« x, f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, F x âˆ‚Î¼)) := by
  suffices Tendsto (fun n â†¦ âˆ« x, -f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, -F x âˆ‚Î¼)) by
    suffices Tendsto (fun n â†¦ âˆ« x, - -f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, - -F x âˆ‚Î¼)) by
      simpa [neg_neg] using this
    convert this.neg <;> rw [integral_neg]
  refine integral_tendsto_of_tendsto_of_monotone (fun n â†¦ (hf n).neg) hF.neg ?_ ?_
  Â· filter_upwards [h_mono] with x hx n m hnm using neg_le_neg_iff.mpr <| hx hnm
  Â· filter_upwards [h_tendsto] with x hx using hx.neg

/-- If a monotone sequence of functions has an upper bound and the sequence of integrals of these
functions tends to the integral of the upper bound, then the sequence of functions converges
almost everywhere to the upper bound. -/
lemma tendsto_of_integral_tendsto_of_monotone {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ â„} {F : Î± â†’ â„}
    (hf_int : âˆ€ n, Integrable (f n) Î¼) (hF_int : Integrable F Î¼)
    (hf_tendsto : Tendsto (fun i â†¦ âˆ« a, f i a âˆ‚Î¼) atTop (ğ“ (âˆ« a, F a âˆ‚Î¼)))
    (hf_mono : âˆ€áµ a âˆ‚Î¼, Monotone (fun i â†¦ f i a))
    (hf_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ i, f i a â‰¤ F a) :
    âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F a)) := by
  -- reduce to the `â„â‰¥0âˆ` case
  let f' : â„• â†’ Î± â†’ â„â‰¥0âˆ := fun n a â†¦ ENNReal.ofReal (f n a - f 0 a)
  let F' : Î± â†’ â„â‰¥0âˆ := fun a â†¦ ENNReal.ofReal (F a - f 0 a)
  have hf'_int_eq : âˆ€ i, âˆ«â» a, f' i a âˆ‚Î¼ = ENNReal.ofReal (âˆ« a, f i a âˆ‚Î¼ - âˆ« a, f 0 a âˆ‚Î¼) := by
    intro i
    unfold_let f'
    rw [â† ofReal_integral_eq_lintegral_ofReal, integral_sub (hf_int i) (hf_int 0)]
    Â· exact (hf_int i).sub (hf_int 0)
    Â· filter_upwards [hf_mono] with a h_mono
      simp [h_mono (zero_le i)]
  have hF'_int_eq : âˆ«â» a, F' a âˆ‚Î¼ = ENNReal.ofReal (âˆ« a, F a âˆ‚Î¼ - âˆ« a, f 0 a âˆ‚Î¼) := by
    unfold_let F'
    rw [â† ofReal_integral_eq_lintegral_ofReal, integral_sub hF_int (hf_int 0)]
    Â· exact hF_int.sub (hf_int 0)
    Â· filter_upwards [hf_bound] with a h_bound
      simp [h_bound 0]
  have h_tendsto : Tendsto (fun i â†¦ âˆ«â» a, f' i a âˆ‚Î¼) atTop (ğ“ (âˆ«â» a, F' a âˆ‚Î¼)) := by
    simp_rw [hf'_int_eq, hF'_int_eq]
    refine (ENNReal.continuous_ofReal.tendsto _).comp ?_
    rwa [tendsto_sub_const_iff]
  have h_mono : âˆ€áµ a âˆ‚Î¼, Monotone (fun i â†¦ f' i a) := by
    filter_upwards [hf_mono] with a ha_mono i j hij
    refine ENNReal.ofReal_le_ofReal ?_
    simp [ha_mono hij]
  have h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ i, f' i a â‰¤ F' a := by
    filter_upwards [hf_bound] with a ha_bound i
    refine ENNReal.ofReal_le_ofReal ?_
    simp only [tsub_le_iff_right, sub_add_cancel, ha_bound i]
  -- use the corresponding lemma for `â„â‰¥0âˆ`
  have h := tendsto_of_lintegral_tendsto_of_monotone ?_ h_tendsto h_mono h_bound ?_
  rotate_left
  Â· exact (hF_int.1.aemeasurable.sub (hf_int 0).1.aemeasurable).ennreal_ofReal
  Â· exact ((lintegral_ofReal_le_lintegral_nnnorm _).trans_lt (hF_int.sub (hf_int 0)).2).ne
  filter_upwards [h, hf_mono, hf_bound] with a ha ha_mono ha_bound
  have h1 : (fun i â†¦ f i a) = fun i â†¦ (f' i a).toReal + f 0 a := by
    unfold_let f'
    ext i
    rw [ENNReal.toReal_ofReal]
    Â· abel
    Â· simp [ha_mono (zero_le i)]
  have h2 : F a = (F' a).toReal + f 0 a := by
    unfold_let F'
    rw [ENNReal.toReal_ofReal]
    Â· abel
    Â· simp [ha_bound 0]
  rw [h1, h2]
  refine Filter.Tendsto.add ?_ tendsto_const_nhds
  exact (ENNReal.continuousAt_toReal ENNReal.ofReal_ne_top).tendsto.comp ha

/-- If an antitone sequence of functions has a lower bound and the sequence of integrals of these
functions tends to the integral of the lower bound, then the sequence of functions converges
almost everywhere to the lower bound. -/
lemma tendsto_of_integral_tendsto_of_antitone {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ â„} {F : Î± â†’ â„}
    (hf_int : âˆ€ n, Integrable (f n) Î¼) (hF_int : Integrable F Î¼)
    (hf_tendsto : Tendsto (fun i â†¦ âˆ« a, f i a âˆ‚Î¼) atTop (ğ“ (âˆ« a, F a âˆ‚Î¼)))
    (hf_mono : âˆ€áµ a âˆ‚Î¼, Antitone (fun i â†¦ f i a))
    (hf_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ i, F a â‰¤ f i a) :
    âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F a)) := by
  let f' : â„• â†’ Î± â†’ â„ := fun i a â†¦ - f i a
  let F' : Î± â†’ â„ := fun a â†¦ - F a
  suffices âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f' i a) atTop (ğ“ (F' a)) by
    filter_upwards [this] with a ha_tendsto
    convert ha_tendsto.neg
    Â· simp [f']
    Â· simp [F']
  refine tendsto_of_integral_tendsto_of_monotone (fun n â†¦ (hf_int n).neg) hF_int.neg ?_ ?_ ?_
  Â· convert hf_tendsto.neg
    Â· rw [integral_neg]
    Â· rw [integral_neg]
  Â· filter_upwards [hf_mono] with a ha i j hij
    simp [f', ha hij]
  Â· filter_upwards [hf_bound] with a ha i
    simp [f', F', ha i]

section NormedAddCommGroup

variable {H : Type*} [NormedAddCommGroup H]

theorem L1.norm_eq_integral_norm (f : Î± â†’â‚[Î¼] H) : â€–fâ€– = âˆ« a, â€–f aâ€– âˆ‚Î¼ := by
  simp only [snorm, snorm', ENNReal.one_toReal, ENNReal.rpow_one, Lp.norm_def, if_false,
    ENNReal.one_ne_top, one_ne_zero, _root_.div_one]
  rw [integral_eq_lintegral_of_nonneg_ae (eventually_of_forall (by simp [norm_nonneg]))
      (Lp.aestronglyMeasurable f).norm]
  simp [ofReal_norm_eq_coe_nnnorm]
set_option linter.uppercaseLean3 false in
#align measure_theory.L1.norm_eq_integral_norm MeasureTheory.L1.norm_eq_integral_norm

theorem L1.dist_eq_integral_dist (f g : Î± â†’â‚[Î¼] H) : dist f g = âˆ« a, dist (f a) (g a) âˆ‚Î¼ := by
  simp only [dist_eq_norm, L1.norm_eq_integral_norm]
  exact integral_congr_ae <| (Lp.coeFn_sub _ _).fun_comp norm

theorem L1.norm_of_fun_eq_integral_norm {f : Î± â†’ H} (hf : Integrable f Î¼) :
    â€–hf.toL1 fâ€– = âˆ« a, â€–f aâ€– âˆ‚Î¼ := by
  rw [L1.norm_eq_integral_norm]
  exact integral_congr_ae <| hf.coeFn_toL1.fun_comp _
set_option linter.uppercaseLean3 false in
#align measure_theory.L1.norm_of_fun_eq_integral_norm MeasureTheory.L1.norm_of_fun_eq_integral_norm

theorem Memâ„’p.snorm_eq_integral_rpow_norm {f : Î± â†’ H} {p : â„â‰¥0âˆ} (hp1 : p â‰  0) (hp2 : p â‰  âˆ)
    (hf : Memâ„’p f p Î¼) :
    snorm f p Î¼ = ENNReal.ofReal ((âˆ« a, â€–f aâ€– ^ p.toReal âˆ‚Î¼) ^ p.toRealâ»Â¹) := by
  have A : âˆ«â» a : Î±, ENNReal.ofReal (â€–f aâ€– ^ p.toReal) âˆ‚Î¼ = âˆ«â» a : Î±, â€–f aâ€–â‚Š ^ p.toReal âˆ‚Î¼ := by
    simp_rw [â† ofReal_rpow_of_nonneg (norm_nonneg _) toReal_nonneg, ofReal_norm_eq_coe_nnnorm]
  simp only [snorm_eq_lintegral_rpow_nnnorm hp1 hp2, one_div]
  rw [integral_eq_lintegral_of_nonneg_ae]; rotate_left
  Â· exact ae_of_all _ fun x => by positivity
  Â· exact (hf.aestronglyMeasurable.norm.aemeasurable.pow_const _).aestronglyMeasurable
  rw [A, â† ofReal_rpow_of_nonneg toReal_nonneg (inv_nonneg.2 toReal_nonneg), ofReal_toReal]
  exact (lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top hp1 hp2 hf.2).ne
#align measure_theory.mem_â„’p.snorm_eq_integral_rpow_norm MeasureTheory.Memâ„’p.snorm_eq_integral_rpow_norm

end NormedAddCommGroup

theorem integral_mono_ae {f g : Î± â†’ â„} (hf : Integrable f Î¼) (hg : Integrable g Î¼) (h : f â‰¤áµ[Î¼] g) :
    âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, g a âˆ‚Î¼ := by
  have A : CompleteSpace â„ := by infer_instance
  simp only [integral, A, L1.integral]
  exact setToFun_mono (dominatedFinMeasAdditive_weightedSMul Î¼)
    (fun s _ _ => weightedSMul_nonneg s) hf hg h
#align measure_theory.integral_mono_ae MeasureTheory.integral_mono_ae

@[mono]
theorem integral_mono {f g : Î± â†’ â„} (hf : Integrable f Î¼) (hg : Integrable g Î¼) (h : f â‰¤ g) :
    âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, g a âˆ‚Î¼ :=
  integral_mono_ae hf hg <| eventually_of_forall h
#align measure_theory.integral_mono MeasureTheory.integral_mono

theorem integral_mono_of_nonneg {f g : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hgi : Integrable g Î¼)
    (h : f â‰¤áµ[Î¼] g) : âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, g a âˆ‚Î¼ := by
  by_cases hfm : AEStronglyMeasurable f Î¼
  Â· refine integral_mono_ae âŸ¨hfm, ?_âŸ© hgi h
    refine hgi.hasFiniteIntegral.mono <| h.mp <| hf.mono fun x hf hfg => ?_
    simpa [abs_of_nonneg hf, abs_of_nonneg (le_trans hf hfg)]
  Â· rw [integral_non_aestronglyMeasurable hfm]
    exact integral_nonneg_of_ae (hf.trans h)
#align measure_theory.integral_mono_of_nonneg MeasureTheory.integral_mono_of_nonneg

theorem integral_mono_measure {f : Î± â†’ â„} {Î½} (hle : Î¼ â‰¤ Î½) (hf : 0 â‰¤áµ[Î½] f)
    (hfi : Integrable f Î½) : âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, f a âˆ‚Î½ := by
  have hfi' : Integrable f Î¼ := hfi.mono_measure hle
  have hf' : 0 â‰¤áµ[Î¼] f := hle.absolutelyContinuous hf
  rw [integral_eq_lintegral_of_nonneg_ae hf' hfi'.1, integral_eq_lintegral_of_nonneg_ae hf hfi.1,
    ENNReal.toReal_le_toReal]
  exacts [lintegral_mono' hle le_rfl, ((hasFiniteIntegral_iff_ofReal hf').1 hfi'.2).ne,
    ((hasFiniteIntegral_iff_ofReal hf).1 hfi.2).ne]
#align measure_theory.integral_mono_measure MeasureTheory.integral_mono_measure

theorem norm_integral_le_integral_norm (f : Î± â†’ G) : â€–âˆ« a, f a âˆ‚Î¼â€– â‰¤ âˆ« a, â€–f aâ€– âˆ‚Î¼ := by
  have le_ae : âˆ€áµ a âˆ‚Î¼, 0 â‰¤ â€–f aâ€– := eventually_of_forall fun a => norm_nonneg _
  by_cases h : AEStronglyMeasurable f Î¼
  Â· calc
      â€–âˆ« a, f a âˆ‚Î¼â€– â‰¤ ENNReal.toReal (âˆ«â» a, ENNReal.ofReal â€–f aâ€– âˆ‚Î¼) :=
        norm_integral_le_lintegral_norm _
      _ = âˆ« a, â€–f aâ€– âˆ‚Î¼ := (integral_eq_lintegral_of_nonneg_ae le_ae <| h.norm).symm
  Â· rw [integral_non_aestronglyMeasurable h, norm_zero]
    exact integral_nonneg_of_ae le_ae
#align measure_theory.norm_integral_le_integral_norm MeasureTheory.norm_integral_le_integral_norm

theorem norm_integral_le_of_norm_le {f : Î± â†’ G} {g : Î± â†’ â„} (hg : Integrable g Î¼)
    (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ g x) : â€–âˆ« x, f x âˆ‚Î¼â€– â‰¤ âˆ« x, g x âˆ‚Î¼ :=
  calc
    â€–âˆ« x, f x âˆ‚Î¼â€– â‰¤ âˆ« x, â€–f xâ€– âˆ‚Î¼ := norm_integral_le_integral_norm f
    _ â‰¤ âˆ« x, g x âˆ‚Î¼ := integral_mono_of_nonneg (eventually_of_forall fun _ => norm_nonneg _) hg h
#align measure_theory.norm_integral_le_of_norm_le MeasureTheory.norm_integral_le_of_norm_le

theorem SimpleFunc.integral_eq_integral (f : Î± â†’â‚› E) (hfi : Integrable f Î¼) :
    f.integral Î¼ = âˆ« x, f x âˆ‚Î¼ := by
  rw [MeasureTheory.integral_eq f hfi, â† L1.SimpleFunc.toLp_one_eq_toL1,
    L1.SimpleFunc.integral_L1_eq_integral, L1.SimpleFunc.integral_eq_integral]
  exact SimpleFunc.integral_congr hfi (Lp.simpleFunc.toSimpleFunc_toLp _ _).symm
#align measure_theory.simple_func.integral_eq_integral MeasureTheory.SimpleFunc.integral_eq_integral

theorem SimpleFunc.integral_eq_sum (f : Î± â†’â‚› E) (hfi : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = âˆ‘ x âˆˆ f.range, ENNReal.toReal (Î¼ (f â»Â¹' {x})) â€¢ x := by
  rw [â† f.integral_eq_integral hfi, SimpleFunc.integral, â† SimpleFunc.integral_eq]; rfl
#align measure_theory.simple_func.integral_eq_sum MeasureTheory.SimpleFunc.integral_eq_sum

@[simp]
theorem integral_const (c : E) : âˆ« _ : Î±, c âˆ‚Î¼ = (Î¼ univ).toReal â€¢ c := by
  cases' (@le_top _ _ _ (Î¼ univ)).lt_or_eq with hÎ¼ hÎ¼
  Â· haveI : IsFiniteMeasure Î¼ := âŸ¨hÎ¼âŸ©
    simp only [integral, hE, L1.integral]
    exact setToFun_const (dominatedFinMeasAdditive_weightedSMul _) _
  Â· by_cases hc : c = 0
    Â· simp [hc, integral_zero]
    Â· have : Â¬Integrable (fun _ : Î± => c) Î¼ := by
        simp only [integrable_const_iff, not_or]
        exact âŸ¨hc, hÎ¼.not_ltâŸ©
      simp [integral_undef, *]
#align measure_theory.integral_const MeasureTheory.integral_const

theorem norm_integral_le_of_norm_le_const [IsFiniteMeasure Î¼] {f : Î± â†’ G} {C : â„}
    (h : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ C) : â€–âˆ« x, f x âˆ‚Î¼â€– â‰¤ C * (Î¼ univ).toReal :=
  calc
    â€–âˆ« x, f x âˆ‚Î¼â€– â‰¤ âˆ« _, C âˆ‚Î¼ := norm_integral_le_of_norm_le (integrable_const C) h
    _ = C * (Î¼ univ).toReal := by rw [integral_const, smul_eq_mul, mul_comm]
#align measure_theory.norm_integral_le_of_norm_le_const MeasureTheory.norm_integral_le_of_norm_le_const

theorem tendsto_integral_approxOn_of_measurable [MeasurableSpace E] [BorelSpace E] {f : Î± â†’ E}
    {s : Set E} [SeparableSpace s] (hfi : Integrable f Î¼) (hfm : Measurable f)
    (hs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s) {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) (hâ‚€i : Integrable (fun _ => yâ‚€) Î¼) :
    Tendsto (fun n => (SimpleFunc.approxOn f hfm s yâ‚€ hâ‚€ n).integral Î¼)
      atTop (ğ“ <| âˆ« x, f x âˆ‚Î¼) := by
  have hfi' := SimpleFunc.integrable_approxOn hfm hfi hâ‚€ hâ‚€i
  simp only [SimpleFunc.integral_eq_integral _ (hfi' _), integral, hE, L1.integral]
  exact tendsto_setToFun_approxOn_of_measurable (dominatedFinMeasAdditive_weightedSMul Î¼)
    hfi hfm hs hâ‚€ hâ‚€i
#align measure_theory.tendsto_integral_approx_on_of_measurable MeasureTheory.tendsto_integral_approxOn_of_measurable

theorem tendsto_integral_approxOn_of_measurable_of_range_subset [MeasurableSpace E] [BorelSpace E]
    {f : Î± â†’ E} (fmeas : Measurable f) (hf : Integrable f Î¼) (s : Set E) [SeparableSpace s]
    (hs : range f âˆª {0} âŠ† s) :
    Tendsto (fun n => (SimpleFunc.approxOn f fmeas s 0 (hs <| by simp) n).integral Î¼) atTop
      (ğ“ <| âˆ« x, f x âˆ‚Î¼) := by
  apply tendsto_integral_approxOn_of_measurable hf fmeas _ _ (integrable_zero _ _ _)
  exact eventually_of_forall fun x => subset_closure (hs (Set.mem_union_left _ (mem_range_self _)))
#align measure_theory.tendsto_integral_approx_on_of_measurable_of_range_subset MeasureTheory.tendsto_integral_approxOn_of_measurable_of_range_subset

theorem tendsto_integral_norm_approxOn_sub [MeasurableSpace E] [BorelSpace E] {f : Î± â†’ E}
    (fmeas : Measurable f) (hf : Integrable f Î¼) [SeparableSpace (range f âˆª {0} : Set E)] :
    Tendsto (fun n â†¦ âˆ« x, â€–SimpleFunc.approxOn f fmeas (range f âˆª {0}) 0 (by simp) n x - f xâ€– âˆ‚Î¼)
      atTop (ğ“ 0) := by
  convert (tendsto_toReal zero_ne_top).comp (tendsto_approxOn_range_L1_nnnorm fmeas hf) with n
  rw [integral_norm_eq_lintegral_nnnorm]
  Â· simp
  Â· apply (SimpleFunc.aestronglyMeasurable _).sub
    apply (stronglyMeasurable_iff_measurable_separable.2 âŸ¨fmeas, ?_âŸ© ).aestronglyMeasurable
    exact .mono (.of_subtype (range f âˆª {0})) (subset_union_left _ _)

variable {Î½ : Measure Î±}

theorem integral_add_measure {f : Î± â†’ G} (hÎ¼ : Integrable f Î¼) (hÎ½ : Integrable f Î½) :
    âˆ« x, f x âˆ‚(Î¼ + Î½) = âˆ« x, f x âˆ‚Î¼ + âˆ« x, f x âˆ‚Î½ := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [integral, hG]
  have hfi := hÎ¼.add_measure hÎ½
  simp_rw [integral_eq_setToFun]
  have hÎ¼_dfma : DominatedFinMeasAdditive (Î¼ + Î½) (weightedSMul Î¼ : Set Î± â†’ G â†’L[â„] G) 1 :=
    DominatedFinMeasAdditive.add_measure_right Î¼ Î½ (dominatedFinMeasAdditive_weightedSMul Î¼)
      zero_le_one
  have hÎ½_dfma : DominatedFinMeasAdditive (Î¼ + Î½) (weightedSMul Î½ : Set Î± â†’ G â†’L[â„] G) 1 :=
    DominatedFinMeasAdditive.add_measure_left Î¼ Î½ (dominatedFinMeasAdditive_weightedSMul Î½)
      zero_le_one
  rw [â† setToFun_congr_measure_of_add_right hÎ¼_dfma
        (dominatedFinMeasAdditive_weightedSMul Î¼) f hfi,
    â† setToFun_congr_measure_of_add_left hÎ½_dfma (dominatedFinMeasAdditive_weightedSMul Î½) f hfi]
  refine setToFun_add_left' _ _ _ (fun s _ hÎ¼Î½s => ?_) f
  rw [Measure.coe_add, Pi.add_apply, add_lt_top] at hÎ¼Î½s
  rw [weightedSMul, weightedSMul, weightedSMul, â† add_smul, Measure.coe_add, Pi.add_apply,
  toReal_add hÎ¼Î½s.1.ne hÎ¼Î½s.2.ne]
#align measure_theory.integral_add_measure MeasureTheory.integral_add_measure

@[simp]
theorem integral_zero_measure {m : MeasurableSpace Î±} (f : Î± â†’ G) :
    (âˆ« x, f x âˆ‚(0 : Measure Î±)) = 0 := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact setToFun_measure_zero (dominatedFinMeasAdditive_weightedSMul _) rfl
  Â· simp [integral, hG]
#align measure_theory.integral_zero_measure MeasureTheory.integral_zero_measure

theorem integral_finset_sum_measure {Î¹} {m : MeasurableSpace Î±} {f : Î± â†’ G} {Î¼ : Î¹ â†’ Measure Î±}
    {s : Finset Î¹} (hf : âˆ€ i âˆˆ s, Integrable f (Î¼ i)) :
    âˆ« a, f a âˆ‚(âˆ‘ i âˆˆ s, Î¼ i) = âˆ‘ i âˆˆ s, âˆ« a, f a âˆ‚Î¼ i := by
  induction s using Finset.cons_induction_on with
  | hâ‚ => simp
  | hâ‚‚ h ih =>
    rw [Finset.forall_mem_cons] at hf
    rw [Finset.sum_cons, Finset.sum_cons, â† ih hf.2]
    exact integral_add_measure hf.1 (integrable_finset_sum_measure.2 hf.2)
#align measure_theory.integral_finset_sum_measure MeasureTheory.integral_finset_sum_measure

theorem nndist_integral_add_measure_le_lintegral
    {f : Î± â†’ G} (hâ‚ : Integrable f Î¼) (hâ‚‚ : Integrable f Î½) :
    (nndist (âˆ« x, f x âˆ‚Î¼) (âˆ« x, f x âˆ‚(Î¼ + Î½)) : â„â‰¥0âˆ) â‰¤ âˆ«â» x, â€–f xâ€–â‚Š âˆ‚Î½ := by
  rw [integral_add_measure hâ‚ hâ‚‚, nndist_comm, nndist_eq_nnnorm, add_sub_cancel_left]
  exact ennnorm_integral_le_lintegral_ennnorm _
#align measure_theory.nndist_integral_add_measure_le_lintegral MeasureTheory.nndist_integral_add_measure_le_lintegral

theorem hasSum_integral_measure {Î¹} {m : MeasurableSpace Î±} {f : Î± â†’ G} {Î¼ : Î¹ â†’ Measure Î±}
    (hf : Integrable f (Measure.sum Î¼)) :
    HasSum (fun i => âˆ« a, f a âˆ‚Î¼ i) (âˆ« a, f a âˆ‚Measure.sum Î¼) := by
  have hfi : âˆ€ i, Integrable f (Î¼ i) := fun i => hf.mono_measure (Measure.le_sum _ _)
  simp only [HasSum, â† integral_finset_sum_measure fun i _ => hfi i]
  refine Metric.nhds_basis_ball.tendsto_right_iff.mpr fun Îµ Îµ0 => ?_
  lift Îµ to â„â‰¥0 using Îµ0.le
  have hf_lt : (âˆ«â» x, â€–f xâ€–â‚Š âˆ‚Measure.sum Î¼) < âˆ := hf.2
  have hmem : âˆ€á¶  y in ğ“ (âˆ«â» x, â€–f xâ€–â‚Š âˆ‚Measure.sum Î¼), (âˆ«â» x, â€–f xâ€–â‚Š âˆ‚Measure.sum Î¼) < y + Îµ := by
    refine tendsto_id.add tendsto_const_nhds (lt_mem_nhds (Î± := â„â‰¥0âˆ) <| ENNReal.lt_add_right ?_ ?_)
    exacts [hf_lt.ne, ENNReal.coe_ne_zero.2 (NNReal.coe_ne_zero.1 Îµ0.ne')]
  refine ((hasSum_lintegral_measure (fun x => â€–f xâ€–â‚Š) Î¼).eventually hmem).mono fun s hs => ?_
  obtain âŸ¨Î½, hÎ½âŸ© : âˆƒ Î½, (âˆ‘ i âˆˆ s, Î¼ i) + Î½ = Measure.sum Î¼ := by
    refine âŸ¨Measure.sum fun i : â†¥(sá¶œ : Set Î¹) => Î¼ i, ?_âŸ©
    simpa only [â† Measure.sum_coe_finset] using Measure.sum_add_sum_compl (s : Set Î¹) Î¼
  rw [Metric.mem_ball, â† coe_nndist, NNReal.coe_lt_coe, â† ENNReal.coe_lt_coe, â† hÎ½]
  rw [â† hÎ½, integrable_add_measure] at hf
  refine (nndist_integral_add_measure_le_lintegral hf.1 hf.2).trans_lt ?_
  rw [â† hÎ½, lintegral_add_measure, lintegral_finset_sum_measure] at hs
  exact lt_of_add_lt_add_left hs
#align measure_theory.has_sum_integral_measure MeasureTheory.hasSum_integral_measure

theorem integral_sum_measure {Î¹} {_ : MeasurableSpace Î±} {f : Î± â†’ G} {Î¼ : Î¹ â†’ Measure Î±}
    (hf : Integrable f (Measure.sum Î¼)) : âˆ« a, f a âˆ‚Measure.sum Î¼ = âˆ‘' i, âˆ« a, f a âˆ‚Î¼ i :=
  (hasSum_integral_measure hf).tsum_eq.symm
#align measure_theory.integral_sum_measure MeasureTheory.integral_sum_measure

@[simp]
theorem integral_smul_measure (f : Î± â†’ G) (c : â„â‰¥0âˆ) :
    âˆ« x, f x âˆ‚c â€¢ Î¼ = c.toReal â€¢ âˆ« x, f x âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [integral, hG]
  -- First we consider the â€œdegenerateâ€ case `c = âˆ`
  rcases eq_or_ne c âˆ with (rfl | hc)
  Â· rw [ENNReal.top_toReal, zero_smul, integral_eq_setToFun, setToFun_top_smul_measure]
  -- Main case: `c â‰  âˆ`
  simp_rw [integral_eq_setToFun, â† setToFun_smul_left]
  have hdfma : DominatedFinMeasAdditive Î¼ (weightedSMul (c â€¢ Î¼) : Set Î± â†’ G â†’L[â„] G) c.toReal :=
    mul_one c.toReal â–¸ (dominatedFinMeasAdditive_weightedSMul (c â€¢ Î¼)).of_smul_measure c hc
  have hdfma_smul := dominatedFinMeasAdditive_weightedSMul (F := G) (c â€¢ Î¼)
  rw [â† setToFun_congr_smul_measure c hc hdfma hdfma_smul f]
  exact setToFun_congr_left' _ _ (fun s _ _ => weightedSMul_smul_measure Î¼ c) f
#align measure_theory.integral_smul_measure MeasureTheory.integral_smul_measure

@[simp]
theorem integral_smul_nnreal_measure (f : Î± â†’ G) (c : â„â‰¥0) :
    âˆ« x, f x âˆ‚(c â€¢ Î¼) = c â€¢ âˆ« x, f x âˆ‚Î¼ :=
  integral_smul_measure f (c : â„â‰¥0âˆ)

theorem integral_map_of_stronglyMeasurable {Î²} [MeasurableSpace Î²] {Ï† : Î± â†’ Î²} (hÏ† : Measurable Ï†)
    {f : Î² â†’ G} (hfm : StronglyMeasurable f) : âˆ« y, f y âˆ‚Measure.map Ï† Î¼ = âˆ« x, f (Ï† x) âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [integral, hG]
  by_cases hfi : Integrable f (Measure.map Ï† Î¼); swap
  Â· rw [integral_undef hfi, integral_undef]
    exact fun hfÏ† => hfi ((integrable_map_measure hfm.aestronglyMeasurable hÏ†.aemeasurable).2 hfÏ†)
  borelize G
  have : SeparableSpace (range f âˆª {0} : Set G) := hfm.separableSpace_range_union_singleton
  refine tendsto_nhds_unique
    (tendsto_integral_approxOn_of_measurable_of_range_subset hfm.measurable hfi _ Subset.rfl) ?_
  convert tendsto_integral_approxOn_of_measurable_of_range_subset (hfm.measurable.comp hÏ†)
    ((integrable_map_measure hfm.aestronglyMeasurable hÏ†.aemeasurable).1 hfi) (range f âˆª {0})
    (by simp [insert_subset_insert, Set.range_comp_subset_range]) using 1
  ext1 i
  simp only [SimpleFunc.approxOn_comp, SimpleFunc.integral_eq, Measure.map_apply, hÏ†,
    SimpleFunc.measurableSet_preimage, â† preimage_comp, SimpleFunc.coe_comp]
  refine (Finset.sum_subset (SimpleFunc.range_comp_subset_range _ hÏ†) fun y _ hy => ?_).symm
  rw [SimpleFunc.mem_range, â† Set.preimage_singleton_eq_empty, SimpleFunc.coe_comp] at hy
  rw [hy]
  simp
#align measure_theory.integral_map_of_strongly_measurable MeasureTheory.integral_map_of_stronglyMeasurable

theorem integral_map {Î²} [MeasurableSpace Î²] {Ï† : Î± â†’ Î²} (hÏ† : AEMeasurable Ï† Î¼) {f : Î² â†’ G}
    (hfm : AEStronglyMeasurable f (Measure.map Ï† Î¼)) :
    âˆ« y, f y âˆ‚Measure.map Ï† Î¼ = âˆ« x, f (Ï† x) âˆ‚Î¼ :=
  let g := hfm.mk f
  calc
    âˆ« y, f y âˆ‚Measure.map Ï† Î¼ = âˆ« y, g y âˆ‚Measure.map Ï† Î¼ := integral_congr_ae hfm.ae_eq_mk
    _ = âˆ« y, g y âˆ‚Measure.map (hÏ†.mk Ï†) Î¼ := by congr 1; exact Measure.map_congr hÏ†.ae_eq_mk
    _ = âˆ« x, g (hÏ†.mk Ï† x) âˆ‚Î¼ :=
      (integral_map_of_stronglyMeasurable hÏ†.measurable_mk hfm.stronglyMeasurable_mk)
    _ = âˆ« x, g (Ï† x) âˆ‚Î¼ := integral_congr_ae (hÏ†.ae_eq_mk.symm.fun_comp _)
    _ = âˆ« x, f (Ï† x) âˆ‚Î¼ := integral_congr_ae <| ae_eq_comp hÏ† hfm.ae_eq_mk.symm
#align measure_theory.integral_map MeasureTheory.integral_map

theorem _root_.MeasurableEmbedding.integral_map {Î²} {_ : MeasurableSpace Î²} {f : Î± â†’ Î²}
    (hf : MeasurableEmbedding f) (g : Î² â†’ G) : âˆ« y, g y âˆ‚Measure.map f Î¼ = âˆ« x, g (f x) âˆ‚Î¼ := by
  by_cases hgm : AEStronglyMeasurable g (Measure.map f Î¼)
  Â· exact MeasureTheory.integral_map hf.measurable.aemeasurable hgm
  Â· rw [integral_non_aestronglyMeasurable hgm, integral_non_aestronglyMeasurable]
    exact fun hgf => hgm (hf.aestronglyMeasurable_map_iff.2 hgf)
#align measurable_embedding.integral_map MeasurableEmbedding.integral_map

theorem _root_.ClosedEmbedding.integral_map {Î²} [TopologicalSpace Î±] [BorelSpace Î±]
    [TopologicalSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²] {Ï† : Î± â†’ Î²} (hÏ† : ClosedEmbedding Ï†)
    (f : Î² â†’ G) : âˆ« y, f y âˆ‚Measure.map Ï† Î¼ = âˆ« x, f (Ï† x) âˆ‚Î¼ :=
  hÏ†.measurableEmbedding.integral_map _
#align closed_embedding.integral_map ClosedEmbedding.integral_map

theorem integral_map_equiv {Î²} [MeasurableSpace Î²] (e : Î± â‰ƒáµ Î²) (f : Î² â†’ G) :
    âˆ« y, f y âˆ‚Measure.map e Î¼ = âˆ« x, f (e x) âˆ‚Î¼ :=
  e.measurableEmbedding.integral_map f
#align measure_theory.integral_map_equiv MeasureTheory.integral_map_equiv

theorem MeasurePreserving.integral_comp {Î²} {_ : MeasurableSpace Î²} {f : Î± â†’ Î²} {Î½}
    (hâ‚ : MeasurePreserving f Î¼ Î½) (hâ‚‚ : MeasurableEmbedding f) (g : Î² â†’ G) :
    âˆ« x, g (f x) âˆ‚Î¼ = âˆ« y, g y âˆ‚Î½ :=
  hâ‚.map_eq â–¸ (hâ‚‚.integral_map g).symm
#align measure_theory.measure_preserving.integral_comp MeasureTheory.MeasurePreserving.integral_comp

theorem MeasurePreserving.integral_comp' {Î²} [MeasurableSpace Î²] {Î½} {f : Î± â‰ƒáµ Î²}
    (h : MeasurePreserving f Î¼ Î½) (g : Î² â†’ G) :
    âˆ« x, g (f x) âˆ‚Î¼ = âˆ« y, g y âˆ‚Î½ := MeasurePreserving.integral_comp h f.measurableEmbedding _

theorem integral_subtype_comap {Î±} [MeasurableSpace Î±] {Î¼ : Measure Î±} {s : Set Î±}
    (hs : MeasurableSet s) (f : Î± â†’ G) :
    âˆ« x : s, f (x : Î±) âˆ‚(Measure.comap Subtype.val Î¼) = âˆ« x in s, f x âˆ‚Î¼ := by
  rw [â† map_comap_subtype_coe hs]
  exact ((MeasurableEmbedding.subtype_coe hs).integral_map _).symm

attribute [local instance] Measure.Subtype.measureSpace in
theorem integral_subtype {Î±} [MeasureSpace Î±] {s : Set Î±} (hs : MeasurableSet s) (f : Î± â†’ G) :
    âˆ« x : s, f x = âˆ« x in s, f x := integral_subtype_comap hs f
#align measure_theory.set_integral_eq_subtype MeasureTheory.integral_subtype

@[simp]
theorem integral_dirac' [MeasurableSpace Î±] (f : Î± â†’ E) (a : Î±) (hfm : StronglyMeasurable f) :
    âˆ« x, f x âˆ‚Measure.dirac a = f a := by
  borelize E
  calc
    âˆ« x, f x âˆ‚Measure.dirac a = âˆ« _, f a âˆ‚Measure.dirac a :=
      integral_congr_ae <| ae_eq_dirac' hfm.measurable
    _ = f a := by simp [Measure.dirac_apply_of_mem]
#align measure_theory.integral_dirac' MeasureTheory.integral_dirac'

@[simp]
theorem integral_dirac [MeasurableSpace Î±] [MeasurableSingletonClass Î±] (f : Î± â†’ E) (a : Î±) :
    âˆ« x, f x âˆ‚Measure.dirac a = f a :=
  calc
    âˆ« x, f x âˆ‚Measure.dirac a = âˆ« _, f a âˆ‚Measure.dirac a := integral_congr_ae <| ae_eq_dirac f
    _ = f a := by simp [Measure.dirac_apply_of_mem]
#align measure_theory.integral_dirac MeasureTheory.integral_dirac

theorem setIntegral_dirac' {mÎ± : MeasurableSpace Î±} {f : Î± â†’ E} (hf : StronglyMeasurable f) (a : Î±)
    {s : Set Î±} (hs : MeasurableSet s) [Decidable (a âˆˆ s)] :
    âˆ« x in s, f x âˆ‚Measure.dirac a = if a âˆˆ s then f a else 0 := by
  rw [restrict_dirac' hs]
  split_ifs
  Â· exact integral_dirac' _ _ hf
  Â· exact integral_zero_measure _
#align measure_theory.set_integral_dirac' MeasureTheory.setIntegral_dirac'

@[deprecated (since := "2024-04-17")]
alias set_integral_dirac' := setIntegral_dirac'

theorem setIntegral_dirac [MeasurableSpace Î±] [MeasurableSingletonClass Î±] (f : Î± â†’ E) (a : Î±)
    (s : Set Î±) [Decidable (a âˆˆ s)] :
    âˆ« x in s, f x âˆ‚Measure.dirac a = if a âˆˆ s then f a else 0 := by
  rw [restrict_dirac]
  split_ifs
  Â· exact integral_dirac _ _
  Â· exact integral_zero_measure _
#align measure_theory.set_integral_dirac MeasureTheory.setIntegral_dirac

@[deprecated (since := "2024-04-17")]
alias set_integral_dirac := setIntegral_dirac

/-- **Markov's inequality** also known as **Chebyshev's first inequality**. -/
theorem mul_meas_ge_le_integral_of_nonneg {f : Î± â†’ â„} (hf_nonneg : 0 â‰¤áµ[Î¼] f)
    (hf_int : Integrable f Î¼) (Îµ : â„) : Îµ * (Î¼ { x | Îµ â‰¤ f x }).toReal â‰¤ âˆ« x, f x âˆ‚Î¼ := by
  cases' eq_top_or_lt_top (Î¼ {x | Îµ â‰¤ f x}) with hÎ¼ hÎ¼
  Â· simpa [hÎ¼] using integral_nonneg_of_ae hf_nonneg
  Â· have := Fact.mk hÎ¼
    calc
      Îµ * (Î¼ { x | Îµ â‰¤ f x }).toReal = âˆ« _ in {x | Îµ â‰¤ f x}, Îµ âˆ‚Î¼ := by simp [mul_comm]
      _ â‰¤ âˆ« x in {x | Îµ â‰¤ f x}, f x âˆ‚Î¼ :=
        integral_mono_ae (integrable_const _) (hf_int.mono_measure Î¼.restrict_le_self) <|
          ae_restrict_memâ‚€ <| hf_int.aemeasurable.nullMeasurable measurableSet_Ici
      _ â‰¤ _ := integral_mono_measure Î¼.restrict_le_self hf_nonneg hf_int
#align measure_theory.mul_meas_ge_le_integral_of_nonneg MeasureTheory.mul_meas_ge_le_integral_of_nonneg

/-- HÃ¶lder's inequality for the integral of a product of norms. The integral of the product of two
norms of functions is bounded by the product of their `â„’p` and `â„’q` seminorms when `p` and `q` are
conjugate exponents. -/
theorem integral_mul_norm_le_Lp_mul_Lq {E} [NormedAddCommGroup E] {f g : Î± â†’ E} {p q : â„}
    (hpq : p.IsConjExponent q) (hf : Memâ„’p f (ENNReal.ofReal p) Î¼)
    (hg : Memâ„’p g (ENNReal.ofReal q) Î¼) :
    âˆ« a, â€–f aâ€– * â€–g aâ€– âˆ‚Î¼ â‰¤ (âˆ« a, â€–f aâ€– ^ p âˆ‚Î¼) ^ (1 / p) * (âˆ« a, â€–g aâ€– ^ q âˆ‚Î¼) ^ (1 / q) := by
  -- translate the Bochner integrals into Lebesgue integrals.
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae,
    integral_eq_lintegral_of_nonneg_ae]
  rotate_left
  Â· exact eventually_of_forall fun x => Real.rpow_nonneg (norm_nonneg _) _
  Â· exact (hg.1.norm.aemeasurable.pow aemeasurable_const).aestronglyMeasurable
  Â· exact eventually_of_forall fun x => Real.rpow_nonneg (norm_nonneg _) _
  Â· exact (hf.1.norm.aemeasurable.pow aemeasurable_const).aestronglyMeasurable
  Â· exact eventually_of_forall fun x => mul_nonneg (norm_nonneg _) (norm_nonneg _)
  Â· exact hf.1.norm.mul hg.1.norm
  rw [ENNReal.toReal_rpow, ENNReal.toReal_rpow, â† ENNReal.toReal_mul]
  -- replace norms by nnnorm
  have h_left : âˆ«â» a, ENNReal.ofReal (â€–f aâ€– * â€–g aâ€–) âˆ‚Î¼ =
      âˆ«â» a, ((fun x => (â€–f xâ€–â‚Š : â„â‰¥0âˆ)) * fun x => (â€–g xâ€–â‚Š : â„â‰¥0âˆ)) a âˆ‚Î¼ := by
    simp_rw [Pi.mul_apply, â† ofReal_norm_eq_coe_nnnorm, ENNReal.ofReal_mul (norm_nonneg _)]
  have h_right_f : âˆ«â» a, ENNReal.ofReal (â€–f aâ€– ^ p) âˆ‚Î¼ = âˆ«â» a, (â€–f aâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼ := by
    refine lintegral_congr fun x => ?_
    rw [â† ofReal_norm_eq_coe_nnnorm, ENNReal.ofReal_rpow_of_nonneg (norm_nonneg _) hpq.nonneg]
  have h_right_g : âˆ«â» a, ENNReal.ofReal (â€–g aâ€– ^ q) âˆ‚Î¼ = âˆ«â» a, (â€–g aâ€–â‚Š : â„â‰¥0âˆ) ^ q âˆ‚Î¼ := by
    refine lintegral_congr fun x => ?_
    rw [â† ofReal_norm_eq_coe_nnnorm, ENNReal.ofReal_rpow_of_nonneg (norm_nonneg _) hpq.symm.nonneg]
  rw [h_left, h_right_f, h_right_g]
  -- we can now apply `ENNReal.lintegral_mul_le_Lp_mul_Lq` (up to the `toReal` application)
  refine ENNReal.toReal_mono ?_ ?_
  Â· refine ENNReal.mul_ne_top ?_ ?_
    Â· convert hf.snorm_ne_top
      rw [snorm_eq_lintegral_rpow_nnnorm]
      Â· rw [ENNReal.toReal_ofReal hpq.nonneg]
      Â· rw [Ne, ENNReal.ofReal_eq_zero, not_le]
        exact hpq.pos
      Â· exact ENNReal.coe_ne_top
    Â· convert hg.snorm_ne_top
      rw [snorm_eq_lintegral_rpow_nnnorm]
      Â· rw [ENNReal.toReal_ofReal hpq.symm.nonneg]
      Â· rw [Ne, ENNReal.ofReal_eq_zero, not_le]
        exact hpq.symm.pos
      Â· exact ENNReal.coe_ne_top
  Â· exact ENNReal.lintegral_mul_le_Lp_mul_Lq Î¼ hpq hf.1.nnnorm.aemeasurable.coe_nnreal_ennreal
      hg.1.nnnorm.aemeasurable.coe_nnreal_ennreal
set_option linter.uppercaseLean3 false in
#align measure_theory.integral_mul_norm_le_Lp_mul_Lq MeasureTheory.integral_mul_norm_le_Lp_mul_Lq

/-- HÃ¶lder's inequality for functions `Î± â†’ â„`. The integral of the product of two nonnegative
functions is bounded by the product of their `â„’p` and `â„’q` seminorms when `p` and `q` are conjugate
exponents. -/
theorem integral_mul_le_Lp_mul_Lq_of_nonneg {p q : â„} (hpq : p.IsConjExponent q) {f g : Î± â†’ â„}
    (hf_nonneg : 0 â‰¤áµ[Î¼] f) (hg_nonneg : 0 â‰¤áµ[Î¼] g) (hf : Memâ„’p f (ENNReal.ofReal p) Î¼)
    (hg : Memâ„’p g (ENNReal.ofReal q) Î¼) :
    âˆ« a, f a * g a âˆ‚Î¼ â‰¤ (âˆ« a, f a ^ p âˆ‚Î¼) ^ (1 / p) * (âˆ« a, g a ^ q âˆ‚Î¼) ^ (1 / q) := by
  have h_left : âˆ« a, f a * g a âˆ‚Î¼ = âˆ« a, â€–f aâ€– * â€–g aâ€– âˆ‚Î¼ := by
    refine integral_congr_ae ?_
    filter_upwards [hf_nonneg, hg_nonneg] with x hxf hxg
    rw [Real.norm_of_nonneg hxf, Real.norm_of_nonneg hxg]
  have h_right_f : âˆ« a, f a ^ p âˆ‚Î¼ = âˆ« a, â€–f aâ€– ^ p âˆ‚Î¼ := by
    refine integral_congr_ae ?_
    filter_upwards [hf_nonneg] with x hxf
    rw [Real.norm_of_nonneg hxf]
  have h_right_g : âˆ« a, g a ^ q âˆ‚Î¼ = âˆ« a, â€–g aâ€– ^ q âˆ‚Î¼ := by
    refine integral_congr_ae ?_
    filter_upwards [hg_nonneg] with x hxg
    rw [Real.norm_of_nonneg hxg]
  rw [h_left, h_right_f, h_right_g]
  exact integral_mul_norm_le_Lp_mul_Lq hpq hf hg
set_option linter.uppercaseLean3 false in
#align measure_theory.integral_mul_le_Lp_mul_Lq_of_nonneg MeasureTheory.integral_mul_le_Lp_mul_Lq_of_nonneg

theorem integral_countable' [Countable Î±] [MeasurableSingletonClass Î±] {Î¼ : Measure Î±}
    {f : Î± â†’ E} (hf : Integrable f Î¼) :
    âˆ« a, f a âˆ‚Î¼ = âˆ‘' a, (Î¼ {a}).toReal â€¢ f a := by
  rw [â† Measure.sum_smul_dirac Î¼] at hf
  rw [â† Measure.sum_smul_dirac Î¼, integral_sum_measure hf]
  congr 1 with a : 1
  rw [integral_smul_measure, integral_dirac, Measure.sum_smul_dirac]

theorem integral_singleton' {Î¼ : Measure Î±} {f : Î± â†’ E} (hf : StronglyMeasurable f) (a : Î±) :
    âˆ« a in {a}, f a âˆ‚Î¼ = (Î¼ {a}).toReal â€¢ f a := by
  simp only [Measure.restrict_singleton, integral_smul_measure, integral_dirac' f a hf, smul_eq_mul,
    mul_comm]

theorem integral_singleton [MeasurableSingletonClass Î±] {Î¼ : Measure Î±} (f : Î± â†’ E) (a : Î±) :
    âˆ« a in {a}, f a âˆ‚Î¼ = (Î¼ {a}).toReal â€¢ f a := by
  simp only [Measure.restrict_singleton, integral_smul_measure, integral_dirac, smul_eq_mul,
    mul_comm]

theorem integral_countable [MeasurableSingletonClass Î±] (f : Î± â†’ E) {s : Set Î±} (hs : s.Countable)
    (hf : Integrable f (Î¼.restrict s)) :
    âˆ« a in s, f a âˆ‚Î¼ = âˆ‘' a : s, (Î¼ {(a : Î±)}).toReal â€¢ f a := by
  have hi : Countable { x // x âˆˆ s } := Iff.mpr countable_coe_iff hs
  have hf' : Integrable (fun (x : s) => f x) (Measure.comap Subtype.val Î¼) := by
    rw [â† map_comap_subtype_coe, integrable_map_measure] at hf
    Â· apply hf
    Â· exact Integrable.aestronglyMeasurable hf
    Â· exact Measurable.aemeasurable measurable_subtype_coe
    Â· exact Countable.measurableSet hs
  rw [â† integral_subtype_comap hs.measurableSet, integral_countable' hf']
  congr 1 with a : 1
  rw [Measure.comap_apply Subtype.val Subtype.coe_injective
    (fun s' hs' => MeasurableSet.subtype_image (Countable.measurableSet hs) hs') _
    (MeasurableSet.singleton a)]
  simp

theorem integral_finset [MeasurableSingletonClass Î±] (s : Finset Î±) (f : Î± â†’ E)
    (hf : Integrable f (Î¼.restrict s)) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ‘ x âˆˆ s, (Î¼ {x}).toReal â€¢ f x := by
  rw [integral_countable _ s.countable_toSet hf, â† Finset.tsum_subtype']

theorem integral_fintype [MeasurableSingletonClass Î±] [Fintype Î±] (f : Î± â†’ E)
    (hf : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = âˆ‘ x, (Î¼ {x}).toReal â€¢ f x := by
  -- NB: Integrable f does not follow from Fintype, because the measure itself could be non-finite
  rw [â† integral_finset .univ, Finset.coe_univ, Measure.restrict_univ]
  simp only [Finset.coe_univ, Measure.restrict_univ, hf]

theorem integral_unique [Unique Î±] (f : Î± â†’ E) : âˆ« x, f x âˆ‚Î¼ = (Î¼ univ).toReal â€¢ f default :=
  calc
    âˆ« x, f x âˆ‚Î¼ = âˆ« _, f default âˆ‚Î¼ := by congr with x; congr; exact Unique.uniq _ x
    _ = (Î¼ univ).toReal â€¢ f default := by rw [integral_const]

theorem integral_pos_of_integrable_nonneg_nonzero [TopologicalSpace Î±] [Measure.IsOpenPosMeasure Î¼]
    {f : Î± â†’ â„} {x : Î±} (f_cont : Continuous f) (f_int : Integrable f Î¼) (f_nonneg : 0 â‰¤ f)
    (f_x : f x â‰  0) : 0 < âˆ« x, f x âˆ‚Î¼ :=
  (integral_pos_iff_support_of_nonneg f_nonneg f_int).2
    (IsOpen.measure_pos Î¼ f_cont.isOpen_support âŸ¨x, f_xâŸ©)

end Properties

section IntegralTrim

variable {H Î² Î³ : Type*} [NormedAddCommGroup H] {m m0 : MeasurableSpace Î²} {Î¼ : Measure Î²}

/-- Simple function seen as simple function of a larger `MeasurableSpace`. -/
def SimpleFunc.toLargerSpace (hm : m â‰¤ m0) (f : @SimpleFunc Î² m Î³) : SimpleFunc Î² Î³ :=
  âŸ¨@SimpleFunc.toFun Î² m Î³ f, fun x => hm _ (@SimpleFunc.measurableSet_fiber Î² Î³ m f x),
    @SimpleFunc.finite_range Î² Î³ m fâŸ©
#align measure_theory.simple_func.to_larger_space MeasureTheory.SimpleFunc.toLargerSpace

theorem SimpleFunc.coe_toLargerSpace_eq (hm : m â‰¤ m0) (f : @SimpleFunc Î² m Î³) :
    â‡‘(f.toLargerSpace hm) = f := rfl
#align measure_theory.simple_func.coe_to_larger_space_eq MeasureTheory.SimpleFunc.coe_toLargerSpace_eq

theorem integral_simpleFunc_larger_space (hm : m â‰¤ m0) (f : @SimpleFunc Î² m F)
    (hf_int : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = âˆ‘ x âˆˆ @SimpleFunc.range Î² F m f, ENNReal.toReal (Î¼ (f â»Â¹' {x})) â€¢ x := by
  simp_rw [â† f.coe_toLargerSpace_eq hm]
  have hf_int : Integrable (f.toLargerSpace hm) Î¼ := by rwa [SimpleFunc.coe_toLargerSpace_eq]
  rw [SimpleFunc.integral_eq_sum _ hf_int]
  congr 1
#align measure_theory.integral_simple_func_larger_space MeasureTheory.integral_simpleFunc_larger_space

theorem integral_trim_simpleFunc (hm : m â‰¤ m0) (f : @SimpleFunc Î² m F) (hf_int : Integrable f Î¼) :
    âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼.trim hm := by
  have hf : StronglyMeasurable[m] f := @SimpleFunc.stronglyMeasurable Î² F m _ f
  have hf_int_m := hf_int.trim hm hf
  rw [integral_simpleFunc_larger_space (le_refl m) f hf_int_m,
    integral_simpleFunc_larger_space hm f hf_int]
  congr with x
  congr 2
  exact (trim_measurableSet_eq hm (@SimpleFunc.measurableSet_fiber Î² F m f x)).symm
#align measure_theory.integral_trim_simple_func MeasureTheory.integral_trim_simpleFunc

theorem integral_trim (hm : m â‰¤ m0) {f : Î² â†’ G} (hf : StronglyMeasurable[m] f) :
    âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼.trim hm := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [integral, hG]
  borelize G
  by_cases hf_int : Integrable f Î¼
  swap
  Â· have hf_int_m : Â¬Integrable f (Î¼.trim hm) := fun hf_int_m =>
      hf_int (integrable_of_integrable_trim hm hf_int_m)
    rw [integral_undef hf_int, integral_undef hf_int_m]
  haveI : SeparableSpace (range f âˆª {0} : Set G) := hf.separableSpace_range_union_singleton
  let f_seq := @SimpleFunc.approxOn G Î² _ _ _ m _ hf.measurable (range f âˆª {0}) 0 (by simp) _
  have hf_seq_meas : âˆ€ n, StronglyMeasurable[m] (f_seq n) := fun n =>
    @SimpleFunc.stronglyMeasurable Î² G m _ (f_seq n)
  have hf_seq_int : âˆ€ n, Integrable (f_seq n) Î¼ :=
    SimpleFunc.integrable_approxOn_range (hf.mono hm).measurable hf_int
  have hf_seq_int_m : âˆ€ n, Integrable (f_seq n) (Î¼.trim hm) := fun n =>
    (hf_seq_int n).trim hm (hf_seq_meas n)
  have hf_seq_eq : âˆ€ n, âˆ« x, f_seq n x âˆ‚Î¼ = âˆ« x, f_seq n x âˆ‚Î¼.trim hm := fun n =>
    integral_trim_simpleFunc hm (f_seq n) (hf_seq_int n)
  have h_lim_1 : atTop.Tendsto (fun n => âˆ« x, f_seq n x âˆ‚Î¼) (ğ“ (âˆ« x, f x âˆ‚Î¼)) := by
    refine tendsto_integral_of_L1 f hf_int (eventually_of_forall hf_seq_int) ?_
    exact SimpleFunc.tendsto_approxOn_range_L1_nnnorm (hf.mono hm).measurable hf_int
  have h_lim_2 : atTop.Tendsto (fun n => âˆ« x, f_seq n x âˆ‚Î¼) (ğ“ (âˆ« x, f x âˆ‚Î¼.trim hm)) := by
    simp_rw [hf_seq_eq]
    refine @tendsto_integral_of_L1 Î² G _ _ m (Î¼.trim hm) _ f (hf_int.trim hm hf) _ _
      (eventually_of_forall hf_seq_int_m) ?_
    exact @SimpleFunc.tendsto_approxOn_range_L1_nnnorm Î² G m _ _ _ f _ _ hf.measurable
      (hf_int.trim hm hf)
  exact tendsto_nhds_unique h_lim_1 h_lim_2
#align measure_theory.integral_trim MeasureTheory.integral_trim

theorem integral_trim_ae (hm : m â‰¤ m0) {f : Î² â†’ G} (hf : AEStronglyMeasurable f (Î¼.trim hm)) :
    âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼.trim hm := by
  rw [integral_congr_ae (ae_eq_of_ae_eq_trim hf.ae_eq_mk), integral_congr_ae hf.ae_eq_mk]
  exact integral_trim hm hf.stronglyMeasurable_mk
#align measure_theory.integral_trim_ae MeasureTheory.integral_trim_ae

theorem ae_eq_trim_of_stronglyMeasurable [TopologicalSpace Î³] [MetrizableSpace Î³] (hm : m â‰¤ m0)
    {f g : Î² â†’ Î³} (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g)
    (hfg : f =áµ[Î¼] g) : f =áµ[Î¼.trim hm] g := by
  rwa [EventuallyEq, ae_iff, trim_measurableSet_eq hm]
  exact (hf.measurableSet_eq_fun hg).compl
#align measure_theory.ae_eq_trim_of_strongly_measurable MeasureTheory.ae_eq_trim_of_stronglyMeasurable

theorem ae_eq_trim_iff [TopologicalSpace Î³] [MetrizableSpace Î³] (hm : m â‰¤ m0) {f g : Î² â†’ Î³}
    (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) :
    f =áµ[Î¼.trim hm] g â†” f =áµ[Î¼] g :=
  âŸ¨ae_eq_of_ae_eq_trim, ae_eq_trim_of_stronglyMeasurable hm hf hgâŸ©
#align measure_theory.ae_eq_trim_iff MeasureTheory.ae_eq_trim_iff

theorem ae_le_trim_of_stronglyMeasurable [LinearOrder Î³] [TopologicalSpace Î³]
    [OrderClosedTopology Î³] [PseudoMetrizableSpace Î³] (hm : m â‰¤ m0) {f g : Î² â†’ Î³}
    (hf : StronglyMeasurable[m] f) (hg : StronglyMeasurable[m] g) (hfg : f â‰¤áµ[Î¼] g) :
    f â‰¤áµ[Î¼.trim hm] g := by
  rwa [EventuallyLE, ae_iff, trim_measurableSet_eq hm]
  exact (hf.measurableSet_le hg).compl
#align measure_theory.ae_le_trim_of_strongly_measurable MeasureTheory.ae_le_trim_of_stronglyMeasurable

theorem ae_le_trim_iff [LinearOrder Î³] [TopologicalSpace Î³] [OrderClosedTopology Î³]
    [PseudoMetrizableSpace Î³] (hm : m â‰¤ m0) {f g : Î² â†’ Î³} (hf : StronglyMeasurable[m] f)
    (hg : StronglyMeasurable[m] g) : f â‰¤áµ[Î¼.trim hm] g â†” f â‰¤áµ[Î¼] g :=
  âŸ¨ae_le_of_ae_le_trim, ae_le_trim_of_stronglyMeasurable hm hf hgâŸ©
#align measure_theory.ae_le_trim_iff MeasureTheory.ae_le_trim_iff

end IntegralTrim

section SnormBound

variable {m0 : MeasurableSpace Î±} {Î¼ : Measure Î±}

theorem snorm_one_le_of_le {r : â„â‰¥0} {f : Î± â†’ â„} (hfint : Integrable f Î¼) (hfint' : 0 â‰¤ âˆ« x, f x âˆ‚Î¼)
    (hf : âˆ€áµ Ï‰ âˆ‚Î¼, f Ï‰ â‰¤ r) : snorm f 1 Î¼ â‰¤ 2 * Î¼ Set.univ * r := by
  by_cases hr : r = 0
  Â· suffices f =áµ[Î¼] 0 by
      rw [snorm_congr_ae this, snorm_zero, hr, ENNReal.coe_zero, mul_zero]
    rw [hr] at hf
    norm_cast at hf
    -- Porting note: two lines above were
    --rw [hr, Nonneg.coe_zero] at hf
    have hnegf : âˆ« x, -f x âˆ‚Î¼ = 0 := by
      rw [integral_neg, neg_eq_zero]
      exact le_antisymm (integral_nonpos_of_ae hf) hfint'
    have := (integral_eq_zero_iff_of_nonneg_ae ?_ hfint.neg).1 hnegf
    Â· filter_upwards [this] with Ï‰ hÏ‰
      rwa [Pi.neg_apply, Pi.zero_apply, neg_eq_zero] at hÏ‰
    Â· filter_upwards [hf] with Ï‰ hÏ‰
      rwa [Pi.zero_apply, Pi.neg_apply, Right.nonneg_neg_iff]
  by_cases hÎ¼ : IsFiniteMeasure Î¼
  swap
  Â· have : Î¼ Set.univ = âˆ := by
      by_contra hÎ¼'
      exact hÎ¼ (IsFiniteMeasure.mk <| lt_top_iff_ne_top.2 hÎ¼')
    rw [this, ENNReal.mul_top', if_neg, ENNReal.top_mul', if_neg]
    Â· exact le_top
    Â· simp [hr]
    Â· norm_num
  haveI := hÎ¼
  rw [integral_eq_integral_pos_part_sub_integral_neg_part hfint, sub_nonneg] at hfint'
  have hposbdd : âˆ« Ï‰, max (f Ï‰) 0 âˆ‚Î¼ â‰¤ (Î¼ Set.univ).toReal â€¢ (r : â„) := by
    rw [â† integral_const]
    refine integral_mono_ae hfint.real_toNNReal (integrable_const (r : â„)) ?_
    filter_upwards [hf] with Ï‰ hÏ‰ using Real.toNNReal_le_iff_le_coe.2 hÏ‰
  rw [Memâ„’p.snorm_eq_integral_rpow_norm one_ne_zero ENNReal.one_ne_top
      (memâ„’p_one_iff_integrable.2 hfint),
    ENNReal.ofReal_le_iff_le_toReal
      (ENNReal.mul_ne_top (ENNReal.mul_ne_top ENNReal.two_ne_top <| @measure_ne_top _ _ _ hÎ¼ _)
        ENNReal.coe_ne_top)]
  simp_rw [ENNReal.one_toReal, _root_.inv_one, Real.rpow_one, Real.norm_eq_abs, â†
    max_zero_add_max_neg_zero_eq_abs_self, â† Real.coe_toNNReal']
  rw [integral_add hfint.real_toNNReal]
  Â· simp only [Real.coe_toNNReal', ENNReal.toReal_mul, ENNReal.one_toReal, ENNReal.coe_toReal,
      ge_iff_le, Left.nonneg_neg_iff, Left.neg_nonpos_iff, toReal_ofNat] at hfint' âŠ¢
    refine (add_le_add_left hfint' _).trans ?_
    rwa [â† two_mul, mul_assoc, mul_le_mul_left (two_pos : (0 : â„) < 2)]
  Â· exact hfint.neg.sup (integrable_zero _ _ Î¼)
#align measure_theory.snorm_one_le_of_le MeasureTheory.snorm_one_le_of_le

theorem snorm_one_le_of_le' {r : â„} {f : Î± â†’ â„} (hfint : Integrable f Î¼) (hfint' : 0 â‰¤ âˆ« x, f x âˆ‚Î¼)
    (hf : âˆ€áµ Ï‰ âˆ‚Î¼, f Ï‰ â‰¤ r) : snorm f 1 Î¼ â‰¤ 2 * Î¼ Set.univ * ENNReal.ofReal r := by
  refine snorm_one_le_of_le hfint hfint' ?_
  simp only [Real.coe_toNNReal', le_max_iff]
  filter_upwards [hf] with Ï‰ hÏ‰ using Or.inl hÏ‰
#align measure_theory.snorm_one_le_of_le' MeasureTheory.snorm_one_le_of_le'

end SnormBound

end MeasureTheory

namespace Mathlib.Meta.Positivity

open Qq Lean Meta MeasureTheory

/-- Positivity extension for integrals.

This extension only proves non-negativity, strict positivity is more delicate for integration and
requires more assumptions. -/
@[positivity MeasureTheory.integral _ _]
def evalIntegral : PositivityExt where eval {u Î±} zÎ± pÎ± e := do
  match u, Î±, e with
  | 0, ~q(â„), ~q(@MeasureTheory.integral $i â„ _ $inst2 _ _ $f) =>
    let i : Q($i) â† mkFreshExprMVarQ q($i) .syntheticOpaque
    have body : Q(â„) := .betaRev f #[i]
    let rbody â† core zÎ± pÎ± body
    let pbody â† rbody.toNonneg
    let pr : Q(âˆ€ x, 0 â‰¤ $f x) â† mkLambdaFVars #[i] pbody
    assertInstancesCommute
    return .nonnegative q(integral_nonneg $pr)
  | _ => throwError "not MeasureTheory.integral"

end Mathlib.Meta.Positivity
