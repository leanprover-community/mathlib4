/-
Copyright (c) 2025 Oliver Butterley. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Butterley, Yoh Tanimoto
-/
import Mathlib.MeasureTheory.Integral.RieszMarkovKakutani.Basic
import Mathlib.MeasureTheory.Integral.RieszMarkovKakutani.Real
import Mathlib.MeasureTheory.Measure.Complex

/-!
# Variation and total variation for vector valued measures

Given a vector-valued measure Î¼ we consider the problem of finding a function f such that, for any
set E, â€–Î¼(E)â€– â‰¤ f(E). This suggests defining f(E) as the supremum over partitions {Eáµ¢} of E, of the
quantity âˆ‘áµ¢, â€–Î¼(Eáµ¢)â€–. Indeed any solution of the problem must be not less than this function. It
turns out that this function actually is a measure.

## Main definitions & statements

* `VectorMeasure.variation` is the definition of the (total) variation measure.

## Implementation notes

Variation is defined as an `â„â‰¥0âˆ`-valued `VectorMeasure` rather than as a `Measure`, this is
somewhat natural since we start with `VectorMeasure`.

Variation is defined for signed measures in `MeasureTheory.SignedMeasure.totalVariation`. This
definition uses the Hahnâ€“Jordan decomposition of a signed measure. However this construction doesn't
generalize to other vector-valued measures, in particular doesn't work for the important case of
complex measures.

## References

* [Walter Rudin, Real and Complex Analysis.][Rud87]

## To do

* Total variation is a norm on the space of vector-valued measures.
* If `Î¼` is a `â„â‰¥0âˆ`-valued `VectorMeasure` then `variation Î¼ = Î¼`.
* Variation is equivalent to that defined via the Hahnâ€“Jordan decomposition for signed measures.
* If `Î¼` is a complex measure then `variation univ < âˆ`.
-/

open MeasureTheory BigOperators NNReal ENNReal Function Filter

section CompleteLinearOrder

variable {Î± : Type*}{Î¹ : Type*} [CompleteLinearOrder Î±] {s : Set Î±} {a b : Î±}

theorem lt_biSup_iff {s : Set Î¹} {f : Î¹ â†’ Î±} : a < â¨† i âˆˆ s, f i â†” âˆƒ i âˆˆ s, a < f i := by
  constructor
  Â· intro h
    obtain âŸ¨i, hiâŸ© := lt_iSup_iff.mp h
    obtain âŸ¨his, haâŸ© := lt_iSup_iff.mp hi
    exact âŸ¨i, âŸ¨his, haâŸ©âŸ©
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    apply lt_iSup_iff.mpr
    use i
    apply lt_iSup_iff.mpr
    simpa [exists_prop]

end CompleteLinearOrder

lemma ENNReal.hasSum_iff (f : â„• â†’ â„â‰¥0âˆ) (a : â„â‰¥0âˆ) : HasSum f a â†”
    (âˆ€ (n : â„•), âˆ‘ i âˆˆ Finset.range n, f i â‰¤ a) âˆ§
    (âˆ€ b < a, âˆƒ (n : â„•), b < âˆ‘ i âˆˆ Finset.range n, f i) := by
  obtain ha | ha | ha := a.trichotomy
  Â· -- The case `a = 0`.
    suffices h : (âˆ€ x, f x = 0) â†” âˆ€ n i, i < n â†’ f i = 0 by simpa [ha, hasSum_zero_iff]
    exact âŸ¨fun h _ i _ â†¦ h i, fun h i â†¦  h (i + 1) i (by omega)âŸ©
  Â· -- The case `a = âˆ`.
    suffices h: (âˆ€ i, Â¬i = âˆ â†’ âˆƒ a, âˆ€ (b : â„•), a â‰¤ b â†’ i < âˆ‘ i âˆˆ Finset.range b, f i) â†”
        âˆ€ b < âŠ¤, âˆƒ n, b < âˆ‘ i âˆˆ Finset.range n, f i by
      simpa [ha, hasSum_iff_tendsto_nat, nhds_top]
    refine âŸ¨fun h b hb â†¦ ?_, fun h b hb â†¦ ?_âŸ©
    Â· obtain âŸ¨n, hnâŸ© := h b (LT.lt.ne_top hb)
      exact âŸ¨n, hn n n.le_reflâŸ©
    Â· obtain âŸ¨n, hnâŸ© := h b (Ne.lt_top' <| Ne.symm hb)
      exact âŸ¨n, fun m _ â†¦ gt_of_ge_of_gt (Finset.sum_le_sum_of_subset (by simpa)) hnâŸ©
  Â· -- The case `0 < a âˆ§ a < âˆ`.
    obtain âŸ¨ha'', ha'âŸ© := (a.toReal_pos_iff).mp ha
    rw [ENNReal.hasSum_iff_tendsto_nat]
    constructor
    Â· intro h
      refine âŸ¨fun n â†¦ ?_, fun b hb â†¦ ?_âŸ©
      Â· rw [tendsto_atTop_nhds] at h
        by_contra! hc
        have hn : âˆ€ m, n â‰¤ m â†’ âˆ‘ i âˆˆ Finset.range n, f i â‰¤  âˆ‘ i âˆˆ Finset.range m, f i :=
          fun _ _ â†¦ Finset.sum_le_sum_of_subset (by simpa)
        let s := Set.Ico 0 (âˆ‘ i âˆˆ Finset.range n, f i)
        obtain âŸ¨â„“, hâ„“âŸ© := h s âŸ¨by simp, hcâŸ© isOpen_Ico_zero
        exact (lt_self_iff_false _).mp <|
          lt_of_lt_of_le ((hâ„“ (max n â„“) (by omega)).2) (hn (max n â„“) (by omega))
      Â· rw [tendsto_atTop_nhds] at h
        let s := Set.Ioo b (a + 1)
        have hs : a âˆˆ s := by simpa [s, hb] using lt_add_right (LT.lt.ne_top ha') one_ne_zero
        obtain âŸ¨n, hnâŸ© := h s hs isOpen_Ioo
        exact âŸ¨n, (hn n (Nat.le_refl _)).1âŸ©
    Â· rw [and_imp]
      intro hf hf'
      rw [ENNReal.tendsto_nhds ha'.ne_top]
      intro Îµ hÎµ
      simp only [Set.mem_Icc, tsub_le_iff_right, Filter.eventually_atTop, ge_iff_le]
      have hÎµ' := (ENNReal.sub_lt_self_iff (LT.lt.ne_top ha')).mpr âŸ¨ha'', hÎµâŸ©
      obtain âŸ¨n, hnâŸ© := hf' (a - Îµ) hÎµ'
      refine âŸ¨n, fun m hm â†¦ ?_âŸ©
      constructor
      Â· calc a
        _ â‰¤ a - Îµ + Îµ := by exact le_tsub_add
        _ â‰¤ âˆ‘ i âˆˆ Finset.range n, f i + Îµ := add_le_add_right (le_of_lt hn) Îµ
        _ â‰¤ âˆ‘ i âˆˆ Finset.range m, f i + Îµ := by gcongr
      Â· exact le_add_right (hf m)

variable {Î¹ E : Type*} [SeminormedAddCommGroup E]
/-- Quantitative result associated to the direct comparison test for series: If, for all `i`,
`â€–f iâ€–â‚‘ â‰¤ g i`, then `â€–âˆ‘' i, f iâ€–â‚‘ â‰¤ âˆ‘' i, g i`. -/
theorem tsum_of_enorm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} (hg : HasSum g a)
    (h : âˆ€ i, â€–f iâ€–â‚‘ â‰¤ g i) : â€–âˆ‘' i : Î¹, f iâ€–â‚‘ â‰¤ a := by
  -- simp [â† NNReal.coe_le_coe, â† NNReal.hasSum_coe, coe_nnnorm] at *
  -- have : âˆ€ i, â€–f iâ€–â‚‘ = ENNReal.ofReal â€–f iâ€– := by simp only [ofReal_norm, implies_true]
  -- have (i : Î¹) := ofReal_norm (f i)
  by_cases hc : a â‰  âˆ
  Â· have hc' : âˆ€ i, g i â‰  âˆ := by
      by_contra! h
      have : HasSum g âˆ := by
        obtain âŸ¨i, hiâŸ© := h
        have hg' : g i â‰¤ âˆ‘' i, g i := ENNReal.le_tsum i
        have : HasSum g (âˆ‘' i, g i) := by
          sorry
        rw [hi] at hg'
        simp only [top_le_iff] at hg'
        rwa [â† hg']
      have : a = âŠ¤ := HasSum.unique hg this
      exact hc this
    simp_rw [â† ofReal_norm] at *
    have hfg (i : Î¹) : â€–f iâ€– â‰¤ (g i).toReal := by
      have := h i
      refine (ofReal_le_iff_le_toReal ?_).mp (h i)
      exact hc' i
    have hg' : HasSum (fun i â†¦ (g i).toReal) a.toReal := by
      -- Since each term and the sum are finite.
      sorry
    have := tsum_of_norm_bounded hg' hfg
    exact (ofReal_le_iff_le_toReal hc).mpr this
  Â· push_neg at hc
    simp [hc]

-- Similar to `norm_tsum_le_tsum_norm` and `nnnorm_tsum_le` in `Analysis/Normed/Group/InfiniteSum`.

variable {Î¹ E : Type*} [SeminormedAddCommGroup E]
/-- `â€–âˆ‘' i, f iâ€–â‚‘ â‰¤ âˆ‘' i, â€–f iâ€–â‚‘`, automatically `âˆ‘' i, â€–f iâ€–â‚‘` is summable. -/
theorem enorm_tsum_le_tsum_enorm {f : Î¹ â†’ E} : â€–âˆ‘' i, f iâ€–â‚‘ â‰¤ âˆ‘' i, â€–f iâ€–â‚‘ := by
  have hg : Summable fun i â†¦  â€–f iâ€–â‚‘ := by exact ENNReal.summable
  exact tsum_of_enorm_bounded hg.hasSum fun _i => le_rfl

namespace VectorMeasure

variable {X V ğ•œ : Type*} [MeasurableSpace X] [NormedAddCommGroup V] [NormedField ğ•œ]
  [NormedSpace ğ•œ V] (Î¼ : VectorMeasure X V)

/-!
## Inner partitions

Instead of working with partitions of a set `s`, we work with finite sets of disjoints sets
contained within `s` since the same value will be achieved in the supremum.

The empty set is forbidden so that partitions of disjoint sets are disjoint sets of sets.
-/

def partitions (s : Set X) : Set (Finset (Set X)) :=
    {P | (âˆ€ t âˆˆ P, t âŠ† s) âˆ§ (âˆ€ t âˆˆ P, MeasurableSet t) âˆ§ (P.toSet.PairwiseDisjoint id) âˆ§
    (âˆ€ p âˆˆ P, p â‰  âˆ…)}

lemma partitions_empty : partitions (âˆ… : Set X) = {âˆ…} := by
  dsimp [partitions]
  ext P
  simp only [Set.subset_empty_iff, Finset.forall_mem_not_eq', Set.mem_setOf_eq,
    Set.mem_singleton_iff]
  constructor
  Â· intro âŸ¨hP', a, b, hP''âŸ©
    by_contra! hP
    obtain âŸ¨p, hpâŸ© := Finset.Nonempty.exists_mem (Finset.nonempty_iff_ne_empty.mpr hP)
    simp_all [hP' p hp, ne_eq]
  Â· intro hp
    simp [hp]

lemma partitions_monotone {sâ‚ sâ‚‚ : Set X} (h : sâ‚ âŠ† sâ‚‚) : partitions sâ‚ âŠ† partitions sâ‚‚ := by
  intro P hP
  obtain âŸ¨h1, h2, h3, h4âŸ© := hP
  exact âŸ¨fun p hp â†¦ subset_trans (h1 p hp) h, h2, h3, h4âŸ©

open Classical in
/-- If each `P i` is a partition of `s i` then the union is a partition of `â‹ƒ i, s i`. -/
lemma partition_union {s : â„• â†’ Set X} (hs : Pairwise (Disjoint on s))
    {P : â„• â†’ Finset (Set X)} (hP : âˆ€ i, P i âˆˆ partitions (s i)) (n : â„•):
    Finset.biUnion (Finset.range n) P âˆˆ partitions (â‹ƒ i, s i) := by
  simp only [partitions, ne_eq, Finset.forall_mem_not_eq', Set.mem_setOf_eq, Finset.mem_biUnion,
    Finset.mem_range, forall_exists_index, and_imp, not_exists, not_and]
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· intro p i _ hp
    exact Set.subset_iUnion_of_subset i ((hP i).1 p hp)
  Â· intro p i _ hp
    exact (hP i).2.1 p hp
  Â· intro p hp q hq hpq r hrp hrq
    simp only [Set.bot_eq_empty, Set.le_eq_subset, Set.subset_empty_iff]
    simp only [id_eq, Set.le_eq_subset] at hrp hrq
    simp only [Finset.coe_biUnion, Finset.coe_range, Set.mem_Iio, Set.mem_iUnion, Finset.mem_coe,
      exists_prop] at hp hq
    obtain âŸ¨i, hi, hpâŸ© := hp
    obtain âŸ¨j, hj, hqâŸ© := hq
    obtain hc | hc : i = j âˆ¨ i â‰  j := by omega
    Â· rw [hc] at hp
      exact Set.subset_eq_empty ((hP j).2.2.1 hp hq hpq hrp hrq) rfl
    Â· have hp' := (hP i).1 p hp
      have hq' := (hP j).1 q hq
      exact Set.subset_eq_empty (hs hc (subset_trans hrp hp') (subset_trans hrq hq')) rfl
  Â· intro i _ h
    exact (hP i).2.2.2 âˆ… h rfl

/-- If P, Q are partitions of two disjoint sets then P and Q are disjoint. -/
lemma partitions_disjoint {s t : Set X} (hst : Disjoint s t) {P Q : Finset (Set X)}
    (hP : P âˆˆ partitions s) (hQ : Q âˆˆ partitions t) : Disjoint P Q := by
  intro R hRP hRQ
  simp only [Finset.bot_eq_empty, Finset.le_eq_subset, Finset.subset_empty]
  by_contra! hc
  obtain âŸ¨r, hrâŸ© := Finset.Nonempty.exists_mem <| Finset.nonempty_iff_ne_empty.mpr hc
  have := hst (hP.1 r <| hRP hr) (hQ.1 r <| hRQ hr)
  have := hP.2.2.2 r (hRP hr)
  simp_all

open Classical in
/-- If `P` is a partition then the restriction of `P` to a set `s` is a partition of `s`. -/
lemma partition_restrict {s t : Set X} {P : Finset (Set X)} (hs : P âˆˆ partitions s)
    (ht : MeasurableSet t) : (P.image (fun p â†¦ p âˆ© t)).filter (Â· â‰  âˆ…) âˆˆ partitions t := by
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· intro _ h
    obtain âŸ¨_, _, hpâŸ© := Finset.mem_image.mp (Finset.mem_filter.mp h).1
    simp [â† hp]
  Â· intro r hr
    have := Finset.mem_filter.mp hr
    obtain âŸ¨p, hp, hp'âŸ© := Finset.mem_image.mp (Finset.mem_filter.mp hr).1
    rw [â† hp']
    exact MeasurableSet.inter (hs.2.1 p hp) ht
  Â· intro _ hr _ hr'
    obtain âŸ¨p, hp, hp'âŸ© := Finset.mem_image.mp (Finset.mem_filter.mp hr).1
    obtain âŸ¨q, hq, hq'âŸ© := Finset.mem_image.mp (Finset.mem_filter.mp hr').1
    rw [â† hp', â† hq']
    intro hpqt
    have hpq : p â‰  q := fun h â†¦ hpqt (congrFun (congrArg Inter.inter h) t)
    intro a ha ha'
    have hap : a â‰¤ p := by
      simp only [id_eq, Set.le_eq_subset, Set.subset_inter_iff] at ha
      exact ha.1
    have haq : a â‰¤ q := by
      simp only [id_eq, Set.le_eq_subset, Set.subset_inter_iff] at ha'
      exact ha'.1
    exact hs.2.2.1 hp hq hpq hap haq
  Â· intro _ hp
    exact (Finset.mem_filter.mp hp).2

/-!
## Definition of variation
-/

/-- Given a partition `E` of a set `s`, this returns the sum of the norm of the measure of the
elements of that partition. -/
private noncomputable def varOfPart (P : Finset (Set X)) := âˆ‘ p âˆˆ P, â€–Î¼ pâ€–â‚‘

open Classical in
noncomputable def variation_aux (s : Set X) :=
    if (MeasurableSet s) then â¨† P âˆˆ partitions s, varOfPart Î¼ P else 0

/-- `variation_aux` of the empty set is equal to zero. -/
lemma variation_empty' : variation_aux Î¼ âˆ… = 0 := by
  simp [variation_aux, varOfPart, partitions_empty]

/-- variation_aux is monotone as a function of the set. -/
lemma variation_aux_monotone {sâ‚ sâ‚‚ : Set X} (h : sâ‚ âŠ† sâ‚‚) (hsâ‚ : MeasurableSet sâ‚)
    (hsâ‚‚ : MeasurableSet sâ‚‚) : variation_aux Î¼ sâ‚ â‰¤ variation_aux Î¼ sâ‚‚ := by
  simp only [variation_aux, hsâ‚, reduceIte, hsâ‚‚]
  exact iSup_le_iSup_of_subset (partitions_monotone h)

lemma variation_aux_lt {s : Set X} (hs : MeasurableSet s) {a : â„â‰¥0âˆ} (ha : a < variation_aux Î¼ s) :
    âˆƒ P âˆˆ partitions s, a < varOfPart Î¼ P := by
  simp only [variation_aux, hs, reduceIte] at ha
  exact lt_biSup_iff.mp ha

lemma variation_aux_le' {s : Set X} (hs : MeasurableSet s) {Îµ : NNReal} (hÎµ: 0 < Îµ)
    (h : variation_aux Î¼ s â‰  âŠ¤) :
    âˆƒ P âˆˆ partitions s, variation_aux Î¼ s â‰¤ varOfPart Î¼ P + Îµ := by
  let Îµ' := min Îµ (variation_aux Î¼ s).toNNReal
  have hÎµ1 : Îµ' â‰¤ variation_aux Î¼ s := by simp_all [Îµ']
  have _ : Îµ' â‰¤ Îµ := by simp_all [Îµ']
  obtain hw | hw : variation_aux Î¼ s â‰  0 âˆ¨ variation_aux Î¼ s = 0 := ne_or_eq _ _
  Â· have : 0 < Îµ' := by
      simp only [lt_inf_iff, Îµ']
      exact âŸ¨hÎµ, toNNReal_pos hw hâŸ©
    let a := variation_aux Î¼ s - Îµ'
    have ha : a < variation_aux Î¼ s := by
      dsimp [a]
      refine ENNReal.sub_lt_self h hw (by positivity)
    have ha' : variation_aux Î¼ s = a + Îµ' := by
      exact Eq.symm (tsub_add_cancel_of_le hÎµ1)
    obtain âŸ¨P, hP, hP'âŸ© := variation_aux_lt Î¼ hs ha
    refine âŸ¨P, hP, ?_âŸ©
    calc variation_aux Î¼ s
      _ = a + Îµ' := ha'
      _ â‰¤ varOfPart Î¼ P + Îµ' := by
        exact (ENNReal.add_le_add_iff_right coe_ne_top).mpr (le_of_lt hP')
      _ â‰¤ varOfPart Î¼ P + Îµ := by gcongr
  Â· simp_rw [hw, zero_le, and_true]
    exact âŸ¨{}, by simp, by simp [hs], by simp, by simpâŸ©

lemma le_variation_aux {s : Set X} (hs : MeasurableSet s) {P : Finset (Set X)}
    (hP : P âˆˆ partitions s) : varOfPart Î¼ P â‰¤ variation_aux Î¼ s := by
  simp only [variation_aux, hs, reduceIte]
  exact le_biSup (varOfPart Î¼) hP

open Classical in
/-- Given a partition `Q`, `varOfPart Î¼ Q` is bounded by the sum of the `varOfPart Î¼ (P i)` where
the `P i` are the partitions formed by restricting to a disjoint set of sets `s i`. -/
lemma varOfPart_le_tsum {s : â„• â†’ Set X} (hs : âˆ€ i, MeasurableSet (s i))
    (hs' : Pairwise (Disjoint on s)) {Q : Finset (Set X)} (hQ : Q âˆˆ partitions (â‹ƒ i, s i)) :
    varOfPart Î¼ Q â‰¤ âˆ‘' i, varOfPart Î¼ ({x âˆˆ Finset.image (fun q â†¦ q âˆ© s i) Q | x â‰  âˆ…}) := by
  let P (i : â„•) := (Q.image (fun q â†¦ q âˆ© (s i))).filter (Â· â‰  âˆ…)
  calc
    _ = âˆ‘ q âˆˆ Q, â€–Î¼ qâ€–â‚‘ := by simp [varOfPart]
    _ = âˆ‘ q âˆˆ Q, â€–Î¼ (â‹ƒ i, q âˆ© s i)â€–â‚‘ := ?_
    _ â‰¤ âˆ‘ q âˆˆ Q, âˆ‘' i, â€–Î¼ (q âˆ© s i)â€–â‚‘ := ?_
    _ = âˆ‘' i, âˆ‘ q âˆˆ Q, â€–Î¼ (q âˆ© s i)â€–â‚‘ := ?_
    _ â‰¤ âˆ‘' i, âˆ‘ p âˆˆ (P i), â€–Î¼ pâ€–â‚‘ := ?_
    _ = âˆ‘' i, (varOfPart Î¼ (P i)) := by simp [varOfPart]
  Â· -- Each `q` is equal to the union of `q âˆ© s i`.
    suffices h : âˆ€ q âˆˆ Q, q = â‹ƒ i, q âˆ© s i by
      refine Finset.sum_congr rfl (fun q hq â†¦ ?_)
      simp_rw [â† h q hq]
    intro q hq
    ext x
    constructor
    Â· intro hx
      obtain âŸ¨_, hsâŸ© := (hQ.1 q hq) hx
      obtain âŸ¨i, _âŸ© := Set.mem_range.mp hs.1
      simp_all [Set.mem_iUnion_of_mem i]
    Â· intro _
      simp_all
  Â· -- Additivity of the measure since the `s i` are pairwise disjoint.
    gcongr with p hp
    have : Î¼ (â‹ƒ i, p âˆ© s i) = âˆ‘' i, Î¼ (p âˆ© s i) := by
      have hps : âˆ€ i, MeasurableSet (p âˆ© s i) := by
        intro i
        refine MeasurableSet.inter (hQ.2.1 p hp) (hs i)
      have hps' : Pairwise (Disjoint on fun i â†¦ p âˆ© s i) := by
        refine (Symmetric.pairwise_on (fun â¦ƒx yâ¦„ a â†¦ Disjoint.symm a) fun i â†¦ p âˆ© s i).mpr ?_
        intro _ _ _
        refine Disjoint.inter_left' p (Disjoint.inter_right' p ?_)
        exact hs' (by omega)
      exact VectorMeasure.of_disjoint_iUnion hps hps'
    rw [this]
    exact enorm_tsum_le_tsum_enorm
  Â· -- Swapping the order of the sum.
    refine Eq.symm (Summable.tsum_finsetSum (fun _ _ â†¦ ENNReal.summable))
  Â· -- By defintion of the restricted partition
    refine ENNReal.tsum_le_tsum ?_
    intro i
    calc âˆ‘ q âˆˆ Q, â€–Î¼ (q âˆ© s i)â€–â‚‘
      _ = âˆ‘ p âˆˆ (Finset.image (fun q â†¦ q âˆ© s i) Q), â€–Î¼ pâ€–â‚‘ := by
        refine Eq.symm (Finset.sum_image_of_disjoint ?_ ?_)
        Â· simp
        Â· intro p hp q hq hpq
          refine Disjoint.inter_left (s i) (Disjoint.inter_right (s i) ?_)
          exact hQ.2.2.1 hp hq hpq
      _ â‰¤  âˆ‘ p âˆˆ P i, â€–Î¼ pâ€–â‚‘ := by
        refine Finset.sum_le_sum_of_ne_zero ?_
        intro p hp hp'
        dsimp [P]
        obtain hc | hc : p = âˆ… âˆ¨ Â¬p = âˆ… := eq_or_ne p âˆ…
        Â· simp [hc] at hp'
        Â· rw [Finset.mem_filter, Finset.mem_image]
          refine âŸ¨?_, hcâŸ©
          obtain âŸ¨q, _, _âŸ© := Finset.mem_image.mp hp
          use q

/-- Aditivity of `variation_aux` for disjoint measurable sets. -/
lemma variation_m_iUnion' (s : â„• â†’ Set X) (hs : âˆ€ i, MeasurableSet (s i))
    (hs' : Pairwise (Disjoint on s)) :
    HasSum (fun i â†¦ variation_aux Î¼ (s i)) (variation_aux Î¼ (â‹ƒ i, s i)) := by
  rw [ENNReal.hasSum_iff]
  constructor
  Â· -- The sum of `variation_aux Î¼ (s i)` is le `variation_aux Î¼ (â‹ƒ i, s i)`.
    intro n
    wlog hn : n â‰  0
    Â· simp [show n = 0 by omega]
    apply ENNReal.le_of_forall_pos_le_add
    intro Îµ' hÎµ' hsnetop
    let Îµ := Îµ' / n
    have hÎµ : 0 < Îµ := by positivity
    have hs'' i : variation_aux Î¼ (s i) â‰  âŠ¤ := by
      have : s i âŠ† â‹ƒ i, s i := Set.subset_iUnion_of_subset i fun â¦ƒaâ¦„ a â†¦ a
      have := variation_aux_monotone Î¼ this (hs i) (MeasurableSet.iUnion hs)
      exact lt_top_iff_ne_top.mp <| lt_of_le_of_lt this hsnetop
    -- For each set `s i` we choose a partition `P i` such that, for each `i`,
    -- `variation_aux Î¼ (s i) â‰¤ varOfPart Î¼ (P i) + Îµ`.
    choose P hP using fun i â†¦ variation_aux_le' Î¼ (hs i) (hÎµ) (hs'' i)
    calc âˆ‘ i âˆˆ Finset.range n, variation_aux Î¼ (s i)
      _ â‰¤ âˆ‘ i âˆˆ Finset.range n, (varOfPart Î¼ (P i) + Îµ) := by
        gcongr with i hi
        exact (hP i).2
      _ = âˆ‘ i âˆˆ Finset.range n, varOfPart Î¼ (P i) + Îµ' := by
        rw [Finset.sum_add_distrib]
        norm_cast
        simp [show n * Îµ = Îµ' by rw [mul_div_cancelâ‚€ _ (by positivity)]]
      _ â‰¤ variation_aux Î¼ (â‹ƒ i, s i) + Îµ' := by
        -- Since the union of the partitions `P i` is a partition of `â‹ƒ i, s i`, we know that
        -- `âˆ‘' i, varOfPart Î¼ (E i) â‰¤ variation_aux Î¼ (â‹ƒ i, s i)`.
        suffices h : âˆ‘ i âˆˆ Finset.range n, varOfPart Î¼ (P i) â‰¤ variation_aux Î¼ (â‹ƒ i, s i) by gcongr
        classical
        let Q := Finset.biUnion (Finset.range n) P
        have hQ : Q âˆˆ partitions (â‹ƒ i, s i) := partition_union hs' (fun i â†¦ (hP i).1) n
        calc
          _ = âˆ‘ i âˆˆ Finset.range n, âˆ‘ p âˆˆ P i, â€–Î¼ pâ€–â‚‘ := by simp [varOfPart]
          _ = âˆ‘ q âˆˆ Q, â€–Î¼ qâ€–â‚‘ := by
            refine Eq.symm (Finset.sum_biUnion ?_)
            intro l _ m _ hlm
            exact partitions_disjoint (hs' hlm) (hP l).1 (hP m).1
          _ â‰¤ variation_aux Î¼ (â‹ƒ i, s i) := by
            have := le_variation_aux Î¼ (MeasurableSet.iUnion hs) hQ
            simpa
  Â· -- Variation of the union, `variation_aux Î¼ (â‹ƒ i, s i)` le the sum of `variation_aux Î¼ (s i)`.
    intro b hb
    -- simp only [variation_aux, hs, reduceIte]
    simp only [variation_aux, MeasurableSet.iUnion hs, reduceIte] at hb
    obtain âŸ¨Q, hQ, hbQâŸ© := lt_biSup_iff.mp hb
    -- Take the partitions defined as intersection of `Q` and `s i`.
    classical
    let P (i : â„•) := (Q.image (fun q â†¦ q âˆ© (s i))).filter (Â· â‰  âˆ…)
    have hP (i : â„•) : P i âˆˆ partitions (s i) := partition_restrict hQ (hs i)
    have hP' := calc
      b < varOfPart Î¼ Q := hbQ
      _ â‰¤ âˆ‘' i, varOfPart Î¼ (P i) := by exact varOfPart_le_tsum Î¼ hs hs' hQ
    have := tendsto_nat_tsum fun i â†¦ VectorMeasure.varOfPart Î¼ (P i)
    obtain âŸ¨n, hn, hn'âŸ© := (((tendsto_order.mp this).1 b hP').and (Ici_mem_atTop 1)).exists
    use n
    calc
      b < âˆ‘ i âˆˆ Finset.range n, varOfPart Î¼ (P i) := hn
      _ â‰¤ âˆ‘ i âˆˆ Finset.range n, variation_aux Î¼ (s i) := by
        gcongr with i hi
        exact le_variation_aux Î¼ (hs i) (hP i)

/-- The variation of a vector-valued measure as a `VectorMeasure`. -/
noncomputable def variation : VectorMeasure X â„â‰¥0âˆ where
  measureOf'          := variation_aux Î¼
  empty'              := variation_empty' Î¼
  not_measurable' _ h := if_neg h
  m_iUnion'           := variation_m_iUnion' Î¼

end VectorMeasure

/-!
## Section : properties of variation
-/

namespace VectorMeasure
variable {X V ğ•œ : Type*} [MeasurableSpace X] [NormedAddCommGroup V] [NormedField ğ•œ]
  [NormedSpace ğ•œ V] (Î¼ : VectorMeasure X V)

theorem norm_measure_le_variation (Î¼ : VectorMeasure X V) (E : Set X) :
    â€–Î¼ Eâ€–â‚‘ â‰¤ variation Î¼ E := by
  dsimp [variation, variation_aux]
  wlog hE' : E â‰  âˆ…
  Â· push_neg at hE'
    simp [hE', varOfPart, partitions_empty]
  by_cases hE : Â¬MeasurableSet E
  Â· simp [hE, Î¼.not_measurable' hE]
  Â· push_neg at hE
    simp only [hE, reduceIte, varOfPart]
    let P' : Finset (Set X) := {E}
    have hP' : P' âˆˆ partitions E := by
      refine âŸ¨by simp [P'], by simpa [P'], by simp [P'], by simpa [P']âŸ©
    have hEP' : â€–Î¼ Eâ€–â‚‘ = varOfPart Î¼ P' := by
      simp [varOfPart, P']
    rw [hEP']
    have := le_biSup (fun P â†¦ âˆ‘ p âˆˆ P, â€–Î¼ pâ€–â‚‘) hP'
    simp only [Finset.sum_singleton] at this
    exact this

end VectorMeasure
