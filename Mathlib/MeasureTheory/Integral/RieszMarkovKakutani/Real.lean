/-
Copyright (c) 2024 Yoh Tanimoto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yoh Tanimoto, Oliver Butterley
-/
import Mathlib.MeasureTheory.Integral.Bochner.Set
import Mathlib.MeasureTheory.Integral.CompactlySupported
import Mathlib.MeasureTheory.Integral.RieszMarkovKakutani.Basic
import Mathlib.Order.Interval.Set.Union

/-!
# Rieszâ€“Markovâ€“Kakutani representation theorem for real-linear functionals

The Rieszâ€“Markovâ€“Kakutani representation theorem relates linear functionals on spaces of continuous
functions on a locally compact space to measures.

There are many closely related variations of the theorem. This file contains that proof of the
version where the space is a locally compact T2 space, the linear functionals are real and the
continuous functions have compact support.

## Main definitions & statements

* `RealRMK.rieszMeasure`: the measure induced by a real linear positive functional.
* `RealRMK.integral_rieszMeasure`: the Rieszâ€“Markovâ€“Kakutani representation theorem for a real
  linear positive functional.
* `RealRMK.rieszMeasure_integralPositiveLinearMap`: the uniqueness of the representing measure in
  the Rieszâ€“Markovâ€“Kakutani representation theorem.

## Implementation notes

The measure is defined through `rieszContent` which is for `NNReal` using the `toNNRealLinear`
version of `Î›`.

The Rieszâ€“Markovâ€“Kakutani representation theorem is first proved for `Real`-linear `Î›` because
equality is proven using two inequalities by considering `Î› f` and `Î› (-f)` for all functions
`f`, yet on `C_c(X, â„â‰¥0)` there is no negation.

## References

* [Walter Rudin, Real and Complex Analysis.][Rud87]
-/

open scoped ENNReal
open CompactlySupported CompactlySupportedContinuousMap Filter Function Set Topology
  TopologicalSpace MeasureTheory

namespace RealRMK

variable {X : Type*} [TopologicalSpace X] [T2Space X] [LocallyCompactSpace X] [MeasurableSpace X]
  [BorelSpace X]
variable (Î› : C_c(X, â„) â†’â‚š[â„] â„)

/-- The measure induced for `Real`-linear positive functional `Î›`, defined through `toNNRealLinear`
and the `NNReal`-version of `rieszContent`. This is under the namespace `RealRMK`, while
`rieszMeasure` without namespace is for `NNReal`-linear `Î›`. -/
noncomputable def rieszMeasure := (rieszContent (toNNRealLinear Î›)).measure

/-- If `f` assumes values between `0` and `1` and the support is contained in `V`, then
`Î› f â‰¤ rieszMeasure V`. -/
lemma le_rieszMeasure_tsupport_subset {f : C_c(X, â„)} (hf : âˆ€ (x : X), 0 â‰¤ f x âˆ§ f x â‰¤ 1)
    {V : Set X} (hV : tsupport f âŠ† V) : ENNReal.ofReal (Î› f) â‰¤ rieszMeasure Î› V := by
  apply le_trans _ (measure_mono hV)
  have := Content.measure_eq_content_of_regular (rieszContent (toNNRealLinear Î›))
    (contentRegular_rieszContent (toNNRealLinear Î›)) (âŸ¨tsupport f, f.hasCompactSupportâŸ©)
  rw [â† Compacts.coe_mk (tsupport f) f.hasCompactSupport, rieszMeasure, this, rieszContent,
    ENNReal.ofReal_eq_coe_nnreal (Î›.map_nonneg fun x â†¦ (hf x).1), Content.mk_apply,
    ENNReal.coe_le_coe]
  apply le_iff_forall_pos_le_add.mpr
  intro _ hÎµ
  obtain âŸ¨g, hgâŸ© := exists_lt_rieszContentAux_add_pos (toNNRealLinear Î›)
    âŸ¨tsupport f, f.hasCompactSupportâŸ© (Real.toNNReal_pos.mpr hÎµ)
  simp_rw [NNReal.val_eq_coe, Real.toNNReal_coe] at hg
  refine (Î›.mono ?_).trans hg.2.le
  intro x
  by_cases hx : x âˆˆ tsupport f
  Â· simpa using le_trans (hf x).2 (hg.1 x hx)
  Â· simp [image_eq_zero_of_notMem_tsupport hx]

/-- If `f` assumes the value `1` on a compact set `K` then `rieszMeasure K â‰¤ Î› f`. -/
lemma rieszMeasure_le_of_eq_one {f : C_c(X, â„)} (hf : âˆ€ x, 0 â‰¤ f x) {K : Set X}
    (hK : IsCompact K) (hfK : âˆ€ x âˆˆ K, f x = 1) : rieszMeasure Î› K â‰¤ ENNReal.ofReal (Î› f) := by
  rw [â† Compacts.coe_mk K hK, rieszMeasure,
    Content.measure_eq_content_of_regular _ (contentRegular_rieszContent (toNNRealLinear Î›))]
  apply ENNReal.coe_le_iff.mpr
  intro p hp
  rw [â† ENNReal.ofReal_coe_nnreal,
    ENNReal.ofReal_eq_ofReal_iff (Î›.map_nonneg hf) NNReal.zero_le_coe] at hp
  apply csInf_le'
  rw [Set.mem_image]
  use f.nnrealPart
  simp_rw [Set.mem_setOf_eq, nnrealPart_apply, Real.one_le_toNNReal]
  refine âŸ¨(fun x hx â†¦ Eq.ge (hfK x hx)), ?_âŸ©
  apply NNReal.eq
  rw [toNNRealLinear_apply, show f.nnrealPart.toReal = f by ext z; simp [hf z], hp]

omit [T2Space X] [LocallyCompactSpace X] in
/-- Given `f : C_c(X, â„)` such that `range f âŠ† [a, b]` we obtain a partition of the support of `f`
determined by partitioning `[a, b]` into `N` pieces. -/
lemma range_cut_partition (f : C_c(X, â„)) (a : â„) {Îµ : â„} (hÎµ : 0 < Îµ) (N : â„•)
    (hf : range f âŠ† Ioo a (a + N * Îµ)) : âˆƒ (E : Fin N â†’ Set X), tsupport f = â‹ƒ j, E j âˆ§
    univ.PairwiseDisjoint E âˆ§ (âˆ€ n : Fin N, âˆ€ x âˆˆ E n, a + Îµ * n < f x âˆ§ f x â‰¤ a + Îµ * (n + 1)) âˆ§
    âˆ€ n : Fin N, MeasurableSet (E n) := by
  let b := a + N * Îµ
  let y : Fin N â†’ â„ := fun n â†¦ a + Îµ * (n + 1)
  -- By definition `y n` and `y m` are separated by at least `Îµ`.
  have hy {n m : Fin N} (h : n < m) : y n + Îµ â‰¤ y m := calc
    _ â‰¤ a + Îµ * m + Îµ := by
      exact add_le_add_three (by rfl) ((mul_le_mul_iff_of_pos_left hÎµ).mpr (by norm_cast)) (by rfl)
    _ = _ := by dsimp [y]; rw [mul_add, mul_one, add_assoc]
  -- Define `E n` as the inverse image of the interval `(y n - Îµ, y n]`.
  let E (n : Fin N) := (f â»Â¹' Ioc (y n - Îµ) (y n)) âˆ© (tsupport f)
  use E
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· -- The sets `E n` are a partition of the support of `f`.
    have partition_aux : range f âŠ† â‹ƒ n, Ioc (y n - Îµ) (y n) := calc
      _ âŠ† Ioc (a + (0 : â„•) * Îµ) (a + N * Îµ) := by
        intro _ hz
        simpa using Ioo_subset_Ioc_self (hf hz)
      _ âŠ† â‹ƒ i âˆˆ Finset.range N, Ioc (a + â†‘i * Îµ) (a + â†‘(i + 1) * Îµ) :=
        Ioc_subset_biUnion_Ioc N (fun n â†¦ a + n * Îµ)
      _ âŠ† _ := by
        intro z
        simp only [Finset.mem_range, mem_iUnion, mem_Ioc, forall_exists_index, and_imp, y]
        refine fun n hn _ _ â†¦ âŸ¨âŸ¨n, hnâŸ©, âŸ¨by linarith, by simp_all [mul_comm Îµ _]âŸ©âŸ©
    simp only [E, â† iUnion_inter, â† preimage_iUnion, eq_comm (a := tsupport _), inter_eq_right]
    exact fun x _ â†¦ partition_aux (mem_range_self x)
  Â· -- The sets `E n` are pairwise disjoint.
    intro m _ n _ hmn
    apply Disjoint.preimage
    simp_rw [mem_preimage, mem_Ioc, disjoint_left]
    intro x hx
    rw [mem_setOf_eq, and_assoc] at hx
    simp_rw [mem_setOf_eq, not_and_or, not_lt, not_le, or_assoc]
    rcases (by cutsat : m < n âˆ¨ n < m) with hc | hc
    Â· left
      exact le_trans hx.2.1 (le_tsub_of_add_le_right (hy hc))
    Â· right; left
      exact lt_of_le_of_lt (le_tsub_of_add_le_right (hy hc)) hx.1
  Â· -- Upper and lower bound on `f x` follow from the definition of `E n` .
    intro _ _ hx
    simp only [mem_inter_iff, mem_preimage, mem_Ioc, E, y] at hx
    constructor <;> linarith
  Â· exact fun _ â†¦ (f.1.measurable measurableSet_Ioc).inter measurableSet_closure

omit [LocallyCompactSpace X] in
/-- Given a set `E`, a function `f : C_c(X, â„)`, `0 < Îµ` and `âˆ€ x âˆˆ E, f x < c`, there exists an
open set `V` such that `E âŠ† V` and the sets are similar in measure and `âˆ€ x âˆˆ V, f x < c`. -/
lemma exists_open_approx (f : C_c(X, â„)) {Îµ : â„} (hÎµ : 0 < Îµ) (E : Set X) {Î¼ : Content X}
    (hÎ¼ : Î¼.outerMeasure E â‰  âˆ) (hÎ¼' : MeasurableSet E) {c : â„} (hfE : âˆ€ x âˆˆ E, f x < c) :
    âˆƒ (V : Opens X), E âŠ† V âˆ§ (âˆ€ x âˆˆ V, f x < c) âˆ§ Î¼.measure V â‰¤ Î¼.measure E + ENNReal.ofReal Îµ := by
  have hÎµ' := ne_of_gt <| Real.toNNReal_pos.mpr hÎµ
  obtain âŸ¨Vâ‚ : Opens X, hVâ‚âŸ© := Content.outerMeasure_exists_open Î¼ hÎ¼ hÎµ'
  let Vâ‚‚ : Opens X := âŸ¨(f â»Â¹' Iio c), IsOpen.preimage f.1.2 isOpen_IioâŸ©
  use Vâ‚ âŠ“ Vâ‚‚
  refine âŸ¨subset_inter hVâ‚.1 hfE, ?_, ?_âŸ©
  Â· intro x hx
    suffices âˆ€ x âˆˆ Vâ‚‚.carrier, f x < c from this x (mem_of_mem_inter_right hx)
    exact fun _ a â†¦ a
  Â· calc
      _ â‰¤ Î¼.measure Vâ‚ := by simp [measure_mono]
      _ = Î¼.outerMeasure Vâ‚ := Content.measure_apply Î¼ (Vâ‚.2.measurableSet)
      _ â‰¤ Î¼.outerMeasure E + Îµ.toNNReal := hVâ‚.2
      _ = _ := by rw [Content.measure_apply Î¼ hÎ¼', ENNReal.ofNNReal_toNNReal]

/-- Choose `N` sufficiently large such that a particular quantity is small. -/
private lemma exists_nat_large (a' b' : â„) {Îµ : â„} (hÎµ : 0 < Îµ) : âˆƒ (N : â„•), 0 < N âˆ§
    a' / N * (b' + a' / N) â‰¤ Îµ := by
  have A : Tendsto (fun (N : â„) â†¦ a' / N * (b' + a' / N)) atTop (ğ“ (0 * (b' + 0))) := by
    apply Tendsto.mul
    Â· exact Tendsto.div_atTop tendsto_const_nhds tendsto_id
    Â· exact Tendsto.add tendsto_const_nhds (Tendsto.div_atTop tendsto_const_nhds tendsto_id)
  have B := A.comp tendsto_natCast_atTop_atTop
  simp only [add_zero, zero_mul] at B
  obtain âŸ¨N, hN, h'NâŸ© := (((tendsto_order.1 B).2 _ hÎµ).and (Ici_mem_atTop 1)).exists
  exact âŸ¨N, h'N, hN.leâŸ©

/-- The main estimate in the proof of the Riesz-Markov-Kakutani: `Î› f` is bounded above by the
integral of `f` with respect to the `rieszMeasure` associated to `Î›`. -/
private lemma integral_riesz_aux (f : C_c(X, â„)) : Î› f â‰¤ âˆ« x, f x âˆ‚(rieszMeasure Î›) := by
  let Î¼ := rieszMeasure Î›
  let K := tsupport f
  -- Suffices to show that `Î› f â‰¤ âˆ« x, f x âˆ‚Î¼ + Îµ` for arbitrary `Îµ`.
  apply le_iff_forall_pos_le_add.mpr
  intro Îµ hÎµ
  -- Choose an interval `(a, b)` which contains the range of `f`.
  obtain âŸ¨a, b, habâŸ© : âˆƒ a b : â„, a < b âˆ§ range f âŠ† Ioo a b := by
    obtain âŸ¨r, hrâŸ© := (Metric.isCompact_iff_isClosed_bounded.mp
      (HasCompactSupport.isCompact_range f.2 f.1.2)).2.subset_ball_lt 0 0
    exact âŸ¨-r, r, by linarith, hr.2.trans_eq (by simp [Real.ball_eq_Ioo])âŸ©
  -- Choose `N` positive and sufficiently large such that `Îµ'` is sufficiently small
  obtain âŸ¨N, hN, hÎµ'âŸ© := exists_nat_large (b - a) (2 * Î¼.real K + |a| + b) hÎµ
  let Îµ' := (b - a) / N
  replace hÎµ' : 0 < Îµ' âˆ§  Îµ' * (2 * Î¼.real K + |a| + b + Îµ') â‰¤ Îµ :=
    âŸ¨div_pos (sub_pos.mpr hab.1) (Nat.cast_pos'.mpr hN), hÎµ'âŸ©
  -- Take a partition of the support of `f` into sets `E` by partitioning the range.
  obtain âŸ¨E, hEâŸ© := range_cut_partition f a hÎµ'.1 N <| by
    dsimp [Îµ']
    field_simp
    simp [hab.2]
  -- Introduce notation for the partition of the range.
  let y : Fin N â†’ â„ := fun n â†¦ a + Îµ' * (n + 1)
  -- The measure of each `E n` is finite.
  have hE' (n : Fin N) : Î¼ (E n) â‰  âˆ := by
    have h : E n âŠ† tsupport f := by rw [hE.1]; exact subset_iUnion _ _
    refine lt_top_iff_ne_top.mp ?_
    apply lt_of_le_of_lt <| measure_mono h
    dsimp [Î¼]
    rw [rieszMeasure, â† coe_toContinuousMap, â† ContinuousMap.toFun_eq_coe,
      Content.measure_apply _ f.2.measurableSet]
    exact Content.outerMeasure_lt_top_of_isCompact _ f.2
  -- Define sets `V` which are open approximations to the sets `E`
  obtain âŸ¨V, hVâŸ© : âˆƒ V : Fin N â†’ Opens X, âˆ€ n, E n âŠ† (V n) âˆ§ (âˆ€ x âˆˆ V n, f x < y n + Îµ') âˆ§
      Î¼ (V n) â‰¤ Î¼ (E n) + ENNReal.ofReal (Îµ' / N) := by
    have h_Îµ' := (div_pos hÎµ'.1 (Nat.cast_pos'.mpr hN))
    have h n x (hx : x âˆˆ E n) := lt_add_of_le_of_pos ((hE.2.2.1 n x hx).right) hÎµ'.1
    have h' n := Eq.trans_ne
      (Content.measure_apply (rieszContent (toNNRealLinear Î›)) (hE.2.2.2 n)).symm (hE' n)
    choose V hV using fun n â†¦ exists_open_approx f h_Îµ' (E n) (h' n) (hE.2.2.2 n) (h n)
    exact âŸ¨V, hVâŸ©
  -- Define a partition of unity subordinated to the sets `V`
  obtain âŸ¨g, hgâŸ© : âˆƒ g : Fin N â†’ C_c(X, â„), (âˆ€ n, tsupport (g n) âŠ† (V n).carrier) âˆ§
      EqOn (âˆ‘ n : Fin N, (g n)) 1 (tsupport f.toFun) âˆ§ (âˆ€ n x, (g n) x âˆˆ Icc 0 1) âˆ§
      âˆ€ n, HasCompactSupport (g n) := by
    have : tsupport f âŠ† â‹ƒ n, (V n).carrier := calc
      _ = â‹ƒ j, E j := hE.1
      _ âŠ† _ := by gcongr with n; exact (hV n).1
    obtain âŸ¨g', hgâŸ© := exists_continuous_sum_one_of_isOpen_isCompact (fun n â†¦ (V n).2) f.2 this
    exact âŸ¨fun n â†¦ âŸ¨g' n, hg.2.2.2 nâŸ©, hgâŸ©
  -- The proof is completed by a chain of inequalities.
  calc Î› f
    _ = Î› (âˆ‘ n, g n â€¢ f) := ?_
    _ = âˆ‘ n, Î› (g n â€¢ f) := by simp
    _ â‰¤ âˆ‘ n, Î› ((y n + Îµ') â€¢ g n) := ?_
    _ = âˆ‘ n, (y n + Îµ') * Î› (g n) := by simp
    -- That `y n + Îµ'` can be negative is bad in the inequalities so we artificially include `|a|`.
    _ = âˆ‘ n, (|a| + y n + Îµ') * Î› (g n) - |a| * âˆ‘ n, Î› (g n) :=
      by simp [add_assoc, add_mul |a|, Finset.sum_add_distrib, Finset.mul_sum]
    _ â‰¤ âˆ‘ n, (|a| + y n + Îµ') * (Î¼.real (E n) + Îµ' / N) - |a| * âˆ‘ n, Î› (g n) := ?_
    _ â‰¤ âˆ‘ n, (|a| + y n + Îµ') * (Î¼.real (E n) + Îµ' / N) - |a| * Î¼.real K := ?_
    _ = âˆ‘ n, (y n - Îµ') * Î¼.real (E n) +
      2 * Îµ' * Î¼.real K + Îµ' / N * âˆ‘ n, (|a| + y n + Îµ') := ?_
    _ â‰¤ âˆ« x, f x âˆ‚Î¼ + 2 * Îµ' * Î¼.real K + Îµ' / N * âˆ‘ n, (|a| + y n + Îµ') := ?_
    _ â‰¤ âˆ« x, f x âˆ‚Î¼ + Îµ' * (2 * Î¼.real K + |a| + b + Îµ') := ?_
    _ â‰¤ âˆ« x, f x âˆ‚Î¼ + Îµ := by simp [hÎµ'.2]
  Â· -- Equality since `âˆ‘ i : Fin N, (g i)` is equal to unity on the support of `f`
    congr; ext x
    simp only [coe_sum, smul_eq_mul, coe_mul, Pi.mul_apply,
      â† Finset.sum_mul]
    by_cases hx : x âˆˆ tsupport f
    Â· simp [hg.2.1 hx]
    Â· simp [image_eq_zero_of_notMem_tsupport hx]
  Â· -- Use that `f â‰¤ y n + Îµ'` on `V n`
    gcongr with n hn
    intro x
    by_cases hx : x âˆˆ tsupport (g n)
    Â· rw [smul_eq_mul, mul_comm]
      apply mul_le_mul_of_nonneg_right ?_ (hg.2.2.1 n x).1
      exact le_of_lt <| (hV n).2.1 x <| mem_of_subset_of_mem (hg.1 n) hx
    Â· simp [image_eq_zero_of_notMem_tsupport hx]
  Â· -- Use that `Î› (g n) â‰¤ Î¼ (V n)).toReal â‰¤ Î¼ (E n)).toReal + Îµ' / N`
    gcongr with n hn
    Â· calc
        _ â‰¤ |a| + a := neg_le_iff_add_nonneg'.mp <| neg_abs_le a
        _ â‰¤ |a| + a + Îµ' * (n + 1) := (le_add_iff_nonneg_right (|a| + a)).mpr <| Left.mul_nonneg
          (le_of_lt hÎµ'.1) <| Left.add_nonneg (Nat.cast_nonneg' n) (zero_le_one' â„)
        _ â‰¤ _ := by rw [â† add_assoc, le_add_iff_nonneg_right]; exact le_of_lt hÎµ'.1
    Â· calc
        _ â‰¤ Î¼.real (V n) := by
          apply (ENNReal.ofReal_le_iff_le_toReal _).mp
          Â· exact le_rieszMeasure_tsupport_subset Î› (fun x â†¦ hg.2.2.1 n x) (hg.1 n)
          Â· rw [â† lt_top_iff_ne_top]
            apply lt_of_le_of_lt (hV n).2.2
            rw [WithTop.add_lt_top]
            exact âŸ¨WithTop.lt_top_iff_ne_top.mpr (hE' n), ENNReal.ofReal_lt_topâŸ©
        _ â‰¤ _ := by
          rw [â† ENNReal.toReal_ofReal (div_nonneg (le_of_lt hÎµ'.1) (Nat.cast_nonneg _))]
          apply ENNReal.toReal_le_add (hV n).2.2 (hE' n)
          Â· finiteness
  Â· -- Use that `Î¼ K â‰¤ Î› (âˆ‘ n, g n)`
    gcongr
    rw [â† map_sum Î› g _]
    have h x : 0 â‰¤ (âˆ‘ n, g n) x := by simpa using Fintype.sum_nonneg fun n â†¦ (hg.2.2.1 n x).1
    apply ENNReal.toReal_le_of_le_ofReal
    Â· exact Î›.map_nonneg (fun x â†¦ h x)
    Â· have h' x (hx : x âˆˆ K) : (âˆ‘ n, g n) x = 1 := by simp [hg.2.1 hx]
      refine rieszMeasure_le_of_eq_one Î› h f.2 h'
  Â· -- Rearrange the sums
    have (n : Fin N) : (|a| + y n + Îµ') * Î¼.real (E n) =
        (|a| + 2 * Îµ') * Î¼.real (E n) + (y n - Îµ') * Î¼.real (E n) := by linarith
    simp_rw [mul_add, this]
    have : âˆ‘ i, Î¼.real (E i) = Î¼.real K := by
      suffices h : Î¼ K = âˆ‘ i, (Î¼ (E i)) by
        simp only [measureReal_def, h]
        exact Eq.symm <| ENNReal.toReal_sum <| fun n _ â†¦ hE' n
      dsimp [K]; rw [hE.1]
      rw [measure_iUnion (fun m n hmn â†¦ hE.2.1 trivial trivial hmn) hE.2.2.2]
      exact tsum_fintype fun b â†¦ Î¼ (E b)
    rw [Finset.sum_add_distrib, Finset.sum_add_distrib, â† Finset.mul_sum, this, â† Finset.sum_mul]
    linarith
  Â· -- Use that `y n - Îµ' â‰¤ f x` on `E n`
    gcongr
    have h : âˆ€ n, (y n - Îµ') * Î¼.real (E n) â‰¤ âˆ« x in (E n), f x âˆ‚Î¼ := by
      intro n
      apply setIntegral_ge_of_const_le (hE.2.2.2 n) (hE' n)
      Â· intro x hx
        dsimp [y]; linarith [(hE.2.2.1 n x hx).1]
      Â· apply Integrable.integrableOn
        dsimp [Î¼, rieszMeasure]
        exact Continuous.integrable_of_hasCompactSupport f.1.2 f.2
    calc
      _ â‰¤ âˆ‘ n, âˆ« (x : X) in E n, f x âˆ‚Î¼ := Finset.sum_le_sum fun i a â†¦ h i
      _ = âˆ« x in (â‹ƒ n, E n), f x âˆ‚Î¼ := by
        refine Eq.symm <| integral_iUnion_fintype hE.2.2.2 (fun _ _ â†¦ hE.2.1 trivial trivial) ?_
        dsimp [Î¼, rieszMeasure]
        exact fun _ â†¦
          Integrable.integrableOn <| Continuous.integrable_of_hasCompactSupport f.1.2 f.2
      _ = âˆ« x in tsupport f, f x âˆ‚Î¼ := by simp_rw [hE.1]
      _ = _ := setIntegral_tsupport
  Â· -- Rough bound of the sum
    have h : âˆ‘ n : Fin N, y n â‰¤ N * b := by
      have (n : Fin N) := calc y n
        _ â‰¤ a + Îµ' * N := by simp_all [y, show (n : â„) + 1 â‰¤ N by norm_cast; cutsat]
        _ = b := by simp [field, Îµ']
      have : âˆ‘ n, y n â‰¤ âˆ‘ n, b := Finset.sum_le_sum (fun n â†¦ fun _ â†¦ this n)
      simp_all
    simp only [Finset.sum_add_distrib, Finset.sum_add_distrib,
               Fin.sum_const, Fin.sum_const, nsmul_eq_mul, â† add_assoc, mul_add, â† mul_assoc]
    simpa [show (N : â„) â‰  0 by simp [hN.ne.symm], mul_comm _ Îµ', div_eq_mul_inv, mul_assoc]
      using (mul_le_mul_iff_of_pos_left hÎµ'.1).mpr <| (inv_mul_le_iffâ‚€ (Nat.cast_pos'.mpr hN)).mpr h

/-- The **Riesz-Markov-Kakutani representation theorem**: given a positive linear functional `Î›`,
the integral of `f` with respect to the `rieszMeasure` associated to `Î›` is equal to `Î› f`. -/
@[simp]
theorem integral_rieszMeasure (f : C_c(X, â„)) : âˆ« x, f x âˆ‚(rieszMeasure Î›) = Î› f := by
  -- We apply the result `Î› f â‰¤ âˆ« x, f x âˆ‚(rieszMeasure hÎ›)` to `f` and `-f`.
  apply le_antisymm
  -- prove the inequality for `- f`
  Â· calc
      _ = - âˆ« x, (-f) x âˆ‚(rieszMeasure Î›) := by simpa using integral_neg' (-f)
      _ â‰¤ - Î› (-f) := neg_le_neg (integral_riesz_aux Î› (-f))
      _ = _ := by simp
  -- prove the inequality for `f`
  Â· exact integral_riesz_aux Î› f

/-- The Riesz measure induced by a positive linear functional on `C_c(X, â„)` is regular. -/
instance regular_rieszMeasure : (rieszMeasure Î›).Regular :=
  (rieszContent _).regular

section integralPositiveLinearMap

variable {Î¼ Î½ : Measure X}

/-! We show that `RealRMK.rieszMeasure` is a bijection between positive linear functionals on
`C_c(X, â„)` and regular measures with inverse `RealRMK.integralPositiveLinearMap`. -/

/-- Note: the assumption `IsFiniteMeasureOnCompacts Î¼` cannot be removed. For example, if
`Î¼` is infinite on any nonempty set and `Î½ = 0`, then the hypotheses are satisfied. -/
lemma measure_le_of_isCompact_of_integral [Î½.OuterRegular]
    [IsFiniteMeasureOnCompacts Î½] [IsFiniteMeasureOnCompacts Î¼]
    (hÎ¼Î½ : âˆ€ f : C_c(X, â„), âˆ« x, f x âˆ‚Î¼ â‰¤ âˆ« x, f x âˆ‚Î½)
    â¦ƒK : Set Xâ¦„ (hK : IsCompact K) : Î¼ K â‰¤ Î½ K := by
  refine ENNReal.le_of_forall_pos_le_add fun Îµ hÎµ hÎ½ â†¦ ?_
  have hÎ½K : Î½ K â‰  âŠ¤ := hÎ½.ne
  have hÎ¼K : Î¼ K â‰  âŠ¤ := hK.measure_lt_top.ne
  obtain âŸ¨V, pV1, pV2, pV3âŸ© : âˆƒ V âŠ‡ K, IsOpen V âˆ§ Î½ V â‰¤ Î½ K + Îµ :=
    exists_isOpen_le_add K Î½ (ne_of_gt (ENNReal.coe_lt_coe.mpr hÎµ))
  suffices Î¼.real K â‰¤ Î½.real K + Îµ by
    rwa [â† ENNReal.toReal_le_toReal, ENNReal.toReal_add, ENNReal.coe_toReal]
    all_goals finiteness
  have VltTop : Î½ V < âŠ¤ := pV3.trans_lt <| by finiteness
  obtain âŸ¨f, pf1, pf2, pf3âŸ© :
      âˆƒ f : C_c(X, â„), Set.EqOn (â‡‘f) 1 K âˆ§ tsupport â‡‘f âŠ† V âˆ§ âˆ€ (x : X), f x âˆˆ Set.Icc 0 1 := by
    obtain âŸ¨f, hf1, hf2, hf3âŸ© := exists_continuous_one_of_compact_subset_open hK pV2 pV1
    exact âŸ¨âŸ¨f, hasCompactSupport_def.mpr hf2âŸ©, hf1, hf3âŸ©
  have hfV (x : X) : f x â‰¤ V.indicator 1 x := by
    by_cases hx : x âˆˆ tsupport f
    Â· simp [(pf2 hx), (pf3 x).2]
    Â· simp [image_eq_zero_of_notMem_tsupport hx, Set.indicator_nonneg]
  have hfK (x : X) : K.indicator 1 x â‰¤ f x := by
    by_cases hx : x âˆˆ K
    Â· simp [hx, pf1 hx]
    Â· simp [hx, (pf3 x).1]
  calc
    Î¼.real K = âˆ« x, K.indicator 1 x âˆ‚Î¼ := integral_indicator_one hK.measurableSet |>.symm
    _ â‰¤ âˆ« x, f x âˆ‚Î¼ := by
      refine integral_mono ?_ f.integrable hfK
      exact (continuousOn_const.integrableOn_compact hK).integrable_indicator hK.measurableSet
    _ â‰¤ âˆ« x, f x âˆ‚Î½ := hÎ¼Î½ f
    _ â‰¤ âˆ« x, V.indicator 1 x âˆ‚Î½ := by
      refine integral_mono f.integrable ?_ hfV
      exact integrableOn_const.integrable_indicator pV2.measurableSet
    _ â‰¤ (Î½ K).toReal + â†‘Îµ := by
      rwa [integral_indicator_one pV2.measurableSet, measureReal_def,
        â† ENNReal.coe_toReal, â† ENNReal.toReal_add, ENNReal.toReal_le_toReal]
      all_goals finiteness

/-- If two regular measures give the same integral for every function in `C_c(X, â„)`,
then they are equal. -/
theorem _root_.MeasureTheory.Measure.ext_of_integral_eq_on_compactlySupported
    [Î¼.Regular] [Î½.Regular] (hÎ¼Î½ : âˆ€ f : C_c(X, â„), âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î½) :
    Î¼ = Î½ := by
  apply Measure.OuterRegular.eq_of_eq_on_isOpen
  apply Measure.InnerRegularWRT.eq_on_outer_of_eq_on_inner Measure.Regular.innerRegular
    Measure.Regular.innerRegular
  intro K hK
  apply le_antisymm
  Â· exact measure_le_of_isCompact_of_integral (fun f â†¦ (hÎ¼Î½ f).le) hK
  Â· exact measure_le_of_isCompact_of_integral (fun f â†¦ (hÎ¼Î½ f).ge) hK

/-- Two regular measures are equal iff they induce the same positive linear functional
on `C_c(X, â„)`. -/
theorem integralPositiveLinearMap_inj [Î¼.Regular] [Î½.Regular] :
    integralPositiveLinearMap Î¼ = integralPositiveLinearMap Î½ â†” Î¼ = Î½ where
  mp hÎ¼Î½ := Measure.ext_of_integral_eq_on_compactlySupported fun f â†¦ congr($hÎ¼Î½ f)
  mpr _ := by congr

/-- Every regular measure is induced by a positive linear functional on `C_c(X, â„)`.
That is, `RealRMK.rieszMeasure` is a surjective function onto regular measures. -/
@[simp]
theorem rieszMeasure_integralPositiveLinearMap [Î¼.Regular] :
    rieszMeasure (integralPositiveLinearMap Î¼) = Î¼ :=
  Measure.ext_of_integral_eq_on_compactlySupported (by simp)

@[simp]
theorem integralPositiveLinearMap_rieszMeasure :
    integralPositiveLinearMap (rieszMeasure Î›) = Î› := by ext; simp

end integralPositiveLinearMap

end RealRMK
