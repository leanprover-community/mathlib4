/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Analysis.Calculus.ContDiff
import Mathlib.Analysis.Calculus.Deriv.Support
import Mathlib.Data.Finset.Interval
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.MeasureTheory.Integral.Marginal
import Mathlib.MeasureTheory.Integral.MeanInequalities

/-!
# Gagliardo-Nirenberg-Sobolev inequality
-/


open scoped Classical BigOperators Topology ENNReal
open Filter
set_option autoImplicit true

noncomputable section

variable {Î¹ Î¹' Î¹'' : Type _}

section Finset

open Finset

namespace Real

theorem prod_rpow {Î¹} (s : Finset Î¹) {f : Î¹ â†’ â„} (hf : 0 â‰¤ f) (r : â„) :
    âˆ i in s, f i ^ r = (âˆ i in s, f i) ^ r :=
  finset_prod_rpow s f (fun i _ â†¦ hf i) r

end Real

namespace NNReal

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

theorem rpow_add_of_nonneg (x : â„â‰¥0) {y z : â„} (hy : 0 â‰¤ y) (hz : 0 â‰¤ z) :
  x ^ (y + z) = x ^ y * x ^ z := by
  by_cases h : y + z = 0
  Â· obtain rfl : y = 0 := by linarith
    obtain rfl : z = 0 := by linarith
    simp [h]
  Â· exact rpow_add' _ h

end NNReal

namespace ENNReal

open NNReal

theorem rpow_add_of_nonneg {x : â„â‰¥0âˆ} (y z : â„) (hy : 0 â‰¤ y) (hz : 0 â‰¤ z) :
    x ^ (y + z) = x ^ y * x ^ z := by
  induction x using recTopCoe
  Â· rcases hy.eq_or_lt with rfl|hy
    Â· rw [rpow_zero, one_mul, zero_add]
    rcases hz.eq_or_lt with rfl|hz
    Â· rw [rpow_zero, mul_one, add_zero]
    simp [top_rpow_of_pos, hy, hz, add_pos hy hz]
  simp [coe_rpow_of_nonneg, hy, hz, add_nonneg hy hz, NNReal.rpow_add_of_nonneg _ hy hz]

theorem prod_rpow_of_nonneg {Î¹} {s : Finset Î¹} {f : Î¹ â†’ â„â‰¥0âˆ} {r : â„} (hr : 0 â‰¤ r) :
    âˆ i in s, f i ^ r = (âˆ i in s, f i) ^ r := by
  induction s using Finset.induction
  case empty => simp
  case insert i s hi ih => simp_rw [prod_insert hi, ih, â† mul_rpow_of_nonneg _ _ hr]

-- unused
theorem prod_rpow_of_ne_top {Î¹} {s : Finset Î¹} {f : Î¹ â†’ â„â‰¥0âˆ} (hf : âˆ€ i âˆˆ s, f i â‰  âˆ) (r : â„) :
    âˆ i in s, f i ^ r = (âˆ i in s, f i) ^ r := by
  induction s using Finset.induction
  case empty => simp
  case insert i s hi ih =>
    have h2f : âˆ€ i âˆˆ s, f i â‰  âˆ := fun i hi â†¦ hf i <| mem_insert_of_mem hi
    rw [prod_insert hi, prod_insert hi, ih h2f, â† mul_rpow_of_ne_top <| hf i <| mem_insert_self ..]
    apply prod_lt_top h2f |>.ne

-- unused
theorem prod_coe_rpow {Î¹} (s : Finset Î¹) (f : Î¹ â†’ â„â‰¥0) (r : â„) :
    âˆ i in s, (f i : â„â‰¥0âˆ) ^ r = ((âˆ i in s, f i : â„â‰¥0) : â„â‰¥0âˆ) ^ r := by
  induction s using Finset.induction
  case empty => simp
  case insert i s hi ih => simp_rw [prod_insert hi, ih, â† coe_mul_rpow, coe_mul]

end ENNReal

end Finset

section Calculus

variable {ğ•œ : Type _} [NontriviallyNormedField ğ•œ] [Fintype Î¹]
variable {E : Î¹ â†’ Type _} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
variable {F : Type _} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

theorem contDiff_update (k : â„•âˆ) (x : âˆ€ i, E i) (i : Î¹) : ContDiff ğ•œ k (Function.update x i) := by
  rw [contDiff_pi]
  intro j
  dsimp [Function.update]
  split_ifs with h
  Â· subst h
    exact contDiff_id
  Â· exact contDiff_const

theorem hasFDerivAt_sub_const {ğ•œ : Type _} [NontriviallyNormedField ğ•œ] {E : Type _}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E]  {x : E} (c : E) :
    HasFDerivAt (Â· - c) (ContinuousLinearMap.id ğ•œ (E)) x :=
  (hasFDerivAt_id x).sub_const c

theorem hasFDerivAt_update {x : âˆ€ i, E i} {i : Î¹} (y : E i) :
    HasFDerivAt (Function.update x i)
      (ContinuousLinearMap.pi (Function.update 0 i (ContinuousLinearMap.id ğ•œ (E i)))) y := by
  set l := (ContinuousLinearMap.pi (Function.update 0 i (ContinuousLinearMap.id ğ•œ (E i))))
  have update_eq : Function.update x i = (fun _ â†¦ x) + l âˆ˜ (Â· - x i)
  Â· ext t j
    dsimp [Function.update]
    split_ifs with hji
    Â· subst hji
      simp
    Â· simp
  rw [update_eq]
  convert (hasFDerivAt_const _ _).add (l.hasFDerivAt.comp y (hasFDerivAt_sub_const (x i)))
  rw [zero_add, ContinuousLinearMap.comp_id]

theorem fderiv_update {x : âˆ€ i, E i} {i : Î¹} (y : E i) :
    fderiv ğ•œ (Function.update x i) y =
      ContinuousLinearMap.pi (Function.update 0 i (ContinuousLinearMap.id ğ•œ (E i))) :=
  (hasFDerivAt_update y).fderiv

theorem hasDerivAt_update {x : Î¹ â†’ ğ•œ} {i : Î¹} (y : ğ•œ) :
    HasDerivAt (Function.update x i) (Pi.single i (1:ğ•œ)) y := by
  convert (hasFDerivAt_update (E := fun _ â†¦ ğ•œ) y).hasDerivAt
  ext z j
  rw [Pi.single, Function.update_apply]
  split_ifs with h
  Â· simp [h]
  Â· simp [Function.update_noteq h]

theorem deriv_update {x : Î¹ â†’ ğ•œ} {i : Î¹} (y : ğ•œ) :
    deriv (Function.update x i) y = (Pi.single i (1:ğ•œ)) :=
  (hasDerivAt_update y).deriv

open NNReal

theorem Pi.nnnorm_single (y : E i) : â€–Pi.single i yâ€–â‚Š = â€–yâ€–â‚Š := by
  classical
  have H : âˆ€ b, â€–single i y bâ€–â‚Š = single (f := fun _ â†¦ â„â‰¥0) i â€–yâ€–â‚Š b
  Â· intro b
    refine Pi.apply_single (fun i (x : E i) â†¦ â€–xâ€–â‚Š) ?_ i y b
    simp
  simp [Pi.nnnorm_def, H, Pi.single_apply, Finset.sup_ite,
    Finset.filter_eq' (Finset.univ : Finset Î¹)]

theorem Pi.norm_single (y : E i) : â€–Pi.single i yâ€– = â€–yâ€– :=
  congr_arg Subtype.val (Pi.nnnorm_single y)

end Calculus

section RealCalculus

open Set MeasureTheory

variable {E : Type*} {f f' : â„ â†’ E} {g g' : â„ â†’ â„} {a b l : â„} {m : E} [NormedAddCommGroup E]
  [NormedSpace â„ E] [CompleteSpace E]

/-- **Fundamental theorem of calculus-2**, on semi-infinite intervals `(-âˆ, a)`.
When a function has a limit `m` at `-âˆ`, and its derivative is integrable, then the
integral of the derivative on `(-âˆ, a)` is `f a - m`. Version assuming differentiability
on `(-âˆ, a)` and continuity on `(-âˆ, a]`.-/
theorem integral_Iio_of_hasDerivAt_of_tendsto (hcont : ContinuousOn f (Iic a))
    (hderiv : âˆ€ x âˆˆ Iio a, HasDerivAt f (f' x) x) (f'int : IntegrableOn f' (Iic a))
    (hf : Tendsto f atBot (ğ“ m)) : âˆ« x in Iic a, f' x = f a - m := by
  refine' tendsto_nhds_unique (intervalIntegral_tendsto_integral_Iic a f'int tendsto_id) _
  apply Tendsto.congr' _ (hf.const_sub _)
  filter_upwards [Iic_mem_atBot a] with x hx
  symm
  apply intervalIntegral.integral_eq_sub_of_hasDerivAt_of_le hx
    (hcont.mono Icc_subset_Iic_self) fun y hy => hderiv y hy.2
  rw [intervalIntegrable_iff_integrable_Ioc_of_le hx]
  exact f'int.mono (fun y hy => hy.2) le_rfl

theorem atBot_le_cocompact : atBot â‰¤ cocompact â„ := by simp
theorem atTop_le_cocompact : atTop â‰¤ cocompact â„ := by simp

theorem _root_.Filter.EventuallyEq.tendsto [TopologicalSpace Î²] {f : Î± â†’ Î²} {l : Filter Î±} {a : Î²}
    (hf : f =á¶ [l] fun _ â†¦ a) : Tendsto f l (ğ“ a) :=
  tendsto_nhds_of_eventually_eq hf

-- very special case of `integral_Iio_of_hasDerivAt_of_tendsto`.
theorem _root_.HasCompactSupport.integral_deriv_eq {f : â„ â†’ E} (hf : ContDiff â„ 1 f)
    (h2f : HasCompactSupport f) (b : â„) : âˆ« x in Iic b, deriv f x = f b := by
  have := fun x (_ : x âˆˆ Iio b) â†¦ hf.differentiable le_rfl x |>.hasDerivAt
  rw [integral_Iio_of_hasDerivAt_of_tendsto hf.continuous.continuousOn this, sub_zero]
  refine hf.continuous_deriv le_rfl |>.integrable_of_hasCompactSupport h2f.deriv |>.integrableOn
  rw [hasCompactSupport_iff_eventuallyEq, Filter.coclosedCompact_eq_cocompact] at h2f
  exact h2f.filter_mono atBot_le_cocompact |>.tendsto

end RealCalculus


open Set Function MeasurableSpace Finset

namespace MeasureTheory

/-- A different formulation of HÃ¶lder's inequality for two functions -/
theorem _root_.ENNReal.lintegral_mul_norm_pow_le {Î±} [MeasurableSpace Î±] {Î¼ : Measure Î±}
    {f g : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼)
    {p q : â„} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) (hpq : p + q = 1) :
    âˆ«â» a, f a ^ p * g a ^ q âˆ‚Î¼ â‰¤ (âˆ«â» a, f a âˆ‚Î¼) ^ p * (âˆ«â» a, g a âˆ‚Î¼) ^ q := by
  rcases hp.eq_or_lt with rfl|hp
  Â· simp at hpq
    subst hpq
    simp
  rcases hq.eq_or_lt with rfl|hq
  Â· simp at hpq
    subst hpq
    simp
  have h2p : 1 < 1 / p
  Â· rw [one_div]
    apply one_lt_inv hp
    linarith
  have h2pq : 1 / (1 / p) + 1 / (1 / q) = 1
  Â· simp [hp.ne', hq.ne', hpq]
  have := ENNReal.lintegral_mul_le_Lp_mul_Lq Î¼ âŸ¨h2p, h2pqâŸ© (hf.pow_const p) (hg.pow_const q)
  simpa [â† ENNReal.rpow_mul, hp.ne', hq.ne'] using this


@[to_additive]
theorem prod_insert_div [CommGroup Î²] [DecidableEq Î±] (ha : a âˆ‰ s) {f : Î± â†’ Î²} :
    (âˆ x in insert a s, f x) / f a = âˆ x in s, f x := by simp [ha]

attribute [gcongr] ENNReal.rpow_le_rpow
set_option maxHeartbeats 300000 in
/-- A version of HÃ¶lder with multiple arguments -/
theorem _root_.ENNReal.lintegral_prod_norm_pow_le {Î±} [MeasurableSpace Î±] {Î¼ : Measure Î±} (s : Finset Î¹)
    (hs : s.Nonempty)
    {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ i âˆˆ s, AEMeasurable (f i) Î¼) {p : Î¹ â†’ â„} (hp : âˆ‘ i in s, p i = 1)
    (h2p : âˆ€ i âˆˆ s, 0 â‰¤ p i) :
      âˆ«â» a, âˆ i in s, f i a ^ p i âˆ‚Î¼ â‰¤
      âˆ i in s, (âˆ«â» a, f i a âˆ‚Î¼) ^ p i := by
  induction s using Finset.induction generalizing p
  case empty =>
    simp at hs
  case insert iâ‚€ s hiâ‚€ ih =>
    rcases eq_or_ne (p iâ‚€) 1 with h2iâ‚€|h2iâ‚€
    Â· simp [hiâ‚€]
      have h2p : âˆ€ i âˆˆ s, p i = 0
      Â· simpa [hiâ‚€, h2iâ‚€, sum_eq_zero_iff_of_nonneg (fun i hi â†¦ h2p i <| mem_insert_of_mem hi)]
          using hp
      calc âˆ«â» a, f iâ‚€ a ^ p iâ‚€ * âˆ i in s, f i a ^ p i âˆ‚Î¼
          = âˆ«â» a, f iâ‚€ a ^ p iâ‚€ * âˆ i in s, 1 âˆ‚Î¼ := by
            congr! 3 with x
            apply prod_congr rfl fun i hi â†¦ by rw [h2p i hi, ENNReal.rpow_zero]
        _ â‰¤ (âˆ«â» a, f iâ‚€ a âˆ‚Î¼) ^ p iâ‚€ * âˆ i in s, 1 := by simp [h2iâ‚€]
        _ = (âˆ«â» a, f iâ‚€ a âˆ‚Î¼) ^ p iâ‚€ * âˆ i in s, (âˆ«â» a, f i a âˆ‚Î¼) ^ p i := by
            congr 1
            apply prod_congr rfl fun i hi â†¦ by rw [h2p i hi, ENNReal.rpow_zero]
    Â· have hs : s.Nonempty
      Â· rw [Finset.nonempty_iff_ne_empty]
        rintro rfl
        simp [h2iâ‚€] at hp
      have hpiâ‚€ : 0 â‰¤ 1 - p iâ‚€
      Â· simp_rw [sub_nonneg, â† hp, single_le_sum h2p (mem_insert_self ..)]
      have h2piâ‚€ : 1 - p iâ‚€ â‰  0
      Â· rwa [sub_ne_zero, ne_comm]
      let q := fun i â†¦ p i / (1 - p iâ‚€)
      have hq : âˆ‘ i in s, q i = 1
      Â· rw [â† sum_div, â† sum_insert_sub hiâ‚€, hp, div_self h2piâ‚€]
      have h2q : âˆ€ i âˆˆ s, 0 â‰¤ q i
      Â· exact fun i hi â†¦ div_nonneg (h2p i <| mem_insert_of_mem hi) hpiâ‚€
      calc âˆ«â» a, âˆ i in insert iâ‚€ s, f i a ^ p i âˆ‚Î¼
          = âˆ«â» a, f iâ‚€ a ^ p iâ‚€ * âˆ i in s, f i a ^ p i âˆ‚Î¼ := by simp [hiâ‚€]
        _ = âˆ«â» a, f iâ‚€ a ^ p iâ‚€ * (âˆ i in s, f i a ^ q i) ^ (1 - p iâ‚€) âˆ‚Î¼ := by
            simp [â† ENNReal.prod_rpow_of_nonneg hpiâ‚€, â† ENNReal.rpow_mul,
              div_mul_cancel (h := h2piâ‚€)]
        _ â‰¤ (âˆ«â» a, f iâ‚€ a âˆ‚Î¼) ^ p iâ‚€ * (âˆ«â» a, âˆ i in s, f i a ^ q i âˆ‚Î¼) ^ (1 - p iâ‚€) := by
            apply ENNReal.lintegral_mul_norm_pow_le
            Â· exact hf iâ‚€ <| mem_insert_self ..
            Â· exact s.aemeasurable_prod <| fun i hi â†¦ (hf i <| mem_insert_of_mem hi).pow_const _
            Â· exact h2p iâ‚€ <| mem_insert_self ..
            Â· exact hpiâ‚€
            Â· apply add_sub_cancel'_right
        _ â‰¤ (âˆ«â» a, f iâ‚€ a âˆ‚Î¼) ^ p iâ‚€ * (âˆ i in s, (âˆ«â» a, f i a âˆ‚Î¼) ^ q i) ^ (1 - p iâ‚€) := by
            gcongr -- the behavior of gcongr is heartbeat-dependent, which makes code really fragile...
            exact ih hs (fun i hi â†¦ hf i <| mem_insert_of_mem hi) hq h2q
        _ = (âˆ«â» a, f iâ‚€ a âˆ‚Î¼) ^ p iâ‚€ * âˆ i in s, (âˆ«â» a, f i a âˆ‚Î¼) ^ p i := by
            simp [â† ENNReal.prod_rpow_of_nonneg hpiâ‚€, â† ENNReal.rpow_mul,
              div_mul_cancel (h := h2piâ‚€)]
        _ = âˆ i in insert iâ‚€ s, (âˆ«â» a, f i a âˆ‚Î¼) ^ p i := by simp [hiâ‚€]

/-- A version of HÃ¶lder with multiple arguments, one of which plays a distinguished role -/
theorem _root_.ENNReal.lintegral_mul_prod_norm_pow_le {Î±} [MeasurableSpace Î±] {Î¼ : Measure Î±} (s : Finset Î¹)
    {g : Î± â†’  â„â‰¥0âˆ} {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} (hg : AEMeasurable g Î¼) (hf : âˆ€ i âˆˆ s, AEMeasurable (f i) Î¼)
    (q : â„) {p : Î¹ â†’ â„} (hpq : q + âˆ‘ i in s, p i = 1) (hq :  0 â‰¤ q)
    (hp : âˆ€ i âˆˆ s, 0 â‰¤ p i) :
    âˆ«â» a, g a ^ q * âˆ i in s, f i a ^ p i âˆ‚Î¼ â‰¤
      (âˆ«â» a, g a âˆ‚Î¼) ^ q * âˆ i in s, (âˆ«â» a, f i a âˆ‚Î¼) ^ p i := by
  calc
    âˆ«â» t, g t ^ q * âˆ j in s, (f j t) ^ p j âˆ‚Î¼
      = âˆ«â» t, âˆ j in insertNone s,
            Option.elim j (g t) (fun j â†¦ f j t) ^ Option.elim j q p âˆ‚Î¼ := by
          congr! 1
          ext t
          rw [prod_insertNone]
          dsimp
    _ â‰¤ âˆ j in insertNone s,
          (âˆ«â» t, Option.elim j (g t) (fun j â†¦ f j t) âˆ‚Î¼) ^ Option.elim j q p := by
          refine ENNReal.lintegral_prod_norm_pow_le _ insertNone_nonempty ?_ ?_ ?_
          Â· rintro (_|i) hi
            Â· exact hg
            Â· refine hf i ?_
              simpa using hi
          Â· simp_rw [sum_insertNone, Option.elim]
            exact hpq
          Â· rintro (_|i) hi
            Â· exact hq
            Â· refine hp i ?_
              simpa using hi
    _ = (âˆ«â» t, g t âˆ‚Î¼) ^ q * âˆ j in s, (âˆ«â» t, f j t âˆ‚Î¼) ^ p j := by
          -- this proof could be `simp [prod_insertNone]` but that's too slow
          simp_rw [prod_insertNone]
          dsimp

end MeasureTheory

open MeasureTheory

section Sobolev

open TopologicalSpace

variable [Fintype Î¹] {Ï€ : Î¹ â†’ Type _} [âˆ€ i, MeasurableSpace (Ï€ i)] (Î¼ : âˆ€ i, Measure (Ï€ i))
  [âˆ€ i, SigmaFinite (Î¼ i)] (u : (Î¹ â†’ â„) â†’ â„) {f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ}


local prefix:max "#" => Fintype.card

/--
  The function that is central in the inductive proof of the Sobolev inequality.
-/
def rhsAux (p : â„) (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ) (s : Finset Î¹) (x : âˆ€ i, Ï€ i) : â„â‰¥0âˆ :=
  (âˆ«â‹¯âˆ«_s, f âˆ‚Î¼) x ^ (1 - (sá¶œ.card - 1 : â„) * p) *
    âˆ i in sá¶œ, (âˆ«â‹¯âˆ«_insert i s, f âˆ‚Î¼) x ^ p

lemma rhsAux_empty' (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ) (x : âˆ€ i, Ï€ i) :
    rhsAux Î¼ p f âˆ… x = f x ^ (1 - (#Î¹ - 1 : â„) * p) * âˆ i, (âˆ«â» xáµ¢, f (Function.update x i xáµ¢) âˆ‚Î¼ i) ^ p := by
  simp [rhsAux, marginal_singleton, card_univ]

lemma rhsAux_empty [Nontrivial Î¹] (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ) (x : âˆ€ i, Ï€ i) :
    rhsAux Î¼ ((1 : â„) / (#Î¹ - 1 : â„)) f âˆ… x
    = âˆ i, (âˆ«â» xáµ¢, f (Function.update x i xáµ¢) âˆ‚Î¼ i) ^ ((1 : â„) / (#Î¹ - 1 : â„)) := by
  rw [rhsAux_empty']
  convert one_mul _
  convert ENNReal.rpow_zero
  have : (1:â„) < #Î¹ := by norm_cast; exact Fintype.one_lt_card
  have : (0:â„) < #Î¹ - 1 := by linarith
  field_simp

lemma rhsAux_univ' (p : â„) (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ) (x : âˆ€ i, Ï€ i) :
   rhsAux Î¼ p f univ x = (âˆ«â» x, f x âˆ‚(Measure.pi Î¼)) ^ (1 + p) := by
  simp [rhsAux, marginal_univ, Finset.card_univ]

lemma rhsAux_univ [Nontrivial Î¹] (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ) (x : âˆ€ i, Ï€ i) :
   rhsAux Î¼ ((1 : â„) / (#Î¹ - 1 : â„)) f univ x
   = (âˆ«â» x, f x âˆ‚(Measure.pi Î¼)) ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) := by
  rw [rhsAux_univ']
  congr
  have : (1:â„) < #Î¹ := by norm_cast; exact Fintype.one_lt_card
  have : (0:â„) < #Î¹ - 1 := by linarith
  field_simp

lemma Measurable.rhsAux (p : â„) (hf : Measurable f) : Measurable (rhsAux Î¼ p f s) := by
  refine ((hf.marginal Î¼).pow_const _).mul ?_
  exact Finset.measurable_prod _ fun i _ â†¦ ((hf.marginal Î¼).pow_const _)

/-- The main inductive step -/
theorem marginal_singleton_rhsAux_le {p : â„} (hp1 : 0 â‰¤ p) (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ)
    (hf : Measurable f) (s : Finset Î¹) (hp2 : (sá¶œ.card - 1 : â„) * p â‰¤ 1) (i : Î¹) (hi : i âˆ‰ s) :
    âˆ«â‹¯âˆ«_sá¶œ, rhsAux Î¼ p f s âˆ‚Î¼ â‰¤ âˆ«â‹¯âˆ«_(insert i s)á¶œ, rhsAux Î¼ p f (insert i s) âˆ‚Î¼ := by
  have hi' : i âˆ‰ (insert i s)á¶œ := not_mem_compl.mpr <| mem_insert_self i s
  calc âˆ«â‹¯âˆ«_sá¶œ, rhsAux Î¼ p f s âˆ‚Î¼
      = âˆ«â‹¯âˆ«_insert i (insert i s)á¶œ, rhsAux Î¼ p f s âˆ‚Î¼ := by simp_rw [â† insert_compl_insert hi]
    _ = âˆ«â‹¯âˆ«_(insert i s)á¶œ, (fun x â†¦ âˆ«â» xáµ¢, rhsAux Î¼ p f s (Function.update x i xáµ¢) âˆ‚Î¼ i) âˆ‚Î¼ :=
        marginal_insert' _ (hf.rhsAux Î¼ p) hi'
    _ â‰¤ âˆ«â‹¯âˆ«_(insert i s)á¶œ, rhsAux Î¼ p f (insert i s) âˆ‚Î¼ := marginal_mono (fun x â†¦ ?_)
  -- it suffices to compare the `i`-integral of `rhsAux s` with `rhsAux (insert i s)`
  have hf' : âˆ€ {s' : Finset Î¹}, Measurable fun t â†¦ (âˆ«â‹¯âˆ«_s', f âˆ‚Î¼) (update x i t) :=
    fun {_} â†¦ hf.marginal Î¼ |>.comp <| measurable_update _
  have hkâ‚€ : sá¶œ.card = (insert i s)á¶œ.card + 1
  Â· have Hâ‚ : ((insert i s).card) = s.card + 1 := Finset.card_insert_of_not_mem hi
    have Hâ‚‚ : ((insert i s).card) + (insert i s)á¶œ.card = #Î¹ := (insert i s).card_add_card_compl
    have Hâ‚ƒ : (s.card) + sá¶œ.card = #Î¹ := s.card_add_card_compl
    zify at Hâ‚ Hâ‚‚ Hâ‚ƒ âŠ¢
    linear_combination Hâ‚ - Hâ‚‚ + Hâ‚ƒ
  let k : â„ := (insert i s)á¶œ.card
  have hk : sá¶œ.card = k + 1 := by exact_mod_cast hkâ‚€
  have hk' : 0 â‰¤ 1 - k * p
  Â· rw [hk] at hp2
    linarith only [hp2]
  let X := update x i
  let F : Finset Î¹ â†’ (âˆ€ j, Ï€ j) â†’ â„â‰¥0âˆ := fun s' â†¦ âˆ«â‹¯âˆ«_s', f âˆ‚Î¼
  calc âˆ«â» t, F s (X t) ^ (1 - (sá¶œ.card - 1 : â„) * p)
          * âˆ i in sá¶œ, F (insert i s) (X t) ^ p âˆ‚ (Î¼ i)
      = âˆ«â» t, F (insert i s) (X t) ^ p * (F s (X t) ^ (1 - k * p)
          * âˆ j in (insert i s)á¶œ, (F (insert j s) (X t) ^ p)) âˆ‚(Î¼ i) := by
              -- rewrite integrand so that `(âˆ«â‹¯âˆ«_insert i s, f âˆ‚Î¼) ^ p` comes first
              clear_value F X
              rw [hk]
              congr! 2 with t
              simp_rw [â† insert_compl_insert hi, prod_insert hi']
              ring_nf
    _ = F (insert i s) x ^ p *
          âˆ«â» t, F s (X t) ^ (1 - k * p) * âˆ j in (insert i s)á¶œ, (F (insert j s) (X t)) ^ p âˆ‚(Î¼ i) := by
              -- pull out this constant factor
              simp_rw [marginal_update_of_mem Î¼ (s.mem_insert_self i)]
              rw [lintegral_const_mul]
              exact (hf'.pow_const _).mul <| Finset.measurable_prod _ fun _ _ â†¦ hf'.pow_const _
    _ â‰¤ F (insert i s) x ^ p *
          ((âˆ«â» t, F s (X t) âˆ‚Î¼ i) ^ (1 - k * p) *
            âˆ j in (insert i s)á¶œ, (âˆ«â» t, F (insert j s) (X t) âˆ‚(Î¼ i)) ^ p) := by
              -- apply HÃ¶lder's inequality
              gcongr
              apply ENNReal.lintegral_mul_prod_norm_pow_le
              Â· exact hf'.aemeasurable
              Â· intros
                exact hf'.aemeasurable
              Â· simp only [sum_const, nsmul_eq_mul]
                ring
              Â· exact hk'
              Â· exact fun _ _ â†¦ hp1
    _ = F (insert i s) x ^ p *
          (F (insert i s) x ^ (1 - k * p) *
            âˆ j in (insert i s)á¶œ, F (insert i (insert j s)) x ^ p) := by
              -- absorb the newly-created integrals into `âˆ«â‹¯âˆ«`
              dsimp only
              rw [marginal_insert _ hf hi]
              congr! 2; refine prod_congr rfl fun j hj => ?_
              have hi' : i âˆ‰ insert j s
              Â· simp only [Finset.mem_insert, Finset.mem_compl] at hj âŠ¢
                tauto
              rw [marginal_insert _ hf hi']
    _ = F (insert i s) x ^ (p + (1 - k * p)) *
            âˆ j in (insert i s)á¶œ, F (insert i (insert j s)) x ^ p := by
              -- combine two `(âˆ«â‹¯âˆ«_insert i s, f âˆ‚Î¼) x` terms
              rw [ENNReal.rpow_add_of_nonneg]
              Â· ring
              Â· exact hp1
              Â· exact hk'
    _ = F (insert i s) x ^ (1 - ((insert i s)á¶œ.card - 1 : â„) * p)
          * âˆ j in (insert i s)á¶œ, F (insert j (insert i s)) x ^ p := by
              -- identify the result with the RHS integrand
              clear_value F
              simp_rw [Insert.comm]
              push_cast
              ring_nf

theorem marginal_rhsAux_monotone {p : â„} (hp1 : 0 â‰¤ p) (hp2 : (#Î¹ - 1 : â„) * p â‰¤ 1)
    (f : (âˆ€ i, Ï€ i) â†’ â„â‰¥0âˆ) (hf : Measurable f) :
    Monotone (fun s â†¦ âˆ«â‹¯âˆ«_sá¶œ, rhsAux Î¼ p f s âˆ‚Î¼) := by
  rw [Finset.monotone_iff']
  intro s i hi
  refine marginal_singleton_rhsAux_le Î¼ hp1 f hf s (le_trans ?_ hp2) i hi
  gcongr
  exact card_le_univ sá¶œ

theorem lintegral_prod_lintegral_pow_le [Nontrivial Î¹] (hf : Measurable f) :
    âˆ«â» x, âˆ i, (âˆ«â» xáµ¢, f (Function.update x i xáµ¢) âˆ‚Î¼ i) ^ ((1 : â„) / (#Î¹ - 1 : â„)) âˆ‚Measure.pi Î¼ â‰¤
      (âˆ«â» x, f x âˆ‚Measure.pi Î¼) ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) := by
  cases isEmpty_or_nonempty (âˆ€ i, Ï€ i)
  Â· simp_rw [lintegral_of_isEmpty]; refine' zero_le _
  inhabit âˆ€ i, Ï€ i
  have H : (âˆ… : Finset Î¹) â‰¤ Finset.univ := Finset.empty_subset _
  have : (1:â„) < #Î¹ := by norm_cast; exact Fintype.one_lt_card
  have : (0:â„) < #Î¹ - 1 := by linarith
  have hp1 : 0 â‰¤ ((1 : â„) / (#Î¹ - 1 : â„)) := by positivity
  have hp2 : (#Î¹ - 1 : â„) * ((1 : â„) / (#Î¹ - 1 : â„)) â‰¤ 1 := by field_simp
  simpa [marginal_univ, rhsAux_empty, rhsAux_univ, -one_div] using
    marginal_rhsAux_monotone Î¼ hp1 hp2 f hf H default
-- theorem integral_prod_integral_pow_le {f : (âˆ€ i, Ï€ i) â†’ â„} (hf : Measurable f)
--     (h2f : âˆ€ x, 0 â‰¤ f x) :
--     âˆ« x,
--         âˆ i,
--           (âˆ« xáµ¢, f (Function.update x i xáµ¢) âˆ‚Î¼ i) ^ ((1 : â„) / (#Î¹ - 1)) âˆ‚Measure.pi Î¼ â‰¤
--       (âˆ« x, f x âˆ‚Measure.pi Î¼) ^ ((#Î¹ : â„) / (#Î¹ - 1)) :=
--   by sorry
section

-- move to MeasureTheory.Function.L1Space
theorem _root_.MeasureTheory.Integrable.nnnorm_toL1 {Î± : Type _} {Î² : Type _}
    {m : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedAddCommGroup Î²] (f : Î± â†’ Î²)
    (hf : Integrable f Î¼) :
    (â€–hf.toL1 fâ€–â‚Š : â„â‰¥0âˆ) = âˆ«â» a, â€–f aâ€–â‚Š âˆ‚Î¼ := by
  simpa [Integrable.toL1, snorm, snorm'] using ENNReal.coe_toNNReal hf.2.ne

-- move to MeasureTheory.Integral.Bochner
theorem _root_.MeasureTheory.L1.nnnorm_Integral_le_one {Î± : Type _} {E : Type _}
    [NormedAddCommGroup E] {_ : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedSpace â„ E]
    [CompleteSpace E] : â€–L1.integralCLM (Î± := Î±) (E := E) (Î¼ := Î¼)â€–â‚Š â‰¤ (1 : â„) :=
  L1.norm_Integral_le_one

-- move to MeasureTheory.Integral.Bochner
theorem _root_.MeasureTheory.L1.nnnorm_integral_le {Î± : Type _} {E : Type _}
    [NormedAddCommGroup E] {_ : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedSpace â„ E]
    [CompleteSpace E] (f : Î± â†’â‚[Î¼] E) : â€–L1.integral fâ€–â‚Š â‰¤ â€–fâ€–â‚Š :=
  L1.norm_integral_le f

end

-- move to MeasureTheory.Integral.Bochner
theorem nnnorm_integral_le_lintegral_nnnorm {Î± E : Type _} [MeasurableSpace Î±] {Î¼ : Measure Î±}
    [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] (f : Î± â†’ E) :
    â€–âˆ« x, f x âˆ‚Î¼â€–â‚Š â‰¤ âˆ«â» x, â€–f xâ€–â‚Š âˆ‚ Î¼ := by
  rw [integral_def, dif_pos â€¹_â€º]
  split_ifs with hf
  Â· calc _ â‰¤ (â€–(Integrable.toL1 f hf)â€–â‚Š : â„â‰¥0âˆ) := by norm_cast; apply L1.nnnorm_integral_le
      _ = _ := hf.nnnorm_toL1
  Â· simp

/-- The Gagliardo-Nirenberg-Sobolev inequality -/
theorem lintegral_pow_le [Nontrivial Î¹] [Fintype Î¹] (hu : ContDiff â„ 1 u)
    (h2u : HasCompactSupport u) : âˆ«â» x, â€–u xâ€–â‚Š ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) â‰¤
      (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š) ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) := by
  have : (1:â„) â‰¤ â†‘#Î¹ - 1
  Â· have hÎ¹ : (2:â„) â‰¤ #Î¹ := by exact_mod_cast Fintype.one_lt_card
    linarith
  calc âˆ«â» x, â€–u xâ€–â‚Š ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„))
      = âˆ«â» x, ((â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / (#Î¹ - 1 : â„))) ^ (#Î¹ : â„) := by
        congr! 2 with x
        rw [â† ENNReal.coe_rpow_of_nonneg _ (by positivity), â† ENNReal.rpow_mul]
        field_simp
    _ = âˆ«â» x, âˆ _i : Î¹, (â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / (#Î¹ - 1 : â„)) := by
        congr! 2 with x
        simp_rw [prod_const, card_univ]
        norm_cast
    _ â‰¤ âˆ«â» x, âˆ i, (âˆ«â» xáµ¢, â€–fderiv â„ u (Function.update x i xáµ¢)â€–â‚Š) ^ ((1 : â„) / (#Î¹ - 1 : â„)) := ?_
    _ â‰¤ (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š) ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) := by
        apply lintegral_prod_lintegral_pow_le
        borelize ((Î¹ â†’ â„) â†’L[â„] â„)
        have : Measurable (fun x â†¦ fderiv â„ u x) := (hu.continuous_fderiv (le_refl _)).measurable
        measurability
  gcongr with x i
  calc (â€–u xâ€–â‚Š : â„â‰¥0âˆ)
      = (â€–âˆ« xáµ¢ : â„ in Set.Iic (x i), deriv (u âˆ˜ update x i) xáµ¢â€–â‚Š : â„â‰¥0âˆ) := by
        have h3u : ContDiff â„ 1 (u âˆ˜ update x i) := hu.comp (contDiff_update 1 x i)
        have h4u : HasCompactSupport (u âˆ˜ update x i)
        Â· apply h2u.comp_closedEmbedding
          -- `update x i` is a closed embedding -- make this a lemma
          have h5u : LeftInverse (fun v â†¦ v i) (update x i) := fun t â†¦ update_same i t x
          apply h5u.closedEmbedding
          Â· exact continuous_apply i
          Â· have : Continuous (fun t : â„ â†¦ (x, t)) := continuous_const.prod_mk continuous_id
            exact (continuous_update i).comp this
        rw [h4u.integral_deriv_eq h3u (x i)]
        simp
    _ â‰¤ âˆ«â» xáµ¢ : â„ in Set.Iic (x i), â€–deriv (u âˆ˜ update x i) xáµ¢â€–â‚Š :=
        nnnorm_integral_le_lintegral_nnnorm _
    _ â‰¤ âˆ«â» (xáµ¢ : â„), â†‘â€–fderiv â„ u (update x i xáµ¢)â€–â‚Š := ?_
  gcongr with y; swap; exact Measure.restrict_le_self
  calc â€–deriv (u âˆ˜ update x i) yâ€–â‚Š = â€–fderiv â„ u (update x i y) (deriv (update x i) y)â€–â‚Š := by
        rw [fderiv.comp_deriv _ (hu.differentiable le_rfl).differentiableAt
          (hasDerivAt_update y).differentiableAt]
    _ â‰¤ â€–fderiv â„ u (update x i y)â€–â‚Š * â€–deriv (update x i) yâ€–â‚Š :=
        ContinuousLinearMap.le_op_nnnorm ..
    _ â‰¤ â€–fderiv â„ u (update x i y)â€–â‚Š := by simp [deriv_update, Pi.nnnorm_single]

-- /-- The Sobolev inequality for the Lebesgue l=integral(?) -/
-- theorem lintegral_pow_le :
--     âˆ«â» x, â€–u xâ€–â‚Š ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) â‰¤
--       (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š) ^ ((#Î¹ : â„) / (#Î¹ - 1 : â„)) :=
--   by sorry

end Sobolev
