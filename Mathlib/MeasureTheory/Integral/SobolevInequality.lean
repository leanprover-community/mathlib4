/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import Mathlib.Analysis.Calculus.Deriv.Pi
import Mathlib.Analysis.Calculus.FDeriv.Measurable
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Data.Finset.Interval
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.MeasureTheory.Integral.MeanInequalities
import Mathlib.MeasureTheory.Measure.Haar.Unique
import Mathlib.Tactic.FunProp.AEMeasurable
import Mathlib.Tactic.FunProp.Measurable

/-!
# Gagliardo-Nirenberg-Sobolev inequality
-/


open scoped Classical BigOperators ENNReal NNReal Topology
open Set Function Finset MeasureTheory Measure Filter

noncomputable section

section fun_prop

attribute [fun_prop] ENNReal.continuous_coe ENNReal.continuous_rpow_const
  Measurable.coe_nnreal_ennreal Measurable.nnnorm
  measurable_fderiv
end fun_prop

section RPow

theorem NNReal.rpow_add_of_nonneg (x : â„â‰¥0) {y z : â„} (hy : 0 â‰¤ y) (hz : 0 â‰¤ z) :
    x ^ (y + z) = x ^ y * x ^ z := by
  by_cases h : y + z = 0
  Â· obtain rfl : y = 0 := by linarith
    obtain rfl : z = 0 := by linarith
    simp [h]
  Â· exact rpow_add' _ h

theorem Real.nnnorm_rpow_of_nonneg {x y : â„} (hx : 0 â‰¤ x) : â€–x ^ yâ€–â‚Š = â€–xâ€–â‚Š ^ y := by
  ext; exact Real.norm_rpow_of_nonneg hx

theorem ENNReal.rpow_add_of_nonneg {x : â„â‰¥0âˆ} (y z : â„) (hy : 0 â‰¤ y) (hz : 0 â‰¤ z) :
    x ^ (y + z) = x ^ y * x ^ z := by
  induction x using recTopCoe
  Â· rcases hy.eq_or_lt with rfl|hy
    Â· rw [rpow_zero, one_mul, zero_add]
    rcases hz.eq_or_lt with rfl|hz
    Â· rw [rpow_zero, mul_one, add_zero]
    simp [top_rpow_of_pos, hy, hz, add_pos hy hz]
  simp [coe_rpow_of_nonneg, hy, hz, add_nonneg hy hz, NNReal.rpow_add_of_nonneg _ hy hz]

@[fun_prop]
theorem Real.continuous_rpow_const {q : â„} (h : 0 < q) :
    Continuous (fun x : â„ => x ^ q) :=
  continuous_iff_continuousAt.mpr fun x â†¦ continuousAt_rpow_const x q (.inr h)

end RPow

namespace MeasureTheory

/-- For a function `f` with support in `s`, the Láµ– norms of `f` with respect to `Î¼` and
`Î¼.restrict s` are the same. -/
theorem snorm_restrict_eq {Î± : Type*} {F : Type*} {m0 : MeasurableSpace Î±} [NormedAddCommGroup F]
    (f : Î± â†’ F) (p : ENNReal) (Î¼ : MeasureTheory.Measure Î±) {s : Set Î±} (hs : MeasurableSet s)
    (hsf : f.support âŠ† s) :
    snorm f p (Î¼.restrict s) = snorm f p Î¼ := by
  dsimp only [snorm]
  split_ifs with hp hp'
  Â· rfl
  Â· dsimp only [snormEssSup]
    rw [â† Function.support_comp_eq (fun x : F â†¦ (â€–xâ€–â‚Š:â„â‰¥0âˆ)) (by simp)] at hsf
    rw [â† Set.indicator_eq_self] at hsf
    rw [â† ENNReal.essSup_indicator_eq_essSup_restrict hs]
    congr
  Â· dsimp only [snorm']
    rw [â† Function.support_comp_eq (fun x : F â†¦ (â€–xâ€–â‚Š:â„â‰¥0âˆ) ^ p.toReal)] at hsf
    Â· rw [â† Set.indicator_eq_self] at hsf
      rw [â† lintegral_indicator _ hs]
      congr
    simp [ENNReal.toReal_pos hp hp']

end MeasureTheory

namespace Filter

theorem eventually_of_isEmpty {Î± : Type*} {p : Î± â†’ Prop} [IsEmpty Î±] {l : Filter Î±} :
    âˆ€á¶  (x : Î±) in l, p x :=
  eventually_of_forall <| fun x â†¦ isEmptyElim x

end Filter

section ContDiff

variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : E â†’ F}

theorem contDiff_one_iff_hasFDerivAt : ContDiff ğ•œ 1 f â†”
    âˆƒ f' : E â†’ E â†’L[ğ•œ] F, Continuous f' âˆ§ âˆ€ x, HasFDerivAt f (f' x) x := by
  convert contDiff_succ_iff_hasFDerivAt using 4; simp

end ContDiff

section ClosedEmbedding
variable {Î¹ : Type*} {Î² : Î¹ â†’ Type*} [DecidableEq Î¹]
  [(i : Î¹) â†’ TopologicalSpace (Î² i)]
  (x : (i : Î¹) â†’ Î² i) (i : Î¹) {s : Set (Î² i)}

theorem forall_and_left {Î¹ : Sort*} [Nonempty Î¹] {q : Prop} {p : Î¹ â†’ Prop} :
    (âˆ€ x, q âˆ§ p x) â†” (q âˆ§ âˆ€ x, p x) := by rw [forall_and, forall_const]

theorem forall_and_right {Î¹ : Sort*} [Nonempty Î¹] {p : Î¹ â†’ Prop} {q : Prop} :
    (âˆ€ x, p x âˆ§ q) â†” (âˆ€ x, p x) âˆ§ q := by rw [forall_and, forall_const]

theorem image_update : update x i '' s = Set.univ.pi (update (fun j â†¦ {x j}) i s) := by
  ext y
  simp [update_eq_iff, and_left_comm (a := _ âˆˆ s), forall_update_iff, eq_comm (a := y _)]

theorem closedEmbedding_update [(i : Î¹) â†’ T1Space (Î² i)] : ClosedEmbedding (update x i) := by
  apply closedEmbedding_of_continuous_injective_closed
  Â· exact continuous_const.update i continuous_id
  Â· exact update_injective x i
  Â· intro s hs
    rw [image_update]
    apply isClosed_set_pi
    simp [forall_update_iff, hs, isClosed_singleton]

end ClosedEmbedding

section ContDiffAbsPow

open Asymptotics Real
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

theorem hasFDerivAt_norm_rpow (x : E) {p : â„} (hp : 1 < p) :
    HasFDerivAt (fun x : E â†¦ â€–xâ€– ^ p) ((p * â€–xâ€– ^ (p - 2)) â€¢ innerSL â„ x) x := by
  by_cases hx : x = 0
  Â· simp [hx]
    have h2p : 0 < p - 1 := sub_pos.mpr hp
    rw [HasFDerivAt, hasFDerivAtFilter_iff_isLittleO]
    simp [zero_lt_one.trans hp |>.ne']
    calc (fun x : E â†¦ â€–xâ€– ^ p) =
      (fun x : E â†¦ â€–xâ€– * â€–xâ€– ^ (p - 1)) := by
          ext x
          rw [â† rpow_one_add' (norm_nonneg x) (by positivity)]
          ring_nf
      _ =o[ğ“ 0] (fun x : E â†¦ â€–xâ€– * 1) := by
        refine (isBigO_refl _ _).mul_isLittleO <| (isLittleO_const_iff <| by norm_num).mpr ?_
        convert continuousAt_id.norm.rpow_const (.inr h2p.le) |>.tendsto
        simp [h2p.ne']
      _ =O[ğ“ 0] id := by
        simp_rw [mul_one, isBigO_norm_left (f' := fun x â†¦ x), id_def, isBigO_refl]
  Â· apply HasStrictFDerivAt.hasFDerivAt
    convert (hasStrictFDerivAt_norm_sq x).rpow_const (p := p / 2) (by simp [hx]) using 0
    simp_rw [â† Real.rpow_natCast_mul (norm_nonneg _), nsmul_eq_smul_cast â„, smul_smul]
    ring_nf -- doesn't close the goal?
    congr! 2
    ring

theorem hasDerivAt_norm_rpow (x : â„) {p : â„} (hp : 1 < p) :
    HasDerivAt (fun x : â„ â†¦ â€–xâ€– ^ p) (p * â€–xâ€– ^ (p - 2) * x) x := by
  convert hasFDerivAt_norm_rpow x hp |>.hasDerivAt using 1; simp

theorem hasDerivAt_abs_rpow (x : â„) {p : â„} (hp : 1 < p) :
    HasDerivAt (fun x : â„ â†¦ |x| ^ p) (p * |x| ^ (p - 2) * x) x := by
  simpa using hasDerivAt_norm_rpow x hp

theorem fderiv_norm_rpow (x : E) {p : â„} (hp : 1 < p) :
    fderiv â„ (fun x â†¦ â€–xâ€– ^ p) x = (p * â€–xâ€– ^ (p - 2)) â€¢ innerSL â„ x :=
  hasFDerivAt_norm_rpow x hp |>.fderiv

theorem Differentiable.fderiv_norm_rpow {f : F â†’ E} (hf : Differentiable â„ f)
    {x : F} {p : â„} (hp : 1 < p) :
    fderiv â„ (fun x â†¦ â€–f xâ€– ^ p) x =
    (p * â€–f xâ€– ^ (p - 2)) â€¢ (innerSL â„ (f x)).comp (fderiv â„ f x) :=
  hasFDerivAt_norm_rpow (f x) hp |>.comp x (hf x).hasFDerivAt |>.fderiv

theorem norm_fderiv_norm_rpow_le {f : F â†’ E} (hf : Differentiable â„ f) {x : F} {p : â„} (hp : 1 < p) :
    â€–fderiv â„ (fun x â†¦ â€–f xâ€– ^ p) xâ€– â‰¤ p * â€–f xâ€– ^ (p - 1) * â€–fderiv â„ f xâ€– := by
  rw [hf.fderiv_norm_rpow hp, norm_smul, norm_mul]
  simp [- Real.norm_eq_abs, Real.norm_rpow_of_nonneg]
  simp [abs_eq_self.mpr <| zero_le_one.trans hp.le, mul_assoc]
  gcongr _ * ?_
  refine mul_le_mul_of_nonneg_left (ContinuousLinearMap.opNorm_comp_le ..) (by positivity)
    |>.trans_eq ?_
  rw [innerSL_apply_norm, â† mul_assoc, â† Real.rpow_add_one' (by positivity) (by linarith)]
  ring_nf

theorem norm_fderiv_norm_id_rpow_le {x : E} {p : â„} (hp : 1 < p) :
    â€–fderiv â„ (fun x â†¦ â€–xâ€– ^ p) xâ€– â‰¤ p * â€–xâ€– ^ (p - 1) := by
  refine norm_fderiv_norm_rpow_le differentiable_id' hp |>.trans ?_
  rw [mul_assoc, fderiv_id']
  gcongr
  exact mul_le_mul_of_nonneg_left ContinuousLinearMap.norm_id_le (by positivity)
    |>.trans_eq (mul_one _)

theorem nnnorm_fderiv_norm_rpow_le {f : F â†’ E} (hf : Differentiable â„ f)
    {x : F} {p : â„â‰¥0} (hp : 1 < p) :
    â€–fderiv â„ (fun x â†¦ â€–f xâ€– ^ (p : â„)) xâ€–â‚Š â‰¤ p * â€–f xâ€–â‚Š ^ ((p : â„) - 1) * â€–fderiv â„ f xâ€–â‚Š :=
  norm_fderiv_norm_rpow_le hf hp

attribute [fun_prop] continuousAt_rpow_const Continuous.clm_comp

-- todo: generalize 1 to n
theorem contDiff_norm_rpow {p : â„} (hp : 1 < p) : ContDiff â„ 1 (fun x : E â†¦ â€–xâ€– ^ p) := by
  rw [contDiff_one_iff_fderiv]
  refine âŸ¨fun x â†¦ hasFDerivAt_norm_rpow x hp |>.differentiableAt, ?_âŸ©
  simp_rw [continuous_iff_continuousAt]
  intro x
  by_cases hx : x = 0
  Â· simp [hx, ContinuousAt, fderiv_norm_rpow (E := E) (x := 0) hp]
    rw [tendsto_zero_iff_norm_tendsto_zero]
    refine tendsto_of_tendsto_of_tendsto_of_le_of_le (tendsto_const_nhds) ?_
      (fun _ â†¦ norm_nonneg _) (fun _ â†¦ norm_fderiv_norm_id_rpow_le hp)
    suffices ContinuousAt (fun x : E â†¦ p * â€–xâ€– ^ (p - 1)) 0  by
      simpa [ContinuousAt, sub_ne_zero_of_ne hp.ne'] using this
    fun_prop (discharger := simp [*])
  Â· simp_rw [funext fun x â†¦ fderiv_norm_rpow (E:=E) (x:=x) hp]
    fun_prop (discharger := simp [*])

theorem ContDiff.norm_rpow {f : F â†’ E} (hf : ContDiff â„ 1 f) {p : â„} (hp : 1 < p) :
    ContDiff â„ 1 (fun x â†¦ â€–f xâ€– ^ p) :=
  contDiff_norm_rpow hp |>.comp hf

theorem Differentiable.norm_rpow {f : F â†’ E} (hf : Differentiable â„ f) {p : â„} (hp : 1 < p) :
    Differentiable â„ (fun x â†¦ â€–f xâ€– ^ p) :=
  contDiff_norm_rpow hp |>.differentiable le_rfl |>.comp hf

end ContDiffAbsPow

namespace HasCompactSupport
variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [AddGroup Î²] [Lattice Î²]
  [CovariantClass Î² Î² (Â· + Â·) (Â· â‰¤ Â·)]

protected theorem abs {f : Î± â†’ Î²} (hf : HasCompactSupport f) : HasCompactSupport |f| :=
  hf.comp_left (g := abs) abs_zero

protected theorem rpow_const {f : Î± â†’ â„} (hf : HasCompactSupport f) {r : â„} (hr : r â‰  0) :
    HasCompactSupport (fun x â†¦ f x ^ r) :=
  hf.comp_left (g := (Â· ^ r)) (Real.zero_rpow hr)
variable (ğ•œ : Type*) {E : Type*} {F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : E â†’ F}
protected theorem fderiv_apply (hf : HasCompactSupport f) (v : E) :
    HasCompactSupport (fderiv ğ•œ f Â· v) :=
  hf.fderiv ğ•œ |>.comp_left (g := fun L : E â†’L[ğ•œ] F â†¦ L v) rfl

end HasCompactSupport

section

variable {E : Type*} [NormedAddCommGroup E] {p : â„â‰¥0âˆ}

theorem Continuous.memâ„’p_of_hasCompactSupport
    {X : Type*} [TopologicalSpace X] [MeasurableSpace X] [OpensMeasurableSpace X]
    {f : X â†’ E} (hf : Continuous f) (h'f : HasCompactSupport f) (Î¼ : Measure X)
    [IsFiniteMeasureOnCompacts Î¼] : Memâ„’p f p Î¼ := by
  have := hf.memâ„’p_top_of_hasCompactSupport h'f Î¼
  exact this.memâ„’p_of_exponent_le_of_measure_support_ne_top
    (fun x â†¦ image_eq_zero_of_nmem_tsupport) (h'f.measure_lt_top.ne) le_top

end

namespace ENNReal

protected theorem inv_mul_le_iff {x y z : â„â‰¥0âˆ} (h1 : x â‰  0) (h2 : x â‰  âˆ) :
    xâ»Â¹ * y â‰¤ z â†” y â‰¤ x * z := by
  rw [â† mul_le_mul_left h1 h2, â† mul_assoc, ENNReal.mul_inv_cancel h1 h2, one_mul]

protected theorem mul_inv_le_iff {x y z : â„â‰¥0âˆ} (h1 : y â‰  0) (h2 : y â‰  âˆ) :
    x * yâ»Â¹ â‰¤ z â†” x â‰¤ z * y := by
  rw [mul_comm, ENNReal.inv_mul_le_iff h1 h2, mul_comm]

protected theorem div_le_iff {x y z : â„â‰¥0âˆ} (h1 : y â‰  0) (h2 : y â‰  âˆ) :
    x / y â‰¤ z â†” x â‰¤ z * y := by
  rw [div_eq_mul_inv, ENNReal.mul_inv_le_iff h1 h2]

protected theorem div_le_iff' {x y z : â„â‰¥0âˆ} (h1 : y â‰  0) (h2 : y â‰  âˆ) :
    x / y â‰¤ z â†” x â‰¤ y * z := by
  rw [mul_comm, ENNReal.div_le_iff h1 h2]

end ENNReal

namespace Real.IsConjExponent

variable {p q : â„} (h : IsConjExponent p q)
lemma conj_inv_eq : qâ»Â¹ = 1 - pâ»Â¹ := by
  rw [eq_sub_iff_add_eq, add_comm, h.inv_add_inv_conj]

end Real.IsConjExponent

namespace MeasureTheory

variable {Î± E : Type*} [NormedAddCommGroup E] {_ : MeasurableSpace Î±}
  {f : Î± â†’ E} {Î¼ : Measure Î±}

lemma snorm_nnreal_eq_snorm' {p : â„â‰¥0} (hp : p â‰  0) : snorm f p Î¼ = snorm' f p Î¼ :=
  snorm_eq_snorm' (by exact_mod_cast hp) ENNReal.coe_ne_top

lemma snorm_nnreal_eq_lintegral {p : â„â‰¥0} (hp : p â‰  0) :
    snorm f p Î¼ = (âˆ«â» x, â€–f xâ€–â‚Š ^ (p : â„) âˆ‚Î¼) ^ (1 / (p : â„)) :=
  snorm_nnreal_eq_snorm' hp

lemma snorm_nnreal_pow_eq_lintegral {p : â„â‰¥0} (hp : p â‰  0) :
    snorm f p Î¼ ^ (p : â„) = âˆ«â» x, â€–f xâ€–â‚Š ^ (p : â„) âˆ‚Î¼ := by
  simp [snorm_eq_snorm' (by exact_mod_cast hp) ENNReal.coe_ne_top,
    lintegral_rpow_nnnorm_eq_rpow_snorm' (show 0 < (p : â„) from pos_iff_ne_zero.mpr hp)]

end MeasureTheory

namespace MeasureTheory

end MeasureTheory

section NormedAddCommGroup
variable {Î¹ : Type*} [DecidableEq Î¹] [Fintype Î¹] {E : Î¹ â†’ Type _} [âˆ€ i, NormedAddCommGroup (E i)]

theorem Pi.nnnorm_single {i : Î¹} (y : E i) : â€–Pi.single i yâ€–â‚Š = â€–yâ€–â‚Š := by
  classical
  have H : âˆ€ b, â€–single i y bâ€–â‚Š = single (f := fun _ â†¦ â„â‰¥0) i â€–yâ€–â‚Š b := by
    intro b
    refine Pi.apply_single (fun i (x : E i) â†¦ â€–xâ€–â‚Š) ?_ i y b
    simp
  simp [Pi.nnnorm_def, H, Pi.single_apply, Finset.sup_ite,
    Finset.filter_eq' (Finset.univ : Finset Î¹)]

theorem Pi.norm_single {i : Î¹} (y : E i) : â€–Pi.single i yâ€– = â€–yâ€– :=
  congr_arg Subtype.val (Pi.nnnorm_single y)

end NormedAddCommGroup

section updateFinset

variable {Î¹ : Sort _} {Ï€ Î± : Î¹ â†’ Sort _} {x : âˆ€ i, Ï€ i} [DecidableEq Î¹]

-- this would be slightly nicer if we had a version of `Equiv.piFinsetUnion` for `insert`.
theorem update_updateFinset {s y i z} (hi : i âˆ‰ s) :
    Function.update (updateFinset x s y) i z = updateFinset x (s âˆª {i})
      ((Equiv.piFinsetUnion Ï€ <| Finset.disjoint_singleton_right.mpr hi) (y, uniqueElim z)) := by
  rw [update_eq_updateFinset, updateFinset_updateFinset]

theorem updateFinset_congr {s t : Finset Î¹} {y : âˆ€ i : s, Ï€ i} (h : s = t) :
    updateFinset x s y = updateFinset x t (fun i â†¦ y âŸ¨i, h â–¸ i.propâŸ©) := by
  subst h; rfl

theorem updateFinset_univ [Fintype Î¹] {y : âˆ€ i : Finset.univ, Ï€ i} :
    updateFinset x .univ y = fun i : Î¹ â†¦ y âŸ¨i, Finset.mem_univ iâŸ© := by
  simp [updateFinset_def]

lemma Finset.singleton_union {s : Finset Î¹} {i : Î¹} : {i} âˆª s = insert i s := by ext; simp
lemma Finset.union_singleton {s : Finset Î¹} {i : Î¹} : s âˆª {i} = insert i s := by ext; simp [or_comm]

namespace Equiv
-- todo: rename `Finset.union_symm_inl`, `Finset.union_symm_inr`

theorem Finset.union_symm_left {s t : Finset Î¹} (h : Disjoint s t) {i : Î¹} (hi : i âˆˆ s)
  (hi' : i âˆˆ s âˆª t) : (Equiv.Finset.union s t h).symm âŸ¨i, hi'âŸ© = Sum.inl âŸ¨i, hiâŸ© := by
  simp [Equiv.symm_apply_eq]

theorem Finset.union_symm_right {s t : Finset Î¹} (h : Disjoint s t) {i : Î¹} (hi : i âˆˆ t)
  (hi' : i âˆˆ s âˆª t) : (Equiv.Finset.union s t h).symm âŸ¨i, hi'âŸ© = Sum.inr âŸ¨i, hiâŸ© := by
  simp [Equiv.symm_apply_eq]

lemma piFinsetUnion_left {Î¹} [DecidableEq Î¹] (Î± : Î¹ â†’ Type*) {s t : Finset Î¹}
    (h : Disjoint s t) {f g} {i : Î¹} (hi : i âˆˆ s) (hi' : i âˆˆ s âˆª t) :
    Equiv.piFinsetUnion Î± h (f, g) âŸ¨i, hi'âŸ© = f âŸ¨i, hiâŸ© := by
  simp [Equiv.piFinsetUnion, eqRec_eq_cast]
  -- painful dependent type manipulations. The library hasn't much to help
  show cast ?h' ((sumPiEquivProdPi fun b â†¦ Î± â†‘((Finset.union s t h) b)).symm (f, g) _) = _
  generalize ?h' = h'
  set x := ((Finset.union s t h).symm { val := i, property := hi' })
  have : x = Sum.inl âŸ¨i, hiâŸ© := Finset.union_symm_left h hi hi'
  show cast h' ((sumPiEquivProdPi fun b â†¦ Î± â†‘((Finset.union s t h) b)).symm (f, g) x) = _
  clear_value x
  subst this
  rfl

lemma piFinsetUnion_right {Î¹} [DecidableEq Î¹] (Î± : Î¹ â†’ Type*) {s t : Finset Î¹}
    (h : Disjoint s t) {f g} {i : Î¹} (hi : i âˆˆ t) (hi' : i âˆˆ s âˆª t) :
    Equiv.piFinsetUnion Î± h (f, g) âŸ¨i, hi'âŸ© = g âŸ¨i, hiâŸ© := by
  simp [Equiv.piFinsetUnion, eqRec_eq_cast]
  -- painful dependent type manipulations. The library hasn't much to help
  show cast ?h' ((sumPiEquivProdPi fun b â†¦ Î± â†‘((Finset.union s t h) b)).symm (f, g) _) = _
  generalize ?h' = h'
  set x := ((Finset.union s t h).symm { val := i, property := hi' })
  have : x = Sum.inr âŸ¨i, hiâŸ© := Finset.union_symm_right h hi hi'
  show cast h' ((sumPiEquivProdPi fun b â†¦ Î± â†‘((Finset.union s t h) b)).symm (f, g) x) = _
  clear_value x
  subst this
  rfl

end Equiv

end updateFinset

variable {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]

local prefix:max "#" => Fintype.card

/-! ## The grid-lines lemma -/

variable {A : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (A i)]
  (Î¼ : âˆ€ i, Measure (A i)) [âˆ€ i, SigmaFinite (Î¼ i)]

namespace GridLines

/-- The "grid-lines operation" (not a standard name) which is central in the inductive proof of the
Sobolev inequality.

For a finite dependent product `Î  i : Î¹, A i` of sigma-finite measure spaces, a finite set `s` of
indices from `Î¹`, and a (later assumed nonnegative) real number `p`, this operation acts on a
function `f` from `Î  i, A i` into the extended nonnegative reals.  The operation is to partially
integrate, in the `s` co-ordinates, the function whose value at `x : Î  i, A i` is obtained by
multiplying a certain power of `f` with the product, for each co-ordinate `i` in `s`, of a certain
power of the integral of `f` along the "grid line" in the `i` direction through `x`.

We are most interested in this operation when the set `s` is the universe in `Î¹`, but as a proxy for
"induction on dimension" we define it for a general set `s` of co-ordinates: the `s`-grid-lines
operation on a function `f` which is constant along the co-ordinates in `sá¶œ` is morally (that is, up
to type-theoretic nonsense) the same thing as the universe-grid-lines operation on the associated
function on the "lower-dimensional" space `Î  i : s, A i`. -/
def T (p : â„) (f : (âˆ€ i, A i) â†’ â„â‰¥0âˆ) (s : Finset Î¹) : (âˆ€ i, A i) â†’ â„â‰¥0âˆ :=
  âˆ«â‹¯âˆ«â»_s, f ^ (1 - (s.card - 1 : â„) * p) * âˆ i in s, (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) ^ p âˆ‚Î¼

variable {p : â„}

@[simp] lemma T_univ (f : (âˆ€ i, A i) â†’ â„â‰¥0âˆ) (x : âˆ€ i, A i) :
    T Î¼ p f univ x
    = âˆ«â» (x : âˆ€ i, A i), (f x ^ (1 - (#Î¹ - 1 : â„) * p)
      * âˆ i : Î¹, (âˆ«â» t : A i, f (update x i t) âˆ‚(Î¼ i)) ^ p) âˆ‚(.pi Î¼) := by
  simp [T, lmarginal_univ, lmarginal_singleton, card_univ]

@[simp] lemma T_empty (f : (âˆ€ i, A i) â†’ â„â‰¥0âˆ) (x : âˆ€ i, A i) :
    T Î¼ p f âˆ… x = f x ^ (1 + p) := by
  simp [T]

set_option maxHeartbeats 500000 in
/-- The main inductive step in the grid-lines lemma for the Gagliardo-Nirenberg-Sobolev inequality.

The grid-lines operation `GridLines.T` on a nonnegative function on a finitary product type is
less than or equal to the grid-lines operation of its partial integral in one co-ordinate
(the latter intuitively considered as a function on a space "one dimension down"). -/
theorem T_insert_le_T_lmarginal_singleton (hpâ‚€ : 0 â‰¤ p) (s : Finset Î¹)
    (hp : (s.card : â„) * p â‰¤ 1)
    (i : Î¹) (hi : i âˆ‰ s) {f : (âˆ€ i, A i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    T Î¼ p f (insert i s) â‰¤ T Î¼ p (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) s := by
  calc T Î¼ p f (insert i s)
      = âˆ«â‹¯âˆ«â»_insert i s,
            f ^ (1 - (s.card : â„) * p) * âˆ j in (insert i s), (âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) ^ p âˆ‚Î¼ := by
          simp_rw [T, card_insert_of_not_mem hi]
          congr!
          push_cast
          ring
    _ = âˆ«â‹¯âˆ«â»_s, (fun x â†¦ âˆ«â» (t : A i),
            (f (update x i t) ^ (1 - (s.card : â„) * p)
            * âˆ j in (insert i s), (âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) (update x i t) ^ p)  âˆ‚ (Î¼ i)) âˆ‚Î¼ := by
          rw [lmarginal_insert' _ _ hi]
          Â· congr! with x t
            simp only [Pi.mul_apply, Pi.pow_apply, Finset.prod_apply]
          Â· change Measurable (fun x â†¦ _)
            simp only [Pi.mul_apply, Pi.pow_apply, Finset.prod_apply]
            refine (hf.pow_const _).mul <| Finset.measurable_prod _ ?_
            exact fun _ _ â†¦ hf.lmarginal Î¼ |>.pow_const _
    _ â‰¤ T Î¼ p (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) s := lmarginal_mono (s:=s) (fun x â†¦ ?_)
  simp only [Pi.mul_apply, Pi.pow_apply, Finset.prod_apply]
  have hFâ‚ : âˆ€ {j : Î¹}, Measurable fun t â†¦ (âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) (update x i t) :=
    fun {_} â†¦ hf.lmarginal Î¼ |>.comp <| measurable_update _
  have hFâ‚€ : Measurable fun t â†¦ f (update x i t) := hf.comp <| measurable_update _
  let k : â„ := s.card
  have hk' : 0 â‰¤ 1 - k * p := by linarith only [hp]
  let X := update x i
  calc âˆ«â» t, f (X t) ^ (1 - k * p)
          * âˆ j in (insert i s), (âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) (X t) ^ p âˆ‚ (Î¼ i)
      = âˆ«â» t, (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) (X t) ^ p * (f (X t) ^ (1 - k * p)
          * âˆ j in s, ((âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) (X t) ^ p)) âˆ‚(Î¼ i) := by
              -- rewrite integrand so that `(âˆ«â‹¯âˆ«â»_insert i s, f âˆ‚Î¼) ^ p` comes first
              clear_value X
              congr! 2 with t
              simp_rw [prod_insert hi]
              ring_nf
    _ = (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x ^ p *
          âˆ«â» t, f (X t) ^ (1 - k * p) * âˆ j in s, ((âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) (X t)) ^ p âˆ‚(Î¼ i) := by
              -- pull out this constant factor
              have : âˆ€ t, (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) (X t) = (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x := by
                intro t
                rw [lmarginal_update_of_mem]
                exact Iff.mpr Finset.mem_singleton rfl
              simp_rw [this]
              rw [lintegral_const_mul]
              exact (hFâ‚€.pow_const _).mul <| Finset.measurable_prod _ fun _ _ â†¦ hFâ‚.pow_const _
    _ â‰¤ (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x ^ p *
          ((âˆ«â» t, f (X t) âˆ‚Î¼ i) ^ (1 - k * p)
          * âˆ j in s, (âˆ«â» t, (âˆ«â‹¯âˆ«â»_{j}, f âˆ‚Î¼) (X t) âˆ‚Î¼ i) ^ p) := by
              -- apply HÃ¶lder's inequality
              gcongr
              apply ENNReal.lintegral_mul_prod_norm_pow_le
              Â· exact hFâ‚€.aemeasurable
              Â· intros
                exact hFâ‚.aemeasurable
              Â· simp only [sum_const, nsmul_eq_mul]
                ring
              Â· exact hk'
              Â· exact fun _ _ â†¦ hpâ‚€
    _ = (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x ^ p *
          ((âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x ^ (1 - k * p) * âˆ j in s, (âˆ«â‹¯âˆ«â»_{i, j}, f âˆ‚Î¼) x ^ p) := by
              -- absorb the newly-created integrals into `âˆ«â‹¯âˆ«`
              dsimp only
              congr! 2
              Â· rw [lmarginal_singleton]
              refine prod_congr rfl fun j hj => ?_
              have hi' : i âˆ‰ ({j} : Finset Î¹) := by
                simp only [Finset.mem_singleton, Finset.mem_insert, Finset.mem_compl] at hj âŠ¢
                exact fun h â†¦ hi (h â–¸ hj)
              rw [lmarginal_insert _ hf hi']
    _ = (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x ^ (p + (1 - k * p)) *  âˆ j in s, (âˆ«â‹¯âˆ«â»_{i, j}, f âˆ‚Î¼) x ^ p := by
              -- combine two `(âˆ«â‹¯âˆ«â»_insert i s, f âˆ‚Î¼) x` terms
              rw [ENNReal.rpow_add_of_nonneg]
              Â· ring
              Â· exact hpâ‚€
              Â· exact hk'
    _ â‰¤ (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) x ^ (1 - (s.card - 1 : â„) * p) *
          âˆ j in s, (âˆ«â‹¯âˆ«â»_{j}, (âˆ«â‹¯âˆ«â»_{i}, f âˆ‚Î¼) âˆ‚Î¼) x ^ p := by
              -- identify the result with the RHS integrand
              congr! 2 with j hj
              Â· push_cast
                ring_nf
              Â· congr! 1
                rw [â† lmarginal_union Î¼ f hf]
                Â· congr
                  rw [Finset.union_comm]
                  rfl
                Â· rw [Finset.disjoint_singleton]
                  simp only [Finset.mem_insert, Finset.mem_compl] at hj
                  exact fun h â†¦ hi (h â–¸ hj)

/-- Auxiliary result for the grid-lines lemma.  Given a nonnegative function on a finitary product
type indexed by `Î¹`, and a set `s` in `Î¹`, consider partially integrating over the variables in
`sá¶œ` and performing the "grid-lines operation" (see `GridLines.T`) to the resulting function in the
variables `s`.  This theorem states that this operation decreases as the number of grid-lines taken
increases. -/
theorem T_lmarginal_antitone (hpâ‚€ : 0 â‰¤ p) (hp : (#Î¹ - 1 : â„) * p â‰¤ 1)
    {f : (âˆ€ i, A i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    Antitone (fun s â†¦ T Î¼ p (âˆ«â‹¯âˆ«â»_sá¶œ, f âˆ‚Î¼) s) := by
  -- Reformulate (by induction): a function is decreasing on `Finset Î¹` if it decreases under the
  -- insertion of any element to any set.
  rw [Finset.antitone_iff_forall_insert_le]
  intro s i hi
  -- apply the lemma designed to encapsulate the inductive step
  convert T_insert_le_T_lmarginal_singleton Î¼ hpâ‚€ s ?_ i hi (hf.lmarginal Î¼) using 2
  Â· rw [â† lmarginal_union Î¼ f hf]
    Â· rw [â† insert_compl_insert hi]
      rfl
    rw [Finset.disjoint_singleton_left, not_mem_compl]
    exact mem_insert_self i s
  Â· -- the main nontrivial point is to check that an exponent `p` satisfying `0 â‰¤ p` and
    -- `(#Î¹ - 1) * p â‰¤ 1` is in the valid range for the inductive-step lemma
    refine le_trans ?_ hp
    gcongr
    suffices (s.card : â„) + 1 â‰¤ #Î¹ by linarith
    rw [â† card_add_card_compl s]
    norm_cast
    gcongr
    have hi' : sá¶œ.Nonempty := âŸ¨i, by rwa [Finset.mem_compl]âŸ©
    rwa [â† card_pos] at hi'

end GridLines

/-- The "grid-lines lemma" (not a standard name), stated with a general parameter `p` as the
exponent.  Compare with `lintegral_prod_lintegral_pow_le`.

For any finite dependent product `Î  i : Î¹, A i` of sigma-finite measure spaces, for any
nonnegative real number `p` such that `(#Î¹ - 1) * p â‰¤ 1`, for any function `f` from `Î  i, A i` into
the extended nonnegative reals, we consider an associated "grid-lines quantity", the integral of an
associated function from `Î  i, A i` into the extended nonnegative reals.  The value of this function
at `x : Î  i, A i` is obtained by multiplying a certain power of `f` with the product, for each
co-ordinate `i`, of a certain power of the integral of `f` along the "grid line" in the `i`
direction through `x`.

This lemma bounds the Lebesgue integral of the grid-lines quantity by a power of the Lebesgue
integral of `f`. -/
theorem lintegral_mul_prod_lintegral_pow_le {p : â„} (hpâ‚€ : 0 â‰¤ p)
    (hp : (#Î¹ - 1 : â„) * p â‰¤ 1) {f : (âˆ€ i : Î¹, A i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» x, f x ^ (1 - (#Î¹ - 1 : â„) * p) * âˆ i, (âˆ«â» xáµ¢, f (update x i xáµ¢) âˆ‚Î¼ i) ^ p âˆ‚.pi Î¼
    â‰¤ (âˆ«â» x, f x âˆ‚.pi Î¼) ^ (1 + p) := by
  cases isEmpty_or_nonempty (âˆ€ i, A i)
  Â· simp_rw [lintegral_of_isEmpty]; refine' zero_le _
  inhabit âˆ€ i, A i
  have H : (âˆ… : Finset Î¹) â‰¤ Finset.univ := Finset.empty_subset _
  simpa [lmarginal_univ] using GridLines.T_lmarginal_antitone Î¼ hpâ‚€ hp hf H default

/-- Special case of the grid-lines lemma `lintegral_mul_prod_lintegral_pow_le`, taking the extremal
exponent `p = (#Î¹ - 1)â»Â¹`. -/
theorem lintegral_prod_lintegral_pow_le [Nontrivial Î¹]
    {p : â„} (hp : Real.IsConjExponent #Î¹ p)
    {f} (hf : Measurable f) :
    âˆ«â» x, âˆ i, (âˆ«â» xáµ¢, f (update x i xáµ¢) âˆ‚Î¼ i) ^ ((1 : â„) / (#Î¹ - 1 : â„)) âˆ‚.pi Î¼
    â‰¤ (âˆ«â» x, f x âˆ‚.pi Î¼) ^ p := by
  have h0 : (1:â„) < #Î¹ := by norm_cast; exact Fintype.one_lt_card
  have h1 : (0:â„) < #Î¹ - 1 := by linarith
  have h3 : 0 â‰¤ ((1 : â„) / (#Î¹ - 1 : â„)) := by positivity
  have h4 : (#Î¹ - 1 : â„) * ((1 : â„) / (#Î¹ - 1 : â„)) â‰¤ 1 := by field_simp
  have h5 : p = 1 + 1 / (â†‘#Î¹ - 1) := by field_simp; rw [mul_comm, hp.sub_one_mul_conj]
  rw [h5]
  convert lintegral_mul_prod_lintegral_pow_le Î¼ h3 h4 hf using 2
  field_simp

/-! ## The Gagliardo-Nirenberg-Sobolev inequality -/

variable [Nontrivial Î¹] {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [CompleteSpace F]

/-- The **Gagliardo-Nirenberg-Sobolev inequality**.  Let `u` be a continuously differentiable
compactly-supported function `u` on `â„â¿`, for `n â‰¥ 2`.  (More literally we encode `â„â¿` as
`Î¹ â†’ â„` where `n := #Î¹` is finite and at least 2.)  Then the Lebesgue integral of the pointwise
expression `|u x| ^ (n / (n - 1))` is bounded above by the `n / (n - 1)`-th power of the Lebesgue
integral of the FrÃ©chet derivative of `u`.

For a basis-free version, see `lintegral_pow_le_pow_lintegral_fderiv`. -/
theorem lintegral_pow_le_pow_lintegral_fderiv_aux
    {p : â„} (hp : Real.IsConjExponent #Î¹ p)
    {u : (Î¹ â†’ â„) â†’ F} (hu : ContDiff â„ 1 u)
    (h2u : HasCompactSupport u) :
    âˆ«â» x, (â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ p â‰¤ (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š) ^ p := by
  have : (1:â„) â‰¤ â†‘#Î¹ - 1 := by
    have hÎ¹ : (2:â„) â‰¤ #Î¹ := by exact_mod_cast Fintype.one_lt_card
    linarith
  calc âˆ«â» x, (â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ p
      = âˆ«â» x, ((â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / (#Î¹ - 1 : â„))) ^ (#Î¹ : â„) := by
        -- a little algebraic manipulation of the exponent
        congr! 2 with x
        rw [â† ENNReal.rpow_mul, hp.conj_eq]
        field_simp
    _ = âˆ«â» x, âˆ _i : Î¹, (â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / (#Î¹ - 1 : â„)) := by
        -- express the left-hand integrand as a product of identical factors
        congr! 2 with x
        simp_rw [prod_const, card_univ]
        norm_cast
    _ â‰¤ âˆ«â» x, âˆ i, (âˆ«â» xáµ¢, â€–fderiv â„ u (update x i xáµ¢)â€–â‚Š) ^ ((1 : â„) / (#Î¹ - 1 : â„)) := ?_
    _ â‰¤ (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š) ^ p :=
        -- apply the grid-lines lemma
        lintegral_prod_lintegral_pow_le _ hp (by fun_prop)
  gcongr with x i
  calc (â€–u xâ€–â‚Š : â„â‰¥0âˆ)
      = (â€–âˆ« xáµ¢ in Iic (x i), deriv (u âˆ˜ update x i) xáµ¢â€–â‚Š : â„â‰¥0âˆ) := by
        -- apply the half-infinite fundamental theorem of calculus
        have h3u : ContDiff â„ 1 (u âˆ˜ update x i) := hu.comp (by convert contDiff_update 1 x i)
        have h4u : HasCompactSupport (u âˆ˜ update x i) :=
          h2u.comp_closedEmbedding (closedEmbedding_update x i)
        simp [HasCompactSupport.integral_Iic_deriv_eq h3u h4u (x i)]
    _ â‰¤ âˆ«â» xáµ¢ in Iic (x i), â€–deriv (u âˆ˜ update x i) xáµ¢â€–â‚Š :=
        ennnorm_integral_le_lintegral_ennnorm _ -- apply the triangle inequality
    _ â‰¤ âˆ«â» xáµ¢, (â€–fderiv â„ u (update x i xáµ¢)â€–â‚Š : â„â‰¥0âˆ) := ?_
  gcongr with y; swap; exact Measure.restrict_le_self
  -- bound the derivative which appears
  calc â€–deriv (u âˆ˜ update x i) yâ€–â‚Š = â€–fderiv â„ u (update x i y) (deriv (update x i) y)â€–â‚Š := by
        rw [fderiv.comp_deriv _ (hu.differentiable le_rfl).differentiableAt
          (hasDerivAt_update x i y).differentiableAt]
    _ â‰¤ â€–fderiv â„ u (update x i y)â€–â‚Š * â€–deriv (update x i) yâ€–â‚Š :=
        ContinuousLinearMap.le_opNNNorm ..
    _ â‰¤ â€–fderiv â„ u (update x i y)â€–â‚Š := by simp [deriv_update, Pi.nnnorm_single]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [MeasurableSpace E] [BorelSpace E]
  [FiniteDimensional â„ E] (Î¼ : Measure E) [IsAddHaarMeasure Î¼]

open FiniteDimensional

section

example (c : â„â‰¥0) (Î¼ : Measure E) : c â€¢ Î¼ = (c : â„â‰¥0âˆ) â€¢ Î¼ := by rw [@ENNReal.smul_def]

set_option linter.unusedVariables false in
variable (F) in
/-- The **Gagliardo-Nirenberg-Sobolev inequality**.  Let `u` be a continuously differentiable
compactly-supported function `u` on a normed space `E` of finite dimension `n â‰¥ 2`, equipped
with Haar measure. There exists a constant `C` depending only on `E`, such that the Lebesgue
integral of the pointwise expression `|u x| ^ (n / (n - 1))` is bounded above by `C` times the
`n / (n - 1)`-th power of the Lebesgue integral of the FrÃ©chet derivative of `u`. -/
theorem lintegral_pow_le_pow_lintegral_fderiv (hE : 2 â‰¤ finrank â„ E)
    {p : â„} (hp : Real.IsConjExponent (finrank â„ E) p) :
    âˆƒ C : â„â‰¥0, âˆ€ {u : E â†’ F} (hu : ContDiff â„ 1 u) (h2u : HasCompactSupport u),
    âˆ«â» x, (â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚Î¼ â‰¤ C * (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š âˆ‚Î¼) ^ p := by
  -- we reduce to the case of `E = Î¹ â†’ â„`, for which we have already proved the result using
  -- matrices in `lintegral_pow_le_pow_lintegral_fderiv_aux`.
  let Î¹ := Fin (finrank â„ E)
  have hÎ¹card : #Î¹ = finrank â„ E := Fintype.card_fin (finrank â„ E)
  have : Nontrivial Î¹ := by rwa [Fin.nontrivial_iff_two_le]
  have : FiniteDimensional â„ (Î¹ â†’ â„) := by infer_instance
  have : finrank â„ E = finrank â„ (Î¹ â†’ â„) := by simp
  have e : E â‰ƒL[â„] Î¹ â†’ â„ := ContinuousLinearEquiv.ofFinrankEq this
  have : IsAddHaarMeasure ((volume : Measure (Î¹ â†’ â„)).map e.symm) :=
    (e.symm : (Î¹ â†’ â„) â‰ƒ+ E).isAddHaarMeasure_map _ e.symm.continuous e.symm.symm.continuous
  have hp : Real.IsConjExponent #Î¹ p := by rwa [hÎ¹card]
  have h0p : 0 â‰¤ p := hp.symm.nonneg
  let c := addHaarScalarFactor Î¼ ((volume : Measure (Î¹ â†’ â„)).map e.symm)
  have hc : 0 < c := addHaarScalarFactor_pos_of_isAddHaarMeasure ..
  have h2c : Î¼ = c â€¢ ((volume : Measure (Î¹ â†’ â„)).map e.symm) := isAddLeftInvariant_eq_smul ..
  have h3c : (c : â„â‰¥0âˆ) â‰  0 := by simp_rw [ne_eq, ENNReal.coe_eq_zero, hc.ne', not_false_eq_true]
  have : âˆƒ C : â„â‰¥0, C * c ^ p = c * â€–(e.symm : (Î¹ â†’ â„) â†’L[â„] E)â€–â‚Š ^ p := by
    use (c * â€–(e.symm : (Î¹ â†’ â„) â†’L[â„] E)â€–â‚Š ^ p) * (c ^ p)â»Â¹
    rw [inv_mul_cancel_rightâ‚€]
    exact (NNReal.rpow_pos hc).ne'
  refine this.imp fun C hC u hu h2u â†¦ ?_
  rw [h2c, ENNReal.smul_def, lintegral_smul_measure, lintegral_smul_measure]
  let v : (Î¹ â†’ â„) â†’ F := u âˆ˜ e.symm
  have hv : ContDiff â„ 1 v := hu.comp e.symm.contDiff
  have h2v : HasCompactSupport v := h2u.comp_homeomorph e.symm.toHomeomorph
  have :=
  calc âˆ«â» x, (â€–u xâ€–â‚Š : â„â‰¥0âˆ) ^ p âˆ‚(volume : Measure (Î¹ â†’ â„)).map e.symm
      = âˆ«â» y, (â€–v yâ€–â‚Š : â„â‰¥0âˆ) ^ p := by
        refine lintegral_map ?_ e.symm.continuous.measurable
        borelize F
        exact hu.continuous.measurable.nnnorm.coe_nnreal_ennreal.pow_const _
    _ â‰¤ (âˆ«â» y, â€–fderiv â„ v yâ€–â‚Š) ^ p :=
        lintegral_pow_le_pow_lintegral_fderiv_aux hp hv h2v
    _ = (âˆ«â» y, â€–(fderiv â„ u (e.symm y)).comp (fderiv â„ e.symm y)â€–â‚Š) ^ p := by
        congr! with y
        apply fderiv.comp _ (hu.differentiable le_rfl _)
        exact e.symm.differentiableAt
    _ â‰¤ (âˆ«â» y, â€–fderiv â„ u (e.symm y)â€–â‚Š * â€–(e.symm : (Î¹ â†’ â„) â†’L[â„] E)â€–â‚Š) ^ p := by
        gcongr with y
        norm_cast
        rw [e.symm.fderiv]
        apply ContinuousLinearMap.opNNNorm_comp_le
    _ = (â€–(e.symm : (Î¹ â†’ â„) â†’L[â„] E)â€–â‚Š * âˆ«â» y, â€–fderiv â„ u (e.symm y)â€–â‚Š) ^ p := by
        rw [lintegral_mul_const, mul_comm]
        refine (Continuous.nnnorm ?_).measurable.coe_nnreal_ennreal
        exact (hu.continuous_fderiv le_rfl).comp e.symm.continuous
    _ = (â€–(e.symm : (Î¹ â†’ â„) â†’L[â„] E)â€–â‚Š ^ p : â„â‰¥0) * (âˆ«â» y, â€–fderiv â„ u (e.symm y)â€–â‚Š) ^ p := by
        rw [ENNReal.mul_rpow_of_nonneg _ _ h0p, ENNReal.coe_rpow_of_nonneg _ h0p]
    _ = (â€–(e.symm : (Î¹ â†’ â„) â†’L[â„] E)â€–â‚Š ^ p : â„â‰¥0)
        * (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š âˆ‚(volume : Measure (Î¹ â†’ â„)).map e.symm) ^ p := by
        congr
        rw [lintegral_map _ e.symm.continuous.measurable]
        fun_prop
  rw [â† ENNReal.mul_le_mul_left h3c ENNReal.coe_ne_top, â† mul_assoc, â† ENNReal.coe_mul, â† hC,
    ENNReal.coe_mul] at this
  rw [ENNReal.mul_rpow_of_nonneg _ _ h0p, â† mul_assoc, ENNReal.coe_rpow_of_ne_zero hc.ne']
  exact this

set_option linter.unusedVariables false in
variable (F) in
/-- The **Gagliardo-Nirenberg-Sobolev inequality**.  Let `u` be a continuously differentiable
compactly-supported function `u` on a normed space `E` of finite dimension `n â‰¥ 2`, equipped
with Haar measure. There exists a constant `C` depending only on `E`, such that the `Láµ–` norm of
`u`, where `p := n / (n - 1)`, is bounded above by `C` times the `LÂ¹` norm of the FrÃ©chet derivative
of `u`. -/
theorem snorm_le_snorm_fderiv (hE : 2 â‰¤ finrank â„ E)
    {p : â„â‰¥0} (hp : NNReal.IsConjExponent (finrank â„ E) p) :
    âˆƒ C : â„â‰¥0, âˆ€ {u : E â†’ F} (hu : ContDiff â„ 1 u) (h2u : HasCompactSupport u),
    snorm u p Î¼ â‰¤ C * snorm (fderiv â„ u) 1 Î¼ := by
  obtain âŸ¨m, hmâŸ© : âˆƒ m, finrank â„ E = m + 2 := Nat.exists_eq_add_of_le' hE
  have h0p : 0 < (p : â„) := hp.coe.symm.pos
  obtain âŸ¨C, hCâŸ© := lintegral_pow_le_pow_lintegral_fderiv F Î¼ hE hp.coe
  use C ^ (p : â„)â»Â¹
  intro u hu h2u
  rw [snorm_one_eq_lintegral_nnnorm,
    â† ENNReal.rpow_le_rpow_iff h0p, ENNReal.mul_rpow_of_nonneg _ _ h0p.le,
    ENNReal.coe_rpow_of_nonneg _ h0p.le, â† NNReal.rpow_mul,
    snorm_nnreal_pow_eq_lintegral hp.symm.pos.ne',
    inv_mul_cancel h0p.ne', NNReal.rpow_one]
  exact hC hu h2u

variable (F' : Type*) [NormedAddCommGroup F'] [InnerProductSpace â„ F'] [CompleteSpace F']
set_option linter.unusedVariables false in
/-- The **Gagliardo-Nirenberg-Sobolev inequality**.  Let `u` be a continuously differentiable
compactly-supported function `u` on a normed space `E` of finite dimension `n`, equipped
with Haar measure, let `1 < p < n` and let `p'â»Â¹ := pâ»Â¹ - nâ»Â¹`.
There exists a constant `C` depending only on `E` and `p`, such that the `Láµ–'` norm of `u`
is bounded above by `C` times the `Láµ–` norm of the FrÃ©chet derivative of `u`.

Note: The codomain of `u` needs to be an inner product space.
-/
theorem snorm_le_snorm_fderiv_of_eq {p p' : â„â‰¥0} (hp : 1 â‰¤ p)
    (h2p : p < finrank â„ E) (hp' : (p' : â„)â»Â¹ = pâ»Â¹ - (finrank â„ E : â„)â»Â¹) :
    âˆƒ C : â„â‰¥0, âˆ€ {u : E â†’ F'} (hu : ContDiff â„ 1 u) (h2u : HasCompactSupport u),
    snorm u p' Î¼ â‰¤ C * snorm (fderiv â„ u) p Î¼ := by
  set n := finrank â„ E
  let n' := NNReal.conjExponent n
  have h0n : 2 â‰¤ n := Nat.succ_le_of_lt <| Nat.one_lt_cast.mp <| hp.trans_lt h2p
  have hn : NNReal.IsConjExponent n n' := .conjExponent (by norm_cast)
  have h1n : 1 â‰¤ (n : â„â‰¥0) := hn.one_le
  have h2n : (0 : â„) < n - 1 := by simp_rw [sub_pos]; exact hn.coe.one_lt
  have hnp : (0 : â„) < n - p := by simp_rw [sub_pos]; exact h2p
  rcases hp.eq_or_lt with rfl|hp
  -- the case `p = 1`
  Â· obtain âŸ¨C, hCâŸ© := snorm_le_snorm_fderiv F' Î¼ h0n hn
    refine âŸ¨C, @fun u hu h2u â†¦ ?_âŸ©
    convert hC hu h2u
    ext
    rw [â† inv_inj, hp']
    field_simp [NNReal.conjExponent]
  -- the case `p > 1`
  let q := Real.conjExponent p
  have hq : Real.IsConjExponent p q := .conjExponent hp
  have h0p : p â‰  0 := zero_lt_one.trans hp |>.ne'
  have h1p : (p : â„) â‰  1 := hq.one_lt.ne'
  -- have h3p : (p : â„) â‰  0 := hq.pos.ne'
  have h3p : (p : â„) - 1 â‰  0 := sub_ne_zero_of_ne h1p
  have h0p' : p' â‰  0 := by
    suffices 0 < (p' : â„) from (show 0 < p' from this) |>.ne'
    rw [â† inv_pos, hp', sub_pos]
    exact inv_lt_inv_of_lt hq.pos h2p
  have h2q : 1 / n' - 1 / q = 1 / p' := by
    simp_rw (config := {zeta := false}) [one_div, hp']
    rw [hq.conj_inv_eq, hn.coe.conj_inv_eq, sub_sub_sub_cancel_left]
    simp
  let Î³ : â„â‰¥0 := âŸ¨p * (n - 1) / (n - p), by positivityâŸ©
  have h0Î³ : (Î³ : â„) = p * (n - 1) / (n - p) := rfl
  have h1Î³ : 1 < (Î³ : â„) := by
    rwa [h0Î³, one_lt_div hnp, mul_sub, mul_one, sub_lt_sub_iff_right, lt_mul_iff_one_lt_left]
    exact hn.coe.pos
  have h2Î³ : Î³ * n' = p' := by
    rw [â† NNReal.coe_inj, â† inv_inj, hp', NNReal.coe_mul, h0Î³, hn.coe.conj_eq]
    field_simp; ring
  have h3Î³ : (Î³ - 1) * q = p' := by
    rw [â† inv_inj, hp', h0Î³, hq.conj_eq]
    have : (p : â„) * (n - 1) - (n - p) = n * (p - 1) := by ring
    field_simp; rw [this]; field_simp; ring
  have h4Î³ : (Î³ : â„) â‰  0 := (zero_lt_one.trans h1Î³).ne'
  obtain âŸ¨C, hCâŸ© := snorm_le_snorm_fderiv â„ Î¼ h0n hn
  refine âŸ¨C * Î³, @fun u hu h2u â†¦ ?_âŸ©
  by_cases h3u : âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼ = 0
  Â· rw [snorm_nnreal_eq_lintegral h0p', h3u, ENNReal.zero_rpow_of_pos] <;> positivity
  have h4u : âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼ â‰  âˆ := by
    refine lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top (pos_iff_ne_zero.mpr h0p') ?_ |>.ne
    dsimp only
    rw [NNReal.val_eq_coe, â† snorm_nnreal_eq_snorm' h0p']
    exact hu.continuous.memâ„’p_of_hasCompactSupport (Î¼ := Î¼) h2u |>.snorm_lt_top
  have h5u : (âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼) ^ (1 / q) â‰  0 :=
    ENNReal.rpow_pos (pos_iff_ne_zero.mpr h3u) h4u |>.ne'
  have h6u : (âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼) ^ (1 / q) â‰  âˆ :=
    ENNReal.rpow_ne_top_of_nonneg (div_nonneg zero_le_one hq.symm.nonneg) h4u
  have h7u := hu.continuous -- for fun_prop
  have h8u := (hu.fderiv_right (m := 0) le_rfl).continuous -- for fun_prop
  let v : E â†’ â„ := fun x â†¦ â€–u xâ€– ^ (Î³ : â„)
  have hv : ContDiff â„ 1 v := hu.norm_rpow h1Î³
  have h2v : HasCompactSupport v := h2u.norm.rpow_const h4Î³
  have :=
  calc (âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼) ^ (1 / (n' : â„)) = snorm v n' Î¼ := by
        rw [â† h2Î³, snorm_nnreal_eq_lintegral hn.symm.pos.ne']
        congr! 3
        simp [Real.nnnorm_rpow_of_nonneg, ENNReal.rpow_mul]
        rw [ENNReal.coe_rpow_of_nonneg]
        positivity
    _ â‰¤ C * snorm (fderiv â„ v) 1 Î¼ := hC hv h2v
    _ = C * âˆ«â» x, â€–fderiv â„ v xâ€–â‚Š âˆ‚Î¼ := by rw [snorm_one_eq_lintegral_nnnorm]
    _ â‰¤ C * Î³ * âˆ«â» x, â€–u xâ€–â‚Š ^ ((Î³ : â„) - 1) * â€–fderiv â„ u xâ€–â‚Š âˆ‚Î¼ := by
      rw [mul_assoc, â† lintegral_const_mul Î³]
      gcongr
      simp_rw [â† mul_assoc, ENNReal.coe_rpow_of_nonneg _ (sub_nonneg.mpr h1Î³.le)]
      exact ENNReal.coe_le_coe.mpr <| nnnorm_fderiv_norm_rpow_le (hu.differentiable le_rfl) h1Î³
      fun_prop
    _ â‰¤ C * Î³ * ((âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼) ^ (1 / q) *
        (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š ^ (p : â„) âˆ‚Î¼) ^ (1 / (p : â„))) := by
        gcongr
        convert ENNReal.lintegral_mul_le_Lp_mul_Lq Î¼
          (.symm <| .conjExponent <| show 1 < (p : â„) from hp) ?_ ?_ using 5
        Â· simp_rw [â† ENNReal.rpow_mul, â† h3Î³]
        Â· borelize F'
          fun_prop
        Â· fun_prop
    _ = C * Î³ * (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š ^ (p : â„) âˆ‚Î¼) ^ (1 / (p : â„)) *
      (âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼) ^ (1 / q) := by ring
  calc
    snorm u p' Î¼ = (âˆ«â» x, â€–u xâ€–â‚Š ^ (p' : â„) âˆ‚Î¼) ^ (1 / (p' : â„)) := snorm_nnreal_eq_lintegral h0p'
    _ â‰¤ C * Î³ * (âˆ«â» x, â€–fderiv â„ u xâ€–â‚Š ^ (p : â„) âˆ‚Î¼) ^ (1 / (p : â„)) :=
      by rwa [â† h2q, ENNReal.rpow_sub _ _ h3u h4u, ENNReal.div_le_iff h5u h6u]
    _ = C * Î³ *  snorm (fderiv â„ u) (â†‘p) Î¼ := by rw [snorm_nnreal_eq_lintegral h0p]

set_option linter.unusedVariables false in
/-- The **Gagliardo-Nirenberg-Sobolev inequality**.  Let `u` be a continuously differentiable
compactly-supported function `u` on a normed space `E` of finite dimension `n`, equipped
with Haar measure, and let `1 < p < n` and `1 â‰¤ q â‰¤ (pâ»Â¹ - (finrank â„ E : â„)â»Â¹)â»Â¹`.
There exists a constant `C` depending only on `E`, `s`, `p` and `q`, such that the `L^q` norm of `u`
is bounded above by `C` times the `Láµ–` norm of the FrÃ©chet derivative of `u`.

Note: The codomain of `u` needs to be an inner product space.
-/
theorem snorm_le_snorm_fderiv_of_le {p q : â„â‰¥0} (hp : 1 â‰¤ p) (hq : 1 â‰¤ q)
    (h2p : p < finrank â„ E) (hpq : pâ»Â¹ - (finrank â„ E : â„)â»Â¹ â‰¤ (q : â„)â»Â¹) {s : Set E}
    (hs : IsCompact s) :
    âˆƒ C : â„â‰¥0, âˆ€ (u : E â†’ F') (hu : ContDiff â„ 1 u) (h2u : u.support âŠ† s),
    snorm u q Î¼ â‰¤ C * snorm (fderiv â„ u) p Î¼ := by
  have h3p : 0 < p :=
    calc 0 < 1 := by norm_num
      _ â‰¤ p := hp
  have hdim : (0:â„â‰¥0) < finrank â„ E := h3p.trans h2p
  let p' : â„â‰¥0 := (pâ»Â¹ - (finrank â„ E : â„â‰¥0)â»Â¹)â»Â¹
  have hp' : p'â»Â¹ = pâ»Â¹ - (finrank â„ E : â„)â»Â¹ := by
    rw [inv_inv, NNReal.coe_sub]
    Â· simp
    Â· gcongr
  have : (q : â„â‰¥0âˆ) â‰¤ p' := by
    have H : (p':â„)â»Â¹ â‰¤ (â†‘q)â»Â¹ := trans hp' hpq
    norm_cast at H âŠ¢
    rwa [inv_le_inv] at H
    Â· dsimp
      have : 0 < pâ»Â¹ - (finrank â„ E : â„â‰¥0)â»Â¹ := by
        simp only [tsub_pos_iff_lt]
        gcongr
      positivity
    Â· positivity
  obtain âŸ¨C, hCâŸ© := snorm_le_snorm_fderiv_of_eq Î¼ F' hp h2p hp'
  set t := (Î¼ s).toNNReal ^ (1 / q - 1 / p' : â„)
  use t * C
  intro u hu h2u
  calc snorm u q Î¼ = snorm u q (Î¼.restrict s) := by
        rw [snorm_restrict_eq u q Î¼ hs.measurableSet h2u]
    _ â‰¤ snorm u p' (Î¼.restrict s) * t := by
        convert snorm_le_snorm_mul_rpow_measure_univ this hu.continuous.aestronglyMeasurable
        rw [â† ENNReal.coe_rpow_of_nonneg]
        Â· simp [ENNReal.coe_toNNReal hs.measure_lt_top.ne]
        Â· rw [one_div, one_div]
          norm_cast
          rw [hp']
          simpa using hpq
    _ = snorm u p' Î¼ * t := by rw [snorm_restrict_eq u p' Î¼ hs.measurableSet h2u]
    _ â‰¤ (C * snorm (fderiv â„ u) p Î¼) * t := by
        have h2u' : HasCompactSupport u := HasCompactSupport.of_support_subset_isCompact hs h2u
        rel [hC hu h2u']
    _ = (t * C) * snorm (fderiv â„ u) p Î¼ := by ring

set_option linter.unusedVariables false in
/-- The **Gagliardo-Nirenberg-Sobolev inequality**.  Let `u` be a continuously differentiable
compactly-supported function `u` on a normed space `E` of finite dimension `n`, equipped
with Haar measure, and let `1 < p < n`.
There exists a constant `C` depending only on `E` and `p`, such that the `Láµ–` norm of `u`
is bounded above by `C` times the `Láµ–` norm of the FrÃ©chet derivative of `u`.

Note: The codomain of `u` needs to be an inner product space.
-/
theorem snorm_le_snorm_fderiv' {p : â„â‰¥0} (hp : 1 â‰¤ p) (h2p : p < finrank â„ E) {s : Set E}
    (hs : IsCompact s) :
    âˆƒ C : â„â‰¥0, âˆ€ (u : E â†’ F') (hu : ContDiff â„ 1 u) (h2u : u.support âŠ† s),
    snorm u p Î¼ â‰¤ C * snorm (fderiv â„ u) p Î¼ := by
  refine snorm_le_snorm_fderiv_of_le Î¼ F' hp hp h2p ?_ hs
  norm_cast
  simp only [tsub_le_iff_right, le_add_iff_nonneg_right]
  positivity
