/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.DivergenceTheorem
import Mathlib.Analysis.BoxIntegral.Integrability
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# Divergence theorem for Bochner integral

In this file we prove the Divergence theorem for Bochner integral on a box in
`â„â¿âºÂ¹ = Fin (n + 1) â†’ â„`. More precisely, we prove the following theorem.

Let `E` be a complete normed space. If `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹` is
continuous on a rectangular box `[a, b] : Set â„â¿âºÂ¹`, `a â‰¤ b`, differentiable on its interior with
derivative `f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹`, and the divergence `fun x â†¦ âˆ‘ i, f' x eáµ¢ i`
is integrable on `[a, b]`, where `eáµ¢ = Pi.single i 1` is the `i`-th basis vector,
then its integral is equal to the sum of integrals of `f` over the faces of `[a, b]`,
taken with appropriate signs. Moreover, the same
is true if the function is not differentiable at countably many points of the interior of `[a, b]`.

Once we prove the general theorem, we deduce corollaries for functions `â„ â†’ E` and pairs of
functions `(â„ Ã— â„) â†’ E`.

## Notation

We use the following local notation to make the statement more readable. Note that the documentation
website shows the actual terms, not those abbreviated using local notations.

* `â„â¿`, `â„â¿âºÂ¹`, `Eâ¿âºÂ¹`: `Fin n â†’ â„`, `Fin (n + 1) â†’ â„`, `Fin (n + 1) â†’ E`;
* `face i`: the `i`-th face of the box `[a, b]` as a closed segment in `â„â¿`, namely
  `[a âˆ˜ Fin.succAbove i, b âˆ˜ Fin.succAbove i]`;
* `e i` : `i`-th basis vector `Pi.single i 1`;
* `frontFace i`, `backFace i`: embeddings `â„â¿ â†’ â„â¿âºÂ¹` corresponding to the front face
  `{x | x i = b i}` and back face `{x | x i = a i}` of the box `[a, b]`, respectively.
  They are given by `Fin.insertNth i (b i)` and `Fin.insertNth i (a i)`.

## TODO

* Add a version that assumes existence and integrability of partial derivatives.

## Tags

divergence theorem, Bochner integral
-/


open Set Finset TopologicalSpace Function BoxIntegral MeasureTheory Filter

open scoped Topology Interval

universe u

namespace MeasureTheory

variable {E : Type u} [NormedAddCommGroup E] [NormedSpace â„ E]

section

variable {n : â„•}

local macro:arg t:term:max noWs "â¿" : term => `(Fin n â†’ $t)

local macro:arg t:term:max noWs "â¿âºÂ¹" : term => `(Fin (n + 1) â†’ $t)

local notation "e " i => Pi.single i 1

section

/-!
### Divergence theorem for functions on `â„â¿âºÂ¹ = Fin (n + 1) â†’ â„`.

In this section we use the divergence theorem for a Henstock-Kurzweil-like integral
`BoxIntegral.hasIntegral_GP_divergence_of_forall_hasDerivWithinAt` to prove the divergence
theorem for Bochner integral. The divergence theorem for Bochner integral
`MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable` assumes that the function
itself is continuous on a closed box, differentiable at all but countably many points of its
interior, and the divergence is integrable on the box.

This statement differs from `BoxIntegral.hasIntegral_GP_divergence_of_forall_hasDerivWithinAt`
in several aspects.

* We use Bochner integral instead of a Henstock-Kurzweil integral. This modification is done in
  `MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable_auxâ‚`. As a side effect
  of this change, we need to assume that the divergence is integrable.

* We don't assume differentiability on the boundary of the box. This modification is done in
  `MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable_auxâ‚‚`. To prove it, we
  choose an increasing sequence of smaller boxes that cover the interior of the original box, then
  apply the previous lemma to these smaller boxes and take the limit of both sides of the equation.

* We assume `a â‰¤ b` instead of `âˆ€ i, a i < b i`. This is the last step of the proof, and it is done
  in the main theorem `MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable`.
-/

/-- An auxiliary lemma for
`MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable`. This is exactly
`BoxIntegral.hasIntegral_GP_divergence_of_forall_hasDerivWithinAt` reformulated for the
Bochner integral. -/
private theorem integral_divergence_of_hasFDerivWithinAt_off_countable_auxâ‚ (I : Box (Fin (n + 1)))
    (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹)
    (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹) (s : Set â„â¿âºÂ¹)
    (hs : s.Countable) (Hc : ContinuousOn f (Box.Icc I))
    (Hd : âˆ€ x âˆˆ (Box.Icc I) \ s, HasFDerivWithinAt f (f' x) (Box.Icc I) x)
    (Hi : IntegrableOn (fun x => âˆ‘ i, f' x (e i) i) (Box.Icc I)) :
    (âˆ« x in Box.Icc I, âˆ‘ i, f' x (e i) i) =
      âˆ‘ i : Fin (n + 1),
        ((âˆ« x in Box.Icc (I.face i), f (i.insertNth (I.upper i) x) i) -
          âˆ« x in Box.Icc (I.face i), f (i.insertNth (I.lower i) x) i) := by
  wlog hE : CompleteSpace E generalizing
  Â· simp [integral, hE]
  simp only [â† setIntegral_congr_set (Box.coe_ae_eq_Icc _)]
  have A := (Hi.mono_set Box.coe_subset_Icc).hasBoxIntegral âŠ¥ rfl
  have B :=
    hasIntegral_GP_divergence_of_forall_hasDerivWithinAt I f f' (s âˆ© Box.Icc I)
      (hs.mono inter_subset_left) (fun x hx => Hc _ hx.2) fun x hx =>
      Hd _ âŸ¨hx.1, fun h => hx.2 âŸ¨h, hx.1âŸ©âŸ©
  rw [continuousOn_pi] at Hc
  refine (A.unique B).trans (sum_congr rfl fun i _ => ?_)
  refine congr_argâ‚‚ Sub.sub ?_ ?_
  Â· have := Box.continuousOn_face_Icc (Hc i) (Set.right_mem_Icc.2 (I.lower_le_upper i))
    have := (this.integrableOn_compact (Î¼ := volume) (Box.isCompact_Icc _)).mono_set
      Box.coe_subset_Icc
    exact (this.hasBoxIntegral âŠ¥ rfl).integral_eq
  Â· have := Box.continuousOn_face_Icc (Hc i) (Set.left_mem_Icc.2 (I.lower_le_upper i))
    have := (this.integrableOn_compact (Î¼ := volume) (Box.isCompact_Icc _)).mono_set
      Box.coe_subset_Icc
    exact (this.hasBoxIntegral âŠ¥ rfl).integral_eq

/-- An auxiliary lemma for
`MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable`. Compared to the previous
lemma, here we drop the assumption of differentiability on the boundary of the box. -/
private theorem integral_divergence_of_hasFDerivAt_off_countable_auxâ‚‚ (I : Box (Fin (n + 1)))
    (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹)
    (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹)
    (s : Set â„â¿âºÂ¹) (hs : s.Countable) (Hc : ContinuousOn f (Box.Icc I))
    (Hd : âˆ€ x âˆˆ Box.Ioo I \ s, HasFDerivAt f (f' x) x)
    (Hi : IntegrableOn (âˆ‘ i, f' Â· (e i) i) (Box.Icc I)) :
    (âˆ« x in Box.Icc I, âˆ‘ i, f' x (e i) i) =
      âˆ‘ i : Fin (n + 1),
        ((âˆ« x in Box.Icc (I.face i), f (i.insertNth (I.upper i) x) i) -
          âˆ« x in Box.Icc (I.face i), f (i.insertNth (I.lower i) x) i) := by
  /- Choose a monotone sequence `J k` of subboxes that cover the interior of `I` and prove that
    these boxes satisfy the assumptions of the previous lemma. -/
  rcases I.exists_seq_mono_tendsto with âŸ¨J, hJ_sub, hJl, hJuâŸ©
  have hJ_sub' : âˆ€ k, Box.Icc (J k) âŠ† Box.Icc I := fun k => (hJ_sub k).trans I.Ioo_subset_Icc
  have hJ_le : âˆ€ k, J k â‰¤ I := fun k => Box.le_iff_Icc.2 (hJ_sub' k)
  have HcJ : âˆ€ k, ContinuousOn f (Box.Icc (J k)) := fun k => Hc.mono (hJ_sub' k)
  have HdJ : âˆ€ (k), âˆ€ x âˆˆ (Box.Icc (J k)) \ s, HasFDerivWithinAt f (f' x) (Box.Icc (J k)) x :=
    fun k x hx => (Hd x âŸ¨hJ_sub k hx.1, hx.2âŸ©).hasFDerivWithinAt
  have HiJ : âˆ€ k, IntegrableOn (âˆ‘ i, f' Â· (e i) i) (Box.Icc (J k)) volume := fun k =>
    Hi.mono_set (hJ_sub' k)
  -- Apply the previous lemma to `J k`.
  have HJ_eq := fun k =>
    integral_divergence_of_hasFDerivWithinAt_off_countable_auxâ‚ (J k) f f' s hs (HcJ k) (HdJ k)
      (HiJ k)
  -- Note that the LHS of `HJ_eq k` tends to the LHS of the goal as `k â†’ âˆ`.
  have hI_tendsto :
    Tendsto (fun k => âˆ« x in Box.Icc (J k), âˆ‘ i, f' x (e i) i) atTop
      (ğ“ (âˆ« x in Box.Icc I, âˆ‘ i, f' x (e i) i)) := by
    simp only [IntegrableOn, â† Measure.restrict_congr_set (Box.Ioo_ae_eq_Icc _)] at Hi âŠ¢
    rw [â† Box.iUnion_Ioo_of_tendsto J.monotone hJl hJu] at Hi âŠ¢
    exact tendsto_setIntegral_of_monotone (fun k => (J k).measurableSet_Ioo)
      (Box.Ioo.comp J).monotone Hi
  -- Thus it suffices to prove the same about the RHS.
  refine tendsto_nhds_unique_of_eventuallyEq hI_tendsto ?_ (Eventually.of_forall HJ_eq)
  clear hI_tendsto
  rw [tendsto_pi_nhds] at hJl hJu
  /- We'll need to prove a similar statement about the integrals over the front sides and the
    integrals over the back sides. In order to avoid repeating ourselves, we formulate a lemma. -/
  suffices âˆ€ (i : Fin (n + 1)) (c : â„• â†’ â„) (d), (âˆ€ k, c k âˆˆ Icc (I.lower i) (I.upper i)) â†’
    Tendsto c atTop (ğ“ d) â†’
      Tendsto (fun k => âˆ« x in Box.Icc ((J k).face i), f (i.insertNth (c k) x) i) atTop
        (ğ“ <| âˆ« x in Box.Icc (I.face i), f (i.insertNth d x) i) by
    rw [Box.Icc_eq_pi] at hJ_sub'
    refine tendsto_finset_sum _ fun i _ => (this _ _ _ ?_ (hJu _)).sub (this _ _ _ ?_ (hJl _))
    exacts [fun k => hJ_sub' k (J k).upper_mem_Icc _ trivial, fun k =>
      hJ_sub' k (J k).lower_mem_Icc _ trivial]
  intro i c d hc hcd
  /- First we prove that the integrals of the restriction of `f` to `{x | x i = d}` over increasing
    boxes `((J k).face i).Icc` tend to the desired limit. The proof mostly repeats the one above. -/
  have hd : d âˆˆ Icc (I.lower i) (I.upper i) :=
    isClosed_Icc.mem_of_tendsto hcd (Eventually.of_forall hc)
  have Hic : âˆ€ k, IntegrableOn (fun x => f (i.insertNth (c k) x) i) (Box.Icc (I.face i)) := fun k =>
    (Box.continuousOn_face_Icc ((continuous_apply i).comp_continuousOn Hc) (hc k)).integrableOn_Icc
  have Hid : IntegrableOn (fun x => f (i.insertNth d x) i) (Box.Icc (I.face i)) :=
    (Box.continuousOn_face_Icc ((continuous_apply i).comp_continuousOn Hc) hd).integrableOn_Icc
  have H :
    Tendsto (fun k => âˆ« x in Box.Icc ((J k).face i), f (i.insertNth d x) i) atTop
      (ğ“ <| âˆ« x in Box.Icc (I.face i), f (i.insertNth d x) i) := by
    have hIoo : (â‹ƒ k, Box.Ioo ((J k).face i)) = Box.Ioo (I.face i) :=
      Box.iUnion_Ioo_of_tendsto ((Box.monotone_face i).comp J.monotone)
        (tendsto_pi_nhds.2 fun _ => hJl _) (tendsto_pi_nhds.2 fun _ => hJu _)
    simp only [IntegrableOn, â† Measure.restrict_congr_set (Box.Ioo_ae_eq_Icc _), â† hIoo] at Hid âŠ¢
    exact tendsto_setIntegral_of_monotone (fun k => ((J k).face i).measurableSet_Ioo)
      (Box.Ioo.monotone.comp ((Box.monotone_face i).comp J.monotone)) Hid
  /- Thus it suffices to show that the distance between the integrals of the restrictions of `f` to
    `{x | x i = c k}` and `{x | x i = d}` over `((J k).face i).Icc` tends to zero as `k â†’ âˆ`. Choose
    `Îµ > 0`. -/
  refine H.congr_dist (Metric.nhds_basis_closedBall.tendsto_right_iff.2 fun Îµ Îµpos => ?_)
  have hvol_pos : âˆ€ J : Box (Fin n), 0 < âˆ j, (J.upper j - J.lower j) := fun J =>
    prod_pos fun j hj => sub_pos.2 <| J.lower_lt_upper _
  /- Choose `Î´ > 0` such that for any `x y âˆˆ I.Icc` at distance at most `Î´`, the distance between
    `f x` and `f y` is at most `Îµ / volume (I.face i).Icc`, then the distance between the integrals
    is at most `(Îµ / volume (I.face i).Icc) * volume ((J k).face i).Icc â‰¤ Îµ`. -/
  rcases Metric.uniformContinuousOn_iff_le.1 (I.isCompact_Icc.uniformContinuousOn_of_continuous Hc)
      (Îµ / âˆ j, ((I.face i).upper j - (I.face i).lower j)) (div_pos Îµpos (hvol_pos (I.face i)))
    with âŸ¨Î´, Î´pos, hÎ´âŸ©
  refine (hcd.eventually (Metric.ball_mem_nhds _ Î´pos)).mono fun k hk => ?_
  have Hsub : Box.Icc ((J k).face i) âŠ† Box.Icc (I.face i) :=
    Box.le_iff_Icc.1 (Box.face_mono (hJ_le _) i)
  rw [mem_closedBall_zero_iff, Real.norm_eq_abs, abs_of_nonneg dist_nonneg, dist_eq_norm,
    â† integral_sub (Hid.mono_set Hsub) ((Hic _).mono_set Hsub)]
  calc
    â€–âˆ« x in Box.Icc ((J k).face i), f (i.insertNth d x) i - f (i.insertNth (c k) x) iâ€– â‰¤
        (Îµ / âˆ j, ((I.face i).upper j - (I.face i).lower j)) *
          (volume (Box.Icc ((J k).face i))).toReal := by
      refine norm_setIntegral_le_of_norm_le_const (((J k).face i).measure_Icc_lt_top _)
        fun x hx => ?_
      rw [â† dist_eq_norm]
      calc
        dist (f (i.insertNth d x) i) (f (i.insertNth (c k) x) i) â‰¤
            dist (f (i.insertNth d x)) (f (i.insertNth (c k) x)) :=
          dist_le_pi_dist (f (i.insertNth d x)) (f (i.insertNth (c k) x)) i
        _ â‰¤ Îµ / âˆ j, ((I.face i).upper j - (I.face i).lower j) :=
          hÎ´ _ (I.mapsTo_insertNth_face_Icc hd <| Hsub hx) _
            (I.mapsTo_insertNth_face_Icc (hc _) <| Hsub hx) ?_
      rw [Fin.dist_insertNth_insertNth, dist_self, dist_comm]
      exact max_le hk.le Î´pos.lt.le
    _ â‰¤ Îµ := by
      rw [Box.Icc_def, Real.volume_Icc_pi_toReal ((J k).face i).lower_le_upper,
        â† le_div_iffâ‚€ (hvol_pos _)]
      gcongr
      exacts [hvol_pos _, fun _ _ â†¦ sub_nonneg.2 (Box.lower_le_upper _ _),
        (hJ_sub' _ (J _).upper_mem_Icc).2 _, (hJ_sub' _ (J _).lower_mem_Icc).1 _]

variable (a b : Fin (n + 1) â†’ â„)

local notation "face " i => Set.Icc (a âˆ˜ Fin.succAbove i) (b âˆ˜ Fin.succAbove i)
local notation:max "frontFace " i:arg => Fin.insertNth i (b i)
local notation:max "backFace " i:arg => Fin.insertNth i (a i)

/-- **Divergence theorem** for Bochner integral. If `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹` is continuous on a rectangular
box `[a, b] : Set â„â¿âºÂ¹`, `a â‰¤ b`, is differentiable on its interior with derivative
`f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹` and the divergence `fun x â†¦ âˆ‘ i, f' x eáµ¢ i` is integrable on `[a, b]`,
where `eáµ¢ = Pi.single i 1` is the `i`-th basis vector, then its integral is equal to the sum of
integrals of `f` over the faces of `[a, b]`, taken with appropriate signs.

Moreover, the same is true if the function is not differentiable at countably many
points of the interior of `[a, b]`.

We represent both faces `x i = a i` and `x i = b i` as the box
`face i = [a âˆ˜ Fin.succAbove i, b âˆ˜ Fin.succAbove i]` in `â„â¿`, where
`Fin.succAbove : Fin n â†ªo Fin (n + 1)` is the order embedding with range `{i}á¶œ`. The restrictions
of `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹` to these faces are given by `f âˆ˜ backFace i` and `f âˆ˜ frontFace i`, where
`backFace i = Fin.insertNth i (a i)` and `frontFace i = Fin.insertNth i (b i)` are embeddings
`â„â¿ â†’ â„â¿âºÂ¹` that take `y : â„â¿` and insert `a i` (resp., `b i`) as `i`-th coordinate. -/
theorem integral_divergence_of_hasFDerivAt_off_countable (hle : a â‰¤ b)
    (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹) (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹)
    (s : Set â„â¿âºÂ¹) (hs : s.Countable) (Hc : ContinuousOn f (Icc a b))
    (Hd : âˆ€ x âˆˆ (Set.pi univ fun i => Ioo (a i) (b i)) \ s, HasFDerivAt f (f' x) x)
    (Hi : IntegrableOn (fun x => âˆ‘ i, f' x (e i) i) (Icc a b)) :
    (âˆ« x in Icc a b, âˆ‘ i, f' x (e i) i) =
      âˆ‘ i : Fin (n + 1),
        ((âˆ« x in face i, f (frontFace i x) i) - âˆ« x in face i, f (backFace i x) i) := by
  rcases em (âˆƒ i, a i = b i) with (âŸ¨i, hiâŸ© | hne)
  Â· -- First we sort out the trivial case `âˆƒ i, a i = b i`.
    rw [volume_pi, â† setIntegral_congr_set Measure.univ_pi_Ioc_ae_eq_Icc]
    have hi' : Ioc (a i) (b i) = âˆ… := Ioc_eq_empty hi.not_lt
    have : (pi Set.univ fun j => Ioc (a j) (b j)) = âˆ… := univ_pi_eq_empty hi'
    rw [this, setIntegral_empty, sum_eq_zero]
    rintro j -
    rcases eq_or_ne i j with (rfl | hne)
    Â· simp [hi]
    Â· rcases Fin.exists_succAbove_eq hne with âŸ¨i, rflâŸ©
      have : Icc (a âˆ˜ j.succAbove) (b âˆ˜ j.succAbove) =áµ[volume] (âˆ… : Set â„â¿) := by
        rw [ae_eq_empty, Real.volume_Icc_pi, prod_eq_zero (Finset.mem_univ i)]
        simp [hi]
      rw [setIntegral_congr_set this, setIntegral_congr_set this, setIntegral_empty,
        setIntegral_empty, sub_self]
  Â· -- In the non-trivial case `âˆ€ i, a i < b i`, we apply a lemma we proved above.
    have hlt : âˆ€ i, a i < b i := fun i => (hle i).lt_of_ne fun hi => hne âŸ¨i, hiâŸ©
    exact integral_divergence_of_hasFDerivAt_off_countable_auxâ‚‚ âŸ¨a, b, hltâŸ© f f' s hs Hc Hd Hi

@[deprecated (since := "2025-05-02")]
alias integral_divergence_of_hasFDerivWithinAt_off_countable :=
  integral_divergence_of_hasFDerivAt_off_countable

/-- **Divergence theorem** for a family of functions `f : Fin (n + 1) â†’ â„â¿âºÂ¹ â†’ E`. See also
`MeasureTheory.integral_divergence_of_hasFDerivWithinAt_off_countable'` for a version formulated
in terms of a vector-valued function `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹`. -/
theorem integral_divergence_of_hasFDerivAt_off_countable' (hle : a â‰¤ b)
    (f : Fin (n + 1) â†’ â„â¿âºÂ¹ â†’ E)
    (f' : Fin (n + 1) â†’ â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] E) (s : Set â„â¿âºÂ¹)
    (hs : s.Countable) (Hc : âˆ€ i, ContinuousOn (f i) (Icc a b))
    (Hd : âˆ€ x âˆˆ (pi Set.univ fun i => Ioo (a i) (b i)) \ s, âˆ€ (i), HasFDerivAt (f i) (f' i x) x)
    (Hi : IntegrableOn (fun x => âˆ‘ i, f' i x (e i)) (Icc a b)) :
    (âˆ« x in Icc a b, âˆ‘ i, f' i x (e i)) =
      âˆ‘ i : Fin (n + 1), ((âˆ« x in face i, f i (frontFace i x)) -
        âˆ« x in face i, f i (backFace i x)) :=
  integral_divergence_of_hasFDerivAt_off_countable a b hle (fun x i => f i x)
    (fun x => ContinuousLinearMap.pi fun i => f' i x) s hs (continuousOn_pi.2 Hc)
    (fun x hx => hasFDerivAt_pi.2 (Hd x hx)) Hi

@[deprecated (since := "2025-05-02")]
alias integral_divergence_of_hasFDerivWithinAt_off_countable' :=
  integral_divergence_of_hasFDerivAt_off_countable'

end

/-- An auxiliary lemma that is used to specialize the general divergence theorem to spaces that do
not have the form `Fin n â†’ â„`. -/
theorem integral_divergence_of_hasFDerivAt_off_countable_of_equiv {F : Type*}
    [NormedAddCommGroup F] [NormedSpace â„ F] [Preorder F] [MeasureSpace F] [BorelSpace F]
    (eL : F â‰ƒL[â„] â„â¿âºÂ¹) (he_ord : âˆ€ x y, eL x â‰¤ eL y â†” x â‰¤ y)
    (he_vol : MeasurePreserving eL volume volume) (f : Fin (n + 1) â†’ F â†’ E)
    (f' : Fin (n + 1) â†’ F â†’ F â†’L[â„] E) (s : Set F) (hs : s.Countable) (a b : F) (hle : a â‰¤ b)
    (Hc : âˆ€ i, ContinuousOn (f i) (Icc a b))
    (Hd : âˆ€ x âˆˆ interior (Icc a b) \ s, âˆ€ (i), HasFDerivAt (f i) (f' i x) x) (DF : F â†’ E)
    (hDF : âˆ€ x, DF x = âˆ‘ i, f' i x (eL.symm <| e i)) (Hi : IntegrableOn DF (Icc a b)) :
    âˆ« x in Icc a b, DF x =
      âˆ‘ i : Fin (n + 1),
        ((âˆ« x in Icc (eL a âˆ˜ i.succAbove) (eL b âˆ˜ i.succAbove),
            f i (eL.symm <| i.insertNth (eL b i) x)) -
          âˆ« x in Icc (eL a âˆ˜ i.succAbove) (eL b âˆ˜ i.succAbove),
            f i (eL.symm <| i.insertNth (eL a i) x)) :=
  have he_emb : MeasurableEmbedding eL := eL.toHomeomorph.measurableEmbedding
  have hIcc : eL â»Â¹' Icc (eL a) (eL b) = Icc a b := by
    ext1 x; simp only [Set.mem_preimage, Set.mem_Icc, he_ord]
  have hIcc' : Icc (eL a) (eL b) = eL.symm â»Â¹' Icc a b := by rw [â† hIcc, eL.symm_preimage_preimage]
  calc
    âˆ« x in Icc a b, DF x = âˆ« x in Icc a b, âˆ‘ i, f' i x (eL.symm <| e i) := by simp only [hDF]
    _ = âˆ« x in Icc (eL a) (eL b), âˆ‘ i, f' i (eL.symm x) (eL.symm <| e i) := by
      rw [â† he_vol.setIntegral_preimage_emb he_emb]
      simp only [hIcc, eL.symm_apply_apply]
    _ = âˆ‘ i : Fin (n + 1),
          ((âˆ« x in Icc (eL a âˆ˜ i.succAbove) (eL b âˆ˜ i.succAbove),
              f i (eL.symm <| i.insertNth (eL b i) x)) -
            âˆ« x in Icc (eL a âˆ˜ i.succAbove) (eL b âˆ˜ i.succAbove),
              f i (eL.symm <| i.insertNth (eL a i) x)) := by
      refine integral_divergence_of_hasFDerivAt_off_countable' (eL a) (eL b)
        ((he_ord _ _).2 hle) (fun i x => f i (eL.symm x))
        (fun i x => f' i (eL.symm x) âˆ˜L (eL.symm : â„â¿âºÂ¹ â†’L[â„] F)) (eL.symm â»Â¹' s)
        (hs.preimage eL.symm.injective) ?_ ?_ ?_
      Â· exact fun i => (Hc i).comp eL.symm.continuousOn hIcc'.subset
      Â· refine fun x hx i => (Hd (eL.symm x) âŸ¨?_, hx.2âŸ© i).comp x eL.symm.hasFDerivAt
        rw [â† hIcc]
        refine preimage_interior_subset_interior_preimage eL.continuous ?_
        simpa only [Set.mem_preimage, eL.apply_symm_apply, â† pi_univ_Icc,
          interior_pi_set (@finite_univ (Fin _) _), interior_Icc] using hx.1
      Â· rw [â† he_vol.integrableOn_comp_preimage he_emb, hIcc]
        simp [â† hDF, Function.comp_def, Hi]

@[deprecated (since := "2025-05-02")]
alias integral_divergence_of_hasFDerivWithinAt_off_countable_of_equiv :=
  integral_divergence_of_hasFDerivAt_off_countable_of_equiv

end

open scoped Interval

open ContinuousLinearMap (smulRight)


local macro:arg t:term:max noWs "Â¹" : term => `(Fin 1 â†’ $t)
local macro:arg t:term:max noWs "Â²" : term => `(Fin 2 â†’ $t)

/-- **Fundamental theorem of calculus, part 2**. This version assumes that `f` is continuous on the
interval and is differentiable off a countable set `s`.

See also

* `intervalIntegral.integral_eq_sub_of_hasDeriv_right_of_le` for a version that only assumes right
  differentiability of `f`;
* `MeasureTheory.integral_eq_of_hasDerivWithinAt_off_countable` for a version that works both
  for `a â‰¤ b` and `b â‰¤ a` at the expense of using unordered intervals instead of `Set.Icc`. -/
theorem integral_eq_of_hasDerivAt_off_countable_of_le [CompleteSpace E] (f f' : â„ â†’ E)
    {a b : â„} (hle : a â‰¤ b) {s : Set â„} (hs : s.Countable) (Hc : ContinuousOn f (Icc a b))
    (Hd : âˆ€ x âˆˆ Ioo a b \ s, HasDerivAt f (f' x) x) (Hi : IntervalIntegrable f' volume a b) :
    âˆ« x in a..b, f' x = f b - f a := by
  set e : â„ â‰ƒL[â„] â„Â¹ := (ContinuousLinearEquiv.funUnique (Fin 1) â„ â„).symm
  have e_symm : âˆ€ x, e.symm x = x 0 := fun x => rfl
  set F' : â„ â†’ â„ â†’L[â„] E := fun x => smulRight (1 : â„ â†’L[â„] â„) (f' x)
  have hF' : âˆ€ x y, F' x y = y â€¢ f' x := fun x y => rfl
  calc
    âˆ« x in a..b, f' x = âˆ« x in Icc a b, f' x := by
      rw [intervalIntegral.integral_of_le hle, setIntegral_congr_set Ioc_ae_eq_Icc]
    _ = âˆ‘ i : Fin 1,
          ((âˆ« x in Icc (e a âˆ˜ i.succAbove) (e b âˆ˜ i.succAbove),
              f (e.symm <| i.insertNth (e b i) x)) -
            âˆ« x in Icc (e a âˆ˜ i.succAbove) (e b âˆ˜ i.succAbove),
              f (e.symm <| i.insertNth (e a i) x)) := by
      simp only [â† interior_Icc] at Hd
      refine
        integral_divergence_of_hasFDerivAt_off_countable_of_equiv e ?_ ?_ (fun _ => f)
          (fun _ => F') s hs a b hle (fun _ => Hc) (fun x hx _ => Hd x hx) _ ?_ ?_
      Â· exact fun x y => (OrderIso.funUnique (Fin 1) â„).symm.le_iff_le
      Â· exact (volume_preserving_funUnique (Fin 1) â„).symm _
      Â· intro x; rw [Fin.sum_univ_one, hF', e_symm, Pi.single_eq_same, one_smul]
      Â· rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hle] at Hi
        exact Hi.congr_set_ae Ioc_ae_eq_Icc.symm
    _ = f b - f a := by
      simp [e, Subsingleton.elim (const (Fin 0) _) isEmptyElim, volume_pi,
        Measure.pi_of_empty fun _ : Fin 0 â†¦ _]

@[deprecated (since := "2025-05-02")]
alias integral_eq_of_hasDerivWithinAt_off_countable_of_le :=
  integral_eq_of_hasDerivAt_off_countable_of_le

/-- **Fundamental theorem of calculus, part 2**. This version assumes that `f` is continuous on the
interval and is differentiable off a countable set `s`.

See also `intervalIntegral.integral_eq_sub_of_hasDeriv_right` for a version that
only assumes right differentiability of `f`.
-/
theorem integral_eq_of_hasDerivAt_off_countable [CompleteSpace E] (f f' : â„ â†’ E) {a b : â„}
    {s : Set â„} (hs : s.Countable) (Hc : ContinuousOn f [[a, b]])
    (Hd : âˆ€ x âˆˆ Ioo (min a b) (max a b) \ s, HasDerivAt f (f' x) x)
    (Hi : IntervalIntegrable f' volume a b) : âˆ« x in a..b, f' x = f b - f a := by
  rcases le_total a b with hab | hab
  Â· simp only [uIcc_of_le hab, min_eq_left hab, max_eq_right hab] at *
    exact integral_eq_of_hasDerivAt_off_countable_of_le f f' hab hs Hc Hd Hi
  Â· simp only [uIcc_of_ge hab, min_eq_right hab, max_eq_left hab] at *
    rw [intervalIntegral.integral_symm, neg_eq_iff_eq_neg, neg_sub]
    exact integral_eq_of_hasDerivAt_off_countable_of_le f f' hab hs Hc Hd Hi.symm

@[deprecated (since := "2025-05-02")]
alias integral_eq_of_hasDerivWithinAt_off_countable := integral_eq_of_hasDerivAt_off_countable

/-- **Divergence theorem** for functions on the plane along rectangles. It is formulated in terms of
two functions `f g : â„ Ã— â„ â†’ E` and an integral over `Icc a b = [a.1, b.1] Ã— [a.2, b.2]`, where
`a b : â„ Ã— â„`, `a â‰¤ b`. When thinking of `f` and `g` as the two coordinates of a single function
`F : â„ Ã— â„ â†’ E Ã— E` and when `E = â„`, this is the usual statement that the integral of the
divergence of `F` inside the rectangle equals the integral of the normal derivative of `F` along the
boundary.

See also `MeasureTheory.integral2_divergence_prod_of_hasFDerivAt_off_countable` for a
version that does not assume `a â‰¤ b` and uses iterated interval integral instead of the integral
over `Icc a b`. -/
theorem integral_divergence_prod_Icc_of_hasFDerivAt_off_countable_of_le (f g : â„ Ã— â„ â†’ E)
    (f' g' : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] E) (a b : â„ Ã— â„) (hle : a â‰¤ b) (s : Set (â„ Ã— â„)) (hs : s.Countable)
    (Hcf : ContinuousOn f (Icc a b)) (Hcg : ContinuousOn g (Icc a b))
    (Hdf : âˆ€ x âˆˆ Ioo a.1 b.1 Ã—Ë¢ Ioo a.2 b.2 \ s, HasFDerivAt f (f' x) x)
    (Hdg : âˆ€ x âˆˆ Ioo a.1 b.1 Ã—Ë¢ Ioo a.2 b.2 \ s, HasFDerivAt g (g' x) x)
    (Hi : IntegrableOn (fun x => f' x (1, 0) + g' x (0, 1)) (Icc a b)) :
    (âˆ« x in Icc a b, f' x (1, 0) + g' x (0, 1)) =
      (((âˆ« x in a.1..b.1, g (x, b.2)) - âˆ« x in a.1..b.1, g (x, a.2)) +
          âˆ« y in a.2..b.2, f (b.1, y)) -
        âˆ« y in a.2..b.2, f (a.1, y) :=
  let e : (â„ Ã— â„) â‰ƒL[â„] â„Â² := (ContinuousLinearEquiv.finTwoArrow â„ â„).symm
  calc
    (âˆ« x in Icc a b, f' x (1, 0) + g' x (0, 1)) =
        âˆ‘ i : Fin 2,
          ((âˆ« x in Icc (e a âˆ˜ i.succAbove) (e b âˆ˜ i.succAbove),
              ![f, g] i (e.symm <| i.insertNth (e b i) x)) -
            âˆ« x in Icc (e a âˆ˜ i.succAbove) (e b âˆ˜ i.succAbove),
              ![f, g] i (e.symm <| i.insertNth (e a i) x)) := by
      refine integral_divergence_of_hasFDerivAt_off_countable_of_equiv e ?_ ?_ ![f, g]
        ![f', g'] s hs a b hle ?_ (fun x hx => ?_) _ ?_ Hi
      Â· exact fun x y => (OrderIso.finTwoArrowIso â„).symm.le_iff_le
      Â· exact (volume_preserving_finTwoArrow â„).symm _
      Â· exact Fin.forall_fin_two.2 âŸ¨Hcf, HcgâŸ©
      Â· rw [Icc_prod_eq, interior_prod_eq, interior_Icc, interior_Icc] at hx
        exact Fin.forall_fin_two.2 âŸ¨Hdf x hx, Hdg x hxâŸ©
      Â· intro x; rw [Fin.sum_univ_two]; rfl
    _ = ((âˆ« y in Icc a.2 b.2, f (b.1, y)) - âˆ« y in Icc a.2 b.2, f (a.1, y)) +
          ((âˆ« x in Icc a.1 b.1, g (x, b.2)) - âˆ« x in Icc a.1 b.1, g (x, a.2)) := by
      have : âˆ€ (a b : â„Â¹) (f : â„Â¹ â†’ E),
          âˆ« x in Icc a b, f x = âˆ« x in Icc (a 0) (b 0), f fun _ => x := fun a b f â†¦ by
        convert (((volume_preserving_funUnique (Fin 1) â„).symm _).setIntegral_preimage_emb
          (MeasurableEquiv.measurableEmbedding _) f _).symm
        exact ((OrderIso.funUnique (Fin 1) â„).symm.preimage_Icc a b).symm
      simp only [Fin.sum_univ_two, this]
      rfl
    _ = (((âˆ« x in a.1..b.1, g (x, b.2)) - âˆ« x in a.1..b.1, g (x, a.2)) +
            âˆ« y in a.2..b.2, f (b.1, y)) - âˆ« y in a.2..b.2, f (a.1, y) := by
      simp only [intervalIntegral.integral_of_le hle.1, intervalIntegral.integral_of_le hle.2,
        setIntegral_congr_set (Ioc_ae_eq_Icc (Î± := â„) (Î¼ := volume))]
      abel

@[deprecated (since := "2025-05-02")]
alias integral_divergence_prod_Icc_of_hasFDerivWithinAt_off_countable_of_le :=
  integral_divergence_prod_Icc_of_hasFDerivAt_off_countable_of_le

/-- **Divergence theorem** for functions on the plane. It is formulated in terms of two functions
`f g : â„ Ã— â„ â†’ E` and iterated integral `âˆ« x in aâ‚..bâ‚, âˆ« y in aâ‚‚..bâ‚‚, _`, where
`aâ‚ aâ‚‚ bâ‚ bâ‚‚ : â„`. When thinking of `f` and `g` as the two coordinates of a single function
`F : â„ Ã— â„ â†’ E Ã— E` and when `E = â„`, this is the usual statement that the integral of the
divergence of `F` inside the rectangle with vertices `(aáµ¢, bâ±¼)`, `i, j =1,2`, equals the integral of
the normal derivative of `F` along the boundary.

See also `MeasureTheory.integral_divergence_prod_Icc_of_hasFDerivAt_off_countable_of_le`
for a version that uses an integral over `Icc a b`, where `a b : â„ Ã— â„`, `a â‰¤ b`. -/
theorem integral2_divergence_prod_of_hasFDerivAt_off_countable (f g : â„ Ã— â„ â†’ E)
    (f' g' : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] E) (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : â„) (s : Set (â„ Ã— â„)) (hs : s.Countable)
    (Hcf : ContinuousOn f ([[aâ‚, bâ‚]] Ã—Ë¢ [[aâ‚‚, bâ‚‚]]))
    (Hcg : ContinuousOn g ([[aâ‚, bâ‚]] Ã—Ë¢ [[aâ‚‚, bâ‚‚]]))
    (Hdf : âˆ€ x âˆˆ Ioo (min aâ‚ bâ‚) (max aâ‚ bâ‚) Ã—Ë¢ Ioo (min aâ‚‚ bâ‚‚) (max aâ‚‚ bâ‚‚) \ s,
      HasFDerivAt f (f' x) x)
    (Hdg : âˆ€ x âˆˆ Ioo (min aâ‚ bâ‚) (max aâ‚ bâ‚) Ã—Ë¢ Ioo (min aâ‚‚ bâ‚‚) (max aâ‚‚ bâ‚‚) \ s,
      HasFDerivAt g (g' x) x)
    (Hi : IntegrableOn (fun x => f' x (1, 0) + g' x (0, 1)) ([[aâ‚, bâ‚]] Ã—Ë¢ [[aâ‚‚, bâ‚‚]])) :
    (âˆ« x in aâ‚..bâ‚, âˆ« y in aâ‚‚..bâ‚‚, f' (x, y) (1, 0) + g' (x, y) (0, 1)) =
      (((âˆ« x in aâ‚..bâ‚, g (x, bâ‚‚)) - âˆ« x in aâ‚..bâ‚, g (x, aâ‚‚)) + âˆ« y in aâ‚‚..bâ‚‚, f (bâ‚, y)) -
        âˆ« y in aâ‚‚..bâ‚‚, f (aâ‚, y) := by
  wlog hâ‚ : aâ‚ â‰¤ bâ‚ generalizing aâ‚ bâ‚
  Â· specialize this bâ‚ aâ‚
    rw [uIcc_comm bâ‚ aâ‚, min_comm bâ‚ aâ‚, max_comm bâ‚ aâ‚] at this
    simp only [intervalIntegral.integral_symm bâ‚ aâ‚]
    refine (congr_arg Neg.neg (this Hcf Hcg Hdf Hdg Hi (le_of_not_ge hâ‚))).trans ?_; abel
  wlog hâ‚‚ : aâ‚‚ â‰¤ bâ‚‚ generalizing aâ‚‚ bâ‚‚
  Â· specialize this bâ‚‚ aâ‚‚
    rw [uIcc_comm bâ‚‚ aâ‚‚, min_comm bâ‚‚ aâ‚‚, max_comm bâ‚‚ aâ‚‚] at this
    simp only [intervalIntegral.integral_symm bâ‚‚ aâ‚‚, intervalIntegral.integral_neg]
    refine (congr_arg Neg.neg (this Hcf Hcg Hdf Hdg Hi (le_of_not_ge hâ‚‚))).trans ?_; abel
  simp only [uIcc_of_le hâ‚, uIcc_of_le hâ‚‚, min_eq_left, max_eq_right, hâ‚, hâ‚‚] at Hcf Hcg Hdf Hdg Hi
  calc
    (âˆ« x in aâ‚..bâ‚, âˆ« y in aâ‚‚..bâ‚‚, f' (x, y) (1, 0) + g' (x, y) (0, 1)) =
        âˆ« x in Icc aâ‚ bâ‚, âˆ« y in Icc aâ‚‚ bâ‚‚, f' (x, y) (1, 0) + g' (x, y) (0, 1) := by
      simp only [intervalIntegral.integral_of_le, hâ‚, hâ‚‚,
        setIntegral_congr_set (Ioc_ae_eq_Icc (Î± := â„) (Î¼ := volume))]
    _ = âˆ« x in Icc aâ‚ bâ‚ Ã—Ë¢ Icc aâ‚‚ bâ‚‚, f' x (1, 0) + g' x (0, 1) := (setIntegral_prod _ Hi).symm
    _ = (((âˆ« x in aâ‚..bâ‚, g (x, bâ‚‚)) - âˆ« x in aâ‚..bâ‚, g (x, aâ‚‚)) + âˆ« y in aâ‚‚..bâ‚‚, f (bâ‚, y)) -
          âˆ« y in aâ‚‚..bâ‚‚, f (aâ‚, y) := by
      rw [Icc_prod_Icc] at *
      apply integral_divergence_prod_Icc_of_hasFDerivAt_off_countable_of_le f g f' g'
        (aâ‚, aâ‚‚) (bâ‚, bâ‚‚) âŸ¨hâ‚, hâ‚‚âŸ© s <;> assumption

@[deprecated (since := "2025-05-02")]
alias integral2_divergence_prod_of_hasFDerivWithinAt_off_countable :=
  integral2_divergence_prod_of_hasFDerivAt_off_countable

end MeasureTheory
