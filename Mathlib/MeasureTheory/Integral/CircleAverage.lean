/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.MeasureTheory.Integral.CircleIntegral
import Mathlib.MeasureTheory.Integral.IntervalAverage
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Periodic

/-!
# Circle Averages

For a function `f` on the complex plane, this file introduces the definition
`Real.circleAverage f c R` as a shorthand for the average of `f` on the circle with center `c` and
radius `R`, equipped with the rotation-invariant measure of total volume one. Like
`IntervalAverage`, this notion exists as a convenience. It avoids notationally inconvenient
compositions of `f` with `circleMap` and avoids the need to manually elemininate `2 * Ï€` every time
an average is computed.

Note: Like the interval average defined in `Mathlib/MeasureTheory/Integral/IntervalAverage.lean`,
the `circleAverage` defined here is a purely measure-theoretic average. It should not be confused
with `circleIntegral`, which is the path integral over the circle path. The relevant integrability
property `circleAverage` is `CircleIntegrable`, as defined in
`Mathlib/MeasureTheory/Integral/CircleIntegral.lean`.

Implementation Note: Like `circleMap`, `circleAverage`s are defined for negative radii. The theorem
`circleAverage_congr_negRadius` shows that the average is independent of the radius' sign.
-/

open Filter Metric Real

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {ğ•œ : Type*} [NormedDivisionRing ğ•œ] [Module ğ•œ E] [NormSMulClass ğ•œ E] [SMulCommClass â„ ğ•œ E]
  {f fâ‚ fâ‚‚ : â„‚ â†’ E} {c : â„‚} {R : â„} {a : ğ•œ}

namespace Real

/-!
# Definition
-/

variable (f c R) in
/--
Define `circleAverage f c R` as the average value of `f` on the circle with center `c` and radius
`R`. This is a real notion, which should not be confused with the complex path integral notion
defined in `circleIntegral` (integrating with respect to `dz`).
-/
noncomputable def circleAverage : E :=
  (2 * Ï€)â»Â¹ â€¢ âˆ« Î¸ in (0)..2 * Ï€, f (circleMap c R Î¸)

lemma circleAverage_def :
    circleAverage f c R = (2 * Ï€)â»Â¹ â€¢ âˆ« Î¸ in (0)..2 * Ï€, f (circleMap c R Î¸) := rfl

/-- Expression of `circleAverageÂ´ in terms of interval averages. -/
lemma circleAverage_eq_intervalAverage :
    circleAverage f c R = â¨ Î¸ in (0)..2 * Ï€, f (circleMap c R Î¸) := by
  simp [circleAverage, interval_average_eq]

/-- Interval averages for zero radii equal values at the center point. -/
@[simp] lemma circleAverage_zero [CompleteSpace E] :
    circleAverage f c 0 = f c := by
  rw [circleAverage]
  simp only [circleMap_zero_radius, Function.const_apply,
    intervalIntegral.integral_const, sub_zero,
    â† smul_assoc, smul_eq_mul, inv_mul_cancelâ‚€ (mul_ne_zero two_ne_zero pi_ne_zero),
    one_smul]

/--
Expression of `circleAverageÂ´ with arbitrary center in terms of `circleAverage` with center zero.
-/
lemma circleAverage_fun_add :
    circleAverage (fun z â†¦ f (z + c)) 0 R = circleAverage f c R := by
  unfold circleAverage circleMap
  congr
  ext Î¸
  simp only [zero_add]
  congr 1
  ring

/-!
## Congruence Lemmata
-/

/-- Circle averages do not change when shifting the angle. -/
lemma circleAverage_eq_integral_add (Î· : â„) :
    circleAverage f c R = (2 * Ï€)â»Â¹ â€¢ âˆ« (Î¸ : â„) in (0)..2 * Ï€, f (circleMap c R (Î¸ + Î·)) := by
  rw [intervalIntegral.integral_comp_add_right (fun Î¸ â†¦ f (circleMap c R Î¸))]
  have tâ‚€ : (fun Î¸ â†¦ f (circleMap c R Î¸)).Periodic (2 * Ï€) :=
    fun x â†¦ by simp [periodic_circleMap c R x]
  have := tâ‚€.intervalIntegral_add_eq 0 Î·
  rw [zero_add, add_comm] at this
  rw [zero_add]
  simp only [circleAverage, mul_inv_rev]
  congr

/-- Circle averages do not change when replacing the radius by its negative. -/
@[simp] theorem circleAverage_neg_radius :
    circleAverage f c (-R) = circleAverage f c R := by
  unfold circleAverage
  simp_rw [circleMap_neg_radius, â† circleAverage_def, circleAverage_eq_integral_add Ï€]

/-- Circle averages do not change when replacing the radius by its absolute value. -/
@[simp] theorem circleAverage_abs_radius :
    circleAverage f c |R| = circleAverage f c R := by
  by_cases hR : 0 â‰¤ R
  Â· rw [abs_of_nonneg hR]
  Â· rw [abs_of_neg (not_le.1 hR), circleAverage_neg_radius]

/-- If two functions agree outside of a discrete set in the circle, then their averages agree. -/
theorem circleAverage_congr_codiscreteWithin
    (hf : fâ‚ =á¶ [codiscreteWithin (sphere c |R|)] fâ‚‚) (hR : R â‰  0) :
    circleAverage fâ‚ c R = circleAverage fâ‚‚ c R := by
  unfold circleAverage
  congr 1
  apply intervalIntegral.integral_congr_ae_restrict
  apply ae_restrict_le_codiscreteWithin measurableSet_uIoc
  apply codiscreteWithin.mono (by tauto) (circleMap_preimage_codiscrete hR hf)

/-!
## Behaviour with Respect to Arithmetic Operations
-/

/-- Circle averages commute with scalar multiplication. -/
theorem circleAverage_smul :
    circleAverage (a â€¢ f) c R = a â€¢ circleAverage f c R := by
  unfold circleAverage
  have := SMulCommClass.symm â„ ğ•œ E
  rw [smul_comm]
  simp [intervalIntegral.integral_smul]

/-- Circle averages commute with scalar multiplication. -/
theorem circleAverage_fun_smul :
    circleAverage (fun z â†¦ a â€¢ f z) c R = a â€¢ circleAverage f c R :=
  circleAverage_smul

/-- Circle averages commute with addition. -/
theorem circleAverage_add (hfâ‚ : CircleIntegrable fâ‚ c R) (hfâ‚‚ : CircleIntegrable fâ‚‚ c R) :
    circleAverage (fâ‚ + fâ‚‚) c R = circleAverage fâ‚ c R + circleAverage fâ‚‚ c R := by
  rw [circleAverage, circleAverage, circleAverage, â† smul_add]
  congr
  apply intervalIntegral.integral_add hfâ‚ hfâ‚‚

/-- Circle averages commute with sums. -/
theorem circleAverage_sum {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ â„‚ â†’ E}
    (h : âˆ€ i âˆˆ s, CircleIntegrable (f i) c R) :
    circleAverage (âˆ‘ i âˆˆ s, f i) c R = âˆ‘ i âˆˆ s, circleAverage (f i) c R := by
  unfold circleAverage
  simp [â† Finset.smul_sum, intervalIntegral.integral_finset_sum h]

end Real
