/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.MeasureTheory.Constructions.Pi
import Mathlib.MeasureTheory.Integral.Prod

/-!
# Integration with respect to a finite product of measures

On a finite product of measure spaces, we show that a product of integrable functions each
depending on a single coordinate is integrable, in `MeasureTheory.integrable_fintype_prod`, and
that its integral is the product of the individual integrals,
in `MeasureTheory.integral_fintype_prod_eq_prod`.
-/

open Fintype MeasureTheory MeasureTheory.Measure

namespace MeasureTheory

namespace Integrable

variable {ğ•œ Î¹ : Type*} [NormedCommRing ğ•œ] [Fintype Î¹]

/-- On a finite product space in `n` variables, for a natural number `n`, a product of integrable
functions depending on each coordinate is integrable. -/
theorem fin_nat_prod {n : â„•} {E : Fin n â†’ Type*}
    {mE : âˆ€ i, MeasurableSpace (E i)} {Î¼ : (i : Fin n) â†’ Measure (E i)} [âˆ€ i, SigmaFinite (Î¼ i)]
    {f : (i : Fin n) â†’ E i â†’ ğ•œ} (hf : âˆ€ i, Integrable (f i) (Î¼ i)) :
    Integrable (fun (x : (i : Fin n) â†’ E i) â†¦ âˆ i, f i (x i)) (Measure.pi Î¼) := by
  induction n with
  | zero => simp only [Finset.univ_eq_empty, Finset.prod_empty, volume_pi, isFiniteMeasure_iff,
      integrable_const_iff, one_ne_zero, pi_empty_univ, ENNReal.one_lt_top, or_true]
  | succ n n_ih =>
      have := ((measurePreserving_piFinSuccAbove Î¼ 0).symm)
      rw [â† this.integrable_comp_emb (MeasurableEquiv.measurableEmbedding _)]
      simp_rw [MeasurableEquiv.piFinSuccAbove_symm_apply, Fin.insertNthEquiv,
        Fin.prod_univ_succ, Fin.insertNth_zero]
      simp only [Fin.zero_succAbove, cast_eq, Function.comp_def, Fin.cons_zero, Fin.cons_succ]
      have : Integrable (fun (x : (j : Fin n) â†’ E (Fin.succ j)) â†¦ âˆ j, f (Fin.succ j) (x j))
          (Measure.pi (fun i â†¦ Î¼ i.succ)) :=
        n_ih (fun i â†¦ hf _)
      exact Integrable.mul_prod (hf 0) this

/-- On a finite product space, a product of integrable functions depending on each coordinate is
integrable. Version with dependent target. -/
theorem fintype_prod_dep {E : Î¹ â†’ Type*}
    {f : (i : Î¹) â†’ E i â†’ ğ•œ} {mE : âˆ€ i, MeasurableSpace (E i)} {Î¼ : (i : Î¹) â†’ Measure (E i)}
    [âˆ€ i, SigmaFinite (Î¼ i)]
    (hf : âˆ€ i, Integrable (f i) (Î¼ i)) :
    Integrable (fun (x : (i : Î¹) â†’ E i) â†¦ âˆ i, f i (x i)) (Measure.pi Î¼) := by
  let e := (equivFin Î¹).symm
  simp_rw [â† (measurePreserving_piCongrLeft _ e).integrable_comp_emb
    (MeasurableEquiv.measurableEmbedding _),
    â† e.prod_comp, MeasurableEquiv.coe_piCongrLeft, Function.comp_def,
    Equiv.piCongrLeft_apply_apply]
  exact .fin_nat_prod (fun i â†¦ hf _)

/-- On a finite product space, a product of integrable functions depending on each coordinate is
integrable. -/
theorem fintype_prod {E : Type*}
    {f : Î¹ â†’ E â†’ ğ•œ} {mE : MeasurableSpace E} {Î¼ : Î¹ â†’ Measure E} [âˆ€ i, SigmaFinite (Î¼ i)]
    (hf : âˆ€ i, Integrable (f i) (Î¼ i)) :
    Integrable (fun (x : Î¹ â†’ E) â†¦ âˆ i, f i (x i)) (Measure.pi Î¼) :=
  Integrable.fintype_prod_dep hf

end Integrable

variable {ğ•œ : Type*} [RCLike ğ•œ]

/-- A version of **Fubini's theorem** in `n` variables, for a natural number `n`. -/
theorem integral_fin_nat_prod_eq_prod {n : â„•} {E : Fin n â†’ Type*}
    {mE : âˆ€ i, MeasurableSpace (E i)} {Î¼ : (i : Fin n) â†’ Measure (E i)} [âˆ€ i, SigmaFinite (Î¼ i)]
    (f : (i : Fin n) â†’ E i â†’ ğ•œ) :
    âˆ« x : (i : Fin n) â†’ E i, âˆ i, f i (x i) âˆ‚(Measure.pi Î¼) = âˆ i, âˆ« x, f i x âˆ‚(Î¼ i) := by
  induction n with
  | zero => simp [measureReal_def]
  | succ n n_ih =>
      calc
        _ = âˆ« x : E 0 Ã— ((i : Fin n) â†’ E (Fin.succ i)),
            f 0 x.1 * âˆ i : Fin n, f (Fin.succ i) (x.2 i)
            âˆ‚((Î¼ 0).prod (Measure.pi (fun i â†¦ Î¼ i.succ))) := by
          rw [â† ((measurePreserving_piFinSuccAbove Î¼ 0).symm).integral_comp']
          simp_rw [MeasurableEquiv.piFinSuccAbove_symm_apply, Fin.insertNthEquiv,
            Fin.prod_univ_succ, Fin.insertNth_zero, Equiv.coe_fn_mk, Fin.cons_succ,
            Fin.zero_succAbove, cast_eq, Fin.cons_zero]
        _ = (âˆ« x, f 0 x âˆ‚Î¼ 0)
            * âˆ i : Fin n, âˆ« (x : E (Fin.succ i)), f (Fin.succ i) x âˆ‚(Î¼ i.succ) := by
          rw [â† n_ih, â† integral_prod_mul]
        _ = âˆ i, âˆ« x, f i x âˆ‚(Î¼ i) := by rw [Fin.prod_univ_succ]

/-- A version of **Fubini's theorem** in `n` variables, for a natural number `n`. -/
theorem integral_fin_nat_prod_volume_eq_prod {n : â„•} {E : Fin n â†’ Type*}
    [âˆ€ i, MeasureSpace (E i)] [âˆ€ i, SigmaFinite (volume : Measure (E i))]
    (f : (i : Fin n) â†’ E i â†’ ğ•œ) :
    âˆ« x : (i : Fin n) â†’ E i, âˆ i, f i (x i) = âˆ i, âˆ« x, f i x := integral_fin_nat_prod_eq_prod _

/-- A version of **Fubini's theorem** with the variables indexed by a general finite type. -/
theorem integral_fintype_prod_eq_prod (Î¹ : Type*) [Fintype Î¹] {E : Î¹ â†’ Type*}
    (f : (i : Î¹) â†’ E i â†’ ğ•œ) {mE : âˆ€ i, MeasurableSpace (E i)} {Î¼ : (i : Î¹) â†’ Measure (E i)}
    [âˆ€ i, SigmaFinite (Î¼ i)] :
    âˆ« x : (i : Î¹) â†’ E i, âˆ i, f i (x i) âˆ‚(Measure.pi Î¼) = âˆ i, âˆ« x, f i x âˆ‚(Î¼ i) := by
  let e := (equivFin Î¹).symm
  rw [â† (measurePreserving_piCongrLeft _ e).integral_comp']
  simp_rw [â† e.prod_comp, MeasurableEquiv.coe_piCongrLeft, Equiv.piCongrLeft_apply_apply,
    MeasureTheory.integral_fin_nat_prod_eq_prod]

/-- A version of **Fubini's theorem** with the variables indexed by a general finite type. -/
theorem integral_fintype_prod_volume_eq_prod (Î¹ : Type*) [Fintype Î¹] {E : Î¹ â†’ Type*}
    (f : (i : Î¹) â†’ E i â†’ ğ•œ) [âˆ€ i, MeasureSpace (E i)]
    [âˆ€ i, SigmaFinite (volume : Measure (E i))] :
    âˆ« x : (i : Î¹) â†’ E i, âˆ i, f i (x i) = âˆ i, âˆ« x, f i x := integral_fintype_prod_eq_prod _ _

theorem integral_fintype_prod_eq_pow {E : Type*} (Î¹ : Type*) [Fintype Î¹] (f : E â†’ ğ•œ)
    {mE : MeasurableSpace E} {Î¼ : Measure E} [SigmaFinite Î¼] :
    âˆ« x : Î¹ â†’ E, âˆ i, f (x i) âˆ‚(Measure.pi (fun _ â†¦ Î¼)) = (âˆ« x, f x âˆ‚Î¼) ^ (card Î¹) := by
  rw [integral_fintype_prod_eq_prod, Finset.prod_const, card]

theorem integral_fintype_prod_volume_eq_pow {E : Type*} (Î¹ : Type*) [Fintype Î¹] (f : E â†’ ğ•œ)
    [MeasureSpace E] [SigmaFinite (volume : Measure E)] :
    âˆ« x : Î¹ â†’ E, âˆ i, f (x i) = (âˆ« x, f x) ^ (card Î¹) := integral_fintype_prod_eq_pow _ _

end MeasureTheory
