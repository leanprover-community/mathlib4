/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Constructions.Pi
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Integral.IntervalIntegral

/-!
# Integrals of functions defined on indexed product types

In this file we prove lemmas about integrals of functions defined on indexed product types
`f : (âˆ€ i, Î± i) â†’ E`. One of the goals of this file is to help with transfer of results about, e.g.,
functions on `â„â¿ = Fin n â†’ â„` for small values of `n` to results about functions on `â„` or `â„ Ã— â„`.

## Main results

- If `Î¹` is an empty type, then `âˆ« x, f x = f default`.
- If `Î¹` is a type with a unique element, then `âˆ« x, f x = âˆ« y, f (const Î¹ y)`.

For a type with a unique element, we also provide lemmas that are more useful for rewriting from an
integral over `Î±` to an integral over `Î¹ â†’ Î±`.

## TODO

- Add a section about `Fin 2 â†’ Î±`.
- Add a section about `Fin.insertNth`.
- Relate integrals over `Î¹ â†’ Î±` and `Î¹' â†’ Î±` given an equivalence `Î¹ â‰ƒ Î¹'`.
- Add a version of Fubini theorem: integrating over `Î¹ â†’ Î±` is the same as integrating over `s â†’ Î±`,
  then over `sá¶œ â†’ Î±`, where `s : Set Î¹`. Can we reformulate it for two embeddings `Î¹â‚ â†’ Î¹` and
  `Î¹â‚‚ â†’ Î¹` with complement ranges?

## Keywords

integral, pi type
-/

open Function Set MeasureTheory.Measure

namespace MeasureTheory

variable {Î¹ E : Type _} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]

section IsEmpty

/-!
### Integral over `(i : Î¹) â†’ Î± i` with empty `Î¹`

If `Î¹` is an empty type (e.g., `Fin 0`), then `(i : Î¹) â†’ Î± i` is a singleton, and a
`MeasureTheory.Measure.pi`-measure on this singleton is the Dirac measure. Thus integral of a
function over a `pi`-measure is equal to the value of this function at the unique point.
-/

section Measure

variable [IsEmpty Î¹] {Î± : Î¹ â†’ Type _} [âˆ€ i, MeasurableSpace (Î± i)]

theorem integral_pi_empty (f : (âˆ€ i, Î± i) â†’ E) (Î¼ : âˆ€ i, Measure (Î± i)) :
    âˆ« x, f x âˆ‚(.pi Î¼) = f default := by
  rw [Measure.pi_of_empty, integral_dirac]; rfl

theorem set_integral_pi_empty (f : (âˆ€ i, Î± i) â†’ E) (Î¼ : âˆ€ i, Measure (Î± i)) {s : Set (âˆ€ i, Î± i)}
    (hs : default âˆˆ s) : âˆ« x in s, f x âˆ‚(Measure.pi Î¼) = f default := by
  obtain rfl : s = univ := Subsingleton.eq_univ_of_nonempty âŸ¨_, hsâŸ©
  rw [restrict_univ, integral_pi_empty]

theorem set_integral_pi_empty_pi (f : (âˆ€ i, Î± i) â†’ E) (Î¼ : âˆ€ i, Measure (Î± i)) (I : Set Î¹)
    (s : âˆ€ i, Set (Î± i)) : âˆ« x in I.pi s, f x âˆ‚(Measure.pi Î¼) = f default :=
  set_integral_pi_empty f Î¼ isEmptyElim

theorem set_integral_pi_empty_Icc [âˆ€ i, Preorder (Î± i)] (f : (âˆ€ i, Î± i) â†’ E)
    (Î¼ : âˆ€ i, Measure (Î± i)) (a b : (âˆ€ i, Î± i)) :
    âˆ« x in Icc a b, f x âˆ‚(.pi Î¼) = f default :=
  set_integral_pi_empty f Î¼ âŸ¨isEmptyElim, isEmptyElimâŸ©

end Measure

variable [IsEmpty Î¹] {Î± : Î¹ â†’ Type _} [âˆ€ i, MeasureSpace (Î± i)]

theorem integral_pi_empty_volume (f : (âˆ€ i, Î± i) â†’ E) : âˆ« x, f x = f default :=
  integral_pi_empty _ _

theorem set_integral_pi_empty_volume (f : (âˆ€ i, Î± i) â†’ E) {s : Set (âˆ€ i, Î± i)} (hs : default âˆˆ s) :
    âˆ« x in s, f x = f default :=
  set_integral_pi_empty f _ hs

theorem set_integral_pi_empty_pi_volume (f : (âˆ€ i, Î± i) â†’ E) (I : Set Î¹) (s : âˆ€ i, Set (Î± i)) :
    âˆ« x in I.pi s, f x = f default :=
  set_integral_pi_empty_pi f _ I s

theorem set_integral_pi_empty_Icc_volume [âˆ€ i, Preorder (Î± i)] (f : (âˆ€ i, Î± i) â†’ E)
    (a b : (âˆ€ i, Î± i)) : âˆ« x in Icc a b, f x = f default :=
  set_integral_pi_empty_Icc _ _ _ _

end IsEmpty

section Unique

/-!
### Integrals of functions on types with a unique element

If `Î¹` has a unique element (e.g., `Î¹ = Fin 1` or `Î¹ = PUnit`), then `Î¹ â†’ Î±` is equivalent to `Î±`,
thus integrating over `Î¹ â†’ Î±` is equivalent to integrating over `Î±`. We provide lemmas that allow
rewriting between these two kinds of integrals in both directions.
-/

section Measure

variable [Unique Î¹] {Î± : Type _} [MeasurableSpace Î±]

theorem integral_pi_unique (f : (Î¹ â†’ Î±) â†’ E) (Î¼ : Measure Î±) :
    âˆ« x, f x âˆ‚(.pi fun _ â†¦ Î¼) = âˆ« x, f (const Î¹ x) âˆ‚Î¼ :=
  Eq.symm <| ((measurePreserving_funUnique _ _).symm _).integral_comp
    (MeasurableEquiv.measurableEmbedding _) _

theorem set_integral_pi_unique (f : (Î¹ â†’ Î±) â†’ E) (Î¼ : Measure Î±) (s : Set (Î¹ â†’ Î±)) :
    âˆ« x in s, f x âˆ‚(.pi fun _ â†¦ Î¼) = âˆ« x in const Î¹ â»Â¹' s, f (const Î¹ x) âˆ‚Î¼ :=
  Eq.symm <| ((measurePreserving_funUnique _ _).symm _).set_integral_preimage_emb
    (MeasurableEquiv.measurableEmbedding _) _ _

theorem set_integral_pi_unique_pi (f : (Î¹ â†’ Î±) â†’ E) (Î¼ : Measure Î±) (s : Î¹ â†’ Set Î±) :
    âˆ« x in Set.pi univ s, f x âˆ‚(.pi fun _ â†¦ Î¼) = âˆ« x in s default, f (const Î¹ x) âˆ‚Î¼ := by
  simp only [set_integral_pi_unique, Set.preimage, Set.mem_univ_pi, Unique.forall_iff]
  rfl

theorem set_integral_pi_unique_Icc [Preorder Î±] (f : (Î¹ â†’ Î±) â†’ E) (Î¼ : Measure Î±) (a b : Î¹ â†’ Î±) :
    âˆ« x in Icc a b, f x âˆ‚(.pi fun _ â†¦ Î¼) =
      âˆ« x in Icc (a default) (b default), f (const Î¹ x) âˆ‚Î¼ := by
  rw [â† pi_univ_Icc, set_integral_pi_unique_pi]

theorem set_integral_pi_unique_Icc_eq_intervalIntegral (f : (Î¹ â†’ â„) â†’ E) (Î¼ : Measure â„)
    [NoAtoms Î¼] {a b : Î¹ â†’ â„} (h : a default â‰¤ b default) :
    âˆ« x in Icc a b, f x âˆ‚(.pi fun _ â†¦ Î¼) = âˆ« x in (a default)..(b default), f (const Î¹ x) âˆ‚Î¼ := by
  rw [set_integral_pi_unique_Icc, intervalIntegral.integral_of_le h,
    restrict_congr_set Ioc_ae_eq_Icc.symm]

variable (Î¹)

theorem integral_eq_pi_unique (f : Î± â†’ E) (Î¼ : Measure Î±) :
    âˆ« x, f x âˆ‚Î¼ = âˆ« x : Î¹ â†’ Î±, f (x default) âˆ‚(.pi fun _ â†¦ Î¼) :=
  Eq.symm <| integral_pi_unique _ _

theorem set_integral_eq_pi_unique (f : Î± â†’ E) (Î¼ : Measure Î±) (s : Set Î±) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x : Î¹ â†’ Î± in eval default â»Â¹' s, f (x default) âˆ‚(.pi fun _ â†¦ Î¼) :=
  Eq.symm <| set_integral_pi_unique _ _ _

theorem set_integral_Icc_eq_pi_unique [Preorder Î±] (f : Î± â†’ E) (Î¼ : Measure Î±) (a b : Î±) :
    âˆ« x in Icc a b, f x âˆ‚Î¼ = âˆ« x in Icc (const Î¹ a) (const Î¹ b), f (x default) âˆ‚(.pi fun _ â†¦ Î¼) :=
  Eq.symm <| set_integral_pi_unique_Icc _ _ _ _

theorem intervalIntegral_eq_pi_unique (f : â„ â†’ E) (Î¼ : Measure â„) [NoAtoms Î¼]
    {a b : â„} (h : a â‰¤ b) :
    âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in Icc (const Î¹ a) (const Î¹ b), f (x default) âˆ‚(.pi fun _ â†¦ Î¼) :=
  Eq.symm <| set_integral_pi_unique_Icc_eq_intervalIntegral _ _ h

end Measure

variable [Unique Î¹] {Î± : Type _} [MeasureSpace Î±]

theorem integral_pi_unique_volume (f : (Î¹ â†’ Î±) â†’ E) : âˆ« x, f x = âˆ« x, f (const Î¹ x) :=
  integral_pi_unique _ _

theorem set_integral_pi_unique_volume (f : (Î¹ â†’ Î±) â†’ E) (s : Set (Î¹ â†’ Î±)) :
    âˆ« x in s, f x = âˆ« x in const Î¹ â»Â¹' s, f (const Î¹ x) :=
  set_integral_pi_unique f _ s

theorem set_integral_pi_unique_pi_volume (f : (Î¹ â†’ Î±) â†’ E) (s : Î¹ â†’ Set Î±) :
    âˆ« x in Set.pi univ s, f x = âˆ« x in s default, f (const Î¹ x) :=
  set_integral_pi_unique_pi f _ s

theorem set_integral_pi_unique_Icc_volume [Preorder Î±] (f : (Î¹ â†’ Î±) â†’ E) (a b : Î¹ â†’ Î±) :
    âˆ« x in Icc a b, f x = âˆ« x in Icc (a default) (b default), f (const Î¹ x) :=
  set_integral_pi_unique_Icc _ _ _ _

theorem set_integral_pi_unique_Icc_volume_eq_intervalIntegral (f : (Î¹ â†’ â„) â†’ E) {a b : Î¹ â†’ â„}
    (h : a default â‰¤ b default) :
    âˆ« x in Icc a b, f x = âˆ« x in (a default)..(b default), f (const Î¹ x) :=
  set_integral_pi_unique_Icc_eq_intervalIntegral _ _ h

variable (Î¹)

theorem integral_volume_eq_pi_unique (f : Î± â†’ E) : âˆ« x, f x = âˆ« x : Î¹ â†’ Î±, f (x default) :=
  integral_eq_pi_unique _ _ _

theorem set_integral_volume_eq_pi_unique (f : Î± â†’ E) (s : Set Î±) :
    âˆ« x in s, f x = âˆ« x : Î¹ â†’ Î± in eval default â»Â¹' s, f (x default) :=
  set_integral_eq_pi_unique _ _ _ _

theorem set_integral_volume_Icc_eq_pi_unique [Preorder Î±] (f : Î± â†’ E) (a b : Î±) :
    âˆ« x in Icc a b, f x = âˆ« x in Icc (const Î¹ a) (const Î¹ b), f (x default) :=
  Eq.symm <| set_integral_pi_unique_Icc _ _ _ _

theorem intervalIntegral_volume_eq_pi_unique (f : â„ â†’ E) {a b : â„} (h : a â‰¤ b) :
    âˆ« x in a..b, f x = âˆ« x in Icc (const Î¹ a) (const Î¹ b), f (x default) :=
  intervalIntegral_eq_pi_unique Î¹ f _ h

end Unique

end MeasureTheory

/-!
# Integration with respect to a finite product of measures

On a finite product of measure spaces, we show that a product of integrable functions each
depending on a single coordinate is integrable, in `MeasureTheory.integrable_fintype_prod`, and
that its integral is the product of the individual integrals,
in `MeasureTheory.integral_fintype_prod_eq_prod`.
-/

open Fintype MeasureTheory MeasureTheory.Measure

variable {ğ•œ : Type*} [RCLike ğ•œ]

namespace MeasureTheory

/-- On a finite product space in `n` variables, for a natural number `n`, a product of integrable
functions depending on each coordinate is integrable. -/
theorem Integrable.fin_nat_prod {n : â„•} {E : Fin n â†’ Type*}
    [âˆ€ i, MeasureSpace (E i)] [âˆ€ i, SigmaFinite (volume : Measure (E i))]
    {f : (i : Fin n) â†’ E i â†’ ğ•œ} (hf : âˆ€ i, Integrable (f i)) :
    Integrable (fun (x : (i : Fin n) â†’ E i) â†¦ âˆ i, f i (x i)) := by
  induction n with
  | zero => simp only [Finset.univ_eq_empty, Finset.prod_empty, volume_pi, isFiniteMeasure_iff,
      integrable_const_iff, one_ne_zero, pi_empty_univ, ENNReal.one_lt_top, or_true]
  | succ n n_ih =>
      have := ((measurePreserving_piFinSuccAbove (fun i => (volume : Measure (E i))) 0).symm)
      rw [volume_pi, â† this.integrable_comp_emb (MeasurableEquiv.measurableEmbedding _)]
      simp_rw [MeasurableEquiv.piFinSuccAbove_symm_apply, Fin.insertNthEquiv,
        Fin.prod_univ_succ, Fin.insertNth_zero]
      simp only [Fin.zero_succAbove, cast_eq, Function.comp_def, Fin.cons_zero, Fin.cons_succ]
      have : Integrable (fun (x : (j : Fin n) â†’ E (Fin.succ j)) â†¦ âˆ j, f (Fin.succ j) (x j)) :=
        n_ih (fun i â†¦ hf _)
      exact Integrable.prod_mul (hf 0) this

/-- On a finite product space, a product of integrable functions depending on each coordinate is
integrable. Version with dependent target. -/
theorem Integrable.fintype_prod_dep {Î¹ : Type*} [Fintype Î¹] {E : Î¹ â†’ Type*}
    {f : (i : Î¹) â†’ E i â†’ ğ•œ} [âˆ€ i, MeasureSpace (E i)] [âˆ€ i, SigmaFinite (volume : Measure (E i))]
    (hf : âˆ€ i, Integrable (f i)) :
    Integrable (fun (x : (i : Î¹) â†’ E i) â†¦ âˆ i, f i (x i)) := by
  let e := (equivFin Î¹).symm
  simp_rw [â† (volume_measurePreserving_piCongrLeft _ e).integrable_comp_emb
    (MeasurableEquiv.measurableEmbedding _),
    â† e.prod_comp, MeasurableEquiv.coe_piCongrLeft, Function.comp_def,
    Equiv.piCongrLeft_apply_apply]
  exact .fin_nat_prod (fun i â†¦ hf _)

/-- On a finite product space, a product of integrable functions depending on each coordinate is
integrable. -/
theorem Integrable.fintype_prod {Î¹ : Type*} [Fintype Î¹] {E : Type*}
    {f : Î¹ â†’ E â†’ ğ•œ} [MeasureSpace E] [SigmaFinite (volume : Measure E)]
    (hf : âˆ€ i, Integrable (f i)) :
    Integrable (fun (x : Î¹ â†’ E) â†¦ âˆ i, f i (x i)) :=
  Integrable.fintype_prod_dep hf

/-- A version of **Fubini's theorem** in `n` variables, for a natural number `n`. -/
theorem integral_fin_nat_prod_eq_prod {n : â„•} {E : Fin n â†’ Type*}
    [âˆ€ i, MeasureSpace (E i)] [âˆ€ i, SigmaFinite (volume : Measure (E i))]
    (f : (i : Fin n) â†’ E i â†’ ğ•œ) :
    âˆ« x : (i : Fin n) â†’ E i, âˆ i, f i (x i) = âˆ i, âˆ« x, f i x := by
  induction n with
  | zero =>
      simp only [volume_pi, Finset.univ_eq_empty, Finset.prod_empty, integral_const,
        pi_empty_univ, ENNReal.toReal_one, smul_eq_mul, mul_one, pow_zero, one_smul]
  | succ n n_ih =>
      calc
        _ = âˆ« x : E 0 Ã— ((i : Fin n) â†’ E (Fin.succ i)),
            f 0 x.1 * âˆ i : Fin n, f (Fin.succ i) (x.2 i) := by
          rw [volume_pi, â† ((measurePreserving_piFinSuccAbove
            (fun i => (volume : Measure (E i))) 0).symm).integral_comp']
          simp_rw [MeasurableEquiv.piFinSuccAbove_symm_apply, Fin.insertNthEquiv,
            Fin.prod_univ_succ, Fin.insertNth_zero, Equiv.coe_fn_mk, Fin.cons_succ, volume_eq_prod,
            volume_pi, Fin.zero_succAbove, cast_eq, Fin.cons_zero]
        _ = (âˆ« x, f 0 x) * âˆ i : Fin n, âˆ« (x : E (Fin.succ i)), f (Fin.succ i) x := by
          rw [â† n_ih, â† integral_prod_mul, volume_eq_prod]
        _ = âˆ i, âˆ« x, f i x := by rw [Fin.prod_univ_succ]

/-- A version of **Fubini's theorem** with the variables indexed by a general finite type. -/
theorem integral_fintype_prod_eq_prod (Î¹ : Type*) [Fintype Î¹] {E : Î¹ â†’ Type*}
    (f : (i : Î¹) â†’ E i â†’ ğ•œ) [âˆ€ i, MeasureSpace (E i)] [âˆ€ i, SigmaFinite (volume : Measure (E i))] :
    âˆ« x : (i : Î¹) â†’ E i, âˆ i, f i (x i) = âˆ i, âˆ« x, f i x := by
  let e := (equivFin Î¹).symm
  rw [â† (volume_measurePreserving_piCongrLeft _ e).integral_comp']
  simp_rw [â† e.prod_comp, MeasurableEquiv.coe_piCongrLeft, Equiv.piCongrLeft_apply_apply,
    MeasureTheory.integral_fin_nat_prod_eq_prod]

theorem integral_fintype_prod_eq_pow {E : Type*} (Î¹ : Type*) [Fintype Î¹] (f : E â†’ ğ•œ)
    [MeasureSpace E] [SigmaFinite (volume : Measure E)] :
    âˆ« x : Î¹ â†’ E, âˆ i, f (x i) = (âˆ« x, f x) ^ (card Î¹) := by
  rw [integral_fintype_prod_eq_prod, Finset.prod_const, card]

end MeasureTheory
