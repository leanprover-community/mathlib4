/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Bhavik Mehta
-/
import Mathlib.Analysis.Calculus.Deriv.Support
import Mathlib.Analysis.SpecialFunctions.Pow.Deriv
import Mathlib.MeasureTheory.Function.JacobianOneDim
import Mathlib.MeasureTheory.Integral.IntervalIntegral.IntegrationByParts
import Mathlib.MeasureTheory.Measure.Haar.NormedSpace
import Mathlib.MeasureTheory.Measure.Haar.Unique

/-!
# Links between an integral and its "improper" version

In its current state, mathlib only knows how to talk about definite ("proper") integrals,
in the sense that it treats integrals over `[x, +âˆ)` the same as it treats integrals over
`[y, z]`. For example, the integral over `[1, +âˆ)` is **not** defined to be the limit of
the integral over `[1, x]` as `x` tends to `+âˆ`, which is known as an **improper integral**.

Indeed, the "proper" definition is stronger than the "improper" one. The usual counterexample
is `x â†¦ sin(x)/x`, which has an improper integral over `[1, +âˆ)` but no definite integral.

Although definite integrals have better properties, they are hardly usable when it comes to
computing integrals on unbounded sets, which is much easier using limits. Thus, in this file,
we prove various ways of studying the proper integral by studying the improper one.

## Definitions

The main definition of this file is `MeasureTheory.AECover`. It is a rather technical definition
whose sole purpose is generalizing and factoring proofs. Given an index type `Î¹`, a countably
generated filter `l` over `Î¹`, and an `Î¹`-indexed family `Ï†` of subsets of a measurable space `Î±`
equipped with a measure `Î¼`, one should think of a hypothesis `hÏ† : MeasureTheory.AECover Î¼ l Ï†` as
a sufficient condition for being able to interpret `âˆ« x, f x âˆ‚Î¼` (if it exists) as the limit of `âˆ« x
in Ï† i, f x âˆ‚Î¼` as `i` tends to `l`.

When using this definition with a measure restricted to a set `s`, which happens fairly often, one
should not try too hard to use a `MeasureTheory.AECover` of subsets of `s`, as it often makes proofs
more complicated than necessary. See for example the proof of
`MeasureTheory.integrableOn_Iic_of_intervalIntegral_norm_tendsto` where we use `(fun x â†¦ oi x)` as a
`MeasureTheory.AECover` w.r.t. `Î¼.restrict (Iic b)`, instead of using `(fun x â†¦ Ioc x b)`.

## Main statements

- `MeasureTheory.AECover.lintegral_tendsto_of_countably_generated` : if `Ï†` is a
  `MeasureTheory.AECover Î¼ l`, where `l` is a countably generated filter, and if `f` is a measurable
  `ENNReal`-valued function, then `âˆ«â» x in Ï† n, f x âˆ‚Î¼` tends to `âˆ«â» x, f x âˆ‚Î¼` as `n` tends to `l`

- `MeasureTheory.AECover.integrable_of_integral_norm_tendsto` : if `Ï†` is a
  `MeasureTheory.AECover Î¼ l`, where `l` is a countably generated filter, if `f` is measurable and
  integrable on each `Ï† n`, and if `âˆ« x in Ï† n, â€–f xâ€– âˆ‚Î¼` tends to some `I : â„` as n tends to `l`,
  then `f` is integrable

- `MeasureTheory.AECover.integral_tendsto_of_countably_generated` : if `Ï†` is a
  `MeasureTheory.AECover Î¼ l`, where `l` is a countably generated filter, and if `f` is measurable
  and integrable (globally), then `âˆ« x in Ï† n, f x âˆ‚Î¼` tends to `âˆ« x, f x âˆ‚Î¼` as `n` tends to `+âˆ`.

We then specialize these lemmas to various use cases involving intervals, which are frequent
in analysis. In particular,

- `MeasureTheory.integral_Ioi_of_hasDerivAt_of_tendsto` is a version of FTC-2 on the interval
  `(a, +âˆ)`, giving the formula `âˆ« x in (a, +âˆ), g' x = l - g a` if `g'` is integrable and
  `g` tends to `l` at `+âˆ`.
- `MeasureTheory.integral_Ioi_of_hasDerivAt_of_nonneg` gives the same result assuming that
  `g'` is nonnegative instead of integrable. Its automatic integrability in this context is proved
  in `MeasureTheory.integrableOn_Ioi_deriv_of_nonneg`.
- `MeasureTheory.integral_comp_smul_deriv_Ioi` is a version of the change of variables formula
  on semi-infinite intervals.
- `MeasureTheory.tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi` shows that a function whose
  derivative is integrable on `(a, +âˆ)` has a limit at `+âˆ`.
- `MeasureTheory.tendsto_zero_of_hasDerivAt_of_integrableOn_Ioi` shows that an integrable function
  whose derivative is integrable on `(a, +âˆ)` tends to `0` at `+âˆ`.

Versions of these results are also given on the intervals `(-âˆ, a]` and `(-âˆ, +âˆ)`, as well as
the corresponding versions of integration by parts.
-/

open MeasureTheory Filter Set TopologicalSpace Topology
open scoped ENNReal NNReal

namespace MeasureTheory

section AECover

variable {Î± Î¹ : Type*} [MeasurableSpace Î±] (Î¼ : Measure Î±) (l : Filter Î¹)

/-- A sequence `Ï†` of subsets of `Î±` is a `MeasureTheory.AECover` w.r.t. a measure `Î¼` and a filter
`l` if almost every point (w.r.t. `Î¼`) of `Î±` eventually belongs to `Ï† n` (w.r.t. `l`), and if
each `Ï† n` is measurable.  This definition is a technical way to avoid duplicating a lot of
proofs.  It should be thought of as a sufficient condition for being able to interpret
`âˆ« x, f x âˆ‚Î¼` (if it exists) as the limit of `âˆ« x in Ï† n, f x âˆ‚Î¼` as `n` tends to `l`.
See for example `MeasureTheory.AECover.lintegral_tendsto_of_countably_generated`,
`MeasureTheory.AECover.integrable_of_integral_norm_tendsto` and
`MeasureTheory.AECover.integral_tendsto_of_countably_generated`. -/
structure AECover (Ï† : Î¹ â†’ Set Î±) : Prop where
  ae_eventually_mem : âˆ€áµ x âˆ‚Î¼, âˆ€á¶  i in l, x âˆˆ Ï† i
  protected measurableSet : âˆ€ i, MeasurableSet <| Ï† i

variable {Î¼} {l}

namespace AECover

/-!
## Operations on `AECover`s
-/

/-- Elementwise intersection of two `AECover`s is an `AECover`. -/
theorem inter {Ï† Ïˆ : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) (hÏˆ : AECover Î¼ l Ïˆ) :
    AECover Î¼ l (fun i â†¦ Ï† i âˆ© Ïˆ i) where
  ae_eventually_mem := hÏˆ.1.mp <| hÏ†.1.mono fun _ â†¦ Eventually.and
  measurableSet _ := (hÏ†.2 _).inter (hÏˆ.2 _)

theorem superset {Ï† Ïˆ : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) (hsub : âˆ€ i, Ï† i âŠ† Ïˆ i)
    (hmeas : âˆ€ i, MeasurableSet (Ïˆ i)) : AECover Î¼ l Ïˆ :=
  âŸ¨hÏ†.1.mono fun _x hx â†¦ hx.mono fun i hi â†¦ hsub i hi, hmeasâŸ©

theorem mono_ac {Î½ : Measure Î±} {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) (hle : Î½ â‰ª Î¼) :
    AECover Î½ l Ï† := âŸ¨hle hÏ†.1, hÏ†.2âŸ©

theorem mono {Î½ : Measure Î±} {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) (hle : Î½ â‰¤ Î¼) :
    AECover Î½ l Ï† := hÏ†.mono_ac hle.absolutelyContinuous

end AECover

section MetricSpace

variable [PseudoMetricSpace Î±] [OpensMeasurableSpace Î±]

theorem aecover_ball {x : Î±} {r : Î¹ â†’ â„} (hr : Tendsto r l atTop) :
    AECover Î¼ l (fun i â†¦ Metric.ball x (r i)) where
  measurableSet _ := Metric.isOpen_ball.measurableSet
  ae_eventually_mem := by
    filter_upwards with y
    filter_upwards [hr (Ioi_mem_atTop (dist x y))] with a ha using by simpa [dist_comm] using ha

theorem aecover_closedBall {x : Î±} {r : Î¹ â†’ â„} (hr : Tendsto r l atTop) :
    AECover Î¼ l (fun i â†¦ Metric.closedBall x (r i)) where
  measurableSet _ := Metric.isClosed_closedBall.measurableSet
  ae_eventually_mem := by
    filter_upwards with y
    filter_upwards [hr (Ici_mem_atTop (dist x y))] with a ha using by simpa [dist_comm] using ha

end MetricSpace

section PreorderÎ±

variable [Preorder Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] [OpensMeasurableSpace Î±]
  {a b : Î¹ â†’ Î±}

theorem aecover_Ici (ha : Tendsto a l atBot) : AECover Î¼ l fun i => Ici (a i) where
  ae_eventually_mem := ae_of_all Î¼ ha.eventually_le_atBot
  measurableSet _ := measurableSet_Ici

theorem aecover_Iic (hb : Tendsto b l atTop) : AECover Î¼ l fun i => Iic <| b i :=
  aecover_Ici (Î± := Î±áµ’áµˆ) hb

theorem aecover_Icc (ha : Tendsto a l atBot) (hb : Tendsto b l atTop) :
    AECover Î¼ l fun i => Icc (a i) (b i) :=
  (aecover_Ici ha).inter (aecover_Iic hb)

end PreorderÎ±

section LinearOrderÎ±

variable [LinearOrder Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] [OpensMeasurableSpace Î±]
  {a b : Î¹ â†’ Î±} (ha : Tendsto a l atBot) (hb : Tendsto b l atTop)

include ha in
theorem aecover_Ioi [NoMinOrder Î±] : AECover Î¼ l fun i => Ioi (a i) where
  ae_eventually_mem := ae_of_all Î¼ ha.eventually_lt_atBot
  measurableSet _ := measurableSet_Ioi

include hb in
theorem aecover_Iio [NoMaxOrder Î±] : AECover Î¼ l fun i => Iio (b i) := aecover_Ioi (Î± := Î±áµ’áµˆ) hb

include ha hb

theorem aecover_Ioo [NoMinOrder Î±] [NoMaxOrder Î±] : AECover Î¼ l fun i => Ioo (a i) (b i) :=
  (aecover_Ioi ha).inter (aecover_Iio hb)

theorem aecover_Ioc [NoMinOrder Î±] : AECover Î¼ l fun i => Ioc (a i) (b i) :=
  (aecover_Ioi ha).inter (aecover_Iic hb)

theorem aecover_Ico [NoMaxOrder Î±] : AECover Î¼ l fun i => Ico (a i) (b i) :=
  (aecover_Ici ha).inter (aecover_Iio hb)

end LinearOrderÎ±

section FiniteIntervals

variable [LinearOrder Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] [OpensMeasurableSpace Î±]
  {a b : Î¹ â†’ Î±} {A B : Î±} (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B))

include ha in
theorem aecover_Ioi_of_Ioi : AECover (Î¼.restrict (Ioi A)) l fun i â†¦ Ioi (a i) where
  ae_eventually_mem := (ae_restrict_mem measurableSet_Ioi).mono fun _x hx â†¦ ha.eventually <|
    eventually_lt_nhds hx
  measurableSet _ := measurableSet_Ioi

include hb in
theorem aecover_Iio_of_Iio : AECover (Î¼.restrict (Iio B)) l fun i â†¦ Iio (b i) :=
  aecover_Ioi_of_Ioi (Î± := Î±áµ’áµˆ) hb

include ha in
theorem aecover_Ioi_of_Ici : AECover (Î¼.restrict (Ioi A)) l fun i â†¦ Ici (a i) :=
  (aecover_Ioi_of_Ioi ha).superset (fun _ â†¦ Ioi_subset_Ici_self) fun _ â†¦ measurableSet_Ici

include hb in
theorem aecover_Iio_of_Iic : AECover (Î¼.restrict (Iio B)) l fun i â†¦ Iic (b i) :=
  aecover_Ioi_of_Ici (Î± := Î±áµ’áµˆ) hb

include ha hb in
theorem aecover_Ioo_of_Ioo : AECover (Î¼.restrict <| Ioo A B) l fun i => Ioo (a i) (b i) :=
  ((aecover_Ioi_of_Ioi ha).mono <| Measure.restrict_mono Ioo_subset_Ioi_self le_rfl).inter
    ((aecover_Iio_of_Iio hb).mono <| Measure.restrict_mono Ioo_subset_Iio_self le_rfl)

include ha hb in
theorem aecover_Ioo_of_Icc : AECover (Î¼.restrict <| Ioo A B) l fun i => Icc (a i) (b i) :=
  (aecover_Ioo_of_Ioo ha hb).superset (fun _ â†¦ Ioo_subset_Icc_self) fun _ â†¦ measurableSet_Icc

include ha hb in
theorem aecover_Ioo_of_Ico : AECover (Î¼.restrict <| Ioo A B) l fun i => Ico (a i) (b i) :=
  (aecover_Ioo_of_Ioo ha hb).superset (fun _ â†¦ Ioo_subset_Ico_self) fun _ â†¦ measurableSet_Ico

include ha hb in
theorem aecover_Ioo_of_Ioc : AECover (Î¼.restrict <| Ioo A B) l fun i => Ioc (a i) (b i) :=
  (aecover_Ioo_of_Ioo ha hb).superset (fun _ â†¦ Ioo_subset_Ioc_self) fun _ â†¦ measurableSet_Ioc

variable [NoAtoms Î¼]

theorem aecover_Ioc_of_Icc (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ioc A B) l fun i => Icc (a i) (b i) :=
  (aecover_Ioo_of_Icc ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ioc).ge

theorem aecover_Ioc_of_Ico (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ioc A B) l fun i => Ico (a i) (b i) :=
  (aecover_Ioo_of_Ico ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ioc).ge

theorem aecover_Ioc_of_Ioc (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ioc A B) l fun i => Ioc (a i) (b i) :=
  (aecover_Ioo_of_Ioc ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ioc).ge

theorem aecover_Ioc_of_Ioo (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ioc A B) l fun i => Ioo (a i) (b i) :=
  (aecover_Ioo_of_Ioo ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ioc).ge

theorem aecover_Ico_of_Icc (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ico A B) l fun i => Icc (a i) (b i) :=
  (aecover_Ioo_of_Icc ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ico).ge

theorem aecover_Ico_of_Ico (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ico A B) l fun i => Ico (a i) (b i) :=
  (aecover_Ioo_of_Ico ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ico).ge

theorem aecover_Ico_of_Ioc (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ico A B) l fun i => Ioc (a i) (b i) :=
  (aecover_Ioo_of_Ioc ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ico).ge

theorem aecover_Ico_of_Ioo (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Ico A B) l fun i => Ioo (a i) (b i) :=
  (aecover_Ioo_of_Ioo ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Ico).ge

theorem aecover_Icc_of_Icc (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Icc A B) l fun i => Icc (a i) (b i) :=
  (aecover_Ioo_of_Icc ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Icc).ge

theorem aecover_Icc_of_Ico (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Icc A B) l fun i => Ico (a i) (b i) :=
  (aecover_Ioo_of_Ico ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Icc).ge

theorem aecover_Icc_of_Ioc (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Icc A B) l fun i => Ioc (a i) (b i) :=
  (aecover_Ioo_of_Ioc ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Icc).ge

theorem aecover_Icc_of_Ioo (ha : Tendsto a l (ğ“ A)) (hb : Tendsto b l (ğ“ B)) :
    AECover (Î¼.restrict <| Icc A B) l fun i => Ioo (a i) (b i) :=
  (aecover_Ioo_of_Ioo ha hb).mono (Measure.restrict_congr_set Ioo_ae_eq_Icc).ge

end FiniteIntervals

protected theorem AECover.restrict {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {s : Set Î±} :
    AECover (Î¼.restrict s) l Ï† :=
  hÏ†.mono Measure.restrict_le_self

theorem aecover_restrict_of_ae_imp {s : Set Î±} {Ï† : Î¹ â†’ Set Î±} (hs : MeasurableSet s)
    (ae_eventually_mem : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆ€á¶  n in l, x âˆˆ Ï† n)
    (measurable : âˆ€ n, MeasurableSet <| Ï† n) : AECover (Î¼.restrict s) l Ï† where
  ae_eventually_mem := by rwa [ae_restrict_iff' hs]
  measurableSet := measurable

theorem AECover.inter_restrict {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {s : Set Î±}
    (hs : MeasurableSet s) : AECover (Î¼.restrict s) l fun i => Ï† i âˆ© s :=
  aecover_restrict_of_ae_imp hs
    (hÏ†.ae_eventually_mem.mono fun _x hx hxs => hx.mono fun _i hi => âŸ¨hi, hxsâŸ©) fun i =>
    (hÏ†.measurableSet i).inter hs

theorem AECover.ae_tendsto_indicator {Î² : Type*} [Zero Î²] [TopologicalSpace Î²] (f : Î± â†’ Î²)
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun i => (Ï† i).indicator f x) l (ğ“ <| f x) :=
  hÏ†.ae_eventually_mem.mono fun _x hx =>
    tendsto_const_nhds.congr' <| hx.mono fun _n hn => (indicator_of_mem hn _).symm

theorem AECover.aemeasurable {Î² : Type*} [MeasurableSpace Î²] [l.IsCountablyGenerated] [l.NeBot]
    {f : Î± â†’ Î²} {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†)
    (hfm : âˆ€ i, AEMeasurable f (Î¼.restrict <| Ï† i)) : AEMeasurable f Î¼ := by
  obtain âŸ¨u, huâŸ© := l.exists_seq_tendsto
  have := aemeasurable_iUnion_iff.mpr fun n : â„• => hfm (u n)
  rwa [Measure.restrict_eq_self_of_ae_mem] at this
  filter_upwards [hÏ†.ae_eventually_mem] with x hx using
    mem_iUnion.mpr (hu.eventually hx).exists

theorem AECover.aestronglyMeasurable {Î² : Type*} [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [l.IsCountablyGenerated] [l.NeBot] {f : Î± â†’ Î²} {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†)
    (hfm : âˆ€ i, AEStronglyMeasurable f (Î¼.restrict <| Ï† i)) : AEStronglyMeasurable f Î¼ := by
  obtain âŸ¨u, huâŸ© := l.exists_seq_tendsto
  have := aestronglyMeasurable_iUnion_iff.mpr fun n : â„• => hfm (u n)
  rwa [Measure.restrict_eq_self_of_ae_mem] at this
  filter_upwards [hÏ†.ae_eventually_mem] with x hx using mem_iUnion.mpr (hu.eventually hx).exists

end AECover

theorem AECover.comp_tendsto {Î± Î¹ Î¹' : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±} {l : Filter Î¹}
    {l' : Filter Î¹'} {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {u : Î¹' â†’ Î¹} (hu : Tendsto u l' l) :
    AECover Î¼ l' (Ï† âˆ˜ u) where
  ae_eventually_mem := hÏ†.ae_eventually_mem.mono fun _x hx => hu.eventually hx
  measurableSet i := hÏ†.measurableSet (u i)

section AECoverUnionInterCountable

variable {Î± Î¹ : Type*} [Countable Î¹] [MeasurableSpace Î±] {Î¼ : Measure Î±}

theorem AECover.biUnion_Iic_aecover [Preorder Î¹] {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ atTop Ï†) :
    AECover Î¼ atTop fun n : Î¹ => â‹ƒ (k) (_h : k âˆˆ Iic n), Ï† k :=
  hÏ†.superset (fun _ â†¦ subset_biUnion_of_mem right_mem_Iic) fun _ â†¦ .biUnion (to_countable _)
    fun _ _ â†¦ (hÏ†.2 _)

theorem AECover.biInter_Ici_aecover [Preorder Î¹] {Ï† : Î¹ â†’ Set Î±}
    (hÏ† : AECover Î¼ atTop Ï†) : AECover Î¼ atTop fun n : Î¹ => â‹‚ (k) (_h : k âˆˆ Ici n), Ï† k where
  ae_eventually_mem := hÏ†.ae_eventually_mem.mono fun x h â†¦ by
    simpa only [mem_iInter, mem_Ici, eventually_forall_ge_atTop]
  measurableSet _ := .biInter (to_countable _) fun n _ => hÏ†.measurableSet n

end AECoverUnionInterCountable

section Lintegral

variable {Î± Î¹ : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±} {l : Filter Î¹}

private theorem lintegral_tendsto_of_monotone_of_nat {Ï† : â„• â†’ Set Î±} (hÏ† : AECover Î¼ atTop Ï†)
    (hmono : Monotone Ï†) {f : Î± â†’ â„â‰¥0âˆ} (hfm : AEMeasurable f Î¼) :
    Tendsto (fun i => âˆ«â» x in Ï† i, f x âˆ‚Î¼) atTop (ğ“ <| âˆ«â» x, f x âˆ‚Î¼) :=
  let F n := (Ï† n).indicator f
  have keyâ‚ : âˆ€ n, AEMeasurable (F n) Î¼ := fun n => hfm.indicator (hÏ†.measurableSet n)
  have keyâ‚‚ : âˆ€áµ x : Î± âˆ‚Î¼, Monotone fun n => F n x := ae_of_all _ fun x _i _j hij =>
    indicator_le_indicator_of_subset (hmono hij) (fun x => zero_le <| f x) x
  have keyâ‚ƒ : âˆ€áµ x : Î± âˆ‚Î¼, Tendsto (fun n => F n x) atTop (ğ“ (f x)) := hÏ†.ae_tendsto_indicator f
  (lintegral_tendsto_of_tendsto_of_monotone keyâ‚ keyâ‚‚ keyâ‚ƒ).congr fun n =>
    lintegral_indicator (hÏ†.measurableSet n) _

theorem AECover.lintegral_tendsto_of_nat {Ï† : â„• â†’ Set Î±} (hÏ† : AECover Î¼ atTop Ï†) {f : Î± â†’ â„â‰¥0âˆ}
    (hfm : AEMeasurable f Î¼) : Tendsto (âˆ«â» x in Ï† Â·, f x âˆ‚Î¼) atTop (ğ“ <| âˆ«â» x, f x âˆ‚Î¼) := by
  have limâ‚ := lintegral_tendsto_of_monotone_of_nat hÏ†.biInter_Ici_aecover
    (fun i j hij => biInter_subset_biInter_left (Ici_subset_Ici.mpr hij)) hfm
  have limâ‚‚ := lintegral_tendsto_of_monotone_of_nat hÏ†.biUnion_Iic_aecover
    (fun i j hij => biUnion_subset_biUnion_left (Iic_subset_Iic.mpr hij)) hfm
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le limâ‚ limâ‚‚ (fun n â†¦ ?_) fun n â†¦ ?_
  exacts [lintegral_mono_set (biInter_subset_of_mem left_mem_Ici),
    lintegral_mono_set (subset_biUnion_of_mem right_mem_Iic)]

theorem AECover.lintegral_tendsto_of_countably_generated [l.IsCountablyGenerated] {Ï† : Î¹ â†’ Set Î±}
    (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ â„â‰¥0âˆ} (hfm : AEMeasurable f Î¼) :
    Tendsto (fun i => âˆ«â» x in Ï† i, f x âˆ‚Î¼) l (ğ“ <| âˆ«â» x, f x âˆ‚Î¼) :=
  tendsto_of_seq_tendsto fun _u hu => (hÏ†.comp_tendsto hu).lintegral_tendsto_of_nat hfm

theorem AECover.lintegral_eq_of_tendsto [l.NeBot] [l.IsCountablyGenerated] {Ï† : Î¹ â†’ Set Î±}
    (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ â„â‰¥0âˆ} (I : â„â‰¥0âˆ) (hfm : AEMeasurable f Î¼)
    (htendsto : Tendsto (fun i => âˆ«â» x in Ï† i, f x âˆ‚Î¼) l (ğ“ I)) : âˆ«â» x, f x âˆ‚Î¼ = I :=
  tendsto_nhds_unique (hÏ†.lintegral_tendsto_of_countably_generated hfm) htendsto

theorem AECover.iSup_lintegral_eq_of_countably_generated [Nonempty Î¹] [l.NeBot]
    [l.IsCountablyGenerated] {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ â„â‰¥0âˆ}
    (hfm : AEMeasurable f Î¼) : â¨† i : Î¹, âˆ«â» x in Ï† i, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by
  have := hÏ†.lintegral_tendsto_of_countably_generated hfm
  refine ciSup_eq_of_forall_le_of_forall_lt_exists_gt
    (fun i => lintegral_mono' Measure.restrict_le_self le_rfl) fun w hw => ?_
  exact (this.eventually_const_lt hw).exists

end Lintegral

section Integrable

variable {Î± Î¹ E : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±} {l : Filter Î¹} [NormedAddCommGroup E]

theorem AECover.integrable_of_lintegral_enorm_bounded [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : â„) (hfm : AEStronglyMeasurable f Î¼)
    (hbounded : âˆ€á¶  i in l, âˆ«â» x in Ï† i, â€–f xâ€–â‚‘ âˆ‚Î¼ â‰¤ ENNReal.ofReal I) : Integrable f Î¼ := by
  refine âŸ¨hfm, (le_of_tendsto ?_ hbounded).trans_lt ENNReal.ofReal_lt_topâŸ©
  exact hÏ†.lintegral_tendsto_of_countably_generated hfm.enorm

theorem AECover.integrable_of_lintegral_enorm_tendsto [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : â„) (hfm : AEStronglyMeasurable f Î¼)
    (htendsto : Tendsto (fun i => âˆ«â» x in Ï† i, â€–f xâ€–â‚‘ âˆ‚Î¼) l (ğ“ <| .ofReal I)) :
    Integrable f Î¼ := by
  refine hÏ†.integrable_of_lintegral_enorm_bounded (max 1 (I + 1)) hfm ?_
  refine htendsto.eventually (ge_mem_nhds ?_)
  refine (ENNReal.ofReal_lt_ofReal_iff (lt_max_of_lt_left zero_lt_one)).2 ?_
  exact lt_max_of_lt_right (lt_add_one I)

theorem AECover.integrable_of_lintegral_enorm_bounded' [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : â„â‰¥0) (hfm : AEStronglyMeasurable f Î¼)
    (hbounded : âˆ€á¶  i in l, âˆ«â» x in Ï† i, â€–f xâ€–â‚‘ âˆ‚Î¼ â‰¤ I) : Integrable f Î¼ :=
  hÏ†.integrable_of_lintegral_enorm_bounded I hfm
    (by simpa only [ENNReal.ofReal_coe_nnreal] using hbounded)

theorem AECover.integrable_of_lintegral_enorm_tendsto' [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : â„â‰¥0) (hfm : AEStronglyMeasurable f Î¼)
    (htendsto : Tendsto (fun i => âˆ«â» x in Ï† i, â€–f xâ€–â‚‘ âˆ‚Î¼) l (ğ“ I)) : Integrable f Î¼ :=
  hÏ†.integrable_of_lintegral_enorm_tendsto I hfm
    (by simpa only [ENNReal.ofReal_coe_nnreal] using htendsto)

theorem AECover.integrable_of_integral_norm_bounded [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : â„) (hfi : âˆ€ i, IntegrableOn f (Ï† i) Î¼)
    (hbounded : âˆ€á¶  i in l, (âˆ« x in Ï† i, â€–f xâ€– âˆ‚Î¼) â‰¤ I) : Integrable f Î¼ := by
  have hfm : AEStronglyMeasurable f Î¼ :=
    hÏ†.aestronglyMeasurable fun i => (hfi i).aestronglyMeasurable
  refine hÏ†.integrable_of_lintegral_enorm_bounded I hfm ?_
  conv at hbounded in integral _ _ =>
    rw [integral_eq_lintegral_of_nonneg_ae (ae_of_all _ fun x => @norm_nonneg E _ (f x))
        hfm.norm.restrict]
  conv at hbounded in ENNReal.ofReal _ =>
    rw [â† coe_nnnorm, ENNReal.ofReal_coe_nnreal]
  refine hbounded.mono fun i hi => ?_
  rw [â† ENNReal.ofReal_toReal <| ne_top_of_lt <| hasFiniteIntegral_iff_enorm.mp (hfi i).2]
  apply ENNReal.ofReal_le_ofReal hi

theorem AECover.integrable_of_integral_norm_tendsto [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : â„) (hfi : âˆ€ i, IntegrableOn f (Ï† i) Î¼)
    (htendsto : Tendsto (fun i => âˆ« x in Ï† i, â€–f xâ€– âˆ‚Î¼) l (ğ“ I)) : Integrable f Î¼ :=
  let âŸ¨I', hI'âŸ© := htendsto.isBoundedUnder_le
  hÏ†.integrable_of_integral_norm_bounded I' hfi hI'

theorem AECover.integrable_of_integral_bounded_of_nonneg_ae [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ â„} (I : â„) (hfi : âˆ€ i, IntegrableOn f (Ï† i) Î¼)
    (hnng : âˆ€áµ x âˆ‚Î¼, 0 â‰¤ f x) (hbounded : âˆ€á¶  i in l, (âˆ« x in Ï† i, f x âˆ‚Î¼) â‰¤ I) : Integrable f Î¼ :=
  hÏ†.integrable_of_integral_norm_bounded I hfi <| hbounded.mono fun _i hi =>
    (integral_congr_ae <| ae_restrict_of_ae <| hnng.mono fun _ => Real.norm_of_nonneg).le.trans hi

theorem AECover.integrable_of_integral_tendsto_of_nonneg_ae [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ â„} (I : â„) (hfi : âˆ€ i, IntegrableOn f (Ï† i) Î¼)
    (hnng : âˆ€áµ x âˆ‚Î¼, 0 â‰¤ f x) (htendsto : Tendsto (fun i => âˆ« x in Ï† i, f x âˆ‚Î¼) l (ğ“ I)) :
    Integrable f Î¼ :=
  let âŸ¨I', hI'âŸ© := htendsto.isBoundedUnder_le
  hÏ†.integrable_of_integral_bounded_of_nonneg_ae I' hfi hnng hI'

end Integrable

section Integral

variable {Î± Î¹ E : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±} {l : Filter Î¹} [NormedAddCommGroup E]
  [NormedSpace â„ E]

theorem AECover.integral_tendsto_of_countably_generated [l.IsCountablyGenerated] {Ï† : Î¹ â†’ Set Î±}
    (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (hfi : Integrable f Î¼) :
    Tendsto (fun i => âˆ« x in Ï† i, f x âˆ‚Î¼) l (ğ“ <| âˆ« x, f x âˆ‚Î¼) :=
  suffices h : Tendsto (fun i => âˆ« x : Î±, (Ï† i).indicator f x âˆ‚Î¼) l (ğ“ (âˆ« x : Î±, f x âˆ‚Î¼)) from by
    convert h using 2; rw [integral_indicator (hÏ†.measurableSet _)]
  tendsto_integral_filter_of_dominated_convergence (fun x => â€–f xâ€–)
    (Eventually.of_forall fun i => hfi.aestronglyMeasurable.indicator <| hÏ†.measurableSet i)
    (Eventually.of_forall fun _ => ae_of_all _ fun _ => norm_indicator_le_norm_self _ _) hfi.norm
    (hÏ†.ae_tendsto_indicator f)

/-- Slight reformulation of `MeasureTheory.AECover.integral_tendsto_of_countably_generated`. -/
theorem AECover.integral_eq_of_tendsto [l.NeBot] [l.IsCountablyGenerated] {Ï† : Î¹ â†’ Set Î±}
    (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ E} (I : E) (hfi : Integrable f Î¼)
    (h : Tendsto (fun n => âˆ« x in Ï† n, f x âˆ‚Î¼) l (ğ“ I)) : âˆ« x, f x âˆ‚Î¼ = I :=
  tendsto_nhds_unique (hÏ†.integral_tendsto_of_countably_generated hfi) h

theorem AECover.integral_eq_of_tendsto_of_nonneg_ae [l.NeBot] [l.IsCountablyGenerated]
    {Ï† : Î¹ â†’ Set Î±} (hÏ† : AECover Î¼ l Ï†) {f : Î± â†’ â„} (I : â„) (hnng : 0 â‰¤áµ[Î¼] f)
    (hfi : âˆ€ n, IntegrableOn f (Ï† n) Î¼) (htendsto : Tendsto (fun n => âˆ« x in Ï† n, f x âˆ‚Î¼) l (ğ“ I)) :
    âˆ« x, f x âˆ‚Î¼ = I :=
  have hfi' : Integrable f Î¼ := hÏ†.integrable_of_integral_tendsto_of_nonneg_ae I hfi hnng htendsto
  hÏ†.integral_eq_of_tendsto I hfi' htendsto

end Integral

section IntegrableOfIntervalIntegral

variable {Î¹ E : Type*} {Î¼ : Measure â„} {l : Filter Î¹} [Filter.NeBot l] [IsCountablyGenerated l]
  [NormedAddCommGroup E] {a b : Î¹ â†’ â„} {f : â„ â†’ E}

theorem integrable_of_intervalIntegral_norm_bounded (I : â„)
    (hfi : âˆ€ i, IntegrableOn f (Ioc (a i) (b i)) Î¼) (ha : Tendsto a l atBot)
    (hb : Tendsto b l atTop) (h : âˆ€á¶  i in l, (âˆ« x in a i..b i, â€–f xâ€– âˆ‚Î¼) â‰¤ I) : Integrable f Î¼ := by
  have hÏ† : AECover Î¼ l _ := aecover_Ioc ha hb
  refine hÏ†.integrable_of_integral_norm_bounded I hfi (h.mp ?_)
  filter_upwards [ha.eventually (eventually_le_atBot 0),
    hb.eventually (eventually_ge_atTop 0)] with i hai hbi ht
  rwa [â† intervalIntegral.integral_of_le (hai.trans hbi)]

/-- If `f` is integrable on intervals `Ioc (a i) (b i)`,
where `a i` tends to -âˆ and `b i` tends to âˆ, and
`âˆ« x in a i .. b i, â€–f xâ€– âˆ‚Î¼` converges to `I : â„` along a filter `l`,
then `f` is integrable on the interval (-âˆ, âˆ) -/
theorem integrable_of_intervalIntegral_norm_tendsto (I : â„)
    (hfi : âˆ€ i, IntegrableOn f (Ioc (a i) (b i)) Î¼) (ha : Tendsto a l atBot)
    (hb : Tendsto b l atTop) (h : Tendsto (fun i => âˆ« x in a i..b i, â€–f xâ€– âˆ‚Î¼) l (ğ“ I)) :
    Integrable f Î¼ :=
  let âŸ¨I', hI'âŸ© := h.isBoundedUnder_le
  integrable_of_intervalIntegral_norm_bounded I' hfi ha hb hI'

theorem integrableOn_Iic_of_intervalIntegral_norm_bounded (I b : â„)
    (hfi : âˆ€ i, IntegrableOn f (Ioc (a i) b) Î¼) (ha : Tendsto a l atBot)
    (h : âˆ€á¶  i in l, (âˆ« x in a i..b, â€–f xâ€– âˆ‚Î¼) â‰¤ I) : IntegrableOn f (Iic b) Î¼ := by
  have hÏ† : AECover (Î¼.restrict <| Iic b) l _ := aecover_Ioi ha
  have hfi : âˆ€ i, IntegrableOn f (Ioi (a i)) (Î¼.restrict <| Iic b) := by
    intro i
    rw [IntegrableOn, Measure.restrict_restrict (hÏ†.measurableSet i)]
    exact hfi i
  refine hÏ†.integrable_of_integral_norm_bounded I hfi (h.mp ?_)
  filter_upwards [ha.eventually (eventually_le_atBot b)] with i hai
  rw [intervalIntegral.integral_of_le hai, Measure.restrict_restrict (hÏ†.measurableSet i)]
  exact id

/-- If `f` is integrable on intervals `Ioc (a i) b`,
where `a i` tends to -âˆ, and
`âˆ« x in a i .. b, â€–f xâ€– âˆ‚Î¼` converges to `I : â„` along a filter `l`,
then `f` is integrable on the interval (-âˆ, b) -/
theorem integrableOn_Iic_of_intervalIntegral_norm_tendsto (I b : â„)
    (hfi : âˆ€ i, IntegrableOn f (Ioc (a i) b) Î¼) (ha : Tendsto a l atBot)
    (h : Tendsto (fun i => âˆ« x in a i..b, â€–f xâ€– âˆ‚Î¼) l (ğ“ I)) : IntegrableOn f (Iic b) Î¼ :=
  let âŸ¨I', hI'âŸ© := h.isBoundedUnder_le
  integrableOn_Iic_of_intervalIntegral_norm_bounded I' b hfi ha hI'

theorem integrableOn_Ioi_of_intervalIntegral_norm_bounded (I a : â„)
    (hfi : âˆ€ i, IntegrableOn f (Ioc a (b i)) Î¼) (hb : Tendsto b l atTop)
    (h : âˆ€á¶  i in l, (âˆ« x in a..b i, â€–f xâ€– âˆ‚Î¼) â‰¤ I) : IntegrableOn f (Ioi a) Î¼ := by
  have hÏ† : AECover (Î¼.restrict <| Ioi a) l _ := aecover_Iic hb
  have hfi : âˆ€ i, IntegrableOn f (Iic (b i)) (Î¼.restrict <| Ioi a) := by
    intro i
    rw [IntegrableOn, Measure.restrict_restrict (hÏ†.measurableSet i), inter_comm]
    exact hfi i
  refine hÏ†.integrable_of_integral_norm_bounded I hfi (h.mp ?_)
  filter_upwards [hb.eventually (eventually_ge_atTop a)] with i hbi
  rw [intervalIntegral.integral_of_le hbi, Measure.restrict_restrict (hÏ†.measurableSet i),
    inter_comm]
  exact id

/-- If `f` is integrable on intervals `Ioc a (b i)`,
where `b i` tends to âˆ, and
`âˆ« x in a .. b i, â€–f xâ€– âˆ‚Î¼` converges to `I : â„` along a filter `l`,
then `f` is integrable on the interval (a, âˆ) -/
theorem integrableOn_Ioi_of_intervalIntegral_norm_tendsto (I a : â„)
    (hfi : âˆ€ i, IntegrableOn f (Ioc a (b i)) Î¼) (hb : Tendsto b l atTop)
    (h : Tendsto (fun i => âˆ« x in a..b i, â€–f xâ€– âˆ‚Î¼) l (ğ“ <| I)) : IntegrableOn f (Ioi a) Î¼ :=
  let âŸ¨I', hI'âŸ© := h.isBoundedUnder_le
  integrableOn_Ioi_of_intervalIntegral_norm_bounded I' a hfi hb hI'

theorem integrableOn_Ioc_of_intervalIntegral_norm_bounded {I aâ‚€ bâ‚€ : â„}
    (hfi : âˆ€ i, IntegrableOn f <| Ioc (a i) (b i)) (ha : Tendsto a l <| ğ“ aâ‚€)
    (hb : Tendsto b l <| ğ“ bâ‚€) (h : âˆ€á¶  i in l, (âˆ« x in Ioc (a i) (b i), â€–f xâ€–) â‰¤ I) :
    IntegrableOn f (Ioc aâ‚€ bâ‚€) := by
  refine (aecover_Ioc_of_Ioc ha hb).integrable_of_integral_norm_bounded I
    (fun i => (hfi i).restrict) (h.mono fun i hi â†¦ ?_)
  rw [Measure.restrict_restrict measurableSet_Ioc]
  grw [â† hi]
  gcongr
  Â· apply ae_of_all
    simp
  Â· exact (hfi i).norm
  Â· exact inter_subset_left

theorem integrableOn_Ioc_of_intervalIntegral_norm_bounded_left {I aâ‚€ b : â„}
    (hfi : âˆ€ i, IntegrableOn f <| Ioc (a i) b) (ha : Tendsto a l <| ğ“ aâ‚€)
    (h : âˆ€á¶  i in l, (âˆ« x in Ioc (a i) b, â€–f xâ€–) â‰¤ I) : IntegrableOn f (Ioc aâ‚€ b) :=
  integrableOn_Ioc_of_intervalIntegral_norm_bounded hfi ha tendsto_const_nhds h

theorem integrableOn_Ioc_of_intervalIntegral_norm_bounded_right {I a bâ‚€ : â„}
    (hfi : âˆ€ i, IntegrableOn f <| Ioc a (b i)) (hb : Tendsto b l <| ğ“ bâ‚€)
    (h : âˆ€á¶  i in l, (âˆ« x in Ioc a (b i), â€–f xâ€–) â‰¤ I) : IntegrableOn f (Ioc a bâ‚€) :=
  integrableOn_Ioc_of_intervalIntegral_norm_bounded hfi tendsto_const_nhds hb h

end IntegrableOfIntervalIntegral

section IntegralOfIntervalIntegral

variable {Î¹ E : Type*} {Î¼ : Measure â„} {l : Filter Î¹} [IsCountablyGenerated l]
  [NormedAddCommGroup E] [NormedSpace â„ E] {a b : Î¹ â†’ â„} {f : â„ â†’ E}

theorem intervalIntegral_tendsto_integral (hfi : Integrable f Î¼) (ha : Tendsto a l atBot)
    (hb : Tendsto b l atTop) : Tendsto (fun i => âˆ« x in a i..b i, f x âˆ‚Î¼) l (ğ“ <| âˆ« x, f x âˆ‚Î¼) := by
  let Ï† i := Ioc (a i) (b i)
  have hÏ† : AECover Î¼ l Ï† := aecover_Ioc ha hb
  refine (hÏ†.integral_tendsto_of_countably_generated hfi).congr' ?_
  filter_upwards [ha.eventually (eventually_le_atBot 0),
    hb.eventually (eventually_ge_atTop 0)] with i hai hbi
  exact (intervalIntegral.integral_of_le (hai.trans hbi)).symm

theorem intervalIntegral_tendsto_integral_Iic (b : â„) (hfi : IntegrableOn f (Iic b) Î¼)
    (ha : Tendsto a l atBot) :
    Tendsto (fun i => âˆ« x in a i..b, f x âˆ‚Î¼) l (ğ“ <| âˆ« x in Iic b, f x âˆ‚Î¼) := by
  let Ï† i := Ioi (a i)
  have hÏ† : AECover (Î¼.restrict <| Iic b) l Ï† := aecover_Ioi ha
  refine (hÏ†.integral_tendsto_of_countably_generated hfi).congr' ?_
  filter_upwards [ha.eventually (eventually_le_atBot <| b)] with i hai
  rw [intervalIntegral.integral_of_le hai, Measure.restrict_restrict (hÏ†.measurableSet i)]
  rfl

theorem intervalIntegral_tendsto_integral_Ioi (a : â„) (hfi : IntegrableOn f (Ioi a) Î¼)
    (hb : Tendsto b l atTop) :
    Tendsto (fun i => âˆ« x in a..b i, f x âˆ‚Î¼) l (ğ“ <| âˆ« x in Ioi a, f x âˆ‚Î¼) := by
  let Ï† i := Iic (b i)
  have hÏ† : AECover (Î¼.restrict <| Ioi a) l Ï† := aecover_Iic hb
  refine (hÏ†.integral_tendsto_of_countably_generated hfi).congr' ?_
  filter_upwards [hb.eventually (eventually_ge_atTop <| a)] with i hbi
  rw [intervalIntegral.integral_of_le hbi, Measure.restrict_restrict (hÏ†.measurableSet i),
    inter_comm]
  rfl

end IntegralOfIntervalIntegral

open Real

open scoped Interval

section IoiFTC

variable {E : Type*} {f f' : â„ â†’ E} {g g' : â„ â†’ â„} {a l : â„} {m : E} [NormedAddCommGroup E]
  [NormedSpace â„ E]

/-- If the derivative of a function defined on the real line is integrable close to `+âˆ`, then
the function has a limit at `+âˆ`. -/
theorem tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi [CompleteSpace E]
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt f (f' x) x) (f'int : IntegrableOn f' (Ioi a)) :
    Tendsto f atTop (ğ“ (limUnder atTop f)) := by
  suffices âˆƒ a, Tendsto f atTop (ğ“ a) from tendsto_nhds_limUnder this
  suffices CauchySeq f from cauchySeq_tendsto_of_complete this
  apply Metric.cauchySeq_iff'.2 (fun Îµ Îµpos â†¦ ?_)
  have A : âˆ€á¶  (n : â„•) in atTop, âˆ« (x : â„) in Ici â†‘n, â€–f' xâ€– < Îµ := by
    have L : Tendsto (fun (n : â„•) â†¦ âˆ« x in Ici (n : â„), â€–f' xâ€–) atTop
        (ğ“ (âˆ« x in â‹‚ (n : â„•), Ici (n : â„), â€–f' xâ€–)) := by
      apply tendsto_setIntegral_of_antitone (fun n â†¦ measurableSet_Ici)
      Â· intro m n hmn
        exact Ici_subset_Ici.2 (Nat.cast_le.mpr hmn)
      Â· rcases exists_nat_gt a with âŸ¨n, hnâŸ©
        exact âŸ¨n, IntegrableOn.mono_set f'int.norm (Ici_subset_Ioi.2 hn)âŸ©
    have B : â‹‚ (n : â„•), Ici (n : â„) = âˆ… := by
      apply eq_empty_of_forall_notMem (fun x â†¦ ?_)
      simpa only [mem_iInter, mem_Ici, not_forall, not_le] using exists_nat_gt x
    simp only [B, Measure.restrict_empty, integral_zero_measure] at L
    exact (tendsto_order.1 L).2 _ Îµpos
  have B : âˆ€á¶  (n : â„•) in atTop, a < n := by
    rcases exists_nat_gt a with âŸ¨n, hnâŸ©
    filter_upwards [Ioi_mem_atTop n] with m (hm : n < m) using hn.trans (Nat.cast_lt.mpr hm)
  rcases (A.and B).exists with âŸ¨N, hN, h'NâŸ©
  refine âŸ¨N, fun x hx â†¦ ?_âŸ©
  calc
  dist (f x) (f â†‘N)
    = â€–f x - f Nâ€– := dist_eq_norm _ _
  _ = â€–âˆ« t in Ioc â†‘N x, f' tâ€– := by
      rw [â† intervalIntegral.integral_of_le hx, intervalIntegral.integral_eq_sub_of_hasDerivAt]
      Â· intro y hy
        simp only [hx, uIcc_of_le, mem_Icc] at hy
        exact hderiv _ (h'N.trans_le hy.1)
      Â· rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hx]
        exact f'int.mono_set (Ioc_subset_Ioi_self.trans (Ioi_subset_Ioi h'N.le))
  _ â‰¤ âˆ« t in Ioc â†‘N x, â€–f' tâ€– := norm_integral_le_integral_norm fun a â†¦ f' a
  _ â‰¤ âˆ« t in Ici â†‘N, â€–f' tâ€– := by
      apply setIntegral_mono_set
      Â· apply IntegrableOn.mono_set f'int.norm (Ici_subset_Ioi.2 h'N)
      Â· filter_upwards with x using norm_nonneg _
      Â· have : Ioc (â†‘N) x âŠ† Ici â†‘N := Ioc_subset_Ioi_self.trans Ioi_subset_Ici_self
        exact this.eventuallyLE
  _ < Îµ := hN

open UniformSpace in
/-- If a function and its derivative are integrable on `(a, +âˆ)`, then the function tends to zero
at `+âˆ`. -/
theorem tendsto_zero_of_hasDerivAt_of_integrableOn_Ioi
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt f (f' x) x)
    (f'int : IntegrableOn f' (Ioi a)) (fint : IntegrableOn f (Ioi a)) :
    Tendsto f atTop (ğ“ 0) := by
  let F : E â†’L[â„] Completion E := Completion.toComplL
  have Fderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt (F âˆ˜ f) (F (f' x)) x :=
    fun x hx â†¦ F.hasFDerivAt.comp_hasDerivAt _ (hderiv x hx)
  have Fint : IntegrableOn (F âˆ˜ f) (Ioi a) := by apply F.integrable_comp fint
  have F'int : IntegrableOn (F âˆ˜ f') (Ioi a) := by apply F.integrable_comp f'int
  have A : Tendsto (F âˆ˜ f) atTop (ğ“ (limUnder atTop (F âˆ˜ f))) := by
    apply tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi Fderiv F'int
  have B : limUnder atTop (F âˆ˜ f) = F 0 := by
    have : IntegrableAtFilter (F âˆ˜ f) atTop := by exact âŸ¨Ioi a, Ioi_mem_atTop _, FintâŸ©
    apply IntegrableAtFilter.eq_zero_of_tendsto this ?_ A
    intro s hs
    rcases mem_atTop_sets.1 hs with âŸ¨b, hbâŸ©
    rw [â† top_le_iff, â† volume_Ici (a := b)]
    exact measure_mono hb
  rwa [B, â† IsEmbedding.tendsto_nhds_iff] at A
  exact (Completion.isUniformEmbedding_coe E).isEmbedding

variable [CompleteSpace E]

/-- **Fundamental theorem of calculus-2**, on semi-infinite intervals `(a, +âˆ)`.
When a function has a limit at infinity `m`, and its derivative is integrable, then the
integral of the derivative on `(a, +âˆ)` is `m - f a`. Version assuming differentiability
on `(a, +âˆ)` and continuity at `aâº`.

Note that such a function always has a limit at infinity,
see `tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi`. -/
theorem integral_Ioi_of_hasDerivAt_of_tendsto (hcont : ContinuousWithinAt f (Ici a) a)
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt f (f' x) x) (f'int : IntegrableOn f' (Ioi a))
    (hf : Tendsto f atTop (ğ“ m)) : âˆ« x in Ioi a, f' x = m - f a := by
  have hcont : ContinuousOn f (Ici a) := by
    intro x hx
    rcases hx.out.eq_or_lt with rfl | hx
    Â· exact hcont
    Â· exact (hderiv x hx).continuousAt.continuousWithinAt
  refine tendsto_nhds_unique (intervalIntegral_tendsto_integral_Ioi a f'int tendsto_id) ?_
  apply Tendsto.congr' _ (hf.sub_const _)
  filter_upwards [Ioi_mem_atTop a] with x hx
  have h'x : a â‰¤ id x := le_of_lt hx
  symm
  apply
    intervalIntegral.integral_eq_sub_of_hasDerivAt_of_le h'x (hcont.mono Icc_subset_Ici_self)
      fun y hy => hderiv y hy.1
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le h'x]
  exact f'int.mono (fun y hy => hy.1) le_rfl

/-- **Fundamental theorem of calculus-2**, on semi-infinite intervals `(a, +âˆ)`.
When a function has a limit at infinity `m`, and its derivative is integrable, then the
integral of the derivative on `(a, +âˆ)` is `m - f a`. Version assuming differentiability
on `[a, +âˆ)`.

Note that such a function always has a limit at infinity,
see `tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi`. -/
theorem integral_Ioi_of_hasDerivAt_of_tendsto' (hderiv : âˆ€ x âˆˆ Ici a, HasDerivAt f (f' x) x)
    (f'int : IntegrableOn f' (Ioi a)) (hf : Tendsto f atTop (ğ“ m)) :
    âˆ« x in Ioi a, f' x = m - f a := by
  refine integral_Ioi_of_hasDerivAt_of_tendsto ?_ (fun x hx => hderiv x hx.out.le)
    f'int hf
  exact (hderiv a left_mem_Ici).continuousAt.continuousWithinAt

/-- A special case of `integral_Ioi_of_hasDerivAt_of_tendsto` where we assume that `f` is C^1 with
compact support. -/
theorem _root_.HasCompactSupport.integral_Ioi_deriv_eq (hf : ContDiff â„ 1 f)
    (h2f : HasCompactSupport f) (b : â„) : âˆ« x in Ioi b, deriv f x = - f b := by
  have := fun x (_ : x âˆˆ Ioi b) â†¦ hf.differentiable le_rfl x |>.hasDerivAt
  rw [integral_Ioi_of_hasDerivAt_of_tendsto hf.continuous.continuousWithinAt this, zero_sub]
  Â· refine hf.continuous_deriv le_rfl |>.integrable_of_hasCompactSupport h2f.deriv |>.integrableOn
  rw [hasCompactSupport_iff_eventuallyEq, Filter.coclosedCompact_eq_cocompact] at h2f
  exact h2f.filter_mono _root_.atTop_le_cocompact |>.tendsto

/-- When a function has a limit at infinity, and its derivative is nonnegative, then the derivative
is automatically integrable on `(a, +âˆ)`. Version assuming differentiability
on `(a, +âˆ)` and continuity at `aâº`. -/
theorem integrableOn_Ioi_deriv_of_nonneg (hcont : ContinuousWithinAt g (Ici a) a)
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt g (g' x) x) (g'pos : âˆ€ x âˆˆ Ioi a, 0 â‰¤ g' x)
    (hg : Tendsto g atTop (ğ“ l)) : IntegrableOn g' (Ioi a) := by
  have hcont : ContinuousOn g (Ici a) := by
    intro x hx
    rcases hx.out.eq_or_lt with rfl | hx
    Â· exact hcont
    Â· exact (hderiv x hx).continuousAt.continuousWithinAt
  refine integrableOn_Ioi_of_intervalIntegral_norm_tendsto (l - g a) a (fun x => ?_) tendsto_id ?_
  Â· exact intervalIntegral.integrableOn_deriv_of_nonneg (hcont.mono Icc_subset_Ici_self)
      (fun y hy => hderiv y hy.1) fun y hy => g'pos y hy.1
  apply Tendsto.congr' _ (hg.sub_const _)
  filter_upwards [Ioi_mem_atTop a] with x hx
  have h'x : a â‰¤ id x := le_of_lt hx
  calc
    g x - g a = âˆ« y in a..id x, g' y := by
      symm
      apply intervalIntegral.integral_eq_sub_of_hasDerivAt_of_le h'x
        (hcont.mono Icc_subset_Ici_self) fun y hy => hderiv y hy.1
      rw [intervalIntegrable_iff_integrableOn_Ioc_of_le h'x]
      exact intervalIntegral.integrableOn_deriv_of_nonneg (hcont.mono Icc_subset_Ici_self)
        (fun y hy => hderiv y hy.1) fun y hy => g'pos y hy.1
    _ = âˆ« y in a..id x, â€–g' yâ€– := by
      simp_rw [intervalIntegral.integral_of_le h'x]
      refine setIntegral_congr_fun measurableSet_Ioc fun y hy => ?_
      dsimp
      rw [abs_of_nonneg]
      exact g'pos _ hy.1

/-- When a function has a limit at infinity, and its derivative is nonnegative, then the derivative
is automatically integrable on `(a, +âˆ)`. Version assuming differentiability
on `[a, +âˆ)`. -/
theorem integrableOn_Ioi_deriv_of_nonneg' (hderiv : âˆ€ x âˆˆ Ici a, HasDerivAt g (g' x) x)
    (g'pos : âˆ€ x âˆˆ Ioi a, 0 â‰¤ g' x) (hg : Tendsto g atTop (ğ“ l)) : IntegrableOn g' (Ioi a) := by
  refine integrableOn_Ioi_deriv_of_nonneg ?_ (fun x hx => hderiv x hx.out.le) g'pos hg
  exact (hderiv a left_mem_Ici).continuousAt.continuousWithinAt

/-- When a function has a limit at infinity `l`, and its derivative is nonnegative, then the
integral of the derivative on `(a, +âˆ)` is `l - g a` (and the derivative is integrable, see
`integrable_on_Ioi_deriv_of_nonneg`). Version assuming differentiability on `(a, +âˆ)` and
continuity at `aâº`. -/
theorem integral_Ioi_of_hasDerivAt_of_nonneg (hcont : ContinuousWithinAt g (Ici a) a)
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt g (g' x) x) (g'pos : âˆ€ x âˆˆ Ioi a, 0 â‰¤ g' x)
    (hg : Tendsto g atTop (ğ“ l)) : âˆ« x in Ioi a, g' x = l - g a :=
  integral_Ioi_of_hasDerivAt_of_tendsto hcont hderiv
    (integrableOn_Ioi_deriv_of_nonneg hcont hderiv g'pos hg) hg

/-- When a function has a limit at infinity `l`, and its derivative is nonnegative, then the
integral of the derivative on `(a, +âˆ)` is `l - g a` (and the derivative is integrable, see
`integrable_on_Ioi_deriv_of_nonneg'`). Version assuming differentiability on `[a, +âˆ)`. -/
theorem integral_Ioi_of_hasDerivAt_of_nonneg' (hderiv : âˆ€ x âˆˆ Ici a, HasDerivAt g (g' x) x)
    (g'pos : âˆ€ x âˆˆ Ioi a, 0 â‰¤ g' x) (hg : Tendsto g atTop (ğ“ l)) : âˆ« x in Ioi a, g' x = l - g a :=
  integral_Ioi_of_hasDerivAt_of_tendsto' hderiv (integrableOn_Ioi_deriv_of_nonneg' hderiv g'pos hg)
    hg

/-- When a function has a limit at infinity, and its derivative is nonpositive, then the derivative
is automatically integrable on `(a, +âˆ)`. Version assuming differentiability
on `(a, +âˆ)` and continuity at `aâº`. -/
theorem integrableOn_Ioi_deriv_of_nonpos (hcont : ContinuousWithinAt g (Ici a) a)
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt g (g' x) x) (g'neg : âˆ€ x âˆˆ Ioi a, g' x â‰¤ 0)
    (hg : Tendsto g atTop (ğ“ l)) : IntegrableOn g' (Ioi a) := by
  apply integrable_neg_iff.1
  exact integrableOn_Ioi_deriv_of_nonneg hcont.neg (fun x hx => (hderiv x hx).neg)
    (fun x hx => neg_nonneg_of_nonpos (g'neg x hx)) hg.neg

/-- When a function has a limit at infinity, and its derivative is nonpositive, then the derivative
is automatically integrable on `(a, +âˆ)`. Version assuming differentiability
on `[a, +âˆ)`. -/
theorem integrableOn_Ioi_deriv_of_nonpos' (hderiv : âˆ€ x âˆˆ Ici a, HasDerivAt g (g' x) x)
    (g'neg : âˆ€ x âˆˆ Ioi a, g' x â‰¤ 0) (hg : Tendsto g atTop (ğ“ l)) : IntegrableOn g' (Ioi a) := by
  refine integrableOn_Ioi_deriv_of_nonpos ?_ (fun x hx â†¦ hderiv x hx.out.le) g'neg hg
  exact (hderiv a left_mem_Ici).continuousAt.continuousWithinAt

/-- When a function has a limit at infinity `l`, and its derivative is nonpositive, then the
integral of the derivative on `(a, +âˆ)` is `l - g a` (and the derivative is integrable, see
`integrable_on_Ioi_deriv_of_nonneg`). Version assuming differentiability on `(a, +âˆ)` and
continuity at `aâº`. -/
theorem integral_Ioi_of_hasDerivAt_of_nonpos (hcont : ContinuousWithinAt g (Ici a) a)
    (hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt g (g' x) x) (g'neg : âˆ€ x âˆˆ Ioi a, g' x â‰¤ 0)
    (hg : Tendsto g atTop (ğ“ l)) : âˆ« x in Ioi a, g' x = l - g a :=
  integral_Ioi_of_hasDerivAt_of_tendsto hcont hderiv
    (integrableOn_Ioi_deriv_of_nonpos hcont hderiv g'neg hg) hg

/-- When a function has a limit at infinity `l`, and its derivative is nonpositive, then the
integral of the derivative on `(a, +âˆ)` is `l - g a` (and the derivative is integrable, see
`integrable_on_Ioi_deriv_of_nonneg'`). Version assuming differentiability on `[a, +âˆ)`. -/
theorem integral_Ioi_of_hasDerivAt_of_nonpos' (hderiv : âˆ€ x âˆˆ Ici a, HasDerivAt g (g' x) x)
    (g'neg : âˆ€ x âˆˆ Ioi a, g' x â‰¤ 0) (hg : Tendsto g atTop (ğ“ l)) : âˆ« x in Ioi a, g' x = l - g a :=
  integral_Ioi_of_hasDerivAt_of_tendsto' hderiv (integrableOn_Ioi_deriv_of_nonpos' hderiv g'neg hg)
    hg

end IoiFTC

section IicFTC

variable {E : Type*} {f f' : â„ â†’ E} {a : â„} {m : E} [NormedAddCommGroup E]
  [NormedSpace â„ E]

/-- If the derivative of a function defined on the real line is integrable close to `-âˆ`, then
the function has a limit at `-âˆ`. -/
theorem tendsto_limUnder_of_hasDerivAt_of_integrableOn_Iic [CompleteSpace E]
    (hderiv : âˆ€ x âˆˆ Iic a, HasDerivAt f (f' x) x) (f'int : IntegrableOn f' (Iic a)) :
    Tendsto f atBot (ğ“ (limUnder atBot f)) := by
  suffices âˆƒ a, Tendsto f atBot (ğ“ a) from tendsto_nhds_limUnder this
  let g := f âˆ˜ (fun x â†¦ -x)
  have hdg : âˆ€ x âˆˆ Ioi (-a), HasDerivAt g (-f' (-x)) x := by
    intro x hx
    have : -x âˆˆ Iic a := by simp only [mem_Iic, mem_Ioi, neg_le] at *; exact hx.le
    simpa using HasDerivAt.scomp x (hderiv (-x) this) (hasDerivAt_neg' x)
  have L : Tendsto g atTop (ğ“ (limUnder atTop g)) := by
    apply tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi hdg
    exact ((MeasurePreserving.integrableOn_comp_preimage (Measure.measurePreserving_neg _)
      (Homeomorph.neg â„).measurableEmbedding).2 f'int.neg).mono_set (by simp)
  refine âŸ¨limUnder atTop g, ?_âŸ©
  have : Tendsto (fun x â†¦ g (-x)) atBot (ğ“ (limUnder atTop g)) := L.comp tendsto_neg_atBot_atTop
  simpa [g] using this

open UniformSpace in
/-- If a function and its derivative are integrable on `(-âˆ, a]`, then the function tends to zero
at `-âˆ`. -/
theorem tendsto_zero_of_hasDerivAt_of_integrableOn_Iic
    (hderiv : âˆ€ x âˆˆ Iic a, HasDerivAt f (f' x) x)
    (f'int : IntegrableOn f' (Iic a)) (fint : IntegrableOn f (Iic a)) :
    Tendsto f atBot (ğ“ 0) := by
  let F : E â†’L[â„] Completion E := Completion.toComplL
  have Fderiv : âˆ€ x âˆˆ Iic a, HasDerivAt (F âˆ˜ f) (F (f' x)) x :=
    fun x hx â†¦ F.hasFDerivAt.comp_hasDerivAt _ (hderiv x hx)
  have Fint : IntegrableOn (F âˆ˜ f) (Iic a) := by apply F.integrable_comp fint
  have F'int : IntegrableOn (F âˆ˜ f') (Iic a) := by apply F.integrable_comp f'int
  have A : Tendsto (F âˆ˜ f) atBot (ğ“ (limUnder atBot (F âˆ˜ f))) := by
    apply tendsto_limUnder_of_hasDerivAt_of_integrableOn_Iic Fderiv F'int
  have B : limUnder atBot (F âˆ˜ f) = F 0 := by
    have : IntegrableAtFilter (F âˆ˜ f) atBot := by exact âŸ¨Iic a, Iic_mem_atBot _, FintâŸ©
    apply IntegrableAtFilter.eq_zero_of_tendsto this ?_ A
    intro s hs
    rcases mem_atBot_sets.1 hs with âŸ¨b, hbâŸ©
    apply le_antisymm (le_top)
    rw [â† volume_Iic (a := b)]
    exact measure_mono hb
  rwa [B, â† IsEmbedding.tendsto_nhds_iff] at A
  exact (Completion.isUniformEmbedding_coe E).isEmbedding

variable [CompleteSpace E]

/-- **Fundamental theorem of calculus-2**, on semi-infinite intervals `(-âˆ, a)`.
When a function has a limit `m` at `-âˆ`, and its derivative is integrable, then the
integral of the derivative on `(-âˆ, a)` is `f a - m`. Version assuming differentiability
on `(-âˆ, a)` and continuity at `aâ»`.

Note that such a function always has a limit at minus infinity,
see `tendsto_limUnder_of_hasDerivAt_of_integrableOn_Iic`. -/
theorem integral_Iic_of_hasDerivAt_of_tendsto (hcont : ContinuousWithinAt f (Iic a) a)
    (hderiv : âˆ€ x âˆˆ Iio a, HasDerivAt f (f' x) x) (f'int : IntegrableOn f' (Iic a))
    (hf : Tendsto f atBot (ğ“ m)) : âˆ« x in Iic a, f' x = f a - m := by
  have hcont : ContinuousOn f (Iic a) := by
    intro x hx
    rcases hx.out.eq_or_lt with rfl | hx
    Â· exact hcont
    Â· exact (hderiv x hx).continuousAt.continuousWithinAt
  refine tendsto_nhds_unique (intervalIntegral_tendsto_integral_Iic a f'int tendsto_id) ?_
  apply Tendsto.congr' _ (hf.const_sub _)
  filter_upwards [Iic_mem_atBot a] with x hx
  symm
  apply intervalIntegral.integral_eq_sub_of_hasDerivAt_of_le hx
    (hcont.mono Icc_subset_Iic_self) fun y hy => hderiv y hy.2
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hx]
  exact f'int.mono (fun y hy => hy.2) le_rfl

/-- **Fundamental theorem of calculus-2**, on semi-infinite intervals `(-âˆ, a)`.
When a function has a limit `m` at `-âˆ`, and its derivative is integrable, then the
integral of the derivative on `(-âˆ, a)` is `f a - m`. Version assuming differentiability
on `(-âˆ, a]`.

Note that such a function always has a limit at minus infinity,
see `tendsto_limUnder_of_hasDerivAt_of_integrableOn_Iic`. -/
theorem integral_Iic_of_hasDerivAt_of_tendsto'
    (hderiv : âˆ€ x âˆˆ Iic a, HasDerivAt f (f' x) x) (f'int : IntegrableOn f' (Iic a))
    (hf : Tendsto f atBot (ğ“ m)) : âˆ« x in Iic a, f' x = f a - m := by
  refine integral_Iic_of_hasDerivAt_of_tendsto ?_ (fun x hx => hderiv x hx.out.le)
    f'int hf
  exact (hderiv a right_mem_Iic).continuousAt.continuousWithinAt

/-- A special case of `integral_Iic_of_hasDerivAt_of_tendsto` where we assume that `f` is C^1 with
compact support. -/
theorem _root_.HasCompactSupport.integral_Iic_deriv_eq (hf : ContDiff â„ 1 f)
    (h2f : HasCompactSupport f) (b : â„) : âˆ« x in Iic b, deriv f x = f b := by
  have := fun x (_ : x âˆˆ Iio b) â†¦ hf.differentiable le_rfl x |>.hasDerivAt
  rw [integral_Iic_of_hasDerivAt_of_tendsto hf.continuous.continuousWithinAt this, sub_zero]
  Â· refine hf.continuous_deriv le_rfl |>.integrable_of_hasCompactSupport h2f.deriv |>.integrableOn
  rw [hasCompactSupport_iff_eventuallyEq, Filter.coclosedCompact_eq_cocompact] at h2f
  exact h2f.filter_mono _root_.atBot_le_cocompact |>.tendsto

open UniformSpace in
lemma _root_.HasCompactSupport.enorm_le_lintegral_Ici_deriv
    {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
    {f : â„ â†’ F} (hf : ContDiff â„ 1 f) (h'f : HasCompactSupport f) (x : â„) :
    â€–f xâ€–â‚‘ â‰¤ âˆ«â» y in Iic x, â€–deriv f yâ€–â‚‘ := by
  let I : F â†’L[â„] Completion F := Completion.toComplL
  let f' : â„ â†’ Completion F := I âˆ˜ f
  have hf' : ContDiff â„ 1 f' := hf.continuousLinearMap_comp I
  have h'f' : HasCompactSupport f' := h'f.comp_left rfl
  have : â€–f' xâ€–â‚‘ â‰¤ âˆ«â» y in Iic x, â€–deriv f' yâ€–â‚‘ := by
    rw [â† HasCompactSupport.integral_Iic_deriv_eq hf' h'f' x]
    exact enorm_integral_le_lintegral_enorm _
  convert this with y
  Â· simp [f', I, Completion.enorm_coe]
  Â· rw [fderiv_comp_deriv _ I.differentiableAt (hf.differentiable le_rfl _)]
    simp only [ContinuousLinearMap.fderiv]
    simp [I]

end IicFTC

section UnivFTC

variable {E : Type*} {f f' : â„ â†’ E} {m n : E} [NormedAddCommGroup E]
  [NormedSpace â„ E]

/-- **Fundamental theorem of calculus-2**, on the whole real line
When a function has a limit `m` at `-âˆ` and `n` at `+âˆ`, and its derivative is integrable, then the
integral of the derivative is `n - m`.

Note that such a function always has a limit at `-âˆ` and `+âˆ`,
see `tendsto_limUnder_of_hasDerivAt_of_integrableOn_Iic` and
`tendsto_limUnder_of_hasDerivAt_of_integrableOn_Ioi`. -/
theorem integral_of_hasDerivAt_of_tendsto [CompleteSpace E]
    (hderiv : âˆ€ x, HasDerivAt f (f' x) x) (hf' : Integrable f')
    (hbot : Tendsto f atBot (ğ“ m)) (htop : Tendsto f atTop (ğ“ n)) : âˆ« x, f' x = n - m := by
  rw [â† setIntegral_univ, â† Set.Iic_union_Ioi (a := 0),
    setIntegral_union (Iic_disjoint_Ioi le_rfl) measurableSet_Ioi hf'.integrableOn hf'.integrableOn,
    integral_Iic_of_hasDerivAt_of_tendsto' (fun x _ â†¦ hderiv x) hf'.integrableOn hbot,
    integral_Ioi_of_hasDerivAt_of_tendsto' (fun x _ â†¦ hderiv x) hf'.integrableOn htop]
  abel

/-- If a function and its derivative are integrable on the real line, then the integral of the
derivative is zero. -/
theorem integral_eq_zero_of_hasDerivAt_of_integrable
    (hderiv : âˆ€ x, HasDerivAt f (f' x) x) (hf' : Integrable f') (hf : Integrable f) :
    âˆ« x, f' x = 0 := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE]
  have A : Tendsto f atBot (ğ“ 0) :=
    tendsto_zero_of_hasDerivAt_of_integrableOn_Iic (a := 0) (fun x _hx â†¦ hderiv x)
      hf'.integrableOn hf.integrableOn
  have B : Tendsto f atTop (ğ“ 0) :=
    tendsto_zero_of_hasDerivAt_of_integrableOn_Ioi (a := 0) (fun x _hx â†¦ hderiv x)
      hf'.integrableOn hf.integrableOn
  simpa using integral_of_hasDerivAt_of_tendsto hderiv hf' A B

end UnivFTC

section IoiChangeVariables

open Real

open scoped Interval

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

/-- Change-of-variables formula for `Ioi` integrals of vector-valued functions, proved by taking
limits from the result for finite intervals. -/
theorem integral_comp_smul_deriv_Ioi {f f' : â„ â†’ â„} {g : â„ â†’ E} {a : â„}
    (hf : ContinuousOn f <| Ici a) (hft : Tendsto f atTop atTop)
    (hff' : âˆ€ x âˆˆ Ioi a, HasDerivWithinAt f (f' x) (Ioi x) x)
    (hg_cont : ContinuousOn g <| f '' Ioi a) (hg1 : IntegrableOn g <| f '' Ici a)
    (hg2 : IntegrableOn (fun x => f' x â€¢ (g âˆ˜ f) x) (Ici a)) :
    (âˆ« x in Ioi a, f' x â€¢ (g âˆ˜ f) x) = âˆ« u in Ioi (f a), g u := by
  have eq : âˆ€ b : â„, a < b â†’ (âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x) = âˆ« u in f a..f b, g u := fun b hb â†¦ by
    have i1 : Ioo (min a b) (max a b) âŠ† Ioi a := by
      rw [min_eq_left hb.le]
      exact Ioo_subset_Ioi_self
    have i2 : [[a, b]] âŠ† Ici a := by rw [uIcc_of_le hb.le]; exact Icc_subset_Ici_self
    refine
      intervalIntegral.integral_comp_smul_deriv''' (hf.mono i2)
        (fun x hx => hff' x <| mem_of_mem_of_subset hx i1) (hg_cont.mono <| image_mono ?_)
        (hg1.mono_set <| image_mono ?_) (hg2.mono_set i2) <;> assumption
  rw [integrableOn_Ici_iff_integrableOn_Ioi] at hg2
  have t2 := intervalIntegral_tendsto_integral_Ioi _ hg2 tendsto_id
  have : Ioi (f a) âŠ† f '' Ici a :=
    Ioi_subset_Ici_self.trans <|
      IsPreconnected.intermediate_value_Ici isPreconnected_Ici left_mem_Ici
        (le_principal_iff.mpr <| Ici_mem_atTop _) hf hft
  have t1 := (intervalIntegral_tendsto_integral_Ioi _ (hg1.mono_set this) tendsto_id).comp hft
  exact tendsto_nhds_unique (Tendsto.congr' (eventuallyEq_of_mem (Ioi_mem_atTop a) eq) t2) t1

/-- Change-of-variables formula for `Ioi` integrals of scalar-valued functions -/
theorem integral_comp_mul_deriv_Ioi {f f' : â„ â†’ â„} {g : â„ â†’ â„} {a : â„}
    (hf : ContinuousOn f <| Ici a) (hft : Tendsto f atTop atTop)
    (hff' : âˆ€ x âˆˆ Ioi a, HasDerivWithinAt f (f' x) (Ioi x) x)
    (hg_cont : ContinuousOn g <| f '' Ioi a) (hg1 : IntegrableOn g <| f '' Ici a)
    (hg2 : IntegrableOn (fun x => (g âˆ˜ f) x * f' x) (Ici a)) :
    (âˆ« x in Ioi a, (g âˆ˜ f) x * f' x) = âˆ« u in Ioi (f a), g u := by
  have hg2' : IntegrableOn (fun x => f' x â€¢ (g âˆ˜ f) x) (Ici a) := by simpa [mul_comm] using hg2
  simpa [mul_comm] using integral_comp_smul_deriv_Ioi hf hft hff' hg_cont hg1 hg2'

/-- Substitution `y = x ^ p` in integrals over `Ioi 0` -/
theorem integral_comp_rpow_Ioi (g : â„ â†’ E) {p : â„} (hp : p â‰  0) :
    (âˆ« x in Ioi 0, (|p| * x ^ (p - 1)) â€¢ g (x ^ p)) = âˆ« y in Ioi 0, g y := by
  let S := Ioi (0 : â„)
  have a1 : âˆ€ x : â„, x âˆˆ S â†’ HasDerivWithinAt (fun t : â„ => t ^ p) (p * x ^ (p - 1)) S x :=
    fun x hx => (hasDerivAt_rpow_const (Or.inl (mem_Ioi.mp hx).ne')).hasDerivWithinAt
  have a2 : InjOn (fun x : â„ => x ^ p) S := by
    rcases lt_or_gt_of_ne hp with (h | h)
    Â· apply StrictAntiOn.injOn
      intro x hx y hy hxy
      rw [â† inv_lt_invâ‚€ (rpow_pos_of_pos hx p) (rpow_pos_of_pos hy p), â† rpow_neg (le_of_lt hx),
        â† rpow_neg (le_of_lt hy)]
      exact rpow_lt_rpow (le_of_lt hx) hxy (neg_pos.mpr h)
    exact StrictMonoOn.injOn fun x hx y _ hxy => rpow_lt_rpow (mem_Ioi.mp hx).le hxy h
  have a3 : (fun t : â„ => t ^ p) '' S = S := by
    ext1 x; rw [mem_image]; constructor
    Â· rintro âŸ¨y, hy, rflâŸ©; exact rpow_pos_of_pos hy p
    Â· intro hx; refine âŸ¨x ^ (1 / p), rpow_pos_of_pos hx _, ?_âŸ©
      rw [â† rpow_mul (le_of_lt hx), one_div_mul_cancel hp, rpow_one]
  have := integral_image_eq_integral_abs_deriv_smul measurableSet_Ioi a1 a2 g
  rw [a3] at this; rw [this]
  refine setIntegral_congr_fun measurableSet_Ioi ?_
  intro x hx; dsimp only
  rw [abs_mul, abs_of_nonneg (rpow_nonneg (le_of_lt hx) _)]

theorem integral_comp_rpow_Ioi_of_pos {g : â„ â†’ E} {p : â„} (hp : 0 < p) :
    (âˆ« x in Ioi 0, (p * x ^ (p - 1)) â€¢ g (x ^ p)) = âˆ« y in Ioi 0, g y := by
  convert integral_comp_rpow_Ioi g hp.ne'
  rw [abs_of_nonneg hp.le]

theorem integral_comp_mul_left_Ioi (g : â„ â†’ E) (a : â„) {b : â„} (hb : 0 < b) :
    (âˆ« x in Ioi a, g (b * x)) = bâ»Â¹ â€¢ âˆ« x in Ioi (b * a), g x := by
  have : âˆ€ c : â„, MeasurableSet (Ioi c) := fun c => measurableSet_Ioi
  rw [â† integral_indicator (this a), â† integral_indicator (this (b * a)),
    â† abs_of_pos (inv_pos.mpr hb), â† Measure.integral_comp_mul_left]
  congr
  ext1 x
  rw [â† indicator_comp_right, preimage_const_mul_Ioi _ hb, mul_div_cancel_leftâ‚€ _ hb.ne',
    Function.comp_def]

theorem integral_comp_mul_right_Ioi (g : â„ â†’ E) (a : â„) {b : â„} (hb : 0 < b) :
    (âˆ« x in Ioi a, g (x * b)) = bâ»Â¹ â€¢ âˆ« x in Ioi (a * b), g x := by
  simpa only [mul_comm] using integral_comp_mul_left_Ioi g a hb

end IoiChangeVariables

section IoiIntegrability

open Real

open scoped Interval

variable {E : Type*} [NormedAddCommGroup E]

/-- The substitution `y = x ^ p` in integrals over `Ioi 0` preserves integrability. -/
theorem integrableOn_Ioi_comp_rpow_iff [NormedSpace â„ E] (f : â„ â†’ E) {p : â„} (hp : p â‰  0) :
    IntegrableOn (fun x => (|p| * x ^ (p - 1)) â€¢ f (x ^ p)) (Ioi 0) â†” IntegrableOn f (Ioi 0) := by
  let S := Ioi (0 : â„)
  have a1 : âˆ€ x : â„, x âˆˆ S â†’ HasDerivWithinAt (fun t : â„ => t ^ p) (p * x ^ (p - 1)) S x :=
    fun x hx => (hasDerivAt_rpow_const (Or.inl (mem_Ioi.mp hx).ne')).hasDerivWithinAt
  have a2 : InjOn (fun x : â„ => x ^ p) S := by
    rcases lt_or_gt_of_ne hp with (h | h)
    Â· apply StrictAntiOn.injOn
      intro x hx y hy hxy
      rw [â† inv_lt_invâ‚€ (rpow_pos_of_pos hx p) (rpow_pos_of_pos hy p), â† rpow_neg (le_of_lt hx), â†
        rpow_neg (le_of_lt hy)]
      exact rpow_lt_rpow (le_of_lt hx) hxy (neg_pos.mpr h)
    exact StrictMonoOn.injOn fun x hx y _hy hxy => rpow_lt_rpow (mem_Ioi.mp hx).le hxy h
  have a3 : (fun t : â„ => t ^ p) '' S = S := by
    ext1 x; rw [mem_image]; constructor
    Â· rintro âŸ¨y, hy, rflâŸ©; exact rpow_pos_of_pos hy p
    Â· intro hx; refine âŸ¨x ^ (1 / p), rpow_pos_of_pos hx _, ?_âŸ©
      rw [â† rpow_mul (le_of_lt hx), one_div_mul_cancel hp, rpow_one]
  have := integrableOn_image_iff_integrableOn_abs_deriv_smul measurableSet_Ioi a1 a2 f
  rw [a3] at this
  rw [this]
  refine integrableOn_congr_fun (fun x hx => ?_) measurableSet_Ioi
  simp_rw [abs_mul, abs_of_nonneg (rpow_nonneg (le_of_lt hx) _)]

/-- The substitution `y = x ^ p` in integrals over `Ioi 0` preserves integrability (version
without `|p|` factor) -/
theorem integrableOn_Ioi_comp_rpow_iff' [NormedSpace â„ E] (f : â„ â†’ E) {p : â„} (hp : p â‰  0) :
    IntegrableOn (fun x => x ^ (p - 1) â€¢ f (x ^ p)) (Ioi 0) â†” IntegrableOn f (Ioi 0) := by
  simpa only [â† integrableOn_Ioi_comp_rpow_iff f hp, mul_smul] using
    (integrable_smul_iff (abs_pos.mpr hp).ne' _).symm

theorem integrableOn_Ioi_comp_mul_left_iff (f : â„ â†’ E) (c : â„) {a : â„} (ha : 0 < a) :
    IntegrableOn (fun x => f (a * x)) (Ioi c) â†” IntegrableOn f (Ioi <| a * c) := by
  rw [â† integrable_indicator_iff (measurableSet_Ioi : MeasurableSet <| Ioi c)]
  rw [â† integrable_indicator_iff (measurableSet_Ioi : MeasurableSet <| Ioi <| a * c)]
  convert integrable_comp_mul_left_iff ((Ioi (a * c)).indicator f) ha.ne' using 2
  ext1 x
  rw [â† indicator_comp_right, preimage_const_mul_Ioi _ ha, mul_comm a c,
    mul_div_cancel_rightâ‚€ _ ha.ne', Function.comp_def]

theorem integrableOn_Ioi_comp_mul_right_iff (f : â„ â†’ E) (c : â„) {a : â„} (ha : 0 < a) :
    IntegrableOn (fun x => f (x * a)) (Ioi c) â†” IntegrableOn f (Ioi <| c * a) := by
  simpa only [mul_comm, mul_zero] using integrableOn_Ioi_comp_mul_left_iff f c ha

end IoiIntegrability

/-!
## Integration by parts
-/

section IntegrationByPartsBilinear

variable {E F G : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [NormedAddCommGroup G] [NormedSpace â„ G]
  {L : E â†’L[â„] F â†’L[â„] G} {u : â„ â†’ E} {v : â„ â†’ F} {u' : â„ â†’ E} {v' : â„ â†’ F}
  {m n : G}

theorem integral_bilinear_hasDerivAt_eq_sub [CompleteSpace G]
    (hu : âˆ€ x, HasDerivAt u (u' x) x) (hv : âˆ€ x, HasDerivAt v (v' x) x)
    (huv : Integrable (fun x â†¦ L (u x) (v' x) + L (u' x) (v x)))
    (h_bot : Tendsto (fun x â†¦ L (u x) (v x)) atBot (ğ“ m))
    (h_top : Tendsto (fun x â†¦ L (u x) (v x)) atTop (ğ“ n)) :
    âˆ« (x : â„), L (u x) (v' x) + L (u' x) (v x) = n - m :=
  integral_of_hasDerivAt_of_tendsto (fun x â†¦ L.hasDerivAt_of_bilinear (hu x) (hv x))
    huv h_bot h_top

/-- **Integration by parts on (-âˆ, âˆ).**
With respect to a general bilinear form. For the specific case of multiplication, see
`integral_mul_deriv_eq_deriv_mul`. -/
theorem integral_bilinear_hasDerivAt_right_eq_sub [CompleteSpace G]
    (hu : âˆ€ x, HasDerivAt u (u' x) x) (hv : âˆ€ x, HasDerivAt v (v' x) x)
    (huv' : Integrable (fun x â†¦ L (u x) (v' x))) (hu'v : Integrable (fun x â†¦ L (u' x) (v x)))
    (h_bot : Tendsto (fun x â†¦ L (u x) (v x)) atBot (ğ“ m))
    (h_top : Tendsto (fun x â†¦ L (u x) (v x)) atTop (ğ“ n)) :
    âˆ« (x : â„), L (u x) (v' x) = n - m - âˆ« (x : â„), L (u' x) (v x) := by
  rw [eq_sub_iff_add_eq, â† integral_add huv' hu'v]
  exact integral_bilinear_hasDerivAt_eq_sub hu hv (huv'.add hu'v) h_bot h_top

/-- **Integration by parts on (-âˆ, âˆ).**
With respect to a general bilinear form, assuming moreover that the total function is integrable.
-/
theorem integral_bilinear_hasDerivAt_right_eq_neg_left_of_integrable
    (hu : âˆ€ x, HasDerivAt u (u' x) x) (hv : âˆ€ x, HasDerivAt v (v' x) x)
    (huv' : Integrable (fun x â†¦ L (u x) (v' x))) (hu'v : Integrable (fun x â†¦ L (u' x) (v x)))
    (huv : Integrable (fun x â†¦ L (u x) (v x))) :
    âˆ« (x : â„), L (u x) (v' x) = - âˆ« (x : â„), L (u' x) (v x) := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [integral, hG]
  have I : Tendsto (fun x â†¦ L (u x) (v x)) atBot (ğ“ 0) :=
    tendsto_zero_of_hasDerivAt_of_integrableOn_Iic (a := 0)
      (fun x _hx â†¦ L.hasDerivAt_of_bilinear (hu x) (hv x))
      (huv'.add hu'v).integrableOn huv.integrableOn
  have J : Tendsto (fun x â†¦ L (u x) (v x)) atTop (ğ“ 0) :=
    tendsto_zero_of_hasDerivAt_of_integrableOn_Ioi (a := 0)
      (fun x _hx â†¦ L.hasDerivAt_of_bilinear (hu x) (hv x))
      (huv'.add hu'v).integrableOn huv.integrableOn
  simp [integral_bilinear_hasDerivAt_right_eq_sub hu hv huv' hu'v I J]

end IntegrationByPartsBilinear

section IntegrationByPartsAlgebra

variable {A : Type*} [NormedRing A] [NormedAlgebra â„ A]
  {a : â„} {a' b' : A} {u : â„ â†’ A} {v : â„ â†’ A} {u' : â„ â†’ A} {v' : â„ â†’ A}

/-- For finite intervals, see: `intervalIntegral.integral_deriv_mul_eq_sub`. -/
theorem integral_deriv_mul_eq_sub [CompleteSpace A]
    (hu : âˆ€ x, HasDerivAt u (u' x) x) (hv : âˆ€ x, HasDerivAt v (v' x) x)
    (huv : Integrable (u' * v + u * v'))
    (h_bot : Tendsto (u * v) atBot (ğ“ a')) (h_top : Tendsto (u * v) atTop (ğ“ b')) :
    âˆ« (x : â„), u' x * v x + u x * v' x = b' - a' :=
  integral_of_hasDerivAt_of_tendsto (fun x â†¦ (hu x).mul (hv x)) huv h_bot h_top

/-- **Integration by parts on (-âˆ, âˆ).**
For finite intervals, see: `intervalIntegral.integral_mul_deriv_eq_deriv_mul`. -/
theorem integral_mul_deriv_eq_deriv_mul [CompleteSpace A]
    (hu : âˆ€ x, HasDerivAt u (u' x) x) (hv : âˆ€ x, HasDerivAt v (v' x) x)
    (huv' : Integrable (u * v')) (hu'v : Integrable (u' * v))
    (h_bot : Tendsto (u * v) atBot (ğ“ a')) (h_top : Tendsto (u * v) atTop (ğ“ b')) :
    âˆ« (x : â„), u x * v' x = b' - a' - âˆ« (x : â„), u' x * v x :=
  integral_bilinear_hasDerivAt_right_eq_sub (L := ContinuousLinearMap.mul â„ A)
    hu hv huv' hu'v h_bot h_top

/-- **Integration by parts on (-âˆ, âˆ).**
Version assuming that the total function is integrable -/
theorem integral_mul_deriv_eq_deriv_mul_of_integrable
    (hu : âˆ€ x, HasDerivAt u (u' x) x) (hv : âˆ€ x, HasDerivAt v (v' x) x)
    (huv' : Integrable (u * v')) (hu'v : Integrable (u' * v)) (huv : Integrable (u * v)) :
    âˆ« (x : â„), u x * v' x = - âˆ« (x : â„), u' x * v x :=
  integral_bilinear_hasDerivAt_right_eq_neg_left_of_integrable (L := ContinuousLinearMap.mul â„ A)
    hu hv huv' hu'v huv

variable [CompleteSpace A]

-- TODO: also apply `Tendsto _ (ğ“[>] a) (ğ“ a')` generalization to
-- `integral_Ioi_of_hasDerivAt_of_tendsto` and `integral_Iic_of_hasDerivAt_of_tendsto`
/-- For finite intervals, see: `intervalIntegral.integral_deriv_mul_eq_sub`. -/
theorem integral_Ioi_deriv_mul_eq_sub
    (hu : âˆ€ x âˆˆ Ioi a, HasDerivAt u (u' x) x) (hv : âˆ€ x âˆˆ Ioi a, HasDerivAt v (v' x) x)
    (huv : IntegrableOn (u' * v + u * v') (Ioi a))
    (h_zero : Tendsto (u * v) (ğ“[>] a) (ğ“ a')) (h_infty : Tendsto (u * v) atTop (ğ“ b')) :
    âˆ« (x : â„) in Ioi a, u' x * v x + u x * v' x = b' - a' := by
  rw [â† Ici_diff_left] at h_zero
  let f := Function.update (u * v) a a'
  have hderiv : âˆ€ x âˆˆ Ioi a, HasDerivAt f (u' x * v x + u x * v' x) x := by
    intro x (hx : a < x)
    apply ((hu x hx).mul (hv x hx)).congr_of_eventuallyEq
    filter_upwards [eventually_ne_nhds hx.ne.symm] with y hy
    exact Function.update_of_ne hy a' (u * v)
  have htendsto : Tendsto f atTop (ğ“ b') := by
    apply h_infty.congr'
    filter_upwards [eventually_ne_atTop a] with x hx
    exact (Function.update_of_ne hx a' (u * v)).symm
  simpa using integral_Ioi_of_hasDerivAt_of_tendsto
    (continuousWithinAt_update_same.mpr h_zero) hderiv huv htendsto

/-- **Integration by parts on (a, âˆ).**
For finite intervals, see: `intervalIntegral.integral_mul_deriv_eq_deriv_mul`. -/
theorem integral_Ioi_mul_deriv_eq_deriv_mul
    (hu : âˆ€ x âˆˆ Ioi a, HasDerivAt u (u' x) x) (hv : âˆ€ x âˆˆ Ioi a, HasDerivAt v (v' x) x)
    (huv' : IntegrableOn (u * v') (Ioi a)) (hu'v : IntegrableOn (u' * v) (Ioi a))
    (h_zero : Tendsto (u * v) (ğ“[>] a) (ğ“ a')) (h_infty : Tendsto (u * v) atTop (ğ“ b')) :
    âˆ« (x : â„) in Ioi a, u x * v' x = b' - a' - âˆ« (x : â„) in Ioi a, u' x * v x := by
  rw [Pi.mul_def] at huv' hu'v
  rw [eq_sub_iff_add_eq, â† integral_add huv' hu'v]
  simpa only [add_comm] using integral_Ioi_deriv_mul_eq_sub hu hv (hu'v.add huv') h_zero h_infty

/-- For finite intervals, see: `intervalIntegral.integral_deriv_mul_eq_sub`. -/
theorem integral_Iic_deriv_mul_eq_sub
    (hu : âˆ€ x âˆˆ Iio a, HasDerivAt u (u' x) x) (hv : âˆ€ x âˆˆ Iio a, HasDerivAt v (v' x) x)
    (huv : IntegrableOn (u' * v + u * v') (Iic a))
    (h_zero : Tendsto (u * v) (ğ“[<] a) (ğ“ a')) (h_infty : Tendsto (u * v) atBot (ğ“ b')) :
    âˆ« (x : â„) in Iic a, u' x * v x + u x * v' x = a' - b' := by
  rw [â† Iic_diff_right] at h_zero
  let f := Function.update (u * v) a a'
  have hderiv : âˆ€ x âˆˆ Iio a, HasDerivAt f (u' x * v x + u x * v' x) x := by
    intro x hx
    apply ((hu x hx).mul (hv x hx)).congr_of_eventuallyEq
    filter_upwards [Iio_mem_nhds hx] with x (hx : x < a)
    exact Function.update_of_ne (ne_of_lt hx) a' (u * v)
  have htendsto : Tendsto f atBot (ğ“ b') := by
    apply h_infty.congr'
    filter_upwards [Iio_mem_atBot a] with x (hx : x < a)
    exact (Function.update_of_ne (ne_of_lt hx) a' (u * v)).symm
  simpa using integral_Iic_of_hasDerivAt_of_tendsto
    (continuousWithinAt_update_same.mpr h_zero) hderiv huv htendsto

/-- **Integration by parts on (âˆ, a].**
For finite intervals, see: `intervalIntegral.integral_mul_deriv_eq_deriv_mul`. -/
theorem integral_Iic_mul_deriv_eq_deriv_mul
    (hu : âˆ€ x âˆˆ Iio a, HasDerivAt u (u' x) x) (hv : âˆ€ x âˆˆ Iio a, HasDerivAt v (v' x) x)
    (huv' : IntegrableOn (u * v') (Iic a)) (hu'v : IntegrableOn (u' * v) (Iic a))
    (h_zero : Tendsto (u * v) (ğ“[<] a) (ğ“ a')) (h_infty : Tendsto (u * v) atBot (ğ“ b')) :
    âˆ« (x : â„) in Iic a, u x * v' x = a' - b' - âˆ« (x : â„) in Iic a, u' x * v x := by
  rw [Pi.mul_def] at huv' hu'v
  rw [eq_sub_iff_add_eq, â† integral_add huv' hu'v]
  simpa only [add_comm] using integral_Iic_deriv_mul_eq_sub hu hv (hu'v.add huv') h_zero h_infty

end IntegrationByPartsAlgebra

end MeasureTheory
