/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, SÃ©bastien GouÃ«zel, RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp

/-!
# Additivity on measurable sets with finite measure

Let `T : Set Î± â†’ E â†’L[â„] F` be additive for measurable sets with finite measure, in the sense that
for `s, t` two such sets, `Disjoint s t â†’ T (s âˆª t) = T s + T t`. `T` is akin to a bilinear map on
`Set Î± Ã— E`, or a linear map on indicator functions.

This property is named `FinMeasAdditive` in this file. We also define `DominatedFinMeasAdditive`,
which requires in addition that the norm on every set is less than the measure of the set
(up to a multiplicative constant); in `Mathlib/MeasureTheory/Integral/SetToL1.lean` we extend
set functions with this stronger property to integrable (L1) functions.

## Main definitions

- `FinMeasAdditive Î¼ T`: the property that `T` is additive on measurable sets with finite measure.
  For two such sets, `Disjoint s t â†’ T (s âˆª t) = T s + T t`.
- `DominatedFinMeasAdditive Î¼ T C`: `FinMeasAdditive Î¼ T âˆ§ âˆ€ s, â€–T sâ€– â‰¤ C * Î¼.real s`.
  This is the property needed to perform the extension from indicators to L1.

## Implementation notes

The starting object `T : Set Î± â†’ E â†’L[â„] F` matters only through its restriction on measurable sets
with finite measure. Its value on other sets is ignored.
-/


noncomputable section

open Set Filter ENNReal Finset

namespace MeasureTheory

variable {Î± E F F' G ğ•œ : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [NormedAddCommGroup F'] [NormedSpace â„ F']
  [NormedAddCommGroup G] {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

local infixr:25 " â†’â‚› " => SimpleFunc

section FinMeasAdditive

/-- A set function is `FinMeasAdditive` if its value on the union of two disjoint measurable
sets with finite measure is the sum of its values on each set. -/
def FinMeasAdditive {Î²} [AddMonoid Î²] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (T : Set Î± â†’ Î²) :
    Prop :=
  âˆ€ s t, MeasurableSet s â†’ MeasurableSet t â†’ Î¼ s â‰  âˆ â†’ Î¼ t â‰  âˆ â†’ Disjoint s t â†’
    T (s âˆª t) = T s + T t

namespace FinMeasAdditive

variable {Î² : Type*} [AddCommMonoid Î²] {T T' : Set Î± â†’ Î²}

theorem zero : FinMeasAdditive Î¼ (0 : Set Î± â†’ Î²) := fun _ _ _ _ _ _ _ => by simp

theorem add (hT : FinMeasAdditive Î¼ T) (hT' : FinMeasAdditive Î¼ T') :
    FinMeasAdditive Î¼ (T + T') := by
  intro s t hs ht hÎ¼s hÎ¼t hst
  simp only [hT s t hs ht hÎ¼s hÎ¼t hst, hT' s t hs ht hÎ¼s hÎ¼t hst, Pi.add_apply]
  abel

theorem smul [DistribSMul ğ•œ Î²] (hT : FinMeasAdditive Î¼ T) (c : ğ•œ) :
    FinMeasAdditive Î¼ fun s => c â€¢ T s := fun s t hs ht hÎ¼s hÎ¼t hst => by
  simp [hT s t hs ht hÎ¼s hÎ¼t hst]

theorem of_eq_top_imp_eq_top {Î¼' : Measure Î±} (h : âˆ€ s, MeasurableSet s â†’ Î¼ s = âˆ â†’ Î¼' s = âˆ)
    (hT : FinMeasAdditive Î¼ T) : FinMeasAdditive Î¼' T := fun s t hs ht hÎ¼'s hÎ¼'t hst =>
  hT s t hs ht (mt (h s hs) hÎ¼'s) (mt (h t ht) hÎ¼'t) hst

theorem of_smul_measure {c : â„â‰¥0âˆ} (hc_ne_top : c â‰  âˆ) (hT : FinMeasAdditive (c â€¢ Î¼) T) :
    FinMeasAdditive Î¼ T := by
  refine of_eq_top_imp_eq_top (fun s _ hÎ¼s => ?_) hT
  rw [Measure.smul_apply, smul_eq_mul, ENNReal.mul_eq_top] at hÎ¼s
  simp only [hc_ne_top, or_false, Ne, false_and] at hÎ¼s
  exact hÎ¼s.2

theorem smul_measure (c : â„â‰¥0âˆ) (hc_ne_zero : c â‰  0) (hT : FinMeasAdditive Î¼ T) :
    FinMeasAdditive (c â€¢ Î¼) T := by
  refine of_eq_top_imp_eq_top (fun s _ hÎ¼s => ?_) hT
  rw [Measure.smul_apply, smul_eq_mul, ENNReal.mul_eq_top]
  simp only [hc_ne_zero, true_and, Ne, not_false_iff]
  exact Or.inl hÎ¼s

theorem smul_measure_iff (c : â„â‰¥0âˆ) (hc_ne_zero : c â‰  0) (hc_ne_top : c â‰  âˆ) :
    FinMeasAdditive (c â€¢ Î¼) T â†” FinMeasAdditive Î¼ T :=
  âŸ¨fun hT => of_smul_measure hc_ne_top hT, fun hT => smul_measure c hc_ne_zero hTâŸ©

theorem map_empty_eq_zero {Î²} [AddCancelMonoid Î²] {T : Set Î± â†’ Î²} (hT : FinMeasAdditive Î¼ T) :
    T âˆ… = 0 := by
  have h_empty : Î¼ âˆ… â‰  âˆ := (measure_empty.le.trans_lt ENNReal.coe_lt_top).ne
  specialize hT âˆ… âˆ… MeasurableSet.empty MeasurableSet.empty h_empty h_empty (disjoint_empty _)
  rw [Set.union_empty] at hT
  nth_rw 1 [â† add_zero (T âˆ…)] at hT
  exact (add_left_cancel hT).symm

theorem map_iUnion_fin_meas_set_eq_sum (T : Set Î± â†’ Î²) (T_empty : T âˆ… = 0)
    (h_add : FinMeasAdditive Î¼ T) {Î¹} (S : Î¹ â†’ Set Î±) (sÎ¹ : Finset Î¹)
    (hS_meas : âˆ€ i, MeasurableSet (S i)) (hSp : âˆ€ i âˆˆ sÎ¹, Î¼ (S i) â‰  âˆ)
    (h_disj : âˆ€áµ‰ (i âˆˆ sÎ¹) (j âˆˆ sÎ¹), i â‰  j â†’ Disjoint (S i) (S j)) :
    T (â‹ƒ i âˆˆ sÎ¹, S i) = âˆ‘ i âˆˆ sÎ¹, T (S i) := by
  classical
  revert hSp h_disj
  refine Finset.induction_on sÎ¹ ?_ ?_
  Â· simp only [Finset.notMem_empty, IsEmpty.forall_iff, iUnion_false, iUnion_empty, sum_empty,
      imp_true_iff, T_empty]
  intro a s has h hps h_disj
  rw [Finset.sum_insert has, â† h]
  swap; Â· exact fun i hi => hps i (Finset.mem_insert_of_mem hi)
  swap
  Â· exact fun i hi j hj hij =>
      h_disj i (Finset.mem_insert_of_mem hi) j (Finset.mem_insert_of_mem hj) hij
  rw [â†
    h_add (S a) (â‹ƒ i âˆˆ s, S i) (hS_meas a) (measurableSet_biUnion _ fun i _ => hS_meas i)
      (hps a (Finset.mem_insert_self a s))]
  Â· congr; convert Finset.iSup_insert a s S
  Â· exact (measure_biUnion_lt_top s.finite_toSet fun i hi â†¦
      (hps i <| Finset.mem_insert_of_mem hi).lt_top).ne
  Â· simp_rw [Set.disjoint_iUnion_right]
    intro i hi
    refine h_disj a (Finset.mem_insert_self a s) i (Finset.mem_insert_of_mem hi) fun hai â†¦ ?_
    rw [â† hai] at hi
    exact has hi

end FinMeasAdditive

/-- A `FinMeasAdditive` set function whose norm on every set is less than the measure of the
set (up to a multiplicative constant). -/
def DominatedFinMeasAdditive {Î²} [SeminormedAddCommGroup Î²] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±)
    (T : Set Î± â†’ Î²) (C : â„) : Prop :=
  FinMeasAdditive Î¼ T âˆ§ âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * Î¼.real s

namespace DominatedFinMeasAdditive

variable {Î² : Type*} [SeminormedAddCommGroup Î²] {T T' : Set Î± â†’ Î²} {C C' : â„}

theorem zero {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (hC : 0 â‰¤ C) :
    DominatedFinMeasAdditive Î¼ (0 : Set Î± â†’ Î²) C := by
  refine âŸ¨FinMeasAdditive.zero, fun s _ _ => ?_âŸ©
  rw [Pi.zero_apply, norm_zero]
  exact mul_nonneg hC toReal_nonneg

theorem eq_zero_of_measure_zero {Î² : Type*} [NormedAddCommGroup Î²] {T : Set Î± â†’ Î²} {C : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) {s : Set Î±} (hs : MeasurableSet s) (hs_zero : Î¼ s = 0) :
    T s = 0 := by
  refine norm_eq_zero.mp ?_
  refine ((hT.2 s hs (by simp [hs_zero])).trans (le_of_eq ?_)).antisymm (norm_nonneg _)
  rw [measureReal_def, hs_zero, ENNReal.toReal_zero, mul_zero]

theorem eq_zero {Î² : Type*} [NormedAddCommGroup Î²] {T : Set Î± â†’ Î²} {C : â„} {_ : MeasurableSpace Î±}
    (hT : DominatedFinMeasAdditive (0 : Measure Î±) T C) {s : Set Î±} (hs : MeasurableSet s) :
    T s = 0 :=
  eq_zero_of_measure_zero hT hs (by simp only [Measure.coe_zero, Pi.zero_apply])

theorem add (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C') :
    DominatedFinMeasAdditive Î¼ (T + T') (C + C') := by
  refine âŸ¨hT.1.add hT'.1, fun s hs hÎ¼s => ?_âŸ©
  rw [Pi.add_apply, add_mul]
  exact (norm_add_le _ _).trans (add_le_add (hT.2 s hs hÎ¼s) (hT'.2 s hs hÎ¼s))

theorem smul [SeminormedAddGroup ğ•œ] [DistribSMul ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]
    (hT : DominatedFinMeasAdditive Î¼ T C) (c : ğ•œ) :
    DominatedFinMeasAdditive Î¼ (fun s => c â€¢ T s) (â€–câ€– * C) := by
  refine âŸ¨hT.1.smul c, fun s hs hÎ¼s => (norm_smul_le _ _).trans ?_âŸ©
  rw [mul_assoc]
  exact mul_le_mul le_rfl (hT.2 s hs hÎ¼s) (norm_nonneg _) (norm_nonneg _)

theorem of_measure_le {Î¼' : Measure Î±} (h : Î¼ â‰¤ Î¼') (hT : DominatedFinMeasAdditive Î¼ T C)
    (hC : 0 â‰¤ C) : DominatedFinMeasAdditive Î¼' T C := by
  have h' : âˆ€ s, Î¼ s = âˆ â†’ Î¼' s = âˆ := fun s hs â†¦ top_unique <| hs.symm.trans_le (h _)
  refine âŸ¨hT.1.of_eq_top_imp_eq_top fun s _ â†¦ h' s, fun s hs hÎ¼'s â†¦ ?_âŸ©
  have hÎ¼s : Î¼ s < âˆ := (h s).trans_lt hÎ¼'s
  calc
    â€–T sâ€– â‰¤ C * Î¼.real s := hT.2 s hs hÎ¼s
    _ â‰¤ C * Î¼'.real s := by
      simp only [measureReal_def]
      gcongr
      exacts [hÎ¼'s.ne, h s]

theorem add_measure_right {_ : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±)
    (hT : DominatedFinMeasAdditive Î¼ T C) (hC : 0 â‰¤ C) : DominatedFinMeasAdditive (Î¼ + Î½) T C :=
  of_measure_le (Measure.le_add_right le_rfl) hT hC

theorem add_measure_left {_ : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±)
    (hT : DominatedFinMeasAdditive Î½ T C) (hC : 0 â‰¤ C) : DominatedFinMeasAdditive (Î¼ + Î½) T C :=
  of_measure_le (Measure.le_add_left le_rfl) hT hC

theorem of_smul_measure {c : â„â‰¥0âˆ} (hc_ne_top : c â‰  âˆ) (hT : DominatedFinMeasAdditive (c â€¢ Î¼) T C) :
    DominatedFinMeasAdditive Î¼ T (c.toReal * C) := by
  have h : âˆ€ s, MeasurableSet s â†’ c â€¢ Î¼ s = âˆ â†’ Î¼ s = âˆ := by
    intro s _ hcÎ¼s
    simp only [hc_ne_top, Algebra.id.smul_eq_mul, ENNReal.mul_eq_top, or_false, Ne,
      false_and] at hcÎ¼s
    exact hcÎ¼s.2
  refine âŸ¨hT.1.of_eq_top_imp_eq_top (Î¼ := c â€¢ Î¼) h, fun s hs hÎ¼s => ?_âŸ©
  have hcÎ¼s : c â€¢ Î¼ s â‰  âˆ := mt (h s hs) hÎ¼s.ne
  rw [smul_eq_mul] at hcÎ¼s
  refine (hT.2 s hs hcÎ¼s.lt_top).trans (le_of_eq ?_)
  simp only [measureReal_ennreal_smul_apply]
  ring

theorem of_measure_le_smul {Î¼' : Measure Î±} {c : â„â‰¥0âˆ} (hc : c â‰  âˆ) (h : Î¼ â‰¤ c â€¢ Î¼')
    (hT : DominatedFinMeasAdditive Î¼ T C) (hC : 0 â‰¤ C) :
    DominatedFinMeasAdditive Î¼' T (c.toReal * C) :=
  (hT.of_measure_le h hC).of_smul_measure hc

end DominatedFinMeasAdditive

end FinMeasAdditive

namespace SimpleFunc

/-- Extend `Set Î± â†’ (F â†’L[â„] F')` to `(Î± â†’â‚› F) â†’ F'`. -/
def setToSimpleFunc {_ : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') (f : Î± â†’â‚› F) : F' :=
  âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) x

@[simp]
theorem setToSimpleFunc_zero {m : MeasurableSpace Î±} (f : Î± â†’â‚› F) :
    setToSimpleFunc (0 : Set Î± â†’ F â†’L[â„] F') f = 0 := by simp [setToSimpleFunc]

theorem setToSimpleFunc_zero' {T : Set Î± â†’ E â†’L[â„] F'}
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0) (f : Î± â†’â‚› E) (hf : Integrable f Î¼) :
    setToSimpleFunc T f = 0 := by
  simp_rw [setToSimpleFunc]
  refine sum_eq_zero fun x _ => ?_
  by_cases hx0 : x = 0
  Â· simp [hx0]
  rw [h_zero (f â»Â¹' ({x} : Set E)) (measurableSet_fiber _ _)
      (measure_preimage_lt_top_of_integrable f hf hx0),
    ContinuousLinearMap.zero_apply]

@[simp]
theorem setToSimpleFunc_zero_apply {m : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') :
    setToSimpleFunc T (0 : Î± â†’â‚› F) = 0 := by
  cases isEmpty_or_nonempty Î± <;> simp [setToSimpleFunc]

theorem setToSimpleFunc_eq_sum_filter [DecidablePred fun x â†¦ x â‰  (0 : F)]
    {m : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') (f : Î± â†’â‚› F) :
    setToSimpleFunc T f = âˆ‘ x âˆˆ f.range with x â‰  0, T (f â»Â¹' {x}) x := by
  symm
  refine sum_filter_of_ne fun x _ => mt fun hx0 => ?_
  rw [hx0]
  exact ContinuousLinearMap.map_zero _

theorem map_setToSimpleFunc (T : Set Î± â†’ F â†’L[â„] F') (h_add : FinMeasAdditive Î¼ T) {f : Î± â†’â‚› G}
    (hf : Integrable f Î¼) {g : G â†’ F} (hg : g 0 = 0) :
    (f.map g).setToSimpleFunc T = âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) (g x) := by
  classical
  have T_empty : T âˆ… = 0 := h_add.map_empty_eq_zero
  have hfp : âˆ€ x âˆˆ f.range, x â‰  0 â†’ Î¼ (f â»Â¹' {x}) â‰  âˆ := fun x _ hx0 =>
    (measure_preimage_lt_top_of_integrable f hf hx0).ne
  simp only [setToSimpleFunc, range_map]
  refine Finset.sum_image' _ fun b hb => ?_
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©
  by_cases h0 : g (f a) = 0
  Â· simp_rw [h0]
    rw [ContinuousLinearMap.map_zero, Finset.sum_eq_zero fun x hx => ?_]
    rw [mem_filter] at hx
    rw [hx.2, ContinuousLinearMap.map_zero]
  have h_left_eq :
    T (map g f â»Â¹' {g (f a)}) (g (f a))
      = T (f â»Â¹' ({b âˆˆ f.range | g b = g (f a)} : Finset _)) (g (f a)) := by
    rw [map_preimage_singleton]
  rw [h_left_eq]
  have h_left_eq' :
    T (f â»Â¹' ({b âˆˆ f.range | g b = g (f a)} : Finset _)) (g (f a))
      = T (â‹ƒ y âˆˆ {b âˆˆ f.range | g b = g (f a)}, f â»Â¹' {y}) (g (f a)) := by
    rw [â† Finset.set_biUnion_preimage_singleton]
  rw [h_left_eq']
  rw [h_add.map_iUnion_fin_meas_set_eq_sum T T_empty]
  Â· simp only [sum_apply, ContinuousLinearMap.coe_sum']
    refine Finset.sum_congr rfl fun x hx => ?_
    rw [mem_filter] at hx
    rw [hx.2]
  Â· exact fun i => measurableSet_fiber _ _
  Â· grind [Finset.mem_filter]
  Â· grind [Set.disjoint_iff]

theorem setToSimpleFunc_congr' (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼)
    (h : Pairwise fun x y => T (f â»Â¹' {x} âˆ© g â»Â¹' {y}) = 0) :
    f.setToSimpleFunc T = g.setToSimpleFunc T :=
  show ((pair f g).map Prod.fst).setToSimpleFunc T = ((pair f g).map Prod.snd).setToSimpleFunc T by
    have h_pair : Integrable (f.pair g) Î¼ := integrable_pair hf hg
    rw [map_setToSimpleFunc T h_add h_pair Prod.fst_zero]
    rw [map_setToSimpleFunc T h_add h_pair Prod.snd_zero]
    refine Finset.sum_congr rfl fun p hp => ?_
    rcases mem_range.1 hp with âŸ¨a, rflâŸ©
    by_cases eq : f a = g a
    Â· dsimp only [pair_apply]; rw [eq]
    Â· have : T (pair f g â»Â¹' {(f a, g a)}) = 0 := by
        have h_eq : T ((â‡‘(f.pair g)) â»Â¹' {(f a, g a)}) = T (f â»Â¹' {f a} âˆ© g â»Â¹' {g a}) := by
          congr; rw [pair_preimage_singleton f g]
        rw [h_eq]
        exact h eq
      simp only [this, ContinuousLinearMap.zero_apply, pair_apply]

theorem setToSimpleFunc_congr (T : Set Î± â†’ E â†’L[â„] F)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (h : f =áµ[Î¼] g) : f.setToSimpleFunc T = g.setToSimpleFunc T := by
  refine setToSimpleFunc_congr' T h_add hf ((integrable_congr h).mp hf) ?_
  refine fun x y hxy => h_zero _ ((measurableSet_fiber f x).inter (measurableSet_fiber g y)) ?_
  rw [EventuallyEq, ae_iff] at h
  refine measure_mono_null (fun z => ?_) h
  simp_rw [Set.mem_inter_iff, Set.mem_setOf_eq, Set.mem_preimage, Set.mem_singleton_iff]
  intro h
  rwa [h.1, h.2]

theorem setToSimpleFunc_congr_left (T T' : Set Î± â†’ E â†’L[â„] F)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s) (f : Î± â†’â‚› E) (hf : Integrable f Î¼) :
    setToSimpleFunc T f = setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc]
  refine sum_congr rfl fun x _ => ?_
  by_cases hx0 : x = 0
  Â· simp [hx0]
  Â· rw [h (f â»Â¹' {x}) (SimpleFunc.measurableSet_fiber _ _)
        (SimpleFunc.measure_preimage_lt_top_of_integrable _ hf hx0)]

theorem setToSimpleFunc_add_left {m : MeasurableSpace Î±} (T T' : Set Î± â†’ F â†’L[â„] F') {f : Î± â†’â‚› F} :
    setToSimpleFunc (T + T') f = setToSimpleFunc T f + setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc, Pi.add_apply]
  push_cast
  simp_rw [Pi.add_apply, sum_add_distrib]

theorem setToSimpleFunc_add_left' (T T' T'' : Set Î± â†’ E â†’L[â„] F)
    (h_add : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T'' s = T s + T' s) {f : Î± â†’â‚› E}
    (hf : Integrable f Î¼) : setToSimpleFunc T'' f = setToSimpleFunc T f + setToSimpleFunc T' f := by
  classical
  simp_rw [setToSimpleFunc_eq_sum_filter]
  suffices âˆ€ x âˆˆ {x âˆˆ f.range | x â‰  0}, T'' (f â»Â¹' {x}) = T (f â»Â¹' {x}) + T' (f â»Â¹' {x}) by
    rw [â† sum_add_distrib]
    refine Finset.sum_congr rfl fun x hx => ?_
    rw [this x hx]
    push_cast
    rw [Pi.add_apply]
  intro x hx
  refine
    h_add (f â»Â¹' {x}) (measurableSet_preimage _ _) (measure_preimage_lt_top_of_integrable _ hf ?_)
  rw [mem_filter] at hx
  exact hx.2

theorem setToSimpleFunc_smul_left {m : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') (c : â„)
    (f : Î± â†’â‚› F) : setToSimpleFunc (fun s => c â€¢ T s) f = c â€¢ setToSimpleFunc T f := by
  simp_rw [setToSimpleFunc, ContinuousLinearMap.smul_apply, smul_sum]

theorem setToSimpleFunc_smul_left' (T T' : Set Î± â†’ E â†’L[â„] F') (c : â„)
    (h_smul : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T' s = c â€¢ T s) {f : Î± â†’â‚› E} (hf : Integrable f Î¼) :
    setToSimpleFunc T' f = c â€¢ setToSimpleFunc T f := by
  classical
  simp_rw [setToSimpleFunc_eq_sum_filter]
  suffices âˆ€ x âˆˆ {x âˆˆ f.range | x â‰  0}, T' (f â»Â¹' {x}) = c â€¢ T (f â»Â¹' {x}) by
    rw [smul_sum]
    refine Finset.sum_congr rfl fun x hx => ?_
    rw [this x hx, ContinuousLinearMap.smul_apply]
  intro x hx
  refine
    h_smul (f â»Â¹' {x}) (measurableSet_preimage _ _) (measure_preimage_lt_top_of_integrable _ hf ?_)
  rw [mem_filter] at hx
  exact hx.2

theorem setToSimpleFunc_add (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    setToSimpleFunc T (f + g) = setToSimpleFunc T f + setToSimpleFunc T g :=
  have hp_pair : Integrable (f.pair g) Î¼ := integrable_pair hf hg
  calc
    setToSimpleFunc T (f + g) = âˆ‘ x âˆˆ (pair f g).range, T (pair f g â»Â¹' {x}) (x.fst + x.snd) := by
      rw [add_eq_mapâ‚‚, map_setToSimpleFunc T h_add hp_pair]; simp
    _ = âˆ‘ x âˆˆ (pair f g).range, (T (pair f g â»Â¹' {x}) x.fst + T (pair f g â»Â¹' {x}) x.snd) :=
      (Finset.sum_congr rfl fun _ _ => ContinuousLinearMap.map_add _ _ _)
    _ = (âˆ‘ x âˆˆ (pair f g).range, T (pair f g â»Â¹' {x}) x.fst) +
          âˆ‘ x âˆˆ (pair f g).range, T (pair f g â»Â¹' {x}) x.snd := by
      rw [Finset.sum_add_distrib]
    _ = ((pair f g).map Prod.fst).setToSimpleFunc T +
          ((pair f g).map Prod.snd).setToSimpleFunc T := by
      rw [map_setToSimpleFunc T h_add hp_pair Prod.snd_zero,
        map_setToSimpleFunc T h_add hp_pair Prod.fst_zero]

theorem setToSimpleFunc_neg (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f : Î± â†’â‚› E}
    (hf : Integrable f Î¼) : setToSimpleFunc T (-f) = -setToSimpleFunc T f :=
  calc
    setToSimpleFunc T (-f) = setToSimpleFunc T (f.map Neg.neg) := rfl
    _ = -setToSimpleFunc T f := by
      rw [map_setToSimpleFunc T h_add hf neg_zero, setToSimpleFunc, â† sum_neg_distrib]
      exact Finset.sum_congr rfl fun x _ => ContinuousLinearMap.map_neg _ _

theorem setToSimpleFunc_sub (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    setToSimpleFunc T (f - g) = setToSimpleFunc T f - setToSimpleFunc T g := by
  rw [sub_eq_add_neg, setToSimpleFunc_add T h_add hf, setToSimpleFunc_neg T h_add hg,
    sub_eq_add_neg]
  rw [integrable_iff] at hg âŠ¢
  intro x hx_ne
  rw [SimpleFunc.coe_neg, Pi.neg_def, â† Function.comp_def, preimage_comp, neg_preimage,
    Set.neg_singleton]
  refine hg (-x) ?_
  simp [hx_ne]

theorem setToSimpleFunc_smul_real (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) (c : â„)
    {f : Î± â†’â‚› E} (hf : Integrable f Î¼) : setToSimpleFunc T (c â€¢ f) = c â€¢ setToSimpleFunc T f :=
  calc
    setToSimpleFunc T (c â€¢ f) = âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) (c â€¢ x) := by
      rw [smul_eq_map c f, map_setToSimpleFunc T h_add hf]; dsimp only; rw [smul_zero]
    _ = âˆ‘ x âˆˆ f.range, c â€¢ T (f â»Â¹' {x}) x :=
      (Finset.sum_congr rfl fun b _ => by rw [ContinuousLinearMap.map_smul (T (f â»Â¹' {b})) c b])
    _ = c â€¢ setToSimpleFunc T f := by simp only [setToSimpleFunc, smul_sum]

theorem setToSimpleFunc_smul {E} [NormedAddCommGroup E] [SMulZeroClass ğ•œ E]
    [NormedSpace â„ E] [DistribSMul ğ•œ F] (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T)
    (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ) {f : Î± â†’â‚› E} (hf : Integrable f Î¼) :
    setToSimpleFunc T (c â€¢ f) = c â€¢ setToSimpleFunc T f :=
  calc
    setToSimpleFunc T (c â€¢ f) = âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) (c â€¢ x) := by
      rw [smul_eq_map c f, map_setToSimpleFunc T h_add hf]; dsimp only; rw [smul_zero]
    _ = âˆ‘ x âˆˆ f.range, c â€¢ T (f â»Â¹' {x}) x := Finset.sum_congr rfl fun b _ => by rw [h_smul]
    _ = c â€¢ setToSimpleFunc T f := by simp only [setToSimpleFunc, smul_sum]

section Order

variable {G' G'' : Type*}
  [NormedAddCommGroup G''] [PartialOrder G''] [IsOrderedAddMonoid G''] [NormedSpace â„ G'']
  [NormedAddCommGroup G'] [PartialOrder G'] [NormedSpace â„ G']

theorem setToSimpleFunc_mono_left {m : MeasurableSpace Î±} (T T' : Set Î± â†’ F â†’L[â„] G'')
    (hTT' : âˆ€ s x, T s x â‰¤ T' s x) (f : Î± â†’â‚› F) : setToSimpleFunc T f â‰¤ setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc]; exact sum_le_sum fun i _ => hTT' _ i

theorem setToSimpleFunc_mono_left' (T T' : Set Î± â†’ E â†’L[â„] G'')
    (hTT' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, T s x â‰¤ T' s x) (f : Î± â†’â‚› E)
    (hf : Integrable f Î¼) : setToSimpleFunc T f â‰¤ setToSimpleFunc T' f := by
  refine sum_le_sum fun i _ => ?_
  by_cases h0 : i = 0
  Â· simp [h0]
  Â· exact hTT' _ (measurableSet_fiber _ _) (measure_preimage_lt_top_of_integrable _ hf h0) i

theorem setToSimpleFunc_nonneg {m : MeasurableSpace Î±} (T : Set Î± â†’ G' â†’L[â„] G'')
    (hT_nonneg : âˆ€ s x, 0 â‰¤ x â†’ 0 â‰¤ T s x) (f : Î± â†’â‚› G') (hf : 0 â‰¤ f) :
    0 â‰¤ setToSimpleFunc T f := by
  refine sum_nonneg fun i hi => hT_nonneg _ i ?_
  rw [mem_range] at hi
  obtain âŸ¨y, hyâŸ© := Set.mem_range.mp hi
  rw [â† hy]
  refine le_trans ?_ (hf y)
  simp

theorem setToSimpleFunc_nonneg' (T : Set Î± â†’ G' â†’L[â„] G'')
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) (f : Î± â†’â‚› G') (hf : 0 â‰¤ f)
    (hfi : Integrable f Î¼) : 0 â‰¤ setToSimpleFunc T f := by
  refine sum_nonneg fun i hi => ?_
  by_cases h0 : i = 0
  Â· simp [h0]
  refine
    hT_nonneg _ (measurableSet_fiber _ _) (measure_preimage_lt_top_of_integrable _ hfi h0) i ?_
  rw [mem_range] at hi
  obtain âŸ¨y, hyâŸ© := Set.mem_range.mp hi
  rw [â† hy]
  convert hf y

theorem setToSimpleFunc_mono [IsOrderedAddMonoid G']
    {T : Set Î± â†’ G' â†’L[â„] G''} (h_add : FinMeasAdditive Î¼ T)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f g : Î± â†’â‚› G'}
    (hfi : Integrable f Î¼) (hgi : Integrable g Î¼) (hfg : f â‰¤ g) :
    setToSimpleFunc T f â‰¤ setToSimpleFunc T g := by
  rw [â† sub_nonneg, â† setToSimpleFunc_sub T h_add hgi hfi]
  refine setToSimpleFunc_nonneg' T hT_nonneg _ ?_ (hgi.sub hfi)
  intro x
  simp only [coe_sub, sub_nonneg, coe_zero, Pi.zero_apply, Pi.sub_apply]
  exact hfg x

end Order

theorem norm_setToSimpleFunc_le_sum_opNorm {m : MeasurableSpace Î±} (T : Set Î± â†’ F' â†’L[â„] F)
    (f : Î± â†’â‚› F') : â€–f.setToSimpleFunc Tâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– :=
  calc
    â€–âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) xâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x}) xâ€– := norm_sum_le _ _
    _ â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– := by
      refine Finset.sum_le_sum fun b _ => ?_; simp_rw [ContinuousLinearMap.le_opNorm]

theorem norm_setToSimpleFunc_le_sum_mul_norm (T : Set Î± â†’ F â†’L[â„] F') {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ â€–T sâ€– â‰¤ C * Î¼.real s) (f : Î± â†’â‚› F) :
    â€–f.setToSimpleFunc Tâ€– â‰¤ C * âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) * â€–xâ€– :=
  calc
    â€–f.setToSimpleFunc Tâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– :=
      norm_setToSimpleFunc_le_sum_opNorm T f
    _ â‰¤ âˆ‘ x âˆˆ f.range, C * Î¼.real (f â»Â¹' {x}) * â€–xâ€– := by
      gcongr
      exact hT_norm _ <| SimpleFunc.measurableSet_fiber _ _
    _ â‰¤ C * âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) * â€–xâ€– := by simp_rw [mul_sum, â† mul_assoc]; rfl

theorem norm_setToSimpleFunc_le_sum_mul_norm_of_integrable (T : Set Î± â†’ E â†’L[â„] F') {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * Î¼.real s) (f : Î± â†’â‚› E)
    (hf : Integrable f Î¼) :
    â€–f.setToSimpleFunc Tâ€– â‰¤ C * âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) * â€–xâ€– :=
  calc
    â€–f.setToSimpleFunc Tâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– :=
      norm_setToSimpleFunc_le_sum_opNorm T f
    _ â‰¤ âˆ‘ x âˆˆ f.range, C * Î¼.real (f â»Â¹' {x}) * â€–xâ€– := by
      refine Finset.sum_le_sum fun b hb => ?_
      obtain rfl | hb := eq_or_ne b 0
      Â· simp
      gcongr
      exact hT_norm _ (SimpleFunc.measurableSet_fiber _ _) <|
        SimpleFunc.measure_preimage_lt_top_of_integrable _ hf hb
    _ â‰¤ C * âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) * â€–xâ€– := by simp_rw [mul_sum, â† mul_assoc]; rfl

theorem setToSimpleFunc_indicator (T : Set Î± â†’ F â†’L[â„] F') (hT_empty : T âˆ… = 0)
    {m : MeasurableSpace Î±} {s : Set Î±} (hs : MeasurableSet s) (x : F) :
    SimpleFunc.setToSimpleFunc T
        (SimpleFunc.piecewise s hs (SimpleFunc.const Î± x) (SimpleFunc.const Î± 0)) =
      T s x := by
  classical
  obtain rfl | hs_empty := s.eq_empty_or_nonempty
  Â· simp only [hT_empty, ContinuousLinearMap.zero_apply, piecewise_empty, const_zero,
      setToSimpleFunc_zero_apply]
  simp_rw [setToSimpleFunc]
  obtain rfl | hs_univ := eq_or_ne s univ
  Â· haveI hÎ± := hs_empty.to_type
    simp [â† Function.const_def]
  rw [range_indicator hs hs_empty hs_univ]
  by_cases hx0 : x = 0
  Â· simp_rw [hx0]; simp
  rw [sum_insert]
  swap; Â· rw [Finset.mem_singleton]; exact hx0
  rw [sum_singleton, (T _).map_zero, add_zero]
  congr
  simp only [coe_piecewise, piecewise_eq_indicator, coe_const, Function.const_zero,
    piecewise_eq_indicator]
  rw [indicator_preimage, â† Function.const_def, preimage_const_of_mem]
  swap; Â· exact Set.mem_singleton x
  rw [â† Function.const_zero, â† Function.const_def, preimage_const_of_notMem]
  swap; Â· rw [Set.mem_singleton_iff]; exact Ne.symm hx0
  simp

theorem setToSimpleFunc_const' [Nonempty Î±] (T : Set Î± â†’ F â†’L[â„] F') (x : F)
    {m : MeasurableSpace Î±} : SimpleFunc.setToSimpleFunc T (SimpleFunc.const Î± x) = T univ x := by
  simp only [setToSimpleFunc, range_const, Set.mem_singleton, preimage_const_of_mem,
    sum_singleton, â† Function.const_def, coe_const]

theorem setToSimpleFunc_const (T : Set Î± â†’ F â†’L[â„] F') (hT_empty : T âˆ… = 0) (x : F)
    {m : MeasurableSpace Î±} : SimpleFunc.setToSimpleFunc T (SimpleFunc.const Î± x) = T univ x := by
  cases isEmpty_or_nonempty Î±
  Â· have h_univ_empty : (univ : Set Î±) = âˆ… := Subsingleton.elim _ _
    rw [h_univ_empty, hT_empty]
    simp only [setToSimpleFunc, ContinuousLinearMap.zero_apply, sum_empty,
      range_eq_empty_of_isEmpty]
  Â· exact setToSimpleFunc_const' T x

end SimpleFunc

end MeasureTheory
