/-
Copyright (c) 2020 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.Analysis.NormedSpace.HahnBanach.SeparatingDual
import Mathlib.MeasureTheory.Integral.Bochner.Set
import Mathlib.Topology.ContinuousMap.ContinuousMapZero

/-!
# Continuous linear maps composed with integration

The goal of this file is to prove that integration commutes with continuous linear maps.
This holds for simple functions. The general result follows from the continuity of all involved
operations on the space `LÂ¹`. Note that composition by a continuous linear map on `LÂ¹` is not just
the composition, as we are dealing with classes of functions, but it has already been defined
as `ContinuousLinearMap.compLp`. We take advantage of this construction here.
-/

open MeasureTheory RCLike
open scoped ENNReal NNReal

variable {X Y E F Fâ‚— : Type*} [MeasurableSpace X] {Î¼ : Measure X} {ğ•œ ğ•œ' : Type*} [RCLike ğ•œ]
  [RCLike ğ•œ'] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ' F]
  [NormedAddCommGroup Fâ‚—] [NormedSpace ğ•œ Fâ‚—] {p : â„â‰¥0âˆ}

namespace ContinuousLinearMap

variable [NormedSpace â„ F] [NormedSpace â„ Fâ‚—]

variable {Ïƒ : ğ•œ â†’+* ğ•œ'} [RingHomIsometric Ïƒ]

theorem integral_compLp (L : E â†’SL[Ïƒ] F) (Ï† : Lp E p Î¼) :
    âˆ« x, (L.compLp Ï†) x âˆ‚Î¼ = âˆ« x, L (Ï† x) âˆ‚Î¼ :=
  integral_congr_ae <| coeFn_compLp _ _

theorem setIntegral_compLp (L : E â†’SL[Ïƒ] F) (Ï† : Lp E p Î¼) {s : Set X} (hs : MeasurableSet s) :
    âˆ« x in s, (L.compLp Ï†) x âˆ‚Î¼ = âˆ« x in s, L (Ï† x) âˆ‚Î¼ :=
  setIntegral_congr_ae hs ((L.coeFn_compLp Ï†).mono fun _x hx _ => hx)

theorem continuous_integral_comp_L1 (L : E â†’SL[Ïƒ] F) :
    Continuous fun Ï† : X â†’â‚[Î¼] E => âˆ« x : X, L (Ï† x) âˆ‚Î¼ := by
  rw [â† funext L.integral_compLp]; exact continuous_integral.comp (L.compLpL 1 Î¼).continuous

variable [CompleteSpace F] [CompleteSpace Fâ‚—] [NormedSpace â„ E]

theorem integral_comp_commSL [CompleteSpace E] (hÏƒ : âˆ€ (r : â„) (x : ğ•œ), Ïƒ (r â€¢ x) = r â€¢ Ïƒ x)
    (L : E â†’SL[Ïƒ] F) {Ï† : X â†’ E} (Ï†_int : Integrable Ï† Î¼) : âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := by
  apply Ï†_int.induction (P := fun Ï† => âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼))
  Â· intro e s s_meas _
    rw [integral_indicator_const e s_meas, â† @smul_one_smul E â„ ğ•œ _ _ _ _ _ (Î¼.real s) e,
      ContinuousLinearMap.map_smulâ‚›â‚—, hÏƒ, map_one, smul_assoc, one_smul,
      â† integral_indicator_const (L e) s_meas]
    congr 1 with a
    rw [â† Function.comp_def L, Set.indicator_comp_of_zero L.map_zero, Function.comp_apply]
  Â· intro f g _ f_int g_int hf hg
    simp [L.map_add, integral_add (Î¼ := Î¼) f_int g_int,
      integral_add (Î¼ := Î¼) (L.integrable_comp f_int) (L.integrable_comp g_int), hf, hg]
  Â· exact isClosed_eq L.continuous_integral_comp_L1 (L.continuous.comp continuous_integral)
  Â· intro f g hfg _ hf
    convert hf using 1 <;> clear hf
    Â· exact integral_congr_ae (hfg.fun_comp L).symm
    Â· rw [integral_congr_ae hfg.symm]

theorem integral_comp_comm [CompleteSpace E] (L : E â†’L[ğ•œ] Fâ‚—) {Ï† : X â†’ E} (Ï†_int : Integrable Ï† Î¼) :
    âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := integral_comp_commSL (by simp) L Ï†_int

theorem integral_apply {H : Type*} [NormedAddCommGroup H] [NormedSpace ğ•œ H] {Ï† : X â†’ H â†’L[ğ•œ] E}
    (Ï†_int : Integrable Ï† Î¼) (v : H) : (âˆ« x, Ï† x âˆ‚Î¼) v = âˆ« x, Ï† x v âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E
  Â· exact ((ContinuousLinearMap.apply ğ•œ E v).integral_comp_comm Ï†_int).symm
  Â· rcases subsingleton_or_nontrivial H with hH|hH
    Â· simp [Subsingleton.eq_zero v]
    Â· have : Â¬(CompleteSpace (H â†’L[ğ•œ] E)) := by
        rwa [SeparatingDual.completeSpace_continuousLinearMap_iff]
      simp [integral, hE, this]

theorem _root_.ContinuousMultilinearMap.integral_apply {Î¹ : Type*} [Fintype Î¹] {M : Î¹ â†’ Type*}
    [âˆ€ i, NormedAddCommGroup (M i)] [âˆ€ i, NormedSpace ğ•œ (M i)]
    {Ï† : X â†’ ContinuousMultilinearMap ğ•œ M E} (Ï†_int : Integrable Ï† Î¼) (m : âˆ€ i, M i) :
    (âˆ« x, Ï† x âˆ‚Î¼) m = âˆ« x, Ï† x m âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E
  Â· exact ((ContinuousMultilinearMap.apply ğ•œ M E m).integral_comp_comm Ï†_int).symm
  Â· by_cases hm : âˆ€ i, m i â‰  0
    Â· have : Â¬ CompleteSpace (ContinuousMultilinearMap ğ•œ M E) := by
        rwa [SeparatingDual.completeSpace_continuousMultilinearMap_iff _ _ hm]
      simp [integral, hE, this]
    Â· push_neg at hm
      rcases hm with âŸ¨i, hiâŸ©
      simp [ContinuousMultilinearMap.map_coord_zero _ i hi]

variable [CompleteSpace E]

theorem integral_comp_comm' (L : E â†’L[ğ•œ] Fâ‚—) {K} (hL : AntilipschitzWith K L) (Ï† : X â†’ E) :
    âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := by
  by_cases h : Integrable Ï† Î¼
  Â· exact integral_comp_comm L h
  have : Â¬Integrable (fun x => L (Ï† x)) Î¼ := by
    rwa [â† Function.comp_def,
      LipschitzWith.integrable_comp_iff_of_antilipschitz L.lipschitz hL L.map_zero]
  simp [integral_undef, h, this]

theorem integral_comp_L1_comm (L : E â†’L[ğ•œ] Fâ‚—) (Ï† : X â†’â‚[Î¼] E) :
    âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) :=
  L.integral_comp_comm (L1.integrable_coeFn Ï†)

end ContinuousLinearMap

namespace LinearIsometry

variable [CompleteSpace F] [NormedSpace ğ•œ F] [NormedSpace â„ F] [CompleteSpace E] [NormedSpace â„ E]

theorem integral_comp_comm (L : E â†’â‚—áµ¢[ğ•œ] F) (Ï† : X â†’ E) : âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) :=
  L.toContinuousLinearMap.integral_comp_comm' L.antilipschitz _

end LinearIsometry

namespace ContinuousLinearEquiv

variable [NormedSpace â„ F] [NormedSpace ğ•œ F] [NormedSpace â„ E]

theorem integral_comp_comm (L : E â‰ƒL[ğ•œ] F) (Ï† : X â†’ E) : âˆ« x, L (Ï† x) âˆ‚Î¼ = L (âˆ« x, Ï† x âˆ‚Î¼) := by
  have : CompleteSpace E â†” CompleteSpace F :=
    completeSpace_congr (e := L.toEquiv) L.isUniformEmbedding
  obtain âŸ¨_, _âŸ©|âŸ¨_, _âŸ© := iff_iff_and_or_not_and_not.mp this
  Â· exact L.toContinuousLinearMap.integral_comp_comm' L.antilipschitz _
  Â· simp [integral, *]

end ContinuousLinearEquiv

section ContinuousMap

variable [TopologicalSpace Y] [CompactSpace Y]

lemma ContinuousMap.integral_apply [NormedSpace â„ E] [CompleteSpace E] {f : X â†’ C(Y, E)}
    (hf : Integrable f Î¼) (y : Y) : (âˆ« x, f x âˆ‚Î¼) y = âˆ« x, f x y âˆ‚Î¼ := by
  calc (âˆ« x, f x âˆ‚Î¼) y = ContinuousMap.evalCLM â„ y (âˆ« x, f x âˆ‚Î¼) := rfl
    _ = âˆ« x, ContinuousMap.evalCLM â„ y (f x) âˆ‚Î¼ :=
          (ContinuousLinearMap.integral_comp_comm _ hf).symm
    _ = _ := rfl

open scoped ContinuousMapZero in
theorem ContinuousMapZero.integral_apply {R : Type*} [NormedCommRing R] [Zero Y]
    [NormedAlgebra â„ R] [CompleteSpace R] {f : X â†’ C(Y, R)â‚€}
    (hf : MeasureTheory.Integrable f Î¼) (y : Y) :
    (âˆ« (x : X), f x âˆ‚Î¼) y = âˆ« (x : X), (f x) y âˆ‚Î¼ := by
  calc (âˆ« x, f x âˆ‚Î¼) y = ContinuousMapZero.evalCLM â„ y (âˆ« x, f x âˆ‚Î¼) := rfl
    _ = âˆ« x, ContinuousMapZero.evalCLM â„ y (f x) âˆ‚Î¼ :=
          (ContinuousLinearMap.integral_comp_comm _ hf).symm
    _ = _ := rfl

end ContinuousMap

@[norm_cast]
theorem integral_ofReal {f : X â†’ â„} : âˆ« x, (f x : ğ•œ) âˆ‚Î¼ = â†‘(âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.ofRealLI ğ•œ _).integral_comp_comm f

theorem integral_complex_ofReal {f : X â†’ â„} : âˆ« x, (f x : â„‚) âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := integral_ofReal

theorem integral_re {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    âˆ« x, RCLike.re (f x) âˆ‚Î¼ = RCLike.re (âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.reCLM ğ•œ _).integral_comp_comm hf

theorem integral_im {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    âˆ« x, RCLike.im (f x) âˆ‚Î¼ = RCLike.im (âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.imCLM ğ•œ _).integral_comp_comm hf

open scoped ComplexConjugate in
theorem integral_conj {f : X â†’ ğ•œ} : âˆ« x, conj (f x) âˆ‚Î¼ = conj (âˆ« x, f x âˆ‚Î¼) :=
  (@RCLike.conjLIE ğ•œ _).toLinearIsometry.integral_comp_comm f

theorem integral_coe_re_add_coe_im {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    âˆ« x, (re (f x) : ğ•œ) âˆ‚Î¼ + (âˆ« x, (im (f x) : ğ•œ) âˆ‚Î¼) * RCLike.I = âˆ« x, f x âˆ‚Î¼ := by
  rw [mul_comm, â† smul_eq_mul, â† integral_smul, â† integral_add]
  Â· congr
    ext1 x
    rw [smul_eq_mul, mul_comm, RCLike.re_add_im]
  Â· exact hf.re.ofReal
  Â· exact hf.im.ofReal.smul (ğ•œ := ğ•œ) (Î² := ğ•œ) RCLike.I

theorem integral_re_add_im {f : X â†’ ğ•œ} (hf : Integrable f Î¼) :
    ((âˆ« x, RCLike.re (f x) âˆ‚Î¼ : â„) : ğ•œ) + (âˆ« x, RCLike.im (f x) âˆ‚Î¼ : â„) * RCLike.I =
      âˆ« x, f x âˆ‚Î¼ := by
  rw [â† integral_ofReal, â† integral_ofReal, integral_coe_re_add_coe_im hf]

theorem setIntegral_re_add_im {f : X â†’ ğ•œ} {i : Set X} (hf : IntegrableOn f i Î¼) :
    ((âˆ« x in i, RCLike.re (f x) âˆ‚Î¼ : â„) : ğ•œ) + (âˆ« x in i, RCLike.im (f x) âˆ‚Î¼ : â„) * RCLike.I =
      âˆ« x in i, f x âˆ‚Î¼ :=
  integral_re_add_im hf

variable [NormedSpace â„ E] [NormedSpace â„ F]

lemma swap_integral (f : X â†’ E Ã— F) : (âˆ« x, f x âˆ‚Î¼).swap = âˆ« x, (f x).swap âˆ‚Î¼ :=
  .symm <| (ContinuousLinearEquiv.prodComm â„ E F).integral_comp_comm f

theorem fst_integral [CompleteSpace F] {f : X â†’ E Ã— F} (hf : Integrable f Î¼) :
    (âˆ« x, f x âˆ‚Î¼).1 = âˆ« x, (f x).1 âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E
  Â· exact ((ContinuousLinearMap.fst â„ E F).integral_comp_comm hf).symm
  Â· have : Â¬(CompleteSpace (E Ã— F)) := fun h â†¦ hE <| .fst_of_prod (Î² := F)
    simp [integral, *]

theorem snd_integral [CompleteSpace E] {f : X â†’ E Ã— F} (hf : Integrable f Î¼) :
    (âˆ« x, f x âˆ‚Î¼).2 = âˆ« x, (f x).2 âˆ‚Î¼ := by
  rw [â† Prod.fst_swap, swap_integral]
  exact fst_integral <| hf.snd.prodMk hf.fst

theorem integral_pair [CompleteSpace E] [CompleteSpace F] {f : X â†’ E} {g : X â†’ F}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« x, (f x, g x) âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼, âˆ« x, g x âˆ‚Î¼) :=
  have := hf.prodMk hg
  Prod.ext (fst_integral this) (snd_integral this)

theorem integral_smul_const {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [CompleteSpace E]
    (f : X â†’ ğ•œ) (c : E) :
    âˆ« x, f x â€¢ c âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼) â€¢ c := by
  by_cases hf : Integrable f Î¼
  Â· exact ((1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight c).integral_comp_comm hf
  Â· by_cases hc : c = 0
    Â· simp [hc, integral_zero, smul_zero]
    rw [integral_undef hf, integral_undef, zero_smul]
    rw [integrable_smul_const hc]
    simp_rw [hf, not_false_eq_true]

/-
Note that the integrability hypothesis in the two lemmas below is necessary: consider the case
where `A = â„ Ã— â„`, `c = (1,0)`, and `f` is only integrable on the first component.
-/
lemma integral_const_mul_of_integrable {A : Type*} [NonUnitalNormedRing A] [NormedSpace â„ A]
    [IsScalarTower â„ A A] [SMulCommClass â„ A A] {f : X â†’ A} (hf : Integrable f Î¼) {c : A} :
    âˆ« x, c * f x âˆ‚Î¼ = c * âˆ« x, f x âˆ‚Î¼ := by
  by_cases hA : CompleteSpace A
  Â· change âˆ« x, ContinuousLinearMap.mul â„ _ c (f x) âˆ‚Î¼ = ContinuousLinearMap.mul â„ _ c (âˆ« x, f x âˆ‚Î¼)
    rw [ContinuousLinearMap.integral_comp_comm _ hf]
  Â· simp [integral, hA]

lemma integral_mul_const_of_integrable {A : Type*} [NonUnitalNormedRing A] [NormedSpace â„ A]
    [IsScalarTower â„ A A] [SMulCommClass â„ A A] {f : X â†’ A} (hf : Integrable f Î¼) {c : A} :
    âˆ« x, f x * c âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼) * c := by
  by_cases hA : CompleteSpace A
  Â· change âˆ« x, (ContinuousLinearMap.mul â„ _).flip c (f x) âˆ‚Î¼
      = (ContinuousLinearMap.mul â„ _).flip c (âˆ« x, f x âˆ‚Î¼)
    rw [ContinuousLinearMap.integral_comp_comm _ hf]
  Â· simp [integral, hA]

theorem integral_withDensity_eq_integral_smul {f : X â†’ â„â‰¥0} (f_meas : Measurable f) (g : X â†’ E) :
    âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, f x â€¢ g x âˆ‚Î¼ := by
  by_cases hE : CompleteSpace E; swap; Â· simp [integral, hE]
  by_cases hg : Integrable g (Î¼.withDensity fun x => f x); swap
  Â· rw [integral_undef hg, integral_undef]
    rwa [â† integrable_withDensity_iff_integrable_smul f_meas]
  refine Integrable.induction
    (P := fun g => âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, f x â€¢ g x âˆ‚Î¼) ?_ ?_ ?_ ?_ hg
  Â· intro c s s_meas hs
    rw [integral_indicator s_meas]
    simp_rw [â† Set.indicator_smul_apply, integral_indicator s_meas]
    simp only [s_meas, integral_const, Measure.restrict_apply', Set.univ_inter, withDensity_apply,
      measureReal_def]
    rw [lintegral_coe_eq_integral, ENNReal.toReal_ofReal, â† integral_smul_const]
    Â· rfl
    Â· exact integral_nonneg fun x => NNReal.coe_nonneg _
    Â· refine âŸ¨f_meas.coe_nnreal_real.aemeasurable.aestronglyMeasurable, ?_âŸ©
      simpa [withDensity_apply _ s_meas, hasFiniteIntegral_iff_enorm] using hs
  Â· intro u u' _ u_int u'_int h h'
    change
      (âˆ« x : X, u x + u' x âˆ‚Î¼.withDensity fun x : X => â†‘(f x)) = âˆ« x : X, f x â€¢ (u x + u' x) âˆ‚Î¼
    simp_rw [smul_add]
    rw [integral_add u_int u'_int, h, h', integral_add]
    Â· exact (integrable_withDensity_iff_integrable_smul f_meas).1 u_int
    Â· exact (integrable_withDensity_iff_integrable_smul f_meas).1 u'_int
  Â· have C1 :
      Continuous fun u : Lp E 1 (Î¼.withDensity fun x => f x) =>
        âˆ« x, u x âˆ‚Î¼.withDensity fun x => f x :=
      continuous_integral
    have C2 : Continuous fun u : Lp E 1 (Î¼.withDensity fun x => f x) => âˆ« x, f x â€¢ u x âˆ‚Î¼ := by
      have : Continuous ((fun u : Lp E 1 Î¼ => âˆ« x, u x âˆ‚Î¼) âˆ˜ withDensitySMulLI (E := E) Î¼ f_meas) :=
        continuous_integral.comp (withDensitySMulLI (E := E) Î¼ f_meas).continuous
      convert this with u
      simp only [Function.comp_apply, withDensitySMulLI_apply]
      exact integral_congr_ae (memL1_smul_of_L1_withDensity f_meas u).coeFn_toLp.symm
    exact isClosed_eq C1 C2
  Â· intro u v huv _ hu
    rw [â† integral_congr_ae huv, hu]
    apply integral_congr_ae
    filter_upwards [(ae_withDensity_iff f_meas.coe_nnreal_ennreal).1 huv] with x hx
    rcases eq_or_ne (f x) 0 with (h'x | h'x)
    Â· simp only [h'x, zero_smul]
    Â· rw [hx _]
      simpa only [Ne, ENNReal.coe_eq_zero] using h'x

theorem integral_withDensity_eq_integral_smulâ‚€ {f : X â†’ â„â‰¥0} (hf : AEMeasurable f Î¼) (g : X â†’ E) :
    âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, f x â€¢ g x âˆ‚Î¼ := by
  let f' := hf.mk _
  calc
    âˆ« x, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x, g x âˆ‚Î¼.withDensity fun x => f' x := by
      congr 1
      apply withDensity_congr_ae
      filter_upwards [hf.ae_eq_mk] with x hx
      rw [hx]
    _ = âˆ« x, f' x â€¢ g x âˆ‚Î¼ := integral_withDensity_eq_integral_smul hf.measurable_mk _
    _ = âˆ« x, f x â€¢ g x âˆ‚Î¼ := by
      apply integral_congr_ae
      filter_upwards [hf.ae_eq_mk] with x hx
      rw [hx]

theorem integral_withDensity_eq_integral_toReal_smulâ‚€ {f : X â†’ â„â‰¥0âˆ} (f_meas : AEMeasurable f Î¼)
    (hf_lt_top : âˆ€áµ x âˆ‚Î¼, f x < âˆ) (g : X â†’ E) :
    âˆ« x, g x âˆ‚Î¼.withDensity f = âˆ« x, (f x).toReal â€¢ g x âˆ‚Î¼ := by
  dsimp only [ENNReal.toReal, â† NNReal.smul_def]
  rw [â† integral_withDensity_eq_integral_smulâ‚€ f_meas.ennreal_toNNReal,
    withDensity_congr_ae (coe_toNNReal_ae_eq hf_lt_top)]

theorem integral_withDensity_eq_integral_toReal_smul {f : X â†’ â„â‰¥0âˆ} (f_meas : Measurable f)
    (hf_lt_top : âˆ€áµ x âˆ‚Î¼, f x < âˆ) (g : X â†’ E) :
    âˆ« x, g x âˆ‚Î¼.withDensity f = âˆ« x, (f x).toReal â€¢ g x âˆ‚Î¼ :=
  integral_withDensity_eq_integral_toReal_smulâ‚€ f_meas.aemeasurable hf_lt_top g

theorem setIntegral_withDensity_eq_setIntegral_smulâ‚€ {f : X â†’ â„â‰¥0} {s : Set X}
    (hf : AEMeasurable f (Î¼.restrict s)) (g : X â†’ E) (hs : MeasurableSet s) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, f x â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity hs, integral_withDensity_eq_integral_smulâ‚€ hf]

theorem setIntegral_withDensity_eq_setIntegral_toReal_smulâ‚€ {f : X â†’ â„â‰¥0âˆ} {s : Set X}
    (hf : AEMeasurable f (Î¼.restrict s)) (hf_top : âˆ€áµ x âˆ‚Î¼.restrict s, f x < âˆ) (g : X â†’ E)
    (hs : MeasurableSet s) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, (f x).toReal â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity hs, integral_withDensity_eq_integral_toReal_smulâ‚€ hf hf_top]

theorem setIntegral_withDensity_eq_setIntegral_smul {f : X â†’ â„â‰¥0} (f_meas : Measurable f)
    (g : X â†’ E) {s : Set X} (hs : MeasurableSet s) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, f x â€¢ g x âˆ‚Î¼ :=
  setIntegral_withDensity_eq_setIntegral_smulâ‚€ f_meas.aemeasurable _ hs

theorem setIntegral_withDensity_eq_setIntegral_toReal_smul {f : X â†’ â„â‰¥0âˆ} {s : Set X}
    (hf : Measurable f) (hf_top : âˆ€áµ x âˆ‚Î¼.restrict s, f x < âˆ) (g : X â†’ E) (hs : MeasurableSet s) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, (f x).toReal â€¢ g x âˆ‚Î¼ :=
  setIntegral_withDensity_eq_setIntegral_toReal_smulâ‚€ hf.aemeasurable hf_top g hs

theorem setIntegral_withDensity_eq_setIntegral_smulâ‚€' [SFinite Î¼] {f : X â†’ â„â‰¥0} (s : Set X)
    (hf : AEMeasurable f (Î¼.restrict s)) (g : X â†’ E) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity (fun x => f x) = âˆ« x in s, f x â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity' s, integral_withDensity_eq_integral_smulâ‚€ hf]

theorem setIntegral_withDensity_eq_setIntegral_toReal_smulâ‚€' [SFinite Î¼] {f : X â†’ â„â‰¥0âˆ} (s : Set X)
    (hf : AEMeasurable f (Î¼.restrict s)) (hf_top : âˆ€áµ x âˆ‚Î¼.restrict s, f x < âˆ) (g : X â†’ E) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity f = âˆ« x in s, (f x).toReal â€¢ g x âˆ‚Î¼ := by
  rw [restrict_withDensity' s, integral_withDensity_eq_integral_toReal_smulâ‚€ hf hf_top]

theorem setIntegral_withDensity_eq_setIntegral_toReal_smul' [SFinite Î¼] {f : X â†’ â„â‰¥0âˆ} (s : Set X)
    (hf : Measurable f) (hf_top : âˆ€áµ x âˆ‚Î¼.restrict s, f x < âˆ) (g : X â†’ E) :
    âˆ« x in s, g x âˆ‚Î¼.withDensity f = âˆ« x in s, (f x).toReal â€¢ g x âˆ‚Î¼ :=
  setIntegral_withDensity_eq_setIntegral_toReal_smulâ‚€' s hf.aemeasurable hf_top g
