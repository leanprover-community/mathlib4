/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, SÃ©bastien GouÃ«zel, RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Integral.SetToL1

/-!
# Bochner integral

The Bochner integral extends the definition of the Lebesgue integral to functions that map from a
measure space into a Banach space (complete normed vector space). It is constructed here
for L1 functions by extending the integral on simple functions. See the file
`Mathlib/MeasureTheory/Integral/Bochner/Basic.lean` for the integral of functions
and corresponding API.

## Main definitions

The Bochner integral is defined through the extension process described in the file
`Mathlib/MeasureTheory/Integral/SetToL1.lean`, which follows these steps:

1. Define the integral of the indicator of a set. This is `weightedSMul Î¼ s x = Î¼.real s * x`.
  `weightedSMul Î¼` is shown to be linear in the value `x` and `DominatedFinMeasAdditive`
  (defined in the file `Mathlib/MeasureTheory/Integral/SetToL1.lean`) with respect to the set `s`.

2. Define the integral on simple functions of the type `SimpleFunc Î± E` (notation : `Î± â†’â‚› E`)
  where `E` is a real normed space. (See `SimpleFunc.integral` for details.)

3. Transfer this definition to define the integral on `L1.simpleFunc Î± E` (notation :
  `Î± â†’â‚â‚›[Î¼] E`), see `L1.simpleFunc.integral`. Show that this integral is a continuous linear
  map from `Î± â†’â‚â‚›[Î¼] E` to `E`.

4. Define the Bochner integral on L1 functions by extending the integral on integrable simple
  functions `Î± â†’â‚â‚›[Î¼] E` using `ContinuousLinearMap.extend` and the fact that the embedding of
  `Î± â†’â‚â‚›[Î¼] E` into `Î± â†’â‚[Î¼] E` is dense.

## Notation

* `Î± â†’â‚› E` : simple functions (defined in `Mathlib/MeasureTheory/Function/SimpleFunc.lean`)
* `Î± â†’â‚[Î¼] E` : functions in L1 space, i.e., equivalence classes of integrable functions (defined in
                `Mathlib/MeasureTheory/Function/LpSpace/Basic.lean`)
* `Î± â†’â‚â‚›[Î¼] E` : simple functions in L1 space, i.e., equivalence classes of integrable simple
                 functions (defined in `Mathlib/MeasureTheory/Function/SimpleFuncDense`)

We also define notations for integral on a set, which are described in the file
`Mathlib/MeasureTheory/Integral/SetIntegral.lean`.

Note : `â‚›` is typed using `\_s`. Sometimes it shows as a box if the font is missing.

## Tags

Bochner integral, simple function, function space, Lebesgue dominated convergence theorem

-/


assert_not_exists Differentiable

noncomputable section

open Filter ENNReal Set
open scoped NNReal ENNReal MeasureTheory

namespace MeasureTheory

variable {Î± E F ğ•œ : Type*}

section WeightedSMul

open ContinuousLinearMap

variable [NormedAddCommGroup F] [NormedSpace â„ F] {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- Given a set `s`, return the continuous linear map `fun x => Î¼.real s â€¢ x`. The extension
of that set function through `setToL1` gives the Bochner integral of L1 functions. -/
def weightedSMul {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (s : Set Î±) : F â†’L[â„] F :=
  Î¼.real s â€¢ ContinuousLinearMap.id â„ F

theorem weightedSMul_apply {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (s : Set Î±) (x : F) :
    weightedSMul Î¼ s x = Î¼.real s â€¢ x := by simp [weightedSMul]

@[simp]
theorem weightedSMul_zero_measure {m : MeasurableSpace Î±} :
    weightedSMul (0 : Measure Î±) = (0 : Set Î± â†’ F â†’L[â„] F) := by ext1; simp [weightedSMul]

@[simp]
theorem weightedSMul_empty {m : MeasurableSpace Î±} (Î¼ : Measure Î±) :
    weightedSMul Î¼ âˆ… = (0 : F â†’L[â„] F) := by ext1 x; rw [weightedSMul_apply]; simp

theorem weightedSMul_add_measure {m : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±) {s : Set Î±}
    (hÎ¼s : Î¼ s â‰  âˆ) (hÎ½s : Î½ s â‰  âˆ) :
    (weightedSMul (Î¼ + Î½) s : F â†’L[â„] F) = weightedSMul Î¼ s + weightedSMul Î½ s := by
  ext1 x
  push_cast
  simp_rw [Pi.add_apply, weightedSMul_apply]
  rw [measureReal_add_apply, add_smul]

theorem weightedSMul_smul_measure {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (c : â„â‰¥0âˆ) {s : Set Î±} :
    (weightedSMul (c â€¢ Î¼) s : F â†’L[â„] F) = c.toReal â€¢ weightedSMul Î¼ s := by
  ext1 x
  simp [weightedSMul_apply, smul_smul]

theorem weightedSMul_congr (s t : Set Î±) (hst : Î¼ s = Î¼ t) :
    (weightedSMul Î¼ s : F â†’L[â„] F) = weightedSMul Î¼ t := by
  ext1 x; simp_rw [weightedSMul_apply, measureReal_def]; congr 2

theorem weightedSMul_null {s : Set Î±} (h_zero : Î¼ s = 0) : (weightedSMul Î¼ s : F â†’L[â„] F) = 0 := by
  ext1 x; rw [weightedSMul_apply, measureReal_def, h_zero]; simp

theorem weightedSMul_union' (s t : Set Î±) (ht : MeasurableSet t) (hs_finite : Î¼ s â‰  âˆ)
    (ht_finite : Î¼ t â‰  âˆ) (hdisj : Disjoint s t) :
    (weightedSMul Î¼ (s âˆª t) : F â†’L[â„] F) = weightedSMul Î¼ s + weightedSMul Î¼ t := by
  ext1 x
  simp_rw [add_apply, weightedSMul_apply, measureReal_union hdisj ht,add_smul]

@[nolint unusedArguments]
theorem weightedSMul_union (s t : Set Î±) (_hs : MeasurableSet s) (ht : MeasurableSet t)
    (hs_finite : Î¼ s â‰  âˆ) (ht_finite : Î¼ t â‰  âˆ) (hdisj : Disjoint s t) :
    (weightedSMul Î¼ (s âˆª t) : F â†’L[â„] F) = weightedSMul Î¼ s + weightedSMul Î¼ t :=
  weightedSMul_union' s t ht hs_finite ht_finite hdisj

theorem weightedSMul_smul [SMul ğ•œ F] [SMulCommClass â„ ğ•œ F] (c : ğ•œ)
    (s : Set Î±) (x : F) : weightedSMul Î¼ s (c â€¢ x) = c â€¢ weightedSMul Î¼ s x := by
  simp_rw [weightedSMul_apply, smul_comm]

theorem norm_weightedSMul_le (s : Set Î±) : â€–(weightedSMul Î¼ s : F â†’L[â„] F)â€– â‰¤ Î¼.real s :=
  calc
    â€–(weightedSMul Î¼ s : F â†’L[â„] F)â€– = â€–Î¼.real sâ€– * â€–ContinuousLinearMap.id â„ Fâ€– :=
      norm_smul (Î¼.real s) (ContinuousLinearMap.id â„ F)
    _ â‰¤ â€–Î¼.real sâ€– :=
      ((mul_le_mul_of_nonneg_left norm_id_le (norm_nonneg _)).trans (mul_one _).le)
    _ = abs Î¼.real s := Real.norm_eq_abs _
    _ = Î¼.real s := abs_eq_self.mpr ENNReal.toReal_nonneg

theorem dominatedFinMeasAdditive_weightedSMul {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) :
    DominatedFinMeasAdditive Î¼ (weightedSMul Î¼ : Set Î± â†’ F â†’L[â„] F) 1 :=
  âŸ¨weightedSMul_union, fun s _ _ => (norm_weightedSMul_le s).trans (one_mul _).symm.leâŸ©

theorem weightedSMul_nonneg [PartialOrder F] [IsOrderedModule â„ F]
    (s : Set Î±) (x : F) (hx : 0 â‰¤ x) : 0 â‰¤ weightedSMul Î¼ s x := by
  simp only [weightedSMul, coe_smul', _root_.id, coe_id', Pi.smul_apply]
  exact smul_nonneg toReal_nonneg hx

end WeightedSMul

local infixr:25 " â†’â‚› " => SimpleFunc

namespace SimpleFunc

section PosPart

variable [LinearOrder E] [Zero E] [MeasurableSpace Î±]

/-- Positive part of a simple function. -/
def posPart (f : Î± â†’â‚› E) : Î± â†’â‚› E :=
  f.map fun b => max b 0

/-- Negative part of a simple function. -/
def negPart [Neg E] (f : Î± â†’â‚› E) : Î± â†’â‚› E :=
  posPart (-f)

theorem posPart_map_norm (f : Î± â†’â‚› â„) : (posPart f).map norm = posPart f := by
  ext; rw [map_apply, Real.norm_eq_abs, abs_of_nonneg]; exact le_max_right _ _

theorem negPart_map_norm (f : Î± â†’â‚› â„) : (negPart f).map norm = negPart f := by
  rw [negPart]; exact posPart_map_norm _

theorem posPart_sub_negPart (f : Î± â†’â‚› â„) : f.posPart - f.negPart = f := by
  simp only [posPart, negPart]
  ext a
  rw [coe_sub]
  exact max_zero_sub_eq_self (f a)

end PosPart

section Integral

/-!
### The Bochner integral of simple functions

Define the Bochner integral of simple functions of the type `Î± â†’â‚› Î²` where `Î²` is a normed group,
and prove basic property of this integral.
-/


open Finset

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ F]
  {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- Bochner integral of simple functions whose codomain is a real `NormedSpace`.
This is equal to `âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) â€¢ x` (see `integral_eq`). -/
def integral {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’â‚› F) : F :=
  f.setToSimpleFunc (weightedSMul Î¼)

theorem integral_def {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’â‚› F) :
    f.integral Î¼ = f.setToSimpleFunc (weightedSMul Î¼) := rfl

theorem integral_eq {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (f : Î± â†’â‚› F) :
    f.integral Î¼ = âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) â€¢ x := by
  simp [integral, setToSimpleFunc, weightedSMul_apply]

theorem integral_eq_sum_filter [DecidablePred fun x : F => x â‰  0] {m : MeasurableSpace Î±}
    (f : Î± â†’â‚› F) (Î¼ : Measure Î±) :
    f.integral Î¼ = âˆ‘ x âˆˆ {x âˆˆ f.range | x â‰  0}, Î¼.real (f â»Â¹' {x}) â€¢ x := by
  simp_rw [integral_def, setToSimpleFunc_eq_sum_filter, weightedSMul_apply]

/-- The Bochner integral is equal to a sum over any set that includes `f.range` (except `0`). -/
theorem integral_eq_sum_of_subset [DecidablePred fun x : F => x â‰  0] {f : Î± â†’â‚› F} {s : Finset F}
    (hs : {x âˆˆ f.range | x â‰  0} âŠ† s) :
    f.integral Î¼ = âˆ‘ x âˆˆ s, Î¼.real (f â»Â¹' {x}) â€¢ x := by
  rw [SimpleFunc.integral_eq_sum_filter, Finset.sum_subset hs]
  rintro x - hx; rw [Finset.mem_filter, not_and_or, Ne, Classical.not_not] at hx
  rcases hx.symm with (rfl | hx)
  Â· simp
  rw [SimpleFunc.mem_range] at hx
  rw [preimage_eq_empty] <;> simp [Set.disjoint_singleton_left, hx]

@[simp]
theorem integral_const {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (y : F) :
    (const Î± y).integral Î¼ = Î¼.real univ â€¢ y := by
  classical
  calc
    (const Î± y).integral Î¼ = âˆ‘ z âˆˆ {y}, Î¼.real (const Î± y â»Â¹' {z}) â€¢ z :=
      integral_eq_sum_of_subset <| (filter_subset _ _).trans (range_const_subset _ _)
    _ = Î¼.real univ â€¢ y := by simp [Set.preimage]

@[simp]
theorem integral_piecewise_zero {m : MeasurableSpace Î±} (f : Î± â†’â‚› F) (Î¼ : Measure Î±) {s : Set Î±}
    (hs : MeasurableSet s) : (piecewise s hs f 0).integral Î¼ = f.integral (Î¼.restrict s) := by
  classical
  refine (integral_eq_sum_of_subset ?_).trans
      ((sum_congr rfl fun y hy => ?_).trans (integral_eq_sum_filter _ _).symm)
  Â· intro y hy
    simp only [mem_filter, mem_range, coe_piecewise, coe_zero, piecewise_eq_indicator,
      mem_range_indicator] at *
    rcases hy with âŸ¨âŸ¨rfl, -âŸ© | âŸ¨x, -, rflâŸ©, hâ‚€âŸ©
    exacts [(hâ‚€ rfl).elim, âŸ¨Set.mem_range_self _, hâ‚€âŸ©]
  Â· dsimp
    rw [Set.piecewise_eq_indicator, indicator_preimage_of_notMem,
      measureReal_restrict_apply (f.measurableSet_preimage _)]
    exact fun hâ‚€ => (mem_filter.1 hy).2 (Eq.symm hâ‚€)

/-- Calculate the integral of `g âˆ˜ f : Î± â†’â‚› F`, where `f` is an integrable function from `Î±` to `E`
and `g` is a function from `E` to `F`. We require `g 0 = 0` so that `g âˆ˜ f` is integrable. -/
theorem map_integral (f : Î± â†’â‚› E) (g : E â†’ F) (hf : Integrable f Î¼) (hg : g 0 = 0) :
    (f.map g).integral Î¼ = âˆ‘ x âˆˆ f.range, (Î¼.real (f â»Â¹' {x})) â€¢ g x :=
  map_setToSimpleFunc _ weightedSMul_union hf hg

/-- `SimpleFunc.integral` and `SimpleFunc.lintegral` agree when the integrand has type
`Î± â†’â‚› â„â‰¥0âˆ`. But since `â„â‰¥0âˆ` is not a `NormedSpace`, we need some form of coercion.
See `integral_eq_lintegral` for a simpler version. -/
theorem integral_eq_lintegral' {f : Î± â†’â‚› E} {g : E â†’ â„â‰¥0âˆ} (hf : Integrable f Î¼) (hg0 : g 0 = 0)
    (ht : âˆ€ b, g b â‰  âˆ) :
    (f.map (ENNReal.toReal âˆ˜ g)).integral Î¼ = ENNReal.toReal (âˆ«â» a, g (f a) âˆ‚Î¼) := by
  have hf' : f.FinMeasSupp Î¼ := integrable_iff_finMeasSupp.1 hf
  simp only [â† map_apply g f, lintegral_eq_lintegral]
  rw [map_integral f _ hf, map_lintegral, ENNReal.toReal_sum]
  Â· refine Finset.sum_congr rfl fun b _ => ?_
    rw [smul_eq_mul, toReal_mul, mul_comm, Function.comp_apply, measureReal_def]
  Â· rintro a -
    by_cases a0 : a = 0
    Â· rw [a0, hg0, zero_mul]; exact WithTop.zero_ne_top
    Â· apply mul_ne_top (ht a) (hf'.meas_preimage_singleton_ne_zero a0).ne
  Â· simp [hg0]

variable [NormedSpace â„ E]

theorem integral_congr {f g : Î± â†’â‚› E} (hf : Integrable f Î¼) (h : f =áµ[Î¼] g) :
    f.integral Î¼ = g.integral Î¼ :=
  setToSimpleFunc_congr (weightedSMul Î¼) (fun _ _ => weightedSMul_null) weightedSMul_union hf h

/-- `SimpleFunc.integral` and `SimpleFunc.lintegral` agree when the integrand has type
`Î± â†’â‚› â„â‰¥0âˆ`. But since `â„â‰¥0âˆ` is not a `NormedSpace`, we need some form of coercion. -/
theorem integral_eq_lintegral {f : Î± â†’â‚› â„} (hf : Integrable f Î¼) (h_pos : 0 â‰¤áµ[Î¼] f) :
    f.integral Î¼ = ENNReal.toReal (âˆ«â» a, ENNReal.ofReal (f a) âˆ‚Î¼) := by
  have : f =áµ[Î¼] f.map (ENNReal.toReal âˆ˜ ENNReal.ofReal) :=
    h_pos.mono fun a h => (ENNReal.toReal_ofReal h).symm
  rw [â† integral_eq_lintegral' hf]
  exacts [integral_congr hf this, ENNReal.ofReal_zero, fun b => ENNReal.ofReal_ne_top]

theorem integral_add {f g : Î± â†’â‚› E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    integral Î¼ (f + g) = integral Î¼ f + integral Î¼ g :=
  setToSimpleFunc_add _ weightedSMul_union hf hg

theorem integral_neg {f : Î± â†’â‚› E} (hf : Integrable f Î¼) : integral Î¼ (-f) = -integral Î¼ f :=
  setToSimpleFunc_neg _ weightedSMul_union hf

theorem integral_sub {f g : Î± â†’â‚› E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    integral Î¼ (f - g) = integral Î¼ f - integral Î¼ g :=
  setToSimpleFunc_sub _ weightedSMul_union hf hg

theorem integral_smul [DistribSMul ğ•œ E] [SMulCommClass â„ ğ•œ E]
    (c : ğ•œ) {f : Î± â†’â‚› E} (hf : Integrable f Î¼) :
    integral Î¼ (c â€¢ f) = c â€¢ integral Î¼ f :=
  setToSimpleFunc_smul _ weightedSMul_union weightedSMul_smul c hf

theorem norm_setToSimpleFunc_le_integral_norm (T : Set Î± â†’ E â†’L[â„] F) {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * Î¼.real s) {f : Î± â†’â‚› E}
    (hf : Integrable f Î¼) : â€–f.setToSimpleFunc Tâ€– â‰¤ C * (f.map norm).integral Î¼ :=
  calc
    â€–f.setToSimpleFunc Tâ€– â‰¤ C * âˆ‘ x âˆˆ f.range, Î¼.real (f â»Â¹' {x}) * â€–xâ€– :=
      norm_setToSimpleFunc_le_sum_mul_norm_of_integrable T hT_norm f hf
    _ = C * (f.map norm).integral Î¼ := by
      rw [map_integral f norm hf norm_zero]; simp_rw [smul_eq_mul]

theorem norm_integral_le_integral_norm (f : Î± â†’â‚› E) (hf : Integrable f Î¼) :
    â€–f.integral Î¼â€– â‰¤ (f.map norm).integral Î¼ := by
  refine (norm_setToSimpleFunc_le_integral_norm _ (fun s _ _ => ?_) hf).trans (one_mul _).le
  exact (norm_weightedSMul_le s).trans (one_mul _).symm.le

theorem integral_add_measure {Î½} (f : Î± â†’â‚› E) (hf : Integrable f (Î¼ + Î½)) :
    f.integral (Î¼ + Î½) = f.integral Î¼ + f.integral Î½ := by
  simp_rw [integral_def]
  refine setToSimpleFunc_add_left'
    (weightedSMul Î¼) (weightedSMul Î½) (weightedSMul (Î¼ + Î½)) (fun s _ hÎ¼Î½s => ?_) hf
  rw [lt_top_iff_ne_top, Measure.coe_add, Pi.add_apply, ENNReal.add_ne_top] at hÎ¼Î½s
  rw [weightedSMul_add_measure _ _ hÎ¼Î½s.1 hÎ¼Î½s.2]

section Order

variable [PartialOrder F] [IsOrderedAddMonoid F] [IsOrderedModule â„ F]

lemma integral_nonneg {f : Î± â†’â‚› F} (hf : 0 â‰¤áµ[Î¼] f) :
    0 â‰¤ f.integral Î¼ := by
  rw [integral_eq]
  apply Finset.sum_nonneg
  rw [forall_mem_range]
  intro y
  by_cases hy : 0 â‰¤ f y
  Â· positivity
  Â· suffices Î¼ (f â»Â¹' {f y}) = 0 by simp [this, measureReal_def]
    rw [â† nonpos_iff_eq_zero]
    refine le_of_le_of_eq (measure_mono fun x hx â†¦ ?_) (ae_iff.mp hf)
    simp only [Set.mem_preimage, mem_singleton_iff, mem_setOf_eq] at hx âŠ¢
    exact hx â–¸ hy

lemma integral_mono {f g : Î± â†’â‚› F} (h : f â‰¤áµ[Î¼] g) (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    f.integral Î¼ â‰¤ g.integral Î¼ := by
  rw [â† sub_nonneg, â† integral_sub hg hf]
  rw [â† sub_nonneg_ae] at h
  exact integral_nonneg h

lemma integral_mono_measure {Î½} {f : Î± â†’â‚› F} (hf : 0 â‰¤áµ[Î½] f) (hÎ¼Î½ : Î¼ â‰¤ Î½) (hfÎ½ : Integrable f Î½) :
    f.integral Î¼ â‰¤ f.integral Î½ := by
  simp only [integral_eq]
  apply Finset.sum_le_sum
  simp only [forall_mem_range]
  intro x
  by_cases hx : 0 â‰¤ f x
  Â· obtain (hx | hx) := hx.eq_or_lt
    Â· simp [â† hx]
    simp only [measureReal_def]
    gcongr
    Â· exact integrable_iff.mp hfÎ½ (f x) hx.ne' |>.ne
    Â· exact hÎ¼Î½ _
  Â· suffices Î½ (f â»Â¹' {f x}) = 0 by
      have A : Î¼ (f â»Â¹' {f x}) = 0 := by simpa using (hÎ¼Î½ _ |>.trans_eq this)
      simp [measureReal_def, A, this]
    rw [â† nonpos_iff_eq_zero, â† ae_iff.mp hf]
    refine measure_mono fun y hy â†¦ ?_
    simp_all

end Order

end Integral

end SimpleFunc

namespace L1

open AEEqFun Lp.simpleFunc Lp

variable [NormedAddCommGroup E] {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

namespace SimpleFunc

theorem norm_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) : â€–fâ€– = ((toSimpleFunc f).map norm).integral Î¼ := by
  rw [norm_eq_sum_mul f, (toSimpleFunc f).map_integral norm (SimpleFunc.integrable f) norm_zero]
  simp_rw [smul_eq_mul]

section PosPart

/-- Positive part of a simple function in L1 space. -/
nonrec def posPart (f : Î± â†’â‚â‚›[Î¼] â„) : Î± â†’â‚â‚›[Î¼] â„ :=
  âŸ¨Lp.posPart (f : Î± â†’â‚[Î¼] â„), by
    rcases f with âŸ¨f, s, hsfâŸ©
    use s.posPart
    simp only [SimpleFunc.posPart, SimpleFunc.coe_map, Function.comp_def, coe_posPart, â† hsf,
      posPart_mk] âŸ©

/-- Negative part of a simple function in L1 space. -/
def negPart (f : Î± â†’â‚â‚›[Î¼] â„) : Î± â†’â‚â‚›[Î¼] â„ :=
  posPart (-f)

@[norm_cast]
theorem coe_posPart (f : Î± â†’â‚â‚›[Î¼] â„) : (posPart f : Î± â†’â‚[Î¼] â„) = Lp.posPart (f : Î± â†’â‚[Î¼] â„) := rfl

@[norm_cast]
theorem coe_negPart (f : Î± â†’â‚â‚›[Î¼] â„) : (negPart f : Î± â†’â‚[Î¼] â„) = Lp.negPart (f : Î± â†’â‚[Î¼] â„) := rfl

end PosPart

section SimpleFuncIntegral

/-!
### The Bochner integral of `L1`

Define the Bochner integral on `Î± â†’â‚â‚›[Î¼] E` by extension from the simple functions `Î± â†’â‚â‚›[Î¼] E`,
and prove basic properties of this integral. -/

variable [NormedRing ğ•œ] [Module ğ•œ E] [IsBoundedSMul ğ•œ E] [NormedSpace â„ E] [SMulCommClass â„ ğ•œ E]

attribute [local instance] simpleFunc.isBoundedSMul simpleFunc.module simpleFunc.normedSpace

/-- The Bochner integral over simple functions in L1 space. -/
def integral (f : Î± â†’â‚â‚›[Î¼] E) : E :=
  (toSimpleFunc f).integral Î¼

theorem integral_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) : integral f = (toSimpleFunc f).integral Î¼ := rfl

nonrec theorem integral_eq_lintegral {f : Î± â†’â‚â‚›[Î¼] â„} (h_pos : 0 â‰¤áµ[Î¼] toSimpleFunc f) :
    integral f = ENNReal.toReal (âˆ«â» a, ENNReal.ofReal ((toSimpleFunc f) a) âˆ‚Î¼) := by
  rw [integral, SimpleFunc.integral_eq_lintegral (SimpleFunc.integrable f) h_pos]

theorem integral_eq_setToL1S (f : Î± â†’â‚â‚›[Î¼] E) : integral f = setToL1S (weightedSMul Î¼) f := rfl

nonrec theorem integral_congr {f g : Î± â†’â‚â‚›[Î¼] E} (h : toSimpleFunc f =áµ[Î¼] toSimpleFunc g) :
    integral f = integral g :=
  SimpleFunc.integral_congr (SimpleFunc.integrable f) h

theorem integral_add (f g : Î± â†’â‚â‚›[Î¼] E) : integral (f + g) = integral f + integral g :=
  setToL1S_add _ (fun _ _ => weightedSMul_null) weightedSMul_union _ _

theorem integral_smul (c : ğ•œ) (f : Î± â†’â‚â‚›[Î¼] E) : integral (c â€¢ f) = c â€¢ integral f :=
  setToL1S_smul _ (fun _ _ => weightedSMul_null) weightedSMul_union weightedSMul_smul c f

theorem norm_integral_le_norm (f : Î± â†’â‚â‚›[Î¼] E) : â€–integral fâ€– â‰¤ â€–fâ€– := by
  rw [integral, norm_eq_integral]
  exact (toSimpleFunc f).norm_integral_le_integral_norm (SimpleFunc.integrable f)

variable (Î± E Î¼ ğ•œ)

/-- The Bochner integral over simple functions in L1 space as a continuous linear map. -/
def integralCLM' : (Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] E :=
  LinearMap.mkContinuous âŸ¨âŸ¨integral, integral_addâŸ©, integral_smulâŸ© 1 fun f =>
    le_trans (norm_integral_le_norm _) <| by rw [one_mul]

/-- The Bochner integral over simple functions in L1 space as a continuous linear map over â„. -/
def integralCLM : (Î± â†’â‚â‚›[Î¼] E) â†’L[â„] E :=
  integralCLM' Î± E â„ Î¼

variable {Î± E Î¼ ğ•œ}

local notation "Integral" => integralCLM Î± E Î¼

open ContinuousLinearMap

theorem norm_Integral_le_one : â€–Integralâ€– â‰¤ 1 :=
  LinearMap.mkContinuous_norm_le _ zero_le_one fun f â†¦ by
    simpa [one_mul] using norm_integral_le_norm f

section PosPart

theorem posPart_toSimpleFunc (f : Î± â†’â‚â‚›[Î¼] â„) :
    toSimpleFunc (posPart f) =áµ[Î¼] (toSimpleFunc f).posPart := by
  have eq : âˆ€ a, (toSimpleFunc f).posPart a = max ((toSimpleFunc f) a) 0 := fun a => rfl
  have ae_eq : âˆ€áµ a âˆ‚Î¼, toSimpleFunc (posPart f) a = max ((toSimpleFunc f) a) 0 := by
    filter_upwards [toSimpleFunc_eq_toFun (posPart f), Lp.coeFn_posPart (f : Î± â†’â‚[Î¼] â„),
      toSimpleFunc_eq_toFun f] with _ _ hâ‚‚ hâ‚ƒ
    convert hâ‚‚ using 1
    rw [hâ‚ƒ]
  refine ae_eq.mono fun a h => ?_
  rw [h, eq]

theorem negPart_toSimpleFunc (f : Î± â†’â‚â‚›[Î¼] â„) :
    toSimpleFunc (negPart f) =áµ[Î¼] (toSimpleFunc f).negPart := by
  rw [SimpleFunc.negPart, MeasureTheory.SimpleFunc.negPart]
  filter_upwards [posPart_toSimpleFunc (-f), neg_toSimpleFunc f]
  intro a hâ‚ hâ‚‚
  rw [hâ‚]
  change max _ _ = max _ _
  rw [hâ‚‚]
  simp

theorem integral_eq_norm_posPart_sub (f : Î± â†’â‚â‚›[Î¼] â„) : integral f = â€–posPart fâ€– - â€–negPart fâ€– := by
  -- Convert things in `LÂ¹` to their `SimpleFunc` counterpart
  have ae_eqâ‚ : (toSimpleFunc f).posPart =áµ[Î¼] (toSimpleFunc (posPart f)).map norm := by
    filter_upwards [posPart_toSimpleFunc f] with _ h
    rw [SimpleFunc.map_apply, h]
    conv_lhs => rw [â† SimpleFunc.posPart_map_norm, SimpleFunc.map_apply]
  -- Convert things in `LÂ¹` to their `SimpleFunc` counterpart
  have ae_eqâ‚‚ : (toSimpleFunc f).negPart =áµ[Î¼] (toSimpleFunc (negPart f)).map norm := by
    filter_upwards [negPart_toSimpleFunc f] with _ h
    rw [SimpleFunc.map_apply, h]
    conv_lhs => rw [â† SimpleFunc.negPart_map_norm, SimpleFunc.map_apply]
  rw [integral, norm_eq_integral, norm_eq_integral, â† SimpleFunc.integral_sub]
  Â· change (toSimpleFunc f).integral Î¼ =
      ((toSimpleFunc (posPart f)).map norm - (toSimpleFunc (negPart f)).map norm).integral Î¼
    apply MeasureTheory.SimpleFunc.integral_congr (SimpleFunc.integrable f)
    filter_upwards [ae_eqâ‚, ae_eqâ‚‚] with _ hâ‚ hâ‚‚
    rw [SimpleFunc.sub_apply, â† hâ‚, â† hâ‚‚]
    exact DFunLike.congr_fun (toSimpleFunc f).posPart_sub_negPart.symm _
  Â· exact (SimpleFunc.integrable f).pos_part.congr ae_eqâ‚
  Â· exact (SimpleFunc.integrable f).neg_part.congr ae_eqâ‚‚

end PosPart

end SimpleFuncIntegral

end SimpleFunc

open SimpleFunc

local notation "Integral" => @integralCLM Î± E _ _ _ _ _ Î¼ _

variable [NormedSpace â„ E] [NormedRing ğ•œ] [Module ğ•œ E] [IsBoundedSMul ğ•œ E] [SMulCommClass â„ ğ•œ E]
  [CompleteSpace E]

section IntegrationInL1

attribute [local instance] simpleFunc.isBoundedSMul simpleFunc.module

open ContinuousLinearMap

variable (ğ•œ) in
/-- The Bochner integral in L1 space as a continuous linear map. -/
nonrec def integralCLM' : (Î± â†’â‚[Î¼] E) â†’L[ğ•œ] E :=
  (integralCLM' Î± E ğ•œ Î¼).extend (coeToLp Î± E ğ•œ) (simpleFunc.denseRange one_ne_top)
    simpleFunc.isUniformInducing

/-- The Bochner integral in L1 space as a continuous linear map over â„. -/
def integralCLM : (Î± â†’â‚[Î¼] E) â†’L[â„] E :=
  integralCLM' â„

/-- The Bochner integral in L1 space -/
irreducible_def integral : (Î± â†’â‚[Î¼] E) â†’ E :=
  integralCLM

theorem integral_eq (f : Î± â†’â‚[Î¼] E) : integral f = integralCLM f := by
  simp only [integral]

theorem integral_eq_setToL1 (f : Î± â†’â‚[Î¼] E) :
    integral f = setToL1 (dominatedFinMeasAdditive_weightedSMul Î¼) f := by
  simp only [integral]; rfl

@[norm_cast]
theorem SimpleFunc.integral_L1_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) :
    L1.integral (f : Î± â†’â‚[Î¼] E) = SimpleFunc.integral f := by
  simp only [integral, L1.integral]
  exact setToL1_eq_setToL1SCLM (dominatedFinMeasAdditive_weightedSMul Î¼) f

variable (Î± E)

@[simp]
theorem integral_zero : integral (0 : Î± â†’â‚[Î¼] E) = 0 := by
  simp only [integral]
  exact map_zero integralCLM

variable {Î± E}

@[integral_simps]
theorem integral_add (f g : Î± â†’â‚[Î¼] E) : integral (f + g) = integral f + integral g := by
  simp only [integral]
  exact map_add integralCLM f g

@[integral_simps]
theorem integral_neg (f : Î± â†’â‚[Î¼] E) : integral (-f) = -integral f := by
  simp only [integral]
  exact map_neg integralCLM f

@[integral_simps]
theorem integral_sub (f g : Î± â†’â‚[Î¼] E) : integral (f - g) = integral f - integral g := by
  simp only [integral]
  exact map_sub integralCLM f g

@[integral_simps]
theorem integral_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] E) : integral (c â€¢ f) = c â€¢ integral f := by
  simp only [integral]
  change (integralCLM' ğ•œ) (c â€¢ f) = c â€¢ (integralCLM' ğ•œ) f
  exact map_smul (integralCLM' ğ•œ) c f

theorem norm_Integral_le_one : â€–integralCLM (Î± := Î±) (E := E) (Î¼ := Î¼)â€– â‰¤ 1 :=
  norm_setToL1_le (dominatedFinMeasAdditive_weightedSMul Î¼) zero_le_one

theorem nnnorm_Integral_le_one : â€–integralCLM (Î± := Î±) (E := E) (Î¼ := Î¼)â€–â‚Š â‰¤ 1 :=
  norm_Integral_le_one

theorem norm_integral_le (f : Î± â†’â‚[Î¼] E) : â€–integral fâ€– â‰¤ â€–fâ€– :=
  calc
    â€–integral fâ€– = â€–integralCLM fâ€– := by simp only [integral]
    _ â‰¤ â€–integralCLM (Î± := Î±) (Î¼ := Î¼)â€– * â€–fâ€– := le_opNorm _ _
    _ â‰¤ 1 * â€–fâ€– := mul_le_mul_of_nonneg_right norm_Integral_le_one <| norm_nonneg _
    _ = â€–fâ€– := one_mul _

theorem nnnorm_integral_le (f : Î± â†’â‚[Î¼] E) : â€–integral fâ€–â‚Š â‰¤ â€–fâ€–â‚Š :=
  norm_integral_le f

@[continuity]
theorem continuous_integral : Continuous fun f : Î± â†’â‚[Î¼] E => integral f := by
  simp only [integral]
  exact L1.integralCLM.continuous

section PosPart

theorem integral_eq_norm_posPart_sub (f : Î± â†’â‚[Î¼] â„) :
    integral f = â€–Lp.posPart fâ€– - â€–Lp.negPart fâ€– := by
  -- Use `isClosed_property` and `isClosed_eq`
  refine @isClosed_property _ _ _ ((â†‘) : (Î± â†’â‚â‚›[Î¼] â„) â†’ Î± â†’â‚[Î¼] â„)
      (fun f : Î± â†’â‚[Î¼] â„ => integral f = â€–Lp.posPart fâ€– - â€–Lp.negPart fâ€–)
      (simpleFunc.denseRange one_ne_top) (isClosed_eq ?_ ?_) ?_ f
  Â· simp only [integral]
    exact cont _
  Â· refine Continuous.sub (continuous_norm.comp Lp.continuous_posPart)
      (continuous_norm.comp Lp.continuous_negPart)
  -- Show that the property holds for all simple functions in the `LÂ¹` space.
  Â· intro s
    norm_cast
    exact SimpleFunc.integral_eq_norm_posPart_sub _

end PosPart

end IntegrationInL1

end L1

end MeasureTheory
