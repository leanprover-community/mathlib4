/-
Copyright (c) 2020 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Integral.Bochner.Set

/-!
# Fundamental theorem of calculus for set integrals

This file proves a version of the
[Fundamental theorem of calculus](https://en.wikipedia.org/wiki/Fundamental_theorem_of_calculus)
for set integrals. See `Filter.Tendsto.integral_sub_linear_isLittleO_ae` and its corollaries.

Namely, consider a measurably generated filter `l`, a measure `Î¼` finite at this filter, and
a function `f` that has a finite limit `c` at `l âŠ“ ae Î¼`. Then `âˆ« x in s, f x âˆ‚Î¼ = Î¼ s â€¢ c + o(Î¼ s)`
as `s` tends to `l.smallSets`, i.e. for any `Îµ>0` there exists `t âˆˆ l` such that
`â€–âˆ« x in s, f x âˆ‚Î¼ - Î¼ s â€¢ câ€– â‰¤ Îµ * Î¼ s` whenever `s âŠ† t`. We also formulate a version of this
theorem for a locally finite measure `Î¼` and a function `f` continuous at a point `a`.
-/

open Filter MeasureTheory Topology Asymptotics Metric

variable {X E Î¹ : Type*} [MeasurableSpace X] [NormedAddCommGroup E] [NormedSpace â„ E]
  [CompleteSpace E]

/-- Fundamental theorem of calculus for set integrals:
if `Î¼` is a measure that is finite at a filter `l` and
`f` is a measurable function that has a finite limit `b` at `l âŠ“ ae Î¼`, then
`âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ b + o(Î¼ (s i))` at a filter `li` provided that
`s i` tends to `l.smallSets` along `li`.
Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `Î¼.real (s i)` in the actual statement.

Often there is a good formula for `Î¼.real (s i)`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => Î¼.real (s i)) =á¶ [li] m`. Without these
arguments, `m i = Î¼.real (s i)` is used in the output. -/
theorem Filter.Tendsto.integral_sub_linear_isLittleO_ae
    {Î¼ : Measure X} {l : Filter X} [l.IsMeasurablyGenerated] {f : X â†’ E} {b : E}
    (h : Tendsto f (l âŠ“ ae Î¼) (ğ“ b)) (hfm : StronglyMeasurableAtFilter f l Î¼)
    (hÎ¼ : Î¼.FiniteAtFilter l) {s : Î¹ â†’ Set X} {li : Filter Î¹} (hs : Tendsto s li l.smallSets)
    (m : Î¹ â†’ â„ := fun i => Î¼.real (s i))
    (hsÎ¼ : (fun i => Î¼.real (s i)) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ b) =o[li] m := by
  suffices
      (fun s => (âˆ« x in s, f x âˆ‚Î¼) - Î¼.real s â€¢ b) =o[l.smallSets] fun s => Î¼.real s from
    (this.comp_tendsto hs).congr'
      (hsÎ¼.mono fun a ha => by dsimp only [Function.comp_apply] at ha âŠ¢; rw [ha]) hsÎ¼
  refine isLittleO_iff.2 fun Îµ Îµâ‚€ => ?_
  have : âˆ€á¶  s in l.smallSets, âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x âˆˆ closedBall b Îµ :=
    eventually_smallSets_eventually.2 (h.eventually <| closedBall_mem_nhds _ Îµâ‚€)
  filter_upwards [hÎ¼.eventually, (hÎ¼.integrableAtFilter_of_tendsto_ae hfm h).eventually,
    hfm.eventually, this]
  simp only [mem_closedBall, dist_eq_norm]
  intro s hÎ¼s h_integrable hfm h_norm
  rw [â† setIntegral_const,
    â† integral_sub h_integrable (integrableOn_const hÎ¼s.ne),
    Real.norm_eq_abs, abs_of_nonneg measureReal_nonneg]
  exact norm_setIntegral_le_of_norm_le_const_ae' hÎ¼s h_norm

/-- Fundamental theorem of calculus for set integrals, `nhdsWithin` version: if `Î¼` is a locally
finite measure and `f` is an almost everywhere measurable function that is continuous at a point `a`
within a measurable set `t`, then `âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at a filter `li`
provided that `s i` tends to `(ğ“[t] a).smallSets` along `li`.  Since `Î¼ (s i)` is an `â„â‰¥0âˆ`
number, we use `Î¼.real (s i)` in the actual statement.

Often there is a good formula for `Î¼.real (s i)`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => Î¼.real (s i)) =á¶ [li] m`. Without these
arguments, `m i = Î¼.real (s i)` is used in the output. -/
theorem ContinuousWithinAt.integral_sub_linear_isLittleO_ae [TopologicalSpace X]
    [OpensMeasurableSpace X] {Î¼ : Measure X}
    [IsLocallyFiniteMeasure Î¼] {x : X} {t : Set X} {f : X â†’ E} (hx : ContinuousWithinAt f t x)
    (ht : MeasurableSet t) (hfm : StronglyMeasurableAtFilter f (ğ“[t] x) Î¼) {s : Î¹ â†’ Set X}
    {li : Filter Î¹} (hs : Tendsto s li (ğ“[t] x).smallSets) (m : Î¹ â†’ â„ := fun i => Î¼.real (s i))
    (hsÎ¼ : (fun i => Î¼.real (s i)) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ f x) =o[li] m :=
  haveI : (ğ“[t] x).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hx.mono_left inf_le_left).integral_sub_linear_isLittleO_ae hfm (Î¼.finiteAt_nhdsWithin x t) hs m
    hsÎ¼

/-- Fundamental theorem of calculus for set integrals, `nhds` version: if `Î¼` is a locally finite
measure and `f` is an almost everywhere measurable function that is continuous at a point `a`, then
`âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at `li` provided that `s` tends to
`(ğ“ a).smallSets` along `li`. Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `Î¼.real (s i)` in
the actual statement.

Often there is a good formula for `Î¼.real (s i)`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => Î¼.real (s i)) =á¶ [li] m`. Without these
arguments, `m i = Î¼.real (s i)` is used in the output. -/
theorem ContinuousAt.integral_sub_linear_isLittleO_ae [TopologicalSpace X] [OpensMeasurableSpace X]
    {Î¼ : Measure X} [IsLocallyFiniteMeasure Î¼] {x : X}
    {f : X â†’ E} (hx : ContinuousAt f x) (hfm : StronglyMeasurableAtFilter f (ğ“ x) Î¼) {s : Î¹ â†’ Set X}
    {li : Filter Î¹} (hs : Tendsto s li (ğ“ x).smallSets) (m : Î¹ â†’ â„ := fun i => Î¼.real (s i))
    (hsÎ¼ : (fun i => Î¼.real (s i)) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ f x) =o[li] m :=
  (hx.mono_left inf_le_left).integral_sub_linear_isLittleO_ae hfm (Î¼.finiteAt_nhds x) hs m hsÎ¼

/-- Fundamental theorem of calculus for set integrals, `nhdsWithin` version: if `Î¼` is a locally
finite measure, `f` is continuous on a measurable set `t`, and `a âˆˆ t`, then `âˆ« x in (s i), f x âˆ‚Î¼ =
Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at `li` provided that `s i` tends to `(ğ“[t] a).smallSets` along `li`.
Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `Î¼.real (s i)` in the actual statement.

Often there is a good formula for `Î¼.real (s i)`, so the formalization can take an optional
argument `m` with this formula and a proof of `(fun i => Î¼.real (s i)) =á¶ [li] m`. Without these
arguments, `m i = Î¼.real (s i)` is used in the output. -/
theorem ContinuousOn.integral_sub_linear_isLittleO_ae [TopologicalSpace X] [OpensMeasurableSpace X]
    [SecondCountableTopologyEither X E] {Î¼ : Measure X}
    [IsLocallyFiniteMeasure Î¼] {x : X} {t : Set X} {f : X â†’ E} (hft : ContinuousOn f t) (hx : x âˆˆ t)
    (ht : MeasurableSet t) {s : Î¹ â†’ Set X} {li : Filter Î¹} (hs : Tendsto s li (ğ“[t] x).smallSets)
    (m : Î¹ â†’ â„ := fun i => Î¼.real (s i))
    (hsÎ¼ : (fun i => Î¼.real (s i)) =á¶ [li] m := by rfl) :
    (fun i => (âˆ« x in s i, f x âˆ‚Î¼) - m i â€¢ f x) =o[li] m :=
  (hft x hx).integral_sub_linear_isLittleO_ae ht
    âŸ¨t, self_mem_nhdsWithin, hft.aestronglyMeasurable htâŸ© hs m hsÎ¼
