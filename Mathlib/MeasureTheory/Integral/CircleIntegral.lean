/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Analysis.SpecialFunctions.Complex.CircleMap
import Mathlib.Analysis.SpecialFunctions.NonIntegrable

/-!
# Integral over a circle in `â„‚`

In this file we define `âˆ® z in C(c, R), f z` to be the integral $\oint_{|z-c|=|R|} f(z)\,dz$ and
prove some properties of this integral. We give definition and prove most lemmas for a function
`f : â„‚ â†’ E`, where `E` is a complex Banach space. For this reason,
some lemmas use, e.g., `(z - c)â»Â¹ â€¢ f z` instead of `f z / (z - c)`.

## Main definitions

* `CircleIntegrable f c R`: a function `f : â„‚ â†’ E` is integrable on the circle with center `c` and
  radius `R` if `f âˆ˜ circleMap c R` is integrable on `[0, 2Ï€]`;

* `circleIntegral f c R`: the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2Ï€}(c + Re^{Î¸ i})' f(c+Re^{Î¸ i})\,dÎ¸$;

* `cauchyPowerSeries f c R`: the power series that is equal to
  $\sum_{n=0}^{\infty} \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
  `w - c`. The coefficients of this power series depend only on `f âˆ˜ circleMap c R`, and the power
  series converges to `f w` if `f` is differentiable on the closed ball `Metric.closedBall c R`
  and `w` belongs to the corresponding open ball.

## Main statements

* `hasFPowerSeriesOn_cauchy_integral`: for any circle integrable function `f`, the power series
  `cauchyPowerSeries f c R`, `R > 0`, converges to the Cauchy integral
  `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open disc `Metric.ball c R`;

* `circleIntegral.integral_sub_zpow_of_undef`, `circleIntegral.integral_sub_zpow_of_ne`, and
  `circleIntegral.integral_sub_inv_of_mem_ball`: formulas for `âˆ® z in C(c, R), (z - w) ^ n`,
  `n : â„¤`. These lemmas cover the following cases:

  - `circleIntegral.integral_sub_zpow_of_undef`, `n < 0` and `|w - c| = |R|`: in this case the
    function is not integrable, so the integral is equal to its default value (zero);

  - `circleIntegral.integral_sub_zpow_of_ne`, `n â‰  -1`: in the cases not covered by the previous
    lemma, we have `(z - w) ^ n = ((z - w) ^ (n + 1) / (n + 1))'`, thus the integral equals zero;

  - `circleIntegral.integral_sub_inv_of_mem_ball`, `n = -1`, `|w - c| < R`: in this case the
    integral is equal to `2Ï€i`.

  The case `n = -1`, `|w -c| > R` is not covered by these lemmas. While it is possible to construct
  an explicit primitive, it is easier to apply Cauchy theorem, so we postpone the proof till we have
  this theorem (see https://github.com/leanprover-community/mathlib4/pull/10000).

## Notation

- `âˆ® z in C(c, R), f z`: notation for the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2Ï€}(c + Re^{Î¸ i})' f(c+Re^{Î¸ i})\,dÎ¸$.

## Tags

integral, circle, Cauchy integral
-/

variable {E : Type*} [NormedAddCommGroup E]

noncomputable section

open scoped Real NNReal Interval Pointwise Topology

open Complex MeasureTheory TopologicalSpace Metric Function Set Filter Asymptotics

/-!
### Facts about `circleMap`
-/

/-- The range of `circleMap c R` is the circle with center `c` and radius `|R|`. -/
@[simp]
theorem range_circleMap (c : â„‚) (R : â„) : range (circleMap c R) = sphere c |R| :=
  calc
    range (circleMap c R) = c +áµ¥ R â€¢ range fun Î¸ : â„ => exp (Î¸ * I) := by
      simp +unfoldPartialApp only [â† image_vadd, â† image_smul, â† range_comp,
        vadd_eq_add, circleMap, comp_def, real_smul]
    _ = sphere c |R| := by
      rw [range_exp_mul_I, smul_sphere R 0 zero_le_one]
      simp

/-- The image of `(0, 2Ï€]` under `circleMap c R` is the circle with center `c` and radius `|R|`. -/
@[simp]
theorem image_circleMap_Ioc (c : â„‚) (R : â„) : circleMap c R '' Ioc 0 (2 * Ï€) = sphere c |R| := by
  rw [â† range_circleMap, â† (periodic_circleMap c R).image_Ioc Real.two_pi_pos 0, zero_add]

theorem hasDerivAt_circleMap (c : â„‚) (R : â„) (Î¸ : â„) :
    HasDerivAt (circleMap c R) (circleMap 0 R Î¸ * I) Î¸ := by
  simpa only [mul_assoc, one_mul, ofRealCLM_apply, circleMap, ofReal_one, zero_add]
    using (((ofRealCLM.hasDerivAt (x := Î¸)).mul_const I).cexp.const_mul (R : â„‚)).const_add c

theorem differentiable_circleMap (c : â„‚) (R : â„) : Differentiable â„ (circleMap c R) := fun Î¸ =>
  (hasDerivAt_circleMap c R Î¸).differentiableAt

/-- The circleMap is real analytic. -/
theorem analyticOnNhd_circleMap (c : â„‚) (R : â„) :
    AnalyticOnNhd â„ (circleMap c R) Set.univ := by
  intro z hz
  apply analyticAt_const.add
  apply analyticAt_const.mul
  rw [â† Function.comp_def]
  apply analyticAt_cexp.restrictScalars.comp ((ofRealCLM.analyticAt z).mul (by fun_prop))

/-- The circleMap is continuously differentiable. -/
theorem contDiff_circleMap (c : â„‚) (R : â„) {n : WithTop â„•âˆ} :
    ContDiff â„ n (circleMap c R) :=
  (analyticOnNhd_circleMap c R).contDiff

@[continuity, fun_prop]
theorem continuous_circleMap (c : â„‚) (R : â„) : Continuous (circleMap c R) :=
  (differentiable_circleMap c R).continuous

@[fun_prop, measurability]
theorem measurable_circleMap (c : â„‚) (R : â„) : Measurable (circleMap c R) :=
  (continuous_circleMap c R).measurable

@[simp]
theorem deriv_circleMap (c : â„‚) (R : â„) (Î¸ : â„) : deriv (circleMap c R) Î¸ = circleMap 0 R Î¸ * I :=
  (hasDerivAt_circleMap _ _ _).deriv

theorem deriv_circleMap_eq_zero_iff {c : â„‚} {R : â„} {Î¸ : â„} :
    deriv (circleMap c R) Î¸ = 0 â†” R = 0 := by simp [I_ne_zero]

theorem deriv_circleMap_ne_zero {c : â„‚} {R : â„} {Î¸ : â„} (hR : R â‰  0) :
    deriv (circleMap c R) Î¸ â‰  0 :=
  mt deriv_circleMap_eq_zero_iff.1 hR

theorem lipschitzWith_circleMap (c : â„‚) (R : â„) : LipschitzWith (Real.nnabs R) (circleMap c R) :=
  lipschitzWith_of_nnnorm_deriv_le (differentiable_circleMap _ _) fun Î¸ =>
    NNReal.coe_le_coe.1 <| by simp

theorem continuous_circleMap_inv {R : â„} {z w : â„‚} (hw : w âˆˆ ball z R) :
    Continuous fun Î¸ => (circleMap z R Î¸ - w)â»Â¹ := by
  have : âˆ€ Î¸, circleMap z R Î¸ - w â‰  0 := by
    simp_rw [sub_ne_zero]
    exact fun Î¸ => circleMap_ne_mem_ball hw Î¸
  -- Porting note: was `continuity`
  exact Continuous.invâ‚€ (by fun_prop) this

theorem circleMap_preimage_codiscrete {c : â„‚} {R : â„} (hR : R â‰  0) :
    map (circleMap c R) (codiscrete â„) â‰¤ codiscreteWithin (Metric.sphere c |R|) := by
  intro s hs
  apply (analyticOnNhd_circleMap c R).preimage_mem_codiscreteWithin
  Â· intro x hx
    by_contra hCon
    obtain âŸ¨a, haâŸ© := eventuallyConst_iff_exists_eventuallyEq.1 hCon
    have := ha.deriv.eq_of_nhds
    simp [hR] at this
  Â· rwa [Set.image_univ, range_circleMap]

theorem circleMap_neg_radius {r x : â„} {c : â„‚} :
    circleMap c (-r) x = circleMap c r (x + Ï€) := by
  simp [circleMap, add_mul, Complex.exp_add]

/-!
### Integrability of a function on a circle
-/

/-- We say that a function `f : â„‚ â†’ E` is integrable on the circle with center `c` and radius `R` if
the function `f âˆ˜ circleMap c R` is integrable on `[0, 2Ï€]`.

Note that the actual function used in the definition of `circleIntegral` is
`(deriv (circleMap c R) Î¸) â€¢ f (circleMap c R Î¸)`. Integrability of this function is equivalent
to integrability of `f âˆ˜ circleMap c R` whenever `R â‰  0`. -/
def CircleIntegrable (f : â„‚ â†’ E) (c : â„‚) (R : â„) : Prop :=
  IntervalIntegrable (fun Î¸ : â„ => f (circleMap c R Î¸)) volume 0 (2 * Ï€)

@[simp]
theorem circleIntegrable_const (a : E) (c : â„‚) (R : â„) : CircleIntegrable (fun _ => a) c R :=
  intervalIntegrable_const

namespace CircleIntegrable

variable {f g : â„‚ â†’ E} {c : â„‚} {R : â„} {A : Type*} [NormedRing A] {a : A}

/--
Analogue of `IntervalIntegrable.abs`: If a real-valued function `f` is circle integrable, then so is
`|f|`.
-/
theorem abs {f : â„‚ â†’ â„} (hf : CircleIntegrable f c R) :
    CircleIntegrable |f| c R := IntervalIntegrable.abs hf

nonrec theorem add (hf : CircleIntegrable f c R) (hg : CircleIntegrable g c R) :
    CircleIntegrable (f + g) c R :=
  hf.add hg

/-- Sums of circle integrable functions are circle integrable. -/
protected theorem sum {Î¹ : Type*} (s : Finset Î¹) {f : Î¹ â†’ â„‚ â†’ E}
    (h : âˆ€ i âˆˆ s, CircleIntegrable (f i) c R) :
    CircleIntegrable (âˆ‘ i âˆˆ s, f i) c R := by
  rw [CircleIntegrable, (by aesop : (fun Î¸ â†¦ (âˆ‘ i âˆˆ s, f i) (circleMap c R Î¸))
    = âˆ‘ i âˆˆ s, fun Î¸ â†¦ f i (circleMap c R Î¸))] at *
  exact IntervalIntegrable.sum s h

/-- Finsums of circle integrable functions are circle integrable. -/
protected theorem finsum {Î¹ : Type*} {f : Î¹ â†’ â„‚ â†’ E} (h : âˆ€ i, CircleIntegrable (f i) c R) :
    CircleIntegrable (âˆ‘á¶  i, f i) c R := by
  by_cases hâ‚ : (Function.support f).Finite
  Â· rw [finsum_eq_sum f hâ‚]
    exact CircleIntegrable.sum hâ‚.toFinset (fun i _ â†¦ h i)
  Â· rw [finsum_of_infinite_support hâ‚]
    apply circleIntegrable_const

nonrec theorem neg (hf : CircleIntegrable f c R) : CircleIntegrable (-f) c R :=
  hf.neg

/-- If `f` is circle integrable, then so are its scalar multiples. -/
theorem const_smul {f : â„‚ â†’ A} (h : CircleIntegrable f c R) : CircleIntegrable (a â€¢ f) c R :=
  IntervalIntegrable.const_mul h _

/-- If `f` is circle integrable, then so are its scalar multiples. -/
theorem const_fun_smul {f : â„‚ â†’ A} (h : CircleIntegrable f c R) :
    CircleIntegrable (fun z â†¦ a â€¢ f z) c R := const_smul h

/-- The function we actually integrate over `[0, 2Ï€]` in the definition of `circleIntegral` is
integrable. -/
theorem out [NormedSpace â„‚ E] (hf : CircleIntegrable f c R) :
    IntervalIntegrable (fun Î¸ : â„ => deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)) volume 0
      (2 * Ï€) := by
  simp only [CircleIntegrable, deriv_circleMap, intervalIntegrable_iff] at *
  refine (hf.norm.const_mul |R|).mono' ?_ ?_
  Â· exact ((continuous_circleMap _ _).aestronglyMeasurable.mul_const I).smul hf.aestronglyMeasurable
  Â· simp [norm_smul]

end CircleIntegrable

@[simp]
theorem circleIntegrable_zero_radius {f : â„‚ â†’ E} {c : â„‚} : CircleIntegrable f c 0 := by
  simp [CircleIntegrable]

/-- Circle integrability is invariant when functions change along discrete sets. -/
theorem CircleIntegrable.congr_codiscreteWithin {c : â„‚} {R : â„} {fâ‚ fâ‚‚ : â„‚ â†’ E}
    (hf : fâ‚ =á¶ [codiscreteWithin (Metric.sphere c |R|)] fâ‚‚) (hfâ‚ : CircleIntegrable fâ‚ c R) :
    CircleIntegrable fâ‚‚ c R := by
  by_cases hR : R = 0
  Â· simp [hR]
  apply (intervalIntegrable_congr_codiscreteWithin _).1 hfâ‚
  rw [eventuallyEq_iff_exists_mem]
  exact âŸ¨(circleMap c R)â»Â¹' {z | fâ‚ z = fâ‚‚ z},
    codiscreteWithin.mono (by simp only [Set.subset_univ]) (circleMap_preimage_codiscrete hR hf),
    by tautoâŸ©

/-- Circle integrability is invariant when functions change along discrete sets. -/
theorem circleIntegrable_congr_codiscreteWithin {c : â„‚} {R : â„} {fâ‚ fâ‚‚ : â„‚ â†’ E}
    (hf : fâ‚ =á¶ [codiscreteWithin (Metric.sphere c |R|)] fâ‚‚) :
    CircleIntegrable fâ‚ c R â†” CircleIntegrable fâ‚‚ c R :=
  âŸ¨(CircleIntegrable.congr_codiscreteWithin hf Â·),
    (CircleIntegrable.congr_codiscreteWithin hf.symm Â·)âŸ©

theorem circleIntegrable_iff [NormedSpace â„‚ E] {f : â„‚ â†’ E} {c : â„‚} (R : â„) :
    CircleIntegrable f c R â†” IntervalIntegrable (fun Î¸ : â„ =>
      deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)) volume 0 (2 * Ï€) := by
  by_cases hâ‚€ : R = 0
  Â· simp +unfoldPartialApp [hâ‚€, const]
  refine âŸ¨fun h => h.out, fun h => ?_âŸ©
  simp only [CircleIntegrable, intervalIntegrable_iff, deriv_circleMap] at h âŠ¢
  refine (h.norm.const_mul |R|â»Â¹).mono' ?_ ?_
  Â· have H : âˆ€ {Î¸}, circleMap 0 R Î¸ * I â‰  0 := fun {Î¸} => by simp [hâ‚€, I_ne_zero]
    simpa only [inv_smul_smulâ‚€ H]
      using ((continuous_circleMap 0 R).aestronglyMeasurable.mul_const
        I).aemeasurable.inv.aestronglyMeasurable.smul h.aestronglyMeasurable
  Â· simp [norm_smul, hâ‚€]

theorem ContinuousOn.circleIntegrable' {f : â„‚ â†’ E} {c : â„‚} {R : â„}
    (hf : ContinuousOn f (sphere c |R|)) : CircleIntegrable f c R :=
  (hf.comp_continuous (continuous_circleMap _ _) (circleMap_mem_sphere' _ _)).intervalIntegrable _ _

theorem ContinuousOn.circleIntegrable {f : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R)
    (hf : ContinuousOn f (sphere c R)) : CircleIntegrable f c R :=
  ContinuousOn.circleIntegrable' <| (abs_of_nonneg hR).symm â–¸ hf

/-- The function `fun z â†¦ (z - w) ^ n`, `n : â„¤`, is circle integrable on the circle with center `c`
and radius `|R|` if and only if `R = 0` or `0 â‰¤ n`, or `w` does not belong to this circle. -/
@[simp]
theorem circleIntegrable_sub_zpow_iff {c w : â„‚} {R : â„} {n : â„¤} :
    CircleIntegrable (fun z => (z - w) ^ n) c R â†” R = 0 âˆ¨ 0 â‰¤ n âˆ¨ w âˆ‰ sphere c |R| := by
  constructor
  Â· intro h; contrapose! h; rcases h with âŸ¨hR, hn, hwâŸ©
    simp only [circleIntegrable_iff R, deriv_circleMap]
    rw [â† image_circleMap_Ioc] at hw; rcases hw with âŸ¨Î¸, hÎ¸, rflâŸ©
    replace hÎ¸ : Î¸ âˆˆ [[0, 2 * Ï€]] := Icc_subset_uIcc (Ioc_subset_Icc_self hÎ¸)
    refine not_intervalIntegrable_of_sub_inv_isBigO_punctured ?_ Real.two_pi_pos.ne hÎ¸
    set f : â„ â†’ â„‚ := fun Î¸' => circleMap c R Î¸' - circleMap c R Î¸
    have : âˆ€á¶  Î¸' in ğ“[â‰ ] Î¸, f Î¸' âˆˆ ball (0 : â„‚) 1 \ {0} := by
      suffices âˆ€á¶  z in ğ“[â‰ ] circleMap c R Î¸, z - circleMap c R Î¸ âˆˆ ball (0 : â„‚) 1 \ {0} from
        ((differentiable_circleMap c R Î¸).hasDerivAt.tendsto_nhdsNE
          (deriv_circleMap_ne_zero hR)).eventually this
      filter_upwards [self_mem_nhdsWithin, mem_nhdsWithin_of_mem_nhds (ball_mem_nhds _ zero_lt_one)]
      simp_all [dist_eq, sub_eq_zero]
    refine (((hasDerivAt_circleMap c R Î¸).isBigO_sub.mono inf_le_left).inv_rev
      (this.mono fun Î¸' hâ‚ hâ‚‚ => absurd hâ‚‚ hâ‚.2)).trans ?_
    refine IsBigO.of_bound |R|â»Â¹ (this.mono fun Î¸' hÎ¸' => ?_)
    set x := â€–f Î¸'â€–
    suffices xâ»Â¹ â‰¤ x ^ n by
      simp only [Algebra.id.smul_eq_mul, norm_mul,
        norm_inv, norm_I, mul_one]
      simpa only [norm_circleMap_zero, norm_zpow, Ne, abs_eq_zero.not.2 hR, not_false_iff,
        inv_mul_cancel_leftâ‚€] using this
    have : x âˆˆ Ioo (0 : â„) 1 := by simpa [x, and_comm] using hÎ¸'
    rw [â† zpow_neg_one]
    refine (zpow_right_strictAntiâ‚€ this.1 this.2).le_iff_ge.2 (Int.lt_add_one_iff.1 ?_); exact hn
  Â· rintro (rfl | H)
    exacts [circleIntegrable_zero_radius,
      ((continuousOn_id.sub continuousOn_const).zpowâ‚€ _ fun z hz =>
        H.symm.imp_left fun (hw : w âˆ‰ sphere c |R|) =>
          sub_ne_zero.2 <| ne_of_mem_of_not_mem hz hw).circleIntegrable']

@[simp]
theorem circleIntegrable_sub_inv_iff {c w : â„‚} {R : â„} :
    CircleIntegrable (fun z => (z - w)â»Â¹) c R â†” R = 0 âˆ¨ w âˆ‰ sphere c |R| := by
  simp only [â† zpow_neg_one, circleIntegrable_sub_zpow_iff]; norm_num

variable [NormedSpace â„‚ E]

/-- Definition for $\oint_{|z-c|=R} f(z)\,dz$ -/
def circleIntegral (f : â„‚ â†’ E) (c : â„‚) (R : â„) : E :=
  âˆ« Î¸ : â„ in (0)..2 * Ï€, deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)

/-- `âˆ® z in C(c, R), f z` is the circle integral $\oint_{|z-c|=R} f(z)\,dz$. -/
notation3 "âˆ® "(...)" in ""C("c", "R")"", "r:(scoped f => circleIntegral f c R) => r

theorem circleIntegral_def_Icc (f : â„‚ â†’ E) (c : â„‚) (R : â„) :
    (âˆ® z in C(c, R), f z) = âˆ« Î¸ in Icc 0 (2 * Ï€),
    deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸) := by
  rw [circleIntegral, intervalIntegral.integral_of_le Real.two_pi_pos.le,
    Measure.restrict_congr_set Ioc_ae_eq_Icc]

namespace circleIntegral

@[simp]
theorem integral_radius_zero (f : â„‚ â†’ E) (c : â„‚) : (âˆ® z in C(c, 0), f z) = 0 := by
  simp +unfoldPartialApp [circleIntegral, const]

theorem integral_congr {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R) (h : EqOn f g (sphere c R)) :
    (âˆ® z in C(c, R), f z) = âˆ® z in C(c, R), g z :=
  intervalIntegral.integral_congr fun Î¸ _ => by simp only [h (circleMap_mem_sphere _ hR _)]

/-- Circle integrals are invariant when functions change along discrete sets. -/
theorem circleIntegral_congr_codiscreteWithin {c : â„‚} {R : â„} {fâ‚ fâ‚‚ : â„‚ â†’ â„‚}
    (hf : fâ‚ =á¶ [codiscreteWithin (Metric.sphere c |R|)] fâ‚‚) (hR : R â‰  0) :
    (âˆ® z in C(c, R), fâ‚ z) = (âˆ® z in C(c, R), fâ‚‚ z) := by
  apply intervalIntegral.integral_congr_ae_restrict
  apply ae_restrict_le_codiscreteWithin measurableSet_uIoc
  simp only [deriv_circleMap, smul_eq_mul, mul_eq_mul_left_iff, mul_eq_zero,
    circleMap_eq_center_iff, hR, Complex.I_ne_zero, or_self, or_false]
  exact codiscreteWithin.mono (by tauto) (circleMap_preimage_codiscrete hR hf)

theorem integral_sub_inv_smul_sub_smul (f : â„‚ â†’ E) (c w : â„‚) (R : â„) :
    (âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ (z - w) â€¢ f z) = âˆ® z in C(c, R), f z := by
  rcases eq_or_ne R 0 with (rfl | hR); Â· simp only [integral_radius_zero]
  have : (circleMap c R â»Â¹' {w}).Countable := (countable_singleton _).preimage_circleMap c hR
  refine intervalIntegral.integral_congr_ae ((this.ae_notMem _).mono fun Î¸ hÎ¸ _' => ?_)
  change circleMap c R Î¸ â‰  w at hÎ¸
  simp only [inv_smul_smulâ‚€ (sub_ne_zero.2 <| hÎ¸)]

theorem integral_undef {f : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : Â¬CircleIntegrable f c R) :
    (âˆ® z in C(c, R), f z) = 0 :=
  intervalIntegral.integral_undef (mt (circleIntegrable_iff R).mpr hf)

theorem integral_add {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : CircleIntegrable f c R)
    (hg : CircleIntegrable g c R) :
    (âˆ® z in C(c, R), f z + g z) = (âˆ® z in C(c, R), f z) + (âˆ® z in C(c, R), g z) := by
  simp only [circleIntegral, smul_add, intervalIntegral.integral_add hf.out hg.out]

theorem integral_sub {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : CircleIntegrable f c R)
    (hg : CircleIntegrable g c R) :
    (âˆ® z in C(c, R), f z - g z) = (âˆ® z in C(c, R), f z) - âˆ® z in C(c, R), g z := by
  simp only [circleIntegral, smul_sub, intervalIntegral.integral_sub hf.out hg.out]

theorem norm_integral_le_of_norm_le_const' {f : â„‚ â†’ E} {c : â„‚} {R C : â„}
    (hf : âˆ€ z âˆˆ sphere c |R|, â€–f zâ€– â‰¤ C) : â€–âˆ® z in C(c, R), f zâ€– â‰¤ 2 * Ï€ * |R| * C :=
  calc
    â€–âˆ® z in C(c, R), f zâ€– â‰¤ |R| * C * |2 * Ï€ - 0| :=
      intervalIntegral.norm_integral_le_of_norm_le_const fun Î¸ _ =>
        calc
          â€–deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)â€– = |R| * â€–f (circleMap c R Î¸)â€– := by
            simp [norm_smul]
          _ â‰¤ |R| * C :=
            mul_le_mul_of_nonneg_left (hf _ <| circleMap_mem_sphere' _ _ _) (abs_nonneg _)
    _ = 2 * Ï€ * |R| * C := by rw [sub_zero, _root_.abs_of_pos Real.two_pi_pos]; ac_rfl

theorem norm_integral_le_of_norm_le_const {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 â‰¤ R)
    (hf : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C) : â€–âˆ® z in C(c, R), f zâ€– â‰¤ 2 * Ï€ * R * C :=
  have : |R| = R := abs_of_nonneg hR
  calc
    â€–âˆ® z in C(c, R), f zâ€– â‰¤ 2 * Ï€ * |R| * C := norm_integral_le_of_norm_le_const' <| by rwa [this]
    _ = 2 * Ï€ * R * C := by rw [this]

theorem norm_two_pi_i_inv_smul_integral_le_of_norm_le_const {f : â„‚ â†’ E} {c : â„‚} {R C : â„}
    (hR : 0 â‰¤ R) (hf : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C) :
    â€–(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), f zâ€– â‰¤ R * C := by
  have : â€–(2 * Ï€ * I : â„‚)â»Â¹â€– = (2 * Ï€)â»Â¹ := by simp [Real.pi_pos.le]
  rw [norm_smul, this, â† div_eq_inv_mul, div_le_iffâ‚€ Real.two_pi_pos, mul_comm (R * C), â† mul_assoc]
  exact norm_integral_le_of_norm_le_const hR hf

/-- If `f` is continuous on the circle `|z - c| = R`, `R > 0`, the `â€–f zâ€–` is less than or equal to
`C : â„` on this circle, and this norm is strictly less than `C` at some point `z` of the circle,
then `â€–âˆ® z in C(c, R), f zâ€– < 2 * Ï€ * R * C`. -/
theorem norm_integral_lt_of_norm_le_const_of_lt {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 < R)
    (hc : ContinuousOn f (sphere c R)) (hf : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C)
    (hlt : âˆƒ z âˆˆ sphere c R, â€–f zâ€– < C) : â€–âˆ® z in C(c, R), f zâ€– < 2 * Ï€ * R * C := by
  rw [â† _root_.abs_of_pos hR, â† image_circleMap_Ioc] at hlt
  rcases hlt with âŸ¨_, âŸ¨Î¸â‚€, hmem, rflâŸ©, hltâŸ©
  calc
    â€–âˆ® z in C(c, R), f zâ€– â‰¤ âˆ« Î¸ in (0)..2 * Ï€, â€–deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)â€– :=
      intervalIntegral.norm_integral_le_integral_norm Real.two_pi_pos.le
    _ < âˆ« _ in (0)..2 * Ï€, R * C := by
      simp only [deriv_circleMap, norm_smul, norm_mul, norm_circleMap_zero, abs_of_pos hR, norm_I,
        mul_one]
      refine intervalIntegral.integral_lt_integral_of_continuousOn_of_le_of_exists_lt
          Real.two_pi_pos ?_ continuousOn_const (fun Î¸ _ => ?_) âŸ¨Î¸â‚€, Ioc_subset_Icc_self hmem, ?_âŸ©
      Â· exact continuousOn_const.mul (hc.comp (continuous_circleMap _ _).continuousOn fun Î¸ _ =>
          circleMap_mem_sphere _ hR.le _).norm
      Â· exact mul_le_mul_of_nonneg_left (hf _ <| circleMap_mem_sphere _ hR.le _) hR.le
      Â· exact (mul_lt_mul_left hR).2 hlt
    _ = 2 * Ï€ * R * C := by simp [mul_assoc]; ring

@[simp]
theorem integral_smul {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [SMulCommClass ğ•œ â„‚ E] (a : ğ•œ)
    (f : â„‚ â†’ E) (c : â„‚) (R : â„) : (âˆ® z in C(c, R), a â€¢ f z) = a â€¢ âˆ® z in C(c, R), f z := by
  simp only [circleIntegral, â† smul_comm a (_ : â„‚) (_ : E), intervalIntegral.integral_smul]

@[simp]
theorem integral_smul_const [CompleteSpace E] (f : â„‚ â†’ â„‚) (a : E) (c : â„‚) (R : â„) :
    (âˆ® z in C(c, R), f z â€¢ a) = (âˆ® z in C(c, R), f z) â€¢ a := by
  simp only [circleIntegral, intervalIntegral.integral_smul_const, â† smul_assoc]

@[simp]
theorem integral_const_mul (a : â„‚) (f : â„‚ â†’ â„‚) (c : â„‚) (R : â„) :
    (âˆ® z in C(c, R), a * f z) = a * âˆ® z in C(c, R), f z :=
  integral_smul a f c R

@[simp]
theorem integral_sub_center_inv (c : â„‚) {R : â„} (hR : R â‰  0) :
    (âˆ® z in C(c, R), (z - c)â»Â¹) = 2 * Ï€ * I := by
  simp [circleIntegral, â† div_eq_mul_inv, mul_div_cancel_leftâ‚€ _ (circleMap_ne_center hR)]

/-- If `f' : â„‚ â†’ E` is a derivative of a complex differentiable function on the circle
`Metric.sphere c |R|`, then `âˆ® z in C(c, R), f' z = 0`. -/
theorem integral_eq_zero_of_hasDerivWithinAt' [CompleteSpace E] {f f' : â„‚ â†’ E} {c : â„‚} {R : â„}
    (h : âˆ€ z âˆˆ sphere c |R|, HasDerivWithinAt f (f' z) (sphere c |R|) z) :
    (âˆ® z in C(c, R), f' z) = 0 := by
  by_cases hi : CircleIntegrable f' c R
  Â· rw [â† sub_eq_zero.2 ((periodic_circleMap c R).comp f).eq]
    refine intervalIntegral.integral_eq_sub_of_hasDerivAt (fun Î¸ _ => ?_) hi.out
    exact (h _ (circleMap_mem_sphere' _ _ _)).scomp_hasDerivAt Î¸
      (differentiable_circleMap _ _ _).hasDerivAt (circleMap_mem_sphere' _ _)
  Â· exact integral_undef hi

/-- If `f' : â„‚ â†’ E` is a derivative of a complex differentiable function on the circle
`Metric.sphere c R`, then `âˆ® z in C(c, R), f' z = 0`. -/
theorem integral_eq_zero_of_hasDerivWithinAt [CompleteSpace E]
    {f f' : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R)
    (h : âˆ€ z âˆˆ sphere c R, HasDerivWithinAt f (f' z) (sphere c R) z) : (âˆ® z in C(c, R), f' z) = 0 :=
  integral_eq_zero_of_hasDerivWithinAt' <| (abs_of_nonneg hR).symm â–¸ h

/-- If `n < 0` and `|w - c| = |R|`, then `(z - w) ^ n` is not circle integrable on the circle with
center `c` and radius `|R|`, so the integral `âˆ® z in C(c, R), (z - w) ^ n` is equal to zero. -/
theorem integral_sub_zpow_of_undef {n : â„¤} {c w : â„‚} {R : â„} (hn : n < 0)
    (hw : w âˆˆ sphere c |R|) : (âˆ® z in C(c, R), (z - w) ^ n) = 0 := by
  rcases eq_or_ne R 0 with (rfl | h0)
  Â· apply integral_radius_zero
  Â· apply integral_undef
    simpa [circleIntegrable_sub_zpow_iff, *, not_or]

/-- If `n â‰  -1` is an integer number, then the integral of `(z - w) ^ n` over the circle equals
zero. -/
theorem integral_sub_zpow_of_ne {n : â„¤} (hn : n â‰  -1) (c w : â„‚) (R : â„) :
    (âˆ® z in C(c, R), (z - w) ^ n) = 0 := by
  rcases em (w âˆˆ sphere c |R| âˆ§ n < -1) with (âŸ¨hw, hnâŸ© | H)
  Â· exact integral_sub_zpow_of_undef (hn.trans (by decide)) hw
  push_neg at H
  have hd : âˆ€ z, z â‰  w âˆ¨ -1 â‰¤ n â†’
      HasDerivAt (fun z => (z - w) ^ (n + 1) / (n + 1)) ((z - w) ^ n) z := by
    intro z hne
    convert ((hasDerivAt_zpow (n + 1) _ (hne.imp _ _)).comp z
      ((hasDerivAt_id z).sub_const w)).div_const _ using 1
    Â· have hn' : (n + 1 : â„‚) â‰  0 := by
        rwa [Ne, â† eq_neg_iff_add_eq_zero, â† Int.cast_one, â† Int.cast_neg, Int.cast_inj]
      simp [mul_div_cancel_leftâ‚€ _ hn']
    exacts [sub_ne_zero.2, neg_le_iff_add_nonneg.1]
  refine integral_eq_zero_of_hasDerivWithinAt' fun z hz => (hd z ?_).hasDerivWithinAt
  exact (ne_or_eq z w).imp_right fun (h : z = w) => H <| h â–¸ hz

end circleIntegral

/-- The power series that is equal to
$\frac{1}{2Ï€i}\sum_{n=0}^{\infty}
  \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
`w - c`. The coefficients of this power series depend only on `f âˆ˜ circleMap c R`, and the power
series converges to `f w` if `f` is differentiable on the closed ball `Metric.closedBall c R` and
`w` belongs to the corresponding open ball. For any circle integrable function `f`, this power
series converges to the Cauchy integral for `f`. -/
def cauchyPowerSeries (f : â„‚ â†’ E) (c : â„‚) (R : â„) : FormalMultilinearSeries â„‚ â„‚ E := fun n =>
  ContinuousMultilinearMap.mkPiRing â„‚ _ <|
    (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c)â»Â¹ ^ n â€¢ (z - c)â»Â¹ â€¢ f z

theorem cauchyPowerSeries_apply (f : â„‚ â†’ E) (c : â„‚) (R : â„) (n : â„•) (w : â„‚) :
    (cauchyPowerSeries f c R n fun _ => w) =
      (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (w / (z - c)) ^ n â€¢ (z - c)â»Â¹ â€¢ f z := by
  simp only [cauchyPowerSeries, ContinuousMultilinearMap.mkPiRing_apply, Fin.prod_const,
    div_eq_mul_inv, mul_pow, mul_smul, circleIntegral.integral_smul]
  rw [â† smul_comm (w ^ n)]

theorem norm_cauchyPowerSeries_le (f : â„‚ â†’ E) (c : â„‚) (R : â„) (n : â„•) :
    â€–cauchyPowerSeries f c R nâ€– â‰¤
      ((2 * Ï€)â»Â¹ * âˆ« Î¸ : â„ in (0)..2 * Ï€, â€–f (circleMap c R Î¸)â€–) * |R|â»Â¹ ^ n :=
  calc â€–cauchyPowerSeries f c R nâ€–
    _ = (2 * Ï€)â»Â¹ * â€–âˆ® z in C(c, R), (z - c)â»Â¹ ^ n â€¢ (z - c)â»Â¹ â€¢ f zâ€– := by
      simp [cauchyPowerSeries, norm_smul, Real.pi_pos.le]
    _ â‰¤ (2 * Ï€)â»Â¹ * âˆ« Î¸ in (0)..2 * Ï€, â€–deriv (circleMap c R) Î¸ â€¢
        (circleMap c R Î¸ - c)â»Â¹ ^ n â€¢ (circleMap c R Î¸ - c)â»Â¹ â€¢ f (circleMap c R Î¸)â€– :=
      (mul_le_mul_of_nonneg_left
        (intervalIntegral.norm_integral_le_integral_norm Real.two_pi_pos.le)
        (by simp [Real.pi_pos.le]))
    _ = (2 * Ï€)â»Â¹ *
        (|R|â»Â¹ ^ n * (|R| * (|R|â»Â¹ * âˆ« x : â„ in (0)..2 * Ï€, â€–f (circleMap c R x)â€–))) := by
      simp [norm_smul, mul_left_comm |R|]
    _ â‰¤ ((2 * Ï€)â»Â¹ * âˆ« Î¸ : â„ in (0)..2 * Ï€, â€–f (circleMap c R Î¸)â€–) * |R|â»Â¹ ^ n := by
      rcases eq_or_ne R 0 with (rfl | hR)
      Â· cases n <;> simp [-mul_inv_rev]
      Â· rw [mul_inv_cancel_leftâ‚€, mul_assoc, mul_comm (|R|â»Â¹ ^ n)]
        rwa [Ne, _root_.abs_eq_zero]

theorem le_radius_cauchyPowerSeries (f : â„‚ â†’ E) (c : â„‚) (R : â„â‰¥0) :
    â†‘R â‰¤ (cauchyPowerSeries f c R).radius := by
  refine
    (cauchyPowerSeries f c R).le_radius_of_bound
      ((2 * Ï€)â»Â¹ * âˆ« Î¸ : â„ in (0)..2 * Ï€, â€–f (circleMap c R Î¸)â€–) fun n => ?_
  refine (mul_le_mul_of_nonneg_right (norm_cauchyPowerSeries_le _ _ _ _)
    (pow_nonneg R.coe_nonneg _)).trans ?_
  rw [abs_of_nonneg R.coe_nonneg]
  rcases eq_or_ne (R ^ n : â„) 0 with hR | hR
  Â· rw_mod_cast [hR, mul_zero]
    exact mul_nonneg (inv_nonneg.2 Real.two_pi_pos.le)
      (intervalIntegral.integral_nonneg Real.two_pi_pos.le fun _ _ => norm_nonneg _)
  Â· rw [inv_pow]
    have : (R : â„) ^ n â‰  0 := by norm_cast at hR âŠ¢
    rw [inv_mul_cancel_rightâ‚€ this]

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R` multiplied
by `2Ï€I` converges to the integral `âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open disc
`Metric.ball c R`. -/
theorem hasSum_two_pi_I_cauchyPowerSeries_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
    (hf : CircleIntegrable f c R) (hw : â€–wâ€– < R) :
    HasSum (fun n : â„• => âˆ® z in C(c, R), (w / (z - c)) ^ n â€¢ (z - c)â»Â¹ â€¢ f z)
      (âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) := by
  have hR : 0 < R := (norm_nonneg w).trans_lt hw
  have hwR : â€–wâ€– / R âˆˆ Ico (0 : â„) 1 :=
    âŸ¨div_nonneg (norm_nonneg w) hR.le, (div_lt_one hR).2 hwâŸ©
  refine intervalIntegral.hasSum_integral_of_dominated_convergence
      (fun n Î¸ => â€–f (circleMap c R Î¸)â€– * (â€–wâ€– / R) ^ n) (fun n => ?_) (fun n => ?_) ?_ ?_ ?_
  Â· simp only [deriv_circleMap]
    apply_rules [AEStronglyMeasurable.smul, hf.def'.1] <;> apply Measurable.aestronglyMeasurable
    Â· fun_prop
    Â· fun_prop
    Â· fun_prop
  Â· simp [norm_smul, abs_of_pos hR, mul_left_comm R, inv_mul_cancel_leftâ‚€ hR.ne', mul_comm â€–_â€–]
  Â· exact Eventually.of_forall fun _ _ => (summable_geometric_of_lt_one hwR.1 hwR.2).mul_left _
  Â· simpa only [tsum_mul_left, tsum_geometric_of_lt_one hwR.1 hwR.2] using
      hf.norm.mul_continuousOn continuousOn_const
  Â· refine Eventually.of_forall fun Î¸ _ => HasSum.const_smul _ ?_
    simp only [smul_smul]
    refine HasSum.smul_const ?_ _
    have : â€–w / (circleMap c R Î¸ - c)â€– < 1 := by simpa [abs_of_pos hR] using hwR.2
    convert (hasSum_geometric_of_norm_lt_one this).mul_right _ using 1
    simp [â† sub_sub, â† mul_inv, sub_mul, div_mul_cancelâ‚€ _ (circleMap_ne_center hR.ne')]

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `Metric.ball c R`. -/
theorem hasSum_cauchyPowerSeries_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
    (hf : CircleIntegrable f c R) (hw : â€–wâ€– < R) :
    HasSum (fun n => cauchyPowerSeries f c R n fun _ => w)
      ((2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) := by
  simp only [cauchyPowerSeries_apply]
  exact (hasSum_two_pi_I_cauchyPowerSeries_integral hf hw).const_smul _

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `Metric.ball c R`. -/
theorem sum_cauchyPowerSeries_eq_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
    (hf : CircleIntegrable f c R) (hw : â€–wâ€– < R) :
    (cauchyPowerSeries f c R).sum w = (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z :=
  (hasSum_cauchyPowerSeries_integral hf hw).tsum_eq

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `Metric.ball c R`. -/
theorem hasFPowerSeriesOn_cauchy_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„â‰¥0}
    (hf : CircleIntegrable f c R) (hR : 0 < R) :
    HasFPowerSeriesOnBall (fun w => (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z)
      (cauchyPowerSeries f c R) c R :=
  { r_le := le_radius_cauchyPowerSeries _ _ _
    r_pos := ENNReal.coe_pos.2 hR
    hasSum := fun hy â†¦ hasSum_cauchyPowerSeries_integral hf <| by simpa using hy }

namespace circleIntegral

/-- Integral $\oint_{|z-c|=R} \frac{dz}{z-w} = 2Ï€i$ whenever $|w-c| < R$. -/
theorem integral_sub_inv_of_mem_ball {c w : â„‚} {R : â„} (hw : w âˆˆ ball c R) :
    (âˆ® z in C(c, R), (z - w)â»Â¹) = 2 * Ï€ * I := by
  have hR : 0 < R := dist_nonneg.trans_lt hw
  suffices H : HasSum (fun n : â„• => âˆ® z in C(c, R), ((w - c) / (z - c)) ^ n * (z - c)â»Â¹)
      (2 * Ï€ * I) by
    have A : CircleIntegrable (fun _ => (1 : â„‚)) c R := continuousOn_const.circleIntegrable'
    refine (H.unique ?_).symm
    simpa only [smul_eq_mul, mul_one, add_sub_cancel] using
      hasSum_two_pi_I_cauchyPowerSeries_integral A hw
  have H : âˆ€ n : â„•, n â‰  0 â†’ (âˆ® z in C(c, R), (z - c) ^ (-n - 1 : â„¤)) = 0 := by
    refine fun n hn => integral_sub_zpow_of_ne ?_ _ _ _; simpa
  have : (âˆ® z in C(c, R), ((w - c) / (z - c)) ^ 0 * (z - c)â»Â¹) = 2 * Ï€ * I := by simp [hR.ne']
  refine this â–¸ hasSum_single _ fun n hn => ?_
  simp only [div_eq_mul_inv, mul_pow, integral_const_mul, mul_assoc]
  rw [(integral_congr hR.le fun z hz => _).trans (H n hn), mul_zero]
  intro z _
  rw [â† pow_succ, â† zpow_natCast, inv_zpow, â† zpow_neg, Int.natCast_succ, neg_add,
    sub_eq_add_neg _ (1 : â„¤)]

end circleIntegral
