/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.MeasureTheory.Integral.IntervalIntegral
import Mathlib.Analysis.Calculus.Deriv.ZPow
import Mathlib.Analysis.NormedSpace.Pointwise
import Mathlib.Analysis.SpecialFunctions.NonIntegrable
import Mathlib.Analysis.Analytic.Basic

#align_import measure_theory.integral.circle_integral from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Integral over a circle in `â„‚`

In this file we define `âˆ® z in C(c, R), f z` to be the integral $\oint_{|z-c|=|R|} f(z)\,dz$ and
prove some properties of this integral. We give definition and prove most lemmas for a function
`f : â„‚ â†’ E`, where `E` is a complex Banach space. For this reason,
some lemmas use, e.g., `(z - c)â»Â¹ â€¢ f z` instead of `f z / (z - c)`.

## Main definitions

* `circleMap c R`: the exponential map $Î¸ â†¦ c + R e^{Î¸i}$;

* `CircleIntegrable f c R`: a function `f : â„‚ â†’ E` is integrable on the circle with center `c` and
  radius `R` if `f âˆ˜ circleMap c R` is integrable on `[0, 2Ï€]`;

* `circleIntegral f c R`: the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2Ï€}(c + Re^{Î¸ i})' f(c+Re^{Î¸ i})\,dÎ¸$;

* `cauchyPowerSeries f c R`: the power series that is equal to
  $\sum_{n=0}^{\infty} \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
  `w - c`. The coefficients of this power series depend only on `f âˆ˜ circleMap c R`, and the power
  series converges to `f w` if `f` is differentiable on the closed ball `Metric.closedBall c R`
  and `w` belongs to the corresponding open ball.

## Main statements

* `hasFPowerSeriesOn_cauchy_integral`: for any circle integrable function `f`, the power series
  `cauchyPowerSeries f c R`, `R > 0`, converges to the Cauchy integral
  `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open disc `Metric.ball c R`;

* `circleIntegral.integral_sub_zpow_of_undef`, `circleIntegral.integral_sub_zpow_of_ne`, and
  `circleIntegral.integral_sub_inv_of_mem_ball`: formulas for `âˆ® z in C(c, R), (z - w) ^ n`,
  `n : â„¤`. These lemmas cover the following cases:

  - `circleIntegral.integral_sub_zpow_of_undef`, `n < 0` and `|w - c| = |R|`: in this case the
    function is not integrable, so the integral is equal to its default value (zero);

  - `circleIntegral.integral_sub_zpow_of_ne`, `n â‰  -1`: in the cases not covered by the previous
    lemma, we have `(z - w) ^ n = ((z - w) ^ (n + 1) / (n + 1))'`, thus the integral equals zero;

  - `circleIntegral.integral_sub_inv_of_mem_ball`, `n = -1`, `|w - c| < R`: in this case the
    integral is equal to `2Ï€i`.

  The case `n = -1`, `|w -c| > R` is not covered by these lemmas. While it is possible to construct
  an explicit primitive, it is easier to apply Cauchy theorem, so we postpone the proof till we have
  this theorem (see #10000).

## Notation

- `âˆ® z in C(c, R), f z`: notation for the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2Ï€}(c + Re^{Î¸ i})' f(c+Re^{Î¸ i})\,dÎ¸$.

## Tags

integral, circle, Cauchy integral
-/


variable {E : Type*} [NormedAddCommGroup E]

noncomputable section

open scoped Real NNReal Interval Pointwise Topology

open Complex MeasureTheory TopologicalSpace Metric Function Set Filter Asymptotics

/-!
### `circleMap`, a parametrization of a circle
-/


/-- The exponential map $Î¸ â†¦ c + R e^{Î¸i}$. The range of this map is the circle in `â„‚` with center
`c` and radius `|R|`. -/
def circleMap (c : â„‚) (R : â„) : â„ â†’ â„‚ := fun Î¸ => c + R * exp (Î¸ * I)
#align circle_map circleMap

/-- `circleMap` is `2Ï€`-periodic. -/
theorem periodic_circleMap (c : â„‚) (R : â„) : Periodic (circleMap c R) (2 * Ï€) := fun Î¸ => by
  simp [circleMap, add_mul, exp_periodic _]
#align periodic_circle_map periodic_circleMap

theorem Set.Countable.preimage_circleMap {s : Set â„‚} (hs : s.Countable) (c : â„‚) {R : â„}
    (hR : R â‰  0) : (circleMap c R â»Â¹' s).Countable :=
  show (((â†‘) : â„ â†’ â„‚) â»Â¹' ((Â· * I) â»Â¹'
      (exp â»Â¹' ((R * Â·) â»Â¹' ((c + Â·) â»Â¹' s))))).Countable from
    (((hs.preimage (add_right_injective _)).preimage <|
      mul_right_injectiveâ‚€ <| ofReal_ne_zero.2 hR).preimage_cexp.preimage <|
        mul_left_injectiveâ‚€ I_ne_zero).preimage ofReal_injective
#align set.countable.preimage_circle_map Set.Countable.preimage_circleMap

@[simp]
theorem circleMap_sub_center (c : â„‚) (R : â„) (Î¸ : â„) : circleMap c R Î¸ - c = circleMap 0 R Î¸ := by
  simp [circleMap]
#align circle_map_sub_center circleMap_sub_center

theorem circleMap_zero (R Î¸ : â„) : circleMap 0 R Î¸ = R * exp (Î¸ * I) :=
  zero_add _
#align circle_map_zero circleMap_zero

@[simp]
theorem abs_circleMap_zero (R : â„) (Î¸ : â„) : abs (circleMap 0 R Î¸) = |R| := by simp [circleMap]
#align abs_circle_map_zero abs_circleMap_zero

theorem circleMap_mem_sphere' (c : â„‚) (R : â„) (Î¸ : â„) : circleMap c R Î¸ âˆˆ sphere c |R| := by simp
#align circle_map_mem_sphere' circleMap_mem_sphere'

theorem circleMap_mem_sphere (c : â„‚) {R : â„} (hR : 0 â‰¤ R) (Î¸ : â„) :
    circleMap c R Î¸ âˆˆ sphere c R := by
  simpa only [_root_.abs_of_nonneg hR] using circleMap_mem_sphere' c R Î¸
#align circle_map_mem_sphere circleMap_mem_sphere

theorem circleMap_mem_closedBall (c : â„‚) {R : â„} (hR : 0 â‰¤ R) (Î¸ : â„) :
    circleMap c R Î¸ âˆˆ closedBall c R :=
  sphere_subset_closedBall (circleMap_mem_sphere c hR Î¸)
#align circle_map_mem_closed_ball circleMap_mem_closedBall

theorem circleMap_not_mem_ball (c : â„‚) (R : â„) (Î¸ : â„) : circleMap c R Î¸ âˆ‰ ball c R := by
  simp [dist_eq, le_abs_self]
#align circle_map_not_mem_ball circleMap_not_mem_ball

theorem circleMap_ne_mem_ball {c : â„‚} {R : â„} {w : â„‚} (hw : w âˆˆ ball c R) (Î¸ : â„) :
    circleMap c R Î¸ â‰  w :=
  (ne_of_mem_of_not_mem hw (circleMap_not_mem_ball _ _ _)).symm
#align circle_map_ne_mem_ball circleMap_ne_mem_ball

/-- The range of `circleMap c R` is the circle with center `c` and radius `|R|`. -/
@[simp]
theorem range_circleMap (c : â„‚) (R : â„) : range (circleMap c R) = sphere c |R| :=
  calc
    range (circleMap c R) = c +áµ¥ R â€¢ range fun Î¸ : â„ => exp (Î¸ * I) := by
      simp (config := { unfoldPartialApp := true }) only [â† image_vadd, â† image_smul, â† range_comp,
        vadd_eq_add, circleMap, Function.comp_def, real_smul]
    _ = sphere c |R| := by
      rw [Complex.range_exp_mul_I, smul_sphere R 0 zero_le_one]
      simp
#align range_circle_map range_circleMap

/-- The image of `(0, 2Ï€]` under `circleMap c R` is the circle with center `c` and radius `|R|`. -/
@[simp]
theorem image_circleMap_Ioc (c : â„‚) (R : â„) : circleMap c R '' Ioc 0 (2 * Ï€) = sphere c |R| := by
  rw [â† range_circleMap, â† (periodic_circleMap c R).image_Ioc Real.two_pi_pos 0, zero_add]
#align image_circle_map_Ioc image_circleMap_Ioc

@[simp]
theorem circleMap_eq_center_iff {c : â„‚} {R : â„} {Î¸ : â„} : circleMap c R Î¸ = c â†” R = 0 := by
  simp [circleMap, exp_ne_zero]
#align circle_map_eq_center_iff circleMap_eq_center_iff

@[simp]
theorem circleMap_zero_radius (c : â„‚) : circleMap c 0 = const â„ c :=
  funext fun _ => circleMap_eq_center_iff.2 rfl
#align circle_map_zero_radius circleMap_zero_radius

theorem circleMap_ne_center {c : â„‚} {R : â„} (hR : R â‰  0) {Î¸ : â„} : circleMap c R Î¸ â‰  c :=
  mt circleMap_eq_center_iff.1 hR
#align circle_map_ne_center circleMap_ne_center

theorem hasDerivAt_circleMap (c : â„‚) (R : â„) (Î¸ : â„) :
    HasDerivAt (circleMap c R) (circleMap 0 R Î¸ * I) Î¸ := by
  simpa only [mul_assoc, one_mul, ofRealCLM_apply, circleMap, ofReal_one, zero_add]
    using (((ofRealCLM.hasDerivAt (x := Î¸)).mul_const I).cexp.const_mul (R : â„‚)).const_add c
#align has_deriv_at_circle_map hasDerivAt_circleMap

/- TODO: prove `ContDiff â„ (circleMap c R)`. This needs a version of `ContDiff.mul`
for multiplication in a normed algebra over the base field. -/
theorem differentiable_circleMap (c : â„‚) (R : â„) : Differentiable â„ (circleMap c R) := fun Î¸ =>
  (hasDerivAt_circleMap c R Î¸).differentiableAt
#align differentiable_circle_map differentiable_circleMap

@[continuity]
theorem continuous_circleMap (c : â„‚) (R : â„) : Continuous (circleMap c R) :=
  (differentiable_circleMap c R).continuous
#align continuous_circle_map continuous_circleMap

@[measurability]
theorem measurable_circleMap (c : â„‚) (R : â„) : Measurable (circleMap c R) :=
  (continuous_circleMap c R).measurable
#align measurable_circle_map measurable_circleMap

@[simp]
theorem deriv_circleMap (c : â„‚) (R : â„) (Î¸ : â„) : deriv (circleMap c R) Î¸ = circleMap 0 R Î¸ * I :=
  (hasDerivAt_circleMap _ _ _).deriv
#align deriv_circle_map deriv_circleMap

theorem deriv_circleMap_eq_zero_iff {c : â„‚} {R : â„} {Î¸ : â„} :
    deriv (circleMap c R) Î¸ = 0 â†” R = 0 := by simp [I_ne_zero]
#align deriv_circle_map_eq_zero_iff deriv_circleMap_eq_zero_iff

theorem deriv_circleMap_ne_zero {c : â„‚} {R : â„} {Î¸ : â„} (hR : R â‰  0) :
    deriv (circleMap c R) Î¸ â‰  0 :=
  mt deriv_circleMap_eq_zero_iff.1 hR
#align deriv_circle_map_ne_zero deriv_circleMap_ne_zero

theorem lipschitzWith_circleMap (c : â„‚) (R : â„) : LipschitzWith (Real.nnabs R) (circleMap c R) :=
  lipschitzWith_of_nnnorm_deriv_le (differentiable_circleMap _ _) fun Î¸ =>
    NNReal.coe_le_coe.1 <| by simp
#align lipschitz_with_circle_map lipschitzWith_circleMap

theorem continuous_circleMap_inv {R : â„} {z w : â„‚} (hw : w âˆˆ ball z R) :
    Continuous fun Î¸ => (circleMap z R Î¸ - w)â»Â¹ := by
  have : âˆ€ Î¸, circleMap z R Î¸ - w â‰  0 := by
    simp_rw [sub_ne_zero]
    exact fun Î¸ => circleMap_ne_mem_ball hw Î¸
  -- Porting note: was `continuity`
  exact Continuous.invâ‚€ (by continuity) this
#align continuous_circle_map_inv continuous_circleMap_inv

/-!
### Integrability of a function on a circle
-/


/-- We say that a function `f : â„‚ â†’ E` is integrable on the circle with center `c` and radius `R` if
the function `f âˆ˜ circleMap c R` is integrable on `[0, 2Ï€]`.

Note that the actual function used in the definition of `circleIntegral` is
`(deriv (circleMap c R) Î¸) â€¢ f (circleMap c R Î¸)`. Integrability of this function is equivalent
to integrability of `f âˆ˜ circleMap c R` whenever `R â‰  0`. -/
def CircleIntegrable (f : â„‚ â†’ E) (c : â„‚) (R : â„) : Prop :=
  IntervalIntegrable (fun Î¸ : â„ => f (circleMap c R Î¸)) volume 0 (2 * Ï€)
#align circle_integrable CircleIntegrable

@[simp]
theorem circleIntegrable_const (a : E) (c : â„‚) (R : â„) : CircleIntegrable (fun _ => a) c R :=
  intervalIntegrable_const
#align circle_integrable_const circleIntegrable_const

namespace CircleIntegrable

variable {f g : â„‚ â†’ E} {c : â„‚} {R : â„}

nonrec theorem add (hf : CircleIntegrable f c R) (hg : CircleIntegrable g c R) :
    CircleIntegrable (f + g) c R :=
  hf.add hg
#align circle_integrable.add CircleIntegrable.add

nonrec theorem neg (hf : CircleIntegrable f c R) : CircleIntegrable (-f) c R :=
  hf.neg
#align circle_integrable.neg CircleIntegrable.neg

/-- The function we actually integrate over `[0, 2Ï€]` in the definition of `circleIntegral` is
integrable. -/
theorem out [NormedSpace â„‚ E] (hf : CircleIntegrable f c R) :
    IntervalIntegrable (fun Î¸ : â„ => deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)) volume 0
      (2 * Ï€) := by
  simp only [CircleIntegrable, deriv_circleMap, intervalIntegrable_iff] at *
  refine (hf.norm.const_mul |R|).mono' ?_ ?_
  Â· exact ((continuous_circleMap _ _).aestronglyMeasurable.mul_const I).smul hf.aestronglyMeasurable
  Â· simp [norm_smul]
#align circle_integrable.out CircleIntegrable.out

end CircleIntegrable

@[simp]
theorem circleIntegrable_zero_radius {f : â„‚ â†’ E} {c : â„‚} : CircleIntegrable f c 0 := by
  simp [CircleIntegrable]
#align circle_integrable_zero_radius circleIntegrable_zero_radius

theorem circleIntegrable_iff [NormedSpace â„‚ E] {f : â„‚ â†’ E} {c : â„‚} (R : â„) :
    CircleIntegrable f c R â†” IntervalIntegrable (fun Î¸ : â„ =>
      deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)) volume 0 (2 * Ï€) := by
  by_cases hâ‚€ : R = 0
  Â· simp (config := { unfoldPartialApp := true }) [hâ‚€, const]
  refine âŸ¨fun h => h.out, fun h => ?_âŸ©
  simp only [CircleIntegrable, intervalIntegrable_iff, deriv_circleMap] at h âŠ¢
  refine (h.norm.const_mul |R|â»Â¹).mono' ?_ ?_
  Â· have H : âˆ€ {Î¸}, circleMap 0 R Î¸ * I â‰  0 := fun {Î¸} => by simp [hâ‚€, I_ne_zero]
    simpa only [inv_smul_smulâ‚€ H]
      using ((continuous_circleMap 0 R).aestronglyMeasurable.mul_const
        I).aemeasurable.inv.aestronglyMeasurable.smul h.aestronglyMeasurable
  Â· simp [norm_smul, hâ‚€]
#align circle_integrable_iff circleIntegrable_iff

theorem ContinuousOn.circleIntegrable' {f : â„‚ â†’ E} {c : â„‚} {R : â„}
    (hf : ContinuousOn f (sphere c |R|)) : CircleIntegrable f c R :=
  (hf.comp_continuous (continuous_circleMap _ _) (circleMap_mem_sphere' _ _)).intervalIntegrable _ _
#align continuous_on.circle_integrable' ContinuousOn.circleIntegrable'

theorem ContinuousOn.circleIntegrable {f : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R)
    (hf : ContinuousOn f (sphere c R)) : CircleIntegrable f c R :=
  ContinuousOn.circleIntegrable' <| (_root_.abs_of_nonneg hR).symm â–¸ hf
#align continuous_on.circle_integrable ContinuousOn.circleIntegrable

#adaptation_note /-- nightly-2024-04-01: the simpNF linter now times out on this lemma. -/
/-- The function `fun z â†¦ (z - w) ^ n`, `n : â„¤`, is circle integrable on the circle with center `c`
and radius `|R|` if and only if `R = 0` or `0 â‰¤ n`, or `w` does not belong to this circle. -/
@[simp, nolint simpNF]
theorem circleIntegrable_sub_zpow_iff {c w : â„‚} {R : â„} {n : â„¤} :
    CircleIntegrable (fun z => (z - w) ^ n) c R â†” R = 0 âˆ¨ 0 â‰¤ n âˆ¨ w âˆ‰ sphere c |R| := by
  constructor
  Â· intro h; contrapose! h; rcases h with âŸ¨hR, hn, hwâŸ©
    simp only [circleIntegrable_iff R, deriv_circleMap]
    rw [â† image_circleMap_Ioc] at hw; rcases hw with âŸ¨Î¸, hÎ¸, rflâŸ©
    replace hÎ¸ : Î¸ âˆˆ [[0, 2 * Ï€]] := Icc_subset_uIcc (Ioc_subset_Icc_self hÎ¸)
    refine not_intervalIntegrable_of_sub_inv_isBigO_punctured ?_ Real.two_pi_pos.ne hÎ¸
    set f : â„ â†’ â„‚ := fun Î¸' => circleMap c R Î¸' - circleMap c R Î¸
    have : âˆ€á¶  Î¸' in ğ“[â‰ ] Î¸, f Î¸' âˆˆ ball (0 : â„‚) 1 \ {0} := by
      suffices âˆ€á¶  z in ğ“[â‰ ] circleMap c R Î¸, z - circleMap c R Î¸ âˆˆ ball (0 : â„‚) 1 \ {0} from
        ((differentiable_circleMap c R Î¸).hasDerivAt.tendsto_punctured_nhds
          (deriv_circleMap_ne_zero hR)).eventually this
      filter_upwards [self_mem_nhdsWithin, mem_nhdsWithin_of_mem_nhds (ball_mem_nhds _ zero_lt_one)]
      simp_all [dist_eq, sub_eq_zero]
    refine (((hasDerivAt_circleMap c R Î¸).isBigO_sub.mono inf_le_left).inv_rev
      (this.mono fun Î¸' hâ‚ hâ‚‚ => absurd hâ‚‚ hâ‚.2)).trans ?_
    refine IsBigO.of_bound |R|â»Â¹ (this.mono fun Î¸' hÎ¸' => ?_)
    set x := abs (f Î¸')
    suffices xâ»Â¹ â‰¤ x ^ n by
      simpa only [inv_mul_cancel_leftâ‚€, abs_eq_zero.not.2 hR, norm_eq_abs, map_invâ‚€,
        Algebra.id.smul_eq_mul, map_mul, abs_circleMap_zero, abs_I, mul_one, abs_zpow, Ne,
        not_false_iff] using this
    have : x âˆˆ Ioo (0 : â„) 1 := by simpa [x, and_comm] using hÎ¸'
    rw [â† zpow_neg_one]
    refine (zpow_strictAnti this.1 this.2).le_iff_le.2 (Int.lt_add_one_iff.1 ?_); exact hn
  Â· rintro (rfl | H)
    exacts [circleIntegrable_zero_radius,
      ((continuousOn_id.sub continuousOn_const).zpowâ‚€ _ fun z hz =>
        H.symm.imp_left fun (hw : w âˆ‰ sphere c |R|) =>
          sub_ne_zero.2 <| ne_of_mem_of_not_mem hz hw).circleIntegrable']
#align circle_integrable_sub_zpow_iff circleIntegrable_sub_zpow_iff

#adaptation_note /-- nightly-2024-04-01
The simpNF linter now times out on this lemma. -/
@[simp, nolint simpNF]
theorem circleIntegrable_sub_inv_iff {c w : â„‚} {R : â„} :
    CircleIntegrable (fun z => (z - w)â»Â¹) c R â†” R = 0 âˆ¨ w âˆ‰ sphere c |R| := by
  simp only [â† zpow_neg_one, circleIntegrable_sub_zpow_iff]; norm_num
#align circle_integrable_sub_inv_iff circleIntegrable_sub_inv_iff

variable [NormedSpace â„‚ E] [CompleteSpace E]

/-- Definition for $\oint_{|z-c|=R} f(z)\,dz$. -/
def circleIntegral (f : â„‚ â†’ E) (c : â„‚) (R : â„) : E :=
  âˆ« Î¸ : â„ in (0)..2 * Ï€, deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)
#align circle_integral circleIntegral

notation3 "âˆ® "(...)" in ""C("c", "R")"", "r:(scoped f => circleIntegral f c R) => r

theorem circleIntegral_def_Icc (f : â„‚ â†’ E) (c : â„‚) (R : â„) :
    (âˆ® z in C(c, R), f z) = âˆ« Î¸ in Icc 0 (2 * Ï€),
    deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸) := by
  rw [circleIntegral, intervalIntegral.integral_of_le Real.two_pi_pos.le,
    Measure.restrict_congr_set Ioc_ae_eq_Icc]
#align circle_integral_def_Icc circleIntegral_def_Icc

namespace circleIntegral

@[simp]
theorem integral_radius_zero (f : â„‚ â†’ E) (c : â„‚) : (âˆ® z in C(c, 0), f z) = 0 := by
  simp (config := { unfoldPartialApp := true }) [circleIntegral, const]
#align circle_integral.integral_radius_zero circleIntegral.integral_radius_zero

theorem integral_congr {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R) (h : EqOn f g (sphere c R)) :
    (âˆ® z in C(c, R), f z) = âˆ® z in C(c, R), g z :=
  intervalIntegral.integral_congr fun Î¸ _ => by simp only [h (circleMap_mem_sphere _ hR _)]
#align circle_integral.integral_congr circleIntegral.integral_congr

theorem integral_sub_inv_smul_sub_smul (f : â„‚ â†’ E) (c w : â„‚) (R : â„) :
    (âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ (z - w) â€¢ f z) = âˆ® z in C(c, R), f z := by
  rcases eq_or_ne R 0 with (rfl | hR); Â· simp only [integral_radius_zero]
  have : (circleMap c R â»Â¹' {w}).Countable := (countable_singleton _).preimage_circleMap c hR
  refine intervalIntegral.integral_congr_ae ((this.ae_not_mem _).mono fun Î¸ hÎ¸ _' => ?_)
  change circleMap c R Î¸ â‰  w at hÎ¸
  simp only [inv_smul_smulâ‚€ (sub_ne_zero.2 <| hÎ¸)]
#align circle_integral.integral_sub_inv_smul_sub_smul circleIntegral.integral_sub_inv_smul_sub_smul

theorem integral_undef {f : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : Â¬CircleIntegrable f c R) :
    (âˆ® z in C(c, R), f z) = 0 :=
  intervalIntegral.integral_undef (mt (circleIntegrable_iff R).mpr hf)
#align circle_integral.integral_undef circleIntegral.integral_undef

theorem integral_sub {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : CircleIntegrable f c R)
    (hg : CircleIntegrable g c R) :
    (âˆ® z in C(c, R), f z - g z) = (âˆ® z in C(c, R), f z) - âˆ® z in C(c, R), g z := by
  simp only [circleIntegral, smul_sub, intervalIntegral.integral_sub hf.out hg.out]
#align circle_integral.integral_sub circleIntegral.integral_sub

theorem norm_integral_le_of_norm_le_const' {f : â„‚ â†’ E} {c : â„‚} {R C : â„}
    (hf : âˆ€ z âˆˆ sphere c |R|, â€–f zâ€– â‰¤ C) : â€–âˆ® z in C(c, R), f zâ€– â‰¤ 2 * Ï€ * |R| * C :=
  calc
    â€–âˆ® z in C(c, R), f zâ€– â‰¤ |R| * C * |2 * Ï€ - 0| :=
      intervalIntegral.norm_integral_le_of_norm_le_const fun Î¸ _ =>
        calc
          â€–deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)â€– = |R| * â€–f (circleMap c R Î¸)â€– := by
            simp [norm_smul]
          _ â‰¤ |R| * C :=
            mul_le_mul_of_nonneg_left (hf _ <| circleMap_mem_sphere' _ _ _) (abs_nonneg _)
    _ = 2 * Ï€ * |R| * C := by rw [sub_zero, _root_.abs_of_pos Real.two_pi_pos]; ac_rfl
#align circle_integral.norm_integral_le_of_norm_le_const' circleIntegral.norm_integral_le_of_norm_le_const'

theorem norm_integral_le_of_norm_le_const {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 â‰¤ R)
    (hf : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C) : â€–âˆ® z in C(c, R), f zâ€– â‰¤ 2 * Ï€ * R * C :=
  have : |R| = R := abs_of_nonneg hR
  calc
    â€–âˆ® z in C(c, R), f zâ€– â‰¤ 2 * Ï€ * |R| * C := norm_integral_le_of_norm_le_const' <| by rwa [this]
    _ = 2 * Ï€ * R * C := by rw [this]
#align circle_integral.norm_integral_le_of_norm_le_const circleIntegral.norm_integral_le_of_norm_le_const

theorem norm_two_pi_i_inv_smul_integral_le_of_norm_le_const {f : â„‚ â†’ E} {c : â„‚} {R C : â„}
    (hR : 0 â‰¤ R) (hf : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C) :
    â€–(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), f zâ€– â‰¤ R * C := by
  have : â€–(2 * Ï€ * I : â„‚)â»Â¹â€– = (2 * Ï€)â»Â¹ := by simp [Real.pi_pos.le]
  rw [norm_smul, this, â† div_eq_inv_mul, div_le_iff Real.two_pi_pos, mul_comm (R * C), â† mul_assoc]
  exact norm_integral_le_of_norm_le_const hR hf
set_option linter.uppercaseLean3 false in
#align circle_integral.norm_two_pi_I_inv_smul_integral_le_of_norm_le_const circleIntegral.norm_two_pi_i_inv_smul_integral_le_of_norm_le_const

/-- If `f` is continuous on the circle `|z - c| = R`, `R > 0`, the `â€–f zâ€–` is less than or equal to
`C : â„` on this circle, and this norm is strictly less than `C` at some point `z` of the circle,
then `â€–âˆ® z in C(c, R), f zâ€– < 2 * Ï€ * R * C`. -/
theorem norm_integral_lt_of_norm_le_const_of_lt {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 < R)
    (hc : ContinuousOn f (sphere c R)) (hf : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C)
    (hlt : âˆƒ z âˆˆ sphere c R, â€–f zâ€– < C) : â€–âˆ® z in C(c, R), f zâ€– < 2 * Ï€ * R * C := by
  rw [â† _root_.abs_of_pos hR, â† image_circleMap_Ioc] at hlt
  rcases hlt with âŸ¨_, âŸ¨Î¸â‚€, hmem, rflâŸ©, hltâŸ©
  calc
    â€–âˆ® z in C(c, R), f zâ€– â‰¤ âˆ« Î¸ in (0)..2 * Ï€, â€–deriv (circleMap c R) Î¸ â€¢ f (circleMap c R Î¸)â€– :=
      intervalIntegral.norm_integral_le_integral_norm Real.two_pi_pos.le
    _ < âˆ« _ in (0)..2 * Ï€, R * C := by
      simp only [norm_smul, deriv_circleMap, norm_eq_abs, map_mul, abs_I, mul_one,
        abs_circleMap_zero, abs_of_pos hR]
      refine intervalIntegral.integral_lt_integral_of_continuousOn_of_le_of_exists_lt
          Real.two_pi_pos ?_ continuousOn_const (fun Î¸ _ => ?_) âŸ¨Î¸â‚€, Ioc_subset_Icc_self hmem, ?_âŸ©
      Â· exact continuousOn_const.mul (hc.comp (continuous_circleMap _ _).continuousOn fun Î¸ _ =>
          circleMap_mem_sphere _ hR.le _).norm
      Â· exact mul_le_mul_of_nonneg_left (hf _ <| circleMap_mem_sphere _ hR.le _) hR.le
      Â· exact (mul_lt_mul_left hR).2 hlt
    _ = 2 * Ï€ * R * C := by simp [mul_assoc]; ring
#align circle_integral.norm_integral_lt_of_norm_le_const_of_lt circleIntegral.norm_integral_lt_of_norm_le_const_of_lt

@[simp]
theorem integral_smul {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [SMulCommClass ğ•œ â„‚ E] (a : ğ•œ)
    (f : â„‚ â†’ E) (c : â„‚) (R : â„) : (âˆ® z in C(c, R), a â€¢ f z) = a â€¢ âˆ® z in C(c, R), f z := by
  simp only [circleIntegral, â† smul_comm a (_ : â„‚) (_ : E), intervalIntegral.integral_smul]
#align circle_integral.integral_smul circleIntegral.integral_smul

@[simp]
theorem integral_smul_const (f : â„‚ â†’ â„‚) (a : E) (c : â„‚) (R : â„) :
    (âˆ® z in C(c, R), f z â€¢ a) = (âˆ® z in C(c, R), f z) â€¢ a := by
  simp only [circleIntegral, intervalIntegral.integral_smul_const, â† smul_assoc]
#align circle_integral.integral_smul_const circleIntegral.integral_smul_const

@[simp]
theorem integral_const_mul (a : â„‚) (f : â„‚ â†’ â„‚) (c : â„‚) (R : â„) :
    (âˆ® z in C(c, R), a * f z) = a * âˆ® z in C(c, R), f z :=
  integral_smul a f c R
#align circle_integral.integral_const_mul circleIntegral.integral_const_mul

@[simp]
theorem integral_sub_center_inv (c : â„‚) {R : â„} (hR : R â‰  0) :
    (âˆ® z in C(c, R), (z - c)â»Â¹) = 2 * Ï€ * I := by
  simp [circleIntegral, â† div_eq_mul_inv, mul_div_cancel_leftâ‚€ _ (circleMap_ne_center hR),
    -- Porting note: `simp` didn't need a hint to apply `integral_const` here
    intervalIntegral.integral_const I]
#align circle_integral.integral_sub_center_inv circleIntegral.integral_sub_center_inv

/-- If `f' : â„‚ â†’ E` is a derivative of a complex differentiable function on the circle
`Metric.sphere c |R|`, then `âˆ® z in C(c, R), f' z = 0`. -/
theorem integral_eq_zero_of_hasDerivWithinAt' {f f' : â„‚ â†’ E} {c : â„‚} {R : â„}
    (h : âˆ€ z âˆˆ sphere c |R|, HasDerivWithinAt f (f' z) (sphere c |R|) z) :
    (âˆ® z in C(c, R), f' z) = 0 := by
  by_cases hi : CircleIntegrable f' c R
  Â· rw [â† sub_eq_zero.2 ((periodic_circleMap c R).comp f).eq]
    refine intervalIntegral.integral_eq_sub_of_hasDerivAt (fun Î¸ _ => ?_) hi.out
    exact (h _ (circleMap_mem_sphere' _ _ _)).scomp_hasDerivAt Î¸
      (differentiable_circleMap _ _ _).hasDerivAt (circleMap_mem_sphere' _ _)
  Â· exact integral_undef hi
#align circle_integral.integral_eq_zero_of_has_deriv_within_at' circleIntegral.integral_eq_zero_of_hasDerivWithinAt'

/-- If `f' : â„‚ â†’ E` is a derivative of a complex differentiable function on the circle
`Metric.sphere c R`, then `âˆ® z in C(c, R), f' z = 0`. -/
theorem integral_eq_zero_of_hasDerivWithinAt {f f' : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R)
    (h : âˆ€ z âˆˆ sphere c R, HasDerivWithinAt f (f' z) (sphere c R) z) : (âˆ® z in C(c, R), f' z) = 0 :=
  integral_eq_zero_of_hasDerivWithinAt' <| (_root_.abs_of_nonneg hR).symm â–¸ h
#align circle_integral.integral_eq_zero_of_has_deriv_within_at circleIntegral.integral_eq_zero_of_hasDerivWithinAt

/-- If `n < 0` and `|w - c| = |R|`, then `(z - w) ^ n` is not circle integrable on the circle with
center `c` and radius `|R|`, so the integral `âˆ® z in C(c, R), (z - w) ^ n` is equal to zero. -/
theorem integral_sub_zpow_of_undef {n : â„¤} {c w : â„‚} {R : â„} (hn : n < 0)
    (hw : w âˆˆ sphere c |R|) : (âˆ® z in C(c, R), (z - w) ^ n) = 0 := by
  rcases eq_or_ne R 0 with (rfl | h0)
  Â· apply integral_radius_zero
  Â· apply integral_undef
    simpa [circleIntegrable_sub_zpow_iff, *, not_or]
#align circle_integral.integral_sub_zpow_of_undef circleIntegral.integral_sub_zpow_of_undef

/-- If `n â‰  -1` is an integer number, then the integral of `(z - w) ^ n` over the circle equals
zero. -/
theorem integral_sub_zpow_of_ne {n : â„¤} (hn : n â‰  -1) (c w : â„‚) (R : â„) :
    (âˆ® z in C(c, R), (z - w) ^ n) = 0 := by
  rcases em (w âˆˆ sphere c |R| âˆ§ n < -1) with (âŸ¨hw, hnâŸ© | H)
  Â· exact integral_sub_zpow_of_undef (hn.trans (by decide)) hw
  push_neg at H
  have hd : âˆ€ z, z â‰  w âˆ¨ -1 â‰¤ n â†’
      HasDerivAt (fun z => (z - w) ^ (n + 1) / (n + 1)) ((z - w) ^ n) z := by
    intro z hne
    convert ((hasDerivAt_zpow (n + 1) _ (hne.imp _ _)).comp z
      ((hasDerivAt_id z).sub_const w)).div_const _ using 1
    Â· have hn' : (n + 1 : â„‚) â‰  0 := by
        rwa [Ne, â† eq_neg_iff_add_eq_zero, â† Int.cast_one, â† Int.cast_neg, Int.cast_inj]
      simp [mul_assoc, mul_div_cancel_leftâ‚€ _ hn']
    exacts [sub_ne_zero.2, neg_le_iff_add_nonneg.1]
  refine integral_eq_zero_of_hasDerivWithinAt' fun z hz => (hd z ?_).hasDerivWithinAt
  exact (ne_or_eq z w).imp_right fun (h : z = w) => H <| h â–¸ hz
#align circle_integral.integral_sub_zpow_of_ne circleIntegral.integral_sub_zpow_of_ne

end circleIntegral

/-- The power series that is equal to
$\frac{1}{2Ï€i}\sum_{n=0}^{\infty}
  \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
`w - c`. The coefficients of this power series depend only on `f âˆ˜ circleMap c R`, and the power
series converges to `f w` if `f` is differentiable on the closed ball `Metric.closedBall c R` and
`w` belongs to the corresponding open ball. For any circle integrable function `f`, this power
series converges to the Cauchy integral for `f`. -/
def cauchyPowerSeries (f : â„‚ â†’ E) (c : â„‚) (R : â„) : FormalMultilinearSeries â„‚ â„‚ E := fun n =>
  ContinuousMultilinearMap.mkPiRing â„‚ _ <|
    (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c)â»Â¹ ^ n â€¢ (z - c)â»Â¹ â€¢ f z
#align cauchy_power_series cauchyPowerSeries

theorem cauchyPowerSeries_apply (f : â„‚ â†’ E) (c : â„‚) (R : â„) (n : â„•) (w : â„‚) :
    (cauchyPowerSeries f c R n fun _ => w) =
      (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (w / (z - c)) ^ n â€¢ (z - c)â»Â¹ â€¢ f z := by
  simp only [cauchyPowerSeries, ContinuousMultilinearMap.mkPiRing_apply, Fin.prod_const,
    div_eq_mul_inv, mul_pow, mul_smul, circleIntegral.integral_smul]
  rw [â† smul_comm (w ^ n)]
#align cauchy_power_series_apply cauchyPowerSeries_apply

theorem norm_cauchyPowerSeries_le (f : â„‚ â†’ E) (c : â„‚) (R : â„) (n : â„•) :
    â€–cauchyPowerSeries f c R nâ€– â‰¤
      ((2 * Ï€)â»Â¹ * âˆ« Î¸ : â„ in (0)..2 * Ï€, â€–f (circleMap c R Î¸)â€–) * |R|â»Â¹ ^ n :=
  calc â€–cauchyPowerSeries f c R nâ€–
    _ = (2 * Ï€)â»Â¹ * â€–âˆ® z in C(c, R), (z - c)â»Â¹ ^ n â€¢ (z - c)â»Â¹ â€¢ f zâ€– := by
      simp [cauchyPowerSeries, norm_smul, Real.pi_pos.le]
    _ â‰¤ (2 * Ï€)â»Â¹ * âˆ« Î¸ in (0)..2 * Ï€, â€–deriv (circleMap c R) Î¸ â€¢
        (circleMap c R Î¸ - c)â»Â¹ ^ n â€¢ (circleMap c R Î¸ - c)â»Â¹ â€¢ f (circleMap c R Î¸)â€– :=
      (mul_le_mul_of_nonneg_left
        (intervalIntegral.norm_integral_le_integral_norm Real.two_pi_pos.le)
        (by simp [Real.pi_pos.le]))
    _ = (2 * Ï€)â»Â¹ *
        (|R|â»Â¹ ^ n * (|R| * (|R|â»Â¹ * âˆ« x : â„ in (0)..2 * Ï€, â€–f (circleMap c R x)â€–))) := by
      simp [norm_smul, mul_left_comm |R|]
    _ â‰¤ ((2 * Ï€)â»Â¹ * âˆ« Î¸ : â„ in (0)..2 * Ï€, â€–f (circleMap c R Î¸)â€–) * |R|â»Â¹ ^ n := by
      rcases eq_or_ne R 0 with (rfl | hR)
      Â· cases n <;> simp [-mul_inv_rev]
        rw [â† mul_assoc, inv_mul_cancel (Real.two_pi_pos.ne.symm), one_mul]
        apply norm_nonneg
      Â· rw [mul_inv_cancel_leftâ‚€, mul_assoc, mul_comm (|R|â»Â¹ ^ n)]
        rwa [Ne, _root_.abs_eq_zero]
#align norm_cauchy_power_series_le norm_cauchyPowerSeries_le

theorem le_radius_cauchyPowerSeries (f : â„‚ â†’ E) (c : â„‚) (R : â„â‰¥0) :
    â†‘R â‰¤ (cauchyPowerSeries f c R).radius := by
  refine
    (cauchyPowerSeries f c R).le_radius_of_bound
      ((2 * Ï€)â»Â¹ * âˆ« Î¸ : â„ in (0)..2 * Ï€, â€–f (circleMap c R Î¸)â€–) fun n => ?_
  refine (mul_le_mul_of_nonneg_right (norm_cauchyPowerSeries_le _ _ _ _)
    (pow_nonneg R.coe_nonneg _)).trans ?_
  rw [_root_.abs_of_nonneg R.coe_nonneg]
  rcases eq_or_ne (R ^ n : â„) 0 with hR | hR
  Â· rw_mod_cast [hR, mul_zero]
    exact mul_nonneg (inv_nonneg.2 Real.two_pi_pos.le)
      (intervalIntegral.integral_nonneg Real.two_pi_pos.le fun _ _ => norm_nonneg _)
  Â· rw [inv_pow]
    have : (R:â„) ^ n â‰  0 := by norm_cast at hR âŠ¢
    rw [inv_mul_cancel_rightâ‚€ this]
#align le_radius_cauchy_power_series le_radius_cauchyPowerSeries

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R` multiplied
by `2Ï€I` converges to the integral `âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open disc
`Metric.ball c R`. -/
theorem hasSum_two_pi_I_cauchyPowerSeries_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
    (hf : CircleIntegrable f c R) (hw : abs w < R) :
    HasSum (fun n : â„• => âˆ® z in C(c, R), (w / (z - c)) ^ n â€¢ (z - c)â»Â¹ â€¢ f z)
      (âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) := by
  have hR : 0 < R := (Complex.abs.nonneg w).trans_lt hw
  have hwR : abs w / R âˆˆ Ico (0 : â„) 1 :=
    âŸ¨div_nonneg (Complex.abs.nonneg w) hR.le, (div_lt_one hR).2 hwâŸ©
  refine intervalIntegral.hasSum_integral_of_dominated_convergence
      (fun n Î¸ => â€–f (circleMap c R Î¸)â€– * (abs w / R) ^ n) (fun n => ?_) (fun n => ?_) ?_ ?_ ?_
  Â· simp only [deriv_circleMap]
    apply_rules [AEStronglyMeasurable.smul, hf.def'.1] <;> apply Measurable.aestronglyMeasurable
    -- Porting note: these were `measurability`
    Â· exact (measurable_circleMap 0 R).mul_const I
    Â· exact (((measurable_circleMap c R).sub measurable_const).const_div w).pow measurable_const
    Â· exact ((measurable_circleMap c R).sub measurable_const).inv
  Â· simp [norm_smul, abs_of_pos hR, mul_left_comm R, inv_mul_cancel_leftâ‚€ hR.ne', mul_comm â€–_â€–]
  Â· exact eventually_of_forall fun _ _ => (summable_geometric_of_lt_one hwR.1 hwR.2).mul_left _
  Â· simpa only [tsum_mul_left, tsum_geometric_of_lt_one hwR.1 hwR.2] using
      hf.norm.mul_continuousOn continuousOn_const
  Â· refine eventually_of_forall fun Î¸ _ => HasSum.const_smul _ ?_
    simp only [smul_smul]
    refine HasSum.smul_const ?_ _
    have : â€–w / (circleMap c R Î¸ - c)â€– < 1 := by simpa [abs_of_pos hR] using hwR.2
    convert (hasSum_geometric_of_norm_lt_one this).mul_right _ using 1
    simp [â† sub_sub, â† mul_inv, sub_mul, div_mul_cancelâ‚€ _ (circleMap_ne_center hR.ne')]
set_option linter.uppercaseLean3 false in
#align has_sum_two_pi_I_cauchy_power_series_integral hasSum_two_pi_I_cauchyPowerSeries_integral

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `Metric.ball c R`. -/
theorem hasSum_cauchyPowerSeries_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
    (hf : CircleIntegrable f c R) (hw : abs w < R) :
    HasSum (fun n => cauchyPowerSeries f c R n fun _ => w)
      ((2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) := by
  simp only [cauchyPowerSeries_apply]
  exact (hasSum_two_pi_I_cauchyPowerSeries_integral hf hw).const_smul _
#align has_sum_cauchy_power_series_integral hasSum_cauchyPowerSeries_integral

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `Metric.ball c R`. -/
theorem sum_cauchyPowerSeries_eq_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
    (hf : CircleIntegrable f c R) (hw : abs w < R) :
    (cauchyPowerSeries f c R).sum w = (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z :=
  (hasSum_cauchyPowerSeries_integral hf hw).tsum_eq
#align sum_cauchy_power_series_eq_integral sum_cauchyPowerSeries_eq_integral

/-- For any circle integrable function `f`, the power series `cauchyPowerSeries f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `Metric.ball c R`. -/
theorem hasFPowerSeriesOn_cauchy_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„â‰¥0}
    (hf : CircleIntegrable f c R) (hR : 0 < R) :
    HasFPowerSeriesOnBall (fun w => (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z)
      (cauchyPowerSeries f c R) c R :=
  { r_le := le_radius_cauchyPowerSeries _ _ _
    r_pos := ENNReal.coe_pos.2 hR
    hasSum := fun hy â†¦ hasSum_cauchyPowerSeries_integral hf <| by simpa using hy }
#align has_fpower_series_on_cauchy_integral hasFPowerSeriesOn_cauchy_integral

namespace circleIntegral

/-- Integral $\oint_{|z-c|=R} \frac{dz}{z-w} = 2Ï€i$ whenever $|w-c| < R$. -/
theorem integral_sub_inv_of_mem_ball {c w : â„‚} {R : â„} (hw : w âˆˆ ball c R) :
    (âˆ® z in C(c, R), (z - w)â»Â¹) = 2 * Ï€ * I := by
  have hR : 0 < R := dist_nonneg.trans_lt hw
  suffices H : HasSum (fun n : â„• => âˆ® z in C(c, R), ((w - c) / (z - c)) ^ n * (z - c)â»Â¹)
      (2 * Ï€ * I) by
    have A : CircleIntegrable (fun _ => (1 : â„‚)) c R := continuousOn_const.circleIntegrable'
    refine (H.unique ?_).symm
    simpa only [smul_eq_mul, mul_one, add_sub_cancel] using
      hasSum_two_pi_I_cauchyPowerSeries_integral A hw
  have H : âˆ€ n : â„•, n â‰  0 â†’ (âˆ® z in C(c, R), (z - c) ^ (-n - 1 : â„¤)) = 0 := by
    refine fun n hn => integral_sub_zpow_of_ne ?_ _ _ _; simpa
  have : (âˆ® z in C(c, R), ((w - c) / (z - c)) ^ 0 * (z - c)â»Â¹) = 2 * Ï€ * I := by simp [hR.ne']
  refine this â–¸ hasSum_single _ fun n hn => ?_
  simp only [div_eq_mul_inv, mul_pow, integral_const_mul, mul_assoc]
  rw [(integral_congr hR.le fun z hz => _).trans (H n hn), mul_zero]
  intro z _
  rw [â† pow_succ, â† zpow_natCast, inv_zpow, â† zpow_neg, Int.ofNat_succ, neg_add,
    sub_eq_add_neg _ (1 : â„¤)]
#align circle_integral.integral_sub_inv_of_mem_ball circleIntegral.integral_sub_inv_of_mem_ball

end circleIntegral
