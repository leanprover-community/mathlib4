/-
Copyright (c) 2022 Cuma KÃ¶kmen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Cuma KÃ¶kmen, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Constructions.Prod.Integral
import Mathlib.MeasureTheory.Integral.CircleIntegral

#align_import measure_theory.integral.torus_integral from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# Integral over a torus in `â„‚â¿`

In this file we define the integral of a function `f : â„‚â¿ â†’ E` over a torus
`{z : â„‚â¿ | âˆ€ i, z i âˆˆ Metric.sphere (c i) (R i)}`. In order to do this, we define
`torusMap (c : â„‚â¿) (R Î¸ : â„â¿)` to be the point in `â„‚â¿` given by $z_k=c_k+R_ke^{Î¸_ki}$,
where $i$ is the imaginary unit, then define `torusIntegral f c R` as the integral over
the cube $[0, (fun _ â†¦ 2Ï€)] = \{Î¸\|âˆ€ k, 0 â‰¤ Î¸_k â‰¤ 2Ï€\}$ of the Jacobian of the
`torusMap` multiplied by `f (torusMap c R Î¸)`.

We also define a predicate saying that `f âˆ˜ torusMap c R` is integrable on the cube
`[0, (fun _ â†¦ 2Ï€)]`.

## Main definitions

* `torusMap c R`: the generalized multidimensional exponential map from `â„â¿` to `â„‚â¿` that sends
  $Î¸=(Î¸_0,â€¦,Î¸_{n-1})$ to $z=(z_0,â€¦,z_{n-1})$, where $z_k= c_k + R_ke^{Î¸_k i}$;

* `TorusIntegrable f c R`: a function `f : â„‚â¿ â†’ E` is integrable over the generalized torus
  with center `c : â„‚â¿` and radius `R : â„â¿` if `f âˆ˜ torusMap c R` is integrable on the
  closed cube `Icc (0 : â„â¿) (fun _ â†¦ 2 * Ï€)`;

* `torusIntegral f c R`: the integral of a function `f : â„‚â¿ â†’ E` over a torus with
  center `c âˆˆ â„‚â¿` and radius `R âˆˆ â„â¿` defined as
  $\iiint_{[0, 2 * Ï€]} (âˆ_{k = 1}^{n} i R_k e^{Î¸_k * i}) â€¢ f (c + Re^{Î¸_k i})\,dÎ¸_0â€¦dÎ¸_{k-1}$.

## Main statements

* `torusIntegral_dim0`, `torusIntegral_dim1`, `torusIntegral_succ`: formulas for `torusIntegral`
  in cases of dimension `0`, `1`, and `n + 1`.

## Notations

- `â„â°`, `â„Â¹`, `â„â¿`, `â„â¿âºÂ¹`: local notation for `Fin 0 â†’ â„`, `Fin 1 â†’ â„`, `Fin n â†’ â„`, and
  `Fin (n + 1) â†’ â„`, respectively;
- `â„‚â°`, `â„‚Â¹`, `â„‚â¿`, `â„‚â¿âºÂ¹`: local notation for `Fin 0 â†’ â„‚`, `Fin 1 â†’ â„‚`, `Fin n â†’ â„‚`, and
  `Fin (n + 1) â†’ â„‚`, respectively;
- `âˆ¯ z in T(c, R), f z`: notation for `torusIntegral f c R`;
- `âˆ® z in C(c, R), f z`: notation for `circleIntegral f c R`, defined elsewhere;
- `âˆ k, f k`: notation for `Finset.prod`, defined elsewhere;
- `Ï€`: notation for `Real.pi`, defined elsewhere.

## Tags

integral, torus
-/


variable {n : â„•}
variable {E : Type*} [NormedAddCommGroup E]

noncomputable section

open Complex Set MeasureTheory Function Filter TopologicalSpace

open scoped Real

-- Porting note: notation copied from `./DivergenceTheorem`
local macro:arg t:term:max noWs "â¿âºÂ¹" : term => `(Fin (n + 1) â†’ $t)
local macro:arg t:term:max noWs "â¿" : term => `(Fin n â†’ $t)
local macro:arg t:term:max noWs "â°" : term => `(Fin 0 â†’ $t)
local macro:arg t:term:max noWs "Â¹" : term => `(Fin 1 â†’ $t)

/-!
### `torusMap`, a parametrization of a torus
-/

/-- The n dimensional exponential map $Î¸_i â†¦ c + R e^{Î¸_i*I}, Î¸ âˆˆ â„â¿$ representing
a torus in `â„‚â¿` with center `c âˆˆ â„‚â¿` and generalized radius `R âˆˆ â„â¿`, so we can adjust
it to every n axis. -/
def torusMap (c : â„‚â¿) (R : â„â¿) : â„â¿ â†’ â„‚â¿ := fun Î¸ i => c i + R i * exp (Î¸ i * I)
#align torus_map torusMap

theorem torusMap_sub_center (c : â„‚â¿) (R : â„â¿) (Î¸ : â„â¿) : torusMap c R Î¸ - c = torusMap 0 R Î¸ := by
  ext1 i; simp [torusMap]
#align torus_map_sub_center torusMap_sub_center

theorem torusMap_eq_center_iff {c : â„‚â¿} {R : â„â¿} {Î¸ : â„â¿} : torusMap c R Î¸ = c â†” R = 0 := by
  simp [funext_iff, torusMap, exp_ne_zero]
#align torus_map_eq_center_iff torusMap_eq_center_iff

@[simp]
theorem torusMap_zero_radius (c : â„‚â¿) : torusMap c 0 = const â„â¿ c :=
  funext fun _ â†¦ torusMap_eq_center_iff.2 rfl
#align torus_map_zero_radius torusMap_zero_radius

/-!
### Integrability of a function on a generalized torus
-/

/-- A function `f : â„‚â¿ â†’ E` is integrable on the generalized torus if the function
`f âˆ˜ torusMap c R Î¸` is integrable on `Icc (0 : â„â¿) (fun _ â†¦ 2 * Ï€)`. -/
def TorusIntegrable (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) : Prop :=
  IntegrableOn (fun Î¸ : â„â¿ => f (torusMap c R Î¸)) (Icc (0 : â„â¿) fun _ => 2 * Ï€) volume
#align torus_integrable TorusIntegrable

namespace TorusIntegrable

-- Porting note (#11215): TODO: restore notation; `neg`, `add` etc fail if I use notation here
variable {f g : (Fin n â†’ â„‚) â†’ E} {c : Fin n â†’ â„‚} {R : Fin n â†’ â„}

/-- Constant functions are torus integrable -/
theorem torusIntegrable_const (a : E) (c : â„‚â¿) (R : â„â¿) : TorusIntegrable (fun _ => a) c R := by
  simp [TorusIntegrable, measure_Icc_lt_top]
#align torus_integrable.torus_integrable_const TorusIntegrable.torusIntegrable_const

/-- If `f` is torus integrable then `-f` is torus integrable. -/
protected nonrec theorem neg (hf : TorusIntegrable f c R) : TorusIntegrable (-f) c R := hf.neg
#align torus_integrable.neg TorusIntegrable.neg

/-- If `f` and `g` are two torus integrable functions, then so is `f + g`. -/
protected nonrec theorem add (hf : TorusIntegrable f c R) (hg : TorusIntegrable g c R) :
    TorusIntegrable (f + g) c R :=
  hf.add hg
#align torus_integrable.add TorusIntegrable.add

/-- If `f` and `g` are two torus integrable functions, then so is `f - g`. -/
protected nonrec theorem sub (hf : TorusIntegrable f c R) (hg : TorusIntegrable g c R) :
    TorusIntegrable (f - g) c R :=
  hf.sub hg
#align torus_integrable.sub TorusIntegrable.sub

theorem torusIntegrable_zero_radius {f : â„‚â¿ â†’ E} {c : â„‚â¿} : TorusIntegrable f c 0 := by
  rw [TorusIntegrable, torusMap_zero_radius]
  apply torusIntegrable_const (f c) c 0
#align torus_integrable.torus_integrable_zero_radius TorusIntegrable.torusIntegrable_zero_radius

/-- The function given in the definition of `torusIntegral` is integrable. -/
theorem function_integrable [NormedSpace â„‚ E] (hf : TorusIntegrable f c R) :
    IntegrableOn (fun Î¸ : â„â¿ => (âˆ i, R i * exp (Î¸ i * I) * I : â„‚) â€¢ f (torusMap c R Î¸))
      (Icc (0 : â„â¿) fun _ => 2 * Ï€) volume := by
  refine (hf.norm.const_mul (âˆ i, |R i|)).mono' ?_ ?_
  Â· refine (Continuous.aestronglyMeasurable ?_).smul hf.1; continuity
  simp [norm_smul, map_prod]
#align torus_integrable.function_integrable TorusIntegrable.function_integrable

end TorusIntegrable

variable [NormedSpace â„‚ E] [CompleteSpace E] {f g : (Fin n â†’ â„‚) â†’ E} {c : Fin n â†’ â„‚} {R : Fin n â†’ â„}

/-- The integral over a generalized torus with center `c âˆˆ â„‚â¿` and radius `R âˆˆ â„â¿`, defined
as the `â€¢`-product of the derivative of `torusMap` and `f (torusMap c R Î¸)`-/
def torusIntegral (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) :=
  âˆ« Î¸ : â„â¿ in Icc (0 : â„â¿) fun _ => 2 * Ï€, (âˆ i, R i * exp (Î¸ i * I) * I : â„‚) â€¢ f (torusMap c R Î¸)
#align torus_integral torusIntegral

@[inherit_doc torusIntegral]
notation3"âˆ¯ "(...)" in ""T("c", "R")"", "r:(scoped f => torusIntegral f c R) => r

theorem torusIntegral_radius_zero (hn : n â‰  0) (f : â„‚â¿ â†’ E) (c : â„‚â¿) :
    (âˆ¯ x in T(c, 0), f x) = 0 := by
  simp only [torusIntegral, Pi.zero_apply, ofReal_zero, mul_zero, zero_mul, Fin.prod_const,
    zero_pow hn, zero_smul, integral_zero]
#align torus_integral_radius_zero torusIntegral_radius_zero

theorem torusIntegral_neg (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) :
    (âˆ¯ x in T(c, R), -f x) = -âˆ¯ x in T(c, R), f x := by simp [torusIntegral, integral_neg]
#align torus_integral_neg torusIntegral_neg

theorem torusIntegral_add (hf : TorusIntegrable f c R) (hg : TorusIntegrable g c R) :
    (âˆ¯ x in T(c, R), f x + g x) = (âˆ¯ x in T(c, R), f x) + âˆ¯ x in T(c, R), g x := by
  simpa only [torusIntegral, smul_add, Pi.add_apply] using
    integral_add hf.function_integrable hg.function_integrable
#align torus_integral_add torusIntegral_add

theorem torusIntegral_sub (hf : TorusIntegrable f c R) (hg : TorusIntegrable g c R) :
    (âˆ¯ x in T(c, R), f x - g x) = (âˆ¯ x in T(c, R), f x) - âˆ¯ x in T(c, R), g x := by
  simpa only [sub_eq_add_neg, â† torusIntegral_neg] using torusIntegral_add hf hg.neg
#align torus_integral_sub torusIntegral_sub

theorem torusIntegral_smul {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [SMulCommClass ğ•œ â„‚ E] (a : ğ•œ)
    (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) : (âˆ¯ x in T(c, R), a â€¢ f x) = a â€¢ âˆ¯ x in T(c, R), f x := by
  simp only [torusIntegral, integral_smul, â† smul_comm a (_ : â„‚) (_ : E)]
#align torus_integral_smul torusIntegral_smul

theorem torusIntegral_const_mul (a : â„‚) (f : â„‚â¿ â†’ â„‚) (c : â„‚â¿) (R : â„â¿) :
    (âˆ¯ x in T(c, R), a * f x) = a * âˆ¯ x in T(c, R), f x :=
  torusIntegral_smul a f c R
#align torus_integral_const_mul torusIntegral_const_mul

/-- If for all `Î¸ : â„â¿`, `â€–f (torusMap c R Î¸)â€–` is less than or equal to a constant `C : â„`, then
`â€–âˆ¯ x in T(c, R), f xâ€–` is less than or equal to `(2 * Ï€)^n * (âˆ i, |R i|) * C`-/
theorem norm_torusIntegral_le_of_norm_le_const {C : â„} (hf : âˆ€ Î¸, â€–f (torusMap c R Î¸)â€– â‰¤ C) :
    â€–âˆ¯ x in T(c, R), f xâ€– â‰¤ ((2 * Ï€) ^ (n : â„•) * âˆ i, |R i|) * C :=
  calc
    â€–âˆ¯ x in T(c, R), f xâ€– â‰¤ (âˆ i, |R i|) * C * (volume (Icc (0 : â„â¿) fun _ => 2 * Ï€)).toReal :=
      norm_setIntegral_le_of_norm_le_const' measure_Icc_lt_top measurableSet_Icc fun Î¸ _ =>
        calc
          â€–(âˆ i : Fin n, R i * exp (Î¸ i * I) * I : â„‚) â€¢ f (torusMap c R Î¸)â€– =
              (âˆ i : Fin n, |R i|) * â€–f (torusMap c R Î¸)â€– := by simp [norm_smul]
          _ â‰¤ (âˆ i : Fin n, |R i|) * C := mul_le_mul_of_nonneg_left (hf _) <| by positivity
    _ = ((2 * Ï€) ^ (n : â„•) * âˆ i, |R i|) * C := by
      simp only [Pi.zero_def, Real.volume_Icc_pi_toReal fun _ => Real.two_pi_pos.le, sub_zero,
        Fin.prod_const, mul_assoc, mul_comm ((2 * Ï€) ^ (n : â„•))]
#align norm_torus_integral_le_of_norm_le_const norm_torusIntegral_le_of_norm_le_const

@[simp]
theorem torusIntegral_dim0 (f : â„‚â° â†’ E) (c : â„‚â°) (R : â„â°) : (âˆ¯ x in T(c, R), f x) = f c := by
  simp only [torusIntegral, Fin.prod_univ_zero, one_smul,
    Subsingleton.elim (fun _ : Fin 0 => 2 * Ï€) 0, Icc_self, Measure.restrict_singleton, volume_pi,
    integral_smul_measure, integral_dirac, Measure.pi_of_empty (fun _ : Fin 0 â†¦ volume) 0,
    Measure.dirac_apply_of_mem (mem_singleton _), Subsingleton.elim (torusMap c R 0) c]
#align torus_integral_dim0 torusIntegral_dim0

/-- In dimension one, `torusIntegral` is the same as `circleIntegral`
(up to the natural equivalence between `â„‚` and `Fin 1 â†’ â„‚`). -/
theorem torusIntegral_dim1 (f : â„‚Â¹ â†’ E) (c : â„‚Â¹) (R : â„Â¹) :
    (âˆ¯ x in T(c, R), f x) = âˆ® z in C(c 0, R 0), f fun _ => z := by
  have Hâ‚ : (((MeasurableEquiv.funUnique _ _).symm) â»Â¹' Icc 0 fun _ => 2 * Ï€) = Icc 0 (2 * Ï€) :=
    (OrderIso.funUnique (Fin 1) â„).symm.preimage_Icc _ _
  have Hâ‚‚ : torusMap c R = fun Î¸ _ â†¦ circleMap (c 0) (R 0) (Î¸ 0) := by
    ext Î¸ i : 2
    rw [Subsingleton.elim i 0]; rfl
  rw [torusIntegral, circleIntegral, intervalIntegral.integral_of_le Real.two_pi_pos.le,
    Measure.restrict_congr_set Ioc_ae_eq_Icc,
    â† ((volume_preserving_funUnique (Fin 1) â„).symm _).setIntegral_preimage_emb
      (MeasurableEquiv.measurableEmbedding _), Hâ‚, Hâ‚‚]
  simp [circleMap_zero]
#align torus_integral_dim1 torusIntegral_dim1

/-- Recurrent formula for `torusIntegral`, see also `torusIntegral_succ`. -/
theorem torusIntegral_succAbove {f : â„‚â¿âºÂ¹ â†’ E} {c : â„‚â¿âºÂ¹} {R : â„â¿âºÂ¹} (hf : TorusIntegrable f c R)
    (i : Fin (n + 1)) :
    (âˆ¯ x in T(c, R), f x) =
      âˆ® x in C(c i, R i), âˆ¯ y in T(c âˆ˜ i.succAbove, R âˆ˜ i.succAbove), f (i.insertNth x y) := by
  set e : â„ Ã— â„â¿ â‰ƒáµ â„â¿âºÂ¹ := (MeasurableEquiv.piFinSuccAbove (fun _ => â„) i).symm
  have hem : MeasurePreserving e :=
    (volume_preserving_piFinSuccAbove (fun _ : Fin (n + 1) => â„) i).symm _
  have heÏ€ : (e â»Â¹' Icc 0 fun _ => 2 * Ï€) = Icc 0 (2 * Ï€) Ã—Ë¢ Icc (0 : â„â¿) fun _ => 2 * Ï€ :=
    ((OrderIso.piFinSuccAboveIso (fun _ => â„) i).symm.preimage_Icc _ _).trans (Icc_prod_eq _ _)
  rw [torusIntegral, â† hem.map_eq, setIntegral_map_equiv, heÏ€, Measure.volume_eq_prod,
    setIntegral_prod, circleIntegral_def_Icc]
  Â· refine setIntegral_congr measurableSet_Icc fun Î¸ _ => ?_
    simp (config := { unfoldPartialApp := true }) only [e, torusIntegral, â† integral_smul,
      deriv_circleMap, i.prod_univ_succAbove _, smul_smul, torusMap, circleMap_zero]
    refine setIntegral_congr measurableSet_Icc fun Î˜ _ => ?_
    simp only [MeasurableEquiv.piFinSuccAbove_symm_apply, i.insertNth_apply_same,
      i.insertNth_apply_succAbove, (Â· âˆ˜ Â·)]
    congr 2
    simp only [funext_iff, i.forall_iff_succAbove, circleMap, Fin.insertNth_apply_same,
      eq_self_iff_true, Fin.insertNth_apply_succAbove, imp_true_iff, and_self_iff]
  Â· have := hf.function_integrable
    rwa [â† hem.integrableOn_comp_preimage e.measurableEmbedding, heÏ€] at this
#align torus_integral_succ_above torusIntegral_succAbove

/-- Recurrent formula for `torusIntegral`, see also `torusIntegral_succAbove`. -/
theorem torusIntegral_succ {f : â„‚â¿âºÂ¹ â†’ E} {c : â„‚â¿âºÂ¹} {R : â„â¿âºÂ¹} (hf : TorusIntegrable f c R) :
    (âˆ¯ x in T(c, R), f x) =
      âˆ® x in C(c 0, R 0), âˆ¯ y in T(c âˆ˜ Fin.succ, R âˆ˜ Fin.succ), f (Fin.cons x y) := by
  simpa using torusIntegral_succAbove hf 0
#align torus_integral_succ torusIntegral_succ
