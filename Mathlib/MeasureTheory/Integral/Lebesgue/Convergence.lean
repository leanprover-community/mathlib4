/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Johannes HÃ¶lzl
-/
import Mathlib.MeasureTheory.Integral.Lebesgue.Basic

/-!
# Monotone convergence, dominated convergence and Fatou's lemma

Informally, the monotone convergence theorem (aka Beppo Levi lemma) states that the
Lebesgue integral and supremum can be swapped for a pointwise monotone sequence of functions.
It is a key result in measure theory, and two other important theorems can be deduced from it:
* (Lebesgue's) dominated convergence theorem replaces "monotone" with
  "dominated by a fixed function".
* Fatou's lemma replaces "supremum" with "limit inferior" and does not require monotonicity.
This file collects several variants of these three theorems. Note however that
`MeasureTheory.lintegral_iSup` is in `Mathlib.MeasureTheory.Integral.Lebesgue.Basic`, not here,
because it is needed to prove (among other things) that the Lebesgue integral is additive.
-/

open Set Filter ENNReal Topology

namespace MeasureTheory

variable {Î± Î² : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±}

/-- **Monotone convergence theorem**, version with `AEMeasurable` functions. -/
theorem lintegral_iSup' {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_mono : âˆ€áµ x âˆ‚Î¼, Monotone fun n => f n x) : âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by
  simp_rw [â† iSup_apply]
  let p : Î± â†’ (â„• â†’ â„â‰¥0âˆ) â†’ Prop := fun _ f' => Monotone f'
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun i => f i x := h_mono
  have h_ae_seq_mono : Monotone (aeSeq hf p) := by
    intro n m hnm x
    by_cases hx : x âˆˆ aeSeqSet hf p
    Â· exact aeSeq.prop_of_mem_aeSeqSet hf hx hnm
    Â· simp only [aeSeq, hx, if_false, le_rfl]
  rw [lintegral_congr_ae (aeSeq.iSup hf hp).symm]
  simp_rw [iSup_apply]
  rw [lintegral_iSup (aeSeq.measurable hf p) h_ae_seq_mono]
  congr with n
  exact lintegral_congr_ae (aeSeq.aeSeq_n_eq_fun_n_ae hf hp n)

/-- **Monotone convergence theorem** expressed with limits. -/
theorem lintegral_tendsto_of_tendsto_of_monotone {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ n, AEMeasurable (f n) Î¼) (h_mono : âˆ€áµ x âˆ‚Î¼, Monotone fun n => f n x)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ <| F x)) :
    Tendsto (fun n => âˆ«â» x, f n x âˆ‚Î¼) atTop (ğ“ <| âˆ«â» x, F x âˆ‚Î¼) := by
  have : Monotone fun n => âˆ«â» x, f n x âˆ‚Î¼ := fun i j hij =>
    lintegral_mono_ae (h_mono.mono fun x hx => hx hij)
  suffices key : âˆ«â» x, F x âˆ‚Î¼ = â¨† n, âˆ«â» x, f n x âˆ‚Î¼ by
    rw [key]
    exact tendsto_atTop_iSup this
  rw [â† lintegral_iSup' hf h_mono]
  refine lintegral_congr_ae ?_
  filter_upwards [h_mono, h_tendsto] with _ hx_mono hx_tendsto using
    tendsto_nhds_unique hx_tendsto (tendsto_atTop_iSup hx_mono)

/-- Weaker version of the **monotone convergence theorem**. -/
theorem lintegral_iSup_ae {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ n, Measurable (f n))
    (h_mono : âˆ€ n, âˆ€áµ a âˆ‚Î¼, f n a â‰¤ f n.succ a) : âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by
  classical
  let âŸ¨s, hsâŸ© := exists_measurable_superset_of_null (ae_iff.1 (ae_all_iff.2 h_mono))
  let g n a := if a âˆˆ s then 0 else f n a
  have g_eq_f : âˆ€áµ a âˆ‚Î¼, âˆ€ n, g n a = f n a :=
    (measure_zero_iff_ae_nmem.1 hs.2.2).mono fun a ha n => if_neg ha
  calc
    âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = âˆ«â» a, â¨† n, g n a âˆ‚Î¼ :=
      lintegral_congr_ae <| g_eq_f.mono fun a ha => by simp only [ha]
    _ = â¨† n, âˆ«â» a, g n a âˆ‚Î¼ :=
      (lintegral_iSup (fun n => measurable_const.piecewise hs.2.1 (hf n))
        (monotone_nat_of_le_succ fun n a => ?_))
    _ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by simp only [lintegral_congr_ae (g_eq_f.mono fun _a ha => ha _)]
  simp only [g]
  split_ifs with h
  Â· rfl
  Â· have := Set.not_mem_subset hs.1 h
    simp only [not_forall, not_le, mem_setOf_eq, not_exists, not_lt] at this
    exact this n

/-- **Monotone convergence theorem** for nonincreasing sequences of functions. -/
theorem lintegral_iInf_ae {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, Measurable (f n))
    (h_mono : âˆ€ n : â„•, f n.succ â‰¤áµ[Î¼] f n) (h_fin : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) :
    âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = â¨… n, âˆ«â» a, f n a âˆ‚Î¼ :=
  have fn_le_f0 : âˆ«â» a, â¨… n, f n a âˆ‚Î¼ â‰¤ âˆ«â» a, f 0 a âˆ‚Î¼ :=
    lintegral_mono fun _ => iInf_le_of_le 0 le_rfl
  have fn_le_f0' : â¨… n, âˆ«â» a, f n a âˆ‚Î¼ â‰¤ âˆ«â» a, f 0 a âˆ‚Î¼ := iInf_le_of_le 0 le_rfl
  (ENNReal.sub_right_inj h_fin fn_le_f0 fn_le_f0').1 <|
    show âˆ«â» a, f 0 a âˆ‚Î¼ - âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = âˆ«â» a, f 0 a âˆ‚Î¼ - â¨… n, âˆ«â» a, f n a âˆ‚Î¼ from
      calc
        âˆ«â» a, f 0 a âˆ‚Î¼ - âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = âˆ«â» a, f 0 a - â¨… n, f n a âˆ‚Î¼ :=
          (lintegral_sub (.iInf h_meas)
              (ne_top_of_le_ne_top h_fin <| lintegral_mono fun _ => iInf_le _ _)
              (ae_of_all _ fun _ => iInf_le _ _)).symm
        _ = âˆ«â» a, â¨† n, f 0 a - f n a âˆ‚Î¼ := congr rfl (funext fun _ => ENNReal.sub_iInf)
        _ = â¨† n, âˆ«â» a, f 0 a - f n a âˆ‚Î¼ :=
          (lintegral_iSup_ae (fun n => (h_meas 0).sub (h_meas n)) fun n =>
            (h_mono n).mono fun _ ha => tsub_le_tsub le_rfl ha)
        _ = â¨† n, âˆ«â» a, f 0 a âˆ‚Î¼ - âˆ«â» a, f n a âˆ‚Î¼ :=
          (have h_mono : âˆ€áµ a âˆ‚Î¼, âˆ€ n : â„•, f n.succ a â‰¤ f n a := ae_all_iff.2 h_mono
          have h_mono : âˆ€ n, âˆ€áµ a âˆ‚Î¼, f n a â‰¤ f 0 a := fun n =>
            h_mono.mono fun a h => by
              induction' n with n ih
              Â· exact le_rfl
              Â· exact le_trans (h n) ih
          congr_arg iSup <|
            funext fun n =>
              lintegral_sub (h_meas _) (ne_top_of_le_ne_top h_fin <| lintegral_mono_ae <| h_mono n)
                (h_mono n))
        _ = âˆ«â» a, f 0 a âˆ‚Î¼ - â¨… n, âˆ«â» a, f n a âˆ‚Î¼ := ENNReal.sub_iInf.symm

/-- **Monotone convergence theorem** for nonincreasing sequences of functions. -/
theorem lintegral_iInf {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, Measurable (f n)) (h_anti : Antitone f)
    (h_fin : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) : âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = â¨… n, âˆ«â» a, f n a âˆ‚Î¼ :=
  lintegral_iInf_ae h_meas (fun n => ae_of_all _ <| h_anti n.le_succ) h_fin

theorem lintegral_iInf' {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_anti : âˆ€áµ a âˆ‚Î¼, Antitone (fun i â†¦ f i a)) (h_fin : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) :
    âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = â¨… n, âˆ«â» a, f n a âˆ‚Î¼ := by
  simp_rw [â† iInf_apply]
  let p : Î± â†’ (â„• â†’ â„â‰¥0âˆ) â†’ Prop := fun _ f' => Antitone f'
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun i => f i x := h_anti
  have h_ae_seq_mono : Antitone (aeSeq h_meas p) := by
    intro n m hnm x
    by_cases hx : x âˆˆ aeSeqSet h_meas p
    Â· exact aeSeq.prop_of_mem_aeSeqSet h_meas hx hnm
    Â· simp only [aeSeq, hx, if_false]
      exact le_rfl
  rw [lintegral_congr_ae (aeSeq.iInf h_meas hp).symm]
  simp_rw [iInf_apply]
  rw [lintegral_iInf (aeSeq.measurable h_meas p) h_ae_seq_mono]
  Â· congr
    exact funext fun n â†¦ lintegral_congr_ae (aeSeq.aeSeq_n_eq_fun_n_ae h_meas hp n)
  Â· rwa [lintegral_congr_ae (aeSeq.aeSeq_n_eq_fun_n_ae h_meas hp 0)]

/-- **Monotone convergence theorem** for an infimum over a directed family and indexed by a
countable type. -/
theorem lintegral_iInf_directed_of_measurable [Countable Î²]
    {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} {Î¼ : Measure Î±} (hÎ¼ : Î¼ â‰  0) (hf : âˆ€ b, Measurable (f b))
    (hf_int : âˆ€ b, âˆ«â» a, f b a âˆ‚Î¼ â‰  âˆ) (h_directed : Directed (Â· â‰¥ Â·) f) :
    âˆ«â» a, â¨… b, f b a âˆ‚Î¼ = â¨… b, âˆ«â» a, f b a âˆ‚Î¼ := by
  cases nonempty_encodable Î²
  cases isEmpty_or_nonempty Î²
  Â· simp only [iInf_of_empty, lintegral_const,
      ENNReal.top_mul (Measure.measure_univ_ne_zero.mpr hÎ¼)]
  inhabit Î²
  have : âˆ€ a, â¨… b, f b a = â¨… n, f (h_directed.sequence f n) a := by
    refine fun a =>
      le_antisymm (le_iInf fun n => iInf_le _ _)
        (le_iInf fun b => iInf_le_of_le (Encodable.encode b + 1) ?_)
    exact h_directed.sequence_le b a
  calc
    âˆ«â» a, â¨… b, f b a âˆ‚Î¼
    _ = âˆ«â» a, â¨… n, (f âˆ˜ h_directed.sequence f) n a âˆ‚Î¼ := by simp only [this, Function.comp_apply]
    _ = â¨… n, âˆ«â» a, (f âˆ˜ h_directed.sequence f) n a âˆ‚Î¼ := by
      rw [lintegral_iInf ?_ h_directed.sequence_anti]
      Â· exact hf_int _
      Â· exact fun n => hf _
    _ = â¨… b, âˆ«â» a, f b a âˆ‚Î¼ := by
      refine le_antisymm (le_iInf fun b => ?_) (le_iInf fun n => ?_)
      Â· exact iInf_le_of_le (Encodable.encode b + 1) (lintegral_mono <| h_directed.sequence_le b)
      Â· exact iInf_le (fun b => âˆ«â» a, f b a âˆ‚Î¼) _

/-- **Fatou's lemma**, version with `AEMeasurable` functions. -/
theorem lintegral_liminf_le' {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, AEMeasurable (f n) Î¼) :
    âˆ«â» a, liminf (fun n => f n a) atTop âˆ‚Î¼ â‰¤ liminf (fun n => âˆ«â» a, f n a âˆ‚Î¼) atTop :=
  calc
    âˆ«â» a, liminf (fun n => f n a) atTop âˆ‚Î¼ = âˆ«â» a, â¨† n : â„•, â¨… i â‰¥ n, f i a âˆ‚Î¼ := by
      simp only [liminf_eq_iSup_iInf_of_nat]
    _ = â¨† n : â„•, âˆ«â» a, â¨… i â‰¥ n, f i a âˆ‚Î¼ :=
      (lintegral_iSup' (fun _ => .biInf _ (to_countable _) (fun i _ â†¦ h_meas i))
        (ae_of_all Î¼ fun _ _ _ hnm => iInf_le_iInf_of_subset fun _ hi => le_trans hnm hi))
    _ â‰¤ â¨† n : â„•, â¨… i â‰¥ n, âˆ«â» a, f i a âˆ‚Î¼ := iSup_mono fun _ => le_iInfâ‚‚_lintegral _
    _ = atTop.liminf fun n => âˆ«â» a, f n a âˆ‚Î¼ := Filter.liminf_eq_iSup_iInf_of_nat.symm

/-- **Fatou's lemma**, version with `Measurable` functions. -/
theorem lintegral_liminf_le {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, Measurable (f n)) :
    âˆ«â» a, liminf (fun n => f n a) atTop âˆ‚Î¼ â‰¤ liminf (fun n => âˆ«â» a, f n a âˆ‚Î¼) atTop :=
  lintegral_liminf_le' fun n => (h_meas n).aemeasurable

theorem limsup_lintegral_le {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (g : Î± â†’ â„â‰¥0âˆ) (hf_meas : âˆ€ n, Measurable (f n))
    (h_bound : âˆ€ n, f n â‰¤áµ[Î¼] g) (h_fin : âˆ«â» a, g a âˆ‚Î¼ â‰  âˆ) :
    limsup (fun n => âˆ«â» a, f n a âˆ‚Î¼) atTop â‰¤ âˆ«â» a, limsup (fun n => f n a) atTop âˆ‚Î¼ :=
  calc
    limsup (fun n => âˆ«â» a, f n a âˆ‚Î¼) atTop = â¨… n : â„•, â¨† i â‰¥ n, âˆ«â» a, f i a âˆ‚Î¼ :=
      limsup_eq_iInf_iSup_of_nat
    _ â‰¤ â¨… n : â„•, âˆ«â» a, â¨† i â‰¥ n, f i a âˆ‚Î¼ := iInf_mono fun _ => iSupâ‚‚_lintegral_le _
    _ = âˆ«â» a, â¨… n : â„•, â¨† i â‰¥ n, f i a âˆ‚Î¼ := by
      refine (lintegral_iInf ?_ ?_ ?_).symm
      Â· intro n
        exact .biSup _ (to_countable _) (fun i _ â†¦ hf_meas i)
      Â· intro n m hnm a
        exact iSup_le_iSup_of_subset fun i hi => le_trans hnm hi
      Â· refine ne_top_of_le_ne_top h_fin (lintegral_mono_ae ?_)
        refine (ae_all_iff.2 h_bound).mono fun n hn => ?_
        exact iSup_le fun i => iSup_le fun _ => hn i
    _ = âˆ«â» a, limsup (fun n => f n a) atTop âˆ‚Î¼ := by simp only [limsup_eq_iInf_iSup_of_nat]

/-- **Dominated convergence theorem** for nonnegative `Measurable` functions. -/
theorem tendsto_lintegral_of_dominated_convergence {F : â„• â†’ Î± â†’ â„â‰¥0âˆ} {f : Î± â†’ â„â‰¥0âˆ}
    (bound : Î± â†’ â„â‰¥0âˆ) (hF_meas : âˆ€ n, Measurable (F n)) (h_bound : âˆ€ n, F n â‰¤áµ[Î¼] bound)
    (h_fin : âˆ«â» a, bound a âˆ‚Î¼ â‰  âˆ) (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => F n a) atTop (ğ“ (f a))) :
    Tendsto (fun n => âˆ«â» a, F n a âˆ‚Î¼) atTop (ğ“ (âˆ«â» a, f a âˆ‚Î¼)) :=
  tendsto_of_le_liminf_of_limsup_le
    (calc
      âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, liminf (fun n : â„• => F n a) atTop âˆ‚Î¼ :=
        lintegral_congr_ae <| h_lim.mono fun _ h => h.liminf_eq.symm
      _ â‰¤ liminf (fun n => âˆ«â» a, F n a âˆ‚Î¼) atTop := lintegral_liminf_le hF_meas)
    (calc
      limsup (fun n : â„• => âˆ«â» a, F n a âˆ‚Î¼) atTop â‰¤ âˆ«â» a, limsup (fun n => F n a) atTop âˆ‚Î¼ :=
        limsup_lintegral_le _ hF_meas h_bound h_fin
      _ = âˆ«â» a, f a âˆ‚Î¼ := lintegral_congr_ae <| h_lim.mono fun _ h => h.limsup_eq)

/-- **Dominated convergence theorem** for nonnegative `AEMeasurable` functions. -/
theorem tendsto_lintegral_of_dominated_convergence' {F : â„• â†’ Î± â†’ â„â‰¥0âˆ} {f : Î± â†’ â„â‰¥0âˆ}
    (bound : Î± â†’ â„â‰¥0âˆ) (hF_meas : âˆ€ n, AEMeasurable (F n) Î¼) (h_bound : âˆ€ n, F n â‰¤áµ[Î¼] bound)
    (h_fin : âˆ«â» a, bound a âˆ‚Î¼ â‰  âˆ) (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => F n a) atTop (ğ“ (f a))) :
    Tendsto (fun n => âˆ«â» a, F n a âˆ‚Î¼) atTop (ğ“ (âˆ«â» a, f a âˆ‚Î¼)) := by
  have : âˆ€ n, âˆ«â» a, F n a âˆ‚Î¼ = âˆ«â» a, (hF_meas n).mk (F n) a âˆ‚Î¼ := fun n =>
    lintegral_congr_ae (hF_meas n).ae_eq_mk
  simp_rw [this]
  apply
    tendsto_lintegral_of_dominated_convergence bound (fun n => (hF_meas n).measurable_mk) _ h_fin
  Â· have : âˆ€ n, âˆ€áµ a âˆ‚Î¼, (hF_meas n).mk (F n) a = F n a := fun n => (hF_meas n).ae_eq_mk.symm
    have : âˆ€áµ a âˆ‚Î¼, âˆ€ n, (hF_meas n).mk (F n) a = F n a := ae_all_iff.mpr this
    filter_upwards [this, h_lim] with a H H'
    simp_rw [H]
    exact H'
  Â· intro n
    filter_upwards [h_bound n, (hF_meas n).ae_eq_mk] with a H H'
    rwa [H'] at H

/-- **Dominated convergence theorem** for filters with a countable basis. -/
theorem tendsto_lintegral_filter_of_dominated_convergence {Î¹} {l : Filter Î¹}
    [l.IsCountablyGenerated] {F : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} {f : Î± â†’ â„â‰¥0âˆ} (bound : Î± â†’ â„â‰¥0âˆ)
    (hF_meas : âˆ€á¶  n in l, Measurable (F n)) (h_bound : âˆ€á¶  n in l, âˆ€áµ a âˆ‚Î¼, F n a â‰¤ bound a)
    (h_fin : âˆ«â» a, bound a âˆ‚Î¼ â‰  âˆ) (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => F n a) l (ğ“ (f a))) :
    Tendsto (fun n => âˆ«â» a, F n a âˆ‚Î¼) l (ğ“ <| âˆ«â» a, f a âˆ‚Î¼) := by
  rw [tendsto_iff_seq_tendsto]
  intro x xl
  have hxl := by
    rw [tendsto_atTop'] at xl
    exact xl
  have h := inter_mem hF_meas h_bound
  replace h := hxl _ h
  rcases h with âŸ¨k, hâŸ©
  rw [â† tendsto_add_atTop_iff_nat k]
  refine tendsto_lintegral_of_dominated_convergence ?_ ?_ ?_ ?_ ?_
  Â· exact bound
  Â· intro
    refine (h _ ?_).1
    exact Nat.le_add_left _ _
  Â· intro
    refine (h _ ?_).2
    exact Nat.le_add_left _ _
  Â· assumption
  Â· refine h_lim.mono fun a h_lim => ?_
    apply @Tendsto.comp _ _ _ (fun n => x (n + k)) fun n => F n a
    Â· assumption
    rw [tendsto_add_atTop_iff_nat]
    assumption

theorem lintegral_tendsto_of_tendsto_of_antitone {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ n, AEMeasurable (f n) Î¼) (h_anti : âˆ€áµ x âˆ‚Î¼, Antitone fun n â†¦ f n x)
    (h0 : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (F x))) :
    Tendsto (fun n â†¦ âˆ«â» x, f n x âˆ‚Î¼) atTop (ğ“ (âˆ«â» x, F x âˆ‚Î¼)) := by
  have : Antitone fun n â†¦ âˆ«â» x, f n x âˆ‚Î¼ := fun i j hij â†¦
    lintegral_mono_ae (h_anti.mono fun x hx â†¦ hx hij)
  suffices key : âˆ«â» x, F x âˆ‚Î¼ = â¨… n, âˆ«â» x, f n x âˆ‚Î¼ by
    rw [key]
    exact tendsto_atTop_iInf this
  rw [â† lintegral_iInf' hf h_anti h0]
  refine lintegral_congr_ae ?_
  filter_upwards [h_anti, h_tendsto] with _ hx_anti hx_tendsto
    using tendsto_nhds_unique hx_tendsto (tendsto_atTop_iInf hx_anti)

open Encodable in
/-- **Monotone convergence theorem** for a supremum over a directed family and indexed by a
countable type. -/
theorem lintegral_iSup_directed_of_measurable [Countable Î²] {f : Î² â†’ Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ b, Measurable (f b)) (h_directed : Directed (Â· â‰¤ Â·) f) :
    âˆ«â» a, â¨† b, f b a âˆ‚Î¼ = â¨† b, âˆ«â» a, f b a âˆ‚Î¼ := by
  cases nonempty_encodable Î²
  cases isEmpty_or_nonempty Î²
  Â· simp [iSup_of_empty]
  inhabit Î²
  have : âˆ€ a, â¨† b, f b a = â¨† n, f (h_directed.sequence f n) a := by
    intro a
    refine le_antisymm (iSup_le fun b => ?_) (iSup_le fun n => le_iSup (fun n => f n a) _)
    exact le_iSup_of_le (encode b + 1) (h_directed.le_sequence b a)
  calc
    âˆ«â» a, â¨† b, f b a âˆ‚Î¼ = âˆ«â» a, â¨† n, f (h_directed.sequence f n) a âˆ‚Î¼ := by simp only [this]
    _ = â¨† n, âˆ«â» a, f (h_directed.sequence f n) a âˆ‚Î¼ :=
      (lintegral_iSup (fun n => hf _) h_directed.sequence_mono)
    _ = â¨† b, âˆ«â» a, f b a âˆ‚Î¼ := by
      refine le_antisymm (iSup_le fun n => ?_) (iSup_le fun b => ?_)
      Â· exact le_iSup (fun b => âˆ«â» a, f b a âˆ‚Î¼) _
      Â· exact le_iSup_of_le (encode b + 1) (lintegral_mono <| h_directed.le_sequence b)

/-- **Monotone convergence theorem** for a supremum over a directed family and indexed by a
countable type. -/
theorem lintegral_iSup_directed [Countable Î²] {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ b, AEMeasurable (f b) Î¼)
    (h_directed : Directed (Â· â‰¤ Â·) f) : âˆ«â» a, â¨† b, f b a âˆ‚Î¼ = â¨† b, âˆ«â» a, f b a âˆ‚Î¼ := by
  simp_rw [â† iSup_apply]
  let p : Î± â†’ (Î² â†’ ENNReal) â†’ Prop := fun x f' => Directed LE.le f'
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun i => f i x := by
    filter_upwards [] with x i j
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h_directed i j
    exact âŸ¨z, hzâ‚ x, hzâ‚‚ xâŸ©
  have h_ae_seq_directed : Directed LE.le (aeSeq hf p) := by
    intro bâ‚ bâ‚‚
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h_directed bâ‚ bâ‚‚
    refine âŸ¨z, ?_, ?_âŸ© <;>
      Â· intro x
        by_cases hx : x âˆˆ aeSeqSet hf p
        Â· repeat rw [aeSeq.aeSeq_eq_fun_of_mem_aeSeqSet hf hx]
          apply_rules [hzâ‚, hzâ‚‚]
        Â· simp only [aeSeq, hx, if_false]
          exact le_rfl
  convert lintegral_iSup_directed_of_measurable (aeSeq.measurable hf p) h_ae_seq_directed using 1
  Â· simp_rw [â† iSup_apply]
    rw [lintegral_congr_ae (aeSeq.iSup hf hp).symm]
  Â· congr 1
    ext1 b
    rw [lintegral_congr_ae]
    apply EventuallyEq.symm
    exact aeSeq.aeSeq_n_eq_fun_n_ae hf hp _

theorem lintegral_tsum [Countable Î²] {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ i, AEMeasurable (f i) Î¼) :
    âˆ«â» a, âˆ‘' i, f i a âˆ‚Î¼ = âˆ‘' i, âˆ«â» a, f i a âˆ‚Î¼ := by
  classical
  simp only [ENNReal.tsum_eq_iSup_sum]
  rw [lintegral_iSup_directed]
  Â· simp [lintegral_finset_sum' _ fun i _ => hf i]
  Â· intro b
    exact Finset.aemeasurable_sum _ fun i _ => hf i
  Â· intro s t
    use s âˆª t
    constructor
    Â· exact fun a => Finset.sum_le_sum_of_subset Finset.subset_union_left
    Â· exact fun a => Finset.sum_le_sum_of_subset Finset.subset_union_right

/-- If a monotone sequence of functions has an upper bound and the sequence of integrals of these
functions tends to the integral of the upper bound, then the sequence of functions converges
almost everywhere to the upper bound. Auxiliary version assuming moreover that the
functions in the sequence are ae measurable. -/
lemma tendsto_of_lintegral_tendsto_of_monotone_aux {Î± : Type*} {mÎ± : MeasurableSpace Î±}
    {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ} {Î¼ : Measure Î±}
    (hf_meas : âˆ€ n, AEMeasurable (f n) Î¼) (hF_meas : AEMeasurable F Î¼)
    (hf_tendsto : Tendsto (fun i â†¦ âˆ«â» a, f i a âˆ‚Î¼) atTop (ğ“ (âˆ«â» a, F a âˆ‚Î¼)))
    (hf_mono : âˆ€áµ a âˆ‚Î¼, Monotone (fun i â†¦ f i a))
    (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ i, f i a â‰¤ F a) (h_int_finite : âˆ«â» a, F a âˆ‚Î¼ â‰  âˆ) :
    âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F a)) := by
  have h_bound_finite : âˆ€áµ a âˆ‚Î¼, F a â‰  âˆ := by
    filter_upwards [ae_lt_top' hF_meas h_int_finite] with a ha using ha.ne
  have h_exists : âˆ€áµ a âˆ‚Î¼, âˆƒ l, Tendsto (fun i â†¦ f i a) atTop (ğ“ l) := by
    filter_upwards [h_bound, h_bound_finite, hf_mono] with a h_le h_fin h_mono
    have h_tendsto : Tendsto (fun i â†¦ f i a) atTop atTop âˆ¨
        âˆƒ l, Tendsto (fun i â†¦ f i a) atTop (ğ“ l) := tendsto_of_monotone h_mono
    rcases h_tendsto with h_absurd | h_tendsto
    Â· rw [tendsto_atTop_atTop_iff_of_monotone h_mono] at h_absurd
      obtain âŸ¨i, hiâŸ© := h_absurd (F a + 1)
      refine absurd (hi.trans (h_le _)) (not_le.mpr ?_)
      exact ENNReal.lt_add_right h_fin one_ne_zero
    Â· exact h_tendsto
  classical
  let F' : Î± â†’ â„â‰¥0âˆ := fun a â†¦ if h : âˆƒ l, Tendsto (fun i â†¦ f i a) atTop (ğ“ l)
    then h.choose else âˆ
  have hF'_tendsto : âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F' a)) := by
    filter_upwards [h_exists] with a ha
    simp_rw [F', dif_pos ha]
    exact ha.choose_spec
  suffices F' =áµ[Î¼] F by
    filter_upwards [this, hF'_tendsto] with a h_eq h_tendsto using h_eq â–¸ h_tendsto
  have hF'_le : F' â‰¤áµ[Î¼] F := by
    filter_upwards [h_bound, hF'_tendsto] with a h_le h_tendsto
    exact le_of_tendsto' h_tendsto (fun m â†¦ h_le _)
  suffices âˆ«â» a, F' a âˆ‚Î¼ = âˆ«â» a, F a âˆ‚Î¼ from
    ae_eq_of_ae_le_of_lintegral_le hF'_le (this â–¸ h_int_finite) hF_meas this.symm.le
  refine tendsto_nhds_unique ?_ hf_tendsto
  exact lintegral_tendsto_of_tendsto_of_monotone hf_meas hf_mono hF'_tendsto

/-- If a monotone sequence of functions has an upper bound and the sequence of integrals of these
functions tends to the integral of the upper bound, then the sequence of functions converges
almost everywhere to the upper bound. -/
lemma tendsto_of_lintegral_tendsto_of_monotone {Î± : Type*} {mÎ± : MeasurableSpace Î±}
    {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ} {Î¼ : Measure Î±}
    (hF_meas : AEMeasurable F Î¼)
    (hf_tendsto : Tendsto (fun i â†¦ âˆ«â» a, f i a âˆ‚Î¼) atTop (ğ“ (âˆ«â» a, F a âˆ‚Î¼)))
    (hf_mono : âˆ€áµ a âˆ‚Î¼, Monotone (fun i â†¦ f i a))
    (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ i, f i a â‰¤ F a) (h_int_finite : âˆ«â» a, F a âˆ‚Î¼ â‰  âˆ) :
    âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F a)) := by
  have : âˆ€ n, âˆƒ g : Î± â†’ â„â‰¥0âˆ, Measurable g âˆ§ g â‰¤ f n âˆ§ âˆ«â» a, f n a âˆ‚Î¼ = âˆ«â» a, g a âˆ‚Î¼ :=
    fun n â†¦ exists_measurable_le_lintegral_eq _ _
  choose g gmeas gf hg using this
  let g' : â„• â†’ Î± â†’ â„â‰¥0âˆ := Nat.rec (g 0) (fun n I x â†¦ max (g (n+1) x) (I x))
  have M n : Measurable (g' n) := by
    induction n with
    | zero => simp [g', gmeas 0]
    | succ n ih => exact Measurable.max (gmeas (n+1)) ih
  have I : âˆ€ n x, g n x â‰¤ g' n x := by
    intro n x
    cases n with | zero | succ => simp [g']
  have I' : âˆ€áµ x âˆ‚Î¼, âˆ€ n, g' n x â‰¤ f n x := by
    filter_upwards [hf_mono] with x hx n
    induction n with
    | zero => simpa [g'] using gf 0 x
    | succ n ih => exact max_le (gf (n+1) x) (ih.trans (hx (Nat.le_succ n)))
  have Int_eq n : âˆ«â» x, g' n x âˆ‚Î¼ = âˆ«â» x, f n x âˆ‚Î¼ := by
    apply le_antisymm
    Â· apply lintegral_mono_ae
      filter_upwards [I'] with x hx using hx n
    Â· rw [hg n]
      exact lintegral_mono (I n)
  have : âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ g' i a) atTop (ğ“ (F a)) := by
    apply tendsto_of_lintegral_tendsto_of_monotone_aux _ hF_meas _ _ _ h_int_finite
    Â· exact fun n â†¦ (M n).aemeasurable
    Â· simp_rw [Int_eq]
      exact hf_tendsto
    Â· exact Eventually.of_forall (fun x â†¦ monotone_nat_of_le_succ (fun n â†¦ le_max_right _ _))
    Â· filter_upwards [h_bound, I'] with x h'x hx n using (hx n).trans (h'x n)
  filter_upwards [this, I', h_bound] with x hx h'x h''x
  exact tendsto_of_tendsto_of_tendsto_of_le_of_le hx tendsto_const_nhds h'x h''x

/-- If an antitone sequence of functions has a lower bound and the sequence of integrals of these
functions tends to the integral of the lower bound, then the sequence of functions converges
almost everywhere to the lower bound. -/
lemma tendsto_of_lintegral_tendsto_of_antitone {Î± : Type*} {mÎ± : MeasurableSpace Î±}
    {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ} {Î¼ : Measure Î±}
    (hf_meas : âˆ€ n, AEMeasurable (f n) Î¼)
    (hf_tendsto : Tendsto (fun i â†¦ âˆ«â» a, f i a âˆ‚Î¼) atTop (ğ“ (âˆ«â» a, F a âˆ‚Î¼)))
    (hf_mono : âˆ€áµ a âˆ‚Î¼, Antitone (fun i â†¦ f i a))
    (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ i, F a â‰¤ f i a) (h0 : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) :
    âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F a)) := by
  have h_int_finite : âˆ«â» a, F a âˆ‚Î¼ â‰  âˆ := by
    refine ((lintegral_mono_ae ?_).trans_lt h0.lt_top).ne
    filter_upwards [h_bound] with a ha using ha 0
  have h_exists : âˆ€áµ a âˆ‚Î¼, âˆƒ l, Tendsto (fun i â†¦ f i a) atTop (ğ“ l) := by
    filter_upwards [hf_mono] with a h_mono
    rcases _root_.tendsto_of_antitone h_mono with h | h
    Â· refine âŸ¨0, h.mono_right ?_âŸ©
      rw [OrderBot.atBot_eq]
      exact pure_le_nhds _
    Â· exact h
  classical
  let F' : Î± â†’ â„â‰¥0âˆ := fun a â†¦ if h : âˆƒ l, Tendsto (fun i â†¦ f i a) atTop (ğ“ l)
    then h.choose else âˆ
  have hF'_tendsto : âˆ€áµ a âˆ‚Î¼, Tendsto (fun i â†¦ f i a) atTop (ğ“ (F' a)) := by
    filter_upwards [h_exists] with a ha
    simp_rw [F', dif_pos ha]
    exact ha.choose_spec
  suffices F' =áµ[Î¼] F by
    filter_upwards [this, hF'_tendsto] with a h_eq h_tendsto using h_eq â–¸ h_tendsto
  have hF'_le : F â‰¤áµ[Î¼] F' := by
    filter_upwards [h_bound, hF'_tendsto] with a h_le h_tendsto
    exact ge_of_tendsto' h_tendsto (fun m â†¦ h_le _)
  suffices âˆ«â» a, F' a âˆ‚Î¼ = âˆ«â» a, F a âˆ‚Î¼ by
    refine (ae_eq_of_ae_le_of_lintegral_le hF'_le h_int_finite ?_ this.le).symm
    exact ENNReal.aemeasurable_of_tendsto hf_meas hF'_tendsto
  refine tendsto_nhds_unique ?_ hf_tendsto
  exact lintegral_tendsto_of_tendsto_of_antitone hf_meas hf_mono h0 hF'_tendsto

end MeasureTheory
