/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Johannes HÃ¶lzl
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Real
import Mathlib.MeasureTheory.Function.SimpleFunc

/-!
# Lower Lebesgue integral for `â„â‰¥0âˆ`-valued functions

We define the lower Lebesgue integral of an `â„â‰¥0âˆ`-valued function.

## Notation

We introduce the following notation for the lower Lebesgue integral of a function `f : Î± â†’ â„â‰¥0âˆ`.

* `âˆ«â» x, f x âˆ‚Î¼`: integral of a function `f : Î± â†’ â„â‰¥0âˆ` with respect to a measure `Î¼`;
* `âˆ«â» x, f x`: integral of a function `f : Î± â†’ â„â‰¥0âˆ` with respect to the canonical measure
  `volume` on `Î±`;
* `âˆ«â» x in s, f x âˆ‚Î¼`: integral of a function `f : Î± â†’ â„â‰¥0âˆ` over a set `s` with respect
  to a measure `Î¼`, defined as `âˆ«â» x, f x âˆ‚(Î¼.restrict s)`;
* `âˆ«â» x in s, f x`: integral of a function `f : Î± â†’ â„â‰¥0âˆ` over a set `s` with respect
  to the canonical measure `volume`, defined as `âˆ«â» x, f x âˆ‚(volume.restrict s)`.
-/

assert_not_exists Basis Norm MeasureTheory.MeasurePreserving MeasureTheory.Measure.dirac

open Set hiding restrict restrict_apply

open Filter ENNReal Topology NNReal

open Function (support)

namespace MeasureTheory

local infixr:25 " â†’â‚› " => SimpleFunc

variable {Î± Î² Î³ : Type*}

section LIntegral

open SimpleFunc

variable {m : MeasurableSpace Î±} {Î¼ Î½ : Measure Î±} {s : Set Î±}

/-- The **lower Lebesgue integral** of a function `f` with respect to a measure `Î¼`. -/
noncomputable irreducible_def lintegral (Î¼ : Measure Î±) (f : Î± â†’ â„â‰¥0âˆ) : â„â‰¥0âˆ :=
  â¨† (g : Î± â†’â‚› â„â‰¥0âˆ) (_ : â‡‘g â‰¤ f), g.lintegral Î¼

/-! In the notation for integrals, an expression like `âˆ«â» x, g â€–xâ€– âˆ‚Î¼` will not be parsed correctly,
  and needs parentheses. We do not set the binding power of `r` to `0`, because then
  `âˆ«â» x, f x = 0` will be parsed incorrectly. -/

@[inherit_doc MeasureTheory.lintegral]
notation3 "âˆ«â» "(...)", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => lintegral Î¼ r

@[inherit_doc MeasureTheory.lintegral]
notation3 "âˆ«â» "(...)", "r:60:(scoped f => lintegral volume f) => r

@[inherit_doc MeasureTheory.lintegral]
notation3"âˆ«â» "(...)" in "s", "r:60:(scoped f => f)" âˆ‚"Î¼:70 => lintegral (Measure.restrict Î¼ s) r

@[inherit_doc MeasureTheory.lintegral]
notation3"âˆ«â» "(...)" in "s", "r:60:(scoped f => lintegral (Measure.restrict volume s) f) => r

theorem SimpleFunc.lintegral_eq_lintegral {m : MeasurableSpace Î±} (f : Î± â†’â‚› â„â‰¥0âˆ) (Î¼ : Measure Î±) :
    âˆ«â» a, f a âˆ‚Î¼ = f.lintegral Î¼ := by
  rw [MeasureTheory.lintegral]
  exact le_antisymm (iSupâ‚‚_le fun g hg => lintegral_mono hg <| le_rfl)
    (le_iSupâ‚‚_of_le f le_rfl le_rfl)

@[gcongr, mono]
theorem lintegral_mono' {m : MeasurableSpace Î±} â¦ƒÎ¼ Î½ : Measure Î±â¦„ (hÎ¼Î½ : Î¼ â‰¤ Î½) â¦ƒf g : Î± â†’ â„â‰¥0âˆâ¦„
    (hfg : f â‰¤ g) : âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, g a âˆ‚Î½ := by
  rw [lintegral, lintegral]
  exact iSup_mono fun Ï† => iSup_mono' fun hÏ† => âŸ¨le_trans hÏ† hfg, lintegral_mono (le_refl Ï†) hÎ¼Î½âŸ©

-- version where `hfg` is an explicit forall, so that `@[gcongr]` can recognize it
@[gcongr] theorem lintegral_mono_fn' â¦ƒf g : Î± â†’ â„â‰¥0âˆâ¦„ (hfg : âˆ€ x, f x â‰¤ g x) (h2 : Î¼ â‰¤ Î½) :
    âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, g a âˆ‚Î½ :=
  lintegral_mono' h2 hfg

theorem lintegral_mono â¦ƒf g : Î± â†’ â„â‰¥0âˆâ¦„ (hfg : f â‰¤ g) : âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, g a âˆ‚Î¼ :=
  lintegral_mono' (le_refl Î¼) hfg

-- version where `hfg` is an explicit forall, so that `@[gcongr]` can recognize it
@[gcongr] theorem lintegral_mono_fn â¦ƒf g : Î± â†’ â„â‰¥0âˆâ¦„ (hfg : âˆ€ x, f x â‰¤ g x) :
    âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, g a âˆ‚Î¼ :=
  lintegral_mono hfg

theorem lintegral_mono_nnreal {f g : Î± â†’ â„â‰¥0} (h : f â‰¤ g) : âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, g a âˆ‚Î¼ :=
  lintegral_mono fun a => ENNReal.coe_le_coe.2 (h a)

theorem iSup_lintegral_measurable_le_eq_lintegral (f : Î± â†’ â„â‰¥0âˆ) :
    â¨† (g : Î± â†’ â„â‰¥0âˆ) (_ : Measurable g) (_ : g â‰¤ f), âˆ«â» a, g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ := by
  apply le_antisymm
  Â· exact iSup_le fun i => iSup_le fun _ => iSup_le fun h'i => lintegral_mono h'i
  Â· rw [lintegral]
    refine iSupâ‚‚_le fun i hi => le_iSupâ‚‚_of_le i i.measurable <| le_iSup_of_le hi ?_
    exact le_of_eq (i.lintegral_eq_lintegral _).symm

theorem lintegral_mono_set {_ : MeasurableSpace Î±} â¦ƒÎ¼ : Measure Î±â¦„ {s t : Set Î±} {f : Î± â†’ â„â‰¥0âˆ}
    (hst : s âŠ† t) : âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in t, f x âˆ‚Î¼ :=
  lintegral_mono' (Measure.restrict_mono hst (le_refl Î¼)) (le_refl f)

theorem lintegral_mono_set' {_ : MeasurableSpace Î±} â¦ƒÎ¼ : Measure Î±â¦„ {s t : Set Î±} {f : Î± â†’ â„â‰¥0âˆ}
    (hst : s â‰¤áµ[Î¼] t) : âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in t, f x âˆ‚Î¼ :=
  lintegral_mono' (Measure.restrict_mono' hst (le_refl Î¼)) (le_refl f)

theorem monotone_lintegral {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) : Monotone (lintegral Î¼) :=
  lintegral_mono

@[simp]
theorem lintegral_const (c : â„â‰¥0âˆ) : âˆ«â» _, c âˆ‚Î¼ = c * Î¼ univ := by
  rw [â† SimpleFunc.const_lintegral, â† SimpleFunc.lintegral_eq_lintegral, SimpleFunc.coe_const]
  rfl

theorem lintegral_zero : âˆ«â» _ : Î±, 0 âˆ‚Î¼ = 0 := by simp

theorem lintegral_zero_fun : lintegral Î¼ (0 : Î± â†’ â„â‰¥0âˆ) = 0 :=
  lintegral_zero

theorem lintegral_one : âˆ«â» _, (1 : â„â‰¥0âˆ) âˆ‚Î¼ = Î¼ univ := by rw [lintegral_const, one_mul]

theorem setLIntegral_const (s : Set Î±) (c : â„â‰¥0âˆ) : âˆ«â» _ in s, c âˆ‚Î¼ = c * Î¼ s := by
  rw [lintegral_const, Measure.restrict_apply_univ]

theorem setLIntegral_one (s) : âˆ«â» _ in s, 1 âˆ‚Î¼ = Î¼ s := by rw [setLIntegral_const, one_mul]

variable (Î¼) in
/-- For any function `f : Î± â†’ â„â‰¥0âˆ`, there exists a measurable function `g â‰¤ f` with the same
integral. -/
theorem exists_measurable_le_lintegral_eq (f : Î± â†’ â„â‰¥0âˆ) :
    âˆƒ g : Î± â†’ â„â‰¥0âˆ, Measurable g âˆ§ g â‰¤ f âˆ§ âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, g a âˆ‚Î¼ := by
  rcases eq_or_ne (âˆ«â» a, f a âˆ‚Î¼) 0 with hâ‚€ | hâ‚€
  Â· exact âŸ¨0, measurable_zero, zero_le f, hâ‚€.trans lintegral_zero.symmâŸ©
  rcases exists_seq_strictMono_tendsto' hâ‚€.bot_lt with âŸ¨L, _, hLf, hL_tendstoâŸ©
  have : âˆ€ n, âˆƒ g : Î± â†’ â„â‰¥0âˆ, Measurable g âˆ§ g â‰¤ f âˆ§ L n < âˆ«â» a, g a âˆ‚Î¼ := by
    intro n
    simpa only [â† iSup_lintegral_measurable_le_eq_lintegral f, lt_iSup_iff, exists_prop] using
      (hLf n).2
  choose g hgm hgf hLg using this
  refine
    âŸ¨fun x => â¨† n, g n x, .iSup hgm, fun x => iSup_le fun n => hgf n x, le_antisymm ?_ ?_âŸ©
  Â· refine le_of_tendsto' hL_tendsto fun n => (hLg n).le.trans <| lintegral_mono fun x => ?_
    exact le_iSup (fun n => g n x) n
  Â· exact lintegral_mono fun x => iSup_le fun n => hgf n x

/-- `âˆ«â» a in s, f a âˆ‚Î¼` is defined as the supremum of integrals of simple functions
`Ï† : Î± â†’â‚› â„â‰¥0âˆ` such that `Ï† â‰¤ f`. This lemma says that it suffices to take
functions `Ï† : Î± â†’â‚› â„â‰¥0`. -/
theorem lintegral_eq_nnreal {m : MeasurableSpace Î±} (f : Î± â†’ â„â‰¥0âˆ) (Î¼ : Measure Î±) :
    âˆ«â» a, f a âˆ‚Î¼ =
      â¨† (Ï† : Î± â†’â‚› â„â‰¥0) (_ : âˆ€ x, â†‘(Ï† x) â‰¤ f x), (Ï†.map ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ)).lintegral Î¼ := by
  rw [lintegral]
  refine
    le_antisymm (iSupâ‚‚_le fun Ï† hÏ† â†¦ ?_) (iSup_mono' fun Ï† â†¦ âŸ¨Ï†.map ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ), le_rflâŸ©)
  by_cases h : âˆ€áµ a âˆ‚Î¼, Ï† a â‰  âˆ
  Â· let Ïˆ := Ï†.map ENNReal.toNNReal
    replace h : Ïˆ.map ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) =áµ[Î¼] Ï† := h.mono fun a => ENNReal.coe_toNNReal
    have : âˆ€ x, â†‘(Ïˆ x) â‰¤ f x := fun x => le_trans ENNReal.coe_toNNReal_le_self (hÏ† x)
    exact le_iSupâ‚‚_of_le (Ï†.map ENNReal.toNNReal) this (ge_of_eq <| lintegral_congr h)
  Â· have h_meas : Î¼ (Ï† â»Â¹' {âˆ}) â‰  0 := mt measure_zero_iff_ae_nmem.1 h
    refine le_trans le_top (ge_of_eq <| (iSup_eq_top _).2 fun b hb => ?_)
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, b < n * Î¼ (Ï† â»Â¹' {âˆ}) := exists_nat_mul_gt h_meas (ne_of_lt hb)
    use (const Î± (n : â„â‰¥0)).restrict (Ï† â»Â¹' {âˆ})
    simp only [lt_iSup_iff, exists_prop, coe_restrict, Ï†.measurableSet_preimage, coe_const,
      ENNReal.coe_indicator, map_coe_ennreal_restrict, SimpleFunc.map_const, ENNReal.coe_natCast,
      restrict_const_lintegral]
    refine âŸ¨indicator_le fun x hx => le_trans ?_ (hÏ† _), hnâŸ©
    simp only [mem_preimage, mem_singleton_iff] at hx
    simp only [hx, le_top]

theorem exists_simpleFunc_forall_lintegral_sub_lt_of_pos {f : Î± â†’ â„â‰¥0âˆ} (h : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ)
    {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ Ï† : Î± â†’â‚› â„â‰¥0,
      (âˆ€ x, â†‘(Ï† x) â‰¤ f x) âˆ§
        âˆ€ Ïˆ : Î± â†’â‚› â„â‰¥0, (âˆ€ x, â†‘(Ïˆ x) â‰¤ f x) â†’ (map (â†‘) (Ïˆ - Ï†)).lintegral Î¼ < Îµ := by
  rw [lintegral_eq_nnreal] at h
  have := ENNReal.lt_add_right h hÎµ
  erw [ENNReal.biSup_add] at this <;> [skip; exact âŸ¨0, fun x => zero_le _âŸ©]
  simp_rw [lt_iSup_iff, iSup_lt_iff, iSup_le_iff] at this
  rcases this with âŸ¨Ï†, hle : âˆ€ x, â†‘(Ï† x) â‰¤ f x, b, hbÏ†, hbâŸ©
  refine âŸ¨Ï†, hle, fun Ïˆ hÏˆ => ?_âŸ©
  have : (map (â†‘) Ï†).lintegral Î¼ â‰  âˆ := ne_top_of_le_ne_top h (by exact le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) Ï† hle)
  rw [â† ENNReal.add_lt_add_iff_left this, â† add_lintegral, â† SimpleFunc.map_add @ENNReal.coe_add]
  refine (hb _ fun x => le_trans ?_ (max_le (hle x) (hÏˆ x))).trans_lt hbÏ†
  norm_cast
  simp only [add_apply, sub_apply, add_tsub_eq_max]
  rfl

theorem iSup_lintegral_le {Î¹ : Sort*} (f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ) :
    â¨† i, âˆ«â» a, f i a âˆ‚Î¼ â‰¤ âˆ«â» a, â¨† i, f i a âˆ‚Î¼ := by
  simp only [â† iSup_apply]
  exact (monotone_lintegral Î¼).le_map_iSup

theorem iSupâ‚‚_lintegral_le {Î¹ : Sort*} {Î¹' : Î¹ â†’ Sort*} (f : âˆ€ i, Î¹' i â†’ Î± â†’ â„â‰¥0âˆ) :
    â¨† (i) (j), âˆ«â» a, f i j a âˆ‚Î¼ â‰¤ âˆ«â» a, â¨† (i) (j), f i j a âˆ‚Î¼ := by
  convert (monotone_lintegral Î¼).le_map_iSupâ‚‚ f with a
  simp only [iSup_apply]

theorem le_iInf_lintegral {Î¹ : Sort*} (f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, â¨… i, f i a âˆ‚Î¼ â‰¤ â¨… i, âˆ«â» a, f i a âˆ‚Î¼ := by
  simp only [â† iInf_apply]
  exact (monotone_lintegral Î¼).map_iInf_le

theorem le_iInfâ‚‚_lintegral {Î¹ : Sort*} {Î¹' : Î¹ â†’ Sort*} (f : âˆ€ i, Î¹' i â†’ Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, â¨… (i) (h : Î¹' i), f i h a âˆ‚Î¼ â‰¤ â¨… (i) (h : Î¹' i), âˆ«â» a, f i h a âˆ‚Î¼ := by
  convert (monotone_lintegral Î¼).map_iInfâ‚‚_le f with a
  simp only [iInf_apply]

theorem lintegral_mono_ae {f g : Î± â†’ â„â‰¥0âˆ} (h : âˆ€áµ a âˆ‚Î¼, f a â‰¤ g a) :
    âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, g a âˆ‚Î¼ := by
  rcases exists_measurable_superset_of_null h with âŸ¨t, hts, ht, ht0âŸ©
  have : âˆ€áµ x âˆ‚Î¼, x âˆ‰ t := measure_zero_iff_ae_nmem.1 ht0
  rw [lintegral, lintegral]
  refine iSupâ‚‚_le fun s hfs â†¦ le_iSupâ‚‚_of_le (s.restrict tá¶œ) ?_ ?_
  Â· intro a
    by_cases h : a âˆˆ t <;>
      simp only [restrict_apply s ht.compl, mem_compl_iff, h, not_true, not_false_eq_true,
        indicator_of_not_mem, zero_le, not_false_eq_true, indicator_of_mem]
    exact le_trans (hfs a) (by_contradiction fun hnfg => h (hts hnfg))
  Â· refine le_of_eq (SimpleFunc.lintegral_congr <| this.mono fun a hnt => ?_)
    by_cases hat : a âˆˆ t <;> simp only [restrict_apply s ht.compl, mem_compl_iff, hat, not_true,
      not_false_eq_true, indicator_of_not_mem, not_false_eq_true, indicator_of_mem]
    exact (hnt hat).elim

/-- Lebesgue integral over a set is monotone in function.

This version assumes that the upper estimate is an a.e. measurable function
and the estimate holds a.e. on the set.
See also `setLIntegral_mono_ae'` for a version that assumes measurability of the set
but assumes no regularity of either function. -/
theorem setLIntegral_mono_ae {s : Set Î±} {f g : Î± â†’ â„â‰¥0âˆ} (hg : AEMeasurable g (Î¼.restrict s))
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x â‰¤ g x) : âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, g x âˆ‚Î¼ := by
  rcases exists_measurable_le_lintegral_eq (Î¼.restrict s) f with âŸ¨f', hf'm, hle, hf'âŸ©
  rw [hf']
  apply lintegral_mono_ae
  rw [ae_restrict_iffâ‚€]
  Â· exact hfg.mono fun x hx hxs â†¦ (hle x).trans (hx hxs)
  Â· exact nullMeasurableSet_le hf'm.aemeasurable hg

theorem setLIntegral_mono {s : Set Î±} {f g : Î± â†’ â„â‰¥0âˆ} (hg : Measurable g)
    (hfg : âˆ€ x âˆˆ s, f x â‰¤ g x) : âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, g x âˆ‚Î¼ :=
  setLIntegral_mono_ae hg.aemeasurable (ae_of_all _ hfg)

theorem setLIntegral_mono_ae' {s : Set Î±} {f g : Î± â†’ â„â‰¥0âˆ} (hs : MeasurableSet s)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x â‰¤ g x) : âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, g x âˆ‚Î¼ :=
  lintegral_mono_ae <| (ae_restrict_iff' hs).2 hfg

theorem setLIntegral_mono' {s : Set Î±} {f g : Î± â†’ â„â‰¥0âˆ} (hs : MeasurableSet s)
    (hfg : âˆ€ x âˆˆ s, f x â‰¤ g x) : âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x in s, g x âˆ‚Î¼ :=
  setLIntegral_mono_ae' hs (ae_of_all _ hfg)

theorem setLIntegral_le_lintegral (s : Set Î±) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x in s, f x âˆ‚Î¼ â‰¤ âˆ«â» x, f x âˆ‚Î¼ :=
  lintegral_mono' Measure.restrict_le_self le_rfl

theorem lintegral_congr_ae {f g : Î± â†’ â„â‰¥0âˆ} (h : f =áµ[Î¼] g) : âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, g a âˆ‚Î¼ :=
  le_antisymm (lintegral_mono_ae <| h.le) (lintegral_mono_ae <| h.symm.le)

theorem lintegral_congr {f g : Î± â†’ â„â‰¥0âˆ} (h : âˆ€ a, f a = g a) : âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, g a âˆ‚Î¼ := by
  simp only [h]

theorem setLIntegral_congr {f : Î± â†’ â„â‰¥0âˆ} {s t : Set Î±} (h : s =áµ[Î¼] t) :
    âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in t, f x âˆ‚Î¼ := by rw [Measure.restrict_congr_set h]

theorem setLIntegral_congr_fun {f g : Î± â†’ â„â‰¥0âˆ} {s : Set Î±} (hs : MeasurableSet s)
    (hfg : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = g x) : âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x in s, g x âˆ‚Î¼ := by
  rw [lintegral_congr_ae]
  rw [EventuallyEq]
  rwa [ae_restrict_iff' hs]

/-- **Monotone convergence theorem** -- sometimes called **Beppo-Levi convergence**.
See `lintegral_iSup_directed` for a more general form. -/
theorem lintegral_iSup {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ n, Measurable (f n)) (h_mono : Monotone f) :
    âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by
  set c : â„â‰¥0 â†’ â„â‰¥0âˆ := (â†‘)
  set F := fun a : Î± => â¨† n, f n a
  refine le_antisymm ?_ (iSup_lintegral_le _)
  rw [lintegral_eq_nnreal]
  refine iSup_le fun s => iSup_le fun hsf => ?_
  refine ENNReal.le_of_forall_lt_one_mul_le fun a ha => ?_
  rcases ENNReal.lt_iff_exists_coe.1 ha with âŸ¨r, rfl, _âŸ©
  have ha : r < 1 := ENNReal.coe_lt_coe.1 ha
  let rs := s.map fun a => r * a
  have eq_rs : rs.map c = (const Î± r : Î± â†’â‚› â„â‰¥0âˆ) * map c s := rfl
  have eq : âˆ€ p, rs.map c â»Â¹' {p} = â‹ƒ n, rs.map c â»Â¹' {p} âˆ© { a | p â‰¤ f n a } := by
    intro p
    rw [â† inter_iUnion]; nth_rw 1 [â† inter_univ (map c rs â»Â¹' {p})]
    refine Set.ext fun x => and_congr_right fun hx => (iff_of_eq (true_iff _)).2 ?_
    by_cases p_eq : p = 0
    Â· simp [p_eq]
    simp only [coe_map, mem_preimage, Function.comp_apply, mem_singleton_iff] at hx
    subst hx
    have : r * s x â‰  0 := by rwa [Ne, â† ENNReal.coe_eq_zero]
    have : s x â‰  0 := right_ne_zero_of_mul this
    have : (rs.map c) x < â¨† n : â„•, f n x := by
      refine lt_of_lt_of_le (ENNReal.coe_lt_coe.2 ?_) (hsf x)
      suffices r * s x < 1 * s x by simpa
      exact mul_lt_mul_of_pos_right ha (pos_iff_ne_zero.2 this)
    rcases lt_iSup_iff.1 this with âŸ¨i, hiâŸ©
    exact mem_iUnion.2 âŸ¨i, le_of_lt hiâŸ©
  have mono : âˆ€ r : â„â‰¥0âˆ, Monotone fun n => rs.map c â»Â¹' {r} âˆ© { a | r â‰¤ f n a } := by
    intro r i j h
    refine inter_subset_inter_right _ ?_
    simp_rw [subset_def, mem_setOf]
    intro x hx
    exact le_trans hx (h_mono h x)
  have h_meas : âˆ€ n, MeasurableSet {a : Î± | map c rs a â‰¤ f n a} := fun n =>
    measurableSet_le (SimpleFunc.measurable _) (hf n)
  calc
    (r : â„â‰¥0âˆ) * (s.map c).lintegral Î¼ = âˆ‘ r âˆˆ (rs.map c).range, r * Î¼ (rs.map c â»Â¹' {r}) := by
      rw [â† const_mul_lintegral, eq_rs, SimpleFunc.lintegral]
    _ = âˆ‘ r âˆˆ (rs.map c).range, r * Î¼ (â‹ƒ n, rs.map c â»Â¹' {r} âˆ© { a | r â‰¤ f n a }) := by
      simp only [(eq _).symm]
    _ = âˆ‘ r âˆˆ (rs.map c).range, â¨† n, r * Î¼ (rs.map c â»Â¹' {r} âˆ© { a | r â‰¤ f n a }) :=
      Finset.sum_congr rfl fun x _ => by rw [(mono x).measure_iUnion, ENNReal.mul_iSup]
    _ = â¨† n, âˆ‘ r âˆˆ (rs.map c).range, r * Î¼ (rs.map c â»Â¹' {r} âˆ© { a | r â‰¤ f n a }) := by
      refine ENNReal.finsetSum_iSup_of_monotone fun p i j h â†¦ ?_
      gcongr _ * Î¼ ?_
      exact mono p h
    _ â‰¤ â¨† n : â„•, ((rs.map c).restrict { a | (rs.map c) a â‰¤ f n a }).lintegral Î¼ := by
      gcongr with n
      rw [restrict_lintegral _ (h_meas n)]
      refine le_of_eq (Finset.sum_congr rfl fun r _ => ?_)
      congr 2 with a
      refine and_congr_right ?_
      simp +contextual
    _ â‰¤ â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by
      simp only [â† SimpleFunc.lintegral_eq_lintegral]
      gcongr with n a
      simp only [map_apply] at h_meas
      simp only [coe_map, restrict_apply _ (h_meas _), (Â· âˆ˜ Â·)]
      exact indicator_apply_le id

/-- Monotone convergence theorem -- sometimes called Beppo-Levi convergence. Version with
ae_measurable functions. -/
theorem lintegral_iSup' {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_mono : âˆ€áµ x âˆ‚Î¼, Monotone fun n => f n x) : âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by
  simp_rw [â† iSup_apply]
  let p : Î± â†’ (â„• â†’ â„â‰¥0âˆ) â†’ Prop := fun _ f' => Monotone f'
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun i => f i x := h_mono
  have h_ae_seq_mono : Monotone (aeSeq hf p) := by
    intro n m hnm x
    by_cases hx : x âˆˆ aeSeqSet hf p
    Â· exact aeSeq.prop_of_mem_aeSeqSet hf hx hnm
    Â· simp only [aeSeq, hx, if_false, le_rfl]
  rw [lintegral_congr_ae (aeSeq.iSup hf hp).symm]
  simp_rw [iSup_apply]
  rw [lintegral_iSup (aeSeq.measurable hf p) h_ae_seq_mono]
  congr with n
  exact lintegral_congr_ae (aeSeq.aeSeq_n_eq_fun_n_ae hf hp n)

/-- Monotone convergence theorem expressed with limits -/
theorem lintegral_tendsto_of_tendsto_of_monotone {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ n, AEMeasurable (f n) Î¼) (h_mono : âˆ€áµ x âˆ‚Î¼, Monotone fun n => f n x)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => f n x) atTop (ğ“ <| F x)) :
    Tendsto (fun n => âˆ«â» x, f n x âˆ‚Î¼) atTop (ğ“ <| âˆ«â» x, F x âˆ‚Î¼) := by
  have : Monotone fun n => âˆ«â» x, f n x âˆ‚Î¼ := fun i j hij =>
    lintegral_mono_ae (h_mono.mono fun x hx => hx hij)
  suffices key : âˆ«â» x, F x âˆ‚Î¼ = â¨† n, âˆ«â» x, f n x âˆ‚Î¼ by
    rw [key]
    exact tendsto_atTop_iSup this
  rw [â† lintegral_iSup' hf h_mono]
  refine lintegral_congr_ae ?_
  filter_upwards [h_mono, h_tendsto] with _ hx_mono hx_tendsto using
    tendsto_nhds_unique hx_tendsto (tendsto_atTop_iSup hx_mono)

theorem lintegral_eq_iSup_eapprox_lintegral {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» a, f a âˆ‚Î¼ = â¨† n, (eapprox f n).lintegral Î¼ :=
  calc
    âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, â¨† n, (eapprox f n : Î± â†’ â„â‰¥0âˆ) a âˆ‚Î¼ := by
      congr; ext a; rw [iSup_eapprox_apply hf]
    _ = â¨† n, âˆ«â» a, (eapprox f n : Î± â†’ â„â‰¥0âˆ) a âˆ‚Î¼ := by
      apply lintegral_iSup
      Â· measurability
      Â· intro i j h
        exact monotone_eapprox f h
    _ = â¨† n, (eapprox f n).lintegral Î¼ := by
      congr; ext n; rw [(eapprox f n).lintegral_eq_lintegral]

lemma lintegral_eapprox_le_lintegral {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (n : â„•) :
    (eapprox f n).lintegral Î¼ â‰¤ âˆ«â» x, f x âˆ‚Î¼ := by
  rw [lintegral_eq_iSup_eapprox_lintegral hf]
  exact le_iSup (fun n â†¦ (eapprox f n).lintegral Î¼) n

lemma measure_support_eapprox_lt_top {f : Î± â†’ â„â‰¥0âˆ} (hf_meas : Measurable f)
    (hf : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) (n : â„•) :
    Î¼ (support (eapprox f n)) < âˆ :=
  measure_support_lt_top_of_lintegral_ne_top <|
    ((lintegral_eapprox_le_lintegral hf_meas n).trans_lt hf.lt_top).ne

/-- If `f` has finite integral, then `âˆ«â» x in s, f x âˆ‚Î¼` is absolutely continuous in `s`: it tends
to zero as `Î¼ s` tends to zero. This lemma states this fact in terms of `Îµ` and `Î´`. -/
theorem exists_pos_setLIntegral_lt_of_measure_lt {f : Î± â†’ â„â‰¥0âˆ} (h : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) {Îµ : â„â‰¥0âˆ}
    (hÎµ : Îµ â‰  0) : âˆƒ Î´ > 0, âˆ€ s, Î¼ s < Î´ â†’ âˆ«â» x in s, f x âˆ‚Î¼ < Îµ := by
  rcases exists_between (pos_iff_ne_zero.mpr hÎµ) with âŸ¨Îµâ‚‚, hÎµâ‚‚0, hÎµâ‚‚ÎµâŸ©
  rcases exists_between hÎµâ‚‚0 with âŸ¨Îµâ‚, hÎµâ‚0, hÎµâ‚â‚‚âŸ©
  rcases exists_simpleFunc_forall_lintegral_sub_lt_of_pos h hÎµâ‚0.ne' with âŸ¨Ï†, _, hÏ†âŸ©
  rcases Ï†.exists_forall_le with âŸ¨C, hCâŸ©
  use (Îµâ‚‚ - Îµâ‚) / C, ENNReal.div_pos_iff.2 âŸ¨(tsub_pos_iff_lt.2 hÎµâ‚â‚‚).ne', ENNReal.coe_ne_topâŸ©
  refine fun s hs => lt_of_le_of_lt ?_ hÎµâ‚‚Îµ
  simp only [lintegral_eq_nnreal, iSup_le_iff]
  intro Ïˆ hÏˆ
  calc
    (map (â†‘) Ïˆ).lintegral (Î¼.restrict s) â‰¤
        (map (â†‘) Ï†).lintegral (Î¼.restrict s) + (map (â†‘) (Ïˆ - Ï†)).lintegral (Î¼.restrict s) := by
      rw [â† SimpleFunc.add_lintegral, â† SimpleFunc.map_add @ENNReal.coe_add]
      refine SimpleFunc.lintegral_mono (fun x => ?_) le_rfl
      simp only [add_tsub_eq_max, le_max_right, coe_map, Function.comp_apply, SimpleFunc.coe_add,
        SimpleFunc.coe_sub, Pi.add_apply, Pi.sub_apply, ENNReal.coe_max (Ï† x) (Ïˆ x)]
    _ â‰¤ (map (â†‘) Ï†).lintegral (Î¼.restrict s) + Îµâ‚ := by
      gcongr
      refine le_trans ?_ (hÏ† _ hÏˆ).le
      exact SimpleFunc.lintegral_mono le_rfl Measure.restrict_le_self
    _ â‰¤ (SimpleFunc.const Î± (C : â„â‰¥0âˆ)).lintegral (Î¼.restrict s) + Îµâ‚ := by
      gcongr
      exact fun x â†¦ ENNReal.coe_le_coe.2 (hC x)
    _ = C * Î¼ s + Îµâ‚ := by
      simp only [â† SimpleFunc.lintegral_eq_lintegral, coe_const, lintegral_const,
        Measure.restrict_apply, MeasurableSet.univ, univ_inter, Function.const]
    _ â‰¤ C * ((Îµâ‚‚ - Îµâ‚) / C) + Îµâ‚ := by gcongr
    _ â‰¤ Îµâ‚‚ - Îµâ‚ + Îµâ‚ := by gcongr; apply mul_div_le
    _ = Îµâ‚‚ := tsub_add_cancel_of_le hÎµâ‚â‚‚.le

/-- If `f` has finite integral, then `âˆ«â» x in s, f x âˆ‚Î¼` is absolutely continuous in `s`: it tends
to zero as `Î¼ s` tends to zero. -/
theorem tendsto_setLIntegral_zero {Î¹} {f : Î± â†’ â„â‰¥0âˆ} (h : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) {l : Filter Î¹}
    {s : Î¹ â†’ Set Î±} (hl : Tendsto (Î¼ âˆ˜ s) l (ğ“ 0)) :
    Tendsto (fun i => âˆ«â» x in s i, f x âˆ‚Î¼) l (ğ“ 0) := by
  simp only [ENNReal.nhds_zero, tendsto_iInf, tendsto_principal, mem_Iio,
    â† pos_iff_ne_zero] at hl âŠ¢
  intro Îµ Îµ0
  rcases exists_pos_setLIntegral_lt_of_measure_lt h Îµ0.ne' with âŸ¨Î´, Î´0, hÎ´âŸ©
  exact (hl Î´ Î´0).mono fun i => hÎ´ _

/-- The sum of the lower Lebesgue integrals of two functions is less than or equal to the integral
of their sum. The other inequality needs one of these functions to be (a.e.-)measurable. -/
theorem le_lintegral_add (f g : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ â‰¤ âˆ«â» a, f a + g a âˆ‚Î¼ := by
  simp only [lintegral]
  refine ENNReal.biSup_add_biSup_le' (p := fun h : Î± â†’â‚› â„â‰¥0âˆ => h â‰¤ f)
    (q := fun h : Î± â†’â‚› â„â‰¥0âˆ => h â‰¤ g) âŸ¨0, zero_le fâŸ© âŸ¨0, zero_le gâŸ© fun f' hf' g' hg' => ?_
  exact le_iSupâ‚‚_of_le (f' + g') (add_le_add hf' hg') (add_lintegral _ _).ge

-- Use stronger lemmas `lintegral_add_left`/`lintegral_add_right` instead
theorem lintegral_add_aux {f g : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (hg : Measurable g) :
    âˆ«â» a, f a + g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ :=
  calc
    âˆ«â» a, f a + g a âˆ‚Î¼ =
        âˆ«â» a, (â¨† n, (eapprox f n : Î± â†’ â„â‰¥0âˆ) a) + â¨† n, (eapprox g n : Î± â†’ â„â‰¥0âˆ) a âˆ‚Î¼ := by
      simp only [iSup_eapprox_apply, hf, hg]
    _ = âˆ«â» a, â¨† n, (eapprox f n + eapprox g n : Î± â†’ â„â‰¥0âˆ) a âˆ‚Î¼ := by
      congr; funext a
      rw [ENNReal.iSup_add_iSup_of_monotone]
      Â· simp only [Pi.add_apply]
      Â· intro i j h
        exact monotone_eapprox _ h a
      Â· intro i j h
        exact monotone_eapprox _ h a
    _ = â¨† n, (eapprox f n).lintegral Î¼ + (eapprox g n).lintegral Î¼ := by
      rw [lintegral_iSup]
      Â· congr
        funext n
        rw [â† SimpleFunc.add_lintegral, â† SimpleFunc.lintegral_eq_lintegral]
        simp only [Pi.add_apply, SimpleFunc.coe_add]
      Â· fun_prop
      Â· intro i j h a
        dsimp
        gcongr <;> exact monotone_eapprox _ h _
    _ = (â¨† n, (eapprox f n).lintegral Î¼) + â¨† n, (eapprox g n).lintegral Î¼ := by
      refine (ENNReal.iSup_add_iSup_of_monotone ?_ ?_).symm <;>
        Â· intro i j h
          exact SimpleFunc.lintegral_mono (monotone_eapprox _ h) le_rfl
    _ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ := by
      rw [lintegral_eq_iSup_eapprox_lintegral hf, lintegral_eq_iSup_eapprox_lintegral hg]

/-- If `f g : Î± â†’ â„â‰¥0âˆ` are two functions and one of them is (a.e.) measurable, then the Lebesgue
integral of `f + g` equals the sum of integrals. This lemma assumes that `f` is integrable, see also
`MeasureTheory.lintegral_add_right` and primed versions of these lemmas. -/
@[simp]
theorem lintegral_add_left {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (g : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, f a + g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ := by
  refine le_antisymm ?_ (le_lintegral_add _ _)
  rcases exists_measurable_le_lintegral_eq Î¼ fun a => f a + g a with âŸ¨Ï†, hÏ†m, hÏ†_le, hÏ†_eqâŸ©
  calc
    âˆ«â» a, f a + g a âˆ‚Î¼ = âˆ«â» a, Ï† a âˆ‚Î¼ := hÏ†_eq
    _ â‰¤ âˆ«â» a, f a + (Ï† a - f a) âˆ‚Î¼ := lintegral_mono fun a => le_add_tsub
    _ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, Ï† a - f a âˆ‚Î¼ := lintegral_add_aux hf (hÏ†m.sub hf)
    _ â‰¤ âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ :=
      add_le_add_left (lintegral_mono fun a => tsub_le_iff_left.2 <| hÏ†_le a) _

theorem lintegral_add_left' {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (g : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, f a + g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ := by
  rw [lintegral_congr_ae hf.ae_eq_mk, â† lintegral_add_left hf.measurable_mk,
    lintegral_congr_ae (hf.ae_eq_mk.add (ae_eq_refl g))]

theorem lintegral_add_right' (f : Î± â†’ â„â‰¥0âˆ) {g : Î± â†’ â„â‰¥0âˆ} (hg : AEMeasurable g Î¼) :
    âˆ«â» a, f a + g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ := by
  simpa only [add_comm] using lintegral_add_left' hg f

/-- If `f g : Î± â†’ â„â‰¥0âˆ` are two functions and one of them is (a.e.) measurable, then the Lebesgue
integral of `f + g` equals the sum of integrals. This lemma assumes that `g` is integrable, see also
`MeasureTheory.lintegral_add_left` and primed versions of these lemmas. -/
@[simp]
theorem lintegral_add_right (f : Î± â†’ â„â‰¥0âˆ) {g : Î± â†’ â„â‰¥0âˆ} (hg : Measurable g) :
    âˆ«â» a, f a + g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, g a âˆ‚Î¼ :=
  lintegral_add_right' f hg.aemeasurable

@[simp]
theorem lintegral_smul_measure {R : Type*} [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]
    (c : R) (f : Î± â†’ â„â‰¥0âˆ) : âˆ«â» a, f a âˆ‚c â€¢ Î¼ = c â€¢ âˆ«â» a, f a âˆ‚Î¼ := by
  simp only [lintegral, iSup_subtype', SimpleFunc.lintegral_smul, ENNReal.smul_iSup]

lemma setLIntegral_smul_measure {R : Type*} [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ]
    (c : R) (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î±) :
    âˆ«â» a in s, f a âˆ‚(c â€¢ Î¼) = c â€¢ âˆ«â» a in s, f a âˆ‚Î¼ := by
  rw [Measure.restrict_smul, lintegral_smul_measure]

@[simp]
theorem lintegral_zero_measure {m : MeasurableSpace Î±} (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, f a âˆ‚(0 : Measure Î±) = 0 := by
  simp [lintegral]

@[simp]
theorem lintegral_add_measure (f : Î± â†’ â„â‰¥0âˆ) (Î¼ Î½ : Measure Î±) :
    âˆ«â» a, f a âˆ‚(Î¼ + Î½) = âˆ«â» a, f a âˆ‚Î¼ + âˆ«â» a, f a âˆ‚Î½ := by
  simp only [lintegral, SimpleFunc.lintegral_add, iSup_subtype']
  refine (ENNReal.iSup_add_iSup ?_).symm
  rintro âŸ¨Ï†, hÏ†âŸ© âŸ¨Ïˆ, hÏˆâŸ©
  refine âŸ¨âŸ¨Ï† âŠ” Ïˆ, sup_le hÏ† hÏˆâŸ©, ?_âŸ©
  apply_rules [add_le_add, SimpleFunc.lintegral_mono, le_rfl] -- TODO: use `gcongr`
  exacts [le_sup_left, le_sup_right]

@[simp]
theorem lintegral_finset_sum_measure {Î¹} (s : Finset Î¹) (f : Î± â†’ â„â‰¥0âˆ) (Î¼ : Î¹ â†’ Measure Î±) :
    âˆ«â» a, f a âˆ‚(âˆ‘ i âˆˆ s, Î¼ i) = âˆ‘ i âˆˆ s, âˆ«â» a, f a âˆ‚Î¼ i :=
  let F : Measure Î± â†’+ â„â‰¥0âˆ :=
    { toFun := (lintegral Â· f),
      map_zero' := lintegral_zero_measure f,
      map_add' := lintegral_add_measure f }
  map_sum F Î¼ s

@[simp]
theorem lintegral_sum_measure {m : MeasurableSpace Î±} {Î¹} (f : Î± â†’ â„â‰¥0âˆ) (Î¼ : Î¹ â†’ Measure Î±) :
    âˆ«â» a, f a âˆ‚Measure.sum Î¼ = âˆ‘' i, âˆ«â» a, f a âˆ‚Î¼ i := by
  simp_rw [ENNReal.tsum_eq_iSup_sum, â† lintegral_finset_sum_measure,
    lintegral, SimpleFunc.lintegral_sum, ENNReal.tsum_eq_iSup_sum,
    SimpleFunc.lintegral_finset_sum, iSup_comm (Î¹ := Finset Î¹)]

theorem hasSum_lintegral_measure {Î¹} {_ : MeasurableSpace Î±} (f : Î± â†’ â„â‰¥0âˆ) (Î¼ : Î¹ â†’ Measure Î±) :
    HasSum (fun i => âˆ«â» a, f a âˆ‚Î¼ i) (âˆ«â» a, f a âˆ‚Measure.sum Î¼) :=
  (lintegral_sum_measure f Î¼).symm â–¸ ENNReal.summable.hasSum

@[simp]
theorem lintegral_of_isEmpty {Î±} [MeasurableSpace Î±] [IsEmpty Î±] (Î¼ : Measure Î±) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x, f x âˆ‚Î¼ = 0 := by
  have : Subsingleton (Measure Î±) := inferInstance
  convert lintegral_zero_measure f

theorem setLIntegral_empty (f : Î± â†’ â„â‰¥0âˆ) : âˆ«â» x in âˆ…, f x âˆ‚Î¼ = 0 := by
  rw [Measure.restrict_empty, lintegral_zero_measure]

theorem setLIntegral_univ (f : Î± â†’ â„â‰¥0âˆ) : âˆ«â» x in univ, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by
  rw [Measure.restrict_univ]

theorem setLIntegral_measure_zero (s : Set Î±) (f : Î± â†’ â„â‰¥0âˆ) (hs' : Î¼ s = 0) :
    âˆ«â» x in s, f x âˆ‚Î¼ = 0 := by
  convert lintegral_zero_measure _
  exact Measure.restrict_eq_zero.2 hs'

theorem lintegral_finset_sum' (s : Finset Î²) {f : Î² â†’ Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ b âˆˆ s, AEMeasurable (f b) Î¼) :
    âˆ«â» a, âˆ‘ b âˆˆ s, f b a âˆ‚Î¼ = âˆ‘ b âˆˆ s, âˆ«â» a, f b a âˆ‚Î¼ := by
  classical
  induction' s using Finset.induction_on with a s has ih
  Â· simp
  Â· simp only [Finset.sum_insert has]
    rw [Finset.forall_mem_insert] at hf
    rw [lintegral_add_left' hf.1, ih hf.2]

theorem lintegral_finset_sum (s : Finset Î²) {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ b âˆˆ s, Measurable (f b)) :
    âˆ«â» a, âˆ‘ b âˆˆ s, f b a âˆ‚Î¼ = âˆ‘ b âˆˆ s, âˆ«â» a, f b a âˆ‚Î¼ :=
  lintegral_finset_sum' s fun b hb => (hf b hb).aemeasurable

@[simp]
theorem lintegral_const_mul (r : â„â‰¥0âˆ) {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» a, r * f a âˆ‚Î¼ = r * âˆ«â» a, f a âˆ‚Î¼ :=
  calc
    âˆ«â» a, r * f a âˆ‚Î¼ = âˆ«â» a, â¨† n, (const Î± r * eapprox f n) a âˆ‚Î¼ := by
      congr
      funext a
      rw [â† iSup_eapprox_apply hf, ENNReal.mul_iSup]
      simp
    _ = â¨† n, r * (eapprox f n).lintegral Î¼ := by
      rw [lintegral_iSup]
      Â· congr
        funext n
        rw [â† SimpleFunc.const_mul_lintegral, â† SimpleFunc.lintegral_eq_lintegral]
      Â· intro n
        exact SimpleFunc.measurable _
      Â· intro i j h a
        exact mul_le_mul_left' (monotone_eapprox _ h _) _
    _ = r * âˆ«â» a, f a âˆ‚Î¼ := by rw [â† ENNReal.mul_iSup, lintegral_eq_iSup_eapprox_lintegral hf]

theorem lintegral_const_mul'' (r : â„â‰¥0âˆ) {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) :
    âˆ«â» a, r * f a âˆ‚Î¼ = r * âˆ«â» a, f a âˆ‚Î¼ := by
  have A : âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, hf.mk f a âˆ‚Î¼ := lintegral_congr_ae hf.ae_eq_mk
  have B : âˆ«â» a, r * f a âˆ‚Î¼ = âˆ«â» a, r * hf.mk f a âˆ‚Î¼ :=
    lintegral_congr_ae (EventuallyEq.fun_comp hf.ae_eq_mk _)
  rw [A, B, lintegral_const_mul _ hf.measurable_mk]

theorem lintegral_const_mul_le (r : â„â‰¥0âˆ) (f : Î± â†’ â„â‰¥0âˆ) :
    r * âˆ«â» a, f a âˆ‚Î¼ â‰¤ âˆ«â» a, r * f a âˆ‚Î¼ := by
  rw [lintegral, ENNReal.mul_iSup]
  refine iSup_le fun s => ?_
  rw [ENNReal.mul_iSup, iSup_le_iff]
  intro hs
  rw [â† SimpleFunc.const_mul_lintegral, lintegral]
  refine le_iSup_of_le (const Î± r * s) (le_iSup_of_le (fun x => ?_) le_rfl)
  exact mul_le_mul_left' (hs x) _

theorem lintegral_const_mul' (r : â„â‰¥0âˆ) (f : Î± â†’ â„â‰¥0âˆ) (hr : r â‰  âˆ) :
    âˆ«â» a, r * f a âˆ‚Î¼ = r * âˆ«â» a, f a âˆ‚Î¼ := by
  by_cases h : r = 0
  Â· simp [h]
  apply le_antisymm _ (lintegral_const_mul_le r f)
  have rinv : r * râ»Â¹ = 1 := ENNReal.mul_inv_cancel h hr
  have rinv' : râ»Â¹ * r = 1 := by
    rw [mul_comm]
    exact rinv
  have := lintegral_const_mul_le (Î¼ := Î¼) râ»Â¹ fun x => r * f x
  simp? [(mul_assoc _ _ _).symm, rinv'] at this says
    simp only [(mul_assoc _ _ _).symm, rinv', one_mul] at this
  simpa [(mul_assoc _ _ _).symm, rinv] using mul_le_mul_left' this r

theorem lintegral_mul_const (r : â„â‰¥0âˆ) {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» a, f a * r âˆ‚Î¼ = (âˆ«â» a, f a âˆ‚Î¼) * r := by simp_rw [mul_comm, lintegral_const_mul r hf]

theorem lintegral_mul_const'' (r : â„â‰¥0âˆ) {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) :
    âˆ«â» a, f a * r âˆ‚Î¼ = (âˆ«â» a, f a âˆ‚Î¼) * r := by simp_rw [mul_comm, lintegral_const_mul'' r hf]

theorem lintegral_mul_const_le (r : â„â‰¥0âˆ) (f : Î± â†’ â„â‰¥0âˆ) :
    (âˆ«â» a, f a âˆ‚Î¼) * r â‰¤ âˆ«â» a, f a * r âˆ‚Î¼ := by
  simp_rw [mul_comm, lintegral_const_mul_le r f]

theorem lintegral_mul_const' (r : â„â‰¥0âˆ) (f : Î± â†’ â„â‰¥0âˆ) (hr : r â‰  âˆ) :
    âˆ«â» a, f a * r âˆ‚Î¼ = (âˆ«â» a, f a âˆ‚Î¼) * r := by simp_rw [mul_comm, lintegral_const_mul' r f hr]

/- A double integral of a product where each factor contains only one variable
  is a product of integrals -/
theorem lintegral_lintegral_mul {Î²} [MeasurableSpace Î²] {Î½ : Measure Î²} {f : Î± â†’ â„â‰¥0âˆ}
    {g : Î² â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î½) :
    âˆ«â» x, âˆ«â» y, f x * g y âˆ‚Î½ âˆ‚Î¼ = (âˆ«â» x, f x âˆ‚Î¼) * âˆ«â» y, g y âˆ‚Î½ := by
  simp [lintegral_const_mul'' _ hg, lintegral_mul_const'' _ hf]

-- TODO: Need a better way of rewriting inside of an integral
theorem lintegral_rwâ‚ {f f' : Î± â†’ Î²} (h : f =áµ[Î¼] f') (g : Î² â†’ â„â‰¥0âˆ) :
    âˆ«â» a, g (f a) âˆ‚Î¼ = âˆ«â» a, g (f' a) âˆ‚Î¼ :=
  lintegral_congr_ae <| h.mono fun a h => by dsimp only; rw [h]

-- TODO: Need a better way of rewriting inside of an integral
theorem lintegral_rwâ‚‚ {fâ‚ fâ‚' : Î± â†’ Î²} {fâ‚‚ fâ‚‚' : Î± â†’ Î³} (hâ‚ : fâ‚ =áµ[Î¼] fâ‚') (hâ‚‚ : fâ‚‚ =áµ[Î¼] fâ‚‚')
    (g : Î² â†’ Î³ â†’ â„â‰¥0âˆ) : âˆ«â» a, g (fâ‚ a) (fâ‚‚ a) âˆ‚Î¼ = âˆ«â» a, g (fâ‚' a) (fâ‚‚' a) âˆ‚Î¼ :=
  lintegral_congr_ae <| hâ‚.mp <| hâ‚‚.mono fun _ hâ‚‚ hâ‚ => by dsimp only; rw [hâ‚, hâ‚‚]

theorem lintegral_indicator_le (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î±) :
    âˆ«â» a, s.indicator f a âˆ‚Î¼ â‰¤ âˆ«â» a in s, f a âˆ‚Î¼ := by
  simp only [lintegral]
  apply iSup_le (fun g â†¦ (iSup_le (fun hg â†¦ ?_)))
  have : g â‰¤ f := hg.trans (indicator_le_self s f)
  refine le_iSup_of_le g (le_iSup_of_le this (le_of_eq ?_))
  rw [lintegral_restrict, SimpleFunc.lintegral]
  congr with t
  by_cases H : t = 0
  Â· simp [H]
  congr with x
  simp only [mem_preimage, mem_singleton_iff, mem_inter_iff, iff_self_and]
  rintro rfl
  contrapose! H
  simpa [H] using hg x

@[simp]
theorem lintegral_indicator {s : Set Î±} (hs : MeasurableSet s) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, s.indicator f a âˆ‚Î¼ = âˆ«â» a in s, f a âˆ‚Î¼ := by
  apply le_antisymm (lintegral_indicator_le f s)
  simp only [lintegral, â† restrict_lintegral_eq_lintegral_restrict _ hs, iSup_subtype']
  refine iSup_mono' (Subtype.forall.2 fun Ï† hÏ† => ?_)
  refine âŸ¨âŸ¨Ï†.restrict s, fun x => ?_âŸ©, le_rflâŸ©
  simp [hÏ† x, hs, indicator_le_indicator]

lemma setLIntegral_indicator {s t : Set Î±} (hs : MeasurableSet s) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in t, s.indicator f a âˆ‚Î¼ = âˆ«â» a in s âˆ© t, f a âˆ‚Î¼ := by
  rw [lintegral_indicator hs, Measure.restrict_restrict hs]

theorem lintegral_indicatorâ‚€ {s : Set Î±} (hs : NullMeasurableSet s Î¼) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a, s.indicator f a âˆ‚Î¼ = âˆ«â» a in s, f a âˆ‚Î¼ := by
  rw [â† lintegral_congr_ae (indicator_ae_eq_of_ae_eq_set hs.toMeasurable_ae_eq),
    lintegral_indicator (measurableSet_toMeasurable _ _),
    Measure.restrict_congr_set hs.toMeasurable_ae_eq]

lemma setLIntegral_indicatorâ‚€ (f : Î± â†’ â„â‰¥0âˆ) {s t : Set Î±}
    (hs : NullMeasurableSet s (Î¼.restrict t)) :
    âˆ«â» a in t, s.indicator f a âˆ‚Î¼ = âˆ«â» a in s âˆ© t, f a âˆ‚Î¼ := by
  rw [lintegral_indicatorâ‚€ hs, Measure.restrict_restrictâ‚€ hs]

theorem lintegral_indicator_const_le (s : Set Î±) (c : â„â‰¥0âˆ) :
    âˆ«â» a, s.indicator (fun _ => c) a âˆ‚Î¼ â‰¤ c * Î¼ s :=
  (lintegral_indicator_le _ _).trans (setLIntegral_const s c).le

theorem lintegral_indicator_constâ‚€ {s : Set Î±} (hs : NullMeasurableSet s Î¼) (c : â„â‰¥0âˆ) :
    âˆ«â» a, s.indicator (fun _ => c) a âˆ‚Î¼ = c * Î¼ s := by
  rw [lintegral_indicatorâ‚€ hs, setLIntegral_const]

theorem lintegral_indicator_const {s : Set Î±} (hs : MeasurableSet s) (c : â„â‰¥0âˆ) :
    âˆ«â» a, s.indicator (fun _ => c) a âˆ‚Î¼ = c * Î¼ s :=
  lintegral_indicator_constâ‚€ hs.nullMeasurableSet c

lemma setLIntegral_eq_of_support_subset {s : Set Î±} {f : Î± â†’ â„â‰¥0âˆ} (hsf : f.support âŠ† s) :
    âˆ«â» x in s, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by
  apply le_antisymm (setLIntegral_le_lintegral s fun x â†¦ f x)
  apply le_trans (le_of_eq _) (lintegral_indicator_le _ _)
  congr with x
  simp only [indicator]
  split_ifs with h
  Â· rfl
  Â· exact Function.support_subset_iff'.1 hsf x h

theorem setLIntegral_eq_const {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (r : â„â‰¥0âˆ) :
    âˆ«â» x in { x | f x = r }, f x âˆ‚Î¼ = r * Î¼ { x | f x = r } := by
  have : âˆ€áµ x âˆ‚Î¼, x âˆˆ { x | f x = r } â†’ f x = r := ae_of_all Î¼ fun _ hx => hx
  rw [setLIntegral_congr_fun _ this]
  Â· rw [lintegral_const, Measure.restrict_apply MeasurableSet.univ, Set.univ_inter]
  Â· exact hf (measurableSet_singleton r)

theorem lintegral_indicator_one_le (s : Set Î±) : âˆ«â» a, s.indicator 1 a âˆ‚Î¼ â‰¤ Î¼ s :=
  (lintegral_indicator_const_le _ _).trans <| (one_mul _).le

@[simp]
theorem lintegral_indicator_oneâ‚€ {s : Set Î±} (hs : NullMeasurableSet s Î¼) :
    âˆ«â» a, s.indicator 1 a âˆ‚Î¼ = Î¼ s :=
  (lintegral_indicator_constâ‚€ hs _).trans <| one_mul _

@[simp]
theorem lintegral_indicator_one {s : Set Î±} (hs : MeasurableSet s) :
    âˆ«â» a, s.indicator 1 a âˆ‚Î¼ = Î¼ s :=
  (lintegral_indicator_const hs _).trans <| one_mul _

theorem Measure.ext_iff_lintegral (Î½ : Measure Î±) :
    Î¼ = Î½ â†” âˆ€ f : Î± â†’ â„â‰¥0âˆ, Measurable f â†’ âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î½ := by
  refine âŸ¨fun h _ _ â†¦ by rw [h], ?_âŸ©
  intro h
  ext s hs
  simp only [â† lintegral_indicator_one hs]
  exact h (s.indicator 1) ((measurable_indicator_const_iff 1).mpr hs)

theorem Measure.ext_of_lintegral (Î½ : Measure Î±)
    (hÎ¼Î½ : âˆ€ f : Î± â†’ â„â‰¥0âˆ, Measurable f â†’ âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î½) : Î¼ = Î½ :=
  (Î¼.ext_iff_lintegral Î½).mpr hÎ¼Î½

/-- A version of **Markov's inequality** for two functions. It doesn't follow from the standard
Markov's inequality because we only assume measurability of `g`, not `f`. -/
theorem lintegral_add_mul_meas_add_le_le_lintegral {f g : Î± â†’ â„â‰¥0âˆ} (hle : f â‰¤áµ[Î¼] g)
    (hg : AEMeasurable g Î¼) (Îµ : â„â‰¥0âˆ) :
    âˆ«â» a, f a âˆ‚Î¼ + Îµ * Î¼ { x | f x + Îµ â‰¤ g x } â‰¤ âˆ«â» a, g a âˆ‚Î¼ := by
  rcases exists_measurable_le_lintegral_eq Î¼ f with âŸ¨Ï†, hÏ†m, hÏ†_le, hÏ†_eqâŸ©
  calc
    âˆ«â» x, f x âˆ‚Î¼ + Îµ * Î¼ { x | f x + Îµ â‰¤ g x } = âˆ«â» x, Ï† x âˆ‚Î¼ + Îµ * Î¼ { x | f x + Îµ â‰¤ g x } := by
      rw [hÏ†_eq]
    _ â‰¤ âˆ«â» x, Ï† x âˆ‚Î¼ + Îµ * Î¼ { x | Ï† x + Îµ â‰¤ g x } := by
      gcongr
      exact fun x => (add_le_add_right (hÏ†_le _) _).trans
    _ = âˆ«â» x, Ï† x + indicator { x | Ï† x + Îµ â‰¤ g x } (fun _ => Îµ) x âˆ‚Î¼ := by
      rw [lintegral_add_left hÏ†m, lintegral_indicatorâ‚€, setLIntegral_const]
      exact measurableSet_le (hÏ†m.nullMeasurable.measurable'.add_const _) hg.nullMeasurable
    _ â‰¤ âˆ«â» x, g x âˆ‚Î¼ := lintegral_mono_ae (hle.mono fun x hxâ‚ => ?_)
  simp only [indicator_apply]; split_ifs with hxâ‚‚
  exacts [hxâ‚‚, (add_zero _).trans_le <| (hÏ†_le x).trans hxâ‚]

/-- **Markov's inequality** also known as **Chebyshev's first inequality**. -/
theorem mul_meas_ge_le_lintegralâ‚€ {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (Îµ : â„â‰¥0âˆ) :
    Îµ * Î¼ { x | Îµ â‰¤ f x } â‰¤ âˆ«â» a, f a âˆ‚Î¼ := by
  simpa only [lintegral_zero, zero_add] using
    lintegral_add_mul_meas_add_le_le_lintegral (ae_of_all _ fun x => zero_le (f x)) hf Îµ

/-- **Markov's inequality** also known as **Chebyshev's first inequality**. For a version assuming
`AEMeasurable`, see `mul_meas_ge_le_lintegralâ‚€`. -/
theorem mul_meas_ge_le_lintegral {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (Îµ : â„â‰¥0âˆ) :
    Îµ * Î¼ { x | Îµ â‰¤ f x } â‰¤ âˆ«â» a, f a âˆ‚Î¼ :=
  mul_meas_ge_le_lintegralâ‚€ hf.aemeasurable Îµ

lemma meas_le_lintegralâ‚€ {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    {s : Set Î±} (hs : âˆ€ x âˆˆ s, 1 â‰¤ f x) : Î¼ s â‰¤ âˆ«â» a, f a âˆ‚Î¼ := by
  apply le_trans _ (mul_meas_ge_le_lintegralâ‚€ hf 1)
  rw [one_mul]
  exact measure_mono hs

lemma lintegral_le_meas {s : Set Î±} {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ a, f a â‰¤ 1) (h'f : âˆ€ a âˆˆ sá¶œ, f a = 0) :
    âˆ«â» a, f a âˆ‚Î¼ â‰¤ Î¼ s := by
  apply (lintegral_mono (fun x â†¦ ?_)).trans (lintegral_indicator_one_le s)
  by_cases hx : x âˆˆ s
  Â· simpa [hx] using hf x
  Â· simpa [hx] using h'f x hx

lemma setLIntegral_le_meas {s t : Set Î±} (hs : MeasurableSet s)
    {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ a âˆˆ s, a âˆˆ t â†’ f a â‰¤ 1)
    (hf' : âˆ€ a âˆˆ s, a âˆ‰ t â†’ f a = 0) : âˆ«â» a in s, f a âˆ‚Î¼ â‰¤ Î¼ t := by
  rw [â† lintegral_indicator hs]
  refine lintegral_le_meas (fun a â†¦ ?_) (by aesop)
  by_cases has : a âˆˆ s <;> [by_cases hat : a âˆˆ t; skip] <;> simp [*]

theorem lintegral_eq_top_of_measure_eq_top_ne_zero {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼)
    (hÎ¼f : Î¼ {x | f x = âˆ} â‰  0) : âˆ«â» x, f x âˆ‚Î¼ = âˆ :=
  eq_top_iff.mpr <|
    calc
      âˆ = âˆ * Î¼ { x | âˆ â‰¤ f x } := by simp [mul_eq_top, hÎ¼f]
      _ â‰¤ âˆ«â» x, f x âˆ‚Î¼ := mul_meas_ge_le_lintegralâ‚€ hf âˆ

theorem setLintegral_eq_top_of_measure_eq_top_ne_zero {f : Î± â†’ â„â‰¥0âˆ} {s : Set Î±}
    (hf : AEMeasurable f (Î¼.restrict s)) (hÎ¼f : Î¼ ({x âˆˆ s | f x = âˆ}) â‰  0) :
    âˆ«â» x in s, f x âˆ‚Î¼ = âˆ :=
  lintegral_eq_top_of_measure_eq_top_ne_zero hf <|
    mt (eq_bot_mono <| by rw [â† setOf_inter_eq_sep]; exact Measure.le_restrict_apply _ _) hÎ¼f

theorem measure_eq_top_of_lintegral_ne_top {f : Î± â†’ â„â‰¥0âˆ}
    (hf : AEMeasurable f Î¼) (hÎ¼f : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) : Î¼ {x | f x = âˆ} = 0 :=
  of_not_not fun h => hÎ¼f <| lintegral_eq_top_of_measure_eq_top_ne_zero hf h

theorem measure_eq_top_of_setLintegral_ne_top {f : Î± â†’ â„â‰¥0âˆ} {s : Set Î±}
    (hf : AEMeasurable f (Î¼.restrict s)) (hÎ¼f : âˆ«â» x in s, f x âˆ‚Î¼ â‰  âˆ) :
    Î¼ ({x âˆˆ s | f x = âˆ}) = 0 :=
  of_not_not fun h => hÎ¼f <| setLintegral_eq_top_of_measure_eq_top_ne_zero hf h

/-- **Markov's inequality**, also known as **Chebyshev's first inequality**. -/
theorem meas_ge_le_lintegral_div {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0)
    (hÎµ' : Îµ â‰  âˆ) : Î¼ { x | Îµ â‰¤ f x } â‰¤ (âˆ«â» a, f a âˆ‚Î¼) / Îµ :=
  (ENNReal.le_div_iff_mul_le (Or.inl hÎµ) (Or.inl hÎµ')).2 <| by
    rw [mul_comm]
    exact mul_meas_ge_le_lintegralâ‚€ hf Îµ

theorem ae_eq_of_ae_le_of_lintegral_le {f g : Î± â†’ â„â‰¥0âˆ} (hfg : f â‰¤áµ[Î¼] g) (hf : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ)
    (hg : AEMeasurable g Î¼) (hgf : âˆ«â» x, g x âˆ‚Î¼ â‰¤ âˆ«â» x, f x âˆ‚Î¼) : f =áµ[Î¼] g := by
  have : âˆ€ n : â„•, âˆ€áµ x âˆ‚Î¼, g x < f x + (n : â„â‰¥0âˆ)â»Â¹ := by
    intro n
    simp only [ae_iff, not_lt]
    have : âˆ«â» x, f x âˆ‚Î¼ + (â†‘n)â»Â¹ * Î¼ { x : Î± | f x + (n : â„â‰¥0âˆ)â»Â¹ â‰¤ g x } â‰¤ âˆ«â» x, f x âˆ‚Î¼ :=
      (lintegral_add_mul_meas_add_le_le_lintegral hfg hg nâ»Â¹).trans hgf
    rw [(ENNReal.cancel_of_ne hf).add_le_iff_nonpos_right, nonpos_iff_eq_zero, mul_eq_zero] at this
    exact this.resolve_left (ENNReal.inv_ne_zero.2 (ENNReal.natCast_ne_top _))
  refine hfg.mp ((ae_all_iff.2 this).mono fun x hlt hle => hle.antisymm ?_)
  suffices Tendsto (fun n : â„• => f x + (n : â„â‰¥0âˆ)â»Â¹) atTop (ğ“ (f x)) from
    ge_of_tendsto' this fun i => (hlt i).le
  simpa only [inv_top, add_zero] using
    tendsto_const_nhds.add (ENNReal.tendsto_inv_iff.2 ENNReal.tendsto_nat_nhds_top)

@[simp]
theorem lintegral_eq_zero_iff' {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) :
    âˆ«â» a, f a âˆ‚Î¼ = 0 â†” f =áµ[Î¼] 0 :=
  have : âˆ«â» _ : Î±, 0 âˆ‚Î¼ â‰  âˆ := by simp [lintegral_zero, zero_ne_top]
  âŸ¨fun h =>
    (ae_eq_of_ae_le_of_lintegral_le (ae_of_all _ <| zero_le f) this hf
        (h.trans lintegral_zero.symm).le).symm,
    fun h => (lintegral_congr_ae h).trans lintegral_zeroâŸ©

@[simp]
theorem lintegral_eq_zero_iff {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) : âˆ«â» a, f a âˆ‚Î¼ = 0 â†” f =áµ[Î¼] 0 :=
  lintegral_eq_zero_iff' hf.aemeasurable

theorem setLIntegral_eq_zero_iff' {s : Set Î±} (hs : MeasurableSet s)
    {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f (Î¼.restrict s)) :
    âˆ«â» a in s, f a âˆ‚Î¼ = 0 â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = 0 :=
  (lintegral_eq_zero_iff' hf).trans (ae_restrict_iff' hs)

theorem setLIntegral_eq_zero_iff {s : Set Î±} (hs : MeasurableSet s) {f : Î± â†’ â„â‰¥0âˆ}
    (hf : Measurable f) : âˆ«â» a in s, f a âˆ‚Î¼ = 0 â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = 0 :=
  setLIntegral_eq_zero_iff' hs hf.aemeasurable

theorem lintegral_pos_iff_support {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    (0 < âˆ«â» a, f a âˆ‚Î¼) â†” 0 < Î¼ (Function.support f) := by
  simp [pos_iff_ne_zero, hf, Filter.EventuallyEq, ae_iff, Function.support]

theorem setLintegral_pos_iff {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) {s : Set Î±} :
    0 < âˆ«â» a in s, f a âˆ‚Î¼ â†” 0 < Î¼ (Function.support f âˆ© s) := by
  rw [lintegral_pos_iff_support hf, Measure.restrict_apply (measurableSet_support hf)]

/-- Weaker version of the monotone convergence theorem -/
theorem lintegral_iSup_ae {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ n, Measurable (f n))
    (h_mono : âˆ€ n, âˆ€áµ a âˆ‚Î¼, f n a â‰¤ f n.succ a) : âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by
  classical
  let âŸ¨s, hsâŸ© := exists_measurable_superset_of_null (ae_iff.1 (ae_all_iff.2 h_mono))
  let g n a := if a âˆˆ s then 0 else f n a
  have g_eq_f : âˆ€áµ a âˆ‚Î¼, âˆ€ n, g n a = f n a :=
    (measure_zero_iff_ae_nmem.1 hs.2.2).mono fun a ha n => if_neg ha
  calc
    âˆ«â» a, â¨† n, f n a âˆ‚Î¼ = âˆ«â» a, â¨† n, g n a âˆ‚Î¼ :=
      lintegral_congr_ae <| g_eq_f.mono fun a ha => by simp only [ha]
    _ = â¨† n, âˆ«â» a, g n a âˆ‚Î¼ :=
      (lintegral_iSup (fun n => measurable_const.piecewise hs.2.1 (hf n))
        (monotone_nat_of_le_succ fun n a => ?_))
    _ = â¨† n, âˆ«â» a, f n a âˆ‚Î¼ := by simp only [lintegral_congr_ae (g_eq_f.mono fun _a ha => ha _)]
  simp only [g]
  split_ifs with h
  Â· rfl
  Â· have := Set.not_mem_subset hs.1 h
    simp only [not_forall, not_le, mem_setOf_eq, not_exists, not_lt] at this
    exact this n

theorem lintegral_sub' {f g : Î± â†’ â„â‰¥0âˆ} (hg : AEMeasurable g Î¼) (hg_fin : âˆ«â» a, g a âˆ‚Î¼ â‰  âˆ)
    (h_le : g â‰¤áµ[Î¼] f) : âˆ«â» a, f a - g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ - âˆ«â» a, g a âˆ‚Î¼ := by
  refine ENNReal.eq_sub_of_add_eq hg_fin ?_
  rw [â† lintegral_add_right' _ hg]
  exact lintegral_congr_ae (h_le.mono fun x hx => tsub_add_cancel_of_le hx)

theorem lintegral_sub {f g : Î± â†’ â„â‰¥0âˆ} (hg : Measurable g) (hg_fin : âˆ«â» a, g a âˆ‚Î¼ â‰  âˆ)
    (h_le : g â‰¤áµ[Î¼] f) : âˆ«â» a, f a - g a âˆ‚Î¼ = âˆ«â» a, f a âˆ‚Î¼ - âˆ«â» a, g a âˆ‚Î¼ :=
  lintegral_sub' hg.aemeasurable hg_fin h_le

theorem lintegral_sub_le' (f g : Î± â†’ â„â‰¥0âˆ) (hf : AEMeasurable f Î¼) :
    âˆ«â» x, g x âˆ‚Î¼ - âˆ«â» x, f x âˆ‚Î¼ â‰¤ âˆ«â» x, g x - f x âˆ‚Î¼ := by
  rw [tsub_le_iff_right]
  by_cases hfi : âˆ«â» x, f x âˆ‚Î¼ = âˆ
  Â· rw [hfi, add_top]
    exact le_top
  Â· rw [â† lintegral_add_right' _ hf]
    gcongr
    exact le_tsub_add

theorem lintegral_sub_le (f g : Î± â†’ â„â‰¥0âˆ) (hf : Measurable f) :
    âˆ«â» x, g x âˆ‚Î¼ - âˆ«â» x, f x âˆ‚Î¼ â‰¤ âˆ«â» x, g x - f x âˆ‚Î¼ :=
  lintegral_sub_le' f g hf.aemeasurable

theorem lintegral_strict_mono_of_ae_le_of_frequently_ae_lt {f g : Î± â†’ â„â‰¥0âˆ} (hg : AEMeasurable g Î¼)
    (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) (h_le : f â‰¤áµ[Î¼] g) (h : âˆƒáµ x âˆ‚Î¼, f x â‰  g x) :
    âˆ«â» x, f x âˆ‚Î¼ < âˆ«â» x, g x âˆ‚Î¼ := by
  contrapose! h
  simp only [not_frequently, Ne, Classical.not_not]
  exact ae_eq_of_ae_le_of_lintegral_le h_le hfi hg h

theorem lintegral_strict_mono_of_ae_le_of_ae_lt_on {f g : Î± â†’ â„â‰¥0âˆ} (hg : AEMeasurable g Î¼)
    (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) (h_le : f â‰¤áµ[Î¼] g) {s : Set Î±} (hÎ¼s : Î¼ s â‰  0)
    (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x < g x) : âˆ«â» x, f x âˆ‚Î¼ < âˆ«â» x, g x âˆ‚Î¼ :=
  lintegral_strict_mono_of_ae_le_of_frequently_ae_lt hg hfi h_le <|
    ((frequently_ae_mem_iff.2 hÎ¼s).and_eventually h).mono fun _x hx => (hx.2 hx.1).ne

theorem lintegral_strict_mono {f g : Î± â†’ â„â‰¥0âˆ} (hÎ¼ : Î¼ â‰  0) (hg : AEMeasurable g Î¼)
    (hfi : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) (h : âˆ€áµ x âˆ‚Î¼, f x < g x) : âˆ«â» x, f x âˆ‚Î¼ < âˆ«â» x, g x âˆ‚Î¼ := by
  rw [Ne, â† Measure.measure_univ_eq_zero] at hÎ¼
  refine lintegral_strict_mono_of_ae_le_of_ae_lt_on hg hfi (ae_le_of_ae_lt h) hÎ¼ ?_
  simpa using h

theorem setLIntegral_strict_mono {f g : Î± â†’ â„â‰¥0âˆ} {s : Set Î±} (hsm : MeasurableSet s)
    (hs : Î¼ s â‰  0) (hg : Measurable g) (hfi : âˆ«â» x in s, f x âˆ‚Î¼ â‰  âˆ)
    (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x < g x) : âˆ«â» x in s, f x âˆ‚Î¼ < âˆ«â» x in s, g x âˆ‚Î¼ :=
  lintegral_strict_mono (by simp [hs]) hg.aemeasurable hfi ((ae_restrict_iff' hsm).mpr h)

/-- Monotone convergence theorem for nonincreasing sequences of functions -/
theorem lintegral_iInf_ae {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, Measurable (f n))
    (h_mono : âˆ€ n : â„•, f n.succ â‰¤áµ[Î¼] f n) (h_fin : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) :
    âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = â¨… n, âˆ«â» a, f n a âˆ‚Î¼ :=
  have fn_le_f0 : âˆ«â» a, â¨… n, f n a âˆ‚Î¼ â‰¤ âˆ«â» a, f 0 a âˆ‚Î¼ :=
    lintegral_mono fun _ => iInf_le_of_le 0 le_rfl
  have fn_le_f0' : â¨… n, âˆ«â» a, f n a âˆ‚Î¼ â‰¤ âˆ«â» a, f 0 a âˆ‚Î¼ := iInf_le_of_le 0 le_rfl
  (ENNReal.sub_right_inj h_fin fn_le_f0 fn_le_f0').1 <|
    show âˆ«â» a, f 0 a âˆ‚Î¼ - âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = âˆ«â» a, f 0 a âˆ‚Î¼ - â¨… n, âˆ«â» a, f n a âˆ‚Î¼ from
      calc
        âˆ«â» a, f 0 a âˆ‚Î¼ - âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = âˆ«â» a, f 0 a - â¨… n, f n a âˆ‚Î¼ :=
          (lintegral_sub (.iInf h_meas)
              (ne_top_of_le_ne_top h_fin <| lintegral_mono fun _ => iInf_le _ _)
              (ae_of_all _ fun _ => iInf_le _ _)).symm
        _ = âˆ«â» a, â¨† n, f 0 a - f n a âˆ‚Î¼ := congr rfl (funext fun _ => ENNReal.sub_iInf)
        _ = â¨† n, âˆ«â» a, f 0 a - f n a âˆ‚Î¼ :=
          (lintegral_iSup_ae (fun n => (h_meas 0).sub (h_meas n)) fun n =>
            (h_mono n).mono fun _ ha => tsub_le_tsub le_rfl ha)
        _ = â¨† n, âˆ«â» a, f 0 a âˆ‚Î¼ - âˆ«â» a, f n a âˆ‚Î¼ :=
          (have h_mono : âˆ€áµ a âˆ‚Î¼, âˆ€ n : â„•, f n.succ a â‰¤ f n a := ae_all_iff.2 h_mono
          have h_mono : âˆ€ n, âˆ€áµ a âˆ‚Î¼, f n a â‰¤ f 0 a := fun n =>
            h_mono.mono fun a h => by
              induction' n with n ih
              Â· exact le_rfl
              Â· exact le_trans (h n) ih
          congr_arg iSup <|
            funext fun n =>
              lintegral_sub (h_meas _) (ne_top_of_le_ne_top h_fin <| lintegral_mono_ae <| h_mono n)
                (h_mono n))
        _ = âˆ«â» a, f 0 a âˆ‚Î¼ - â¨… n, âˆ«â» a, f n a âˆ‚Î¼ := ENNReal.sub_iInf.symm

/-- Monotone convergence theorem for nonincreasing sequences of functions -/
theorem lintegral_iInf {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, Measurable (f n)) (h_anti : Antitone f)
    (h_fin : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) : âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = â¨… n, âˆ«â» a, f n a âˆ‚Î¼ :=
  lintegral_iInf_ae h_meas (fun n => ae_of_all _ <| h_anti n.le_succ) h_fin

theorem lintegral_iInf' {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, AEMeasurable (f n) Î¼)
    (h_anti : âˆ€áµ a âˆ‚Î¼, Antitone (fun i â†¦ f i a)) (h_fin : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ) :
    âˆ«â» a, â¨… n, f n a âˆ‚Î¼ = â¨… n, âˆ«â» a, f n a âˆ‚Î¼ := by
  simp_rw [â† iInf_apply]
  let p : Î± â†’ (â„• â†’ â„â‰¥0âˆ) â†’ Prop := fun _ f' => Antitone f'
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun i => f i x := h_anti
  have h_ae_seq_mono : Antitone (aeSeq h_meas p) := by
    intro n m hnm x
    by_cases hx : x âˆˆ aeSeqSet h_meas p
    Â· exact aeSeq.prop_of_mem_aeSeqSet h_meas hx hnm
    Â· simp only [aeSeq, hx, if_false]
      exact le_rfl
  rw [lintegral_congr_ae (aeSeq.iInf h_meas hp).symm]
  simp_rw [iInf_apply]
  rw [lintegral_iInf (aeSeq.measurable h_meas p) h_ae_seq_mono]
  Â· congr
    exact funext fun n â†¦ lintegral_congr_ae (aeSeq.aeSeq_n_eq_fun_n_ae h_meas hp n)
  Â· rwa [lintegral_congr_ae (aeSeq.aeSeq_n_eq_fun_n_ae h_meas hp 0)]

/-- Monotone convergence for an infimum over a directed family and indexed by a countable type -/
theorem lintegral_iInf_directed_of_measurable {mÎ± : MeasurableSpace Î±} [Countable Î²]
    {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} {Î¼ : Measure Î±} (hÎ¼ : Î¼ â‰  0) (hf : âˆ€ b, Measurable (f b))
    (hf_int : âˆ€ b, âˆ«â» a, f b a âˆ‚Î¼ â‰  âˆ) (h_directed : Directed (Â· â‰¥ Â·) f) :
    âˆ«â» a, â¨… b, f b a âˆ‚Î¼ = â¨… b, âˆ«â» a, f b a âˆ‚Î¼ := by
  cases nonempty_encodable Î²
  cases isEmpty_or_nonempty Î²
  Â· simp only [iInf_of_empty, lintegral_const,
      ENNReal.top_mul (Measure.measure_univ_ne_zero.mpr hÎ¼)]
  inhabit Î²
  have : âˆ€ a, â¨… b, f b a = â¨… n, f (h_directed.sequence f n) a := by
    refine fun a =>
      le_antisymm (le_iInf fun n => iInf_le _ _)
        (le_iInf fun b => iInf_le_of_le (Encodable.encode b + 1) ?_)
    exact h_directed.sequence_le b a
  calc
    âˆ«â» a, â¨… b, f b a âˆ‚Î¼
    _ = âˆ«â» a, â¨… n, (f âˆ˜ h_directed.sequence f) n a âˆ‚Î¼ := by simp only [this, Function.comp_apply]
    _ = â¨… n, âˆ«â» a, (f âˆ˜ h_directed.sequence f) n a âˆ‚Î¼ := by
      rw [lintegral_iInf ?_ h_directed.sequence_anti]
      Â· exact hf_int _
      Â· exact fun n => hf _
    _ = â¨… b, âˆ«â» a, f b a âˆ‚Î¼ := by
      refine le_antisymm (le_iInf fun b => ?_) (le_iInf fun n => ?_)
      Â· exact iInf_le_of_le (Encodable.encode b + 1) (lintegral_mono <| h_directed.sequence_le b)
      Â· exact iInf_le (fun b => âˆ«â» a, f b a âˆ‚Î¼) _

/-- Known as Fatou's lemma, version with `AEMeasurable` functions -/
theorem lintegral_liminf_le' {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, AEMeasurable (f n) Î¼) :
    âˆ«â» a, liminf (fun n => f n a) atTop âˆ‚Î¼ â‰¤ liminf (fun n => âˆ«â» a, f n a âˆ‚Î¼) atTop :=
  calc
    âˆ«â» a, liminf (fun n => f n a) atTop âˆ‚Î¼ = âˆ«â» a, â¨† n : â„•, â¨… i â‰¥ n, f i a âˆ‚Î¼ := by
      simp only [liminf_eq_iSup_iInf_of_nat]
    _ = â¨† n : â„•, âˆ«â» a, â¨… i â‰¥ n, f i a âˆ‚Î¼ :=
      (lintegral_iSup' (fun _ => .biInf _ (to_countable _) (fun i _ â†¦ h_meas i))
        (ae_of_all Î¼ fun _ _ _ hnm => iInf_le_iInf_of_subset fun _ hi => le_trans hnm hi))
    _ â‰¤ â¨† n : â„•, â¨… i â‰¥ n, âˆ«â» a, f i a âˆ‚Î¼ := iSup_mono fun _ => le_iInfâ‚‚_lintegral _
    _ = atTop.liminf fun n => âˆ«â» a, f n a âˆ‚Î¼ := Filter.liminf_eq_iSup_iInf_of_nat.symm

/-- Known as Fatou's lemma -/
theorem lintegral_liminf_le {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} (h_meas : âˆ€ n, Measurable (f n)) :
    âˆ«â» a, liminf (fun n => f n a) atTop âˆ‚Î¼ â‰¤ liminf (fun n => âˆ«â» a, f n a âˆ‚Î¼) atTop :=
  lintegral_liminf_le' fun n => (h_meas n).aemeasurable

theorem lintegral_tendsto_of_tendsto_of_antitone {f : â„• â†’ Î± â†’ â„â‰¥0âˆ} {F : Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ n, AEMeasurable (f n) Î¼) (h_anti : âˆ€áµ x âˆ‚Î¼, Antitone fun n â†¦ f n x)
    (h0 : âˆ«â» a, f 0 a âˆ‚Î¼ â‰  âˆ)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (F x))) :
    Tendsto (fun n â†¦ âˆ«â» x, f n x âˆ‚Î¼) atTop (ğ“ (âˆ«â» x, F x âˆ‚Î¼)) := by
  have : Antitone fun n â†¦ âˆ«â» x, f n x âˆ‚Î¼ := fun i j hij â†¦
    lintegral_mono_ae (h_anti.mono fun x hx â†¦ hx hij)
  suffices key : âˆ«â» x, F x âˆ‚Î¼ = â¨… n, âˆ«â» x, f n x âˆ‚Î¼ by
    rw [key]
    exact tendsto_atTop_iInf this
  rw [â† lintegral_iInf' hf h_anti h0]
  refine lintegral_congr_ae ?_
  filter_upwards [h_anti, h_tendsto] with _ hx_anti hx_tendsto
    using tendsto_nhds_unique hx_tendsto (tendsto_atTop_iInf hx_anti)

open Encodable in
/-- Monotone convergence for a supremum over a directed family and indexed by a countable type -/
theorem lintegral_iSup_directed_of_measurable [Countable Î²] {f : Î² â†’ Î± â†’ â„â‰¥0âˆ}
    (hf : âˆ€ b, Measurable (f b)) (h_directed : Directed (Â· â‰¤ Â·) f) :
    âˆ«â» a, â¨† b, f b a âˆ‚Î¼ = â¨† b, âˆ«â» a, f b a âˆ‚Î¼ := by
  cases nonempty_encodable Î²
  cases isEmpty_or_nonempty Î²
  Â· simp [iSup_of_empty]
  inhabit Î²
  have : âˆ€ a, â¨† b, f b a = â¨† n, f (h_directed.sequence f n) a := by
    intro a
    refine le_antisymm (iSup_le fun b => ?_) (iSup_le fun n => le_iSup (fun n => f n a) _)
    exact le_iSup_of_le (encode b + 1) (h_directed.le_sequence b a)
  calc
    âˆ«â» a, â¨† b, f b a âˆ‚Î¼ = âˆ«â» a, â¨† n, f (h_directed.sequence f n) a âˆ‚Î¼ := by simp only [this]
    _ = â¨† n, âˆ«â» a, f (h_directed.sequence f n) a âˆ‚Î¼ :=
      (lintegral_iSup (fun n => hf _) h_directed.sequence_mono)
    _ = â¨† b, âˆ«â» a, f b a âˆ‚Î¼ := by
      refine le_antisymm (iSup_le fun n => ?_) (iSup_le fun b => ?_)
      Â· exact le_iSup (fun b => âˆ«â» a, f b a âˆ‚Î¼) _
      Â· exact le_iSup_of_le (encode b + 1) (lintegral_mono <| h_directed.le_sequence b)

/-- Monotone convergence for a supremum over a directed family and indexed by a countable type. -/
theorem lintegral_iSup_directed [Countable Î²] {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ b, AEMeasurable (f b) Î¼)
    (h_directed : Directed (Â· â‰¤ Â·) f) : âˆ«â» a, â¨† b, f b a âˆ‚Î¼ = â¨† b, âˆ«â» a, f b a âˆ‚Î¼ := by
  simp_rw [â† iSup_apply]
  let p : Î± â†’ (Î² â†’ ENNReal) â†’ Prop := fun x f' => Directed LE.le f'
  have hp : âˆ€áµ x âˆ‚Î¼, p x fun i => f i x := by
    filter_upwards [] with x i j
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h_directed i j
    exact âŸ¨z, hzâ‚ x, hzâ‚‚ xâŸ©
  have h_ae_seq_directed : Directed LE.le (aeSeq hf p) := by
    intro bâ‚ bâ‚‚
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h_directed bâ‚ bâ‚‚
    refine âŸ¨z, ?_, ?_âŸ© <;>
      Â· intro x
        by_cases hx : x âˆˆ aeSeqSet hf p
        Â· repeat rw [aeSeq.aeSeq_eq_fun_of_mem_aeSeqSet hf hx]
          apply_rules [hzâ‚, hzâ‚‚]
        Â· simp only [aeSeq, hx, if_false]
          exact le_rfl
  convert lintegral_iSup_directed_of_measurable (aeSeq.measurable hf p) h_ae_seq_directed using 1
  Â· simp_rw [â† iSup_apply]
    rw [lintegral_congr_ae (aeSeq.iSup hf hp).symm]
  Â· congr 1
    ext1 b
    rw [lintegral_congr_ae]
    apply EventuallyEq.symm
    exact aeSeq.aeSeq_n_eq_fun_n_ae hf hp _

theorem lintegral_tsum [Countable Î²] {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ i, AEMeasurable (f i) Î¼) :
    âˆ«â» a, âˆ‘' i, f i a âˆ‚Î¼ = âˆ‘' i, âˆ«â» a, f i a âˆ‚Î¼ := by
  classical
  simp only [ENNReal.tsum_eq_iSup_sum]
  rw [lintegral_iSup_directed]
  Â· simp [lintegral_finset_sum' _ fun i _ => hf i]
  Â· intro b
    exact Finset.aemeasurable_sum _ fun i _ => hf i
  Â· intro s t
    use s âˆª t
    constructor
    Â· exact fun a => Finset.sum_le_sum_of_subset Finset.subset_union_left
    Â· exact fun a => Finset.sum_le_sum_of_subset Finset.subset_union_right

open Measure

open scoped Function -- required for scoped `on` notation

theorem lintegral_iUnionâ‚€ [Countable Î²] {s : Î² â†’ Set Î±} (hm : âˆ€ i, NullMeasurableSet (s i) Î¼)
    (hd : Pairwise (AEDisjoint Î¼ on s)) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in â‹ƒ i, s i, f a âˆ‚Î¼ = âˆ‘' i, âˆ«â» a in s i, f a âˆ‚Î¼ := by
  simp only [Measure.restrict_iUnion_ae hd hm, lintegral_sum_measure]

theorem lintegral_iUnion [Countable Î²] {s : Î² â†’ Set Î±} (hm : âˆ€ i, MeasurableSet (s i))
    (hd : Pairwise (Disjoint on s)) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in â‹ƒ i, s i, f a âˆ‚Î¼ = âˆ‘' i, âˆ«â» a in s i, f a âˆ‚Î¼ :=
  lintegral_iUnionâ‚€ (fun i => (hm i).nullMeasurableSet) hd.aedisjoint f

theorem lintegral_biUnionâ‚€ {t : Set Î²} {s : Î² â†’ Set Î±} (ht : t.Countable)
    (hm : âˆ€ i âˆˆ t, NullMeasurableSet (s i) Î¼) (hd : t.Pairwise (AEDisjoint Î¼ on s)) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in â‹ƒ i âˆˆ t, s i, f a âˆ‚Î¼ = âˆ‘' i : t, âˆ«â» a in s i, f a âˆ‚Î¼ := by
  haveI := ht.toEncodable
  rw [biUnion_eq_iUnion, lintegral_iUnionâ‚€ (SetCoe.forall'.1 hm) (hd.subtype _ _)]

theorem lintegral_biUnion {t : Set Î²} {s : Î² â†’ Set Î±} (ht : t.Countable)
    (hm : âˆ€ i âˆˆ t, MeasurableSet (s i)) (hd : t.PairwiseDisjoint s) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in â‹ƒ i âˆˆ t, s i, f a âˆ‚Î¼ = âˆ‘' i : t, âˆ«â» a in s i, f a âˆ‚Î¼ :=
  lintegral_biUnionâ‚€ ht (fun i hi => (hm i hi).nullMeasurableSet) hd.aedisjoint f

theorem lintegral_biUnion_finsetâ‚€ {s : Finset Î²} {t : Î² â†’ Set Î±}
    (hd : Set.Pairwise (â†‘s) (AEDisjoint Î¼ on t)) (hm : âˆ€ b âˆˆ s, NullMeasurableSet (t b) Î¼)
    (f : Î± â†’ â„â‰¥0âˆ) : âˆ«â» a in â‹ƒ b âˆˆ s, t b, f a âˆ‚Î¼ = âˆ‘ b âˆˆ s, âˆ«â» a in t b, f a âˆ‚Î¼ := by
  simp only [â† Finset.mem_coe, lintegral_biUnionâ‚€ s.countable_toSet hm hd, â† Finset.tsum_subtype']

theorem lintegral_biUnion_finset {s : Finset Î²} {t : Î² â†’ Set Î±} (hd : Set.PairwiseDisjoint (â†‘s) t)
    (hm : âˆ€ b âˆˆ s, MeasurableSet (t b)) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in â‹ƒ b âˆˆ s, t b, f a âˆ‚Î¼ = âˆ‘ b âˆˆ s, âˆ«â» a in t b, f a âˆ‚Î¼ :=
  lintegral_biUnion_finsetâ‚€ hd.aedisjoint (fun b hb => (hm b hb).nullMeasurableSet) f

theorem lintegral_iUnion_le [Countable Î²] (s : Î² â†’ Set Î±) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» a in â‹ƒ i, s i, f a âˆ‚Î¼ â‰¤ âˆ‘' i, âˆ«â» a in s i, f a âˆ‚Î¼ := by
  rw [â† lintegral_sum_measure]
  exact lintegral_mono' restrict_iUnion_le le_rfl

theorem lintegral_union {f : Î± â†’ â„â‰¥0âˆ} {A B : Set Î±} (hB : MeasurableSet B) (hAB : Disjoint A B) :
    âˆ«â» a in A âˆª B, f a âˆ‚Î¼ = âˆ«â» a in A, f a âˆ‚Î¼ + âˆ«â» a in B, f a âˆ‚Î¼ := by
  rw [restrict_union hAB hB, lintegral_add_measure]

theorem lintegral_union_le (f : Î± â†’ â„â‰¥0âˆ) (s t : Set Î±) :
    âˆ«â» a in s âˆª t, f a âˆ‚Î¼ â‰¤ âˆ«â» a in s, f a âˆ‚Î¼ + âˆ«â» a in t, f a âˆ‚Î¼ := by
  rw [â† lintegral_add_measure]
  exact lintegral_mono' (restrict_union_le _ _) le_rfl

theorem lintegral_inter_add_diff {B : Set Î±} (f : Î± â†’ â„â‰¥0âˆ) (A : Set Î±) (hB : MeasurableSet B) :
    âˆ«â» x in A âˆ© B, f x âˆ‚Î¼ + âˆ«â» x in A \ B, f x âˆ‚Î¼ = âˆ«â» x in A, f x âˆ‚Î¼ := by
  rw [â† lintegral_add_measure, restrict_inter_add_diff _ hB]

theorem lintegral_add_compl (f : Î± â†’ â„â‰¥0âˆ) {A : Set Î±} (hA : MeasurableSet A) :
    âˆ«â» x in A, f x âˆ‚Î¼ + âˆ«â» x in Aá¶œ, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ := by
  rw [â† lintegral_add_measure, Measure.restrict_add_restrict_compl hA]

lemma lintegral_piecewise (hs : MeasurableSet s) (f g : Î± â†’ â„â‰¥0âˆ) [âˆ€ j, Decidable (j âˆˆ s)] :
    âˆ«â» a, s.piecewise f g a âˆ‚Î¼ = âˆ«â» a in s, f a âˆ‚Î¼ + âˆ«â» a in sá¶œ, g a âˆ‚Î¼ := by
  rw [â† lintegral_add_compl _ hs]
  congr 1
  Â· exact setLIntegral_congr_fun hs <| ae_of_all Î¼ fun _ â†¦ Set.piecewise_eq_of_mem _ _ _
  Â· exact setLIntegral_congr_fun hs.compl <| ae_of_all Î¼ fun _ â†¦ Set.piecewise_eq_of_not_mem _ _ _

theorem setLintegral_compl {f : Î± â†’ â„â‰¥0âˆ} {s : Set Î±} (hsm : MeasurableSet s)
    (hfs : âˆ«â» x in s, f x âˆ‚Î¼ â‰  âˆ) :
    âˆ«â» x in sá¶œ, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ - âˆ«â» x in s, f x âˆ‚Î¼ := by
  rw [â† lintegral_add_compl (Î¼ := Î¼) f hsm, ENNReal.add_sub_cancel_left hfs]

theorem setLIntegral_iUnion_of_directed {Î¹ : Type*} [Countable Î¹]
    (f : Î± â†’ â„â‰¥0âˆ) {s : Î¹ â†’ Set Î±} (hd : Directed (Â· âŠ† Â·) s) :
    âˆ«â» x in â‹ƒ i, s i, f x âˆ‚Î¼ = â¨† i, âˆ«â» x in s i, f x âˆ‚Î¼ := by
  simp only [lintegral_def, iSup_comm (Î¹ := Î¹),
    SimpleFunc.lintegral_restrict_iUnion_of_directed _ hd]

theorem lintegral_max {f g : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (hg : Measurable g) :
    âˆ«â» x, max (f x) (g x) âˆ‚Î¼ =
      âˆ«â» x in { x | f x â‰¤ g x }, g x âˆ‚Î¼ + âˆ«â» x in { x | g x < f x }, f x âˆ‚Î¼ := by
  have hm : MeasurableSet { x | f x â‰¤ g x } := measurableSet_le hf hg
  rw [â† lintegral_add_compl (fun x => max (f x) (g x)) hm]
  simp only [â† compl_setOf, â† not_le]
  refine congr_argâ‚‚ (Â· + Â·) (setLIntegral_congr_fun hm ?_) (setLIntegral_congr_fun hm.compl ?_)
  exacts [ae_of_all _ fun x => max_eq_right (a := f x) (b := g x),
    ae_of_all _ fun x (hx : Â¬ f x â‰¤ g x) => max_eq_left (not_le.1 hx).le]

theorem setLIntegral_max {f g : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (hg : Measurable g) (s : Set Î±) :
    âˆ«â» x in s, max (f x) (g x) âˆ‚Î¼ =
      âˆ«â» x in s âˆ© { x | f x â‰¤ g x }, g x âˆ‚Î¼ + âˆ«â» x in s âˆ© { x | g x < f x }, f x âˆ‚Î¼ := by
  rw [lintegral_max hf hg, restrict_restrict, restrict_restrict, inter_comm s, inter_comm s]
  exacts [measurableSet_lt hg hf, measurableSet_le hf hg]

theorem lintegral_map {mÎ² : MeasurableSpace Î²} {f : Î² â†’ â„â‰¥0âˆ} {g : Î± â†’ Î²} (hf : Measurable f)
    (hg : Measurable g) : âˆ«â» a, f a âˆ‚map g Î¼ = âˆ«â» a, f (g a) âˆ‚Î¼ := by
  rw [lintegral_eq_iSup_eapprox_lintegral hf]
  simp only [â† Function.comp_apply (f := f) (g := g)]
  rw [lintegral_eq_iSup_eapprox_lintegral (hf.comp hg)]
  congr with n : 1
  convert SimpleFunc.lintegral_map _ hg
  ext1 x; simp only [eapprox_comp hf hg, coe_comp]

theorem lintegral_map' {mÎ² : MeasurableSpace Î²} {f : Î² â†’ â„â‰¥0âˆ} {g : Î± â†’ Î²}
    (hf : AEMeasurable f (Measure.map g Î¼)) (hg : AEMeasurable g Î¼) :
    âˆ«â» a, f a âˆ‚Measure.map g Î¼ = âˆ«â» a, f (g a) âˆ‚Î¼ :=
  calc
    âˆ«â» a, f a âˆ‚Measure.map g Î¼ = âˆ«â» a, hf.mk f a âˆ‚Measure.map g Î¼ :=
      lintegral_congr_ae hf.ae_eq_mk
    _ = âˆ«â» a, hf.mk f a âˆ‚Measure.map (hg.mk g) Î¼ := by
      congr 1
      exact Measure.map_congr hg.ae_eq_mk
    _ = âˆ«â» a, hf.mk f (hg.mk g a) âˆ‚Î¼ := lintegral_map hf.measurable_mk hg.measurable_mk
    _ = âˆ«â» a, hf.mk f (g a) âˆ‚Î¼ := lintegral_congr_ae <| hg.ae_eq_mk.symm.fun_comp _
    _ = âˆ«â» a, f (g a) âˆ‚Î¼ := lintegral_congr_ae (ae_eq_comp hg hf.ae_eq_mk.symm)

theorem lintegral_map_le {mÎ² : MeasurableSpace Î²} (f : Î² â†’ â„â‰¥0âˆ) (g : Î± â†’ Î²) :
    âˆ«â» a, f a âˆ‚Measure.map g Î¼ â‰¤ âˆ«â» a, f (g a) âˆ‚Î¼ := by
  by_cases hg : AEMeasurable g Î¼
  Â· rw [â† iSup_lintegral_measurable_le_eq_lintegral]
    refine iSupâ‚‚_le fun i hi => iSup_le fun h'i => ?_
    rw [lintegral_map' hi.aemeasurable hg]
    exact lintegral_mono fun _ â†¦ h'i _
  Â· simp [map_of_not_aemeasurable hg]

theorem lintegral_comp [MeasurableSpace Î²] {f : Î² â†’ â„â‰¥0âˆ} {g : Î± â†’ Î²} (hf : Measurable f)
    (hg : Measurable g) : lintegral Î¼ (f âˆ˜ g) = âˆ«â» a, f a âˆ‚map g Î¼ :=
  (lintegral_map hf hg).symm

theorem setLIntegral_map [MeasurableSpace Î²] {f : Î² â†’ â„â‰¥0âˆ} {g : Î± â†’ Î²} {s : Set Î²}
    (hs : MeasurableSet s) (hf : Measurable f) (hg : Measurable g) :
    âˆ«â» y in s, f y âˆ‚map g Î¼ = âˆ«â» x in g â»Â¹' s, f (g x) âˆ‚Î¼ := by
  rw [restrict_map hg hs, lintegral_map hf hg]

theorem lintegral_indicator_const_comp {mÎ² : MeasurableSpace Î²} {f : Î± â†’ Î²} {s : Set Î²}
    (hf : Measurable f) (hs : MeasurableSet s) (c : â„â‰¥0âˆ) :
    âˆ«â» a, s.indicator (fun _ => c) (f a) âˆ‚Î¼ = c * Î¼ (f â»Â¹' s) := by
  erw [lintegral_comp (measurable_const.indicator hs) hf]
  rw [lintegral_indicator_const hs, Measure.map_apply hf hs]

/-- If `g : Î± â†’ Î²` is a measurable embedding and `f : Î² â†’ â„â‰¥0âˆ` is any function (not necessarily
measurable), then `âˆ«â» a, f a âˆ‚(map g Î¼) = âˆ«â» a, f (g a) âˆ‚Î¼`. Compare with `lintegral_map` which
applies to any measurable `g : Î± â†’ Î²` but requires that `f` is measurable as well. -/
theorem _root_.MeasurableEmbedding.lintegral_map [MeasurableSpace Î²] {g : Î± â†’ Î²}
    (hg : MeasurableEmbedding g) (f : Î² â†’ â„â‰¥0âˆ) : âˆ«â» a, f a âˆ‚map g Î¼ = âˆ«â» a, f (g a) âˆ‚Î¼ := by
  rw [lintegral, lintegral]
  refine le_antisymm (iSupâ‚‚_le fun fâ‚€ hfâ‚€ => ?_) (iSupâ‚‚_le fun fâ‚€ hfâ‚€ => ?_)
  Â· rw [SimpleFunc.lintegral_map _ hg.measurable]
    have : (fâ‚€.comp g hg.measurable : Î± â†’ â„â‰¥0âˆ) â‰¤ f âˆ˜ g := fun x => hfâ‚€ (g x)
    exact le_iSup_of_le (comp fâ‚€ g hg.measurable) (by exact le_iSup (Î± := â„â‰¥0âˆ) _ this)
  Â· rw [â† fâ‚€.extend_comp_eq hg (const _ 0), â† SimpleFunc.lintegral_map, â†
      SimpleFunc.lintegral_eq_lintegral, â† lintegral]
    refine lintegral_mono_ae (hg.ae_map_iff.2 <| Eventually.of_forall fun x => ?_)
    exact (extend_apply _ _ _ _).trans_le (hfâ‚€ _)

/-- The `lintegral` transforms appropriately under a measurable equivalence `g : Î± â‰ƒáµ Î²`.
(Compare `lintegral_map`, which applies to a wider class of functions `g : Î± â†’ Î²`, but requires
measurability of the function being integrated.) -/
theorem lintegral_map_equiv [MeasurableSpace Î²] (f : Î² â†’ â„â‰¥0âˆ) (g : Î± â‰ƒáµ Î²) :
    âˆ«â» a, f a âˆ‚map g Î¼ = âˆ«â» a, f (g a) âˆ‚Î¼ :=
  g.measurableEmbedding.lintegral_map f

theorem lintegral_subtype_comap {s : Set Î±} (hs : MeasurableSet s) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x : s, f x âˆ‚(Î¼.comap (â†‘)) = âˆ«â» x in s, f x âˆ‚Î¼ := by
  rw [â† (MeasurableEmbedding.subtype_coe hs).lintegral_map, map_comap_subtype_coe hs]

theorem setLIntegral_subtype {s : Set Î±} (hs : MeasurableSet s) (t : Set s) (f : Î± â†’ â„â‰¥0âˆ) :
    âˆ«â» x in t, f x âˆ‚(Î¼.comap (â†‘)) = âˆ«â» x in (â†‘) '' t, f x âˆ‚Î¼ := by
  rw [(MeasurableEmbedding.subtype_coe hs).restrict_comap, lintegral_subtype_comap hs,
    restrict_restrict hs, inter_eq_right.2 (Subtype.coe_image_subset _ _)]

section UnifTight

/-- If `f : Î± â†’ â„â‰¥0âˆ` has finite integral, then there exists a measurable set `s` of finite measure
such that the integral of `f` over `sá¶œ` is less than a given positive number.

Also used to prove an `Láµ–`-norm version in
`MeasureTheory.MemLp.exists_eLpNorm_indicator_compl_le`. -/
theorem exists_setLintegral_compl_lt {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ)
    {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) :
    âˆƒ s : Set Î±, MeasurableSet s âˆ§ Î¼ s < âˆ âˆ§ âˆ«â» a in sá¶œ, f a âˆ‚Î¼ < Îµ := by
  by_cases hfâ‚€ : âˆ«â» a, f a âˆ‚Î¼ = 0
  Â· exact âŸ¨âˆ…, .empty, by simp, by simpa [hfâ‚€, pos_iff_ne_zero]âŸ©
  obtain âŸ¨g, hgf, hg_meas, hgsupp, hgÎµâŸ© :
      âˆƒ g â‰¤ f, Measurable g âˆ§ Î¼ (support g) < âˆ âˆ§ âˆ«â» a, f a âˆ‚Î¼ - Îµ < âˆ«â» a, g a âˆ‚Î¼ := by
    obtain âŸ¨g, hgf, hgÎµâŸ© : âˆƒ (g : Î± â†’â‚› â„â‰¥0âˆ) (_ : g â‰¤ f), âˆ«â» a, f a âˆ‚Î¼ - Îµ < g.lintegral Î¼ := by
      simpa only [â† lt_iSup_iff, â† lintegral_def] using ENNReal.sub_lt_self hf hfâ‚€ hÎµ
    refine âŸ¨g, hgf, g.measurable, ?_, by rwa [g.lintegral_eq_lintegral]âŸ©
    exact SimpleFunc.FinMeasSupp.of_lintegral_ne_top <| ne_top_of_le_ne_top hf <|
      g.lintegral_eq_lintegral Î¼ â–¸ lintegral_mono hgf
  refine âŸ¨_, measurableSet_support hg_meas, hgsupp, ?_âŸ©
  calc
    âˆ«â» a in (support g)á¶œ, f a âˆ‚Î¼
      = âˆ«â» a in (support g)á¶œ, f a - g a âˆ‚Î¼ := setLIntegral_congr_fun
      (measurableSet_support hg_meas).compl <| ae_of_all _ <| by intro; simp_all
    _ â‰¤ âˆ«â» a, f a - g a âˆ‚Î¼ := setLIntegral_le_lintegral _ _
    _ = âˆ«â» a, f a âˆ‚Î¼ - âˆ«â» a, g a âˆ‚Î¼ :=
      lintegral_sub hg_meas (ne_top_of_le_ne_top hf <| lintegral_mono hgf) (ae_of_all _ hgf)
    _ < Îµ := ENNReal.sub_lt_of_lt_add (lintegral_mono hgf) <|
      ENNReal.lt_add_of_sub_lt_left (.inl hf) hgÎµ

/-- For any function `f : Î± â†’ â„â‰¥0âˆ`, there exists a measurable function `g â‰¤ f` with the same
integral over any measurable set. -/
theorem exists_measurable_le_setLintegral_eq_of_integrable {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ«â» a, f a âˆ‚Î¼ â‰  âˆ) :
    âˆƒ (g : Î± â†’ â„â‰¥0âˆ), Measurable g âˆ§ g â‰¤ f âˆ§ âˆ€ s : Set Î±, MeasurableSet s â†’
      âˆ«â» a in s, f a âˆ‚Î¼ = âˆ«â» a in s, g a âˆ‚Î¼ := by
  obtain âŸ¨g, hmg, hgf, hifgâŸ© := exists_measurable_le_lintegral_eq (Î¼ := Î¼) f
  use g, hmg, hgf
  refine fun s hms â†¦ le_antisymm ?_ (lintegral_mono hgf)
  rw [â† compl_compl s, setLintegral_compl hms.compl, setLintegral_compl hms.compl, hifg]
  Â· gcongr; apply hgf
  Â· rw [hifg] at hf
    exact ne_top_of_le_ne_top hf (setLIntegral_le_lintegral _ _)
  Â· exact ne_top_of_le_ne_top hf (setLIntegral_le_lintegral _ _)

end UnifTight

theorem ae_lt_top' {f : Î± â†’ â„â‰¥0âˆ} (hf : AEMeasurable f Î¼) (h2f : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) :
    âˆ€áµ x âˆ‚Î¼, f x < âˆ := by
  simp_rw [ae_iff, ENNReal.not_lt_top]
  exact measure_eq_top_of_lintegral_ne_top hf h2f

theorem ae_lt_top {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) (h2f : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) :
    âˆ€áµ x âˆ‚Î¼, f x < âˆ :=
  ae_lt_top' hf.aemeasurable h2f

/-- Lebesgue integral of a bounded function over a set of finite measure is finite.
Note that this lemma assumes no regularity of either `f` or `s`. -/
theorem setLIntegral_lt_top_of_le_nnreal {s : Set Î±} (hs : Î¼ s â‰  âˆ) {f : Î± â†’ â„â‰¥0âˆ}
    (hbdd : âˆƒ y : â„â‰¥0, âˆ€ x âˆˆ s, f x â‰¤ y) : âˆ«â» x in s, f x âˆ‚Î¼ < âˆ := by
  obtain âŸ¨M, hMâŸ© := hbdd
  refine lt_of_le_of_lt (setLIntegral_mono measurable_const hM) ?_
  simp [ENNReal.mul_lt_top, hs.lt_top]

/-- Lebesgue integral of a bounded function over a set of finite measure is finite.
Note that this lemma assumes no regularity of either `f` or `s`. -/
theorem setLIntegral_lt_top_of_bddAbove {s : Set Î±} (hs : Î¼ s â‰  âˆ) {f : Î± â†’ â„â‰¥0}
    (hbdd : BddAbove (f '' s)) : âˆ«â» x in s, f x âˆ‚Î¼ < âˆ :=
  setLIntegral_lt_top_of_le_nnreal hs <| hbdd.imp fun _M hM _x hx â†¦
    ENNReal.coe_le_coe.2 <| hM (mem_image_of_mem f hx)

theorem setLIntegral_lt_top_of_isCompact [TopologicalSpace Î±] {s : Set Î±}
    (hs : Î¼ s â‰  âˆ) (hsc : IsCompact s) {f : Î± â†’ â„â‰¥0} (hf : Continuous f) :
    âˆ«â» x in s, f x âˆ‚Î¼ < âˆ :=
  setLIntegral_lt_top_of_bddAbove hs (hsc.image hf).bddAbove

end LIntegral

variable {m m0 : MeasurableSpace Î±}

theorem lintegral_trim {Î¼ : Measure Î±} (hm : m â‰¤ m0) {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable[m] f) :
    âˆ«â» a, f a âˆ‚Î¼.trim hm = âˆ«â» a, f a âˆ‚Î¼ := by
  refine
    @Measurable.ennreal_induction Î± m (fun f => âˆ«â» a, f a âˆ‚Î¼.trim hm = âˆ«â» a, f a âˆ‚Î¼) ?_ ?_ ?_ f hf
  Â· intro c s hs
    rw [lintegral_indicator hs, lintegral_indicator (hm s hs), setLIntegral_const,
      setLIntegral_const]
    suffices h_trim_s : Î¼.trim hm s = Î¼ s by rw [h_trim_s]
    exact trim_measurableSet_eq hm hs
  Â· intro f g _ hf _ hf_prop hg_prop
    have h_m := lintegral_add_left (Î¼ := Measure.trim Î¼ hm) hf g
    have h_m0 := lintegral_add_left (Î¼ := Î¼) (Measurable.mono hf hm le_rfl) g
    rwa [hf_prop, hg_prop, â† h_m0] at h_m
  Â· intro f hf hf_mono hf_prop
    rw [lintegral_iSup hf hf_mono]
    rw [lintegral_iSup (fun n => Measurable.mono (hf n) hm le_rfl) hf_mono]
    congr with n
    exact hf_prop n

theorem lintegral_trim_ae {Î¼ : Measure Î±} (hm : m â‰¤ m0) {f : Î± â†’ â„â‰¥0âˆ}
    (hf : AEMeasurable f (Î¼.trim hm)) : âˆ«â» a, f a âˆ‚Î¼.trim hm = âˆ«â» a, f a âˆ‚Î¼ := by
  rw [lintegral_congr_ae (ae_eq_of_ae_eq_trim hf.ae_eq_mk), lintegral_congr_ae hf.ae_eq_mk,
    lintegral_trim hm hf.measurable_mk]

end MeasureTheory
