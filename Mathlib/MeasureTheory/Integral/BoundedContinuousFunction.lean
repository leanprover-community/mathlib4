/-
Copyright (c) 2023 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.MeasureTheory.Integral.Bochner

/-!
# Integration of bounded continuous functions

In this file, some results are collected about integrals of bounded continuous functions. They are
mostly specializations of results in general integration theory, but they are used directly in this
specialized form in some other files, in particular in those related to the topology of weak
convergence of probability measures and finite measures.
-/

open MeasureTheory Filter
open scoped ENNReal NNReal BoundedContinuousFunction Topology

namespace BoundedContinuousFunction

section NNRealValued

lemma apply_le_nndist_zero {X : Type*} [TopologicalSpace X] (f : X ‚Üí·µá ‚Ñù‚â•0) (x : X) :
    f x ‚â§ nndist 0 f := by
  convert nndist_coe_le_nndist x
  simp only [coe_zero, Pi.zero_apply, NNReal.nndist_zero_eq_val]

variable {X : Type*} [MeasurableSpace X] [TopologicalSpace X] [OpensMeasurableSpace X]

lemma lintegral_le_edist_mul (f : X ‚Üí·µá ‚Ñù‚â•0) (Œº : Measure X) :
    (‚à´‚Åª x, f x ‚àÇŒº) ‚â§ edist 0 f * (Œº Set.univ) :=
  le_trans (lintegral_mono (fun x ‚Ü¶ ENNReal.coe_le_coe.mpr (f.apply_le_nndist_zero x))) (by simp)

theorem measurable_coe_ennreal_comp (f : X ‚Üí·µá ‚Ñù‚â•0) :
    Measurable fun x ‚Ü¶ (f x : ‚Ñù‚â•0‚àû) :=
  measurable_coe_nnreal_ennreal.comp f.continuous.measurable
#align bounded_continuous_function.nnreal.to_ennreal_comp_measurable BoundedContinuousFunction.measurable_coe_ennreal_comp

variable (Œº : Measure X) [IsFiniteMeasure Œº]

theorem lintegral_lt_top_of_nnreal (f : X ‚Üí·µá ‚Ñù‚â•0) : ‚à´‚Åª x, f x ‚àÇŒº < ‚àû := by
  apply IsFiniteMeasure.lintegral_lt_top_of_bounded_to_ennreal
  refine ‚ü®nndist f 0, fun x ‚Ü¶ ?_‚ü©
  have key := BoundedContinuousFunction.NNReal.upper_bound f x
  rwa [ENNReal.coe_le_coe]
#align measure_theory.lintegral_lt_top_of_bounded_continuous_to_nnreal BoundedContinuousFunction.lintegral_lt_top_of_nnreal

theorem integrable_of_nnreal (f : X ‚Üí·µá ‚Ñù‚â•0) : Integrable (((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò ‚áëf) Œº := by
  refine ‚ü®(NNReal.continuous_coe.comp f.continuous).measurable.aestronglyMeasurable, ?_‚ü©
  simp only [HasFiniteIntegral, Function.comp_apply, NNReal.nnnorm_eq]
  exact lintegral_lt_top_of_nnreal _ f
#align measure_theory.finite_measure.integrable_of_bounded_continuous_to_nnreal BoundedContinuousFunction.integrable_of_nnreal

theorem integral_eq_integral_nnrealPart_sub (f : X ‚Üí·µá ‚Ñù) :
    ‚à´ x, f x ‚àÇŒº = (‚à´ x, (f.nnrealPart x : ‚Ñù) ‚àÇŒº) - ‚à´ x, ((-f).nnrealPart x : ‚Ñù) ‚àÇŒº := by
  simp only [f.self_eq_nnrealPart_sub_nnrealPart_neg, Pi.sub_apply, integral_sub,
             integrable_of_nnreal]
  simp only [Function.comp_apply]
#align bounded_continuous_function.integral_eq_integral_nnreal_part_sub BoundedContinuousFunction.integral_eq_integral_nnrealPart_sub

theorem lintegral_of_real_lt_top (f : X ‚Üí·µá ‚Ñù) :
    ‚à´‚Åª x, ENNReal.ofReal (f x) ‚àÇŒº < ‚àû := lintegral_lt_top_of_nnreal _ f.nnrealPart
#align measure_theory.finite_measure.lintegral_lt_top_of_bounded_continuous_to_real BoundedContinuousFunction.lintegral_of_real_lt_top

theorem toReal_lintegral_coe_eq_integral (f : X ‚Üí·µá ‚Ñù‚â•0) (Œº : Measure X) :
    (‚à´‚Åª x, (f x : ‚Ñù‚â•0‚àû) ‚àÇŒº).toReal = ‚à´ x, (f x : ‚Ñù) ‚àÇŒº := by
  rw [integral_eq_lintegral_of_nonneg_ae _ (by simpa [Function.comp_apply] using
        (NNReal.continuous_coe.comp f.continuous).measurable.aestronglyMeasurable)]
  ¬∑ simp only [ENNReal.ofReal_coe_nnreal]
  ¬∑ exact eventually_of_forall (by simp only [Pi.zero_apply, NNReal.zero_le_coe, imp_true_iff])
#align bounded_continuous_function.nnreal.to_real_lintegral_eq_integral BoundedContinuousFunction.toReal_lintegral_coe_eq_integral

end NNRealValued

section BochnerIntegral

variable {X : Type*} [MeasurableSpace X] [TopologicalSpace X] [OpensMeasurableSpace X]
variable (Œº : Measure X)
variable {E : Type*} [NormedAddCommGroup E] [SecondCountableTopology E]
variable [MeasurableSpace E] [BorelSpace E]

lemma lintegral_nnnorm_le (f : X ‚Üí·µá E) :
    ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çä ‚àÇŒº ‚â§ ‚Äñf‚Äñ‚Çä * (Œº Set.univ) := by
  calc  ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çä ‚àÇŒº
    _ ‚â§ ‚à´‚Åª _, ‚Äñf‚Äñ‚Çä ‚àÇŒº         := by gcongr; apply nnnorm_coe_le_nnnorm
    _ = ‚Äñf‚Äñ‚Çä * (Œº Set.univ)   := by rw [lintegral_const]

lemma integrable [IsFiniteMeasure Œº] (f : X ‚Üí·µá E) :
    Integrable f Œº := by
  refine ‚ü®f.continuous.measurable.aestronglyMeasurable, (hasFiniteIntegral_def _ _).mp ?_‚ü©
  calc  ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çä ‚àÇŒº
    _ ‚â§ ‚Äñf‚Äñ‚Çä * (Œº Set.univ)   := f.lintegral_nnnorm_le Œº
    _ < ‚àû                     := ENNReal.mul_lt_top ENNReal.coe_ne_top (measure_ne_top Œº Set.univ)
#align measure_theory.finite_measure.integrable_of_bounded_continuous_to_real BoundedContinuousFunction.integrable

variable [NormedSpace ‚Ñù E]

lemma norm_integral_le_mul_norm [IsFiniteMeasure Œº] (f : X ‚Üí·µá E) :
    ‚Äñ‚à´ x, f x ‚àÇŒº‚Äñ ‚â§ ENNReal.toReal (Œº Set.univ) * ‚Äñf‚Äñ := by
  calc  ‚Äñ‚à´ x, f x ‚àÇŒº‚Äñ
    _ ‚â§ ‚à´ x, ‚Äñf x‚Äñ ‚àÇŒº                       := by exact norm_integral_le_integral_norm _
    _ ‚â§ ‚à´ _, ‚Äñf‚Äñ ‚àÇŒº                         := ?_
    _ = ENNReal.toReal (Œº Set.univ) ‚Ä¢ ‚Äñf‚Äñ   := by rw [integral_const]
  apply integral_mono _ (integrable_const ‚Äñf‚Äñ) (fun x ‚Ü¶ f.norm_coe_le_norm x) -- NOTE: `gcongr`?
  exact (integrable_norm_iff f.continuous.measurable.aestronglyMeasurable).mpr (f.integrable Œº)

lemma norm_integral_le_norm [IsProbabilityMeasure Œº] (f : X ‚Üí·µá E) :
    ‚Äñ‚à´ x, f x ‚àÇŒº‚Äñ ‚â§ ‚Äñf‚Äñ := by
  convert f.norm_integral_le_mul_norm Œº
  simp only [measure_univ, ENNReal.one_toReal, one_mul]

lemma isBounded_range_integral
    {Œπ : Type*} (Œºs : Œπ ‚Üí Measure X) [‚àÄ i, IsProbabilityMeasure (Œºs i)] (f : X ‚Üí·µá E) :
    Bornology.IsBounded (Set.range (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i))) := by
  apply isBounded_iff_forall_norm_le.mpr ‚ü®‚Äñf‚Äñ, fun v hv ‚Ü¶ ?_‚ü©
  obtain ‚ü®i, hi‚ü© := hv
  rw [‚Üê hi]
  apply f.norm_integral_le_norm (Œºs i)

end BochnerIntegral

section RealValued

variable {X : Type*} [TopologicalSpace X]
variable [MeasurableSpace X] [OpensMeasurableSpace X] {Œº : Measure X} [IsFiniteMeasure Œº]

lemma integral_add_const (f : X ‚Üí·µá ‚Ñù) (c : ‚Ñù) :
    ‚à´ x, (f + const X c) x ‚àÇŒº = ‚à´ x, f x ‚àÇŒº + ENNReal.toReal (Œº (Set.univ)) ‚Ä¢ c := by
  simp [integral_add (f.integrable _) (integrable_const c)]

lemma integral_const_sub (f : X ‚Üí·µá ‚Ñù) (c : ‚Ñù) :
    ‚à´ x, (const X c - f) x ‚àÇŒº = ENNReal.toReal (Œº (Set.univ)) ‚Ä¢ c - ‚à´ x, f x ‚àÇŒº := by
  simp [integral_sub (integrable_const c) (f.integrable _)]

end RealValued

section tendsto_integral

variable {X : Type*} [TopologicalSpace X] [MeasurableSpace X] [OpensMeasurableSpace X]

lemma tendsto_integral_of_forall_limsup_integral_le_integral {Œπ : Type*} {L : Filter Œπ}
    {Œº : Measure X} [IsProbabilityMeasure Œº] {Œºs : Œπ ‚Üí Measure X} [‚àÄ i, IsProbabilityMeasure (Œºs i)]
    (h : ‚àÄ f : X ‚Üí·µá ‚Ñù, 0 ‚â§ f ‚Üí L.limsup (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) ‚â§ ‚à´ x, f x ‚àÇŒº)
    (f : X ‚Üí·µá ‚Ñù) :
    Tendsto (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) L (ùìù (‚à´ x, f x ‚àÇŒº)) := by
  rcases eq_or_neBot L with rfl|hL
  ¬∑ simp only [tendsto_bot]
  have obs := BoundedContinuousFunction.isBounded_range_integral Œºs f
  have bdd_above : IsBoundedUnder (¬∑ ‚â§ ¬∑) L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇŒºs i) := obs.bddAbove.isBoundedUnder
  have bdd_below : IsBoundedUnder (¬∑ ‚â• ¬∑) L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇŒºs i) := obs.bddBelow.isBoundedUnder
  apply tendsto_of_le_liminf_of_limsup_le _ _ bdd_above bdd_below
  ¬∑ have key := h _ (f.norm_sub_nonneg)
    simp_rw [f.integral_const_sub ‚Äñf‚Äñ] at key
    simp only [measure_univ, ENNReal.one_toReal, smul_eq_mul, one_mul] at key
    have := limsup_const_sub L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) ‚Äñf‚Äñ bdd_above bdd_below
    rwa [this, _root_.sub_le_sub_iff_left ‚Äñf‚Äñ] at key
  ¬∑ have key := h _ (f.add_norm_nonneg)
    simp_rw [f.integral_add_const ‚Äñf‚Äñ] at key
    simp only [measure_univ, ENNReal.one_toReal, smul_eq_mul, one_mul] at key
    have := limsup_add_const L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) ‚Äñf‚Äñ bdd_above bdd_below
    rwa [this, add_le_add_iff_right] at key

lemma tendsto_integral_of_forall_integral_le_liminf_integral {Œπ : Type*} {L : Filter Œπ}
    {Œº : Measure X} [IsProbabilityMeasure Œº] {Œºs : Œπ ‚Üí Measure X} [‚àÄ i, IsProbabilityMeasure (Œºs i)]
    (h : ‚àÄ f : X ‚Üí·µá ‚Ñù, 0 ‚â§ f ‚Üí ‚à´ x, f x ‚àÇŒº ‚â§ L.liminf (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)))
    (f : X ‚Üí·µá ‚Ñù) :
    Tendsto (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) L (ùìù (‚à´ x, f x ‚àÇŒº)) := by
  rcases eq_or_neBot L with rfl|hL
  ¬∑ simp only [tendsto_bot]
  have obs := BoundedContinuousFunction.isBounded_range_integral Œºs f
  have bdd_above : IsBoundedUnder (¬∑ ‚â§ ¬∑) L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇŒºs i) := obs.bddAbove.isBoundedUnder
  have bdd_below : IsBoundedUnder (¬∑ ‚â• ¬∑) L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇŒºs i) := obs.bddBelow.isBoundedUnder
  apply @tendsto_of_le_liminf_of_limsup_le ‚Ñù Œπ _ _ _ L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) (‚à´ x, f x ‚àÇŒº)
  ¬∑ have key := h _ (f.add_norm_nonneg)
    simp_rw [f.integral_add_const ‚Äñf‚Äñ] at key
    simp only [measure_univ, ENNReal.one_toReal, smul_eq_mul, one_mul] at key
    have := liminf_add_const L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) ‚Äñf‚Äñ bdd_above bdd_below
    rwa [this, add_le_add_iff_right] at key
  ¬∑ have key := h _ (f.norm_sub_nonneg)
    simp_rw [f.integral_const_sub ‚Äñf‚Äñ] at key
    simp only [measure_univ, ENNReal.one_toReal, smul_eq_mul, one_mul] at key
    have := liminf_const_sub L (fun i ‚Ü¶ ‚à´ x, f x ‚àÇ (Œºs i)) ‚Äñf‚Äñ bdd_above bdd_below
    rwa [this, sub_le_sub_iff_left] at key
  ¬∑ exact bdd_above
  ¬∑ exact bdd_below

end tendsto_integral --section

end BoundedContinuousFunction
