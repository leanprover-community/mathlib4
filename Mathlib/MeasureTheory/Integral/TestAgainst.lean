/-
Copyright (c) 2025 Luigi Massacci. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luigi Massacci
-/
import Mathlib.MeasureTheory.Integral.Bochner.ContinuousLinearMap
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction

/-!
# Integration of bounded continuous functions against finite measures and locally integrable maps

In this file, some specialized definitions are introduced for bundling properties of integrals of
bounded continuous functions against finite measures and locally integrable maps.
They are meant to be used as intermediate constructions for the development of distribution theory.

## Main definitions

- `FiniteMeasure.testAgainstCLM` wraps the integral with respect to a finite measure
  as a continuous linear map on bounded continuous functions.
- `LocallyIntegrable.testAgainstCLM` wraps the integral against a locally integrable function as
  as a continuous linear map on bounded continuous functions.
-/

open MeasureTheory Filter
open scoped BoundedContinuousFunction Topology

namespace BoundedContinuousFunction

section BochnerIntegral

variable {X : Type*} {mX : MeasurableSpace X} [TopologicalSpace X] [OpensMeasurableSpace X]
variable (Î¼ : Measure X)
variable {E : Type*} [NormedAddCommGroup E] [SecondCountableTopology E] [MeasurableSpace E]
  [BorelSpace E] [NormedSpace â„ E]

namespace FiniteMeasure

/-- `FiniteMeasure.testAgainstâ‚—` wraps the integral with respect to a finite measure `Î¼`
as a `ğ•œ`-linear map on bounded continuous functions. -/
noncomputable def testAgainstâ‚— (ğ•œ : Type*) [NormedField ğ•œ] [NormedSpace ğ•œ E]
    [SMulCommClass â„ ğ•œ E] [IsFiniteMeasure Î¼] :
    (X â†’áµ‡ E) â†’â‚—[ğ•œ] E where
  toFun := (âˆ« x, Â· x âˆ‚Î¼)
  map_add' f g := integral_add (f.integrable Î¼) (g.integrable Î¼)
  map_smul' c f := integral_smul c f

/-- `FiniteMeasure.testAgainstCLM` wraps the integral with respect to a finite measure `Î¼`
as a continuous `ğ•œ`-linear map on bounded continuous functions. -/
noncomputable def testAgainstCLM (ğ•œ : Type*) [NormedField ğ•œ] [NormedSpace ğ•œ E]
    [SMulCommClass â„ ğ•œ E] [IsFiniteMeasure Î¼] :
    (X â†’áµ‡ E) â†’L[ğ•œ] E :=
  (testAgainstâ‚— Î¼ ğ•œ).mkContinuous (Î¼.real Set.univ) (fun f â†¦ f.norm_integral_le_mul_norm Î¼)

end FiniteMeasure

namespace LocallyIntegrable

variable {ğ•œ : Type*} [NormedField ğ•œ] [Module ğ•œ E] [NormSMulClass ğ•œ E]
variable [LocallyCompactSpace X] [T2Space X] [SecondCountableTopology X]

open TopologicalSpace

omit [SecondCountableTopology E] [MeasurableSpace E] [BorelSpace E] [NormedSpace â„ E] in
theorem integrable_smul_LocallyIntegrable {f : X â†’ E} (hf : LocallyIntegrable f Î¼) (K : Compacts X)
    (Ï† : X â†’áµ‡ ğ•œ) :
    Integrable (fun x â†¦ (Ï† x) â€¢ (f x)) (Î¼.restrict K) :=
  ((hf.locallyIntegrableOn K).continuousOn_smul K.isCompact.isClosed.isLocallyClosed
      Ï†.continuous.continuousOn).integrableOn_isCompact K.isCompact

variable [SMulCommClass â„ ğ•œ E]

variable (ğ•œ) {Î¼}

/-- `LocallyIntegrable.testAgainstâ‚—` wraps the integral against a locally integrable function `f` on
a fixed compact `K` as a `ğ•œ`-linear map on scalar valued bounded continuous functions. -/
noncomputable def testAgainstâ‚— {f : X â†’ E} (hf : LocallyIntegrable f Î¼) (K : Compacts X) :
    (X â†’áµ‡ ğ•œ) â†’â‚—[ğ•œ] E where
  toFun Ï† := âˆ« x, Ï† x â€¢ f x âˆ‚(Î¼.restrict K)
  map_add' Ï† Î¦:= by
    simp_rw [add_apply, add_smul, integral_add (integrable_smul_LocallyIntegrable Î¼ hf K Ï†)
      (integrable_smul_LocallyIntegrable Î¼ hf K Î¦)]
  map_smul' c Ï† := by
    simp_rw [coe_smul, RingHom.id_apply, â† integral_smul c (fun x â†¦ Ï† x â€¢ f x), smul_assoc]

/-- `LocallyIntegrable.testAgainstCLM` wraps the integral against a locally integrable
function `f` on a fixed compact `K` as a continuous `ğ•œ`-linear map on scalar valued bounded
continuous functions. -/
noncomputable def testAgainstCLM {f : X â†’ E} (hf : LocallyIntegrable f Î¼) (K : Compacts X) :
    (X â†’áµ‡ ğ•œ) â†’L[ğ•œ] E :=
  (testAgainstâ‚— ğ•œ hf K).mkContinuous (âˆ« x, â€–f xâ€– âˆ‚(Î¼.restrict K))
  (by
    intro Ï†
    have h : âˆ€áµ x âˆ‚(Î¼.restrict K), â€–Ï† x â€¢ f xâ€– â‰¤ â€–Ï†â€– * â€–f xâ€– :=
      (ae_of_all _ fun x â†¦ by grw [norm_smul, norm_coe_le_norm])
    apply le_trans (norm_integral_le_of_norm_le
      ((hf.integrableOn_isCompact K.isCompact).norm.const_mul _) h)
    rw [mul_comm, integral_const_mul_of_integrable (hf.integrableOn_isCompact K.isCompact).norm]
  )

end LocallyIntegrable

end BochnerIntegral

end BoundedContinuousFunction
