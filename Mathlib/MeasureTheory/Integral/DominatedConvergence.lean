/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Patrick Massot
-/
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic
import Mathlib.MeasureTheory.Measure.Real
import Mathlib.Order.Filter.IndicatorFunction

/-!
# The dominated convergence theorem

This file collects various results related to the Lebesgue dominated convergence theorem
for the Bochner integral.

## Main results
- `MeasureTheory.tendsto_integral_of_dominated_convergence`:
  the Lebesgue dominated convergence theorem for the Bochner integral
- `MeasureTheory.hasSum_integral_of_dominated_convergence`:
  the Lebesgue dominated convergence theorem for series
- `MeasureTheory.integral_tsum`, `MeasureTheory.integral_tsum_of_summable_integral_norm`:
  the integral and `tsum`s commute, if the norms of the functions form a summable series
- `intervalIntegral.hasSum_integral_of_dominated_convergence`: the Lebesgue dominated convergence
  theorem for parametric interval integrals
- `intervalIntegral.continuous_of_dominated_interval`: continuity of the interval integral
  w.r.t. a parameter
- `intervalIntegral.continuous_primitive` and friends: primitives of interval integrable
  measurable functions are continuous

-/

open MeasureTheory Metric

/-!
## The Lebesgue dominated convergence theorem for the Bochner integral
-/
section DominatedConvergenceTheorem

open Set Filter TopologicalSpace ENNReal
open scoped Topology Interval

namespace MeasureTheory

variable {Î± E G : Type*}
  [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup G] [NormedSpace â„ G]
  {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- **Lebesgue dominated convergence theorem** provides sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their integrals.
  We could weaken the condition `bound_integrable` to require `HasFiniteIntegral bound Î¼` instead
  (i.e. not requiring that `bound` is measurable), but in all applications proving integrability
  is easier. -/
theorem tendsto_integral_of_dominated_convergence {F : â„• â†’ Î± â†’ G} {f : Î± â†’ G} (bound : Î± â†’ â„)
    (F_measurable : âˆ€ n, AEStronglyMeasurable (F n) Î¼) (bound_integrable : Integrable bound Î¼)
    (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, â€–F n aâ€– â‰¤ bound a)
    (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => F n a) atTop (ğ“ (f a))) :
    Tendsto (fun n => âˆ« a, F n a âˆ‚Î¼) atTop (ğ“ <| âˆ« a, f a âˆ‚Î¼) := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact tendsto_setToFun_of_dominated_convergence (dominatedFinMeasAdditive_weightedSMul Î¼)
      bound F_measurable bound_integrable h_bound h_lim
  Â· simp [integral, hG]

/-- Lebesgue dominated convergence theorem for filters with a countable basis -/
theorem tendsto_integral_filter_of_dominated_convergence {Î¹} {l : Filter Î¹} [l.IsCountablyGenerated]
    {F : Î¹ â†’ Î± â†’ G} {f : Î± â†’ G} (bound : Î± â†’ â„) (hF_meas : âˆ€á¶  n in l, AEStronglyMeasurable (F n) Î¼)
    (h_bound : âˆ€á¶  n in l, âˆ€áµ a âˆ‚Î¼, â€–F n aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => F n a) l (ğ“ (f a))) :
    Tendsto (fun n => âˆ« a, F n a âˆ‚Î¼) l (ğ“ <| âˆ« a, f a âˆ‚Î¼) := by
  by_cases hG : CompleteSpace G
  Â· simp only [integral, hG, L1.integral]
    exact tendsto_setToFun_filter_of_dominated_convergence (dominatedFinMeasAdditive_weightedSMul Î¼)
      bound hF_meas h_bound bound_integrable h_lim
  Â· simp [integral, hG, tendsto_const_nhds]

/-- Lebesgue dominated convergence theorem for series. -/
theorem hasSum_integral_of_dominated_convergence {Î¹} [Countable Î¹] {F : Î¹ â†’ Î± â†’ G} {f : Î± â†’ G}
    (bound : Î¹ â†’ Î± â†’ â„) (hF_meas : âˆ€ n, AEStronglyMeasurable (F n) Î¼)
    (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, â€–F n aâ€– â‰¤ bound n a)
    (bound_summable : âˆ€áµ a âˆ‚Î¼, Summable fun n => bound n a)
    (bound_integrable : Integrable (fun a => âˆ‘' n, bound n a) Î¼)
    (h_lim : âˆ€áµ a âˆ‚Î¼, HasSum (fun n => F n a) (f a)) :
    HasSum (fun n => âˆ« a, F n a âˆ‚Î¼) (âˆ« a, f a âˆ‚Î¼) := by
  have hb_nonneg : âˆ€áµ a âˆ‚Î¼, âˆ€ n, 0 â‰¤ bound n a :=
    eventually_countable_forall.2 fun n => (h_bound n).mono fun a => (norm_nonneg _).trans
  have hb_le_tsum : âˆ€ n, bound n â‰¤áµ[Î¼] fun a => âˆ‘' n, bound n a := by
    intro n
    filter_upwards [hb_nonneg, bound_summable]
      with _ ha0 ha_sum using ha_sum.le_tsum _ fun i _ => ha0 i
  have hF_integrable : âˆ€ n, Integrable (F n) Î¼ := by
    refine fun n => bound_integrable.mono' (hF_meas n) ?_
    exact EventuallyLE.trans (h_bound n) (hb_le_tsum n)
  simp only [HasSum, â† integral_finset_sum _ fun n _ => hF_integrable n]
  refine tendsto_integral_filter_of_dominated_convergence
      (fun a => âˆ‘' n, bound n a) ?_ ?_ bound_integrable h_lim
  Â· exact Eventually.of_forall fun s => s.aestronglyMeasurable_fun_sum fun n _ => hF_meas n
  Â· filter_upwards with s
    filter_upwards [eventually_countable_forall.2 h_bound, hb_nonneg, bound_summable]
      with a hFa ha0 has
    calc
      â€–âˆ‘ n âˆˆ s, F n aâ€– â‰¤ âˆ‘ n âˆˆ s, bound n a := norm_sum_le_of_le _ fun n _ => hFa n
      _ â‰¤ âˆ‘' n, bound n a := has.sum_le_tsum _ (fun n _ => ha0 n)

theorem integral_tsum {Î¹} [Countable Î¹] {f : Î¹ â†’ Î± â†’ G} (hf : âˆ€ i, AEStronglyMeasurable (f i) Î¼)
    (hf' : âˆ‘' i, âˆ«â» a : Î±, â€–f i aâ€–â‚‘ âˆ‚Î¼ â‰  âˆ) :
    âˆ« a : Î±, âˆ‘' i, f i a âˆ‚Î¼ = âˆ‘' i, âˆ« a : Î±, f i a âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G; swap
  Â· simp [integral, hG]
  have hf'' i : AEMeasurable (â€–f i Â·â€–â‚‘) Î¼ := (hf i).enorm
  have hhh : âˆ€áµ a : Î± âˆ‚Î¼, Summable fun n => (â€–f n aâ€–â‚Š : â„) := by
    rw [â† lintegral_tsum hf''] at hf'
    refine (ae_lt_top' (AEMeasurable.ennreal_tsum hf'') hf').mono ?_
    intro x hx
    rw [â† ENNReal.tsum_coe_ne_top_iff_summable_coe]
    exact hx.ne
  convert (MeasureTheory.hasSum_integral_of_dominated_convergence (fun i a => â€–f i aâ€–â‚Š) hf _ hhh
          âŸ¨_, _âŸ© _).tsum_eq.symm
  Â· intro n
    filter_upwards with x
    rfl
  Â· simp_rw [â† NNReal.coe_tsum]
    rw [aestronglyMeasurable_iff_aemeasurable]
    apply AEMeasurable.coe_nnreal_real
    apply AEMeasurable.nnreal_tsum
    exact fun i => (hf i).nnnorm.aemeasurable
  Â· dsimp [HasFiniteIntegral]
    have : âˆ«â» a, âˆ‘' n, â€–f n aâ€–â‚‘ âˆ‚Î¼ < âŠ¤ := by rwa [lintegral_tsum hf'', lt_top_iff_ne_top]
    convert this using 1
    apply lintegral_congr_ae
    simp_rw [â† coe_nnnorm, â† NNReal.coe_tsum, enorm_eq_nnnorm, NNReal.nnnorm_eq]
    filter_upwards [hhh] with a ha
    exact ENNReal.coe_tsum (NNReal.summable_coe.mp ha)
  Â· filter_upwards [hhh] with x hx
    exact hx.of_norm.hasSum

lemma hasSum_integral_of_summable_integral_norm {Î¹} [Countable Î¹] {F : Î¹ â†’ Î± â†’ E}
    (hF_int : âˆ€ i : Î¹, Integrable (F i) Î¼) (hF_sum : Summable fun i â†¦ âˆ« a, â€–F i aâ€– âˆ‚Î¼) :
    HasSum (âˆ« a, F Â· a âˆ‚Î¼) (âˆ« a, (âˆ‘' i, F i a) âˆ‚Î¼) := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE, hasSum_zero]
  rw [integral_tsum (fun i â†¦ (hF_int i).1)]
  Â· exact (hF_sum.of_norm_bounded fun i â†¦ norm_integral_le_integral_norm _).hasSum
  have (i : Î¹) : âˆ«â» a, â€–F i aâ€–â‚‘ âˆ‚Î¼ = â€–âˆ« a, â€–F i aâ€– âˆ‚Î¼â€–â‚‘ := by
    dsimp [enorm]
    rw [lintegral_coe_eq_integral _ (hF_int i).norm, coe_nnreal_eq, coe_nnnorm,
      Real.norm_of_nonneg (integral_nonneg (fun a â†¦ norm_nonneg (F i a)))]
    simp only [coe_nnnorm]
  rw [funext this]
  exact ENNReal.tsum_coe_ne_top_iff_summable.2 <| NNReal.summable_coe.1 hF_sum.abs

lemma integral_tsum_of_summable_integral_norm {Î¹} [Countable Î¹] {F : Î¹ â†’ Î± â†’ E}
    (hF_int : âˆ€ i : Î¹, Integrable (F i) Î¼) (hF_sum : Summable fun i â†¦ âˆ« a, â€–F i aâ€– âˆ‚Î¼) :
    âˆ‘' i, (âˆ« a, F i a âˆ‚Î¼) = âˆ« a, (âˆ‘' i, F i a) âˆ‚Î¼ :=
  (hasSum_integral_of_summable_integral_norm hF_int hF_sum).tsum_eq

/-- Corollary of the Lebesgue dominated convergence theorem: If a sequence of functions `F n` is
(eventually) uniformly bounded by a constant and converges (eventually) pointwise to a
function `f`, then the integrals of `F n` with respect to a finite measure `Î¼` converge
to the integral of `f`. -/
theorem tendsto_integral_filter_of_norm_le_const {Î¹} {l : Filter Î¹} [l.IsCountablyGenerated]
    {F : Î¹ â†’ Î± â†’ G} [IsFiniteMeasure Î¼] {f : Î± â†’ G}
    (h_meas : âˆ€á¶  n in l, AEStronglyMeasurable (F n) Î¼)
    (h_bound : âˆƒ C, âˆ€á¶  n in l, (âˆ€áµ Ï‰ âˆ‚Î¼, â€–F n Ï‰â€– â‰¤ C))
    (h_lim : âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => F n Ï‰) l (ğ“ (f Ï‰))) :
    Tendsto (fun n => âˆ« Ï‰, F n Ï‰ âˆ‚Î¼) l (nhds (âˆ« Ï‰, f Ï‰ âˆ‚Î¼)) := by
  obtain âŸ¨c, h_boundcâŸ© := h_bound
  let C : Î± â†’ â„ := (fun _ => c)
  exact tendsto_integral_filter_of_dominated_convergence
    C h_meas h_boundc (integrable_const c) h_lim

end MeasureTheory

section TendstoMono

variable {Î± E : Type*} [MeasurableSpace Î±]
  {Î¼ : Measure Î±} [NormedAddCommGroup E] [NormedSpace â„ E] {s : â„• â†’ Set Î±}
  {f : Î± â†’ E}

theorem _root_.Antitone.tendsto_setIntegral (hsm : âˆ€ i, MeasurableSet (s i)) (h_anti : Antitone s)
    (hfi : IntegrableOn f (s 0) Î¼) :
    Tendsto (fun i => âˆ« a in s i, f a âˆ‚Î¼) atTop (ğ“ (âˆ« a in â‹‚ n, s n, f a âˆ‚Î¼)) := by
  let bound : Î± â†’ â„ := indicator (s 0) fun a => â€–f aâ€–
  have h_int_eq : (fun i => âˆ« a in s i, f a âˆ‚Î¼) = fun i => âˆ« a, (s i).indicator f a âˆ‚Î¼ :=
    funext fun i => (integral_indicator (hsm i)).symm
  rw [h_int_eq]
  rw [â† integral_indicator (MeasurableSet.iInter hsm)]
  refine tendsto_integral_of_dominated_convergence bound ?_ ?_ ?_ ?_
  Â· intro n
    rw [aestronglyMeasurable_indicator_iff (hsm n)]
    exact (IntegrableOn.mono_set hfi (h_anti (zero_le n))).1
  Â· rw [integrable_indicator_iff (hsm 0)]
    exact hfi.norm
  Â· simp_rw [norm_indicator_eq_indicator_norm]
    refine fun n => Eventually.of_forall fun x => ?_
    exact indicator_le_indicator_of_subset (h_anti (zero_le n)) (fun a => norm_nonneg _) _
  Â· filter_upwards [] with a using le_trans (h_anti.tendsto_indicator _ _ _) (pure_le_nhds _)

end TendstoMono

/-!
## The Lebesgue dominated convergence theorem for interval integrals
As an application, we show continuity of parametric integrals.
-/
namespace intervalIntegral

section DCT

variable {Î¹ E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {a b : â„} {f : â„ â†’ E} {Î¼ : Measure â„}

/-- Lebesgue dominated convergence theorem for filters with a countable basis -/
nonrec theorem tendsto_integral_filter_of_dominated_convergence {Î¹} {l : Filter Î¹}
    [l.IsCountablyGenerated] {F : Î¹ â†’ â„ â†’ E} (bound : â„ â†’ â„)
    (hF_meas : âˆ€á¶  n in l, AEStronglyMeasurable (F n) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€á¶  n in l, âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ â€–F n xâ€– â‰¤ bound x)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_lim : âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ Tendsto (fun n => F n x) l (ğ“ (f x))) :
    Tendsto (fun n => âˆ« x in a..b, F n x âˆ‚Î¼) l (ğ“ <| âˆ« x in a..b, f x âˆ‚Î¼) := by
  simp only [intervalIntegrable_iff, intervalIntegral_eq_integral_uIoc,
    â† ae_restrict_iff' (Î± := â„) (Î¼ := Î¼) measurableSet_uIoc] at *
  exact tendsto_const_nhds.smul <|
    tendsto_integral_filter_of_dominated_convergence bound hF_meas h_bound bound_integrable h_lim

/-- Lebesgue dominated convergence theorem for parametric interval integrals. -/
nonrec theorem hasSum_integral_of_dominated_convergence {Î¹} [Countable Î¹] {F : Î¹ â†’ â„ â†’ E}
    (bound : Î¹ â†’ â„ â†’ â„) (hF_meas : âˆ€ n, AEStronglyMeasurable (F n) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€ n, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F n tâ€– â‰¤ bound n t)
    (bound_summable : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ Summable fun n => bound n t)
    (bound_integrable : IntervalIntegrable (fun t => âˆ‘' n, bound n t) Î¼ a b)
    (h_lim : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasSum (fun n => F n t) (f t)) :
    HasSum (fun n => âˆ« t in a..b, F n t âˆ‚Î¼) (âˆ« t in a..b, f t âˆ‚Î¼) := by
  simp only [intervalIntegrable_iff, intervalIntegral_eq_integral_uIoc, â†
    ae_restrict_iff' (Î± := â„) (Î¼ := Î¼) measurableSet_uIoc] at *
  exact
    (hasSum_integral_of_dominated_convergence bound hF_meas h_bound bound_summable bound_integrable
          h_lim).const_smul
      _

/-- Interval integrals commute with countable sums, when the supremum norms are summable (a
special case of the dominated convergence theorem). -/
theorem hasSum_intervalIntegral_of_summable_norm [Countable Î¹] {f : Î¹ â†’ C(â„, E)}
    (hf_sum : Summable fun i : Î¹ => â€–(f i).restrict (âŸ¨uIcc a b, isCompact_uIccâŸ© : Compacts â„)â€–) :
    HasSum (fun i : Î¹ => âˆ« x in a..b, f i x) (âˆ« x in a..b, âˆ‘' i : Î¹, f i x) := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [intervalIntegral, integral, hE, hasSum_zero]
  apply hasSum_integral_of_dominated_convergence
    (fun i (x : â„) => â€–(f i).restrict â†‘(âŸ¨uIcc a b, isCompact_uIccâŸ© : Compacts â„)â€–)
    (fun i => (map_continuous <| f i).aestronglyMeasurable)
  Â· intro i; filter_upwards with x hx
    apply ContinuousMap.norm_coe_le_norm ((f i).restrict _) âŸ¨x, _âŸ©
    exact âŸ¨hx.1.le, hx.2âŸ©
  Â· exact ae_of_all _ fun x _ => hf_sum
  Â· exact intervalIntegrable_const
  Â· refine ae_of_all _ fun x hx => Summable.hasSum ?_
    let x : (âŸ¨uIcc a b, isCompact_uIccâŸ© : Compacts â„) := âŸ¨x, âŸ¨hx.1.le, hx.2âŸ©âŸ©
    have := hf_sum.of_norm
    simpa only [Compacts.coe_mk, ContinuousMap.restrict_apply]
      using ContinuousMap.summable_apply this x

theorem tsum_intervalIntegral_eq_of_summable_norm [Countable Î¹] {f : Î¹ â†’ C(â„, E)}
    (hf_sum : Summable fun i : Î¹ => â€–(f i).restrict (âŸ¨uIcc a b, isCompact_uIccâŸ© : Compacts â„)â€–) :
    âˆ‘' i : Î¹, âˆ« x in a..b, f i x = âˆ« x in a..b, âˆ‘' i : Î¹, f i x :=
  (hasSum_intervalIntegral_of_summable_norm hf_sum).tsum_eq

variable {X : Type*} [TopologicalSpace X] [FirstCountableTopology X]

/-- Continuity of interval integral with respect to a parameter, at a point within a set.
  Given `F : X â†’ â„ â†’ E`, assume `F x` is ae-measurable on `[a, b]` for `x` in a
  neighborhood of `xâ‚€` within `s` and at `xâ‚€`, and assume it is bounded by a function integrable
  on `[a, b]` independent of `x` in a neighborhood of `xâ‚€` within `s`. If `(fun x â†¦ F x t)`
  is continuous at `xâ‚€` within `s` for almost every `t` in `[a, b]`
  then the same holds for `(fun x â†¦ âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€`. -/
theorem continuousWithinAt_of_dominated_interval {F : X â†’ â„ â†’ E} {xâ‚€ : X} {bound : â„ â†’ â„} {a b : â„}
    {s : Set X} (hF_meas : âˆ€á¶  x in ğ“[s] xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict <| Î™ a b))
    (h_bound : âˆ€á¶  x in ğ“[s] xâ‚€, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_cont : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ ContinuousWithinAt (fun x => F x t) s xâ‚€) :
    ContinuousWithinAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€ :=
  tendsto_integral_filter_of_dominated_convergence bound hF_meas h_bound bound_integrable h_cont

/-- Continuity of interval integral with respect to a parameter at a point.
  Given `F : X â†’ â„ â†’ E`, assume `F x` is ae-measurable on `[a, b]` for `x` in a
  neighborhood of `xâ‚€`, and assume it is bounded by a function integrable on
  `[a, b]` independent of `x` in a neighborhood of `xâ‚€`. If `(fun x â†¦ F x t)`
  is continuous at `xâ‚€` for almost every `t` in `[a, b]`
  then the same holds for `(fun x â†¦ âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€`. -/
theorem continuousAt_of_dominated_interval {F : X â†’ â„ â†’ E} {xâ‚€ : X} {bound : â„ â†’ â„} {a b : â„}
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict <| Î™ a b))
    (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_cont : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ ContinuousAt (fun x => F x t) xâ‚€) :
    ContinuousAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) xâ‚€ :=
  tendsto_integral_filter_of_dominated_convergence bound hF_meas h_bound bound_integrable h_cont

/-- Continuity of interval integral with respect to a parameter.
  Given `F : X â†’ â„ â†’ E`, assume each `F x` is ae-measurable on `[a, b]`,
  and assume it is bounded by a function integrable on `[a, b]` independent of `x`.
  If `(fun x â†¦ F x t)` is continuous for almost every `t` in `[a, b]`
  then the same holds for `(fun x â†¦ âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€`. -/
theorem continuous_of_dominated_interval {F : X â†’ â„ â†’ E} {bound : â„ â†’ â„} {a b : â„}
    (hF_meas : âˆ€ x, AEStronglyMeasurable (F x) <| Î¼.restrict <| Î™ a b)
    (h_bound : âˆ€ x, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_cont : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ Continuous fun x => F x t) :
    Continuous fun x => âˆ« t in a..b, F x t âˆ‚Î¼ :=
  continuous_iff_continuousAt.mpr fun _ =>
    continuousAt_of_dominated_interval (Eventually.of_forall hF_meas) (Eventually.of_forall h_bound)
        bound_integrable <|
      h_cont.mono fun _ himp hx => (himp hx).continuousAt

end DCT

section ContinuousPrimitive

open scoped Interval

variable {E X : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [TopologicalSpace X]
  {a b bâ‚€ bâ‚ bâ‚‚ : â„} {Î¼ : Measure â„} {f : â„ â†’ E}

theorem continuousWithinAt_primitive (hbâ‚€ : Î¼ {bâ‚€} = 0)
    (h_int : IntervalIntegrable f Î¼ (min a bâ‚) (max a bâ‚‚)) :
    ContinuousWithinAt (fun b => âˆ« x in a..b, f x âˆ‚Î¼) (Icc bâ‚ bâ‚‚) bâ‚€ := by
  by_cases hâ‚€ : bâ‚€ âˆˆ Icc bâ‚ bâ‚‚
  Â· have hâ‚â‚‚ : bâ‚ â‰¤ bâ‚‚ := hâ‚€.1.trans hâ‚€.2
    have minâ‚â‚‚ : min bâ‚ bâ‚‚ = bâ‚ := min_eq_left hâ‚â‚‚
    have h_int' : âˆ€ {x}, x âˆˆ Icc bâ‚ bâ‚‚ â†’ IntervalIntegrable f Î¼ bâ‚ x := by
      rintro x âŸ¨hâ‚, hâ‚‚âŸ©
      apply h_int.mono_set
      apply uIcc_subset_uIcc
      Â· exact âŸ¨min_le_of_left_le (min_le_right a bâ‚),
          hâ‚.trans (hâ‚‚.trans <| le_max_of_le_right <| le_max_right _ _)âŸ©
      Â· exact âŸ¨min_le_of_left_le <| (min_le_right _ _).trans hâ‚,
          le_max_of_le_right <| hâ‚‚.trans <| le_max_right _ _âŸ©
    have : âˆ€ b âˆˆ Icc bâ‚ bâ‚‚,
        âˆ« x in a..b, f x âˆ‚Î¼ = (âˆ« x in a..bâ‚, f x âˆ‚Î¼) + âˆ« x in bâ‚..b, f x âˆ‚Î¼ := by
      rintro b âŸ¨hâ‚, hâ‚‚âŸ©
      rw [â† integral_add_adjacent_intervals _ (h_int' âŸ¨hâ‚, hâ‚‚âŸ©)]
      apply h_int.mono_set
      apply uIcc_subset_uIcc
      Â· exact âŸ¨min_le_of_left_le (min_le_left a bâ‚), le_max_of_le_right (le_max_left _ _)âŸ©
      Â· exact âŸ¨min_le_of_left_le (min_le_right _ _),
          le_max_of_le_right (hâ‚.trans <| hâ‚‚.trans (le_max_right a bâ‚‚))âŸ©
    apply ContinuousWithinAt.congr _ this (this _ hâ‚€); clear this
    refine continuousWithinAt_const.add ?_
    have :
      (fun b => âˆ« x in bâ‚..b, f x âˆ‚Î¼) =á¶ [ğ“[Icc bâ‚ bâ‚‚] bâ‚€] fun b =>
        âˆ« x in bâ‚..bâ‚‚, indicator {x | x â‰¤ b} f x âˆ‚Î¼ := by
      apply eventuallyEq_of_mem self_mem_nhdsWithin
      exact fun b b_in => (integral_indicator b_in).symm
    apply ContinuousWithinAt.congr_of_eventuallyEq _ this (integral_indicator hâ‚€).symm
    have : IntervalIntegrable (fun x => â€–f xâ€–) Î¼ bâ‚ bâ‚‚ :=
      IntervalIntegrable.norm (h_int' <| right_mem_Icc.mpr hâ‚â‚‚)
    refine continuousWithinAt_of_dominated_interval ?_ ?_ this ?_ <;> clear this
    Â· filter_upwards [self_mem_nhdsWithin]
      intro x hx
      rw [aestronglyMeasurable_indicator_iff, Measure.restrict_restrict, uIoc, Iic_def,
        Iic_inter_Ioc_of_le]
      Â· rw [minâ‚â‚‚]
        exact (h_int' hx).1.aestronglyMeasurable
      Â· exact le_max_of_le_right hx.2
      exacts [measurableSet_Iic, measurableSet_Iic]
    Â· filter_upwards with x; filter_upwards with t
      dsimp [indicator]
      split_ifs <;> simp
    Â· have : âˆ€áµ t âˆ‚Î¼, t < bâ‚€ âˆ¨ bâ‚€ < t := by
        filter_upwards [compl_mem_ae_iff.mpr hbâ‚€] with x hx using Ne.lt_or_gt hx
      apply this.mono
      rintro xâ‚€ (hxâ‚€ | hxâ‚€) -
      Â· have : âˆ€á¶  x in ğ“[Icc bâ‚ bâ‚‚] bâ‚€, {t : â„ | t â‰¤ x}.indicator f xâ‚€ = f xâ‚€ := by
          apply mem_nhdsWithin_of_mem_nhds
          apply Eventually.mono (Ioi_mem_nhds hxâ‚€)
          intro x hx
          simp [hx.le]
        apply continuousWithinAt_const.congr_of_eventuallyEq this
        simp [hxâ‚€.le]
      Â· have : âˆ€á¶  x in ğ“[Icc bâ‚ bâ‚‚] bâ‚€, {t : â„ | t â‰¤ x}.indicator f xâ‚€ = 0 := by
          apply mem_nhdsWithin_of_mem_nhds
          apply Eventually.mono (Iio_mem_nhds hxâ‚€)
          intro x hx
          simp [hx]
        apply continuousWithinAt_const.congr_of_eventuallyEq this
        simp [hxâ‚€]
  Â· apply continuousWithinAt_of_notMem_closure
    rwa [closure_Icc]

theorem continuousAt_parametric_primitive_of_dominated [FirstCountableTopology X]
    {F : X â†’ â„ â†’ E} (bound : â„ â†’ â„) (a b : â„)
    {aâ‚€ bâ‚€ : â„} {xâ‚€ : X} (hF_meas : âˆ€ x, AEStronglyMeasurable (F x) (Î¼.restrict <| Î™ a b))
    (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ t âˆ‚Î¼.restrict <| Î™ a b, â€–F x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_cont : âˆ€áµ t âˆ‚Î¼.restrict <| Î™ a b, ContinuousAt (fun x â†¦ F x t) xâ‚€) (haâ‚€ : aâ‚€ âˆˆ Ioo a b)
    (hbâ‚€ : bâ‚€ âˆˆ Ioo a b) (hÎ¼bâ‚€ : Î¼ {bâ‚€} = 0) :
    ContinuousAt (fun p : X Ã— â„ â†¦ âˆ« t : â„ in aâ‚€..p.2, F p.1 t âˆ‚Î¼) (xâ‚€, bâ‚€) := by
  have hsub : âˆ€ {aâ‚€ bâ‚€}, aâ‚€ âˆˆ Ioo a b â†’ bâ‚€ âˆˆ Ioo a b â†’ Î™ aâ‚€ bâ‚€ âŠ† Î™ a b := fun haâ‚€ hbâ‚€ â†¦
    (ordConnected_Ioo.uIoc_subset haâ‚€ hbâ‚€).trans (Ioo_subset_Ioc_self.trans Ioc_subset_uIoc)
  have Ioo_nhds : Ioo a b âˆˆ ğ“ bâ‚€ := Ioo_mem_nhds hbâ‚€.1 hbâ‚€.2
  have Icc_nhds : Icc a b âˆˆ ğ“ bâ‚€ := Icc_mem_nhds hbâ‚€.1 hbâ‚€.2
  have hxâ‚€ : âˆ€áµ t : â„ âˆ‚Î¼.restrict (Î™ a b), â€–F xâ‚€ tâ€– â‰¤ bound t := h_bound.self_of_nhds
  have : âˆ€á¶  p : X Ã— â„ in ğ“ (xâ‚€, bâ‚€),
      âˆ« s in aâ‚€..p.2, F p.1 s âˆ‚Î¼ =
        âˆ« s in aâ‚€..bâ‚€, F p.1 s âˆ‚Î¼ + âˆ« s in bâ‚€..p.2, F xâ‚€ s âˆ‚Î¼ +
          âˆ« s in bâ‚€..p.2, F p.1 s - F xâ‚€ s âˆ‚Î¼ := by
    rw [nhds_prod_eq]
    refine (h_bound.prod_mk Ioo_nhds).mono ?_
    rintro âŸ¨x, tâŸ© âŸ¨hx : âˆ€áµ t : â„ âˆ‚Î¼.restrict (Î™ a b), â€–F x tâ€– â‰¤ bound t, ht : t âˆˆ Ioo a bâŸ©
    dsimp
    have hiF : âˆ€ {x aâ‚€ bâ‚€},
        (âˆ€áµ t : â„ âˆ‚Î¼.restrict (Î™ a b), â€–F x tâ€– â‰¤ bound t) â†’ aâ‚€ âˆˆ Ioo a b â†’ bâ‚€ âˆˆ Ioo a b â†’
          IntervalIntegrable (F x) Î¼ aâ‚€ bâ‚€ := fun {x aâ‚€ bâ‚€} hx haâ‚€ hbâ‚€ â†¦
      (bound_integrable.mono_set_ae <| Eventually.of_forall <| hsub haâ‚€ hbâ‚€).mono_fun'
        ((hF_meas x).mono_set <| hsub haâ‚€ hbâ‚€)
        (ae_restrict_of_ae_restrict_of_subset (hsub haâ‚€ hbâ‚€) hx)
    rw [intervalIntegral.integral_sub, add_assoc, add_sub_cancel,
      intervalIntegral.integral_add_adjacent_intervals]
    Â· exact hiF hx haâ‚€ hbâ‚€
    Â· exact hiF hx hbâ‚€ ht
    Â· exact hiF hx hbâ‚€ ht
    Â· exact hiF hxâ‚€ hbâ‚€ ht
  rw [continuousAt_congr this]; clear this
  refine (ContinuousAt.add ?_ ?_).add ?_
  Â· exact (intervalIntegral.continuousAt_of_dominated_interval
        (Eventually.of_forall fun x â†¦ (hF_meas x).mono_set <| hsub haâ‚€ hbâ‚€)
          (h_bound.mono fun x hx â†¦
            ae_imp_of_ae_restrict <| ae_restrict_of_ae_restrict_of_subset (hsub haâ‚€ hbâ‚€) hx)
          (bound_integrable.mono_set_ae <| Eventually.of_forall <| hsub haâ‚€ hbâ‚€) <|
          ae_imp_of_ae_restrict <| ae_restrict_of_ae_restrict_of_subset (hsub haâ‚€ hbâ‚€) h_cont).fst'
  Â· refine (?_ : ContinuousAt (fun t â†¦ âˆ« s in bâ‚€..t, F xâ‚€ s âˆ‚Î¼) bâ‚€).snd'
    apply ContinuousWithinAt.continuousAt _ (Icc_mem_nhds hbâ‚€.1 hbâ‚€.2)
    apply intervalIntegral.continuousWithinAt_primitive hÎ¼bâ‚€
    rw [min_eq_right hbâ‚€.1.le, max_eq_right hbâ‚€.2.le]
    exact bound_integrable.mono_fun' (hF_meas xâ‚€) hxâ‚€
  Â· suffices Tendsto (fun x : X Ã— â„ â†¦ âˆ« s in bâ‚€..x.2, F x.1 s - F xâ‚€ s âˆ‚Î¼) (ğ“ (xâ‚€, bâ‚€)) (ğ“ 0) by
      simpa [ContinuousAt]
    have : âˆ€á¶  p : X Ã— â„ in ğ“ (xâ‚€, bâ‚€),
        â€–âˆ« s in bâ‚€..p.2, F p.1 s - F xâ‚€ s âˆ‚Î¼â€– â‰¤ |âˆ« s in bâ‚€..p.2, 2 * bound s âˆ‚Î¼| := by
      rw [nhds_prod_eq]
      refine (h_bound.prod_mk Ioo_nhds).mono ?_
      rintro âŸ¨x, tâŸ© âŸ¨hx : âˆ€áµ t âˆ‚Î¼.restrict (Î™ a b), â€–F x tâ€– â‰¤ bound t, ht : t âˆˆ Ioo a bâŸ©
      have H : âˆ€áµ t : â„ âˆ‚Î¼.restrict (Î™ bâ‚€ t), â€–F x t - F xâ‚€ tâ€– â‰¤ 2 * bound t := by
        apply (ae_restrict_of_ae_restrict_of_subset (hsub hbâ‚€ ht) (hx.and hxâ‚€)).mono
        rintro s âŸ¨hsâ‚, hsâ‚‚âŸ©
        calc
          â€–F x s - F xâ‚€ sâ€– â‰¤ â€–F x sâ€– + â€–F xâ‚€ sâ€– := norm_sub_le _ _
          _ â‰¤ 2 * bound s := by linarith only [hsâ‚, hsâ‚‚]
      exact intervalIntegral.norm_integral_le_abs_of_norm_le H
        ((bound_integrable.mono_set' <| hsub hbâ‚€ ht).const_mul 2)
    apply squeeze_zero_norm' this
    have : Tendsto (fun t â†¦ âˆ« s in bâ‚€..t, 2 * bound s âˆ‚Î¼) (ğ“ bâ‚€) (ğ“ 0) := by
      suffices ContinuousAt (fun t â†¦ âˆ« s in bâ‚€..t, 2 * bound s âˆ‚Î¼) bâ‚€ by
        simpa [ContinuousAt] using this
      apply ContinuousWithinAt.continuousAt _ Icc_nhds
      apply intervalIntegral.continuousWithinAt_primitive hÎ¼bâ‚€
      apply IntervalIntegrable.const_mul
      apply bound_integrable.mono_set'
      rw [min_eq_right hbâ‚€.1.le, max_eq_right hbâ‚€.2.le]
    rw [nhds_prod_eq]
    exact (continuous_abs.tendsto' _ _ abs_zero).comp (this.comp tendsto_snd)

variable [NoAtoms Î¼]

theorem continuousOn_primitive (h_int : IntegrableOn f (Icc a b) Î¼) :
    ContinuousOn (fun x => âˆ« t in Ioc a x, f t âˆ‚Î¼) (Icc a b) := by
  by_cases h : a â‰¤ b
  Â· have : âˆ€ x âˆˆ Icc a b, âˆ« t in Ioc a x, f t âˆ‚Î¼ = âˆ« t in a..x, f t âˆ‚Î¼ := by
      intro x x_in
      simp_rw [integral_of_le x_in.1]
    rw [continuousOn_congr this]
    intro xâ‚€ _
    refine continuousWithinAt_primitive (measure_singleton xâ‚€) ?_
    simp only [intervalIntegrable_iff_integrableOn_Ioc_of_le, max_eq_right, h,
      min_self]
    exact h_int.mono Ioc_subset_Icc_self le_rfl
  Â· rw [Icc_eq_empty h]
    exact continuousOn_empty _

theorem continuousOn_primitive_Icc (h_int : IntegrableOn f (Icc a b) Î¼) :
    ContinuousOn (fun x => âˆ« t in Icc a x, f t âˆ‚Î¼) (Icc a b) := by
  have aux : (fun x => âˆ« t in Icc a x, f t âˆ‚Î¼) = fun x => âˆ« t in Ioc a x, f t âˆ‚Î¼ := by
    ext x
    exact integral_Icc_eq_integral_Ioc
  rw [aux]
  exact continuousOn_primitive h_int

/-- Note: this assumes that `f` is `IntervalIntegrable`, in contrast to some other lemmas here. -/
theorem continuousOn_primitive_interval' (h_int : IntervalIntegrable f Î¼ bâ‚ bâ‚‚)
    (ha : a âˆˆ [[bâ‚, bâ‚‚]]) : ContinuousOn (fun b => âˆ« x in a..b, f x âˆ‚Î¼) [[bâ‚, bâ‚‚]] := fun _ _ â†¦ by
  refine continuousWithinAt_primitive (measure_singleton _) ?_
  rw [min_eq_right ha.1, max_eq_right ha.2]
  simpa [intervalIntegrable_iff, uIoc] using h_int

theorem continuousOn_primitive_interval (h_int : IntegrableOn f (uIcc a b) Î¼) :
    ContinuousOn (fun x => âˆ« t in a..x, f t âˆ‚Î¼) (uIcc a b) :=
  continuousOn_primitive_interval' h_int.intervalIntegrable left_mem_uIcc

theorem continuousOn_primitive_interval_left (h_int : IntegrableOn f (uIcc a b) Î¼) :
    ContinuousOn (fun x => âˆ« t in x..b, f t âˆ‚Î¼) (uIcc a b) := by
  rw [uIcc_comm a b] at h_int âŠ¢
  simp only [integral_symm b]
  exact (continuousOn_primitive_interval h_int).neg

theorem continuous_primitive (h_int : âˆ€ a b, IntervalIntegrable f Î¼ a b) (a : â„) :
    Continuous fun b => âˆ« x in a..b, f x âˆ‚Î¼ := by
  rw [continuous_iff_continuousAt]
  intro bâ‚€
  obtain âŸ¨bâ‚, hbâ‚âŸ© := exists_lt bâ‚€
  obtain âŸ¨bâ‚‚, hbâ‚‚âŸ© := exists_gt bâ‚€
  apply ContinuousWithinAt.continuousAt _ (Icc_mem_nhds hbâ‚ hbâ‚‚)
  exact continuousWithinAt_primitive (measure_singleton bâ‚€) (h_int _ _)

nonrec theorem _root_.MeasureTheory.Integrable.continuous_primitive (h_int : Integrable f Î¼)
    (a : â„) : Continuous fun b => âˆ« x in a..b, f x âˆ‚Î¼ :=
  continuous_primitive (fun _ _ => h_int.intervalIntegrable) a

variable [IsLocallyFiniteMeasure Î¼] {f : X â†’ â„ â†’ E}

theorem continuous_parametric_primitive_of_continuous
    {aâ‚€ : â„} (hf : Continuous f.uncurry) :
    Continuous fun p : X Ã— â„ â†¦ âˆ« t in aâ‚€..p.2, f p.1 t âˆ‚Î¼ := by
  -- We will prove continuity at a point `(q, bâ‚€)`.
  rw [continuous_iff_continuousAt]
  rintro âŸ¨q, bâ‚€âŸ©
  apply Metric.continuousAt_iff'.2 (fun Îµ Îµpos â†¦ ?_)
  -- choose `a` and `b` such that `(a, b)` contains both `aâ‚€` and `bâ‚€`. We will use uniform
  -- estimates on a neighborhood of the compact set `{q} Ã— [a, b]`.
  obtain âŸ¨a, a_ltâŸ© := exists_lt (min aâ‚€ bâ‚€)
  obtain âŸ¨b, lt_bâŸ© := exists_gt (max aâ‚€ bâ‚€)
  rw [lt_min_iff] at a_lt
  rw [max_lt_iff] at lt_b
  have : IsCompact ({q} Ã—Ë¢ (Icc a b)) := isCompact_singleton.prod isCompact_Icc
  -- let `M` be a bound for `f` on the compact set `{q} Ã— [a, b]`.
  obtain âŸ¨M, hMâŸ© := this.bddAbove_image hf.norm.continuousOn
  -- let `Î´` be small enough to satisfy several properties that will show up later.
  obtain âŸ¨Î´, Î´pos, hÎ´, h'Î´, h''Î´âŸ© : âˆƒ (Î´ : â„), 0 < Î´ âˆ§ Î´ < 1 âˆ§ Icc (bâ‚€ - Î´) (bâ‚€ + Î´) âŠ† Icc a b âˆ§
      (M + 1) * Î¼.real (Icc (bâ‚€ - Î´) (bâ‚€ + Î´)) + Î´ * Î¼.real (Icc a b) < Îµ := by
    have A : âˆ€á¶  Î´ in ğ“[>] (0 : â„), Î´ âˆˆ Ioo 0 1 := Ioo_mem_nhdsGT zero_lt_one
    have B : âˆ€á¶  Î´ in ğ“ 0, Icc (bâ‚€ - Î´) (bâ‚€ + Î´) âŠ† Icc a b := by
      have I : Tendsto (fun Î´ â†¦ bâ‚€ - Î´) (ğ“ 0) (ğ“ (bâ‚€ - 0)) := tendsto_const_nhds.sub tendsto_id
      have J : Tendsto (fun Î´ â†¦ bâ‚€ + Î´) (ğ“ 0) (ğ“ (bâ‚€ + 0)) := tendsto_const_nhds.add tendsto_id
      simp only [sub_zero, add_zero] at I J
      filter_upwards [(tendsto_order.1 I).1 _ a_lt.2, (tendsto_order.1 J).2 _ lt_b.2] with Î´ hÎ´ h'Î´
      exact Icc_subset_Icc hÎ´.le h'Î´.le
    have C : âˆ€á¶  Î´ in ğ“ 0,
        (M + 1) * Î¼.real (Icc (bâ‚€ - Î´) (bâ‚€ + Î´)) + Î´ * Î¼.real (Icc a b) < Îµ := by
      suffices Tendsto
        (fun Î´ â†¦ (M + 1) * Î¼.real (Icc (bâ‚€ - Î´) (bâ‚€ + Î´)) + Î´ * Î¼.real (Icc a b))
          (ğ“ 0) (ğ“ ((M + 1) * (0 : â„â‰¥0âˆ).toReal + 0 * Î¼.real (Icc a b))) by
        simp only [toReal_zero, mul_zero, zero_mul, add_zero] at this
        exact (tendsto_order.1 this).2 _ Îµpos
      apply Tendsto.add (Tendsto.mul tendsto_const_nhds _)
        (Tendsto.mul tendsto_id tendsto_const_nhds)
      exact (tendsto_toReal zero_ne_top).comp (tendsto_measure_Icc _ _)
    rcases (A.and ((B.and C).filter_mono nhdsWithin_le_nhds)).exists with âŸ¨Î´, hÎ´, h'Î´, h''Î´âŸ©
    exact âŸ¨Î´, hÎ´.1, hÎ´.2, h'Î´, h''Î´âŸ©
  -- By compactness of `[a, b]` and continuity of `f` there, if `p` is close enough to `q`
  -- then `f p x` is `Î´`-close to `f q x`, uniformly in `x âˆˆ [a, b]`.
  -- (Note in particular that this implies a bound `M + Î´ â‰¤ M + 1` for `f p x`).
  obtain âŸ¨v, v_mem, hvâŸ© : âˆƒ v âˆˆ ğ“[univ] q, âˆ€ p âˆˆ v, âˆ€ x âˆˆ Icc a b, dist (f p x) (f q x) < Î´ :=
    IsCompact.mem_uniformity_of_prod isCompact_Icc hf.continuousOn (mem_univ _)
      (dist_mem_uniformity Î´pos)
  -- for `p` in this neighborhood and `s` which is `Î´`-close to `bâ‚€`, we will show that the
  -- integrals are `Îµ`-close.
  have : v Ã—Ë¢ (Ioo (bâ‚€ - Î´) (bâ‚€ + Î´)) âˆˆ ğ“ (q, bâ‚€) := by
    rw [nhdsWithin_univ] at v_mem
    simp only [prod_mem_nhds_iff, v_mem, true_and]
    apply Ioo_mem_nhds <;> linarith
  filter_upwards [this]
  rintro âŸ¨p, sâŸ© âŸ¨hp : p âˆˆ v, hs : s âˆˆ Ioo (bâ‚€ - Î´) (bâ‚€ + Î´)âŸ©
  simp only [dist_eq_norm] at hv âŠ¢
  have J r u v : IntervalIntegrable (f r) Î¼ u v := (hf.uncurry_left _).intervalIntegrable _ _
  /- we compute the difference between the integrals by splitting the contribution of the change
  from `bâ‚€` to `s` (which gives a contribution controlled by the measure of `(bâ‚€ - Î´, bâ‚€ + Î´)`,
  small enough thanks to our choice of `Î´`) and the change from `q` to `p`, which is small as
  `f p x` and `f q x` are uniformly close by design. -/
  calc
  â€–âˆ« t in aâ‚€..s, f p t âˆ‚Î¼ - âˆ« t in aâ‚€..bâ‚€, f q t âˆ‚Î¼â€–
    = â€–(âˆ« t in aâ‚€..s, f p t âˆ‚Î¼ - âˆ« t in aâ‚€..bâ‚€, f p t âˆ‚Î¼)
        + (âˆ« t in aâ‚€..bâ‚€, f p t âˆ‚Î¼ - âˆ« t in aâ‚€..bâ‚€, f q t âˆ‚Î¼)â€– := by congr 1; abel
  _ â‰¤ â€–âˆ« t in aâ‚€..s, f p t âˆ‚Î¼ - âˆ« t in aâ‚€..bâ‚€, f p t âˆ‚Î¼â€–
        + â€–âˆ« t in aâ‚€..bâ‚€, f p t âˆ‚Î¼ - âˆ« t in aâ‚€..bâ‚€, f q t âˆ‚Î¼â€– := norm_add_le _ _
  _ = â€–âˆ« t in bâ‚€..s, f p t âˆ‚Î¼â€– + â€–âˆ« t in aâ‚€..bâ‚€, (f p t - f q t) âˆ‚Î¼â€– := by
      congr 2
      Â· rw [integral_interval_sub_left (J _ _ _) (J _ _ _)]
      Â· rw [integral_sub (J _ _ _) (J _ _ _)]
  _ â‰¤ âˆ« t in Î™ bâ‚€ s, â€–f p tâ€– âˆ‚Î¼ + âˆ« t in Î™ aâ‚€ bâ‚€, â€–f p t - f q tâ€– âˆ‚Î¼ := by
      gcongr
      Â· exact norm_integral_le_integral_norm_uIoc
      Â· exact norm_integral_le_integral_norm_uIoc
  _ â‰¤ âˆ« t in Icc (bâ‚€ - Î´) (bâ‚€ + Î´), â€–f p tâ€– âˆ‚Î¼ + âˆ« t in Icc a b, â€–f p t - f q tâ€– âˆ‚Î¼ := by
      gcongr
      Â· exact Eventually.of_forall (fun x â†¦ norm_nonneg _)
      Â· exact (hf.uncurry_left _).norm.integrableOn_Icc
      Â· apply uIoc_subset_uIcc.trans (uIcc_subset_Icc ?_ âŸ¨hs.1.le, hs.2.leâŸ© )
        simp [Î´pos.le]
      Â· exact Eventually.of_forall (fun x â†¦ norm_nonneg _)
      Â· exact ((hf.uncurry_left _).sub (hf.uncurry_left _)).norm.integrableOn_Icc
      Â· exact uIoc_subset_uIcc.trans (uIcc_subset_Icc âŸ¨a_lt.1.le, lt_b.1.leâŸ© âŸ¨a_lt.2.le, lt_b.2.leâŸ©)
  _ â‰¤ âˆ« t in Icc (bâ‚€ - Î´) (bâ‚€ + Î´), M + 1 âˆ‚Î¼ + âˆ« _t in Icc a b, Î´ âˆ‚Î¼ := by
      gcongr with x hx x hx
      Â· exact (hf.uncurry_left _).norm.integrableOn_Icc
      Â· exact continuous_const.integrableOn_Icc
      Â· exact measurableSet_Icc
      Â· calc â€–f p xâ€– = â€–f q x + (f p x - f q x)â€– := by congr; abel
        _ â‰¤ â€–f q xâ€– + â€–f p x - f q xâ€– := norm_add_le _ _
        _ â‰¤ M + Î´ := by
            gcongr
            Â· apply hM
              change (fun x â†¦ â€–Function.uncurry f xâ€–) (q, x) âˆˆ _
              apply mem_image_of_mem
              simp only [singleton_prod, mem_image, Prod.mk.injEq, true_and, exists_eq_right]
              exact h'Î´ hx
            Â· exact le_of_lt (hv _ hp _ (h'Î´ hx))
        _ â‰¤ M + 1 := by linarith
      Â· exact ((hf.uncurry_left _).sub (hf.uncurry_left _)).norm.integrableOn_Icc
      Â· exact continuous_const.integrableOn_Icc
      Â· exact measurableSet_Icc
      Â· exact le_of_lt (hv _ hp _ hx)
  _ = (M + 1) * Î¼.real (Icc (bâ‚€ - Î´) (bâ‚€ + Î´)) + Î´ * Î¼.real (Icc a b) := by simp [mul_comm]
  _ < Îµ := h''Î´

@[fun_prop]
theorem continuous_parametric_intervalIntegral_of_continuous {aâ‚€ : â„}
    (hf : Continuous f.uncurry) {s : X â†’ â„} (hs : Continuous s) :
    Continuous fun x â†¦ âˆ« t in aâ‚€..s x, f x t âˆ‚Î¼ :=
  show Continuous ((fun p : X Ã— â„ â†¦ âˆ« t in aâ‚€..p.2, f p.1 t âˆ‚Î¼) âˆ˜ fun x â†¦ (x, s x)) from
    (continuous_parametric_primitive_of_continuous hf).compâ‚‚ continuous_id hs

theorem continuous_parametric_intervalIntegral_of_continuous'
    (hf : Continuous f.uncurry) (aâ‚€ bâ‚€ : â„) :
    Continuous fun x â†¦ âˆ« t in aâ‚€..bâ‚€, f x t âˆ‚Î¼ := by fun_prop

end ContinuousPrimitive

end intervalIntegral

end DominatedConvergenceTheorem
