/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.MeasureTheory.Constructions.Pi
import Mathlib.MeasureTheory.Integral.Bochner.ContinuousLinearMap

/-!
# Integrability in a product space

We prove that `f : (i : Î¹) â†’ X â†’ E i` is in `Láµ–` if and only if for all `i`, `f i` is in `Láµ–`.
We do the same for `f : X â†’ (E Ã— F)`.
-/

section aux

variable {Î¹ ğ•œ : Type*} [Fintype Î¹] [DecidableEq Î¹] [NontriviallyNormedField ğ•œ] {E : Î¹ â†’ Type*}

lemma Isometry.single [âˆ€ i, PseudoEMetricSpace (E i)] [âˆ€ i, Zero (E i)] (i : Î¹) :
    Isometry (Pi.single (M := E) i) := by
  intro x y
  rw [edist_pi_def, Finset.sup_univ_eq_ciSup]
  refine le_antisymm (iSup_le fun j â†¦ ?_) (le_iSup_of_le i (by simp))
  obtain rfl | h := eq_or_ne i j
  Â· simp
  Â· simp [h]

lemma ContinuousLinearMap.norm_single_le_one [âˆ€ i, SeminormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace ğ•œ (E i)] (i : Î¹) :
    â€–ContinuousLinearMap.single ğ•œ E iâ€– â‰¤ 1 := by
  have : Isometry (ContinuousLinearMap.single ğ•œ E i).toLinearMap := Isometry.single i
  change
    â€–((ContinuousLinearMap.single ğ•œ E i).toLinearMap.toLinearIsometry
      this).toContinuousLinearMapâ€– â‰¤ 1
  exact LinearIsometry.norm_toContinuousLinearMap_le _

lemma ContinuousLinearMap.norm_single [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
    (i : Î¹) [Nontrivial (E i)] :
    â€–ContinuousLinearMap.single ğ•œ E iâ€– = 1 := by
  have : Isometry (ContinuousLinearMap.single ğ•œ E i).toLinearMap := Isometry.single i
  change
    â€–((ContinuousLinearMap.single ğ•œ E i).toLinearMap.toLinearIsometry
      this).toContinuousLinearMapâ€– = 1
  exact LinearIsometry.norm_toContinuousLinearMap _

variable {E F : Type*} [PseudoEMetricSpace E] [PseudoEMetricSpace F]
    [AddZeroClass E] [AddZeroClass F]

lemma Isometry.inl : Isometry (AddMonoidHom.inl E F) := by
  intro x y
  rw [Prod.edist_eq]
  simp

lemma Isometry.inr : Isometry (AddMonoidHom.inr E F) := by
  intro x y
  rw [Prod.edist_eq]
  simp

end aux

namespace MeasureTheory

open scoped ENNReal

variable {X : Type*} {mX : MeasurableSpace X} {Î¼ : Measure X} {p : â„â‰¥0âˆ}

section Pi

variable {Î¹ : Type*} [Fintype Î¹] {E : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    {f : (i : Î¹) â†’ X â†’ E i}

lemma memLp_pi_iff : MemLp (fun x â†¦ (f Â· x)) p Î¼ â†” âˆ€ i, MemLp (f i) p Î¼ where
  mp hf i := by
    have : f i = (Function.eval i) âˆ˜ (fun x â†¦ (f Â· x)) := by ext; simp
    rw [this]
    exact (LipschitzWith.eval i).comp_memLp (by simp) hf
  mpr hf := by
    classical
    have : (fun x â†¦ (f Â· x)) = âˆ‘ i, (Pi.single i) âˆ˜ (f i) := by ext; simp
    rw [this]
    refine memLp_finset_sum' _ fun i _ â†¦ ?_
    exact (Isometry.single i).lipschitz.comp_memLp (by simp) (hf i)

alias âŸ¨MemLp.eval, MemLp.of_evalâŸ© := memLp_pi_iff

lemma integrable_pi_iff : Integrable (fun x â†¦ (f Â· x)) Î¼ â†” âˆ€ i, Integrable (f i) Î¼ := by
  simp_rw [â† memLp_one_iff_integrable, memLp_pi_iff]

alias âŸ¨Integrable.eval, Integrable.of_evalâŸ© := integrable_pi_iff

variable [âˆ€ i, NormedSpace â„ (E i)] [âˆ€ i, CompleteSpace (E i)]

lemma eval_integral (hf : âˆ€ i, Integrable (f i) Î¼) (i : Î¹) :
    (âˆ« x, (f Â· x) âˆ‚Î¼) i = âˆ« x, f i x âˆ‚Î¼ := by
  rw [â† ContinuousLinearMap.proj_apply (R := â„) i (âˆ« x, (f Â· x) âˆ‚Î¼),
    â† ContinuousLinearMap.integral_comp_comm]
  Â· simp
  exact Integrable.of_eval hf

variable {X : Î¹ â†’ Type*} {mX : âˆ€ i, MeasurableSpace (X i)} {Î¼ : (i : Î¹) â†’ Measure (X i)}

lemma Measure.pi_map_eval [âˆ€ i, SigmaFinite (Î¼ i)] [DecidableEq Î¹] (i : Î¹) :
    (Measure.pi Î¼).map (Function.eval i) = (âˆ j âˆˆ Finset.univ.erase i, Î¼ j Set.univ) â€¢ (Î¼ i) := by
  ext s hs
  classical
  rw [Measure.map_apply (measurable_pi_apply i) hs, â† Set.univ_pi_update_univ, Measure.pi_pi,
    Measure.smul_apply, smul_eq_mul, â† Finset.prod_erase_mul _ _ (a := i) (by simp)]
  congrm ?_ * ?_
  swap; Â· simp
  refine Finset.prod_congr rfl fun j hj â†¦ ?_
  simp [Function.update, Finset.ne_of_mem_erase hj]

variable {E : Type*} [NormedAddCommGroup E]

lemma integrable_eval_pi [âˆ€ i, IsFiniteMeasure (Î¼ i)] {i : Î¹} {f : X i â†’ E}
    (hf : Integrable f (Î¼ i)) :
    Integrable (fun x â†¦ f (x i)) (Measure.pi Î¼) := by
  simp_rw [â† Function.eval_apply (x := i)]
  refine Integrable.comp_measurable ?_ (by fun_prop)
  classical
  rw [Measure.pi_map_eval]
  exact hf.smul_measure <| ENNReal.prod_ne_top (fun _ _ â†¦ measure_ne_top _ _)

lemma measurePreserving_eval [âˆ€ i, IsProbabilityMeasure (Î¼ i)] (i : Î¹) :
    MeasurePreserving (Function.eval i) (Measure.pi Î¼) (Î¼ i) := by
  refine âŸ¨measurable_pi_apply i, ?_âŸ©
  classical
  rw [Measure.pi_map_eval, Finset.prod_eq_one, one_smul]
  exact fun _ _ â†¦ measure_univ

lemma integral_eval_pi [NormedSpace â„ E] [CompleteSpace E]
    [âˆ€ i, IsProbabilityMeasure (Î¼ i)] {i : Î¹} {f : X i â†’ E} (hf : AEStronglyMeasurable f (Î¼ i)) :
    âˆ« (x : Î  i, X i), f (x i) âˆ‚Measure.pi Î¼ = âˆ« x, f x âˆ‚Î¼ i := by
  simp_rw [â† Function.eval_apply (Î² := X) (x := i)]
  rw [â† integral_map, (measurePreserving_eval i).map_eq]
  Â· exact Measurable.aemeasurable (by fun_prop)
  Â· rwa [(measurePreserving_eval i).map_eq]

end Pi

section Prod

variable {E F : Type*} [NormedAddCommGroup E] [NormedAddCommGroup F] {f : X â†’ E Ã— F}

lemma memLp_prod_iff :
    MemLp f p Î¼ â†” MemLp (fun x â†¦ (f x).fst) p Î¼ âˆ§ MemLp (fun x â†¦ (f x).snd) p Î¼ where
  mp h := âŸ¨LipschitzWith.prod_fst.comp_memLp (by simp) h,
    LipschitzWith.prod_snd.comp_memLp (by simp) hâŸ©
  mpr h := by
    have : f = (AddMonoidHom.inl E F) âˆ˜ (fun x â†¦ (f x).fst) +
        (AddMonoidHom.inr E F) âˆ˜ (fun x â†¦ (f x).snd) := by
      ext; all_goals simp
    rw [this]
    exact MemLp.add (Isometry.inl.lipschitz.comp_memLp (by simp) h.1)
      (Isometry.inr.lipschitz.comp_memLp (by simp) h.2)

lemma MemLp.fst (h : MemLp f p Î¼) : MemLp (fun x â†¦ (f x).fst) p Î¼ :=
  memLp_prod_iff.1 h |>.1

lemma MemLp.snd (h : MemLp f p Î¼) : MemLp (fun x â†¦ (f x).snd) p Î¼ :=
  memLp_prod_iff.1 h |>.2

alias âŸ¨_, MemLp.of_fst_sndâŸ© := memLp_prod_iff

end Prod

end MeasureTheory
