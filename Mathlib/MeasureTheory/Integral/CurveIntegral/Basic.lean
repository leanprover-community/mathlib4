/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Order.Field.Pointwise
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.AffineMap
import Mathlib.Analysis.Calculus.Deriv.Shift
import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# Integral of a 1-form along a path

In this file we define the integral of a 1-form along a path indexed by `[0, 1]`
and prove basic properties of this operation.

The integral `âˆ«á¶œ x in Î³, Ï‰ x` is defined as $\int_0^1 \omega(\gamma(t))(\gamma'(t))$.
More precisely, we use

- `Path.extend Î³ t` instead of `Î³ t`, because both derivatives and `intervalIntegral`
  expect globally defined functions;
- `derivWithin Î³.extend (Set.Icc 0 1) t`, not `deriv Î³.extend t`, for the derivative,
  so that it takes meaningful values at `t = 0` and `t = 1`,
  even though this does not affect the integral.

The argument `Ï‰ : E â†’ E â†’L[ğ•œ] F` is a `ğ•œ`-linear 1-form on `E` taking values in `F`,
where `ğ•œ` is `â„` or `â„‚`.
The definition does not depend on `ğ•œ`, see `curveIntegral_restrictScalars` and nearby lemmas.
However, the fact that `ğ•œ = â„` is not hardcoded
allows us to avoid inserting `ContinuousLinearMap.restrictScalars` here and there.

## Main definitions

- `curveIntegral Ï‰ Î³`, notation `âˆ«á¶œ x in Î³, Ï‰ x`, is the integral of a 1-form `Ï‰` along a path `Î³`.
- `CurveIntegrable Ï‰ Î³` is the predicate saying that the above integral makes sense.

## Main results

We prove that `curveIntegral` well behaves with respect to

- operations on `Path`s, see `curveIntegral_refl`, `curveIntegral_symm`, `curveIntegral_trans` etc;
- algebraic operations on 1-forms, see `curveIntegral_add` etc.

We also show that the derivative of `fun b â†¦ âˆ«á¶œ x in Path.segment a b, Ï‰ x`
has derivative `Ï‰ a` at `b = a`.
We provide 2 versions of this result: one for derivative (`HasFDerivWithinAt`) within a convex set
and one for `HasFDerivAt`.

## Implementation notes

### Naming

In literature, the integral of a function or a 1-form along a path
is called â€œline integralâ€, â€œpath integralâ€, â€œcurve integralâ€, or â€œcurvelinear integralâ€.

We use the name â€œcurve integralâ€ instead of other names for the following reasons:

- for many people whose mother tongue is not English,
  â€œline integralâ€ sounds like an integral along a straight line;

- we reserve the name "path integral" for Feynmann-style integrals over the space of paths.

### Usage of `ContinuousLinearMap`s for 1-forms

Similarly to the way `fderiv` uses continuous linear maps
while higher order derivatives use continuous multilinear maps,
this file uses `E â†’ E â†’L[ğ•œ] F` instead of continuous alternating maps for 1-forms.

### Differentiability assumptions

The definitions in this file make sense if the path is a piecewise $C^1$ curve.
PoincarÃ© lemma (formalization WIP, see #24019) implies that for a closed 1-form on an open set `U`,
the integral depends on the homotopy class of the path only,
thus we can define the integral along a continuous path
or an element of the fundamental groupoid of `U`.

### Usage of an extra field

The definitions in this file deal with `ğ•œ`-linear 1-forms.
This allows us to avoid using `ContinuousLinearMap.restrictScalars`
in `HasFDerivWithinAt.curveIntegral_segment_source`
and a future formalization of PoincarÃ© lemma.
-/

open Metric MeasureTheory Topology Set Interval AffineMap Convex Filter
open scoped Pointwise unitInterval

section Defs

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {a b : E}

/-- The function `t â†¦ Ï‰ (Î³ t) (Î³' t)` which appears in the definition of a curve integral.

This definition is used to factor out common parts of lemmas
about `CurveIntegrable` and `curveIntegral`. -/
noncomputable irreducible_def curveIntegralFun (lemma := curveIntegralFun_def')
    (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (t : â„) : F :=
  letI : NormedSpace â„ E := .restrictScalars â„ ğ•œ E
  Ï‰ (Î³.extend t) (derivWithin Î³.extend I t)

/-- A 1-form `Ï‰` is *curve integrable* along a path `Î³`,
if the function `curveIntegralFun Ï‰ Î³ t = Ï‰ (Î³ t) (Î³' t)` is integrable on `[0, 1]`.

The actual definition uses `Path.extend Î³`,
because both interval integrals and derivatives expect globally defined functions.
-/
def CurveIntegrable (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) : Prop :=
  IntervalIntegrable (curveIntegralFun Ï‰ Î³) volume 0 1

/-- Integral of a 1-form `Ï‰ : E â†’ E â†’L[ğ•œ] F` along a path `Î³`,
defined as $\int_0^1 \omega(\gamma(t))(\gamma'(t))$.

The actual definition uses `curveIntegralFun` which uses `Path.extend Î³`
and `derivWithin (Path.extend Î³) (Set.Icc 0 1) t`,
because calculus-related definitions in Mathlib expect globally defined functions as arguments. -/
noncomputable irreducible_def curveIntegral (lemma := curveIntegral_def')
    (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) : F :=
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  âˆ« t in 0..1, curveIntegralFun Ï‰ Î³ t

@[inherit_doc curveIntegral]
notation3 "âˆ«á¶œ "(...)" in " Î³ ", "r:67:(scoped Ï‰ => curveIntegral Ï‰ Î³) => r

/-- curve integral is defined using Bochner integral,
thus it is defined as zero whenever the codomain is not a complete space. -/
theorem curveIntegral_of_not_completeSpace (h : Â¬CompleteSpace F) (Ï‰ : E â†’ E â†’L[ğ•œ] F)
    (Î³ : Path a b) : âˆ«á¶œ x in Î³, Ï‰ x = 0 := by
  simp [curveIntegral, intervalIntegral, integral, h]

theorem curveIntegralFun_def [NormedSpace â„ E] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (t : â„) :
    curveIntegralFun Ï‰ Î³ t = Ï‰ (Î³.extend t) (derivWithin Î³.extend I t) := by
  simp only [curveIntegralFun, NormedSpace.restrictScalars_eq]

theorem curveIntegral_def [NormedSpace â„ F] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    curveIntegral Ï‰ Î³ = âˆ« t in 0..1, curveIntegralFun Ï‰ Î³ t := by
  simp only [curveIntegral, NormedSpace.restrictScalars_eq]

theorem curveIntegral_eq_intervalIntegral_deriv [NormedSpace â„ E] [NormedSpace â„ F]
    (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    âˆ«á¶œ x in Î³, Ï‰ x = âˆ« t in 0..1, Ï‰ (Î³.extend t) (deriv Î³.extend t) := by
  simp only [curveIntegral_def, curveIntegralFun_def]
  apply intervalIntegral.integral_congr_ae_restrict
  rw [uIoc_of_le zero_le_one, â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem (by measurability)] with x hx
  rw [derivWithin_of_mem_nhds (by simpa)]

end Defs

/-!
### Operations on paths
-/

section PathOperations

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {a b c d : E} {Ï‰ : E â†’ E â†’L[ğ•œ] F}
  {Î³ Î³ab : Path a b} {Î³bc : Path b c} {t : â„}

@[simp]
theorem curveIntegralFun_refl (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a : E) : curveIntegralFun Ï‰ (.refl a) = 0 := by
  ext
  simp [curveIntegralFun, â† Function.const_def]

@[simp]
theorem curveIntegral_refl (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a : E) : âˆ«á¶œ x in .refl a, Ï‰ x = 0 := by
  simp [curveIntegral]

@[simp]
theorem CurveIntegrable.refl (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a : E) : CurveIntegrable Ï‰ (.refl a) := by
  simp [CurveIntegrable, Pi.zero_def]

@[simp]
theorem curveIntegralFun_cast (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (hc : c = a) (hd : d = b) :
    curveIntegralFun Ï‰ (Î³.cast hc hd) = curveIntegralFun Ï‰ Î³ := by
  ext t
  simp only [curveIntegralFun_def', Path.extend_cast]

@[simp]
theorem curveIntegral_cast (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (hc : c = a) (hd : d = b) :
    âˆ«á¶œ x in Î³.cast hc hd, Ï‰ x = âˆ«á¶œ x in Î³, Ï‰ x := by
  simp [curveIntegral]

@[simp]
theorem curveIntegrable_cast_iff (hc : c = a) (hd : d = b) :
    CurveIntegrable Ï‰ (Î³.cast hc hd) â†” CurveIntegrable Ï‰ Î³ := by
  simp [CurveIntegrable]

protected alias âŸ¨_, CurveIntegrable.castâŸ© := curveIntegrable_cast_iff

theorem curveIntegralFun_symm_apply (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) (t : â„) :
    curveIntegralFun Ï‰ Î³.symm t = -curveIntegralFun Ï‰ Î³ (1 - t) := by
  simp [curveIntegralFun, Î³.extend_symm, derivWithin_comp_const_sub]

@[simp]
theorem curveIntegralFun_symm (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    curveIntegralFun Ï‰ Î³.symm = (-curveIntegralFun Ï‰ Î³ <| 1 - Â·) :=
  funext <| curveIntegralFun_symm_apply Ï‰ Î³

protected theorem CurveIntegrable.symm (h : CurveIntegrable Ï‰ Î³) : CurveIntegrable Ï‰ Î³.symm := by
  simpa [CurveIntegrable] using (h.comp_sub_left 1).neg.symm

@[simp]
theorem curveIntegrable_symm : CurveIntegrable Ï‰ Î³.symm â†” CurveIntegrable Ï‰ Î³ :=
  âŸ¨fun h â†¦ by simpa using h.symm, .symmâŸ©

@[simp]
theorem curveIntegral_symm (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ : Path a b) :
    âˆ«á¶œ x in Î³.symm, Ï‰ x = -âˆ«á¶œ x in Î³, Ï‰ x := by
  simp [curveIntegral, curveIntegralFun_symm]

theorem curveIntegralFun_trans_of_lt_half (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c)
    (ht : t < 1 / 2) :
    curveIntegralFun Ï‰ (Î³ab.trans Î³bc) t = (2 : â„•) â€¢ curveIntegralFun Ï‰ Î³ab (2 * t) := by
  let instE := NormedSpace.restrictScalars â„ ğ•œ E
  have Hâ‚ : (Î³ab.trans Î³bc).extend =á¶ [ğ“ t] (fun s â†¦ Î³ab.extend (2 * s)) :=
    (eventually_le_nhds ht).mono fun _ â†¦ Path.extend_trans_of_le_half _ _
  have Hâ‚‚ : (2 : â„) â€¢ I =á¶ [ğ“ (2 * t)] I := by
    rw [LinearOrderedField.smul_Icc two_pos, mul_zero, mul_one, â† nhdsWithin_eq_iff_eventuallyEq]
    rcases lt_trichotomy t 0 with htâ‚€ | rfl | htâ‚€
    Â· rw [notMem_closure_iff_nhdsWithin_eq_bot.mp, notMem_closure_iff_nhdsWithin_eq_bot.mp] <;>
        simp_intro h <;> linarith
    Â· simp
    Â· rw [nhdsWithin_eq_nhds.2, nhdsWithin_eq_nhds.2] <;> simp [*] <;> linarith
  rw [curveIntegralFun_def, Hâ‚.self_of_nhds, Hâ‚.derivWithin_eq_of_nhds, curveIntegralFun_def,
    derivWithin_comp_mul_left, ofNat_smul_eq_nsmul, map_nsmul, derivWithin_congr_set Hâ‚‚]

theorem curveIntegralFun_trans_aeeq_left (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c) :
    curveIntegralFun Ï‰ (Î³ab.trans Î³bc) =áµ[volume.restrict (Î™ (0 : â„) (1 / 2))]
      fun t â†¦ (2 : â„•) â€¢ curveIntegralFun Ï‰ Î³ab (2 * t) := by
  rw [uIoc_of_le (by positivity), â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem measurableSet_Ioo] with t âŸ¨htâ‚€, htâŸ©
  exact curveIntegralFun_trans_of_lt_half Ï‰ Î³ab Î³bc ht

theorem curveIntegralFun_trans_of_half_lt (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c)
    (htâ‚€ : 1 / 2 < t) :
    curveIntegralFun Ï‰ (Î³ab.trans Î³bc) t = (2 : â„•) â€¢ curveIntegralFun Ï‰ Î³bc (2 * t - 1) := by
  rw [â† (Î³ab.trans Î³bc).symm_symm, curveIntegralFun_symm_apply, Path.trans_symm,
    curveIntegralFun_trans_of_lt_half (ht := by linarith), curveIntegralFun_symm_apply, smul_neg,
    neg_neg]
  congr 2
  ring

theorem curveIntegralFun_trans_aeeq_right (Ï‰ : E â†’ E â†’L[ğ•œ] F) (Î³ab : Path a b) (Î³bc : Path b c) :
    curveIntegralFun Ï‰ (Î³ab.trans Î³bc) =áµ[volume.restrict (Î™ (1 / 2 : â„) 1)]
      fun t â†¦ (2 : â„•) â€¢ curveIntegralFun Ï‰ Î³bc (2 * t - 1) := by
  rw [uIoc_of_le (by linarith), â† restrict_Ioo_eq_restrict_Ioc]
  filter_upwards [ae_restrict_mem measurableSet_Ioo] with t âŸ¨htâ‚, htâ‚‚âŸ©
  exact curveIntegralFun_trans_of_half_lt Ï‰ Î³ab Î³bc htâ‚

theorem CurveIntegrable.intervalIntegrable_curveIntegralFun_trans_left
    (h : CurveIntegrable Ï‰ Î³ab) (Î³bc : Path b c) :
    IntervalIntegrable (curveIntegralFun Ï‰ (Î³ab.trans Î³bc)) volume 0 (1 / 2) := by
  refine .congr_ae ?_ (curveIntegralFun_trans_aeeq_left _ _ _).symm
  simpa [ofNat_smul_eq_nsmul] using h.comp_mul_left.smul (2 : ğ•œ)

theorem CurveIntegrable.intervalIntegrable_curveIntegralFun_trans_right
    (Î³ab : Path a b) (h : CurveIntegrable Ï‰ Î³bc) :
    IntervalIntegrable (curveIntegralFun Ï‰ (Î³ab.trans Î³bc)) volume (1 / 2) 1 := by
  refine .congr_ae ?_ (curveIntegralFun_trans_aeeq_right _ _ _).symm
  simpa [ofNat_smul_eq_nsmul] using h.comp_sub_right 1 |>.comp_mul_left (c := 2) |>.smul (2 : ğ•œ)

protected theorem CurveIntegrable.trans (hâ‚ : CurveIntegrable Ï‰ Î³ab) (hâ‚‚ : CurveIntegrable Ï‰ Î³bc) :
    CurveIntegrable Ï‰ (Î³ab.trans Î³bc) :=
  (hâ‚.intervalIntegrable_curveIntegralFun_trans_left Î³bc).trans
    (hâ‚‚.intervalIntegrable_curveIntegralFun_trans_right Î³ab)

theorem curveIntegral_trans (hâ‚ : CurveIntegrable Ï‰ Î³ab) (hâ‚‚ : CurveIntegrable Ï‰ Î³bc) :
    âˆ«á¶œ x in Î³ab.trans Î³bc, Ï‰ x = (âˆ«á¶œ x in Î³ab, Ï‰ x) + âˆ«á¶œ x in Î³bc, Ï‰ x := by
  let instF := NormedSpace.restrictScalars â„ ğ•œ F
  rw [curveIntegral_def, â† intervalIntegral.integral_add_adjacent_intervals
    (hâ‚.intervalIntegrable_curveIntegralFun_trans_left Î³bc)
    (hâ‚‚.intervalIntegrable_curveIntegralFun_trans_right Î³ab),
    intervalIntegral.integral_congr_ae_restrict (curveIntegralFun_trans_aeeq_left _ _ _),
    intervalIntegral.integral_congr_ae_restrict (curveIntegralFun_trans_aeeq_right _ _ _)]
  simp only [â† ofNat_smul_eq_nsmul (R := â„)]
  rw [intervalIntegral.integral_smul, intervalIntegral.smul_integral_comp_mul_left,
    intervalIntegral.integral_smul,
    intervalIntegral.smul_integral_comp_mul_left (f := (curveIntegralFun Ï‰ Î³bc <| Â· - 1)),
    intervalIntegral.integral_comp_sub_right]
  simp only [curveIntegral_def]
  norm_num

theorem curveIntegralFun_segment [NormedSpace â„ E] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a b : E)
    {t : â„} (ht : t âˆˆ I) : curveIntegralFun Ï‰ (.segment a b) t = Ï‰ (lineMap a b t) (b - a) := by
  have := Path.eqOn_extend_segment a b
  simp only [curveIntegralFun_def, this ht, derivWithin_congr this (this ht),
    (hasDerivWithinAt_lineMap ..).derivWithin (uniqueDiffOn_Icc_zero_one t ht)]

theorem curveIntegrable_segment [NormedSpace â„ E] :
    CurveIntegrable Ï‰ (.segment a b) â†”
      IntervalIntegrable (fun t â†¦ Ï‰ (lineMap a b t) (b - a)) volume 0 1 := by
  rw [CurveIntegrable, intervalIntegrable_congr]
  rw [uIoc_of_le zero_le_one]
  exact .mono Ioc_subset_Icc_self fun _t â†¦ curveIntegralFun_segment Ï‰ a b

theorem curveIntegral_segment [NormedSpace â„ E] [NormedSpace â„ F] (Ï‰ : E â†’ E â†’L[ğ•œ] F) (a b : E) :
    âˆ«á¶œ x in .segment a b, Ï‰ x = âˆ« t in 0..1, Ï‰ (lineMap a b t) (b - a) := by
  rw [curveIntegral_def]
  refine intervalIntegral.integral_congr fun t ht â†¦ ?_
  rw [uIcc_of_le zero_le_one] at ht
  exact curveIntegralFun_segment Ï‰ a b ht

@[simp]
theorem curveIntegral_segment_const [NormedSpace â„ E] [CompleteSpace F] (Ï‰ : E â†’L[ğ•œ] F) (a b : E) :
    âˆ«á¶œ _ in .segment a b, Ï‰ = Ï‰ (b - a) := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp [curveIntegral_segment]

/-- If `â€–Ï‰ zâ€– â‰¤ C` at all points of the segment `[a -[â„] b]`,
then the curve integral `âˆ«á¶œ x in .segment a b, Ï‰ x` has norm at most `C * â€–b - aâ€–`. -/
theorem norm_curveIntegral_segment_le [NormedSpace â„ E] {C : â„} (h : âˆ€ z âˆˆ [a -[â„] b], â€–Ï‰ zâ€– â‰¤ C) :
    â€–âˆ«á¶œ x in .segment a b, Ï‰ xâ€– â‰¤ C * â€–b - aâ€– := calc
  â€–âˆ«á¶œ x in .segment a b, Ï‰ xâ€– â‰¤ C * â€–b - aâ€– * |1 - 0| := by
    letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
    rw [curveIntegral_segment]
    refine intervalIntegral.norm_integral_le_of_norm_le_const fun t ht â†¦ ?_
    rw [segment_eq_image_lineMap] at h
    rw [uIoc_of_le zero_le_one] at ht
    apply_rules [(Ï‰ _).le_of_opNorm_le, mem_image_of_mem, Ioc_subset_Icc_self]
  _ = C * â€–b - aâ€– := by simp

/-- If a 1-form `Ï‰` is continuous on a set `s`,
then it is curve integrable along any $C^1$ path in this set. -/
theorem ContinuousOn.curveIntegrable_of_contDiffOn [NormedSpace â„ E] {s : Set E}
    (hÏ‰ : ContinuousOn Ï‰ s) (hÎ³ : ContDiffOn â„ 1 Î³.extend I) (hÎ³s : âˆ€ t, Î³ t âˆˆ s) :
    CurveIntegrable Ï‰ Î³ := by
  apply ContinuousOn.intervalIntegrable_of_Icc zero_le_one
  simp only [funext (curveIntegralFun_def Ï‰ Î³)]
  apply ContinuousOn.clm_apply
  Â· exact hÏ‰.comp (by fun_prop) fun _ _ â†¦ hÎ³s _
  Â· exact hÎ³.continuousOn_derivWithin uniqueDiffOn_Icc_zero_one le_rfl

end PathOperations

/-!
### Algebraic operations on the 1-form
-/

section Algebra

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {a b : E}
  {Ï‰ Ï‰â‚ Ï‰â‚‚ : E â†’ E â†’L[ğ•œ] F} {Î³ : Path a b} {t : â„}

@[simp]
theorem curveIntegralFun_add :
    curveIntegralFun (Ï‰â‚ + Ï‰â‚‚) Î³ = curveIntegralFun Ï‰â‚ Î³ + curveIntegralFun Ï‰â‚‚ Î³ := by
  ext; simp [curveIntegralFun]

protected theorem CurveIntegrable.add (hâ‚ : CurveIntegrable Ï‰â‚ Î³) (hâ‚‚ : CurveIntegrable Ï‰â‚‚ Î³) :
    CurveIntegrable (Ï‰â‚ + Ï‰â‚‚) Î³ := by
  simpa [CurveIntegrable] using IntervalIntegrable.add hâ‚ hâ‚‚

theorem curveIntegral_add (hâ‚ : CurveIntegrable Ï‰â‚ Î³) (hâ‚‚ : CurveIntegrable Ï‰â‚‚ Î³) :
    curveIntegral (Ï‰â‚ + Ï‰â‚‚) Î³ = âˆ«á¶œ x in Î³, Ï‰â‚ x + âˆ«á¶œ x in Î³, Ï‰â‚‚ x := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp only [curveIntegral, curveIntegralFun_add]
  exact intervalIntegral.integral_add hâ‚ hâ‚‚

theorem curveIntegral_fun_add (hâ‚ : CurveIntegrable Ï‰â‚ Î³) (hâ‚‚ : CurveIntegrable Ï‰â‚‚ Î³) :
    âˆ«á¶œ x in Î³, (Ï‰â‚ x + Ï‰â‚‚ x) = âˆ«á¶œ x in Î³, Ï‰â‚ x + âˆ«á¶œ x in Î³, Ï‰â‚‚ x :=
  curveIntegral_add hâ‚ hâ‚‚

@[simp]
theorem curveIntegralFun_zero : curveIntegralFun (0 : E â†’ E â†’L[ğ•œ] F) Î³ = 0 := by
  ext; simp [curveIntegralFun]

@[simp]
theorem curveIntegralFun_fun_zero : curveIntegralFun (fun _ â†¦ 0 : E â†’ E â†’L[ğ•œ] F) Î³ = 0 :=
  curveIntegralFun_zero

theorem CurveIntegrable.zero : CurveIntegrable (0 : E â†’ E â†’L[ğ•œ] F) Î³ := by
  simp [CurveIntegrable, IntervalIntegrable.zero]

theorem CurveIntegrable.fun_zero : CurveIntegrable (fun _ â†¦ 0 : E â†’ E â†’L[ğ•œ] F) Î³ := .zero

@[simp]
theorem curveIntegral_zero : curveIntegral (0 : E â†’ E â†’L[ğ•œ] F) Î³ = 0 := by simp [curveIntegral]

@[simp]
theorem curveIntegral_fun_zero : âˆ«á¶œ _ in Î³, (0 : E â†’L[ğ•œ] F) = 0 := curveIntegral_zero

@[simp]
theorem curveIntegralFun_neg : curveIntegralFun (-Ï‰) Î³ = -curveIntegralFun Ï‰ Î³ := by
  ext; simp [curveIntegralFun]

theorem CurveIntegrable.neg (h : CurveIntegrable Ï‰ Î³) : CurveIntegrable (-Ï‰) Î³ := by
  simpa [CurveIntegrable] using IntervalIntegrable.neg h

theorem CurveIntegrable.fun_neg (h : CurveIntegrable Ï‰ Î³) : CurveIntegrable (-Ï‰ Â·) Î³ :=
  h.neg

@[simp]
theorem curveIntegrable_neg_iff : CurveIntegrable (-Ï‰) Î³ â†” CurveIntegrable Ï‰ Î³ :=
  âŸ¨fun h â†¦ by simpa using h.neg, .negâŸ©

@[simp]
theorem curveIntegrable_fun_neg_iff : CurveIntegrable (-Ï‰ Â·) Î³ â†” CurveIntegrable Ï‰ Î³ :=
  curveIntegrable_neg_iff

@[simp]
theorem curveIntegral_neg : curveIntegral (-Ï‰) Î³ = -âˆ«á¶œ x in Î³, Ï‰ x := by
  simp [curveIntegral]

@[simp]
theorem curveIntegral_fun_neg : âˆ«á¶œ x in Î³, -Ï‰ x = -âˆ«á¶œ x in Î³, Ï‰ x := curveIntegral_neg

@[simp]
theorem curveIntegralFun_sub :
    curveIntegralFun (Ï‰â‚ - Ï‰â‚‚) Î³ = curveIntegralFun Ï‰â‚ Î³ - curveIntegralFun Ï‰â‚‚ Î³ := by
  simp [sub_eq_add_neg]

protected theorem CurveIntegrable.sub (hâ‚ : CurveIntegrable Ï‰â‚ Î³) (hâ‚‚ : CurveIntegrable Ï‰â‚‚ Î³) :
    CurveIntegrable (Ï‰â‚ - Ï‰â‚‚) Î³ :=
  sub_eq_add_neg Ï‰â‚ Ï‰â‚‚ â–¸ hâ‚.add hâ‚‚.neg

theorem curveIntegral_sub (hâ‚ : CurveIntegrable Ï‰â‚ Î³) (hâ‚‚ : CurveIntegrable Ï‰â‚‚ Î³) :
    curveIntegral (Ï‰â‚ - Ï‰â‚‚) Î³ = âˆ«á¶œ x in Î³, Ï‰â‚ x - âˆ«á¶œ x in Î³, Ï‰â‚‚ x := by
  rw [sub_eq_add_neg, sub_eq_add_neg, curveIntegral_add hâ‚ hâ‚‚.neg, curveIntegral_neg]

theorem curveIntegral_fun_sub (hâ‚ : CurveIntegrable Ï‰â‚ Î³) (hâ‚‚ : CurveIntegrable Ï‰â‚‚ Î³) :
    âˆ«á¶œ x in Î³, (Ï‰â‚ x - Ï‰â‚‚ x) = âˆ«á¶œ x in Î³, Ï‰â‚ x - âˆ«á¶œ x in Î³, Ï‰â‚‚ x :=
  curveIntegral_sub hâ‚ hâ‚‚


section RestrictScalars

variable {ğ• : Type*} [RCLike ğ•] [NormedSpace ğ• F] [NormedSpace ğ• E]
  [LinearMap.CompatibleSMul E F ğ• ğ•œ]

@[simp]
theorem curveIntegralFun_restrictScalars :
    curveIntegralFun (fun t â†¦ (Ï‰ t).restrictScalars ğ•) Î³ = curveIntegralFun Ï‰ Î³ := by
  ext
  letI : NormedSpace â„ E := .restrictScalars â„ ğ•œ E
  simp [curveIntegralFun_def]

@[simp]
theorem curveIntegrable_restrictScalars_iff :
    CurveIntegrable (fun t â†¦ (Ï‰ t).restrictScalars ğ•) Î³ â†” CurveIntegrable Ï‰ Î³ := by
  simp [CurveIntegrable]

@[simp]
theorem curveIntegral_restrictScalars :
    âˆ«á¶œ x in Î³, (Ï‰ x).restrictScalars ğ• = âˆ«á¶œ x in Î³, Ï‰ x := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp [curveIntegral_def]

end RestrictScalars

variable {ğ• : Type*} [RCLike ğ•] [NormedSpace ğ• F] [SMulCommClass ğ•œ ğ• F] {c : ğ•}

@[simp]
theorem curveIntegralFun_smul : curveIntegralFun (c â€¢ Ï‰) Î³ = c â€¢ curveIntegralFun Ï‰ Î³ := by
  ext
  simp [curveIntegralFun]

theorem CurveIntegrable.smul (h : CurveIntegrable Ï‰ Î³) :
    CurveIntegrable (c â€¢ Ï‰) Î³ := by
  simpa [CurveIntegrable] using IntervalIntegrable.smul h c

@[simp]
theorem curveIntegrable_smul_iff : CurveIntegrable (c â€¢ Ï‰) Î³ â†” c = 0 âˆ¨ CurveIntegrable Ï‰ Î³ := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp [CurveIntegrable.zero]
  Â· simp only [hc, false_or]
    refine âŸ¨fun h â†¦ ?_, .smulâŸ©
    simpa [hc] using h.smul (c := câ»Â¹)

@[simp]
theorem curveIntegral_smul : curveIntegral (c â€¢ Ï‰) Î³ = c â€¢ curveIntegral Ï‰ Î³ := by
  letI : NormedSpace â„ F := .restrictScalars â„ ğ•œ F
  simp [curveIntegral_def, intervalIntegral.integral_smul]

@[simp]
theorem curveIntegral_fun_smul : âˆ«á¶œ x in Î³, c â€¢ Ï‰ x = c â€¢ âˆ«á¶œ x in Î³, Ï‰ x := curveIntegral_smul

end Algebra

section FDeriv

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
  {a b : E} {s : Set E} {Ï‰ : E â†’ E â†’L[ğ•œ] F}

/-!
### Derivative of the curve integral w.r.t. the right endpoint

In this section we prove that the integral of `Ï‰` along `[a -[â„] b]`, as a function of `b`,
has derivative `Ï‰ a` at `b = a`.
We provide several versions of this theorem, for `HasFDerivWithinAt` and `HasFDerivAt`,
as well as for continuity near a point and for continuity on the whole set or space.

Note that we take the derivative at the left endpoint of the segment.
Similar facts about the derivative at a different point are true
provided that `Ï‰` is a closed 1-form (formalization WIP, see #24019).
-/

/-- The integral of `Ï‰` along `[a -[â„] b]`, as a function of `b`, has derivative `Ï‰ a` at `b = a`.
This is a `HasFDerivWithinAt` version assuming that `Ï‰` is continuous within a convex set `s`
in a neighborhood of `a` within `s`. -/
theorem HasFDerivWithinAt.curveIntegral_segment_source' (hs : Convex â„ s)
    (hÏ‰ : âˆ€á¶  x in ğ“[s] a, ContinuousWithinAt Ï‰ s x) (ha : a âˆˆ s) :
    HasFDerivWithinAt (âˆ«á¶œ x in .segment a Â·, Ï‰ x) (Ï‰ a) s a := by
  /- Given `Îµ > 0`, take a number `Î´ > 0` such that `Ï‰` is continuous on `ball a Î´ âˆ© s`
  and `â€–Ï‰ z - Ï‰ aâ€– â‰¤ Îµ` on this set.
  Then for `b âˆˆ ball a Î´ âˆ© s`, we have
  `â€–(âˆ«á¶œ x in .segment a b, Ï‰ x) - Ï‰ a (b - a)â€–
    = â€–(âˆ«á¶œ x in .segment a b, Ï‰ x) - âˆ«á¶œ x in .segment a b, Ï‰ aâ€–
    â‰¤ âˆ« x in 0..1, â€–Ï‰ x - Ï‰ aâ€– * â€–b - aâ€–
    â‰¤ Îµ * â€–b - aâ€–`
  -/
  simp only [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO, Path.segment_same,
    curveIntegral_refl, sub_zero, Asymptotics.isLittleO_iff]
  intro Îµ hÎµ
  obtain âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© : âˆƒ Î´ > 0,
      ball a Î´ âˆ© s âŠ† {z | ContinuousWithinAt Ï‰ s z âˆ§ dist (Ï‰ z) (Ï‰ a) â‰¤ Îµ} := by
    rw [â† Metric.mem_nhdsWithin_iff, setOf_and, inter_mem_iff]
    exact âŸ¨hÏ‰, (hÏ‰.self_of_nhdsWithin ha).eventually <| closedBall_mem_nhds _ hÎµâŸ©
  rw [eventually_nhdsWithin_iff]
  filter_upwards [Metric.ball_mem_nhds _ hÎ´â‚€] with b hb hbs
  have hsub : [a -[â„] b] âŠ† ball a Î´ âˆ© s :=
    ((convex_ball _ _).inter hs).segment_subset (by simp [*]) (by simp [*])
  rw [â† curveIntegral_segment_const, â† curveIntegral_fun_sub]
  Â· refine norm_curveIntegral_segment_le fun z hz â†¦ (hÎ´ (hsub hz)).2
  Â· rw [curveIntegrable_segment]
    refine ContinuousOn.intervalIntegrable_of_Icc zero_le_one fun t ht â†¦ ?_
    refine ((hÎ´ ?_).1.eval_const _).comp AffineMap.lineMap_continuous.continuousWithinAt ?_
    Â· refine hsub <| segment_eq_image_lineMap â„ a b â–¸ mem_image_of_mem _ ht
    Â· rw [mapsTo_iff_image_subset, â† segment_eq_image_lineMap]
      exact hs.segment_subset ha hbs
  Â· rw [curveIntegrable_segment]
    exact intervalIntegrable_const

/-- The integral of `Ï‰` along `[a -[â„] b]`, as a function of `b`, has derivative `Ï‰ a` at `b = a`.
This is a `HasFDerivWithinAt` version assuming that `Ï‰` is continuous on `s`. -/
theorem HasFDerivWithinAt.curveIntegral_segment_source (hs : Convex â„ s) (hÏ‰ : ContinuousOn Ï‰ s)
    (ha : a âˆˆ s) : HasFDerivWithinAt (âˆ«á¶œ x in .segment a Â·, Ï‰ x) (Ï‰ a) s a :=
  .curveIntegral_segment_source' hs (mem_of_superset self_mem_nhdsWithin hÏ‰) ha

/-- The integral of `Ï‰` along `[a -[â„] b]`, as a function of `b`, has derivative `Ï‰ a` at `b = a`.
This is a `HasFDerivAt` version assuming that `Ï‰` is continuous in a neighborhood of `a`. -/
theorem HasFDerivAt.curveIntegral_segment_source' (hÏ‰ : âˆ€á¶  z in ğ“ a, ContinuousAt Ï‰ z) :
    HasFDerivAt (âˆ«á¶œ x in .segment a Â·, Ï‰ x) (Ï‰ a) a :=
  HasFDerivWithinAt.curveIntegral_segment_source' convex_univ
    (by simpa only [nhdsWithin_univ, continuousWithinAt_univ]) (mem_univ _) |>.hasFDerivAt_of_univ

/-- The integral of `Ï‰` along `[a -[â„] b]`, as a function of `b`, has derivative `Ï‰ a` at `b = a`.
This is a `HasFDerivAt` version assuming that `Ï‰` is continuous on the whole space. -/
theorem HasFDerivAt.curveIntegral_segment_source (hÏ‰ : Continuous Ï‰) :
    HasFDerivAt (âˆ«á¶œ x in .segment a Â·, Ï‰ x) (Ï‰ a) a :=
  .curveIntegral_segment_source' <| .of_forall fun _ â†¦ hÏ‰.continuousAt

end FDeriv
