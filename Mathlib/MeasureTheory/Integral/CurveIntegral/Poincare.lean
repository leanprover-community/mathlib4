/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.Deriv.Prod
public import Mathlib.Analysis.Calculus.DiffContOnCl
public import Mathlib.Analysis.Calculus.FDeriv.Symmetric
public import Mathlib.Analysis.Calculus.TangentCone.Prod
public import Mathlib.MeasureTheory.Integral.CurveIntegral.Basic
public import Mathlib.MeasureTheory.Integral.DivergenceTheorem
public import Mathlib.Topology.Homotopy.Affine

/-!
# PoincarÃ© lemma for 1-forms

In this file we prove PoincarÃ© lemma for 1-forms for convex sets.
Namely, we show that a closed 1-form on a convex subset of a normed space is exact.

We also prove that the integrals of a closed 1-form
along 2 curves that are joined by a `CÂ²`-smooth homotopy are equal.
In the future, this will allow us to prove PoincarÃ© lemma for simply connected open sets
and, more generally, for simply connected locally convex sets.

## Implementation notes

In this file, we represent a 1-form as `Ï‰ : E â†’ E â†’L[ğ•œ] F`, where `ğ•œ` is `â„` or `â„‚`,
not as `Ï‰ : E â†’ E [â‹€^Fin 1]â†’L[ğ•œ] F`.
A 1-form represented this way is closed
iff its FrÃ©chet derivative `dÏ‰ : E â†’ E â†’L[ğ•œ] E â†’L[ğ•œ] F` is symmetric, `dÏ‰ a x y = dÏ‰ a y x`.
-/

@[expose] public section

open scoped unitInterval Interval Pointwise Topology
open AffineMap Filter Function MeasureTheory Set

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace ContinuousMap.Homotopy

variable [NormedSpace â„ E] [NormedSpace â„ F] {a b c d : E}
    {Î³â‚ : Path a b} {Î³â‚‚ : Path c d} {s : Set (I Ã— I)} {t : Set E}

private theorem curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt_off_countable_real
    {Ï‰ : E â†’ E â†’L[â„] F} {dÏ‰ : E â†’ E â†’L[â„] E â†’L[â„] F}
    (Ï† : (Î³â‚ : C(I, E)).Homotopy Î³â‚‚)
    (hs : s.Countable)
    (hÏ†t : âˆ€ a âˆˆ Ioo 0 1, âˆ€ b âˆˆ Ioo 0 1, Ï† (a, b) âˆˆ t)
    (hÏ‰ : âˆ€ a âˆˆ Ioo (0 : I) 1, âˆ€ b âˆˆ Ioo (0 : I) 1, (a, b) âˆ‰ s â†’
      HasFDerivWithinAt Ï‰ (dÏ‰ <| Ï† (a, b)) t (Ï† (a, b))) (hÏ‰c : ContinuousOn Ï‰ (closure t))
    (hdÏ‰_symm : âˆ€ a âˆˆ Ioo (0 : I) 1, âˆ€ b âˆˆ Ioo (0 : I) 1, (a, b) âˆ‰ s â†’
      âˆ€ u âˆˆ tangentConeAt â„ t (Ï† (a, b)), âˆ€ v âˆˆ tangentConeAt â„ t (Ï† (a, b)),
        dÏ‰ (Ï† (a, b)) u v = dÏ‰ (Ï† (a, b)) v u)
    (hcontdiff : ContDiffOn â„ 2
      (fun xy : â„ Ã— â„ â†¦ Set.IccExtend zero_le_one (Ï†.extend xy.1) xy.2) (Icc 0 1)) :
    âˆ«á¶œ x in Î³â‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 1, Ï‰ x = âˆ«á¶œ x in Î³â‚‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 0, Ï‰ x := by
  -- The overall plan of the proof is to pullback the 1-form to the unit square along the homotopy,
  -- prove that it's a closed 1-form, then apply the divergence theorem.
  -- Let `U` be the interior of the unit square
  -- Warning: throughout the proof, we sometimes have `0` or `1` in product spaces,
  -- not only in `I` or `â„`, so, e.g., `Icc 0 1` may refer to the unit square
  -- in `â„ Ã— â„`.
  set U : Set (â„ Ã— â„) := Ioo 0 1 Ã—Ë¢ Ioo 0 1 with hU
  have hinterior : interior (Icc 0 1) = U := by
    rw [hU, â† interior_Icc, â† interior_prod_eq]
    simp [Prod.mk_zero_zero, Prod.mk_one_one]
  have hunique : UniqueDiffOn â„ (Icc 0 1 : Set (â„ Ã— â„)) := by
    rw [Icc_prod_eq]
    exact uniqueDiffOn_Icc_zero_one.prod uniqueDiffOn_Icc_zero_one
  have hUopen : IsOpen U := isOpen_Ioo.prod isOpen_Ioo
  have hU_subset : U âŠ† Icc 0 1 := hinterior â–¸ interior_subset
  have hclosure : closure U = Icc 0 1 := by
    simp [hU, closure_prod_eq, Prod.mk_zero_zero, Prod.mk_one_one]
  -- Extend the homotopy `Ï†` to a continuous map  `Ïˆ : â„ Ã— â„ â†’ E`
  set Ïˆ : â„ Ã— â„ â†’ E := fun xy : â„ Ã— â„ â†¦ Set.IccExtend zero_le_one (Ï†.extend xy.1) xy.2 with hÏˆ
  have hÏˆÏ† : âˆ€ a b : I, Ïˆ (a, b) = Ï† (a, b) := by simp [Ïˆ]
  have hÏˆ_cont : Continuous Ïˆ := by fun_prop
  have hÏˆUt : MapsTo Ïˆ U t := by
    rintro âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©
    lift a to I using Ioo_subset_Icc_self ha
    lift b to I using Ioo_subset_Icc_self hb
    simpa [hÏˆÏ†] using hÏ†t a ha b hb
  -- Let `dÏˆ` be its derivative.
  set dÏˆ : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] E := fderivWithin â„ Ïˆ (Icc 0 1)
  -- Let `s'` be the set `s` interpreted as a set in `â„ Ã— â„`
  set s' : Set (â„ Ã— â„) := Prod.map (â†‘) (â†‘) '' s with hs'
  have hmem_s' (x y : I) : (â†‘x, â†‘y) âˆˆ s' â†” (x, y) âˆˆ s := by
    rw [hs', â† Prod.map_apply, Injective.mem_set_image]
    apply Injective.prodMap <;> apply Subtype.val_injective
  have hs'c : s'.Countable := hs.image _
  have hdÏˆ : âˆ€ a âˆˆ U, HasFDerivAt Ïˆ (dÏˆ a) a := by
    rintro a haU
    refine hcontdiff.differentiableOn (by decide) a (hU_subset haU)
      |>.hasFDerivWithinAt |>.hasFDerivAt ?_
    rwa [â† mem_interior_iff_mem_nhds, hinterior]
  -- Let `d2Ïˆ` be its second derivative
  set d2Ïˆ : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] â„ Ã— â„ â†’L[â„] E := fderivWithin â„ dÏˆ (Icc 0 1)
  have hd2Ïˆ : âˆ€ a âˆˆ U, HasFDerivAt dÏˆ (d2Ïˆ a) a := by
    rintro a haU
    refine hcontdiff.fderivWithin hunique (by decide) |>.differentiableOn_one a (hU_subset haU)
      |>.hasFDerivWithinAt |>.hasFDerivAt ?_
    rwa [â† mem_interior_iff_mem_nhds, hinterior]
  -- Note that `d2Ïˆ` is symmetric
  have hd2Ïˆ_symm : âˆ€ a âˆˆ Icc 0 1, âˆ€ x y, d2Ïˆ a x y = d2Ïˆ a y x := by
    intro a ha
    exact (hcontdiff a ha).isSymmSndFDerivWithinAt (by simp) hunique
      (by simp [hinterior, hclosure, ha]) ha
  -- Consider `Î· a = Ï‰ (Ïˆ a) âˆ˜L dÏˆ a`.
  set Î· : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] F := fun a â†¦ Ï‰ (Ïˆ a) âˆ˜L dÏˆ a
  -- Put `f a = Î· a (0, 1)`, `g a = -Î· a (1, 0)`.
  set f : â„ Ã— â„ â†’ F := fun a â†¦ Î· a (0, 1)
  have hf : âˆ€ a âˆˆ Icc 0 1, f a = Ï‰ (Ïˆ a) (derivWithin (Ïˆ âˆ˜ (a.1, Â·)) I a.2) := by
    intro a ha
    simp only [f, Î·, dÏˆ, ContinuousLinearMap.comp_apply]
    congr 1
    have : HasDerivWithinAt (a.1, Â·) (0, 1) I a.2 :=
      .prodMk (hasDerivWithinAt_const ..) (hasDerivWithinAt_id ..)
    refine DifferentiableWithinAt.hasFDerivWithinAt ?_ |>.comp_hasDerivWithinAt _ this ?_
      |>.derivWithin ?_ |>.symm
    Â· exact hcontdiff.differentiableOn (by decide) _ ha
    Â· exact fun t ht â†¦ âŸ¨âŸ¨ha.1.1, ht.1âŸ©, âŸ¨ha.2.1, ht.2âŸ©âŸ©
    Â· exact uniqueDiffOn_Icc_zero_one _ âŸ¨ha.1.2, ha.2.2âŸ©
  set g : â„ Ã— â„ â†’ F := fun a â†¦ -Î· a (1, 0)
  have hg : âˆ€ a âˆˆ Icc 0 1, g a = Ï‰ (Ïˆ a) (-derivWithin (Ïˆ âˆ˜ (Â·, a.2)) I a.1) := by
    intro a ha
    simp only [g, Î·, dÏˆ, ContinuousLinearMap.comp_apply, map_neg]
    congr 2
    have : HasDerivWithinAt (Â·, a.2) (1, 0) I a.1 :=
      .prodMk (hasDerivWithinAt_id ..) (hasDerivWithinAt_const ..)
    refine DifferentiableWithinAt.hasFDerivWithinAt ?_ |>.comp_hasDerivWithinAt _ this ?_
      |>.derivWithin ?_ |>.symm
    Â· exact hcontdiff.differentiableOn (by decide) _ ha
    Â· exact fun t ht â†¦ âŸ¨âŸ¨ht.1, ha.1.2âŸ©, âŸ¨ht.2, ha.2.2âŸ©âŸ©
    Â· exact uniqueDiffOn_Icc_zero_one _ âŸ¨ha.1.1, ha.2.1âŸ©
  -- Then our goal is to prove that the integral of `Î·`
  -- along the boundary of the unit square is zero.
  suffices (((âˆ« x in 0..1, g (x, 1)) - âˆ« x in 0..1, g (x, 0)) +
      âˆ« y in 0..1, f (1, y)) - âˆ« y in 0..1, f (0, y) = 0 by
    have hfi (s : I) :
        âˆ« t in 0..1, f (s, t) = âˆ«á¶œ x in âŸ¨Ï†.curry s, rfl, rflâŸ©, Ï‰ x := by
      simp only [curveIntegral_def, curveIntegralFun_def]
      apply intervalIntegral.integral_congr
      rw [uIcc_of_le zero_le_one]
      intro t ht
      simp [Path.extend, hf (s, t), Prod.le_def, s.2.1, s.2.2, ht.1, ht.2, Function.comp_def, hÏˆ]
    have hfâ‚€ : âˆ« t in 0..1, f (0, t) = âˆ«á¶œ x in Î³â‚, Ï‰ x := by
      simpa [curveIntegral_def, curveIntegralFun_def, Path.extend] using hfi 0
    have hfâ‚ : âˆ« t in 0..1, f (1, t) = curveIntegral Ï‰ Î³â‚‚ := by
      simpa [curveIntegral_def, curveIntegralFun_def, Path.extend] using hfi 1
    have hgi (t : I) : âˆ«á¶œ x in Ï†.evalAt t, Ï‰ x = -âˆ« s in 0..1, g (s, t) := by
      simp only [curveIntegral_def, curveIntegralFun_def, â† intervalIntegral.integral_neg]
      apply intervalIntegral.integral_congr
      rw [uIcc_of_le zero_le_one]
      intro s hs
      simp only [hs, Path.extend_apply, Ï†.evalAt_apply]
      simp [hg (s, t), Prod.le_def, hs.1, hs.2, t.2.1, t.2.2, Function.comp_def, hÏˆ]
    rw [â† hfâ‚€, â† hfâ‚, hgi, hgi]
    linear_combination (norm := {dsimp; abel}) -this
  -- Write a formula for the derivative of `Î·`.
  set dÎ· : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] â„ Ã— â„ â†’L[â„] F := fun a â†¦
    .compL â„ (â„ Ã— â„) E F (Ï‰ (Ïˆ a)) âˆ˜L d2Ïˆ a + (dÏ‰ (Ïˆ a)).bilinearComp (dÏˆ a) (dÏˆ a)
  have hdÎ· : âˆ€ a âˆˆ U \ s', HasFDerivAt Î· (dÎ· a) a := by
    rintro a âŸ¨haU, hasâŸ©
    refine HasFDerivWithinAt.comp_hasFDerivAt (t := t) a ?_ ?_ ?_ |>.clm_comp (hd2Ïˆ a haU)
    Â· rcases a with âŸ¨x, yâŸ©
      lift x to I using Ioo_subset_Icc_self haU.1
      lift y to I using Ioo_subset_Icc_self haU.2
      apply hÏ‰
      Â· simpa using haU.1
      Â· simpa using haU.2
      Â· simpa [hmem_s'] using has
    Â· exact hdÏˆ a haU
    Â· filter_upwards [hUopen.mem_nhds haU] using hÏˆUt
  have hdÎ·_symm : âˆ€ a âˆˆ U \ s', âˆ€ u v, dÎ· a u v = dÎ· a v u := by
    rintro âŸ¨a, bâŸ© âŸ¨hU, hs'âŸ© u v
    lift a to I using Ioo_subset_Icc_self hU.1
    lift b to I using Ioo_subset_Icc_self hU.2
    have hdÏˆ_mem (u) : dÏˆ (a, b) u âˆˆ tangentConeAt â„ t (Ï† (a, b)) := by
      refine tangentConeAt_mono hÏˆUt.image_subset ?_
      rw [â† hÏˆÏ†]
      refine (hdÏˆ _ hU).hasFDerivWithinAt.mapsTo_tangent_cone ?_
      simp [tangentConeAt_of_mem_nhds (hUopen.mem_nhds hU)]
    have := hdÏ‰_symm a hU.1 b hU.2 (by simpa [hmem_s'] using hs') _ (hdÏˆ_mem u) _ (hdÏˆ_mem v)
    simp [dÎ·, hÏˆÏ†, this, hd2Ïˆ_symm _ (hU_subset hU)]
  -- It gives formulas for the derivatives of `f` and `g`
  set f' : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] F := fun a â†¦ ContinuousLinearMap.apply â„ F (0, 1) âˆ˜L dÎ· a
  have hf' : âˆ€ a âˆˆ U \ s', HasFDerivAt f (f' a) a := by
    intro a ha
    exact (ContinuousLinearMap.apply â„ F (0, 1)).hasFDerivAt.comp a (hdÎ· a ha)
  set g' : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] F := fun a â†¦ -(ContinuousLinearMap.apply â„ F (1, 0) âˆ˜L dÎ· a)
  have hg' : âˆ€ a âˆˆ U \ s', HasFDerivAt g (g' a) a := by
    intro a ha
    exact (ContinuousLinearMap.apply â„ F (1, 0)).hasFDerivAt.comp a (hdÎ· a ha) |>.neg
  -- Note that the divergence of `(f, g)` is a.e. zero.
  have hf'g' : (fun a â†¦ f' a (1, 0) + g' a (0, 1)) =áµ[volume.restrict (Icc 0 1)] 0 := by
    rw [Icc_prod_eq, Measure.volume_eq_prod,
      Measure.restrict_congr_set (Measure.set_prod_ae_eq Ioo_ae_eq_Icc Ioo_ae_eq_Icc).symm]
    filter_upwards [ae_restrict_mem (measurableSet_Ioo.prod measurableSet_Ioo), hs'c.ae_notMem _]
      with a hU hs
    simp [f', g', hdÎ·_symm a âŸ¨hU, hsâŸ© (0, 1)]
  suffices âˆ« a : â„ Ã— â„ in Icc 0 1, f' a (1, 0) + g' a (0, 1) = 0 by
    have hÎ·c : ContinuousOn Î· (Icc 0 1) := by
      refine .clm_comp (hÏ‰c.comp hÏˆ_cont.continuousOn ?_) ?_
      Â· rw [â† hclosure]
        refine MapsTo.closure (fun a ha â†¦ ?_) hÏˆ_cont
        lift a to I Ã— I using âŸ¨Ioo_subset_Icc_self ha.1, Ioo_subset_Icc_self ha.2âŸ©
        simpa [Ïˆ] using hÏ†t a.1 ha.1 a.2 ha.2
      Â· exact hcontdiff.continuousOn_fderivWithin hunique (by decide)
    rwa [integral_divergence_prod_Icc_of_hasFDerivAt_off_countable_of_le] at this
    Â· exact zero_le_one
    Â· exact s'
    Â· exact hs'c
    Â· exact hÎ·c.clm_apply continuousOn_const
    Â· exact hÎ·c.clm_apply continuousOn_const |>.neg
    Â· exact hf'
    Â· exact hg'
    Â· rw [integrableOn_congr_fun_ae hf'g']
      apply integrableOn_zero
  simp [integral_congr_ae hf'g']

/-- The curve integral of a closed 1-form along the boundary of the image of a unit square
under a smooth map is zero. We may ignore the behavior on a countable set.

This theorem is stated in terms of a $$C^2$$ homotopy between two paths. -/
theorem curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt_off_countable
    {Ï‰ : E â†’ E â†’L[ğ•œ] F} {dÏ‰ : E â†’ E â†’L[â„] E â†’L[ğ•œ] F}
    (Ï† : (Î³â‚ : C(I, E)).Homotopy Î³â‚‚)
    (hs : s.Countable)
    (hÏ†t : âˆ€ a âˆˆ Ioo 0 1, âˆ€ b âˆˆ Ioo 0 1, Ï† (a, b) âˆˆ t)
    (hÏ‰ : âˆ€ a âˆˆ Ioo (0 : I) 1, âˆ€ b âˆˆ Ioo (0 : I) 1, (a, b) âˆ‰ s â†’
      HasFDerivWithinAt Ï‰ (dÏ‰ <| Ï† (a, b)) t (Ï† (a, b)))
    (hÏ‰c : ContinuousOn Ï‰ (closure t))
    (hdÏ‰_symm : âˆ€ a âˆˆ Ioo (0 : I) 1, âˆ€ b âˆˆ Ioo (0 : I) 1, (a, b) âˆ‰ s â†’
      âˆ€ u âˆˆ tangentConeAt â„ t (Ï† (a, b)), âˆ€ v âˆˆ tangentConeAt â„ t (Ï† (a, b)),
        dÏ‰ (Ï† (a, b)) u v = dÏ‰ (Ï† (a, b)) v u)
    (hcontdiff : ContDiffOn â„ 2
      (fun xy : â„ Ã— â„ â†¦ Set.IccExtend zero_le_one (Ï†.extend xy.1) xy.2) (Icc 0 1)) :
    âˆ«á¶œ x in Î³â‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 1, Ï‰ x = âˆ«á¶œ x in Î³â‚‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 0, Ï‰ x := by
  simp only [â† curveIntegral_restrictScalars (ğ•œ := ğ•œ) (ğ• := â„)]
  set e := ContinuousLinearMap.restrictScalarsL ğ•œ E F â„ â„
  exact Ï†.curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt_off_countable_real hs hÏ†t
    (dÏ‰ := fun x â†¦ e âˆ˜L dÏ‰ x)
    (fun a ha b hb hs â†¦ e.hasFDerivAt.comp_hasFDerivWithinAt _ (hÏ‰ a ha b hb hs))
    (e.continuous.comp_continuousOn hÏ‰c) hdÏ‰_symm hcontdiff

/-- The curve integral of a closed 1-form along the boundary of the image of a unit square
under a smooth map is zero.

This theorem is stated in terms of a $$C^2$$ homotopy between two paths. -/
theorem curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt
    {Ï‰ : E â†’ E â†’L[ğ•œ] F} {dÏ‰ : E â†’ E â†’L[â„] E â†’L[ğ•œ] F}
    (Ï† : (Î³â‚ : C(I, E)).Homotopy Î³â‚‚)
    (hÏ†t : âˆ€ a âˆˆ Ioo 0 1, âˆ€ b âˆˆ Ioo 0 1, Ï† (a, b) âˆˆ t)
    (hÏ‰ : âˆ€ x âˆˆ t, HasFDerivWithinAt Ï‰ (dÏ‰ x) t x)
    (hÏ‰c : ContinuousOn Ï‰ (closure t))
    (hdÏ‰_symm : âˆ€ x âˆˆ t, âˆ€ u âˆˆ tangentConeAt â„ t x, âˆ€ v âˆˆ tangentConeAt â„ t x, dÏ‰ x u v = dÏ‰ x v u)
    (hcontdiff : ContDiffOn â„ 2
      (fun xy : â„ Ã— â„ â†¦ Set.IccExtend zero_le_one (Ï†.extend xy.1) xy.2) (Icc 0 1)) :
    âˆ«á¶œ x in Î³â‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 1, Ï‰ x = âˆ«á¶œ x in Î³â‚‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 0, Ï‰ x :=
  Ï†.curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt_off_countable (s := âˆ…) (by simp)
    hÏ†t (fun a ha b hb _ â†¦ hÏ‰ _ <| hÏ†t a ha b hb) hÏ‰c
    (fun a ha b hb _ â†¦ hdÏ‰_symm _ <| hÏ†t a ha b hb) hcontdiff

/-- The curve integral of a closed 1-form along the boundary of the image of a unit square
under a smooth map is zero, a version stated in terms of `DiffContOnC1`.

This theorem is stated in terms of a $$C^2$$ homotopy between two paths. -/
theorem curveIntegral_add_curveIntegral_eq_of_diffContOnCl
    {Ï‰ : E â†’ E â†’L[ğ•œ] F} (Ï† : (Î³â‚ : C(I, E)).Homotopy Î³â‚‚)
    (hÏ†t : âˆ€ a âˆˆ Ioo 0 1, âˆ€ b âˆˆ Ioo 0 1, Ï† (a, b) âˆˆ t)
    (hÏ‰ : DiffContOnCl â„ Ï‰ t)
    (hdÏ‰_symm : âˆ€ x âˆˆ t, âˆ€ u âˆˆ tangentConeAt â„ t x, âˆ€ v âˆˆ tangentConeAt â„ t x,
      fderivWithin â„ Ï‰ t x u v = fderivWithin â„ Ï‰ t x v u)
    (hcontdiff : ContDiffOn â„ 2
      (fun xy : â„ Ã— â„ â†¦ Set.IccExtend zero_le_one (Ï†.extend xy.1) xy.2) (Icc 0 1)) :
    âˆ«á¶œ x in Î³â‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 1, Ï‰ x = âˆ«á¶œ x in Î³â‚‚, Ï‰ x + âˆ«á¶œ x in Ï†.evalAt 0, Ï‰ x :=
  Ï†.curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt
    hÏ†t (fun t ht â†¦ (hÏ‰.differentiableOn t ht).hasFDerivWithinAt) hÏ‰.continuousOn
    hdÏ‰_symm hcontdiff

end ContinuousMap.Homotopy

namespace Convex

variable [NormedSpace â„ E] [NormedSpace â„ F]
  {a b c : E} {s : Set E} {Ï‰ : E â†’ E â†’L[ğ•œ] F} {dÏ‰ : E â†’ E â†’L[â„] E â†’L[ğ•œ] F}

/-- If `Ï‰` is a closed `1`-form on a convex set,
then `âˆ«á¶œ x in Path.segment a b, Ï‰ x + âˆ«á¶œ x in Path.segment b c, Ï‰ x = âˆ«á¶œ x in Path.segment a c, Ï‰ x`
for all `a b c âˆˆ s`.

This is the key lemma used to establish that closed a `1`-form on  a convex set
has a primitive.
-/
theorem curveIntegral_segment_add_eq_of_hasFDerivWithinAt_symmetric (hs : Convex â„ s)
    (hÏ‰ : âˆ€ x âˆˆ s, HasFDerivWithinAt Ï‰ (dÏ‰ x) s x)
    (hdÏ‰ : âˆ€ a âˆˆ s, âˆ€ x âˆˆ tangentConeAt â„ s a, âˆ€ y âˆˆ tangentConeAt â„ s a, dÏ‰ a x y = dÏ‰ a y x)
    (ha : a âˆˆ s) (hb : b âˆˆ s) (hc : c âˆˆ s) :
    (âˆ«á¶œ x in .segment a b, Ï‰ x) + âˆ«á¶œ x in .segment b c, Ï‰ x = âˆ«á¶œ x in .segment a c, Ï‰ x := by
  set Ï† := ContinuousMap.Homotopy.affine (Path.segment a b : C(I, E)) (Path.segment a c)
  have hÏ†s : range Ï† âŠ† s := by
    rw [range_subset_iff]
    intro x
    simp [Ï†, ha, hb, hc, hs.lineMap_mem]
  have := Ï†.curveIntegral_add_curveIntegral_eq_of_hasFDerivWithinAt (t := range Ï†) (Ï‰ := Ï‰)
    (dÏ‰ := dÏ‰) ?_ ?_ ?_ ?_ ?_
  Â· convert this using 2
    Â· dsimp [Ï†]
      rw [â† Path.cast_segment (lineMap_apply_one a b) (lineMap_apply_one a c), curveIntegral_cast]
    Â· dsimp [Ï†]
      rw [â† Path.cast_segment (lineMap_apply_zero a b) (lineMap_apply_zero a c)]
      simp
  Â· intros
    apply mem_range_self
  Â· exact fun x hx â†¦ (hÏ‰ x (hÏ†s hx)).mono hÏ†s
  Â· rw [(isCompact_range <| map_continuous _).isClosed.closure_eq]
    exact fun x hx â†¦ (hÏ‰ x <| hÏ†s hx).continuousWithinAt.mono hÏ†s
  Â· intro x hx u hu v hv
    apply hdÏ‰ <;> grw [â† hÏ†s] <;> assumption
  Â· have : EqOn (fun x : â„ Ã— â„ â†¦ IccExtend zero_le_one (Ï†.extend x.1) x.2)
        (fun x â†¦ lineMap (lineMap a b x.2) (lineMap a c x.2) x.1) (Icc 0 1) := by
      rw [Icc_prod_eq]
      rintro âŸ¨x, yâŸ© âŸ¨hx, hyâŸ©
      lift x to I using hx
      lift y to I using hy
      simp [Ï†]
    refine .congr ?_ this
    -- TODO: add `ContDiff.lineMap` etc
    simp only [AffineMap.lineMap_apply_module]
    fun_prop

variable [CompleteSpace F]

/-- If `Ï‰` is a closed `1`-form on a convex set `s`,
then the function given by `F b = âˆ«á¶œ x in Path.segment a b, Ï‰ x` is a primitive of `Ï‰` on `s`,
i.e., `dF = Ï‰`.
-/
theorem hasFDerivWithinAt_curveIntegral_segment_of_hasFDerivWithinAt_symmetric (hs : Convex â„ s)
    (hÏ‰ : âˆ€ x âˆˆ s, HasFDerivWithinAt Ï‰ (dÏ‰ x) s x)
    (hdÏ‰ : âˆ€ a âˆˆ s, âˆ€ x âˆˆ tangentConeAt â„ s a, âˆ€ y âˆˆ tangentConeAt â„ s a, dÏ‰ a x y = dÏ‰ a y x)
    (ha : a âˆˆ s) (hb : b âˆˆ s) :
    HasFDerivWithinAt (âˆ«á¶œ x in .segment a Â·, Ï‰ x) (Ï‰ b) s b := by
  suffices HasFDerivWithinAt (âˆ«á¶œ x in .segment a b, Ï‰ x + âˆ«á¶œ x in .segment b Â·, Ï‰ x) (Ï‰ b) s b from
    this.congr' (fun _ h â†¦
      (hs.curveIntegral_segment_add_eq_of_hasFDerivWithinAt_symmetric hÏ‰ hdÏ‰ ha hb h).symm) hb
  refine .const_add _ <| ?_
  refine HasFDerivWithinAt.curveIntegral_segment_source hs ?_ hb
  exact fun x hx â†¦ (hÏ‰ x hx).continuousWithinAt

/-- If `Ï‰` is a closed `1`-form on a convex set `s`, then it admits a primitive,
a version stated in terms of `HasFDerivWithinAt`. -/
theorem exists_forall_hasFDerivWithinAt_of_hasFDerivWithinAt_symmetric
    (hs : Convex â„ s) (hÏ‰ : âˆ€ x âˆˆ s, HasFDerivWithinAt Ï‰ (dÏ‰ x) s x)
    (hdÏ‰ : âˆ€ a âˆˆ s, âˆ€ x âˆˆ tangentConeAt â„ s a, âˆ€ y âˆˆ tangentConeAt â„ s a, dÏ‰ a x y = dÏ‰ a y x) :
    âˆƒ f, âˆ€ a âˆˆ s, HasFDerivWithinAt f (Ï‰ a) s a := by
  rcases s.eq_empty_or_nonempty with rfl | âŸ¨a, haâŸ©
  Â· simp
  Â· use (curveIntegral Ï‰ <| .segment a Â·)
    intro b hb
    exact hs.hasFDerivWithinAt_curveIntegral_segment_of_hasFDerivWithinAt_symmetric hÏ‰ hdÏ‰ ha hb

/-- If `Ï‰` is a closed `1`-form on a convex set `s`, then it admits a primitive,
a version stated in terms of `fderivWithin`. -/
theorem exists_forall_hasFDerivWithinAt_of_fderivWithin_symmetric
    (hs : Convex â„ s) (hÏ‰ : DifferentiableOn â„ Ï‰ s)
    (hdÏ‰ : âˆ€ a âˆˆ s, âˆ€ x âˆˆ tangentConeAt â„ s a, âˆ€ y âˆˆ tangentConeAt â„ s a,
      fderivWithin â„ Ï‰ s a x y = fderivWithin â„ Ï‰ s a y x) :
    âˆƒ f, âˆ€ a âˆˆ s, HasFDerivWithinAt f (Ï‰ a) s a :=
  hs.exists_forall_hasFDerivWithinAt_of_hasFDerivWithinAt_symmetric
    (fun a ha â†¦ (hÏ‰ a ha).hasFDerivWithinAt) hdÏ‰

/-- If `Ï‰` is a closed `1`-form on an open convex set `s`, then it admits a primitive,
a version stated in terms of `fderiv`. -/
theorem exists_forall_hasFDerivAt_of_fderiv_symmetric (hs : Convex â„ s) (hso : IsOpen s)
    (hÏ‰ : DifferentiableOn â„ Ï‰ s) (hdÏ‰ : âˆ€ a âˆˆ s, âˆ€ x y, fderiv â„ Ï‰ a x y = fderiv â„ Ï‰ a y x) :
    âˆƒ f, âˆ€ a âˆˆ s, HasFDerivAt f (Ï‰ a) a := by
  obtain âŸ¨f, hfâŸ© : âˆƒ f, âˆ€ a âˆˆ s, HasFDerivWithinAt f (Ï‰ a) s a := by
    refine hs.exists_forall_hasFDerivWithinAt_of_fderivWithin_symmetric hÏ‰ fun a ha x _ y _ â†¦ ?_
    rw [fderivWithin_eq_fderiv, hdÏ‰ a ha]
    exacts [hso.uniqueDiffOn a ha, hÏ‰.differentiableAt (hso.mem_nhds ha)]
  exact âŸ¨f, fun a ha â†¦ (hf a ha).hasFDerivAt (hso.mem_nhds ha)âŸ©

end Convex

namespace Convex

variable [CompleteSpace E] {f : ğ•œ â†’ E} {s : Set ğ•œ}

/-- If `f : ğ•œ â†’ E`, `ğ•œ = â„` or `ğ•œ = â„‚`, is differentiable on a convex set `s`,
then it admits a primitive. -/
theorem exists_forall_hasDerivWithinAt (hs : Convex â„ s) (hf : DifferentiableOn ğ•œ f s) :
    âˆƒ g : ğ•œ â†’ E, âˆ€ a âˆˆ s, HasDerivWithinAt g (f a) s a := by
  letI : NormedSpace â„ E := .restrictScalars â„ ğ•œ E
  apply hs.exists_forall_hasFDerivWithinAt_of_hasFDerivWithinAt_symmetric
  Â· intro a ha
    exact (ContinuousLinearMap.smulRightL ğ•œ ğ•œ E 1).hasFDerivAt
      |>.comp_hasDerivWithinAt a (hf a ha).hasDerivWithinAt |>.restrictScalars â„
  Â· rintro a ha x - y -
    simpa using smul_comm ..

end Convex
