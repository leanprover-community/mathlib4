/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Function.L1Space.Integrable
import Mathlib.MeasureTheory.Function.LpSpace.Indicator

/-! # Functions integrable on a set and at a filter

We define `IntegrableOn f s Î¼ := Integrable f (Î¼.restrict s)` and prove theorems like
`integrableOn_union : IntegrableOn f (s âˆª t) Î¼ â†” IntegrableOn f s Î¼ âˆ§ IntegrableOn f t Î¼`.

Next we define a predicate `IntegrableAtFilter (f : Î± â†’ E) (l : Filter Î±) (Î¼ : Measure Î±)`
saying that `f` is integrable at some set `s âˆˆ l` and prove that a measurable function is integrable
at `l` with respect to `Î¼` provided that `f` is bounded above at `l âŠ“ ae Î¼` and `Î¼` is finite
at `l`.

-/


noncomputable section

open Set Filter TopologicalSpace MeasureTheory Function

open scoped Topology Interval Filter ENNReal MeasureTheory

variable {Î± Î² Îµ E F : Type*} [MeasurableSpace Î±] [ENorm Îµ] [TopologicalSpace Îµ]

section

variable [TopologicalSpace Î²] {l l' : Filter Î±} {f g : Î± â†’ Î²} {Î¼ Î½ : Measure Î±}

/-- A function `f` is strongly measurable at a filter `l` w.r.t. a measure `Î¼` if it is
ae strongly measurable w.r.t. `Î¼.restrict s` for some `s âˆˆ l`. -/
def StronglyMeasurableAtFilter (f : Î± â†’ Î²) (l : Filter Î±) (Î¼ : Measure Î± := by volume_tac) :=
  âˆƒ s âˆˆ l, AEStronglyMeasurable f (Î¼.restrict s)

@[simp]
theorem stronglyMeasurableAt_bot {f : Î± â†’ Î²} : StronglyMeasurableAtFilter f âŠ¥ Î¼ :=
  âŸ¨âˆ…, mem_bot, by simpâŸ©

protected theorem StronglyMeasurableAtFilter.eventually (h : StronglyMeasurableAtFilter f l Î¼) :
    âˆ€á¶  s in l.smallSets, AEStronglyMeasurable f (Î¼.restrict s) :=
  (eventually_smallSets' fun _ _ => AEStronglyMeasurable.mono_set).2 h

protected theorem StronglyMeasurableAtFilter.filter_mono (h : StronglyMeasurableAtFilter f l Î¼)
    (h' : l' â‰¤ l) : StronglyMeasurableAtFilter f l' Î¼ :=
  let âŸ¨s, hsl, hsâŸ© := h
  âŸ¨s, h' hsl, hsâŸ©

protected theorem MeasureTheory.AEStronglyMeasurable.stronglyMeasurableAtFilter
    (h : AEStronglyMeasurable f Î¼) : StronglyMeasurableAtFilter f l Î¼ :=
  âŸ¨univ, univ_mem, by rwa [Measure.restrict_univ]âŸ©

theorem AEStronglyMeasurable.stronglyMeasurableAtFilter_of_mem {s}
    (h : AEStronglyMeasurable f (Î¼.restrict s)) (hl : s âˆˆ l) : StronglyMeasurableAtFilter f l Î¼ :=
  âŸ¨s, hl, hâŸ©

@[deprecated (since := "2025-02-12")]
alias AeStronglyMeasurable.stronglyMeasurableAtFilter_of_mem :=
    AEStronglyMeasurable.stronglyMeasurableAtFilter_of_mem

protected theorem MeasureTheory.StronglyMeasurable.stronglyMeasurableAtFilter
    (h : StronglyMeasurable f) : StronglyMeasurableAtFilter f l Î¼ :=
  h.aestronglyMeasurable.stronglyMeasurableAtFilter

end

namespace MeasureTheory

section NormedAddCommGroup

theorem hasFiniteIntegral_restrict_of_bounded [NormedAddCommGroup E] {f : Î± â†’ E} {s : Set Î±}
    {Î¼ : Measure Î±} {C} (hs : Î¼ s < âˆ) (hf : âˆ€áµ x âˆ‚Î¼.restrict s, â€–f xâ€– â‰¤ C) :
    HasFiniteIntegral f (Î¼.restrict s) :=
  haveI : IsFiniteMeasure (Î¼.restrict s) := âŸ¨by rwa [Measure.restrict_apply_univ]âŸ©
  hasFiniteIntegral_of_bounded hf

variable [NormedAddCommGroup E] {f g : Î± â†’ E} {s t : Set Î±} {Î¼ Î½ : Measure Î±}

/-- A function is `IntegrableOn` a set `s` if it is almost everywhere strongly measurable on `s`
and if the integral of its pointwise norm over `s` is less than infinity. -/
def IntegrableOn (f : Î± â†’ Îµ) (s : Set Î±) (Î¼ : Measure Î± := by volume_tac) : Prop :=
  Integrable f (Î¼.restrict s)

theorem IntegrableOn.integrable (h : IntegrableOn f s Î¼) : Integrable f (Î¼.restrict s) :=
  h

@[simp]
theorem integrableOn_empty : IntegrableOn f âˆ… Î¼ := by simp [IntegrableOn, integrable_zero_measure]

@[simp]
theorem integrableOn_univ : IntegrableOn f univ Î¼ â†” Integrable f Î¼ := by
  rw [IntegrableOn, Measure.restrict_univ]

theorem integrableOn_zero : IntegrableOn (fun _ => (0 : E)) s Î¼ :=
  integrable_zero _ _ _

@[simp]
theorem integrableOn_const {C : E} : IntegrableOn (fun _ => C) s Î¼ â†” C = 0 âˆ¨ Î¼ s < âˆ :=
  integrable_const_iff.trans <| by rw [isFiniteMeasure_restrict, lt_top_iff_ne_top]

theorem IntegrableOn.mono (h : IntegrableOn f t Î½) (hs : s âŠ† t) (hÎ¼ : Î¼ â‰¤ Î½) : IntegrableOn f s Î¼ :=
  h.mono_measure <| Measure.restrict_mono hs hÎ¼

theorem IntegrableOn.mono_set (h : IntegrableOn f t Î¼) (hst : s âŠ† t) : IntegrableOn f s Î¼ :=
  h.mono hst le_rfl

theorem IntegrableOn.mono_measure (h : IntegrableOn f s Î½) (hÎ¼ : Î¼ â‰¤ Î½) : IntegrableOn f s Î¼ :=
  h.mono (Subset.refl _) hÎ¼

theorem IntegrableOn.mono_set_ae (h : IntegrableOn f t Î¼) (hst : s â‰¤áµ[Î¼] t) : IntegrableOn f s Î¼ :=
  h.integrable.mono_measure <| Measure.restrict_mono_ae hst

theorem IntegrableOn.congr_set_ae (h : IntegrableOn f t Î¼) (hst : s =áµ[Î¼] t) : IntegrableOn f s Î¼ :=
  h.mono_set_ae hst.le

theorem IntegrableOn.congr_fun_ae (h : IntegrableOn f s Î¼) (hst : f =áµ[Î¼.restrict s] g) :
    IntegrableOn g s Î¼ :=
  Integrable.congr h hst

theorem integrableOn_congr_fun_ae (hst : f =áµ[Î¼.restrict s] g) :
    IntegrableOn f s Î¼ â†” IntegrableOn g s Î¼ :=
  âŸ¨fun h => h.congr_fun_ae hst, fun h => h.congr_fun_ae hst.symmâŸ©

theorem IntegrableOn.congr_fun (h : IntegrableOn f s Î¼) (hst : EqOn f g s) (hs : MeasurableSet s) :
    IntegrableOn g s Î¼ :=
  h.congr_fun_ae ((ae_restrict_iff' hs).2 (Eventually.of_forall hst))

theorem integrableOn_congr_fun (hst : EqOn f g s) (hs : MeasurableSet s) :
    IntegrableOn f s Î¼ â†” IntegrableOn g s Î¼ :=
  âŸ¨fun h => h.congr_fun hst hs, fun h => h.congr_fun hst.symm hsâŸ©

theorem Integrable.integrableOn (h : Integrable f Î¼) : IntegrableOn f s Î¼ := h.restrict

theorem IntegrableOn.restrict (h : IntegrableOn f s Î¼) : IntegrableOn f s (Î¼.restrict t) := by
  dsimp only [IntegrableOn] at h âŠ¢
  exact h.mono_measure <| Measure.restrict_mono_measure Measure.restrict_le_self _

theorem IntegrableOn.inter_of_restrict (h : IntegrableOn f s (Î¼.restrict t)) :
    IntegrableOn f (s âˆ© t) Î¼ := by
  have := h.mono_set (inter_subset_left (t := t))
  rwa [IntegrableOn, Î¼.restrict_restrict_of_subset inter_subset_right] at this

lemma Integrable.piecewise [DecidablePred (Â· âˆˆ s)]
    (hs : MeasurableSet s) (hf : IntegrableOn f s Î¼) (hg : IntegrableOn g sá¶œ Î¼) :
    Integrable (s.piecewise f g) Î¼ := by
  rw [IntegrableOn] at hf hg
  rw [â† memLp_one_iff_integrable] at hf hg âŠ¢
  exact MemLp.piecewise hs hf hg

theorem IntegrableOn.left_of_union (h : IntegrableOn f (s âˆª t) Î¼) : IntegrableOn f s Î¼ :=
  h.mono_set subset_union_left

theorem IntegrableOn.right_of_union (h : IntegrableOn f (s âˆª t) Î¼) : IntegrableOn f t Î¼ :=
  h.mono_set subset_union_right

theorem IntegrableOn.union (hs : IntegrableOn f s Î¼) (ht : IntegrableOn f t Î¼) :
    IntegrableOn f (s âˆª t) Î¼ :=
  (hs.add_measure ht).mono_measure <| Measure.restrict_union_le _ _

@[simp]
theorem integrableOn_union : IntegrableOn f (s âˆª t) Î¼ â†” IntegrableOn f s Î¼ âˆ§ IntegrableOn f t Î¼ :=
  âŸ¨fun h => âŸ¨h.left_of_union, h.right_of_unionâŸ©, fun h => h.1.union h.2âŸ©

@[simp]
theorem integrableOn_singleton_iff {x : Î±} [MeasurableSingletonClass Î±] :
    IntegrableOn f {x} Î¼ â†” f x = 0 âˆ¨ Î¼ {x} < âˆ := by
  have : f =áµ[Î¼.restrict {x}] fun _ => f x := by
    filter_upwards [ae_restrict_mem (measurableSet_singleton x)] with _ ha
    simp only [mem_singleton_iff.1 ha]
  rw [IntegrableOn, integrable_congr this, integrable_const_iff, isFiniteMeasure_restrict,
    lt_top_iff_ne_top]

@[simp]
theorem integrableOn_finite_biUnion {s : Set Î²} (hs : s.Finite) {t : Î² â†’ Set Î±} :
    IntegrableOn f (â‹ƒ i âˆˆ s, t i) Î¼ â†” âˆ€ i âˆˆ s, IntegrableOn f (t i) Î¼ := by
  induction s, hs using Set.Finite.induction_on with
  | empty => simp
  | insert _ _ hf => simp [hf, or_imp, forall_and]

@[simp]
theorem integrableOn_finset_iUnion {s : Finset Î²} {t : Î² â†’ Set Î±} :
    IntegrableOn f (â‹ƒ i âˆˆ s, t i) Î¼ â†” âˆ€ i âˆˆ s, IntegrableOn f (t i) Î¼ :=
  integrableOn_finite_biUnion s.finite_toSet

@[simp]
theorem integrableOn_finite_iUnion [Finite Î²] {t : Î² â†’ Set Î±} :
    IntegrableOn f (â‹ƒ i, t i) Î¼ â†” âˆ€ i, IntegrableOn f (t i) Î¼ := by
  cases nonempty_fintype Î²
  simpa using @integrableOn_finset_iUnion _ _ _ _ _ f Î¼ Finset.univ t

lemma IntegrableOn.finset [MeasurableSingletonClass Î±] {Î¼ : Measure Î±} [IsFiniteMeasure Î¼]
    {s : Finset Î±} {f : Î± â†’ E} : IntegrableOn f s Î¼ := by
  rw [â† s.toSet.biUnion_of_singleton]
  simp [integrableOn_finset_iUnion, measure_lt_top]

lemma IntegrableOn.of_finite [MeasurableSingletonClass Î±] {Î¼ : Measure Î±} [IsFiniteMeasure Î¼]
    {s : Set Î±} (hs : s.Finite) {f : Î± â†’ E} : IntegrableOn f s Î¼ := by
  simpa using IntegrableOn.finset (s := hs.toFinset)

theorem IntegrableOn.add_measure (hÎ¼ : IntegrableOn f s Î¼) (hÎ½ : IntegrableOn f s Î½) :
    IntegrableOn f s (Î¼ + Î½) := by
  delta IntegrableOn; rw [Measure.restrict_add]; exact hÎ¼.integrable.add_measure hÎ½

@[simp]
theorem integrableOn_add_measure :
    IntegrableOn f s (Î¼ + Î½) â†” IntegrableOn f s Î¼ âˆ§ IntegrableOn f s Î½ :=
  âŸ¨fun h =>
    âŸ¨h.mono_measure (Measure.le_add_right le_rfl), h.mono_measure (Measure.le_add_left le_rfl)âŸ©,
    fun h => h.1.add_measure h.2âŸ©

theorem _root_.MeasurableEmbedding.integrableOn_map_iff [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î±} {s : Set Î²} :
    IntegrableOn f s (Î¼.map e) â†” IntegrableOn (f âˆ˜ e) (e â»Â¹' s) Î¼ := by
  simp_rw [IntegrableOn, he.restrict_map, he.integrable_map_iff]

theorem _root_.MeasurableEmbedding.integrableOn_iff_comap [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î²} {s : Set Î²} (hs : s âŠ† range e) :
    IntegrableOn f s Î¼ â†” IntegrableOn (f âˆ˜ e) (e â»Â¹' s) (Î¼.comap e) := by
  simp_rw [â† he.integrableOn_map_iff, he.map_comap, IntegrableOn,
    Measure.restrict_restrict_of_subset hs]

theorem _root_.MeasurableEmbedding.integrableOn_range_iff_comap [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î²} :
    IntegrableOn f (range e) Î¼ â†” Integrable (f âˆ˜ e) (Î¼.comap e) := by
  rw [he.integrableOn_iff_comap .rfl, preimage_range, integrableOn_univ]

theorem integrableOn_iff_comap_subtypeVal (hs : MeasurableSet s) :
    IntegrableOn f s Î¼ â†” Integrable (f âˆ˜ (â†‘) : s â†’ E) (Î¼.comap (â†‘)) := by
  rw [â† (MeasurableEmbedding.subtype_coe hs).integrableOn_range_iff_comap, Subtype.range_val]

theorem integrableOn_map_equiv [MeasurableSpace Î²] (e : Î± â‰ƒáµ Î²) {f : Î² â†’ E} {Î¼ : Measure Î±}
    {s : Set Î²} : IntegrableOn f s (Î¼.map e) â†” IntegrableOn (f âˆ˜ e) (e â»Â¹' s) Î¼ := by
  simp only [IntegrableOn, e.restrict_map, integrable_map_equiv e]

theorem MeasurePreserving.integrableOn_comp_preimage [MeasurableSpace Î²] {e : Î± â†’ Î²} {Î½}
    (hâ‚ : MeasurePreserving e Î¼ Î½) (hâ‚‚ : MeasurableEmbedding e) {f : Î² â†’ E} {s : Set Î²} :
    IntegrableOn (f âˆ˜ e) (e â»Â¹' s) Î¼ â†” IntegrableOn f s Î½ :=
  (hâ‚.restrict_preimage_emb hâ‚‚ s).integrable_comp_emb hâ‚‚

theorem MeasurePreserving.integrableOn_image [MeasurableSpace Î²] {e : Î± â†’ Î²} {Î½}
    (hâ‚ : MeasurePreserving e Î¼ Î½) (hâ‚‚ : MeasurableEmbedding e) {f : Î² â†’ E} {s : Set Î±} :
    IntegrableOn f (e '' s) Î½ â†” IntegrableOn (f âˆ˜ e) s Î¼ :=
  ((hâ‚.restrict_image_emb hâ‚‚ s).integrable_comp_emb hâ‚‚).symm

theorem integrable_indicator_iff (hs : MeasurableSet s) :
    Integrable (indicator s f) Î¼ â†” IntegrableOn f s Î¼ := by
  simp_rw [IntegrableOn, Integrable, hasFiniteIntegral_iff_enorm,
    enorm_indicator_eq_indicator_enorm, lintegral_indicator hs,
    aestronglyMeasurable_indicator_iff hs]

theorem IntegrableOn.integrable_indicator (h : IntegrableOn f s Î¼) (hs : MeasurableSet s) :
    Integrable (indicator s f) Î¼ :=
  (integrable_indicator_iff hs).2 h

@[fun_prop]
theorem Integrable.indicator (h : Integrable f Î¼) (hs : MeasurableSet s) :
    Integrable (indicator s f) Î¼ :=
  h.integrableOn.integrable_indicator hs

theorem IntegrableOn.indicator (h : IntegrableOn f s Î¼) (ht : MeasurableSet t) :
    IntegrableOn (indicator t f) s Î¼ :=
  Integrable.indicator h ht

theorem integrable_indicatorConstLp {E} [NormedAddCommGroup E] {p : â„â‰¥0âˆ} {s : Set Î±}
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) :
    Integrable (indicatorConstLp p hs hÎ¼s c) Î¼ := by
  rw [integrable_congr indicatorConstLp_coeFn, integrable_indicator_iff hs, IntegrableOn,
    integrable_const_iff, isFiniteMeasure_restrict]
  exact .inr hÎ¼s

/-- If a function is integrable on a set `s` and nonzero there, then the measurable hull of `s` is
well behaved: the restriction of the measure to `toMeasurable Î¼ s` coincides with its restriction
to `s`. -/
theorem IntegrableOn.restrict_toMeasurable (hf : IntegrableOn f s Î¼) (h's : âˆ€ x âˆˆ s, f x â‰  0) :
    Î¼.restrict (toMeasurable Î¼ s) = Î¼.restrict s := by
  rcases exists_seq_strictAnti_tendsto (0 : â„) with âŸ¨u, _, u_pos, u_limâŸ©
  let v n := toMeasurable (Î¼.restrict s) { x | u n â‰¤ â€–f xâ€– }
  have A : âˆ€ n, Î¼ (s âˆ© v n) â‰  âˆ := by
    intro n
    rw [inter_comm, â† Measure.restrict_apply (measurableSet_toMeasurable _ _),
      measure_toMeasurable]
    exact (hf.measure_norm_ge_lt_top (u_pos n)).ne
  apply Measure.restrict_toMeasurable_of_cover _ A
  intro x hx
  have : 0 < â€–f xâ€– := by simp only [h's x hx, norm_pos_iff, Ne, not_false_iff]
  obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < â€–f xâ€– := ((tendsto_order.1 u_lim).2 _ this).exists
  exact mem_iUnion.2 âŸ¨n, subset_toMeasurable _ _ hn.leâŸ©

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is null-measurable. -/
theorem IntegrableOn.of_ae_diff_eq_zero (hf : IntegrableOn f s Î¼) (ht : NullMeasurableSet t Î¼)
    (h't : âˆ€áµ x âˆ‚Î¼, x âˆˆ t \ s â†’ f x = 0) : IntegrableOn f t Î¼ := by
  let u := { x âˆˆ s | f x â‰  0 }
  have hu : IntegrableOn f u Î¼ := hf.mono_set fun x hx => hx.1
  let v := toMeasurable Î¼ u
  have A : IntegrableOn f v Î¼ := by
    rw [IntegrableOn, hu.restrict_toMeasurable]
    Â· exact hu
    Â· intro x hx; exact hx.2
  have B : IntegrableOn f (t \ v) Î¼ := by
    apply integrableOn_zero.congr
    filter_upwards [ae_restrict_of_ae h't,
      ae_restrict_memâ‚€ (ht.diff (measurableSet_toMeasurable Î¼ u).nullMeasurableSet)] with x hxt hx
    by_cases h'x : x âˆˆ s
    Â· by_contra H
      exact hx.2 (subset_toMeasurable Î¼ u âŸ¨h'x, Ne.symm HâŸ©)
    Â· exact (hxt âŸ¨hx.1, h'xâŸ©).symm
  apply (A.union B).mono_set _
  rw [union_diff_self]
  exact subset_union_right

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is measurable. -/
theorem IntegrableOn.of_forall_diff_eq_zero (hf : IntegrableOn f s Î¼) (ht : MeasurableSet t)
    (h't : âˆ€ x âˆˆ t \ s, f x = 0) : IntegrableOn f t Î¼ :=
  hf.of_ae_diff_eq_zero ht.nullMeasurableSet (Eventually.of_forall h't)

/-- If a function is integrable on a set `s` and vanishes almost everywhere on its complement,
then it is integrable. -/
theorem IntegrableOn.integrable_of_ae_not_mem_eq_zero (hf : IntegrableOn f s Î¼)
    (h't : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ f x = 0) : Integrable f Î¼ := by
  rw [â† integrableOn_univ]
  apply hf.of_ae_diff_eq_zero nullMeasurableSet_univ
  filter_upwards [h't] with x hx h'x using hx h'x.2

/-- If a function is integrable on a set `s` and vanishes everywhere on its complement,
then it is integrable. -/
theorem IntegrableOn.integrable_of_forall_not_mem_eq_zero (hf : IntegrableOn f s Î¼)
    (h't : âˆ€ x, x âˆ‰ s â†’ f x = 0) : Integrable f Î¼ :=
  hf.integrable_of_ae_not_mem_eq_zero (Eventually.of_forall fun x hx => h't x hx)

theorem integrableOn_iff_integrable_of_support_subset (h1s : support f âŠ† s) :
    IntegrableOn f s Î¼ â†” Integrable f Î¼ := by
  refine âŸ¨fun h => ?_, fun h => h.integrableOnâŸ©
  refine h.integrable_of_forall_not_mem_eq_zero fun x hx => ?_
  contrapose! hx
  exact h1s (mem_support.2 hx)

theorem integrableOn_Lp_of_measure_ne_top {E} [NormedAddCommGroup E] {p : â„â‰¥0âˆ} {s : Set Î±}
    (f : Lp E p Î¼) (hp : 1 â‰¤ p) (hÎ¼s : Î¼ s â‰  âˆ) : IntegrableOn f s Î¼ := by
  refine memLp_one_iff_integrable.mp ?_
  have hÎ¼_restrict_univ : (Î¼.restrict s) Set.univ < âˆ := by
    simpa only [Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply, lt_top_iff_ne_top]
  haveI hÎ¼_finite : IsFiniteMeasure (Î¼.restrict s) := âŸ¨hÎ¼_restrict_univâŸ©
  exact ((Lp.memLp _).restrict s).mono_exponent hp

theorem Integrable.lintegral_lt_top {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    (âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼) < âˆ :=
  calc
    (âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼) â‰¤ âˆ«â» x, â†‘â€–f xâ€–â‚Š âˆ‚Î¼ := lintegral_ofReal_le_lintegral_enorm f
    _ < âˆ := hf.2

theorem IntegrableOn.setLIntegral_lt_top {f : Î± â†’ â„} {s : Set Î±} (hf : IntegrableOn f s Î¼) :
    (âˆ«â» x in s, ENNReal.ofReal (f x) âˆ‚Î¼) < âˆ :=
  Integrable.lintegral_lt_top hf

/-- We say that a function `f` is *integrable at filter* `l` if it is integrable on some
set `s âˆˆ l`. Equivalently, it is eventually integrable on `s` in `l.smallSets`. -/
def IntegrableAtFilter (f : Î± â†’ Îµ) (l : Filter Î±) (Î¼ : Measure Î± := by volume_tac) :=
  âˆƒ s âˆˆ l, IntegrableOn f s Î¼

variable {l l' : Filter Î±}

theorem _root_.MeasurableEmbedding.integrableAtFilter_map_iff [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} :
    IntegrableAtFilter f (l.map e) (Î¼.map e) â†” IntegrableAtFilter (f âˆ˜ e) l Î¼ := by
  simp_rw [IntegrableAtFilter, he.integrableOn_map_iff]
  constructor <;> rintro âŸ¨s, hsâŸ©
  Â· exact âŸ¨_, hsâŸ©
  Â· exact âŸ¨e '' s, by rwa [mem_map, he.injective.preimage_image]âŸ©

theorem _root_.MeasurableEmbedding.integrableAtFilter_iff_comap [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î²} :
    IntegrableAtFilter f (l.map e) Î¼ â†” IntegrableAtFilter (f âˆ˜ e) l (Î¼.comap e) := by
  simp_rw [â† he.integrableAtFilter_map_iff, IntegrableAtFilter, he.map_comap]
  constructor <;> rintro âŸ¨s, hs, intâŸ©
  Â· exact âŸ¨s, hs, int.mono_measure <| Î¼.restrict_le_selfâŸ©
  Â· exact âŸ¨_, inter_mem hs range_mem_map, int.inter_of_restrictâŸ©

theorem Integrable.integrableAtFilter (h : Integrable f Î¼) (l : Filter Î±) :
    IntegrableAtFilter f l Î¼ :=
  âŸ¨univ, Filter.univ_mem, integrableOn_univ.2 hâŸ©

protected theorem IntegrableAtFilter.eventually (h : IntegrableAtFilter f l Î¼) :
    âˆ€á¶  s in l.smallSets, IntegrableOn f s Î¼ :=
  Iff.mpr (eventually_smallSets' fun _s _t hst ht => ht.mono_set hst) h

theorem integrableAtFilter_atBot_iff [Preorder Î±] [IsDirected Î± fun (x1 x2 : Î±) => x1 â‰¥ x2]
    [Nonempty Î±] :
    IntegrableAtFilter f atBot Î¼ â†” âˆƒ a, IntegrableOn f (Iic a) Î¼ := by
  refine âŸ¨fun âŸ¨s, hs, hiâŸ© â†¦ ?_, fun âŸ¨a, haâŸ© â†¦ âŸ¨Iic a, Iic_mem_atBot a, haâŸ©âŸ©
  obtain âŸ¨t, htâŸ© := mem_atBot_sets.mp hs
  exact âŸ¨t, hi.mono_set fun _ hx â†¦ ht _ hxâŸ©

theorem integrableAtFilter_atTop_iff [Preorder Î±] [IsDirected Î± fun (x1 x2 : Î±) => x1 â‰¤ x2]
    [Nonempty Î±] :
    IntegrableAtFilter f atTop Î¼ â†” âˆƒ a, IntegrableOn f (Ici a) Î¼ :=
  integrableAtFilter_atBot_iff (Î± := Î±áµ’áµˆ)

protected theorem IntegrableAtFilter.add {f g : Î± â†’ E}
    (hf : IntegrableAtFilter f l Î¼) (hg : IntegrableAtFilter g l Î¼) :
    IntegrableAtFilter (f + g) l Î¼ := by
  rcases hf with âŸ¨s, sl, hsâŸ©
  rcases hg with âŸ¨t, tl, htâŸ©
  refine âŸ¨s âˆ© t, inter_mem sl tl, ?_âŸ©
  exact (hs.mono_set inter_subset_left).add (ht.mono_set inter_subset_right)

protected theorem IntegrableAtFilter.neg {f : Î± â†’ E} (hf : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter (-f) l Î¼ := by
  rcases hf with âŸ¨s, sl, hsâŸ©
  exact âŸ¨s, sl, hs.negâŸ©

protected theorem IntegrableAtFilter.sub {f g : Î± â†’ E}
    (hf : IntegrableAtFilter f l Î¼) (hg : IntegrableAtFilter g l Î¼) :
    IntegrableAtFilter (f - g) l Î¼ := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg

protected theorem IntegrableAtFilter.smul {ğ•œ : Type*} [NormedAddCommGroup ğ•œ] [SMulZeroClass ğ•œ E]
    [IsBoundedSMul ğ•œ E] {f : Î± â†’ E} (hf : IntegrableAtFilter f l Î¼) (c : ğ•œ) :
    IntegrableAtFilter (c â€¢ f) l Î¼ := by
  rcases hf with âŸ¨s, sl, hsâŸ©
  exact âŸ¨s, sl, hs.smul câŸ©

protected theorem IntegrableAtFilter.norm (hf : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter (fun x => â€–f xâ€–) l Î¼ :=
  Exists.casesOn hf fun s hs â†¦ âŸ¨s, hs.1, hs.2.normâŸ©

theorem IntegrableAtFilter.filter_mono (hl : l â‰¤ l') (hl' : IntegrableAtFilter f l' Î¼) :
    IntegrableAtFilter f l Î¼ :=
  let âŸ¨s, hs, hsfâŸ© := hl'
  âŸ¨s, hl hs, hsfâŸ©

theorem IntegrableAtFilter.inf_of_left (hl : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter f (l âŠ“ l') Î¼ :=
  hl.filter_mono inf_le_left

theorem IntegrableAtFilter.inf_of_right (hl : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter f (l' âŠ“ l) Î¼ :=
  hl.filter_mono inf_le_right

@[simp]
theorem IntegrableAtFilter.inf_ae_iff {l : Filter Î±} :
    IntegrableAtFilter f (l âŠ“ ae Î¼) Î¼ â†” IntegrableAtFilter f l Î¼ := by
  refine âŸ¨?_, fun h â†¦ h.filter_mono inf_le_leftâŸ©
  rintro âŸ¨s, âŸ¨t, ht, u, hu, rflâŸ©, hfâŸ©
  refine âŸ¨t, ht, hf.congr_set_ae <| eventuallyEq_set.2 ?_âŸ©
  filter_upwards [hu] with x hx using (and_iff_left hx).symm

alias âŸ¨IntegrableAtFilter.of_inf_ae, _âŸ© := IntegrableAtFilter.inf_ae_iff

@[simp]
theorem integrableAtFilter_top : IntegrableAtFilter f âŠ¤ Î¼ â†” Integrable f Î¼ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h.integrableAtFilter âŠ¤âŸ©
  obtain âŸ¨s, hsf, hsâŸ© := h
  exact (integrableOn_iff_integrable_of_support_subset fun _ _ â†¦ hsf _).mp hs

theorem IntegrableAtFilter.sup_iff {l l' : Filter Î±} :
    IntegrableAtFilter f (l âŠ” l') Î¼ â†” IntegrableAtFilter f l Î¼ âˆ§ IntegrableAtFilter f l' Î¼ := by
  constructor
  Â· exact fun h => âŸ¨h.filter_mono le_sup_left, h.filter_mono le_sup_rightâŸ©
  Â· exact fun âŸ¨âŸ¨s, hsl, hsâŸ©, âŸ¨t, htl, htâŸ©âŸ© â†¦ âŸ¨s âˆª t, union_mem_sup hsl htl, hs.union htâŸ©

/-- If `Î¼` is a measure finite at filter `l` and `f` is a function such that its norm is bounded
above at `l`, then `f` is integrable at `l`. -/
theorem Measure.FiniteAtFilter.integrableAtFilter {l : Filter Î±} [IsMeasurablyGenerated l]
    (hfm : StronglyMeasurableAtFilter f l Î¼) (hÎ¼ : Î¼.FiniteAtFilter l)
    (hf : l.IsBoundedUnder (Â· â‰¤ Â·) (norm âˆ˜ f)) : IntegrableAtFilter f l Î¼ := by
  obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€á¶  s in l.smallSets, âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ C :=
    hf.imp fun C hC => eventually_smallSets.2 âŸ¨_, hC, fun t => idâŸ©
  rcases (hfm.eventually.and (hÎ¼.eventually.and hC)).exists_measurable_mem_of_smallSets with
    âŸ¨s, hsl, hsm, hfm, hÎ¼, hCâŸ©
  refine âŸ¨s, hsl, âŸ¨hfm, hasFiniteIntegral_restrict_of_bounded hÎ¼ (C := C) ?_âŸ©âŸ©
  rw [ae_restrict_eq hsm, eventually_inf_principal]
  exact Eventually.of_forall hC

theorem Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae {l : Filter Î±}
    [IsMeasurablyGenerated l] (hfm : StronglyMeasurableAtFilter f l Î¼) (hÎ¼ : Î¼.FiniteAtFilter l) {b}
    (hf : Tendsto f (l âŠ“ ae Î¼) (ğ“ b)) : IntegrableAtFilter f l Î¼ :=
  (hÎ¼.inf_of_left.integrableAtFilter (hfm.filter_mono inf_le_left)
      hf.norm.isBoundedUnder_le).of_inf_ae

alias _root_.Filter.Tendsto.integrableAtFilter_ae :=
  Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae

theorem Measure.FiniteAtFilter.integrableAtFilter_of_tendsto {l : Filter Î±}
    [IsMeasurablyGenerated l] (hfm : StronglyMeasurableAtFilter f l Î¼) (hÎ¼ : Î¼.FiniteAtFilter l) {b}
    (hf : Tendsto f l (ğ“ b)) : IntegrableAtFilter f l Î¼ :=
  hÎ¼.integrableAtFilter hfm hf.norm.isBoundedUnder_le

alias _root_.Filter.Tendsto.integrableAtFilter :=
  Measure.FiniteAtFilter.integrableAtFilter_of_tendsto

lemma Measure.integrableOn_of_bounded (s_finite : Î¼ s â‰  âˆ) (f_mble : AEStronglyMeasurable f Î¼)
    {M : â„} (f_bdd : âˆ€áµ a âˆ‚(Î¼.restrict s), â€–f aâ€– â‰¤ M) :
    IntegrableOn f s Î¼ :=
  âŸ¨f_mble.restrict, hasFiniteIntegral_restrict_of_bounded (C := M) s_finite.lt_top f_bddâŸ©

theorem integrable_add_of_disjoint {f g : Î± â†’ E} (h : Disjoint (support f) (support g))
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    Integrable (f + g) Î¼ â†” Integrable f Î¼ âˆ§ Integrable g Î¼ := by
  refine âŸ¨fun hfg => âŸ¨?_, ?_âŸ©, fun h => h.1.add h.2âŸ©
  Â· rw [â† indicator_add_eq_left h]; exact hfg.indicator hf.measurableSet_support
  Â· rw [â† indicator_add_eq_right h]; exact hfg.indicator hg.measurableSet_support

/-- If a function converges along a filter to a limit `a`, is integrable along this filter, and
all elements of the filter have infinite measure, then the limit has to vanish. -/
lemma IntegrableAtFilter.eq_zero_of_tendsto
    (h : IntegrableAtFilter f l Î¼) (h' : âˆ€ s âˆˆ l, Î¼ s = âˆ) {a : E}
    (hf : Tendsto f l (ğ“ a)) : a = 0 := by
  by_contra H
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ (Îµ : â„), 0 < Îµ âˆ§ Îµ < â€–aâ€– := exists_between (norm_pos_iff.mpr H)
  rcases h with âŸ¨u, ul, huâŸ©
  let v := u âˆ© {b | Îµ < â€–f bâ€–}
  have hv : IntegrableOn f v Î¼ := hu.mono_set inter_subset_left
  have vl : v âˆˆ l := inter_mem ul ((tendsto_order.1 hf.norm).1 _ hÎµ)
  have : Î¼.restrict v v < âˆ := lt_of_le_of_lt (measure_mono inter_subset_right)
    (Integrable.measure_gt_lt_top hv.norm Îµpos)
  have : Î¼ v â‰  âˆ := ne_of_lt (by simpa only [Measure.restrict_apply_self])
  exact this (h' v vl)

end NormedAddCommGroup

end MeasureTheory

open MeasureTheory

variable [NormedAddCommGroup E]

/-- A function which is continuous on a set `s` is almost everywhere measurable with respect to
`Î¼.restrict s`. -/
theorem ContinuousOn.aemeasurable [TopologicalSpace Î±] [OpensMeasurableSpace Î±] [MeasurableSpace Î²]
    [TopologicalSpace Î²] [BorelSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±}
    (hf : ContinuousOn f s) (hs : MeasurableSet s) : AEMeasurable f (Î¼.restrict s) := by
  classical
  nontriviality Î±; inhabit Î±
  have : (Set.piecewise s f fun _ => f default) =áµ[Î¼.restrict s] f := piecewise_ae_eq_restrict hs
  refine âŸ¨Set.piecewise s f fun _ => f default, ?_, this.symmâŸ©
  apply measurable_of_isOpen
  intro t ht
  obtain âŸ¨u, u_open, huâŸ© : âˆƒ u : Set Î±, IsOpen u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
    _root_.continuousOn_iff'.1 hf t ht
  rw [piecewise_preimage, Set.ite, hu]
  exact (u_open.measurableSet.inter hs).union ((measurable_const ht.measurableSet).diff hs)

/-- A function which is continuous on a separable set `s` is almost everywhere strongly measurable
with respect to `Î¼.restrict s`. -/
theorem ContinuousOn.aestronglyMeasurable_of_isSeparable [TopologicalSpace Î±]
    [PseudoMetrizableSpace Î±] [OpensMeasurableSpace Î±] [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±} (hf : ContinuousOn f s)
    (hs : MeasurableSet s) (h's : TopologicalSpace.IsSeparable s) :
    AEStronglyMeasurable f (Î¼.restrict s) := by
  letI := pseudoMetrizableSpacePseudoMetric Î±
  borelize Î²
  rw [aestronglyMeasurable_iff_aemeasurable_separable]
  refine âŸ¨hf.aemeasurable hs, f '' s, hf.isSeparable_image h's, ?_âŸ©
  exact mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)

/-- A function which is continuous on a set `s` is almost everywhere strongly measurable with
respect to `Î¼.restrict s` when either the source space or the target space is second-countable. -/
theorem ContinuousOn.aestronglyMeasurable [TopologicalSpace Î±] [TopologicalSpace Î²]
    [h : SecondCountableTopologyEither Î± Î²] [OpensMeasurableSpace Î±] [PseudoMetrizableSpace Î²]
    {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±} (hf : ContinuousOn f s) (hs : MeasurableSet s) :
    AEStronglyMeasurable f (Î¼.restrict s) := by
  borelize Î²
  refine
    aestronglyMeasurable_iff_aemeasurable_separable.2
      âŸ¨hf.aemeasurable hs, f '' s, ?_,
        mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)âŸ©
  cases h.out
  Â· rw [image_eq_range]
    exact isSeparable_range <| continuousOn_iff_continuous_restrict.1 hf
  Â· exact .of_separableSpace _

/-- A function which is continuous on a compact set `s` is almost everywhere strongly measurable
with respect to `Î¼.restrict s`. -/
theorem ContinuousOn.aestronglyMeasurable_of_isCompact [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±}
    (hf : ContinuousOn f s) (hs : IsCompact s) (h's : MeasurableSet s) :
    AEStronglyMeasurable f (Î¼.restrict s) := by
  letI := pseudoMetrizableSpacePseudoMetric Î²
  borelize Î²
  rw [aestronglyMeasurable_iff_aemeasurable_separable]
  refine âŸ¨hf.aemeasurable h's, f '' s, ?_, ?_âŸ©
  Â· exact (hs.image_of_continuousOn hf).isSeparable
  Â· exact mem_of_superset (self_mem_ae_restrict h's) (subset_preimage_image _ _)

theorem ContinuousOn.integrableAt_nhdsWithin_of_isSeparable [TopologicalSpace Î±]
    [PseudoMetrizableSpace Î±] [OpensMeasurableSpace Î±] {Î¼ : Measure Î±} [IsLocallyFiniteMeasure Î¼]
    {a : Î±} {t : Set Î±} {f : Î± â†’ E} (hft : ContinuousOn f t) (ht : MeasurableSet t)
    (h't : TopologicalSpace.IsSeparable t) (ha : a âˆˆ t) : IntegrableAtFilter f (ğ“[t] a) Î¼ :=
  haveI : (ğ“[t] a).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hft a ha).integrableAtFilter
    âŸ¨_, self_mem_nhdsWithin, hft.aestronglyMeasurable_of_isSeparable ht h'tâŸ©
    (Î¼.finiteAt_nhdsWithin _ _)

theorem ContinuousOn.integrableAt_nhdsWithin [TopologicalSpace Î±]
    [SecondCountableTopologyEither Î± E] [OpensMeasurableSpace Î±] {Î¼ : Measure Î±}
    [IsLocallyFiniteMeasure Î¼] {a : Î±} {t : Set Î±} {f : Î± â†’ E} (hft : ContinuousOn f t)
    (ht : MeasurableSet t) (ha : a âˆˆ t) : IntegrableAtFilter f (ğ“[t] a) Î¼ :=
  haveI : (ğ“[t] a).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hft a ha).integrableAtFilter âŸ¨_, self_mem_nhdsWithin, hft.aestronglyMeasurable htâŸ©
    (Î¼.finiteAt_nhdsWithin _ _)

theorem Continuous.integrableAt_nhds [TopologicalSpace Î±] [SecondCountableTopologyEither Î± E]
    [OpensMeasurableSpace Î±] {Î¼ : Measure Î±} [IsLocallyFiniteMeasure Î¼] {f : Î± â†’ E}
    (hf : Continuous f) (a : Î±) : IntegrableAtFilter f (ğ“ a) Î¼ := by
  rw [â† nhdsWithin_univ]
  exact hf.continuousOn.integrableAt_nhdsWithin MeasurableSet.univ (mem_univ a)

/-- If a function is continuous on an open set `s`, then it is strongly measurable at the filter
`ğ“ x` for all `x âˆˆ s` if either the source space or the target space is second-countable. -/
theorem ContinuousOn.stronglyMeasurableAtFilter [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²}
    {s : Set Î±} {Î¼ : Measure Î±} (hs : IsOpen s) (hf : ContinuousOn f s) :
    âˆ€ x âˆˆ s, StronglyMeasurableAtFilter f (ğ“ x) Î¼ := fun _x hx =>
  âŸ¨s, IsOpen.mem_nhds hs hx, hf.aestronglyMeasurable hs.measurableSetâŸ©

theorem ContinuousAt.stronglyMeasurableAtFilter [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [SecondCountableTopologyEither Î± E] {f : Î± â†’ E} {s : Set Î±} {Î¼ : Measure Î±} (hs : IsOpen s)
    (hf : âˆ€ x âˆˆ s, ContinuousAt f x) : âˆ€ x âˆˆ s, StronglyMeasurableAtFilter f (ğ“ x) Î¼ :=
  ContinuousOn.stronglyMeasurableAtFilter hs <| continuousOn_of_forall_continuousAt hf

theorem Continuous.stronglyMeasurableAtFilter [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²}
    (hf : Continuous f) (Î¼ : Measure Î±) (l : Filter Î±) : StronglyMeasurableAtFilter f l Î¼ :=
  hf.stronglyMeasurable.stronglyMeasurableAtFilter

/-- If a function is continuous on a measurable set `s`, then it is measurable at the filter
  `ğ“[s] x` for all `x`. -/
theorem ContinuousOn.stronglyMeasurableAtFilter_nhdsWithin {Î± Î² : Type*} [MeasurableSpace Î±]
    [TopologicalSpace Î±] [OpensMeasurableSpace Î±] [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±}
    (hf : ContinuousOn f s) (hs : MeasurableSet s) (x : Î±) :
    StronglyMeasurableAtFilter f (ğ“[s] x) Î¼ :=
  âŸ¨s, self_mem_nhdsWithin, hf.aestronglyMeasurable hsâŸ©

/-! ### Lemmas about adding and removing interval boundaries

The primed lemmas take explicit arguments about the measure being finite at the endpoint, while
the unprimed ones use `[NoAtoms Î¼]`.
-/


section PartialOrder

variable [PartialOrder Î±] [MeasurableSingletonClass Î±] {f : Î± â†’ E} {Î¼ : Measure Î±} {a b : Î±}

theorem integrableOn_Icc_iff_integrableOn_Ioc' (ha : Î¼ {a} â‰  âˆ) :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioc a b) Î¼ := by
  by_cases hab : a â‰¤ b
  Â· rw [â† Ioc_union_left hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr ha.lt_top), and_true]
  Â· rw [Icc_eq_empty hab, Ioc_eq_empty]
    contrapose! hab
    exact hab.le

theorem integrableOn_Icc_iff_integrableOn_Ico' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ico a b) Î¼ := by
  by_cases hab : a â‰¤ b
  Â· rw [â† Ico_union_right hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true]
  Â· rw [Icc_eq_empty hab, Ico_eq_empty]
    contrapose! hab
    exact hab.le

theorem integrableOn_Ico_iff_integrableOn_Ioo' (ha : Î¼ {a} â‰  âˆ) :
    IntegrableOn f (Ico a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  by_cases hab : a < b
  Â· rw [â† Ioo_union_left hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr ha.lt_top), and_true]
  Â· rw [Ioo_eq_empty hab, Ico_eq_empty hab]

theorem integrableOn_Ioc_iff_integrableOn_Ioo' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Ioc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  by_cases hab : a < b
  Â· rw [â† Ioo_union_right hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true]
  Â· rw [Ioo_eq_empty hab, Ioc_eq_empty hab]

theorem integrableOn_Icc_iff_integrableOn_Ioo' (ha : Î¼ {a} â‰  âˆ) (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  rw [integrableOn_Icc_iff_integrableOn_Ioc' ha, integrableOn_Ioc_iff_integrableOn_Ioo' hb]

theorem integrableOn_Ici_iff_integrableOn_Ioi' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Ici b) Î¼ â†” IntegrableOn f (Ioi b) Î¼ := by
  rw [â† Ioi_union_left, integrableOn_union,
    eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true]

theorem integrableOn_Iic_iff_integrableOn_Iio' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Iic b) Î¼ â†” IntegrableOn f (Iio b) Î¼ := by
  rw [â† Iio_union_right, integrableOn_union,
    eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true]

variable [NoAtoms Î¼]

theorem integrableOn_Icc_iff_integrableOn_Ioc :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioc a b) Î¼ :=
  integrableOn_Icc_iff_integrableOn_Ioc' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)

theorem integrableOn_Icc_iff_integrableOn_Ico :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ico a b) Î¼ :=
  integrableOn_Icc_iff_integrableOn_Ico' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)

theorem integrableOn_Ico_iff_integrableOn_Ioo :
    IntegrableOn f (Ico a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ :=
  integrableOn_Ico_iff_integrableOn_Ioo' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)

theorem integrableOn_Ioc_iff_integrableOn_Ioo :
    IntegrableOn f (Ioc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ :=
  integrableOn_Ioc_iff_integrableOn_Ioo' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)

theorem integrableOn_Icc_iff_integrableOn_Ioo :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  rw [integrableOn_Icc_iff_integrableOn_Ioc, integrableOn_Ioc_iff_integrableOn_Ioo]

theorem integrableOn_Ici_iff_integrableOn_Ioi :
    IntegrableOn f (Ici b) Î¼ â†” IntegrableOn f (Ioi b) Î¼ :=
  integrableOn_Ici_iff_integrableOn_Ioi' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)

theorem integrableOn_Iic_iff_integrableOn_Iio :
    IntegrableOn f (Iic b) Î¼ â†” IntegrableOn f (Iio b) Î¼ :=
  integrableOn_Iic_iff_integrableOn_Iio' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)

end PartialOrder
