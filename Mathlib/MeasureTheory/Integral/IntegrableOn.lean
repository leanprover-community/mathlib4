/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.Analysis.NormedSpace.IndicatorFunction

#align_import measure_theory.integral.integrable_on from "leanprover-community/mathlib"@"8b8ba04e2f326f3f7cf24ad129beda58531ada61"

/-! # Functions integrable on a set and at a filter

We define `IntegrableOn f s Î¼ := Integrable f (Î¼.restrict s)` and prove theorems like
`integrableOn_union : IntegrableOn f (s âˆª t) Î¼ â†” IntegrableOn f s Î¼ âˆ§ IntegrableOn f t Î¼`.

Next we define a predicate `IntegrableAtFilter (f : Î± â†’ E) (l : Filter Î±) (Î¼ : Measure Î±)`
saying that `f` is integrable at some set `s âˆˆ l` and prove that a measurable function is integrable
at `l` with respect to `Î¼` provided that `f` is bounded above at `l âŠ“ Î¼.ae` and `Î¼` is finite
at `l`.

-/


noncomputable section

open Set Filter TopologicalSpace MeasureTheory Function

open scoped Classical Topology Interval BigOperators Filter ENNReal MeasureTheory

variable {Î± Î² E F : Type*} [MeasurableSpace Î±]

section

variable [TopologicalSpace Î²] {l l' : Filter Î±} {f g : Î± â†’ Î²} {Î¼ Î½ : Measure Î±}

/-- A function `f` is strongly measurable at a filter `l` w.r.t. a measure `Î¼` if it is
ae strongly measurable w.r.t. `Î¼.restrict s` for some `s âˆˆ l`. -/
def StronglyMeasurableAtFilter (f : Î± â†’ Î²) (l : Filter Î±) (Î¼ : Measure Î± := by volume_tac) :=
  âˆƒ s âˆˆ l, AEStronglyMeasurable f (Î¼.restrict s)
#align strongly_measurable_at_filter StronglyMeasurableAtFilter

@[simp]
theorem stronglyMeasurableAt_bot {f : Î± â†’ Î²} : StronglyMeasurableAtFilter f âŠ¥ Î¼ :=
  âŸ¨âˆ…, mem_bot, by simpâŸ©
#align strongly_measurable_at_bot stronglyMeasurableAt_bot

protected theorem StronglyMeasurableAtFilter.eventually (h : StronglyMeasurableAtFilter f l Î¼) :
    âˆ€á¶  s in l.smallSets, AEStronglyMeasurable f (Î¼.restrict s) :=
  (eventually_smallSets' fun _ _ => AEStronglyMeasurable.mono_set).2 h
#align strongly_measurable_at_filter.eventually StronglyMeasurableAtFilter.eventually

protected theorem StronglyMeasurableAtFilter.filter_mono (h : StronglyMeasurableAtFilter f l Î¼)
    (h' : l' â‰¤ l) : StronglyMeasurableAtFilter f l' Î¼ :=
  let âŸ¨s, hsl, hsâŸ© := h
  âŸ¨s, h' hsl, hsâŸ©
#align strongly_measurable_at_filter.filter_mono StronglyMeasurableAtFilter.filter_mono

protected theorem MeasureTheory.AEStronglyMeasurable.stronglyMeasurableAtFilter
    (h : AEStronglyMeasurable f Î¼) : StronglyMeasurableAtFilter f l Î¼ :=
  âŸ¨univ, univ_mem, by rwa [Measure.restrict_univ]âŸ©
#align measure_theory.ae_strongly_measurable.strongly_measurable_at_filter MeasureTheory.AEStronglyMeasurable.stronglyMeasurableAtFilter

theorem AeStronglyMeasurable.stronglyMeasurableAtFilter_of_mem {s}
    (h : AEStronglyMeasurable f (Î¼.restrict s)) (hl : s âˆˆ l) : StronglyMeasurableAtFilter f l Î¼ :=
  âŸ¨s, hl, hâŸ©
#align ae_strongly_measurable.strongly_measurable_at_filter_of_mem AeStronglyMeasurable.stronglyMeasurableAtFilter_of_mem

protected theorem MeasureTheory.StronglyMeasurable.stronglyMeasurableAtFilter
    (h : StronglyMeasurable f) : StronglyMeasurableAtFilter f l Î¼ :=
  h.aestronglyMeasurable.stronglyMeasurableAtFilter
#align measure_theory.strongly_measurable.strongly_measurable_at_filter MeasureTheory.StronglyMeasurable.stronglyMeasurableAtFilter

end

namespace MeasureTheory

section NormedAddCommGroup

theorem hasFiniteIntegral_restrict_of_bounded [NormedAddCommGroup E] {f : Î± â†’ E} {s : Set Î±}
    {Î¼ : Measure Î±} {C} (hs : Î¼ s < âˆ) (hf : âˆ€áµ x âˆ‚Î¼.restrict s, â€–f xâ€– â‰¤ C) :
    HasFiniteIntegral f (Î¼.restrict s) :=
  haveI : IsFiniteMeasure (Î¼.restrict s) := âŸ¨by rwa [Measure.restrict_apply_univ]âŸ©
  hasFiniteIntegral_of_bounded hf
#align measure_theory.has_finite_integral_restrict_of_bounded MeasureTheory.hasFiniteIntegral_restrict_of_bounded

variable [NormedAddCommGroup E] {f g : Î± â†’ E} {s t : Set Î±} {Î¼ Î½ : Measure Î±}

/-- A function is `IntegrableOn` a set `s` if it is almost everywhere strongly measurable on `s`
and if the integral of its pointwise norm over `s` is less than infinity. -/
def IntegrableOn (f : Î± â†’ E) (s : Set Î±) (Î¼ : Measure Î± := by volume_tac) : Prop :=
  Integrable f (Î¼.restrict s)
#align measure_theory.integrable_on MeasureTheory.IntegrableOn

-- Porting note: TODO Delete this when leanprover/lean4#2243 is fixed.
theorem integrableOn_def (f : Î± â†’ E) (s : Set Î±) (Î¼ : Measure Î±) :
    IntegrableOn f s Î¼ â†” Integrable f (Î¼.restrict s) :=
  Iff.rfl

attribute [eqns integrableOn_def] IntegrableOn

theorem IntegrableOn.integrable (h : IntegrableOn f s Î¼) : Integrable f (Î¼.restrict s) :=
  h
#align measure_theory.integrable_on.integrable MeasureTheory.IntegrableOn.integrable

@[simp]
theorem integrableOn_empty : IntegrableOn f âˆ… Î¼ := by simp [IntegrableOn, integrable_zero_measure]
#align measure_theory.integrable_on_empty MeasureTheory.integrableOn_empty

@[simp]
theorem integrableOn_univ : IntegrableOn f univ Î¼ â†” Integrable f Î¼ := by
  rw [IntegrableOn, Measure.restrict_univ]
#align measure_theory.integrable_on_univ MeasureTheory.integrableOn_univ

theorem integrableOn_zero : IntegrableOn (fun _ => (0 : E)) s Î¼ :=
  integrable_zero _ _ _
#align measure_theory.integrable_on_zero MeasureTheory.integrableOn_zero

@[simp]
theorem integrableOn_const {C : E} : IntegrableOn (fun _ => C) s Î¼ â†” C = 0 âˆ¨ Î¼ s < âˆ :=
  integrable_const_iff.trans <| by rw [Measure.restrict_apply_univ]
#align measure_theory.integrable_on_const MeasureTheory.integrableOn_const

theorem IntegrableOn.mono (h : IntegrableOn f t Î½) (hs : s âŠ† t) (hÎ¼ : Î¼ â‰¤ Î½) : IntegrableOn f s Î¼ :=
  h.mono_measure <| Measure.restrict_mono hs hÎ¼
#align measure_theory.integrable_on.mono MeasureTheory.IntegrableOn.mono

theorem IntegrableOn.mono_set (h : IntegrableOn f t Î¼) (hst : s âŠ† t) : IntegrableOn f s Î¼ :=
  h.mono hst le_rfl
#align measure_theory.integrable_on.mono_set MeasureTheory.IntegrableOn.mono_set

theorem IntegrableOn.mono_measure (h : IntegrableOn f s Î½) (hÎ¼ : Î¼ â‰¤ Î½) : IntegrableOn f s Î¼ :=
  h.mono (Subset.refl _) hÎ¼
#align measure_theory.integrable_on.mono_measure MeasureTheory.IntegrableOn.mono_measure

theorem IntegrableOn.mono_set_ae (h : IntegrableOn f t Î¼) (hst : s â‰¤áµ[Î¼] t) : IntegrableOn f s Î¼ :=
  h.integrable.mono_measure <| Measure.restrict_mono_ae hst
#align measure_theory.integrable_on.mono_set_ae MeasureTheory.IntegrableOn.mono_set_ae

theorem IntegrableOn.congr_set_ae (h : IntegrableOn f t Î¼) (hst : s =áµ[Î¼] t) : IntegrableOn f s Î¼ :=
  h.mono_set_ae hst.le
#align measure_theory.integrable_on.congr_set_ae MeasureTheory.IntegrableOn.congr_set_ae

theorem IntegrableOn.congr_fun_ae (h : IntegrableOn f s Î¼) (hst : f =áµ[Î¼.restrict s] g) :
    IntegrableOn g s Î¼ :=
  Integrable.congr h hst
#align measure_theory.integrable_on.congr_fun_ae MeasureTheory.IntegrableOn.congr_fun_ae

theorem integrableOn_congr_fun_ae (hst : f =áµ[Î¼.restrict s] g) :
    IntegrableOn f s Î¼ â†” IntegrableOn g s Î¼ :=
  âŸ¨fun h => h.congr_fun_ae hst, fun h => h.congr_fun_ae hst.symmâŸ©
#align measure_theory.integrable_on_congr_fun_ae MeasureTheory.integrableOn_congr_fun_ae

theorem IntegrableOn.congr_fun (h : IntegrableOn f s Î¼) (hst : EqOn f g s) (hs : MeasurableSet s) :
    IntegrableOn g s Î¼ :=
  h.congr_fun_ae ((ae_restrict_iff' hs).2 (eventually_of_forall hst))
#align measure_theory.integrable_on.congr_fun MeasureTheory.IntegrableOn.congr_fun

theorem integrableOn_congr_fun (hst : EqOn f g s) (hs : MeasurableSet s) :
    IntegrableOn f s Î¼ â†” IntegrableOn g s Î¼ :=
  âŸ¨fun h => h.congr_fun hst hs, fun h => h.congr_fun hst.symm hsâŸ©
#align measure_theory.integrable_on_congr_fun MeasureTheory.integrableOn_congr_fun

theorem Integrable.integrableOn (h : Integrable f Î¼) : IntegrableOn f s Î¼ :=
  h.mono_measure <| Measure.restrict_le_self
#align measure_theory.integrable.integrable_on MeasureTheory.Integrable.integrableOn

theorem IntegrableOn.restrict (h : IntegrableOn f s Î¼) (hs : MeasurableSet s) :
    IntegrableOn f s (Î¼.restrict t) := by
  rw [IntegrableOn, Measure.restrict_restrict hs]; exact h.mono_set (inter_subset_left _ _)
#align measure_theory.integrable_on.restrict MeasureTheory.IntegrableOn.restrict

theorem IntegrableOn.inter_of_restrict (h : IntegrableOn f s (Î¼.restrict t)) :
    IntegrableOn f (s âˆ© t) Î¼ := by
  have := h.mono_set (inter_subset_left s t)
  rwa [IntegrableOn, Î¼.restrict_restrict_of_subset (inter_subset_right s t)] at this

lemma Integrable.piecewise [DecidablePred (Â· âˆˆ s)]
    (hs : MeasurableSet s) (hf : IntegrableOn f s Î¼) (hg : IntegrableOn g sá¶œ Î¼) :
    Integrable (s.piecewise f g) Î¼ := by
  rw [IntegrableOn] at hf hg
  rw [â† memâ„’p_one_iff_integrable] at hf hg âŠ¢
  exact Memâ„’p.piecewise hs hf hg

theorem IntegrableOn.left_of_union (h : IntegrableOn f (s âˆª t) Î¼) : IntegrableOn f s Î¼ :=
  h.mono_set <| subset_union_left _ _
#align measure_theory.integrable_on.left_of_union MeasureTheory.IntegrableOn.left_of_union

theorem IntegrableOn.right_of_union (h : IntegrableOn f (s âˆª t) Î¼) : IntegrableOn f t Î¼ :=
  h.mono_set <| subset_union_right _ _
#align measure_theory.integrable_on.right_of_union MeasureTheory.IntegrableOn.right_of_union

theorem IntegrableOn.union (hs : IntegrableOn f s Î¼) (ht : IntegrableOn f t Î¼) :
    IntegrableOn f (s âˆª t) Î¼ :=
  (hs.add_measure ht).mono_measure <| Measure.restrict_union_le _ _
#align measure_theory.integrable_on.union MeasureTheory.IntegrableOn.union

@[simp]
theorem integrableOn_union : IntegrableOn f (s âˆª t) Î¼ â†” IntegrableOn f s Î¼ âˆ§ IntegrableOn f t Î¼ :=
  âŸ¨fun h => âŸ¨h.left_of_union, h.right_of_unionâŸ©, fun h => h.1.union h.2âŸ©
#align measure_theory.integrable_on_union MeasureTheory.integrableOn_union

@[simp]
theorem integrableOn_singleton_iff {x : Î±} [MeasurableSingletonClass Î±] :
    IntegrableOn f {x} Î¼ â†” f x = 0 âˆ¨ Î¼ {x} < âˆ := by
  have : f =áµ[Î¼.restrict {x}] fun _ => f x := by
    filter_upwards [ae_restrict_mem (measurableSet_singleton x)] with _ ha
    simp only [mem_singleton_iff.1 ha]
  rw [IntegrableOn, integrable_congr this, integrable_const_iff]
  simp
#align measure_theory.integrable_on_singleton_iff MeasureTheory.integrableOn_singleton_iff

@[simp]
theorem integrableOn_finite_biUnion {s : Set Î²} (hs : s.Finite) {t : Î² â†’ Set Î±} :
    IntegrableOn f (â‹ƒ i âˆˆ s, t i) Î¼ â†” âˆ€ i âˆˆ s, IntegrableOn f (t i) Î¼ := by
  refine hs.induction_on ?_ ?_
  Â· simp
  Â· intro a s _ _ hf; simp [hf, or_imp, forall_and]
#align measure_theory.integrable_on_finite_bUnion MeasureTheory.integrableOn_finite_biUnion

@[simp]
theorem integrableOn_finset_iUnion {s : Finset Î²} {t : Î² â†’ Set Î±} :
    IntegrableOn f (â‹ƒ i âˆˆ s, t i) Î¼ â†” âˆ€ i âˆˆ s, IntegrableOn f (t i) Î¼ :=
  integrableOn_finite_biUnion s.finite_toSet
#align measure_theory.integrable_on_finset_Union MeasureTheory.integrableOn_finset_iUnion

@[simp]
theorem integrableOn_finite_iUnion [Finite Î²] {t : Î² â†’ Set Î±} :
    IntegrableOn f (â‹ƒ i, t i) Î¼ â†” âˆ€ i, IntegrableOn f (t i) Î¼ := by
  cases nonempty_fintype Î²
  simpa using @integrableOn_finset_iUnion _ _ _ _ _ f Î¼ Finset.univ t
#align measure_theory.integrable_on_finite_Union MeasureTheory.integrableOn_finite_iUnion

theorem IntegrableOn.add_measure (hÎ¼ : IntegrableOn f s Î¼) (hÎ½ : IntegrableOn f s Î½) :
    IntegrableOn f s (Î¼ + Î½) := by
  delta IntegrableOn; rw [Measure.restrict_add]; exact hÎ¼.integrable.add_measure hÎ½
#align measure_theory.integrable_on.add_measure MeasureTheory.IntegrableOn.add_measure

@[simp]
theorem integrableOn_add_measure :
    IntegrableOn f s (Î¼ + Î½) â†” IntegrableOn f s Î¼ âˆ§ IntegrableOn f s Î½ :=
  âŸ¨fun h =>
    âŸ¨h.mono_measure (Measure.le_add_right le_rfl), h.mono_measure (Measure.le_add_left le_rfl)âŸ©,
    fun h => h.1.add_measure h.2âŸ©
#align measure_theory.integrable_on_add_measure MeasureTheory.integrableOn_add_measure

theorem _root_.MeasurableEmbedding.integrableOn_map_iff [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î±} {s : Set Î²} :
    IntegrableOn f s (Î¼.map e) â†” IntegrableOn (f âˆ˜ e) (e â»Â¹' s) Î¼ := by
  simp_rw [IntegrableOn, he.restrict_map, he.integrable_map_iff]
#align measurable_embedding.integrable_on_map_iff MeasurableEmbedding.integrableOn_map_iff

theorem _root_.MeasurableEmbedding.integrableOn_iff_comap [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î²} {s : Set Î²} (hs : s âŠ† range e) :
    IntegrableOn f s Î¼ â†” IntegrableOn (f âˆ˜ e) (e â»Â¹' s) (Î¼.comap e) := by
  simp_rw [â† he.integrableOn_map_iff, he.map_comap, IntegrableOn,
    Measure.restrict_restrict_of_subset hs]

theorem integrableOn_map_equiv [MeasurableSpace Î²] (e : Î± â‰ƒáµ Î²) {f : Î² â†’ E} {Î¼ : Measure Î±}
    {s : Set Î²} : IntegrableOn f s (Î¼.map e) â†” IntegrableOn (f âˆ˜ e) (e â»Â¹' s) Î¼ := by
  simp only [IntegrableOn, e.restrict_map, integrable_map_equiv e]
#align measure_theory.integrable_on_map_equiv MeasureTheory.integrableOn_map_equiv

theorem MeasurePreserving.integrableOn_comp_preimage [MeasurableSpace Î²] {e : Î± â†’ Î²} {Î½}
    (hâ‚ : MeasurePreserving e Î¼ Î½) (hâ‚‚ : MeasurableEmbedding e) {f : Î² â†’ E} {s : Set Î²} :
    IntegrableOn (f âˆ˜ e) (e â»Â¹' s) Î¼ â†” IntegrableOn f s Î½ :=
  (hâ‚.restrict_preimage_emb hâ‚‚ s).integrable_comp_emb hâ‚‚
#align measure_theory.measure_preserving.integrable_on_comp_preimage MeasureTheory.MeasurePreserving.integrableOn_comp_preimage

theorem MeasurePreserving.integrableOn_image [MeasurableSpace Î²] {e : Î± â†’ Î²} {Î½}
    (hâ‚ : MeasurePreserving e Î¼ Î½) (hâ‚‚ : MeasurableEmbedding e) {f : Î² â†’ E} {s : Set Î±} :
    IntegrableOn f (e '' s) Î½ â†” IntegrableOn (f âˆ˜ e) s Î¼ :=
  ((hâ‚.restrict_image_emb hâ‚‚ s).integrable_comp_emb hâ‚‚).symm
#align measure_theory.measure_preserving.integrable_on_image MeasureTheory.MeasurePreserving.integrableOn_image

theorem integrable_indicator_iff (hs : MeasurableSet s) :
    Integrable (indicator s f) Î¼ â†” IntegrableOn f s Î¼ := by
  simp [IntegrableOn, Integrable, HasFiniteIntegral, nnnorm_indicator_eq_indicator_nnnorm,
    ENNReal.coe_indicator, lintegral_indicator _ hs, aestronglyMeasurable_indicator_iff hs]
#align measure_theory.integrable_indicator_iff MeasureTheory.integrable_indicator_iff

theorem IntegrableOn.integrable_indicator (h : IntegrableOn f s Î¼) (hs : MeasurableSet s) :
    Integrable (indicator s f) Î¼ :=
  (integrable_indicator_iff hs).2 h
#align measure_theory.integrable_on.integrable_indicator MeasureTheory.IntegrableOn.integrable_indicator

theorem Integrable.indicator (h : Integrable f Î¼) (hs : MeasurableSet s) :
    Integrable (indicator s f) Î¼ :=
  h.integrableOn.integrable_indicator hs
#align measure_theory.integrable.indicator MeasureTheory.Integrable.indicator

theorem IntegrableOn.indicator (h : IntegrableOn f s Î¼) (ht : MeasurableSet t) :
    IntegrableOn (indicator t f) s Î¼ :=
  Integrable.indicator h ht
#align measure_theory.integrable_on.indicator MeasureTheory.IntegrableOn.indicator

theorem integrable_indicatorConstLp {E} [NormedAddCommGroup E] {p : â„â‰¥0âˆ} {s : Set Î±}
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) :
    Integrable (indicatorConstLp p hs hÎ¼s c) Î¼ := by
  rw [integrable_congr indicatorConstLp_coeFn, integrable_indicator_iff hs, IntegrableOn,
    integrable_const_iff, lt_top_iff_ne_top]
  right
  simpa only [Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply] using hÎ¼s
set_option linter.uppercaseLean3 false in
#align measure_theory.integrable_indicator_const_Lp MeasureTheory.integrable_indicatorConstLp

/-- If a function is integrable on a set `s` and nonzero there, then the measurable hull of `s` is
well behaved: the restriction of the measure to `toMeasurable Î¼ s` coincides with its restriction
to `s`. -/
theorem IntegrableOn.restrict_toMeasurable (hf : IntegrableOn f s Î¼) (h's : âˆ€ x âˆˆ s, f x â‰  0) :
    Î¼.restrict (toMeasurable Î¼ s) = Î¼.restrict s := by
  rcases exists_seq_strictAnti_tendsto (0 : â„) with âŸ¨u, _, u_pos, u_limâŸ©
  let v n := toMeasurable (Î¼.restrict s) { x | u n â‰¤ â€–f xâ€– }
  have A : âˆ€ n, Î¼ (s âˆ© v n) â‰  âˆ := by
    intro n
    rw [inter_comm, â† Measure.restrict_apply (measurableSet_toMeasurable _ _),
      measure_toMeasurable]
    exact (hf.measure_norm_ge_lt_top (u_pos n)).ne
  apply Measure.restrict_toMeasurable_of_cover _ A
  intro x hx
  have : 0 < â€–f xâ€– := by simp only [h's x hx, norm_pos_iff, Ne.def, not_false_iff]
  obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < â€–f xâ€–; exact ((tendsto_order.1 u_lim).2 _ this).exists
  refine' mem_iUnion.2 âŸ¨n, _âŸ©
  exact subset_toMeasurable _ _ hn.le
#align measure_theory.integrable_on.restrict_to_measurable MeasureTheory.IntegrableOn.restrict_toMeasurable

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is null-measurable. -/
theorem IntegrableOn.of_ae_diff_eq_zero (hf : IntegrableOn f s Î¼) (ht : NullMeasurableSet t Î¼)
    (h't : âˆ€áµ x âˆ‚Î¼, x âˆˆ t \ s â†’ f x = 0) : IntegrableOn f t Î¼ := by
  let u := { x âˆˆ s | f x â‰  0 }
  have hu : IntegrableOn f u Î¼ := hf.mono_set fun x hx => hx.1
  let v := toMeasurable Î¼ u
  have A : IntegrableOn f v Î¼ := by
    rw [IntegrableOn, hu.restrict_toMeasurable]
    Â· exact hu
    Â· intro x hx; exact hx.2
  have B : IntegrableOn f (t \ v) Î¼ := by
    apply integrableOn_zero.congr
    filter_upwards [ae_restrict_of_ae h't,
      ae_restrict_memâ‚€ (ht.diff (measurableSet_toMeasurable Î¼ u).nullMeasurableSet)] with x hxt hx
    by_cases h'x : x âˆˆ s
    Â· by_contra H
      exact hx.2 (subset_toMeasurable Î¼ u âŸ¨h'x, Ne.symm HâŸ©)
    Â· exact (hxt âŸ¨hx.1, h'xâŸ©).symm
  apply (A.union B).mono_set _
  rw [union_diff_self]
  exact subset_union_right _ _
#align measure_theory.integrable_on.of_ae_diff_eq_zero MeasureTheory.IntegrableOn.of_ae_diff_eq_zero

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is measurable. -/
theorem IntegrableOn.of_forall_diff_eq_zero (hf : IntegrableOn f s Î¼) (ht : MeasurableSet t)
    (h't : âˆ€ x âˆˆ t \ s, f x = 0) : IntegrableOn f t Î¼ :=
  hf.of_ae_diff_eq_zero ht.nullMeasurableSet (eventually_of_forall h't)
#align measure_theory.integrable_on.of_forall_diff_eq_zero MeasureTheory.IntegrableOn.of_forall_diff_eq_zero

/-- If a function is integrable on a set `s` and vanishes almost everywhere on its complement,
then it is integrable. -/
theorem IntegrableOn.integrable_of_ae_not_mem_eq_zero (hf : IntegrableOn f s Î¼)
    (h't : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ f x = 0) : Integrable f Î¼ := by
  rw [â† integrableOn_univ]
  apply hf.of_ae_diff_eq_zero nullMeasurableSet_univ
  filter_upwards [h't] with x hx h'x using hx h'x.2
#align measure_theory.integrable_on.integrable_of_ae_not_mem_eq_zero MeasureTheory.IntegrableOn.integrable_of_ae_not_mem_eq_zero

/-- If a function is integrable on a set `s` and vanishes everywhere on its complement,
then it is integrable. -/
theorem IntegrableOn.integrable_of_forall_not_mem_eq_zero (hf : IntegrableOn f s Î¼)
    (h't : âˆ€ x, x âˆ‰ s â†’ f x = 0) : Integrable f Î¼ :=
  hf.integrable_of_ae_not_mem_eq_zero (eventually_of_forall fun x hx => h't x hx)
#align measure_theory.integrable_on.integrable_of_forall_not_mem_eq_zero MeasureTheory.IntegrableOn.integrable_of_forall_not_mem_eq_zero

theorem integrableOn_iff_integrable_of_support_subset (h1s : support f âŠ† s) :
    IntegrableOn f s Î¼ â†” Integrable f Î¼ := by
  refine' âŸ¨fun h => _, fun h => h.integrableOnâŸ©
  refine h.integrable_of_forall_not_mem_eq_zero fun x hx => ?_
  contrapose! hx
  exact h1s (mem_support.2 hx)
#align measure_theory.integrable_on_iff_integrable_of_support_subset MeasureTheory.integrableOn_iff_integrable_of_support_subset

theorem integrableOn_Lp_of_measure_ne_top {E} [NormedAddCommGroup E] {p : â„â‰¥0âˆ} {s : Set Î±}
    (f : Lp E p Î¼) (hp : 1 â‰¤ p) (hÎ¼s : Î¼ s â‰  âˆ) : IntegrableOn f s Î¼ := by
  refine' memâ„’p_one_iff_integrable.mp _
  have hÎ¼_restrict_univ : (Î¼.restrict s) Set.univ < âˆ := by
    simpa only [Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply, lt_top_iff_ne_top]
  haveI hÎ¼_finite : IsFiniteMeasure (Î¼.restrict s) := âŸ¨hÎ¼_restrict_univâŸ©
  exact ((Lp.memâ„’p _).restrict s).memâ„’p_of_exponent_le hp
set_option linter.uppercaseLean3 false in
#align measure_theory.integrable_on_Lp_of_measure_ne_top MeasureTheory.integrableOn_Lp_of_measure_ne_top

theorem Integrable.lintegral_lt_top {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    (âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼) < âˆ :=
  calc
    (âˆ«â» x, ENNReal.ofReal (f x) âˆ‚Î¼) â‰¤ âˆ«â» x, â†‘â€–f xâ€–â‚Š âˆ‚Î¼ := lintegral_ofReal_le_lintegral_nnnorm f
    _ < âˆ := hf.2
#align measure_theory.integrable.lintegral_lt_top MeasureTheory.Integrable.lintegral_lt_top

theorem IntegrableOn.set_lintegral_lt_top {f : Î± â†’ â„} {s : Set Î±} (hf : IntegrableOn f s Î¼) :
    (âˆ«â» x in s, ENNReal.ofReal (f x) âˆ‚Î¼) < âˆ :=
  Integrable.lintegral_lt_top hf
#align measure_theory.integrable_on.set_lintegral_lt_top MeasureTheory.IntegrableOn.set_lintegral_lt_top

/-- We say that a function `f` is *integrable at filter* `l` if it is integrable on some
set `s âˆˆ l`. Equivalently, it is eventually integrable on `s` in `l.smallSets`. -/
def IntegrableAtFilter (f : Î± â†’ E) (l : Filter Î±) (Î¼ : Measure Î± := by volume_tac) :=
  âˆƒ s âˆˆ l, IntegrableOn f s Î¼
#align measure_theory.integrable_at_filter MeasureTheory.IntegrableAtFilter

variable {l l' : Filter Î±}

theorem _root_.MeasurableEmbedding.integrableAtFilter_map_iff [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} :
    IntegrableAtFilter f (l.map e) (Î¼.map e) â†” IntegrableAtFilter (f âˆ˜ e) l Î¼ := by
  simp_rw [IntegrableAtFilter, he.integrableOn_map_iff]
  constructor <;> rintro âŸ¨s, hsâŸ©
  Â· exact âŸ¨_, hsâŸ©
  Â· exact âŸ¨e '' s, by rwa [mem_map, he.injective.preimage_image]âŸ©

theorem _root_.MeasurableEmbedding.integrableAtFilter_iff_comap [MeasurableSpace Î²] {e : Î± â†’ Î²}
    (he : MeasurableEmbedding e) {f : Î² â†’ E} {Î¼ : Measure Î²} :
    IntegrableAtFilter f (l.map e) Î¼ â†” IntegrableAtFilter (f âˆ˜ e) l (Î¼.comap e) := by
  simp_rw [â† he.integrableAtFilter_map_iff, IntegrableAtFilter, he.map_comap]
  constructor <;> rintro âŸ¨s, hs, intâŸ©
  Â· exact âŸ¨s, hs, int.mono_measure <| Î¼.restrict_le_selfâŸ©
  Â· exact âŸ¨_, inter_mem hs range_mem_map, int.inter_of_restrictâŸ©

theorem Integrable.integrableAtFilter (h : Integrable f Î¼) (l : Filter Î±) :
    IntegrableAtFilter f l Î¼ :=
  âŸ¨univ, Filter.univ_mem, integrableOn_univ.2 hâŸ©
#align measure_theory.integrable.integrable_at_filter MeasureTheory.Integrable.integrableAtFilter

protected theorem IntegrableAtFilter.eventually (h : IntegrableAtFilter f l Î¼) :
    âˆ€á¶  s in l.smallSets, IntegrableOn f s Î¼ :=
  Iff.mpr (eventually_smallSets' fun _s _t hst ht => ht.mono_set hst) h
#align measure_theory.integrable_at_filter.eventually MeasureTheory.IntegrableAtFilter.eventually

protected theorem IntegrableAtFilter.add {f g : Î± â†’ E}
    (hf : IntegrableAtFilter f l Î¼) (hg : IntegrableAtFilter g l Î¼) :
    IntegrableAtFilter (f + g) l Î¼ := by
  rcases hf with âŸ¨s, sl, hsâŸ©
  rcases hg with âŸ¨t, tl, htâŸ©
  refine âŸ¨s âˆ© t, inter_mem sl tl, ?_âŸ©
  exact (hs.mono_set (inter_subset_left _ _)).add (ht.mono_set (inter_subset_right _ _))

protected theorem IntegrableAtFilter.neg {f : Î± â†’ E} (hf : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter (-f) l Î¼ := by
  rcases hf with âŸ¨s, sl, hsâŸ©
  exact âŸ¨s, sl, hs.negâŸ©

protected theorem IntegrableAtFilter.sub {f g : Î± â†’ E}
    (hf : IntegrableAtFilter f l Î¼) (hg : IntegrableAtFilter g l Î¼) :
    IntegrableAtFilter (f - g) l Î¼ := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg

protected theorem IntegrableAtFilter.smul {ğ•œ : Type*} [NormedAddCommGroup ğ•œ] [SMulZeroClass ğ•œ E]
    [BoundedSMul ğ•œ E] {f : Î± â†’ E} (hf : IntegrableAtFilter f l Î¼) (c : ğ•œ) :
    IntegrableAtFilter (c â€¢ f) l Î¼ := by
  rcases hf with âŸ¨s, sl, hsâŸ©
  exact âŸ¨s, sl, hs.smul câŸ©

theorem IntegrableAtFilter.filter_mono (hl : l â‰¤ l') (hl' : IntegrableAtFilter f l' Î¼) :
    IntegrableAtFilter f l Î¼ :=
  let âŸ¨s, hs, hsfâŸ© := hl'
  âŸ¨s, hl hs, hsfâŸ©
#align measure_theory.integrable_at_filter.filter_mono MeasureTheory.IntegrableAtFilter.filter_mono

theorem IntegrableAtFilter.inf_of_left (hl : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter f (l âŠ“ l') Î¼ :=
  hl.filter_mono inf_le_left
#align measure_theory.integrable_at_filter.inf_of_left MeasureTheory.IntegrableAtFilter.inf_of_left

theorem IntegrableAtFilter.inf_of_right (hl : IntegrableAtFilter f l Î¼) :
    IntegrableAtFilter f (l' âŠ“ l) Î¼ :=
  hl.filter_mono inf_le_right
#align measure_theory.integrable_at_filter.inf_of_right MeasureTheory.IntegrableAtFilter.inf_of_right

@[simp]
theorem IntegrableAtFilter.inf_ae_iff {l : Filter Î±} :
    IntegrableAtFilter f (l âŠ“ Î¼.ae) Î¼ â†” IntegrableAtFilter f l Î¼ := by
  refine' âŸ¨_, fun h => h.filter_mono inf_le_leftâŸ©
  rintro âŸ¨s, âŸ¨t, ht, u, hu, rflâŸ©, hfâŸ©
  refine' âŸ¨t, ht, _âŸ©
  refine' hf.integrable.mono_measure fun v hv => _
  simp only [Measure.restrict_apply hv]
  refine' measure_mono_ae (mem_of_superset hu fun x hx => _)
  exact fun âŸ¨hv, htâŸ© => âŸ¨hv, âŸ¨ht, hxâŸ©âŸ©
#align measure_theory.integrable_at_filter.inf_ae_iff MeasureTheory.IntegrableAtFilter.inf_ae_iff

alias âŸ¨IntegrableAtFilter.of_inf_ae, _âŸ© := IntegrableAtFilter.inf_ae_iff
#align measure_theory.integrable_at_filter.of_inf_ae MeasureTheory.IntegrableAtFilter.of_inf_ae

@[simp]
theorem IntegrableAtFilter.top : IntegrableAtFilter f âŠ¤ Î¼ â†” Integrable f Î¼ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h.integrableAtFilter âŠ¤âŸ©
  obtain âŸ¨s, hsf, hsâŸ© := h
  exact (integrableOn_iff_integrable_of_support_subset fun _ _ â†¦ hsf _).mp hs

/-- If `Î¼` is a measure finite at filter `l` and `f` is a function such that its norm is bounded
above at `l`, then `f` is integrable at `l`. -/
theorem Measure.FiniteAtFilter.integrableAtFilter {l : Filter Î±} [IsMeasurablyGenerated l]
    (hfm : StronglyMeasurableAtFilter f l Î¼) (hÎ¼ : Î¼.FiniteAtFilter l)
    (hf : l.IsBoundedUnder (Â· â‰¤ Â·) (norm âˆ˜ f)) : IntegrableAtFilter f l Î¼ := by
  obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€á¶  s in l.smallSets, âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ C :=
    hf.imp fun C hC => eventually_smallSets.2 âŸ¨_, hC, fun t => idâŸ©
  rcases (hfm.eventually.and (hÎ¼.eventually.and hC)).exists_measurable_mem_of_smallSets with
    âŸ¨s, hsl, hsm, hfm, hÎ¼, hCâŸ©
  refine' âŸ¨s, hsl, âŸ¨hfm, hasFiniteIntegral_restrict_of_bounded hÎ¼ (C := C) _âŸ©âŸ©
  rw [ae_restrict_eq hsm, eventually_inf_principal]
  exact eventually_of_forall hC
#align measure_theory.measure.finite_at_filter.integrable_at_filter MeasureTheory.Measure.FiniteAtFilter.integrableAtFilter

theorem Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae {l : Filter Î±}
    [IsMeasurablyGenerated l] (hfm : StronglyMeasurableAtFilter f l Î¼) (hÎ¼ : Î¼.FiniteAtFilter l) {b}
    (hf : Tendsto f (l âŠ“ Î¼.ae) (ğ“ b)) : IntegrableAtFilter f l Î¼ :=
  (hÎ¼.inf_of_left.integrableAtFilter (hfm.filter_mono inf_le_left)
      hf.norm.isBoundedUnder_le).of_inf_ae
#align measure_theory.measure.finite_at_filter.integrable_at_filter_of_tendsto_ae MeasureTheory.Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae

alias _root_.Filter.Tendsto.integrableAtFilter_ae :=
  Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae
#align filter.tendsto.integrable_at_filter_ae Filter.Tendsto.integrableAtFilter_ae

theorem Measure.FiniteAtFilter.integrableAtFilter_of_tendsto {l : Filter Î±}
    [IsMeasurablyGenerated l] (hfm : StronglyMeasurableAtFilter f l Î¼) (hÎ¼ : Î¼.FiniteAtFilter l) {b}
    (hf : Tendsto f l (ğ“ b)) : IntegrableAtFilter f l Î¼ :=
  hÎ¼.integrableAtFilter hfm hf.norm.isBoundedUnder_le
#align measure_theory.measure.finite_at_filter.integrable_at_filter_of_tendsto MeasureTheory.Measure.FiniteAtFilter.integrableAtFilter_of_tendsto

alias _root_.Filter.Tendsto.integrableAtFilter :=
  Measure.FiniteAtFilter.integrableAtFilter_of_tendsto
#align filter.tendsto.integrable_at_filter Filter.Tendsto.integrableAtFilter

theorem integrable_add_of_disjoint {f g : Î± â†’ E} (h : Disjoint (support f) (support g))
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    Integrable (f + g) Î¼ â†” Integrable f Î¼ âˆ§ Integrable g Î¼ := by
  refine' âŸ¨fun hfg => âŸ¨_, _âŸ©, fun h => h.1.add h.2âŸ©
  Â· rw [â† indicator_add_eq_left h]; exact hfg.indicator hf.measurableSet_support
  Â· rw [â† indicator_add_eq_right h]; exact hfg.indicator hg.measurableSet_support
#align measure_theory.integrable_add_of_disjoint MeasureTheory.integrable_add_of_disjoint

end NormedAddCommGroup

end MeasureTheory

open MeasureTheory

variable [NormedAddCommGroup E]

/-- A function which is continuous on a set `s` is almost everywhere measurable with respect to
`Î¼.restrict s`. -/
theorem ContinuousOn.aemeasurable [TopologicalSpace Î±] [OpensMeasurableSpace Î±] [MeasurableSpace Î²]
    [TopologicalSpace Î²] [BorelSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±}
    (hf : ContinuousOn f s) (hs : MeasurableSet s) : AEMeasurable f (Î¼.restrict s) := by
  nontriviality Î±; inhabit Î±
  have : (Set.piecewise s f fun _ => f default) =áµ[Î¼.restrict s] f := piecewise_ae_eq_restrict hs
  refine' âŸ¨Set.piecewise s f fun _ => f default, _, this.symmâŸ©
  apply measurable_of_isOpen
  intro t ht
  obtain âŸ¨u, u_open, huâŸ© : âˆƒ u : Set Î±, IsOpen u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
    _root_.continuousOn_iff'.1 hf t ht
  rw [piecewise_preimage, Set.ite, hu]
  exact (u_open.measurableSet.inter hs).union ((measurable_const ht.measurableSet).diff hs)
#align continuous_on.ae_measurable ContinuousOn.aemeasurable

/-- A function which is continuous on a separable set `s` is almost everywhere strongly measurable
with respect to `Î¼.restrict s`. -/
theorem ContinuousOn.aestronglyMeasurable_of_isSeparable [TopologicalSpace Î±]
    [PseudoMetrizableSpace Î±] [OpensMeasurableSpace Î±] [TopologicalSpace Î²]
    [PseudoMetrizableSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±} (hf : ContinuousOn f s)
    (hs : MeasurableSet s) (h's : TopologicalSpace.IsSeparable s) :
    AEStronglyMeasurable f (Î¼.restrict s) := by
  letI := pseudoMetrizableSpacePseudoMetric Î±
  borelize Î²
  rw [aestronglyMeasurable_iff_aemeasurable_separable]
  refine' âŸ¨hf.aemeasurable hs, f '' s, hf.isSeparable_image h's, _âŸ©
  exact mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)
#align continuous_on.ae_strongly_measurable_of_is_separable ContinuousOn.aestronglyMeasurable_of_isSeparable

/-- A function which is continuous on a set `s` is almost everywhere strongly measurable with
respect to `Î¼.restrict s` when either the source space or the target space is second-countable. -/
theorem ContinuousOn.aestronglyMeasurable [TopologicalSpace Î±] [TopologicalSpace Î²]
    [h : SecondCountableTopologyEither Î± Î²] [OpensMeasurableSpace Î±] [PseudoMetrizableSpace Î²]
    {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±} (hf : ContinuousOn f s) (hs : MeasurableSet s) :
    AEStronglyMeasurable f (Î¼.restrict s) := by
  borelize Î²
  refine'
    aestronglyMeasurable_iff_aemeasurable_separable.2
      âŸ¨hf.aemeasurable hs, f '' s, _,
        mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)âŸ©
  cases h.out
  Â· let f' : s â†’ Î² := s.restrict f
    have A : Continuous f' := continuousOn_iff_continuous_restrict.1 hf
    have B : IsSeparable (univ : Set s) := isSeparable_of_separableSpace _
    convert IsSeparable.image B A using 1
    ext x
    simp
  Â· exact isSeparable_of_separableSpace _
#align continuous_on.ae_strongly_measurable ContinuousOn.aestronglyMeasurable

/-- A function which is continuous on a compact set `s` is almost everywhere strongly measurable
with respect to `Î¼.restrict s`. -/
theorem ContinuousOn.aestronglyMeasurable_of_isCompact [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±}
    (hf : ContinuousOn f s) (hs : IsCompact s) (h's : MeasurableSet s) :
    AEStronglyMeasurable f (Î¼.restrict s) := by
  letI := pseudoMetrizableSpacePseudoMetric Î²
  borelize Î²
  rw [aestronglyMeasurable_iff_aemeasurable_separable]
  refine' âŸ¨hf.aemeasurable h's, f '' s, _, _âŸ©
  Â· exact (hs.image_of_continuousOn hf).isSeparable
  Â· exact mem_of_superset (self_mem_ae_restrict h's) (subset_preimage_image _ _)
#align continuous_on.ae_strongly_measurable_of_is_compact ContinuousOn.aestronglyMeasurable_of_isCompact

theorem ContinuousOn.integrableAt_nhdsWithin_of_isSeparable [TopologicalSpace Î±]
    [PseudoMetrizableSpace Î±] [OpensMeasurableSpace Î±] {Î¼ : Measure Î±} [IsLocallyFiniteMeasure Î¼]
    {a : Î±} {t : Set Î±} {f : Î± â†’ E} (hft : ContinuousOn f t) (ht : MeasurableSet t)
    (h't : TopologicalSpace.IsSeparable t) (ha : a âˆˆ t) : IntegrableAtFilter f (ğ“[t] a) Î¼ :=
  haveI : (ğ“[t] a).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hft a ha).integrableAtFilter
    âŸ¨_, self_mem_nhdsWithin, hft.aestronglyMeasurable_of_isSeparable ht h'tâŸ©
    (Î¼.finiteAt_nhdsWithin _ _)
#align continuous_on.integrable_at_nhds_within_of_is_separable ContinuousOn.integrableAt_nhdsWithin_of_isSeparable

theorem ContinuousOn.integrableAt_nhdsWithin [TopologicalSpace Î±]
    [SecondCountableTopologyEither Î± E] [OpensMeasurableSpace Î±] {Î¼ : Measure Î±}
    [IsLocallyFiniteMeasure Î¼] {a : Î±} {t : Set Î±} {f : Î± â†’ E} (hft : ContinuousOn f t)
    (ht : MeasurableSet t) (ha : a âˆˆ t) : IntegrableAtFilter f (ğ“[t] a) Î¼ :=
  haveI : (ğ“[t] a).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hft a ha).integrableAtFilter âŸ¨_, self_mem_nhdsWithin, hft.aestronglyMeasurable htâŸ©
    (Î¼.finiteAt_nhdsWithin _ _)
#align continuous_on.integrable_at_nhds_within ContinuousOn.integrableAt_nhdsWithin

theorem Continuous.integrableAt_nhds [TopologicalSpace Î±] [SecondCountableTopologyEither Î± E]
    [OpensMeasurableSpace Î±] {Î¼ : Measure Î±} [IsLocallyFiniteMeasure Î¼] {f : Î± â†’ E}
    (hf : Continuous f) (a : Î±) : IntegrableAtFilter f (ğ“ a) Î¼ := by
  rw [â† nhdsWithin_univ]
  exact hf.continuousOn.integrableAt_nhdsWithin MeasurableSet.univ (mem_univ a)
#align continuous.integrable_at_nhds Continuous.integrableAt_nhds

/-- If a function is continuous on an open set `s`, then it is strongly measurable at the filter
`ğ“ x` for all `x âˆˆ s` if either the source space or the target space is second-countable. -/
theorem ContinuousOn.stronglyMeasurableAtFilter [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²}
    {s : Set Î±} {Î¼ : Measure Î±} (hs : IsOpen s) (hf : ContinuousOn f s) :
    âˆ€ x âˆˆ s, StronglyMeasurableAtFilter f (ğ“ x) Î¼ := fun _x hx =>
  âŸ¨s, IsOpen.mem_nhds hs hx, hf.aestronglyMeasurable hs.measurableSetâŸ©
#align continuous_on.strongly_measurable_at_filter ContinuousOn.stronglyMeasurableAtFilter

theorem ContinuousAt.stronglyMeasurableAtFilter [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [SecondCountableTopologyEither Î± E] {f : Î± â†’ E} {s : Set Î±} {Î¼ : Measure Î±} (hs : IsOpen s)
    (hf : âˆ€ x âˆˆ s, ContinuousAt f x) : âˆ€ x âˆˆ s, StronglyMeasurableAtFilter f (ğ“ x) Î¼ :=
  ContinuousOn.stronglyMeasurableAtFilter hs <| ContinuousAt.continuousOn hf
#align continuous_at.strongly_measurable_at_filter ContinuousAt.stronglyMeasurableAtFilter

theorem Continuous.stronglyMeasurableAtFilter [TopologicalSpace Î±] [OpensMeasurableSpace Î±]
    [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²}
    (hf : Continuous f) (Î¼ : Measure Î±) (l : Filter Î±) : StronglyMeasurableAtFilter f l Î¼ :=
  hf.stronglyMeasurable.stronglyMeasurableAtFilter
#align continuous.strongly_measurable_at_filter Continuous.stronglyMeasurableAtFilter

/-- If a function is continuous on a measurable set `s`, then it is measurable at the filter
  `ğ“[s] x` for all `x`. -/
theorem ContinuousOn.stronglyMeasurableAtFilter_nhdsWithin {Î± Î² : Type*} [MeasurableSpace Î±]
    [TopologicalSpace Î±] [OpensMeasurableSpace Î±] [TopologicalSpace Î²] [PseudoMetrizableSpace Î²]
    [SecondCountableTopologyEither Î± Î²] {f : Î± â†’ Î²} {s : Set Î±} {Î¼ : Measure Î±}
    (hf : ContinuousOn f s) (hs : MeasurableSet s) (x : Î±) :
    StronglyMeasurableAtFilter f (ğ“[s] x) Î¼ :=
  âŸ¨s, self_mem_nhdsWithin, hf.aestronglyMeasurable hsâŸ©
#align continuous_on.strongly_measurable_at_filter_nhds_within ContinuousOn.stronglyMeasurableAtFilter_nhdsWithin

/-! ### Lemmas about adding and removing interval boundaries

The primed lemmas take explicit arguments about the measure being finite at the endpoint, while
the unprimed ones use `[NoAtoms Î¼]`.
-/


section PartialOrder

variable [PartialOrder Î±] [MeasurableSingletonClass Î±] {f : Î± â†’ E} {Î¼ : Measure Î±} {a b : Î±}

theorem integrableOn_Icc_iff_integrableOn_Ioc' (ha : Î¼ {a} â‰  âˆ) :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioc a b) Î¼ := by
  by_cases hab : a â‰¤ b
  Â· rw [â† Ioc_union_left hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr ha.lt_top), and_true_iff]
  Â· rw [Icc_eq_empty hab, Ioc_eq_empty]
    contrapose! hab
    exact hab.le
#align integrable_on_Icc_iff_integrable_on_Ioc' integrableOn_Icc_iff_integrableOn_Ioc'

theorem integrableOn_Icc_iff_integrableOn_Ico' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ico a b) Î¼ := by
  by_cases hab : a â‰¤ b
  Â· rw [â† Ico_union_right hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
  Â· rw [Icc_eq_empty hab, Ico_eq_empty]
    contrapose! hab
    exact hab.le
#align integrable_on_Icc_iff_integrable_on_Ico' integrableOn_Icc_iff_integrableOn_Ico'

theorem integrableOn_Ico_iff_integrableOn_Ioo' (ha : Î¼ {a} â‰  âˆ) :
    IntegrableOn f (Ico a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  by_cases hab : a < b
  Â· rw [â† Ioo_union_left hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr ha.lt_top), and_true_iff]
  Â· rw [Ioo_eq_empty hab, Ico_eq_empty hab]
#align integrable_on_Ico_iff_integrable_on_Ioo' integrableOn_Ico_iff_integrableOn_Ioo'

theorem integrableOn_Ioc_iff_integrableOn_Ioo' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Ioc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  by_cases hab : a < b
  Â· rw [â† Ioo_union_right hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
  Â· rw [Ioo_eq_empty hab, Ioc_eq_empty hab]
#align integrable_on_Ioc_iff_integrable_on_Ioo' integrableOn_Ioc_iff_integrableOn_Ioo'

theorem integrableOn_Icc_iff_integrableOn_Ioo' (ha : Î¼ {a} â‰  âˆ) (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  rw [integrableOn_Icc_iff_integrableOn_Ioc' ha, integrableOn_Ioc_iff_integrableOn_Ioo' hb]
#align integrable_on_Icc_iff_integrable_on_Ioo' integrableOn_Icc_iff_integrableOn_Ioo'

theorem integrableOn_Ici_iff_integrableOn_Ioi' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Ici b) Î¼ â†” IntegrableOn f (Ioi b) Î¼ := by
  rw [â† Ioi_union_left, integrableOn_union,
    eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
#align integrable_on_Ici_iff_integrable_on_Ioi' integrableOn_Ici_iff_integrableOn_Ioi'

theorem integrableOn_Iic_iff_integrableOn_Iio' (hb : Î¼ {b} â‰  âˆ) :
    IntegrableOn f (Iic b) Î¼ â†” IntegrableOn f (Iio b) Î¼ := by
  rw [â† Iio_union_right, integrableOn_union,
    eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
#align integrable_on_Iic_iff_integrable_on_Iio' integrableOn_Iic_iff_integrableOn_Iio'

variable [NoAtoms Î¼]

theorem integrableOn_Icc_iff_integrableOn_Ioc :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioc a b) Î¼ :=
  integrableOn_Icc_iff_integrableOn_Ioc' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Icc_iff_integrable_on_Ioc integrableOn_Icc_iff_integrableOn_Ioc

theorem integrableOn_Icc_iff_integrableOn_Ico :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ico a b) Î¼ :=
  integrableOn_Icc_iff_integrableOn_Ico' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Icc_iff_integrable_on_Ico integrableOn_Icc_iff_integrableOn_Ico

theorem integrableOn_Ico_iff_integrableOn_Ioo :
    IntegrableOn f (Ico a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ :=
  integrableOn_Ico_iff_integrableOn_Ioo' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Ico_iff_integrable_on_Ioo integrableOn_Ico_iff_integrableOn_Ioo

theorem integrableOn_Ioc_iff_integrableOn_Ioo :
    IntegrableOn f (Ioc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ :=
  integrableOn_Ioc_iff_integrableOn_Ioo' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Ioc_iff_integrable_on_Ioo integrableOn_Ioc_iff_integrableOn_Ioo

theorem integrableOn_Icc_iff_integrableOn_Ioo :
    IntegrableOn f (Icc a b) Î¼ â†” IntegrableOn f (Ioo a b) Î¼ := by
  rw [integrableOn_Icc_iff_integrableOn_Ioc, integrableOn_Ioc_iff_integrableOn_Ioo]
#align integrable_on_Icc_iff_integrable_on_Ioo integrableOn_Icc_iff_integrableOn_Ioo

theorem integrableOn_Ici_iff_integrableOn_Ioi :
    IntegrableOn f (Ici b) Î¼ â†” IntegrableOn f (Ioi b) Î¼ :=
  integrableOn_Ici_iff_integrableOn_Ioi' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Ici_iff_integrable_on_Ioi integrableOn_Ici_iff_integrableOn_Ioi

theorem integrableOn_Iic_iff_integrableOn_Iio :
    IntegrableOn f (Iic b) Î¼ â†” IntegrableOn f (Iio b) Î¼ :=
  integrableOn_Iic_iff_integrableOn_Iio' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Iic_iff_integrable_on_Iio integrableOn_Iic_iff_integrableOn_Iio

end PartialOrder
