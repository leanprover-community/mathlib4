/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.Analysis.NormedSpace.IndicatorFunction

#align_import measure_theory.integral.integrable_on from "leanprover-community/mathlib"@"8b8ba04e2f326f3f7cf24ad129beda58531ada61"

/-! # Functions integrable on a set and at a filter

We define `IntegrableOn f s Œº := Integrable f (Œº.restrict s)` and prove theorems like
`integrableOn_union : IntegrableOn f (s ‚à™ t) Œº ‚Üî IntegrableOn f s Œº ‚àß IntegrableOn f t Œº`.

Next we define a predicate `IntegrableAtFilter (f : Œ± ‚Üí E) (l : Filter Œ±) (Œº : Measure Œ±)`
saying that `f` is integrable at some set `s ‚àà l` and prove that a measurable function is integrable
at `l` with respect to `Œº` provided that `f` is bounded above at `l ‚äì ae Œº` and `Œº` is finite
at `l`.

-/


noncomputable section

open Set Filter TopologicalSpace MeasureTheory Function

open scoped Classical Topology Interval Filter ENNReal MeasureTheory

variable {Œ± Œ≤ E F : Type*} [MeasurableSpace Œ±]

section

variable [TopologicalSpace Œ≤] {l l' : Filter Œ±} {f g : Œ± ‚Üí Œ≤} {Œº ŒΩ : Measure Œ±}

/-- A function `f` is strongly measurable at a filter `l` w.r.t. a measure `Œº` if it is
ae strongly measurable w.r.t. `Œº.restrict s` for some `s ‚àà l`. -/
def StronglyMeasurableAtFilter (f : Œ± ‚Üí Œ≤) (l : Filter Œ±) (Œº : Measure Œ± := by volume_tac) :=
  ‚àÉ s ‚àà l, AEStronglyMeasurable f (Œº.restrict s)
#align strongly_measurable_at_filter StronglyMeasurableAtFilter

@[simp]
theorem stronglyMeasurableAt_bot {f : Œ± ‚Üí Œ≤} : StronglyMeasurableAtFilter f ‚ä• Œº :=
  ‚ü®‚àÖ, mem_bot, by simp‚ü©
#align strongly_measurable_at_bot stronglyMeasurableAt_bot

protected theorem StronglyMeasurableAtFilter.eventually (h : StronglyMeasurableAtFilter f l Œº) :
    ‚àÄ·∂† s in l.smallSets, AEStronglyMeasurable f (Œº.restrict s) :=
  (eventually_smallSets' fun _ _ => AEStronglyMeasurable.mono_set).2 h
#align strongly_measurable_at_filter.eventually StronglyMeasurableAtFilter.eventually

protected theorem StronglyMeasurableAtFilter.filter_mono (h : StronglyMeasurableAtFilter f l Œº)
    (h' : l' ‚â§ l) : StronglyMeasurableAtFilter f l' Œº :=
  let ‚ü®s, hsl, hs‚ü© := h
  ‚ü®s, h' hsl, hs‚ü©
#align strongly_measurable_at_filter.filter_mono StronglyMeasurableAtFilter.filter_mono

protected theorem MeasureTheory.AEStronglyMeasurable.stronglyMeasurableAtFilter
    (h : AEStronglyMeasurable f Œº) : StronglyMeasurableAtFilter f l Œº :=
  ‚ü®univ, univ_mem, by rwa [Measure.restrict_univ]‚ü©
#align measure_theory.ae_strongly_measurable.strongly_measurable_at_filter MeasureTheory.AEStronglyMeasurable.stronglyMeasurableAtFilter

theorem AeStronglyMeasurable.stronglyMeasurableAtFilter_of_mem {s}
    (h : AEStronglyMeasurable f (Œº.restrict s)) (hl : s ‚àà l) : StronglyMeasurableAtFilter f l Œº :=
  ‚ü®s, hl, h‚ü©
#align ae_strongly_measurable.strongly_measurable_at_filter_of_mem AeStronglyMeasurable.stronglyMeasurableAtFilter_of_mem

protected theorem MeasureTheory.StronglyMeasurable.stronglyMeasurableAtFilter
    (h : StronglyMeasurable f) : StronglyMeasurableAtFilter f l Œº :=
  h.aestronglyMeasurable.stronglyMeasurableAtFilter
#align measure_theory.strongly_measurable.strongly_measurable_at_filter MeasureTheory.StronglyMeasurable.stronglyMeasurableAtFilter

end

namespace MeasureTheory

section NormedAddCommGroup

theorem hasFiniteIntegral_restrict_of_bounded [NormedAddCommGroup E] {f : Œ± ‚Üí E} {s : Set Œ±}
    {Œº : Measure Œ±} {C} (hs : Œº s < ‚àû) (hf : ‚àÄ·µê x ‚àÇŒº.restrict s, ‚Äñf x‚Äñ ‚â§ C) :
    HasFiniteIntegral f (Œº.restrict s) :=
  haveI : IsFiniteMeasure (Œº.restrict s) := ‚ü®by rwa [Measure.restrict_apply_univ]‚ü©
  hasFiniteIntegral_of_bounded hf
#align measure_theory.has_finite_integral_restrict_of_bounded MeasureTheory.hasFiniteIntegral_restrict_of_bounded

variable [NormedAddCommGroup E] {f g : Œ± ‚Üí E} {s t : Set Œ±} {Œº ŒΩ : Measure Œ±}

/-- A function is `IntegrableOn` a set `s` if it is almost everywhere strongly measurable on `s`
and if the integral of its pointwise norm over `s` is less than infinity. -/
def IntegrableOn (f : Œ± ‚Üí E) (s : Set Œ±) (Œº : Measure Œ± := by volume_tac) : Prop :=
  Integrable f (Œº.restrict s)
#align measure_theory.integrable_on MeasureTheory.IntegrableOn

theorem IntegrableOn.integrable (h : IntegrableOn f s Œº) : Integrable f (Œº.restrict s) :=
  h
#align measure_theory.integrable_on.integrable MeasureTheory.IntegrableOn.integrable

@[simp]
theorem integrableOn_empty : IntegrableOn f ‚àÖ Œº := by simp [IntegrableOn, integrable_zero_measure]
#align measure_theory.integrable_on_empty MeasureTheory.integrableOn_empty

@[simp]
theorem integrableOn_univ : IntegrableOn f univ Œº ‚Üî Integrable f Œº := by
  rw [IntegrableOn, Measure.restrict_univ]
#align measure_theory.integrable_on_univ MeasureTheory.integrableOn_univ

theorem integrableOn_zero : IntegrableOn (fun _ => (0 : E)) s Œº :=
  integrable_zero _ _ _
#align measure_theory.integrable_on_zero MeasureTheory.integrableOn_zero

@[simp]
theorem integrableOn_const {C : E} : IntegrableOn (fun _ => C) s Œº ‚Üî C = 0 ‚à® Œº s < ‚àû :=
  integrable_const_iff.trans <| by rw [Measure.restrict_apply_univ]
#align measure_theory.integrable_on_const MeasureTheory.integrableOn_const

theorem IntegrableOn.mono (h : IntegrableOn f t ŒΩ) (hs : s ‚äÜ t) (hŒº : Œº ‚â§ ŒΩ) : IntegrableOn f s Œº :=
  h.mono_measure <| Measure.restrict_mono hs hŒº
#align measure_theory.integrable_on.mono MeasureTheory.IntegrableOn.mono

theorem IntegrableOn.mono_set (h : IntegrableOn f t Œº) (hst : s ‚äÜ t) : IntegrableOn f s Œº :=
  h.mono hst le_rfl
#align measure_theory.integrable_on.mono_set MeasureTheory.IntegrableOn.mono_set

theorem IntegrableOn.mono_measure (h : IntegrableOn f s ŒΩ) (hŒº : Œº ‚â§ ŒΩ) : IntegrableOn f s Œº :=
  h.mono (Subset.refl _) hŒº
#align measure_theory.integrable_on.mono_measure MeasureTheory.IntegrableOn.mono_measure

theorem IntegrableOn.mono_set_ae (h : IntegrableOn f t Œº) (hst : s ‚â§·µê[Œº] t) : IntegrableOn f s Œº :=
  h.integrable.mono_measure <| Measure.restrict_mono_ae hst
#align measure_theory.integrable_on.mono_set_ae MeasureTheory.IntegrableOn.mono_set_ae

theorem IntegrableOn.congr_set_ae (h : IntegrableOn f t Œº) (hst : s =·µê[Œº] t) : IntegrableOn f s Œº :=
  h.mono_set_ae hst.le
#align measure_theory.integrable_on.congr_set_ae MeasureTheory.IntegrableOn.congr_set_ae

theorem IntegrableOn.congr_fun_ae (h : IntegrableOn f s Œº) (hst : f =·µê[Œº.restrict s] g) :
    IntegrableOn g s Œº :=
  Integrable.congr h hst
#align measure_theory.integrable_on.congr_fun_ae MeasureTheory.IntegrableOn.congr_fun_ae

theorem integrableOn_congr_fun_ae (hst : f =·µê[Œº.restrict s] g) :
    IntegrableOn f s Œº ‚Üî IntegrableOn g s Œº :=
  ‚ü®fun h => h.congr_fun_ae hst, fun h => h.congr_fun_ae hst.symm‚ü©
#align measure_theory.integrable_on_congr_fun_ae MeasureTheory.integrableOn_congr_fun_ae

theorem IntegrableOn.congr_fun (h : IntegrableOn f s Œº) (hst : EqOn f g s) (hs : MeasurableSet s) :
    IntegrableOn g s Œº :=
  h.congr_fun_ae ((ae_restrict_iff' hs).2 (eventually_of_forall hst))
#align measure_theory.integrable_on.congr_fun MeasureTheory.IntegrableOn.congr_fun

theorem integrableOn_congr_fun (hst : EqOn f g s) (hs : MeasurableSet s) :
    IntegrableOn f s Œº ‚Üî IntegrableOn g s Œº :=
  ‚ü®fun h => h.congr_fun hst hs, fun h => h.congr_fun hst.symm hs‚ü©
#align measure_theory.integrable_on_congr_fun MeasureTheory.integrableOn_congr_fun

theorem Integrable.integrableOn (h : Integrable f Œº) : IntegrableOn f s Œº :=
  h.mono_measure <| Measure.restrict_le_self
#align measure_theory.integrable.integrable_on MeasureTheory.Integrable.integrableOn

theorem IntegrableOn.restrict (h : IntegrableOn f s Œº) (hs : MeasurableSet s) :
    IntegrableOn f s (Œº.restrict t) := by
  rw [IntegrableOn, Measure.restrict_restrict hs]; exact h.mono_set inter_subset_left
#align measure_theory.integrable_on.restrict MeasureTheory.IntegrableOn.restrict

theorem IntegrableOn.inter_of_restrict (h : IntegrableOn f s (Œº.restrict t)) :
    IntegrableOn f (s ‚à© t) Œº := by
  have := h.mono_set (inter_subset_left (t := t))
  rwa [IntegrableOn, Œº.restrict_restrict_of_subset inter_subset_right] at this

lemma Integrable.piecewise [DecidablePred (¬∑ ‚àà s)]
    (hs : MeasurableSet s) (hf : IntegrableOn f s Œº) (hg : IntegrableOn g s·∂ú Œº) :
    Integrable (s.piecewise f g) Œº := by
  rw [IntegrableOn] at hf hg
  rw [‚Üê mem‚Ñíp_one_iff_integrable] at hf hg ‚ä¢
  exact Mem‚Ñíp.piecewise hs hf hg

theorem IntegrableOn.left_of_union (h : IntegrableOn f (s ‚à™ t) Œº) : IntegrableOn f s Œº :=
  h.mono_set subset_union_left
#align measure_theory.integrable_on.left_of_union MeasureTheory.IntegrableOn.left_of_union

theorem IntegrableOn.right_of_union (h : IntegrableOn f (s ‚à™ t) Œº) : IntegrableOn f t Œº :=
  h.mono_set subset_union_right
#align measure_theory.integrable_on.right_of_union MeasureTheory.IntegrableOn.right_of_union

theorem IntegrableOn.union (hs : IntegrableOn f s Œº) (ht : IntegrableOn f t Œº) :
    IntegrableOn f (s ‚à™ t) Œº :=
  (hs.add_measure ht).mono_measure <| Measure.restrict_union_le _ _
#align measure_theory.integrable_on.union MeasureTheory.IntegrableOn.union

@[simp]
theorem integrableOn_union : IntegrableOn f (s ‚à™ t) Œº ‚Üî IntegrableOn f s Œº ‚àß IntegrableOn f t Œº :=
  ‚ü®fun h => ‚ü®h.left_of_union, h.right_of_union‚ü©, fun h => h.1.union h.2‚ü©
#align measure_theory.integrable_on_union MeasureTheory.integrableOn_union

@[simp]
theorem integrableOn_singleton_iff {x : Œ±} [MeasurableSingletonClass Œ±] :
    IntegrableOn f {x} Œº ‚Üî f x = 0 ‚à® Œº {x} < ‚àû := by
  have : f =·µê[Œº.restrict {x}] fun _ => f x := by
    filter_upwards [ae_restrict_mem (measurableSet_singleton x)] with _ ha
    simp only [mem_singleton_iff.1 ha]
  rw [IntegrableOn, integrable_congr this, integrable_const_iff]
  simp
#align measure_theory.integrable_on_singleton_iff MeasureTheory.integrableOn_singleton_iff

@[simp]
theorem integrableOn_finite_biUnion {s : Set Œ≤} (hs : s.Finite) {t : Œ≤ ‚Üí Set Œ±} :
    IntegrableOn f (‚ãÉ i ‚àà s, t i) Œº ‚Üî ‚àÄ i ‚àà s, IntegrableOn f (t i) Œº := by
  refine hs.induction_on ?_ ?_
  ¬∑ simp
  ¬∑ intro a s _ _ hf; simp [hf, or_imp, forall_and]
#align measure_theory.integrable_on_finite_bUnion MeasureTheory.integrableOn_finite_biUnion

@[simp]
theorem integrableOn_finset_iUnion {s : Finset Œ≤} {t : Œ≤ ‚Üí Set Œ±} :
    IntegrableOn f (‚ãÉ i ‚àà s, t i) Œº ‚Üî ‚àÄ i ‚àà s, IntegrableOn f (t i) Œº :=
  integrableOn_finite_biUnion s.finite_toSet
#align measure_theory.integrable_on_finset_Union MeasureTheory.integrableOn_finset_iUnion

@[simp]
theorem integrableOn_finite_iUnion [Finite Œ≤] {t : Œ≤ ‚Üí Set Œ±} :
    IntegrableOn f (‚ãÉ i, t i) Œº ‚Üî ‚àÄ i, IntegrableOn f (t i) Œº := by
  cases nonempty_fintype Œ≤
  simpa using @integrableOn_finset_iUnion _ _ _ _ _ f Œº Finset.univ t
#align measure_theory.integrable_on_finite_Union MeasureTheory.integrableOn_finite_iUnion

theorem IntegrableOn.add_measure (hŒº : IntegrableOn f s Œº) (hŒΩ : IntegrableOn f s ŒΩ) :
    IntegrableOn f s (Œº + ŒΩ) := by
  delta IntegrableOn; rw [Measure.restrict_add]; exact hŒº.integrable.add_measure hŒΩ
#align measure_theory.integrable_on.add_measure MeasureTheory.IntegrableOn.add_measure

@[simp]
theorem integrableOn_add_measure :
    IntegrableOn f s (Œº + ŒΩ) ‚Üî IntegrableOn f s Œº ‚àß IntegrableOn f s ŒΩ :=
  ‚ü®fun h =>
    ‚ü®h.mono_measure (Measure.le_add_right le_rfl), h.mono_measure (Measure.le_add_left le_rfl)‚ü©,
    fun h => h.1.add_measure h.2‚ü©
#align measure_theory.integrable_on_add_measure MeasureTheory.integrableOn_add_measure

theorem _root_.MeasurableEmbedding.integrableOn_map_iff [MeasurableSpace Œ≤] {e : Œ± ‚Üí Œ≤}
    (he : MeasurableEmbedding e) {f : Œ≤ ‚Üí E} {Œº : Measure Œ±} {s : Set Œ≤} :
    IntegrableOn f s (Œº.map e) ‚Üî IntegrableOn (f ‚àò e) (e ‚Åª¬π' s) Œº := by
  simp_rw [IntegrableOn, he.restrict_map, he.integrable_map_iff]
#align measurable_embedding.integrable_on_map_iff MeasurableEmbedding.integrableOn_map_iff

theorem _root_.MeasurableEmbedding.integrableOn_iff_comap [MeasurableSpace Œ≤] {e : Œ± ‚Üí Œ≤}
    (he : MeasurableEmbedding e) {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} {s : Set Œ≤} (hs : s ‚äÜ range e) :
    IntegrableOn f s Œº ‚Üî IntegrableOn (f ‚àò e) (e ‚Åª¬π' s) (Œº.comap e) := by
  simp_rw [‚Üê he.integrableOn_map_iff, he.map_comap, IntegrableOn,
    Measure.restrict_restrict_of_subset hs]

theorem integrableOn_map_equiv [MeasurableSpace Œ≤] (e : Œ± ‚âÉ·µê Œ≤) {f : Œ≤ ‚Üí E} {Œº : Measure Œ±}
    {s : Set Œ≤} : IntegrableOn f s (Œº.map e) ‚Üî IntegrableOn (f ‚àò e) (e ‚Åª¬π' s) Œº := by
  simp only [IntegrableOn, e.restrict_map, integrable_map_equiv e]
#align measure_theory.integrable_on_map_equiv MeasureTheory.integrableOn_map_equiv

theorem MeasurePreserving.integrableOn_comp_preimage [MeasurableSpace Œ≤] {e : Œ± ‚Üí Œ≤} {ŒΩ}
    (h‚ÇÅ : MeasurePreserving e Œº ŒΩ) (h‚ÇÇ : MeasurableEmbedding e) {f : Œ≤ ‚Üí E} {s : Set Œ≤} :
    IntegrableOn (f ‚àò e) (e ‚Åª¬π' s) Œº ‚Üî IntegrableOn f s ŒΩ :=
  (h‚ÇÅ.restrict_preimage_emb h‚ÇÇ s).integrable_comp_emb h‚ÇÇ
#align measure_theory.measure_preserving.integrable_on_comp_preimage MeasureTheory.MeasurePreserving.integrableOn_comp_preimage

theorem MeasurePreserving.integrableOn_image [MeasurableSpace Œ≤] {e : Œ± ‚Üí Œ≤} {ŒΩ}
    (h‚ÇÅ : MeasurePreserving e Œº ŒΩ) (h‚ÇÇ : MeasurableEmbedding e) {f : Œ≤ ‚Üí E} {s : Set Œ±} :
    IntegrableOn f (e '' s) ŒΩ ‚Üî IntegrableOn (f ‚àò e) s Œº :=
  ((h‚ÇÅ.restrict_image_emb h‚ÇÇ s).integrable_comp_emb h‚ÇÇ).symm
#align measure_theory.measure_preserving.integrable_on_image MeasureTheory.MeasurePreserving.integrableOn_image

theorem integrable_indicator_iff (hs : MeasurableSet s) :
    Integrable (indicator s f) Œº ‚Üî IntegrableOn f s Œº := by
  simp [IntegrableOn, Integrable, HasFiniteIntegral, nnnorm_indicator_eq_indicator_nnnorm,
    ENNReal.coe_indicator, lintegral_indicator _ hs, aestronglyMeasurable_indicator_iff hs]
#align measure_theory.integrable_indicator_iff MeasureTheory.integrable_indicator_iff

theorem IntegrableOn.integrable_indicator (h : IntegrableOn f s Œº) (hs : MeasurableSet s) :
    Integrable (indicator s f) Œº :=
  (integrable_indicator_iff hs).2 h
#align measure_theory.integrable_on.integrable_indicator MeasureTheory.IntegrableOn.integrable_indicator

theorem Integrable.indicator (h : Integrable f Œº) (hs : MeasurableSet s) :
    Integrable (indicator s f) Œº :=
  h.integrableOn.integrable_indicator hs
#align measure_theory.integrable.indicator MeasureTheory.Integrable.indicator

theorem IntegrableOn.indicator (h : IntegrableOn f s Œº) (ht : MeasurableSet t) :
    IntegrableOn (indicator t f) s Œº :=
  Integrable.indicator h ht
#align measure_theory.integrable_on.indicator MeasureTheory.IntegrableOn.indicator

theorem integrable_indicatorConstLp {E} [NormedAddCommGroup E] {p : ‚Ñù‚â•0‚àû} {s : Set Œ±}
    (hs : MeasurableSet s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
    Integrable (indicatorConstLp p hs hŒºs c) Œº := by
  rw [integrable_congr indicatorConstLp_coeFn, integrable_indicator_iff hs, IntegrableOn,
    integrable_const_iff, lt_top_iff_ne_top]
  right
  simpa only [Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply] using hŒºs
set_option linter.uppercaseLean3 false in
#align measure_theory.integrable_indicator_const_Lp MeasureTheory.integrable_indicatorConstLp

/-- If a function is integrable on a set `s` and nonzero there, then the measurable hull of `s` is
well behaved: the restriction of the measure to `toMeasurable Œº s` coincides with its restriction
to `s`. -/
theorem IntegrableOn.restrict_toMeasurable (hf : IntegrableOn f s Œº) (h's : ‚àÄ x ‚àà s, f x ‚â† 0) :
    Œº.restrict (toMeasurable Œº s) = Œº.restrict s := by
  rcases exists_seq_strictAnti_tendsto (0 : ‚Ñù) with ‚ü®u, _, u_pos, u_lim‚ü©
  let v n := toMeasurable (Œº.restrict s) { x | u n ‚â§ ‚Äñf x‚Äñ }
  have A : ‚àÄ n, Œº (s ‚à© v n) ‚â† ‚àû := by
    intro n
    rw [inter_comm, ‚Üê Measure.restrict_apply (measurableSet_toMeasurable _ _),
      measure_toMeasurable]
    exact (hf.measure_norm_ge_lt_top (u_pos n)).ne
  apply Measure.restrict_toMeasurable_of_cover _ A
  intro x hx
  have : 0 < ‚Äñf x‚Äñ := by simp only [h's x hx, norm_pos_iff, Ne, not_false_iff]
  obtain ‚ü®n, hn‚ü© : ‚àÉ n, u n < ‚Äñf x‚Äñ := ((tendsto_order.1 u_lim).2 _ this).exists
  exact mem_iUnion.2 ‚ü®n, subset_toMeasurable _ _ hn.le‚ü©
#align measure_theory.integrable_on.restrict_to_measurable MeasureTheory.IntegrableOn.restrict_toMeasurable

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is null-measurable. -/
theorem IntegrableOn.of_ae_diff_eq_zero (hf : IntegrableOn f s Œº) (ht : NullMeasurableSet t Œº)
    (h't : ‚àÄ·µê x ‚àÇŒº, x ‚àà t \ s ‚Üí f x = 0) : IntegrableOn f t Œº := by
  let u := { x ‚àà s | f x ‚â† 0 }
  have hu : IntegrableOn f u Œº := hf.mono_set fun x hx => hx.1
  let v := toMeasurable Œº u
  have A : IntegrableOn f v Œº := by
    rw [IntegrableOn, hu.restrict_toMeasurable]
    ¬∑ exact hu
    ¬∑ intro x hx; exact hx.2
  have B : IntegrableOn f (t \ v) Œº := by
    apply integrableOn_zero.congr
    filter_upwards [ae_restrict_of_ae h't,
      ae_restrict_mem‚ÇÄ (ht.diff (measurableSet_toMeasurable Œº u).nullMeasurableSet)] with x hxt hx
    by_cases h'x : x ‚àà s
    ¬∑ by_contra H
      exact hx.2 (subset_toMeasurable Œº u ‚ü®h'x, Ne.symm H‚ü©)
    ¬∑ exact (hxt ‚ü®hx.1, h'x‚ü©).symm
  apply (A.union B).mono_set _
  rw [union_diff_self]
  exact subset_union_right
#align measure_theory.integrable_on.of_ae_diff_eq_zero MeasureTheory.IntegrableOn.of_ae_diff_eq_zero

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is measurable. -/
theorem IntegrableOn.of_forall_diff_eq_zero (hf : IntegrableOn f s Œº) (ht : MeasurableSet t)
    (h't : ‚àÄ x ‚àà t \ s, f x = 0) : IntegrableOn f t Œº :=
  hf.of_ae_diff_eq_zero ht.nullMeasurableSet (eventually_of_forall h't)
#align measure_theory.integrable_on.of_forall_diff_eq_zero MeasureTheory.IntegrableOn.of_forall_diff_eq_zero

/-- If a function is integrable on a set `s` and vanishes almost everywhere on its complement,
then it is integrable. -/
theorem IntegrableOn.integrable_of_ae_not_mem_eq_zero (hf : IntegrableOn f s Œº)
    (h't : ‚àÄ·µê x ‚àÇŒº, x ‚àâ s ‚Üí f x = 0) : Integrable f Œº := by
  rw [‚Üê integrableOn_univ]
  apply hf.of_ae_diff_eq_zero nullMeasurableSet_univ
  filter_upwards [h't] with x hx h'x using hx h'x.2
#align measure_theory.integrable_on.integrable_of_ae_not_mem_eq_zero MeasureTheory.IntegrableOn.integrable_of_ae_not_mem_eq_zero

/-- If a function is integrable on a set `s` and vanishes everywhere on its complement,
then it is integrable. -/
theorem IntegrableOn.integrable_of_forall_not_mem_eq_zero (hf : IntegrableOn f s Œº)
    (h't : ‚àÄ x, x ‚àâ s ‚Üí f x = 0) : Integrable f Œº :=
  hf.integrable_of_ae_not_mem_eq_zero (eventually_of_forall fun x hx => h't x hx)
#align measure_theory.integrable_on.integrable_of_forall_not_mem_eq_zero MeasureTheory.IntegrableOn.integrable_of_forall_not_mem_eq_zero

theorem integrableOn_iff_integrable_of_support_subset (h1s : support f ‚äÜ s) :
    IntegrableOn f s Œº ‚Üî Integrable f Œº := by
  refine ‚ü®fun h => ?_, fun h => h.integrableOn‚ü©
  refine h.integrable_of_forall_not_mem_eq_zero fun x hx => ?_
  contrapose! hx
  exact h1s (mem_support.2 hx)
#align measure_theory.integrable_on_iff_integrable_of_support_subset MeasureTheory.integrableOn_iff_integrable_of_support_subset

theorem integrableOn_Lp_of_measure_ne_top {E} [NormedAddCommGroup E] {p : ‚Ñù‚â•0‚àû} {s : Set Œ±}
    (f : Lp E p Œº) (hp : 1 ‚â§ p) (hŒºs : Œº s ‚â† ‚àû) : IntegrableOn f s Œº := by
  refine mem‚Ñíp_one_iff_integrable.mp ?_
  have hŒº_restrict_univ : (Œº.restrict s) Set.univ < ‚àû := by
    simpa only [Set.univ_inter, MeasurableSet.univ, Measure.restrict_apply, lt_top_iff_ne_top]
  haveI hŒº_finite : IsFiniteMeasure (Œº.restrict s) := ‚ü®hŒº_restrict_univ‚ü©
  exact ((Lp.mem‚Ñíp _).restrict s).mem‚Ñíp_of_exponent_le hp
set_option linter.uppercaseLean3 false in
#align measure_theory.integrable_on_Lp_of_measure_ne_top MeasureTheory.integrableOn_Lp_of_measure_ne_top

theorem Integrable.lintegral_lt_top {f : Œ± ‚Üí ‚Ñù} (hf : Integrable f Œº) :
    (‚à´‚Åª x, ENNReal.ofReal (f x) ‚àÇŒº) < ‚àû :=
  calc
    (‚à´‚Åª x, ENNReal.ofReal (f x) ‚àÇŒº) ‚â§ ‚à´‚Åª x, ‚Üë‚Äñf x‚Äñ‚Çä ‚àÇŒº := lintegral_ofReal_le_lintegral_nnnorm f
    _ < ‚àû := hf.2
#align measure_theory.integrable.lintegral_lt_top MeasureTheory.Integrable.lintegral_lt_top

theorem IntegrableOn.set_lintegral_lt_top {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} (hf : IntegrableOn f s Œº) :
    (‚à´‚Åª x in s, ENNReal.ofReal (f x) ‚àÇŒº) < ‚àû :=
  Integrable.lintegral_lt_top hf
#align measure_theory.integrable_on.set_lintegral_lt_top MeasureTheory.IntegrableOn.set_lintegral_lt_top

/-- We say that a function `f` is *integrable at filter* `l` if it is integrable on some
set `s ‚àà l`. Equivalently, it is eventually integrable on `s` in `l.smallSets`. -/
def IntegrableAtFilter (f : Œ± ‚Üí E) (l : Filter Œ±) (Œº : Measure Œ± := by volume_tac) :=
  ‚àÉ s ‚àà l, IntegrableOn f s Œº
#align measure_theory.integrable_at_filter MeasureTheory.IntegrableAtFilter

variable {l l' : Filter Œ±}

theorem _root_.MeasurableEmbedding.integrableAtFilter_map_iff [MeasurableSpace Œ≤] {e : Œ± ‚Üí Œ≤}
    (he : MeasurableEmbedding e) {f : Œ≤ ‚Üí E} :
    IntegrableAtFilter f (l.map e) (Œº.map e) ‚Üî IntegrableAtFilter (f ‚àò e) l Œº := by
  simp_rw [IntegrableAtFilter, he.integrableOn_map_iff]
  constructor <;> rintro ‚ü®s, hs‚ü©
  ¬∑ exact ‚ü®_, hs‚ü©
  ¬∑ exact ‚ü®e '' s, by rwa [mem_map, he.injective.preimage_image]‚ü©

theorem _root_.MeasurableEmbedding.integrableAtFilter_iff_comap [MeasurableSpace Œ≤] {e : Œ± ‚Üí Œ≤}
    (he : MeasurableEmbedding e) {f : Œ≤ ‚Üí E} {Œº : Measure Œ≤} :
    IntegrableAtFilter f (l.map e) Œº ‚Üî IntegrableAtFilter (f ‚àò e) l (Œº.comap e) := by
  simp_rw [‚Üê he.integrableAtFilter_map_iff, IntegrableAtFilter, he.map_comap]
  constructor <;> rintro ‚ü®s, hs, int‚ü©
  ¬∑ exact ‚ü®s, hs, int.mono_measure <| Œº.restrict_le_self‚ü©
  ¬∑ exact ‚ü®_, inter_mem hs range_mem_map, int.inter_of_restrict‚ü©

theorem Integrable.integrableAtFilter (h : Integrable f Œº) (l : Filter Œ±) :
    IntegrableAtFilter f l Œº :=
  ‚ü®univ, Filter.univ_mem, integrableOn_univ.2 h‚ü©
#align measure_theory.integrable.integrable_at_filter MeasureTheory.Integrable.integrableAtFilter

protected theorem IntegrableAtFilter.eventually (h : IntegrableAtFilter f l Œº) :
    ‚àÄ·∂† s in l.smallSets, IntegrableOn f s Œº :=
  Iff.mpr (eventually_smallSets' fun _s _t hst ht => ht.mono_set hst) h
#align measure_theory.integrable_at_filter.eventually MeasureTheory.IntegrableAtFilter.eventually

protected theorem IntegrableAtFilter.add {f g : Œ± ‚Üí E}
    (hf : IntegrableAtFilter f l Œº) (hg : IntegrableAtFilter g l Œº) :
    IntegrableAtFilter (f + g) l Œº := by
  rcases hf with ‚ü®s, sl, hs‚ü©
  rcases hg with ‚ü®t, tl, ht‚ü©
  refine ‚ü®s ‚à© t, inter_mem sl tl, ?_‚ü©
  exact (hs.mono_set inter_subset_left).add (ht.mono_set inter_subset_right)

protected theorem IntegrableAtFilter.neg {f : Œ± ‚Üí E} (hf : IntegrableAtFilter f l Œº) :
    IntegrableAtFilter (-f) l Œº := by
  rcases hf with ‚ü®s, sl, hs‚ü©
  exact ‚ü®s, sl, hs.neg‚ü©

protected theorem IntegrableAtFilter.sub {f g : Œ± ‚Üí E}
    (hf : IntegrableAtFilter f l Œº) (hg : IntegrableAtFilter g l Œº) :
    IntegrableAtFilter (f - g) l Œº := by
  rw [sub_eq_add_neg]
  exact hf.add hg.neg

protected theorem IntegrableAtFilter.smul {ùïú : Type*} [NormedAddCommGroup ùïú] [SMulZeroClass ùïú E]
    [BoundedSMul ùïú E] {f : Œ± ‚Üí E} (hf : IntegrableAtFilter f l Œº) (c : ùïú) :
    IntegrableAtFilter (c ‚Ä¢ f) l Œº := by
  rcases hf with ‚ü®s, sl, hs‚ü©
  exact ‚ü®s, sl, hs.smul c‚ü©

protected theorem IntegrableAtFilter.norm (hf : IntegrableAtFilter f l Œº) :
    IntegrableAtFilter (fun x => ‚Äñf x‚Äñ) l Œº :=
  Exists.casesOn hf fun s hs ‚Ü¶ ‚ü®s, hs.1, hs.2.norm‚ü©

theorem IntegrableAtFilter.filter_mono (hl : l ‚â§ l') (hl' : IntegrableAtFilter f l' Œº) :
    IntegrableAtFilter f l Œº :=
  let ‚ü®s, hs, hsf‚ü© := hl'
  ‚ü®s, hl hs, hsf‚ü©
#align measure_theory.integrable_at_filter.filter_mono MeasureTheory.IntegrableAtFilter.filter_mono

theorem IntegrableAtFilter.inf_of_left (hl : IntegrableAtFilter f l Œº) :
    IntegrableAtFilter f (l ‚äì l') Œº :=
  hl.filter_mono inf_le_left
#align measure_theory.integrable_at_filter.inf_of_left MeasureTheory.IntegrableAtFilter.inf_of_left

theorem IntegrableAtFilter.inf_of_right (hl : IntegrableAtFilter f l Œº) :
    IntegrableAtFilter f (l' ‚äì l) Œº :=
  hl.filter_mono inf_le_right
#align measure_theory.integrable_at_filter.inf_of_right MeasureTheory.IntegrableAtFilter.inf_of_right

@[simp]
theorem IntegrableAtFilter.inf_ae_iff {l : Filter Œ±} :
    IntegrableAtFilter f (l ‚äì ae Œº) Œº ‚Üî IntegrableAtFilter f l Œº := by
  refine ‚ü®?_, fun h ‚Ü¶ h.filter_mono inf_le_left‚ü©
  rintro ‚ü®s, ‚ü®t, ht, u, hu, rfl‚ü©, hf‚ü©
  refine ‚ü®t, ht, hf.congr_set_ae <| eventuallyEq_set.2 ?_‚ü©
  filter_upwards [hu] with x hx using (and_iff_left hx).symm
#align measure_theory.integrable_at_filter.inf_ae_iff MeasureTheory.IntegrableAtFilter.inf_ae_iff

alias ‚ü®IntegrableAtFilter.of_inf_ae, _‚ü© := IntegrableAtFilter.inf_ae_iff
#align measure_theory.integrable_at_filter.of_inf_ae MeasureTheory.IntegrableAtFilter.of_inf_ae

@[simp]
theorem integrableAtFilter_top : IntegrableAtFilter f ‚ä§ Œº ‚Üî Integrable f Œº := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ h.integrableAtFilter ‚ä§‚ü©
  obtain ‚ü®s, hsf, hs‚ü© := h
  exact (integrableOn_iff_integrable_of_support_subset fun _ _ ‚Ü¶ hsf _).mp hs

theorem IntegrableAtFilter.sup_iff {l l' : Filter Œ±} :
    IntegrableAtFilter f (l ‚äî l') Œº ‚Üî IntegrableAtFilter f l Œº ‚àß IntegrableAtFilter f l' Œº := by
  constructor
  ¬∑ exact fun h => ‚ü®h.filter_mono le_sup_left, h.filter_mono le_sup_right‚ü©
  ¬∑ exact fun ‚ü®‚ü®s, hsl, hs‚ü©, ‚ü®t, htl, ht‚ü©‚ü© ‚Ü¶ ‚ü®s ‚à™ t, union_mem_sup hsl htl, hs.union ht‚ü©

/-- If `Œº` is a measure finite at filter `l` and `f` is a function such that its norm is bounded
above at `l`, then `f` is integrable at `l`. -/
theorem Measure.FiniteAtFilter.integrableAtFilter {l : Filter Œ±} [IsMeasurablyGenerated l]
    (hfm : StronglyMeasurableAtFilter f l Œº) (hŒº : Œº.FiniteAtFilter l)
    (hf : l.IsBoundedUnder (¬∑ ‚â§ ¬∑) (norm ‚àò f)) : IntegrableAtFilter f l Œº := by
  obtain ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ·∂† s in l.smallSets, ‚àÄ x ‚àà s, ‚Äñf x‚Äñ ‚â§ C :=
    hf.imp fun C hC => eventually_smallSets.2 ‚ü®_, hC, fun t => id‚ü©
  rcases (hfm.eventually.and (hŒº.eventually.and hC)).exists_measurable_mem_of_smallSets with
    ‚ü®s, hsl, hsm, hfm, hŒº, hC‚ü©
  refine ‚ü®s, hsl, ‚ü®hfm, hasFiniteIntegral_restrict_of_bounded hŒº (C := C) ?_‚ü©‚ü©
  rw [ae_restrict_eq hsm, eventually_inf_principal]
  exact eventually_of_forall hC
#align measure_theory.measure.finite_at_filter.integrable_at_filter MeasureTheory.Measure.FiniteAtFilter.integrableAtFilter

theorem Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae {l : Filter Œ±}
    [IsMeasurablyGenerated l] (hfm : StronglyMeasurableAtFilter f l Œº) (hŒº : Œº.FiniteAtFilter l) {b}
    (hf : Tendsto f (l ‚äì ae Œº) (ùìù b)) : IntegrableAtFilter f l Œº :=
  (hŒº.inf_of_left.integrableAtFilter (hfm.filter_mono inf_le_left)
      hf.norm.isBoundedUnder_le).of_inf_ae
#align measure_theory.measure.finite_at_filter.integrable_at_filter_of_tendsto_ae MeasureTheory.Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae

alias _root_.Filter.Tendsto.integrableAtFilter_ae :=
  Measure.FiniteAtFilter.integrableAtFilter_of_tendsto_ae
#align filter.tendsto.integrable_at_filter_ae Filter.Tendsto.integrableAtFilter_ae

theorem Measure.FiniteAtFilter.integrableAtFilter_of_tendsto {l : Filter Œ±}
    [IsMeasurablyGenerated l] (hfm : StronglyMeasurableAtFilter f l Œº) (hŒº : Œº.FiniteAtFilter l) {b}
    (hf : Tendsto f l (ùìù b)) : IntegrableAtFilter f l Œº :=
  hŒº.integrableAtFilter hfm hf.norm.isBoundedUnder_le
#align measure_theory.measure.finite_at_filter.integrable_at_filter_of_tendsto MeasureTheory.Measure.FiniteAtFilter.integrableAtFilter_of_tendsto

alias _root_.Filter.Tendsto.integrableAtFilter :=
  Measure.FiniteAtFilter.integrableAtFilter_of_tendsto
#align filter.tendsto.integrable_at_filter Filter.Tendsto.integrableAtFilter

lemma Measure.integrableOn_of_bounded (s_finite : Œº s ‚â† ‚àû) (f_mble : AEStronglyMeasurable f Œº)
    {M : ‚Ñù} (f_bdd : ‚àÄ·µê a ‚àÇ(Œº.restrict s), ‚Äñf a‚Äñ ‚â§ M) :
    IntegrableOn f s Œº :=
  ‚ü®f_mble.restrict, hasFiniteIntegral_restrict_of_bounded (C := M) s_finite.lt_top f_bdd‚ü©

theorem integrable_add_of_disjoint {f g : Œ± ‚Üí E} (h : Disjoint (support f) (support g))
    (hf : StronglyMeasurable f) (hg : StronglyMeasurable g) :
    Integrable (f + g) Œº ‚Üî Integrable f Œº ‚àß Integrable g Œº := by
  refine ‚ü®fun hfg => ‚ü®?_, ?_‚ü©, fun h => h.1.add h.2‚ü©
  ¬∑ rw [‚Üê indicator_add_eq_left h]; exact hfg.indicator hf.measurableSet_support
  ¬∑ rw [‚Üê indicator_add_eq_right h]; exact hfg.indicator hg.measurableSet_support
#align measure_theory.integrable_add_of_disjoint MeasureTheory.integrable_add_of_disjoint

/-- If a function converges along a filter to a limit `a`, is integrable along this filter, and
all elements of the filter have infinite measure, then the limit has to vanish. -/
lemma IntegrableAtFilter.eq_zero_of_tendsto
    (h : IntegrableAtFilter f l Œº) (h' : ‚àÄ s ‚àà l, Œº s = ‚àû) {a : E}
    (hf : Tendsto f l (ùìù a)) : a = 0 := by
  by_contra H
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ (Œµ : ‚Ñù), 0 < Œµ ‚àß Œµ < ‚Äña‚Äñ := exists_between (norm_pos_iff'.mpr H)
  rcases h with ‚ü®u, ul, hu‚ü©
  let v := u ‚à© {b | Œµ < ‚Äñf b‚Äñ}
  have hv : IntegrableOn f v Œº := hu.mono_set inter_subset_left
  have vl : v ‚àà l := inter_mem ul ((tendsto_order.1 hf.norm).1 _ hŒµ)
  have : Œº.restrict v v < ‚àû := lt_of_le_of_lt (measure_mono inter_subset_right)
    (Integrable.measure_gt_lt_top hv.norm Œµpos)
  have : Œº v ‚â† ‚àû := ne_of_lt (by simpa only [Measure.restrict_apply_self])
  exact this (h' v vl)

end NormedAddCommGroup

end MeasureTheory

open MeasureTheory

variable [NormedAddCommGroup E]

/-- A function which is continuous on a set `s` is almost everywhere measurable with respect to
`Œº.restrict s`. -/
theorem ContinuousOn.aemeasurable [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±] [MeasurableSpace Œ≤]
    [TopologicalSpace Œ≤] [BorelSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {Œº : Measure Œ±}
    (hf : ContinuousOn f s) (hs : MeasurableSet s) : AEMeasurable f (Œº.restrict s) := by
  nontriviality Œ±; inhabit Œ±
  have : (Set.piecewise s f fun _ => f default) =·µê[Œº.restrict s] f := piecewise_ae_eq_restrict hs
  refine ‚ü®Set.piecewise s f fun _ => f default, ?_, this.symm‚ü©
  apply measurable_of_isOpen
  intro t ht
  obtain ‚ü®u, u_open, hu‚ü© : ‚àÉ u : Set Œ±, IsOpen u ‚àß f ‚Åª¬π' t ‚à© s = u ‚à© s :=
    _root_.continuousOn_iff'.1 hf t ht
  rw [piecewise_preimage, Set.ite, hu]
  exact (u_open.measurableSet.inter hs).union ((measurable_const ht.measurableSet).diff hs)
#align continuous_on.ae_measurable ContinuousOn.aemeasurable

/-- A function which is continuous on a separable set `s` is almost everywhere strongly measurable
with respect to `Œº.restrict s`. -/
theorem ContinuousOn.aestronglyMeasurable_of_isSeparable [TopologicalSpace Œ±]
    [PseudoMetrizableSpace Œ±] [OpensMeasurableSpace Œ±] [TopologicalSpace Œ≤]
    [PseudoMetrizableSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {Œº : Measure Œ±} (hf : ContinuousOn f s)
    (hs : MeasurableSet s) (h's : TopologicalSpace.IsSeparable s) :
    AEStronglyMeasurable f (Œº.restrict s) := by
  letI := pseudoMetrizableSpacePseudoMetric Œ±
  borelize Œ≤
  rw [aestronglyMeasurable_iff_aemeasurable_separable]
  refine ‚ü®hf.aemeasurable hs, f '' s, hf.isSeparable_image h's, ?_‚ü©
  exact mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)
#align continuous_on.ae_strongly_measurable_of_is_separable ContinuousOn.aestronglyMeasurable_of_isSeparable

/-- A function which is continuous on a set `s` is almost everywhere strongly measurable with
respect to `Œº.restrict s` when either the source space or the target space is second-countable. -/
theorem ContinuousOn.aestronglyMeasurable [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [h : SecondCountableTopologyEither Œ± Œ≤] [OpensMeasurableSpace Œ±] [PseudoMetrizableSpace Œ≤]
    {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {Œº : Measure Œ±} (hf : ContinuousOn f s) (hs : MeasurableSet s) :
    AEStronglyMeasurable f (Œº.restrict s) := by
  borelize Œ≤
  refine
    aestronglyMeasurable_iff_aemeasurable_separable.2
      ‚ü®hf.aemeasurable hs, f '' s, ?_,
        mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)‚ü©
  cases h.out
  ¬∑ rw [image_eq_range]
    exact isSeparable_range <| continuousOn_iff_continuous_restrict.1 hf
  ¬∑ exact .of_separableSpace _
#align continuous_on.ae_strongly_measurable ContinuousOn.aestronglyMeasurable

/-- A function which is continuous on a compact set `s` is almost everywhere strongly measurable
with respect to `Œº.restrict s`. -/
theorem ContinuousOn.aestronglyMeasurable_of_isCompact [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {Œº : Measure Œ±}
    (hf : ContinuousOn f s) (hs : IsCompact s) (h's : MeasurableSet s) :
    AEStronglyMeasurable f (Œº.restrict s) := by
  letI := pseudoMetrizableSpacePseudoMetric Œ≤
  borelize Œ≤
  rw [aestronglyMeasurable_iff_aemeasurable_separable]
  refine ‚ü®hf.aemeasurable h's, f '' s, ?_, ?_‚ü©
  ¬∑ exact (hs.image_of_continuousOn hf).isSeparable
  ¬∑ exact mem_of_superset (self_mem_ae_restrict h's) (subset_preimage_image _ _)
#align continuous_on.ae_strongly_measurable_of_is_compact ContinuousOn.aestronglyMeasurable_of_isCompact

theorem ContinuousOn.integrableAt_nhdsWithin_of_isSeparable [TopologicalSpace Œ±]
    [PseudoMetrizableSpace Œ±] [OpensMeasurableSpace Œ±] {Œº : Measure Œ±} [IsLocallyFiniteMeasure Œº]
    {a : Œ±} {t : Set Œ±} {f : Œ± ‚Üí E} (hft : ContinuousOn f t) (ht : MeasurableSet t)
    (h't : TopologicalSpace.IsSeparable t) (ha : a ‚àà t) : IntegrableAtFilter f (ùìù[t] a) Œº :=
  haveI : (ùìù[t] a).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hft a ha).integrableAtFilter
    ‚ü®_, self_mem_nhdsWithin, hft.aestronglyMeasurable_of_isSeparable ht h't‚ü©
    (Œº.finiteAt_nhdsWithin _ _)
#align continuous_on.integrable_at_nhds_within_of_is_separable ContinuousOn.integrableAt_nhdsWithin_of_isSeparable

theorem ContinuousOn.integrableAt_nhdsWithin [TopologicalSpace Œ±]
    [SecondCountableTopologyEither Œ± E] [OpensMeasurableSpace Œ±] {Œº : Measure Œ±}
    [IsLocallyFiniteMeasure Œº] {a : Œ±} {t : Set Œ±} {f : Œ± ‚Üí E} (hft : ContinuousOn f t)
    (ht : MeasurableSet t) (ha : a ‚àà t) : IntegrableAtFilter f (ùìù[t] a) Œº :=
  haveI : (ùìù[t] a).IsMeasurablyGenerated := ht.nhdsWithin_isMeasurablyGenerated _
  (hft a ha).integrableAtFilter ‚ü®_, self_mem_nhdsWithin, hft.aestronglyMeasurable ht‚ü©
    (Œº.finiteAt_nhdsWithin _ _)
#align continuous_on.integrable_at_nhds_within ContinuousOn.integrableAt_nhdsWithin

theorem Continuous.integrableAt_nhds [TopologicalSpace Œ±] [SecondCountableTopologyEither Œ± E]
    [OpensMeasurableSpace Œ±] {Œº : Measure Œ±} [IsLocallyFiniteMeasure Œº] {f : Œ± ‚Üí E}
    (hf : Continuous f) (a : Œ±) : IntegrableAtFilter f (ùìù a) Œº := by
  rw [‚Üê nhdsWithin_univ]
  exact hf.continuousOn.integrableAt_nhdsWithin MeasurableSet.univ (mem_univ a)
#align continuous.integrable_at_nhds Continuous.integrableAt_nhds

/-- If a function is continuous on an open set `s`, then it is strongly measurable at the filter
`ùìù x` for all `x ‚àà s` if either the source space or the target space is second-countable. -/
theorem ContinuousOn.stronglyMeasurableAtFilter [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [SecondCountableTopologyEither Œ± Œ≤] {f : Œ± ‚Üí Œ≤}
    {s : Set Œ±} {Œº : Measure Œ±} (hs : IsOpen s) (hf : ContinuousOn f s) :
    ‚àÄ x ‚àà s, StronglyMeasurableAtFilter f (ùìù x) Œº := fun _x hx =>
  ‚ü®s, IsOpen.mem_nhds hs hx, hf.aestronglyMeasurable hs.measurableSet‚ü©
#align continuous_on.strongly_measurable_at_filter ContinuousOn.stronglyMeasurableAtFilter

theorem ContinuousAt.stronglyMeasurableAtFilter [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    [SecondCountableTopologyEither Œ± E] {f : Œ± ‚Üí E} {s : Set Œ±} {Œº : Measure Œ±} (hs : IsOpen s)
    (hf : ‚àÄ x ‚àà s, ContinuousAt f x) : ‚àÄ x ‚àà s, StronglyMeasurableAtFilter f (ùìù x) Œº :=
  ContinuousOn.stronglyMeasurableAtFilter hs <| ContinuousAt.continuousOn hf
#align continuous_at.strongly_measurable_at_filter ContinuousAt.stronglyMeasurableAtFilter

theorem Continuous.stronglyMeasurableAtFilter [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤] [SecondCountableTopologyEither Œ± Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Continuous f) (Œº : Measure Œ±) (l : Filter Œ±) : StronglyMeasurableAtFilter f l Œº :=
  hf.stronglyMeasurable.stronglyMeasurableAtFilter
#align continuous.strongly_measurable_at_filter Continuous.stronglyMeasurableAtFilter

/-- If a function is continuous on a measurable set `s`, then it is measurable at the filter
  `ùìù[s] x` for all `x`. -/
theorem ContinuousOn.stronglyMeasurableAtFilter_nhdsWithin {Œ± Œ≤ : Type*} [MeasurableSpace Œ±]
    [TopologicalSpace Œ±] [OpensMeasurableSpace Œ±] [TopologicalSpace Œ≤] [PseudoMetrizableSpace Œ≤]
    [SecondCountableTopologyEither Œ± Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {Œº : Measure Œ±}
    (hf : ContinuousOn f s) (hs : MeasurableSet s) (x : Œ±) :
    StronglyMeasurableAtFilter f (ùìù[s] x) Œº :=
  ‚ü®s, self_mem_nhdsWithin, hf.aestronglyMeasurable hs‚ü©
#align continuous_on.strongly_measurable_at_filter_nhds_within ContinuousOn.stronglyMeasurableAtFilter_nhdsWithin

/-! ### Lemmas about adding and removing interval boundaries

The primed lemmas take explicit arguments about the measure being finite at the endpoint, while
the unprimed ones use `[NoAtoms Œº]`.
-/


section PartialOrder

variable [PartialOrder Œ±] [MeasurableSingletonClass Œ±] {f : Œ± ‚Üí E} {Œº : Measure Œ±} {a b : Œ±}

theorem integrableOn_Icc_iff_integrableOn_Ioc' (ha : Œº {a} ‚â† ‚àû) :
    IntegrableOn f (Icc a b) Œº ‚Üî IntegrableOn f (Ioc a b) Œº := by
  by_cases hab : a ‚â§ b
  ¬∑ rw [‚Üê Ioc_union_left hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr ha.lt_top), and_true_iff]
  ¬∑ rw [Icc_eq_empty hab, Ioc_eq_empty]
    contrapose! hab
    exact hab.le
#align integrable_on_Icc_iff_integrable_on_Ioc' integrableOn_Icc_iff_integrableOn_Ioc'

theorem integrableOn_Icc_iff_integrableOn_Ico' (hb : Œº {b} ‚â† ‚àû) :
    IntegrableOn f (Icc a b) Œº ‚Üî IntegrableOn f (Ico a b) Œº := by
  by_cases hab : a ‚â§ b
  ¬∑ rw [‚Üê Ico_union_right hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
  ¬∑ rw [Icc_eq_empty hab, Ico_eq_empty]
    contrapose! hab
    exact hab.le
#align integrable_on_Icc_iff_integrable_on_Ico' integrableOn_Icc_iff_integrableOn_Ico'

theorem integrableOn_Ico_iff_integrableOn_Ioo' (ha : Œº {a} ‚â† ‚àû) :
    IntegrableOn f (Ico a b) Œº ‚Üî IntegrableOn f (Ioo a b) Œº := by
  by_cases hab : a < b
  ¬∑ rw [‚Üê Ioo_union_left hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr ha.lt_top), and_true_iff]
  ¬∑ rw [Ioo_eq_empty hab, Ico_eq_empty hab]
#align integrable_on_Ico_iff_integrable_on_Ioo' integrableOn_Ico_iff_integrableOn_Ioo'

theorem integrableOn_Ioc_iff_integrableOn_Ioo' (hb : Œº {b} ‚â† ‚àû) :
    IntegrableOn f (Ioc a b) Œº ‚Üî IntegrableOn f (Ioo a b) Œº := by
  by_cases hab : a < b
  ¬∑ rw [‚Üê Ioo_union_right hab, integrableOn_union,
      eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
  ¬∑ rw [Ioo_eq_empty hab, Ioc_eq_empty hab]
#align integrable_on_Ioc_iff_integrable_on_Ioo' integrableOn_Ioc_iff_integrableOn_Ioo'

theorem integrableOn_Icc_iff_integrableOn_Ioo' (ha : Œº {a} ‚â† ‚àû) (hb : Œº {b} ‚â† ‚àû) :
    IntegrableOn f (Icc a b) Œº ‚Üî IntegrableOn f (Ioo a b) Œº := by
  rw [integrableOn_Icc_iff_integrableOn_Ioc' ha, integrableOn_Ioc_iff_integrableOn_Ioo' hb]
#align integrable_on_Icc_iff_integrable_on_Ioo' integrableOn_Icc_iff_integrableOn_Ioo'

theorem integrableOn_Ici_iff_integrableOn_Ioi' (hb : Œº {b} ‚â† ‚àû) :
    IntegrableOn f (Ici b) Œº ‚Üî IntegrableOn f (Ioi b) Œº := by
  rw [‚Üê Ioi_union_left, integrableOn_union,
    eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
#align integrable_on_Ici_iff_integrable_on_Ioi' integrableOn_Ici_iff_integrableOn_Ioi'

theorem integrableOn_Iic_iff_integrableOn_Iio' (hb : Œº {b} ‚â† ‚àû) :
    IntegrableOn f (Iic b) Œº ‚Üî IntegrableOn f (Iio b) Œº := by
  rw [‚Üê Iio_union_right, integrableOn_union,
    eq_true (integrableOn_singleton_iff.mpr <| Or.inr hb.lt_top), and_true_iff]
#align integrable_on_Iic_iff_integrable_on_Iio' integrableOn_Iic_iff_integrableOn_Iio'

variable [NoAtoms Œº]

theorem integrableOn_Icc_iff_integrableOn_Ioc :
    IntegrableOn f (Icc a b) Œº ‚Üî IntegrableOn f (Ioc a b) Œº :=
  integrableOn_Icc_iff_integrableOn_Ioc' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Icc_iff_integrable_on_Ioc integrableOn_Icc_iff_integrableOn_Ioc

theorem integrableOn_Icc_iff_integrableOn_Ico :
    IntegrableOn f (Icc a b) Œº ‚Üî IntegrableOn f (Ico a b) Œº :=
  integrableOn_Icc_iff_integrableOn_Ico' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Icc_iff_integrable_on_Ico integrableOn_Icc_iff_integrableOn_Ico

theorem integrableOn_Ico_iff_integrableOn_Ioo :
    IntegrableOn f (Ico a b) Œº ‚Üî IntegrableOn f (Ioo a b) Œº :=
  integrableOn_Ico_iff_integrableOn_Ioo' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Ico_iff_integrable_on_Ioo integrableOn_Ico_iff_integrableOn_Ioo

theorem integrableOn_Ioc_iff_integrableOn_Ioo :
    IntegrableOn f (Ioc a b) Œº ‚Üî IntegrableOn f (Ioo a b) Œº :=
  integrableOn_Ioc_iff_integrableOn_Ioo' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Ioc_iff_integrable_on_Ioo integrableOn_Ioc_iff_integrableOn_Ioo

theorem integrableOn_Icc_iff_integrableOn_Ioo :
    IntegrableOn f (Icc a b) Œº ‚Üî IntegrableOn f (Ioo a b) Œº := by
  rw [integrableOn_Icc_iff_integrableOn_Ioc, integrableOn_Ioc_iff_integrableOn_Ioo]
#align integrable_on_Icc_iff_integrable_on_Ioo integrableOn_Icc_iff_integrableOn_Ioo

theorem integrableOn_Ici_iff_integrableOn_Ioi :
    IntegrableOn f (Ici b) Œº ‚Üî IntegrableOn f (Ioi b) Œº :=
  integrableOn_Ici_iff_integrableOn_Ioi' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Ici_iff_integrable_on_Ioi integrableOn_Ici_iff_integrableOn_Ioi

theorem integrableOn_Iic_iff_integrableOn_Iio :
    IntegrableOn f (Iic b) Œº ‚Üî IntegrableOn f (Iio b) Œº :=
  integrableOn_Iic_iff_integrableOn_Iio' (by rw [measure_singleton]; exact ENNReal.zero_ne_top)
#align integrable_on_Iic_iff_integrable_on_Iio integrableOn_Iic_iff_integrableOn_Iio

end PartialOrder
