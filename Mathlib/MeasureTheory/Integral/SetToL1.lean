/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, SÃ©bastien GouÃ«zel, RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.SimpleFuncDenseLp

#align_import measure_theory.integral.set_to_l1 from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Extension of a linear function from indicators to L1

Let `T : Set Î± â†’ E â†’L[â„] F` be additive for measurable sets with finite measure, in the sense that
for `s, t` two such sets, `s âˆ© t = âˆ… â†’ T (s âˆª t) = T s + T t`. `T` is akin to a bilinear map on
`Set Î± Ã— E`, or a linear map on indicator functions.

This file constructs an extension of `T` to integrable simple functions, which are finite sums of
indicators of measurable sets with finite measure, then to integrable functions, which are limits of
integrable simple functions.

The main result is a continuous linear map `(Î± â†’â‚[Î¼] E) â†’L[â„] F`. This extension process is used to
define the Bochner integral in the `MeasureTheory.Integral.Bochner` file and the conditional
expectation of an integrable function in `MeasureTheory.Function.ConditionalExpectation`.

## Main Definitions

- `FinMeasAdditive Î¼ T`: the property that `T` is additive on measurable sets with finite measure.
  For two such sets, `s âˆ© t = âˆ… â†’ T (s âˆª t) = T s + T t`.
- `DominatedFinMeasAdditive Î¼ T C`: `FinMeasAdditive Î¼ T âˆ§ âˆ€ s, â€–T sâ€– â‰¤ C * (Î¼ s).toReal`.
  This is the property needed to perform the extension from indicators to L1.
- `setToL1 (hT : DominatedFinMeasAdditive Î¼ T C) : (Î± â†’â‚[Î¼] E) â†’L[â„] F`: the extension of `T`
  from indicators to L1.
- `setToFun Î¼ T (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’ E) : F`: a version of the
  extension which applies to functions (with value 0 if the function is not integrable).

## Properties

For most properties of `setToFun`, we provide two lemmas. One version uses hypotheses valid on
all sets, like `T = T'`, and a second version which uses a primed name uses hypotheses on
measurable sets with finite measure, like `âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s`.

The lemmas listed here don't show all hypotheses. Refer to the actual lemmas for details.

Linearity:
- `setToFun_zero_left : setToFun Î¼ 0 hT f = 0`
- `setToFun_add_left : setToFun Î¼ (T + T') _ f = setToFun Î¼ T hT f + setToFun Î¼ T' hT' f`
- `setToFun_smul_left : setToFun Î¼ (fun s â†¦ c â€¢ (T s)) (hT.smul c) f = c â€¢ setToFun Î¼ T hT f`
- `setToFun_zero : setToFun Î¼ T hT (0 : Î± â†’ E) = 0`
- `setToFun_neg : setToFun Î¼ T hT (-f) = - setToFun Î¼ T hT f`
If `f` and `g` are integrable:
- `setToFun_add : setToFun Î¼ T hT (f + g) = setToFun Î¼ T hT f + setToFun Î¼ T hT g`
- `setToFun_sub : setToFun Î¼ T hT (f - g) = setToFun Î¼ T hT f - setToFun Î¼ T hT g`
If `T` is verifies `âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x`:
- `setToFun_smul : setToFun Î¼ T hT (c â€¢ f) = c â€¢ setToFun Î¼ T hT f`

Other:
- `setToFun_congr_ae (h : f =áµ[Î¼] g) : setToFun Î¼ T hT f = setToFun Î¼ T hT g`
- `setToFun_measure_zero (h : Î¼ = 0) : setToFun Î¼ T hT f = 0`

If the space is a `NormedLatticeAddCommGroup` and `T` is such that `0 â‰¤ T s x` for `0 â‰¤ x`, we
also prove order-related properties:
- `setToFun_mono_left (h : âˆ€ s x, T s x â‰¤ T' s x) : setToFun Î¼ T hT f â‰¤ setToFun Î¼ T' hT' f`
- `setToFun_nonneg (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ setToFun Î¼ T hT f`
- `setToFun_mono (hfg : f â‰¤áµ[Î¼] g) : setToFun Î¼ T hT f â‰¤ setToFun Î¼ T hT g`

## Implementation notes

The starting object `T : Set Î± â†’ E â†’L[â„] F` matters only through its restriction on measurable sets
with finite measure. Its value on other sets is ignored.
-/


noncomputable section

open scoped Classical Topology NNReal ENNReal MeasureTheory Pointwise

open Set Filter TopologicalSpace ENNReal EMetric

namespace MeasureTheory

variable {Î± E F F' G ğ•œ : Type*} {p : â„â‰¥0âˆ} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [NormedAddCommGroup F'] [NormedSpace â„ F']
  [NormedAddCommGroup G] {m : MeasurableSpace Î±} {Î¼ : Measure Î±}

local infixr:25 " â†’â‚› " => SimpleFunc

open Finset

section FinMeasAdditive

/-- A set function is `FinMeasAdditive` if its value on the union of two disjoint measurable
sets with finite measure is the sum of its values on each set. -/
def FinMeasAdditive {Î²} [AddMonoid Î²] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±) (T : Set Î± â†’ Î²) :
    Prop :=
  âˆ€ s t, MeasurableSet s â†’ MeasurableSet t â†’ Î¼ s â‰  âˆ â†’ Î¼ t â‰  âˆ â†’ s âˆ© t = âˆ… â†’ T (s âˆª t) = T s + T t
#align measure_theory.fin_meas_additive MeasureTheory.FinMeasAdditive

namespace FinMeasAdditive

variable {Î² : Type*} [AddCommMonoid Î²] {T T' : Set Î± â†’ Î²}

theorem zero : FinMeasAdditive Î¼ (0 : Set Î± â†’ Î²) := fun s t _ _ _ _ _ => by simp
#align measure_theory.fin_meas_additive.zero MeasureTheory.FinMeasAdditive.zero

theorem add (hT : FinMeasAdditive Î¼ T) (hT' : FinMeasAdditive Î¼ T') :
    FinMeasAdditive Î¼ (T + T') := by
  intro s t hs ht hÎ¼s hÎ¼t hst
  simp only [hT s t hs ht hÎ¼s hÎ¼t hst, hT' s t hs ht hÎ¼s hÎ¼t hst, Pi.add_apply]
  abel
#align measure_theory.fin_meas_additive.add MeasureTheory.FinMeasAdditive.add

theorem smul [Monoid ğ•œ] [DistribMulAction ğ•œ Î²] (hT : FinMeasAdditive Î¼ T) (c : ğ•œ) :
    FinMeasAdditive Î¼ fun s => c â€¢ T s := fun s t hs ht hÎ¼s hÎ¼t hst => by
  simp [hT s t hs ht hÎ¼s hÎ¼t hst]
#align measure_theory.fin_meas_additive.smul MeasureTheory.FinMeasAdditive.smul

theorem of_eq_top_imp_eq_top {Î¼' : Measure Î±} (h : âˆ€ s, MeasurableSet s â†’ Î¼ s = âˆ â†’ Î¼' s = âˆ)
    (hT : FinMeasAdditive Î¼ T) : FinMeasAdditive Î¼' T := fun s t hs ht hÎ¼'s hÎ¼'t hst =>
  hT s t hs ht (mt (h s hs) hÎ¼'s) (mt (h t ht) hÎ¼'t) hst
#align measure_theory.fin_meas_additive.of_eq_top_imp_eq_top MeasureTheory.FinMeasAdditive.of_eq_top_imp_eq_top

theorem of_smul_measure (c : â„â‰¥0âˆ) (hc_ne_top : c â‰  âˆ) (hT : FinMeasAdditive (c â€¢ Î¼) T) :
    FinMeasAdditive Î¼ T := by
  refine of_eq_top_imp_eq_top (fun s _ hÎ¼s => ?_) hT
  rw [Measure.smul_apply, smul_eq_mul, ENNReal.mul_eq_top] at hÎ¼s
  simp only [hc_ne_top, or_false_iff, Ne, false_and_iff] at hÎ¼s
  exact hÎ¼s.2
#align measure_theory.fin_meas_additive.of_smul_measure MeasureTheory.FinMeasAdditive.of_smul_measure

theorem smul_measure (c : â„â‰¥0âˆ) (hc_ne_zero : c â‰  0) (hT : FinMeasAdditive Î¼ T) :
    FinMeasAdditive (c â€¢ Î¼) T := by
  refine of_eq_top_imp_eq_top (fun s _ hÎ¼s => ?_) hT
  rw [Measure.smul_apply, smul_eq_mul, ENNReal.mul_eq_top]
  simp only [hc_ne_zero, true_and_iff, Ne, not_false_iff]
  exact Or.inl hÎ¼s
#align measure_theory.fin_meas_additive.smul_measure MeasureTheory.FinMeasAdditive.smul_measure

theorem smul_measure_iff (c : â„â‰¥0âˆ) (hc_ne_zero : c â‰  0) (hc_ne_top : c â‰  âˆ) :
    FinMeasAdditive (c â€¢ Î¼) T â†” FinMeasAdditive Î¼ T :=
  âŸ¨fun hT => of_smul_measure c hc_ne_top hT, fun hT => smul_measure c hc_ne_zero hTâŸ©
#align measure_theory.fin_meas_additive.smul_measure_iff MeasureTheory.FinMeasAdditive.smul_measure_iff

theorem map_empty_eq_zero {Î²} [AddCancelMonoid Î²] {T : Set Î± â†’ Î²} (hT : FinMeasAdditive Î¼ T) :
    T âˆ… = 0 := by
  have h_empty : Î¼ âˆ… â‰  âˆ := (measure_empty.le.trans_lt ENNReal.coe_lt_top).ne
  specialize hT âˆ… âˆ… MeasurableSet.empty MeasurableSet.empty h_empty h_empty (Set.inter_empty âˆ…)
  rw [Set.union_empty] at hT
  nth_rw 1 [â† add_zero (T âˆ…)] at hT
  exact (add_left_cancel hT).symm
#align measure_theory.fin_meas_additive.map_empty_eq_zero MeasureTheory.FinMeasAdditive.map_empty_eq_zero

theorem map_iUnion_fin_meas_set_eq_sum (T : Set Î± â†’ Î²) (T_empty : T âˆ… = 0)
    (h_add : FinMeasAdditive Î¼ T) {Î¹} (S : Î¹ â†’ Set Î±) (sÎ¹ : Finset Î¹)
    (hS_meas : âˆ€ i, MeasurableSet (S i)) (hSp : âˆ€ i âˆˆ sÎ¹, Î¼ (S i) â‰  âˆ)
    (h_disj : âˆ€áµ‰ (i âˆˆ sÎ¹) (j âˆˆ sÎ¹), i â‰  j â†’ Disjoint (S i) (S j)) :
    T (â‹ƒ i âˆˆ sÎ¹, S i) = âˆ‘ i âˆˆ sÎ¹, T (S i) := by
  revert hSp h_disj
  refine Finset.induction_on sÎ¹ ?_ ?_
  Â· simp only [Finset.not_mem_empty, IsEmpty.forall_iff, iUnion_false, iUnion_empty, sum_empty,
      forallâ‚‚_true_iff, imp_true_iff, forall_true_left, not_false_iff, T_empty]
  intro a s has h hps h_disj
  rw [Finset.sum_insert has, â† h]
  swap; Â· exact fun i hi => hps i (Finset.mem_insert_of_mem hi)
  swap;
  Â· exact fun i hi j hj hij =>
      h_disj i (Finset.mem_insert_of_mem hi) j (Finset.mem_insert_of_mem hj) hij
  rw [â†
    h_add (S a) (â‹ƒ i âˆˆ s, S i) (hS_meas a) (measurableSet_biUnion _ fun i _ => hS_meas i)
      (hps a (Finset.mem_insert_self a s))]
  Â· congr; convert Finset.iSup_insert a s S
  Â· exact
      ((measure_biUnion_finset_le _ _).trans_lt <|
          ENNReal.sum_lt_top fun i hi => hps i <| Finset.mem_insert_of_mem hi).ne
  Â· simp_rw [Set.inter_iUnion]
    refine iUnion_eq_empty.mpr fun i => iUnion_eq_empty.mpr fun hi => ?_
    rw [â† Set.disjoint_iff_inter_eq_empty]
    refine h_disj a (Finset.mem_insert_self a s) i (Finset.mem_insert_of_mem hi) fun hai => ?_
    rw [â† hai] at hi
    exact has hi
#align measure_theory.fin_meas_additive.map_Union_fin_meas_set_eq_sum MeasureTheory.FinMeasAdditive.map_iUnion_fin_meas_set_eq_sum

end FinMeasAdditive

/-- A `FinMeasAdditive` set function whose norm on every set is less than the measure of the
set (up to a multiplicative constant). -/
def DominatedFinMeasAdditive {Î²} [SeminormedAddCommGroup Î²] {_ : MeasurableSpace Î±} (Î¼ : Measure Î±)
    (T : Set Î± â†’ Î²) (C : â„) : Prop :=
  FinMeasAdditive Î¼ T âˆ§ âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * (Î¼ s).toReal
#align measure_theory.dominated_fin_meas_additive MeasureTheory.DominatedFinMeasAdditive

namespace DominatedFinMeasAdditive

variable {Î² : Type*} [SeminormedAddCommGroup Î²] {T T' : Set Î± â†’ Î²} {C C' : â„}

theorem zero {m : MeasurableSpace Î±} (Î¼ : Measure Î±) (hC : 0 â‰¤ C) :
    DominatedFinMeasAdditive Î¼ (0 : Set Î± â†’ Î²) C := by
  refine âŸ¨FinMeasAdditive.zero, fun s _ _ => ?_âŸ©
  rw [Pi.zero_apply, norm_zero]
  exact mul_nonneg hC toReal_nonneg
#align measure_theory.dominated_fin_meas_additive.zero MeasureTheory.DominatedFinMeasAdditive.zero

theorem eq_zero_of_measure_zero {Î² : Type*} [NormedAddCommGroup Î²] {T : Set Î± â†’ Î²} {C : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) {s : Set Î±} (hs : MeasurableSet s) (hs_zero : Î¼ s = 0) :
    T s = 0 := by
  refine norm_eq_zero.mp ?_
  refine ((hT.2 s hs (by simp [hs_zero])).trans (le_of_eq ?_)).antisymm (norm_nonneg _)
  rw [hs_zero, ENNReal.zero_toReal, mul_zero]
#align measure_theory.dominated_fin_meas_additive.eq_zero_of_measure_zero MeasureTheory.DominatedFinMeasAdditive.eq_zero_of_measure_zero

theorem eq_zero {Î² : Type*} [NormedAddCommGroup Î²] {T : Set Î± â†’ Î²} {C : â„} {m : MeasurableSpace Î±}
    (hT : DominatedFinMeasAdditive (0 : Measure Î±) T C) {s : Set Î±} (hs : MeasurableSet s) :
    T s = 0 :=
  eq_zero_of_measure_zero hT hs (by simp only [Measure.coe_zero, Pi.zero_apply])
#align measure_theory.dominated_fin_meas_additive.eq_zero MeasureTheory.DominatedFinMeasAdditive.eq_zero

theorem add (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C') :
    DominatedFinMeasAdditive Î¼ (T + T') (C + C') := by
  refine âŸ¨hT.1.add hT'.1, fun s hs hÎ¼s => ?_âŸ©
  rw [Pi.add_apply, add_mul]
  exact (norm_add_le _ _).trans (add_le_add (hT.2 s hs hÎ¼s) (hT'.2 s hs hÎ¼s))
#align measure_theory.dominated_fin_meas_additive.add MeasureTheory.DominatedFinMeasAdditive.add

theorem smul [NormedField ğ•œ] [NormedSpace ğ•œ Î²] (hT : DominatedFinMeasAdditive Î¼ T C) (c : ğ•œ) :
    DominatedFinMeasAdditive Î¼ (fun s => c â€¢ T s) (â€–câ€– * C) := by
  refine âŸ¨hT.1.smul c, fun s hs hÎ¼s => ?_âŸ©
  dsimp only
  rw [norm_smul, mul_assoc]
  exact mul_le_mul le_rfl (hT.2 s hs hÎ¼s) (norm_nonneg _) (norm_nonneg _)
#align measure_theory.dominated_fin_meas_additive.smul MeasureTheory.DominatedFinMeasAdditive.smul

theorem of_measure_le {Î¼' : Measure Î±} (h : Î¼ â‰¤ Î¼') (hT : DominatedFinMeasAdditive Î¼ T C)
    (hC : 0 â‰¤ C) : DominatedFinMeasAdditive Î¼' T C := by
  have h' : âˆ€ s, Î¼ s = âˆ â†’ Î¼' s = âˆ := fun s hs â†¦ top_unique <| hs.symm.trans_le (h _)
  refine âŸ¨hT.1.of_eq_top_imp_eq_top fun s _ â†¦ h' s, fun s hs hÎ¼'s â†¦ ?_âŸ©
  have hÎ¼s : Î¼ s < âˆ := (h s).trans_lt hÎ¼'s
  calc
    â€–T sâ€– â‰¤ C * (Î¼ s).toReal := hT.2 s hs hÎ¼s
    _ â‰¤ C * (Î¼' s).toReal := by gcongr; exacts [hÎ¼'s.ne, h _]
#align measure_theory.dominated_fin_meas_additive.of_measure_le MeasureTheory.DominatedFinMeasAdditive.of_measure_le

theorem add_measure_right {_ : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±)
    (hT : DominatedFinMeasAdditive Î¼ T C) (hC : 0 â‰¤ C) : DominatedFinMeasAdditive (Î¼ + Î½) T C :=
  of_measure_le (Measure.le_add_right le_rfl) hT hC
#align measure_theory.dominated_fin_meas_additive.add_measure_right MeasureTheory.DominatedFinMeasAdditive.add_measure_right

theorem add_measure_left {_ : MeasurableSpace Î±} (Î¼ Î½ : Measure Î±)
    (hT : DominatedFinMeasAdditive Î½ T C) (hC : 0 â‰¤ C) : DominatedFinMeasAdditive (Î¼ + Î½) T C :=
  of_measure_le (Measure.le_add_left le_rfl) hT hC
#align measure_theory.dominated_fin_meas_additive.add_measure_left MeasureTheory.DominatedFinMeasAdditive.add_measure_left

theorem of_smul_measure (c : â„â‰¥0âˆ) (hc_ne_top : c â‰  âˆ) (hT : DominatedFinMeasAdditive (c â€¢ Î¼) T C) :
    DominatedFinMeasAdditive Î¼ T (c.toReal * C) := by
  have h : âˆ€ s, MeasurableSet s â†’ c â€¢ Î¼ s = âˆ â†’ Î¼ s = âˆ := by
    intro s _ hcÎ¼s
    simp only [hc_ne_top, Algebra.id.smul_eq_mul, ENNReal.mul_eq_top, or_false_iff, Ne,
      false_and_iff] at hcÎ¼s
    exact hcÎ¼s.2
  refine âŸ¨hT.1.of_eq_top_imp_eq_top (Î¼ := c â€¢ Î¼) h, fun s hs hÎ¼s => ?_âŸ©
  have hcÎ¼s : c â€¢ Î¼ s â‰  âˆ := mt (h s hs) hÎ¼s.ne
  rw [smul_eq_mul] at hcÎ¼s
  simp_rw [DominatedFinMeasAdditive, Measure.smul_apply, smul_eq_mul, toReal_mul] at hT
  refine (hT.2 s hs hcÎ¼s.lt_top).trans (le_of_eq ?_)
  ring
#align measure_theory.dominated_fin_meas_additive.of_smul_measure MeasureTheory.DominatedFinMeasAdditive.of_smul_measure

theorem of_measure_le_smul {Î¼' : Measure Î±} (c : â„â‰¥0âˆ) (hc : c â‰  âˆ) (h : Î¼ â‰¤ c â€¢ Î¼')
    (hT : DominatedFinMeasAdditive Î¼ T C) (hC : 0 â‰¤ C) :
    DominatedFinMeasAdditive Î¼' T (c.toReal * C) :=
  (hT.of_measure_le h hC).of_smul_measure c hc
#align measure_theory.dominated_fin_meas_additive.of_measure_le_smul MeasureTheory.DominatedFinMeasAdditive.of_measure_le_smul

end DominatedFinMeasAdditive

end FinMeasAdditive

namespace SimpleFunc

/-- Extend `Set Î± â†’ (F â†’L[â„] F')` to `(Î± â†’â‚› F) â†’ F'`. -/
def setToSimpleFunc {_ : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') (f : Î± â†’â‚› F) : F' :=
  âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) x
#align measure_theory.simple_func.set_to_simple_func MeasureTheory.SimpleFunc.setToSimpleFunc

@[simp]
theorem setToSimpleFunc_zero {m : MeasurableSpace Î±} (f : Î± â†’â‚› F) :
    setToSimpleFunc (0 : Set Î± â†’ F â†’L[â„] F') f = 0 := by simp [setToSimpleFunc]
#align measure_theory.simple_func.set_to_simple_func_zero MeasureTheory.SimpleFunc.setToSimpleFunc_zero

theorem setToSimpleFunc_zero' {T : Set Î± â†’ E â†’L[â„] F'}
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0) (f : Î± â†’â‚› E) (hf : Integrable f Î¼) :
    setToSimpleFunc T f = 0 := by
  simp_rw [setToSimpleFunc]
  refine sum_eq_zero fun x _ => ?_
  by_cases hx0 : x = 0
  Â· simp [hx0]
  rw [h_zero (f â»Â¹' ({x} : Set E)) (measurableSet_fiber _ _)
      (measure_preimage_lt_top_of_integrable f hf hx0),
    ContinuousLinearMap.zero_apply]
#align measure_theory.simple_func.set_to_simple_func_zero' MeasureTheory.SimpleFunc.setToSimpleFunc_zero'

@[simp]
theorem setToSimpleFunc_zero_apply {m : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') :
    setToSimpleFunc T (0 : Î± â†’â‚› F) = 0 := by
  cases isEmpty_or_nonempty Î± <;> simp [setToSimpleFunc]
#align measure_theory.simple_func.set_to_simple_func_zero_apply MeasureTheory.SimpleFunc.setToSimpleFunc_zero_apply

theorem setToSimpleFunc_eq_sum_filter {m : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F')
    (f : Î± â†’â‚› F) :
    setToSimpleFunc T f = âˆ‘ x âˆˆ f.range.filter fun x => x â‰  0, (T (f â»Â¹' {x})) x := by
  symm
  refine sum_filter_of_ne fun x _ => mt fun hx0 => ?_
  rw [hx0]
  exact ContinuousLinearMap.map_zero _
#align measure_theory.simple_func.set_to_simple_func_eq_sum_filter MeasureTheory.SimpleFunc.setToSimpleFunc_eq_sum_filter

theorem map_setToSimpleFunc (T : Set Î± â†’ F â†’L[â„] F') (h_add : FinMeasAdditive Î¼ T) {f : Î± â†’â‚› G}
    (hf : Integrable f Î¼) {g : G â†’ F} (hg : g 0 = 0) :
    (f.map g).setToSimpleFunc T = âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) (g x) := by
  have T_empty : T âˆ… = 0 := h_add.map_empty_eq_zero
  have hfp : âˆ€ x âˆˆ f.range, x â‰  0 â†’ Î¼ (f â»Â¹' {x}) â‰  âˆ := fun x _ hx0 =>
    (measure_preimage_lt_top_of_integrable f hf hx0).ne
  simp only [setToSimpleFunc, range_map]
  refine Finset.sum_image' _ fun b hb => ?_
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©
  by_cases h0 : g (f a) = 0
  Â· simp_rw [h0]
    rw [ContinuousLinearMap.map_zero, Finset.sum_eq_zero fun x hx => ?_]
    rw [mem_filter] at hx
    rw [hx.2, ContinuousLinearMap.map_zero]
  have h_left_eq :
    T (map g f â»Â¹' {g (f a)}) (g (f a)) =
      T (f â»Â¹' (f.range.filter fun b => g b = g (f a))) (g (f a)) := by
    congr; rw [map_preimage_singleton]
  rw [h_left_eq]
  have h_left_eq' :
    T (f â»Â¹' (filter (fun b : G => g b = g (f a)) f.range)) (g (f a)) =
      T (â‹ƒ y âˆˆ filter (fun b : G => g b = g (f a)) f.range, f â»Â¹' {y}) (g (f a)) := by
    congr; rw [â† Finset.set_biUnion_preimage_singleton]
  rw [h_left_eq']
  rw [h_add.map_iUnion_fin_meas_set_eq_sum T T_empty]
  Â· simp only [sum_apply, ContinuousLinearMap.coe_sum']
    refine Finset.sum_congr rfl fun x hx => ?_
    rw [mem_filter] at hx
    rw [hx.2]
  Â· exact fun i => measurableSet_fiber _ _
  Â· intro i hi
    rw [mem_filter] at hi
    refine hfp i hi.1 fun hi0 => ?_
    rw [hi0, hg] at hi
    exact h0 hi.2.symm
  Â· intro i _j hi _ hij
    rw [Set.disjoint_iff]
    intro x hx
    rw [Set.mem_inter_iff, Set.mem_preimage, Set.mem_preimage, Set.mem_singleton_iff,
      Set.mem_singleton_iff] at hx
    rw [â† hx.1, â† hx.2] at hij
    exact absurd rfl hij
#align measure_theory.simple_func.map_set_to_simple_func MeasureTheory.SimpleFunc.map_setToSimpleFunc

theorem setToSimpleFunc_congr' (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼)
    (h : Pairwise fun x y => T (f â»Â¹' {x} âˆ© g â»Â¹' {y}) = 0) :
    f.setToSimpleFunc T = g.setToSimpleFunc T :=
  show ((pair f g).map Prod.fst).setToSimpleFunc T = ((pair f g).map Prod.snd).setToSimpleFunc T by
    have h_pair : Integrable (f.pair g) Î¼ := integrable_pair hf hg
    rw [map_setToSimpleFunc T h_add h_pair Prod.fst_zero]
    rw [map_setToSimpleFunc T h_add h_pair Prod.snd_zero]
    refine Finset.sum_congr rfl fun p hp => ?_
    rcases mem_range.1 hp with âŸ¨a, rflâŸ©
    by_cases eq : f a = g a
    Â· dsimp only [pair_apply]; rw [eq]
    Â· have : T (pair f g â»Â¹' {(f a, g a)}) = 0 := by
        have h_eq : T ((â‡‘(f.pair g)) â»Â¹' {(f a, g a)}) = T (f â»Â¹' {f a} âˆ© g â»Â¹' {g a}) := by
          congr; rw [pair_preimage_singleton f g]
        rw [h_eq]
        exact h eq
      simp only [this, ContinuousLinearMap.zero_apply, pair_apply]
#align measure_theory.simple_func.set_to_simple_func_congr' MeasureTheory.SimpleFunc.setToSimpleFunc_congr'

theorem setToSimpleFunc_congr (T : Set Î± â†’ E â†’L[â„] F)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (h : f =áµ[Î¼] g) : f.setToSimpleFunc T = g.setToSimpleFunc T := by
  refine setToSimpleFunc_congr' T h_add hf ((integrable_congr h).mp hf) ?_
  refine fun x y hxy => h_zero _ ((measurableSet_fiber f x).inter (measurableSet_fiber g y)) ?_
  rw [EventuallyEq, ae_iff] at h
  refine measure_mono_null (fun z => ?_) h
  simp_rw [Set.mem_inter_iff, Set.mem_setOf_eq, Set.mem_preimage, Set.mem_singleton_iff]
  intro h
  rwa [h.1, h.2]
#align measure_theory.simple_func.set_to_simple_func_congr MeasureTheory.SimpleFunc.setToSimpleFunc_congr

theorem setToSimpleFunc_congr_left (T T' : Set Î± â†’ E â†’L[â„] F)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s) (f : Î± â†’â‚› E) (hf : Integrable f Î¼) :
    setToSimpleFunc T f = setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc]
  refine sum_congr rfl fun x _ => ?_
  by_cases hx0 : x = 0
  Â· simp [hx0]
  Â· rw [h (f â»Â¹' {x}) (SimpleFunc.measurableSet_fiber _ _)
        (SimpleFunc.measure_preimage_lt_top_of_integrable _ hf hx0)]
#align measure_theory.simple_func.set_to_simple_func_congr_left MeasureTheory.SimpleFunc.setToSimpleFunc_congr_left

theorem setToSimpleFunc_add_left {m : MeasurableSpace Î±} (T T' : Set Î± â†’ F â†’L[â„] F') {f : Î± â†’â‚› F} :
    setToSimpleFunc (T + T') f = setToSimpleFunc T f + setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc, Pi.add_apply]
  push_cast
  simp_rw [Pi.add_apply, sum_add_distrib]
#align measure_theory.simple_func.set_to_simple_func_add_left MeasureTheory.SimpleFunc.setToSimpleFunc_add_left

theorem setToSimpleFunc_add_left' (T T' T'' : Set Î± â†’ E â†’L[â„] F)
    (h_add : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T'' s = T s + T' s) {f : Î± â†’â‚› E}
    (hf : Integrable f Î¼) : setToSimpleFunc T'' f = setToSimpleFunc T f + setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc_eq_sum_filter]
  suffices
    âˆ€ x âˆˆ filter (fun x : E => x â‰  0) f.range, T'' (f â»Â¹' {x}) = T (f â»Â¹' {x}) + T' (f â»Â¹' {x}) by
    rw [â† sum_add_distrib]
    refine Finset.sum_congr rfl fun x hx => ?_
    rw [this x hx]
    push_cast
    rw [Pi.add_apply]
  intro x hx
  refine
    h_add (f â»Â¹' {x}) (measurableSet_preimage _ _) (measure_preimage_lt_top_of_integrable _ hf ?_)
  rw [mem_filter] at hx
  exact hx.2
#align measure_theory.simple_func.set_to_simple_func_add_left' MeasureTheory.SimpleFunc.setToSimpleFunc_add_left'

theorem setToSimpleFunc_smul_left {m : MeasurableSpace Î±} (T : Set Î± â†’ F â†’L[â„] F') (c : â„)
    (f : Î± â†’â‚› F) : setToSimpleFunc (fun s => c â€¢ T s) f = c â€¢ setToSimpleFunc T f := by
  simp_rw [setToSimpleFunc, ContinuousLinearMap.smul_apply, smul_sum]
#align measure_theory.simple_func.set_to_simple_func_smul_left MeasureTheory.SimpleFunc.setToSimpleFunc_smul_left

theorem setToSimpleFunc_smul_left' (T T' : Set Î± â†’ E â†’L[â„] F') (c : â„)
    (h_smul : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T' s = c â€¢ T s) {f : Î± â†’â‚› E} (hf : Integrable f Î¼) :
    setToSimpleFunc T' f = c â€¢ setToSimpleFunc T f := by
  simp_rw [setToSimpleFunc_eq_sum_filter]
  suffices âˆ€ x âˆˆ filter (fun x : E => x â‰  0) f.range, T' (f â»Â¹' {x}) = c â€¢ T (f â»Â¹' {x}) by
    rw [smul_sum]
    refine Finset.sum_congr rfl fun x hx => ?_
    rw [this x hx]
    rfl
  intro x hx
  refine
    h_smul (f â»Â¹' {x}) (measurableSet_preimage _ _) (measure_preimage_lt_top_of_integrable _ hf ?_)
  rw [mem_filter] at hx
  exact hx.2
#align measure_theory.simple_func.set_to_simple_func_smul_left' MeasureTheory.SimpleFunc.setToSimpleFunc_smul_left'

theorem setToSimpleFunc_add (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    setToSimpleFunc T (f + g) = setToSimpleFunc T f + setToSimpleFunc T g :=
  have hp_pair : Integrable (f.pair g) Î¼ := integrable_pair hf hg
  calc
    setToSimpleFunc T (f + g) = âˆ‘ x âˆˆ (pair f g).range, T (pair f g â»Â¹' {x}) (x.fst + x.snd) := by
      rw [add_eq_mapâ‚‚, map_setToSimpleFunc T h_add hp_pair]; simp
    _ = âˆ‘ x âˆˆ (pair f g).range, (T (pair f g â»Â¹' {x}) x.fst + T (pair f g â»Â¹' {x}) x.snd) :=
      (Finset.sum_congr rfl fun a _ => ContinuousLinearMap.map_add _ _ _)
    _ = (âˆ‘ x âˆˆ (pair f g).range, T (pair f g â»Â¹' {x}) x.fst) +
          âˆ‘ x âˆˆ (pair f g).range, T (pair f g â»Â¹' {x}) x.snd := by
      rw [Finset.sum_add_distrib]
    _ = ((pair f g).map Prod.fst).setToSimpleFunc T +
          ((pair f g).map Prod.snd).setToSimpleFunc T := by
      rw [map_setToSimpleFunc T h_add hp_pair Prod.snd_zero,
        map_setToSimpleFunc T h_add hp_pair Prod.fst_zero]
#align measure_theory.simple_func.set_to_simple_func_add MeasureTheory.SimpleFunc.setToSimpleFunc_add

theorem setToSimpleFunc_neg (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f : Î± â†’â‚› E}
    (hf : Integrable f Î¼) : setToSimpleFunc T (-f) = -setToSimpleFunc T f :=
  calc
    setToSimpleFunc T (-f) = setToSimpleFunc T (f.map Neg.neg) := rfl
    _ = -setToSimpleFunc T f := by
      rw [map_setToSimpleFunc T h_add hf neg_zero, setToSimpleFunc, â† sum_neg_distrib]
      exact Finset.sum_congr rfl fun x _ => ContinuousLinearMap.map_neg _ _
#align measure_theory.simple_func.set_to_simple_func_neg MeasureTheory.SimpleFunc.setToSimpleFunc_neg

theorem setToSimpleFunc_sub (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚› E}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    setToSimpleFunc T (f - g) = setToSimpleFunc T f - setToSimpleFunc T g := by
  rw [sub_eq_add_neg, setToSimpleFunc_add T h_add hf, setToSimpleFunc_neg T h_add hg,
    sub_eq_add_neg]
  rw [integrable_iff] at hg âŠ¢
  intro x hx_ne
  change Î¼ (Neg.neg âˆ˜ g â»Â¹' {x}) < âˆ
  rw [preimage_comp, neg_preimage, Set.neg_singleton]
  refine hg (-x) ?_
  simp [hx_ne]
#align measure_theory.simple_func.set_to_simple_func_sub MeasureTheory.SimpleFunc.setToSimpleFunc_sub

theorem setToSimpleFunc_smul_real (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T) (c : â„)
    {f : Î± â†’â‚› E} (hf : Integrable f Î¼) : setToSimpleFunc T (c â€¢ f) = c â€¢ setToSimpleFunc T f :=
  calc
    setToSimpleFunc T (c â€¢ f) = âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) (c â€¢ x) := by
      rw [smul_eq_map c f, map_setToSimpleFunc T h_add hf]; dsimp only; rw [smul_zero]
    _ = âˆ‘ x âˆˆ f.range, c â€¢ T (f â»Â¹' {x}) x :=
      (Finset.sum_congr rfl fun b _ => by rw [ContinuousLinearMap.map_smul (T (f â»Â¹' {b})) c b])
    _ = c â€¢ setToSimpleFunc T f := by simp only [setToSimpleFunc, smul_sum, smul_smul, mul_comm]
#align measure_theory.simple_func.set_to_simple_func_smul_real MeasureTheory.SimpleFunc.setToSimpleFunc_smul_real

theorem setToSimpleFunc_smul {E} [NormedAddCommGroup E] [NormedField ğ•œ] [NormedSpace ğ•œ E]
    [NormedSpace â„ E] [NormedSpace ğ•œ F] (T : Set Î± â†’ E â†’L[â„] F) (h_add : FinMeasAdditive Î¼ T)
    (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ) {f : Î± â†’â‚› E} (hf : Integrable f Î¼) :
    setToSimpleFunc T (c â€¢ f) = c â€¢ setToSimpleFunc T f :=
  calc
    setToSimpleFunc T (c â€¢ f) = âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) (c â€¢ x) := by
      rw [smul_eq_map c f, map_setToSimpleFunc T h_add hf]; dsimp only; rw [smul_zero]
    _ = âˆ‘ x âˆˆ f.range, c â€¢ T (f â»Â¹' {x}) x := Finset.sum_congr rfl fun b _ => by rw [h_smul]
    _ = c â€¢ setToSimpleFunc T f := by simp only [setToSimpleFunc, smul_sum, smul_smul, mul_comm]
#align measure_theory.simple_func.set_to_simple_func_smul MeasureTheory.SimpleFunc.setToSimpleFunc_smul

section Order

variable {G' G'' : Type*} [NormedLatticeAddCommGroup G''] [NormedSpace â„ G'']
  [NormedLatticeAddCommGroup G'] [NormedSpace â„ G']

theorem setToSimpleFunc_mono_left {m : MeasurableSpace Î±} (T T' : Set Î± â†’ F â†’L[â„] G'')
    (hTT' : âˆ€ s x, T s x â‰¤ T' s x) (f : Î± â†’â‚› F) : setToSimpleFunc T f â‰¤ setToSimpleFunc T' f := by
  simp_rw [setToSimpleFunc]; exact sum_le_sum fun i _ => hTT' _ i
#align measure_theory.simple_func.set_to_simple_func_mono_left MeasureTheory.SimpleFunc.setToSimpleFunc_mono_left

theorem setToSimpleFunc_mono_left' (T T' : Set Î± â†’ E â†’L[â„] G'')
    (hTT' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, T s x â‰¤ T' s x) (f : Î± â†’â‚› E)
    (hf : Integrable f Î¼) : setToSimpleFunc T f â‰¤ setToSimpleFunc T' f := by
  refine sum_le_sum fun i _ => ?_
  by_cases h0 : i = 0
  Â· simp [h0]
  Â· exact hTT' _ (measurableSet_fiber _ _) (measure_preimage_lt_top_of_integrable _ hf h0) i
#align measure_theory.simple_func.set_to_simple_func_mono_left' MeasureTheory.SimpleFunc.setToSimpleFunc_mono_left'

theorem setToSimpleFunc_nonneg {m : MeasurableSpace Î±} (T : Set Î± â†’ G' â†’L[â„] G'')
    (hT_nonneg : âˆ€ s x, 0 â‰¤ x â†’ 0 â‰¤ T s x) (f : Î± â†’â‚› G') (hf : 0 â‰¤ f) :
    0 â‰¤ setToSimpleFunc T f := by
  refine sum_nonneg fun i hi => hT_nonneg _ i ?_
  rw [mem_range] at hi
  obtain âŸ¨y, hyâŸ© := Set.mem_range.mp hi
  rw [â† hy]
  refine le_trans ?_ (hf y)
  simp
#align measure_theory.simple_func.set_to_simple_func_nonneg MeasureTheory.SimpleFunc.setToSimpleFunc_nonneg

theorem setToSimpleFunc_nonneg' (T : Set Î± â†’ G' â†’L[â„] G'')
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) (f : Î± â†’â‚› G') (hf : 0 â‰¤ f)
    (hfi : Integrable f Î¼) : 0 â‰¤ setToSimpleFunc T f := by
  refine sum_nonneg fun i hi => ?_
  by_cases h0 : i = 0
  Â· simp [h0]
  refine
    hT_nonneg _ (measurableSet_fiber _ _) (measure_preimage_lt_top_of_integrable _ hfi h0) i ?_
  rw [mem_range] at hi
  obtain âŸ¨y, hyâŸ© := Set.mem_range.mp hi
  rw [â† hy]
  convert hf y
#align measure_theory.simple_func.set_to_simple_func_nonneg' MeasureTheory.SimpleFunc.setToSimpleFunc_nonneg'

theorem setToSimpleFunc_mono {T : Set Î± â†’ G' â†’L[â„] G''} (h_add : FinMeasAdditive Î¼ T)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f g : Î± â†’â‚› G'}
    (hfi : Integrable f Î¼) (hgi : Integrable g Î¼) (hfg : f â‰¤ g) :
    setToSimpleFunc T f â‰¤ setToSimpleFunc T g := by
  rw [â† sub_nonneg, â† setToSimpleFunc_sub T h_add hgi hfi]
  refine setToSimpleFunc_nonneg' T hT_nonneg _ ?_ (hgi.sub hfi)
  intro x
  simp only [coe_sub, sub_nonneg, coe_zero, Pi.zero_apply, Pi.sub_apply]
  exact hfg x
#align measure_theory.simple_func.set_to_simple_func_mono MeasureTheory.SimpleFunc.setToSimpleFunc_mono

end Order

theorem norm_setToSimpleFunc_le_sum_opNorm {m : MeasurableSpace Î±} (T : Set Î± â†’ F' â†’L[â„] F)
    (f : Î± â†’â‚› F') : â€–f.setToSimpleFunc Tâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– :=
  calc
    â€–âˆ‘ x âˆˆ f.range, T (f â»Â¹' {x}) xâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x}) xâ€– := norm_sum_le _ _
    _ â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– := by
      refine Finset.sum_le_sum fun b _ => ?_; simp_rw [ContinuousLinearMap.le_opNorm]
#align measure_theory.simple_func.norm_set_to_simple_func_le_sum_op_norm MeasureTheory.SimpleFunc.norm_setToSimpleFunc_le_sum_opNorm

@[deprecated (since := "2024-02-02")]
alias norm_setToSimpleFunc_le_sum_op_norm := norm_setToSimpleFunc_le_sum_opNorm

theorem norm_setToSimpleFunc_le_sum_mul_norm (T : Set Î± â†’ F â†’L[â„] F') {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ â€–T sâ€– â‰¤ C * (Î¼ s).toReal) (f : Î± â†’â‚› F) :
    â€–f.setToSimpleFunc Tâ€– â‰¤ C * âˆ‘ x âˆˆ f.range, (Î¼ (f â»Â¹' {x})).toReal * â€–xâ€– :=
  calc
    â€–f.setToSimpleFunc Tâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– :=
      norm_setToSimpleFunc_le_sum_opNorm T f
    _ â‰¤ âˆ‘ x âˆˆ f.range, C * (Î¼ (f â»Â¹' {x})).toReal * â€–xâ€– := by
      gcongr
      exact hT_norm _ <| SimpleFunc.measurableSet_fiber _ _
    _ â‰¤ C * âˆ‘ x âˆˆ f.range, (Î¼ (f â»Â¹' {x})).toReal * â€–xâ€– := by simp_rw [mul_sum, â† mul_assoc]; rfl
#align measure_theory.simple_func.norm_set_to_simple_func_le_sum_mul_norm MeasureTheory.SimpleFunc.norm_setToSimpleFunc_le_sum_mul_norm

theorem norm_setToSimpleFunc_le_sum_mul_norm_of_integrable (T : Set Î± â†’ E â†’L[â„] F') {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * (Î¼ s).toReal) (f : Î± â†’â‚› E)
    (hf : Integrable f Î¼) :
    â€–f.setToSimpleFunc Tâ€– â‰¤ C * âˆ‘ x âˆˆ f.range, (Î¼ (f â»Â¹' {x})).toReal * â€–xâ€– :=
  calc
    â€–f.setToSimpleFunc Tâ€– â‰¤ âˆ‘ x âˆˆ f.range, â€–T (f â»Â¹' {x})â€– * â€–xâ€– :=
      norm_setToSimpleFunc_le_sum_opNorm T f
    _ â‰¤ âˆ‘ x âˆˆ f.range, C * (Î¼ (f â»Â¹' {x})).toReal * â€–xâ€– := by
      refine Finset.sum_le_sum fun b hb => ?_
      obtain rfl | hb := eq_or_ne b 0
      Â· simp
      gcongr
      exact hT_norm _ (SimpleFunc.measurableSet_fiber _ _) <|
        SimpleFunc.measure_preimage_lt_top_of_integrable _ hf hb
    _ â‰¤ C * âˆ‘ x âˆˆ f.range, (Î¼ (f â»Â¹' {x})).toReal * â€–xâ€– := by simp_rw [mul_sum, â† mul_assoc]; rfl
#align measure_theory.simple_func.norm_set_to_simple_func_le_sum_mul_norm_of_integrable MeasureTheory.SimpleFunc.norm_setToSimpleFunc_le_sum_mul_norm_of_integrable

theorem setToSimpleFunc_indicator (T : Set Î± â†’ F â†’L[â„] F') (hT_empty : T âˆ… = 0)
    {m : MeasurableSpace Î±} {s : Set Î±} (hs : MeasurableSet s) (x : F) :
    SimpleFunc.setToSimpleFunc T
        (SimpleFunc.piecewise s hs (SimpleFunc.const Î± x) (SimpleFunc.const Î± 0)) =
      T s x := by
  obtain rfl | hs_empty := s.eq_empty_or_nonempty
  Â· simp only [hT_empty, ContinuousLinearMap.zero_apply, piecewise_empty, const_zero,
      setToSimpleFunc_zero_apply]
  simp_rw [setToSimpleFunc]
  obtain rfl | hs_univ := eq_or_ne s univ
  Â· haveI hÎ± := hs_empty.to_type
    simp [â† Function.const_def]
  rw [range_indicator hs hs_empty hs_univ]
  by_cases hx0 : x = 0
  Â· simp_rw [hx0]; simp
  rw [sum_insert]
  swap; Â· rw [Finset.mem_singleton]; exact hx0
  rw [sum_singleton, (T _).map_zero, add_zero]
  congr
  simp only [coe_piecewise, piecewise_eq_indicator, coe_const, Function.const_zero,
    piecewise_eq_indicator]
  rw [indicator_preimage, â† Function.const_def, preimage_const_of_mem]
  swap; Â· exact Set.mem_singleton x
  rw [â† Function.const_zero, â† Function.const_def, preimage_const_of_not_mem]
  swap; Â· rw [Set.mem_singleton_iff]; exact Ne.symm hx0
  simp
#align measure_theory.simple_func.set_to_simple_func_indicator MeasureTheory.SimpleFunc.setToSimpleFunc_indicator

theorem setToSimpleFunc_const' [Nonempty Î±] (T : Set Î± â†’ F â†’L[â„] F') (x : F)
    {m : MeasurableSpace Î±} : SimpleFunc.setToSimpleFunc T (SimpleFunc.const Î± x) = T univ x := by
  simp only [setToSimpleFunc, range_const, Set.mem_singleton, preimage_const_of_mem,
    sum_singleton, â† Function.const_def, coe_const]
#align measure_theory.simple_func.set_to_simple_func_const' MeasureTheory.SimpleFunc.setToSimpleFunc_const'

theorem setToSimpleFunc_const (T : Set Î± â†’ F â†’L[â„] F') (hT_empty : T âˆ… = 0) (x : F)
    {m : MeasurableSpace Î±} : SimpleFunc.setToSimpleFunc T (SimpleFunc.const Î± x) = T univ x := by
  cases isEmpty_or_nonempty Î±
  Â· have h_univ_empty : (univ : Set Î±) = âˆ… := Subsingleton.elim _ _
    rw [h_univ_empty, hT_empty]
    simp only [setToSimpleFunc, ContinuousLinearMap.zero_apply, sum_empty,
      range_eq_empty_of_isEmpty]
  Â· exact setToSimpleFunc_const' T x
#align measure_theory.simple_func.set_to_simple_func_const MeasureTheory.SimpleFunc.setToSimpleFunc_const

end SimpleFunc

namespace L1

set_option linter.uppercaseLean3 false

open AEEqFun Lp.simpleFunc Lp

namespace SimpleFunc

theorem norm_eq_sum_mul (f : Î± â†’â‚â‚›[Î¼] G) :
    â€–fâ€– = âˆ‘ x âˆˆ (toSimpleFunc f).range, (Î¼ (toSimpleFunc f â»Â¹' {x})).toReal * â€–xâ€– := by
  rw [norm_toSimpleFunc, snorm_one_eq_lintegral_nnnorm]
  have h_eq := SimpleFunc.map_apply (fun x => (â€–xâ€–â‚Š : â„â‰¥0âˆ)) (toSimpleFunc f)
  simp_rw [â† h_eq]
  rw [SimpleFunc.lintegral_eq_lintegral, SimpleFunc.map_lintegral, ENNReal.toReal_sum]
  Â· congr
    ext1 x
    rw [ENNReal.toReal_mul, mul_comm, â† ofReal_norm_eq_coe_nnnorm,
      ENNReal.toReal_ofReal (norm_nonneg _)]
  Â· intro x _
    by_cases hx0 : x = 0
    Â· rw [hx0]; simp
    Â· exact
        ENNReal.mul_ne_top ENNReal.coe_ne_top
          (SimpleFunc.measure_preimage_lt_top_of_integrable _ (SimpleFunc.integrable f) hx0).ne
#align measure_theory.L1.simple_func.norm_eq_sum_mul MeasureTheory.L1.SimpleFunc.norm_eq_sum_mul

section SetToL1S

variable [NormedField ğ•œ] [NormedSpace ğ•œ E]

attribute [local instance] Lp.simpleFunc.module

attribute [local instance] Lp.simpleFunc.normedSpace

/-- Extend `Set Î± â†’ (E â†’L[â„] F')` to `(Î± â†’â‚â‚›[Î¼] E) â†’ F'`. -/
def setToL1S (T : Set Î± â†’ E â†’L[â„] F) (f : Î± â†’â‚â‚›[Î¼] E) : F :=
  (toSimpleFunc f).setToSimpleFunc T
#align measure_theory.L1.simple_func.set_to_L1s MeasureTheory.L1.SimpleFunc.setToL1S

theorem setToL1S_eq_setToSimpleFunc (T : Set Î± â†’ E â†’L[â„] F) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T f = (toSimpleFunc f).setToSimpleFunc T :=
  rfl
#align measure_theory.L1.simple_func.set_to_L1s_eq_set_to_simple_func MeasureTheory.L1.SimpleFunc.setToL1S_eq_setToSimpleFunc

@[simp]
theorem setToL1S_zero_left (f : Î± â†’â‚â‚›[Î¼] E) : setToL1S (0 : Set Î± â†’ E â†’L[â„] F) f = 0 :=
  SimpleFunc.setToSimpleFunc_zero _
#align measure_theory.L1.simple_func.set_to_L1s_zero_left MeasureTheory.L1.SimpleFunc.setToL1S_zero_left

theorem setToL1S_zero_left' {T : Set Î± â†’ E â†’L[â„] F}
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0) (f : Î± â†’â‚â‚›[Î¼] E) : setToL1S T f = 0 :=
  SimpleFunc.setToSimpleFunc_zero' h_zero _ (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_zero_left' MeasureTheory.L1.SimpleFunc.setToL1S_zero_left'

theorem setToL1S_congr (T : Set Î± â†’ E â†’L[â„] F) (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T) {f g : Î± â†’â‚â‚›[Î¼] E} (h : toSimpleFunc f =áµ[Î¼] toSimpleFunc g) :
    setToL1S T f = setToL1S T g :=
  SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable f) h
#align measure_theory.L1.simple_func.set_to_L1s_congr MeasureTheory.L1.SimpleFunc.setToL1S_congr

theorem setToL1S_congr_left (T T' : Set Î± â†’ E â†’L[â„] F)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T f = setToL1S T' f :=
  SimpleFunc.setToSimpleFunc_congr_left T T' h (simpleFunc.toSimpleFunc f) (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_congr_left MeasureTheory.L1.SimpleFunc.setToL1S_congr_left

/-- `setToL1S` does not change if we replace the measure `Î¼` by `Î¼'` with `Î¼ â‰ª Î¼'`. The statement
uses two functions `f` and `f'` because they have to belong to different types, but morally these
are the same function (we have `f =áµ[Î¼] f'`). -/
theorem setToL1S_congr_measure {Î¼' : Measure Î±} (T : Set Î± â†’ E â†’L[â„] F)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : FinMeasAdditive Î¼ T) (hÎ¼ : Î¼ â‰ª Î¼')
    (f : Î± â†’â‚â‚›[Î¼] E) (f' : Î± â†’â‚â‚›[Î¼'] E) (h : (f : Î± â†’ E) =áµ[Î¼] f') :
    setToL1S T f = setToL1S T f' := by
  refine SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable f) ?_
  refine (toSimpleFunc_eq_toFun f).trans ?_
  suffices (f' : Î± â†’ E) =áµ[Î¼] simpleFunc.toSimpleFunc f' from h.trans this
  have goal' : (f' : Î± â†’ E) =áµ[Î¼'] simpleFunc.toSimpleFunc f' := (toSimpleFunc_eq_toFun f').symm
  exact hÎ¼.ae_eq goal'
#align measure_theory.L1.simple_func.set_to_L1s_congr_measure MeasureTheory.L1.SimpleFunc.setToL1S_congr_measure

theorem setToL1S_add_left (T T' : Set Î± â†’ E â†’L[â„] F) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S (T + T') f = setToL1S T f + setToL1S T' f :=
  SimpleFunc.setToSimpleFunc_add_left T T'
#align measure_theory.L1.simple_func.set_to_L1s_add_left MeasureTheory.L1.SimpleFunc.setToL1S_add_left

theorem setToL1S_add_left' (T T' T'' : Set Î± â†’ E â†’L[â„] F)
    (h_add : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T'' s = T s + T' s) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T'' f = setToL1S T f + setToL1S T' f :=
  SimpleFunc.setToSimpleFunc_add_left' T T' T'' h_add (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_add_left' MeasureTheory.L1.SimpleFunc.setToL1S_add_left'

theorem setToL1S_smul_left (T : Set Î± â†’ E â†’L[â„] F) (c : â„) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S (fun s => c â€¢ T s) f = c â€¢ setToL1S T f :=
  SimpleFunc.setToSimpleFunc_smul_left T c _
#align measure_theory.L1.simple_func.set_to_L1s_smul_left MeasureTheory.L1.SimpleFunc.setToL1S_smul_left

theorem setToL1S_smul_left' (T T' : Set Î± â†’ E â†’L[â„] F) (c : â„)
    (h_smul : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T' s = c â€¢ T s) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T' f = c â€¢ setToL1S T f :=
  SimpleFunc.setToSimpleFunc_smul_left' T T' c h_smul (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_smul_left' MeasureTheory.L1.SimpleFunc.setToL1S_smul_left'

theorem setToL1S_add (T : Set Î± â†’ E â†’L[â„] F) (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T) (f g : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T (f + g) = setToL1S T f + setToL1S T g := by
  simp_rw [setToL1S]
  rw [â† SimpleFunc.setToSimpleFunc_add T h_add (SimpleFunc.integrable f)
      (SimpleFunc.integrable g)]
  exact
    SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable _)
      (add_toSimpleFunc f g)
#align measure_theory.L1.simple_func.set_to_L1s_add MeasureTheory.L1.SimpleFunc.setToL1S_add

theorem setToL1S_neg {T : Set Î± â†’ E â†’L[â„] F} (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T) (f : Î± â†’â‚â‚›[Î¼] E) : setToL1S T (-f) = -setToL1S T f := by
  simp_rw [setToL1S]
  have : simpleFunc.toSimpleFunc (-f) =áµ[Î¼] â‡‘(-simpleFunc.toSimpleFunc f) :=
    neg_toSimpleFunc f
  rw [SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable _) this]
  exact SimpleFunc.setToSimpleFunc_neg T h_add (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_neg MeasureTheory.L1.SimpleFunc.setToL1S_neg

theorem setToL1S_sub {T : Set Î± â†’ E â†’L[â„] F} (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T) (f g : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T (f - g) = setToL1S T f - setToL1S T g := by
  rw [sub_eq_add_neg, setToL1S_add T h_zero h_add, setToL1S_neg h_zero h_add, sub_eq_add_neg]
#align measure_theory.L1.simple_func.set_to_L1s_sub MeasureTheory.L1.SimpleFunc.setToL1S_sub

theorem setToL1S_smul_real (T : Set Î± â†’ E â†’L[â„] F)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : FinMeasAdditive Î¼ T) (c : â„)
    (f : Î± â†’â‚â‚›[Î¼] E) : setToL1S T (c â€¢ f) = c â€¢ setToL1S T f := by
  simp_rw [setToL1S]
  rw [â† SimpleFunc.setToSimpleFunc_smul_real T h_add c (SimpleFunc.integrable f)]
  refine SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable _) ?_
  exact smul_toSimpleFunc c f
#align measure_theory.L1.simple_func.set_to_L1s_smul_real MeasureTheory.L1.SimpleFunc.setToL1S_smul_real

theorem setToL1S_smul {E} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace ğ•œ E]
    [NormedSpace ğ•œ F] (T : Set Î± â†’ E â†’L[â„] F) (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T) (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ)
    (f : Î± â†’â‚â‚›[Î¼] E) : setToL1S T (c â€¢ f) = c â€¢ setToL1S T f := by
  simp_rw [setToL1S]
  rw [â† SimpleFunc.setToSimpleFunc_smul T h_add h_smul c (SimpleFunc.integrable f)]
  refine SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable _) ?_
  exact smul_toSimpleFunc c f
#align measure_theory.L1.simple_func.set_to_L1s_smul MeasureTheory.L1.SimpleFunc.setToL1S_smul

theorem norm_setToL1S_le (T : Set Î± â†’ E â†’L[â„] F) {C : â„}
    (hT_norm : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ â€–T sâ€– â‰¤ C * (Î¼ s).toReal) (f : Î± â†’â‚â‚›[Î¼] E) :
    â€–setToL1S T fâ€– â‰¤ C * â€–fâ€– := by
  rw [setToL1S, norm_eq_sum_mul f]
  exact
    SimpleFunc.norm_setToSimpleFunc_le_sum_mul_norm_of_integrable T hT_norm _
      (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.norm_set_to_L1s_le MeasureTheory.L1.SimpleFunc.norm_setToL1S_le

theorem setToL1S_indicatorConst {T : Set Î± â†’ E â†’L[â„] F} {s : Set Î±}
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : FinMeasAdditive Î¼ T)
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s < âˆ) (x : E) :
    setToL1S T (simpleFunc.indicatorConst 1 hs hÎ¼s.ne x) = T s x := by
  have h_empty : T âˆ… = 0 := h_zero _ MeasurableSet.empty measure_empty
  rw [setToL1S_eq_setToSimpleFunc]
  refine Eq.trans ?_ (SimpleFunc.setToSimpleFunc_indicator T h_empty hs x)
  refine SimpleFunc.setToSimpleFunc_congr T h_zero h_add (SimpleFunc.integrable _) ?_
  exact toSimpleFunc_indicatorConst hs hÎ¼s.ne x
#align measure_theory.L1.simple_func.set_to_L1s_indicator_const MeasureTheory.L1.SimpleFunc.setToL1S_indicatorConst

theorem setToL1S_const [IsFiniteMeasure Î¼] {T : Set Î± â†’ E â†’L[â„] F}
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : FinMeasAdditive Î¼ T) (x : E) :
    setToL1S T (simpleFunc.indicatorConst 1 MeasurableSet.univ (measure_ne_top Î¼ _) x) = T univ x :=
  setToL1S_indicatorConst h_zero h_add MeasurableSet.univ (measure_lt_top _ _) x
#align measure_theory.L1.simple_func.set_to_L1s_const MeasureTheory.L1.SimpleFunc.setToL1S_const

section Order

variable {G'' G' : Type*} [NormedLatticeAddCommGroup G'] [NormedSpace â„ G']
  [NormedLatticeAddCommGroup G''] [NormedSpace â„ G''] {T : Set Î± â†’ G'' â†’L[â„] G'}

theorem setToL1S_mono_left {T T' : Set Î± â†’ E â†’L[â„] G''} (hTT' : âˆ€ s x, T s x â‰¤ T' s x)
    (f : Î± â†’â‚â‚›[Î¼] E) : setToL1S T f â‰¤ setToL1S T' f :=
  SimpleFunc.setToSimpleFunc_mono_left T T' hTT' _
#align measure_theory.L1.simple_func.set_to_L1s_mono_left MeasureTheory.L1.SimpleFunc.setToL1S_mono_left

theorem setToL1S_mono_left' {T T' : Set Î± â†’ E â†’L[â„] G''}
    (hTT' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, T s x â‰¤ T' s x) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1S T f â‰¤ setToL1S T' f :=
  SimpleFunc.setToSimpleFunc_mono_left' T T' hTT' _ (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_mono_left' MeasureTheory.L1.SimpleFunc.setToL1S_mono_left'

theorem setToL1S_nonneg (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f : Î± â†’â‚â‚›[Î¼] G''}
    (hf : 0 â‰¤ f) : 0 â‰¤ setToL1S T f := by
  simp_rw [setToL1S]
  obtain âŸ¨f', hf', hff'âŸ© : âˆƒ f' : Î± â†’â‚› G'', 0 â‰¤ f' âˆ§ simpleFunc.toSimpleFunc f =áµ[Î¼] f' := by
    obtain âŸ¨f'', hf'', hff''âŸ© := exists_simpleFunc_nonneg_ae_eq hf
    exact âŸ¨f'', hf'', (Lp.simpleFunc.toSimpleFunc_eq_toFun f).trans hff''âŸ©
  rw [SimpleFunc.setToSimpleFunc_congr _ h_zero h_add (SimpleFunc.integrable _) hff']
  exact
    SimpleFunc.setToSimpleFunc_nonneg' T hT_nonneg _ hf' ((SimpleFunc.integrable f).congr hff')
#align measure_theory.L1.simple_func.set_to_L1s_nonneg MeasureTheory.L1.SimpleFunc.setToL1S_nonneg

theorem setToL1S_mono (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s = 0 â†’ T s = 0)
    (h_add : FinMeasAdditive Î¼ T)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f g : Î± â†’â‚â‚›[Î¼] G''}
    (hfg : f â‰¤ g) : setToL1S T f â‰¤ setToL1S T g := by
  rw [â† sub_nonneg] at hfg âŠ¢
  rw [â† setToL1S_sub h_zero h_add]
  exact setToL1S_nonneg h_zero h_add hT_nonneg hfg
#align measure_theory.L1.simple_func.set_to_L1s_mono MeasureTheory.L1.SimpleFunc.setToL1S_mono

end Order

variable [NormedSpace ğ•œ F]
variable (Î± E Î¼ ğ•œ)

/-- Extend `Set Î± â†’ E â†’L[â„] F` to `(Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] F`. -/
def setToL1SCLM' {T : Set Î± â†’ E â†’L[â„] F} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) : (Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] F :=
  LinearMap.mkContinuous
    âŸ¨âŸ¨setToL1S T, setToL1S_add T (fun _ => hT.eq_zero_of_measure_zero) hT.1âŸ©,
      setToL1S_smul T (fun _ => hT.eq_zero_of_measure_zero) hT.1 h_smulâŸ©
    C fun f => norm_setToL1S_le T hT.2 f
#align measure_theory.L1.simple_func.set_to_L1s_clm' MeasureTheory.L1.SimpleFunc.setToL1SCLM'

/-- Extend `Set Î± â†’ E â†’L[â„] F` to `(Î± â†’â‚â‚›[Î¼] E) â†’L[â„] F`. -/
def setToL1SCLM {T : Set Î± â†’ E â†’L[â„] F} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C) :
    (Î± â†’â‚â‚›[Î¼] E) â†’L[â„] F :=
  LinearMap.mkContinuous
    âŸ¨âŸ¨setToL1S T, setToL1S_add T (fun _ => hT.eq_zero_of_measure_zero) hT.1âŸ©,
      setToL1S_smul_real T (fun _ => hT.eq_zero_of_measure_zero) hT.1âŸ©
    C fun f => norm_setToL1S_le T hT.2 f
#align measure_theory.L1.simple_func.set_to_L1s_clm MeasureTheory.L1.SimpleFunc.setToL1SCLM

variable {Î± E Î¼ ğ•œ}
variable {T T' T'' : Set Î± â†’ E â†’L[â„] F} {C C' C'' : â„}

@[simp]
theorem setToL1SCLM_zero_left (hT : DominatedFinMeasAdditive Î¼ (0 : Set Î± â†’ E â†’L[â„] F) C)
    (f : Î± â†’â‚â‚›[Î¼] E) : setToL1SCLM Î± E Î¼ hT f = 0 :=
  setToL1S_zero_left _
#align measure_theory.L1.simple_func.set_to_L1s_clm_zero_left MeasureTheory.L1.SimpleFunc.setToL1SCLM_zero_left

theorem setToL1SCLM_zero_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ hT f = 0 :=
  setToL1S_zero_left' h_zero f
#align measure_theory.L1.simple_func.set_to_L1s_clm_zero_left' MeasureTheory.L1.SimpleFunc.setToL1SCLM_zero_left'

theorem setToL1SCLM_congr_left (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (h : T = T') (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ hT f = setToL1SCLM Î± E Î¼ hT' f :=
  setToL1S_congr_left T T' (fun _ _ _ => by rw [h]) f
#align measure_theory.L1.simple_func.set_to_L1s_clm_congr_left MeasureTheory.L1.SimpleFunc.setToL1SCLM_congr_left

theorem setToL1SCLM_congr_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s)
    (f : Î± â†’â‚â‚›[Î¼] E) : setToL1SCLM Î± E Î¼ hT f = setToL1SCLM Î± E Î¼ hT' f :=
  setToL1S_congr_left T T' h f
#align measure_theory.L1.simple_func.set_to_L1s_clm_congr_left' MeasureTheory.L1.SimpleFunc.setToL1SCLM_congr_left'

theorem setToL1SCLM_congr_measure {Î¼' : Measure Î±} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼' T C') (hÎ¼ : Î¼ â‰ª Î¼') (f : Î± â†’â‚â‚›[Î¼] E) (f' : Î± â†’â‚â‚›[Î¼'] E)
    (h : (f : Î± â†’ E) =áµ[Î¼] f') : setToL1SCLM Î± E Î¼ hT f = setToL1SCLM Î± E Î¼' hT' f' :=
  setToL1S_congr_measure T (fun _ => hT.eq_zero_of_measure_zero) hT.1 hÎ¼ _ _ h
#align measure_theory.L1.simple_func.set_to_L1s_clm_congr_measure MeasureTheory.L1.SimpleFunc.setToL1SCLM_congr_measure

theorem setToL1SCLM_add_left (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ (hT.add hT') f = setToL1SCLM Î± E Î¼ hT f + setToL1SCLM Î± E Î¼ hT' f :=
  setToL1S_add_left T T' f
#align measure_theory.L1.simple_func.set_to_L1s_clm_add_left MeasureTheory.L1.SimpleFunc.setToL1SCLM_add_left

theorem setToL1SCLM_add_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (hT'' : DominatedFinMeasAdditive Î¼ T'' C'')
    (h_add : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T'' s = T s + T' s) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ hT'' f = setToL1SCLM Î± E Î¼ hT f + setToL1SCLM Î± E Î¼ hT' f :=
  setToL1S_add_left' T T' T'' h_add f
#align measure_theory.L1.simple_func.set_to_L1s_clm_add_left' MeasureTheory.L1.SimpleFunc.setToL1SCLM_add_left'

theorem setToL1SCLM_smul_left (c : â„) (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ (hT.smul c) f = c â€¢ setToL1SCLM Î± E Î¼ hT f :=
  setToL1S_smul_left T c f
#align measure_theory.L1.simple_func.set_to_L1s_clm_smul_left MeasureTheory.L1.SimpleFunc.setToL1SCLM_smul_left

theorem setToL1SCLM_smul_left' (c : â„) (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (h_smul : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T' s = c â€¢ T s) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ hT' f = c â€¢ setToL1SCLM Î± E Î¼ hT f :=
  setToL1S_smul_left' T T' c h_smul f
#align measure_theory.L1.simple_func.set_to_L1s_clm_smul_left' MeasureTheory.L1.SimpleFunc.setToL1SCLM_smul_left'

theorem norm_setToL1SCLM_le {T : Set Î± â†’ E â†’L[â„] F} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hC : 0 â‰¤ C) : â€–setToL1SCLM Î± E Î¼ hTâ€– â‰¤ C :=
  LinearMap.mkContinuous_norm_le _ hC _
#align measure_theory.L1.simple_func.norm_set_to_L1s_clm_le MeasureTheory.L1.SimpleFunc.norm_setToL1SCLM_le

theorem norm_setToL1SCLM_le' {T : Set Î± â†’ E â†’L[â„] F} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C) :
    â€–setToL1SCLM Î± E Î¼ hTâ€– â‰¤ max C 0 :=
  LinearMap.mkContinuous_norm_le' _ _
#align measure_theory.L1.simple_func.norm_set_to_L1s_clm_le' MeasureTheory.L1.SimpleFunc.norm_setToL1SCLM_le'

theorem setToL1SCLM_const [IsFiniteMeasure Î¼] {T : Set Î± â†’ E â†’L[â„] F} {C : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (x : E) :
    setToL1SCLM Î± E Î¼ hT (simpleFunc.indicatorConst 1 MeasurableSet.univ (measure_ne_top Î¼ _) x) =
      T univ x :=
  setToL1S_const (fun _ => hT.eq_zero_of_measure_zero) hT.1 x
#align measure_theory.L1.simple_func.set_to_L1s_clm_const MeasureTheory.L1.SimpleFunc.setToL1SCLM_const

section Order

variable {G' G'' : Type*} [NormedLatticeAddCommGroup G''] [NormedSpace â„ G'']
  [NormedLatticeAddCommGroup G'] [NormedSpace â„ G']

theorem setToL1SCLM_mono_left {T T' : Set Î± â†’ E â†’L[â„] G''} {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (hTT' : âˆ€ s x, T s x â‰¤ T' s x) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ hT f â‰¤ setToL1SCLM Î± E Î¼ hT' f :=
  SimpleFunc.setToSimpleFunc_mono_left T T' hTT' _
#align measure_theory.L1.simple_func.set_to_L1s_clm_mono_left MeasureTheory.L1.SimpleFunc.setToL1SCLM_mono_left

theorem setToL1SCLM_mono_left' {T T' : Set Î± â†’ E â†’L[â„] G''} {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (hTT' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, T s x â‰¤ T' s x) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1SCLM Î± E Î¼ hT f â‰¤ setToL1SCLM Î± E Î¼ hT' f :=
  SimpleFunc.setToSimpleFunc_mono_left' T T' hTT' _ (SimpleFunc.integrable f)
#align measure_theory.L1.simple_func.set_to_L1s_clm_mono_left' MeasureTheory.L1.SimpleFunc.setToL1SCLM_mono_left'

theorem setToL1SCLM_nonneg {T : Set Î± â†’ G' â†’L[â„] G''} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f : Î± â†’â‚â‚›[Î¼] G'}
    (hf : 0 â‰¤ f) : 0 â‰¤ setToL1SCLM Î± G' Î¼ hT f :=
  setToL1S_nonneg (fun _ => hT.eq_zero_of_measure_zero) hT.1 hT_nonneg hf
#align measure_theory.L1.simple_func.set_to_L1s_clm_nonneg MeasureTheory.L1.SimpleFunc.setToL1SCLM_nonneg

theorem setToL1SCLM_mono {T : Set Î± â†’ G' â†’L[â„] G''} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f g : Î± â†’â‚â‚›[Î¼] G'}
    (hfg : f â‰¤ g) : setToL1SCLM Î± G' Î¼ hT f â‰¤ setToL1SCLM Î± G' Î¼ hT g :=
  setToL1S_mono (fun _ => hT.eq_zero_of_measure_zero) hT.1 hT_nonneg hfg
#align measure_theory.L1.simple_func.set_to_L1s_clm_mono MeasureTheory.L1.SimpleFunc.setToL1SCLM_mono

end Order

end SetToL1S

end SimpleFunc

open SimpleFunc

section SetToL1

attribute [local instance] Lp.simpleFunc.module

attribute [local instance] Lp.simpleFunc.normedSpace

variable (ğ•œ) [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] [CompleteSpace F]
  {T T' T'' : Set Î± â†’ E â†’L[â„] F} {C C' C'' : â„}

/-- Extend `set Î± â†’ (E â†’L[â„] F)` to `(Î± â†’â‚[Î¼] E) â†’L[ğ•œ] F`. -/
def setToL1' (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) : (Î± â†’â‚[Î¼] E) â†’L[ğ•œ] F :=
  (setToL1SCLM' Î± E ğ•œ Î¼ hT h_smul).extend (coeToLp Î± E ğ•œ) (simpleFunc.denseRange one_ne_top)
    simpleFunc.uniformInducing
#align measure_theory.L1.set_to_L1' MeasureTheory.L1.setToL1'

variable {ğ•œ}

/-- Extend `Set Î± â†’ E â†’L[â„] F` to `(Î± â†’â‚[Î¼] E) â†’L[â„] F`. -/
def setToL1 (hT : DominatedFinMeasAdditive Î¼ T C) : (Î± â†’â‚[Î¼] E) â†’L[â„] F :=
  (setToL1SCLM Î± E Î¼ hT).extend (coeToLp Î± E â„) (simpleFunc.denseRange one_ne_top)
    simpleFunc.uniformInducing
#align measure_theory.L1.set_to_L1 MeasureTheory.L1.setToL1

theorem setToL1_eq_setToL1SCLM (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚â‚›[Î¼] E) :
    setToL1 hT f = setToL1SCLM Î± E Î¼ hT f :=
  uniformly_extend_of_ind simpleFunc.uniformInducing (simpleFunc.denseRange one_ne_top)
    (setToL1SCLM Î± E Î¼ hT).uniformContinuous _
#align measure_theory.L1.set_to_L1_eq_set_to_L1s_clm MeasureTheory.L1.setToL1_eq_setToL1SCLM

theorem setToL1_eq_setToL1' (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (f : Î± â†’â‚[Î¼] E) :
    setToL1 hT f = setToL1' ğ•œ hT h_smul f :=
  rfl
#align measure_theory.L1.set_to_L1_eq_set_to_L1' MeasureTheory.L1.setToL1_eq_setToL1'

@[simp]
theorem setToL1_zero_left (hT : DominatedFinMeasAdditive Î¼ (0 : Set Î± â†’ E â†’L[â„] F) C)
    (f : Î± â†’â‚[Î¼] E) : setToL1 hT f = 0 := by
  suffices setToL1 hT = 0 by rw [this]; simp
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ hT) _ _ _ _ ?_
  ext1 f
  rw [setToL1SCLM_zero_left hT f, ContinuousLinearMap.zero_comp, ContinuousLinearMap.zero_apply]
#align measure_theory.L1.set_to_L1_zero_left MeasureTheory.L1.setToL1_zero_left

theorem setToL1_zero_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0) (f : Î± â†’â‚[Î¼] E) : setToL1 hT f = 0 := by
  suffices setToL1 hT = 0 by rw [this]; simp
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ hT) _ _ _ _ ?_
  ext1 f
  rw [setToL1SCLM_zero_left' hT h_zero f, ContinuousLinearMap.zero_comp,
    ContinuousLinearMap.zero_apply]
#align measure_theory.L1.set_to_L1_zero_left' MeasureTheory.L1.setToL1_zero_left'

theorem setToL1_congr_left (T T' : Set Î± â†’ E â†’L[â„] F) {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C') (h : T = T')
    (f : Î± â†’â‚[Î¼] E) : setToL1 hT f = setToL1 hT' f := by
  suffices setToL1 hT = setToL1 hT' by rw [this]
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ hT) _ _ _ _ ?_
  ext1 f
  suffices setToL1 hT' f = setToL1SCLM Î± E Î¼ hT f by rw [â† this]; rfl
  rw [setToL1_eq_setToL1SCLM]
  exact setToL1SCLM_congr_left hT' hT h.symm f
#align measure_theory.L1.set_to_L1_congr_left MeasureTheory.L1.setToL1_congr_left

theorem setToL1_congr_left' (T T' : Set Î± â†’ E â†’L[â„] F) {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s) (f : Î± â†’â‚[Î¼] E) :
    setToL1 hT f = setToL1 hT' f := by
  suffices setToL1 hT = setToL1 hT' by rw [this]
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ hT) _ _ _ _ ?_
  ext1 f
  suffices setToL1 hT' f = setToL1SCLM Î± E Î¼ hT f by rw [â† this]; rfl
  rw [setToL1_eq_setToL1SCLM]
  exact (setToL1SCLM_congr_left' hT hT' h f).symm
#align measure_theory.L1.set_to_L1_congr_left' MeasureTheory.L1.setToL1_congr_left'

theorem setToL1_add_left (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (f : Î± â†’â‚[Î¼] E) :
    setToL1 (hT.add hT') f = setToL1 hT f + setToL1 hT' f := by
  suffices setToL1 (hT.add hT') = setToL1 hT + setToL1 hT' by
    rw [this, ContinuousLinearMap.add_apply]
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ (hT.add hT')) _ _ _ _ ?_
  ext1 f
  suffices setToL1 hT f + setToL1 hT' f = setToL1SCLM Î± E Î¼ (hT.add hT') f by
    rw [â† this]; rfl
  rw [setToL1_eq_setToL1SCLM, setToL1_eq_setToL1SCLM, setToL1SCLM_add_left hT hT']
#align measure_theory.L1.set_to_L1_add_left MeasureTheory.L1.setToL1_add_left

theorem setToL1_add_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (hT'' : DominatedFinMeasAdditive Î¼ T'' C'')
    (h_add : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T'' s = T s + T' s) (f : Î± â†’â‚[Î¼] E) :
    setToL1 hT'' f = setToL1 hT f + setToL1 hT' f := by
  suffices setToL1 hT'' = setToL1 hT + setToL1 hT' by rw [this, ContinuousLinearMap.add_apply]
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ hT'') _ _ _ _ ?_
  ext1 f
  suffices setToL1 hT f + setToL1 hT' f = setToL1SCLM Î± E Î¼ hT'' f by rw [â† this]; congr
  rw [setToL1_eq_setToL1SCLM, setToL1_eq_setToL1SCLM,
    setToL1SCLM_add_left' hT hT' hT'' h_add]
#align measure_theory.L1.set_to_L1_add_left' MeasureTheory.L1.setToL1_add_left'

theorem setToL1_smul_left (hT : DominatedFinMeasAdditive Î¼ T C) (c : â„) (f : Î± â†’â‚[Î¼] E) :
    setToL1 (hT.smul c) f = c â€¢ setToL1 hT f := by
  suffices setToL1 (hT.smul c) = c â€¢ setToL1 hT by rw [this, ContinuousLinearMap.smul_apply]
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ (hT.smul c)) _ _ _ _ ?_
  ext1 f
  suffices c â€¢ setToL1 hT f = setToL1SCLM Î± E Î¼ (hT.smul c) f by rw [â† this]; congr
  rw [setToL1_eq_setToL1SCLM, setToL1SCLM_smul_left c hT]
#align measure_theory.L1.set_to_L1_smul_left MeasureTheory.L1.setToL1_smul_left

theorem setToL1_smul_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (c : â„)
    (h_smul : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T' s = c â€¢ T s) (f : Î± â†’â‚[Î¼] E) :
    setToL1 hT' f = c â€¢ setToL1 hT f := by
  suffices setToL1 hT' = c â€¢ setToL1 hT by rw [this, ContinuousLinearMap.smul_apply]
  refine ContinuousLinearMap.extend_unique (setToL1SCLM Î± E Î¼ hT') _ _ _ _ ?_
  ext1 f
  suffices c â€¢ setToL1 hT f = setToL1SCLM Î± E Î¼ hT' f by rw [â† this]; congr
  rw [setToL1_eq_setToL1SCLM, setToL1SCLM_smul_left' c hT hT' h_smul]
#align measure_theory.L1.set_to_L1_smul_left' MeasureTheory.L1.setToL1_smul_left'

theorem setToL1_smul (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ) (f : Î± â†’â‚[Î¼] E) :
    setToL1 hT (c â€¢ f) = c â€¢ setToL1 hT f := by
  rw [setToL1_eq_setToL1' hT h_smul, setToL1_eq_setToL1' hT h_smul]
  exact ContinuousLinearMap.map_smul _ _ _
#align measure_theory.L1.set_to_L1_smul MeasureTheory.L1.setToL1_smul

theorem setToL1_simpleFunc_indicatorConst (hT : DominatedFinMeasAdditive Î¼ T C) {s : Set Î±}
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s < âˆ) (x : E) :
    setToL1 hT (simpleFunc.indicatorConst 1 hs hÎ¼s.ne x) = T s x := by
  rw [setToL1_eq_setToL1SCLM]
  exact setToL1S_indicatorConst (fun s => hT.eq_zero_of_measure_zero) hT.1 hs hÎ¼s x
#align measure_theory.L1.set_to_L1_simple_func_indicator_const MeasureTheory.L1.setToL1_simpleFunc_indicatorConst

theorem setToL1_indicatorConstLp (hT : DominatedFinMeasAdditive Î¼ T C) {s : Set Î±}
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) :
    setToL1 hT (indicatorConstLp 1 hs hÎ¼s x) = T s x := by
  rw [â† Lp.simpleFunc.coe_indicatorConst hs hÎ¼s x]
  exact setToL1_simpleFunc_indicatorConst hT hs hÎ¼s.lt_top x
#align measure_theory.L1.set_to_L1_indicator_const_Lp MeasureTheory.L1.setToL1_indicatorConstLp

theorem setToL1_const [IsFiniteMeasure Î¼] (hT : DominatedFinMeasAdditive Î¼ T C) (x : E) :
    setToL1 hT (indicatorConstLp 1 MeasurableSet.univ (measure_ne_top _ _) x) = T univ x :=
  setToL1_indicatorConstLp hT MeasurableSet.univ (measure_ne_top _ _) x
#align measure_theory.L1.set_to_L1_const MeasureTheory.L1.setToL1_const

section Order

variable {G' G'' : Type*} [NormedLatticeAddCommGroup G''] [NormedSpace â„ G''] [CompleteSpace G'']
  [NormedLatticeAddCommGroup G'] [NormedSpace â„ G']

theorem setToL1_mono_left' {T T' : Set Î± â†’ E â†’L[â„] G''} {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (hTT' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, T s x â‰¤ T' s x) (f : Î± â†’â‚[Î¼] E) :
    setToL1 hT f â‰¤ setToL1 hT' f := by
  induction f using Lp.induction (hp_ne_top := one_ne_top) with
  | @h_ind c s hs hÎ¼s =>
    rw [setToL1_simpleFunc_indicatorConst hT hs hÎ¼s, setToL1_simpleFunc_indicatorConst hT' hs hÎ¼s]
    exact hTT' s hs hÎ¼s c
  | @h_add f g hf hg _ hf_le hg_le =>
    rw [(setToL1 hT).map_add, (setToL1 hT').map_add]
    exact add_le_add hf_le hg_le
  | h_closed => exact isClosed_le (setToL1 hT).continuous (setToL1 hT').continuous
#align measure_theory.L1.set_to_L1_mono_left' MeasureTheory.L1.setToL1_mono_left'

theorem setToL1_mono_left {T T' : Set Î± â†’ E â†’L[â„] G''} {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (hTT' : âˆ€ s x, T s x â‰¤ T' s x) (f : Î± â†’â‚[Î¼] E) : setToL1 hT f â‰¤ setToL1 hT' f :=
  setToL1_mono_left' hT hT' (fun s _ _ x => hTT' s x) f
#align measure_theory.L1.set_to_L1_mono_left MeasureTheory.L1.setToL1_mono_left

theorem setToL1_nonneg {T : Set Î± â†’ G' â†’L[â„] G''} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f : Î± â†’â‚[Î¼] G'}
    (hf : 0 â‰¤ f) : 0 â‰¤ setToL1 hT f := by
  suffices âˆ€ f : { g : Î± â†’â‚[Î¼] G' // 0 â‰¤ g }, 0 â‰¤ setToL1 hT f from
    this (âŸ¨f, hfâŸ© : { g : Î± â†’â‚[Î¼] G' // 0 â‰¤ g })
  refine fun g =>
    @isClosed_property { g : Î± â†’â‚â‚›[Î¼] G' // 0 â‰¤ g } { g : Î± â†’â‚[Î¼] G' // 0 â‰¤ g } _ _
      (fun g => 0 â‰¤ setToL1 hT g)
      (denseRange_coeSimpleFuncNonnegToLpNonneg 1 Î¼ G' one_ne_top) ?_ ?_ g
  Â· exact isClosed_le continuous_zero ((setToL1 hT).continuous.comp continuous_induced_dom)
  Â· intro g
    have : (coeSimpleFuncNonnegToLpNonneg 1 Î¼ G' g : Î± â†’â‚[Î¼] G') = (g : Î± â†’â‚â‚›[Î¼] G') := rfl
    rw [this, setToL1_eq_setToL1SCLM]
    exact setToL1S_nonneg (fun s => hT.eq_zero_of_measure_zero) hT.1 hT_nonneg g.2
#align measure_theory.L1.set_to_L1_nonneg MeasureTheory.L1.setToL1_nonneg

theorem setToL1_mono {T : Set Î± â†’ G' â†’L[â„] G''} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f g : Î± â†’â‚[Î¼] G'}
    (hfg : f â‰¤ g) : setToL1 hT f â‰¤ setToL1 hT g := by
  rw [â† sub_nonneg] at hfg âŠ¢
  rw [â† (setToL1 hT).map_sub]
  exact setToL1_nonneg hT hT_nonneg hfg
#align measure_theory.L1.set_to_L1_mono MeasureTheory.L1.setToL1_mono

end Order

theorem norm_setToL1_le_norm_setToL1SCLM (hT : DominatedFinMeasAdditive Î¼ T C) :
    â€–setToL1 hTâ€– â‰¤ â€–setToL1SCLM Î± E Î¼ hTâ€– :=
  calc
    â€–setToL1 hTâ€– â‰¤ (1 : â„â‰¥0) * â€–setToL1SCLM Î± E Î¼ hTâ€– := by
      refine
        ContinuousLinearMap.opNorm_extend_le (setToL1SCLM Î± E Î¼ hT) (coeToLp Î± E â„)
          (simpleFunc.denseRange one_ne_top) fun x => le_of_eq ?_
      rw [NNReal.coe_one, one_mul]
      rfl
    _ = â€–setToL1SCLM Î± E Î¼ hTâ€– := by rw [NNReal.coe_one, one_mul]
#align measure_theory.L1.norm_set_to_L1_le_norm_set_to_L1s_clm MeasureTheory.L1.norm_setToL1_le_norm_setToL1SCLM

theorem norm_setToL1_le_mul_norm (hT : DominatedFinMeasAdditive Î¼ T C) (hC : 0 â‰¤ C)
    (f : Î± â†’â‚[Î¼] E) : â€–setToL1 hT fâ€– â‰¤ C * â€–fâ€– :=
  calc
    â€–setToL1 hT fâ€– â‰¤ â€–setToL1SCLM Î± E Î¼ hTâ€– * â€–fâ€– :=
      ContinuousLinearMap.le_of_opNorm_le _ (norm_setToL1_le_norm_setToL1SCLM hT) _
    _ â‰¤ C * â€–fâ€– := mul_le_mul (norm_setToL1SCLM_le hT hC) le_rfl (norm_nonneg _) hC
#align measure_theory.L1.norm_set_to_L1_le_mul_norm MeasureTheory.L1.norm_setToL1_le_mul_norm

theorem norm_setToL1_le_mul_norm' (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚[Î¼] E) :
    â€–setToL1 hT fâ€– â‰¤ max C 0 * â€–fâ€– :=
  calc
    â€–setToL1 hT fâ€– â‰¤ â€–setToL1SCLM Î± E Î¼ hTâ€– * â€–fâ€– :=
      ContinuousLinearMap.le_of_opNorm_le _ (norm_setToL1_le_norm_setToL1SCLM hT) _
    _ â‰¤ max C 0 * â€–fâ€– :=
      mul_le_mul (norm_setToL1SCLM_le' hT) le_rfl (norm_nonneg _) (le_max_right _ _)
#align measure_theory.L1.norm_set_to_L1_le_mul_norm' MeasureTheory.L1.norm_setToL1_le_mul_norm'

theorem norm_setToL1_le (hT : DominatedFinMeasAdditive Î¼ T C) (hC : 0 â‰¤ C) : â€–setToL1 hTâ€– â‰¤ C :=
  ContinuousLinearMap.opNorm_le_bound _ hC (norm_setToL1_le_mul_norm hT hC)
#align measure_theory.L1.norm_set_to_L1_le MeasureTheory.L1.norm_setToL1_le

theorem norm_setToL1_le' (hT : DominatedFinMeasAdditive Î¼ T C) : â€–setToL1 hTâ€– â‰¤ max C 0 :=
  ContinuousLinearMap.opNorm_le_bound _ (le_max_right _ _) (norm_setToL1_le_mul_norm' hT)
#align measure_theory.L1.norm_set_to_L1_le' MeasureTheory.L1.norm_setToL1_le'

theorem setToL1_lipschitz (hT : DominatedFinMeasAdditive Î¼ T C) :
    LipschitzWith (Real.toNNReal C) (setToL1 hT) :=
  (setToL1 hT).lipschitz.weaken (norm_setToL1_le' hT)
#align measure_theory.L1.set_to_L1_lipschitz MeasureTheory.L1.setToL1_lipschitz

/-- If `fs i â†’ f` in `L1`, then `setToL1 hT (fs i) â†’ setToL1 hT f`. -/
theorem tendsto_setToL1 (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚[Î¼] E) {Î¹}
    (fs : Î¹ â†’ Î± â†’â‚[Î¼] E) {l : Filter Î¹} (hfs : Tendsto fs l (ğ“ f)) :
    Tendsto (fun i => setToL1 hT (fs i)) l (ğ“ <| setToL1 hT f) :=
  ((setToL1 hT).continuous.tendsto _).comp hfs
#align measure_theory.L1.tendsto_set_to_L1 MeasureTheory.L1.tendsto_setToL1

end SetToL1

end L1

section Function

set_option linter.uppercaseLean3 false

variable [CompleteSpace F] {T T' T'' : Set Î± â†’ E â†’L[â„] F} {C C' C'' : â„} {f g : Î± â†’ E}
variable (Î¼ T)

/-- Extend `T : Set Î± â†’ E â†’L[â„] F` to `(Î± â†’ E) â†’ F` (for integrable functions `Î± â†’ E`). We set it to
0 if the function is not integrable. -/
def setToFun (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’ E) : F :=
  if hf : Integrable f Î¼ then L1.setToL1 hT (hf.toL1 f) else 0
#align measure_theory.set_to_fun MeasureTheory.setToFun

variable {Î¼ T}

theorem setToFun_eq (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Integrable f Î¼) :
    setToFun Î¼ T hT f = L1.setToL1 hT (hf.toL1 f) :=
  dif_pos hf
#align measure_theory.set_to_fun_eq MeasureTheory.setToFun_eq

theorem L1.setToFun_eq_setToL1 (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚[Î¼] E) :
    setToFun Î¼ T hT f = L1.setToL1 hT f := by
  rw [setToFun_eq hT (L1.integrable_coeFn f), Integrable.toL1_coeFn]
#align measure_theory.L1.set_to_fun_eq_set_to_L1 MeasureTheory.L1.setToFun_eq_setToL1

theorem setToFun_undef (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Â¬Integrable f Î¼) :
    setToFun Î¼ T hT f = 0 :=
  dif_neg hf
#align measure_theory.set_to_fun_undef MeasureTheory.setToFun_undef

theorem setToFun_non_aEStronglyMeasurable (hT : DominatedFinMeasAdditive Î¼ T C)
    (hf : Â¬AEStronglyMeasurable f Î¼) : setToFun Î¼ T hT f = 0 :=
  setToFun_undef hT (not_and_of_not_left _ hf)
#align measure_theory.set_to_fun_non_ae_strongly_measurable MeasureTheory.setToFun_non_aEStronglyMeasurable

theorem setToFun_congr_left (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (h : T = T') (f : Î± â†’ E) :
    setToFun Î¼ T hT f = setToFun Î¼ T' hT' f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf, L1.setToL1_congr_left T T' hT hT' h]
  Â· simp_rw [setToFun_undef _ hf]
#align measure_theory.set_to_fun_congr_left MeasureTheory.setToFun_congr_left

theorem setToFun_congr_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = T' s)
    (f : Î± â†’ E) : setToFun Î¼ T hT f = setToFun Î¼ T' hT' f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf, L1.setToL1_congr_left' T T' hT hT' h]
  Â· simp_rw [setToFun_undef _ hf]
#align measure_theory.set_to_fun_congr_left' MeasureTheory.setToFun_congr_left'

theorem setToFun_add_left (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (f : Î± â†’ E) :
    setToFun Î¼ (T + T') (hT.add hT') f = setToFun Î¼ T hT f + setToFun Î¼ T' hT' f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf, L1.setToL1_add_left hT hT']
  Â· simp_rw [setToFun_undef _ hf, add_zero]
#align measure_theory.set_to_fun_add_left MeasureTheory.setToFun_add_left

theorem setToFun_add_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (hT'' : DominatedFinMeasAdditive Î¼ T'' C'')
    (h_add : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T'' s = T s + T' s) (f : Î± â†’ E) :
    setToFun Î¼ T'' hT'' f = setToFun Î¼ T hT f + setToFun Î¼ T' hT' f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf, L1.setToL1_add_left' hT hT' hT'' h_add]
  Â· simp_rw [setToFun_undef _ hf, add_zero]
#align measure_theory.set_to_fun_add_left' MeasureTheory.setToFun_add_left'

theorem setToFun_smul_left (hT : DominatedFinMeasAdditive Î¼ T C) (c : â„) (f : Î± â†’ E) :
    setToFun Î¼ (fun s => c â€¢ T s) (hT.smul c) f = c â€¢ setToFun Î¼ T hT f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf, L1.setToL1_smul_left hT c]
  Â· simp_rw [setToFun_undef _ hf, smul_zero]
#align measure_theory.set_to_fun_smul_left MeasureTheory.setToFun_smul_left

theorem setToFun_smul_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼ T' C') (c : â„)
    (h_smul : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T' s = c â€¢ T s) (f : Î± â†’ E) :
    setToFun Î¼ T' hT' f = c â€¢ setToFun Î¼ T hT f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf, L1.setToL1_smul_left' hT hT' c h_smul]
  Â· simp_rw [setToFun_undef _ hf, smul_zero]
#align measure_theory.set_to_fun_smul_left' MeasureTheory.setToFun_smul_left'

@[simp]
theorem setToFun_zero (hT : DominatedFinMeasAdditive Î¼ T C) : setToFun Î¼ T hT (0 : Î± â†’ E) = 0 := by
  erw [setToFun_eq hT (integrable_zero _ _ _), Integrable.toL1_zero, ContinuousLinearMap.map_zero]
#align measure_theory.set_to_fun_zero MeasureTheory.setToFun_zero

@[simp]
theorem setToFun_zero_left {hT : DominatedFinMeasAdditive Î¼ (0 : Set Î± â†’ E â†’L[â„] F) C} :
    setToFun Î¼ 0 hT f = 0 := by
  by_cases hf : Integrable f Î¼
  Â· rw [setToFun_eq hT hf]; exact L1.setToL1_zero_left hT _
  Â· exact setToFun_undef hT hf
#align measure_theory.set_to_fun_zero_left MeasureTheory.setToFun_zero_left

theorem setToFun_zero_left' (hT : DominatedFinMeasAdditive Î¼ T C)
    (h_zero : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0) : setToFun Î¼ T hT f = 0 := by
  by_cases hf : Integrable f Î¼
  Â· rw [setToFun_eq hT hf]; exact L1.setToL1_zero_left' hT h_zero _
  Â· exact setToFun_undef hT hf
#align measure_theory.set_to_fun_zero_left' MeasureTheory.setToFun_zero_left'

theorem setToFun_add (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Integrable f Î¼)
    (hg : Integrable g Î¼) : setToFun Î¼ T hT (f + g) = setToFun Î¼ T hT f + setToFun Î¼ T hT g := by
  rw [setToFun_eq hT (hf.add hg), setToFun_eq hT hf, setToFun_eq hT hg, Integrable.toL1_add,
    (L1.setToL1 hT).map_add]
#align measure_theory.set_to_fun_add MeasureTheory.setToFun_add

theorem setToFun_finset_sum' (hT : DominatedFinMeasAdditive Î¼ T C) {Î¹} (s : Finset Î¹)
    {f : Î¹ â†’ Î± â†’ E} (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) :
    setToFun Î¼ T hT (âˆ‘ i âˆˆ s, f i) = âˆ‘ i âˆˆ s, setToFun Î¼ T hT (f i) := by
  revert hf
  refine Finset.induction_on s ?_ ?_
  Â· intro _
    simp only [setToFun_zero, Finset.sum_empty]
  Â· intro i s his ih hf
    simp only [his, Finset.sum_insert, not_false_iff]
    rw [setToFun_add hT (hf i (Finset.mem_insert_self i s)) _]
    Â· rw [ih fun i hi => hf i (Finset.mem_insert_of_mem hi)]
    Â· convert integrable_finset_sum s fun i hi => hf i (Finset.mem_insert_of_mem hi) with x
      simp
#align measure_theory.set_to_fun_finset_sum' MeasureTheory.setToFun_finset_sum'

theorem setToFun_finset_sum (hT : DominatedFinMeasAdditive Î¼ T C) {Î¹} (s : Finset Î¹) {f : Î¹ â†’ Î± â†’ E}
    (hf : âˆ€ i âˆˆ s, Integrable (f i) Î¼) :
    (setToFun Î¼ T hT fun a => âˆ‘ i âˆˆ s, f i a) = âˆ‘ i âˆˆ s, setToFun Î¼ T hT (f i) := by
  convert setToFun_finset_sum' hT s hf with a; simp
#align measure_theory.set_to_fun_finset_sum MeasureTheory.setToFun_finset_sum

theorem setToFun_neg (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’ E) :
    setToFun Î¼ T hT (-f) = -setToFun Î¼ T hT f := by
  by_cases hf : Integrable f Î¼
  Â· rw [setToFun_eq hT hf, setToFun_eq hT hf.neg, Integrable.toL1_neg,
      (L1.setToL1 hT).map_neg]
  Â· rw [setToFun_undef hT hf, setToFun_undef hT, neg_zero]
    rwa [â† integrable_neg_iff] at hf
#align measure_theory.set_to_fun_neg MeasureTheory.setToFun_neg

theorem setToFun_sub (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Integrable f Î¼)
    (hg : Integrable g Î¼) : setToFun Î¼ T hT (f - g) = setToFun Î¼ T hT f - setToFun Î¼ T hT g := by
  rw [sub_eq_add_neg, sub_eq_add_neg, setToFun_add hT hf hg.neg, setToFun_neg hT g]
#align measure_theory.set_to_fun_sub MeasureTheory.setToFun_sub

theorem setToFun_smul [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]
    (hT : DominatedFinMeasAdditive Î¼ T C) (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ)
    (f : Î± â†’ E) : setToFun Î¼ T hT (c â€¢ f) = c â€¢ setToFun Î¼ T hT f := by
  by_cases hf : Integrable f Î¼
  Â· rw [setToFun_eq hT hf, setToFun_eq hT, Integrable.toL1_smul',
      L1.setToL1_smul hT h_smul c _]
  Â· by_cases hr : c = 0
    Â· rw [hr]; simp
    Â· have hf' : Â¬Integrable (c â€¢ f) Î¼ := by rwa [integrable_smul_iff hr f]
      rw [setToFun_undef hT hf, setToFun_undef hT hf', smul_zero]
#align measure_theory.set_to_fun_smul MeasureTheory.setToFun_smul

theorem setToFun_congr_ae (hT : DominatedFinMeasAdditive Î¼ T C) (h : f =áµ[Î¼] g) :
    setToFun Î¼ T hT f = setToFun Î¼ T hT g := by
  by_cases hfi : Integrable f Î¼
  Â· have hgi : Integrable g Î¼ := hfi.congr h
    rw [setToFun_eq hT hfi, setToFun_eq hT hgi, (Integrable.toL1_eq_toL1_iff f g hfi hgi).2 h]
  Â· have hgi : Â¬Integrable g Î¼ := by rw [integrable_congr h] at hfi; exact hfi
    rw [setToFun_undef hT hfi, setToFun_undef hT hgi]
#align measure_theory.set_to_fun_congr_ae MeasureTheory.setToFun_congr_ae

theorem setToFun_measure_zero (hT : DominatedFinMeasAdditive Î¼ T C) (h : Î¼ = 0) :
    setToFun Î¼ T hT f = 0 := by
  have : f =áµ[Î¼] 0 := by simp [h, EventuallyEq]
  rw [setToFun_congr_ae hT this, setToFun_zero]
#align measure_theory.set_to_fun_measure_zero MeasureTheory.setToFun_measure_zero

theorem setToFun_measure_zero' (hT : DominatedFinMeasAdditive Î¼ T C)
    (h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ Î¼ s = 0) : setToFun Î¼ T hT f = 0 :=
  setToFun_zero_left' hT fun s hs hÎ¼s => hT.eq_zero_of_measure_zero hs (h s hs hÎ¼s)
#align measure_theory.set_to_fun_measure_zero' MeasureTheory.setToFun_measure_zero'

theorem setToFun_toL1 (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Integrable f Î¼) :
    setToFun Î¼ T hT (hf.toL1 f) = setToFun Î¼ T hT f :=
  setToFun_congr_ae hT hf.coeFn_toL1
#align measure_theory.set_to_fun_to_L1 MeasureTheory.setToFun_toL1

theorem setToFun_indicator_const (hT : DominatedFinMeasAdditive Î¼ T C) {s : Set Î±}
    (hs : MeasurableSet s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) :
    setToFun Î¼ T hT (s.indicator fun _ => x) = T s x := by
  rw [setToFun_congr_ae hT (@indicatorConstLp_coeFn _ _ _ 1 _ _ _ hs hÎ¼s x).symm]
  rw [L1.setToFun_eq_setToL1 hT]
  exact L1.setToL1_indicatorConstLp hT hs hÎ¼s x
#align measure_theory.set_to_fun_indicator_const MeasureTheory.setToFun_indicator_const

theorem setToFun_const [IsFiniteMeasure Î¼] (hT : DominatedFinMeasAdditive Î¼ T C) (x : E) :
    (setToFun Î¼ T hT fun _ => x) = T univ x := by
  have : (fun _ : Î± => x) = Set.indicator univ fun _ => x := (indicator_univ _).symm
  rw [this]
  exact setToFun_indicator_const hT MeasurableSet.univ (measure_ne_top _ _) x
#align measure_theory.set_to_fun_const MeasureTheory.setToFun_const

section Order

variable {G' G'' : Type*} [NormedLatticeAddCommGroup G''] [NormedSpace â„ G''] [CompleteSpace G'']
  [NormedLatticeAddCommGroup G'] [NormedSpace â„ G']

theorem setToFun_mono_left' {T T' : Set Î± â†’ E â†’L[â„] G''} {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (hTT' : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, T s x â‰¤ T' s x) (f : Î± â†’ E) :
    setToFun Î¼ T hT f â‰¤ setToFun Î¼ T' hT' f := by
  by_cases hf : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hf]; exact L1.setToL1_mono_left' hT hT' hTT' _
  Â· simp_rw [setToFun_undef _ hf]; rfl
#align measure_theory.set_to_fun_mono_left' MeasureTheory.setToFun_mono_left'

theorem setToFun_mono_left {T T' : Set Î± â†’ E â†’L[â„] G''} {C C' : â„}
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT' : DominatedFinMeasAdditive Î¼ T' C')
    (hTT' : âˆ€ s x, T s x â‰¤ T' s x) (f : Î± â†’â‚[Î¼] E) : setToFun Î¼ T hT f â‰¤ setToFun Î¼ T' hT' f :=
  setToFun_mono_left' hT hT' (fun s _ _ x => hTT' s x) f
#align measure_theory.set_to_fun_mono_left MeasureTheory.setToFun_mono_left

theorem setToFun_nonneg {T : Set Î± â†’ G' â†’L[â„] G''} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f : Î± â†’ G'}
    (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ setToFun Î¼ T hT f := by
  by_cases hfi : Integrable f Î¼
  Â· simp_rw [setToFun_eq _ hfi]
    refine L1.setToL1_nonneg hT hT_nonneg ?_
    rw [â† Lp.coeFn_le]
    have h0 := Lp.coeFn_zero G' 1 Î¼
    have h := Integrable.coeFn_toL1 hfi
    filter_upwards [h0, h, hf] with _ h0a ha hfa
    rw [h0a, ha]
    exact hfa
  Â· simp_rw [setToFun_undef _ hfi]; rfl
#align measure_theory.set_to_fun_nonneg MeasureTheory.setToFun_nonneg

theorem setToFun_mono {T : Set Î± â†’ G' â†’L[â„] G''} {C : â„} (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT_nonneg : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ âˆ€ x, 0 â‰¤ x â†’ 0 â‰¤ T s x) {f g : Î± â†’ G'}
    (hf : Integrable f Î¼) (hg : Integrable g Î¼) (hfg : f â‰¤áµ[Î¼] g) :
    setToFun Î¼ T hT f â‰¤ setToFun Î¼ T hT g := by
  rw [â† sub_nonneg, â† setToFun_sub hT hg hf]
  refine setToFun_nonneg hT hT_nonneg (hfg.mono fun a ha => ?_)
  rw [Pi.sub_apply, Pi.zero_apply, sub_nonneg]
  exact ha
#align measure_theory.set_to_fun_mono MeasureTheory.setToFun_mono

end Order

@[continuity]
theorem continuous_setToFun (hT : DominatedFinMeasAdditive Î¼ T C) :
    Continuous fun f : Î± â†’â‚[Î¼] E => setToFun Î¼ T hT f := by
  simp_rw [L1.setToFun_eq_setToL1 hT]; exact ContinuousLinearMap.continuous _
#align measure_theory.continuous_set_to_fun MeasureTheory.continuous_setToFun

/-- If `F i â†’ f` in `L1`, then `setToFun Î¼ T hT (F i) â†’ setToFun Î¼ T hT f`. -/
theorem tendsto_setToFun_of_L1 (hT : DominatedFinMeasAdditive Î¼ T C) {Î¹} (f : Î± â†’ E)
    (hfi : Integrable f Î¼) {fs : Î¹ â†’ Î± â†’ E} {l : Filter Î¹} (hfsi : âˆ€á¶  i in l, Integrable (fs i) Î¼)
    (hfs : Tendsto (fun i => âˆ«â» x, â€–fs i x - f xâ€–â‚Š âˆ‚Î¼) l (ğ“ 0)) :
    Tendsto (fun i => setToFun Î¼ T hT (fs i)) l (ğ“ <| setToFun Î¼ T hT f) := by
  classical
    let f_lp := hfi.toL1 f
    let F_lp i := if hFi : Integrable (fs i) Î¼ then hFi.toL1 (fs i) else 0
    have tendsto_L1 : Tendsto F_lp l (ğ“ f_lp) := by
      rw [Lp.tendsto_Lp_iff_tendsto_â„’p']
      simp_rw [snorm_one_eq_lintegral_nnnorm, Pi.sub_apply]
      refine (tendsto_congr' ?_).mp hfs
      filter_upwards [hfsi] with i hi
      refine lintegral_congr_ae ?_
      filter_upwards [hi.coeFn_toL1, hfi.coeFn_toL1] with x hxi hxf
      simp_rw [F_lp, dif_pos hi, hxi, hxf]
    suffices Tendsto (fun i => setToFun Î¼ T hT (F_lp i)) l (ğ“ (setToFun Î¼ T hT f)) by
      refine (tendsto_congr' ?_).mp this
      filter_upwards [hfsi] with i hi
      suffices h_ae_eq : F_lp i =áµ[Î¼] fs i from setToFun_congr_ae hT h_ae_eq
      simp_rw [F_lp, dif_pos hi]
      exact hi.coeFn_toL1
    rw [setToFun_congr_ae hT hfi.coeFn_toL1.symm]
    exact ((continuous_setToFun hT).tendsto f_lp).comp tendsto_L1
#align measure_theory.tendsto_set_to_fun_of_L1 MeasureTheory.tendsto_setToFun_of_L1

theorem tendsto_setToFun_approxOn_of_measurable (hT : DominatedFinMeasAdditive Î¼ T C)
    [MeasurableSpace E] [BorelSpace E] {f : Î± â†’ E} {s : Set E} [SeparableSpace s]
    (hfi : Integrable f Î¼) (hfm : Measurable f) (hs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s) {yâ‚€ : E}
    (hâ‚€ : yâ‚€ âˆˆ s) (hâ‚€i : Integrable (fun _ => yâ‚€) Î¼) :
    Tendsto (fun n => setToFun Î¼ T hT (SimpleFunc.approxOn f hfm s yâ‚€ hâ‚€ n)) atTop
      (ğ“ <| setToFun Î¼ T hT f) :=
  tendsto_setToFun_of_L1 hT _ hfi
    (eventually_of_forall (SimpleFunc.integrable_approxOn hfm hfi hâ‚€ hâ‚€i))
    (SimpleFunc.tendsto_approxOn_L1_nnnorm hfm _ hs (hfi.sub hâ‚€i).2)
#align measure_theory.tendsto_set_to_fun_approx_on_of_measurable MeasureTheory.tendsto_setToFun_approxOn_of_measurable

theorem tendsto_setToFun_approxOn_of_measurable_of_range_subset
    (hT : DominatedFinMeasAdditive Î¼ T C) [MeasurableSpace E] [BorelSpace E] {f : Î± â†’ E}
    (fmeas : Measurable f) (hf : Integrable f Î¼) (s : Set E) [SeparableSpace s]
    (hs : range f âˆª {0} âŠ† s) :
    Tendsto (fun n => setToFun Î¼ T hT (SimpleFunc.approxOn f fmeas s 0 (hs <| by simp) n)) atTop
      (ğ“ <| setToFun Î¼ T hT f) := by
  refine tendsto_setToFun_approxOn_of_measurable hT hf fmeas ?_ _ (integrable_zero _ _ _)
  exact eventually_of_forall fun x => subset_closure (hs (Set.mem_union_left _ (mem_range_self _)))
#align measure_theory.tendsto_set_to_fun_approx_on_of_measurable_of_range_subset MeasureTheory.tendsto_setToFun_approxOn_of_measurable_of_range_subset

/-- Auxiliary lemma for `setToFun_congr_measure`: the function sending `f : Î± â†’â‚[Î¼] G` to
`f : Î± â†’â‚[Î¼'] G` is continuous when `Î¼' â‰¤ c' â€¢ Î¼` for `c' â‰  âˆ`. -/
theorem continuous_L1_toL1 {Î¼' : Measure Î±} (c' : â„â‰¥0âˆ) (hc' : c' â‰  âˆ) (hÎ¼'_le : Î¼' â‰¤ c' â€¢ Î¼) :
    Continuous fun f : Î± â†’â‚[Î¼] G =>
      (Integrable.of_measure_le_smul c' hc' hÎ¼'_le (L1.integrable_coeFn f)).toL1 f := by
  by_cases hc'0 : c' = 0
  Â· have hÎ¼'0 : Î¼' = 0 := by rw [â† Measure.nonpos_iff_eq_zero']; refine hÎ¼'_le.trans ?_; simp [hc'0]
    have h_im_zero :
      (fun f : Î± â†’â‚[Î¼] G =>
          (Integrable.of_measure_le_smul c' hc' hÎ¼'_le (L1.integrable_coeFn f)).toL1 f) =
        0 := by
      ext1 f; ext1; simp_rw [hÎ¼'0]; simp only [ae_zero, EventuallyEq, eventually_bot]
    rw [h_im_zero]
    exact continuous_zero
  rw [Metric.continuous_iff]
  intro f Îµ hÎµ_pos
  use Îµ / 2 / c'.toReal
  refine âŸ¨div_pos (half_pos hÎµ_pos) (toReal_pos hc'0 hc'), ?_âŸ©
  intro g hfg
  rw [Lp.dist_def] at hfg âŠ¢
  let h_int := fun f' : Î± â†’â‚[Î¼] G => (L1.integrable_coeFn f').of_measure_le_smul c' hc' hÎ¼'_le
  have :
    snorm (â‡‘(Integrable.toL1 g (h_int g)) - â‡‘(Integrable.toL1 f (h_int f))) 1 Î¼' =
      snorm (â‡‘g - â‡‘f) 1 Î¼' :=
    snorm_congr_ae ((Integrable.coeFn_toL1 _).sub (Integrable.coeFn_toL1 _))
  rw [this]
  have h_snorm_ne_top : snorm (â‡‘g - â‡‘f) 1 Î¼ â‰  âˆ := by
    rw [â† snorm_congr_ae (Lp.coeFn_sub _ _)]; exact Lp.snorm_ne_top _
  have h_snorm_ne_top' : snorm (â‡‘g - â‡‘f) 1 Î¼' â‰  âˆ := by
    refine ((snorm_mono_measure _ hÎ¼'_le).trans_lt ?_).ne
    rw [snorm_smul_measure_of_ne_zero hc'0, smul_eq_mul]
    refine ENNReal.mul_lt_top ?_ h_snorm_ne_top
    simp [hc', hc'0]
  calc
    (snorm (â‡‘g - â‡‘f) 1 Î¼').toReal â‰¤ (c' * snorm (â‡‘g - â‡‘f) 1 Î¼).toReal := by
      rw [toReal_le_toReal h_snorm_ne_top' (ENNReal.mul_ne_top hc' h_snorm_ne_top)]
      refine (snorm_mono_measure (â‡‘g - â‡‘f) hÎ¼'_le).trans ?_
      rw [snorm_smul_measure_of_ne_zero hc'0, smul_eq_mul]
      simp
    _ = c'.toReal * (snorm (â‡‘g - â‡‘f) 1 Î¼).toReal := toReal_mul
    _ â‰¤ c'.toReal * (Îµ / 2 / c'.toReal) :=
      (mul_le_mul le_rfl hfg.le toReal_nonneg toReal_nonneg)
    _ = Îµ / 2 := by
      refine mul_div_cancelâ‚€ (Îµ / 2) ?_; rw [Ne, toReal_eq_zero_iff]; simp [hc', hc'0]
    _ < Îµ := half_lt_self hÎµ_pos
#align measure_theory.continuous_L1_to_L1 MeasureTheory.continuous_L1_toL1

theorem setToFun_congr_measure_of_integrable {Î¼' : Measure Î±} (c' : â„â‰¥0âˆ) (hc' : c' â‰  âˆ)
    (hÎ¼'_le : Î¼' â‰¤ c' â€¢ Î¼) (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼' T C') (f : Î± â†’ E) (hfÎ¼ : Integrable f Î¼) :
    setToFun Î¼ T hT f = setToFun Î¼' T hT' f := by
  -- integrability for `Î¼` implies integrability for `Î¼'`.
  have h_int : âˆ€ g : Î± â†’ E, Integrable g Î¼ â†’ Integrable g Î¼' := fun g hg =>
    Integrable.of_measure_le_smul c' hc' hÎ¼'_le hg
  -- We use `Integrable.induction`
  apply hfÎ¼.induction (P := fun f => setToFun Î¼ T hT f = setToFun Î¼' T hT' f)
  Â· intro c s hs hÎ¼s
    have hÎ¼'s : Î¼' s â‰  âˆ := by
      refine ((hÎ¼'_le s).trans_lt ?_).ne
      rw [Measure.smul_apply, smul_eq_mul]
      exact ENNReal.mul_lt_top hc' hÎ¼s.ne
    rw [setToFun_indicator_const hT hs hÎ¼s.ne, setToFun_indicator_const hT' hs hÎ¼'s]
  Â· intro fâ‚‚ gâ‚‚ _ hfâ‚‚ hgâ‚‚ h_eq_f h_eq_g
    rw [setToFun_add hT hfâ‚‚ hgâ‚‚, setToFun_add hT' (h_int fâ‚‚ hfâ‚‚) (h_int gâ‚‚ hgâ‚‚), h_eq_f, h_eq_g]
  Â· refine isClosed_eq (continuous_setToFun hT) ?_
    have :
      (fun f : Î± â†’â‚[Î¼] E => setToFun Î¼' T hT' f) = fun f : Î± â†’â‚[Î¼] E =>
        setToFun Î¼' T hT' ((h_int f (L1.integrable_coeFn f)).toL1 f) := by
      ext1 f; exact setToFun_congr_ae hT' (Integrable.coeFn_toL1 _).symm
    rw [this]
    exact (continuous_setToFun hT').comp (continuous_L1_toL1 c' hc' hÎ¼'_le)
  Â· intro fâ‚‚ gâ‚‚ hfg _ hf_eq
    have hfg' : fâ‚‚ =áµ[Î¼'] gâ‚‚ := (Measure.absolutelyContinuous_of_le_smul hÎ¼'_le).ae_eq hfg
    rw [â† setToFun_congr_ae hT hfg, hf_eq, setToFun_congr_ae hT' hfg']
#align measure_theory.set_to_fun_congr_measure_of_integrable MeasureTheory.setToFun_congr_measure_of_integrable

theorem setToFun_congr_measure {Î¼' : Measure Î±} (c c' : â„â‰¥0âˆ) (hc : c â‰  âˆ) (hc' : c' â‰  âˆ)
    (hÎ¼_le : Î¼ â‰¤ c â€¢ Î¼') (hÎ¼'_le : Î¼' â‰¤ c' â€¢ Î¼) (hT : DominatedFinMeasAdditive Î¼ T C)
    (hT' : DominatedFinMeasAdditive Î¼' T C') (f : Î± â†’ E) :
    setToFun Î¼ T hT f = setToFun Î¼' T hT' f := by
  by_cases hf : Integrable f Î¼
  Â· exact setToFun_congr_measure_of_integrable c' hc' hÎ¼'_le hT hT' f hf
  Â· -- if `f` is not integrable, both `setToFun` are 0.
    have h_int : âˆ€ g : Î± â†’ E, Â¬Integrable g Î¼ â†’ Â¬Integrable g Î¼' := fun g =>
      mt fun h => h.of_measure_le_smul _ hc hÎ¼_le
    simp_rw [setToFun_undef _ hf, setToFun_undef _ (h_int f hf)]
#align measure_theory.set_to_fun_congr_measure MeasureTheory.setToFun_congr_measure

theorem setToFun_congr_measure_of_add_right {Î¼' : Measure Î±}
    (hT_add : DominatedFinMeasAdditive (Î¼ + Î¼') T C') (hT : DominatedFinMeasAdditive Î¼ T C)
    (f : Î± â†’ E) (hf : Integrable f (Î¼ + Î¼')) :
    setToFun (Î¼ + Î¼') T hT_add f = setToFun Î¼ T hT f := by
  refine setToFun_congr_measure_of_integrable 1 one_ne_top ?_ hT_add hT f hf
  rw [one_smul]
  nth_rw 1 [â† add_zero Î¼]
  exact add_le_add le_rfl bot_le
#align measure_theory.set_to_fun_congr_measure_of_add_right MeasureTheory.setToFun_congr_measure_of_add_right

theorem setToFun_congr_measure_of_add_left {Î¼' : Measure Î±}
    (hT_add : DominatedFinMeasAdditive (Î¼ + Î¼') T C') (hT : DominatedFinMeasAdditive Î¼' T C)
    (f : Î± â†’ E) (hf : Integrable f (Î¼ + Î¼')) :
    setToFun (Î¼ + Î¼') T hT_add f = setToFun Î¼' T hT f := by
  refine setToFun_congr_measure_of_integrable 1 one_ne_top ?_ hT_add hT f hf
  rw [one_smul]
  nth_rw 1 [â† zero_add Î¼']
  exact add_le_add bot_le le_rfl
#align measure_theory.set_to_fun_congr_measure_of_add_left MeasureTheory.setToFun_congr_measure_of_add_left

theorem setToFun_top_smul_measure (hT : DominatedFinMeasAdditive (âˆ â€¢ Î¼) T C) (f : Î± â†’ E) :
    setToFun (âˆ â€¢ Î¼) T hT f = 0 := by
  refine setToFun_measure_zero' hT fun s _ hÎ¼s => ?_
  rw [lt_top_iff_ne_top] at hÎ¼s
  simp only [true_and_iff, Measure.smul_apply, ENNReal.mul_eq_top, eq_self_iff_true,
    top_ne_zero, Ne, not_false_iff, not_or, Classical.not_not, smul_eq_mul] at hÎ¼s
  simp only [hÎ¼s.right, Measure.smul_apply, mul_zero, smul_eq_mul]
#align measure_theory.set_to_fun_top_smul_measure MeasureTheory.setToFun_top_smul_measure

theorem setToFun_congr_smul_measure (c : â„â‰¥0âˆ) (hc_ne_top : c â‰  âˆ)
    (hT : DominatedFinMeasAdditive Î¼ T C) (hT_smul : DominatedFinMeasAdditive (c â€¢ Î¼) T C')
    (f : Î± â†’ E) : setToFun Î¼ T hT f = setToFun (c â€¢ Î¼) T hT_smul f := by
  by_cases hc0 : c = 0
  Â· simp [hc0] at hT_smul
    have h : âˆ€ s, MeasurableSet s â†’ Î¼ s < âˆ â†’ T s = 0 := fun s hs _ => hT_smul.eq_zero hs
    rw [setToFun_zero_left' _ h, setToFun_measure_zero]
    simp [hc0]
  refine setToFun_congr_measure câ»Â¹ c ?_ hc_ne_top (le_of_eq ?_) le_rfl hT hT_smul f
  Â· simp [hc0]
  Â· rw [smul_smul, ENNReal.inv_mul_cancel hc0 hc_ne_top, one_smul]
#align measure_theory.set_to_fun_congr_smul_measure MeasureTheory.setToFun_congr_smul_measure

theorem norm_setToFun_le_mul_norm (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚[Î¼] E)
    (hC : 0 â‰¤ C) : â€–setToFun Î¼ T hT fâ€– â‰¤ C * â€–fâ€– := by
  rw [L1.setToFun_eq_setToL1]; exact L1.norm_setToL1_le_mul_norm hT hC f
#align measure_theory.norm_set_to_fun_le_mul_norm MeasureTheory.norm_setToFun_le_mul_norm

theorem norm_setToFun_le_mul_norm' (hT : DominatedFinMeasAdditive Î¼ T C) (f : Î± â†’â‚[Î¼] E) :
    â€–setToFun Î¼ T hT fâ€– â‰¤ max C 0 * â€–fâ€– := by
  rw [L1.setToFun_eq_setToL1]; exact L1.norm_setToL1_le_mul_norm' hT f
#align measure_theory.norm_set_to_fun_le_mul_norm' MeasureTheory.norm_setToFun_le_mul_norm'

theorem norm_setToFun_le (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Integrable f Î¼) (hC : 0 â‰¤ C) :
    â€–setToFun Î¼ T hT fâ€– â‰¤ C * â€–hf.toL1 fâ€– := by
  rw [setToFun_eq hT hf]; exact L1.norm_setToL1_le_mul_norm hT hC _
#align measure_theory.norm_set_to_fun_le MeasureTheory.norm_setToFun_le

theorem norm_setToFun_le' (hT : DominatedFinMeasAdditive Î¼ T C) (hf : Integrable f Î¼) :
    â€–setToFun Î¼ T hT fâ€– â‰¤ max C 0 * â€–hf.toL1 fâ€– := by
  rw [setToFun_eq hT hf]; exact L1.norm_setToL1_le_mul_norm' hT _
#align measure_theory.norm_set_to_fun_le' MeasureTheory.norm_setToFun_le'

/-- Lebesgue dominated convergence theorem provides sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their image by
  `setToFun`.
  We could weaken the condition `bound_integrable` to require `HasFiniteIntegral bound Î¼` instead
  (i.e. not requiring that `bound` is measurable), but in all applications proving integrability
  is easier. -/
theorem tendsto_setToFun_of_dominated_convergence (hT : DominatedFinMeasAdditive Î¼ T C)
    {fs : â„• â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„)
    (fs_measurable : âˆ€ n, AEStronglyMeasurable (fs n) Î¼) (bound_integrable : Integrable bound Î¼)
    (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, â€–fs n aâ€– â‰¤ bound a)
    (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => fs n a) atTop (ğ“ (f a))) :
    Tendsto (fun n => setToFun Î¼ T hT (fs n)) atTop (ğ“ <| setToFun Î¼ T hT f) := by
  -- `f` is a.e.-measurable, since it is the a.e.-pointwise limit of a.e.-measurable functions.
  have f_measurable : AEStronglyMeasurable f Î¼ :=
    aestronglyMeasurable_of_tendsto_ae _ fs_measurable h_lim
  -- all functions we consider are integrable
  have fs_int : âˆ€ n, Integrable (fs n) Î¼ := fun n =>
    bound_integrable.mono' (fs_measurable n) (h_bound _)
  have f_int : Integrable f Î¼ :=
    âŸ¨f_measurable,
      hasFiniteIntegral_of_dominated_convergence bound_integrable.hasFiniteIntegral h_bound
        h_limâŸ©
  -- it suffices to prove the result for the corresponding L1 functions
  suffices
    Tendsto (fun n => L1.setToL1 hT ((fs_int n).toL1 (fs n))) atTop
      (ğ“ (L1.setToL1 hT (f_int.toL1 f))) by
    convert this with n
    Â· exact setToFun_eq hT (fs_int n)
    Â· exact setToFun_eq hT f_int
  -- the convergence of setToL1 follows from the convergence of the L1 functions
  refine L1.tendsto_setToL1 hT _ _ ?_
  -- up to some rewriting, what we need to prove is `h_lim`
  rw [tendsto_iff_norm_sub_tendsto_zero]
  have lintegral_norm_tendsto_zero :
    Tendsto (fun n => ENNReal.toReal <| âˆ«â» a, ENNReal.ofReal â€–fs n a - f aâ€– âˆ‚Î¼) atTop (ğ“ 0) :=
    (tendsto_toReal zero_ne_top).comp
      (tendsto_lintegral_norm_of_dominated_convergence fs_measurable
        bound_integrable.hasFiniteIntegral h_bound h_lim)
  convert lintegral_norm_tendsto_zero with n
  rw [L1.norm_def]
  congr 1
  refine lintegral_congr_ae ?_
  rw [â† Integrable.toL1_sub]
  refine ((fs_int n).sub f_int).coeFn_toL1.mono fun x hx => ?_
  dsimp only
  rw [hx, ofReal_norm_eq_coe_nnnorm, Pi.sub_apply]
#align measure_theory.tendsto_set_to_fun_of_dominated_convergence MeasureTheory.tendsto_setToFun_of_dominated_convergence

/-- Lebesgue dominated convergence theorem for filters with a countable basis -/
theorem tendsto_setToFun_filter_of_dominated_convergence (hT : DominatedFinMeasAdditive Î¼ T C) {Î¹}
    {l : Filter Î¹} [l.IsCountablyGenerated] {fs : Î¹ â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„)
    (hfs_meas : âˆ€á¶  n in l, AEStronglyMeasurable (fs n) Î¼)
    (h_bound : âˆ€á¶  n in l, âˆ€áµ a âˆ‚Î¼, â€–fs n aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_lim : âˆ€áµ a âˆ‚Î¼, Tendsto (fun n => fs n a) l (ğ“ (f a))) :
    Tendsto (fun n => setToFun Î¼ T hT (fs n)) l (ğ“ <| setToFun Î¼ T hT f) := by
  rw [tendsto_iff_seq_tendsto]
  intro x xl
  have hxl : âˆ€ s âˆˆ l, âˆƒ a, âˆ€ b â‰¥ a, x b âˆˆ s := by rwa [tendsto_atTop'] at xl
  have h :
    { x : Î¹ | (fun n => AEStronglyMeasurable (fs n) Î¼) x } âˆ©
        { x : Î¹ | (fun n => âˆ€áµ a âˆ‚Î¼, â€–fs n aâ€– â‰¤ bound a) x } âˆˆ l :=
    inter_mem hfs_meas h_bound
  obtain âŸ¨k, hâŸ© := hxl _ h
  rw [â† tendsto_add_atTop_iff_nat k]
  refine tendsto_setToFun_of_dominated_convergence hT bound ?_ bound_integrable ?_ ?_
  Â· exact fun n => (h _ (self_le_add_left _ _)).1
  Â· exact fun n => (h _ (self_le_add_left _ _)).2
  Â· filter_upwards [h_lim]
    refine fun a h_lin => @Tendsto.comp _ _ _ (fun n => x (n + k)) (fun n => fs n a) _ _ _ h_lin ?_
    rw [tendsto_add_atTop_iff_nat]
    assumption
#align measure_theory.tendsto_set_to_fun_filter_of_dominated_convergence MeasureTheory.tendsto_setToFun_filter_of_dominated_convergence

variable {X : Type*} [TopologicalSpace X] [FirstCountableTopology X]

theorem continuousWithinAt_setToFun_of_dominated (hT : DominatedFinMeasAdditive Î¼ T C)
    {fs : X â†’ Î± â†’ E} {xâ‚€ : X} {bound : Î± â†’ â„} {s : Set X}
    (hfs_meas : âˆ€á¶  x in ğ“[s] xâ‚€, AEStronglyMeasurable (fs x) Î¼)
    (h_bound : âˆ€á¶  x in ğ“[s] xâ‚€, âˆ€áµ a âˆ‚Î¼, â€–fs x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, ContinuousWithinAt (fun x => fs x a) s xâ‚€) :
    ContinuousWithinAt (fun x => setToFun Î¼ T hT (fs x)) s xâ‚€ :=
  tendsto_setToFun_filter_of_dominated_convergence hT bound â€¹_â€º â€¹_â€º â€¹_â€º â€¹_â€º
#align measure_theory.continuous_within_at_set_to_fun_of_dominated MeasureTheory.continuousWithinAt_setToFun_of_dominated

theorem continuousAt_setToFun_of_dominated (hT : DominatedFinMeasAdditive Î¼ T C) {fs : X â†’ Î± â†’ E}
    {xâ‚€ : X} {bound : Î± â†’ â„} (hfs_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (fs x) Î¼)
    (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ a âˆ‚Î¼, â€–fs x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, ContinuousAt (fun x => fs x a) xâ‚€) :
    ContinuousAt (fun x => setToFun Î¼ T hT (fs x)) xâ‚€ :=
  tendsto_setToFun_filter_of_dominated_convergence hT bound â€¹_â€º â€¹_â€º â€¹_â€º â€¹_â€º
#align measure_theory.continuous_at_set_to_fun_of_dominated MeasureTheory.continuousAt_setToFun_of_dominated

theorem continuousOn_setToFun_of_dominated (hT : DominatedFinMeasAdditive Î¼ T C) {fs : X â†’ Î± â†’ E}
    {bound : Î± â†’ â„} {s : Set X} (hfs_meas : âˆ€ x âˆˆ s, AEStronglyMeasurable (fs x) Î¼)
    (h_bound : âˆ€ x âˆˆ s, âˆ€áµ a âˆ‚Î¼, â€–fs x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, ContinuousOn (fun x => fs x a) s) :
    ContinuousOn (fun x => setToFun Î¼ T hT (fs x)) s := by
  intro x hx
  refine continuousWithinAt_setToFun_of_dominated hT ?_ ?_ bound_integrable ?_
  Â· filter_upwards [self_mem_nhdsWithin] with x hx using hfs_meas x hx
  Â· filter_upwards [self_mem_nhdsWithin] with x hx using h_bound x hx
  Â· filter_upwards [h_cont] with a ha using ha x hx
#align measure_theory.continuous_on_set_to_fun_of_dominated MeasureTheory.continuousOn_setToFun_of_dominated

theorem continuous_setToFun_of_dominated (hT : DominatedFinMeasAdditive Î¼ T C) {fs : X â†’ Î± â†’ E}
    {bound : Î± â†’ â„} (hfs_meas : âˆ€ x, AEStronglyMeasurable (fs x) Î¼)
    (h_bound : âˆ€ x, âˆ€áµ a âˆ‚Î¼, â€–fs x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_cont : âˆ€áµ a âˆ‚Î¼, Continuous fun x => fs x a) : Continuous fun x => setToFun Î¼ T hT (fs x) :=
  continuous_iff_continuousAt.mpr fun xâ‚€ =>
    continuousAt_setToFun_of_dominated hT (eventually_of_forall hfs_meas)
        (eventually_of_forall h_bound) â€¹_â€º <|
      h_cont.mono fun _ => Continuous.continuousAt
#align measure_theory.continuous_set_to_fun_of_dominated MeasureTheory.continuous_setToFun_of_dominated

end Function

end MeasureTheory
