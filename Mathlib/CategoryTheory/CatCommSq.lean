/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Equivalence

/-!
# 2-commutative squares of functors

Similarly as `CommSq.lean` defines the notion of commutative squares,
this file introduces the notion of 2-commutative squares of functors.

If `T : C‚ÇÅ ‚•§ C‚ÇÇ`, `L : C‚ÇÅ ‚•§ C‚ÇÉ`, `R : C‚ÇÇ ‚•§ C‚ÇÑ`, `B : C‚ÇÉ ‚•§ C‚ÇÑ` are functors,
then `[CatCommSq T L R B]` contains the datum of an isomorphism `T ‚ãô R ‚âÖ L ‚ãô B`.

Future work: using this notion in the development of the localization of categories
(e.g. localization of adjunctions).

-/

namespace CategoryTheory

open Category

variable {C‚ÇÅ C‚ÇÇ C‚ÇÉ C‚ÇÑ C‚ÇÖ C‚ÇÜ : Type*} [Category C‚ÇÅ] [Category C‚ÇÇ] [Category C‚ÇÉ] [Category C‚ÇÑ]
  [Category C‚ÇÖ] [Category C‚ÇÜ]
  (T : C‚ÇÅ ‚•§ C‚ÇÇ) (L : C‚ÇÅ ‚•§ C‚ÇÉ) (R : C‚ÇÇ ‚•§ C‚ÇÑ) (B : C‚ÇÉ ‚•§ C‚ÇÑ)

/-- `CatCommSq T L R B` expresses that there is a 2-commutative square of functors, where
the functors `T`, `L`, `R` and `B` are respectively the left, top, right and bottom functors
of the square. -/
@[ext]
class CatCommSq where
  /-- the isomorphism corresponding to a 2-commutative diagram -/
  iso' : T ‚ãô R ‚âÖ L ‚ãô B

namespace CatCommSq

/-- Assuming `[CatCommSq T L R B]`, `iso T L R B` is the isomorphism `T ‚ãô R ‚âÖ L ‚ãô B`
given by the 2-commutative square. -/
def iso [h : CatCommSq T L R B] : T ‚ãô R ‚âÖ L ‚ãô B := h.iso'

/-- Horizontal composition of 2-commutative squares -/
@[simps! iso'_hom_app iso'_inv_app]
def hComp (T‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÇ) (T‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÉ) (V‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÑ) (V‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÖ) (V‚ÇÉ : C‚ÇÉ ‚•§ C‚ÇÜ)
    (B‚ÇÅ : C‚ÇÑ ‚•§ C‚ÇÖ) (B‚ÇÇ : C‚ÇÖ ‚•§ C‚ÇÜ) [CatCommSq T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ] [CatCommSq T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ] :
    CatCommSq (T‚ÇÅ ‚ãô T‚ÇÇ) V‚ÇÅ V‚ÇÉ (B‚ÇÅ ‚ãô B‚ÇÇ) where
  iso' := Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft T‚ÇÅ (iso T‚ÇÇ V‚ÇÇ V‚ÇÉ B‚ÇÇ) ‚â™‚â´
    (Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight (iso T‚ÇÅ V‚ÇÅ V‚ÇÇ B‚ÇÅ) B‚ÇÇ ‚â™‚â´
    Functor.associator _ _ _

/-- Vertical composition of 2-commutative squares -/
@[simps! iso'_hom_app iso'_inv_app]
def vComp (L‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÇ) (L‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÉ) (H‚ÇÅ : C‚ÇÅ ‚•§ C‚ÇÑ) (H‚ÇÇ : C‚ÇÇ ‚•§ C‚ÇÖ) (H‚ÇÉ : C‚ÇÉ ‚•§ C‚ÇÜ)
    (R‚ÇÅ : C‚ÇÑ ‚•§ C‚ÇÖ) (R‚ÇÇ : C‚ÇÖ ‚•§ C‚ÇÜ) [CatCommSq H‚ÇÅ L‚ÇÅ R‚ÇÅ H‚ÇÇ] [CatCommSq H‚ÇÇ L‚ÇÇ R‚ÇÇ H‚ÇÉ] :
    CatCommSq H‚ÇÅ (L‚ÇÅ ‚ãô L‚ÇÇ) (R‚ÇÅ ‚ãô R‚ÇÇ) H‚ÇÉ where
  iso' := (Functor.associator _ _ _).symm ‚â™‚â´ isoWhiskerRight (iso H‚ÇÅ L‚ÇÅ R‚ÇÅ H‚ÇÇ) R‚ÇÇ ‚â™‚â´
      Functor.associator _ _ _ ‚â™‚â´ isoWhiskerLeft L‚ÇÅ (iso H‚ÇÇ L‚ÇÇ R‚ÇÇ H‚ÇÉ) ‚â™‚â´
      (Functor.associator _ _ _).symm

section

variable (T : C‚ÇÅ ‚âå C‚ÇÇ) (L : C‚ÇÅ ‚•§ C‚ÇÉ) (R : C‚ÇÇ ‚•§ C‚ÇÑ) (B : C‚ÇÉ ‚âå C‚ÇÑ)

/-- Horizontal inverse of a 2-commutative square -/
@[simps! iso'_hom_app iso'_inv_app]
def hInv (_ : CatCommSq T.functor L R B.functor) : CatCommSq T.inverse R L B.inverse where
  iso' := isoWhiskerLeft _ (L.rightUnitor.symm ‚â™‚â´ isoWhiskerLeft L B.unitIso ‚â™‚â´
      (Functor.associator _ _ _).symm ‚â™‚â´
      isoWhiskerRight (iso T.functor L R B.functor).symm B.inverse ‚â™‚â´
      Functor.associator _ _ _  ) ‚â™‚â´ (Functor.associator _ _ _).symm ‚â™‚â´
      isoWhiskerRight T.counitIso _ ‚â™‚â´ Functor.leftUnitor _

lemma hInv_hInv (h : CatCommSq T.functor L R B.functor) :
  hInv T.symm R L B.symm (hInv T L R B h) = h := by
    ext X
    -- ‚ä¢ NatTrans.app iso'.hom X = NatTrans.app iso'.hom X
    erw [‚Üê cancel_mono (B.functor.map (L.map (T.unitIso.hom.app X))),
      ‚Üê h.iso'.hom.naturality (T.unitIso.hom.app X), hInv_iso'_hom_app, hInv_iso'_inv_app]
    dsimp
    -- ‚ä¢ (NatTrans.app B.counitIso.inv (R.obj (T.functor.obj X)) ‚â´ B.functor.map (B.i ‚Ä¶
    simp only [Functor.comp_obj, assoc, ‚Üê Functor.map_comp, Iso.inv_hom_id_app,
      Equivalence.counitInv_app_functor, Functor.map_id]
    simp only [Functor.map_comp, Equivalence.fun_inv_map, assoc,
      Equivalence.counitInv_functor_comp, comp_id, Iso.inv_hom_id_app_assoc]
    rfl
    -- üéâ no goals

/-- In a square of categories, when the top and bottom functors are part
of equivalence of categorires, it is equivalent to show 2-commutativity for
the functors of these equivalences or for their inverses. -/
def hInvEquiv : CatCommSq T.functor L R B.functor ‚âÉ CatCommSq T.inverse R L B.inverse where
  toFun := hInv T L R B
  invFun := hInv T.symm R L B.symm
  left_inv := hInv_hInv T L R B
  right_inv := hInv_hInv T.symm R L B.symm

end

instance hInv' [h : CatCommSq T L R B] [IsEquivalence T] [IsEquivalence B] :
    CatCommSq T.inv R L B.inv :=
  hInv T.asEquivalence L R B.asEquivalence h

end CatCommSq

end CategoryTheory
