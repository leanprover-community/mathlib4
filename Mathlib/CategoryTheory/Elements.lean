/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Comma.StructuredArrow.Basic
import Mathlib.CategoryTheory.Category.Cat

/-!
# The category of elements

This file defines the category of elements, also known as (a special case of) the Grothendieck
construction.

Given a functor `F : C ‚•§ Type`, an object of `F.Elements` is a pair `(X : C, x : F.obj X)`.
A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.

## Implementation notes

This construction is equivalent to a special case of a comma construction, so this is mostly just a
more convenient API. We prove the equivalence in
`CategoryTheory.CategoryOfElements.structuredArrowEquivalence`.

## References
* [Emily Riehl, *Category Theory in Context*, Section 2.4][riehl2017]
* <https://en.wikipedia.org/wiki/Category_of_elements>
* <https://ncatlab.org/nlab/show/category+of+elements>

## Tags
category of elements, Grothendieck construction, comma category
-/


namespace CategoryTheory

universe w v u

variable {C : Type u} [Category.{v} C]

/-- The type of objects for the category of elements of a functor `F : C ‚•§ Type`
is a pair `(X : C, x : F.obj X)`.
-/
def Functor.Elements (F : C ‚•§ Type w) :=
  Œ£c : C, F.obj c

/-- Constructor for the type `F.Elements` when `F` is a functor to types. -/
abbrev Functor.elementsMk (F : C ‚•§ Type w) (X : C) (x : F.obj X) : F.Elements := ‚ü®X, x‚ü©

lemma Functor.Elements.ext {F : C ‚•§ Type w} (x y : F.Elements) (h‚ÇÅ : x.fst = y.fst)
    (h‚ÇÇ : F.map (eqToHom h‚ÇÅ) x.snd = y.snd) : x = y := by
  cases x
  cases y
  cases h‚ÇÅ
  simp only [eqToHom_refl, FunctorToTypes.map_id_apply] at h‚ÇÇ
  simp [h‚ÇÇ]

/-- The category structure on `F.Elements`, for `F : C ‚•§ Type`.
    A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.
-/
instance categoryOfElements (F : C ‚•§ Type w) : Category.{v} F.Elements where
  Hom p q := { f : p.1 ‚ü∂ q.1 // (F.map f) p.2 = q.2 }
  id p := ‚ü®ùüô p.1, by simp‚ü©
  comp {X Y Z} f g := ‚ü®f.val ‚â´ g.val, by simp [f.2, g.2]‚ü©

/-- Natural transformations are mapped to functors between category of elements -/
@[simps]
def NatTrans.mapElements {F G : C ‚•§ Type w} (œÜ : F ‚ü∂ G) : F.Elements ‚•§ G.Elements where
  obj := fun ‚ü®X, x‚ü© ‚Ü¶ ‚ü®_, œÜ.app X x‚ü©
  map {p q} := fun ‚ü®f, h‚ü© ‚Ü¶ ‚ü®f, by have hb := congrFun (œÜ.naturality f) p.2; aesop_cat‚ü©

/-- The functor mapping functors `C ‚•§ Type w` to their category of elements -/
@[simps]
def Functor.elementsFunctor : (C ‚•§ Type w) ‚•§ Cat where
  obj F := Cat.of F.Elements
  map n := NatTrans.mapElements n

namespace CategoryOfElements

/-- Constructor for morphisms in the category of elements of a functor to types. -/
@[simps]
def homMk {F : C ‚•§ Type w} (x y : F.Elements) (f : x.1 ‚ü∂ y.1) (hf : F.map f x.snd = y.snd) :
    x ‚ü∂ y :=
  ‚ü®f, hf‚ü©

@[ext]
theorem ext (F : C ‚•§ Type w) {x y : F.Elements} (f g : x ‚ü∂ y) (w : f.val = g.val) : f = g :=
  Subtype.ext_val w

@[simp]
theorem comp_val {F : C ‚•§ Type w} {p q r : F.Elements} {f : p ‚ü∂ q} {g : q ‚ü∂ r} :
    (f ‚â´ g).val = f.val ‚â´ g.val :=
  rfl

@[simp]
theorem id_val {F : C ‚•§ Type w} {p : F.Elements} : (ùüô p : p ‚ü∂ p).val = ùüô p.1 :=
  rfl

@[simp]
theorem map_snd {F : C ‚•§ Type w} {p q : F.Elements} (f : p ‚ü∂ q) : (F.map f.val) p.2 = q.2 :=
  f.property

/-- Constructor for isomorphisms in the category of elements of a functor to types. -/
@[simps]
def isoMk {F : C ‚•§ Type w} (x y : F.Elements) (e : x.1 ‚âÖ y.1) (he : F.map e.hom x.snd = y.snd) :
    x ‚âÖ y where
  hom := homMk x y e.hom he
  inv := homMk y x e.inv (by rw [‚Üê he, FunctorToTypes.map_inv_map_hom_apply])

end CategoryOfElements

instance groupoidOfElements {G : Type u} [Groupoid.{v} G] (F : G ‚•§ Type w) :
    Groupoid F.Elements where
  inv {p q} f :=
    ‚ü®Groupoid.inv f.val,
      calc
        F.map (Groupoid.inv f.val) q.2 = F.map (Groupoid.inv f.val) (F.map f.val p.2) := by rw [f.2]
        _ = (F.map f.val ‚â´ F.map (Groupoid.inv f.val)) p.2 := rfl
        _ = p.2 := by
          rw [‚Üê F.map_comp]
          simp
        ‚ü©
  inv_comp _ := by
    ext
    simp
  comp_inv _ := by
    ext
    simp

namespace CategoryOfElements

variable (F : C ‚•§ Type w)

/-- The functor out of the category of elements which forgets the element. -/
@[simps]
def œÄ : F.Elements ‚•§ C where
  obj X := X.1
  map f := f.val

instance : (œÄ F).Faithful where

instance : (œÄ F).ReflectsIsomorphisms where
  reflects {X Y} f h := ‚ü®‚ü®‚ü®inv ((œÄ F).map f),
    by rw [‚Üê map_snd f, ‚Üê FunctorToTypes.map_comp_apply]; simp‚ü©, by aesop_cat‚ü©‚ü©

/-- A natural transformation between functors induces a functor between the categories of elements.
-/
@[simps]
def map {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : F‚ÇÅ.Elements ‚•§ F‚ÇÇ.Elements where
  obj t := ‚ü®t.1, Œ±.app t.1 t.2‚ü©
  map {t‚ÇÅ t‚ÇÇ} k := ‚ü®k.1, by simpa [map_snd] using (FunctorToTypes.naturality _ _ Œ± k.1 t‚ÇÅ.2).symm‚ü©

@[simp]
theorem map_œÄ {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : map Œ± ‚ãô œÄ F‚ÇÇ = œÄ F‚ÇÅ :=
  rfl

/-- The forward direction of the equivalence `F.Elements ‚âÖ (*, F)`. -/
def toStructuredArrow : F.Elements ‚•§ StructuredArrow PUnit F where
  obj X := StructuredArrow.mk fun _ => X.2
  map {X Y} f := StructuredArrow.homMk f.val (by funext; simp [f.2])

@[simp]
theorem toStructuredArrow_obj (X) :
    (toStructuredArrow F).obj X =
      { left := ‚ü®‚ü®‚ü©‚ü©
        right := X.1
        hom := fun _ => X.2 } :=
  rfl

@[simp]
theorem to_comma_map_right {X Y} (f : X ‚ü∂ Y) : ((toStructuredArrow F).map f).right = f.val :=
  rfl

/-- The reverse direction of the equivalence `F.Elements ‚âÖ (*, F)`. -/
def fromStructuredArrow : StructuredArrow PUnit F ‚•§ F.Elements where
  obj X := ‚ü®X.right, X.hom PUnit.unit‚ü©
  map f := ‚ü®f.right, congr_fun f.w.symm PUnit.unit‚ü©

@[simp]
theorem fromStructuredArrow_obj (X) : (fromStructuredArrow F).obj X = ‚ü®X.right, X.hom PUnit.unit‚ü© :=
  rfl

@[simp]
theorem fromStructuredArrow_map {X Y} (f : X ‚ü∂ Y) :
    (fromStructuredArrow F).map f = ‚ü®f.right, congr_fun f.w.symm PUnit.unit‚ü© :=
  rfl

/-- The equivalence between the category of elements `F.Elements`
    and the comma category `(*, F)`. -/
@[simps]
def structuredArrowEquivalence : F.Elements ‚âå StructuredArrow PUnit F where
  functor := toStructuredArrow F
  inverse := fromStructuredArrow F
  unitIso := Iso.refl _
  counitIso := Iso.refl _

open Opposite

/-- The forward direction of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `CategoryTheory.yonedaEquiv`.
-/
@[simps]
def toCostructuredArrow (F : C·µí·µñ ‚•§ Type v) : F.Elements·µí·µñ ‚•§ CostructuredArrow yoneda F where
  obj X := CostructuredArrow.mk (yonedaEquiv.symm (unop X).2)
  map f :=
    CostructuredArrow.homMk f.unop.val.unop (by
      ext Z y
      dsimp [yonedaEquiv]
      simp only [FunctorToTypes.map_comp_apply, ‚Üê f.unop.2])

/-- The reverse direction of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `CategoryTheory.yonedaEquiv`.
-/
@[simps]
def fromCostructuredArrow (F : C·µí·µñ ‚•§ Type v) : (CostructuredArrow yoneda F)·µí·µñ ‚•§ F.Elements where
  obj X := ‚ü®op (unop X).1, yonedaEquiv.1 (unop X).3‚ü©
  map {X Y} f :=
    ‚ü®f.unop.1.op, by
      convert (congr_fun ((unop X).hom.naturality f.unop.left.op) (ùüô _)).symm
      simp only [Equiv.toFun_as_coe, Quiver.Hom.unop_op, yonedaEquiv_apply, types_comp_apply,
        Category.comp_id, yoneda_obj_map]
      have : yoneda.map f.unop.left ‚â´ (unop X).hom = (unop Y).hom := by
        convert f.unop.3
      rw [‚Üê this]
      simp only [yoneda_map_app, FunctorToTypes.comp]
      rw [Category.id_comp]‚ü©

@[simp]
theorem fromCostructuredArrow_obj_mk (F : C·µí·µñ ‚•§ Type v) {X : C} (f : yoneda.obj X ‚ü∂ F) :
    (fromCostructuredArrow F).obj (op (CostructuredArrow.mk f)) = ‚ü®op X, yonedaEquiv.1 f‚ü© :=
  rfl

/-- The equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` given by yoneda lemma. -/
@[simps]
def costructuredArrowYonedaEquivalence (F : C·µí·µñ ‚•§ Type v) :
    F.Elements·µí·µñ ‚âå CostructuredArrow yoneda F where
  functor := toCostructuredArrow F
  inverse := (fromCostructuredArrow F).rightOp
  unitIso :=
    NatIso.ofComponents
      (fun X ‚Ü¶ Iso.op (CategoryOfElements.isoMk _ _ (Iso.refl _) (by simp))) (by
        rintro ‚ü®x‚ü© ‚ü®y‚ü© ‚ü®f : y ‚ü∂ x‚ü©
        exact Quiver.Hom.unop_inj (by ext; simp))
  counitIso := NatIso.ofComponents (fun X ‚Ü¶ CostructuredArrow.isoMk (Iso.refl _))

/-- The equivalence `(-.Elements)·µí·µñ ‚âÖ (yoneda, -)` of is actually a natural isomorphism of functors.
-/
theorem costructuredArrow_yoneda_equivalence_naturality {F‚ÇÅ F‚ÇÇ : C·µí·µñ ‚•§ Type v} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) :
    (map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ = toCostructuredArrow F‚ÇÅ ‚ãô CostructuredArrow.map Œ± := by
  fapply Functor.ext
  ¬∑ intro X
    simp only [CostructuredArrow.map_mk, toCostructuredArrow_obj, Functor.op_obj,
      Functor.comp_obj]
    congr
    ext _ f
    simpa using congr_fun (Œ±.naturality f.op).symm (unop X).snd
  ¬∑ simp

/-- The equivalence `F.elements·µí·µñ ‚âå (yoneda, F)` is compatible with the forgetful functors. -/
@[simps!]
def costructuredArrowYonedaEquivalenceFunctorProj (F : C·µí·µñ ‚•§ Type v) :
    (costructuredArrowYonedaEquivalence F).functor ‚ãô CostructuredArrow.proj _ _ ‚âÖ (œÄ F).leftOp :=
  Iso.refl _

/-- The equivalence `F.elements·µí·µñ ‚âå (yoneda, F)` is compatible with the forgetful functors. -/
@[simps!]
def costructuredArrowYonedaEquivalenceInverseœÄ (F : C·µí·µñ ‚•§ Type v) :
    (costructuredArrowYonedaEquivalence F).inverse ‚ãô (œÄ F).leftOp ‚âÖ CostructuredArrow.proj _ _ :=
  Iso.refl _

end CategoryOfElements

namespace Functor

/--
The initial object in the category of elements for a representable functor. In `isInitial` it is
shown that this is initial.
-/
def Elements.initial (A : C) : (yoneda.obj A).Elements :=
  ‚ü®Opposite.op A, ùüô _‚ü©

/-- Show that `Elements.initial A` is initial in the category of elements for the `yoneda` functor.
-/
def Elements.isInitial (A : C) : Limits.IsInitial (Elements.initial A) where
  desc s := ‚ü®s.pt.2.op, Category.comp_id _‚ü©
  uniq s m _ := by
    simp_rw [‚Üê m.2]
    dsimp [Elements.initial]
    simp
  fac := by rintro s ‚ü®‚ü®‚ü©‚ü©

end Functor

end CategoryTheory
