/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Comma.StructuredArrow
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.PUnit

#align_import category_theory.elements from "leanprover-community/mathlib"@"8a318021995877a44630c898d0b2bc376fceef3b"

/-!
# The category of elements

This file defines the category of elements, also known as (a special case of) the Grothendieck
construction.

Given a functor `F : C ‚•§ Type`, an object of `F.Elements` is a pair `(X : C, x : F.obj X)`.
A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.

## Implementation notes

This construction is equivalent to a special case of a comma construction, so this is mostly just a
more convenient API. We prove the equivalence in
`CategoryTheory.CategoryOfElements.structuredArrowEquivalence`.

## References
* [Emily Riehl, *Category Theory in Context*, Section 2.4][riehl2017]
* <https://en.wikipedia.org/wiki/Category_of_elements>
* <https://ncatlab.org/nlab/show/category+of+elements>

## Tags
category of elements, Grothendieck construction, comma category
-/


namespace CategoryTheory

universe w v u

variable {C : Type u} [Category.{v} C]

/-- The type of objects for the category of elements of a functor `F : C ‚•§ Type`
is a pair `(X : C, x : F.obj X)`.
-/
def Functor.Elements (F : C ‚•§ Type w) :=
  Œ£c : C, F.obj c
#align category_theory.functor.elements CategoryTheory.Functor.Elements

-- Porting note: added because Sigma.ext would be triggered automatically
lemma Functor.Elements.ext {F : C ‚•§ Type w} (x y : F.Elements) (h‚ÇÅ : x.fst = y.fst)
    (h‚ÇÇ : F.map (eqToHom h‚ÇÅ) x.snd = y.snd) : x = y := by
  cases x
  cases y
  cases h‚ÇÅ
  simp only [eqToHom_refl, FunctorToTypes.map_id_apply] at h‚ÇÇ
  simp [h‚ÇÇ]

/-- The category structure on `F.Elements`, for `F : C ‚•§ Type`.
    A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.
 -/
instance categoryOfElements (F : C ‚•§ Type w) : Category.{v} F.Elements where
  Hom p q := { f : p.1 ‚ü∂ q.1 // (F.map f) p.2 = q.2 }
  id p := ‚ü®ùüô p.1, by aesop_cat‚ü©
  comp {X Y Z} f g := ‚ü®f.val ‚â´ g.val, by simp [f.2, g.2]‚ü©
#align category_theory.category_of_elements CategoryTheory.categoryOfElements

namespace CategoryOfElements

@[ext]
theorem ext (F : C ‚•§ Type w) {x y : F.Elements} (f g : x ‚ü∂ y) (w : f.val = g.val) : f = g :=
  Subtype.ext_val w
#align category_theory.category_of_elements.ext CategoryTheory.CategoryOfElements.ext

@[simp]
theorem comp_val {F : C ‚•§ Type w} {p q r : F.Elements} {f : p ‚ü∂ q} {g : q ‚ü∂ r} :
    (f ‚â´ g).val = f.val ‚â´ g.val :=
  rfl
#align category_theory.category_of_elements.comp_val CategoryTheory.CategoryOfElements.comp_val

@[simp]
theorem id_val {F : C ‚•§ Type w} {p : F.Elements} : (ùüô p : p ‚ü∂ p).val = ùüô p.1 :=
  rfl
#align category_theory.category_of_elements.id_val CategoryTheory.CategoryOfElements.id_val

@[simp]
theorem map_snd {F : C ‚•§ Type w} {p q : F.Elements} (f : p ‚ü∂ q) : (F.map f.val) p.2 = q.2 :=
  f.property

end CategoryOfElements

instance groupoidOfElements {G : Type u} [Groupoid.{v} G] (F : G ‚•§ Type w) :
    Groupoid F.Elements where
  inv {p q} f :=
    ‚ü®Groupoid.inv f.val,
      calc
        F.map (Groupoid.inv f.val) q.2 = F.map (Groupoid.inv f.val) (F.map f.val p.2) := by rw [f.2]
        _ = (F.map f.val ‚â´ F.map (Groupoid.inv f.val)) p.2 := rfl
        _ = p.2 := by
          rw [‚Üê F.map_comp]
          simp
        ‚ü©
  inv_comp _ := by
    ext
    simp
  comp_inv _ := by
    ext
    simp
#align category_theory.groupoid_of_elements CategoryTheory.groupoidOfElements

namespace CategoryOfElements

variable (F : C ‚•§ Type w)

/-- The functor out of the category of elements which forgets the element. -/
@[simps]
def œÄ : F.Elements ‚•§ C where
  obj X := X.1
  map f := f.val
#align category_theory.category_of_elements.œÄ CategoryTheory.CategoryOfElements.œÄ

instance : (œÄ F).Faithful where

instance : (œÄ F).ReflectsIsomorphisms where
  reflects {X Y} f h := ‚ü®‚ü®‚ü®inv ((œÄ F).map f),
    by rw [‚Üê map_snd f, ‚Üê FunctorToTypes.map_comp_apply]; simp‚ü©, by aesop_cat‚ü©‚ü©

/-- A natural transformation between functors induces a functor between the categories of elements.
-/
@[simps]
def map {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : F‚ÇÅ.Elements ‚•§ F‚ÇÇ.Elements where
  obj t := ‚ü®t.1, Œ±.app t.1 t.2‚ü©
  map {t‚ÇÅ t‚ÇÇ} k := ‚ü®k.1, by simpa [‚Üê map_snd] using (FunctorToTypes.naturality _ _ Œ± k.1 t‚ÇÅ.2).symm‚ü©
#align category_theory.category_of_elements.map CategoryTheory.CategoryOfElements.map

@[simp]
theorem map_œÄ {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : map Œ± ‚ãô œÄ F‚ÇÇ = œÄ F‚ÇÅ :=
  rfl
#align category_theory.category_of_elements.map_œÄ CategoryTheory.CategoryOfElements.map_œÄ

/-- The forward direction of the equivalence `F.Elements ‚âÖ (*, F)`. -/
def toStructuredArrow : F.Elements ‚•§ StructuredArrow PUnit F where
  obj X := StructuredArrow.mk fun _ => X.2
  map {X Y} f := StructuredArrow.homMk f.val (by funext; simp [f.2])
#align category_theory.category_of_elements.to_structured_arrow CategoryTheory.CategoryOfElements.toStructuredArrow

@[simp]
theorem toStructuredArrow_obj (X) :
    (toStructuredArrow F).obj X =
      { left := ‚ü®‚ü®‚ü©‚ü©
        right := X.1
        hom := fun _ => X.2 } :=
  rfl
#align category_theory.category_of_elements.to_structured_arrow_obj CategoryTheory.CategoryOfElements.toStructuredArrow_obj

@[simp]
theorem to_comma_map_right {X Y} (f : X ‚ü∂ Y) : ((toStructuredArrow F).map f).right = f.val :=
  rfl
#align category_theory.category_of_elements.to_comma_map_right CategoryTheory.CategoryOfElements.to_comma_map_right

/-- The reverse direction of the equivalence `F.Elements ‚âÖ (*, F)`. -/
def fromStructuredArrow : StructuredArrow PUnit F ‚•§ F.Elements where
  obj X := ‚ü®X.right, X.hom PUnit.unit‚ü©
  map f := ‚ü®f.right, congr_fun f.w.symm PUnit.unit‚ü©
#align category_theory.category_of_elements.from_structured_arrow CategoryTheory.CategoryOfElements.fromStructuredArrow

@[simp]
theorem fromStructuredArrow_obj (X) : (fromStructuredArrow F).obj X = ‚ü®X.right, X.hom PUnit.unit‚ü© :=
  rfl
#align category_theory.category_of_elements.from_structured_arrow_obj CategoryTheory.CategoryOfElements.fromStructuredArrow_obj

@[simp]
theorem fromStructuredArrow_map {X Y} (f : X ‚ü∂ Y) :
    (fromStructuredArrow F).map f = ‚ü®f.right, congr_fun f.w.symm PUnit.unit‚ü© :=
  rfl
#align category_theory.category_of_elements.from_structured_arrow_map CategoryTheory.CategoryOfElements.fromStructuredArrow_map

/-- The equivalence between the category of elements `F.Elements`
    and the comma category `(*, F)`. -/
@[simps! functor_obj functor_map inverse_obj inverse_map unitIso_hom
  unitIso_inv counitIso_hom counitIso_inv]
def structuredArrowEquivalence : F.Elements ‚âå StructuredArrow PUnit F :=
  Equivalence.mk (toStructuredArrow F) (fromStructuredArrow F)
    (NatIso.ofComponents fun X => eqToIso (by aesop_cat))
    (NatIso.ofComponents fun X => StructuredArrow.isoMk (Iso.refl _))
#align category_theory.category_of_elements.structured_arrow_equivalence CategoryTheory.CategoryOfElements.structuredArrowEquivalence

open Opposite

/-- The forward direction of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `CategoryTheory.yonedaEquiv`.
-/
@[simps]
def toCostructuredArrow (F : C·µí·µñ ‚•§ Type v) : F.Elements·µí·µñ ‚•§ CostructuredArrow yoneda F where
  obj X := CostructuredArrow.mk (yonedaEquiv.symm (unop X).2)
  map f := by
    fapply CostructuredArrow.homMk
    ¬∑ exact f.unop.val.unop
    ¬∑ ext Z y
      dsimp [yonedaEquiv]
      simp only [FunctorToTypes.map_comp_apply, ‚Üê f.unop.2]
#align category_theory.category_of_elements.to_costructured_arrow CategoryTheory.CategoryOfElements.toCostructuredArrow

/-- The reverse direction of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `CategoryTheory.yonedaEquiv`.
-/
@[simps]
def fromCostructuredArrow (F : C·µí·µñ ‚•§ Type v) : (CostructuredArrow yoneda F)·µí·µñ ‚•§ F.Elements where
  obj X := ‚ü®op (unop X).1, yonedaEquiv.1 (unop X).3‚ü©
  map {X Y} f :=
    ‚ü®f.unop.1.op, by
      convert (congr_fun ((unop X).hom.naturality f.unop.left.op) (ùüô _)).symm
      simp only [Equiv.toFun_as_coe, Quiver.Hom.unop_op, yonedaEquiv_apply, types_comp_apply,
        Category.comp_id, yoneda_obj_map]
      have : yoneda.map f.unop.left ‚â´ (unop X).hom = (unop Y).hom := by
        convert f.unop.3
      erw [‚Üê this]
      simp only [yoneda_map_app, FunctorToTypes.comp]
      erw [Category.id_comp]‚ü©
#align category_theory.category_of_elements.from_costructured_arrow CategoryTheory.CategoryOfElements.fromCostructuredArrow

@[simp]
theorem fromCostructuredArrow_obj_mk (F : C·µí·µñ ‚•§ Type v) {X : C} (f : yoneda.obj X ‚ü∂ F) :
    (fromCostructuredArrow F).obj (op (CostructuredArrow.mk f)) = ‚ü®op X, yonedaEquiv.1 f‚ü© :=
  rfl
#align category_theory.category_of_elements.from_costructured_arrow_obj_mk CategoryTheory.CategoryOfElements.fromCostructuredArrow_obj_mk

/-- The unit of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` is indeed iso. -/
theorem from_toCostructuredArrow_eq (F : C·µí·µñ ‚•§ Type v) :
    (toCostructuredArrow F).rightOp ‚ãô fromCostructuredArrow F = ùü≠ _ := by
  refine Functor.ext ?_ ?_
  ¬∑ intro X
    exact Functor.Elements.ext _ _ rfl (by simp [yonedaEquiv])
  ¬∑ intro X Y f
    have : ‚àÄ {a b : F.Elements} (H : a = b),
        (eqToHom H).1 = eqToHom (show a.fst = b.fst by cases H; rfl) := by
      rintro _ _ rfl
      simp
    ext
    simp [this]
#align category_theory.category_of_elements.from_to_costructured_arrow_eq CategoryTheory.CategoryOfElements.from_toCostructuredArrow_eq

/-- The counit of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` is indeed iso. -/
theorem to_fromCostructuredArrow_eq (F : C·µí·µñ ‚•§ Type v) :
    (fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F = ùü≠ _ := by
  refine Functor.ext ?_ ?_
  ¬∑ intro X
    cases' X with X_left X_right X_hom
    cases X_right
    simp only [Functor.id_obj, Functor.rightOp_obj, toCostructuredArrow_obj, Functor.comp_obj,
      CostructuredArrow.mk]
    congr
    ext x f
    convert congr_fun (X_hom.naturality f.op).symm (ùüô X_left)
    simp
  ¬∑ aesop
#align category_theory.category_of_elements.to_from_costructured_arrow_eq CategoryTheory.CategoryOfElements.to_fromCostructuredArrow_eq

/-- The equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` given by yoneda lemma. -/
@[simps! functor_obj functor_map inverse_obj inverse_map unitIso_inv counitIso_hom counitIso_inv]
def costructuredArrowYonedaEquivalence (F : C·µí·µñ ‚•§ Type v) :
    F.Elements·µí·µñ ‚âå CostructuredArrow yoneda F :=
  Equivalence.mk (toCostructuredArrow F) (fromCostructuredArrow F).rightOp
    (NatIso.op (eqToIso (from_toCostructuredArrow_eq F))) (eqToIso <| to_fromCostructuredArrow_eq F)
#align category_theory.category_of_elements.costructured_arrow_yoneda_equivalence CategoryTheory.CategoryOfElements.costructuredArrowYonedaEquivalence

-- Porting note:
-- Running `@[simps! unitIso_hom]` is mysteriously slow.
-- We separate it out to avoid needing to increase the maxHeartbeats.
attribute [simps! unitIso_hom] costructuredArrowYonedaEquivalence

/-- The equivalence `(-.Elements)·µí·µñ ‚âÖ (yoneda, -)` of is actually a natural isomorphism of functors.
-/
theorem costructuredArrow_yoneda_equivalence_naturality {F‚ÇÅ F‚ÇÇ : C·µí·µñ ‚•§ Type v} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) :
    (map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ = toCostructuredArrow F‚ÇÅ ‚ãô CostructuredArrow.map Œ± := by
  fapply Functor.ext
  ¬∑ intro X
    simp only [CostructuredArrow.map_mk, toCostructuredArrow_obj, Functor.op_obj,
      Functor.comp_obj]
    congr
    ext _ f
    simpa using congr_fun (Œ±.naturality f.op).symm (unop X).snd
  ¬∑ simp [autoParam]
#align category_theory.category_of_elements.costructured_arrow_yoneda_equivalence_naturality CategoryTheory.CategoryOfElements.costructuredArrow_yoneda_equivalence_naturality

/-- The equivalence `F.elements·µí·µñ ‚âå (yoneda, F)` is compatible with the forgetful functors. -/
@[simps!]
def costructuredArrowYonedaEquivalenceFunctorProj (F : C·µí·µñ ‚•§ Type v) :
    (costructuredArrowYonedaEquivalence F).functor ‚ãô CostructuredArrow.proj _ _ ‚âÖ (œÄ F).leftOp :=
  Iso.refl _

/-- The equivalence `F.elements·µí·µñ ‚âå (yoneda, F)` is compatible with the forgetful functors. -/
@[simps!]
def costructuredArrowYonedaEquivalenceInverseœÄ (F : C·µí·µñ ‚•§ Type v) :
    (costructuredArrowYonedaEquivalence F).inverse ‚ãô (œÄ F).leftOp ‚âÖ CostructuredArrow.proj _ _ :=
  Iso.refl _

end CategoryOfElements

end CategoryTheory
