/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/
import Mathlib.CategoryTheory.Distributive.Monoidal
import Mathlib.CategoryTheory.Limits.MonoCoprod
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic

/-!

# Distributive categories

## Main definitions

A category `C` with finite products and binary coproducts is called distributive if the
canonical distributivity morphism `(X ‚®Ø Y) ‚®ø (X ‚®Ø Z) ‚ü∂ X ‚®Ø (Y ‚®ø Z)` is an isomorphism
for all objects `X`, `Y`, and `Z` in `C`.

## Implementation Details

A cartesian distributive category is defined as a cartesian monoidal category which is
monoidal distributive.

## Main results

- The coproduct coprojections are monic in a cartesian distributive category.


## TODO

- Every cartesian distributive category is finitary distributive, meaning that
  the left tensor product functor `X ‚äó -` preserves all finite coproducts.

- Show that any extensive distributive category can be embedded into a topos.

## References

- [J.R.B.Cockett, Introduction to distributive categories, 1993][cockett1993]
- [Carboni et al, Introduction to extensive and distributive categories][CARBONI1993145]
-/

universe v v‚ÇÇ u u‚ÇÇ

noncomputable section

namespace CategoryTheory

open Category Limits MonoidalCategory Distributive CartesianMonoidalCategory

variable (C : Type u) [Category.{v} C] [CartesianMonoidalCategory C] [HasBinaryCoproducts C]

/-- A category `C` with finite products is cartesian distributive if is monoidal distributive
with respect to the cartesian monoidal structure. -/
abbrev IsCartesianDistributive :=
  IsMonoidalDistrib C

namespace IsCartesianDistributive

/-- To show a category is cartesian distributive it is enough to show it is left distributive.
The right distributivity is inferred from symmetry of the cartesian monoidal structure. -/
lemma of_isMonoidalLeftDistrib [IsMonoidalLeftDistrib C] : IsCartesianDistributive C :=
  letI : BraidedCategory C := Nonempty.some inferInstance
  SymmetricCategory.isMonoidalDistrib_of_isMonoidalLeftDistrib

/-- The coproduct coprojections are monic in a cartesian distributive category. -/
instance monoCoprod [IsCartesianDistributive C] : MonoCoprod C :=
  MonoCoprod.mk' fun A B ‚Ü¶
    ‚ü®_, coprodIsCoprod A B, ‚ü®fun {Z} f g he ‚Ü¶ by
      let Œπ := coprod.inl (X := A) (Y := B)
      have : Mono (Z ‚óÅ Œπ) := SplitMono.mono
        { retraction := (‚àÇL Z A B).inv ‚â´ coprod.desc (ùüô _) (fst Z B ‚â´ lift (ùüô Z) f) }
      have : lift (ùüô Z) f = lift (ùüô Z) g := by rw [‚Üê cancel_mono (Z ‚óÅ Œπ)]; aesop
      simpa only [lift_snd] using this =‚â´ snd _ _‚ü©‚ü©

end IsCartesianDistributive

end CategoryTheory
