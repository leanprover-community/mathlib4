/-
Copyright (c) 2020 David W√§rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn
-/
module

public import Mathlib.CategoryTheory.NatIso
public import Mathlib.CategoryTheory.EqToHom
public import Mathlib.CategoryTheory.Groupoid

/-!
# Quotient category

Constructs the quotient of a category by an arbitrary family of relations on its hom-sets,
by introducing a type synonym for the objects, and identifying homs as necessary.

This is analogous to 'the quotient of a group by the normal closure of a subset', rather
than 'the quotient of a group by a normal subgroup'. When taking the quotient by a congruence
relation, `functor_map_eq_iff` says that no unnecessary identifications have been made.
-/

@[expose] public section


/-- A `HomRel` on `C` consists of a relation on every hom-set. -/
def HomRel (C) [Quiver C] :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ, (X ‚ü∂ Y) ‚Üí (X ‚ü∂ Y) ‚Üí Prop
deriving Inhabited

namespace CategoryTheory

open Functor

section

variable {C D : Type*} [Category* C] [Category* D] (F : C ‚•§ D)

/-- A functor induces a `HomRel` on its domain, relating those maps that have the same image. -/
def Functor.homRel : HomRel C :=
  fun _ _ f g ‚Ü¶ F.map f = F.map g

@[simp]
lemma Functor.homRel_iff {X Y : C} (f g : X ‚ü∂ Y) :
    F.homRel f g ‚Üî F.map f = F.map g := Iff.rfl

end

variable {C : Type*} [Category* C] (r : HomRel C)

namespace HomRel

/-- The condition that a `HomRel` is stable under precomposition. -/
class IsStableUnderPrecomp : Prop where
  comp_left {X Y Z} (f : X ‚ü∂ Y) {g g' : Y ‚ü∂ Z} : r g g' ‚Üí r (f ‚â´ g) (f ‚â´ g')

/-- The condition that a `HomRel` is stable under postcomposition. -/
class IsStableUnderPostcomp : Prop where
  comp_right {X Y Z} {f f' : X ‚ü∂ Y} (g : Y ‚ü∂ Z) : r f f' ‚Üí r (f ‚â´ g) (f' ‚â´ g)

export IsStableUnderPrecomp (comp_left)
export IsStableUnderPostcomp (comp_right)

/-- Generates the closure of a family of relations w.r.t. composition from left and right. -/
inductive CompClosure (r : HomRel C) : HomRel C
  | intro {s t : C} (a b : C) (f : s ‚ü∂ a) (m‚ÇÅ m‚ÇÇ : a ‚ü∂ b) (g : b ‚ü∂ t) (h : r m‚ÇÅ m‚ÇÇ) :
    CompClosure r (f ‚â´ m‚ÇÅ ‚â´ g) (f ‚â´ m‚ÇÇ ‚â´ g)

variable {r} in
theorem CompClosure.of {a b : C} {m‚ÇÅ m‚ÇÇ : a ‚ü∂ b} (h : r m‚ÇÅ m‚ÇÇ) : CompClosure r m‚ÇÅ m‚ÇÇ := by
  simpa using CompClosure.intro _ _ (ùüô _) m‚ÇÅ m‚ÇÇ (ùüô _) h

instance : IsStableUnderPrecomp (CompClosure r) where
  comp_left := by
    rintro a b e f _ _ ‚ü®c, d, g, h‚ÇÅ, h‚ÇÇ, i, h‚ü©
    simpa using CompClosure.intro _ _ (f ‚â´ g) _ _ i h

instance : IsStableUnderPostcomp (CompClosure r) where
  comp_right := by
    rintro a d e _ _ g ‚ü®b, c, f, g‚ÇÅ, g‚ÇÇ, i, h‚ü©
    simpa using CompClosure.intro _ _ f _ _ (i ‚â´ g) h

section

variable [IsStableUnderPrecomp r] [IsStableUnderPostcomp r]

lemma compClosure_iff_self {X Y : C} (f g : X ‚ü∂ Y) :
    CompClosure r f g ‚Üî r f g := by
  refine ‚ü®?_, CompClosure.of‚ü©
  rintro ‚ü®_, _, _, _, _, _, h‚ü©
  exact HomRel.comp_left _ (HomRel.comp_right _ h)

@[simp]
theorem compClosure_eq_self :
    CompClosure r = r := by
  dsimp [HomRel]
  ext
  simp only [compClosure_iff_self]

end

end HomRel

/-- A `HomRel` is a congruence when it's an equivalence on every hom-set, and it can be composed
from left and right. -/
class Congruence : Prop
    extends HomRel.IsStableUnderPrecomp r, HomRel.IsStableUnderPostcomp r where
  /-- `r` is an equivalence on every hom-set. -/
  equivalence : ‚àÄ {X Y}, _root_.Equivalence (@r X Y)

@[deprecated (since := "2025-12-23")] alias Congruence.compLeft := HomRel.comp_left
@[deprecated (since := "2025-12-23")] alias Congruence.compRight := HomRel.comp_right

/-- For `F : C ‚•§ D`, `F.homRel` is a congruence. -/
instance Functor.congruence_homRel {C D : Type*} [Category* C] [Category* D] (F : C ‚•§ D) :
    Congruence F.homRel where
  equivalence :=
    { refl := fun _ ‚Ü¶ rfl
      symm := by aesop
      trans := by aesop }
  comp_left := by aesop
  comp_right := by aesop

/-- A type synonym for `C`, thought of as the objects of the quotient category. -/
@[ext]
structure Quotient (r : HomRel C) where
  /-- The object of `C`. -/
  as : C

instance [Inhabited C] : Inhabited (Quotient r) :=
  ‚ü®{ as := default }‚ü©

@[deprecated (since := "2025-12-23")] alias Quotient.CompClosure := HomRel.CompClosure
@[deprecated (since := "2025-12-23")] alias Quotient.CompClosure.of := HomRel.CompClosure.of
@[deprecated (since := "2025-12-23")] alias Quotient.comp_left := HomRel.comp_left
@[deprecated (since := "2025-12-23")] alias Quotient.comp_right := HomRel.comp_right
@[deprecated (since := "2025-12-23")] alias Quotient.compClosure_iff_self :=
  HomRel.compClosure_iff_self
@[deprecated (since := "2025-12-23")] alias Quotient.compClosure_eq_self :=
  HomRel.compClosure_eq_self

namespace Quotient

/-- Hom-sets of the quotient category. -/
def Hom (s t : Quotient r) : Type _ :=
  Quot <| @HomRel.CompClosure C _ r s.as t.as

instance (a : Quotient r) : Inhabited (Hom r a a) :=
  ‚ü®Quot.mk _ (ùüô a.as)‚ü©

/-- Composition in the quotient category. -/
def comp ‚¶Éa b c : Quotient r‚¶Ñ : Hom r a b ‚Üí Hom r b c ‚Üí Hom r a c := fun hf hg ‚Ü¶
  Quot.liftOn hf
    (fun f ‚Ü¶
      Quot.liftOn hg (fun g ‚Ü¶ Quot.mk _ (f ‚â´ g)) fun _ _ h ‚Ü¶
        Quot.sound (HomRel.comp_left f h))
    fun _ _ h ‚Ü¶ Quot.inductionOn hg fun _ ‚Ü¶ Quot.sound (HomRel.comp_right _ h)

@[simp]
theorem comp_mk {a b c : Quotient r} (f : a.as ‚ü∂ b.as) (g : b.as ‚ü∂ c.as) :
    comp r (Quot.mk _ f) (Quot.mk _ g) = Quot.mk _ (f ‚â´ g) :=
  rfl

instance category : Category (Quotient r) where
  Hom := Hom r
  id a := Quot.mk _ (ùüô a.as)
  comp := @comp _ _ r
  comp_id f := Quot.inductionOn f <| by simp
  id_comp f := Quot.inductionOn f <| by simp
  assoc f g h := Quot.inductionOn f <| Quot.inductionOn g <| Quot.inductionOn h <| by simp

/-- An equivalence between the type synonym for a quotient category and the type alias
for the original category. -/
def equiv {C : Type _} [Category* C] (r : HomRel C) : Quotient r ‚âÉ C where
  toFun x := x.1
  invFun x := ‚ü®x‚ü©

noncomputable section

variable {G : Type*} [Groupoid G] (r : HomRel G)

/-- Inverse of a map in the quotient category of a groupoid. -/
protected def inv {X Y : Quotient r} (f : X ‚ü∂ Y) : Y ‚ü∂ X :=
  Quot.liftOn f (fun f' => Quot.mk _ (Groupoid.inv f')) (fun _ _ con => by
    obtain ‚ü®_, _, a, f, g, b, hfg‚ü© := con
    simpa using (Quot.sound (HomRel.CompClosure.intro _ _
      (inv b ‚â´ inv g) _ _ (inv f ‚â´ inv a) hfg)).symm)

@[simp]
theorem inv_mk {X Y : Quotient r} (f : X.as ‚ü∂ Y.as) :
    Quotient.inv r (Quot.mk _ f) = Quot.mk _ (Groupoid.inv f) :=
  rfl

/-- The quotient of a groupoid is a groupoid. -/
instance groupoid : Groupoid (Quotient r) where
  inv f := Quotient.inv r f
  inv_comp f := Quot.inductionOn f <| by simp [CategoryStruct.comp, CategoryStruct.id]
  comp_inv f := Quot.inductionOn f <| by simp [CategoryStruct.comp, CategoryStruct.id]

end

/-- The functor from a category to its quotient. -/
def functor : C ‚•§ Quotient r where
  obj a := { as := a }
  map f := Quot.mk _ f

instance full_functor : (functor r).Full where
  map_surjective f := ‚ü®Quot.out f, by simp [functor]‚ü©

instance essSurj_functor : (functor r).EssSurj where
  mem_essImage Y := ‚ü®Y.as, ‚ü®eqToIso rfl‚ü©‚ü©

instance [Unique C] : Unique (Quotient r) where
  uniq a := by ext; subsingleton

instance [‚àÄ (x y : C), Subsingleton (x ‚ü∂ y)] (x y : Quotient r) :
    Subsingleton (x ‚ü∂ y) := (full_functor r).map_surjective.subsingleton

protected theorem induction {P : ‚àÄ {a b : Quotient r}, (a ‚ü∂ b) ‚Üí Prop}
    (h : ‚àÄ {x y : C} (f : x ‚ü∂ y), P ((functor r).map f)) :
    ‚àÄ {a b : Quotient r} (f : a ‚ü∂ b), P f := by
  rintro ‚ü®x‚ü© ‚ü®y‚ü© ‚ü®f‚ü©
  exact h f

protected theorem sound {a b : C} {f‚ÇÅ f‚ÇÇ : a ‚ü∂ b} (h : r f‚ÇÅ f‚ÇÇ) :
    (functor r).map f‚ÇÅ = (functor r).map f‚ÇÇ := by
  simpa using Quot.sound (HomRel.CompClosure.intro _ _ (ùüô a) f‚ÇÅ f‚ÇÇ (ùüô b) h)

theorem functor_map_eq_iff [h : Congruence r] {X Y : C} (f f' : X ‚ü∂ Y) :
    (functor r).map f = (functor r).map f' ‚Üî r f f' := by
  dsimp [functor]
  rw [Equivalence.quot_mk_eq_iff, HomRel.compClosure_eq_self r]
  simpa only [HomRel.compClosure_eq_self r] using h.equivalence

theorem functor_homRel_eq_compClosure_eqvGen {X Y : C} (f g : X ‚ü∂ Y) :
    (functor r).homRel f g ‚Üî Relation.EqvGen (@HomRel.CompClosure C _ r X Y) f g :=
  Quot.eq

theorem compClosure.congruence :
    Congruence fun X Y => Relation.EqvGen (@HomRel.CompClosure C _ r X Y) := by
  convert inferInstanceAs (Congruence (functor r).homRel)
  ext
  rw [functor_homRel_eq_compClosure_eqvGen]

variable {D : Type _} [Category* D] (F : C ‚•§ D)

/-- The induced functor on the quotient category. -/
def lift (H : ‚àÄ (x y : C) (f‚ÇÅ f‚ÇÇ : x ‚ü∂ y), r f‚ÇÅ f‚ÇÇ ‚Üí F.map f‚ÇÅ = F.map f‚ÇÇ) : Quotient r ‚•§ D where
  obj a := F.obj a.as
  map hf :=
    Quot.liftOn hf (fun f ‚Ü¶ F.map f)
      (by
        rintro _ _ ‚ü®_, _, _, _, _, _, h‚ü©
        simp [H _ _ _ _ h])
  map_id a := F.map_id a.as
  map_comp := by
    rintro a b c ‚ü®f‚ü© ‚ü®g‚ü©
    exact F.map_comp f g

variable (H : ‚àÄ (x y : C) (f‚ÇÅ f‚ÇÇ : x ‚ü∂ y), r f‚ÇÅ f‚ÇÇ ‚Üí F.map f‚ÇÅ = F.map f‚ÇÇ)

theorem lift_spec : functor r ‚ãô lift r F H = F := by
  tauto

theorem lift_unique (Œ¶ : Quotient r ‚•§ D) (hŒ¶ : functor r ‚ãô Œ¶ = F) : Œ¶ = lift r F H := by
  subst_vars
  fapply Functor.hext
  ¬∑ rintro X
    dsimp [lift, Functor]
    congr
  ¬∑ rintro _ _ f
    dsimp [lift, Functor]
    refine Quot.inductionOn f fun _ ‚Ü¶ ?_
    simp only [heq_eq_eq]
    congr

lemma lift_unique' (F‚ÇÅ F‚ÇÇ : Quotient r ‚•§ D) (h : functor r ‚ãô F‚ÇÅ = functor r ‚ãô F‚ÇÇ) :
    F‚ÇÅ = F‚ÇÇ := by
  rw [lift_unique r (functor r ‚ãô F‚ÇÇ) _ F‚ÇÇ rfl]; swap
  ¬∑ rintro X Y f g h
    dsimp
    rw [Quotient.sound r h]
  apply lift_unique
  rw [h]

/-- The original functor factors through the induced functor. -/
def lift.isLift : functor r ‚ãô lift r F H ‚âÖ F :=
  NatIso.ofComponents fun _ ‚Ü¶ Iso.refl _

@[simp]
theorem lift.isLift_hom (X : C) : (lift.isLift r F H).hom.app X = ùüô (F.obj X) :=
  rfl

@[simp]
theorem lift.isLift_inv (X : C) : (lift.isLift r F H).inv.app X = ùüô (F.obj X) :=
  rfl

theorem lift_obj_functor_obj (X : C) :
    (lift r F H).obj ((functor r).obj X) = F.obj X := rfl

theorem lift_map_functor_map {X Y : C} (f : X ‚ü∂ Y) :
    (lift r F H).map ((functor r).map f) = F.map f :=
  rfl

variable {r}

lemma natTrans_ext {F G : Quotient r ‚•§ D} (œÑ‚ÇÅ œÑ‚ÇÇ : F ‚ü∂ G)
    (h : whiskerLeft (Quotient.functor r) œÑ‚ÇÅ = whiskerLeft (Quotient.functor r) œÑ‚ÇÇ) : œÑ‚ÇÅ = œÑ‚ÇÇ :=
  NatTrans.ext (by ext1 ‚ü®X‚ü©; exact NatTrans.congr_app h X)

variable (r)

/-- In order to define a natural transformation `F ‚ü∂ G` with `F G : Quotient r ‚•§ D`, it suffices
to do so after precomposing with `Quotient.functor r`. -/
def natTransLift {F G : Quotient r ‚•§ D} (œÑ : Quotient.functor r ‚ãô F ‚ü∂ Quotient.functor r ‚ãô G) :
    F ‚ü∂ G where
  app := fun ‚ü®X‚ü© => œÑ.app X
  naturality := fun ‚ü®X‚ü© ‚ü®Y‚ü© => by
    rintro ‚ü®f‚ü©
    exact œÑ.naturality f

@[simp]
lemma natTransLift_app (F G : Quotient r ‚•§ D)
    (œÑ : Quotient.functor r ‚ãô F ‚ü∂ Quotient.functor r ‚ãô G) (X : C) :
    (natTransLift r œÑ).app ((Quotient.functor r).obj X) = œÑ.app X := rfl

@[reassoc]
lemma comp_natTransLift {F G H : Quotient r ‚•§ D}
    (œÑ : Quotient.functor r ‚ãô F ‚ü∂ Quotient.functor r ‚ãô G)
    (œÑ' : Quotient.functor r ‚ãô G ‚ü∂ Quotient.functor r ‚ãô H) :
    natTransLift r œÑ ‚â´ natTransLift r œÑ' = natTransLift r (œÑ ‚â´ œÑ') := by cat_disch

@[simp]
lemma natTransLift_id (F : Quotient r ‚•§ D) :
    natTransLift r (ùüô (Quotient.functor r ‚ãô F)) = ùüô _ := by cat_disch

/-- In order to define a natural isomorphism `F ‚âÖ G` with `F G : Quotient r ‚•§ D`, it suffices
to do so after precomposing with `Quotient.functor r`. -/
@[simps]
def natIsoLift {F G : Quotient r ‚•§ D} (œÑ : Quotient.functor r ‚ãô F ‚âÖ Quotient.functor r ‚ãô G) :
    F ‚âÖ G where
  hom := natTransLift _ œÑ.hom
  inv := natTransLift _ œÑ.inv
  hom_inv_id := by rw [comp_natTransLift, œÑ.hom_inv_id, natTransLift_id]
  inv_hom_id := by rw [comp_natTransLift, œÑ.inv_hom_id, natTransLift_id]

variable (D)

instance full_whiskeringLeft_functor :
    ((whiskeringLeft C _ D).obj (functor r)).Full where
  map_surjective f := ‚ü®natTransLift r f, by cat_disch‚ü©

instance faithful_whiskeringLeft_functor :
    ((whiskeringLeft C _ D).obj (functor r)).Faithful := ‚ü®by apply natTrans_ext‚ü©

end Quotient

end CategoryTheory
