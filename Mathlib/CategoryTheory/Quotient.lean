/-
Copyright (c) 2020 David WÃ¤rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David WÃ¤rn
-/
import Mathlib.CategoryTheory.NatIso
import Mathlib.CategoryTheory.EqToHom

#align_import category_theory.quotient from "leanprover-community/mathlib"@"740acc0e6f9adf4423f92a485d0456fc271482da"

/-!
# Quotient category

Constructs the quotient of a category by an arbitrary family of relations on its hom-sets,
by introducing a type synonym for the objects, and identifying homs as necessary.

This is analogous to 'the quotient of a group by the normal closure of a subset', rather
than 'the quotient of a group by a normal subgroup'. When taking the quotient by a congruence
relation, `functor_map_eq_iff` says that no unnecessary identifications have been made.
-/


/-- A `HomRel` on `C` consists of a relation on every hom-set. -/
def HomRel (C) [Quiver C] :=
  âˆ€ â¦ƒX Y : Câ¦„, (X âŸ¶ Y) â†’ (X âŸ¶ Y) â†’ Prop
#align hom_rel HomRel

-- Porting Note: `deriving Inhabited` was not able to deduce this typeclass
instance (C) [Quiver C] : Inhabited (HomRel C) where
  default := fun _ _ _ _ â†¦ PUnit

namespace CategoryTheory

variable {C : Type _} [Category C] (r : HomRel C)

/-- A `HomRel` is a congruence when it's an equivalence on every hom-set, and it can be composed
from left and right. -/
class Congruence : Prop where
  /-- `r` is an equivalence on every hom-set. -/
  equivalence : âˆ€ {X Y}, _root_.Equivalence (@r X Y)
  /-- Precomposition with an arrow respects `r`. -/
  compLeft : âˆ€ {X Y Z} (f : X âŸ¶ Y) {g g' : Y âŸ¶ Z}, r g g' â†’ r (f â‰« g) (f â‰« g')
  /-- Postcomposition with an arrow respects `r`. -/
  compRight : âˆ€ {X Y Z} {f f' : X âŸ¶ Y} (g : Y âŸ¶ Z), r f f' â†’ r (f â‰« g) (f' â‰« g)
#align category_theory.congruence CategoryTheory.Congruence

/-- A type synonym for `C`, thought of as the objects of the quotient category. -/
@[ext]
structure Quotient (r : HomRel C) where
  /-- The object of `C`. -/
  as : C
#align category_theory.quotient CategoryTheory.Quotient

instance [Inhabited C] : Inhabited (Quotient r) :=
  âŸ¨{ as := default }âŸ©

namespace Quotient

/-- Generates the closure of a family of relations w.r.t. composition from left and right. -/
inductive CompClosure (r : HomRel C) â¦ƒs t : Câ¦„ : (s âŸ¶ t) â†’ (s âŸ¶ t) â†’ Prop
  | intro {a b : C} (f : s âŸ¶ a) (mâ‚ mâ‚‚ : a âŸ¶ b) (g : b âŸ¶ t) (h : r mâ‚ mâ‚‚) :
    CompClosure r (f â‰« mâ‚ â‰« g) (f â‰« mâ‚‚ â‰« g)
#align category_theory.quotient.comp_closure CategoryTheory.Quotient.CompClosure

theorem CompClosure.of {a b : C} (mâ‚ mâ‚‚ : a âŸ¶ b) (h : r mâ‚ mâ‚‚) : CompClosure r mâ‚ mâ‚‚ := by
  simpa using CompClosure.intro (ğŸ™ _) mâ‚ mâ‚‚ (ğŸ™ _) h
#align category_theory.quotient.comp_closure.of CategoryTheory.Quotient.CompClosure.of

theorem comp_left {a b c : C} (f : a âŸ¶ b) :
    âˆ€ (gâ‚ gâ‚‚ : b âŸ¶ c) (_ : CompClosure r gâ‚ gâ‚‚), CompClosure r (f â‰« gâ‚) (f â‰« gâ‚‚)
  | _, _, âŸ¨x, mâ‚, mâ‚‚, y, hâŸ© => by simpa using CompClosure.intro (f â‰« x) mâ‚ mâ‚‚ y h
#align category_theory.quotient.comp_left CategoryTheory.Quotient.comp_left

theorem comp_right {a b c : C} (g : b âŸ¶ c) :
    âˆ€ (fâ‚ fâ‚‚ : a âŸ¶ b) (_ : CompClosure r fâ‚ fâ‚‚), CompClosure r (fâ‚ â‰« g) (fâ‚‚ â‰« g)
  | _, _, âŸ¨x, mâ‚, mâ‚‚, y, hâŸ© => by simpa using CompClosure.intro x mâ‚ mâ‚‚ (y â‰« g) h
#align category_theory.quotient.comp_right CategoryTheory.Quotient.comp_right

/-- Hom-sets of the quotient category. -/
def Hom (s t : Quotient r) :=
  Quot <| @CompClosure C _ r s.as t.as
#align category_theory.quotient.hom CategoryTheory.Quotient.Hom

instance (a : Quotient r) : Inhabited (Hom r a a) :=
  âŸ¨Quot.mk _ (ğŸ™ a.as)âŸ©

/-- Composition in the quotient category. -/
def comp â¦ƒa b c : Quotient râ¦„ : Hom r a b â†’ Hom r b c â†’ Hom r a c := fun hf hg â†¦
  Quot.liftOn hf
    (fun f â†¦
      Quot.liftOn hg (fun g â†¦ Quot.mk _ (f â‰« g)) fun gâ‚ gâ‚‚ h â†¦
        Quot.sound <| comp_left r f gâ‚ gâ‚‚ h)
    fun fâ‚ fâ‚‚ h â†¦ Quot.inductionOn hg fun g â†¦ Quot.sound <| comp_right r g fâ‚ fâ‚‚ h
#align category_theory.quotient.comp CategoryTheory.Quotient.comp

@[simp]
theorem comp_mk {a b c : Quotient r} (f : a.as âŸ¶ b.as) (g : b.as âŸ¶ c.as) :
    comp r (Quot.mk _ f) (Quot.mk _ g) = Quot.mk _ (f â‰« g) :=
  rfl
#align category_theory.quotient.comp_mk CategoryTheory.Quotient.comp_mk

-- Porting note: Had to manually add the proofs of `comp_id` `id_comp` and `assoc`
instance category : Category (Quotient r) where
  Hom := Hom r
  id a := Quot.mk _ (ğŸ™ a.as)
  comp := @comp _ _ r
  comp_id f := Quot.inductionOn f <| by simp
  id_comp f := Quot.inductionOn f <| by simp
  assoc f g h := Quot.inductionOn f <| Quot.inductionOn g <| Quot.inductionOn h <| by simp
#align category_theory.quotient.category CategoryTheory.Quotient.category

/-- The functor from a category to its quotient. -/
def functor : C â¥¤ Quotient r where
  obj a := { as := a }
  map := @fun _ _ f â†¦ Quot.mk _ f
#align category_theory.quotient.functor CategoryTheory.Quotient.functor

instance full_functor : (functor r).Full where
  map_surjective f:= âŸ¨Quot.out f, by simp [functor]âŸ©

instance essSurj_functor : (functor r).EssSurj where
  mem_essImage Y :=
    âŸ¨Y.as, âŸ¨eqToIso (by
            ext
            rfl)âŸ©âŸ©

protected theorem induction {P : âˆ€ {a b : Quotient r}, (a âŸ¶ b) â†’ Prop}
    (h : âˆ€ {x y : C} (f : x âŸ¶ y), P ((functor r).map f)) :
    âˆ€ {a b : Quotient r} (f : a âŸ¶ b), P f := by
  rintro âŸ¨xâŸ© âŸ¨yâŸ© âŸ¨fâŸ©
  exact h f
#align category_theory.quotient.induction CategoryTheory.Quotient.induction

protected theorem sound {a b : C} {fâ‚ fâ‚‚ : a âŸ¶ b} (h : r fâ‚ fâ‚‚) :
    (functor r).map fâ‚ = (functor r).map fâ‚‚ := by
  simpa using Quot.sound (CompClosure.intro (ğŸ™ a) fâ‚ fâ‚‚ (ğŸ™ b) h)
#align category_theory.quotient.sound CategoryTheory.Quotient.sound

lemma compClosure_iff_self [h : Congruence r] {X Y : C} (f g : X âŸ¶ Y) :
    CompClosure r f g â†” r f g := by
  constructor
  Â· intro hfg
    induction' hfg with m m' hm
    exact Congruence.compLeft _ (Congruence.compRight _ (by assumption))
  Â· exact CompClosure.of _ _ _

@[simp]
theorem compClosure_eq_self [h : Congruence r] :
    CompClosure r = r := by
  ext
  simp only [compClosure_iff_self]

theorem functor_map_eq_iff [h : Congruence r] {X Y : C} (f f' : X âŸ¶ Y) :
    (functor r).map f = (functor r).map f' â†” r f f' := by
  dsimp [functor]
  rw [Equivalence.quot_mk_eq_iff, compClosure_eq_self r]
  simpa only [compClosure_eq_self r] using h.equivalence
#align category_theory.quotient.functor_map_eq_iff CategoryTheory.Quotient.functor_map_eq_iff

variable {D : Type _} [Category D] (F : C â¥¤ D)
  (H : âˆ€ (x y : C) (fâ‚ fâ‚‚ : x âŸ¶ y), r fâ‚ fâ‚‚ â†’ F.map fâ‚ = F.map fâ‚‚)

/-- The induced functor on the quotient category. -/
def lift : Quotient r â¥¤ D where
  obj a := F.obj a.as
  map := @fun a b hf â†¦
    Quot.liftOn hf (fun f â†¦ F.map f)
      (by
        rintro _ _ âŸ¨_, _, _, _, hâŸ©
        simp [H _ _ _ _ h])
  map_id a := F.map_id a.as
  map_comp := by
    rintro a b c âŸ¨fâŸ© âŸ¨gâŸ©
    exact F.map_comp f g
#align category_theory.quotient.lift CategoryTheory.Quotient.lift

theorem lift_spec : functor r â‹™ lift r F H = F := by
  apply Functor.ext; rotate_left
  Â· rintro X
    rfl
  Â· rintro X Y f
    dsimp [lift, functor]
    simp
#align category_theory.quotient.lift_spec CategoryTheory.Quotient.lift_spec

theorem lift_unique (Î¦ : Quotient r â¥¤ D) (hÎ¦ : functor r â‹™ Î¦ = F) : Î¦ = lift r F H := by
  subst_vars
  fapply Functor.hext
  Â· rintro X
    dsimp [lift, Functor]
    congr
  Â· rintro _ _ f
    dsimp [lift, Functor]
    refine Quot.inductionOn f (fun _ â†¦ ?_) -- Porting note: this line was originally an `apply`
    simp only [Quot.liftOn_mk, Functor.comp_map]
    congr
#align category_theory.quotient.lift_unique CategoryTheory.Quotient.lift_unique

lemma lift_unique' (Fâ‚ Fâ‚‚ : Quotient r â¥¤ D) (h : functor r â‹™ Fâ‚ = functor r â‹™ Fâ‚‚) :
    Fâ‚ = Fâ‚‚ := by
  rw [lift_unique r (functor r â‹™ Fâ‚‚) _ Fâ‚‚ rfl]; swap
  Â· rintro X Y f g h
    dsimp
    rw [Quotient.sound r h]
  apply lift_unique
  rw [h]

/-- The original functor factors through the induced functor. -/
def lift.isLift : functor r â‹™ lift r F H â‰… F :=
  NatIso.ofComponents fun X â†¦ Iso.refl _
#align category_theory.quotient.lift.is_lift CategoryTheory.Quotient.lift.isLift

@[simp]
theorem lift.isLift_hom (X : C) : (lift.isLift r F H).hom.app X = ğŸ™ (F.obj X) :=
  rfl
#align category_theory.quotient.lift.is_lift_hom CategoryTheory.Quotient.lift.isLift_hom

@[simp]
theorem lift.isLift_inv (X : C) : (lift.isLift r F H).inv.app X = ğŸ™ (F.obj X) :=
  rfl
#align category_theory.quotient.lift.is_lift_inv CategoryTheory.Quotient.lift.isLift_inv

theorem lift_obj_functor_obj (X : C) :
    (lift r F H).obj ((functor r).obj X) = F.obj X := rfl

theorem lift_map_functor_map {X Y : C} (f : X âŸ¶ Y) :
    (lift r F H).map ((functor r).map f) = F.map f := by
  rw [â† NatIso.naturality_1 (lift.isLift r F H)]
  dsimp [lift, functor]
  simp
#align category_theory.quotient.lift_map_functor_map CategoryTheory.Quotient.lift_map_functor_map

variable {r}

lemma natTrans_ext {F G : Quotient r â¥¤ D} (Ï„â‚ Ï„â‚‚ : F âŸ¶ G)
    (h : whiskerLeft (Quotient.functor r) Ï„â‚ = whiskerLeft (Quotient.functor r) Ï„â‚‚) : Ï„â‚ = Ï„â‚‚ :=
  NatTrans.ext _ _ (by ext1 âŸ¨XâŸ©; exact NatTrans.congr_app h X)

variable (r)

/-- In order to define a natural transformation `F âŸ¶ G` with `F G : Quotient r â¥¤ D`, it suffices
to do so after precomposing with `Quotient.functor r`. -/
def natTransLift {F G : Quotient r â¥¤ D} (Ï„ : Quotient.functor r â‹™ F âŸ¶ Quotient.functor r â‹™ G) :
    F âŸ¶ G where
  app := fun âŸ¨XâŸ© => Ï„.app X
  naturality := fun âŸ¨XâŸ© âŸ¨YâŸ© => by
    rintro âŸ¨fâŸ©
    exact Ï„.naturality f

@[simp]
lemma natTransLift_app (F G : Quotient r â¥¤ D)
    (Ï„ : Quotient.functor r â‹™ F âŸ¶ Quotient.functor r â‹™ G) (X : C) :
  (natTransLift r Ï„).app ((Quotient.functor r).obj X) = Ï„.app X := rfl

@[reassoc]
lemma comp_natTransLift {F G H : Quotient r â¥¤ D}
    (Ï„ : Quotient.functor r â‹™ F âŸ¶ Quotient.functor r â‹™ G)
    (Ï„' : Quotient.functor r â‹™ G âŸ¶ Quotient.functor r â‹™ H) :
    natTransLift r Ï„ â‰« natTransLift r Ï„' =  natTransLift r (Ï„ â‰« Ï„') := by aesop_cat

@[simp]
lemma natTransLift_id (F : Quotient r â¥¤ D) :
    natTransLift r (ğŸ™ (Quotient.functor r â‹™ F)) = ğŸ™ _ := by aesop_cat

/-- In order to define a natural isomorphism `F â‰… G` with `F G : Quotient r â¥¤ D`, it suffices
to do so after precomposing with `Quotient.functor r`. -/
@[simps]
def natIsoLift {F G : Quotient r â¥¤ D} (Ï„ : Quotient.functor r â‹™ F â‰… Quotient.functor r â‹™ G) :
    F â‰… G where
  hom := natTransLift _ Ï„.hom
  inv := natTransLift _ Ï„.inv
  hom_inv_id := by rw [comp_natTransLift, Ï„.hom_inv_id, natTransLift_id]
  inv_hom_id := by rw [comp_natTransLift, Ï„.inv_hom_id, natTransLift_id]

variable (D)

instance full_whiskeringLeft_functor :
    ((whiskeringLeft C _ D).obj (functor r)).Full where
  map_surjective f := âŸ¨natTransLift r f, by aesop_catâŸ©

instance faithful_whiskeringLeft_functor :
    ((whiskeringLeft C _ D).obj (functor r)).Faithful := âŸ¨by apply natTrans_extâŸ©

end Quotient

end CategoryTheory
