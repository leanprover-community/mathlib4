/-
Copyright (c) 2020 David W√§rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn
-/
import Mathlib.CategoryTheory.NatIso
import Mathlib.CategoryTheory.EqToHom

#align_import category_theory.quotient from "leanprover-community/mathlib"@"740acc0e6f9adf4423f92a485d0456fc271482da"

/-!
# Quotient category

Constructs the quotient of a category by an arbitrary family of relations on its hom-sets,
by introducing a type synonym for the objects, and identifying homs as necessary.

This is analogous to 'the quotient of a group by the normal closure of a subset', rather
than 'the quotient of a group by a normal subgroup'. When taking the quotient by a congruence
relation, `functor_map_eq_iff` says that no unnecessary identifications have been made.
-/


/-- A `HomRel` on `C` consists of a relation on every hom-set. -/
def HomRel (C) [Quiver C] :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ, (X ‚ü∂ Y) ‚Üí (X ‚ü∂ Y) ‚Üí Prop
#align hom_rel HomRel

-- Porting Note: `deriving Inhabited` was not able to deduce this typeclass
instance (C) [Quiver C] : Inhabited (HomRel C) where
  default := fun _ _ _ _ ‚Ü¶ PUnit

namespace CategoryTheory

variable {C : Type _} [Category C] (r : HomRel C)

/-- A `HomRel` is a congruence when it's an equivalence on every hom-set, and it can be composed
from left and right. -/
class Congruence : Prop where
  /-- `r` is an equivalence on every hom-set. -/
  isEquiv : ‚àÄ {X Y}, IsEquiv _ (@r X Y)
  /-- Precomposition with an arrow respects `r`. -/
  compLeft : ‚àÄ {X Y Z} (f : X ‚ü∂ Y) {g g' : Y ‚ü∂ Z}, r g g' ‚Üí r (f ‚â´ g) (f ‚â´ g')
  /-- Postcomposition with an arrow respects `r`. -/
  compRight : ‚àÄ {X Y Z} {f f' : X ‚ü∂ Y} (g : Y ‚ü∂ Z), r f f' ‚Üí r (f ‚â´ g) (f' ‚â´ g)
#align category_theory.congruence CategoryTheory.Congruence

attribute [instance] Congruence.isEquiv

/-- A type synonym for `C`, thought of as the objects of the quotient category. -/
@[ext]
structure Quotient (r : HomRel C) where
  /-- The object of `C`. -/
  as : C
#align category_theory.quotient CategoryTheory.Quotient

instance [Inhabited C] : Inhabited (Quotient r) :=
  ‚ü®{ as := default }‚ü©

namespace Quotient

/-- Generates the closure of a family of relations w.r.t. composition from left and right. -/
inductive CompClosure (r : HomRel C) ‚¶És t : C‚¶Ñ : (s ‚ü∂ t) ‚Üí (s ‚ü∂ t) ‚Üí Prop
  | intro {a b : C} (f : s ‚ü∂ a) (m‚ÇÅ m‚ÇÇ : a ‚ü∂ b) (g : b ‚ü∂ t) (h : r m‚ÇÅ m‚ÇÇ) :
    CompClosure r (f ‚â´ m‚ÇÅ ‚â´ g) (f ‚â´ m‚ÇÇ ‚â´ g)
#align category_theory.quotient.comp_closure CategoryTheory.Quotient.CompClosure

theorem CompClosure.of {a b : C} (m‚ÇÅ m‚ÇÇ : a ‚ü∂ b) (h : r m‚ÇÅ m‚ÇÇ) : CompClosure r m‚ÇÅ m‚ÇÇ := by
  simpa using CompClosure.intro (ùüô _) m‚ÇÅ m‚ÇÇ (ùüô _) h
#align category_theory.quotient.comp_closure.of CategoryTheory.Quotient.CompClosure.of

theorem comp_left {a b c : C} (f : a ‚ü∂ b) :
    ‚àÄ (g‚ÇÅ g‚ÇÇ : b ‚ü∂ c) (_ : CompClosure r g‚ÇÅ g‚ÇÇ), CompClosure r (f ‚â´ g‚ÇÅ) (f ‚â´ g‚ÇÇ)
  | _, _, ‚ü®x, m‚ÇÅ, m‚ÇÇ, y, h‚ü© => by simpa using CompClosure.intro (f ‚â´ x) m‚ÇÅ m‚ÇÇ y h
#align category_theory.quotient.comp_left CategoryTheory.Quotient.comp_left

theorem comp_right {a b c : C} (g : b ‚ü∂ c) :
    ‚àÄ (f‚ÇÅ f‚ÇÇ : a ‚ü∂ b) (_ : CompClosure r f‚ÇÅ f‚ÇÇ), CompClosure r (f‚ÇÅ ‚â´ g) (f‚ÇÇ ‚â´ g)
  | _, _, ‚ü®x, m‚ÇÅ, m‚ÇÇ, y, h‚ü© => by simpa using CompClosure.intro x m‚ÇÅ m‚ÇÇ (y ‚â´ g) h
#align category_theory.quotient.comp_right CategoryTheory.Quotient.comp_right

/-- Hom-sets of the quotient category. -/
def Hom (s t : Quotient r) :=
  Quot <| @CompClosure C _ r s.as t.as
#align category_theory.quotient.hom CategoryTheory.Quotient.Hom

instance (a : Quotient r) : Inhabited (Hom r a a) :=
  ‚ü®Quot.mk _ (ùüô a.as)‚ü©

/-- Composition in the quotient category. -/
def comp ‚¶Éa b c : Quotient r‚¶Ñ : Hom r a b ‚Üí Hom r b c ‚Üí Hom r a c := fun hf hg ‚Ü¶
  Quot.liftOn hf
    (fun f ‚Ü¶
      Quot.liftOn hg (fun g ‚Ü¶ Quot.mk _ (f ‚â´ g)) fun g‚ÇÅ g‚ÇÇ h ‚Ü¶
        Quot.sound <| comp_left r f g‚ÇÅ g‚ÇÇ h)
    fun f‚ÇÅ f‚ÇÇ h ‚Ü¶ Quot.inductionOn hg fun g ‚Ü¶ Quot.sound <| comp_right r g f‚ÇÅ f‚ÇÇ h
#align category_theory.quotient.comp CategoryTheory.Quotient.comp

@[simp]
theorem comp_mk {a b c : Quotient r} (f : a.as ‚ü∂ b.as) (g : b.as ‚ü∂ c.as) :
    comp r (Quot.mk _ f) (Quot.mk _ g) = Quot.mk _ (f ‚â´ g) :=
  rfl
#align category_theory.quotient.comp_mk CategoryTheory.Quotient.comp_mk

-- porting note: Had to manually add the proofs of `comp_id` `id_comp` and `assoc`
instance category : Category (Quotient r) where
  Hom := Hom r
  id a := Quot.mk _ (ùüô a.as)
  comp := @comp _ _ r
  comp_id f := Quot.inductionOn f $ by simp
  id_comp f := Quot.inductionOn f $ by simp
  assoc f g h := Quot.inductionOn f $ Quot.inductionOn g $ Quot.inductionOn h $ by simp
#align category_theory.quotient.category CategoryTheory.Quotient.category

/-- The functor from a category to its quotient. -/
@[simps]
def functor : C ‚•§ Quotient r where
  obj a := { as := a }
  map := @fun _ _ f ‚Ü¶ Quot.mk _ f
#align category_theory.quotient.functor CategoryTheory.Quotient.functor

noncomputable instance fullFunctor : Full (functor r) where preimage := @fun X Y f ‚Ü¶ Quot.out f

instance : EssSurj (functor r)
    where mem_essImage Y :=
    ‚ü®Y.as, ‚ü®eqToIso (by
            ext
            rfl)‚ü©‚ü©

protected theorem induction {P : ‚àÄ {a b : Quotient r}, (a ‚ü∂ b) ‚Üí Prop}
    (h : ‚àÄ {x y : C} (f : x ‚ü∂ y), P ((functor r).map f)) :
    ‚àÄ {a b : Quotient r} (f : a ‚ü∂ b), P f := by
  rintro ‚ü®x‚ü© ‚ü®y‚ü© ‚ü®f‚ü©
  exact h f
#align category_theory.quotient.induction CategoryTheory.Quotient.induction

protected theorem sound {a b : C} {f‚ÇÅ f‚ÇÇ : a ‚ü∂ b} (h : r f‚ÇÅ f‚ÇÇ) :
    (functor r).map f‚ÇÅ = (functor r).map f‚ÇÇ := by
  simpa using Quot.sound (CompClosure.intro (ùüô a) f‚ÇÅ f‚ÇÇ (ùüô b) h)
#align category_theory.quotient.sound CategoryTheory.Quotient.sound

theorem functor_map_eq_iff [h : Congruence r] {X Y : C} (f f' : X ‚ü∂ Y) :
    (functor r).map f = (functor r).map f' ‚Üî r f f' := by
  constructor
  ¬∑ erw [Quot.eq]
    intro h
    induction' h with m m' hm
    ¬∑ cases hm
      apply Congruence.compLeft
      apply Congruence.compRight
      assumption
    ¬∑ haveI := (h.isEquiv : IsEquiv _ (@r X Y))
      -- porting note: had to add this line for `refl` (and name the `Congruence` argument)
      apply refl
    ¬∑ apply symm
      assumption
    ¬∑ apply _root_.trans <;> assumption
  ¬∑ apply Quotient.sound
#align category_theory.quotient.functor_map_eq_iff CategoryTheory.Quotient.functor_map_eq_iff

variable {D : Type _} [Category D] (F : C ‚•§ D)
  (H : ‚àÄ (x y : C) (f‚ÇÅ f‚ÇÇ : x ‚ü∂ y), r f‚ÇÅ f‚ÇÇ ‚Üí F.map f‚ÇÅ = F.map f‚ÇÇ)

/-- The induced functor on the quotient category. -/
@[simps]
def lift : Quotient r ‚•§ D where
  obj a := F.obj a.as
  map := @fun a b hf ‚Ü¶
    Quot.liftOn hf (fun f ‚Ü¶ F.map f)
      (by
        rintro _ _ ‚ü®_, _, _, _, h‚ü©
        simp [H _ _ _ _ h])
  map_id a := F.map_id a.as
  map_comp := by
    rintro a b c ‚ü®f‚ü© ‚ü®g‚ü©
    exact F.map_comp f g
#align category_theory.quotient.lift CategoryTheory.Quotient.lift

theorem lift_spec : functor r ‚ãô lift r F H = F := by
  apply Functor.ext; rotate_left
  ¬∑ rintro X
    rfl
  ¬∑ rintro X Y f
    simp
#align category_theory.quotient.lift_spec CategoryTheory.Quotient.lift_spec

theorem lift_unique (Œ¶ : Quotient r ‚•§ D) (hŒ¶ : functor r ‚ãô Œ¶ = F) : Œ¶ = lift r F H := by
  subst_vars
  fapply Functor.hext
  ¬∑ rintro X
    dsimp [lift, Functor]
    congr
  ¬∑ rintro _ _ f
    dsimp [lift, Functor]
    refine Quot.inductionOn f (fun _ ‚Ü¶ ?_) -- porting note: this line was originally an `apply`
    simp only [Quot.liftOn_mk, Functor.comp_map]
    congr
#align category_theory.quotient.lift_unique CategoryTheory.Quotient.lift_unique

/-- The original functor factors through the induced functor. -/
def lift.isLift : functor r ‚ãô lift r F H ‚âÖ F :=
  NatIso.ofComponents fun X ‚Ü¶ Iso.refl _
#align category_theory.quotient.lift.is_lift CategoryTheory.Quotient.lift.isLift

@[simp]
theorem lift.isLift_hom (X : C) : (lift.isLift r F H).hom.app X = ùüô (F.obj X) :=
  rfl
#align category_theory.quotient.lift.is_lift_hom CategoryTheory.Quotient.lift.isLift_hom

@[simp]
theorem lift.isLift_inv (X : C) : (lift.isLift r F H).inv.app X = ùüô (F.obj X) :=
  rfl
#align category_theory.quotient.lift.is_lift_inv CategoryTheory.Quotient.lift.isLift_inv

theorem lift_map_functor_map {X Y : C} (f : X ‚ü∂ Y) :
    (lift r F H).map ((functor r).map f) = F.map f := by
  rw [‚Üê NatIso.naturality_1 (lift.isLift r F H)]
  dsimp
  simp
#align category_theory.quotient.lift_map_functor_map CategoryTheory.Quotient.lift_map_functor_map

end Quotient

end CategoryTheory
