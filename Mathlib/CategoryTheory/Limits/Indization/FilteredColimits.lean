/-
Copyright (c) 2024 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Comma.Presheaf.Colimit
import Mathlib.CategoryTheory.Limits.Filtered
import Mathlib.CategoryTheory.Limits.FilteredColimitCommutesFiniteLimit
import Mathlib.CategoryTheory.Limits.FunctorToTypes
import Mathlib.CategoryTheory.Limits.Indization.IndObject
import Mathlib.Logic.Small.Set

/-!
# Ind-objects are closed under filtered colimits

We show that if `F : I â¥¤ Cáµ’áµ– â¥¤ Type v` is a functor such that `I` is small and filtered and
`F.obj i` is an ind-object for all `i`, then `colimit F` is also an ind-object.

Our proof is a slight variant of the proof given in Kashiwara-Schapira.

## References
* [M. Kashiwara, P. Schapira, *Categories and Sheaves*][Kashiwara2006], Theorem 6.1.8
-/

universe v u

namespace CategoryTheory.Limits

open CategoryTheory CategoryTheory.CostructuredArrow CategoryTheory.Functor

variable {C : Type u} [Category.{v} C]

namespace IndizationClosedUnderFilteredColimitsAux

variable {I : Type v} [SmallCategory I] (F : I â¥¤ Cáµ’áµ– â¥¤ Type v)


section Interchange

/-!
We start by stating the key interchange property `exists_nonempty_limit_obj_of_isColimit`. It
consists of pulling out a colimit out of a hom functor and interchanging a filtered colimit with
a finite limit.
-/

variable {J : Type v} [SmallCategory J] [FinCategory J]

variable (G : J â¥¤ CostructuredArrow yoneda (colimit F))

-- We introduce notation for the functor `J â¥¤ Over (colimit F)` induced by `G`.
local notation "ğ’¢" => Functor.op G â‹™ Functor.op (toOver yoneda (colimit F))

variable {K : Type v} [SmallCategory K] (H : K â¥¤ Over (colimit F))

/-- (implementation) Pulling out a colimit out of a hom functor is one half of the key lemma. Note
that all of the heavy lifting actually happens in `CostructuredArrow.toOverCompYonedaColimit`
and `yonedaYonedaColimit`. -/
noncomputable def compYonedaColimitIsoColimitCompYoneda :
    ğ’¢ â‹™ yoneda.obj (colimit H) â‰… colimit (H â‹™ yoneda â‹™ (whiskeringLeft _ _ _).obj ğ’¢) := calc
  ğ’¢ â‹™ yoneda.obj (colimit H) â‰… ğ’¢ â‹™ colimit (H â‹™ yoneda) :=
        isoWhiskerLeft G.op (toOverCompYonedaColimit H)
  _ â‰… ğ’¢ â‹™ (H â‹™ yoneda).flip â‹™ colim := isoWhiskerLeft _ (colimitIsoFlipCompColim _)
  _ â‰… (H â‹™ yoneda â‹™ (whiskeringLeft _ _ _).obj ğ’¢).flip â‹™ colim := Iso.refl _
  _ â‰… colimit (H â‹™ yoneda â‹™ (whiskeringLeft _ _ _).obj ğ’¢) := (colimitIsoFlipCompColim _).symm

theorem exists_nonempty_limit_obj_of_colimit [IsFiltered K]
    (h : Nonempty <| limit <| ğ’¢ â‹™ yoneda.obj (colimit H)) :
    âˆƒ k, Nonempty <| limit <| ğ’¢ â‹™ yoneda.obj (H.obj k) := by
  obtain âŸ¨tâŸ© := h
  let tâ‚‚ := limMap (compYonedaColimitIsoColimitCompYoneda F G H).hom t
  let tâ‚ƒ := (colimitLimitIso (H â‹™ yoneda â‹™ (whiskeringLeft _ _ _).obj ğ’¢).flip).inv tâ‚‚
  obtain âŸ¨k, y, -âŸ© := Types.jointly_surjective'.{v, max u v} tâ‚ƒ
  refine âŸ¨k, âŸ¨?_âŸ©âŸ©
  let z := (limitObjIsoLimitCompEvaluation (H â‹™ yoneda â‹™ (whiskeringLeft _ _ _).obj ğ’¢).flip k).hom y
  let y := flipCompEvaluation (H â‹™ yoneda â‹™ (whiskeringLeft _ _ _).obj ğ’¢) k
  exact (lim.mapIso y).hom z

theorem exists_nonempty_limit_obj_of_isColimit [IsFiltered K] {c : Cocone H} (hc : IsColimit c)
    (T : Over (colimit F)) (hT : c.pt â‰… T)
    (h : Nonempty <| limit <| ğ’¢ â‹™ yoneda.obj T) :
    âˆƒ k, Nonempty <| limit <| ğ’¢ â‹™ yoneda.obj (H.obj k) := by
  refine exists_nonempty_limit_obj_of_colimit F G H ?_
  suffices T â‰… colimit H from Nonempty.map (lim.map (whiskerLeft ğ’¢ (yoneda.map this.hom))) h
  refine hT.symm â‰ªâ‰« IsColimit.coconePointUniqueUpToIso hc (colimit.isColimit _)

end Interchange

theorem isFiltered [IsFiltered I] (hF : âˆ€ i, IsIndObject (F.obj i)) :
    IsFiltered (CostructuredArrow yoneda (colimit F)) := by
  -- It suffices to show that for any functor `G : J â¥¤ CostructuredArrow yoneda (colimit F)` with
  -- `J` finite there is some `X` such that the set
  -- `lim Hom_{CostructuredArrow yoneda (colimit F)}(GÂ·, X)` is nonempty.
  refine IsFiltered.iff_nonempty_limit.mpr (fun {J _ _} G => ?_)
  -- We begin by remarking that `lim Hom_{Over (colimit F)}(yGÂ·, ğŸ™ (colimit F))` is nonempty,
  -- simply because `ğŸ™ (colimit F)` is the terminal object. Here `y` is the functor
  -- `CostructuredArrow yoneda (colimit F) â¥¤ Over (colimit F)` induced by `yoneda`.
  have hâ‚ : Nonempty (limit (G.op â‹™ (CostructuredArrow.toOver _ _).op â‹™
      yoneda.obj (Over.mk (ğŸ™ (colimit F))))) :=
    âŸ¨Types.Limit.mk _ (fun j => Over.mkIdTerminal.from _) (by simp)âŸ©
  -- `ğŸ™ (colimit F)` is the colimit of the diagram in `Over (colimit F)` given by the arrows of
  -- the form `Fi âŸ¶ colimit F`. Thus, pulling the colimit out of the hom functor and commuting
  -- the finite limit with the filtered colimit, we obtain
  -- `lim_j Hom_{Over (colimit F)}(yGj, ğŸ™ (colimit F)) â‰…`
  --   `colim_i lim_j Hom_{Over (colimit F)}(yGj, colimit.Î¹ F i)`, and so we find `i` such that
  -- the limit is non-empty.
  obtain âŸ¨i, hiâŸ© := exists_nonempty_limit_obj_of_isColimit F G _
    (colimit.isColimitToOver F) _ (Iso.refl _) hâ‚
  -- `F.obj i` is a small filtered colimit of representables, say of the functor `H : K â¥¤ C`, so
  -- `ğŸ™ (F.obj i)` is the colimit of the arrows of the form `yHk âŸ¶ Fi` in `Over Fi`.
  -- Then `colimit.Î¹ F i` is the colimit of the arrows of the form
  -- `H.obj F âŸ¶ F.obj i âŸ¶ colimit F` in `Over (colimit F)`.
  obtain âŸ¨âŸ¨PâŸ©âŸ© := hF i
  let hc : IsColimit ((Over.map (colimit.Î¹ F i)).mapCocone P.cocone.toOver) :=
    isColimitOfPreserves (Over.map _) (Over.isColimitToOver P.coconeIsColimit)
  -- Again, we pull the colimit out of the hom functor and commute limit and colimit to obtain
  -- `lim_j Hom_{Over (colimit F)}(yGj, colimit.Î¹ F i) â‰…`
  --   `colim_k lim_j Hom_{Over (colimit F)}(yGj, yHk)`, and so we find `k` such that the limit
  -- is non-empty.
  obtain âŸ¨k, hkâŸ© : âˆƒ k, Nonempty (limit (G.op â‹™ (CostructuredArrow.toOver yoneda (colimit F)).op â‹™
      yoneda.obj ((CostructuredArrow.toOver yoneda (colimit F)).obj <|
        (CostructuredArrow.pre P.F yoneda (colimit F)).obj <|
          (map (colimit.Î¹ F i)).obj <| mk _))) :=
    exists_nonempty_limit_obj_of_isColimit F G _ hc _ (Iso.refl _) hi
  have htO : (CostructuredArrow.toOver yoneda (colimit F)).FullyFaithful := .ofFullyFaithful _
  -- Since the inclusion `y : CostructuredArrow yoneda (colimit F) â¥¤ Over (colimit F)` is fully
  -- faithful, `lim_j Hom_{Over (colimit F)}(yGj, yHk) â‰…`
  --   `lim_j Hom_{CostructuredArrow yoneda (colimit F)}(Gj, Hk)` and so `Hk` is the object we're
  -- looking for.
  let q := htO.homNatIsoMaxRight
  obtain âŸ¨t'âŸ© := Nonempty.map (limMap (isoWhiskerLeft G.op (q _)).hom) hk
  exact âŸ¨_, âŸ¨((preservesLimitIso uliftFunctor.{u, v} _).inv t').downâŸ©âŸ©

end IndizationClosedUnderFilteredColimitsAux

theorem isIndObject_colimit (I : Type v) [SmallCategory I] [IsFiltered I]
    (F : I â¥¤ Cáµ’áµ– â¥¤ Type v) (hF : âˆ€ i, IsIndObject (F.obj i)) : IsIndObject (colimit F) := by
  have : IsFiltered (CostructuredArrow yoneda (colimit F)) :=
    IndizationClosedUnderFilteredColimitsAux.isFiltered F hF
  refine (isIndObject_iff _).mpr âŸ¨this, ?_âŸ©
  -- It remains to show that `CostructuredArrow yoneda (colimit F)` is finally small. Because we
  -- have already shown it is filtered, it suffices to exhibit a small weakly terminal set. For this
  -- we use that all the `CostructuredArrow yoneda (F.obj i)` have small weakly terminal sets.
  have : âˆ€ i, âˆƒ (s : Set (CostructuredArrow yoneda (F.obj i))) (_ : Small.{v} s),
      âˆ€ i, âˆƒ j âˆˆ s, Nonempty (i âŸ¶ j) :=
    fun i => (hF i).finallySmall.exists_small_weakly_terminal_set
  choose s hs j hjs hj using this
  refine finallySmall_of_small_weakly_terminal_set
    (â‹ƒ i, (map (colimit.Î¹ F i)).obj '' (s i)) (fun A => ?_)
  obtain âŸ¨i, y, hyâŸ© := FunctorToTypes.jointly_surjective'.{v, v} F _ (yonedaEquiv A.hom)
  let y' : CostructuredArrow yoneda (F.obj i) := mk (yonedaEquiv.symm y)
  obtain âŸ¨xâŸ© := hj _ y'
  refine âŸ¨(map (colimit.Î¹ F i)).obj (j i y'), ?_, âŸ¨?_âŸ©âŸ©
  Â· simp only [Set.mem_iUnion, Set.mem_image]
    exact âŸ¨i, j i y', hjs _ _, rflâŸ©
  Â· refine ?_ â‰« (map (colimit.Î¹ F i)).map x
    refine homMk (ğŸ™ A.left) (yonedaEquiv.injective ?_)
    simp [-EmbeddingLike.apply_eq_iff_eq, hy, yonedaEquiv_comp, y']

end CategoryTheory.Limits
