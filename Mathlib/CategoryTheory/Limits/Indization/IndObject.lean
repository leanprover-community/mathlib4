/-
Copyright (c) 2024 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.FinallySmall
import Mathlib.CategoryTheory.Limits.Presheaf
import Mathlib.CategoryTheory.Filtered.Small

/-!
# Ind-objects

For a presheaf `A : C·µí·µñ ‚•§ Type v` we define the type `IndObjectPresentation A` of presentations
of `A` as a small filtered colimit of representable presheaves and define the predicate
`IsIndObject A` asserting that there is at least one such presentation.

A presheaf is an ind-object if and only if the category `CostructuredArrow yoneda A` is filtered
and finally small. In this way, `CostructuredArrow yoneda A` can be thought of the universal
indexing category for the representation of `A` as a small filtered colimit of representable
presheaves.

## Future work

There are various useful ways to understand natural transformations between ind-objects in terms
of their presentations.

The ind-objects form a locally `v`-small category `IndCategory C` which has numerous interesting
properties.

## Implementation notes

One might be tempted to introduce another universe parameter and consider being a `w`-ind-object
as a property of presheaves `C ‚•§ TypeMax.{v, w}`. This comes with significant technical hurdles.
The recommended alternative is to consider ind-objects over `ULiftHom.{w} C` instead.

## References
* [M. Kashiwara, P. Schapira, *Categories and Sheaves*][Kashiwara2006], Chapter 6
-/

universe v u

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]

/-- The data that witnesses that a presheaf `A` is an ind-object. It consists of a small
    filtered indexing category `I`, a diagram `F : I ‚•§ C` and the data for a colimit cocone on
    `F ‚ãô yoneda : I ‚•§ C·µí·µñ ‚•§ Type v` with cocone point `A`. -/
structure IndObjectPresentation (A : C·µí·µñ ‚•§ Type v) where
  /-- The indexing category of the filtered colimit presentation -/
  I : Type v
  /-- The indexing category of the filtered colimit presentation -/
  [‚Ñê : SmallCategory I]
  [hI : IsFiltered I]
  /-- The diagram of the filtered colimit presentation -/
  F : I ‚•§ C
  /-- Use `IndObjectPresentation.cocone` instead. -/
  Œπ : F ‚ãô yoneda ‚ü∂ (Functor.const I).obj A
  /-- Use `IndObjectPresenation.coconeIsColimit` instead. -/
  isColimit : IsColimit (Cocone.mk A Œπ)

namespace IndObjectPresentation

/-- Alternative constructor for `IndObjectPresentation` taking a cocone instead of its defining
    natural transformation. -/
@[simps]
def ofCocone {I : Type v} [SmallCategory I] [IsFiltered I] {F : I ‚•§ C}
    (c : Cocone (F ‚ãô yoneda)) (hc : IsColimit c) : IndObjectPresentation c.pt where
  I := I
  F := F
  Œπ := c.Œπ
  isColimit := hc

variable {A : C·µí·µñ ‚•§ Type v} (P : IndObjectPresentation A)

instance : SmallCategory P.I := P.‚Ñê
instance : IsFiltered P.I := P.hI

/-- The (colimit) cocone with cocone point `A`. -/
@[simps pt]
def cocone : Cocone (P.F ‚ãô yoneda) where
  pt := A
  Œπ := P.Œπ

/-- `P.cocone` is a colimit cocone. -/
def coconeIsColimit : IsColimit P.cocone :=
  P.isColimit

/-- If `A` and `B` are isomorphic, then an ind-object presentation of `A` can be extended to an
    ind-object presentation of `B`. -/
@[simps!]
noncomputable def extend {A B : C·µí·µñ ‚•§ Type v} (P : IndObjectPresentation A) (Œ∑ : A ‚ü∂ B) [IsIso Œ∑] :
    IndObjectPresentation B :=
  .ofCocone (P.cocone.extend Œ∑) (P.coconeIsColimit.extendIso (by exact Œ∑))

/-- The canonical comparison functor between the indexing category of the presentation and the
    comma category `CostructuredArrow yoneda A`. This functor is always final. -/
@[simps! obj_left obj_right_as obj_hom map_left]
def toCostructuredArrow : P.I ‚•§ CostructuredArrow yoneda A :=
  P.cocone.toCostructuredArrow ‚ãô CostructuredArrow.pre _ _ _

instance : P.toCostructuredArrow.Final :=
  final_toCostructuredArrow_comp_pre _ P.coconeIsColimit

/-- Representable presheaves are (trivially) ind-objects. -/
@[simps]
def yoneda (X : C) : IndObjectPresentation (yoneda.obj X) where
  I := Discrete PUnit.{v + 1}
  F := Functor.fromPUnit X
  Œπ := { app := fun s => ùüô _ }
  isColimit :=
    { desc := fun s => s.Œπ.app ‚ü®PUnit.unit‚ü©
      uniq := fun s m h => h ‚ü®PUnit.unit‚ü© }

end IndObjectPresentation

/-- A presheaf is called an ind-object if it can be written as a filtered colimit of representable
    presheaves. -/
structure IsIndObject (A : C·µí·µñ ‚•§ Type v) : Prop where
  mk' :: nonempty_presentation : Nonempty (IndObjectPresentation A)

theorem IsIndObject.mk {A : C·µí·µñ ‚•§ Type v} (P : IndObjectPresentation A) : IsIndObject A :=
  ‚ü®‚ü®P‚ü©‚ü©

/-- Representable presheaves are (trivially) ind-objects. -/
theorem isIndObject_yoneda (X : C) : IsIndObject (yoneda.obj X) :=
  .mk <| IndObjectPresentation.yoneda X

namespace IsIndObject

variable {A : C·µí·µñ ‚•§ Type v}

theorem map {A B : C·µí·µñ ‚•§ Type v} (Œ∑ : A ‚ü∂ B) [IsIso Œ∑] : IsIndObject A ‚Üí IsIndObject B
  | ‚ü®‚ü®P‚ü©‚ü© => ‚ü®‚ü®P.extend Œ∑‚ü©‚ü©

theorem iff_of_iso {A B : C·µí·µñ ‚•§ Type v} (Œ∑ : A ‚ü∂ B) [IsIso Œ∑] : IsIndObject A ‚Üî IsIndObject B :=
  ‚ü®.map Œ∑, .map (inv Œ∑)‚ü©

/-- Pick a presentation for an ind-object using choice. -/
noncomputable def presentation : IsIndObject A ‚Üí IndObjectPresentation A
  | ‚ü®P‚ü© => P.some

theorem isFiltered (h : IsIndObject A) : IsFiltered (CostructuredArrow yoneda A) :=
  IsFiltered.of_final h.presentation.toCostructuredArrow

theorem finallySmall (h : IsIndObject A) : FinallySmall.{v} (CostructuredArrow yoneda A) :=
  FinallySmall.mk' h.presentation.toCostructuredArrow

end IsIndObject

open IsFiltered.SmallFilteredIntermediate

theorem isIndObject_of_isFiltered_of_finallySmall (A : C·µí·µñ ‚•§ Type v)
    [IsFiltered (CostructuredArrow yoneda A)] [FinallySmall.{v} (CostructuredArrow yoneda A)] :
    IsIndObject A := by
  have h‚ÇÅ : (factoring (fromFinalModel (CostructuredArrow yoneda A)) ‚ãô
      inclusion (fromFinalModel (CostructuredArrow yoneda A))).Final := Functor.final_of_natIso
    (factoringCompInclusion (fromFinalModel <| CostructuredArrow yoneda A)).symm
  have h‚ÇÇ : Functor.Final (inclusion (fromFinalModel (CostructuredArrow yoneda A))) :=
    Functor.final_of_comp_full_faithful' (factoring _) (inclusion _)
  let c := (tautologicalCocone A).whisker (inclusion (fromFinalModel (CostructuredArrow yoneda A)))
  let hc : IsColimit c := (Functor.Final.isColimitWhiskerEquiv _ _).symm
    (isColimitTautologicalCocone A)
  have hq : Nonempty (FinalModel (CostructuredArrow yoneda A)) := Nonempty.map
    (Functor.Final.lift (fromFinalModel (CostructuredArrow yoneda A))) IsFiltered.nonempty
  exact ‚ü®_, inclusion (fromFinalModel _) ‚ãô CostructuredArrow.proj yoneda A, c.Œπ, hc‚ü©

/-- The recognition theorem for ind-objects: `A : C·µí·µñ ‚•§ Type v` is an ind-object if and only if
    `CostructuredArrow yoneda A` is filtered and finally `v`-small.

    Theorem 6.1.5 of [Kashiwara2006] -/
theorem isIndObject_iff (A : C·µí·µñ ‚•§ Type v) : IsIndObject A ‚Üî
    (IsFiltered (CostructuredArrow yoneda A) ‚àß FinallySmall.{v} (CostructuredArrow yoneda A)) :=
  ‚ü®fun h => ‚ü®h.isFiltered, h.finallySmall‚ü©,
   fun ‚ü®_, _‚ü© => isIndObject_of_isFiltered_of_finallySmall A‚ü©

end CategoryTheory.Limits
