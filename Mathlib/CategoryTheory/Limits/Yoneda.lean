/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.FunctorCategory
import Mathlib.Util.AssertExists

#align_import category_theory.limits.yoneda from "leanprover-community/mathlib"@"e97cf15cd1aec9bd5c193b2ffac5a6dc9118912b"

/-!
# Limit properties relating to the (co)yoneda embedding.

We calculate the colimit of `Y â†¦ (X âŸ¶ Y)`, which is just `PUnit`.
(This is used in characterising cofinal functors.)

We also show the (co)yoneda embeddings preserve limits and jointly reflect them.
-/

open Opposite CategoryTheory Limits

universe w v u

namespace CategoryTheory

namespace Coyoneda

variable {C : Type v} [SmallCategory C]

/-- The colimit cocone over `coyoneda.obj X`, with cocone point `PUnit`.
-/
@[simps]
def colimitCocone (X : Cáµ’áµ–) : Cocone (coyoneda.obj X) where
  pt := PUnit
  Î¹ := { app := by aesop_cat }
                   -- ğŸ‰ no goals
#align category_theory.coyoneda.colimit_cocone CategoryTheory.Coyoneda.colimitCocone

/-- The proposed colimit cocone over `coyoneda.obj X` is a colimit cocone.
-/
@[simps]
def colimitCoconeIsColimit (X : Cáµ’áµ–) : IsColimit (colimitCocone X)
    where
  desc s _ := s.Î¹.app (unop X) (ğŸ™ _)
  fac s Y := by
    funext f
    -- âŠ¢ (NatTrans.app (colimitCocone X).Î¹ Y â‰« (fun s x => NatTrans.app s.Î¹ X.unop (ğŸ™ â€¦
    convert congr_fun (s.w f).symm (ğŸ™ (unop X))
    -- âŠ¢ NatTrans.app s.Î¹ Y f = ((coyoneda.obj X).map f â‰« NatTrans.app s.Î¹ Y) (ğŸ™ X.un â€¦
    simp only [coyoneda_obj_obj, Functor.const_obj_obj, types_comp_apply,
      coyoneda_obj_map, Category.id_comp]
  uniq s m w := by
    apply funext; rintro âŸ¨âŸ©
    -- âŠ¢ âˆ€ (x : (colimitCocone X).pt), m x = (fun s x => NatTrans.app s.Î¹ X.unop (ğŸ™ X â€¦
                  -- âŠ¢ m PUnit.unit = (fun s x => NatTrans.app s.Î¹ X.unop (ğŸ™ X.unop)) s PUnit.unit
    dsimp
    -- âŠ¢ m PUnit.unit = NatTrans.app s.Î¹ X.unop (ğŸ™ X.unop)
    rw [â† w]
    -- âŠ¢ m PUnit.unit = (NatTrans.app (colimitCocone X).Î¹ X.unop â‰« m) (ğŸ™ X.unop)
    simp
    -- ğŸ‰ no goals
#align category_theory.coyoneda.colimit_cocone_is_colimit CategoryTheory.Coyoneda.colimitCoconeIsColimit

instance (X : Cáµ’áµ–) : HasColimit (coyoneda.obj X) :=
  HasColimit.mk
    { cocone := _
      isColimit := colimitCoconeIsColimit X }

/-- The colimit of `coyoneda.obj X` is isomorphic to `PUnit`.
-/
noncomputable def colimitCoyonedaIso (X : Cáµ’áµ–) : colimit (coyoneda.obj X) â‰… PUnit := by
  apply colimit.isoColimitCocone
    { cocone := _
      isColimit := colimitCoconeIsColimit X }
#align category_theory.coyoneda.colimit_coyoneda_iso CategoryTheory.Coyoneda.colimitCoyonedaIso

end Coyoneda

variable {C : Type u} [Category.{v} C]

open Limits

/-- The yoneda embedding `yoneda.obj X : Cáµ’áµ– â¥¤ Type v` for `X : C` preserves limits. -/
instance yonedaPreservesLimits (X : C) : PreservesLimits (yoneda.obj X)
    where preservesLimitsOfShape {J} ğ’¥ :=
    { preservesLimit := fun {K} =>
        { preserves := fun {c} t =>
            { lift := fun s x =>
                Quiver.Hom.unop (t.lift âŸ¨op X, fun j => (s.Ï€.app j x).op, fun jâ‚ jâ‚‚ Î± => _âŸ©)
              fac := fun s j => funext fun x => Quiver.Hom.op_inj (t.fac _ _)
              uniq := fun s m w =>
                funext fun x => by
                  refine' Quiver.Hom.op_inj (t.uniq âŸ¨op X, _, _âŸ© _ fun j => _)
                  -- âŠ¢ âˆ€ (X : C) {J : Type v} (ğ’¥ : Category.{v, v} J) {K : J â¥¤ Cáµ’áµ–} {c : Cone K}, I â€¦
                  Â· intro X _ _ _ _ _ s _ _ _ Î±  -- Porting note: refine' gave a crazy goal
                    -- âŠ¢ ((Functor.const Jâœ).obj (op X)).map Î± â‰« (fun j => (NatTrans.app s.Ï€ j xâœ).op â€¦
                    dsimp
                    -- âŠ¢ ğŸ™ (op X) â‰« (NatTrans.app s.Ï€ jâ‚‚âœ xâœ).op = (NatTrans.app s.Ï€ jâ‚âœ xâœ).op â‰« Kâœ. â€¦
                    simp [â† s.w Î±]
                    -- ğŸ‰ no goals
                  -- See library note [dsimp, simp]
                  Â· exact Quiver.Hom.unop_inj (congrFun (w j) x) } } }
                    -- ğŸ‰ no goals
#align category_theory.yoneda_preserves_limits CategoryTheory.yonedaPreservesLimits

/-- The coyoneda embedding `coyoneda.obj X : C â¥¤ Type v` for `X : Cáµ’áµ–` preserves limits. -/
instance coyonedaPreservesLimits (X : Cáµ’áµ–) : PreservesLimits (coyoneda.obj X)
    where preservesLimitsOfShape {J} ğ’¥ :=
    { preservesLimit := fun {K} =>
        { preserves := fun {c} t =>
            { lift := fun s x =>
                t.lift
                  âŸ¨unop X, fun j => s.Ï€.app j x, fun jâ‚ jâ‚‚ Î± => by
                    dsimp
                    -- âŠ¢ ğŸ™ X.unop â‰« NatTrans.app s.Ï€ jâ‚‚ x = NatTrans.app s.Ï€ jâ‚ x â‰« K.map Î±
                    simp [â† s.w Î±]âŸ©
                    -- ğŸ‰ no goals
              -- See library note [dsimp, simp]
              fac := fun s j => funext fun x => t.fac _ _
              uniq := fun s m w =>
                funext fun x => by
                  refine' t.uniq âŸ¨unop X, _âŸ© _ fun j => _
                  -- âŠ¢ m x â‰« NatTrans.app c.Ï€ j = NatTrans.app { pt := X.unop, Ï€ := NatTrans.mk fun â€¦
                  exact congrFun (w j) x } } }
                  -- ğŸ‰ no goals
#align category_theory.coyoneda_preserves_limits CategoryTheory.coyonedaPreservesLimits

/-- The yoneda embeddings jointly reflect limits. -/
def yonedaJointlyReflectsLimits (J : Type w) [SmallCategory J] (K : J â¥¤ Cáµ’áµ–) (c : Cone K)
    (t : âˆ€ X : C, IsLimit ((yoneda.obj X).mapCone c)) : IsLimit c :=
  let s' : âˆ€ s : Cone K, Cone (K â‹™ yoneda.obj s.pt.unop) := fun s =>
    âŸ¨PUnit, fun j _ => (s.Ï€.app j).unop, fun jâ‚ jâ‚‚ Î± =>
      funext fun _ => Quiver.Hom.op_inj (s.w Î±).symmâŸ©
  { lift := fun s => ((t s.pt.unop).lift (s' s) PUnit.unit).op
    fac := fun s j => Quiver.Hom.unop_inj (congr_fun ((t s.pt.unop).fac (s' s) j) PUnit.unit)
    uniq := fun s m w => by
      apply Quiver.Hom.unop_inj
      -- âŠ¢ m.unop = ((fun s => (IsLimit.lift (t s.pt.unop) (s' s) PUnit.unit).op) s).unop
      suffices (fun _ : PUnit => m.unop) = (t s.pt.unop).lift (s' s) by
        apply congr_fun this PUnit.unit
      apply (t _).uniq (s' s) _ fun j => _
      -- âŠ¢ âˆ€ (j : J), (fun x => m.unop) â‰« NatTrans.app ((yoneda.obj s.pt.unop).mapCone  â€¦
      intro j
      -- âŠ¢ (fun x => m.unop) â‰« NatTrans.app ((yoneda.obj s.pt.unop).mapCone c).Ï€ j = Na â€¦
      funext
      -- âŠ¢ ((fun x => m.unop) â‰« NatTrans.app ((yoneda.obj s.pt.unop).mapCone c).Ï€ j) xâœ â€¦
      exact Quiver.Hom.op_inj (w j) }
      -- ğŸ‰ no goals
#align category_theory.yoneda_jointly_reflects_limits CategoryTheory.yonedaJointlyReflectsLimits

/-- The coyoneda embeddings jointly reflect limits. -/
def coyonedaJointlyReflectsLimits (J : Type w) [SmallCategory J] (K : J â¥¤ C) (c : Cone K)
    (t : âˆ€ X : Cáµ’áµ–, IsLimit ((coyoneda.obj X).mapCone c)) : IsLimit c :=
  let s' : âˆ€ s : Cone K, Cone (K â‹™ coyoneda.obj (op s.pt)) := fun s =>
    âŸ¨PUnit, fun j _ => s.Ï€.app j, fun jâ‚ jâ‚‚ Î± => funext fun _ => (s.w Î±).symmâŸ©
  { lift := fun s => (t (op s.pt)).lift (s' s) PUnit.unit
    fac := fun s j => congr_fun ((t _).fac (s' s) j) PUnit.unit
    uniq := fun s m w => by
      suffices (fun _ : PUnit => m) = (t _).lift (s' s) by apply congr_fun this PUnit.unit
      -- âŠ¢ (fun x => m) = IsLimit.lift (t (op s.pt)) (s' s)
      apply (t _).uniq (s' s) _ fun j => _
      -- âŠ¢ âˆ€ (j : J), (fun x => m) â‰« NatTrans.app ((coyoneda.obj (op s.pt)).mapCone c). â€¦
      intro j
      -- âŠ¢ (fun x => m) â‰« NatTrans.app ((coyoneda.obj (op s.pt)).mapCone c).Ï€ j = NatTr â€¦
      funext
      -- âŠ¢ ((fun x => m) â‰« NatTrans.app ((coyoneda.obj (op s.pt)).mapCone c).Ï€ j) xâœ =  â€¦
      exact w j }
      -- ğŸ‰ no goals
#align category_theory.coyoneda_jointly_reflects_limits CategoryTheory.coyonedaJointlyReflectsLimits

variable {D : Type u} [SmallCategory D]

instance yonedaFunctorPreservesLimits : PreservesLimits (@yoneda D _) := by
  apply preservesLimitsOfEvaluation
  -- âŠ¢ (k : Dáµ’áµ–) â†’ PreservesLimitsOfSize.{u, u, u, u, u, u + 1} (yoneda â‹™ (evaluati â€¦
  intro K
  -- âŠ¢ PreservesLimitsOfSize.{u, u, u, u, u, u + 1} (yoneda â‹™ (evaluation Dáµ’áµ– (Type â€¦
  change PreservesLimits (coyoneda.obj K)
  -- âŠ¢ PreservesLimits (coyoneda.obj K)
  infer_instance
  -- ğŸ‰ no goals
#align category_theory.yoneda_functor_preserves_limits CategoryTheory.yonedaFunctorPreservesLimits

instance coyonedaFunctorPreservesLimits : PreservesLimits (@coyoneda D _) := by
  apply preservesLimitsOfEvaluation
  -- âŠ¢ (k : D) â†’ PreservesLimitsOfSize.{u, u, u, u, u, u + 1} (coyoneda â‹™ (evaluati â€¦
  intro K
  -- âŠ¢ PreservesLimitsOfSize.{u, u, u, u, u, u + 1} (coyoneda â‹™ (evaluation D (Type â€¦
  change PreservesLimits (yoneda.obj K)
  -- âŠ¢ PreservesLimits (yoneda.obj K)
  infer_instance
  -- ğŸ‰ no goals
#align category_theory.coyoneda_functor_preserves_limits CategoryTheory.coyonedaFunctorPreservesLimits

instance yonedaFunctorReflectsLimits : ReflectsLimits (@yoneda D _) :=
  Limits.fullyFaithfulReflectsLimits _
#align category_theory.yoneda_functor_reflects_limits CategoryTheory.yonedaFunctorReflectsLimits

instance coyonedaFunctorReflectsLimits : ReflectsLimits (@coyoneda D _) :=
  Limits.fullyFaithfulReflectsLimits _
#align category_theory.coyoneda_functor_reflects_limits CategoryTheory.coyonedaFunctorReflectsLimits

end CategoryTheory

assert_not_exists Set.range

-- Porting note: after the port see if this import can be removed
-- assert_not_exists AddCommMonoid
