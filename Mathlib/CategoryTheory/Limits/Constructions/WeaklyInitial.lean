/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.WideEqualizers
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

#align_import category_theory.limits.constructions.weakly_initial from "leanprover-community/mathlib"@"239d882c4fb58361ee8b3b39fb2091320edef10a"

/-!
# Constructions related to weakly initial objects

This file gives constructions related to weakly initial objects, namely:
* If a category has small products and a small weakly initial set of objects, then it has a weakly
  initial object.
* If a category has wide equalizers and a weakly initial object, then it has an initial object.

These are primarily useful to show the General Adjoint Functor Theorem.
-/


universe v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C]

/--
If `C` has (small) products and a small weakly initial set of objects, then it has a weakly initial
object.
-/
theorem has_weakly_initial_of_weakly_initial_set_and_hasProducts [HasProducts.{v} C] {Î¹ : Type v}
    {B : Î¹ â†’ C} (hB : âˆ€ A : C, âˆƒ i, Nonempty (B i âŸ¶ A)) : âˆƒ T : C, âˆ€ X, Nonempty (T âŸ¶ X) :=
  âŸ¨âˆá¶œ B, fun X => âŸ¨Pi.Ï€ _ _ â‰« (hB X).choose_spec.someâŸ©âŸ©
#align category_theory.has_weakly_initial_of_weakly_initial_set_and_has_products CategoryTheory.has_weakly_initial_of_weakly_initial_set_and_hasProducts

/-- If `C` has (small) wide equalizers and a weakly initial object, then it has an initial object.

The initial object is constructed as the wide equalizer of all endomorphisms on the given weakly
initial object.
-/
theorem hasInitial_of_weakly_initial_and_hasWideEqualizers [HasWideEqualizers.{v} C] {T : C}
    (hT : âˆ€ X, Nonempty (T âŸ¶ X)) : HasInitial C := by
  let endos := T âŸ¶ T
  let i := wideEqualizer.Î¹ (id : endos â†’ endos)
  haveI : Nonempty endos := âŸ¨ğŸ™ _âŸ©
  have : âˆ€ X : C, Unique (wideEqualizer (id : endos â†’ endos) âŸ¶ X) := by
    intro X
    refine âŸ¨âŸ¨i â‰« Classical.choice (hT X)âŸ©, fun a => ?_âŸ©
    let E := equalizer a (i â‰« Classical.choice (hT _))
    let e : E âŸ¶ wideEqualizer id := equalizer.Î¹ _ _
    let h : T âŸ¶ E := Classical.choice (hT E)
    have : ((i â‰« h) â‰« e) â‰« i = i â‰« ğŸ™ _ := by
      rw [Category.assoc, Category.assoc]
      apply wideEqualizer.condition (id : endos â†’ endos) (h â‰« e â‰« i)
    rw [Category.comp_id, cancel_mono_id i] at this
    haveI : IsSplitEpi e := IsSplitEpi.mk' âŸ¨i â‰« h, thisâŸ©
    rw [â† cancel_epi e]
    apply equalizer.condition
  exact hasInitial_of_unique (wideEqualizer (id : endos â†’ endos))
#align category_theory.has_initial_of_weakly_initial_and_has_wide_equalizers CategoryTheory.hasInitial_of_weakly_initial_and_hasWideEqualizers

end CategoryTheory
