/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback

/-!
# Constructing pullbacks from binary products and equalizers

If a category as binary products and equalizers, then it has pullbacks.
Also, if a category has binary coproducts and coequalizers, then it has pushouts
-/


universe v u

open CategoryTheory

namespace CategoryTheory.Limits

/-- If the product `X â¨¯ Y` and the equalizer of `Ï€â‚ â‰« f` and `Ï€â‚‚ â‰« g` exist, then the
pullback of `f` and `g` exists: It is given by composing the equalizer with the projections. -/
theorem hasLimit_cospan_of_hasLimit_pair_of_hasLimit_parallelPair {C : Type u} [ğ’ : Category.{v} C]
    {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [HasLimit (pair X Y)]
    [HasLimit (parallelPair (prod.fst â‰« f) (prod.snd â‰« g))] : HasLimit (cospan f g) :=
  let Ï€â‚ : X â¨¯ Y âŸ¶ X := prod.fst
  let Ï€â‚‚ : X â¨¯ Y âŸ¶ Y := prod.snd
  let e := equalizer.Î¹ (Ï€â‚ â‰« f) (Ï€â‚‚ â‰« g)
  HasLimit.mk
    { cone :=
        PullbackCone.mk (e â‰« Ï€â‚) (e â‰« Ï€â‚‚) <| by
          rw [Category.assoc, equalizer.condition]
          simp [e]
      isLimit :=
        PullbackCone.IsLimit.mk _ (fun s => equalizer.lift
          (prod.lift (s.Ï€.app WalkingCospan.left) (s.Ï€.app WalkingCospan.right)) <| by
            rw [â† Category.assoc, limit.lift_Ï€, â† Category.assoc, limit.lift_Ï€]
            exact PullbackCone.condition _)
          (by simp [Ï€â‚, e]) (by simp [Ï€â‚‚, e]) fun s m hâ‚ hâ‚‚ => by
          ext
          Â· dsimp; simpa using hâ‚
          Â· simpa using hâ‚‚ }

section

attribute [local instance] hasLimit_cospan_of_hasLimit_pair_of_hasLimit_parallelPair

/-- If a category has all binary products and all equalizers, then it also has all pullbacks.
As usual, this is not an instance, since there may be a more direct way to construct
pullbacks. -/
theorem hasPullbacks_of_hasBinaryProducts_of_hasEqualizers (C : Type u) [Category.{v} C]
    [HasBinaryProducts C] [HasEqualizers C] : HasPullbacks C :=
  { has_limit := fun F => hasLimit_of_iso (diagramIsoCospan F).symm }

end

/-- If the coproduct `Y â¨¿ Z` and the coequalizer of `f â‰« Î¹â‚` and `g â‰« Î¹â‚‚` exist, then the
pushout of `f` and `g` exists: It is given by composing the inclusions with the coequalizer. -/
theorem hasColimit_span_of_hasColimit_pair_of_hasColimit_parallelPair {C : Type u}
    [ğ’ : Category.{v} C] {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [HasColimit (pair Y Z)]
    [HasColimit (parallelPair (f â‰« coprod.inl) (g â‰« coprod.inr))] : HasColimit (span f g) :=
  let Î¹â‚ : Y âŸ¶ Y â¨¿ Z := coprod.inl
  let Î¹â‚‚ : Z âŸ¶ Y â¨¿ Z := coprod.inr
  let c := coequalizer.Ï€ (f â‰« Î¹â‚) (g â‰« Î¹â‚‚)
  HasColimit.mk
    { cocone :=
        PushoutCocone.mk (Î¹â‚ â‰« c) (Î¹â‚‚ â‰« c) <| by
          rw [â† Category.assoc, â† Category.assoc, coequalizer.condition]
      isColimit :=
        PushoutCocone.IsColimit.mk _
          (fun s => coequalizer.desc
              (coprod.desc (s.Î¹.app WalkingSpan.left) (s.Î¹.app WalkingSpan.right)) <| by
            rw [Category.assoc, colimit.Î¹_desc, Category.assoc, colimit.Î¹_desc]
            exact PushoutCocone.condition _)
          (by simp [Î¹â‚, c]) (by simp [Î¹â‚‚, c]) fun s m hâ‚ hâ‚‚ => by
          ext
          Â· simpa using hâ‚
          Â· simpa using hâ‚‚ }

section

attribute [local instance] hasColimit_span_of_hasColimit_pair_of_hasColimit_parallelPair

/-- If a category has all binary coproducts and all coequalizers, then it also has all pushouts.
As usual, this is not an instance, since there may be a more direct way to construct pushouts. -/
theorem hasPushouts_of_hasBinaryCoproducts_of_hasCoequalizers (C : Type u) [Category.{v} C]
    [HasBinaryCoproducts C] [HasCoequalizers C] : HasPushouts C :=
  hasPushouts_of_hasColimit_span C

end

end CategoryTheory.Limits
