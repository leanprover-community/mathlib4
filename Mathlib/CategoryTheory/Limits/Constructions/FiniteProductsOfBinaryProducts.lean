/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products

/-!
# Constructing finite products from binary products and terminal.

If a category has binary products and a terminal object then it has finite products.
If a functor preserves binary products and the terminal object then it preserves finite products.

## TODO

Provide the dual results.
Show the analogous results for functors which reflect or create (co)limits.
-/


universe v v' u u'

noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

namespace CategoryTheory

variable {J : Type v} [SmallCategory J]
variable {C : Type u} [Category.{v} C]
variable {D : Type u'} [Category.{v'} D]

/--
Given `n+1` objects of `C`, a fan for the last `n` with point `c‚ÇÅ.pt` and
a binary fan on `c‚ÇÅ.pt` and `f 0`, we can build a fan for all `n+1`.

In `extendFanIsLimit` we show that if the two given fans are limits, then this fan is also a
limit.
-/
@[simps!]
def extendFan {n : ‚Ñï} {f : Fin (n + 1) ‚Üí C} (c‚ÇÅ : Fan fun i : Fin n => f i.succ)
    (c‚ÇÇ : BinaryFan (f 0) c‚ÇÅ.pt) : Fan f :=
  Fan.mk c‚ÇÇ.pt
    (by
      refine Fin.cases ?_ ?_
      ¬∑ apply c‚ÇÇ.fst
      ¬∑ intro i
        apply c‚ÇÇ.snd ‚â´ c‚ÇÅ.œÄ.app ‚ü®i‚ü©)

/-- Show that if the two given fans in `extendFan` are limits, then the constructed fan is also a
limit.
-/
def extendFanIsLimit {n : ‚Ñï} (f : Fin (n + 1) ‚Üí C) {c‚ÇÅ : Fan fun i : Fin n => f i.succ}
    {c‚ÇÇ : BinaryFan (f 0) c‚ÇÅ.pt} (t‚ÇÅ : IsLimit c‚ÇÅ) (t‚ÇÇ : IsLimit c‚ÇÇ) :
    IsLimit (extendFan c‚ÇÅ c‚ÇÇ) where
  lift s := by
    apply (BinaryFan.IsLimit.lift' t‚ÇÇ (s.œÄ.app ‚ü®0‚ü©) _).1
    apply t‚ÇÅ.lift ‚ü®_, Discrete.natTrans fun ‚ü®i‚ü© => s.œÄ.app ‚ü®i.succ‚ü©‚ü©
  fac := fun s ‚ü®j‚ü© => by
    refine Fin.inductionOn j ?_ ?_
    ¬∑ apply (BinaryFan.IsLimit.lift' t‚ÇÇ _ _).2.1
    ¬∑ rintro i -
      dsimp only [extendFan_œÄ_app]
      rw [Fin.cases_succ, ‚Üê assoc, (BinaryFan.IsLimit.lift' t‚ÇÇ _ _).2.2, t‚ÇÅ.fac]
      rfl
  uniq s m w := by
    apply BinaryFan.IsLimit.hom_ext t‚ÇÇ
    ¬∑ rw [(BinaryFan.IsLimit.lift' t‚ÇÇ _ _).2.1]
      apply w ‚ü®0‚ü©
    ¬∑ rw [(BinaryFan.IsLimit.lift' t‚ÇÇ _ _).2.2]
      apply t‚ÇÅ.uniq ‚ü®_, _‚ü©
      rintro ‚ü®j‚ü©
      rw [assoc]
      dsimp only [Discrete.natTrans_app]
      rw [‚Üê w ‚ü®j.succ‚ü©]
      dsimp only [extendFan_œÄ_app]
      rw [Fin.cases_succ]

section

variable [HasBinaryProducts C] [HasTerminal C]

/-- If `C` has a terminal object and binary products, then it has a product for objects indexed by
`Fin n`.
This is a helper lemma for `hasFiniteProductsOfHasBinaryAndTerminal`, which is more general
than this.
-/
private theorem hasProduct_fin : ‚àÄ (n : ‚Ñï) (f : Fin n ‚Üí C), HasProduct f
  | 0 => fun _ =>
    letI : HasLimitsOfShape (Discrete (Fin 0)) C :=
      hasLimitsOfShape_of_equivalence (Discrete.equivalence.{0} finZeroEquiv'.symm)
    inferInstance
  | n + 1 => fun f =>
    haveI := hasProduct_fin n
    HasLimit.mk ‚ü®_, extendFanIsLimit f (limit.isLimit _) (limit.isLimit _)‚ü©

/-- If `C` has a terminal object and binary products, then it has finite products. -/
theorem hasFiniteProducts_of_has_binary_and_terminal : HasFiniteProducts C :=
  ‚ü®fun n => ‚ü®fun K => by
    let that : (Discrete.functor fun n => K.obj ‚ü®n‚ü©) ‚âÖ K := Discrete.natIso fun ‚ü®_‚ü© => Iso.refl _
    rw [‚Üê hasLimit_iff_of_iso that]
    apply hasProduct_fin‚ü©‚ü©


end

section Preserves

variable (F : C ‚•§ D)
variable [PreservesLimitsOfShape (Discrete WalkingPair) F]
variable [PreservesLimitsOfShape (Discrete.{0} PEmpty) F]
variable [HasFiniteProducts.{v} C]

/-- If `F` preserves the terminal object and binary products, then it preserves products indexed by
`Fin n` for any `n`.
-/
lemma preservesFinOfPreservesBinaryAndTerminal :
    ‚àÄ (n : ‚Ñï) (f : Fin n ‚Üí C), PreservesLimit (Discrete.functor f) F
  | 0 => fun f => by
    letI : PreservesLimitsOfShape (Discrete (Fin 0)) F :=
      preservesLimitsOfShape_of_equiv.{0, 0} (Discrete.equivalence finZeroEquiv'.symm) _
    infer_instance
  | n + 1 => by
    haveI := preservesFinOfPreservesBinaryAndTerminal n
    intro f
    apply
      preservesLimit_of_preserves_limit_cone
        (extendFanIsLimit f (limit.isLimit _) (limit.isLimit _)) _
    apply (isLimitMapConeFanMkEquiv _ _ _).symm _
    let this :=
      extendFanIsLimit (fun i => F.obj (f i)) (isLimitOfHasProductOfPreservesLimit F _)
        (isLimitOfHasBinaryProductOfPreservesLimit F _ _)
    refine IsLimit.ofIsoLimit this ?_
    apply Cones.ext _ _
    ¬∑ apply Iso.refl _
    rintro ‚ü®j‚ü©
    refine Fin.inductionOn j ?_ ?_
    ¬∑ apply (Category.id_comp _).symm
    ¬∑ rintro i _
      dsimp [extendFan_œÄ_app, Iso.refl_hom, Fan.mk_œÄ_app]
      change F.map _ ‚â´ _ = ùüô _ ‚â´ _
      simp only [id_comp, ‚Üê F.map_comp]
      rfl

/-- If `F` preserves the terminal object and binary products then it preserves finite products. -/
lemma Limits.PreservesFiniteProducts.of_preserves_binary_and_terminal :
    PreservesFiniteProducts F where
  preserves n := by
    refine ‚ü®fun {K} ‚Ü¶ ?_‚ü©
    let that : (Discrete.functor fun n => K.obj ‚ü®n‚ü©) ‚âÖ K := Discrete.natIso fun ‚ü®i‚ü© => Iso.refl _
    haveI := preservesFinOfPreservesBinaryAndTerminal F n fun n => K.obj ‚ü®n‚ü©
    apply preservesLimit_of_iso_diagram F that

@[deprecated PreservesFiniteProducts.of_preserves_binary_and_terminal (since := "2025-04-22")]
lemma preservesShape_fin_of_preserves_binary_and_terminal (n : ‚Ñï) :
    PreservesLimitsOfShape (Discrete (Fin n)) F :=
  have : PreservesFiniteProducts F := .of_preserves_binary_and_terminal _; inferInstance

end Preserves

/-- Given `n+1` objects of `C`, a cofan for the last `n` with point `c‚ÇÅ.pt`
and a binary cofan on `c‚ÇÅ.X` and `f 0`, we can build a cofan for all `n+1`.

In `extendCofanIsColimit` we show that if the two given cofans are colimits,
then this cofan is also a colimit.
-/
@[simps!]
def extendCofan {n : ‚Ñï} {f : Fin (n + 1) ‚Üí C} (c‚ÇÅ : Cofan fun i : Fin n => f i.succ)
    (c‚ÇÇ : BinaryCofan (f 0) c‚ÇÅ.pt) : Cofan f :=
  Cofan.mk c‚ÇÇ.pt
    (by
      refine Fin.cases ?_ ?_
      ¬∑ apply c‚ÇÇ.inl
      ¬∑ intro i
        apply c‚ÇÅ.Œπ.app ‚ü®i‚ü© ‚â´ c‚ÇÇ.inr)

/-- Show that if the two given cofans in `extendCofan` are colimits,
then the constructed cofan is also a colimit.
-/
def extendCofanIsColimit {n : ‚Ñï} (f : Fin (n + 1) ‚Üí C) {c‚ÇÅ : Cofan fun i : Fin n => f i.succ}
    {c‚ÇÇ : BinaryCofan (f 0) c‚ÇÅ.pt} (t‚ÇÅ : IsColimit c‚ÇÅ) (t‚ÇÇ : IsColimit c‚ÇÇ) :
    IsColimit (extendCofan c‚ÇÅ c‚ÇÇ) where
  desc s := by
    apply (BinaryCofan.IsColimit.desc' t‚ÇÇ (s.Œπ.app ‚ü®0‚ü©) _).1
    apply t‚ÇÅ.desc ‚ü®_, Discrete.natTrans fun i => s.Œπ.app ‚ü®i.as.succ‚ü©‚ü©
  fac s := by
    rintro ‚ü®j‚ü©
    refine Fin.inductionOn j ?_ ?_
    ¬∑ apply (BinaryCofan.IsColimit.desc' t‚ÇÇ _ _).2.1
    ¬∑ rintro i -
      dsimp only [extendCofan_Œπ_app]
      rw [Fin.cases_succ, assoc, (BinaryCofan.IsColimit.desc' t‚ÇÇ _ _).2.2, t‚ÇÅ.fac]
      rfl
  uniq s m w := by
    apply BinaryCofan.IsColimit.hom_ext t‚ÇÇ
    ¬∑ rw [(BinaryCofan.IsColimit.desc' t‚ÇÇ _ _).2.1]
      apply w ‚ü®0‚ü©
    ¬∑ rw [(BinaryCofan.IsColimit.desc' t‚ÇÇ _ _).2.2]
      apply t‚ÇÅ.uniq ‚ü®_, _‚ü©
      rintro ‚ü®j‚ü©
      dsimp only [Discrete.natTrans_app]
      rw [‚Üê w ‚ü®j.succ‚ü©]
      dsimp only [extendCofan_Œπ_app]
      rw [Fin.cases_succ, assoc]

section

variable [HasBinaryCoproducts C] [HasInitial C]

/--
If `C` has an initial object and binary coproducts, then it has a coproduct for objects indexed by
`Fin n`.
This is a helper lemma for `hasCofiniteProductsOfHasBinaryAndTerminal`, which is more general
than this.
-/
private theorem hasCoproduct_fin : ‚àÄ (n : ‚Ñï) (f : Fin n ‚Üí C), HasCoproduct f
  | 0 => fun _ =>
    letI : HasColimitsOfShape (Discrete (Fin 0)) C :=
      hasColimitsOfShape_of_equivalence (Discrete.equivalence.{0} finZeroEquiv'.symm)
    inferInstance
  | n + 1 => fun f =>
    haveI := hasCoproduct_fin n
    HasColimit.mk ‚ü®_, extendCofanIsColimit f (colimit.isColimit _) (colimit.isColimit _)‚ü©

/-- If `C` has an initial object and binary coproducts, then it has finite coproducts. -/
theorem hasFiniteCoproducts_of_has_binary_and_initial : HasFiniteCoproducts C :=
  ‚ü®fun n => ‚ü®fun K => by
    let that : K ‚âÖ Discrete.functor fun n => K.obj ‚ü®n‚ü© := Discrete.natIso fun ‚ü®_‚ü© => Iso.refl _
    rw [hasColimit_iff_of_iso that]
    apply hasCoproduct_fin‚ü©‚ü©

end

section Preserves

variable (F : C ‚•§ D)
variable [PreservesColimitsOfShape (Discrete WalkingPair) F]
variable [PreservesColimitsOfShape (Discrete.{0} PEmpty) F]
variable [HasFiniteCoproducts.{v} C]

/-- If `F` preserves the initial object and binary coproducts, then it preserves products indexed by
`Fin n` for any `n`.
-/
lemma preserves_fin_of_preserves_binary_and_initial :
    ‚àÄ (n : ‚Ñï) (f : Fin n ‚Üí C), PreservesColimit (Discrete.functor f) F
  | 0 => fun f => by
    letI : PreservesColimitsOfShape (Discrete (Fin 0)) F :=
      preservesColimitsOfShape_of_equiv.{0, 0} (Discrete.equivalence finZeroEquiv'.symm) _
    infer_instance
  | n + 1 => by
    haveI := preserves_fin_of_preserves_binary_and_initial n
    intro f
    apply
      preservesColimit_of_preserves_colimit_cocone
        (extendCofanIsColimit f (colimit.isColimit _) (colimit.isColimit _)) _
    apply (isColimitMapCoconeCofanMkEquiv _ _ _).symm _
    let this :=
      extendCofanIsColimit (fun i => F.obj (f i))
        (isColimitOfHasCoproductOfPreservesColimit F _)
        (isColimitOfHasBinaryCoproductOfPreservesColimit F _ _)
    refine IsColimit.ofIsoColimit this ?_
    apply Cocones.ext _ _
    ¬∑ apply Iso.refl _
    rintro ‚ü®j‚ü©
    refine Fin.inductionOn j ?_ ?_
    ¬∑ apply Category.comp_id
    ¬∑ rintro i _
      dsimp [extendCofan_Œπ_app, Iso.refl_hom, Cofan.mk_Œπ_app]
      rw [comp_id, ‚Üê F.map_comp]

/-- If `F` preserves the initial object and binary coproducts, then it preserves colimits of shape
`Discrete (Fin n)`.
-/
lemma preservesShape_fin_of_preserves_binary_and_initial (n : ‚Ñï) :
    PreservesColimitsOfShape (Discrete (Fin n)) F where
  preservesColimit {K} := by
    let that : (Discrete.functor fun n => K.obj ‚ü®n‚ü©) ‚âÖ K := Discrete.natIso fun ‚ü®i‚ü© => Iso.refl _
    haveI := preserves_fin_of_preserves_binary_and_initial F n fun n => K.obj ‚ü®n‚ü©
    apply preservesColimit_of_iso_diagram F that

/-- If `F` preserves the initial object and binary coproducts then it preserves finite products. -/
lemma preservesFiniteCoproductsOfPreservesBinaryAndInitial (J : Type*) [Finite J] :
    PreservesColimitsOfShape (Discrete J) F :=
  let ‚ü®n, ‚ü®e‚ü©‚ü© := Finite.exists_equiv_fin J
  have := preservesShape_fin_of_preserves_binary_and_initial F n
  preservesColimitsOfShape_of_equiv (Discrete.equivalence e).symm _

end Preserves

end CategoryTheory
