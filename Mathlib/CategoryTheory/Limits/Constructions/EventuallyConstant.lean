/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Filtered.Basic
import Mathlib.CategoryTheory.Limits.HasLimits

/-!
# Limits of eventually constant functors

If `F : J â¥¤ C` is a functor from a cofiltered category, and `j : J`,
we introduce a property `F.IsEventuallyConstantTo j` which says
that for any `f : i âŸ¶ j`, the induced morphism `F.map f` is an isomorphism.
Under this assumption, it is shown that `F` admits `F.obj j` as a limit
(`Functor.IsEventuallyConstantTo.isLimitCone`).

A typeclass `Cofiltered.IsEventuallyConstant` is also introduced, and
the dual results for filtered categories and colimits are also obtained.

-/

namespace CategoryTheory

open Category Limits

variable {J C : Type*} [Category J] [Category C] (F : J â¥¤ C)

namespace Functor

/-- A functor `F : J â¥¤ C` is eventually constant to `j : J` if
for any map `f : i âŸ¶ j`, the induced morphism `F.map f` is an isomorphism.
If `J` is cofiltered, this implies `F` has a limit. -/
def IsEventuallyConstantTo (j : J) : Prop :=
  âˆ€ â¦ƒi : Jâ¦„ (f : i âŸ¶ j), IsIso (F.map f)

/-- A functor `F : J â¥¤ C` is eventually constant from `i : J` if
for any map `f : i âŸ¶ j`, the induced morphism `F.map f` is an isomorphism.
If `J` is filtered, this implies `F` has a colimit. -/
def IsEventuallyConstantFrom (i : J) : Prop :=
  âˆ€ â¦ƒj : Jâ¦„ (f : i âŸ¶ j), IsIso (F.map f)

namespace IsEventuallyConstantTo

variable {F} {iâ‚€ : J} (h : F.IsEventuallyConstantTo iâ‚€)

include h

lemma isIso_map {i j : J} (Ï† : i âŸ¶ j) (Ï€ : j âŸ¶ iâ‚€) : IsIso (F.map Ï†) := by
  have := h Ï€
  have := h (Ï† â‰« Ï€)
  exact IsIso.of_isIso_fac_right (F.map_comp Ï† Ï€).symm

lemma precomp {j : J} (f : j âŸ¶ iâ‚€) : F.IsEventuallyConstantTo j :=
  fun _ Ï† â†¦ h.isIso_map Ï† f

section

variable {i j : J} (Ï† : i âŸ¶ j) (hÏ† : Nonempty (j âŸ¶ iâ‚€))

/-- The isomorphism `F.obj i â‰… F.obj j` induced by `Ï† : i âŸ¶ j`,
when `h : F.IsEventuallyConstantTo iâ‚€` and there exists a map `j âŸ¶ iâ‚€`. -/
@[simps! hom]
noncomputable def isoMap : F.obj i â‰… F.obj j :=
  have := h.isIso_map Ï† hÏ†.some
  asIso (F.map Ï†)

@[reassoc (attr := simp)]
lemma isoMap_hom_inv_id : F.map Ï† â‰« (h.isoMap Ï† hÏ†).inv = ğŸ™ _ :=
  (h.isoMap Ï† hÏ†).hom_inv_id

@[reassoc (attr := simp)]
lemma isoMap_inv_hom_id : (h.isoMap Ï† hÏ†).inv â‰« F.map Ï† = ğŸ™ _ :=
  (h.isoMap Ï† hÏ†).inv_hom_id

end

variable [IsCofiltered J]
open IsCofiltered

/-- Auxiliary definition for `IsEventuallyConstantTo.cone`. -/
noncomputable def coneÏ€App (j : J) : F.obj iâ‚€ âŸ¶ F.obj j :=
  (h.isoMap (minToLeft iâ‚€ j) âŸ¨ğŸ™ _âŸ©).inv â‰« F.map (minToRight iâ‚€ j)

lemma coneÏ€App_eq (j j' : J) (Î± : j' âŸ¶ iâ‚€) (Î² : j' âŸ¶ j) :
    h.coneÏ€App j = (h.isoMap Î± âŸ¨ğŸ™ _âŸ©).inv â‰« F.map Î² := by
  obtain âŸ¨s, Î³, Î´, hâ‚, hâ‚‚âŸ© := IsCofiltered.bowtie
    (IsCofiltered.minToRight iâ‚€ j) Î² (IsCofiltered.minToLeft iâ‚€ j) Î±
  dsimp [coneÏ€App]
  rw [â† cancel_epi ((h.isoMap Î± âŸ¨ğŸ™ _âŸ©).hom), isoMap_hom, isoMap_hom_inv_id_assoc,
    â† cancel_epi (h.isoMap Î´ âŸ¨Î±âŸ©).hom, isoMap_hom,
    â† F.map_comp Î´ Î², â† hâ‚, F.map_comp, â† F.map_comp_assoc, â† hâ‚‚, F.map_comp_assoc,
    isoMap_hom_inv_id_assoc]

@[simp]
lemma coneÏ€App_eq_id : h.coneÏ€App iâ‚€ = ğŸ™ _ := by
  rw [h.coneÏ€App_eq iâ‚€ iâ‚€ (ğŸ™ _) (ğŸ™ _), h.isoMap_inv_hom_id]

/-- Given `h : F.IsEventuallyConstantTo iâ‚€`, this is the (limit) cone for `F` whose
point is `F.obj iâ‚€`. -/
@[simps]
noncomputable def cone : Cone F where
  pt := F.obj iâ‚€
  Ï€ :=
    { app := h.coneÏ€App
      naturality := fun j j' Ï† â†¦ by
        dsimp
        rw [id_comp]
        let i := IsCofiltered.min iâ‚€ j
        let Î± : i âŸ¶ iâ‚€ := IsCofiltered.minToLeft _ _
        let Î² : i âŸ¶ j := IsCofiltered.minToRight _ _
        rw [h.coneÏ€App_eq j _ Î± Î², assoc, h.coneÏ€App_eq j' _ Î± (Î² â‰« Ï†), map_comp] }

/-- When `h : F.IsEventuallyConstantTo iâ‚€`, the limit of `F` exists and is `F.obj iâ‚€`. -/
noncomputable def isLimitCone : IsLimit h.cone where
  lift s := s.Ï€.app iâ‚€
  fac s j := by
    dsimp [coneÏ€App]
    rw [â† s.w (IsCofiltered.minToLeft iâ‚€ j), â† s.w (IsCofiltered.minToRight iâ‚€ j), assoc,
      isoMap_hom_inv_id_assoc]
  uniq s m hm := by simp only [â† hm iâ‚€, cone_Ï€_app, coneÏ€App_eq_id, cone_pt, comp_id]

lemma hasLimit : HasLimit F := âŸ¨_, h.isLimitConeâŸ©

lemma isIso_Ï€_of_isLimit {c : Cone F} (hc : IsLimit c) :
    IsIso (c.Ï€.app iâ‚€) := by
  simp only [â† IsLimit.conePointUniqueUpToIso_hom_comp hc h.isLimitCone iâ‚€,
    cone_Ï€_app, coneÏ€App_eq_id, cone_pt, comp_id]
  infer_instance

/-- More general version of `isIso_Ï€_of_isLimit`. -/
lemma isIso_Ï€_of_isLimit' {c : Cone F} (hc : IsLimit c) (j : J) (Ï€ : j âŸ¶ iâ‚€) :
    IsIso (c.Ï€.app j) :=
  (h.precomp Ï€).isIso_Ï€_of_isLimit hc

end IsEventuallyConstantTo

namespace IsEventuallyConstantFrom

variable {F} {iâ‚€ : J} (h : F.IsEventuallyConstantFrom iâ‚€)

include h

lemma isIso_map {i j : J} (Ï† : i âŸ¶ j) (Î¹ : iâ‚€ âŸ¶ i) : IsIso (F.map Ï†) := by
  have := h Î¹
  have := h (Î¹ â‰« Ï†)
  exact IsIso.of_isIso_fac_left (F.map_comp Î¹ Ï†).symm

lemma postcomp {j : J} (f : iâ‚€ âŸ¶ j) : F.IsEventuallyConstantFrom j :=
  fun _ Ï† â†¦ h.isIso_map Ï† f

section

variable {i j : J} (Ï† : i âŸ¶ j) (hÏ† : Nonempty (iâ‚€ âŸ¶ i))

/-- The isomorphism `F.obj i â‰… F.obj j` induced by `Ï† : i âŸ¶ j`,
when `h : F.IsEventuallyConstantFrom iâ‚€` and there exists a map `iâ‚€ âŸ¶ i`. -/
@[simps! hom]
noncomputable def isoMap : F.obj i â‰… F.obj j :=
  have := h.isIso_map Ï† hÏ†.some
  asIso (F.map Ï†)

@[reassoc (attr := simp)]
lemma isoMap_hom_inv_id : F.map Ï† â‰« (h.isoMap Ï† hÏ†).inv = ğŸ™ _ :=
  (h.isoMap Ï† hÏ†).hom_inv_id

@[reassoc (attr := simp)]
lemma isoMap_inv_hom_id : (h.isoMap Ï† hÏ†).inv â‰« F.map Ï† = ğŸ™ _ :=
  (h.isoMap Ï† hÏ†).inv_hom_id

end

variable [IsFiltered J]
open IsFiltered

/-- Auxiliary definition for `IsEventuallyConstantFrom.cocone`. -/
noncomputable def coconeÎ¹App (j : J) : F.obj j âŸ¶ F.obj iâ‚€ :=
  F.map (rightToMax iâ‚€ j) â‰« (h.isoMap (leftToMax iâ‚€ j) âŸ¨ğŸ™ _âŸ©).inv

lemma coconeÎ¹App_eq (j j' : J) (Î± : j âŸ¶ j') (Î² : iâ‚€ âŸ¶ j') :
    h.coconeÎ¹App j = F.map Î± â‰« (h.isoMap Î² âŸ¨ğŸ™ _âŸ©).inv  := by
  obtain âŸ¨s, Î³, Î´, hâ‚, hâ‚‚âŸ© := IsFiltered.bowtie
    (IsFiltered.leftToMax iâ‚€ j) Î² (IsFiltered.rightToMax iâ‚€ j) Î±
  dsimp [coconeÎ¹App]
  rw [â† cancel_mono ((h.isoMap Î² âŸ¨ğŸ™ _âŸ©).hom), assoc, assoc, isoMap_hom, isoMap_inv_hom_id,
    comp_id, â† cancel_mono (h.isoMap Î´ âŸ¨Î²âŸ©).hom, isoMap_hom, assoc, assoc, â† F.map_comp Î± Î´,
    â† hâ‚‚, F.map_comp, â† F.map_comp Î² Î´, â† hâ‚, F.map_comp, isoMap_inv_hom_id_assoc]

@[simp]
lemma coconeÎ¹App_eq_id : h.coconeÎ¹App iâ‚€ = ğŸ™ _ := by
  rw [h.coconeÎ¹App_eq iâ‚€ iâ‚€ (ğŸ™ _) (ğŸ™ _), h.isoMap_hom_inv_id]

/-- Given `h : F.IsEventuallyConstantFrom iâ‚€`, this is the (limit) cocone for `F` whose
point is `F.obj iâ‚€`. -/
@[simps]
noncomputable def cocone : Cocone F where
  pt := F.obj iâ‚€
  Î¹ :=
    { app := h.coconeÎ¹App
      naturality := fun j j' Ï† â†¦ by
        dsimp
        rw [comp_id]
        let i := IsFiltered.max iâ‚€ j'
        let Î± : iâ‚€ âŸ¶ i := IsFiltered.leftToMax _ _
        let Î² : j' âŸ¶ i := IsFiltered.rightToMax _ _
        rw [h.coconeÎ¹App_eq j' _ Î² Î±, h.coconeÎ¹App_eq j _ (Ï† â‰« Î²) Î±, map_comp, assoc] }

/-- When `h : F.IsEventuallyConstantFrom iâ‚€`, the colimit of `F` exists and is `F.obj iâ‚€`. -/
noncomputable def isColimitCocone : IsColimit h.cocone where
  desc s := s.Î¹.app iâ‚€
  fac s j := by
    dsimp [coconeÎ¹App]
    rw [â† s.w (IsFiltered.rightToMax iâ‚€ j), â† s.w (IsFiltered.leftToMax iâ‚€ j), assoc,
      isoMap_inv_hom_id_assoc]
  uniq s m hm := by simp only [â† hm iâ‚€, cocone_Î¹_app, coconeÎ¹App_eq_id, id_comp]

lemma hasColimit : HasColimit F := âŸ¨_, h.isColimitCoconeâŸ©

lemma isIso_Î¹_of_isColimit {c : Cocone F} (hc : IsColimit c) :
    IsIso (c.Î¹.app iâ‚€) := by
  simp only [â† IsColimit.comp_coconePointUniqueUpToIso_inv hc h.isColimitCocone iâ‚€,
    cocone_Î¹_app, coconeÎ¹App_eq_id, id_comp]
  infer_instance

/-- More general version of `isIso_Î¹_of_isColimit`. -/
lemma isIso_Î¹_of_isColimit' {c : Cocone F} (hc : IsColimit c) (j : J) (Î¹ : iâ‚€ âŸ¶ j) :
    IsIso (c.Î¹.app j) :=
  (h.postcomp Î¹).isIso_Î¹_of_isColimit hc

end IsEventuallyConstantFrom

end Functor

namespace IsCofiltered

/-- A functor `F : J â¥¤ C` from a cofiltered category is eventually constant if there
exists `j : J`, such that for any `f : i âŸ¶ j`, the induced map `F.map f` is an isomorphism. -/
class IsEventuallyConstant : Prop where
  exists_isEventuallyConstantTo : âˆƒ (j : J), F.IsEventuallyConstantTo j

instance [hF : IsEventuallyConstant F] [IsCofiltered J] : HasLimit F := by
  obtain âŸ¨j, hâŸ© := hF.exists_isEventuallyConstantTo
  exact h.hasLimit

end IsCofiltered

namespace IsFiltered

/-- A functor `F : J â¥¤ C` from a filtered category is eventually constant if there
exists `i : J`, such that for any `f : i âŸ¶ j`, the induced map `F.map f` is an isomorphism. -/
class IsEventuallyConstant : Prop where
  exists_isEventuallyConstantFrom : âˆƒ (i : J), F.IsEventuallyConstantFrom i

instance [hF : IsEventuallyConstant F] [IsFiltered J] : HasColimit F := by
  obtain âŸ¨j, hâŸ© := hF.exists_isEventuallyConstantFrom
  exact h.hasColimit

end IsFiltered

end CategoryTheory
