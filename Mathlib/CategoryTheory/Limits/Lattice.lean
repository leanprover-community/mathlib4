/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Justus Springer
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.Shapes.FiniteLimits
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.Data.Finset.Lattice.Fold

/-!
# Limits in lattice categories are given by infimums and supremums.
-/


universe w w' u

namespace CategoryTheory.Limits.CompleteLattice

section Semilattice

variable {Œ± : Type u} {J : Type w} [SmallCategory J] [FinCategory J]

/-- The limit cone over any functor from a finite diagram into a `SemilatticeInf` with `OrderTop`.
-/
@[simps]
def finiteLimitCone [SemilatticeInf Œ±] [OrderTop Œ±] (F : J ‚•§ Œ±) : LimitCone F where
  cone :=
    { pt := Finset.univ.inf F.obj
      œÄ := { app := fun _ ‚Ü¶ homOfLE (Finset.inf_le (Fintype.complete _)) } }
  isLimit := { lift := fun s ‚Ü¶ homOfLE (Finset.le_inf fun j _ ‚Ü¶ (s.œÄ.app j).down.down) }

/--
The colimit cocone over any functor from a finite diagram into a `SemilatticeSup` with `OrderBot`.
-/
@[simps]
def finiteColimitCocone [SemilatticeSup Œ±] [OrderBot Œ±] (F : J ‚•§ Œ±) : ColimitCocone F where
  cocone :=
    { pt := Finset.univ.sup F.obj
      Œπ := { app := fun _ ‚Ü¶ homOfLE (Finset.le_sup (Fintype.complete _)) } }
  isColimit := { desc := fun s ‚Ü¶ homOfLE (Finset.sup_le fun j _ ‚Ü¶ (s.Œπ.app j).down.down) }

-- see Note [lower instance priority]
instance (priority := 100) hasFiniteLimits_of_semilatticeInf_orderTop [SemilatticeInf Œ±]
    [OrderTop Œ±] : HasFiniteLimits Œ± := ‚ü®by
  intro J ùí•‚ÇÅ ùí•‚ÇÇ
  exact { has_limit := fun F ‚Ü¶ HasLimit.mk (finiteLimitCone F) }‚ü©

-- see Note [lower instance priority]
instance (priority := 100) hasFiniteColimits_of_semilatticeSup_orderBot [SemilatticeSup Œ±]
    [OrderBot Œ±] : HasFiniteColimits Œ± := ‚ü®by
  intro J ùí•‚ÇÅ ùí•‚ÇÇ
  exact { has_colimit := fun F ‚Ü¶ HasColimit.mk (finiteColimitCocone F) }‚ü©

/-- The limit of a functor from a finite diagram into a `SemilatticeInf` with `OrderTop` is the
infimum of the objects in the image.
-/
theorem finite_limit_eq_finset_univ_inf [SemilatticeInf Œ±] [OrderTop Œ±] (F : J ‚•§ Œ±) :
    limit F = Finset.univ.inf F.obj :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit F) (finiteLimitCone F).isLimit).to_eq

/-- The colimit of a functor from a finite diagram into a `SemilatticeSup` with `OrderBot`
is the supremum of the objects in the image.
-/
theorem finite_colimit_eq_finset_univ_sup [SemilatticeSup Œ±] [OrderBot Œ±] (F : J ‚•§ Œ±) :
    colimit F = Finset.univ.sup F.obj :=
  (IsColimit.coconePointUniqueUpToIso (colimit.isColimit F) (finiteColimitCocone F).isColimit).to_eq

/--
A finite product in the category of a `SemilatticeInf` with `OrderTop` is the same as the infimum.
-/
theorem finite_product_eq_finset_inf [SemilatticeInf Œ±] [OrderTop Œ±] {Œπ : Type u} [Fintype Œπ]
    (f : Œπ ‚Üí Œ±) : ‚àè·∂ú f = Fintype.elems.inf f := by
  trans
  ¬∑ exact
      (IsLimit.conePointUniqueUpToIso (limit.isLimit _)
          (finiteLimitCone (Discrete.functor f)).isLimit).to_eq
  change Finset.univ.inf (f ‚àò discreteEquiv.toEmbedding) = Fintype.elems.inf f
  simp only [‚Üê Finset.inf_map, Finset.univ_map_equiv_to_embedding]
  rfl

/-- A finite coproduct in the category of a `SemilatticeSup` with `OrderBot` is the same as the
supremum.
-/
theorem finite_coproduct_eq_finset_sup [SemilatticeSup Œ±] [OrderBot Œ±] {Œπ : Type u} [Fintype Œπ]
    (f : Œπ ‚Üí Œ±) : ‚àê f = Fintype.elems.sup f := by
  trans
  ¬∑ exact
      (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
          (finiteColimitCocone (Discrete.functor f)).isColimit).to_eq
  change Finset.univ.sup (f ‚àò discreteEquiv.toEmbedding) = Fintype.elems.sup f
  simp only [‚Üê Finset.sup_map, Finset.univ_map_equiv_to_embedding]
  rfl

-- see Note [lower instance priority]
instance (priority := 100) [SemilatticeInf Œ±] [OrderTop Œ±] : HasBinaryProducts Œ± := by
  have : ‚àÄ x y : Œ±, HasLimit (pair x y) := by
    letI := hasFiniteLimits_of_hasFiniteLimits_of_size.{u} Œ±
    infer_instance
  apply hasBinaryProducts_of_hasLimit_pair

/-- The binary product in the category of a `SemilatticeInf` with `OrderTop` is the same as the
infimum.
-/
@[simp]
theorem prod_eq_inf [SemilatticeInf Œ±] [OrderTop Œ±] (x y : Œ±) : Limits.prod x y = x ‚äì y :=
  calc
    Limits.prod x y = limit (pair x y) := rfl
    _ = Finset.univ.inf (pair x y).obj := by rw [finite_limit_eq_finset_univ_inf (pair.{u} x y)]
    _ = x ‚äì (y ‚äì ‚ä§) := rfl
    -- Note: finset.inf is realized as a fold, hence the definitional equality
    _ = x ‚äì y := by rw [inf_top_eq]

-- see Note [lower instance priority]
instance (priority := 100) [SemilatticeSup Œ±] [OrderBot Œ±] : HasBinaryCoproducts Œ± := by
  have : ‚àÄ x y : Œ±, HasColimit (pair x y) := by
    letI := hasFiniteColimits_of_hasFiniteColimits_of_size.{u} Œ±
    infer_instance
  apply hasBinaryCoproducts_of_hasColimit_pair

/-- The binary coproduct in the category of a `SemilatticeSup` with `OrderBot` is the same as the
supremum.
-/
@[simp]
theorem coprod_eq_sup [SemilatticeSup Œ±] [OrderBot Œ±] (x y : Œ±) : Limits.coprod x y = x ‚äî y :=
  calc
    Limits.coprod x y = colimit (pair x y) := rfl
    _ = Finset.univ.sup (pair x y).obj := by rw [finite_colimit_eq_finset_univ_sup (pair x y)]
    _ = x ‚äî (y ‚äî ‚ä•) := rfl
    -- Note: Finset.sup is realized as a fold, hence the definitional equality
    _ = x ‚äî y := by rw [sup_bot_eq]

/-- The pullback in the category of a `SemilatticeInf` with `OrderTop` is the same as the infimum
over the objects.
-/
@[simp]
theorem pullback_eq_inf [SemilatticeInf Œ±] [OrderTop Œ±] {x y z : Œ±} (f : x ‚ü∂ z) (g : y ‚ü∂ z) :
    pullback f g = x ‚äì y :=
  calc
    pullback f g = limit (cospan f g) := rfl
    _ = Finset.univ.inf (cospan f g).obj := by rw [finite_limit_eq_finset_univ_inf]
    _ = z ‚äì (x ‚äì (y ‚äì ‚ä§)) := rfl
    _ = z ‚äì (x ‚äì y) := by rw [inf_top_eq]
    _ = x ‚äì y := inf_eq_right.mpr (inf_le_of_left_le f.le)

/-- The pushout in the category of a `SemilatticeSup` with `OrderBot` is the same as the supremum
over the objects.
-/
@[simp]
theorem pushout_eq_sup [SemilatticeSup Œ±] [OrderBot Œ±] (x y z : Œ±) (f : z ‚ü∂ x) (g : z ‚ü∂ y) :
    pushout f g = x ‚äî y :=
  calc
    pushout f g = colimit (span f g) := rfl
    _ = Finset.univ.sup (span f g).obj := by rw [finite_colimit_eq_finset_univ_sup]
    _ = z ‚äî (x ‚äî (y ‚äî ‚ä•)) := rfl
    _ = z ‚äî (x ‚äî y) := by rw [sup_bot_eq]
    _ = x ‚äî y := sup_eq_right.mpr (le_sup_of_le_left f.le)

end Semilattice

variable {Œ± : Type u} [CompleteLattice Œ±] {J : Type w} [Category.{w'} J]

/-- The limit cone over any functor into a complete lattice.
-/
@[simps]
def limitCone (F : J ‚•§ Œ±) : LimitCone F where
  cone :=
    { pt := iInf F.obj
      œÄ := { app := fun _ ‚Ü¶ homOfLE (CompleteLattice.sInf_le _ _ (Set.mem_range_self _)) } }
  isLimit :=
    { lift := fun s ‚Ü¶
        homOfLE (CompleteLattice.le_sInf _ _ (by rintro _ ‚ü®j, rfl‚ü©; exact (s.œÄ.app j).le)) }

/-- The colimit cocone over any functor into a complete lattice.
-/
@[simps]
def colimitCocone (F : J ‚•§ Œ±) : ColimitCocone F where
  cocone :=
    { pt := iSup F.obj
      Œπ := { app := fun _ ‚Ü¶ homOfLE (CompleteLattice.le_sSup _ _ (Set.mem_range_self _)) } }
  isColimit :=
    { desc := fun s ‚Ü¶
        homOfLE (CompleteLattice.sSup_le _ _ (by rintro _ ‚ü®j, rfl‚ü©; exact (s.Œπ.app j).le)) }

-- see Note [lower instance priority]
instance (priority := 100) hasLimits_of_completeLattice : HasLimitsOfSize.{w, w'} Œ± where
  has_limits_of_shape _ := { has_limit := fun F ‚Ü¶ HasLimit.mk (limitCone F) }

-- see Note [lower instance priority]
instance (priority := 100) hasColimits_of_completeLattice : HasColimitsOfSize.{w, w'} Œ± where
  has_colimits_of_shape _ := { has_colimit := fun F ‚Ü¶ HasColimit.mk (colimitCocone F) }

/-- The limit of a functor into a complete lattice is the infimum of the objects in the image.
-/
theorem limit_eq_iInf (F : J ‚•§ Œ±) : limit F = iInf F.obj :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit F) (limitCone F).isLimit).to_eq

/-- The colimit of a functor into a complete lattice is the supremum of the objects in the image.
-/
theorem colimit_eq_iSup (F : J ‚•§ Œ±) : colimit F = iSup F.obj :=
  (IsColimit.coconePointUniqueUpToIso (colimit.isColimit F) (colimitCocone F).isColimit).to_eq

end CategoryTheory.Limits.CompleteLattice
