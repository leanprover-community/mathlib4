/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Justus Springer
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.Limits.Shapes.FiniteLimits
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.Data.Finset.Lattice.Fold

/-!
# Limits in lattice categories are given by infimums and supremums.
-/


universe w w' u

namespace CategoryTheory.Limits.CompleteLattice

section Semilattice

variable {Î± : Type u} {J : Type w} [SmallCategory J] [FinCategory J]

/-- The limit cone over any functor from a finite diagram into a `SemilatticeInf` with `OrderTop`.
-/
@[simps]
def finiteLimitCone [SemilatticeInf Î±] [OrderTop Î±] (F : J â¥¤ Î±) : LimitCone F where
  cone :=
    { pt := Finset.univ.inf F.obj
      Ï€ := { app := fun _ => homOfLE (Finset.inf_le (Fintype.complete _)) } }
  isLimit := { lift := fun s => homOfLE (Finset.le_inf fun j _ => (s.Ï€.app j).down.down) }

/--
The colimit cocone over any functor from a finite diagram into a `SemilatticeSup` with `OrderBot`.
-/
@[simps]
def finiteColimitCocone [SemilatticeSup Î±] [OrderBot Î±] (F : J â¥¤ Î±) : ColimitCocone F where
  cocone :=
    { pt := Finset.univ.sup F.obj
      Î¹ := { app := fun _ => homOfLE (Finset.le_sup (Fintype.complete _)) } }
  isColimit := { desc := fun s => homOfLE (Finset.sup_le fun j _ => (s.Î¹.app j).down.down) }

-- see Note [lower instance priority]
instance (priority := 100) hasFiniteLimits_of_semilatticeInf_orderTop [SemilatticeInf Î±]
    [OrderTop Î±] : HasFiniteLimits Î± := âŸ¨by
  intro J ð’¥â‚ ð’¥â‚‚
  exact { has_limit := fun F => HasLimit.mk (finiteLimitCone F) }âŸ©

-- see Note [lower instance priority]
instance (priority := 100) hasFiniteColimits_of_semilatticeSup_orderBot [SemilatticeSup Î±]
    [OrderBot Î±] : HasFiniteColimits Î± := âŸ¨by
  intro J ð’¥â‚ ð’¥â‚‚
  exact { has_colimit := fun F => HasColimit.mk (finiteColimitCocone F) }âŸ©

/-- The limit of a functor from a finite diagram into a `SemilatticeInf` with `OrderTop` is the
infimum of the objects in the image.
-/
theorem finite_limit_eq_finset_univ_inf [SemilatticeInf Î±] [OrderTop Î±] (F : J â¥¤ Î±) :
    limit F = Finset.univ.inf F.obj :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit F) (finiteLimitCone F).isLimit).to_eq

/-- The colimit of a functor from a finite diagram into a `SemilatticeSup` with `OrderBot`
is the supremum of the objects in the image.
-/
theorem finite_colimit_eq_finset_univ_sup [SemilatticeSup Î±] [OrderBot Î±] (F : J â¥¤ Î±) :
    colimit F = Finset.univ.sup F.obj :=
  (IsColimit.coconePointUniqueUpToIso (colimit.isColimit F) (finiteColimitCocone F).isColimit).to_eq

/--
A finite product in the category of a `SemilatticeInf` with `OrderTop` is the same as the infimum.
-/
theorem finite_product_eq_finset_inf [SemilatticeInf Î±] [OrderTop Î±] {Î¹ : Type u} [Fintype Î¹]
    (f : Î¹ â†’ Î±) : âˆá¶œ f = Fintype.elems.inf f := by
  trans
  Â· exact
      (IsLimit.conePointUniqueUpToIso (limit.isLimit _)
          (finiteLimitCone (Discrete.functor f)).isLimit).to_eq
  change Finset.univ.inf (f âˆ˜ discreteEquiv.toEmbedding) = Fintype.elems.inf f
  simp only [â† Finset.inf_map, Finset.univ_map_equiv_to_embedding]
  rfl

/-- A finite coproduct in the category of a `SemilatticeSup` with `OrderBot` is the same as the
supremum.
-/
theorem finite_coproduct_eq_finset_sup [SemilatticeSup Î±] [OrderBot Î±] {Î¹ : Type u} [Fintype Î¹]
    (f : Î¹ â†’ Î±) : âˆ f = Fintype.elems.sup f := by
  trans
  Â· exact
      (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _)
          (finiteColimitCocone (Discrete.functor f)).isColimit).to_eq
  change Finset.univ.sup (f âˆ˜ discreteEquiv.toEmbedding) = Fintype.elems.sup f
  simp only [â† Finset.sup_map, Finset.univ_map_equiv_to_embedding]
  rfl

-- see Note [lower instance priority]
instance (priority := 100) [SemilatticeInf Î±] [OrderTop Î±] : HasBinaryProducts Î± := by
  have : âˆ€ x y : Î±, HasLimit (pair x y) := by
    letI := hasFiniteLimits_of_hasFiniteLimits_of_size.{u} Î±
    infer_instance
  apply hasBinaryProducts_of_hasLimit_pair

/-- The binary product in the category of a `SemilatticeInf` with `OrderTop` is the same as the
infimum.
-/
@[simp]
theorem prod_eq_inf [SemilatticeInf Î±] [OrderTop Î±] (x y : Î±) : Limits.prod x y = x âŠ“ y :=
  calc
    Limits.prod x y = limit (pair x y) := rfl
    _ = Finset.univ.inf (pair x y).obj := by rw [finite_limit_eq_finset_univ_inf (pair.{u} x y)]
    _ = x âŠ“ (y âŠ“ âŠ¤) := rfl
    -- Note: finset.inf is realized as a fold, hence the definitional equality
    _ = x âŠ“ y := by rw [inf_top_eq]

-- see Note [lower instance priority]
instance (priority := 100) [SemilatticeSup Î±] [OrderBot Î±] : HasBinaryCoproducts Î± := by
  have : âˆ€ x y : Î±, HasColimit (pair x y) := by
    letI := hasFiniteColimits_of_hasFiniteColimits_of_size.{u} Î±
    infer_instance
  apply hasBinaryCoproducts_of_hasColimit_pair

/-- The binary coproduct in the category of a `SemilatticeSup` with `OrderBot` is the same as the
supremum.
-/
@[simp]
theorem coprod_eq_sup [SemilatticeSup Î±] [OrderBot Î±] (x y : Î±) : Limits.coprod x y = x âŠ” y :=
  calc
    Limits.coprod x y = colimit (pair x y) := rfl
    _ = Finset.univ.sup (pair x y).obj := by rw [finite_colimit_eq_finset_univ_sup (pair x y)]
    _ = x âŠ” (y âŠ” âŠ¥) := rfl
    -- Note: Finset.sup is realized as a fold, hence the definitional equality
    _ = x âŠ” y := by rw [sup_bot_eq]

/-- The pullback in the category of a `SemilatticeInf` with `OrderTop` is the same as the infimum
over the objects.
-/
@[simp]
theorem pullback_eq_inf [SemilatticeInf Î±] [OrderTop Î±] {x y z : Î±} (f : x âŸ¶ z) (g : y âŸ¶ z) :
    pullback f g = x âŠ“ y :=
  calc
    pullback f g = limit (cospan f g) := rfl
    _ = Finset.univ.inf (cospan f g).obj := by rw [finite_limit_eq_finset_univ_inf]
    _ = z âŠ“ (x âŠ“ (y âŠ“ âŠ¤)) := rfl
    _ = z âŠ“ (x âŠ“ y) := by rw [inf_top_eq]
    _ = x âŠ“ y := inf_eq_right.mpr (inf_le_of_left_le f.le)

/-- The pushout in the category of a `SemilatticeSup` with `OrderBot` is the same as the supremum
over the objects.
-/
@[simp]
theorem pushout_eq_sup [SemilatticeSup Î±] [OrderBot Î±] (x y z : Î±) (f : z âŸ¶ x) (g : z âŸ¶ y) :
    pushout f g = x âŠ” y :=
  calc
    pushout f g = colimit (span f g) := rfl
    _ = Finset.univ.sup (span f g).obj := by rw [finite_colimit_eq_finset_univ_sup]
    _ = z âŠ” (x âŠ” (y âŠ” âŠ¥)) := rfl
    _ = z âŠ” (x âŠ” y) := by rw [sup_bot_eq]
    _ = x âŠ” y := sup_eq_right.mpr (le_sup_of_le_left f.le)

end Semilattice

variable {Î± : Type u} [CompleteLattice Î±] {J : Type w} [Category.{w'} J]

/-- The limit cone over any functor into a complete lattice.
-/
@[simps]
def limitCone (F : J â¥¤ Î±) : LimitCone F where
  cone :=
    { pt := iInf F.obj
      Ï€ := { app := fun _ => homOfLE (CompleteLattice.sInf_le _ _ (Set.mem_range_self _)) } }
  isLimit :=
    { lift := fun s =>
        homOfLE (CompleteLattice.le_sInf _ _ (by rintro _ âŸ¨j, rflâŸ©; exact (s.Ï€.app j).le)) }

/-- The colimit cocone over any functor into a complete lattice.
-/
@[simps]
def colimitCocone (F : J â¥¤ Î±) : ColimitCocone F where
  cocone :=
    { pt := iSup F.obj
      Î¹ := { app := fun _ => homOfLE (CompleteLattice.le_sSup _ _ (Set.mem_range_self _)) } }
  isColimit :=
    { desc := fun s =>
        homOfLE (CompleteLattice.sSup_le _ _ (by rintro _ âŸ¨j, rflâŸ©; exact (s.Î¹.app j).le)) }

-- see Note [lower instance priority]
instance (priority := 100) hasLimits_of_completeLattice : HasLimitsOfSize.{w, w'} Î± where
  has_limits_of_shape _ := { has_limit := fun F => HasLimit.mk (limitCone F) }

-- see Note [lower instance priority]
instance (priority := 100) hasColimits_of_completeLattice : HasColimitsOfSize.{w, w'} Î± where
  has_colimits_of_shape _ := { has_colimit := fun F => HasColimit.mk (colimitCocone F) }

/-- The limit of a functor into a complete lattice is the infimum of the objects in the image.
-/
theorem limit_eq_iInf (F : J â¥¤ Î±) : limit F = iInf F.obj :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit F) (limitCone F).isLimit).to_eq

/-- The colimit of a functor into a complete lattice is the supremum of the objects in the image.
-/
theorem colimit_eq_iSup (F : J â¥¤ Î±) : colimit F = iSup F.obj :=
  (IsColimit.coconePointUniqueUpToIso (colimit.isColimit F) (colimitCocone F).isColimit).to_eq

end CategoryTheory.Limits.CompleteLattice
