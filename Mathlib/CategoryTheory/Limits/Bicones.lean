/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.Limits.Cones
import Mathlib.CategoryTheory.FinCategory.Basic

/-!
# Bicones

Given a category `J`, a walking `Bicone J` is a category whose objects are the objects of `J` and
two extra vertices `Bicone.left` and `Bicone.right`. The morphisms are the morphisms of `J` and
`left âŸ¶ j`, `right âŸ¶ j` for each `j : J` such that `(Â· âŸ¶ j)` and `(Â· âŸ¶ k)` commutes with each
`f : j âŸ¶ k`.

Given a diagram `F : J â¥¤ C` and two `Cone F`s, we can join them into a diagram `Bicone J â¥¤ C` via
`biconeMk`.

This is used in `CategoryTheory.Functor.Flat`.
-/


universe vâ‚ uâ‚

noncomputable section

open CategoryTheory.Limits

namespace CategoryTheory

section Bicone

/-- Given a category `J`, construct a walking `Bicone J` by adjoining two elements. -/
inductive Bicone (J : Type uâ‚)
  | left : Bicone J
  | right : Bicone J
  | diagram (val : J) : Bicone J
  deriving DecidableEq

variable (J : Type uâ‚)

instance : Inhabited (Bicone J) :=
  âŸ¨Bicone.leftâŸ©

open scoped Classical in
instance finBicone [Fintype J] : Fintype (Bicone J) where
  elems := [Bicone.left, Bicone.right].toFinset âˆª Finset.image Bicone.diagram Fintype.elems
  complete j := by
    cases j <;> simp [Fintype.complete]

variable [Category.{vâ‚} J]

/-- The homs for a walking `Bicone J`. -/
inductive BiconeHom : Bicone J â†’ Bicone J â†’ Type max uâ‚ vâ‚
  | left_id : BiconeHom Bicone.left Bicone.left
  | right_id : BiconeHom Bicone.right Bicone.right
  | left (j : J) : BiconeHom Bicone.left (Bicone.diagram j)
  | right (j : J) : BiconeHom Bicone.right (Bicone.diagram j)
  | diagram {j k : J} (f : j âŸ¶ k) : BiconeHom (Bicone.diagram j) (Bicone.diagram k)

instance : Inhabited (BiconeHom J Bicone.left Bicone.left) :=
  âŸ¨BiconeHom.left_idâŸ©

instance BiconeHom.decidableEq {j k : Bicone J} : DecidableEq (BiconeHom J j k) := fun f g => by
  classical cases f <;> cases g <;> simp only [diagram.injEq] <;> infer_instance

@[simps]
instance biconeCategoryStruct : CategoryStruct (Bicone J) where
  Hom := BiconeHom J
  id j := Bicone.casesOn j BiconeHom.left_id BiconeHom.right_id fun k => BiconeHom.diagram (ğŸ™ k)
  comp f g := by
    rcases f with (_ | _ | _ | _ | f)
    Â· exact g
    Â· exact g
    Â· cases g
      apply BiconeHom.left
    Â· cases g
      apply BiconeHom.right
    Â· rcases g with (_ | _ | _ | _ | g)
      exact BiconeHom.diagram (f â‰« g)

instance biconeCategory : Category (Bicone J) where
  id_comp f := by cases f <;> simp
  comp_id f := by cases f <;> simp
  assoc f g h := by cases f <;> cases g <;> cases h <;> simp

end Bicone

section SmallCategory

variable (J : Type vâ‚) [SmallCategory J]

/-- Given a diagram `F : J â¥¤ C` and two `Cone F`s, we can join them into a diagram `Bicone J â¥¤ C`.
-/
@[simps]
def biconeMk {C : Type uâ‚} [Category.{vâ‚} C] {F : J â¥¤ C} (câ‚ câ‚‚ : Cone F) : Bicone J â¥¤ C where
  obj X := Bicone.casesOn X câ‚.pt câ‚‚.pt fun j => F.obj j
  map f := by
    rcases f with (_ | _ | _ | _ | f)
    Â· exact ğŸ™ _
    Â· exact ğŸ™ _
    Â· exact câ‚.Ï€.app _
    Â· exact câ‚‚.Ï€.app _
    Â· exact F.map f
  map_id X := by cases X <;> simp
  map_comp f g := by
    rcases f with (_ | _ | _ | _ | _)
    Â· exact (Category.id_comp _).symm
    Â· exact (Category.id_comp _).symm
    Â· cases g
      exact (Category.id_comp _).symm.trans (câ‚.Ï€.naturality _)
    Â· cases g
      exact (Category.id_comp _).symm.trans (câ‚‚.Ï€.naturality _)
    Â· cases g
      apply F.map_comp

open scoped Classical in
instance finBiconeHom [FinCategory J] (j k : Bicone J) : Fintype (j âŸ¶ k) := by
  cases j <;> cases k
  Â· exact
      { elems := {BiconeHom.left_id}
        complete := fun f => by cases f; simp }
  Â· exact
    { elems := âˆ…
      complete := fun f => by cases f }
  Â· exact
    { elems := {BiconeHom.left _}
      complete := fun f => by cases f; simp }
  Â· exact
    { elems := âˆ…
      complete := fun f => by cases f }
  Â· exact
      { elems := {BiconeHom.right_id}
        complete := fun f => by cases f; simp }
  Â· exact
    { elems := {BiconeHom.right _}
      complete := fun f => by cases f; simp }
  Â· exact
    { elems := âˆ…
      complete := fun f => by cases f }
  Â· exact
    { elems := âˆ…
      complete := fun f => by cases f }
  Â· exact
    { elems := Finset.image BiconeHom.diagram Fintype.elems
      complete := fun f => by
        rcases f with (_ | _ | _ | _ | f)
        simp only [Finset.mem_image]
        use f
        simpa using Fintype.complete _ }

instance biconeSmallCategory : SmallCategory (Bicone J) :=
  CategoryTheory.biconeCategory J

instance biconeFinCategory [FinCategory J] : FinCategory (Bicone J) where

end SmallCategory

end CategoryTheory
