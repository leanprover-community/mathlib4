/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Preserves.Basic

#align_import category_theory.limits.creates from "leanprover-community/mathlib"@"fe5e4ce6c72d96d77ad40ac832a6e7f8040990bc"

/-!
# Creating (co)limits

We say that `F` creates limits of `K` if, given any limit cone `c` for `K ‚ãô F`
(i.e. below) we can lift it to a cone "above", and further that `F` reflects
limits for `K`.
-/


open CategoryTheory CategoryTheory.Limits

noncomputable section

namespace CategoryTheory

universe w' w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

section Creates

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {J : Type w} [Category.{w'} J] {K : J ‚•§ C}

/-- Define the lift of a cone: For a cone `c` for `K ‚ãô F`, give a cone for `K`
which is a lift of `c`, i.e. the image of it under `F` is (iso) to `c`.

We will then use this as part of the definition of creation of limits:
every limit cone has a lift.

Note this definition is really only useful when `c` is a limit already.
-/
structure LiftableCone (K : J ‚•§ C) (F : C ‚•§ D) (c : Cone (K ‚ãô F)) where
  /-- a cone in the source category of the functor -/
  liftedCone : Cone K
  /-- the isomorphism expressing that `liftedCone` lifts the given cone -/
  validLift : F.mapCone liftedCone ‚âÖ c
#align category_theory.liftable_cone CategoryTheory.LiftableCone

/-- Define the lift of a cocone: For a cocone `c` for `K ‚ãô F`, give a cocone for
`K` which is a lift of `c`, i.e. the image of it under `F` is (iso) to `c`.

We will then use this as part of the definition of creation of colimits:
every limit cocone has a lift.

Note this definition is really only useful when `c` is a colimit already.
-/
structure LiftableCocone (K : J ‚•§ C) (F : C ‚•§ D) (c : Cocone (K ‚ãô F)) where
  /-- a cocone in the source category of the functor -/
  liftedCocone : Cocone K
  /-- the isomorphism expressing that `liftedCocone` lifts the given cocone -/
  validLift : F.mapCocone liftedCocone ‚âÖ c
#align category_theory.liftable_cocone CategoryTheory.LiftableCocone

/-- Definition 3.3.1 of [Riehl].
We say that `F` creates limits of `K` if, given any limit cone `c` for `K ‚ãô F`
(i.e. below) we can lift it to a cone "above", and further that `F` reflects
limits for `K`.

If `F` reflects isomorphisms, it suffices to show only that the lifted cone is
a limit - see `createsLimitOfReflectsIso`.
-/
class CreatesLimit (K : J ‚•§ C) (F : C ‚•§ D) extends ReflectsLimit K F where
  /-- any limit cone can be lifted to a cone above -/
  lifts : ‚àÄ c, IsLimit c ‚Üí LiftableCone K F c
#align category_theory.creates_limit CategoryTheory.CreatesLimit

/-- `F` creates limits of shape `J` if `F` creates the limit of any diagram
`K : J ‚•§ C`.
-/
class CreatesLimitsOfShape (J : Type w) [Category.{w'} J] (F : C ‚•§ D) where
  CreatesLimit : ‚àÄ {K : J ‚•§ C}, CreatesLimit K F := by infer_instance
#align category_theory.creates_limits_of_shape CategoryTheory.CreatesLimitsOfShape

-- This should be used with explicit universe variables.
/-- `F` creates limits if it creates limits of shape `J` for any `J`. -/
@[nolint checkUnivs, pp_with_univ]
class CreatesLimitsOfSize (F : C ‚•§ D) where
  CreatesLimitsOfShape : ‚àÄ {J : Type w} [Category.{w'} J], CreatesLimitsOfShape J F := by
    infer_instance
#align category_theory.creates_limits_of_size CategoryTheory.CreatesLimitsOfSize

/-- `F` creates small limits if it creates limits of shape `J` for any small `J`. -/
abbrev CreatesLimits (F : C ‚•§ D) :=
  CreatesLimitsOfSize.{v‚ÇÇ, v‚ÇÇ} F
#align category_theory.creates_limits CategoryTheory.CreatesLimits

/-- Dual of definition 3.3.1 of [Riehl].
We say that `F` creates colimits of `K` if, given any limit cocone `c` for
`K ‚ãô F` (i.e. below) we can lift it to a cocone "above", and further that `F`
reflects limits for `K`.

If `F` reflects isomorphisms, it suffices to show only that the lifted cocone is
a limit - see `createsColimitOfReflectsIso`.
-/
class CreatesColimit (K : J ‚•§ C) (F : C ‚•§ D) extends ReflectsColimit K F where
  /-- any limit cocone can be lifted to a cocone above -/
  lifts : ‚àÄ c, IsColimit c ‚Üí LiftableCocone K F c
#align category_theory.creates_colimit CategoryTheory.CreatesColimit

/-- `F` creates colimits of shape `J` if `F` creates the colimit of any diagram
`K : J ‚•§ C`.
-/
class CreatesColimitsOfShape (J : Type w) [Category.{w'} J] (F : C ‚•§ D) where
  CreatesColimit : ‚àÄ {K : J ‚•§ C}, CreatesColimit K F := by infer_instance
#align category_theory.creates_colimits_of_shape CategoryTheory.CreatesColimitsOfShape

-- This should be used with explicit universe variables.
/-- `F` creates colimits if it creates colimits of shape `J` for any small `J`. -/
@[nolint checkUnivs, pp_with_univ]
class CreatesColimitsOfSize (F : C ‚•§ D) where
  CreatesColimitsOfShape : ‚àÄ {J : Type w} [Category.{w'} J], CreatesColimitsOfShape J F := by
    infer_instance
#align category_theory.creates_colimits_of_size CategoryTheory.CreatesColimitsOfSize

/-- `F` creates small colimits if it creates colimits of shape `J` for any small `J`. -/
abbrev CreatesColimits (F : C ‚•§ D) :=
  CreatesColimitsOfSize.{v‚ÇÇ, v‚ÇÇ} F
#align category_theory.creates_colimits CategoryTheory.CreatesColimits

-- see Note [lower instance priority]
attribute [instance 100] CreatesLimitsOfShape.CreatesLimit CreatesLimitsOfSize.CreatesLimitsOfShape
  CreatesColimitsOfShape.CreatesColimit CreatesColimitsOfSize.CreatesColimitsOfShape

-- see Note [lower instance priority]
-- Interface to the `CreatesLimit` class.
/-- `liftLimit t` is the cone for `K` given by lifting the limit `t` for `K ‚ãô F`. -/
def liftLimit {K : J ‚•§ C} {F : C ‚•§ D} [CreatesLimit K F] {c : Cone (K ‚ãô F)} (t : IsLimit c) :
    Cone K :=
  (CreatesLimit.lifts c t).liftedCone
#align category_theory.lift_limit CategoryTheory.liftLimit

/-- The lifted cone has an image isomorphic to the original cone. -/
def liftedLimitMapsToOriginal {K : J ‚•§ C} {F : C ‚•§ D} [CreatesLimit K F] {c : Cone (K ‚ãô F)}
    (t : IsLimit c) : F.mapCone (liftLimit t) ‚âÖ c :=
  (CreatesLimit.lifts c t).validLift
#align category_theory.lifted_limit_maps_to_original CategoryTheory.liftedLimitMapsToOriginal

/-- The lifted cone is a limit. -/
def liftedLimitIsLimit {K : J ‚•§ C} {F : C ‚•§ D} [CreatesLimit K F] {c : Cone (K ‚ãô F)}
    (t : IsLimit c) : IsLimit (liftLimit t) :=
  ReflectsLimit.reflects (IsLimit.ofIsoLimit t (liftedLimitMapsToOriginal t).symm)
#align category_theory.lifted_limit_is_limit CategoryTheory.liftedLimitIsLimit

/-- If `F` creates the limit of `K` and `K ‚ãô F` has a limit, then `K` has a limit. -/
theorem hasLimit_of_created (K : J ‚•§ C) (F : C ‚•§ D) [HasLimit (K ‚ãô F)] [CreatesLimit K F] :
    HasLimit K :=
  HasLimit.mk
    { cone := liftLimit (limit.isLimit (K ‚ãô F))
      isLimit := liftedLimitIsLimit _ }
#align category_theory.has_limit_of_created CategoryTheory.hasLimit_of_created

/-- If `F` creates limits of shape `J`, and `D` has limits of shape `J`, then
`C` has limits of shape `J`.
-/
theorem hasLimitsOfShape_of_hasLimitsOfShape_createsLimitsOfShape (F : C ‚•§ D) [HasLimitsOfShape J D]
    [CreatesLimitsOfShape J F] : HasLimitsOfShape J C :=
  ‚ü®fun G => hasLimit_of_created G F‚ü©
#align category_theory.has_limits_of_shape_of_has_limits_of_shape_creates_limits_of_shape CategoryTheory.hasLimitsOfShape_of_hasLimitsOfShape_createsLimitsOfShape

/-- If `F` creates limits, and `D` has all limits, then `C` has all limits. -/
theorem hasLimits_of_hasLimits_createsLimits (F : C ‚•§ D) [HasLimitsOfSize.{w, w'} D]
    [CreatesLimitsOfSize.{w, w'} F] : HasLimitsOfSize.{w, w'} C :=
  ‚ü®fun _ _ => hasLimitsOfShape_of_hasLimitsOfShape_createsLimitsOfShape F‚ü©
#align category_theory.has_limits_of_has_limits_creates_limits CategoryTheory.hasLimits_of_hasLimits_createsLimits

-- Interface to the `CreatesColimit` class.
/-- `liftColimit t` is the cocone for `K` given by lifting the colimit `t` for `K ‚ãô F`. -/
def liftColimit {K : J ‚•§ C} {F : C ‚•§ D} [CreatesColimit K F] {c : Cocone (K ‚ãô F)}
    (t : IsColimit c) : Cocone K :=
  (CreatesColimit.lifts c t).liftedCocone
#align category_theory.lift_colimit CategoryTheory.liftColimit

/-- The lifted cocone has an image isomorphic to the original cocone. -/
def liftedColimitMapsToOriginal {K : J ‚•§ C} {F : C ‚•§ D} [CreatesColimit K F] {c : Cocone (K ‚ãô F)}
    (t : IsColimit c) : F.mapCocone (liftColimit t) ‚âÖ c :=
  (CreatesColimit.lifts c t).validLift
#align category_theory.lifted_colimit_maps_to_original CategoryTheory.liftedColimitMapsToOriginal

/-- The lifted cocone is a colimit. -/
def liftedColimitIsColimit {K : J ‚•§ C} {F : C ‚•§ D} [CreatesColimit K F] {c : Cocone (K ‚ãô F)}
    (t : IsColimit c) : IsColimit (liftColimit t) :=
  ReflectsColimit.reflects (IsColimit.ofIsoColimit t (liftedColimitMapsToOriginal t).symm)
#align category_theory.lifted_colimit_is_colimit CategoryTheory.liftedColimitIsColimit

/-- If `F` creates the limit of `K` and `K ‚ãô F` has a limit, then `K` has a limit. -/
theorem hasColimit_of_created (K : J ‚•§ C) (F : C ‚•§ D) [HasColimit (K ‚ãô F)] [CreatesColimit K F] :
    HasColimit K :=
  HasColimit.mk
    { cocone := liftColimit (colimit.isColimit (K ‚ãô F))
      isColimit := liftedColimitIsColimit _ }
#align category_theory.has_colimit_of_created CategoryTheory.hasColimit_of_created

/-- If `F` creates colimits of shape `J`, and `D` has colimits of shape `J`, then
`C` has colimits of shape `J`.
-/
theorem hasColimitsOfShape_of_hasColimitsOfShape_createsColimitsOfShape (F : C ‚•§ D)
    [HasColimitsOfShape J D] [CreatesColimitsOfShape J F] : HasColimitsOfShape J C :=
  ‚ü®fun G => hasColimit_of_created G F‚ü©
#align category_theory.has_colimits_of_shape_of_has_colimits_of_shape_creates_colimits_of_shape CategoryTheory.hasColimitsOfShape_of_hasColimitsOfShape_createsColimitsOfShape

/-- If `F` creates colimits, and `D` has all colimits, then `C` has all colimits. -/
theorem hasColimits_of_hasColimits_createsColimits (F : C ‚•§ D) [HasColimitsOfSize.{w, w'} D]
    [CreatesColimitsOfSize.{w, w'} F] : HasColimitsOfSize.{w, w'} C :=
  ‚ü®fun _ _ => hasColimitsOfShape_of_hasColimitsOfShape_createsColimitsOfShape F‚ü©
#align category_theory.has_colimits_of_has_colimits_creates_colimits CategoryTheory.hasColimits_of_hasColimits_createsColimits

instance (priority := 10) reflectsLimitsOfShapeOfCreatesLimitsOfShape (F : C ‚•§ D)
    [CreatesLimitsOfShape J F] : ReflectsLimitsOfShape J F where
#align category_theory.reflects_limits_of_shape_of_creates_limits_of_shape CategoryTheory.reflectsLimitsOfShapeOfCreatesLimitsOfShape

instance (priority := 10) reflectsLimitsOfCreatesLimits (F : C ‚•§ D)
    [CreatesLimitsOfSize.{w, w'} F] : ReflectsLimitsOfSize.{w, w'} F where
#align category_theory.reflects_limits_of_creates_limits CategoryTheory.reflectsLimitsOfCreatesLimits

instance (priority := 10) reflectsColimitsOfShapeOfCreatesColimitsOfShape (F : C ‚•§ D)
    [CreatesColimitsOfShape J F] : ReflectsColimitsOfShape J F where
#align category_theory.reflects_colimits_of_shape_of_creates_colimits_of_shape CategoryTheory.reflectsColimitsOfShapeOfCreatesColimitsOfShape

instance (priority := 10) reflectsColimitsOfCreatesColimits (F : C ‚•§ D)
    [CreatesColimitsOfSize.{w, w'} F] : ReflectsColimitsOfSize.{w, w'} F where
#align category_theory.reflects_colimits_of_creates_colimits CategoryTheory.reflectsColimitsOfCreatesColimits

/-- A helper to show a functor creates limits. In particular, if we can show
that for any limit cone `c` for `K ‚ãô F`, there is a lift of it which is
a limit and `F` reflects isomorphisms, then `F` creates limits.
Usually, `F` creating limits says that _any_ lift of `c` is a limit, but
here we only need to show that our particular lift of `c` is a limit.
-/
structure LiftsToLimit (K : J ‚•§ C) (F : C ‚•§ D) (c : Cone (K ‚ãô F)) (t : IsLimit c) extends
  LiftableCone K F c where
  /-- the lifted cone is limit -/
  makesLimit : IsLimit liftedCone
#align category_theory.lifts_to_limit CategoryTheory.LiftsToLimit

/-- A helper to show a functor creates colimits. In particular, if we can show
that for any limit cocone `c` for `K ‚ãô F`, there is a lift of it which is
a limit and `F` reflects isomorphisms, then `F` creates colimits.
Usually, `F` creating colimits says that _any_ lift of `c` is a colimit, but
here we only need to show that our particular lift of `c` is a colimit.
-/
structure LiftsToColimit (K : J ‚•§ C) (F : C ‚•§ D) (c : Cocone (K ‚ãô F)) (t : IsColimit c) extends
  LiftableCocone K F c where
  /-- the lifted cocone is colimit -/
  makesColimit : IsColimit liftedCocone
#align category_theory.lifts_to_colimit CategoryTheory.LiftsToColimit

/-- If `F` reflects isomorphisms and we can lift any limit cone to a limit cone,
then `F` creates limits.
In particular here we don't need to assume that F reflects limits.
-/
def createsLimitOfReflectsIso {K : J ‚•§ C} {F : C ‚•§ D} [F.ReflectsIsomorphisms]
    (h : ‚àÄ c t, LiftsToLimit K F c t) : CreatesLimit K F where
  lifts c t := (h c t).toLiftableCone
  toReflectsLimit :=
    { reflects := fun {d} hd => by
        let d' : Cone K := (h (F.mapCone d) hd).toLiftableCone.liftedCone
        let i : F.mapCone d' ‚âÖ F.mapCone d :=
          (h (F.mapCone d) hd).toLiftableCone.validLift
        let hd' : IsLimit d' := (h (F.mapCone d) hd).makesLimit
        let f : d ‚ü∂ d' := hd'.liftConeMorphism d
        have : (Cones.functoriality K F).map f = i.inv :=
          (hd.ofIsoLimit i.symm).uniq_cone_morphism
        haveI : IsIso ((Cones.functoriality K F).map f) := by
          rw [this]
          infer_instance
        haveI : IsIso f := isIso_of_reflects_iso f (Cones.functoriality K F)
        exact IsLimit.ofIsoLimit hd' (asIso f).symm }
#align category_theory.creates_limit_of_reflects_iso CategoryTheory.createsLimitOfReflectsIso

/-- If `F` reflects isomorphisms and we can lift a single limit cone to a limit cone, then `F`
    creates limits. Note that unlike `createsLimitOfReflectsIso`, to apply this result it is
    necessary to know that `K ‚ãô F` actually has a limit. -/
def createsLimitOfReflectsIso' {K : J ‚•§ C} {F : C ‚•§ D} [F.ReflectsIsomorphisms]
    {c : Cone (K ‚ãô F)} (hc : IsLimit c) (h : LiftsToLimit K F c hc) : CreatesLimit K F :=
  createsLimitOfReflectsIso fun _ t =>
    { liftedCone := h.liftedCone
      validLift := h.validLift ‚â™‚â´ IsLimit.uniqueUpToIso hc t
      makesLimit := h.makesLimit }

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cone maps,
-- so the constructed limits may not be ideal, definitionally.
/--
When `F` is fully faithful, to show that `F` creates the limit for `K` it suffices to exhibit a lift
of a limit cone for `K ‚ãô F`.
-/
def createsLimitOfFullyFaithfulOfLift' {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    {l : Cone (K ‚ãô F)} (hl : IsLimit l) (c : Cone K) (i : F.mapCone c ‚âÖ l) :
    CreatesLimit K F :=
  createsLimitOfReflectsIso fun _ t =>
    { liftedCone := c
      validLift := i ‚â™‚â´ IsLimit.uniqueUpToIso hl t
      makesLimit :=
        IsLimit.ofFaithful F (IsLimit.ofIsoLimit hl i.symm) _ fun _ => F.map_preimage _ }
#align category_theory.creates_limit_of_fully_faithful_of_lift' CategoryTheory.createsLimitOfFullyFaithfulOfLift'

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cone maps,
-- so the constructed limits may not be ideal, definitionally.
/-- When `F` is fully faithful, and `HasLimit (K ‚ãô F)`, to show that `F` creates the limit for `K`
it suffices to exhibit a lift of the chosen limit cone for `K ‚ãô F`.
-/
def createsLimitOfFullyFaithfulOfLift {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    [HasLimit (K ‚ãô F)] (c : Cone K) (i : F.mapCone c ‚âÖ limit.cone (K ‚ãô F)) :
    CreatesLimit K F :=
  createsLimitOfFullyFaithfulOfLift' (limit.isLimit _) c i
#align category_theory.creates_limit_of_fully_faithful_of_lift CategoryTheory.createsLimitOfFullyFaithfulOfLift

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cone maps,
-- so the constructed limits may not be ideal, definitionally.
/--
When `F` is fully faithful, to show that `F` creates the limit for `K` it suffices to show that a
limit point is in the essential image of `F`.
-/
def createsLimitOfFullyFaithfulOfIso' {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    {l : Cone (K ‚ãô F)} (hl : IsLimit l) (X : C) (i : F.obj X ‚âÖ l.pt) : CreatesLimit K F :=
  createsLimitOfFullyFaithfulOfLift' hl
    { pt := X
      œÄ :=
        { app := fun j => F.preimage (i.hom ‚â´ l.œÄ.app j)
          naturality := fun Y Z f =>
            F.map_injective <| by
              dsimp
              simpa using (l.w f).symm } }
    (Cones.ext i fun j => by simp only [Functor.map_preimage, Functor.mapCone_œÄ_app])
#align category_theory.creates_limit_of_fully_faithful_of_iso' CategoryTheory.createsLimitOfFullyFaithfulOfIso'

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cone maps,
-- so the constructed limits may not be ideal, definitionally.
/-- When `F` is fully faithful, and `HasLimit (K ‚ãô F)`, to show that `F` creates the limit for `K`
it suffices to show that the chosen limit point is in the essential image of `F`.
-/
def createsLimitOfFullyFaithfulOfIso {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    [HasLimit (K ‚ãô F)] (X : C) (i : F.obj X ‚âÖ limit (K ‚ãô F)) : CreatesLimit K F :=
  createsLimitOfFullyFaithfulOfIso' (limit.isLimit _) X i
#align category_theory.creates_limit_of_fully_faithful_of_iso CategoryTheory.createsLimitOfFullyFaithfulOfIso

/-- A fully faithful functor that preserves a limit that exists also creates the limit. -/
def createsLimitOfFullyFaithfulOfPreserves {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    [HasLimit K] [PreservesLimit K F] : CreatesLimit K F :=
  createsLimitOfFullyFaithfulOfLift' (PreservesLimit.preserves <| limit.isLimit K) _ (Iso.refl _)

-- see Note [lower instance priority]
/-- `F` preserves the limit of `K` if it creates the limit and `K ‚ãô F` has the limit. -/
instance (priority := 100) preservesLimitOfCreatesLimitAndHasLimit (K : J ‚•§ C) (F : C ‚•§ D)
    [CreatesLimit K F] [HasLimit (K ‚ãô F)] : PreservesLimit K F where
  preserves t := IsLimit.ofIsoLimit (limit.isLimit _)
    ((liftedLimitMapsToOriginal (limit.isLimit _)).symm ‚â™‚â´
      (Cones.functoriality K F).mapIso ((liftedLimitIsLimit (limit.isLimit _)).uniqueUpToIso t))
#align category_theory.preserves_limit_of_creates_limit_and_has_limit CategoryTheory.preservesLimitOfCreatesLimitAndHasLimit

-- see Note [lower instance priority]
/-- `F` preserves the limit of shape `J` if it creates these limits and `D` has them. -/
instance (priority := 100) preservesLimitOfShapeOfCreatesLimitsOfShapeAndHasLimitsOfShape
    (F : C ‚•§ D) [CreatesLimitsOfShape J F] [HasLimitsOfShape J D] : PreservesLimitsOfShape J F where
#align category_theory.preserves_limit_of_shape_of_creates_limits_of_shape_and_has_limits_of_shape CategoryTheory.preservesLimitOfShapeOfCreatesLimitsOfShapeAndHasLimitsOfShape

-- see Note [lower instance priority]
/-- `F` preserves limits if it creates limits and `D` has limits. -/
instance (priority := 100) preservesLimitsOfCreatesLimitsAndHasLimits (F : C ‚•§ D)
    [CreatesLimitsOfSize.{w, w'} F] [HasLimitsOfSize.{w, w'} D] :
    PreservesLimitsOfSize.{w, w'} F where
#align category_theory.preserves_limits_of_creates_limits_and_has_limits CategoryTheory.preservesLimitsOfCreatesLimitsAndHasLimits

/-- If `F` reflects isomorphisms and we can lift any colimit cocone to a colimit cocone,
then `F` creates colimits.
In particular here we don't need to assume that F reflects colimits.
-/
def createsColimitOfReflectsIso {K : J ‚•§ C} {F : C ‚•§ D} [F.ReflectsIsomorphisms]
    (h : ‚àÄ c t, LiftsToColimit K F c t) : CreatesColimit K F where
  lifts c t := (h c t).toLiftableCocone
  toReflectsColimit :=
    {
      reflects := fun {d} hd => by
        let d' : Cocone K := (h (F.mapCocone d) hd).toLiftableCocone.liftedCocone
        let i : F.mapCocone d' ‚âÖ F.mapCocone d :=
          (h (F.mapCocone d) hd).toLiftableCocone.validLift
        let hd' : IsColimit d' := (h (F.mapCocone d) hd).makesColimit
        let f : d' ‚ü∂ d := hd'.descCoconeMorphism d
        have : (Cocones.functoriality K F).map f = i.hom :=
          (hd.ofIsoColimit i.symm).uniq_cocone_morphism
        haveI : IsIso ((Cocones.functoriality K F).map f) := by
          rw [this]
          infer_instance
        haveI := isIso_of_reflects_iso f (Cocones.functoriality K F)
        exact IsColimit.ofIsoColimit hd' (asIso f) }
#align category_theory.creates_colimit_of_reflects_iso CategoryTheory.createsColimitOfReflectsIso

/-- If `F` reflects isomorphisms and we can lift a single colimit cocone to a colimit cocone, then
    `F` creates limits. Note that unlike `createsColimitOfReflectsIso`, to apply this result it is
    necessary to know that `K ‚ãô F` actually has a colimit. -/
def createsColimitOfReflectsIso' {K : J ‚•§ C} {F : C ‚•§ D} [F.ReflectsIsomorphisms]
    {c : Cocone (K ‚ãô F)} (hc : IsColimit c) (h : LiftsToColimit K F c hc) : CreatesColimit K F :=
  createsColimitOfReflectsIso fun _ t =>
    { liftedCocone := h.liftedCocone
      validLift := h.validLift ‚â™‚â´ IsColimit.uniqueUpToIso hc t
      makesColimit := h.makesColimit }

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cocone maps,
-- so the constructed colimits may not be ideal, definitionally.
/--
When `F` is fully faithful, to show that `F` creates the colimit for `K` it suffices to exhibit a
lift of a colimit cocone for `K ‚ãô F`.
-/
def createsColimitOfFullyFaithfulOfLift' {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    {l : Cocone (K ‚ãô F)} (hl : IsColimit l) (c : Cocone K) (i : F.mapCocone c ‚âÖ l) :
    CreatesColimit K F :=
  createsColimitOfReflectsIso fun _ t =>
    { liftedCocone := c
      validLift := i ‚â™‚â´ IsColimit.uniqueUpToIso hl t
      makesColimit :=
        IsColimit.ofFaithful F (IsColimit.ofIsoColimit hl i.symm) _ fun _ => F.map_preimage _ }
#align category_theory.creates_colimit_of_fully_faithful_of_lift' CategoryTheory.createsColimitOfFullyFaithfulOfLift'

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cocone maps,
-- so the constructed colimits may not be ideal, definitionally.
/--
When `F` is fully faithful, and `HasColimit (K ‚ãô F)`, to show that `F` creates the colimit for `K`
it suffices to exhibit a lift of the chosen colimit cocone for `K ‚ãô F`.
-/
def createsColimitOfFullyFaithfulOfLift {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    [HasColimit (K ‚ãô F)] (c : Cocone K) (i : F.mapCocone c ‚âÖ colimit.cocone (K ‚ãô F)) :
    CreatesColimit K F :=
  createsColimitOfFullyFaithfulOfLift' (colimit.isColimit _) c i
#align category_theory.creates_colimit_of_fully_faithful_of_lift CategoryTheory.createsColimitOfFullyFaithfulOfLift

/-- A fully faithful functor that preserves a colimit that exists also creates the colimit. -/
def createsColimitOfFullyFaithfulOfPreserves {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    [HasColimit K] [PreservesColimit K F] : CreatesColimit K F :=
  createsColimitOfFullyFaithfulOfLift' (PreservesColimit.preserves <| colimit.isColimit K) _
    (Iso.refl _)

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cocone maps,
-- so the constructed colimits may not be ideal, definitionally.
/--
When `F` is fully faithful, to show that `F` creates the colimit for `K` it suffices to show that
a colimit point is in the essential image of `F`.
-/
def createsColimitOfFullyFaithfulOfIso' {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    {l : Cocone (K ‚ãô F)} (hl : IsColimit l) (X : C) (i : F.obj X ‚âÖ l.pt) : CreatesColimit K F :=
  createsColimitOfFullyFaithfulOfLift' hl
    { pt := X
      Œπ :=
        { app := fun j => F.preimage (l.Œπ.app j ‚â´ i.inv)
          naturality := fun Y Z f =>
            F.map_injective <| by
              dsimp
              simpa [‚Üê cancel_mono i.hom] using l.w f } }
    (Cocones.ext i fun j => by simp)
#align category_theory.creates_colimit_of_fully_faithful_of_iso' CategoryTheory.createsColimitOfFullyFaithfulOfIso'

-- Notice however that even if the isomorphism is `Iso.refl _`,
-- this construction will insert additional identity morphisms in the cocone maps,
-- so the constructed colimits may not be ideal, definitionally.
/--
When `F` is fully faithful, and `HasColimit (K ‚ãô F)`, to show that `F` creates the colimit for `K`
it suffices to show that the chosen colimit point is in the essential image of `F`.
-/
def createsColimitOfFullyFaithfulOfIso {K : J ‚•§ C} {F : C ‚•§ D} [F.Full] [F.Faithful]
    [HasColimit (K ‚ãô F)] (X : C) (i : F.obj X ‚âÖ colimit (K ‚ãô F)) : CreatesColimit K F :=
  createsColimitOfFullyFaithfulOfIso' (colimit.isColimit _) X i
#align category_theory.creates_colimit_of_fully_faithful_of_iso CategoryTheory.createsColimitOfFullyFaithfulOfIso

-- see Note [lower instance priority]
/-- `F` preserves the colimit of `K` if it creates the colimit and `K ‚ãô F` has the colimit. -/
instance (priority := 100) preservesColimitOfCreatesColimitAndHasColimit (K : J ‚•§ C) (F : C ‚•§ D)
    [CreatesColimit K F] [HasColimit (K ‚ãô F)] : PreservesColimit K F where
  preserves t :=
    IsColimit.ofIsoColimit (colimit.isColimit _)
      ((liftedColimitMapsToOriginal (colimit.isColimit _)).symm ‚â™‚â´
        (Cocones.functoriality K F).mapIso
          ((liftedColimitIsColimit (colimit.isColimit _)).uniqueUpToIso t))
#align category_theory.preserves_colimit_of_creates_colimit_and_has_colimit CategoryTheory.preservesColimitOfCreatesColimitAndHasColimit

-- see Note [lower instance priority]
/-- `F` preserves the colimit of shape `J` if it creates these colimits and `D` has them. -/
instance (priority := 100) preservesColimitOfShapeOfCreatesColimitsOfShapeAndHasColimitsOfShape
    (F : C ‚•§ D) [CreatesColimitsOfShape J F] [HasColimitsOfShape J D] :
    PreservesColimitsOfShape J F where
#align category_theory.preserves_colimit_of_shape_of_creates_colimits_of_shape_and_has_colimits_of_shape CategoryTheory.preservesColimitOfShapeOfCreatesColimitsOfShapeAndHasColimitsOfShape

-- see Note [lower instance priority]
/-- `F` preserves limits if it creates limits and `D` has limits. -/
instance (priority := 100) preservesColimitsOfCreatesColimitsAndHasColimits (F : C ‚•§ D)
    [CreatesColimitsOfSize.{w, w'} F] [HasColimitsOfSize.{w, w'} D] :
    PreservesColimitsOfSize.{w, w'} F where
#align category_theory.preserves_colimits_of_creates_colimits_and_has_colimits CategoryTheory.preservesColimitsOfCreatesColimitsAndHasColimits

/-- Transfer creation of limits along a natural isomorphism in the diagram. -/
def createsLimitOfIsoDiagram {K‚ÇÅ K‚ÇÇ : J ‚•§ C} (F : C ‚•§ D) (h : K‚ÇÅ ‚âÖ K‚ÇÇ) [CreatesLimit K‚ÇÅ F] :
    CreatesLimit K‚ÇÇ F :=
  { reflectsLimitOfIsoDiagram F h with
    lifts := fun c t =>
      let t' := (IsLimit.postcomposeInvEquiv (isoWhiskerRight h F : _) c).symm t
      { liftedCone := (Cones.postcompose h.hom).obj (liftLimit t')
        validLift :=
          Functor.mapConePostcompose F ‚â™‚â´
            (Cones.postcompose (isoWhiskerRight h F).hom).mapIso (liftedLimitMapsToOriginal t') ‚â™‚â´
              Cones.ext (Iso.refl _) fun j => by
                dsimp
                rw [Category.assoc, ‚Üê F.map_comp]
                simp } }
#align category_theory.creates_limit_of_iso_diagram CategoryTheory.createsLimitOfIsoDiagram

/-- If `F` creates the limit of `K` and `F ‚âÖ G`, then `G` creates the limit of `K`. -/
def createsLimitOfNatIso {F G : C ‚•§ D} (h : F ‚âÖ G) [CreatesLimit K F] : CreatesLimit K G where
  lifts c t :=
    { liftedCone := liftLimit ((IsLimit.postcomposeInvEquiv (isoWhiskerLeft K h : _) c).symm t)
      validLift := by
        refine (IsLimit.mapConeEquiv h ?_).uniqueUpToIso t
        apply IsLimit.ofIsoLimit _ (liftedLimitMapsToOriginal _).symm
        apply (IsLimit.postcomposeInvEquiv _ _).symm t }
  toReflectsLimit := reflectsLimitOfNatIso _ h
#align category_theory.creates_limit_of_nat_iso CategoryTheory.createsLimitOfNatIso

/-- If `F` creates limits of shape `J` and `F ‚âÖ G`, then `G` creates limits of shape `J`. -/
def createsLimitsOfShapeOfNatIso {F G : C ‚•§ D} (h : F ‚âÖ G) [CreatesLimitsOfShape J F] :
    CreatesLimitsOfShape J G where CreatesLimit := createsLimitOfNatIso h
#align category_theory.creates_limits_of_shape_of_nat_iso CategoryTheory.createsLimitsOfShapeOfNatIso

/-- If `F` creates limits and `F ‚âÖ G`, then `G` creates limits. -/
def createsLimitsOfNatIso {F G : C ‚•§ D} (h : F ‚âÖ G) [CreatesLimitsOfSize.{w, w'} F] :
    CreatesLimitsOfSize.{w, w'} G where
  CreatesLimitsOfShape := createsLimitsOfShapeOfNatIso h
#align category_theory.creates_limits_of_nat_iso CategoryTheory.createsLimitsOfNatIso

/-- Transfer creation of colimits along a natural isomorphism in the diagram. -/
def createsColimitOfIsoDiagram {K‚ÇÅ K‚ÇÇ : J ‚•§ C} (F : C ‚•§ D) (h : K‚ÇÅ ‚âÖ K‚ÇÇ) [CreatesColimit K‚ÇÅ F] :
    CreatesColimit K‚ÇÇ F :=
  { reflectsColimitOfIsoDiagram F h with
    lifts := fun c t =>
      let t' := (IsColimit.precomposeHomEquiv (isoWhiskerRight h F : _) c).symm t
      { liftedCocone := (Cocones.precompose h.inv).obj (liftColimit t')
        validLift :=
          Functor.mapCoconePrecompose F ‚â™‚â´
            (Cocones.precompose (isoWhiskerRight h F).inv).mapIso
                (liftedColimitMapsToOriginal t') ‚â™‚â´
              Cocones.ext (Iso.refl _) fun j => by
                dsimp
                rw [‚Üê F.map_comp_assoc]
                simp } }
#align category_theory.creates_colimit_of_iso_diagram CategoryTheory.createsColimitOfIsoDiagram

/-- If `F` creates the colimit of `K` and `F ‚âÖ G`, then `G` creates the colimit of `K`. -/
def createsColimitOfNatIso {F G : C ‚•§ D} (h : F ‚âÖ G) [CreatesColimit K F] : CreatesColimit K G where
  lifts c t :=
    { liftedCocone := liftColimit ((IsColimit.precomposeHomEquiv (isoWhiskerLeft K h : _) c).symm t)
      validLift := by
        refine (IsColimit.mapCoconeEquiv h ?_).uniqueUpToIso t
        apply IsColimit.ofIsoColimit _ (liftedColimitMapsToOriginal _).symm
        apply (IsColimit.precomposeHomEquiv _ _).symm t }
  toReflectsColimit := reflectsColimitOfNatIso _ h
#align category_theory.creates_colimit_of_nat_iso CategoryTheory.createsColimitOfNatIso

/-- If `F` creates colimits of shape `J` and `F ‚âÖ G`, then `G` creates colimits of shape `J`. -/
def createsColimitsOfShapeOfNatIso {F G : C ‚•§ D} (h : F ‚âÖ G) [CreatesColimitsOfShape J F] :
    CreatesColimitsOfShape J G where CreatesColimit := createsColimitOfNatIso h
#align category_theory.creates_colimits_of_shape_of_nat_iso CategoryTheory.createsColimitsOfShapeOfNatIso

/-- If `F` creates colimits and `F ‚âÖ G`, then `G` creates colimits. -/
def createsColimitsOfNatIso {F G : C ‚•§ D} (h : F ‚âÖ G) [CreatesColimitsOfSize.{w, w'} F] :
    CreatesColimitsOfSize.{w, w'} G where
  CreatesColimitsOfShape := createsColimitsOfShapeOfNatIso h
#align category_theory.creates_colimits_of_nat_iso CategoryTheory.createsColimitsOfNatIso

-- For the inhabited linter later.
/-- If F creates the limit of K, any cone lifts to a limit. -/
def liftsToLimitOfCreates (K : J ‚•§ C) (F : C ‚•§ D) [CreatesLimit K F] (c : Cone (K ‚ãô F))
    (t : IsLimit c) : LiftsToLimit K F c t where
  liftedCone := liftLimit t
  validLift := liftedLimitMapsToOriginal t
  makesLimit := liftedLimitIsLimit t
#align category_theory.lifts_to_limit_of_creates CategoryTheory.liftsToLimitOfCreates

-- For the inhabited linter later.
/-- If F creates the colimit of K, any cocone lifts to a colimit. -/
def liftsToColimitOfCreates (K : J ‚•§ C) (F : C ‚•§ D) [CreatesColimit K F] (c : Cocone (K ‚ãô F))
    (t : IsColimit c) : LiftsToColimit K F c t where
  liftedCocone := liftColimit t
  validLift := liftedColimitMapsToOriginal t
  makesColimit := liftedColimitIsColimit t
#align category_theory.lifts_to_colimit_of_creates CategoryTheory.liftsToColimitOfCreates

/-- Any cone lifts through the identity functor. -/
def idLiftsCone (c : Cone (K ‚ãô ùü≠ C)) : LiftableCone K (ùü≠ C) c where
  liftedCone :=
    { pt := c.pt
      œÄ := c.œÄ ‚â´ K.rightUnitor.hom }
  validLift := Cones.ext (Iso.refl _)
#align category_theory.id_lifts_cone CategoryTheory.idLiftsCone

/-- The identity functor creates all limits. -/
instance idCreatesLimits : CreatesLimitsOfSize.{w, w'} (ùü≠ C) where
  CreatesLimitsOfShape :=
    { CreatesLimit := { lifts := fun c _ => idLiftsCone c } }
#align category_theory.id_creates_limits CategoryTheory.idCreatesLimits

/-- Any cocone lifts through the identity functor. -/
def idLiftsCocone (c : Cocone (K ‚ãô ùü≠ C)) : LiftableCocone K (ùü≠ C) c where
  liftedCocone :=
    { pt := c.pt
      Œπ := K.rightUnitor.inv ‚â´ c.Œπ }
  validLift := Cocones.ext (Iso.refl _)
#align category_theory.id_lifts_cocone CategoryTheory.idLiftsCocone

/-- The identity functor creates all colimits. -/
instance idCreatesColimits : CreatesColimitsOfSize.{w, w'} (ùü≠ C) where
  CreatesColimitsOfShape :=
    { CreatesColimit := { lifts := fun c _ => idLiftsCocone c } }
#align category_theory.id_creates_colimits CategoryTheory.idCreatesColimits

/-- Satisfy the inhabited linter -/
instance inhabitedLiftableCone (c : Cone (K ‚ãô ùü≠ C)) : Inhabited (LiftableCone K (ùü≠ C) c) :=
  ‚ü®idLiftsCone c‚ü©
#align category_theory.inhabited_liftable_cone CategoryTheory.inhabitedLiftableCone

instance inhabitedLiftableCocone (c : Cocone (K ‚ãô ùü≠ C)) : Inhabited (LiftableCocone K (ùü≠ C) c) :=
  ‚ü®idLiftsCocone c‚ü©
#align category_theory.inhabited_liftable_cocone CategoryTheory.inhabitedLiftableCocone

/-- Satisfy the inhabited linter -/
instance inhabitedLiftsToLimit (K : J ‚•§ C) (F : C ‚•§ D) [CreatesLimit K F] (c : Cone (K ‚ãô F))
    (t : IsLimit c) : Inhabited (LiftsToLimit _ _ _ t) :=
  ‚ü®liftsToLimitOfCreates K F c t‚ü©
#align category_theory.inhabited_lifts_to_limit CategoryTheory.inhabitedLiftsToLimit

instance inhabitedLiftsToColimit (K : J ‚•§ C) (F : C ‚•§ D) [CreatesColimit K F] (c : Cocone (K ‚ãô F))
    (t : IsColimit c) : Inhabited (LiftsToColimit _ _ _ t) :=
  ‚ü®liftsToColimitOfCreates K F c t‚ü©
#align category_theory.inhabited_lifts_to_colimit CategoryTheory.inhabitedLiftsToColimit

section Comp

variable {E : Type u‚ÇÉ} [‚Ñ∞ : Category.{v‚ÇÉ} E]
variable (F : C ‚•§ D) (G : D ‚•§ E)

instance compCreatesLimit [CreatesLimit K F] [CreatesLimit (K ‚ãô F) G] :
    CreatesLimit K (F ‚ãô G) where
  lifts c t := by
    let c' : Cone ((K ‚ãô F) ‚ãô G) := c
    let t' : IsLimit c' := t
    exact
      { liftedCone := liftLimit (liftedLimitIsLimit t')
        validLift := (Cones.functoriality (K ‚ãô F) G).mapIso
            (liftedLimitMapsToOriginal (liftedLimitIsLimit t')) ‚â™‚â´
          liftedLimitMapsToOriginal t' }
#align category_theory.comp_creates_limit CategoryTheory.compCreatesLimit

instance compCreatesLimitsOfShape [CreatesLimitsOfShape J F] [CreatesLimitsOfShape J G] :
    CreatesLimitsOfShape J (F ‚ãô G) where CreatesLimit := inferInstance
#align category_theory.comp_creates_limits_of_shape CategoryTheory.compCreatesLimitsOfShape

instance compCreatesLimits [CreatesLimitsOfSize.{w, w'} F] [CreatesLimitsOfSize.{w, w'} G] :
    CreatesLimitsOfSize.{w, w'} (F ‚ãô G) where CreatesLimitsOfShape := inferInstance
#align category_theory.comp_creates_limits CategoryTheory.compCreatesLimits

instance compCreatesColimit [CreatesColimit K F] [CreatesColimit (K ‚ãô F) G] :
    CreatesColimit K (F ‚ãô G) where
  lifts c t :=
    let c' : Cocone ((K ‚ãô F) ‚ãô G) := c
    let t' : IsColimit c' := t
    { liftedCocone := liftColimit (liftedColimitIsColimit t')
      validLift :=
        (Cocones.functoriality (K ‚ãô F) G).mapIso
            (liftedColimitMapsToOriginal (liftedColimitIsColimit t')) ‚â™‚â´
          liftedColimitMapsToOriginal t' }
#align category_theory.comp_creates_colimit CategoryTheory.compCreatesColimit

instance compCreatesColimitsOfShape [CreatesColimitsOfShape J F] [CreatesColimitsOfShape J G] :
    CreatesColimitsOfShape J (F ‚ãô G) where CreatesColimit := inferInstance
#align category_theory.comp_creates_colimits_of_shape CategoryTheory.compCreatesColimitsOfShape

instance compCreatesColimits [CreatesColimitsOfSize.{w, w'} F] [CreatesColimitsOfSize.{w, w'} G] :
    CreatesColimitsOfSize.{w, w'} (F ‚ãô G) where CreatesColimitsOfShape := inferInstance
#align category_theory.comp_creates_colimits CategoryTheory.compCreatesColimits

end Comp

end Creates

end CategoryTheory
