/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Limits.Shapes.Kernels
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Zero

#align_import category_theory.limits.preserves.shapes.kernels from "leanprover-community/mathlib"@"956af7c76589f444f2e1313911bad16366ea476d"

/-!
# Preserving (co)kernels

Constructions to relate the notions of preserving (co)kernels and reflecting (co)kernels
to concrete (co)forks.

In particular, we show that `kernel_comparison f g G` is an isomorphism iff `G` preserves
the limit of the parallel pair `f,0`, as well as the dual result.
-/


noncomputable section

universe vâ‚ vâ‚‚ uâ‚ uâ‚‚

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

variable {C : Type uâ‚} [Category.{vâ‚} C] [HasZeroMorphisms C]

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [HasZeroMorphisms D]

namespace CategoryTheory.Limits

namespace KernelFork

variable {X Y : C} {f : X âŸ¶ Y} (c : KernelFork f)
  (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G]

@[reassoc (attr := simp)]
lemma map_condition : G.map c.Î¹ â‰« G.map f = 0 := by
  rw [â† G.map_comp, c.condition, G.map_zero]
  -- ğŸ‰ no goals

/-- A kernel fork for `f` is mapped to a kernel fork for `G.map f` if `G` is a functor
which preserves zero morphisms. -/
def map : KernelFork (G.map f) :=
  KernelFork.ofÎ¹ (G.map c.Î¹) (c.map_condition G)

@[simp]
lemma map_Î¹ : (c.map G).Î¹ = G.map c.Î¹ := rfl

/-- The underlying cone of a kernel fork is mapped to a limit cone if and only if
the mapped kernel fork is limit. -/
def isLimitMapConeEquiv :
    IsLimit (G.mapCone c) â‰ƒ IsLimit (c.map G) := by
  refine' (IsLimit.postcomposeHomEquiv _ _).symm.trans (IsLimit.equivIsoLimit _)
  -- âŠ¢ parallelPair f 0 â‹™ G â‰… parallelPair (G.map f) 0
  refine' parallelPair.ext (Iso.refl _) (Iso.refl _) _ _ <;> simp
  -- âŠ¢ (parallelPair f 0 â‹™ G).map WalkingParallelPairHom.left â‰« (Iso.refl ((paralle â€¦
                                                             -- ğŸ‰ no goals
                                                             -- ğŸ‰ no goals
  exact Cones.ext (Iso.refl _) (by rintro (_|_) <;> aesop_cat)
  -- ğŸ‰ no goals

/-- A limit kernel fork is mapped to a limit kernel fork by a functor `G` when this functor
preserves the corresponding limit. -/
def mapIsLimit (hc : IsLimit c) (G : C â¥¤ D)
    [Functor.PreservesZeroMorphisms G] [PreservesLimit (parallelPair f 0) G] :
    IsLimit (c.map G) :=
  c.isLimitMapConeEquiv G (isLimitOfPreserves G hc)

end KernelFork

section Kernels

variable (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G]
  {X Y Z : C} {f : X âŸ¶ Y} {h : Z âŸ¶ X} (w : h â‰« f = 0)

/-- The map of a kernel fork is a limit iff
the kernel fork consisting of the mapped morphisms is a limit.
This essentially lets us commute `KernelFork.ofÎ¹` with `Functor.mapCone`.

This is a variant of `isLimitMapConeForkEquiv` for equalizers,
which we can't use directly between `G.map 0 = 0` does not hold definitionally.
-/
def isLimitMapConeForkEquiv' :
    IsLimit (G.mapCone (KernelFork.ofÎ¹ h w)) â‰ƒ
      IsLimit
        (KernelFork.ofÎ¹ (G.map h) (by simp only [â† G.map_comp, w, Functor.map_zero]) :
                                      -- ğŸ‰ no goals
          Fork (G.map f) 0) :=
  KernelFork.isLimitMapConeEquiv _ _
#align category_theory.limits.is_limit_map_cone_fork_equiv' CategoryTheory.Limits.isLimitMapConeForkEquiv'

/-- The property of preserving kernels expressed in terms of kernel forks.

This is a variant of `isLimitForkMapOfIsLimit` for equalizers,
which we can't use directly between `G.map 0 = 0` does not hold definitionally.
-/
def isLimitForkMapOfIsLimit' [PreservesLimit (parallelPair f 0) G]
    (l : IsLimit (KernelFork.ofÎ¹ h w)) :
    IsLimit
      (KernelFork.ofÎ¹ (G.map h) (by simp only [â† G.map_comp, w, Functor.map_zero]) :
                                    -- ğŸ‰ no goals
        Fork (G.map f) 0) :=
  isLimitMapConeForkEquiv' G w (PreservesLimit.preserves l)
#align category_theory.limits.is_limit_fork_map_of_is_limit' CategoryTheory.Limits.isLimitForkMapOfIsLimit'

variable (f) [HasKernel f]

/-- If `G` preserves kernels and `C` has them, then the fork constructed of the mapped morphisms of
a kernel fork is a limit.
-/
def isLimitOfHasKernelOfPreservesLimit [PreservesLimit (parallelPair f 0) G] :
    IsLimit
      (Fork.ofÎ¹ (G.map (kernel.Î¹ f))
          (by simp only [â† G.map_comp, kernel.condition, comp_zero, Functor.map_zero]) :
              -- ğŸ‰ no goals
        Fork (G.map f) 0) :=
  isLimitForkMapOfIsLimit' G (kernel.condition f) (kernelIsKernel f)
#align category_theory.limits.is_limit_of_has_kernel_of_preserves_limit CategoryTheory.Limits.isLimitOfHasKernelOfPreservesLimit

instance [PreservesLimit (parallelPair f 0) G] : HasKernel (G.map f)
    where exists_limit := âŸ¨âŸ¨_, isLimitOfHasKernelOfPreservesLimit G fâŸ©âŸ©

variable [HasKernel (G.map f)]

/-- If the kernel comparison map for `G` at `f` is an isomorphism, then `G` preserves the
kernel of `f`.
-/
def PreservesKernel.ofIsoComparison [i : IsIso (kernelComparison f G)] :
    PreservesLimit (parallelPair f 0) G := by
  apply preservesLimitOfPreservesLimitCone (kernelIsKernel f)
  -- âŠ¢ IsLimit (G.mapCone (Fork.ofÎ¹ (kernel.Î¹ f) (_ : kernel.Î¹ f â‰« f = kernel.Î¹ f â‰« â€¦
  apply (isLimitMapConeForkEquiv' G (kernel.condition f)).symm _
  -- âŠ¢ IsLimit (KernelFork.ofÎ¹ (G.map (kernel.Î¹ f)) (_ : G.map (kernel.Î¹ f) â‰« G.map â€¦
  exact @IsLimit.ofPointIso _ _ _ _ _ _ _ (kernelIsKernel (G.map f)) i
  -- ğŸ‰ no goals
#align category_theory.limits.preserves_kernel.of_iso_comparison CategoryTheory.Limits.PreservesKernel.ofIsoComparison

variable [PreservesLimit (parallelPair f 0) G]

/-- If `G` preserves the kernel of `f`, then the kernel comparison map for `G` at `f` is
an isomorphism.
-/
def PreservesKernel.iso : G.obj (kernel f) â‰… kernel (G.map f) :=
  IsLimit.conePointUniqueUpToIso (isLimitOfHasKernelOfPreservesLimit G f) (limit.isLimit _)
#align category_theory.limits.preserves_kernel.iso CategoryTheory.Limits.PreservesKernel.iso

@[simp]
theorem PreservesKernel.iso_hom : (PreservesKernel.iso G f).hom = kernelComparison f G := by
  rw [â† cancel_mono (kernel.Î¹ _)]
  -- âŠ¢ (iso G f).hom â‰« kernel.Î¹ (G.map f) = kernelComparison f G â‰« kernel.Î¹ (G.map f)
  simp [PreservesKernel.iso]
  -- ğŸ‰ no goals
#align category_theory.limits.preserves_kernel.iso_hom CategoryTheory.Limits.PreservesKernel.iso_hom

instance : IsIso (kernelComparison f G) := by
  rw [â† PreservesKernel.iso_hom]
  -- âŠ¢ IsIso (PreservesKernel.iso G f).hom
  infer_instance
  -- ğŸ‰ no goals

@[reassoc]
theorem kernel_map_comp_preserves_kernel_iso_inv {X' Y' : C} (g : X' âŸ¶ Y') [HasKernel g]
    [HasKernel (G.map g)] [PreservesLimit (parallelPair g 0) G] (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (hpq : f â‰« q = p â‰« g) :
    kernel.map (G.map f) (G.map g) (G.map p) (G.map q) (by rw [â† G.map_comp, hpq, G.map_comp]) â‰«
                                                           -- ğŸ‰ no goals
        (PreservesKernel.iso G _).inv =
      (PreservesKernel.iso G _).inv â‰« G.map (kernel.map f g p q hpq) := by
  rw [Iso.comp_inv_eq, Category.assoc, PreservesKernel.iso_hom, Iso.eq_inv_comp,
    PreservesKernel.iso_hom, kernelComparison_comp_kernel_map]
#align category_theory.limits.kernel_map_comp_preserves_kernel_iso_inv CategoryTheory.Limits.kernel_map_comp_preserves_kernel_iso_inv

end Kernels

namespace CokernelCofork

variable {X Y : C} {f : X âŸ¶ Y} (c : CokernelCofork f)
  (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G]

@[reassoc (attr := simp)]
lemma map_condition : G.map f â‰« G.map c.Ï€ = 0 := by
  rw [â† G.map_comp, c.condition, G.map_zero]
  -- ğŸ‰ no goals

/-- A cokernel cofork for `f` is mapped to a cokernel cofork for `G.map f` if `G` is a functor
which preserves zero morphisms. -/
def map : CokernelCofork (G.map f) :=
  CokernelCofork.ofÏ€ (G.map c.Ï€) (c.map_condition G)

@[simp]
lemma map_Ï€ : (c.map G).Ï€ = G.map c.Ï€ := rfl

/-- The underlying cocone of a cokernel cofork is mapped to a colimit cocone if and only if
the mapped cokernel cofork is colimit. -/
def isColimitMapCoconeEquiv :
    IsColimit (G.mapCocone c) â‰ƒ IsColimit (c.map G) := by
  refine' (IsColimit.precomposeHomEquiv _ _).symm.trans (IsColimit.equivIsoColimit _)
  -- âŠ¢ parallelPair (G.map f) 0 â‰… parallelPair f 0 â‹™ G
  refine' parallelPair.ext (Iso.refl _) (Iso.refl _) _ _ <;> simp
  -- âŠ¢ (parallelPair (G.map f) 0).map WalkingParallelPairHom.left â‰« (Iso.refl ((par â€¦
                                                             -- ğŸ‰ no goals
                                                             -- ğŸ‰ no goals
  exact Cocones.ext (Iso.refl _) (by rintro (_|_) <;> aesop_cat)
  -- ğŸ‰ no goals

/-- A colimit cokernel cofork is mapped to a colimit cokernel cofork by a functor `G`
when this functor preserves the corresponding colimit. -/
def mapIsColimit  (hc : IsColimit c) (G : C â¥¤ D)
    [Functor.PreservesZeroMorphisms G] [PreservesColimit (parallelPair f 0) G] :
    IsColimit (c.map G) :=
  c.isColimitMapCoconeEquiv G (isColimitOfPreserves G hc)

end CokernelCofork

section Cokernels

variable (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G]
  {X Y Z : C} {f : X âŸ¶ Y} {h : Y âŸ¶ Z} (w : f â‰« h = 0)

/-- The map of a cokernel cofork is a colimit iff
the cokernel cofork consisting of the mapped morphisms is a colimit.
This essentially lets us commute `CokernelCofork.ofÏ€` with `Functor.mapCocone`.

This is a variant of `isColimitMapCoconeCoforkEquiv` for equalizers,
which we can't use directly between `G.map 0 = 0` does not hold definitionally.
-/
def isColimitMapCoconeCoforkEquiv' :
    IsColimit (G.mapCocone (CokernelCofork.ofÏ€ h w)) â‰ƒ
      IsColimit
        (CokernelCofork.ofÏ€ (G.map h) (by simp only [â† G.map_comp, w, Functor.map_zero]) :
                                          -- ğŸ‰ no goals
          Cofork (G.map f) 0) :=
  CokernelCofork.isColimitMapCoconeEquiv _ _
#align category_theory.limits.is_colimit_map_cocone_cofork_equiv' CategoryTheory.Limits.isColimitMapCoconeCoforkEquiv'

/-- The property of preserving cokernels expressed in terms of cokernel coforks.

This is a variant of `isColimitCoforkMapOfIsColimit` for equalizers,
which we can't use directly between `G.map 0 = 0` does not hold definitionally.
-/
def isColimitCoforkMapOfIsColimit' [PreservesColimit (parallelPair f 0) G]
    (l : IsColimit (CokernelCofork.ofÏ€ h w)) :
    IsColimit
      (CokernelCofork.ofÏ€ (G.map h) (by simp only [â† G.map_comp, w, Functor.map_zero]) :
                                        -- ğŸ‰ no goals
        Cofork (G.map f) 0) :=
  isColimitMapCoconeCoforkEquiv' G w (PreservesColimit.preserves l)
#align category_theory.limits.is_colimit_cofork_map_of_is_colimit' CategoryTheory.Limits.isColimitCoforkMapOfIsColimit'

variable (f) [HasCokernel f]

/--
If `G` preserves cokernels and `C` has them, then the cofork constructed of the mapped morphisms of
a cokernel cofork is a colimit.
-/
def isColimitOfHasCokernelOfPreservesColimit [PreservesColimit (parallelPair f 0) G] :
    IsColimit
      (Cofork.ofÏ€ (G.map (cokernel.Ï€ f))
          (by simp only [â† G.map_comp, cokernel.condition, zero_comp, Functor.map_zero]) :
              -- ğŸ‰ no goals
        Cofork (G.map f) 0) :=
  isColimitCoforkMapOfIsColimit' G (cokernel.condition f) (cokernelIsCokernel f)
#align category_theory.limits.is_colimit_of_has_cokernel_of_preserves_colimit CategoryTheory.Limits.isColimitOfHasCokernelOfPreservesColimit

instance [PreservesColimit (parallelPair f 0) G] : HasCokernel (G.map f)
    where exists_colimit := âŸ¨âŸ¨_, isColimitOfHasCokernelOfPreservesColimit G fâŸ©âŸ©

variable [HasCokernel (G.map f)]

/-- If the cokernel comparison map for `G` at `f` is an isomorphism, then `G` preserves the
cokernel of `f`.
-/
def PreservesCokernel.ofIsoComparison [i : IsIso (cokernelComparison f G)] :
    PreservesColimit (parallelPair f 0) G := by
  apply preservesColimitOfPreservesColimitCocone (cokernelIsCokernel f)
  -- âŠ¢ IsColimit (G.mapCocone (Cofork.ofÏ€ (cokernel.Ï€ f) (_ : f â‰« cokernel.Ï€ f = 0  â€¦
  apply (isColimitMapCoconeCoforkEquiv' G (cokernel.condition f)).symm _
  -- âŠ¢ IsColimit (CokernelCofork.ofÏ€ (G.map (cokernel.Ï€ f)) (_ : G.map f â‰« G.map (c â€¦
  exact @IsColimit.ofPointIso _ _ _ _ _ _ _ (cokernelIsCokernel (G.map f)) i
  -- ğŸ‰ no goals
#align category_theory.limits.preserves_cokernel.of_iso_comparison CategoryTheory.Limits.PreservesCokernel.ofIsoComparison

variable [PreservesColimit (parallelPair f 0) G]

/-- If `G` preserves the cokernel of `f`, then the cokernel comparison map for `G` at `f` is
an isomorphism.
-/
def PreservesCokernel.iso : G.obj (cokernel f) â‰… cokernel (G.map f) :=
  IsColimit.coconePointUniqueUpToIso (isColimitOfHasCokernelOfPreservesColimit G f)
    (colimit.isColimit _)
#align category_theory.limits.preserves_cokernel.iso CategoryTheory.Limits.PreservesCokernel.iso

@[simp]
theorem PreservesCokernel.iso_inv : (PreservesCokernel.iso G f).inv = cokernelComparison f G := by
  rw [â† cancel_epi (cokernel.Ï€ _)]
  -- âŠ¢ cokernel.Ï€ (G.map f) â‰« (iso G f).inv = cokernel.Ï€ (G.map f) â‰« cokernelCompar â€¦
  simp [PreservesCokernel.iso]
  -- ğŸ‰ no goals
#align category_theory.limits.preserves_cokernel.iso_inv CategoryTheory.Limits.PreservesCokernel.iso_inv

instance : IsIso (cokernelComparison f G) := by
  rw [â† PreservesCokernel.iso_inv]
  -- âŠ¢ IsIso (PreservesCokernel.iso G f).inv
  infer_instance
  -- ğŸ‰ no goals

@[reassoc]
theorem preserves_cokernel_iso_comp_cokernel_map {X' Y' : C} (g : X' âŸ¶ Y') [HasCokernel g]
    [HasCokernel (G.map g)] [PreservesColimit (parallelPair g 0) G] (p : X âŸ¶ X') (q : Y âŸ¶ Y')
    (hpq : f â‰« q = p â‰« g) :
    (PreservesCokernel.iso G _).hom â‰«
        cokernel.map (G.map f) (G.map g) (G.map p) (G.map q)
          (by rw [â† G.map_comp, hpq, G.map_comp]) =
              -- ğŸ‰ no goals
      G.map (cokernel.map f g p q hpq) â‰« (PreservesCokernel.iso G _).hom := by
  rw [â† Iso.comp_inv_eq, Category.assoc, â† Iso.eq_inv_comp, PreservesCokernel.iso_inv,
    cokernel_map_comp_cokernelComparison, PreservesCokernel.iso_inv]
#align category_theory.limits.preserves_cokernel_iso_comp_cokernel_map CategoryTheory.Limits.preserves_cokernel_iso_comp_cokernel_map

end Cokernels

variable (X Y : C) (G : C â¥¤ D) [Functor.PreservesZeroMorphisms G]

noncomputable instance preservesKernelZero :
    PreservesLimit (parallelPair (0 : X âŸ¶ Y) 0) G where
  preserves {c} hc := by
    have := KernelFork.IsLimit.isIso_Î¹ c hc rfl
    -- âŠ¢ IsLimit (G.mapCone c)
    refine' (KernelFork.isLimitMapConeEquiv c G).symm _
    -- âŠ¢ IsLimit (KernelFork.map c G)
    refine' IsLimit.ofIsoLimit (KernelFork.IsLimit.ofId _ (G.map_zero _ _)) _
    -- âŠ¢ KernelFork.ofÎ¹ (ğŸ™ (G.obj X)) (_ : ğŸ™ (G.obj X) â‰« G.map 0 = 0) â‰… KernelFork.ma â€¦
    exact (Fork.ext (G.mapIso (asIso (Fork.Î¹ c))).symm (by simp))
    -- ğŸ‰ no goals

noncomputable instance preservesCokernelZero :
    PreservesColimit (parallelPair (0 : X âŸ¶ Y) 0) G where
  preserves {c} hc := by
    have := CokernelCofork.IsColimit.isIso_Ï€ c hc rfl
    -- âŠ¢ IsColimit (G.mapCocone c)
    refine' (CokernelCofork.isColimitMapCoconeEquiv c G).symm _
    -- âŠ¢ IsColimit (CokernelCofork.map c G)
    refine' IsColimit.ofIsoColimit (CokernelCofork.IsColimit.ofId _ (G.map_zero _ _)) _
    -- âŠ¢ CokernelCofork.ofÏ€ (ğŸ™ (G.obj Y)) (_ : G.map 0 â‰« ğŸ™ (G.obj Y) = 0) â‰… CokernelC â€¦
    exact (Cofork.ext (G.mapIso (asIso (Cofork.Ï€ c))) (by simp))
    -- ğŸ‰ no goals

variable {X Y}

/-- The kernel of a zero map is preserved by any functor which preserves zero morphisms. -/
noncomputable def preservesKernelZero' (f : X âŸ¶ Y) (hf : f = 0) :
    PreservesLimit (parallelPair f 0) G := by
  rw [hf]
  -- âŠ¢ PreservesLimit (parallelPair 0 0) G
  infer_instance
  -- ğŸ‰ no goals

/-- The cokernel of a zero map is preserved by any functor which preserves zero morphisms. -/
noncomputable def preservesCokernelZero' (f : X âŸ¶ Y) (hf : f = 0) :
    PreservesColimit (parallelPair f 0) G := by
  rw [hf]
  -- âŠ¢ PreservesColimit (parallelPair 0 0) G
  infer_instance
  -- ğŸ‰ no goals

end CategoryTheory.Limits
