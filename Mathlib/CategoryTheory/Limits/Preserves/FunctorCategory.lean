/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Preserves.Ulift
import Mathlib.CategoryTheory.Limits.Presheaf
import Mathlib.CategoryTheory.Limits.Yoneda

/-!
# Preservation of (co)limits in the functor category

* Show that if `X ‚®Ø -` preserves colimits in `D` for any `X : D`, then the product functor `F ‚®Ø -`
  for `F : C ‚•§ D` preserves colimits.

  The idea of the proof is simply that products and colimits in the functor category are computed
  pointwise, so pointwise preservation implies general preservation.
* Show that `F ‚ãô -` preserves limits if the target category has limits.
* Show that `F : C ‚•§ D` preserves limits of a certain shape
  if `Lan F.op : C·µí·µñ ‚•§ Type*` preserves such limits.

# References

https://ncatlab.org/nlab/show/commutativity+of+limits+and+colimits#preservation_by_functor_categories_and_localizations

-/


universe w w' v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable section

namespace CategoryTheory

open Category Limits Functor

section

variable {C : Type u} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{u} D]
variable {E : Type u} [Category.{v‚ÇÇ} E]

/-- If `X √ó -` preserves colimits in `D` for any `X : D`, then the product functor `F ‚®Ø -` for
`F : C ‚•§ D` also preserves colimits.

Note this is (mathematically) a special case of the statement that
"if limits commute with colimits in `D`, then they do as well in `C ‚•§ D`"
but the story in Lean is a bit more complex, and this statement isn't directly a special case.
That is, even with a formalised proof of the general statement, there would still need to be some
work to convert to this version: namely, the natural isomorphism
`(evaluation C D).obj k ‚ãô prod.functor.obj (F.obj k) ‚âÖ
  prod.functor.obj F ‚ãô (evaluation C D).obj k`
-/
lemma FunctorCategory.prod_preservesColimits [HasBinaryProducts D] [HasColimits D]
    [‚àÄ X : D, PreservesColimits (prod.functor.obj X)] (F : C ‚•§ D) :
    PreservesColimits (prod.functor.obj F) where
  preservesColimitsOfShape {J : Type u} [Category.{u, u} J] :=
    {
      preservesColimit := fun {K : J ‚•§ C ‚•§ D} ‚Ü¶ ({
          preserves := fun {c : Cocone K} (t : IsColimit c) ‚Ü¶ ‚ü®by
            apply evaluationJointlyReflectsColimits _ fun {k} ‚Ü¶ ?_
            change IsColimit ((prod.functor.obj F ‚ãô (evaluation _ _).obj k).mapCocone c)
            let this :=
              isColimitOfPreserves ((evaluation C D).obj k ‚ãô prod.functor.obj (F.obj k)) t
            apply IsColimit.mapCoconeEquiv _ this
            apply (NatIso.ofComponents _ _).symm
            ¬∑ intro G
              apply asIso (prodComparison ((evaluation C D).obj k) F G)
            ¬∑ intro G G'
              apply prodComparison_natural ((evaluation C D).obj k) (ùüô F)‚ü© } ) }

end

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

instance whiskeringLeft_preservesLimitsOfShape (J : Type u) [Category.{v} J]
    [HasLimitsOfShape J D] (F : C ‚•§ E) :
    PreservesLimitsOfShape J ((whiskeringLeft C E D).obj F) :=
  ‚ü®fun {K} ‚Ü¶
    ‚ü®fun c {hc} ‚Ü¶ ‚ü®by
      apply evaluationJointlyReflectsLimits
      intro Y
      change IsLimit (((evaluation E D).obj (F.obj Y)).mapCone c)
      exact isLimitOfPreserves _ hc‚ü©‚ü©‚ü©

instance whiskeringLeft_preservesColimitsOfShape (J : Type u) [Category.{v} J]
    [HasColimitsOfShape J D] (F : C ‚•§ E) :
    PreservesColimitsOfShape J ((whiskeringLeft C E D).obj F) :=
  ‚ü®fun {K} ‚Ü¶
    ‚ü®fun c {hc} ‚Ü¶ ‚ü®by
      apply evaluationJointlyReflectsColimits
      intro Y
      change IsColimit (((evaluation E D).obj (F.obj Y)).mapCocone c)
      exact isColimitOfPreserves _ hc‚ü©‚ü©‚ü©

instance whiskeringLeft_preservesLimits [HasLimitsOfSize.{w, w'} D] (F : C ‚•§ E) :
    PreservesLimitsOfSize.{w, w'} ((whiskeringLeft C E D).obj F) :=
  ‚ü®fun {J} _ ‚Ü¶ whiskeringLeft_preservesLimitsOfShape J F‚ü©

instance whiskeringLeft_preservesColimit [HasColimitsOfSize.{w, w'} D] (F : C ‚•§ E) :
    PreservesColimitsOfSize.{w, w'} ((whiskeringLeft C E D).obj F) :=
  ‚ü®fun {J} _ ‚Ü¶ whiskeringLeft_preservesColimitsOfShape J F‚ü©

instance whiskeringRight_preservesLimitsOfShape {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasLimitsOfShape J D] (F : D ‚•§ E) [PreservesLimitsOfShape J F] :
    PreservesLimitsOfShape J ((whiskeringRight C D E).obj F) :=
  ‚ü®fun {K} ‚Ü¶
    ‚ü®fun c {hc} ‚Ü¶ ‚ü®by
      apply evaluationJointlyReflectsLimits _ (fun k ‚Ü¶ ?_)
      change IsLimit (((evaluation _ _).obj k ‚ãô F).mapCone c)
      exact isLimitOfPreserves _ hc‚ü©‚ü©‚ü©

/-- Whiskering right and then taking a limit is the same as taking the limit and applying the
functor. -/
def limitCompWhiskeringRightIsoLimitComp {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasLimitsOfShape J D] (F : D ‚•§ E) [PreservesLimitsOfShape J F] (G : J ‚•§ C ‚•§ D) :
    limit (G ‚ãô (whiskeringRight _ _ _).obj F) ‚âÖ limit G ‚ãô F :=
  (preservesLimitIso _ _).symm

@[reassoc (attr := simp)]
theorem limitCompWhiskeringRightIsoLimitComp_inv_œÄ {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasLimitsOfShape J D] (F : D ‚•§ E) [PreservesLimitsOfShape J F] (G : J ‚•§ C ‚•§ D) (j : J) :
    (limitCompWhiskeringRightIsoLimitComp F G).inv ‚â´
      limit.œÄ (G ‚ãô (whiskeringRight _ _ _).obj F) j = whiskerRight (limit.œÄ G j) F := by
  simp [limitCompWhiskeringRightIsoLimitComp]

@[reassoc (attr := simp)]
theorem limitCompWhiskeringRightIsoLimitComp_hom_whiskerRight_œÄ {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasLimitsOfShape J D] (F : D ‚•§ E) [PreservesLimitsOfShape J F] (G : J ‚•§ C ‚•§ D) (j : J) :
    (limitCompWhiskeringRightIsoLimitComp F G).hom ‚â´ whiskerRight (limit.œÄ G j) F =
      limit.œÄ (G ‚ãô (whiskeringRight _ _ _).obj F) j := by
  simp [‚Üê Iso.eq_inv_comp]

instance whiskeringRight_preservesColimitsOfShape {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasColimitsOfShape J D] (F : D ‚•§ E) [PreservesColimitsOfShape J F] :
    PreservesColimitsOfShape J ((whiskeringRight C D E).obj F) :=
  ‚ü®fun {K} ‚Ü¶
    ‚ü®fun c {hc} ‚Ü¶ ‚ü®by
      apply evaluationJointlyReflectsColimits _ (fun k ‚Ü¶ ?_)
      change IsColimit (((evaluation _ _).obj k ‚ãô F).mapCocone c)
      exact isColimitOfPreserves _ hc‚ü©‚ü©‚ü©

/-- Whiskering right and then taking a colimit is the same as taking the colimit and applying the
functor. -/
def colimitCompWhiskeringRightIsoColimitComp {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasColimitsOfShape J D] (F : D ‚•§ E) [PreservesColimitsOfShape J F] (G : J ‚•§ C ‚•§ D) :
    colimit (G ‚ãô (whiskeringRight _ _ _).obj F) ‚âÖ colimit G ‚ãô F :=
  (preservesColimitIso _ _).symm

@[reassoc (attr := simp)]
theorem Œπ_colimitCompWhiskeringRightIsoColimitComp_hom {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasColimitsOfShape J D] (F : D ‚•§ E) [PreservesColimitsOfShape J F] (G : J ‚•§ C ‚•§ D) (j : J) :
    colimit.Œπ (G ‚ãô (whiskeringRight _ _ _).obj F) j ‚â´
      (colimitCompWhiskeringRightIsoColimitComp F G).hom = whiskerRight (colimit.Œπ G j) F := by
  simp [colimitCompWhiskeringRightIsoColimitComp]

@[reassoc (attr := simp)]
theorem whiskerRight_Œπ_colimitCompWhiskeringRightIsoColimitComp_inv {C : Type*} [Category C]
    {D : Type*} [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasColimitsOfShape J D] (F : D ‚•§ E) [PreservesColimitsOfShape J F] (G : J ‚•§ C ‚•§ D) (j : J) :
    whiskerRight (colimit.Œπ G j) F ‚â´ (colimitCompWhiskeringRightIsoColimitComp F G).inv =
      colimit.Œπ (G ‚ãô (whiskeringRight _ _ _).obj F) j := by
  simp [Iso.comp_inv_eq]

instance whiskeringRightPreservesLimits {C : Type*} [Category C] {D : Type*} [Category D]
    {E : Type*} [Category E] (F : D ‚•§ E) [HasLimitsOfSize.{w, w'} D]
    [PreservesLimitsOfSize.{w, w'} F] :
    PreservesLimitsOfSize.{w, w'} ((whiskeringRight C D E).obj F) :=
  ‚ü®inferInstance‚ü©

instance whiskeringRightPreservesColimits {C : Type*} [Category C] {D : Type*} [Category D]
    {E : Type*} [Category E] (F : D ‚•§ E) [HasColimitsOfSize.{w, w'} D]
    [PreservesColimitsOfSize.{w, w'} F] :
    PreservesColimitsOfSize.{w, w'} ((whiskeringRight C D E).obj F) :=
  ‚ü®inferInstance‚ü©

/-- If `Lan F.op : (C·µí·µñ ‚•§ Type*) ‚•§ (D·µí·µñ ‚•§ Type*)` preserves limits of shape `J`, so will `F`. -/
lemma preservesLimit_of_lan_preservesLimit {C D : Type u} [SmallCategory C]
    [SmallCategory D] (F : C ‚•§ D) (J : Type u) [SmallCategory J]
    [PreservesLimitsOfShape J (F.op.lan : _ ‚•§ D·µí·µñ ‚•§ Type u)] : PreservesLimitsOfShape J F :=
  letI := preservesLimitsOfShape_of_natIso (J := J)
    (Presheaf.compULiftYonedaIsoULiftYonedaCompLan.{u} F).symm
  preservesLimitsOfShape_of_reflects_of_preserves F uliftYoneda.{u}

/-- `F : C ‚•§ D ‚•§ E` preserves finite limits if it does for each `d : D`. -/
lemma preservesFiniteLimits_of_evaluation {D : Type*} [Category D] {E : Type*} [Category E]
    (F : C ‚•§ D ‚•§ E) (h : ‚àÄ d : D, PreservesFiniteLimits (F ‚ãô (evaluation D E).obj d)) :
    PreservesFiniteLimits F :=
  ‚ü®fun J _ _ ‚Ü¶ preservesLimitsOfShape_of_evaluation F J fun k ‚Ü¶ (h k).preservesFiniteLimits _‚ü©

/-- `F : C ‚•§ D ‚•§ E` preserves finite limits if it does for each `d : D`. -/
lemma preservesFiniteColimits_of_evaluation {D : Type*} [Category D] {E : Type*} [Category E]
    (F : C ‚•§ D ‚•§ E) (h : ‚àÄ d : D, PreservesFiniteColimits (F ‚ãô (evaluation D E).obj d)) :
    PreservesFiniteColimits F :=
  ‚ü®fun J _ _ ‚Ü¶ preservesColimitsOfShape_of_evaluation F J fun k ‚Ü¶ (h k).preservesFiniteColimits _‚ü©

end

section

variable {C : Type u} [Category.{v} C]
variable {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]
variable {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K]
variable {D : Type u‚ÇÉ} [Category.{v‚ÇÉ} D]

section

variable [HasLimitsOfShape J C] [HasColimitsOfShape K C]
variable [PreservesLimitsOfShape J (colim : (K ‚•§ C) ‚•§ _)]

noncomputable instance : PreservesLimitsOfShape J (colim : (K ‚•§ D ‚•§ C) ‚•§ _) :=
  preservesLimitsOfShape_of_evaluation _ _ (fun d ‚Ü¶
    let i : (colim : (K ‚•§ D ‚•§ C) ‚•§ _) ‚ãô (evaluation D C).obj d ‚âÖ
        colimit ((whiskeringRight K (D ‚•§ C) C).obj ((evaluation D C).obj d)).flip :=
      NatIso.ofComponents (fun X ‚Ü¶ (colimitObjIsoColimitCompEvaluation _ _) ‚â™‚â´
          (by exact HasColimit.isoOfNatIso (Iso.refl _)) ‚â™‚â´
          (colimitObjIsoColimitCompEvaluation _ _).symm)
        (fun {F G} Œ∑ ‚Ü¶ colimit_obj_ext (fun j ‚Ü¶ by simp [‚Üê NatTrans.comp_app_assoc]))
    preservesLimitsOfShape_of_natIso (i ‚â™‚â´ colimitFlipIsoCompColim _).symm)

end

section

variable [HasColimitsOfShape J C] [HasLimitsOfShape K C]
variable [PreservesColimitsOfShape J (lim : (K ‚•§ C) ‚•§ _)]

noncomputable instance : PreservesColimitsOfShape J (lim : (K ‚•§ D ‚•§ C) ‚•§ _) :=
  preservesColimitsOfShape_of_evaluation _ _ (fun d ‚Ü¶
    let i : (lim : (K ‚•§ D ‚•§ C) ‚•§ _) ‚ãô (evaluation D C).obj d ‚âÖ
        limit ((whiskeringRight K (D ‚•§ C) C).obj ((evaluation D C).obj d)).flip :=
      NatIso.ofComponents (fun X ‚Ü¶ (limitObjIsoLimitCompEvaluation _ _) ‚â™‚â´
          (by exact HasLimit.isoOfNatIso (Iso.refl _)) ‚â™‚â´
          (limitObjIsoLimitCompEvaluation _ _).symm)
        (fun {F G} Œ∑ ‚Ü¶ limit_obj_ext (fun j ‚Ü¶ by simp [‚Üê NatTrans.comp_app]))
    preservesColimitsOfShape_of_natIso (i ‚â™‚â´ limitFlipIsoCompLim _).symm)

end

end

end CategoryTheory
