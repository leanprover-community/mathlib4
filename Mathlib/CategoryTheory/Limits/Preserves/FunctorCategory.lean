/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Yoneda
import Mathlib.CategoryTheory.Limits.Presheaf

/-!
# Preservation of (co)limits in the functor category

* Show that if `X â¨¯ -` preserves colimits in `D` for any `X : D`, then the product functor `F â¨¯ -`
for `F : C â¥¤ D` preserves colimits.

The idea of the proof is simply that products and colimits in the functor category are computed
pointwise, so pointwise preservation implies general preservation.

* Show that `F â‹™ -` preserves limits if the target category has limits.
* Show that `F : C â¥¤ D` preserves limits of a certain shape
  if `Lan F.op : Cáµ’áµ– â¥¤ Type*` preserves such limits.

# References

https://ncatlab.org/nlab/show/commutativity+of+limits+and+colimits#preservation_by_functor_categories_and_localizations

-/


universe w w' v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

noncomputable section

namespace CategoryTheory

open Category Limits

section

variable {C : Type u} [Category.{vâ‚} C]
variable {D : Type uâ‚‚} [Category.{u} D]
variable {E : Type u} [Category.{vâ‚‚} E]

/-- If `X Ã— -` preserves colimits in `D` for any `X : D`, then the product functor `F â¨¯ -` for
`F : C â¥¤ D` also preserves colimits.

Note this is (mathematically) a special case of the statement that
"if limits commute with colimits in `D`, then they do as well in `C â¥¤ D`"
but the story in Lean is a bit more complex, and this statement isn't directly a special case.
That is, even with a formalised proof of the general statement, there would still need to be some
work to convert to this version: namely, the natural isomorphism
`(evaluation C D).obj k â‹™ prod.functor.obj (F.obj k) â‰…
  prod.functor.obj F â‹™ (evaluation C D).obj k`
-/
def FunctorCategory.prodPreservesColimits [HasBinaryProducts D] [HasColimits D]
    [âˆ€ X : D, PreservesColimits (prod.functor.obj X)] (F : C â¥¤ D) :
    PreservesColimits (prod.functor.obj F) where
  preservesColimitsOfShape {J : Type u} [Category.{u, u} J] :=
    {
      preservesColimit := fun {K : J â¥¤ C â¥¤ D} =>
        ( {
          preserves := fun {c : Cocone K} (t : IsColimit c) => by
            apply evaluationJointlyReflectsColimits _ fun {k} => ?_
            change IsColimit ((prod.functor.obj F â‹™ (evaluation _ _).obj k).mapCocone c)
            let this :=
              isColimitOfPreserves ((evaluation C D).obj k â‹™ prod.functor.obj (F.obj k)) t
            apply IsColimit.mapCoconeEquiv _ this
            apply (NatIso.ofComponents _ _).symm
            Â· intro G
              apply asIso (prodComparison ((evaluation C D).obj k) F G)
            Â· intro G G'
              apply prodComparison_natural ((evaluation C D).obj k) (ğŸ™ F) } ) }

end

variable {C : Type uâ‚} [Category.{vâ‚} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]
variable {E : Type uâ‚ƒ} [Category.{vâ‚ƒ} E]

instance whiskeringLeftPreservesLimitsOfShape (J : Type u) [Category.{v} J]
    [HasLimitsOfShape J D] (F : C â¥¤ E) :
    PreservesLimitsOfShape J ((whiskeringLeft C E D).obj F) :=
  âŸ¨fun {K} =>
    âŸ¨fun c {hc} => by
      apply evaluationJointlyReflectsLimits
      intro Y
      change IsLimit (((evaluation E D).obj (F.obj Y)).mapCone c)
      exact PreservesLimit.preserves hcâŸ©âŸ©

instance whiskeringLeftPreservesLimits [HasLimitsOfSize.{w} D] (F : C â¥¤ E) :
    PreservesLimitsOfSize.{w, w'} ((whiskeringLeft C E D).obj F) :=
  âŸ¨fun {J} _ => whiskeringLeftPreservesLimitsOfShape J FâŸ©

instance whiskeringRightPreservesLimitsOfShape {C : Type*} [Category C] {D : Type*}
    [Category D] {E : Type*} [Category E] {J : Type*} [Category J]
    [HasLimitsOfShape J D] (F : D â¥¤ E) [PreservesLimitsOfShape J F] :
    PreservesLimitsOfShape J ((whiskeringRight C D E).obj F) :=
  âŸ¨fun {K} =>
    âŸ¨fun c {hc} => by
      apply evaluationJointlyReflectsLimits _ (fun k => ?_)
      change IsLimit (((evaluation _ _).obj k â‹™ F).mapCone c)
      exact PreservesLimit.preserves hcâŸ©âŸ©

instance whiskeringRightPreservesLimits {C : Type*} [Category C] {D : Type*} [Category D]
    {E : Type*} [Category E] (F : D â¥¤ E) [HasLimitsOfSize.{w, w'} D]
    [PreservesLimitsOfSize.{w, w'} F] :
    PreservesLimitsOfSize.{w, w'} ((whiskeringRight C D E).obj F) :=
  âŸ¨inferInstanceâŸ©

-- Porting note: fixed spelling mistake in def
/-- If `Lan F.op : (Cáµ’áµ– â¥¤ Type*) â¥¤ (Dáµ’áµ– â¥¤ Type*)` preserves limits of shape `J`, so will `F`. -/
noncomputable def preservesLimitOfLanPreservesLimit {C D : Type u} [SmallCategory C]
    [SmallCategory D] (F : C â¥¤ D) (J : Type u) [SmallCategory J]
    [PreservesLimitsOfShape J (F.op.lan : _ â¥¤ Dáµ’áµ– â¥¤ Type u)] : PreservesLimitsOfShape J F := by
  apply @preservesLimitsOfShapeOfReflectsOfPreserves _ _ _ _ _ _ _ _ F yoneda ?_
  exact preservesLimitsOfShapeOfNatIso (Presheaf.compYonedaIsoYonedaCompLan F).symm

/-- `F : C â¥¤ D â¥¤ E` preserves finite limits if it does for each `d : D`. -/
def preservesFiniteLimitsOfEvaluation {D : Type*} [Category D] {E : Type*} [Category E]
    (F : C â¥¤ D â¥¤ E) (h : âˆ€ d : D, PreservesFiniteLimits (F â‹™ (evaluation D E).obj d)) :
    PreservesFiniteLimits F :=
  âŸ¨fun J _ _ => preservesLimitsOfShapeOfEvaluation F J fun k => (h k).preservesFiniteLimits _âŸ©

/-- `F : C â¥¤ D â¥¤ E` preserves finite limits if it does for each `d : D`. -/
def preservesFiniteColimitsOfEvaluation {D : Type*} [Category D] {E : Type*} [Category E]
    (F : C â¥¤ D â¥¤ E) (h : âˆ€ d : D, PreservesFiniteColimits (F â‹™ (evaluation D E).obj d)) :
    PreservesFiniteColimits F :=
  âŸ¨fun J _ _ => preservesColimitsOfShapeOfEvaluation F J fun k => (h k).preservesFiniteColimits _âŸ©

end CategoryTheory
