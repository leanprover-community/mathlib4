/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Adam Topaz
-/
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Basic
import Mathlib.CategoryTheory.Limits.Types.Colimits
import Mathlib.CategoryTheory.Limits.Types.Images
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.CategoryTheory.Limits.Yoneda

/-!
# Facts about (co)limits of functors into concrete categories
-/


universe s t w v u r

open CategoryTheory

namespace CategoryTheory.Types

open Limits

/-! The forgetful functor on `Type u` is the identity; copy the instances on `ùü≠ (Type u)`
over to `forget (Type u)`.

We currently have two instances for `HasForget (Type u)`:

* A global `HasForget` instance where `forget (Type u)` reduces to `ùü≠ Type`
* A locally enabled `ConcreteCategory` where `forget (Type u)` is only reducible-with-instances
  equal to `ùü≠ Type`.

Since instance synthesis only looks through reducible definitions, we need to help it out by copying
over the instances that wouldn't be found otherwise.
-/

attribute [local instance] Types.instFunLike Types.instConcreteCategory

instance : (@forget (Type u) _ ConcreteCategory.toHasForget).Full :=
  Functor.Full.id

instance : PreservesLimitsOfSize (@forget (Type u) _ ConcreteCategory.toHasForget) :=
  id_preservesLimitsOfSize
instance : PreservesColimitsOfSize (@forget (Type u) _ ConcreteCategory.toHasForget) :=
  id_preservesColimitsOfSize

instance : ReflectsLimitsOfSize (@forget (Type u) _ ConcreteCategory.toHasForget) :=
  id_reflectsLimits
instance : ReflectsColimitsOfSize (@forget (Type u) _ ConcreteCategory.toHasForget) :=
  id_reflectsColimits

instance : (@forget (Type u) _ ConcreteCategory.toHasForget).IsEquivalence :=
  Functor.isEquivalence_refl

instance : (@forget (Type u) _ ConcreteCategory.toHasForget).IsCorepresentable :=
  instIsCorepresentableIdType

end CategoryTheory.Types

namespace CategoryTheory.Limits.Concrete

section Limits

/-- If a functor `G : J ‚•§ C` to a concrete category has a limit and that `forget C`
is corepresentable, then `(G ‚ãô forget C).sections` is small. -/
lemma small_sections_of_hasLimit
    {C : Type u} [Category.{v} C] [HasForget.{v} C]
    [(forget C).IsCorepresentable] {J : Type w} [Category.{t} J] (G : J ‚•§ C) [HasLimit G] :
    Small.{v} (G ‚ãô forget C).sections := by
  rw [‚Üê Types.hasLimit_iff_small_sections]
  infer_instance

variable {C : Type u} [Category.{v} C] {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type r}
variable [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)] [ConcreteCategory.{r} C FC]
variable {J : Type w} [Category.{t} J] (F : J ‚•§ C) [PreservesLimit F (forget C)]

theorem to_product_injective_of_isLimit
    {D : Cone F} (hD : IsLimit D) :
    Function.Injective fun (x : ToType D.pt) (j : J) => D.œÄ.app j x := by
  let E := (forget C).mapCone D
  intro (x : E.pt) y H
  apply (Types.isLimitEquivSections (isLimitOfPreserves _ hD)).injective
  ext j
  exact funext_iff.mp H j

theorem isLimit_ext {D : Cone F} (hD : IsLimit D) (x y : ToType D.pt) :
    (‚àÄ j, D.œÄ.app j x = D.œÄ.app j y) ‚Üí x = y := fun h =>
  Concrete.to_product_injective_of_isLimit _ hD (funext h)

theorem limit_ext [HasLimit F] (x y : ToType (limit F)) :
    (‚àÄ j, limit.œÄ F j x = limit.œÄ F j y) ‚Üí x = y :=
  Concrete.isLimit_ext F (limit.isLimit _) _ _

section Surjective

/--
Given surjections `‚ãØ ‚ü∂ X‚Çô‚Çä‚ÇÅ ‚ü∂ X‚Çô ‚ü∂ ‚ãØ ‚ü∂ X‚ÇÄ` in a concrete category whose forgetful functor
preserves sequential limits, the projection map `lim X‚Çô ‚ü∂ X‚ÇÄ` is surjective.
-/
lemma surjective_œÄ_app_zero_of_surjective_map {C : Type u} [Category.{v} C] {FC : C ‚Üí C ‚Üí Type*}
    {CC : C ‚Üí Type v} [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)] [ConcreteCategory.{v} C FC]
    [PreservesLimitsOfShape ‚Ñï·µí·µñ (forget C)] {F : ‚Ñï·µí·µñ ‚•§ C} {c : Cone F}
    (hc : IsLimit c) (hF : ‚àÄ n, Function.Surjective (F.map (homOfLE (Nat.le_succ n)).op)) :
    Function.Surjective (c.œÄ.app ‚ü®0‚ü©) :=
  Types.surjective_œÄ_app_zero_of_surjective_map (isLimitOfPreserves (forget C) hc) hF

end Surjective

end Limits

section Colimits

section

variable {C : Type u} [Category.{v} C] {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type t}
variable [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)] [ConcreteCategory.{t} C FC]
variable {J : Type w} [Category.{r} J] (F : J ‚•§ C)

section
variable [PreservesColimit F (forget C)]

theorem from_union_surjective_of_isColimit {D : Cocone F} (hD : IsColimit D) :
    let ff : (Œ£ j : J, ToType (F.obj j)) ‚Üí ToType D.pt := fun a => D.Œπ.app a.1 a.2
    Function.Surjective ff := by
  intro ff x
  let E : Cocone (F ‚ãô forget C) := (forget C).mapCocone D
  let hE : IsColimit E := isColimitOfPreserves (forget C) hD
  obtain ‚ü®j, y, hy‚ü© := Types.jointly_surjective_of_isColimit hE x
  exact ‚ü®‚ü®j, y‚ü©, hy‚ü©

theorem isColimit_exists_rep {D : Cocone F} (hD : IsColimit D) (x : ToType D.pt) :
    ‚àÉ (j : J) (y : ToType (F.obj j)), D.Œπ.app j y = x := by
  obtain ‚ü®a, rfl‚ü© := Concrete.from_union_surjective_of_isColimit F hD x
  exact ‚ü®a.1, a.2, rfl‚ü©

theorem colimit_exists_rep [HasColimit F] (x : ToType (colimit F)) :
    ‚àÉ (j : J) (y : ToType (F.obj j)), colimit.Œπ F j y = x :=
  Concrete.isColimit_exists_rep F (colimit.isColimit _) x

end

theorem isColimit_rep_eq_of_exists {D : Cocone F} {i j : J} (x : ToType (F.obj i))
    (y : ToType (F.obj j))
    (h : ‚àÉ (k : _) (f : i ‚ü∂ k) (g : j ‚ü∂ k), F.map f x = F.map g y) :
    D.Œπ.app i x = D.Œπ.app j y := by
  let E := (forget C).mapCocone D
  obtain ‚ü®k, f, g, (hfg : (F ‚ãô forget C).map f x = F.map g y)‚ü© := h
  let h1 : (F ‚ãô forget C).map f ‚â´ E.Œπ.app k = E.Œπ.app i := E.Œπ.naturality f
  let h2 : (F ‚ãô forget C).map g ‚â´ E.Œπ.app k = E.Œπ.app j := E.Œπ.naturality g
  change E.Œπ.app i x = E.Œπ.app j y
  rw [‚Üê h1, types_comp_apply, hfg]
  exact congrFun h2 y

theorem colimit_rep_eq_of_exists [HasColimit F] {i j : J} (x : ToType (F.obj i))
    (y : ToType (F.obj j))
    (h : ‚àÉ (k : _) (f : i ‚ü∂ k) (g : j ‚ü∂ k), F.map f x = F.map g y) :
    colimit.Œπ F i x = colimit.Œπ F j y :=
  Concrete.isColimit_rep_eq_of_exists F x y h

end

section FilteredColimits

variable {C : Type u} [Category.{v} C] {FC : C ‚Üí C ‚Üí Type*} {CC : C ‚Üí Type s}
variable [‚àÄ X Y, FunLike (FC X Y) (CC X) (CC Y)] [ConcreteCategory C FC]
variable {J : Type w} [Category.{r} J] (F : J ‚•§ C) [PreservesColimit F (forget C)] [IsFiltered J]

theorem isColimit_exists_of_rep_eq {D : Cocone F} {i j : J} (hD : IsColimit D)
    (x : ToType (F.obj i)) (y : ToType (F.obj j)) (h : D.Œπ.app _ x = D.Œπ.app _ y) :
    ‚àÉ (k : _) (f : i ‚ü∂ k) (g : j ‚ü∂ k), F.map f x = F.map g y := by
  let E := (forget C).mapCocone D
  let hE : IsColimit E := isColimitOfPreserves _ hD
  exact (Types.FilteredColimit.isColimit_eq_iff (F ‚ãô forget C) hE).mp h

theorem isColimit_rep_eq_iff_exists {D : Cocone F} {i j : J} (hD : IsColimit D)
    (x : ToType (F.obj i)) (y : ToType (F.obj j)) :
    D.Œπ.app i x = D.Œπ.app j y ‚Üî ‚àÉ (k : _) (f : i ‚ü∂ k) (g : j ‚ü∂ k), F.map f x = F.map g y :=
  ‚ü®Concrete.isColimit_exists_of_rep_eq.{s} _ hD _ _,
   Concrete.isColimit_rep_eq_of_exists _ _ _‚ü©

theorem colimit_exists_of_rep_eq [HasColimit F] {i j : J} (x : ToType (F.obj i))
    (y : ToType (F.obj j)) (h : colimit.Œπ F _ x = colimit.Œπ F _ y) :
    ‚àÉ (k : _) (f : i ‚ü∂ k) (g : j ‚ü∂ k), F.map f x = F.map g y :=
  Concrete.isColimit_exists_of_rep_eq.{s} F (colimit.isColimit _) x y h

theorem colimit_rep_eq_iff_exists [HasColimit F] {i j : J} (x : ToType (F.obj i))
    (y : ToType (F.obj j)) :
    colimit.Œπ F i x = colimit.Œπ F j y ‚Üî ‚àÉ (k : _) (f : i ‚ü∂ k) (g : j ‚ü∂ k), F.map f x = F.map g y :=
  ‚ü®Concrete.colimit_exists_of_rep_eq.{s} _ _ _, Concrete.colimit_rep_eq_of_exists _ _ _‚ü©

end FilteredColimits

end Colimits

end CategoryTheory.Limits.Concrete
