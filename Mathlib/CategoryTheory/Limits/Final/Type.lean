/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Limits.Final

/-!
# Action of an initial functor on sections

Given `F : C ‚•§ D` and `P : D ‚•§ Type w`, we define a map
`sectionsPrecomp F : P.sections ‚Üí (F ‚ãô P).sections` and
show that it is a bijection when `F` is initial.
As `Functor.sections` identify to limits of functors to types
(at least under suitable universe assumptions), this could
be deduced from general results about limits and
initial functors, but we provide a more down to earth proof.

We also obtain the dual result that if `F` is final,
then `F.colimitTypePrecomp : (F ‚ãô P).ColimitType ‚Üí P.ColimitType`
is a bijection.

-/

@[expose] public section

universe w v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

namespace Functor

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]

/-- When `F : C ‚•§ D` and `P : D ‚•§ Type _`, this is the obvious map
`P.section ‚Üí (F ‚ãô P).sections`. -/
@[simps]
def sectionsPrecomp (F : C ‚•§ D) {P : D ‚•§ Type w} (x : P.sections) :
    (F ‚ãô P).sections where
  val _ := x.val _
  property _ := x.property _

lemma bijective_sectionsPrecomp (F : C ‚•§ D) (P : D ‚•§ Type w) [F.Initial] :
    Function.Bijective (F.sectionsPrecomp (P := P)) := by
  refine ‚ü®fun s‚ÇÅ s‚ÇÇ h ‚Ü¶ ?_, fun t ‚Ü¶ ?_‚ü©
  ¬∑ ext Y
    let X : CostructuredArrow F Y := Classical.arbitrary _
    have := congr_fun (congr_arg Subtype.val h) X.left
    have h‚ÇÅ := s‚ÇÅ.property X.hom
    have h‚ÇÇ := s‚ÇÇ.property X.hom
    dsimp at this h‚ÇÅ h‚ÇÇ
    rw [‚Üê h‚ÇÅ, this, h‚ÇÇ]
  ¬∑ have h (Y : D) := constant_of_preserves_morphisms'
      (fun (Z : CostructuredArrow F Y) ‚Ü¶ P.map Z.hom (t.val Z.left)) (by
          intro Z‚ÇÅ Z‚ÇÇ œÜ
          dsimp
          rw [‚Üê t.property œÜ.left]
          dsimp
          rw [‚Üê FunctorToTypes.map_comp_apply, CostructuredArrow.w])
    choose val hval using h
    refine ‚ü®‚ü®val, fun {Y‚ÇÅ Y‚ÇÇ} f ‚Ü¶ ?_‚ü©, ?_‚ü©
    ¬∑ let X : CostructuredArrow F Y‚ÇÅ := Classical.arbitrary _
      simp [‚Üê hval Y‚ÇÅ X, ‚Üê hval Y‚ÇÇ ((CostructuredArrow.map f).obj X)]
    ¬∑ ext X : 2
      simpa using (hval (F.obj X) (CostructuredArrow.mk (ùüô _))).symm

/-- Given `P : D ‚•§ Type w` and `F : C ‚•§ D`, this is the obvious map
`(F ‚ãô P).ColimitType ‚Üí P.ColimitType`. -/
def colimitTypePrecomp (F : C ‚•§ D) (P : D ‚•§ Type w) :
    (F ‚ãô P).ColimitType ‚Üí P.ColimitType :=
  (F ‚ãô P).descColimitType (P.coconeTypes.precomp F)

@[simp]
lemma colimitTypePrecomp_ŒπColimitType (F : C ‚•§ D) {P : D ‚•§ Type w}
    (i : C) (x : P.obj (F.obj i)) :
    colimitTypePrecomp F P ((F ‚ãô P).ŒπColimitType i x) = P.ŒπColimitType (F.obj i) x :=
  rfl

lemma bijective_colimitTypePrecomp (F : C ‚•§ D) (P : D ‚•§ Type w) [F.Final] :
    Function.Bijective (F.colimitTypePrecomp (P := P)) := by
  refine ‚ü®?_, fun x ‚Ü¶ ?_‚ü©
  ¬∑ have h (Y : D) := constant_of_preserves_morphisms'
      (fun (Z : StructuredArrow Y F) ‚Ü¶ (F ‚ãô P).ŒπColimitType Z.right ‚àò P.map Z.hom) (by
        intro Z‚ÇÅ Z‚ÇÇ f
        ext x
        dsimp
        rw [‚Üê (F ‚ãô P).ŒπColimitType_map f.right, comp_map,
          ‚Üê FunctorToTypes.map_comp_apply, StructuredArrow.w f])
    choose œÜ hœÜ using h
    let c : P.CoconeTypes :=
      { pt := (F ‚ãô P).ColimitType
        Œπ Y := œÜ Y
        Œπ_naturality {Y‚ÇÅ Y‚ÇÇ} f := by
          ext
          have X : StructuredArrow Y‚ÇÇ F := Classical.arbitrary _
          rw [‚Üê hœÜ Y‚ÇÇ X, ‚Üê hœÜ Y‚ÇÅ ((StructuredArrow.map f).obj X)]
          simp }
    refine Function.RightInverse.injective (g := (P.descColimitType c)) (fun x ‚Ü¶ ?_)
    obtain ‚ü®X, x, rfl‚ü© := (F ‚ãô P).ŒπColimitType_jointly_surjective x
    simp [c, ‚Üê hœÜ (F.obj X) (StructuredArrow.mk (ùüô _))]
  ¬∑ obtain ‚ü®X, x, rfl‚ü© := P.ŒπColimitType_jointly_surjective x
    let Y : StructuredArrow X F := Classical.arbitrary _
    exact ‚ü®(F ‚ãô P).ŒπColimitType Y.right (P.map Y.hom x), by simp‚ü©

end Functor

end CategoryTheory
