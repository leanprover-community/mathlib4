/-
Copyright (c) 2025 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Final

/-!
# Action of an initial functor on sections

Given `F : C â¥¤ D` and `P : D â¥¤ Type w`, we define a map
`sectionsPrecomp F : P.sections â†’ (F â‹™ P).sections` and
show that it is a bijection when `F` is initial.
As `Functor.sections` identify to limits of functors to types
(at least under suitable universe assumptions), this could
be deduced from general results about limits and
initial functors, but we provide a more down to earth proof.

We also obtain the dual result that if `F` is final,
then `F.colimitTypePrecomp : (F â‹™ P).ColimitType â†’ P.ColimitType`
is a bijection.

-/

universe w vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace CategoryTheory

namespace Functor

variable {C : Type uâ‚} {D : Type uâ‚‚} [Category.{vâ‚} C] [Category.{vâ‚‚} D]

/-- When `F : C â¥¤ D` and `P : D â¥¤ Type _`, this is the obvious map
`P.section â†’ (F â‹™ P).sections`. -/
@[simps]
def sectionsPrecomp (F : C â¥¤ D) {P : D â¥¤ Type w} (x : P.sections) :
    (F â‹™ P).sections where
  val _ := x.val _
  property _ := x.property _

lemma bijective_sectionsPrecomp (F : C â¥¤ D) (P : D â¥¤ Type w) [F.Initial] :
    Function.Bijective (F.sectionsPrecomp (P := P)) := by
  refine âŸ¨fun sâ‚ sâ‚‚ h â†¦ ?_, fun t â†¦ ?_âŸ©
  Â· ext Y
    let X : CostructuredArrow F Y := Classical.arbitrary _
    have := congr_fun (congr_arg Subtype.val h) X.left
    have hâ‚ := sâ‚.property X.hom
    have hâ‚‚ := sâ‚‚.property X.hom
    dsimp at this hâ‚ hâ‚‚
    rw [â† hâ‚, this, hâ‚‚]
  Â· have h (Y : D) := constant_of_preserves_morphisms'
      (fun (Z : CostructuredArrow F Y) â†¦ P.map Z.hom (t.val Z.left)) (by
          intro Zâ‚ Zâ‚‚ Ï†
          dsimp
          rw [â† t.property Ï†.left]
          dsimp
          rw [â† FunctorToTypes.map_comp_apply, CostructuredArrow.w])
    choose val hval using h
    refine âŸ¨âŸ¨val, fun {Yâ‚ Yâ‚‚} f â†¦ ?_âŸ©, ?_âŸ©
    Â· let X : CostructuredArrow F Yâ‚ := Classical.arbitrary _
      simp [â† hval Yâ‚ X, â† hval Yâ‚‚ ((CostructuredArrow.map f).obj X)]
    Â· ext X : 2
      simpa using (hval (F.obj X) (CostructuredArrow.mk (ğŸ™ _))).symm

/-- Given `P : D â¥¤ Type w` and `F : C â¥¤ D`, this is the obvious map
`(F â‹™ P).ColimitType â†’ P.ColimitType`. -/
def colimitTypePrecomp (F : C â¥¤ D) (P : D â¥¤ Type w) :
    (F â‹™ P).ColimitType â†’ P.ColimitType :=
  (F â‹™ P).descColimitType (P.coconeTypes.precomp F)

@[simp]
lemma colimitTypePrecomp_Î¹ColimitType (F : C â¥¤ D) {P : D â¥¤ Type w}
    (i : C) (x : P.obj (F.obj i)) :
    colimitTypePrecomp F P ((F â‹™ P).Î¹ColimitType i x) = P.Î¹ColimitType (F.obj i) x :=
  rfl

lemma bijective_colimitTypePrecomp (F : C â¥¤ D) (P : D â¥¤ Type w) [F.Final] :
    Function.Bijective (F.colimitTypePrecomp (P := P)) := by
  refine âŸ¨?_, fun x â†¦ ?_âŸ©
  Â· have h (Y : D) := constant_of_preserves_morphisms' (Î± := P.obj Y â†’ (F â‹™ P).ColimitType)
      (fun (Z : StructuredArrow Y F) â†¦ (F â‹™ P).Î¹ColimitType Z.right âˆ˜ P.map Z.hom) (by
        intro Zâ‚ Zâ‚‚ f
        ext x
        dsimp
        rw [â† (F â‹™ P).Î¹ColimitType_map f.right, comp_map,
          â† FunctorToTypes.map_comp_apply, StructuredArrow.w f])
    choose Ï† hÏ† using h
    let c : P.CoconeTypes :=
      { pt := (F â‹™ P).ColimitType
        Î¹ Y := Ï† Y
        Î¹_naturality {Yâ‚ Yâ‚‚} f := by
          ext
          have X : StructuredArrow Yâ‚‚ F := Classical.arbitrary _
          rw [â† hÏ† Yâ‚‚ X, â† hÏ† Yâ‚ ((StructuredArrow.map f).obj X)]
          simp }
    refine Function.RightInverse.injective (g := (P.descColimitType c)) (fun x â†¦ ?_)
    obtain âŸ¨X, x, rflâŸ© := (F â‹™ P).Î¹ColimitType_jointly_surjective x
    simp [c, â† hÏ† (F.obj X) (StructuredArrow.mk (ğŸ™ _))]
  Â· obtain âŸ¨X, x, rflâŸ© := P.Î¹ColimitType_jointly_surjective x
    let Y : StructuredArrow X F := Classical.arbitrary _
    exact âŸ¨(F â‹™ P).Î¹ColimitType Y.right (P.map Y.hom x), by simpâŸ©

end Functor

end CategoryTheory
