/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Mario Carneiro, Scott Morrison, Floris van Doorn
-/
import Mathlib.CategoryTheory.Limits.IsLimit
import Mathlib.CategoryTheory.Category.ULift
import Mathlib.CategoryTheory.EssentiallySmall
import Mathlib.Logic.Equiv.Basic

#align_import category_theory.limits.has_limits from "leanprover-community/mathlib"@"2738d2ca56cbc63be80c3bd48e9ed90ad94e947d"

/-!
# Existence of limits and colimits

In `CategoryTheory.Limits.IsLimit` we defined `IsLimit c`,
the data showing that a cone `c` is a limit cone.

The two main structures defined in this file are:
* `LimitCone F`, which consists of a choice of cone for `F` and the fact it is a limit cone, and
* `HasLimit F`, asserting the mere existence of some limit cone for `F`.

`HasLimit` is a propositional typeclass
(it's important that it is a proposition merely asserting the existence of a limit,
as otherwise we would have non-defeq problems from incompatible instances).

While `HasLimit` only asserts the existence of a limit cone,
we happily use the axiom of choice in mathlib,
so there are convenience functions all depending on `HasLimit F`:
* `limit F : C`, producing some limit object (of course all such are isomorphic)
* `limit.œÄ F j : limit F ‚ü∂ F.obj j`, the morphisms out of the limit,
* `limit.lift F c : c.pt ‚ü∂ limit F`, the universal morphism from any other `c : Cone F`, etc.

Key to using the `HasLimit` interface is that there is an `@[ext]` lemma stating that
to check `f = g`, for `f g : Z ‚ü∂ limit F`, it suffices to check `f ‚â´ limit.œÄ F j = g ‚â´ limit.œÄ F j`
for every `j`.
This, combined with `@[simp]` lemmas, makes it possible to prove many easy facts about limits using
automation (e.g. `tidy`).

There are abbreviations `HasLimitsOfShape J C` and `HasLimits C`
asserting the existence of classes of limits.
Later more are introduced, for finite limits, special shapes of limits, etc.

Ideally, many results about limits should be stated first in terms of `IsLimit`,
and then a result in terms of `HasLimit` derived from this.
At this point, however, this is far from uniformly achieved in mathlib ---
often statements are only written in terms of `HasLimit`.

## Implementation
At present we simply say everything twice, in order to handle both limits and colimits.
It would be highly desirable to have some automation support,
e.g. a `@[dualize]` attribute that behaves similarly to `@[to_additive]`.

## References
* [Stacks: Limits and colimits](https://stacks.math.columbia.edu/tag/002D)

-/


noncomputable section

open CategoryTheory CategoryTheory.Category CategoryTheory.Functor Opposite

namespace CategoryTheory.Limits

-- morphism levels before object levels. See note [CategoryTheory universes].
universe v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ v‚ÇÉ u‚ÇÉ v v' v'' u u' u''

variable {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J] {K : Type u‚ÇÇ} [Category.{v‚ÇÇ} K]
variable {C : Type u} [Category.{v} C]
variable {F : J ‚•§ C}

section Limit

/-- `LimitCone F` contains a cone over `F` together with the information that it is a limit. -/
-- @[nolint has_nonempty_instance] -- Porting note(#5171): removed; linter not ported yet
structure LimitCone (F : J ‚•§ C) where
  /-- The cone itself -/
  cone : Cone F
  /-- The proof that is the limit cone -/
  isLimit : IsLimit cone
#align category_theory.limits.limit_cone CategoryTheory.Limits.LimitCone
#align category_theory.limits.limit_cone.is_limit CategoryTheory.Limits.LimitCone.isLimit

/-- `HasLimit F` represents the mere existence of a limit for `F`. -/
class HasLimit (F : J ‚•§ C) : Prop where mk' ::
  /-- There is some limit cone for `F` -/
  exists_limit : Nonempty (LimitCone F)
#align category_theory.limits.has_limit CategoryTheory.Limits.HasLimit

theorem HasLimit.mk {F : J ‚•§ C} (d : LimitCone F) : HasLimit F :=
  ‚ü®Nonempty.intro d‚ü©
#align category_theory.limits.has_limit.mk CategoryTheory.Limits.HasLimit.mk

/-- Use the axiom of choice to extract explicit `LimitCone F` from `HasLimit F`. -/
def getLimitCone (F : J ‚•§ C) [HasLimit F] : LimitCone F :=
  Classical.choice <| HasLimit.exists_limit
#align category_theory.limits.get_limit_cone CategoryTheory.Limits.getLimitCone

variable (J C)

/-- `C` has limits of shape `J` if there exists a limit for every functor `F : J ‚•§ C`. -/
class HasLimitsOfShape : Prop where
  /-- All functors `F : J ‚•§ C` from `J` have limits -/
  has_limit : ‚àÄ F : J ‚•§ C, HasLimit F := by infer_instance
#align category_theory.limits.has_limits_of_shape CategoryTheory.Limits.HasLimitsOfShape

/-- `C` has all limits of size `v‚ÇÅ u‚ÇÅ` (`HasLimitsOfSize.{v‚ÇÅ u‚ÇÅ} C`)
if it has limits of every shape `J : Type u‚ÇÅ` with `[Category.{v‚ÇÅ} J]`.
-/
@[pp_with_univ]
class HasLimitsOfSize (C : Type u) [Category.{v} C] : Prop where
  /-- All functors `F : J ‚•§ C` from all small `J` have limits -/
  has_limits_of_shape : ‚àÄ (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J], HasLimitsOfShape J C := by
    infer_instance
#align category_theory.limits.has_limits_of_size CategoryTheory.Limits.HasLimitsOfSize

/-- `C` has all (small) limits if it has limits of every shape that is as big as its hom-sets. -/
abbrev HasLimits (C : Type u) [Category.{v} C] : Prop :=
  HasLimitsOfSize.{v, v} C
#align category_theory.limits.has_limits CategoryTheory.Limits.HasLimits

theorem HasLimits.has_limits_of_shape {C : Type u} [Category.{v} C] [HasLimits C] (J : Type v)
    [Category.{v} J] : HasLimitsOfShape J C :=
  HasLimitsOfSize.has_limits_of_shape J
#align category_theory.limits.has_limits.has_limits_of_shape CategoryTheory.Limits.HasLimits.has_limits_of_shape

variable {J C}

-- see Note [lower instance priority]
instance (priority := 100) hasLimitOfHasLimitsOfShape {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]
    [HasLimitsOfShape J C] (F : J ‚•§ C) : HasLimit F :=
  HasLimitsOfShape.has_limit F
#align category_theory.limits.has_limit_of_has_limits_of_shape CategoryTheory.Limits.hasLimitOfHasLimitsOfShape

-- see Note [lower instance priority]
instance (priority := 100) hasLimitsOfShapeOfHasLimits {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]
    [HasLimitsOfSize.{v‚ÇÅ, u‚ÇÅ} C] : HasLimitsOfShape J C :=
  HasLimitsOfSize.has_limits_of_shape J
#align category_theory.limits.has_limits_of_shape_of_has_limits CategoryTheory.Limits.hasLimitsOfShapeOfHasLimits

-- Interface to the `HasLimit` class.
/-- An arbitrary choice of limit cone for a functor. -/
def limit.cone (F : J ‚•§ C) [HasLimit F] : Cone F :=
  (getLimitCone F).cone
#align category_theory.limits.limit.cone CategoryTheory.Limits.limit.cone

/-- An arbitrary choice of limit object of a functor. -/
def limit (F : J ‚•§ C) [HasLimit F] :=
  (limit.cone F).pt
#align category_theory.limits.limit CategoryTheory.Limits.limit

/-- The projection from the limit object to a value of the functor. -/
def limit.œÄ (F : J ‚•§ C) [HasLimit F] (j : J) : limit F ‚ü∂ F.obj j :=
  (limit.cone F).œÄ.app j
#align category_theory.limits.limit.œÄ CategoryTheory.Limits.limit.œÄ

@[simp]
theorem limit.cone_x {F : J ‚•§ C} [HasLimit F] : (limit.cone F).pt = limit F :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.limits.limit.cone_X CategoryTheory.Limits.limit.cone_x

@[simp]
theorem limit.cone_œÄ {F : J ‚•§ C} [HasLimit F] : (limit.cone F).œÄ.app = limit.œÄ _ :=
  rfl
#align category_theory.limits.limit.cone_œÄ CategoryTheory.Limits.limit.cone_œÄ

@[reassoc (attr := simp)]
theorem limit.w (F : J ‚•§ C) [HasLimit F] {j j' : J} (f : j ‚ü∂ j') :
    limit.œÄ F j ‚â´ F.map f = limit.œÄ F j' :=
  (limit.cone F).w f
#align category_theory.limits.limit.w CategoryTheory.Limits.limit.w

/-- Evidence that the arbitrary choice of cone provided by `limit.cone F` is a limit cone. -/
def limit.isLimit (F : J ‚•§ C) [HasLimit F] : IsLimit (limit.cone F) :=
  (getLimitCone F).isLimit
#align category_theory.limits.limit.is_limit CategoryTheory.Limits.limit.isLimit

/-- The morphism from the cone point of any other cone to the limit object. -/
def limit.lift (F : J ‚•§ C) [HasLimit F] (c : Cone F) : c.pt ‚ü∂ limit F :=
  (limit.isLimit F).lift c
#align category_theory.limits.limit.lift CategoryTheory.Limits.limit.lift

@[simp]
theorem limit.isLimit_lift {F : J ‚•§ C} [HasLimit F] (c : Cone F) :
    (limit.isLimit F).lift c = limit.lift F c :=
  rfl
#align category_theory.limits.limit.is_limit_lift CategoryTheory.Limits.limit.isLimit_lift

@[reassoc (attr := simp)]
theorem limit.lift_œÄ {F : J ‚•§ C} [HasLimit F] (c : Cone F) (j : J) :
    limit.lift F c ‚â´ limit.œÄ F j = c.œÄ.app j :=
  IsLimit.fac _ c j
#align category_theory.limits.limit.lift_œÄ CategoryTheory.Limits.limit.lift_œÄ

/-- Functoriality of limits.

Usually this morphism should be accessed through `lim.map`,
but may be needed separately when you have specified limits for the source and target functors,
but not necessarily for all functors of shape `J`.
-/
def limMap {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (Œ± : F ‚ü∂ G) : limit F ‚ü∂ limit G :=
  IsLimit.map _ (limit.isLimit G) Œ±
#align category_theory.limits.lim_map CategoryTheory.Limits.limMap

@[reassoc (attr := simp)]
theorem limMap_œÄ {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (Œ± : F ‚ü∂ G) (j : J) :
    limMap Œ± ‚â´ limit.œÄ G j = limit.œÄ F j ‚â´ Œ±.app j :=
  limit.lift_œÄ _ j
#align category_theory.limits.lim_map_œÄ CategoryTheory.Limits.limMap_œÄ

/-- The cone morphism from any cone to the arbitrary choice of limit cone. -/
def limit.coneMorphism {F : J ‚•§ C} [HasLimit F] (c : Cone F) : c ‚ü∂ limit.cone F :=
  (limit.isLimit F).liftConeMorphism c
#align category_theory.limits.limit.cone_morphism CategoryTheory.Limits.limit.coneMorphism

@[simp]
theorem limit.coneMorphism_hom {F : J ‚•§ C} [HasLimit F] (c : Cone F) :
    (limit.coneMorphism c).hom = limit.lift F c :=
  rfl
#align category_theory.limits.limit.cone_morphism_hom CategoryTheory.Limits.limit.coneMorphism_hom

theorem limit.coneMorphism_œÄ {F : J ‚•§ C} [HasLimit F] (c : Cone F) (j : J) :
    (limit.coneMorphism c).hom ‚â´ limit.œÄ F j = c.œÄ.app j := by simp
#align category_theory.limits.limit.cone_morphism_œÄ CategoryTheory.Limits.limit.coneMorphism_œÄ

@[reassoc (attr := simp)]
theorem limit.conePointUniqueUpToIso_hom_comp {F : J ‚•§ C} [HasLimit F] {c : Cone F} (hc : IsLimit c)
    (j : J) : (IsLimit.conePointUniqueUpToIso hc (limit.isLimit _)).hom ‚â´ limit.œÄ F j = c.œÄ.app j :=
  IsLimit.conePointUniqueUpToIso_hom_comp _ _ _
#align category_theory.limits.limit.cone_point_unique_up_to_iso_hom_comp CategoryTheory.Limits.limit.conePointUniqueUpToIso_hom_comp

@[reassoc (attr := simp)]
theorem limit.conePointUniqueUpToIso_inv_comp {F : J ‚•§ C} [HasLimit F] {c : Cone F} (hc : IsLimit c)
    (j : J) : (IsLimit.conePointUniqueUpToIso (limit.isLimit _) hc).inv ‚â´ limit.œÄ F j = c.œÄ.app j :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ _ _
#align category_theory.limits.limit.cone_point_unique_up_to_iso_inv_comp CategoryTheory.Limits.limit.conePointUniqueUpToIso_inv_comp

theorem limit.existsUnique {F : J ‚•§ C} [HasLimit F] (t : Cone F) :
    ‚àÉ! l : t.pt ‚ü∂ limit F, ‚àÄ j, l ‚â´ limit.œÄ F j = t.œÄ.app j :=
  (limit.isLimit F).existsUnique _
#align category_theory.limits.limit.exists_unique CategoryTheory.Limits.limit.existsUnique

/-- Given any other limit cone for `F`, the chosen `limit F` is isomorphic to the cone point.
-/
def limit.isoLimitCone {F : J ‚•§ C} [HasLimit F] (t : LimitCone F) : limit F ‚âÖ t.cone.pt :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit F) t.isLimit
#align category_theory.limits.limit.iso_limit_cone CategoryTheory.Limits.limit.isoLimitCone

@[reassoc (attr := simp)]
theorem limit.isoLimitCone_hom_œÄ {F : J ‚•§ C} [HasLimit F] (t : LimitCone F) (j : J) :
    (limit.isoLimitCone t).hom ‚â´ t.cone.œÄ.app j = limit.œÄ F j := by
  dsimp [limit.isoLimitCone, IsLimit.conePointUniqueUpToIso]
  aesop_cat
#align category_theory.limits.limit.iso_limit_cone_hom_œÄ CategoryTheory.Limits.limit.isoLimitCone_hom_œÄ

@[reassoc (attr := simp)]
theorem limit.isoLimitCone_inv_œÄ {F : J ‚•§ C} [HasLimit F] (t : LimitCone F) (j : J) :
    (limit.isoLimitCone t).inv ‚â´ limit.œÄ F j = t.cone.œÄ.app j := by
  dsimp [limit.isoLimitCone, IsLimit.conePointUniqueUpToIso]
  aesop_cat
#align category_theory.limits.limit.iso_limit_cone_inv_œÄ CategoryTheory.Limits.limit.isoLimitCone_inv_œÄ

@[ext]
theorem limit.hom_ext {F : J ‚•§ C} [HasLimit F] {X : C} {f f' : X ‚ü∂ limit F}
    (w : ‚àÄ j, f ‚â´ limit.œÄ F j = f' ‚â´ limit.œÄ F j) : f = f' :=
  (limit.isLimit F).hom_ext w
#align category_theory.limits.limit.hom_ext CategoryTheory.Limits.limit.hom_ext

@[simp]
theorem limit.lift_map {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (c : Cone F) (Œ± : F ‚ü∂ G) :
    limit.lift F c ‚â´ limMap Œ± = limit.lift G ((Cones.postcompose Œ±).obj c) := by
  ext
  rw [assoc, limMap_œÄ, limit.lift_œÄ_assoc, limit.lift_œÄ]
  rfl
#align category_theory.limits.limit.lift_map CategoryTheory.Limits.limit.lift_map

@[simp]
theorem limit.lift_cone {F : J ‚•§ C} [HasLimit F] : limit.lift F (limit.cone F) = ùüô (limit F) :=
  (limit.isLimit _).lift_self
#align category_theory.limits.limit.lift_cone CategoryTheory.Limits.limit.lift_cone

/-- The isomorphism (in `Type`) between
morphisms from a specified object `W` to the limit object,
and cones with cone point `W`.
-/
def limit.homIso (F : J ‚•§ C) [HasLimit F] (W : C) :
    ULift.{u‚ÇÅ} (W ‚ü∂ limit F : Type v) ‚âÖ F.cones.obj (op W) :=
  (limit.isLimit F).homIso W
#align category_theory.limits.limit.hom_iso CategoryTheory.Limits.limit.homIso

@[simp]
theorem limit.homIso_hom (F : J ‚•§ C) [HasLimit F] {W : C} (f : ULift (W ‚ü∂ limit F)) :
    (limit.homIso F W).hom f = (const J).map f.down ‚â´ (limit.cone F).œÄ :=
  (limit.isLimit F).homIso_hom f
#align category_theory.limits.limit.hom_iso_hom CategoryTheory.Limits.limit.homIso_hom

/-- The isomorphism (in `Type`) between
morphisms from a specified object `W` to the limit object,
and an explicit componentwise description of cones with cone point `W`.
-/
def limit.homIso' (F : J ‚•§ C) [HasLimit F] (W : C) :
    ULift.{u‚ÇÅ} (W ‚ü∂ limit F : Type v) ‚âÖ
      { p : ‚àÄ j, W ‚ü∂ F.obj j // ‚àÄ {j j' : J} (f : j ‚ü∂ j'), p j ‚â´ F.map f = p j' } :=
  (limit.isLimit F).homIso' W
#align category_theory.limits.limit.hom_iso' CategoryTheory.Limits.limit.homIso'

theorem limit.lift_extend {F : J ‚•§ C} [HasLimit F] (c : Cone F) {X : C} (f : X ‚ü∂ c.pt) :
    limit.lift F (c.extend f) = f ‚â´ limit.lift F c := by aesop_cat
#align category_theory.limits.limit.lift_extend CategoryTheory.Limits.limit.lift_extend

/-- If a functor `F` has a limit, so does any naturally isomorphic functor.
-/
theorem hasLimitOfIso {F G : J ‚•§ C} [HasLimit F] (Œ± : F ‚âÖ G) : HasLimit G :=
  HasLimit.mk
    { cone := (Cones.postcompose Œ±.hom).obj (limit.cone F)
      isLimit := (IsLimit.postcomposeHomEquiv _ _).symm (limit.isLimit F) }
#align category_theory.limits.has_limit_of_iso CategoryTheory.Limits.hasLimitOfIso

-- See the construction of limits from products and equalizers
-- for an example usage.
/-- If a functor `G` has the same collection of cones as a functor `F`
which has a limit, then `G` also has a limit. -/
theorem HasLimit.ofConesIso {J K : Type u‚ÇÅ} [Category.{v‚ÇÅ} J] [Category.{v‚ÇÇ} K] (F : J ‚•§ C)
    (G : K ‚•§ C) (h : F.cones ‚âÖ G.cones) [HasLimit F] : HasLimit G :=
  HasLimit.mk ‚ü®_, IsLimit.ofNatIso (IsLimit.natIso (limit.isLimit F) ‚â™‚â´ h)‚ü©
#align category_theory.limits.has_limit.of_cones_iso CategoryTheory.Limits.HasLimit.ofConesIso

/-- The limits of `F : J ‚•§ C` and `G : J ‚•§ C` are isomorphic,
if the functors are naturally isomorphic.
-/
def HasLimit.isoOfNatIso {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (w : F ‚âÖ G) : limit F ‚âÖ limit G :=
  IsLimit.conePointsIsoOfNatIso (limit.isLimit F) (limit.isLimit G) w
#align category_theory.limits.has_limit.iso_of_nat_iso CategoryTheory.Limits.HasLimit.isoOfNatIso

@[reassoc (attr := simp)]
theorem HasLimit.isoOfNatIso_hom_œÄ {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (w : F ‚âÖ G) (j : J) :
    (HasLimit.isoOfNatIso w).hom ‚â´ limit.œÄ G j = limit.œÄ F j ‚â´ w.hom.app j :=
  IsLimit.conePointsIsoOfNatIso_hom_comp _ _ _ _
#align category_theory.limits.has_limit.iso_of_nat_iso_hom_œÄ CategoryTheory.Limits.HasLimit.isoOfNatIso_hom_œÄ

@[reassoc (attr := simp)]
theorem HasLimit.isoOfNatIso_inv_œÄ {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (w : F ‚âÖ G) (j : J) :
    (HasLimit.isoOfNatIso w).inv ‚â´ limit.œÄ F j = limit.œÄ G j ‚â´ w.inv.app j :=
  IsLimit.conePointsIsoOfNatIso_inv_comp _ _ _ _
#align category_theory.limits.has_limit.iso_of_nat_iso_inv_œÄ CategoryTheory.Limits.HasLimit.isoOfNatIso_inv_œÄ

@[reassoc (attr := simp)]
theorem HasLimit.lift_isoOfNatIso_hom {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (t : Cone F)
    (w : F ‚âÖ G) :
    limit.lift F t ‚â´ (HasLimit.isoOfNatIso w).hom =
      limit.lift G ((Cones.postcompose w.hom).obj _) :=
  IsLimit.lift_comp_conePointsIsoOfNatIso_hom _ _ _
#align category_theory.limits.has_limit.lift_iso_of_nat_iso_hom CategoryTheory.Limits.HasLimit.lift_isoOfNatIso_hom

@[reassoc (attr := simp)]
theorem HasLimit.lift_isoOfNatIso_inv {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (t : Cone G)
    (w : F ‚âÖ G) :
    limit.lift G t ‚â´ (HasLimit.isoOfNatIso w).inv =
      limit.lift F ((Cones.postcompose w.inv).obj _) :=
  IsLimit.lift_comp_conePointsIsoOfNatIso_inv _ _ _
#align category_theory.limits.has_limit.lift_iso_of_nat_iso_inv CategoryTheory.Limits.HasLimit.lift_isoOfNatIso_inv

/-- The limits of `F : J ‚•§ C` and `G : K ‚•§ C` are isomorphic,
if there is an equivalence `e : J ‚âå K` making the triangle commute up to natural isomorphism.
-/
def HasLimit.isoOfEquivalence {F : J ‚•§ C} [HasLimit F] {G : K ‚•§ C} [HasLimit G] (e : J ‚âå K)
    (w : e.functor ‚ãô G ‚âÖ F) : limit F ‚âÖ limit G :=
  IsLimit.conePointsIsoOfEquivalence (limit.isLimit F) (limit.isLimit G) e w
#align category_theory.limits.has_limit.iso_of_equivalence CategoryTheory.Limits.HasLimit.isoOfEquivalence

@[simp]
theorem HasLimit.isoOfEquivalence_hom_œÄ {F : J ‚•§ C} [HasLimit F] {G : K ‚•§ C} [HasLimit G]
    (e : J ‚âå K) (w : e.functor ‚ãô G ‚âÖ F) (k : K) :
    (HasLimit.isoOfEquivalence e w).hom ‚â´ limit.œÄ G k =
      limit.œÄ F (e.inverse.obj k) ‚â´ w.inv.app (e.inverse.obj k) ‚â´ G.map (e.counit.app k) := by
  simp only [HasLimit.isoOfEquivalence, IsLimit.conePointsIsoOfEquivalence_hom]
  dsimp
  simp
#align category_theory.limits.has_limit.iso_of_equivalence_hom_œÄ CategoryTheory.Limits.HasLimit.isoOfEquivalence_hom_œÄ

@[simp]
theorem HasLimit.isoOfEquivalence_inv_œÄ {F : J ‚•§ C} [HasLimit F] {G : K ‚•§ C} [HasLimit G]
    (e : J ‚âå K) (w : e.functor ‚ãô G ‚âÖ F) (j : J) :
    (HasLimit.isoOfEquivalence e w).inv ‚â´ limit.œÄ F j =
    limit.œÄ G (e.functor.obj j) ‚â´ w.hom.app j := by
  simp only [HasLimit.isoOfEquivalence, IsLimit.conePointsIsoOfEquivalence_hom]
  dsimp
  simp
#align category_theory.limits.has_limit.iso_of_equivalence_inv_œÄ CategoryTheory.Limits.HasLimit.isoOfEquivalence_inv_œÄ

section Pre

variable (F)
variable [HasLimit F] (E : K ‚•§ J) [HasLimit (E ‚ãô F)]

/-- The canonical morphism from the limit of `F` to the limit of `E ‚ãô F`.
-/
def limit.pre : limit F ‚ü∂ limit (E ‚ãô F) :=
  limit.lift (E ‚ãô F) ((limit.cone F).whisker E)
#align category_theory.limits.limit.pre CategoryTheory.Limits.limit.pre

@[reassoc (attr := simp)]
theorem limit.pre_œÄ (k : K) : limit.pre F E ‚â´ limit.œÄ (E ‚ãô F) k = limit.œÄ F (E.obj k) := by
  erw [IsLimit.fac]
  rfl
#align category_theory.limits.limit.pre_œÄ CategoryTheory.Limits.limit.pre_œÄ

@[simp]
theorem limit.lift_pre (c : Cone F) :
    limit.lift F c ‚â´ limit.pre F E = limit.lift (E ‚ãô F) (c.whisker E) := by ext; simp
#align category_theory.limits.limit.lift_pre CategoryTheory.Limits.limit.lift_pre

variable {L : Type u‚ÇÉ} [Category.{v‚ÇÉ} L]
variable (D : L ‚•§ K) [HasLimit (D ‚ãô E ‚ãô F)]

@[simp]
theorem limit.pre_pre [h : HasLimit (D ‚ãô E ‚ãô F)] : haveI : HasLimit ((D ‚ãô E) ‚ãô F) := h;
    limit.pre F E ‚â´ limit.pre (E ‚ãô F) D = limit.pre F (D ‚ãô E) := by
  haveI : HasLimit ((D ‚ãô E) ‚ãô F) := h
  ext j; erw [assoc, limit.pre_œÄ, limit.pre_œÄ, limit.pre_œÄ]; rfl
#align category_theory.limits.limit.pre_pre CategoryTheory.Limits.limit.pre_pre

variable {E F}

/-- -
If we have particular limit cones available for `E ‚ãô F` and for `F`,
we obtain a formula for `limit.pre F E`.
-/
theorem limit.pre_eq (s : LimitCone (E ‚ãô F)) (t : LimitCone F) :
    limit.pre F E = (limit.isoLimitCone t).hom ‚â´ s.isLimit.lift (t.cone.whisker E) ‚â´
      (limit.isoLimitCone s).inv := by aesop_cat
#align category_theory.limits.limit.pre_eq CategoryTheory.Limits.limit.pre_eq

end Pre

section Post

variable {D : Type u'} [Category.{v'} D]
variable (F : J ‚•§ C) [HasLimit F] (G : C ‚•§ D) [HasLimit (F ‚ãô G)]

/-- The canonical morphism from `G` applied to the limit of `F` to the limit of `F ‚ãô G`.
-/
def limit.post : G.obj (limit F) ‚ü∂ limit (F ‚ãô G) :=
  limit.lift (F ‚ãô G) (G.mapCone (limit.cone F))
#align category_theory.limits.limit.post CategoryTheory.Limits.limit.post

@[reassoc (attr := simp)]
theorem limit.post_œÄ (j : J) : limit.post F G ‚â´ limit.œÄ (F ‚ãô G) j = G.map (limit.œÄ F j) := by
  erw [IsLimit.fac]
  rfl
#align category_theory.limits.limit.post_œÄ CategoryTheory.Limits.limit.post_œÄ

@[simp]
theorem limit.lift_post (c : Cone F) :
    G.map (limit.lift F c) ‚â´ limit.post F G = limit.lift (F ‚ãô G) (G.mapCone c) := by
  ext
  rw [assoc, limit.post_œÄ, ‚Üê G.map_comp, limit.lift_œÄ, limit.lift_œÄ]
  rfl
#align category_theory.limits.limit.lift_post CategoryTheory.Limits.limit.lift_post

@[simp]
theorem limit.post_post {E : Type u''} [Category.{v''} E] (H : D ‚•§ E) [h : HasLimit ((F ‚ãô G) ‚ãô H)] :
    -- H G (limit F) ‚ü∂ H (limit (F ‚ãô G)) ‚ü∂ limit ((F ‚ãô G) ‚ãô H) equals
    -- H G (limit F) ‚ü∂ limit (F ‚ãô (G ‚ãô H))
    haveI : HasLimit (F ‚ãô G ‚ãô H) := h
    H.map (limit.post F G) ‚â´ limit.post (F ‚ãô G) H = limit.post F (G ‚ãô H) := by
  haveI : HasLimit (F ‚ãô G ‚ãô H) := h
  ext; erw [assoc, limit.post_œÄ, ‚Üê H.map_comp, limit.post_œÄ, limit.post_œÄ]; rfl
#align category_theory.limits.limit.post_post CategoryTheory.Limits.limit.post_post

end Post

theorem limit.pre_post {D : Type u'} [Category.{v'} D] (E : K ‚•§ J) (F : J ‚•§ C) (G : C ‚•§ D)
    [HasLimit F] [HasLimit (E ‚ãô F)] [HasLimit (F ‚ãô G)]
    [h : HasLimit ((E ‚ãô F) ‚ãô G)] :-- G (limit F) ‚ü∂ G (limit (E ‚ãô F)) ‚ü∂ limit ((E ‚ãô F) ‚ãô G) vs
            -- G (limit F) ‚ü∂ limit F ‚ãô G ‚ü∂ limit (E ‚ãô (F ‚ãô G)) or
    haveI : HasLimit (E ‚ãô F ‚ãô G) := h
    G.map (limit.pre F E) ‚â´ limit.post (E ‚ãô F) G = limit.post F G ‚â´ limit.pre (F ‚ãô G) E := by
  haveI : HasLimit (E ‚ãô F ‚ãô G) := h
  ext; erw [assoc, limit.post_œÄ, ‚Üê G.map_comp, limit.pre_œÄ, assoc, limit.pre_œÄ, limit.post_œÄ]
#align category_theory.limits.limit.pre_post CategoryTheory.Limits.limit.pre_post

open CategoryTheory.Equivalence

instance hasLimitEquivalenceComp (e : K ‚âå J) [HasLimit F] : HasLimit (e.functor ‚ãô F) :=
  HasLimit.mk
    { cone := Cone.whisker e.functor (limit.cone F)
      isLimit := IsLimit.whiskerEquivalence (limit.isLimit F) e }
#align category_theory.limits.has_limit_equivalence_comp CategoryTheory.Limits.hasLimitEquivalenceComp

-- Porting note: testing whether this still needed
-- attribute [local elab_without_expected_type] inv_fun_id_assoc

-- not entirely sure why this is needed
/-- If a `E ‚ãô F` has a limit, and `E` is an equivalence, we can construct a limit of `F`.
-/
theorem hasLimitOfEquivalenceComp (e : K ‚âå J) [HasLimit (e.functor ‚ãô F)] : HasLimit F := by
  haveI : HasLimit (e.inverse ‚ãô e.functor ‚ãô F) := Limits.hasLimitEquivalenceComp e.symm
  apply hasLimitOfIso (e.invFunIdAssoc F)
#align category_theory.limits.has_limit_of_equivalence_comp CategoryTheory.Limits.hasLimitOfEquivalenceComp

-- `hasLimitCompEquivalence` and `hasLimitOfCompEquivalence`
-- are proved in `CategoryTheory/Adjunction/Limits.lean`.
section LimFunctor

variable [HasLimitsOfShape J C]

section

/-- `limit F` is functorial in `F`, when `C` has all limits of shape `J`. -/
@[simps]
def lim : (J ‚•§ C) ‚•§ C where
  obj F := limit F
  map Œ± := limMap Œ±
  map_id F := by
    apply Limits.limit.hom_ext; intro j
    erw [limMap_œÄ, Category.id_comp, Category.comp_id]
  map_comp Œ± Œ≤ := by
    apply Limits.limit.hom_ext; intro j
    erw [assoc, IsLimit.fac, IsLimit.fac, ‚Üê assoc, IsLimit.fac, assoc]; rfl
#align category_theory.limits.lim CategoryTheory.Limits.lim
#align category_theory.limits.lim_map_eq_lim_map CategoryTheory.Limits.lim_map

end

variable {G : J ‚•§ C} (Œ± : F ‚ü∂ G)

theorem limit.map_pre [HasLimitsOfShape K C] (E : K ‚•§ J) :
    lim.map Œ± ‚â´ limit.pre G E = limit.pre F E ‚â´ lim.map (whiskerLeft E Œ±) := by
  ext
  simp
#align category_theory.limits.limit.map_pre CategoryTheory.Limits.limit.map_pre

theorem limit.map_pre' [HasLimitsOfShape K C] (F : J ‚•§ C) {E‚ÇÅ E‚ÇÇ : K ‚•§ J} (Œ± : E‚ÇÅ ‚ü∂ E‚ÇÇ) :
    limit.pre F E‚ÇÇ = limit.pre F E‚ÇÅ ‚â´ lim.map (whiskerRight Œ± F) := by
  ext1; simp [‚Üê category.assoc]
#align category_theory.limits.limit.map_pre' CategoryTheory.Limits.limit.map_pre'

theorem limit.id_pre (F : J ‚•§ C) : limit.pre F (ùü≠ _) = lim.map (Functor.leftUnitor F).inv := by
  aesop_cat
#align category_theory.limits.limit.id_pre CategoryTheory.Limits.limit.id_pre

theorem limit.map_post {D : Type u'} [Category.{v'} D] [HasLimitsOfShape J D] (H : C ‚•§ D) :
    /- H (limit F) ‚ü∂ H (limit G) ‚ü∂ limit (G ‚ãô H) vs
     H (limit F) ‚ü∂ limit (F ‚ãô H) ‚ü∂ limit (G ‚ãô H) -/
    H.map (limMap Œ±) ‚â´ limit.post G H = limit.post F H ‚â´ limMap (whiskerRight Œ± H) := by
  ext
  simp only [whiskerRight_app, limMap_œÄ, assoc, limit.post_œÄ_assoc, limit.post_œÄ, ‚Üê H.map_comp]
#align category_theory.limits.limit.map_post CategoryTheory.Limits.limit.map_post

/-- The isomorphism between
morphisms from `W` to the cone point of the limit cone for `F`
and cones over `F` with cone point `W`
is natural in `F`.
-/
def limYoneda :
    lim ‚ãô yoneda ‚ãô (whiskeringRight _ _ _).obj uliftFunctor.{u‚ÇÅ} ‚âÖ CategoryTheory.cones J C :=
  NatIso.ofComponents fun F => NatIso.ofComponents fun W => limit.homIso F (unop W)
#align category_theory.limits.lim_yoneda CategoryTheory.Limits.limYoneda

/-- The constant functor and limit functor are adjoint to each other-/
def constLimAdj : (const J : C ‚•§ J ‚•§ C) ‚ä£ lim where
  homEquiv c g :=
    { toFun := fun f => limit.lift _ ‚ü®c, f‚ü©
      invFun := fun f =>
        { app := fun j => f ‚â´ limit.œÄ _ _ }
      left_inv := by aesop_cat
      right_inv := by aesop_cat }
  unit := { app := fun c => limit.lift _ ‚ü®_, ùüô _‚ü© }
  counit := { app := fun g => { app := limit.œÄ _ } }
  -- This used to be automatic before leanprover/lean4#2644
  homEquiv_unit := by
    -- Sad that aesop can no longer do this!
    intros
    dsimp
    ext
    simp
#align category_theory.limits.const_lim_adj CategoryTheory.Limits.constLimAdj

instance : IsRightAdjoint (lim : (J ‚•§ C) ‚•§ C) :=
  ‚ü®_, ‚ü®constLimAdj‚ü©‚ü©

end LimFunctor

instance limMap_mono' {F G : J ‚•§ C} [HasLimitsOfShape J C] (Œ± : F ‚ü∂ G) [Mono Œ±] : Mono (limMap Œ±) :=
  (lim : (J ‚•§ C) ‚•§ C).map_mono Œ±
#align category_theory.limits.lim_map_mono' CategoryTheory.Limits.limMap_mono'

instance limMap_mono {F G : J ‚•§ C} [HasLimit F] [HasLimit G] (Œ± : F ‚ü∂ G) [‚àÄ j, Mono (Œ±.app j)] :
    Mono (limMap Œ±) :=
  ‚ü®fun {Z} u v h =>
    limit.hom_ext fun j => (cancel_mono (Œ±.app j)).1 <| by simpa using h =‚â´ limit.œÄ _ j‚ü©
#align category_theory.limits.lim_map_mono CategoryTheory.Limits.limMap_mono

section Adjunction

variable {L : (J ‚•§ C) ‚•§ C} (adj : Functor.const _ ‚ä£ L)

/- The fact that the existence of limits of shape `J` is equivalent to the existence
of a right adjoint to the constant functor `C ‚•§ (J ‚•§ C)` is obtained in
the file `Mathlib.CategoryTheory.Limits.ConeCategory`: see the lemma
`hasLimitsOfShape_iff_isLeftAdjoint_const`. In the definitions below, given an
adjunction `adj : Functor.const _ ‚ä£ (L : (J ‚•§ C) ‚•§ C)`, we directly construct
a limit cone for any `F : J ‚•§ C`. -/

/-- The limit cone obtained from a right adjoint of the constant functor. -/
@[simps]
noncomputable def coneOfAdj (F : J ‚•§ C) : Cone F where
  pt := L.obj F
  œÄ := adj.counit.app F

/-- The cones defined by `coneOfAdj` are limit cones. -/
@[simps]
def isLimitConeOfAdj (F : J ‚•§ C) :
    IsLimit (coneOfAdj adj F) where
  lift s := adj.homEquiv _ _ s.œÄ
  fac s j := by
    have eq := NatTrans.congr_app (adj.counit.naturality s.œÄ) j
    have eq' := NatTrans.congr_app (adj.left_triangle_components s.pt) j
    dsimp at eq eq' ‚ä¢
    rw [Adjunction.homEquiv_unit, assoc, eq, reassoc_of% eq']
  uniq s m hm := (adj.homEquiv _ _).symm.injective (by ext j; simpa using hm j)

end Adjunction

/-- We can transport limits of shape `J` along an equivalence `J ‚âå J'`.
-/
theorem hasLimitsOfShape_of_equivalence {J' : Type u‚ÇÇ} [Category.{v‚ÇÇ} J'] (e : J ‚âå J')
    [HasLimitsOfShape J C] : HasLimitsOfShape J' C := by
  constructor
  intro F
  apply hasLimitOfEquivalenceComp e
#align category_theory.limits.has_limits_of_shape_of_equivalence CategoryTheory.Limits.hasLimitsOfShape_of_equivalence

variable (C)

/-- A category that has larger limits also has smaller limits. -/
theorem hasLimitsOfSizeOfUnivLE [UnivLE.{v‚ÇÇ, v‚ÇÅ}] [UnivLE.{u‚ÇÇ, u‚ÇÅ}]
    [HasLimitsOfSize.{v‚ÇÅ, u‚ÇÅ} C] : HasLimitsOfSize.{v‚ÇÇ, u‚ÇÇ} C where
  has_limits_of_shape J {_} := hasLimitsOfShape_of_equivalence
    ((ShrinkHoms.equivalence J).trans <| Shrink.equivalence _).symm

/-- `hasLimitsOfSizeShrink.{v u} C` tries to obtain `HasLimitsOfSize.{v u} C`
from some other `HasLimitsOfSize C`.
-/
theorem hasLimitsOfSizeShrink [HasLimitsOfSize.{max v‚ÇÅ v‚ÇÇ, max u‚ÇÅ u‚ÇÇ} C] :
    HasLimitsOfSize.{v‚ÇÅ, u‚ÇÅ} C := hasLimitsOfSizeOfUnivLE.{max v‚ÇÅ v‚ÇÇ, max u‚ÇÅ u‚ÇÇ} C
#align category_theory.limits.has_limits_of_size_shrink CategoryTheory.Limits.hasLimitsOfSizeShrink

instance (priority := 100) hasSmallestLimitsOfHasLimits [HasLimits C] : HasLimitsOfSize.{0, 0} C :=
  hasLimitsOfSizeShrink.{0, 0} C
#align category_theory.limits.has_smallest_limits_of_has_limits CategoryTheory.Limits.hasSmallestLimitsOfHasLimits

end Limit

section Colimit

/-- `ColimitCocone F` contains a cocone over `F` together with the information that it is a
    colimit. -/
-- @[nolint has_nonempty_instance] -- Porting note(#5171): removed; linter not ported yet
structure ColimitCocone (F : J ‚•§ C) where
  /-- The cocone itself -/
  cocone : Cocone F
  /-- The proof that it is the colimit cocone -/
  isColimit : IsColimit cocone
#align category_theory.limits.colimit_cocone CategoryTheory.Limits.ColimitCocone
#align category_theory.limits.colimit_cocone.is_colimit CategoryTheory.Limits.ColimitCocone.isColimit

/-- `HasColimit F` represents the mere existence of a colimit for `F`. -/
class HasColimit (F : J ‚•§ C) : Prop where mk' ::
  /-- There exists a colimit for `F` -/
  exists_colimit : Nonempty (ColimitCocone F)
#align category_theory.limits.has_colimit CategoryTheory.Limits.HasColimit

theorem HasColimit.mk {F : J ‚•§ C} (d : ColimitCocone F) : HasColimit F :=
  ‚ü®Nonempty.intro d‚ü©
#align category_theory.limits.has_colimit.mk CategoryTheory.Limits.HasColimit.mk

/-- Use the axiom of choice to extract explicit `ColimitCocone F` from `HasColimit F`. -/
def getColimitCocone (F : J ‚•§ C) [HasColimit F] : ColimitCocone F :=
  Classical.choice <| HasColimit.exists_colimit
#align category_theory.limits.get_colimit_cocone CategoryTheory.Limits.getColimitCocone

variable (J C)

/-- `C` has colimits of shape `J` if there exists a colimit for every functor `F : J ‚•§ C`. -/
class HasColimitsOfShape : Prop where
  /-- All `F : J ‚•§ C` have colimits for a fixed `J` -/
  has_colimit : ‚àÄ F : J ‚•§ C, HasColimit F := by infer_instance
#align category_theory.limits.has_colimits_of_shape CategoryTheory.Limits.HasColimitsOfShape

/-- `C` has all colimits of size `v‚ÇÅ u‚ÇÅ` (`HasColimitsOfSize.{v‚ÇÅ u‚ÇÅ} C`)
if it has colimits of every shape `J : Type u‚ÇÅ` with `[Category.{v‚ÇÅ} J]`.
-/
@[pp_with_univ]
class HasColimitsOfSize (C : Type u) [Category.{v} C] : Prop where
  /-- All `F : J ‚•§ C` have colimits for all small `J` -/
  has_colimits_of_shape : ‚àÄ (J : Type u‚ÇÅ) [Category.{v‚ÇÅ} J], HasColimitsOfShape J C := by
    infer_instance
#align category_theory.limits.has_colimits_of_size CategoryTheory.Limits.HasColimitsOfSize

/-- `C` has all (small) colimits if it has colimits of every shape that is as big as its hom-sets.
-/
abbrev HasColimits (C : Type u) [Category.{v} C] : Prop :=
  HasColimitsOfSize.{v, v} C
#align category_theory.limits.has_colimits CategoryTheory.Limits.HasColimits

theorem HasColimits.hasColimitsOfShape {C : Type u} [Category.{v} C] [HasColimits C] (J : Type v)
    [Category.{v} J] : HasColimitsOfShape J C :=
  HasColimitsOfSize.has_colimits_of_shape J
#align category_theory.limits.has_colimits.has_colimits_of_shape CategoryTheory.Limits.HasColimits.hasColimitsOfShape

variable {J C}

-- see Note [lower instance priority]
instance (priority := 100) hasColimitOfHasColimitsOfShape {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]
    [HasColimitsOfShape J C] (F : J ‚•§ C) : HasColimit F :=
  HasColimitsOfShape.has_colimit F
#align category_theory.limits.has_colimit_of_has_colimits_of_shape CategoryTheory.Limits.hasColimitOfHasColimitsOfShape

-- see Note [lower instance priority]
instance (priority := 100) hasColimitsOfShapeOfHasColimitsOfSize {J : Type u‚ÇÅ} [Category.{v‚ÇÅ} J]
    [HasColimitsOfSize.{v‚ÇÅ, u‚ÇÅ} C] : HasColimitsOfShape J C :=
  HasColimitsOfSize.has_colimits_of_shape J
#align category_theory.limits.has_colimits_of_shape_of_has_colimits_of_size CategoryTheory.Limits.hasColimitsOfShapeOfHasColimitsOfSize

-- Interface to the `HasColimit` class.
/-- An arbitrary choice of colimit cocone of a functor. -/
def colimit.cocone (F : J ‚•§ C) [HasColimit F] : Cocone F :=
  (getColimitCocone F).cocone
#align category_theory.limits.colimit.cocone CategoryTheory.Limits.colimit.cocone

/-- An arbitrary choice of colimit object of a functor. -/
def colimit (F : J ‚•§ C) [HasColimit F] :=
  (colimit.cocone F).pt
#align category_theory.limits.colimit CategoryTheory.Limits.colimit

/-- The coprojection from a value of the functor to the colimit object. -/
def colimit.Œπ (F : J ‚•§ C) [HasColimit F] (j : J) : F.obj j ‚ü∂ colimit F :=
  (colimit.cocone F).Œπ.app j
#align category_theory.limits.colimit.Œπ CategoryTheory.Limits.colimit.Œπ

@[simp]
theorem colimit.cocone_Œπ {F : J ‚•§ C} [HasColimit F] (j : J) :
    (colimit.cocone F).Œπ.app j = colimit.Œπ _ j :=
  rfl
#align category_theory.limits.colimit.cocone_Œπ CategoryTheory.Limits.colimit.cocone_Œπ

@[simp]
theorem colimit.cocone_x {F : J ‚•§ C} [HasColimit F] : (colimit.cocone F).pt = colimit F :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.limits.colimit.cocone_X CategoryTheory.Limits.colimit.cocone_x

@[reassoc (attr := simp)]
theorem colimit.w (F : J ‚•§ C) [HasColimit F] {j j' : J} (f : j ‚ü∂ j') :
    F.map f ‚â´ colimit.Œπ F j' = colimit.Œπ F j :=
  (colimit.cocone F).w f
#align category_theory.limits.colimit.w CategoryTheory.Limits.colimit.w

/-- Evidence that the arbitrary choice of cocone is a colimit cocone. -/
def colimit.isColimit (F : J ‚•§ C) [HasColimit F] : IsColimit (colimit.cocone F) :=
  (getColimitCocone F).isColimit
#align category_theory.limits.colimit.is_colimit CategoryTheory.Limits.colimit.isColimit

/-- The morphism from the colimit object to the cone point of any other cocone. -/
def colimit.desc (F : J ‚•§ C) [HasColimit F] (c : Cocone F) : colimit F ‚ü∂ c.pt :=
  (colimit.isColimit F).desc c
#align category_theory.limits.colimit.desc CategoryTheory.Limits.colimit.desc

@[simp]
theorem colimit.isColimit_desc {F : J ‚•§ C} [HasColimit F] (c : Cocone F) :
    (colimit.isColimit F).desc c = colimit.desc F c :=
  rfl
#align category_theory.limits.colimit.is_colimit_desc CategoryTheory.Limits.colimit.isColimit_desc

/-- We have lots of lemmas describing how to simplify `colimit.Œπ F j ‚â´ _`,
and combined with `colimit.ext` we rely on these lemmas for many calculations.

However, since `Category.assoc` is a `@[simp]` lemma, often expressions are
right associated, and it's hard to apply these lemmas about `colimit.Œπ`.

We thus use `reassoc` to define additional `@[simp]` lemmas, with an arbitrary extra morphism.
(see `Tactic/reassoc_axiom.lean`)
 -/
@[reassoc (attr := simp)]
theorem colimit.Œπ_desc {F : J ‚•§ C} [HasColimit F] (c : Cocone F) (j : J) :
    colimit.Œπ F j ‚â´ colimit.desc F c = c.Œπ.app j :=
  IsColimit.fac _ c j
#align category_theory.limits.colimit.Œπ_desc CategoryTheory.Limits.colimit.Œπ_desc

/-- Functoriality of colimits.

Usually this morphism should be accessed through `colim.map`,
but may be needed separately when you have specified colimits for the source and target functors,
but not necessarily for all functors of shape `J`.
-/
def colimMap {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (Œ± : F ‚ü∂ G) : colimit F ‚ü∂ colimit G :=
  IsColimit.map (colimit.isColimit F) _ Œ±
#align category_theory.limits.colim_map CategoryTheory.Limits.colimMap

@[reassoc (attr := simp)]
theorem Œπ_colimMap {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (Œ± : F ‚ü∂ G) (j : J) :
    colimit.Œπ F j ‚â´ colimMap Œ± = Œ±.app j ‚â´ colimit.Œπ G j :=
  colimit.Œπ_desc _ j
#align category_theory.limits.Œπ_colim_map CategoryTheory.Limits.Œπ_colimMap

/-- The cocone morphism from the arbitrary choice of colimit cocone to any cocone. -/
def colimit.coconeMorphism {F : J ‚•§ C} [HasColimit F] (c : Cocone F) : colimit.cocone F ‚ü∂ c :=
  (colimit.isColimit F).descCoconeMorphism c
#align category_theory.limits.colimit.cocone_morphism CategoryTheory.Limits.colimit.coconeMorphism

@[simp]
theorem colimit.coconeMorphism_hom {F : J ‚•§ C} [HasColimit F] (c : Cocone F) :
    (colimit.coconeMorphism c).hom = colimit.desc F c :=
  rfl
#align category_theory.limits.colimit.cocone_morphism_hom CategoryTheory.Limits.colimit.coconeMorphism_hom

theorem colimit.Œπ_coconeMorphism {F : J ‚•§ C} [HasColimit F] (c : Cocone F) (j : J) :
    colimit.Œπ F j ‚â´ (colimit.coconeMorphism c).hom = c.Œπ.app j := by simp
#align category_theory.limits.colimit.Œπ_cocone_morphism CategoryTheory.Limits.colimit.Œπ_coconeMorphism

@[reassoc (attr := simp)]
theorem colimit.comp_coconePointUniqueUpToIso_hom {F : J ‚•§ C} [HasColimit F] {c : Cocone F}
    (hc : IsColimit c) (j : J) :
    colimit.Œπ F j ‚â´ (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) hc).hom = c.Œπ.app j :=
  IsColimit.comp_coconePointUniqueUpToIso_hom _ _ _
#align category_theory.limits.colimit.comp_cocone_point_unique_up_to_iso_hom CategoryTheory.Limits.colimit.comp_coconePointUniqueUpToIso_hom

@[reassoc (attr := simp)]
theorem colimit.comp_coconePointUniqueUpToIso_inv {F : J ‚•§ C} [HasColimit F] {c : Cocone F}
    (hc : IsColimit c) (j : J) :
    colimit.Œπ F j ‚â´ (IsColimit.coconePointUniqueUpToIso hc (colimit.isColimit _)).inv = c.Œπ.app j :=
  IsColimit.comp_coconePointUniqueUpToIso_inv _ _ _
#align category_theory.limits.colimit.comp_cocone_point_unique_up_to_iso_inv CategoryTheory.Limits.colimit.comp_coconePointUniqueUpToIso_inv

theorem colimit.existsUnique {F : J ‚•§ C} [HasColimit F] (t : Cocone F) :
    ‚àÉ! d : colimit F ‚ü∂ t.pt, ‚àÄ j, colimit.Œπ F j ‚â´ d = t.Œπ.app j :=
  (colimit.isColimit F).existsUnique _
#align category_theory.limits.colimit.exists_unique CategoryTheory.Limits.colimit.existsUnique

/--
Given any other colimit cocone for `F`, the chosen `colimit F` is isomorphic to the cocone point.
-/
def colimit.isoColimitCocone {F : J ‚•§ C} [HasColimit F] (t : ColimitCocone F) :
    colimit F ‚âÖ t.cocone.pt :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit F) t.isColimit
#align category_theory.limits.colimit.iso_colimit_cocone CategoryTheory.Limits.colimit.isoColimitCocone

@[reassoc (attr := simp)]
theorem colimit.isoColimitCocone_Œπ_hom {F : J ‚•§ C} [HasColimit F] (t : ColimitCocone F) (j : J) :
    colimit.Œπ F j ‚â´ (colimit.isoColimitCocone t).hom = t.cocone.Œπ.app j := by
  dsimp [colimit.isoColimitCocone, IsColimit.coconePointUniqueUpToIso]
  aesop_cat
#align category_theory.limits.colimit.iso_colimit_cocone_Œπ_hom CategoryTheory.Limits.colimit.isoColimitCocone_Œπ_hom

@[reassoc (attr := simp)]
theorem colimit.isoColimitCocone_Œπ_inv {F : J ‚•§ C} [HasColimit F] (t : ColimitCocone F) (j : J) :
    t.cocone.Œπ.app j ‚â´ (colimit.isoColimitCocone t).inv = colimit.Œπ F j := by
  dsimp [colimit.isoColimitCocone, IsColimit.coconePointUniqueUpToIso]
  aesop_cat
#align category_theory.limits.colimit.iso_colimit_cocone_Œπ_inv CategoryTheory.Limits.colimit.isoColimitCocone_Œπ_inv

@[ext]
theorem colimit.hom_ext {F : J ‚•§ C} [HasColimit F] {X : C} {f f' : colimit F ‚ü∂ X}
    (w : ‚àÄ j, colimit.Œπ F j ‚â´ f = colimit.Œπ F j ‚â´ f') : f = f' :=
  (colimit.isColimit F).hom_ext w
#align category_theory.limits.colimit.hom_ext CategoryTheory.Limits.colimit.hom_ext

@[simp]
theorem colimit.desc_cocone {F : J ‚•§ C} [HasColimit F] :
    colimit.desc F (colimit.cocone F) = ùüô (colimit F) :=
  (colimit.isColimit _).desc_self
#align category_theory.limits.colimit.desc_cocone CategoryTheory.Limits.colimit.desc_cocone

/-- The isomorphism (in `Type`) between
morphisms from the colimit object to a specified object `W`,
and cocones with cone point `W`.
-/
def colimit.homIso (F : J ‚•§ C) [HasColimit F] (W : C) :
    ULift.{u‚ÇÅ} (colimit F ‚ü∂ W : Type v) ‚âÖ F.cocones.obj W :=
  (colimit.isColimit F).homIso W
#align category_theory.limits.colimit.hom_iso CategoryTheory.Limits.colimit.homIso

@[simp]
theorem colimit.homIso_hom (F : J ‚•§ C) [HasColimit F] {W : C} (f : ULift (colimit F ‚ü∂ W)) :
    (colimit.homIso F W).hom f = (colimit.cocone F).Œπ ‚â´ (const J).map f.down :=
  (colimit.isColimit F).homIso_hom f
#align category_theory.limits.colimit.hom_iso_hom CategoryTheory.Limits.colimit.homIso_hom

/-- The isomorphism (in `Type`) between
morphisms from the colimit object to a specified object `W`,
and an explicit componentwise description of cocones with cone point `W`.
-/
def colimit.homIso' (F : J ‚•§ C) [HasColimit F] (W : C) :
    ULift.{u‚ÇÅ} (colimit F ‚ü∂ W : Type v) ‚âÖ
      { p : ‚àÄ j, F.obj j ‚ü∂ W // ‚àÄ {j j'} (f : j ‚ü∂ j'), F.map f ‚â´ p j' = p j } :=
  (colimit.isColimit F).homIso' W
#align category_theory.limits.colimit.hom_iso' CategoryTheory.Limits.colimit.homIso'

theorem colimit.desc_extend (F : J ‚•§ C) [HasColimit F] (c : Cocone F) {X : C} (f : c.pt ‚ü∂ X) :
    colimit.desc F (c.extend f) = colimit.desc F c ‚â´ f := by ext1; rw [‚Üê Category.assoc]; simp
#align category_theory.limits.colimit.desc_extend CategoryTheory.Limits.colimit.desc_extend

-- This has the isomorphism pointing in the opposite direction than in `has_limit_of_iso`.
-- This is intentional; it seems to help with elaboration.
/-- If `F` has a colimit, so does any naturally isomorphic functor.
-/
theorem hasColimitOfIso {F G : J ‚•§ C} [HasColimit F] (Œ± : G ‚âÖ F) : HasColimit G :=
  HasColimit.mk
    { cocone := (Cocones.precompose Œ±.hom).obj (colimit.cocone F)
      isColimit := (IsColimit.precomposeHomEquiv _ _).symm (colimit.isColimit F) }
#align category_theory.limits.has_colimit_of_iso CategoryTheory.Limits.hasColimitOfIso

/-- If a functor `G` has the same collection of cocones as a functor `F`
which has a colimit, then `G` also has a colimit. -/
theorem HasColimit.ofCoconesIso {K : Type u‚ÇÅ} [Category.{v‚ÇÇ} K] (F : J ‚•§ C) (G : K ‚•§ C)
    (h : F.cocones ‚âÖ G.cocones) [HasColimit F] : HasColimit G :=
  HasColimit.mk ‚ü®_, IsColimit.ofNatIso (IsColimit.natIso (colimit.isColimit F) ‚â™‚â´ h)‚ü©
#align category_theory.limits.has_colimit.of_cocones_iso CategoryTheory.Limits.HasColimit.ofCoconesIso

/-- The colimits of `F : J ‚•§ C` and `G : J ‚•§ C` are isomorphic,
if the functors are naturally isomorphic.
-/
def HasColimit.isoOfNatIso {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (w : F ‚âÖ G) :
    colimit F ‚âÖ colimit G :=
  IsColimit.coconePointsIsoOfNatIso (colimit.isColimit F) (colimit.isColimit G) w
#align category_theory.limits.has_colimit.iso_of_nat_iso CategoryTheory.Limits.HasColimit.isoOfNatIso

@[reassoc (attr := simp)]
theorem HasColimit.isoOfNatIso_Œπ_hom {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (w : F ‚âÖ G)
    (j : J) : colimit.Œπ F j ‚â´ (HasColimit.isoOfNatIso w).hom = w.hom.app j ‚â´ colimit.Œπ G j :=
  IsColimit.comp_coconePointsIsoOfNatIso_hom _ _ _ _
#align category_theory.limits.has_colimit.iso_of_nat_iso_Œπ_hom CategoryTheory.Limits.HasColimit.isoOfNatIso_Œπ_hom

@[reassoc (attr := simp)]
theorem HasColimit.isoOfNatIso_Œπ_inv {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (w : F ‚âÖ G)
    (j : J) : colimit.Œπ G j ‚â´ (HasColimit.isoOfNatIso w).inv = w.inv.app j ‚â´ colimit.Œπ F j :=
  IsColimit.comp_coconePointsIsoOfNatIso_inv _ _ _ _
#align category_theory.limits.has_colimit.iso_of_nat_iso_Œπ_inv CategoryTheory.Limits.HasColimit.isoOfNatIso_Œπ_inv

@[reassoc (attr := simp)]
theorem HasColimit.isoOfNatIso_hom_desc {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (t : Cocone G)
    (w : F ‚âÖ G) :
    (HasColimit.isoOfNatIso w).hom ‚â´ colimit.desc G t =
      colimit.desc F ((Cocones.precompose w.hom).obj _) :=
  IsColimit.coconePointsIsoOfNatIso_hom_desc _ _ _
#align category_theory.limits.has_colimit.iso_of_nat_iso_hom_desc CategoryTheory.Limits.HasColimit.isoOfNatIso_hom_desc

@[reassoc (attr := simp)]
theorem HasColimit.isoOfNatIso_inv_desc {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (t : Cocone F)
    (w : F ‚âÖ G) :
    (HasColimit.isoOfNatIso w).inv ‚â´ colimit.desc F t =
      colimit.desc G ((Cocones.precompose w.inv).obj _) :=
  IsColimit.coconePointsIsoOfNatIso_inv_desc _ _ _
#align category_theory.limits.has_colimit.iso_of_nat_iso_inv_desc CategoryTheory.Limits.HasColimit.isoOfNatIso_inv_desc

/-- The colimits of `F : J ‚•§ C` and `G : K ‚•§ C` are isomorphic,
if there is an equivalence `e : J ‚âå K` making the triangle commute up to natural isomorphism.
-/
def HasColimit.isoOfEquivalence {F : J ‚•§ C} [HasColimit F] {G : K ‚•§ C} [HasColimit G] (e : J ‚âå K)
    (w : e.functor ‚ãô G ‚âÖ F) : colimit F ‚âÖ colimit G :=
  IsColimit.coconePointsIsoOfEquivalence (colimit.isColimit F) (colimit.isColimit G) e w
#align category_theory.limits.has_colimit.iso_of_equivalence CategoryTheory.Limits.HasColimit.isoOfEquivalence

@[simp]
theorem HasColimit.isoOfEquivalence_hom_œÄ {F : J ‚•§ C} [HasColimit F] {G : K ‚•§ C} [HasColimit G]
    (e : J ‚âå K) (w : e.functor ‚ãô G ‚âÖ F) (j : J) :
    colimit.Œπ F j ‚â´ (HasColimit.isoOfEquivalence e w).hom =
      F.map (e.unit.app j) ‚â´ w.inv.app _ ‚â´ colimit.Œπ G _ := by
  simp [HasColimit.isoOfEquivalence, IsColimit.coconePointsIsoOfEquivalence_inv]
#align category_theory.limits.has_colimit.iso_of_equivalence_hom_œÄ CategoryTheory.Limits.HasColimit.isoOfEquivalence_hom_œÄ

@[simp]
theorem HasColimit.isoOfEquivalence_inv_œÄ {F : J ‚•§ C} [HasColimit F] {G : K ‚•§ C} [HasColimit G]
    (e : J ‚âå K) (w : e.functor ‚ãô G ‚âÖ F) (k : K) :
    colimit.Œπ G k ‚â´ (HasColimit.isoOfEquivalence e w).inv =
      G.map (e.counitInv.app k) ‚â´ w.hom.app (e.inverse.obj k) ‚â´ colimit.Œπ F (e.inverse.obj k) := by
  simp [HasColimit.isoOfEquivalence, IsColimit.coconePointsIsoOfEquivalence_inv]
#align category_theory.limits.has_colimit.iso_of_equivalence_inv_œÄ CategoryTheory.Limits.HasColimit.isoOfEquivalence_inv_œÄ

section Pre

variable (F)
variable [HasColimit F] (E : K ‚•§ J) [HasColimit (E ‚ãô F)]

/-- The canonical morphism from the colimit of `E ‚ãô F` to the colimit of `F`.
-/
def colimit.pre : colimit (E ‚ãô F) ‚ü∂ colimit F :=
  colimit.desc (E ‚ãô F) ((colimit.cocone F).whisker E)
#align category_theory.limits.colimit.pre CategoryTheory.Limits.colimit.pre

@[reassoc (attr := simp)]
theorem colimit.Œπ_pre (k : K) : colimit.Œπ (E ‚ãô F) k ‚â´ colimit.pre F E = colimit.Œπ F (E.obj k) := by
  erw [IsColimit.fac]
  rfl
#align category_theory.limits.colimit.Œπ_pre CategoryTheory.Limits.colimit.Œπ_pre

@[reassoc (attr := simp)]
theorem colimit.pre_desc (c : Cocone F) :
    colimit.pre F E ‚â´ colimit.desc F c = colimit.desc (E ‚ãô F) (c.whisker E) := by
  ext; rw [‚Üê assoc, colimit.Œπ_pre]; simp
#align category_theory.limits.colimit.pre_desc CategoryTheory.Limits.colimit.pre_desc

variable {L : Type u‚ÇÉ} [Category.{v‚ÇÉ} L]
variable (D : L ‚•§ K) [HasColimit (D ‚ãô E ‚ãô F)]

@[simp]
theorem colimit.pre_pre [h : HasColimit (D ‚ãô E ‚ãô F)] :
    haveI : HasColimit ((D ‚ãô E) ‚ãô F) := h
    colimit.pre (E ‚ãô F) D ‚â´ colimit.pre F E = colimit.pre F (D ‚ãô E) := by
  ext j
  rw [‚Üê assoc, colimit.Œπ_pre, colimit.Œπ_pre]
  haveI : HasColimit ((D ‚ãô E) ‚ãô F) := h
  exact (colimit.Œπ_pre F (D ‚ãô E) j).symm
#align category_theory.limits.colimit.pre_pre CategoryTheory.Limits.colimit.pre_pre

variable {E F}

/-- -
If we have particular colimit cocones available for `E ‚ãô F` and for `F`,
we obtain a formula for `colimit.pre F E`.
-/
theorem colimit.pre_eq (s : ColimitCocone (E ‚ãô F)) (t : ColimitCocone F) :
    colimit.pre F E =
      (colimit.isoColimitCocone s).hom ‚â´
        s.isColimit.desc (t.cocone.whisker E) ‚â´ (colimit.isoColimitCocone t).inv := by
  aesop_cat
#align category_theory.limits.colimit.pre_eq CategoryTheory.Limits.colimit.pre_eq

end Pre

section Post

variable {D : Type u'} [Category.{v'} D]
variable (F)
variable [HasColimit F] (G : C ‚•§ D) [HasColimit (F ‚ãô G)]

/-- The canonical morphism from `G` applied to the colimit of `F ‚ãô G`
to `G` applied to the colimit of `F`.
-/
def colimit.post : colimit (F ‚ãô G) ‚ü∂ G.obj (colimit F) :=
  colimit.desc (F ‚ãô G) (G.mapCocone (colimit.cocone F))
#align category_theory.limits.colimit.post CategoryTheory.Limits.colimit.post

@[reassoc (attr := simp)]
theorem colimit.Œπ_post (j : J) :
    colimit.Œπ (F ‚ãô G) j ‚â´ colimit.post F G = G.map (colimit.Œπ F j) := by
  erw [IsColimit.fac]
  rfl
#align category_theory.limits.colimit.Œπ_post CategoryTheory.Limits.colimit.Œπ_post

@[simp]
theorem colimit.post_desc (c : Cocone F) :
    colimit.post F G ‚â´ G.map (colimit.desc F c) = colimit.desc (F ‚ãô G) (G.mapCocone c) := by
  ext
  rw [‚Üê assoc, colimit.Œπ_post, ‚Üê G.map_comp, colimit.Œπ_desc, colimit.Œπ_desc]
  rfl
#align category_theory.limits.colimit.post_desc CategoryTheory.Limits.colimit.post_desc

@[simp]
theorem colimit.post_post {E : Type u''} [Category.{v''} E] (H : D ‚•§ E)
    -- H G (colimit F) ‚ü∂ H (colimit (F ‚ãô G)) ‚ü∂ colimit ((F ‚ãô G) ‚ãô H) equals
    -- H G (colimit F) ‚ü∂ colimit (F ‚ãô (G ‚ãô H))
    [h : HasColimit ((F ‚ãô G) ‚ãô H)] : haveI : HasColimit (F ‚ãô G ‚ãô H) := h
    colimit.post (F ‚ãô G) H ‚â´ H.map (colimit.post F G) = colimit.post F (G ‚ãô H) := by
  ext j
  rw [‚Üê assoc, colimit.Œπ_post, ‚Üê H.map_comp, colimit.Œπ_post]
  haveI : HasColimit (F ‚ãô G ‚ãô H) := h
  exact (colimit.Œπ_post F (G ‚ãô H) j).symm
#align category_theory.limits.colimit.post_post CategoryTheory.Limits.colimit.post_post

end Post

theorem colimit.pre_post {D : Type u'} [Category.{v'} D] (E : K ‚•§ J) (F : J ‚•§ C) (G : C ‚•§ D)
    [HasColimit F] [HasColimit (E ‚ãô F)] [HasColimit (F ‚ãô G)] [h : HasColimit ((E ‚ãô F) ‚ãô G)] :
    -- G (colimit F) ‚ü∂ G (colimit (E ‚ãô F)) ‚ü∂ colimit ((E ‚ãô F) ‚ãô G) vs
    -- G (colimit F) ‚ü∂ colimit F ‚ãô G ‚ü∂ colimit (E ‚ãô (F ‚ãô G)) or
    haveI : HasColimit (E ‚ãô F ‚ãô G) := h
    colimit.post (E ‚ãô F) G ‚â´ G.map (colimit.pre F E) =
      colimit.pre (F ‚ãô G) E ‚â´ colimit.post F G := by
  ext j
  rw [‚Üê assoc, colimit.Œπ_post, ‚Üê G.map_comp, colimit.Œπ_pre, ‚Üê assoc]
  haveI : HasColimit (E ‚ãô F ‚ãô G) := h
  erw [colimit.Œπ_pre (F ‚ãô G) E j, colimit.Œπ_post]
#align category_theory.limits.colimit.pre_post CategoryTheory.Limits.colimit.pre_post

open CategoryTheory.Equivalence

instance hasColimit_equivalence_comp (e : K ‚âå J) [HasColimit F] : HasColimit (e.functor ‚ãô F) :=
  HasColimit.mk
    { cocone := Cocone.whisker e.functor (colimit.cocone F)
      isColimit := IsColimit.whiskerEquivalence (colimit.isColimit F) e }
#align category_theory.limits.has_colimit_equivalence_comp CategoryTheory.Limits.hasColimit_equivalence_comp

/-- If a `E ‚ãô F` has a colimit, and `E` is an equivalence, we can construct a colimit of `F`.
-/
theorem hasColimit_of_equivalence_comp (e : K ‚âå J) [HasColimit (e.functor ‚ãô F)] : HasColimit F := by
  haveI : HasColimit (e.inverse ‚ãô e.functor ‚ãô F) := Limits.hasColimit_equivalence_comp e.symm
  apply hasColimitOfIso (e.invFunIdAssoc F).symm
#align category_theory.limits.has_colimit_of_equivalence_comp CategoryTheory.Limits.hasColimit_of_equivalence_comp

section ColimFunctor

variable [HasColimitsOfShape J C]

section

-- attribute [local simp] colimMap -- Porting note: errors out colim.map_id and map_comp now

/-- `colimit F` is functorial in `F`, when `C` has all colimits of shape `J`. -/
@[simps] -- Porting note: simps on all fields now
def colim : (J ‚•§ C) ‚•§ C where
  obj F := colimit F
  map Œ± := colimMap Œ±
#align category_theory.limits.colim CategoryTheory.Limits.colim

end

variable {G : J ‚•§ C} (Œ± : F ‚ü∂ G)

-- @[reassoc (attr := simp)] Porting note: now simp can prove these
@[reassoc]
theorem colimit.Œπ_map (j : J) : colimit.Œπ F j ‚â´ colim.map Œ± = Œ±.app j ‚â´ colimit.Œπ G j := by simp
#align category_theory.limits.colimit.Œπ_map CategoryTheory.Limits.colimit.Œπ_map

@[simp] -- Porting note: proof adjusted to account for @[simps] on all fields of colim
theorem colimit.map_desc (c : Cocone G) :
    colimMap Œ± ‚â´ colimit.desc G c = colimit.desc F ((Cocones.precompose Œ±).obj c) := by
  ext j
  simp [‚Üê assoc, colimit.Œπ_map, assoc, colimit.Œπ_desc, colimit.Œπ_desc]
#align category_theory.limits.colimit.map_desc CategoryTheory.Limits.colimit.map_desc

theorem colimit.pre_map [HasColimitsOfShape K C] (E : K ‚•§ J) :
    colimit.pre F E ‚â´ colim.map Œ± = colim.map (whiskerLeft E Œ±) ‚â´ colimit.pre G E := by
  ext
  rw [‚Üê assoc, colimit.Œπ_pre, colimit.Œπ_map, ‚Üê assoc, colimit.Œπ_map, assoc, colimit.Œπ_pre]
  rfl
#align category_theory.limits.colimit.pre_map CategoryTheory.Limits.colimit.pre_map

theorem colimit.pre_map' [HasColimitsOfShape K C] (F : J ‚•§ C) {E‚ÇÅ E‚ÇÇ : K ‚•§ J} (Œ± : E‚ÇÅ ‚ü∂ E‚ÇÇ) :
    colimit.pre F E‚ÇÅ = colim.map (whiskerRight Œ± F) ‚â´ colimit.pre F E‚ÇÇ := by
  ext1
  simp [‚Üê assoc, assoc]
#align category_theory.limits.colimit.pre_map' CategoryTheory.Limits.colimit.pre_map'

theorem colimit.pre_id (F : J ‚•§ C) :
    colimit.pre F (ùü≠ _) = colim.map (Functor.leftUnitor F).hom := by aesop_cat
#align category_theory.limits.colimit.pre_id CategoryTheory.Limits.colimit.pre_id

theorem colimit.map_post {D : Type u'} [Category.{v'} D] [HasColimitsOfShape J D]
    (H : C ‚•§ D) :/- H (colimit F) ‚ü∂ H (colimit G) ‚ü∂ colimit (G ‚ãô H) vs
             H (colimit F) ‚ü∂ colimit (F ‚ãô H) ‚ü∂ colimit (G ‚ãô H) -/
          colimit.post
          F H ‚â´
        H.map (colim.map Œ±) =
      colim.map (whiskerRight Œ± H) ‚â´ colimit.post G H := by
  ext
  rw [‚Üê assoc, colimit.Œπ_post, ‚Üê H.map_comp, colimit.Œπ_map, H.map_comp]
  rw [‚Üê assoc, colimit.Œπ_map, assoc, colimit.Œπ_post]
  rfl
#align category_theory.limits.colimit.map_post CategoryTheory.Limits.colimit.map_post

/-- The isomorphism between
morphisms from the cone point of the colimit cocone for `F` to `W`
and cocones over `F` with cone point `W`
is natural in `F`.
-/
def colimCoyoneda : colim.op ‚ãô coyoneda ‚ãô (whiskeringRight _ _ _).obj uliftFunctor.{u‚ÇÅ}
    ‚âÖ CategoryTheory.cocones J C :=
  NatIso.ofComponents fun F => NatIso.ofComponents fun W => colimit.homIso (unop F) W
#align category_theory.limits.colim_coyoneda CategoryTheory.Limits.colimCoyoneda

/-- The colimit functor and constant functor are adjoint to each other
-/
def colimConstAdj : (colim : (J ‚•§ C) ‚•§ C) ‚ä£ const J where
  homEquiv f c :=
    { toFun := fun g =>
        { app := fun _ => colimit.Œπ _ _ ‚â´ g }
      invFun := fun g => colimit.desc _ ‚ü®_, g‚ü©
      left_inv := by aesop_cat
      right_inv := by aesop_cat }
  unit := { app := fun g => { app := colimit.Œπ _ } }
  counit := { app := fun c => colimit.desc _ ‚ü®_, ùüô _‚ü© }
#align category_theory.limits.colim_const_adj CategoryTheory.Limits.colimConstAdj

instance : IsLeftAdjoint (colim : (J ‚•§ C) ‚•§ C) :=
  ‚ü®_, ‚ü®colimConstAdj‚ü©‚ü©

end ColimFunctor

instance colimMap_epi' {F G : J ‚•§ C} [HasColimitsOfShape J C] (Œ± : F ‚ü∂ G) [Epi Œ±] :
    Epi (colimMap Œ±) :=
  (colim : (J ‚•§ C) ‚•§ C).map_epi Œ±
#align category_theory.limits.colim_map_epi' CategoryTheory.Limits.colimMap_epi'

instance colimMap_epi {F G : J ‚•§ C} [HasColimit F] [HasColimit G] (Œ± : F ‚ü∂ G) [‚àÄ j, Epi (Œ±.app j)] :
    Epi (colimMap Œ±) :=
  ‚ü®fun {Z} u v h =>
    colimit.hom_ext fun j => (cancel_epi (Œ±.app j)).1 <| by simpa using colimit.Œπ _ j ‚â´= h‚ü©
#align category_theory.limits.colim_map_epi CategoryTheory.Limits.colimMap_epi

/-- We can transport colimits of shape `J` along an equivalence `J ‚âå J'`.
-/
theorem hasColimitsOfShape_of_equivalence {J' : Type u‚ÇÇ} [Category.{v‚ÇÇ} J'] (e : J ‚âå J')
    [HasColimitsOfShape J C] : HasColimitsOfShape J' C := by
  constructor
  intro F
  apply hasColimit_of_equivalence_comp e
#align category_theory.limits.has_colimits_of_shape_of_equivalence CategoryTheory.Limits.hasColimitsOfShape_of_equivalence

variable (C)

/-- A category that has larger colimits also has smaller colimits. -/
theorem hasColimitsOfSizeOfUnivLE [UnivLE.{v‚ÇÇ, v‚ÇÅ}] [UnivLE.{u‚ÇÇ, u‚ÇÅ}]
    [HasColimitsOfSize.{v‚ÇÅ, u‚ÇÅ} C] : HasColimitsOfSize.{v‚ÇÇ, u‚ÇÇ} C where
  has_colimits_of_shape J {_} := hasColimitsOfShape_of_equivalence
    ((ShrinkHoms.equivalence J).trans <| Shrink.equivalence _).symm

/-- `hasColimitsOfSizeShrink.{v u} C` tries to obtain `HasColimitsOfSize.{v u} C`
from some other `HasColimitsOfSize C`.
-/
theorem hasColimitsOfSizeShrink [HasColimitsOfSize.{max v‚ÇÅ v‚ÇÇ, max u‚ÇÅ u‚ÇÇ} C] :
    HasColimitsOfSize.{v‚ÇÅ, u‚ÇÅ} C := hasColimitsOfSizeOfUnivLE.{max v‚ÇÅ v‚ÇÇ, max u‚ÇÅ u‚ÇÇ} C
#align category_theory.limits.has_colimits_of_size_shrink CategoryTheory.Limits.hasColimitsOfSizeShrink

instance (priority := 100) hasSmallestColimitsOfHasColimits [HasColimits C] :
    HasColimitsOfSize.{0, 0} C :=
  hasColimitsOfSizeShrink.{0, 0} C
#align category_theory.limits.has_smallest_colimits_of_has_colimits CategoryTheory.Limits.hasSmallestColimitsOfHasColimits

end Colimit

section Opposite

/-- If `t : Cone F` is a limit cone, then `t.op : Cocone F.op` is a colimit cocone.
-/
def IsLimit.op {t : Cone F} (P : IsLimit t) : IsColimit t.op where
  desc s := (P.lift s.unop).op
  fac s j := congrArg Quiver.Hom.op (P.fac s.unop (unop j))
  uniq s m w := by
    dsimp
    rw [‚Üê P.uniq s.unop m.unop]
    ¬∑ rfl
    ¬∑ dsimp
      intro j
      rw [‚Üê w]
      rfl
#align category_theory.limits.is_limit.op CategoryTheory.Limits.IsLimit.op

/-- If `t : Cocone F` is a colimit cocone, then `t.op : Cone F.op` is a limit cone.
-/
def IsColimit.op {t : Cocone F} (P : IsColimit t) : IsLimit t.op where
  lift s := (P.desc s.unop).op
  fac s j := congrArg Quiver.Hom.op (P.fac s.unop (unop j))
  uniq s m w := by
    dsimp
    rw [‚Üê P.uniq s.unop m.unop]
    ¬∑ rfl
    ¬∑ dsimp
      intro j
      rw [‚Üê w]
      rfl
#align category_theory.limits.is_colimit.op CategoryTheory.Limits.IsColimit.op

/-- If `t : Cone F.op` is a limit cone, then `t.unop : Cocone F` is a colimit cocone.
-/
def IsLimit.unop {t : Cone F.op} (P : IsLimit t) : IsColimit t.unop where
  desc s := (P.lift s.op).unop
  fac s j := congrArg Quiver.Hom.unop (P.fac s.op (Opposite.op j))
  -- Porting note: thinks `op j` is `IsLimit.op j`
  uniq s m w := by
    dsimp
    rw [‚Üê P.uniq s.op m.op]
    ¬∑ rfl
    ¬∑ dsimp
      intro j
      rw [‚Üê w]
      rfl
#align category_theory.limits.is_limit.unop CategoryTheory.Limits.IsLimit.unop

/-- If `t : Cocone F.op` is a colimit cocone, then `t.unop : Cone F.` is a limit cone.
-/
def IsColimit.unop {t : Cocone F.op} (P : IsColimit t) : IsLimit t.unop where
  lift s := (P.desc s.op).unop
  fac s j := congrArg Quiver.Hom.unop (P.fac s.op (Opposite.op j))
  -- Porting note: thinks `op j` is `IsLimit.op j`
  uniq s m w := by
    dsimp
    rw [‚Üê P.uniq s.op m.op]
    ¬∑ rfl
    ¬∑ dsimp
      intro j
      rw [‚Üê w]
      rfl
#align category_theory.limits.is_colimit.unop CategoryTheory.Limits.IsColimit.unop

/-- `t : Cone F` is a limit cone if and only if `t.op : Cocone F.op` is a colimit cocone.
-/
def isLimitEquivIsColimitOp {t : Cone F} : IsLimit t ‚âÉ IsColimit t.op :=
  equivOfSubsingletonOfSubsingleton IsLimit.op fun P =>
    P.unop.ofIsoLimit (Cones.ext (Iso.refl _))
#align category_theory.limits.is_limit_equiv_is_colimit_op CategoryTheory.Limits.isLimitEquivIsColimitOp

/-- `t : Cocone F` is a colimit cocone if and only if `t.op : Cone F.op` is a limit cone.
-/
def isColimitEquivIsLimitOp {t : Cocone F} : IsColimit t ‚âÉ IsLimit t.op :=
  equivOfSubsingletonOfSubsingleton IsColimit.op fun P =>
    P.unop.ofIsoColimit (Cocones.ext (Iso.refl _))
#align category_theory.limits.is_colimit_equiv_is_limit_op CategoryTheory.Limits.isColimitEquivIsLimitOp

end Opposite
