/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Limits.Connected
import Mathlib.CategoryTheory.Presentable.Finite

/-!
# (Co)limit presentations

Let `J` and `C` be categories and `X : C`. We define type `ColimitPresentation J X` that contains
the data of objects `Dâ±¼` and natural maps `sâ±¼ : Dâ±¼ âŸ¶ X` that make `X` the colimit of the `Dâ±¼`.

## Main definitions:

- `CategoryTheory.Limits.ColimitPresentation`: A colimit presentation of `X` over `J` is a diagram
  `{Dáµ¢}` in `C` and natural maps `sáµ¢ : Dáµ¢ âŸ¶ X` making `X` into the colimit of the `Dáµ¢`.
- `CategoryTheory.Limits.ColimitPresentation.bind`: Given a colimit presentation of `X` and
  colimit presentations of the components, this is the colimit presentation over the sigma type.

## TODOs:

- Dualise to obtain `LimitPresentation`.
- Refactor `TransfiniteCompositionOfShape` so that it extends `ColimitPresentation`.
-/

universe s t w v u

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]

/-- A colimit presentation of `X` over `J` is a diagram `{Dáµ¢}` in `C` and natural maps
`sáµ¢ : Dáµ¢ âŸ¶ X` making `X` into the colimit of the `Dáµ¢`. -/
structure ColimitPresentation (J : Type w) [Category.{t} J] (X : C) where
  /-- The diagram `{Dáµ¢}`. -/
  diag : J â¥¤ C
  /-- The natural maps `sáµ¢ : Dáµ¢ âŸ¶ X`. -/
  Î¹ : diag âŸ¶ (Functor.const J).obj X
  /-- `X` is the colimit of the `Dáµ¢` via `sáµ¢`. -/
  isColimit : IsColimit (Cocone.mk _ Î¹)

variable {J : Type w} [Category.{t} J] {X : C}

namespace ColimitPresentation

initialize_simps_projections ColimitPresentation (-isColimit)

/-- The cocone associated to a colimit presentation. -/
abbrev cocone (pres : ColimitPresentation J X) : Cocone pres.diag :=
  Cocone.mk _ pres.Î¹

/-- The canonical colimit presentation of any object over a point. -/
@[simps]
noncomputable
def self (X : C) : ColimitPresentation PUnit.{s + 1} X where
  diag := (Functor.const _).obj X
  Î¹ := ğŸ™ _
  isColimit := isColimitConstCocone _ _

/-- If `F` preserves colimits of shape `J`, it maps colimit presentations of `X` to
colimit presentations of `F(X)`. -/
@[simps]
noncomputable
def map (P : ColimitPresentation J X) {D : Type*} [Category D] (F : C â¥¤ D)
    [PreservesColimitsOfShape J F] : ColimitPresentation J (F.obj X) where
  diag := P.diag â‹™ F
  Î¹ := Functor.whiskerRight P.Î¹ F â‰« (F.constComp _ _).hom
  isColimit := (isColimitOfPreserves F P.isColimit).ofIsoColimit (Cocones.ext (.refl _) (by simp))

/-- Map a colimit presentation under an isomorphism. -/
@[simps]
def ofIso (P : ColimitPresentation J X) {Y : C} (e : X â‰… Y) : ColimitPresentation J Y where
  diag := P.diag
  Î¹ := P.Î¹ â‰« (Functor.const J).map e.hom
  isColimit := P.isColimit.ofIsoColimit (Cocones.ext e fun _ â†¦ rfl)

section

variable {J : Type*} {I : J â†’ Type*} [Category J] [âˆ€ j, Category (I j)]
  {D : J â¥¤ C} {P : âˆ€ j, ColimitPresentation (I j) (D.obj j)}

set_option linter.unusedVariables false in
/-- The type underlying the category used in the construction of the composition
of colimit presentations. This is simply `Î£ j, I j` but with a different category structure. -/
@[nolint unusedArguments]
def Total (P : âˆ€ j, ColimitPresentation (I j) (D.obj j)) : Type _ :=
  Î£ j, I j

variable (P) in
/-- Constructor for `Total` to guide type checking. -/
abbrev Total.mk (i : J) (k : I i) : Total P := âŸ¨i, kâŸ©

/-- Morphisms in the `Total` category. -/
@[ext]
structure Total.Hom (k l : Total P) where
  /-- The underlying morphism in the first component. -/
  base : k.1 âŸ¶ l.1
  /-- A morphism in `C`. -/
  hom : (P k.1).diag.obj k.2 âŸ¶ (P l.1).diag.obj l.2
  w : (P k.1).Î¹.app k.2 â‰« D.map base = hom â‰« (P l.1).Î¹.app l.2 := by cat_disch

attribute [reassoc] Total.Hom.w

/-- Composition of morphisms in the `Total` category. -/
@[simps]
def Total.Hom.comp {k l m : Total P} (f : k.Hom l) (g : l.Hom m) : k.Hom m where
  base := f.base â‰« g.base
  hom := f.hom â‰« g.hom
  w := by
    simp only [Functor.const_obj_obj, Functor.map_comp, Category.assoc]
    rw [f.w_assoc, g.w]

@[simps! id_base id_hom comp_base comp_hom]
instance : Category (Total P) where
  Hom := Total.Hom
  id _ := { base := ğŸ™ _, hom := ğŸ™ _ }
  comp := Total.Hom.comp

section Small

variable {J : Type w} {I : J â†’ Type w} [SmallCategory J] [âˆ€ j, SmallCategory (I j)]
  {D : J â¥¤ C} {P : âˆ€ j, ColimitPresentation (I j) (D.obj j)}

lemma Total.exists_hom_of_hom {j j' : J} (i : I j) (u : j âŸ¶ j')
    [IsFiltered (I j')] [IsFinitelyPresentable.{w} ((P j).diag.obj i)] :
    âˆƒ (i' : I j') (f : Total.mk P j i âŸ¶ Total.mk P j' i'), f.base = u := by
  obtain âŸ¨i', q, hqâŸ© := IsFinitelyPresentable.exists_hom_of_isColimit (P j').isColimit
    ((P j).Î¹.app i â‰« D.map u)
  use i', { base := u, hom := q, w := by simp [â† hq] }

instance [IsFiltered J] [âˆ€ j, IsFiltered (I j)] : Nonempty (Total P) := by
  obtain âŸ¨jâŸ© : Nonempty J := IsFiltered.nonempty
  obtain âŸ¨iâŸ© : Nonempty (I j) := IsFiltered.nonempty
  exact âŸ¨âŸ¨j, iâŸ©âŸ©

instance [IsFiltered J] [âˆ€ j, IsFiltered (I j)]
    [âˆ€ j i, IsFinitelyPresentable.{w} ((P j).diag.obj i)] :
    IsFiltered (Total P) where
  cocone_objs k l := by
    let a := IsFiltered.max k.1 l.1
    obtain âŸ¨a', f, hfâŸ© := Total.exists_hom_of_hom (P := P) k.2 (IsFiltered.leftToMax k.1 l.1)
    obtain âŸ¨b', g, hgâŸ© := Total.exists_hom_of_hom (P := P) l.2 (IsFiltered.rightToMax k.1 l.1)
    refine âŸ¨âŸ¨a, IsFiltered.max a' b'âŸ©, ?_, ?_, trivialâŸ©
    Â· exact f â‰« { base := ğŸ™ _, hom := (P _).diag.map (IsFiltered.leftToMax _ _) }
    Â· exact g â‰« { base := ğŸ™ _, hom := (P _).diag.map (IsFiltered.rightToMax _ _) }
  cocone_maps {k l} f g := by
    let a := IsFiltered.coeq f.base g.base
    obtain âŸ¨a', u, huâŸ© := Total.exists_hom_of_hom (P := P) l.2 (IsFiltered.coeqHom f.base g.base)
    have : (f.hom â‰« u.hom) â‰« (P _).Î¹.app _ = (g.hom â‰« u.hom) â‰« (P _).Î¹.app _ := by
      simp only [Category.assoc, Functor.const_obj_obj, â† u.w, â† f.w_assoc, â† g.w_assoc]
      rw [â† Functor.map_comp, hu, IsFiltered.coeq_condition f.base g.base]
      simp
    obtain âŸ¨j, p, q, hpqâŸ© := IsFinitelyPresentable.exists_eq_of_isColimit (P _).isColimit _ _ this
    dsimp at p q
    refine âŸ¨âŸ¨a, IsFiltered.coeq p qâŸ©,
      u â‰« { base := ğŸ™ _, hom := (P _).diag.map (p â‰« IsFiltered.coeqHom p q) }, ?_âŸ©
    apply Total.Hom.ext
    Â· simp [hu, IsFiltered.coeq_condition f.base g.base]
    Â· rw [Category.assoc] at hpq
      simp only [Functor.map_comp, comp_hom, reassoc_of% hpq]
      simp [â† Functor.map_comp, â† IsFiltered.coeq_condition]

/-- If `P` is a colimit presentation over `J` of `X` and for every `j` we are given a colimit
presentation `Qâ±¼` over `I j` of the `P.diag.obj j`, this is the refined colimit presentation of `X`
over `Total Q`. -/
@[simps]
def bind {X : C} (P : ColimitPresentation J X) (Q : âˆ€ j, ColimitPresentation (I j) (P.diag.obj j))
    [âˆ€ j, IsFiltered (I j)] [âˆ€ j i, IsFinitelyPresentable.{w} ((Q j).diag.obj i)] :
    ColimitPresentation (Total Q) X where
  diag.obj k := (Q k.1).diag.obj k.2
  diag.map {k l} f := f.hom
  Î¹.app k := (Q k.1).Î¹.app k.2 â‰« P.Î¹.app k.1
  Î¹.naturality {k l} u := by simp [â† u.w_assoc]
  isColimit.desc c := P.isColimit.desc
    { pt := c.pt
      Î¹.app j := (Q j).isColimit.desc
        { pt := c.pt
          Î¹.app i := c.Î¹.app âŸ¨j, iâŸ©
          Î¹.naturality {i i'} u := by
            let v : Total.mk Q j i âŸ¶ .mk _ j i' := { base := ğŸ™ _, hom := (Q _).diag.map u }
            simpa using c.Î¹.naturality v }
      Î¹.naturality {j j'} u := by
        refine (Q j).isColimit.hom_ext fun i â†¦ ?_
        simp only [Functor.const_obj_obj, Functor.const_obj_map, Category.comp_id,
          (Q j).isColimit.fac]
        obtain âŸ¨i', hom, rflâŸ© := Total.exists_hom_of_hom (P := Q) i u
        rw [reassoc_of% hom.w, (Q j').isColimit.fac]
        simpa using c.Î¹.naturality hom }
  isColimit.fac := fun c âŸ¨j, iâŸ© â†¦ by simp [P.isColimit.fac, (Q j).isColimit.fac]
  isColimit.uniq c m hm := by
    refine P.isColimit.hom_ext fun j â†¦ ?_
    simp only [Functor.const_obj_obj, P.isColimit.fac]
    refine (Q j).isColimit.hom_ext fun i â†¦ ?_
    simpa [(Q j).isColimit.fac] using hm (.mk _ j i)

end Small

end

end CategoryTheory.Limits.ColimitPresentation
