/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Limits.ColimitLimit
import Mathlib.CategoryTheory.Limits.Preserves.FunctorCategory
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Shapes.FiniteLimits
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Products.Bifunctor
import Mathlib.Data.Countable.Small

/-!
# Filtered colimits commute with finite limits.

We show that for a functor `F : J Ã— K â¥¤ Type v`, when `J` is finite and `K` is filtered,
the universal morphism `colimitLimitToLimitColimit F` comparing the
colimit (over `K`) of the limits (over `J`) with the limit of the colimits is an isomorphism.

(In fact, to prove that it is injective only requires that `J` has finitely many objects.)

## References
* Borceux, Handbook of categorical algebra 1, Theorem 2.13.4
* [Stacks: Filtered colimits](https://stacks.math.columbia.edu/tag/002W)
-/

-- Various pieces of algebra that have previously been spuriously imported here:
assert_not_exists map_ne_zero MonoidWithZero

universe w vâ‚ vâ‚‚ v uâ‚ uâ‚‚ u

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits.Types
  CategoryTheory.Limits.Types.FilteredColimit CategoryTheory.Functor

namespace CategoryTheory.Limits

section

variable {J : Type uâ‚} {K : Type uâ‚‚} [Category.{vâ‚} J] [Category.{vâ‚‚} K] [Small.{v} K]

/-- `(G â‹™ lim).obj j` = `limit (G.obj j)` definitionally, so this
is just a variant of `limit_ext'`. -/
@[ext] lemma comp_lim_obj_ext {j : J} {G : J â¥¤ K â¥¤ Type v} (x y : (G â‹™ lim).obj j)
    (w : âˆ€ (k : K), limit.Ï€ (G.obj j) k x = limit.Ï€ (G.obj j) k y) : x = y :=
  limit_ext _ x y w

variable (F : J Ã— K â¥¤ Type v)

open CategoryTheory.Prod

variable [IsFiltered K]

section

/-!
Injectivity doesn't need that we have finitely many morphisms in `J`,
only that there are finitely many objects.
-/

variable [Finite J]

/-- This follows this proof from
* Borceux, Handbook of categorical algebra 1, Theorem 2.13.4
-/
theorem colimitLimitToLimitColimit_injective :
    Function.Injective (colimitLimitToLimitColimit F) := by
  classical
    cases nonempty_fintype J
    -- Suppose we have two terms `x y` in the colimit (over `K`) of the limits (over `J`),
    -- and that these have the same image under `colimitLimitToLimitColimit F`.
    intro x y h
    -- These elements of the colimit have representatives somewhere:
    obtain âŸ¨kx, x, rflâŸ© := jointly_surjective' x
    obtain âŸ¨ky, y, rflâŸ© := jointly_surjective' y
    dsimp at x y
    -- Since the images of `x` and `y` are equal in a limit, they are equal componentwise
    -- (indexed by `j : J`),
    replace h := fun j => congr_arg (limit.Ï€ (curry.obj F â‹™ colim) j) h
    -- and they are equations in a filtered colimit,
    -- so for each `j` we have some place `k j` to the right of both `kx` and `ky`
    simp? [colimit_eq_iff] at h says
      simp only [comp_obj, colim_obj, Î¹_colimitLimitToLimitColimit_Ï€_apply,
        colimit_eq_iff, curry_obj_obj_obj, curry_obj_obj_map] at h
    let k j := (h j).choose
    let f : âˆ€ j, kx âŸ¶ k j := fun j => (h j).choose_spec.choose
    let g : âˆ€ j, ky âŸ¶ k j := fun j => (h j).choose_spec.choose_spec.choose
    -- where the images of the components of the representatives become equal:
    have w :
      âˆ€ j, F.map (ğŸ™ j Ã—â‚˜ f j) (limit.Ï€ ((curry.obj (swap K J â‹™ F)).obj kx) j x) =
          F.map (ğŸ™ j Ã—â‚˜ g j) (limit.Ï€ ((curry.obj (swap K J â‹™ F)).obj ky) j y) :=
      fun j => (h j).choose_spec.choose_spec.choose_spec
    -- We now use that `K` is filtered, picking some point to the right of all these
    -- morphisms `f j` and `g j`.
    let O : Finset K := Finset.univ.image k âˆª {kx, ky}
    have kxO : kx âˆˆ O := Finset.mem_union.mpr (Or.inr (by simp))
    have kyO : ky âˆˆ O := Finset.mem_union.mpr (Or.inr (by simp))
    have kjO : âˆ€ j, k j âˆˆ O := fun j => Finset.mem_union.mpr (Or.inl (by simp))
    let H : Finset (Î£' (X Y : K) (_ : X âˆˆ O) (_ : Y âˆˆ O), X âŸ¶ Y) :=
      (Finset.univ.image fun j : J =>
          âŸ¨kx, k j, kxO, Finset.mem_union.mpr (Or.inl (by simp)), f jâŸ©) âˆª
        Finset.univ.image fun j : J => âŸ¨ky, k j, kyO, Finset.mem_union.mpr (Or.inl (by simp)), g jâŸ©
    obtain âŸ¨S, T, WâŸ© := IsFiltered.sup_exists O H
    have fH : âˆ€ j, (âŸ¨kx, k j, kxO, kjO j, f jâŸ© : Î£' (X Y : K) (_ : X âˆˆ O) (_ : Y âˆˆ O), X âŸ¶ Y) âˆˆ H :=
      fun j =>
      Finset.mem_union.mpr
        (Or.inl
          (by
            simp only [true_and, Finset.mem_univ,
              Finset.mem_image]
            refine âŸ¨j, ?_âŸ©
            simp only ))
    have gH :
      âˆ€ j, (âŸ¨ky, k j, kyO, kjO j, g jâŸ© : Î£' (X Y : K) (_ : X âˆˆ O) (_ : Y âˆˆ O), X âŸ¶ Y) âˆˆ H :=
      fun j =>
      Finset.mem_union.mpr
        (Or.inr
          (by
            simp only [true_and, Finset.mem_univ,
              Finset.mem_image]
            refine âŸ¨j, ?_âŸ©
            simp only))
    -- Our goal is now an equation between equivalence classes of representatives of a colimit,
    -- and so it suffices to show those representative become equal somewhere, in particular at `S`.
    apply colimit_sound' (T kxO) (T kyO)
    -- We can check if two elements of a limit (in `Type`)
    -- are equal by comparing them componentwise.
    ext j
    -- Now it's just a calculation using `W` and `w`.
    simp only [Functor.comp_map]
    rw [â† W _ _ (fH j), â† W _ _ (gH j)]
    simp [w]

end

end

section

variable {J : Type uâ‚} {K : Type uâ‚‚} [SmallCategory J] [Category.{vâ‚‚} K] [Small.{v} K]

variable [FinCategory J]

variable (F : J Ã— K â¥¤ Type v)

open CategoryTheory.Prod

variable [IsFiltered K]

/-- This follows this proof from `Borceux, Handbook of categorical algebra 1, Theorem 2.13.4`
although with different names.
-/
theorem colimitLimitToLimitColimit_surjective :
    Function.Surjective (colimitLimitToLimitColimit F) := by
  classical
    -- We begin with some element `x` in the limit (over J) over the colimits (over K),
    intro x
    -- This consists of some coherent family of elements in the various colimits,
    -- and so our first task is to pick representatives of these elements.
    have z := fun j => jointly_surjective' (limit.Ï€ (curry.obj F â‹™ Limits.colim) j x)
    -- `k : J âŸ¶ K` records where the representative of the
    -- element in the `j`-th element of `x` lives
    let k : J â†’ K := fun j => (z j).choose
    -- `y j : F.obj (j, k j)` is the representative
    let y : âˆ€ j, F.obj (j, k j) := fun j => (z j).choose_spec.choose
    -- and we record that these representatives, when mapped back into the relevant colimits,
    -- are actually the components of `x`.
    have e : âˆ€ j,
        colimit.Î¹ ((curry.obj F).obj j) (k j) (y j) = limit.Ï€ (curry.obj F â‹™ Limits.colim) j x :=
      fun j => (z j).choose_spec.choose_spec
    clear_value k y
    -- A little tidying up of things we no longer need.
    clear z
    -- As a first step, we use that `K` is filtered to pick some point `k' : K` above all the `k j`
    let k' : K := IsFiltered.sup (Finset.univ.image k) âˆ…
    -- and name the morphisms as `g j : k j âŸ¶ k'`.
    have g : âˆ€ j, k j âŸ¶ k' := fun j => IsFiltered.toSup (Finset.univ.image k) âˆ… (by simp)
    clear_value k'
    -- Recalling that the components of `x`, which are indexed by `j : J`, are "coherent",
    -- in other words preserved by morphisms in the `J` direction,
    -- we see that for any morphism `f : j âŸ¶ j'` in `J`,
    -- the images of `y j` and `y j'`, when mapped to `F.obj (j', k')` respectively by
    -- `(f, g j)` and `(ğŸ™ j', g j')`, both represent the same element in the colimit.
    have w :
      âˆ€ {j j' : J} (f : j âŸ¶ j'),
        colimit.Î¹ ((curry.obj F).obj j') k' (F.map (ğŸ™ j' Ã—â‚˜ g j') (y j')) =
          colimit.Î¹ ((curry.obj F).obj j') k' (F.map (f Ã—â‚˜ g j) (y j)) := by
      intro j j' f
      simp only [Colimit.w_apply, â† Bifunctor.diagonal', â† curry_obj_obj_map, â† curry_obj_map_app]
      rw [types_comp_apply, Colimit.w_apply, e, â† Limit.w_apply.{uâ‚, v, uâ‚} f, â† e]
      simp [Types.Colimit.Î¹_map_apply]
    -- Because `K` is filtered, we can restate this as saying that
    -- for each such `f`, there is some place to the right of `k'`
    -- where these images of `y j` and `y j'` become equal.
    simp_rw [colimit_eq_iff] at w
    -- We take a moment to restate `w` more conveniently.
    let kf : âˆ€ {j j'} (_ : j âŸ¶ j'), K := fun f => (w f).choose
    let gf : âˆ€ {j j'} (f : j âŸ¶ j'), k' âŸ¶ kf f := fun f => (w f).choose_spec.choose
    let hf : âˆ€ {j j'} (f : j âŸ¶ j'), k' âŸ¶ kf f := fun f =>
      (w f).choose_spec.choose_spec.choose
    have wf :
      âˆ€ {j j'} (f : j âŸ¶ j'),
        F.map (ğŸ™ j' Ã—â‚˜ (g j' â‰« gf f)) (y j') = F.map (f Ã—â‚˜ (g j â‰« hf f)) (y j) :=
      fun {j j'} f => by
      have q :
        ((curry.obj F).obj j').map (gf f) (F.map (ğŸ™ j' Ã—â‚˜ g j') (y j')) =
          ((curry.obj F).obj j').map (hf f) (F.map (f Ã—â‚˜ g j) (y j)) :=
        (w f).choose_spec.choose_spec.choose_spec
      dsimp only [curry_obj_obj_map, curry_obj_obj_map] at q
      simp_rw [â† FunctorToTypes.map_comp_apply, CategoryStruct.comp] at q
      convert q <;> simp only [comp_id]
    clear_value kf gf hf
    -- and clean up some things that are no longer needed.
    clear w
    -- We're now ready to use the fact that `K` is filtered a second time,
    -- picking some place to the right of all of
    -- the morphisms `gf f : k' âŸ¶ kh f` and `hf f : k' âŸ¶ kf f`.
    -- At this point we're relying on there being only finitely morphisms in `J`.
    let O :=
      (Finset.univ.biUnion fun j => Finset.univ.biUnion fun j' => Finset.univ.image
        (@kf j j')) âˆª {k'}
    have kfO : âˆ€ {j j'} (f : j âŸ¶ j'), kf f âˆˆ O := fun {j} {j'} f =>
      Finset.mem_union.mpr
        (Or.inl
          (Finset.mem_biUnion.mpr âŸ¨j, Finset.mem_univ j,
            Finset.mem_biUnion.mpr âŸ¨j', Finset.mem_univ j',
              Finset.mem_image.mpr âŸ¨f, Finset.mem_univ _, rflâŸ©âŸ©âŸ©))
    have k'O : k' âˆˆ O := Finset.mem_union.mpr (Or.inr (Finset.mem_singleton.mpr rfl))
    let H : Finset (Î£' (X Y : K) (_ : X âˆˆ O) (_ : Y âˆˆ O), X âŸ¶ Y) :=
      Finset.univ.biUnion fun j : J =>
        Finset.univ.biUnion fun j' : J =>
          Finset.univ.biUnion fun f : j âŸ¶ j' =>
            {âŸ¨k', kf f, k'O, kfO f, gf fâŸ©, âŸ¨k', kf f, k'O, kfO f, hf fâŸ©}
    obtain âŸ¨k'', i', s'âŸ© := IsFiltered.sup_exists O H
    -- We then restate this slightly more conveniently, as a family of morphism `i f : kf f âŸ¶ k''`,
    -- satisfying `gf f â‰« i f = hf f' â‰« i f'`.
    let i : âˆ€ {j j'} (f : j âŸ¶ j'), kf f âŸ¶ k'' := fun {j} {j'} f => i' (kfO f)
    have s : âˆ€ {jâ‚ jâ‚‚ jâ‚ƒ jâ‚„} (f : jâ‚ âŸ¶ jâ‚‚) (f' : jâ‚ƒ âŸ¶ jâ‚„), gf f â‰« i f = hf f' â‰« i f' := by
      intro jâ‚ jâ‚‚ jâ‚ƒ jâ‚„ f f'
      rw [s', s']
      Â· exact k'O
      Â· exact Finset.mem_biUnion.mpr âŸ¨jâ‚ƒ, Finset.mem_univ _,
          Finset.mem_biUnion.mpr âŸ¨jâ‚„, Finset.mem_univ _,
            Finset.mem_biUnion.mpr âŸ¨f', Finset.mem_univ _, by
              -- This works by `simp`, but has very high variation in heartbeats.
              rw [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, PSigma.mk.injEq, heq_eq_eq,
                PSigma.mk.injEq, heq_eq_eq, PSigma.mk.injEq, heq_eq_eq, eq_self, true_and, eq_self,
                true_and, eq_self, true_and, eq_self, true_and, Finset.mem_singleton, eq_self,
                or_true]
              trivialâŸ©âŸ©âŸ©
      Â· exact Finset.mem_biUnion.mpr âŸ¨jâ‚, Finset.mem_univ _,
          Finset.mem_biUnion.mpr âŸ¨jâ‚‚, Finset.mem_univ _,
            Finset.mem_biUnion.mpr âŸ¨f, Finset.mem_univ _, by
              -- This works by `simp`, but has very high variation in heartbeats.
              rw [Finset.mem_insert, PSigma.mk.injEq, heq_eq_eq, PSigma.mk.injEq, heq_eq_eq,
                PSigma.mk.injEq, heq_eq_eq, PSigma.mk.injEq, heq_eq_eq, eq_self, true_and, eq_self,
                true_and, eq_self, true_and, eq_self, true_and, Finset.mem_singleton, eq_self,
                true_or]
              trivialâŸ©âŸ©âŸ©
    clear_value i
    clear s' i' H kfO k'O O
    -- We're finally ready to construct the pre-image, and verify it really maps to `x`.
    -- âŠ¢ âˆƒ a, colimitLimitToLimitColimit F a = x
    fconstructor
    Â· -- We construct the pre-image (which, recall is meant to be a point
      -- in the colimit (over `K`) of the limits (over `J`)) via a representative at `k''`.
      apply colimit.Î¹ (curry.obj (swap K J â‹™ F) â‹™ Limits.lim) k'' _
      dsimp
      -- This representative is meant to be an element of a limit,
      -- so we need to construct a family of elements in `F.obj (j, k'')` for varying `j`,
      -- then show that are coherent with respect to morphisms in the `j` direction.
      apply Limit.mk
      swap
      Â·-- We construct the elements as the images of the `y j`.
        exact fun j => F.map (ğŸ™ j Ã—â‚˜ (g j â‰« gf (ğŸ™ j) â‰« i (ğŸ™ j))) (y j)
      Â· -- After which it's just a calculation, using `s` and `wf`, to see they are coherent.
        dsimp
        intro j j' f
        simp only [â† FunctorToTypes.map_comp_apply, prod_comp, id_comp, comp_id]
        calc
          F.map (f Ã—â‚˜ (g j â‰« gf (ğŸ™ j) â‰« i (ğŸ™ j))) (y j) =
              F.map (f Ã—â‚˜ (g j â‰« hf f â‰« i f)) (y j) := by
            rw [s (ğŸ™ j) f]
          _ =
              F.map (ğŸ™ j' Ã—â‚˜ i f) (F.map (f Ã—â‚˜ (g j â‰« hf f)) (y j)) := by
            rw [â† FunctorToTypes.map_comp_apply, prod_comp, comp_id, assoc]
          _ =
              F.map (ğŸ™ j' Ã—â‚˜ i f) (F.map (ğŸ™ j' Ã—â‚˜ (g j' â‰« gf f)) (y j')) := by
            rw [â† wf f]
          _ = F.map (ğŸ™ j' Ã—â‚˜ (g j' â‰« gf f â‰« i f)) (y j') := by
            rw [â† FunctorToTypes.map_comp_apply, prod_comp, id_comp, assoc]
          _ = F.map (ğŸ™ j' Ã—â‚˜ (g j' â‰« gf (ğŸ™ j') â‰« i (ğŸ™ j'))) (y j') := by
            rw [s f (ğŸ™ j'), â† s (ğŸ™ j') (ğŸ™ j')]
    -- Finally we check that this maps to `x`.
    Â· -- We can do this componentwise:
      apply limit_ext
      intro j
      -- and as each component is an equation in a colimit, we can verify it by
      -- pointing out the morphism which carries one representative to the other:
      simp only [id, â† e, Limits.Î¹_colimitLimitToLimitColimit_Ï€_apply,
          colimit_eq_iff, Bifunctor.map_id_comp, types_comp_apply, curry_obj_obj_map,
          Functor.comp_obj, colim_obj, Limit.Ï€_mk]
      refine âŸ¨k'', ğŸ™ k'', g j â‰« gf (ğŸ™ j) â‰« i (ğŸ™ j), ?_âŸ©
      rw [Bifunctor.map_id_comp, Bifunctor.map_id_comp, types_comp_apply, types_comp_apply,
        Bifunctor.map_id, types_id_apply]

instance colimitLimitToLimitColimit_isIso : IsIso (colimitLimitToLimitColimit F) :=
  (isIso_iff_bijective _).mpr
    âŸ¨colimitLimitToLimitColimit_injective F, colimitLimitToLimitColimit_surjective FâŸ©

instance colimitLimitToLimitColimitCone_iso (F : J â¥¤ K â¥¤ Type v) :
    IsIso (colimitLimitToLimitColimitCone F) := by
  have : IsIso (colimitLimitToLimitColimitCone F).hom := by
    suffices IsIso (colimitLimitToLimitColimit (uncurry.obj F) â‰«
        lim.map (whiskerRight (currying.unitIso.app F).inv colim)) by
      apply IsIso.comp_isIso
    infer_instance
  apply Cones.cone_iso_of_hom_iso

noncomputable instance filtered_colim_preservesFiniteLimits_of_types :
    PreservesFiniteLimits (colim : (K â¥¤ Type v) â¥¤ _) := by
  apply preservesFiniteLimits_of_preservesFiniteLimitsOfSize.{vâ‚‚}
  intro J _ _
  refine âŸ¨fun {F} => âŸ¨fun {c} hc => âŸ¨IsLimit.ofIsoLimit (limit.isLimit _) ?_âŸ©âŸ©âŸ©
  symm
  trans colim.mapCone (limit.cone F)
  Â· exact Functor.mapIso _ (hc.uniqueUpToIso (limit.isLimit F))
  Â· exact asIso (colimitLimitToLimitColimitCone F)

variable {C : Type u} [Category.{v} C] [HasForget.{v} C]

section

variable [HasLimitsOfShape J C] [HasColimitsOfShape K C]
variable [ReflectsLimitsOfShape J (forget C)] [PreservesColimitsOfShape K (forget C)]
variable [PreservesLimitsOfShape J (forget C)]

noncomputable instance filtered_colim_preservesFiniteLimits :
    PreservesLimitsOfShape J (colim : (K â¥¤ C) â¥¤ _) :=
  haveI : PreservesLimitsOfShape J ((colim : (K â¥¤ C) â¥¤ _) â‹™ forget C) :=
    preservesLimitsOfShape_of_natIso (preservesColimitNatIso _).symm
  preservesLimitsOfShape_of_reflects_of_preserves _ (forget C)

end

attribute [local instance] reflectsLimitsOfShape_of_reflectsIsomorphisms

noncomputable instance [PreservesFiniteLimits (forget C)] [PreservesColimitsOfShape K (forget C)]
    [HasFiniteLimits C] [HasColimitsOfShape K C] [(forget C).ReflectsIsomorphisms] :
    PreservesFiniteLimits (colim : (K â¥¤ C) â¥¤ _) := by
  apply preservesFiniteLimits_of_preservesFiniteLimitsOfSize.{v}
  intro J _ _
  infer_instance

end

section

variable {C : Type u} [Category.{v} C]
variable {J : Type uâ‚} [Category.{vâ‚} J]
variable {K : Type uâ‚‚} [Category.{vâ‚‚} K]
variable [HasLimitsOfShape J C] [HasColimitsOfShape K C]
variable [PreservesLimitsOfShape J (colim : (K â¥¤ C) â¥¤ _)]

/-- A curried version of the fact that filtered colimits commute with finite limits. -/
noncomputable def colimitLimitIso (F : J â¥¤ K â¥¤ C) : colimit (limit F) â‰… limit (colimit F.flip) :=
  (isLimitOfPreserves colim (limit.isLimit _)).conePointUniqueUpToIso (limit.isLimit _) â‰ªâ‰«
    HasLimit.isoOfNatIso (colimitFlipIsoCompColim _).symm

@[reassoc (attr := simp)]
theorem Î¹_colimitLimitIso_limit_Ï€ (F : J â¥¤ K â¥¤ C) (a) (b) :
    colimit.Î¹ (limit F) a â‰« (colimitLimitIso F).hom â‰« limit.Ï€ (colimit F.flip) b =
      (limit.Ï€ F b).app a â‰« (colimit.Î¹ F.flip a).app b := by
  dsimp [colimitLimitIso]
  simp only [Functor.mapCone_Ï€_app, Iso.symm_hom,
    Limits.limit.conePointUniqueUpToIso_hom_comp_assoc, Limits.limit.cone_Ï€,
    Limits.colimit.Î¹_map_assoc, Limits.colimitFlipIsoCompColim_inv_app, assoc,
    Limits.HasLimit.isoOfNatIso_hom_Ï€]
  congr 1
  simp only [â† Category.assoc, Iso.comp_inv_eq,
    Limits.colimitObjIsoColimitCompEvaluation_Î¹_app_hom,
    Limits.HasColimit.isoOfNatIso_Î¹_hom]
  simp

end

end CategoryTheory.Limits
