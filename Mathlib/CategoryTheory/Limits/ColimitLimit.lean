/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Limits.Types.Colimits
import Mathlib.CategoryTheory.Limits.Types.Limits
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Limits.FunctorCategory.Basic

/-!
# The morphism comparing a colimit of limits with the corresponding limit of colimits.

For `F : J Ã— K â¥¤ C` there is always a morphism $\colim_k \lim_j F(j,k) â†’ \lim_j \colim_k F(j, k)$.
While it is not usually an isomorphism, with additional hypotheses on `J` and `K` it may be,
in which case we say that "colimits commute with limits".

The prototypical example, proved in `CategoryTheory.Limits.FilteredColimitCommutesFiniteLimit`,
is that when `C = Type`, filtered colimits commute with finite limits.

## References
* Borceux, Handbook of categorical algebra 1, Section 2.13
* [Stacks: Filtered colimits](https://stacks.math.columbia.edu/tag/002W)
-/


universe vâ‚ vâ‚‚ v uâ‚ uâ‚‚ u

open CategoryTheory

namespace CategoryTheory.Limits

variable {J : Type uâ‚} {K : Type uâ‚‚} [Category.{vâ‚} J] [Category.{vâ‚‚} K]
variable {C : Type u} [Category.{v} C]
variable (F : J Ã— K â¥¤ C)

open CategoryTheory.prod

theorem map_id_left_eq_curry_map {j : J} {k k' : K} {f : k âŸ¶ k'} :
    F.map ((ðŸ™ j, f) : (j, k) âŸ¶ (j, k')) = ((curry.obj F).obj j).map f :=
  rfl

theorem map_id_right_eq_curry_swap_map {j j' : J} {f : j âŸ¶ j'} {k : K} :
    F.map ((f, ðŸ™ k) : (j, k) âŸ¶ (j', k)) = ((curry.obj (Prod.swap K J â‹™ F)).obj k).map f :=
  rfl

variable [HasLimitsOfShape J C]
variable [HasColimitsOfShape K C]

/-- The universal morphism
$\colim_k \lim_j F(j,k) â†’ \lim_j \colim_k F(j, k)$.
-/
noncomputable def colimitLimitToLimitColimit :
    colimit (curry.obj (Prod.swap K J â‹™ F) â‹™ lim) âŸ¶ limit (curry.obj F â‹™ colim) :=
  limit.lift (curry.obj F â‹™ colim)
    { pt := _
      Ï€ :=
        { app := fun j =>
            colimit.desc (curry.obj (Prod.swap K J â‹™ F) â‹™ lim)
              { pt := _
                Î¹ :=
                  { app := fun k =>
                      limit.Ï€ ((curry.obj (Prod.swap K J â‹™ F)).obj k) j â‰«
                        colimit.Î¹ ((curry.obj F).obj j) k
                    naturality := by
                      intro k k' f
                      simp only [Functor.comp_obj, lim_obj, colimit.cocone_x,
                        Functor.const_obj_obj, Functor.comp_map, lim_map,
                        curry_obj_obj_obj, Prod.swap_obj, limMap_Ï€_assoc, curry_obj_map_app,
                        Prod.swap_map, Functor.const_obj_map, Category.comp_id]
                      rw [map_id_left_eq_curry_map, colimit.w] } }
          naturality := by
            intro j j' f
            dsimp
            ext k
            simp only [Functor.comp_obj, lim_obj, Category.id_comp, colimit.Î¹_desc,
              colimit.Î¹_desc_assoc, Category.assoc, Î¹_colimMap,
              curry_obj_obj_obj, curry_obj_map_app]
            rw [map_id_right_eq_curry_swap_map, limit.w_assoc] } }

/-- Since `colimit_limit_to_limit_colimit` is a morphism from a colimit to a limit,
this lemma characterises it.
-/
@[reassoc (attr := simp)]
theorem Î¹_colimitLimitToLimitColimit_Ï€ (j) (k) :
    colimit.Î¹ _ k â‰« colimitLimitToLimitColimit F â‰« limit.Ï€ _ j =
      limit.Ï€ ((curry.obj (Prod.swap K J â‹™ F)).obj k) j â‰« colimit.Î¹ ((curry.obj F).obj j) k := by
  dsimp [colimitLimitToLimitColimit]
  simp

@[simp]
theorem Î¹_colimitLimitToLimitColimit_Ï€_apply [Small.{v} J] [Small.{v} K] (F : J Ã— K â¥¤ Type v)
    (j : J) (k : K) (f) : limit.Ï€ (curry.obj F â‹™ colim) j
        (colimitLimitToLimitColimit F (colimit.Î¹ (curry.obj (Prod.swap K J â‹™ F) â‹™ lim) k f)) =
      colimit.Î¹ ((curry.obj F).obj j) k (limit.Ï€ ((curry.obj (Prod.swap K J â‹™ F)).obj k) j f) := by
  dsimp [colimitLimitToLimitColimit]
  rw [Types.Limit.lift_Ï€_apply]
  dsimp only
  rw [Types.Colimit.Î¹_desc_apply]
  dsimp

/-- The map `colimit_limit_to_limit_colimit` realized as a map of cones. -/
@[simps]
noncomputable def colimitLimitToLimitColimitCone (G : J â¥¤ K â¥¤ C) [HasLimit G] :
    colim.mapCone (limit.cone G) âŸ¶ limit.cone (G â‹™ colim) where
  hom :=
    colim.map (limitIsoSwapCompLim G).hom â‰«
      colimitLimitToLimitColimit (uncurry.obj G :) â‰«
        lim.map (whiskerRight (currying.unitIso.app G).inv colim)
  w j := by
    dsimp
    ext1 k
    simp only [Category.assoc, limMap_Ï€, Functor.comp_obj, colim_obj, whiskerRight_app,
      colim_map, Î¹_colimMap_assoc, lim_obj, limitIsoSwapCompLim_hom_app,
      Î¹_colimitLimitToLimitColimit_Ï€_assoc, curry_obj_obj_obj, Prod.swap_obj,
      uncurry_obj_obj, Î¹_colimMap, currying_unitIso_inv_app_app_app, Category.id_comp,
      limMap_Ï€_assoc, Functor.flip_obj_obj, flipIsoCurrySwapUncurry_hom_app_app]
    erw [limitObjIsoLimitCompEvaluation_hom_Ï€_assoc]

end CategoryTheory.Limits
