/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Kenny Lau
-/

import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# Formal Coproducts

In this file we construct the category of formal coproducts given a category.

## Main definitions

* `FormalCoproduct`: the category of formal coproducts, which are indexed sets of objects in a
  category. A morphism `âˆ i : X.I, X.obj i âŸ¶ âˆ j : Y.I, Y.obj j` is given by a function
  `f : X.I â†’ Y.I` and maps `X.obj i âŸ¶ Y.obj (f i)` for each `i : X.I`.
* `FormalCoproduct.eval : (Cáµ’áµ– â¥¤ A) â¥¤ ((FormalCoproduct C)áµ’áµ– â¥¤ A)`:
  the universal property that a presheaf on `C` where the target category has arbitrary coproducts,
  can be extended to a presheaf on `FormalCoproduct C`.

-/


universe w wâ‚ wâ‚‚ wâ‚ƒ v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

open Opposite

namespace CategoryTheory

namespace Limits

variable {C : Type u} [Category.{v} C] (A : Type uâ‚) [Category.{vâ‚} A]

variable (C) in
/-- A formal coproduct is an indexed set of objects. -/
structure FormalCoproduct where
  I : Type w
  obj (i : I) : C

namespace FormalCoproduct

structure Hom (X Y : FormalCoproduct.{w} C) where
  f : X.I â†’ Y.I
  Ï† (i : X.I) : X.obj i âŸ¶ Y.obj (f i)

-- this category identifies to the fullsubcategory of the category of
-- presheaves of sets on `C` which are coproducts of representable presheaves
@[simps!] instance category : Category (FormalCoproduct.{w} C) where
  Hom := Hom
  -- ğŸ™ should be eqToHom -- that way lean doesnt check objects def eq
  id X := { f := id, Ï† := fun _ â†¦ ğŸ™ _ }
  comp Î± Î² := { f := Î².f âˆ˜ Î±.f, Ï† := fun _ â†¦ Î±.Ï† _ â‰« Î².Ï† _ }

@[ext (iff := false)]
lemma hom_ext {X Y : FormalCoproduct.{w} C} {f g : X âŸ¶ Y} (hâ‚ : f.f = g.f)
    (hâ‚‚ : âˆ€ (i : X.I), f.Ï† i â‰« eqToHom (by rw [hâ‚]) = g.Ï† i) : f = g := by
  obtain âŸ¨f, FâŸ© := f
  obtain âŸ¨g, GâŸ© := g
  obtain rfl : f = g := hâ‚
  obtain rfl : F = G := by ext i; simpa using hâ‚‚ i
  rfl

lemma hom_ext_iff {X Y : FormalCoproduct.{w} C} (f g : X âŸ¶ Y) :
    f = g â†” âˆƒ hâ‚ : f.f = g.f, âˆ€ (i : X.I), f.Ï† i â‰« eqToHom (by rw [hâ‚]) = g.Ï† i :=
  âŸ¨(Â· â–¸ by simp), fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ hom_ext hâ‚ hâ‚‚âŸ©

lemma hom_ext_iff' {X Y : FormalCoproduct.{w} C} (f g : X âŸ¶ Y) :
    f = g â†” âˆ€ i : X.I, âˆƒ hâ‚ : f.f i = g.f i, f.Ï† i â‰« eqToHom (by rw [hâ‚]) = g.Ï† i :=
  âŸ¨(Â· â–¸ by simp), fun h â†¦ hom_ext (funext fun i â†¦ (h i).fst) fun i â†¦ (h i).sndâŸ©

@[simps!] def isoOfComponents {X Y : FormalCoproduct.{w} C} (e : X.I â‰ƒ Y.I)
    (h : âˆ€ i, X.obj i â‰… Y.obj (e i)) : X â‰… Y where
  hom := { f := e, Ï† := fun i â†¦ (h i).hom }
  inv := { f := e.symm, Ï† := fun i â†¦ eqToHom (by simp) â‰« (h (e.symm i)).inv }
  hom_inv_id := by ext <;> aesop
  inv_hom_id := by ext <;> aesop

variable (C) in
@[simps!] def of : C â¥¤ FormalCoproduct.{w} C where
  obj X := âŸ¨PUnit, fun _ â†¦ XâŸ©
  map f := âŸ¨fun _ â†¦ PUnit.unit, fun _ â†¦ fâŸ©

section ofHom

variable {X : C} {Y : FormalCoproduct.{w} C}

def ofHom.mk (i : Y.I) (f : X âŸ¶ Y.obj i) : (of C).obj X âŸ¶ Y :=
  âŸ¨fun _ â†¦ i, fun _ â†¦ fâŸ©

def ofHom.fst (f : (of C).obj X âŸ¶ Y) : Y.I :=
  f.f PUnit.unit

def ofHom.snd (f : (of C).obj X âŸ¶ Y) :
    X âŸ¶ Y.obj (ofHom.fst f) :=
  f.Ï† PUnit.unit

lemma ofHom.mk_fst_snd (f : (of C).obj X âŸ¶ Y) : ofHom.mk (ofHom.fst f) (ofHom.snd f) = f := by
  ext <;> aesop

end ofHom

-- This is probably some form of adjunction.
def ofHomEquiv (X : C) (Y : FormalCoproduct.{w} C) :
    ((of C).obj X âŸ¶ Y) â‰ƒ (i : Y.I) Ã— (X âŸ¶ Y.obj i) where
  toFun f := âŸ¨ofHom.fst f, ofHom.snd fâŸ©
  invFun f := ofHom.mk f.1 f.2
  left_inv f := ofHom.mk_fst_snd f
  right_inv _ := rfl

def fullyFaithfulOf : (of C).FullyFaithful where
  preimage f := f.Ï† PUnit.unit

instance : (of C).Full :=
  fullyFaithfulOf.full

instance : (of C).Faithful :=
  fullyFaithfulOf.faithful


section Coproduct

variable (ğ’œ : Type w) (f : ğ’œ â†’ FormalCoproduct.{w} C) (t X : FormalCoproduct.{w} C)

def cofan : Cofan f :=
  Cofan.mk âŸ¨(i : ğ’œ) Ã— (f i).I, fun p â†¦ (f p.1).obj p.2âŸ©
    fun i â†¦ âŸ¨fun x â†¦ âŸ¨i, xâŸ©, fun x â†¦ ğŸ™ ((f i).obj x)âŸ©

section simp_lemmas

variable {ğ’œ f}

theorem cofan_inj (i : ğ’œ) : (cofan ğ’œ f).inj i = âŸ¨fun x â†¦ âŸ¨i, xâŸ©, fun x â†¦ ğŸ™ ((f i).obj x)âŸ© := rfl
-- JH: this is probably a bad theorem? At least not good for simp

@[simp] lemma cofan_inj_f_fst (i : ğ’œ) (x) : (((cofan ğ’œ f).inj i).f x).1 = i := rfl

@[simp] lemma cofan_inj_f_snd (i : ğ’œ) (x) : (((cofan ğ’œ f).inj i).f x).2 = x := rfl

@[simp] lemma cofan_inj_Ï† (i : ğ’œ) (x) : ((cofan ğ’œ f).inj i).Ï† x = ğŸ™ ((f i).obj x) := rfl

end simp_lemmas

@[simps!] def cofanHomEquiv :
    ((cofan ğ’œ f).pt âŸ¶ t) â‰ƒ ((i : ğ’œ) â†’ (f i âŸ¶ t)) where
  toFun m i := (cofan ğ’œ f).inj i â‰« m
  invFun s := âŸ¨fun p â†¦ (s p.1).f p.2, fun p â†¦ (s p.1).Ï† p.2âŸ©
  left_inv m := hom_ext rfl (fun âŸ¨i, xâŸ© â†¦ by simp [cofan_inj])
  right_inv p := by ext <;> simp

@[simps!] def isColimitCofan : IsColimit (cofan ğ’œ f) :=
  mkCofanColimit (cofan ğ’œ f) (fun t â†¦ (cofanHomEquiv _ _ _).symm t.inj)
    (fun t i â†¦ congrFun ((cofanHomEquiv _ _ _).right_inv t.inj) i)
    (fun _ _ h â†¦ (Equiv.eq_symm_apply _).2 (funext h))

instance : HasCoproducts.{w} (FormalCoproduct.{w} C) :=
  hasCoproducts_of_colimit_cofans _ (isColimitCofan _)

noncomputable def coproductIsoCofan : âˆ f â‰… (cofan ğ’œ f).pt :=
  colimit.isoColimitCocone âŸ¨_, isColimitCofan _ _âŸ©

variable {ğ’œ f} in
@[reassoc (attr := simp)] lemma Î¹_comp_coproductIsoCofan (i) :
    Sigma.Î¹ f i â‰« (coproductIsoCofan ğ’œ f).hom = (cofan ğ’œ f).inj i :=
  colimit.isoColimitCocone_Î¹_hom _ _

def toFun (X : FormalCoproduct.{w} C) : X.I â†’ FormalCoproduct.{w} C :=
  (of C).obj âˆ˜ X.obj

def coproductCoconeIsoSelf : (cofan X.I X.toFun).pt â‰… X :=
  isoOfComponents (Equiv.sigmaPUnit X.I) fun i â†¦ Iso.refl (X.obj i.fst)

@[reassoc (attr := simp)]
lemma inj_comp_coproductCoconeIsoSelf (i : X.I) :
    (cofan X.I X.toFun).inj i â‰« (coproductCoconeIsoSelf X).hom = ofHom.mk i (ğŸ™ (X.obj i)) :=
  hom_ext rfl (fun i => by simp; rfl)

@[simps!] noncomputable def coproductIsoSelf :
    âˆ X.toFun â‰… X :=
  coproductIsoCofan _ _ â‰ªâ‰« coproductCoconeIsoSelf X

@[reassoc (attr := simp)] lemma Î¹_comp_coproductIsoSelf (i : X.I) :
    Sigma.Î¹ _ i â‰« (coproductIsoSelf X).hom = ofHom.mk i (ğŸ™ (X.obj i)) := by
  simp [coproductIsoSelf]

end Coproduct


noncomputable
instance [HasTerminal C] (X : FormalCoproduct.{w} C) : Unique (X âŸ¶ (of C).obj (âŠ¤_ C)) :=
  âŸ¨âŸ¨âŸ¨fun _ â†¦ PUnit.unit, fun _ â†¦ terminal.from _âŸ©âŸ©,
  fun j â†¦ hom_ext (funext fun _ â†¦ rfl) (by aesop)âŸ©

instance [HasTerminal C] : HasTerminal (FormalCoproduct.{w} C) :=
  (IsTerminal.ofUnique <| (of C).obj (âŠ¤_ C)).hasTerminal


noncomputable section Pullback

variable [HasPullbacks C] (T : FormalCoproduct.{w} C)
  {X Y Z : FormalCoproduct.{w} C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z)

def pullbackCone : PullbackCone f g :=
  .mk (W := âŸ¨Function.Pullback f.f g.f, fun xy â†¦
      Limits.pullback (f.Ï† xy.1.1 â‰« eqToHom (by rw [xy.2])) (g.Ï† xy.1.2)âŸ©)
    âŸ¨fun i â†¦ i.1.1, fun i â†¦ pullback.fst _ _âŸ©
    âŸ¨fun i â†¦ i.1.2, fun i â†¦ pullback.snd _ _âŸ©
    (hom_ext (funext fun i â†¦ i.2) (by simp [pullback.condition]))

section simp_lemmas

@[simp] lemma pullbackCone_fst_f (i) : (pullbackCone f g).fst.f i = i.1.1 := rfl

@[simp] lemma pullbackCone_fst_Ï† (i) : (pullbackCone f g).fst.Ï† i = pullback.fst _ _ := rfl

@[simp] lemma pullbackCone_snd_f (i) : (pullbackCone f g).snd.f i = i.1.2 := rfl

@[simp] lemma pullbackCone_snd_Ï† (i) : (pullbackCone f g).snd.Ï† i = pullback.snd _ _ := rfl

@[simp] lemma pullbackCone_condition : (pullbackCone f g).fst â‰« f = (pullbackCone f g).snd â‰« g :=
  (pullbackCone f g).condition

end simp_lemmas

@[simps!] def homPullbackEquiv : (T âŸ¶ (pullbackCone f g).pt) â‰ƒ
    { p : (T âŸ¶ X) Ã— (T âŸ¶ Y) // p.1 â‰« f = p.2 â‰« g } where
  toFun m := âŸ¨âŸ¨m â‰« (pullbackCone f g).fst, m â‰« (pullbackCone f g).sndâŸ©, by simpâŸ©
  invFun s := âŸ¨fun i â†¦ âŸ¨(s.1.1.f i, s.1.2.f i), congrFun (congrArg Hom.f s.2) iâŸ©,
    fun i â†¦ pullback.lift (s.1.1.Ï† i) (s.1.2.Ï† i) (by simpa using ((hom_ext_iff _ _).1 s.2).2 i)âŸ©
  left_inv m := hom_ext rfl (fun i â†¦ by
    simp only [category_comp_f, category_comp_Ï†, eqToHom_refl, Category.comp_id]
    exact pullback.hom_ext (pullback.lift_fst _ _ _) (pullback.lift_snd _ _ _))
  right_inv s := by ext <;> simp

def isLimitPullback : IsLimit (pullbackCone f g) := by
  refine PullbackCone.IsLimit.mk (fst := (pullbackCone f g).fst) (snd := (pullbackCone f g).snd) _
    (fun s â†¦ (homPullbackEquiv s.pt f g).2 âŸ¨âŸ¨s.fst, s.sndâŸ©, s.conditionâŸ©)
    (fun s â†¦ by ext <;> simp) (fun s â†¦ by ext <;> simp)
    (fun s m hâ‚ hâ‚‚ â†¦ ?_)
  convert ((homPullbackEquiv s.pt f g).left_inv m).symm using 3
  rw [â† hâ‚, â† hâ‚‚]; rfl

instance : HasPullback f g :=
  âŸ¨âŸ¨âŸ¨_, isLimitPullback f gâŸ©âŸ©âŸ©

instance : HasPullbacks (FormalCoproduct.{w} C) :=
  hasPullbacks_of_hasLimit_cospan _

def pullbackIsoPullback : pullback f g â‰… (pullbackCone f g).pt :=
  limit.isoLimitCone âŸ¨_, isLimitPullback f gâŸ©

@[reassoc (attr := simp)] lemma pullbackIsoPullback_hom_fst :
    (pullbackIsoPullback f g).hom â‰« (pullbackCone f g).fst = pullback.fst f g :=
  limit.isoLimitCone_hom_Ï€ _ _

@[reassoc (attr := simp)] lemma pullbackIsoPullback_inv_fst :
    (pullbackIsoPullback f g).inv â‰« pullback.fst f g = (pullbackCone f g).fst :=
  limit.isoLimitCone_inv_Ï€ _ _

@[reassoc (attr := simp)] lemma pullbackIsoPullback_hom_snd :
    (pullbackIsoPullback f g).hom â‰« (pullbackCone f g).snd = pullback.snd f g :=
  limit.isoLimitCone_hom_Ï€ _ _

@[reassoc (attr := simp)] lemma pullbackIsoPullback_inv_snd :
    (pullbackIsoPullback f g).inv â‰« pullback.snd f g = (pullbackCone f g).snd :=
  limit.isoLimitCone_inv_Ï€ _ _

end Pullback


noncomputable section HasCoproducts

variable [HasCoproducts.{w} A] (C) (J : Type w) (f : J â†’ FormalCoproduct.{w} C) (F : C â¥¤ A)

@[simps] def eval : (C â¥¤ A) â¥¤ (FormalCoproduct.{w} C â¥¤ A) where
  obj F :=
    { obj X := âˆ fun (i : X.I) â†¦ F.obj (X.obj i)
      map {X Y} f := Sigma.desc fun i â†¦ F.map (f.Ï† i) â‰« Sigma.Î¹ (F.obj âˆ˜ Y.obj) (f.f i)
      map_comp _ _ := Sigma.hom_ext _ _ (fun _ â†¦ by simp [Sigma.Î¹_desc]) }
  map Î± := { app f := Sigma.map fun i â†¦ Î±.app (f.obj i) }

def evalOf : eval C A â‹™ (whiskeringLeft _ _ A).obj (of C) â‰… Functor.id (C â¥¤ A) :=
  NatIso.ofComponents fun F â†¦ NatIso.ofComponents
    (fun x â†¦ âŸ¨Sigma.desc fun _ â†¦ ğŸ™ _, Sigma.Î¹ (fun _ â†¦ F.obj x) PUnit.unit, by aesop, by simpâŸ©)
    (fun f â†¦ Sigma.hom_ext _ _ (by simp [Sigma.Î¹_desc]))

variable {C A}

def isColimitEvalMapCocone : IsColimit (((eval.{w} C A).obj F).mapCocone (cofan.{w} J f)) where
  desc s := Sigma.desc fun i â†¦ Sigma.Î¹ (F.obj âˆ˜ (f i.1).obj) i.2 â‰« s.Î¹.app âŸ¨i.1âŸ©
  fac s i := Sigma.hom_ext _ _ fun i â†¦ by simp [Sigma.Î¹_desc, cofan]; rfl
  uniq s m h := Sigma.hom_ext _ _ fun i â†¦ by simp [Sigma.Î¹_desc, â† h, â† Category.assoc, cofan]; rfl

theorem preservesCoproductEval : PreservesColimit (Discrete.functor f) ((eval.{w} C A).obj F) :=
  âŸ¨fun hc â†¦ âŸ¨IsColimit.ofIsoColimit (isColimitEvalMapCocone J f F)
    ((Cocones.functoriality _ _).mapIso ((isColimitCofan J f).uniqueUpToIso hc))âŸ©âŸ©

end HasCoproducts


noncomputable section HasProducts

variable [HasProducts.{w} A] (C) (J : Type w) (f : J â†’ FormalCoproduct.{w} C) (F : Cáµ’áµ– â¥¤ A)

@[simps] def evalOp : (Cáµ’áµ– â¥¤ A) â¥¤ ((FormalCoproduct.{w} C)áµ’áµ– â¥¤ A) where
  obj F :=
    { obj X := âˆá¶œ fun (i : X.unop.I) â†¦ F.obj (op (X.unop.obj i))
      map f := Pi.lift fun i â†¦ Pi.Ï€ _ (f.unop.f i) â‰« F.map (f.unop.Ï† i).op }
  map Î± := { app f := Pi.map fun i â†¦ Î±.app (op (f.unop.obj i)) }

variable {A} in
def evalOpOf :
    evalOp C A â‹™ (whiskeringLeft _ _ A).obj (of C).op â‰… Functor.id (Cáµ’áµ– â¥¤ A) :=
  NatIso.ofComponents fun F â†¦ NatIso.ofComponents fun x â†¦
    âŸ¨Pi.Ï€ _ PUnit.unit, Pi.lift fun _ â†¦ ğŸ™ _, by aesop, by simpâŸ©

variable {C A}

def isLimitEvalMapCone : IsLimit (((evalOp.{w} C A).obj F).mapCone (cofan.{w} J f).op) where
  lift s := Pi.lift fun i â†¦ s.Ï€.app âŸ¨i.1âŸ© â‰« Pi.Ï€ _ i.2
  fac s i := Pi.hom_ext _ _ fun i â†¦ by simp [Pi.lift_Ï€, cofan]
  uniq s m h := Pi.hom_ext _ _ fun âŸ¨iâ‚, iâ‚‚âŸ© â†¦ by simp [Pi.lift_Ï€, â† h, cofan]

theorem preservesProductEval :
    PreservesLimit (Discrete.functor (op âˆ˜ f)) ((evalOp.{w} C A).obj F) :=
  âŸ¨fun hc â†¦ âŸ¨IsLimit.ofIsoLimit (isLimitEvalMapCone J f F) ((Cones.functoriality _ _).mapIso
    ((Cofan.IsColimit.op (isColimitCofan J f)).uniqueUpToIso hc))âŸ©âŸ©

end HasProducts


def arrowOfMaps (X : C) {J : Type w} (f : (j : J) â†’ C) (Ï† : (j : J) â†’ f j âŸ¶ X) :
    FormalCoproduct.mk _ f âŸ¶ (of C).obj X :=
  âŸ¨fun _ â†¦ PUnit.unit, Ï†âŸ©


end FormalCoproduct

end Limits

end CategoryTheory
