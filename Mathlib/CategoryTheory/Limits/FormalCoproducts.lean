/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Kenny Lau
-/
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Opposites.Products
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# Formal Coproducts

In this file we construct the category of formal coproducts given a category.

## Main definitions

* `FormalCoproduct`: the category of formal coproducts, which are indexed sets of objects in a
  category. A morphism `‚àê i : X.I, X.obj i ‚ü∂ ‚àê j : Y.I, Y.obj j` is given by a function
  `f : X.I ‚Üí Y.I` and maps `X.obj i ‚ü∂ Y.obj (f i)` for each `i : X.I`.
* `FormalCoproduct.eval : (C·µí·µñ ‚•§ A) ‚•§ ((FormalCoproduct C)·µí·µñ ‚•§ A)`:
  the universal property that a presheaf on `C` where the target category has arbitrary coproducts,
  can be extended to a presheaf on `FormalCoproduct C`.

## TODO

* `FormalCoproduct.incl C : C ‚•§ FormalCoproduct.{w} C` probably preserves every limit?

-/

universe w w‚ÇÅ w‚ÇÇ w‚ÇÉ v v‚ÇÅ v‚ÇÇ v‚ÇÉ u u‚ÇÅ u‚ÇÇ u‚ÇÉ

open Opposite CategoryTheory Functor

namespace CategoryTheory

namespace Limits

variable {C : Type u} [Category.{v} C] (A : Type u‚ÇÅ) [Category.{v‚ÇÅ} A]

variable (C) in
/-- A formal coproduct is an indexed set of objects, where ‚ü®I, f‚ü© corresponds to the "formal
coproduct" `‚®ø (i : I), f i`, where `f i : C` is the `i`·µó ∞ component. -/
structure FormalCoproduct where
  /-- The indexing type. -/
  I : Type w
  /-- The object in the original category indexed by `x : I`. -/
  obj (i : I) : C

namespace FormalCoproduct

/-- A morphism `(‚®ø (i : X.I), X.obj i) ‚ü∂ (‚®ø (j : Y.I), Y.obj i)` is given by first a function
on the indexing sets `f : X.I ‚Üí Y.I`, and then for each `i : X.I` a morphism
`X.obj i ‚ü∂ Y.obj (f i)`. -/
structure Hom (X Y : FormalCoproduct.{w} C) where
  /-- The function on the indexing sets. -/
  f : X.I ‚Üí Y.I
  /-- The map on each component. -/
  œÜ (i : X.I) : X.obj i ‚ü∂ Y.obj (f i)

-- this category identifies to the fullsubcategory of the category of
-- presheaves of sets on `C` which are coproducts of representable presheaves
@[simps!] instance category : Category (FormalCoproduct.{w} C) where
  Hom := Hom
  id X := { f := id, œÜ := fun _ ‚Ü¶ ùüô _ }
  comp Œ± Œ≤ := { f := Œ≤.f ‚àò Œ±.f, œÜ := fun _ ‚Ü¶ Œ±.œÜ _ ‚â´ Œ≤.œÜ _ }

@[ext (iff := false)]
lemma hom_ext {X Y : FormalCoproduct.{w} C} {f g : X ‚ü∂ Y} (h‚ÇÅ : f.f = g.f)
    (h‚ÇÇ : ‚àÄ (i : X.I), f.œÜ i ‚â´ eqToHom (by rw [h‚ÇÅ]) = g.œÜ i) : f = g := by
  obtain ‚ü®f, F‚ü© := f
  obtain ‚ü®g, G‚ü© := g
  obtain rfl : f = g := h‚ÇÅ
  obtain rfl : F = G := by ext i; simpa using h‚ÇÇ i
  rfl

lemma hom_ext_iff {X Y : FormalCoproduct.{w} C} (f g : X ‚ü∂ Y) :
    f = g ‚Üî ‚àÉ h‚ÇÅ : f.f = g.f, ‚àÄ (i : X.I), f.œÜ i ‚â´ eqToHom (by rw [h‚ÇÅ]) = g.œÜ i :=
  ‚ü®(¬∑ ‚ñ∏ by simp), fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ hom_ext h‚ÇÅ h‚ÇÇ‚ü©

lemma hom_ext_iff' {X Y : FormalCoproduct.{w} C} (f g : X ‚ü∂ Y) :
    f = g ‚Üî ‚àÄ i : X.I, ‚àÉ h‚ÇÅ : f.f i = g.f i, f.œÜ i ‚â´ eqToHom (by rw [h‚ÇÅ]) = g.œÜ i :=
  ‚ü®(¬∑ ‚ñ∏ by simp), fun h ‚Ü¶ hom_ext (funext fun i ‚Ü¶ (h i).fst) fun i ‚Ü¶ (h i).snd‚ü©

/-- A way to create isomorphisms in the category of formal coproducts, by creating an `Equiv`
between the indexing sets, and then correspondingly isomorphisms of each component. -/
@[simps!] def isoOfComponents {X Y : FormalCoproduct.{w} C} (e : X.I ‚âÉ Y.I)
    (h : ‚àÄ i, X.obj i ‚âÖ Y.obj (e i)) : X ‚âÖ Y where
  hom := { f := e, œÜ := fun i ‚Ü¶ (h i).hom }
  inv := { f := e.symm, œÜ := fun i ‚Ü¶ eqToHom (by simp) ‚â´ (h (e.symm i)).inv }
  hom_inv_id := by ext <;> aesop
  inv_hom_id := by ext <;> aesop

variable (C) in
/-- An object of the original category produces a formal coproduct on that object only, so indexed
by `PUnit`, the type with one element. -/
@[simps!] def incl : C ‚•§ FormalCoproduct.{w} C where
  obj X := ‚ü®PUnit, fun _ ‚Ü¶ X‚ü©
  map f := ‚ü®fun _ ‚Ü¶ PUnit.unit, fun _ ‚Ü¶ f‚ü©

section fromIncl

variable {X : C} {Y : FormalCoproduct.{w} C}

/-- A map `incl(X) ‚ü∂ Y` is specified by an element of `Y`'s indexing set, and then a morphism
`X ‚ü∂ Y.obj i` in the original category. -/
@[simps!] def Hom.fromIncl (i : Y.I) (f : X ‚ü∂ Y.obj i) : (incl C).obj X ‚ü∂ Y :=
  ‚ü®fun _ ‚Ü¶ i, fun _ ‚Ü¶ f‚ü©

/-- A map `incl(X) ‚ü∂ Y` is specified by an element of `Y`'s indexing set, and then a morphism
`X ‚ü∂ Y.obj i` in the original category. -/
def Hom.asSigma (f : (incl C).obj X ‚ü∂ Y) : Œ£ (i : Y.I), X ‚ü∂ Y.obj i :=
  ‚ü®f.f PUnit.unit, f.œÜ PUnit.unit‚ü©

lemma Hom.fromIncl_asSigma (f : (incl C).obj X ‚ü∂ Y) :
    Hom.fromIncl f.asSigma.fst f.asSigma.snd = f := by
  ext <;> aesop

end fromIncl

-- This is probably some form of adjunction?
/-- A map `incl(X) ‚ü∂ Y` is specified by an element of `Y`'s indexing set, and then a morphism
`X ‚ü∂ Y.obj i` in the original category. -/
@[simps!] def inclHomEquiv (X : C) (Y : FormalCoproduct.{w} C) :
    ((incl C).obj X ‚ü∂ Y) ‚âÉ (i : Y.I) √ó (X ‚ü∂ Y.obj i) where
  toFun f := f.asSigma
  invFun f := .fromIncl f.1 f.2
  left_inv f := f.fromIncl_asSigma
  right_inv _ := rfl

/-- `incl` is fully faithful, which means that `(X ‚ü∂ Y) ‚âÉ (incl(X) ‚ü∂ incl(Y))`. -/
@[simps!] def fullyFaithfulIncl : (incl C).FullyFaithful where
  preimage f := f.œÜ PUnit.unit

instance : (incl C).Full :=
  fullyFaithfulIncl.full

instance : (incl C).Faithful :=
  fullyFaithfulIncl.faithful

/-- A family of maps with the same target can be turned into one arrow in the category of formal
coproducts. This is used in ƒåech cohomology. -/
@[simps!] def homOfPiHom (X : C) {J : Type w} (f : (j : J) ‚Üí C) (œÜ : (j : J) ‚Üí f j ‚ü∂ X) :
    FormalCoproduct.mk _ f ‚ü∂ (incl C).obj X :=
  ‚ü®fun _ ‚Ü¶ PUnit.unit, œÜ‚ü©

section Coproduct

variable (ùíú : Type w) (f : ùíú ‚Üí FormalCoproduct.{w} C) (t X : FormalCoproduct.{w} C)

/-- We construct explicitly the data that specify the coproduct of a given family of formal
coproducts. -/
def cofan : Cofan f :=
  Cofan.mk ‚ü®(i : ùíú) √ó (f i).I, fun p ‚Ü¶ (f p.1).obj p.2‚ü©
    fun i ‚Ü¶ ‚ü®fun x ‚Ü¶ ‚ü®i, x‚ü©, fun x ‚Ü¶ ùüô ((f i).obj x)‚ü©

section simp_lemmas

variable {ùíú f}

theorem cofan_inj (i : ùíú) : (cofan ùíú f).inj i = ‚ü®fun x ‚Ü¶ ‚ü®i, x‚ü©, fun x ‚Ü¶ ùüô ((f i).obj x)‚ü© := rfl

@[simp] lemma cofan_inj_f_fst (i : ùíú) (x) : (((cofan ùíú f).inj i).f x).1 = i := rfl

@[simp] lemma cofan_inj_f_snd (i : ùíú) (x) : (((cofan ùíú f).inj i).f x).2 = x := rfl

@[simp] lemma cofan_inj_œÜ (i : ùíú) (x) : ((cofan ùíú f).inj i).œÜ x = ùüô ((f i).obj x) := rfl

end simp_lemmas

/-- The explicit `Equiv` between maps from the constructed coproduct `cofan ùíú f` and families of
maps from each component, which is the universal property of coproducts. -/
@[simps!] def cofanHomEquiv :
    ((cofan ùíú f).pt ‚ü∂ t) ‚âÉ ((i : ùíú) ‚Üí (f i ‚ü∂ t)) where
  toFun m i := (cofan ùíú f).inj i ‚â´ m
  invFun s := ‚ü®fun p ‚Ü¶ (s p.1).f p.2, fun p ‚Ü¶ (s p.1).œÜ p.2‚ü©
  left_inv m := hom_ext rfl (fun ‚ü®i, x‚ü© ‚Ü¶ by simp [cofan_inj])
  right_inv p := by ext <;> simp

/-- `cofan ùíú f` is a coproduct of `f`. -/
@[simps!] def isColimitCofan : IsColimit (cofan ùíú f) :=
  mkCofanColimit (cofan ùíú f) (fun t ‚Ü¶ (cofanHomEquiv _ _ _).symm t.inj)
    (fun t i ‚Ü¶ congrFun ((cofanHomEquiv _ _ _).right_inv t.inj) i)
    (fun _ _ h ‚Ü¶ (Equiv.eq_symm_apply _).2 (funext h))

instance : HasCoproducts.{w} (FormalCoproduct.{w} C) :=
  hasCoproducts_of_colimit_cofans _ (isColimitCofan _)

/-- The arbitrary choice of the coproduct is isomorphic to our constructed coproduct `cofan ùíú f`.
-/
noncomputable def coproductIsoCofanPt : ‚àê f ‚âÖ (cofan ùíú f).pt :=
  colimit.isoColimitCocone ‚ü®_, isColimitCofan _ _‚ü©

variable {ùíú f} in
@[reassoc (attr := simp)] lemma Œπ_comp_coproductIsoCofanPt (i) :
    Sigma.Œπ f i ‚â´ (coproductIsoCofanPt ùíú f).hom = (cofan ùíú f).inj i :=
  colimit.isoColimitCocone_Œπ_hom _ _

/-- Each `X : FormalCoproduct.{w} C` is actually itself a coproduct of objects of the original
category (after coercion using `incl C`). This is the function that specifies the family for which
`X` is a coproduct of. -/
def toFun (X : FormalCoproduct.{w} C) : X.I ‚Üí FormalCoproduct.{w} C :=
  (incl C).obj ‚àò X.obj

/-- The witness that each `X : FormalCoproduct.{w} C` is itself a coproduct of objects of the
original category (after coercion using `incl C`), specified by `X.toFun`. -/
def cofanPtIsoSelf : (cofan X.I X.toFun).pt ‚âÖ X :=
  isoOfComponents (Equiv.sigmaPUnit X.I) fun i ‚Ü¶ Iso.refl (X.obj i.fst)

@[reassoc (attr := simp)]
lemma inj_comp_cofanPtIsoSelf_hom (i : X.I) :
    (cofan X.I X.toFun).inj i ‚â´ (cofanPtIsoSelf X).hom = .fromIncl i (ùüô (X.obj i)) :=
  hom_ext rfl (fun i => by aesop)

@[reassoc (attr := simp)]
lemma fromIncl_comp_cofanPtIsoSelf_inv (i : X.I) :
    Hom.fromIncl i (ùüô (X.obj i)) ‚â´ (cofanPtIsoSelf X).inv = (cofan X.I X.toFun).inj i :=
  (Iso.comp_inv_eq _).2 (inj_comp_cofanPtIsoSelf_hom _ _).symm

/-- The isomorphism between the coproduct of `X.toFun` and the object `X` itself. -/
@[simps!] noncomputable def coproductIsoSelf :
    ‚àê X.toFun ‚âÖ X :=
  coproductIsoCofanPt _ _ ‚â™‚â´ cofanPtIsoSelf X

@[reassoc (attr := simp)] lemma Œπ_comp_coproductIsoSelf_hom (i : X.I) :
    Sigma.Œπ _ i ‚â´ (coproductIsoSelf X).hom = .fromIncl i (ùüô (X.obj i)) := by
  simp [coproductIsoSelf]

@[reassoc (attr := simp)] lemma fromIncl_comp_coproductIsoSelf_inv (i : X.I) :
    Hom.fromIncl i (ùüô (X.obj i)) ‚â´ (coproductIsoSelf X).inv = Sigma.Œπ X.toFun i :=
  (Iso.comp_inv_eq _).2 (Œπ_comp_coproductIsoSelf_hom _ _).symm

end Coproduct

section Terminal

/-- Given a terminal object `T` in the original category, we show that `incl(T)` is a terminal
object in the category of formal coproducts. -/
def isTerminalIncl (T : C) (ht : IsTerminal T) : IsTerminal ((incl C).obj T) :=
  IsTerminal.ofUniqueHom (fun _ ‚Ü¶ ‚ü®fun _ ‚Ü¶ PUnit.unit, fun _ ‚Ü¶ ht.from _‚ü©)
    (fun _ _ ‚Ü¶ hom_ext (funext fun _ ‚Ü¶ rfl) (fun _ ‚Ü¶ ht.hom_ext _ _))

instance [HasTerminal C] : HasTerminal (FormalCoproduct.{w} C) :=
  (isTerminalIncl (‚ä§_ C) terminalIsTerminal).hasTerminal

end Terminal

section Pullback

variable {X Y Z : FormalCoproduct.{w} C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
  (pb : ‚àÄ i : Function.Pullback f.f g.f,
    PullbackCone (f.œÜ i.1.1 ‚â´ eqToHom (by rw [i.2])) (g.œÜ i.1.2))
  (hpb : ‚àÄ i, IsLimit (pb i))
  (T : FormalCoproduct.{w} C)

/-- Given two morphisms `f : X ‚ü∂ Z` and `g : Y ‚ü∂ Z`, given pullback in `C` over each component,
construct the pullback in `FormalCategory.{w} C`. -/
def pullbackCone : PullbackCone f g :=
  .mk (W := ‚ü®Function.Pullback f.f g.f, fun i ‚Ü¶ (pb i).pt‚ü©)
    ‚ü®fun i ‚Ü¶ i.1.fst, fun i ‚Ü¶ (pb i).fst‚ü©
    ‚ü®fun i ‚Ü¶ i.1.snd, fun i ‚Ü¶ (pb i).snd‚ü©
    (hom_ext (funext fun i ‚Ü¶ i.2) (fun i ‚Ü¶ by simp [(pb i).condition]))

section simp_lemmas

@[simp] lemma pullbackCone_fst_f (i) : (pullbackCone f g pb).fst.f i = i.1.1 := rfl

@[simp] lemma pullbackCone_fst_œÜ (i) : (pullbackCone f g pb).fst.œÜ i = (pb i).fst := rfl

@[simp] lemma pullbackCone_snd_f (i) : (pullbackCone f g pb).snd.f i = i.1.2 := rfl

@[simp] lemma pullbackCone_snd_œÜ (i) : (pullbackCone f g pb).snd.œÜ i = (pb i).snd := rfl

@[simp] lemma pullbackCone_condition :
    (pullbackCone f g pb).fst ‚â´ f = (pullbackCone f g pb).snd ‚â´ g :=
  PullbackCone.condition _

end simp_lemmas

/-- The `Equiv` that witnesses that `pullbackCone f g pb` is actually a pullback. This is the
universal property of pullbacks. -/
@[simps!] def homPullbackEquiv : (T ‚ü∂ (pullbackCone f g pb).pt) ‚âÉ
    { p : (T ‚ü∂ X) √ó (T ‚ü∂ Y) // p.1 ‚â´ f = p.2 ‚â´ g } where
  toFun m := ‚ü®‚ü®m ‚â´ (pullbackCone f g pb).fst, m ‚â´ (pullbackCone f g pb).snd‚ü©, by simp‚ü©
  invFun s := ‚ü®fun i ‚Ü¶ ‚ü®(s.1.1.f i, s.1.2.f i), congrFun (congrArg Hom.f s.2) i‚ü©,
    fun i ‚Ü¶ (hpb _).lift (PullbackCone.mk (s.1.1.œÜ i) (s.1.2.œÜ i)
      (by simpa using ((hom_ext_iff _ _).1 s.2).2 i))‚ü©
  left_inv m := hom_ext rfl (fun i ‚Ü¶ by
    simp only [category_comp_f, category_comp_œÜ, eqToHom_refl, Category.comp_id]
    exact (hpb _).hom_ext ((pb _).equalizer_ext (by aesop) (by aesop)))
  right_inv s := by ext <;> simp

/-- `pullbackCone f g pb` is a pullback. -/
def isLimitPullbackCone : IsLimit (pullbackCone f g pb) := by
  refine PullbackCone.IsLimit.mk
    (fst := (pullbackCone f g pb).fst) (snd := (pullbackCone f g pb).snd) _
    (fun s ‚Ü¶ (homPullbackEquiv f g pb hpb s.pt).2 ‚ü®(s.fst, s.snd), s.condition‚ü©)
    (fun s ‚Ü¶ congrArg (¬∑.1.fst)
      ((homPullbackEquiv f g pb hpb s.pt).right_inv ‚ü®(s.fst, s.snd), s.condition‚ü©))
    (fun s ‚Ü¶ congrArg (¬∑.1.snd)
      ((homPullbackEquiv f g pb hpb s.pt).right_inv ‚ü®(s.fst, s.snd), s.condition‚ü©))
    (fun s m h‚ÇÅ h‚ÇÇ ‚Ü¶ ?_)
  convert ((homPullbackEquiv f g pb hpb s.pt).left_inv m).symm using 3
  rw [‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ]; rfl

-- Arguments cannot be inferred.
include pb hpb in
theorem hasPullback_of_pullbackCone : HasPullback f g :=
  ‚ü®‚ü®‚ü®_, isLimitPullbackCone f g pb hpb‚ü©‚ü©‚ü©

include hpb in
lemma isPullback : IsPullback (pullbackCone f g pb).fst (pullbackCone f g pb).snd f g :=
  ‚ü®‚ü®pullbackCone_condition f g pb‚ü©, ‚ü®isLimitPullbackCone f g pb hpb‚ü©‚ü©

omit pb
variable [HasPullbacks C]

instance : HasPullback f g :=
  hasPullback_of_pullbackCone f g (fun _ ‚Ü¶ pullback.cone _ _) (fun _ ‚Ü¶ pullback.isLimit _ _)

instance : HasPullbacks (FormalCoproduct.{w} C) :=
  hasPullbacks_of_hasLimit_cospan _

end Pullback

noncomputable section HasCoproducts

variable [HasCoproducts.{w} A] (C) (J : Type w) (f : J ‚Üí FormalCoproduct.{w} C) (F : C ‚•§ A)

/-- A copresheaf valued in a category `A` with arbitrary coproducts, can be extended to the category
of formal coproducts. -/
@[simps!] def eval : (C ‚•§ A) ‚•§ (FormalCoproduct.{w} C ‚•§ A) where
  obj F :=
    { obj X := ‚àê fun (i : X.I) ‚Ü¶ F.obj (X.obj i)
      map {X Y} f := Sigma.desc fun i ‚Ü¶ F.map (f.œÜ i) ‚â´ Sigma.Œπ (F.obj ‚àò Y.obj) (f.f i)
      map_comp _ _ := Sigma.hom_ext _ _ (fun _ ‚Ü¶ by simp [Sigma.Œπ_desc]) }
  map Œ± := { app f := Sigma.map fun i ‚Ü¶ Œ±.app (f.obj i) }

/-- `eval(F)` restricted to the original category (via `incl`) is the original copresheaf `F`. -/
@[simps!] def evalCompInclIsoId :
    eval C A ‚ãô (whiskeringLeft _ _ A).obj (incl C) ‚âÖ Functor.id (C ‚•§ A) :=
  NatIso.ofComponents fun F ‚Ü¶ NatIso.ofComponents
    (fun x ‚Ü¶ ‚ü®Sigma.desc fun _ ‚Ü¶ ùüô _, Sigma.Œπ (fun _ ‚Ü¶ F.obj x) PUnit.unit, by aesop, by simp‚ü©)
    (fun f ‚Ü¶ Sigma.hom_ext _ _ (by simp [Sigma.Œπ_desc]))

variable {C A}

/-- `eval(F)` preserves arbitrary coproducts. -/
def isColimitEvalMapCoconeCofan : IsColimit (((eval.{w} C A).obj F).mapCocone (cofan.{w} J f)) where
  desc s := Sigma.desc fun i ‚Ü¶ Sigma.Œπ (F.obj ‚àò (f i.1).obj) i.2 ‚â´ s.Œπ.app ‚ü®i.1‚ü©
  fac s i := Sigma.hom_ext _ _ fun i ‚Ü¶ by simp [cofan, Function.comp_def]
  uniq s m h := Sigma.hom_ext _ _ fun ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© ‚Ü¶ by simp [‚Üê h, cofan, Function.comp_def]

instance : PreservesColimit (Discrete.functor f) ((eval.{w} C A).obj F) :=
  ‚ü®fun hc ‚Ü¶ ‚ü®IsColimit.ofIsoColimit (isColimitEvalMapCoconeCofan J f F)
    ((Cocones.functoriality _ _).mapIso ((isColimitCofan J f).uniqueUpToIso hc))‚ü©‚ü©

instance : PreservesColimitsOfShape (Discrete J) ((eval.{w} C A).obj F) :=
  preservesColimitsOfShape_of_discrete _

end HasCoproducts

noncomputable section HasProducts

variable [HasProducts.{w} A] (C) (J : Type w) (f : J ‚Üí FormalCoproduct.{w} C) (F : C·µí·µñ ‚•§ A)

/-- A presheaf valued in a category `A` with arbitrary products can be extended to the category of
formal coproducts. -/
@[simps!] def evalOp : (C·µí·µñ ‚•§ A) ‚•§ ((FormalCoproduct.{w} C)·µí·µñ ‚•§ A) where
  obj F :=
    { obj X := ‚àè·∂ú fun (i : X.unop.I) ‚Ü¶ F.obj (op (X.unop.obj i))
      map f := Pi.lift fun i ‚Ü¶ Pi.œÄ _ (f.unop.f i) ‚â´ F.map (f.unop.œÜ i).op }
  map Œ± := { app f := Pi.map fun i ‚Ü¶ Œ±.app (op (f.unop.obj i)) }

/-- `evalOp(F)` restricted to the original category (via `incl`) is the original presheaf `F`. -/
@[simps!] def evalOpCompInlIsoId :
    evalOp C A ‚ãô (whiskeringLeft _ _ A).obj (incl C).op ‚âÖ Functor.id (C·µí·µñ ‚•§ A) :=
  NatIso.ofComponents fun F ‚Ü¶ NatIso.ofComponents fun x ‚Ü¶
    ‚ü®Pi.œÄ _ PUnit.unit, Pi.lift fun _ ‚Ü¶ ùüô _, by aesop, by simp‚ü©

variable {C A}

/-- `evalOp(F)` preserves arbitrary products. -/
def isLimitEvalMapConeCofanOp : IsLimit (((evalOp.{w} C A).obj F).mapCone (cofan.{w} J f).op) where
  lift s := Pi.lift fun i ‚Ü¶ s.œÄ.app ‚ü®i.1‚ü© ‚â´ Pi.œÄ _ i.2
  fac s i := Pi.hom_ext _ _ fun i ‚Ü¶ by simp [cofan]
  uniq s m h := Pi.hom_ext _ _ fun ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© ‚Ü¶ by simp [‚Üê h, cofan]

instance : PreservesLimit (Discrete.functor (op ‚àò f)) ((evalOp.{w} C A).obj F) :=
  ‚ü®fun hc ‚Ü¶ ‚ü®IsLimit.ofIsoLimit (isLimitEvalMapConeCofanOp J f F) ((Cones.functoriality _ _).mapIso
    ((Cofan.IsColimit.op (isColimitCofan J f)).uniqueUpToIso hc))‚ü©‚ü©

end HasProducts

end FormalCoproduct

end Limits

end CategoryTheory
