/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Products.Basic
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Products.Bifunctor

/-!
# A Fubini theorem for categorical (co)limits

We prove that $lim_{J Ã— K} G = lim_J (lim_K G(j, -))$ for a functor `G : J Ã— K â¥¤ C`,
when all the appropriate limits exist.

We begin working with a functor `F : J â¥¤ K â¥¤ C`. We'll write `G : J Ã— K â¥¤ C` for the associated
"uncurried" functor.

In the first part, given a coherent family `D` of limit cones over the functors `F.obj j`,
and a cone `c` over `G`, we construct a cone over the cone points of `D`.
We then show that if `c` is a limit cone, the constructed cone is also a limit cone.

In the second part, we state the Fubini theorem in the setting where limits are
provided by suitable `HasLimit` classes.

We construct
`limitUncurryIsoLimitCompLim F : limit (uncurry.obj F) â‰… limit (F â‹™ lim)`
and give simp lemmas characterising it.
For convenience, we also provide
`limitIsoLimitCurryCompLim G : limit G â‰… limit ((curry.obj G) â‹™ lim)`
in terms of the uncurried functor.

All statements have their counterpart for colimits.
-/


open CategoryTheory

namespace CategoryTheory.Limits

variable {J K : Type*} [Category J] [Category K]
variable {C : Type*} [Category C]
variable (F : J â¥¤ K â¥¤ C) (G : J Ã— K â¥¤ C)

-- We could try introducing a "dependent functor type" to handle this?
/-- A structure carrying a diagram of cones over the functors `F.obj j`.
-/
structure DiagramOfCones where
  /-- For each object, a cone. -/
  obj : âˆ€ j : J, Cone (F.obj j)
  /-- For each map, a map of cones. -/
  map : âˆ€ {j j' : J} (f : j âŸ¶ j'), (Cones.postcompose (F.map f)).obj (obj j) âŸ¶ obj j'
  id : âˆ€ j : J, (map (ðŸ™ j)).hom = ðŸ™ _ := by aesop_cat
  comp : âˆ€ {jâ‚ jâ‚‚ jâ‚ƒ : J} (f : jâ‚ âŸ¶ jâ‚‚) (g : jâ‚‚ âŸ¶ jâ‚ƒ),
    (map (f â‰« g)).hom = (map f).hom â‰« (map g).hom := by aesop_cat

/-- A structure carrying a diagram of cocones over the functors `F.obj j`.
-/
structure DiagramOfCocones where
  /-- For each object, a cocone. -/
  obj : âˆ€ j : J, Cocone (F.obj j)
  /-- For each map, a map of cocones. -/
  map : âˆ€ {j j' : J} (f : j âŸ¶ j'), (obj j) âŸ¶ (Cocones.precompose (F.map f)).obj (obj j')
  id : âˆ€ j : J, (map (ðŸ™ j)).hom = ðŸ™ _ := by aesop_cat
  comp : âˆ€ {jâ‚ jâ‚‚ jâ‚ƒ : J} (f : jâ‚ âŸ¶ jâ‚‚) (g : jâ‚‚ âŸ¶ jâ‚ƒ),
    (map (f â‰« g)).hom = (map f).hom â‰« (map g).hom := by aesop_cat

variable {F}

/-- Extract the functor `J â¥¤ C` consisting of the cone points and the maps between them,
from a `DiagramOfCones`.
-/
@[simps]
def DiagramOfCones.conePoints (D : DiagramOfCones F) : J â¥¤ C where
  obj j := (D.obj j).pt
  map f := (D.map f).hom
  map_id j := D.id j
  map_comp f g := D.comp f g

/-- Extract the functor `J â¥¤ C` consisting of the cocone points and the maps between them,
from a `DiagramOfCocones`.
-/
@[simps]
def DiagramOfCocones.coconePoints (D : DiagramOfCocones F) : J â¥¤ C where
  obj j := (D.obj j).pt
  map f := (D.map f).hom
  map_id j := D.id j
  map_comp f g := D.comp f g

/-- Given a diagram `D` of limit cones over the `F.obj j`, and a cone over `uncurry.obj F`,
we can construct a cone over the diagram consisting of the cone points from `D`.
-/
@[simps]
def coneOfConeUncurry {D : DiagramOfCones F} (Q : âˆ€ j, IsLimit (D.obj j))
    (c : Cone (uncurry.obj F)) : Cone D.conePoints where
  pt := c.pt
  Ï€ :=
    { app := fun j =>
        (Q j).lift
          { pt := c.pt
            Ï€ :=
              { app := fun k => c.Ï€.app (j, k)
                naturality := fun k k' f => by
                  dsimp; simp only [Category.id_comp]
                  have := @NatTrans.naturality _ _ _ _ _ _ c.Ï€ (j, k) (j, k') (ðŸ™ j, f)
                  dsimp at this
                  simp? at this says
                    simp only [Category.id_comp, Functor.map_id, NatTrans.id_app] at this
                  exact this } }
      naturality := fun j j' f =>
        (Q j').hom_ext
          (by
            dsimp
            intro k
            simp only [Limits.ConeMorphism.w, Limits.Cones.postcompose_obj_Ï€,
              Limits.IsLimit.fac_assoc, Limits.IsLimit.fac, NatTrans.comp_app, Category.id_comp,
              Category.assoc]
            have := @NatTrans.naturality _ _ _ _ _ _ c.Ï€ (j, k) (j', k) (f, ðŸ™ k)
            dsimp at this
            simp only [Category.id_comp, Category.comp_id, CategoryTheory.Functor.map_id,
              NatTrans.id_app] at this
            exact this) }

/-- Given a diagram `D` of limit cones over the `curry.obj G j`, and a cone over `G`,
we can construct a cone over the diagram consisting of the cone points from `D`.
-/
@[simps]
def coneOfConeCurry {D : DiagramOfCones (curry.obj G)} (Q : âˆ€ j, IsLimit (D.obj j))
    (c : Cone G) : Cone D.conePoints where
  pt := c.pt
  Ï€ :=
    { app j := (Q j).lift
        { pt := c.pt
          Ï€ := { app k := c.Ï€.app (j, k) } }
      naturality {_ j'} _ := (Q j').hom_ext (by simp) }

/-- Given a diagram `D` of colimit cocones over the `F.obj j`, and a cocone over `uncurry.obj F`,
we can construct a cocone over the diagram consisting of the cocone points from `D`.
-/
@[simps]
def coconeOfCoconeUncurry {D : DiagramOfCocones F} (Q : âˆ€ j, IsColimit (D.obj j))
    (c : Cocone (uncurry.obj F)) : Cocone D.coconePoints where
  pt := c.pt
  Î¹ :=
    { app := fun j =>
        (Q j).desc
          { pt := c.pt
            Î¹ :=
              { app := fun k => c.Î¹.app (j, k)
                naturality := fun k k' f => by
                  dsimp; simp only [Category.comp_id]
                  conv_lhs =>
                    arg 1; equals (F.map (ðŸ™ _)).app _ â‰«  (F.obj j).map f =>
                      simp
                  conv_lhs => arg 1; rw [â† uncurry_obj_map F ((ðŸ™ j,f) : (j,k) âŸ¶ (j,k'))]
                  rw [c.w] } }
      naturality := fun j j' f =>
        (Q j).hom_ext
          (by
            dsimp
            intro k
            simp only [Limits.CoconeMorphism.w_assoc, Limits.Cocones.precompose_obj_Î¹,
              Limits.IsColimit.fac_assoc, Limits.IsColimit.fac, NatTrans.comp_app, Category.comp_id,
              Category.assoc]
            have := @NatTrans.naturality _ _ _ _ _ _ c.Î¹ (j, k) (j', k) (f, ðŸ™ k)
            dsimp at this
            simp only [Category.id_comp, Category.comp_id, CategoryTheory.Functor.map_id,
              NatTrans.id_app] at this
            exact this) }

/-- Given a diagram `D` of colimit cocones under the `curry.obj G j`, and a cocone under `G`,
we can construct a cocone under the diagram consisting of the cocone points from `D`.
-/
@[simps]
def coconeOfCoconeCurry {D : DiagramOfCocones (curry.obj G)} (Q : âˆ€ j, IsColimit (D.obj j))
    (c : Cocone G) : Cocone D.coconePoints where
  pt := c.pt
  Î¹ :=
    { app j := (Q j).desc
        { pt := c.pt
          Î¹ := { app k := c.Î¹.app (j, k) } }
      naturality {j _} _ := (Q j).hom_ext (by simp) }

/-- `coneOfConeUncurry Q c` is a limit cone when `c` is a limit cone.
-/
def coneOfConeUncurryIsLimit {D : DiagramOfCones F} (Q : âˆ€ j, IsLimit (D.obj j))
    {c : Cone (uncurry.obj F)} (P : IsLimit c) : IsLimit (coneOfConeUncurry Q c) where
  lift s :=
    P.lift
      { pt := s.pt
        Ï€ :=
          { app := fun p => s.Ï€.app p.1 â‰« (D.obj p.1).Ï€.app p.2
            naturality := fun p p' f => by
              dsimp; simp only [Category.id_comp, Category.assoc]
              rcases p with âŸ¨j, kâŸ©
              rcases p' with âŸ¨j', k'âŸ©
              rcases f with âŸ¨fj, fkâŸ©
              dsimp
              slice_rhs 3 4 => rw [â† NatTrans.naturality]
              slice_rhs 2 3 => rw [â† (D.obj j).Ï€.naturality]
              simp only [Functor.const_obj_map, Category.id_comp, Category.assoc]
              have w := (D.map fj).w k'
              dsimp at w
              rw [â† w]
              have n := s.Ï€.naturality fj
              dsimp at n
              simp only [Category.id_comp] at n
              rw [n]
              simp } }
  fac s j := by
    apply (Q j).hom_ext
    intro k
    simp
  uniq s m w := by
    refine P.uniq
      { pt := s.pt
        Ï€ := _ } m ?_
    rintro âŸ¨j, kâŸ©
    dsimp
    rw [â† w j]
    simp

/-- `coconeOfCoconeUncurry Q c` is a colimit cocone when `c` is a colimit cocone.
-/
def coconeOfCoconeUncurryIsColimit {D : DiagramOfCocones F} (Q : âˆ€ j, IsColimit (D.obj j))
    {c : Cocone (uncurry.obj F)} (P : IsColimit c) : IsColimit (coconeOfCoconeUncurry Q c) where
  desc s :=
    P.desc
      { pt := s.pt
        Î¹ :=
          { app := fun p => (D.obj p.1).Î¹.app p.2 â‰« s.Î¹.app p.1
            naturality := fun p p' f => by
              dsimp; simp only [Category.id_comp, Category.assoc]
              rcases p with âŸ¨j, kâŸ©
              rcases p' with âŸ¨j', k'âŸ©
              rcases f with âŸ¨fj, fkâŸ©
              dsimp
              slice_lhs 2 3 => rw [(D.obj j').Î¹.naturality]
              simp only [Functor.const_obj_map, Category.id_comp, Category.assoc]
              have w := (D.map fj).w k
              dsimp at w
              slice_lhs 1 2 => rw [â† w]
              have n := s.Î¹.naturality fj
              dsimp at n
              simp only [Category.comp_id] at n
              rw [â† n]
              simp } }
  fac s j := by
    apply (Q j).hom_ext
    intro k
    simp
  uniq s m w := by
    refine P.uniq
      { pt := s.pt
        Î¹ := _ } m ?_
    rintro âŸ¨j, kâŸ©
    dsimp
    rw [â† w j]
    simp

section

variable (F)
variable [HasLimitsOfShape K C]

/-- Given a functor `F : J â¥¤ K â¥¤ C`, with all needed limits,
we can construct a diagram consisting of the limit cone over each functor `F.obj j`,
and the universal cone morphisms between these.
-/
@[simps]
noncomputable def DiagramOfCones.mkOfHasLimits : DiagramOfCones F where
  obj j := limit.cone (F.obj j)
  map f := { hom := lim.map (F.map f) }

-- Satisfying the inhabited linter.
noncomputable instance diagramOfConesInhabited : Inhabited (DiagramOfCones F) :=
  âŸ¨DiagramOfCones.mkOfHasLimits FâŸ©

@[simp]
theorem DiagramOfCones.mkOfHasLimits_conePoints :
    (DiagramOfCones.mkOfHasLimits F).conePoints = F â‹™ lim :=
  rfl

section

variable [HasLimit (curry.obj G â‹™ lim)]

/-- Given a functor `G : J Ã— K â¥¤ C` such that `(curry.obj G â‹™ lim)` makes sense and has a limit,
we can construct a cone over `G` with `limit (curry.obj G â‹™ lim)` as a cone point -/
noncomputable def coneOfHasLimitCurryCompLim : Cone G :=
  let Q : DiagramOfCones (curry.obj G) := .mkOfHasLimits _
  { pt := limit (curry.obj G â‹™ lim),
    Ï€ :=
    { app x := limit.Ï€ (curry.obj G â‹™ lim) x.fst â‰« (Q.obj x.fst).Ï€.app x.snd
      naturality {x y} := fun âŸ¨fâ‚, fâ‚‚âŸ© â†¦ by
        have := (Q.obj x.1).w fâ‚‚
        dsimp [Q] at this âŠ¢
        rw [â† limit.w (F := curry.obj G â‹™ lim) (f := fâ‚)]
        dsimp
        simp only [Category.assoc, Category.id_comp, Prod.fac (fâ‚, fâ‚‚),
          G.map_comp, limMap_Ï€, curry_obj_map_app, reassoc_of% this] } }

/-- The cone `coneOfHasLimitCurryCompLim` is in fact a limit cone.
-/
noncomputable def isLimitConeOfHasLimitCurryCompLim : IsLimit (coneOfHasLimitCurryCompLim G) :=
  let Q : DiagramOfCones (curry.obj G) := .mkOfHasLimits _
  let Q' : âˆ€ j, IsLimit (Q.obj j) := fun j => limit.isLimit _
  { lift c' := limit.lift (F := curry.obj G â‹™ lim) (coneOfConeCurry G Q' c')
    fac c' f := by simp [coneOfHasLimitCurryCompLim, Q, Q']
    uniq c' f h := by
      dsimp [coneOfHasLimitCurryCompLim] at f h âŠ¢
      refine limit.hom_ext (F := curry.obj G â‹™ lim) (fun j â†¦ limit.hom_ext (fun k â†¦ ?_))
      simp [h âŸ¨j, kâŸ©, Q'] }

/-- The functor `G` has a limit if `C` has `K`-shaped limits and `(curry.obj G â‹™ lim)` has a limit.
-/
instance : HasLimit G where
  exists_limit :=
    âŸ¨ { cone := coneOfHasLimitCurryCompLim G
        isLimit := isLimitConeOfHasLimitCurryCompLim G }âŸ©

end

variable [HasLimit (uncurry.obj F)] [HasLimit (F â‹™ lim)]

/-- The Fubini theorem for a functor `F : J â¥¤ K â¥¤ C`,
showing that the limit of `uncurry.obj F` can be computed as
the limit of the limits of the functors `F.obj j`.
-/
noncomputable def limitUncurryIsoLimitCompLim : limit (uncurry.obj F) â‰… limit (F â‹™ lim) := by
  let c := limit.cone (uncurry.obj F)
  let P : IsLimit c := limit.isLimit _
  let G := DiagramOfCones.mkOfHasLimits F
  let Q : âˆ€ j, IsLimit (G.obj j) := fun j => limit.isLimit _
  have Q' := coneOfConeUncurryIsLimit Q P
  have Q'' := limit.isLimit (F â‹™ lim)
  exact IsLimit.conePointUniqueUpToIso Q' Q''

@[simp, reassoc]
theorem limitUncurryIsoLimitCompLim_hom_Ï€_Ï€ {j} {k} :
    (limitUncurryIsoLimitCompLim F).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k = limit.Ï€ _ (j, k) := by
  dsimp [limitUncurryIsoLimitCompLim, IsLimit.conePointUniqueUpToIso, IsLimit.uniqueUpToIso]
  simp

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp, reassoc]
theorem limitUncurryIsoLimitCompLim_inv_Ï€ {j} {k} :
    (limitUncurryIsoLimitCompLim F).inv â‰« limit.Ï€ _ (j, k) =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  rw [â† cancel_epi (limitUncurryIsoLimitCompLim F).hom]
  simp

end

section

variable (F)
variable [HasColimitsOfShape K C]

/-- Given a functor `F : J â¥¤ K â¥¤ C`, with all needed colimits,
we can construct a diagram consisting of the colimit cocone over each functor `F.obj j`,
and the universal cocone morphisms between these.
-/
@[simps]
noncomputable def DiagramOfCocones.mkOfHasColimits : DiagramOfCocones F where
  obj j := colimit.cocone (F.obj j)
  map f := { hom := colim.map (F.map f) }

-- Satisfying the inhabited linter.
noncomputable instance diagramOfCoconesInhabited : Inhabited (DiagramOfCocones F) :=
  âŸ¨DiagramOfCocones.mkOfHasColimits FâŸ©

@[simp]
theorem DiagramOfCocones.mkOfHasColimits_coconePoints :
    (DiagramOfCocones.mkOfHasColimits F).coconePoints = F â‹™ colim :=
  rfl

section

variable [HasColimit (curry.obj G â‹™ colim)]

/-- Given a functor `G : J Ã— K â¥¤ C` such that `(curry.obj G â‹™ colim)` makes sense and has a colimit,
we can construct a cocone under `G` with `colimit (curry.obj G â‹™ colim)` as a cocone point -/
noncomputable def coconeOfHasColimitCurryCompColim : Cocone G :=
  let Q : DiagramOfCocones (curry.obj G) := .mkOfHasColimits _
  { pt := colimit (curry.obj G â‹™ colim),
    Î¹ :=
    { app x := (Q.obj x.fst).Î¹.app x.snd â‰« colimit.Î¹ (curry.obj G â‹™ colim) x.fst
      naturality {x y} := fun âŸ¨fâ‚, fâ‚‚âŸ© â†¦ by
        have := (Q.obj y.1).w fâ‚‚
        dsimp [Q] at this âŠ¢
        rw [â† colimit.w (F := curry.obj G â‹™ colim) (f := fâ‚)]
        dsimp
        simp [Category.assoc, Category.comp_id, Prod.fac' (fâ‚, fâ‚‚),
          G.map_comp, Î¹_colimMap_assoc, curry_obj_map_app, reassoc_of% this] } }


/-- The cocone `coconeOfHasColimitCurryCompColim` is in fact a limit cocone.
-/
noncomputable def isColimitCoconeOfHasColimitCurryCompColim :
    IsColimit (coconeOfHasColimitCurryCompColim G) :=
  let Q : DiagramOfCocones (curry.obj G) := .mkOfHasColimits _
  let Q' : âˆ€ j, IsColimit (Q.obj j) := fun j => colimit.isColimit _
  { desc c' := colimit.desc (F := curry.obj G â‹™ colim) (coconeOfCoconeCurry G Q' c')
    fac c' f := by simp [coconeOfHasColimitCurryCompColim, Q, Q']
    uniq c' f h := by
      dsimp [coconeOfHasColimitCurryCompColim] at f h âŠ¢
      refine colimit.hom_ext (F := curry.obj G â‹™ colim) (fun j â†¦ colimit.hom_ext (fun k â†¦ ?_))
      simp [â† h âŸ¨j, kâŸ©, Q'] }

/-- The functor `G` has a colimit if `C` has `K`-shaped colimits and `(curry.obj G â‹™ colim)` has a
colimit. -/
instance : HasColimit G where
  exists_colimit :=
    âŸ¨ { cocone := coconeOfHasColimitCurryCompColim G
        isColimit := isColimitCoconeOfHasColimitCurryCompColim G }âŸ©

end

variable [HasColimit (uncurry.obj F)] [HasColimit (F â‹™ colim)]

/-- The Fubini theorem for a functor `F : J â¥¤ K â¥¤ C`,
showing that the colimit of `uncurry.obj F` can be computed as
the colimit of the colimits of the functors `F.obj j`.
-/
noncomputable def colimitUncurryIsoColimitCompColim :
    colimit (uncurry.obj F) â‰… colimit (F â‹™ colim) := by
  let c := colimit.cocone (uncurry.obj F)
  let P : IsColimit c := colimit.isColimit _
  let G := DiagramOfCocones.mkOfHasColimits F
  let Q : âˆ€ j, IsColimit (G.obj j) := fun j => colimit.isColimit _
  have Q' := coconeOfCoconeUncurryIsColimit Q P
  have Q'' := colimit.isColimit (F â‹™ colim)
  exact IsColimit.coconePointUniqueUpToIso Q' Q''

@[simp, reassoc]
theorem colimitUncurryIsoColimitCompColim_Î¹_Î¹_inv {j} {k} :
    colimit.Î¹ (F.obj j) k â‰« colimit.Î¹ (F â‹™ colim) j â‰« (colimitUncurryIsoColimitCompColim F).inv =
      colimit.Î¹ (uncurry.obj F) (j, k) := by
  dsimp [colimitUncurryIsoColimitCompColim, IsColimit.coconePointUniqueUpToIso,
    IsColimit.uniqueUpToIso]
  simp

@[simp, reassoc]
theorem colimitUncurryIsoColimitCompColim_Î¹_hom {j} {k} :
    colimit.Î¹ _ (j, k) â‰« (colimitUncurryIsoColimitCompColim F).hom =
      (colimit.Î¹ _ k â‰« colimit.Î¹ (F â‹™ colim) j : _ âŸ¶ (colimit (F â‹™ colim))) := by
  rw [â† cancel_mono (colimitUncurryIsoColimitCompColim F).inv]
  simp

end

section

variable (F) [HasLimitsOfShape J C] [HasLimitsOfShape K C]

/-- The limit of `F.flip â‹™ lim` is isomorphic to the limit of `F â‹™ lim`. -/
noncomputable def limitFlipCompLimIsoLimitCompLim : limit (F.flip â‹™ lim) â‰… limit (F â‹™ lim) :=
  (limitUncurryIsoLimitCompLim _).symm â‰ªâ‰«
    HasLimit.isoOfNatIso (uncurryObjFlip _) â‰ªâ‰«
      HasLimit.isoOfEquivalence (Prod.braiding _ _)
          (NatIso.ofComponents fun _ => by rfl) â‰ªâ‰«
        limitUncurryIsoLimitCompLim _

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp, reassoc]
theorem limitFlipCompLimIsoLimitCompLim_hom_Ï€_Ï€ (j) (k) :
    (limitFlipCompLimIsoLimitCompLim F).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k =
      (limit.Ï€ _ k â‰« limit.Ï€ _ j : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitFlipCompLimIsoLimitCompLim]
  simp [Equivalence.counit]

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
-- See note [dsimp, simp]
@[simp, reassoc]
theorem limitFlipCompLimIsoLimitCompLim_inv_Ï€_Ï€ (k) (j) :
    (limitFlipCompLimIsoLimitCompLim F).inv â‰« limit.Ï€ _ k â‰« limit.Ï€ _ j =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitFlipCompLimIsoLimitCompLim]
  simp

end

section

variable (F) [HasColimitsOfShape J C] [HasColimitsOfShape K C]

/-- The colimit of `F.flip â‹™ colim` is isomorphic to the colimit of `F â‹™ colim`. -/
noncomputable def colimitFlipCompColimIsoColimitCompColim :
    colimit (F.flip â‹™ colim) â‰… colimit (F â‹™ colim) :=
  (colimitUncurryIsoColimitCompColim _).symm â‰ªâ‰«
    HasColimit.isoOfNatIso (uncurryObjFlip _) â‰ªâ‰«
      HasColimit.isoOfEquivalence (Prod.braiding _ _)
          (NatIso.ofComponents fun _ => by rfl) â‰ªâ‰«
        colimitUncurryIsoColimitCompColim _

@[simp, reassoc]
theorem colimitFlipCompColimIsoColimitCompColim_Î¹_Î¹_hom (j) (k) :
    colimit.Î¹ (F.flip.obj k) j â‰« colimit.Î¹ (F.flip â‹™ colim) k â‰«
      (colimitFlipCompColimIsoColimitCompColim F).hom =
        (colimit.Î¹ _ k â‰« colimit.Î¹ (F â‹™ colim) j : _ âŸ¶ colimit (Fâ‹™ colim)) := by
  dsimp [colimitFlipCompColimIsoColimitCompColim]
  slice_lhs 1 3 => simp only []
  simp [Equivalence.unit]

@[simp, reassoc]
theorem colimitFlipCompColimIsoColimitCompColim_Î¹_Î¹_inv (k) (j) :
    colimit.Î¹ (F.obj j) k â‰« colimit.Î¹ (F â‹™ colim) j â‰«
      (colimitFlipCompColimIsoColimitCompColim F).inv =
        (colimit.Î¹ _ j â‰« colimit.Î¹ (F.flip â‹™ colim) k : _ âŸ¶ colimit (F.flip â‹™ colim)) := by
  dsimp [colimitFlipCompColimIsoColimitCompColim]
  slice_lhs 1 3 => simp only []
  simp [Equivalence.counitInv]

end

section

variable [HasLimitsOfShape K C] [HasLimit (curry.obj G â‹™ lim)]

/-- The Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that the limit of `G` can be computed as
the limit of the limits of the functors `G.obj (j, _)`.
-/
noncomputable def limitIsoLimitCurryCompLim : limit G â‰… limit (curry.obj G â‹™ lim) := by
  have i : G â‰… uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unitIso.app G
  haveI : Limits.HasLimit (uncurry.obj ((@curry J _ K _ C _).obj G)) := hasLimitOfIso i
  trans limit (uncurry.obj ((@curry J _ K _ C _).obj G))
  Â· apply HasLimit.isoOfNatIso i
  Â· exact limitUncurryIsoLimitCompLim ((@curry J _ K _ C _).obj G)

@[simp, reassoc]
theorem limitIsoLimitCurryCompLim_hom_Ï€_Ï€ {j} {k} :
    (limitIsoLimitCurryCompLim G).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k = limit.Ï€ _ (j, k) := by
  simp [limitIsoLimitCurryCompLim, Trans.simple]

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp, reassoc]
theorem limitIsoLimitCurryCompLim_inv_Ï€ {j} {k} :
    (limitIsoLimitCurryCompLim G).inv â‰« limit.Ï€ _ (j, k) =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  rw [â† cancel_epi (limitIsoLimitCurryCompLim G).hom]
  simp

end

section

variable [HasColimitsOfShape K C] [HasColimit (curry.obj G â‹™ colim)]

/-- The Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that the colimit of `G` can be computed as
the colimit of the colimits of the functors `G.obj (j, _)`.
-/
noncomputable def colimitIsoColimitCurryCompColim : colimit G â‰… colimit (curry.obj G â‹™ colim) := by
  have i : G â‰… uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unitIso.app G
  haveI : Limits.HasColimit (uncurry.obj ((@curry J _ K _ C _).obj G)) := hasColimitOfIso i.symm
  trans colimit (uncurry.obj ((@curry J _ K _ C _).obj G))
  Â· apply HasColimit.isoOfNatIso i
  Â· exact colimitUncurryIsoColimitCompColim ((@curry J _ K _ C _).obj G)

@[simp, reassoc]
theorem colimitIsoColimitCurryCompColim_Î¹_Î¹_inv {j} {k} :
    colimit.Î¹ ((curry.obj G).obj j) k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j â‰«
      (colimitIsoColimitCurryCompColim G).inv  = colimit.Î¹ _ (j, k) := by
  simp [colimitIsoColimitCurryCompColim, Trans.simple, colimitUncurryIsoColimitCompColim]

@[simp, reassoc]
theorem colimitIsoColimitCurryCompColim_Î¹_hom {j} {k} :
    colimit.Î¹ _ (j, k) â‰« (colimitIsoColimitCurryCompColim G).hom =
      (colimit.Î¹ (_) k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j : _ âŸ¶ colimit (_ â‹™ colim)) := by
  rw [â† cancel_mono (colimitIsoColimitCurryCompColim G).inv]
  simp

end

section

variable [HasLimitsOfShape K C] [HasLimitsOfShape J C] [HasLimit (curry.obj G â‹™ lim)]

open CategoryTheory.prod

/-- A variant of the Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that $\lim_k \lim_j G(j,k) â‰… \lim_j \lim_k G(j,k)$.
-/
noncomputable def limitCurrySwapCompLimIsoLimitCurryCompLim :
    limit (curry.obj (Prod.swap K J â‹™ G) â‹™ lim) â‰… limit (curry.obj G â‹™ lim) :=
  calc
    limit (curry.obj (Prod.swap K J â‹™ G) â‹™ lim) â‰… limit (Prod.swap K J â‹™ G) :=
      (limitIsoLimitCurryCompLim _).symm
    _ â‰… limit G := HasLimit.isoOfEquivalence (Prod.braiding K J) (Iso.refl _)
    _ â‰… limit (curry.obj G â‹™ lim) := limitIsoLimitCurryCompLim _

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp]
theorem limitCurrySwapCompLimIsoLimitCurryCompLim_hom_Ï€_Ï€ {j} {k} :
    (limitCurrySwapCompLimIsoLimitCurryCompLim G).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k =
      (limit.Ï€ _ k â‰« limit.Ï€ _ j : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitCurrySwapCompLimIsoLimitCurryCompLim, Equivalence.counit]
  rw [Category.assoc, Category.assoc, limitIsoLimitCurryCompLim_hom_Ï€_Ï€,
    HasLimit.isoOfEquivalence_hom_Ï€]
  dsimp [Equivalence.counit]
  rw [â† prod_id, G.map_id]
  simp

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp]
theorem limitCurrySwapCompLimIsoLimitCurryCompLim_inv_Ï€_Ï€ {j} {k} :
    (limitCurrySwapCompLimIsoLimitCurryCompLim G).inv â‰« limit.Ï€ _ k â‰« limit.Ï€ _ j =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  simp [limitCurrySwapCompLimIsoLimitCurryCompLim]

end

section

variable [HasColimitsOfShape K C] [HasColimitsOfShape J C] [HasColimit (curry.obj G â‹™ colim)]

open CategoryTheory.prod

/-- A variant of the Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that $\colim_k \colim_j G(j,k) â‰… \colim_j \colim_k G(j,k)$.
-/
noncomputable def colimitCurrySwapCompColimIsoColimitCurryCompColim :
    colimit (curry.obj (Prod.swap K J â‹™ G) â‹™ colim) â‰… colimit (curry.obj G â‹™ colim) :=
  calc
    colimit (curry.obj (Prod.swap K J â‹™ G) â‹™ colim) â‰… colimit (Prod.swap K J â‹™ G) :=
      (colimitIsoColimitCurryCompColim _).symm
    _ â‰… colimit G := HasColimit.isoOfEquivalence (Prod.braiding K J) (Iso.refl _)
    _ â‰… colimit (curry.obj G â‹™ colim) := colimitIsoColimitCurryCompColim _

@[simp]
theorem colimitCurrySwapCompColimIsoColimitCurryCompColim_Î¹_Î¹_hom {j} {k} :
    colimit.Î¹ _ j â‰« colimit.Î¹ (curry.obj (Prod.swap K J â‹™ G) â‹™ colim) k â‰«
      (colimitCurrySwapCompColimIsoColimitCurryCompColim G).hom =
        (colimit.Î¹ _ k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j : _ âŸ¶ colimit (curry.obj Gâ‹™ colim)) := by
  dsimp [colimitCurrySwapCompColimIsoColimitCurryCompColim]
  slice_lhs 1 3 => simp only []
  simp

@[simp]
theorem colimitCurrySwapCompColimIsoColimitCurryCompColim_Î¹_Î¹_inv {j} {k} :
    colimit.Î¹ _ k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j â‰«
      (colimitCurrySwapCompColimIsoColimitCurryCompColim G).inv =
        (colimit.Î¹ _ j â‰«
          colimit.Î¹ (curry.obj _ â‹™ colim) k :
            _ âŸ¶ colimit (curry.obj (Prod.swap K J â‹™ G) â‹™ colim)) := by
  dsimp [colimitCurrySwapCompColimIsoColimitCurryCompColim]
  slice_lhs 1 3 => simp only []
  rw [colimitIsoColimitCurryCompColim_Î¹_Î¹_inv, HasColimit.isoOfEquivalence_inv_Ï€]
  dsimp [Equivalence.counitInv]
  rw [CategoryTheory.Bifunctor.map_id]
  simp

end

end CategoryTheory.Limits
