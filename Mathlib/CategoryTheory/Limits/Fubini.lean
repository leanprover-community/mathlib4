/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Products.Basic
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Products.Bifunctor

#align_import category_theory.limits.fubini from "leanprover-community/mathlib"@"59382264386afdbaf1727e617f5fdda511992eb9"

/-!
# A Fubini theorem for categorical (co)limits

We prove that $lim_{J Ã— K} G = lim_J (lim_K G(j, -))$ for a functor `G : J Ã— K â¥¤ C`,
when all the appropriate limits exist.

We begin working with a functor `F : J â¥¤ K â¥¤ C`. We'll write `G : J Ã— K â¥¤ C` for the associated
"uncurried" functor.

In the first part, given a coherent family `D` of limit cones over the functors `F.obj j`,
and a cone `c` over `G`, we construct a cone over the cone points of `D`.
We then show that if `c` is a limit cone, the constructed cone is also a limit cone.

In the second part, we state the Fubini theorem in the setting where limits are
provided by suitable `HasLimit` classes.

We construct
`limitUncurryIsoLimitCompLim F : limit (uncurry.obj F) â‰… limit (F â‹™ lim)`
and give simp lemmas characterising it.
For convenience, we also provide
`limitIsoLimitCurryCompLim G : limit G â‰… limit ((curry.obj G) â‹™ lim)`
in terms of the uncurried functor.

All statements have their counterpart for colimits.
-/


universe v u

open CategoryTheory

namespace CategoryTheory.Limits

variable {J K : Type v} [SmallCategory J] [SmallCategory K]
variable {C : Type u} [Category.{v} C]
variable (F : J â¥¤ K â¥¤ C)

-- We could try introducing a "dependent functor type" to handle this?
/-- A structure carrying a diagram of cones over the functors `F.obj j`.
-/
structure DiagramOfCones where
  /-- For each object, a cone. -/
  obj : âˆ€ j : J, Cone (F.obj j)
  /-- For each map, a map of cones. -/
  map : âˆ€ {j j' : J} (f : j âŸ¶ j'), (Cones.postcompose (F.map f)).obj (obj j) âŸ¶ obj j'
  id : âˆ€ j : J, (map (ðŸ™ j)).hom = ðŸ™ _ := by aesop_cat
  comp : âˆ€ {jâ‚ jâ‚‚ jâ‚ƒ : J} (f : jâ‚ âŸ¶ jâ‚‚) (g : jâ‚‚ âŸ¶ jâ‚ƒ),
    (map (f â‰« g)).hom = (map f).hom â‰« (map g).hom := by aesop_cat
#align category_theory.limits.diagram_of_cones CategoryTheory.Limits.DiagramOfCones

/-- A structure carrying a diagram of cocones over the functors `F.obj j`.
-/
structure DiagramOfCocones where
  /-- For each object, a cocone. -/
  obj : âˆ€ j : J, Cocone (F.obj j)
  /-- For each map, a map of cocones. -/
  map : âˆ€ {j j' : J} (f : j âŸ¶ j'), (obj j) âŸ¶ (Cocones.precompose (F.map f)).obj (obj j')
  id : âˆ€ j : J, (map (ðŸ™ j)).hom = ðŸ™ _ := by aesop_cat
  comp : âˆ€ {jâ‚ jâ‚‚ jâ‚ƒ : J} (f : jâ‚ âŸ¶ jâ‚‚) (g : jâ‚‚ âŸ¶ jâ‚ƒ),
    (map (f â‰« g)).hom = (map f).hom â‰« (map g).hom := by aesop_cat

variable {F}

/-- Extract the functor `J â¥¤ C` consisting of the cone points and the maps between them,
from a `DiagramOfCones`.
-/
@[simps]
def DiagramOfCones.conePoints (D : DiagramOfCones F) : J â¥¤ C where
  obj j := (D.obj j).pt
  map f := (D.map f).hom
  map_id j := D.id j
  map_comp f g := D.comp f g
#align category_theory.limits.diagram_of_cones.cone_points CategoryTheory.Limits.DiagramOfCones.conePoints

/-- Extract the functor `J â¥¤ C` consisting of the cocone points and the maps between them,
from a `DiagramOfCocones`.
-/
@[simps]
def DiagramOfCocones.coconePoints (D : DiagramOfCocones F) : J â¥¤ C where
  obj j := (D.obj j).pt
  map f := (D.map f).hom
  map_id j := D.id j
  map_comp f g := D.comp f g

/-- Given a diagram `D` of limit cones over the `F.obj j`, and a cone over `uncurry.obj F`,
we can construct a cone over the diagram consisting of the cone points from `D`.
-/
@[simps]
def coneOfConeUncurry {D : DiagramOfCones F} (Q : âˆ€ j, IsLimit (D.obj j))
    (c : Cone (uncurry.obj F)) : Cone D.conePoints where
  pt := c.pt
  Ï€ :=
    { app := fun j =>
        (Q j).lift
          { pt := c.pt
            Ï€ :=
              { app := fun k => c.Ï€.app (j, k)
                naturality := fun k k' f => by
                  dsimp; simp only [Category.id_comp]
                  have := @NatTrans.naturality _ _ _ _ _ _ c.Ï€ (j, k) (j, k') (ðŸ™ j, f)
                  dsimp at this
                  simp? at this says
                    simp only [Category.id_comp, Functor.map_id, NatTrans.id_app] at this
                  exact this } }
      naturality := fun j j' f =>
        (Q j').hom_ext
          (by
            dsimp
            intro k
            simp only [Limits.ConeMorphism.w, Limits.Cones.postcompose_obj_Ï€,
              Limits.IsLimit.fac_assoc, Limits.IsLimit.fac, NatTrans.comp_app, Category.id_comp,
              Category.assoc]
            have := @NatTrans.naturality _ _ _ _ _ _ c.Ï€ (j, k) (j', k) (f, ðŸ™ k)
            dsimp at this
            simp only [Category.id_comp, Category.comp_id, CategoryTheory.Functor.map_id,
              NatTrans.id_app] at this
            exact this) }
#align category_theory.limits.cone_of_cone_uncurry CategoryTheory.Limits.coneOfConeUncurry

/-- Given a diagram `D` of colimit cocones over the `F.obj j`, and a cocone over `uncurry.obj F`,
we can construct a cocone over the diagram consisting of the cocone points from `D`.
-/
@[simps]
def coconeOfCoconeUncurry {D : DiagramOfCocones F} (Q : âˆ€ j, IsColimit (D.obj j))
    (c : Cocone (uncurry.obj F)) : Cocone D.coconePoints where
  pt := c.pt
  Î¹ :=
    { app := fun j =>
        (Q j).desc
          { pt := c.pt
            Î¹ :=
              { app := fun k => c.Î¹.app (j, k)
                naturality := fun k k' f => by
                  dsimp; simp only [Category.comp_id]
                  conv_lhs =>
                    arg 1; equals (F.map (ðŸ™ _)).app _ â‰«  (F.obj j).map f =>
                      simp;
                  conv_lhs => arg 1; rw [â† uncurry_obj_map F ((ðŸ™ j,f) : (j,k) âŸ¶ (j,k'))]
                  rw [c.w] } }
      naturality := fun j j' f =>
        (Q j).hom_ext
          (by
            dsimp
            intro k
            simp only [Limits.CoconeMorphism.w_assoc, Limits.Cocones.precompose_obj_Î¹,
              Limits.IsColimit.fac_assoc, Limits.IsColimit.fac, NatTrans.comp_app, Category.comp_id,
              Category.assoc]
            have := @NatTrans.naturality _ _ _ _ _ _ c.Î¹ (j, k) (j', k) (f, ðŸ™ k)
            dsimp at this
            simp only [Category.id_comp, Category.comp_id, CategoryTheory.Functor.map_id,
              NatTrans.id_app] at this
            exact this) }

/-- `coneOfConeUncurry Q c` is a limit cone when `c` is a limit cone.
-/
def coneOfConeUncurryIsLimit {D : DiagramOfCones F} (Q : âˆ€ j, IsLimit (D.obj j))
    {c : Cone (uncurry.obj F)} (P : IsLimit c) : IsLimit (coneOfConeUncurry Q c) where
  lift s :=
    P.lift
      { pt := s.pt
        Ï€ :=
          { app := fun p => s.Ï€.app p.1 â‰« (D.obj p.1).Ï€.app p.2
            naturality := fun p p' f => by
              dsimp; simp only [Category.id_comp, Category.assoc]
              rcases p with âŸ¨j, kâŸ©
              rcases p' with âŸ¨j', k'âŸ©
              rcases f with âŸ¨fj, fkâŸ©
              dsimp
              slice_rhs 3 4 => rw [â† NatTrans.naturality]
              slice_rhs 2 3 => rw [â† (D.obj j).Ï€.naturality]
              simp only [Functor.const_obj_map, Category.id_comp, Category.assoc]
              have w := (D.map fj).w k'
              dsimp at w
              rw [â† w]
              have n := s.Ï€.naturality fj
              dsimp at n
              simp only [Category.id_comp] at n
              rw [n]
              simp } }
  fac s j := by
    apply (Q j).hom_ext
    intro k
    simp
  uniq s m w := by
    refine P.uniq
      { pt := s.pt
        Ï€ := _ } m ?_
    rintro âŸ¨j, kâŸ©
    dsimp
    rw [â† w j]
    simp
#align category_theory.limits.cone_of_cone_uncurry_is_limit CategoryTheory.Limits.coneOfConeUncurryIsLimit

/-- `coconeOfCoconeUncurry Q c` is a colimit cocone when `c` is a colimit cocone.
-/
def coconeOfCoconeUncurryIsColimit {D : DiagramOfCocones F} (Q : âˆ€ j, IsColimit (D.obj j))
    {c : Cocone (uncurry.obj F)} (P : IsColimit c) : IsColimit (coconeOfCoconeUncurry Q c) where
  desc s :=
    P.desc
      { pt := s.pt
        Î¹ :=
          { app := fun p => (D.obj p.1).Î¹.app p.2 â‰« s.Î¹.app p.1
            naturality := fun p p' f => by
              dsimp; simp only [Category.id_comp, Category.assoc]
              rcases p with âŸ¨j, kâŸ©
              rcases p' with âŸ¨j', k'âŸ©
              rcases f with âŸ¨fj, fkâŸ©
              dsimp
              slice_lhs 2 3 => rw [(D.obj j').Î¹.naturality]
              simp only [Functor.const_obj_map, Category.id_comp, Category.assoc]
              have w := (D.map fj).w k
              dsimp at w
              slice_lhs 1 2 => rw [â† w]
              have n := s.Î¹.naturality fj
              dsimp at n
              simp only [Category.comp_id] at n
              rw [â† n]
              simp } }
  fac s j := by
    apply (Q j).hom_ext
    intro k
    simp
  uniq s m w := by
    refine P.uniq
      { pt := s.pt
        Î¹ := _ } m ?_
    rintro âŸ¨j, kâŸ©
    dsimp
    rw [â† w j]
    simp

section

variable (F)
variable [HasLimitsOfShape K C]

/-- Given a functor `F : J â¥¤ K â¥¤ C`, with all needed limits,
we can construct a diagram consisting of the limit cone over each functor `F.obj j`,
and the universal cone morphisms between these.
-/
@[simps]
noncomputable def DiagramOfCones.mkOfHasLimits : DiagramOfCones F where
  obj j := limit.cone (F.obj j)
  map f := { hom := lim.map (F.map f) }
#align category_theory.limits.diagram_of_cones.mk_of_has_limits CategoryTheory.Limits.DiagramOfCones.mkOfHasLimits

-- Satisfying the inhabited linter.
noncomputable instance diagramOfConesInhabited : Inhabited (DiagramOfCones F) :=
  âŸ¨DiagramOfCones.mkOfHasLimits FâŸ©
#align category_theory.limits.diagram_of_cones_inhabited CategoryTheory.Limits.diagramOfConesInhabited

@[simp]
theorem DiagramOfCones.mkOfHasLimits_conePoints :
    (DiagramOfCones.mkOfHasLimits F).conePoints = F â‹™ lim :=
  rfl
#align category_theory.limits.diagram_of_cones.mk_of_has_limits_cone_points CategoryTheory.Limits.DiagramOfCones.mkOfHasLimits_conePoints

variable [HasLimit (uncurry.obj F)]
variable [HasLimit (F â‹™ lim)]

/-- The Fubini theorem for a functor `F : J â¥¤ K â¥¤ C`,
showing that the limit of `uncurry.obj F` can be computed as
the limit of the limits of the functors `F.obj j`.
-/
noncomputable def limitUncurryIsoLimitCompLim : limit (uncurry.obj F) â‰… limit (F â‹™ lim) := by
  let c := limit.cone (uncurry.obj F)
  let P : IsLimit c := limit.isLimit _
  let G := DiagramOfCones.mkOfHasLimits F
  let Q : âˆ€ j, IsLimit (G.obj j) := fun j => limit.isLimit _
  have Q' := coneOfConeUncurryIsLimit Q P
  have Q'' := limit.isLimit (F â‹™ lim)
  exact IsLimit.conePointUniqueUpToIso Q' Q''
#align category_theory.limits.limit_uncurry_iso_limit_comp_lim CategoryTheory.Limits.limitUncurryIsoLimitCompLim

@[simp, reassoc]
theorem limitUncurryIsoLimitCompLim_hom_Ï€_Ï€ {j} {k} :
    (limitUncurryIsoLimitCompLim F).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k = limit.Ï€ _ (j, k) := by
  dsimp [limitUncurryIsoLimitCompLim, IsLimit.conePointUniqueUpToIso, IsLimit.uniqueUpToIso]
  simp
#align category_theory.limits.limit_uncurry_iso_limit_comp_lim_hom_Ï€_Ï€ CategoryTheory.Limits.limitUncurryIsoLimitCompLim_hom_Ï€_Ï€

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp, reassoc]
theorem limitUncurryIsoLimitCompLim_inv_Ï€ {j} {k} :
    (limitUncurryIsoLimitCompLim F).inv â‰« limit.Ï€ _ (j, k) =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  rw [â† cancel_epi (limitUncurryIsoLimitCompLim F).hom]
  simp
#align category_theory.limits.limit_uncurry_iso_limit_comp_lim_inv_Ï€ CategoryTheory.Limits.limitUncurryIsoLimitCompLim_inv_Ï€

end

section

variable (F)
variable [HasColimitsOfShape K C]

/-- Given a functor `F : J â¥¤ K â¥¤ C`, with all needed colimits,
we can construct a diagram consisting of the colimit cocone over each functor `F.obj j`,
and the universal cocone morphisms between these.
-/
@[simps]
noncomputable def DiagramOfCocones.mkOfHasColimits : DiagramOfCocones F where
  obj j := colimit.cocone (F.obj j)
  map f := { hom := colim.map (F.map f) }

-- Satisfying the inhabited linter.
noncomputable instance diagramOfCoconesInhabited : Inhabited (DiagramOfCocones F) :=
  âŸ¨DiagramOfCocones.mkOfHasColimits FâŸ©

@[simp]
theorem DiagramOfCocones.mkOfHasColimits_coconePoints :
    (DiagramOfCocones.mkOfHasColimits F).coconePoints = F â‹™ colim :=
  rfl

variable [HasColimit (uncurry.obj F)]
variable [HasColimit (F â‹™ colim)]

/-- The Fubini theorem for a functor `F : J â¥¤ K â¥¤ C`,
showing that the colimit of `uncurry.obj F` can be computed as
the colimit of the colimits of the functors `F.obj j`.
-/
noncomputable def colimitUncurryIsoColimitCompColim :
    colimit (uncurry.obj F) â‰… colimit (F â‹™ colim) := by
  let c := colimit.cocone (uncurry.obj F)
  let P : IsColimit c := colimit.isColimit _
  let G := DiagramOfCocones.mkOfHasColimits F
  let Q : âˆ€ j, IsColimit (G.obj j) := fun j => colimit.isColimit _
  have Q' := coconeOfCoconeUncurryIsColimit Q P
  have Q'' := colimit.isColimit (F â‹™ colim)
  exact IsColimit.coconePointUniqueUpToIso Q' Q''

@[simp, reassoc]
theorem colimitUncurryIsoColimitCompColim_Î¹_Î¹_inv {j} {k} :
    colimit.Î¹ (F.obj j) k â‰« colimit.Î¹ (F â‹™ colim) j â‰« (colimitUncurryIsoColimitCompColim F).inv =
      colimit.Î¹ (uncurry.obj F) (j, k) := by
  dsimp [colimitUncurryIsoColimitCompColim, IsColimit.coconePointUniqueUpToIso,
    IsColimit.uniqueUpToIso]
  simp

@[simp, reassoc]
theorem colimitUncurryIsoColimitCompColim_Î¹_hom {j} {k} :
    colimit.Î¹ _ (j, k) â‰« (colimitUncurryIsoColimitCompColim F).hom =
      (colimit.Î¹ _ k â‰« colimit.Î¹ (F â‹™ colim) j : _ âŸ¶ (colimit (F â‹™ colim))) := by
  rw [â† cancel_mono (colimitUncurryIsoColimitCompColim F).inv]
  simp

end

section

variable (F) [HasLimitsOfShape J C] [HasLimitsOfShape K C]

-- With only moderate effort these could be derived if needed:
variable [HasLimitsOfShape (J Ã— K) C] [HasLimitsOfShape (K Ã— J) C]

/-- The limit of `F.flip â‹™ lim` is isomorphic to the limit of `F â‹™ lim`. -/
noncomputable def limitFlipCompLimIsoLimitCompLim : limit (F.flip â‹™ lim) â‰… limit (F â‹™ lim) :=
  (limitUncurryIsoLimitCompLim _).symm â‰ªâ‰«
    HasLimit.isoOfNatIso (uncurryObjFlip _) â‰ªâ‰«
      HasLimit.isoOfEquivalence (Prod.braiding _ _)
          (NatIso.ofComponents fun _ => by rfl) â‰ªâ‰«
        limitUncurryIsoLimitCompLim _
#align category_theory.limits.limit_flip_comp_lim_iso_limit_comp_lim CategoryTheory.Limits.limitFlipCompLimIsoLimitCompLim

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp, reassoc]
theorem limitFlipCompLimIsoLimitCompLim_hom_Ï€_Ï€ (j) (k) :
    (limitFlipCompLimIsoLimitCompLim F).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k =
      (limit.Ï€ _ k â‰« limit.Ï€ _ j : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitFlipCompLimIsoLimitCompLim]
  simp
#align category_theory.limits.limit_flip_comp_lim_iso_limit_comp_lim_hom_Ï€_Ï€ CategoryTheory.Limits.limitFlipCompLimIsoLimitCompLim_hom_Ï€_Ï€

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
-- See note [dsimp, simp]
@[simp, reassoc]
theorem limitFlipCompLimIsoLimitCompLim_inv_Ï€_Ï€ (k) (j) :
    (limitFlipCompLimIsoLimitCompLim F).inv â‰« limit.Ï€ _ k â‰« limit.Ï€ _ j =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitFlipCompLimIsoLimitCompLim]
  simp
#align category_theory.limits.limit_flip_comp_lim_iso_limit_comp_lim_inv_Ï€_Ï€ CategoryTheory.Limits.limitFlipCompLimIsoLimitCompLim_inv_Ï€_Ï€

end

section

variable (F) [HasColimitsOfShape J C] [HasColimitsOfShape K C]
variable [HasColimitsOfShape (J Ã— K) C] [HasColimitsOfShape (K Ã— J) C]

/-- The colimit of `F.flip â‹™ colim` is isomorphic to the colimit of `F â‹™ colim`. -/
noncomputable def colimitFlipCompColimIsoColimitCompColim :
    colimit (F.flip â‹™ colim) â‰… colimit (F â‹™ colim) :=
  (colimitUncurryIsoColimitCompColim _).symm â‰ªâ‰«
    HasColimit.isoOfNatIso (uncurryObjFlip _) â‰ªâ‰«
      HasColimit.isoOfEquivalence (Prod.braiding _ _)
          (NatIso.ofComponents fun _ => by rfl) â‰ªâ‰«
        colimitUncurryIsoColimitCompColim _

@[simp, reassoc]
theorem colimitFlipCompColimIsoColimitCompColim_Î¹_Î¹_hom (j) (k) :
    colimit.Î¹ (F.flip.obj k) j â‰« colimit.Î¹ (F.flip â‹™ colim) k â‰«
      (colimitFlipCompColimIsoColimitCompColim F).hom =
        (colimit.Î¹ _ k â‰« colimit.Î¹ (F â‹™ colim) j : _ âŸ¶ colimit (Fâ‹™ colim)) := by
  dsimp [colimitFlipCompColimIsoColimitCompColim]
  slice_lhs 1 3 => simp only []
  simp

@[simp, reassoc]
theorem colimitFlipCompColimIsoColimitCompColim_Î¹_Î¹_inv (k) (j) :
    colimit.Î¹ (F.obj j) k â‰« colimit.Î¹ (F â‹™ colim) j â‰«
      (colimitFlipCompColimIsoColimitCompColim F).inv =
        (colimit.Î¹ _ j â‰« colimit.Î¹ (F.flip â‹™ colim) k : _ âŸ¶ colimit (F.flip â‹™ colim)) := by
  dsimp [colimitFlipCompColimIsoColimitCompColim]
  slice_lhs 1 3 => simp only []
  simp

end

variable (G : J Ã— K â¥¤ C)

section

variable [HasLimitsOfShape K C]
variable [HasLimit G]
variable [HasLimit (curry.obj G â‹™ lim)]

/-- The Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that the limit of `G` can be computed as
the limit of the limits of the functors `G.obj (j, _)`.
-/
noncomputable def limitIsoLimitCurryCompLim : limit G â‰… limit (curry.obj G â‹™ lim) := by
  have i : G â‰… uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unitIso.app G
  haveI : Limits.HasLimit (uncurry.obj ((@curry J _ K _ C _).obj G)) := hasLimitOfIso i
  trans limit (uncurry.obj ((@curry J _ K _ C _).obj G))
  Â· apply HasLimit.isoOfNatIso i
  Â· exact limitUncurryIsoLimitCompLim ((@curry J _ K _ C _).obj G)
#align category_theory.limits.limit_iso_limit_curry_comp_lim CategoryTheory.Limits.limitIsoLimitCurryCompLim

@[simp, reassoc]
theorem limitIsoLimitCurryCompLim_hom_Ï€_Ï€ {j} {k} :
    (limitIsoLimitCurryCompLim G).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k = limit.Ï€ _ (j, k) := by
  set_option tactic.skipAssignedInstances false in
  simp [limitIsoLimitCurryCompLim, Trans.simple, HasLimit.isoOfNatIso, limitUncurryIsoLimitCompLim]
#align category_theory.limits.limit_iso_limit_curry_comp_lim_hom_Ï€_Ï€ CategoryTheory.Limits.limitIsoLimitCurryCompLim_hom_Ï€_Ï€

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp, reassoc]
theorem limitIsoLimitCurryCompLim_inv_Ï€ {j} {k} :
    (limitIsoLimitCurryCompLim G).inv â‰« limit.Ï€ _ (j, k) =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  rw [â† cancel_epi (limitIsoLimitCurryCompLim G).hom]
  simp
#align category_theory.limits.limit_iso_limit_curry_comp_lim_inv_Ï€ CategoryTheory.Limits.limitIsoLimitCurryCompLim_inv_Ï€

end

section

variable [HasColimitsOfShape K C]
variable [HasColimit G]
variable [HasColimit (curry.obj G â‹™ colim)]

/-- The Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that the colimit of `G` can be computed as
the colimit of the colimits of the functors `G.obj (j, _)`.
-/
noncomputable def colimitIsoColimitCurryCompColim : colimit G â‰… colimit (curry.obj G â‹™ colim) := by
  have i : G â‰… uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unitIso.app G
  haveI : Limits.HasColimit (uncurry.obj ((@curry J _ K _ C _).obj G)) := hasColimitOfIso i.symm
  trans colimit (uncurry.obj ((@curry J _ K _ C _).obj G))
  Â· apply HasColimit.isoOfNatIso i
  Â· exact colimitUncurryIsoColimitCompColim ((@curry J _ K _ C _).obj G)

@[simp, reassoc]
theorem colimitIsoColimitCurryCompColim_Î¹_Î¹_inv {j} {k} :
    colimit.Î¹ ((curry.obj G).obj j) k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j â‰«
      (colimitIsoColimitCurryCompColim G).inv  = colimit.Î¹ _ (j, k) := by
  set_option tactic.skipAssignedInstances false in
  simp [colimitIsoColimitCurryCompColim, Trans.simple, HasColimit.isoOfNatIso,
    colimitUncurryIsoColimitCompColim]

@[simp, reassoc]
theorem colimitIsoColimitCurryCompColim_Î¹_hom {j} {k} :
    colimit.Î¹ _ (j, k) â‰« (colimitIsoColimitCurryCompColim G).hom =
      (colimit.Î¹ (_) k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j : _ âŸ¶ colimit (_ â‹™ colim)) := by
  rw [â† cancel_mono (colimitIsoColimitCurryCompColim G).inv]
  simp

end

section

variable [HasLimits C]

-- Certainly one could weaken the hypotheses here.
open CategoryTheory.prod

/-- A variant of the Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that $\lim_k \lim_j G(j,k) â‰… \lim_j \lim_k G(j,k)$.
-/
noncomputable def limitCurrySwapCompLimIsoLimitCurryCompLim :
    limit (curry.obj (Prod.swap K J â‹™ G) â‹™ lim) â‰… limit (curry.obj G â‹™ lim) :=
  calc
    limit (curry.obj (Prod.swap K J â‹™ G) â‹™ lim) â‰… limit (Prod.swap K J â‹™ G) :=
      (limitIsoLimitCurryCompLim _).symm
    _ â‰… limit G := HasLimit.isoOfEquivalence (Prod.braiding K J) (Iso.refl _)
    _ â‰… limit (curry.obj G â‹™ lim) := limitIsoLimitCurryCompLim _
#align category_theory.limits.limit_curry_swap_comp_lim_iso_limit_curry_comp_lim CategoryTheory.Limits.limitCurrySwapCompLimIsoLimitCurryCompLim

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp]
theorem limitCurrySwapCompLimIsoLimitCurryCompLim_hom_Ï€_Ï€ {j} {k} :
    (limitCurrySwapCompLimIsoLimitCurryCompLim G).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k =
      (limit.Ï€ _ k â‰« limit.Ï€ _ j : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitCurrySwapCompLimIsoLimitCurryCompLim]
  simp only [Iso.refl_hom, Prod.braiding_counitIso_hom_app, Limits.HasLimit.isoOfEquivalence_hom_Ï€,
    Iso.refl_inv, limitIsoLimitCurryCompLim_hom_Ï€_Ï€, eqToIso_refl, Category.assoc]
  erw [NatTrans.id_app]
  -- Why can't `simp` do this?
  dsimp
  -- Porting note: the original proof only had `simp`.
  -- However, now `CategoryTheory.Bifunctor.map_id` does not get used by `simp`
  rw [CategoryTheory.Bifunctor.map_id]
  simp

#align category_theory.limits.limit_curry_swap_comp_lim_iso_limit_curry_comp_lim_hom_Ï€_Ï€ CategoryTheory.Limits.limitCurrySwapCompLimIsoLimitCurryCompLim_hom_Ï€_Ï€

-- Porting note: Added type annotation `limit (_ â‹™ lim) âŸ¶ _`
@[simp]
theorem limitCurrySwapCompLimIsoLimitCurryCompLim_inv_Ï€_Ï€ {j} {k} :
    (limitCurrySwapCompLimIsoLimitCurryCompLim G).inv â‰« limit.Ï€ _ k â‰« limit.Ï€ _ j =
      (limit.Ï€ _ j â‰« limit.Ï€ _ k : limit (_ â‹™ lim) âŸ¶ _) := by
  dsimp [limitCurrySwapCompLimIsoLimitCurryCompLim]
  simp only [Iso.refl_hom, Prod.braiding_counitIso_hom_app, Limits.HasLimit.isoOfEquivalence_inv_Ï€,
    Iso.refl_inv, limitIsoLimitCurryCompLim_hom_Ï€_Ï€, eqToIso_refl, Category.assoc]
  erw [NatTrans.id_app]
  -- Porting note (#10618): `simp` can do this in lean 4.
  simp
#align category_theory.limits.limit_curry_swap_comp_lim_iso_limit_curry_comp_lim_inv_Ï€_Ï€ CategoryTheory.Limits.limitCurrySwapCompLimIsoLimitCurryCompLim_inv_Ï€_Ï€

end

section

variable [HasColimits C]

open CategoryTheory.prod

/-- A variant of the Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that $\colim_k \colim_j G(j,k) â‰… \colim_j \colim_k G(j,k)$.
-/
noncomputable def colimitCurrySwapCompColimIsoColimitCurryCompColim :
    colimit (curry.obj (Prod.swap K J â‹™ G) â‹™ colim) â‰… colimit (curry.obj G â‹™ colim) :=
  calc
    colimit (curry.obj (Prod.swap K J â‹™ G) â‹™ colim) â‰… colimit (Prod.swap K J â‹™ G) :=
      (colimitIsoColimitCurryCompColim _).symm
    _ â‰… colimit G := HasColimit.isoOfEquivalence (Prod.braiding K J) (Iso.refl _)
    _ â‰… colimit (curry.obj G â‹™ colim) := colimitIsoColimitCurryCompColim _

@[simp]
theorem colimitCurrySwapCompColimIsoColimitCurryCompColim_Î¹_Î¹_hom {j} {k} :
    colimit.Î¹ _ j â‰« colimit.Î¹ (curry.obj (Prod.swap K J â‹™ G) â‹™ colim) k â‰«
      (colimitCurrySwapCompColimIsoColimitCurryCompColim G).hom =
        (colimit.Î¹ _ k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j : _ âŸ¶ colimit (curry.obj Gâ‹™ colim)) := by
  dsimp [colimitCurrySwapCompColimIsoColimitCurryCompColim]
  slice_lhs 1 3 => simp only []
  simp

@[simp]
theorem colimitCurrySwapCompColimIsoColimitCurryCompColim_Î¹_Î¹_inv {j} {k} :
    colimit.Î¹ _ k â‰« colimit.Î¹ (curry.obj G â‹™ colim) j â‰«
      (colimitCurrySwapCompColimIsoColimitCurryCompColim G).inv =
        (colimit.Î¹ _ j â‰«
          colimit.Î¹ (curry.obj _ â‹™ colim) k :
            _ âŸ¶ colimit (curry.obj (Prod.swap K J â‹™ G) â‹™ colim)) := by
  dsimp [colimitCurrySwapCompColimIsoColimitCurryCompColim]
  slice_lhs 1 3 => simp only []
  simp only [colimitIsoColimitCurryCompColim_Î¹_Î¹_inv, HasColimit.isoOfEquivalence_inv_Ï€,
    Functor.id_obj, Functor.comp_obj, Prod.braiding_inverse_obj, Prod.braiding_functor_obj,
    Prod.braiding_counitIso_inv_app, Prod.swap_obj, Iso.refl_hom, NatTrans.id_app, Category.id_comp,
    Category.assoc, colimitIsoColimitCurryCompColim_Î¹_hom, curry_obj_obj_obj]
  erw [CategoryTheory.Bifunctor.map_id]
  simp

end

end CategoryTheory.Limits
