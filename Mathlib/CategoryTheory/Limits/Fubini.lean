/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Products.Basic
import Mathlib.CategoryTheory.Functor.Currying

#align_import category_theory.limits.fubini from "leanprover-community/mathlib"@"59382264386afdbaf1727e617f5fdda511992eb9"

/-!
# A Fubini theorem for categorical (co)limits

We prove that $lim_{J √ó K} G = lim_J (lim_K G(j, -))$ for a functor `G : J √ó K ‚•§ C`,
when all the appropriate limits exist.

We begin working with a functor `F : J ‚•§ K ‚•§ C`. We'll write `G : J √ó K ‚•§ C` for the associated
"uncurried" functor.

In the first part, given a coherent family `D` of limit cones over the functors `F.obj j`,
and a cone `c` over `G`, we construct a cone over the cone points of `D`.
We then show that if `c` is a limit cone, the constructed cone is also a limit cone.

In the second part, we state the Fubini theorem in the setting where limits are
provided by suitable `HasLimit` classes.

We construct
`limitUncurryIsoLimitCompLim F : limit (uncurry.obj F) ‚âÖ limit (F ‚ãô lim)`
and give simp lemmas characterising it.
For convenience, we also provide
`limitIsoLimitCurryCompLim G : limit G ‚âÖ limit ((curry.obj G) ‚ãô lim)`
in terms of the uncurried functor.

All statements have their counterpart for colimits.
-/


universe v u

open CategoryTheory

namespace CategoryTheory.Limits

variable {J K : Type v} [SmallCategory J] [SmallCategory K]

variable {C : Type u} [Category.{v} C]

variable (F : J ‚•§ K ‚•§ C)

-- We could try introducing a "dependent functor type" to handle this?
/-- A structure carrying a diagram of cones over the functors `F.obj j`.
-/
structure DiagramOfCones where
  /-- For each object, a cone. -/
  obj : ‚àÄ j : J, Cone (F.obj j)
  /-- For each map, a map of cones. -/
  map : ‚àÄ {j j' : J} (f : j ‚ü∂ j'), (Cones.postcompose (F.map f)).obj (obj j) ‚ü∂ obj j'
  id : ‚àÄ j : J, (map (ùüô j)).hom = ùüô _ := by aesop_cat
  comp : ‚àÄ {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ) (g : j‚ÇÇ ‚ü∂ j‚ÇÉ),
    (map (f ‚â´ g)).hom = (map f).hom ‚â´ (map g).hom := by aesop_cat
#align category_theory.limits.diagram_of_cones CategoryTheory.Limits.DiagramOfCones

/-- A structure carrying a diagram of cocones over the functors `F.obj j`.
-/
structure DiagramOfCocones where
  /-- For each object, a cocone. -/
  obj : ‚àÄ j : J, Cocone (F.obj j)
  /-- For each map, a map of cocones. -/
  map : ‚àÄ {j j' : J} (f : j ‚ü∂ j'), (obj j) ‚ü∂ (Cocones.precompose (F.map f)).obj (obj j')
  id : ‚àÄ j : J, (map (ùüô j)).hom = ùüô _ := by aesop_cat
  comp : ‚àÄ {j‚ÇÅ j‚ÇÇ j‚ÇÉ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ) (g : j‚ÇÇ ‚ü∂ j‚ÇÉ),
    (map (f ‚â´ g)).hom = (map f).hom ‚â´ (map g).hom := by aesop_cat

variable {F}

/-- Extract the functor `J ‚•§ C` consisting of the cone points and the maps between them,
from a `DiagramOfCones`.
-/
@[simps]
def DiagramOfCones.conePoints (D : DiagramOfCones F) : J ‚•§ C where
  obj j := (D.obj j).pt
  map f := (D.map f).hom
  map_id j := D.id j
  map_comp f g := D.comp f g
#align category_theory.limits.diagram_of_cones.cone_points CategoryTheory.Limits.DiagramOfCones.conePoints

/-- Extract the functor `J ‚•§ C` consisting of the cocone points and the maps between them,
from a `DiagramOfCocones`.
-/
@[simps]
def DiagramOfCocones.coconePoints (D : DiagramOfCocones F) : J ‚•§ C where
  obj j := (D.obj j).pt
  map f := (D.map f).hom
  map_id j := D.id j
  map_comp f g := D.comp f g

/-- Given a diagram `D` of limit cones over the `F.obj j`, and a cone over `uncurry.obj F`,
we can construct a cone over the diagram consisting of the cone points from `D`.
-/
@[simps]
def coneOfConeUncurry {D : DiagramOfCones F} (Q : ‚àÄ j, IsLimit (D.obj j))
    (c : Cone (uncurry.obj F)) : Cone D.conePoints where
  pt := c.pt
  œÄ :=
    { app := fun j =>
        (Q j).lift
          { pt := c.pt
            œÄ :=
              { app := fun k => c.œÄ.app (j, k)
                naturality := fun k k' f => by
                  dsimp; simp only [Category.id_comp]
                  have := @NatTrans.naturality _ _ _ _ _ _ c.œÄ (j, k) (j, k') (ùüô j, f)
                  dsimp at this
                  simp? at this says
                    simp only [Category.id_comp, Functor.map_id, NatTrans.id_app] at this
                  exact this } }
      naturality := fun j j' f =>
        (Q j').hom_ext
          (by
            dsimp
            intro k
            simp only [Limits.ConeMorphism.w, Limits.Cones.postcompose_obj_œÄ,
              Limits.IsLimit.fac_assoc, Limits.IsLimit.fac, NatTrans.comp_app, Category.id_comp,
              Category.assoc]
            have := @NatTrans.naturality _ _ _ _ _ _ c.œÄ (j, k) (j', k) (f, ùüô k)
            dsimp at this
            simp only [Category.id_comp, Category.comp_id, CategoryTheory.Functor.map_id,
              NatTrans.id_app] at this
            exact this) }
#align category_theory.limits.cone_of_cone_uncurry CategoryTheory.Limits.coneOfConeUncurry

/-- Given a diagram `D` of colimit cocones over the `F.obj j`, and a cocone over `uncurry.obj F`,
we can construct a cocone over the diagram consisting of the cocone points from `D`.
-/
@[simps]
def coconeOfCoconeUncurry {D : DiagramOfCocones F} (Q : ‚àÄ j, IsColimit (D.obj j))
    (c : Cocone (uncurry.obj F)) : Cocone D.coconePoints where
  pt := c.pt
  Œπ :=
    { app := fun j =>
        (Q j).desc
          { pt := c.pt
            Œπ :=
              { app := fun k => c.Œπ.app (j, k)
                naturality := fun k k' f => by
                  dsimp; simp only [Category.comp_id]
                  conv_lhs =>
                    arg 1; equals (F.map (ùüô _)).app _ ‚â´  (F.obj j).map f =>
                      simp;
                  conv_lhs => arg 1; rw [‚Üê uncurry_obj_map F ((ùüô j,f) : (j,k) ‚ü∂ (j,k'))]
                  rw [c.w] } }
      naturality := fun j j' f =>
        (Q j).hom_ext
          (by
            dsimp
            intro k
            simp only [Limits.CoconeMorphism.w_assoc, Limits.Cocones.precompose_obj_Œπ,
              Limits.IsColimit.fac_assoc, Limits.IsColimit.fac, NatTrans.comp_app, Category.comp_id,
              Category.assoc]
            have := @NatTrans.naturality _ _ _ _ _ _ c.Œπ (j, k) (j', k) (f, ùüô k)
            dsimp at this
            simp only [Category.id_comp, Category.comp_id, CategoryTheory.Functor.map_id,
              NatTrans.id_app] at this
            exact this) }

/-- `coneOfConeUncurry Q c` is a limit cone when `c` is a limit cone.
-/
def coneOfConeUncurryIsLimit {D : DiagramOfCones F} (Q : ‚àÄ j, IsLimit (D.obj j))
    {c : Cone (uncurry.obj F)} (P : IsLimit c) : IsLimit (coneOfConeUncurry Q c) where
  lift s :=
    P.lift
      { pt := s.pt
        œÄ :=
          { app := fun p => s.œÄ.app p.1 ‚â´ (D.obj p.1).œÄ.app p.2
            naturality := fun p p' f => by
              dsimp; simp only [Category.id_comp, Category.assoc]
              rcases p with ‚ü®j, k‚ü©
              rcases p' with ‚ü®j', k'‚ü©
              rcases f with ‚ü®fj, fk‚ü©
              dsimp
              slice_rhs 3 4 => rw [‚Üê NatTrans.naturality]
              slice_rhs 2 3 => rw [‚Üê (D.obj j).œÄ.naturality]
              simp only [Functor.const_obj_map, Category.id_comp, Category.assoc]
              have w := (D.map fj).w k'
              dsimp at w
              rw [‚Üê w]
              have n := s.œÄ.naturality fj
              dsimp at n
              simp only [Category.id_comp] at n
              rw [n]
              simp } }
  fac s j := by
    apply (Q j).hom_ext
    intro k
    simp
  uniq s m w := by
    refine' P.uniq
      { pt := s.pt
        œÄ := _ } m _
    rintro ‚ü®j, k‚ü©
    dsimp
    rw [‚Üê w j]
    simp
#align category_theory.limits.cone_of_cone_uncurry_is_limit CategoryTheory.Limits.coneOfConeUncurryIsLimit

/-- `coconeOfCoconeUncurry Q c` is a colimit cocone when `c` is a colimit cocone.
-/
def coconeOfCoconeUncurryIsColimit {D : DiagramOfCocones F} (Q : ‚àÄ j, IsColimit (D.obj j))
    {c : Cocone (uncurry.obj F)} (P : IsColimit c) : IsColimit (coconeOfCoconeUncurry Q c) where
  desc s :=
    P.desc
      { pt := s.pt
        Œπ :=
          { app := fun p => (D.obj p.1).Œπ.app p.2 ‚â´ s.Œπ.app p.1
            naturality := fun p p' f => by
              dsimp; simp only [Category.id_comp, Category.assoc]
              rcases p with ‚ü®j, k‚ü©
              rcases p' with ‚ü®j', k'‚ü©
              rcases f with ‚ü®fj, fk‚ü©
              dsimp
              slice_lhs 2 3 => rw [(D.obj j').Œπ.naturality]
              simp only [Functor.const_obj_map, Category.id_comp, Category.assoc]
              have w := (D.map fj).w k
              dsimp at w
              slice_lhs 1 2 => rw [‚Üê w]
              have n := s.Œπ.naturality fj
              dsimp at n
              simp only [Category.comp_id] at n
              rw [‚Üê n]
              simp } }
  fac s j := by
    apply (Q j).hom_ext
    intro k
    simp
  uniq s m w := by
    refine' P.uniq
      { pt := s.pt
        Œπ := _ } m _
    rintro ‚ü®j, k‚ü©
    dsimp
    rw [‚Üê w j]
    simp

section

variable (F)

variable [HasLimitsOfShape K C]

/-- Given a functor `F : J ‚•§ K ‚•§ C`, with all needed limits,
we can construct a diagram consisting of the limit cone over each functor `F.obj j`,
and the universal cone morphisms between these.
-/
@[simps]
noncomputable def DiagramOfCones.mkOfHasLimits : DiagramOfCones F where
  obj j := limit.cone (F.obj j)
  map f := { hom := lim.map (F.map f) }
#align category_theory.limits.diagram_of_cones.mk_of_has_limits CategoryTheory.Limits.DiagramOfCones.mkOfHasLimits

-- Satisfying the inhabited linter.
noncomputable instance diagramOfConesInhabited : Inhabited (DiagramOfCones F) :=
  ‚ü®DiagramOfCones.mkOfHasLimits F‚ü©
#align category_theory.limits.diagram_of_cones_inhabited CategoryTheory.Limits.diagramOfConesInhabited

@[simp]
theorem DiagramOfCones.mkOfHasLimits_conePoints :
    (DiagramOfCones.mkOfHasLimits F).conePoints = F ‚ãô lim :=
  rfl
#align category_theory.limits.diagram_of_cones.mk_of_has_limits_cone_points CategoryTheory.Limits.DiagramOfCones.mkOfHasLimits_conePoints

variable [HasLimit (uncurry.obj F)]

variable [HasLimit (F ‚ãô lim)]

/-- The Fubini theorem for a functor `F : J ‚•§ K ‚•§ C`,
showing that the limit of `uncurry.obj F` can be computed as
the limit of the limits of the functors `F.obj j`.
-/
noncomputable def limitUncurryIsoLimitCompLim : limit (uncurry.obj F) ‚âÖ limit (F ‚ãô lim) := by
  let c := limit.cone (uncurry.obj F)
  let P : IsLimit c := limit.isLimit _
  let G := DiagramOfCones.mkOfHasLimits F
  let Q : ‚àÄ j, IsLimit (G.obj j) := fun j => limit.isLimit _
  have Q' := coneOfConeUncurryIsLimit Q P
  have Q'' := limit.isLimit (F ‚ãô lim)
  exact IsLimit.conePointUniqueUpToIso Q' Q''
#align category_theory.limits.limit_uncurry_iso_limit_comp_lim CategoryTheory.Limits.limitUncurryIsoLimitCompLim

@[simp, reassoc]
theorem limitUncurryIsoLimitCompLim_hom_œÄ_œÄ {j} {k} :
    (limitUncurryIsoLimitCompLim F).hom ‚â´ limit.œÄ _ j ‚â´ limit.œÄ _ k = limit.œÄ _ (j, k) := by
  dsimp [limitUncurryIsoLimitCompLim, IsLimit.conePointUniqueUpToIso, IsLimit.uniqueUpToIso]
  simp
#align category_theory.limits.limit_uncurry_iso_limit_comp_lim_hom_œÄ_œÄ CategoryTheory.Limits.limitUncurryIsoLimitCompLim_hom_œÄ_œÄ

-- Porting note: Added type annotation `limit (_ ‚ãô lim) ‚ü∂ _`
@[simp, reassoc]
theorem limitUncurryIsoLimitCompLim_inv_œÄ {j} {k} :
    (limitUncurryIsoLimitCompLim F).inv ‚â´ limit.œÄ _ (j, k) =
      (limit.œÄ _ j ‚â´ limit.œÄ _ k : limit (_ ‚ãô lim) ‚ü∂ _) := by
  rw [‚Üê cancel_epi (limitUncurryIsoLimitCompLim F).hom]
  simp
#align category_theory.limits.limit_uncurry_iso_limit_comp_lim_inv_œÄ CategoryTheory.Limits.limitUncurryIsoLimitCompLim_inv_œÄ

end

section

variable (F)

variable [HasColimitsOfShape K C]

/-- Given a functor `F : J ‚•§ K ‚•§ C`, with all needed colimits,
we can construct a diagram consisting of the colimit cocone over each functor `F.obj j`,
and the universal cocone morphisms between these.
-/
@[simps]
noncomputable def DiagramOfCocones.mkOfHasColimits : DiagramOfCocones F where
  obj j := colimit.cocone (F.obj j)
  map f := { hom := colim.map (F.map f) }

-- Satisfying the inhabited linter.
noncomputable instance diagramOfCoconesInhabited : Inhabited (DiagramOfCocones F) :=
  ‚ü®DiagramOfCocones.mkOfHasColimits F‚ü©

@[simp]
theorem DiagramOfCocones.mkOfHasColimits_coconePoints :
    (DiagramOfCocones.mkOfHasColimits F).coconePoints = F ‚ãô colim :=
  rfl

variable [HasColimit (uncurry.obj F)]

variable [HasColimit (F ‚ãô colim)]

/-- The Fubini theorem for a functor `F : J ‚•§ K ‚•§ C`,
showing that the colimit of `uncurry.obj F` can be computed as
the colimit of the colimits of the functors `F.obj j`.
-/
noncomputable def colimitUncurryIsoColimitCompColim :
    colimit (uncurry.obj F) ‚âÖ colimit (F ‚ãô colim) := by
  let c := colimit.cocone (uncurry.obj F)
  let P : IsColimit c := colimit.isColimit _
  let G := DiagramOfCocones.mkOfHasColimits F
  let Q : ‚àÄ j, IsColimit (G.obj j) := fun j => colimit.isColimit _
  have Q' := coconeOfCoconeUncurryIsColimit Q P
  have Q'' := colimit.isColimit (F ‚ãô colim)
  exact IsColimit.coconePointUniqueUpToIso Q' Q''

@[simp, reassoc]
theorem colimitUncurryIsoColimitCompColim_Œπ_Œπ_inv {j} {k} :
    colimit.Œπ (F.obj j) k ‚â´ colimit.Œπ (F ‚ãô colim) j ‚â´ (colimitUncurryIsoColimitCompColim F).inv =
      colimit.Œπ (uncurry.obj F) (j, k) := by
  dsimp [colimitUncurryIsoColimitCompColim, IsColimit.coconePointUniqueUpToIso,
    IsColimit.uniqueUpToIso]
  simp

@[simp, reassoc]
theorem colimitUncurryIsoColimitCompColim_Œπ_hom {j} {k} :
    colimit.Œπ _ (j, k) ‚â´ (colimitUncurryIsoColimitCompColim F).hom =
      (colimit.Œπ _ k ‚â´ colimit.Œπ (F ‚ãô colim) j : _ ‚ü∂ (colimit (F ‚ãô colim))) := by
  rw [‚Üê cancel_mono (colimitUncurryIsoColimitCompColim F).inv]
  simp

end

section

variable (F) [HasLimitsOfShape J C] [HasLimitsOfShape K C]

-- With only moderate effort these could be derived if needed:
variable [HasLimitsOfShape (J √ó K) C] [HasLimitsOfShape (K √ó J) C]

/-- The limit of `F.flip ‚ãô lim` is isomorphic to the limit of `F ‚ãô lim`. -/
noncomputable def limitFlipCompLimIsoLimitCompLim : limit (F.flip ‚ãô lim) ‚âÖ limit (F ‚ãô lim) :=
  (limitUncurryIsoLimitCompLim _).symm ‚â™‚â´
    HasLimit.isoOfNatIso (uncurryObjFlip _) ‚â™‚â´
      HasLimit.isoOfEquivalence (Prod.braiding _ _)
          (NatIso.ofComponents fun _ => by rfl) ‚â™‚â´
        limitUncurryIsoLimitCompLim _
#align category_theory.limits.limit_flip_comp_lim_iso_limit_comp_lim CategoryTheory.Limits.limitFlipCompLimIsoLimitCompLim

-- Porting note: Added type annotation `limit (_ ‚ãô lim) ‚ü∂ _`
@[simp, reassoc]
theorem limitFlipCompLimIsoLimitCompLim_hom_œÄ_œÄ (j) (k) :
    (limitFlipCompLimIsoLimitCompLim F).hom ‚â´ limit.œÄ _ j ‚â´ limit.œÄ _ k =
      (limit.œÄ _ k ‚â´ limit.œÄ _ j : limit (_ ‚ãô lim) ‚ü∂ _) := by
  dsimp [limitFlipCompLimIsoLimitCompLim]
  simp
#align category_theory.limits.limit_flip_comp_lim_iso_limit_comp_lim_hom_œÄ_œÄ CategoryTheory.Limits.limitFlipCompLimIsoLimitCompLim_hom_œÄ_œÄ

-- Porting note: Added type annotation `limit (_ ‚ãô lim) ‚ü∂ _`
-- See note [dsimp, simp]
@[simp, reassoc]
theorem limitFlipCompLimIsoLimitCompLim_inv_œÄ_œÄ (k) (j) :
    (limitFlipCompLimIsoLimitCompLim F).inv ‚â´ limit.œÄ _ k ‚â´ limit.œÄ _ j =
      (limit.œÄ _ j ‚â´ limit.œÄ _ k : limit (_ ‚ãô lim) ‚ü∂ _) := by
  dsimp [limitFlipCompLimIsoLimitCompLim]
  simp
#align category_theory.limits.limit_flip_comp_lim_iso_limit_comp_lim_inv_œÄ_œÄ CategoryTheory.Limits.limitFlipCompLimIsoLimitCompLim_inv_œÄ_œÄ

end

section

variable (F) [HasColimitsOfShape J C] [HasColimitsOfShape K C]

variable [HasColimitsOfShape (J √ó K) C] [HasColimitsOfShape (K √ó J) C]

/-- The colimit of `F.flip ‚ãô colim` is isomorphic to the colimit of `F ‚ãô colim`. -/
noncomputable def colimitFlipCompColimIsoColimitCompColim :
    colimit (F.flip ‚ãô colim) ‚âÖ colimit (F ‚ãô colim) :=
  (colimitUncurryIsoColimitCompColim _).symm ‚â™‚â´
    HasColimit.isoOfNatIso (uncurryObjFlip _) ‚â™‚â´
      HasColimit.isoOfEquivalence (Prod.braiding _ _)
          (NatIso.ofComponents fun _ => by rfl) ‚â™‚â´
        colimitUncurryIsoColimitCompColim _

@[simp, reassoc]
theorem colimitFlipCompColimIsoColimitCompColim_Œπ_Œπ_hom (j) (k) :
    colimit.Œπ (F.flip.obj k) j ‚â´ colimit.Œπ (F.flip ‚ãô colim) k ‚â´
      (colimitFlipCompColimIsoColimitCompColim F).hom =
        (colimit.Œπ _ k ‚â´ colimit.Œπ (F ‚ãô colim) j : _ ‚ü∂ colimit (F‚ãô colim)) := by
  dsimp [colimitFlipCompColimIsoColimitCompColim]
  slice_lhs 1 3 => simp only []
  simp

@[simp, reassoc]
theorem colimitFlipCompColimIsoColimitCompColim_Œπ_Œπ_inv (k) (j) :
    colimit.Œπ (F.obj j) k ‚â´ colimit.Œπ (F ‚ãô colim) j ‚â´
      (colimitFlipCompColimIsoColimitCompColim F).inv =
        (colimit.Œπ _ j ‚â´ colimit.Œπ (F.flip ‚ãô colim) k : _ ‚ü∂ colimit (F.flip ‚ãô colim)) := by
  dsimp [colimitFlipCompColimIsoColimitCompColim]
  slice_lhs 1 3 => simp only []
  simp

end

variable (G : J √ó K ‚•§ C)

section

variable [HasLimitsOfShape K C]

variable [HasLimit G]

variable [HasLimit (curry.obj G ‚ãô lim)]

/-- The Fubini theorem for a functor `G : J √ó K ‚•§ C`,
showing that the limit of `G` can be computed as
the limit of the limits of the functors `G.obj (j, _)`.
-/
noncomputable def limitIsoLimitCurryCompLim : limit G ‚âÖ limit (curry.obj G ‚ãô lim) := by
  have i : G ‚âÖ uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unitIso.app G
  haveI : Limits.HasLimit (uncurry.obj ((@curry J _ K _ C _).obj G)) := hasLimitOfIso i
  trans limit (uncurry.obj ((@curry J _ K _ C _).obj G))
  ¬∑ apply HasLimit.isoOfNatIso i
  ¬∑ exact limitUncurryIsoLimitCompLim ((@curry J _ K _ C _).obj G)
#align category_theory.limits.limit_iso_limit_curry_comp_lim CategoryTheory.Limits.limitIsoLimitCurryCompLim

@[simp, reassoc]
theorem limitIsoLimitCurryCompLim_hom_œÄ_œÄ {j} {k} :
    (limitIsoLimitCurryCompLim G).hom ‚â´ limit.œÄ _ j ‚â´ limit.œÄ _ k = limit.œÄ _ (j, k) := by
  simp [limitIsoLimitCurryCompLim, Trans.simple, HasLimit.isoOfNatIso, limitUncurryIsoLimitCompLim]
#align category_theory.limits.limit_iso_limit_curry_comp_lim_hom_œÄ_œÄ CategoryTheory.Limits.limitIsoLimitCurryCompLim_hom_œÄ_œÄ

-- Porting note: Added type annotation `limit (_ ‚ãô lim) ‚ü∂ _`
@[simp, reassoc]
theorem limitIsoLimitCurryCompLim_inv_œÄ {j} {k} :
    (limitIsoLimitCurryCompLim G).inv ‚â´ limit.œÄ _ (j, k) =
      (limit.œÄ _ j ‚â´ limit.œÄ _ k : limit (_ ‚ãô lim) ‚ü∂ _) := by
  rw [‚Üê cancel_epi (limitIsoLimitCurryCompLim G).hom]
  simp
#align category_theory.limits.limit_iso_limit_curry_comp_lim_inv_œÄ CategoryTheory.Limits.limitIsoLimitCurryCompLim_inv_œÄ

end

section

variable [HasColimitsOfShape K C]

variable [HasColimit G]

variable [HasColimit (curry.obj G ‚ãô colim)]

/-- The Fubini theorem for a functor `G : J √ó K ‚•§ C`,
showing that the colimit of `G` can be computed as
the colimit of the colimits of the functors `G.obj (j, _)`.
-/
noncomputable def colimitIsoColimitCurryCompColim : colimit G ‚âÖ colimit (curry.obj G ‚ãô colim) := by
  have i : G ‚âÖ uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unitIso.app G
  haveI : Limits.HasColimit (uncurry.obj ((@curry J _ K _ C _).obj G)) := hasColimitOfIso i.symm
  trans colimit (uncurry.obj ((@curry J _ K _ C _).obj G))
  apply HasColimit.isoOfNatIso i
  exact colimitUncurryIsoColimitCompColim ((@curry J _ K _ C _).obj G)

@[simp, reassoc]
theorem colimitIsoColimitCurryCompColim_Œπ_Œπ_inv {j} {k} :
    colimit.Œπ ((curry.obj G).obj j) k ‚â´ colimit.Œπ (curry.obj G ‚ãô colim) j ‚â´
      (colimitIsoColimitCurryCompColim G).inv  = colimit.Œπ _ (j, k) := by
  simp [colimitIsoColimitCurryCompColim, Trans.simple, HasColimit.isoOfNatIso,
    colimitUncurryIsoColimitCompColim]

@[simp, reassoc]
theorem colimitIsoColimitCurryCompColim_Œπ_hom {j} {k} :
    colimit.Œπ _ (j, k) ‚â´ (colimitIsoColimitCurryCompColim G).hom =
      (colimit.Œπ (_) k ‚â´ colimit.Œπ (curry.obj G ‚ãô colim) j : _ ‚ü∂ colimit (_ ‚ãô colim)) := by
  rw [‚Üê cancel_mono (colimitIsoColimitCurryCompColim G).inv]
  simp

end

section

variable [HasLimits C]

-- Certainly one could weaken the hypotheses here.
open CategoryTheory.prod

/-- A variant of the Fubini theorem for a functor `G : J √ó K ‚•§ C`,
showing that $\lim_k \lim_j G(j,k) ‚âÖ \lim_j \lim_k G(j,k)$.
-/
noncomputable def limitCurrySwapCompLimIsoLimitCurryCompLim :
    limit (curry.obj (Prod.swap K J ‚ãô G) ‚ãô lim) ‚âÖ limit (curry.obj G ‚ãô lim) :=
  calc
    limit (curry.obj (Prod.swap K J ‚ãô G) ‚ãô lim) ‚âÖ limit (Prod.swap K J ‚ãô G) :=
      (limitIsoLimitCurryCompLim _).symm
    _ ‚âÖ limit G := (HasLimit.isoOfEquivalence (Prod.braiding K J) (Iso.refl _))
    _ ‚âÖ limit (curry.obj G ‚ãô lim) := limitIsoLimitCurryCompLim _
#align category_theory.limits.limit_curry_swap_comp_lim_iso_limit_curry_comp_lim CategoryTheory.Limits.limitCurrySwapCompLimIsoLimitCurryCompLim

-- Porting note: Added type annotation `limit (_ ‚ãô lim) ‚ü∂ _`
@[simp]
theorem limitCurrySwapCompLimIsoLimitCurryCompLim_hom_œÄ_œÄ {j} {k} :
    (limitCurrySwapCompLimIsoLimitCurryCompLim G).hom ‚â´ limit.œÄ _ j ‚â´ limit.œÄ _ k =
      (limit.œÄ _ k ‚â´ limit.œÄ _ j : limit (_ ‚ãô lim) ‚ü∂ _) := by
  dsimp [limitCurrySwapCompLimIsoLimitCurryCompLim]
  simp only [Iso.refl_hom, Prod.braiding_counitIso_hom_app, Limits.HasLimit.isoOfEquivalence_hom_œÄ,
    Iso.refl_inv, limitIsoLimitCurryCompLim_hom_œÄ_œÄ, eqToIso_refl, Category.assoc]
  erw [NatTrans.id_app]
  -- Why can't `simp` do this?
  dsimp
  -- porting note: the original proof only had `simp`.
  -- However, now `CategoryTheory.Bifunctor.map_id` does not get used by `simp`
  rw [CategoryTheory.Bifunctor.map_id]
  simp

#align category_theory.limits.limit_curry_swap_comp_lim_iso_limit_curry_comp_lim_hom_œÄ_œÄ CategoryTheory.Limits.limitCurrySwapCompLimIsoLimitCurryCompLim_hom_œÄ_œÄ

-- Porting note: Added type annotation `limit (_ ‚ãô lim) ‚ü∂ _`
@[simp]
theorem limitCurrySwapCompLimIsoLimitCurryCompLim_inv_œÄ_œÄ {j} {k} :
    (limitCurrySwapCompLimIsoLimitCurryCompLim G).inv ‚â´ limit.œÄ _ k ‚â´ limit.œÄ _ j =
      (limit.œÄ _ j ‚â´ limit.œÄ _ k : limit (_ ‚ãô lim) ‚ü∂ _) := by
  dsimp [limitCurrySwapCompLimIsoLimitCurryCompLim]
  simp only [Iso.refl_hom, Prod.braiding_counitIso_hom_app, Limits.HasLimit.isoOfEquivalence_inv_œÄ,
    Iso.refl_inv, limitIsoLimitCurryCompLim_hom_œÄ_œÄ, eqToIso_refl, Category.assoc]
  erw [NatTrans.id_app]
  -- Porting note: `simp` can do this in lean 4.
  simp
#align category_theory.limits.limit_curry_swap_comp_lim_iso_limit_curry_comp_lim_inv_œÄ_œÄ CategoryTheory.Limits.limitCurrySwapCompLimIsoLimitCurryCompLim_inv_œÄ_œÄ

end

section

variable [HasColimits C]

open CategoryTheory.prod

/-- A variant of the Fubini theorem for a functor `G : J √ó K ‚•§ C`,
showing that $\colim_k \colim_j G(j,k) ‚âÖ \colim_j \colim_k G(j,k)$.
-/
noncomputable def colimitCurrySwapCompColimIsoColimitCurryCompColim :
    colimit (curry.obj (Prod.swap K J ‚ãô G) ‚ãô colim) ‚âÖ colimit (curry.obj G ‚ãô colim) :=
  calc
    colimit (curry.obj (Prod.swap K J ‚ãô G) ‚ãô colim) ‚âÖ colimit (Prod.swap K J ‚ãô G) :=
      (colimitIsoColimitCurryCompColim _).symm
    _ ‚âÖ colimit G := (HasColimit.isoOfEquivalence (Prod.braiding K J) (Iso.refl _))
    _ ‚âÖ colimit (curry.obj G ‚ãô colim) := colimitIsoColimitCurryCompColim _

@[simp]
theorem colimitCurrySwapCompColimIsoColimitCurryCompColim_Œπ_Œπ_hom {j} {k} :
    colimit.Œπ _ j ‚â´ colimit.Œπ (curry.obj (Prod.swap K J ‚ãô G) ‚ãô colim) k ‚â´
      (colimitCurrySwapCompColimIsoColimitCurryCompColim G).hom =
        (colimit.Œπ _ k ‚â´ colimit.Œπ (curry.obj G ‚ãô colim) j : _ ‚ü∂ colimit (curry.obj G‚ãô colim)) := by
  dsimp [colimitCurrySwapCompColimIsoColimitCurryCompColim]
  slice_lhs 1 3 => simp only []
  simp

@[simp]
theorem colimitCurrySwapCompColimIsoColimitCurryCompColim_Œπ_Œπ_inv {j} {k} :
    colimit.Œπ _ k ‚â´ colimit.Œπ (curry.obj G ‚ãô colim) j ‚â´
      (colimitCurrySwapCompColimIsoColimitCurryCompColim G).inv =
        (colimit.Œπ _ j ‚â´
          colimit.Œπ (curry.obj _ ‚ãô colim) k :
            _ ‚ü∂ colimit (curry.obj (Prod.swap K J ‚ãô G) ‚ãô colim)) := by
  dsimp [colimitCurrySwapCompColimIsoColimitCurryCompColim]
  slice_lhs 1 3 => simp only []
  simp only [colimitIsoColimitCurryCompColim_Œπ_Œπ_inv, HasColimit.isoOfEquivalence_inv_œÄ,
    Functor.id_obj, Functor.comp_obj, Prod.braiding_inverse_obj, Prod.braiding_functor_obj,
    Prod.braiding_counitIso_inv_app, Prod.swap_obj, Iso.refl_hom, NatTrans.id_app, Category.id_comp,
    Category.assoc, colimitIsoColimitCurryCompColim_Œπ_hom, curry_obj_obj_obj]
  erw [CategoryTheory.Bifunctor.map_id]
  simp

end

end CategoryTheory.Limits
