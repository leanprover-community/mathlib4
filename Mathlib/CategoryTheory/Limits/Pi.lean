/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Pi.Basic
import Mathlib.CategoryTheory.Limits.HasLimits

#align_import category_theory.limits.pi from "leanprover-community/mathlib"@"744d59af0b28d0c42f631038627df9b85ae1d1ce"

/-!
# Limits in the category of indexed families of objects.

Given a functor `F : J ‚•§ Œ† i, C i` into a category of indexed families,
1. we can assemble a collection of cones over `F ‚ãô Pi.eval C i` into a cone over `F`
2. if all those cones are limit cones, the assembled cone is a limit cone, and
3. if we have limits for each of `F ‚ãô Pi.eval C i`, we can produce a
   `HasLimit F` instance
-/


open CategoryTheory

open CategoryTheory.Limits

namespace CategoryTheory.pi

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

variable {I : Type v‚ÇÅ} {C : I ‚Üí Type u‚ÇÅ} [‚àÄ i, Category.{v‚ÇÅ} (C i)]

variable {J : Type v‚ÇÅ} [SmallCategory J]

variable {F : J ‚•§ ‚àÄ i, C i}

/-- A cone over `F : J ‚•§ Œ† i, C i` has as its components cones over each of the `F ‚ãô Pi.eval C i`.
-/
def coneCompEval (c : Cone F) (i : I) : Cone (F ‚ãô Pi.eval C i) where
  pt := c.pt i
  œÄ :=
    { app := fun j => c.œÄ.app j i
      naturality := fun _ _ f => congr_fun (c.œÄ.naturality f) i }
#align category_theory.pi.cone_comp_eval CategoryTheory.pi.coneCompEval

/--
A cocone over `F : J ‚•§ Œ† i, C i` has as its components cocones over each of the `F ‚ãô Pi.eval C i`.
-/
def coconeCompEval (c : Cocone F) (i : I) : Cocone (F ‚ãô Pi.eval C i) where
  pt := c.pt i
  Œπ :=
    { app := fun j => c.Œπ.app j i
      naturality := fun _ _ f => congr_fun (c.Œπ.naturality f) i }
#align category_theory.pi.cocone_comp_eval CategoryTheory.pi.coconeCompEval

/--
Given a family of cones over the `F ‚ãô Pi.eval C i`, we can assemble these together as a `Cone F`.
-/
def coneOfConeCompEval (c : ‚àÄ i, Cone (F ‚ãô Pi.eval C i)) : Cone F where
  pt i := (c i).pt
  œÄ :=
    { app := fun j i => (c i).œÄ.app j
      naturality := fun j j' f => by
        funext i
        -- ‚ä¢ (((Functor.const J).obj fun i => (c i).pt).map f ‚â´ (fun j i => NatTrans.app  ‚Ä¶
        exact (c i).œÄ.naturality f }
        -- üéâ no goals
#align category_theory.pi.cone_of_cone_comp_eval CategoryTheory.pi.coneOfConeCompEval

/-- Given a family of cocones over the `F ‚ãô Pi.eval C i`,
we can assemble these together as a `Cocone F`.
-/
def coconeOfCoconeCompEval (c : ‚àÄ i, Cocone (F ‚ãô Pi.eval C i)) : Cocone F where
  pt i := (c i).pt
  Œπ :=
    { app := fun j i => (c i).Œπ.app j
      naturality := fun j j' f => by
        funext i
        -- ‚ä¢ (F.map f ‚â´ (fun j i => NatTrans.app (c i).Œπ j) j') i = ((fun j i => NatTrans ‚Ä¶
        exact (c i).Œπ.naturality f }
        -- üéâ no goals
#align category_theory.pi.cocone_of_cocone_comp_eval CategoryTheory.pi.coconeOfCoconeCompEval

/-- Given a family of limit cones over the `F ‚ãô Pi.eval C i`,
assembling them together as a `Cone F` produces a limit cone.
-/
def coneOfConeEvalIsLimit {c : ‚àÄ i, Cone (F ‚ãô Pi.eval C i)} (P : ‚àÄ i, IsLimit (c i)) :
    IsLimit (coneOfConeCompEval c) where
  lift s i := (P i).lift (coneCompEval s i)
  fac s j := by
    funext i
    -- ‚ä¢ ((fun s i => IsLimit.lift (P i) (coneCompEval s i)) s ‚â´ NatTrans.app (coneOf ‚Ä¶
    exact (P i).fac (coneCompEval s i) j
    -- üéâ no goals
  uniq s m w := by
    funext i
    -- ‚ä¢ m i = (fun s i => IsLimit.lift (P i) (coneCompEval s i)) s i
    exact (P i).uniq (coneCompEval s i) (m i) fun j => congr_fun (w j) i
    -- üéâ no goals
#align category_theory.pi.cone_of_cone_eval_is_limit CategoryTheory.pi.coneOfConeEvalIsLimit

/-- Given a family of colimit cocones over the `F ‚ãô Pi.eval C i`,
assembling them together as a `Cocone F` produces a colimit cocone.
-/
def coconeOfCoconeEvalIsColimit {c : ‚àÄ i, Cocone (F ‚ãô Pi.eval C i)} (P : ‚àÄ i, IsColimit (c i)) :
    IsColimit (coconeOfCoconeCompEval c) where
  desc s i := (P i).desc (coconeCompEval s i)
  fac s j := by
    funext i
    -- ‚ä¢ (NatTrans.app (coconeOfCoconeCompEval c).Œπ j ‚â´ (fun s i => IsColimit.desc (P ‚Ä¶
    exact (P i).fac (coconeCompEval s i) j
    -- üéâ no goals
  uniq s m w := by
    funext i
    -- ‚ä¢ m i = (fun s i => IsColimit.desc (P i) (coconeCompEval s i)) s i
    exact (P i).uniq (coconeCompEval s i) (m i) fun j => congr_fun (w j) i
    -- üéâ no goals
#align category_theory.pi.cocone_of_cocone_eval_is_colimit CategoryTheory.pi.coconeOfCoconeEvalIsColimit

section

variable [‚àÄ i, HasLimit (F ‚ãô Pi.eval C i)]

/-- If we have a functor `F : J ‚•§ Œ† i, C i` into a category of indexed families,
and we have limits for each of the `F ‚ãô Pi.eval C i`,
then `F` has a limit.
-/
theorem hasLimit_of_hasLimit_comp_eval : HasLimit F :=
  HasLimit.mk
    { cone := coneOfConeCompEval fun _ => limit.cone _
      isLimit := coneOfConeEvalIsLimit fun _ => limit.isLimit _ }
#align category_theory.pi.has_limit_of_has_limit_comp_eval CategoryTheory.pi.hasLimit_of_hasLimit_comp_eval

end

section

variable [‚àÄ i, HasColimit (F ‚ãô Pi.eval C i)]

/-- If we have a functor `F : J ‚•§ Œ† i, C i` into a category of indexed families,
and colimits exist for each of the `F ‚ãô Pi.eval C i`,
there is a colimit for `F`.
-/
theorem hasColimit_of_hasColimit_comp_eval : HasColimit F :=
  HasColimit.mk
    { cocone := coconeOfCoconeCompEval fun _ => colimit.cocone _
      isColimit := coconeOfCoconeEvalIsColimit fun _ => colimit.isColimit _ }
#align category_theory.pi.has_colimit_of_has_colimit_comp_eval CategoryTheory.pi.hasColimit_of_hasColimit_comp_eval

end

/-!
As an example, we can use this to construct particular shapes of limits
in a category of indexed families.

With the addition of
`import CategoryTheory.Limits.Shapes.Types`
we can use:
```
local attribute [instance] hasLimit_of_hasLimit_comp_eval
example : hasBinaryProducts (I ‚Üí Type v‚ÇÅ) := ‚ü®by infer_instance‚ü©
```
-/


end CategoryTheory.pi
