/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Limits.Preserves.Limits

/-!
# (Co)limits in functor categories.

We show that if `D` has limits, then the functor category `C â¥¤ D` also has limits
(`CategoryTheory.Limits.functorCategoryHasLimits`),
and the evaluation functors preserve limits
(`CategoryTheory.Limits.evaluation_preservesLimits`)
(and similarly for colimits).

We also show that `F : D â¥¤ K â¥¤ C` preserves (co)limits if it does so for each `k : K`
(`CategoryTheory.Limits.preservesLimits_of_evaluation` and
`CategoryTheory.Limits.preservesColimits_of_evaluation`).
-/


open CategoryTheory CategoryTheory.Category CategoryTheory.Functor

-- morphism levels before object levels. See note [CategoryTheory universes].
universe w' w vâ‚ vâ‚‚ uâ‚ uâ‚‚ v v' u u'

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D]
variable {J : Type uâ‚} [Category.{vâ‚} J] {K : Type uâ‚‚} [Category.{vâ‚‚} K]

@[reassoc (attr := simp)]
theorem limit.lift_Ï€_app (H : J â¥¤ K â¥¤ C) [HasLimit H] (c : Cone H) (j : J) (k : K) :
    (limit.lift H c).app k â‰« (limit.Ï€ H j).app k = (c.Ï€.app j).app k :=
  congr_app (limit.lift_Ï€ c j) k

@[reassoc (attr := simp)]
theorem colimit.Î¹_desc_app (H : J â¥¤ K â¥¤ C) [HasColimit H] (c : Cocone H) (j : J) (k : K) :
    (colimit.Î¹ H j).app k â‰« (colimit.desc H c).app k = (c.Î¹.app j).app k :=
  congr_app (colimit.Î¹_desc c j) k

/-- The evaluation functors jointly reflect limits: that is, to show a cone is a limit of `F`
it suffices to show that each evaluation cone is a limit. In other words, to prove a cone is
limiting you can show it's pointwise limiting.
-/
def evaluationJointlyReflectsLimits {F : J â¥¤ K â¥¤ C} (c : Cone F)
    (t : âˆ€ k : K, IsLimit (((evaluation K C).obj k).mapCone c)) : IsLimit c where
  lift s :=
    { app := fun k => (t k).lift âŸ¨s.pt.obj k, whiskerRight s.Ï€ ((evaluation K C).obj k)âŸ©
      naturality := fun X Y f =>
        (t Y).hom_ext fun j => by
          rw [assoc, (t Y).fac _ j]
          simpa using
            ((t X).fac_assoc âŸ¨s.pt.obj X, whiskerRight s.Ï€ ((evaluation K C).obj X)âŸ© j _).symm }
  fac s j := by ext k; exact (t k).fac _ j
  uniq s m w := by
    ext x
    exact (t x).hom_ext fun j =>
      (congr_app (w j) x).trans
        ((t x).fac âŸ¨s.pt.obj _, whiskerRight s.Ï€ ((evaluation K C).obj _)âŸ© j).symm

/-- Given a functor `F` and a collection of limit cones for each diagram `X â†¦ F X k`, we can stitch
them together to give a cone for the diagram `F`.
`combinedIsLimit` shows that the new cone is limiting, and `evalCombined` shows it is
(essentially) made up of the original cones.
-/
@[simps]
def combineCones (F : J â¥¤ K â¥¤ C) (c : âˆ€ k : K, LimitCone (F.flip.obj k)) : Cone F where
  pt :=
    { obj := fun k => (c k).cone.pt
      map := fun {kâ‚} {kâ‚‚} f => (c kâ‚‚).isLimit.lift âŸ¨_, (c kâ‚).cone.Ï€ â‰« F.flip.map fâŸ©
      map_id := fun k =>
        (c k).isLimit.hom_ext fun j => by simp
      map_comp := fun {kâ‚} {kâ‚‚} {kâ‚ƒ} fâ‚ fâ‚‚ => (c kâ‚ƒ).isLimit.hom_ext fun j => by simp }
  Ï€ :=
    { app := fun j => { app := fun k => (c k).cone.Ï€.app j }
      naturality := fun jâ‚ jâ‚‚ g => by ext k; exact (c k).cone.Ï€.naturality g }

/-- The stitched together cones each project down to the original given cones (up to iso). -/
def evaluateCombinedCones (F : J â¥¤ K â¥¤ C) (c : âˆ€ k : K, LimitCone (F.flip.obj k)) (k : K) :
    ((evaluation K C).obj k).mapCone (combineCones F c) â‰… (c k).cone :=
  Cones.ext (Iso.refl _)

/-- Stitching together limiting cones gives a limiting cone. -/
def combinedIsLimit (F : J â¥¤ K â¥¤ C) (c : âˆ€ k : K, LimitCone (F.flip.obj k)) :
    IsLimit (combineCones F c) :=
  evaluationJointlyReflectsLimits _ fun k =>
    (c k).isLimit.ofIsoLimit (evaluateCombinedCones F c k).symm

/-- The evaluation functors jointly reflect colimits: that is, to show a cocone is a colimit of `F`
it suffices to show that each evaluation cocone is a colimit. In other words, to prove a cocone is
colimiting you can show it's pointwise colimiting.
-/
def evaluationJointlyReflectsColimits {F : J â¥¤ K â¥¤ C} (c : Cocone F)
    (t : âˆ€ k : K, IsColimit (((evaluation K C).obj k).mapCocone c)) : IsColimit c where
  desc s :=
    { app := fun k => (t k).desc âŸ¨s.pt.obj k, whiskerRight s.Î¹ ((evaluation K C).obj k)âŸ©
      naturality := fun X Y f =>
        (t X).hom_ext fun j => by
          rw [(t X).fac_assoc _ j]
          erw [â† (c.Î¹.app j).naturality_assoc f]
          erw [(t Y).fac âŸ¨s.pt.obj _, whiskerRight s.Î¹ _âŸ© j]
          simp }
  fac s j := by ext k; exact (t k).fac _ j
  uniq s m w := by
    ext x
    exact (t x).hom_ext fun j =>
      (congr_app (w j) x).trans
        ((t x).fac âŸ¨s.pt.obj _, whiskerRight s.Î¹ ((evaluation K C).obj _)âŸ© j).symm

/--
Given a functor `F` and a collection of colimit cocones for each diagram `X â†¦ F X k`, we can stitch
them together to give a cocone for the diagram `F`.
`combinedIsColimit` shows that the new cocone is colimiting, and `evalCombined` shows it is
(essentially) made up of the original cocones.
-/
@[simps]
def combineCocones (F : J â¥¤ K â¥¤ C) (c : âˆ€ k : K, ColimitCocone (F.flip.obj k)) : Cocone F where
  pt :=
    { obj := fun k => (c k).cocone.pt
      map := fun {kâ‚} {kâ‚‚} f => (c kâ‚).isColimit.desc âŸ¨_, F.flip.map f â‰« (c kâ‚‚).cocone.Î¹âŸ©
      map_id := fun k =>
        (c k).isColimit.hom_ext fun j => by simp
      map_comp := fun {kâ‚} {kâ‚‚} {kâ‚ƒ} fâ‚ fâ‚‚ => (c kâ‚).isColimit.hom_ext fun j => by simp }
  Î¹ :=
    { app := fun j => { app := fun k => (c k).cocone.Î¹.app j }
      naturality := fun jâ‚ jâ‚‚ g => by ext k; exact (c k).cocone.Î¹.naturality g }

/-- The stitched together cocones each project down to the original given cocones (up to iso). -/
def evaluateCombinedCocones (F : J â¥¤ K â¥¤ C) (c : âˆ€ k : K, ColimitCocone (F.flip.obj k)) (k : K) :
    ((evaluation K C).obj k).mapCocone (combineCocones F c) â‰… (c k).cocone :=
  Cocones.ext (Iso.refl _)

/-- Stitching together colimiting cocones gives a colimiting cocone. -/
def combinedIsColimit (F : J â¥¤ K â¥¤ C) (c : âˆ€ k : K, ColimitCocone (F.flip.obj k)) :
    IsColimit (combineCocones F c) :=
  evaluationJointlyReflectsColimits _ fun k =>
    (c k).isColimit.ofIsoColimit (evaluateCombinedCocones F c k).symm

/--
An alternative colimit cocone in the functor category `K â¥¤ C` in the case where `C` has
`J`-shaped colimits, with cocone point `F.flip â‹™ colim`.
-/
@[simps]
noncomputable def pointwiseCocone [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C) : Cocone F where
  pt := F.flip â‹™ colim
  Î¹ := {
    app X := { app Y := (colimit.Î¹ _ X : (F.flip.obj Y).obj X âŸ¶ _) }
    naturality X Y f := by
      ext x
      simp only [Functor.const_obj_obj, Functor.comp_obj, colim_obj, NatTrans.comp_app,
        Functor.const_obj_map, Category.comp_id]
      change (F.flip.obj x).map f â‰« _ = _
      rw [colimit.w] }

/-- `pointwiseCocone` is indeed a colimit cocone. -/
noncomputable def pointwiseIsColimit [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C) :
    IsColimit (pointwiseCocone F) := by
  apply IsColimit.ofIsoColimit (combinedIsColimit _
    (fun k â†¦ âŸ¨colimit.cocone _, colimit.isColimit _âŸ©))
  exact Cocones.ext (Iso.refl _)

noncomputable section

instance functorCategoryHasLimit (F : J â¥¤ K â¥¤ C) [âˆ€ k, HasLimit (F.flip.obj k)] : HasLimit F :=
  HasLimit.mk
    { cone := combineCones F fun _ => getLimitCone _
      isLimit := combinedIsLimit _ _ }

instance functorCategoryHasLimitsOfShape [HasLimitsOfShape J C] : HasLimitsOfShape J (K â¥¤ C) where
  has_limit _ := inferInstance

instance functorCategoryHasColimit (F : J â¥¤ K â¥¤ C) [âˆ€ k, HasColimit (F.flip.obj k)] :
    HasColimit F :=
  HasColimit.mk
    { cocone := combineCocones F fun _ => getColimitCocone _
      isColimit := combinedIsColimit _ _ }

instance functorCategoryHasColimitsOfShape [HasColimitsOfShape J C] :
    HasColimitsOfShape J (K â¥¤ C) where
  has_colimit _ := inferInstance

instance functorCategoryHasLimitsOfSize [HasLimitsOfSize.{vâ‚, uâ‚} C] :
    HasLimitsOfSize.{vâ‚, uâ‚} (K â¥¤ C) where
  has_limits_of_shape := inferInstance

instance functorCategoryHasColimitsOfSize [HasColimitsOfSize.{vâ‚, uâ‚} C] :
    HasColimitsOfSize.{vâ‚, uâ‚} (K â¥¤ C) where
  has_colimits_of_shape := inferInstance

instance hasLimitCompEvaluation (F : J â¥¤ K â¥¤ C) (k : K) [HasLimit (F.flip.obj k)] :
    HasLimit (F â‹™ (evaluation _ _).obj k) :=
  hasLimit_of_iso (F := F.flip.obj k) (Iso.refl _)

instance evaluation_preservesLimit (F : J â¥¤ K â¥¤ C) [âˆ€ k, HasLimit (F.flip.obj k)] (k : K) :
    PreservesLimit F ((evaluation K C).obj k) :=
  -- Porting note: added a let because X was not inferred
  let X : (k : K) â†’ LimitCone (F.flip.obj k) := fun k => getLimitCone (F.flip.obj k)
  preservesLimit_of_preserves_limit_cone (combinedIsLimit _ X) <|
    IsLimit.ofIsoLimit (limit.isLimit _) (evaluateCombinedCones F X k).symm

instance evaluation_preservesLimitsOfShape [HasLimitsOfShape J C] (k : K) :
    PreservesLimitsOfShape J ((evaluation K C).obj k) where
  preservesLimit := inferInstance

/-- If `F : J â¥¤ K â¥¤ C` is a functor into a functor category which has a limit,
then the evaluation of that limit at `k` is the limit of the evaluations of `F.obj j` at `k`.
-/
def limitObjIsoLimitCompEvaluation [HasLimitsOfShape J C] (F : J â¥¤ K â¥¤ C) (k : K) :
    (limit F).obj k â‰… limit (F â‹™ (evaluation K C).obj k) :=
  preservesLimitIso ((evaluation K C).obj k) F

@[reassoc (attr := simp)]
theorem limitObjIsoLimitCompEvaluation_hom_Ï€ [HasLimitsOfShape J C] (F : J â¥¤ K â¥¤ C) (j : J)
    (k : K) :
    (limitObjIsoLimitCompEvaluation F k).hom â‰« limit.Ï€ (F â‹™ (evaluation K C).obj k) j =
      (limit.Ï€ F j).app k := by
  dsimp [limitObjIsoLimitCompEvaluation]
  simp

@[reassoc (attr := simp)]
theorem limitObjIsoLimitCompEvaluation_inv_Ï€_app [HasLimitsOfShape J C] (F : J â¥¤ K â¥¤ C) (j : J)
    (k : K) :
    (limitObjIsoLimitCompEvaluation F k).inv â‰« (limit.Ï€ F j).app k =
      limit.Ï€ (F â‹™ (evaluation K C).obj k) j := by
  dsimp [limitObjIsoLimitCompEvaluation]
  rw [Iso.inv_comp_eq]
  simp

@[reassoc (attr := simp)]
theorem limit_map_limitObjIsoLimitCompEvaluation_hom [HasLimitsOfShape J C] {i j : K}
    (F : J â¥¤ K â¥¤ C) (f : i âŸ¶ j) : (limit F).map f â‰« (limitObjIsoLimitCompEvaluation _ _).hom =
    (limitObjIsoLimitCompEvaluation _ _).hom â‰« limMap (whiskerLeft _ ((evaluation _ _).map f)) := by
  ext
  simp

@[reassoc (attr := simp)]
theorem limitObjIsoLimitCompEvaluation_inv_limit_map [HasLimitsOfShape J C] {i j : K}
    (F : J â¥¤ K â¥¤ C) (f : i âŸ¶ j) : (limitObjIsoLimitCompEvaluation _ _).inv â‰« (limit F).map f =
    limMap (whiskerLeft _ ((evaluation _ _).map f)) â‰« (limitObjIsoLimitCompEvaluation _ _).inv := by
  rw [Iso.inv_comp_eq, â† Category.assoc, Iso.eq_comp_inv,
    limit_map_limitObjIsoLimitCompEvaluation_hom]

@[ext]
theorem limit_obj_ext {H : J â¥¤ K â¥¤ C} [HasLimitsOfShape J C] {k : K} {W : C}
    {f g : W âŸ¶ (limit H).obj k}
    (w : âˆ€ j, f â‰« (Limits.limit.Ï€ H j).app k = g â‰« (Limits.limit.Ï€ H j).app k) : f = g := by
  apply (cancel_mono (limitObjIsoLimitCompEvaluation H k).hom).1
  ext j
  simpa using w j

/-- Taking a limit after whiskering by `G` is the same as using `G` and then taking a limit. -/
def limitCompWhiskeringLeftIsoCompLimit (F : J â¥¤ K â¥¤ C) (G : D â¥¤ K) [HasLimitsOfShape J C] :
    limit (F â‹™ (whiskeringLeft _ _ _).obj G) â‰… G â‹™ limit F :=
  NatIso.ofComponents (fun j =>
    limitObjIsoLimitCompEvaluation (F â‹™ (whiskeringLeft _ _ _).obj G) j â‰ªâ‰«
      HasLimit.isoOfNatIso (isoWhiskerLeft F (whiskeringLeftCompEvaluation G j)) â‰ªâ‰«
      (limitObjIsoLimitCompEvaluation F (G.obj j)).symm)

@[reassoc (attr := simp)]
theorem limitCompWhiskeringLeftIsoCompLimit_hom_whiskerLeft_Ï€ (F : J â¥¤ K â¥¤ C) (G : D â¥¤ K)
    [HasLimitsOfShape J C] (j : J) :
    (limitCompWhiskeringLeftIsoCompLimit F G).hom â‰« whiskerLeft G (limit.Ï€ F j) =
      limit.Ï€ (F â‹™ (whiskeringLeft _ _ _).obj G) j := by
  ext d
  simp [limitCompWhiskeringLeftIsoCompLimit]

@[reassoc (attr := simp)]
theorem limitCompWhiskeringLeftIsoCompLimit_inv_Ï€ (F : J â¥¤ K â¥¤ C) (G : D â¥¤ K)
    [HasLimitsOfShape J C] (j : J) :
    (limitCompWhiskeringLeftIsoCompLimit F G).inv â‰« limit.Ï€ (F â‹™ (whiskeringLeft _ _ _).obj G) j =
      whiskerLeft G (limit.Ï€ F j) := by
  simp [Iso.inv_comp_eq]

instance hasColimitCompEvaluation (F : J â¥¤ K â¥¤ C) (k : K) [HasColimit (F.flip.obj k)] :
    HasColimit (F â‹™ (evaluation _ _).obj k) :=
  hasColimit_of_iso (F := F.flip.obj k) (Iso.refl _)

instance evaluation_preservesColimit (F : J â¥¤ K â¥¤ C) [âˆ€ k, HasColimit (F.flip.obj k)] (k : K) :
    PreservesColimit F ((evaluation K C).obj k) :=
  -- Porting note: added a let because X was not inferred
  let X : (k : K) â†’ ColimitCocone (F.flip.obj k) := fun k => getColimitCocone (F.flip.obj k)
  preservesColimit_of_preserves_colimit_cocone (combinedIsColimit _ X) <|
    IsColimit.ofIsoColimit (colimit.isColimit _) (evaluateCombinedCocones F X k).symm

instance evaluation_preservesColimitsOfShape [HasColimitsOfShape J C] (k : K) :
    PreservesColimitsOfShape J ((evaluation K C).obj k) where
  preservesColimit := inferInstance

/-- If `F : J â¥¤ K â¥¤ C` is a functor into a functor category which has a colimit,
then the evaluation of that colimit at `k` is the colimit of the evaluations of `F.obj j` at `k`.
-/
def colimitObjIsoColimitCompEvaluation [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C) (k : K) :
    (colimit F).obj k â‰… colimit (F â‹™ (evaluation K C).obj k) :=
  preservesColimitIso ((evaluation K C).obj k) F

@[reassoc (attr := simp)]
theorem colimitObjIsoColimitCompEvaluation_Î¹_inv [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C) (j : J)
    (k : K) :
    colimit.Î¹ (F â‹™ (evaluation K C).obj k) j â‰« (colimitObjIsoColimitCompEvaluation F k).inv =
      (colimit.Î¹ F j).app k := by
  dsimp [colimitObjIsoColimitCompEvaluation]
  simp

@[reassoc (attr := simp)]
theorem colimitObjIsoColimitCompEvaluation_Î¹_app_hom [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C)
    (j : J) (k : K) :
    (colimit.Î¹ F j).app k â‰« (colimitObjIsoColimitCompEvaluation F k).hom =
      colimit.Î¹ (F â‹™ (evaluation K C).obj k) j := by
  dsimp [colimitObjIsoColimitCompEvaluation]
  rw [â† Iso.eq_comp_inv]
  simp

@[reassoc (attr := simp)]
theorem colimitObjIsoColimitCompEvaluation_inv_colimit_map [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C)
    {i j : K} (f : i âŸ¶ j) :
    (colimitObjIsoColimitCompEvaluation _ _).inv â‰« (colimit F).map f =
      colimMap (whiskerLeft _ ((evaluation _ _).map f)) â‰«
        (colimitObjIsoColimitCompEvaluation _ _).inv := by
  ext
  simp

@[reassoc (attr := simp)]
theorem colimit_map_colimitObjIsoColimitCompEvaluation_hom [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C)
    {i j : K} (f : i âŸ¶ j) :
    (colimit F).map f â‰« (colimitObjIsoColimitCompEvaluation _ _).hom =
      (colimitObjIsoColimitCompEvaluation _ _).hom â‰«
        colimMap (whiskerLeft _ ((evaluation _ _).map f)) := by
  rw [â† Iso.inv_comp_eq, â† Category.assoc, â† Iso.eq_comp_inv,
    colimitObjIsoColimitCompEvaluation_inv_colimit_map]

@[ext]
theorem colimit_obj_ext {H : J â¥¤ K â¥¤ C} [HasColimitsOfShape J C] {k : K} {W : C}
    {f g : (colimit H).obj k âŸ¶ W} (w : âˆ€ j, (colimit.Î¹ H j).app k â‰« f = (colimit.Î¹ H j).app k â‰« g) :
    f = g := by
  apply (cancel_epi (colimitObjIsoColimitCompEvaluation H k).inv).1
  ext j
  simpa using w j

/-- Taking a colimit after whiskering by `G` is the same as using `G` and then taking a colimit. -/
def colimitCompWhiskeringLeftIsoCompColimit (F : J â¥¤ K â¥¤ C) (G : D â¥¤ K) [HasColimitsOfShape J C] :
    colimit (F â‹™ (whiskeringLeft _ _ _).obj G) â‰… G â‹™ colimit F :=
  NatIso.ofComponents (fun j =>
    colimitObjIsoColimitCompEvaluation (F â‹™ (whiskeringLeft _ _ _).obj G) j â‰ªâ‰«
      HasColimit.isoOfNatIso (isoWhiskerLeft F (whiskeringLeftCompEvaluation G j)) â‰ªâ‰«
      (colimitObjIsoColimitCompEvaluation F (G.obj j)).symm)

@[reassoc (attr := simp)]
theorem Î¹_colimitCompWhiskeringLeftIsoCompColimit_hom (F : J â¥¤ K â¥¤ C) (G : D â¥¤ K)
    [HasColimitsOfShape J C] (j : J) :
    colimit.Î¹ (F â‹™ (whiskeringLeft _ _ _).obj G) j â‰«
      (colimitCompWhiskeringLeftIsoCompColimit F G).hom = whiskerLeft G (colimit.Î¹ F j) := by
  ext d
  simp [colimitCompWhiskeringLeftIsoCompColimit]

@[reassoc (attr := simp)]
theorem whiskerLeft_Î¹_colimitCompWhiskeringLeftIsoCompColimit_inv (F : J â¥¤ K â¥¤ C) (G : D â¥¤ K)
    [HasColimitsOfShape J C] (j : J) :
    whiskerLeft G (colimit.Î¹ F j) â‰« (colimitCompWhiskeringLeftIsoCompColimit F G).inv =
      colimit.Î¹ (F â‹™ (whiskeringLeft _ _ _).obj G) j := by
  simp [Iso.comp_inv_eq]

instance evaluationPreservesLimits [HasLimits C] (k : K) :
    PreservesLimits ((evaluation K C).obj k) where
  preservesLimitsOfShape {_} _ğ’¥ := inferInstance

/-- `F : D â¥¤ K â¥¤ C` preserves the limit of some `G : J â¥¤ D` if it does for each `k : K`. -/
lemma preservesLimit_of_evaluation (F : D â¥¤ K â¥¤ C) (G : J â¥¤ D)
    (H : âˆ€ k : K, PreservesLimit G (F â‹™ (evaluation K C).obj k : D â¥¤ C)) : PreservesLimit G F :=
  âŸ¨fun {c} hc => âŸ¨by
    apply evaluationJointlyReflectsLimits
    intro X
    haveI := H X
    change IsLimit ((F â‹™ (evaluation K C).obj X).mapCone c)
    exact isLimitOfPreserves _ hcâŸ©âŸ©

/-- `F : D â¥¤ K â¥¤ C` preserves limits of shape `J` if it does for each `k : K`. -/
lemma preservesLimitsOfShape_of_evaluation (F : D â¥¤ K â¥¤ C) (J : Type*) [Category J]
    (_ : âˆ€ k : K, PreservesLimitsOfShape J (F â‹™ (evaluation K C).obj k)) :
    PreservesLimitsOfShape J F :=
  âŸ¨fun {G} => preservesLimit_of_evaluation F G fun _ => PreservesLimitsOfShape.preservesLimitâŸ©

/-- `F : D â¥¤ K â¥¤ C` preserves all limits if it does for each `k : K`. -/
lemma preservesLimits_of_evaluation (F : D â¥¤ K â¥¤ C)
    (_ : âˆ€ k : K, PreservesLimitsOfSize.{w', w} (F â‹™ (evaluation K C).obj k)) :
    PreservesLimitsOfSize.{w', w} F :=
  âŸ¨fun {L} _ =>
    preservesLimitsOfShape_of_evaluation F L fun _ => PreservesLimitsOfSize.preservesLimitsOfShapeâŸ©

/-- The constant functor `C â¥¤ (D â¥¤ C)` preserves limits. -/
instance preservesLimits_const : PreservesLimitsOfSize.{w', w} (const D : C â¥¤ _) :=
  preservesLimits_of_evaluation _ fun _ =>
    preservesLimits_of_natIso <| Iso.symm <| constCompEvaluationObj _ _

instance evaluation_preservesColimits [HasColimits C] (k : K) :
    PreservesColimits ((evaluation K C).obj k) where
  preservesColimitsOfShape := inferInstance

/-- `F : D â¥¤ K â¥¤ C` preserves the colimit of some `G : J â¥¤ D` if it does for each `k : K`. -/
lemma preservesColimit_of_evaluation (F : D â¥¤ K â¥¤ C) (G : J â¥¤ D)
    (H : âˆ€ k, PreservesColimit G (F â‹™ (evaluation K C).obj k)) : PreservesColimit G F :=
  âŸ¨fun {c} hc => âŸ¨by
    apply evaluationJointlyReflectsColimits
    intro X
    haveI := H X
    change IsColimit ((F â‹™ (evaluation K C).obj X).mapCocone c)
    exact isColimitOfPreserves _ hcâŸ©âŸ©

/-- `F : D â¥¤ K â¥¤ C` preserves all colimits of shape `J` if it does for each `k : K`. -/
lemma preservesColimitsOfShape_of_evaluation (F : D â¥¤ K â¥¤ C) (J : Type*) [Category J]
    (_ : âˆ€ k : K, PreservesColimitsOfShape J (F â‹™ (evaluation K C).obj k)) :
    PreservesColimitsOfShape J F :=
  âŸ¨fun {G} => preservesColimit_of_evaluation F G fun _ => PreservesColimitsOfShape.preservesColimitâŸ©

/-- `F : D â¥¤ K â¥¤ C` preserves all colimits if it does for each `k : K`. -/
lemma preservesColimits_of_evaluation (F : D â¥¤ K â¥¤ C)
    (_ : âˆ€ k : K, PreservesColimitsOfSize.{w', w} (F â‹™ (evaluation K C).obj k)) :
    PreservesColimitsOfSize.{w', w} F :=
  âŸ¨fun {L} _ =>
    preservesColimitsOfShape_of_evaluation F L fun _ =>
      PreservesColimitsOfSize.preservesColimitsOfShapeâŸ©

/-- The constant functor `C â¥¤ (D â¥¤ C)` preserves colimits. -/
instance preservesColimits_const : PreservesColimitsOfSize.{w', w} (const D : C â¥¤ _) :=
  preservesColimits_of_evaluation _ fun _ =>
    preservesColimits_of_natIso <| Iso.symm <| constCompEvaluationObj _ _

open CategoryTheory.prod

/-- The limit of a diagram `F : J â¥¤ K â¥¤ C` is isomorphic to the functor given by
the individual limits on objects. -/
@[simps!]
def limitIsoFlipCompLim [HasLimitsOfShape J C] (F : J â¥¤ K â¥¤ C) : limit F â‰… F.flip â‹™ lim :=
  NatIso.ofComponents (limitObjIsoLimitCompEvaluation F)

/-- `limitIsoFlipCompLim` is natural with respect to diagrams. -/
@[simps!]
def limIsoFlipCompWhiskerLim [HasLimitsOfShape J C] :
    lim â‰… flipFunctor J K C â‹™ (whiskeringRight _ _ _).obj lim :=
  (NatIso.ofComponents (limitIsoFlipCompLim Â· |>.symm) fun {F G} Î· â†¦ by
    ext k
    apply limit_obj_ext
    intro j
    simp [comp_evaluation, â† NatTrans.comp_app (limMap Î·)]).symm

/-- A variant of `limitIsoFlipCompLim` where the arguments of `F` are flipped. -/
@[simps!]
def limitFlipIsoCompLim [HasLimitsOfShape J C] (F : K â¥¤ J â¥¤ C) : limit F.flip â‰… F â‹™ lim :=
  let f := fun k =>
    limitObjIsoLimitCompEvaluation F.flip k â‰ªâ‰« HasLimit.isoOfNatIso (flipCompEvaluation _ _)
  NatIso.ofComponents f

/-- `limitFlipIsoCompLim` is natural with respect to diagrams. -/
@[simps!]
def limCompFlipIsoWhiskerLim [HasLimitsOfShape J C] :
    flipFunctor K J C â‹™ lim â‰… (whiskeringRight _ _ _).obj lim :=
  (NatIso.ofComponents (limitFlipIsoCompLim Â· |>.symm) fun {F G} Î· â†¦ by
    ext k
    apply limit_obj_ext
    intro j
    simp [comp_evaluation, â† NatTrans.comp_app (limMap _)]).symm

/-- For a functor `G : J â¥¤ K â¥¤ C`, its limit `K â¥¤ C` is given by `(G' : K â¥¤ J â¥¤ C) â‹™ lim`.
Note that this does not require `K` to be small.
-/
@[simps!]
def limitIsoSwapCompLim [HasLimitsOfShape J C] (G : J â¥¤ K â¥¤ C) :
    limit G â‰… curry.obj (Prod.swap K J â‹™ uncurry.obj G) â‹™ lim :=
  limitIsoFlipCompLim G â‰ªâ‰« isoWhiskerRight (flipIsoCurrySwapUncurry _) _

/-- The colimit of a diagram `F : J â¥¤ K â¥¤ C` is isomorphic to the functor given by
the individual colimits on objects. -/
@[simps!]
def colimitIsoFlipCompColim [HasColimitsOfShape J C] (F : J â¥¤ K â¥¤ C) : colimit F â‰… F.flip â‹™ colim :=
  NatIso.ofComponents (colimitObjIsoColimitCompEvaluation F)

/-- `colimitIsoFlipCompColim` is natural with respect to diagrams. -/
@[simps!]
def colimIsoFlipCompWhiskerColim [HasColimitsOfShape J C] :
    colim â‰… flipFunctor J K C â‹™ (whiskeringRight _ _ _).obj colim :=
  NatIso.ofComponents colimitIsoFlipCompColim fun {F G} Î· â†¦ by
    ext k
    apply colimit_obj_ext
    intro j
    simp [comp_evaluation, â† NatTrans.comp_app_assoc _ (colimMap Î·)]

/-- A variant of `colimitIsoFlipCompColim` where the arguments of `F` are flipped. -/
@[simps!]
def colimitFlipIsoCompColim [HasColimitsOfShape J C] (F : K â¥¤ J â¥¤ C) : colimit F.flip â‰… F â‹™ colim :=
  let f := fun _ =>
      colimitObjIsoColimitCompEvaluation _ _ â‰ªâ‰« HasColimit.isoOfNatIso (flipCompEvaluation _ _)
  NatIso.ofComponents f

/-- `colimitFlipIsoCompColim` is natural with respect to diagrams. -/
@[simps!]
def colimCompFlipIsoWhiskerColim [HasColimitsOfShape J C] :
    flipFunctor K J C â‹™ colim â‰… (whiskeringRight _ _ _).obj colim :=
  NatIso.ofComponents colimitFlipIsoCompColim fun {F G} Î· â†¦ by
    ext k
    apply colimit_obj_ext
    intro j
    simp [comp_evaluation, â† NatTrans.comp_app_assoc _ (colimMap _)]

/-- For a functor `G : J â¥¤ K â¥¤ C`, its colimit `K â¥¤ C` is given by `(G' : K â¥¤ J â¥¤ C) â‹™ colim`.
Note that this does not require `K` to be small.
-/
@[simps!]
def colimitIsoSwapCompColim [HasColimitsOfShape J C] (G : J â¥¤ K â¥¤ C) :
    colimit G â‰… curry.obj (Prod.swap K J â‹™ uncurry.obj G) â‹™ colim :=
  colimitIsoFlipCompColim G â‰ªâ‰« isoWhiskerRight (flipIsoCurrySwapUncurry _) _

end

end Limits

end CategoryTheory
