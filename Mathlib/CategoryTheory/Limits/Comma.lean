/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Arrow
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono
import Mathlib.CategoryTheory.Limits.Creates
import Mathlib.CategoryTheory.Limits.Unit
import Mathlib.CategoryTheory.StructuredArrow

#align_import category_theory.limits.comma from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Limits and colimits in comma categories

We build limits in the comma category `Comma L R` provided that the two source categories have
limits and `R` preserves them.
This is used to construct limits in the arrow category, structured arrow category and under
category, and show that the appropriate forgetful functors create limits.

The duals of all the above are also given.
-/


namespace CategoryTheory

open Category Limits

universe w' w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {J : Type w} [Category.{w'} J]

variable {A : Type u‚ÇÅ} [Category.{v‚ÇÅ} A]

variable {B : Type u‚ÇÇ} [Category.{v‚ÇÇ} B]

variable {T : Type u‚ÇÉ} [Category.{v‚ÇÉ} T]

namespace Comma

variable {L : A ‚•§ T} {R : B ‚•§ T}

variable (F : J ‚•§ Comma L R)

/-- (Implementation). An auxiliary cone which is useful in order to construct limits
in the comma category. -/
@[simps!]
def limitAuxiliaryCone (c‚ÇÅ : Cone (F ‚ãô fst L R)) : Cone ((F ‚ãô snd L R) ‚ãô R) :=
  (Cones.postcompose (whiskerLeft F (Comma.natTrans L R) : _)).obj (L.mapCone c‚ÇÅ)
#align category_theory.comma.limit_auxiliary_cone CategoryTheory.Comma.limitAuxiliaryCone

/-- If `R` preserves the appropriate limit, then given a cone for `F ‚ãô fst L R : J ‚•§ L` and a
limit cone for `F ‚ãô snd L R : J ‚•§ R` we can build a cone for `F` which will turn out to be a limit
cone.
-/
@[simps]
def coneOfPreserves [PreservesLimit (F ‚ãô snd L R) R] (c‚ÇÅ : Cone (F ‚ãô fst L R))
    {c‚ÇÇ : Cone (F ‚ãô snd L R)} (t‚ÇÇ : IsLimit c‚ÇÇ) : Cone F
    where
  pt :=
    { left := c‚ÇÅ.pt
      right := c‚ÇÇ.pt
      hom := (isLimitOfPreserves R t‚ÇÇ).lift (limitAuxiliaryCone _ c‚ÇÅ) }
  œÄ :=
    { app := fun j =>
        { left := c‚ÇÅ.œÄ.app j
          right := c‚ÇÇ.œÄ.app j
          w := ((isLimitOfPreserves R t‚ÇÇ).fac (limitAuxiliaryCone F c‚ÇÅ) j).symm }
      naturality := fun j‚ÇÅ j‚ÇÇ t => by
        ext
        -- ‚ä¢ (((Functor.const J).obj { left := c‚ÇÅ.pt, right := c‚ÇÇ.pt, hom := IsLimit.lift ‚Ä¶
        ¬∑ simp [‚Üê c‚ÇÅ.w t]
          -- üéâ no goals
        ¬∑ simp [‚Üê c‚ÇÇ.w t] }
          -- üéâ no goals
#align category_theory.comma.cone_of_preserves CategoryTheory.Comma.coneOfPreserves

/-- Provided that `R` preserves the appropriate limit, then the cone in `coneOfPreserves` is a
limit. -/
def coneOfPreservesIsLimit [PreservesLimit (F ‚ãô snd L R) R] {c‚ÇÅ : Cone (F ‚ãô fst L R)}
    (t‚ÇÅ : IsLimit c‚ÇÅ) {c‚ÇÇ : Cone (F ‚ãô snd L R)} (t‚ÇÇ : IsLimit c‚ÇÇ) :
    IsLimit (coneOfPreserves F c‚ÇÅ t‚ÇÇ) where
  lift s :=
    { left := t‚ÇÅ.lift ((fst L R).mapCone s)
      right := t‚ÇÇ.lift ((snd L R).mapCone s)
      w :=
        (isLimitOfPreserves R t‚ÇÇ).hom_ext fun j => by
          rw [coneOfPreserves_pt_hom, assoc, assoc, (isLimitOfPreserves R t‚ÇÇ).fac,
            limitAuxiliaryCone_œÄ_app, ‚Üê L.map_comp_assoc, t‚ÇÅ.fac, R.mapCone_œÄ_app,
            ‚Üê R.map_comp, t‚ÇÇ.fac]
          exact (s.œÄ.app j).w }
          -- üéâ no goals
  uniq s m w := by
    apply CommaMorphism.ext
    -- ‚ä¢ m.left = ((fun s => CommaMorphism.mk (IsLimit.lift t‚ÇÅ ((fst L R).mapCone s)) ‚Ä¶
    ¬∑ exact t‚ÇÅ.uniq ((fst L R).mapCone s) _ (fun j => by simp [‚Üê w])
      -- üéâ no goals
    ¬∑ exact t‚ÇÇ.uniq ((snd L R).mapCone s) _ (fun j => by simp [‚Üê w])
      -- üéâ no goals
#align category_theory.comma.cone_of_preserves_is_limit CategoryTheory.Comma.coneOfPreservesIsLimit

/-- (Implementation). An auxiliary cocone which is useful in order to construct colimits
in the comma category. -/
@[simps!]
def colimitAuxiliaryCocone (c‚ÇÇ : Cocone (F ‚ãô snd L R)) : Cocone ((F ‚ãô fst L R) ‚ãô L) :=
  (Cocones.precompose (whiskerLeft F (Comma.natTrans L R) : _)).obj (R.mapCocone c‚ÇÇ)
#align category_theory.comma.colimit_auxiliary_cocone CategoryTheory.Comma.colimitAuxiliaryCocone

/--
If `L` preserves the appropriate colimit, then given a colimit cocone for `F ‚ãô fst L R : J ‚•§ L` and
a cocone for `F ‚ãô snd L R : J ‚•§ R` we can build a cocone for `F` which will turn out to be a
colimit cocone.
-/
@[simps]
def coconeOfPreserves [PreservesColimit (F ‚ãô fst L R) L] {c‚ÇÅ : Cocone (F ‚ãô fst L R)}
    (t‚ÇÅ : IsColimit c‚ÇÅ) (c‚ÇÇ : Cocone (F ‚ãô snd L R)) : Cocone F
    where
  pt :=
    { left := c‚ÇÅ.pt
      right := c‚ÇÇ.pt
      hom := (isColimitOfPreserves L t‚ÇÅ).desc (colimitAuxiliaryCocone _ c‚ÇÇ) }
  Œπ :=
    { app := fun j =>
        { left := c‚ÇÅ.Œπ.app j
          right := c‚ÇÇ.Œπ.app j
          w := (isColimitOfPreserves L t‚ÇÅ).fac (colimitAuxiliaryCocone _ c‚ÇÇ) j }
      naturality := fun j‚ÇÅ j‚ÇÇ t => by
        ext
        -- ‚ä¢ (F.map t ‚â´ (fun j => CommaMorphism.mk (NatTrans.app c‚ÇÅ.Œπ j) (NatTrans.app c‚ÇÇ ‚Ä¶
        ¬∑ simp [‚Üê c‚ÇÅ.w t]
          -- üéâ no goals
        ¬∑ simp [‚Üê c‚ÇÇ.w t] }
          -- üéâ no goals
#align category_theory.comma.cocone_of_preserves CategoryTheory.Comma.coconeOfPreserves

/-- Provided that `L` preserves the appropriate colimit, then the cocone in `coconeOfPreserves` is
a colimit. -/
def coconeOfPreservesIsColimit [PreservesColimit (F ‚ãô fst L R) L] {c‚ÇÅ : Cocone (F ‚ãô fst L R)}
    (t‚ÇÅ : IsColimit c‚ÇÅ) {c‚ÇÇ : Cocone (F ‚ãô snd L R)} (t‚ÇÇ : IsColimit c‚ÇÇ) :
    IsColimit (coconeOfPreserves F t‚ÇÅ c‚ÇÇ) where
  desc s :=
    { left := t‚ÇÅ.desc ((fst L R).mapCocone s)
      right := t‚ÇÇ.desc ((snd L R).mapCocone s)
      w :=
        (isColimitOfPreserves L t‚ÇÅ).hom_ext fun j => by
          rw [coconeOfPreserves_pt_hom, (isColimitOfPreserves L t‚ÇÅ).fac_assoc,
            colimitAuxiliaryCocone_Œπ_app, assoc, ‚Üê R.map_comp, t‚ÇÇ.fac, L.mapCocone_Œπ_app, ‚Üê
            L.map_comp_assoc, t‚ÇÅ.fac]
          exact (s.Œπ.app j).w }
          -- üéâ no goals
  uniq s m w := by
    apply CommaMorphism.ext
    -- ‚ä¢ m.left = ((fun s => CommaMorphism.mk (IsColimit.desc t‚ÇÅ ((fst L R).mapCocone ‚Ä¶
    ¬∑ exact t‚ÇÅ.uniq ((fst L R).mapCocone s) _ (fun j => by simp [‚Üê w])
      -- üéâ no goals
    ¬∑ exact t‚ÇÇ.uniq ((snd L R).mapCocone s) _ (fun j => by simp [‚Üê w])
      -- üéâ no goals
#align category_theory.comma.cocone_of_preserves_is_colimit CategoryTheory.Comma.coconeOfPreservesIsColimit

instance hasLimit (F : J ‚•§ Comma L R) [HasLimit (F ‚ãô fst L R)] [HasLimit (F ‚ãô snd L R)]
    [PreservesLimit (F ‚ãô snd L R) R] : HasLimit F :=
  HasLimit.mk ‚ü®_, coneOfPreservesIsLimit _ (limit.isLimit _) (limit.isLimit _)‚ü©
#align category_theory.comma.has_limit CategoryTheory.Comma.hasLimit

instance hasLimitsOfShape [HasLimitsOfShape J A] [HasLimitsOfShape J B]
    [PreservesLimitsOfShape J R] : HasLimitsOfShape J (Comma L R) where
#align category_theory.comma.has_limits_of_shape CategoryTheory.Comma.hasLimitsOfShape

instance hasLimitsOfSize [HasLimitsOfSize.{w, w'} A] [HasLimitsOfSize.{w, w'} B]
    [PreservesLimitsOfSize.{w, w'} R] : HasLimitsOfSize.{w, w'} (Comma L R) :=
  ‚ü®fun _ _ => inferInstance‚ü©
#align category_theory.comma.has_limits CategoryTheory.Comma.hasLimitsOfSize

instance hasColimit (F : J ‚•§ Comma L R) [HasColimit (F ‚ãô fst L R)] [HasColimit (F ‚ãô snd L R)]
    [PreservesColimit (F ‚ãô fst L R) L] : HasColimit F :=
  HasColimit.mk ‚ü®_, coconeOfPreservesIsColimit _ (colimit.isColimit _) (colimit.isColimit _)‚ü©
#align category_theory.comma.has_colimit CategoryTheory.Comma.hasColimit

instance hasColimitsOfShape [HasColimitsOfShape J A] [HasColimitsOfShape J B]
    [PreservesColimitsOfShape J L] : HasColimitsOfShape J (Comma L R) where
#align category_theory.comma.has_colimits_of_shape CategoryTheory.Comma.hasColimitsOfShape

instance hasColimitsOfSize [HasColimitsOfSize.{w, w'} A] [HasColimitsOfSize.{w, w'} B]
    [PreservesColimitsOfSize.{w, w'} L] : HasColimitsOfSize.{w, w'} (Comma L R) :=
  ‚ü®fun _ _ => inferInstance‚ü©
#align category_theory.comma.has_colimits CategoryTheory.Comma.hasColimitsOfSize

end Comma

namespace Arrow

instance hasLimit (F : J ‚•§ Arrow T) [i‚ÇÅ : HasLimit (F ‚ãô leftFunc)] [i‚ÇÇ : HasLimit (F ‚ãô rightFunc)] :
    HasLimit F := by
  haveI : HasLimit (F ‚ãô Comma.fst _ _) := i‚ÇÅ
  -- ‚ä¢ HasLimit F
  haveI : HasLimit (F ‚ãô Comma.snd _ _) := i‚ÇÇ
  -- ‚ä¢ HasLimit F
  apply Comma.hasLimit
  -- üéâ no goals
#align category_theory.arrow.has_limit CategoryTheory.Arrow.hasLimit

instance hasLimitsOfShape [HasLimitsOfShape J T] : HasLimitsOfShape J (Arrow T) where
#align category_theory.arrow.has_limits_of_shape CategoryTheory.Arrow.hasLimitsOfShape

instance hasLimits [HasLimits T] : HasLimits (Arrow T) :=
  ‚ü®fun _ _ => inferInstance‚ü©
#align category_theory.arrow.has_limits CategoryTheory.Arrow.hasLimits

instance hasColimit (F : J ‚•§ Arrow T) [i‚ÇÅ : HasColimit (F ‚ãô leftFunc)]
    [i‚ÇÇ : HasColimit (F ‚ãô rightFunc)] : HasColimit F := by
  haveI : HasColimit (F ‚ãô Comma.fst _ _) := i‚ÇÅ
  -- ‚ä¢ HasColimit F
  haveI : HasColimit (F ‚ãô Comma.snd _ _) := i‚ÇÇ
  -- ‚ä¢ HasColimit F
  apply Comma.hasColimit
  -- üéâ no goals
#align category_theory.arrow.has_colimit CategoryTheory.Arrow.hasColimit

instance hasColimitsOfShape [HasColimitsOfShape J T] : HasColimitsOfShape J (Arrow T) where
#align category_theory.arrow.has_colimits_of_shape CategoryTheory.Arrow.hasColimitsOfShape

instance hasColimits [HasColimits T] : HasColimits (Arrow T) :=
  ‚ü®fun _ _ => inferInstance‚ü©
#align category_theory.arrow.has_colimits CategoryTheory.Arrow.hasColimits

end Arrow

namespace StructuredArrow

variable {X : T} {G : A ‚•§ T} (F : J ‚•§ StructuredArrow X G)

instance hasLimit [i‚ÇÅ : HasLimit (F ‚ãô proj X G)] [i‚ÇÇ : PreservesLimit (F ‚ãô proj X G) G] :
    HasLimit F := by
  haveI : HasLimit (F ‚ãô Comma.snd (Functor.fromPUnit X) G) := i‚ÇÅ
  -- ‚ä¢ HasLimit F
  haveI : PreservesLimit (F ‚ãô Comma.snd (Functor.fromPUnit X) G) _ := i‚ÇÇ
  -- ‚ä¢ HasLimit F
  apply Comma.hasLimit
  -- üéâ no goals
#align category_theory.structured_arrow.has_limit CategoryTheory.StructuredArrow.hasLimit

instance hasLimitsOfShape [HasLimitsOfShape J A] [PreservesLimitsOfShape J G] :
    HasLimitsOfShape J (StructuredArrow X G) where
#align category_theory.structured_arrow.has_limits_of_shape CategoryTheory.StructuredArrow.hasLimitsOfShape

instance hasLimitsOfSize [HasLimitsOfSize.{w, w'} A] [PreservesLimitsOfSize.{w, w'} G] :
    HasLimitsOfSize.{w, w'} (StructuredArrow X G) :=
  ‚ü®fun J hJ => by infer_instance‚ü©
                  -- üéâ no goals
#align category_theory.structured_arrow.has_limits CategoryTheory.StructuredArrow.hasLimitsOfSize

noncomputable instance createsLimit [i : PreservesLimit (F ‚ãô proj X G) G] :
    CreatesLimit F (proj X G) :=
  letI : PreservesLimit (F ‚ãô Comma.snd (Functor.fromPUnit X) G) G := i
  createsLimitOfReflectsIso fun _ t =>
    { liftedCone := Comma.coneOfPreserves F punitCone t
      makesLimit := Comma.coneOfPreservesIsLimit _ punitConeIsLimit _
      validLift := Cones.ext (Iso.refl _) fun _ => (id_comp _).symm }
#align category_theory.structured_arrow.creates_limit CategoryTheory.StructuredArrow.createsLimit

noncomputable instance createsLimitsOfShape [PreservesLimitsOfShape J G] :
    CreatesLimitsOfShape J (proj X G) where
#align category_theory.structured_arrow.creates_limits_of_shape CategoryTheory.StructuredArrow.createsLimitsOfShape

noncomputable instance createsLimitsOfSize [PreservesLimitsOfSize.{w, w'} G] :
    CreatesLimitsOfSize.{w, w'} (proj X G : _) where
#align category_theory.structured_arrow.creates_limits CategoryTheory.StructuredArrow.createsLimitsOfSize

instance mono_right_of_mono [HasPullbacks A] [PreservesLimitsOfShape WalkingCospan G]
    {Y Z : StructuredArrow X G} (f : Y ‚ü∂ Z) [Mono f] : Mono f.right :=
  show Mono ((proj X G).map f) from inferInstance
#align category_theory.structured_arrow.mono_right_of_mono CategoryTheory.StructuredArrow.mono_right_of_mono

theorem mono_iff_mono_right [HasPullbacks A] [PreservesLimitsOfShape WalkingCospan G]
    {Y Z : StructuredArrow X G} (f : Y ‚ü∂ Z) : Mono f ‚Üî Mono f.right :=
  ‚ü®fun _ => inferInstance, fun _ => mono_of_mono_right f‚ü©
#align category_theory.structured_arrow.mono_iff_mono_right CategoryTheory.StructuredArrow.mono_iff_mono_right

end StructuredArrow

namespace CostructuredArrow

variable {G : A ‚•§ T} {X : T} (F : J ‚•§ CostructuredArrow G X)

instance hasColimit [i‚ÇÅ : HasColimit (F ‚ãô proj G X)] [i‚ÇÇ : PreservesColimit (F ‚ãô proj G X) G] :
    HasColimit F := by
  haveI : HasColimit (F ‚ãô Comma.fst G (Functor.fromPUnit X)) := i‚ÇÅ
  -- ‚ä¢ HasColimit F
  haveI : PreservesColimit (F ‚ãô Comma.fst G (Functor.fromPUnit X)) _ := i‚ÇÇ
  -- ‚ä¢ HasColimit F
  apply Comma.hasColimit
  -- üéâ no goals
#align category_theory.costructured_arrow.has_colimit CategoryTheory.CostructuredArrow.hasColimit

instance hasColimitsOfShape [HasColimitsOfShape J A] [PreservesColimitsOfShape J G] :
    HasColimitsOfShape J (CostructuredArrow G X) where
#align category_theory.costructured_arrow.has_colimits_of_shape CategoryTheory.CostructuredArrow.hasColimitsOfShape

instance hasColimitsOfSize [HasColimitsOfSize.{w, w'} A] [PreservesColimitsOfSize.{w, w'} G] :
    HasColimitsOfSize.{w, w'} (CostructuredArrow G X) :=
  ‚ü®fun _ _ => inferInstance‚ü©
#align category_theory.costructured_arrow.has_colimits CategoryTheory.CostructuredArrow.hasColimitsOfSize

noncomputable instance createsColimit [i : PreservesColimit (F ‚ãô proj G X) G] :
    CreatesColimit F (proj G X) :=
  letI : PreservesColimit (F ‚ãô Comma.fst G (Functor.fromPUnit X)) G := i
  createsColimitOfReflectsIso fun _ t =>
    { liftedCocone := Comma.coconeOfPreserves F t punitCocone
      makesColimit := Comma.coconeOfPreservesIsColimit _ _ punitCoconeIsColimit
      validLift := Cocones.ext (Iso.refl _) fun _ => comp_id _ }
#align category_theory.costructured_arrow.creates_colimit CategoryTheory.CostructuredArrow.createsColimit

noncomputable instance createsColimitsOfShape [PreservesColimitsOfShape J G] :
    CreatesColimitsOfShape J (proj G X) where
#align category_theory.costructured_arrow.creates_colimits_of_shape CategoryTheory.CostructuredArrow.createsColimitsOfShape

noncomputable instance createsColimitsOfSize [PreservesColimitsOfSize.{w, w'} G] :
    CreatesColimitsOfSize.{w, w'} (proj G X : _) where
#align category_theory.costructured_arrow.creates_colimits CategoryTheory.CostructuredArrow.createsColimitsOfSize

instance epi_left_of_epi [HasPushouts A] [PreservesColimitsOfShape WalkingSpan G]
    {Y Z : CostructuredArrow G X} (f : Y ‚ü∂ Z) [Epi f] : Epi f.left :=
  show Epi ((proj G X).map f) from inferInstance
#align category_theory.costructured_arrow.epi_left_of_epi CategoryTheory.CostructuredArrow.epi_left_of_epi

theorem epi_iff_epi_left [HasPushouts A] [PreservesColimitsOfShape WalkingSpan G]
    {Y Z : CostructuredArrow G X} (f : Y ‚ü∂ Z) : Epi f ‚Üî Epi f.left :=
  ‚ü®fun _ => inferInstance, fun _ => epi_of_epi_left f‚ü©
#align category_theory.costructured_arrow.epi_iff_epi_left CategoryTheory.CostructuredArrow.epi_iff_epi_left

end CostructuredArrow

end CategoryTheory
