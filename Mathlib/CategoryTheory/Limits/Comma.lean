/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.Comma.Over
import Mathlib.CategoryTheory.Limits.Constructions.EpiMono
import Mathlib.CategoryTheory.Limits.Creates
import Mathlib.CategoryTheory.Limits.Unit
import Mathlib.CategoryTheory.Limits.Preserves.Filtered

/-!
# Limits and colimits in comma categories

We build limits in the comma category `Comma L R` provided that the two source categories have
limits and `R` preserves them.
This is used to construct limits in the arrow category, structured arrow category and under
category, and show that the appropriate forgetful functors create limits.

The duals of all the above are also given.
-/


namespace CategoryTheory

open Category Limits

universe w' w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {J : Type w} [Category.{w'} J]
variable {A : Type u‚ÇÅ} [Category.{v‚ÇÅ} A]
variable {B : Type u‚ÇÇ} [Category.{v‚ÇÇ} B]
variable {T : Type u‚ÇÉ} [Category.{v‚ÇÉ} T]

namespace Comma

variable {L : A ‚•§ T} {R : B ‚•§ T}
variable (F : J ‚•§ Comma L R)

/-- (Implementation). An auxiliary cone which is useful in order to construct limits
in the comma category. -/
@[simps!]
def limitAuxiliaryCone (c‚ÇÅ : Cone (F ‚ãô fst L R)) : Cone ((F ‚ãô snd L R) ‚ãô R) :=
  (Cones.postcompose (whiskerLeft F (Comma.natTrans L R) :)).obj (L.mapCone c‚ÇÅ)

/-- If `R` preserves the appropriate limit, then given a cone for `F ‚ãô fst L R : J ‚•§ L` and a
limit cone for `F ‚ãô snd L R : J ‚•§ R` we can build a cone for `F` which will turn out to be a limit
cone.
-/
@[simps]
noncomputable def coneOfPreserves [PreservesLimit (F ‚ãô snd L R) R] (c‚ÇÅ : Cone (F ‚ãô fst L R))
    {c‚ÇÇ : Cone (F ‚ãô snd L R)} (t‚ÇÇ : IsLimit c‚ÇÇ) : Cone F where
  pt :=
    { left := c‚ÇÅ.pt
      right := c‚ÇÇ.pt
      hom := (isLimitOfPreserves R t‚ÇÇ).lift (limitAuxiliaryCone _ c‚ÇÅ) }
  œÄ :=
    { app := fun j =>
        { left := c‚ÇÅ.œÄ.app j
          right := c‚ÇÇ.œÄ.app j
          w := ((isLimitOfPreserves R t‚ÇÇ).fac (limitAuxiliaryCone F c‚ÇÅ) j).symm }
      naturality := fun j‚ÇÅ j‚ÇÇ t => by
        ext
        ¬∑ simp [‚Üê c‚ÇÅ.w t]
        ¬∑ simp [‚Üê c‚ÇÇ.w t] }

/-- Provided that `R` preserves the appropriate limit, then the cone in `coneOfPreserves` is a
limit. -/
noncomputable def coneOfPreservesIsLimit [PreservesLimit (F ‚ãô snd L R) R] {c‚ÇÅ : Cone (F ‚ãô fst L R)}
    (t‚ÇÅ : IsLimit c‚ÇÅ) {c‚ÇÇ : Cone (F ‚ãô snd L R)} (t‚ÇÇ : IsLimit c‚ÇÇ) :
    IsLimit (coneOfPreserves F c‚ÇÅ t‚ÇÇ) where
  lift s :=
    { left := t‚ÇÅ.lift ((fst L R).mapCone s)
      right := t‚ÇÇ.lift ((snd L R).mapCone s)
      w :=
        (isLimitOfPreserves R t‚ÇÇ).hom_ext fun j => by
          rw [coneOfPreserves_pt_hom, assoc, assoc, (isLimitOfPreserves R t‚ÇÇ).fac,
            limitAuxiliaryCone_œÄ_app, ‚Üê L.map_comp_assoc, t‚ÇÅ.fac, R.mapCone_œÄ_app,
            ‚Üê R.map_comp, t‚ÇÇ.fac]
          exact (s.œÄ.app j).w }
  uniq s m w := by
    apply CommaMorphism.ext
    ¬∑ exact t‚ÇÅ.uniq ((fst L R).mapCone s) _ (fun j => by simp [‚Üê w])
    ¬∑ exact t‚ÇÇ.uniq ((snd L R).mapCone s) _ (fun j => by simp [‚Üê w])

/-- (Implementation). An auxiliary cocone which is useful in order to construct colimits
in the comma category. -/
@[simps!]
def colimitAuxiliaryCocone (c‚ÇÇ : Cocone (F ‚ãô snd L R)) : Cocone ((F ‚ãô fst L R) ‚ãô L) :=
  (Cocones.precompose (whiskerLeft F (Comma.natTrans L R) :)).obj (R.mapCocone c‚ÇÇ)

/--
If `L` preserves the appropriate colimit, then given a colimit cocone for `F ‚ãô fst L R : J ‚•§ L` and
a cocone for `F ‚ãô snd L R : J ‚•§ R` we can build a cocone for `F` which will turn out to be a
colimit cocone.
-/
@[simps]
noncomputable def coconeOfPreserves [PreservesColimit (F ‚ãô fst L R) L] {c‚ÇÅ : Cocone (F ‚ãô fst L R)}
    (t‚ÇÅ : IsColimit c‚ÇÅ) (c‚ÇÇ : Cocone (F ‚ãô snd L R)) : Cocone F where
  pt :=
    { left := c‚ÇÅ.pt
      right := c‚ÇÇ.pt
      hom := (isColimitOfPreserves L t‚ÇÅ).desc (colimitAuxiliaryCocone _ c‚ÇÇ) }
  Œπ :=
    { app := fun j =>
        { left := c‚ÇÅ.Œπ.app j
          right := c‚ÇÇ.Œπ.app j
          w := (isColimitOfPreserves L t‚ÇÅ).fac (colimitAuxiliaryCocone _ c‚ÇÇ) j }
      naturality := fun j‚ÇÅ j‚ÇÇ t => by
        ext
        ¬∑ simp [‚Üê c‚ÇÅ.w t]
        ¬∑ simp [‚Üê c‚ÇÇ.w t] }

/-- Provided that `L` preserves the appropriate colimit, then the cocone in `coconeOfPreserves` is
a colimit. -/
noncomputable def coconeOfPreservesIsColimit [PreservesColimit (F ‚ãô fst L R) L]
    {c‚ÇÅ : Cocone (F ‚ãô fst L R)}
    (t‚ÇÅ : IsColimit c‚ÇÅ) {c‚ÇÇ : Cocone (F ‚ãô snd L R)} (t‚ÇÇ : IsColimit c‚ÇÇ) :
    IsColimit (coconeOfPreserves F t‚ÇÅ c‚ÇÇ) where
  desc s :=
    { left := t‚ÇÅ.desc ((fst L R).mapCocone s)
      right := t‚ÇÇ.desc ((snd L R).mapCocone s)
      w :=
        (isColimitOfPreserves L t‚ÇÅ).hom_ext fun j => by
          rw [coconeOfPreserves_pt_hom, (isColimitOfPreserves L t‚ÇÅ).fac_assoc,
            colimitAuxiliaryCocone_Œπ_app, assoc, ‚Üê R.map_comp, t‚ÇÇ.fac, L.mapCocone_Œπ_app, ‚Üê
            L.map_comp_assoc, t‚ÇÅ.fac]
          exact (s.Œπ.app j).w }
  uniq s m w := by
    apply CommaMorphism.ext
    ¬∑ exact t‚ÇÅ.uniq ((fst L R).mapCocone s) _ (fun j => by simp [‚Üê w])
    ¬∑ exact t‚ÇÇ.uniq ((snd L R).mapCocone s) _ (fun j => by simp [‚Üê w])

instance hasLimit (F : J ‚•§ Comma L R) [HasLimit (F ‚ãô fst L R)] [HasLimit (F ‚ãô snd L R)]
    [PreservesLimit (F ‚ãô snd L R) R] : HasLimit F :=
  HasLimit.mk ‚ü®_, coneOfPreservesIsLimit _ (limit.isLimit _) (limit.isLimit _)‚ü©

instance hasLimitsOfShape [HasLimitsOfShape J A] [HasLimitsOfShape J B]
    [PreservesLimitsOfShape J R] : HasLimitsOfShape J (Comma L R) where

instance hasLimitsOfSize [HasLimitsOfSize.{w, w'} A] [HasLimitsOfSize.{w, w'} B]
    [PreservesLimitsOfSize.{w, w'} R] : HasLimitsOfSize.{w, w'} (Comma L R) :=
  ‚ü®fun _ _ => inferInstance‚ü©

instance hasColimit (F : J ‚•§ Comma L R) [HasColimit (F ‚ãô fst L R)] [HasColimit (F ‚ãô snd L R)]
    [PreservesColimit (F ‚ãô fst L R) L] : HasColimit F :=
  HasColimit.mk ‚ü®_, coconeOfPreservesIsColimit _ (colimit.isColimit _) (colimit.isColimit _)‚ü©

instance hasColimitsOfShape [HasColimitsOfShape J A] [HasColimitsOfShape J B]
    [PreservesColimitsOfShape J L] : HasColimitsOfShape J (Comma L R) where

instance hasColimitsOfSize [HasColimitsOfSize.{w, w'} A] [HasColimitsOfSize.{w, w'} B]
    [PreservesColimitsOfSize.{w, w'} L] : HasColimitsOfSize.{w, w'} (Comma L R) :=
  ‚ü®fun _ _ => inferInstance‚ü©

end Comma

namespace Arrow

instance hasLimit (F : J ‚•§ Arrow T) [i‚ÇÅ : HasLimit (F ‚ãô leftFunc)] [i‚ÇÇ : HasLimit (F ‚ãô rightFunc)] :
    HasLimit F := by
  haveI : HasLimit (F ‚ãô Comma.fst _ _) := i‚ÇÅ
  haveI : HasLimit (F ‚ãô Comma.snd _ _) := i‚ÇÇ
  apply Comma.hasLimit

instance hasLimitsOfShape [HasLimitsOfShape J T] : HasLimitsOfShape J (Arrow T) where

instance hasLimits [HasLimits T] : HasLimits (Arrow T) :=
  ‚ü®fun _ _ => inferInstance‚ü©

instance hasColimit (F : J ‚•§ Arrow T) [i‚ÇÅ : HasColimit (F ‚ãô leftFunc)]
    [i‚ÇÇ : HasColimit (F ‚ãô rightFunc)] : HasColimit F := by
  haveI : HasColimit (F ‚ãô Comma.fst _ _) := i‚ÇÅ
  haveI : HasColimit (F ‚ãô Comma.snd _ _) := i‚ÇÇ
  apply Comma.hasColimit

instance hasColimitsOfShape [HasColimitsOfShape J T] : HasColimitsOfShape J (Arrow T) where

instance hasColimits [HasColimits T] : HasColimits (Arrow T) :=
  ‚ü®fun _ _ => inferInstance‚ü©

end Arrow

namespace StructuredArrow

variable {X : T} {G : A ‚•§ T} (F : J ‚•§ StructuredArrow X G)

instance hasInitial [G.Faithful] [G.Full] {Y : A} :
    HasInitial (StructuredArrow (G.obj Y) G) :=
  StructuredArrow.mkIdInitial.hasInitial

instance hasLimit [i‚ÇÅ : HasLimit (F ‚ãô proj X G)] [i‚ÇÇ : PreservesLimit (F ‚ãô proj X G) G] :
    HasLimit F := by
  haveI : HasLimit (F ‚ãô Comma.snd (Functor.fromPUnit X) G) := i‚ÇÅ
  haveI : PreservesLimit (F ‚ãô Comma.snd (Functor.fromPUnit X) G) _ := i‚ÇÇ
  apply Comma.hasLimit

instance hasLimitsOfShape [HasLimitsOfShape J A] [PreservesLimitsOfShape J G] :
    HasLimitsOfShape J (StructuredArrow X G) where

instance hasLimitsOfSize [HasLimitsOfSize.{w, w'} A] [PreservesLimitsOfSize.{w, w'} G] :
    HasLimitsOfSize.{w, w'} (StructuredArrow X G) :=
  ‚ü®fun J hJ => by infer_instance‚ü©

noncomputable instance createsLimit [i : PreservesLimit (F ‚ãô proj X G) G] :
    CreatesLimit F (proj X G) :=
  letI : PreservesLimit (F ‚ãô Comma.snd (Functor.fromPUnit X) G) G := i
  createsLimitOfReflectsIso fun _ t =>
    { liftedCone := Comma.coneOfPreserves F punitCone t
      makesLimit := Comma.coneOfPreservesIsLimit _ punitConeIsLimit _
      validLift := Cones.ext (Iso.refl _) fun _ => (id_comp _).symm }

noncomputable instance createsLimitsOfShape [PreservesLimitsOfShape J G] :
    CreatesLimitsOfShape J (proj X G) where

noncomputable instance createsLimitsOfSize [PreservesLimitsOfSize.{w, w'} G] :
    CreatesLimitsOfSize.{w, w'} (proj X G :) where

instance mono_right_of_mono [HasPullbacks A] [PreservesLimitsOfShape WalkingCospan G]
    {Y Z : StructuredArrow X G} (f : Y ‚ü∂ Z) [Mono f] : Mono f.right :=
  show Mono ((proj X G).map f) from inferInstance

theorem mono_iff_mono_right [HasPullbacks A] [PreservesLimitsOfShape WalkingCospan G]
    {Y Z : StructuredArrow X G} (f : Y ‚ü∂ Z) : Mono f ‚Üî Mono f.right :=
  ‚ü®fun _ => inferInstance, fun _ => mono_of_mono_right f‚ü©

end StructuredArrow

namespace CostructuredArrow

variable {G : A ‚•§ T} {X : T} (F : J ‚•§ CostructuredArrow G X)

instance hasTerminal [G.Faithful] [G.Full] {Y : A} :
    HasTerminal (CostructuredArrow G (G.obj Y)) :=
  CostructuredArrow.mkIdTerminal.hasTerminal

instance hasColimit [i‚ÇÅ : HasColimit (F ‚ãô proj G X)] [i‚ÇÇ : PreservesColimit (F ‚ãô proj G X) G] :
    HasColimit F := by
  haveI : HasColimit (F ‚ãô Comma.fst G (Functor.fromPUnit X)) := i‚ÇÅ
  haveI : PreservesColimit (F ‚ãô Comma.fst G (Functor.fromPUnit X)) _ := i‚ÇÇ
  apply Comma.hasColimit

instance hasColimitsOfShape [HasColimitsOfShape J A] [PreservesColimitsOfShape J G] :
    HasColimitsOfShape J (CostructuredArrow G X) where

instance hasColimitsOfSize [HasColimitsOfSize.{w, w'} A] [PreservesColimitsOfSize.{w, w'} G] :
    HasColimitsOfSize.{w, w'} (CostructuredArrow G X) :=
  ‚ü®fun _ _ => inferInstance‚ü©

noncomputable instance createsColimit [i : PreservesColimit (F ‚ãô proj G X) G] :
    CreatesColimit F (proj G X) :=
  letI : PreservesColimit (F ‚ãô Comma.fst G (Functor.fromPUnit X)) G := i
  createsColimitOfReflectsIso fun _ t =>
    { liftedCocone := Comma.coconeOfPreserves F t punitCocone
      makesColimit := Comma.coconeOfPreservesIsColimit _ _ punitCoconeIsColimit
      validLift := Cocones.ext (Iso.refl _) fun _ => comp_id _ }

noncomputable instance createsColimitsOfShape [PreservesColimitsOfShape J G] :
    CreatesColimitsOfShape J (proj G X) where

noncomputable instance createsColimitsOfSize [PreservesColimitsOfSize.{w, w'} G] :
    CreatesColimitsOfSize.{w, w'} (proj G X :) where

instance epi_left_of_epi [HasPushouts A] [PreservesColimitsOfShape WalkingSpan G]
    {Y Z : CostructuredArrow G X} (f : Y ‚ü∂ Z) [Epi f] : Epi f.left :=
  show Epi ((proj G X).map f) from inferInstance

theorem epi_iff_epi_left [HasPushouts A] [PreservesColimitsOfShape WalkingSpan G]
    {Y Z : CostructuredArrow G X} (f : Y ‚ü∂ Z) : Epi f ‚Üî Epi f.left :=
  ‚ü®fun _ => inferInstance, fun _ => epi_of_epi_left f‚ü©

end CostructuredArrow

namespace Over

instance {X : T} : HasTerminal (Over X) := CostructuredArrow.hasTerminal

instance {X : T} : PreservesCofilteredLimitsOfSize (Over.forget X) := by
  refine ‚ü®fun J hJ hJ' ‚Ü¶ ‚ü®fun {F} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®.ofExistsUnique fun s ‚Ü¶ ?_‚ü©‚ü©‚ü©‚ü©
  obtain i := Nonempty.some (inferInstanceAs (Nonempty J))
  let s' : Cone F := ‚ü®Over.mk (s.œÄ.app i ‚â´ (F.obj i).hom), fun j ‚Ü¶ Over.homMk (s.œÄ.app j) (by
    obtain ‚ü®k, hik, hjk, -‚ü© := IsCofilteredOrEmpty.cone_objs i j
    simp only [Functor.const_obj_obj, Functor.id_obj, Over.mk_left, Over.mk_hom,
      ‚Üê s.w hjk, ‚Üê s.w hik]
    simp), fun j k e ‚Ü¶ by ext; simpa using (s.w e).symm‚ü©
  refine ‚ü®(hc.lift s').left, fun j ‚Ü¶ congr($(hc.fac s' j).left), fun f hf ‚Ü¶ ?_‚ü©
  dsimp at hf
  exact congr($(hc.uniq s' (Over.homMk f (by simp [s', ‚Üê hf]))
    fun j ‚Ü¶ Over.OverMorphism.ext (hf j)).left)

end Over

namespace Under

instance {X : T} : HasInitial (Under X) := StructuredArrow.hasInitial (G := ùü≠ T) (Y := X)

instance {X : T} : PreservesFilteredColimitsOfSize (Under.forget X) := by
  refine ‚ü®fun J hJ hJ' ‚Ü¶ ‚ü®fun {F} ‚Ü¶ ‚ü®fun {c} hc ‚Ü¶ ‚ü®.ofExistsUnique fun s ‚Ü¶ ?_‚ü©‚ü©‚ü©‚ü©
  obtain i := Nonempty.some (inferInstanceAs (Nonempty J))
  let s' : Cocone F := ‚ü®Under.mk ((F.obj i).hom ‚â´ s.Œπ.app i), fun j ‚Ü¶ Under.homMk (s.Œπ.app j) (by
    obtain ‚ü®k, hik, hjk, -‚ü© := IsFilteredOrEmpty.cocone_objs i j
    simp only [Functor.const_obj_obj, Functor.id_obj, Under.mk_right, Under.mk_hom,
      ‚Üê s.w hjk, ‚Üê s.w hik]
    simp), fun j k e ‚Ü¶ by ext; simpa using s.w e‚ü©
  refine ‚ü®(hc.desc s').right, fun j ‚Ü¶ congr($(hc.fac s' j).right), fun f hf ‚Ü¶ ?_‚ü©
  dsimp at hf
  exact congr($(hc.uniq s' (Under.homMk f (by simp [s', ‚Üê hf]))
    fun j ‚Ü¶ Under.UnderMorphism.ext (hf j)).right)

end Under

end CategoryTheory
