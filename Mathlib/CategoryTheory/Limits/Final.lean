/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Category.Cat.AsSmall
import Mathlib.CategoryTheory.Comma.StructuredArrow.Basic
import Mathlib.CategoryTheory.IsConnected
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Types.Shapes
import Mathlib.CategoryTheory.Limits.Shapes.Grothendieck
import Mathlib.CategoryTheory.Filtered.Basic
import Mathlib.CategoryTheory.Limits.Yoneda
import Mathlib.CategoryTheory.PUnit
import Mathlib.CategoryTheory.Grothendieck

/-!
# Final and initial functors

A functor `F : C ‚•§ D` is final if for every `d : D`,
the comma category of morphisms `d ‚ü∂ F.obj c` is connected.

Dually, a functor `F : C ‚•§ D` is initial if for every `d : D`,
the comma category of morphisms `F.obj c ‚ü∂ d` is connected.

We show that right adjoints are examples of final functors, while
left adjoints are examples of initial functors.

For final functors, we prove that the following three statements are equivalent:
1. `F : C ‚•§ D` is final.
2. Every functor `G : D ‚•§ E` has a colimit if and only if `F ‚ãô G` does,
   and these colimits are isomorphic via `colimit.pre G F`.
3. `colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ PUnit`.

Starting at 1. we show (in `coconesEquiv`) that
the categories of cocones over `G : D ‚•§ E` and over `F ‚ãô G` are equivalent.
(In fact, via an equivalence which does not change the cocone point.)
This readily implies 2., as `comp_hasColimit`, `hasColimit_of_comp`, and `colimitIso`.

From 2. we can specialize to `G = coyoneda.obj (op d)` to obtain 3., as `colimitCompCoyonedaIso`.

From 3., we prove 1. directly in `final_of_colimit_comp_coyoneda_iso_pUnit`.

Dually, we prove that if a functor `F : C ‚•§ D` is initial, then any functor `G : D ‚•§ E` has a
limit if and only if `F ‚ãô G` does, and these limits are isomorphic via `limit.pre G F`.

In the end of the file, we characterize the finality of some important induced functors on the
(co)structured arrow category (`StructuredArrow.pre` and `CostructuredArrow.pre`) and on the
Grothendieck construction (`Grothendieck.pre` and `Grothendieck.map`).

## Naming
There is some discrepancy in the literature about naming; some say 'cofinal' instead of 'final'.
The explanation for this is that the 'co' prefix here is *not* the usual category-theoretic one
indicating duality, but rather indicating the sense of "along with".

## See also
In `CategoryTheory.Filtered.Final` we give additional equivalent conditions in the case that
`C` is filtered.

## Future work
Dualise condition 3 above and the implications 2 ‚áí 3 and 3 ‚áí 1 to initial functors.

## References
* https://stacks.math.columbia.edu/tag/09WN
* https://ncatlab.org/nlab/show/final+functor
* Borceux, Handbook of Categorical Algebra I, Section 2.11.
  (Note he reverses the roles of definition and main result relative to here!)
-/


noncomputable section

universe v v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

namespace CategoryTheory

namespace Functor

open Opposite

open CategoryTheory.Limits

section ArbitraryUniverse

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/--
A functor `F : C ‚•§ D` is final if for every `d : D`, the comma category of morphisms `d ‚ü∂ F.obj c`
is connected. -/
@[stacks 04E6]
class Final (F : C ‚•§ D) : Prop where
  out (d : D) : IsConnected (StructuredArrow d F)

attribute [instance] Final.out

/-- A functor `F : C ‚•§ D` is initial if for every `d : D`, the comma category of morphisms
`F.obj c ‚ü∂ d` is connected.
-/
class Initial (F : C ‚•§ D) : Prop where
  out (d : D) : IsConnected (CostructuredArrow F d)

attribute [instance] Initial.out

instance final_op_of_initial (F : C ‚•§ D) [Initial F] : Final F.op where
  out d := isConnected_of_equivalent (costructuredArrowOpEquivalence F (unop d))

instance initial_op_of_final (F : C ‚•§ D) [Final F] : Initial F.op where
  out d := isConnected_of_equivalent (structuredArrowOpEquivalence F (unop d))

theorem final_of_initial_op (F : C ‚•§ D) [Initial F.op] : Final F :=
  {
    out := fun d =>
      @isConnected_of_isConnected_op _ _
        (isConnected_of_equivalent (structuredArrowOpEquivalence F d).symm) }

theorem initial_of_final_op (F : C ‚•§ D) [Final F.op] : Initial F :=
  {
    out := fun d =>
      @isConnected_of_isConnected_op _ _
        (isConnected_of_equivalent (costructuredArrowOpEquivalence F d).symm) }

attribute [local simp] Adjunction.homEquiv_unit Adjunction.homEquiv_counit

/-- If a functor `R : D ‚•§ C` is a right adjoint, it is final. -/
theorem final_of_adjunction {L : C ‚•§ D} {R : D ‚•§ C} (adj : L ‚ä£ R) : Final R :=
  { out := fun c =>
      let u : StructuredArrow c R := StructuredArrow.mk (adj.unit.app c)
      @zigzag_isConnected _ _ ‚ü®u‚ü© fun f g =>
        Relation.ReflTransGen.trans
          (Relation.ReflTransGen.single
            (show Zag f u from
              Or.inr ‚ü®StructuredArrow.homMk ((adj.homEquiv c f.right).symm f.hom) (by simp [u])‚ü©))
          (Relation.ReflTransGen.single
            (show Zag u g from
              Or.inl ‚ü®StructuredArrow.homMk ((adj.homEquiv c g.right).symm g.hom) (by simp [u])‚ü©)) }

/-- If a functor `L : C ‚•§ D` is a left adjoint, it is initial. -/
theorem initial_of_adjunction {L : C ‚•§ D} {R : D ‚•§ C} (adj : L ‚ä£ R) : Initial L :=
  { out := fun d =>
      let u : CostructuredArrow L d := CostructuredArrow.mk (adj.counit.app d)
      @zigzag_isConnected _ _ ‚ü®u‚ü© fun f g =>
        Relation.ReflTransGen.trans
          (Relation.ReflTransGen.single
            (show Zag f u from
              Or.inl ‚ü®CostructuredArrow.homMk (adj.homEquiv f.left d f.hom) (by simp [u])‚ü©))
          (Relation.ReflTransGen.single
            (show Zag u g from
              Or.inr ‚ü®CostructuredArrow.homMk (adj.homEquiv g.left d g.hom) (by simp [u])‚ü©)) }

instance (priority := 100) final_of_isRightAdjoint (F : C ‚•§ D) [IsRightAdjoint F] : Final F :=
  final_of_adjunction (Adjunction.ofIsRightAdjoint F)

instance (priority := 100) initial_of_isLeftAdjoint (F : C ‚•§ D) [IsLeftAdjoint F] : Initial F :=
  initial_of_adjunction (Adjunction.ofIsLeftAdjoint F)

theorem final_of_natIso {F F' : C ‚•§ D} [Final F] (i : F ‚âÖ F') : Final F' where
  out _ := isConnected_of_equivalent (StructuredArrow.mapNatIso i)

theorem final_natIso_iff {F F' : C ‚•§ D} (i : F ‚âÖ F') : Final F ‚Üî Final F' :=
  ‚ü®fun _ => final_of_natIso i, fun _ => final_of_natIso i.symm‚ü©

theorem initial_of_natIso {F F' : C ‚•§ D} [Initial F] (i : F ‚âÖ F') : Initial F' where
  out _ := isConnected_of_equivalent (CostructuredArrow.mapNatIso i)

theorem initial_natIso_iff {F F' : C ‚•§ D} (i : F ‚âÖ F') : Initial F ‚Üî Initial F' :=
  ‚ü®fun _ => initial_of_natIso i, fun _ => initial_of_natIso i.symm‚ü©

namespace Final

variable (F : C ‚•§ D) [Final F]

instance (d : D) : Nonempty (StructuredArrow d F) :=
  IsConnected.is_nonempty

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (G : D ‚•§ E)

/--
When `F : C ‚•§ D` is final, we denote by `lift F d` an arbitrary choice of object in `C` such that
there exists a morphism `d ‚ü∂ F.obj (lift F d)`.
-/
def lift (d : D) : C :=
  (Classical.arbitrary (StructuredArrow d F)).right

/-- When `F : C ‚•§ D` is final, we denote by `homToLift` an arbitrary choice of morphism
`d ‚ü∂ F.obj (lift F d)`.
-/
def homToLift (d : D) : d ‚ü∂ F.obj (lift F d) :=
  (Classical.arbitrary (StructuredArrow d F)).hom

/-- We provide an induction principle for reasoning about `lift` and `homToLift`.
We want to perform some construction (usually just a proof) about
the particular choices `lift F d` and `homToLift F d`,
it suffices to perform that construction for some other pair of choices
(denoted `X‚ÇÄ : C` and `k‚ÇÄ : d ‚ü∂ F.obj X‚ÇÄ` below),
and to show how to transport such a construction
*both* directions along a morphism between such choices.
-/
def induction {d : D} (Z : ‚àÄ (X : C) (_ : d ‚ü∂ F.obj X), Sort*)
    (h‚ÇÅ :
      ‚àÄ (X‚ÇÅ X‚ÇÇ) (k‚ÇÅ : d ‚ü∂ F.obj X‚ÇÅ) (k‚ÇÇ : d ‚ü∂ F.obj X‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
        k‚ÇÅ ‚â´ F.map f = k‚ÇÇ ‚Üí Z X‚ÇÅ k‚ÇÅ ‚Üí Z X‚ÇÇ k‚ÇÇ)
    (h‚ÇÇ :
      ‚àÄ (X‚ÇÅ X‚ÇÇ) (k‚ÇÅ : d ‚ü∂ F.obj X‚ÇÅ) (k‚ÇÇ : d ‚ü∂ F.obj X‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
        k‚ÇÅ ‚â´ F.map f = k‚ÇÇ ‚Üí Z X‚ÇÇ k‚ÇÇ ‚Üí Z X‚ÇÅ k‚ÇÅ)
    {X‚ÇÄ : C} {k‚ÇÄ : d ‚ü∂ F.obj X‚ÇÄ} (z : Z X‚ÇÄ k‚ÇÄ) : Z (lift F d) (homToLift F d) := by
  apply Nonempty.some
  apply
    @isPreconnected_induction _ _ _ (fun Y : StructuredArrow d F => Z Y.right Y.hom) _ _
      (StructuredArrow.mk k‚ÇÄ) z
  ¬∑ intro j‚ÇÅ j‚ÇÇ f a
    fapply h‚ÇÅ _ _ _ _ f.right _ a
    convert f.w.symm
    simp
  ¬∑ intro j‚ÇÅ j‚ÇÇ f a
    fapply h‚ÇÇ _ _ _ _ f.right _ a
    convert f.w.symm
    simp

variable {F G}

/-- Given a cocone over `F ‚ãô G`, we can construct a `Cocone G` with the same cocone point.
-/
@[simps]
def extendCocone : Cocone (F ‚ãô G) ‚•§ Cocone G where
  obj c :=
    { pt := c.pt
      Œπ :=
        { app := fun X => G.map (homToLift F X) ‚â´ c.Œπ.app (lift F X)
          naturality := fun X Y f => by
            dsimp; simp only [Category.comp_id]
            -- This would be true if we'd chosen `lift F X` to be `lift F Y`
            -- and `homToLift F X` to be `f ‚â´ homToLift F Y`.
            apply
              induction F fun Z k =>
                G.map f ‚â´ G.map (homToLift F Y) ‚â´ c.Œπ.app (lift F Y) = G.map k ‚â´ c.Œπ.app Z
            ¬∑ intro Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z
              rw [‚Üê a, Functor.map_comp, Category.assoc, ‚Üê Functor.comp_map, c.w, z]
            ¬∑ intro Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z
              rw [‚Üê a, Functor.map_comp, Category.assoc, ‚Üê Functor.comp_map, c.w] at z
              rw [z]
            ¬∑ rw [‚Üê Functor.map_comp_assoc] } }
  map f := { hom := f.hom }

/-- Alternative equational lemma for `(extendCocone c).Œπ.app` in case a lift of the object
is given explicitly. -/
lemma extendCocone_obj_Œπ_app' (c : Cocone (F ‚ãô G)) {X : D} {Y : C} (f : X ‚ü∂ F.obj Y) :
    (extendCocone.obj c).Œπ.app X = G.map f ‚â´ c.Œπ.app Y := by
  apply induction (k‚ÇÄ := f) (z := rfl) F fun Z g =>
    G.map g ‚â´ c.Œπ.app Z = G.map f ‚â´ c.Œπ.app Y
  ¬∑ intro _ _ _ _ _ h‚ÇÅ h‚ÇÇ
    simp [‚Üê h‚ÇÅ, ‚Üê Functor.comp_map, c.Œπ.naturality, h‚ÇÇ]
  ¬∑ intro _ _ _ _ _ h‚ÇÅ h‚ÇÇ
    simp [‚Üê h‚ÇÇ, ‚Üê h‚ÇÅ, ‚Üê Functor.comp_map, c.Œπ.naturality]

@[simp]
theorem colimit_cocone_comp_aux (s : Cocone (F ‚ãô G)) (j : C) :
    G.map (homToLift F (F.obj j)) ‚â´ s.Œπ.app (lift F (F.obj j)) = s.Œπ.app j := by
  -- This point is that this would be true if we took `lift (F.obj j)` to just be `j`
  -- and `homToLift (F.obj j)` to be `ùüô (F.obj j)`.
  apply induction F fun X k => G.map k ‚â´ s.Œπ.app X = (s.Œπ.app j :)
  ¬∑ intro j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h
    rw [‚Üê w]
    rw [‚Üê s.w f] at h
    simpa using h
  ¬∑ intro j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h
    rw [‚Üê w] at h
    rw [‚Üê s.w f]
    simpa using h
  ¬∑ exact s.w (ùüô _)

variable (F G)

/-- If `F` is final,
the category of cocones on `F ‚ãô G` is equivalent to the category of cocones on `G`,
for any `G : D ‚•§ E`.
-/
@[simps]
def coconesEquiv : Cocone (F ‚ãô G) ‚âå Cocone G where
  functor := extendCocone
  inverse := Cocones.whiskering F
  unitIso := NatIso.ofComponents fun c => Cocones.ext (Iso.refl _)
  counitIso := NatIso.ofComponents fun c => Cocones.ext (Iso.refl _)

variable {G}

/-- When `F : C ‚•§ D` is final, and `t : Cocone G` for some `G : D ‚•§ E`,
`t.whisker F` is a colimit cocone exactly when `t` is.
-/
def isColimitWhiskerEquiv (t : Cocone G) : IsColimit (t.whisker F) ‚âÉ IsColimit t :=
  IsColimit.ofCoconeEquiv (coconesEquiv F G).symm

/-- When `F` is final, and `t : Cocone (F ‚ãô G)`,
`extendCocone.obj t` is a colimit cocone exactly when `t` is.
-/
def isColimitExtendCoconeEquiv (t : Cocone (F ‚ãô G)) :
    IsColimit (extendCocone.obj t) ‚âÉ IsColimit t :=
  IsColimit.ofCoconeEquiv (coconesEquiv F G)

/-- Given a colimit cocone over `G : D ‚•§ E` we can construct a colimit cocone over `F ‚ãô G`. -/
@[simps]
def colimitCoconeComp (t : ColimitCocone G) : ColimitCocone (F ‚ãô G) where
  cocone := _
  isColimit := (isColimitWhiskerEquiv F _).symm t.isColimit

instance (priority := 100) comp_hasColimit [HasColimit G] : HasColimit (F ‚ãô G) :=
  HasColimit.mk (colimitCoconeComp F (getColimitCocone G))

instance (priority := 100) comp_preservesColimit {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [PreservesColimit G H] : PreservesColimit (F ‚ãô G) H where
  preserves {c} hc := by
    refine ‚ü®isColimitExtendCoconeEquiv (G := G ‚ãô H) F (H.mapCocone c) ?_‚ü©
    let hc' := isColimitOfPreserves H ((isColimitExtendCoconeEquiv F c).symm hc)
    exact IsColimit.ofIsoColimit hc' (Cocones.ext (Iso.refl _) (by simp))

instance (priority := 100) comp_reflectsColimit {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [ReflectsColimit G H] : ReflectsColimit (F ‚ãô G) H where
  reflects {c} hc := by
    refine ‚ü®isColimitExtendCoconeEquiv F _ (isColimitOfReflects H ?_)‚ü©
    let hc' := (isColimitExtendCoconeEquiv (G := G ‚ãô H) F _).symm hc
    exact IsColimit.ofIsoColimit hc' (Cocones.ext (Iso.refl _) (by simp))

instance (priority := 100) compCreatesColimit {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [CreatesColimit G H] : CreatesColimit (F ‚ãô G) H where
  lifts {c} hc := by
    refine ‚ü®(liftColimit ((isColimitExtendCoconeEquiv F (G := G ‚ãô H) _).symm hc)).whisker F, ?_‚ü©
    let i := liftedColimitMapsToOriginal ((isColimitExtendCoconeEquiv F (G := G ‚ãô H) _).symm hc)
    exact (Cocones.whiskering F).mapIso i ‚â™‚â´ ((coconesEquiv F (G ‚ãô H)).unitIso.app _).symm

instance colimit_pre_isIso [HasColimit G] : IsIso (colimit.pre G F) := by
  rw [colimit.pre_eq (colimitCoconeComp F (getColimitCocone G)) (getColimitCocone G)]
  erw [IsColimit.desc_self]
  dsimp
  infer_instance

section

variable (G)

/-- When `F : C ‚•§ D` is final, and `G : D ‚•§ E` has a colimit, then `F ‚ãô G` has a colimit also and
`colimit (F ‚ãô G) ‚âÖ colimit G`. -/
@[simps! -isSimp, stacks 04E7]
def colimitIso [HasColimit G] : colimit (F ‚ãô G) ‚âÖ colimit G :=
  asIso (colimit.pre G F)

@[reassoc (attr := simp)]
theorem Œπ_colimitIso_hom [HasColimit G] (X : C) :
    colimit.Œπ (F ‚ãô G) X ‚â´ (colimitIso F G).hom = colimit.Œπ G (F.obj X) := by
  simp [colimitIso]

@[reassoc (attr := simp)]
theorem Œπ_colimitIso_inv [HasColimit G] (X : C) :
    colimit.Œπ G (F.obj X) ‚â´ (colimitIso F G).inv = colimit.Œπ (F ‚ãô G) X := by
  simp [colimitIso]

/-- A pointfree version of `colimitIso`, stating that whiskering by `F` followed by taking the
colimit is isomorpic to taking the colimit on the codomain of `F`. -/
def colimIso [HasColimitsOfShape D E] [HasColimitsOfShape C E] :
    (whiskeringLeft _ _ _).obj F ‚ãô colim ‚âÖ colim (J := D) (C := E) :=
  NatIso.ofComponents (fun G => colimitIso F G) fun f => by
    simp only [comp_obj, whiskeringLeft_obj_obj, colim_obj, comp_map, whiskeringLeft_obj_map,
      colim_map, colimitIso_hom]
    ext
    simp only [comp_obj, Œπ_colimMap_assoc, whiskerLeft_app, colimit.Œπ_pre, colimit.Œπ_pre_assoc,
      Œπ_colimMap]

end

/-- Given a colimit cocone over `F ‚ãô G` we can construct a colimit cocone over `G`. -/
@[simps]
def colimitCoconeOfComp (t : ColimitCocone (F ‚ãô G)) : ColimitCocone G where
  cocone := extendCocone.obj t.cocone
  isColimit := (isColimitExtendCoconeEquiv F _).symm t.isColimit

/-- When `F` is final, and `F ‚ãô G` has a colimit, then `G` has a colimit also.

We can't make this an instance, because `F` is not determined by the goal.
(Even if this weren't a problem, it would cause a loop with `comp_hasColimit`.)
-/
theorem hasColimit_of_comp [HasColimit (F ‚ãô G)] : HasColimit G :=
  HasColimit.mk (colimitCoconeOfComp F (getColimitCocone (F ‚ãô G)))

theorem preservesColimit_of_comp {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [PreservesColimit (F ‚ãô G) H] : PreservesColimit G H where
  preserves {c} hc := by
    refine ‚ü®isColimitWhiskerEquiv F _ ?_‚ü©
    let hc' := isColimitOfPreserves H ((isColimitWhiskerEquiv F _).symm hc)
    exact IsColimit.ofIsoColimit hc' (Cocones.ext (Iso.refl _) (by simp))

theorem reflectsColimit_of_comp {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [ReflectsColimit (F ‚ãô G) H] : ReflectsColimit G H where
  reflects {c} hc := by
    refine ‚ü®isColimitWhiskerEquiv F _ (isColimitOfReflects H ?_)‚ü©
    let hc' := (isColimitWhiskerEquiv F _).symm hc
    exact IsColimit.ofIsoColimit hc' (Cocones.ext (Iso.refl _) (by simp))

/-- If `F` is final and `F ‚ãô G` creates colimits of `H`, then so does `G`. -/
def createsColimitOfComp {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [CreatesColimit (F ‚ãô G) H] : CreatesColimit G H where
  reflects := (reflectsColimit_of_comp F).reflects
  lifts {c} hc := by
    refine ‚ü®(extendCocone (F := F)).obj (liftColimit ((isColimitWhiskerEquiv F _).symm hc)), ?_‚ü©
    let i := liftedColimitMapsToOriginal (K := (F ‚ãô G)) ((isColimitWhiskerEquiv F _).symm hc)
    refine ?_ ‚â™‚â´ ((extendCocone (F := F)).mapIso i) ‚â™‚â´ ((coconesEquiv F (G ‚ãô H)).counitIso.app _)
    exact Cocones.ext (Iso.refl _)

include F in
theorem hasColimitsOfShape_of_final [HasColimitsOfShape C E] : HasColimitsOfShape D E where
  has_colimit := fun _ => hasColimit_of_comp F

include F in
theorem preservesColimitsOfShape_of_final {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] (H : E ‚•§ B)
    [PreservesColimitsOfShape C H] : PreservesColimitsOfShape D H where
  preservesColimit := preservesColimit_of_comp F

include F in
theorem reflectsColimitsOfShape_of_final {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] (H : E ‚•§ B)
    [ReflectsColimitsOfShape C H] : ReflectsColimitsOfShape D H where
  reflectsColimit := reflectsColimit_of_comp F

include F in
/-- If `H` creates colimits of shape `C` and `F : C ‚•§ D` is final, then `H` creates colimits of
shape `D`. -/
def createsColimitsOfShapeOfFinal {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] (H : E ‚•§ B)
    [CreatesColimitsOfShape C H] : CreatesColimitsOfShape D H where
  CreatesColimit := createsColimitOfComp F

end Final

end ArbitraryUniverse

section LocallySmall

variable {C : Type v} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v} D] (F : C ‚•§ D)

namespace Final

theorem zigzag_of_eqvGen_quot_rel {F : C ‚•§ D} {d : D} {f‚ÇÅ f‚ÇÇ : Œ£ X, d ‚ü∂ F.obj X}
    (t : Relation.EqvGen (Types.Quot.Rel.{v, v} (F ‚ãô coyoneda.obj (op d))) f‚ÇÅ f‚ÇÇ) :
    Zigzag (StructuredArrow.mk f‚ÇÅ.2) (StructuredArrow.mk f‚ÇÇ.2) := by
  induction t with
  | rel x y r =>
    obtain ‚ü®f, w‚ü© := r
    fconstructor
    swap
    ¬∑ fconstructor
    left; fconstructor
    exact StructuredArrow.homMk f
  | refl => fconstructor
  | symm x y _ ih =>
    apply zigzag_symmetric
    exact ih
  | trans x y z _ _ ih‚ÇÅ ih‚ÇÇ =>
    apply Relation.ReflTransGen.trans
    ¬∑ exact ih‚ÇÅ
    ¬∑ exact ih‚ÇÇ

end Final

/-- If `colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ PUnit` for all `d : D`, then `F` is final.
-/
theorem final_of_colimit_comp_coyoneda_iso_pUnit
    (I : ‚àÄ d, colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ PUnit) : Final F :=
  ‚ü®fun d => by
    have : Nonempty (StructuredArrow d F) := by
      have := (I d).inv PUnit.unit
      obtain ‚ü®j, y, rfl‚ü© := Limits.Types.jointly_surjective'.{v, v} this
      exact ‚ü®StructuredArrow.mk y‚ü©
    apply zigzag_isConnected
    rintro ‚ü®‚ü®‚ü®‚ü©‚ü©, X‚ÇÅ, f‚ÇÅ‚ü© ‚ü®‚ü®‚ü®‚ü©‚ü©, X‚ÇÇ, f‚ÇÇ‚ü©
    let y‚ÇÅ := colimit.Œπ (F ‚ãô coyoneda.obj (op d)) X‚ÇÅ f‚ÇÅ
    let y‚ÇÇ := colimit.Œπ (F ‚ãô coyoneda.obj (op d)) X‚ÇÇ f‚ÇÇ
    have e : y‚ÇÅ = y‚ÇÇ := by
      apply (I d).toEquiv.injective
      ext
    have t := Types.colimit_eq.{v, v} e
    clear e y‚ÇÅ y‚ÇÇ
    exact Final.zigzag_of_eqvGen_quot_rel t‚ü©

/-- A variant of `final_of_colimit_comp_coyoneda_iso_pUnit` where we bind the various claims
    about `colimit (F ‚ãô coyoneda.obj (Opposite.op d))` for each `d : D` into a single claim about
    the presheaf `colimit (F ‚ãô yoneda)`. -/
theorem final_of_isTerminal_colimit_comp_yoneda
    (h : IsTerminal (colimit (F ‚ãô yoneda))) : Final F := by
  refine final_of_colimit_comp_coyoneda_iso_pUnit _ (fun d => ?_)
  refine Types.isTerminalEquivIsoPUnit _ ?_
  let b := IsTerminal.isTerminalObj ((evaluation _ _).obj (Opposite.op d)) _ h
  exact b.ofIso <| preservesColimitIso ((evaluation _ _).obj (Opposite.op d)) (F ‚ãô yoneda)

/-- If the universal morphism `colimit (F ‚ãô coyoneda.obj (op d)) ‚ü∂ colimit (coyoneda.obj (op d))`
is an isomorphism (as it always is when `F` is final),
then `colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ PUnit`
(simply because `colimit (coyoneda.obj (op d)) ‚âÖ PUnit`).
-/
def Final.colimitCompCoyonedaIso (d : D) [IsIso (colimit.pre (coyoneda.obj (op d)) F)] :
    colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ PUnit :=
  asIso (colimit.pre (coyoneda.obj (op d)) F) ‚â™‚â´ Coyoneda.colimitCoyonedaIso (op d)

end LocallySmall

section SmallCategory

variable {C : Type v} [Category.{v} C] {D : Type v} [Category.{v} D] (F : C ‚•§ D)

theorem final_iff_isIso_colimit_pre : Final F ‚Üî ‚àÄ G : D ‚•§ Type v, IsIso (colimit.pre G F) :=
  ‚ü®fun _ => inferInstance,
   fun _ => final_of_colimit_comp_coyoneda_iso_pUnit _ fun _ => Final.colimitCompCoyonedaIso _ _‚ü©

end SmallCategory

namespace Initial

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : C ‚•§ D) [Initial F]

instance (d : D) : Nonempty (CostructuredArrow F d) :=
  IsConnected.is_nonempty

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (G : D ‚•§ E)

/--
When `F : C ‚•§ D` is initial, we denote by `lift F d` an arbitrary choice of object in `C` such that
there exists a morphism `F.obj (lift F d) ‚ü∂ d`.
-/
def lift (d : D) : C :=
  (Classical.arbitrary (CostructuredArrow F d)).left

/-- When `F : C ‚•§ D` is initial, we denote by `homToLift` an arbitrary choice of morphism
`F.obj (lift F d) ‚ü∂ d`.
-/
def homToLift (d : D) : F.obj (lift F d) ‚ü∂ d :=
  (Classical.arbitrary (CostructuredArrow F d)).hom

/-- We provide an induction principle for reasoning about `lift` and `homToLift`.
We want to perform some construction (usually just a proof) about
the particular choices `lift F d` and `homToLift F d`,
it suffices to perform that construction for some other pair of choices
(denoted `X‚ÇÄ : C` and `k‚ÇÄ : F.obj X‚ÇÄ ‚ü∂ d` below),
and to show how to transport such a construction
*both* directions along a morphism between such choices.
-/
def induction {d : D} (Z : ‚àÄ (X : C) (_ : F.obj X ‚ü∂ d), Sort*)
    (h‚ÇÅ :
      ‚àÄ (X‚ÇÅ X‚ÇÇ) (k‚ÇÅ : F.obj X‚ÇÅ ‚ü∂ d) (k‚ÇÇ : F.obj X‚ÇÇ ‚ü∂ d) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
        F.map f ‚â´ k‚ÇÇ = k‚ÇÅ ‚Üí Z X‚ÇÅ k‚ÇÅ ‚Üí Z X‚ÇÇ k‚ÇÇ)
    (h‚ÇÇ :
      ‚àÄ (X‚ÇÅ X‚ÇÇ) (k‚ÇÅ : F.obj X‚ÇÅ ‚ü∂ d) (k‚ÇÇ : F.obj X‚ÇÇ ‚ü∂ d) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
        F.map f ‚â´ k‚ÇÇ = k‚ÇÅ ‚Üí Z X‚ÇÇ k‚ÇÇ ‚Üí Z X‚ÇÅ k‚ÇÅ)
    {X‚ÇÄ : C} {k‚ÇÄ : F.obj X‚ÇÄ ‚ü∂ d} (z : Z X‚ÇÄ k‚ÇÄ) : Z (lift F d) (homToLift F d) := by
  apply Nonempty.some
  apply
    @isPreconnected_induction _ _ _ (fun Y : CostructuredArrow F d => Z Y.left Y.hom) _ _
      (CostructuredArrow.mk k‚ÇÄ) z
  ¬∑ intro j‚ÇÅ j‚ÇÇ f a
    fapply h‚ÇÅ _ _ _ _ f.left _ a
    convert f.w
    simp
  ¬∑ intro j‚ÇÅ j‚ÇÇ f a
    fapply h‚ÇÇ _ _ _ _ f.left _ a
    convert f.w
    simp

variable {F G}

/-- Given a cone over `F ‚ãô G`, we can construct a `Cone G` with the same cocone point.
-/
@[simps]
def extendCone : Cone (F ‚ãô G) ‚•§ Cone G where
  obj c :=
    { pt := c.pt
      œÄ :=
        { app := fun d => c.œÄ.app (lift F d) ‚â´ G.map (homToLift F d)
          naturality := fun X Y f => by
            dsimp; simp only [Category.id_comp, Category.assoc]
            -- This would be true if we'd chosen `lift F Y` to be `lift F X`
            -- and `homToLift F Y` to be `homToLift F X ‚â´ f`.
            apply
              induction F fun Z k =>
                (c.œÄ.app Z ‚â´ G.map k : c.pt ‚ü∂ _) =
                  c.œÄ.app (lift F X) ‚â´ G.map (homToLift F X) ‚â´ G.map f
            ¬∑ intro Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z
              rw [‚Üê a, Functor.map_comp, ‚Üê Functor.comp_map, ‚Üê Category.assoc, ‚Üê Category.assoc,
                c.w] at z
              rw [z, Category.assoc]
            ¬∑ intro Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z
              rw [‚Üê a, Functor.map_comp, ‚Üê Functor.comp_map, ‚Üê Category.assoc, ‚Üê Category.assoc,
                c.w, z, Category.assoc]
            ¬∑ rw [‚Üê Functor.map_comp] } }
  map f := { hom := f.hom }

/-- Alternative equational lemma for `(extendCone c).œÄ.app` in case a lift of the object
is given explicitly. -/
lemma extendCone_obj_œÄ_app' (c : Cone (F ‚ãô G)) {X : C} {Y : D} (f : F.obj X ‚ü∂ Y) :
    (extendCone.obj c).œÄ.app Y = c.œÄ.app X ‚â´ G.map f := by
  apply induction (k‚ÇÄ := f) (z := rfl) F fun Z g =>
    c.œÄ.app Z ‚â´ G.map g = c.œÄ.app X ‚â´ G.map f
  ¬∑ intro _ _ _ _ _ h‚ÇÅ h‚ÇÇ
    simp [‚Üê h‚ÇÇ, ‚Üê h‚ÇÅ, ‚Üê Functor.comp_map, c.œÄ.naturality]
  ¬∑ intro _ _ _ _ _ h‚ÇÅ h‚ÇÇ
    simp [‚Üê h‚ÇÅ, ‚Üê Functor.comp_map, c.œÄ.naturality, h‚ÇÇ]

@[simp]
theorem limit_cone_comp_aux (s : Cone (F ‚ãô G)) (j : C) :
    s.œÄ.app (lift F (F.obj j)) ‚â´ G.map (homToLift F (F.obj j)) = s.œÄ.app j := by
  -- This point is that this would be true if we took `lift (F.obj j)` to just be `j`
  -- and `homToLift (F.obj j)` to be `ùüô (F.obj j)`.
  apply induction F fun X k => s.œÄ.app X ‚â´ G.map k = (s.œÄ.app j :)
  ¬∑ intro j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h
    rw [‚Üê s.w f]
    rw [‚Üê w] at h
    simpa using h
  ¬∑ intro j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h
    rw [‚Üê s.w f] at h
    rw [‚Üê w]
    simpa using h
  ¬∑ exact s.w (ùüô _)

variable (F G)

/-- If `F` is initial,
the category of cones on `F ‚ãô G` is equivalent to the category of cones on `G`,
for any `G : D ‚•§ E`.
-/
@[simps]
def conesEquiv : Cone (F ‚ãô G) ‚âå Cone G where
  functor := extendCone
  inverse := Cones.whiskering F
  unitIso := NatIso.ofComponents fun c => Cones.ext (Iso.refl _)
  counitIso := NatIso.ofComponents fun c => Cones.ext (Iso.refl _)

variable {G}

/-- When `F : C ‚•§ D` is initial, and `t : Cone G` for some `G : D ‚•§ E`,
`t.whisker F` is a limit cone exactly when `t` is.
-/
def isLimitWhiskerEquiv (t : Cone G) : IsLimit (t.whisker F) ‚âÉ IsLimit t :=
  IsLimit.ofConeEquiv (conesEquiv F G).symm

/-- When `F` is initial, and `t : Cone (F ‚ãô G)`,
`extendCone.obj t` is a limit cone exactly when `t` is.
-/
def isLimitExtendConeEquiv (t : Cone (F ‚ãô G)) : IsLimit (extendCone.obj t) ‚âÉ IsLimit t :=
  IsLimit.ofConeEquiv (conesEquiv F G)

/-- Given a limit cone over `G : D ‚•§ E` we can construct a limit cone over `F ‚ãô G`. -/
@[simps]
def limitConeComp (t : LimitCone G) : LimitCone (F ‚ãô G) where
  cone := _
  isLimit := (isLimitWhiskerEquiv F _).symm t.isLimit

instance (priority := 100) comp_hasLimit [HasLimit G] : HasLimit (F ‚ãô G) :=
  HasLimit.mk (limitConeComp F (getLimitCone G))

instance (priority := 100) comp_preservesLimit {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [PreservesLimit G H] : PreservesLimit (F ‚ãô G) H where
  preserves {c} hc := by
    refine ‚ü®isLimitExtendConeEquiv (G := G ‚ãô H) F (H.mapCone c) ?_‚ü©
    let hc' := isLimitOfPreserves H ((isLimitExtendConeEquiv F c).symm hc)
    exact IsLimit.ofIsoLimit hc' (Cones.ext (Iso.refl _) (by simp))

instance (priority := 100) comp_reflectsLimit {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [ReflectsLimit G H] : ReflectsLimit (F ‚ãô G) H where
  reflects {c} hc := by
    refine ‚ü®isLimitExtendConeEquiv F _ (isLimitOfReflects H ?_)‚ü©
    let hc' := (isLimitExtendConeEquiv (G := G ‚ãô H) F _).symm hc
    exact IsLimit.ofIsoLimit hc' (Cones.ext (Iso.refl _) (by simp))

instance (priority := 100) compCreatesLimit {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [CreatesLimit G H] : CreatesLimit (F ‚ãô G) H where
  lifts {c} hc := by
    refine ‚ü®(liftLimit ((isLimitExtendConeEquiv F (G := G ‚ãô H) _).symm hc)).whisker F, ?_‚ü©
    let i := liftedLimitMapsToOriginal ((isLimitExtendConeEquiv F (G := G ‚ãô H) _).symm hc)
    exact (Cones.whiskering F).mapIso i ‚â™‚â´ ((conesEquiv F (G ‚ãô H)).unitIso.app _).symm

instance limit_pre_isIso [HasLimit G] : IsIso (limit.pre G F) := by
  rw [limit.pre_eq (limitConeComp F (getLimitCone G)) (getLimitCone G)]
  erw [IsLimit.lift_self]
  dsimp
  infer_instance

section

variable (G)

/-- When `F : C ‚•§ D` is initial, and `G : D ‚•§ E` has a limit, then `F ‚ãô G` has a limit also and
`limit (F ‚ãô G) ‚âÖ limit G`. -/
@[simps! -isSimp, stacks 04E7]
def limitIso [HasLimit G] : limit (F ‚ãô G) ‚âÖ limit G :=
  (asIso (limit.pre G F)).symm

/-- A pointfree version of `limitIso`, stating that whiskering by `F` followed by taking the
limit is isomorpic to taking the limit on the codomain of `F`. -/
def limIso [HasLimitsOfShape D E] [HasLimitsOfShape C E] :
    (whiskeringLeft _ _ _).obj F ‚ãô lim ‚âÖ lim (J := D) (C := E) :=
  Iso.symm <| NatIso.ofComponents (fun G => (limitIso F G).symm) fun f => by
    simp only [comp_obj, whiskeringLeft_obj_obj, lim_obj, comp_map, whiskeringLeft_obj_map, lim_map,
      Iso.symm_hom, limitIso_inv]
    ext
    simp

end

/-- Given a limit cone over `F ‚ãô G` we can construct a limit cone over `G`. -/
@[simps]
def limitConeOfComp (t : LimitCone (F ‚ãô G)) : LimitCone G where
  cone := extendCone.obj t.cone
  isLimit := (isLimitExtendConeEquiv F _).symm t.isLimit

/-- When `F` is initial, and `F ‚ãô G` has a limit, then `G` has a limit also.

We can't make this an instance, because `F` is not determined by the goal.
(Even if this weren't a problem, it would cause a loop with `comp_hasLimit`.)
-/
theorem hasLimit_of_comp [HasLimit (F ‚ãô G)] : HasLimit G :=
  HasLimit.mk (limitConeOfComp F (getLimitCone (F ‚ãô G)))

theorem preservesLimit_of_comp {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [PreservesLimit (F ‚ãô G) H] : PreservesLimit G H where
  preserves {c} hc := by
    refine ‚ü®isLimitWhiskerEquiv F _ ?_‚ü©
    let hc' := isLimitOfPreserves H ((isLimitWhiskerEquiv F _).symm hc)
    exact IsLimit.ofIsoLimit hc' (Cones.ext (Iso.refl _) (by simp))

theorem reflectsLimit_of_comp {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [ReflectsLimit (F ‚ãô G) H] : ReflectsLimit G H where
  reflects {c} hc := by
    refine ‚ü®isLimitWhiskerEquiv F _ (isLimitOfReflects H ?_)‚ü©
    let hc' := (isLimitWhiskerEquiv F _).symm hc
    exact IsLimit.ofIsoLimit hc' (Cones.ext (Iso.refl _) (by simp))

/-- If `F` is initial and `F ‚ãô G` creates limits of `H`, then so does `G`. -/
def createsLimitOfComp {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] {H : E ‚•§ B}
    [CreatesLimit (F ‚ãô G) H] : CreatesLimit G H where
  reflects := (reflectsLimit_of_comp F).reflects
  lifts {c} hc := by
    refine ‚ü®(extendCone (F := F)).obj (liftLimit ((isLimitWhiskerEquiv F _).symm hc)), ?_‚ü©
    let i := liftedLimitMapsToOriginal (K := (F ‚ãô G)) ((isLimitWhiskerEquiv F _).symm hc)
    refine ?_ ‚â™‚â´ ((extendCone (F := F)).mapIso i) ‚â™‚â´ ((conesEquiv F (G ‚ãô H)).counitIso.app _)
    exact Cones.ext (Iso.refl _)

include F in
theorem hasLimitsOfShape_of_initial [HasLimitsOfShape C E] : HasLimitsOfShape D E where
  has_limit := fun _ => hasLimit_of_comp F

include F in
theorem preservesLimitsOfShape_of_initial {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] (H : E ‚•§ B)
    [PreservesLimitsOfShape C H] : PreservesLimitsOfShape D H where
  preservesLimit := preservesLimit_of_comp F

include F in
theorem reflectsLimitsOfShape_of_initial {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] (H : E ‚•§ B)
    [ReflectsLimitsOfShape C H] : ReflectsLimitsOfShape D H where
  reflectsLimit := reflectsLimit_of_comp F

include F in
/-- If `H` creates limits of shape `C` and `F : C ‚•§ D` is initial, then `H` creates limits of shape
`D`. -/
def createsLimitsOfShapeOfInitial {B : Type u‚ÇÑ} [Category.{v‚ÇÑ} B] (H : E ‚•§ B)
    [CreatesLimitsOfShape C H] : CreatesLimitsOfShape D H where
  CreatesLimit := createsLimitOfComp F

end Initial

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] (F : C ‚•§ D) (G : D ‚•§ E)

/-- The hypotheses also imply that `G` is final, see `final_of_comp_full_faithful'`. -/
theorem final_of_comp_full_faithful [Full G] [Faithful G] [Final (F ‚ãô G)] : Final F where
  out d := isConnected_of_equivalent (StructuredArrow.post d F G).asEquivalence.symm

/-- The hypotheses also imply that `G` is initial, see `initial_of_comp_full_faithful'`. -/
theorem initial_of_comp_full_faithful [Full G] [Faithful G] [Initial (F ‚ãô G)] : Initial F where
  out d := isConnected_of_equivalent (CostructuredArrow.post F G d).asEquivalence.symm

/-- See also the strictly more general `final_comp` below. -/
theorem final_comp_equivalence [Final F] [IsEquivalence G] : Final (F ‚ãô G) :=
  let i : F ‚âÖ (F ‚ãô G) ‚ãô G.inv := isoWhiskerLeft F G.asEquivalence.unitIso
  have : Final ((F ‚ãô G) ‚ãô G.inv) := final_of_natIso i
  final_of_comp_full_faithful (F ‚ãô G) G.inv

/-- See also the strictly more general `initial_comp` below. -/
theorem initial_comp_equivalence [Initial F] [IsEquivalence G] : Initial (F ‚ãô G) :=
  let i : F ‚âÖ (F ‚ãô G) ‚ãô G.inv := isoWhiskerLeft F G.asEquivalence.unitIso
  have : Initial ((F ‚ãô G) ‚ãô G.inv) := initial_of_natIso i
  initial_of_comp_full_faithful (F ‚ãô G) G.inv

/-- See also the strictly more general `final_comp` below. -/
theorem final_equivalence_comp [IsEquivalence F] [Final G] : Final (F ‚ãô G) where
  out d := isConnected_of_equivalent (StructuredArrow.pre d F G).asEquivalence.symm

/-- See also the strictly more general `initial_comp` below. -/
theorem initial_equivalence_comp [IsEquivalence F] [Initial G] : Initial (F ‚ãô G) where
  out d := isConnected_of_equivalent (CostructuredArrow.pre F G d).asEquivalence.symm

/-- See also the strictly more general `final_of_final_comp` below. -/
theorem final_of_equivalence_comp [IsEquivalence F] [Final (F ‚ãô G)] : Final G where
  out d := isConnected_of_equivalent (StructuredArrow.pre d F G).asEquivalence

/-- See also the strictly more general `initial_of_initial_comp` below. -/
theorem initial_of_equivalence_comp [IsEquivalence F] [Initial (F ‚ãô G)] : Initial G where
  out d := isConnected_of_equivalent (CostructuredArrow.pre F G d).asEquivalence

/-- See also the strictly more general `final_iff_comp_final_full_faithful` below. -/
theorem final_iff_comp_equivalence [IsEquivalence G] : Final F ‚Üî Final (F ‚ãô G) :=
  ‚ü®fun _ => final_comp_equivalence _ _, fun _ => final_of_comp_full_faithful _ G‚ü©

/-- See also the strictly more general `final_iff_final_comp` below. -/
theorem final_iff_equivalence_comp [IsEquivalence F] : Final G ‚Üî Final (F ‚ãô G) :=
  ‚ü®fun _ => final_equivalence_comp _ _, fun _ => final_of_equivalence_comp F _‚ü©

/-- See also the strictly more general `initial_iff_comp_initial_full_faithful` below. -/
theorem initial_iff_comp_equivalence [IsEquivalence G] : Initial F ‚Üî Initial (F ‚ãô G) :=
  ‚ü®fun _ => initial_comp_equivalence _ _, fun _ => initial_of_comp_full_faithful _ G‚ü©

/-- See also the strictly more general `initial_iff_initial_comp` below. -/
theorem initial_iff_equivalence_comp [IsEquivalence F] : Initial G ‚Üî Initial (F ‚ãô G) :=
  ‚ü®fun _ => initial_equivalence_comp _ _, fun _ => initial_of_equivalence_comp F _‚ü©

instance final_comp [hF : Final F] [hG : Final G] : Final (F ‚ãô G) := by
  let s‚ÇÅ : C ‚âå AsSmall.{max u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ} C := AsSmall.equiv
  let s‚ÇÇ : D ‚âå AsSmall.{max u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ} D := AsSmall.equiv
  let s‚ÇÉ : E ‚âå AsSmall.{max u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ} E := AsSmall.equiv
  let i : s‚ÇÅ.inverse ‚ãô (F ‚ãô G) ‚ãô s‚ÇÉ.functor ‚âÖ
      (s‚ÇÅ.inverse ‚ãô F ‚ãô s‚ÇÇ.functor) ‚ãô (s‚ÇÇ.inverse ‚ãô G ‚ãô s‚ÇÉ.functor) :=
    isoWhiskerLeft (s‚ÇÅ.inverse ‚ãô F) (isoWhiskerRight s‚ÇÇ.unitIso (G ‚ãô s‚ÇÉ.functor))
  rw [final_iff_comp_equivalence (F ‚ãô G) s‚ÇÉ.functor, final_iff_equivalence_comp s‚ÇÅ.inverse,
    final_natIso_iff i, final_iff_isIso_colimit_pre]
  rw [final_iff_comp_equivalence F s‚ÇÇ.functor, final_iff_equivalence_comp s‚ÇÅ.inverse,
    final_iff_isIso_colimit_pre] at hF
  rw [final_iff_comp_equivalence G s‚ÇÉ.functor, final_iff_equivalence_comp s‚ÇÇ.inverse,
    final_iff_isIso_colimit_pre] at hG
  intro H
  rw [‚Üê colimit.pre_pre]
  infer_instance

instance initial_comp [Initial F] [Initial G] : Initial (F ‚ãô G) := by
  suffices Final (F ‚ãô G).op from initial_of_final_op _
  exact final_comp F.op G.op

theorem final_of_final_comp [hF : Final F] [hFG : Final (F ‚ãô G)] : Final G := by
  let s‚ÇÅ : C ‚âå AsSmall.{max u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ} C := AsSmall.equiv
  let s‚ÇÇ : D ‚âå AsSmall.{max u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ} D := AsSmall.equiv
  let s‚ÇÉ : E ‚âå AsSmall.{max u‚ÇÅ v‚ÇÅ u‚ÇÇ v‚ÇÇ u‚ÇÉ v‚ÇÉ} E := AsSmall.equiv
  let _i : s‚ÇÅ.inverse ‚ãô (F ‚ãô G) ‚ãô s‚ÇÉ.functor ‚âÖ
      (s‚ÇÅ.inverse ‚ãô F ‚ãô s‚ÇÇ.functor) ‚ãô (s‚ÇÇ.inverse ‚ãô G ‚ãô s‚ÇÉ.functor) :=
    isoWhiskerLeft (s‚ÇÅ.inverse ‚ãô F) (isoWhiskerRight s‚ÇÇ.unitIso (G ‚ãô s‚ÇÉ.functor))
  rw [final_iff_comp_equivalence G s‚ÇÉ.functor, final_iff_equivalence_comp s‚ÇÇ.inverse,
    final_iff_isIso_colimit_pre]
  rw [final_iff_comp_equivalence F s‚ÇÇ.functor, final_iff_equivalence_comp s‚ÇÅ.inverse,
    final_iff_isIso_colimit_pre] at hF
  rw [final_iff_comp_equivalence (F ‚ãô G) s‚ÇÉ.functor, final_iff_equivalence_comp s‚ÇÅ.inverse,
    final_natIso_iff _i, final_iff_isIso_colimit_pre] at hFG
  intro H
  replace hFG := hFG H
  rw [‚Üê colimit.pre_pre] at hFG
  exact IsIso.of_isIso_comp_left (colimit.pre _ (s‚ÇÅ.inverse ‚ãô F ‚ãô s‚ÇÇ.functor)) _

theorem initial_of_initial_comp [Initial F] [Initial (F ‚ãô G)] : Initial G := by
  suffices Final G.op from initial_of_final_op _
  have : Final (F.op ‚ãô G.op) := show Final (F ‚ãô G).op from inferInstance
  exact final_of_final_comp F.op G.op

/-- The hypotheses also imply that `F` is final, see `final_of_comp_full_faithful`. -/
theorem final_of_comp_full_faithful' [Full G] [Faithful G] [Final (F ‚ãô G)] : Final G :=
  have := final_of_comp_full_faithful F G
  final_of_final_comp F G

/-- The hypotheses also imply that `F` is initial, see `initial_of_comp_full_faithful`. -/
theorem initial_of_comp_full_faithful' [Full G] [Faithful G] [Initial (F ‚ãô G)] : Initial G :=
  have := initial_of_comp_full_faithful F G
  initial_of_initial_comp F G

theorem final_iff_comp_final_full_faithful [Final G] [Full G] [Faithful G] :
    Final F ‚Üî Final (F ‚ãô G) :=
  ‚ü®fun _ => final_comp _ _, fun _ => final_of_comp_full_faithful F G‚ü©

theorem initial_iff_comp_initial_full_faithful [Initial G] [Full G] [Faithful G] :
    Initial F ‚Üî Initial (F ‚ãô G) :=
  ‚ü®fun _ => initial_comp _ _, fun _ => initial_of_comp_full_faithful F G‚ü©

theorem final_iff_final_comp [Final F] : Final G ‚Üî Final (F ‚ãô G) :=
  ‚ü®fun _ => final_comp _ _, fun _ => final_of_final_comp F G‚ü©

theorem initial_iff_initial_comp [Initial F] : Initial G ‚Üî Initial (F ‚ãô G) :=
  ‚ü®fun _ => initial_comp _ _, fun _ => initial_of_initial_comp F G‚ü©

end

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {c : C}

lemma final_fromPUnit_of_isTerminal (hc : Limits.IsTerminal c) : (fromPUnit c).Final where
  out c' := by
    letI : Inhabited (StructuredArrow c' (fromPUnit c)) := ‚ü®.mk (Y := default) (hc.from c')‚ü©
    letI : Subsingleton (StructuredArrow c' (fromPUnit c)) :=
      ‚ü®fun i j ‚Ü¶ StructuredArrow.obj_ext _ _ (by aesop_cat) (hc.hom_ext _ _)‚ü©
    infer_instance

lemma initial_fromPUnit_of_isInitial (hc : Limits.IsInitial c) : (fromPUnit c).Initial where
  out c' := by
    letI : Inhabited (CostructuredArrow (fromPUnit c) c') := ‚ü®.mk (Y := default) (hc.to c')‚ü©
    letI : Subsingleton (CostructuredArrow (fromPUnit c) c') :=
      ‚ü®fun i j ‚Ü¶ CostructuredArrow.obj_ext _ _ (by aesop_cat) (hc.hom_ext _ _)‚ü©
    infer_instance

end

end Functor

section Filtered
open Functor

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]

/-- Final functors preserve filteredness.

This can be seen as a generalization of `IsFiltered.of_right_adjoint` (which states that right
adjoints preserve filteredness), as right adjoints are always final, see `final_of_adjunction`.
-/
theorem IsFilteredOrEmpty.of_final (F : C ‚•§ D) [Final F] [IsFilteredOrEmpty C] :
    IsFilteredOrEmpty D where
  cocone_objs X Y := ‚ü®F.obj (IsFiltered.max (Final.lift F X) (Final.lift F Y)),
    Final.homToLift F X ‚â´ F.map (IsFiltered.leftToMax _ _),
    ‚ü®Final.homToLift F Y ‚â´ F.map (IsFiltered.rightToMax _ _), trivial‚ü©‚ü©
  cocone_maps {X Y} f g := by
    let P : StructuredArrow X F ‚Üí Prop := fun h => ‚àÉ (Z : C) (q‚ÇÅ : h.right ‚ü∂ Z)
      (q‚ÇÇ : Final.lift F Y ‚ü∂ Z), h.hom ‚â´ F.map q‚ÇÅ = f ‚â´ Final.homToLift F Y ‚â´ F.map q‚ÇÇ
    rsuffices ‚ü®Z, q‚ÇÅ, q‚ÇÇ, h‚ü© : Nonempty (P (StructuredArrow.mk (g ‚â´ Final.homToLift F Y)))
    ¬∑ refine ‚ü®F.obj (IsFiltered.coeq q‚ÇÅ q‚ÇÇ),
        Final.homToLift F Y ‚â´ F.map (q‚ÇÅ ‚â´ IsFiltered.coeqHom q‚ÇÅ q‚ÇÇ), ?_‚ü©
      conv_lhs => rw [IsFiltered.coeq_condition]
      simp only [F.map_comp, ‚Üê reassoc_of% h, StructuredArrow.mk_hom_eq_self, Category.assoc]
    have h‚ÇÄ : P (StructuredArrow.mk (f ‚â´ Final.homToLift F Y)) := ‚ü®_, ùüô _, ùüô _, by simp‚ü©
    refine isPreconnected_induction P ?_ ?_ h‚ÇÄ _
    ¬∑ rintro U V h ‚ü®Z, q‚ÇÅ, q‚ÇÇ, hq‚ü©
      obtain ‚ü®W, q‚ÇÉ, q‚ÇÑ, hq'‚ü© := IsFiltered.span q‚ÇÅ h.right
      refine ‚ü®W, q‚ÇÑ, q‚ÇÇ ‚â´ q‚ÇÉ, ?_‚ü©
      rw [F.map_comp, ‚Üê reassoc_of% hq, ‚Üê F.map_comp, hq', F.map_comp, StructuredArrow.w_assoc]
    ¬∑ rintro U V h ‚ü®Z, q‚ÇÅ, q‚ÇÇ, hq‚ü©
      exact ‚ü®Z, h.right ‚â´ q‚ÇÅ, q‚ÇÇ, by simp only [F.map_comp, StructuredArrow.w_assoc, hq]‚ü©

/-- Final functors preserve filteredness.

This can be seen as a generalization of `IsFiltered.of_right_adjoint` (which states that right
adjoints preserve filteredness), as right adjoints are always final, see `final_of_adjunction`.
-/
theorem IsFiltered.of_final (F : C ‚•§ D) [Final F] [IsFiltered C] : IsFiltered D :=
{ IsFilteredOrEmpty.of_final F with
  nonempty := Nonempty.map F.obj IsFiltered.nonempty }

/-- Initial functors preserve cofilteredness.

This can be seen as a generalization of `IsCofiltered.of_left_adjoint` (which states that left
adjoints preserve cofilteredness), as right adjoints are always initial,
see `initial_of_adjunction`.
-/
theorem IsCofilteredOrEmpty.of_initial (F : C ‚•§ D) [Initial F] [IsCofilteredOrEmpty C] :
    IsCofilteredOrEmpty D :=
  have : IsFilteredOrEmpty D·µí·µñ := IsFilteredOrEmpty.of_final F.op
  isCofilteredOrEmpty_of_isFilteredOrEmpty_op _

/-- Initial functors preserve cofilteredness.

This can be seen as a generalization of `IsCofiltered.of_left_adjoint` (which states that left
adjoints preserve cofilteredness), as right adjoints are always initial,
see `initial_of_adjunction`.
-/
theorem IsCofiltered.of_initial (F : C ‚•§ D) [Initial F] [IsCofiltered C] : IsCofiltered D :=
  have : IsFiltered D·µí·µñ := IsFiltered.of_final F.op
  isCofiltered_of_isFiltered_op _

end Filtered

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]

open Functor

/-- The functor `StructuredArrow.pre X T S` is final if `T` is final. -/
instance StructuredArrow.final_pre (T : C ‚•§ D) [Final T] (S : D ‚•§ E) (X : E) :
    Final (pre X T S) := by
  refine ‚ü®fun f => ?_‚ü©
  rw [isConnected_iff_of_equivalence (StructuredArrow.preEquivalence T f)]
  exact Final.out f.right

/-- The functor `CostructuredArrow.pre X T S` is initial if `T` is initial. -/
instance CostructuredArrow.initial_pre (T : C ‚•§ D) [Initial T] (S : D ‚•§ E) (X : E) :
    Initial (CostructuredArrow.pre T S X) := by
  refine ‚ü®fun f => ?_‚ü©
  rw [isConnected_iff_of_equivalence (CostructuredArrow.preEquivalence T f)]
  exact Initial.out f.left

end

section Grothendieck

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable (F : D ‚•§ Cat) (G : C ‚•§ D)

open Functor

set_option backward.dsimp.proofs true in
/-- A prefunctor mapping structured arrows on `G` to structured arrows on `pre F G` with their
action on fibers being the identity. -/
def Grothendieck.structuredArrowToStructuredArrowPre (d : D) (f : F.obj d) :
    StructuredArrow d G ‚•§q StructuredArrow ‚ü®d, f‚ü© (pre F G) where
  obj := fun X => StructuredArrow.mk (Y := ‚ü®X.right, (F.map X.hom).obj f‚ü©)
    (Grothendieck.Hom.mk (by exact X.hom) (by dsimp; exact ùüô _))
  map := fun g => StructuredArrow.homMk
    (Grothendieck.Hom.mk (by exact g.right)
      (eqToHom (by dsimp; rw [‚Üê StructuredArrow.w g, map_comp, Cat.comp_obj])))
    (by simp only [StructuredArrow.mk_right]
        apply Grothendieck.ext <;> simp)

instance Grothendieck.final_pre [hG : Final G] : (Grothendieck.pre F G).Final := by
  constructor
  rintro ‚ü®d, f‚ü©
  let ‚ü®u, c, g‚ü© : Nonempty (StructuredArrow d G) := inferInstance
  letI :  Nonempty (StructuredArrow ‚ü®d, f‚ü© (pre F G)) :=
    ‚ü®u, ‚ü®c, (F.map g).obj f‚ü©, ‚ü®(by exact g), (by exact ùüô _)‚ü©‚ü©
  apply zigzag_isConnected
  rintro ‚ü®‚ü®‚ü®‚ü©‚ü©, ‚ü®bi, fi‚ü©, ‚ü®gbi, gfi‚ü©‚ü© ‚ü®‚ü®‚ü®‚ü©‚ü©, ‚ü®bj, fj‚ü©, ‚ü®gbj, gfj‚ü©‚ü©
  dsimp at fj fi gfi gbi gbj gfj
  apply Zigzag.trans (j‚ÇÇ := StructuredArrow.mk (Y := ‚ü®bi, ((F.map gbi).obj f)‚ü©)
      (Grothendieck.Hom.mk gbi (ùüô _)))
    (.of_zag (.inr ‚ü®StructuredArrow.homMk (Grothendieck.Hom.mk (by dsimp; exact ùüô _)
      (eqToHom (by simp) ‚â´ gfi)) (by apply Grothendieck.ext <;> simp)‚ü©))
  refine Zigzag.trans (j‚ÇÇ := StructuredArrow.mk (Y := ‚ü®bj, ((F.map gbj).obj f)‚ü©)
      (Grothendieck.Hom.mk gbj (ùüô _))) ?_
    (.of_zag (.inl ‚ü®StructuredArrow.homMk (Grothendieck.Hom.mk (by dsimp; exact ùüô _)
      (eqToHom (by simp) ‚â´ gfj)) (by apply Grothendieck.ext <;> simp)‚ü©))
  exact zigzag_prefunctor_obj_of_zigzag (Grothendieck.structuredArrowToStructuredArrowPre F G d f)
    (isPreconnected_zigzag (.mk gbi) (.mk gbj))

open Limits

/-- A natural transformation `Œ± : F ‚ü∂ G` between functors `F G : C ‚•§ Cat` which is is final on each
fiber `(Œ±.app X)` induces an equivalence of fiberwise colimits of `map Œ± ‚ãô H` and `H` for each
functor `H : Grothendieck G ‚•§ Type`. -/
def Grothendieck.fiberwiseColimitMapCompEquivalence {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
    {F G : C ‚•§ Cat.{v‚ÇÇ, u‚ÇÇ}} (Œ± : F ‚ü∂ G) [‚àÄ X, Final (Œ±.app X)] (H : Grothendieck G ‚•§ Type u‚ÇÇ) :
    fiberwiseColimit (map Œ± ‚ãô H) ‚âÖ fiberwiseColimit H :=
  NatIso.ofComponents
    (fun X =>
      HasColimit.isoOfNatIso ((Functor.associator _ _ _).symm ‚â™‚â´
        isoWhiskerRight (ŒπCompMap Œ± X) H ‚â™‚â´  Functor.associator _ _ _) ‚â™‚â´
      Final.colimitIso (Œ±.app X) (Œπ G X ‚ãô H))
    (fun f => colimit.hom_ext <| fun d => by
      simp only [map, Cat.comp_obj, comp_obj, Œπ_obj, fiberwiseColimit_obj, fiberwiseColimit_map,
        ŒπNatTrans, ŒπCompMap, Iso.trans_hom, Category.assoc, Œπ_colimMap_assoc, NatTrans.comp_app,
        whiskerRight_app, Functor.comp_map, Cat.eqToHom_app, map_id, Category.comp_id,
        associator_hom_app, colimit.Œπ_pre_assoc, HasColimit.isoOfNatIso_Œπ_hom_assoc, Iso.symm_hom,
        isoWhiskerRight_hom, associator_inv_app, NatIso.ofComponents_hom_app, Iso.refl_hom,
        Final.Œπ_colimitIso_hom, Category.id_comp, Final.Œπ_colimitIso_hom_assoc, colimit.Œπ_pre]
      have := Functor.congr_obj (Œ±.naturality f) d
      dsimp at this
      congr
      apply eqToHom_heq_id_dom)

/-- This is the small version of the more general lemma `Grothendieck.final_map` below. -/
private lemma Grothendieck.final_map_small {C : Type u‚ÇÅ} [SmallCategory C] {F G : C ‚•§ Cat.{u‚ÇÅ, u‚ÇÅ}}
    (Œ± : F ‚ü∂ G) [hŒ± : ‚àÄ X, Final (Œ±.app X)] : Final (map Œ±) := by
  rw [final_iff_isIso_colimit_pre]
  intro H
  let i := (colimitFiberwiseColimitIso _).symm ‚â™‚â´
    HasColimit.isoOfNatIso (fiberwiseColimitMapCompEquivalence Œ± H) ‚â™‚â´ colimitFiberwiseColimitIso _
  convert Iso.isIso_hom i
  apply colimit.hom_ext
  intro X
  simp [i, fiberwiseColimitMapCompEquivalence]

/-- The functor `Grothendieck.map Œ±` for a natural transformation `Œ± : F ‚ü∂ G`, with
`F G : C ‚•§ Cat`, is final if for each `X : C`, the functor `Œ±.app X` is final. -/
lemma Grothendieck.final_map {F G : C ‚•§ Cat.{v‚ÇÇ, u‚ÇÇ}} (Œ± : F ‚ü∂ G) [hŒ± : ‚àÄ X, Final (Œ±.app X)] :
    Final (map Œ±) := by
  let sC : C ‚âå AsSmall.{max u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ} C := AsSmall.equiv
  let F' : AsSmall C ‚•§ Cat := sC.inverse ‚ãô F ‚ãô Cat.asSmallFunctor.{max v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ}
  let G' : AsSmall C ‚•§ Cat := sC.inverse ‚ãô G ‚ãô Cat.asSmallFunctor.{max v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ}
  let Œ±' : F' ‚ü∂ G' := whiskerLeft _ (whiskerRight Œ± _)
  have : ‚àÄ X, Final (Œ±'.app X) := fun X =>
    inferInstanceAs (AsSmall.equiv.inverse ‚ãô _ ‚ãô AsSmall.equiv.functor).Final
  have hŒ±' : (map Œ±').Final := final_map_small _
  dsimp only [Œ±', ‚Üê Equivalence.symm_functor] at hŒ±'
  have i := mapWhiskerLeftIsoConjPreMap sC.symm (whiskerRight Œ± Cat.asSmallFunctor)
    ‚â™‚â´ isoWhiskerLeft _ (isoWhiskerRight (mapWhiskerRightAsSmallFunctor Œ±) _)
  have := final_of_natIso i
  rwa [‚Üê final_iff_equivalence_comp, ‚Üê final_iff_comp_equivalence,
    ‚Üê final_iff_equivalence_comp, ‚Üê final_iff_comp_equivalence] at this

end Grothendieck

section Prod

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {C' : Type u‚ÇÉ} [Category.{v‚ÇÉ} C']
variable {D' : Type u‚ÇÑ} [Category.{v‚ÇÑ} D']
variable (F : C ‚•§ D) (G : C' ‚•§ D')

instance [F.Final] [G.Final] : (F.prod G).Final where
  out := fun ‚ü®d, d'‚ü© => isConnected_of_equivalent (StructuredArrow.prodEquivalence d d' F G).symm

end Prod

end CategoryTheory
