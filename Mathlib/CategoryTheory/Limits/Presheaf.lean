/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Comma.Presheaf.Basic
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Limits.Over

/-!
# Colimit of representables

This file constructs an adjunction `Presheaf.yonedaAdjunction` between `(Cáµ’áµ– â¥¤ Type u)` and
`â„°` given a functor `A : C â¥¤ â„°`, where the right adjoint `restrictedYoneda`
sends `(E : â„°)` to `c â†¦ (A.obj c âŸ¶ E)`, and the left adjoint `(Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°`
is a pointwise left Kan extension of `A` along the Yoneda embedding, which
exists provided `â„°` has colimits)

We also show that every presheaf is a colimit of representables. This result
is also known as the density theorem, the co-Yoneda lemma and
the Ninja Yoneda lemma. Two formulations are given:
* `colimitOfRepresentable` uses the category of elements of a functor to types;
* `isColimitTautologicalCocone` uses the category of costructured arrows.

In the lemma `isLeftKanExtension_along_yoneda_iff`, we show that
if `L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°)` and `Î± : A âŸ¶ yoneda â‹™ L`, then
`Î±` makes `L` the left Kan extension of `L` along yoneda if and only if
`Î±` is an isomorphism (i.e. `L` extends `A`) and `L` preserves colimits.
`uniqueExtensionAlongYoneda` shows `yoneda.leftKanExtension A` is unique amongst
functors preserving colimits with this property, establishing the
presheaf category as the free cocompletion of a category.

Given a functor `F : C â¥¤ D`, we also show construct an
isomorphism `compYonedaIsoYonedaCompLan : F â‹™ yoneda â‰… yoneda â‹™ F.op.lan`, and
show that it makes `F.op.lan` a left Kan extension of `F â‹™ yoneda`.

## Tags
colimit, representable, presheaf, free cocompletion

## References
* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
* https://ncatlab.org/nlab/show/Yoneda+extension
-/

namespace CategoryTheory

open Category Limits

universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

namespace Presheaf

variable {C : Type uâ‚} [Category.{vâ‚} C]

variable {â„° : Type uâ‚‚} [Category.{vâ‚} â„°] (A : C â¥¤ â„°)

/--
The functor taking `(E : â„°) (c : Cáµ’áµ–)` to the homset `(A.obj C âŸ¶ E)`. It is shown in `L_adjunction`
that this functor has a left adjoint (provided `E` has colimits) given by taking colimits over
categories of elements.
In the case where `â„° = Cáµ’áµ– â¥¤ Type u` and `A = yoneda`, this functor is isomorphic to the identity.

Defined as in [MM92], Chapter I, Section 5, Theorem 2.
-/
@[simps!]
def restrictedYoneda : â„° â¥¤ Cáµ’áµ– â¥¤ Type vâ‚ :=
  yoneda â‹™ (Functor.whiskeringLeft _ _ (Type vâ‚)).obj (Functor.op A)

/-- Auxiliary definition for `restrictedYonedaHomEquiv`. -/
def restrictedYonedaHomEquiv' (P : Cáµ’áµ– â¥¤ Type vâ‚) (E : â„°) :
    (CostructuredArrow.proj yoneda P â‹™ A âŸ¶
      (Functor.const (CostructuredArrow yoneda P)).obj E) â‰ƒ
      (P âŸ¶ (restrictedYoneda A).obj E) where
  toFun f :=
    { app := fun _ x => f.app (CostructuredArrow.mk (yonedaEquiv.symm x))
      naturality := fun {Xâ‚ Xâ‚‚} Ï† => by
        ext x
        let Ïˆ : CostructuredArrow.mk (yonedaEquiv.symm (P.map Ï† x)) âŸ¶
          CostructuredArrow.mk (yonedaEquiv.symm x) := CostructuredArrow.homMk Ï†.unop (by
            dsimp [yonedaEquiv]
            cat_disch )
        simpa using (f.naturality Ïˆ).symm }
  invFun g :=
    { app := fun y => yonedaEquiv (y.hom â‰« g)
      naturality := fun {Xâ‚ Xâ‚‚} Ï† => by
        dsimp
        rw [â† CostructuredArrow.w Ï†]
        dsimp [yonedaEquiv]
        simp only [comp_id, id_comp]
        refine (congr_fun (g.naturality Ï†.left.op) (Xâ‚‚.hom.app (Opposite.op Xâ‚‚.left)
          (ðŸ™ _))).symm.trans ?_
        dsimp
        apply congr_arg
        simpa using congr_fun (Xâ‚‚.hom.naturality Ï†.left.op).symm (ðŸ™ _) }
  left_inv f := by
    ext âŸ¨X, âŸ¨âŸ¨âŸ©âŸ©, Ï†âŸ©
    suffices yonedaEquiv.symm (Ï†.app (Opposite.op X) (ðŸ™ X)) = Ï† by
      dsimp
      erw [yonedaEquiv_apply]
      dsimp [CostructuredArrow.mk]
      erw [this]
    exact yonedaEquiv.injective (by cat_disch)
  right_inv g := by
    ext X x
    dsimp
    erw [yonedaEquiv_apply]
    dsimp
    rw [yonedaEquiv_symm_app_apply]
    simp

section

example [HasColimitsOfSize.{vâ‚, max uâ‚ vâ‚} â„°] :
    yoneda.HasPointwiseLeftKanExtension A := inferInstance

variable [yoneda.HasPointwiseLeftKanExtension A]

variable {A}
variable (L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°) (Î± : A âŸ¶ yoneda â‹™ L) [L.IsLeftKanExtension Î±]

/-- Auxiliary definition for `yonedaAdjunction`. -/
noncomputable def restrictedYonedaHomEquiv (P : Cáµ’áµ– â¥¤ Type vâ‚) (E : â„°) :
    (L.obj P âŸ¶ E) â‰ƒ (P âŸ¶ (restrictedYoneda A).obj E) :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension _ Î± P).homEquiv.trans
    (restrictedYonedaHomEquiv' A P E)

/-- If `L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°` is a pointwise left Kan extension
of a functor `A : C â¥¤ â„°` along the Yoneda embedding,
then `L` is a left adjoint of `restrictedYoneda A : â„° â¥¤ Cáµ’áµ– â¥¤ Type vâ‚` -/
noncomputable def yonedaAdjunction : L âŠ£ restrictedYoneda A :=
  Adjunction.mkOfHomEquiv
    { homEquiv := restrictedYonedaHomEquiv L Î±
      homEquiv_naturality_left_symm := fun {P Q X} f g => by
        obtain âŸ¨g, rflâŸ© := (restrictedYonedaHomEquiv L Î± Q X).surjective g
        apply (restrictedYonedaHomEquiv L Î± P X).injective
        simp only [Equiv.apply_symm_apply, Equiv.symm_apply_apply]
        ext Y y
        dsimp [restrictedYonedaHomEquiv, restrictedYonedaHomEquiv', IsColimit.homEquiv]
        rw [assoc, assoc, â† L.map_comp_assoc]
        congr 3
        apply yonedaEquiv.injective
        simp [yonedaEquiv]
      homEquiv_naturality_right := fun {P X Y} f g => by
        apply (restrictedYonedaHomEquiv L Î± P Y).symm.injective
        simp only [Equiv.symm_apply_apply]
        dsimp [restrictedYonedaHomEquiv, restrictedYonedaHomEquiv', IsColimit.homEquiv]
        apply (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Î± P).hom_ext
        intro p
        rw [IsColimit.fac]
        dsimp [restrictedYoneda, yonedaEquiv]
        simp only [assoc]
        congr 3
        apply yonedaEquiv.injective
        simp [yonedaEquiv] }

include Î± in
/-- Any left Kan extension along the Yoneda embedding preserves colimits. -/
lemma preservesColimitsOfSize_of_isLeftKanExtension :
    PreservesColimitsOfSize.{vâ‚ƒ, uâ‚ƒ} L :=
  (yonedaAdjunction L Î±).leftAdjoint_preservesColimits

lemma isIso_of_isLeftKanExtension : IsIso Î± :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension _ Î±).isIso_hom

variable (A)

/-- See Property 2 of https://ncatlab.org/nlab/show/Yoneda+extension#properties. -/
noncomputable instance preservesColimitsOfSize_leftKanExtension :
    PreservesColimitsOfSize.{vâ‚ƒ, uâ‚ƒ} (yoneda.leftKanExtension A) :=
  (yonedaAdjunction _ (yoneda.leftKanExtensionUnit A)).leftAdjoint_preservesColimits

instance : IsIso (yoneda.leftKanExtensionUnit A) :=
  isIso_of_isLeftKanExtension _ (yoneda.leftKanExtensionUnit A)

/-- A pointwise left Kan extension along the Yoneda embedding is an extension. -/
noncomputable def isExtensionAlongYoneda :
    yoneda â‹™ yoneda.leftKanExtension A â‰… A :=
  (asIso (yoneda.leftKanExtensionUnit A)).symm

end

/-- A functor to the presheaf category in which everything in the image is representable (witnessed
by the fact that it factors through the yoneda embedding).
`coconeOfRepresentable` gives a cocone for this functor which is a colimit and has point `P`.
-/
@[reducible]
def functorToRepresentables (P : Cáµ’áµ– â¥¤ Type vâ‚) : P.Elementsáµ’áµ– â¥¤ Cáµ’áµ– â¥¤ Type vâ‚ :=
  (CategoryOfElements.Ï€ P).leftOp â‹™ yoneda

/-- This is a cocone with point `P` for the functor `functorToRepresentables P`. It is shown in
`colimitOfRepresentable P` that this cocone is a colimit: that is, we have exhibited an arbitrary
presheaf `P` as a colimit of representables.

The construction of [MM92], Chapter I, Section 5, Corollary 3.
-/
@[simps]
noncomputable def coconeOfRepresentable (P : Cáµ’áµ– â¥¤ Type vâ‚) :
    Cocone (functorToRepresentables P) where
  pt := P
  Î¹ :=
    { app := fun x => yonedaEquiv.symm x.unop.2
      naturality := fun {xâ‚ xâ‚‚} f => by
        dsimp
        rw [comp_id, â† yonedaEquiv_symm_map]
        congr 1
        rw [f.unop.2] }

/-- The legs of the cocone `coconeOfRepresentable` are natural in the choice of presheaf. -/
theorem coconeOfRepresentable_naturality {Pâ‚ Pâ‚‚ : Cáµ’áµ– â¥¤ Type vâ‚} (Î± : Pâ‚ âŸ¶ Pâ‚‚) (j : Pâ‚.Elementsáµ’áµ–) :
    (coconeOfRepresentable Pâ‚).Î¹.app j â‰« Î± =
      (coconeOfRepresentable Pâ‚‚).Î¹.app ((CategoryOfElements.map Î±).op.obj j) := by
  ext T f
  simpa [coconeOfRepresentable_Î¹_app] using FunctorToTypes.naturality _ _ Î± f.op _

/-- The cocone with point `P` given by `coconeOfRepresentable` is a colimit:
that is, we have exhibited an arbitrary presheaf `P` as a colimit of representables.

The result of [MM92], Chapter I, Section 5, Corollary 3.
-/
noncomputable def colimitOfRepresentable (P : Cáµ’áµ– â¥¤ Type vâ‚) :
    IsColimit (coconeOfRepresentable P) where
  desc s :=
    { app := fun X x => (s.Î¹.app (Opposite.op (Functor.elementsMk P X x))).app X (ðŸ™ _)
      naturality := fun X Y f => by
        ext x
        have eqâ‚ := congr_fun (congr_app (s.w (CategoryOfElements.homMk (P.elementsMk X x)
          (P.elementsMk Y (P.map f x)) f rfl).op) Y) (ðŸ™ _)
        dsimp at eqâ‚ âŠ¢
        simpa [â† eqâ‚, id_comp] using
          congr_fun ((s.Î¹.app (Opposite.op (P.elementsMk X x))).naturality f) (ðŸ™ _) }
  fac s j := by
    ext X x
    let Ï† : j.unop âŸ¶ Functor.elementsMk P X ((yonedaEquiv.symm j.unop.2).app X x) := âŸ¨x.op, rflâŸ©
    simpa using congr_fun (congr_app (s.Î¹.naturality Ï†.op).symm X) (ðŸ™ _)
  uniq s m hm := by
    ext X x
    dsimp
    rw [â† hm]
    apply congr_arg
    simp [coconeOfRepresentable_Î¹_app, yonedaEquiv]

variable {A : C â¥¤ â„°}

example [HasColimitsOfSize.{vâ‚, max uâ‚ vâ‚} â„°] :
    yoneda.HasPointwiseLeftKanExtension A :=
  inferInstance

variable [yoneda.HasPointwiseLeftKanExtension A]

section

variable (L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°) (Î± : A âŸ¶ yoneda â‹™ L)

instance [L.IsLeftKanExtension Î±] : IsIso Î± :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Î±).isIso_hom

lemma isLeftKanExtension_along_yoneda_iff :
    L.IsLeftKanExtension Î± â†”
      (IsIso Î± âˆ§ PreservesColimitsOfSize.{vâ‚, max uâ‚ vâ‚} L) := by
  constructor
  Â· intro
    exact âŸ¨inferInstance, preservesColimits_of_natIso
      (Functor.leftKanExtensionUnique _ (yoneda.leftKanExtensionUnit A) _ Î±)âŸ©
  Â· rintro âŸ¨_, _âŸ©
    apply Functor.LeftExtension.IsPointwiseLeftKanExtension.isLeftKanExtension
      (E := Functor.LeftExtension.mk _ Î±)
    intro P
    dsimp [Functor.LeftExtension.IsPointwiseLeftKanExtensionAt]
    apply IsColimit.ofWhiskerEquivalence (CategoryOfElements.costructuredArrowYonedaEquivalence _)
    let e : CategoryOfElements.toCostructuredArrow P â‹™ CostructuredArrow.proj yoneda P â‹™ A â‰…
        functorToRepresentables P â‹™ L :=
      Functor.isoWhiskerLeft _ (Functor.isoWhiskerLeft _ (asIso Î±)) â‰ªâ‰«
        Functor.isoWhiskerLeft _ (Functor.associator _ _ _).symm â‰ªâ‰«
        (Functor.associator _ _ _).symm â‰ªâ‰« Functor.isoWhiskerRight (Iso.refl _) L
    apply (IsColimit.precomposeHomEquiv e.symm _).1
    exact IsColimit.ofIsoColimit (isColimitOfPreserves L (colimitOfRepresentable P))
      (Cocones.ext (Iso.refl _))

lemma isLeftKanExtension_of_preservesColimits
    (L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°) (e : A â‰… yoneda â‹™ L)
    [PreservesColimitsOfSize.{vâ‚, max uâ‚ vâ‚} L] :
    L.IsLeftKanExtension e.hom := by
  rw [isLeftKanExtension_along_yoneda_iff]
  exact âŸ¨inferInstance, âŸ¨inferInstanceâŸ©âŸ©

end

/-- Show that `yoneda.leftKanExtension A` is the unique colimit-preserving
functor which extends `A` to the presheaf category.

The second part of [MM92], Chapter I, Section 5, Corollary 4.
See Property 3 of https://ncatlab.org/nlab/show/Yoneda+extension#properties.
-/
noncomputable def uniqueExtensionAlongYoneda (L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°) (e : A â‰… yoneda â‹™ L)
    [PreservesColimitsOfSize.{vâ‚, max uâ‚ vâ‚} L] : L â‰… yoneda.leftKanExtension A :=
  have := isLeftKanExtension_of_preservesColimits L e
  Functor.leftKanExtensionUnique _ e.hom _ (yoneda.leftKanExtensionUnit A)

instance (L : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ â„°) [PreservesColimitsOfSize.{vâ‚, max uâ‚ vâ‚} L]
    [yoneda.HasPointwiseLeftKanExtension (yoneda â‹™ L)] :
    L.IsLeftKanExtension (ðŸ™ _ : yoneda â‹™ L âŸ¶ _) :=
  isLeftKanExtension_of_preservesColimits _ (Iso.refl _)

/-- If `L` preserves colimits and `â„°` has them, then it is a left adjoint. Note this is a (partial)
converse to `leftAdjointPreservesColimits`.
-/
lemma isLeftAdjoint_of_preservesColimits (L : (C â¥¤ Type vâ‚) â¥¤ â„°)
    [PreservesColimitsOfSize.{vâ‚, max uâ‚ vâ‚} L]
    [yoneda.HasPointwiseLeftKanExtension
      (yoneda â‹™ (opOpEquivalence C).congrLeft.functor.comp L)] :
    L.IsLeftAdjoint :=
  âŸ¨_, âŸ¨((opOpEquivalence C).congrLeft.symm.toAdjunction.comp
    (yonedaAdjunction _ (ðŸ™ _))).ofNatIsoLeft ((opOpEquivalence C).congrLeft.invFunIdAssoc L)âŸ©âŸ©

section

variable {D : Type uâ‚‚} [Category.{vâ‚} D] (F : C â¥¤ D)

section

instance (X : C) (Y : F.op.LeftExtension (yoneda.obj X)) :
    Unique (Functor.LeftExtension.mk _ (yonedaMap F X) âŸ¶ Y) where
  default := StructuredArrow.homMk
      (yonedaEquiv.symm (yonedaEquiv (F := F.op.comp Y.right) Y.hom)) (by
        ext Z f
        simpa using congr_fun (Y.hom.naturality f.op).symm (ðŸ™ _))
  uniq Ï† := by
    ext1
    apply yonedaEquiv.injective
    dsimp
    simp only [Equiv.apply_symm_apply, â† StructuredArrow.w Ï†]
    dsimp [yonedaEquiv]
    simp only [Functor.map_id]

/-- Given `F : C â¥¤ D` and `X : C`, `yoneda.obj (F.obj X) : Dáµ’áµ– â¥¤ Type _` is the
left Kan extension of `yoneda.obj X : Cáµ’áµ– â¥¤ Type _` along `F.op`. -/
instance (X : C) : (yoneda.obj (F.obj X)).IsLeftKanExtension (yonedaMap F X) :=
  âŸ¨âŸ¨Limits.IsInitial.ofUnique _âŸ©âŸ©

end

section
variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type vâ‚), F.op.HasLeftKanExtension P]

/-- `F â‹™ yoneda` is naturally isomorphic to `yoneda â‹™ F.op.lan`. -/
noncomputable def compYonedaIsoYonedaCompLan :
    F â‹™ yoneda â‰… yoneda â‹™ F.op.lan :=
  NatIso.ofComponents (fun X => Functor.leftKanExtensionUnique _
    (yonedaMap F X) (F.op.lan.obj _) (F.op.lanUnit.app (yoneda.obj X))) (fun {X Y} f => by
      apply yonedaEquiv.injective
      have eqâ‚ := congr_fun ((yoneda.obj (F.obj Y)).descOfIsLeftKanExtension_fac_app
        (yonedaMap F Y) (F.op.lan.obj (yoneda.obj Y)) (F.op.lanUnit.app (yoneda.obj Y)) _) f
      have eqâ‚‚ := congr_fun (((yoneda.obj (F.obj X)).descOfIsLeftKanExtension_fac_app
        (yonedaMap F X) (F.op.lan.obj (yoneda.obj X)) (F.op.lanUnit.app (yoneda.obj X))) _) (ðŸ™ _)
      have eqâ‚ƒ := congr_fun (congr_app (F.op.lanUnit.naturality (yoneda.map f)) _) (ðŸ™ _)
      dsimp at eqâ‚ eqâ‚‚ eqâ‚ƒ
      simp only [Functor.map_id] at eqâ‚‚
      simp only [id_comp] at eqâ‚ƒ
      simp [yonedaEquiv, eqâ‚, eqâ‚‚, eqâ‚ƒ])

@[simp]
lemma compYonedaIsoYonedaCompLan_inv_app_app_apply_eq_id (X : C) :
    ((compYonedaIsoYonedaCompLan F).inv.app X).app (Opposite.op (F.obj X))
      ((F.op.lanUnit.app (yoneda.obj X)).app _ (ðŸ™ X)) = ðŸ™ _ :=
  (congr_fun (Functor.descOfIsLeftKanExtension_fac_app _
    (F.op.lanUnit.app (yoneda.obj X)) _ (yonedaMap F X) (Opposite.op X)) (ðŸ™ _)).trans (by simp)

end

namespace compYonedaIsoYonedaCompLan

variable {F}

section

variable {X : C} {G : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ Dáµ’áµ– â¥¤ Type vâ‚} (Ï† : F â‹™ yoneda âŸ¶ yoneda â‹™ G)

/-- Auxiliary definition for `presheafHom`. -/
def coconeApp {P : Cáµ’áµ– â¥¤ Type vâ‚} (x : P.Elements) :
    yoneda.obj x.1.unop âŸ¶ F.op â‹™ G.obj P := yonedaEquiv.symm
      ((G.map (yonedaEquiv.symm x.2)).app _ ((Ï†.app x.1.unop).app _ (ðŸ™ _)))

@[reassoc (attr := simp)]
lemma coconeApp_naturality {P : Cáµ’áµ– â¥¤ Type vâ‚} {x y : P.Elements} (f : x âŸ¶ y) :
    yoneda.map f.1.unop â‰« coconeApp Ï† x = coconeApp Ï† y := by
  have eqâ‚ : yoneda.map f.1.unop â‰« yonedaEquiv.symm x.2 = yonedaEquiv.symm y.2 :=
    yonedaEquiv.injective
      (by simpa only [Equiv.apply_symm_apply, â† yonedaEquiv_naturality] using f.2)
  have eqâ‚‚ := congr_fun ((G.map (yonedaEquiv.symm x.2)).naturality (F.map f.1.unop).op)
    ((Ï†.app x.1.unop).app _ (ðŸ™ _))
  have eqâ‚ƒ := congr_fun (congr_app (Ï†.naturality f.1.unop) _) (ðŸ™ _)
  have eqâ‚„ := congr_fun ((Ï†.app x.1.unop).naturality (F.map f.1.unop).op)
  dsimp at eqâ‚‚ eqâ‚ƒ eqâ‚„
  apply yonedaEquiv.injective
  dsimp only [coconeApp]
  rw [Equiv.apply_symm_apply, â† yonedaEquiv_naturality, Equiv.apply_symm_apply]
  simp [â† eqâ‚, â† eqâ‚‚, â† eqâ‚ƒ, â† eqâ‚„, Functor.map_comp, FunctorToTypes.comp, id_comp, comp_id]

/-- Given functors `F : C â¥¤ D` and `G : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ (Dáµ’áµ– â¥¤ Type vâ‚)`, and
a natural transformation `Ï† : F â‹™ yoneda âŸ¶ yoneda â‹™ G`, this is the
(natural) morphism `P âŸ¶ F.op â‹™ G.obj P` for all `P : Cáµ’áµ– â¥¤ Type vâ‚` that is
determined by `Ï†`. -/
noncomputable def presheafHom (P : Cáµ’áµ– â¥¤ Type vâ‚) : P âŸ¶ F.op â‹™ G.obj P :=
  (colimitOfRepresentable P).desc
    (Cocone.mk _ { app := fun x => coconeApp Ï† x.unop })

lemma yonedaEquiv_Î¹_presheafHom (P : Cáµ’áµ– â¥¤ Type vâ‚) {X : C} (f : yoneda.obj X âŸ¶ P) :
    yonedaEquiv (f â‰« presheafHom Ï† P) =
      (G.map f).app (Opposite.op (F.obj X)) ((Ï†.app X).app _ (ðŸ™ _)) := by
  obtain âŸ¨x, rflâŸ© := yonedaEquiv.symm.surjective f
  erw [(colimitOfRepresentable P).fac _ (Opposite.op (P.elementsMk _ x))]
  dsimp only [coconeApp]
  apply Equiv.apply_symm_apply

lemma yonedaEquiv_presheafHom_yoneda_obj (X : C) :
    yonedaEquiv (presheafHom Ï† (yoneda.obj X)) =
      ((Ï†.app X).app (F.op.obj (Opposite.op X)) (ðŸ™ _)) := by
  simpa using yonedaEquiv_Î¹_presheafHom Ï† (yoneda.obj X) (ðŸ™ _)

@[reassoc (attr := simp)]
lemma presheafHom_naturality {P Q : Cáµ’áµ– â¥¤ Type vâ‚} (f : P âŸ¶ Q) :
    presheafHom Ï† P â‰« Functor.whiskerLeft F.op (G.map f) = f â‰« presheafHom Ï† Q :=
  hom_ext_yoneda (fun X p => yonedaEquiv.injective (by
    rw [â† assoc p f, yonedaEquiv_Î¹_presheafHom, â† assoc,
      yonedaEquiv_comp, yonedaEquiv_Î¹_presheafHom,
      Functor.whiskerLeft_app, Functor.map_comp, FunctorToTypes.comp]
    dsimp))

variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type vâ‚), F.op.HasLeftKanExtension P]

/-- Given functors `F : C â¥¤ D` and `G : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ (Dáµ’áµ– â¥¤ Type vâ‚)`,
and a natural transformation `Ï† : F â‹™ yoneda âŸ¶ yoneda â‹™ G`, this is
the canonical natural transformation `F.op.lan âŸ¶ G`, which is part of the
that `F.op.lan : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` is the left Kan extension
of `F â‹™ yoneda : C â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` along `yoneda : C â¥¤ Cáµ’áµ– â¥¤ Type vâ‚`. -/
noncomputable def natTrans : F.op.lan âŸ¶ G where
  app P := (F.op.lan.obj P).descOfIsLeftKanExtension (F.op.lanUnit.app P) _ (presheafHom Ï† P)
  naturality {P Q} f := by
    apply (F.op.lan.obj P).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app P)
    have eq := F.op.lanUnit.naturality f
    dsimp at eq âŠ¢
    rw [Functor.descOfIsLeftKanExtension_fac_assoc, â† reassoc_of% eq,
      Functor.descOfIsLeftKanExtension_fac, presheafHom_naturality]

lemma natTrans_app_yoneda_obj (X : C) : (natTrans Ï†).app (yoneda.obj X) =
    (compYonedaIsoYonedaCompLan F).inv.app X â‰« Ï†.app X := by
  dsimp [natTrans]
  apply (F.op.lan.obj (yoneda.obj X)).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app _)
  rw [Functor.descOfIsLeftKanExtension_fac]
  apply yonedaEquiv.injective
  rw [yonedaEquiv_presheafHom_yoneda_obj]
  exact congr_arg _ (compYonedaIsoYonedaCompLan_inv_app_app_apply_eq_id F X).symm

end

variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type vâ‚), F.op.HasLeftKanExtension P]

/-- Given a functor `F : C â¥¤ D`, this definition is part of the verification that
`Functor.LeftExtension.mk F.op.lan (compYonedaIsoYonedaCompLan F).hom`
is universal, i.e. that  `F.op.lan : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` is the
left Kan extension of `F â‹™ yoneda : C â¥¤ Dáµ’áµ– â¥¤ Type vâ‚`
along `yoneda : C â¥¤ Cáµ’áµ– â¥¤ Type vâ‚`. -/
noncomputable def extensionHom (Î¦ : yoneda.LeftExtension (F â‹™ yoneda)) :
    Functor.LeftExtension.mk F.op.lan (compYonedaIsoYonedaCompLan F).hom âŸ¶ Î¦ :=
  StructuredArrow.homMk (natTrans Î¦.hom) (by
    ext X : 2
    dsimp
    rw [natTrans_app_yoneda_obj, Iso.hom_inv_id_app_assoc])

@[ext]
lemma hom_ext {Î¦ : yoneda.LeftExtension (F â‹™ yoneda)}
    (f g : Functor.LeftExtension.mk F.op.lan (compYonedaIsoYonedaCompLan F).hom âŸ¶ Î¦) :
    f = g := by
  ext P : 3
  apply (F.op.lan.obj P).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app P)
  apply (colimitOfRepresentable P).hom_ext
  intro x
  have eq := F.op.lanUnit.naturality (yonedaEquiv.symm x.unop.2)
  have eqâ‚ := congr_fun (congr_app (congr_app (StructuredArrow.w f) x.unop.1.unop)
    (F.op.obj x.unop.1)) (ðŸ™ _)
  have eqâ‚‚ := congr_fun (congr_app (congr_app (StructuredArrow.w g) x.unop.1.unop)
    (F.op.obj x.unop.1)) (ðŸ™ _)
  dsimp at eqâ‚ eqâ‚‚ eq âŠ¢
  simp only [reassoc_of% eq, â† Functor.whiskerLeft_comp]
  congr 2
  simp only [â† cancel_epi ((compYonedaIsoYonedaCompLan F).hom.app x.unop.1.unop),
    NatTrans.naturality]
  apply yonedaEquiv.injective
  dsimp [yonedaEquiv_apply]
  rw [eqâ‚, eqâ‚‚]

end compYonedaIsoYonedaCompLan

variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type vâ‚), F.op.HasLeftKanExtension P]

noncomputable instance (Î¦ : StructuredArrow (F â‹™ yoneda)
    ((Functor.whiskeringLeft C (Cáµ’áµ– â¥¤ Type vâ‚) (Dáµ’áµ– â¥¤ Type vâ‚)).obj yoneda)) :
    Unique (Functor.LeftExtension.mk F.op.lan (compYonedaIsoYonedaCompLan F).hom âŸ¶ Î¦) where
  default := compYonedaIsoYonedaCompLan.extensionHom Î¦
  uniq _ := compYonedaIsoYonedaCompLan.hom_ext _ _

/-- Given a functor `F : C â¥¤ D`, `F.op.lan : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` is the
left Kan extension of `F â‹™ yoneda : C â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` along `yoneda : C â¥¤ Cáµ’áµ– â¥¤ Type vâ‚`. -/
instance : F.op.lan.IsLeftKanExtension (compYonedaIsoYonedaCompLan F).hom :=
  âŸ¨âŸ¨Limits.IsInitial.ofUnique _âŸ©âŸ©

end

section

variable {C : Type uâ‚} [Category.{vâ‚} C] (P : Cáµ’áµ– â¥¤ Type vâ‚)

/-- For a presheaf `P`, consider the forgetful functor from the category of representable
presheaves over `P` to the category of presheaves. There is a tautological cocone over this
functor whose leg for a natural transformation `V âŸ¶ P` with `V` representable is just that
natural transformation. -/
@[simps]
def tautologicalCocone : Cocone (CostructuredArrow.proj yoneda P â‹™ yoneda) where
  pt := P
  Î¹ := { app := fun X => X.hom }

/-- The tautological cocone with point `P` is a colimit cocone, exhibiting `P` as a colimit of
representables.

Proposition 2.6.3(i) in [Kashiwara2006] -/
def isColimitTautologicalCocone : IsColimit (tautologicalCocone P) where
  desc := fun s => by
    refine âŸ¨fun X t => yonedaEquiv (s.Î¹.app (CostructuredArrow.mk (yonedaEquiv.symm t))), ?_âŸ©
    intros X Y f
    ext t
    dsimp
    rw [yonedaEquiv_naturality', yonedaEquiv_symm_map]
    simpa using (s.Î¹.naturality
      (CostructuredArrow.homMk' (CostructuredArrow.mk (yonedaEquiv.symm t)) f.unop)).symm
  fac := by
    intro s t
    dsimp
    apply yonedaEquiv.injective
    rw [yonedaEquiv_comp]
    dsimp only
    rw [Equiv.symm_apply_apply]
    rfl
  uniq := by
    intro s j h
    ext V x
    obtain âŸ¨t, rflâŸ© := yonedaEquiv.surjective x
    dsimp
    rw [Equiv.symm_apply_apply, â† yonedaEquiv_comp]
    exact congr_arg _ (h (CostructuredArrow.mk t))

variable {I : Type vâ‚} [SmallCategory I] (F : I â¥¤ C)

/-- Given a functor `F : I â¥¤ C`, a cocone `c` on `F â‹™ yoneda : I â¥¤ Cáµ’áµ– â¥¤ Type vâ‚` induces a
functor `I â¥¤ CostructuredArrow yoneda c.pt` which maps `i : I` to the leg
`yoneda.obj (F.obj i) âŸ¶ c.pt`. If `c` is a colimit cocone, then that functor is
final.

Proposition 2.6.3(ii) in [Kashiwara2006] -/
theorem final_toCostructuredArrow_comp_pre {c : Cocone (F â‹™ yoneda)} (hc : IsColimit c) :
    Functor.Final (c.toCostructuredArrow â‹™ CostructuredArrow.pre F yoneda c.pt) := by
  apply Functor.final_of_isTerminal_colimit_comp_yoneda
  suffices IsTerminal (colimit ((c.toCostructuredArrow â‹™ CostructuredArrow.pre F yoneda c.pt) â‹™
      CostructuredArrow.toOver yoneda c.pt)) by
    apply IsTerminal.isTerminalOfObj (overEquivPresheafCostructuredArrow c.pt).inverse
    apply IsTerminal.ofIso this
    refine ?_ â‰ªâ‰« (preservesColimitIso (overEquivPresheafCostructuredArrow c.pt).inverse _).symm
    apply HasColimit.isoOfNatIso
    exact Functor.isoWhiskerLeft _
      (CostructuredArrow.toOverCompOverEquivPresheafCostructuredArrow c.pt).isoCompInverse
  apply IsTerminal.ofIso Over.mkIdTerminal
  let isc : IsColimit ((Over.forget _).mapCocone _) := isColimitOfPreserves _
    (colimit.isColimit ((c.toCostructuredArrow â‹™ CostructuredArrow.pre F yoneda c.pt) â‹™
      CostructuredArrow.toOver yoneda c.pt))
  exact Over.isoMk (hc.coconePointUniqueUpToIso isc) (hc.hom_ext fun i => by simp)

end

end Presheaf

end CategoryTheory
