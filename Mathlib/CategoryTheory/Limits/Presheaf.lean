/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Comma.Presheaf.Basic
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Limits.Over

/-!
# Colimit of representables

In this file, We show that every presheaf of types on a category `C` (with `Category.{vâ‚} C`)
is a colimit of representables. This result is also known as the density theorem,
the co-Yoneda lemma and the Ninja Yoneda lemma. Three formulations are given:
* `colimitOfRepresentable` uses the category of elements of a functor to types;
* `isColimitTautologicalCocone` uses the category of costructured arrows
for `yoneda : C â¥¤ Cáµ’áµ– â¥¤ Type vâ‚`;
* `isColimitTautologicalCocone'` uses the category of costructured arrows
for `uliftYoneda : C â¥¤ Cáµ’áµ– â¥¤ Type max w vâ‚`, when the presheaf has values
in `Type (max w vâ‚)`;

In this file, we also study the left Kan extensions of functors `A : C â¥¤ â„°`
along the Yoneda embedding `uliftYoneda : C â¥¤ Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`
(when `Category.{vâ‚‚} â„°` and `w` is an auxiliary universe). In particular,
the definition `uliftYonedaAdjunction` shows that such a pointwise left Kan
extension  (which exists when `â„°` has colimits) is a left adjoint to the
functor `restrictedULiftYoneda : â„° â¥¤ Cáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)`.

In the lemma `isLeftKanExtension_along_uliftYoneda_iff`, we show that
if `L : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°` and `Î± : A âŸ¶ uliftYoneda â‹™ L`, then
`Î±` makes `L` the left Kan extension of `L` along yoneda if and only if
`Î±` is an isomorphism (i.e. `L` extends `A`) and `L` preserves colimits.
`uniqueExtensionAlongULiftYoneda` shows `uliftYoneda.leftKanExtension A` is
unique amongst functors preserving colimits with this property, establishing the
presheaf category as the free cocompletion of a category.

Given a functor `F : C â¥¤ D`, we also show construct an isomorphism
`compULiftYonedaIsoULiftYonedaCompLan : F â‹™ uliftYoneda â‰… uliftYoneda â‹™ F.op.lan`, and
show that it makes `F.op.lan` a left Kan extension of `F â‹™ uliftYoneda`.

## Tags
colimit, representable, presheaf, free cocompletion

## References
* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
* https://ncatlab.org/nlab/show/Yoneda+extension
-/

namespace CategoryTheory

open Category Limits Opposite

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {C : Type uâ‚} [Category.{vâ‚} C]

namespace Presheaf

variable {â„° : Type uâ‚‚} [Category.{vâ‚‚} â„°] (A : C â¥¤ â„°)

/--
Given a functor `A : C â¥¤ â„°` (with `Category.{vâ‚‚} â„°`) and a auxiliary universe `w`,
this is the functor `â„° â¥¤ Cáµ’áµ– â¥¤ Type (max w vâ‚‚)` which sends `(E : â„°) (c : Cáµ’áµ–)`
to the homset `A.obj C âŸ¶ E` (considered in the higher universe `max w vâ‚‚`).
Under the existence of a suitable pointwise left Kan extension, it is shown in
`uliftYonedaAdjunction` that this functor has a left adjoint.

Defined as in [MM92], Chapter I, Section 5, Theorem 2.
-/
@[simps! obj_map map_app]
def restrictedULiftYoneda : â„° â¥¤ Cáµ’áµ– â¥¤ Type (max w vâ‚‚) :=
    uliftYoneda.{w} â‹™ (Functor.whiskeringLeft _ _ _).obj A.op

@[reassoc]
lemma map_comp_uliftYonedaEquiv_down (E : â„°) {X Y : C} (f : X âŸ¶ Y)
    (g : uliftYoneda.{max w vâ‚‚}.obj Y âŸ¶ (restrictedULiftYoneda.{max w vâ‚} A).obj E) :
    A.map f â‰« (uliftYonedaEquiv g).down =
      (uliftYonedaEquiv (uliftYoneda.map f â‰« g)).down := by
  have this := congr_fun (g.naturality f.op) (ULift.up (ðŸ™ Y))
  dsimp [uliftYonedaEquiv, uliftYoneda] at this âŠ¢
  simp only [comp_id] at this
  simp [id_comp, this]

/-- Auxiliary definition for `restrictedULiftYonedaHomEquiv`. -/
def restrictedULiftYonedaHomEquiv' (P : Cáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)) (E : â„°) :
    (CostructuredArrow.proj uliftYoneda.{max w vâ‚‚} P â‹™ A âŸ¶
      (Functor.const (CostructuredArrow uliftYoneda.{max w vâ‚‚} P)).obj E) â‰ƒ
      (P âŸ¶ (restrictedULiftYoneda.{max w vâ‚} A).obj E) where
  toFun f :=
    { app _ x := ULift.up (f.app (CostructuredArrow.mk (uliftYonedaEquiv.symm x)))
      naturality _ _ g := by
        ext x
        let Ï† : CostructuredArrow.mk (uliftYonedaEquiv.{max w vâ‚‚}.symm (P.map g x)) âŸ¶
          CostructuredArrow.mk (uliftYonedaEquiv.symm x) :=
            CostructuredArrow.homMk g.unop (by
              dsimp
              rw [uliftYonedaEquiv_symm_map])
        dsimp
        congr 1
        simpa using (f.naturality Ï†).symm }
  invFun g :=
    { app y := (uliftYonedaEquiv.{max w vâ‚‚} (y.hom â‰« g)).down
      naturality y y' f := by
        dsimp
        rw [comp_id, â† CostructuredArrow.w f, assoc, map_comp_uliftYonedaEquiv_down] }
  left_inv f := by
    ext X
    let e : CostructuredArrow.mk
      (uliftYonedaEquiv.{max w vâ‚‚}.symm (X.hom.app (op X.left) âŸ¨ðŸ™ X.leftâŸ©)) â‰… X :=
        CostructuredArrow.isoMk (Iso.refl _) (by
          ext Y x
          dsimp
          rw [â† FunctorToTypes.naturality]
          congr )
    simpa [e] using f.naturality e.inv
  right_inv g := by
    ext X x
    apply ULift.down_injective
    simp [uliftYonedaEquiv]

@[reassoc]
lemma restrictedULiftYonedaHomEquiv'_symm_naturality_right (P : Cáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)) {E E' : â„°}
    (g : E âŸ¶ E') (f : (P âŸ¶ (restrictedULiftYoneda.{max w vâ‚} A).obj E)) :
    (restrictedULiftYonedaHomEquiv' A P E').symm (f â‰« (restrictedULiftYoneda A).map g) =
      (restrictedULiftYonedaHomEquiv' A P E).symm f â‰« (Functor.const _ ).map g := by
  rfl

@[reassoc]
lemma restrictedULiftYonedaHomEquiv'_symm_app_naturality_left
    {P Q : Cáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)} (f : P âŸ¶ Q) (E : â„°)
    (g : Q âŸ¶ (restrictedULiftYoneda.{max w vâ‚} A).obj E)
    (p : CostructuredArrow uliftYoneda.{max w vâ‚‚} P) :
    ((restrictedULiftYonedaHomEquiv' A P E).symm (f â‰« g)).app p =
      ((restrictedULiftYonedaHomEquiv' A Q E).symm g).app
        ((CostructuredArrow.map f).obj p) :=
  rfl

section

variable (P : â„°áµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚))

example [HasColimitsOfSize.{vâ‚, max uâ‚ vâ‚ vâ‚‚ w} â„°] :
    (uliftYoneda.{max w vâ‚‚}).HasPointwiseLeftKanExtension A := by
  infer_instance

variable [(uliftYoneda.{max w vâ‚‚}).HasPointwiseLeftKanExtension A]

variable {A}
variable (L : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°)
  (Î± : A âŸ¶ uliftYoneda.{max w vâ‚‚} â‹™ L) [L.IsLeftKanExtension Î±]

/-- Auxiliary definition for `uliftYonedaAdjunction`. -/
noncomputable def restrictedULiftYonedaHomEquiv (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) (E : â„°) :
    (L.obj P âŸ¶ E) â‰ƒ (P âŸ¶ (restrictedULiftYoneda.{max w vâ‚} A).obj E) :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension _ Î± P).homEquiv.trans
    (restrictedULiftYonedaHomEquiv' A P E)

/-- If `L : (Cáµ’áµ– â¥¤ Type max vâ‚ vâ‚‚) â¥¤ â„°` is a pointwise left Kan extension
of a functor `A : C â¥¤ â„°` along the Yoneda embedding,
then `L` is a left adjoint of `restrictedULiftYoneda A : â„° â¥¤ Cáµ’áµ– â¥¤ Type max vâ‚ vâ‚‚` -/
noncomputable def uliftYonedaAdjunction : L âŠ£ restrictedULiftYoneda.{max w vâ‚} A :=
  Adjunction.mkOfHomEquiv
    { homEquiv := restrictedULiftYonedaHomEquiv L Î±
      homEquiv_naturality_left_symm {P Q X} f g := by
        apply (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Î± P).hom_ext
        intro p
        have hfg := (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension
          L Î± P).comp_homEquiv_symm ((restrictedULiftYonedaHomEquiv' A P X).symm (f â‰« g)) p
        have hg := (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension
          L Î± Q).comp_homEquiv_symm ((restrictedULiftYonedaHomEquiv' A Q X).symm g)
            ((CostructuredArrow.map f).obj p)
        dsimp at hfg hg
        dsimp [restrictedULiftYonedaHomEquiv]
        simp only [assoc, hfg, â† L.map_comp_assoc, hg,
          restrictedULiftYonedaHomEquiv'_symm_app_naturality_left]
      homEquiv_naturality_right {P X Y} f g := by
        have := @IsColimit.homEquiv_symm_naturality (h :=
          Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Î± P)
        dsimp at this
        apply (restrictedULiftYonedaHomEquiv L Î± P Y).symm.injective
        apply (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Î± P).hom_ext
        intro
        simp [restrictedULiftYonedaHomEquiv,
          restrictedULiftYonedaHomEquiv'_symm_naturality_right, this] }

@[simp]
lemma uliftYonedaAdjunction_homEquiv_app {P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚}
    {Y : â„°} (f : L.obj P âŸ¶ Y) {Z : Cáµ’áµ–} (z : P.obj Z) :
    ((uliftYonedaAdjunction.{w} L Î±).homEquiv P Y f).app Z z =
      ULift.up (Î±.app Z.unop â‰« L.map (uliftYonedaEquiv.symm z) â‰« f) := by
  simp [uliftYonedaAdjunction, restrictedULiftYonedaHomEquiv,
    restrictedULiftYonedaHomEquiv', IsColimit.homEquiv]

@[simp]
lemma uliftYonedaAdjunction_unit_app_app (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚)
    {Z : Cáµ’áµ–} (z : P.obj Z) :
    ((uliftYonedaAdjunction.{w} L Î±).unit.app P).app Z z =
      ULift.up (Î±.app Z.unop â‰« L.map (uliftYonedaEquiv.symm z)) := by
  have hâ‚ := (uliftYonedaAdjunction.{w} L Î±).homEquiv_unit P _ (ðŸ™ _)
  simp only [Functor.comp_obj, Functor.map_id, comp_id] at hâ‚
  simp [â† hâ‚]

include Î± in
/-- Any left Kan extension along the Yoneda embedding preserves colimits. -/
lemma preservesColimitsOfSize_of_isLeftKanExtension :
    PreservesColimitsOfSize.{vâ‚ƒ, uâ‚ƒ} L :=
  (uliftYonedaAdjunction L Î±).leftAdjoint_preservesColimits

lemma isIso_of_isLeftKanExtension : IsIso Î± :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension _ Î±).isIso_hom

variable (A)

/-- See Property 2 of https://ncatlab.org/nlab/show/Yoneda+extension#properties. -/
instance preservesColimitsOfSize_leftKanExtension :
    PreservesColimitsOfSize.{vâ‚ƒ, uâ‚ƒ} (uliftYoneda.{max w vâ‚‚}.leftKanExtension A) :=
  (uliftYonedaAdjunction _ (uliftYoneda.leftKanExtensionUnit A)).leftAdjoint_preservesColimits

instance : IsIso (uliftYoneda.{max w vâ‚‚}.leftKanExtensionUnit A) :=
  isIso_of_isLeftKanExtension _ (uliftYoneda.leftKanExtensionUnit A)

/-- A pointwise left Kan extension along the Yoneda embedding is an extension. -/
noncomputable def isExtensionAlongULiftYoneda :
    uliftYoneda.{max w vâ‚‚} â‹™ uliftYoneda.leftKanExtension A â‰… A :=
  (asIso (uliftYoneda.leftKanExtensionUnit A)).symm

end

/-- Given `P : Cáµ’áµ– â¥¤ Type max w vâ‚`, this is the functor from the opposite category
of the category of elements of `X` which sends an element in `P.obj (op X)` to the
presheaf represented by `X`. The definition`coconeOfRepresentable`
gives a cocone for this functor which is a colimit and has point `P`.
-/
@[simps! obj map]
def functorToRepresentables (P : Cáµ’áµ– â¥¤ Type max w vâ‚) :
    P.Elementsáµ’áµ– â¥¤ Cáµ’áµ– â¥¤ Type (max w vâ‚) :=
  (CategoryOfElements.Ï€ P).leftOp â‹™ uliftYoneda.{w}

/-- This is a cocone with point `P` for the functor `functorToRepresentables P`. It is shown in
`colimitOfRepresentable P` that this cocone is a colimit: that is, we have exhibited an arbitrary
presheaf `P` as a colimit of representables.

The construction of [MM92], Chapter I, Section 5, Corollary 3.
-/
@[simps]
def coconeOfRepresentable (P : Cáµ’áµ– â¥¤ Type max w vâ‚) :
    Cocone (functorToRepresentables P) where
  pt := P
  Î¹ :=
    { app x := uliftYonedaEquiv.symm x.unop.2
      naturality {xâ‚ xâ‚‚} f := by
        dsimp
        rw [comp_id, â† uliftYonedaEquiv_symm_map, f.unop.2] }

/-- The legs of the cocone `coconeOfRepresentable` are natural in the choice of presheaf. -/
theorem coconeOfRepresentable_naturality
    {Pâ‚ Pâ‚‚ : Cáµ’áµ– â¥¤ Type max w vâ‚} (Î± : Pâ‚ âŸ¶ Pâ‚‚) (j : Pâ‚.Elementsáµ’áµ–) :
    (coconeOfRepresentable Pâ‚).Î¹.app j â‰« Î± =
      (coconeOfRepresentable Pâ‚‚).Î¹.app ((CategoryOfElements.map Î±).op.obj j) := by
  ext T f
  simp [uliftYonedaEquiv, FunctorToTypes.naturality]

/-- The cocone with point `P` given by `coconeOfRepresentable` is a colimit:
that is, we have exhibited an arbitrary presheaf `P` as a colimit of representables.

The result of [MM92], Chapter I, Section 5, Corollary 3.
-/
def colimitOfRepresentable (P : Cáµ’áµ– â¥¤ Type max w vâ‚) :
    IsColimit (coconeOfRepresentable P) where
  desc s :=
    { app X x := uliftYonedaEquiv (s.Î¹.app (Opposite.op (Functor.elementsMk P X x)))
      naturality X Y f := by
        ext x
        have := s.w (Quiver.Hom.op (CategoryOfElements.homMk (P.elementsMk X x)
          (P.elementsMk Y (P.map f x)) f rfl))
        dsimp at this x âŠ¢
        rw [â† this, uliftYonedaEquiv_comp]
        dsimp
        rw [uliftYonedaEquiv_apply, â† FunctorToTypes.naturality,
          uliftYonedaEquiv_uliftYoneda_map]
        simp [uliftYoneda] }
  fac s j := by
    ext X x
    let Ï† : j.unop âŸ¶ (Functor.elementsMk P _
      ((uliftYonedaEquiv.symm (unop j).snd).app X x)) := âŸ¨x.down.op, rflâŸ©
    have := s.w Ï†.op
    dsimp [Ï†] at this x âŠ¢
    rw [â† this, uliftYonedaEquiv_apply]
    dsimp [uliftYoneda]
    rw [id_comp]
  uniq s m hm := by
    ext X x
    dsimp at hm âŠ¢
    rw [â† hm, uliftYonedaEquiv_comp, Equiv.apply_symm_apply]

variable {A : C â¥¤ â„°}

example [HasColimitsOfSize.{vâ‚, max w uâ‚ vâ‚ vâ‚‚} â„°] :
    uliftYoneda.{max w vâ‚‚}.HasPointwiseLeftKanExtension A := by
  infer_instance

variable [uliftYoneda.{max w vâ‚‚}.HasPointwiseLeftKanExtension A]

section

variable (L : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°) (Î± : A âŸ¶ uliftYoneda.{max w vâ‚‚} â‹™ L)

instance [L.IsLeftKanExtension Î±] : IsIso Î± :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Î±).isIso_hom

lemma isLeftKanExtension_along_uliftYoneda_iff :
    L.IsLeftKanExtension Î± â†”
      (IsIso Î± âˆ§ PreservesColimitsOfSize.{vâ‚, max w uâ‚ vâ‚ vâ‚‚} L) := by
  constructor
  Â· intro
    exact âŸ¨inferInstance, preservesColimits_of_natIso (Functor.leftKanExtensionUnique _
      (uliftYoneda.{max w vâ‚‚}.leftKanExtensionUnit A) _ Î±)âŸ©
  Â· rintro âŸ¨_, _âŸ©
    apply Functor.LeftExtension.IsPointwiseLeftKanExtension.isLeftKanExtension
      (E := Functor.LeftExtension.mk _ Î±)
    intro P
    dsimp [Functor.LeftExtension.IsPointwiseLeftKanExtensionAt]
    apply IsColimit.ofWhiskerEquivalence
      (CategoryOfElements.costructuredArrowULiftYonedaEquivalence _)
    let e : (CategoryOfElements.costructuredArrowULiftYonedaEquivalence P).functor â‹™
      CostructuredArrow.proj uliftYoneda.{max w vâ‚‚} P â‹™ A â‰…
        functorToRepresentables.{max w vâ‚‚} P â‹™ L :=
      Functor.isoWhiskerLeft _ (Functor.isoWhiskerLeft _ (asIso Î±)) â‰ªâ‰«
        Functor.isoWhiskerLeft _ (Functor.associator _ _ _).symm â‰ªâ‰«
        (Functor.associator _ _ _).symm â‰ªâ‰« Functor.isoWhiskerRight (Iso.refl _) L
    refine (IsColimit.precomposeHomEquiv e.symm _).1 ?_
    exact IsColimit.ofIsoColimit (isColimitOfPreserves L (colimitOfRepresentable.{max w vâ‚‚} P))
      (Cocones.ext (Iso.refl _))

lemma isLeftKanExtension_of_preservesColimits
    (L : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°) (e : A â‰… uliftYoneda.{max w vâ‚‚} â‹™ L)
    [PreservesColimitsOfSize.{vâ‚, max w uâ‚ vâ‚ vâ‚‚} L] :
    L.IsLeftKanExtension e.hom := by
  rw [isLeftKanExtension_along_uliftYoneda_iff]
  exact âŸ¨inferInstance, âŸ¨inferInstanceâŸ©âŸ©

end

/-- Show that `uliftYoneda.leftKanExtension A` is the unique colimit-preserving
functor which extends `A` to the presheaf category.

The second part of [MM92], Chapter I, Section 5, Corollary 4.
See Property 3 of https://ncatlab.org/nlab/show/Yoneda+extension#properties.
-/
noncomputable def uniqueExtensionAlongULiftYoneda (L : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°)
    (e : A â‰… uliftYoneda.{max w vâ‚‚} â‹™ L)
    [PreservesColimitsOfSize.{vâ‚, max w uâ‚ vâ‚ vâ‚‚} L] :
    L â‰… uliftYoneda.{max w vâ‚‚}.leftKanExtension A :=
  have := isLeftKanExtension_of_preservesColimits L e
  Functor.leftKanExtensionUnique _ e.hom _ (uliftYoneda.leftKanExtensionUnit A)

instance (L : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°) [PreservesColimitsOfSize.{vâ‚, max w uâ‚ vâ‚ vâ‚‚} L]
    [uliftYoneda.{max w vâ‚‚}.HasPointwiseLeftKanExtension (uliftYoneda.{max w vâ‚‚} â‹™ L)] :
    L.IsLeftKanExtension (ðŸ™ _ : uliftYoneda.{max w vâ‚‚} â‹™ L âŸ¶ _) :=
  isLeftKanExtension_of_preservesColimits _ (Iso.refl _)

/-- If `L` preserves colimits and `â„°` has them, then it is a left adjoint. Note this is a (partial)
converse to `leftAdjointPreservesColimits`.
-/
lemma isLeftAdjoint_of_preservesColimits (L : (C â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ â„°)
    [PreservesColimitsOfSize.{vâ‚, max w uâ‚ vâ‚ vâ‚‚} L]
    [uliftYoneda.{max w vâ‚‚}.HasPointwiseLeftKanExtension
      (uliftYoneda.{max w vâ‚‚} â‹™ (opOpEquivalence C).congrLeft.functor.comp L)] :
    L.IsLeftAdjoint :=
  âŸ¨_, âŸ¨((opOpEquivalence C).congrLeft.symm.toAdjunction.comp
    (uliftYonedaAdjunction _ (ðŸ™ _))).ofNatIsoLeft
      ((opOpEquivalence C).congrLeft.invFunIdAssoc L)âŸ©âŸ©

section

variable {D : Type uâ‚‚} [Category.{vâ‚} D] (F : C â¥¤ D)

instance (X : C) (Y : F.op.LeftExtension (yoneda.obj X)) :
    Unique (Functor.LeftExtension.mk _ (yonedaMap F X) âŸ¶ Y) where
  default := StructuredArrow.homMk
      (yonedaEquiv.symm (yonedaEquiv (F := F.op.comp Y.right) Y.hom)) (by
        ext Z f
        simpa using congr_fun (Y.hom.naturality f.op).symm (ðŸ™ _))
  uniq Ï† := by
    ext1
    apply yonedaEquiv.injective
    simp [â† StructuredArrow.w Ï†, yonedaEquiv]

/-- Given `F : C â¥¤ D` and `X : C`, `yoneda.obj (F.obj X) : Dáµ’áµ– â¥¤ Type _` is the
left Kan extension of `yoneda.obj X : Cáµ’áµ– â¥¤ Type _` along `F.op`. -/
instance (X : C) : (yoneda.obj (F.obj X)).IsLeftKanExtension (yonedaMap F X) :=
  âŸ¨âŸ¨Limits.IsInitial.ofUnique _âŸ©âŸ©

end

section

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : C â¥¤ D)

instance (X : C) (Y : F.op.LeftExtension (uliftYoneda.{max w vâ‚‚}.obj X)) :
    Unique (Functor.LeftExtension.mk _ (uliftYonedaMap.{w} F X) âŸ¶ Y) where
  default := StructuredArrow.homMk
    (uliftYonedaEquiv.symm (uliftYonedaEquiv (F := F.op â‹™ Y.right) Y.hom)) (by
      ext Z âŸ¨fâŸ©
      simpa [uliftYonedaEquiv, uliftYoneda] using
        congr_fun (Y.hom.naturality f.op).symm (ULift.up (ðŸ™ _)) )
  uniq Ï† := by
    ext : 1
    apply uliftYonedaEquiv.injective
    simp [â† StructuredArrow.w Ï†, uliftYonedaEquiv, uliftYonedaMap]

/-- Given `F : C â¥¤ D` and `X : C`, `uliftYoneda.obj (F.obj X) : Dáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)` is the
left Kan extension of `uliftYoneda.obj X : Cáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)` along `F.op`. -/
instance (X : C) : (uliftYoneda.{max w vâ‚}.obj (F.obj X)).IsLeftKanExtension
    (uliftYonedaMap.{w} F X) :=
  âŸ¨âŸ¨Limits.IsInitial.ofUnique _âŸ©âŸ©

section
variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚), F.op.HasLeftKanExtension P]

/-- `F â‹™ uliftYoneda` is naturally isomorphic to `uliftYoneda â‹™ F.op.lan`. -/
noncomputable def compULiftYonedaIsoULiftYonedaCompLan :
    F â‹™ uliftYoneda.{max w vâ‚} â‰… uliftYoneda.{max w vâ‚‚} â‹™ F.op.lan :=
  NatIso.ofComponents (fun X => Functor.leftKanExtensionUnique _
    (uliftYonedaMap.{w} F X) (F.op.lan.obj _) (F.op.lanUnit.app (uliftYoneda.obj X)))
    (fun {X Y} f => by
      apply uliftYonedaEquiv.injective
      have eqâ‚ := congr_fun
        ((uliftYoneda.{max w vâ‚}.obj (F.obj Y)).descOfIsLeftKanExtension_fac_app
        (uliftYonedaMap F Y) (F.op.lan.obj (uliftYoneda.obj Y))
          (F.op.lanUnit.app (uliftYoneda.obj Y)) _) âŸ¨fâŸ©
      have eqâ‚‚ := congr_fun
        (((uliftYoneda.{max w vâ‚}.obj (F.obj X)).descOfIsLeftKanExtension_fac_app
        (uliftYonedaMap F X) (F.op.lan.obj (uliftYoneda.obj X))
          (F.op.lanUnit.app (uliftYoneda.obj X))) _) âŸ¨ðŸ™ _âŸ©
      have eqâ‚ƒ := congr_fun (congr_app (F.op.lanUnit.naturality
        (uliftYoneda.{max w vâ‚‚}.map f)) _) âŸ¨ðŸ™ _âŸ©
      dsimp [uliftYoneda, uliftYonedaMap, uliftYonedaEquiv,
        Functor.leftKanExtensionUnique] at eqâ‚ eqâ‚‚ eqâ‚ƒ âŠ¢
      simp only [Functor.map_id] at eqâ‚‚
      simp only [id_comp] at eqâ‚ƒ
      simp [eqâ‚, eqâ‚‚, eqâ‚ƒ])

@[simp]
lemma compULiftYonedaIsoULiftYonedaCompLan_inv_app_app_apply_eq_id (X : C) :
    ((compULiftYonedaIsoULiftYonedaCompLan.{w} F).inv.app X).app (op (F.obj X))
          ((F.op.lanUnit.app ((uliftYoneda.{max w vâ‚‚}).obj X)).app (op X)
        (ULift.up (ðŸ™ X))) = ULift.up (ðŸ™ (F.obj X)) :=
  (congr_fun (Functor.descOfIsLeftKanExtension_fac_app _
    (F.op.lanUnit.app ((uliftYoneda.{max w vâ‚‚}).obj X)) _
    (uliftYonedaMap.{w} F X) (op X)) (ULift.up (ðŸ™ X))).trans (by simp [uliftYonedaMap])

end

namespace compULiftYonedaIsoULiftYonedaCompLan

variable {F}

section

variable {X : C} {G : (Cáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)) â¥¤ Dáµ’áµ– â¥¤ Type (max w vâ‚ vâ‚‚)}
  (Ï† : F â‹™ uliftYoneda.{max w vâ‚} âŸ¶ uliftYoneda.{max w vâ‚‚} â‹™ G)

/-- Auxiliary definition for `presheafHom`. -/
def coconeApp {P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚} (x : P.Elements) :
    uliftYoneda.{max w vâ‚‚}.obj x.1.unop âŸ¶ F.op â‹™ G.obj P :=
  uliftYonedaEquiv.symm
    ((G.map (uliftYonedaEquiv.{max w vâ‚‚}.symm x.2)).app _
      ((Ï†.app x.1.unop).app _ (ULift.up (ðŸ™ _))))

@[reassoc (attr := simp)]
lemma coconeApp_naturality {P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚} {x y : P.Elements} (f : x âŸ¶ y) :
    uliftYoneda.map f.1.unop â‰« coconeApp.{w} Ï† x = coconeApp Ï† y := by
  have eqâ‚ : uliftYoneda.map f.1.unop â‰« uliftYonedaEquiv.symm x.2 =
      uliftYonedaEquiv.{max w vâ‚‚}.symm y.2 :=
    uliftYonedaEquiv.injective
      (by simpa only [Equiv.apply_symm_apply, â† uliftYonedaEquiv_naturality] using f.2)
  have eqâ‚‚ := congr_fun ((G.map (uliftYonedaEquiv.{max w vâ‚‚}.symm x.2)).naturality
    (F.map f.1.unop).op) ((Ï†.app x.1.unop).app _ (ULift.up (ðŸ™ _)))
  have eqâ‚ƒ := congr_fun (congr_app (Ï†.naturality f.1.unop) _) (ULift.up (ðŸ™ _))
  have eqâ‚„ := congr_fun ((Ï†.app x.1.unop).naturality (F.map f.1.unop).op)
  dsimp at eqâ‚‚ eqâ‚ƒ eqâ‚„
  apply uliftYonedaEquiv.{max w vâ‚‚}.injective
  dsimp only [coconeApp]
  rw [Equiv.apply_symm_apply, â† uliftYonedaEquiv_naturality, Equiv.apply_symm_apply]
  simp only [â† eqâ‚, â† eqâ‚‚, â† eqâ‚ƒ, â† eqâ‚„, op_unop, Functor.comp_obj,
    Functor.op_obj, yoneda_obj_obj, Functor.comp_map,
    Functor.op_map, Functor.map_comp, FunctorToTypes.comp,]
  simp [uliftYoneda]

/-- Given functors `F : C â¥¤ D` and `G : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ (Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚)`,
and a natural transformation `Ï† : F â‹™ uliftYoneda âŸ¶ uliftYoneda â‹™ G`, this is the
(natural) morphism `P âŸ¶ F.op â‹™ G.obj P` for all `P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚` that is
determined by `Ï†`. -/
def presheafHom (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) : P âŸ¶ F.op â‹™ G.obj P :=
  (colimitOfRepresentable P).desc
    (Cocone.mk _ { app x := coconeApp.{w} Ï† x.unop })

lemma uliftYonedaEquiv_Î¹_presheafHom (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) {X : C}
    (f : uliftYoneda.{max w vâ‚‚}.obj X âŸ¶ P) :
    uliftYonedaEquiv (f â‰« presheafHom.{w} Ï† P) =
      (G.map f).app (Opposite.op (F.obj X)) ((Ï†.app X).app _ (ULift.up (ðŸ™ _))) := by
  obtain âŸ¨x, rflâŸ© := uliftYonedaEquiv.symm.surjective f
  erw [(colimitOfRepresentable P).fac _ (Opposite.op (P.elementsMk _ x))]
  dsimp only [coconeApp]
  apply Equiv.apply_symm_apply

lemma uliftYonedaEquiv_presheafHom_uliftYoneda_obj (X : C) :
    uliftYonedaEquiv.{max w vâ‚‚} (presheafHom.{w} Ï† (uliftYoneda.{max w vâ‚‚}.obj X)) =
      ((Ï†.app X).app (F.op.obj (Opposite.op X)) (ULift.up (ðŸ™ _))) := by
  simpa using uliftYonedaEquiv_Î¹_presheafHom.{w} Ï† (uliftYoneda.obj X) (ðŸ™ _)

@[reassoc (attr := simp)]
lemma presheafHom_naturality {P Q : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚} (f : P âŸ¶ Q) :
    presheafHom.{w} Ï† P â‰« Functor.whiskerLeft F.op (G.map f) = f â‰« presheafHom Ï† Q :=
  hom_ext_uliftYoneda.{max w vâ‚‚} (fun X p â†¦ uliftYonedaEquiv.injective (by
    rw [â† assoc p f, uliftYonedaEquiv_Î¹_presheafHom, â† assoc,
      uliftYonedaEquiv_comp, uliftYonedaEquiv_Î¹_presheafHom,
      Functor.map_comp, FunctorToTypes.comp]
    dsimp))

variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚), F.op.HasLeftKanExtension P]

/-- Given functors `F : C â¥¤ D` and `G : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ (Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚)`,
and a natural transformation `Ï† : F â‹™ uliftYoneda âŸ¶ uliftYoneda â‹™ G`, this is
the canonical natural transformation `F.op.lan âŸ¶ G`, which is part of the
fact that `F.op.lan : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`
is the left Kan extension of `F â‹™ uliftYoneda : C â¥¤ Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`
along `uliftYoneda : C â¥¤ Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`. -/
noncomputable def natTrans : F.op.lan âŸ¶ G where
  app P := (F.op.lan.obj P).descOfIsLeftKanExtension (F.op.lanUnit.app P) _ (presheafHom Ï† P)
  naturality {P Q} f := by
    apply (F.op.lan.obj P).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app P)
    have eq := F.op.lanUnit.naturality f
    dsimp at eq âŠ¢
    rw [Functor.descOfIsLeftKanExtension_fac_assoc, â† reassoc_of% eq,
      Functor.descOfIsLeftKanExtension_fac, presheafHom_naturality]

lemma natTrans_app_uliftYoneda_obj (X : C) :
    (natTrans.{w} Ï†).app (uliftYoneda.{max w vâ‚‚}.obj X) =
      (compULiftYonedaIsoULiftYonedaCompLan.{w} F).inv.app X â‰« Ï†.app X := by
  dsimp [natTrans]
  apply (F.op.lan.obj (uliftYoneda.obj X)).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app _)
  rw [Functor.descOfIsLeftKanExtension_fac]
  apply uliftYonedaEquiv.injective
  rw [uliftYonedaEquiv_presheafHom_uliftYoneda_obj]
  exact congr_arg _ (compULiftYonedaIsoULiftYonedaCompLan_inv_app_app_apply_eq_id F X).symm

end

variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚), F.op.HasLeftKanExtension P]

/-- Given a functor `F : C â¥¤ D`, this definition is part of the verification that
`Functor.LeftExtension.mk F.op.lan (compULiftYonedaIsoULiftYonedaCompLan F).hom`
is universal, i.e. that  `F.op.lan : (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚) â¥¤ Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`
is the left Kan extension of `F â‹™ uliftYoneda : C â¥¤ Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`
along `uliftYoneda : C â¥¤ Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚`. -/
noncomputable def extensionHom
    (Î¦ : uliftYoneda.{max w vâ‚‚}.LeftExtension (F â‹™ uliftYoneda.{max w vâ‚})) :
    Functor.LeftExtension.mk F.op.lan (compULiftYonedaIsoULiftYonedaCompLan.{w} F).hom âŸ¶ Î¦ :=
  StructuredArrow.homMk (natTrans Î¦.hom) (by
    ext X : 2
    dsimp
    rw [natTrans_app_uliftYoneda_obj, Iso.hom_inv_id_app_assoc])

@[ext]
lemma hom_ext {Î¦ : uliftYoneda.{max w vâ‚‚}.LeftExtension (F â‹™ uliftYoneda.{max w vâ‚})}
    (f g : Functor.LeftExtension.mk F.op.lan (compULiftYonedaIsoULiftYonedaCompLan F).hom âŸ¶ Î¦) :
    f = g := by
  ext P : 3
  apply (F.op.lan.obj P).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app P)
  apply (colimitOfRepresentable.{max w vâ‚‚} P).hom_ext
  intro x
  have eq := F.op.lanUnit.naturality (uliftYonedaEquiv.{max w vâ‚‚}.symm x.unop.2)
  have eqâ‚ := congr_fun (congr_app (congr_app (StructuredArrow.w f) x.unop.1.unop)
    (F.op.obj x.unop.1)) (ULift.up (ðŸ™ _))
  have eqâ‚‚ := congr_fun (congr_app (congr_app (StructuredArrow.w g) x.unop.1.unop)
    (F.op.obj x.unop.1)) (ULift.up (ðŸ™ _))
  dsimp at eqâ‚ eqâ‚‚ eq âŠ¢
  simp only [reassoc_of% eq, â† Functor.whiskerLeft_comp]
  congr 2
  simp only [â† cancel_epi ((compULiftYonedaIsoULiftYonedaCompLan F).hom.app x.unop.1.unop),
    NatTrans.naturality]
  apply uliftYonedaEquiv.injective
  dsimp [uliftYonedaEquiv_apply]
  rw [eqâ‚, eqâ‚‚]

end compULiftYonedaIsoULiftYonedaCompLan

variable [âˆ€ (P : Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚), F.op.HasLeftKanExtension P]

noncomputable instance (Î¦ : StructuredArrow (F â‹™ uliftYoneda.{max w vâ‚})
    ((Functor.whiskeringLeft C (Cáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚)
      (Dáµ’áµ– â¥¤ Type max w vâ‚ vâ‚‚)).obj uliftYoneda.{max w vâ‚‚})) :
    Unique (Functor.LeftExtension.mk F.op.lan
      (compULiftYonedaIsoULiftYonedaCompLan.{w} F).hom âŸ¶ Î¦) where
  default := compULiftYonedaIsoULiftYonedaCompLan.extensionHom Î¦
  uniq _ := compULiftYonedaIsoULiftYonedaCompLan.hom_ext _ _

/-- Given a functor `F : C â¥¤ D`, `F.op.lan : (Cáµ’áµ– â¥¤ Type vâ‚) â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` is the
left Kan extension of `F â‹™ yoneda : C â¥¤ Dáµ’áµ– â¥¤ Type vâ‚` along `yoneda : C â¥¤ Cáµ’áµ– â¥¤ Type vâ‚`. -/
instance : F.op.lan.IsLeftKanExtension (compULiftYonedaIsoULiftYonedaCompLan.{w} F).hom :=
  âŸ¨âŸ¨Limits.IsInitial.ofUnique _âŸ©âŸ©

end

/-- For a presheaf `P`, consider the forgetful functor from the category of representable
    presheaves over `P` to the category of presheaves. There is a tautological cocone over this
    functor whose leg for a natural transformation `V âŸ¶ P` with `V` representable is just that
    natural transformation. (In this version, we allow the presheaf `P` to have values in
    a larger universe.) -/
@[simps]
def tautologicalCocone' (P : Cáµ’áµ– â¥¤ Type max w vâ‚) :
    Cocone (CostructuredArrow.proj uliftYoneda.{w} P â‹™ uliftYoneda.{w}) where
  pt := P
  Î¹ := { app X := X.hom }

/-- The tautological cocone with point `P` is a colimit cocone, exhibiting `P` as a colimit of
    representables. (In this version, we allow the presheaf `P` to have values in
    a larger universe.)

    Proposition 2.6.3(i) in [Kashiwara2006] -/
def isColimitTautologicalCocone' (P : Cáµ’áµ– â¥¤ Type max w vâ‚) :
    IsColimit (tautologicalCocone'.{w} P) :=
  (IsColimit.whiskerEquivalenceEquiv
    (CategoryOfElements.costructuredArrowULiftYonedaEquivalence.{w} P)).2
      (colimitOfRepresentable.{w} P)


/-- For a presheaf `P`, consider the forgetful functor from the category of representable
    presheaves over `P` to the category of presheaves. There is a tautological cocone over this
    functor whose leg for a natural transformation `V âŸ¶ P` with `V` representable is just that
    natural transformation. -/
@[simps]
def tautologicalCocone (P : Cáµ’áµ– â¥¤ Type vâ‚) :
    Cocone (CostructuredArrow.proj yoneda P â‹™ yoneda) where
  pt := P
  Î¹ := { app X := X.hom }

/-- The tautological cocone with point `P` is a colimit cocone, exhibiting `P` as a colimit of
    representables.

    Proposition 2.6.3(i) in [Kashiwara2006] -/
def isColimitTautologicalCocone (P : Cáµ’áµ– â¥¤ Type vâ‚) :
    IsColimit (tautologicalCocone P) :=
  let e : functorToRepresentables.{vâ‚} P â‰…
    ((CategoryOfElements.costructuredArrowYonedaEquivalence P).functor â‹™
      CostructuredArrow.proj yoneda P â‹™ yoneda) :=
    NatIso.ofComponents (fun e â†¦ NatIso.ofComponents (fun X â†¦ Equiv.ulift.toIso))
  (IsColimit.whiskerEquivalenceEquiv
    (CategoryOfElements.costructuredArrowYonedaEquivalence P)).2
      ((IsColimit.precomposeHomEquiv e _).1 (colimitOfRepresentable.{vâ‚} P))

variable {I : Type vâ‚} [SmallCategory I] (F : I â¥¤ C)

/-- Given a functor `F : I â¥¤ C`, a cocone `c` on `F â‹™ yoneda : I â¥¤ Cáµ’áµ– â¥¤ Type vâ‚` induces a
    functor `I â¥¤ CostructuredArrow yoneda c.pt` which maps `i : I` to the leg
    `yoneda.obj (F.obj i) âŸ¶ c.pt`. If `c` is a colimit cocone, then that functor is
    final.

    Proposition 2.6.3(ii) in [Kashiwara2006] -/
theorem final_toCostructuredArrow_comp_pre {c : Cocone (F â‹™ yoneda)} (hc : IsColimit c) :
    Functor.Final (c.toCostructuredArrow â‹™ CostructuredArrow.pre F yoneda c.pt) := by
  apply Functor.final_of_isTerminal_colimit_comp_yoneda
  suffices IsTerminal (colimit ((c.toCostructuredArrow â‹™ CostructuredArrow.pre F yoneda c.pt) â‹™
      CostructuredArrow.toOver yoneda c.pt)) by
    apply IsTerminal.isTerminalOfObj (overEquivPresheafCostructuredArrow c.pt).inverse
    apply IsTerminal.ofIso this
    refine ?_ â‰ªâ‰« (preservesColimitIso (overEquivPresheafCostructuredArrow c.pt).inverse _).symm
    apply HasColimit.isoOfNatIso
    exact Functor.isoWhiskerLeft _
      (CostructuredArrow.toOverCompOverEquivPresheafCostructuredArrow c.pt).isoCompInverse
  apply IsTerminal.ofIso Over.mkIdTerminal
  let isc : IsColimit ((Over.forget _).mapCocone _) := isColimitOfPreserves _
    (colimit.isColimit ((c.toCostructuredArrow â‹™ CostructuredArrow.pre F yoneda c.pt) â‹™
      CostructuredArrow.toOver yoneda c.pt))
  exact Over.isoMk (hc.coconePointUniqueUpToIso isc) (hc.hom_ext fun i => by simp)

@[deprecated (since := "2025-08-16")] alias restrictedYoneda := restrictedULiftYoneda
@[deprecated (since := "2025-08-16")] alias isExtensionAlongYoneda := isExtensionAlongULiftYoneda
@[deprecated (since := "2025-08-16")] alias isLeftKanExtension_along_yoneda_iff :=
  isLeftKanExtension_along_uliftYoneda_iff
@[deprecated (since := "2025-08-16")] alias yonedaAdjunction := uliftYonedaAdjunction
@[deprecated (since := "2025-08-16")] alias uniqueExtensionAlongYoneda :=
  uniqueExtensionAlongULiftYoneda

end Presheaf

end CategoryTheory
