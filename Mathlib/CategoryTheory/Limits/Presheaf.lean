/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Adjunction.Opposites
import Mathlib.CategoryTheory.Comma.Presheaf
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Limits.ConeCategory
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Limits.KanExtension
import Mathlib.CategoryTheory.Limits.Over

#align_import category_theory.limits.presheaf from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Colimit of representables

This file constructs an adjunction `yonedaAdjunction` between `(C·µí·µñ ‚•§ Type u)` and `‚Ñ∞` given a
functor `A : C ‚•§ ‚Ñ∞`, where the right adjoint sends `(E : ‚Ñ∞)` to `c ‚Ü¶ (A.obj c ‚ü∂ E)` (provided `‚Ñ∞`
has colimits).

This adjunction is used to show that every presheaf is a colimit of representables. This result is
also known as the density theorem, the co-Yoneda lemma and the Ninja Yoneda lemma.

Further, the left adjoint `colimitAdj.extendAlongYoneda : (C·µí·µñ ‚•§ Type u) ‚•§ ‚Ñ∞` satisfies
`yoneda ‚ãô L ‚âÖ A`, that is, an extension of `A : C ‚•§ ‚Ñ∞` to `(C·µí·µñ ‚•§ Type u) ‚•§ ‚Ñ∞` through
`yoneda : C ‚•§ C·µí·µñ ‚•§ Type u`. It is the left Kan extension of `A` along the yoneda embedding,
sometimes known as the Yoneda extension, as proved in `extendAlongYonedaIsoKan`.

`uniqueExtensionAlongYoneda` shows `extendAlongYoneda` is unique amongst cocontinuous functors
with this property, establishing the presheaf category as the free cocompletion of a small category.

We also give a direct pedestrian proof that every presheaf is a colimit of representables. This
version of the proof is valid for any category `C`, even if it is not small.

## Tags
colimit, representable, presheaf, free cocompletion

## References
* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
* https://ncatlab.org/nlab/show/Yoneda+extension
-/

namespace CategoryTheory

open Category Limits

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

section SmallCategory

variable {C : Type u‚ÇÅ} [SmallCategory C]
variable {‚Ñ∞ : Type u‚ÇÇ} [Category.{u‚ÇÅ} ‚Ñ∞]
variable (A : C ‚•§ ‚Ñ∞)

namespace ColimitAdj

/--
The functor taking `(E : ‚Ñ∞) (c : C·µí·µñ)` to the homset `(A.obj C ‚ü∂ E)`. It is shown in `L_adjunction`
that this functor has a left adjoint (provided `E` has colimits) given by taking colimits over
categories of elements.
In the case where `‚Ñ∞ = C·µí·µñ ‚•§ Type u` and `A = yoneda`, this functor is isomorphic to the identity.

Defined as in [MM92], Chapter I, Section 5, Theorem 2.
-/
@[simps!]
def restrictedYoneda : ‚Ñ∞ ‚•§ C·µí·µñ ‚•§ Type u‚ÇÅ :=
  yoneda ‚ãô (whiskeringLeft _ _ (Type u‚ÇÅ)).obj (Functor.op A)
#align category_theory.colimit_adj.restricted_yoneda CategoryTheory.ColimitAdj.restrictedYoneda

/--
The functor `restrictedYoneda` is isomorphic to the identity functor when evaluated at the yoneda
embedding.
-/
def restrictedYonedaYoneda : restrictedYoneda (yoneda : C ‚•§ C·µí·µñ ‚•§ Type u‚ÇÅ) ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun P =>
    NatIso.ofComponents (fun X => Equiv.toIso yonedaEquiv) @ fun X Y f =>
      funext fun x => by
        dsimp [yonedaEquiv]
        have : x.app X (CategoryStruct.id (Opposite.unop X)) =
            (x.app X (ùüô (Opposite.unop X))) := rfl
        rw [this]
        rw [‚Üê FunctorToTypes.naturality _ _ x f (ùüô _)]
        simp only [id_comp, Functor.op_obj, Opposite.unop_op, yoneda_obj_map, comp_id]
#align category_theory.colimit_adj.restricted_yoneda_yoneda CategoryTheory.ColimitAdj.restrictedYonedaYoneda

/-- (Implementation). The equivalence of homsets which helps construct the left adjoint to
`colimitAdj.restrictedYoneda`.
It is shown in `restrictYonedaHomEquivNatural` that this is a natural bijection.
-/
def restrictYonedaHomEquiv (P : C·µí·µñ ‚•§ Type u‚ÇÅ) (E : ‚Ñ∞)
    {c : Cocone ((CategoryOfElements.œÄ P).leftOp ‚ãô A)} (t : IsColimit c) :
    (c.pt ‚ü∂ E) ‚âÉ (P ‚ü∂ (restrictedYoneda A).obj E) :=
  ((uliftTrivial _).symm ‚â™‚â´ t.homIso' E).toEquiv.trans
    { toFun := fun k =>
        { app := fun c p => k.1 (Opposite.op ‚ü®_, p‚ü©)
          naturality := fun c c' f =>
            funext fun p =>
              (k.2
                  (Quiver.Hom.op ‚ü®f, rfl‚ü© :
                    (Opposite.op ‚ü®c', P.map f p‚ü© : P.Elements·µí·µñ) ‚ü∂ Opposite.op ‚ü®c, p‚ü©)).symm }
      invFun := fun œÑ =>
        { val := fun p => œÑ.app p.unop.1 p.unop.2
          property := @fun p p' f => by
            simp_rw [‚Üê f.unop.2]
            apply (congr_fun (œÑ.naturality f.unop.1) p'.unop.2).symm }
      left_inv := by
        rintro ‚ü®k‚ÇÅ, k‚ÇÇ‚ü©
        ext
        dsimp
        congr 1
      right_inv := by
        rintro ‚ü®_, _‚ü©
        rfl }
#align category_theory.colimit_adj.restrict_yoneda_hom_equiv CategoryTheory.ColimitAdj.restrictYonedaHomEquiv

/--
(Implementation). Show that the bijection in `restrictYonedaHomEquiv` is natural (on the right).
-/
theorem restrictYonedaHomEquiv_natural (P : C·µí·µñ ‚•§ Type u‚ÇÅ) (E‚ÇÅ E‚ÇÇ : ‚Ñ∞) (g : E‚ÇÅ ‚ü∂ E‚ÇÇ) {c : Cocone _}
    (t : IsColimit c) (k : c.pt ‚ü∂ E‚ÇÅ) :
    restrictYonedaHomEquiv A P E‚ÇÇ t (k ‚â´ g) =
      restrictYonedaHomEquiv A P E‚ÇÅ t k ‚â´ (restrictedYoneda A).map g := by
  ext x X
  apply (assoc _ _ _).symm
#align category_theory.colimit_adj.restrict_yoneda_hom_equiv_natural CategoryTheory.ColimitAdj.restrictYonedaHomEquiv_natural

variable [HasColimits ‚Ñ∞]

/--
The left adjoint to the functor `restrictedYoneda` (shown in `yonedaAdjunction`). It is also an
extension of `A` along the yoneda embedding (shown in `isExtensionAlongYoneda`), in particular
it is the left Kan extension of `A` through the yoneda embedding.
-/
noncomputable def extendAlongYoneda : (C·µí·µñ ‚•§ Type u‚ÇÅ) ‚•§ ‚Ñ∞ :=
  Adjunction.leftAdjointOfEquiv (fun P E => restrictYonedaHomEquiv A P E (colimit.isColimit _))
    fun P E E' g => restrictYonedaHomEquiv_natural A P E E' g _
#align category_theory.colimit_adj.extend_along_yoneda CategoryTheory.ColimitAdj.extendAlongYoneda

@[simp]
theorem extendAlongYoneda_obj (P : C·µí·µñ ‚•§ Type u‚ÇÅ) :
    (extendAlongYoneda A).obj P = colimit ((CategoryOfElements.œÄ P).leftOp ‚ãô A) :=
  rfl
#align category_theory.colimit_adj.extend_along_yoneda_obj CategoryTheory.ColimitAdj.extendAlongYoneda_obj

-- Porting note: adding this lemma because lean 4 ext no longer applies all ext lemmas when
-- stuck (and hence can see through definitional equalities). The previous lemma shows that
-- `(extendAlongYoneda A).obj P` is definitionally a colimit, and the ext lemma is just
-- a special case of `CategoryTheory.Limits.colimit.hom_ext`.
-- See https://github.com/leanprover-community/mathlib4/issues/5229
@[ext] lemma extendAlongYoneda_obj.hom_ext {X : ‚Ñ∞} {P : C·µí·µñ ‚•§ Type u‚ÇÅ}
    {f f' : (extendAlongYoneda A).obj P ‚ü∂ X}
    (w : ‚àÄ j, colimit.Œπ ((CategoryOfElements.œÄ P).leftOp ‚ãô A) j ‚â´ f =
      colimit.Œπ ((CategoryOfElements.œÄ P).leftOp ‚ãô A) j ‚â´ f') : f = f' :=
CategoryTheory.Limits.colimit.hom_ext w

theorem extendAlongYoneda_map {X Y : C·µí·µñ ‚•§ Type u‚ÇÅ} (f : X ‚ü∂ Y) :
    (extendAlongYoneda A).map f =
      colimit.pre ((CategoryOfElements.œÄ Y).leftOp ‚ãô A) (CategoryOfElements.map f).op := by
  ext J
  erw [colimit.Œπ_pre ((CategoryOfElements.œÄ Y).leftOp ‚ãô A) (CategoryOfElements.map f).op]
  dsimp only [extendAlongYoneda, restrictYonedaHomEquiv, IsColimit.homIso', IsColimit.homIso,
    uliftTrivial]
  -- Porting note: in mathlib3 the rest of the proof was `simp, refl`; this is squeezed
  -- and appropriately reordered, presumably because of a non-confluence issue.
  simp only [Adjunction.leftAdjointOfEquiv_map, Iso.symm_mk, Iso.toEquiv_comp, Equiv.coe_trans,
    Equiv.coe_fn_mk, Iso.toEquiv_fun, Equiv.symm_trans_apply, Equiv.coe_fn_symm_mk,
    Iso.toEquiv_symm_fun, id, colimit.isColimit_desc, colimit.Œπ_desc, FunctorToTypes.comp,
    Cocone.extend_Œπ, Cocone.extensions_app, Functor.map_id, Category.comp_id, colimit.cocone_Œπ]
  simp only [Functor.comp_obj, Functor.leftOp_obj, CategoryOfElements.œÄ_obj, colimit.cocone_x,
    Functor.comp_map, Functor.leftOp_map, CategoryOfElements.œÄ_map, Opposite.unop_op,
    Adjunction.leftAdjointOfEquiv_obj, Function.comp_apply, Functor.map_id, comp_id,
    colimit.cocone_Œπ, Functor.op_obj]
  rfl
#align category_theory.colimit_adj.extend_along_yoneda_map CategoryTheory.ColimitAdj.extendAlongYoneda_map

/-- Show `extendAlongYoneda` is left adjoint to `restrictedYoneda`.

The construction of [MM92], Chapter I, Section 5, Theorem 2.
-/
noncomputable def yonedaAdjunction : extendAlongYoneda A ‚ä£ restrictedYoneda A :=
  Adjunction.adjunctionOfEquivLeft _ _
#align category_theory.colimit_adj.yoneda_adjunction CategoryTheory.ColimitAdj.yonedaAdjunction

/--
The initial object in the category of elements for a representable functor. In `isInitial` it is
shown that this is initial.
-/
def Elements.initial (A : C) : (yoneda.obj A).Elements :=
  ‚ü®Opposite.op A, ùüô _‚ü©
#align category_theory.colimit_adj.elements.initial CategoryTheory.ColimitAdj.Elements.initial

/-- Show that `Elements.initial A` is initial in the category of elements for the `yoneda` functor.
-/
def isInitial (A : C) : IsInitial (Elements.initial A) where
  desc s := ‚ü®s.pt.2.op, comp_id _‚ü©
  uniq s m _ := by
    simp_rw [‚Üê m.2]
    dsimp [Elements.initial]
    simp
  fac := by rintro s ‚ü®‚ü®‚ü©‚ü©
#align category_theory.colimit_adj.is_initial CategoryTheory.ColimitAdj.isInitial

/--
`extendAlongYoneda A` is an extension of `A` to the presheaf category along the yoneda embedding.
`uniqueExtensionAlongYoneda` shows it is unique among functors preserving colimits with this
property (up to isomorphism).

The first part of [MM92], Chapter I, Section 5, Corollary 4.
See Property 1 of <https://ncatlab.org/nlab/show/Yoneda+extension#properties>.
-/
noncomputable def isExtensionAlongYoneda :
    (yoneda : C ‚•§ C·µí·µñ ‚•§ Type u‚ÇÅ) ‚ãô extendAlongYoneda A ‚âÖ A :=
  NatIso.ofComponents
    (fun X =>
      (colimit.isColimit _).coconePointUniqueUpToIso
        (colimitOfDiagramTerminal (terminalOpOfInitial (isInitial _)) _))
    (by
      intro X Y f
      -- Porting note: this is slightly different to the `change` in mathlib3 which
      -- didn't work
      change (colimit.desc _ _ ‚â´ _) = colimit.desc _ _ ‚â´ _
      ext
      rw [colimit.Œπ_desc_assoc, colimit.Œπ_desc_assoc]
      change (colimit.Œπ _ _ ‚â´ ùüô _) ‚â´ colimit.desc _ _ = _
      rw [comp_id, colimit.Œπ_desc]
      dsimp
      rw [‚Üê A.map_comp]
      congr 1)
#align category_theory.colimit_adj.is_extension_along_yoneda CategoryTheory.ColimitAdj.isExtensionAlongYoneda

/-- See Property 2 of https://ncatlab.org/nlab/show/Yoneda+extension#properties. -/
noncomputable instance : PreservesColimits (extendAlongYoneda A) :=
  (yonedaAdjunction A).leftAdjointPreservesColimits

/-- Show that the images of `X` after `extendAlongYoneda` and `Lan yoneda` are indeed isomorphic.
This follows from `CategoryTheory.CategoryOfElements.costructuredArrowYonedaEquivalence`.
-/
@[simps]
noncomputable def extendAlongYonedaIsoKanApp (X) :
    (extendAlongYoneda A).obj X ‚âÖ ((lan yoneda : (_ ‚•§ ‚Ñ∞) ‚•§ _).obj A).obj X :=
  let eq := CategoryOfElements.costructuredArrowYonedaEquivalence X
  { hom := colimit.pre (Lan.diagram (yoneda : C ‚•§ _ ‚•§ Type u‚ÇÅ) A X) eq.functor
    inv := colimit.pre ((CategoryOfElements.œÄ X).leftOp ‚ãô A) eq.inverse
    hom_inv_id := by
      erw [colimit.pre_pre ((CategoryOfElements.œÄ X).leftOp ‚ãô A) eq.inverse]
      trans colimit.pre ((CategoryOfElements.œÄ X).leftOp ‚ãô A) (ùü≠ _)
      ¬∑ congr
        exact congr_arg Functor.op (CategoryOfElements.from_toCostructuredArrow_eq X)
      ¬∑ ext
        simp only [colimit.Œπ_pre]
        erw [Category.comp_id]
        congr
    inv_hom_id := by
      erw [colimit.pre_pre (Lan.diagram (yoneda : C ‚•§ _ ‚•§ Type u‚ÇÅ) A X) eq.functor]
      trans colimit.pre (Lan.diagram (yoneda : C ‚•§ _ ‚•§ Type u‚ÇÅ) A X) (ùü≠ _)
      ¬∑ congr
        exact CategoryOfElements.to_fromCostructuredArrow_eq X
      ¬∑ ext
        simp only [colimit.Œπ_pre]
        erw [Category.comp_id]
        congr }
set_option linter.uppercaseLean3 false in
#align category_theory.colimit_adj.extend_along_yoneda_iso_Kan_app CategoryTheory.ColimitAdj.extendAlongYonedaIsoKanApp

/-- Verify that `extendAlongYoneda` is indeed the left Kan extension along the yoneda embedding.
-/
@[simps!]
noncomputable def extendAlongYonedaIsoKan :
    extendAlongYoneda A ‚âÖ (lan yoneda : (_ ‚•§ ‚Ñ∞) ‚•§ _).obj A :=
  NatIso.ofComponents (extendAlongYonedaIsoKanApp A) (by
    intro X Y f; simp
    rw [extendAlongYoneda_map]
    erw [colimit.pre_pre (Lan.diagram (yoneda : C ‚•§ _ ‚•§ Type u‚ÇÅ) A Y) (CostructuredArrow.map f)]
    erw [colimit.pre_pre (Lan.diagram (yoneda : C ‚•§ _ ‚•§ Type u‚ÇÅ) A Y)
        (CategoryOfElements.costructuredArrowYonedaEquivalence Y).functor]
    congr 1
    apply CategoryOfElements.costructuredArrow_yoneda_equivalence_naturality)
set_option linter.uppercaseLean3 false in
#align category_theory.colimit_adj.extend_along_yoneda_iso_Kan CategoryTheory.ColimitAdj.extendAlongYonedaIsoKan

/-- extending `F ‚ãô yoneda` along the yoneda embedding is isomorphic to `Lan F.op`. -/
noncomputable def extendOfCompYonedaIsoLan {D : Type u‚ÇÅ} [SmallCategory D] (F : C ‚•§ D) :
    extendAlongYoneda (F ‚ãô yoneda) ‚âÖ lan F.op :=
  Adjunction.natIsoOfRightAdjointNatIso (yonedaAdjunction (F ‚ãô yoneda))
    (Lan.adjunction (Type u‚ÇÅ) F.op)
    (isoWhiskerRight curriedYonedaLemma' ((whiskeringLeft C·µí·µñ D·µí·µñ (Type u‚ÇÅ)).obj F.op : _))
set_option linter.uppercaseLean3 false in
#align category_theory.colimit_adj.extend_of_comp_yoneda_iso_Lan CategoryTheory.ColimitAdj.extendOfCompYonedaIsoLan

-- Porting note: attaching `[simps!]` directly to the declaration causes a timeout.
attribute [simps!] extendOfCompYonedaIsoLan

end ColimitAdj

open ColimitAdj

/-- `F ‚ãô yoneda` is naturally isomorphic to `yoneda ‚ãô Lan F.op`. -/
@[simps!]
noncomputable def compYonedaIsoYonedaCompLan {D : Type u‚ÇÅ} [SmallCategory D] (F : C ‚•§ D) :
    F ‚ãô yoneda ‚âÖ yoneda ‚ãô lan F.op :=
  (isExtensionAlongYoneda (F ‚ãô yoneda)).symm ‚â™‚â´ isoWhiskerLeft yoneda (extendOfCompYonedaIsoLan F)
set_option linter.uppercaseLean3 false in
#align category_theory.comp_yoneda_iso_yoneda_comp_Lan CategoryTheory.compYonedaIsoYonedaCompLan

/-- Since `extendAlongYoneda A` is adjoint to `restrictedYoneda A`, if we use `A = yoneda`
then `restrictedYoneda A` is isomorphic to the identity, and so `extendAlongYoneda A` is as well.
-/
noncomputable def extendAlongYonedaYoneda : extendAlongYoneda (yoneda : C ‚•§ _) ‚âÖ ùü≠ _ :=
  Adjunction.natIsoOfRightAdjointNatIso (yonedaAdjunction _) Adjunction.id restrictedYonedaYoneda
#align category_theory.extend_along_yoneda_yoneda CategoryTheory.extendAlongYonedaYoneda

-- Maybe this should be reducible or an abbreviation?
/-- A functor to the presheaf category in which everything in the image is representable (witnessed
by the fact that it factors through the yoneda embedding).
`coconeOfRepresentable` gives a cocone for this functor which is a colimit and has point `P`.
-/
def functorToRepresentables (P : C·µí·µñ ‚•§ Type u‚ÇÅ) : P.Elements·µí·µñ ‚•§ C·µí·µñ ‚•§ Type u‚ÇÅ :=
  (CategoryOfElements.œÄ P).leftOp ‚ãô yoneda
#align category_theory.functor_to_representables CategoryTheory.functorToRepresentables

/-- This is a cocone with point `P` for the functor `functorToRepresentables P`. It is shown in
`colimitOfRepresentable P` that this cocone is a colimit: that is, we have exhibited an arbitrary
presheaf `P` as a colimit of representables.

The construction of [MM92], Chapter I, Section 5, Corollary 3.
-/
noncomputable def coconeOfRepresentable (P : C·µí·µñ ‚•§ Type u‚ÇÅ) : Cocone (functorToRepresentables P) :=
  Cocone.extend (colimit.cocone _) (extendAlongYonedaYoneda.hom.app P)
#align category_theory.cocone_of_representable CategoryTheory.coconeOfRepresentable

@[simp]
theorem coconeOfRepresentable_pt (P : C·µí·µñ ‚•§ Type u‚ÇÅ) : (coconeOfRepresentable P).pt = P :=
  rfl
set_option linter.uppercaseLean3 false in
#align category_theory.cocone_of_representable_X CategoryTheory.coconeOfRepresentable_pt

-- Marking this as a simp lemma seems to make things more awkward.
/-- An explicit formula for the legs of the cocone `coconeOfRepresentable`. -/
theorem coconeOfRepresentable_Œπ_app (P : C·µí·µñ ‚•§ Type u‚ÇÅ) (j : P.Elements·µí·µñ) :
    (coconeOfRepresentable P).Œπ.app j = yonedaEquiv.symm j.unop.2 :=
  colimit.Œπ_desc _ _
#align category_theory.cocone_of_representable_Œπ_app CategoryTheory.coconeOfRepresentable_Œπ_app

/-- The legs of the cocone `coconeOfRepresentable` are natural in the choice of presheaf. -/
theorem coconeOfRepresentable_naturality {P‚ÇÅ P‚ÇÇ : C·µí·µñ ‚•§ Type u‚ÇÅ} (Œ± : P‚ÇÅ ‚ü∂ P‚ÇÇ) (j : P‚ÇÅ.Elements·µí·µñ) :
    (coconeOfRepresentable P‚ÇÅ).Œπ.app j ‚â´ Œ± =
      (coconeOfRepresentable P‚ÇÇ).Œπ.app ((CategoryOfElements.map Œ±).op.obj j) := by
  ext T f
  simpa [coconeOfRepresentable_Œπ_app] using FunctorToTypes.naturality _ _ Œ± f.op _
#align category_theory.cocone_of_representable_naturality CategoryTheory.coconeOfRepresentable_naturality

/-- The cocone with point `P` given by `coconeOfRepresentable` is a colimit:
that is, we have exhibited an arbitrary presheaf `P` as a colimit of representables.

The result of [MM92], Chapter I, Section 5, Corollary 3.
-/
noncomputable def colimitOfRepresentable (P : C·µí·µñ ‚•§ Type u‚ÇÅ) :
    IsColimit (coconeOfRepresentable P) := by
  -- Porting note:
  -- the `suffices` was not necessary in mathlib3; the function being `apply`ed has an
  -- `IsIso` input in square brackets; lean 3 was happy to give the user the input as a goal but
  -- lean 4 complains that typeclass inference can't find it.
  suffices IsIso (IsColimit.desc (colimit.isColimit (functorToRepresentables P))
    (coconeOfRepresentable P)) by
    apply IsColimit.ofPointIso (colimit.isColimit (functorToRepresentables P))
  change IsIso (colimit.desc _ (Cocone.extend _ _))
  rw [colimit.desc_extend, colimit.desc_cocone]
  infer_instance
#align category_theory.colimit_of_representable CategoryTheory.colimitOfRepresentable

/-- Given two functors L‚ÇÅ and L‚ÇÇ which preserve colimits, if they agree when restricted to the
representable presheaves then they agree everywhere.
-/
noncomputable def natIsoOfNatIsoOnRepresentables (L‚ÇÅ L‚ÇÇ : (C·µí·µñ ‚•§ Type u‚ÇÅ) ‚•§ ‚Ñ∞)
    [PreservesColimits L‚ÇÅ] [PreservesColimits L‚ÇÇ] (h : yoneda ‚ãô L‚ÇÅ ‚âÖ yoneda ‚ãô L‚ÇÇ) : L‚ÇÅ ‚âÖ L‚ÇÇ := by
  apply NatIso.ofComponents _ _
  ¬∑ intro P
    refine
      (isColimitOfPreserves L‚ÇÅ (colimitOfRepresentable P)).coconePointsIsoOfNatIso
        (isColimitOfPreserves L‚ÇÇ (colimitOfRepresentable P)) ?_
    apply Functor.associator _ _ _ ‚â™‚â´ _
    exact isoWhiskerLeft (CategoryOfElements.œÄ P).leftOp h
  ¬∑ intro P‚ÇÅ P‚ÇÇ f
    apply (isColimitOfPreserves L‚ÇÅ (colimitOfRepresentable P‚ÇÅ)).hom_ext
    intro j
    dsimp only [id, isoWhiskerLeft_hom]
    have :
      (L‚ÇÅ.mapCocone (coconeOfRepresentable P‚ÇÅ)).Œπ.app j ‚â´ L‚ÇÅ.map f =
        (L‚ÇÅ.mapCocone (coconeOfRepresentable P‚ÇÇ)).Œπ.app
          ((CategoryOfElements.map f).op.obj j) := by
      dsimp
      rw [‚Üê L‚ÇÅ.map_comp, coconeOfRepresentable_naturality]
      rfl
    erw [reassoc_of% this, IsColimit.Œπ_map_assoc, IsColimit.Œπ_map]
    dsimp
    rw [‚Üê L‚ÇÇ.map_comp, coconeOfRepresentable_naturality]
    rfl
#align category_theory.nat_iso_of_nat_iso_on_representables CategoryTheory.natIsoOfNatIsoOnRepresentables

variable [HasColimits ‚Ñ∞]

/-- Show that `extendAlongYoneda` is the unique colimit-preserving functor which extends `A` to
the presheaf category.

The second part of [MM92], Chapter I, Section 5, Corollary 4.
See Property 3 of https://ncatlab.org/nlab/show/Yoneda+extension#properties.
-/
noncomputable def uniqueExtensionAlongYoneda (L : (C·µí·µñ ‚•§ Type u‚ÇÅ) ‚•§ ‚Ñ∞) (hL : yoneda ‚ãô L ‚âÖ A)
    [PreservesColimits L] : L ‚âÖ extendAlongYoneda A :=
  natIsoOfNatIsoOnRepresentables _ _ (hL ‚â™‚â´ (isExtensionAlongYoneda _).symm)
#align category_theory.unique_extension_along_yoneda CategoryTheory.uniqueExtensionAlongYoneda

/-- Auxiliary definition for `isLeftAdjointOfPreservesColimits`. -/
noncomputable def adjunctionOfPreservesColimitsAux (L : (C·µí·µñ ‚•§ Type u‚ÇÅ) ‚•§ ‚Ñ∞)
    [PreservesColimits L] : L ‚ä£ restrictedYoneda (yoneda ‚ãô L) :=
  (yonedaAdjunction _).ofNatIsoLeft (uniqueExtensionAlongYoneda _ L (Iso.refl _)).symm
#align category_theory.is_left_adjoint_of_preserves_colimits_aux CategoryTheory.adjunctionOfPreservesColimitsAux

/-- If `L` preserves colimits and `‚Ñ∞` has them, then it is a left adjoint. Note this is a (partial)
converse to `leftAdjointPreservesColimits`.
-/
lemma isLeftAdjointOfPreservesColimits (L : (C ‚•§ Type u‚ÇÅ) ‚•§ ‚Ñ∞) [PreservesColimits L] :
    L.IsLeftAdjoint :=
  ‚ü®_, ‚ü®((opOpEquivalence C).congrLeft.symm.toAdjunction.comp
    (adjunctionOfPreservesColimitsAux ((opOpEquivalence C).congrLeft.functor ‚ãô L))).ofNatIsoLeft
        ((opOpEquivalence C).congrLeft.invFunIdAssoc L)‚ü©‚ü©
#align category_theory.is_left_adjoint_of_preserves_colimits CategoryTheory.isLeftAdjointOfPreservesColimits

end SmallCategory

section ArbitraryUniverses

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] (P : C·µí·µñ ‚•§ Type v‚ÇÅ)

/-- For a presheaf `P`, consider the forgetful functor from the category of representable
    presheaves over `P` to the category of presheaves. There is a tautological cocone over this
    functor whose leg for a natural transformation `V ‚ü∂ P` with `V` representable is just that
    natural transformation. -/
@[simps]
def tautologicalCocone : Cocone (CostructuredArrow.proj yoneda P ‚ãô yoneda) where
  pt := P
  Œπ := { app := fun X => X.hom }

/-- The tautological cocone with point `P` is a colimit cocone, exhibiting `P` as a colimit of
    representables.

    Proposition 2.6.3(i) in [Kashiwara2006] -/
def isColimitTautologicalCocone : IsColimit (tautologicalCocone P) where
  desc := fun s => by
    refine ‚ü®fun X t => yonedaEquiv (s.Œπ.app (CostructuredArrow.mk (yonedaEquiv.symm t))), ?_‚ü©
    intros X Y f
    ext t
    dsimp
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [yonedaEquiv_naturality', yonedaEquiv_symm_map]
    simpa using (s.Œπ.naturality
      (CostructuredArrow.homMk' (CostructuredArrow.mk (yonedaEquiv.symm t)) f.unop)).symm
  fac := by
    intro s t
    dsimp
    apply yonedaEquiv.injective
    rw [yonedaEquiv_comp]
    dsimp only
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [Equiv.symm_apply_apply]
    rfl
  uniq := by
    intro s j h
    ext V x
    obtain ‚ü®t, rfl‚ü© := yonedaEquiv.surjective x
    dsimp
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [Equiv.symm_apply_apply, ‚Üê yonedaEquiv_comp]
    exact congr_arg _ (h (CostructuredArrow.mk t))

variable {I : Type v‚ÇÅ} [SmallCategory I] (F : I ‚•§ C)

/-- Given a functor `F : I ‚•§ C`, a cocone `c` on `F ‚ãô yoneda : I ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ` induces a
    functor `I ‚•§ CostructuredArrow yoneda c.pt` which maps `i : I` to the leg
    `yoneda.obj (F.obj i) ‚ü∂ c.pt`. If `c` is a colimit cocone, then that functor is
    final.

    Proposition 2.6.3(ii) in [Kashiwara2006] -/
theorem final_toCostructuredArrow_comp_pre {c : Cocone (F ‚ãô yoneda)} (hc : IsColimit c) :
    Functor.Final (c.toCostructuredArrow ‚ãô CostructuredArrow.pre F yoneda c.pt) := by
  apply Functor.cofinal_of_isTerminal_colimit_comp_yoneda

  suffices IsTerminal (colimit ((c.toCostructuredArrow ‚ãô CostructuredArrow.pre F yoneda c.pt) ‚ãô
      CostructuredArrow.toOver yoneda c.pt)) by
    apply IsTerminal.isTerminalOfObj (overEquivPresheafCostructuredArrow c.pt).inverse
    apply IsTerminal.ofIso this
    refine ?_ ‚â™‚â´ (preservesColimitIso (overEquivPresheafCostructuredArrow c.pt).inverse _).symm
    apply HasColimit.isoOfNatIso
    exact isoWhiskerLeft _
      (CostructuredArrow.toOverCompOverEquivPresheafCostructuredArrow c.pt).isoCompInverse

  apply IsTerminal.ofIso Over.mkIdTerminal
  let isc : IsColimit ((Over.forget _).mapCocone _) := PreservesColimit.preserves
    (colimit.isColimit ((c.toCostructuredArrow ‚ãô CostructuredArrow.pre F yoneda c.pt) ‚ãô
      CostructuredArrow.toOver yoneda c.pt))
  exact Over.isoMk (hc.coconePointUniqueUpToIso isc) (hc.hom_ext fun i => by simp)

end ArbitraryUniverses

end CategoryTheory
