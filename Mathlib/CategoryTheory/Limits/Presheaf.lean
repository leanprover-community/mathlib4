/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Jo√´l Riou
-/
import Mathlib.CategoryTheory.Comma.Presheaf.Basic
import Mathlib.CategoryTheory.Elements
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Limits.Over

/-!
# Colimit of representables

In this file, We show that every presheaf of types on a category `C` (with `Category.{v‚ÇÅ} C`)
is a colimit of representables. This result is also known as the density theorem,
the co-Yoneda lemma and the Ninja Yoneda lemma. Three formulations are given:
* `colimitOfRepresentable` uses the category of elements of a functor to types;
* `isColimitTautologicalCocone` uses the category of costructured arrows
for `yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ`;
* `isColimitTautologicalCocone'` uses the category of costructured arrows
for `uliftYoneda : C ‚•§ C·µí·µñ ‚•§ Type max w v‚ÇÅ`, when the presheaf has values
in `Type (max w v‚ÇÅ)`;

In this file, we also study the left Kan extensions of functors `A : C ‚•§ ‚Ñ∞`
along the Yoneda embedding `uliftYoneda : C ‚•§ C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`
(when `Category.{v‚ÇÇ} ‚Ñ∞` and `w` is an auxiliary universe). In particular,
the definition `uliftYonedaAdjunction` shows that such a pointwise left Kan
extension  (which exists when `‚Ñ∞` has colimits) is a left adjoint to the
functor `restrictedULiftYoneda : ‚Ñ∞ ‚•§ C·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)`.

In the lemma `isLeftKanExtension_along_uliftYoneda_iff`, we show that
if `L : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞` and `Œ± : A ‚ü∂ uliftYoneda ‚ãô L`, then
`Œ±` makes `L` the left Kan extension of `L` along yoneda if and only if
`Œ±` is an isomorphism (i.e. `L` extends `A`) and `L` preserves colimits.
`uniqueExtensionAlongULiftYoneda` shows `uliftYoneda.leftKanExtension A` is
unique amongst functors preserving colimits with this property, establishing the
presheaf category as the free cocompletion of a category.

Given a functor `F : C ‚•§ D`, we also show construct an isomorphism
`compULiftYonedaIsoULiftYonedaCompLan : F ‚ãô uliftYoneda ‚âÖ uliftYoneda ‚ãô F.op.lan`, and
show that it makes `F.op.lan` a left Kan extension of `F ‚ãô uliftYoneda`.

## Tags
colimit, representable, presheaf, free cocompletion

## References
* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]
* https://ncatlab.org/nlab/show/Yoneda+extension
-/

namespace CategoryTheory

open Category Limits Opposite

universe w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

namespace Presheaf

variable {‚Ñ∞ : Type u‚ÇÇ} [Category.{v‚ÇÇ} ‚Ñ∞] (A : C ‚•§ ‚Ñ∞)

/--
Given a functor `A : C ‚•§ ‚Ñ∞` (with `Category.{v‚ÇÇ} ‚Ñ∞`) and a auxiliary universe `w`,
this is the functor `‚Ñ∞ ‚•§ C·µí·µñ ‚•§ Type (max w v‚ÇÇ)` which sends `(E : ‚Ñ∞) (c : C·µí·µñ)`
to the homset `A.obj C ‚ü∂ E` (considered in the higher universe `max w v‚ÇÇ`).
Under the existence of a suitable pointwise left Kan extension, it is shown in
`uliftYonedaAdjunction` that this functor has a left adjoint.

Defined as in [MM92], Chapter I, Section 5, Theorem 2.
-/
@[simps! obj_map map_app]
def restrictedULiftYoneda : ‚Ñ∞ ‚•§ C·µí·µñ ‚•§ Type (max w v‚ÇÇ) :=
    uliftYoneda.{w} ‚ãô (Functor.whiskeringLeft _ _ _).obj A.op

@[reassoc]
lemma map_comp_uliftYonedaEquiv_down (E : ‚Ñ∞) {X Y : C} (f : X ‚ü∂ Y)
    (g : uliftYoneda.{max w v‚ÇÇ}.obj Y ‚ü∂ (restrictedULiftYoneda.{max w v‚ÇÅ} A).obj E) :
    A.map f ‚â´ (uliftYonedaEquiv g).down =
      (uliftYonedaEquiv (uliftYoneda.map f ‚â´ g)).down := by
  have this := congr_fun (g.naturality f.op) (ULift.up (ùüô Y))
  dsimp [uliftYonedaEquiv, uliftYoneda] at this ‚ä¢
  simp only [comp_id] at this
  simp [id_comp, this]

/-- Auxiliary definition for `restrictedULiftYonedaHomEquiv`. -/
def restrictedULiftYonedaHomEquiv' (P : C·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)) (E : ‚Ñ∞) :
    (CostructuredArrow.proj uliftYoneda.{max w v‚ÇÇ} P ‚ãô A ‚ü∂
      (Functor.const (CostructuredArrow uliftYoneda.{max w v‚ÇÇ} P)).obj E) ‚âÉ
      (P ‚ü∂ (restrictedULiftYoneda.{max w v‚ÇÅ} A).obj E) where
  toFun f :=
    { app _ x := ULift.up (f.app (CostructuredArrow.mk (uliftYonedaEquiv.symm x)))
      naturality _ _ g := by
        ext x
        let œÜ : CostructuredArrow.mk (uliftYonedaEquiv.{max w v‚ÇÇ}.symm (P.map g x)) ‚ü∂
          CostructuredArrow.mk (uliftYonedaEquiv.symm x) :=
            CostructuredArrow.homMk g.unop (by
              dsimp
              rw [uliftYonedaEquiv_symm_map])
        dsimp
        congr 1
        simpa using (f.naturality œÜ).symm }
  invFun g :=
    { app y := (uliftYonedaEquiv.{max w v‚ÇÇ} (y.hom ‚â´ g)).down
      naturality y y' f := by
        dsimp
        rw [comp_id, ‚Üê CostructuredArrow.w f, assoc, map_comp_uliftYonedaEquiv_down] }
  left_inv f := by
    ext X
    let e : CostructuredArrow.mk
      (uliftYonedaEquiv.{max w v‚ÇÇ}.symm (X.hom.app (op X.left) ‚ü®ùüô X.left‚ü©)) ‚âÖ X :=
        CostructuredArrow.isoMk (Iso.refl _) (by
          ext Y x
          dsimp
          rw [‚Üê FunctorToTypes.naturality]
          congr )
    simpa [e] using f.naturality e.inv
  right_inv g := by
    ext X x
    apply ULift.down_injective
    simp [uliftYonedaEquiv]

@[reassoc]
lemma restrictedULiftYonedaHomEquiv'_symm_naturality_right (P : C·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)) {E E' : ‚Ñ∞}
    (g : E ‚ü∂ E') (f : (P ‚ü∂ (restrictedULiftYoneda.{max w v‚ÇÅ} A).obj E)) :
    (restrictedULiftYonedaHomEquiv' A P E').symm (f ‚â´ (restrictedULiftYoneda A).map g) =
      (restrictedULiftYonedaHomEquiv' A P E).symm f ‚â´ (Functor.const _ ).map g := by
  rfl

@[reassoc]
lemma restrictedULiftYonedaHomEquiv'_symm_app_naturality_left
    {P Q : C·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)} (f : P ‚ü∂ Q) (E : ‚Ñ∞)
    (g : Q ‚ü∂ (restrictedULiftYoneda.{max w v‚ÇÅ} A).obj E)
    (p : CostructuredArrow uliftYoneda.{max w v‚ÇÇ} P) :
    ((restrictedULiftYonedaHomEquiv' A P E).symm (f ‚â´ g)).app p =
      ((restrictedULiftYonedaHomEquiv' A Q E).symm g).app
        ((CostructuredArrow.map f).obj p) :=
  rfl

section

variable (P : ‚Ñ∞·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ))

example [HasColimitsOfSize.{v‚ÇÅ, max u‚ÇÅ v‚ÇÅ v‚ÇÇ w} ‚Ñ∞] :
    (uliftYoneda.{max w v‚ÇÇ}).HasPointwiseLeftKanExtension A := by
  infer_instance

variable [(uliftYoneda.{max w v‚ÇÇ}).HasPointwiseLeftKanExtension A]

variable {A}
variable (L : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞)
  (Œ± : A ‚ü∂ uliftYoneda.{max w v‚ÇÇ} ‚ãô L) [L.IsLeftKanExtension Œ±]

/-- Auxiliary definition for `uliftYonedaAdjunction`. -/
noncomputable def restrictedULiftYonedaHomEquiv (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) (E : ‚Ñ∞) :
    (L.obj P ‚ü∂ E) ‚âÉ (P ‚ü∂ (restrictedULiftYoneda.{max w v‚ÇÅ} A).obj E) :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension _ Œ± P).homEquiv.trans
    (restrictedULiftYonedaHomEquiv' A P E)

/-- If `L : (C·µí·µñ ‚•§ Type max v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞` is a pointwise left Kan extension
of a functor `A : C ‚•§ ‚Ñ∞` along the Yoneda embedding,
then `L` is a left adjoint of `restrictedULiftYoneda A : ‚Ñ∞ ‚•§ C·µí·µñ ‚•§ Type max v‚ÇÅ v‚ÇÇ` -/
noncomputable def uliftYonedaAdjunction : L ‚ä£ restrictedULiftYoneda.{max w v‚ÇÅ} A :=
  Adjunction.mkOfHomEquiv
    { homEquiv := restrictedULiftYonedaHomEquiv L Œ±
      homEquiv_naturality_left_symm {P Q X} f g := by
        apply (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Œ± P).hom_ext
        intro p
        have hfg := (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension
          L Œ± P).comp_homEquiv_symm ((restrictedULiftYonedaHomEquiv' A P X).symm (f ‚â´ g)) p
        have hg := (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension
          L Œ± Q).comp_homEquiv_symm ((restrictedULiftYonedaHomEquiv' A Q X).symm g)
            ((CostructuredArrow.map f).obj p)
        dsimp at hfg hg
        dsimp [restrictedULiftYonedaHomEquiv]
        simp only [assoc, hfg, ‚Üê L.map_comp_assoc, hg,
          restrictedULiftYonedaHomEquiv'_symm_app_naturality_left]
      homEquiv_naturality_right {P X Y} f g := by
        have := @IsColimit.homEquiv_symm_naturality (h :=
          Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Œ± P)
        dsimp at this
        apply (restrictedULiftYonedaHomEquiv L Œ± P Y).symm.injective
        apply (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Œ± P).hom_ext
        intro
        simp [restrictedULiftYonedaHomEquiv,
          restrictedULiftYonedaHomEquiv'_symm_naturality_right, this] }

@[simp]
lemma uliftYonedaAdjunction_homEquiv_app {P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ}
    {Y : ‚Ñ∞} (f : L.obj P ‚ü∂ Y) {Z : C·µí·µñ} (z : P.obj Z) :
    ((uliftYonedaAdjunction.{w} L Œ±).homEquiv P Y f).app Z z =
      ULift.up (Œ±.app Z.unop ‚â´ L.map (uliftYonedaEquiv.symm z) ‚â´ f) := by
  simp [uliftYonedaAdjunction, restrictedULiftYonedaHomEquiv,
    restrictedULiftYonedaHomEquiv', IsColimit.homEquiv]

@[simp]
lemma uliftYonedaAdjunction_unit_app_app (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ)
    {Z : C·µí·µñ} (z : P.obj Z) :
    ((uliftYonedaAdjunction.{w} L Œ±).unit.app P).app Z z =
      ULift.up (Œ±.app Z.unop ‚â´ L.map (uliftYonedaEquiv.symm z)) := by
  have h‚ÇÅ := (uliftYonedaAdjunction.{w} L Œ±).homEquiv_unit P _ (ùüô _)
  simp only [Functor.comp_obj, Functor.map_id, comp_id] at h‚ÇÅ
  simp [‚Üê h‚ÇÅ]

include Œ± in
/-- Any left Kan extension along the Yoneda embedding preserves colimits. -/
lemma preservesColimitsOfSize_of_isLeftKanExtension :
    PreservesColimitsOfSize.{v‚ÇÉ, u‚ÇÉ} L :=
  (uliftYonedaAdjunction L Œ±).leftAdjoint_preservesColimits

lemma isIso_of_isLeftKanExtension : IsIso Œ± :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension _ Œ±).isIso_hom

variable (A)

/-- See Property 2 of https://ncatlab.org/nlab/show/Yoneda+extension#properties. -/
instance preservesColimitsOfSize_leftKanExtension :
    PreservesColimitsOfSize.{v‚ÇÉ, u‚ÇÉ} (uliftYoneda.{max w v‚ÇÇ}.leftKanExtension A) :=
  (uliftYonedaAdjunction _ (uliftYoneda.leftKanExtensionUnit A)).leftAdjoint_preservesColimits

instance : IsIso (uliftYoneda.{max w v‚ÇÇ}.leftKanExtensionUnit A) :=
  isIso_of_isLeftKanExtension _ (uliftYoneda.leftKanExtensionUnit A)

/-- A pointwise left Kan extension along the Yoneda embedding is an extension. -/
noncomputable def isExtensionAlongULiftYoneda :
    uliftYoneda.{max w v‚ÇÇ} ‚ãô uliftYoneda.leftKanExtension A ‚âÖ A :=
  (asIso (uliftYoneda.leftKanExtensionUnit A)).symm

end

/-- Given `P : C·µí·µñ ‚•§ Type max w v‚ÇÅ`, this is the functor from the opposite category
of the category of elements of `X` which sends an element in `P.obj (op X)` to the
presheaf represented by `X`. The definition`coconeOfRepresentable`
gives a cocone for this functor which is a colimit and has point `P`.
-/
@[simps! obj map]
def functorToRepresentables (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ) :
    P.Elements·µí·µñ ‚•§ C·µí·µñ ‚•§ Type (max w v‚ÇÅ) :=
  (CategoryOfElements.œÄ P).leftOp ‚ãô uliftYoneda.{w}

/-- This is a cocone with point `P` for the functor `functorToRepresentables P`. It is shown in
`colimitOfRepresentable P` that this cocone is a colimit: that is, we have exhibited an arbitrary
presheaf `P` as a colimit of representables.

The construction of [MM92], Chapter I, Section 5, Corollary 3.
-/
@[simps]
def coconeOfRepresentable (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ) :
    Cocone (functorToRepresentables P) where
  pt := P
  Œπ :=
    { app x := uliftYonedaEquiv.symm x.unop.2
      naturality {x‚ÇÅ x‚ÇÇ} f := by
        dsimp
        rw [comp_id, ‚Üê uliftYonedaEquiv_symm_map, f.unop.2] }

/-- The legs of the cocone `coconeOfRepresentable` are natural in the choice of presheaf. -/
theorem coconeOfRepresentable_naturality
    {P‚ÇÅ P‚ÇÇ : C·µí·µñ ‚•§ Type max w v‚ÇÅ} (Œ± : P‚ÇÅ ‚ü∂ P‚ÇÇ) (j : P‚ÇÅ.Elements·µí·µñ) :
    (coconeOfRepresentable P‚ÇÅ).Œπ.app j ‚â´ Œ± =
      (coconeOfRepresentable P‚ÇÇ).Œπ.app ((CategoryOfElements.map Œ±).op.obj j) := by
  ext T f
  simp [uliftYonedaEquiv, FunctorToTypes.naturality]

/-- The cocone with point `P` given by `coconeOfRepresentable` is a colimit:
that is, we have exhibited an arbitrary presheaf `P` as a colimit of representables.

The result of [MM92], Chapter I, Section 5, Corollary 3.
-/
def colimitOfRepresentable (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ) :
    IsColimit (coconeOfRepresentable P) where
  desc s :=
    { app X x := uliftYonedaEquiv (s.Œπ.app (Opposite.op (Functor.elementsMk P X x)))
      naturality X Y f := by
        ext x
        have := s.w (Quiver.Hom.op (CategoryOfElements.homMk (P.elementsMk X x)
          (P.elementsMk Y (P.map f x)) f rfl))
        dsimp at this x ‚ä¢
        rw [‚Üê this, uliftYonedaEquiv_comp]
        dsimp
        rw [uliftYonedaEquiv_apply, ‚Üê FunctorToTypes.naturality,
          uliftYonedaEquiv_uliftYoneda_map]
        simp [uliftYoneda] }
  fac s j := by
    ext X x
    let œÜ : j.unop ‚ü∂ (Functor.elementsMk P _
      ((uliftYonedaEquiv.symm (unop j).snd).app X x)) := ‚ü®x.down.op, rfl‚ü©
    have := s.w œÜ.op
    dsimp [œÜ] at this x ‚ä¢
    rw [‚Üê this, uliftYonedaEquiv_apply]
    dsimp [uliftYoneda]
    rw [id_comp]
  uniq s m hm := by
    ext X x
    dsimp at hm ‚ä¢
    rw [‚Üê hm, uliftYonedaEquiv_comp, Equiv.apply_symm_apply]

variable {A : C ‚•§ ‚Ñ∞}

example [HasColimitsOfSize.{v‚ÇÅ, max w u‚ÇÅ v‚ÇÅ v‚ÇÇ} ‚Ñ∞] :
    uliftYoneda.{max w v‚ÇÇ}.HasPointwiseLeftKanExtension A := by
  infer_instance

variable [uliftYoneda.{max w v‚ÇÇ}.HasPointwiseLeftKanExtension A]

section

variable (L : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞) (Œ± : A ‚ü∂ uliftYoneda.{max w v‚ÇÇ} ‚ãô L)

instance [L.IsLeftKanExtension Œ±] : IsIso Œ± :=
  (Functor.isPointwiseLeftKanExtensionOfIsLeftKanExtension L Œ±).isIso_hom

lemma isLeftKanExtension_along_uliftYoneda_iff :
    L.IsLeftKanExtension Œ± ‚Üî
      (IsIso Œ± ‚àß PreservesColimitsOfSize.{v‚ÇÅ, max w u‚ÇÅ v‚ÇÅ v‚ÇÇ} L) := by
  constructor
  ¬∑ intro
    exact ‚ü®inferInstance, preservesColimits_of_natIso (Functor.leftKanExtensionUnique _
      (uliftYoneda.{max w v‚ÇÇ}.leftKanExtensionUnit A) _ Œ±)‚ü©
  ¬∑ rintro ‚ü®_, _‚ü©
    apply Functor.LeftExtension.IsPointwiseLeftKanExtension.isLeftKanExtension
      (E := Functor.LeftExtension.mk _ Œ±)
    intro P
    dsimp [Functor.LeftExtension.IsPointwiseLeftKanExtensionAt]
    apply IsColimit.ofWhiskerEquivalence
      (CategoryOfElements.costructuredArrowULiftYonedaEquivalence _)
    let e : (CategoryOfElements.costructuredArrowULiftYonedaEquivalence P).functor ‚ãô
      CostructuredArrow.proj uliftYoneda.{max w v‚ÇÇ} P ‚ãô A ‚âÖ
        functorToRepresentables.{max w v‚ÇÇ} P ‚ãô L :=
      Functor.isoWhiskerLeft _ (Functor.isoWhiskerLeft _ (asIso Œ±)) ‚â™‚â´
        Functor.isoWhiskerLeft _ (Functor.associator _ _ _).symm ‚â™‚â´
        (Functor.associator _ _ _).symm ‚â™‚â´ Functor.isoWhiskerRight (Iso.refl _) L
    refine (IsColimit.precomposeHomEquiv e.symm _).1 ?_
    exact IsColimit.ofIsoColimit (isColimitOfPreserves L (colimitOfRepresentable.{max w v‚ÇÇ} P))
      (Cocones.ext (Iso.refl _))

lemma isLeftKanExtension_of_preservesColimits
    (L : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞) (e : A ‚âÖ uliftYoneda.{max w v‚ÇÇ} ‚ãô L)
    [PreservesColimitsOfSize.{v‚ÇÅ, max w u‚ÇÅ v‚ÇÅ v‚ÇÇ} L] :
    L.IsLeftKanExtension e.hom := by
  rw [isLeftKanExtension_along_uliftYoneda_iff]
  exact ‚ü®inferInstance, ‚ü®inferInstance‚ü©‚ü©

end

/-- Show that `uliftYoneda.leftKanExtension A` is the unique colimit-preserving
functor which extends `A` to the presheaf category.

The second part of [MM92], Chapter I, Section 5, Corollary 4.
See Property 3 of https://ncatlab.org/nlab/show/Yoneda+extension#properties.
-/
noncomputable def uniqueExtensionAlongULiftYoneda (L : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞)
    (e : A ‚âÖ uliftYoneda.{max w v‚ÇÇ} ‚ãô L)
    [PreservesColimitsOfSize.{v‚ÇÅ, max w u‚ÇÅ v‚ÇÅ v‚ÇÇ} L] :
    L ‚âÖ uliftYoneda.{max w v‚ÇÇ}.leftKanExtension A :=
  have := isLeftKanExtension_of_preservesColimits L e
  Functor.leftKanExtensionUnique _ e.hom _ (uliftYoneda.leftKanExtensionUnit A)

instance (L : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞) [PreservesColimitsOfSize.{v‚ÇÅ, max w u‚ÇÅ v‚ÇÅ v‚ÇÇ} L]
    [uliftYoneda.{max w v‚ÇÇ}.HasPointwiseLeftKanExtension (uliftYoneda.{max w v‚ÇÇ} ‚ãô L)] :
    L.IsLeftKanExtension (ùüô _ : uliftYoneda.{max w v‚ÇÇ} ‚ãô L ‚ü∂ _) :=
  isLeftKanExtension_of_preservesColimits _ (Iso.refl _)

/-- If `L` preserves colimits and `‚Ñ∞` has them, then it is a left adjoint. Note this is a (partial)
converse to `leftAdjointPreservesColimits`.
-/
lemma isLeftAdjoint_of_preservesColimits (L : (C ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ ‚Ñ∞)
    [PreservesColimitsOfSize.{v‚ÇÅ, max w u‚ÇÅ v‚ÇÅ v‚ÇÇ} L]
    [uliftYoneda.{max w v‚ÇÇ}.HasPointwiseLeftKanExtension
      (uliftYoneda.{max w v‚ÇÇ} ‚ãô (opOpEquivalence C).congrLeft.functor.comp L)] :
    L.IsLeftAdjoint :=
  ‚ü®_, ‚ü®((opOpEquivalence C).congrLeft.symm.toAdjunction.comp
    (uliftYonedaAdjunction _ (ùüô _))).ofNatIsoLeft
      ((opOpEquivalence C).congrLeft.invFunIdAssoc L)‚ü©‚ü©

section

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} D] (F : C ‚•§ D)

instance (X : C) (Y : F.op.LeftExtension (yoneda.obj X)) :
    Unique (Functor.LeftExtension.mk _ (yonedaMap F X) ‚ü∂ Y) where
  default := StructuredArrow.homMk
      (yonedaEquiv.symm (yonedaEquiv (F := F.op.comp Y.right) Y.hom)) (by
        ext Z f
        simpa using congr_fun (Y.hom.naturality f.op).symm (ùüô _))
  uniq œÜ := by
    ext1
    apply yonedaEquiv.injective
    simp [‚Üê StructuredArrow.w œÜ, yonedaEquiv]

/-- Given `F : C ‚•§ D` and `X : C`, `yoneda.obj (F.obj X) : D·µí·µñ ‚•§ Type _` is the
left Kan extension of `yoneda.obj X : C·µí·µñ ‚•§ Type _` along `F.op`. -/
instance (X : C) : (yoneda.obj (F.obj X)).IsLeftKanExtension (yonedaMap F X) :=
  ‚ü®‚ü®Limits.IsInitial.ofUnique _‚ü©‚ü©

end

section

variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : C ‚•§ D)

instance (X : C) (Y : F.op.LeftExtension (uliftYoneda.{max w v‚ÇÇ}.obj X)) :
    Unique (Functor.LeftExtension.mk _ (uliftYonedaMap.{w} F X) ‚ü∂ Y) where
  default := StructuredArrow.homMk
    (uliftYonedaEquiv.symm (uliftYonedaEquiv (F := F.op ‚ãô Y.right) Y.hom)) (by
      ext Z ‚ü®f‚ü©
      simpa [uliftYonedaEquiv, uliftYoneda] using
        congr_fun (Y.hom.naturality f.op).symm (ULift.up (ùüô _)) )
  uniq œÜ := by
    ext : 1
    apply uliftYonedaEquiv.injective
    simp [‚Üê StructuredArrow.w œÜ, uliftYonedaEquiv, uliftYonedaMap]

/-- Given `F : C ‚•§ D` and `X : C`, `uliftYoneda.obj (F.obj X) : D·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)` is the
left Kan extension of `uliftYoneda.obj X : C·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)` along `F.op`. -/
instance (X : C) : (uliftYoneda.{max w v‚ÇÅ}.obj (F.obj X)).IsLeftKanExtension
    (uliftYonedaMap.{w} F X) :=
  ‚ü®‚ü®Limits.IsInitial.ofUnique _‚ü©‚ü©

section
variable [‚àÄ (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ), F.op.HasLeftKanExtension P]

/-- `F ‚ãô uliftYoneda` is naturally isomorphic to `uliftYoneda ‚ãô F.op.lan`. -/
noncomputable def compULiftYonedaIsoULiftYonedaCompLan :
    F ‚ãô uliftYoneda.{max w v‚ÇÅ} ‚âÖ uliftYoneda.{max w v‚ÇÇ} ‚ãô F.op.lan :=
  NatIso.ofComponents (fun X ‚Ü¶ Functor.leftKanExtensionUnique _
    (uliftYonedaMap.{w} F X) (F.op.lan.obj _) (F.op.lanUnit.app (uliftYoneda.obj X)))
    (fun {X Y} f ‚Ü¶ by
      apply uliftYonedaEquiv.injective
      have eq‚ÇÅ := congr_fun
        ((uliftYoneda.{max w v‚ÇÅ}.obj (F.obj Y)).descOfIsLeftKanExtension_fac_app
        (uliftYonedaMap F Y) (F.op.lan.obj (uliftYoneda.obj Y))
          (F.op.lanUnit.app (uliftYoneda.obj Y)) _) ‚ü®f‚ü©
      have eq‚ÇÇ := congr_fun
        (((uliftYoneda.{max w v‚ÇÅ}.obj (F.obj X)).descOfIsLeftKanExtension_fac_app
        (uliftYonedaMap F X) (F.op.lan.obj (uliftYoneda.obj X))
          (F.op.lanUnit.app (uliftYoneda.obj X))) _) ‚ü®ùüô _‚ü©
      have eq‚ÇÉ := congr_fun (congr_app (F.op.lanUnit.naturality
        (uliftYoneda.{max w v‚ÇÇ}.map f)) _) ‚ü®ùüô _‚ü©
      dsimp [uliftYoneda, uliftYonedaMap, uliftYonedaEquiv,
        Functor.leftKanExtensionUnique] at eq‚ÇÅ eq‚ÇÇ eq‚ÇÉ ‚ä¢
      simp only [Functor.map_id] at eq‚ÇÇ
      simp only [id_comp] at eq‚ÇÉ
      simp [eq‚ÇÅ, eq‚ÇÇ, eq‚ÇÉ])

@[simp]
lemma compULiftYonedaIsoULiftYonedaCompLan_inv_app_app_apply_eq_id (X : C) :
    ((compULiftYonedaIsoULiftYonedaCompLan.{w} F).inv.app X).app (op (F.obj X))
          ((F.op.lanUnit.app ((uliftYoneda.{max w v‚ÇÇ}).obj X)).app (op X)
        (ULift.up (ùüô X))) = ULift.up (ùüô (F.obj X)) :=
  (congr_fun (Functor.descOfIsLeftKanExtension_fac_app _
    (F.op.lanUnit.app ((uliftYoneda.{max w v‚ÇÇ}).obj X)) _
    (uliftYonedaMap.{w} F X) (op X)) (ULift.up (ùüô X))).trans (by simp [uliftYonedaMap])

end

namespace compULiftYonedaIsoULiftYonedaCompLan

variable {F}

section

variable {X : C} {G : (C·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)) ‚•§ D·µí·µñ ‚•§ Type (max w v‚ÇÅ v‚ÇÇ)}
  (œÜ : F ‚ãô uliftYoneda.{max w v‚ÇÅ} ‚ü∂ uliftYoneda.{max w v‚ÇÇ} ‚ãô G)

/-- Auxiliary definition for `presheafHom`. -/
def coconeApp {P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ} (x : P.Elements) :
    uliftYoneda.{max w v‚ÇÇ}.obj x.1.unop ‚ü∂ F.op ‚ãô G.obj P :=
  uliftYonedaEquiv.symm
    ((G.map (uliftYonedaEquiv.{max w v‚ÇÇ}.symm x.2)).app _
      ((œÜ.app x.1.unop).app _ (ULift.up (ùüô _))))

@[reassoc (attr := simp)]
lemma coconeApp_naturality {P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ} {x y : P.Elements} (f : x ‚ü∂ y) :
    uliftYoneda.map f.1.unop ‚â´ coconeApp.{w} œÜ x = coconeApp œÜ y := by
  have eq‚ÇÅ : uliftYoneda.map f.1.unop ‚â´ uliftYonedaEquiv.symm x.2 =
      uliftYonedaEquiv.{max w v‚ÇÇ}.symm y.2 :=
    uliftYonedaEquiv.injective
      (by simpa only [Equiv.apply_symm_apply, ‚Üê uliftYonedaEquiv_naturality] using f.2)
  have eq‚ÇÇ := congr_fun ((G.map (uliftYonedaEquiv.{max w v‚ÇÇ}.symm x.2)).naturality
    (F.map f.1.unop).op) ((œÜ.app x.1.unop).app _ (ULift.up (ùüô _)))
  have eq‚ÇÉ := congr_fun (congr_app (œÜ.naturality f.1.unop) _) (ULift.up (ùüô _))
  have eq‚ÇÑ := congr_fun ((œÜ.app x.1.unop).naturality (F.map f.1.unop).op)
  dsimp at eq‚ÇÇ eq‚ÇÉ eq‚ÇÑ
  apply uliftYonedaEquiv.{max w v‚ÇÇ}.injective
  dsimp only [coconeApp]
  rw [Equiv.apply_symm_apply, ‚Üê uliftYonedaEquiv_naturality, Equiv.apply_symm_apply]
  simp only [‚Üê eq‚ÇÅ, ‚Üê eq‚ÇÇ, ‚Üê eq‚ÇÉ, ‚Üê eq‚ÇÑ, op_unop, Functor.comp_obj,
    Functor.op_obj, yoneda_obj_obj, Functor.comp_map,
    Functor.op_map, Functor.map_comp, FunctorToTypes.comp,]
  simp [uliftYoneda]

/-- Given functors `F : C ‚•§ D` and `G : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ (D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ)`,
and a natural transformation `œÜ : F ‚ãô uliftYoneda ‚ü∂ uliftYoneda ‚ãô G`, this is the
(natural) morphism `P ‚ü∂ F.op ‚ãô G.obj P` for all `P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ` that is
determined by `œÜ`. -/
def presheafHom (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) : P ‚ü∂ F.op ‚ãô G.obj P :=
  (colimitOfRepresentable P).desc
    (Cocone.mk _ { app x := coconeApp.{w} œÜ x.unop })

lemma uliftYonedaEquiv_Œπ_presheafHom (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) {X : C}
    (f : uliftYoneda.{max w v‚ÇÇ}.obj X ‚ü∂ P) :
    uliftYonedaEquiv (f ‚â´ presheafHom.{w} œÜ P) =
      (G.map f).app (Opposite.op (F.obj X)) ((œÜ.app X).app _ (ULift.up (ùüô _))) := by
  obtain ‚ü®x, rfl‚ü© := uliftYonedaEquiv.symm.surjective f
  erw [(colimitOfRepresentable P).fac _ (Opposite.op (P.elementsMk _ x))]
  dsimp only [coconeApp]
  apply Equiv.apply_symm_apply

lemma uliftYonedaEquiv_presheafHom_uliftYoneda_obj (X : C) :
    uliftYonedaEquiv.{max w v‚ÇÇ} (presheafHom.{w} œÜ (uliftYoneda.{max w v‚ÇÇ}.obj X)) =
      ((œÜ.app X).app (F.op.obj (Opposite.op X)) (ULift.up (ùüô _))) := by
  simpa using uliftYonedaEquiv_Œπ_presheafHom.{w} œÜ (uliftYoneda.obj X) (ùüô _)

@[reassoc (attr := simp)]
lemma presheafHom_naturality {P Q : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ} (f : P ‚ü∂ Q) :
    presheafHom.{w} œÜ P ‚â´ Functor.whiskerLeft F.op (G.map f) = f ‚â´ presheafHom œÜ Q :=
  hom_ext_uliftYoneda.{max w v‚ÇÇ} (fun X p ‚Ü¶ uliftYonedaEquiv.injective (by
    rw [‚Üê assoc p f, uliftYonedaEquiv_Œπ_presheafHom, ‚Üê assoc,
      uliftYonedaEquiv_comp, uliftYonedaEquiv_Œπ_presheafHom,
      Functor.map_comp, FunctorToTypes.comp]
    dsimp))

variable [‚àÄ (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ), F.op.HasLeftKanExtension P]

/-- Given functors `F : C ‚•§ D` and `G : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ (D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ)`,
and a natural transformation `œÜ : F ‚ãô uliftYoneda ‚ü∂ uliftYoneda ‚ãô G`, this is
the canonical natural transformation `F.op.lan ‚ü∂ G`, which is part of the
fact that `F.op.lan : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`
is the left Kan extension of `F ‚ãô uliftYoneda : C ‚•§ D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`
along `uliftYoneda : C ‚•§ C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`. -/
noncomputable def natTrans : F.op.lan ‚ü∂ G where
  app P := (F.op.lan.obj P).descOfIsLeftKanExtension (F.op.lanUnit.app P) _ (presheafHom œÜ P)
  naturality {P Q} f := by
    apply (F.op.lan.obj P).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app P)
    have eq := F.op.lanUnit.naturality f
    dsimp at eq ‚ä¢
    rw [Functor.descOfIsLeftKanExtension_fac_assoc, ‚Üê reassoc_of% eq,
      Functor.descOfIsLeftKanExtension_fac, presheafHom_naturality]

lemma natTrans_app_uliftYoneda_obj (X : C) :
    (natTrans.{w} œÜ).app (uliftYoneda.{max w v‚ÇÇ}.obj X) =
      (compULiftYonedaIsoULiftYonedaCompLan.{w} F).inv.app X ‚â´ œÜ.app X := by
  dsimp [natTrans]
  apply (F.op.lan.obj (uliftYoneda.obj X)).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app _)
  rw [Functor.descOfIsLeftKanExtension_fac]
  apply uliftYonedaEquiv.injective
  rw [uliftYonedaEquiv_presheafHom_uliftYoneda_obj]
  exact congr_arg _ (compULiftYonedaIsoULiftYonedaCompLan_inv_app_app_apply_eq_id F X).symm

end

variable [‚àÄ (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ), F.op.HasLeftKanExtension P]

/-- Given a functor `F : C ‚•§ D`, this definition is part of the verification that
`Functor.LeftExtension.mk F.op.lan (compULiftYonedaIsoULiftYonedaCompLan F).hom`
is universal, i.e. that  `F.op.lan : (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ) ‚•§ D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`
is the left Kan extension of `F ‚ãô uliftYoneda : C ‚•§ D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`
along `uliftYoneda : C ‚•§ C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ`. -/
noncomputable def extensionHom
    (Œ¶ : uliftYoneda.{max w v‚ÇÇ}.LeftExtension (F ‚ãô uliftYoneda.{max w v‚ÇÅ})) :
    Functor.LeftExtension.mk F.op.lan (compULiftYonedaIsoULiftYonedaCompLan.{w} F).hom ‚ü∂ Œ¶ :=
  StructuredArrow.homMk (natTrans Œ¶.hom) (by
    ext X : 2
    dsimp
    rw [natTrans_app_uliftYoneda_obj, Iso.hom_inv_id_app_assoc])

@[ext]
lemma hom_ext {Œ¶ : uliftYoneda.{max w v‚ÇÇ}.LeftExtension (F ‚ãô uliftYoneda.{max w v‚ÇÅ})}
    (f g : Functor.LeftExtension.mk F.op.lan (compULiftYonedaIsoULiftYonedaCompLan F).hom ‚ü∂ Œ¶) :
    f = g := by
  ext P : 3
  apply (F.op.lan.obj P).hom_ext_of_isLeftKanExtension (F.op.lanUnit.app P)
  apply (colimitOfRepresentable.{max w v‚ÇÇ} P).hom_ext
  intro x
  have eq := F.op.lanUnit.naturality (uliftYonedaEquiv.{max w v‚ÇÇ}.symm x.unop.2)
  have eq‚ÇÅ := congr_fun (congr_app (congr_app (StructuredArrow.w f) x.unop.1.unop)
    (F.op.obj x.unop.1)) (ULift.up (ùüô _))
  have eq‚ÇÇ := congr_fun (congr_app (congr_app (StructuredArrow.w g) x.unop.1.unop)
    (F.op.obj x.unop.1)) (ULift.up (ùüô _))
  dsimp at eq‚ÇÅ eq‚ÇÇ eq ‚ä¢
  simp only [reassoc_of% eq, ‚Üê Functor.whiskerLeft_comp]
  congr 2
  simp only [‚Üê cancel_epi ((compULiftYonedaIsoULiftYonedaCompLan F).hom.app x.unop.1.unop),
    NatTrans.naturality]
  apply uliftYonedaEquiv.injective
  dsimp [uliftYonedaEquiv_apply]
  rw [eq‚ÇÅ, eq‚ÇÇ]

end compULiftYonedaIsoULiftYonedaCompLan

variable [‚àÄ (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ), F.op.HasLeftKanExtension P]

noncomputable instance (Œ¶ : StructuredArrow (F ‚ãô uliftYoneda.{max w v‚ÇÅ})
    ((Functor.whiskeringLeft C (C·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ)
      (D·µí·µñ ‚•§ Type max w v‚ÇÅ v‚ÇÇ)).obj uliftYoneda.{max w v‚ÇÇ})) :
    Unique (Functor.LeftExtension.mk F.op.lan
      (compULiftYonedaIsoULiftYonedaCompLan.{w} F).hom ‚ü∂ Œ¶) where
  default := compULiftYonedaIsoULiftYonedaCompLan.extensionHom Œ¶
  uniq _ := compULiftYonedaIsoULiftYonedaCompLan.hom_ext _ _

/-- Given a functor `F : C ‚•§ D`, `F.op.lan : (C·µí·µñ ‚•§ Type v‚ÇÅ) ‚•§ D·µí·µñ ‚•§ Type v‚ÇÅ` is the
left Kan extension of `F ‚ãô yoneda : C ‚•§ D·µí·µñ ‚•§ Type v‚ÇÅ` along `yoneda : C ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ`. -/
instance : F.op.lan.IsLeftKanExtension (compULiftYonedaIsoULiftYonedaCompLan.{w} F).hom :=
  ‚ü®‚ü®Limits.IsInitial.ofUnique _‚ü©‚ü©

end

/-- For a presheaf `P`, consider the forgetful functor from the category of representable
    presheaves over `P` to the category of presheaves. There is a tautological cocone over this
    functor whose leg for a natural transformation `V ‚ü∂ P` with `V` representable is just that
    natural transformation. (In this version, we allow the presheaf `P` to have values in
    a larger universe.) -/
@[simps]
def tautologicalCocone' (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ) :
    Cocone (CostructuredArrow.proj uliftYoneda.{w} P ‚ãô uliftYoneda.{w}) where
  pt := P
  Œπ := { app X := X.hom }

/-- The tautological cocone with point `P` is a colimit cocone, exhibiting `P` as a colimit of
    representables. (In this version, we allow the presheaf `P` to have values in
    a larger universe.)

    Proposition 2.6.3(i) in [Kashiwara2006] -/
def isColimitTautologicalCocone' (P : C·µí·µñ ‚•§ Type max w v‚ÇÅ) :
    IsColimit (tautologicalCocone'.{w} P) :=
  (IsColimit.whiskerEquivalenceEquiv
    (CategoryOfElements.costructuredArrowULiftYonedaEquivalence.{w} P)).2
      (colimitOfRepresentable.{w} P)


/-- For a presheaf `P`, consider the forgetful functor from the category of representable
    presheaves over `P` to the category of presheaves. There is a tautological cocone over this
    functor whose leg for a natural transformation `V ‚ü∂ P` with `V` representable is just that
    natural transformation. -/
@[simps]
def tautologicalCocone (P : C·µí·µñ ‚•§ Type v‚ÇÅ) :
    Cocone (CostructuredArrow.proj yoneda P ‚ãô yoneda) where
  pt := P
  Œπ := { app X := X.hom }

/-- The tautological cocone with point `P` is a colimit cocone, exhibiting `P` as a colimit of
    representables.

    Proposition 2.6.3(i) in [Kashiwara2006] -/
def isColimitTautologicalCocone (P : C·µí·µñ ‚•§ Type v‚ÇÅ) :
    IsColimit (tautologicalCocone P) :=
  let e : functorToRepresentables.{v‚ÇÅ} P ‚âÖ
    ((CategoryOfElements.costructuredArrowYonedaEquivalence P).functor ‚ãô
      CostructuredArrow.proj yoneda P ‚ãô yoneda) :=
    NatIso.ofComponents (fun e ‚Ü¶ NatIso.ofComponents (fun X ‚Ü¶ Equiv.ulift.toIso))
  (IsColimit.whiskerEquivalenceEquiv
    (CategoryOfElements.costructuredArrowYonedaEquivalence P)).2
      ((IsColimit.precomposeHomEquiv e _).1 (colimitOfRepresentable.{v‚ÇÅ} P))

variable {I : Type v‚ÇÅ} [SmallCategory I] (F : I ‚•§ C)

/-- Given a functor `F : I ‚•§ C`, a cocone `c` on `F ‚ãô yoneda : I ‚•§ C·µí·µñ ‚•§ Type v‚ÇÅ` induces a
    functor `I ‚•§ CostructuredArrow yoneda c.pt` which maps `i : I` to the leg
    `yoneda.obj (F.obj i) ‚ü∂ c.pt`. If `c` is a colimit cocone, then that functor is
    final.

    Proposition 2.6.3(ii) in [Kashiwara2006] -/
theorem final_toCostructuredArrow_comp_pre {c : Cocone (F ‚ãô yoneda)} (hc : IsColimit c) :
    Functor.Final (c.toCostructuredArrow ‚ãô CostructuredArrow.pre F yoneda c.pt) := by
  apply Functor.final_of_isTerminal_colimit_comp_yoneda
  suffices IsTerminal (colimit ((c.toCostructuredArrow ‚ãô CostructuredArrow.pre F yoneda c.pt) ‚ãô
      CostructuredArrow.toOver yoneda c.pt)) by
    apply IsTerminal.isTerminalOfObj (overEquivPresheafCostructuredArrow c.pt).inverse
    apply IsTerminal.ofIso this
    refine ?_ ‚â™‚â´ (preservesColimitIso (overEquivPresheafCostructuredArrow c.pt).inverse _).symm
    apply HasColimit.isoOfNatIso
    exact Functor.isoWhiskerLeft _
      (CostructuredArrow.toOverCompOverEquivPresheafCostructuredArrow c.pt).isoCompInverse
  apply IsTerminal.ofIso Over.mkIdTerminal
  let isc : IsColimit ((Over.forget _).mapCocone _) := isColimitOfPreserves _
    (colimit.isColimit ((c.toCostructuredArrow ‚ãô CostructuredArrow.pre F yoneda c.pt) ‚ãô
      CostructuredArrow.toOver yoneda c.pt))
  exact Over.isoMk (hc.coconePointUniqueUpToIso isc) (hc.hom_ext fun i ‚Ü¶ by simp)

@[deprecated (since := "2025-08-16")] alias restrictedYoneda := restrictedULiftYoneda
@[deprecated (since := "2025-08-16")] alias isExtensionAlongYoneda := isExtensionAlongULiftYoneda
@[deprecated (since := "2025-08-16")] alias isLeftKanExtension_along_yoneda_iff :=
  isLeftKanExtension_along_uliftYoneda_iff
@[deprecated (since := "2025-08-16")] alias yonedaAdjunction := uliftYonedaAdjunction
@[deprecated (since := "2025-08-16")] alias uniqueExtensionAlongYoneda :=
  uniqueExtensionAlongULiftYoneda

end Presheaf

end CategoryTheory
