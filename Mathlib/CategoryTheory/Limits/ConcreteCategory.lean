/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Adam Topaz
-/
import Mathlib.CategoryTheory.Limits.Preserves.Basic
import Mathlib.CategoryTheory.Limits.Types
import Mathlib.CategoryTheory.Limits.Shapes.WidePullbacks
import Mathlib.CategoryTheory.Limits.Shapes.Multiequalizer
import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Kernels
import Mathlib.Tactic.ApplyFun

#align_import category_theory.limits.concrete_category from "leanprover-community/mathlib"@"c3019c79074b0619edb4b27553a91b2e82242395"

/-!
# Facts about (co)limits of functors into concrete categories
-/


universe w v u

open CategoryTheory

namespace CategoryTheory.Limits

attribute [local instance] ConcreteCategory.funLike ConcreteCategory.hasCoeToSort

section Limits

variable {C : Type u} [Category.{v} C] [ConcreteCategory.{max w v} C] {J : Type w} [SmallCategory J]
  (F : J â¥¤ C) [PreservesLimit F (forget C)]

theorem Concrete.to_product_injective_of_isLimit {D : Cone F} (hD : IsLimit D) :
    Function.Injective fun (x : D.pt) (j : J) => D.Ï€.app j x := by
  let E := (forget C).mapCone D
  let hE : IsLimit E := isLimitOfPreserves _ hD
  let G := Types.limitCone.{w, v} (F â‹™ forget C)
  let hG := Types.limitConeIsLimit.{w, v} (F â‹™ forget C)
  let T : E.pt â‰… G.pt := hE.conePointUniqueUpToIso hG
  change Function.Injective (T.hom â‰« fun x j => G.Ï€.app j x)
  have h : Function.Injective T.hom := by
    intro a b h
    suffices T.inv (T.hom a) = T.inv (T.hom b) by simpa
    rw [h]
  suffices Function.Injective fun (x : G.pt) j => G.Ï€.app j x by exact this.comp h
  apply Subtype.ext
#align category_theory.limits.concrete.to_product_injective_of_is_limit CategoryTheory.Limits.Concrete.to_product_injective_of_isLimit

theorem Concrete.isLimit_ext {D : Cone F} (hD : IsLimit D) (x y : D.pt) :
    (âˆ€ j, D.Ï€.app j x = D.Ï€.app j y) â†’ x = y := fun h =>
  Concrete.to_product_injective_of_isLimit _ hD (funext h)
#align category_theory.limits.concrete.is_limit_ext CategoryTheory.Limits.Concrete.isLimit_ext

theorem Concrete.limit_ext [HasLimit F] (x y : â†‘(limit F)) :
    (âˆ€ j, limit.Ï€ F j x = limit.Ï€ F j y) â†’ x = y :=
  Concrete.isLimit_ext F (limit.isLimit _) _ _
#align category_theory.limits.concrete.limit_ext CategoryTheory.Limits.Concrete.limit_ext

section WidePullback

open WidePullback

open WidePullbackShape

theorem Concrete.widePullback_ext {B : C} {Î¹ : Type w} {X : Î¹ â†’ C} (f : âˆ€ j : Î¹, X j âŸ¶ B)
    [HasWidePullback B X f] [PreservesLimit (wideCospan B X f) (forget C)]
    (x y : â†‘(widePullback B X f)) (hâ‚€ : base f x = base f y) (h : âˆ€ j, Ï€ f j x = Ï€ f j y) :
    x = y := by
  apply Concrete.limit_ext
  rintro (_ | j)
  Â· exact hâ‚€
  Â· apply h
#align category_theory.limits.concrete.wide_pullback_ext CategoryTheory.Limits.Concrete.widePullback_ext

theorem Concrete.widePullback_ext' {B : C} {Î¹ : Type w} [Nonempty Î¹] {X : Î¹ â†’ C}
    (f : âˆ€ j : Î¹, X j âŸ¶ B) [HasWidePullback.{w} B X f]
    [PreservesLimit (wideCospan B X f) (forget C)] (x y : â†‘(widePullback B X f))
    (h : âˆ€ j, Ï€ f j x = Ï€ f j y) : x = y := by
  apply Concrete.widePullback_ext _ _ _ _ h
  inhabit Î¹
  simp only [â† Ï€_arrow f default, comp_apply, h]
#align category_theory.limits.concrete.wide_pullback_ext' CategoryTheory.Limits.Concrete.widePullback_ext'

end WidePullback

section Multiequalizer

theorem Concrete.multiequalizer_ext {I : MulticospanIndex.{w} C} [HasMultiequalizer I]
    [PreservesLimit I.multicospan (forget C)] (x y : â†‘(multiequalizer I))
    (h : âˆ€ t : I.L, Multiequalizer.Î¹ I t x = Multiequalizer.Î¹ I t y) : x = y := by
  apply Concrete.limit_ext
  rintro (a | b)
  Â· apply h
  Â· rw [â† limit.w I.multicospan (WalkingMulticospan.Hom.fst b), comp_apply, comp_apply]
    simp [h]
#align category_theory.limits.concrete.multiequalizer_ext CategoryTheory.Limits.Concrete.multiequalizer_ext

/-- An auxiliary equivalence to be used in `multiequalizerEquiv` below.-/
def Concrete.multiequalizerEquivAux (I : MulticospanIndex C) :
    (I.multicospan â‹™ forget C).sections â‰ƒ
    { x : âˆ€ i : I.L, I.left i // âˆ€ i : I.R, I.fst i (x _) = I.snd i (x _) } where
  toFun x :=
    âŸ¨fun i => x.1 (WalkingMulticospan.left _), fun i => by
      have a := x.2 (WalkingMulticospan.Hom.fst i)
      have b := x.2 (WalkingMulticospan.Hom.snd i)
      rw [â† b] at a
      exact aâŸ©
  invFun x :=
    { val := fun j =>
        match j with
        | WalkingMulticospan.left a => x.1 _
        | WalkingMulticospan.right b => I.fst b (x.1 _)
      property := by
        rintro (a | b) (a' | b') (f | f | f)
        Â· simp
        Â· rfl
        Â· dsimp
          exact (x.2 b').symm
        Â· simp }
  left_inv := by
    intro x; ext (a | b)
    Â· rfl
    Â· rw [â† x.2 (WalkingMulticospan.Hom.fst b)]
      rfl
  right_inv := by
    intro x
    ext i
    rfl
#align category_theory.limits.concrete.multiequalizer_equiv_aux CategoryTheory.Limits.Concrete.multiequalizerEquivAux

/-- The equivalence between the noncomputable multiequalizer and
the concrete multiequalizer. -/
noncomputable def Concrete.multiequalizerEquiv (I : MulticospanIndex.{w} C) [HasMultiequalizer I]
    [PreservesLimit I.multicospan (forget C)] :
    (multiequalizer I : C) â‰ƒ
      { x : âˆ€ i : I.L, I.left i // âˆ€ i : I.R, I.fst i (x _) = I.snd i (x _) } := by
  let h1 := limit.isLimit I.multicospan
  let h2 := isLimitOfPreserves (forget C) h1
  let E := h2.conePointUniqueUpToIso (Types.limitConeIsLimit.{w, v} _)
  exact Equiv.trans E.toEquiv (Concrete.multiequalizerEquivAux.{w, v} I)
#align category_theory.limits.concrete.multiequalizer_equiv CategoryTheory.Limits.Concrete.multiequalizerEquiv

@[simp]
theorem Concrete.multiequalizerEquiv_apply (I : MulticospanIndex.{w} C) [HasMultiequalizer I]
    [PreservesLimit I.multicospan (forget C)] (x : â†‘(multiequalizer I)) (i : I.L) :
    ((Concrete.multiequalizerEquiv I) x : âˆ€ i : I.L, I.left i) i = Multiequalizer.Î¹ I i x :=
  rfl
#align category_theory.limits.concrete.multiequalizer_equiv_apply CategoryTheory.Limits.Concrete.multiequalizerEquiv_apply

end Multiequalizer

-- TODO: Add analogous lemmas about products and equalizers.
end Limits

section Colimits

-- We don't mark this as an `@[ext]` lemma as we don't always want to work elementwise.
theorem cokernel_funext {C : Type*} [Category C] [HasZeroMorphisms C] [ConcreteCategory C]
    {M N K : C} {f : M âŸ¶ N} [HasCokernel f] {g h : cokernel f âŸ¶ K}
    (w : âˆ€ n : N, g (cokernel.Ï€ f n) = h (cokernel.Ï€ f n)) : g = h := by
  ext x
  simpa using w x
#align category_theory.limits.cokernel_funext CategoryTheory.Limits.cokernel_funext

variable {C : Type u} [Category.{v} C] [ConcreteCategory.{v} C] {J : Type v} [SmallCategory J]
  (F : J â¥¤ C) [PreservesColimit F (forget C)]

theorem Concrete.from_union_surjective_of_isColimit {D : Cocone F} (hD : IsColimit D) :
    let ff : (Î£j : J, F.obj j) â†’ D.pt := fun a => D.Î¹.app a.1 a.2
    Function.Surjective ff := by
  intro ff
  let E := (forget C).mapCocone D
  let hE : IsColimit E := isColimitOfPreserves _ hD
  let G := Types.colimitCocone.{v, v} (F â‹™ forget C)
  let hG := Types.colimitCoconeIsColimit.{v, v} (F â‹™ forget C)
  let T : E â‰… G := hE.uniqueUpToIso hG
  let TX : E.pt â‰… G.pt := (Cocones.forget _).mapIso T
  suffices Function.Surjective (TX.hom âˆ˜ ff) by
    intro a
    obtain âŸ¨b, hbâŸ© := this (TX.hom a)
    refine' âŸ¨b, _âŸ©
    apply_fun TX.inv at hb
    change (TX.hom â‰« TX.inv) (ff b) = (TX.hom â‰« TX.inv) _ at hb
    simpa only [TX.hom_inv_id] using hb
  have : TX.hom âˆ˜ ff = fun a => G.Î¹.app a.1 a.2 := by
    ext a
    change (E.Î¹.app a.1 â‰« hE.desc G) a.2 = _
    rw [hE.fac]
  rw [this]
  rintro âŸ¨âŸ¨j, aâŸ©âŸ©
  exact âŸ¨âŸ¨j, aâŸ©, rflâŸ©
#align category_theory.limits.concrete.from_union_surjective_of_is_colimit CategoryTheory.Limits.Concrete.from_union_surjective_of_isColimit

theorem Concrete.isColimit_exists_rep {D : Cocone F} (hD : IsColimit D) (x : D.pt) :
    âˆƒ (j : J) (y : F.obj j), D.Î¹.app j y = x := by
  obtain âŸ¨a, rflâŸ© := Concrete.from_union_surjective_of_isColimit F hD x
  exact âŸ¨a.1, a.2, rflâŸ©
#align category_theory.limits.concrete.is_colimit_exists_rep CategoryTheory.Limits.Concrete.isColimit_exists_rep

theorem Concrete.colimit_exists_rep [HasColimit F] (x : â†‘(colimit F)) :
    âˆƒ (j : J) (y : F.obj j), colimit.Î¹ F j y = x :=
  Concrete.isColimit_exists_rep F (colimit.isColimit _) x
#align category_theory.limits.concrete.colimit_exists_rep CategoryTheory.Limits.Concrete.colimit_exists_rep

theorem Concrete.isColimit_rep_eq_of_exists {D : Cocone F} {i j : J} (hD : IsColimit D)
    (x : F.obj i) (y : F.obj j) (h : âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f x = F.map g y) :
    D.Î¹.app i x = D.Î¹.app j y := by
  let E := (forget C).mapCocone D
  let hE : IsColimit E := isColimitOfPreserves _ hD
  let G := Types.colimitCocone.{v, v} (F â‹™ forget C)
  let hG := Types.colimitCoconeIsColimit.{v, v} (F â‹™ forget C)
  let T : E â‰… G := hE.uniqueUpToIso hG
  let TX : E.pt â‰… G.pt := (Cocones.forget _).mapIso T
  apply_fun TX.hom using injective_of_mono TX.hom
  change (E.Î¹.app i â‰« TX.hom) x = (E.Î¹.app j â‰« TX.hom) y
  erw [T.hom.w, T.hom.w]
  obtain âŸ¨k, f, g, hâŸ© := h
  have : G.Î¹.app i x = (G.Î¹.app k (F.map f x) : G.pt) := Quot.sound âŸ¨f, rflâŸ©
  rw [this, h]
  symm
  exact Quot.sound âŸ¨g, rflâŸ©
#align category_theory.limits.concrete.is_colimit_rep_eq_of_exists CategoryTheory.Limits.Concrete.isColimit_rep_eq_of_exists

theorem Concrete.colimit_rep_eq_of_exists [HasColimit F] {i j : J} (x : F.obj i) (y : F.obj j)
    (h : âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f x = F.map g y) :
    colimit.Î¹ F i x = colimit.Î¹ F j y :=
  Concrete.isColimit_rep_eq_of_exists F (colimit.isColimit _) x y h
#align category_theory.limits.concrete.colimit_rep_eq_of_exists CategoryTheory.Limits.Concrete.colimit_rep_eq_of_exists

section FilteredColimits

variable [IsFiltered J]

theorem Concrete.isColimit_exists_of_rep_eq {D : Cocone F} {i j : J} (hD : IsColimit D)
    (x : F.obj i) (y : F.obj j) (h : D.Î¹.app _ x = D.Î¹.app _ y) :
    âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f x = F.map g y := by
  let E := (forget C).mapCocone D
  let hE : IsColimit E := isColimitOfPreserves _ hD
  let G := Types.colimitCocone.{v, v} (F â‹™ forget C)
  let hG := Types.colimitCoconeIsColimit.{v, v} (F â‹™ forget C)
  let T : E â‰… G := hE.uniqueUpToIso hG
  let TX : E.pt â‰… G.pt := (Cocones.forget _).mapIso T
  apply_fun TX.hom at h
  change (E.Î¹.app i â‰« TX.hom) x = (E.Î¹.app j â‰« TX.hom) y at h
  erw [T.hom.w, T.hom.w] at h
  replace h := Quot.exact _ h
  suffices
    âˆ€ (a b : Î£j, F.obj j) (_ : EqvGen (Limits.Types.Quot.Rel.{v, v} (F â‹™ forget C)) a b),
      âˆƒ (k : _) (f : a.1 âŸ¶ k) (g : b.1 âŸ¶ k), F.map f a.2 = F.map g b.2
    by exact this âŸ¨i, xâŸ© âŸ¨j, yâŸ© h
  intro a b h
  induction h
  case rel x y hh =>
    obtain âŸ¨e, heâŸ© := hh
    use y.1, e, ğŸ™ _
    simpa using he.symm
  case refl x =>
    exact âŸ¨x.1, ğŸ™ _, ğŸ™ _, rflâŸ©
  case symm x y _ hh =>
    obtain âŸ¨k, f, g, hhâŸ© := hh
    exact âŸ¨k, g, f, hh.symmâŸ©
  case trans x y z _ _ hh1 hh2 =>
    obtain âŸ¨k1, f1, g1, h1âŸ© := hh1
    obtain âŸ¨k2, f2, g2, h2âŸ© := hh2
    let k0 : J := IsFiltered.max k1 k2
    let e1 : k1 âŸ¶ k0 := IsFiltered.leftToMax _ _
    let e2 : k2 âŸ¶ k0 := IsFiltered.rightToMax _ _
    let k : J := IsFiltered.coeq (g1 â‰« e1) (f2 â‰« e2)
    let e : k0 âŸ¶ k := IsFiltered.coeqHom _ _
    use k, f1 â‰« e1 â‰« e, g2 â‰« e2 â‰« e
    simp only [F.map_comp, comp_apply, h1, â† h2]
    simp only [â† comp_apply, â† F.map_comp]
    rw [IsFiltered.coeq_condition]
#align category_theory.limits.concrete.is_colimit_exists_of_rep_eq CategoryTheory.Limits.Concrete.isColimit_exists_of_rep_eq

theorem Concrete.isColimit_rep_eq_iff_exists {D : Cocone F} {i j : J} (hD : IsColimit D)
    (x : F.obj i) (y : F.obj j) :
    D.Î¹.app i x = D.Î¹.app j y â†” âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f x = F.map g y :=
  âŸ¨Concrete.isColimit_exists_of_rep_eq _ hD _ _, Concrete.isColimit_rep_eq_of_exists _ hD _ _âŸ©
#align category_theory.limits.concrete.is_colimit_rep_eq_iff_exists CategoryTheory.Limits.Concrete.isColimit_rep_eq_iff_exists

theorem Concrete.colimit_exists_of_rep_eq [HasColimit F] {i j : J} (x : F.obj i) (y : F.obj j)
    (h : colimit.Î¹ F _ x = colimit.Î¹ F _ y) :
    âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f x = F.map g y :=
  Concrete.isColimit_exists_of_rep_eq F (colimit.isColimit _) x y h
#align category_theory.limits.concrete.colimit_exists_of_rep_eq CategoryTheory.Limits.Concrete.colimit_exists_of_rep_eq

theorem Concrete.colimit_rep_eq_iff_exists [HasColimit F] {i j : J} (x : F.obj i) (y : F.obj j) :
    colimit.Î¹ F i x = colimit.Î¹ F j y â†” âˆƒ (k : _) (f : i âŸ¶ k) (g : j âŸ¶ k), F.map f x = F.map g y :=
  âŸ¨Concrete.colimit_exists_of_rep_eq _ _ _, Concrete.colimit_rep_eq_of_exists _ _ _âŸ©
#align category_theory.limits.concrete.colimit_rep_eq_iff_exists CategoryTheory.Limits.Concrete.colimit_rep_eq_iff_exists

end FilteredColimits

section WidePushout

open WidePushout

open WidePushoutShape

theorem Concrete.widePushout_exists_rep {B : C} {Î± : Type _} {X : Î± â†’ C} (f : âˆ€ j : Î±, B âŸ¶ X j)
    [HasWidePushout.{v} B X f] [PreservesColimit (wideSpan B X f) (forget C)]
    (x : â†‘(widePushout B X f)) : (âˆƒ y : B, head f y = x) âˆ¨ âˆƒ (i : Î±) (y : X i), Î¹ f i y = x := by
  obtain âŸ¨_ | j, y, rflâŸ© := Concrete.colimit_exists_rep _ x
  Â· left
    use y
    rfl
  Â· right
    use j, y
    rfl
#align category_theory.limits.concrete.wide_pushout_exists_rep CategoryTheory.Limits.Concrete.widePushout_exists_rep

theorem Concrete.widePushout_exists_rep' {B : C} {Î± : Type _} [Nonempty Î±] {X : Î± â†’ C}
    (f : âˆ€ j : Î±, B âŸ¶ X j) [HasWidePushout.{v} B X f] [PreservesColimit (wideSpan B X f) (forget C)]
    (x : â†‘(widePushout B X f)) : âˆƒ (i : Î±) (y : X i), Î¹ f i y = x := by
  rcases Concrete.widePushout_exists_rep f x with (âŸ¨y, rflâŸ© | âŸ¨i, y, rflâŸ©)
  Â· inhabit Î±
    use default, f _ y
    simp only [â† arrow_Î¹ _ default, comp_apply]
  Â· use i, y
#align category_theory.limits.concrete.wide_pushout_exists_rep' CategoryTheory.Limits.Concrete.widePushout_exists_rep'

end WidePushout

-- TODO: Add analogous lemmas about coproducts and coequalizers.
end Colimits

end CategoryTheory.Limits
