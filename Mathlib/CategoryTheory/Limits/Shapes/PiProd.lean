/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
public import Mathlib.CategoryTheory.Limits.Shapes.Products
/-!

# A product as a binary product

We write a product indexed by `I` as a binary product of the products indexed by a subset of `I`
and its complement.

-/

@[expose] public section

namespace CategoryTheory.Limits

variable {C I : Type*} [Category* C] {X Y : I â†’ C}
  (f : (i : I) â†’ X i âŸ¶ Y i) (P : I â†’ Prop)
  [HasProduct X] [HasProduct Y]
  [HasProduct (fun (i : {x : I // P x}) â†¦ X i.val)]
  [HasProduct (fun (i : {x : I // Â¬ P x}) â†¦ X i.val)]
  [HasProduct (fun (i : {x : I // P x}) â†¦ Y i.val)]
  [HasProduct (fun (i : {x : I // Â¬ P x}) â†¦ Y i.val)]

variable (X) in
/--
The projection maps of a product to the products indexed by a subset and its complement, as a
binary fan.
-/
noncomputable def Pi.binaryFanOfProp : BinaryFan (âˆá¶œ (fun (i : {x : I // P x}) â†¦ X i.val))
    (âˆá¶œ (fun (i : {x : I // Â¬ P x}) â†¦ X i.val)) :=
  BinaryFan.mk (P := âˆá¶œ X) (Pi.map' Subtype.val fun _ â†¦ ğŸ™ _)
    (Pi.map' Subtype.val fun _ â†¦ ğŸ™ _)

variable (X) in
/--
A product indexed by `I` is a binary product of the products indexed by a subset of `I` and its
complement.
-/
noncomputable def Pi.binaryFanOfPropIsLimit [âˆ€ i, Decidable (P i)] :
    IsLimit (Pi.binaryFanOfProp X P) :=
  BinaryFan.isLimitMk
    (fun s â†¦ Pi.lift fun b â†¦ if h : P b then
      s.Ï€.app âŸ¨WalkingPair.leftâŸ© â‰« Pi.Ï€ (fun (i : {x : I // P x}) â†¦ X i.val) âŸ¨b, hâŸ© else
      s.Ï€.app âŸ¨WalkingPair.rightâŸ© â‰« Pi.Ï€ (fun (i : {x : I // Â¬ P x}) â†¦ X i.val) âŸ¨b, hâŸ©)
    (by aesop) (by aesop)
    (fun _ _ hâ‚ hâ‚‚ â†¦ Pi.hom_ext _ _ fun b â†¦ by
      by_cases h : P b
      Â· simp [â† hâ‚, dif_pos h]
      Â· simp [â† hâ‚‚, dif_neg h])

lemma hasBinaryProduct_of_products : HasBinaryProduct (âˆá¶œ (fun (i : {x : I // P x}) â†¦ X i.val))
    (âˆá¶œ (fun (i : {x : I // Â¬ P x}) â†¦ X i.val)) := by
  classical exact âŸ¨Pi.binaryFanOfProp X P, Pi.binaryFanOfPropIsLimit X PâŸ©

attribute [local instance] hasBinaryProduct_of_products

lemma Pi.map_eq_prod_map [âˆ€ i, Decidable (P i)] : Pi.map f =
    ((Pi.binaryFanOfPropIsLimit X P).conePointUniqueUpToIso (prodIsProd _ _)).hom â‰«
      prod.map (Pi.map (fun (i : {x : I // P x}) â†¦ f i.val))
      (Pi.map (fun (i : {x : I // Â¬ P x}) â†¦ f i.val)) â‰«
        ((Pi.binaryFanOfPropIsLimit Y P).conePointUniqueUpToIso (prodIsProd _ _)).inv := by
  rw [â† Category.assoc, Iso.eq_comp_inv]
  dsimp only [IsLimit.conePointUniqueUpToIso, binaryFanOfProp, prodIsProd]
  cat_disch

end CategoryTheory.Limits
