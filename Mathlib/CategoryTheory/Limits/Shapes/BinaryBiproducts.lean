/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Jakob von Raumer
-/
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Biproducts

/-!
# Binary biproducts

We introduce the notion of binary biproducts.

These are slightly unusual relative to the other shapes in the library,
as they are simultaneously limits and colimits.
(Zero objects are similar; they are "biterminal".)

For results about biproducts in preadditive categories see
`CategoryTheory.Preadditive.Biproducts`.

In a category with zero morphisms, we model the (binary) biproduct of `P Q : C`
using a `BinaryBicone`, which has a cone point `X`,
and morphisms `fst : X ‚ü∂ P`, `snd : X ‚ü∂ Q`, `inl : P ‚ü∂ X` and `inr : X ‚ü∂ Q`,
such that `inl ‚â´ fst = ùüô P`, `inl ‚â´ snd = 0`, `inr ‚â´ fst = 0`, and `inr ‚â´ snd = ùüô Q`.
Such a `BinaryBicone` is a biproduct if the cone is a limit cone, and the cocone is a colimit
cocone.

-/

noncomputable section

universe w w' v u

open CategoryTheory Functor

namespace CategoryTheory.Limits

variable {J : Type w}
universe uC' uC uD' uD
variable {C : Type uC} [Category.{uC'} C] [HasZeroMorphisms C]
variable {D : Type uD} [Category.{uD'} D] [HasZeroMorphisms D]

/-- A binary bicone for a pair of objects `P Q : C` consists of the cone point `X`,
maps from `X` to both `P` and `Q`, and maps from both `P` and `Q` to `X`,
so that `inl ‚â´ fst = ùüô P`, `inl ‚â´ snd = 0`, `inr ‚â´ fst = 0`, and `inr ‚â´ snd = ùüô Q` -/
structure BinaryBicone (P Q : C) where
  pt : C
  fst : pt ‚ü∂ P
  snd : pt ‚ü∂ Q
  inl : P ‚ü∂ pt
  inr : Q ‚ü∂ pt
  inl_fst : inl ‚â´ fst = ùüô P := by aesop
  inl_snd : inl ‚â´ snd = 0 := by aesop
  inr_fst : inr ‚â´ fst = 0 := by aesop
  inr_snd : inr ‚â´ snd = ùüô Q := by aesop

attribute [inherit_doc BinaryBicone] BinaryBicone.pt BinaryBicone.fst BinaryBicone.snd
  BinaryBicone.inl BinaryBicone.inr BinaryBicone.inl_fst BinaryBicone.inl_snd
  BinaryBicone.inr_fst BinaryBicone.inr_snd

attribute [reassoc (attr := simp)]
  BinaryBicone.inl_fst BinaryBicone.inl_snd BinaryBicone.inr_fst BinaryBicone.inr_snd

/-- A binary bicone morphism between two binary bicones for the same diagram is a morphism of the
binary bicone points which commutes with the cone and cocone legs. -/
structure BinaryBiconeMorphism {P Q : C} (A B : BinaryBicone P Q) where
  /-- A morphism between the two vertex objects of the bicones -/
  hom : A.pt ‚ü∂ B.pt
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wfst : hom ‚â´ B.fst = A.fst := by cat_disch
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  wsnd : hom ‚â´ B.snd = A.snd := by cat_disch
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  winl : A.inl ‚â´ hom = B.inl := by cat_disch
  /-- The triangle consisting of the two natural transformations and `hom` commutes -/
  winr : A.inr ‚â´ hom = B.inr := by cat_disch

attribute [reassoc (attr := simp)] BinaryBiconeMorphism.wfst BinaryBiconeMorphism.wsnd
attribute [reassoc (attr := simp)] BinaryBiconeMorphism.winl BinaryBiconeMorphism.winr

/-- The category of binary bicones on a given diagram. -/
@[simps]
instance BinaryBicone.category {P Q : C} : Category (BinaryBicone P Q) where
  Hom A B := BinaryBiconeMorphism A B
  comp f g := { hom := f.hom ‚â´ g.hom }
  id B := { hom := ùüô B.pt }

/- We do not want `simps` automatically generate the lemma for simplifying the `Hom` field of
-- a category. So we need to write the `ext` lemma in terms of the categorical morphism, rather than
the underlying structure. -/
@[ext]
theorem BinaryBiconeMorphism.ext {P Q : C} {c c' : BinaryBicone P Q}
    (f g : c ‚ü∂ c') (w : f.hom = g.hom) : f = g := by
  cases f
  cases g
  congr

namespace BinaryBicones

/-- To give an isomorphism between cocones, it suffices to give an
  isomorphism between their vertices which commutes with the cocone
  maps. -/
@[aesop apply safe (rule_sets := [CategoryTheory]), simps]
def ext {P Q : C} {c c' : BinaryBicone P Q} (œÜ : c.pt ‚âÖ c'.pt)
    (winl : c.inl ‚â´ œÜ.hom = c'.inl := by cat_disch)
    (winr : c.inr ‚â´ œÜ.hom = c'.inr := by cat_disch)
    (wfst : œÜ.hom ‚â´ c'.fst = c.fst := by cat_disch)
    (wsnd : œÜ.hom ‚â´ c'.snd = c.snd := by cat_disch) : c ‚âÖ c' where
  hom := { hom := œÜ.hom }
  inv :=
    { hom := œÜ.inv
      wfst := œÜ.inv_comp_eq.mpr wfst.symm
      wsnd := œÜ.inv_comp_eq.mpr wsnd.symm
      winl := œÜ.comp_inv_eq.mpr winl.symm
      winr := œÜ.comp_inv_eq.mpr winr.symm }

variable (P Q : C) (F : C ‚•§ D) [Functor.PreservesZeroMorphisms F]

/-- A functor `F : C ‚•§ D` sends binary bicones for `P` and `Q`
to binary bicones for `G.obj P` and `G.obj Q` functorially. -/
@[simps]
def functoriality : BinaryBicone P Q ‚•§ BinaryBicone (F.obj P) (F.obj Q) where
  obj A :=
    { pt := F.obj A.pt
      fst := F.map A.fst
      snd := F.map A.snd
      inl := F.map A.inl
      inr := F.map A.inr
      inl_fst := by rw [‚Üê F.map_comp, A.inl_fst, F.map_id]
      inl_snd := by rw [‚Üê F.map_comp, A.inl_snd, F.map_zero]
      inr_fst := by rw [‚Üê F.map_comp, A.inr_fst, F.map_zero]
      inr_snd := by rw [‚Üê F.map_comp, A.inr_snd, F.map_id] }
  map f :=
    { hom := F.map f.hom
      wfst := by simp [-BinaryBiconeMorphism.wfst, ‚Üê f.wfst]
      wsnd := by simp [-BinaryBiconeMorphism.wsnd, ‚Üê f.wsnd]
      winl := by simp [-BinaryBiconeMorphism.winl, ‚Üê f.winl]
      winr := by simp [-BinaryBiconeMorphism.winr, ‚Üê f.winr] }

instance functoriality_full [F.Full] [F.Faithful] : (functoriality P Q F).Full where
  map_surjective t :=
   ‚ü®{ hom := F.preimage t.hom
      winl := F.map_injective (by simpa using t.winl)
      winr := F.map_injective (by simpa using t.winr)
      wfst := F.map_injective (by simpa using t.wfst)
      wsnd := F.map_injective (by simpa using t.wsnd) }, by cat_disch‚ü©

instance functoriality_faithful [F.Faithful] : (functoriality P Q F).Faithful where
  map_injective {_X} {_Y} f g h :=
    BinaryBiconeMorphism.ext f g <| F.map_injective <| congr_arg BinaryBiconeMorphism.hom h

end BinaryBicones

namespace BinaryBicone

variable {P Q : C}

/-- Extract the cone from a binary bicone. -/
def toCone (c : BinaryBicone P Q) : Cone (pair P Q) :=
  BinaryFan.mk c.fst c.snd

@[simp]
theorem toCone_pt (c : BinaryBicone P Q) : c.toCone.pt = c.pt := rfl

@[simp]
theorem toCone_œÄ_app_left (c : BinaryBicone P Q) : c.toCone.œÄ.app ‚ü®WalkingPair.left‚ü© = c.fst :=
  rfl

@[simp]
theorem toCone_œÄ_app_right (c : BinaryBicone P Q) : c.toCone.œÄ.app ‚ü®WalkingPair.right‚ü© = c.snd :=
  rfl

@[simp]
theorem binary_fan_fst_toCone (c : BinaryBicone P Q) : BinaryFan.fst c.toCone = c.fst := rfl

@[simp]
theorem binary_fan_snd_toCone (c : BinaryBicone P Q) : BinaryFan.snd c.toCone = c.snd := rfl

/-- Extract the cocone from a binary bicone. -/
def toCocone (c : BinaryBicone P Q) : Cocone (pair P Q) := BinaryCofan.mk c.inl c.inr

@[simp]
theorem toCocone_pt (c : BinaryBicone P Q) : c.toCocone.pt = c.pt := rfl

@[simp]
theorem toCocone_Œπ_app_left (c : BinaryBicone P Q) : c.toCocone.Œπ.app ‚ü®WalkingPair.left‚ü© = c.inl :=
  rfl

@[simp]
theorem toCocone_Œπ_app_right (c : BinaryBicone P Q) :
    c.toCocone.Œπ.app ‚ü®WalkingPair.right‚ü© = c.inr := rfl

@[simp]
theorem binary_cofan_inl_toCocone (c : BinaryBicone P Q) : BinaryCofan.inl c.toCocone = c.inl :=
  rfl

@[simp]
theorem binary_cofan_inr_toCocone (c : BinaryBicone P Q) : BinaryCofan.inr c.toCocone = c.inr :=
  rfl

instance (c : BinaryBicone P Q) : IsSplitMono c.inl :=
  IsSplitMono.mk'
    { retraction := c.fst
      id := c.inl_fst }

instance (c : BinaryBicone P Q) : IsSplitMono c.inr :=
  IsSplitMono.mk'
    { retraction := c.snd
      id := c.inr_snd }

instance (c : BinaryBicone P Q) : IsSplitEpi c.fst :=
  IsSplitEpi.mk'
    { section_ := c.inl
      id := c.inl_fst }

instance (c : BinaryBicone P Q) : IsSplitEpi c.snd :=
  IsSplitEpi.mk'
    { section_ := c.inr
      id := c.inr_snd }

/-- Convert a `BinaryBicone` into a `Bicone` over a pair. -/
@[simps]
def toBiconeFunctor {X Y : C} : BinaryBicone X Y ‚•§ Bicone (pairFunction X Y) where
  obj b :=
    { pt := b.pt
      œÄ := fun j => WalkingPair.casesOn j b.fst b.snd
      Œπ := fun j => WalkingPair.casesOn j b.inl b.inr
      Œπ_œÄ := fun j j' => by
        rcases j with ‚ü®‚ü© <;> rcases j' with ‚ü®‚ü© <;> simp }
  map f := {
    hom := f.hom
    wœÄ := fun i => WalkingPair.casesOn i f.wfst f.wsnd
    wŒπ := fun i => WalkingPair.casesOn i f.winl f.winr }

/-- A shorthand for `toBiconeFunctor.obj` -/
abbrev toBicone {X Y : C} (b : BinaryBicone X Y) : Bicone (pairFunction X Y) :=
  toBiconeFunctor.obj b

/-- A binary bicone is a limit cone if and only if the corresponding bicone is a limit cone. -/
def toBiconeIsLimit {X Y : C} (b : BinaryBicone X Y) :
    IsLimit b.toBicone.toCone ‚âÉ IsLimit b.toCone :=
  IsLimit.equivIsoLimit <| Cones.ext (Iso.refl _) fun ‚ü®as‚ü© => by cases as <;> simp

/-- A binary bicone is a colimit cocone if and only if the corresponding bicone is a colimit
cocone. -/
def toBiconeIsColimit {X Y : C} (b : BinaryBicone X Y) :
    IsColimit b.toBicone.toCocone ‚âÉ IsColimit b.toCocone :=
  IsColimit.equivIsoColimit <| Cocones.ext (Iso.refl _) fun ‚ü®as‚ü© => by cases as <;> simp

end BinaryBicone

namespace Bicone

/-- Convert a `Bicone` over a function on `WalkingPair` to a BinaryBicone. -/
@[simps]
def toBinaryBiconeFunctor {X Y : C} : Bicone (pairFunction X Y) ‚•§ BinaryBicone X Y where
  obj b :=
    { pt := b.pt
      fst := b.œÄ WalkingPair.left
      snd := b.œÄ WalkingPair.right
      inl := b.Œπ WalkingPair.left
      inr := b.Œπ WalkingPair.right
      inl_fst := by simp
      inr_fst := by simp
      inl_snd := by simp
      inr_snd := by simp }
  map f :=
    { hom := f.hom }

/-- A shorthand for `toBinaryBiconeFunctor.obj` -/
abbrev toBinaryBicone {X Y : C} (b : Bicone (pairFunction X Y)) : BinaryBicone X Y :=
  toBinaryBiconeFunctor.obj b

/-- A bicone over a pair is a limit cone if and only if the corresponding binary bicone is a limit
cone. -/
def toBinaryBiconeIsLimit {X Y : C} (b : Bicone (pairFunction X Y)) :
    IsLimit b.toBinaryBicone.toCone ‚âÉ IsLimit b.toCone :=
  IsLimit.equivIsoLimit <| Cones.ext (Iso.refl _) fun j => by rcases j with ‚ü®‚ü®‚ü©‚ü© <;> simp

/-- A bicone over a pair is a colimit cocone if and only if the corresponding binary bicone is a
colimit cocone. -/
def toBinaryBiconeIsColimit {X Y : C} (b : Bicone (pairFunction X Y)) :
    IsColimit b.toBinaryBicone.toCocone ‚âÉ IsColimit b.toCocone :=
  IsColimit.equivIsoColimit <| Cocones.ext (Iso.refl _) fun j => by rcases j with ‚ü®‚ü®‚ü©‚ü© <;> simp

end Bicone

/-- Structure witnessing that a binary bicone is a limit cone and a limit cocone. -/
structure BinaryBicone.IsBilimit {P Q : C} (b : BinaryBicone P Q) where
  isLimit : IsLimit b.toCone
  isColimit : IsColimit b.toCocone

attribute [inherit_doc BinaryBicone.IsBilimit] BinaryBicone.IsBilimit.isLimit
  BinaryBicone.IsBilimit.isColimit

/-- A binary bicone is a bilimit bicone if and only if the corresponding bicone is a bilimit. -/
def BinaryBicone.toBiconeIsBilimit {X Y : C} (b : BinaryBicone X Y) :
    b.toBicone.IsBilimit ‚âÉ b.IsBilimit where
  toFun h := ‚ü®b.toBiconeIsLimit h.isLimit, b.toBiconeIsColimit h.isColimit‚ü©
  invFun h := ‚ü®b.toBiconeIsLimit.symm h.isLimit, b.toBiconeIsColimit.symm h.isColimit‚ü©
  left_inv := fun ‚ü®h, h'‚ü© => by dsimp only; simp
  right_inv := fun ‚ü®h, h'‚ü© => by dsimp only; simp

/-- A bicone over a pair is a bilimit bicone if and only if the corresponding binary bicone is a
bilimit. -/
def Bicone.toBinaryBiconeIsBilimit {X Y : C} (b : Bicone (pairFunction X Y)) :
    b.toBinaryBicone.IsBilimit ‚âÉ b.IsBilimit where
  toFun h := ‚ü®b.toBinaryBiconeIsLimit h.isLimit, b.toBinaryBiconeIsColimit h.isColimit‚ü©
  invFun h := ‚ü®b.toBinaryBiconeIsLimit.symm h.isLimit, b.toBinaryBiconeIsColimit.symm h.isColimit‚ü©
  left_inv := fun ‚ü®h, h'‚ü© => by dsimp only; simp
  right_inv := fun ‚ü®h, h'‚ü© => by dsimp only; simp

/-- A bicone over `P Q : C`, which is both a limit cone and a colimit cocone. -/
structure BinaryBiproductData (P Q : C) where
  bicone : BinaryBicone P Q
  isBilimit : bicone.IsBilimit

attribute [inherit_doc BinaryBiproductData] BinaryBiproductData.bicone
  BinaryBiproductData.isBilimit

/-- `HasBinaryBiproduct P Q` expresses the mere existence of a bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`. -/
class HasBinaryBiproduct (P Q : C) : Prop where mk' ::
  exists_binary_biproduct : Nonempty (BinaryBiproductData P Q)

attribute [inherit_doc HasBinaryBiproduct] HasBinaryBiproduct.exists_binary_biproduct

theorem HasBinaryBiproduct.mk {P Q : C} (d : BinaryBiproductData P Q) : HasBinaryBiproduct P Q :=
  ‚ü®Nonempty.intro d‚ü©

/--
Use the axiom of choice to extract explicit `BinaryBiproductData F` from `HasBinaryBiproduct F`.
-/
def getBinaryBiproductData (P Q : C) [HasBinaryBiproduct P Q] : BinaryBiproductData P Q :=
  Classical.choice HasBinaryBiproduct.exists_binary_biproduct

/-- A bicone for `P Q` which is both a limit cone and a colimit cocone. -/
def BinaryBiproduct.bicone (P Q : C) [HasBinaryBiproduct P Q] : BinaryBicone P Q :=
  (getBinaryBiproductData P Q).bicone

/-- `BinaryBiproduct.bicone P Q` is a limit bicone. -/
def BinaryBiproduct.isBilimit (P Q : C) [HasBinaryBiproduct P Q] :
    (BinaryBiproduct.bicone P Q).IsBilimit :=
  (getBinaryBiproductData P Q).isBilimit

/-- `BinaryBiproduct.bicone P Q` is a limit cone. -/
def BinaryBiproduct.isLimit (P Q : C) [HasBinaryBiproduct P Q] :
    IsLimit (BinaryBiproduct.bicone P Q).toCone :=
  (getBinaryBiproductData P Q).isBilimit.isLimit

/-- `BinaryBiproduct.bicone P Q` is a colimit cocone. -/
def BinaryBiproduct.isColimit (P Q : C) [HasBinaryBiproduct P Q] :
    IsColimit (BinaryBiproduct.bicone P Q).toCocone :=
  (getBinaryBiproductData P Q).isBilimit.isColimit

section

variable (C)

/-- `HasBinaryBiproducts C` represents the existence of a bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`, for every `P Q : C`. -/
class HasBinaryBiproducts : Prop where
  has_binary_biproduct : ‚àÄ P Q : C, HasBinaryBiproduct P Q

attribute [instance 100] HasBinaryBiproducts.has_binary_biproduct

/-- A category with finite biproducts has binary biproducts.

This is not an instance as typically in concrete categories there will be
an alternative construction with nicer definitional properties. -/
theorem hasBinaryBiproducts_of_finite_biproducts [HasFiniteBiproducts C] : HasBinaryBiproducts C :=
  { has_binary_biproduct := fun P Q =>
      HasBinaryBiproduct.mk
        { bicone := (biproduct.bicone (pairFunction P Q)).toBinaryBicone
          isBilimit := (Bicone.toBinaryBiconeIsBilimit _).symm (biproduct.isBilimit _) } }

end

variable {P Q : C}

instance HasBinaryBiproduct.hasLimit_pair [HasBinaryBiproduct P Q] : HasLimit (pair P Q) :=
  HasLimit.mk ‚ü®_, BinaryBiproduct.isLimit P Q‚ü©

instance HasBinaryBiproduct.hasColimit_pair [HasBinaryBiproduct P Q] : HasColimit (pair P Q) :=
  HasColimit.mk ‚ü®_, BinaryBiproduct.isColimit P Q‚ü©

instance (priority := 100) hasBinaryProducts_of_hasBinaryBiproducts [HasBinaryBiproducts C] :
    HasBinaryProducts C where
  has_limit F := hasLimit_of_iso (diagramIsoPair F).symm

instance (priority := 100) hasBinaryCoproducts_of_hasBinaryBiproducts [HasBinaryBiproducts C] :
    HasBinaryCoproducts C where
  has_colimit F := hasColimit_of_iso (diagramIsoPair F)

/-- The isomorphism between the specified binary product and the specified binary coproduct for
a pair for a binary biproduct. -/
def biprodIso (X Y : C) [HasBinaryBiproduct X Y] : Limits.prod X Y ‚âÖ Limits.coprod X Y :=
  (IsLimit.conePointUniqueUpToIso (limit.isLimit _) (BinaryBiproduct.isLimit X Y)).trans <|
    IsColimit.coconePointUniqueUpToIso (BinaryBiproduct.isColimit X Y) (colimit.isColimit _)

/-- An arbitrary choice of biproduct of a pair of objects. -/
abbrev biprod (X Y : C) [HasBinaryBiproduct X Y] :=
  (BinaryBiproduct.bicone X Y).pt

@[inherit_doc biprod]
notation:20 X " ‚äû " Y:20 => biprod X Y

/-- The projection onto the first summand of a binary biproduct. -/
abbrev biprod.fst {X Y : C} [HasBinaryBiproduct X Y] : X ‚äû Y ‚ü∂ X :=
  (BinaryBiproduct.bicone X Y).fst

/-- The projection onto the second summand of a binary biproduct. -/
abbrev biprod.snd {X Y : C} [HasBinaryBiproduct X Y] : X ‚äû Y ‚ü∂ Y :=
  (BinaryBiproduct.bicone X Y).snd

/-- The inclusion into the first summand of a binary biproduct. -/
abbrev biprod.inl {X Y : C} [HasBinaryBiproduct X Y] : X ‚ü∂ X ‚äû Y :=
  (BinaryBiproduct.bicone X Y).inl

/-- The inclusion into the second summand of a binary biproduct. -/
abbrev biprod.inr {X Y : C} [HasBinaryBiproduct X Y] : Y ‚ü∂ X ‚äû Y :=
  (BinaryBiproduct.bicone X Y).inr

section

variable {X Y : C} [HasBinaryBiproduct X Y]

@[simp] theorem BinaryBiproduct.bicone_fst : (BinaryBiproduct.bicone X Y).fst = biprod.fst := rfl

@[simp] theorem BinaryBiproduct.bicone_snd : (BinaryBiproduct.bicone X Y).snd = biprod.snd := rfl

@[simp] theorem BinaryBiproduct.bicone_inl : (BinaryBiproduct.bicone X Y).inl = biprod.inl := rfl

@[simp] theorem BinaryBiproduct.bicone_inr : (BinaryBiproduct.bicone X Y).inr = biprod.inr := rfl

end

@[reassoc]
theorem biprod.inl_fst {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inl : X ‚ü∂ X ‚äû Y) ‚â´ (biprod.fst : X ‚äû Y ‚ü∂ X) = ùüô X :=
  (BinaryBiproduct.bicone X Y).inl_fst

@[reassoc]
theorem biprod.inl_snd {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inl : X ‚ü∂ X ‚äû Y) ‚â´ (biprod.snd : X ‚äû Y ‚ü∂ Y) = 0 :=
  (BinaryBiproduct.bicone X Y).inl_snd

@[reassoc]
theorem biprod.inr_fst {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inr : Y ‚ü∂ X ‚äû Y) ‚â´ (biprod.fst : X ‚äû Y ‚ü∂ X) = 0 :=
  (BinaryBiproduct.bicone X Y).inr_fst

@[reassoc]
theorem biprod.inr_snd {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.inr : Y ‚ü∂ X ‚äû Y) ‚â´ (biprod.snd : X ‚äû Y ‚ü∂ Y) = ùüô Y :=
  (BinaryBiproduct.bicone X Y).inr_snd

/-- Given a pair of maps into the summands of a binary biproduct,
we obtain a map into the binary biproduct. -/
abbrev biprod.lift {W X Y : C} [HasBinaryBiproduct X Y] (f : W ‚ü∂ X) (g : W ‚ü∂ Y) : W ‚ü∂ X ‚äû Y :=
  (BinaryBiproduct.isLimit X Y).lift (BinaryFan.mk f g)

/-- Given a pair of maps out of the summands of a binary biproduct,
we obtain a map out of the binary biproduct. -/
abbrev biprod.desc {W X Y : C} [HasBinaryBiproduct X Y] (f : X ‚ü∂ W) (g : Y ‚ü∂ W) : X ‚äû Y ‚ü∂ W :=
  (BinaryBiproduct.isColimit X Y).desc (BinaryCofan.mk f g)

@[reassoc (attr := simp)]
theorem biprod.lift_fst {W X Y : C} [HasBinaryBiproduct X Y] (f : W ‚ü∂ X) (g : W ‚ü∂ Y) :
    biprod.lift f g ‚â´ biprod.fst = f :=
  (BinaryBiproduct.isLimit X Y).fac _ ‚ü®WalkingPair.left‚ü©

@[reassoc (attr := simp)]
theorem biprod.lift_snd {W X Y : C} [HasBinaryBiproduct X Y] (f : W ‚ü∂ X) (g : W ‚ü∂ Y) :
    biprod.lift f g ‚â´ biprod.snd = g :=
  (BinaryBiproduct.isLimit X Y).fac _ ‚ü®WalkingPair.right‚ü©

@[reassoc (attr := simp)]
theorem biprod.inl_desc {W X Y : C} [HasBinaryBiproduct X Y] (f : X ‚ü∂ W) (g : Y ‚ü∂ W) :
    biprod.inl ‚â´ biprod.desc f g = f :=
  (BinaryBiproduct.isColimit X Y).fac _ ‚ü®WalkingPair.left‚ü©

@[reassoc (attr := simp)]
theorem biprod.inr_desc {W X Y : C} [HasBinaryBiproduct X Y] (f : X ‚ü∂ W) (g : Y ‚ü∂ W) :
    biprod.inr ‚â´ biprod.desc f g = g :=
  (BinaryBiproduct.isColimit X Y).fac _ ‚ü®WalkingPair.right‚ü©

instance biprod.mono_lift_of_mono_left {W X Y : C} [HasBinaryBiproduct X Y] (f : W ‚ü∂ X) (g : W ‚ü∂ Y)
    [Mono f] : Mono (biprod.lift f g) :=
  mono_of_mono_fac <| biprod.lift_fst _ _

instance biprod.mono_lift_of_mono_right {W X Y : C} [HasBinaryBiproduct X Y] (f : W ‚ü∂ X) (g : W ‚ü∂ Y)
    [Mono g] : Mono (biprod.lift f g) :=
  mono_of_mono_fac <| biprod.lift_snd _ _

instance biprod.epi_desc_of_epi_left {W X Y : C} [HasBinaryBiproduct X Y] (f : X ‚ü∂ W) (g : Y ‚ü∂ W)
    [Epi f] : Epi (biprod.desc f g) :=
  epi_of_epi_fac <| biprod.inl_desc _ _

instance biprod.epi_desc_of_epi_right {W X Y : C} [HasBinaryBiproduct X Y] (f : X ‚ü∂ W) (g : Y ‚ü∂ W)
    [Epi g] : Epi (biprod.desc f g) :=
  epi_of_epi_fac <| biprod.inr_desc _ _

/-- Given a pair of maps between the summands of a pair of binary biproducts,
we obtain a map between the binary biproducts. -/
abbrev biprod.map {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚ü∂ Y)
    (g : X ‚ü∂ Z) : W ‚äû X ‚ü∂ Y ‚äû Z :=
  IsLimit.map (BinaryBiproduct.bicone W X).toCone (BinaryBiproduct.isLimit Y Z)
    (@mapPair _ _ (pair W X) (pair Y Z) f g)

/-- An alternative to `biprod.map` constructed via colimits.
This construction only exists in order to show it is equal to `biprod.map`. -/
abbrev biprod.map' {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚ü∂ Y)
    (g : X ‚ü∂ Z) : W ‚äû X ‚ü∂ Y ‚äû Z :=
  IsColimit.map (BinaryBiproduct.isColimit W X) (BinaryBiproduct.bicone Y Z).toCocone
    (@mapPair _ _ (pair W X) (pair Y Z) f g)

@[ext]
theorem biprod.hom_ext {X Y Z : C} [HasBinaryBiproduct X Y] (f g : Z ‚ü∂ X ‚äû Y)
    (h‚ÇÄ : f ‚â´ biprod.fst = g ‚â´ biprod.fst) (h‚ÇÅ : f ‚â´ biprod.snd = g ‚â´ biprod.snd) : f = g :=
  BinaryFan.IsLimit.hom_ext (BinaryBiproduct.isLimit X Y) h‚ÇÄ h‚ÇÅ

@[ext]
theorem biprod.hom_ext' {X Y Z : C} [HasBinaryBiproduct X Y] (f g : X ‚äû Y ‚ü∂ Z)
    (h‚ÇÄ : biprod.inl ‚â´ f = biprod.inl ‚â´ g) (h‚ÇÅ : biprod.inr ‚â´ f = biprod.inr ‚â´ g) : f = g :=
  BinaryCofan.IsColimit.hom_ext (BinaryBiproduct.isColimit X Y) h‚ÇÄ h‚ÇÅ

/-- The canonical isomorphism between the chosen biproduct and the chosen product. -/
def biprod.isoProd (X Y : C) [HasBinaryBiproduct X Y] : X ‚äû Y ‚âÖ X ‚®Ø Y :=
  IsLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit X Y) (limit.isLimit _)

@[simp]
theorem biprod.isoProd_hom {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoProd X Y).hom = prod.lift biprod.fst biprod.snd := by
      ext <;> simp [biprod.isoProd]

@[simp]
theorem biprod.isoProd_inv {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoProd X Y).inv = biprod.lift prod.fst prod.snd := by
  ext <;> simp [Iso.inv_comp_eq]

/-- The canonical isomorphism between the chosen biproduct and the chosen coproduct. -/
def biprod.isoCoprod (X Y : C) [HasBinaryBiproduct X Y] : X ‚äû Y ‚âÖ X ‚®ø Y :=
  IsColimit.coconePointUniqueUpToIso (BinaryBiproduct.isColimit X Y) (colimit.isColimit _)

@[simp]
theorem biprod.isoCoprod_inv {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoCoprod X Y).inv = coprod.desc biprod.inl biprod.inr := by
  ext <;> simp [biprod.isoCoprod]

@[simp]
theorem biprod_isoCoprod_hom {X Y : C} [HasBinaryBiproduct X Y] :
    (biprod.isoCoprod X Y).hom = biprod.desc coprod.inl coprod.inr := by
  ext <;> simp [‚Üê Iso.eq_comp_inv]

theorem biprod.map_eq_map' {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z]
    (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) : biprod.map f g = biprod.map' f g := by
  ext
  ¬∑ simp only [mapPair_left, IsColimit.Œπ_map, IsLimit.map_œÄ,
      Category.assoc, ‚Üê BinaryBicone.toCone_œÄ_app_left, ‚Üê
      BinaryBicone.toCocone_Œπ_app_left]
    simp
  ¬∑ simp only [mapPair_left, IsColimit.Œπ_map, IsLimit.map_œÄ,
      Category.assoc, ‚Üê BinaryBicone.toCone_œÄ_app_right, ‚Üê
      BinaryBicone.toCocone_Œπ_app_left]
    simp
  ¬∑ simp only [mapPair_right, IsColimit.Œπ_map, IsLimit.map_œÄ,
      Category.assoc, ‚Üê BinaryBicone.toCone_œÄ_app_left, ‚Üê
      BinaryBicone.toCocone_Œπ_app_right]
    simp
  ¬∑ simp only [mapPair_right, IsColimit.Œπ_map, IsLimit.map_œÄ,
      Category.assoc, ‚Üê BinaryBicone.toCone_œÄ_app_right, ‚Üê
      BinaryBicone.toCocone_Œπ_app_right]
    simp

instance biprod.inl_mono {X Y : C} [HasBinaryBiproduct X Y] :
    IsSplitMono (biprod.inl : X ‚ü∂ X ‚äû Y) :=
  IsSplitMono.mk' { retraction := biprod.fst }

instance biprod.inr_mono {X Y : C} [HasBinaryBiproduct X Y] :
    IsSplitMono (biprod.inr : Y ‚ü∂ X ‚äû Y) :=
  IsSplitMono.mk' { retraction := biprod.snd }

instance biprod.fst_epi {X Y : C} [HasBinaryBiproduct X Y] : IsSplitEpi (biprod.fst : X ‚äû Y ‚ü∂ X) :=
  IsSplitEpi.mk' { section_ := biprod.inl }

instance biprod.snd_epi {X Y : C} [HasBinaryBiproduct X Y] : IsSplitEpi (biprod.snd : X ‚äû Y ‚ü∂ Y) :=
  IsSplitEpi.mk' { section_ := biprod.inr }

@[reassoc (attr := simp)]
theorem biprod.map_fst {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚ü∂ Y)
    (g : X ‚ü∂ Z) : biprod.map f g ‚â´ biprod.fst = biprod.fst ‚â´ f :=
  IsLimit.map_œÄ _ _ _ (‚ü®WalkingPair.left‚ü© : Discrete WalkingPair)

@[reassoc (attr := simp)]
theorem biprod.map_snd {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚ü∂ Y)
    (g : X ‚ü∂ Z) : biprod.map f g ‚â´ biprod.snd = biprod.snd ‚â´ g :=
  IsLimit.map_œÄ _ _ _ (‚ü®WalkingPair.right‚ü© : Discrete WalkingPair)

-- Because `biprod.map` is defined in terms of `lim` rather than `colim`,
-- we need to provide additional `simp` lemmas.
@[reassoc (attr := simp)]
theorem biprod.inl_map {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚ü∂ Y)
    (g : X ‚ü∂ Z) : biprod.inl ‚â´ biprod.map f g = f ‚â´ biprod.inl := by
  rw [biprod.map_eq_map']
  exact IsColimit.Œπ_map (BinaryBiproduct.isColimit W X) _ _ ‚ü®WalkingPair.left‚ü©

@[reassoc (attr := simp)]
theorem biprod.inr_map {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚ü∂ Y)
    (g : X ‚ü∂ Z) : biprod.inr ‚â´ biprod.map f g = g ‚â´ biprod.inr := by
  rw [biprod.map_eq_map']
  exact IsColimit.Œπ_map (BinaryBiproduct.isColimit W X) _ _ ‚ü®WalkingPair.right‚ü©

/-- Given a pair of isomorphisms between the summands of a pair of binary biproducts,
we obtain an isomorphism between the binary biproducts. -/
@[simps]
def biprod.mapIso {W X Y Z : C} [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] (f : W ‚âÖ Y)
    (g : X ‚âÖ Z) : W ‚äû X ‚âÖ Y ‚äû Z where
  hom := biprod.map f.hom g.hom
  inv := biprod.map f.inv g.inv

/-- Auxiliary lemma for `biprod.uniqueUpToIso`. -/
theorem biprod.conePointUniqueUpToIso_hom (X Y : C) [HasBinaryBiproduct X Y] {b : BinaryBicone X Y}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit _ _)).hom =
      biprod.lift b.fst b.snd := rfl

/-- Auxiliary lemma for `biprod.uniqueUpToIso`. -/
theorem biprod.conePointUniqueUpToIso_inv (X Y : C) [HasBinaryBiproduct X Y] {b : BinaryBicone X Y}
    (hb : b.IsBilimit) :
    (hb.isLimit.conePointUniqueUpToIso (BinaryBiproduct.isLimit _ _)).inv =
      biprod.desc b.inl b.inr := by
  refine biprod.hom_ext' _ _ (hb.isLimit.hom_ext fun j => ?_) (hb.isLimit.hom_ext fun j => ?_)
  all_goals
    simp only [Category.assoc, IsLimit.conePointUniqueUpToIso_inv_comp]
    rcases j with ‚ü®‚ü®‚ü©‚ü©
  all_goals simp

/-- Binary biproducts are unique up to isomorphism. This already follows because bilimits are
limits, but in the case of biproducts we can give an isomorphism with particularly nice
definitional properties, namely that `biprod.lift b.fst b.snd` and `biprod.desc b.inl b.inr`
are inverses of each other. -/
@[simps]
def biprod.uniqueUpToIso (X Y : C) [HasBinaryBiproduct X Y] {b : BinaryBicone X Y}
    (hb : b.IsBilimit) : b.pt ‚âÖ X ‚äû Y where
  hom := biprod.lift b.fst b.snd
  inv := biprod.desc b.inl b.inr
  hom_inv_id := by
    rw [‚Üê biprod.conePointUniqueUpToIso_hom X Y hb, ‚Üê
      biprod.conePointUniqueUpToIso_inv X Y hb, Iso.hom_inv_id]
  inv_hom_id := by
    rw [‚Üê biprod.conePointUniqueUpToIso_hom X Y hb, ‚Üê
      biprod.conePointUniqueUpToIso_inv X Y hb, Iso.inv_hom_id]

-- There are three further variations,
-- about `IsIso biprod.inr`, `IsIso biprod.fst` and `IsIso biprod.snd`,
-- but any one suffices to prove `indecomposable_of_simple`
-- and they are likely not separately useful.
theorem biprod.isIso_inl_iff_id_eq_fst_comp_inl (X Y : C) [HasBinaryBiproduct X Y] :
    IsIso (biprod.inl : X ‚ü∂ X ‚äû Y) ‚Üî ùüô (X ‚äû Y) = biprod.fst ‚â´ biprod.inl := by
  constructor
  ¬∑ intro h
    have := (cancel_epi (inv biprod.inl : X ‚äû Y ‚ü∂ X)).2 <| @biprod.inl_fst _ _ _ X Y _
    rw [IsIso.inv_hom_id_assoc, Category.comp_id] at this
    rw [this, IsIso.inv_hom_id]
  ¬∑ intro h
    exact ‚ü®‚ü®biprod.fst, biprod.inl_fst, h.symm‚ü©‚ü©

instance biprod.map_epi {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) [Epi f]
    [Epi g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Epi (biprod.map f g) := by
  rw [show biprod.map f g =
    (biprod.isoCoprod _ _).hom ‚â´ coprod.map f g ‚â´ (biprod.isoCoprod _ _).inv by aesop]
  infer_instance

instance prod.map_epi {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) [Epi f]
    [Epi g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Epi (prod.map f g) := by
  rw [show prod.map f g = (biprod.isoProd _ _).inv ‚â´ biprod.map f g ‚â´
    (biprod.isoProd _ _).hom by simp]
  infer_instance

instance biprod.map_mono {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) [Mono f]
    [Mono g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Mono (biprod.map f g) := by
  rw [show biprod.map f g = (biprod.isoProd _ _).hom ‚â´ prod.map f g ‚â´
    (biprod.isoProd _ _).inv by aesop]
  infer_instance

instance coprod.map_mono {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) [Mono f]
    [Mono g] [HasBinaryBiproduct W X] [HasBinaryBiproduct Y Z] : Mono (coprod.map f g) := by
  rw [show coprod.map f g = (biprod.isoCoprod _ _).inv ‚â´ biprod.map f g ‚â´
    (biprod.isoCoprod _ _).hom by simp]
  infer_instance

section BiprodKernel

section BinaryBicone

variable {X Y : C} (c : BinaryBicone X Y)

/-- A kernel fork for the kernel of `BinaryBicone.fst`. It consists of the morphism
`BinaryBicone.inr`. -/
def BinaryBicone.fstKernelFork : KernelFork c.fst :=
  KernelFork.ofŒπ c.inr c.inr_fst

@[simp]
theorem BinaryBicone.fstKernelFork_Œπ : (BinaryBicone.fstKernelFork c).Œπ = c.inr := rfl

/-- A kernel fork for the kernel of `BinaryBicone.snd`. It consists of the morphism
`BinaryBicone.inl`. -/
def BinaryBicone.sndKernelFork : KernelFork c.snd :=
  KernelFork.ofŒπ c.inl c.inl_snd

@[simp]
theorem BinaryBicone.sndKernelFork_Œπ : (BinaryBicone.sndKernelFork c).Œπ = c.inl := rfl

/-- A cokernel cofork for the cokernel of `BinaryBicone.inl`. It consists of the morphism
`BinaryBicone.snd`. -/
def BinaryBicone.inlCokernelCofork : CokernelCofork c.inl :=
  CokernelCofork.ofœÄ c.snd c.inl_snd

@[simp]
theorem BinaryBicone.inlCokernelCofork_œÄ : (BinaryBicone.inlCokernelCofork c).œÄ = c.snd := rfl

/-- A cokernel cofork for the cokernel of `BinaryBicone.inr`. It consists of the morphism
`BinaryBicone.fst`. -/
def BinaryBicone.inrCokernelCofork : CokernelCofork c.inr :=
  CokernelCofork.ofœÄ c.fst c.inr_fst

@[simp]
theorem BinaryBicone.inrCokernelCofork_œÄ : (BinaryBicone.inrCokernelCofork c).œÄ = c.fst := rfl

variable {c}

/-- The fork defined in `BinaryBicone.fstKernelFork` is indeed a kernel. -/
def BinaryBicone.isLimitFstKernelFork (i : IsLimit c.toCone) : IsLimit c.fstKernelFork :=
  Fork.IsLimit.mk' _ fun s =>
    ‚ü®s.Œπ ‚â´ c.snd, by apply BinaryFan.IsLimit.hom_ext i <;> simp, fun hm => by simp [‚Üê hm]‚ü©

/-- The fork defined in `BinaryBicone.sndKernelFork` is indeed a kernel. -/
def BinaryBicone.isLimitSndKernelFork (i : IsLimit c.toCone) : IsLimit c.sndKernelFork :=
  Fork.IsLimit.mk' _ fun s =>
    ‚ü®s.Œπ ‚â´ c.fst, by apply BinaryFan.IsLimit.hom_ext i <;> simp, fun hm => by simp [‚Üê hm]‚ü©

/-- The cofork defined in `BinaryBicone.inlCokernelCofork` is indeed a cokernel. -/
def BinaryBicone.isColimitInlCokernelCofork (i : IsColimit c.toCocone) :
    IsColimit c.inlCokernelCofork :=
  Cofork.IsColimit.mk' _ fun s =>
    ‚ü®c.inr ‚â´ s.œÄ, by apply BinaryCofan.IsColimit.hom_ext i <;> simp, fun hm => by simp [‚Üê hm]‚ü©

/-- The cofork defined in `BinaryBicone.inrCokernelCofork` is indeed a cokernel. -/
def BinaryBicone.isColimitInrCokernelCofork (i : IsColimit c.toCocone) :
    IsColimit c.inrCokernelCofork :=
  Cofork.IsColimit.mk' _ fun s =>
    ‚ü®c.inl ‚â´ s.œÄ, by apply BinaryCofan.IsColimit.hom_ext i <;> simp, fun hm => by simp [‚Üê hm]‚ü©

end BinaryBicone

section HasBinaryBiproduct

variable (X Y : C) [HasBinaryBiproduct X Y]

/-- A kernel fork for the kernel of `biprod.fst`. It consists of the
morphism `biprod.inr`. -/
def biprod.fstKernelFork : KernelFork (biprod.fst : X ‚äû Y ‚ü∂ X) :=
  BinaryBicone.fstKernelFork _

@[simp]
theorem biprod.fstKernelFork_Œπ : Fork.Œπ (biprod.fstKernelFork X Y) = (biprod.inr : Y ‚ü∂ X ‚äû Y) :=
  rfl

/-- The fork `biprod.fstKernelFork` is indeed a limit. -/
def biprod.isKernelFstKernelFork : IsLimit (biprod.fstKernelFork X Y) :=
  BinaryBicone.isLimitFstKernelFork (BinaryBiproduct.isLimit _ _)

/-- A kernel fork for the kernel of `biprod.snd`. It consists of the
morphism `biprod.inl`. -/
def biprod.sndKernelFork : KernelFork (biprod.snd : X ‚äû Y ‚ü∂ Y) :=
  BinaryBicone.sndKernelFork _

@[simp]
theorem biprod.sndKernelFork_Œπ : Fork.Œπ (biprod.sndKernelFork X Y) = (biprod.inl : X ‚ü∂ X ‚äû Y) :=
  rfl

/-- The fork `biprod.sndKernelFork` is indeed a limit. -/
def biprod.isKernelSndKernelFork : IsLimit (biprod.sndKernelFork X Y) :=
  BinaryBicone.isLimitSndKernelFork (BinaryBiproduct.isLimit _ _)

/-- A cokernel cofork for the cokernel of `biprod.inl`. It consists of the
morphism `biprod.snd`. -/
def biprod.inlCokernelCofork : CokernelCofork (biprod.inl : X ‚ü∂ X ‚äû Y) :=
  BinaryBicone.inlCokernelCofork _

@[simp]
theorem biprod.inlCokernelCofork_œÄ : Cofork.œÄ (biprod.inlCokernelCofork X Y) = biprod.snd :=
  rfl

/-- The cofork `biprod.inlCokernelFork` is indeed a colimit. -/
def biprod.isCokernelInlCokernelFork : IsColimit (biprod.inlCokernelCofork X Y) :=
  BinaryBicone.isColimitInlCokernelCofork (BinaryBiproduct.isColimit _ _)

/-- A cokernel cofork for the cokernel of `biprod.inr`. It consists of the
morphism `biprod.fst`. -/
def biprod.inrCokernelCofork : CokernelCofork (biprod.inr : Y ‚ü∂ X ‚äû Y) :=
  BinaryBicone.inrCokernelCofork _

@[simp]
theorem biprod.inrCokernelCofork_œÄ : Cofork.œÄ (biprod.inrCokernelCofork X Y) = biprod.fst :=
  rfl

/-- The cofork `biprod.inrCokernelFork` is indeed a colimit. -/
def biprod.isCokernelInrCokernelFork : IsColimit (biprod.inrCokernelCofork X Y) :=
  BinaryBicone.isColimitInrCokernelCofork (BinaryBiproduct.isColimit _ _)

end HasBinaryBiproduct

variable {X Y : C} [HasBinaryBiproduct X Y]

instance : HasKernel (biprod.fst : X ‚äû Y ‚ü∂ X) :=
  HasLimit.mk ‚ü®_, biprod.isKernelFstKernelFork X Y‚ü©

/-- The kernel of `biprod.fst : X ‚äû Y ‚ü∂ X` is `Y`. -/
@[simps!]
def kernelBiprodFstIso : kernel (biprod.fst : X ‚äû Y ‚ü∂ X) ‚âÖ Y :=
  limit.isoLimitCone ‚ü®_, biprod.isKernelFstKernelFork X Y‚ü©

instance : HasKernel (biprod.snd : X ‚äû Y ‚ü∂ Y) :=
  HasLimit.mk ‚ü®_, biprod.isKernelSndKernelFork X Y‚ü©

/-- The kernel of `biprod.snd : X ‚äû Y ‚ü∂ Y` is `X`. -/
@[simps!]
def kernelBiprodSndIso : kernel (biprod.snd : X ‚äû Y ‚ü∂ Y) ‚âÖ X :=
  limit.isoLimitCone ‚ü®_, biprod.isKernelSndKernelFork X Y‚ü©

instance : HasCokernel (biprod.inl : X ‚ü∂ X ‚äû Y) :=
  HasColimit.mk ‚ü®_, biprod.isCokernelInlCokernelFork X Y‚ü©

/-- The cokernel of `biprod.inl : X ‚ü∂ X ‚äû Y` is `Y`. -/
@[simps!]
def cokernelBiprodInlIso : cokernel (biprod.inl : X ‚ü∂ X ‚äû Y) ‚âÖ Y :=
  colimit.isoColimitCocone ‚ü®_, biprod.isCokernelInlCokernelFork X Y‚ü©

instance : HasCokernel (biprod.inr : Y ‚ü∂ X ‚äû Y) :=
  HasColimit.mk ‚ü®_, biprod.isCokernelInrCokernelFork X Y‚ü©

/-- The cokernel of `biprod.inr : Y ‚ü∂ X ‚äû Y` is `X`. -/
@[simps!]
def cokernelBiprodInrIso : cokernel (biprod.inr : Y ‚ü∂ X ‚äû Y) ‚âÖ X :=
  colimit.isoColimitCocone ‚ü®_, biprod.isCokernelInrCokernelFork X Y‚ü©

end BiprodKernel

section IsZero

/-- If `Y` is a zero object, `X ‚âÖ X ‚äû Y` for any `X`. -/
@[simps!]
def isoBiprodZero {X Y : C} [HasBinaryBiproduct X Y] (hY : IsZero Y) : X ‚âÖ X ‚äû Y where
  hom := biprod.inl
  inv := biprod.fst
  inv_hom_id := by
    apply CategoryTheory.Limits.biprod.hom_ext <;>
      simp only [Category.assoc, biprod.inl_fst, Category.comp_id, Category.id_comp, biprod.inl_snd,
        comp_zero]
    apply hY.eq_of_tgt

/-- If `X` is a zero object, `Y ‚âÖ X ‚äû Y` for any `Y`. -/
@[simps]
def isoZeroBiprod {X Y : C} [HasBinaryBiproduct X Y] (hY : IsZero X) : Y ‚âÖ X ‚äû Y where
  hom := biprod.inr
  inv := biprod.snd
  inv_hom_id := by
    apply CategoryTheory.Limits.biprod.hom_ext <;>
      simp only [Category.assoc, biprod.inr_snd, Category.comp_id, Category.id_comp, biprod.inr_fst,
        comp_zero]
    apply hY.eq_of_tgt

@[simp]
lemma biprod_isZero_iff (A B : C) [HasBinaryBiproduct A B] :
    IsZero (biprod A B) ‚Üî IsZero A ‚àß IsZero B := by
  constructor
  ¬∑ intro h
    simp only [IsZero.iff_id_eq_zero] at h ‚ä¢
    simp only [show ùüô A = biprod.inl ‚â´ ùüô (A ‚äû B) ‚â´ biprod.fst by simp,
      show ùüô B = biprod.inr ‚â´ ùüô (A ‚äû B) ‚â´ biprod.snd by simp, h, zero_comp, comp_zero,
      and_self]
  ¬∑ rintro ‚ü®hA, hB‚ü©
    rw [IsZero.iff_id_eq_zero]
    apply biprod.hom_ext
    ¬∑ apply hA.eq_of_tgt
    ¬∑ apply hB.eq_of_tgt

end IsZero

section

variable [HasBinaryBiproducts C]

/-- The braiding isomorphism which swaps a binary biproduct. -/
@[simps]
def biprod.braiding (P Q : C) : P ‚äû Q ‚âÖ Q ‚äû P where
  hom := biprod.lift biprod.snd biprod.fst
  inv := biprod.lift biprod.snd biprod.fst

/-- An alternative formula for the braiding isomorphism which swaps a binary biproduct,
using the fact that the biproduct is a coproduct. -/
@[simps]
def biprod.braiding' (P Q : C) : P ‚äû Q ‚âÖ Q ‚äû P where
  hom := biprod.desc biprod.inr biprod.inl
  inv := biprod.desc biprod.inr biprod.inl

theorem biprod.braiding'_eq_braiding {P Q : C} : biprod.braiding' P Q = biprod.braiding P Q := by
  cat_disch

/-- The braiding isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem biprod.braid_natural {W X Y Z : C} (f : X ‚ü∂ Y) (g : Z ‚ü∂ W) :
    biprod.map f g ‚â´ (biprod.braiding _ _).hom = (biprod.braiding _ _).hom ‚â´ biprod.map g f := by
  cat_disch

@[reassoc]
theorem biprod.braiding_map_braiding {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) :
    (biprod.braiding X W).hom ‚â´ biprod.map f g ‚â´ (biprod.braiding Y Z).hom = biprod.map g f := by
  cat_disch

@[reassoc (attr := simp)]
theorem biprod.symmetry' (P Q : C) :
    biprod.lift biprod.snd biprod.fst ‚â´ biprod.lift biprod.snd biprod.fst = ùüô (P ‚äû Q) := by
  cat_disch

/-- The braiding isomorphism is symmetric. -/
@[reassoc]
theorem biprod.symmetry (P Q : C) :
    (biprod.braiding P Q).hom ‚â´ (biprod.braiding Q P).hom = ùüô _ := by simp

/-- The associator isomorphism which associates a binary biproduct. -/
@[simps]
def biprod.associator (P Q R : C) : (P ‚äû Q) ‚äû R ‚âÖ P ‚äû (Q ‚äû R) where
  hom := biprod.lift (biprod.fst ‚â´ biprod.fst) (biprod.lift (biprod.fst ‚â´ biprod.snd) biprod.snd)
  inv := biprod.lift (biprod.lift biprod.fst (biprod.snd ‚â´ biprod.fst)) (biprod.snd ‚â´ biprod.snd)

/-- The associator isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem biprod.associator_natural {U V W X Y Z : C} (f : U ‚ü∂ X) (g : V ‚ü∂ Y) (h : W ‚ü∂ Z) :
    biprod.map (biprod.map f g) h ‚â´ (biprod.associator _ _ _).hom
      = (biprod.associator _ _ _).hom ‚â´ biprod.map f (biprod.map g h) := by
  cat_disch

/-- The associator isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem biprod.associator_inv_natural {U V W X Y Z : C} (f : U ‚ü∂ X) (g : V ‚ü∂ Y) (h : W ‚ü∂ Z) :
    biprod.map f (biprod.map g h) ‚â´ (biprod.associator _ _ _).inv
      = (biprod.associator _ _ _).inv ‚â´ biprod.map (biprod.map f g) h := by
  cat_disch

end

end Limits

open CategoryTheory.Limits

-- TODO:
-- If someone is interested, they could provide the constructions:
--   HasBinaryBiproducts ‚Üî HasFiniteBiproducts
variable {C : Type u} [Category.{v} C] [HasZeroMorphisms C] [HasBinaryBiproducts C]

/-- An object is indecomposable if it cannot be written as the biproduct of two nonzero objects. -/
def Indecomposable (X : C) : Prop :=
  ¬¨IsZero X ‚àß ‚àÄ Y Z, (X ‚âÖ Y ‚äû Z) ‚Üí IsZero Y ‚à® IsZero Z

/-- If
```
(f 0)
(0 g)
```
is invertible, then `f` is invertible.
-/
theorem isIso_left_of_isIso_biprod_map {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z)
    [IsIso (biprod.map f g)] : IsIso f :=
  ‚ü®‚ü®biprod.inl ‚â´ inv (biprod.map f g) ‚â´ biprod.fst,
      ‚ü®by
        have t := congrArg (fun p : W ‚äû X ‚ü∂ W ‚äû X => biprod.inl ‚â´ p ‚â´ biprod.fst)
          (IsIso.hom_inv_id (biprod.map f g))
        simp only [Category.id_comp, Category.assoc, biprod.inl_map_assoc] at t
        simp [t], by
        have t := congrArg (fun p : Y ‚äû Z ‚ü∂ Y ‚äû Z => biprod.inl ‚â´ p ‚â´ biprod.fst)
          (IsIso.inv_hom_id (biprod.map f g))
        simp only [Category.id_comp, Category.assoc, biprod.map_fst] at t
        simp only [Category.assoc]
        simp [t]‚ü©‚ü©‚ü©

/-- If
```
(f 0)
(0 g)
```
is invertible, then `g` is invertible.
-/
theorem isIso_right_of_isIso_biprod_map {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z)
    [IsIso (biprod.map f g)] : IsIso g :=
  letI : IsIso (biprod.map g f) := by
    rw [‚Üê biprod.braiding_map_braiding]
    infer_instance
  isIso_left_of_isIso_biprod_map g f

end CategoryTheory
