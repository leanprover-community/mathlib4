/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers

/-!
# Split coequalizers

We define what it means for a triple of morphisms `f g : X âŸ¶ Y`, `Ï€ : Y âŸ¶ Z` to be a split
coequalizer: there is a section `s` of `Ï€` and a section `t` of `g`, which additionally satisfy
`t â‰« f = Ï€ â‰« s`.

In addition, we show that every split coequalizer is a coequalizer
(`CategoryTheory.IsSplitCoequalizer.isCoequalizer`) and absolute
(`CategoryTheory.IsSplitCoequalizer.map`)

A pair `f g : X âŸ¶ Y` has a split coequalizer if there is a `Z` and `Ï€ : Y âŸ¶ Z` making `f,g,Ï€` a
split coequalizer.
A pair `f g : X âŸ¶ Y` has a `G`-split coequalizer if `G f, G g` has a split coequalizer.

These definitions and constructions are useful in particular for the monadicity theorems.

This file has been adapted to `Mathlib/CategoryTheory/Limits/Shapes/SplitEqualizer.lean`. Please try
to keep them in sync.

-/


namespace CategoryTheory

universe v vâ‚‚ u uâ‚‚

variable {C : Type u} [Category.{v} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]
variable (G : C â¥¤ D)
variable {X Y : C} (f g : X âŸ¶ Y)

/-- A split coequalizer diagram consists of morphisms

      f   Ï€
    X â‡‰ Y â†’ Z
      g

satisfying `f â‰« Ï€ = g â‰« Ï€` together with morphisms

      t   s
    X â† Y â† Z

satisfying `s â‰« Ï€ = ğŸ™ Z`, `t â‰« g = ğŸ™ Y` and `t â‰« f = Ï€ â‰« s`.

The name "coequalizer" is appropriate, since any split coequalizer is a coequalizer, see
`CategoryTheory.IsSplitCoequalizer.isCoequalizer`.
Split coequalizers are also absolute, since a functor preserves all the structure above.
-/
structure IsSplitCoequalizer {Z : C} (Ï€ : Y âŸ¶ Z) where
  /-- A map from the coequalizer to `Y` -/
  rightSection : Z âŸ¶ Y
  /-- A map in the opposite direction to `f` and `g` -/
  leftSection : Y âŸ¶ X
  /-- Composition of `Ï€` with `f` and with `g` agree -/
  condition : f â‰« Ï€ = g â‰« Ï€ := by cat_disch
  /-- `rightSection` splits `Ï€` -/
  rightSection_Ï€ : rightSection â‰« Ï€ = ğŸ™ Z := by cat_disch
  /-- `leftSection` splits `g` -/
  leftSection_bottom : leftSection â‰« g = ğŸ™ Y := by cat_disch
  /-- `leftSection` composed with `f` is `pi` composed with `rightSection` -/
  leftSection_top : leftSection â‰« f = Ï€ â‰« rightSection := by cat_disch

instance {X : C} : Inhabited (IsSplitCoequalizer (ğŸ™ X) (ğŸ™ X) (ğŸ™ X)) where
  default := { rightSection := ğŸ™ X, leftSection := ğŸ™ X }

open IsSplitCoequalizer

attribute [reassoc] condition

attribute [reassoc (attr := simp)] rightSection_Ï€ leftSection_bottom leftSection_top

variable {f g}

/-- Split coequalizers are absolute: they are preserved by any functor. -/
@[simps]
def IsSplitCoequalizer.map {Z : C} {Ï€ : Y âŸ¶ Z} (q : IsSplitCoequalizer f g Ï€) (F : C â¥¤ D) :
    IsSplitCoequalizer (F.map f) (F.map g) (F.map Ï€) where
  rightSection := F.map q.rightSection
  leftSection := F.map q.leftSection
  condition := by rw [â† F.map_comp, q.condition, F.map_comp]
  rightSection_Ï€ := by rw [â† F.map_comp, q.rightSection_Ï€, F.map_id]
  leftSection_bottom := by rw [â† F.map_comp, q.leftSection_bottom, F.map_id]
  leftSection_top := by rw [â† F.map_comp, q.leftSection_top, F.map_comp]

section

open Limits

/-- A split coequalizer clearly induces a cofork. -/
@[simps! pt]
def IsSplitCoequalizer.asCofork {Z : C} {h : Y âŸ¶ Z} (t : IsSplitCoequalizer f g h) :
    Cofork f g := Cofork.ofÏ€ h t.condition

@[simp]
theorem IsSplitCoequalizer.asCofork_Ï€ {Z : C} {h : Y âŸ¶ Z} (t : IsSplitCoequalizer f g h) :
    t.asCofork.Ï€ = h := rfl

/--
The cofork induced by a split coequalizer is a coequalizer, justifying the name. In some cases it
is more convenient to show a given cofork is a coequalizer by showing it is split.
-/
def IsSplitCoequalizer.isCoequalizer {Z : C} {h : Y âŸ¶ Z} (t : IsSplitCoequalizer f g h) :
    IsColimit t.asCofork :=
  Cofork.IsColimit.mk' _ fun s â†¦
    âŸ¨t.rightSection â‰« s.Ï€, by
      dsimp
      rw [â† t.leftSection_top_assoc, s.condition, t.leftSection_bottom_assoc], fun hm â†¦ by
      simp [â† hm]âŸ©

end

variable (f g)

/--
The pair `f,g` is a split pair if there is an `h : Y âŸ¶ Z` so that `f, g, h` forms a split
coequalizer in `C`.
-/
class HasSplitCoequalizer : Prop where
  /-- There is some split coequalizer -/
  splittable : âˆƒ (Z : C) (h : Y âŸ¶ Z), Nonempty (IsSplitCoequalizer f g h)

/--
The pair `f,g` is a `G`-split pair if there is an `h : G Y âŸ¶ Z` so that `G f, G g, h` forms a split
coequalizer in `D`.
-/
abbrev Functor.IsSplitPair : Prop :=
  HasSplitCoequalizer (G.map f) (G.map g)

/-- Get the coequalizer object from the typeclass `IsSplitPair`. -/
noncomputable def HasSplitCoequalizer.coequalizerOfSplit [HasSplitCoequalizer f g] : C :=
  (splittable (f := f) (g := g)).choose

/-- Get the coequalizer morphism from the typeclass `IsSplitPair`. -/
noncomputable def HasSplitCoequalizer.coequalizerÏ€ [HasSplitCoequalizer f g] :
    Y âŸ¶ HasSplitCoequalizer.coequalizerOfSplit f g :=
  (splittable (f := f) (g := g)).choose_spec.choose

/-- The coequalizer morphism `coequalizeÏ€` gives a split coequalizer on `f,g`. -/
noncomputable def HasSplitCoequalizer.isSplitCoequalizer [HasSplitCoequalizer f g] :
    IsSplitCoequalizer f g (HasSplitCoequalizer.coequalizerÏ€ f g) :=
  Classical.choice (splittable (f := f) (g := g)).choose_spec.choose_spec

/-- If `f, g` is split, then `G f, G g` is split. -/
instance map_is_split_pair [HasSplitCoequalizer f g] : HasSplitCoequalizer (G.map f) (G.map g) where
  splittable :=
    âŸ¨_, _, âŸ¨IsSplitCoequalizer.map (HasSplitCoequalizer.isSplitCoequalizer f g) _âŸ©âŸ©

namespace Limits

/-- If a pair has a split coequalizer, it has a coequalizer. -/
instance (priority := 1) hasCoequalizer_of_hasSplitCoequalizer [HasSplitCoequalizer f g] :
    HasCoequalizer f g :=
  HasColimit.mk âŸ¨_, (HasSplitCoequalizer.isSplitCoequalizer f g).isCoequalizerâŸ©

end Limits

end CategoryTheory
