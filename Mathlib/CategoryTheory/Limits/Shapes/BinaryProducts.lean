/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Comma.Over.Basic
import Mathlib.CategoryTheory.Discrete.Basic
import Mathlib.CategoryTheory.EpiMono
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

/-!
# Binary (co)products

We define a category `WalkingPair`, which is the index category
for a binary (co)product diagram. A convenience method `pair X Y`
constructs the functor from the walking pair, hitting the given objects.

We define `prod X Y` and `coprod X Y` as limits and colimits of such functors.

Typeclasses `HasBinaryProducts` and `HasBinaryCoproducts` assert the existence
of (co)limits shaped as walking pairs.

We include lemmas for simplifying equations involving projections and coprojections, and define
braiding and associating isomorphisms, and the product comparison morphism.

## References
* [Stacks: Products of pairs](https://stacks.math.columbia.edu/tag/001R)
* [Stacks: coproducts of pairs](https://stacks.math.columbia.edu/tag/04AN)
-/

universe v vâ‚ u uâ‚ uâ‚‚

open CategoryTheory Category

namespace CategoryTheory.Limits

/-- The type of objects for the diagram indexing a binary (co)product. -/
inductive WalkingPair : Type
  | left
  | right
  deriving DecidableEq, Inhabited

open WalkingPair

/-- The equivalence swapping left and right.
-/
def WalkingPair.swap : WalkingPair â‰ƒ WalkingPair where
  toFun
    | left => right
    | right => left
  invFun
    | left => right
    | right => left
  left_inv j := by cases j <;> rfl
  right_inv j := by cases j <;> rfl

@[simp]
theorem WalkingPair.swap_apply_left : WalkingPair.swap left = right :=
  rfl

@[simp]
theorem WalkingPair.swap_apply_right : WalkingPair.swap right = left :=
  rfl

@[simp]
theorem WalkingPair.swap_symm_apply_tt : WalkingPair.swap.symm left = right :=
  rfl

@[simp]
theorem WalkingPair.swap_symm_apply_ff : WalkingPair.swap.symm right = left :=
  rfl

/-- An equivalence from `WalkingPair` to `Bool`, sometimes useful when reindexing limits.
-/
def WalkingPair.equivBool : WalkingPair â‰ƒ Bool where
  toFun
    | left => true
    | right => false
  -- to match equiv.sum_equiv_sigma_bool
  invFun b := Bool.recOn b right left
  left_inv j := by cases j <;> rfl
  right_inv b := by cases b <;> rfl

@[simp]
theorem WalkingPair.equivBool_apply_left : WalkingPair.equivBool left = true :=
  rfl

@[simp]
theorem WalkingPair.equivBool_apply_right : WalkingPair.equivBool right = false :=
  rfl

@[simp]
theorem WalkingPair.equivBool_symm_apply_true : WalkingPair.equivBool.symm true = left :=
  rfl

@[simp]
theorem WalkingPair.equivBool_symm_apply_false : WalkingPair.equivBool.symm false = right :=
  rfl

variable {C : Type u}

/-- The function on the walking pair, sending the two points to `X` and `Y`. -/
def pairFunction (X Y : C) : WalkingPair â†’ C := fun j => WalkingPair.casesOn j X Y

@[simp]
theorem pairFunction_left (X Y : C) : pairFunction X Y left = X :=
  rfl

@[simp]
theorem pairFunction_right (X Y : C) : pairFunction X Y right = Y :=
  rfl

variable [Category.{v} C]

/-- The diagram on the walking pair, sending the two points to `X` and `Y`. -/
def pair (X Y : C) : Discrete WalkingPair â¥¤ C :=
  Discrete.functor fun j => WalkingPair.casesOn j X Y

@[simp]
theorem pair_obj_left (X Y : C) : (pair X Y).obj âŸ¨leftâŸ© = X :=
  rfl

@[simp]
theorem pair_obj_right (X Y : C) : (pair X Y).obj âŸ¨rightâŸ© = Y :=
  rfl

section

variable {F G : Discrete WalkingPair â¥¤ C} (f : F.obj âŸ¨leftâŸ© âŸ¶ G.obj âŸ¨leftâŸ©)
  (g : F.obj âŸ¨rightâŸ© âŸ¶ G.obj âŸ¨rightâŸ©)

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases

/-- The natural transformation between two functors out of the
walking pair, specified by its components. -/
def mapPair : F âŸ¶ G where
  app
    | âŸ¨leftâŸ© => f
    | âŸ¨rightâŸ© => g
  naturality := fun âŸ¨XâŸ© âŸ¨YâŸ© âŸ¨âŸ¨uâŸ©âŸ© => by cat_disch

@[simp]
theorem mapPair_left : (mapPair f g).app âŸ¨leftâŸ© = f :=
  rfl

@[simp]
theorem mapPair_right : (mapPair f g).app âŸ¨rightâŸ© = g :=
  rfl

/-- The natural isomorphism between two functors out of the walking pair, specified by its
components. -/
@[simps!]
def mapPairIso (f : F.obj âŸ¨leftâŸ© â‰… G.obj âŸ¨leftâŸ©) (g : F.obj âŸ¨rightâŸ© â‰… G.obj âŸ¨rightâŸ©) : F â‰… G :=
  NatIso.ofComponents (fun j â†¦ match j with
    | âŸ¨leftâŸ© => f
    | âŸ¨rightâŸ© => g)
    (fun âŸ¨âŸ¨uâŸ©âŸ© => by cat_disch)

end

/-- Every functor out of the walking pair is naturally isomorphic (actually, equal) to a `pair` -/
@[simps!]
def diagramIsoPair (F : Discrete WalkingPair â¥¤ C) :
    F â‰… pair (F.obj âŸ¨WalkingPair.leftâŸ©) (F.obj âŸ¨WalkingPair.rightâŸ©) :=
  mapPairIso (Iso.refl _) (Iso.refl _)

section

variable {D : Type uâ‚} [Category.{vâ‚} D]

/-- The natural isomorphism between `pair X Y â‹™ F` and `pair (F.obj X) (F.obj Y)`. -/
def pairComp (X Y : C) (F : C â¥¤ D) : pair X Y â‹™ F â‰… pair (F.obj X) (F.obj Y) :=
  diagramIsoPair _

end

/-- A binary fan is just a cone on a diagram indexing a product. -/
abbrev BinaryFan (X Y : C) :=
  Cone (pair X Y)

/-- The first projection of a binary fan. -/
abbrev BinaryFan.fst {X Y : C} (s : BinaryFan X Y) :=
  s.Ï€.app âŸ¨WalkingPair.leftâŸ©

/-- The second projection of a binary fan. -/
abbrev BinaryFan.snd {X Y : C} (s : BinaryFan X Y) :=
  s.Ï€.app âŸ¨WalkingPair.rightâŸ©

-- Marking this `@[simp]` causes loops since `s.fst` is reducibly defeq to the LHS.
theorem BinaryFan.Ï€_app_left {X Y : C} (s : BinaryFan X Y) : s.Ï€.app âŸ¨WalkingPair.leftâŸ© = s.fst :=
  rfl

-- Marking this `@[simp]` causes loops since `s.snd` is reducibly defeq to the LHS.
theorem BinaryFan.Ï€_app_right {X Y : C} (s : BinaryFan X Y) : s.Ï€.app âŸ¨WalkingPair.rightâŸ© = s.snd :=
  rfl

/-- Constructs an isomorphism of `BinaryFan`s out of an isomorphism of the tips that commutes with
the projections. -/
def BinaryFan.ext {A B : C} {c c' : BinaryFan A B} (e : c.pt â‰… c'.pt)
    (hâ‚ : c.fst = e.hom â‰« c'.fst) (hâ‚‚ : c.snd = e.hom â‰« c'.snd) : c â‰… c' :=
  Cones.ext e (fun j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> assumption)

@[simp]
lemma BinaryFan.ext_hom_hom {A B : C} {c c' : BinaryFan A B} (e : c.pt â‰… c'.pt)
    (hâ‚ : c.fst = e.hom â‰« c'.fst) (hâ‚‚ : c.snd = e.hom â‰« c'.snd) :
    (ext e hâ‚ hâ‚‚).hom.hom = e.hom := rfl

/-- A convenient way to show that a binary fan is a limit. -/
def BinaryFan.IsLimit.mk {X Y : C} (s : BinaryFan X Y)
    (lift : âˆ€ {T : C} (_ : T âŸ¶ X) (_ : T âŸ¶ Y), T âŸ¶ s.pt)
    (hlâ‚ : âˆ€ {T : C} (f : T âŸ¶ X) (g : T âŸ¶ Y), lift f g â‰« s.fst = f)
    (hlâ‚‚ : âˆ€ {T : C} (f : T âŸ¶ X) (g : T âŸ¶ Y), lift f g â‰« s.snd = g)
    (uniq :
      âˆ€ {T : C} (f : T âŸ¶ X) (g : T âŸ¶ Y) (m : T âŸ¶ s.pt) (_ : m â‰« s.fst = f) (_ : m â‰« s.snd = g),
        m = lift f g) :
    IsLimit s :=
  Limits.IsLimit.mk (fun t => lift (BinaryFan.fst t) (BinaryFan.snd t))
    (by
      rintro t (rfl | rfl)
      Â· exact hlâ‚ _ _
      Â· exact hlâ‚‚ _ _)
    fun _ _ h => uniq _ _ _ (h âŸ¨WalkingPair.leftâŸ©) (h âŸ¨WalkingPair.rightâŸ©)

theorem BinaryFan.IsLimit.hom_ext {W X Y : C} {s : BinaryFan X Y} (h : IsLimit s) {f g : W âŸ¶ s.pt}
    (hâ‚ : f â‰« s.fst = g â‰« s.fst) (hâ‚‚ : f â‰« s.snd = g â‰« s.snd) : f = g :=
  h.hom_ext fun j => Discrete.recOn j fun j => WalkingPair.casesOn j hâ‚ hâ‚‚

/-- A binary cofan is just a cocone on a diagram indexing a coproduct. -/
abbrev BinaryCofan (X Y : C) := Cocone (pair X Y)

/-- The first inclusion of a binary cofan. -/
abbrev BinaryCofan.inl {X Y : C} (s : BinaryCofan X Y) := s.Î¹.app âŸ¨WalkingPair.leftâŸ©

/-- The second inclusion of a binary cofan. -/
abbrev BinaryCofan.inr {X Y : C} (s : BinaryCofan X Y) := s.Î¹.app âŸ¨WalkingPair.rightâŸ©

/-- Constructs an isomorphism of `BinaryCofan`s out of an isomorphism of the tips that commutes with
the injections. -/
def BinaryCofan.ext {A B : C} {c c' : BinaryCofan A B} (e : c.pt â‰… c'.pt)
    (hâ‚ : c.inl â‰« e.hom = c'.inl) (hâ‚‚ : c.inr â‰« e.hom = c'.inr) : c â‰… c' :=
  Cocones.ext e (fun j => by rcases j with âŸ¨âŸ¨âŸ©âŸ© <;> assumption)

@[simp]
lemma BinaryCofan.ext_hom_hom {A B : C} {c c' : BinaryCofan A B} (e : c.pt â‰… c'.pt)
    (hâ‚ : c.inl â‰« e.hom = c'.inl) (hâ‚‚ : c.inr â‰« e.hom = c'.inr) :
    (ext e hâ‚ hâ‚‚).hom.hom = e.hom := rfl

-- This cannot be `@[simp]` because `s.inl` is reducibly defeq to the LHS.
theorem BinaryCofan.Î¹_app_left {X Y : C} (s : BinaryCofan X Y) :
    s.Î¹.app âŸ¨WalkingPair.leftâŸ© = s.inl := rfl

-- This cannot be `@[simp]` because `s.inr` is reducibly defeq to the LHS.
theorem BinaryCofan.Î¹_app_right {X Y : C} (s : BinaryCofan X Y) :
    s.Î¹.app âŸ¨WalkingPair.rightâŸ© = s.inr := rfl

/-- A convenient way to show that a binary cofan is a colimit. -/
def BinaryCofan.IsColimit.mk {X Y : C} (s : BinaryCofan X Y)
    (desc : âˆ€ {T : C} (_ : X âŸ¶ T) (_ : Y âŸ¶ T), s.pt âŸ¶ T)
    (hdâ‚ : âˆ€ {T : C} (f : X âŸ¶ T) (g : Y âŸ¶ T), s.inl â‰« desc f g = f)
    (hdâ‚‚ : âˆ€ {T : C} (f : X âŸ¶ T) (g : Y âŸ¶ T), s.inr â‰« desc f g = g)
    (uniq :
      âˆ€ {T : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) (m : s.pt âŸ¶ T) (_ : s.inl â‰« m = f) (_ : s.inr â‰« m = g),
        m = desc f g) :
    IsColimit s :=
  Limits.IsColimit.mk (fun t => desc (BinaryCofan.inl t) (BinaryCofan.inr t))
    (by
      rintro t (rfl | rfl)
      Â· exact hdâ‚ _ _
      Â· exact hdâ‚‚ _ _)
    fun _ _ h => uniq _ _ _ (h âŸ¨WalkingPair.leftâŸ©) (h âŸ¨WalkingPair.rightâŸ©)

theorem BinaryCofan.IsColimit.hom_ext {W X Y : C} {s : BinaryCofan X Y} (h : IsColimit s)
    {f g : s.pt âŸ¶ W} (hâ‚ : s.inl â‰« f = s.inl â‰« g) (hâ‚‚ : s.inr â‰« f = s.inr â‰« g) : f = g :=
  h.hom_ext fun j => Discrete.recOn j fun j => WalkingPair.casesOn j hâ‚ hâ‚‚

variable {X Y : C}

section

attribute [local aesop safe tactic (rule_sets := [CategoryTheory])]
  CategoryTheory.Discrete.discreteCases
-- TODO: would it be okay to use this more generally?
attribute [local aesop safe cases (rule_sets := [CategoryTheory])] Eq

/-- A binary fan with vertex `P` consists of the two projections `Ï€â‚ : P âŸ¶ X` and `Ï€â‚‚ : P âŸ¶ Y`. -/
@[simps pt]
def BinaryFan.mk {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) : BinaryFan X Y where
  pt := P
  Ï€ := { app := fun | { as := j } => match j with | left => Ï€â‚ | right => Ï€â‚‚ }

/-- A binary cofan with vertex `P` consists of the two inclusions `Î¹â‚ : X âŸ¶ P` and `Î¹â‚‚ : Y âŸ¶ P`. -/
@[simps pt]
def BinaryCofan.mk {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) : BinaryCofan X Y where
  pt := P
  Î¹ := { app := fun | { as := j } => match j with | left => Î¹â‚ | right => Î¹â‚‚ }

end

@[simp]
theorem BinaryFan.mk_fst {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) : (BinaryFan.mk Ï€â‚ Ï€â‚‚).fst = Ï€â‚ :=
  rfl

@[simp]
theorem BinaryFan.mk_snd {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) : (BinaryFan.mk Ï€â‚ Ï€â‚‚).snd = Ï€â‚‚ :=
  rfl

@[simp]
theorem BinaryCofan.mk_inl {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) : (BinaryCofan.mk Î¹â‚ Î¹â‚‚).inl = Î¹â‚ :=
  rfl

@[simp]
theorem BinaryCofan.mk_inr {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) : (BinaryCofan.mk Î¹â‚ Î¹â‚‚).inr = Î¹â‚‚ :=
  rfl

/-- Every `BinaryFan` is isomorphic to an application of `BinaryFan.mk`. -/
def isoBinaryFanMk {X Y : C} (c : BinaryFan X Y) : c â‰… BinaryFan.mk c.fst c.snd :=
    Cones.ext (Iso.refl _) fun âŸ¨lâŸ© => by cases l; repeat simp

/-- Every `BinaryFan` is isomorphic to an application of `BinaryFan.mk`. -/
def isoBinaryCofanMk {X Y : C} (c : BinaryCofan X Y) : c â‰… BinaryCofan.mk c.inl c.inr :=
    Cocones.ext (Iso.refl _) fun âŸ¨lâŸ© => by cases l; repeat simp

/-- This is a more convenient formulation to show that a `BinaryFan` constructed using
`BinaryFan.mk` is a limit cone.
-/
def BinaryFan.isLimitMk {W : C} {fst : W âŸ¶ X} {snd : W âŸ¶ Y} (lift : âˆ€ s : BinaryFan X Y, s.pt âŸ¶ W)
    (fac_left : âˆ€ s : BinaryFan X Y, lift s â‰« fst = s.fst)
    (fac_right : âˆ€ s : BinaryFan X Y, lift s â‰« snd = s.snd)
    (uniq :
      âˆ€ (s : BinaryFan X Y) (m : s.pt âŸ¶ W) (_ : m â‰« fst = s.fst) (_ : m â‰« snd = s.snd),
        m = lift s) :
    IsLimit (BinaryFan.mk fst snd) :=
  { lift := lift
    fac := fun s j => by
      rcases j with âŸ¨âŸ¨âŸ©âŸ©
      exacts [fac_left s, fac_right s]
    uniq := fun s m w => uniq s m (w âŸ¨WalkingPair.leftâŸ©) (w âŸ¨WalkingPair.rightâŸ©) }

/-- This is a more convenient formulation to show that a `BinaryCofan` constructed using
`BinaryCofan.mk` is a colimit cocone.
-/
def BinaryCofan.isColimitMk {W : C} {inl : X âŸ¶ W} {inr : Y âŸ¶ W}
    (desc : âˆ€ s : BinaryCofan X Y, W âŸ¶ s.pt)
    (fac_left : âˆ€ s : BinaryCofan X Y, inl â‰« desc s = s.inl)
    (fac_right : âˆ€ s : BinaryCofan X Y, inr â‰« desc s = s.inr)
    (uniq :
      âˆ€ (s : BinaryCofan X Y) (m : W âŸ¶ s.pt) (_ : inl â‰« m = s.inl) (_ : inr â‰« m = s.inr),
        m = desc s) :
    IsColimit (BinaryCofan.mk inl inr) :=
  { desc := desc
    fac := fun s j => by
      rcases j with âŸ¨âŸ¨âŸ©âŸ©
      exacts [fac_left s, fac_right s]
    uniq := fun s m w => uniq s m (w âŸ¨WalkingPair.leftâŸ©) (w âŸ¨WalkingPair.rightâŸ©) }

/-- If `s` is a limit binary fan over `X` and `Y`, then every pair of morphisms `f : W âŸ¶ X` and
`g : W âŸ¶ Y` induces a morphism `l : W âŸ¶ s.pt` satisfying `l â‰« s.fst = f` and `l â‰« s.snd = g`.
-/
@[simps]
def BinaryFan.IsLimit.lift' {W X Y : C} {s : BinaryFan X Y} (h : IsLimit s) (f : W âŸ¶ X)
    (g : W âŸ¶ Y) : { l : W âŸ¶ s.pt // l â‰« s.fst = f âˆ§ l â‰« s.snd = g } :=
  âŸ¨h.lift <| BinaryFan.mk f g, h.fac _ _, h.fac _ _âŸ©

/-- If `s` is a colimit binary cofan over `X` and `Y`,, then every pair of morphisms `f : X âŸ¶ W` and
`g : Y âŸ¶ W` induces a morphism `l : s.pt âŸ¶ W` satisfying `s.inl â‰« l = f` and `s.inr â‰« l = g`.
-/
@[simps]
def BinaryCofan.IsColimit.desc' {W X Y : C} {s : BinaryCofan X Y} (h : IsColimit s) (f : X âŸ¶ W)
    (g : Y âŸ¶ W) : { l : s.pt âŸ¶ W // s.inl â‰« l = f âˆ§ s.inr â‰« l = g } :=
  âŸ¨h.desc <| BinaryCofan.mk f g, h.fac _ _, h.fac _ _âŸ©

/-- Binary products are symmetric. -/
def BinaryFan.isLimitFlip {X Y : C} {c : BinaryFan X Y} (hc : IsLimit c) :
    IsLimit (BinaryFan.mk c.snd c.fst) :=
  BinaryFan.isLimitMk (fun s => hc.lift (BinaryFan.mk s.snd s.fst)) (fun _ => hc.fac _ _)
    (fun _ => hc.fac _ _) fun s _ eâ‚ eâ‚‚ =>
    BinaryFan.IsLimit.hom_ext hc
      (eâ‚‚.trans (hc.fac (BinaryFan.mk s.snd s.fst) âŸ¨WalkingPair.leftâŸ©).symm)
      (eâ‚.trans (hc.fac (BinaryFan.mk s.snd s.fst) âŸ¨WalkingPair.rightâŸ©).symm)

theorem BinaryFan.isLimit_iff_isIso_fst {X Y : C} (h : IsTerminal Y) (c : BinaryFan X Y) :
    Nonempty (IsLimit c) â†” IsIso c.fst := by
  constructor
  Â· rintro âŸ¨HâŸ©
    obtain âŸ¨l, hl, -âŸ© := BinaryFan.IsLimit.lift' H (ğŸ™ X) (h.from X)
    exact
      âŸ¨âŸ¨l,
          BinaryFan.IsLimit.hom_ext H (by simpa [hl, -comp_id] using comp_id _)
            (h.hom_ext _ _),
          hlâŸ©âŸ©
  Â· intro
    exact
      âŸ¨BinaryFan.IsLimit.mk _ (fun f _ => f â‰« inv c.fst) (fun _ _ => by simp)
          (fun _ _ => h.hom_ext _ _) fun _ _ _ e _ => by simp [â† e]âŸ©

theorem BinaryFan.isLimit_iff_isIso_snd {X Y : C} (h : IsTerminal X) (c : BinaryFan X Y) :
    Nonempty (IsLimit c) â†” IsIso c.snd := by
  refine Iff.trans ?_ (BinaryFan.isLimit_iff_isIso_fst h (BinaryFan.mk c.snd c.fst))
  exact
    âŸ¨fun h => âŸ¨BinaryFan.isLimitFlip h.someâŸ©, fun h =>
      âŸ¨(BinaryFan.isLimitFlip h.some).ofIsoLimit (isoBinaryFanMk c).symmâŸ©âŸ©

/-- If `X' â‰… X`, then `X Ã— Y` also is the product of `X'` and `Y`. -/
noncomputable def BinaryFan.isLimitCompLeftIso {X Y X' : C} (c : BinaryFan X Y) (f : X âŸ¶ X')
    [IsIso f] (h : IsLimit c) : IsLimit (BinaryFan.mk (c.fst â‰« f) c.snd) := by
  fapply BinaryFan.isLimitMk
  Â· exact fun s => h.lift (BinaryFan.mk (s.fst â‰« inv f) s.snd)
  Â· simp
  Â· simp
  Â· intro s m eâ‚ eâ‚‚
    apply BinaryFan.IsLimit.hom_ext h
    Â· simpa
    Â· simpa

/-- If `Y' â‰… Y`, then `X x Y` also is the product of `X` and `Y'`. -/
noncomputable def BinaryFan.isLimitCompRightIso {X Y Y' : C} (c : BinaryFan X Y) (f : Y âŸ¶ Y')
    [IsIso f] (h : IsLimit c) : IsLimit (BinaryFan.mk c.fst (c.snd â‰« f)) :=
  BinaryFan.isLimitFlip <| BinaryFan.isLimitCompLeftIso _ f (BinaryFan.isLimitFlip h)

/-- Binary coproducts are symmetric. -/
def BinaryCofan.isColimitFlip {X Y : C} {c : BinaryCofan X Y} (hc : IsColimit c) :
    IsColimit (BinaryCofan.mk c.inr c.inl) :=
  BinaryCofan.isColimitMk (fun s => hc.desc (BinaryCofan.mk s.inr s.inl)) (fun _ => hc.fac _ _)
    (fun _ => hc.fac _ _) fun s _ eâ‚ eâ‚‚ =>
    BinaryCofan.IsColimit.hom_ext hc
      (eâ‚‚.trans (hc.fac (BinaryCofan.mk s.inr s.inl) âŸ¨WalkingPair.leftâŸ©).symm)
      (eâ‚.trans (hc.fac (BinaryCofan.mk s.inr s.inl) âŸ¨WalkingPair.rightâŸ©).symm)

theorem BinaryCofan.isColimit_iff_isIso_inl {X Y : C} (h : IsInitial Y) (c : BinaryCofan X Y) :
    Nonempty (IsColimit c) â†” IsIso c.inl := by
  constructor
  Â· rintro âŸ¨HâŸ©
    obtain âŸ¨l, hl, -âŸ© := BinaryCofan.IsColimit.desc' H (ğŸ™ X) (h.to X)
    refine âŸ¨âŸ¨l, hl, BinaryCofan.IsColimit.hom_ext H (?_) (h.hom_ext _ _)âŸ©âŸ©
    rw [Category.comp_id]
    have e : (inl c â‰« l) â‰« inl c = ğŸ™ X â‰« inl c := congrArg (Â· â‰« inl c) hl
    rwa [Category.assoc, Category.id_comp] at e
  Â· intro
    exact
      âŸ¨BinaryCofan.IsColimit.mk _ (fun f _ => inv c.inl â‰« f)
          (fun _ _ => IsIso.hom_inv_id_assoc _ _) (fun _ _ => h.hom_ext _ _) fun _ _ _ e _ =>
          (IsIso.eq_inv_comp _).mpr eâŸ©

theorem BinaryCofan.isColimit_iff_isIso_inr {X Y : C} (h : IsInitial X) (c : BinaryCofan X Y) :
    Nonempty (IsColimit c) â†” IsIso c.inr := by
  refine Iff.trans ?_ (BinaryCofan.isColimit_iff_isIso_inl h (BinaryCofan.mk c.inr c.inl))
  exact
    âŸ¨fun h => âŸ¨BinaryCofan.isColimitFlip h.someâŸ©, fun h =>
      âŸ¨(BinaryCofan.isColimitFlip h.some).ofIsoColimit (isoBinaryCofanMk c).symmâŸ©âŸ©

/-- If `X' â‰… X`, then `X â¨¿ Y` also is the coproduct of `X'` and `Y`. -/
noncomputable def BinaryCofan.isColimitCompLeftIso {X Y X' : C} (c : BinaryCofan X Y) (f : X' âŸ¶ X)
    [IsIso f] (h : IsColimit c) : IsColimit (BinaryCofan.mk (f â‰« c.inl) c.inr) := by
  fapply BinaryCofan.isColimitMk
  Â· exact fun s => h.desc (BinaryCofan.mk (inv f â‰« s.inl) s.inr)
  Â· simp
  Â· simp
  Â· intro s m eâ‚ eâ‚‚
    apply BinaryCofan.IsColimit.hom_ext h
    Â· rw [â† cancel_epi f]
      simpa using eâ‚
    Â· simpa

/-- If `Y' â‰… Y`, then `X â¨¿ Y` also is the coproduct of `X` and `Y'`. -/
noncomputable def BinaryCofan.isColimitCompRightIso {X Y Y' : C} (c : BinaryCofan X Y) (f : Y' âŸ¶ Y)
    [IsIso f] (h : IsColimit c) : IsColimit (BinaryCofan.mk c.inl (f â‰« c.inr)) :=
  BinaryCofan.isColimitFlip <| BinaryCofan.isColimitCompLeftIso _ f (BinaryCofan.isColimitFlip h)

/-- An abbreviation for `HasLimit (pair X Y)`. -/
abbrev HasBinaryProduct (X Y : C) :=
  HasLimit (pair X Y)

/-- An abbreviation for `HasColimit (pair X Y)`. -/
abbrev HasBinaryCoproduct (X Y : C) :=
  HasColimit (pair X Y)

/-- If we have a product of `X` and `Y`, we can access it using `prod X Y` or `X â¨¯ Y`. -/
noncomputable abbrev prod (X Y : C) [HasBinaryProduct X Y] :=
  limit (pair X Y)

/-- If we have a coproduct of `X` and `Y`, we can access it using `coprod X Y` or `X â¨¿ Y`. -/
noncomputable abbrev coprod (X Y : C) [HasBinaryCoproduct X Y] :=
  colimit (pair X Y)

/-- Notation for the product -/
notation:20 X " â¨¯ " Y:20 => prod X Y

/-- Notation for the coproduct -/
notation:20 X " â¨¿ " Y:20 => coprod X Y

/-- The projection map to the first component of the product. -/
noncomputable abbrev prod.fst {X Y : C} [HasBinaryProduct X Y] : X â¨¯ Y âŸ¶ X :=
  limit.Ï€ (pair X Y) âŸ¨WalkingPair.leftâŸ©

/-- The projection map to the second component of the product. -/
noncomputable abbrev prod.snd {X Y : C} [HasBinaryProduct X Y] : X â¨¯ Y âŸ¶ Y :=
  limit.Ï€ (pair X Y) âŸ¨WalkingPair.rightâŸ©

/-- The inclusion map from the first component of the coproduct. -/
noncomputable abbrev coprod.inl {X Y : C} [HasBinaryCoproduct X Y] : X âŸ¶ X â¨¿ Y :=
  colimit.Î¹ (pair X Y) âŸ¨WalkingPair.leftâŸ©

/-- The inclusion map from the second component of the coproduct. -/
noncomputable abbrev coprod.inr {X Y : C} [HasBinaryCoproduct X Y] : Y âŸ¶ X â¨¿ Y :=
  colimit.Î¹ (pair X Y) âŸ¨WalkingPair.rightâŸ©

/-- The binary fan constructed from the projection maps is a limit. -/
noncomputable def prodIsProd (X Y : C) [HasBinaryProduct X Y] :
    IsLimit (BinaryFan.mk (prod.fst : X â¨¯ Y âŸ¶ X) prod.snd) :=
  (limit.isLimit _).ofIsoLimit (Cones.ext (Iso.refl _) (fun âŸ¨uâŸ© => by
    cases u
    Â· simp [Category.id_comp]
    Â· simp [Category.id_comp]
  ))

/-- The binary cofan constructed from the coprojection maps is a colimit. -/
noncomputable def coprodIsCoprod (X Y : C) [HasBinaryCoproduct X Y] :
    IsColimit (BinaryCofan.mk (coprod.inl : X âŸ¶ X â¨¿ Y) coprod.inr) :=
  (colimit.isColimit _).ofIsoColimit (Cocones.ext (Iso.refl _) (fun âŸ¨uâŸ© => by
    cases u
    Â· dsimp; simp only [comp_id]
    Â· dsimp; simp only [comp_id]
  ))

@[ext 1100]
theorem prod.hom_ext {W X Y : C} [HasBinaryProduct X Y] {f g : W âŸ¶ X â¨¯ Y}
    (hâ‚ : f â‰« prod.fst = g â‰« prod.fst) (hâ‚‚ : f â‰« prod.snd = g â‰« prod.snd) : f = g :=
  BinaryFan.IsLimit.hom_ext (limit.isLimit _) hâ‚ hâ‚‚

@[ext 1100]
theorem coprod.hom_ext {W X Y : C} [HasBinaryCoproduct X Y] {f g : X â¨¿ Y âŸ¶ W}
    (hâ‚ : coprod.inl â‰« f = coprod.inl â‰« g) (hâ‚‚ : coprod.inr â‰« f = coprod.inr â‰« g) : f = g :=
  BinaryCofan.IsColimit.hom_ext (colimit.isColimit _) hâ‚ hâ‚‚

/-- If the product of `X` and `Y` exists, then every pair of morphisms `f : W âŸ¶ X` and `g : W âŸ¶ Y`
induces a morphism `prod.lift f g : W âŸ¶ X â¨¯ Y`. -/
noncomputable abbrev prod.lift {W X Y : C} [HasBinaryProduct X Y]
    (f : W âŸ¶ X) (g : W âŸ¶ Y) : W âŸ¶ X â¨¯ Y :=
  limit.lift _ (BinaryFan.mk f g)

/-- diagonal arrow of the binary product in the category `fam I` -/
noncomputable abbrev diag (X : C) [HasBinaryProduct X X] : X âŸ¶ X â¨¯ X :=
  prod.lift (ğŸ™ _) (ğŸ™ _)

/-- If the coproduct of `X` and `Y` exists, then every pair of morphisms `f : X âŸ¶ W` and
`g : Y âŸ¶ W` induces a morphism `coprod.desc f g : X â¨¿ Y âŸ¶ W`. -/
noncomputable abbrev coprod.desc {W X Y : C} [HasBinaryCoproduct X Y]
    (f : X âŸ¶ W) (g : Y âŸ¶ W) : X â¨¿ Y âŸ¶ W :=
  colimit.desc _ (BinaryCofan.mk f g)

/-- codiagonal arrow of the binary coproduct -/
noncomputable abbrev codiag (X : C) [HasBinaryCoproduct X X] : X â¨¿ X âŸ¶ X :=
  coprod.desc (ğŸ™ _) (ğŸ™ _)

@[reassoc]
theorem prod.lift_fst {W X Y : C} [HasBinaryProduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
    prod.lift f g â‰« prod.fst = f :=
  limit.lift_Ï€ _ _

@[reassoc]
theorem prod.lift_snd {W X Y : C} [HasBinaryProduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
    prod.lift f g â‰« prod.snd = g :=
  limit.lift_Ï€ _ _

@[reassoc]
theorem coprod.inl_desc {W X Y : C} [HasBinaryCoproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
    coprod.inl â‰« coprod.desc f g = f :=
  colimit.Î¹_desc _ _

@[reassoc]
theorem coprod.inr_desc {W X Y : C} [HasBinaryCoproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
    coprod.inr â‰« coprod.desc f g = g :=
  colimit.Î¹_desc _ _

instance prod.mono_lift_of_mono_left {W X Y : C} [HasBinaryProduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono f] : Mono (prod.lift f g) :=
  mono_of_mono_fac <| prod.lift_fst _ _

instance prod.mono_lift_of_mono_right {W X Y : C} [HasBinaryProduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
    [Mono g] : Mono (prod.lift f g) :=
  mono_of_mono_fac <| prod.lift_snd _ _

instance coprod.epi_desc_of_epi_left {W X Y : C} [HasBinaryCoproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
    [Epi f] : Epi (coprod.desc f g) :=
  epi_of_epi_fac <| coprod.inl_desc _ _

instance coprod.epi_desc_of_epi_right {W X Y : C} [HasBinaryCoproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
    [Epi g] : Epi (coprod.desc f g) :=
  epi_of_epi_fac <| coprod.inr_desc _ _

/-- If the product of `X` and `Y` exists, then every pair of morphisms `f : W âŸ¶ X` and `g : W âŸ¶ Y`
induces a morphism `l : W âŸ¶ X â¨¯ Y` satisfying `l â‰« Prod.fst = f` and `l â‰« Prod.snd = g`. -/
noncomputable def prod.lift' {W X Y : C} [HasBinaryProduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
    { l : W âŸ¶ X â¨¯ Y // l â‰« prod.fst = f âˆ§ l â‰« prod.snd = g } :=
  âŸ¨prod.lift f g, prod.lift_fst _ _, prod.lift_snd _ _âŸ©

/-- If the coproduct of `X` and `Y` exists, then every pair of morphisms `f : X âŸ¶ W` and
`g : Y âŸ¶ W` induces a morphism `l : X â¨¿ Y âŸ¶ W` satisfying `coprod.inl â‰« l = f` and
`coprod.inr â‰« l = g`. -/
noncomputable def coprod.desc' {W X Y : C} [HasBinaryCoproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
    { l : X â¨¿ Y âŸ¶ W // coprod.inl â‰« l = f âˆ§ coprod.inr â‰« l = g } :=
  âŸ¨coprod.desc f g, coprod.inl_desc _ _, coprod.inr_desc _ _âŸ©

/-- If the products `W â¨¯ X` and `Y â¨¯ Z` exist, then every pair of morphisms `f : W âŸ¶ Y` and
`g : X âŸ¶ Z` induces a morphism `prod.map f g : W â¨¯ X âŸ¶ Y â¨¯ Z`. -/
noncomputable def prod.map {W X Y Z : C} [HasBinaryProduct W X] [HasBinaryProduct Y Z]
    (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W â¨¯ X âŸ¶ Y â¨¯ Z :=
  limMap (mapPair f g)

/-- If the coproducts `W â¨¿ X` and `Y â¨¿ Z` exist, then every pair of morphisms `f : W âŸ¶ Y` and
`g : W âŸ¶ Z` induces a morphism `coprod.map f g : W â¨¿ X âŸ¶ Y â¨¿ Z`. -/
noncomputable def coprod.map {W X Y Z : C} [HasBinaryCoproduct W X] [HasBinaryCoproduct Y Z]
    (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W â¨¿ X âŸ¶ Y â¨¿ Z :=
  colimMap (mapPair f g)

noncomputable section ProdLemmas

-- Making the reassoc version of this a simp lemma seems to be more harmful than helpful.
@[reassoc, simp]
theorem prod.comp_lift {V W X Y : C} [HasBinaryProduct X Y] (f : V âŸ¶ W) (g : W âŸ¶ X) (h : W âŸ¶ Y) :
    f â‰« prod.lift g h = prod.lift (f â‰« g) (f â‰« h) := by ext <;> simp

theorem prod.comp_diag {X Y : C} [HasBinaryProduct Y Y] (f : X âŸ¶ Y) :
    f â‰« diag Y = lift f f := by simp

@[reassoc (attr := simp)]
theorem prod.map_fst {W X Y Z : C} [HasBinaryProduct W X] [HasBinaryProduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : prod.map f g â‰« prod.fst = prod.fst â‰« f :=
  limMap_Ï€ _ _

@[reassoc (attr := simp)]
theorem prod.map_snd {W X Y Z : C} [HasBinaryProduct W X] [HasBinaryProduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : prod.map f g â‰« prod.snd = prod.snd â‰« g :=
  limMap_Ï€ _ _

@[simp]
theorem prod.map_id_id {X Y : C} [HasBinaryProduct X Y] : prod.map (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ := by
  ext <;> simp

@[simp]
theorem prod.lift_fst_snd {X Y : C} [HasBinaryProduct X Y] :
    prod.lift prod.fst prod.snd = ğŸ™ (X â¨¯ Y) := by ext <;> simp

@[simp]
lemma prod.lift_comp_fst_snd {X Y Z : C} [HasBinaryProduct Y Z] (f : X âŸ¶ Y â¨¯ Z) :
    prod.lift (f â‰« prod.fst) (f â‰« prod.snd) = f := by ext <;> simp

@[reassoc (attr := simp)]
theorem prod.lift_map {V W X Y Z : C} [HasBinaryProduct W X] [HasBinaryProduct Y Z] (f : V âŸ¶ W)
    (g : V âŸ¶ X) (h : W âŸ¶ Y) (k : X âŸ¶ Z) :
    prod.lift f g â‰« prod.map h k = prod.lift (f â‰« h) (g â‰« k) := by ext <;> simp

@[simp]
theorem prod.lift_fst_comp_snd_comp {W X Y Z : C} [HasBinaryProduct W Y] [HasBinaryProduct X Z]
    (g : W âŸ¶ X) (g' : Y âŸ¶ Z) : prod.lift (prod.fst â‰« g) (prod.snd â‰« g') = prod.map g g' := by
  rw [â† prod.lift_map]
  simp

-- We take the right-hand side here to be simp normal form, as this way composition lemmas for
-- `f â‰« h` and `g â‰« k` can fire (e.g. `id_comp`), while `map_fst` and `map_snd` can still work just
-- as well.
@[reassoc (attr := simp)]
theorem prod.map_map {Aâ‚ Aâ‚‚ Aâ‚ƒ Bâ‚ Bâ‚‚ Bâ‚ƒ : C} [HasBinaryProduct Aâ‚ Bâ‚] [HasBinaryProduct Aâ‚‚ Bâ‚‚]
    [HasBinaryProduct Aâ‚ƒ Bâ‚ƒ] (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Bâ‚ âŸ¶ Bâ‚‚) (h : Aâ‚‚ âŸ¶ Aâ‚ƒ) (k : Bâ‚‚ âŸ¶ Bâ‚ƒ) :
    prod.map f g â‰« prod.map h k = prod.map (f â‰« h) (g â‰« k) := by ext <;> simp

-- TODO: is it necessary to weaken the assumption here?
@[reassoc]
theorem prod.map_swap {A B X Y : C} (f : A âŸ¶ B) (g : X âŸ¶ Y)
    [HasLimitsOfShape (Discrete WalkingPair) C] :
    prod.map (ğŸ™ X) f â‰« prod.map g (ğŸ™ B) = prod.map g (ğŸ™ A) â‰« prod.map (ğŸ™ Y) f := by simp

@[reassoc]
theorem prod.map_comp_id {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasBinaryProduct X W]
    [HasBinaryProduct Z W] [HasBinaryProduct Y W] :
    prod.map (f â‰« g) (ğŸ™ W) = prod.map f (ğŸ™ W) â‰« prod.map g (ğŸ™ W) := by simp

@[reassoc]
theorem prod.map_id_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasBinaryProduct W X]
    [HasBinaryProduct W Y] [HasBinaryProduct W Z] :
    prod.map (ğŸ™ W) (f â‰« g) = prod.map (ğŸ™ W) f â‰« prod.map (ğŸ™ W) g := by simp

/-- If the products `W â¨¯ X` and `Y â¨¯ Z` exist, then every pair of isomorphisms `f : W â‰… Y` and
`g : X â‰… Z` induces an isomorphism `prod.mapIso f g : W â¨¯ X â‰… Y â¨¯ Z`. -/
@[simps]
def prod.mapIso {W X Y Z : C} [HasBinaryProduct W X] [HasBinaryProduct Y Z] (f : W â‰… Y)
    (g : X â‰… Z) : W â¨¯ X â‰… Y â¨¯ Z where
  hom := prod.map f.hom g.hom
  inv := prod.map f.inv g.inv

instance isIso_prod {W X Y Z : C} [HasBinaryProduct W X] [HasBinaryProduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) [IsIso f] [IsIso g] : IsIso (prod.map f g) :=
  (prod.mapIso (asIso f) (asIso g)).isIso_hom

instance prod.map_mono {C : Type*} [Category C] {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) [Mono f]
    [Mono g] [HasBinaryProduct W X] [HasBinaryProduct Y Z] : Mono (prod.map f g) :=
  âŸ¨fun iâ‚ iâ‚‚ h => by
    ext
    Â· rw [â† cancel_mono f]
      simpa using congr_arg (fun f => f â‰« prod.fst) h
    Â· rw [â† cancel_mono g]
      simpa using congr_arg (fun f => f â‰« prod.snd) hâŸ©

@[reassoc]
theorem prod.diag_map {X Y : C} (f : X âŸ¶ Y) [HasBinaryProduct X X] [HasBinaryProduct Y Y] :
    diag X â‰« prod.map f f = f â‰« diag Y := by simp

@[reassoc]
theorem prod.diag_map_fst_snd {X Y : C} [HasBinaryProduct X Y] [HasBinaryProduct (X â¨¯ Y) (X â¨¯ Y)] :
    diag (X â¨¯ Y) â‰« prod.map prod.fst prod.snd = ğŸ™ (X â¨¯ Y) := by simp

@[reassoc]
theorem prod.diag_map_fst_snd_comp [HasLimitsOfShape (Discrete WalkingPair) C] {X X' Y Y' : C}
    (g : X âŸ¶ Y) (g' : X' âŸ¶ Y') :
    diag (X â¨¯ X') â‰« prod.map (prod.fst â‰« g) (prod.snd â‰« g') = prod.map g g' := by simp

instance {X : C} [HasBinaryProduct X X] : IsSplitMono (diag X) :=
  IsSplitMono.mk' { retraction := prod.fst }

end ProdLemmas

noncomputable section CoprodLemmas

@[reassoc, simp]
theorem coprod.desc_comp {V W X Y : C} [HasBinaryCoproduct X Y] (f : V âŸ¶ W) (g : X âŸ¶ V)
    (h : Y âŸ¶ V) : coprod.desc g h â‰« f = coprod.desc (g â‰« f) (h â‰« f) := by
  ext <;> simp

theorem coprod.diag_comp {X Y : C} [HasBinaryCoproduct X X] (f : X âŸ¶ Y) :
    codiag X â‰« f = coprod.desc f f := by simp

@[reassoc (attr := simp)]
theorem coprod.inl_map {W X Y Z : C} [HasBinaryCoproduct W X] [HasBinaryCoproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : coprod.inl â‰« coprod.map f g = f â‰« coprod.inl :=
  Î¹_colimMap _ _

@[reassoc (attr := simp)]
theorem coprod.inr_map {W X Y Z : C} [HasBinaryCoproduct W X] [HasBinaryCoproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) : coprod.inr â‰« coprod.map f g = g â‰« coprod.inr :=
  Î¹_colimMap _ _

@[simp]
theorem coprod.map_id_id {X Y : C} [HasBinaryCoproduct X Y] : coprod.map (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ := by
  ext <;> simp

@[simp]
theorem coprod.desc_inl_inr {X Y : C} [HasBinaryCoproduct X Y] :
    coprod.desc coprod.inl coprod.inr = ğŸ™ (X â¨¿ Y) := by ext <;> simp

@[simp]
lemma coprod.desc_comp_inl_inr {X Y Z : C} [HasBinaryCoproduct X Y] (f : X â¨¿ Y âŸ¶ Z) :
    coprod.desc (coprod.inl â‰« f) (coprod.inr â‰« f) = f := by ext <;> simp

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
theorem coprod.map_desc {S T U V W : C} [HasBinaryCoproduct U W] [HasBinaryCoproduct T V]
    (f : U âŸ¶ S) (g : W âŸ¶ S) (h : T âŸ¶ U) (k : V âŸ¶ W) :
    coprod.map h k â‰« coprod.desc f g = coprod.desc (h â‰« f) (k â‰« g) := by
  ext <;> simp

@[simp]
theorem coprod.desc_comp_inl_comp_inr {W X Y Z : C} [HasBinaryCoproduct W Y]
    [HasBinaryCoproduct X Z] (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
    coprod.desc (g â‰« coprod.inl) (g' â‰« coprod.inr) = coprod.map g g' := by
  rw [â† coprod.map_desc]; simp

-- We take the right-hand side here to be simp normal form, as this way composition lemmas for
-- `f â‰« h` and `g â‰« k` can fire (e.g. `id_comp`), while `inl_map` and `inr_map` can still work just
-- as well.
@[reassoc (attr := simp)]
theorem coprod.map_map {Aâ‚ Aâ‚‚ Aâ‚ƒ Bâ‚ Bâ‚‚ Bâ‚ƒ : C} [HasBinaryCoproduct Aâ‚ Bâ‚] [HasBinaryCoproduct Aâ‚‚ Bâ‚‚]
    [HasBinaryCoproduct Aâ‚ƒ Bâ‚ƒ] (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Bâ‚ âŸ¶ Bâ‚‚) (h : Aâ‚‚ âŸ¶ Aâ‚ƒ) (k : Bâ‚‚ âŸ¶ Bâ‚ƒ) :
    coprod.map f g â‰« coprod.map h k = coprod.map (f â‰« h) (g â‰« k) := by
  ext <;> simp

-- I don't think it's a good idea to make any of the following three simp lemmas.
@[reassoc]
theorem coprod.map_swap {A B X Y : C} (f : A âŸ¶ B) (g : X âŸ¶ Y)
    [HasColimitsOfShape (Discrete WalkingPair) C] :
    coprod.map (ğŸ™ X) f â‰« coprod.map g (ğŸ™ B) = coprod.map g (ğŸ™ A) â‰« coprod.map (ğŸ™ Y) f := by simp

@[reassoc]
theorem coprod.map_comp_id {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasBinaryCoproduct Z W]
    [HasBinaryCoproduct Y W] [HasBinaryCoproduct X W] :
    coprod.map (f â‰« g) (ğŸ™ W) = coprod.map f (ğŸ™ W) â‰« coprod.map g (ğŸ™ W) := by simp

@[reassoc]
theorem coprod.map_id_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [HasBinaryCoproduct W X]
    [HasBinaryCoproduct W Y] [HasBinaryCoproduct W Z] :
    coprod.map (ğŸ™ W) (f â‰« g) = coprod.map (ğŸ™ W) f â‰« coprod.map (ğŸ™ W) g := by simp

/-- If the coproducts `W â¨¿ X` and `Y â¨¿ Z` exist, then every pair of isomorphisms `f : W â‰… Y` and
`g : W â‰… Z` induces an isomorphism `coprod.mapIso f g : W â¨¿ X â‰… Y â¨¿ Z`. -/
@[simps]
def coprod.mapIso {W X Y Z : C} [HasBinaryCoproduct W X] [HasBinaryCoproduct Y Z] (f : W â‰… Y)
    (g : X â‰… Z) : W â¨¿ X â‰… Y â¨¿ Z where
  hom := coprod.map f.hom g.hom
  inv := coprod.map f.inv g.inv

instance isIso_coprod {W X Y Z : C} [HasBinaryCoproduct W X] [HasBinaryCoproduct Y Z] (f : W âŸ¶ Y)
    (g : X âŸ¶ Z) [IsIso f] [IsIso g] : IsIso (coprod.map f g) :=
  (coprod.mapIso (asIso f) (asIso g)).isIso_hom

instance coprod.map_epi {C : Type*} [Category C] {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) [Epi f]
    [Epi g] [HasBinaryCoproduct W X] [HasBinaryCoproduct Y Z] : Epi (coprod.map f g) :=
  âŸ¨fun iâ‚ iâ‚‚ h => by
    ext
    Â· rw [â† cancel_epi f]
      simpa using congr_arg (fun f => coprod.inl â‰« f) h
    Â· rw [â† cancel_epi g]
      simpa using congr_arg (fun f => coprod.inr â‰« f) hâŸ©

@[reassoc]
theorem coprod.map_codiag {X Y : C} (f : X âŸ¶ Y) [HasBinaryCoproduct X X] [HasBinaryCoproduct Y Y] :
    coprod.map f f â‰« codiag Y = codiag X â‰« f := by simp

@[reassoc]
theorem coprod.map_inl_inr_codiag {X Y : C} [HasBinaryCoproduct X Y]
    [HasBinaryCoproduct (X â¨¿ Y) (X â¨¿ Y)] :
    coprod.map coprod.inl coprod.inr â‰« codiag (X â¨¿ Y) = ğŸ™ (X â¨¿ Y) := by simp

@[reassoc]
theorem coprod.map_comp_inl_inr_codiag [HasColimitsOfShape (Discrete WalkingPair) C] {X X' Y Y' : C}
    (g : X âŸ¶ Y) (g' : X' âŸ¶ Y') :
    coprod.map (g â‰« coprod.inl) (g' â‰« coprod.inr) â‰« codiag (Y â¨¿ Y') = coprod.map g g' := by simp

end CoprodLemmas

variable (C)

/-- A category `HasBinaryProducts` if it has all limits of shape `Discrete WalkingPair`,
i.e. if it has a product for every pair of objects. -/
@[stacks 001T]
abbrev HasBinaryProducts :=
  HasLimitsOfShape (Discrete WalkingPair) C

/-- A category `HasBinaryCoproducts` if it has all colimit of shape `Discrete WalkingPair`,
i.e. if it has a coproduct for every pair of objects. -/
@[stacks 04AP]
abbrev HasBinaryCoproducts :=
  HasColimitsOfShape (Discrete WalkingPair) C

/-- If `C` has all limits of diagrams `pair X Y`, then it has all binary products -/
theorem hasBinaryProducts_of_hasLimit_pair [âˆ€ {X Y : C}, HasLimit (pair X Y)] :
    HasBinaryProducts C :=
  { has_limit := fun F => hasLimit_of_iso (diagramIsoPair F).symm }

/-- If `C` has all colimits of diagrams `pair X Y`, then it has all binary coproducts -/
theorem hasBinaryCoproducts_of_hasColimit_pair [âˆ€ {X Y : C}, HasColimit (pair X Y)] :
    HasBinaryCoproducts C :=
  { has_colimit := fun F => hasColimit_of_iso (diagramIsoPair F) }

noncomputable section

variable {C}

/-- The braiding isomorphism which swaps a binary product. -/
@[simps]
def prod.braiding (P Q : C) [HasBinaryProduct P Q] [HasBinaryProduct Q P] : P â¨¯ Q â‰… Q â¨¯ P where
  hom := prod.lift prod.snd prod.fst
  inv := prod.lift prod.snd prod.fst

/-- The braiding isomorphism can be passed through a map by swapping the order. -/
@[reassoc]
theorem braid_natural [HasBinaryProducts C] {W X Y Z : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) :
    prod.map f g â‰« (prod.braiding _ _).hom = (prod.braiding _ _).hom â‰« prod.map g f := by simp

@[reassoc]
theorem prod.symmetry' (P Q : C) [HasBinaryProduct P Q] [HasBinaryProduct Q P] :
    prod.lift prod.snd prod.fst â‰« prod.lift prod.snd prod.fst = ğŸ™ (P â¨¯ Q) :=
  (prod.braiding _ _).hom_inv_id

/-- The braiding isomorphism is symmetric. -/
@[reassoc]
theorem prod.symmetry (P Q : C) [HasBinaryProduct P Q] [HasBinaryProduct Q P] :
    (prod.braiding P Q).hom â‰« (prod.braiding Q P).hom = ğŸ™ _ :=
  (prod.braiding _ _).hom_inv_id

/-- The associator isomorphism for binary products. -/
@[simps]
def prod.associator [HasBinaryProducts C] (P Q R : C) : (P â¨¯ Q) â¨¯ R â‰… P â¨¯ Q â¨¯ R where
  hom := prod.lift (prod.fst â‰« prod.fst) (prod.lift (prod.fst â‰« prod.snd) prod.snd)
  inv := prod.lift (prod.lift prod.fst (prod.snd â‰« prod.fst)) (prod.snd â‰« prod.snd)

@[reassoc]
theorem prod.pentagon [HasBinaryProducts C] (W X Y Z : C) :
    prod.map (prod.associator W X Y).hom (ğŸ™ Z) â‰«
        (prod.associator W (X â¨¯ Y) Z).hom â‰« prod.map (ğŸ™ W) (prod.associator X Y Z).hom =
      (prod.associator (W â¨¯ X) Y Z).hom â‰« (prod.associator W X (Y â¨¯ Z)).hom := by
  simp

@[reassoc]
theorem prod.associator_naturality [HasBinaryProducts C] {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚)
    (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    prod.map (prod.map fâ‚ fâ‚‚) fâ‚ƒ â‰« (prod.associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
      (prod.associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« prod.map fâ‚ (prod.map fâ‚‚ fâ‚ƒ) := by
  simp

variable [HasTerminal C]

/-- The left unitor isomorphism for binary products with the terminal object. -/
@[simps]
def prod.leftUnitor (P : C) [HasBinaryProduct (âŠ¤_ C) P] : (âŠ¤_ C) â¨¯ P â‰… P where
  hom := prod.snd
  inv := prod.lift (terminal.from P) (ğŸ™ _)
  hom_inv_id := by apply prod.hom_ext <;> simp [eq_iff_true_of_subsingleton]
  inv_hom_id := by simp

/-- The right unitor isomorphism for binary products with the terminal object. -/
@[simps]
def prod.rightUnitor (P : C) [HasBinaryProduct P (âŠ¤_ C)] : P â¨¯ âŠ¤_ C â‰… P where
  hom := prod.fst
  inv := prod.lift (ğŸ™ _) (terminal.from P)
  hom_inv_id := by apply prod.hom_ext <;> simp [eq_iff_true_of_subsingleton]
  inv_hom_id := by simp

@[reassoc]
theorem prod.leftUnitor_hom_naturality [HasBinaryProducts C] (f : X âŸ¶ Y) :
    prod.map (ğŸ™ _) f â‰« (prod.leftUnitor Y).hom = (prod.leftUnitor X).hom â‰« f :=
  prod.map_snd _ _

@[reassoc]
theorem prod.leftUnitor_inv_naturality [HasBinaryProducts C] (f : X âŸ¶ Y) :
    (prod.leftUnitor X).inv â‰« prod.map (ğŸ™ _) f = f â‰« (prod.leftUnitor Y).inv := by
  rw [Iso.inv_comp_eq, â† assoc, Iso.eq_comp_inv, prod.leftUnitor_hom_naturality]

@[reassoc]
theorem prod.rightUnitor_hom_naturality [HasBinaryProducts C] (f : X âŸ¶ Y) :
    prod.map f (ğŸ™ _) â‰« (prod.rightUnitor Y).hom = (prod.rightUnitor X).hom â‰« f :=
  prod.map_fst _ _

@[reassoc]
theorem prod_rightUnitor_inv_naturality [HasBinaryProducts C] (f : X âŸ¶ Y) :
    (prod.rightUnitor X).inv â‰« prod.map f (ğŸ™ _) = f â‰« (prod.rightUnitor Y).inv := by
  rw [Iso.inv_comp_eq, â† assoc, Iso.eq_comp_inv, prod.rightUnitor_hom_naturality]

theorem prod.triangle [HasBinaryProducts C] (X Y : C) :
    (prod.associator X (âŠ¤_ C) Y).hom â‰« prod.map (ğŸ™ X) (prod.leftUnitor Y).hom =
      prod.map (prod.rightUnitor X).hom (ğŸ™ Y) := by
  ext <;> simp

end

noncomputable section

variable {C}
variable [HasBinaryCoproducts C]

/-- The braiding isomorphism which swaps a binary coproduct. -/
@[simps]
def coprod.braiding (P Q : C) : P â¨¿ Q â‰… Q â¨¿ P where
  hom := coprod.desc coprod.inr coprod.inl
  inv := coprod.desc coprod.inr coprod.inl

@[reassoc]
theorem coprod.symmetry' (P Q : C) :
    coprod.desc coprod.inr coprod.inl â‰« coprod.desc coprod.inr coprod.inl = ğŸ™ (P â¨¿ Q) :=
  (coprod.braiding _ _).hom_inv_id

/-- The braiding isomorphism is symmetric. -/
theorem coprod.symmetry (P Q : C) : (coprod.braiding P Q).hom â‰« (coprod.braiding Q P).hom = ğŸ™ _ :=
  coprod.symmetry' _ _

/-- The associator isomorphism for binary coproducts. -/
@[simps]
def coprod.associator (P Q R : C) : (P â¨¿ Q) â¨¿ R â‰… P â¨¿ Q â¨¿ R where
  hom := coprod.desc (coprod.desc coprod.inl (coprod.inl â‰« coprod.inr)) (coprod.inr â‰« coprod.inr)
  inv := coprod.desc (coprod.inl â‰« coprod.inl) (coprod.desc (coprod.inr â‰« coprod.inl) coprod.inr)

theorem coprod.pentagon (W X Y Z : C) :
    coprod.map (coprod.associator W X Y).hom (ğŸ™ Z) â‰«
        (coprod.associator W (X â¨¿ Y) Z).hom â‰« coprod.map (ğŸ™ W) (coprod.associator X Y Z).hom =
      (coprod.associator (W â¨¿ X) Y Z).hom â‰« (coprod.associator W X (Y â¨¿ Z)).hom := by
  simp

theorem coprod.associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
    (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    coprod.map (coprod.map fâ‚ fâ‚‚) fâ‚ƒ â‰« (coprod.associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
      (coprod.associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« coprod.map fâ‚ (coprod.map fâ‚‚ fâ‚ƒ) := by
  simp

variable [HasInitial C]

/-- The left unitor isomorphism for binary coproducts with the initial object. -/
@[simps]
def coprod.leftUnitor (P : C) : (âŠ¥_ C) â¨¿ P â‰… P where
  hom := coprod.desc (initial.to P) (ğŸ™ _)
  inv := coprod.inr
  hom_inv_id := by apply coprod.hom_ext <;> simp [eq_iff_true_of_subsingleton]
  inv_hom_id := by simp

/-- The right unitor isomorphism for binary coproducts with the initial object. -/
@[simps]
def coprod.rightUnitor (P : C) : P â¨¿ âŠ¥_ C â‰… P where
  hom := coprod.desc (ğŸ™ _) (initial.to P)
  inv := coprod.inl
  hom_inv_id := by apply coprod.hom_ext <;> simp [eq_iff_true_of_subsingleton]
  inv_hom_id := by simp

theorem coprod.triangle (X Y : C) :
    (coprod.associator X (âŠ¥_ C) Y).hom â‰« coprod.map (ğŸ™ X) (coprod.leftUnitor Y).hom =
      coprod.map (coprod.rightUnitor X).hom (ğŸ™ Y) := by
  ext <;> simp

end

noncomputable section ProdFunctor

variable {C} [Category.{v} C] [HasBinaryProducts C]

/-- The binary product functor. -/
@[simps]
def prod.functor : C â¥¤ C â¥¤ C where
  obj X :=
    { obj := fun Y => X â¨¯ Y
      map := fun {_ _} => prod.map (ğŸ™ X) }
  map f :=
    { app := fun T => prod.map f (ğŸ™ T) }

/-- The product functor can be decomposed. -/
def prod.functorLeftComp (X Y : C) :
    prod.functor.obj (X â¨¯ Y) â‰… prod.functor.obj Y â‹™ prod.functor.obj X :=
  NatIso.ofComponents (prod.associator _ _)

end ProdFunctor

noncomputable section CoprodFunctor

variable {C} [HasBinaryCoproducts C]

/-- The binary coproduct functor. -/
@[simps]
def coprod.functor : C â¥¤ C â¥¤ C where
  obj X :=
    { obj := fun Y => X â¨¿ Y
      map := fun {_ _} => coprod.map (ğŸ™ X) }
  map f := { app := fun T => coprod.map f (ğŸ™ T) }

/-- The coproduct functor can be decomposed. -/
def coprod.functorLeftComp (X Y : C) :
    coprod.functor.obj (X â¨¿ Y) â‰… coprod.functor.obj Y â‹™ coprod.functor.obj X :=
  NatIso.ofComponents (coprod.associator _ _)

end CoprodFunctor

noncomputable section ProdComparison

universe w w' uâ‚ƒ

variable {C} {D : Type uâ‚‚} [Category.{w} D] {E : Type uâ‚ƒ} [Category.{w'} E]
variable (F : C â¥¤ D) (G : D â¥¤ E) {A A' B B' : C}
variable [HasBinaryProduct A B] [HasBinaryProduct A' B']
variable [HasBinaryProduct (F.obj A) (F.obj B)]
variable [HasBinaryProduct (F.obj A') (F.obj B')]
variable [HasBinaryProduct (G.obj (F.obj A)) (G.obj (F.obj B))]
variable [HasBinaryProduct ((F â‹™ G).obj A) ((F â‹™ G).obj B)]

/-- The product comparison morphism.

In `CategoryTheory/Limits/Preserves` we show this is always an iso iff F preserves binary products.
-/
def prodComparison (F : C â¥¤ D) (A B : C) [HasBinaryProduct A B]
    [HasBinaryProduct (F.obj A) (F.obj B)] : F.obj (A â¨¯ B) âŸ¶ F.obj A â¨¯ F.obj B :=
  prod.lift (F.map prod.fst) (F.map prod.snd)

variable (A B)

@[reassoc (attr := simp)]
theorem prodComparison_fst : prodComparison F A B â‰« prod.fst = F.map prod.fst :=
  prod.lift_fst _ _

@[reassoc (attr := simp)]
theorem prodComparison_snd : prodComparison F A B â‰« prod.snd = F.map prod.snd :=
  prod.lift_snd _ _

variable {A B}

/-- Naturality of the `prodComparison` morphism in both arguments. -/
@[reassoc]
theorem prodComparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
    F.map (prod.map f g) â‰« prodComparison F A' B' =
      prodComparison F A B â‰« prod.map (F.map f) (F.map g) := by
  rw [prodComparison, prodComparison, prod.lift_map, â† F.map_comp, â† F.map_comp, prod.comp_lift, â†
    F.map_comp, prod.map_fst, â† F.map_comp, prod.map_snd]

/-- The product comparison morphism from `F(A â¨¯ -)` to `FA â¨¯ F-`, whose components are given by
`prodComparison`.
-/
@[simps]
def prodComparisonNatTrans [HasBinaryProducts C] [HasBinaryProducts D] (F : C â¥¤ D) (A : C) :
    prod.functor.obj A â‹™ F âŸ¶ F â‹™ prod.functor.obj (F.obj A) where
  app B := prodComparison F A B
  naturality f := by simp [prodComparison_natural]

@[reassoc]
theorem inv_prodComparison_map_fst [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) â‰« F.map prod.fst = prod.fst := by simp [IsIso.inv_comp_eq]

@[reassoc]
theorem inv_prodComparison_map_snd [IsIso (prodComparison F A B)] :
    inv (prodComparison F A B) â‰« F.map prod.snd = prod.snd := by simp [IsIso.inv_comp_eq]

/-- If the product comparison morphism is an iso, its inverse is natural. -/
@[reassoc]
theorem prodComparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B') [IsIso (prodComparison F A B)]
    [IsIso (prodComparison F A' B')] :
    inv (prodComparison F A B) â‰« F.map (prod.map f g) =
      prod.map (F.map f) (F.map g) â‰« inv (prodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, assoc, IsIso.inv_comp_eq, prodComparison_natural]

/-- The natural isomorphism `F(A â¨¯ -) â‰… FA â¨¯ F-`, provided each `prodComparison F A B` is an
isomorphism (as `B` changes).
-/
@[simps]
def prodComparisonNatIso [HasBinaryProducts C] [HasBinaryProducts D] (A : C)
    [âˆ€ B, IsIso (prodComparison F A B)] :
    prod.functor.obj A â‹™ F â‰… F â‹™ prod.functor.obj (F.obj A) := by
  refine { @asIso _ _ _ _ _ (?_) with hom := prodComparisonNatTrans F A }
  apply NatIso.isIso_of_isIso_app

theorem prodComparison_comp :
    prodComparison (F â‹™ G) A B =
      G.map (prodComparison F A B) â‰« prodComparison G (F.obj A) (F.obj B) := by
  unfold prodComparison
  ext <;> simp [â† G.map_comp]

end ProdComparison

noncomputable section CoprodComparison

universe w

variable {C} {D : Type uâ‚‚} [Category.{w} D]
variable (F : C â¥¤ D) {A A' B B' : C}
variable [HasBinaryCoproduct A B] [HasBinaryCoproduct A' B']
variable [HasBinaryCoproduct (F.obj A) (F.obj B)] [HasBinaryCoproduct (F.obj A') (F.obj B')]

/-- The coproduct comparison morphism.

In `Mathlib/CategoryTheory/Limits/Preserves/` we show
this is always an iso iff F preserves binary coproducts.
-/
def coprodComparison (F : C â¥¤ D) (A B : C) [HasBinaryCoproduct A B]
    [HasBinaryCoproduct (F.obj A) (F.obj B)] : F.obj A â¨¿ F.obj B âŸ¶ F.obj (A â¨¿ B) :=
  coprod.desc (F.map coprod.inl) (F.map coprod.inr)

@[reassoc (attr := simp)]
theorem coprodComparison_inl : coprod.inl â‰« coprodComparison F A B = F.map coprod.inl :=
  coprod.inl_desc _ _

@[reassoc (attr := simp)]
theorem coprodComparison_inr : coprod.inr â‰« coprodComparison F A B = F.map coprod.inr :=
  coprod.inr_desc _ _

/-- Naturality of the coprod_comparison morphism in both arguments. -/
@[reassoc]
theorem coprodComparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
    coprodComparison F A B â‰« F.map (coprod.map f g) =
      coprod.map (F.map f) (F.map g) â‰« coprodComparison F A' B' := by
  rw [coprodComparison, coprodComparison, coprod.map_desc, â† F.map_comp, â† F.map_comp,
    coprod.desc_comp, â† F.map_comp, coprod.inl_map, â† F.map_comp, coprod.inr_map]

/-- The coproduct comparison morphism from `FA â¨¿ F-` to `F(A â¨¿ -)`, whose components are given by
`coprodComparison`.
-/
@[simps]
def coprodComparisonNatTrans [HasBinaryCoproducts C] [HasBinaryCoproducts D] (F : C â¥¤ D) (A : C) :
    F â‹™ coprod.functor.obj (F.obj A) âŸ¶ coprod.functor.obj A â‹™ F where
  app B := coprodComparison F A B
  naturality f := by simp [coprodComparison_natural]

@[reassoc]
theorem map_inl_inv_coprodComparison [IsIso (coprodComparison F A B)] :
    F.map coprod.inl â‰« inv (coprodComparison F A B) = coprod.inl := by simp

@[reassoc]
theorem map_inr_inv_coprodComparison [IsIso (coprodComparison F A B)] :
    F.map coprod.inr â‰« inv (coprodComparison F A B) = coprod.inr := by simp

/-- If the coproduct comparison morphism is an iso, its inverse is natural. -/
@[reassoc]
theorem coprodComparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B') [IsIso (coprodComparison F A B)]
    [IsIso (coprodComparison F A' B')] :
    inv (coprodComparison F A B) â‰« coprod.map (F.map f) (F.map g) =
      F.map (coprod.map f g) â‰« inv (coprodComparison F A' B') := by
  rw [IsIso.eq_comp_inv, assoc, IsIso.inv_comp_eq, coprodComparison_natural]

/-- The natural isomorphism `FA â¨¿ F- â‰… F(A â¨¿ -)`, provided each `coprodComparison F A B` is an
isomorphism (as `B` changes).
-/
@[simps]
def coprodComparisonNatIso [HasBinaryCoproducts C] [HasBinaryCoproducts D] (A : C)
    [âˆ€ B, IsIso (coprodComparison F A B)] :
    F â‹™ coprod.functor.obj (F.obj A) â‰… coprod.functor.obj A â‹™ F :=
  { @asIso _ _ _ _ _ (NatIso.isIso_of_isIso_app ..) with hom := coprodComparisonNatTrans F A }

end CoprodComparison

end CategoryTheory.Limits

open CategoryTheory.Limits

namespace CategoryTheory

variable {C : Type u} [Category.{v} C]

/-- Auxiliary definition for `Over.coprod`. -/
@[simps]
noncomputable def Over.coprodObj [HasBinaryCoproducts C] {A : C} :
    Over A â†’ Over A â¥¤ Over A :=
  fun f =>
  { obj := fun g => Over.mk (coprod.desc f.hom g.hom)
    map := fun k => Over.homMk (coprod.map (ğŸ™ _) k.left) }

/-- A category with binary coproducts has a functorial `sup` operation on over categories. -/
@[simps]
noncomputable def Over.coprod [HasBinaryCoproducts C] {A : C} : Over A â¥¤ Over A â¥¤ Over A where
  obj f := Over.coprodObj f
  map k :=
    { app := fun g => Over.homMk (coprod.map k.left (ğŸ™ _)) (by
        dsimp; rw [coprod.map_desc, id_comp, Over.w k])
      naturality := fun f g k => by
        ext
        simp }
  map_id X := by
    ext
    simp
  map_comp f g := by
    ext
    simp

end CategoryTheory

namespace CategoryTheory.Limits
open Opposite

variable {C : Type u} [Category.{v} C] {X Y Z P : C}

section opposite

/-- A binary fan gives a binary cofan in the opposite category. -/
protected abbrev BinaryFan.op (c : BinaryFan X Y) : BinaryCofan (op X) (op Y) :=
  .mk c.fst.op c.snd.op

/-- A binary cofan gives a binary fan in the opposite category. -/
protected abbrev BinaryCofan.op (c : BinaryCofan X Y) : BinaryFan (op X) (op Y) :=
  .mk c.inl.op c.inr.op

/-- A binary fan in the opposite category gives a binary cofan. -/
protected abbrev BinaryFan.unop (c : BinaryFan (op X) (op Y)) : BinaryCofan X Y :=
  .mk c.fst.unop c.snd.unop

/-- A binary cofan in the opposite category gives a binary fan. -/
protected abbrev BinaryCofan.unop (c : BinaryCofan (op X) (op Y)) : BinaryFan X Y :=
  .mk c.inl.unop c.inr.unop

@[simp] lemma BinaryFan.op_mk (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) :
    BinaryFan.op (mk Ï€â‚ Ï€â‚‚) = .mk Ï€â‚.op Ï€â‚‚.op := rfl

@[simp] lemma BinaryFan.unop_mk (Ï€â‚ : op P âŸ¶ op X) (Ï€â‚‚ : op P âŸ¶ op Y) :
    BinaryFan.unop (mk Ï€â‚ Ï€â‚‚) = .mk Ï€â‚.unop Ï€â‚‚.unop := rfl

@[simp] lemma BinaryCofan.op_mk (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) :
    BinaryCofan.op (mk Î¹â‚ Î¹â‚‚) = .mk Î¹â‚.op Î¹â‚‚.op := rfl

@[simp] lemma BinaryCofan.unop_mk (Î¹â‚ : op X âŸ¶ op P) (Î¹â‚‚ : op Y âŸ¶ op P) :
    BinaryCofan.unop (mk Î¹â‚ Î¹â‚‚) = .mk Î¹â‚.unop Î¹â‚‚.unop := rfl

/-- If a `BinaryFan` is a limit, then its opposite is a colimit. -/
protected def BinaryFan.IsLimit.op {c : BinaryFan X Y} (hc : IsLimit c) : IsColimit c.op :=
  BinaryCofan.isColimitMk (fun s â†¦ (hc.lift s.unop).op)
    (fun _ â†¦ Quiver.Hom.unop_inj (by simp)) (fun _ â†¦ Quiver.Hom.unop_inj (by simp))
    (fun s m hâ‚ hâ‚‚ â†¦ Quiver.Hom.unop_inj
      (BinaryFan.IsLimit.hom_ext hc (by simp [â† hâ‚]) (by simp [â† hâ‚‚])))

/-- If a `BinaryCofan` is a colimit, then its opposite is a limit. -/
protected def BinaryCofan.IsColimit.op {c : BinaryCofan X Y} (hc : IsColimit c) : IsLimit c.op :=
  BinaryFan.isLimitMk (fun s â†¦ (hc.desc s.unop).op)
    (fun _ â†¦ Quiver.Hom.unop_inj (by simp)) (fun _ â†¦ Quiver.Hom.unop_inj (by simp))
    (fun s m hâ‚ hâ‚‚ â†¦ Quiver.Hom.unop_inj
      (BinaryCofan.IsColimit.hom_ext hc (by simp [â† hâ‚]) (by simp [â† hâ‚‚])))

/-- If a `BinaryFan` in the opposite category is a limit, then its `unop` is a colimit. -/
protected def BinaryFan.IsLimit.unop {c : BinaryFan (op X) (op Y)} (hc : IsLimit c) :
    IsColimit c.unop :=
  BinaryCofan.isColimitMk (fun s â†¦ (hc.lift s.op).unop)
    (fun _ â†¦ Quiver.Hom.op_inj (by simp)) (fun _ â†¦ Quiver.Hom.op_inj (by simp))
    (fun s m hâ‚ hâ‚‚ â†¦ Quiver.Hom.op_inj
      (BinaryFan.IsLimit.hom_ext hc (by simp [â† hâ‚]) (by simp [â† hâ‚‚])))

/-- If a `BinaryCofan` in the opposite category is a colimit, then its `unop` is a limit. -/
protected def BinaryCofan.IsColimit.unop {c : BinaryCofan (op X) (op Y)} (hc : IsColimit c) :
    IsLimit c.unop :=
  BinaryFan.isLimitMk (fun s â†¦ (hc.desc s.op).unop)
    (fun _ â†¦ Quiver.Hom.op_inj (by simp)) (fun _ â†¦ Quiver.Hom.op_inj (by simp))
    (fun s m hâ‚ hâ‚‚ â†¦ Quiver.Hom.op_inj
      (BinaryCofan.IsColimit.hom_ext hc (by simp [â† hâ‚]) (by simp [â† hâ‚‚])))

end opposite

section swap
variable {s : BinaryFan X Y} {t : BinaryFan Y X}

/-- Swap the two sides of a `BinaryFan`. -/
def BinaryFan.swap (s : BinaryFan X Y) : BinaryFan Y X := .mk s.snd s.fst

@[simp] lemma BinaryFan.swap_fst (s : BinaryFan X Y) : s.swap.fst = s.snd := rfl
@[simp] lemma BinaryFan.swap_snd (s : BinaryFan X Y) : s.swap.snd = s.fst := rfl

/-- If a binary fan `s` over `X Y` is a limit cone, then `s.swap` is a limit cone over `Y X`. -/
@[simps]
def IsLimit.binaryFanSwap (I : IsLimit s) : IsLimit s.swap where
  lift t := I.lift (BinaryFan.swap t)
  fac t := by rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
  uniq t m w := by
    have h := I.uniq (BinaryFan.swap t) m
    rw [h]
    rintro âŸ¨jâŸ©
    specialize w âŸ¨WalkingPair.swap jâŸ©
    cases j <;> exact w

@[deprecated (since := "2025-05-04")] alias IsLimit.swapBinaryFan := IsLimit.binaryFanSwap

/-- Construct `HasBinaryProduct Y X` from `HasBinaryProduct X Y`.
This can't be an instance, as it would cause a loop in typeclass search. -/
lemma HasBinaryProduct.swap (X Y : C) [HasBinaryProduct X Y] : HasBinaryProduct Y X :=
  .mk âŸ¨BinaryFan.swap (limit.cone (pair X Y)), (limit.isLimit (pair X Y)).binaryFanSwapâŸ©

end swap

section braiding
variable {X Y : C} {s : BinaryFan X Y} (P : IsLimit s) {t : BinaryFan Y X} (Q : IsLimit t)

/-- Given a limit cone over `X` and `Y`, and another limit cone over `Y` and `X`, we can construct
an isomorphism between the cone points. Relative to some fixed choice of limits cones for every
pair, these isomorphisms constitute a braiding. -/
def BinaryFan.braiding (P : IsLimit s) (Q : IsLimit t) : s.pt â‰… t.pt :=
  P.conePointUniqueUpToIso Q.binaryFanSwap

@[reassoc (attr := simp)]
lemma BinaryFan.braiding_hom_fst : (braiding P Q).hom â‰« t.fst = s.snd :=
  P.conePointUniqueUpToIso_hom_comp _ âŸ¨.rightâŸ©

@[reassoc (attr := simp)]
lemma BinaryFan.braiding_hom_snd : (braiding P Q).hom â‰« t.snd = s.fst :=
  P.conePointUniqueUpToIso_hom_comp _ âŸ¨.leftâŸ©

@[reassoc (attr := simp)]
lemma BinaryFan.braiding_inv_fst : (braiding P Q).inv â‰« s.fst = t.snd :=
  P.conePointUniqueUpToIso_inv_comp _ âŸ¨.leftâŸ©

@[reassoc (attr := simp)]
lemma BinaryFan.braiding_inv_snd : (braiding P Q).inv â‰« s.snd = t.fst :=
  P.conePointUniqueUpToIso_inv_comp _ âŸ¨.rightâŸ©

end braiding

section assoc
variable {sXY : BinaryFan X Y} {sYZ : BinaryFan Y Z}

/-- Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `sXY.X Z`,
if `sYZ` is a limit cone we can construct a binary fan over `X sYZ.X`.

This is an ingredient of building the associator for a Cartesian category. -/
def BinaryFan.assoc (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) : BinaryFan X sYZ.pt :=
  mk (s.fst â‰« sXY.fst) (Q.lift (mk (s.fst â‰« sXY.snd) s.snd))

@[simp]
lemma BinaryFan.assoc_fst (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) :
    (assoc Q s).fst = s.fst â‰« sXY.fst := rfl

@[simp]
lemma BinaryFan.assoc_snd (Q : IsLimit sYZ) (s : BinaryFan sXY.pt Z) :
    (assoc Q s).snd = Q.lift (mk (s.fst â‰« sXY.snd) s.snd) := rfl

/-- Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `X sYZ.X`,
if `sYZ` is a limit cone we can construct a binary fan over `sXY.X Z`.

This is an ingredient of building the associator for a Cartesian category. -/
def BinaryFan.assocInv (P : IsLimit sXY) (s : BinaryFan X sYZ.pt) : BinaryFan sXY.pt Z :=
  BinaryFan.mk (P.lift (BinaryFan.mk s.fst (s.snd â‰« sYZ.fst))) (s.snd â‰« sYZ.snd)

@[simp]
lemma BinaryFan.assocInv_fst (P : IsLimit sXY) (s : BinaryFan X sYZ.pt) :
    (assocInv P s).fst = P.lift (mk s.fst (s.snd â‰« sYZ.fst)) := rfl

@[simp]
lemma BinaryFan.assocInv_snd (P : IsLimit sXY) (s : BinaryFan X sYZ.pt) :
    (assocInv P s).snd = s.snd â‰« sYZ.snd := rfl

/-- If all the binary fans involved a limit cones, `BinaryFan.assoc` produces another limit cone. -/
@[simps]
protected def IsLimit.assoc (P : IsLimit sXY) (Q : IsLimit sYZ) {s : BinaryFan sXY.pt Z}
    (R : IsLimit s) : IsLimit (BinaryFan.assoc Q s) where
  lift t := R.lift (BinaryFan.assocInv P t)
  fac t := by rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp; apply Q.hom_ext; rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
  uniq t m w := by
    have h := R.uniq (BinaryFan.assocInv P t) m
    rw [h]
    rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
    Â· apply P.hom_ext
      rintro âŸ¨âŸ¨âŸ©âŸ© <;> simp
      Â· exact w âŸ¨.leftâŸ©
      Â· replace w : m â‰« Q.lift (BinaryFan.mk (s.fst â‰« sXY.snd) s.snd) = t.Ï€.app âŸ¨.rightâŸ© := by
          simpa using w âŸ¨.rightâŸ©
        simp [â† w]
    Â· replace w : m â‰« Q.lift (BinaryFan.mk (s.fst â‰« sXY.snd) s.snd) = t.Ï€.app âŸ¨.rightâŸ© := by
        simpa using w âŸ¨.rightâŸ©
      simp [â† w]

/-- Given two pairs of limit cones corresponding to the parenthesisations of `X Ã— Y Ã— Z`,
we obtain an isomorphism between the cone points. -/
abbrev BinaryFan.associator (P : IsLimit sXY) (Q : IsLimit sYZ) {s : BinaryFan sXY.pt Z}
    (R : IsLimit s) {t : BinaryFan X sYZ.pt} (S : IsLimit t) : s.pt â‰… t.pt :=
  (P.assoc Q R).conePointUniqueUpToIso S

/-- Given a fixed family of limit data for every pair `X Y`, we obtain an associator. -/
abbrev BinaryFan.associatorOfLimitCone (L : âˆ€ X Y : C, LimitCone (pair X Y)) (X Y Z : C) :
    (L (L X Y).cone.pt Z).cone.pt â‰… (L X (L Y Z).cone.pt).cone.pt :=
  associator (L X Y).isLimit (L Y Z).isLimit (L (L X Y).cone.pt Z).isLimit
    (L X (L Y Z).cone.pt).isLimit

end assoc

section unitor

/-- Construct a left unitor from specified limit cones. -/
@[simps]
def BinaryFan.leftUnitor {X : C} {s : Cone (Functor.empty.{0} C)} (P : IsLimit s)
    {t : BinaryFan s.pt X} (Q : IsLimit t) : t.pt â‰… X where
  hom := t.snd
  inv := Q.lift <| BinaryFan.mk (P.lift âŸ¨_, fun x => x.as.elim, fun {x} => x.as.elimâŸ©) (ğŸ™ _)
  hom_inv_id := by
    apply Q.hom_ext
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· apply P.hom_ext
      rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp

/-- Construct a right unitor from specified limit cones. -/
@[simps]
def BinaryFan.rightUnitor {X : C} {s : Cone (Functor.empty.{0} C)} (P : IsLimit s)
    {t : BinaryFan X s.pt} (Q : IsLimit t) : t.pt â‰… X where
  hom := t.fst
  inv := Q.lift <| BinaryFan.mk (ğŸ™ _) <| P.lift âŸ¨_, fun x => x.as.elim, fun {x} => x.as.elimâŸ©
  hom_inv_id := by
    apply Q.hom_ext
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp
    Â· apply P.hom_ext
      rintro âŸ¨âŸ¨âŸ©âŸ©

end unitor
end CategoryTheory.Limits
