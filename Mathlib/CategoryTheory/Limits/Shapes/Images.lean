/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Mono
import Mathlib.CategoryTheory.Limits.Shapes.StrongEpi
import Mathlib.CategoryTheory.MorphismProperty.Factorization

/-!
# Categorical images

We define the categorical image of `f` as a factorisation `f = e ‚â´ m` through a monomorphism `m`,
so that `m` factors through the `m'` in any other such factorisation.

## Main definitions

* A `MonoFactorisation` is a factorisation `f = e ‚â´ m`, where `m` is a monomorphism
* `IsImage F` means that a given mono factorisation `F` has the universal property of the image.
* `HasImage f` means that there is some image factorization for the morphism `f : X ‚ü∂ Y`.
  * In this case, `image f` is some image object (selected with choice), `image.Œπ f : image f ‚ü∂ Y`
    is the monomorphism `m` of the factorisation and `factorThruImage f : X ‚ü∂ image f` is the
    morphism `e`.
* `HasImages C` means that every morphism in `C` has an image.
* Let `f : X ‚ü∂ Y` and `g : P ‚ü∂ Q` be morphisms in `C`, which we will represent as objects of the
  arrow category `Arrow C`. Then `sq : f ‚ü∂ g` is a commutative square in `C`. If `f` and `g` have
  images, then `HasImageMap sq` represents the fact that there is a morphism
  `i : image f ‚ü∂ image g` making the diagram

  X ----‚Üí image f ----‚Üí Y
  |         |           |
  |         |           |
  ‚Üì         ‚Üì           ‚Üì
  P ----‚Üí image g ----‚Üí Q

  commute, where the top row is the image factorisation of `f`, the bottom row is the image
  factorisation of `g`, and the outer rectangle is the commutative square `sq`.
* If a category `HasImages`, then `HasImageMaps` means that every commutative square admits an
  image map.
* If a category `HasImages`, then `HasStrongEpiImages` means that the morphism to the image is
  always a strong epimorphism.

## Main statements

* When `C` has equalizers, the morphism `e` appearing in an image factorisation is an epimorphism.
* When `C` has strong epi images, then these images admit image maps.

## Future work
* TODO: coimages, and abelian categories.
* TODO: connect this with existing working in the group theory and ring theory libraries.

-/


noncomputable section

universe v u

open CategoryTheory

open CategoryTheory.Limits.WalkingParallelPair

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]
variable {X Y : C} (f : X ‚ü∂ Y)

/-- A factorisation of a morphism `f = e ‚â´ m`, with `m` monic. -/
structure MonoFactorisation (f : X ‚ü∂ Y) where
  I : C
  m : I ‚ü∂ Y
  [m_mono : Mono m]
  e : X ‚ü∂ I
  fac : e ‚â´ m = f := by cat_disch

attribute [inherit_doc MonoFactorisation] MonoFactorisation.I MonoFactorisation.m
  MonoFactorisation.m_mono MonoFactorisation.e MonoFactorisation.fac

attribute [reassoc (attr := simp)] MonoFactorisation.fac

attribute [instance] MonoFactorisation.m_mono

namespace MonoFactorisation

/-- The obvious factorisation of a monomorphism through itself. -/
def self [Mono f] : MonoFactorisation f where
  I := X
  m := f
  e := ùüô X

-- I'm not sure we really need this, but the linter says that an inhabited instance
-- ought to exist...
instance [Mono f] : Inhabited (MonoFactorisation f) := ‚ü®self f‚ü©

variable {f}

/-- The morphism `m` in a factorisation `f = e ‚â´ m` through a monomorphism is uniquely
determined. -/
@[ext (iff := false)]
theorem ext {F F' : MonoFactorisation f} (hI : F.I = F'.I)
    (hm : F.m = eqToHom hI ‚â´ F'.m) : F = F' := by
  obtain ‚ü®_, Fm, _, Ffac‚ü© := F; obtain ‚ü®_, Fm', _, Ffac'‚ü© := F'
  cases hI
  replace hm : Fm = Fm' := by simpa using hm
  congr
  apply (cancel_mono Fm).1
  rw [Ffac, hm, Ffac']

/-- Any mono factorisation of `f` gives a mono factorisation of `f ‚â´ g` when `g` is a mono. -/
@[simps]
def compMono (F : MonoFactorisation f) {Y' : C} (g : Y ‚ü∂ Y') [Mono g] :
    MonoFactorisation (f ‚â´ g) where
  I := F.I
  m := F.m ‚â´ g
  m_mono := mono_comp _ _
  e := F.e

/-- A mono factorisation of `f ‚â´ g`, where `g` is an isomorphism,
gives a mono factorisation of `f`. -/
@[simps]
def ofCompIso {Y' : C} {g : Y ‚ü∂ Y'} [IsIso g] (F : MonoFactorisation (f ‚â´ g)) :
    MonoFactorisation f where
  I := F.I
  m := F.m ‚â´ inv g
  m_mono := mono_comp _ _
  e := F.e

/-- Any mono factorisation of `f` gives a mono factorisation of `g ‚â´ f`. -/
@[simps]
def isoComp (F : MonoFactorisation f) {X' : C} (g : X' ‚ü∂ X) : MonoFactorisation (g ‚â´ f) where
  I := F.I
  m := F.m
  e := g ‚â´ F.e

/-- A mono factorisation of `g ‚â´ f`, where `g` is an isomorphism,
gives a mono factorisation of `f`. -/
@[simps]
def ofIsoComp {X' : C} (g : X' ‚ü∂ X) [IsIso g] (F : MonoFactorisation (g ‚â´ f)) :
    MonoFactorisation f where
  I := F.I
  m := F.m
  e := inv g ‚â´ F.e

/-- If `f` and `g` are isomorphic arrows, then a mono factorisation of `f`
gives a mono factorisation of `g` -/
@[simps]
def ofArrowIso {f g : Arrow C} (F : MonoFactorisation f.hom) (sq : f ‚ü∂ g) [IsIso sq] :
    MonoFactorisation g.hom where
  I := F.I
  m := F.m ‚â´ sq.right
  e := inv sq.left ‚â´ F.e
  m_mono := mono_comp _ _
  fac := by simp only [fac_assoc, Arrow.w, IsIso.inv_comp_eq, Category.assoc]

end MonoFactorisation

variable {f}

/-- Data exhibiting that a given factorisation through a mono is initial. -/
structure IsImage (F : MonoFactorisation f) where
  lift : ‚àÄ F' : MonoFactorisation f, F.I ‚ü∂ F'.I
  lift_fac : ‚àÄ F' : MonoFactorisation f, lift F' ‚â´ F'.m = F.m := by cat_disch

attribute [inherit_doc IsImage] IsImage.lift IsImage.lift_fac

attribute [reassoc (attr := simp)] IsImage.lift_fac

namespace IsImage

@[reassoc (attr := simp)]
theorem fac_lift {F : MonoFactorisation f} (hF : IsImage F) (F' : MonoFactorisation f) :
    F.e ‚â´ hF.lift F' = F'.e :=
  (cancel_mono F'.m).1 <| by simp

variable (f)

/-- The trivial factorisation of a monomorphism satisfies the universal property. -/
@[simps]
def self [Mono f] : IsImage (MonoFactorisation.self f) where lift F' := F'.e

instance [Mono f] : Inhabited (IsImage (MonoFactorisation.self f)) :=
  ‚ü®self f‚ü©

variable {f}

-- TODO this is another good candidate for a future `UniqueUpToCanonicalIso`.
/-- Two factorisations through monomorphisms satisfying the universal property
must factor through isomorphic objects. -/
@[simps]
def isoExt {F F' : MonoFactorisation f} (hF : IsImage F) (hF' : IsImage F') :
    F.I ‚âÖ F'.I where
  hom := hF.lift F'
  inv := hF'.lift F
  hom_inv_id := (cancel_mono F.m).1 (by simp)
  inv_hom_id := (cancel_mono F'.m).1 (by simp)

variable {F F' : MonoFactorisation f} (hF : IsImage F) (hF' : IsImage F')

theorem isoExt_hom_m : (isoExt hF hF').hom ‚â´ F'.m = F.m := by simp

theorem isoExt_inv_m : (isoExt hF hF').inv ‚â´ F.m = F'.m := by simp

theorem e_isoExt_hom : F.e ‚â´ (isoExt hF hF').hom = F'.e := by simp

theorem e_isoExt_inv : F'.e ‚â´ (isoExt hF hF').inv = F.e := by simp

/-- If `f` and `g` are isomorphic arrows, then a mono factorisation of `f` that is an image
gives a mono factorisation of `g` that is an image -/
@[simps]
def ofArrowIso {f g : Arrow C} {F : MonoFactorisation f.hom} (hF : IsImage F) (sq : f ‚ü∂ g)
    [IsIso sq] : IsImage (F.ofArrowIso sq) where
  lift F' := hF.lift (F'.ofArrowIso (inv sq))
  lift_fac F' := by
    simpa only [MonoFactorisation.ofArrowIso_m, Arrow.inv_right, ‚Üê Category.assoc,
      IsIso.comp_inv_eq] using hF.lift_fac (F'.ofArrowIso (inv sq))

end IsImage

variable (f)

/-- Data exhibiting that a morphism `f` has an image. -/
structure ImageFactorisation (f : X ‚ü∂ Y) where
  F : MonoFactorisation f
  isImage : IsImage F

attribute [inherit_doc ImageFactorisation] ImageFactorisation.F ImageFactorisation.isImage

namespace ImageFactorisation

instance [Mono f] : Inhabited (ImageFactorisation f) :=
  ‚ü®‚ü®_, IsImage.self f‚ü©‚ü©

/-- If `f` and `g` are isomorphic arrows, then an image factorisation of `f`
gives an image factorisation of `g` -/
@[simps]
def ofArrowIso {f g : Arrow C} (F : ImageFactorisation f.hom) (sq : f ‚ü∂ g) [IsIso sq] :
    ImageFactorisation g.hom where
  F := F.F.ofArrowIso sq
  isImage := F.isImage.ofArrowIso sq

end ImageFactorisation

/-- `HasImage f` means that there exists an image factorisation of `f`. -/
class HasImage (f : X ‚ü∂ Y) : Prop where mk' ::
  exists_image : Nonempty (ImageFactorisation f)

attribute [inherit_doc HasImage] HasImage.exists_image

theorem HasImage.mk {f : X ‚ü∂ Y} (F : ImageFactorisation f) : HasImage f :=
  ‚ü®Nonempty.intro F‚ü©

theorem HasImage.of_arrow_iso {f g : Arrow C} [h : HasImage f.hom] (sq : f ‚ü∂ g) [IsIso sq] :
    HasImage g.hom :=
  ‚ü®‚ü®h.exists_image.some.ofArrowIso sq‚ü©‚ü©

instance (priority := 100) mono_hasImage (f : X ‚ü∂ Y) [Mono f] : HasImage f :=
  HasImage.mk ‚ü®_, IsImage.self f‚ü©

section

variable [HasImage f]

/-- Some factorisation of `f` through a monomorphism (selected with choice). -/
def Image.monoFactorisation : MonoFactorisation f :=
  (Classical.choice HasImage.exists_image).F

/-- The witness of the universal property for the chosen factorisation of `f` through
a monomorphism. -/
def Image.isImage : IsImage (Image.monoFactorisation f) :=
  (Classical.choice HasImage.exists_image).isImage

/-- The categorical image of a morphism. -/
def image : C :=
  (Image.monoFactorisation f).I

/-- The inclusion of the image of a morphism into the target. -/
def image.Œπ : image f ‚ü∂ Y :=
  (Image.monoFactorisation f).m

@[simp]
theorem image.as_Œπ : (Image.monoFactorisation f).m = image.Œπ f := rfl

instance : Mono (image.Œπ f) :=
  (Image.monoFactorisation f).m_mono

/-- The map from the source to the image of a morphism. -/
def factorThruImage : X ‚ü∂ image f :=
  (Image.monoFactorisation f).e

/-- Rewrite in terms of the `factorThruImage` interface. -/
@[simp]
theorem as_factorThruImage : (Image.monoFactorisation f).e = factorThruImage f :=
  rfl

@[reassoc (attr := simp)]
theorem image.fac : factorThruImage f ‚â´ image.Œπ f = f :=
  (Image.monoFactorisation f).fac

variable {f}

/-- Any other factorisation of the morphism `f` through a monomorphism receives a map from the
image. -/
def image.lift (F' : MonoFactorisation f) : image f ‚ü∂ F'.I :=
  (Image.isImage f).lift F'

@[reassoc (attr := simp)]
theorem image.lift_fac (F' : MonoFactorisation f) : image.lift F' ‚â´ F'.m = image.Œπ f :=
  (Image.isImage f).lift_fac F'

@[reassoc (attr := simp)]
theorem image.fac_lift (F' : MonoFactorisation f) : factorThruImage f ‚â´ image.lift F' = F'.e :=
  (Image.isImage f).fac_lift F'

@[simp]
theorem image.isImage_lift (F : MonoFactorisation f) : (Image.isImage f).lift F = image.lift F :=
  rfl

@[reassoc (attr := simp)]
theorem IsImage.lift_Œπ {F : MonoFactorisation f} (hF : IsImage F) :
    hF.lift (Image.monoFactorisation f) ‚â´ image.Œπ f = F.m :=
  hF.lift_fac _

@[reassoc (attr := simp)]
theorem image.lift_mk_factorThruImage :
    image.lift { I := image f, m := Œπ f, e := factorThruImage f } ‚â´ image.Œπ f = image.Œπ f :=
  (Image.isImage f).lift_fac _

@[reassoc (attr := simp)]
theorem image.lift_mk_comp {C : Type u} [Category.{v} C] {X Y Z : C}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [HasImage g] [HasImage (f ‚â´ g)]
    (h : Y ‚ü∂ image g) (H : (f ‚â´ h) ‚â´ image.Œπ g = f ‚â´ g) :
    image.lift { I := image g, m := Œπ g, e := (f ‚â´ h) } ‚â´ image.Œπ g = image.Œπ (f ‚â´ g) :=
  image.lift_fac _

-- TODO we could put a category structure on `MonoFactorisation f`,
-- with the morphisms being `g : I ‚ü∂ I'` commuting with the `m`s
-- (they then automatically commute with the `e`s)
-- and show that an `imageOf f` gives an initial object there
-- (uniqueness of the lift comes for free).
instance image.lift_mono (F' : MonoFactorisation f) : Mono (image.lift F') := by
  refine @mono_of_mono _ _ _ _ _ _ F'.m ?_
  simpa using MonoFactorisation.m_mono _

theorem HasImage.uniq (F' : MonoFactorisation f) (l : image f ‚ü∂ F'.I) (w : l ‚â´ F'.m = image.Œπ f) :
    l = image.lift F' :=
  (cancel_mono F'.m).1 (by simp [w])

/-- If `has_image g`, then `has_image (f ‚â´ g)` when `f` is an isomorphism. -/
instance {X Y Z : C} (f : X ‚ü∂ Y) [IsIso f] (g : Y ‚ü∂ Z) [HasImage g] : HasImage (f ‚â´ g) where
  exists_image :=
    ‚ü®{  F :=
          { I := image g
            m := image.Œπ g
            e := f ‚â´ factorThruImage g }
        isImage :=
          { lift := fun F' => image.lift
                { I := F'.I
                  m := F'.m
                  e := inv f ‚â´ F'.e } } }‚ü©

end

section

variable (C)

/-- `HasImages` asserts that every morphism has an image. -/
class HasImages : Prop where
  has_image : ‚àÄ {X Y : C} (f : X ‚ü∂ Y), HasImage f

attribute [inherit_doc HasImages] HasImages.has_image

attribute [instance 100] HasImages.has_image

end

section

/-- The image of a monomorphism is isomorphic to the source. -/
def imageMonoIsoSource [Mono f] : image f ‚âÖ X :=
  IsImage.isoExt (Image.isImage f) (IsImage.self f)

@[reassoc (attr := simp)]
theorem imageMonoIsoSource_inv_Œπ [Mono f] : (imageMonoIsoSource f).inv ‚â´ image.Œπ f = f := by
  simp [imageMonoIsoSource]

@[reassoc (attr := simp)]
theorem imageMonoIsoSource_hom_self [Mono f] : (imageMonoIsoSource f).hom ‚â´ f = image.Œπ f := by
  simp only [‚Üê imageMonoIsoSource_inv_Œπ f]
  rw [‚Üê Category.assoc, Iso.hom_inv_id, Category.id_comp]

-- This is the proof that `factorThruImage f` is an epimorphism
-- from https://en.wikipedia.org/wiki/Image_%28category_theory%29, which is in turn taken from:
-- Mitchell, Barry (1965), Theory of categories, MR 0202787, p.12, Proposition 10.1
@[ext (iff := false)]
theorem image.ext [HasImage f] {W : C} {g h : image f ‚ü∂ W} [HasLimit (parallelPair g h)]
    (w : factorThruImage f ‚â´ g = factorThruImage f ‚â´ h) : g = h := by
  let q := equalizer.Œπ g h
  let e' := equalizer.lift _ w
  let F' : MonoFactorisation f :=
    { I := equalizer g h
      m := q ‚â´ image.Œπ f
      m_mono := mono_comp _ _
      e := e' }
  let v := image.lift F'
  have t‚ÇÄ : v ‚â´ q ‚â´ image.Œπ f = image.Œπ f := image.lift_fac F'
  have t : v ‚â´ q = ùüô (image f) :=
    (cancel_mono_id (image.Œπ f)).1
      (by
        convert t‚ÇÄ using 1
        rw [Category.assoc])
  -- The proof from wikipedia next proves `q ‚â´ v = ùüô _`,
  -- and concludes that `equalizer g h ‚âÖ image f`,
  -- but this isn't necessary.
  calc
    g = ùüô (image f) ‚â´ g := by rw [Category.id_comp]
    _ = v ‚â´ q ‚â´ g := by rw [‚Üê t, Category.assoc]
    _ = v ‚â´ q ‚â´ h := by rw [equalizer.condition g h]
    _ = ùüô (image f) ‚â´ h := by rw [‚Üê Category.assoc, t]
    _ = h := by rw [Category.id_comp]

instance [HasImage f] [‚àÄ {Z : C} (g h : image f ‚ü∂ Z), HasLimit (parallelPair g h)] :
    Epi (factorThruImage f) :=
  ‚ü®fun _ _ w => image.ext f w‚ü©

theorem epi_image_of_epi {X Y : C} (f : X ‚ü∂ Y) [HasImage f] [E : Epi f] : Epi (image.Œπ f) := by
  rw [‚Üê image.fac f] at E
  exact epi_of_epi (factorThruImage f) (image.Œπ f)

theorem epi_of_epi_image {X Y : C} (f : X ‚ü∂ Y) [HasImage f] [Epi (image.Œπ f)]
    [Epi (factorThruImage f)] : Epi f := by
  rw [‚Üê image.fac f]
  apply epi_comp

end

section

variable {f}
variable {f' : X ‚ü∂ Y} [HasImage f] [HasImage f']

/-- An equation between morphisms gives a comparison map between the images
(which momentarily we prove is an iso).
-/
def image.eqToHom (h : f = f') : image f ‚ü∂ image f' :=
  image.lift
    { I := image f'
      m := image.Œπ f'
      e := factorThruImage f'
      fac := by rw [h]; simp only [image.fac]}

instance (h : f = f') : IsIso (image.eqToHom h) :=
  ‚ü®‚ü®image.eqToHom h.symm,
      ‚ü®(cancel_mono (image.Œπ f)).1 (by
          subst h
          simp [image.eqToHom, Category.assoc, Category.id_comp]),
        (cancel_mono (image.Œπ f')).1 (by
          subst h
          simp [image.eqToHom])‚ü©‚ü©‚ü©

/-- An equation between morphisms gives an isomorphism between the images. -/
def image.eqToIso (h : f = f') : image f ‚âÖ image f' :=
  asIso (image.eqToHom h)

/-- As long as the category has equalizers,
the image inclusion maps commute with `image.eqToIso`.
-/
theorem image.eq_fac [HasEqualizers C] (h : f = f') :
    image.Œπ f = (image.eqToIso h).hom ‚â´ image.Œπ f' := by
  apply image.ext
  subst h
  simp [asIso,image.eqToIso, image.eqToHom]

end

section

variable {Z : C} (g : Y ‚ü∂ Z)

/-- The comparison map `image (f ‚â´ g) ‚ü∂ image g`. -/
def image.preComp [HasImage g] [HasImage (f ‚â´ g)] : image (f ‚â´ g) ‚ü∂ image g :=
  image.lift
    { I := image g
      m := image.Œπ g
      e := f ‚â´ factorThruImage g }

@[reassoc (attr := simp)]
theorem image.preComp_Œπ [HasImage g] [HasImage (f ‚â´ g)] :
    image.preComp f g ‚â´ image.Œπ g = image.Œπ (f ‚â´ g) := by
      simp [image.preComp]

@[reassoc (attr := simp)]
theorem image.factorThruImage_preComp [HasImage g] [HasImage (f ‚â´ g)] :
    factorThruImage (f ‚â´ g) ‚â´ image.preComp f g = f ‚â´ factorThruImage g := by simp [image.preComp]

/-- `image.preComp f g` is a monomorphism.
-/
instance image.preComp_mono [HasImage g] [HasImage (f ‚â´ g)] : Mono (image.preComp f g) := by
  refine @mono_of_mono _ _ _ _ _ _ (image.Œπ g) ?_
  simp only [image.preComp_Œπ]
  infer_instance

/-- The two step comparison map
  `image (f ‚â´ (g ‚â´ h)) ‚ü∂ image (g ‚â´ h) ‚ü∂ image h`
agrees with the one step comparison map
  `image (f ‚â´ (g ‚â´ h)) ‚âÖ image ((f ‚â´ g) ‚â´ h) ‚ü∂ image h`.
-/
theorem image.preComp_comp {W : C} (h : Z ‚ü∂ W) [HasImage (g ‚â´ h)] [HasImage (f ‚â´ g ‚â´ h)]
    [HasImage h] [HasImage ((f ‚â´ g) ‚â´ h)] :
    image.preComp f (g ‚â´ h) ‚â´ image.preComp g h =
      image.eqToHom (Category.assoc f g h).symm ‚â´ image.preComp (f ‚â´ g) h := by
  apply (cancel_mono (image.Œπ h)).1
  simp only [preComp, Category.assoc, fac, lift_mk_comp, eqToHom]
  rw [image.lift_fac]

variable [HasEqualizers C]

/-- `image.preComp f g` is an epimorphism when `f` is an epimorphism
(we need `C` to have equalizers to prove this).
-/
instance image.preComp_epi_of_epi [HasImage g] [HasImage (f ‚â´ g)] [Epi f] :
    Epi (image.preComp f g) := by
  apply @epi_of_epi_fac _ _ _ _ _ _ _ _ ?_ (image.factorThruImage_preComp _ _)
  exact epi_comp _ _

instance hasImage_iso_comp [IsIso f] [HasImage g] : HasImage (f ‚â´ g) :=
  HasImage.mk
    { F := (Image.monoFactorisation g).isoComp f
      isImage := { lift := fun F' => image.lift (F'.ofIsoComp f)
                   lift_fac := fun F' => by
                    dsimp
                    have : (MonoFactorisation.ofIsoComp f F').m = F'.m := rfl
                    rw [‚Üê this,image.lift_fac (MonoFactorisation.ofIsoComp f F')] } }

/-- `image.preComp f g` is an isomorphism when `f` is an isomorphism
(we need `C` to have equalizers to prove this).
-/
instance image.isIso_precomp_iso (f : X ‚ü∂ Y) [IsIso f] [HasImage g] : IsIso (image.preComp f g) :=
  ‚ü®‚ü®image.lift
        { I := image (f ‚â´ g)
          m := image.Œπ (f ‚â´ g)
          e := inv f ‚â´ factorThruImage (f ‚â´ g) },
      ‚ü®by
        ext
        simp [image.preComp], by
        ext
        simp [image.preComp]‚ü©‚ü©‚ü©

-- Note that in general we don't have the other comparison map you might expect
-- `image f ‚ü∂ image (f ‚â´ g)`.
instance hasImage_comp_iso [HasImage f] [IsIso g] : HasImage (f ‚â´ g) :=
  HasImage.mk
    { F := (Image.monoFactorisation f).compMono g
      isImage :=
      { lift := fun F' => image.lift F'.ofCompIso
        lift_fac := fun F' => by
          rw [‚Üê Category.comp_id (image.lift (MonoFactorisation.ofCompIso F') ‚â´ F'.m),
            ‚Üê IsIso.inv_hom_id g,‚Üê Category.assoc]
          refine congrArg (¬∑ ‚â´ g) ?_
          have : (image.lift (MonoFactorisation.ofCompIso F') ‚â´ F'.m) ‚â´ inv g =
            image.lift (MonoFactorisation.ofCompIso F') ‚â´
            ((MonoFactorisation.ofCompIso F').m) := by
              simp only [MonoFactorisation.ofCompIso_I, Category.assoc,
                MonoFactorisation.ofCompIso_m]
          rw [this, image.lift_fac (MonoFactorisation.ofCompIso F'),image.as_Œπ] }}

/-- Postcomposing by an isomorphism induces an isomorphism on the image. -/
def image.compIso [HasImage f] [IsIso g] : image f ‚âÖ image (f ‚â´ g) where
  hom := image.lift (Image.monoFactorisation (f ‚â´ g)).ofCompIso
  inv := image.lift ((Image.monoFactorisation f).compMono g)

@[reassoc (attr := simp)]
theorem image.compIso_hom_comp_image_Œπ [HasImage f] [IsIso g] :
    (image.compIso f g).hom ‚â´ image.Œπ (f ‚â´ g) = image.Œπ f ‚â´ g := by
  ext
  simp [image.compIso]

@[reassoc (attr := simp)]
theorem image.compIso_inv_comp_image_Œπ [HasImage f] [IsIso g] :
    (image.compIso f g).inv ‚â´ image.Œπ f = image.Œπ (f ‚â´ g) ‚â´ inv g := by
  ext
  simp [image.compIso]

end

end CategoryTheory.Limits

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]

section

instance {X Y : C} (f : X ‚ü∂ Y) [HasImage f] : HasImage (Arrow.mk f).hom :=
  show HasImage f by infer_instance

end

section HasImageMap

-- Don't generate unnecessary injectivity lemmas which the `simpNF` linter will complain about.
set_option genInjectivity false in
/-- An image map is a morphism `image f ‚Üí image g` fitting into a commutative square and satisfying
the obvious commutativity conditions. -/
structure ImageMap {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g) where
  map : image f.hom ‚ü∂ image g.hom
  map_Œπ : map ‚â´ image.Œπ g.hom = image.Œπ f.hom ‚â´ sq.right := by aesop

attribute [inherit_doc ImageMap] ImageMap.map ImageMap.map_Œπ

instance inhabitedImageMap {f : Arrow C} [HasImage f.hom] : Inhabited (ImageMap (ùüô f)) :=
  ‚ü®‚ü®ùüô _, by simp‚ü©‚ü©

attribute [reassoc (attr := simp)] ImageMap.map_Œπ

@[reassoc (attr := simp)]
theorem ImageMap.factor_map {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g)
    (m : ImageMap sq) : factorThruImage f.hom ‚â´ m.map = sq.left ‚â´ factorThruImage g.hom :=
  (cancel_mono (image.Œπ g.hom)).1 <| by simp

/-- To give an image map for a commutative square with `f` at the top and `g` at the bottom, it
suffices to give a map between any mono factorisation of `f` and any image factorisation of `g`. -/
def ImageMap.transport {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g)
    (F : MonoFactorisation f.hom) {F' : MonoFactorisation g.hom} (hF' : IsImage F')
    {map : F.I ‚ü∂ F'.I} (map_Œπ : map ‚â´ F'.m = F.m ‚â´ sq.right) : ImageMap sq where
  map := image.lift F ‚â´ map ‚â´ hF'.lift (Image.monoFactorisation g.hom)
  map_Œπ := by simp [map_Œπ]

/-- `HasImageMap sq` means that there is an `ImageMap` for the square `sq`. -/
class HasImageMap {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g) : Prop where
mk' ::
  has_image_map : Nonempty (ImageMap sq)

attribute [inherit_doc HasImageMap] HasImageMap.has_image_map

theorem HasImageMap.mk {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] {sq : f ‚ü∂ g}
    (m : ImageMap sq) : HasImageMap sq :=
  ‚ü®Nonempty.intro m‚ü©

theorem HasImageMap.transport {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g)
    (F : MonoFactorisation f.hom) {F' : MonoFactorisation g.hom} (hF' : IsImage F')
    (map : F.I ‚ü∂ F'.I) (map_Œπ : map ‚â´ F'.m = F.m ‚â´ sq.right) : HasImageMap sq :=
  HasImageMap.mk <| ImageMap.transport sq F hF' map_Œπ

/-- Obtain an `ImageMap` from a `HasImageMap` instance. -/
def HasImageMap.imageMap {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g)
    [HasImageMap sq] : ImageMap sq :=
  Classical.choice <| @HasImageMap.has_image_map _ _ _ _ _ _ sq _

-- see Note [lower instance priority]
instance (priority := 100) hasImageMapOfIsIso {f g : Arrow C} [HasImage f.hom] [HasImage g.hom]
    (sq : f ‚ü∂ g) [IsIso sq] : HasImageMap sq :=
  HasImageMap.mk
    { map := image.lift ((Image.monoFactorisation g.hom).ofArrowIso (inv sq))
      map_Œπ := by
        erw [‚Üê cancel_mono (inv sq).right, Category.assoc, ‚Üê MonoFactorisation.ofArrowIso_m,
          image.lift_fac, Category.assoc, ‚Üê Comma.comp_right, IsIso.hom_inv_id, Comma.id_right,
          Category.comp_id] }

instance HasImageMap.comp {f g h : Arrow C} [HasImage f.hom] [HasImage g.hom] [HasImage h.hom]
    (sq1 : f ‚ü∂ g) (sq2 : g ‚ü∂ h) [HasImageMap sq1] [HasImageMap sq2] : HasImageMap (sq1 ‚â´ sq2) :=
  HasImageMap.mk
    { map := (HasImageMap.imageMap sq1).map ‚â´ (HasImageMap.imageMap sq2).map
      map_Œπ := by
        rw [Category.assoc,ImageMap.map_Œπ, ImageMap.map_Œπ_assoc, Comma.comp_right] }

variable {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] (sq : f ‚ü∂ g)

section

attribute [local ext] ImageMap

theorem ImageMap.map_uniq_aux {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] {sq : f ‚ü∂ g}
    (map : image f.hom ‚ü∂ image g.hom)
    (map_Œπ : map ‚â´ image.Œπ g.hom = image.Œπ f.hom ‚â´ sq.right := by cat_disch)
    (map' : image f.hom ‚ü∂ image g.hom)
    (map_Œπ' : map' ‚â´ image.Œπ g.hom = image.Œπ f.hom ‚â´ sq.right) : (map = map') := by
  have : map ‚â´ image.Œπ g.hom = map' ‚â´ image.Œπ g.hom := by rw [map_Œπ,map_Œπ']
  apply (cancel_mono (image.Œπ g.hom)).1 this

theorem ImageMap.map_uniq {f g : Arrow C} [HasImage f.hom] [HasImage g.hom]
    {sq : f ‚ü∂ g} (F G : ImageMap sq) : F.map = G.map := by
  apply ImageMap.map_uniq_aux _ F.map_Œπ _ G.map_Œπ

/-- `@[simp]`-normal form of `ImageMap.mk.injEq`. -/
@[simp]
theorem ImageMap.mk.injEq' {f g : Arrow C} [HasImage f.hom] [HasImage g.hom] {sq : f ‚ü∂ g}
    (map : image f.hom ‚ü∂ image g.hom)
    (map_Œπ : map ‚â´ image.Œπ g.hom = image.Œπ f.hom ‚â´ sq.right := by cat_disch)
    (map' : image f.hom ‚ü∂ image g.hom)
    (map_Œπ' : map' ‚â´ image.Œπ g.hom = image.Œπ f.hom ‚â´ sq.right) : (map = map') = True := by
  simp only [Functor.id_obj, eq_iff_iff, iff_true]
  apply ImageMap.map_uniq_aux _ map_Œπ _ map_Œπ'

instance : Subsingleton (ImageMap sq) :=
  Subsingleton.intro fun a b =>
    ImageMap.ext <| ImageMap.map_uniq a b

end

variable [HasImageMap sq]

/-- The map on images induced by a commutative square. -/
abbrev image.map : image f.hom ‚ü∂ image g.hom :=
  (HasImageMap.imageMap sq).map

theorem image.factor_map :
    factorThruImage f.hom ‚â´ image.map sq = sq.left ‚â´ factorThruImage g.hom := by simp

theorem image.map_Œπ : image.map sq ‚â´ image.Œπ g.hom = image.Œπ f.hom ‚â´ sq.right := by simp

theorem image.map_homMk'_Œπ {X Y P Q : C} {k : X ‚ü∂ Y} [HasImage k] {l : P ‚ü∂ Q} [HasImage l]
    {m : X ‚ü∂ P} {n : Y ‚ü∂ Q} (w : m ‚â´ l = k ‚â´ n) [HasImageMap (Arrow.homMk' _ _ w)] :
    image.map (Arrow.homMk' _ _ w) ‚â´ image.Œπ l = image.Œπ k ‚â´ n :=
  image.map_Œπ _

section

variable {h : Arrow C} [HasImage h.hom] (sq' : g ‚ü∂ h)
variable [HasImageMap sq']

/-- Image maps for composable commutative squares induce an image map in the composite square. -/
def imageMapComp : ImageMap (sq ‚â´ sq') where map := image.map sq ‚â´ image.map sq'

@[simp]
theorem image.map_comp [HasImageMap (sq ‚â´ sq')] :
    image.map (sq ‚â´ sq') = image.map sq ‚â´ image.map sq' :=
  show (HasImageMap.imageMap (sq ‚â´ sq')).map = (imageMapComp sq sq').map by
    congr; simp only [eq_iff_true_of_subsingleton]

end

section

variable (f)

/-- The identity `image f ‚ü∂ image f` fits into the commutative square represented by the identity
morphism `ùüô f` in the arrow category. -/
def imageMapId : ImageMap (ùüô f) where map := ùüô (image f.hom)

@[simp]
theorem image.map_id [HasImageMap (ùüô f)] : image.map (ùüô f) = ùüô (image f.hom) :=
  show (HasImageMap.imageMap (ùüô f)).map = (imageMapId f).map by
    congr; simp only [eq_iff_true_of_subsingleton]

end

end HasImageMap

section

variable (C) [HasImages C]

/-- If a category `has_image_maps`, then all commutative squares induce morphisms on images. -/
class HasImageMaps : Prop where
  has_image_map : ‚àÄ {f g : Arrow C} (st : f ‚ü∂ g), HasImageMap st

attribute [instance 100] HasImageMaps.has_image_map

end

section HasImageMaps

variable [HasImages C] [HasImageMaps C]

/-- The functor from the arrow category of `C` to `C` itself that maps a morphism to its image
and a commutative square to the induced morphism on images. -/
@[simps]
def im : Arrow C ‚•§ C where
  obj f := image f.hom
  map st := image.map st

end HasImageMaps

section StrongEpiMonoFactorisation

/-- A strong epi-mono factorisation is a decomposition `f = e ‚â´ m` with `e` a strong epimorphism
and `m` a monomorphism. -/
structure StrongEpiMonoFactorisation {X Y : C} (f : X ‚ü∂ Y) extends MonoFactorisation f where
  [e_strong_epi : StrongEpi e]

attribute [inherit_doc StrongEpiMonoFactorisation] StrongEpiMonoFactorisation.e_strong_epi

attribute [instance] StrongEpiMonoFactorisation.e_strong_epi

/-- Satisfying the inhabited linter -/
instance strongEpiMonoFactorisationInhabited {X Y : C} (f : X ‚ü∂ Y) [StrongEpi f] :
    Inhabited (StrongEpiMonoFactorisation f) :=
  ‚ü®‚ü®‚ü®Y, ùüô Y, f, by simp‚ü©‚ü©‚ü©

/-- A mono factorisation coming from a strong epi-mono factorisation always has the universal
property of the image. -/
def StrongEpiMonoFactorisation.toMonoIsImage {X Y : C} {f : X ‚ü∂ Y}
    (F : StrongEpiMonoFactorisation f) : IsImage F.toMonoFactorisation where
  lift G :=
    (CommSq.mk (show G.e ‚â´ G.m = F.e ‚â´ F.m by rw [F.toMonoFactorisation.fac, G.fac])).lift

variable (C)

/-- A category has strong epi-mono factorisations if every morphism admits a strong epi-mono
factorisation. -/
class HasStrongEpiMonoFactorisations : Prop where mk' ::
  has_fac : ‚àÄ {X Y : C} (f : X ‚ü∂ Y), Nonempty (StrongEpiMonoFactorisation f)

attribute [inherit_doc HasStrongEpiMonoFactorisations] HasStrongEpiMonoFactorisations.has_fac

variable {C}

theorem HasStrongEpiMonoFactorisations.mk
    (d : ‚àÄ {X Y : C} (f : X ‚ü∂ Y), StrongEpiMonoFactorisation f) :
    HasStrongEpiMonoFactorisations C :=
  ‚ü®fun f => Nonempty.intro <| d f‚ü©

instance (priority := 100) hasImages_of_hasStrongEpiMonoFactorisations
    [HasStrongEpiMonoFactorisations C] : HasImages C where
  has_image f :=
    let F' := Classical.choice (HasStrongEpiMonoFactorisations.has_fac f)
    HasImage.mk
      { F := F'.toMonoFactorisation
        isImage := F'.toMonoIsImage }

end StrongEpiMonoFactorisation

section HasStrongEpiImages

variable (C) [HasImages C]

/-- A category has strong epi images if it has all images and `factorThruImage f` is a strong
epimorphism for all `f`. -/
class HasStrongEpiImages : Prop where
  strong_factorThruImage : ‚àÄ {X Y : C} (f : X ‚ü∂ Y), StrongEpi (factorThruImage f)

attribute [instance] HasStrongEpiImages.strong_factorThruImage

end HasStrongEpiImages

section HasStrongEpiImages

/-- If there is a single strong epi-mono factorisation of `f`, then every image factorisation is a
strong epi-mono factorisation. -/
theorem strongEpi_of_strongEpiMonoFactorisation {X Y : C} {f : X ‚ü∂ Y}
    (F : StrongEpiMonoFactorisation f) {F' : MonoFactorisation f} (hF' : IsImage F') :
    StrongEpi F'.e := by
  rw [‚Üê IsImage.e_isoExt_hom F.toMonoIsImage hF']
  apply strongEpi_comp

theorem strongEpi_factorThruImage_of_strongEpiMonoFactorisation {X Y : C} {f : X ‚ü∂ Y} [HasImage f]
    (F : StrongEpiMonoFactorisation f) : StrongEpi (factorThruImage f) :=
  strongEpi_of_strongEpiMonoFactorisation F <| Image.isImage f

/-- If we constructed our images from strong epi-mono factorisations, then these images are
strong epi images. -/
instance (priority := 100) hasStrongEpiImages_of_hasStrongEpiMonoFactorisations
    [HasStrongEpiMonoFactorisations C] : HasStrongEpiImages C where
  strong_factorThruImage f :=
    strongEpi_factorThruImage_of_strongEpiMonoFactorisation <|
      Classical.choice <| HasStrongEpiMonoFactorisations.has_fac f

end HasStrongEpiImages

section HasStrongEpiImages

variable [HasImages C]

/-- A category with strong epi images has image maps. -/
instance (priority := 100) hasImageMapsOfHasStrongEpiImages [HasStrongEpiImages C] :
    HasImageMaps C where
  has_image_map {f} {g} st :=
    HasImageMap.mk
      { map :=
          (CommSq.mk
              (show
                (st.left ‚â´ factorThruImage g.hom) ‚â´ image.Œπ g.hom =
                  factorThruImage f.hom ‚â´ image.Œπ f.hom ‚â´ st.right
                by simp)).lift }

/-- If a category has images, equalizers and pullbacks, then images are automatically strong epi
images. -/
instance (priority := 100) hasStrongEpiImages_of_hasPullbacks_of_hasEqualizers [HasPullbacks C]
    [HasEqualizers C] : HasStrongEpiImages C where
  strong_factorThruImage f :=
    StrongEpi.mk' fun {A} {B} h h_mono x y sq =>
      CommSq.HasLift.mk'
        { l :=
            image.lift
                { I := pullback h y
                  m := pullback.snd h y ‚â´ image.Œπ f
                  m_mono := mono_comp _ _
                  e := pullback.lift _ _ sq.w } ‚â´
              pullback.fst h y
          fac_left := by simp only [image.fac_lift_assoc, pullback.lift_fst]
          fac_right := by
            apply image.ext
            simp only [sq.w, Category.assoc, image.fac_lift_assoc, pullback.lift_fst_assoc] }

end HasStrongEpiImages

variable [HasStrongEpiMonoFactorisations C]
variable {X Y : C} {f : X ‚ü∂ Y}

/--
If `C` has strong epi mono factorisations, then the image is unique up to isomorphism, in that if
`f` factors as a strong epi followed by a mono, this factorisation is essentially the image
factorisation.
-/
def image.isoStrongEpiMono {I' : C} (e : X ‚ü∂ I') (m : I' ‚ü∂ Y) (comm : e ‚â´ m = f) [StrongEpi e]
    [Mono m] : I' ‚âÖ image f :=
  let F : StrongEpiMonoFactorisation f := { I := I', m := m, e := e}
  IsImage.isoExt F.toMonoIsImage <| Image.isImage f

@[simp]
theorem image.isoStrongEpiMono_hom_comp_Œπ {I' : C} (e : X ‚ü∂ I') (m : I' ‚ü∂ Y) (comm : e ‚â´ m = f)
    [StrongEpi e] [Mono m] : (image.isoStrongEpiMono e m comm).hom ‚â´ image.Œπ f = m := by
  dsimp [isoStrongEpiMono]
  apply IsImage.lift_fac

@[simp]
theorem image.isoStrongEpiMono_inv_comp_mono {I' : C} (e : X ‚ü∂ I') (m : I' ‚ü∂ Y) (comm : e ‚â´ m = f)
    [StrongEpi e] [Mono m] : (image.isoStrongEpiMono e m comm).inv ‚â´ m = image.Œπ f :=
  image.lift_fac _

open MorphismProperty

variable (C)

/-- A category with strong epi mono factorisations admits functorial epi/mono factorizations. -/
noncomputable def functorialEpiMonoFactorizationData :
    FunctorialFactorizationData (epimorphisms C) (monomorphisms C) where
  Z := im
  i := { app := fun f => factorThruImage f.hom }
  p := { app := fun f => image.Œπ f.hom }
  hi _ := epimorphisms.infer_property _
  hp _ := monomorphisms.infer_property _

end CategoryTheory.Limits

namespace CategoryTheory.Functor

open CategoryTheory.Limits

variable {C D : Type*} [Category C] [Category D]

theorem hasStrongEpiMonoFactorisations_imp_of_isEquivalence (F : C ‚•§ D) [IsEquivalence F]
    [h : HasStrongEpiMonoFactorisations C] : HasStrongEpiMonoFactorisations D :=
  ‚ü®fun {X} {Y} f => by
    let em : StrongEpiMonoFactorisation (F.inv.map f) :=
      (HasStrongEpiMonoFactorisations.has_fac (F.inv.map f)).some
    haveI : Mono (F.map em.m ‚â´ F.asEquivalence.counitIso.hom.app Y) := mono_comp _ _
    haveI : StrongEpi (F.asEquivalence.counitIso.inv.app X ‚â´ F.map em.e) := strongEpi_comp _ _
    exact
      Nonempty.intro
        { I := F.obj em.I
          e := F.asEquivalence.counitIso.inv.app X ‚â´ F.map em.e
          m := F.map em.m ‚â´ F.asEquivalence.counitIso.hom.app Y
          fac := by
            simp only [asEquivalence_functor, Category.assoc, ‚Üê F.map_comp_assoc,
              MonoFactorisation.fac, fun_inv_map, id_obj, Iso.inv_hom_id_app, Category.comp_id,
              Iso.inv_hom_id_app_assoc] }‚ü©

end CategoryTheory.Functor
