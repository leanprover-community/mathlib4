/-
Copyright (c) 2018 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Discrete.Basic

/-!
# Categorical (co)products

This file defines (co)products as special cases of (co)limits.

A product is the categorical generalization of the object `Î  i, f i` where `f : Î¹ â†’ C`. It is a
limit cone over the diagram formed by `f`, implemented by converting `f` into a functor
`Discrete Î¹ â¥¤ C`.

A coproduct is the dual concept.

## Main definitions

* a `Fan` is a cone over a discrete category
* `Fan.mk` constructs a fan from an indexed collection of maps
* a `Pi` is a `limit (Discrete.functor f)`

Each of these has a dual.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.
-/

noncomputable section

universe w w' wâ‚‚ wâ‚ƒ v vâ‚‚ u uâ‚‚

open CategoryTheory

namespace CategoryTheory.Limits

variable {Î² : Type w} {Î± : Type wâ‚‚} {Î³ : Type wâ‚ƒ}
variable {C : Type u} [Category.{v} C]

-- We don't need an analogue of `Pair` (for binary products), `ParallelPair` (for equalizers),
-- or `(Co)span`, since we already have `Discrete.functor`.

/-- A fan over `f : Î² â†’ C` consists of a collection of maps from an object `P` to every `f b`. -/
abbrev Fan (f : Î² â†’ C) :=
  Cone (Discrete.functor f)

/-- A cofan over `f : Î² â†’ C` consists of a collection of maps from every `f b` to an object `P`. -/
abbrev Cofan (f : Î² â†’ C) :=
  Cocone (Discrete.functor f)

/-- A fan over `f : Î² â†’ C` consists of a collection of maps from an object `P` to every `f b`. -/
@[simps! pt Ï€_app]
def Fan.mk {f : Î² â†’ C} (P : C) (p : âˆ€ b, P âŸ¶ f b) : Fan f where
  pt := P
  Ï€ := Discrete.natTrans (fun X => p X.as)

/-- A cofan over `f : Î² â†’ C` consists of a collection of maps from every `f b` to an object `P`. -/
@[simps! pt Î¹_app]
def Cofan.mk {f : Î² â†’ C} (P : C) (p : âˆ€ b, f b âŸ¶ P) : Cofan f where
  pt := P
  Î¹ := Discrete.natTrans (fun X => p X.as)

/-- Get the `j`th "projection" in the fan.
(Note that the initial letter of `proj` matches the greek letter in `Cone.Ï€`.) -/
def Fan.proj {f : Î² â†’ C} (p : Fan f) (j : Î²) : p.pt âŸ¶ f j :=
  p.Ï€.app (Discrete.mk j)

/-- Get the `j`th "injection" in the cofan.
(Note that the initial letter of `inj` matches the greek letter in `Cocone.Î¹`.) -/
def Cofan.inj {f : Î² â†’ C} (p : Cofan f) (j : Î²) : f j âŸ¶ p.pt :=
  p.Î¹.app (Discrete.mk j)

@[simp]
theorem fan_mk_proj {f : Î² â†’ C} (P : C) (p : âˆ€ b, P âŸ¶ f b) : (Fan.mk P p).proj = p :=
  rfl

@[simp]
theorem cofan_mk_inj {f : Î² â†’ C} (P : C) (p : âˆ€ b, f b âŸ¶ P) : (Cofan.mk P p).inj = p :=
  rfl

/-- An abbreviation for `HasLimit (Discrete.functor f)`. -/
abbrev HasProduct (f : Î² â†’ C) :=
  HasLimit (Discrete.functor f)

/-- An abbreviation for `HasColimit (Discrete.functor f)`. -/
abbrev HasCoproduct (f : Î² â†’ C) :=
  HasColimit (Discrete.functor f)

lemma hasCoproduct_of_equiv_of_iso (f : Î± â†’ C) (g : Î² â†’ C)
    [HasCoproduct f] (e : Î² â‰ƒ Î±) (iso : âˆ€ j, g j â‰… f (e j)) : HasCoproduct g := by
  have : HasColimit ((Discrete.equivalence e).functor â‹™ Discrete.functor f) :=
    hasColimit_equivalence_comp _
  have Î± : Discrete.functor g â‰… (Discrete.equivalence e).functor â‹™ Discrete.functor f :=
    Discrete.natIso (fun âŸ¨jâŸ© => iso j)
  exact hasColimit_of_iso Î±

lemma hasProduct_of_equiv_of_iso (f : Î± â†’ C) (g : Î² â†’ C)
    [HasProduct f] (e : Î² â‰ƒ Î±) (iso : âˆ€ j, g j â‰… f (e j)) : HasProduct g := by
  have : HasLimit ((Discrete.equivalence e).functor â‹™ Discrete.functor f) :=
    hasLimit_equivalence_comp _
  have Î± : Discrete.functor g â‰… (Discrete.equivalence e).functor â‹™ Discrete.functor f :=
    Discrete.natIso (fun âŸ¨jâŸ© => iso j)
  exact hasLimit_of_iso Î±.symm

/-- Make a fan `f` into a limit fan by providing `lift`, `fac`, and `uniq` --
  just a convenience lemma to avoid having to go through `Discrete` -/
@[simps]
def mkFanLimit {f : Î² â†’ C} (t : Fan f) (lift : âˆ€ s : Fan f, s.pt âŸ¶ t.pt)
    (fac : âˆ€ (s : Fan f) (j : Î²), lift s â‰« t.proj j = s.proj j := by aesop_cat)
    (uniq : âˆ€ (s : Fan f) (m : s.pt âŸ¶ t.pt) (_ : âˆ€ j : Î², m â‰« t.proj j = s.proj j),
      m = lift s := by aesop_cat) :
    IsLimit t :=
  { lift }

/-- Constructor for morphisms to the point of a limit fan. -/
def Fan.IsLimit.desc {F : Î² â†’ C} {c : Fan F} (hc : IsLimit c) {A : C}
    (f : âˆ€ i, A âŸ¶ F i) : A âŸ¶ c.pt :=
  hc.lift (Fan.mk A f)

@[reassoc (attr := simp)]
lemma Fan.IsLimit.fac {F : Î² â†’ C} {c : Fan F} (hc : IsLimit c) {A : C}
    (f : âˆ€ i, A âŸ¶ F i) (i : Î²) :
    Fan.IsLimit.desc hc f â‰« c.proj i = f i :=
  hc.fac (Fan.mk A f) âŸ¨iâŸ©

@[reassoc (attr := simp)]
lemma Fan.IsLimit.lift_proj {X : Î² â†’ C} {c : Fan X} (d : Fan X) (hc : IsLimit c)
    (i : Î²) : hc.lift d â‰« c.proj i = d.proj i :=
  hc.fac _ _

lemma Fan.IsLimit.hom_ext {I : Type*} {F : I â†’ C} {c : Fan F} (hc : IsLimit c) {A : C}
    (f g : A âŸ¶ c.pt) (h : âˆ€ i, f â‰« c.proj i = g â‰« c.proj i) : f = g :=
  hc.hom_ext (fun âŸ¨iâŸ© => h i)

/-- Make a cofan `f` into a colimit cofan by providing `desc`, `fac`, and `uniq` --
  just a convenience lemma to avoid having to go through `Discrete` -/
@[simps]
def mkCofanColimit {f : Î² â†’ C} (s : Cofan f) (desc : âˆ€ t : Cofan f, s.pt âŸ¶ t.pt)
    (fac : âˆ€ (t : Cofan f) (j : Î²), s.inj j â‰« desc t = t.inj j := by aesop_cat)
    (uniq : âˆ€ (t : Cofan f) (m : s.pt âŸ¶ t.pt) (_ : âˆ€ j : Î², s.inj j â‰« m = t.inj j),
      m = desc t := by aesop_cat) :
    IsColimit s :=
  { desc }

/-- Constructor for morphisms from the point of a colimit cofan. -/
def Cofan.IsColimit.desc {F : Î² â†’ C} {c : Cofan F} (hc : IsColimit c) {A : C}
    (f : âˆ€ i, F i âŸ¶ A) : c.pt âŸ¶ A :=
  hc.desc (Cofan.mk A f)

@[reassoc (attr := simp)]
lemma Cofan.IsColimit.fac {F : Î² â†’ C} {c : Cofan F} (hc : IsColimit c) {A : C}
    (f : âˆ€ i, F i âŸ¶ A) (i : Î²) :
    c.inj i â‰« Cofan.IsColimit.desc hc f = f i :=
  hc.fac (Cofan.mk A f) âŸ¨iâŸ©

@[reassoc (attr := simp)]
lemma Cofan.IsColimit.inj_desc {X : Î² â†’ C} {c : Cofan X} (d : Cofan X) (hc : IsColimit c)
    (i : Î²) : c.inj i â‰« hc.desc d = d.inj i :=
  hc.fac _ _

lemma Cofan.IsColimit.hom_ext {I : Type*} {F : I â†’ C} {c : Cofan F} (hc : IsColimit c) {A : C}
    (f g : c.pt âŸ¶ A) (h : âˆ€ i, c.inj i â‰« f = c.inj i â‰« g) : f = g :=
  hc.hom_ext (fun âŸ¨iâŸ© => h i)

section

variable (C)

/-- An abbreviation for `HasLimitsOfShape (Discrete f)`. -/
abbrev HasProductsOfShape (Î² : Type v) :=
  HasLimitsOfShape.{v} (Discrete Î²)

/-- An abbreviation for `HasColimitsOfShape (Discrete f)`. -/
abbrev HasCoproductsOfShape (Î² : Type v) :=
  HasColimitsOfShape.{v} (Discrete Î²)

end

/-- `piObj f` computes the product of a family of elements `f`.
(It is defined as an abbreviation for `limit (Discrete.functor f)`,
so for most facts about `piObj f`, you will just use general facts about limits.) -/
abbrev piObj (f : Î² â†’ C) [HasProduct f] :=
  limit (Discrete.functor f)

/-- `sigmaObj f` computes the coproduct of a family of elements `f`.
(It is defined as an abbreviation for `colimit (Discrete.functor f)`,
so for most facts about `sigmaObj f`, you will just use general facts about colimits.) -/
abbrev sigmaObj (f : Î² â†’ C) [HasCoproduct f] :=
  colimit (Discrete.functor f)

/-- notation for categorical products. We need `á¶œ` to avoid conflict with `Finset.prod`. -/
notation "âˆá¶œ " f:60 => piObj f

/-- notation for categorical coproducts -/
notation "âˆ " f:60 => sigmaObj f

/-- The `b`-th projection from the pi object over `f` has the form `âˆá¶œ f âŸ¶ f b`. -/
abbrev Pi.Ï€ (f : Î² â†’ C) [HasProduct f] (b : Î²) : âˆá¶œ f âŸ¶ f b :=
  limit.Ï€ (Discrete.functor f) (Discrete.mk b)

/-- The `b`-th inclusion into the sigma object over `f` has the form `f b âŸ¶ âˆ f`. -/
abbrev Sigma.Î¹ (f : Î² â†’ C) [HasCoproduct f] (b : Î²) : f b âŸ¶ âˆ f :=
  colimit.Î¹ (Discrete.functor f) (Discrete.mk b)

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/10688): added the next two lemmas to ease automation; without these lemmas,
-- `limit.hom_ext` would be applied, but the goal would involve terms
-- in `Discrete Î²` rather than `Î²` itself
@[ext 1050]
lemma Pi.hom_ext {f : Î² â†’ C} [HasProduct f] {X : C} (gâ‚ gâ‚‚ : X âŸ¶ âˆá¶œ f)
    (h : âˆ€ (b : Î²), gâ‚ â‰« Pi.Ï€ f b = gâ‚‚ â‰« Pi.Ï€ f b) : gâ‚ = gâ‚‚ :=
  limit.hom_ext (fun âŸ¨jâŸ© => h j)

@[ext 1050]
lemma Sigma.hom_ext {f : Î² â†’ C} [HasCoproduct f] {X : C} (gâ‚ gâ‚‚ : âˆ f âŸ¶ X)
    (h : âˆ€ (b : Î²), Sigma.Î¹ f b â‰« gâ‚ = Sigma.Î¹ f b â‰« gâ‚‚) : gâ‚ = gâ‚‚ :=
  colimit.hom_ext (fun âŸ¨jâŸ© => h j)

/-- The fan constructed of the projections from the product is limiting. -/
def productIsProduct (f : Î² â†’ C) [HasProduct f] : IsLimit (Fan.mk _ (Pi.Ï€ f)) :=
  IsLimit.ofIsoLimit (limit.isLimit (Discrete.functor f)) (Cones.ext (Iso.refl _))

/-- The cofan constructed of the inclusions from the coproduct is colimiting. -/
def coproductIsCoproduct (f : Î² â†’ C) [HasCoproduct f] : IsColimit (Cofan.mk _ (Sigma.Î¹ f)) :=
  IsColimit.ofIsoColimit (colimit.isColimit (Discrete.functor f)) (Cocones.ext (Iso.refl _))

-- The `simpNF` linter incorrectly identifies these as simp lemmas that could never apply.
-- It seems the side condition `w` is not applied by `simpNF`.
-- https://github.com/leanprover-community/mathlib4/issues/5049
-- They are used by `simp` in `Pi.whiskerEquiv` below.
@[reassoc (attr := simp, nolint simpNF)]
theorem Pi.Ï€_comp_eqToHom {J : Type*} (f : J â†’ C) [HasProduct f] {j j' : J} (w : j = j') :
    Pi.Ï€ f j â‰« eqToHom (by simp [w]) = Pi.Ï€ f j' := by
  cases w
  simp

-- The `simpNF` linter incorrectly identifies these as simp lemmas that could never apply.
-- It seems the side condition `w` is not applied by `simpNF`.
-- https://github.com/leanprover-community/mathlib4/issues/5049
-- They are used by `simp` in `Sigma.whiskerEquiv` below.
@[reassoc (attr := simp, nolint simpNF)]
theorem Sigma.eqToHom_comp_Î¹ {J : Type*} (f : J â†’ C) [HasCoproduct f] {j j' : J} (w : j = j') :
    eqToHom (by simp [w]) â‰« Sigma.Î¹ f j' = Sigma.Î¹ f j := by
  cases w
  simp

/-- A collection of morphisms `P âŸ¶ f b` induces a morphism `P âŸ¶ âˆá¶œ f`. -/
abbrev Pi.lift {f : Î² â†’ C} [HasProduct f] {P : C} (p : âˆ€ b, P âŸ¶ f b) : P âŸ¶ âˆá¶œ f :=
  limit.lift _ (Fan.mk P p)

theorem Pi.lift_Ï€ {Î² : Type w} {f : Î² â†’ C} [HasProduct f] {P : C} (p : âˆ€ b, P âŸ¶ f b) (b : Î²) :
    Pi.lift p â‰« Pi.Ï€ f b = p b := by
  simp only [limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app]

/-- A version of `Cones.ext` for `Fan`s. -/
@[simps!]
def Fan.ext {f : Î² â†’ C} {câ‚ câ‚‚ : Fan f} (e : câ‚.pt â‰… câ‚‚.pt)
    (w : âˆ€ (b : Î²), câ‚.proj b = e.hom â‰« câ‚‚.proj b := by aesop_cat) : câ‚ â‰… câ‚‚ :=
  Cones.ext e (fun âŸ¨jâŸ© => w j)

/-- A collection of morphisms `f b âŸ¶ P` induces a morphism `âˆ f âŸ¶ P`. -/
abbrev Sigma.desc {f : Î² â†’ C} [HasCoproduct f] {P : C} (p : âˆ€ b, f b âŸ¶ P) : âˆ f âŸ¶ P :=
  colimit.desc _ (Cofan.mk P p)

theorem Sigma.Î¹_desc {Î² : Type w} {f : Î² â†’ C} [HasCoproduct f] {P : C} (p : âˆ€ b, f b âŸ¶ P) (b : Î²) :
    Sigma.Î¹ f b â‰« Sigma.desc p = p b := by
  simp only [colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]

instance {f : Î² â†’ C} [HasCoproduct f] : IsIso (Sigma.desc (fun a â†¦ Sigma.Î¹ f a)) := by
  convert IsIso.id _
  ext
  simp

/-- A version of `Cocones.ext` for `Cofan`s. -/
@[simps!]
def Cofan.ext {f : Î² â†’ C} {câ‚ câ‚‚ : Cofan f} (e : câ‚.pt â‰… câ‚‚.pt)
    (w : âˆ€ (b : Î²), câ‚.inj b â‰« e.hom = câ‚‚.inj b := by aesop_cat) : câ‚ â‰… câ‚‚ :=
  Cocones.ext e (fun âŸ¨jâŸ© => w j)

/-- A cofan `c` on `f` such that the induced map `âˆ f âŸ¶ c.pt` is an iso, is a coproduct. -/
def Cofan.isColimitOfIsIsoSigmaDesc {f : Î² â†’ C} [HasCoproduct f] (c : Cofan f)
    [hc : IsIso (Sigma.desc c.inj)] : IsColimit c :=
  IsColimit.ofIsoColimit (colimit.isColimit (Discrete.functor f))
    (Cofan.ext (@asIso _ _ _ _ _ hc) (fun _ => colimit.Î¹_desc _ _))

lemma Cofan.isColimit_iff_isIso_sigmaDesc {f : Î² â†’ C} [HasCoproduct f] (c : Cofan f) :
    IsIso (Sigma.desc c.inj) â†” Nonempty (IsColimit c) := by
  refine âŸ¨fun h â†¦ âŸ¨isColimitOfIsIsoSigmaDesc câŸ©, fun âŸ¨hcâŸ© â†¦ ?_âŸ©
  have : IsIso (((coproductIsCoproduct f).coconePointUniqueUpToIso hc).hom â‰« hc.desc c) := by
    simp; infer_instance
  convert this
  ext
  simp only [colimit.Î¹_desc, mk_pt, mk_Î¹_app, IsColimit.coconePointUniqueUpToIso,
    coproductIsCoproduct, colimit.cocone_x, Functor.mapIso_hom, IsColimit.uniqueUpToIso_hom,
    Cocones.forget_map, IsColimit.descCoconeMorphism_hom, IsColimit.ofIsoColimit_desc,
    Cocones.ext_inv_hom, Iso.refl_inv, colimit.isColimit_desc, Category.id_comp,
    IsColimit.desc_self, Category.comp_id]
  rfl

/-- A coproduct of coproducts is a coproduct -/
def Cofan.isColimitTrans {X : Î± â†’ C} (c : Cofan X) (hc : IsColimit c)
    {Î² : Î± â†’ Type*} {Y : (a : Î±) â†’ Î² a â†’ C} (Ï€ : (a : Î±) â†’ (b : Î² a) â†’ Y a b âŸ¶ X a)
      (hs : âˆ€ a, IsColimit (Cofan.mk (X a) (Ï€ a))) :
        IsColimit (Cofan.mk (f := fun âŸ¨a,bâŸ© => Y a b) c.pt
          (fun (âŸ¨a, bâŸ© : Î£ a, _) â†¦ Ï€ a b â‰« c.inj a)) := by
  refine mkCofanColimit _ ?_ ?_ ?_
  Â· exact fun t â†¦ hc.desc (Cofan.mk _ fun a â†¦ (hs a).desc (Cofan.mk t.pt (fun b â†¦ t.inj âŸ¨a, bâŸ©)))
  Â· intro t âŸ¨a, bâŸ©
    simp only [mk_pt, cofan_mk_inj, Category.assoc]
    erw [hc.fac, (hs a).fac]
    rfl
  Â· intro t m h
    refine hc.hom_ext fun âŸ¨aâŸ© â†¦ (hs a).hom_ext fun âŸ¨bâŸ© â†¦ ?_
    erw [hc.fac, (hs a).fac]
    simpa using h âŸ¨a, bâŸ©

/-- Construct a morphism between categorical products (indexed by the same type)
from a family of morphisms between the factors.
-/
abbrev Pi.map {f g : Î² â†’ C} [HasProduct f] [HasProduct g] (p : âˆ€ b, f b âŸ¶ g b) : âˆá¶œ f âŸ¶ âˆá¶œ g :=
  limMap (Discrete.natTrans fun X => p X.as)

@[simp]
lemma Pi.map_id {f : Î± â†’ C} [HasProduct f] : Pi.map (fun a => ğŸ™ (f a)) = ğŸ™ (âˆá¶œ f) := by
  ext; simp

lemma Pi.map_comp_map {f g h : Î± â†’ C} [HasProduct f] [HasProduct g] [HasProduct h]
    (q : âˆ€ (a : Î±), f a âŸ¶ g a) (q' : âˆ€ (a : Î±), g a âŸ¶ h a) :
    Pi.map q â‰« Pi.map q' = Pi.map (fun a => q a â‰« q' a) := by
  ext; simp

instance Pi.map_mono {f g : Î² â†’ C} [HasProduct f] [HasProduct g] (p : âˆ€ b, f b âŸ¶ g b)
    [âˆ€ i, Mono (p i)] : Mono <| Pi.map p :=
  @Limits.limMap_mono _ _ _ _ (Discrete.functor f) (Discrete.functor g) _ _
    (Discrete.natTrans fun X => p X.as) (by dsimp; infer_instance)

/-- Construct a morphism between categorical products from a family of morphisms between the
    factors. -/
def Pi.map' {f : Î± â†’ C} {g : Î² â†’ C} [HasProduct f] [HasProduct g] (p : Î² â†’ Î±)
    (q : âˆ€ (b : Î²), f (p b) âŸ¶ g b) : âˆá¶œ f âŸ¶ âˆá¶œ g :=
  Pi.lift (fun a => Pi.Ï€ _ _ â‰« q a)

@[reassoc (attr := simp)]
lemma Pi.map'_comp_Ï€ {f : Î± â†’ C} {g : Î² â†’ C} [HasProduct f] [HasProduct g] (p : Î² â†’ Î±)
    (q : âˆ€ (b : Î²), f (p b) âŸ¶ g b) (b : Î²) : Pi.map' p q â‰« Pi.Ï€ g b = Pi.Ï€ f (p b) â‰« q b :=
  limit.lift_Ï€ _ _

lemma Pi.map'_id_id {f : Î± â†’ C} [HasProduct f] : Pi.map' id (fun a => ğŸ™ (f a)) = ğŸ™ (âˆá¶œ f) := by
  ext; simp

@[simp]
lemma Pi.map'_id {f g : Î± â†’ C} [HasProduct f] [HasProduct g] (p : âˆ€ b, f b âŸ¶ g b) :
    Pi.map' id p = Pi.map p :=
  rfl

lemma Pi.map'_comp_map' {f : Î± â†’ C} {g : Î² â†’ C} {h : Î³ â†’ C} [HasProduct f] [HasProduct g]
    [HasProduct h] (p : Î² â†’ Î±) (p' : Î³ â†’ Î²) (q : âˆ€ (b : Î²), f (p b) âŸ¶ g b)
    (q' : âˆ€ (c : Î³), g (p' c) âŸ¶ h c) :
    Pi.map' p q â‰« Pi.map' p' q' = Pi.map' (p âˆ˜ p') (fun c => q (p' c) â‰« q' c) := by
  ext; simp

lemma Pi.map'_comp_map {f : Î± â†’ C} {g h : Î² â†’ C} [HasProduct f] [HasProduct g] [HasProduct h]
    (p : Î² â†’ Î±) (q : âˆ€ (b : Î²), f (p b) âŸ¶ g b) (q' : âˆ€ (b : Î²), g b âŸ¶ h b) :
    Pi.map' p q â‰« Pi.map q' = Pi.map' p (fun b => q b â‰« q' b) := by
  ext; simp

lemma Pi.map_comp_map' {f g : Î± â†’ C} {h : Î² â†’ C} [HasProduct f] [HasProduct g] [HasProduct h]
    (p : Î² â†’ Î±) (q : âˆ€ (a : Î±), f a âŸ¶ g a) (q' : âˆ€ (b : Î²), g (p b) âŸ¶ h b) :
    Pi.map q â‰« Pi.map' p q' = Pi.map' p (fun b => q (p b) â‰« q' b) := by
  ext; simp

lemma Pi.map'_eq {f : Î± â†’ C} {g : Î² â†’ C} [HasProduct f] [HasProduct g] {p p' : Î² â†’ Î±}
    {q : âˆ€ (b : Î²), f (p b) âŸ¶ g b} {q' : âˆ€ (b : Î²), f (p' b) âŸ¶ g b} (hp : p = p')
    (hq : âˆ€ (b : Î²), eqToHom (hp â–¸ rfl) â‰« q b = q' b) : Pi.map' p q = Pi.map' p' q' := by
  aesop_cat

/-- Construct an isomorphism between categorical products (indexed by the same type)
from a family of isomorphisms between the factors.
-/
abbrev Pi.mapIso {f g : Î² â†’ C} [HasProductsOfShape Î² C] (p : âˆ€ b, f b â‰… g b) : âˆá¶œ f â‰… âˆá¶œ g :=
  lim.mapIso (Discrete.natIso fun X => p X.as)

instance Pi.map_isIso {f g : Î² â†’ C} [HasProductsOfShape Î² C] (p : âˆ€ b, f b âŸ¶ g b)
    [âˆ€ b, IsIso <| p b] : IsIso <| Pi.map p :=
  inferInstanceAs (IsIso (Pi.mapIso (fun b â†¦ asIso (p b))).hom)

section

/- In this section, we provide some API for products when we are given a functor
`Discrete Î± â¥¤ C` instead of a map `Î± â†’ C`. -/

variable (X : Discrete Î± â¥¤ C) [HasProduct (fun j => X.obj (Discrete.mk j))]

/-- A limit cone for `X : Discrete Î± â¥¤ C` that is given
by `âˆá¶œ (fun j => X.obj (Discrete.mk j))`. -/
@[simps]
def Pi.cone : Cone X where
  pt := âˆá¶œ (fun j => X.obj (Discrete.mk j))
  Ï€ := Discrete.natTrans (fun _ => Pi.Ï€ _ _)

/-- The cone `Pi.cone X` is a limit cone. -/
def productIsProduct' :
    IsLimit (Pi.cone X) where
  lift s := Pi.lift (fun j => s.Ï€.app âŸ¨jâŸ©)
  fac s := by simp
  uniq s m hm := by
    dsimp
    ext
    simp only [limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app]
    apply hm

variable [HasLimit X]

/-- The isomorphism `âˆá¶œ (fun j => X.obj (Discrete.mk j)) â‰… limit X`. -/
def Pi.isoLimit :
    âˆá¶œ (fun j => X.obj (Discrete.mk j)) â‰… limit X :=
  IsLimit.conePointUniqueUpToIso (productIsProduct' X) (limit.isLimit X)

@[reassoc (attr := simp)]
lemma Pi.isoLimit_inv_Ï€ (j : Î±) :
    (Pi.isoLimit X).inv â‰« Pi.Ï€ _ j = limit.Ï€ _ (Discrete.mk j) :=
  IsLimit.conePointUniqueUpToIso_inv_comp _ _ _

@[reassoc (attr := simp)]
lemma Pi.isoLimit_hom_Ï€ (j : Î±) :
    (Pi.isoLimit X).hom â‰« limit.Ï€ _ (Discrete.mk j) = Pi.Ï€ _ j :=
  IsLimit.conePointUniqueUpToIso_hom_comp _ _ _

end

/-- Construct a morphism between categorical coproducts (indexed by the same type)
from a family of morphisms between the factors.
-/
abbrev Sigma.map {f g : Î² â†’ C} [HasCoproduct f] [HasCoproduct g] (p : âˆ€ b, f b âŸ¶ g b) :
    âˆ f âŸ¶ âˆ g :=
  colimMap (Discrete.natTrans fun X => p X.as)

@[simp]
lemma Sigma.map_id {f : Î± â†’ C} [HasCoproduct f] : Sigma.map (fun a => ğŸ™ (f a)) = ğŸ™ (âˆ f) := by
  ext; simp

lemma Sigma.map_comp_map {f g h : Î± â†’ C} [HasCoproduct f] [HasCoproduct g] [HasCoproduct h]
    (q : âˆ€ (a : Î±), f a âŸ¶ g a) (q' : âˆ€ (a : Î±), g a âŸ¶ h a) :
    Sigma.map q â‰« Sigma.map q' = Sigma.map (fun a => q a â‰« q' a) := by
  ext; simp

instance Sigma.map_epi {f g : Î² â†’ C} [HasCoproduct f] [HasCoproduct g] (p : âˆ€ b, f b âŸ¶ g b)
    [âˆ€ i, Epi (p i)] : Epi <| Sigma.map p :=
  @Limits.colimMap_epi _ _ _ _ (Discrete.functor f) (Discrete.functor g) _ _
    (Discrete.natTrans fun X => p X.as) (by dsimp; infer_instance)

/-- Construct a morphism between categorical coproducts from a family of morphisms between the
    factors. -/
def Sigma.map' {f : Î± â†’ C} {g : Î² â†’ C} [HasCoproduct f] [HasCoproduct g] (p : Î± â†’ Î²)
    (q : âˆ€ (a : Î±), f a âŸ¶ g (p a)) : âˆ f âŸ¶ âˆ g :=
  Sigma.desc (fun a => q a â‰« Sigma.Î¹ _ _)

@[reassoc (attr := simp)]
lemma Sigma.Î¹_comp_map' {f : Î± â†’ C} {g : Î² â†’ C} [HasCoproduct f] [HasCoproduct g]
    (p : Î± â†’ Î²) (q : âˆ€ (a : Î±), f a âŸ¶ g (p a)) (a : Î±) :
    Sigma.Î¹ f a â‰« Sigma.map' p q = q a â‰« Sigma.Î¹ g (p a) :=
  colimit.Î¹_desc _ _

lemma Sigma.map'_id_id {f : Î± â†’ C} [HasCoproduct f] :
    Sigma.map' id (fun a => ğŸ™ (f a)) = ğŸ™ (âˆ f) := by
  ext; simp

@[simp]
lemma Sigma.map'_id {f g : Î± â†’ C} [HasCoproduct f] [HasCoproduct g] (p : âˆ€ b, f b âŸ¶ g b) :
    Sigma.map' id p = Sigma.map p :=
  rfl

lemma Sigma.map'_comp_map' {f : Î± â†’ C} {g : Î² â†’ C} {h : Î³ â†’ C} [HasCoproduct f] [HasCoproduct g]
    [HasCoproduct h] (p : Î± â†’ Î²) (p' : Î² â†’ Î³) (q : âˆ€ (a : Î±), f a âŸ¶ g (p a))
    (q' : âˆ€ (b : Î²), g b âŸ¶ h (p' b)) :
    Sigma.map' p q â‰« Sigma.map' p' q' = Sigma.map' (p' âˆ˜ p) (fun a => q a â‰« q' (p a)) := by
  ext; simp

lemma Sigma.map'_comp_map {f : Î± â†’ C} {g h : Î² â†’ C} [HasCoproduct f] [HasCoproduct g]
    [HasCoproduct h] (p : Î± â†’ Î²) (q : âˆ€ (a : Î±), f a âŸ¶ g (p a)) (q' : âˆ€ (b : Î²), g b âŸ¶ h b) :
    Sigma.map' p q â‰« Sigma.map q' = Sigma.map' p (fun a => q a â‰« q' (p a)) := by
  ext; simp

lemma Sigma.map_comp_map' {f g : Î± â†’ C} {h : Î² â†’ C} [HasCoproduct f] [HasCoproduct g]
    [HasCoproduct h] (p : Î± â†’ Î²) (q : âˆ€ (a : Î±), f a âŸ¶ g a) (q' : âˆ€ (a : Î±), g a âŸ¶ h (p a)) :
    Sigma.map q â‰« Sigma.map' p q' = Sigma.map' p (fun a => q a â‰« q' a) := by
  ext; simp

lemma Sigma.map'_eq {f : Î± â†’ C} {g : Î² â†’ C} [HasCoproduct f] [HasCoproduct g]
    {p p' : Î± â†’ Î²} {q : âˆ€ (a : Î±), f a âŸ¶ g (p a)} {q' : âˆ€ (a : Î±), f a âŸ¶ g (p' a)}
    (hp : p = p') (hq : âˆ€ (a : Î±), q a â‰« eqToHom (hp â–¸ rfl) = q' a) :
    Sigma.map' p q = Sigma.map' p' q' := by
  aesop_cat

/-- Construct an isomorphism between categorical coproducts (indexed by the same type)
from a family of isomorphisms between the factors.
-/
abbrev Sigma.mapIso {f g : Î² â†’ C} [HasCoproductsOfShape Î² C] (p : âˆ€ b, f b â‰… g b) : âˆ f â‰… âˆ g :=
  colim.mapIso (Discrete.natIso fun X => p X.as)

instance Sigma.map_isIso {f g : Î² â†’ C} [HasCoproductsOfShape Î² C] (p : âˆ€ b, f b âŸ¶ g b)
    [âˆ€ b, IsIso <| p b] : IsIso (Sigma.map p) :=
  inferInstanceAs (IsIso (Sigma.mapIso (fun b â†¦ asIso (p b))).hom)

section

/- In this section, we provide some API for coproducts when we are given a functor
`Discrete Î± â¥¤ C` instead of a map `Î± â†’ C`. -/

variable (X : Discrete Î± â¥¤ C) [HasCoproduct (fun j => X.obj (Discrete.mk j))]

/-- A colimit cocone for `X : Discrete Î± â¥¤ C` that is given
by `âˆ (fun j => X.obj (Discrete.mk j))`. -/
@[simps]
def Sigma.cocone : Cocone X where
  pt := âˆ (fun j => X.obj (Discrete.mk j))
  Î¹ := Discrete.natTrans (fun _ => Sigma.Î¹ (fun j â†¦ X.obj âŸ¨jâŸ©) _)

/-- The cocone `Sigma.cocone X` is a colimit cocone. -/
def coproductIsCoproduct' :
    IsColimit (Sigma.cocone X) where
  desc s := Sigma.desc (fun j => s.Î¹.app âŸ¨jâŸ©)
  fac s := by simp
  uniq s m hm := by
    dsimp
    ext
    simp only [colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app]
    apply hm

variable [HasColimit X]

/-- The isomorphism `âˆ (fun j => X.obj (Discrete.mk j)) â‰… colimit X`. -/
def Sigma.isoColimit :
    âˆ (fun j => X.obj (Discrete.mk j)) â‰… colimit X :=
  IsColimit.coconePointUniqueUpToIso (coproductIsCoproduct' X) (colimit.isColimit X)

@[reassoc (attr := simp)]
lemma Sigma.Î¹_isoColimit_hom (j : Î±) :
    Sigma.Î¹ _ j â‰« (Sigma.isoColimit X).hom = colimit.Î¹ _ (Discrete.mk j) :=
  IsColimit.comp_coconePointUniqueUpToIso_hom (coproductIsCoproduct' X) _ _

@[reassoc (attr := simp)]
lemma Sigma.Î¹_isoColimit_inv (j : Î±) :
    colimit.Î¹ _ âŸ¨jâŸ© â‰« (Sigma.isoColimit X).inv = Sigma.Î¹ (fun j â†¦ X.obj âŸ¨jâŸ©) _ :=
  IsColimit.comp_coconePointUniqueUpToIso_inv _ _ _

end

/-- Two products which differ by an equivalence in the indexing type,
and up to isomorphism in the factors, are isomorphic.
-/
@[simps]
def Pi.whiskerEquiv {J K : Type*} {f : J â†’ C} {g : K â†’ C} (e : J â‰ƒ K) (w : âˆ€ j, g (e j) â‰… f j)
    [HasProduct f] [HasProduct g] : âˆá¶œ f â‰… âˆá¶œ g where
  hom := Pi.map' e.symm fun k => (w (e.symm k)).inv â‰« eqToHom (by simp)
  inv := Pi.map' e fun j => (w j).hom

/-- Two coproducts which differ by an equivalence in the indexing type,
and up to isomorphism in the factors, are isomorphic.
-/
@[simps]
def Sigma.whiskerEquiv {J K : Type*} {f : J â†’ C} {g : K â†’ C} (e : J â‰ƒ K) (w : âˆ€ j, g (e j) â‰… f j)
    [HasCoproduct f] [HasCoproduct g] : âˆ f â‰… âˆ g where
  hom := Sigma.map' e fun j => (w j).inv
  inv := Sigma.map' e.symm fun k => eqToHom (by simp) â‰« (w (e.symm k)).hom

#adaptation_note /-- nightly-2024-04-01
The last proof was previously by `aesop_cat`. -/
instance {Î¹ : Type*} (f : Î¹ â†’ Type*) (g : (i : Î¹) â†’ (f i) â†’ C)
    [âˆ€ i, HasProduct (g i)] [HasProduct fun i => âˆá¶œ g i] :
    HasProduct fun p : Î£ i, f i => g p.1 p.2 where
  exists_limit := Nonempty.intro
    { cone := Fan.mk (âˆá¶œ fun i => âˆá¶œ g i) (fun X => Pi.Ï€ (fun i => âˆá¶œ g i) X.1 â‰« Pi.Ï€ (g X.1) X.2)
      isLimit := mkFanLimit _ (fun s => Pi.lift fun b => Pi.lift fun c => s.proj âŸ¨b, câŸ©)
        (by simp)
        (by intro s m w; simp only [Fan.mk_pt]; symm; ext i x; simp_all [Sigma.forall]) }

/-- An iterated product is a product over a sigma type. -/
@[simps]
def piPiIso {Î¹ : Type*} (f : Î¹ â†’ Type*) (g : (i : Î¹) â†’ (f i) â†’ C)
    [âˆ€ i, HasProduct (g i)] [HasProduct fun i => âˆá¶œ g i] :
    (âˆá¶œ fun i => âˆá¶œ g i) â‰… (âˆá¶œ fun p : Î£ i, f i => g p.1 p.2) where
  hom := Pi.lift fun âŸ¨i, xâŸ© => Pi.Ï€ _ i â‰« Pi.Ï€ _ x
  inv := Pi.lift fun i => Pi.lift fun x => Pi.Ï€ _ (âŸ¨i, xâŸ© : Î£ i, f i)

#adaptation_note /-- nightly-2024-04-01
The last proof was previously by `aesop_cat`. -/
instance {Î¹ : Type*} (f : Î¹ â†’ Type*) (g : (i : Î¹) â†’ (f i) â†’ C)
    [âˆ€ i, HasCoproduct (g i)] [HasCoproduct fun i => âˆ g i] :
    HasCoproduct fun p : Î£ i, f i => g p.1 p.2 where
  exists_colimit := Nonempty.intro
    { cocone := Cofan.mk (âˆ fun i => âˆ g i)
        (fun X => Sigma.Î¹ (g X.1) X.2 â‰« Sigma.Î¹ (fun i => âˆ g i) X.1)
      isColimit := mkCofanColimit _
        (fun s => Sigma.desc fun b => Sigma.desc fun c => s.inj âŸ¨b, câŸ©)
        (by simp)
        (by intro s m w; simp only [Cofan.mk_pt]; symm; ext i x; simp_all [Sigma.forall]) }

/-- An iterated coproduct is a coproduct over a sigma type. -/
@[simps]
def sigmaSigmaIso {Î¹ : Type*} (f : Î¹ â†’ Type*) (g : (i : Î¹) â†’ (f i) â†’ C)
    [âˆ€ i, HasCoproduct (g i)] [HasCoproduct fun i => âˆ g i] :
    (âˆ fun i => âˆ g i) â‰… (âˆ fun p : Î£ i, f i => g p.1 p.2) where
  hom := Sigma.desc fun i => Sigma.desc fun x => Sigma.Î¹ (fun p : Î£ i, f i => g p.1 p.2) âŸ¨i, xâŸ©
  inv := Sigma.desc fun âŸ¨i, xâŸ© => Sigma.Î¹ (g i) x â‰« Sigma.Î¹ (fun i => âˆ g i) i

section Comparison

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (G : C â¥¤ D)
variable (f : Î² â†’ C)

/-- The comparison morphism for the product of `f`. This is an iso iff `G` preserves the product
of `f`, see `PreservesProduct.ofIsoComparison`. -/
def piComparison [HasProduct f] [HasProduct fun b => G.obj (f b)] :
    G.obj (âˆá¶œ f) âŸ¶ âˆá¶œ fun b => G.obj (f b) :=
  Pi.lift fun b => G.map (Pi.Ï€ f b)

@[reassoc (attr := simp)]
theorem piComparison_comp_Ï€ [HasProduct f] [HasProduct fun b => G.obj (f b)] (b : Î²) :
    piComparison G f â‰« Pi.Ï€ _ b = G.map (Pi.Ï€ f b) :=
  limit.lift_Ï€ _ (Discrete.mk b)

@[reassoc (attr := simp)]
theorem map_lift_piComparison [HasProduct f] [HasProduct fun b => G.obj (f b)] (P : C)
    (g : âˆ€ j, P âŸ¶ f j) : G.map (Pi.lift g) â‰« piComparison G f = Pi.lift fun j => G.map (g j) := by
  ext j
  simp only [Discrete.functor_obj, Category.assoc, piComparison_comp_Ï€, â† G.map_comp,
    limit.lift_Ï€, Fan.mk_pt, Fan.mk_Ï€_app]

/-- The comparison morphism for the coproduct of `f`. This is an iso iff `G` preserves the coproduct
of `f`, see `PreservesCoproduct.ofIsoComparison`. -/
def sigmaComparison [HasCoproduct f] [HasCoproduct fun b => G.obj (f b)] :
    âˆ (fun b => G.obj (f b)) âŸ¶ G.obj (âˆ f) :=
  Sigma.desc fun b => G.map (Sigma.Î¹ f b)

@[reassoc (attr := simp)]
theorem Î¹_comp_sigmaComparison [HasCoproduct f] [HasCoproduct fun b => G.obj (f b)] (b : Î²) :
    Sigma.Î¹ _ b â‰« sigmaComparison G f = G.map (Sigma.Î¹ f b) :=
  colimit.Î¹_desc _ (Discrete.mk b)

@[reassoc (attr := simp)]
theorem sigmaComparison_map_desc [HasCoproduct f] [HasCoproduct fun b => G.obj (f b)] (P : C)
    (g : âˆ€ j, f j âŸ¶ P) :
    sigmaComparison G f â‰« G.map (Sigma.desc g) = Sigma.desc fun j => G.map (g j) := by
  ext j
  simp only [Discrete.functor_obj, Î¹_comp_sigmaComparison_assoc, â† G.map_comp, colimit.Î¹_desc,
    Cofan.mk_pt, Cofan.mk_Î¹_app]

end Comparison

variable (C)

/-- An abbreviation for `Î  J, HasLimitsOfShape (Discrete J) C` -/
abbrev HasProducts :=
  âˆ€ J : Type w, HasLimitsOfShape (Discrete J) C

/-- An abbreviation for `Î  J, HasColimitsOfShape (Discrete J) C` -/
abbrev HasCoproducts :=
  âˆ€ J : Type w, HasColimitsOfShape (Discrete J) C

variable {C}

lemma hasProducts_shrink [HasProducts.{max w w'} C] : HasProducts.{w} C := fun J =>
  hasLimitsOfShape_of_equivalence (Discrete.equivalence Equiv.ulift : Discrete (ULift.{w'} J) â‰Œ _)

lemma hasCoproducts_shrink [HasCoproducts.{max w w'} C] : HasCoproducts.{w} C := fun J =>
  hasColimitsOfShape_of_equivalence (Discrete.equivalence Equiv.ulift : Discrete (ULift.{w'} J) â‰Œ _)

theorem has_smallest_products_of_hasProducts [HasProducts.{w} C] : HasProducts.{0} C :=
  hasProducts_shrink

theorem has_smallest_coproducts_of_hasCoproducts [HasCoproducts.{w} C] : HasCoproducts.{0} C :=
  hasCoproducts_shrink

theorem hasProducts_of_limit_fans (lf : âˆ€ {J : Type w} (f : J â†’ C), Fan f)
    (lf_isLimit : âˆ€ {J : Type w} (f : J â†’ C), IsLimit (lf f)) : HasProducts.{w} C :=
  fun _ : Type w =>
  { has_limit := fun F =>
      HasLimit.mk
        âŸ¨(Cones.postcompose Discrete.natIsoFunctor.inv).obj (lf fun j => F.obj âŸ¨jâŸ©),
          (IsLimit.postcomposeInvEquiv _ _).symm (lf_isLimit _)âŸ© }

instance (priority := 100) hasProductsOfShape_of_hasProducts [HasProducts.{w} C] (J : Type w) :
    HasProductsOfShape J C := inferInstance

instance (priority := 100) hasCoproductsOfShape_of_hasCoproducts [HasCoproducts.{w} C]
    (J : Type w) : HasCoproductsOfShape J C := inferInstance

open Opposite in
/-- The functor sending `(X, n)` to the product of copies of `X` indexed by `n`. -/
@[simps]
def piConst [Limits.HasProducts.{w} C] : C â¥¤ Type wáµ’áµ– â¥¤ C where
  obj X := { obj n := âˆá¶œ fun _ : (unop n) â†¦ X, map f := Limits.Pi.map' f.unop fun _ â†¦ ğŸ™ _ }
  map f := { app n := Limits.Pi.map fun _ â†¦ f }

/-- `n â†¦ âˆâ‚™ X` is left adjoint to `Hom(-, X)`. -/
def piConstAdj [Limits.HasProducts.{v} C] (X : C) :
    (piConst.obj X).rightOp âŠ£ yoneda.obj X where
  unit := { app n i := Limits.Pi.Ï€ (fun _ : n â†¦ X) i }
  counit :=
  { app Y := (Limits.Pi.lift id).op,
    naturality _ _ _ := by apply Quiver.Hom.unop_inj; aesop_cat }
  left_triangle_components _ := by apply Quiver.Hom.unop_inj; aesop_cat

/-- The functor sending `(X, n)` to the coproduct of copies of `X` indexed by `n`. -/
@[simps]
def sigmaConst [Limits.HasCoproducts.{w} C] : C â¥¤ Type w â¥¤ C where
  obj X := { obj n := âˆ fun _ : n â†¦ X, map f := Limits.Sigma.map' f fun _ â†¦ ğŸ™ _ }
  map f := { app n := Limits.Sigma.map fun _ â†¦ f }

/-- `n â†¦ âˆâ‚™ X` is left adjoint to `Hom(X, -)`. -/
def sigmaConstAdj [Limits.HasCoproducts.{v} C] (X : C) :
    sigmaConst.obj X âŠ£ coyoneda.obj (Opposite.op X) where
  unit := { app n i := Limits.Sigma.Î¹ (fun _ : n â†¦ X) i }
  counit := { app Y := Limits.Sigma.desc id }

/-!
(Co)products over a type with a unique term.
-/


section Unique

/-- The limit cone for the product over an index type with exactly one term. -/
@[simps]
def limitConeOfUnique [Unique Î²] (f : Î² â†’ C) : LimitCone (Discrete.functor f) where
  cone :=
    { pt := f default
      Ï€ := Discrete.natTrans (fun âŸ¨jâŸ© => eqToHom (by
        dsimp
        congr
        subsingleton)) }
  isLimit :=
    { lift := fun s => s.Ï€.app default
      fac := fun s j => by
        have h := Subsingleton.elim j default
        subst h
        simp
      uniq := fun s m w => by
        specialize w default
        simpa using w }

instance (priority := 100) hasProduct_unique [Nonempty Î²] [Subsingleton Î²] (f : Î² â†’ C) :
    HasProduct f :=
  let âŸ¨_âŸ© := nonempty_unique Î²; HasLimit.mk (limitConeOfUnique f)

/-- A product over an index type with exactly one term is just the object over that term. -/
@[simps!]
def productUniqueIso [Unique Î²] (f : Î² â†’ C) : âˆá¶œ f â‰… f default :=
  IsLimit.conePointUniqueUpToIso (limit.isLimit _) (limitConeOfUnique f).isLimit

/-- The colimit cocone for the coproduct over an index type with exactly one term. -/
@[simps]
def colimitCoconeOfUnique [Unique Î²] (f : Î² â†’ C) : ColimitCocone (Discrete.functor f) where
  cocone :=
    { pt := f default
      Î¹ := Discrete.natTrans (fun âŸ¨jâŸ© => eqToHom (by
        dsimp
        congr
        subsingleton)) }
  isColimit :=
    { desc := fun s => s.Î¹.app default
      fac := fun s j => by
        have h := Subsingleton.elim j default
        subst h
        apply Category.id_comp
      uniq := fun s m w => by
        specialize w default
        erw [Category.id_comp] at w
        exact w }

instance (priority := 100) hasCoproduct_unique [Nonempty Î²] [Subsingleton Î²] (f : Î² â†’ C) :
    HasCoproduct f :=
  let âŸ¨_âŸ© := nonempty_unique Î²; HasColimit.mk (colimitCoconeOfUnique f)

/-- A coproduct over an index type with exactly one term is just the object over that term. -/
@[simps!]
def coproductUniqueIso [Unique Î²] (f : Î² â†’ C) : âˆ f â‰… f default :=
  IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) (colimitCoconeOfUnique f).isColimit

end Unique

section Reindex

variable {Î³ : Type w'} (Îµ : Î² â‰ƒ Î³) (f : Î³ â†’ C)

section

variable [HasProduct f] [HasProduct (f âˆ˜ Îµ)]

/-- Reindex a categorical product via an equivalence of the index types. -/
def Pi.reindex : piObj (f âˆ˜ Îµ) â‰… piObj f :=
  HasLimit.isoOfEquivalence (Discrete.equivalence Îµ) (Discrete.natIso fun _ => Iso.refl _)

@[reassoc (attr := simp)]
theorem Pi.reindex_hom_Ï€ (b : Î²) : (Pi.reindex Îµ f).hom â‰« Pi.Ï€ f (Îµ b) = Pi.Ï€ (f âˆ˜ Îµ) b := by
  dsimp [Pi.reindex]
  simp only [HasLimit.isoOfEquivalence_hom_Ï€, Discrete.equivalence_inverse, Discrete.functor_obj,
    Function.comp_apply, Functor.id_obj, Discrete.equivalence_functor, Functor.comp_obj,
    Discrete.natIso_inv_app, Iso.refl_inv, Category.id_comp]
  exact limit.w (Discrete.functor (f âˆ˜ Îµ)) (Discrete.eqToHom' (Îµ.symm_apply_apply b))

@[reassoc (attr := simp)]
theorem Pi.reindex_inv_Ï€ (b : Î²) : (Pi.reindex Îµ f).inv â‰« Pi.Ï€ (f âˆ˜ Îµ) b = Pi.Ï€ f (Îµ b) := by
  simp [Iso.inv_comp_eq]

end

section

variable [HasCoproduct f] [HasCoproduct (f âˆ˜ Îµ)]

/-- Reindex a categorical coproduct via an equivalence of the index types. -/
def Sigma.reindex : sigmaObj (f âˆ˜ Îµ) â‰… sigmaObj f :=
  HasColimit.isoOfEquivalence (Discrete.equivalence Îµ) (Discrete.natIso fun _ => Iso.refl _)

@[reassoc (attr := simp)]
theorem Sigma.Î¹_reindex_hom (b : Î²) :
    Sigma.Î¹ (f âˆ˜ Îµ) b â‰« (Sigma.reindex Îµ f).hom = Sigma.Î¹ f (Îµ b) := by
  dsimp [Sigma.reindex]
  simp only [HasColimit.isoOfEquivalence_hom_Ï€, Functor.id_obj, Discrete.functor_obj,
    Function.comp_apply, Discrete.equivalence_functor, Discrete.equivalence_inverse,
    Functor.comp_obj, Discrete.natIso_inv_app, Iso.refl_inv, Category.id_comp]
  have h := colimit.w (Discrete.functor f) (Discrete.eqToHom' (Îµ.apply_symm_apply (Îµ b)))
  simp only [Discrete.functor_obj] at h
  erw [â† h, eqToHom_map, eqToHom_map, eqToHom_trans_assoc]
  all_goals { simp }

@[reassoc (attr := simp)]
theorem Sigma.Î¹_reindex_inv (b : Î²) :
    Sigma.Î¹ f (Îµ b) â‰« (Sigma.reindex Îµ f).inv = Sigma.Î¹ (f âˆ˜ Îµ) b := by simp [Iso.comp_inv_eq]

end

end Reindex

end CategoryTheory.Limits
