/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin
-/
import Mathlib.CategoryTheory.Limits.Shapes.Terminal

#align_import category_theory.limits.shapes.zero_objects from "leanprover-community/mathlib"@"74333bd53d25b6809203a2bfae80eea5fc1fc076"

/-!
# Zero objects

A category "has a zero object" if it has an object which is both initial and terminal. Having a
zero object provides zero morphisms, as the unique morphisms factoring through the zero object;
see `CategoryTheory.Limits.Shapes.ZeroMorphisms`.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/


noncomputable section

universe v u v' u'

open CategoryTheory

open CategoryTheory.Category

variable {C : Type u} [Category.{v} C]
variable {D : Type u'} [Category.{v'} D]

namespace CategoryTheory

namespace Limits

/-- An object `X` in a category is a *zero object* if for every object `Y`
there is a unique morphism `to : X â†’ Y` and a unique morphism `from : Y â†’ X`.

This is a characteristic predicate for `has_zero_object`. -/
structure IsZero (X : C) : Prop where
  /-- there are unique morphisms to the object -/
  unique_to : âˆ€ Y, Nonempty (Unique (X âŸ¶ Y))
  /-- there are unique morphisms from the object -/
  unique_from : âˆ€ Y, Nonempty (Unique (Y âŸ¶ X))
#align category_theory.limits.is_zero CategoryTheory.Limits.IsZero

namespace IsZero

variable {X Y : C}

-- Porting note: `to` is a reserved word, it was replaced by `to_`
/-- If `h : IsZero X`, then `h.to_ Y` is a choice of unique morphism `X â†’ Y`. -/
protected def to_ (h : IsZero X) (Y : C) : X âŸ¶ Y :=
  @default _ <| (h.unique_to Y).some.toInhabited
#align category_theory.limits.is_zero.to CategoryTheory.Limits.IsZero.to_

theorem eq_to (h : IsZero X) (f : X âŸ¶ Y) : f = h.to_ Y :=
  @Unique.eq_default _ (id _) _
#align category_theory.limits.is_zero.eq_to CategoryTheory.Limits.IsZero.eq_to

theorem to_eq (h : IsZero X) (f : X âŸ¶ Y) : h.to_ Y = f :=
  (h.eq_to f).symm
#align category_theory.limits.is_zero.to_eq CategoryTheory.Limits.IsZero.to_eq

-- Porting note: `from` is a reserved word, it was replaced by `from_`
/-- If `h : is_zero X`, then `h.from_ Y` is a choice of unique morphism `Y â†’ X`. -/
protected def from_ (h : IsZero X) (Y : C) : Y âŸ¶ X :=
  @default _ <| (h.unique_from Y).some.toInhabited
#align category_theory.limits.is_zero.from CategoryTheory.Limits.IsZero.from_

theorem eq_from (h : IsZero X) (f : Y âŸ¶ X) : f = h.from_ Y :=
  @Unique.eq_default _ (id _) _
#align category_theory.limits.is_zero.eq_from CategoryTheory.Limits.IsZero.eq_from

theorem from_eq (h : IsZero X) (f : Y âŸ¶ X) : h.from_ Y = f :=
  (h.eq_from f).symm
#align category_theory.limits.is_zero.from_eq CategoryTheory.Limits.IsZero.from_eq

theorem eq_of_src (hX : IsZero X) (f g : X âŸ¶ Y) : f = g :=
  (hX.eq_to f).trans (hX.eq_to g).symm
#align category_theory.limits.is_zero.eq_of_src CategoryTheory.Limits.IsZero.eq_of_src

theorem eq_of_tgt (hX : IsZero X) (f g : Y âŸ¶ X) : f = g :=
  (hX.eq_from f).trans (hX.eq_from g).symm
#align category_theory.limits.is_zero.eq_of_tgt CategoryTheory.Limits.IsZero.eq_of_tgt

/-- Any two zero objects are isomorphic. -/
def iso (hX : IsZero X) (hY : IsZero Y) : X â‰… Y where
  hom := hX.to_ Y
  inv := hX.from_ Y
  hom_inv_id := hX.eq_of_src _ _
  inv_hom_id := hY.eq_of_src _ _
#align category_theory.limits.is_zero.iso CategoryTheory.Limits.IsZero.iso

/-- A zero object is in particular initial. -/
protected def isInitial (hX : IsZero X) : IsInitial X :=
  @IsInitial.ofUnique _ _ X fun Y => (hX.unique_to Y).some
#align category_theory.limits.is_zero.is_initial CategoryTheory.Limits.IsZero.isInitial

/-- A zero object is in particular terminal. -/
protected def isTerminal (hX : IsZero X) : IsTerminal X :=
  @IsTerminal.ofUnique _ _ X fun Y => (hX.unique_from Y).some
#align category_theory.limits.is_zero.is_terminal CategoryTheory.Limits.IsZero.isTerminal

/-- The (unique) isomorphism between any initial object and the zero object. -/
def isoIsInitial (hX : IsZero X) (hY : IsInitial Y) : X â‰… Y :=
  IsInitial.uniqueUpToIso hX.isInitial hY
#align category_theory.limits.is_zero.iso_is_initial CategoryTheory.Limits.IsZero.isoIsInitial

/-- The (unique) isomorphism between any terminal object and the zero object. -/
def isoIsTerminal (hX : IsZero X) (hY : IsTerminal Y) : X â‰… Y :=
  IsTerminal.uniqueUpToIso hX.isTerminal hY
#align category_theory.limits.is_zero.iso_is_terminal CategoryTheory.Limits.IsZero.isoIsTerminal

theorem of_iso (hY : IsZero Y) (e : X â‰… Y) : IsZero X := by
  refine âŸ¨fun Z => âŸ¨âŸ¨âŸ¨e.hom â‰« hY.to_ ZâŸ©, fun f => ?_âŸ©âŸ©,
    fun Z => âŸ¨âŸ¨âŸ¨hY.from_ Z â‰« e.invâŸ©, fun f => ?_âŸ©âŸ©âŸ©
  Â· rw [â† cancel_epi e.inv]
    apply hY.eq_of_src
  Â· rw [â† cancel_mono e.hom]
    apply hY.eq_of_tgt
#align category_theory.limits.is_zero.of_iso CategoryTheory.Limits.IsZero.of_iso

theorem op (h : IsZero X) : IsZero (Opposite.op X) :=
  âŸ¨fun Y => âŸ¨âŸ¨âŸ¨(h.from_ (Opposite.unop Y)).opâŸ©, fun _ => Quiver.Hom.unop_inj (h.eq_of_tgt _ _)âŸ©âŸ©,
    fun Y => âŸ¨âŸ¨âŸ¨(h.to_ (Opposite.unop Y)).opâŸ©, fun _ => Quiver.Hom.unop_inj (h.eq_of_src _ _)âŸ©âŸ©âŸ©
#align category_theory.limits.is_zero.op CategoryTheory.Limits.IsZero.op

theorem unop {X : Cáµ’áµ–} (h : IsZero X) : IsZero (Opposite.unop X) :=
  âŸ¨fun Y => âŸ¨âŸ¨âŸ¨(h.from_ (Opposite.op Y)).unopâŸ©, fun _ => Quiver.Hom.op_inj (h.eq_of_tgt _ _)âŸ©âŸ©,
    fun Y => âŸ¨âŸ¨âŸ¨(h.to_ (Opposite.op Y)).unopâŸ©, fun _ => Quiver.Hom.op_inj (h.eq_of_src _ _)âŸ©âŸ©âŸ©
#align category_theory.limits.is_zero.unop CategoryTheory.Limits.IsZero.unop

end IsZero

end Limits

open CategoryTheory.Limits

theorem Iso.isZero_iff {X Y : C} (e : X â‰… Y) : IsZero X â†” IsZero Y :=
  âŸ¨fun h => h.of_iso e.symm, fun h => h.of_iso eâŸ©
#align category_theory.iso.is_zero_iff CategoryTheory.Iso.isZero_iff

theorem Functor.isZero (F : C â¥¤ D) (hF : âˆ€ X, IsZero (F.obj X)) : IsZero F := by
  constructor <;> intro G <;> refine âŸ¨âŸ¨âŸ¨?_âŸ©, ?_âŸ©âŸ©
  Â· refine
      { app := fun X => (hF _).to_ _
        naturality := ?_ }
    intros
    exact (hF _).eq_of_src _ _
  Â· intro f
    ext
    apply (hF _).eq_of_src _ _
  Â· refine
      { app := fun X => (hF _).from_ _
        naturality := ?_ }
    intros
    exact (hF _).eq_of_tgt _ _
  Â· intro f
    ext
    apply (hF _).eq_of_tgt _ _
#align category_theory.functor.is_zero CategoryTheory.Functor.isZero

namespace Limits

variable (C)

/-- A category "has a zero object" if it has an object which is both initial and terminal. -/
class HasZeroObject : Prop where
  /-- there exists a zero object -/
  zero : âˆƒ X : C, IsZero X
#align category_theory.limits.has_zero_object CategoryTheory.Limits.HasZeroObject

instance hasZeroObject_pUnit : HasZeroObject (Discrete PUnit) where zero :=
  âŸ¨âŸ¨âŸ¨âŸ©âŸ©,
    { unique_to := fun âŸ¨âŸ¨âŸ©âŸ© =>
      âŸ¨{ default := ðŸ™ _,
          uniq := fun _ => Subsingleton.elim _ _ }âŸ©
      unique_from := fun âŸ¨âŸ¨âŸ©âŸ© =>
      âŸ¨{ default := ðŸ™ _,
          uniq := fun _ => Subsingleton.elim _ _ }âŸ©}âŸ©
#align category_theory.limits.has_zero_object_punit CategoryTheory.Limits.hasZeroObject_pUnit

section

variable [HasZeroObject C]

/-- Construct a `Zero C` for a category with a zero object.
This can not be a global instance as it will trigger for every `Zero C` typeclass search.
-/
protected def HasZeroObject.zero' : Zero C where zero := HasZeroObject.zero.choose
#align category_theory.limits.has_zero_object.has_zero CategoryTheory.Limits.HasZeroObject.zero'

scoped[ZeroObject] attribute [instance] CategoryTheory.Limits.HasZeroObject.zero'

open ZeroObject

theorem isZero_zero : IsZero (0 : C) :=
  HasZeroObject.zero.choose_spec
#align category_theory.limits.is_zero_zero CategoryTheory.Limits.isZero_zero

instance hasZeroObject_op : HasZeroObject Cáµ’áµ– :=
  âŸ¨âŸ¨Opposite.op 0, IsZero.op (isZero_zero C)âŸ©âŸ©
#align category_theory.limits.has_zero_object_op CategoryTheory.Limits.hasZeroObject_op

end

open ZeroObject

theorem hasZeroObject_unop [HasZeroObject Cáµ’áµ–] : HasZeroObject C :=
  âŸ¨âŸ¨Opposite.unop 0, IsZero.unop (isZero_zero Cáµ’áµ–)âŸ©âŸ©
#align category_theory.limits.has_zero_object_unop CategoryTheory.Limits.hasZeroObject_unop

variable {C}

theorem IsZero.hasZeroObject {X : C} (hX : IsZero X) : HasZeroObject C :=
  âŸ¨âŸ¨X, hXâŸ©âŸ©
#align category_theory.limits.is_zero.has_zero_object CategoryTheory.Limits.IsZero.hasZeroObject

/-- Every zero object is isomorphic to *the* zero object. -/
def IsZero.isoZero [HasZeroObject C] {X : C} (hX : IsZero X) : X â‰… 0 :=
  hX.iso (isZero_zero C)
#align category_theory.limits.is_zero.iso_zero CategoryTheory.Limits.IsZero.isoZero

theorem IsZero.obj [HasZeroObject D] {F : C â¥¤ D} (hF : IsZero F) (X : C) : IsZero (F.obj X) := by
  let G : C â¥¤ D := (CategoryTheory.Functor.const C).obj 0
  have hG : IsZero G := Functor.isZero _ fun _ => isZero_zero _
  let e : F â‰… G := hF.iso hG
  exact (isZero_zero _).of_iso (e.app X)
#align category_theory.limits.is_zero.obj CategoryTheory.Limits.IsZero.obj

namespace HasZeroObject

variable [HasZeroObject C]

/-- There is a unique morphism from the zero object to any object `X`. -/
protected def uniqueTo (X : C) : Unique (0 âŸ¶ X) :=
  ((isZero_zero C).unique_to X).some
#align category_theory.limits.has_zero_object.unique_to CategoryTheory.Limits.HasZeroObject.uniqueTo

/-- There is a unique morphism from any object `X` to the zero object. -/
protected def uniqueFrom (X : C) : Unique (X âŸ¶ 0) :=
  ((isZero_zero C).unique_from X).some
#align category_theory.limits.has_zero_object.unique_from CategoryTheory.Limits.HasZeroObject.uniqueFrom

scoped[ZeroObject] attribute [instance] CategoryTheory.Limits.HasZeroObject.uniqueTo

scoped[ZeroObject] attribute [instance] CategoryTheory.Limits.HasZeroObject.uniqueFrom

@[ext]
theorem to_zero_ext {X : C} (f g : X âŸ¶ 0) : f = g :=
  (isZero_zero C).eq_of_tgt _ _
#align category_theory.limits.has_zero_object.to_zero_ext CategoryTheory.Limits.HasZeroObject.to_zero_ext

@[ext]
theorem from_zero_ext {X : C} (f g : 0 âŸ¶ X) : f = g :=
  (isZero_zero C).eq_of_src _ _
#align category_theory.limits.has_zero_object.from_zero_ext CategoryTheory.Limits.HasZeroObject.from_zero_ext

instance (X : C) : Subsingleton (X â‰… 0) := âŸ¨fun f g => by extâŸ©

instance {X : C} (f : 0 âŸ¶ X) : Mono f where right_cancellation g h _ := by ext

instance {X : C} (f : X âŸ¶ 0) : Epi f where left_cancellation g h _ := by ext

instance zero_to_zero_isIso (f : (0 : C) âŸ¶ 0) : IsIso f := by
  convert show IsIso (ðŸ™ (0 : C)) by infer_instance
  apply Subsingleton.elim
#align category_theory.limits.has_zero_object.zero_to_zero_is_iso CategoryTheory.Limits.HasZeroObject.zero_to_zero_isIso

/-- A zero object is in particular initial. -/
def zeroIsInitial : IsInitial (0 : C) :=
  (isZero_zero C).isInitial
#align category_theory.limits.has_zero_object.zero_is_initial CategoryTheory.Limits.HasZeroObject.zeroIsInitial

/-- A zero object is in particular terminal. -/
def zeroIsTerminal : IsTerminal (0 : C) :=
  (isZero_zero C).isTerminal
#align category_theory.limits.has_zero_object.zero_is_terminal CategoryTheory.Limits.HasZeroObject.zeroIsTerminal

/-- A zero object is in particular initial. -/
instance (priority := 10) hasInitial : HasInitial C :=
  hasInitial_of_unique 0
#align category_theory.limits.has_zero_object.has_initial CategoryTheory.Limits.HasZeroObject.hasInitial

/-- A zero object is in particular terminal. -/
instance (priority := 10) hasTerminal : HasTerminal C :=
  hasTerminal_of_unique 0
#align category_theory.limits.has_zero_object.has_terminal CategoryTheory.Limits.HasZeroObject.hasTerminal

/-- The (unique) isomorphism between any initial object and the zero object. -/
def zeroIsoIsInitial {X : C} (t : IsInitial X) : 0 â‰… X :=
  zeroIsInitial.uniqueUpToIso t
#align category_theory.limits.has_zero_object.zero_iso_is_initial CategoryTheory.Limits.HasZeroObject.zeroIsoIsInitial

/-- The (unique) isomorphism between any terminal object and the zero object. -/
def zeroIsoIsTerminal {X : C} (t : IsTerminal X) : 0 â‰… X :=
  zeroIsTerminal.uniqueUpToIso t
#align category_theory.limits.has_zero_object.zero_iso_is_terminal CategoryTheory.Limits.HasZeroObject.zeroIsoIsTerminal

/-- The (unique) isomorphism between the chosen initial object and the chosen zero object. -/
def zeroIsoInitial [HasInitial C] : 0 â‰… âŠ¥_ C :=
  zeroIsInitial.uniqueUpToIso initialIsInitial
#align category_theory.limits.has_zero_object.zero_iso_initial CategoryTheory.Limits.HasZeroObject.zeroIsoInitial

/-- The (unique) isomorphism between the chosen terminal object and the chosen zero object. -/
def zeroIsoTerminal [HasTerminal C] : 0 â‰… âŠ¤_ C :=
  zeroIsTerminal.uniqueUpToIso terminalIsTerminal
#align category_theory.limits.has_zero_object.zero_iso_terminal CategoryTheory.Limits.HasZeroObject.zeroIsoTerminal

instance (priority := 100) initialMonoClass : InitialMonoClass C :=
  InitialMonoClass.of_isInitial zeroIsInitial fun X => by infer_instance
#align category_theory.limits.has_zero_object.has_strict_initial CategoryTheory.Limits.HasZeroObject.initialMonoClass

end HasZeroObject

end Limits

open CategoryTheory.Limits

open ZeroObject

theorem Functor.isZero_iff [HasZeroObject D] (F : C â¥¤ D) : IsZero F â†” âˆ€ X, IsZero (F.obj X) :=
  âŸ¨fun hF X => hF.obj X, Functor.isZero _âŸ©
#align category_theory.functor.is_zero_iff CategoryTheory.Functor.isZero_iff

end CategoryTheory
