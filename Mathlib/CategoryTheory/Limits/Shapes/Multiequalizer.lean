/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.ConeCategory

/-!

# Multi-(co)equalizers

A *multiequalizer* is an equalizer of two morphisms between two products.
Since both products and equalizers are limits, such an object is again a limit.
This file provides the diagram whose limit is indeed such an object.
In fact, it is well-known that any limit can be obtained as a multiequalizer.
The dual construction (multicoequalizers) is also provided.

## Projects

Prove that a multiequalizer can be identified with
an equalizer between products (and analogously for multicoequalizers).

Prove that the limit of any diagram is a multiequalizer (and similarly for colimits).

-/


namespace CategoryTheory.Limits

open CategoryTheory

universe w w' v u

/-- The shape of a multiequalizer diagram. It involves two types `L` and `R`,
and two maps `R â†’ L`. -/
@[nolint checkUnivs]
structure MulticospanShape where
  /-- the left type -/
  L : Type w
  /-- the right type -/
  R : Type w'
  /-- the first map `R â†’ L` -/
  fst : R â†’ L
  /-- the second map `R â†’ L` -/
  snd : R â†’ L

/-- Given a type `Î¹`, this is the shape of multiequalizer diagrams corresponding
to situations where we want to equalize two families of maps `U i âŸ¶ V âŸ¨i, jâŸ©`
and `U j âŸ¶ V âŸ¨i, jâŸ©` with `i : Î¹` and `j : Î¹`. -/
@[simps]
def MulticospanShape.prod (Î¹ : Type w) : MulticospanShape where
  L := Î¹
  R := Î¹ Ã— Î¹
  fst := _root_.Prod.fst
  snd := _root_.Prod.snd

/-- The shape of a multicoequalizer diagram. It involves two types `L` and `R`,
and two maps `L â†’ R`. -/
@[nolint checkUnivs]
structure MultispanShape where
  /-- the left type -/
  L : Type w
  /-- the right type -/
  R : Type w'
  /-- the first map `L â†’ R` -/
  fst : L â†’ R
  /-- the second map `L â†’ R` -/
  snd : L â†’ R

/-- Given a type `Î¹`, this is the shape of multicoequalizer diagrams corresponding
to situations where we want to coequalize two families of maps `V âŸ¨i, jâŸ© âŸ¶ U i`
and `V âŸ¨i, jâŸ© âŸ¶ U j` with `i : Î¹` and `j : Î¹`. -/
@[simps]
def MultispanShape.prod (Î¹ : Type w) : MultispanShape where
  L := Î¹ Ã— Î¹
  R := Î¹
  fst := _root_.Prod.fst
  snd := _root_.Prod.snd

/-- The type underlying the multiequalizer diagram. -/
inductive WalkingMulticospan (J : MulticospanShape.{w, w'}) : Type max w w'
  | left : J.L â†’ WalkingMulticospan J
  | right : J.R â†’ WalkingMulticospan J

/-- The type underlying the multiecoqualizer diagram. -/
inductive WalkingMultispan (J : MultispanShape.{w, w'}) : Type max w w'
  | left : J.L â†’ WalkingMultispan J
  | right : J.R â†’ WalkingMultispan J

namespace WalkingMulticospan

variable {J : MulticospanShape.{w, w'}}

instance [Inhabited J.L] : Inhabited (WalkingMulticospan J) :=
  âŸ¨left defaultâŸ©

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- Morphisms for `WalkingMulticospan`. -/
inductive Hom : âˆ€ _ _ : WalkingMulticospan J, Type max w w'
  | id (A) : Hom A A
  | fst (b) : Hom (left (J.fst b)) (right b)
  | snd (b) : Hom (left (J.snd b)) (right b)

instance {a : WalkingMulticospan J} : Inhabited (Hom a a) :=
  âŸ¨Hom.id _âŸ©

/-- Composition of morphisms for `WalkingMulticospan`. -/
def Hom.comp : âˆ€ {A B C : WalkingMulticospan J} (_ : Hom A B) (_ : Hom B C), Hom A C
  | _, _, _, Hom.id X, f => f
  | _, _, _, Hom.fst b, Hom.id _ => Hom.fst b
  | _, _, _, Hom.snd b, Hom.id _ => Hom.snd b

instance : SmallCategory (WalkingMulticospan J) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  id_comp := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  comp_id := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  assoc := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) (_ | _ | _) <;> rfl

@[simp]
lemma Hom.id_eq_id (X : WalkingMulticospan J) :
    Hom.id X = ğŸ™ X := rfl

@[simp]
lemma Hom.comp_eq_comp {X Y Z : WalkingMulticospan J}
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : Hom.comp f g = f â‰« g := rfl

end WalkingMulticospan

namespace WalkingMultispan

variable {J : MultispanShape.{w, w'}}

instance [Inhabited J.L] : Inhabited (WalkingMultispan J) :=
  âŸ¨left defaultâŸ©

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- Morphisms for `WalkingMultispan`. -/
inductive Hom : âˆ€ _ _ : WalkingMultispan J, Type max w w'
  | id (A) : Hom A A
  | fst (a) : Hom (left a) (right (J.fst a))
  | snd (a) : Hom (left a) (right (J.snd a))

instance {a : WalkingMultispan J} : Inhabited (Hom a a) :=
  âŸ¨Hom.id _âŸ©

/-- Composition of morphisms for `WalkingMultispan`. -/
def Hom.comp : âˆ€ {A B C : WalkingMultispan J} (_ : Hom A B) (_ : Hom B C), Hom A C
  | _, _, _, Hom.id X, f => f
  | _, _, _, Hom.fst a, Hom.id _ => Hom.fst a
  | _, _, _, Hom.snd a, Hom.id _ => Hom.snd a

instance : SmallCategory (WalkingMultispan J) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  id_comp := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  comp_id := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  assoc := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) (_ | _ | _) <;> rfl

@[simp]
lemma Hom.id_eq_id (X : WalkingMultispan J) : Hom.id X = ğŸ™ X := rfl

@[simp]
lemma Hom.comp_eq_comp {X Y Z : WalkingMultispan J}
    (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : Hom.comp f g = f â‰« g := rfl

end WalkingMultispan

/-- This is a structure encapsulating the data necessary to define a `Multicospan`. -/
@[nolint checkUnivs]
structure MulticospanIndex (J : MulticospanShape.{w, w'})
    (C : Type u) [Category.{v} C] where
  /-- Left map, from `J.L` to `C` -/
  left : J.L â†’ C
  /-- Right map, from `J.R` to `C` -/
  right : J.R â†’ C
  /-- A family of maps from `left (J.fst b)` to `right b` -/
  fst : âˆ€ b, left (J.fst b) âŸ¶ right b
  /-- A family of maps from `left (J.snd b)` to `right b` -/
  snd : âˆ€ b, left (J.snd b) âŸ¶ right b

/-- This is a structure encapsulating the data necessary to define a `Multispan`. -/
@[nolint checkUnivs]
structure MultispanIndex (J : MultispanShape.{w, w'})
    (C : Type u) [Category.{v} C] where
  /-- Left map, from `J.L` to `C` -/
  left : J.L â†’ C
  /-- Right map, from `J.R` to `C` -/
  right : J.R â†’ C
  /-- A family of maps from `left a` to `right (J.fst a)` -/
  fst : âˆ€ a, left a âŸ¶ right (J.fst a)
  /-- A family of maps from `left a` to `right (J.snd a)` -/
  snd : âˆ€ a, left a âŸ¶ right (J.snd a)

namespace MulticospanIndex

variable {C : Type u} [Category.{v} C] {J : MulticospanShape.{w, w'}}
  (I : MulticospanIndex J C)

/-- The multicospan associated to `I : MulticospanIndex`. -/
@[simps]
def multicospan : WalkingMulticospan J â¥¤ C where
  obj x :=
    match x with
    | WalkingMulticospan.left a => I.left a
    | WalkingMulticospan.right b => I.right b
  map {x y} f :=
    match x, y, f with
    | _, _, WalkingMulticospan.Hom.id x => ğŸ™ _
    | _, _, WalkingMulticospan.Hom.fst b => I.fst _
    | _, _, WalkingMulticospan.Hom.snd b => I.snd _
  map_id := by
    rintro (_ | _) <;> rfl
  map_comp := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) <;> aesop_cat

variable [HasProduct I.left] [HasProduct I.right]

/-- The induced map `âˆá¶œ I.left âŸ¶ âˆá¶œ I.right` via `I.fst`. -/
noncomputable def fstPiMap : âˆá¶œ I.left âŸ¶ âˆá¶œ I.right :=
  Pi.lift fun b => Pi.Ï€ I.left (J.fst b) â‰« I.fst b

/-- The induced map `âˆá¶œ I.left âŸ¶ âˆá¶œ I.right` via `I.snd`. -/
noncomputable def sndPiMap : âˆá¶œ I.left âŸ¶ âˆá¶œ I.right :=
  Pi.lift fun b => Pi.Ï€ I.left (J.snd b) â‰« I.snd b

@[reassoc (attr := simp)]
theorem fstPiMap_Ï€ (b) : I.fstPiMap â‰« Pi.Ï€ I.right b = Pi.Ï€ I.left _ â‰« I.fst b := by
  simp [fstPiMap]

@[reassoc (attr := simp)]
theorem sndPiMap_Ï€ (b) : I.sndPiMap â‰« Pi.Ï€ I.right b = Pi.Ï€ I.left _ â‰« I.snd b := by
  simp [sndPiMap]

/-- Taking the multiequalizer over the multicospan index is equivalent to taking the equalizer over
the two morphisms `âˆá¶œ I.left â‡‰ âˆá¶œ I.right`. This is the diagram of the latter.
-/
@[simps!]
protected noncomputable def parallelPairDiagram :=
  parallelPair I.fstPiMap I.sndPiMap

end MulticospanIndex

namespace MultispanIndex

variable {C : Type u} [Category.{v} C] {J : MultispanShape.{w, w'}}
    (I : MultispanIndex J C)

/-- The multispan associated to `I : MultispanIndex`. -/
def multispan : WalkingMultispan J â¥¤ C where
  obj x :=
    match x with
    | WalkingMultispan.left a => I.left a
    | WalkingMultispan.right b => I.right b
  map {x y} f :=
    match x, y, f with
    | _, _, WalkingMultispan.Hom.id x => ğŸ™ _
    | _, _, WalkingMultispan.Hom.fst b => I.fst _
    | _, _, WalkingMultispan.Hom.snd b => I.snd _
  map_id := by
    rintro (_ | _) <;> rfl
  map_comp := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) <;> aesop_cat

@[simp]
theorem multispan_obj_left (a) : I.multispan.obj (WalkingMultispan.left a) = I.left a :=
  rfl

@[simp]
theorem multispan_obj_right (b) : I.multispan.obj (WalkingMultispan.right b) = I.right b :=
  rfl

@[simp]
theorem multispan_map_fst (a) : I.multispan.map (WalkingMultispan.Hom.fst a) = I.fst a :=
  rfl

@[simp]
theorem multispan_map_snd (a) : I.multispan.map (WalkingMultispan.Hom.snd a) = I.snd a :=
  rfl

variable [HasCoproduct I.left] [HasCoproduct I.right]

/-- The induced map `âˆ I.left âŸ¶ âˆ I.right` via `I.fst`. -/
noncomputable def fstSigmaMap : âˆ I.left âŸ¶ âˆ I.right :=
  Sigma.desc fun b => I.fst b â‰« Sigma.Î¹ _ (J.fst b)

/-- The induced map `âˆ I.left âŸ¶ âˆ I.right` via `I.snd`. -/
noncomputable def sndSigmaMap : âˆ I.left âŸ¶ âˆ I.right :=
  Sigma.desc fun b => I.snd b â‰« Sigma.Î¹ _ (J.snd b)

@[reassoc (attr := simp)]
theorem Î¹_fstSigmaMap (b) : Sigma.Î¹ I.left b â‰« I.fstSigmaMap = I.fst b â‰« Sigma.Î¹ I.right _ := by
  simp [fstSigmaMap]

@[reassoc (attr := simp)]
theorem Î¹_sndSigmaMap (b) : Sigma.Î¹ I.left b â‰« I.sndSigmaMap = I.snd b â‰« Sigma.Î¹ I.right _ := by
  simp [sndSigmaMap]

/--
Taking the multicoequalizer over the multispan index is equivalent to taking the coequalizer over
the two morphsims `âˆ I.left â‡‰ âˆ I.right`. This is the diagram of the latter.
-/
protected noncomputable abbrev parallelPairDiagram :=
  parallelPair I.fstSigmaMap I.sndSigmaMap

end MultispanIndex

variable {C : Type u} [Category.{v} C]

/-- A multifork is a cone over a multicospan. -/
abbrev Multifork {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C) :=
  Cone I.multicospan

/-- A multicofork is a cocone over a multispan. -/
abbrev Multicofork {J : MultispanShape.{w, w'}} (I : MultispanIndex J C) :=
  Cocone I.multispan

namespace Multifork

variable {J : MulticospanShape.{w, w'}} {I : MulticospanIndex J C} (K : Multifork I)

/-- The maps from the cone point of a multifork to the objects on the left. -/
def Î¹ (a : J.L) : K.pt âŸ¶ I.left a :=
  K.Ï€.app (WalkingMulticospan.left _)

@[simp]
theorem app_left_eq_Î¹ (a) : K.Ï€.app (WalkingMulticospan.left a) = K.Î¹ a :=
  rfl

@[simp]
theorem app_right_eq_Î¹_comp_fst (b) :
    K.Ï€.app (WalkingMulticospan.right b) = K.Î¹ (J.fst b) â‰« I.fst b := by
  rw [â† K.w (WalkingMulticospan.Hom.fst b)]
  rfl

@[reassoc]
theorem app_right_eq_Î¹_comp_snd (b) :
    K.Ï€.app (WalkingMulticospan.right b) = K.Î¹ (J.snd b) â‰« I.snd b := by
  rw [â† K.w (WalkingMulticospan.Hom.snd b)]
  rfl

@[reassoc (attr := simp)]
theorem hom_comp_Î¹ (Kâ‚ Kâ‚‚ : Multifork I) (f : Kâ‚ âŸ¶ Kâ‚‚) (j : J.L) : f.hom â‰« Kâ‚‚.Î¹ j = Kâ‚.Î¹ j :=
  f.w _

/-- Construct a multifork using a collection `Î¹` of morphisms. -/
@[simps]
def ofÎ¹ {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
    (P : C) (Î¹ : âˆ€ a, P âŸ¶ I.left a)
    (w : âˆ€ b, Î¹ (J.fst b) â‰« I.fst b = Î¹ (J.snd b) â‰« I.snd b) : Multifork I where
  pt := P
  Ï€ :=
    { app := fun x =>
        match x with
        | WalkingMulticospan.left _ => Î¹ _
        | WalkingMulticospan.right b => Î¹ (J.fst b) â‰« I.fst b
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;>
          dsimp <;> simp only [Category.id_comp, Category.comp_id]
        apply w }

@[reassoc (attr := simp)]
theorem condition (b) : K.Î¹ (J.fst b) â‰« I.fst b = K.Î¹ (J.snd b) â‰« I.snd b := by
  rw [â† app_right_eq_Î¹_comp_fst, â† app_right_eq_Î¹_comp_snd]

/-- This definition provides a convenient way to show that a multifork is a limit. -/
@[simps]
def IsLimit.mk (lift : âˆ€ E : Multifork I, E.pt âŸ¶ K.pt)
    (fac : âˆ€ (E : Multifork I) (i : J.L), lift E â‰« K.Î¹ i = E.Î¹ i)
    (uniq : âˆ€ (E : Multifork I) (m : E.pt âŸ¶ K.pt), (âˆ€ i : J.L, m â‰« K.Î¹ i = E.Î¹ i) â†’ m = lift E) :
    IsLimit K :=
  { lift
    fac := by
      rintro E (a | b)
      Â· apply fac
      Â· rw [â† E.w (WalkingMulticospan.Hom.fst b), â† K.w (WalkingMulticospan.Hom.fst b), â†
          Category.assoc]
        congr 1
        apply fac
    uniq := by
      rintro E m hm
      apply uniq
      intro i
      apply hm }

variable {K}

lemma IsLimit.hom_ext (hK : IsLimit K) {T : C} {f g : T âŸ¶ K.pt}
    (h : âˆ€ a, f â‰« K.Î¹ a = g â‰« K.Î¹ a) : f = g := by
  apply hK.hom_ext
  rintro (_|b)
  Â· apply h
  Â· dsimp
    rw [app_right_eq_Î¹_comp_fst, reassoc_of% h]

/-- Constructor for morphisms to the point of a limit multifork. -/
def IsLimit.lift (hK : IsLimit K) {T : C} (k : âˆ€ a, T âŸ¶ I.left a)
    (hk : âˆ€ b, k (J.fst b) â‰« I.fst b = k (J.snd b) â‰« I.snd b) :
    T âŸ¶ K.pt :=
  hK.lift (Multifork.ofÎ¹ _ _ k hk)

@[reassoc (attr := simp)]
lemma IsLimit.fac (hK : IsLimit K) {T : C} (k : âˆ€ a, T âŸ¶ I.left a)
    (hk : âˆ€ b, k (J.fst b) â‰« I.fst b = k (J.snd b) â‰« I.snd b) (a : J.L) :
    IsLimit.lift hK k hk â‰« K.Î¹ a = k a :=
  hK.fac _ _

variable (K)

variable [HasProduct I.left] [HasProduct I.right]

@[reassoc (attr := simp)]
theorem pi_condition : Pi.lift K.Î¹ â‰« I.fstPiMap = Pi.lift K.Î¹ â‰« I.sndPiMap := by
  ext
  simp

/-- Given a multifork, we may obtain a fork over `âˆá¶œ I.left â‡‰ âˆá¶œ I.right`. -/
@[simps pt]
noncomputable def toPiFork (K : Multifork I) : Fork I.fstPiMap I.sndPiMap where
  pt := K.pt
  Ï€ :=
    { app := fun x =>
        match x with
        | WalkingParallelPair.zero => Pi.lift K.Î¹
        | WalkingParallelPair.one => Pi.lift K.Î¹ â‰« I.fstPiMap
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;>
          dsimp <;>
          simp only [Category.id_comp, Functor.map_id, parallelPair_obj_zero, Category.comp_id,
            pi_condition, parallelPair_obj_one] }

@[simp]
theorem toPiFork_Ï€_app_zero : K.toPiFork.Î¹ = Pi.lift K.Î¹ :=
  rfl

@[simp]
theorem toPiFork_Ï€_app_one : K.toPiFork.Ï€.app WalkingParallelPair.one = Pi.lift K.Î¹ â‰« I.fstPiMap :=
  rfl

variable (I)

/-- Given a fork over `âˆá¶œ I.left â‡‰ âˆá¶œ I.right`, we may obtain a multifork. -/
@[simps pt]
noncomputable def ofPiFork (c : Fork I.fstPiMap I.sndPiMap) : Multifork I where
  pt := c.pt
  Ï€ :=
    { app := fun x =>
        match x with
        | WalkingMulticospan.left _ => c.Î¹ â‰« Pi.Ï€ _ _
        | WalkingMulticospan.right _ => c.Î¹ â‰« I.fstPiMap â‰« Pi.Ï€ _ _
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _)
        Â· simp
        Â· simp
        Â· dsimp; rw [c.condition_assoc]; simp
        Â· simp }

@[simp]
theorem ofPiFork_Ï€_app_left (c : Fork I.fstPiMap I.sndPiMap) (a) :
    (ofPiFork I c).Î¹ a = c.Î¹ â‰« Pi.Ï€ _ _ :=
  rfl

@[simp]
theorem ofPiFork_Ï€_app_right (c : Fork I.fstPiMap I.sndPiMap) (a) :
    (ofPiFork I c).Ï€.app (WalkingMulticospan.right a) = c.Î¹ â‰« I.fstPiMap â‰« Pi.Ï€ _ _ :=
  rfl

end Multifork

namespace MulticospanIndex

variable {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
    [HasProduct I.left] [HasProduct I.right]

/-- `Multifork.toPiFork` as a functor. -/
@[simps]
noncomputable def toPiForkFunctor : Multifork I â¥¤ Fork I.fstPiMap I.sndPiMap where
  obj := Multifork.toPiFork
  map {Kâ‚ Kâ‚‚} f :=
    { hom := f.hom
      w := by
        rintro (_ | _)
        Â· apply limit.hom_ext
          simp
        Â· apply limit.hom_ext
          intros j
          simp only [Multifork.toPiFork_Ï€_app_one, Multifork.pi_condition, Category.assoc]
          dsimp [sndPiMap]
          simp }

/-- `Multifork.ofPiFork` as a functor. -/
@[simps]
noncomputable def ofPiForkFunctor : Fork I.fstPiMap I.sndPiMap â¥¤ Multifork I where
  obj := Multifork.ofPiFork I
  map {Kâ‚ Kâ‚‚} f :=
    { hom := f.hom
      w := by rintro (_ | _) <;> simp }

/-- The category of multiforks is equivalent to the category of forks over `âˆá¶œ I.left â‡‰ âˆá¶œ I.right`.
It then follows from `CategoryTheory.IsLimit.ofPreservesConeTerminal` (or `reflects`) that it
preserves and reflects limit cones.
-/
@[simps]
noncomputable def multiforkEquivPiFork : Multifork I â‰Œ Fork I.fstPiMap I.sndPiMap where
  functor := toPiForkFunctor I
  inverse := ofPiForkFunctor I
  unitIso :=
    NatIso.ofComponents fun K =>
      Cones.ext (Iso.refl _) (by
        rintro (_ | _) <;> simp [â† Fork.app_one_eq_Î¹_comp_left])
  counitIso :=
    NatIso.ofComponents fun K => Fork.ext (Iso.refl _)

end MulticospanIndex

namespace Multicofork

variable {J : MultispanShape.{w, w'}} {I : MultispanIndex J C} (K : Multicofork I)

/-- The maps to the cocone point of a multicofork from the objects on the right. -/
def Ï€ (b : J.R) : I.right b âŸ¶ K.pt :=
  K.Î¹.app (WalkingMultispan.right _)

@[simp]
theorem Ï€_eq_app_right (b) : K.Î¹.app (WalkingMultispan.right _) = K.Ï€ b :=
  rfl

@[simp]
theorem fst_app_right (a) : K.Î¹.app (WalkingMultispan.left a) = I.fst a â‰« K.Ï€ _ := by
  rw [â† K.w (WalkingMultispan.Hom.fst a)]
  rfl

@[reassoc]
theorem snd_app_right (a) : K.Î¹.app (WalkingMultispan.left a) = I.snd a â‰« K.Ï€ _ := by
  rw [â† K.w (WalkingMultispan.Hom.snd a)]
  rfl

@[reassoc (attr := simp)]
lemma Ï€_comp_hom (Kâ‚ Kâ‚‚ : Multicofork I) (f : Kâ‚ âŸ¶ Kâ‚‚) (b : J.R) : Kâ‚.Ï€ b â‰« f.hom = Kâ‚‚.Ï€ b :=
  f.w _

/-- Construct a multicofork using a collection `Ï€` of morphisms. -/
@[simps]
def ofÏ€ {J : MultispanShape.{w, w'}} (I : MultispanIndex J C)
    (P : C) (Ï€ : âˆ€ b, I.right b âŸ¶ P)
    (w : âˆ€ a, I.fst a â‰« Ï€ (J.fst a) = I.snd a â‰« Ï€ (J.snd a)) : Multicofork I where
  pt := P
  Î¹ :=
    { app := fun x =>
        match x with
        | WalkingMultispan.left a => I.fst a â‰« Ï€ _
        | WalkingMultispan.right _ => Ï€ _
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;> dsimp <;>
          simp only [Functor.map_id, MultispanIndex.multispan_obj_left,
            Category.id_comp, Category.comp_id, MultispanIndex.multispan_obj_right]
        symm
        apply w }

@[reassoc (attr := simp)]
theorem condition (a) : I.fst a â‰« K.Ï€ (J.fst a) = I.snd a â‰« K.Ï€ (J.snd a) := by
  rw [â† K.snd_app_right, â† K.fst_app_right]

/-- This definition provides a convenient way to show that a multicofork is a colimit. -/
@[simps]
def IsColimit.mk (desc : âˆ€ E : Multicofork I, K.pt âŸ¶ E.pt)
    (fac : âˆ€ (E : Multicofork I) (i : J.R), K.Ï€ i â‰« desc E = E.Ï€ i)
    (uniq : âˆ€ (E : Multicofork I) (m : K.pt âŸ¶ E.pt), (âˆ€ i : J.R, K.Ï€ i â‰« m = E.Ï€ i) â†’ m = desc E) :
    IsColimit K :=
  { desc
    fac := by
      rintro S (a | b)
      Â· rw [â† K.w (WalkingMultispan.Hom.fst a), â† S.w (WalkingMultispan.Hom.fst a),
          Category.assoc]
        congr 1
        apply fac
      Â· apply fac
    uniq := by
      intro S m hm
      apply uniq
      intro i
      apply hm }

variable [HasCoproduct I.left] [HasCoproduct I.right]

@[reassoc (attr := simp)]
theorem sigma_condition : I.fstSigmaMap â‰« Sigma.desc K.Ï€ = I.sndSigmaMap â‰« Sigma.desc K.Ï€ := by
  ext
  simp

/-- Given a multicofork, we may obtain a cofork over `âˆ I.left â‡‰ âˆ I.right`. -/
@[simps pt]
noncomputable def toSigmaCofork (K : Multicofork I) : Cofork I.fstSigmaMap I.sndSigmaMap where
  pt := K.pt
  Î¹ :=
    { app := fun x =>
        match x with
        | WalkingParallelPair.zero => I.fstSigmaMap â‰« Sigma.desc K.Ï€
        | WalkingParallelPair.one => Sigma.desc K.Ï€
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;> dsimp <;>
          simp only [Functor.map_id, parallelPair_obj_zero,
            parallelPair_obj_one, sigma_condition, Category.id_comp, Category.comp_id] }

@[simp]
theorem toSigmaCofork_Ï€ : K.toSigmaCofork.Ï€ = Sigma.desc K.Ï€ :=
  rfl

variable (I)

/-- Given a cofork over `âˆ I.left â‡‰ âˆ I.right`, we may obtain a multicofork. -/
@[simps pt]
noncomputable def ofSigmaCofork (c : Cofork I.fstSigmaMap I.sndSigmaMap) : Multicofork I where
  pt := c.pt
  Î¹ :=
    { app := fun x =>
        match x with
        | WalkingMultispan.left a => (Sigma.Î¹ I.left a :) â‰« I.fstSigmaMap â‰« c.Ï€
        | WalkingMultispan.right b => (Sigma.Î¹ I.right b :) â‰« c.Ï€
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _)
        Â· simp
        Â· simp
        Â· simp [c.condition]
        Â· simp }

@[simp]
theorem ofSigmaCofork_Î¹_app_left (c : Cofork I.fstSigmaMap I.sndSigmaMap) (a) :
    (ofSigmaCofork I c).Î¹.app (WalkingMultispan.left a) =
      (Sigma.Î¹ I.left a :) â‰« I.fstSigmaMap â‰« c.Ï€ :=
  rfl

-- LHS simplifies; `(d)simp`-normal form is `ofSigmaCofork_Î¹_app_right'`
theorem ofSigmaCofork_Î¹_app_right (c : Cofork I.fstSigmaMap I.sndSigmaMap) (b) :
    (ofSigmaCofork I c).Î¹.app (WalkingMultispan.right b) = (Sigma.Î¹ I.right b :) â‰« c.Ï€ :=
  rfl

@[simp]
theorem ofSigmaCofork_Î¹_app_right' (c : Cofork I.fstSigmaMap I.sndSigmaMap) (b) :
    Ï€ (ofSigmaCofork I c) b = (Sigma.Î¹ I.right b :) â‰« c.Ï€ :=
  rfl

variable {I} in
/-- Constructor for isomorphisms between multicoforks. -/
@[simps!]
def ext {K K' : Multicofork I}
    (e : K.pt â‰… K'.pt) (h : âˆ€ (i : J.R), K.Ï€ i â‰« e.hom = K'.Ï€ i := by aesop_cat) :
    K â‰… K' :=
  Cocones.ext e (by rintro (i | j) <;> simp [h])

end Multicofork

namespace MultispanIndex

variable {J : MultispanShape.{w, w'}} (I : MultispanIndex J C)
  [HasCoproduct I.left] [HasCoproduct I.right]

/-- `Multicofork.toSigmaCofork` as a functor. -/
@[simps]
noncomputable def toSigmaCoforkFunctor : Multicofork I â¥¤ Cofork I.fstSigmaMap I.sndSigmaMap where
  obj := Multicofork.toSigmaCofork
  map {Kâ‚ Kâ‚‚} f :=
  { hom := f.hom
    w := by
      rintro (_|_)
      all_goals {
        apply colimit.hom_ext
        rintro âŸ¨jâŸ©
        simp } }

/-- `Multicofork.ofSigmaCofork` as a functor. -/
@[simps]
noncomputable def ofSigmaCoforkFunctor : Cofork I.fstSigmaMap I.sndSigmaMap â¥¤ Multicofork I where
  obj := Multicofork.ofSigmaCofork I
  map {Kâ‚ Kâ‚‚} f :=
    { hom := f.hom
      w := by --sorry --by rintro (_ | _) <;> simp
        rintro (_ | _)
        -- porting note; in mathlib3, `simp` worked. What seems to be happening is that
        -- the `simp` set is not confluent, and mathlib3 found
        -- `Multicofork.ofSigmaCofork_Î¹_app_left` before `Multicofork.fst_app_right`,
        -- but mathlib4 finds `Multicofork.fst_app_right` first.
        { simp [-Multicofork.fst_app_right] }
        -- Porting note: similarly here, the `simp` set seems to be non-confluent
        { simp [-Multicofork.ofSigmaCofork_pt] } }

/--
The category of multicoforks is equivalent to the category of coforks over `âˆ I.left â‡‰ âˆ I.right`.
It then follows from `CategoryTheory.IsColimit.ofPreservesCoconeInitial` (or `reflects`) that
it preserves and reflects colimit cocones.
-/
@[simps]
noncomputable def multicoforkEquivSigmaCofork :
    Multicofork I â‰Œ Cofork I.fstSigmaMap I.sndSigmaMap where
  functor := toSigmaCoforkFunctor I
  inverse := ofSigmaCoforkFunctor I
  unitIso := NatIso.ofComponents fun K => Cocones.ext (Iso.refl _) (by
      rintro (_ | _) <;> simp)
  counitIso := NatIso.ofComponents fun K =>
    Cofork.ext (Iso.refl _)
      (by
        -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): in mathlib3 this was just `ext` and I don't know why it's not here
        apply Limits.colimit.hom_ext
        rintro âŸ¨jâŸ©
        dsimp
        simp only [Category.comp_id, colimit.Î¹_desc, Cofan.mk_Î¹_app]
        rfl)

end MultispanIndex

/-- For `I : MulticospanIndex J C`, we say that it has a multiequalizer if the associated
  multicospan has a limit. -/
abbrev HasMultiequalizer {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C) :=
  HasLimit I.multicospan

noncomputable section

/-- The multiequalizer of `I : MulticospanIndex J C`. -/
abbrev multiequalizer {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
    [HasMultiequalizer I] : C :=
  limit I.multicospan

/-- For `I : MultispanIndex J C`, we say that it has a multicoequalizer if
  the associated multicospan has a limit. -/
abbrev HasMulticoequalizer {J : MultispanShape.{w, w'}} (I : MultispanIndex J C) :=
  HasColimit I.multispan

/-- The multiecoqualizer of `I : MultispanIndex J C`. -/
abbrev multicoequalizer {J : MultispanShape.{w, w'}} (I : MultispanIndex J C)
    [HasMulticoequalizer I] : C :=
  colimit I.multispan

namespace Multiequalizer

variable {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C) [HasMultiequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbrev Î¹ (a : J.L) : multiequalizer I âŸ¶ I.left a :=
  limit.Ï€ _ (WalkingMulticospan.left a)

/-- The multifork associated to the multiequalizer. -/
abbrev multifork : Multifork I :=
  limit.cone _

@[simp]
theorem multifork_Î¹ (a) : (Multiequalizer.multifork I).Î¹ a = Multiequalizer.Î¹ I a :=
  rfl

@[simp]
theorem multifork_Ï€_app_left (a) :
    (Multiequalizer.multifork I).Ï€.app (WalkingMulticospan.left a) = Multiequalizer.Î¹ I a :=
  rfl

@[reassoc]
theorem condition (b) :
    Multiequalizer.Î¹ I (J.fst b) â‰« I.fst b = Multiequalizer.Î¹ I (J.snd b) â‰« I.snd b :=
  Multifork.condition _ _

/-- Construct a morphism to the multiequalizer from its universal property. -/
abbrev lift (W : C) (k : âˆ€ a, W âŸ¶ I.left a)
    (h : âˆ€ b, k (J.fst b) â‰« I.fst b = k (J.snd b) â‰« I.snd b) : W âŸ¶ multiequalizer I :=
  limit.lift _ (Multifork.ofÎ¹ I _ k h)

@[reassoc]
theorem lift_Î¹ (W : C) (k : âˆ€ a, W âŸ¶ I.left a)
    (h : âˆ€ b, k (J.fst b) â‰« I.fst b = k (J.snd b) â‰« I.snd b) (a) :
    Multiequalizer.lift I _ k h â‰« Multiequalizer.Î¹ I a = k _ :=
  limit.lift_Ï€ _ _

@[ext]
theorem hom_ext {W : C} (i j : W âŸ¶ multiequalizer I)
    (h : âˆ€ a, i â‰« Multiequalizer.Î¹ I a = j â‰« Multiequalizer.Î¹ I a) : i = j :=
  Multifork.IsLimit.hom_ext (limit.isLimit _) h

variable [HasProduct I.left] [HasProduct I.right]

instance : HasEqualizer I.fstPiMap I.sndPiMap :=
  âŸ¨âŸ¨âŸ¨_, IsLimit.ofPreservesConeTerminal I.multiforkEquivPiFork.functor (limit.isLimit _)âŸ©âŸ©âŸ©

/-- The multiequalizer is isomorphic to the equalizer of `âˆá¶œ I.left â‡‰ âˆá¶œ I.right`. -/
def isoEqualizer : multiequalizer I â‰… equalizer I.fstPiMap I.sndPiMap :=
  limit.isoLimitCone
    âŸ¨_, IsLimit.ofPreservesConeTerminal I.multiforkEquivPiFork.inverse (limit.isLimit _)âŸ©

/-- The canonical injection `multiequalizer I âŸ¶ âˆá¶œ I.left`. -/
def Î¹Pi : multiequalizer I âŸ¶ âˆá¶œ I.left :=
  (isoEqualizer I).hom â‰« equalizer.Î¹ I.fstPiMap I.sndPiMap

@[reassoc (attr := simp)]
theorem Î¹Pi_Ï€ (a) : Î¹Pi I â‰« Pi.Ï€ I.left a = Î¹ I a := by
  rw [Î¹Pi, Category.assoc, â† Iso.eq_inv_comp, isoEqualizer]
  simp

instance : Mono (Î¹Pi I) := mono_comp _ _

end Multiequalizer

namespace Multicoequalizer

variable {J : MultispanShape.{w, w'}} (I : MultispanIndex J C) [HasMulticoequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbrev Ï€ (b : J.R) : I.right b âŸ¶ multicoequalizer I :=
  colimit.Î¹ I.multispan (WalkingMultispan.right _)

/-- The multicofork associated to the multicoequalizer. -/
abbrev multicofork : Multicofork I :=
  colimit.cocone _

@[simp]
theorem multicofork_Ï€ (b) : (Multicoequalizer.multicofork I).Ï€ b = Multicoequalizer.Ï€ I b :=
  rfl

-- @[simp] -- Porting note: LHS simplifies to obtain the normal form below
theorem multicofork_Î¹_app_right (b) :
    (Multicoequalizer.multicofork I).Î¹.app (WalkingMultispan.right b) = Multicoequalizer.Ï€ I b :=
  rfl

@[simp]
theorem multicofork_Î¹_app_right' (b) :
    colimit.Î¹ (MultispanIndex.multispan I) (WalkingMultispan.right b) = Ï€ I b :=
  rfl

@[reassoc]
theorem condition (a) :
    I.fst a â‰« Multicoequalizer.Ï€ I (J.fst a) = I.snd a â‰« Multicoequalizer.Ï€ I (J.snd a) :=
  Multicofork.condition _ _

/-- Construct a morphism from the multicoequalizer from its universal property. -/
abbrev desc (W : C) (k : âˆ€ b, I.right b âŸ¶ W)
    (h : âˆ€ a, I.fst a â‰« k (J.fst a) = I.snd a â‰« k (J.snd a)) : multicoequalizer I âŸ¶ W :=
  colimit.desc _ (Multicofork.ofÏ€ I _ k h)

@[reassoc]
theorem Ï€_desc (W : C) (k : âˆ€ b, I.right b âŸ¶ W)
    (h : âˆ€ a, I.fst a â‰« k (J.fst a) = I.snd a â‰« k (J.snd a)) (b) :
    Multicoequalizer.Ï€ I b â‰« Multicoequalizer.desc I _ k h = k _ :=
  colimit.Î¹_desc _ _

@[ext]
theorem hom_ext {W : C} (i j : multicoequalizer I âŸ¶ W)
    (h : âˆ€ b, Multicoequalizer.Ï€ I b â‰« i = Multicoequalizer.Ï€ I b â‰« j) : i = j :=
  colimit.hom_ext
    (by
      rintro (a | b)
      Â· simp_rw [â† colimit.w I.multispan (WalkingMultispan.Hom.fst a), Category.assoc, h]
      Â· apply h)

variable [HasCoproduct I.left] [HasCoproduct I.right]

instance : HasCoequalizer I.fstSigmaMap I.sndSigmaMap :=
  âŸ¨âŸ¨âŸ¨_,
      IsColimit.ofPreservesCoconeInitial
        I.multicoforkEquivSigmaCofork.functor (colimit.isColimit _)âŸ©âŸ©âŸ©

/-- The multicoequalizer is isomorphic to the coequalizer of `âˆ I.left â‡‰ âˆ I.right`. -/
def isoCoequalizer : multicoequalizer I â‰… coequalizer I.fstSigmaMap I.sndSigmaMap :=
  colimit.isoColimitCocone
    âŸ¨_,
      IsColimit.ofPreservesCoconeInitial I.multicoforkEquivSigmaCofork.inverse
        (colimit.isColimit _)âŸ©

/-- The canonical projection `âˆ I.right âŸ¶ multicoequalizer I`. -/
def sigmaÏ€ : âˆ I.right âŸ¶ multicoequalizer I :=
  coequalizer.Ï€ I.fstSigmaMap I.sndSigmaMap â‰« (isoCoequalizer I).inv

@[reassoc (attr := simp)]
theorem Î¹_sigmaÏ€ (b) : Sigma.Î¹ I.right b â‰« sigmaÏ€ I = Ï€ I b := by
  rw [sigmaÏ€, â† Category.assoc, Iso.comp_inv_eq, isoCoequalizer]
  simp only [MultispanIndex.multicoforkEquivSigmaCofork_inverse,
    MultispanIndex.ofSigmaCoforkFunctor_obj, colimit.isoColimitCocone_Î¹_hom,
    Multicofork.ofSigmaCofork_pt, colimit.cocone_x, Multicofork.Ï€_eq_app_right]
  rfl

instance : Epi (sigmaÏ€ I) := epi_comp _ _

end Multicoequalizer

end

end CategoryTheory.Limits
