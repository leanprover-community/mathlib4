/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.ConeCategory

#align_import category_theory.limits.shapes.multiequalizer from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!

# Multi-(co)equalizers

A *multiequalizer* is an equalizer of two morphisms between two products.
Since both products and equalizers are limits, such an object is again a limit.
This file provides the diagram whose limit is indeed such an object.
In fact, it is well-known that any limit can be obtained as a multiequalizer.
The dual construction (multicoequalizers) is also provided.

## Projects

Prove that a multiequalizer can be identified with
an equalizer between products (and analogously for multicoequalizers).

Prove that the limit of any diagram is a multiequalizer (and similarly for colimits).

-/


namespace CategoryTheory.Limits

open CategoryTheory

universe w v u

/-- The type underlying the multiequalizer diagram. -/
--@[nolint unused_arguments]
inductive WalkingMulticospan {L R : Type w} (fst snd : R ‚Üí L) : Type w
  | left : L ‚Üí WalkingMulticospan fst snd
  | right : R ‚Üí WalkingMulticospan fst snd
#align category_theory.limits.walking_multicospan CategoryTheory.Limits.WalkingMulticospan

/-- The type underlying the multiecoqualizer diagram. -/
--@[nolint unused_arguments]
inductive WalkingMultispan {L R : Type w} (fst snd : L ‚Üí R) : Type w
  | left : L ‚Üí WalkingMultispan fst snd
  | right : R ‚Üí WalkingMultispan fst snd
#align category_theory.limits.walking_multispan CategoryTheory.Limits.WalkingMultispan

namespace WalkingMulticospan

variable {L R : Type w} {fst snd : R ‚Üí L}

instance [Inhabited L] : Inhabited (WalkingMulticospan fst snd) :=
  ‚ü®left default‚ü©

/-- Morphisms for `WalkingMulticospan`. -/
inductive Hom : ‚àÄ _ _ : WalkingMulticospan fst snd, Type w
  | id (A) : Hom A A
  | fst (b) : Hom (left (fst b)) (right b)
  | snd (b) : Hom (left (snd b)) (right b)
#align category_theory.limits.walking_multicospan.hom CategoryTheory.Limits.WalkingMulticospan.Hom

/- Porting note: simpNF says the LHS of this internal identifier simplifies
(which it does, using Hom.id_eq_id) -/
attribute [-simp, nolint simpNF] WalkingMulticospan.Hom.id.sizeOf_spec

instance {a : WalkingMulticospan fst snd} : Inhabited (Hom a a) :=
  ‚ü®Hom.id _‚ü©

/-- Composition of morphisms for `WalkingMulticospan`. -/
def Hom.comp : ‚àÄ {A B C : WalkingMulticospan fst snd} (_ : Hom A B) (_ : Hom B C), Hom A C
  | _, _, _, Hom.id X, f => f
  | _, _, _, Hom.fst b, Hom.id _ => Hom.fst b
  | _, _, _, Hom.snd b, Hom.id _ => Hom.snd b
#align category_theory.limits.walking_multicospan.hom.comp CategoryTheory.Limits.WalkingMulticospan.Hom.comp

instance : SmallCategory (WalkingMulticospan fst snd) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  id_comp := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  comp_id := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  assoc := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) (_ | _ | _) <;> rfl

@[simp] -- Porting note (#10756): added simp lemma
lemma Hom.id_eq_id (X : WalkingMulticospan fst snd) :
    Hom.id X = ùüô X := rfl

@[simp] -- Porting note (#10756): added simp lemma
lemma Hom.comp_eq_comp {X Y Z : WalkingMulticospan fst snd}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : Hom.comp f g = f ‚â´ g := rfl

end WalkingMulticospan

namespace WalkingMultispan

variable {L R : Type v} {fst snd : L ‚Üí R}

instance [Inhabited L] : Inhabited (WalkingMultispan fst snd) :=
  ‚ü®left default‚ü©

/-- Morphisms for `WalkingMultispan`. -/
inductive Hom : ‚àÄ _ _ : WalkingMultispan fst snd, Type v
  | id (A) : Hom A A
  | fst (a) : Hom (left a) (right (fst a))
  | snd (a) : Hom (left a) (right (snd a))
#align category_theory.limits.walking_multispan.hom CategoryTheory.Limits.WalkingMultispan.Hom

/- Porting note: simpNF says the LHS of this internal identifier simplifies
(which it does, using Hom.id_eq_id) -/
attribute [-simp, nolint simpNF] WalkingMultispan.Hom.id.sizeOf_spec

instance {a : WalkingMultispan fst snd} : Inhabited (Hom a a) :=
  ‚ü®Hom.id _‚ü©

/-- Composition of morphisms for `WalkingMultispan`. -/
def Hom.comp : ‚àÄ {A B C : WalkingMultispan fst snd} (_ : Hom A B) (_ : Hom B C), Hom A C
  | _, _, _, Hom.id X, f => f
  | _, _, _, Hom.fst a, Hom.id _ => Hom.fst a
  | _, _, _, Hom.snd a, Hom.id _ => Hom.snd a
#align category_theory.limits.walking_multispan.hom.comp CategoryTheory.Limits.WalkingMultispan.Hom.comp

instance : SmallCategory (WalkingMultispan fst snd) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  id_comp := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  comp_id := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  assoc := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) (_ | _ | _) <;> rfl

@[simp] -- Porting note (#10756): added simp lemma
lemma Hom.id_eq_id (X : WalkingMultispan fst snd) : Hom.id X = ùüô X := rfl

@[simp] -- Porting note (#10756): added simp lemma
lemma Hom.comp_eq_comp {X Y Z : WalkingMultispan fst snd}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : Hom.comp f g = f ‚â´ g := rfl

end WalkingMultispan

/-- This is a structure encapsulating the data necessary to define a `Multicospan`. -/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
structure MulticospanIndex (C : Type u) [Category.{v} C] where
  (L R : Type w)
  (fstTo sndTo : R ‚Üí L)
  left : L ‚Üí C
  right : R ‚Üí C
  fst : ‚àÄ b, left (fstTo b) ‚ü∂ right b
  snd : ‚àÄ b, left (sndTo b) ‚ü∂ right b
#align category_theory.limits.multicospan_index CategoryTheory.Limits.MulticospanIndex

/-- This is a structure encapsulating the data necessary to define a `Multispan`. -/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
structure MultispanIndex (C : Type u) [Category.{v} C] where
  (L R : Type w)
  (fstFrom sndFrom : L ‚Üí R)
  left : L ‚Üí C
  right : R ‚Üí C
  fst : ‚àÄ a, left a ‚ü∂ right (fstFrom a)
  snd : ‚àÄ a, left a ‚ü∂ right (sndFrom a)
#align category_theory.limits.multispan_index CategoryTheory.Limits.MultispanIndex

namespace MulticospanIndex

variable {C : Type u} [Category.{v} C] (I : MulticospanIndex.{w} C)

/-- The multicospan associated to `I : MulticospanIndex`. -/
def multicospan : WalkingMulticospan I.fstTo I.sndTo ‚•§ C where
  obj x :=
    match x with
    | WalkingMulticospan.left a => I.left a
    | WalkingMulticospan.right b => I.right b
  map {x y} f :=
    match x, y, f with
    | _, _, WalkingMulticospan.Hom.id x => ùüô _
    | _, _, WalkingMulticospan.Hom.fst b => I.fst _
    | _, _, WalkingMulticospan.Hom.snd b => I.snd _
  map_id := by
    rintro (_ | _) <;> rfl
  map_comp := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) <;> aesop_cat
#align category_theory.limits.multicospan_index.multicospan CategoryTheory.Limits.MulticospanIndex.multicospan

@[simp]
theorem multicospan_obj_left (a) : I.multicospan.obj (WalkingMulticospan.left a) = I.left a :=
  rfl
#align category_theory.limits.multicospan_index.multicospan_obj_left CategoryTheory.Limits.MulticospanIndex.multicospan_obj_left

@[simp]
theorem multicospan_obj_right (b) : I.multicospan.obj (WalkingMulticospan.right b) = I.right b :=
  rfl
#align category_theory.limits.multicospan_index.multicospan_obj_right CategoryTheory.Limits.MulticospanIndex.multicospan_obj_right

@[simp]
theorem multicospan_map_fst (b) : I.multicospan.map (WalkingMulticospan.Hom.fst b) = I.fst b :=
  rfl
#align category_theory.limits.multicospan_index.multicospan_map_fst CategoryTheory.Limits.MulticospanIndex.multicospan_map_fst

@[simp]
theorem multicospan_map_snd (b) : I.multicospan.map (WalkingMulticospan.Hom.snd b) = I.snd b :=
  rfl
#align category_theory.limits.multicospan_index.multicospan_map_snd CategoryTheory.Limits.MulticospanIndex.multicospan_map_snd

variable [HasProduct I.left] [HasProduct I.right]

/-- The induced map `‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right` via `I.fst`. -/
noncomputable def fstPiMap : ‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right :=
  Pi.lift fun b => Pi.œÄ I.left (I.fstTo b) ‚â´ I.fst b
#align category_theory.limits.multicospan_index.fst_pi_map CategoryTheory.Limits.MulticospanIndex.fstPiMap

/-- The induced map `‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right` via `I.snd`. -/
noncomputable def sndPiMap : ‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right :=
  Pi.lift fun b => Pi.œÄ I.left (I.sndTo b) ‚â´ I.snd b
#align category_theory.limits.multicospan_index.snd_pi_map CategoryTheory.Limits.MulticospanIndex.sndPiMap

@[reassoc (attr := simp)]
theorem fstPiMap_œÄ (b) : I.fstPiMap ‚â´ Pi.œÄ I.right b = Pi.œÄ I.left _ ‚â´ I.fst b := by
  simp [fstPiMap]
#align category_theory.limits.multicospan_index.fst_pi_map_œÄ CategoryTheory.Limits.MulticospanIndex.fstPiMap_œÄ

@[reassoc (attr := simp)]
theorem sndPiMap_œÄ (b) : I.sndPiMap ‚â´ Pi.œÄ I.right b = Pi.œÄ I.left _ ‚â´ I.snd b := by
  simp [sndPiMap]
#align category_theory.limits.multicospan_index.snd_pi_map_œÄ CategoryTheory.Limits.MulticospanIndex.sndPiMap_œÄ

/-- Taking the multiequalizer over the multicospan index is equivalent to taking the equalizer over
the two morphisms `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. This is the diagram of the latter.
-/
@[simps!]
protected noncomputable def parallelPairDiagram :=
  parallelPair I.fstPiMap I.sndPiMap
#align category_theory.limits.multicospan_index.parallel_pair_diagram CategoryTheory.Limits.MulticospanIndex.parallelPairDiagram

end MulticospanIndex

namespace MultispanIndex

variable {C : Type u} [Category.{v} C] (I : MultispanIndex.{w} C)

/-- The multispan associated to `I : MultispanIndex`. -/
def multispan : WalkingMultispan I.fstFrom I.sndFrom ‚•§ C where
  obj x :=
    match x with
    | WalkingMultispan.left a => I.left a
    | WalkingMultispan.right b => I.right b
  map {x y} f :=
    match x, y, f with
    | _, _, WalkingMultispan.Hom.id x => ùüô _
    | _, _, WalkingMultispan.Hom.fst b => I.fst _
    | _, _, WalkingMultispan.Hom.snd b => I.snd _
  map_id := by
    rintro (_ | _) <;> rfl
  map_comp := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) <;> aesop_cat
#align category_theory.limits.multispan_index.multispan CategoryTheory.Limits.MultispanIndex.multispan

@[simp]
theorem multispan_obj_left (a) : I.multispan.obj (WalkingMultispan.left a) = I.left a :=
  rfl
#align category_theory.limits.multispan_index.multispan_obj_left CategoryTheory.Limits.MultispanIndex.multispan_obj_left

@[simp]
theorem multispan_obj_right (b) : I.multispan.obj (WalkingMultispan.right b) = I.right b :=
  rfl
#align category_theory.limits.multispan_index.multispan_obj_right CategoryTheory.Limits.MultispanIndex.multispan_obj_right

@[simp]
theorem multispan_map_fst (a) : I.multispan.map (WalkingMultispan.Hom.fst a) = I.fst a :=
  rfl
#align category_theory.limits.multispan_index.multispan_map_fst CategoryTheory.Limits.MultispanIndex.multispan_map_fst

@[simp]
theorem multispan_map_snd (a) : I.multispan.map (WalkingMultispan.Hom.snd a) = I.snd a :=
  rfl
#align category_theory.limits.multispan_index.multispan_map_snd CategoryTheory.Limits.MultispanIndex.multispan_map_snd

variable [HasCoproduct I.left] [HasCoproduct I.right]

/-- The induced map `‚àê I.left ‚ü∂ ‚àê I.right` via `I.fst`. -/
noncomputable def fstSigmaMap : ‚àê I.left ‚ü∂ ‚àê I.right :=
  Sigma.desc fun b => I.fst b ‚â´ Sigma.Œπ _ (I.fstFrom b)
#align category_theory.limits.multispan_index.fst_sigma_map CategoryTheory.Limits.MultispanIndex.fstSigmaMap

/-- The induced map `‚àê I.left ‚ü∂ ‚àê I.right` via `I.snd`. -/
noncomputable def sndSigmaMap : ‚àê I.left ‚ü∂ ‚àê I.right :=
  Sigma.desc fun b => I.snd b ‚â´ Sigma.Œπ _ (I.sndFrom b)
#align category_theory.limits.multispan_index.snd_sigma_map CategoryTheory.Limits.MultispanIndex.sndSigmaMap

@[reassoc (attr := simp)]
theorem Œπ_fstSigmaMap (b) : Sigma.Œπ I.left b ‚â´ I.fstSigmaMap = I.fst b ‚â´ Sigma.Œπ I.right _ := by
  simp [fstSigmaMap]
#align category_theory.limits.multispan_index.Œπ_fst_sigma_map CategoryTheory.Limits.MultispanIndex.Œπ_fstSigmaMap

@[reassoc (attr := simp)]
theorem Œπ_sndSigmaMap (b) : Sigma.Œπ I.left b ‚â´ I.sndSigmaMap = I.snd b ‚â´ Sigma.Œπ I.right _ := by
  simp [sndSigmaMap]
#align category_theory.limits.multispan_index.Œπ_snd_sigma_map CategoryTheory.Limits.MultispanIndex.Œπ_sndSigmaMap

/--
Taking the multicoequalizer over the multispan index is equivalent to taking the coequalizer over
the two morphsims `‚àê I.left ‚áâ ‚àê I.right`. This is the diagram of the latter.
-/
protected noncomputable abbrev parallelPairDiagram :=
  parallelPair I.fstSigmaMap I.sndSigmaMap
#align category_theory.limits.multispan_index.parallel_pair_diagram CategoryTheory.Limits.MultispanIndex.parallelPairDiagram

end MultispanIndex

variable {C : Type u} [Category.{v} C]

/-- A multifork is a cone over a multicospan. -/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
abbrev Multifork (I : MulticospanIndex.{w} C) :=
  Cone I.multicospan
#align category_theory.limits.multifork CategoryTheory.Limits.Multifork

/-- A multicofork is a cocone over a multispan. -/
-- Porting note(#5171): linter not ported yet
-- @[nolint has_nonempty_instance]
abbrev Multicofork (I : MultispanIndex.{w} C) :=
  Cocone I.multispan
#align category_theory.limits.multicofork CategoryTheory.Limits.Multicofork

namespace Multifork

variable {I : MulticospanIndex.{w} C} (K : Multifork I)

/-- The maps from the cone point of a multifork to the objects on the left. -/
def Œπ (a : I.L) : K.pt ‚ü∂ I.left a :=
  K.œÄ.app (WalkingMulticospan.left _)
#align category_theory.limits.multifork.Œπ CategoryTheory.Limits.Multifork.Œπ

@[simp]
theorem app_left_eq_Œπ (a) : K.œÄ.app (WalkingMulticospan.left a) = K.Œπ a :=
  rfl
#align category_theory.limits.multifork.app_left_eq_Œπ CategoryTheory.Limits.Multifork.app_left_eq_Œπ

@[simp]
theorem app_right_eq_Œπ_comp_fst (b) :
    K.œÄ.app (WalkingMulticospan.right b) = K.Œπ (I.fstTo b) ‚â´ I.fst b := by
  rw [‚Üê K.w (WalkingMulticospan.Hom.fst b)]
  rfl
#align category_theory.limits.multifork.app_right_eq_Œπ_comp_fst CategoryTheory.Limits.Multifork.app_right_eq_Œπ_comp_fst

@[reassoc]
theorem app_right_eq_Œπ_comp_snd (b) :
    K.œÄ.app (WalkingMulticospan.right b) = K.Œπ (I.sndTo b) ‚â´ I.snd b := by
  rw [‚Üê K.w (WalkingMulticospan.Hom.snd b)]
  rfl
#align category_theory.limits.multifork.app_right_eq_Œπ_comp_snd CategoryTheory.Limits.Multifork.app_right_eq_Œπ_comp_snd

@[reassoc (attr := simp)]
theorem hom_comp_Œπ (K‚ÇÅ K‚ÇÇ : Multifork I) (f : K‚ÇÅ ‚ü∂ K‚ÇÇ) (j : I.L) : f.hom ‚â´ K‚ÇÇ.Œπ j = K‚ÇÅ.Œπ j :=
  f.w _
#align category_theory.limits.multifork.hom_comp_Œπ CategoryTheory.Limits.Multifork.hom_comp_Œπ

/-- Construct a multifork using a collection `Œπ` of morphisms. -/
@[simps]
def ofŒπ (I : MulticospanIndex.{w} C) (P : C) (Œπ : ‚àÄ a, P ‚ü∂ I.left a)
    (w : ‚àÄ b, Œπ (I.fstTo b) ‚â´ I.fst b = Œπ (I.sndTo b) ‚â´ I.snd b) : Multifork I where
  pt := P
  œÄ :=
    { app := fun x =>
        match x with
        | WalkingMulticospan.left a => Œπ _
        | WalkingMulticospan.right b => Œπ (I.fstTo b) ‚â´ I.fst b
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;>
          dsimp <;>
          simp only [Category.id_comp, Category.comp_id, Functor.map_id,
            MulticospanIndex.multicospan_obj_left, MulticospanIndex.multicospan_obj_right]
        apply w }
#align category_theory.limits.multifork.of_Œπ CategoryTheory.Limits.Multifork.ofŒπ

@[reassoc (attr := simp)]
theorem condition (b) : K.Œπ (I.fstTo b) ‚â´ I.fst b = K.Œπ (I.sndTo b) ‚â´ I.snd b := by
  rw [‚Üê app_right_eq_Œπ_comp_fst, ‚Üê app_right_eq_Œπ_comp_snd]
#align category_theory.limits.multifork.condition CategoryTheory.Limits.Multifork.condition

/-- This definition provides a convenient way to show that a multifork is a limit. -/
@[simps]
def IsLimit.mk (lift : ‚àÄ E : Multifork I, E.pt ‚ü∂ K.pt)
    (fac : ‚àÄ (E : Multifork I) (i : I.L), lift E ‚â´ K.Œπ i = E.Œπ i)
    (uniq : ‚àÄ (E : Multifork I) (m : E.pt ‚ü∂ K.pt), (‚àÄ i : I.L, m ‚â´ K.Œπ i = E.Œπ i) ‚Üí m = lift E) :
    IsLimit K :=
  { lift
    fac := by
      rintro E (a | b)
      ¬∑ apply fac
      ¬∑ rw [‚Üê E.w (WalkingMulticospan.Hom.fst b), ‚Üê K.w (WalkingMulticospan.Hom.fst b), ‚Üê
          Category.assoc]
        congr 1
        apply fac
    uniq := by
      rintro E m hm
      apply uniq
      intro i
      apply hm }
#align category_theory.limits.multifork.is_limit.mk CategoryTheory.Limits.Multifork.IsLimit.mk

variable {K}

lemma IsLimit.hom_ext (hK : IsLimit K) {T : C} {f g : T ‚ü∂ K.pt}
    (h : ‚àÄ a, f ‚â´ K.Œπ a = g ‚â´ K.Œπ a) : f = g := by
  apply hK.hom_ext
  rintro (_|b)
  ¬∑ apply h
  ¬∑ dsimp
    rw [app_right_eq_Œπ_comp_fst, reassoc_of% h]

/-- Constructor for morphisms to the point of a limit multifork. -/
def IsLimit.lift (hK : IsLimit K) {T : C} (k : ‚àÄ a, T ‚ü∂ I.left a)
    (hk : ‚àÄ b, k (I.fstTo b) ‚â´ I.fst b = k (I.sndTo b) ‚â´ I.snd b) :
    T ‚ü∂ K.pt :=
  hK.lift (Multifork.ofŒπ _ _ k hk)

@[reassoc (attr := simp)]
lemma IsLimit.fac (hK : IsLimit K) {T : C} (k : ‚àÄ a, T ‚ü∂ I.left a)
    (hk : ‚àÄ b, k (I.fstTo b) ‚â´ I.fst b = k (I.sndTo b) ‚â´ I.snd b) (a : I.L):
    IsLimit.lift hK k hk ‚â´ K.Œπ a = k a :=
  hK.fac _ _

variable (K)

variable [HasProduct I.left] [HasProduct I.right]

@[reassoc (attr := simp)]
theorem pi_condition : Pi.lift K.Œπ ‚â´ I.fstPiMap = Pi.lift K.Œπ ‚â´ I.sndPiMap := by
  ext
  simp
#align category_theory.limits.multifork.pi_condition CategoryTheory.Limits.Multifork.pi_condition

/-- Given a multifork, we may obtain a fork over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. -/
@[simps pt]
noncomputable def toPiFork (K : Multifork I) : Fork I.fstPiMap I.sndPiMap where
  pt := K.pt
  œÄ :=
    { app := fun x =>
        match x with
        | WalkingParallelPair.zero => Pi.lift K.Œπ
        | WalkingParallelPair.one => Pi.lift K.Œπ ‚â´ I.fstPiMap
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;>
          dsimp <;>
          simp only [Category.id_comp, Functor.map_id, parallelPair_obj_zero, Category.comp_id,
            pi_condition, parallelPair_obj_one] }
#align category_theory.limits.multifork.to_pi_fork CategoryTheory.Limits.Multifork.toPiFork

@[simp]
theorem toPiFork_œÄ_app_zero : K.toPiFork.Œπ = Pi.lift K.Œπ :=
  rfl
#align category_theory.limits.multifork.to_pi_fork_œÄ_app_zero CategoryTheory.Limits.Multifork.toPiFork_œÄ_app_zero

@[simp, nolint simpNF] -- Porting note (#10675): dsimp cannot prove this
theorem toPiFork_œÄ_app_one : K.toPiFork.œÄ.app WalkingParallelPair.one = Pi.lift K.Œπ ‚â´ I.fstPiMap :=
  rfl
#align category_theory.limits.multifork.to_pi_fork_œÄ_app_one CategoryTheory.Limits.Multifork.toPiFork_œÄ_app_one

variable (I)

/-- Given a fork over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`, we may obtain a multifork. -/
@[simps pt]
noncomputable def ofPiFork (c : Fork I.fstPiMap I.sndPiMap) : Multifork I where
  pt := c.pt
  œÄ :=
    { app := fun x =>
        match x with
        | WalkingMulticospan.left a => c.Œπ ‚â´ Pi.œÄ _ _
        | WalkingMulticospan.right b => c.Œπ ‚â´ I.fstPiMap ‚â´ Pi.œÄ _ _
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _)
        ¬∑ simp
        ¬∑ simp
        ¬∑ dsimp; rw [c.condition_assoc]; simp
        ¬∑ simp }
#align category_theory.limits.multifork.of_pi_fork CategoryTheory.Limits.Multifork.ofPiFork

@[simp]
theorem ofPiFork_œÄ_app_left (c : Fork I.fstPiMap I.sndPiMap) (a) :
    (ofPiFork I c).Œπ a = c.Œπ ‚â´ Pi.œÄ _ _ :=
  rfl
#align category_theory.limits.multifork.of_pi_fork_œÄ_app_left CategoryTheory.Limits.Multifork.ofPiFork_œÄ_app_left

@[simp, nolint simpNF] -- Porting note (#10675): dsimp cannot prove this
theorem ofPiFork_œÄ_app_right (c : Fork I.fstPiMap I.sndPiMap) (a) :
    (ofPiFork I c).œÄ.app (WalkingMulticospan.right a) = c.Œπ ‚â´ I.fstPiMap ‚â´ Pi.œÄ _ _ :=
  rfl
#align category_theory.limits.multifork.of_pi_fork_œÄ_app_right CategoryTheory.Limits.Multifork.ofPiFork_œÄ_app_right

end Multifork

namespace MulticospanIndex

variable (I : MulticospanIndex.{w} C) [HasProduct I.left] [HasProduct I.right]

--attribute [local tidy] tactic.case_bash

/-- `Multifork.toPiFork` as a functor. -/
@[simps]
noncomputable def toPiForkFunctor : Multifork I ‚•§ Fork I.fstPiMap I.sndPiMap where
  obj := Multifork.toPiFork
  map {K‚ÇÅ K‚ÇÇ} f :=
    { hom := f.hom
      w := by
        rintro (_ | _)
        ¬∑ apply limit.hom_ext
          simp
        ¬∑ apply limit.hom_ext
          intros j
          simp only [Multifork.toPiFork_œÄ_app_one, Multifork.pi_condition, Category.assoc]
          dsimp [sndPiMap]
          simp }
#align category_theory.limits.multicospan_index.to_pi_fork_functor CategoryTheory.Limits.MulticospanIndex.toPiForkFunctor

/-- `Multifork.ofPiFork` as a functor. -/
@[simps]
noncomputable def ofPiForkFunctor : Fork I.fstPiMap I.sndPiMap ‚•§ Multifork I where
  obj := Multifork.ofPiFork I
  map {K‚ÇÅ K‚ÇÇ} f :=
    { hom := f.hom
      w := by rintro (_ | _) <;> simp }
#align category_theory.limits.multicospan_index.of_pi_fork_functor CategoryTheory.Limits.MulticospanIndex.ofPiForkFunctor

/-- The category of multiforks is equivalent to the category of forks over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`.
It then follows from `CategoryTheory.IsLimit.ofPreservesConeTerminal` (or `reflects`) that it
preserves and reflects limit cones.
-/
@[simps]
noncomputable def multiforkEquivPiFork : Multifork I ‚âå Fork I.fstPiMap I.sndPiMap where
  functor := toPiForkFunctor I
  inverse := ofPiForkFunctor I
  unitIso :=
    NatIso.ofComponents fun K =>
      Cones.ext (Iso.refl _) (by
        rintro (_ | _) <;> dsimp <;>
          simp [‚Üê Fork.app_one_eq_Œπ_comp_left, -Fork.app_one_eq_Œπ_comp_left])
  counitIso :=
    NatIso.ofComponents fun K => Fork.ext (Iso.refl _)
#align category_theory.limits.multicospan_index.multifork_equiv_pi_fork CategoryTheory.Limits.MulticospanIndex.multiforkEquivPiFork

end MulticospanIndex

namespace Multicofork

variable {I : MultispanIndex.{w} C} (K : Multicofork I)

/-- The maps to the cocone point of a multicofork from the objects on the right. -/
def œÄ (b : I.R) : I.right b ‚ü∂ K.pt :=
  K.Œπ.app (WalkingMultispan.right _)
#align category_theory.limits.multicofork.œÄ CategoryTheory.Limits.Multicofork.œÄ

@[simp]
theorem œÄ_eq_app_right (b) : K.Œπ.app (WalkingMultispan.right _) = K.œÄ b :=
  rfl
#align category_theory.limits.multicofork.œÄ_eq_app_right CategoryTheory.Limits.Multicofork.œÄ_eq_app_right

@[simp]
theorem fst_app_right (a) : K.Œπ.app (WalkingMultispan.left a) = I.fst a ‚â´ K.œÄ _ := by
  rw [‚Üê K.w (WalkingMultispan.Hom.fst a)]
  rfl
#align category_theory.limits.multicofork.fst_app_right CategoryTheory.Limits.Multicofork.fst_app_right

@[reassoc]
theorem snd_app_right (a) : K.Œπ.app (WalkingMultispan.left a) = I.snd a ‚â´ K.œÄ _ := by
  rw [‚Üê K.w (WalkingMultispan.Hom.snd a)]
  rfl
#align category_theory.limits.multicofork.snd_app_right CategoryTheory.Limits.Multicofork.snd_app_right

@[reassoc (attr := simp)] -- Porting note (#10756): added simp lemma
lemma œÄ_comp_hom (K‚ÇÅ K‚ÇÇ : Multicofork I) (f : K‚ÇÅ ‚ü∂ K‚ÇÇ) (b : I.R) : K‚ÇÅ.œÄ b ‚â´ f.hom = K‚ÇÇ.œÄ b :=
  f.w _

/-- Construct a multicofork using a collection `œÄ` of morphisms. -/
@[simps]
def ofœÄ (I : MultispanIndex.{w} C) (P : C) (œÄ : ‚àÄ b, I.right b ‚ü∂ P)
    (w : ‚àÄ a, I.fst a ‚â´ œÄ (I.fstFrom a) = I.snd a ‚â´ œÄ (I.sndFrom a)) : Multicofork I where
  pt := P
  Œπ :=
    { app := fun x =>
        match x with
        | WalkingMultispan.left a => I.fst a ‚â´ œÄ _
        | WalkingMultispan.right b => œÄ _
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;> dsimp <;>
          simp only [Functor.map_id, MultispanIndex.multispan_obj_left,
            Category.id_comp, Category.comp_id, MultispanIndex.multispan_obj_right]
        symm
        apply w }
#align category_theory.limits.multicofork.of_œÄ CategoryTheory.Limits.Multicofork.ofœÄ

@[reassoc (attr := simp)]
theorem condition (a) : I.fst a ‚â´ K.œÄ (I.fstFrom a) = I.snd a ‚â´ K.œÄ (I.sndFrom a) := by
  rw [‚Üê K.snd_app_right, ‚Üê K.fst_app_right]
#align category_theory.limits.multicofork.condition CategoryTheory.Limits.Multicofork.condition

/-- This definition provides a convenient way to show that a multicofork is a colimit. -/
@[simps]
def IsColimit.mk (desc : ‚àÄ E : Multicofork I, K.pt ‚ü∂ E.pt)
    (fac : ‚àÄ (E : Multicofork I) (i : I.R), K.œÄ i ‚â´ desc E = E.œÄ i)
    (uniq : ‚àÄ (E : Multicofork I) (m : K.pt ‚ü∂ E.pt), (‚àÄ i : I.R, K.œÄ i ‚â´ m = E.œÄ i) ‚Üí m = desc E) :
    IsColimit K :=
  { desc
    fac := by
      rintro S (a | b)
      ¬∑ rw [‚Üê K.w (WalkingMultispan.Hom.fst a), ‚Üê S.w (WalkingMultispan.Hom.fst a),
          Category.assoc]
        congr 1
        apply fac
      ¬∑ apply fac
    uniq := by
      intro S m hm
      apply uniq
      intro i
      apply hm }
#align category_theory.limits.multicofork.is_colimit.mk CategoryTheory.Limits.Multicofork.IsColimit.mk

variable [HasCoproduct I.left] [HasCoproduct I.right]

@[reassoc (attr := simp)]
theorem sigma_condition : I.fstSigmaMap ‚â´ Sigma.desc K.œÄ = I.sndSigmaMap ‚â´ Sigma.desc K.œÄ := by
  ext
  simp
#align category_theory.limits.multicofork.sigma_condition CategoryTheory.Limits.Multicofork.sigma_condition

/-- Given a multicofork, we may obtain a cofork over `‚àê I.left ‚áâ ‚àê I.right`. -/
@[simps pt]
noncomputable def toSigmaCofork (K : Multicofork I) : Cofork I.fstSigmaMap I.sndSigmaMap where
  pt := K.pt
  Œπ :=
    { app := fun x =>
        match x with
        | WalkingParallelPair.zero => I.fstSigmaMap ‚â´ Sigma.desc K.œÄ
        | WalkingParallelPair.one => Sigma.desc K.œÄ
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;> dsimp <;>
          simp only [Functor.map_id, parallelPair_obj_zero,
            parallelPair_obj_one, sigma_condition, Category.id_comp, Category.comp_id] }
#align category_theory.limits.multicofork.to_sigma_cofork CategoryTheory.Limits.Multicofork.toSigmaCofork

@[simp]
theorem toSigmaCofork_œÄ : K.toSigmaCofork.œÄ = Sigma.desc K.œÄ :=
  rfl
#align category_theory.limits.multicofork.to_sigma_cofork_œÄ CategoryTheory.Limits.Multicofork.toSigmaCofork_œÄ

variable (I)

/-- Given a cofork over `‚àê I.left ‚áâ ‚àê I.right`, we may obtain a multicofork. -/
@[simps pt]
noncomputable def ofSigmaCofork (c : Cofork I.fstSigmaMap I.sndSigmaMap) : Multicofork I where
  pt := c.pt
  Œπ :=
    { app := fun x =>
        match x with
        | WalkingMultispan.left a => (Sigma.Œπ I.left a : _) ‚â´ I.fstSigmaMap ‚â´ c.œÄ
        | WalkingMultispan.right b => (Sigma.Œπ I.right b : _) ‚â´ c.œÄ
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _)
        ¬∑ simp
        ¬∑ simp
        ¬∑ simp [c.condition]
        ¬∑ simp }
#align category_theory.limits.multicofork.of_sigma_cofork CategoryTheory.Limits.Multicofork.ofSigmaCofork

-- Porting note (#10675): dsimp cannot prove this... once ofSigmaCofork_Œπ_app_right' is defined
@[simp, nolint simpNF]
theorem ofSigmaCofork_Œπ_app_left (c : Cofork I.fstSigmaMap I.sndSigmaMap) (a) :
    (ofSigmaCofork I c).Œπ.app (WalkingMultispan.left a) =
      (Sigma.Œπ I.left a : _) ‚â´ I.fstSigmaMap ‚â´ c.œÄ :=
  rfl
#align category_theory.limits.multicofork.of_sigma_cofork_Œπ_app_left CategoryTheory.Limits.Multicofork.ofSigmaCofork_Œπ_app_left

-- @[simp] -- Porting note: LHS simplifies to obtain the normal form below
theorem ofSigmaCofork_Œπ_app_right (c : Cofork I.fstSigmaMap I.sndSigmaMap) (b) :
    (ofSigmaCofork I c).Œπ.app (WalkingMultispan.right b) = (Sigma.Œπ I.right b : _) ‚â´ c.œÄ :=
  rfl
#align category_theory.limits.multicofork.of_sigma_cofork_Œπ_app_right CategoryTheory.Limits.Multicofork.ofSigmaCofork_Œπ_app_right

@[simp]
theorem ofSigmaCofork_Œπ_app_right' (c : Cofork I.fstSigmaMap I.sndSigmaMap) (b) :
    œÄ (ofSigmaCofork I c) b = (Sigma.Œπ I.right b : _) ‚â´ c.œÄ :=
  rfl

end Multicofork

namespace MultispanIndex

variable (I : MultispanIndex.{w} C) [HasCoproduct I.left] [HasCoproduct I.right]

--attribute [local tidy] tactic.case_bash

/-- `Multicofork.toSigmaCofork` as a functor. -/
@[simps]
noncomputable def toSigmaCoforkFunctor : Multicofork I ‚•§ Cofork I.fstSigmaMap I.sndSigmaMap where
  obj := Multicofork.toSigmaCofork
  map {K‚ÇÅ K‚ÇÇ} f :=
  { hom := f.hom
    w := by
      rintro (_|_)
      all_goals {
        apply colimit.hom_ext
        rintro ‚ü®j‚ü©
        simp } }
#align category_theory.limits.multispan_index.to_sigma_cofork_functor CategoryTheory.Limits.MultispanIndex.toSigmaCoforkFunctor

/-- `Multicofork.ofSigmaCofork` as a functor. -/
@[simps]
noncomputable def ofSigmaCoforkFunctor : Cofork I.fstSigmaMap I.sndSigmaMap ‚•§ Multicofork I where
  obj := Multicofork.ofSigmaCofork I
  map {K‚ÇÅ K‚ÇÇ} f :=
    { hom := f.hom
      w := by --sorry --by rintro (_ | _) <;> simp
        rintro (_ | _)
        -- porting note; in mathlib3, `simp` worked. What seems to be happening is that
        -- the `simp` set is not confluent, and mathlib3 found
        -- `Multicofork.ofSigmaCofork_Œπ_app_left` before `Multicofork.fst_app_right`,
        -- but mathlib4 finds `Multicofork.fst_app_right` first.
        { simp [-Multicofork.fst_app_right] }
        -- Porting note: similarly here, the `simp` set seems to be non-confluent
        { simp [-Multicofork.ofSigmaCofork_pt] } }

/--
The category of multicoforks is equivalent to the category of coforks over `‚àê I.left ‚áâ ‚àê I.right`.
It then follows from `CategoryTheory.IsColimit.ofPreservesCoconeInitial` (or `reflects`) that
it preserves and reflects colimit cocones.
-/
@[simps]
noncomputable def multicoforkEquivSigmaCofork :
    Multicofork I ‚âå Cofork I.fstSigmaMap I.sndSigmaMap where
  functor := toSigmaCoforkFunctor I
  inverse := ofSigmaCoforkFunctor I
  unitIso := NatIso.ofComponents fun K => Cocones.ext (Iso.refl _) (by
      rintro (_ | _) <;> simp)
  counitIso := NatIso.ofComponents fun K =>
    Cofork.ext (Iso.refl _)
      (by
        -- Porting note: in mathlib3 this was just `ext` and I don't know why it's not here
        apply Limits.colimit.hom_ext
        rintro ‚ü®j‚ü©
        dsimp
        simp only [Category.comp_id, colimit.Œπ_desc, Cofan.mk_Œπ_app]
        rfl)
#align category_theory.limits.multispan_index.multicofork_equiv_sigma_cofork CategoryTheory.Limits.MultispanIndex.multicoforkEquivSigmaCofork

end MultispanIndex

/-- For `I : MulticospanIndex C`, we say that it has a multiequalizer if the associated
  multicospan has a limit. -/
abbrev HasMultiequalizer (I : MulticospanIndex.{w} C) :=
  HasLimit I.multicospan
#align category_theory.limits.has_multiequalizer CategoryTheory.Limits.HasMultiequalizer

noncomputable section

/-- The multiequalizer of `I : MulticospanIndex C`. -/
abbrev multiequalizer (I : MulticospanIndex.{w} C) [HasMultiequalizer I] : C :=
  limit I.multicospan
#align category_theory.limits.multiequalizer CategoryTheory.Limits.multiequalizer

/-- For `I : MultispanIndex C`, we say that it has a multicoequalizer if
  the associated multicospan has a limit. -/
abbrev HasMulticoequalizer (I : MultispanIndex.{w} C) :=
  HasColimit I.multispan
#align category_theory.limits.has_multicoequalizer CategoryTheory.Limits.HasMulticoequalizer

/-- The multiecoqualizer of `I : MultispanIndex C`. -/
abbrev multicoequalizer (I : MultispanIndex.{w} C) [HasMulticoequalizer I] : C :=
  colimit I.multispan
#align category_theory.limits.multicoequalizer CategoryTheory.Limits.multicoequalizer

namespace Multiequalizer

variable (I : MulticospanIndex.{w} C) [HasMultiequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbrev Œπ (a : I.L) : multiequalizer I ‚ü∂ I.left a :=
  limit.œÄ _ (WalkingMulticospan.left a)
#align category_theory.limits.multiequalizer.Œπ CategoryTheory.Limits.Multiequalizer.Œπ

/-- The multifork associated to the multiequalizer. -/
abbrev multifork : Multifork I :=
  limit.cone _
#align category_theory.limits.multiequalizer.multifork CategoryTheory.Limits.Multiequalizer.multifork

@[simp]
theorem multifork_Œπ (a) : (Multiequalizer.multifork I).Œπ a = Multiequalizer.Œπ I a :=
  rfl
#align category_theory.limits.multiequalizer.multifork_Œπ CategoryTheory.Limits.Multiequalizer.multifork_Œπ

@[simp]
theorem multifork_œÄ_app_left (a) :
    (Multiequalizer.multifork I).œÄ.app (WalkingMulticospan.left a) = Multiequalizer.Œπ I a :=
  rfl
#align category_theory.limits.multiequalizer.multifork_œÄ_app_left CategoryTheory.Limits.Multiequalizer.multifork_œÄ_app_left

@[reassoc]
theorem condition (b) :
    Multiequalizer.Œπ I (I.fstTo b) ‚â´ I.fst b = Multiequalizer.Œπ I (I.sndTo b) ‚â´ I.snd b :=
  Multifork.condition _ _
#align category_theory.limits.multiequalizer.condition CategoryTheory.Limits.Multiequalizer.condition

/-- Construct a morphism to the multiequalizer from its universal property. -/
abbrev lift (W : C) (k : ‚àÄ a, W ‚ü∂ I.left a)
    (h : ‚àÄ b, k (I.fstTo b) ‚â´ I.fst b = k (I.sndTo b) ‚â´ I.snd b) : W ‚ü∂ multiequalizer I :=
  limit.lift _ (Multifork.ofŒπ I _ k h)
#align category_theory.limits.multiequalizer.lift CategoryTheory.Limits.Multiequalizer.lift

@[reassoc] -- Porting note (#10618): simp can prove this, removed attribute
theorem lift_Œπ (W : C) (k : ‚àÄ a, W ‚ü∂ I.left a)
    (h : ‚àÄ b, k (I.fstTo b) ‚â´ I.fst b = k (I.sndTo b) ‚â´ I.snd b) (a) :
    Multiequalizer.lift I _ k h ‚â´ Multiequalizer.Œπ I a = k _ :=
  limit.lift_œÄ _ _
#align category_theory.limits.multiequalizer.lift_Œπ CategoryTheory.Limits.Multiequalizer.lift_Œπ

@[ext]
theorem hom_ext {W : C} (i j : W ‚ü∂ multiequalizer I)
    (h : ‚àÄ a, i ‚â´ Multiequalizer.Œπ I a = j ‚â´ Multiequalizer.Œπ I a) : i = j :=
  Multifork.IsLimit.hom_ext (limit.isLimit _) h
#align category_theory.limits.multiequalizer.hom_ext CategoryTheory.Limits.Multiequalizer.hom_ext

variable [HasProduct I.left] [HasProduct I.right]

instance : HasEqualizer I.fstPiMap I.sndPiMap :=
  ‚ü®‚ü®‚ü®_, IsLimit.ofPreservesConeTerminal I.multiforkEquivPiFork.functor (limit.isLimit _)‚ü©‚ü©‚ü©

/-- The multiequalizer is isomorphic to the equalizer of `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. -/
def isoEqualizer : multiequalizer I ‚âÖ equalizer I.fstPiMap I.sndPiMap :=
  limit.isoLimitCone
    ‚ü®_, IsLimit.ofPreservesConeTerminal I.multiforkEquivPiFork.inverse (limit.isLimit _)‚ü©
#align category_theory.limits.multiequalizer.iso_equalizer CategoryTheory.Limits.Multiequalizer.isoEqualizer

/-- The canonical injection `multiequalizer I ‚ü∂ ‚àè·∂ú I.left`. -/
def ŒπPi : multiequalizer I ‚ü∂ ‚àè·∂ú I.left :=
  (isoEqualizer I).hom ‚â´ equalizer.Œπ I.fstPiMap I.sndPiMap
#align category_theory.limits.multiequalizer.Œπ_pi CategoryTheory.Limits.Multiequalizer.ŒπPi

@[reassoc (attr := simp)]
theorem ŒπPi_œÄ (a) : ŒπPi I ‚â´ Pi.œÄ I.left a = Œπ I a := by
  rw [ŒπPi, Category.assoc, ‚Üê Iso.eq_inv_comp, isoEqualizer]
  simp
#align category_theory.limits.multiequalizer.Œπ_pi_œÄ CategoryTheory.Limits.Multiequalizer.ŒπPi_œÄ

instance : Mono (ŒπPi I) := mono_comp _ _

end Multiequalizer

namespace Multicoequalizer

variable (I : MultispanIndex.{w} C) [HasMulticoequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbrev œÄ (b : I.R) : I.right b ‚ü∂ multicoequalizer I :=
  colimit.Œπ I.multispan (WalkingMultispan.right _)
#align category_theory.limits.multicoequalizer.œÄ CategoryTheory.Limits.Multicoequalizer.œÄ

/-- The multicofork associated to the multicoequalizer. -/
abbrev multicofork : Multicofork I :=
  colimit.cocone _
#align category_theory.limits.multicoequalizer.multicofork CategoryTheory.Limits.Multicoequalizer.multicofork

@[simp]
theorem multicofork_œÄ (b) : (Multicoequalizer.multicofork I).œÄ b = Multicoequalizer.œÄ I b :=
  rfl
#align category_theory.limits.multicoequalizer.multicofork_œÄ CategoryTheory.Limits.Multicoequalizer.multicofork_œÄ

-- @[simp] -- Porting note: LHS simplifies to obtain the normal form below
theorem multicofork_Œπ_app_right (b) :
    (Multicoequalizer.multicofork I).Œπ.app (WalkingMultispan.right b) = Multicoequalizer.œÄ I b :=
  rfl
#align category_theory.limits.multicoequalizer.multicofork_Œπ_app_right CategoryTheory.Limits.Multicoequalizer.multicofork_Œπ_app_right

@[simp]
theorem multicofork_Œπ_app_right' (b) :
    colimit.Œπ (MultispanIndex.multispan I) (WalkingMultispan.right b) = œÄ I b :=
  rfl

@[reassoc]
theorem condition (a) :
    I.fst a ‚â´ Multicoequalizer.œÄ I (I.fstFrom a) = I.snd a ‚â´ Multicoequalizer.œÄ I (I.sndFrom a) :=
  Multicofork.condition _ _
#align category_theory.limits.multicoequalizer.condition CategoryTheory.Limits.Multicoequalizer.condition

/-- Construct a morphism from the multicoequalizer from its universal property. -/
abbrev desc (W : C) (k : ‚àÄ b, I.right b ‚ü∂ W)
    (h : ‚àÄ a, I.fst a ‚â´ k (I.fstFrom a) = I.snd a ‚â´ k (I.sndFrom a)) : multicoequalizer I ‚ü∂ W :=
  colimit.desc _ (Multicofork.ofœÄ I _ k h)
#align category_theory.limits.multicoequalizer.desc CategoryTheory.Limits.Multicoequalizer.desc

@[reassoc] -- Porting note (#10618): simp can prove this, removed attribute
theorem œÄ_desc (W : C) (k : ‚àÄ b, I.right b ‚ü∂ W)
    (h : ‚àÄ a, I.fst a ‚â´ k (I.fstFrom a) = I.snd a ‚â´ k (I.sndFrom a)) (b) :
    Multicoequalizer.œÄ I b ‚â´ Multicoequalizer.desc I _ k h = k _ :=
  colimit.Œπ_desc _ _
#align category_theory.limits.multicoequalizer.œÄ_desc CategoryTheory.Limits.Multicoequalizer.œÄ_desc

@[ext]
theorem hom_ext {W : C} (i j : multicoequalizer I ‚ü∂ W)
    (h : ‚àÄ b, Multicoequalizer.œÄ I b ‚â´ i = Multicoequalizer.œÄ I b ‚â´ j) : i = j :=
  colimit.hom_ext
    (by
      rintro (a | b)
      ¬∑ simp_rw [‚Üê colimit.w I.multispan (WalkingMultispan.Hom.fst a), Category.assoc, h]
      ¬∑ apply h)
#align category_theory.limits.multicoequalizer.hom_ext CategoryTheory.Limits.Multicoequalizer.hom_ext

variable [HasCoproduct I.left] [HasCoproduct I.right]

instance : HasCoequalizer I.fstSigmaMap I.sndSigmaMap :=
  ‚ü®‚ü®‚ü®_,
      IsColimit.ofPreservesCoconeInitial
        I.multicoforkEquivSigmaCofork.functor (colimit.isColimit _)‚ü©‚ü©‚ü©

/-- The multicoequalizer is isomorphic to the coequalizer of `‚àê I.left ‚áâ ‚àê I.right`. -/
def isoCoequalizer : multicoequalizer I ‚âÖ coequalizer I.fstSigmaMap I.sndSigmaMap :=
  colimit.isoColimitCocone
    ‚ü®_,
      IsColimit.ofPreservesCoconeInitial I.multicoforkEquivSigmaCofork.inverse
        (colimit.isColimit _)‚ü©
#align category_theory.limits.multicoequalizer.iso_coequalizer CategoryTheory.Limits.Multicoequalizer.isoCoequalizer

/-- The canonical projection `‚àê I.right ‚ü∂ multicoequalizer I`. -/
def sigmaœÄ : ‚àê I.right ‚ü∂ multicoequalizer I :=
  coequalizer.œÄ I.fstSigmaMap I.sndSigmaMap ‚â´ (isoCoequalizer I).inv
#align category_theory.limits.multicoequalizer.sigma_œÄ CategoryTheory.Limits.Multicoequalizer.sigmaœÄ

@[reassoc (attr := simp)]
theorem Œπ_sigmaœÄ (b) : Sigma.Œπ I.right b ‚â´ sigmaœÄ I = œÄ I b := by
  rw [sigmaœÄ, ‚Üê Category.assoc, Iso.comp_inv_eq, isoCoequalizer]
  simp only [MultispanIndex.multicoforkEquivSigmaCofork_inverse,
    MultispanIndex.ofSigmaCoforkFunctor_obj, colimit.isoColimitCocone_Œπ_hom,
    Multicofork.ofSigmaCofork_pt, colimit.cocone_x, Multicofork.œÄ_eq_app_right]
  rfl
#align category_theory.limits.multicoequalizer.Œπ_sigma_œÄ CategoryTheory.Limits.Multicoequalizer.Œπ_sigmaœÄ

instance : Epi (sigmaœÄ I) := epi_comp _ _

end Multicoequalizer

end

end CategoryTheory.Limits
