/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Products
public import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
public import Mathlib.CategoryTheory.Limits.ConeCategory

/-!

# Multi-(co)equalizers

A *multiequalizer* is an equalizer of two morphisms between two products.
Since both products and equalizers are limits, such an object is again a limit.
This file provides the diagram whose limit is indeed such an object.
In fact, it is well-known that any limit can be obtained as a multiequalizer.
The dual construction (multicoequalizers) is also provided.

## Projects

Prove that a multiequalizer can be identified with
an equalizer between products (and analogously for multicoequalizers).

Prove that the limit of any diagram is a multiequalizer (and similarly for colimits).

-/

@[expose] public section


namespace CategoryTheory.Limits

universe t w w' v u

/-- The shape of a multiequalizer diagram. It involves two types `L` and `R`,
and two maps `R ‚Üí L`. -/
@[nolint checkUnivs]
structure MulticospanShape where
  /-- the left type -/
  L : Type w
  /-- the right type -/
  R : Type w'
  /-- the first map `R ‚Üí L` -/
  fst : R ‚Üí L
  /-- the second map `R ‚Üí L` -/
  snd : R ‚Üí L

/-- Given a type `Œπ`, this is the shape of multiequalizer diagrams corresponding
to situations where we want to equalize two families of maps `U i ‚ü∂ V ‚ü®i, j‚ü©`
and `U j ‚ü∂ V ‚ü®i, j‚ü©` with `i : Œπ` and `j : Œπ`. -/
@[simps]
def MulticospanShape.prod (Œπ : Type w) : MulticospanShape where
  L := Œπ
  R := Œπ √ó Œπ
  fst := _root_.Prod.fst
  snd := _root_.Prod.snd

/-- The shape of a multicoequalizer diagram. It involves two types `L` and `R`,
and two maps `L ‚Üí R`. -/
@[nolint checkUnivs]
structure MultispanShape where
  /-- the left type -/
  L : Type w
  /-- the right type -/
  R : Type w'
  /-- the first map `L ‚Üí R` -/
  fst : L ‚Üí R
  /-- the second map `L ‚Üí R` -/
  snd : L ‚Üí R

/-- Given a type `Œπ`, this is the shape of multicoequalizer diagrams corresponding
to situations where we want to coequalize two families of maps `V ‚ü®i, j‚ü© ‚ü∂ U i`
and `V ‚ü®i, j‚ü© ‚ü∂ U j` with `i : Œπ` and `j : Œπ`. -/
@[simps]
def MultispanShape.prod (Œπ : Type w) : MultispanShape where
  L := Œπ √ó Œπ
  R := Œπ
  fst := _root_.Prod.fst
  snd := _root_.Prod.snd

/-- Given a linearly ordered type `Œπ`, this is the shape of multicoequalizer diagrams
corresponding to situations where we want to coequalize two families of maps
`V ‚ü®i, j‚ü© ‚ü∂ U i` and `V ‚ü®i, j‚ü© ‚ü∂ U j` with `i < j`. -/
@[simps]
def MultispanShape.ofLinearOrder (Œπ : Type w) [LinearOrder Œπ] : MultispanShape where
  L := {x : Œπ √ó Œπ | x.1 < x.2}
  R := Œπ
  fst x := x.1.1
  snd x := x.1.2

instance : Unique (MultispanShape.ofLinearOrder Bool).L where
  default := ‚ü®‚ü®False, True‚ü©, by simp‚ü©
  uniq := by rintro ‚ü®‚ü®(_ | _), (_ | _)‚ü©, _‚ü© <;> tauto

/-- The type underlying the multiequalizer diagram. -/
inductive WalkingMulticospan (J : MulticospanShape.{w, w'}) : Type max w w'
  | left : J.L ‚Üí WalkingMulticospan J
  | right : J.R ‚Üí WalkingMulticospan J

/-- The type underlying the multicoequalizer diagram. -/
inductive WalkingMultispan (J : MultispanShape.{w, w'}) : Type max w w'
  | left : J.L ‚Üí WalkingMultispan J
  | right : J.R ‚Üí WalkingMultispan J

namespace WalkingMulticospan

variable {J : MulticospanShape.{w, w'}}

instance [Inhabited J.L] : Inhabited (WalkingMulticospan J) :=
  ‚ü®left default‚ü©

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- Morphisms for `WalkingMulticospan`. -/
inductive Hom : ‚àÄ _ _ : WalkingMulticospan J, Type max w w'
  | id (A) : Hom A A
  | fst (b) : Hom (left (J.fst b)) (right b)
  | snd (b) : Hom (left (J.snd b)) (right b)

instance {a : WalkingMulticospan J} : Inhabited (Hom a a) :=
  ‚ü®Hom.id _‚ü©

/-- Composition of morphisms for `WalkingMulticospan`. -/
def Hom.comp : ‚àÄ {A B C : WalkingMulticospan J} (_ : Hom A B) (_ : Hom B C), Hom A C
  | _, _, _, Hom.id X, f => f
  | _, _, _, Hom.fst b, Hom.id _ => Hom.fst b
  | _, _, _, Hom.snd b, Hom.id _ => Hom.snd b

instance : SmallCategory (WalkingMulticospan J) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  id_comp := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  comp_id := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  assoc := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) (_ | _ | _) <;> rfl

@[simp]
lemma Hom.id_eq_id (X : WalkingMulticospan J) :
    Hom.id X = ùüô X := rfl

@[simp]
lemma Hom.comp_eq_comp {X Y Z : WalkingMulticospan J}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : Hom.comp f g = f ‚â´ g := rfl

/-- Construct a natural isomorphism between functors out of a walking multicospan from its
components. -/
@[simps!]
def functorExt {C : Type*} [Category* C] {F G : WalkingMulticospan J ‚•§ C}
    (left : ‚àÄ i, F.obj (.left i) ‚âÖ G.obj (.left i))
    (right : ‚àÄ i, F.obj (.right i) ‚âÖ G.obj (.right i))
    (wl : ‚àÄ i, F.map (WalkingMulticospan.Hom.fst i) ‚â´ (right i).hom =
      (left _).hom ‚â´ G.map (WalkingMulticospan.Hom.fst i) := by cat_disch)
    (wr : ‚àÄ i, F.map (WalkingMulticospan.Hom.snd i) ‚â´ (right i).hom =
      (left _).hom ‚â´ G.map (WalkingMulticospan.Hom.snd i) := by cat_disch) :
    F ‚âÖ G :=
  NatIso.ofComponents (fun j ‚Ü¶ match j with | .left i => left i | .right i => right i) <| by
    rintro _ _ ‚ü®_‚ü© <;> simp [wl, wr]

end WalkingMulticospan

namespace WalkingMultispan

variable {J : MultispanShape.{w, w'}}

instance [Inhabited J.L] : Inhabited (WalkingMultispan J) :=
  ‚ü®left default‚ü©

instance [Small.{t} J.L] [Small.{t} J.R] : Small.{t} (WalkingMultispan J) :=
  small_of_surjective (f := Sum.elim WalkingMultispan.left WalkingMultispan.right)
    (by rintro (_ | _) <;> aesop)

-- Don't generate unnecessary `sizeOf_spec` lemma which the `simpNF` linter will complain about.
set_option genSizeOfSpec false in
/-- Morphisms for `WalkingMultispan`. -/
inductive Hom : ‚àÄ _ _ : WalkingMultispan J, Type max w w'
  | id (A) : Hom A A
  | fst (a) : Hom (left a) (right (J.fst a))
  | snd (a) : Hom (left a) (right (J.snd a))

instance {a : WalkingMultispan J} : Inhabited (Hom a a) :=
  ‚ü®Hom.id _‚ü©

/-- Composition of morphisms for `WalkingMultispan`. -/
def Hom.comp : ‚àÄ {A B C : WalkingMultispan J} (_ : Hom A B) (_ : Hom B C), Hom A C
  | _, _, _, Hom.id X, f => f
  | _, _, _, Hom.fst a, Hom.id _ => Hom.fst a
  | _, _, _, Hom.snd a, Hom.id _ => Hom.snd a

instance : SmallCategory (WalkingMultispan J) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  id_comp := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  comp_id := by
    rintro (_ | _) (_ | _) (_ | _ | _) <;> rfl
  assoc := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) (_ | _ | _) <;> rfl

@[simp]
lemma Hom.id_eq_id (X : WalkingMultispan J) : Hom.id X = ùüô X := rfl

@[simp]
lemma Hom.comp_eq_comp {X Y Z : WalkingMultispan J}
    (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : Hom.comp f g = f ‚â´ g := rfl

/-- Construct a natural isomorphism between functors out of a walking multispan from its
components. -/
@[simps!]
def functorExt {C : Type*} [Category* C] {F G : WalkingMultispan J ‚•§ C}
    (left : ‚àÄ i, F.obj (.left i) ‚âÖ G.obj (.left i))
    (right : ‚àÄ i, F.obj (.right i) ‚âÖ G.obj (.right i))
    (wl : ‚àÄ i, F.map (WalkingMultispan.Hom.fst i) ‚â´ (right _).hom =
      (left i).hom ‚â´ G.map (WalkingMultispan.Hom.fst _) := by cat_disch)
    (wr : ‚àÄ i, F.map (WalkingMultispan.Hom.snd i) ‚â´ (right _).hom =
      (left i).hom ‚â´ G.map (WalkingMultispan.Hom.snd _) := by cat_disch) :
    F ‚âÖ G :=
  NatIso.ofComponents (fun j ‚Ü¶ match j with | .left i => left i | .right i => right i) <| by
    rintro _ _ ‚ü®_‚ü© <;> simp [wl, wr]

instance (a : WalkingMultispan J) : Unique (a ‚ü∂ a) where
  default := ùüô _
  uniq := by rintro ‚ü®‚ü©; rfl

instance (a b : J.L) : Subsingleton (left a ‚ü∂ left b) := by
  by_cases h : a = b
  ¬∑ subst h
    infer_instance
  ¬∑ have : IsEmpty (left a ‚ü∂ left b) := ‚ü®by rintro ‚ü®‚ü©; simp at h‚ü©
    infer_instance

instance (a b : J.R) : Subsingleton (right a ‚ü∂ right b) := by
  by_cases h : a = b
  ¬∑ subst h
    infer_instance
  ¬∑ have : IsEmpty (right a ‚ü∂ right b) := ‚ü®by rintro ‚ü®‚ü©; simp at h‚ü©
    infer_instance

instance (a : J.R) (b : J.L) : IsEmpty (right a ‚ü∂ left b) := ‚ü®by rintro ‚ü®‚ü©‚ü©

instance : LocallySmall.{t} (WalkingMultispan J) where
  hom_small := by
    rintro (l | r) (l' | r')
    ¬∑ infer_instance
    ¬∑ let T‚ÇÅ := { u : Unit // J.fst l = r' }
      let T‚ÇÇ := { u : Unit // J.snd l = r' }
      let f : T‚ÇÅ ‚äï T‚ÇÇ ‚Üí (left l ‚ü∂ right r') :=
        Sum.elim (fun ‚ü®_, h‚ü© ‚Ü¶ by subst h; exact Hom.fst l)
          (fun ‚ü®_, h‚ü© ‚Ü¶ by subst h; exact Hom.snd l)
      refine small_of_surjective (f := f) ?_
      rintro (_ | _)
      ¬∑ exact ‚ü®Sum.inl ‚ü®‚ü®‚ü©, rfl‚ü©, rfl‚ü©
      ¬∑ exact ‚ü®Sum.inr ‚ü®‚ü®‚ü©, rfl‚ü©, rfl‚ü©
    ¬∑ infer_instance
    ¬∑ infer_instance

variable (J) in
/-- The bijection `WalkingMultispan J ‚âÉ J.L ‚äï J.R`. -/
def equiv : WalkingMultispan J ‚âÉ J.L ‚äï J.R where
  toFun x := match x with
    | left a => Sum.inl a
    | right b => Sum.inr b
  invFun := Sum.elim left right
  left_inv := by rintro (_ | _) <;> rfl
  right_inv := by rintro (_ | _) <;> rfl

variable (J) in
/-- The bijection `Arrow (WalkingMultispan J) ‚âÉ WalkingMultispan J ‚äï J.R ‚äï J.R`. -/
def arrowEquiv :
    Arrow (WalkingMultispan J) ‚âÉ WalkingMultispan J ‚äï J.L ‚äï J.L where
  toFun f := match f.hom with
    | .id x => Sum.inl x
    | .fst a => Sum.inr (Sum.inl a)
    | .snd a => Sum.inr (Sum.inr a)
  invFun :=
    Sum.elim (fun X ‚Ü¶ Arrow.mk (ùüô X))
      (Sum.elim (fun a ‚Ü¶ Arrow.mk (Hom.fst a : left _ ‚ü∂ right _))
        (fun a ‚Ü¶ Arrow.mk (Hom.snd a : left _ ‚ü∂ right _)))
  left_inv := by rintro ‚ü®_, _, (_ | _ | _)‚ü© <;> rfl
  right_inv := by rintro (_ | _ | _) <;> rfl

end WalkingMultispan

/-- This is a structure encapsulating the data necessary to define a `Multicospan`. -/
@[nolint checkUnivs]
structure MulticospanIndex (J : MulticospanShape.{w, w'})
    (C : Type u) [Category.{v} C] where
  /-- Left map, from `J.L` to `C` -/
  left : J.L ‚Üí C
  /-- Right map, from `J.R` to `C` -/
  right : J.R ‚Üí C
  /-- A family of maps from `left (J.fst b)` to `right b` -/
  fst : ‚àÄ b, left (J.fst b) ‚ü∂ right b
  /-- A family of maps from `left (J.snd b)` to `right b` -/
  snd : ‚àÄ b, left (J.snd b) ‚ü∂ right b

/-- This is a structure encapsulating the data necessary to define a `Multispan`. -/
@[nolint checkUnivs]
structure MultispanIndex (J : MultispanShape.{w, w'})
    (C : Type u) [Category.{v} C] where
  /-- Left map, from `J.L` to `C` -/
  left : J.L ‚Üí C
  /-- Right map, from `J.R` to `C` -/
  right : J.R ‚Üí C
  /-- A family of maps from `left a` to `right (J.fst a)` -/
  fst : ‚àÄ a, left a ‚ü∂ right (J.fst a)
  /-- A family of maps from `left a` to `right (J.snd a)` -/
  snd : ‚àÄ a, left a ‚ü∂ right (J.snd a)

namespace MulticospanIndex

variable {C : Type u} [Category.{v} C] {J : MulticospanShape.{w, w'}}
  (I : MulticospanIndex J C)

/-- The multicospan associated to `I : MulticospanIndex`. -/
@[simps]
def multicospan : WalkingMulticospan J ‚•§ C where
  obj x :=
    match x with
    | WalkingMulticospan.left a => I.left a
    | WalkingMulticospan.right b => I.right b
  map {x y} f :=
    match x, y, f with
    | _, _, WalkingMulticospan.Hom.id x => ùüô _
    | _, _, WalkingMulticospan.Hom.fst b => I.fst _
    | _, _, WalkingMulticospan.Hom.snd b => I.snd _
  map_id := by
    rintro (_ | _) <;> rfl
  map_comp := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) <;> cat_disch

/-- The induced map `‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right` via `I.fst` for limiting fans. -/
def fstPiMapOfIsLimit (c : Fan I.left) {d : Fan I.right} (hd : IsLimit d) : c.pt ‚ü∂ d.pt :=
  Fan.IsLimit.lift hd fun i ‚Ü¶ c.proj _ ‚â´ I.fst i

/-- The induced map `‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right` via `I.snd` for limiting fans. -/
def sndPiMapOfIsLimit (c : Fan I.left) {d : Fan I.right} (hd : IsLimit d) : c.pt ‚ü∂ d.pt :=
  Fan.IsLimit.lift hd fun i ‚Ü¶ c.proj _ ‚â´ I.snd i

@[reassoc (attr := simp)]
lemma fstPiMapOfIsLimit_proj (c : Fan I.left) {d : Fan I.right} (hd : IsLimit d) (i) :
    fstPiMapOfIsLimit I c hd ‚â´ d.proj i = c.proj _ ‚â´ I.fst i := by
  simp [fstPiMapOfIsLimit]

@[reassoc (attr := simp)]
lemma sndPiMapOfIsLimit_proj (c : Fan I.left) {d : Fan I.right} (hd : IsLimit d) (i) :
    sndPiMapOfIsLimit I c hd ‚â´ d.proj i = c.proj _ ‚â´ I.snd i := by
  simp [sndPiMapOfIsLimit]

/-- Taking the multiequalizer over the multicospan index is equivalent to taking the equalizer over
the two morphisms `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. This is the diagram of the latter for limiting fans.
-/
@[simps!]
protected noncomputable def parallelPairDiagramOfIsLimit
    (c : Fan I.left) {d : Fan I.right} (hd : IsLimit d) : WalkingParallelPair ‚•§ C :=
  parallelPair (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd)

variable [HasProduct I.left] [HasProduct I.right]

/-- The induced map `‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right` via `I.fst`. -/
noncomputable def fstPiMap : ‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right :=
  I.fstPiMapOfIsLimit _ <| limit.isLimit (Discrete.functor I.right)

/-- The induced map `‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right` via `I.snd`. -/
noncomputable def sndPiMap : ‚àè·∂ú I.left ‚ü∂ ‚àè·∂ú I.right :=
  I.sndPiMapOfIsLimit _ <| limit.isLimit (Discrete.functor I.right)

@[reassoc (attr := simp)]
theorem fstPiMap_œÄ (b) : I.fstPiMap ‚â´ Pi.œÄ I.right b = Pi.œÄ I.left _ ‚â´ I.fst b :=
  fstPiMapOfIsLimit_proj ..

@[reassoc (attr := simp)]
theorem sndPiMap_œÄ (b) : I.sndPiMap ‚â´ Pi.œÄ I.right b = Pi.œÄ I.left _ ‚â´ I.snd b :=
  sndPiMapOfIsLimit_proj ..

/-- Taking the multiequalizer over the multicospan index is equivalent to taking the equalizer over
the two morphisms `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. This is the diagram of the latter.
-/
@[simps!]
protected noncomputable def parallelPairDiagram :=
  parallelPair I.fstPiMap I.sndPiMap

end MulticospanIndex

namespace MultispanIndex

variable {C : Type u} [Category.{v} C] {J : MultispanShape.{w, w'}}
    (I : MultispanIndex J C)

/-- The multispan associated to `I : MultispanIndex`. -/
def multispan : WalkingMultispan J ‚•§ C where
  obj x :=
    match x with
    | WalkingMultispan.left a => I.left a
    | WalkingMultispan.right b => I.right b
  map {x y} f :=
    match x, y, f with
    | _, _, WalkingMultispan.Hom.id x => ùüô _
    | _, _, WalkingMultispan.Hom.fst b => I.fst _
    | _, _, WalkingMultispan.Hom.snd b => I.snd _
  map_id := by
    rintro (_ | _) <;> rfl
  map_comp := by
    rintro (_ | _) (_ | _) (_ | _) (_ | _ | _) (_ | _ | _) <;> cat_disch

@[simp]
theorem multispan_obj_left (a) : I.multispan.obj (WalkingMultispan.left a) = I.left a :=
  rfl

@[simp]
theorem multispan_obj_right (b) : I.multispan.obj (WalkingMultispan.right b) = I.right b :=
  rfl

@[simp]
theorem multispan_map_fst (a) : I.multispan.map (WalkingMultispan.Hom.fst a) = I.fst a :=
  rfl

@[simp]
theorem multispan_map_snd (a) : I.multispan.map (WalkingMultispan.Hom.snd a) = I.snd a :=
  rfl

/-- The induced map `‚àê I.left ‚ü∂ ‚àê I.right` via `I.fst` for colimiting cofans. -/
def fstSigmaMapOfIsColimit {c : Cofan I.left} (d : Cofan I.right) (hc : IsColimit c) :
    c.pt ‚ü∂ d.pt :=
  Cofan.IsColimit.desc hc fun i ‚Ü¶ I.fst i ‚â´ d.inj _

/-- The induced map `‚àê I.left ‚ü∂ ‚àê I.right` via `I.snd` for colimiting cofans. -/
def sndSigmaMapOfIsColimit {c : Cofan I.left} (d : Cofan I.right) (hc : IsColimit c) :
    c.pt ‚ü∂ d.pt :=
  Cofan.IsColimit.desc hc fun i ‚Ü¶ I.snd i ‚â´ d.inj _

@[reassoc (attr := simp)]
lemma inj_fstSigmaMapOfIsColimit {c : Cofan I.left} (d : Cofan I.right) (hc : IsColimit c) (i) :
    c.inj _ ‚â´ fstSigmaMapOfIsColimit I d hc = I.fst i ‚â´ d.inj _ := by
  simp [fstSigmaMapOfIsColimit]

@[reassoc (attr := simp)]
lemma inj_sndSigmaMapOfIsColimit {c : Cofan I.left} (d : Cofan I.right) (hc : IsColimit c) (i) :
    c.inj _ ‚â´ sndSigmaMapOfIsColimit I d hc = I.snd i ‚â´ d.inj _ := by
  simp [sndSigmaMapOfIsColimit]

/-- Taking the multicoequalizer over the multispan index is equivalent to taking the coequalizer
over the two morphisms `‚àê I.left ‚áâ ‚àê I.right`. This is the diagram of the latter for colimiting
cofans. -/
@[simps!]
protected noncomputable def parallelPairDiagramOfIsColimit
    {c : Cofan I.left} (d : Cofan I.right) (hc : IsColimit c) : WalkingParallelPair ‚•§ C :=
  parallelPair (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc)

variable [HasCoproduct I.left] [HasCoproduct I.right]

/-- The induced map `‚àê I.left ‚ü∂ ‚àê I.right` via `I.fst`. -/
noncomputable def fstSigmaMap : ‚àê I.left ‚ü∂ ‚àê I.right :=
  I.fstSigmaMapOfIsColimit _ <| colimit.isColimit _

/-- The induced map `‚àê I.left ‚ü∂ ‚àê I.right` via `I.snd`. -/
noncomputable def sndSigmaMap : ‚àê I.left ‚ü∂ ‚àê I.right :=
  I.sndSigmaMapOfIsColimit _ <| colimit.isColimit _

@[reassoc (attr := simp)]
theorem Œπ_fstSigmaMap (b) : Sigma.Œπ I.left b ‚â´ I.fstSigmaMap = I.fst b ‚â´ Sigma.Œπ I.right _ :=
  inj_fstSigmaMapOfIsColimit ..

@[reassoc (attr := simp)]
theorem Œπ_sndSigmaMap (b) : Sigma.Œπ I.left b ‚â´ I.sndSigmaMap = I.snd b ‚â´ Sigma.Œπ I.right _ :=
  inj_sndSigmaMapOfIsColimit ..

/--
Taking the multicoequalizer over the multispan index is equivalent to taking the coequalizer over
the two morphisms `‚àê I.left ‚áâ ‚àê I.right`. This is the diagram of the latter.
-/
protected noncomputable abbrev parallelPairDiagram :=
  parallelPair I.fstSigmaMap I.sndSigmaMap

end MultispanIndex

variable {C : Type u} [Category.{v} C]

/-- A multifork is a cone over a multicospan. -/
abbrev Multifork {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C) :=
  Cone I.multicospan

/-- A multicofork is a cocone over a multispan. -/
abbrev Multicofork {J : MultispanShape.{w, w'}} (I : MultispanIndex J C) :=
  Cocone I.multispan

namespace Multifork

variable {J : MulticospanShape.{w, w'}} {I : MulticospanIndex J C} (K : Multifork I)

/-- The maps from the cone point of a multifork to the objects on the left. -/
def Œπ (a : J.L) : K.pt ‚ü∂ I.left a :=
  K.œÄ.app (WalkingMulticospan.left _)

@[simp]
theorem app_left_eq_Œπ (a) : K.œÄ.app (WalkingMulticospan.left a) = K.Œπ a :=
  rfl

@[simp]
theorem app_right_eq_Œπ_comp_fst (b) :
    K.œÄ.app (WalkingMulticospan.right b) = K.Œπ (J.fst b) ‚â´ I.fst b := by
  rw [‚Üê K.w (WalkingMulticospan.Hom.fst b)]
  rfl

@[reassoc]
theorem app_right_eq_Œπ_comp_snd (b) :
    K.œÄ.app (WalkingMulticospan.right b) = K.Œπ (J.snd b) ‚â´ I.snd b := by
  rw [‚Üê K.w (WalkingMulticospan.Hom.snd b)]
  rfl

@[reassoc (attr := simp)]
theorem hom_comp_Œπ (K‚ÇÅ K‚ÇÇ : Multifork I) (f : K‚ÇÅ ‚ü∂ K‚ÇÇ) (j : J.L) : f.hom ‚â´ K‚ÇÇ.Œπ j = K‚ÇÅ.Œπ j :=
  f.w _

/-- Construct a multifork using a collection `Œπ` of morphisms. -/
@[simps]
def ofŒπ {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
    (P : C) (Œπ : ‚àÄ a, P ‚ü∂ I.left a)
    (w : ‚àÄ b, Œπ (J.fst b) ‚â´ I.fst b = Œπ (J.snd b) ‚â´ I.snd b) : Multifork I where
  pt := P
  œÄ :=
    { app := fun x =>
        match x with
        | WalkingMulticospan.left _ => Œπ _
        | WalkingMulticospan.right b => Œπ (J.fst b) ‚â´ I.fst b
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;>
          dsimp <;> simp only [Category.id_comp, Category.comp_id]
        apply w }

@[simp]
lemma Œπ_ofŒπ {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
    (P : C) (Œπ : ‚àÄ a, P ‚ü∂ I.left a)
    (w : ‚àÄ b, Œπ (J.fst b) ‚â´ I.fst b = Œπ (J.snd b) ‚â´ I.snd b) (i) :
    (ofŒπ I P Œπ w).Œπ i = Œπ i :=
  rfl

@[reassoc (attr := simp)]
theorem condition (b) : K.Œπ (J.fst b) ‚â´ I.fst b = K.Œπ (J.snd b) ‚â´ I.snd b := by
  rw [‚Üê app_right_eq_Œπ_comp_fst, ‚Üê app_right_eq_Œπ_comp_snd]

/-- Constructor for isomorphisms between multiforks. -/
@[simps!]
def ext {t s : Multifork I} (e : t.pt ‚âÖ s.pt)
    (h : ‚àÄ i : J.L, e.hom ‚â´ s.Œπ i = t.Œπ i := by cat_disch) : t ‚âÖ s :=
  Cones.ext e (by rintro (i | j) <;> simp [‚Üê h])

/-- Every multifork is isomorphic to one of the form `Multifork.ofŒπ`. -/
@[simps!]
def isoOfŒπ (t : Multifork I) : t ‚âÖ ofŒπ _ t.pt t.Œπ t.condition :=
  ext (Iso.refl _)

/-- This definition provides a convenient way to show that a multifork is a limit. -/
@[simps]
def IsLimit.mk (lift : ‚àÄ E : Multifork I, E.pt ‚ü∂ K.pt)
    (fac : ‚àÄ (E : Multifork I) (i : J.L), lift E ‚â´ K.Œπ i = E.Œπ i)
    (uniq : ‚àÄ (E : Multifork I) (m : E.pt ‚ü∂ K.pt), (‚àÄ i : J.L, m ‚â´ K.Œπ i = E.Œπ i) ‚Üí m = lift E) :
    IsLimit K :=
  { lift
    fac := by
      rintro E (a | b)
      ¬∑ apply fac
      ¬∑ rw [‚Üê E.w (WalkingMulticospan.Hom.fst b), ‚Üê K.w (WalkingMulticospan.Hom.fst b), ‚Üê
          Category.assoc]
        congr 1
        apply fac
    uniq := by
      rintro E m hm
      apply uniq
      intro i
      apply hm }

variable {K}

lemma IsLimit.hom_ext (hK : IsLimit K) {T : C} {f g : T ‚ü∂ K.pt}
    (h : ‚àÄ a, f ‚â´ K.Œπ a = g ‚â´ K.Œπ a) : f = g := by
  apply hK.hom_ext
  rintro (_ | b)
  ¬∑ apply h
  ¬∑ dsimp
    rw [app_right_eq_Œπ_comp_fst, reassoc_of% h]

/-- Constructor for morphisms to the point of a limit multifork. -/
def IsLimit.lift (hK : IsLimit K) {T : C} (k : ‚àÄ a, T ‚ü∂ I.left a)
    (hk : ‚àÄ b, k (J.fst b) ‚â´ I.fst b = k (J.snd b) ‚â´ I.snd b) :
    T ‚ü∂ K.pt :=
  hK.lift (Multifork.ofŒπ _ _ k hk)

@[reassoc (attr := simp)]
lemma IsLimit.fac (hK : IsLimit K) {T : C} (k : ‚àÄ a, T ‚ü∂ I.left a)
    (hk : ‚àÄ b, k (J.fst b) ‚â´ I.fst b = k (J.snd b) ‚â´ I.snd b) (a : J.L) :
    IsLimit.lift hK k hk ‚â´ K.Œπ a = k a :=
  hK.fac _ _

/-- Given two multiforks with isomorphic components in such a way that the natural diagrams
commute, then one is a limit if and only if the other one is. -/
def isLimitEquivOfIsos {I I' : MulticospanIndex J C} (c : Multifork I) (c' : Multifork I')
    (e : c.pt ‚âÖ c'.pt) (el : ‚àÄ i, I.left i ‚âÖ I'.left i) (er : ‚àÄ i, I.right i ‚âÖ I'.right i)
    (hl : ‚àÄ (i : J.R), I.fst i ‚â´ (er i).hom = (el (J.fst i)).hom ‚â´ I'.fst i := by cat_disch)
    (hr : ‚àÄ (i : J.R), I.snd i ‚â´ (er i).hom = (el (J.snd i)).hom ‚â´ I'.snd i := by cat_disch)
    (he : ‚àÄ (i : J.L), e.hom ‚â´ c'.Œπ i = c.Œπ i ‚â´ (el i).hom := by cat_disch) :
    IsLimit c ‚âÉ IsLimit c' :=
  letI i : I.multicospan ‚âÖ I'.multicospan :=
    WalkingMulticospan.functorExt el er hl hr
  IsLimit.equivOfNatIsoOfIso i _ _ (Multifork.ext e he)

variable (K)
variable {c : Fan I.left} (hc : IsLimit c) {d : Fan I.right} (hd : IsLimit d)

@[reassoc (attr := simp)]
theorem pi_condition :
    Fan.IsLimit.lift hc K.Œπ ‚â´ I.fstPiMapOfIsLimit c hd =
      Fan.IsLimit.lift hc K.Œπ ‚â´ I.sndPiMapOfIsLimit c hd := by
  apply Fan.IsLimit.hom_ext hd
  simp

/-- Given a multifork, we may obtain a fork over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. -/
@[simps! pt]
def toPiFork (K : Multifork I) :
    Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd) :=
  .ofŒπ (Fan.IsLimit.lift hc K.Œπ) (by simp)

@[simp]
theorem toPiFork_œÄ_app_zero :
    (K.toPiFork hc hd).Œπ = Fan.IsLimit.lift hc K.Œπ :=
  rfl

@[simp]
theorem toPiFork_œÄ_app_one :
    (K.toPiFork hc hd).œÄ.app WalkingParallelPair.one =
      Fan.IsLimit.lift hc K.Œπ ‚â´ I.fstPiMapOfIsLimit c hd :=
  rfl

variable {hd} in
/-- Given a fork over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`, we may obtain a multifork. -/
@[simps pt]
def ofPiFork
    (a : Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd)) :
    Multifork I where
  pt := a.pt
  œÄ.app
    | WalkingMulticospan.left _ => a.Œπ ‚â´ c.proj _
    | WalkingMulticospan.right _ => a.Œπ ‚â´ I.fstPiMapOfIsLimit c hd ‚â´ d.proj _
  œÄ.naturality := by
    rintro (_ | _) (_ | _) (_ | _ | _)
    ¬∑ simp
    ¬∑ simp
    ¬∑ dsimp; rw [a.condition_assoc]; simp
    ¬∑ simp

@[simp]
theorem ofPiFork_Œπ (a : Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd)) (i) :
    (ofPiFork a).Œπ i = a.Œπ ‚â´ c.proj _ :=
  rfl

@[deprecated (since := "2025-12-08")]
alias ofPiFork_œÄ_app_left := ofPiFork_Œπ

@[simp]
theorem ofPiFork_œÄ_app_right
    (a : Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd)) (i) :
    (ofPiFork a).œÄ.app (WalkingMulticospan.right i) =
      a.Œπ ‚â´ I.fstPiMapOfIsLimit c hd ‚â´ d.proj _ :=
  rfl

end Multifork

namespace MulticospanIndex

variable {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
variable {c : Fan I.left} (hc : IsLimit c) {d : Fan I.right} (hd : IsLimit d)

/-- `Multifork.toPiFork` as a functor. -/
@[simps]
def toPiForkFunctor :
    Multifork I ‚•§ Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd) where
  obj := Multifork.toPiFork hc hd
  map {K‚ÇÅ K‚ÇÇ} f :=
    { hom := f.hom
      w := by
        rintro (_ | _)
        ¬∑ apply Fan.IsLimit.hom_ext hc
          simp
        ¬∑ apply Fan.IsLimit.hom_ext hd
          simp }

/-- `Multifork.ofPiFork` as a functor. -/
@[simps]
def ofPiForkFunctor :
    Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd) ‚•§ Multifork I where
  obj := Multifork.ofPiFork
  map {K‚ÇÅ K‚ÇÇ} f :=
    { hom := f.hom
      w := by rintro (_ | _) <;> simp }

/-- The category of multiforks is equivalent to the category of forks over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`.
It then follows from `CategoryTheory.IsLimit.ofPreservesConeTerminal` (or `reflects`) that it
preserves and reflects limit cones.
-/
@[simps]
def multiforkEquivPiForkOfIsLimit :
    Multifork I ‚âå Fork (I.fstPiMapOfIsLimit c hd) (I.sndPiMapOfIsLimit c hd) where
  functor := toPiForkFunctor I hc hd
  inverse := ofPiForkFunctor I hd
  unitIso :=
    NatIso.ofComponents fun K =>
      Cones.ext (Iso.refl _) (by
        rintro (_ | _) <;> simp)
  counitIso :=
    NatIso.ofComponents (fun K =>
      Fork.ext (Iso.refl _) <| Fan.IsLimit.hom_ext hc _ _ (by simp))

variable [HasProduct I.left] [HasProduct I.right]

/-- The category of multiforks is equivalent to the category of forks over `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`.
It then follows from `CategoryTheory.IsLimit.ofPreservesConeTerminal` (or `reflects`) that it
preserves and reflects limit cones.
-/
@[simps!]
noncomputable def multiforkEquivPiFork : Multifork I ‚âå Fork I.fstPiMap I.sndPiMap :=
  multiforkEquivPiForkOfIsLimit I (limit.isLimit _) (limit.isLimit _)

/-- The constant `MulticospanShape` for a pair of parallel morphisms. -/
@[simps]
def ofParallelHoms (J : MulticospanShape) {X Y : C} (f g : X ‚ü∂ Y) : MulticospanIndex J C where
  left _ := X
  right _ := Y
  fst _ := f
  snd _ := g

/-- A fork on a pair of morphisms `f` and `g` is the same as a multifork on the
single point index defined by `f` and `g`. -/
def multiforkOfParallelHomsEquivFork (J : MulticospanShape) [Unique J.L] [Unique J.R] {X Y : C}
    (f g : X ‚ü∂ Y) :
    Multifork (ofParallelHoms J f g) ‚âå Fork f g := by
  refine (multiforkEquivPiForkOfIsLimit _
      (Fan.isLimitMkOfUnique (Iso.refl X) _) (Fan.isLimitMkOfUnique (Iso.refl Y) _)).trans
      (Fork.equivOfIsos (.refl _) (.refl _) ?_ ?_)
  ¬∑ refine Fan.IsLimit.hom_ext (Fan.isLimitMkOfUnique (Iso.refl Y) J.R) _ _ fun _ ‚Ü¶ ?_
    rw [Category.assoc, Iso.refl_hom ((Fan.mk Y fun x ‚Ü¶ (Iso.refl Y).hom).pt),
      Category.comp_id, fstPiMapOfIsLimit_proj]
    simp
  ¬∑ refine Fan.IsLimit.hom_ext (Fan.isLimitMkOfUnique (Iso.refl Y) J.R) _ _ fun _ ‚Ü¶ ?_
    rw [Category.assoc, Iso.refl_hom ((Fan.mk Y fun x ‚Ü¶ (Iso.refl Y).hom).pt),
      Category.comp_id, sndPiMapOfIsLimit_proj]
    simp

@[simp]
lemma multiforkOfParallelHomsEquivFork_functor_obj_Œπ (J : MulticospanShape) [Unique J.L]
    [Unique J.R] {X Y : C} (f g : X ‚ü∂ Y) (c : Multifork (ofParallelHoms J f g)) :
    ((multiforkOfParallelHomsEquivFork J f g).functor.obj c).Œπ = c.Œπ default :=
  Fan.IsLimit.fac (Fan.isLimitMkOfUnique (Iso.refl X) J.L) _ default

@[simp]
lemma multiforkOfParallelHomsEquivFork_inverse_obj_Œπ (J : MulticospanShape) [Unique J.L]
    [Unique J.R] {X Y : C} (f g : X ‚ü∂ Y) (c : Fork f g) (a : J.L) :
    ((multiforkOfParallelHomsEquivFork J f g).inverse.obj c).Œπ a = c.Œπ := by
  simp [multiforkOfParallelHomsEquivFork]

end MulticospanIndex

namespace Multicofork

variable {J : MultispanShape.{w, w'}} {I : MultispanIndex J C} (K : Multicofork I)

/-- The maps to the cocone point of a multicofork from the objects on the right. -/
def œÄ (b : J.R) : I.right b ‚ü∂ K.pt :=
  K.Œπ.app (WalkingMultispan.right _)

@[simp]
theorem œÄ_eq_app_right (b) : K.Œπ.app (WalkingMultispan.right _) = K.œÄ b :=
  rfl

@[simp]
theorem fst_app_right (a) : K.Œπ.app (WalkingMultispan.left a) = I.fst a ‚â´ K.œÄ _ := by
  rw [‚Üê K.w (WalkingMultispan.Hom.fst a)]
  rfl

@[reassoc]
theorem snd_app_right (a) : K.Œπ.app (WalkingMultispan.left a) = I.snd a ‚â´ K.œÄ _ := by
  rw [‚Üê K.w (WalkingMultispan.Hom.snd a)]
  rfl

@[reassoc (attr := simp)]
lemma œÄ_comp_hom (K‚ÇÅ K‚ÇÇ : Multicofork I) (f : K‚ÇÅ ‚ü∂ K‚ÇÇ) (b : J.R) : K‚ÇÅ.œÄ b ‚â´ f.hom = K‚ÇÇ.œÄ b :=
  f.w _

/-- Construct a multicofork using a collection `œÄ` of morphisms. -/
@[simps]
def ofœÄ {J : MultispanShape.{w, w'}} (I : MultispanIndex J C)
    (P : C) (œÄ : ‚àÄ b, I.right b ‚ü∂ P)
    (w : ‚àÄ a, I.fst a ‚â´ œÄ (J.fst a) = I.snd a ‚â´ œÄ (J.snd a)) : Multicofork I where
  pt := P
  Œπ :=
    { app := fun x =>
        match x with
        | WalkingMultispan.left a => I.fst a ‚â´ œÄ _
        | WalkingMultispan.right _ => œÄ _
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _) <;> dsimp <;>
          simp only [Functor.map_id, MultispanIndex.multispan_obj_left,
            Category.id_comp, Category.comp_id, MultispanIndex.multispan_obj_right]
        symm
        apply w }

@[reassoc (attr := simp)]
theorem condition (a) : I.fst a ‚â´ K.œÄ (J.fst a) = I.snd a ‚â´ K.œÄ (J.snd a) := by
  rw [‚Üê K.snd_app_right, ‚Üê K.fst_app_right]

/-- This definition provides a convenient way to show that a multicofork is a colimit. -/
@[simps]
def IsColimit.mk (desc : ‚àÄ E : Multicofork I, K.pt ‚ü∂ E.pt)
    (fac : ‚àÄ (E : Multicofork I) (i : J.R), K.œÄ i ‚â´ desc E = E.œÄ i)
    (uniq : ‚àÄ (E : Multicofork I) (m : K.pt ‚ü∂ E.pt), (‚àÄ i : J.R, K.œÄ i ‚â´ m = E.œÄ i) ‚Üí m = desc E) :
    IsColimit K :=
  { desc
    fac := by
      rintro S (a | b)
      ¬∑ rw [‚Üê K.w (WalkingMultispan.Hom.fst a), ‚Üê S.w (WalkingMultispan.Hom.fst a),
          Category.assoc]
        congr 1
        apply fac
      ¬∑ apply fac
    uniq := by
      intro S m hm
      apply uniq
      intro i
      apply hm }

variable {K}

lemma IsColimit.hom_ext (hK : IsColimit K) {T : C} {f g : K.pt ‚ü∂ T}
    (h : ‚àÄ a, K.œÄ a ‚â´ f = K.œÄ a ‚â´ g) : f = g := by
  apply hK.hom_ext
  rintro (_ | _) <;> simp [h]

/-- Constructor for morphisms from the point of a colimit multicofork. -/
def IsColimit.desc (hK : IsColimit K) {T : C} (k : ‚àÄ a, I.right a ‚ü∂ T)
    (hk : ‚àÄ b, I.fst b ‚â´ k (J.fst b) = I.snd b ‚â´ k (J.snd b)) :
    K.pt ‚ü∂ T :=
  hK.desc (Multicofork.ofœÄ _ _ k hk)

@[reassoc (attr := simp)]
lemma IsColimit.fac (hK : IsColimit K) {T : C} (k : ‚àÄ a, I.right a ‚ü∂ T)
    (hk : ‚àÄ b, I.fst b ‚â´ k (J.fst b) = I.snd b ‚â´ k (J.snd b)) (a : J.R) :
    K.œÄ a ‚â´ IsColimit.desc hK k hk = k a :=
  hK.fac _ _

variable (K)
variable {c : Cofan I.left} (hc : IsColimit c) {d : Cofan I.right} (hd : IsColimit d)

@[reassoc (attr := simp)]
theorem sigma_condition :
    I.fstSigmaMapOfIsColimit d hc ‚â´ Cofan.IsColimit.desc hd K.œÄ =
      I.sndSigmaMapOfIsColimit d hc ‚â´ Cofan.IsColimit.desc hd K.œÄ := by
  apply Cofan.IsColimit.hom_ext hc
  simp

/-- Given a multicofork, we may obtain a cofork over `‚àê I.left ‚áâ ‚àê I.right`. -/
@[simps! pt]
noncomputable def toSigmaCofork (K : Multicofork I) :
    Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc) :=
  .ofœÄ (Cofan.IsColimit.desc hd K.œÄ) (by simp)

@[simp]
theorem toSigmaCofork_œÄ :
    (K.toSigmaCofork hc hd).œÄ = Cofan.IsColimit.desc hd K.œÄ :=
  rfl

variable {hc} in
/-- Given a cofork over `‚àê I.left ‚áâ ‚àê I.right`, we may obtain a multicofork. -/
@[simps pt]
noncomputable def ofSigmaCofork
    (a : Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc)) :
    Multicofork I where
  pt := a.pt
  Œπ :=
    { app := fun x =>
        match x with
        | WalkingMultispan.left _ => c.inj _ ‚â´ I.fstSigmaMapOfIsColimit d hc ‚â´ a.œÄ
        | WalkingMultispan.right _ => d.inj _ ‚â´ a.œÄ
      naturality := by
        rintro (_ | _) (_ | _) (_ | _ | _)
        ¬∑ simp
        ¬∑ simp
        ¬∑ simp [a.condition]
        ¬∑ simp }

@[simp]
theorem ofSigmaCofork_Œπ_app_left
    (a : Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc)) (i) :
    (ofSigmaCofork a).Œπ.app (WalkingMultispan.left i) =
      c.inj _ ‚â´ I.fstSigmaMapOfIsColimit d hc ‚â´ a.œÄ :=
  rfl

@[simp]
theorem ofSigmaCofork_œÄ
    (a : Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc)) (i) :
    (ofSigmaCofork a).œÄ i = d.inj i ‚â´ a.œÄ :=
  rfl

@[deprecated (since := "2025-12-08")]
alias ofSigmaCofork_Œπ_app_right := ofSigmaCofork_œÄ

@[deprecated (since := "2025-12-08")]
alias ofSigmaCofork_Œπ_app_right' := ofSigmaCofork_œÄ

/-- Constructor for isomorphisms between multicoforks. -/
@[simps!]
def ext {K K' : Multicofork I}
    (e : K.pt ‚âÖ K'.pt) (h : ‚àÄ (i : J.R), K.œÄ i ‚â´ e.hom = K'.œÄ i := by cat_disch) :
    K ‚âÖ K' :=
  Cocones.ext e (by rintro (i | j) <;> simp [h])

/-- Every multicofork is isomorphic to one of the form `Multicofork.ofœÄ`. -/
@[simps!]
def isoOfœÄ (t : Multicofork I) : t ‚âÖ ofœÄ _ t.pt t.œÄ t.condition :=
  ext (Iso.refl _)

end Multicofork

namespace MultispanIndex

variable {J : MultispanShape.{w, w'}} (I : MultispanIndex J C)
variable {c : Cofan I.left} (hc : IsColimit c) {d : Cofan I.right} (hd : IsColimit d)

/-- `Multicofork.toSigmaCofork` as a functor. -/
@[simps]
noncomputable def toSigmaCoforkFunctor :
    Multicofork I ‚•§ Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc) where
  obj := Multicofork.toSigmaCofork hc hd
  map {K‚ÇÅ K‚ÇÇ} f :=
  { hom := f.hom
    w := by
      rintro (_ | _)
      ¬∑ apply Cofan.IsColimit.hom_ext hc
        simp
      ¬∑ apply Cofan.IsColimit.hom_ext hd
        simp }

/-- `Multicofork.ofSigmaCofork` as a functor. -/
@[simps]
noncomputable def ofSigmaCoforkFunctor :
    Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc) ‚•§ Multicofork I where
  obj := Multicofork.ofSigmaCofork
  map {K‚ÇÅ K‚ÇÇ} f :=
    { hom := f.hom
      w := by rintro (_ | _) <;> simp }

/--
The category of multicoforks is equivalent to the category of coforks over `‚àê I.left ‚áâ ‚àê I.right`.
It then follows from `CategoryTheory.IsColimit.ofPreservesCoconeInitial` (or `reflects`) that
it preserves and reflects colimit cocones.
-/
@[simps]
noncomputable def multicoforkEquivSigmaCoforkOfIsColimit :
    Multicofork I ‚âå Cofork (I.fstSigmaMapOfIsColimit d hc) (I.sndSigmaMapOfIsColimit d hc) where
  functor := toSigmaCoforkFunctor I hc hd
  inverse := ofSigmaCoforkFunctor I hc
  unitIso := NatIso.ofComponents fun K => Cocones.ext (Iso.refl _) (by
      rintro (_ | _) <;> simp)
  counitIso := NatIso.ofComponents fun K =>
    Cofork.ext (Iso.refl _)
      (by
        apply Cofan.IsColimit.hom_ext hd
        simp)

variable [HasCoproduct I.left] [HasCoproduct I.right]

/--
The category of multicoforks is equivalent to the category of coforks over `‚àê I.left ‚áâ ‚àê I.right`.
It then follows from `CategoryTheory.IsColimit.ofPreservesCoconeInitial` (or `reflects`) that
it preserves and reflects colimit cocones.
-/
@[simps!]
noncomputable def multicoforkEquivSigmaCofork :
    Multicofork I ‚âå Cofork I.fstSigmaMap I.sndSigmaMap :=
  multicoforkEquivSigmaCoforkOfIsColimit _ (colimit.isColimit _) (colimit.isColimit _)

end MultispanIndex

/-- For `I : MulticospanIndex J C`, we say that it has a multiequalizer if the associated
  multicospan has a limit. -/
abbrev HasMultiequalizer {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C) :=
  HasLimit I.multicospan

noncomputable section

/-- The multiequalizer of `I : MulticospanIndex J C`. -/
abbrev multiequalizer {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C)
    [HasMultiequalizer I] : C :=
  limit I.multicospan

/-- For `I : MultispanIndex J C`, we say that it has a multicoequalizer if
  the associated multicospan has a limit. -/
abbrev HasMulticoequalizer {J : MultispanShape.{w, w'}} (I : MultispanIndex J C) :=
  HasColimit I.multispan

/-- The multicoequalizer of `I : MultispanIndex J C`. -/
abbrev multicoequalizer {J : MultispanShape.{w, w'}} (I : MultispanIndex J C)
    [HasMulticoequalizer I] : C :=
  colimit I.multispan

namespace Multiequalizer

variable {J : MulticospanShape.{w, w'}} (I : MulticospanIndex J C) [HasMultiequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbrev Œπ (a : J.L) : multiequalizer I ‚ü∂ I.left a :=
  limit.œÄ _ (WalkingMulticospan.left a)

/-- The multifork associated to the multiequalizer. -/
abbrev multifork : Multifork I :=
  limit.cone _

@[simp]
theorem multifork_Œπ (a) : (Multiequalizer.multifork I).Œπ a = Multiequalizer.Œπ I a :=
  rfl

@[simp]
theorem multifork_œÄ_app_left (a) :
    (Multiequalizer.multifork I).œÄ.app (WalkingMulticospan.left a) = Multiequalizer.Œπ I a :=
  rfl

@[reassoc]
theorem condition (b) :
    Multiequalizer.Œπ I (J.fst b) ‚â´ I.fst b = Multiequalizer.Œπ I (J.snd b) ‚â´ I.snd b :=
  Multifork.condition _ _

/-- Construct a morphism to the multiequalizer from its universal property. -/
abbrev lift (W : C) (k : ‚àÄ a, W ‚ü∂ I.left a)
    (h : ‚àÄ b, k (J.fst b) ‚â´ I.fst b = k (J.snd b) ‚â´ I.snd b) : W ‚ü∂ multiequalizer I :=
  limit.lift _ (Multifork.ofŒπ I _ k h)

@[reassoc]
theorem lift_Œπ (W : C) (k : ‚àÄ a, W ‚ü∂ I.left a)
    (h : ‚àÄ b, k (J.fst b) ‚â´ I.fst b = k (J.snd b) ‚â´ I.snd b) (a) :
    Multiequalizer.lift I _ k h ‚â´ Multiequalizer.Œπ I a = k _ :=
  limit.lift_œÄ _ _

@[ext]
theorem hom_ext {W : C} (i j : W ‚ü∂ multiequalizer I)
    (h : ‚àÄ a, i ‚â´ Multiequalizer.Œπ I a = j ‚â´ Multiequalizer.Œπ I a) : i = j :=
  Multifork.IsLimit.hom_ext (limit.isLimit _) h

variable [HasProduct I.left] [HasProduct I.right]

instance : HasEqualizer I.fstPiMap I.sndPiMap :=
  ‚ü®‚ü®‚ü®_, IsLimit.ofPreservesConeTerminal I.multiforkEquivPiFork.functor (limit.isLimit _)‚ü©‚ü©‚ü©

/-- The multiequalizer is isomorphic to the equalizer of `‚àè·∂ú I.left ‚áâ ‚àè·∂ú I.right`. -/
def isoEqualizer : multiequalizer I ‚âÖ equalizer I.fstPiMap I.sndPiMap :=
  limit.isoLimitCone
    ‚ü®_, IsLimit.ofPreservesConeTerminal I.multiforkEquivPiFork.inverse (limit.isLimit _)‚ü©

/-- The canonical injection `multiequalizer I ‚ü∂ ‚àè·∂ú I.left`. -/
def ŒπPi : multiequalizer I ‚ü∂ ‚àè·∂ú I.left :=
  (isoEqualizer I).hom ‚â´ equalizer.Œπ I.fstPiMap I.sndPiMap

@[reassoc (attr := simp)]
theorem ŒπPi_œÄ (a) : ŒπPi I ‚â´ Pi.œÄ I.left a = Œπ I a := by
  rw [ŒπPi, Category.assoc, ‚Üê Iso.eq_inv_comp, isoEqualizer]
  simp only [limit.isoLimitCone_inv_œÄ, MulticospanIndex.multiforkEquivPiFork_inverse_obj_pt,
    limit.cone_x, MulticospanIndex.multiforkEquivPiFork_inverse_obj_œÄ_app]
  rfl

instance : Mono (ŒπPi I) := mono_comp _ _

end Multiequalizer

namespace Multicoequalizer

variable {J : MultispanShape.{w, w'}} (I : MultispanIndex J C) [HasMulticoequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbrev œÄ (b : J.R) : I.right b ‚ü∂ multicoequalizer I :=
  colimit.Œπ I.multispan (WalkingMultispan.right _)

/-- The multicofork associated to the multicoequalizer. -/
abbrev multicofork : Multicofork I :=
  colimit.cocone _

@[simp]
theorem multicofork_œÄ (b) : (Multicoequalizer.multicofork I).œÄ b = Multicoequalizer.œÄ I b :=
  rfl

theorem multicofork_Œπ_app_right (b) :
    (Multicoequalizer.multicofork I).Œπ.app (WalkingMultispan.right b) = Multicoequalizer.œÄ I b :=
  rfl

/-- `@[simp]`-normal form of multicofork_Œπ_app_right. -/
@[simp]
theorem multicofork_Œπ_app_right' (b) :
    colimit.Œπ (MultispanIndex.multispan I) (WalkingMultispan.right b) = œÄ I b :=
  rfl

@[reassoc]
theorem condition (a) :
    I.fst a ‚â´ Multicoequalizer.œÄ I (J.fst a) = I.snd a ‚â´ Multicoequalizer.œÄ I (J.snd a) :=
  Multicofork.condition _ _

/-- Construct a morphism from the multicoequalizer from its universal property. -/
abbrev desc (W : C) (k : ‚àÄ b, I.right b ‚ü∂ W)
    (h : ‚àÄ a, I.fst a ‚â´ k (J.fst a) = I.snd a ‚â´ k (J.snd a)) : multicoequalizer I ‚ü∂ W :=
  colimit.desc _ (Multicofork.ofœÄ I _ k h)

@[reassoc]
theorem œÄ_desc (W : C) (k : ‚àÄ b, I.right b ‚ü∂ W)
    (h : ‚àÄ a, I.fst a ‚â´ k (J.fst a) = I.snd a ‚â´ k (J.snd a)) (b) :
    Multicoequalizer.œÄ I b ‚â´ Multicoequalizer.desc I _ k h = k _ :=
  colimit.Œπ_desc _ _

@[ext]
theorem hom_ext {W : C} (i j : multicoequalizer I ‚ü∂ W)
    (h : ‚àÄ b, Multicoequalizer.œÄ I b ‚â´ i = Multicoequalizer.œÄ I b ‚â´ j) : i = j :=
  colimit.hom_ext
    (by
      rintro (a | b)
      ¬∑ simp_rw [‚Üê colimit.w I.multispan (WalkingMultispan.Hom.fst a), Category.assoc, h]
      ¬∑ apply h)

variable [HasCoproduct I.left] [HasCoproduct I.right]

instance : HasCoequalizer I.fstSigmaMap I.sndSigmaMap :=
  ‚ü®‚ü®‚ü®_,
      IsColimit.ofPreservesCoconeInitial
        I.multicoforkEquivSigmaCofork.functor (colimit.isColimit _)‚ü©‚ü©‚ü©

/-- The multicoequalizer is isomorphic to the coequalizer of `‚àê I.left ‚áâ ‚àê I.right`. -/
def isoCoequalizer : multicoequalizer I ‚âÖ coequalizer I.fstSigmaMap I.sndSigmaMap :=
  colimit.isoColimitCocone
    ‚ü®_,
      IsColimit.ofPreservesCoconeInitial I.multicoforkEquivSigmaCofork.inverse
        (colimit.isColimit _)‚ü©

/-- The canonical projection `‚àê I.right ‚ü∂ multicoequalizer I`. -/
def sigmaœÄ : ‚àê I.right ‚ü∂ multicoequalizer I :=
  coequalizer.œÄ I.fstSigmaMap I.sndSigmaMap ‚â´ (isoCoequalizer I).inv

@[reassoc (attr := simp)]
theorem Œπ_sigmaœÄ (b) : Sigma.Œπ I.right b ‚â´ sigmaœÄ I = œÄ I b := by
  rw [sigmaœÄ, ‚Üê Category.assoc, Iso.comp_inv_eq, isoCoequalizer]
  simp
  rfl

instance : Epi (sigmaœÄ I) := epi_comp _ _

end Multicoequalizer

end

/-- The inclusion functor `WalkingMultispan (.ofLinearOrder Œπ) ‚•§ WalkingMultispan (.prod Œπ)`. -/
@[simps!]
def WalkingMultispan.inclusionOfLinearOrder (Œπ : Type w) [LinearOrder Œπ] :
    WalkingMultispan (.ofLinearOrder Œπ) ‚•§ WalkingMultispan (.prod Œπ) :=
  MultispanIndex.multispan
    { left j := .left j.1
      right i := .right i
      fst j := WalkingMultispan.Hom.fst (J := .prod Œπ) j.1
      snd j := WalkingMultispan.Hom.snd (J := .prod Œπ) j.1 }

section symmetry

namespace MultispanIndex

variable {Œπ : Type w} (I : MultispanIndex (.prod Œπ) C)

/-- Structure expressing a symmetry of `I : MultispanIndex (.prod Œπ) C` which
allows to compare the corresponding multicoequalizer to the multicoequalizer
of `I.toLinearOrder`. -/
structure SymmStruct where
  /-- the symmetry isomorphism -/
  iso (i j : Œπ) : I.left ‚ü®i, j‚ü© ‚âÖ I.left ‚ü®j, i‚ü©
  iso_hom_fst (i j : Œπ) : (iso i j).hom ‚â´ I.fst ‚ü®j, i‚ü© = I.snd ‚ü®i, j‚ü©
  iso_hom_snd (i j : Œπ) : (iso i j).hom ‚â´ I.snd ‚ü®j, i‚ü© = I.fst ‚ü®i, j‚ü©
  fst_eq_snd (i : Œπ) : I.fst ‚ü®i, i‚ü© = I.snd ‚ü®i, i‚ü©

attribute [reassoc] SymmStruct.iso_hom_fst SymmStruct.iso_hom_snd

variable [LinearOrder Œπ]

/-- The multispan index for `MultispanShape.ofLinearOrder Œπ` deduced from
a multispan index for `MultispanShape.prod Œπ` when `Œπ` is linearly ordered. -/
@[simps]
def toLinearOrder : MultispanIndex (.ofLinearOrder Œπ) C where
  left j := I.left j.1
  right i := I.right i
  fst j := I.fst j.1
  snd j := I.snd j.1

/-- Given a linearly ordered type `Œπ` and `I : MultispanIndex (.prod Œπ) C`,
this is the isomorphism of functors between
`WalkingMultispan.inclusionOfLinearOrder Œπ ‚ãô I.multispan`
and `I.toLinearOrder.multispan`. -/
@[simps!]
def toLinearOrderMultispanIso :
    WalkingMultispan.inclusionOfLinearOrder Œπ ‚ãô I.multispan ‚âÖ
      I.toLinearOrder.multispan :=
  NatIso.ofComponents (fun i ‚Ü¶ match i with
    | .left _ => Iso.refl _
    | .right _ => Iso.refl _)

end MultispanIndex

namespace Multicofork

variable {Œπ : Type w} [LinearOrder Œπ] {I : MultispanIndex (.prod Œπ) C}

/-- The multicofork for `I.toLinearOrder` deduced from a multicofork
for `I : MultispanIndex (.prod Œπ) C` when `Œπ` is linearly ordered. -/
def toLinearOrder (c : Multicofork I) : Multicofork I.toLinearOrder :=
  Multicofork.ofœÄ _ c.pt c.œÄ (fun _ ‚Ü¶ c.condition _)

/-- The multicofork for `I : MultispanIndex (.prod Œπ) C` deduced from
a multicofork for `I.toLinearOrder` when `Œπ` is linearly ordered
and `I` is symmetric. -/
def ofLinearOrder (c : Multicofork I.toLinearOrder) (h : I.SymmStruct) :
    Multicofork I :=
  Multicofork.ofœÄ _ c.pt c.œÄ (by
    rintro ‚ü®x, y‚ü©
    obtain hxy | rfl | hxy := lt_trichotomy x y
    ¬∑ exact c.condition ‚ü®‚ü®x, y‚ü©, hxy‚ü©
    ¬∑ simp [h.fst_eq_snd]
    ¬∑ have := c.condition ‚ü®‚ü®y, x‚ü©, hxy‚ü©
      dsimp at this ‚ä¢
      rw [‚Üê h.iso_hom_fst_assoc, ‚Üê h.iso_hom_snd_assoc, this])

/-- If `Œπ` is a linearly ordered type, `I : MultispanIndex (.prod Œπ) C`, and
`c` a colimit multicofork for `I`, then `c.toLinearOrder` is a colimit
multicofork for `I.toLinearOrder`. -/
def isColimitToLinearOrder (c : Multicofork I) (hc : IsColimit c) (h : I.SymmStruct) :
    IsColimit c.toLinearOrder :=
  Multicofork.IsColimit.mk _ (fun s ‚Ü¶ hc.desc (ofLinearOrder s h))
    (fun s _ ‚Ü¶ hc.fac (ofLinearOrder s h) _)
    (fun s m hm ‚Ü¶ Multicofork.IsColimit.hom_ext hc (fun i ‚Ü¶ by
      have := hc.fac (ofLinearOrder s h) (.right i)
      dsimp at this
      rw [this]
      apply hm))

end Multicofork

end symmetry

end CategoryTheory.Limits
