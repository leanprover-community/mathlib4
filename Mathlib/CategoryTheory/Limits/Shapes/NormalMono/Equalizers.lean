/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.Shapes.NormalMono.Basic
import Mathlib.CategoryTheory.Limits.Shapes.FiniteProducts

#align_import category_theory.limits.shapes.normal_mono.equalizers from "leanprover-community/mathlib"@"3a061790136d13594ec10c7c90d202335ac5d854"

/-!
# Normal mono categories with finite products and kernels have all equalizers.

This, and the dual result, are used in the development of abelian categories.
-/


noncomputable section

open CategoryTheory

open CategoryTheory.Limits

variable {C : Type*} [Category C] [HasZeroMorphisms C]

namespace CategoryTheory.NormalMonoCategory

variable [HasFiniteProducts C] [HasKernels C] [NormalMonoCategory C]

/-- The pullback of two monomorphisms exists. -/
@[irreducible, nolint defLemma] -- Porting note: changed to irreducible and a def
def pullback_of_mono {X Y Z : C} (a : X ‚ü∂ Z) (b : Y ‚ü∂ Z) [Mono a] [Mono b] :
    HasLimit (cospan a b) :=
  let ‚ü®P, f, haf, i‚ü© := normalMonoOfMono a
  let ‚ü®Q, g, hbg, i'‚ü© := normalMonoOfMono b
  let ‚ü®a', ha'‚ü© :=
    KernelFork.IsLimit.lift' i (kernel.Œπ (prod.lift f g)) <|
      calc kernel.Œπ (prod.lift f g) ‚â´ f
        _ = kernel.Œπ (prod.lift f g) ‚â´ prod.lift f g ‚â´ Limits.prod.fst := by rw [prod.lift_fst]
        _ = (0 : kernel (prod.lift f g) ‚ü∂ P ‚®Ø Q) ‚â´ Limits.prod.fst := by rw [kernel.condition_assoc]
        _ = 0 := zero_comp

  let ‚ü®b', hb'‚ü© :=
    KernelFork.IsLimit.lift' i' (kernel.Œπ (prod.lift f g)) <|
      calc kernel.Œπ (prod.lift f g) ‚â´ g
        _ = kernel.Œπ (prod.lift f g) ‚â´ prod.lift f g ‚â´ Limits.prod.snd := by rw [prod.lift_snd]
        _ = (0 : kernel (prod.lift f g) ‚ü∂ P ‚®Ø Q) ‚â´ Limits.prod.snd := by rw [kernel.condition_assoc]
        _ = 0 := zero_comp

  HasLimit.mk
    { cone :=
        PullbackCone.mk a' b' <| by
          simp? at ha' hb' says
            simp only [parallelPair_obj_zero, Fork.ofŒπ_pt, Fork.Œπ_ofŒπ] at ha' hb'
          rw [ha', hb']
      isLimit :=
        PullbackCone.IsLimit.mk _
          (fun s =>
            kernel.lift (prod.lift f g) (PullbackCone.snd s ‚â´ b) <|
              prod.hom_ext
                (calc
                  ((PullbackCone.snd s ‚â´ b) ‚â´ prod.lift f g) ‚â´ Limits.prod.fst =
                      PullbackCone.snd s ‚â´ b ‚â´ f := by simp only [prod.lift_fst, Category.assoc]
                  _ = PullbackCone.fst s ‚â´ a ‚â´ f := by rw [PullbackCone.condition_assoc]
                  _ = PullbackCone.fst s ‚â´ 0 := by rw [haf]
                  _ = 0 ‚â´ Limits.prod.fst := by rw [comp_zero, zero_comp]
                  )
                (calc
                  ((PullbackCone.snd s ‚â´ b) ‚â´ prod.lift f g) ‚â´ Limits.prod.snd =
                      PullbackCone.snd s ‚â´ b ‚â´ g := by
                    simp only [prod.lift_snd, Category.assoc]
                  _ = PullbackCone.snd s ‚â´ 0 := by rw [hbg]
                  _ = 0 ‚â´ Limits.prod.snd := by rw [comp_zero, zero_comp]
                  ))
          (fun s =>
            (cancel_mono a).1 <| by
              rw [KernelFork.Œπ_ofŒπ] at ha'
              simp [ha', PullbackCone.condition s])
          (fun s =>
            (cancel_mono b).1 <| by
              rw [KernelFork.Œπ_ofŒπ] at hb'
              simp [hb'])
          fun s m h‚ÇÅ _ =>
          (cancel_mono (kernel.Œπ (prod.lift f g))).1 <|
            calc
              m ‚â´ kernel.Œπ (prod.lift f g) = m ‚â´ a' ‚â´ a := by
                congr
                exact ha'.symm
              _ = PullbackCone.fst s ‚â´ a := by rw [‚Üê Category.assoc, h‚ÇÅ]
              _ = PullbackCone.snd s ‚â´ b := PullbackCone.condition s
              _ =
                  kernel.lift (prod.lift f g) (PullbackCone.snd s ‚â´ b) _ ‚â´
                    kernel.Œπ (prod.lift f g) := by rw [kernel.lift_Œπ]
               }
#align category_theory.normal_mono_category.pullback_of_mono CategoryTheory.NormalMonoCategory.pullback_of_mono

section

attribute [local instance] pullback_of_mono

/-- The pullback of `(ùüô X, f)` and `(ùüô X, g)` -/
private abbrev P {X Y : C} (f g : X ‚ü∂ Y) [Mono (prod.lift (ùüô X) f)] [Mono (prod.lift (ùüô X) g)] :
    C :=
  pullback (prod.lift (ùüô X) f) (prod.lift (ùüô X) g)

/-- The equalizer of `f` and `g` exists. -/
 -- Porting note: changed to irreducible def since irreducible_def was breaking things
@[irreducible, nolint defLemma]
def hasLimit_parallelPair {X Y : C} (f g : X ‚ü∂ Y) : HasLimit (parallelPair f g) :=
  have huv : (pullback.fst : P f g ‚ü∂ X) = pullback.snd :=
    calc
      (pullback.fst : P f g ‚ü∂ X) = pullback.fst ‚â´ ùüô _ := Eq.symm <| Category.comp_id _
      _ = pullback.fst ‚â´ prod.lift (ùüô X) f ‚â´ Limits.prod.fst := by rw [prod.lift_fst]
      _ = pullback.snd ‚â´ prod.lift (ùüô X) g ‚â´ Limits.prod.fst := by rw [pullback.condition_assoc]
      _ = pullback.snd := by rw [prod.lift_fst, Category.comp_id]

  have hvu : (pullback.fst : P f g ‚ü∂ X) ‚â´ f = pullback.snd ‚â´ g :=
    calc
      (pullback.fst : P f g ‚ü∂ X) ‚â´ f = pullback.fst ‚â´ prod.lift (ùüô X) f ‚â´ Limits.prod.snd := by
        rw [prod.lift_snd]
      _ = pullback.snd ‚â´ prod.lift (ùüô X) g ‚â´ Limits.prod.snd := by rw [pullback.condition_assoc]
      _ = pullback.snd ‚â´ g := by rw [prod.lift_snd]

  have huu : (pullback.fst : P f g ‚ü∂ X) ‚â´ f = pullback.fst ‚â´ g := by rw [hvu, ‚Üê huv]
  HasLimit.mk
    { cone := Fork.ofŒπ pullback.fst huu
      isLimit :=
        Fork.IsLimit.mk _
          (fun s =>
            pullback.lift (Fork.Œπ s) (Fork.Œπ s) <|
              prod.hom_ext (by simp only [prod.lift_fst, Category.assoc])
                (by simp only [prod.comp_lift, Fork.condition s]))
          (fun s => by simp) fun s m h =>
          pullback.hom_ext (by simpa only [pullback.lift_fst] using h)
            (by simpa only [huv.symm, pullback.lift_fst] using h) }
#align category_theory.normal_mono_category.has_limit_parallel_pair CategoryTheory.NormalMonoCategory.hasLimit_parallelPair

end

section

attribute [local instance] hasLimit_parallelPair

/-- A `NormalMonoCategory` category with finite products and kernels has all equalizers. -/
instance (priority := 100) hasEqualizers : HasEqualizers C :=
  hasEqualizers_of_hasLimit_parallelPair _
#align category_theory.normal_mono_category.has_equalizers CategoryTheory.NormalMonoCategory.hasEqualizers

end

/-- If a zero morphism is a cokernel of `f`, then `f` is an epimorphism. -/
theorem epi_of_zero_cokernel {X Y : C} (f : X ‚ü∂ Y) (Z : C)
    (l : IsColimit (CokernelCofork.ofœÄ (0 : Y ‚ü∂ Z) (show f ‚â´ 0 = 0 by simp))) : Epi f :=
  ‚ü®fun u v huv => by
    obtain ‚ü®W, w, hw, hl‚ü© := normalMonoOfMono (equalizer.Œπ u v)
    obtain ‚ü®m, hm‚ü© := equalizer.lift' f huv
    have hwf : f ‚â´ w = 0 := by rw [‚Üê hm, Category.assoc, hw, comp_zero]
    obtain ‚ü®n, hn‚ü© := CokernelCofork.IsColimit.desc' l _ hwf
    rw [Cofork.œÄ_ofœÄ, zero_comp] at hn
    have : IsIso (equalizer.Œπ u v) := by apply isIso_limit_cone_parallelPair_of_eq hn.symm hl
    apply (cancel_epi (equalizer.Œπ u v)).1
    exact equalizer.condition _ _‚ü©
#align category_theory.normal_mono_category.epi_of_zero_cokernel CategoryTheory.NormalMonoCategory.epi_of_zero_cokernel

section

variable [HasZeroObject C]

open ZeroObject

/-- If `f ‚â´ g = 0` implies `g = 0` for all `g`, then `g` is a monomorphism. -/
theorem epi_of_zero_cancel {X Y : C} (f : X ‚ü∂ Y)
    (hf : ‚àÄ (Z : C) (g : Y ‚ü∂ Z) (_ : f ‚â´ g = 0), g = 0) : Epi f :=
  epi_of_zero_cokernel f 0 <| zeroCokernelOfZeroCancel f hf
#align category_theory.normal_mono_category.epi_of_zero_cancel CategoryTheory.NormalMonoCategory.epi_of_zero_cancel

end

end CategoryTheory.NormalMonoCategory

namespace CategoryTheory.NormalEpiCategory

variable [HasFiniteCoproducts C] [HasCokernels C] [NormalEpiCategory C]

/-- The pushout of two epimorphisms exists. -/
@[irreducible, nolint defLemma] -- Porting note: made a def and re-added irreducible
def pushout_of_epi {X Y Z : C} (a : X ‚ü∂ Y) (b : X ‚ü∂ Z) [Epi a] [Epi b] :
    HasColimit (span a b) :=
  let ‚ü®P, f, hfa, i‚ü© := normalEpiOfEpi a
  let ‚ü®Q, g, hgb, i'‚ü© := normalEpiOfEpi b
  let ‚ü®a', ha'‚ü© :=
    CokernelCofork.IsColimit.desc' i (cokernel.œÄ (coprod.desc f g)) <|
      calc
        f ‚â´ cokernel.œÄ (coprod.desc f g) =
            coprod.inl ‚â´ coprod.desc f g ‚â´ cokernel.œÄ (coprod.desc f g) := by
          rw [coprod.inl_desc_assoc]
        _ = coprod.inl ‚â´ (0 : P ‚®ø Q ‚ü∂ cokernel (coprod.desc f g)) := by rw [cokernel.condition]
        _ = 0 := HasZeroMorphisms.comp_zero _ _

  let ‚ü®b', hb'‚ü© :=
    CokernelCofork.IsColimit.desc' i' (cokernel.œÄ (coprod.desc f g)) <|
      calc
        g ‚â´ cokernel.œÄ (coprod.desc f g) =
            coprod.inr ‚â´ coprod.desc f g ‚â´ cokernel.œÄ (coprod.desc f g) := by
          rw [coprod.inr_desc_assoc]
        _ = coprod.inr ‚â´ (0 : P ‚®ø Q ‚ü∂ cokernel (coprod.desc f g)) := by rw [cokernel.condition]
        _ = 0 := HasZeroMorphisms.comp_zero _ _

  HasColimit.mk
    { cocone :=
        PushoutCocone.mk a' b' <| by
          simp only [Cofork.œÄ_ofœÄ] at ha' hb'
          rw [ha', hb']
      isColimit :=
        PushoutCocone.IsColimit.mk _
          (fun s =>
            cokernel.desc (coprod.desc f g) (b ‚â´ PushoutCocone.inr s) <|
              coprod.hom_ext
                (calc
                  coprod.inl ‚â´ coprod.desc f g ‚â´ b ‚â´ PushoutCocone.inr s =
                      f ‚â´ b ‚â´ PushoutCocone.inr s := by rw [coprod.inl_desc_assoc]
                  _ = f ‚â´ a ‚â´ PushoutCocone.inl s := by rw [PushoutCocone.condition]
                  _ = 0 ‚â´ PushoutCocone.inl s := by rw [‚Üê Category.assoc, eq_whisker hfa]
                  _ = coprod.inl ‚â´ 0 := by rw [comp_zero, zero_comp]
                  )
                (calc
                  coprod.inr ‚â´ coprod.desc f g ‚â´ b ‚â´ PushoutCocone.inr s =
                      g ‚â´ b ‚â´ PushoutCocone.inr s := by rw [coprod.inr_desc_assoc]
                  _ = 0 ‚â´ PushoutCocone.inr s := by rw [‚Üê Category.assoc, eq_whisker hgb]
                  _ = coprod.inr ‚â´ 0 := by rw [comp_zero, zero_comp]
                  ))
          (fun s =>
            (cancel_epi a).1 <| by
              rw [CokernelCofork.œÄ_ofœÄ] at ha'
              have reassoced {W : C} (h : cokernel (coprod.desc f g) ‚ü∂ W) : a ‚â´ a' ‚â´ h
                = cokernel.œÄ (coprod.desc f g) ‚â´ h := by rw [‚Üê Category.assoc, eq_whisker ha']
              simp [reassoced , PushoutCocone.condition s])
          (fun s =>
            (cancel_epi b).1 <| by
              rw [CokernelCofork.œÄ_ofœÄ] at hb'
              have reassoced' {W : C} (h : cokernel (coprod.desc f g) ‚ü∂ W) : b ‚â´ b' ‚â´ h
                = cokernel.œÄ (coprod.desc f g) ‚â´ h := by rw [‚Üê Category.assoc, eq_whisker hb']
              simp [reassoced'])
          fun s m h‚ÇÅ _ =>
          (cancel_epi (cokernel.œÄ (coprod.desc f g))).1 <|
            calc
              cokernel.œÄ (coprod.desc f g) ‚â´ m = (a ‚â´ a') ‚â´ m := by
                congr
                exact ha'.symm
              _ = a ‚â´ PushoutCocone.inl s := by rw [Category.assoc, h‚ÇÅ]
              _ = b ‚â´ PushoutCocone.inr s := PushoutCocone.condition s
              _ =
                  cokernel.œÄ (coprod.desc f g) ‚â´
                    cokernel.desc (coprod.desc f g) (b ‚â´ PushoutCocone.inr s) _ := by
                rw [cokernel.œÄ_desc]
               }
#align category_theory.normal_epi_category.pushout_of_epi CategoryTheory.NormalEpiCategory.pushout_of_epi

section

attribute [local instance] pushout_of_epi

/-- The pushout of `(ùüô Y, f)` and `(ùüô Y, g)`. -/
private abbrev Q {X Y : C} (f g : X ‚ü∂ Y) [Epi (coprod.desc (ùüô Y) f)] [Epi (coprod.desc (ùüô Y) g)] :
    C :=
  pushout (coprod.desc (ùüô Y) f) (coprod.desc (ùüô Y) g)

/-- The coequalizer of `f` and `g` exists. -/
@[irreducible, nolint defLemma] -- Porting note: changed to def and restored irreducible
def hasColimit_parallelPair {X Y : C} (f g : X ‚ü∂ Y) : HasColimit (parallelPair f g) :=
  have huv : (pushout.inl : Y ‚ü∂ Q f g) = pushout.inr :=
    calc
      (pushout.inl : Y ‚ü∂ Q f g) = ùüô _ ‚â´ pushout.inl := Eq.symm <| Category.id_comp _
      _ = (coprod.inl ‚â´ coprod.desc (ùüô Y) f) ‚â´ pushout.inl := by rw [coprod.inl_desc]
      _ = (coprod.inl ‚â´ coprod.desc (ùüô Y) g) ‚â´ pushout.inr := by
        simp only [Category.assoc, pushout.condition]
      _ = pushout.inr := by rw [coprod.inl_desc, Category.id_comp]

  have hvu : f ‚â´ (pushout.inl : Y ‚ü∂ Q f g) = g ‚â´ pushout.inr :=
    calc
      f ‚â´ (pushout.inl : Y ‚ü∂ Q f g) = (coprod.inr ‚â´ coprod.desc (ùüô Y) f) ‚â´ pushout.inl := by
        rw [coprod.inr_desc]
      _ = (coprod.inr ‚â´ coprod.desc (ùüô Y) g) ‚â´ pushout.inr := by
        simp only [Category.assoc, pushout.condition]
      _ = g ‚â´ pushout.inr := by rw [coprod.inr_desc]

  have huu : f ‚â´ (pushout.inl : Y ‚ü∂ Q f g) = g ‚â´ pushout.inl := by rw [hvu, huv]
  HasColimit.mk
    { cocone := Cofork.ofœÄ pushout.inl huu
      isColimit :=
        Cofork.IsColimit.mk _
          (fun s =>
            pushout.desc (Cofork.œÄ s) (Cofork.œÄ s) <|
              coprod.hom_ext (by simp only [coprod.inl_desc_assoc])
                (by simp only [coprod.desc_comp, Cofork.condition s]))
          (fun s => by simp only [pushout.inl_desc, Cofork.œÄ_ofœÄ]) fun s m h =>
          pushout.hom_ext (by simpa only [pushout.inl_desc] using h)
            (by simpa only [huv.symm, pushout.inl_desc] using h) }
#align category_theory.normal_epi_category.has_colimit_parallel_pair CategoryTheory.NormalEpiCategory.hasColimit_parallelPair

end

section

attribute [local instance] hasColimit_parallelPair

/-- A `NormalEpiCategory` category with finite coproducts and cokernels has all coequalizers. -/
instance (priority := 100) hasCoequalizers : HasCoequalizers C :=
  hasCoequalizers_of_hasColimit_parallelPair _
#align category_theory.normal_epi_category.has_coequalizers CategoryTheory.NormalEpiCategory.hasCoequalizers

end

/-- If a zero morphism is a kernel of `f`, then `f` is a monomorphism. -/
theorem mono_of_zero_kernel {X Y : C} (f : X ‚ü∂ Y) (Z : C)
    (l : IsLimit (KernelFork.ofŒπ (0 : Z ‚ü∂ X) (show 0 ‚â´ f = 0 by simp))) : Mono f :=
  ‚ü®fun u v huv => by
    obtain ‚ü®W, w, hw, hl‚ü© := normalEpiOfEpi (coequalizer.œÄ u v)
    obtain ‚ü®m, hm‚ü© := coequalizer.desc' f huv
    have reassoced {W : C} (h : coequalizer u v ‚ü∂ W) : w ‚â´ coequalizer.œÄ u v ‚â´ h = 0 ‚â´ h := by
      rw [‚Üê Category.assoc, eq_whisker hw]
    have hwf : w ‚â´ f = 0 := by rw [‚Üê hm, reassoced, zero_comp]
    obtain ‚ü®n, hn‚ü© := KernelFork.IsLimit.lift' l _ hwf
    rw [Fork.Œπ_ofŒπ, HasZeroMorphisms.comp_zero] at hn
    have : IsIso (coequalizer.œÄ u v) := by
      apply isIso_colimit_cocone_parallelPair_of_eq hn.symm hl
    apply (cancel_mono (coequalizer.œÄ u v)).1
    exact coequalizer.condition _ _‚ü©
#align category_theory.normal_epi_category.mono_of_zero_kernel CategoryTheory.NormalEpiCategory.mono_of_zero_kernel

section

variable [HasZeroObject C]

open ZeroObject

/-- If `g ‚â´ f = 0` implies `g = 0` for all `g`, then `f` is a monomorphism. -/
theorem mono_of_cancel_zero {X Y : C} (f : X ‚ü∂ Y)
    (hf : ‚àÄ (Z : C) (g : Z ‚ü∂ X) (_ : g ‚â´ f = 0), g = 0) : Mono f :=
  mono_of_zero_kernel f 0 <| zeroKernelOfCancelZero f hf
#align category_theory.normal_epi_category.mono_of_cancel_zero CategoryTheory.NormalEpiCategory.mono_of_cancel_zero

end

end CategoryTheory.NormalEpiCategory
