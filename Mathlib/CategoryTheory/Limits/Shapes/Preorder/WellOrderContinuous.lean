/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.IsLimit
import Mathlib.CategoryTheory.Limits.Shapes.Preorder.PrincipalSeg
import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Filtered.Final
import Mathlib.Data.Nat.SuccPred
import Mathlib.Data.Fin.SuccPred
import Mathlib.Order.Interval.Set.InitialSeg
import Mathlib.Order.SuccPred.InitialSeg
import Mathlib.Order.SuccPred.Limit
import Mathlib.Order.SuccPred.LinearLocallyFinite

/-!
# Continuity of functors from well ordered types

Let `F : J â¥¤ C` be functor from a well ordered type `J`.
We introduce the typeclass `F.IsWellOrderContinuous`
to say that if `m` is a limit element, then `F.obj m`
is the colimit of the `F.obj j` for `j < m`.

-/

universe w w' v u

namespace CategoryTheory.Functor

open Category Limits

variable {C : Type u} [Category.{v} C] {J : Type w} [PartialOrder J]

/-- A functor `F : J â¥¤ C` is well-order-continuous if for any limit element `m : J`,
`F.obj m` identifies to the colimit of the `F.obj j` for `j < m`. -/
class IsWellOrderContinuous (F : J â¥¤ C) : Prop where
  nonempty_isColimit (m : J) (hm : Order.IsSuccLimit m) :
    Nonempty (IsColimit ((Set.principalSegIio m).cocone F))

/-- If `F : J â¥¤ C` is well-order-continuous and `m : J` is a limit element, then
`F.obj m` identifies to the colimit of the `F.obj j` for `j < m`. -/
noncomputable def isColimitOfIsWellOrderContinuous (F : J â¥¤ C) [F.IsWellOrderContinuous]
    (m : J) (hm : Order.IsSuccLimit m) :
    IsColimit ((Set.principalSegIio m).cocone F) :=
      (IsWellOrderContinuous.nonempty_isColimit m hm).some

/-- If `F : J â¥¤ C` is well-order-continuous and `h : Î± <i J` is a principal
segment such that `h.top` is a limit element, then
`F.obj h.top` identifies to the colimit of the `F.obj j` for `j : Î±`. -/
noncomputable def isColimitOfIsWellOrderContinuous' (F : J â¥¤ C) [F.IsWellOrderContinuous]
    {Î± : Type*} [PartialOrder Î±] (f : Î± <i J) (hÎ± : Order.IsSuccLimit f.top) :
    IsColimit (f.cocone F) :=
  (F.isColimitOfIsWellOrderContinuous f.top hÎ±).whiskerEquivalence
    f.orderIsoIio.equivalence

instance (F : â„• â¥¤ C) : F.IsWellOrderContinuous where
  nonempty_isColimit m hm := by simp at hm

instance {n : â„•} (F : Fin n â¥¤ C) : F.IsWellOrderContinuous where
  nonempty_isColimit _ hj := (Order.not_isSuccLimit hj).elim

lemma isWellOrderContinuous_of_iso {F G : J â¥¤ C} (e : F â‰… G) [F.IsWellOrderContinuous] :
    G.IsWellOrderContinuous where
  nonempty_isColimit (m : J) (hm : Order.IsSuccLimit m) :=
    âŸ¨(IsColimit.precomposeHomEquiv (isoWhiskerLeft _ e) _).1
      (IsColimit.ofIsoColimit (F.isColimitOfIsWellOrderContinuous m hm)
        (Cocones.ext (e.app _)))âŸ©

instance (F : J â¥¤ C) {J' : Type w'} [PartialOrder J'] (f : J' â‰¤i J)
    [F.IsWellOrderContinuous] :
    (f.monotone.functor â‹™ F).IsWellOrderContinuous where
  nonempty_isColimit m' hm' := âŸ¨F.isColimitOfIsWellOrderContinuous'
    ((Set.principalSegIio m').transInitial f) (by simpa)âŸ©

instance (F : J â¥¤ C) {J' : Type w'} [PartialOrder J'] (e : J' â‰ƒo J)
    [F.IsWellOrderContinuous] :
    (e.equivalence.functor â‹™ F).IsWellOrderContinuous :=
  inferInstanceAs (e.toInitialSeg.monotone.functor â‹™ F).IsWellOrderContinuous


lemma _root_.Set.not_isMax_coe {S : Set J} (m : S) (hm : Â¬ IsMax m) :
    Â¬ IsMax m.1 := by
  aesop

lemma _root_.Set.not_isMin_coe {S : Set J} (m : S) (hm : Â¬ IsMin m) :
    Â¬ IsMin m.1 := by
  aesop

section

variable {J' : Type w} [LinearOrder J']

lemma _root_.Set.Ici.isSuccLimit_coe {j : J'} (m : Set.Ici j) (hm : Order.IsSuccLimit m) :
    Order.IsSuccLimit m.1 :=
  âŸ¨_root_.Set.not_isMin_coe _ hm.1, fun b â†¦ by
    simp only [CovBy, not_lt, not_and, not_forall, Classical.not_imp, not_le]
    intro hb
    by_cases hb' : j â‰¤ b
    Â· have := hm.2 âŸ¨b, hb'âŸ©
      simp [CovBy] at this
      obtain âŸ¨x, hâ‚, hâ‚‚, hâ‚ƒâŸ© := this hb
      exact âŸ¨x, hâ‚, hâ‚ƒâŸ©
    Â· simp only [not_le] at hb'
      refine âŸ¨j, hb', ?_âŸ©
      by_contra!
      apply hm.1
      rintro âŸ¨k, hkâŸ© _
      exact this.trans (by simpa using hk)âŸ©



lemma _root_.Monotone.final_functor_iff {Jâ‚ Jâ‚‚ : Type*} [Preorder Jâ‚] [Preorder Jâ‚‚]
    [IsDirected Jâ‚ (Â· â‰¤ Â· )]
    {f : Jâ‚ â†’ Jâ‚‚} (hf : Monotone f) :
    Final hf.functor â†” âˆ€ (jâ‚‚ : Jâ‚‚), âˆƒ (jâ‚ : Jâ‚), jâ‚‚ â‰¤ f jâ‚ := by
  rw [Functor.final_iff_of_isFiltered]
  constructor
  Â· rintro âŸ¨h, _âŸ© jâ‚‚
    obtain âŸ¨jâ‚, âŸ¨Ï†âŸ©âŸ© := h jâ‚‚
    exact âŸ¨jâ‚, leOfHom Ï†âŸ©
  Â· intro h
    constructor
    Â· intro jâ‚‚
      obtain âŸ¨jâ‚, hâ‚âŸ© := h jâ‚‚
      exact âŸ¨jâ‚, âŸ¨homOfLE hâ‚âŸ©âŸ©
    Â· intro _ c _ _
      exact âŸ¨c, ðŸ™ _, rflâŸ©

instance IsWellOrderContinuous.restriction_setIci
    {F : J' â¥¤ C} [F.IsWellOrderContinuous] (j : J') :
    ((Subtype.mono_coe (Set.Ici j)).functor â‹™ F).IsWellOrderContinuous where
  nonempty_isColimit m hm := âŸ¨by
    have h := F.isColimitOfIsWellOrderContinuous m.1 (Set.Ici.isSuccLimit_coe m hm)
    let f : Set.Iio m â†’ Set.Iio m.1 := fun âŸ¨âŸ¨a, haâŸ©, ha'âŸ© â†¦ âŸ¨a, ha'âŸ©
    have hf : Monotone f := fun _ _ h â†¦ h
    have : hf.functor.Final := by
      rw [_root_.Monotone.final_functor_iff]
      rintro âŸ¨j', hj'âŸ©
      simp at hj'
      dsimp [f]
      by_cases h : j' â‰¤ j
      Â· refine âŸ¨âŸ¨âŸ¨j, ?_âŸ©, ?_âŸ©, hâŸ©
        Â· change j â‰¤ j
          rfl
        Â· by_contra!
          simp at this
          apply hm.1
          rintro âŸ¨k, hkâŸ© hkm
          exact this.trans hk
      Â· simp only [not_le, f] at h
        exact âŸ¨âŸ¨âŸ¨j', h.leâŸ©, hj'âŸ©, by rflâŸ©
    exact (Functor.Final.isColimitWhiskerEquiv (F := hf.functor) _).2 hâŸ©

end

end CategoryTheory.Functor
