/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Notation.Pi.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Images
import Mathlib.CategoryTheory.IsomorphismClasses
import Mathlib.CategoryTheory.Limits.Shapes.ZeroObjects

/-!
# Zero morphisms and zero objects

A category "has zero morphisms" if there is a designated "zero morphism" in each morphism space,
and compositions of zero morphisms with anything give the zero morphism. (Notice this is extra
structure, not merely a property.)

A category "has a zero object" if it has an object which is both initial and terminal. Having a
zero object provides zero morphisms, as the unique morphisms factoring through the zero object.

## References

* https://en.wikipedia.org/wiki/Zero_morphism
* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/


noncomputable section

universe w v v' u u'

open CategoryTheory

open CategoryTheory.Category

namespace CategoryTheory.Limits

variable (C : Type u) [Category.{v} C]
variable (D : Type u') [Category.{v'} D]

/-- A category "has zero morphisms" if there is a designated "zero morphism" in each morphism space,
and compositions of zero morphisms with anything give the zero morphism. -/
class HasZeroMorphisms where
  /-- Every morphism space has zero -/
  [zero : ‚àÄ X Y : C, Zero (X ‚ü∂ Y)]
  /-- `f` composed with `0` is `0` -/
  comp_zero : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) (Z : C), f ‚â´ (0 : Y ‚ü∂ Z) = (0 : X ‚ü∂ Z) := by cat_disch
  /-- `0` composed with `f` is `0` -/
  zero_comp : ‚àÄ (X : C) {Y Z : C} (f : Y ‚ü∂ Z), (0 : X ‚ü∂ Y) ‚â´ f = (0 : X ‚ü∂ Z) := by cat_disch

attribute [instance] HasZeroMorphisms.zero

variable {C}

@[simp]
theorem comp_zero [HasZeroMorphisms C] {X Y : C} {f : X ‚ü∂ Y} {Z : C} :
    f ‚â´ (0 : Y ‚ü∂ Z) = (0 : X ‚ü∂ Z) :=
  HasZeroMorphisms.comp_zero f Z

@[simp]
theorem zero_comp [HasZeroMorphisms C] {X : C} {Y Z : C} {f : Y ‚ü∂ Z} :
    (0 : X ‚ü∂ Y) ‚â´ f = (0 : X ‚ü∂ Z) :=
  HasZeroMorphisms.zero_comp X f

instance hasZeroMorphismsPEmpty : HasZeroMorphisms (Discrete PEmpty) where
  zero := by cat_disch

instance hasZeroMorphismsPUnit : HasZeroMorphisms (Discrete PUnit) where
  zero X Y := by repeat (constructor)

namespace HasZeroMorphisms

/-- This lemma will be immediately superseded by `ext`, below. -/
private theorem ext_aux (I J : HasZeroMorphisms C)
    (w : ‚àÄ X Y : C, (I.zero X Y).zero = (J.zero X Y).zero) : I = J := by
  have : I.zero = J.zero := by
    funext X Y
    specialize w X Y
    apply congrArg Zero.mk w
  cases I; cases J
  congr
  ¬∑ apply proof_irrel_heq
  ¬∑ apply proof_irrel_heq

/-- If you're tempted to use this lemma "in the wild", you should probably
carefully consider whether you've made a mistake in allowing two
instances of `HasZeroMorphisms` to exist at all.

See, particularly, the note on `zeroMorphismsOfZeroObject` below.
-/
theorem ext (I J : HasZeroMorphisms C) : I = J := by
  apply ext_aux
  intro X Y
  have : (I.zero X Y).zero ‚â´ (J.zero Y Y).zero = (I.zero X Y).zero := by
    apply I.zero_comp X (J.zero Y Y).zero
  have that : (I.zero X Y).zero ‚â´ (J.zero Y Y).zero = (J.zero X Y).zero := by
    apply J.comp_zero (I.zero X Y).zero Y
  rw [‚Üê this, ‚Üê that]

instance : Subsingleton (HasZeroMorphisms C) :=
  ‚ü®ext‚ü©

end HasZeroMorphisms

open Opposite HasZeroMorphisms

instance hasZeroMorphismsOpposite [HasZeroMorphisms C] : HasZeroMorphisms C·µí·µñ where
  zero X Y := ‚ü®(0 : unop Y ‚ü∂ unop X).op‚ü©
  comp_zero f Z := congr_arg Quiver.Hom.op (HasZeroMorphisms.zero_comp (unop Z) f.unop)
  zero_comp X {Y Z} (f : Y ‚ü∂ Z) :=
    congrArg Quiver.Hom.op (HasZeroMorphisms.comp_zero f.unop (unop X))

section

variable [HasZeroMorphisms C]

@[simp] lemma op_zero (X Y : C) : (0 : X ‚ü∂ Y).op = 0 := rfl

@[simp] lemma unop_zero (X Y : C·µí·µñ) : (0 : X ‚ü∂ Y).unop = 0 := rfl

theorem zero_of_comp_mono {X Y Z : C} {f : X ‚ü∂ Y} (g : Y ‚ü∂ Z) [Mono g] (h : f ‚â´ g = 0) : f = 0 := by
  rw [‚Üê zero_comp, cancel_mono] at h
  exact h

theorem zero_of_epi_comp {X Y Z : C} (f : X ‚ü∂ Y) {g : Y ‚ü∂ Z} [Epi f] (h : f ‚â´ g = 0) : g = 0 := by
  rw [‚Üê comp_zero, cancel_epi] at h
  exact h

theorem eq_zero_of_image_eq_zero {X Y : C} {f : X ‚ü∂ Y} [HasImage f] (w : image.Œπ f = 0) :
    f = 0 := by rw [‚Üê image.fac f, w, HasZeroMorphisms.comp_zero]

theorem nonzero_image_of_nonzero {X Y : C} {f : X ‚ü∂ Y} [HasImage f] (w : f ‚â† 0) : image.Œπ f ‚â† 0 :=
  fun h ‚Ü¶ w (eq_zero_of_image_eq_zero h)

end

section

variable [HasZeroMorphisms D]

instance : HasZeroMorphisms (C ‚•§ D) where
  zero F G := ‚ü®{ app := fun _ ‚Ü¶ 0 }‚ü©
  comp_zero := fun Œ∑ H ‚Ü¶ by
    ext X; dsimp; apply comp_zero
  zero_comp := fun F {G H} Œ∑ ‚Ü¶ by
    ext X; dsimp; apply zero_comp

@[simp]
theorem zero_app (F G : C ‚•§ D) (j : C) : (0 : F ‚ü∂ G).app j = 0 := rfl

end

namespace IsZero

variable [HasZeroMorphisms C]

theorem eq_zero_of_src {X Y : C} (o : IsZero X) (f : X ‚ü∂ Y) : f = 0 :=
  o.eq_of_src _ _

theorem eq_zero_of_tgt {X Y : C} (o : IsZero Y) (f : X ‚ü∂ Y) : f = 0 :=
  o.eq_of_tgt _ _

theorem iff_id_eq_zero (X : C) : IsZero X ‚Üî ùüô X = 0 :=
  ‚ü®fun h ‚Ü¶ h.eq_of_src _ _, fun h ‚Ü¶
    ‚ü®fun Y ‚Ü¶ ‚ü®‚ü®‚ü®0‚ü©, fun f ‚Ü¶ by
        rw [‚Üê id_comp f, ‚Üê id_comp (0 : X ‚ü∂ Y), h, zero_comp, zero_comp]; simp only‚ü©‚ü©,
    fun Y ‚Ü¶ ‚ü®‚ü®‚ü®0‚ü©, fun f ‚Ü¶ by
        rw [‚Üê comp_id f, ‚Üê comp_id (0 : Y ‚ü∂ X), h, comp_zero, comp_zero]; simp only ‚ü©‚ü©‚ü©‚ü©

theorem of_mono_zero (X Y : C) [Mono (0 : X ‚ü∂ Y)] : IsZero X :=
  (iff_id_eq_zero X).mpr ((cancel_mono (0 : X ‚ü∂ Y)).1 (by simp))

theorem of_epi_zero (X Y : C) [Epi (0 : X ‚ü∂ Y)] : IsZero Y :=
  (iff_id_eq_zero Y).mpr ((cancel_epi (0 : X ‚ü∂ Y)).1 (by simp))

theorem of_mono_eq_zero {X Y : C} (f : X ‚ü∂ Y) [Mono f] (h : f = 0) : IsZero X := by
  subst h
  apply of_mono_zero X Y

theorem of_epi_eq_zero {X Y : C} (f : X ‚ü∂ Y) [Epi f] (h : f = 0) : IsZero Y := by
  subst h
  apply of_epi_zero X Y

theorem iff_isSplitMono_eq_zero {X Y : C} (f : X ‚ü∂ Y) [IsSplitMono f] : IsZero X ‚Üî f = 0 := by
  rw [iff_id_eq_zero]
  constructor
  ¬∑ intro h
    rw [‚Üê Category.id_comp f, h, zero_comp]
  ¬∑ intro h
    rw [‚Üê IsSplitMono.id f]
    simp only [h, zero_comp]

theorem iff_isSplitEpi_eq_zero {X Y : C} (f : X ‚ü∂ Y) [IsSplitEpi f] : IsZero Y ‚Üî f = 0 := by
  rw [iff_id_eq_zero]
  constructor
  ¬∑ intro h
    rw [‚Üê Category.comp_id f, h, comp_zero]
  ¬∑ intro h
    rw [‚Üê IsSplitEpi.id f]
    simp [h]

theorem of_mono {X Y : C} (f : X ‚ü∂ Y) [Mono f] (i : IsZero Y) : IsZero X := by
  have hf := i.eq_zero_of_tgt f
  subst hf
  exact IsZero.of_mono_zero X Y

theorem of_epi {X Y : C} (f : X ‚ü∂ Y) [Epi f] (i : IsZero X) : IsZero Y := by
  have hf := i.eq_zero_of_src f
  subst hf
  exact IsZero.of_epi_zero X Y

end IsZero

/-- A category with a zero object has zero morphisms.

It is rarely a good idea to use this. Many categories that have a zero object have zero
morphisms for some other reason, for example from additivity. Library code that uses
`zeroMorphismsOfZeroObject` will then be incompatible with these categories because
the `HasZeroMorphisms` instances will not be definitionally equal. For this reason library
code should generally ask for an instance of `HasZeroMorphisms` separately, even if it already
asks for an instance of `HasZeroObjects`. -/
def IsZero.hasZeroMorphisms {O : C} (hO : IsZero O) : HasZeroMorphisms C where
  zero X Y := { zero := hO.from_ X ‚â´ hO.to_ Y }
  zero_comp X {Y Z} f := by
    change (hO.from_ X ‚â´ hO.to_ Y) ‚â´ f = hO.from_ X ‚â´ hO.to_ Z
    rw [Category.assoc]
    congr
    apply hO.eq_of_src
  comp_zero {X Y} f Z := by
    change f ‚â´ (hO.from_ Y ‚â´ hO.to_ Z) = hO.from_ X ‚â´ hO.to_ Z
    rw [‚Üê Category.assoc]
    congr
    apply hO.eq_of_tgt

namespace HasZeroObject

variable [HasZeroObject C]

open ZeroObject

/-- A category with a zero object has zero morphisms.

It is rarely a good idea to use this. Many categories that have a zero object have zero
morphisms for some other reason, for example from additivity. Library code that uses
`zeroMorphismsOfZeroObject` will then be incompatible with these categories because
the `has_zero_morphisms` instances will not be definitionally equal. For this reason library
code should generally ask for an instance of `HasZeroMorphisms` separately, even if it already
asks for an instance of `HasZeroObjects`. -/
def zeroMorphismsOfZeroObject : HasZeroMorphisms C where
  zero X _ := { zero := (default : X ‚ü∂ 0) ‚â´ default }
  zero_comp X {Y Z} f := by
    change ((default : X ‚ü∂ 0) ‚â´ default) ‚â´ f = (default : X ‚ü∂ 0) ‚â´ default
    rw [Category.assoc]
    congr
    simp only [eq_iff_true_of_subsingleton]
  comp_zero {X Y} f Z := by
    change f ‚â´ (default : Y ‚ü∂ 0) ‚â´ default = (default : X ‚ü∂ 0) ‚â´ default
    rw [‚Üê Category.assoc]
    congr
    simp only [eq_iff_true_of_subsingleton]

section HasZeroMorphisms

variable [HasZeroMorphisms C]

@[simp]
theorem zeroIsoIsInitial_hom {X : C} (t : IsInitial X) : (zeroIsoIsInitial t).hom = 0 := by ext

@[simp]
theorem zeroIsoIsInitial_inv {X : C} (t : IsInitial X) : (zeroIsoIsInitial t).inv = 0 := by ext

@[simp]
theorem zeroIsoIsTerminal_hom {X : C} (t : IsTerminal X) : (zeroIsoIsTerminal t).hom = 0 := by ext

@[simp]
theorem zeroIsoIsTerminal_inv {X : C} (t : IsTerminal X) : (zeroIsoIsTerminal t).inv = 0 := by ext

@[simp]
theorem zeroIsoInitial_hom [HasInitial C] : zeroIsoInitial.hom = (0 : 0 ‚ü∂ ‚ä•_ C) := by ext

@[simp]
theorem zeroIsoInitial_inv [HasInitial C] : zeroIsoInitial.inv = (0 : ‚ä•_ C ‚ü∂ 0) := by ext

@[simp]
theorem zeroIsoTerminal_hom [HasTerminal C] : zeroIsoTerminal.hom = (0 : 0 ‚ü∂ ‚ä§_ C) := by ext

@[simp]
theorem zeroIsoTerminal_inv [HasTerminal C] : zeroIsoTerminal.inv = (0 : ‚ä§_ C ‚ü∂ 0) := by ext

end HasZeroMorphisms

open ZeroObject

instance {B : Type*} [Category B] : HasZeroObject (B ‚•§ C) :=
  (((CategoryTheory.Functor.const B).obj (0 : C)).isZero fun _ ‚Ü¶ isZero_zero _).hasZeroObject

end HasZeroObject

open ZeroObject

variable {D}

@[simp]
theorem IsZero.map [HasZeroObject D] [HasZeroMorphisms D] {F : C ‚•§ D} (hF : IsZero F) {X Y : C}
    (f : X ‚ü∂ Y) : F.map f = 0 :=
  (hF.obj _).eq_of_src _ _

@[simp]
theorem _root_.CategoryTheory.Functor.zero_obj [HasZeroObject D] (X : C) :
    IsZero ((0 : C ‚•§ D).obj X) :=
  (isZero_zero _).obj _

@[simp]
theorem _root_.CategoryTheory.zero_map [HasZeroObject D] [HasZeroMorphisms D] {X Y : C}
    (f : X ‚ü∂ Y) : (0 : C ‚•§ D).map f = 0 :=
  (isZero_zero _).map _

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

@[simp]
theorem id_zero : ùüô (0 : C) = (0 : (0 : C) ‚ü∂ 0) := by apply HasZeroObject.from_zero_ext

-- This can't be a `simp` lemma because the left hand side would be a metavariable.
/-- An arrow ending in the zero object is zero -/
theorem zero_of_to_zero {X : C} (f : X ‚ü∂ 0) : f = 0 := by ext

theorem zero_of_target_iso_zero {X Y : C} (f : X ‚ü∂ Y) (i : Y ‚âÖ 0) : f = 0 := by
  have h : f = f ‚â´ i.hom ‚â´ ùüô 0 ‚â´ i.inv := by simp only [Iso.hom_inv_id, id_comp, comp_id]
  simpa using h

/-- An arrow starting at the zero object is zero -/
theorem zero_of_from_zero {X : C} (f : 0 ‚ü∂ X) : f = 0 := by ext

theorem zero_of_source_iso_zero {X Y : C} (f : X ‚ü∂ Y) (i : X ‚âÖ 0) : f = 0 := by
  have h : f = i.hom ‚â´ ùüô 0 ‚â´ i.inv ‚â´ f := by simp only [Iso.hom_inv_id_assoc, id_comp]
  simpa using h

theorem zero_of_source_iso_zero' {X Y : C} (f : X ‚ü∂ Y) (i : IsIsomorphic X 0) : f = 0 :=
  zero_of_source_iso_zero f (Nonempty.some i)

theorem zero_of_target_iso_zero' {X Y : C} (f : X ‚ü∂ Y) (i : IsIsomorphic Y 0) : f = 0 :=
  zero_of_target_iso_zero f (Nonempty.some i)

theorem mono_of_source_iso_zero {X Y : C} (f : X ‚ü∂ Y) (i : X ‚âÖ 0) : Mono f :=
  ‚ü®fun {Z} g h _ ‚Ü¶ by rw [zero_of_target_iso_zero g i, zero_of_target_iso_zero h i]‚ü©

theorem epi_of_target_iso_zero {X Y : C} (f : X ‚ü∂ Y) (i : Y ‚âÖ 0) : Epi f :=
  ‚ü®fun {Z} g h _ ‚Ü¶ by rw [zero_of_source_iso_zero g i, zero_of_source_iso_zero h i]‚ü©

/-- An object `X` has `ùüô X = 0` if and only if it is isomorphic to the zero object.

Because `X ‚âÖ 0` contains data (even if a subsingleton), we express this `‚Üî` as an `‚âÉ`.
-/
def idZeroEquivIsoZero (X : C) : ùüô X = 0 ‚âÉ (X ‚âÖ 0) where
  toFun h :=
    { hom := 0
      inv := 0 }
  invFun i := zero_of_target_iso_zero (ùüô X) i
  left_inv := by cat_disch
  right_inv := by cat_disch

@[simp]
theorem idZeroEquivIsoZero_apply_hom (X : C) (h : ùüô X = 0) : ((idZeroEquivIsoZero X) h).hom = 0 :=
  rfl

@[simp]
theorem idZeroEquivIsoZero_apply_inv (X : C) (h : ùüô X = 0) : ((idZeroEquivIsoZero X) h).inv = 0 :=
  rfl

/-- If `0 : X ‚ü∂ Y` is a monomorphism, then `X ‚âÖ 0`. -/
@[simps]
def isoZeroOfMonoZero {X Y : C} (_ : Mono (0 : X ‚ü∂ Y)) : X ‚âÖ 0 where
  hom := 0
  inv := 0
  hom_inv_id := (cancel_mono (0 : X ‚ü∂ Y)).mp (by simp)

/-- If `0 : X ‚ü∂ Y` is an epimorphism, then `Y ‚âÖ 0`. -/
@[simps]
def isoZeroOfEpiZero {X Y : C} (_ : Epi (0 : X ‚ü∂ Y)) : Y ‚âÖ 0 where
  hom := 0
  inv := 0
  hom_inv_id := (cancel_epi (0 : X ‚ü∂ Y)).mp (by simp)

/-- If a monomorphism out of `X` is zero, then `X ‚âÖ 0`. -/
def isoZeroOfMonoEqZero {X Y : C} {f : X ‚ü∂ Y} [Mono f] (h : f = 0) : X ‚âÖ 0 := by
  subst h
  apply isoZeroOfMonoZero ‚Äπ_‚Ä∫

/-- If an epimorphism in to `Y` is zero, then `Y ‚âÖ 0`. -/
def isoZeroOfEpiEqZero {X Y : C} {f : X ‚ü∂ Y} [Epi f] (h : f = 0) : Y ‚âÖ 0 := by
  subst h
  apply isoZeroOfEpiZero ‚Äπ_‚Ä∫

/-- If an object `X` is isomorphic to 0, there's no need to use choice to construct
an explicit isomorphism: the zero morphism suffices. -/
def isoOfIsIsomorphicZero {X : C} (P : IsIsomorphic X 0) : X ‚âÖ 0 where
  hom := 0
  inv := 0
  hom_inv_id := by
    have P := P.some
    rw [‚Üê P.hom_inv_id, ‚Üê Category.id_comp P.inv]
    apply Eq.symm
    simp only [id_comp, Iso.hom_inv_id, comp_zero]
    apply (idZeroEquivIsoZero X).invFun P
  inv_hom_id := by simp

end

section IsIso

variable [HasZeroMorphisms C]

/-- A zero morphism `0 : X ‚ü∂ Y` is an isomorphism if and only if
the identities on both `X` and `Y` are zero.
-/
def isIsoZeroEquiv (X Y : C) : IsIso (0 : X ‚ü∂ Y) ‚âÉ ùüô X = 0 ‚àß ùüô Y = 0 where
  toFun := by
    intro i
    rw [‚Üê IsIso.hom_inv_id (0 : X ‚ü∂ Y)]
    rw [‚Üê IsIso.inv_hom_id (0 : X ‚ü∂ Y)]
    simp only [comp_zero,and_self,zero_comp]
  invFun h := ‚ü®‚ü®(0 : Y ‚ü∂ X), by cat_disch‚ü©‚ü©
  left_inv := by cat_disch
  right_inv := by cat_disch

/-- A zero morphism `0 : X ‚ü∂ X` is an isomorphism if and only if
the identity on `X` is zero.
-/
def isIsoZeroSelfEquiv (X : C) : IsIso (0 : X ‚ü∂ X) ‚âÉ ùüô X = 0 := by simpa using isIsoZeroEquiv X X

variable [HasZeroObject C]

open ZeroObject

/-- A zero morphism `0 : X ‚ü∂ Y` is an isomorphism if and only if
`X` and `Y` are isomorphic to the zero object.
-/
def isIsoZeroEquivIsoZero (X Y : C) : IsIso (0 : X ‚ü∂ Y) ‚âÉ (X ‚âÖ 0) √ó (Y ‚âÖ 0) := by
  -- This is lame, because `Prod` can't cope with `Prop`, so we can't use `Equiv.prodCongr`.
  refine (isIsoZeroEquiv X Y).trans ?_
  symm
  fconstructor
  ¬∑ rintro ‚ü®eX, eY‚ü©
    fconstructor
    ¬∑ exact (idZeroEquivIsoZero X).symm eX
    ¬∑ exact (idZeroEquivIsoZero Y).symm eY
  ¬∑ rintro ‚ü®hX, hY‚ü©
    fconstructor
    ¬∑ exact (idZeroEquivIsoZero X) hX
    ¬∑ exact (idZeroEquivIsoZero Y) hY
  ¬∑ cat_disch
  ¬∑ cat_disch

theorem isIso_of_source_target_iso_zero {X Y : C} (f : X ‚ü∂ Y) (i : X ‚âÖ 0) (j : Y ‚âÖ 0) :
    IsIso f := by
  rw [zero_of_source_iso_zero f i]
  exact (isIsoZeroEquivIsoZero _ _).invFun ‚ü®i, j‚ü©

/-- A zero morphism `0 : X ‚ü∂ X` is an isomorphism if and only if
`X` is isomorphic to the zero object.
-/
def isIsoZeroSelfEquivIsoZero (X : C) : IsIso (0 : X ‚ü∂ X) ‚âÉ (X ‚âÖ 0) :=
  (isIsoZeroEquivIsoZero X X).trans subsingletonProdSelfEquiv

end IsIso

/-- If there are zero morphisms, any initial object is a zero object. -/
theorem hasZeroObject_of_hasInitial_object [HasZeroMorphisms C] [HasInitial C] :
    HasZeroObject C := by
  refine ‚ü®‚ü®‚ä•_ C, fun X ‚Ü¶ ‚ü®‚ü®‚ü®0‚ü©, by cat_disch‚ü©‚ü©, fun X ‚Ü¶ ‚ü®‚ü®‚ü®0‚ü©, fun f ‚Ü¶ ?_‚ü©‚ü©‚ü©‚ü©
  calc
    f = f ‚â´ ùüô _ := (Category.comp_id _).symm
    _ = f ‚â´ 0 := by congr!; subsingleton
    _ = 0 := HasZeroMorphisms.comp_zero _ _

/-- If there are zero morphisms, any terminal object is a zero object. -/
theorem hasZeroObject_of_hasTerminal_object [HasZeroMorphisms C] [HasTerminal C] :
    HasZeroObject C := by
  refine ‚ü®‚ü®‚ä§_ C, fun X ‚Ü¶ ‚ü®‚ü®‚ü®0‚ü©, fun f ‚Ü¶ ?_‚ü©‚ü©, fun X ‚Ü¶ ‚ü®‚ü®‚ü®0‚ü©, by cat_disch‚ü©‚ü©‚ü©‚ü©
  calc
    f = ùüô _ ‚â´ f := (Category.id_comp _).symm
    _ = 0 ‚â´ f := by congr!; subsingleton
    _ = 0 := zero_comp

section Image

variable [HasZeroMorphisms C]

theorem image_Œπ_comp_eq_zero {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} [HasImage f]
    [Epi (factorThruImage f)] (h : f ‚â´ g = 0) : image.Œπ f ‚â´ g = 0 :=
  zero_of_epi_comp (factorThruImage f) <| by simp [h]

theorem comp_factorThruImage_eq_zero {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} [HasImage g]
    (h : f ‚â´ g = 0) : f ‚â´ factorThruImage g = 0 :=
  zero_of_comp_mono (image.Œπ g) <| by simp [h]

variable [HasZeroObject C]

open ZeroObject

/-- The zero morphism has a `MonoFactorisation` through the zero object.
-/
@[simps]
def monoFactorisationZero (X Y : C) : MonoFactorisation (0 : X ‚ü∂ Y) where
  I := 0
  m := 0
  e := 0

/-- The factorisation through the zero object is an image factorisation.
-/
def imageFactorisationZero (X Y : C) : ImageFactorisation (0 : X ‚ü∂ Y) where
  F := monoFactorisationZero X Y
  isImage := { lift := fun _ ‚Ü¶ 0 }

instance hasImage_zero {X Y : C} : HasImage (0 : X ‚ü∂ Y) :=
  HasImage.mk <| imageFactorisationZero _ _

/-- The image of a zero morphism is the zero object. -/
def imageZero {X Y : C} : image (0 : X ‚ü∂ Y) ‚âÖ 0 :=
  IsImage.isoExt (Image.isImage (0 : X ‚ü∂ Y)) (imageFactorisationZero X Y).isImage

/-- The image of a morphism which is equal to zero is the zero object. -/
def imageZero' {X Y : C} {f : X ‚ü∂ Y} (h : f = 0) [HasImage f] : image f ‚âÖ 0 :=
  image.eqToIso h ‚â™‚â´ imageZero

@[simp]
theorem image.Œπ_zero {X Y : C} [HasImage (0 : X ‚ü∂ Y)] : image.Œπ (0 : X ‚ü∂ Y) = 0 := by
  rw [‚Üê image.lift_fac (monoFactorisationZero X Y)]
  simp

/-- If we know `f = 0`,
it requires a little work to conclude `image.Œπ f = 0`,
because `f = g` only implies `image f ‚âÖ image g`.
-/
@[simp]
theorem image.Œπ_zero' [HasEqualizers C] {X Y : C} {f : X ‚ü∂ Y} (h : f = 0) [HasImage f] :
    image.Œπ f = 0 := by
  rw [image.eq_fac h]
  simp

end Image

/-- In the presence of zero morphisms, coprojections into a coproduct are (split) monomorphisms. -/
instance isSplitMono_sigma_Œπ {Œ≤ : Type u'} [HasZeroMorphisms C] (f : Œ≤ ‚Üí C)
    [HasColimit (Discrete.functor f)] (b : Œ≤) : IsSplitMono (Sigma.Œπ f b) := by
  classical exact IsSplitMono.mk' { retraction := Sigma.desc <| Pi.single b (ùüô _) }

/-- In the presence of zero morphisms, projections into a product are (split) epimorphisms. -/
instance isSplitEpi_pi_œÄ {Œ≤ : Type u'} [HasZeroMorphisms C] (f : Œ≤ ‚Üí C)
    [HasLimit (Discrete.functor f)] (b : Œ≤) : IsSplitEpi (Pi.œÄ f b) := by
  classical exact IsSplitEpi.mk' { section_ := Pi.lift <| Pi.single b (ùüô _) }

/-- In the presence of zero morphisms, coprojections into a coproduct are (split) monomorphisms. -/
instance isSplitMono_coprod_inl [HasZeroMorphisms C] {X Y : C} [HasColimit (pair X Y)] :
    IsSplitMono (coprod.inl : X ‚ü∂ X ‚®ø Y) :=
  IsSplitMono.mk' { retraction := coprod.desc (ùüô X) 0 }

/-- In the presence of zero morphisms, coprojections into a coproduct are (split) monomorphisms. -/
instance isSplitMono_coprod_inr [HasZeroMorphisms C] {X Y : C} [HasColimit (pair X Y)] :
    IsSplitMono (coprod.inr : Y ‚ü∂ X ‚®ø Y) :=
  IsSplitMono.mk' { retraction := coprod.desc 0 (ùüô Y) }

/-- In the presence of zero morphisms, projections into a product are (split) epimorphisms. -/
instance isSplitEpi_prod_fst [HasZeroMorphisms C] {X Y : C} [HasLimit (pair X Y)] :
    IsSplitEpi (prod.fst : X ‚®Ø Y ‚ü∂ X) :=
  IsSplitEpi.mk' { section_ := prod.lift (ùüô X) 0 }

/-- In the presence of zero morphisms, projections into a product are (split) epimorphisms. -/
instance isSplitEpi_prod_snd [HasZeroMorphisms C] {X Y : C} [HasLimit (pair X Y)] :
    IsSplitEpi (prod.snd : X ‚®Ø Y ‚ü∂ Y) :=
  IsSplitEpi.mk' { section_ := prod.lift 0 (ùüô Y) }


section

variable [HasZeroMorphisms C] [HasZeroObject C] {F : D ‚•§ C}

/-- If a functor `F` is zero, then any cone for `F` with a zero point is limit. -/
def IsLimit.ofIsZero (c : Cone F) (hF : IsZero F) (hc : IsZero c.pt) : IsLimit c where
  lift _ := 0
  fac _ j := (F.isZero_iff.1 hF j).eq_of_tgt _ _
  uniq _ _ _ := hc.eq_of_tgt _ _

/-- If a functor `F` is zero, then any cocone for `F` with a zero point is colimit. -/
def IsColimit.ofIsZero (c : Cocone F) (hF : IsZero F) (hc : IsZero c.pt) : IsColimit c where
  desc _ := 0
  fac _ j := (F.isZero_iff.1 hF j).eq_of_src _ _
  uniq _ _ _ := hc.eq_of_src _ _

lemma IsLimit.isZero_pt {c : Cone F} (hc : IsLimit c) (hF : IsZero F) : IsZero c.pt :=
  (isZero_zero C).of_iso (IsLimit.conePointUniqueUpToIso hc
    (IsLimit.ofIsZero (Cone.mk 0 0) hF (isZero_zero C)))

lemma IsColimit.isZero_pt {c : Cocone F} (hc : IsColimit c) (hF : IsZero F) : IsZero c.pt :=
  (isZero_zero C).of_iso (IsColimit.coconePointUniqueUpToIso hc
    (IsColimit.ofIsZero (Cocone.mk 0 0) hF (isZero_zero C)))

end

section

variable [HasZeroMorphisms C]

lemma IsTerminal.isZero {X : C} (hX : IsTerminal X) : IsZero X := by
  rw [IsZero.iff_id_eq_zero]
  apply hX.hom_ext

lemma IsInitial.isZero {X : C} (hX : IsInitial X) : IsZero X := by
  rw [IsZero.iff_id_eq_zero]
  apply hX.hom_ext

end

section PiIota

variable [HasZeroMorphisms C] {Œ≤ : Type w} [DecidableEq Œ≤] (f : Œ≤ ‚Üí C) [HasProduct f]

/-- In the presence of 0-morphism we can define an inclusion morphism into any product. -/
def Pi.Œπ (b : Œ≤) : f b ‚ü∂ ‚àè·∂ú f :=
  Pi.lift (Function.update (fun _ ‚Ü¶ 0) b (ùüô _))

@[reassoc (attr := simp)]
lemma Pi.Œπ_œÄ_eq_id (b : Œ≤) : Pi.Œπ f b ‚â´ Pi.œÄ f b = ùüô _ := by
  simp [Pi.Œπ]

@[reassoc]
lemma Pi.Œπ_œÄ_of_ne {b c : Œ≤} (h : b ‚â† c) : Pi.Œπ f b ‚â´ Pi.œÄ f c = 0 := by
  simp [Pi.Œπ, Function.update_of_ne h.symm]

@[reassoc]
lemma Pi.Œπ_œÄ (b c : Œ≤) :
    Pi.Œπ f b ‚â´ Pi.œÄ f c = if h : b = c then eqToHom (congrArg f h) else 0 := by
  split_ifs with h
  ¬∑ subst h; simp
  ¬∑ simp [Pi.Œπ_œÄ_of_ne f h]

instance (b : Œ≤) : Mono (Pi.Œπ f b) where
  right_cancellation _ _ e := by simpa using congrArg (¬∑ ‚â´ Pi.œÄ f b) e

end PiIota

section SigmaPi

variable [HasZeroMorphisms C] {Œ≤ : Type w} [DecidableEq Œ≤] (f : Œ≤ ‚Üí C) [HasCoproduct f]

/-- In the presence of 0-morphisms we can define a projection morphism from any coproduct. -/
def Sigma.œÄ (b : Œ≤) : ‚àê f ‚ü∂ f b :=
  Limits.Sigma.desc (Function.update (fun _ ‚Ü¶ 0) b (ùüô _))

@[reassoc (attr := simp)]
lemma Sigma.Œπ_œÄ_eq_id (b : Œ≤) : Sigma.Œπ f b ‚â´ Sigma.œÄ f b = ùüô _ := by
  simp [Sigma.œÄ]

@[reassoc]
lemma Sigma.Œπ_œÄ_of_ne {b c : Œ≤} (h : b ‚â† c) : Sigma.Œπ f b ‚â´ Sigma.œÄ f c = 0 := by
  simp [Sigma.œÄ, Function.update_of_ne h]

@[reassoc]
theorem Sigma.Œπ_œÄ (b c : Œ≤) :
    Sigma.Œπ f b ‚â´ Sigma.œÄ f c = if h : b = c then eqToHom (congrArg f h) else 0 := by
  split_ifs with h
  ¬∑ subst h; simp
  ¬∑ simp [Sigma.Œπ_œÄ_of_ne f h]

instance (b : Œ≤) : Epi (Sigma.œÄ f b) where
  left_cancellation _ _ e := by simpa using congrArg (Sigma.Œπ f b ‚â´ ¬∑) e

end SigmaPi

section ProdInlInr

variable [HasZeroMorphisms C] (X Y : C) [HasBinaryProduct X Y]

/-- If a category `C` has 0-morphisms, there is a canonical inclusion from the first component `X`
into any product of objects `X ‚®Ø Y`. -/
def prod.inl : X ‚ü∂ X ‚®Ø Y :=
  prod.lift (ùüô _) 0

/-- If a category `C` has 0-morphisms, there is a canonical inclusion from the second component `Y`
into any product of objects `X ‚®Ø Y`. -/
def prod.inr : Y ‚ü∂ X ‚®Ø Y :=
  prod.lift 0 (ùüô _)

@[reassoc (attr := simp)]
lemma prod.inl_fst : prod.inl X Y ‚â´ prod.fst = ùüô X := by
  simp [prod.inl]

@[reassoc (attr := simp)]
lemma prod.inl_snd : prod.inl X Y ‚â´ prod.snd = 0 := by
  simp [prod.inl]

@[reassoc (attr := simp)]
lemma prod.inr_fst : prod.inr X Y ‚â´ prod.fst = 0 := by
  simp [prod.inr]

@[reassoc (attr := simp)]
lemma prod.inr_snd : prod.inr X Y ‚â´ prod.snd = ùüô Y := by
  simp [prod.inr]

instance : Mono (prod.inl X Y) where
  right_cancellation _ _ e := by simpa using congrArg (¬∑ ‚â´ prod.fst) e

instance : Mono (prod.inr X Y) where
  right_cancellation _ _ e := by simpa using congrArg (¬∑ ‚â´ prod.snd) e

end ProdInlInr

section CoprodFstSnd

variable [HasZeroMorphisms C] (X Y : C) [HasBinaryCoproduct X Y]

/-- If a category `C` has 0-morphisms, there is a canonical projection from a coproduct `X ‚®ø Y` to
its first component `X`. -/
def coprod.fst : X ‚®ø Y ‚ü∂ X :=
  coprod.desc (ùüô _) 0

/-- If a category `C` has 0-morphisms, there is a canonical projection from a coproduct `X ‚®ø Y` to
its second component `Y`. -/
def coprod.snd : X ‚®ø Y ‚ü∂ Y :=
  coprod.desc 0 (ùüô _)

@[reassoc (attr := simp)]
lemma coprod.inl_fst : coprod.inl ‚â´ coprod.fst X Y = ùüô X := by
  simp [coprod.fst]

@[reassoc (attr := simp)]
lemma coprod.inr_fst : coprod.inr ‚â´ coprod.fst X Y = 0 := by
  simp [coprod.fst]

@[reassoc (attr := simp)]
lemma coprod.inl_snd : coprod.inl ‚â´ coprod.snd X Y = 0 := by
  simp [coprod.snd]

@[reassoc (attr := simp)]
lemma coprod.inr_snd : coprod.inr ‚â´ coprod.snd X Y = ùüô Y := by
  simp [coprod.snd]

instance : Epi (coprod.fst X Y) where
  left_cancellation _ _ e := by simpa using congrArg (coprod.inl ‚â´ ¬∑) e

instance : Epi (coprod.snd X Y) where
  left_cancellation _ _ e := by simpa using congrArg (coprod.inr ‚â´ ¬∑) e

end CoprodFstSnd

end CategoryTheory.Limits
