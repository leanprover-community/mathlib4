/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Notation.Pi.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.CategoryTheory.Limits.Shapes.Images
import Mathlib.CategoryTheory.IsomorphismClasses
import Mathlib.CategoryTheory.Limits.Shapes.ZeroObjects

/-!
# Zero morphisms and zero objects

A category "has zero morphisms" if there is a designated "zero morphism" in each morphism space,
and compositions of zero morphisms with anything give the zero morphism. (Notice this is extra
structure, not merely a property.)

A category "has a zero object" if it has an object which is both initial and terminal. Having a
zero object provides zero morphisms, as the unique morphisms factoring through the zero object.

## References

* https://en.wikipedia.org/wiki/Zero_morphism
* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/


noncomputable section

universe w v v' u u'

open CategoryTheory

open CategoryTheory.Category

namespace CategoryTheory.Limits

variable (C : Type u) [Category.{v} C]
variable (D : Type u') [Category.{v'} D]

/-- A category "has zero morphisms" if there is a designated "zero morphism" in each morphism space,
and compositions of zero morphisms with anything give the zero morphism. -/
class HasZeroMorphisms where
  /-- Every morphism space has zero -/
  [zero : âˆ€ X Y : C, Zero (X âŸ¶ Y)]
  /-- `f` composed with `0` is `0` -/
  comp_zero : âˆ€ {X Y : C} (f : X âŸ¶ Y) (Z : C), f â‰« (0 : Y âŸ¶ Z) = (0 : X âŸ¶ Z) := by cat_disch
  /-- `0` composed with `f` is `0` -/
  zero_comp : âˆ€ (X : C) {Y Z : C} (f : Y âŸ¶ Z), (0 : X âŸ¶ Y) â‰« f = (0 : X âŸ¶ Z) := by cat_disch

attribute [instance] HasZeroMorphisms.zero

variable {C}

@[simp]
theorem comp_zero [HasZeroMorphisms C] {X Y : C} {f : X âŸ¶ Y} {Z : C} :
    f â‰« (0 : Y âŸ¶ Z) = (0 : X âŸ¶ Z) :=
  HasZeroMorphisms.comp_zero f Z

@[simp]
theorem zero_comp [HasZeroMorphisms C] {X : C} {Y Z : C} {f : Y âŸ¶ Z} :
    (0 : X âŸ¶ Y) â‰« f = (0 : X âŸ¶ Z) :=
  HasZeroMorphisms.zero_comp X f

instance hasZeroMorphismsPEmpty : HasZeroMorphisms (Discrete PEmpty) where
  zero := by cat_disch

instance hasZeroMorphismsPUnit : HasZeroMorphisms (Discrete PUnit) where
  zero X Y := by repeat (constructor)

namespace HasZeroMorphisms

/-- This lemma will be immediately superseded by `ext`, below. -/
private theorem ext_aux (I J : HasZeroMorphisms C)
    (w : âˆ€ X Y : C, (I.zero X Y).zero = (J.zero X Y).zero) : I = J := by
  have : I.zero = J.zero := by
    funext X Y
    specialize w X Y
    apply congrArg Zero.mk w
  cases I; cases J
  congr
  Â· apply proof_irrel_heq
  Â· apply proof_irrel_heq

/-- If you're tempted to use this lemma "in the wild", you should probably
carefully consider whether you've made a mistake in allowing two
instances of `HasZeroMorphisms` to exist at all.

See, particularly, the note on `zeroMorphismsOfZeroObject` below.
-/
theorem ext (I J : HasZeroMorphisms C) : I = J := by
  apply ext_aux
  intro X Y
  have : (I.zero X Y).zero â‰« (J.zero Y Y).zero = (I.zero X Y).zero := by
    apply I.zero_comp X (J.zero Y Y).zero
  have that : (I.zero X Y).zero â‰« (J.zero Y Y).zero = (J.zero X Y).zero := by
    apply J.comp_zero (I.zero X Y).zero Y
  rw [â† this, â† that]

instance : Subsingleton (HasZeroMorphisms C) :=
  âŸ¨extâŸ©

end HasZeroMorphisms

open Opposite HasZeroMorphisms

instance hasZeroMorphismsOpposite [HasZeroMorphisms C] : HasZeroMorphisms Cáµ’áµ– where
  zero X Y := âŸ¨(0 : unop Y âŸ¶ unop X).opâŸ©
  comp_zero f Z := congr_arg Quiver.Hom.op (HasZeroMorphisms.zero_comp (unop Z) f.unop)
  zero_comp X {Y Z} (f : Y âŸ¶ Z) :=
    congrArg Quiver.Hom.op (HasZeroMorphisms.comp_zero f.unop (unop X))

section

variable [HasZeroMorphisms C]

@[simp] lemma op_zero (X Y : C) : (0 : X âŸ¶ Y).op = 0 := rfl

@[simp] lemma unop_zero (X Y : Cáµ’áµ–) : (0 : X âŸ¶ Y).unop = 0 := rfl

theorem zero_of_comp_mono {X Y Z : C} {f : X âŸ¶ Y} (g : Y âŸ¶ Z) [Mono g] (h : f â‰« g = 0) : f = 0 := by
  rw [â† zero_comp, cancel_mono] at h
  exact h

theorem zero_of_epi_comp {X Y Z : C} (f : X âŸ¶ Y) {g : Y âŸ¶ Z} [Epi f] (h : f â‰« g = 0) : g = 0 := by
  rw [â† comp_zero, cancel_epi] at h
  exact h

theorem eq_zero_of_image_eq_zero {X Y : C} {f : X âŸ¶ Y} [HasImage f] (w : image.Î¹ f = 0) :
    f = 0 := by rw [â† image.fac f, w, HasZeroMorphisms.comp_zero]

theorem nonzero_image_of_nonzero {X Y : C} {f : X âŸ¶ Y} [HasImage f] (w : f â‰  0) : image.Î¹ f â‰  0 :=
  fun h => w (eq_zero_of_image_eq_zero h)

end

section

variable [HasZeroMorphisms D]

instance : HasZeroMorphisms (C â¥¤ D) where
  zero F G := âŸ¨{ app := fun _ => 0 }âŸ©
  comp_zero := fun Î· H => by
    ext X; dsimp; apply comp_zero
  zero_comp := fun F {G H} Î· => by
    ext X; dsimp; apply zero_comp

@[simp]
theorem zero_app (F G : C â¥¤ D) (j : C) : (0 : F âŸ¶ G).app j = 0 := rfl

end

namespace IsZero

variable [HasZeroMorphisms C]

theorem eq_zero_of_src {X Y : C} (o : IsZero X) (f : X âŸ¶ Y) : f = 0 :=
  o.eq_of_src _ _

theorem eq_zero_of_tgt {X Y : C} (o : IsZero Y) (f : X âŸ¶ Y) : f = 0 :=
  o.eq_of_tgt _ _

theorem iff_id_eq_zero (X : C) : IsZero X â†” ğŸ™ X = 0 :=
  âŸ¨fun h => h.eq_of_src _ _, fun h =>
    âŸ¨fun Y => âŸ¨âŸ¨âŸ¨0âŸ©, fun f => by
        rw [â† id_comp f, â† id_comp (0 : X âŸ¶ Y), h, zero_comp, zero_comp]; simp onlyâŸ©âŸ©,
    fun Y => âŸ¨âŸ¨âŸ¨0âŸ©, fun f => by
        rw [â† comp_id f, â† comp_id (0 : Y âŸ¶ X), h, comp_zero, comp_zero]; simp only âŸ©âŸ©âŸ©âŸ©

theorem of_mono_zero (X Y : C) [Mono (0 : X âŸ¶ Y)] : IsZero X :=
  (iff_id_eq_zero X).mpr ((cancel_mono (0 : X âŸ¶ Y)).1 (by simp))

theorem of_epi_zero (X Y : C) [Epi (0 : X âŸ¶ Y)] : IsZero Y :=
  (iff_id_eq_zero Y).mpr ((cancel_epi (0 : X âŸ¶ Y)).1 (by simp))

theorem of_mono_eq_zero {X Y : C} (f : X âŸ¶ Y) [Mono f] (h : f = 0) : IsZero X := by
  subst h
  apply of_mono_zero X Y

theorem of_epi_eq_zero {X Y : C} (f : X âŸ¶ Y) [Epi f] (h : f = 0) : IsZero Y := by
  subst h
  apply of_epi_zero X Y

theorem iff_isSplitMono_eq_zero {X Y : C} (f : X âŸ¶ Y) [IsSplitMono f] : IsZero X â†” f = 0 := by
  rw [iff_id_eq_zero]
  constructor
  Â· intro h
    rw [â† Category.id_comp f, h, zero_comp]
  Â· intro h
    rw [â† IsSplitMono.id f]
    simp only [h, zero_comp]

theorem iff_isSplitEpi_eq_zero {X Y : C} (f : X âŸ¶ Y) [IsSplitEpi f] : IsZero Y â†” f = 0 := by
  rw [iff_id_eq_zero]
  constructor
  Â· intro h
    rw [â† Category.comp_id f, h, comp_zero]
  Â· intro h
    rw [â† IsSplitEpi.id f]
    simp [h]

theorem of_mono {X Y : C} (f : X âŸ¶ Y) [Mono f] (i : IsZero Y) : IsZero X := by
  have hf := i.eq_zero_of_tgt f
  subst hf
  exact IsZero.of_mono_zero X Y

theorem of_epi {X Y : C} (f : X âŸ¶ Y) [Epi f] (i : IsZero X) : IsZero Y := by
  have hf := i.eq_zero_of_src f
  subst hf
  exact IsZero.of_epi_zero X Y

end IsZero

/-- A category with a zero object has zero morphisms.

It is rarely a good idea to use this. Many categories that have a zero object have zero
morphisms for some other reason, for example from additivity. Library code that uses
`zeroMorphismsOfZeroObject` will then be incompatible with these categories because
the `HasZeroMorphisms` instances will not be definitionally equal. For this reason library
code should generally ask for an instance of `HasZeroMorphisms` separately, even if it already
asks for an instance of `HasZeroObjects`. -/
def IsZero.hasZeroMorphisms {O : C} (hO : IsZero O) : HasZeroMorphisms C where
  zero X Y := { zero := hO.from_ X â‰« hO.to_ Y }
  zero_comp X {Y Z} f := by
    change (hO.from_ X â‰« hO.to_ Y) â‰« f = hO.from_ X â‰« hO.to_ Z
    rw [Category.assoc]
    congr
    apply hO.eq_of_src
  comp_zero {X Y} f Z := by
    change f â‰« (hO.from_ Y â‰« hO.to_ Z) = hO.from_ X â‰« hO.to_ Z
    rw [â† Category.assoc]
    congr
    apply hO.eq_of_tgt

namespace HasZeroObject

variable [HasZeroObject C]

open ZeroObject

/-- A category with a zero object has zero morphisms.

It is rarely a good idea to use this. Many categories that have a zero object have zero
morphisms for some other reason, for example from additivity. Library code that uses
`zeroMorphismsOfZeroObject` will then be incompatible with these categories because
the `has_zero_morphisms` instances will not be definitionally equal. For this reason library
code should generally ask for an instance of `HasZeroMorphisms` separately, even if it already
asks for an instance of `HasZeroObjects`. -/
def zeroMorphismsOfZeroObject : HasZeroMorphisms C where
  zero X _ := { zero := (default : X âŸ¶ 0) â‰« default }
  zero_comp X {Y Z} f := by
    change ((default : X âŸ¶ 0) â‰« default) â‰« f = (default : X âŸ¶ 0) â‰« default
    rw [Category.assoc]
    congr
    simp only [eq_iff_true_of_subsingleton]
  comp_zero {X Y} f Z := by
    change f â‰« (default : Y âŸ¶ 0) â‰« default = (default : X âŸ¶ 0) â‰« default
    rw [â† Category.assoc]
    congr
    simp only [eq_iff_true_of_subsingleton]

section HasZeroMorphisms

variable [HasZeroMorphisms C]

@[simp]
theorem zeroIsoIsInitial_hom {X : C} (t : IsInitial X) : (zeroIsoIsInitial t).hom = 0 := by ext

@[simp]
theorem zeroIsoIsInitial_inv {X : C} (t : IsInitial X) : (zeroIsoIsInitial t).inv = 0 := by ext

@[simp]
theorem zeroIsoIsTerminal_hom {X : C} (t : IsTerminal X) : (zeroIsoIsTerminal t).hom = 0 := by ext

@[simp]
theorem zeroIsoIsTerminal_inv {X : C} (t : IsTerminal X) : (zeroIsoIsTerminal t).inv = 0 := by ext

@[simp]
theorem zeroIsoInitial_hom [HasInitial C] : zeroIsoInitial.hom = (0 : 0 âŸ¶ âŠ¥_ C) := by ext

@[simp]
theorem zeroIsoInitial_inv [HasInitial C] : zeroIsoInitial.inv = (0 : âŠ¥_ C âŸ¶ 0) := by ext

@[simp]
theorem zeroIsoTerminal_hom [HasTerminal C] : zeroIsoTerminal.hom = (0 : 0 âŸ¶ âŠ¤_ C) := by ext

@[simp]
theorem zeroIsoTerminal_inv [HasTerminal C] : zeroIsoTerminal.inv = (0 : âŠ¤_ C âŸ¶ 0) := by ext

end HasZeroMorphisms

open ZeroObject

instance {B : Type*} [Category B] : HasZeroObject (B â¥¤ C) :=
  (((CategoryTheory.Functor.const B).obj (0 : C)).isZero fun _ => isZero_zero _).hasZeroObject

end HasZeroObject

open ZeroObject

variable {D}

@[simp]
theorem IsZero.map [HasZeroObject D] [HasZeroMorphisms D] {F : C â¥¤ D} (hF : IsZero F) {X Y : C}
    (f : X âŸ¶ Y) : F.map f = 0 :=
  (hF.obj _).eq_of_src _ _

@[simp]
theorem _root_.CategoryTheory.Functor.zero_obj [HasZeroObject D] (X : C) :
    IsZero ((0 : C â¥¤ D).obj X) :=
  (isZero_zero _).obj _

@[simp]
theorem _root_.CategoryTheory.zero_map [HasZeroObject D] [HasZeroMorphisms D] {X Y : C}
    (f : X âŸ¶ Y) : (0 : C â¥¤ D).map f = 0 :=
  (isZero_zero _).map _

section

variable [HasZeroObject C] [HasZeroMorphisms C]

open ZeroObject

@[simp]
theorem id_zero : ğŸ™ (0 : C) = (0 : (0 : C) âŸ¶ 0) := by apply HasZeroObject.from_zero_ext

-- This can't be a `simp` lemma because the left-hand side would be a metavariable.
/-- An arrow ending in the zero object is zero -/
theorem zero_of_to_zero {X : C} (f : X âŸ¶ 0) : f = 0 := by ext

theorem zero_of_target_iso_zero {X Y : C} (f : X âŸ¶ Y) (i : Y â‰… 0) : f = 0 := by
  have h : f = f â‰« i.hom â‰« ğŸ™ 0 â‰« i.inv := by simp only [Iso.hom_inv_id, id_comp, comp_id]
  simpa using h

/-- An arrow starting at the zero object is zero -/
theorem zero_of_from_zero {X : C} (f : 0 âŸ¶ X) : f = 0 := by ext

theorem zero_of_source_iso_zero {X Y : C} (f : X âŸ¶ Y) (i : X â‰… 0) : f = 0 := by
  have h : f = i.hom â‰« ğŸ™ 0 â‰« i.inv â‰« f := by simp only [Iso.hom_inv_id_assoc, id_comp]
  simpa using h

theorem zero_of_source_iso_zero' {X Y : C} (f : X âŸ¶ Y) (i : IsIsomorphic X 0) : f = 0 :=
  zero_of_source_iso_zero f (Nonempty.some i)

theorem zero_of_target_iso_zero' {X Y : C} (f : X âŸ¶ Y) (i : IsIsomorphic Y 0) : f = 0 :=
  zero_of_target_iso_zero f (Nonempty.some i)

theorem mono_of_source_iso_zero {X Y : C} (f : X âŸ¶ Y) (i : X â‰… 0) : Mono f :=
  âŸ¨fun {Z} g h _ => by rw [zero_of_target_iso_zero g i, zero_of_target_iso_zero h i]âŸ©

theorem epi_of_target_iso_zero {X Y : C} (f : X âŸ¶ Y) (i : Y â‰… 0) : Epi f :=
  âŸ¨fun {Z} g h _ => by rw [zero_of_source_iso_zero g i, zero_of_source_iso_zero h i]âŸ©

/-- An object `X` has `ğŸ™ X = 0` if and only if it is isomorphic to the zero object.

Because `X â‰… 0` contains data (even if a subsingleton), we express this `â†”` as an `â‰ƒ`.
-/
def idZeroEquivIsoZero (X : C) : ğŸ™ X = 0 â‰ƒ (X â‰… 0) where
  toFun h :=
    { hom := 0
      inv := 0 }
  invFun i := zero_of_target_iso_zero (ğŸ™ X) i
  left_inv := by cat_disch
  right_inv := by cat_disch

@[simp]
theorem idZeroEquivIsoZero_apply_hom (X : C) (h : ğŸ™ X = 0) : ((idZeroEquivIsoZero X) h).hom = 0 :=
  rfl

@[simp]
theorem idZeroEquivIsoZero_apply_inv (X : C) (h : ğŸ™ X = 0) : ((idZeroEquivIsoZero X) h).inv = 0 :=
  rfl

/-- If `0 : X âŸ¶ Y` is a monomorphism, then `X â‰… 0`. -/
@[simps]
def isoZeroOfMonoZero {X Y : C} (_ : Mono (0 : X âŸ¶ Y)) : X â‰… 0 where
  hom := 0
  inv := 0
  hom_inv_id := (cancel_mono (0 : X âŸ¶ Y)).mp (by simp)

/-- If `0 : X âŸ¶ Y` is an epimorphism, then `Y â‰… 0`. -/
@[simps]
def isoZeroOfEpiZero {X Y : C} (_ : Epi (0 : X âŸ¶ Y)) : Y â‰… 0 where
  hom := 0
  inv := 0
  hom_inv_id := (cancel_epi (0 : X âŸ¶ Y)).mp (by simp)

/-- If a monomorphism out of `X` is zero, then `X â‰… 0`. -/
def isoZeroOfMonoEqZero {X Y : C} {f : X âŸ¶ Y} [Mono f] (h : f = 0) : X â‰… 0 := by
  subst h
  apply isoZeroOfMonoZero â€¹_â€º

/-- If an epimorphism in to `Y` is zero, then `Y â‰… 0`. -/
def isoZeroOfEpiEqZero {X Y : C} {f : X âŸ¶ Y} [Epi f] (h : f = 0) : Y â‰… 0 := by
  subst h
  apply isoZeroOfEpiZero â€¹_â€º

/-- If an object `X` is isomorphic to 0, there's no need to use choice to construct
an explicit isomorphism: the zero morphism suffices. -/
def isoOfIsIsomorphicZero {X : C} (P : IsIsomorphic X 0) : X â‰… 0 where
  hom := 0
  inv := 0
  hom_inv_id := by
    have P := P.some
    rw [â† P.hom_inv_id, â† Category.id_comp P.inv]
    apply Eq.symm
    simp only [id_comp, Iso.hom_inv_id, comp_zero]
    apply (idZeroEquivIsoZero X).invFun P
  inv_hom_id := by simp

end

section IsIso

variable [HasZeroMorphisms C]

/-- A zero morphism `0 : X âŸ¶ Y` is an isomorphism if and only if
the identities on both `X` and `Y` are zero.
-/
def isIsoZeroEquiv (X Y : C) : IsIso (0 : X âŸ¶ Y) â‰ƒ ğŸ™ X = 0 âˆ§ ğŸ™ Y = 0 where
  toFun := by
    intro i
    rw [â† IsIso.hom_inv_id (0 : X âŸ¶ Y)]
    rw [â† IsIso.inv_hom_id (0 : X âŸ¶ Y)]
    simp only [comp_zero,and_self,zero_comp]
  invFun h := âŸ¨âŸ¨(0 : Y âŸ¶ X), by cat_dischâŸ©âŸ©
  left_inv := by cat_disch
  right_inv := by cat_disch

/-- A zero morphism `0 : X âŸ¶ X` is an isomorphism if and only if
the identity on `X` is zero.
-/
def isIsoZeroSelfEquiv (X : C) : IsIso (0 : X âŸ¶ X) â‰ƒ ğŸ™ X = 0 := by simpa using isIsoZeroEquiv X X

variable [HasZeroObject C]

open ZeroObject

/-- A zero morphism `0 : X âŸ¶ Y` is an isomorphism if and only if
`X` and `Y` are isomorphic to the zero object.
-/
def isIsoZeroEquivIsoZero (X Y : C) : IsIso (0 : X âŸ¶ Y) â‰ƒ (X â‰… 0) Ã— (Y â‰… 0) := by
  -- This is lame, because `Prod` can't cope with `Prop`, so we can't use `Equiv.prodCongr`.
  refine (isIsoZeroEquiv X Y).trans ?_
  symm
  fconstructor
  Â· rintro âŸ¨eX, eYâŸ©
    fconstructor
    Â· exact (idZeroEquivIsoZero X).symm eX
    Â· exact (idZeroEquivIsoZero Y).symm eY
  Â· rintro âŸ¨hX, hYâŸ©
    fconstructor
    Â· exact (idZeroEquivIsoZero X) hX
    Â· exact (idZeroEquivIsoZero Y) hY
  Â· cat_disch
  Â· cat_disch

theorem isIso_of_source_target_iso_zero {X Y : C} (f : X âŸ¶ Y) (i : X â‰… 0) (j : Y â‰… 0) :
    IsIso f := by
  rw [zero_of_source_iso_zero f i]
  exact (isIsoZeroEquivIsoZero _ _).invFun âŸ¨i, jâŸ©

/-- A zero morphism `0 : X âŸ¶ X` is an isomorphism if and only if
`X` is isomorphic to the zero object.
-/
def isIsoZeroSelfEquivIsoZero (X : C) : IsIso (0 : X âŸ¶ X) â‰ƒ (X â‰… 0) :=
  (isIsoZeroEquivIsoZero X X).trans subsingletonProdSelfEquiv

end IsIso

/-- If there are zero morphisms, any initial object is a zero object. -/
theorem hasZeroObject_of_hasInitial_object [HasZeroMorphisms C] [HasInitial C] :
    HasZeroObject C := by
  refine âŸ¨âŸ¨âŠ¥_ C, fun X => âŸ¨âŸ¨âŸ¨0âŸ©, by cat_dischâŸ©âŸ©, fun X => âŸ¨âŸ¨âŸ¨0âŸ©, fun f => ?_âŸ©âŸ©âŸ©âŸ©
  calc
    f = f â‰« ğŸ™ _ := (Category.comp_id _).symm
    _ = f â‰« 0 := by congr!; subsingleton
    _ = 0 := HasZeroMorphisms.comp_zero _ _

/-- If there are zero morphisms, any terminal object is a zero object. -/
theorem hasZeroObject_of_hasTerminal_object [HasZeroMorphisms C] [HasTerminal C] :
    HasZeroObject C := by
  refine âŸ¨âŸ¨âŠ¤_ C, fun X => âŸ¨âŸ¨âŸ¨0âŸ©, fun f => ?_âŸ©âŸ©, fun X => âŸ¨âŸ¨âŸ¨0âŸ©, by cat_dischâŸ©âŸ©âŸ©âŸ©
  calc
    f = ğŸ™ _ â‰« f := (Category.id_comp _).symm
    _ = 0 â‰« f := by congr!; subsingleton
    _ = 0 := zero_comp

section Image

variable [HasZeroMorphisms C]

theorem image_Î¹_comp_eq_zero {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} [HasImage f]
    [Epi (factorThruImage f)] (h : f â‰« g = 0) : image.Î¹ f â‰« g = 0 :=
  zero_of_epi_comp (factorThruImage f) <| by simp [h]

theorem comp_factorThruImage_eq_zero {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} [HasImage g]
    (h : f â‰« g = 0) : f â‰« factorThruImage g = 0 :=
  zero_of_comp_mono (image.Î¹ g) <| by simp [h]

variable [HasZeroObject C]

open ZeroObject

/-- The zero morphism has a `MonoFactorisation` through the zero object.
-/
@[simps]
def monoFactorisationZero (X Y : C) : MonoFactorisation (0 : X âŸ¶ Y) where
  I := 0
  m := 0
  e := 0

/-- The factorisation through the zero object is an image factorisation.
-/
def imageFactorisationZero (X Y : C) : ImageFactorisation (0 : X âŸ¶ Y) where
  F := monoFactorisationZero X Y
  isImage := { lift := fun _ => 0 }

instance hasImage_zero {X Y : C} : HasImage (0 : X âŸ¶ Y) :=
  HasImage.mk <| imageFactorisationZero _ _

/-- The image of a zero morphism is the zero object. -/
def imageZero {X Y : C} : image (0 : X âŸ¶ Y) â‰… 0 :=
  IsImage.isoExt (Image.isImage (0 : X âŸ¶ Y)) (imageFactorisationZero X Y).isImage

/-- The image of a morphism which is equal to zero is the zero object. -/
def imageZero' {X Y : C} {f : X âŸ¶ Y} (h : f = 0) [HasImage f] : image f â‰… 0 :=
  image.eqToIso h â‰ªâ‰« imageZero

@[simp]
theorem image.Î¹_zero {X Y : C} [HasImage (0 : X âŸ¶ Y)] : image.Î¹ (0 : X âŸ¶ Y) = 0 := by
  rw [â† image.lift_fac (monoFactorisationZero X Y)]
  simp

/-- If we know `f = 0`,
it requires a little work to conclude `image.Î¹ f = 0`,
because `f = g` only implies `image f â‰… image g`.
-/
@[simp]
theorem image.Î¹_zero' [HasEqualizers C] {X Y : C} {f : X âŸ¶ Y} (h : f = 0) [HasImage f] :
    image.Î¹ f = 0 := by
  rw [image.eq_fac h]
  simp

end Image

/-- In the presence of zero morphisms, coprojections into a coproduct are (split) monomorphisms. -/
instance isSplitMono_sigma_Î¹ {Î² : Type u'} [HasZeroMorphisms C] (f : Î² â†’ C)
    [HasColimit (Discrete.functor f)] (b : Î²) : IsSplitMono (Sigma.Î¹ f b) := by
  classical exact IsSplitMono.mk' { retraction := Sigma.desc <| Pi.single b (ğŸ™ _) }

/-- In the presence of zero morphisms, projections into a product are (split) epimorphisms. -/
instance isSplitEpi_pi_Ï€ {Î² : Type u'} [HasZeroMorphisms C] (f : Î² â†’ C)
    [HasLimit (Discrete.functor f)] (b : Î²) : IsSplitEpi (Pi.Ï€ f b) := by
  classical exact IsSplitEpi.mk' { section_ := Pi.lift <| Pi.single b (ğŸ™ _) }

/-- In the presence of zero morphisms, coprojections into a coproduct are (split) monomorphisms. -/
instance isSplitMono_coprod_inl [HasZeroMorphisms C] {X Y : C} [HasColimit (pair X Y)] :
    IsSplitMono (coprod.inl : X âŸ¶ X â¨¿ Y) :=
  IsSplitMono.mk' { retraction := coprod.desc (ğŸ™ X) 0 }

/-- In the presence of zero morphisms, coprojections into a coproduct are (split) monomorphisms. -/
instance isSplitMono_coprod_inr [HasZeroMorphisms C] {X Y : C} [HasColimit (pair X Y)] :
    IsSplitMono (coprod.inr : Y âŸ¶ X â¨¿ Y) :=
  IsSplitMono.mk' { retraction := coprod.desc 0 (ğŸ™ Y) }

/-- In the presence of zero morphisms, projections into a product are (split) epimorphisms. -/
instance isSplitEpi_prod_fst [HasZeroMorphisms C] {X Y : C} [HasLimit (pair X Y)] :
    IsSplitEpi (prod.fst : X â¨¯ Y âŸ¶ X) :=
  IsSplitEpi.mk' { section_ := prod.lift (ğŸ™ X) 0 }

/-- In the presence of zero morphisms, projections into a product are (split) epimorphisms. -/
instance isSplitEpi_prod_snd [HasZeroMorphisms C] {X Y : C} [HasLimit (pair X Y)] :
    IsSplitEpi (prod.snd : X â¨¯ Y âŸ¶ Y) :=
  IsSplitEpi.mk' { section_ := prod.lift 0 (ğŸ™ Y) }


section

variable [HasZeroMorphisms C] [HasZeroObject C] {F : D â¥¤ C}

/-- If a functor `F` is zero, then any cone for `F` with a zero point is limit. -/
def IsLimit.ofIsZero (c : Cone F) (hF : IsZero F) (hc : IsZero c.pt) : IsLimit c where
  lift _ := 0
  fac _ j := (F.isZero_iff.1 hF j).eq_of_tgt _ _
  uniq _ _ _ := hc.eq_of_tgt _ _

/-- If a functor `F` is zero, then any cocone for `F` with a zero point is colimit. -/
def IsColimit.ofIsZero (c : Cocone F) (hF : IsZero F) (hc : IsZero c.pt) : IsColimit c where
  desc _ := 0
  fac _ j := (F.isZero_iff.1 hF j).eq_of_src _ _
  uniq _ _ _ := hc.eq_of_src _ _

lemma IsLimit.isZero_pt {c : Cone F} (hc : IsLimit c) (hF : IsZero F) : IsZero c.pt :=
  (isZero_zero C).of_iso (IsLimit.conePointUniqueUpToIso hc
    (IsLimit.ofIsZero (Cone.mk 0 0) hF (isZero_zero C)))

lemma IsColimit.isZero_pt {c : Cocone F} (hc : IsColimit c) (hF : IsZero F) : IsZero c.pt :=
  (isZero_zero C).of_iso (IsColimit.coconePointUniqueUpToIso hc
    (IsColimit.ofIsZero (Cocone.mk 0 0) hF (isZero_zero C)))

end

section

variable [HasZeroMorphisms C]

lemma IsTerminal.isZero {X : C} (hX : IsTerminal X) : IsZero X := by
  rw [IsZero.iff_id_eq_zero]
  apply hX.hom_ext

lemma IsInitial.isZero {X : C} (hX : IsInitial X) : IsZero X := by
  rw [IsZero.iff_id_eq_zero]
  apply hX.hom_ext

end

section PiIota

variable [HasZeroMorphisms C] {Î² : Type w} [DecidableEq Î²] (f : Î² â†’ C) [HasProduct f]

/-- In the presence of 0-morphism we can define an inclusion morphism into any product. -/
def Pi.Î¹ (b : Î²) : f b âŸ¶ âˆá¶œ f :=
  Pi.lift (Function.update (fun _ â†¦ 0) b (ğŸ™ _))

@[reassoc (attr := simp)]
lemma Pi.Î¹_Ï€_eq_id (b : Î²) : Pi.Î¹ f b â‰« Pi.Ï€ f b = ğŸ™ _ := by
  simp [Pi.Î¹]

@[reassoc]
lemma Pi.Î¹_Ï€_of_ne {b c : Î²} (h : b â‰  c) : Pi.Î¹ f b â‰« Pi.Ï€ f c = 0 := by
  simp [Pi.Î¹, Function.update_of_ne h.symm]

@[reassoc]
lemma Pi.Î¹_Ï€ (b c : Î²) :
    Pi.Î¹ f b â‰« Pi.Ï€ f c = if h : b = c then eqToHom (congrArg f h) else 0 := by
  split_ifs with h
  Â· subst h; simp
  Â· simp [Pi.Î¹_Ï€_of_ne f h]

instance (b : Î²) : Mono (Pi.Î¹ f b) where
  right_cancellation _ _ e := by simpa using congrArg (Â· â‰« Pi.Ï€ f b) e

end PiIota

section SigmaPi

variable [HasZeroMorphisms C] {Î² : Type w} [DecidableEq Î²] (f : Î² â†’ C) [HasCoproduct f]

/-- In the presence of 0-morphisms we can define a projection morphism from any coproduct. -/
def Sigma.Ï€ (b : Î²) : âˆ f âŸ¶ f b :=
  Limits.Sigma.desc (Function.update (fun _ â†¦ 0) b (ğŸ™ _))

@[reassoc (attr := simp)]
lemma Sigma.Î¹_Ï€_eq_id (b : Î²) : Sigma.Î¹ f b â‰« Sigma.Ï€ f b = ğŸ™ _ := by
  simp [Sigma.Ï€]

@[reassoc]
lemma Sigma.Î¹_Ï€_of_ne {b c : Î²} (h : b â‰  c) : Sigma.Î¹ f b â‰« Sigma.Ï€ f c = 0 := by
  simp [Sigma.Ï€, Function.update_of_ne h]

@[reassoc]
theorem Sigma.Î¹_Ï€ (b c : Î²) :
    Sigma.Î¹ f b â‰« Sigma.Ï€ f c = if h : b = c then eqToHom (congrArg f h) else 0 := by
  split_ifs with h
  Â· subst h; simp
  Â· simp [Sigma.Î¹_Ï€_of_ne f h]

instance (b : Î²) : Epi (Sigma.Ï€ f b) where
  left_cancellation _ _ e := by simpa using congrArg (Sigma.Î¹ f b â‰« Â·) e

end SigmaPi

section ProdInlInr

variable [HasZeroMorphisms C] (X Y : C) [HasBinaryProduct X Y]

/-- If a category `C` has 0-morphisms, there is a canonical inclusion from the first component `X`
into any product of objects `X â¨¯ Y`. -/
def prod.inl : X âŸ¶ X â¨¯ Y :=
  prod.lift (ğŸ™ _) 0

/-- If a category `C` has 0-morphisms, there is a canonical inclusion from the second component `Y`
into any product of objects `X â¨¯ Y`. -/
def prod.inr : Y âŸ¶ X â¨¯ Y :=
  prod.lift 0 (ğŸ™ _)

@[reassoc (attr := simp)]
lemma prod.inl_fst : prod.inl X Y â‰« prod.fst = ğŸ™ X := by
  simp [prod.inl]

@[reassoc (attr := simp)]
lemma prod.inl_snd : prod.inl X Y â‰« prod.snd = 0 := by
  simp [prod.inl]

@[reassoc (attr := simp)]
lemma prod.inr_fst : prod.inr X Y â‰« prod.fst = 0 := by
  simp [prod.inr]

@[reassoc (attr := simp)]
lemma prod.inr_snd : prod.inr X Y â‰« prod.snd = ğŸ™ Y := by
  simp [prod.inr]

instance : Mono (prod.inl X Y) where
  right_cancellation _ _ e := by simpa using congrArg (Â· â‰« prod.fst) e

instance : Mono (prod.inr X Y) where
  right_cancellation _ _ e := by simpa using congrArg (Â· â‰« prod.snd) e

end ProdInlInr

section CoprodFstSnd

variable [HasZeroMorphisms C] (X Y : C) [HasBinaryCoproduct X Y]

/-- If a category `C` has 0-morphisms, there is a canonical projection from a coproduct `X â¨¿ Y` to
its first component `X`. -/
def coprod.fst : X â¨¿ Y âŸ¶ X :=
  coprod.desc (ğŸ™ _) 0

/-- If a category `C` has 0-morphisms, there is a canonical projection from a coproduct `X â¨¿ Y` to
its second component `Y`. -/
def coprod.snd : X â¨¿ Y âŸ¶ Y :=
  coprod.desc 0 (ğŸ™ _)

@[reassoc (attr := simp)]
lemma coprod.inl_fst : coprod.inl â‰« coprod.fst X Y = ğŸ™ X := by
  simp [coprod.fst]

@[reassoc (attr := simp)]
lemma coprod.inr_fst : coprod.inr â‰« coprod.fst X Y = 0 := by
  simp [coprod.fst]

@[reassoc (attr := simp)]
lemma coprod.inl_snd : coprod.inl â‰« coprod.snd X Y = 0 := by
  simp [coprod.snd]

@[reassoc (attr := simp)]
lemma coprod.inr_snd : coprod.inr â‰« coprod.snd X Y = ğŸ™ Y := by
  simp [coprod.snd]

instance : Epi (coprod.fst X Y) where
  left_cancellation _ _ e := by simpa using congrArg (coprod.inl â‰« Â·) e

instance : Epi (coprod.snd X Y) where
  left_cancellation _ _ e := by simpa using congrArg (coprod.inr â‰« Â·) e

end CoprodFstSnd

end CategoryTheory.Limits
