/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.CommSq
import Mathlib.CategoryTheory.Limits.Shapes.RegularMono

#align_import category_theory.limits.shapes.kernel_pair from "leanprover-community/mathlib"@"f6bab67886fb92c3e2f539cc90a83815f69a189d"

/-!
# Kernel pairs

This file defines what it means for a parallel pair of morphisms `a b : R âŸ¶ X` to be the kernel pair
for a morphism `f`.
Some properties of kernel pairs are given, namely allowing one to transfer between
the kernel pair of `fâ‚ â‰« fâ‚‚` to the kernel pair of `fâ‚`.
It is also proved that if `f` is a coequalizer of some pair, and `a`,`b` is a kernel pair for `f`
then it is a coequalizer of `a`,`b`.

## Implementation

The definition is essentially just a wrapper for `IsLimit (PullbackCone.mk _ _ _)`, but the
constructions given here are useful, yet awkward to present in that language, so a basic API
is developed here.

## TODO

- Internal equivalence relations (or congruences) and the fact that every kernel pair induces one,
  and the converse in an effective regular category (WIP by b-mehta).

-/


universe v u uâ‚‚

namespace CategoryTheory

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]
variable {R X Y Z : C} (f : X âŸ¶ Y) (a b : R âŸ¶ X)

/-- `IsKernelPair f a b` expresses that `(a, b)` is a kernel pair for `f`, i.e. `a â‰« f = b â‰« f`
and the square
  R â†’ X
  â†“   â†“
  X â†’ Y
is a pullback square.
This is just an abbreviation for `IsPullback a b f f`.
-/
abbrev IsKernelPair :=
  IsPullback a b f f
#align category_theory.is_kernel_pair CategoryTheory.IsKernelPair

namespace IsKernelPair

/-- The data expressing that `(a, b)` is a kernel pair is subsingleton. -/
instance : Subsingleton (IsKernelPair f a b) :=
  âŸ¨fun P Q => by
    cases P
    cases Q
    congr âŸ©

/-- If `f` is a monomorphism, then `(ğŸ™ _, ğŸ™ _)` is a kernel pair for `f`. -/
theorem id_of_mono [Mono f] : IsKernelPair f (ğŸ™ _) (ğŸ™ _) :=
  âŸ¨âŸ¨rflâŸ©, âŸ¨PullbackCone.isLimitMkIdId _âŸ©âŸ©
#align category_theory.is_kernel_pair.id_of_mono CategoryTheory.IsKernelPair.id_of_mono

instance [Mono f] : Inhabited (IsKernelPair f (ğŸ™ _) (ğŸ™ _)) :=
  âŸ¨id_of_mono fâŸ©

variable {f a b}

-- Porting note: `lift` and the two following simp lemmas were introduced to ease the port
/--
Given a pair of morphisms `p`, `q` to `X` which factor through `f`, they factor through any kernel
pair of `f`.
-/
noncomputable def lift {S : C} (k : IsKernelPair f a b) (p q : S âŸ¶ X) (w : p â‰« f = q â‰« f) :
    S âŸ¶ R :=
  PullbackCone.IsLimit.lift k.isLimit _ _ w

@[reassoc (attr := simp)]
lemma lift_fst {S : C} (k : IsKernelPair f a b) (p q : S âŸ¶ X) (w : p â‰« f = q â‰« f) :
    k.lift p q w â‰« a = p :=
  PullbackCone.IsLimit.lift_fst _ _ _ _

@[reassoc (attr := simp)]
lemma lift_snd {S : C} (k : IsKernelPair f a b) (p q : S âŸ¶ X) (w : p â‰« f = q â‰« f) :
    k.lift p q w â‰« b = q :=
  PullbackCone.IsLimit.lift_snd _ _ _ _

/--
Given a pair of morphisms `p`, `q` to `X` which factor through `f`, they factor through any kernel
pair of `f`.
-/
noncomputable def lift' {S : C} (k : IsKernelPair f a b) (p q : S âŸ¶ X) (w : p â‰« f = q â‰« f) :
    { t : S âŸ¶ R // t â‰« a = p âˆ§ t â‰« b = q } :=
  âŸ¨k.lift p q w, by simpâŸ©
#align category_theory.is_kernel_pair.lift' CategoryTheory.IsKernelPair.lift'

/--
If `(a,b)` is a kernel pair for `fâ‚ â‰« fâ‚‚` and `a â‰« fâ‚ = b â‰« fâ‚`, then `(a,b)` is a kernel pair for
just `fâ‚`.
That is, to show that `(a,b)` is a kernel pair for `fâ‚` it suffices to only show the square
commutes, rather than to additionally show it's a pullback.
-/
theorem cancel_right {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} (comm : a â‰« fâ‚ = b â‰« fâ‚)
    (big_k : IsKernelPair (fâ‚ â‰« fâ‚‚) a b) : IsKernelPair fâ‚ a b :=
  { w := comm
    isLimit' :=
      âŸ¨PullbackCone.isLimitAux' _ fun s => by
        let s' : PullbackCone (fâ‚ â‰« fâ‚‚) (fâ‚ â‰« fâ‚‚) :=
          PullbackCone.mk s.fst s.snd (s.condition_assoc _)
        refine âŸ¨big_k.isLimit.lift s', big_k.isLimit.fac _ WalkingCospan.left,
          big_k.isLimit.fac _ WalkingCospan.right, fun mâ‚ mâ‚‚ => ?_âŸ©
        apply big_k.isLimit.hom_ext
        refine (PullbackCone.mk a b ?_ : PullbackCone (fâ‚ â‰« fâ‚‚) _).equalizer_ext ?_ ?_
        Â· apply reassoc_of% comm
        Â· apply mâ‚.trans (big_k.isLimit.fac s' WalkingCospan.left).symm
        Â· apply mâ‚‚.trans (big_k.isLimit.fac s' WalkingCospan.right).symmâŸ© }
#align category_theory.is_kernel_pair.cancel_right CategoryTheory.IsKernelPair.cancel_right

/-- If `(a,b)` is a kernel pair for `fâ‚ â‰« fâ‚‚` and `fâ‚‚` is mono, then `(a,b)` is a kernel pair for
just `fâ‚`.
The converse of `comp_of_mono`.
-/
theorem cancel_right_of_mono {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} [Mono fâ‚‚]
    (big_k : IsKernelPair (fâ‚ â‰« fâ‚‚) a b) : IsKernelPair fâ‚ a b :=
  cancel_right (by rw [â† cancel_mono fâ‚‚, assoc, assoc, big_k.w]) big_k
#align category_theory.is_kernel_pair.cancel_right_of_mono CategoryTheory.IsKernelPair.cancel_right_of_mono

/--
If `(a,b)` is a kernel pair for `fâ‚` and `fâ‚‚` is mono, then `(a,b)` is a kernel pair for `fâ‚ â‰« fâ‚‚`.
The converse of `cancel_right_of_mono`.
-/
theorem comp_of_mono {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} [Mono fâ‚‚] (small_k : IsKernelPair fâ‚ a b) :
    IsKernelPair (fâ‚ â‰« fâ‚‚) a b :=
  { w := by rw [small_k.w_assoc]
    isLimit' := âŸ¨by
      refine PullbackCone.isLimitAux _
        (fun s => small_k.lift s.fst s.snd (by rw [â† cancel_mono fâ‚‚, assoc, s.condition, assoc]))
        (by simp) (by simp) ?_
      intro s m hm
      apply small_k.isLimit.hom_ext
      apply PullbackCone.equalizer_ext small_k.cone _ _
      Â· exact (hm WalkingCospan.left).trans (by simp)
      Â· exact (hm WalkingCospan.right).trans (by simp)âŸ© }
#align category_theory.is_kernel_pair.comp_of_mono CategoryTheory.IsKernelPair.comp_of_mono

/--
If `(a,b)` is the kernel pair of `f`, and `f` is a coequalizer morphism for some parallel pair, then
`f` is a coequalizer morphism of `a` and `b`.
-/
def toCoequalizer (k : IsKernelPair f a b) [r : RegularEpi f] : IsColimit (Cofork.ofÏ€ f k.w) := by
  let t := k.isLimit.lift (PullbackCone.mk _ _ r.w)
  have ht : t â‰« a = r.left := k.isLimit.fac _ WalkingCospan.left
  have kt : t â‰« b = r.right := k.isLimit.fac _ WalkingCospan.right
  refine Cofork.IsColimit.mk _
    (fun s => Cofork.IsColimit.desc r.isColimit s.Ï€
      (by rw [â† ht, assoc, s.condition, reassoc_of% kt]))
    (fun s => ?_) (fun s m w => ?_)
  Â· apply Cofork.IsColimit.Ï€_desc' r.isColimit
  Â· apply Cofork.IsColimit.hom_ext r.isColimit
    exact w.trans (Cofork.IsColimit.Ï€_desc' r.isColimit _ _).symm
#align category_theory.is_kernel_pair.to_coequalizer CategoryTheory.IsKernelPair.toCoequalizer

/-- If `aâ‚ aâ‚‚ : A âŸ¶ Y` is a kernel pair for `g : Y âŸ¶ Z`, then `aâ‚ Ã—[Z] X` and `aâ‚‚ Ã—[Z] X`
(`A Ã—[Z] X âŸ¶ Y Ã—[Z] X`) is a kernel pair for `Y Ã—[Z] X âŸ¶ X`. -/
protected theorem pullback {X Y Z A : C} {g : Y âŸ¶ Z} {aâ‚ aâ‚‚ : A âŸ¶ Y} (h : IsKernelPair g aâ‚ aâ‚‚)
    (f : X âŸ¶ Z) [HasPullback f g] [HasPullback f (aâ‚ â‰« g)] :
    IsKernelPair (pullback.fst : pullback f g âŸ¶ X)
      (pullback.map f _ f _ (ğŸ™ X) aâ‚ (ğŸ™ Z) (by simp) <| Category.comp_id _)
      (pullback.map _ _ _ _ (ğŸ™ X) aâ‚‚ (ğŸ™ Z) (by simp) <| (Category.comp_id _).trans h.1.1) := by
  refine âŸ¨âŸ¨by rw [pullback.lift_fst, pullback.lift_fst]âŸ©, âŸ¨PullbackCone.isLimitAux _
    (fun s => pullback.lift (s.fst â‰« pullback.fst)
      (h.lift (s.fst â‰« pullback.snd) (s.snd â‰« pullback.snd) ?_ ) ?_) (fun s => ?_) (fun s => ?_)
        (fun s m hm => ?_)âŸ©âŸ©
  Â· simp_rw [Category.assoc, â† pullback.condition, â† Category.assoc, s.condition]
  Â· simp only [assoc, lift_fst_assoc, pullback.condition]
  Â· ext <;> simp
  Â· ext
    Â· simp [s.condition]
    Â· simp
  Â· #adaptation_note /-- nightly-2024-04-01
    This `symm` (or the following ones that undo it) wasn't previously necessary. -/
    symm
    apply pullback.hom_ext
    Â· symm
      simpa using hm WalkingCospan.left =â‰« pullback.fst
    Â· symm
      apply PullbackCone.IsLimit.hom_ext h.isLimit
      Â· simpa using hm WalkingCospan.left =â‰« pullback.snd
      Â· simpa using hm WalkingCospan.right =â‰« pullback.snd
#align category_theory.is_kernel_pair.pullback CategoryTheory.IsKernelPair.pullback

theorem mono_of_isIso_fst (h : IsKernelPair f a b) [IsIso a] : Mono f := by
  obtain âŸ¨l, hâ‚, hâ‚‚âŸ© := Limits.PullbackCone.IsLimit.lift' h.isLimit (ğŸ™ _) (ğŸ™ _) (by simp [h.w])
  rw [IsPullback.cone_fst, â† IsIso.eq_comp_inv, Category.id_comp] at hâ‚
  rw [hâ‚, IsIso.inv_comp_eq, Category.comp_id] at hâ‚‚
  constructor
  intro Z gâ‚ gâ‚‚ e
  obtain âŸ¨l', rfl, rflâŸ© := Limits.PullbackCone.IsLimit.lift' h.isLimit _ _ e
  rw [IsPullback.cone_fst, hâ‚‚]
#align category_theory.is_kernel_pair.mono_of_is_iso_fst CategoryTheory.IsKernelPair.mono_of_isIso_fst

theorem isIso_of_mono (h : IsKernelPair f a b) [Mono f] : IsIso a := by
  rw [â†
    show _ = a from
      (Category.comp_id _).symm.trans
        ((IsKernelPair.id_of_mono f).isLimit.conePointUniqueUpToIso_inv_comp h.isLimit
          WalkingCospan.left)]
  infer_instance
#align category_theory.is_kernel_pair.is_iso_of_mono CategoryTheory.IsKernelPair.isIso_of_mono

theorem of_isIso_of_mono [IsIso a] [Mono f] : IsKernelPair f a a := by
  change IsPullback _ _ _ _
  convert (IsPullback.of_horiz_isIso âŸ¨(rfl : a â‰« ğŸ™ X = _ )âŸ©).paste_vert (IsKernelPair.id_of_mono f)
  all_goals { simp }
#align category_theory.is_kernel_pair.of_is_iso_of_mono CategoryTheory.IsKernelPair.of_isIso_of_mono

end IsKernelPair

end CategoryTheory
