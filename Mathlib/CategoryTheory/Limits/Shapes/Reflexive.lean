/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair

#align_import category_theory.limits.shapes.reflexive from "leanprover-community/mathlib"@"d6814c584384ddf2825ff038e868451a7c956f31"

/-!
# Reflexive coequalizers

We define reflexive pairs as a pair of morphisms which have a common section. We say a category has
reflexive coequalizers if it has coequalizers of all reflexive pairs.
Reflexive coequalizers often enjoy nicer properties than general coequalizers, and feature heavily
in some versions of the monadicity theorem.

We also give some examples of reflexive pairs: for an adjunction `F âŠ£ G` with counit `Îµ`, the pair
`(FGÎµ_B, Îµ_FGB)` is reflexive. If a pair `f,g` is a kernel pair for some morphism, then it is
reflexive.

# TODO
* If `C` has binary coproducts and reflexive coequalizers, then it has all coequalizers.
* If `T` is a monad on cocomplete category `C`, then `Algebra T` is cocomplete iff it has reflexive
  coequalizers.
* If `C` is locally cartesian closed and has reflexive coequalizers, then it has images: in fact
  regular epi (and hence strong epi) images.
-/


namespace CategoryTheory

universe v vâ‚‚ u uâ‚‚

variable {C : Type u} [Category.{v} C]
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D]
variable {A B : C} {f g : A âŸ¶ B}

/-- The pair `f g : A âŸ¶ B` is reflexive if there is a morphism `B âŸ¶ A` which is a section for both.
-/
class IsReflexivePair (f g : A âŸ¶ B) : Prop where
  common_section' : âˆƒ s : B âŸ¶ A, s â‰« f = ğŸ™ B âˆ§ s â‰« g = ğŸ™ B
#align category_theory.is_reflexive_pair CategoryTheory.IsReflexivePair

theorem IsReflexivePair.common_section (f g : A âŸ¶ B) [IsReflexivePair f g] :
    âˆƒ s : B âŸ¶ A, s â‰« f = ğŸ™ B âˆ§ s â‰« g = ğŸ™ B := IsReflexivePair.common_section'

/--
The pair `f g : A âŸ¶ B` is coreflexive if there is a morphism `B âŸ¶ A` which is a retraction for both.
-/
class IsCoreflexivePair (f g : A âŸ¶ B) : Prop where
  common_retraction' : âˆƒ s : B âŸ¶ A, f â‰« s = ğŸ™ A âˆ§ g â‰« s = ğŸ™ A
#align category_theory.is_coreflexive_pair CategoryTheory.IsCoreflexivePair

theorem IsCoreflexivePair.common_retraction (f g : A âŸ¶ B) [IsCoreflexivePair f g] :
    âˆƒ s : B âŸ¶ A, f â‰« s = ğŸ™ A âˆ§ g â‰« s = ğŸ™ A := IsCoreflexivePair.common_retraction'

theorem IsReflexivePair.mk' (s : B âŸ¶ A) (sf : s â‰« f = ğŸ™ B) (sg : s â‰« g = ğŸ™ B) :
    IsReflexivePair f g :=
  âŸ¨âŸ¨s, sf, sgâŸ©âŸ©
#align category_theory.is_reflexive_pair.mk' CategoryTheory.IsReflexivePair.mk'

theorem IsCoreflexivePair.mk' (s : B âŸ¶ A) (fs : f â‰« s = ğŸ™ A) (gs : g â‰« s = ğŸ™ A) :
    IsCoreflexivePair f g :=
  âŸ¨âŸ¨s, fs, gsâŸ©âŸ©
#align category_theory.is_coreflexive_pair.mk' CategoryTheory.IsCoreflexivePair.mk'

/-- Get the common section for a reflexive pair. -/
noncomputable def commonSection (f g : A âŸ¶ B) [IsReflexivePair f g] : B âŸ¶ A :=
  (IsReflexivePair.common_section f g).choose
#align category_theory.common_section CategoryTheory.commonSection

@[reassoc (attr := simp)]
theorem section_comp_left (f g : A âŸ¶ B) [IsReflexivePair f g] : commonSection f g â‰« f = ğŸ™ B :=
  (IsReflexivePair.common_section f g).choose_spec.1
#align category_theory.section_comp_left CategoryTheory.section_comp_left

@[reassoc (attr := simp)]
theorem section_comp_right (f g : A âŸ¶ B) [IsReflexivePair f g] : commonSection f g â‰« g = ğŸ™ B :=
  (IsReflexivePair.common_section f g).choose_spec.2
#align category_theory.section_comp_right CategoryTheory.section_comp_right

/-- Get the common retraction for a coreflexive pair. -/
noncomputable def commonRetraction (f g : A âŸ¶ B) [IsCoreflexivePair f g] : B âŸ¶ A :=
  (IsCoreflexivePair.common_retraction f g).choose
#align category_theory.common_retraction CategoryTheory.commonRetraction

@[reassoc (attr := simp)]
theorem left_comp_retraction (f g : A âŸ¶ B) [IsCoreflexivePair f g] :
    f â‰« commonRetraction f g = ğŸ™ A :=
  (IsCoreflexivePair.common_retraction f g).choose_spec.1
#align category_theory.left_comp_retraction CategoryTheory.left_comp_retraction

@[reassoc (attr := simp)]
theorem right_comp_retraction (f g : A âŸ¶ B) [IsCoreflexivePair f g] :
    g â‰« commonRetraction f g = ğŸ™ A :=
  (IsCoreflexivePair.common_retraction f g).choose_spec.2
#align category_theory.right_comp_retraction CategoryTheory.right_comp_retraction

/-- If `f,g` is a kernel pair for some morphism `q`, then it is reflexive. -/
theorem IsKernelPair.isReflexivePair {R : C} {f g : R âŸ¶ A} {q : A âŸ¶ B} (h : IsKernelPair q f g) :
    IsReflexivePair f g :=
  IsReflexivePair.mk' _ (h.lift' _ _ rfl).2.1 (h.lift' _ _ _).2.2
#align category_theory.is_kernel_pair.is_reflexive_pair CategoryTheory.IsKernelPair.isReflexivePair

-- This shouldn't be an instance as it would instantly loop.
/-- If `f,g` is reflexive, then `g,f` is reflexive. -/
theorem IsReflexivePair.swap [IsReflexivePair f g] : IsReflexivePair g f :=
  IsReflexivePair.mk' _ (section_comp_right f g) (section_comp_left f g)
#align category_theory.is_reflexive_pair.swap CategoryTheory.IsReflexivePair.swap

-- This shouldn't be an instance as it would instantly loop.
/-- If `f,g` is coreflexive, then `g,f` is coreflexive. -/
theorem IsCoreflexivePair.swap [IsCoreflexivePair f g] : IsCoreflexivePair g f :=
  IsCoreflexivePair.mk' _ (right_comp_retraction f g) (left_comp_retraction f g)
#align category_theory.is_coreflexive_pair.swap CategoryTheory.IsCoreflexivePair.swap

variable {F : C â¥¤ D} {G : D â¥¤ C} (adj : F âŠ£ G)

/-- For an adjunction `F âŠ£ G` with counit `Îµ`, the pair `(FGÎµ_B, Îµ_FGB)` is reflexive. -/
instance (B : D) :
    IsReflexivePair (F.map (G.map (adj.counit.app B))) (adj.counit.app (F.obj (G.obj B))) :=
  IsReflexivePair.mk' (F.map (adj.unit.app (G.obj B)))
    (by
      rw [â† F.map_comp, adj.right_triangle_components]
      apply F.map_id)
    (adj.left_triangle_components _)

namespace Limits

variable (C)

/-- `C` has reflexive coequalizers if it has coequalizers for every reflexive pair. -/
class HasReflexiveCoequalizers : Prop where
  has_coeq : âˆ€ â¦ƒA B : Câ¦„ (f g : A âŸ¶ B) [IsReflexivePair f g], HasCoequalizer f g
#align category_theory.limits.has_reflexive_coequalizers CategoryTheory.Limits.HasReflexiveCoequalizers

/-- `C` has coreflexive equalizers if it has equalizers for every coreflexive pair. -/
class HasCoreflexiveEqualizers : Prop where
  has_eq : âˆ€ â¦ƒA B : Câ¦„ (f g : A âŸ¶ B) [IsCoreflexivePair f g], HasEqualizer f g
#align category_theory.limits.has_coreflexive_equalizers CategoryTheory.Limits.HasCoreflexiveEqualizers

attribute [instance 1] HasReflexiveCoequalizers.has_coeq

attribute [instance 1] HasCoreflexiveEqualizers.has_eq

theorem hasCoequalizer_of_common_section [HasReflexiveCoequalizers C] {A B : C} {f g : A âŸ¶ B}
    (r : B âŸ¶ A) (rf : r â‰« f = ğŸ™ _) (rg : r â‰« g = ğŸ™ _) : HasCoequalizer f g := by
  letI := IsReflexivePair.mk' r rf rg
  infer_instance
#align category_theory.limits.has_coequalizer_of_common_section CategoryTheory.Limits.hasCoequalizer_of_common_section

theorem hasEqualizer_of_common_retraction [HasCoreflexiveEqualizers C] {A B : C} {f g : A âŸ¶ B}
    (r : B âŸ¶ A) (fr : f â‰« r = ğŸ™ _) (gr : g â‰« r = ğŸ™ _) : HasEqualizer f g := by
  letI := IsCoreflexivePair.mk' r fr gr
  infer_instance
#align category_theory.limits.has_equalizer_of_common_retraction CategoryTheory.Limits.hasEqualizer_of_common_retraction

/-- If `C` has coequalizers, then it has reflexive coequalizers. -/
instance (priority := 100) hasReflexiveCoequalizers_of_hasCoequalizers [HasCoequalizers C] :
    HasReflexiveCoequalizers C where has_coeq A B f g _ := by infer_instance
#align category_theory.limits.has_reflexive_coequalizers_of_has_coequalizers CategoryTheory.Limits.hasReflexiveCoequalizers_of_hasCoequalizers

/-- If `C` has equalizers, then it has coreflexive equalizers. -/
instance (priority := 100) hasCoreflexiveEqualizers_of_hasEqualizers [HasEqualizers C] :
    HasCoreflexiveEqualizers C where has_eq A B f g _ := by infer_instance
#align category_theory.limits.has_coreflexive_equalizers_of_has_equalizers CategoryTheory.Limits.hasCoreflexiveEqualizers_of_hasEqualizers

end Limits

open Limits

end CategoryTheory
